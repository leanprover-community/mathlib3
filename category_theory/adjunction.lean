import category_theory.limits.preserves
import category_theory.whiskering
import data.equiv.basic

namespace category_theory
open category
open category_theory.limits

universes uâ‚ vâ‚ uâ‚‚ vâ‚‚ uâ‚ƒ vâ‚ƒ

local attribute [elab_simple] whisker_left whisker_right

variables {C : Type uâ‚} [ğ’ : category.{uâ‚ vâ‚} C] {D : Type uâ‚‚} [ğ’Ÿ : category.{uâ‚‚ vâ‚‚} D]
include ğ’ ğ’Ÿ

/--
`adjunction F G` represents the data of an adjunction between two functors
`F : C â¥¤ D` and `G : D â¥¤ C`. `F` is the left adjoint and `G` is the right adjoint.

The adjunction is represented by a pair of natural transformations satisfying
the triangle equations.
-/
structure adjunction (F : C â¥¤ D) (G : D â¥¤ C) :=
(unit : functor.id C âŸ¹ F.comp G)
(counit : G.comp F âŸ¹ functor.id D)
(left_triangle : (whisker_right unit F).vcomp (whisker_left F counit) = nat_trans.id _)
(right_triangle : (whisker_left G unit).vcomp (whisker_right counit G) = nat_trans.id _)

attribute [simp] adjunction.left_triangle adjunction.right_triangle

namespace adjunction

section
variables {F : C â¥¤ D} {G : D â¥¤ C} (adj : adjunction F G)

lemma left_triangle_components {c : C} :
  F.map (adj.unit.app c) â‰« adj.counit.app (F.obj c) = ğŸ™ _ :=
congr_arg (Î» (t : _ âŸ¹ functor.id C â‹™ F), nat_trans.app t c) adj.left_triangle

lemma right_triangle_components {d : D} :
  adj.unit.app (G.obj d) â‰« G.map (adj.counit.app d) = ğŸ™ _ :=
congr_arg (Î» (t : _ âŸ¹ G â‹™ functor.id C), nat_trans.app t d) adj.right_triangle

def hom_equiv {X Y} : (F.obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G.obj Y) :=
{ to_fun := Î» f, adj.unit.app X â‰« G.map f,
  inv_fun := Î» g, F.map g â‰« adj.counit.app Y,
  left_inv := Î» f, begin
    change F.map (_ â‰« _) â‰« _ = _,
    rw [F.map_comp, assoc, â†functor.comp_map, adj.counit.naturality, â†assoc],
    convert id_comp _ f,
    apply left_triangle_components
  end,
  right_inv := Î» g, begin
    change _ â‰« G.map (_ â‰« _) = _,
    rw [G.map_comp, â†assoc, â†functor.comp_map, â†adj.unit.naturality, assoc],
    convert comp_id _ g,
    apply right_triangle_components
  end }

@[simp] lemma hom_equiv_naturality {X Y Y'} (f : F.obj X âŸ¶ Y) (g : Y âŸ¶ Y') :
  adj.hom_equiv.to_fun (f â‰« g) = adj.hom_equiv.to_fun f â‰« G.map g :=
show adj.unit.app X â‰« G.map (f â‰« g) = (adj.unit.app X â‰« G.map f) â‰« G.map g,
by simp

@[simp] lemma hom_equiv_symm_naturality {X' X Y} (f : X' âŸ¶ X) (g : X âŸ¶ G.obj Y) :
  adj.hom_equiv.inv_fun (f â‰« g) = F.map f â‰« adj.hom_equiv.inv_fun g :=
show F.map (f â‰« g) â‰« adj.counit.app Y = F.map f â‰« F.map g â‰« adj.counit.app Y,
by simp

@[simp] lemma hom_equiv_naturality' {X' X Y} (f : X' âŸ¶ X) (g : F.obj X âŸ¶ Y) :
  adj.hom_equiv.to_fun (F.map f â‰« g) = f â‰« adj.hom_equiv.to_fun g :=
by dsimp [adjunction.hom_equiv]; erw [â†assoc, adj.unit.naturality]; simp

@[simp] lemma hom_equiv_symm_naturality' {X Y Y'} (f : X âŸ¶ G.obj Y) (g : Y âŸ¶ Y') :
  adj.hom_equiv.inv_fun (f â‰« G.map g) = adj.hom_equiv.inv_fun f â‰« g :=
by dsimp [adjunction.hom_equiv]; conv { to_rhs, rw assoc }; erw â†adj.counit.naturality; simp

def nat_equiv {E : Type uâ‚ƒ} [category.{uâ‚ƒ vâ‚ƒ} E] (X : E â¥¤ C) (Y : E â¥¤ D) :
  (X.comp F âŸ¹ Y) â‰ƒ (X âŸ¹ Y.comp G) :=
{ to_fun := Î» t,
  { app := Î» e, adj.hom_equiv.to_fun (t.app e),
    naturality' := Î» e e' f,
      by erw [â†adj.hom_equiv_naturality, â†adj.hom_equiv_naturality', t.naturality] },
  inv_fun := Î» t,
  { app := Î» e, adj.hom_equiv.inv_fun (t.app e),
    naturality' := Î» e e' f,
      by erw [â†adj.hom_equiv_symm_naturality, â†adj.hom_equiv_symm_naturality', â†t.naturality] },
  left_inv := Î» t, by ext e; apply adj.hom_equiv.left_inv,
  right_inv := Î» t, by ext e; apply adj.hom_equiv.right_inv }

def nat_equiv' {E : Type uâ‚ƒ} [category.{uâ‚ƒ vâ‚ƒ} E] (X : C â¥¤ E) (Y : D â¥¤ E) :
  (G.comp X âŸ¹ Y) â‰ƒ (X âŸ¹ F.comp Y) :=
{ to_fun := Î» t,
  { app := Î» c, X.map (adj.unit.app c) â‰« t.app (F.obj c),
    naturality' := Î» c c' f, begin
      erw [â†assoc, â†X.map_comp, adj.unit.naturality, X.map_comp, assoc, assoc, t.naturality],
      refl
    end },
  inv_fun := Î» t,
  { app := Î» d, t.app (G.obj d) â‰« Y.map (adj.counit.app d),
    naturality' := Î» d d' f, begin
      erw [assoc, â†Y.map_comp, â†adj.counit.naturality, Y.map_comp, â†assoc, â†assoc, â†t.naturality],
      refl
    end },
  left_inv := Î» t, begin
    ext c, dsimp [],
    erw [assoc, â†t.naturality, â†assoc, â†X.map_comp, adj.right_triangle_components],
    dsimp, simp
  end,
  right_inv := Î» t, begin
    ext d, dsimp [],
    erw [â†assoc, t.naturality, assoc, â†Y.map_comp, adj.left_triangle_components],
    dsimp, simp
  end }

section mate
universes uâ‚' vâ‚' uâ‚‚' vâ‚‚'
variables {C' : Type uâ‚'} [ğ’' : category.{uâ‚' vâ‚'} C'] {D' : Type uâ‚‚'} [ğ’Ÿ' : category.{uâ‚‚' vâ‚‚'} D']
include ğ’' ğ’Ÿ'
variables {F' : C' â¥¤ D'} {G' : D' â¥¤ C'} (adj' : adjunction F' G')
variables {X : C â¥¤ C'} {Y : D â¥¤ D'}

def mate : (X.comp F' âŸ¹ F.comp Y) â‰ƒ (G.comp X âŸ¹ Y.comp G') :=
calc
  (X.comp F' âŸ¹ F.comp Y)
    â‰ƒ (X âŸ¹ (F.comp Y).comp G')  : adj'.nat_equiv _ _
... â‰ƒ (X âŸ¹ F.comp (Y.comp G'))  : equiv.refl _
... â‰ƒ (G.comp X âŸ¹ Y.comp G')    : (adj.nat_equiv' _ _).symm

-- TODO: This is a double functor, or something like that

end mate

section morphism
variables {F' : C â¥¤ D} {G' : D â¥¤ C} (adj' : adjunction F' G')

def nat_trans_equiv : (F' âŸ¹ F) â‰ƒ (G âŸ¹ G') :=
calc
  (F' âŸ¹ F)
    â‰ƒ ((functor.id C).comp F' âŸ¹ F.comp (functor.id D))
    : iso.hom_equiv_of_isos (functor.id_comp F') (functor.comp_id F).symm
... â‰ƒ (G.comp (functor.id C) âŸ¹ (functor.id D).comp G')
    : mate adj adj'
... â‰ƒ (G âŸ¹ G')
    : iso.hom_equiv_of_isos (functor.comp_id G).symm (functor.id_comp G')

lemma nat_trans_lemma (t : F' âŸ¹ F) {X Y} (f : F.obj X âŸ¶ Y) :
  adj'.hom_equiv.to_fun (t.app X â‰« f) =
  adj.hom_equiv.to_fun f â‰« (nat_trans_equiv adj adj' t).app Y :=
begin
  dsimp [hom_equiv],
  rw [assoc, nat_trans.naturality, G'.map_comp, â†assoc, â†assoc],
  congr' 1,
  dsimp [nat_trans_equiv, mate, nat_equiv', nat_equiv, hom_equiv, iso.hom_equiv_of_isos],
  simp only [id_comp, comp_id, assoc],
/-
            /\
           |  |
   /\      |  |   /\
  |  |     |  |  |  |
  t  |  =  |  |  t  |
  |  |     |  |  |  |
           |   \/   |
           |        |
-/
  conv in (adj.unit.app X) { change (functor.id _).map (adj.unit.app X) },
  erw [â†assoc, adj'.unit.naturality, assoc], congr,
  erw [â†G'.map_comp, â†G'.map_comp], congr,
  erw [â†assoc, t.naturality, assoc, adj.left_triangle_components],
  symmetry, apply comp_id
end

lemma nat_trans_iff {t u} : nat_trans_equiv adj adj' t = u â†” âˆ€ {X Y} (f : F.obj X âŸ¶ Y),
  adj'.hom_equiv.to_fun (t.app X â‰« f) = adj.hom_equiv.to_fun f â‰« u.app Y :=
begin
  split; intro H,
  { subst H, apply nat_trans_lemma },
  { ext Y,
    have := H (adj.hom_equiv.inv_fun (ğŸ™ (G.obj Y))),
    rw [adj.hom_equiv.right_inv, id_comp] at this,
    rw â†this,
    have := nat_trans_lemma adj adj' t (adj.hom_equiv.inv_fun (ğŸ™ (G.obj Y))),
    rw [adj.hom_equiv.right_inv, id_comp] at this,
    rw this }
end

lemma nat_trans_iff' {t u} : nat_trans_equiv adj adj' t = u â†” âˆ€ {X Y} (f : X âŸ¶ G.obj Y),
  t.app X â‰« adj.hom_equiv.inv_fun f = adj'.hom_equiv.inv_fun (f â‰« u.app Y) :=
begin
  rw nat_trans_iff,
  apply forall_congr, intro X,
  apply forall_congr, intro Y,
  split; intros H f,
  { erw adj'.hom_equiv.eq_symm_apply, convert â†H _, exact (hom_equiv adj).right_inv f },
  { erw â†adj'.hom_equiv.eq_symm_apply, convert â†H _, exact (hom_equiv adj).left_inv f }
end

lemma nat_trans_equiv_id : nat_trans_equiv adj adj (ğŸ™ F) = (ğŸ™ G) :=
by rw nat_trans_iff; simp

variables {F'' : C â¥¤ D} {G'' : D â¥¤ C} (adj'' : adjunction F'' G'')

-- FIXME: (_ âŠŸ _)
lemma nat_trans_equiv_vcomp {t' t} :
  nat_trans_equiv adj adj'' (t' âŠŸ t) =
  (nat_trans_equiv adj adj' t âŠŸ nat_trans_equiv adj' adj'' t') :=
by rw nat_trans_iff; intros X Y f; erw [â†assoc, â†nat_trans_lemma, â†nat_trans_lemma, assoc]

lemma nat_trans_equiv_symm_id : (nat_trans_equiv adj adj).symm (ğŸ™ G) = (ğŸ™ F) :=
by rw [equiv.symm_apply_eq, nat_trans_equiv_id]

lemma nat_trans_equiv_symm_vcomp {t' t} :
  (nat_trans_equiv adj'' adj).symm (t' âŠŸ t) =
  ((nat_trans_equiv adj' adj).symm t âŠŸ (nat_trans_equiv adj'' adj').symm t') :=
by rw [equiv.symm_apply_eq, nat_trans_equiv_vcomp adj'' adj' adj]; simp

-- and therefore...

def nat_iso_equiv : (F' â‰… F) â‰ƒ (G â‰… G') :=
âŸ¨Î» i,
 { hom := nat_trans_equiv adj adj' i.hom,
   inv := nat_trans_equiv adj' adj i.inv,
   hom_inv_id' := begin
     change (_ âŠŸ _) = _,        -- FIXME
     rw â†nat_trans_equiv_vcomp,
     conv in (i.inv âŠŸ i.hom) { change i.inv â‰« i.hom, rw i.inv_hom_id }, -- ugh
     apply nat_trans_equiv_id
   end,
   inv_hom_id' := begin
     change (_ âŠŸ _) = _,        -- FIXME
     rw â†nat_trans_equiv_vcomp,
     conv in (i.hom âŠŸ i.inv) { change i.hom â‰« i.inv, rw i.hom_inv_id }, -- ugh
     apply nat_trans_equiv_id
   end },
 Î» i,
 { hom := (nat_trans_equiv adj adj').symm i.hom,
   inv := (nat_trans_equiv adj' adj).symm i.inv,
   hom_inv_id' := begin
     change (_ âŠŸ _) = _,        -- FIXME
     rw â†nat_trans_equiv_symm_vcomp,
     conv in (i.inv âŠŸ i.hom) { change i.inv â‰« i.hom, rw i.inv_hom_id }, -- ugh
     apply nat_trans_equiv_symm_id
   end,
   inv_hom_id' := begin
     change (_ âŠŸ _) = _,        -- FIXME
     rw â†nat_trans_equiv_symm_vcomp,
     conv in (i.hom âŠŸ i.inv) { change i.hom â‰« i.inv, rw i.hom_inv_id }, -- ugh
     apply nat_trans_equiv_symm_id
   end },
 Î» i, by ext; simp,
 Î» i, by ext; simpâŸ©

end morphism

section cat
-- Categories and adjunctions form a category. TODO: Lots

omit ğ’Ÿ
def id : adjunction (functor.id C) (functor.id C) :=
{ unit := nat_trans.id _,
  counit := nat_trans.id _,
  left_triangle := by tidy,
  right_triangle := by tidy }

@[simp] lemma id.hom_equiv_app {X Y : C} (f : X âŸ¶ Y) : id.hom_equiv.to_fun f = f :=
by dsimp [hom_equiv, id]; simp

@[simp] lemma id.hom_equiv_inv {X Y : C} (f : X âŸ¶ Y) : id.hom_equiv.inv_fun f = f :=
by dsimp [hom_equiv, id]; simp

end cat

end


section construction

section
variables {F : C â¥¤ D} {G : D â¥¤ C}
variables (e : Î  X Y, (F.obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G.obj Y))
variables (heX : Î  X' X Y f h, e X' Y (F.map f â‰« h) = f â‰« e X Y h)
variables (heY : Î  X Y Y' g h, e X Y' (h â‰« g) = e X Y h â‰« G.map g)
include heX heY

def adjunction_of_equiv : adjunction F G :=
{ unit := 
  { app := Î» X, e X (F.obj X) (ğŸ™ _),
    naturality' := Î» X X' f, by erw [â†heX, â†heY]; dsimp; simp },
  counit :=
  { app := Î» Y, (e (G.obj Y) Y).symm (ğŸ™ _),
    naturality' := Î» Y Y' g, by rw [â†(e _ _).apply_eq_iff_eq]; erw [heX, heY]; dsimp; simp },
  left_triangle := by ext X; dsimp; rw [â†(e _ _).apply_eq_iff_eq, heX]; simp,
  right_triangle := by ext Y; dsimp; rw [â†heY]; simp }

lemma adjunction_of_equiv_equiv {X Y} : (adjunction_of_equiv e heX heY).hom_equiv = e X Y :=
begin
  ext h,
  dsimp [hom_equiv, adjunction_of_equiv],
  rw â†heY, simp
end

end

section construct_left
-- Construction of a left adjoint. In order to construct a left
-- adjoint to a functor G : D â†’ C, it suffices to give the object part
-- of a functor F : C â†’ D together with isomorphisms Hom(FX, Y) â‰ƒ
-- Hom(X, GY) natural in Y. The action of F on morphisms can be
-- constructed from this data.
variables {F_obj : C â†’ D} {G : D â¥¤ C}
variables (e : Î  X Y, (F_obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G.obj Y))
variables (he : Î  X Y Y' g h, e X Y' (h â‰« g) = e X Y h â‰« G.map g)
include he

def left_adjoint_of_equiv : C â¥¤ D :=
{ obj := F_obj,
  map := Î» X X' f, (e X (F_obj X')).symm (f â‰« e X' (F_obj X') (ğŸ™ _)),
  map_id' := Î» X, by simp,
  map_comp' := Î» X X' X'' f f', begin
    rw equiv.symm_apply_eq,
    rw he,
    rw equiv.apply_inverse_apply,
    conv { to_rhs, rw assoc, rw â†he, rw id_comp, rw equiv.apply_inverse_apply },
    simp
  end }

def adjunction_of_equiv_left : adjunction (left_adjoint_of_equiv e he) G :=
begin
  refine adjunction_of_equiv e _ he,
  intros X' X Y f h,
  dsimp [left_adjoint_of_equiv],
  rw [he, equiv.apply_inverse_apply, assoc, â†he],
  simp
end

lemma adjunction_of_equiv_left_equiv {X Y} :
  (adjunction_of_equiv_left e he).hom_equiv = e X Y :=
adjunction_of_equiv_equiv e _ he

end construct_left

section construct_right
-- Construction of a right adjoint, analogous to the above.
variables {F : C â¥¤ D} {G_obj : D â†’ C}
variables (e : Î  X Y, (F.obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G_obj Y))
variables (he : Î  X' X Y f h, e X' Y (F.map f â‰« h) = f â‰« e X Y h)
include he

private lemma he' (X' X Y f h) : F.map f â‰« (e X Y).symm h = (e X' Y).symm (f â‰« h) :=
by intros; rw [equiv.eq_symm_apply, he]; simp

def right_adjoint_of_equiv : D â¥¤ C :=
{ obj := G_obj,
  map := Î» Y Y' g, (e (G_obj Y) Y') ((e (G_obj Y) Y).symm (ğŸ™ _) â‰« g),
  map_id' := Î» Y, by simp,
  map_comp' := Î» Y Y' Y'' g g', begin
    rw â†equiv.eq_symm_apply,
    rw â†he' e he,
    rw equiv.inverse_apply_apply,
    conv { to_rhs, rw â†assoc, rw he' e he, rw comp_id, rw equiv.inverse_apply_apply },
    simp
  end }

def adjunction_of_equiv_right : adjunction F (right_adjoint_of_equiv e he) :=
begin
  refine adjunction_of_equiv e he _,
  intros X Y Y' g h,
  dsimp [right_adjoint_of_equiv],
  rw [â†he, equiv.apply_eq_iff_eq, â†assoc, he' e he],
  simp
end

lemma adjunction_of_equiv_right_equiv {X Y} :
  (adjunction_of_equiv_right e he).hom_equiv = e X Y :=
adjunction_of_equiv_equiv e he _

end construct_right

end construction

end adjunction

end category_theory

-- For limits, we need to set up again with C and D having morphisms
-- living in the same universe.

namespace category_theory.adjunction
open category_theory
open category_theory.limits

universes uâ‚ uâ‚‚ v

variables {C : Type uâ‚} [ğ’ : category.{uâ‚ v} C] {D : Type uâ‚‚} [ğ’Ÿ : category.{uâ‚‚ v} D]
include ğ’ ğ’Ÿ

variables {F : C â¥¤ D} {G : D â¥¤ C} (adj : adjunction F G)

def cocone_equiv {J : Type v} [small_category J] {X : J â¥¤ C} {Y : D} :
  (X.comp F âŸ¹ (functor.const J).obj Y) â‰ƒ (X âŸ¹ (functor.const J).obj (G.obj Y)) :=
{ to_fun := Î» t,
  { app := Î» j, adj.hom_equiv.to_fun (t.app j),
    naturality' := Î» j j' f, by erw [â†hom_equiv_naturality', t.naturality]; dsimp; simp },
  inv_fun := Î» t,
  { app := Î» j, adj.hom_equiv.inv_fun (t.app j),
    naturality' := Î» j j' f, begin
      erw [â†adj.hom_equiv_symm_naturality, â†adj.hom_equiv_symm_naturality', t.naturality],
      congr, dsimp, simp
    end },
  left_inv := Î» t, by ext j; apply adj.hom_equiv.left_inv,
  right_inv := Î» t, by ext j; apply adj.hom_equiv.right_inv }

def cone_equiv {J : Type v} [small_category J] {X : C} {Y : J â¥¤ D} :
  ((functor.const J).obj (F.obj X) âŸ¹ Y) â‰ƒ ((functor.const J).obj X âŸ¹ Y.comp G) :=
{ to_fun := Î» t,
  { app := Î» j, adj.hom_equiv.to_fun (t.app j),
    naturality' := Î» j j' f, begin
      erw [â†adj.hom_equiv_naturality, â†adj.hom_equiv_naturality', â†t.naturality],
      dsimp, simp
    end },
  inv_fun := Î» t,
  { app := Î» j, adj.hom_equiv.inv_fun (t.app j),
    naturality' := Î» j j' f,
      by erw [â†adj.hom_equiv_symm_naturality', â†t.naturality]; dsimp; simp },
  left_inv := Î» t, by ext j; apply adj.hom_equiv.left_inv,
  right_inv := Î» t, by ext j; apply adj.hom_equiv.right_inv }

section preservation

include adj

/-- A left adjoint preserves colimits. -/
def left_adjoint_preserves_colimits : preserves_colimits F :=
âŸ¨Î» J ğ’¥, by exactI Î» Y c h, limits.is_colimit.of_equiv
  (Î» Z, calc
     (F.obj c.X âŸ¶ Z) â‰ƒ (c.X âŸ¶ G.obj Z)            : adj.hom_equiv
     ... â‰ƒ (Y âŸ¹ (functor.const J).obj (G.obj Z))  : h.equiv
     ... â‰ƒ (Y.comp F âŸ¹ (functor.const J).obj Z)   : adj.cocone_equiv.symm)
  (Î» Z f j, begin
     dsimp [is_colimit.equiv, cocone_equiv],
     rw adj.hom_equiv_symm_naturality,
     erw adj.hom_equiv.left_inv f
   end)âŸ©

/-- A right adjoint preserves limits. -/
def right_adjoint_preserves_limits : preserves_limits G :=
âŸ¨Î» J ğ’¥, by exactI Î» Y c h, limits.is_limit.of_equiv
  (Î» Z, calc
     (Z âŸ¶ G.obj c.X) â‰ƒ (F.obj Z âŸ¶ c.X)            : adj.hom_equiv.symm
     ... â‰ƒ ((functor.const J).obj (F.obj Z) âŸ¹ Y)  : (h.equiv (F.obj Z)).to_equiv
     ... â‰ƒ ((functor.const J).obj Z âŸ¹ Y.comp G)   : adj.cone_equiv)
  (Î» Z f j, begin
     dsimp [is_limit.equiv, cone_equiv],
     rw adj.hom_equiv_naturality,
     erw adj.hom_equiv.right_inv f,
     simp
   end)âŸ©

end preservation

end category_theory.adjunction
