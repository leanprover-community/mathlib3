/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Scott Morrison, Johannes HÃ¶lzl, Reid Barton

Defines a category, as a typeclass parametrised by the type of objects.
Introduces notations
  `X âŸ¶ Y` for the morphism spaces,
  `f â‰« g` for composition in the 'arrows' convention.

Users may like to add `f âŠš g` for composition in the standard convention, using
```
local notation f ` âŠš `:80 g:80 := category.comp g f    -- type as \oo
```
-/

import tactic.restate_axiom
import tactic.replacer
import tactic.interactive
import tactic.tidy

namespace category_theory

universes u v

/-
The propositional fields of `category` are annotated with the auto_param `obviously`,
which is defined here as a
[`replacer` tactic](https://github.com/leanprover/mathlib/blob/master/docs/tactics.md#def_replacer).
We then immediately set up `obviously` to call `tidy`. Later, this can be replaced with more
powerful tactics.
-/
def_replacer obviously
@[obviously] meta def obviously' := tactic.tidy

/--
The typeclass `category C` describes morphisms associated to objects of type `C`.
The universe levels of the objects and morphisms are unconstrained, and will often need to be
specified explicitly, as `category.{u v} C`. (See also `large_category` and `small_category`.)
-/
class category (obj : Type u) : Type (max u (v+1)) :=
(hom      : obj â†’ obj â†’ Type v)
(id       : Î  X : obj, hom X X)
(comp     : Î  {X Y Z : obj}, hom X Y â†’ hom Y Z â†’ hom X Z)
(id_comp' : âˆ€ {X Y : obj} (f : hom X Y), comp (id X) f = f . obviously)
(comp_id' : âˆ€ {X Y : obj} (f : hom X Y), comp f (id Y) = f . obviously)
(assoc'   : âˆ€ {W X Y Z : obj} (f : hom W X) (g : hom X Y) (h : hom Y Z),
  comp (comp f g) h = comp f (comp g h) . obviously)

notation `ğŸ™` := category.id -- type as \b1
infixr ` â‰« `:80 := category.comp -- type as \gg
infixr ` âŸ¶ `:10 := category.hom -- type as \h

-- `restate_axiom` is a command that creates a lemma from a structure field,
-- discarding any auto_param wrappers from the type.
-- (It removes a backtick from the name, if it finds one, and otherwise adds "_lemma".)
restate_axiom category.id_comp'
restate_axiom category.comp_id'
restate_axiom category.assoc'
attribute [simp] category.id_comp category.comp_id category.assoc
attribute [trans] category.comp

lemma category.assoc_symm {C : Type u} [category.{u v} C] {W X Y Z : C} (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  f â‰« (g â‰« h) = (f â‰« g) â‰« h :=
by rw â†category.assoc

/--
A `large_category` has objects in one universe level higher than the universe level of
the morphisms. It is useful for examples such as the category of types, or the category
of groups, etc.
-/
abbreviation large_category (C : Type (u+1)) : Type (u+1) := category.{u+1 u} C
/--
A `small_category` has objects and morphisms in the same universe level.
-/
abbreviation small_category (C : Type u)     : Type (u+1) := category.{u u} C

structure bundled (c : Type u â†’ Type v) :=
(Î± : Type u)
(str : c Î±)

instance (c : Type u â†’ Type v) : has_coe_to_sort (bundled c) :=
{ S := Type u, coe := bundled.Î± }

def mk_ob {c : Type u â†’ Type v} (Î± : Type u) [str : c Î±] : bundled c :=
@bundled.mk c Î± str

/-- `concrete_category hom` collects the evidence that a type constructor `c` and a morphism
predicate `hom` can be thought of as a concrete category.
In a typical example, `c` is the type class `topological_space` and `hom` is `continuous`. -/
structure concrete_category {c : Type u â†’ Type v}
  (hom : out_param $ âˆ€{Î± Î² : Type u}, c Î± â†’ c Î² â†’ (Î± â†’ Î²) â†’ Prop) :=
(hom_id : âˆ€{Î±} (ia : c Î±), hom ia ia id)
(hom_comp : âˆ€{Î± Î² Î³} (ia : c Î±) (ib : c Î²) (ic : c Î³) {f g},
  hom ia ib f â†’ hom ib ic g â†’ hom ia ic (g âˆ˜ f))
attribute [class] concrete_category

instance {c : Type u â†’ Type v} (hom : âˆ€{Î± Î² : Type u}, c Î± â†’ c Î² â†’ (Î± â†’ Î²) â†’ Prop)
  [h : concrete_category @hom] : category (bundled c) :=
{ hom   := Î»a b, subtype (hom a.2 b.2),
  id    := Î»a, âŸ¨@id a.1, h.hom_id a.2âŸ©,
  comp  := Î»a b c f g, âŸ¨g.1 âˆ˜ f.1, h.hom_comp a.2 b.2 c.2 f.2 g.2âŸ© }

@[simp] lemma concrete_category_id
  {c : Type u â†’ Type v} (hom : âˆ€{Î± Î² : Type u}, c Î± â†’ c Î² â†’ (Î± â†’ Î²) â†’ Prop)
  [h : concrete_category @hom] (X : bundled c) : subtype.val (ğŸ™ X) = id := rfl
@[simp] lemma concrete_category_comp
  {c : Type u â†’ Type v} (hom : âˆ€{Î± Î² : Type u}, c Î± â†’ c Î² â†’ (Î± â†’ Î²) â†’ Prop)
  [h : concrete_category @hom] {X Y Z : bundled c} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  subtype.val (f â‰« g) = g.val âˆ˜ f.val := rfl

instance {c : Type u â†’ Type v} (hom : âˆ€{Î± Î² : Type u}, c Î± â†’ c Î² â†’ (Î± â†’ Î²) â†’ Prop)
  [h : concrete_category @hom] {R S : bundled c} : has_coe_to_fun (R âŸ¶ S) :=
{ F := Î» f, R â†’ S,
  coe := Î» f, f.1 }

@[simp] lemma bundled_hom_coe
  {c : Type u â†’ Type v} (hom : âˆ€{Î± Î² : Type u}, c Î± â†’ c Î² â†’ (Î± â†’ Î²) â†’ Prop)
  [h : concrete_category @hom] {R S : bundled c} (val : R â†’ S) (prop) (r : R) :
  (âŸ¨val, propâŸ© : R âŸ¶ S) r = val r := rfl

section
variables {C : Type u} [ğ’ : category.{u v} C] {X Y Z : C}
include ğ’

class epi  (f : X âŸ¶ Y) : Prop :=
(left_cancellation : Î  {Z : C} (g h : Y âŸ¶ Z) (w : f â‰« g = f â‰« h), g = h)
class mono (f : X âŸ¶ Y) : Prop :=
(right_cancellation : Î  {Z : C} (g h : Z âŸ¶ X) (w : g â‰« f = h â‰« f), g = h)

@[simp] lemma cancel_epi  (f : X âŸ¶ Y) [epi f]  (g h : Y âŸ¶ Z) : (f â‰« g = f â‰« h) â†” g = h :=
âŸ¨ Î» p, epi.left_cancellation g h p, begin intro a, subst a end âŸ©
@[simp] lemma cancel_mono (f : X âŸ¶ Y) [mono f] (g h : Z âŸ¶ X) : (g â‰« f = h â‰« f) â†” g = h :=
âŸ¨ Î» p, mono.right_cancellation g h p, begin intro a, subst a end âŸ©
end

section
variable (C : Type u)
variable [category.{u v} C]

universe u'

instance ulift_category : category.{(max u u') v} (ulift.{u'} C) :=
{ hom  := Î» X Y, (X.down âŸ¶ Y.down),
  id   := Î» X, ğŸ™ X.down,
  comp := Î» _ _ _ f g, f â‰« g }

-- We verify that this previous instance can lift small categories to large categories.
example (D : Type u) [small_category D] : large_category (ulift.{u+1} D) := by apply_instance
end

variables (Î± : Type u)

instance [preorder Î±] : small_category Î± :=
{ hom  := Î» U V, ulift (plift (U â‰¤ V)),
  id   := Î» X, âŸ¨ âŸ¨ le_refl X âŸ© âŸ©,
  comp := Î» X Y Z f g, âŸ¨ âŸ¨ le_trans f.down.down g.down.down âŸ© âŸ© }

section
variables {C : Type u} [ğ’ : category.{u v} C]
include ğ’

def End (X : C) := X âŸ¶ X

instance {X : C} : monoid (End X) := by refine { one := ğŸ™ X, mul := Î» x y, x â‰« y, .. } ; obviously
end

end category_theory
