import category_theory.category
import category_theory.functor
import category_theory.functor_category
import algebra.category.CommRing
import algebra.category.Group.images
import algebra.homology.homology
import topology.category.Top
import topology.instances.real
import category_theory.limits.shapes.finite_limits
import data.int.parity
import data.zmod.basic
import category_theory.abelian.basic
import algebra.category.Module.monoidal
import topology.category.UniformSpace
import ..for_mathlib.algebra.category.Group.kernels


/-!
This is a demo of the category theory library in mathlib,
as part of "Lean for the Curious Mathematician 2020".

You can get this file by:
* installing Lean if necessary: https://leanprover-community.github.io/get_started.html#regular-install
* `leanproject get mathlib`
* `code mathlib`
* open the file `docs/tutorial/lftcm2020/src/demos/category_theory.lean`

If you've already got a copy of `mathlib`, you should update it now, using
```
  cd /path/to/mathlib/
  git pull
  leanproject get-cache
```

There are also exercises associated with this demo, in
`exercise_sources/thursday/category_theory/`
with hints at
`hints/category_theory/`
and (partial) solutions at
`solutions/thursday/category_theory/`
Any of Exercises 1-7 should be approachable after the demo.
The later exercises are quite hard, and will take you longer than the afternoon problem session!
-/

open category_theory

/-!
## Categories

Categories are implemented in mathlib as a typeclass, parametrised by the type of objects.

Thus to talk about an arbitrary category, we can write
-/
variables (C : Type) [category C]

/-!
There is special notation for the morphisms in a category: if `X Y : C`, we write
* `X âŸ¶ Y` for the type of morphisms from `X` to `Y`.
  (To enter the special arrow `âŸ¶`, type `\hom`, or hover over the symbol to see the hint.)
* `ðŸ™ X` is a the identity morphisms on `X` (i.e., a term of type `X âŸ¶ X`).
* If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z`, then we write `f â‰« g` for the composition, a morphism `X âŸ¶ Z`.
-/

example {W X Y Z : C} (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (f â‰« (ðŸ™ X â‰« g)) â‰« h = f â‰« g â‰« h :=
begin
  rw category.id_comp,
  rw category.assoc,
  -- alternatively, just `simp` will do
end

/-!
## Functors

To introduce functors, we'll need a second category around.
-/
variables (D : Type) [category D]

/-!
We write a functor as `F : C â¥¤ D`.
(Unlike categories, which are partially unbundled, a functor is "fully bundled",
containing the function on objects as field. This parallels the design for algebraic structures.)
-/

example (F : C â¥¤ D) (X : C) : F.map (ðŸ™ X) = ðŸ™ (F.obj X) :=
F.map_id X

example (F : C â¥¤ D) {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : F.map (f â‰« g) = F.map f â‰« F.map g :=
F.map_comp f g

/-!
The identity functor is written as `ðŸ­ C`, and functor composition is written `â‹™`.
-/
example (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) : (ðŸ­ C â‹™ F).map (f â‰« ðŸ™ Y) = F.map f :=
begin
  rw functor.comp_map,
  rw functor.map_comp,
  rw category_theory.functor.map_id, -- yuck! we really should fix this
  rw functor.id_map,
  rw functor.map_comp,
  rw category_theory.functor.map_id,
  rw category.comp_id,
  -- or just replace the entire proof with `by simp`
end

/-!
To build a functor `F : C â¥¤ D` we need to specify four fields
* `obj : C â†’ D`
* `map : âˆ€ {X Y : C} (f : X âŸ¶ Y), obj X âŸ¶ obj Y`
* `map_id'` and `map_comp'`, expressing the functor laws.
-/

example {X : C} : C â¥¤ Type* :=
{ obj := Î» Y, X âŸ¶ Y,
  map := Î» Y Y' f g, g â‰« f,
  map_id' := Î» X, begin funext, simp, end,
  map_comp' := Î» X Y Z f g, begin funext, simp, end }

/-!
However Lean will automatically attempt to fill in the `map_id'` and `map_comp'` fields itself,
because these fields are marked with `auto_param`. This lets us specify a tactic to use to
try to synthesize the field.

(In fact, the whole category theory library started off as an experiment to see how far we could
push this automation.)
-/

example {X : C} : C â¥¤ Type* :=
{ obj := Î» Y, X âŸ¶ Y,
  map := Î» Y Y' f g, g â‰« f, }

/-!
Lean automatically checked functoriality here!
This was pretty easy: we just need to use `category.comp_id` and `category.assoc`.
The more powerful we make the `simp` lemmas, the more boring goals can be discharged automatically.

Most of the `auto_param`s appearing in mathlib so far are in the `category_theory` library,
where they are nearly all filled using the tactic `tidy`, which repeatedly attempts to use
one of a list of "conservative" tactics.

You can see what `tidy` is doing using `tidy?`:
-/

example {X : C} : C â¥¤ Type* :=
{ obj := Î» Y, X âŸ¶ Y,
  map := Î» Y Y' f g, g â‰« f,
  map_id' := by tidy?,
  map_comp' := by tidy? }

/-!
Sebastien's talk on differential geometry tomorrow will give another example of `auto_param` being used.

You can also watch me doing a speed-run https://youtu.be/oz3z2NSNY8c of Floris's "pointed map" exercises
from yesterday, taking advantage of `auto_param`.
-/




/-!
## Natural transformations

The collection of functors from `C` to `D` has been given the structure of a category:
to talk about the natural transformations, you just write `F âŸ¶ G` using the usual "morphism" arrow.

If `Î± : F âŸ¶ G`, then `Î±.app X` is the component at `X`, i.e. a morphism `F.obj X âŸ¶ G.obj X`.
-/
example {F G : C â¥¤ D} {Î± : F âŸ¶ G} {X Y : C} (f : X âŸ¶ Y) :
  F.map f â‰« Î±.app Y = Î±.app X â‰« G.map f :=
Î±.naturality f   -- or just `by simp`

/-!
Again, to construct a natural transformation `F âŸ¶ G` we need to provide two fields
* `app : Î  X : C, F.obj X âŸ¶ G.obj X` and
* `naturality'`, which often is provided by automation.
-/


/-!
## A note on universes

Before we go on, we should mention a slight complication: out in the world we meet
both small and large categories. In set-theoretic foundations, this distinction is about
whether the objects form a set or merely a class.

In the type-theoretic foundations used in Lean, this distinction is about whether
the objects and morphisms live in the same universe, or if the objects live one universe higher up.

Rather than making separate definitions for the two cases, we simply allow the objects and morphisms
to live in two unrelated universes. To talk about a general category we thus write
-/
universes u v

variables (E : Type u) [category.{v} E]

/-!
This says that the objects live in universe `u`, while the morphisms live in universe `v`.
In fact, the definition `category` is paramaterised by two universe levels, and
when we write `category.{v} E` Lean actually understands this as `category.{v u} E`,
automatically filling in the second argument from the universe level of `E`.

There are abbreviations available for the two standard cases:
* if `E : Type (u+1)`, then `large_category E` means `category.{u (u+1)} E`
* if `E : Type u`, then `small_category E` means `category.{u u} E`.

However you'll rarely use these except when setting up particular examples.
All the "concrete" categories, like `Group`, `Ring`, and `Top`, described below,
are instances of `large_category`.
Typically the indexing diagrams for limits and colimits are instances of `small_category`.

If you're talking about an arbitrary category, and you don't mind whether it is small or large,
you should just allow two independent universe variables, as above.
-/


/-!
## Concrete categories

We've set up a number of concrete categories in mathlib.
-/

example (R S : CommRing) (f : R âŸ¶ S) (x y : R) : f (x * y) = f x * f y := by simp

/-!
Note here we have a particularly succinct way of introducing a commutative ring:
we just write `R : CommRing`, rather than `(R : Type) [comm_ring R]`.
Rather than writing `f : R â†’+* S` for a `ring_hom`, we can just use the morphism arrow,
and Lean works out the appropriate notion automatically.

There's a coercion from `CommRing` to `Type`,
so we can still talk about elements by writing `x : R`,
and morphisms automatically behave properly as functions (e.g. in `f (x * y)`).
-/


/-!
## Limits and colimits

We talk about limits using the following notions:
* For `F : J â¥¤ C`, `c : cone F` consists of
  * `c.X : C` an object in `C`, and
  * `c.Ï€`, a natural transformation with components `c.Ï€.app j : c.X âŸ¶ F.obj j`.
* For `c : cone F`, `is_limit c` expresses that `c` is a limit cone.
* `has_limit F`, a typeclass specifying a particular choice of limit cone for a functor `F`.
* `has_limits C`, a typeclass specifying a choice of limit for any functor into `C`.

(There are also all the dual notions, `cocone`, `is_colimit`, `has_colimit`, etc.)

There are also typeclasses for various "special shapes", in particular
* `has_equalizers`
* `has_pullbacks`
* `has_binary_products` / `has_finite_products` / `has_products`
* `has_terminal`

A related typeclass `has_zero_morphisms C` specifies a choice of zero morphism in each hom space,
satisfying the usual axioms (equivalent to `C` being enriched in pointed sets), and using that
we can also express some other special shapes, including
* `has_kernels`
* `has_binary_biproducts` / `has_finite_biproducts`
* `has_zero_object`

For most of the concrete categories, these instances are all available when appropriate.
-/

/-!
### Examples of using (co)limits in `Top`
-/

noncomputable theory
open category_theory.limits

def R : Top := Top.of â„
def I : Top := Top.of (set.Icc 0 1 : set â„)
def pt : Top := Top.of unit

-- Let's construct the mapping cylinder.
def to_pt (X : Top) : X âŸ¶ pt :=
{ to_fun := Î» _, unit.star, continuous_to_fun := continuous_const }

def Iâ‚€ : pt âŸ¶ I :=
{ to_fun := Î» _, âŸ¨(0 : â„), by norm_num [set.left_mem_Icc]âŸ©,
  continuous_to_fun := continuous_const }

def Iâ‚ : pt âŸ¶ I :=
{ to_fun := Î» _, âŸ¨(1 : â„), by norm_num [set.right_mem_Icc]âŸ©,
  continuous_to_fun := continuous_const }

-- We now construct a cylinder as a categorical limit.
-- `limits.prod` is a shorthand for constructing a limit over the two point diagram:
def cylinder (X : Top) : Top := prod X I

-- To define a map to the cylinder, we give a map to each factor.
-- `prod.lift` is a helper method, providing a wrapper around `limit.lift` for binary products.
def cylinderâ‚€ (X : Top) : X âŸ¶ cylinder X := prod.lift (ðŸ™ X) (to_pt X â‰« Iâ‚€)
def cylinderâ‚ (X : Top) : X âŸ¶ cylinder X := prod.lift (ðŸ™ X) (to_pt X â‰« Iâ‚)

/--
The mapping cylinder is the pushout of the diagram
```
    X
   â†™ â†˜
  Y   (X x I)
```
(`pushout` is implemented just as a wrapper around `colimit`)
-/
def mapping_cylinder {X Y : Top} (f : X âŸ¶ Y) : Top := pushout f (cylinderâ‚ X)


/-!
It's perhaps worth admitting here that constructing objects using categorical (co)limits
typically gives quite ghastly "definitional" properties --- if you want to use these objects,
you're going to have to work through their universal properties.

This is not necessarily a bad thing, but takes some getting used to.
-/

/-!
## Applications

We're only just getting to the point in mathlib where we're ready to do the sorts of mathematics
that rely on category theory as a basic language. There's lots more to come ---
big chunks of algebraic geometry, homological algebra, quantum topology, etc.

One important way in which we'll use the category theory library is to achieve polymorphism.
We don't want to separately prove theorems about sheaves of sets, sheaves of rings, etc.
Instead we'd like to talk about sheaves in an arbitrary category,
possibly with some additional typeclasses providing extra structure
(`has_products`, `concrete_category`, `monoidal_category`, etc),
and prove our theorems there.
-/

/-!
### Homological algebra

We've recently set up the very basics of homoological algebra using the category theory library.
There's still a way to go --- good projects for the near future include
* injective covers and resolutions
* `Ext` and `Tor`
* bicomplexes, the salamander, snake, five, and nine lemmas

Here's something you can do already:
-/

open category_theory.limits
local notation `Ab` := AddCommGroup.{0}

local attribute [instance] has_equalizers_of_has_finite_limits
local attribute [instance] has_coequalizers_of_has_finite_colimits

noncomputable theory -- `has_images Ab` is noncomputable!

open cochain_complex homological_complex

abbreviation Z := AddCommGroup.of â„¤

def mul_by (k : â„¤) : Z âŸ¶ Z :=
add_monoid_hom.of (gsmul k)

/--
We define the complex `... --0--> â„¤ --2--> â„¤ --0--> â„¤ --4--> â„¤ --0--> ...`
-/
def P : cochain_complex Ab :=
{ X := Î» i, Z,
  d := Î» i, if i.even then mul_by i else 0,
  d_squared' :=
  begin
    ext i, dsimp,
    by_cases h : i.even;
    simp [h] with parity_simps,
  end }

#check (graded_cohomology Ab).obj P

/--
Let's try to calculate the cohomology!
-/

@[simp] lemma mul_by_apply (k x : â„¤) : mul_by k x = (k * x : â„¤) := by simp [mul_by]

lemma mul_by_ker {k : â„¤} (h : k â‰  0) : (mul_by k).ker = âŠ¥ :=
begin
  tidy,
  { simp only [add_monoid_hom.mem_ker] at a, finish, },
  { subst a, simp [add_monoid_hom.mem_ker], },
end

def P_2 : P.cohomology_group 2 â‰… 0 :=
begin
  change cokernel (image_to_kernel_map 0 (mul_by 2) _) â‰… 0,
  calc cokernel (image_to_kernel_map 0 (mul_by 2) (has_zero_morphisms.zero_comp _ _))
         â‰… cokernel (0 : image (0 : Z âŸ¶ Z) âŸ¶ kernel (mul_by 2)) : cokernel_iso_of_eq (by simp)
     ... â‰… kernel (mul_by 2) : cokernel_zero_iso_target
     ... â‰… AddCommGroup.of (mul_by 2).ker : AddCommGroup.kernel_iso_ker (mul_by 2)
     ... â‰… AddCommGroup.of (âŠ¥ : add_subgroup Z) : AddCommGroup.of_add_subgroup_eq (mul_by_ker (by norm_num))
     ... â‰… 0 : AddCommGroup.of_add_subgroup_bot,
end.

-- Somehow this isn't in mathlib?
def quotient_gsmul (k : â„•) :
  quotient_add_group.quotient (set.range (gsmul k : â„¤ â†’ â„¤)) â‰ƒ+ zmod k :=
sorry

def P_3 : P.cohomology_group 3 â‰… AddCommGroup.of (zmod 2) :=
begin
  change cokernel (image_to_kernel_map (mul_by 2) 0 _) â‰… AddCommGroup.of (zmod 2),
  calc cokernel (image_to_kernel_map (mul_by 2) 0 _)
         â‰… cokernel (image.Î¹ (mul_by 2) â‰« inv (kernel.Î¹ (0 : Z âŸ¶ Z))) :
            cokernel_iso_of_eq (image_to_kernel_map_zero_right _)
     ... â‰… cokernel (image.Î¹ (mul_by 2)) : cokernel_comp_is_iso _ _
     ... â‰… cokernel (mul_by 2) : cokernel_image_Î¹ _
     ... â‰… AddCommGroup.of (quotient_add_group.quotient (set.range (mul_by 2))) : AddCommGroup.cokernel_iso_quotient _
     ... â‰… AddCommGroup.of (zmod 2) : add_equiv_iso_AddCommGroup_iso.hom (quotient_gsmul 2),
end

/-!
## Odds and ends

There's a bunch in mathlib's `category_theory/` folder that hasn't been mentioned at all here,
including:

* Adjunctions
* Equivalences
* Monads
* Abelian categories
* Monoidal categories
* ...
-/

#print category_theory.adjunction.right_adjoint_preserves_limits

#print category_theory.abelian
-- Right now we don't have a single instance of `abelian` in the library:
-- constructing `abelian AddCommGroup` is a great exercise, and all the ingredients are available.

example (R : CommRing) : monoidal_category (Module R) := by apply_instance

example : reflective (forgetâ‚‚ CpltSepUniformSpace UniformSpace) := by apply_instance
