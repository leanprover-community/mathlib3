/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import tactic.noncomm_ring
import field_theory.is_alg_closed.basic
/-!
# Spectrum of an element in an algebra
This file develops the basic theory of the spectrum of an element of an algebra.
This theory will serve as the foundation for spectral theory in Banach algebras.

## Main definitions

* `resolvent_set a : set R`: the resolvent set of an element `a : A` where
  `A` is an  `R`-algebra.
* `spectrum a : set R`: the spectrum of an element `a : A` where
  `A` is an  `R`-algebra.
* `resolvent : R â†’ A`: the resolvent function is `Î» r, ring.inverse (â†‘â‚r - a)`, and hence
  when `r âˆˆ resolvent R A`, it is actually the inverse of the unit `(â†‘â‚r - a)`.

## Main statements

* `spectrum.unit_smul_eq_smul` and `spectrum.smul_eq_smul`: units in the scalar ring commute
  (multiplication) with the spectrum, and over a field even `0` commutes with the spectrum.
* `spectrum.left_add_coset_eq`: elements of the scalar ring commute (addition) with the spectrum.
* `spectrum.unit_mem_mul_iff_mem_swap_mul` and `spectrum.preimage_units_mul_eq_swap_mul`: the
  units (of `R`) in `Ïƒ (a*b)` coincide with those in `Ïƒ (b*a)`.
* `spectrum.scalar_eq`: in a nontrivial algebra over a field, the spectrum of a scalar is
  a singleton.
* `spectrum.subset_polynomial_aeval`, `spectrum.map_polynomial_aeval_of_degree_pos`,
  `spectrum.map_polynomial_aeval_of_nonempty`: variations on the spectral mapping theorem.

## Notations

* `Ïƒ a` : `spectrum R a` of `a : A`
-/

universes u v


section defs

variables (R : Type u) {A : Type v}
variables [comm_semiring R] [ring A] [algebra R A]

-- definition and basic properties

/-- Given a commutative ring `R` and an `R`-algebra `A`, the *resolvent set* of `a : A`
is the `set R` consisting of those `r : R` for which `râ€¢1 - a` is a unit of the
algebra `A`.  -/
def resolvent_set (a : A) : set R :=
{ r : R | is_unit (algebra_map R A r - a) }


/-- Given a commutative ring `R` and an `R`-algebra `A`, the *spectrum* of `a : A`
is the `set R` consisting of those `r : R` for which `râ€¢1 - a` is not a unit of the
algebra `A`.

The spectrum is simply the complement of the resolvent set.  -/
def spectrum (a : A) : set R :=
(resolvent_set R a)á¶œ

variable {R}
/-- Given an `a : A` where `A` is an `R`-algebra, the *resolvent* is
    a map `R â†’ A` which sends `r : R` to `(algebra_map R A r - a)â»Â¹` when
    `r âˆˆ resolvent R A` and `0` when `r âˆˆ spectrum R A`. -/
noncomputable def resolvent (a : A) (r : R) : A :=
ring.inverse (algebra_map R A r - a)


end defs


-- products of scalar units and algebra units


lemma is_unit.smul_sub_iff_sub_inv_smul {R : Type u} {A : Type v}
  [comm_ring R] [ring A] [algebra R A] {r : units R} {a : A} :
  is_unit (r â€¢ 1 - a) â†” is_unit (1 - râ»Â¹ â€¢ a) :=
begin
  have a_eq : a = râ€¢râ»Â¹â€¢a, by simp,
  nth_rewrite 0 a_eq,
  rw [â†smul_sub,is_unit_smul_iff],
end

namespace spectrum

section scalar_ring

variables {R : Type u} {A : Type v}
variables [comm_ring R] [ring A] [algebra R A]

local notation `Ïƒ` := spectrum R
local notation `â†‘â‚` := algebra_map R A

lemma mem_iff {r : R} {a : A} :
  r âˆˆ Ïƒ a â†” Â¬ is_unit (â†‘â‚r - a) :=
iff.rfl

lemma not_mem_iff {r : R} {a : A} :
  r âˆ‰ Ïƒ a â†” is_unit (â†‘â‚r - a) :=
by { apply not_iff_not.mp, simp [set.not_not_mem, mem_iff] }

lemma mem_resolvent_set_of_left_right_inverse {r : R} {a b c : A}
  (hâ‚ : (â†‘â‚r - a) * b = 1) (hâ‚‚ : c * (â†‘â‚r - a) = 1) :
  r âˆˆ resolvent_set R a :=
units.is_unit âŸ¨â†‘â‚r - a, b, hâ‚, by rwa â†left_inv_eq_right_inv hâ‚‚ hâ‚âŸ©

lemma mem_resolvent_set_iff {r : R} {a : A} :
  r âˆˆ resolvent_set R a â†” is_unit (â†‘â‚r - a) :=
iff.rfl

lemma resolvent_eq {a : A} {r : R} (h : r âˆˆ resolvent_set R a) :
  resolvent a r = â†‘h.unitâ»Â¹ :=
ring.inverse_unit h.unit

lemma add_mem_iff {a : A} {r s : R} :
  r âˆˆ Ïƒ a â†” r + s âˆˆ Ïƒ (â†‘â‚s + a) :=
begin
  apply not_iff_not.mpr,
  simp only [mem_resolvent_set_iff],
  have h_eq : â†‘â‚(r + s) - (â†‘â‚s + a) = â†‘â‚r - a,
    { simp, noncomm_ring },
  rw h_eq,
end

lemma smul_mem_smul_iff {a : A} {s : R} {r : units R} :
  r â€¢ s âˆˆ Ïƒ (r â€¢ a) â†” s âˆˆ Ïƒ a :=
begin
  apply not_iff_not.mpr,
  simp only [mem_resolvent_set_iff, algebra.algebra_map_eq_smul_one],
  have h_eq : (r â€¢ s) â€¢ (1 : A) = r â€¢ s â€¢ 1, by simp,
  rw [h_eq, â†smul_sub, is_unit_smul_iff],
end

open_locale pointwise

theorem unit_smul_eq_smul (a : A) (r : units R) :
  Ïƒ (r â€¢ a) = r â€¢ Ïƒ a :=
begin
  ext,
  have x_eq : x = r â€¢ râ»Â¹ â€¢ x, by simp,
  nth_rewrite 0 x_eq,
  rw smul_mem_smul_iff,
  split,
    { exact Î» h, âŸ¨râ»Â¹ â€¢ x, âŸ¨h, by simpâŸ©âŸ©},
    { rintros âŸ¨_, _, x'_eqâŸ©, simpa [â†x'_eq],}
end

theorem left_add_coset_eq (a : A) (r : R) :
  left_add_coset r (Ïƒ a) = Ïƒ (â†‘â‚r + a) :=
by { ext, rw [mem_left_add_coset_iff, neg_add_eq_sub, add_mem_iff],
     nth_rewrite 1 â†sub_add_cancel x r, }

-- `r âˆˆ Ïƒ(a*b) â†” r âˆˆ Ïƒ(b*a)` for any `r : units R`
theorem unit_mem_mul_iff_mem_swap_mul {a b : A} {r : units R} :
  â†‘r âˆˆ Ïƒ (a * b) â†” â†‘r âˆˆ Ïƒ (b * a) :=
begin
  apply not_iff_not.mpr,
  simp only [mem_resolvent_set_iff, algebra.algebra_map_eq_smul_one],
  have coe_smul_eq : â†‘r â€¢ 1 = r â€¢ (1 : A), from rfl,
  rw coe_smul_eq,
  simp only [is_unit.smul_sub_iff_sub_inv_smul],
  have right_inv_of_swap : âˆ€ {x y z : A} (h : (1 - x * y) * z = 1),
    (1 - y * x) * (1 + y * z * x) = 1, from Î» x y z h,
      calc (1 - y * x) * (1 + y * z * x) = 1 - y * x + y * ((1 - x * y) * z) * x : by noncomm_ring
      ...                                = 1                                     : by simp [h],
  have left_inv_of_swap : âˆ€ {x y z : A} (h : z * (1 - x * y) = 1),
    (1 + y * z * x) * (1 - y * x) = 1, from Î» x y z h,
      calc (1 + y * z * x) * (1 - y * x) = 1 - y * x + y * (z * (1 - x * y)) * x : by noncomm_ring
      ...                                = 1                                     : by simp [h],
  have is_unit_one_sub_mul_of_swap : âˆ€ {x y : A} (h : is_unit (1 - x * y)),
    is_unit (1 - y * x), from Î» x y h, by
      { let hâ‚ := right_inv_of_swap h.unit.val_inv,
        let hâ‚‚ := left_inv_of_swap h.unit.inv_val,
        exact âŸ¨âŸ¨1 - y * x, 1 + y * h.unit.inv * x, hâ‚, hâ‚‚âŸ©, rflâŸ©, },
  have is_unit_one_sub_mul_iff_swap : âˆ€ {x y : A},
    is_unit (1 - x * y) â†” is_unit (1 - y * x), by
      { intros, split, repeat {apply is_unit_one_sub_mul_of_swap}, },
  rw [â†smul_mul_assoc, â†mul_smul_comm râ»Â¹ b a, is_unit_one_sub_mul_iff_swap],
end

theorem preimage_units_mul_eq_swap_mul {a b : A} :
  (coe : units R â†’ R) â»Â¹' Ïƒ (a * b) = coe â»Â¹'  Ïƒ (b * a) :=
by { ext, exact unit_mem_mul_iff_mem_swap_mul, }

end scalar_ring

section scalar_field

variables {ğ•œ : Type u} {A : Type v}
variables [field ğ•œ] [ring A] [algebra ğ•œ A]

local notation `Ïƒ` := spectrum ğ•œ
local notation `â†‘â‚` := algebra_map ğ•œ A

/-- Without the assumption `nontrivial A`, then `0 : A` would be invertible. -/
@[simp] lemma zero_eq [nontrivial A] : Ïƒ (0 : A) = {0} :=
begin
  refine set.subset.antisymm _ (by simp [algebra.algebra_map_eq_smul_one, mem_iff]),
  rw [spectrum, set.compl_subset_comm],
  intros k hk,
  rw set.mem_compl_singleton_iff at hk,
  have : is_unit (units.mk0 k hk â€¢ (1 : A)) := is_unit.smul (units.mk0 k hk) is_unit_one,
  simpa [mem_resolvent_set_iff, algebra.algebra_map_eq_smul_one]
end

@[simp] theorem scalar_eq [nontrivial A] (k : ğ•œ) : Ïƒ (â†‘â‚k) = {k} :=
begin
  have coset_eq : left_add_coset k {0} = {k}, by
    { ext, split,
      { intro hx, simp [left_add_coset] at hx, exact hx, },
      { intro hx, simp at hx, exact âŸ¨0, âŸ¨set.mem_singleton 0, by simp [hx]âŸ©âŸ©, }, },
  calc Ïƒ (â†‘â‚k) = Ïƒ (â†‘â‚k + 0)                  : by simp
    ...        = left_add_coset k (Ïƒ (0 : A)) : by rw â†left_add_coset_eq
    ...        = left_add_coset k {0}         : by rw zero_eq
    ...        = {k}                          : coset_eq,
end

@[simp] lemma one_eq [nontrivial A] : Ïƒ (1 : A) = {1} :=
calc Ïƒ (1 : A) = Ïƒ (â†‘â‚1) : by simp [algebra.algebra_map_eq_smul_one]
  ...          = {1}     : scalar_eq 1

open_locale pointwise

/-- the assumption `(Ïƒ a).nonempty` is necessary and cannot be removed without
    further conditions on the algebra `A` and scalar field `ğ•œ`. -/
theorem smul_eq_smul [nontrivial A] (k : ğ•œ) (a : A) (ha : (Ïƒ a).nonempty) :
  Ïƒ (k â€¢ a) = k â€¢ (Ïƒ a) :=
begin
  rcases eq_or_ne k 0 with rfl | h,
  { simpa [ha, zero_smul_set] },
  { exact unit_smul_eq_smul a (units.mk0 k h) },
end

theorem nonzero_mul_eq_swap_mul (a b : A) : Ïƒ (a * b) \ {0} = Ïƒ (b * a) \ {0} :=
begin
  suffices h : âˆ€ (x y : A), Ïƒ (x * y) \ {0} âŠ† Ïƒ (y * x) \ {0},
  { exact set.eq_of_subset_of_subset (h a b) (h b a) },
  { rintros _ _ k âŸ¨k_mem, k_neqâŸ©,
    change k with â†‘(units.mk0 k k_neq) at k_mem,
    exact âŸ¨unit_mem_mul_iff_mem_swap_mul.mp k_mem, k_neqâŸ© },
end

open polynomial
/-- Half of the spectral mapping theorem for polynomials. We prove it separately
because it holds over any field, whereas `spectrum.map_polynomial_aeval_of_degree_pos` and
`spectrum.map_polynomial_aeval_of_nonempty` need the field to be algebraically closed. -/
theorem subset_polynomial_aeval (a : A) (p : polynomial ğ•œ) :
  (Î» k, eval k p) '' (Ïƒ a) âŠ† Ïƒ (aeval a p) :=
begin
  rintros _ âŸ¨k, hk, rflâŸ©,
  let q := C (eval k p) - p,
  have hroot : is_root q k, by simp only [eval_C, eval_sub, sub_self, is_root.def],
  rw [â†mul_div_eq_iff_is_root, â†neg_mul_neg, neg_sub] at hroot,
  have aeval_q_eq : â†‘â‚(eval k p) - aeval a p = aeval a q,
    by simp only [aeval_C, alg_hom.map_sub, sub_left_inj],
  rw [mem_iff, aeval_q_eq, â†hroot, aeval_mul],
  have hcomm := (commute.all (C k - X) (- (q / (X - C k)))).map (aeval a),
  apply mt (Î» h, (hcomm.is_unit_mul_iff.mp h).1),
  simpa only [aeval_X, aeval_C, alg_hom.map_sub] using hk,
end

lemma exists_mem_of_not_is_unit_aeval_prod {p : polynomial ğ•œ} {a : A} (hp : p â‰  0)
  (h : Â¬is_unit (aeval a (multiset.map (Î» (x : ğ•œ), X - C x) p.roots).prod)) :
  âˆƒ k : ğ•œ, k âˆˆ Ïƒ a âˆ§ eval k p = 0 :=
begin
  rw [â†multiset.prod_to_list, alg_hom.map_list_prod] at h,
  replace h := mt list.prod_is_unit h,
  simp only [not_forall, exists_prop, aeval_C, multiset.mem_to_list,
    list.mem_map, aeval_X, exists_exists_and_eq_and, multiset.mem_map, alg_hom.map_sub] at h,
  rcases h with âŸ¨r, r_mem, r_nuâŸ©,
  exact âŸ¨r, by rwa [mem_iff, â†is_unit.sub_iff], by rwa [â†is_root.def, â†mem_roots hp]âŸ©
end

/-- The *spectral mapping theorem* for polynomials.  Note: the assumption `degree p > 0`
is necessary in case `Ïƒ a = âˆ…`, for then the left-hand side is `âˆ…` and the right-hand side,
assuming `[nontrivial A]`, is `{k}` where `p = polynomial.C k`. -/
theorem map_polynomial_aeval_of_degree_pos [is_alg_closed ğ•œ] (a : A) (p : polynomial ğ•œ)
  (hdeg : 0 < degree p) : Ïƒ (aeval a p) = (Î» k, eval k p) '' (Ïƒ a) :=
begin
  /- handle the easy direction via `spectrum.subset_polynomial_aeval` -/
  refine set.eq_of_subset_of_subset (Î» k hk, _) (subset_polynomial_aeval a p),
  /- write `C k - p` product of linear factors and a constant; show `C k - p â‰  0`. -/
  have hprod := eq_prod_roots_of_splits_id (is_alg_closed.splits (C k - p)),
  have h_ne : C k - p â‰  0, from ne_zero_of_degree_gt
    (by rwa [degree_sub_eq_right_of_degree_lt (lt_of_le_of_lt degree_C_le hdeg)]),
  have lead_ne := leading_coeff_ne_zero.mpr h_ne,
  have lead_unit := (units.map (â†‘â‚).to_monoid_hom (units.mk0 _ lead_ne)).is_unit,
  /- leading coefficient is a unit so product of linear factors is not a unit;
  apply `exists_mem_of_not_is_unit_aeval_prod`. -/
  have p_a_eq : aeval a (C k - p) = â†‘â‚k - aeval a p,
    by simp only [aeval_C, alg_hom.map_sub, sub_left_inj],
  rw [mem_iff, â†p_a_eq, hprod, aeval_mul,
    ((commute.all _ _).map (aeval a)).is_unit_mul_iff, aeval_C] at hk,
  replace hk := exists_mem_of_not_is_unit_aeval_prod h_ne (not_and.mp hk lead_unit),
  rcases hk with âŸ¨r, r_mem, r_evâŸ©,
  exact âŸ¨r, r_mem, symm (by simpa [eval_sub, eval_C, sub_eq_zero] using r_ev)âŸ©,
end

/-- In this version of the spectral mapping theorem, we assume the spectrum
is nonempty instead of assuming the degree of the polynomial is positive. Note: the
assumption `[nontrivial A]` is necessary for the same reason as in `spectrum.zero_eq`. -/
theorem map_polynomial_aeval_of_nonempty [is_alg_closed ğ•œ] [nontrivial A] (a : A) (p : polynomial ğ•œ)
  (hnon : (Ïƒ a).nonempty) : Ïƒ (aeval a p) = (Î» k, eval k p) '' (Ïƒ a) :=
begin
  refine or.elim (le_or_gt (degree p) 0) (Î» h, _) (map_polynomial_aeval_of_degree_pos a p),
  { rw eq_C_of_degree_le_zero h,
    simp only [set.image_congr, eval_C, aeval_C, scalar_eq, set.nonempty.image_const hnon] },
end

variable (ğ•œ)
/--
Every element `a` in a nontrivial finite-dimensional algebra `A`
over an algebraically closed field `ğ•œ` has non-empty spectrum. -/
-- We will use this both to show eigenvalues exist, and to prove Schur's lemma.
lemma nonempty_of_is_alg_closed_of_finite_dimensional [is_alg_closed ğ•œ]
  [nontrivial A] [I : finite_dimensional ğ•œ A] (a : A) :
  âˆƒ k : ğ•œ, k âˆˆ Ïƒ a :=
begin
  obtain âŸ¨p, âŸ¨h_mon, h_eval_pâŸ©âŸ© := is_integral_of_noetherian (is_noetherian.iff_fg.2 I) a,
  have nu : Â¬ is_unit (aeval a p), { rw [â†aeval_def] at h_eval_p, rw h_eval_p, simp, },
  rw [eq_prod_roots_of_monic_of_splits_id h_mon (is_alg_closed.splits p)] at nu,
  obtain âŸ¨k, hk, _âŸ© := exists_mem_of_not_is_unit_aeval_prod (monic.ne_zero h_mon) nu,
  exact âŸ¨k, hkâŸ©
end

end scalar_field

end spectrum
