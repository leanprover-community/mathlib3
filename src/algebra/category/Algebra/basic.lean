/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.algebra.subalgebra.basic
import algebra.free_algebra
import algebra.category.Ring.basic
import algebra.category.Module.basic

/-!
# Category instance for algebras over a commutative ring

We introduce the bundled category `Algebra` of algebras over a fixed commutative ring `R ` along
with the forgetful functors to `Ring` and `Module`. We furthermore show that the functor associating
to a type the free `R`-algebra on that type is left adjoint to the forgetful functor.
-/

open category_theory
open category_theory.limits

universes v u

variables (R : Type u) [comm_ring R]

/-- The category of R-algebras and their morphisms. -/
structure Algebra :=
(carrier : Type v)
[is_ring : ring carrier]
[is_algebra : algebra R carrier]

attribute [instance] Algebra.is_ring Algebra.is_algebra

namespace Algebra

instance : has_coe_to_sort (Algebra R) (Type v) := ‚ü®Algebra.carrier‚ü©

instance : category (Algebra.{v} R) :=
{ hom   := Œª A B, A ‚Üí‚Çê[R] B,
  id    := Œª A, alg_hom.id R A,
  comp  := Œª A B C f g, g.comp f }

instance : concrete_category.{v} (Algebra.{v} R) :=
{ forget := { obj := Œª R, R, map := Œª R S f, (f : R ‚Üí S) },
  forget_faithful := { } }

instance has_forget_to_Ring : has_forget‚ÇÇ (Algebra.{v} R) Ring.{v} :=
{ forget‚ÇÇ :=
  { obj := Œª A, Ring.of A,
    map := Œª A‚ÇÅ A‚ÇÇ f, alg_hom.to_ring_hom f, } }

instance has_forget_to_Module : has_forget‚ÇÇ (Algebra.{v} R) (Module.{v} R) :=
{ forget‚ÇÇ :=
  { obj := Œª M, Module.of R M,
    map := Œª M‚ÇÅ M‚ÇÇ f, alg_hom.to_linear_map f, } }

/-- The object in the category of R-algebras associated to a type equipped with the appropriate
typeclasses. -/
def of (X : Type v) [ring X] [algebra R X] : Algebra.{v} R := ‚ü®X‚ü©

/-- Typecheck a `alg_hom` as a morphism in `Algebra R`. -/
def of_hom {R : Type u} [comm_ring R] {X Y : Type v} [ring X] [algebra R X] [ring Y] [algebra R Y]
  (f : X ‚Üí‚Çê[R] Y) : of R X ‚ü∂ of R Y := f

@[simp] lemma of_hom_apply {R : Type u} [comm_ring R]
  {X Y : Type v} [ring X] [algebra R X] [ring Y] [algebra R Y] (f : X ‚Üí‚Çê[R] Y) (x : X) :
  of_hom f x = f x := rfl

instance : inhabited (Algebra R) := ‚ü®of R R‚ü©

@[simp]
lemma coe_of (X : Type u) [ring X] [algebra R X] : (of R X : Type u) = X := rfl

variables {R}

/-- Forgetting to the underlying type and then building the bundled object returns the original
algebra. -/
@[simps]
def of_self_iso (M : Algebra.{v} R) : Algebra.of R M ‚âÖ M :=
{ hom := ùüô M, inv := ùüô M }

variables {R} {M N U : Module.{v} R}

@[simp] lemma id_apply (m : M) : (ùüô M : M ‚Üí M) m = m := rfl

@[simp] lemma coe_comp (f : M ‚ü∂ N) (g : N ‚ü∂ U) :
  ((f ‚â´ g) : M ‚Üí U) = g ‚àò f := rfl

variables (R)
/-- The "free algebra" functor, sending a type `S` to the free algebra on `S`. -/
@[simps]
def free : Type u ‚•§ Algebra.{u} R :=
{ obj := Œª S,
  { carrier := free_algebra R S,
    is_ring := algebra.semiring_to_ring R },
  map := Œª S T f, free_algebra.lift _ $ (free_algebra.Œπ _) ‚àò f,
  -- obviously can fill the next two goals, but it is slow
  map_id' := by { intros X, ext1, simp only [free_algebra.Œπ_comp_lift], refl },
  map_comp' := by { intros, ext1, simp only [free_algebra.Œπ_comp_lift], ext1,
    simp only [free_algebra.lift_Œπ_apply, category_theory.coe_comp, function.comp_app,
      types_comp_apply] } }

/-- The free/forget adjunction for `R`-algebras. -/
def adj : free.{u} R ‚ä£ forget (Algebra.{u} R) :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X A, (free_algebra.lift _).symm,
  -- Relying on `obviously` to fill out these proofs is very slow :(
  hom_equiv_naturality_left_symm' := by { intros, ext,
    simp only [free_map, equiv.symm_symm, free_algebra.lift_Œπ_apply, category_theory.coe_comp,
      function.comp_app, types_comp_apply] },
  hom_equiv_naturality_right' := by { intros, ext,
    simp only [forget_map_eq_coe, category_theory.coe_comp, function.comp_app,
      free_algebra.lift_symm_apply, types_comp_apply] } }

instance : is_right_adjoint (forget (Algebra.{u} R)) := ‚ü®_, adj R‚ü©

end Algebra

variables {R}
variables {X‚ÇÅ X‚ÇÇ : Type u}

/-- Build an isomorphism in the category `Algebra R` from a `alg_equiv` between `algebra`s. -/
@[simps]
def alg_equiv.to_Algebra_iso
  {g‚ÇÅ : ring X‚ÇÅ} {g‚ÇÇ : ring X‚ÇÇ} {m‚ÇÅ : algebra R X‚ÇÅ} {m‚ÇÇ : algebra R X‚ÇÇ} (e : X‚ÇÅ ‚âÉ‚Çê[R] X‚ÇÇ) :
  Algebra.of R X‚ÇÅ ‚âÖ Algebra.of R X‚ÇÇ :=
{ hom := (e : X‚ÇÅ ‚Üí‚Çê[R] X‚ÇÇ),
  inv := (e.symm : X‚ÇÇ ‚Üí‚Çê[R] X‚ÇÅ),
  hom_inv_id' := begin ext, exact e.left_inv x, end,
  inv_hom_id' := begin ext, exact e.right_inv x, end, }

namespace category_theory.iso

/-- Build a `alg_equiv` from an isomorphism in the category `Algebra R`. -/
@[simps]
def to_alg_equiv {X Y : Algebra R} (i : X ‚âÖ Y) : X ‚âÉ‚Çê[R] Y :=
{ to_fun    := i.hom,
  inv_fun   := i.inv,
  left_inv  := by tidy,
  right_inv := by tidy,
  map_add'  := by tidy,
  map_mul'  := by tidy,
  commutes' := by tidy, }.

end category_theory.iso

/-- Algebra equivalences between `algebras`s are the same as (isomorphic to) isomorphisms in
`Algebra`. -/
@[simps]
def alg_equiv_iso_Algebra_iso {X Y : Type u}
  [ring X] [ring Y] [algebra R X] [algebra R Y] :
  (X ‚âÉ‚Çê[R] Y) ‚âÖ (Algebra.of R X ‚âÖ Algebra.of R Y) :=
{ hom := Œª e, e.to_Algebra_iso,
  inv := Œª i, i.to_alg_equiv, }

instance (X : Type u) [ring X] [algebra R X] : has_coe (subalgebra R X) (Algebra R) :=
‚ü® Œª N, Algebra.of R N ‚ü©

instance Algebra.forget_reflects_isos : reflects_isomorphisms (forget (Algebra.{u} R)) :=
{ reflects := Œª X Y f _,
  begin
    resetI,
    let i := as_iso ((forget (Algebra.{u} R)).map f),
    let e : X ‚âÉ‚Çê[R] Y := { ..f, ..i.to_equiv },
    exact ‚ü®(is_iso.of_iso e.to_Algebra_iso).1‚ü©,
  end }
