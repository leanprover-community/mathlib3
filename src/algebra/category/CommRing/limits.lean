/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.category.CommRing.basic
import algebra.category.Group.limits
import category_theory.limits.creates
import deprecated.subgroup

/-!
# The category of commutative rings has all limits

Further, these limits are preserved by the forgetful functor --- that is,
the underlying types are just the limits in the category of types.

## Further work
A lot of this should be generalised / automated, as it's quite common for concrete
categories that the forgetful functor preserves limits.
-/

open category_theory
open category_theory.limits

universe u

namespace Ring

variables {J : Type u} [small_category J]

instance ring_obj (F : J â¥¤ Ring) (j) :
  ring ((F â‹™ forget Ring).obj j) :=
by { change ring (F.obj j), apply_instance }

/--
The flat sections of a functor into `Ring` form a multiplicative submonoid of all sections.
-/
def sections_submonoid (F : J â¥¤ Ring) :
  submonoid (Î  j, F.obj j) :=
{ carrier := (F â‹™ forget Ring).sections,
  one_mem' := Î» j j' f, by simp,
  mul_mem' := Î» a b ah bh j j' f,
  begin
    simp only [forget_map_eq_coe, functor.comp_map, ring_hom.map_mul, pi.mul_apply],
    dsimp [functor.sections] at ah bh,
    rw [ah f, bh f],
  end }

-- We still don't have bundled subrings,
-- so we need to convert the bundled sub-objects back to unbundled

instance sections_submonoid' (F : J â¥¤ Ring) :
  is_submonoid (F â‹™ forget Ring).sections :=
(sections_submonoid F).is_submonoid

instance sections_add_subgroup' (F : J â¥¤ Ring) :
  is_add_subgroup (F â‹™ forget Ring).sections :=
(AddCommGroup.sections_add_subgroup (F â‹™ forgetâ‚‚ Ring AddCommGroup)).is_subgroup

instance sections_subring (F : J â¥¤ Ring) :
  is_subring (F â‹™ forget Ring).sections := {}

instance limit_comm_ring (F : J â¥¤ Ring) :
  ring (limit (F â‹™ forget Ring)) :=
@subtype.ring ((Î  (j : J), (F â‹™ forget _).obj j)) (by apply_instance) _
  (by convert (Ring.sections_subring F))

/-- `limit.Ï€ (F â‹™ forget Ring) j` as a `ring_hom`. -/
def limit_Ï€_ring_hom (F : J â¥¤ Ring) (j) :
  limit (F â‹™ forget Ring) â†’+* (F â‹™ forget Ring).obj j :=
{ to_fun := limit.Ï€ (F â‹™ forget Ring) j,
  map_one' := by { simp only [types.types_limit_Ï€], refl },
  map_zero' := by { simp only [types.types_limit_Ï€], refl },
  map_mul' := Î» x y, by { simp only [types.types_limit_Ï€], refl },
  map_add' := Î» x y, by { simp only [types.types_limit_Ï€], refl } }

namespace has_limits
-- The next two definitions are used in the construction of `has_limits Ring`.
-- After that, the limits should be constructed using the generic limits API,
-- e.g. `limit F`, `limit.cone F`, and `limit.is_limit F`.

/--
Construction of a limit cone in `Ring`.
(Internal use only; use the limits API.)
-/
def limit (F : J â¥¤ Ring) : cone F :=
{ X := Ring.of (limit (F â‹™ forget _)),
  Ï€ :=
  { app := limit_Ï€_ring_hom F,
    naturality' := Î» j j' f,
      ring_hom.coe_inj ((limit.cone (F â‹™ forget _)).Ï€.naturality f) } }

/--
Witness that the limit cone in `Ring` is a limit cone.
(Internal use only; use the limits API.)
-/
def limit_is_limit (F : J â¥¤ Ring) : is_limit (limit F) :=
begin
  refine is_limit.of_faithful
    (forget Ring) (limit.is_limit _)
    (Î» s, âŸ¨_, _, _, _, _âŸ©) (Î» s, rfl); tidy
end

end has_limits

open has_limits

/-- The category of rings has all limits. -/
instance has_limits : has_limits Ring :=
{ has_limits_of_shape := Î» J ğ’¥,
  { has_limit := Î» F, by exactI
    { cone     := limit F,
      is_limit := limit_is_limit F } } }

/--
The forgetful functor from commutative rings to types preserves all limits. (That is, the underlying
types could have been computed instead as limits in the category of types.)
-/
instance forget_preserves_limits : preserves_limits (forget Ring) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (limit.is_limit (F â‹™ forget _)) } }

end Ring


namespace CommRing

variables {J : Type u} [small_category J]

instance comm_ring_obj (F : J â¥¤ CommRing) (j) :
  comm_ring ((F â‹™ forget CommRing).obj j) :=
by { change comm_ring (F.obj j), apply_instance }

instance limit_comm_ring (F : J â¥¤ CommRing) :
  comm_ring (limit (F â‹™ forget CommRing)) :=
@subtype.comm_ring ((Î  (j : J), (F â‹™ forget _).obj j)) (by apply_instance) _
  (by convert (Ring.sections_subring (F â‹™ forgetâ‚‚ CommRing Ring)))

/--
We show that the forgetful functor `CommRing â¥¤ Ring` creates limits.

All we need to do is notice that the limit point has a `comm_ring` instance available,
and then reuse the existing limit.
-/
instance (F : J â¥¤ CommRing) : creates_limit F (forgetâ‚‚ CommRing Ring) :=
/-
A terse solution here would be
```
creates_limit_of_fully_faithful_of_iso (CommRing.of (limit (F â‹™ forget _))) (iso.refl _)
```
but it seems this would introduce additional identity morphisms in `limit.Ï€`.
-/
creates_limit_of_reflects_iso (Î» c' t,
{ lifted_cone :=
  { X := CommRing.of (limit (F â‹™ forget _)),
    Ï€ :=
    { app := Ring.limit_Ï€_ring_hom (F â‹™ forgetâ‚‚ CommRing Ring),
      naturality' := (Ring.has_limits.limit (F â‹™ forgetâ‚‚ _ _)).Ï€.naturality, } },
  valid_lift := is_limit.unique_up_to_iso (limit.is_limit _) t,
  makes_limit := is_limit.of_faithful (forgetâ‚‚ CommRing Ring) (limit.is_limit _)
    (Î» s, _) (Î» s, rfl) })

/-- The category of commutative rings has all limits. -/
instance has_limits : has_limits CommRing :=
{ has_limits_of_shape := Î» J ğ’¥, by exactI
  { has_limit := Î» F, has_limit_of_created F (forgetâ‚‚ CommRing Ring) } }

/--
The forgetful functor from commutative rings to rings preserves all limits.
(That is, the underlying rings could have been computed instead as limits in the category of rings.)
-/
instance forgetâ‚‚_Ring_preserves_limits : preserves_limits (forgetâ‚‚ CommRing Ring) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F, by apply_instance } }

/--
The forgetful functor from commutative rings to types preserves all limits.
(That is, the underlying types could have been computed instead as limits in the category of types.)
-/
instance forget_preserves_limits : preserves_limits (forget CommRing) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (limit.is_limit (F â‹™ forget _)) } }

end CommRing
