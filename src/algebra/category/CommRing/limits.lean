/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.ring.pi
import algebra.category.CommRing.basic
import algebra.category.Group.limits
import ring_theory.subring
import ring_theory.subsemiring

/-!
# The category of (commutative) rings has all limits

Further, these limits are preserved by the forgetful functor --- that is,
the underlying types are just the limits in the category of types.
-/

open category_theory
open category_theory.limits

universe u

namespace SemiRing

variables {J : Type u} [small_category J]

instance semiring_obj (F : J â¥¤ SemiRing) (j) :
  semiring ((F â‹™ forget SemiRing).obj j) :=
by { change semiring (F.obj j), apply_instance }

/--
The flat sections of a functor into `SemiRing` form a subsemiring of all sections.
-/
def sections_subsemiring (F : J â¥¤ SemiRing) :
  subsemiring (Î  j, F.obj j) :=
{ carrier := (F â‹™ forget SemiRing).sections,
  ..(AddMon.sections_add_submonoid (F â‹™ forgetâ‚‚ SemiRing AddCommMon â‹™ forgetâ‚‚ AddCommMon AddMon)),
  ..(Mon.sections_submonoid (F â‹™ forgetâ‚‚ SemiRing Mon)) }

instance limit_semiring (F : J â¥¤ SemiRing) :
  semiring (limit (F â‹™ forget SemiRing.{u})) :=
(sections_subsemiring F).to_semiring

/-- `limit.Ï€ (F â‹™ forget SemiRing) j` as a `ring_hom`. -/
def limit_Ï€_ring_hom (F : J â¥¤ SemiRing.{u}) (j) :
  limit (F â‹™ forget SemiRing) â†’+* (F â‹™ forget SemiRing).obj j :=
{ to_fun := limit.Ï€ (F â‹™ forget SemiRing) j,
  ..AddMon.limit_Ï€_add_monoid_hom (F â‹™ forgetâ‚‚ SemiRing AddCommMon.{u} â‹™ forgetâ‚‚ AddCommMon AddMon) j,
  ..Mon.limit_Ï€_monoid_hom (F â‹™ forgetâ‚‚ SemiRing Mon) j, }

namespace has_limits
-- The next two definitions are used in the construction of `has_limits SemiRing`.
-- After that, the limits should be constructed using the generic limits API,
-- e.g. `limit F`, `limit.cone F`, and `limit.is_limit F`.

/--
Construction of a limit cone in `SemiRing`.
(Internal use only; use the limits API.)
-/
def limit (F : J â¥¤ SemiRing) : cone F :=
{ X := SemiRing.of (limit (F â‹™ forget _)),
  Ï€ :=
  { app := limit_Ï€_ring_hom F,
    naturality' := Î» j j' f,
      ring_hom.coe_inj ((limit.cone (F â‹™ forget _)).Ï€.naturality f) } }

/--
Witness that the limit cone in `SemiRing` is a limit cone.
(Internal use only; use the limits API.)
-/
def limit_is_limit (F : J â¥¤ SemiRing) : is_limit (limit F) :=
begin
  refine is_limit.of_faithful
    (forget SemiRing) (limit.is_limit _)
    (Î» s, âŸ¨_, _, _, _, _âŸ©) (Î» s, rfl); tidy
end

end has_limits

open has_limits

/-- The category of rings has all limits. -/
instance has_limits : has_limits SemiRing :=
{ has_limits_of_shape := Î» J ğ’¥,
  { has_limit := Î» F, by exactI
    { cone     := limit F,
      is_limit := limit_is_limit F } } }

/--
An auxiliary declaration to speed up typechecking.
-/
def forgetâ‚‚_AddCommMon_preserves_limits_aux (F : J â¥¤ SemiRing) :
  is_limit ((forgetâ‚‚ SemiRing AddCommMon).map_cone (limit.cone F)) :=
  limit.is_limit (F â‹™ forgetâ‚‚ SemiRing AddCommMon)

/--
The forgetful functor from semirings to additive commutative monoids preserves all limits.
-/
instance forgetâ‚‚_AddCommMon_preserves_limits : preserves_limits (forgetâ‚‚ SemiRing AddCommMon) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (forgetâ‚‚_AddCommMon_preserves_limits_aux F) } }

/--
An auxiliary declaration to speed up typechecking.
-/
def forgetâ‚‚_Mon_preserves_limits_aux (F : J â¥¤ SemiRing) :
  is_limit ((forgetâ‚‚ SemiRing Mon).map_cone (limit.cone F)) :=
  limit.is_limit (F â‹™ forgetâ‚‚ SemiRing Mon)

/--
The forgetful functor from semirings to monoids preserves all limits.
-/
instance forgetâ‚‚_Mon_preserves_limits :
  preserves_limits (forgetâ‚‚ SemiRing Mon) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (forgetâ‚‚_Mon_preserves_limits_aux F) } }

/--
The forgetful functor from semirings to types preserves all limits.
-/
instance forget_preserves_limits : preserves_limits (forget SemiRing) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (limit.is_limit (F â‹™ forget _)) } }

end SemiRing

namespace CommSemiRing

variables {J : Type u} [small_category J]

instance comm_semiring_obj (F : J â¥¤ CommSemiRing) (j) :
  comm_semiring ((F â‹™ forget CommSemiRing).obj j) :=
by { change comm_semiring (F.obj j), apply_instance }

instance limit_comm_semiring (F : J â¥¤ CommSemiRing) :
  comm_semiring (limit (F â‹™ forget CommSemiRing.{u})) :=
@subsemiring.to_comm_semiring (Î  j, F.obj j) _
  (SemiRing.sections_subsemiring (F â‹™ forgetâ‚‚ CommSemiRing SemiRing.{u}))

/--
We show that the forgetful functor `CommSemiRing â¥¤ SemiRing` creates limits.

All we need to do is notice that the limit point has a `comm_semiring` instance available,
and then reuse the existing limit.
-/
instance (F : J â¥¤ CommSemiRing) : creates_limit F (forgetâ‚‚ CommSemiRing SemiRing.{u}) :=
creates_limit_of_reflects_iso (Î» c' t,
{ lifted_cone :=
  { X := CommSemiRing.of (limit (F â‹™ forget _)),
    Ï€ :=
    { app := SemiRing.limit_Ï€_ring_hom (F â‹™ forgetâ‚‚ CommSemiRing SemiRing),
      naturality' := (SemiRing.has_limits.limit (F â‹™ forgetâ‚‚ _ _)).Ï€.naturality, } },
  valid_lift := is_limit.unique_up_to_iso (limit.is_limit _) t,
  makes_limit := is_limit.of_faithful (forgetâ‚‚ CommSemiRing SemiRing.{u}) (limit.is_limit _)
    (Î» s, _) (Î» s, rfl) })

/-- The category of rings has all limits. -/
instance has_limits : has_limits CommSemiRing.{u} :=
{ has_limits_of_shape := Î» J ğ’¥, by exactI
  { has_limit := Î» F, has_limit_of_created F (forgetâ‚‚ CommSemiRing SemiRing.{u}) } }

/--
The forgetful functor from rings to semirings preserves all limits.
-/
instance forgetâ‚‚_SemiRing_preserves_limits : preserves_limits (forgetâ‚‚ CommSemiRing SemiRing) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F, by apply_instance } }

/--
The forgetful functor from rings to types preserves all limits. (That is, the underlying
types could have been computed instead as limits in the category of types.)
-/
instance forget_preserves_limits : preserves_limits (forget CommSemiRing) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (limit.is_limit (F â‹™ forget _)) } }

end CommSemiRing


namespace Ring

variables {J : Type u} [small_category J]

instance ring_obj (F : J â¥¤ Ring) (j) :
  ring ((F â‹™ forget Ring).obj j) :=
by { change ring (F.obj j), apply_instance }

-- We still don't have bundled subrings,
-- so we need to convert the bundled sub-objects back to unbundled

instance sections_submonoid' (F : J â¥¤ Ring) :
  is_submonoid (F â‹™ forget Ring).sections :=
(Mon.sections_submonoid (F â‹™ forgetâ‚‚ Ring SemiRing â‹™ forgetâ‚‚ SemiRing Mon)).is_submonoid

instance sections_add_subgroup' (F : J â¥¤ Ring) :
  is_add_subgroup (F â‹™ forget Ring).sections :=
(AddGroup.sections_add_subgroup (F â‹™ forgetâ‚‚ Ring AddCommGroup â‹™ forgetâ‚‚ AddCommGroup AddGroup)).is_add_subgroup

instance sections_subring (F : J â¥¤ Ring) :
  is_subring (F â‹™ forget Ring).sections := {}

instance limit_ring (F : J â¥¤ Ring) :
  ring (limit (F â‹™ forget Ring.{u})) :=
@subtype.ring ((Î  (j : J), (F â‹™ forget _).obj j)) (by apply_instance) _
  (by convert (Ring.sections_subring F))

/--
We show that the forgetful functor `CommRing â¥¤ Ring` creates limits.

All we need to do is notice that the limit point has a `ring` instance available,
and then reuse the existing limit.
-/
instance (F : J â¥¤ Ring) : creates_limit F (forgetâ‚‚ Ring SemiRing.{u}) :=
creates_limit_of_reflects_iso (Î» c' t,
{ lifted_cone :=
  { X := Ring.of (limit (F â‹™ forget _)),
    Ï€ :=
    { app := SemiRing.limit_Ï€_ring_hom (F â‹™ forgetâ‚‚ Ring SemiRing),
      naturality' := (SemiRing.has_limits.limit (F â‹™ forgetâ‚‚ _ _)).Ï€.naturality, } },
  valid_lift := is_limit.unique_up_to_iso (limit.is_limit _) t,
  makes_limit := is_limit.of_faithful (forgetâ‚‚ Ring SemiRing.{u}) (limit.is_limit _)
    (Î» s, _) (Î» s, rfl) })

/-- The category of rings has all limits. -/
instance has_limits : has_limits Ring :=
{ has_limits_of_shape := Î» J ğ’¥, by exactI
  { has_limit := Î» F, has_limit_of_created F (forgetâ‚‚ Ring SemiRing) } }

/--
The forgetful functor from rings to semirings preserves all limits.
-/
instance forgetâ‚‚_SemiRing_preserves_limits : preserves_limits (forgetâ‚‚ Ring SemiRing) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F, by apply_instance } }

/--
An auxiliary declaration to speed up typechecking.
-/
def forgetâ‚‚_AddCommGroup_preserves_limits_aux (F : J â¥¤ Ring) :
  is_limit ((forgetâ‚‚ Ring AddCommGroup).map_cone (limit.cone F)) :=
  limit.is_limit (F â‹™ forgetâ‚‚ Ring AddCommGroup)

/--
The forgetful functor from rings to additive commutative groups preserves all limits.
-/
instance forgetâ‚‚_AddCommGroup_preserves_limits : preserves_limits (forgetâ‚‚ Ring AddCommGroup) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (forgetâ‚‚_AddCommGroup_preserves_limits_aux F) } }

/--
The forgetful functor from rings to types preserves all limits. (That is, the underlying
types could have been computed instead as limits in the category of types.)
-/
instance forget_preserves_limits : preserves_limits (forget Ring) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (limit.is_limit (F â‹™ forget _)) } }

end Ring


namespace CommRing

variables {J : Type u} [small_category J]

instance comm_ring_obj (F : J â¥¤ CommRing) (j) :
  comm_ring ((F â‹™ forget CommRing).obj j) :=
by { change comm_ring (F.obj j), apply_instance }

instance limit_comm_ring (F : J â¥¤ CommRing) :
  comm_ring (limit (F â‹™ forget CommRing.{u})) :=
@subtype.comm_ring ((Î  (j : J), (F â‹™ forget _).obj j)) (by apply_instance) _
  (by convert (Ring.sections_subring (F â‹™ forgetâ‚‚ CommRing Ring.{u})))

/--
We show that the forgetful functor `CommRing â¥¤ Ring` creates limits.

All we need to do is notice that the limit point has a `comm_ring` instance available,
and then reuse the existing limit.
-/
instance (F : J â¥¤ CommRing) : creates_limit F (forgetâ‚‚ CommRing Ring.{u}) :=
/-
A terse solution here would be
```
creates_limit_of_fully_faithful_of_iso (CommRing.of (limit (F â‹™ forget _))) (iso.refl _)
```
but it seems this would introduce additional identity morphisms in `limit.Ï€`.
-/
creates_limit_of_reflects_iso (Î» c' t,
{ lifted_cone :=
  { X := CommRing.of (limit (F â‹™ forget _)),
    Ï€ :=
    { app := SemiRing.limit_Ï€_ring_hom (F â‹™ forgetâ‚‚ CommRing Ring.{u} â‹™ forgetâ‚‚ Ring SemiRing),
      naturality' := (SemiRing.has_limits.limit (F â‹™ forgetâ‚‚ _ _ â‹™ forgetâ‚‚ _ _)).Ï€.naturality, } },
  valid_lift := is_limit.unique_up_to_iso (limit.is_limit _) t,
  makes_limit := is_limit.of_faithful (forgetâ‚‚ CommRing Ring.{u}) (limit.is_limit _)
    (Î» s, _) (Î» s, rfl) })

/-- The category of commutative rings has all limits. -/
instance has_limits : has_limits CommRing.{u} :=
{ has_limits_of_shape := Î» J ğ’¥, by exactI
  { has_limit := Î» F, has_limit_of_created F (forgetâ‚‚ CommRing Ring.{u}) } }

/--
The forgetful functor from commutative rings to rings preserves all limits.
(That is, the underlying rings could have been computed instead as limits in the category of rings.)
-/
instance forgetâ‚‚_Ring_preserves_limits : preserves_limits (forgetâ‚‚ CommRing Ring) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F, by apply_instance } }

/--
The forgetful functor from commutative rings to types preserves all limits.
(That is, the underlying types could have been computed instead as limits in the category of types.)
-/
instance forget_preserves_limits : preserves_limits (forget CommRing) :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  { preserves_limit := Î» F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit.is_limit F) (limit.is_limit (F â‹™ forget _)) } }

end CommRing
