/-
Copyright (c) 2021 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import category_theory.monoidal.rigid.basic
import category_theory.monoidal.subcategory
import linear_algebra.tensor_product_basis
import linear_algebra.coevaluation
import algebra.category.Module.monoidal

/-!
# The category of finite dimensional vector spaces

This introduces `FinVect K`, the category of finite dimensional vector spaces over a field `K`.
It is implemented as a full subcategory on a subtype of `Module K`.

We first create the instance as a `K`-linear category,
then as a `K`-linear monoidal category and then as a right-rigid monoidal category.

## Future work

* Show that `FinVect K` is a symmetric monoidal category (it is already monoidal).
* Show that `FinVect K` is abelian.
* Show that `FinVect K` is rigid (it is already right rigid).

-/
noncomputable theory

open category_theory Module.monoidal_category
open_locale classical big_operators

universes u

variables (K : Type u) [field K]

instance monoidal_predicate_finite_dimensional :
  monoidal_category.monoidal_predicate (Œª V : Module.{u} K, finite_dimensional K V) :=
{ prop_id' := finite_dimensional.finite_dimensional_self K,
  prop_tensor' := Œª X Y hX hY, by exactI module.finite.tensor_product K X Y }

/-- Define `FinVect` as the subtype of `Module.{u} K` of finite dimensional vector spaces. -/
@[derive [large_category, Œª Œ±, has_coe_to_sort Œ± (Sort*), concrete_category, preadditive, linear K,
  monoidal_category, symmetric_category, monoidal_preadditive, monoidal_linear K]]
def FinVect := { V : Module.{u} K // finite_dimensional K V }

namespace FinVect

instance finite_dimensional (V : FinVect K) : finite_dimensional K V := V.prop

instance : inhabited (FinVect K) := ‚ü®‚ü®Module.of K K, finite_dimensional.finite_dimensional_self K‚ü©‚ü©

instance : has_coe (FinVect.{u} K) (Module.{u} K) := { coe := Œª V, V.1, }

protected lemma coe_comp {U V W : FinVect K} (f : U ‚ü∂ V) (g : V ‚ü∂ W) :
  ((f ‚â´ g) : U ‚Üí W) = (g : V ‚Üí W) ‚àò (f : U ‚Üí V) := rfl

/-- Lift an unbundled vector space to `FinVect K`. -/
def of (V : Type u) [add_comm_group V] [module K V] [finite_dimensional K V] : FinVect K :=
‚ü®Module.of K V, by { change finite_dimensional K V, apply_instance }‚ü©

instance (V W : FinVect K) : finite_dimensional K (V ‚ü∂ W) :=
(by apply_instance : finite_dimensional K (V ‚Üí‚Çó[K] W))

instance : has_forget‚ÇÇ (FinVect.{u} K) (Module.{u} K) :=
by { dsimp [FinVect], apply_instance, }

instance : full (forget‚ÇÇ (FinVect K) (Module.{u} K)) :=
{ preimage := Œª X Y f, f, }

/-- The forgetful functor `FinVect K ‚•§ Module K` as a monoidal functor. -/
def forget‚ÇÇ_monoidal : monoidal_functor (FinVect K) (Module.{u} K) :=
monoidal_category.full_monoidal_subcategory_inclusion _

instance forget‚ÇÇ_monoidal_faithful : faithful (forget‚ÇÇ_monoidal K).to_functor :=
by { dsimp [forget‚ÇÇ_monoidal], apply_instance, }

instance forget‚ÇÇ_monoidal_additive : (forget‚ÇÇ_monoidal K).to_functor.additive :=
by { dsimp [forget‚ÇÇ_monoidal], apply_instance, }

instance forget‚ÇÇ_monoidal_linear : (forget‚ÇÇ_monoidal K).to_functor.linear K :=
by { dsimp [forget‚ÇÇ_monoidal], apply_instance, }

variables (V : FinVect K)

/-- The dual module is the dual in the rigid monoidal category `FinVect K`. -/
def FinVect_dual : FinVect K :=
‚ü®Module.of K (module.dual K V), subspace.module.dual.finite_dimensional‚ü©

instance : has_coe_to_fun (FinVect_dual K V) (Œª _, V ‚Üí K) :=
{ coe := Œª v, by { change V ‚Üí‚Çó[K] K at v, exact v, } }

open category_theory.monoidal_category

/-- The coevaluation map is defined in `linear_algebra.coevaluation`. -/
def FinVect_coevaluation : ùüô_ (FinVect K) ‚ü∂ V ‚äó (FinVect_dual K V) :=
by apply coevaluation K V

lemma FinVect_coevaluation_apply_one : FinVect_coevaluation K V (1 : K) =
   ‚àë (i : basis.of_vector_space_index K V),
    (basis.of_vector_space K V) i ‚äó‚Çú[K] (basis.of_vector_space K V).coord i :=
by apply coevaluation_apply_one K V

/-- The evaluation morphism is given by the contraction map. -/
def FinVect_evaluation : (FinVect_dual K V) ‚äó V ‚ü∂ ùüô_ (FinVect K) :=
by apply contract_left K V

@[simp]
lemma FinVect_evaluation_apply (f : (FinVect_dual K V)) (x : V) :
  (FinVect_evaluation K V) (f ‚äó‚Çú x) = f x :=
by apply contract_left_apply f x

private theorem coevaluation_evaluation :
  let V' : FinVect K := FinVect_dual K V in
  (ùüô V' ‚äó (FinVect_coevaluation K V)) ‚â´ (Œ±_ V' V V').inv ‚â´ (FinVect_evaluation K V ‚äó ùüô V')
  = (œÅ_ V').hom ‚â´ (Œª_ V').inv :=
by apply contract_left_assoc_coevaluation K V

private theorem evaluation_coevaluation :
  (FinVect_coevaluation K V ‚äó ùüô V)
  ‚â´ (Œ±_ V (FinVect_dual K V) V).hom ‚â´ (ùüô V ‚äó FinVect_evaluation K V)
  = (Œª_ V).hom ‚â´ (œÅ_ V).inv :=
by apply contract_left_assoc_coevaluation' K V

instance exact_pairing : exact_pairing V (FinVect_dual K V) :=
{ coevaluation := FinVect_coevaluation K V,
  evaluation := FinVect_evaluation K V,
  coevaluation_evaluation' := coevaluation_evaluation K V,
  evaluation_coevaluation' := evaluation_coevaluation K V }

instance right_dual : has_right_dual V := ‚ü®FinVect_dual K V‚ü©

instance right_rigid_category : right_rigid_category (FinVect K) := { }

variables {K V} (W : FinVect K)

/-- Converts and isomorphism in the category `FinVect` to a `linear_equiv` between the underlying
vector spaces. -/
def iso_to_linear_equiv {V W : FinVect K} (i : V ‚âÖ W) : V ‚âÉ‚Çó[K] W :=
  ((forget‚ÇÇ (FinVect.{u} K) (Module.{u} K)).map_iso i).to_linear_equiv

lemma iso.conj_eq_conj {V W : FinVect K} (i : V ‚âÖ W) (f : End V) :
  iso.conj i f = linear_equiv.conj (iso_to_linear_equiv i) f := rfl

end FinVect

variables {K}

/-- Converts a `linear_equiv` to an isomorphism in the category `FinVect`. -/
@[simps] def linear_equiv.to_FinVect_iso
  {V W : Type u} [add_comm_group V] [module K V] [finite_dimensional K V]
  [add_comm_group W] [module K W] [finite_dimensional K W]
  (e : V ‚âÉ‚Çó[K] W) :
  FinVect.of K V ‚âÖ FinVect.of K W :=
{ hom := e.to_linear_map,
  inv := e.symm.to_linear_map,
  hom_inv_id' := by {ext, exact e.left_inv x},
  inv_hom_id' := by {ext, exact e.right_inv x} }
