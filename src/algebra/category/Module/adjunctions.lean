/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johan Commelin
-/
import algebra.category.Module.monoidal
import algebra.category.Group.basic
import algebra.category.Group.Z_Module_equivalence
import category_theory.monoidal.functorial
import category_theory.monoidal.types
import linear_algebra.direct_sum.finsupp
import category_theory.linear.linear_functor

/-!
The functor of forming finitely supported functions on a type with values in a `[ring R]`
is the left adjoint of
the forgetful functor from `R`-modules to types.
-/

noncomputable theory

open category_theory

namespace Module

universe u

open_locale classical

variables (R : Type u)

section
variables [ring R]

/--
The free functor `Type u ‚•§ Module R` sending a type `X` to the
free `R`-module with generators `x : X`, implemented as the type `X ‚Üí‚ÇÄ R`.
-/
@[simps]
def free : Type u ‚•§ Module R :=
{ obj := Œª X, Module.of R (X ‚Üí‚ÇÄ R),
  map := Œª X Y f, finsupp.lmap_domain _ _ f,
  map_id' := by { intros, exact finsupp.lmap_domain_id _ _ },
  map_comp' := by { intros, exact finsupp.lmap_domain_comp _ _ _ _, } }

/--
The free-forgetful adjunction for R-modules.
-/
def adj : free R ‚ä£ forget (Module.{u} R) :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X M, (finsupp.lift M R X).to_equiv.symm,
  hom_equiv_naturality_left_symm' := Œª _ _ M f g,
  finsupp.lhom_ext' (Œª x, linear_map.ext_ring
    (finsupp.sum_map_domain_index_add_monoid_hom (Œª y, ((smul_add_hom R M).flip) (g y))).symm) }

instance : is_right_adjoint (forget (Module.{u} R)) := ‚ü®_, adj R‚ü©

end

namespace free
variables [comm_ring R]
local attribute [ext] tensor_product.ext

/-- (Implementation detail) The unitor for `free R`. -/
def Œµ : ùüô_ (Module.{u} R) ‚ü∂ (free R).obj (ùüô_ (Type u)) :=
finsupp.lsingle punit.star

@[simp] lemma Œµ_apply (r : R) : Œµ R r = finsupp.single punit.star r := rfl

/-- (Implementation detail) The tensorator for `free R`. -/
def Œº (Œ± Œ≤ : Type u) : (free R).obj Œ± ‚äó (free R).obj Œ≤ ‚âÖ (free R).obj (Œ± ‚äó Œ≤) :=
(finsupp_tensor_finsupp' R Œ± Œ≤).to_Module_iso

lemma Œº_natural {X Y X' Y' : Type u} (f : X ‚ü∂ Y) (g : X' ‚ü∂ Y') :
  ((free R).map f ‚äó (free R).map g) ‚â´ (Œº R Y Y').hom =
    (Œº R X X').hom ‚â´ (free R).map (f ‚äó g) :=
begin
  intros,
  ext x x' ‚ü®y, y'‚ü©,
  dsimp [Œº],
  simp_rw [finsupp.map_domain_single, finsupp_tensor_finsupp'_single_tmul_single, mul_one,
    finsupp.map_domain_single, category_theory.tensor_apply],
end

lemma left_unitality (X : Type u) :
  (Œª_ ((free R).obj X)).hom =
  (Œµ R ‚äó ùüô ((free R).obj X)) ‚â´ (Œº R (ùüô_ (Type u)) X).hom ‚â´ map (free R).obj (Œª_ X).hom :=
begin
  intros,
  ext,
  dsimp [Œµ, Œº],
  simp_rw [finsupp_tensor_finsupp'_single_tmul_single,
    Module.monoidal_category.left_unitor_hom_apply, finsupp.smul_single', mul_one,
    finsupp.map_domain_single, category_theory.left_unitor_hom_apply],
end

lemma right_unitality (X : Type u) :
  (œÅ_ ((free R).obj X)).hom =
  (ùüô ((free R).obj X) ‚äó Œµ R) ‚â´ (Œº R X (ùüô_ (Type u))).hom ‚â´ map (free R).obj (œÅ_ X).hom :=
begin
  intros,
  ext,
  dsimp [Œµ, Œº],
  simp_rw [finsupp_tensor_finsupp'_single_tmul_single,
    Module.monoidal_category.right_unitor_hom_apply, finsupp.smul_single', mul_one,
    finsupp.map_domain_single, category_theory.right_unitor_hom_apply],
end

lemma associativity (X Y Z : Type u) :
  ((Œº R X Y).hom ‚äó ùüô ((free R).obj Z)) ‚â´ (Œº R (X ‚äó Y) Z).hom ‚â´ map (free R).obj (Œ±_ X Y Z).hom =
  (Œ±_ ((free R).obj X) ((free R).obj Y) ((free R).obj Z)).hom ‚â´
    (ùüô ((free R).obj X) ‚äó (Œº R Y Z).hom) ‚â´ (Œº R X (Y ‚äó Z)).hom :=
begin
  intros,
  ext,
  dsimp [Œº],
  simp_rw [finsupp_tensor_finsupp'_single_tmul_single, finsupp.map_domain_single, mul_one,
    category_theory.associator_hom_apply],
end

/-- The free R-module functor is lax monoidal. -/
-- In fact, it's strong monoidal, but we don't yet have a typeclass for that.
@[simps]
instance : lax_monoidal.{u} (free R).obj :=
{ -- Send `R` to `punit ‚Üí‚ÇÄ R`
  Œµ := Œµ R,
  -- Send `(Œ± ‚Üí‚ÇÄ R) ‚äó (Œ≤ ‚Üí‚ÇÄ R)` to `Œ± √ó Œ≤ ‚Üí‚ÇÄ R`
  Œº := Œª X Y, (Œº R X Y).hom,
  Œº_natural' := Œª X Y X' Y' f g, Œº_natural R f g,
  left_unitality' := left_unitality R,
  right_unitality' := right_unitality R,
  associativity' := associativity R, }

instance : is_iso (lax_monoidal.Œµ (free R).obj) :=
‚ü®‚ü®finsupp.lapply punit.star, ‚ü®by { ext, simp, }, by { ext ‚ü®‚ü© ‚ü®‚ü©, simp, }‚ü©‚ü©‚ü©

end free

variables [comm_ring R]

/-- The free functor `Type u ‚•§ Module R`, as a monoidal functor. -/
def monoidal_free : monoidal_functor (Type u) (Module.{u} R) :=
{ Œµ_is_iso := by { dsimp, apply_instance, },
  Œº_is_iso := Œª X Y, by { dsimp, apply_instance, },
  ..lax_monoidal_functor.of (free R).obj }

example (X Y : Type u) : (free R).obj (X √ó Y) ‚âÖ (free R).obj X ‚äó (free R).obj Y :=
((monoidal_free R).Œº_iso X Y).symm

end Module

namespace category_theory

universes v u

/--
`Free R C` is a type synonym for `C`, which, given `[comm_ring R]` and `[category C]`,
we will equip with a category structure where the morphisms are formal `R`-linear combinations
of the morphisms in `C`.
-/
@[nolint unused_arguments has_inhabited_instance]
def Free (R : Type*) (C : Type u) := C

/--
Consider an object of `C` as an object of the `R`-linear completion.

It may be preferable to use `(Free.embedding R C).obj X` instead;
this functor can also be used to lift morphisms.
-/
def Free.of (R : Type*) {C : Type u} (X : C) : Free R C := X

variables (R : Type*) [comm_ring R] (C : Type u) [category.{v} C]

open finsupp

-- Conceptually, it would be nice to construct this via "transport of enrichment",
-- using the fact that `Module.free R : Type ‚•§ Module R` and `Module.forget` are both lax monoidal.
-- This still seems difficult, so we just do it by hand.
instance category_Free : category (Free R C) :=
{ hom := Œª (X Y : C), (X ‚ü∂ Y) ‚Üí‚ÇÄ R,
  id := Œª (X : C), finsupp.single (ùüô X) 1,
  comp := Œª (X Y Z : C) f g, f.sum (Œª f' s, g.sum (Œª g' t, finsupp.single (f' ‚â´ g') (s * t))),
  assoc' := Œª W X Y Z f g h,
  begin
    dsimp,
    -- This imitates the proof of associativity for `monoid_algebra`.
    simp only [sum_sum_index, sum_single_index,
      single_zero, single_add, eq_self_iff_true, forall_true_iff, forall_3_true_iff,
      add_mul, mul_add, category.assoc, mul_assoc, zero_mul, mul_zero, sum_zero, sum_add],
  end }.

namespace Free

section
local attribute [reducible] category_theory.category_Free

instance : preadditive (Free R C) :=
{ hom_group := Œª X Y, finsupp.add_comm_group,
  add_comp' := Œª X Y Z f f' g, begin
    dsimp,
    rw [finsupp.sum_add_index];
    { simp [add_mul], }
  end,
  comp_add' := Œª X Y Z f g g', begin
    dsimp,
    rw ‚Üê finsupp.sum_add,
    congr, ext r h,
    rw [finsupp.sum_add_index];
    { simp [mul_add], },
  end, }

instance : linear R (Free R C) :=
{ hom_module := Œª X Y, finsupp.module (X ‚ü∂ Y) R,
  smul_comp' := Œª X Y Z r f g, begin
    dsimp,
    rw [finsupp.sum_smul_index];
    simp [finsupp.smul_sum, mul_assoc],
  end,
  comp_smul' := Œª X Y Z f r g, begin
    dsimp,
    simp_rw [finsupp.smul_sum],
    congr, ext h s,
    rw [finsupp.sum_smul_index];
    simp [finsupp.smul_sum, mul_left_comm],
  end, }

lemma single_comp_single {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (r s : R) :
  (single f r ‚â´ single g s : (Free.of R X) ‚ü∂ (Free.of R Z)) = single (f ‚â´ g) (r * s) :=
by { dsimp, simp, }

end

local attribute [simp] single_comp_single

/--
A category embeds into its `R`-linear completion.
-/
@[simps]
def embedding : C ‚•§ Free R C :=
{ obj := Œª X, X,
  map := Œª X Y f, finsupp.single f 1,
  map_id' := Œª X, rfl,
  map_comp' := Œª X Y Z f g, by simp, }

variables (R) {C} {D : Type u} [category.{v} D] [preadditive D] [linear R D]

open preadditive linear

/--
A functor to an `R`-linear category lifts to a functor from its `R`-linear completion.
-/
@[simps]
def lift (F : C ‚•§ D) : Free R C ‚•§ D :=
{ obj := Œª X, F.obj X,
  map := Œª X Y f, f.sum (Œª f' r, r ‚Ä¢ (F.map f')),
  map_id' := by { dsimp [category_theory.category_Free], simp },
  map_comp' := Œª X Y Z f g, begin
    apply finsupp.induction_linear f,
    { simp, },
    { intros f‚ÇÅ f‚ÇÇ w‚ÇÅ w‚ÇÇ,
      rw add_comp,
      rw [finsupp.sum_add_index, finsupp.sum_add_index],
      { simp [w‚ÇÅ, w‚ÇÇ, add_comp], },
      { simp, },
      { intros, simp only [add_smul], },
      { simp, },
      { intros, simp only [add_smul], }, },
    { intros f' r,
      apply finsupp.induction_linear g,
      { simp, },
      { intros f‚ÇÅ f‚ÇÇ w‚ÇÅ w‚ÇÇ,
        rw comp_add,
        rw [finsupp.sum_add_index, finsupp.sum_add_index],
        { simp [w‚ÇÅ, w‚ÇÇ, add_comp], },
        { simp, },
        { intros, simp only [add_smul], },
        { simp, },
        { intros, simp only [add_smul], }, },
      { intros g' s,
        erw single_comp_single,
        simp [mul_comm r s, mul_smul], } }
  end, }

@[simp]
lemma lift_map_single (F : C ‚•§ D) {X Y : C} (f : X ‚ü∂ Y) (r : R) :
  (lift R F).map (single f r) = r ‚Ä¢ F.map f :=
by simp

instance lift_additive (F : C ‚•§ D) : (lift R F).additive :=
{ map_add' := Œª X Y f g, begin
    dsimp,
    rw finsupp.sum_add_index; simp [add_smul]
  end, }

instance lift_linear (F : C ‚•§ D) : (lift R F).linear R :=
{ map_smul' := Œª X Y f r, begin
    dsimp,
    rw finsupp.sum_smul_index;
    simp [finsupp.smul_sum, mul_smul],
  end, }

/--
The embedding into the `R`-linear completion, followed by the lift,
is isomorphic to the original functor.
-/
def embedding_lift_iso (F : C ‚•§ D) : embedding R C ‚ãô lift R F ‚âÖ F :=
nat_iso.of_components
  (Œª X, iso.refl _)
  (by tidy)

/--
Two `R`-linear functors out of the `R`-linear completion are isomorphic iff their
compositions with the embedding functor are isomorphic.
-/
@[ext]
def ext {F G : Free R C ‚•§ D} [F.additive] [F.linear R] [G.additive] [G.linear R]
  (Œ± : embedding R C ‚ãô F ‚âÖ embedding R C ‚ãô G) : F ‚âÖ G :=
nat_iso.of_components
  (Œª X, Œ±.app X)
  begin
    intros X Y f,
    apply finsupp.induction_linear f,
    { simp, },
    { intros f‚ÇÅ f‚ÇÇ w‚ÇÅ w‚ÇÇ,
      simp only [F.map_add, G.map_add, add_comp, comp_add, w‚ÇÅ, w‚ÇÇ], },
    { intros f' r,
      rw [iso.app_hom, iso.app_hom, ‚Üêsmul_single_one, F.map_smul, G.map_smul, smul_comp, comp_smul],
      change r ‚Ä¢ (embedding R C ‚ãô F).map f' ‚â´ _ = r ‚Ä¢ _ ‚â´ (embedding R C ‚ãô G).map f',
      rw Œ±.hom.naturality f',
      apply_instance, -- Why are these not picked up automatically when we rewrite?
      apply_instance, }
  end

/--
`Free.lift` is unique amongst `R`-linear functors `Free R C ‚•§ D`
which compose with `embedding ‚Ñ§ C` to give the original functor.
-/
def lift_unique (F : C ‚•§ D) (L : Free R C ‚•§ D) [L.additive] [L.linear R]
  (Œ± : embedding R C ‚ãô L ‚âÖ F) :
  L ‚âÖ lift R F :=
ext R (Œ±.trans (embedding_lift_iso R F).symm)

end Free

end category_theory

namespace change_of_rings

universes u‚ÇÅ u‚ÇÇ

namespace restriction_of_scalars

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [ring S] (f : R ‚Üí+* S)
variable (M : Module S)

/--Any `S`-module M is also an `R`-module via a ring homomorphism `f : R ‚ü∂ S` by defining
`r ‚Ä¢ m := f r ‚Ä¢ m`. This is called restriction of scalars. -/
def obj' : Module R :=
{ carrier := M,
  is_add_comm_group := infer_instance,
  is_module := module.comp_hom M f }

section

include f

/--The `R`-scalar multiplication on `S`-module M defined by `r ‚Ä¢ m := f r ‚Ä¢ m`
-/
protected def has_smul : has_smul R M :=
begin
  haveI : module R M := (obj' f M).is_module,
  apply_instance
end

end

localized "notation r ` r‚Ä¢[` f `] ` m :=
  @@has_smul.smul (restriction_of_scalars.has_smul f _) r m"
  in change_of_rings

@[simp] lemma smul_def (r : R) (m : M) :
  (r r‚Ä¢[f] m) = f r ‚Ä¢ m := rfl

/--
Given an `S`-linear map `g : M ‚Üí M'` between `S`-modules, `g` is also `R`-linear between `M` and
`M'` by means of restriction of scalars.
-/
@[simps] def map' {M M' : Module S} (g : M ‚ü∂ M') :
  obj' f M ‚ü∂ obj' f M' :=
{ map_smul' := Œª r (x : M), by simp,
  ..g }

private lemma map_id' : map' f (ùüô M) = ùüô _ := linear_map.ext $ Œª (m : M), rfl

private lemma map_comp' {M M' M'' : Module S} (g : M ‚ü∂ M') (h : M' ‚ü∂ M'') :
  map' f (g ‚â´ h) = map' f g ‚â´ map' f h :=
linear_map.ext $ Œª (x : M), rfl

/--
The restriction of scalars operation is functorial. For any `f : R ‚Üí+* S` a ring homomorphism,
* `S`-module `M` can be considered as `R`-module by `r ‚Ä¢ m = f r ‚Ä¢ m`
* `S`-linear map is also `R`-linear
-/
@[simps] protected def functor : Module S ‚•§ Module R :=
{ obj := obj' f,
  map := Œª _ _, map' f,
  map_id' := map_id' f,
  map_comp' := Œª _ _ _ g h, map_comp' f _ _ }

end restriction_of_scalars

namespace coextension_of_scalars

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [ring S] (f : R ‚Üí+* S)
variable (M : Module R)

local notation `Hom` M := (restriction_of_scalars.functor f).obj ‚ü®S‚ü© ‚Üí‚Çó[R] M

/--
Given an `R`-module M, consider the Hom(S, M) -- the `R`-linear maps between S (as an `R`-module by
means of restriciton of scalars) and M. `S` acts on Hom(S, M) by `s ‚Ä¢ g = x ‚Ü¶ g (x ‚Ä¢ s)`
-/
protected def has_smul : has_smul S $ Hom M :=
{ smul := Œª s g,
  { to_fun := Œª (s' : S), g (s' ‚Ä¢ s : S),
    map_add' := Œª (x y : S), by simp [add_smul, map_add],
    map_smul' := Œª r (t : S), by rw [ring_hom.id_apply, restriction_of_scalars.smul_def f ‚ü®S‚ü©,
      ‚Üêlinear_map.map_smul, restriction_of_scalars.smul_def f ‚ü®S‚ü©, smul_assoc] } }

localized "notation s ` c‚Ä¢[` f `] ` m :=
  @has_smul.smul _ _ (coextension_of_scalars.has_smul f _) s m" in change_of_rings

@[simp] lemma smul_apply (s : S) (g : Hom M) (s' : S) :
  (s c‚Ä¢[f] g) s' = g (s' ‚Ä¢ s : S) := rfl

/--
`S` acts on Hom(S, M) by `s ‚Ä¢ g = x ‚Ü¶ g (x ‚Ä¢ s)` such that `1 ‚Ä¢ g = g` and `(s * t) ‚Ä¢ g = s ‚Ä¢ t ‚Ä¢ g`
-/
protected def mul_action : mul_action S $ Hom M :=
{ one_smul := Œª g, linear_map.ext $ Œª (s : S), by simp,
  mul_smul := Œª (s t : S) g, linear_map.ext $ Œª (x : S), by simp [mul_assoc],
  ..coextension_of_scalars.has_smul f _ }

/--
`S` acts on Hom(S, M) by `s ‚Ä¢ g = x ‚Ü¶ g (x ‚Ä¢ s)` such that
* 1 ‚Ä¢ g = g
* (s * t) ‚Ä¢ g = s ‚Ä¢ t ‚Ä¢ g
* s ‚Ä¢ (g + h) = s ‚Ä¢ g + s ‚Ä¢ h
* s ‚Ä¢ 0 = 0
-/
protected def distrib_mul_action : distrib_mul_action S $ Hom M :=
{ smul_add := Œª s g h, linear_map.ext $ Œª (t : S), by simp,
  smul_zero := Œª s, linear_map.ext $ Œª (t : S), by simp,
  ..coextension_of_scalars.mul_action f _ }

/--
`S` acts on Hom(S, M) by `s ‚Ä¢ g = x ‚Ü¶ g (x ‚Ä¢ s)`, this action defines an `S`-module structure on
Hom(S, M).
-/
protected def is_module : module S $ Hom M :=
{ add_smul := Œª s1 s2 g, linear_map.ext $ Œª (x : S), by simp,
  zero_smul := Œª g, linear_map.ext $ Œª (x : S), by simp,
  ..coextension_of_scalars.distrib_mul_action f _ }

/--
Given any `R`-module M, Hom(S, M) has an `R`-module structure via `f : R ‚Üí+* S`, this is called the
coextension of scalars.
-/
def obj' : Module S :=
{ carrier := Hom M,
  is_add_comm_group := infer_instance,
  is_module := coextension_of_scalars.is_module f _ }

/--
Given an `R`-linear map between `R`-modules `M` and `M'`, there is an `S`-linear map between
Hom(S, M) and Hom(S, M').
-/
@[simps] def map' {M M' : Module R} (g : M ‚ü∂ M') :
  obj' f M ‚ü∂ obj' f M' :=
{ to_fun := Œª h, g.comp h,
  map_add' := Œª _ _, linear_map.comp_add _ _ _,
  map_smul' := Œª s h, linear_map.ext $ Œª (t : S), by simp }

private lemma map_id' : map' f (ùüô M) = ùüô _ := linear_map.ext $ Œª h, linear_map.ext $ Œª x, rfl
private lemma map_comp' {M M' M'' : Module R} (g : M ‚ü∂ M') (h : M' ‚ü∂ M'') :
  map' f (g ‚â´ h) = map' f g ‚â´ map' f h :=
linear_map.ext $ Œª h, linear_map.ext $ Œª x, rfl

/--
The coextension of scalars is functorial by sending `M ‚Ü¶ Hom(S, M)` and `g ‚Ü¶ (h ‚Ü¶ g ‚àò h)`
-/
@[simps] protected def functor : Module R ‚•§ Module S :=
{ obj := obj' f,
  map := Œª _ _, map' _,
  map_id' := map_id' _,
  map_comp' := Œª _ _ _, map_comp' f }

end coextension_of_scalars

namespace extension_of_scalars

open_locale tensor_product
open tensor_product

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [comm_ring R] [comm_ring S] (f : R ‚Üí+* S) (M : Module R)
include f

include M
localized "notation M `‚äó[` R `,` f `]` S := @tensor_product R _ M S _ _ _
  (module.comp_hom S f)" in change_of_rings
localized "notation m `‚äó‚Çú[` R `,` f `]` s := @tensor_product.tmul R _ _ _ _ _ _
  (module.comp_hom _ f) m s" in change_of_rings

/--
Given `R`-module M, consider M ‚®Ç S, we define an `S`-action on M ‚®Ç S by
  `s ‚Ä¢ (m ‚äó s') := m ‚äó (s * s')`.
-/
def smul_by (s : S) : (M ‚äó[R, f] S) ‚ü∂ (M ‚äó[R, f] S) :=
let m : module R S := module.comp_hom S f in
begin
  resetI,
  refine tensor_product.lift ‚ü®_, _, _‚ü©,
  { -- we define `m ‚Ü¶ (s' ‚Ü¶ m ‚äó (s * s'))`
    refine Œª m, ‚ü®Œª s', m ‚äó‚Çú[R, f] (s * s'), _, _‚ü©,
    { -- map_add
      intros,
      erw [mul_add, tmul_add], },
    { -- map_smul
      intros,
      rw [ring_hom.id_apply, smul_tmul', smul_tmul],
      congr' 1,
      rw [restriction_of_scalars.smul_def f ‚ü®S‚ü©, smul_eq_mul, ‚Üê mul_assoc, mul_comm s,
        mul_assoc, restriction_of_scalars.smul_def f ‚ü®S‚ü©, smul_eq_mul],
    }, },
  { intros,
    ext,
    simp only [linear_map.coe_mk, map_add, add_tmul],
    refl, },
  { intros,
    ext,
    simp only [linear_map.coe_mk, ring_hom.id_apply, linear_map.smul_apply],
    rw [tensor_product.smul_tmul'], }
end

lemma smul_by.pure_tensor (s s' : S) (m : M) :
  (smul_by f M s (m ‚äó‚Çú[R, f] s')) =
  m ‚äó‚Çú[R, f] (s * s') :=
begin
  simp only [smul_by, tensor_product.lift.tmul, linear_map.coe_mk],
end

lemma smul_by.one : smul_by f M 1 = ùüô _ :=
begin
  ext,
  induction x using tensor_product.induction_on with _ _ _ _ ih1 ih2,
  { simpa only [smul_by, map_zero], },
  { simpa only [smul_by.pure_tensor, one_mul], },
  { simp only [category_theory.types_id_apply] at ih1 ih2 ‚ä¢,
    conv_rhs { rw [‚Üê ih1, ‚Üê ih2] },
    convert map_add _ _ _, },
end

lemma smul_by.mul (s s' : S) : smul_by f M (s * s') = smul_by f M s' ‚â´ smul_by f M s :=
begin
  ext,
  induction x using tensor_product.induction_on with _ _ x y ih1 ih2,
  { simp only [smul_by, map_zero, types_comp_apply], },
  { simp [smul_by, mul_assoc], },
  { convert congr_arg2 (+) ih1 ih2 using 1,
    { convert map_add _ _ _ },
    { simp only [types_comp_apply],
      calc  smul_by f M s (smul_by f M s' (x + y))
          = smul_by f M s (smul_by f M s' x + smul_by f M s' y)
          : by { congr' 1, convert map_add _ _ _}
      ... = smul_by f M s (smul_by f M s' x) + smul_by f M s (smul_by f M s' y)
          : by convert map_add _ _ _, }, }
end

lemma smul_by.apply_zero (s : S) : smul_by f M s 0 = 0 :=
by simp only [smul_by, map_zero]

lemma smul_by.apply_add (s : S) (a b) : smul_by f M s (a + b) = smul_by f M s a + smul_by f M s b :=
by simp [smul_by, map_add]


lemma smul_by.add (s s') : smul_by f M (s + s') = smul_by f M s + smul_by f M s' :=
begin
  ext x,
  induction x using tensor_product.induction_on with _ _ x y ih1 ih2,
  { simp [smul_by], },
  { simp [smul_by, add_mul, tmul_add], },
  { simp only [pi.add_apply, smul_by.apply_add, ih1, ih2],
    rw show ‚àÄ (a b c d : M ‚äó[R, f] S), a + b + (c + d) = a + c + (b + d), from _,
    intros,
    -- `ring` doesn't work here for some reason
    rw calc a + b + (c + d) = a + (b + (c + d)) : by rw add_assoc
      ... = a + (b + c + d) : by rw add_assoc
      ... = a + (c + b + d) : by rw add_comm b c
      ... = a + (c + (b + d)) : by rw add_assoc
      ... = a + c + (b + d) : by rw add_assoc, }
end

lemma smul_by.zero : smul_by f M 0 = 0 :=
begin
  ext,
  induction x using tensor_product.induction_on with _ _ x y ih1 ih2,
  { simp [smul_by], },
  { simp [smul_by], },
  { simp [smul_by.apply_add, ih1, ih2], }
end

/--
Since `S` has an `R`-module structure, `M ‚äó[R] S` can be given an `S`-module structure.
The scalar multiplication is defined by `s ‚Ä¢ (m ‚äó s') := m ‚äó (s * s')`
-/
@[reducible] def has_smul_S_M_tensor_S : has_smul S (M ‚äó[R, f] S) :=
{ smul := Œª s', smul_by f M s' }

local attribute [instance] has_smul_S_M_tensor_S

lemma smul_pure_tensor (s s' : S) (m : M) :
  (s ‚Ä¢ (m ‚äó‚Çú[R, f] s')) =
  m ‚äó‚Çú[R, f] (s * s') :=
by simp only [smul_by, tensor_product.lift.tmul, linear_map.coe_mk]

@[simp] lemma smul_zero (s : S) : s ‚Ä¢ (0 : M ‚äó[R, f] S) = 0 :=
by simp [smul_by]

/--
Since `S` has an `R`-module structure, `M ‚äó[R] S` can be given an `S`-module structure.
The scalar multiplication is defined by `s ‚Ä¢ (m ‚äó s') := m ‚äó (s * s')`
-/
def mul_action_S_M_tensor_S : mul_action S (M ‚äó[R, f] S) :=
{ one_smul := Œª x, begin
    change smul_by _ _ _ _ = _,
    rw smul_by.one f M,
    refl,
  end,
  mul_smul := Œª s s' x, begin
    change smul_by _ _ _ _ = smul_by _ _ _ (smul_by _ _ _ _),
    rw smul_by.mul f M,
    refl,
  end,
  ..(has_smul_S_M_tensor_S f M) }.

localized "attribute [instance] extension_of_scalars.mul_action_S_M_tensor_S" in change_of_rings

/--
Since `S` has an `R`-module structure, `M ‚äó[R] S` can be given an `S`-module structure.
The scalar multiplication is defined by `s ‚Ä¢ (m ‚äó s') := m ‚äó (s * s')`
-/
def distrib_mul_action_S_M_tensor_S : _root_.distrib_mul_action S (M ‚äó[R, f] S) :=
{ smul_zero := Œª s, by { change smul_by f M s 0 = 0, apply smul_by.apply_zero, },
  smul_add := Œª s x y, begin
    change smul_by f M s (x + y) = smul_by f M s x + smul_by f M s y,
    apply smul_by.apply_add,
  end }

/--
Since `S` has an `R`-module structure, `M ‚äó[R] S` can be given an `S`-module structure.
The scalar multiplication is defined by `s ‚Ä¢ (m ‚äó s') := m ‚äó (s * s')`
-/
def is_module : module S (M ‚äó[R, f] S) :=
{ add_smul := Œª s s' x, begin
    change smul_by _ _ _ _ = smul_by _ _ _ _ + smul_by _ _ _ _,
    rw smul_by.add,
    refl,
  end,
  zero_smul := Œª x, begin
    change smul_by _ _ _ _ = _,
    rw smul_by.zero,
    refl,
  end,
  ..(distrib_mul_action_S_M_tensor_S f M) }.

/--
M ‚®Ç S is also an `R`-module
-/
def is_module' : module R (M ‚äó[R, f] S) := infer_instance

localized "attribute [instance] extension_of_scalars.is_module extension_of_scalars.is_module'"
  in change_of_rings

def obj' : Module S :=
{ carrier := M ‚äó[R, f] S,
  is_module := is_module f M }

omit M
/--
Extension of scalars is a functor where an `R`-module `M` is sent to `M ‚äó S` and
`l : M1 ‚ü∂ M2` is sent to `m ‚äó s ‚Ü¶ l m ‚äó s`
-/
@[simps] def map' {M1 M2 : Module R} (l : M1 ‚ü∂ M2) : (obj' f M1) ‚ü∂ (obj' f M2) :=
let im1 : module R S := module.comp_hom S f,
    im2 : module R (obj' f M2) := is_module' f M2 in
begin
  resetI,
  refine
    { to_fun := tensor_product.lift { to_fun := Œª m1, _, map_add' := _, map_smul' := _ },
      map_add' := _,
      map_smul' := _ },
  { -- `S ‚ü∂ f _* M2` given by `s ‚Ü¶ l m ‚äó s`
    refine { to_fun := Œª s, (l m1) ‚äó‚Çú[R, f] s, map_add' := _, map_smul' := _ },
    { -- map_add
      intros,
      rw [tmul_add], },
    { -- map_smul
      intros,
      rw [ring_hom.id_apply, restriction_of_scalars.smul_def f ‚ü®S‚ü© r x, smul_tmul',
        smul_tmul],
      refl, } },
  { intros m m',
    ext s,
    simp only [add_tmul, map_add, linear_map.coe_mk, linear_map.add_apply], },
  { intros r m,
    ext s,
    rw [ring_hom.id_apply, linear_map.map_smul, linear_map.coe_mk, linear_map.smul_apply,
      linear_map.coe_mk, smul_tmul'], },
  { intros z1 z2,
    rw [map_add], },
  { intros s z,
    induction z using tensor_product.induction_on with _ _ z1 z2 ih1 ih2,
    { simp only [smul_zero, map_zero], },
    { simp only [smul_pure_tensor, ring_hom.id_apply, linear_map.coe_mk, lift.tmul], },
    { rw [smul_add, map_add, ring_hom.id_apply, ih1, ih2, map_add, smul_add,
        ring_hom.id_apply], } }
end

/--
Extension of scalars is a functor where an `R`-module `M` is sent to `M ‚äó S` and
`l : M1 ‚ü∂ M2` is sent to `m ‚äó s ‚Ü¶ l m ‚äó s`
-/
def functor : Module R ‚•§ Module S :=
{ obj := Œª M, obj' f M,
  map := Œª M1 M2 l, map' f l,
  map_id' := Œª M, begin
    ext x,
    rw [map', Module.id_apply],
    induction x using tensor_product.induction_on with _ _ m s ihx ihy,
    { rw map_zero },
    { rw [linear_map.coe_mk, tensor_product.lift.tmul], refl, },
    { rw [linear_map.coe_mk] at ihx ihy ‚ä¢,
      rw [map_add, ihx, ihy], }
  end,
  map_comp' := Œª M1 M2 M3 g h, begin
    ext x,
    rw [map', map', map', linear_map.coe_mk, category_theory.comp_apply,
      linear_map.coe_mk, linear_map.coe_mk],
    induction x using tensor_product.induction_on with _ _ m s ihx ihy,
    { rw [map_zero, map_zero, map_zero], },
    { rw [tensor_product.lift.tmul, tensor_product.lift.tmul], refl, },
    { rw [map_add, ihx, ihy, map_add, map_add], }
  end }

end extension_of_scalars

namespace extension_restriction_adj

open_locale change_of_rings
open tensor_product

universes v

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [comm_ring R] [comm_ring S] (f : R ‚Üí+* S)

/--
Given `R`-module X and `S`-module Y and a map `(extension_of_scalars.functor f).obj X ‚ü∂ Y`,
there is a map `X ‚ü∂ (restriction_of_scalars.functor f).obj Y`
-/
@[simps] def hom_equiv.to_restriction {X Y} (g : (extension_of_scalars.functor f).obj X ‚ü∂ Y) :
  X ‚ü∂ (restriction_of_scalars.functor f).obj Y :=
let m1 : module R S := module.comp_hom S f,
    m2 : module R Y := module.comp_hom Y f in
{ to_fun := Œª x, g (x ‚äó‚Çú[R, f] 1),
  map_add' := Œª x x', by rw [tensor_product.add_tmul, map_add],
  map_smul' := Œª r x, begin
    resetI,
    rw [ring_hom.id_apply, smul_tmul, restriction_of_scalars.smul_def f ‚ü®S‚ü©],
    change _ = f r ‚Ä¢ _,
    rw [‚Üêlinear_map.map_smul],
    congr,
  end }

/--
Given `R`-module X and `S`-module Y and a map `X ‚ü∂ (restriction_of_scalars.functor f).obj Y`,
there is a map `(extension_of_scalars.functor f).obj X ‚ü∂ Y`
-/
@[simps] def hom_equiv.to_extension {X Y} (g : X ‚ü∂ (restriction_of_scalars.functor f).obj Y) :
  (extension_of_scalars.functor f).obj X ‚ü∂ Y :=
{ to_fun := Œª z,
  let m1 := module.comp_hom S f,
      m2 : module R Y := module.comp_hom Y f,
      m3 : module S ((restriction_of_scalars.functor f).obj Y) := Y.is_module in
  begin
    resetI,
    refine tensor_product.lift
      { to_fun := Œª x,
          { to_fun := Œª s, s ‚Ä¢ (g x : Y),
            map_add' := _,
            map_smul' := _, },
        map_add' := _,
        map_smul' := _ } z,
    { intros, rw add_smul, },
    { intros r s,
      rw [ring_hom.id_apply],
      calc  (r ‚Ä¢ s) ‚Ä¢ g x
          = (f r * s) ‚Ä¢ g x : rfl
      ... = f r ‚Ä¢ s ‚Ä¢ g x : by rw [mul_smul], },
    { intros x y,
      ext s,
      simp only [linear_map.coe_mk, smul_add, linear_map.add_apply, map_add], },
    { intros r x,
      ext s,
      simp only [linear_map.coe_mk, ring_hom.id_apply, linear_map.smul_apply,
        linear_map.map_smul],
      erw [‚Üê mul_smul, mul_comm, mul_smul],
      refl, },
  end,
  map_add' := Œª z1 z2, by simp only [map_add],
  map_smul' := Œª r z, begin
    rw [ring_hom.id_apply],
    induction z using tensor_product.induction_on with x y x y ih1 ih2,
    { simp only [smul_zero, map_zero], },
    { erw [extension_of_scalars.smul_pure_tensor],
      simp [tensor_product.lift.tmul, mul_smul], },
    { simp only [smul_add, map_add],
      dsimp only at ih1 ih2,
      rw [ih1, ih2], },
  end }

/--
Given `R`-module X and `S`-module Y, the linear maps `(extension_of_scalars.functor f).obj X ‚ü∂ Y`
bijectively corresponding to `X ‚ü∂ (restriction_of_scalars.functor f).obj Y`
-/
@[simps] def hom_equiv' {X Y} :
  ((extension_of_scalars.functor f).obj X ‚ü∂ Y) ‚âÉ (X ‚ü∂ (restriction_of_scalars.functor f).obj Y) :=
{ to_fun := hom_equiv.to_restriction f,
  inv_fun := hom_equiv.to_extension f,
  left_inv := Œª g, begin
    ext z,
    induction z using tensor_product.induction_on with x s z1 z2 ih1 ih2,
    { simp only [map_zero], },
    { erw tensor_product.lift.tmul,
      simp only [linear_map.coe_mk],
      erw [‚Üê linear_map.map_smul, extension_of_scalars.smul_pure_tensor, mul_one], },
    { rw [map_add, map_add, ih1, ih2], }
  end,
  right_inv := Œª g, by { ext, simp } }

/--
For any `R`-module X, there is a natural `R`-linear map from `X` to `X ‚®Ç S` by sending `x ‚Ü¶ x ‚äó 1`
-/
@[simps] def unit.map {X} :
  X ‚ü∂ (extension_of_scalars.functor f ‚ãô restriction_of_scalars.functor f).obj X :=
let m1 : module R S := module.comp_hom S f in
{ to_fun := Œª x, x ‚äó‚Çú[R, f] 1,
  map_add' := Œª x x', by { rw tensor_product.add_tmul, },
  map_smul' := Œª r x, begin
    resetI,
    erw [smul_tmul, extension_of_scalars.smul_pure_tensor],
    congr,
  end }

/--
The natural transformation from ideantity functor on `R`-module to the composition of extension and
restriction of scalars.
-/
def unit : ùü≠ (Module R) ‚ü∂ extension_of_scalars.functor f ‚ãô restriction_of_scalars.functor f :=
{ app := Œª _, unit.map f,
  naturality' := Œª X X' g, begin
    ext,
    simp only [unit.map, functor.id_map, Module.coe_comp, linear_map.coe_mk,
      function.comp_app, functor.comp_map],
    rw show (restriction_of_scalars.functor f).map ((extension_of_scalars.functor f).map g) =
      { to_fun := (extension_of_scalars.functor f).map g, map_add' := _, map_smul' := _ }, from rfl,
    simp only [linear_map.coe_mk],
    erw tensor_product.lift.tmul,
    simp only [linear_map.coe_mk],
  end }

/--
For any `S`-module Y, there is a natural `R`-linear map from `Y ‚®Ç S` to `Y` by
`y ‚äó s ‚Ü¶ s ‚Ä¢ y`-/
@[simps] def counit.map {Y} :
  (restriction_of_scalars.functor f ‚ãô extension_of_scalars.functor f).obj Y ‚ü∂ Y :=
let m1 : module R S := module.comp_hom S f,
    m2 : module R Y := module.comp_hom Y f in
{ to_fun :=
    begin
      resetI,
      refine tensor_product.lift
        { to_fun := Œª y,
            { to_fun := Œª s, _,
              map_add' := _,
              map_smul' := _ },
          map_add' := _,
          map_smul' := _ },
      { haveI t : has_smul S ((restriction_of_scalars.functor f).obj Y),
        { haveI : module S ((restriction_of_scalars.functor f).obj Y) :=
          (infer_instance : module S Y),
          apply_instance, },
        exact @has_smul.smul _ _ t s y, },
      { intros s s', rw add_smul, },
      { intros r s,
        rw [ring_hom.id_apply, restriction_of_scalars.smul_def f ‚ü®S‚ü©,
          restriction_of_scalars.smul_def f, smul_eq_mul, mul_smul], },
      { intros y1 y2,
        ext,
        simp only [linear_map.coe_mk, smul_add, linear_map.add_apply], },
      { intros r y,
        ext s,
        simp only [ring_hom.id_apply, restriction_of_scalars.smul_def,
          linear_map.coe_mk, linear_map.smul_apply],
        erw [‚Üê mul_smul, mul_comm, mul_smul],
        refl, },
    end,
  map_add' := Œª z1 z2, by simp only [map_add],
  map_smul' := Œª s z, begin
    simp only [ring_hom.id_apply],
    induction z using tensor_product.induction_on with x s' z1 z2 ih1 ih2,
    { simp only [smul_zero, map_zero], },
    { erw extension_of_scalars.smul_pure_tensor,
      simp only [linear_map.coe_mk, tensor_product.lift.tmul],
      rw mul_smul, },
    { rw [smul_add, map_add, map_add, ih1, ih2, smul_add], },
  end }

/--
The natural transformation from the composition of restriction and extension of scalars to the
identity functor on `S`-module.
-/
@[simps] def counit :
  (restriction_of_scalars.functor f ‚ãô extension_of_scalars.functor f) ‚ü∂ (ùü≠ (Module S)) :=
{ app := Œª _, counit.map f,
  naturality' := Œª Y Y' g, begin
    ext z,
    simp only [functor.comp_map, Module.coe_comp, function.comp_app, functor.id_map],
    induction z using tensor_product.induction_on with y s z1 z2 ih1 ih2,
    { simp only [map_zero], },
    { unfold counit.map,
      erw [tensor_product.lift.tmul, tensor_product.lift.tmul],
      simp only [linear_map.coe_mk, linear_map.map_smul‚Çõ‚Çó, ring_hom.id_apply],
      refl },
    { rw [map_add, map_add, ih1, ih2, map_add, map_add], }
  end }

/--
extension of scalars ‚ä£ restriction of scalars
-/
def adjunction : adjunction (extension_of_scalars.functor f) (restriction_of_scalars.functor f) :=
{ hom_equiv := Œª _ _, hom_equiv' f,
  unit := unit f,
  counit := counit f,
  hom_equiv_unit' := Œª X Y g, by { ext, simpa },
  hom_equiv_counit' := Œª X Y g,
  begin
    ext z,
    simp only [hom_equiv'_symm_apply, hom_equiv.to_extension_apply, counit_app, Module.coe_comp,
      function.comp_app, counit.map_apply],
    induction z using tensor_product.induction_on with x s z1 z2 ih1 ih2,
    { simp only [map_zero], },
    { erw tensor_product.lift.tmul, },
    { simp only [map_add, ih1, ih2], }
  end }

end extension_restriction_adj

namespace restriction_coextension_adj

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [ring S] (f : R ‚Üí+* S)

/--
Given `R`-module X and `S`-module Y, any `g : (restriction_of_scalars.functor f).obj Y ‚ü∂ X`
corresponds to `Y ‚ü∂ (coextension_of_scalars.functor f).obj X` by sendind `y ‚Ü¶ (s ‚Ü¶ g (s ‚Ä¢ y))`
-/
@[simps] def hom_equiv.from_restriction_to_to_coextension
  {X Y} (g : (restriction_of_scalars.functor f).obj Y ‚ü∂ X) :
  Y ‚ü∂ (coextension_of_scalars.functor f).obj X :=
{ to_fun := Œª (y : Y),
  { to_fun := Œª (s : S), g $ (s ‚Ä¢ y : Y),
    map_add' := Œª (s1 s2 : S), by simp [add_smul],
    map_smul' := Œª r (s : S),
    begin
      rw [restriction_of_scalars.smul_def f ‚ü®S‚ü©, ring_hom.id_apply,
        ‚Üêg.map_smul, smul_eq_mul, mul_smul],
      congr,
    end },
  map_add' := Œª (y1 y2 : Y), linear_map.ext $ Œª (s : S),
  begin
    rw [linear_map.add_apply, linear_map.coe_mk, linear_map.coe_mk, linear_map.coe_mk,
      smul_add, map_add],
  end,
  map_smul' := Œª s y, linear_map.ext $ Œª (t : S), by simp [mul_smul] }

/--
Given `R`-module X and `S`-module Y, any `g : Y ‚ü∂ (coextension_of_scalars.functor f).obj X`
corresponds to `(restriction_of_scalars.functor f).obj Y ‚ü∂ X` by `y ‚Ü¶ g y 1`
-/
@[simps] def hom_equiv.to_coextension_to_from_restriction
  {X Y} (g : Y ‚ü∂ (coextension_of_scalars.functor f).obj X) :
  (restriction_of_scalars.functor f).obj Y ‚ü∂ X :=
{ to_fun := Œª (y : Y), (g y).to_fun (1 : S),
  map_add' := Œª x y, by simp only [g.map_add, linear_map.to_fun_eq_coe, linear_map.add_apply],
  map_smul' := Œª r (y : Y),
  by rw [linear_map.to_fun_eq_coe, linear_map.to_fun_eq_coe, ring_hom.id_apply,
    ‚Üêlinear_map.map_smul, restriction_of_scalars.smul_def f ‚ü®S‚ü©, smul_eq_mul, mul_one,
    restriction_of_scalars.smul_def f Y, linear_map.map_smul, coextension_of_scalars.smul_apply,
    smul_eq_mul, one_mul], }

lemma hom_equiv.fb {X Y} (g : (restriction_of_scalars.functor f).obj Y ‚ü∂ X) :
  hom_equiv.to_coextension_to_from_restriction f
    (hom_equiv.from_restriction_to_to_coextension f g) = g :=
linear_map.ext $ Œª (y : Y), by simp

lemma hom_equiv.bf {X Y} (g : Y ‚ü∂ (coextension_of_scalars.functor f).obj X) :
  hom_equiv.from_restriction_to_to_coextension f
    (hom_equiv.to_coextension_to_from_restriction f g) = g :=
linear_map.ext $ Œª (y : Y), by { ext, simp }

/--
The natural transformation from identity functor to the composition of restriction and coextension
of scalars.
-/
@[simps] protected def unit' :
  ùü≠ (Module S) ‚ü∂ restriction_of_scalars.functor f ‚ãô coextension_of_scalars.functor f :=
{ app := Œª Y,
  { to_fun := Œª (y : Y),
    { to_fun := Œª (s : S), (s ‚Ä¢ y : Y),
      map_add' := Œª s s', add_smul _ _ _,
      map_smul' := Œª r (s : S), by rw [ring_hom.id_apply, restriction_of_scalars.smul_def f ‚ü®S‚ü©,
        smul_eq_mul, mul_smul, restriction_of_scalars.smul_def] },
    map_add' := Œª y1 y2, linear_map.ext $ Œª (s : S), by rw [linear_map.add_apply, linear_map.coe_mk,
      linear_map.coe_mk, linear_map.coe_mk, smul_add],
    map_smul' := Œª s (y : Y), linear_map.ext $ Œª (t : S), by simp [mul_smul] },
  naturality' := Œª Y Y' g, linear_map.ext $ Œª (y : Y), linear_map.ext $ Œª (s : S), by simp }

/--
The natural transforamtion from the composition of coextension and restriction of scalars to
identity functor.
-/
@[simps] protected def counit' :
  coextension_of_scalars.functor f ‚ãô restriction_of_scalars.functor f ‚ü∂ ùü≠ (Module R) :=
{ app := Œª X,
  { to_fun := Œª g, g.to_fun (1 : S),
    map_add' := Œª x1 x2, by simp [linear_map.to_fun_eq_coe],
    map_smul' := Œª r (g : (restriction_of_scalars.functor f).obj
      ((coextension_of_scalars.functor f).obj X)),
    begin
      simp only [linear_map.to_fun_eq_coe, ring_hom.id_apply],
      rw [restriction_of_scalars.smul_def f, coextension_of_scalars.smul_apply, smul_eq_mul,
        one_mul, ‚Üêlinear_map.map_smul, restriction_of_scalars.smul_def f ‚ü®S‚ü©, smul_eq_mul, mul_one],
    end },
  naturality' := Œª X X' g, linear_map.ext $ Œª h, by simp }

/--
restriction of scalars ‚ä£ coextension of scalars
-/
@[simps] def _root_.change_of_rings.restriction_coextension_adj :
  restriction_of_scalars.functor f ‚ä£ coextension_of_scalars.functor f :=
{ hom_equiv := Œª Y X, ‚ü®hom_equiv.from_restriction_to_to_coextension f,
    hom_equiv.to_coextension_to_from_restriction f, hom_equiv.fb f, hom_equiv.bf f‚ü©,
  unit := restriction_coextension_adj.unit' f,
  counit := restriction_coextension_adj.counit' f,
  hom_equiv_unit' := Œª Y X g, linear_map.ext $ Œª y, rfl,
  hom_equiv_counit' := Œª Y X g, linear_map.ext $ Œª (y : Y), by simp }

end restriction_coextension_adj

namespace coextension_forget‚ÇÇ_adj

universes u

variables {S : Type u} [ring S] (f : ‚Ñ§ ‚Üí+* S)

open_locale change_of_rings

/-- A more constructive version of `Ab ‚•§ Module ‚Ñ§`-/
@[simps] def Ab_to_Z_Module : AddCommGroup ‚•§ Module ‚Ñ§ :=
{ obj := Œª X, ‚ü®X‚ü©,
  map := Œª X Y g,
  { map_smul' := Œª z (x : X), by rw [add_monoid_hom.to_fun_eq_coe, g.map_zsmul, ring_hom.id_apply],
    ..g },
  map_id' := Œª X, fun_like.ext _ _ $ Œª (x : X), rfl,
  map_comp' := Œª X Y Z g h, fun_like.ext _ _ $ Œª (x : X), rfl }

/--
Given a group homomorphism `g : X ‚ü∂ Y`, define linear map `X ‚ü∂ Hom(S, Y)` by `x ‚Ü¶ (s ‚Ü¶ g (s ‚Ä¢ x))`
-/
@[simps] def hom_equiv.forget‚ÇÇ_to_coextension {X : Module S} {Y : AddCommGroup}
  (g : (forget‚ÇÇ (Module S) AddCommGroup).obj X ‚ü∂ Y) :
  X ‚ü∂ (Ab_to_Z_Module ‚ãô coextension_of_scalars.functor f).obj Y :=
{ to_fun := Œª x,
  { to_fun := Œª (s : S), g (s ‚Ä¢ x : X),
    map_add' := Œª (s‚ÇÅ s‚ÇÇ : S), by simp [add_smul],
    map_smul' := Œª z (s : S),
    begin
      rw [restriction_of_scalars.smul_def f ‚ü®S‚ü©, ring_hom.id_apply, smul_eq_mul, mul_smul,
        ‚Üêg.map_zsmul],
      induction z using int.induction_on with i ih i ih,
      { rw [map_zero, zero_smul, map_zero, zero_smul, map_zero], },
      { rw [map_add, add_smul, map_add, ih, map_one, one_smul, add_smul, one_smul, map_add], },
      { rw [map_sub, sub_smul, map_one, one_smul, map_sub, ih, sub_smul, one_smul, map_sub], },
    end },
  map_add' := Œª X‚ÇÅ X‚ÇÇ, by { ext, simp },
  map_smul' := Œª s x, by { ext, simp [mul_smul], } }

/--
Given linear map `g : X ‚ü∂ Hom(S, Y)`, define `X ‚ü∂ Y` by `x ‚Ü¶ g x 1`
-/
@[simps] def hom_equiv.coextension_to_forget‚ÇÇ {X : Module S} {Y : AddCommGroup}
  (g : X ‚ü∂ (Ab_to_Z_Module ‚ãô coextension_of_scalars.functor f).obj Y) :
  (forget‚ÇÇ (Module S) AddCommGroup).obj X ‚ü∂ Y :=
{ to_fun := Œª (x : X), (g x).to_fun (1 : S),
  map_zero' := by simp,
  map_add' := Œª (x‚ÇÅ x‚ÇÇ : X), by simp }

lemma hom_equiv.fb {X : Module S} {Y : AddCommGroup}
  (g : (forget‚ÇÇ (Module S) AddCommGroup).obj X ‚ü∂ Y) :
  hom_equiv.coextension_to_forget‚ÇÇ f (hom_equiv.forget‚ÇÇ_to_coextension f g) = g :=
by { ext, simp }

lemma hom_equiv.bf {X : Module S} {Y : AddCommGroup}
  (g : X ‚ü∂ (Ab_to_Z_Module ‚ãô coextension_of_scalars.functor f).obj Y) :
  hom_equiv.forget‚ÇÇ_to_coextension f (hom_equiv.coextension_to_forget‚ÇÇ f g) = g :=
by { ext, simp }

/--
The natural transformation from identity functor to the composition of identity to the composition
of forgetful and coextension of scalars functor.
-/
@[simps] def unit' :
  ùü≠ (Module S) ‚ü∂
  forget‚ÇÇ (Module S) AddCommGroup ‚ãô Ab_to_Z_Module ‚ãô coextension_of_scalars.functor f :=
{ app := Œª X,
  { to_fun := Œª (x : X),
    { to_fun := Œª (s : S), (s ‚Ä¢ x : X),
      map_add' := Œª x‚ÇÅ x‚ÇÇ, add_smul _ _ _,
      map_smul' := Œª z (s : S),
      begin
        rw [ring_hom.id_apply],

        induction z using int.induction_on with i ih i ih,
        { rw [zero_smul, restriction_of_scalars.smul_def f ‚ü®S‚ü©, map_zero, zero_smul, zero_smul], },
        { rw [add_smul, add_smul, one_smul, ih, add_smul, one_smul], },
        { rw [sub_smul, sub_smul, one_smul, ih, sub_smul, one_smul], },
      end },
    map_add' := Œª (x‚ÇÅ x‚ÇÇ : X), by { ext, simp },
    map_smul' := Œª s (x : X), by { ext, simp [mul_smul], } },
  naturality' := Œª X Y g, by { ext, simp, } }

/--
The natural transformation from the coextension of scalars functor to the identity functor.
-/
@[simps] def counit' :
  (Ab_to_Z_Module ‚ãô coextension_of_scalars.functor f) ‚ãô forget‚ÇÇ (Module S) AddCommGroup ‚ü∂
  ùü≠ AddCommGroup :=
{ app := Œª X,
  { to_fun := Œª g, g.to_fun (1 : S),
    map_zero' := by simp,
    map_add' := Œª _ _, by simp },
  naturality' := Œª X Y g, by { ext, simp, } }

/--
forgetful functor ‚ä£ coextension of scalars
-/
def _root_.change_of_rings.coextension_forget‚ÇÇ_adj :
  forget‚ÇÇ (Module S) AddCommGroup ‚ä£
  (Ab_to_Z_Module.comp $ coextension_of_scalars.functor f) :=
{ hom_equiv := Œª X Y, ‚ü®hom_equiv.forget‚ÇÇ_to_coextension f, hom_equiv.coextension_to_forget‚ÇÇ f,
    hom_equiv.fb f, hom_equiv.bf f‚ü©,
  unit := unit' f,
  counit := counit' f,
  hom_equiv_unit' := Œª X Y g, by { ext, simp },
  hom_equiv_counit' := Œª X Y g, by { ext, simp } }

instance : is_left_adjoint (forget‚ÇÇ (Module S) AddCommGroup) :=
‚ü®_, change_of_rings.coextension_forget‚ÇÇ_adj $ algebra_map ‚Ñ§ S‚ü©

end coextension_forget‚ÇÇ_adj

end change_of_rings
#lint
