/-
Copyright (c) 2019 Robert A. Spencer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Robert A. Spencer, Markus Himmel
-/
import algebra.category.Group.basic
import category_theory.concrete_category
import category_theory.limits.shapes.kernels
import category_theory.preadditive
import linear_algebra.basic

open category_theory
open category_theory.limits
open category_theory.limits.walking_parallel_pair

universe u

variables (R : Type u) [ring R]

/-- The category of R-modules and their morphisms. -/
structure Module :=
(carrier : Type u)
[is_add_comm_group : add_comm_group carrier]
[is_module : module R carrier]

attribute [instance] Module.is_add_comm_group Module.is_module

namespace Module

-- TODO revisit this after #1438 merges, to check coercions and instances are handled consistently
instance : has_coe_to_sort (Module R) :=
{ S := Type u, coe := Module.carrier }

instance : category (Module.{u} R) :=
{ hom   := Œª M N, M ‚Üí‚Çó[R] N,
  id    := Œª M, 1,
  comp  := Œª A B C f g, g.comp f }

instance : concrete_category (Module.{u} R) :=
{ forget := { obj := Œª R, R, map := Œª R S f, (f : R ‚Üí S) },
  forget_faithful := { } }

instance has_forget_to_AddCommGroup : has_forget‚ÇÇ (Module R) AddCommGroup :=
{ forget‚ÇÇ :=
  { obj := Œª M, AddCommGroup.of M,
    map := Œª M‚ÇÅ M‚ÇÇ f, linear_map.to_add_monoid_hom f } }

/-- The object in the category of R-modules associated to an R-module -/
def of (X : Type u) [add_comm_group X] [module R X] : Module R := ‚ü®X‚ü©

instance : inhabited (Module R) := ‚ü®of R punit‚ü©

@[simp]
lemma of_apply (X : Type u) [add_comm_group X] [module R X] : (of R X : Type u) = X := rfl

variables {R}

/-- Forgetting to the underlying type and then building the bundled object returns the original module. -/
@[simps]
def of_self_iso (M : Module R) : Module.of R M ‚âÖ M :=
{ hom := ùüô M, inv := ùüô M }

instance : subsingleton (of R punit) :=
by { rw of_apply R punit, apply_instance }

instance : has_zero_object.{u} (Module R) :=
{ zero := of R punit,
  unique_to := Œª X,
  { default := (0 : punit ‚Üí‚Çó[R] X),
    uniq := Œª _, linear_map.ext $ Œª x,
      have h : x = 0, from subsingleton.elim _ _,
      by simp only [h, linear_map.map_zero]},
  unique_from := Œª X,
  { default := (0 : X ‚Üí‚Çó[R] punit),
    uniq := Œª _, linear_map.ext $ Œª x, subsingleton.elim _ _ } }

variables {R} {M N U : Module R}

@[simp] lemma id_apply (m : M) : (ùüô M : M ‚Üí M) m = m := rfl

@[simp] lemma coe_comp (f : M ‚ü∂ N) (g : N ‚ü∂ U) :
  ((f ‚â´ g) : M ‚Üí U) = g ‚àò f := rfl

end Module

variables {R}
variables {X‚ÇÅ X‚ÇÇ : Type u}

/-- Build an isomorphism in the category `Module R` from a `linear_equiv` between `module`s. -/
@[simps]
def linear_equiv.to_Module_iso
  {g‚ÇÅ : add_comm_group X‚ÇÅ} {g‚ÇÇ : add_comm_group X‚ÇÇ} {m‚ÇÅ : module R X‚ÇÅ} {m‚ÇÇ : module R X‚ÇÇ} (e : X‚ÇÅ ‚âÉ‚Çó[R] X‚ÇÇ) :
  Module.of R X‚ÇÅ ‚âÖ Module.of R X‚ÇÇ :=
{ hom := (e : X‚ÇÅ ‚Üí‚Çó[R] X‚ÇÇ),
  inv := (e.symm : X‚ÇÇ ‚Üí‚Çó[R] X‚ÇÅ),
  hom_inv_id' := begin ext, exact e.left_inv x, end,
  inv_hom_id' := begin ext, exact e.right_inv x, end, }

namespace category_theory.iso

/-- Build a `linear_equiv` from an isomorphism in the category `Module R`. -/
@[simps]
def to_linear_equiv {X Y : Module.{u} R} (i : X ‚âÖ Y) : X ‚âÉ‚Çó[R] Y :=
{ to_fun    := i.hom,
  inv_fun   := i.inv,
  left_inv  := by tidy,
  right_inv := by tidy,
  map_add'  := by tidy,
  map_smul' := by tidy, }.

end category_theory.iso

/-- linear equivalences between `module`s are the same as (isomorphic to) isomorphisms in `Module` -/
@[simps]
def linear_equiv_iso_Module_iso {X Y : Type u} [add_comm_group X] [add_comm_group Y] [module R X] [module R Y] :
  (X ‚âÉ‚Çó[R] Y) ‚âÖ (Module.of R X ‚âÖ Module.of R Y) :=
{ hom := Œª e, e.to_Module_iso,
  inv := Œª i, i.to_linear_equiv, }

namespace Module

section preadditive

instance : preadditive.{u} (Module.{u} R) :=
{ add_comp' := Œª P Q R f f' g,
    show (f + f') ‚â´ g = f ‚â´ g + f' ‚â´ g, by { ext, simp },
  comp_add' := Œª P Q R f g g',
    show f ‚â´ (g + g') = f ‚â´ g + f ‚â´ g', by { ext, simp } }

end preadditive

section kernel
variables {R} {M N : Module R} (f : M ‚ü∂ N)

/-- The cone on the equalizer diagram of f and 0 induced by the kernel of f -/
def kernel_cone : cone (parallel_pair f 0) :=
{ X := of R f.ker,
  œÄ :=
  { app := Œª j,
    match j with
    | zero := f.ker.subtype
    | one := 0
    end,
    naturality' := Œª j j' g, by { cases j; cases j'; cases g; tidy } } }

/-- The kernel of a linear map is a kernel in the categorical sense -/
def kernel_is_limit : is_limit (kernel_cone f) :=
{ lift := Œª s, linear_map.cod_restrict f.ker (fork.Œπ s) (Œª c, linear_map.mem_ker.2 $
  by { erw [‚Üê@function.comp_apply _ _ _ f (fork.Œπ s) c, ‚Üêcoe_comp, fork.condition,
    has_zero_morphisms.comp_zero (fork.Œπ s) N], refl }),
  fac' := Œª s j, linear_map.ext $ Œª x,
  begin
    rw [coe_comp, function.comp_app, ‚Üêlinear_map.comp_apply],
    cases j,
    { erw @linear_map.subtype_comp_cod_restrict _ _ _ _ _ _ _ _ (fork.Œπ s) f.ker _ },
    { rw [‚Üêfork.app_zero_left, ‚Üêfork.app_zero_left], refl }
  end,
  uniq' := Œª s m h, linear_map.ext $ Œª x, subtype.ext_iff_val.2 $
    have h‚ÇÅ : (m ‚â´ (kernel_cone f).œÄ.app zero).to_fun = (s.œÄ.app zero).to_fun,
    by { congr, exact h zero },
    by convert @congr_fun _ _ _ _ h‚ÇÅ x }

end kernel

instance : has_kernels.{u} (Module R) :=
‚ü®Œª _ _ f, ‚ü®kernel_cone f, kernel_is_limit f‚ü©‚ü©

end Module

instance (M : Type u) [add_comm_group M] [module R M] : has_coe (submodule R M) (Module R) :=
‚ü® Œª N, Module.of R N ‚ü©
