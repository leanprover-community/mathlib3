/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import algebra.category.Module.basic
import algebra.category.Algebra.basic
import ring_theory.tensor_product

/-!
# Change Of Rings

## Main definitions

* `category_theory.Module.restrict_scalars`: given rings `R, S` and a ring homomorphism `R ‚ü∂ S`,
  then `restrict_scalars : Module S ‚•§ Module R` is defined by `M ‚Ü¶ M` where `M : S-module` is seen
  as `R-module` by `r ‚Ä¢ m := f r ‚Ä¢ m` and `S`-linear map `l : M ‚ü∂ M'` is `R`-linear as well.

* `category_theory.Module.extend_scalars`: given **commutative** rings `R, S` and ring homomorphism
  `f : R ‚ü∂ S`, then `extend_scalars : Module R ‚•§ Module S` is defined by `M ‚Ü¶ S ‚®Ç M` where the
  module structure is defined by `s ‚Ä¢ (s' ‚äó m) := (s * s') ‚äó m` and `R`-linear map `l : M ‚ü∂ M'`
  is sent to `S`-linear map `s ‚äó m ‚Ü¶ s ‚äó l m : S ‚®Ç M ‚ü∂ S ‚®Ç M'`.

## List of notations
Let `R, S` be rings and `f : R ‚Üí+* S`
* if `M` is an `R`-module, then notation `S ‚®Ç[R, f] M` means the tensor product `S ‚®Ç M` where `S`
  is considered as an `R`-module via restriction of scalars.
* if `M` is an `R`-module, `s : S` and `m : M`, then `s ‚äó‚Çú[R, f]` is the pure tensor
  `s ‚äó m : S ‚äó[R, f] M`.
-/


namespace category_theory.Module

universes v u‚ÇÅ u‚ÇÇ

namespace restrict_scalars

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [ring S] (f : R ‚Üí+* S)
variable (M : Module.{v} S)

/-- Any `S`-module M is also an `R`-module via a ring homomorphism `f : R ‚ü∂ S` by defining
    `r ‚Ä¢ m := f r ‚Ä¢ m` (`module.comp_hom`). This is called restriction of scalars. -/
def obj' : Module R :=
{ carrier := M,
  is_module := module.comp_hom M f }

/--
Given an `S`-linear map `g : M ‚Üí M'` between `S`-modules, `g` is also `R`-linear between `M` and
`M'` by means of restriction of scalars.
-/
def map' {M M' : Module.{v} S} (g : M ‚ü∂ M') :
  obj' f M ‚ü∂ obj' f M' :=
{ map_smul' := Œª r, g.map_smul (f r), ..g }

/--
If `R, S` are commutative rings and `f : R ‚Üí+* S`, then any `S`-algebra is also an `R`-algebra
-/
def is_algebra {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [comm_ring R] [comm_ring S] (f : R ‚Üí+* S)
  (A : Type v) [comm_semiring A] [algebra S A] : algebra R A :=
{ smul := (module.comp_hom A f).to_has_smul.smul,
  to_fun := (algebra_map _ _).comp f,
  map_one' := by simp,
  map_mul' := Œª _ _, by simp [map_mul],
  map_zero' := by simp,
  map_add' := Œª _ _, by simp [map_add],
  commutes' := Œª _ _, by ring,
  smul_def' := Œª r a, algebra.smul_def _ _ }

end restrict_scalars

/--
The restriction of scalars operation is functorial. For any `f : R ‚Üí+* S` a ring homomorphism,
* an `S`-module `M` can be considered as `R`-module by `r ‚Ä¢ m = f r ‚Ä¢ m`
* an `S`-linear map is also `R`-linear
-/
def restrict_scalars {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [ring S] (f : R ‚Üí+* S) :
  Module.{v} S ‚•§ Module.{v} R :=
{ obj := restrict_scalars.obj' f,
  map := Œª _ _, restrict_scalars.map' f,
  map_id' := Œª _, linear_map.ext $ Œª m, rfl,
  map_comp' := Œª _ _ _ g h, linear_map.ext $ Œª m, rfl }

namespace restrict_scalars

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [ring S] (f : R ‚Üí+* S)

section unbundled

@[simp] lemma smul_def_mk {M : Type v} [add_comm_group M] [module S M] (r : R)
  (m : (restrict_scalars f).obj (Module.mk M)) : r ‚Ä¢ m = (f r ‚Ä¢ m : M) := rfl

@[simp] lemma smul_def_mk' {M : Type v} [add_comm_group M] [module S M] (r : R)
  (m : M) : (r ‚Ä¢ m : (restrict_scalars f).obj (Module.mk M)) = (f r ‚Ä¢ m : M) := rfl

instance smul_comm_class_mk {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [comm_ring S] (f : R ‚Üí+* S)
  (M : Type v) [add_comm_group M] [module S M] :
  @smul_comm_class R S M ((restrict_scalars.obj' f (Module.mk M)).is_module.to_has_smul) _ :=
{ smul_comm := Œª r s m, by simp only [‚Üêmul_smul, smul_def_mk', mul_comm] }

end unbundled

section bundled

@[simp] lemma map_apply  {M M' : Module.{v} S} (g : M ‚ü∂ M') (x : (restrict_scalars f).obj M) :
  (restrict_scalars f).map g x = g x := rfl

@[simp] lemma map_apply'  {M M' : Module.{v} S} (g : M ‚ü∂ M') (x : M) :
  (restrict_scalars f).map g x = g x := rfl

@[simp] lemma smul_def {M : Module.{v} S} (r : R) (m : (restrict_scalars f).obj M) :
  r ‚Ä¢ m = (f r ‚Ä¢ m : M) := rfl

@[simp] lemma smul_def' {M : Module.{v} S} (r : R) (m : M) :
  (r ‚Ä¢ m : (restrict_scalars f).obj M) = (f r ‚Ä¢ m : M) := rfl

instance smul_comm_class {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [ring R] [comm_ring S] (f : R ‚Üí+* S)
  (M : Module.{v} S) :
  @smul_comm_class R S M ((restrict_scalars.obj' f M).is_module.to_has_smul) _ :=
{ smul_comm := Œª r s m, by simp only [‚Üêmul_smul, smul_def', mul_comm] }

end bundled

end restrict_scalars

namespace extend_scalars

open_locale tensor_product
open tensor_product

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [comm_ring R] [comm_ring S] (f : R ‚Üí+* S)

section unbundled

variables (M : Type v) [add_comm_monoid M] [module R M]

localized "notation s `‚äó‚Çú[` R `,` f `]` m := @tensor_product.tmul R _ _ _ _ _
  (module.comp_hom _ f) _ s m" in change_of_rings

end unbundled

open_locale change_of_rings

variables (M : Module.{v} R)

/--
Extension of scalars turn an `R`-module into `S`-module by M ‚Ü¶ S ‚®Ç M
-/
def obj' : Module S :=
‚ü®@tensor_product R _ S M _ _ (module.comp_hom S f) _‚ü©

/--
Extension of scalars is a functor where an `R`-module `M` is sent to `S ‚äó M` and
`l : M1 ‚ü∂ M2` is sent to `s ‚äó m ‚Ü¶ s ‚äó l m`
-/
def map' {M1 M2 : Module.{v} R} (l : M1 ‚ü∂ M2) : (obj' f M1) ‚ü∂ (obj' f M2) :=
@linear_map.base_change R S M1 M2 _ _ (restrict_scalars.is_algebra f S) _ _ _ _ l

lemma map'_id {M : Module.{v} R} : map' f (ùüô M) = ùüô _ :=
linear_map.ext $ Œª (x : obj' f M),
begin
  dsimp [map'],
  induction x using tensor_product.induction_on with _ _ m s ihx ihy,
  { simp only [map_zero], },
  { rw [linear_map.base_change_tmul, Module.id_apply], },
  { rw [map_add, ihx, ihy] },
end

lemma map'_comp {M‚ÇÅ M‚ÇÇ M‚ÇÉ : Module.{v} R} (l‚ÇÅ‚ÇÇ : M‚ÇÅ ‚ü∂ M‚ÇÇ) (l‚ÇÇ‚ÇÉ : M‚ÇÇ ‚ü∂ M‚ÇÉ) :
  map' f (l‚ÇÅ‚ÇÇ ‚â´ l‚ÇÇ‚ÇÉ) = map' f l‚ÇÅ‚ÇÇ ‚â´ map' f l‚ÇÇ‚ÇÉ :=
linear_map.ext $ Œª (x : obj' f M‚ÇÅ),
begin
  dsimp [map'],
  induction x using tensor_product.induction_on with _ _ x y ihx ihy,
  { refl, },
  { tidy, },
  { simp only [map_add, ihx, ihy], },
end

end extend_scalars

/--
Extension of scalars is a functor where an `R`-module `M` is sent to `S ‚äó M` and
`l : M1 ‚ü∂ M2` is sent to `s ‚äó m ‚Ü¶ s ‚äó l m`
-/
def extend_scalars {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [comm_ring R] [comm_ring S] (f : R ‚Üí+* S) :
  Module R ‚•§ Module S :=
{ obj := Œª M, extend_scalars.obj' f M,
  map := Œª M1 M2 l, extend_scalars.map' f l,
  map_id' := Œª _, extend_scalars.map'_id f,
  map_comp' := Œª _ _ _, extend_scalars.map'_comp f }

namespace extend_scalars

open_locale change_of_rings

variables {R : Type u‚ÇÅ} {S : Type u‚ÇÇ} [comm_ring R] [comm_ring S] (f : R ‚Üí+* S)

lemma smul_tmul {M : Module.{v} R} (s s' : S) (m : M) :
  s ‚Ä¢ (s' ‚äó‚Çú[R, f] m : (extend_scalars f).obj M) = (s * s') ‚äó‚Çú[R, f] m := rfl

lemma map_tmul {M M' : Module.{v} R} (g : M ‚ü∂ M') (s : S) (m : M) :
  (extend_scalars f).map g (s ‚äó‚Çú[R, f] m) = s ‚äó‚Çú[R, f] g m := rfl

end extend_scalars

end category_theory.Module
