/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Scott Morrison, Jakob von Raumer
-/
import category_theory.monoidal.braided
import category_theory.closed.monoidal
import algebra.category.Module.basic
import linear_algebra.tensor_product
import category_theory.linear.yoneda
import category_theory.monoidal.linear

/-!
# The symmetric monoidal category structure on R-modules

Mostly this uses existing machinery in `linear_algebra.tensor_product`.
We just need to provide a few small missing pieces to build the
`monoidal_category` instance and then the `symmetric_category` instance.

Note the universe level of the modules must be at least the universe level of the ring,
so that we have a monoidal unit.
For now, we simplify by insisting both universe levels are the same.

We then construct the monoidal closed structure on `Module R`.

If you're happy using the bundled `Module R`, it may be possible to mostly
use this as an interface and not need to interact much with the implementation details.
-/

universes v w x u

open category_theory

namespace Module

variables {R : Type u} [comm_ring R]

namespace monoidal_category
-- The definitions inside this namespace are essentially private.
-- After we build the `monoidal_category (Module R)` instance,
-- you should use that API.

open_locale tensor_product
local attribute [ext] tensor_product.ext

/-- (implementation) tensor product of R-modules -/
def tensor_obj (M N : Module R) : Module R := Module.of R (M âŠ—[R] N)
/-- (implementation) tensor product of morphisms R-modules -/
def tensor_hom {M N M' N' : Module R} (f : M âŸ¶ N) (g : M' âŸ¶ N') :
  tensor_obj M M' âŸ¶ tensor_obj N N' :=
tensor_product.map f g

lemma tensor_id (M N : Module R) : tensor_hom (ğŸ™ M) (ğŸ™ N) = ğŸ™ (Module.of R (M âŠ— N)) :=
by { ext1, refl }

lemma tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : Module R}
  (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
    tensor_hom (fâ‚ â‰« gâ‚) (fâ‚‚ â‰« gâ‚‚) = tensor_hom fâ‚ fâ‚‚ â‰« tensor_hom gâ‚ gâ‚‚ :=
by { ext1, refl }

/-- (implementation) the associator for R-modules -/
def associator (M : Module.{v} R) (N : Module.{w} R) (K : Module.{x} R) :
  tensor_obj (tensor_obj M N) K â‰… tensor_obj M (tensor_obj N K) :=
(tensor_product.assoc R M N K).to_Module_iso

section

/-! The `associator_naturality` and `pentagon` lemmas below are very slow to elaborate.

We give them some help by expressing the lemmas first non-categorically, then using
`convert _aux using 1` to have the elaborator work as little as possible. -/

open tensor_product (assoc map)

private lemma associator_naturality_aux
  {Xâ‚ Xâ‚‚ Xâ‚ƒ : Type*}
  [add_comm_monoid Xâ‚] [add_comm_monoid Xâ‚‚] [add_comm_monoid Xâ‚ƒ]
  [module R Xâ‚] [module R Xâ‚‚] [module R Xâ‚ƒ]
  {Yâ‚ Yâ‚‚ Yâ‚ƒ : Type*}
  [add_comm_monoid Yâ‚] [add_comm_monoid Yâ‚‚] [add_comm_monoid Yâ‚ƒ]
  [module R Yâ‚] [module R Yâ‚‚] [module R Yâ‚ƒ]
  (fâ‚ : Xâ‚ â†’â‚—[R] Yâ‚) (fâ‚‚ : Xâ‚‚ â†’â‚—[R] Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ â†’â‚—[R] Yâ‚ƒ) :
  (â†‘(assoc R Yâ‚ Yâ‚‚ Yâ‚ƒ) âˆ˜â‚— (map (map fâ‚ fâ‚‚) fâ‚ƒ)) = ((map fâ‚ (map fâ‚‚ fâ‚ƒ)) âˆ˜â‚— â†‘(assoc R Xâ‚ Xâ‚‚ Xâ‚ƒ)) :=
begin
  apply tensor_product.ext_threefold,
  intros x y z,
  refl
end

variables (R)

private lemma pentagon_aux
  (W X Y Z : Type*)
  [add_comm_monoid W] [add_comm_monoid X] [add_comm_monoid Y] [add_comm_monoid Z]
  [module R W] [module R X] [module R Y] [module R Z] :
  ((map (1 : W â†’â‚—[R] W) (assoc R X Y Z).to_linear_map).comp (assoc R W (X âŠ—[R] Y) Z).to_linear_map)
    .comp (map â†‘(assoc R W X Y) (1 : Z â†’â‚—[R] Z)) =
  (assoc R W X (Y âŠ—[R] Z)).to_linear_map.comp (assoc R (W âŠ—[R] X) Y Z).to_linear_map :=
begin
  apply tensor_product.ext_fourfold,
  intros w x y z,
  refl
end

end

lemma associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : Module R}
  (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
    tensor_hom (tensor_hom fâ‚ fâ‚‚) fâ‚ƒ â‰« (associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
    (associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« tensor_hom fâ‚ (tensor_hom fâ‚‚ fâ‚ƒ) :=
by convert associator_naturality_aux fâ‚ fâ‚‚ fâ‚ƒ using 1

lemma pentagon (W X Y Z : Module R) :
  tensor_hom (associator W X Y).hom (ğŸ™ Z) â‰« (associator W (tensor_obj X Y) Z).hom
  â‰« tensor_hom (ğŸ™ W) (associator X Y Z).hom =
    (associator (tensor_obj W X) Y Z).hom â‰« (associator W X (tensor_obj Y Z)).hom :=
by convert pentagon_aux R W X Y Z using 1

/-- (implementation) the left unitor for R-modules -/
def left_unitor (M : Module.{u} R) : Module.of R (R âŠ—[R] M) â‰… M :=
(linear_equiv.to_Module_iso (tensor_product.lid R M) : of R (R âŠ— M) â‰… of R M).trans (of_self_iso M)

lemma left_unitor_naturality {M N : Module R} (f : M âŸ¶ N) :
  tensor_hom (ğŸ™ (Module.of R R)) f â‰« (left_unitor N).hom = (left_unitor M).hom â‰« f :=
begin
  ext x y, dsimp,
  erw [tensor_product.lid_tmul, tensor_product.lid_tmul],
  rw linear_map.map_smul,
  refl,
end

/-- (implementation) the right unitor for R-modules -/
def right_unitor (M : Module.{u} R) : Module.of R (M âŠ—[R] R) â‰… M :=
(linear_equiv.to_Module_iso (tensor_product.rid R M) : of R (M âŠ— R) â‰… of R M).trans (of_self_iso M)

lemma right_unitor_naturality {M N : Module R} (f : M âŸ¶ N) :
  tensor_hom f (ğŸ™ (Module.of R R)) â‰« (right_unitor N).hom = (right_unitor M).hom â‰« f :=
begin
  ext x y, dsimp,
  erw [tensor_product.rid_tmul, tensor_product.rid_tmul],
  rw linear_map.map_smul,
  refl,
end

lemma triangle (M N : Module.{u} R) :
  (associator M (Module.of R R) N).hom â‰« tensor_hom (ğŸ™ M) (left_unitor N).hom =
    tensor_hom (right_unitor M).hom (ğŸ™ N) :=
begin
  apply tensor_product.ext_threefold,
  intros x y z,
  change R at y,
  dsimp [tensor_hom, associator],
  erw [tensor_product.lid_tmul, tensor_product.rid_tmul],
  exact (tensor_product.smul_tmul _ _ _).symm
end

end monoidal_category

open monoidal_category

instance monoidal_category : monoidal_category (Module.{u} R) :=
{ -- data
  tensor_obj   := tensor_obj,
  tensor_hom   := @tensor_hom _ _,
  tensor_unit  := Module.of R R,
  associator   := associator,
  left_unitor  := left_unitor,
  right_unitor := right_unitor,
  -- properties
  tensor_id'               := Î» M N, tensor_id M N,
  tensor_comp'             := Î» M N K M' N' K' f g h, tensor_comp f g h,
  associator_naturality'   := Î» M N K M' N' K' f g h, associator_naturality f g h,
  left_unitor_naturality'  := Î» M N f, left_unitor_naturality f,
  right_unitor_naturality' := Î» M N f, right_unitor_naturality f,
  pentagon'                := Î» M N K L, pentagon M N K L,
  triangle'                := Î» M N, triangle M N, }

/-- Remind ourselves that the monoidal unit, being just `R`, is still a commutative ring. -/
instance : comm_ring ((ğŸ™_ (Module.{u} R) : Module.{u} R) : Type u) :=
(by apply_instance : comm_ring R)

namespace monoidal_category

@[simp]
lemma hom_apply {K L M N : Module.{u} R} (f : K âŸ¶ L) (g : M âŸ¶ N) (k : K) (m : M) :
  (f âŠ— g) (k âŠ—â‚œ m) = f k âŠ—â‚œ g m := rfl

@[simp]
lemma left_unitor_hom_apply {M : Module.{u} R} (r : R) (m : M) :
  ((Î»_ M).hom : ğŸ™_ (Module R) âŠ— M âŸ¶ M) (r âŠ—â‚œ[R] m) = r â€¢ m :=
tensor_product.lid_tmul m r

@[simp]
lemma left_unitor_inv_apply {M : Module.{u} R} (m : M) :
  ((Î»_ M).inv : M âŸ¶ ğŸ™_ (Module.{u} R) âŠ— M) m = 1 âŠ—â‚œ[R] m :=
tensor_product.lid_symm_apply m

@[simp]
lemma right_unitor_hom_apply {M : Module.{u} R} (m : M) (r : R) :
  ((Ï_ M).hom : M âŠ— ğŸ™_ (Module R) âŸ¶ M) (m âŠ—â‚œ r) = r â€¢ m :=
tensor_product.rid_tmul m r

@[simp]
lemma right_unitor_inv_apply {M : Module.{u} R} (m : M) :
  ((Ï_ M).inv : M âŸ¶ M âŠ— ğŸ™_ (Module.{u} R)) m = m âŠ—â‚œ[R] 1 :=
tensor_product.rid_symm_apply m

@[simp]
lemma associator_hom_apply {M N K : Module.{u} R} (m : M) (n : N) (k : K) :
  ((Î±_ M N K).hom : (M âŠ— N) âŠ— K âŸ¶ M âŠ— (N âŠ— K)) ((m âŠ—â‚œ n) âŠ—â‚œ k) = (m âŠ—â‚œ (n âŠ—â‚œ k)) := rfl

@[simp]
lemma associator_inv_apply {M N K : Module.{u} R} (m : M) (n : N) (k : K) :
  ((Î±_ M N K).inv : M âŠ— (N âŠ— K) âŸ¶ (M âŠ— N) âŠ— K) (m âŠ—â‚œ (n âŠ—â‚œ k)) = ((m âŠ—â‚œ n) âŠ—â‚œ k) := rfl

end monoidal_category

/-- (implementation) the braiding for R-modules -/
def braiding (M N : Module R) : tensor_obj M N â‰… tensor_obj N M :=
linear_equiv.to_Module_iso (tensor_product.comm R M N)

@[simp] lemma braiding_naturality {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : Module.{u} R} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
  (f âŠ— g) â‰« (Yâ‚.braiding Yâ‚‚).hom =
    (Xâ‚.braiding Xâ‚‚).hom â‰« (g âŠ— f) :=
begin
  apply tensor_product.ext',
  intros x y,
  refl
end

@[simp] lemma hexagon_forward (X Y Z : Module.{u} R) :
  (Î±_ X Y Z).hom â‰« (braiding X _).hom â‰« (Î±_ Y Z X).hom =
  ((braiding X Y).hom âŠ— ğŸ™ Z) â‰« (Î±_ Y X Z).hom â‰« (ğŸ™ Y âŠ— (braiding X Z).hom) :=
begin
  apply tensor_product.ext_threefold,
  intros x y z,
  refl,
end

@[simp] lemma hexagon_reverse (X Y Z : Module.{u} R) :
  (Î±_ X Y Z).inv â‰« (braiding _ Z).hom â‰« (Î±_ Z X Y).inv =
  (ğŸ™ X âŠ— (Y.braiding Z).hom) â‰« (Î±_ X Z Y).inv â‰« ((X.braiding Z).hom âŠ— ğŸ™ Y) :=
begin
  apply (cancel_epi (Î±_ X Y Z).hom).1,
  apply tensor_product.ext_threefold,
  intros x y z,
  refl,
end

local attribute [ext] tensor_product.ext

/-- The symmetric monoidal structure on `Module R`. -/
instance symmetric_category : symmetric_category (Module.{u} R) :=
{ braiding := braiding,
  braiding_naturality' := Î» Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ f g, braiding_naturality f g,
  hexagon_forward' := hexagon_forward,
  hexagon_reverse' := hexagon_reverse, }

namespace monoidal_category

@[simp] lemma braiding_hom_apply {M N : Module.{u} R} (m : M) (n : N) :
  ((Î²_ M N).hom : M âŠ— N âŸ¶ N âŠ— M) (m âŠ—â‚œ n) = n âŠ—â‚œ m := rfl

@[simp] lemma braiding_inv_apply {M N : Module.{u} R} (m : M) (n : N) :
  ((Î²_ M N).inv : N âŠ— M âŸ¶ M âŠ— N) (n âŠ—â‚œ m) = m âŠ—â‚œ n := rfl

end monoidal_category

open opposite

instance : monoidal_preadditive (Module.{u} R) :=
by refine âŸ¨_, _, _, _âŸ©; dsimp only [auto_param]; intros;
  refine tensor_product.ext (linear_map.ext $ Î» x, linear_map.ext $ Î» y, _);
  simp only [linear_map.comprâ‚‚_apply, tensor_product.mk_apply, monoidal_category.hom_apply,
    linear_map.zero_apply, tensor_product.tmul_zero, tensor_product.zero_tmul,
    linear_map.add_apply, tensor_product.tmul_add, tensor_product.add_tmul]

instance : monoidal_linear R (Module.{u} R) :=
by refine âŸ¨_, _âŸ©; dsimp only [auto_param]; intros;
  refine tensor_product.ext (linear_map.ext $ Î» x, linear_map.ext $ Î» y, _);
  simp only [linear_map.comprâ‚‚_apply, tensor_product.mk_apply, monoidal_category.hom_apply,
    linear_map.smul_apply, tensor_product.tmul_smul, tensor_product.smul_tmul]

/--
Auxiliary definition for the `monoidal_closed` instance on `Module R`.
(This is only a separate definition in order to speed up typechecking. )
-/
@[simps]
def monoidal_closed_hom_equiv (M N P : Module.{u} R) :
  ((monoidal_category.tensor_left M).obj N âŸ¶ P) â‰ƒ
    (N âŸ¶ ((linear_coyoneda R (Module R)).obj (op M)).obj P) :=
{ to_fun := Î» f, linear_map.comprâ‚‚ (tensor_product.mk R N M) ((Î²_ N M).hom â‰« f),
  inv_fun := Î» f, (Î²_ M N).hom â‰« tensor_product.lift f,
  left_inv := Î» f, begin ext m n,
    simp only [tensor_product.mk_apply, tensor_product.lift.tmul, linear_map.comprâ‚‚_apply,
      function.comp_app, coe_comp, monoidal_category.braiding_hom_apply],
  end,
  right_inv := Î» f, begin ext m n,
    simp only [tensor_product.mk_apply, tensor_product.lift.tmul, linear_map.comprâ‚‚_apply,
      symmetric_category.symmetry_assoc],
  end, }

instance : monoidal_closed (Module.{u} R) :=
{ closed' := Î» M,
  { is_adj :=
    { right := (linear_coyoneda R (Module.{u} R)).obj (op M),
      adj := adjunction.mk_of_hom_equiv
      { hom_equiv := Î» N P, monoidal_closed_hom_equiv M N P, } } } }

-- I can't seem to express the function coercion here without writing `@coe_fn`.
@[simp]
lemma monoidal_closed_curry {M N P : Module.{u} R} (f : M âŠ— N âŸ¶ P) (x : M) (y : N) :
  @coe_fn _ _ linear_map.has_coe_to_fun ((monoidal_closed.curry f : N â†’â‚—[R] (M â†’â‚—[R] P)) y) x =
    f (x âŠ—â‚œ[R] y) :=
rfl

@[simp]
lemma monoidal_closed_uncurry {M N P : Module.{u} R}
  (f : N âŸ¶ (M âŸ¶[Module.{u} R] P)) (x : M) (y : N) :
  monoidal_closed.uncurry f (x âŠ—â‚œ[R] y) = (@coe_fn _ _ linear_map.has_coe_to_fun (f y)) x :=
by { simp only [monoidal_closed.uncurry, ihom.adjunction, is_left_adjoint.adj], simp, }

end Module
