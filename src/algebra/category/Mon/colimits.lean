/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.category.Mon.basic
import category_theory.limits.has_limits
import category_theory.limits.concrete_category
import category_theory.limits.types
import category_theory.limits.preserves.filtered

/-!
# The category of monoids has all colimits.

We do this construction knowing nothing about monoids.
In particular, I want to claim that this file could be produced by a python script
that just looks at the output of `#print monoid`:

  -- structure monoid : Type u ‚Üí Type u
  -- fields:
  -- monoid.mul : Œ† {Œ± : Type u} [c : monoid Œ±], Œ± ‚Üí Œ± ‚Üí Œ±
  -- monoid.mul_assoc : ‚àÄ {Œ± : Type u} [c : monoid Œ±] (a b c_1 : Œ±), a * b * c_1 = a * (b * c_1)
  -- monoid.one : Œ† (Œ± : Type u) [c : monoid Œ±], Œ±
  -- monoid.one_mul : ‚àÄ {Œ± : Type u} [c : monoid Œ±] (a : Œ±), 1 * a = a
  -- monoid.mul_one : ‚àÄ {Œ± : Type u} [c : monoid Œ±] (a : Œ±), a * 1 = a

and if we'd fed it the output of `#print comm_ring`, this file would instead build
colimits of commutative rings.

A slightly bolder claim is that we could do this with tactics, as well.
-/

universes v

open category_theory
open category_theory.limits

namespace Mon.colimits
/-!
We build the colimit of a diagram in `Mon` by constructing the
free monoid on the disjoint union of all the monoids in the diagram,
then taking the quotient by the monoid laws within each monoid,
and the identifications given by the morphisms in the diagram.
-/

variables {J : Type v} [small_category J] (F : J ‚•§ Mon.{v})

/--
An inductive type representing all monoid expressions (without relations)
on a collection of types indexed by the objects of `J`.
-/
inductive prequotient
-- There's always `of`
| of : Œ† (j : J) (x : F.obj j), prequotient
-- Then one generator for each operation
| one : prequotient
| mul : prequotient ‚Üí prequotient ‚Üí prequotient

instance : inhabited (prequotient F) := ‚ü®prequotient.one‚ü©

open prequotient

/--
The relation on `prequotient` saying when two expressions are equal
because of the monoid laws, or
because one element is mapped to another by a morphism in the diagram.
-/
inductive relation : prequotient F ‚Üí prequotient F ‚Üí Prop
-- Make it an equivalence relation:
| refl : Œ† (x), relation x x
| symm : Œ† (x y) (h : relation x y), relation y x
| trans : Œ† (x y z) (h : relation x y) (k : relation y z), relation x z
-- There's always a `map` relation
| map : Œ† (j j' : J) (f : j ‚ü∂ j') (x : F.obj j), relation (of j' ((F.map f) x)) (of j x)
-- Then one relation per operation, describing the interaction with `of`
| mul : Œ† (j) (x y : F.obj j), relation (of j (x * y)) (mul (of j x) (of j y))
| one : Œ† (j), relation (of j 1) one
-- Then one relation per argument of each operation
| mul_1 : Œ† (x x' y) (r : relation x x'), relation (mul x y) (mul x' y)
| mul_2 : Œ† (x y y') (r : relation y y'), relation (mul x y) (mul x y')
-- And one relation per axiom
| mul_assoc : Œ† (x y z), relation (mul (mul x y) z) (mul x (mul y z))
| one_mul : Œ† (x), relation (mul one x) x
| mul_one : Œ† (x), relation (mul x one) x

/--
The setoid corresponding to monoid expressions modulo monoid relations and identifications.
-/
def colimit_setoid : setoid (prequotient F) :=
{ r := relation F, iseqv := ‚ü®relation.refl, relation.symm, relation.trans‚ü© }
attribute [instance] colimit_setoid

/--
The underlying type of the colimit of a diagram in `Mon`.
-/
@[derive inhabited]
def colimit_type : Type v := quotient (colimit_setoid F)

instance monoid_colimit_type : monoid (colimit_type F) :=
{ mul :=
  begin
    fapply @quot.lift _ _ ((colimit_type F) ‚Üí (colimit_type F)),
    { intro x,
      fapply @quot.lift,
      { intro y,
        exact quot.mk _ (mul x y) },
      { intros y y' r,
        apply quot.sound,
        exact relation.mul_2 _ _ _ r } },
    { intros x x' r,
      funext y,
      induction y,
      dsimp,
      apply quot.sound,
      { exact relation.mul_1 _ _ _ r },
      { refl } },
  end,
  one :=
  begin
    exact quot.mk _ one
  end,
  mul_assoc := Œª x y z,
  begin
    induction x,
    induction y,
    induction z,
    dsimp,
    apply quot.sound,
    apply relation.mul_assoc,
    refl,
    refl,
    refl,
  end,
  one_mul := Œª x,
  begin
    induction x,
    dsimp,
    apply quot.sound,
    apply relation.one_mul,
    refl,
  end,
  mul_one := Œª x,
  begin
    induction x,
    dsimp,
    apply quot.sound,
    apply relation.mul_one,
    refl,
  end }

@[simp] lemma quot_one : quot.mk setoid.r one = (1 : colimit_type F) := rfl
@[simp] lemma quot_mul (x y) : quot.mk setoid.r (mul x y) =
  ((quot.mk setoid.r x) * (quot.mk setoid.r y) : colimit_type F) := rfl

/-- The bundled monoid giving the colimit of a diagram. -/
def colimit : Mon := ‚ü®colimit_type F, by apply_instance‚ü©

/-- The function from a given monoid in the diagram to the colimit monoid. -/
def cocone_fun (j : J) (x : F.obj j) : colimit_type F :=
quot.mk _ (of j x)

/-- The monoid homomorphism from a given monoid in the diagram to the colimit monoid. -/
def cocone_morphism (j : J) : F.obj j ‚ü∂ colimit F :=
{ to_fun := cocone_fun F j,
  map_one' := quot.sound (relation.one _),
  map_mul' := Œª x y, quot.sound (relation.mul _ _ _) }

@[simp] lemma cocone_naturality {j j' : J} (f : j ‚ü∂ j') :
  F.map f ‚â´ (cocone_morphism F j') = cocone_morphism F j :=
begin
  ext,
  apply quot.sound,
  apply relation.map,
end

@[simp] lemma cocone_naturality_components (j j' : J) (f : j ‚ü∂ j') (x : F.obj j):
  (cocone_morphism F j') (F.map f x) = (cocone_morphism F j) x :=
by { rw ‚Üêcocone_naturality F f, refl }

/-- The cocone over the proposed colimit monoid. -/
def colimit_cocone : cocone F :=
{ X := colimit F,
  Œπ :=
  { app := cocone_morphism F, } }.

/-- The function from the free monoid on the diagram to the cone point of any other cocone. -/
@[simp] def desc_fun_lift (s : cocone F) : prequotient F ‚Üí s.X
| (of j x)  := (s.Œπ.app j) x
| one       := 1
| (mul x y) := desc_fun_lift x * desc_fun_lift y

/-- The function from the colimit monoid to the cone point of any other cocone. -/
def desc_fun (s : cocone F) : colimit_type F ‚Üí s.X :=
begin
  fapply quot.lift,
  { exact desc_fun_lift F s },
  { intros x y r,
    induction r; try { dsimp },
    -- refl
    { refl },
    -- symm
    { exact r_ih.symm },
    -- trans
    { exact eq.trans r_ih_h r_ih_k },
    -- map
    { simp, },
    -- mul
    { simp, },
    -- one
    { simp, },
    -- mul_1
    { rw r_ih, },
    -- mul_2
    { rw r_ih, },
    -- mul_assoc
    { rw mul_assoc, },
    -- one_mul
    { rw one_mul, },
    -- mul_one
    { rw mul_one, } }
end

/-- The monoid homomorphism from the colimit monoid to the cone point of any other cocone. -/
def desc_morphism (s : cocone F) : colimit F ‚ü∂ s.X :=
{ to_fun := desc_fun F s,
  map_one' := rfl,
  map_mul' := Œª x y, by { induction x; induction y; refl }, }

/-- Evidence that the proposed colimit is the colimit. -/
def colimit_cocone_is_colimit : is_colimit (colimit_cocone F) :=
{ desc := Œª s, desc_morphism F s,
  uniq' := Œª s m w,
  begin
    ext,
    induction x,
    induction x,
    { have w' := congr_fun (congr_arg (Œª f : F.obj x_j ‚ü∂ s.X, (f : F.obj x_j ‚Üí s.X)) (w x_j)) x_x,
      erw w',
      refl, },
    { simp *, },
    { simp *, },
    refl
  end }.

instance has_colimits_Mon : has_colimits Mon :=
{ has_colimits_of_shape := Œª J ùí•, by exactI
  { has_colimit := Œª F, has_colimit.mk
    { cocone := colimit_cocone F,
      is_colimit := colimit_cocone_is_colimit F } } }

instance monoid_comp_forget (j : J) : monoid ((F ‚ãô forget Mon).obj j) :=
by { change monoid (F.obj j), apply_instance, }

@[simps]
def one_nat_trans : (F ‚ãô forget Mon) ‚ãô types.star ‚ü∂ F ‚ãô forget Mon :=
{ app := Œª j p, 1, }

@[simps]
def mul_nat_trans : (F ‚ãô forget Mon) ‚ãô types.diagonal ‚ü∂ F ‚ãô forget Mon :=
{ app := Œª j p, p.1 * p.2, }

@[simps]
def mul_one_nat_trans : (F ‚ãô forget Mon) ‚ü∂ (F ‚ãô forget Mon) ‚ãô types.Prop :=
{ app := Œª j p, ‚ü®p * 1 = p‚ü©, }

@[simps]
def mul_assoc_nat_trans : (F ‚ãô forget Mon) ‚ãô types.triple_diagonal ‚ü∂ (F ‚ãô forget Mon) ‚ãô types.Prop :=
{ app := Œª j p, ‚ü®(p.1 * p.2.1) * p.2.2 = p.1 * (p.2.1 * p.2.2)‚ü©,
  naturality' := Œª j j' f,
  begin
    ext ‚ü®x,y,z‚ü©,
    dsimp,
    split,
    { intro h, simp only [mul_assoc], },
    { intro h, simp only [‚Üê(F.map f).map_mul, h], },
  end }

noncomputable
def one_colimit [is_filtered J] (F : J ‚•§ Mon) :
  punit.{v+1} ‚ü∂ limits.colimit (F ‚ãô forget Mon) :=
(preserves_colimit_iso' (F ‚ãô forget Mon) types.star).hom ‚â´ colim.map (one_nat_trans F)

noncomputable
def mul_colimit [is_filtered J] (F : J ‚•§ Mon) :
  types.diagonal.obj (limits.colimit (F ‚ãô forget Mon)) ‚ü∂ limits.colimit (F ‚ãô forget Mon) :=
(preserves_colimit_iso' (F ‚ãô forget Mon) types.diagonal).hom ‚â´ colim.map (mul_nat_trans F)

noncomputable
def mul_assoc_colimit [is_filtered J] (F : J ‚•§ Mon) :
  types.triple_diagonal.obj (limits.colimit (F ‚ãô forget Mon)) ‚ü∂ ulift Prop :=
(preserves_colimit_iso' (F ‚ãô forget Mon) types.triple_diagonal).hom ‚â´
  colim.map (mul_assoc_nat_trans F) ‚â´
  (preserves_colimit_iso' (F ‚ãô forget Mon) types.Prop).inv

lemma desc_comp_star (F : J ‚•§ Type v) (s) (x) :
  ‚àÉ j : J, colimit.desc (F ‚ãô types.star) s x = s.Œπ.app j punit.star :=
begin
  obtain ‚ü®j, ‚ü®‚ü©, rfl‚ü© := types.jointly_surjective _ (colimit.is_colimit _) x,
  exact ‚ü®j, rfl‚ü©,
end

lemma desc_comp_star' [is_filtered J] (F : J ‚•§ Type v) (s) (x) (j) :
  colimit.desc (F ‚ãô types.star) s x = s.Œπ.app j punit.star :=
begin
  obtain ‚ü®j', e‚ü© := desc_comp_star F s x,
  rw e, clear e,
  rw [‚Üês.w (is_filtered.left_to_sup j j'), ‚Üês.w (is_filtered.right_to_sup j j')],
  simp,
end

@[simp]
lemma desc_comp_star'' [is_filtered J] (F : J ‚•§ Type v) (s) (x) :
  colimit.desc (F ‚ãô types.star) s x = s.Œπ.app (is_filtered.nonempty.some) punit.star :=
desc_comp_star' F s x _

lemma inv_desc_comp_diagonal (F : J ‚•§ Type v) (s : cocone F)
  [is_iso (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone s))]
  {j‚ÇÅ j‚ÇÇ : J} (x‚ÇÅ : F.obj j‚ÇÅ) (x‚ÇÇ : F.obj j‚ÇÇ)
  {j : J} (i‚ÇÅ : j‚ÇÅ ‚ü∂ j) (i‚ÇÇ : j‚ÇÇ ‚ü∂ j) :
  inv (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone s))
    (s.Œπ.app j‚ÇÅ x‚ÇÅ, s.Œπ.app j‚ÇÇ x‚ÇÇ) = colimit.Œπ (F ‚ãô types.diagonal) j (F.map i‚ÇÅ x‚ÇÅ, F.map i‚ÇÇ x‚ÇÇ) :=
begin
  sorry,
end

lemma inv_desc_comp_diagonal' (F : J ‚•§ Type v) (s : cocone F)
  [is_iso (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone s))]
  {j : J} (x‚ÇÅ : F.obj j) (x‚ÇÇ : F.obj j) :
  inv (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone s))
    (s.Œπ.app j x‚ÇÅ, s.Œπ.app j x‚ÇÇ) = colimit.Œπ (F ‚ãô types.diagonal) j (x‚ÇÅ, x‚ÇÇ) :=
by simpa using inv_desc_comp_diagonal F s x‚ÇÅ x‚ÇÇ (ùüô _) (ùüô _)

lemma inv_desc_comp_diagonal'' [is_filtered J] (F : J ‚•§ Type v) (s : cocone F)
  [is_iso (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone s))]
  {j‚ÇÅ j‚ÇÇ : J} (x‚ÇÅ : F.obj j‚ÇÅ) (x‚ÇÇ : F.obj j‚ÇÇ)  :
  inv (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone s))
    (s.Œπ.app j‚ÇÅ x‚ÇÅ, s.Œπ.app j‚ÇÇ x‚ÇÇ) =
    colimit.Œπ (F ‚ãô types.diagonal) (is_filtered.sup j‚ÇÅ j‚ÇÇ)
      (F.map (is_filtered.left_to_sup j‚ÇÅ j‚ÇÇ) x‚ÇÅ, F.map (is_filtered.right_to_sup j‚ÇÅ j‚ÇÇ) x‚ÇÇ) :=
inv_desc_comp_diagonal F s x‚ÇÅ x‚ÇÇ _ _

@[simp]
lemma inv_desc_comp_diagonal''' [is_filtered J] (F : J ‚•§ Type v)
  [is_iso (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone (colimit.cocone F)))]
  {j‚ÇÅ j‚ÇÇ : J} (x‚ÇÅ : F.obj j‚ÇÅ) (x‚ÇÇ : F.obj j‚ÇÇ)  :
  inv (colimit.desc (F ‚ãô types.diagonal) (types.diagonal.map_cocone (colimit.cocone F)))
    (colimit.Œπ F j‚ÇÅ x‚ÇÅ, colimit.Œπ F j‚ÇÇ x‚ÇÇ) =
    colimit.Œπ (F ‚ãô types.diagonal) (is_filtered.sup j‚ÇÅ j‚ÇÇ)
      (F.map (is_filtered.left_to_sup j‚ÇÅ j‚ÇÇ) x‚ÇÅ, F.map (is_filtered.right_to_sup j‚ÇÅ j‚ÇÇ) x‚ÇÇ) :=
inv_desc_comp_diagonal'' F (colimit.cocone F) _ _

noncomputable
instance [is_filtered J] : monoid (limits.colimit (F ‚ãô forget Mon)) :=
{ one := one_colimit F punit.star,
  mul := Œª x y, mul_colimit F (x, y),
  one_mul := Œª x,
  begin
    obtain ‚ü®j, x, rfl‚ü© := types.jointly_surjective' _ x,
    dsimp [(*), mul_colimit, one_colimit],
    dunfold preserves_colimit_iso',
    dsimp [preserves_colimit_iso, is_colimit.cocone_point_unique_up_to_iso],
    dsimp [colim, colim_map, is_colimit.map],
    simp,
  end,
  mul_one := sorry,
  mul_assoc := Œª x y z,
  begin
    obtain ‚ü®jx, x, rfl‚ü© := types.jointly_surjective' _ x,
    obtain ‚ü®jy, y, rfl‚ü© := types.jointly_surjective' _ y,
    obtain ‚ü®jz, z, rfl‚ü© := types.jointly_surjective' _ z,
    dsimp [(*), mul_colimit, one_colimit],
    dunfold preserves_colimit_iso',
    dsimp [preserves_colimit_iso, is_colimit.cocone_point_unique_up_to_iso],
    dsimp [colim, colim_map, is_colimit.map],
    simp [monoid_hom.map_mul],
    rw [mul_assoc],

    sorry,
  end, }

end Mon.colimits
