/-
Copyright (c) 2021 Justus Springer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Justus Springer
-/
import algebra.category.Mon.basic
import category_theory.limits.concrete_category
import category_theory.limits.preserves.filtered

/-!
# The forgetful functor from (commutative) (additive) monoids preserves filtered colimits.

Forgetful functors from algebraic categories usually don't preserve colimits. However, they tend
to preserve _filtered_ colimits.

In this file, we start with a small filtered category `J` and a functor `F : J ‚•§ Mon`.
We then construct a monoid structure on the colimit of `F ‚ãô forget Mon` (in `Type`), thereby
showing that the forgetful functor `forget Mon` preserves filtered colimits. Similarly for `AddMon`,
`CommMon` and `AddCommMon`.

-/

universes v u

noncomputable theory
open_locale classical

open category_theory
open category_theory.limits
open category_theory.is_filtered (renaming max ‚Üí max') -- avoid name collision with `_root_.max`.

namespace Mon.filtered_colimits

section

-- We use parameters here, mainly so we can have the abbreviations `M` and `M.mk` below, without
-- passing around `F` all the time.
parameters {J : Type v} [small_category J] (F : J ‚•§ Mon.{max v u})

/--
The colimit of `F ‚ãô forget Mon` in the category of types.
In the following, we will construct a monoid structure on `M`.
-/
@[to_additive "The colimit of `F ‚ãô forget AddMon` in the category of types.
In the following, we will construct an additive monoid structure on `M`."]
abbreviation M : Type (max v u) := types.quot (F ‚ãô forget Mon)

/-- The canonical projection into the colimit, as a quotient type. -/
@[to_additive "The canonical projection into the colimit, as a quotient type."]
abbreviation M.mk : (Œ£ j, F.obj j) ‚Üí M := quot.mk (types.quot.rel (F ‚ãô forget Mon))

@[to_additive]
lemma M.mk_eq (x y : Œ£ j, F.obj j)
  (h : ‚àÉ (k : J) (f : x.1 ‚ü∂ k) (g : y.1 ‚ü∂ k), F.map f x.2 = F.map g y.2) :
  M.mk x = M.mk y :=
quot.eqv_gen_sound (types.filtered_colimit.eqv_gen_quot_rel_of_rel (F ‚ãô forget Mon) x y h)

variables [is_filtered J]

/--
As `J` is nonempty, we can pick an arbitrary object `j‚ÇÄ : J`. We use this object to define the
"one" in the colimit as the equivalence class of `‚ü®j‚ÇÄ, 1 : F.obj j‚ÇÄ‚ü©`.
-/
@[to_additive "As `J` is nonempty, we can pick an arbitrary object `j‚ÇÄ : J`. We use this object to
define the \"zero\" in the colimit as the equivalence class of `‚ü®j‚ÇÄ, 0 : F.obj j‚ÇÄ‚ü©`."]
instance colimit_has_one : has_one M :=
{ one := M.mk ‚ü®is_filtered.nonempty.some, 1‚ü© }

/--
The definition of the "one" in the colimit is independent of the chosen object of `J`.
In particular, this lemma allows us to "unfold" the definition of `colimit_one` at a custom chosen
object `j`.
-/
@[to_additive "The definition of the \"zero\" in the colimit is independent of the chosen object
of `J`. In particular, this lemma allows us to \"unfold\" the definition of `colimit_zero` at a
custom chosen object `j`."]
lemma colimit_one_eq (j : J) : (1 : M) = M.mk ‚ü®j, 1‚ü© :=
begin
  apply M.mk_eq,
  refine ‚ü®max' _ j, left_to_max _ j, right_to_max _ j, _‚ü©,
  simp,
end

/--
The "unlifted" version of multiplication in the colimit. To multiply two dependent pairs
`‚ü®j‚ÇÅ, x‚ü©` and `‚ü®j‚ÇÇ, y‚ü©`, we pass to a common successor of `j‚ÇÅ` and `j‚ÇÇ` (given by `is_filtered.max`)
and multiply them there.
-/
@[to_additive "The \"unlifted\" version of addition in the colimit. To add two dependent pairs
`‚ü®j‚ÇÅ, x‚ü©` and `‚ü®j‚ÇÇ, y‚ü©`, we pass to a common successor of `j‚ÇÅ` and `j‚ÇÇ` (given by `is_filtered.max`)
and add them there."]
def colimit_mul_aux (x y : Œ£ j, F.obj j) : M :=
M.mk ‚ü®max' x.1 y.1, F.map (left_to_max x.1 y.1) x.2 * F.map (right_to_max x.1 y.1) y.2‚ü©

/-- Multiplication in the colimit is well-defined in the left argument. -/
@[to_additive "Addition in the colimit is well-defined in the left argument."]
lemma colimit_mul_aux_eq_of_rel_left {x x' y : Œ£ j, F.obj j}
  (hxx' : types.filtered_colimit.rel (F ‚ãô forget Mon) x x') :
  colimit_mul_aux x y = colimit_mul_aux x' y :=
begin
  cases x with j‚ÇÅ x, cases y with j‚ÇÇ y, cases x' with j‚ÇÉ x',
  obtain ‚ü®l, f, g, hfg‚ü© := hxx',
  simp at hfg,
  obtain ‚ü®s, Œ±, Œ≤, Œ≥, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© := tulip (left_to_max j‚ÇÅ j‚ÇÇ) (right_to_max j‚ÇÅ j‚ÇÇ)
    (right_to_max j‚ÇÉ j‚ÇÇ) (left_to_max j‚ÇÉ j‚ÇÇ) f g,
  apply M.mk_eq,
  use [s, Œ±, Œ≥],
  dsimp,
  simp_rw [monoid_hom.map_mul, ‚Üê comp_apply, ‚Üê F.map_comp, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, F.map_comp, comp_apply, hfg]
end

/-- Multiplication in the colimit is well-defined in the right argument. -/
@[to_additive "Addition in the colimit is well-defined in the right argument."]
lemma colimit_mul_aux_eq_of_rel_right {x y y' : Œ£ j, F.obj j}
  (hyy' : types.filtered_colimit.rel (F ‚ãô forget Mon) y y') :
  colimit_mul_aux x y = colimit_mul_aux x y' :=
begin
  cases y with j‚ÇÅ y, cases x with j‚ÇÇ x, cases y' with j‚ÇÉ y',
  obtain ‚ü®l, f, g, hfg‚ü© := hyy',
  simp at hfg,
  obtain ‚ü®s, Œ±, Œ≤, Œ≥, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü© := tulip (right_to_max j‚ÇÇ j‚ÇÅ) (left_to_max j‚ÇÇ j‚ÇÅ)
    (left_to_max j‚ÇÇ j‚ÇÉ) (right_to_max j‚ÇÇ j‚ÇÉ) f g,
  apply M.mk_eq,
  use [s, Œ±, Œ≥],
  dsimp,
  simp_rw [monoid_hom.map_mul, ‚Üê comp_apply, ‚Üê F.map_comp, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, F.map_comp, comp_apply, hfg]
end

/-- Multiplication in the colimit. See also `colimit_mul_aux`. -/
@[to_additive "Addition in the colimit. See also `colimit_add_aux`."]
instance colimit_has_mul : has_mul M :=
{ mul := Œª x y, begin
    refine quot.lift‚ÇÇ (colimit_mul_aux F) _ _ x y,
  { intros x y y' h,
    apply colimit_mul_aux_eq_of_rel_right,
    apply types.filtered_colimit.rel_of_quot_rel,
    exact h },
  { intros x x' y h,
    apply colimit_mul_aux_eq_of_rel_left,
    apply types.filtered_colimit.rel_of_quot_rel,
    exact h },
  end }

/--
Multiplication in the colimit is independent of the chosen "maximum" in the filtered category.
In particular, this lemma allows us to "unfold" the definition of the multiplication of `x` and `y`,
using a custom object `k` and morphisms `f : x.1 ‚ü∂ k` and `g : y.1 ‚ü∂ k`.
-/
@[to_additive "Addition in the colimit is independent of the chosen \"maximum\" in the filtered
category. In particular, this lemma allows us to \"unfold\" the definition of the addition of `x`
and `y`, using a custom object `k` and morphisms `f : x.1 ‚ü∂ k` and `g : y.1 ‚ü∂ k`."]
lemma colimit_mul_mk_eq (x y : Œ£ j, F.obj j) (k : J) (f : x.1 ‚ü∂ k) (g : y.1 ‚ü∂ k) :
  (M.mk x) * (M.mk y) = M.mk ‚ü®k, F.map f x.2 * F.map g y.2‚ü© :=
begin
  cases x with j‚ÇÅ x, cases y with j‚ÇÇ y,
  obtain ‚ü®s, Œ±, Œ≤, h‚ÇÅ, h‚ÇÇ‚ü© := bowtie (left_to_max j‚ÇÅ j‚ÇÇ) f (right_to_max j‚ÇÅ j‚ÇÇ) g,
  apply M.mk_eq,
  use [s, Œ±, Œ≤],
  dsimp,
  simp_rw [monoid_hom.map_mul, ‚Üê comp_apply, ‚Üê F.map_comp, h‚ÇÅ, h‚ÇÇ],
end

@[to_additive]
instance colimit_monoid : monoid M :=
{ one_mul := Œª x, begin
    apply quot.induction_on x, clear x, intro x, cases x with j x,
    rw [colimit_one_eq F j, colimit_mul_mk_eq F ‚ü®j, 1‚ü© ‚ü®j, x‚ü© j (ùüô j) (ùüô j),
      monoid_hom.map_one, one_mul, F.map_id, id_apply],
  end,
  mul_one := Œª x, begin
    apply quot.induction_on x, clear x, intro x, cases x with j x,
    rw [colimit_one_eq F j, colimit_mul_mk_eq F ‚ü®j, x‚ü© ‚ü®j, 1‚ü© j (ùüô j) (ùüô j),
      monoid_hom.map_one, mul_one, F.map_id, id_apply],
  end,
  mul_assoc := Œª x y z, begin
    apply quot.induction_on‚ÇÉ x y z, clear x y z, intros x y z,
    cases x with j‚ÇÅ x, cases y with j‚ÇÇ y, cases z with j‚ÇÉ z,
    rw [colimit_mul_mk_eq F ‚ü®j‚ÇÅ, x‚ü© ‚ü®j‚ÇÇ, y‚ü© _ (first_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ) (second_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ),
      colimit_mul_mk_eq F ‚ü®max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ, _‚ü© ‚ü®j‚ÇÉ, z‚ü© _ (ùüô _) (third_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ),
      colimit_mul_mk_eq F ‚ü®j‚ÇÇ, y‚ü© ‚ü®j‚ÇÉ, z‚ü© _ (second_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ) (third_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ),
      colimit_mul_mk_eq F ‚ü®j‚ÇÅ, x‚ü© ‚ü®max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ, _‚ü© _ (first_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ) (ùüô _)],
    simp only [F.map_id, id_apply, mul_assoc],
  end,
  ..colimit_has_one,
  ..colimit_has_mul }

/-- The bundled monoid giving the filtered colimit of a diagram. -/
@[to_additive "The bundled additive monoid giving the filtered colimit of a diagram."]
def colimit : Mon := Mon.of M

/-- The monoid homomorphism from a given monoid in the diagram to the colimit monoid. -/
@[to_additive "The additive monoid homomorphism from a given additive monoid in the diagram to the
colimit additive monoid."]
def cocone_morphism (j : J) : F.obj j ‚ü∂ colimit :=
{ to_fun := (types.colimit_cocone (F ‚ãô forget Mon)).Œπ.app j,
  map_one' := (colimit_one_eq j).symm,
  map_mul' := Œª x y, begin
    convert (colimit_mul_mk_eq F ‚ü®j, x‚ü© ‚ü®j, y‚ü© j (ùüô j) (ùüô j)).symm,
    rw [F.map_id, id_apply, id_apply], refl,
  end }

@[simp, to_additive]
lemma cocone_naturality {j j' : J} (f : j ‚ü∂ j') :
  F.map f ‚â´ (cocone_morphism j') = cocone_morphism j :=
monoid_hom.coe_inj ((types.colimit_cocone (F ‚ãô forget Mon)).Œπ.naturality f)

/-- The cocone over the proposed colimit monoid. -/
@[to_additive "The cocone over the proposed colimit additive monoid."]
def colimit_cocone : cocone F :=
{ X := colimit,
  Œπ := { app := cocone_morphism } }.

/--
Given a cocone `t` of `F`, the induced monoid homomorphism from the colimit to the cocone point.
As a function, this is simply given by the induced map of the corresponding cocone in `Type`.
The only thing left to see is that it is a monoid homomorphism.
-/
@[to_additive "Given a cocone `t` of `F`, the induced additive monoid homomorphism from the colimit
to the cocone point. As a function, this is simply given by the induced map of the corresponding
cocone in `Type`. The only thing left to see is that it is an additive monoid homomorphism."]
def colimit_desc (t : cocone F) : colimit ‚ü∂ t.X :=
{ to_fun := (types.colimit_cocone_is_colimit (F ‚ãô forget Mon)).desc ((forget Mon).map_cocone t),
  map_one' := begin
    rw colimit_one_eq F is_filtered.nonempty.some,
    exact monoid_hom.map_one _,
  end,
  map_mul' := Œª x y, begin
    apply quot.induction_on‚ÇÇ x y, clear x y, intros x y,
    cases x with i x, cases y with j y,
    rw colimit_mul_mk_eq F ‚ü®i, x‚ü© ‚ü®j, y‚ü© (max' i j) (left_to_max i j) (right_to_max i j),
    dsimp [types.colimit_cocone_is_colimit],
    rw [monoid_hom.map_mul, t.w_apply, t.w_apply],
  end }

/-- The proposed colimit cocone is a colimit in `Mon`. -/
@[to_additive "The proposed colimit cocone is a colimit in `AddMon`."]
def colimit_cocone_is_colimit : is_colimit colimit_cocone :=
{ desc := colimit_desc,
  fac' := Œª t j, monoid_hom.coe_inj
    ((types.colimit_cocone_is_colimit (F ‚ãô forget Mon)).fac ((forget Mon).map_cocone t) j),
  uniq' := Œª t m h, monoid_hom.coe_inj $
    (types.colimit_cocone_is_colimit (F ‚ãô forget Mon)).uniq ((forget Mon).map_cocone t) m
      (Œª j, funext $ Œª x, monoid_hom.congr_fun (h j) x) }

@[to_additive]
instance forget_preserves_filtered_colimits : preserves_filtered_colimits (forget Mon.{u}) :=
{ preserves_filtered_colimits := Œª J _ _, by exactI
  { preserves_colimit := Œª F, preserves_colimit_of_preserves_colimit_cocone
      (colimit_cocone_is_colimit.{u u} F) (types.colimit_cocone_is_colimit (F ‚ãô forget Mon.{u})) } }

end

end Mon.filtered_colimits


namespace CommMon.filtered_colimits

open Mon.filtered_colimits (colimit_mul_mk_eq)

section

-- We use parameters here, mainly so we can have the abbreviation `M` below, without
-- passing around `F` all the time.
parameters {J : Type v} [small_category J] [is_filtered J] (F : J ‚•§ CommMon.{max v u})

/--
The colimit of `F ‚ãô forget‚ÇÇ CommMon Mon` in the category `Mon`.
In the following, we will show that this has the structure of a _commutative_ monoid.
-/
@[to_additive "The colimit of `F ‚ãô forget‚ÇÇ AddCommMon AddMon` in the category `AddMon`. In the
following, we will show that this has the structure of a _commutative_ additive monoid."]
abbreviation M : Mon := Mon.filtered_colimits.colimit (F ‚ãô forget‚ÇÇ CommMon Mon.{max v u})

@[to_additive]
instance colimit_comm_monoid : comm_monoid M :=
{ mul_comm := Œª x y, begin
    apply quot.induction_on‚ÇÇ x y, clear x y, intros x y,
    let k := max' x.1 y.1,
    let f := left_to_max x.1 y.1,
    let g := right_to_max x.1 y.1,
    rw [colimit_mul_mk_eq _ x y k f g, colimit_mul_mk_eq _ y x k g f],
    dsimp,
    rw mul_comm,
 end
  ..M.monoid }

/-- The bundled commutative monoid giving the filtered colimit of a diagram. -/
@[to_additive "The bundled additive commutative monoid giving the filtered colimit of a diagram."]
def colimit : CommMon := CommMon.of M

/-- The cocone over the proposed colimit commutative monoid. -/
@[to_additive "The cocone over the proposed colimit additive commutative monoid."]
def colimit_cocone : cocone F :=
{ X := colimit,
  Œπ := { ..(Mon.filtered_colimits.colimit_cocone (F ‚ãô forget‚ÇÇ CommMon Mon.{max v u})).Œπ } }

/-- The proposed colimit cocone is a colimit in `CommMon`. -/
@[to_additive "The proposed colimit cocone is a colimit in `AddCommMon`."]
def colimit_cocone_is_colimit : is_colimit colimit_cocone :=
{ desc := Œª t, Mon.filtered_colimits.colimit_desc (F ‚ãô forget‚ÇÇ CommMon Mon.{max v u})
    ((forget‚ÇÇ CommMon Mon.{max v u}).map_cocone t),
  fac' := Œª t j, monoid_hom.coe_inj $
    (types.colimit_cocone_is_colimit (F ‚ãô forget CommMon)).fac ((forget CommMon).map_cocone t) j,
  uniq' := Œª t m h, monoid_hom.coe_inj $
    (types.colimit_cocone_is_colimit (F ‚ãô forget CommMon)).uniq ((forget CommMon).map_cocone t) m
    ((Œª j, funext $ Œª x, monoid_hom.congr_fun (h j) x)) }

@[to_additive forget‚ÇÇ_AddMon_preserves_filtered_colimits]
instance forget‚ÇÇ_Mon_preserves_filtered_colimits :
  preserves_filtered_colimits (forget‚ÇÇ CommMon Mon.{u}) :=
{ preserves_filtered_colimits := Œª J _ _, by exactI
  { preserves_colimit := Œª F, preserves_colimit_of_preserves_colimit_cocone
      (colimit_cocone_is_colimit.{u u} F)
      (Mon.filtered_colimits.colimit_cocone_is_colimit (F ‚ãô forget‚ÇÇ CommMon Mon.{u})) } }

@[to_additive]
instance forget_preserves_filtered_colimits :
  preserves_filtered_colimits (forget CommMon.{u}) :=
limits.comp_preserves_filtered_colimits (forget‚ÇÇ CommMon Mon) (forget Mon)

end

end CommMon.filtered_colimits
