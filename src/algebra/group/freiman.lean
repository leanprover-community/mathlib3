/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import algebra.big_operators.multiset
import data.fun_like.basic

/-!
# Freiman homomorphisms

In this file, we define Freiman homomorphisms. A `n`-Freiman homomorphism on `A` is a function
`f : Œ± ‚Üí Œ≤` such that `f (x‚ÇÅ) * ... * f (x‚Çô) = f (y‚ÇÅ) * ... * f (y‚Çô)` for all
`x‚ÇÅ, ..., x‚Çô, y‚ÇÅ, ..., y‚Çô ‚àà A` such that `x‚ÇÅ * ... * x‚Çô = y‚ÇÅ * ... * y‚Çô`. In particular, any
`mul_hom` is a Freiman homomorphism.

They are of interest in additive combinatorics.

## Main declaration

* `freiman_hom`: Freiman homomorphism.
* `add_freiman_hom`: Additive Freiman homomorphism.

## Notation

* `A ‚Üí*[n] Œ≤`: Multiplicative `n`-Freiman homomorphism on `A`
* `A ‚Üí+[n] Œ≤`: Additive `n`-Freiman homomorphism on `A`

## Implementation notes

In the context of combinatorics, we are interested in Freiman homomorphisms over sets which are not
necessarily closed under addition/multiplication. This means we must parametrize them with a set in
an `add_monoid`/`monoid` instead of the `add_monoid`/`monoid` itself.

## References

[Yufei Zhao, *18.225: Graph Theory and Additive Combinatorics*](https://yufeizhao.com/gtac/)

## TODO

`monoid_hom.to_freiman_hom` could be relaxed to `mul_hom.to_freiman_hom` by proving
`(s.map f).prod = (t.map f).prod` directly by induction instead of going through `f s.prod`.

Define `n`-Freiman isomorphisms.

Affine maps induce Freiman homs. Concretely, provide the `add_freiman_hom_class (Œ± ‚Üí‚Çê[ùïú] Œ≤) A Œ≤ n`
instance.
-/

open multiset

variables {F Œ± Œ≤ Œ≥ Œ¥ G : Type*}

/-- An additive `n`-Freiman homomorphism is a map which preserves sums of `n` elements. -/
structure add_freiman_hom (A : set Œ±) (Œ≤ : Type*) [add_comm_monoid Œ±] [add_comm_monoid Œ≤] (n : ‚Ñï) :=
(to_fun : Œ± ‚Üí Œ≤)
(map_sum_eq_map_sum' {s t : multiset Œ±} (hsA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí x ‚àà A) (htA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà t ‚Üí x ‚àà A)
  (hs : s.card = n) (ht : t.card = n) (h : s.sum = t.sum) :
  (s.map to_fun).sum = (t.map to_fun).sum)

/-- A `n`-Freiman homomorphism on a set `A` is a map which preserves products of `n` elements. -/
@[to_additive add_freiman_hom]
structure freiman_hom (A : set Œ±) (Œ≤ : Type*) [comm_monoid Œ±] [comm_monoid Œ≤] (n : ‚Ñï) :=
(to_fun : Œ± ‚Üí Œ≤)
(map_prod_eq_map_prod' {s t : multiset Œ±} (hsA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí x ‚àà A) (htA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà t ‚Üí x ‚àà A)
  (hs : s.card = n) (ht : t.card = n) (h : s.prod = t.prod) :
  (s.map to_fun).prod = (t.map to_fun).prod)

notation A ` ‚Üí+[`:25 n:25 `] `:0 Œ≤:0 := add_freiman_hom A Œ≤ n
notation A ` ‚Üí*[`:25 n:25 `] `:0 Œ≤:0 := freiman_hom A Œ≤ n

/-- `add_freiman_hom_class F s Œ≤ n` states that `F` is a type of `n`-ary sums-preserving morphisms.
You should extend this class when you extend `add_freiman_hom`. -/
class add_freiman_hom_class (F : Type*) (A : out_param $ set Œ±) (Œ≤ : out_param $ Type*)
  [add_comm_monoid Œ±] [add_comm_monoid Œ≤] (n : ‚Ñï) [fun_like F Œ± (Œª _, Œ≤)] :=
(map_sum_eq_map_sum' (f : F) {s t : multiset Œ±} (hsA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí x ‚àà A)
  (htA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà t ‚Üí x ‚àà A) (hs : s.card = n) (ht : t.card = n) (h : s.sum = t.sum) :
  (s.map f).sum = (t.map f).sum)

/-- `freiman_hom_class F A Œ≤ n` states that `F` is a type of `n`-ary products-preserving morphisms.
You should extend this class when you extend `freiman_hom`. -/
@[to_additive add_freiman_hom_class]
class freiman_hom_class (F : Type*) (A : out_param $ set Œ±) (Œ≤ : out_param $ Type*) [comm_monoid Œ±]
  [comm_monoid Œ≤] (n : ‚Ñï) [fun_like F Œ± (Œª _, Œ≤)] :=
(map_prod_eq_map_prod' (f : F) {s t : multiset Œ±} (hsA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí x ‚àà A)
  (htA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà t ‚Üí x ‚àà A) (hs : s.card = n) (ht : t.card = n) (h : s.prod = t.prod) :
  (s.map f).prod = (t.map f).prod)

variables [fun_like F Œ± (Œª _, Œ≤)]

section comm_monoid
variables [comm_monoid Œ±] [comm_monoid Œ≤] [comm_monoid Œ≥] [comm_monoid Œ¥] [comm_group G] {A : set Œ±}
  {B : set Œ≤} {C : set Œ≥} {n : ‚Ñï}

@[to_additive]
lemma map_prod_eq_map_prod [freiman_hom_class F A Œ≤ n] (f : F) {s t : multiset Œ±}
  (hsA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí x ‚àà A) (htA : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà t ‚Üí x ‚àà A) (hs : s.card = n) (ht : t.card = n)
  (h : s.prod = t.prod) :
  (s.map f).prod = (t.map f).prod :=
freiman_hom_class.map_prod_eq_map_prod' f hsA htA hs ht h

namespace freiman_hom

@[to_additive]
instance fun_like : fun_like (A ‚Üí*[n] Œ≤) Œ± (Œª _, Œ≤) :=
{ coe := to_fun,
  coe_injective' := Œª f g h, by cases f; cases g; congr' }

@[to_additive]
instance freiman_hom_class : freiman_hom_class (A ‚Üí*[n] Œ≤) A Œ≤ n :=
{ map_prod_eq_map_prod' := map_prod_eq_map_prod' }

/-- Helper instance for when there's too many metavariables to apply
`fun_like.has_coe_to_fun` directly. -/
@[to_additive]
instance : has_coe_to_fun (A ‚Üí*[n] Œ≤) (Œª _, Œ± ‚Üí Œ≤) := ‚ü®to_fun‚ü©

initialize_simps_projections freiman_hom (to_fun ‚Üí apply)

@[simp, to_additive]
lemma to_fun_eq_coe (f : A ‚Üí*[n] Œ≤) : f.to_fun = f := rfl

@[ext, to_additive]
lemma ext ‚¶Éf g : A ‚Üí*[n] Œ≤‚¶Ñ (h : ‚àÄ x, f x = g x) : f = g := fun_like.ext f g h

@[simp, to_additive]
lemma coe_mk (f : Œ± ‚Üí Œ≤) (h : ‚àÄ s t : multiset Œ±, (‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí x ‚àà A) ‚Üí (‚àÄ ‚¶Éx‚¶Ñ, x ‚àà t ‚Üí x ‚àà A) ‚Üí
  s.card = n ‚Üí t.card = n ‚Üí s.prod = t.prod ‚Üí (s.map f).prod = (t.map f).prod) :
  ‚áë(mk f h) = f := rfl

@[simp, to_additive] lemma mk_coe (f : A ‚Üí*[n] Œ≤) (h) : mk f h = f := ext $ Œª _, rfl

/-- The identity map from a commutative monoid to itself. -/
@[to_additive "The identity map from an additive commutative monoid to itself.", simps]
protected def id (A : set Œ±) (n : ‚Ñï) : A ‚Üí*[n] Œ±  :=
{ to_fun := Œª x, x, map_prod_eq_map_prod' := Œª s t _ _ _ _ h, by rw [map_id', map_id', h] }

/-- Composition of Freiman homomorphisms as a Freiman homomorphism. -/
@[to_additive "Composition of additive Freiman homomorphisms as an additive Freiman homomorphism."]
protected def comp (f : B ‚Üí*[n] Œ≥) (g : A ‚Üí*[n] Œ≤) (hAB : A.maps_to g B) : A ‚Üí*[n] Œ≥ :=
{ to_fun := f ‚àò g,
  map_prod_eq_map_prod' := Œª s t hsA htA hs ht h, begin
    rw [‚Üêmap_map,
    map_prod_eq_map_prod f _ _ ((s.card_map _).trans hs) ((t.card_map _).trans ht)
      (map_prod_eq_map_prod g hsA htA hs ht h), map_map],
    { simpa using (Œª a h, hAB (hsA h)) },
    { simpa using (Œª a h, hAB (htA h)) }
  end }

@[simp, to_additive]
lemma coe_comp (f : B ‚Üí*[n] Œ≥) (g : A ‚Üí*[n] Œ≤) {hfg} : ‚áë(f.comp g hfg) = f ‚àò g := rfl

@[to_additive]
lemma comp_apply (f : B ‚Üí*[n] Œ≥) (g : A ‚Üí*[n] Œ≤) {hfg} (x : Œ±) : f.comp g hfg x = f (g x) := rfl

@[to_additive]
lemma comp_assoc (f : A ‚Üí*[n] Œ≤) (g : B ‚Üí*[n] Œ≥) (h : C ‚Üí*[n] Œ¥) {hf hhg hgf}
  {hh : A.maps_to (g.comp f hgf) C} :
  (h.comp g hhg).comp f hf = h.comp (g.comp f hgf) hh := rfl

@[to_additive]
lemma cancel_right {g‚ÇÅ g‚ÇÇ : B ‚Üí*[n] Œ≥} {f : A ‚Üí*[n] Œ≤} (hf : function.surjective f) {hg‚ÇÅ hg‚ÇÇ} :
  g‚ÇÅ.comp f hg‚ÇÅ = g‚ÇÇ.comp f hg‚ÇÇ ‚Üî g‚ÇÅ = g‚ÇÇ :=
‚ü®Œª h, ext $ hf.forall.2 $ fun_like.ext_iff.1 h, Œª h, h ‚ñ∏ rfl‚ü©

@[to_additive]
lemma cancel_right_on {g‚ÇÅ g‚ÇÇ : B ‚Üí*[n] Œ≥} {f : A ‚Üí*[n] Œ≤} (hf : A.surj_on f B) {hf'} :
  A.eq_on (g‚ÇÅ.comp f hf') (g‚ÇÇ.comp f hf') ‚Üî B.eq_on g‚ÇÅ g‚ÇÇ :=
hf.cancel_right hf'

@[to_additive]
lemma cancel_left_on {g : B ‚Üí*[n] Œ≥} {f‚ÇÅ f‚ÇÇ : A ‚Üí*[n] Œ≤} (hg : B.inj_on g) {hf‚ÇÅ hf‚ÇÇ} :
  A.eq_on (g.comp f‚ÇÅ hf‚ÇÅ) (g.comp f‚ÇÇ hf‚ÇÇ) ‚Üî A.eq_on f‚ÇÅ f‚ÇÇ :=
hg.cancel_left hf‚ÇÅ hf‚ÇÇ

@[simp, to_additive] lemma comp_id (f : A ‚Üí*[n] Œ≤) {hf} : f.comp (freiman_hom.id A n) hf = f :=
ext $ Œª x, rfl

@[simp, to_additive] lemma id_comp (f : A ‚Üí*[n] Œ≤) {hf} : (freiman_hom.id B n).comp f hf = f :=
ext $ Œª x, rfl

/-- `freiman_hom.const A n b` is the Freiman homomorphism sending everything to `b`. -/
@[to_additive "`add_freiman_hom.const n b` is the Freiman homomorphism sending everything to `b`."]
def const (A : set Œ±) (n : ‚Ñï) (b : Œ≤) : A ‚Üí*[n] Œ≤ :=
{ to_fun := Œª _, b,
  map_prod_eq_map_prod' := Œª s t _ _ hs ht _,
    by rw [multiset.map_const, multiset.map_const, prod_repeat, prod_repeat, hs, ht] }

@[simp, to_additive] lemma const_apply (n : ‚Ñï) (b : Œ≤) (x : Œ±) : const A n b x = b := rfl

@[simp, to_additive]
lemma const_comp (n : ‚Ñï) (c : Œ≥) (f : A ‚Üí*[n] Œ≤) {hf} : (const B n c).comp f hf = const A n c := rfl

/-- `1` is the Freiman homomorphism sending everything to `1`. -/
@[to_additive "`0` is the Freiman homomorphism sending everything to `0`."]
instance : has_one (A ‚Üí*[n] Œ≤) := ‚ü®const A n 1‚ü©

@[simp, to_additive] lemma one_apply (x : Œ±) : (1 : A ‚Üí*[n] Œ≤) x = 1 := rfl

@[simp, to_additive] lemma one_comp (f : A ‚Üí*[n] Œ≤) {hf} : (1 : B ‚Üí*[n] Œ≥).comp f hf = 1 := rfl

@[to_additive] instance : inhabited (A ‚Üí*[n] Œ≤) := ‚ü®1‚ü©

/-- `f * g` is the Freiman homomorphism  sends `x` to `f x * g x`. -/
@[to_additive "`f + g` is the Freiman homomorphism sending `x` to `f x + g x`."]
instance : has_mul (A ‚Üí*[n] Œ≤) :=
‚ü®Œª f g, { to_fun := Œª x, f x * g x,
  map_prod_eq_map_prod' := Œª s t hsA htA hs ht h,
    by rw [prod_map_mul, prod_map_mul, map_prod_eq_map_prod f hsA htA hs ht h,
           map_prod_eq_map_prod g hsA htA hs ht h] }‚ü©

@[simp, to_additive] lemma mul_apply (f g : A ‚Üí*[n] Œ≤) (x : Œ±) : (f * g) x = f x * g x := rfl

@[to_additive] lemma mul_comp (g‚ÇÅ g‚ÇÇ : B ‚Üí*[n] Œ≥) (f : A ‚Üí*[n] Œ≤) {hg hg‚ÇÅ hg‚ÇÇ} :
  (g‚ÇÅ * g‚ÇÇ).comp f hg = g‚ÇÅ.comp f hg‚ÇÅ * g‚ÇÇ.comp f hg‚ÇÇ := rfl

/-- If `f` is a Freiman homomorphism to a commutative group, then `f‚Åª¬π` is the Freiman homomorphism
sending `x` to `(f x)‚Åª¬π`. -/
@[to_additive]
instance : has_inv (A ‚Üí*[n] G) :=
‚ü®Œª f, { to_fun := Œª x, (f x)‚Åª¬π,
  map_prod_eq_map_prod' := Œª s t hsA htA hs ht h,
    by rw [prod_map_inv', prod_map_inv', map_prod_eq_map_prod f hsA htA hs ht h] }‚ü©

@[simp, to_additive] lemma inv_apply (f : A ‚Üí*[n] G) (x : Œ±) : f‚Åª¬π x = (f x)‚Åª¬π := rfl

@[simp, to_additive] lemma inv_comp (f : B ‚Üí*[n] G) (g : A ‚Üí*[n] Œ≤) {hf hf'} :
  f‚Åª¬π.comp g hf = (f.comp g hf')‚Åª¬π :=
ext $ Œª x, rfl

/-- If `f` and `g` are Freiman homomorphisms to a commutative group, then `f / g` is the Freiman
homomorphism sending `x` to `f x / g x`. -/
@[to_additive "If `f` and `g` are additive Freiman homomorphisms to an additive commutative group,
then `f - g` is the additive Freiman homomorphism sending `x` to `f x - g x`"]
instance : has_div (A ‚Üí*[n] G) :=
‚ü®Œª f g, { to_fun := Œª x, f x / g x,
  map_prod_eq_map_prod' := Œª s t hsA htA hs ht h,
    by rw [prod_map_div, prod_map_div, map_prod_eq_map_prod f hsA htA hs ht h,
           map_prod_eq_map_prod g hsA htA hs ht h] }‚ü©

@[simp, to_additive] lemma div_apply (f g : A ‚Üí*[n] G) (x : Œ±) : (f / g) x = f x / g x := rfl

@[simp, to_additive] lemma div_comp (f‚ÇÅ f‚ÇÇ : B ‚Üí*[n] G) (g : A ‚Üí*[n] Œ≤) {hf hf‚ÇÅ hf‚ÇÇ} :
  (f‚ÇÅ / f‚ÇÇ).comp g hf = f‚ÇÅ.comp g hf‚ÇÅ / f‚ÇÇ.comp g hf‚ÇÇ :=
ext $ Œª x, rfl

/-! ### Instances -/

/-- `A ‚Üí*[n] Œ≤` is a `comm_monoid`. -/
@[to_additive "`Œ± ‚Üí+[n] Œ≤` is an `add_comm_monoid`."]
instance : comm_monoid (A ‚Üí*[n] Œ≤) :=
{ mul := (*),
  mul_assoc := Œª a b c, by { ext, apply mul_assoc },
  one := 1,
  one_mul := Œª a, by { ext, apply one_mul },
  mul_one := Œª a, by { ext, apply mul_one },
  mul_comm :=  Œª a b, by { ext, apply mul_comm },
  npow := Œª m f,
  { to_fun := Œª x, f x ^ m,
    map_prod_eq_map_prod' := Œª s t hsA htA hs ht h,
      by rw [prod_map_pow, prod_map_pow, map_prod_eq_map_prod f hsA htA hs ht h] },
  npow_zero' := Œª f, by { ext x, exact pow_zero _ },
  npow_succ' := Œª n f, by { ext x, exact pow_succ _ _ } }

/-- If `Œ≤` is a commutative group, then `A ‚Üí*[n] Œ≤` is a commutative group too. -/
@[to_additive "If `Œ≤` is an additive commutative group, then `A ‚Üí*[n] Œ≤` is an additive commutative
group too."]
instance {Œ≤} [comm_group Œ≤] : comm_group (A ‚Üí*[n] Œ≤) :=
{ inv := has_inv.inv,
  div := has_div.div,
  div_eq_mul_inv := by { intros, ext, apply div_eq_mul_inv },
  mul_left_inv := by { intros, ext, apply mul_left_inv },
  zpow := Œª n f, { to_fun := Œª x, (f x) ^ n,
    map_prod_eq_map_prod' := Œª s t hsA htA hs ht h,
      by rw [prod_map_zpow, prod_map_zpow, map_prod_eq_map_prod f hsA htA hs ht h] },
  zpow_zero' := Œª f, by { ext x, exact zpow_zero _ },
  zpow_succ' := Œª n f, by { ext x, simp_rw [zpow_of_nat, pow_succ, mul_apply, coe_mk] },
  zpow_neg'  := Œª n f, by { ext x, simp_rw [zpow_neg_succ_of_nat, zpow_coe_nat], refl },
  ..freiman_hom.comm_monoid }

end freiman_hom

/-! ### Hom hierarchy -/

--TODO: change to `monoid_hom_class F A Œ≤ ‚Üí freiman_hom_class F A Œ≤ n` once `map_multiset_prod` is
-- generalized
/-- A monoid homomorphism is naturally a `freiman_hom` on its entire domain.

We can't leave the domain `A : set Œ±` of the `freiman_hom` a free variable, since it wouldn't be
inferrable. -/
@[to_additive]
instance monoid_hom.freiman_hom_class : freiman_hom_class (Œ± ‚Üí* Œ≤) set.univ Œ≤ n :=
{ map_prod_eq_map_prod' := Œª f s t _ _ _ _ h, by rw [‚Üêf.map_multiset_prod, h, f.map_multiset_prod] }

/-- A `monoid_hom` is naturally a `freiman_hom`. -/
@[to_additive add_monoid_hom.to_add_freiman_hom "An `add_monoid_hom` is naturally an
`add_freiman_hom`"]
def monoid_hom.to_freiman_hom (A : set Œ±) (n : ‚Ñï) (f : Œ± ‚Üí* Œ≤) : A ‚Üí*[n] Œ≤ :=
{ to_fun := f,
  map_prod_eq_map_prod' := Œª s t hsA htA, map_prod_eq_map_prod f
    (Œª _ _, set.mem_univ _) (Œª _ _, set.mem_univ _) }

@[simp, to_additive]
lemma monoid_hom.to_freiman_hom_coe (f : Œ± ‚Üí* Œ≤) : (f.to_freiman_hom A n : Œ± ‚Üí Œ≤) = f := rfl

@[to_additive]
lemma monoid_hom.to_freiman_hom_injective :
  function.injective (monoid_hom.to_freiman_hom A n : (Œ± ‚Üí* Œ≤) ‚Üí A ‚Üí*[n] Œ≤) :=
Œª f g h, monoid_hom.ext $ show _, from fun_like.ext_iff.mp h

end comm_monoid

section cancel_comm_monoid
variables [comm_monoid Œ±] [cancel_comm_monoid Œ≤] {A : set Œ±} {m n : ‚Ñï}

@[to_additive]
lemma map_prod_eq_map_prod_of_le [freiman_hom_class F A Œ≤ n] (f : F) {s t : multiset Œ±}
  (hsA : ‚àÄ x ‚àà s, x ‚àà A) (htA : ‚àÄ x ‚àà t, x ‚àà A) (hs : s.card = m)
  (ht : t.card = m) (hst : s.prod = t.prod) (h : m ‚â§ n) :
  (s.map f).prod = (t.map f).prod :=
begin
  obtain rfl | hm := m.eq_zero_or_pos,
  { rw card_eq_zero at hs ht,
    rw [hs, ht] },
  rw [‚Üêhs, card_pos_iff_exists_mem] at hm,
  obtain ‚ü®a, ha‚ü© := hm,
  suffices : ((s + repeat a (n - m)).map f).prod = ((t + repeat a (n - m)).map f).prod,
  { simp_rw [multiset.map_add, prod_add] at this,
    exact mul_right_cancel this },
  replace ha := hsA _ ha,
  refine map_prod_eq_map_prod f (Œª x hx, _) (Œª x hx, _) _ _ _,
  rotate 2, assumption, -- Can't infer `A` and `n` from the context, so do it manually.
  { rw mem_add at hx,
    refine hx.elim (hsA _) (Œª h, _),
    rwa eq_of_mem_repeat h },
  { rw mem_add at hx,
    refine hx.elim (htA _) (Œª h, _),
    rwa eq_of_mem_repeat h },
  { rw [card_add, hs, card_repeat, add_tsub_cancel_of_le h] },
  { rw [card_add, ht, card_repeat, add_tsub_cancel_of_le h] },
  { rw [prod_add, prod_add, hst] }
end

/-- `Œ± ‚Üí*[n] Œ≤` is naturally included in  `A ‚Üí*[m] Œ≤` for any `m ‚â§ n`. -/
@[to_additive add_freiman_hom.to_add_freiman_hom "`Œ± ‚Üí+[n] Œ≤` is naturally included in  `Œ± ‚Üí+[m] Œ≤`
for any `m ‚â§ n`"]
def freiman_hom.to_freiman_hom (h : m ‚â§ n) (f : A ‚Üí*[n] Œ≤) : A ‚Üí*[m] Œ≤ :=
{ to_fun := f,
  map_prod_eq_map_prod' := Œª s t hsA htA hs ht hst,
    map_prod_eq_map_prod_of_le f hsA htA hs ht hst h }

/-- A `n`-Freiman homomorphism is also a `m`-Freiman homomorphism for any `m ‚â§ n`. -/
@[to_additive add_freiman_hom.add_freiman_hom_class_of_le "An additive `n`-Freiman homomorphism is
also an additive `m`-Freiman homomorphism for any `m ‚â§ n`."]
def freiman_hom.freiman_hom_class_of_le [freiman_hom_class F A Œ≤ n] (h : m ‚â§ n) :
  freiman_hom_class F A Œ≤ m :=
{ map_prod_eq_map_prod' := Œª f s t hsA htA hs ht hst,
    map_prod_eq_map_prod_of_le f hsA htA hs ht hst h }

@[simp, to_additive add_freiman_hom.to_add_freiman_hom_coe]
lemma freiman_hom.to_freiman_hom_coe (h : m ‚â§ n) (f : A ‚Üí*[n] Œ≤) :
  (f.to_freiman_hom h : Œ± ‚Üí Œ≤) = f := rfl

@[to_additive]
lemma freiman_hom.to_freiman_hom_injective (h : m ‚â§ n) :
  function.injective (freiman_hom.to_freiman_hom h : (A ‚Üí*[n] Œ≤) ‚Üí A ‚Üí*[m] Œ≤) :=
Œª f g hfg, freiman_hom.ext $ by convert fun_like.ext_iff.1 hfg

end cancel_comm_monoid
