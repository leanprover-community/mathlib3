/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.homology
import algebra.homology.single
import category_theory.preadditive.additive_functor

/-!
# Homology is an additive functor

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

When `V` is preadditive, `homological_complex V c` is also preadditive,
and `homology_functor` is additive.

TODO: similarly for `R`-linear.
-/

universes v u

open_locale classical
noncomputable theory

open category_theory category_theory.category category_theory.limits homological_complex

variables {Œπ : Type*}
variables {V : Type u} [category.{v} V] [preadditive V]

variables {c : complex_shape Œπ} {C D E : homological_complex V c}
variables (f g : C ‚ü∂ D) (h k : D ‚ü∂ E) (i : Œπ)

namespace homological_complex

instance : has_zero (C ‚ü∂ D) := ‚ü®{ f := Œª i, 0 }‚ü©
instance : has_add (C ‚ü∂ D) := ‚ü®Œª f g, { f := Œª i, f.f i + g.f i, }‚ü©
instance : has_neg (C ‚ü∂ D) := ‚ü®Œª f, { f := Œª i, -(f.f i) }‚ü©
instance : has_sub (C ‚ü∂ D) := ‚ü®Œª f g, { f := Œª i, f.f i - g.f i, }‚ü©
instance has_nat_scalar : has_smul ‚Ñï (C ‚ü∂ D) := ‚ü®Œª n f,
  { f := Œª i, n ‚Ä¢ f.f i,
    comm' := Œª i j h, by simp [preadditive.nsmul_comp, preadditive.comp_nsmul] }‚ü©
instance has_int_scalar : has_smul ‚Ñ§ (C ‚ü∂ D) := ‚ü®Œª n f,
  { f := Œª i, n ‚Ä¢ f.f i,
    comm' := Œª i j h, by simp [preadditive.zsmul_comp, preadditive.comp_zsmul] }‚ü©

@[simp] lemma zero_f_apply (i : Œπ) : (0 : C ‚ü∂ D).f i = 0 := rfl
@[simp] lemma add_f_apply (f g : C ‚ü∂ D) (i : Œπ) : (f + g).f i = f.f i + g.f i := rfl
@[simp] lemma neg_f_apply (f : C ‚ü∂ D) (i : Œπ) : (-f).f i = -(f.f i) := rfl
@[simp] lemma sub_f_apply (f g : C ‚ü∂ D) (i : Œπ) : (f - g).f i = f.f i - g.f i := rfl
@[simp] lemma nsmul_f_apply (n : ‚Ñï) (f : C ‚ü∂ D) (i : Œπ) : (n ‚Ä¢ f).f i = n ‚Ä¢ f.f i := rfl
@[simp] lemma zsmul_f_apply (n : ‚Ñ§) (f : C ‚ü∂ D) (i : Œπ) : (n ‚Ä¢ f).f i = n ‚Ä¢ f.f i := rfl

instance : add_comm_group (C ‚ü∂ D) :=
function.injective.add_comm_group hom.f
  homological_complex.hom_f_injective (by tidy) (by tidy) (by tidy) (by tidy) (by tidy) (by tidy)

instance : preadditive (homological_complex V c) := {}

/-- The `i`-th component of a chain map, as an additive map from chain maps to morphisms. -/
@[simps]
def hom.f_add_monoid_hom {C‚ÇÅ C‚ÇÇ : homological_complex V c} (i : Œπ) :
  (C‚ÇÅ ‚ü∂ C‚ÇÇ) ‚Üí+ (C‚ÇÅ.X i ‚ü∂ C‚ÇÇ.X i) :=
add_monoid_hom.mk' (Œª f, hom.f f i) (Œª _ _, rfl)

end homological_complex

namespace homological_complex

instance eval_additive (i : Œπ) : (eval V c i).additive := {}

instance cycles_additive [has_equalizers V] : (cycles_functor V c i).additive := {}

variables [has_images V] [has_image_maps V]

instance boundaries_additive : (boundaries_functor V c i).additive := {}

variables [has_equalizers V] [has_cokernels V]

instance homology_additive : (homology_functor V c i).additive :=
{ map_add' := Œª C D f g, begin
    dsimp [homology_functor],
    ext,
    simp only [homology.œÄ_map, preadditive.comp_add, ‚Üêpreadditive.add_comp],
    congr,
    ext, simp,
  end }


end homological_complex

namespace category_theory

variables {W : Type*} [category W] [preadditive W]

/--
An additive functor induces a functor between homological complexes.
This is sometimes called the "prolongation".
-/
@[simps]
def functor.map_homological_complex (F : V ‚•§ W) [F.additive] (c : complex_shape Œπ) :
  homological_complex V c ‚•§ homological_complex W c :=
{ obj := Œª C,
  { X := Œª i, F.obj (C.X i),
    d := Œª i j, F.map (C.d i j),
    shape' := Œª i j w, by rw [C.shape _ _ w, F.map_zero],
    d_comp_d' := Œª i j k _ _, by rw [‚ÜêF.map_comp, C.d_comp_d, F.map_zero], },
  map := Œª C D f,
  { f := Œª i, F.map (f.f i),
    comm' := Œª i j h, by { dsimp,  rw [‚ÜêF.map_comp, ‚ÜêF.map_comp, f.comm], }, }, }.

variable (V)

/-- The functor on homological complexes induced by the identity functor is
isomorphic to the identity functor. -/
@[simps]
def functor.map_homological_complex_id_iso (c : complex_shape Œπ) :
  (ùü≠ V).map_homological_complex c ‚âÖ ùü≠ _ :=
nat_iso.of_components (Œª K, hom.iso_of_components (Œª i, iso.refl _) (by tidy)) (by tidy)

variable {V}

instance functor.map_homogical_complex_additive
  (F : V ‚•§ W) [F.additive] (c : complex_shape Œπ) : (F.map_homological_complex c).additive := {}

instance functor.map_homological_complex_reflects_iso
  (F : V ‚•§ W) [F.additive] [reflects_isomorphisms F] (c : complex_shape Œπ) :
  reflects_isomorphisms (F.map_homological_complex c) :=
‚ü®Œª X Y f, begin
  introI,
  haveI : ‚àÄ (n : Œπ), is_iso (F.map (f.f n)) := Œª n, is_iso.of_iso
    ((homological_complex.eval W c n).map_iso (as_iso ((F.map_homological_complex c).map f))),
  haveI := Œª n, is_iso_of_reflects_iso (f.f n) F,
  exact homological_complex.hom.is_iso_of_components f,
end‚ü©

/--
A natural transformation between functors induces a natural transformation
between those functors applied to homological complexes.
-/
@[simps]
def nat_trans.map_homological_complex {F G : V ‚•§ W} [F.additive] [G.additive]
  (Œ± : F ‚ü∂ G) (c : complex_shape Œπ) : F.map_homological_complex c ‚ü∂ G.map_homological_complex c :=
{ app := Œª C, { f := Œª i, Œ±.app _, }, }

@[simp] lemma nat_trans.map_homological_complex_id (c : complex_shape Œπ) (F : V ‚•§ W) [F.additive] :
  nat_trans.map_homological_complex (ùüô F) c = ùüô (F.map_homological_complex c) :=
by tidy

@[simp] lemma nat_trans.map_homological_complex_comp (c : complex_shape Œπ)
  {F G H : V ‚•§ W} [F.additive] [G.additive] [H.additive]
  (Œ± : F ‚ü∂ G) (Œ≤ : G ‚ü∂ H):
  nat_trans.map_homological_complex (Œ± ‚â´ Œ≤) c =
    nat_trans.map_homological_complex Œ± c ‚â´ nat_trans.map_homological_complex Œ≤ c :=
by tidy

@[simp, reassoc] lemma nat_trans.map_homological_complex_naturality {c : complex_shape Œπ}
  {F G : V ‚•§ W} [F.additive] [G.additive] (Œ± : F ‚ü∂ G) {C D : homological_complex V c} (f : C ‚ü∂ D) :
  (F.map_homological_complex c).map f ‚â´ (nat_trans.map_homological_complex Œ± c).app D =
    (nat_trans.map_homological_complex Œ± c).app C ‚â´ (G.map_homological_complex c).map f :=
by tidy

/--
A natural isomorphism between functors induces a natural isomorphism
between those functors applied to homological complexes.
-/
@[simps]
def nat_iso.map_homological_complex {F G : V ‚•§ W} [F.additive] [G.additive]
  (Œ± : F ‚âÖ G) (c : complex_shape Œπ) : F.map_homological_complex c ‚âÖ G.map_homological_complex c :=
{ hom := Œ±.hom.map_homological_complex c,
  inv := Œ±.inv.map_homological_complex c,
  hom_inv_id' := by simpa only [‚Üê nat_trans.map_homological_complex_comp, Œ±.hom_inv_id],
  inv_hom_id' := by simpa only [‚Üê nat_trans.map_homological_complex_comp, Œ±.inv_hom_id], }

/--
An equivalence of categories induces an equivalences between the respective categories
of homological complex.
-/
@[simps]
def equivalence.map_homological_complex (e : V ‚âå W) [e.functor.additive] (c : complex_shape Œπ):
  homological_complex V c ‚âå homological_complex W c :=
{ functor := e.functor.map_homological_complex c,
  inverse := e.inverse.map_homological_complex c,
  unit_iso := (functor.map_homological_complex_id_iso V c).symm ‚â™‚â´
    nat_iso.map_homological_complex e.unit_iso c,
  counit_iso := nat_iso.map_homological_complex e.counit_iso c ‚â™‚â´
    functor.map_homological_complex_id_iso W c, }

end category_theory

namespace chain_complex

variables {W : Type*} [category W] [preadditive W]
variables {Œ± : Type*} [add_right_cancel_semigroup Œ±] [has_one Œ±] [decidable_eq Œ±]

lemma map_chain_complex_of (F : V ‚•§ W) [F.additive] (X : Œ± ‚Üí V) (d : Œ† n, X (n+1) ‚ü∂ X n)
  (sq : ‚àÄ n, d (n+1) ‚â´ d n = 0) :
  (F.map_homological_complex _).obj (chain_complex.of X d sq) =
  chain_complex.of (Œª n, F.obj (X n))
    (Œª n, F.map (d n)) (Œª n, by rw [ ‚Üê F.map_comp, sq n, functor.map_zero]) :=
begin
  refine homological_complex.ext rfl _,
  rintro i j (rfl : j + 1 = i),
  simp only [category_theory.functor.map_homological_complex_obj_d, of_d,
    eq_to_hom_refl, comp_id, id_comp],
end

end chain_complex

variables [has_zero_object V] {W : Type*} [category W] [preadditive W] [has_zero_object W]

namespace homological_complex

local attribute [simp] eq_to_hom_map

/--
Turning an object into a complex supported at `j` then applying a functor is
the same as applying the functor then forming the complex.
-/
def single_map_homological_complex (F : V ‚•§ W) [F.additive] (c : complex_shape Œπ) (j : Œπ):
  single V c j ‚ãô F.map_homological_complex _ ‚âÖ F ‚ãô single W c j :=
nat_iso.of_components (Œª X,
{ hom := { f := Œª i, if h : i = j then
    eq_to_hom (by simp [h])
  else
    0, },
  inv := { f := Œª i, if h : i = j then
    eq_to_hom (by simp [h])
  else
    0, },
  hom_inv_id' := begin
    ext i,
    dsimp,
    split_ifs with h,
    { simp [h] },
    { rw [zero_comp, if_neg h],
      exact (zero_of_source_iso_zero _ F.map_zero_object).symm, },
  end,
  inv_hom_id' := begin
    ext i,
    dsimp,
    split_ifs with h,
    { simp [h] },
    { rw [zero_comp, if_neg h],
      simp, },
  end, })
  (Œª X Y f, begin
    ext i,
    dsimp,
    split_ifs with h; simp [h],
  end).

variables (F : V ‚•§ W) [functor.additive F] (c)

@[simp] lemma single_map_homological_complex_hom_app_self (j : Œπ) (X : V) :
  ((single_map_homological_complex F c j).hom.app X).f j = eq_to_hom (by simp) :=
by simp [single_map_homological_complex]
@[simp] lemma single_map_homological_complex_hom_app_ne
  {i j : Œπ} (h : i ‚â† j) (X : V) :
  ((single_map_homological_complex F c j).hom.app X).f i = 0 :=
by simp [single_map_homological_complex, h]
@[simp] lemma single_map_homological_complex_inv_app_self (j : Œπ) (X : V) :
  ((single_map_homological_complex F c j).inv.app X).f j = eq_to_hom (by simp) :=
by simp [single_map_homological_complex]
@[simp] lemma single_map_homological_complex_inv_app_ne
  {i j : Œπ} (h : i ‚â† j) (X : V):
  ((single_map_homological_complex F c j).inv.app X).f i = 0 :=
by simp [single_map_homological_complex, h]

end homological_complex

namespace chain_complex

/--
Turning an object into a chain complex supported at zero then applying a functor is
the same as applying the functor then forming the complex.
-/
def single‚ÇÄ_map_homological_complex (F : V ‚•§ W) [F.additive] :
  single‚ÇÄ V ‚ãô F.map_homological_complex _ ‚âÖ F ‚ãô single‚ÇÄ W :=
nat_iso.of_components (Œª X,
{ hom := { f := Œª i, match i with
    | 0 := ùüô _
    | (i+1) := F.map_zero_object.hom
    end, },
  inv := { f := Œª i, match i with
    | 0 := ùüô _
    | (i+1) := F.map_zero_object.inv
    end, },
  hom_inv_id' := begin
    ext (_|i),
    { unfold_aux, simp, },
    { unfold_aux,
      dsimp,
      simp only [comp_f, id_f, zero_comp],
      exact (zero_of_source_iso_zero _ F.map_zero_object).symm, }
  end,
  inv_hom_id' := by { ext (_|i); { unfold_aux, dsimp, simp, }, }, })
  (Œª X Y f, by { ext (_|i); { unfold_aux, dsimp, simp, }, }).

@[simp] lemma single‚ÇÄ_map_homological_complex_hom_app_zero (F : V ‚•§ W) [F.additive] (X : V) :
  ((single‚ÇÄ_map_homological_complex F).hom.app X).f 0 = ùüô _ := rfl
@[simp] lemma single‚ÇÄ_map_homological_complex_hom_app_succ
  (F : V ‚•§ W) [F.additive] (X : V) (n : ‚Ñï) :
  ((single‚ÇÄ_map_homological_complex F).hom.app X).f (n+1) = 0 := rfl
@[simp] lemma single‚ÇÄ_map_homological_complex_inv_app_zero (F : V ‚•§ W) [F.additive] (X : V) :
  ((single‚ÇÄ_map_homological_complex F).inv.app X).f 0 = ùüô _ := rfl
@[simp] lemma single‚ÇÄ_map_homological_complex_inv_app_succ
  (F : V ‚•§ W) [F.additive] (X : V) (n : ‚Ñï) :
  ((single‚ÇÄ_map_homological_complex F).inv.app X).f (n+1) = 0 := rfl

end chain_complex

namespace cochain_complex

/--
Turning an object into a cochain complex supported at zero then applying a functor is
the same as applying the functor then forming the cochain complex.
-/
def single‚ÇÄ_map_homological_complex (F : V ‚•§ W) [F.additive] :
  single‚ÇÄ V ‚ãô F.map_homological_complex _ ‚âÖ F ‚ãô single‚ÇÄ W :=
nat_iso.of_components (Œª X,
{ hom := { f := Œª i, match i with
    | 0 := ùüô _
    | (i+1) := F.map_zero_object.hom
    end, },
  inv := { f := Œª i, match i with
    | 0 := ùüô _
    | (i+1) := F.map_zero_object.inv
    end, },
  hom_inv_id' := begin
    ext (_|i),
    { unfold_aux, simp, },
    { unfold_aux,
      dsimp,
      simp only [comp_f, id_f, zero_comp],
      exact (zero_of_source_iso_zero _ F.map_zero_object).symm, }
  end,
  inv_hom_id' := by { ext (_|i); { unfold_aux, dsimp, simp, }, }, })
  (Œª X Y f, by { ext (_|i); { unfold_aux, dsimp, simp, }, }).

@[simp] lemma single‚ÇÄ_map_homological_complex_hom_app_zero (F : V ‚•§ W) [F.additive] (X : V) :
  ((single‚ÇÄ_map_homological_complex F).hom.app X).f 0 = ùüô _ := rfl
@[simp] lemma single‚ÇÄ_map_homological_complex_hom_app_succ
  (F : V ‚•§ W) [F.additive] (X : V) (n : ‚Ñï) :
  ((single‚ÇÄ_map_homological_complex F).hom.app X).f (n+1) = 0 := rfl
@[simp] lemma single‚ÇÄ_map_homological_complex_inv_app_zero (F : V ‚•§ W) [F.additive] (X : V) :
  ((single‚ÇÄ_map_homological_complex F).inv.app X).f 0 = ùüô _ := rfl
@[simp] lemma single‚ÇÄ_map_homological_complex_inv_app_succ
  (F : V ‚•§ W) [F.additive] (X : V) (n : ‚Ñï) :
  ((single‚ÇÄ_map_homological_complex F).inv.app X).f (n+1) = 0 := rfl

end cochain_complex
