/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.additive

/-!
# Chain homotopies

We define chain homotopies, and prove that homotopic chain maps induce the same map on homology.
-/

universes v u

open_locale classical
noncomputable theory

open category_theory category_theory.limits homological_complex

variables {Œπ : Type*}
variables {V : Type u} [category.{v} V] [has_zero_object V] [preadditive V]

variables {c : complex_shape Œπ} {C D E : homological_complex V c}
variables (f g : C ‚ü∂ D) (h k : D ‚ü∂ E) (i : Œπ)

section

/-- Auxiliary definition for `homotopy`. Use `homotopy.from_next` instead. -/
def from_next' (f : Œ† i j, C.X i ‚ü∂ D.X j) (i j : Œπ) : C.X_next i ‚ü∂ D.X j :=
match c.next i with
| none := 0
| some ‚ü®i',w‚ü© := (C.X_next_iso w).hom ‚â´ f i' j
end

lemma from_next'_eq (f : Œ† i j, C.X i ‚ü∂ D.X j) {i i' : Œπ} (w : c.rel i i') (j : Œπ) :
  from_next' f i j = (C.X_next_iso w).hom ‚â´ f i' j :=
begin
  dsimp [from_next'],
  rw c.next_eq_some w,
  refl,
end

@[simp] lemma from_next'_zero (i j : Œπ) : from_next' (Œª i j, (0 : C.X i ‚ü∂ D.X j)) i j = 0 :=
begin
  dsimp [from_next'],
  rcases c.next i with ‚ü®‚ü©|‚ü®‚ü®i', w‚ü©‚ü©;
  { dsimp [from_next'._match_1], simp, },
end

@[simp] lemma from_next'_add (f g : Œ† i j, C.X i ‚ü∂ D.X j) (i j : Œπ) :
  from_next' (Œª i j, f i j + g i j) i j = from_next' f i j + from_next' g i j :=
begin
  dsimp [from_next'],
  rcases c.next i with _|‚ü®i',w‚ü©,
  exact (zero_add _).symm,
  exact preadditive.comp_add _ _ _ _ _ _,
end

@[simp] lemma from_next'_neg (f : Œ† i j, C.X i ‚ü∂ D.X j) (i j : Œπ) :
  from_next' (Œª i j, -(f i j)) i j = - from_next' f i j :=
begin
  dsimp [from_next'],
  rcases c.next i with _|‚ü®i',w‚ü©,
  exact neg_zero.symm,
  exact preadditive.comp_neg _ _,
end

@[simp] lemma from_next'_comp_left (f : C ‚ü∂ D) (g : Œ† i j, D.X i ‚ü∂ E.X j) (i j : Œπ) :
  from_next' (Œª i j, f.f i ‚â´ g i j) i j = f.f_next i ‚â´ from_next' g i j :=
begin
  dsimp [from_next', hom.f_next],
  rcases c.next i with _|‚ü®i',w‚ü©,
  { exact comp_zero.symm, },
  { dsimp [from_next'._match_1, hom.f_next._match_1],
    simp, },
end

@[simp] lemma from_next'_comp_right (f : Œ† i j, C.X i ‚ü∂ D.X j) (g : D ‚ü∂ E) (i j : Œπ) :
  from_next' (Œª i j, f i j ‚â´ g.f j) i j = from_next' f i j ‚â´ g.f j :=
begin
  dsimp [from_next'],
  rcases c.next i with _|‚ü®i',w‚ü©,
  { exact zero_comp.symm, },
  { dsimp [from_next'._match_1, hom.f_next._match_1],
    simp, },
end

/-- Auxiliary definition for `homotopy`. Use `homotopy.to_prev` instead. -/
def to_prev' (f : Œ† i j, C.X i ‚ü∂ D.X j) (i j : Œπ) : C.X i ‚ü∂ D.X_prev j :=
match c.prev j with
| none := 0
| some ‚ü®j',w‚ü© := f i j' ‚â´ (D.X_prev_iso w).inv
end

lemma to_prev'_eq (f : Œ† i j, C.X i ‚ü∂ D.X j) (i : Œπ) {j j' : Œπ} (w : c.rel j' j) :
  to_prev' f i j = f i j' ‚â´ (D.X_prev_iso w).inv :=
begin
  dsimp [to_prev'],
  rw c.prev_eq_some w,
  refl,
end

@[simp] lemma to_prev'_zero (i j : Œπ) : to_prev' (Œª i j, (0 : C.X i ‚ü∂ D.X j)) i j = 0 :=
begin
  dsimp [to_prev'],
  rcases c.prev j with ‚ü®‚ü©|‚ü®‚ü®j', w‚ü©‚ü©;
  { dsimp [to_prev'._match_1], simp, },
end

@[simp] lemma to_prev'_add (f g : Œ† i j, C.X i ‚ü∂ D.X j) (i j : Œπ) :
  to_prev' (Œª i j, f i j + g i j) i j = to_prev' f i j + to_prev' g i j :=
begin
  dsimp [to_prev'],
  rcases c.prev j with _|‚ü®j',w‚ü©,
  exact (zero_add _).symm,
  exact preadditive.add_comp _ _ _ _ _ _,
end

@[simp] lemma to_prev'_neg (f : Œ† i j, C.X i ‚ü∂ D.X j) (i j : Œπ) :
  to_prev' (Œª i j, -(f i j)) i j = - to_prev' f i j :=
begin
  dsimp [to_prev'],
  rcases c.prev j with _|‚ü®j',w‚ü©,
  exact neg_zero.symm,
  exact preadditive.neg_comp _ _,
end

@[simp] lemma to_prev'_comp_left (f : C ‚ü∂ D) (g : Œ† i j, D.X i ‚ü∂ E.X j) (i j : Œπ) :
  to_prev' (Œª i j, f.f i ‚â´ g i j) i j = f.f i ‚â´ to_prev' g i j :=
begin
  dsimp [to_prev'],
  rcases c.prev j with _|‚ü®j',w‚ü©,
  { exact comp_zero.symm, },
  { dsimp [to_prev'._match_1, hom.f_prev._match_1],
    simp, },
end

@[simp] lemma to_prev'_comp_right (f : Œ† i j, C.X i ‚ü∂ D.X j) (g : D ‚ü∂ E) (i j : Œπ) :
  to_prev' (Œª i j, f i j ‚â´ g.f j) i j = to_prev' f i j ‚â´ g.f_prev j :=
begin
  dsimp [to_prev', hom.f_prev],
  rcases c.prev j with _|‚ü®j',w‚ü©,
  { exact zero_comp.symm, },
  { dsimp [to_prev'._match_1, hom.f_prev._match_1],
    simp, },
end

/--
A homotopy `h` between chain maps `f` and `g` consists of components `h i j : C.X i ‚ü∂ D.X i`
which are zero unless `c.rel j i`,
satisfying the homotopy condition.
-/
@[ext, nolint has_inhabited_instance]
structure homotopy (f g : C ‚ü∂ D) :=
(hom : Œ† i j, C.X i ‚ü∂ D.X j)
(zero' : ‚àÄ i j, ¬¨ c.rel j i ‚Üí hom i j = 0 . obviously)
(comm' : ‚àÄ i,
  f.f i = to_prev' hom i i ‚â´ D.d_to i + C.d_from i ‚â´ from_next' hom i i + g.f i . obviously')

variables {f g}
namespace homotopy

restate_axiom homotopy.zero'

/--
The component of a homotopy from `next i` to `j`.
-/
def from_next (h : homotopy f g) (i j : Œπ) : C.X_next i ‚ü∂ D.X j :=
from_next' h.hom i j

/--
The component of a homotopy from `i` to `prev j`.
-/
def to_prev (h : homotopy f g) (i j : Œπ) : C.X i ‚ü∂ D.X_prev j :=
to_prev' h.hom i j

lemma comm (h : homotopy f g) (i : Œπ) :
  f.f i = h.to_prev i i ‚â´ D.d_to i + C.d_from i ‚â´ h.from_next i i + g.f i :=
h.comm' i

/--
`f` is homotopic to `g` iff `f - g` is homotopic to `0`.
-/
def equiv_sub_zero : homotopy f g ‚âÉ homotopy (f - g) 0 :=
{ to_fun := Œª h,
  { hom := Œª i j, h.hom i j,
    zero' := Œª i j w, h.zero _ _ w,
    comm' := Œª i, begin simp [h.comm], refl, end, },
  inv_fun := Œª h,
  { hom := Œª i j, h.hom i j,
    zero' := Œª i j w, h.zero _ _ w,
    comm' := Œª i, begin
      have c := h.comm i,
      simp only [homological_complex.sub_f_apply, add_zero, homological_complex.zero_f_apply,
        sub_eq_iff_eq_add] at c,
      rw c,
      refl,
    end, },
  left_inv := by tidy,
  right_inv := by tidy, }

/-- Every chain map is homotopic to itself. -/
@[refl]
def refl (f : C ‚ü∂ D) : homotopy f f :=
{ hom := Œª i j, 0,
  zero' := Œª i j w, rfl,
  comm' := Œª i, by simp, }

/-- `f` is homotopic to `g` iff `g` is homotopic to `f`. -/
@[symm]
def symm {f g : C ‚ü∂ D} (h : homotopy f g) : homotopy g f :=
{ hom := Œª i j, -h.hom i j,
  zero' := Œª i j w, by rw [h.zero i j w, neg_zero],
  comm' := Œª i, begin
    simp only [preadditive.comp_neg, preadditive.neg_comp,
      from_next'_neg, to_prev'_neg, h.comm' i],
    abel,
  end, }

/-- homotopy is a transitive relation. -/
@[trans]
def trans {e f g : C ‚ü∂ D} (h : homotopy e f) (k : homotopy f g) : homotopy e g :=
{ hom := Œª i j, h.hom i j + k.hom i j,
  zero' := Œª i j w, by rw [h.zero i j w, k.zero i j w, zero_add],
  comm' := Œª i, begin
    simp only [h.comm' i, k.comm' i, from_next'_add, to_prev'_add,
      preadditive.add_comp, preadditive.comp_add],
    abel,
  end, }

/-- homotopy is closed under composition (on the right) -/
def comp_right {e f : C ‚ü∂ D} (h : homotopy e f) (g : D ‚ü∂ E) : homotopy (e ‚â´ g) (f ‚â´ g) :=
{ hom := Œª i j, h.hom i j ‚â´ g.f j,
  zero' := Œª i j w, by rw [h.zero i j w, zero_comp],
  comm' := Œª i, by simp [h.comm' i], }

/-- homotopy is closed under composition (on the left) -/
def comp_left {f g : D ‚ü∂ E} (h : homotopy f g) (e : C ‚ü∂ D) : homotopy (e ‚â´ f) (e ‚â´ g) :=
{ hom := Œª i j, e.f i ‚â´ h.hom i j,
  zero' := Œª i j w, by rw [h.zero i j w, comp_zero],
  comm' := Œª i, by simp [h.comm' i], }

/-- a variant of `homotopy.comp_right` useful for dealing with homotopy equivalences. -/
def comp_right_id {f : C ‚ü∂ C} (h : homotopy f (ùüô C)) (g : C ‚ü∂ D) : homotopy (f ‚â´ g) g :=
by { convert h.comp_right g, simp, }

/-- a variant of `homotopy.comp_left` useful for dealing with homotopy equivalences. -/
def comp_left_id {f : D ‚ü∂ D} (h : homotopy f (ùüô D)) (g : C ‚ü∂ D) : homotopy (g ‚â´ f) g :=
by { convert h.comp_left g, simp, }

/-!
`homotopy.mk_inductive` allows us to build a homotopy inductively,
so that as we construct each component, we have available the previous two components,
and the fact that they satisfy the homotopy condition.

To simplify the situation, we only construction homotopies of the form `homotopy e 0`.
`homotopy.equiv_sub_zero` can provide the general case.

Notice however, that this construction does not have particularly good definitional properties:
we have to insert `eq_to_hom` in several places.
Hopefully this is okay in most applications, where we only need to have the existence of some
homotopy.
-/
section mk_inductive

variables {P Q : chain_complex V ‚Ñï}

@[simp] lemma to_prev'_chain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) (i j : ‚Ñï) :
  to_prev' f i j = f i (j+1) ‚â´ (Q.X_prev_iso rfl).inv :=
begin
  dsimp [to_prev', X_prev_iso],
  simp only [chain_complex.prev],
  refl,
end

@[simp] lemma from_next'_succ_chain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) (i j : ‚Ñï) :
  from_next' f (i+1) j = (P.X_next_iso rfl).hom ‚â´ f i j :=
begin
  dsimp [from_next', X_next_iso],
  simp only [chain_complex.next_nat_succ],
  refl,
end

@[simp] lemma from_next'_zero_chain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) (j : ‚Ñï) :
  from_next' f 0 j = 0 :=
begin
  dsimp [from_next'],
  simp only [chain_complex.next_nat_zero],
  refl,
end

variables (e : P ‚ü∂ Q)
  (zero : P.X 0 ‚ü∂ Q.X 1)
  (comm_zero : e.f 0 = zero ‚â´ Q.d 1 0)
  (one : P.X 1 ‚ü∂ Q.X 2)
  (comm_one : e.f 1 = one ‚â´ Q.d 2 1 + P.d 1 0 ‚â´ zero)
  (succ : ‚àÄ (n : ‚Ñï)
    (p : Œ£' (f : P.X n ‚ü∂ Q.X (n+1)) (f' : P.X (n+1) ‚ü∂ Q.X (n+2)),
      e.f (n+1) = f' ‚â´ Q.d (n+2) (n+1) + P.d (n+1) n ‚â´ f),
    Œ£' f'' : P.X (n+2) ‚ü∂ Q.X (n+3), e.f (n+2) = f'' ‚â´ Q.d (n+3) (n+2) + P.d (n+2) (n+1) ‚â´ p.2.1)

include comm_zero comm_one

/--
An auxiliary construction for `mk_inductive`.

Here we build by induction a family of diagrams,
but don't require at the type level that these successive diagrams actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a homotopy)
in `mk_inductive`.

At this stage, we don't check the homotopy condition in degree 0,
because it "falls off the end", and is easier to treat using `X_next` and `X_prev`,
which we do in `mk_inductive_aux‚ÇÇ`.
-/
@[simp]
def mk_inductive_aux‚ÇÅ :
  Œ† n, Œ£' (f : P.X n ‚ü∂ Q.X (n+1)) (f' : P.X (n+1) ‚ü∂ Q.X (n+2)),
    e.f (n+1) = f' ‚â´ Q.d (n+2) (n+1) + P.d (n+1) n ‚â´ f
| 0 := ‚ü®zero, one, comm_one‚ü©
| 1 := ‚ü®one, (succ 0 ‚ü®zero, one, comm_one‚ü©).1, (succ 0 ‚ü®zero, one, comm_one‚ü©).2‚ü©
| (n+2) :=
  ‚ü®(mk_inductive_aux‚ÇÅ (n+1)).2.1,
    (succ (n+1) (mk_inductive_aux‚ÇÅ (n+1))).1,
    (succ (n+1) (mk_inductive_aux‚ÇÅ (n+1))).2‚ü©

/--
An auxiliary construction for `mk_inductive`.
-/
@[simp]
def mk_inductive_aux‚ÇÇ :
  Œ† n, Œ£' (f : P.X_next n ‚ü∂ Q.X n) (f' : P.X n ‚ü∂ Q.X_prev n), e.f n = f' ‚â´ Q.d_to n + P.d_from n ‚â´ f
| 0 := ‚ü®0, zero ‚â´ (Q.X_prev_iso rfl).inv, by simpa using comm_zero‚ü©
| (n+1) := let I := mk_inductive_aux‚ÇÅ e zero comm_zero one comm_one succ n in
  ‚ü®(P.X_next_iso rfl).hom ‚â´ I.1, I.2.1 ‚â´ (Q.X_prev_iso rfl).inv, by simpa using I.2.2‚ü©

lemma mk_inductive_aux‚ÇÉ (i : ‚Ñï) :
  (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.1 ‚â´ (Q.X_prev_iso rfl).hom
    = (P.X_next_iso rfl).inv ‚â´ (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ (i+1)).1 :=
by rcases i with (_|_|i); { dsimp, simp, }

/--
A constructor for a `homotopy e 0`, for `e` a chain map between `‚Ñï`-indexed chain complexes,
working by induction.

You need to provide the components of the homotopy in degrees 0 and 1,
show that these satisfy the homotopy condition,
and then give a construction of each component,
and the fact that it satisfies the homotopy condition,
using as an inductive hypothesis the data and homotopy condition for the previous two components.
-/
def mk_inductive : homotopy e 0 :=
{ hom := Œª i j, if h : i + 1 = j then
    (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.1 ‚â´ (Q.X_prev_iso h).hom
  else
    0,
  zero' := Œª i j w, by rwa dif_neg,
  comm' := Œª i, begin
    dsimp, simp only [add_zero],
    convert (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.2,
    { simp, },
    { cases i; simp [mk_inductive_aux‚ÇÉ], },
  end, }

end mk_inductive


end homotopy

structure homotopy_equiv (C D : homological_complex V c) :=
(hom : C ‚ü∂ D)
(inv : D ‚ü∂ C)
(homotopy_hom_inv_id : homotopy (hom ‚â´ inv) (ùüô C))
(homotopy_inv_hom_id : homotopy (inv ‚â´ hom) (ùüô D))

variables [has_equalizers V] [has_cokernels V] [has_images V] [has_image_maps V]

/--
Homotopic maps induce the same map on homology.
-/
theorem homology_map_eq_of_homotopy (h : homotopy f g) (i : Œπ) :
  (homology_functor V c i).map f = (homology_functor V c i).map g :=
begin
  dsimp [homology_functor],
  apply eq_of_sub_eq_zero,
  ext,
  dunfold cycles_map,
  simp only [comp_zero, preadditive.comp_sub, cokernel.œÄ_desc],
  simp_rw [h.comm i],
  simp only [add_zero, zero_comp, cycles_arrow_d_from_assoc, preadditive.comp_add],
  rw [‚Üêpreadditive.sub_comp],
  simp only [category_theory.subobject.factor_thru_add_sub_factor_thru_right],
  dsimp [cycles],
  erw [subobject.factor_thru_of_le (D.boundaries_le_cycles i)],
  { simp, },
  { rw [‚Üêcategory.assoc],
    apply image_subobject_factors_comp_self, },
end

/-- Homotopy equivalent complexes have isomorphic homologies. -/
def homology_obj_iso_of_homotopy_equiv (f : homotopy_equiv C D) (i : Œπ) :
  (homology_functor V c i).obj C ‚âÖ (homology_functor V c i).obj D :=
{ hom := (homology_functor V c i).map f.hom,
  inv := (homology_functor V c i).map f.inv,
  hom_inv_id' := begin
    rw [‚Üêfunctor.map_comp, homology_map_eq_of_homotopy f.homotopy_hom_inv_id,
      category_theory.functor.map_id],
  end,
  inv_hom_id' := begin
    rw [‚Üêfunctor.map_comp, homology_map_eq_of_homotopy f.homotopy_inv_hom_id,
      category_theory.functor.map_id],
  end, }

end

namespace category_theory

variables {W : Type*} [category W] [preadditive W] [has_zero_object W]

/-- An additive functor takes homotopies to homotopies. -/
@[simps]
def functor.map_homotopy (F : V ‚•§ W) [F.additive] {f g : C ‚ü∂ D} (h : homotopy f g) :
  homotopy ((F.map_homological_complex c).map f) ((F.map_homological_complex c).map g) :=
{ hom := Œª i j, F.map (h.hom i j),
  zero' := Œª i j w, by { rw [h.zero i j w, F.map_zero], },
  comm' := Œª i, begin
    dsimp,
    simp only [h.comm' i, add_left_inj, functor.map_add, functor.map_comp],
    -- We now embark on an unpleasant case bash,
    -- depending on whether there are `next` and `prev` for `i`.
    rcases h‚ÇÅ : c.prev i with _ | ‚ü®i‚ÇÅ, w‚ÇÅ‚ü©,
    { rw [d_to_eq_zero _ h‚ÇÅ, d_to_eq_zero _ h‚ÇÅ],
      simp only [comp_zero, zero_add, functor.map_zero],
      rcases h‚ÇÇ : c.next i with _ | ‚ü®i‚ÇÇ, w‚ÇÇ‚ü©,
      { rw [d_from_eq_zero _ h‚ÇÇ, d_from_eq_zero _ h‚ÇÇ],
        simp only [zero_comp, functor.map_zero], },
      { rw [d_from_eq _ w‚ÇÇ, d_from_eq _ w‚ÇÇ, from_next'_eq _ w‚ÇÇ, from_next'_eq _ w‚ÇÇ],
        simp only [category.assoc, functor.map_homological_complex_obj_d,
          ‚Üêfunctor.map_comp, iso.inv_hom_id_assoc], }, },
    { rw [d_to_eq _ w‚ÇÅ, d_to_eq _ w‚ÇÅ,
        to_prev'_eq _ _ w‚ÇÅ, to_prev'_eq _ _ w‚ÇÅ],
      simp only [functor.map_homological_complex_obj_d],
      rcases h‚ÇÇ : c.next i with _ | ‚ü®i‚ÇÇ, w‚ÇÇ‚ü©,
      { rw [d_from_eq_zero _ h‚ÇÇ, d_from_eq_zero _ h‚ÇÇ],
        simp only [add_zero, zero_comp, ‚Üêfunctor.map_comp, functor.map_zero, category.assoc,
          iso.inv_hom_id_assoc], },
      { rw [d_from_eq _ w‚ÇÇ, d_from_eq _ w‚ÇÇ, from_next'_eq _ w‚ÇÇ, from_next'_eq _ w‚ÇÇ],
        simp only [‚Üêfunctor.map_comp, category.assoc, iso.inv_hom_id_assoc,
          functor.map_homological_complex_obj_d], } }
  end, }

/-- An additive functor preserves homotopy equivalences. -/
@[simps]
def functor.map_homotopy_equiv (F : V ‚•§ W) [F.additive] (h : homotopy_equiv C D) :
  homotopy_equiv ((F.map_homological_complex c).obj C) ((F.map_homological_complex c).obj D) :=
{ hom := (F.map_homological_complex c).map h.hom,
  inv := (F.map_homological_complex c).map h.inv,
  homotopy_hom_inv_id := begin
    rw [‚Üê(F.map_homological_complex c).map_comp, ‚Üê(F.map_homological_complex c).map_id],
    exact F.map_homotopy h.homotopy_hom_inv_id,
  end,
  homotopy_inv_hom_id := begin
    rw [‚Üê(F.map_homological_complex c).map_comp, ‚Üê(F.map_homological_complex c).map_id],
    exact F.map_homotopy h.homotopy_inv_hom_id,
  end }

end category_theory
