/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.additive
import tactic.abel

/-!
# Chain homotopies

We define chain homotopies, and prove that homotopic chain maps induce the same map on homology.
-/

universes v u

open_locale classical
noncomputable theory

open category_theory category_theory.limits homological_complex

variables {Œπ : Type*}
variables {V : Type u} [category.{v} V] [preadditive V]

variables {c : complex_shape Œπ} {C D E : homological_complex V c}
variables (f g : C ‚ü∂ D) (h k : D ‚ü∂ E) (i : Œπ)

section 

/-- The subset of Œπ √ó Œπ consisting of those (i,j) such that c.rel j i) -/
def homotopy.set_of_cs (c : complex_shape Œπ) : set (Œπ √ó Œπ) := Œª (x : Œπ √ó Œπ), c.rel x.2 x.1

/-- A prehomotopy C D consists of morphisms C.X i ‚ü∂ C.X j when c.rel j i -/
abbreviation prehomotopy (C D : homological_complex V c) :=
Œ† (ij : homotopy.set_of_cs c), C.X ij.val.1 ‚ü∂ D.X ij.val.2

namespace homotopy

/-- The composition of `C.d i i' ‚â´ f ‚ü®‚ü®i',i‚ü©,_‚ü©` if there is some `i'` coming after `i`,
and `0` otherwise. -/
def d_next (i : Œπ) : prehomotopy C D ‚Üí+ (C.X i ‚ü∂ D.X i) :=
add_monoid_hom.mk' (Œª f, match c.next i with
| none := 0
| some ‚ü®i',w‚ü© := C.d i i' ‚â´ f ‚ü®‚ü®i',i‚ü©,w‚ü©
end)
begin
  intros f g,
  rcases c.next i with _|‚ü®i',w‚ü©,
  exact (zero_add _).symm,
  exact preadditive.comp_add _ _ _ _ _ _,
end

/-- `f ‚ü®‚ü®i',i‚ü©,_‚ü©` if `i'` comes after `i`, and 0 if there's no such `i'`.
Hopefully there won't be much need for this, except in `d_next_eq_d_from_from_next`
to see that `d_next` factors through `C.d_from i`. -/
def from_next [has_zero_object V] (i : Œπ) : prehomotopy C D ‚Üí+ (C.X_next i ‚ü∂ D.X i) :=
add_monoid_hom.mk' (Œª f, match c.next i with
| none := 0
| some ‚ü®i',w‚ü© := (C.X_next_iso w).hom ‚â´ f ‚ü®‚ü®i',i‚ü©,w‚ü©
end)
begin
  intros f g,
  rcases c.next i with _|‚ü®i',w‚ü©,
  exact (zero_add _).symm,
  exact preadditive.comp_add _ _ _ _ _ _,
end

lemma d_next_eq_d_from_from_next [has_zero_object V] (f : prehomotopy C D) (i : Œπ) :
  d_next i f = C.d_from i ‚â´ from_next i f :=
begin
  dsimp [d_next, from_next],
  rcases c.next i with ‚ü®‚ü©|‚ü®‚ü®i', w‚ü©‚ü©;
  { dsimp [d_next, from_next], simp },
end

lemma d_next_eq (f : prehomotopy C D) {i i' : Œπ} (w : c.rel i i') :
  d_next i f = C.d i i' ‚â´ f ‚ü®‚ü®i',i‚ü©,w‚ü© :=
begin
  dsimp [d_next],
  rw c.next_eq_some w,
  refl,
end

@[simp] lemma d_next_comp_left (f : C ‚ü∂ D) (g : prehomotopy D E) (i : Œπ) :
  d_next i (Œª ij, f.f ij.val.1 ‚â´ g ij) = f.f i ‚â´ d_next i g :=
begin
  dsimp [d_next],
  rcases c.next i with _|‚ü®i',w‚ü©,
  { exact comp_zero.symm, },
  { dsimp [d_next],
    simp, },
end

@[simp] lemma d_next_comp_right (f : prehomotopy C D) (g : D ‚ü∂ E) (i : Œπ) :
  d_next i (Œª ij, f ij ‚â´ g.f ij.val.2) = d_next i f ‚â´ g.f i :=
begin
  dsimp [d_next],
  rcases c.next i with _|‚ü®i',w‚ü©,
  { exact zero_comp.symm, },
  { dsimp [d_next],
    simp, },
end

/-- The composition of `f ‚ü®‚ü®j,j'‚ü©,_‚ü© ‚â´ D.d j' j` if there is some `j'` coming before `j`,
and `0` otherwise. -/
def prev_d (j : Œπ) : prehomotopy C D ‚Üí+ (C.X j ‚ü∂ D.X j) :=
add_monoid_hom.mk' (Œª f, match c.prev j with
| none := 0
| some ‚ü®j',w‚ü© := f ‚ü®‚ü®j,j'‚ü©,w‚ü© ‚â´ D.d j' j
end)
begin
  intros f g,
  rcases c.prev j with _|‚ü®j',w‚ü©,
  exact (zero_add _).symm,
  exact preadditive.add_comp _ _ _ _ _ _,
end

/-- `f ‚ü®‚ü®j,j‚ü©,_‚ü©'` if `j'` comes after `j`, and 0 if there's no such `j'`.
Hopefully there won't be much need for this, except in `d_next_eq_d_from_from_next`
to see that `d_next` factors through `C.d_from i`. -/
def to_prev [has_zero_object V] (j : Œπ) : prehomotopy C D ‚Üí+ (C.X j ‚ü∂ D.X_prev j) :=
add_monoid_hom.mk' (Œª f, match c.prev j with
| none := 0
| some ‚ü®j',w‚ü© := f ‚ü®‚ü®j,j'‚ü©,w‚ü© ‚â´ (D.X_prev_iso w).inv
end)
begin
  intros f g,
  rcases c.prev j with _|‚ü®j',w‚ü©,
  exact (zero_add _).symm,
  exact preadditive.add_comp _ _ _ _ _ _,
end

lemma prev_d_eq_to_prev_d_to [has_zero_object V] (f : prehomotopy C D) (j : Œπ) :
  prev_d j f = to_prev j f ‚â´ D.d_to j :=
begin
  dsimp [prev_d, to_prev],
  rcases c.prev j with ‚ü®‚ü©|‚ü®‚ü®j', w‚ü©‚ü©;
  { dsimp [prev_d, to_prev], simp },
end

lemma prev_d_eq (f : prehomotopy C D) {j j' : Œπ} (w : c.rel j' j) :
  prev_d j f = f ‚ü®‚ü®j,j'‚ü©,w‚ü© ‚â´ D.d j' j :=
begin
  dsimp [prev_d],
  rw c.prev_eq_some w,
  refl,
end

@[simp] lemma prev_d_comp_left (f : C ‚ü∂ D) (g : prehomotopy D E) (j : Œπ) :
  prev_d j (Œª ij, f.f ij.val.1 ‚â´ g ij) = f.f j ‚â´ prev_d j g :=
begin
  dsimp [prev_d],
  rcases c.prev j with _|‚ü®j',w‚ü©,
  { exact comp_zero.symm, },
  { dsimp [prev_d, hom.prev],
    simp, },
end

@[simp] lemma to_prev'_comp_right (f : prehomotopy C D) (g : D ‚ü∂ E) (j : Œπ) :
  prev_d j (Œª ij, f ij ‚â´ g.f ij.val.2) = prev_d j f ‚â´ g.f j :=
begin
  dsimp [prev_d],
  rcases c.prev j with _|‚ü®j',w‚ü©,
  { exact zero_comp.symm, },
  { dsimp [prev_d],
    simp, },
end

/-!
Null homotopic maps can be constructed using the formula `hd+dh`. We provide some
convenient simplification lemmas that give a degreewise description of `hd+dh`,
depending on whether we have two differentials going to and from a certain degree,
only one, or none.
-/

/-- The null homotopic map associated to a family `hom` of morphisms `C_i ‚ü∂ D_j`
when `c.rel j i`. This is the same datum as for the field `hom` in the structure
`homotopy`. -/
def null_homotopic_map (hom : prehomotopy C D) : C ‚ü∂ D :=
{ f      := Œª i, d_next i hom + prev_d i hom,
  comm'  := Œª i j hij,
  begin
    have eq1 : prev_d i hom ‚â´ D.d i j = 0,
    { rcases h : c.prev i with _|‚ü®i',w‚ü©,
      { dsimp [prev_d], rw h, erw zero_comp, },
      { rw [prev_d_eq hom w, category.assoc, D.d_comp_d' i' i j w hij, comp_zero], }, },
    have eq2 : C.d i j ‚â´ d_next j hom = 0,
    { rcases h : c.next j with _|‚ü®j',w‚ü©,
      { dsimp [d_next], rw h, erw comp_zero, },
      { rw [d_next_eq hom w, ‚Üê category.assoc, C.d_comp_d' i j j' hij w, zero_comp], }, },
    rw [d_next_eq hom hij, prev_d_eq hom hij, preadditive.comp_add, preadditive.add_comp,
      eq1, eq2, add_zero, zero_add, category.assoc], 
  end }

/-- make `null_homotopic_map` into an additive map of monoids -/
def add_monoid_hom_null_homotopic_map : prehomotopy C D ‚Üí+ (C ‚ü∂ D) :=
add_monoid_hom.mk' null_homotopic_map
begin
  intros hom‚ÇÅ hom‚ÇÇ,
  unfold null_homotopic_map,
  ext,
  dsimp,
  simp only [map_add, add_f_apply],
  abel, 
end

/-- Use this lemma when you need the additivity of `null_homotopic_map` -/
lemma additive_null_homotopic_map (hom : prehomotopy C D) :
   null_homotopic_map hom = add_monoid_hom_null_homotopic_map hom :=
by { dsimp [add_monoid_hom_null_homotopic_map], refl, }

/-- null homotopies can be postcomposed with a morphism of complexes,
and the corresponding null homotopic maps are computed by `null_homotopic_map_comp` -/
@[simp]
def prehomotopy_comp (hom : prehomotopy C D) (g : D ‚ü∂ E) : prehomotopy C E :=
Œª ij, hom ij ‚â´ g.f ij.val.2 

/-- null homotopies can be precomposed with a morphism of complexes,
and the corresponding null homotopic maps are computed by `comp_null_homotopic_map` -/
@[simp]
def comp_prehomotopy (g : C ‚ü∂ D) (hom : prehomotopy D E) : prehomotopy C E :=
Œª ij, g.f ij.val.1 ‚â´ hom ij

@[simp]
lemma null_homotopic_map_comp (hom : prehomotopy C D) (g : D ‚ü∂ E) :
  null_homotopic_map hom ‚â´ g = null_homotopic_map (prehomotopy_comp hom g) :=
begin
  ext,
  simp only [null_homotopic_map, prehomotopy_comp, d_next_comp_right, preadditive.add_comp,
    to_prev'_comp_right, comp_f],
end

@[simp]
lemma comp_null_homotopic_map (g : C ‚ü∂ D) (hom : prehomotopy D E)  :
   g ‚â´ null_homotopic_map hom = null_homotopic_map (comp_prehomotopy g hom) :=
begin
  ext,
  simp only [null_homotopic_map, d_next_comp_left, prev_d_comp_left, preadditive.comp_add,
    comp_prehomotopy, comp_f],
end

/-! This lemma and the following ones can be used in order to compute
the degreewise morphisms induced by the null homotopic maps constructed
with `null_homotopic_map` -/
@[simp]
lemma null_homotopic_map_f {k‚ÇÇ k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÇ‚ÇÅ : c.rel k‚ÇÇ k‚ÇÅ) (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hom : prehomotopy C D) :
  (null_homotopic_map hom).f k‚ÇÅ = C.d k‚ÇÅ k‚ÇÄ ‚â´ hom ‚ü®‚ü®k‚ÇÄ,k‚ÇÅ‚ü©,r‚ÇÅ‚ÇÄ‚ü© + hom ‚ü®‚ü®k‚ÇÅ,k‚ÇÇ‚ü©,r‚ÇÇ‚ÇÅ‚ü© ‚â´ D.d k‚ÇÇ k‚ÇÅ :=
by { dsimp [null_homotopic_map], rw [d_next_eq hom r‚ÇÅ‚ÇÄ, prev_d_eq hom r‚ÇÇ‚ÇÅ], }

@[simp]
lemma null_homotopic_map_f_of_not_rel_left {k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hk‚ÇÄ : ‚àÄ l : Œπ, ¬¨c.rel k‚ÇÄ l) (hom : prehomotopy C D) :
  (null_homotopic_map hom).f k‚ÇÄ = hom ‚ü®‚ü®k‚ÇÄ,k‚ÇÅ‚ü©,r‚ÇÅ‚ÇÄ‚ü© ‚â´ D.d k‚ÇÅ k‚ÇÄ :=
begin
  dsimp [null_homotopic_map],
  rw prev_d_eq hom r‚ÇÅ‚ÇÄ,
  rcases h : c.next k‚ÇÄ with _|‚ü®l,w‚ü©, swap, exfalso, exact hk‚ÇÄ l w,
  dsimp [d_next], rw h, erw zero_add,
end

@[simp]
lemma null_homotopic_map_f_of_not_rel_right {k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hk‚ÇÅ : ‚àÄ l : Œπ, ¬¨c.rel l k‚ÇÅ)
  (hom : prehomotopy C D) :
  (null_homotopic_map hom).f k‚ÇÅ = C.d k‚ÇÅ k‚ÇÄ ‚â´ hom ‚ü®‚ü®k‚ÇÄ,k‚ÇÅ‚ü©,r‚ÇÅ‚ÇÄ‚ü© :=
begin
  dsimp [null_homotopic_map],
  rw d_next_eq hom r‚ÇÅ‚ÇÄ,
  rcases h : c.prev k‚ÇÅ with _|‚ü®l,w‚ü©, swap, exfalso, exact hk‚ÇÅ l w,
  dsimp [prev_d], rw h, erw add_zero,
end

@[simp]
lemma null_homotopic_map_f_eq_zero {k‚ÇÄ : Œπ} 
  (hk‚ÇÄ : ‚àÄ l : Œπ, ¬¨c.rel k‚ÇÄ l) (hk‚ÇÄ' : ‚àÄ l : Œπ, ¬¨c.rel l k‚ÇÄ)
  (hom : prehomotopy C D) :
  (null_homotopic_map hom).f k‚ÇÄ = 0 :=
begin
  dsimp [null_homotopic_map],
  rcases h1 : c.next k‚ÇÄ with _|‚ü®l,w‚ü©, swap, exfalso, exact hk‚ÇÄ l w,
  rcases h2 : c.prev k‚ÇÄ with _|‚ü®l,w‚ü©, swap, exfalso, exact hk‚ÇÄ' l w,
  dsimp [d_next, prev_d],
  rw [h1, h2],
  erw zero_add,
  refl,
end

end homotopy

/--
A homotopy `h` between chain maps `f` and `g` consists of a prehomotopy `hom`
such the difference between `f` and `g` is the `null_homotopic_map`
attached to hom. -/
@[ext, nolint has_inhabited_instance]
structure homotopy (f g : C ‚ü∂ D) :=
(hom : prehomotopy C D)
(comm : f = homotopy.null_homotopic_map hom + g)

variables {f g}
namespace homotopy

lemma comm_ext (e : homotopy f g) (i : Œπ) :
  f.f i = (null_homotopic_map e.hom).f i + g.f i :=
begin
  have H := congr_arg (Œª œÜ, œÜ.f i : (C ‚ü∂ D) ‚Üí (C.X i ‚ü∂ D.X i)) e.comm,
  simp only [add_f_apply] at H,
  exact H,
end

/-- Tautological construction of the `homotopy` to zero for maps constructed by
`null_homotopic_map` -/
@[simps]
def null_homotopy (hom : prehomotopy C D) :
  homotopy (null_homotopic_map hom) 0 :=
{ hom := hom,
  comm := by simp only [add_zero], }

/--
`f` is homotopic to `g` iff `f - g` is homotopic to `0`.
-/
def equiv_sub_zero : homotopy f g ‚âÉ homotopy (f - g) 0 :=
{ to_fun := Œª h,
  { hom := h.hom,
    comm := by { simpa only [add_zero] using sub_eq_of_eq_add h.comm, }, },
  inv_fun := Œª h,
  { hom := h.hom,
    comm := by { simpa only [add_zero] using eq_add_of_sub_eq h.comm, }, },
  left_inv  := by { intro, dsimp, ext, refl, },
  right_inv := by { intro, dsimp, ext, refl, }, }

/-- Equal chain maps are homotopic. -/
@[simps]
def of_eq (h : f = g) : homotopy f g :=
{ hom := 0,
  comm := by { simpa only [additive_null_homotopic_map, zero_add, map_zero], }, }

/-- Every chain map is homotopic to itself. -/
@[simps, refl]
def refl (f : C ‚ü∂ D) : homotopy f f :=
of_eq (rfl : f = f)

/-- `f` is homotopic to `g` iff `g` is homotopic to `f`. -/
@[simps, symm]
def symm {f g : C ‚ü∂ D} (h : homotopy f g) : homotopy g f :=
{ hom := -h.hom,
  comm :=
  begin
    have H := h.comm,
    simp only [additive_null_homotopic_map, map_neg] at H ‚ä¢,
    exact eq_neg_add_of_add_eq (eq.symm H),
  end }

/-- homotopy is a transitive relation. -/
@[simps, trans]
def trans {e f g : C ‚ü∂ D} (h : homotopy e f) (k : homotopy f g) : homotopy e g :=
{ hom := h.hom + k.hom,
  comm :=
  begin
    have H := eq.trans h.comm (congr_arg (has_add.add _) k.comm),
    simpa only [additive_null_homotopic_map, map_add, add_assoc] using H,
  end }

/-- the sum of two homotopies is a homotopy between the sum of the respective morphisms. -/
@[simps]
def add {f‚ÇÅ g‚ÇÅ f‚ÇÇ g‚ÇÇ: C ‚ü∂ D}
  (h‚ÇÅ : homotopy f‚ÇÅ g‚ÇÅ) (h‚ÇÇ : homotopy f‚ÇÇ g‚ÇÇ) : homotopy (f‚ÇÅ+f‚ÇÇ) (g‚ÇÅ+g‚ÇÇ) :=
{ hom := h‚ÇÅ.hom + h‚ÇÇ.hom,
  comm := 
  begin
    have H1 := h‚ÇÅ.comm,
    have H2 := h‚ÇÇ.comm,
    simp only [additive_null_homotopic_map, map_add] at H1 H2 ‚ä¢,
    simp only [congr (congr_arg has_add.add H1) H2],
    abel,
  end }

/-- the difference of two homotopies is a homotopy between the differences
of the respective morphisms. -/
@[simps]
def sub {f‚ÇÅ g‚ÇÅ f‚ÇÇ g‚ÇÇ: C ‚ü∂ D}
  (h‚ÇÅ : homotopy f‚ÇÅ g‚ÇÅ) (h‚ÇÇ : homotopy f‚ÇÇ g‚ÇÇ) : homotopy (f‚ÇÅ-f‚ÇÇ) (g‚ÇÅ-g‚ÇÇ) :=
{ hom := h‚ÇÅ.hom - h‚ÇÇ.hom,
  comm := 
  begin
    have H1 := h‚ÇÅ.comm,
    have H2 := h‚ÇÇ.comm,
    simp only [additive_null_homotopic_map, map_sub] at H1 H2 ‚ä¢,
    simp only [congr (congr_arg has_sub.sub H1) H2],
    abel,
  end }

/-- homotopy is closed under composition (on the right) -/
@[simps]
def comp_right {e f : C ‚ü∂ D} (h : homotopy e f) (g : D ‚ü∂ E) : homotopy (e ‚â´ g) (f ‚â´ g) :=
{ hom := prehomotopy_comp h.hom g,
  comm :=
  begin
    simp only [‚Üê null_homotopic_map_comp, ‚Üê preadditive.add_comp],
    congr',
    exact h.comm,
  end}

/-- homotopy is closed under composition (on the left) -/
@[simps]
def comp_left {f g : D ‚ü∂ E} (h : homotopy f g) (e : C ‚ü∂ D) : homotopy (e ‚â´ f) (e ‚â´ g) :=
{ hom := comp_prehomotopy e h.hom,
  comm :=
  begin
    simp only [‚Üê comp_null_homotopic_map, ‚Üê preadditive.comp_add],
    congr',
    exact h.comm,
  end}

/-- homotopy is closed under composition -/
@[simps]
def comp {C‚ÇÅ C‚ÇÇ C‚ÇÉ : homological_complex V c} {f‚ÇÅ g‚ÇÅ : C‚ÇÅ ‚ü∂ C‚ÇÇ} {f‚ÇÇ g‚ÇÇ : C‚ÇÇ ‚ü∂ C‚ÇÉ}
  (h‚ÇÅ : homotopy f‚ÇÅ g‚ÇÅ) (h‚ÇÇ : homotopy f‚ÇÇ g‚ÇÇ) : homotopy (f‚ÇÅ ‚â´ f‚ÇÇ) (g‚ÇÅ ‚â´ g‚ÇÇ) :=
(h‚ÇÅ.comp_right _).trans (h‚ÇÇ.comp_left _)

/-- a variant of `homotopy.comp_right` useful for dealing with homotopy equivalences. -/
@[simps]
def comp_right_id {f : C ‚ü∂ C} (h : homotopy f (ùüô C)) (g : C ‚ü∂ D) : homotopy (f ‚â´ g) g :=
(h.comp_right g).trans (of_eq $ category.id_comp _)

/-- a variant of `homotopy.comp_left` useful for dealing with homotopy equivalences. -/
@[simps]
def comp_left_id {f : D ‚ü∂ D} (h : homotopy f (ùüô D)) (g : C ‚ü∂ D) : homotopy (g ‚â´ f) g :=
(h.comp_left g).trans (of_eq $ category.comp_id _)

/-!
`homotopy.mk_inductive` allows us to build a homotopy inductively,
so that as we construct each component, we have available the previous two components,
and the fact that they satisfy the homotopy condition.

To simplify the situation, we only construct homotopies of the form `homotopy e 0`.
`homotopy.equiv_sub_zero` can provide the general case.

Notice however, that this construction does not have particularly good definitional properties:
we have to insert `eq_to_hom` in several places.
Hopefully this is okay in most applications, where we only need to have the existence of some
homotopy.
-/
section mk_inductive

variables {P Q : chain_complex V ‚Ñï}

lemma cs_down_succ (j : ‚Ñï) : (complex_shape.down ‚Ñï).rel (j+1) j :=
by { have eq : j+1 = j+1 := rfl, assumption, }

lemma cs_down_0_not_rel_left (j : ‚Ñï) : ¬¨(complex_shape.down ‚Ñï).rel 0 j :=
begin
  intro h,
  dsimp at h,
  apply nat.not_succ_le_zero j,
  rw [show j.succ=j+1, by refl, h],
end

@[simp] lemma prev_d_chain_complex (f : prehomotopy P Q) (j : ‚Ñï) :
  prev_d j f = f ‚ü®‚ü®j,j+1‚ü©,cs_down_succ j‚ü© ‚â´ Q.d _ _ :=
begin
  dsimp [prev_d],
  simp only [chain_complex.prev],
  refl,
end

@[simp] lemma d_next_succ_chain_complex (f : prehomotopy P Q) (i : ‚Ñï) :
  d_next (i+1) f = P.d _ _ ‚â´ f ‚ü®‚ü®i,i+1‚ü©,cs_down_succ i‚ü© :=
begin
  dsimp [d_next],
  simp only [chain_complex.next_nat_succ],
  refl,
end

@[simp] lemma d_next_zero_chain_complex (f : prehomotopy P Q) :
  d_next 0 f = 0 :=
begin
  dsimp [d_next],
  simp only [chain_complex.next_nat_zero],
  refl,
end

variables (e : P ‚ü∂ Q)
  (zero : P.X 0 ‚ü∂ Q.X 1)
  (comm_zero : e.f 0 = zero ‚â´ Q.d 1 0)
  (one : P.X 1 ‚ü∂ Q.X 2)
  (comm_one : e.f 1 = P.d 1 0 ‚â´ zero + one ‚â´ Q.d 2 1)
  (succ : ‚àÄ (n : ‚Ñï)
    (p : Œ£' (f : P.X n ‚ü∂ Q.X (n+1)) (f' : P.X (n+1) ‚ü∂ Q.X (n+2)),
      e.f (n+1) = P.d (n+1) n ‚â´ f + f' ‚â´ Q.d (n+2) (n+1)),
    Œ£' f'' : P.X (n+2) ‚ü∂ Q.X (n+3), e.f (n+2) = P.d (n+2) (n+1) ‚â´ p.2.1 + f'' ‚â´ Q.d (n+3) (n+2))

include comm_one comm_zero

/--
An auxiliary construction for `mk_inductive`.

Here we build by induction a family of diagrams,
but don't require at the type level that these successive diagrams actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a homotopy)
in `mk_inductive`.

At this stage, we don't check the homotopy condition in degree 0,
because it "falls off the end", and is easier to treat using `X_next` and `X_prev`,
which we do in `mk_inductive_aux‚ÇÇ`.
-/
@[simp, nolint unused_arguments]
def mk_inductive_aux‚ÇÅ :
  Œ† n, Œ£' (f : P.X n ‚ü∂ Q.X (n+1)) (f' : P.X (n+1) ‚ü∂ Q.X (n+2)),
    e.f (n+1) = P.d (n+1) n ‚â´ f + f' ‚â´ Q.d (n+2) (n+1)
| 0 := ‚ü®zero, one, comm_one‚ü©
| 1 := ‚ü®one, (succ 0 ‚ü®zero, one, comm_one‚ü©).1, (succ 0 ‚ü®zero, one, comm_one‚ü©).2‚ü©
| (n+2) :=
  ‚ü®(mk_inductive_aux‚ÇÅ (n+1)).2.1,
    (succ (n+1) (mk_inductive_aux‚ÇÅ (n+1))).1,
    (succ (n+1) (mk_inductive_aux‚ÇÅ (n+1))).2‚ü©

section

variable [has_zero_object V]
/--
An auxiliary construction for `mk_inductive`.
-/
@[simp]
def mk_inductive_aux‚ÇÇ :
  Œ† n, Œ£' (f : P.X_next n ‚ü∂ Q.X n) (f' : P.X n ‚ü∂ Q.X_prev n), e.f n = P.d_from n ‚â´ f + f' ‚â´ Q.d_to n
| 0 := ‚ü®0, zero ‚â´ (Q.X_prev_iso rfl).inv, by simpa using comm_zero‚ü©
| (n+1) := let I := mk_inductive_aux‚ÇÅ e zero comm_zero one comm_one succ n in
  ‚ü®(P.X_next_iso rfl).hom ‚â´ I.1, I.2.1 ‚â´ (Q.X_prev_iso rfl).inv, by simpa using I.2.2‚ü©

lemma mk_inductive_aux‚ÇÉ (i : ‚Ñï) :
  (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.1 ‚â´ (Q.X_prev_iso rfl).hom
    = (P.X_next_iso rfl).inv ‚â´ (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ (i+1)).1 :=
by rcases i with (_|_|i); { dsimp, simp, }

/--
A constructor for a `homotopy e 0`, for `e` a chain map between `‚Ñï`-indexed chain complexes,
working by induction.

You need to provide the components of the homotopy in degrees 0 and 1,
show that these satisfy the homotopy condition,
and then give a construction of each component,
and the fact that it satisfies the homotopy condition,
using as an inductive hypothesis the data and homotopy condition for the previous two components.
-/
def mk_inductive : homotopy e 0 :=
{ hom := Œª ij, (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ ij.val.1).2.1 ‚â´
    (Q.X_prev_iso ij.property).hom,
  comm := begin
    ext i,
    dsimp, simp only [add_zero],
    convert (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.2,
    { rcases i with (_|_|_|i),
      { simp only [null_homotopic_map_f_of_not_rel_left
          (cs_down_succ 0) cs_down_0_not_rel_left],
        dsimp,
        simp only [zero_add, comp_zero],
        slice_rhs 2 3 { erw X_prev_iso_comp_d_to, },
        slice_lhs 2 3 { erw iso.inv_hom_id, },
        simp only [category.id_comp], },
      simp only [null_homotopic_map_f (cs_down_succ 1) (cs_down_succ 0)], rotate,
      simp only [null_homotopic_map_f (cs_down_succ 2) (cs_down_succ 1)], rotate,
      simp only [null_homotopic_map_f (cs_down_succ (i.succ.succ.succ))
        (cs_down_succ (i.succ.succ))],
      all_goals
      { dsimp,
        simp only [d_from_comp_X_next_iso_assoc, category.assoc],
        erw iso.inv_hom_id,
        simp only [X_prev_iso_comp_d_to, category.comp_id],
        simp only [add_right_inj],
        slice_lhs 2 3 { erw iso.inv_hom_id, },
        simp only [category.id_comp], }, },
  end }
end

end mk_inductive

end homotopy

/--
A homotopy equivalence between two chain complexes consists of a chain map each way,
and homotopies from the compositions to the identity chain maps.

Note that this contains data;
arguably it might be more useful for many applications if we truncated it to a Prop.
-/
structure homotopy_equiv (C D : homological_complex V c) :=
(hom : C ‚ü∂ D)
(inv : D ‚ü∂ C)
(homotopy_hom_inv_id : homotopy (hom ‚â´ inv) (ùüô C))
(homotopy_inv_hom_id : homotopy (inv ‚â´ hom) (ùüô D))

namespace homotopy_equiv

/-- Any complex is homotopy equivalent to itself. -/
@[refl] def refl (C : homological_complex V c) : homotopy_equiv C C :=
{ hom := ùüô C,
  inv := ùüô C,
  homotopy_hom_inv_id := by simp,
  homotopy_inv_hom_id := by simp, }

instance : inhabited (homotopy_equiv C C) := ‚ü®refl C‚ü©

/-- Being homotopy equivalent is a symmetric relation. -/
@[symm] def symm
  {C D : homological_complex V c} (f : homotopy_equiv C D) :
  homotopy_equiv D C :=
{ hom := f.inv,
  inv := f.hom,
  homotopy_hom_inv_id := f.homotopy_inv_hom_id,
  homotopy_inv_hom_id := f.homotopy_hom_inv_id, }

/-- Homotopy equivalence is a transitive relation. -/
@[trans] def trans
  {C D E : homological_complex V c} (f : homotopy_equiv C D) (g : homotopy_equiv D E) :
  homotopy_equiv C E :=
{ hom := f.hom ‚â´ g.hom,
  inv := g.inv ‚â´ f.inv,
  homotopy_hom_inv_id := by simpa using
    ((g.homotopy_hom_inv_id.comp_right_id f.inv).comp_left f.hom).trans f.homotopy_hom_inv_id,
  homotopy_inv_hom_id := by simpa using
    ((f.homotopy_inv_hom_id.comp_right_id g.hom).comp_left g.inv).trans g.homotopy_inv_hom_id, }

end homotopy_equiv

variables [has_equalizers V] [has_cokernels V] [has_images V] [has_image_maps V]

variable [has_zero_object V]

/--
Null homotopic maps induce the zero map on homology.
-/
theorem homology_map_eq_zero (hom : prehomotopy C D) (i : Œπ) :
  (homology_functor V c i).map (homotopy.null_homotopic_map hom) = 0 :=
begin
  dsimp [homology_functor, kernel_subobject_map, homotopy.null_homotopic_map],
  ext,
  simp only [homology.œÄ_map, comp_zero],
  dsimp [kernel_subobject_map],
  simp only [preadditive.comp_add, homotopy.d_next_eq_d_from_from_next,
    kernel_subobject_arrow_comp_assoc, zero_comp,zero_add],
  erw [subobject.factor_thru_of_le (D.boundaries_le_cycles i)],
  { simp only [comp_zero, image_to_kernel_as_boundaries_to_cycles,
    homology.condition, category.assoc], },
  { rw [homotopy.prev_d_eq_to_prev_d_to, ‚Üê category.assoc],
    apply image_subobject_factors_comp_self, },
end

/--
Homotopic maps induce the same map on homology.
-/
theorem homology_map_eq_of_homotopy (h : homotopy f g) (i : Œπ) :
  (homology_functor V c i).map f = (homology_functor V c i).map g :=
begin
  rw h.comm,
  simp only [add_left_eq_self, functor.map_add],
  exact homology_map_eq_zero h.hom i,
end

/-- Homotopy equivalent complexes have isomorphic homologies. -/
def homology_obj_iso_of_homotopy_equiv (f : homotopy_equiv C D) (i : Œπ) :
  (homology_functor V c i).obj C ‚âÖ (homology_functor V c i).obj D :=
{ hom := (homology_functor V c i).map f.hom,
  inv := (homology_functor V c i).map f.inv,
  hom_inv_id' := begin
    rw [‚Üêfunctor.map_comp, homology_map_eq_of_homotopy f.homotopy_hom_inv_id,
      category_theory.functor.map_id],
  end,
  inv_hom_id' := begin
    rw [‚Üêfunctor.map_comp, homology_map_eq_of_homotopy f.homotopy_inv_hom_id,
      category_theory.functor.map_id],
  end, }

end

namespace category_theory

variables {W : Type*} [category W] [preadditive W]

/-- An additive functor takes homotopies to homotopies. -/
@[simps]
def functor.map_homotopy (F : V ‚•§ W) [F.additive] {f g : C ‚ü∂ D} (h : homotopy f g) :
  homotopy ((F.map_homological_complex c).map f) ((F.map_homological_complex c).map g) :=
{ hom := Œª ij, F.map (h.hom ij),
  comm := begin
    ext i,
    have := homotopy.comm_ext h i,
    dsimp [homotopy.null_homotopic_map, homotopy.d_next, homotopy.prev_d] at *,
    rcases c.next i with _|‚ü®inext,wn‚ü©;
    rcases c.prev i with _|‚ü®iprev,wp‚ü©;
    dsimp [homotopy.d_next, homotopy.prev_d] at *;
    { intro h, simp only [h, functor.map_add, zero_add, functor.map_comp, functor.map_zero], },
  end, }

/-- An additive functor preserves homotopy equivalences. -/
@[simps]
def functor.map_homotopy_equiv (F : V ‚•§ W) [F.additive] (h : homotopy_equiv C D) :
  homotopy_equiv ((F.map_homological_complex c).obj C) ((F.map_homological_complex c).obj D) :=
{ hom := (F.map_homological_complex c).map h.hom,
  inv := (F.map_homological_complex c).map h.inv,
  homotopy_hom_inv_id := begin
    rw [‚Üê(F.map_homological_complex c).map_comp, ‚Üê(F.map_homological_complex c).map_id],
    exact F.map_homotopy h.homotopy_hom_inv_id,
  end,
  homotopy_inv_hom_id := begin
    rw [‚Üê(F.map_homological_complex c).map_comp, ‚Üê(F.map_homological_complex c).map_id],
    exact F.map_homotopy h.homotopy_inv_hom_id,
  end }

end category_theory
