/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.additive
import tactic.abel

/-!
# Chain homotopies

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define chain homotopies, and prove that homotopic chain maps induce the same map on homology.
-/

universes v u

open_locale classical
noncomputable theory

open category_theory category_theory.limits homological_complex

variables {Œπ : Type*}
variables {V : Type u} [category.{v} V] [preadditive V]

variables {c : complex_shape Œπ} {C D E : homological_complex V c}
variables (f g : C ‚ü∂ D) (h k : D ‚ü∂ E) (i : Œπ)

section

/-- The composition of `C.d i i' ‚â´ f i' i` if there is some `i'` coming after `i`,
and `0` otherwise. -/
def d_next (i : Œπ) : (Œ† i j, C.X i ‚ü∂ D.X j) ‚Üí+ (C.X i ‚ü∂ D.X i) :=
add_monoid_hom.mk' (Œª f, C.d i (c.next i) ‚â´ f (c.next i) i) $
Œª f g, preadditive.comp_add _ _ _ _ _ _

/-- `f i' i` if `i'` comes after `i`, and 0 if there's no such `i'`.
Hopefully there won't be much need for this, except in `d_next_eq_d_from_from_next`
to see that `d_next` factors through `C.d_from i`. -/
def from_next (i : Œπ) : (Œ† i j, C.X i ‚ü∂ D.X j) ‚Üí+ (C.X_next i ‚ü∂ D.X i) :=
add_monoid_hom.mk' (Œª f, f (c.next i) i) $ Œª f g, rfl

@[simp]
lemma d_next_eq_d_from_from_next (f : Œ† i j, C.X i ‚ü∂ D.X j) (i : Œπ) :
  d_next i f = C.d_from i ‚â´ from_next i f := rfl

lemma d_next_eq (f : Œ† i j, C.X i ‚ü∂ D.X j) {i i' : Œπ} (w : c.rel i i') :
  d_next i f = C.d i i' ‚â´ f i' i :=
by { obtain rfl := c.next_eq' w, refl }

@[simp] lemma d_next_comp_left (f : C ‚ü∂ D) (g : Œ† i j, D.X i ‚ü∂ E.X j) (i : Œπ) :
  d_next i (Œª i j, f.f i ‚â´ g i j) = f.f i ‚â´ d_next i g :=
(f.comm_assoc _ _ _).symm

@[simp] lemma d_next_comp_right (f : Œ† i j, C.X i ‚ü∂ D.X j) (g : D ‚ü∂ E) (i : Œπ) :
  d_next i (Œª i j, f i j ‚â´ g.f j) = d_next i f ‚â´ g.f i :=
(category.assoc _ _ _).symm

/-- The composition of `f j j' ‚â´ D.d j' j` if there is some `j'` coming before `j`,
and `0` otherwise. -/
def prev_d (j : Œπ) : (Œ† i j, C.X i ‚ü∂ D.X j) ‚Üí+ (C.X j ‚ü∂ D.X j) :=
add_monoid_hom.mk' (Œª f, f j (c.prev j) ‚â´ D.d (c.prev j) j) $
Œª f g, preadditive.add_comp _ _ _ _ _ _

/-- `f j j'` if `j'` comes after `j`, and 0 if there's no such `j'`.
Hopefully there won't be much need for this, except in `d_next_eq_d_from_from_next`
to see that `d_next` factors through `C.d_from i`. -/
def to_prev (j : Œπ) : (Œ† i j, C.X i ‚ü∂ D.X j) ‚Üí+ (C.X j ‚ü∂ D.X_prev j) :=
add_monoid_hom.mk' (Œª f, f j (c.prev j)) $ Œª f g, rfl

@[simp]
lemma prev_d_eq_to_prev_d_to (f : Œ† i j, C.X i ‚ü∂ D.X j) (j : Œπ) :
  prev_d j f = to_prev j f ‚â´ D.d_to j := rfl

lemma prev_d_eq (f : Œ† i j, C.X i ‚ü∂ D.X j) {j j' : Œπ} (w : c.rel j' j) :
  prev_d j f = f j j' ‚â´ D.d j' j :=
by { obtain rfl := c.prev_eq' w, refl }

@[simp] lemma prev_d_comp_left (f : C ‚ü∂ D) (g : Œ† i j, D.X i ‚ü∂ E.X j) (j : Œπ) :
  prev_d j (Œª i j, f.f i ‚â´ g i j) = f.f j ‚â´ prev_d j g :=
category.assoc _ _ _

@[simp] lemma prev_d_comp_right (f : Œ† i j, C.X i ‚ü∂ D.X j) (g : D ‚ü∂ E) (j : Œπ) :
  prev_d j (Œª i j, f i j ‚â´ g.f j) = prev_d j f ‚â´ g.f j :=
by { dsimp [prev_d], simp only [category.assoc, g.comm] }

lemma d_next_nat (C D : chain_complex V ‚Ñï) (i : ‚Ñï) (f : Œ† i j, C.X i ‚ü∂ D.X j) :
  d_next i f = C.d i (i-1) ‚â´ f (i-1) i :=
begin
  dsimp [d_next],
  cases i,
  { simp only [shape, chain_complex.next_nat_zero, complex_shape.down_rel,
      nat.one_ne_zero, not_false_iff, zero_comp], },
  { dsimp only [nat.succ_eq_add_one],
    have : (complex_shape.down ‚Ñï).next (i + 1) = i + 1 - 1,
    { rw chain_complex.next_nat_succ, refl },
    congr' 2, }
end

lemma prev_d_nat (C D : cochain_complex V ‚Ñï) (i : ‚Ñï) (f : Œ† i j, C.X i ‚ü∂ D.X j) :
  prev_d i f = f i (i-1) ‚â´ D.d (i-1) i :=
begin
  dsimp [prev_d],
  cases i,
  { simp only [shape, cochain_complex.prev_nat_zero, complex_shape.up_rel,
      nat.one_ne_zero, not_false_iff, comp_zero]},
  { dsimp only [nat.succ_eq_add_one],
    have : (complex_shape.up ‚Ñï).prev (i + 1) = i + 1 - 1,
    { rw cochain_complex.prev_nat_succ, refl },
    congr' 2, },
end

/--
A homotopy `h` between chain maps `f` and `g` consists of components `h i j : C.X i ‚ü∂ D.X j`
which are zero unless `c.rel j i`, satisfying the homotopy condition.
-/
@[ext, nolint has_nonempty_instance]
structure homotopy (f g : C ‚ü∂ D) :=
(hom : Œ† i j, C.X i ‚ü∂ D.X j)
(zero' : ‚àÄ i j, ¬¨ c.rel j i ‚Üí hom i j = 0 . obviously)
(comm : ‚àÄ i, f.f i = d_next i hom + prev_d i hom + g.f i . obviously')

variables {f g}
namespace homotopy

restate_axiom homotopy.zero'

/--
`f` is homotopic to `g` iff `f - g` is homotopic to `0`.
-/
def equiv_sub_zero : homotopy f g ‚âÉ homotopy (f - g) 0 :=
{ to_fun := Œª h,
  { hom := Œª i j, h.hom i j,
    zero' := Œª i j w, h.zero _ _ w,
    comm := Œª i, by simp [h.comm] },
  inv_fun := Œª h,
  { hom := Œª i j, h.hom i j,
    zero' := Œª i j w, h.zero _ _ w,
    comm := Œª i, by simpa [sub_eq_iff_eq_add] using h.comm i },
  left_inv := by tidy,
  right_inv := by tidy, }

/-- Equal chain maps are homotopic. -/
@[simps]
def of_eq (h : f = g) : homotopy f g :=
{ hom := 0,
  zero' := Œª _ _ _, rfl,
  comm := Œª _, by simp only [add_monoid_hom.map_zero, zero_add, h] }

/-- Every chain map is homotopic to itself. -/
@[simps, refl]
def refl (f : C ‚ü∂ D) : homotopy f f :=
of_eq (rfl : f = f)

/-- `f` is homotopic to `g` iff `g` is homotopic to `f`. -/
@[simps, symm]
def symm {f g : C ‚ü∂ D} (h : homotopy f g) : homotopy g f :=
{ hom := -h.hom,
  zero' := Œª i j w, by rw [pi.neg_apply, pi.neg_apply, h.zero i j w, neg_zero],
  comm := Œª i, by rw [add_monoid_hom.map_neg, add_monoid_hom.map_neg, h.comm, ‚Üê neg_add,
      ‚Üê add_assoc, neg_add_self, zero_add] }

/-- homotopy is a transitive relation. -/
@[simps, trans]
def trans {e f g : C ‚ü∂ D} (h : homotopy e f) (k : homotopy f g) : homotopy e g :=
{ hom := h.hom + k.hom,
  zero' := Œª i j w, by rw [pi.add_apply, pi.add_apply, h.zero i j w, k.zero i j w, zero_add],
  comm := Œª i, by { rw [add_monoid_hom.map_add, add_monoid_hom.map_add, h.comm, k.comm], abel }, }

/-- the sum of two homotopies is a homotopy between the sum of the respective morphisms. -/
@[simps]
def add {f‚ÇÅ g‚ÇÅ f‚ÇÇ g‚ÇÇ : C ‚ü∂ D}
  (h‚ÇÅ : homotopy f‚ÇÅ g‚ÇÅ) (h‚ÇÇ : homotopy f‚ÇÇ g‚ÇÇ) : homotopy (f‚ÇÅ+f‚ÇÇ) (g‚ÇÅ+g‚ÇÇ) :=
{ hom := h‚ÇÅ.hom + h‚ÇÇ.hom,
  zero' := Œª i j hij, by
    rw [pi.add_apply, pi.add_apply, h‚ÇÅ.zero' i j hij, h‚ÇÇ.zero' i j hij, add_zero],
  comm := Œª i, by
    { simp only [homological_complex.add_f_apply, h‚ÇÅ.comm, h‚ÇÇ.comm,
        add_monoid_hom.map_add],
      abel, }, }

/-- homotopy is closed under composition (on the right) -/
@[simps]
def comp_right {e f : C ‚ü∂ D} (h : homotopy e f) (g : D ‚ü∂ E) : homotopy (e ‚â´ g) (f ‚â´ g) :=
{ hom := Œª i j, h.hom i j ‚â´ g.f j,
  zero' := Œª i j w, by rw [h.zero i j w, zero_comp],
  comm := Œª i, by simp only [h.comm i, d_next_comp_right, preadditive.add_comp,
    prev_d_comp_right, comp_f], }

/-- homotopy is closed under composition (on the left) -/
@[simps]
def comp_left {f g : D ‚ü∂ E} (h : homotopy f g) (e : C ‚ü∂ D) : homotopy (e ‚â´ f) (e ‚â´ g) :=
{ hom := Œª i j, e.f i ‚â´ h.hom i j,
  zero' := Œª i j w, by rw [h.zero i j w, comp_zero],
  comm := Œª i, by simp only [h.comm i, d_next_comp_left, preadditive.comp_add,
    prev_d_comp_left, comp_f], }

/-- homotopy is closed under composition -/
@[simps]
def comp {C‚ÇÅ C‚ÇÇ C‚ÇÉ : homological_complex V c} {f‚ÇÅ g‚ÇÅ : C‚ÇÅ ‚ü∂ C‚ÇÇ} {f‚ÇÇ g‚ÇÇ : C‚ÇÇ ‚ü∂ C‚ÇÉ}
  (h‚ÇÅ : homotopy f‚ÇÅ g‚ÇÅ) (h‚ÇÇ : homotopy f‚ÇÇ g‚ÇÇ) : homotopy (f‚ÇÅ ‚â´ f‚ÇÇ) (g‚ÇÅ ‚â´ g‚ÇÇ) :=
(h‚ÇÅ.comp_right _).trans (h‚ÇÇ.comp_left _)

/-- a variant of `homotopy.comp_right` useful for dealing with homotopy equivalences. -/
@[simps]
def comp_right_id {f : C ‚ü∂ C} (h : homotopy f (ùüô C)) (g : C ‚ü∂ D) : homotopy (f ‚â´ g) g :=
(h.comp_right g).trans (of_eq $ category.id_comp _)

/-- a variant of `homotopy.comp_left` useful for dealing with homotopy equivalences. -/
@[simps]
def comp_left_id {f : D ‚ü∂ D} (h : homotopy f (ùüô D)) (g : C ‚ü∂ D) : homotopy (g ‚â´ f) g :=
(h.comp_left g).trans (of_eq $ category.comp_id _)

/-!
Null homotopic maps can be constructed using the formula `hd+dh`. We show that
these morphisms are homotopic to `0` and provide some convenient simplification
lemmas that give a degreewise description of `hd+dh`, depending on whether we have
two differentials going to and from a certain degree, only one, or none.
-/

/-- The null homotopic map associated to a family `hom` of morphisms `C_i ‚ü∂ D_j`.
This is the same datum as for the field `hom` in the structure `homotopy`. For
this definition, we do not need the field `zero` of that structure
as this definition uses only the maps `C_i ‚ü∂ C_j` when `c.rel j i`. -/
def null_homotopic_map (hom : Œ† i j, C.X i ‚ü∂ D.X j) : C ‚ü∂ D :=
{ f      := Œª i, d_next i hom + prev_d i hom,
  comm'  := Œª i j hij,
  begin
    have eq1 : prev_d i hom ‚â´ D.d i j = 0,
    { simp only [prev_d, add_monoid_hom.mk'_apply, category.assoc, d_comp_d, comp_zero], },
    have eq2 : C.d i j ‚â´ d_next j hom = 0,
    { simp only [d_next, add_monoid_hom.mk'_apply, d_comp_d_assoc, zero_comp], },
    rw [d_next_eq hom hij, prev_d_eq hom hij, preadditive.comp_add, preadditive.add_comp,
      eq1, eq2, add_zero, zero_add, category.assoc],
  end }

/-- Variant of `null_homotopic_map` where the input consists only of the
relevant maps `C_i ‚ü∂ D_j` such that `c.rel j i`. -/
def null_homotopic_map' (h : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) : C ‚ü∂ D :=
null_homotopic_map (Œª i j, dite (c.rel j i) (h i j) (Œª _, 0))

/-- Compatibility of `null_homotopic_map` with the postcomposition by a morphism
of complexes. -/
lemma null_homotopic_map_comp (hom : Œ† i j, C.X i ‚ü∂ D.X j) (g : D ‚ü∂ E) :
null_homotopic_map hom ‚â´ g = null_homotopic_map (Œª i j, hom i j ‚â´ g.f j) :=
begin
  ext n,
  dsimp [null_homotopic_map, from_next, to_prev, add_monoid_hom.mk'_apply],
  simp only [preadditive.add_comp, category.assoc, g.comm],
end

/-- Compatibility of `null_homotopic_map'` with the postcomposition by a morphism
of complexes. -/
lemma null_homotopic_map'_comp (hom : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) (g : D ‚ü∂ E) :
null_homotopic_map' hom ‚â´ g = null_homotopic_map' (Œª i j hij, hom i j hij ‚â´ g.f j) :=
begin
  ext n,
  erw null_homotopic_map_comp,
  congr',
  ext i j,
  split_ifs,
  { refl, },
  { rw zero_comp, },
end

/-- Compatibility of `null_homotopic_map` with the precomposition by a morphism
of complexes. -/
lemma comp_null_homotopic_map (f : C ‚ü∂ D) (hom : Œ† i j, D.X i ‚ü∂ E.X j) :
f ‚â´ null_homotopic_map hom = null_homotopic_map (Œª i j, f.f i ‚â´ hom i j) :=
begin
  ext n,
  dsimp [null_homotopic_map, from_next, to_prev, add_monoid_hom.mk'_apply],
  simp only [preadditive.comp_add, category.assoc, f.comm_assoc],
end

/-- Compatibility of `null_homotopic_map'` with the precomposition by a morphism
of complexes. -/
lemma comp_null_homotopic_map' (f : C ‚ü∂ D) (hom : Œ† i j, c.rel j i ‚Üí (D.X i ‚ü∂ E.X j)) :
f ‚â´ null_homotopic_map' hom = null_homotopic_map' (Œª i j hij, f.f i ‚â´ hom i j hij) :=
begin
  ext n,
  erw comp_null_homotopic_map,
  congr',
  ext i j,
  split_ifs,
  { refl, },
  { rw comp_zero, },
end

/-- Compatibility of `null_homotopic_map` with the application of additive functors -/
lemma map_null_homotopic_map {W : Type*} [category W] [preadditive W]
  (G : V ‚•§ W) [G.additive] (hom : Œ† i j, C.X i ‚ü∂ D.X j) :
  (G.map_homological_complex c).map (null_homotopic_map hom) =
  null_homotopic_map (Œª i j, G.map (hom i j)) :=
begin
  ext i,
  dsimp [null_homotopic_map, d_next, prev_d],
  simp only [G.map_comp, functor.map_add],
end

/-- Compatibility of `null_homotopic_map'` with the application of additive functors -/
lemma map_null_homotopic_map' {W : Type*} [category W] [preadditive W]
  (G : V ‚•§ W) [G.additive] (hom : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) :
  (G.map_homological_complex c).map (null_homotopic_map' hom) =
  null_homotopic_map' (Œª i j hij, G.map (hom i j hij)) :=
begin
  ext n,
  erw map_null_homotopic_map,
  congr',
  ext i j,
  split_ifs,
  { refl, },
  { rw G.map_zero, }
end

/-- Tautological construction of the `homotopy` to zero for maps constructed by
`null_homotopic_map`, at least when we have the `zero'` condition. -/
@[simps]
def null_homotopy (hom : Œ† i j, C.X i ‚ü∂ D.X j) (zero' : ‚àÄ i j, ¬¨ c.rel j i ‚Üí hom i j = 0) :
  homotopy (null_homotopic_map hom) 0 :=
{ hom := hom,
  zero' := zero',
  comm := by { intro i, rw [homological_complex.zero_f_apply, add_zero], refl, }, }

/-- Homotopy to zero for maps constructed with `null_homotopic_map'` -/
@[simps]
def null_homotopy' (h : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) :
  homotopy (null_homotopic_map' h) 0 :=
begin
  apply null_homotopy (Œª i j, dite (c.rel j i) (h i j) (Œª _, 0)),
  intros i j hij,
  dsimp,
  rw [dite_eq_right_iff],
  intro hij',
  exfalso,
  exact hij hij',
end

/-! This lemma and the following ones can be used in order to compute
the degreewise morphisms induced by the null homotopic maps constructed
with `null_homotopic_map` or `null_homotopic_map'` -/
@[simp]
lemma null_homotopic_map_f {k‚ÇÇ k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÇ‚ÇÅ : c.rel k‚ÇÇ k‚ÇÅ) (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hom : Œ† i j, C.X i ‚ü∂ D.X j) :
  (null_homotopic_map hom).f k‚ÇÅ = C.d k‚ÇÅ k‚ÇÄ ‚â´ hom k‚ÇÄ k‚ÇÅ + hom k‚ÇÅ k‚ÇÇ ‚â´ D.d k‚ÇÇ k‚ÇÅ :=
by { dsimp only [null_homotopic_map], rw [d_next_eq hom r‚ÇÅ‚ÇÄ, prev_d_eq hom r‚ÇÇ‚ÇÅ], }

@[simp]
lemma null_homotopic_map'_f {k‚ÇÇ k‚ÇÅ k‚ÇÄ  : Œπ} (r‚ÇÇ‚ÇÅ : c.rel k‚ÇÇ k‚ÇÅ) (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (h : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) :
  (null_homotopic_map' h).f k‚ÇÅ = C.d k‚ÇÅ k‚ÇÄ ‚â´ h k‚ÇÄ k‚ÇÅ r‚ÇÅ‚ÇÄ + h k‚ÇÅ k‚ÇÇ r‚ÇÇ‚ÇÅ ‚â´ D.d k‚ÇÇ k‚ÇÅ :=
begin
  simp only [‚Üê null_homotopic_map'],
  rw null_homotopic_map_f r‚ÇÇ‚ÇÅ r‚ÇÅ‚ÇÄ (Œª i j, dite (c.rel j i) (h i j) (Œª _, 0)),
  dsimp,
  split_ifs,
  refl,
end

@[simp]
lemma null_homotopic_map_f_of_not_rel_left {k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hk‚ÇÄ : ‚àÄ l : Œπ, ¬¨c.rel k‚ÇÄ l)
  (hom : Œ† i j, C.X i ‚ü∂ D.X j) :
  (null_homotopic_map hom).f k‚ÇÄ = hom k‚ÇÄ k‚ÇÅ ‚â´ D.d k‚ÇÅ k‚ÇÄ :=
begin
  dsimp only [null_homotopic_map],
  rw [prev_d_eq hom r‚ÇÅ‚ÇÄ, d_next, add_monoid_hom.mk'_apply, C.shape, zero_comp, zero_add],
  exact hk‚ÇÄ _
end

@[simp]
lemma null_homotopic_map'_f_of_not_rel_left {k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hk‚ÇÄ : ‚àÄ l : Œπ, ¬¨c.rel k‚ÇÄ l)
  (h : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) :
  (null_homotopic_map' h).f k‚ÇÄ = h k‚ÇÄ k‚ÇÅ r‚ÇÅ‚ÇÄ ‚â´ D.d k‚ÇÅ k‚ÇÄ :=
begin
  simp only [‚Üê null_homotopic_map'],
  rw null_homotopic_map_f_of_not_rel_left r‚ÇÅ‚ÇÄ hk‚ÇÄ (Œª i j, dite (c.rel j i) (h i j) (Œª _, 0)),
  dsimp,
  split_ifs,
  refl,
end

@[simp]
lemma null_homotopic_map_f_of_not_rel_right {k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hk‚ÇÅ : ‚àÄ l : Œπ, ¬¨c.rel l k‚ÇÅ)
  (hom : Œ† i j, C.X i ‚ü∂ D.X j) :
  (null_homotopic_map hom).f k‚ÇÅ = C.d k‚ÇÅ k‚ÇÄ ‚â´ hom k‚ÇÄ k‚ÇÅ :=
begin
  dsimp only [null_homotopic_map],
  rw [d_next_eq hom r‚ÇÅ‚ÇÄ, prev_d, add_monoid_hom.mk'_apply, D.shape, comp_zero, add_zero],
  exact hk‚ÇÅ _,
end

@[simp]
lemma null_homotopic_map'_f_of_not_rel_right {k‚ÇÅ k‚ÇÄ : Œπ} (r‚ÇÅ‚ÇÄ : c.rel k‚ÇÅ k‚ÇÄ)
  (hk‚ÇÅ : ‚àÄ l : Œπ, ¬¨c.rel l k‚ÇÅ)
  (h : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) :
  (null_homotopic_map' h).f k‚ÇÅ = C.d k‚ÇÅ k‚ÇÄ ‚â´ h k‚ÇÄ k‚ÇÅ r‚ÇÅ‚ÇÄ :=
begin
  simp only [‚Üê null_homotopic_map'],
  rw null_homotopic_map_f_of_not_rel_right r‚ÇÅ‚ÇÄ hk‚ÇÅ (Œª i j, dite (c.rel j i) (h i j) (Œª _, 0)),
  dsimp,
  split_ifs,
  refl,
end

@[simp]
lemma null_homotopic_map_f_eq_zero {k‚ÇÄ : Œπ}
  (hk‚ÇÄ : ‚àÄ l : Œπ, ¬¨c.rel k‚ÇÄ l) (hk‚ÇÄ' : ‚àÄ l : Œπ, ¬¨c.rel l k‚ÇÄ)
  (hom : Œ† i j, C.X i ‚ü∂ D.X j) :
  (null_homotopic_map hom).f k‚ÇÄ = 0 :=
begin
  dsimp [null_homotopic_map, d_next, prev_d],
  rw [C.shape, D.shape, zero_comp, comp_zero, add_zero]; apply_assumption,
end

@[simp]
lemma null_homotopic_map'_f_eq_zero {k‚ÇÄ : Œπ}
  (hk‚ÇÄ : ‚àÄ l : Œπ, ¬¨c.rel k‚ÇÄ l) (hk‚ÇÄ' : ‚àÄ l : Œπ, ¬¨c.rel l k‚ÇÄ)
  (h : Œ† i j, c.rel j i ‚Üí (C.X i ‚ü∂ D.X j)) :
  (null_homotopic_map' h).f k‚ÇÄ = 0 :=
begin
  simp only [‚Üê null_homotopic_map'],
  exact null_homotopic_map_f_eq_zero hk‚ÇÄ hk‚ÇÄ'
    (Œª i j, dite (c.rel j i) (h i j) (Œª _, 0)),
end

/-!
`homotopy.mk_inductive` allows us to build a homotopy of chain complexes inductively,
so that as we construct each component, we have available the previous two components,
and the fact that they satisfy the homotopy condition.

To simplify the situation, we only construct homotopies of the form `homotopy e 0`.
`homotopy.equiv_sub_zero` can provide the general case.

Notice however, that this construction does not have particularly good definitional properties:
we have to insert `eq_to_hom` in several places.
Hopefully this is okay in most applications, where we only need to have the existence of some
homotopy.
-/
section mk_inductive

variables {P Q : chain_complex V ‚Ñï}

@[simp] lemma prev_d_chain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) (j : ‚Ñï) :
  prev_d j f = f j (j+1) ‚â´ Q.d _ _ :=
begin
  dsimp [prev_d],
  have : (complex_shape.down ‚Ñï).prev j = j + 1 := chain_complex.prev ‚Ñï j,
  congr' 2,
end

@[simp] lemma d_next_succ_chain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) (i : ‚Ñï) :
  d_next (i+1) f = P.d _ _ ‚â´ f i (i+1) :=
begin
  dsimp [d_next],
  have : (complex_shape.down ‚Ñï).next (i + 1) = i := chain_complex.next_nat_succ _,
  congr' 2,
end

@[simp] lemma d_next_zero_chain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) :
  d_next 0 f = 0 :=
begin
  dsimp [d_next],
  rw [P.shape, zero_comp],
  rw chain_complex.next_nat_zero, dsimp, dec_trivial,
end

variables (e : P ‚ü∂ Q)
  (zero : P.X 0 ‚ü∂ Q.X 1)
  (comm_zero : e.f 0 = zero ‚â´ Q.d 1 0)
  (one : P.X 1 ‚ü∂ Q.X 2)
  (comm_one : e.f 1 = P.d 1 0 ‚â´ zero + one ‚â´ Q.d 2 1)
  (succ : ‚àÄ (n : ‚Ñï)
    (p : Œ£' (f : P.X n ‚ü∂ Q.X (n+1)) (f' : P.X (n+1) ‚ü∂ Q.X (n+2)),
      e.f (n+1) = P.d (n+1) n ‚â´ f + f' ‚â´ Q.d (n+2) (n+1)),
    Œ£' f'' : P.X (n+2) ‚ü∂ Q.X (n+3), e.f (n+2) = P.d (n+2) (n+1) ‚â´ p.2.1 + f'' ‚â´ Q.d (n+3) (n+2))

include comm_one comm_zero

/--
An auxiliary construction for `mk_inductive`.

Here we build by induction a family of diagrams,
but don't require at the type level that these successive diagrams actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a homotopy)
in `mk_inductive`.

At this stage, we don't check the homotopy condition in degree 0,
because it "falls off the end", and is easier to treat using `X_next` and `X_prev`,
which we do in `mk_inductive_aux‚ÇÇ`.
-/
@[simp, nolint unused_arguments]
def mk_inductive_aux‚ÇÅ :
  Œ† n, Œ£' (f : P.X n ‚ü∂ Q.X (n+1)) (f' : P.X (n+1) ‚ü∂ Q.X (n+2)),
    e.f (n+1) = P.d (n+1) n ‚â´ f + f' ‚â´ Q.d (n+2) (n+1)
| 0 := ‚ü®zero, one, comm_one‚ü©
| 1 := ‚ü®one, (succ 0 ‚ü®zero, one, comm_one‚ü©).1, (succ 0 ‚ü®zero, one, comm_one‚ü©).2‚ü©
| (n+2) :=
  ‚ü®(mk_inductive_aux‚ÇÅ (n+1)).2.1,
    (succ (n+1) (mk_inductive_aux‚ÇÅ (n+1))).1,
    (succ (n+1) (mk_inductive_aux‚ÇÅ (n+1))).2‚ü©

section

/--
An auxiliary construction for `mk_inductive`.
-/
@[simp]
def mk_inductive_aux‚ÇÇ :
  Œ† n, Œ£' (f : P.X_next n ‚ü∂ Q.X n) (f' : P.X n ‚ü∂ Q.X_prev n), e.f n = P.d_from n ‚â´ f + f' ‚â´ Q.d_to n
| 0 := ‚ü®0, zero ‚â´ (Q.X_prev_iso rfl).inv, by simpa using comm_zero‚ü©
| (n+1) := let I := mk_inductive_aux‚ÇÅ e zero comm_zero one comm_one succ n in
  ‚ü®(P.X_next_iso rfl).hom ‚â´ I.1, I.2.1 ‚â´ (Q.X_prev_iso rfl).inv, by simpa using I.2.2‚ü©

lemma mk_inductive_aux‚ÇÉ (i j : ‚Ñï) (h : i+1 = j) :
  (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.1 ‚â´ (Q.X_prev_iso h).hom
    = (P.X_next_iso h).inv ‚â´ (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ j).1 :=
by subst j; rcases i with (_|_|i); { dsimp, simp, }

/--
A constructor for a `homotopy e 0`, for `e` a chain map between `‚Ñï`-indexed chain complexes,
working by induction.

You need to provide the components of the homotopy in degrees 0 and 1,
show that these satisfy the homotopy condition,
and then give a construction of each component,
and the fact that it satisfies the homotopy condition,
using as an inductive hypothesis the data and homotopy condition for the previous two components.
-/
def mk_inductive : homotopy e 0 :=
{ hom := Œª i j, if h : i + 1 = j then
    (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.1 ‚â´ (Q.X_prev_iso h).hom
  else
    0,
  zero' := Œª i j w, by rwa dif_neg,
  comm := Œª i, begin
    dsimp, simp only [add_zero],
    convert (mk_inductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.2,
    { cases i,
      { dsimp [from_next], rw dif_neg,
        simp only [chain_complex.next_nat_zero, nat.one_ne_zero, not_false_iff], },
      { dsimp [from_next], rw dif_pos, swap, { simp only [chain_complex.next_nat_succ] },
        have aux : (complex_shape.down ‚Ñï).next i.succ = i := chain_complex.next_nat_succ i,
        rw mk_inductive_aux‚ÇÉ e zero comm_zero one comm_one succ
          ((complex_shape.down ‚Ñï).next i.succ) (i+1) (by rw aux),
        dsimp [X_next_iso], erw category.id_comp, } },
    { dsimp [to_prev], rw dif_pos, swap, { simp only [chain_complex.prev] },
      dsimp [X_prev_iso], erw category.comp_id, },
  end, }

end

end mk_inductive

/-!
`homotopy.mk_coinductive` allows us to build a homotopy of cochain complexes inductively,
so that as we construct each component, we have available the previous two components,
and the fact that they satisfy the homotopy condition.
-/
section mk_coinductive

variables {P Q : cochain_complex V ‚Ñï}

@[simp] lemma d_next_cochain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) (j : ‚Ñï) :
  d_next j f = P.d _ _ ‚â´ f (j+1) j :=
begin
  dsimp [d_next],
  have : (complex_shape.up ‚Ñï).next j = j + 1 := cochain_complex.next ‚Ñï j,
  congr' 2,
end

@[simp] lemma prev_d_succ_cochain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) (i : ‚Ñï) :
  prev_d (i+1) f = f (i+1) _ ‚â´ Q.d i (i+1) :=
begin
  dsimp [prev_d],
  have : (complex_shape.up ‚Ñï).prev (i+1) = i := cochain_complex.prev_nat_succ i,
  congr' 2,
end

@[simp] lemma prev_d_zero_cochain_complex (f : Œ† i j, P.X i ‚ü∂ Q.X j) :
  prev_d 0 f = 0 :=
begin
  dsimp [prev_d],
  rw [Q.shape, comp_zero],
  rw [cochain_complex.prev_nat_zero], dsimp, dec_trivial,
end

variables (e : P ‚ü∂ Q)
  (zero : P.X 1 ‚ü∂ Q.X 0)
  (comm_zero : e.f 0 = P.d 0 1 ‚â´ zero)
  (one : P.X 2 ‚ü∂ Q.X 1)
  (comm_one : e.f 1 = zero ‚â´ Q.d 0 1 + P.d 1 2 ‚â´ one)
  (succ : ‚àÄ (n : ‚Ñï)
    (p : Œ£' (f : P.X (n+1) ‚ü∂ Q.X n) (f' : P.X (n+2) ‚ü∂ Q.X (n+1)),
      e.f (n+1) = f ‚â´ Q.d n (n+1) + P.d (n+1) (n+2) ‚â´ f'),
    Œ£' f'' : P.X (n+3) ‚ü∂ Q.X (n+2), e.f (n+2) = p.2.1 ‚â´ Q.d (n+1) (n+2) + P.d (n+2) (n+3) ‚â´ f'')

include comm_one comm_zero succ

/--
An auxiliary construction for `mk_coinductive`.

Here we build by induction a family of diagrams,
but don't require at the type level that these successive diagrams actually agree.
They do in fact agree, and we then capture that at the type level (i.e. by constructing a homotopy)
in `mk_coinductive`.

At this stage, we don't check the homotopy condition in degree 0,
because it "falls off the end", and is easier to treat using `X_next` and `X_prev`,
which we do in `mk_inductive_aux‚ÇÇ`.
-/
@[simp, nolint unused_arguments]
def mk_coinductive_aux‚ÇÅ :
  Œ† n, Œ£' (f : P.X (n+1) ‚ü∂ Q.X n) (f' : P.X (n+2) ‚ü∂ Q.X (n+1)),
    e.f (n+1) = f ‚â´ Q.d n (n+1) + P.d (n+1) (n+2) ‚â´ f'
| 0 := ‚ü®zero, one, comm_one‚ü©
| 1 := ‚ü®one, (succ 0 ‚ü®zero, one, comm_one‚ü©).1, (succ 0 ‚ü®zero, one, comm_one‚ü©).2‚ü©
| (n+2) :=
  ‚ü®(mk_coinductive_aux‚ÇÅ (n+1)).2.1,
    (succ (n+1) (mk_coinductive_aux‚ÇÅ (n+1))).1,
    (succ (n+1) (mk_coinductive_aux‚ÇÅ (n+1))).2‚ü©

section

/--
An auxiliary construction for `mk_inductive`.
-/
@[simp]
def mk_coinductive_aux‚ÇÇ :
  Œ† n, Œ£' (f : P.X n ‚ü∂ Q.X_prev n) (f' : P.X_next n ‚ü∂ Q.X n),
    e.f n = f ‚â´ Q.d_to n + P.d_from n ‚â´ f'
| 0 := ‚ü®0, (P.X_next_iso rfl).hom ‚â´ zero, by simpa using comm_zero‚ü©
| (n+1) := let I := mk_coinductive_aux‚ÇÅ e zero comm_zero one comm_one succ n in
  ‚ü®I.1 ‚â´ (Q.X_prev_iso rfl).inv, (P.X_next_iso rfl).hom ‚â´ I.2.1, by simpa using I.2.2‚ü©

lemma mk_coinductive_aux‚ÇÉ (i j : ‚Ñï) (h : i + 1 = j) :
  (P.X_next_iso h).inv ‚â´ (mk_coinductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.1
    = (mk_coinductive_aux‚ÇÇ e zero comm_zero one comm_one succ j).1 ‚â´ (Q.X_prev_iso h).hom :=
by subst j; rcases i with (_|_|i); { dsimp, simp, }

/--
A constructor for a `homotopy e 0`, for `e` a chain map between `‚Ñï`-indexed cochain complexes,
working by induction.

You need to provide the components of the homotopy in degrees 0 and 1,
show that these satisfy the homotopy condition,
and then give a construction of each component,
and the fact that it satisfies the homotopy condition,
using as an inductive hypothesis the data and homotopy condition for the previous two components.
-/
def mk_coinductive : homotopy e 0 :=
{ hom := Œª i j, if h : j + 1 = i then
    (P.X_next_iso h).inv ‚â´ (mk_coinductive_aux‚ÇÇ e zero comm_zero one comm_one succ j).2.1
  else
    0,
  zero' := Œª i j w, by rwa dif_neg,
  comm := Œª i, begin
    dsimp,
    rw [add_zero, add_comm],
    convert (mk_coinductive_aux‚ÇÇ e zero comm_zero one comm_one succ i).2.2 using 2,
    { cases i,
      { dsimp [to_prev], rw dif_neg,
        simp only [cochain_complex.prev_nat_zero, nat.one_ne_zero, not_false_iff], },
      { dsimp [to_prev], rw dif_pos, swap, { simp only [cochain_complex.prev_nat_succ] },
        have aux : (complex_shape.up ‚Ñï).prev i.succ = i := cochain_complex.prev_nat_succ i,
        rw mk_coinductive_aux‚ÇÉ e zero comm_zero one comm_one succ
          ((complex_shape.up ‚Ñï).prev i.succ) (i+1) (by rw aux),
        dsimp [X_prev_iso], erw category.comp_id, } },
    { dsimp [from_next], rw dif_pos, swap, { simp only [cochain_complex.next] },
      dsimp [X_next_iso], erw category.id_comp, },
  end }

end

end mk_coinductive

end homotopy

/--
A homotopy equivalence between two chain complexes consists of a chain map each way,
and homotopies from the compositions to the identity chain maps.

Note that this contains data;
arguably it might be more useful for many applications if we truncated it to a Prop.
-/
structure homotopy_equiv (C D : homological_complex V c) :=
(hom : C ‚ü∂ D)
(inv : D ‚ü∂ C)
(homotopy_hom_inv_id : homotopy (hom ‚â´ inv) (ùüô C))
(homotopy_inv_hom_id : homotopy (inv ‚â´ hom) (ùüô D))

namespace homotopy_equiv

/-- Any complex is homotopy equivalent to itself. -/
@[refl] def refl (C : homological_complex V c) : homotopy_equiv C C :=
{ hom := ùüô C,
  inv := ùüô C,
  homotopy_hom_inv_id := by simp,
  homotopy_inv_hom_id := by simp, }

instance : inhabited (homotopy_equiv C C) := ‚ü®refl C‚ü©

/-- Being homotopy equivalent is a symmetric relation. -/
@[symm] def symm
  {C D : homological_complex V c} (f : homotopy_equiv C D) :
  homotopy_equiv D C :=
{ hom := f.inv,
  inv := f.hom,
  homotopy_hom_inv_id := f.homotopy_inv_hom_id,
  homotopy_inv_hom_id := f.homotopy_hom_inv_id, }

/-- Homotopy equivalence is a transitive relation. -/
@[trans] def trans
  {C D E : homological_complex V c} (f : homotopy_equiv C D) (g : homotopy_equiv D E) :
  homotopy_equiv C E :=
{ hom := f.hom ‚â´ g.hom,
  inv := g.inv ‚â´ f.inv,
  homotopy_hom_inv_id := by simpa using
    ((g.homotopy_hom_inv_id.comp_right_id f.inv).comp_left f.hom).trans f.homotopy_hom_inv_id,
  homotopy_inv_hom_id := by simpa using
    ((f.homotopy_inv_hom_id.comp_right_id g.hom).comp_left g.inv).trans g.homotopy_inv_hom_id, }

/-- An isomorphism of complexes induces a homotopy equivalence. -/
def of_iso {Œπ : Type*} {V : Type u} [category.{v} V] [preadditive V]
  {c : complex_shape Œπ} {C D : homological_complex V c} (f : C ‚âÖ D) :
  homotopy_equiv C D :=
‚ü®f.hom, f.inv, homotopy.of_eq f.3, homotopy.of_eq f.4‚ü©

end homotopy_equiv

variables [has_equalizers V] [has_cokernels V] [has_images V] [has_image_maps V]

/--
Homotopic maps induce the same map on homology.
-/
theorem homology_map_eq_of_homotopy (h : homotopy f g) (i : Œπ) :
  (homology_functor V c i).map f = (homology_functor V c i).map g :=
begin
  dsimp [homology_functor],
  apply eq_of_sub_eq_zero,
  ext,
  simp only [homology.œÄ_map, comp_zero, preadditive.comp_sub],
  dsimp [kernel_subobject_map],
  simp_rw [h.comm i],
  simp only [zero_add, zero_comp, d_next_eq_d_from_from_next, kernel_subobject_arrow_comp_assoc,
    preadditive.comp_add],
  rw [‚Üêpreadditive.sub_comp],
  simp only [category_theory.subobject.factor_thru_add_sub_factor_thru_right],
  erw [subobject.factor_thru_of_le (D.boundaries_le_cycles i)],
  { simp, },
  { rw [prev_d_eq_to_prev_d_to, ‚Üêcategory.assoc],
    apply image_subobject_factors_comp_self, },
end

/-- Homotopy equivalent complexes have isomorphic homologies. -/
def homology_obj_iso_of_homotopy_equiv (f : homotopy_equiv C D) (i : Œπ) :
  (homology_functor V c i).obj C ‚âÖ (homology_functor V c i).obj D :=
{ hom := (homology_functor V c i).map f.hom,
  inv := (homology_functor V c i).map f.inv,
  hom_inv_id' := begin
    rw [‚Üêfunctor.map_comp, homology_map_eq_of_homotopy f.homotopy_hom_inv_id,
      category_theory.functor.map_id],
  end,
  inv_hom_id' := begin
    rw [‚Üêfunctor.map_comp, homology_map_eq_of_homotopy f.homotopy_inv_hom_id,
      category_theory.functor.map_id],
  end, }

end

namespace category_theory

variables {W : Type*} [category W] [preadditive W]

/-- An additive functor takes homotopies to homotopies. -/
@[simps]
def functor.map_homotopy (F : V ‚•§ W) [F.additive] {f g : C ‚ü∂ D} (h : homotopy f g) :
  homotopy ((F.map_homological_complex c).map f) ((F.map_homological_complex c).map g) :=
{ hom := Œª i j, F.map (h.hom i j),
  zero' := Œª i j w, by { rw [h.zero i j w, F.map_zero], },
  comm := Œª i, begin
    dsimp [d_next, prev_d] at *,
    rw h.comm i,
    simp only [F.map_add, ‚Üê F.map_comp],
    refl
  end, }

/-- An additive functor preserves homotopy equivalences. -/
@[simps]
def functor.map_homotopy_equiv (F : V ‚•§ W) [F.additive] (h : homotopy_equiv C D) :
  homotopy_equiv ((F.map_homological_complex c).obj C) ((F.map_homological_complex c).obj D) :=
{ hom := (F.map_homological_complex c).map h.hom,
  inv := (F.map_homological_complex c).map h.inv,
  homotopy_hom_inv_id := begin
    rw [‚Üê(F.map_homological_complex c).map_comp, ‚Üê(F.map_homological_complex c).map_id],
    exact F.map_homotopy h.homotopy_hom_inv_id,
  end,
  homotopy_inv_hom_id := begin
    rw [‚Üê(F.map_homological_complex c).map_comp, ‚Üê(F.map_homological_complex c).map_id],
    exact F.map_homotopy h.homotopy_inv_hom_id,
  end }

end category_theory
