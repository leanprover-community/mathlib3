/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.subobject.limits

/-!
# Image-to-kernel comparison maps

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Whenever `f : A âŸ¶ B` and `g : B âŸ¶ C` satisfy `w : f â‰« g = 0`,
we have `image_le_kernel f g w : image_subobject f â‰¤ kernel_subobject g`
(assuming the appropriate images and kernels exist).

`image_to_kernel f g w` is the corresponding morphism between objects in `C`.

We define `homology f g w` of such a pair as the cokernel of `image_to_kernel f g w`.
-/

universes v u

open category_theory category_theory.limits

variables {Î¹ : Type*}
variables {V : Type u} [category.{v} V] [has_zero_morphisms V]

open_locale classical
noncomputable theory

section
variables {A B C : V} (f : A âŸ¶ B) [has_image f] (g : B âŸ¶ C) [has_kernel g]

lemma image_le_kernel (w : f â‰« g = 0) :
  image_subobject f â‰¤ kernel_subobject g :=
image_subobject_le_mk _ _ (kernel.lift _ _ w) (by simp)

/--
The canonical morphism `image_subobject f âŸ¶ kernel_subobject g` when `f â‰« g = 0`.
-/
@[derive mono]
def image_to_kernel (w : f â‰« g = 0) :
  (image_subobject f : V) âŸ¶ (kernel_subobject g : V) :=
(subobject.of_le _ _ (image_le_kernel _ _ w))

/-- Prefer `image_to_kernel`. -/
@[simp] lemma subobject_of_le_as_image_to_kernel (w : f â‰« g = 0) (h) :
  subobject.of_le (image_subobject f) (kernel_subobject g) h = image_to_kernel f g w :=
rfl

@[simp, reassoc, elementwise]
lemma image_to_kernel_arrow (w : f â‰« g = 0) :
  image_to_kernel f g w â‰« (kernel_subobject g).arrow = (image_subobject f).arrow :=
by simp [image_to_kernel]

-- This is less useful as a `simp` lemma than it initially appears,
-- as it "loses" the information the morphism factors through the image.
lemma factor_thru_image_subobject_comp_image_to_kernel (w : f â‰« g = 0) :
  factor_thru_image_subobject f â‰« image_to_kernel f g w = factor_thru_kernel_subobject g f w :=
by { ext, simp, }

end

section
variables {A B C : V} (f : A âŸ¶ B) (g : B âŸ¶ C)

@[simp]
lemma image_to_kernel_zero_left [has_kernels V] [has_zero_object V] {w} :
  image_to_kernel (0 : A âŸ¶ B) g w = 0 :=
by { ext, simp, }

lemma image_to_kernel_zero_right [has_images V] {w} :
  image_to_kernel f (0 : B âŸ¶ C) w =
    (image_subobject f).arrow â‰« inv (kernel_subobject (0 : B âŸ¶ C)).arrow :=
by { ext, simp }

section
variables [has_kernels V] [has_images V]

lemma image_to_kernel_comp_right {D : V} (h : C âŸ¶ D) (w : f â‰« g = 0) :
  image_to_kernel f (g â‰« h) (by simp [reassoc_of w]) =
    image_to_kernel f g w â‰« subobject.of_le _ _ (kernel_subobject_comp_le g h) :=
by { ext, simp }

lemma image_to_kernel_comp_left {Z : V} (h : Z âŸ¶ A) (w : f â‰« g = 0) :
  image_to_kernel (h â‰« f) g (by simp [w]) =
    subobject.of_le _ _ (image_subobject_comp_le h f) â‰« image_to_kernel f g w :=
by { ext, simp }

@[simp]
lemma image_to_kernel_comp_mono {D : V} (h : C âŸ¶ D) [mono h] (w) :
  image_to_kernel f (g â‰« h) w =
  image_to_kernel f g ((cancel_mono h).mp (by simpa using w : (f â‰« g) â‰« h = 0 â‰« h)) â‰«
    (subobject.iso_of_eq _ _ (kernel_subobject_comp_mono g h)).inv :=
by { ext, simp, }

@[simp]
lemma image_to_kernel_epi_comp {Z : V} (h : Z âŸ¶ A) [epi h] (w) :
  image_to_kernel (h â‰« f) g w =
  subobject.of_le _ _ (image_subobject_comp_le h f) â‰«
    image_to_kernel f g ((cancel_epi h).mp (by simpa using w : h â‰« f â‰« g = h â‰« 0)) :=
by { ext, simp, }

end

@[simp]
lemma image_to_kernel_comp_hom_inv_comp [has_equalizers V] [has_images V] {Z : V} {i : B â‰… Z} (w) :
  image_to_kernel (f â‰« i.hom) (i.inv â‰« g) w =
  (image_subobject_comp_iso _ _).hom â‰« image_to_kernel f g (by simpa using w) â‰«
    (kernel_subobject_iso_comp i.inv g).inv :=
by { ext, simp, }

open_locale zero_object

/--
`image_to_kernel` for `A --0--> B --g--> C`, where `g` is a mono is itself an epi
(i.e. the sequence is exact at `B`).
-/
instance image_to_kernel_epi_of_zero_of_mono [has_kernels V] [has_zero_object V] [mono g] :
  epi (image_to_kernel (0 : A âŸ¶ B) g (by simp)) :=
epi_of_target_iso_zero _ (kernel_subobject_iso g â‰ªâ‰« kernel.of_mono g)

/--
`image_to_kernel` for `A --f--> B --0--> C`, where `g` is an epi is itself an epi
(i.e. the sequence is exact at `B`).
-/
instance image_to_kernel_epi_of_epi_of_zero [has_images V] [epi f] :
  epi (image_to_kernel f (0 : B âŸ¶ C) (by simp)) :=
begin
  simp only [image_to_kernel_zero_right],
  haveI := epi_image_of_epi f,
  rw â†image_subobject_arrow,
  refine @epi_comp _ _ _ _ _ _ (epi_comp _ _) _ _,
end

end

section
variables {A B C : V} (f : A âŸ¶ B) [has_image f] (g : B âŸ¶ C) [has_kernel g]

/--
The homology of a pair of morphisms `f : A âŸ¶ B` and `g : B âŸ¶ C` satisfying `f â‰« g = 0`
is the cokernel of the `image_to_kernel` morphism for `f` and `g`.
-/
def homology {A B C : V} (f : A âŸ¶ B) [has_image f] (g : B âŸ¶ C) [has_kernel g]
  (w : f â‰« g = 0) [has_cokernel (image_to_kernel f g w)] : V :=
cokernel (image_to_kernel f g w)

section
variables (w : f â‰« g = 0) [has_cokernel (image_to_kernel f g w)]

/-- The morphism from cycles to homology. -/
def homology.Ï€ : (kernel_subobject g : V) âŸ¶ homology f g w :=
cokernel.Ï€ _

@[simp] lemma homology.condition : image_to_kernel f g w â‰« homology.Ï€ f g w = 0 :=
cokernel.condition _

/--
To construct a map out of homology, it suffices to construct a map out of the cycles
which vanishes on boundaries.
-/
def homology.desc {D : V} (k : (kernel_subobject g : V) âŸ¶ D) (p : image_to_kernel f g w â‰« k = 0) :
  homology f g w âŸ¶ D :=
cokernel.desc _ k p

@[simp, reassoc, elementwise]
lemma homology.Ï€_desc
  {D : V} (k : (kernel_subobject g : V) âŸ¶ D) (p : image_to_kernel f g w â‰« k = 0) :
  homology.Ï€ f g w â‰« homology.desc f g w k p = k :=
by { simp [homology.Ï€, homology.desc], }

/-- To check two morphisms out of `homology f g w` are equal, it suffices to check on cycles. -/
@[ext]
lemma homology.ext {D : V} {k k' : homology f g w âŸ¶ D}
  (p : homology.Ï€ f g w â‰« k = homology.Ï€ f g w â‰« k') : k = k' :=
by { ext, exact p, }

/-- The cokernel of the map `Im f âŸ¶ Ker 0` is isomorphic to the cokernel of `f.` -/
def homology_of_zero_right [has_cokernel (image_to_kernel f (0 : B âŸ¶ C) comp_zero)]
  [has_cokernel f] [has_cokernel (image.Î¹ f)] [epi (factor_thru_image f)] :
  homology f (0 : B âŸ¶ C) comp_zero â‰… cokernel f :=
(cokernel.map_iso _ _ (image_subobject_iso _) ((kernel_subobject_iso 0).trans
  kernel_zero_iso_source) (by simp)).trans (cokernel_image_Î¹ _)

/-- The kernel of the map `Im 0 âŸ¶ Ker f` is isomorphic to the kernel of `f.` -/
def homology_of_zero_left [has_zero_object V] [has_kernels V] [has_image (0 : A âŸ¶ B)]
  [has_cokernel (image_to_kernel (0 : A âŸ¶ B) g zero_comp)] :
  homology (0 : A âŸ¶ B) g zero_comp â‰… kernel g :=
((cokernel_iso_of_eq $ image_to_kernel_zero_left _).trans cokernel_zero_iso_target).trans
  (kernel_subobject_iso _)

/-- `homology 0 0 _` is just the middle object. -/
@[simps]
def homology_zero_zero [has_zero_object V]
  [has_image (0 : A âŸ¶ B)] [has_cokernel (image_to_kernel (0 : A âŸ¶ B) (0 : B âŸ¶ C) (by simp))] :
  homology (0 : A âŸ¶ B) (0 : B âŸ¶ C) (by simp) â‰… B :=
{ hom := homology.desc (0 : A âŸ¶ B) (0 : B âŸ¶ C) (by simp) (kernel_subobject 0).arrow (by simp),
  inv := inv (kernel_subobject 0).arrow â‰« homology.Ï€ _ _ _, }

end

section
variables {f g} (w : f â‰« g = 0)
  {A' B' C' : V} {f' : A' âŸ¶ B'} [has_image f'] {g' : B' âŸ¶ C'} [has_kernel g'] (w' : f' â‰« g' = 0)
  (Î± : arrow.mk f âŸ¶ arrow.mk f') [has_image_map Î±] (Î² : arrow.mk g âŸ¶ arrow.mk g')
  {Aâ‚ Bâ‚ Câ‚ : V} {fâ‚ : Aâ‚ âŸ¶ Bâ‚} [has_image fâ‚] {gâ‚ : Bâ‚ âŸ¶ Câ‚} [has_kernel gâ‚] (wâ‚ : fâ‚ â‰« gâ‚ = 0)
  {Aâ‚‚ Bâ‚‚ Câ‚‚ : V} {fâ‚‚ : Aâ‚‚ âŸ¶ Bâ‚‚} [has_image fâ‚‚] {gâ‚‚ : Bâ‚‚ âŸ¶ Câ‚‚} [has_kernel gâ‚‚] (wâ‚‚ : fâ‚‚ â‰« gâ‚‚ = 0)
  {Aâ‚ƒ Bâ‚ƒ Câ‚ƒ : V} {fâ‚ƒ : Aâ‚ƒ âŸ¶ Bâ‚ƒ} [has_image fâ‚ƒ] {gâ‚ƒ : Bâ‚ƒ âŸ¶ Câ‚ƒ} [has_kernel gâ‚ƒ] (wâ‚ƒ : fâ‚ƒ â‰« gâ‚ƒ = 0)
  (Î±â‚ : arrow.mk fâ‚ âŸ¶ arrow.mk fâ‚‚) [has_image_map Î±â‚] (Î²â‚ : arrow.mk gâ‚ âŸ¶ arrow.mk gâ‚‚)
  (Î±â‚‚ : arrow.mk fâ‚‚ âŸ¶ arrow.mk fâ‚ƒ) [has_image_map Î±â‚‚] (Î²â‚‚ : arrow.mk gâ‚‚ âŸ¶ arrow.mk gâ‚ƒ)

/--
Given compatible commutative squares between
a pair `f g` and a pair `f' g'` satisfying `f â‰« g = 0` and `f' â‰« g' = 0`,
the `image_to_kernel` morphisms intertwine the induced map on kernels and the induced map on images.
-/
@[reassoc]
lemma image_subobject_map_comp_image_to_kernel (p : Î±.right = Î².left) :
  image_to_kernel f g w â‰« kernel_subobject_map Î² =
    image_subobject_map Î± â‰« image_to_kernel f' g' w' :=
by { ext, simp [p], }

variables [has_cokernel (image_to_kernel f g w)] [has_cokernel (image_to_kernel f' g' w')]
variables [has_cokernel (image_to_kernel fâ‚ gâ‚ wâ‚)]
variables [has_cokernel (image_to_kernel fâ‚‚ gâ‚‚ wâ‚‚)]
variables [has_cokernel (image_to_kernel fâ‚ƒ gâ‚ƒ wâ‚ƒ)]

/--
Given compatible commutative squares between
a pair `f g` and a pair `f' g'` satisfying `f â‰« g = 0` and `f' â‰« g' = 0`,
we get a morphism on homology.
-/
def homology.map (p : Î±.right = Î².left) :
  homology f g w âŸ¶ homology f' g' w' :=
cokernel.desc _ (kernel_subobject_map Î² â‰« cokernel.Ï€ _)
  begin
    rw [image_subobject_map_comp_image_to_kernel_assoc w w' Î± Î² p],
    simp only [cokernel.condition, comp_zero],
  end

@[simp, reassoc, elementwise]
lemma homology.Ï€_map (p : Î±.right = Î².left) :
  homology.Ï€ f g w â‰« homology.map w w' Î± Î² p = kernel_subobject_map Î² â‰« homology.Ï€ f' g' w' :=
by simp only [homology.Ï€, homology.map, cokernel.Ï€_desc]

@[simp, reassoc, elementwise]
lemma homology.map_desc (p : Î±.right = Î².left)
  {D : V} (k : (kernel_subobject g' : V) âŸ¶ D) (z : image_to_kernel f' g' w' â‰« k = 0) :
  homology.map w w' Î± Î² p â‰« homology.desc f' g' w' k z =
    homology.desc f g w (kernel_subobject_map Î² â‰« k)
      (by simp only [image_subobject_map_comp_image_to_kernel_assoc w w' Î± Î² p, z, comp_zero]) :=
by ext; simp only [homology.Ï€_desc, homology.Ï€_map_assoc]

@[simp]
lemma homology.map_id : homology.map w w (ğŸ™ _) (ğŸ™ _) rfl = ğŸ™ _ :=
by ext; simp only [homology.Ï€_map, kernel_subobject_map_id, category.id_comp, category.comp_id]

/-- Auxiliary lemma for homology computations. -/
lemma homology.comp_right_eq_comp_left
  {V : Type*} [category V] {Aâ‚ Bâ‚ Câ‚ Aâ‚‚ Bâ‚‚ Câ‚‚ Aâ‚ƒ Bâ‚ƒ Câ‚ƒ : V}
  {fâ‚ : Aâ‚ âŸ¶ Bâ‚} {gâ‚ : Bâ‚ âŸ¶ Câ‚} {fâ‚‚ : Aâ‚‚ âŸ¶ Bâ‚‚} {gâ‚‚ : Bâ‚‚ âŸ¶ Câ‚‚} {fâ‚ƒ : Aâ‚ƒ âŸ¶ Bâ‚ƒ} {gâ‚ƒ : Bâ‚ƒ âŸ¶ Câ‚ƒ}
  {Î±â‚ : arrow.mk fâ‚ âŸ¶ arrow.mk fâ‚‚} {Î²â‚ : arrow.mk gâ‚ âŸ¶ arrow.mk gâ‚‚}
  {Î±â‚‚ : arrow.mk fâ‚‚ âŸ¶ arrow.mk fâ‚ƒ} {Î²â‚‚ : arrow.mk gâ‚‚ âŸ¶ arrow.mk gâ‚ƒ}
  (pâ‚ : Î±â‚.right = Î²â‚.left) (pâ‚‚ : Î±â‚‚.right = Î²â‚‚.left) :
  (Î±â‚ â‰« Î±â‚‚).right = (Î²â‚ â‰« Î²â‚‚).left :=
by simp only [comma.comp_left, comma.comp_right, pâ‚, pâ‚‚]

@[reassoc]
lemma homology.map_comp (pâ‚ : Î±â‚.right = Î²â‚.left) (pâ‚‚ : Î±â‚‚.right = Î²â‚‚.left) :
  homology.map wâ‚ wâ‚‚ Î±â‚ Î²â‚ pâ‚ â‰« homology.map wâ‚‚ wâ‚ƒ Î±â‚‚ Î²â‚‚ pâ‚‚ =
    homology.map wâ‚ wâ‚ƒ (Î±â‚ â‰« Î±â‚‚) (Î²â‚ â‰« Î²â‚‚) (homology.comp_right_eq_comp_left pâ‚ pâ‚‚) :=
by ext; simp only [kernel_subobject_map_comp, homology.Ï€_map_assoc, homology.Ï€_map, category.assoc]

/-- An isomorphism between two three-term complexes induces an isomorphism on homology. -/
def homology.map_iso (Î± : arrow.mk fâ‚ â‰… arrow.mk fâ‚‚) (Î² : arrow.mk gâ‚ â‰… arrow.mk gâ‚‚)
  (p : Î±.hom.right = Î².hom.left) :
  homology fâ‚ gâ‚ wâ‚ â‰… homology fâ‚‚ gâ‚‚ wâ‚‚ :=
{ hom := homology.map wâ‚ wâ‚‚ Î±.hom Î².hom p,
  inv := homology.map wâ‚‚ wâ‚ Î±.inv Î².inv
  (by { rw [â† cancel_mono (Î±.hom.right), â† comma.comp_right, Î±.inv_hom_id, comma.id_right, p,
      â† comma.comp_left, Î².inv_hom_id, comma.id_left], refl }),
  hom_inv_id' := by { rw [homology.map_comp], convert homology.map_id _; rw [iso.hom_inv_id] },
  inv_hom_id' := by { rw [homology.map_comp], convert homology.map_id _; rw [iso.inv_hom_id] } }

end

end

section
variables {A B C : V} {f : A âŸ¶ B} {g : B âŸ¶ C} (w : f â‰« g = 0)
  {f' : A âŸ¶ B} {g' : B âŸ¶ C} (w' : f' â‰« g' = 0)
  [has_kernels V] [has_cokernels V] [has_images V] [has_image_maps V]

/-- Custom tactic to golf and speedup boring proofs in `homology.congr`. -/
private meta def aux_tac : tactic unit :=
`[ dsimp only [auto_param_eq], erw [category.id_comp, category.comp_id], cases pf, cases pg, refl ]

/--
`homology f g w â‰… homology f' g' w'` if `f = f'` and `g = g'`.
(Note the objects are not changing here.)
-/
@[simps]
def homology.congr (pf : f = f') (pg : g = g') : homology f g w â‰… homology f' g' w' :=
{ hom := homology.map w w' âŸ¨ğŸ™ _, ğŸ™ _, by aux_tacâŸ© âŸ¨ğŸ™ _, ğŸ™ _, by aux_tacâŸ© rfl,
  inv := homology.map w' w âŸ¨ğŸ™ _, ğŸ™ _, by aux_tacâŸ© âŸ¨ğŸ™ _, ğŸ™ _, by aux_tacâŸ© rfl,
  hom_inv_id' := begin
    cases pf, cases pg, rw [homology.map_comp, â† homology.map_id],
    congr' 1; exact category.comp_id _,
  end,
  inv_hom_id' := begin
    cases pf, cases pg, rw [homology.map_comp, â† homology.map_id],
    congr' 1; exact category.comp_id _,
  end, }

end

/-!
We provide a variant `image_to_kernel' : image f âŸ¶ kernel g`,
and use this to give alternative formulas for `homology f g w`.
-/
section image_to_kernel'
variables {A B C : V} (f : A âŸ¶ B) (g : B âŸ¶ C) (w : f â‰« g = 0)
  [has_kernels V] [has_images V]

/--
While `image_to_kernel f g w` provides a morphism
`image_subobject f âŸ¶ kernel_subobject g`
in terms of the subobject API,
this variant provides a morphism
`image f âŸ¶ kernel g`,
which is sometimes more convenient.
-/
def image_to_kernel' (w : f â‰« g = 0) : image f âŸ¶ kernel g :=
kernel.lift g (image.Î¹ f) (by { ext, simpa using w, })

@[simp] lemma image_subobject_iso_image_to_kernel' (w : f â‰« g = 0) :
  (image_subobject_iso f).hom â‰« image_to_kernel' f g w =
    image_to_kernel f g w â‰« (kernel_subobject_iso g).hom :=
by { ext, simp [image_to_kernel'], }

@[simp] lemma image_to_kernel'_kernel_subobject_iso (w : f â‰« g = 0) :
  image_to_kernel' f g w â‰« (kernel_subobject_iso g).inv =
    (image_subobject_iso f).inv â‰« image_to_kernel f g w :=
by { ext, simp [image_to_kernel'], }

variables [has_cokernels V]

/--
`homology f g w` can be computed as the cokernel of `image_to_kernel' f g w`.
-/
def homology_iso_cokernel_image_to_kernel' (w : f â‰« g = 0) :
  homology f g w â‰… cokernel (image_to_kernel' f g w) :=
{ hom := cokernel.map _ _ (image_subobject_iso f).hom (kernel_subobject_iso g).hom
    (by simp only [image_subobject_iso_image_to_kernel']),
  inv := cokernel.map _ _ (image_subobject_iso f).inv (kernel_subobject_iso g).inv
    (by simp only [image_to_kernel'_kernel_subobject_iso]),
  hom_inv_id' := begin
    apply coequalizer.hom_ext,
    simp only [iso.hom_inv_id_assoc, cokernel.Ï€_desc, cokernel.Ï€_desc_assoc, category.assoc,
      coequalizer_as_cokernel],
    exact (category.comp_id _).symm,
  end,
  inv_hom_id' := by { ext1, simp only [iso.inv_hom_id_assoc, cokernel.Ï€_desc, category.comp_id,
    cokernel.Ï€_desc_assoc, category.assoc], } }

variables [has_equalizers V]

/--
`homology f g w` can be computed as the cokernel of `kernel.lift g f w`.
-/
def homology_iso_cokernel_lift (w : f â‰« g = 0) :
  homology f g w â‰… cokernel (kernel.lift g f w) :=
begin
  refine homology_iso_cokernel_image_to_kernel' f g w â‰ªâ‰« _,
  have p : factor_thru_image f â‰« image_to_kernel' f g w = kernel.lift g f w,
  { ext, simp [image_to_kernel'], },
  exact (cokernel_epi_comp _ _).symm â‰ªâ‰« cokernel_iso_of_eq p,
end

end image_to_kernel'
