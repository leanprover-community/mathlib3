import category_theory.shift
import algebra.homology.homological_complex
import algebra.homology.homotopy_category
import data.int.parity
import tactic.ring

-- move this section
namespace int

def neg_one_pow (n : ‚Ñ§) : ‚Ñ§ := @has_pow.pow (units ‚Ñ§) ‚Ñ§ _ (-1) n

@[simp] lemma neg_one_pow_add (n m : ‚Ñ§) : neg_one_pow (n + m) = neg_one_pow n * neg_one_pow m :=
by { delta neg_one_pow, rw zpow_add, simp }

@[simp] lemma neg_one_pow_one : neg_one_pow 1 = -1 := rfl

@[simp] lemma neg_one_pow_neg_one : neg_one_pow (-1) = -1 := rfl

@[simp] lemma neg_one_pow_neg_zero : neg_one_pow 0 = 1 := rfl

lemma neg_one_pow_ite (n : ‚Ñ§) : neg_one_pow n = if even n then 1 else -1 :=
begin
  induction n using int.induction_on with n h n h,
  { simp [neg_one_pow] },
  { simp [h, apply_ite has_neg.neg] with parity_simps },
  { rw [sub_eq_add_neg, neg_one_pow_add],
    simp [h, apply_ite has_neg.neg] with parity_simps }
end

lemma neg_one_pow_eq_pow_abs (n : ‚Ñ§) : neg_one_pow n = (-1) ^ n.nat_abs :=
begin
  rw neg_one_pow_ite,
  convert (neg_one_pow_ite n.nat_abs).symm using 2,
  { simp with parity_simps },
  { delta neg_one_pow, simp }
end

lemma neg_one_pow_eq_neg_one_npow (n : ‚Ñï) : neg_one_pow n = (-1) ^ n :=
by { delta neg_one_pow, simp }

@[simp] lemma neg_one_pow_neg (n : ‚Ñ§) : neg_one_pow (-n) = neg_one_pow n :=
by simp [neg_one_pow_ite] with parity_simps

@[simp] lemma neg_one_pow_sub (n m : ‚Ñ§) : neg_one_pow (n - m) = neg_one_pow n * neg_one_pow m :=
by rw [sub_eq_neg_add, neg_one_pow_add, neg_one_pow_neg, mul_comm]

@[simp] lemma neg_one_pow_mul_self (n : ‚Ñ§) : neg_one_pow n * neg_one_pow n = 1 :=
by { delta neg_one_pow, simp }

@[simp] lemma neg_one_pow_smul_self {Œ± : Type*} [add_comm_group Œ±] (n : ‚Ñ§) (X : Œ±) :
  neg_one_pow n ‚Ä¢ neg_one_pow n ‚Ä¢ X = X :=
by simp [smul_smul]

end int

universes v u

open category_theory category_theory.limits category_theory.preadditive

variables (V : Type u) [category.{v} V] [preadditive V]

namespace homological_complex

lemma complex_shape.up'_add_right_cancel {Œ± : Type*} [add_cancel_comm_monoid Œ±] (a : Œ±)
  {i j} (k : Œ±) : (complex_shape.up' a).rel (i+k) (j+k) ‚Üî (complex_shape.up' a).rel i j :=
by { dsimp, rw [add_assoc, add_comm k a, ‚Üê add_assoc], exact add_left_inj _ }

lemma complex_shape.up_add_right_cancel {Œ± : Type*} [add_cancel_comm_monoid Œ±] [has_one Œ±]
  {i j} (k : Œ±) : (complex_shape.up Œ±).rel (i+k) (j+k) ‚Üî (complex_shape.up Œ±).rel i j :=
complex_shape.up'_add_right_cancel 1 k

local attribute [simp] zsmul_comp comp_zsmul

@[simps]
def shift_functor (n : ‚Ñ§) : cochain_complex V ‚Ñ§ ‚•§ cochain_complex V ‚Ñ§ :=
{ obj := Œª X,
  { X := Œª i, X.X (i + n),
    d := Œª i j, n.neg_one_pow ‚Ä¢ X.d _ _,
    shape' := Œª i j h, by { rw [X.shape (i+n) (j+n), smul_zero],
      rwa complex_shape.up_add_right_cancel } },
  map := Œª X Y f, { f := Œª i, f.f _ } }

.

variables {V} {Œπ : Type*} {c : complex_shape Œπ}

-- @[simps]
-- def iso_of_components {X Y : homological_complex V c} (e : Œ† i, X.X i ‚âÖ Y.X i)
--   (h : ‚àÄ i j, c.rel i j ‚Üí (e i).hom ‚â´ Y.d i j = X.d i j ‚â´ (e j).hom) : X ‚âÖ Y :=
-- { hom := { f := Œª i, (e i).hom },
--   inv := { f := Œª i, (e i).inv,
--     comm' := Œª i j r, by { rwa [iso.inv_comp_eq, ‚Üê category.assoc, iso.eq_comp_inv, h] } } }
-- .

def X_eq_to_iso (X : homological_complex V c) {i j : Œπ} (h : i = j) : X.X i ‚âÖ X.X j :=
eq_to_iso $ congr_arg X.X h

@[simp]
lemma X_eq_to_iso_inv (X : homological_complex V c) {i j : Œπ} (h : i = j) :
  (X.X_eq_to_iso h).inv = (X.X_eq_to_iso h.symm).hom := rfl

@[simp, reassoc]
lemma X_eq_to_iso_d (X : homological_complex V c) {i j k : Œπ} (h : i = j) :
  (X.X_eq_to_iso h).hom ‚â´ X.d j k = X.d i k := by { subst h, exact category.id_comp _ }

@[simp, reassoc]
lemma X_d_eq_to_iso (X : homological_complex V c) {i j k : Œπ} (h : j = k) :
  X.d i j ‚â´ (X.X_eq_to_iso h).hom = X.d i k := by { subst h, exact category.comp_id _ }

@[simp, reassoc]
lemma X_eq_to_iso_trans (X : homological_complex V c) {i j k : Œπ} (h : i = j) (h' : j = k) :
  (X.X_eq_to_iso h).hom ‚â´ (X.X_eq_to_iso h').hom = (X.X_eq_to_iso (h.trans h')).hom :=
by { simp [X_eq_to_iso] }

@[simp]
lemma X_eq_to_iso_refl (X : homological_complex V c) {i : Œπ} :
  (X.X_eq_to_iso (refl i)).hom = ùüô _ := rfl

@[simp, reassoc]
lemma X_eq_to_iso_f {X Y : homological_complex V c} (f : X ‚ü∂ Y) {i j : Œπ} (h : i = j) :
  (X.X_eq_to_iso h).hom ‚â´ f.f j = f.f i ‚â´ (Y.X_eq_to_iso h).hom :=
by { subst h, simp [X_eq_to_iso] }

@[simp]
lemma eq_to_hom_f {X Y : homological_complex V c} (h : X = Y) (i) :
  hom.f (eq_to_hom h) i = eq_to_hom (by rw h) := by { subst h, simp }

variables (V)

instance : has_shift (cochain_complex V ‚Ñ§) ‚Ñ§ :=
has_shift_mk _ _
{ F := shift_functor V,
  Œµ := nat_iso.of_components (Œª X, hom.iso_of_components (Œª i, X.X_eq_to_iso (add_zero _).symm)
    (Œª i j r, by { dsimp, simp })) (Œª X Y f, by { ext, dsimp, simp }),
  Œº := Œª n m, nat_iso.of_components (Œª X, hom.iso_of_components
    (Œª i, X.X_eq_to_iso (by rw [add_comm n m, add_assoc]))
    (Œª i j r, by { dsimp, simp [smul_smul, mul_comm] })) (Œª i j f, by { ext, dsimp, simp }),
  associativity := Œª m‚ÇÅ m‚ÇÇ m‚ÇÉ X, by { ext, dsimp, simp [X_eq_to_iso] },
  left_unitality := Œª n X, by { ext, dsimp, simpa [X_eq_to_iso] },
  right_unitality := Œª n X, by { ext, dsimp, simpa [X_eq_to_iso] } }
.

local attribute[instance] endofunctor_monoidal_category

@[simp] lemma shift_X (X : cochain_complex V ‚Ñ§) (n m : ‚Ñ§) :
  (X‚ü¶n‚üß).X m = X.X (m + n) := rfl

@[simp] lemma shift_d (X : cochain_complex V ‚Ñ§) (n i j : ‚Ñ§) :
  (X‚ü¶n‚üß).d i j = n.neg_one_pow ‚Ä¢ X.d (i + n) (j + n) := rfl

@[simp] lemma shift_f {X Y : cochain_complex V ‚Ñ§} (f : X ‚ü∂ Y) (n i : ‚Ñ§) :
  (f‚ü¶n‚üß').f i = f.f (i + n) := rfl

instance (n : ‚Ñ§) : functor.additive ((shift_monoidal_functor (cochain_complex V ‚Ñ§) ‚Ñ§).obj n) := {}
instance shift_functor_additive (n : ‚Ñ§) : functor.additive (shift_functor V n) := {}

variable {V}

def homotopy_shift {X Y : cochain_complex V ‚Ñ§} {f g : X ‚ü∂ Y} (h : homotopy f g) (n : ‚Ñ§)  :
  homotopy (f‚ü¶n‚üß') (g‚ü¶n‚üß') :=
{ hom := Œª i j, n.neg_one_pow ‚Ä¢ h.hom _ _,
  zero' := Œª i j r, by { rw ‚Üê complex_shape.up_add_right_cancel n at r, simp [h.zero _ _ r] },
  comm := Œª i, begin
    dsimp,
    suffices : X.d (i + n) (i + n + 1) ‚â´ h.hom (i + n + 1) (i + n) +
      h.hom (i + n) (i + n - 1) ‚â´ Y.d (i + n - 1) (i + n) =
      X.d (i + n) (i + 1 + n) ‚â´ h.hom (i + 1 + n) (i + n) +
      h.hom (i + n) (i - 1 + n) ‚â´ Y.d (i - 1 + n) (i + n),
    { simpa [h.comm (i+n), d_next, prev_d, add_right_inj] },
    congr' 3; ring,
  end }

variable (V)

def homotopy_category.shift_functor (n : ‚Ñ§) :
  (homotopy_category V (complex_shape.up ‚Ñ§)) ‚•§ (homotopy_category V (complex_shape.up ‚Ñ§)) :=
category_theory.quotient.lift _ (shift_functor _ n ‚ãô homotopy_category.quotient _ _)
begin
  rintros X Y f g ‚ü®h‚ü©,
  apply homotopy_category.eq_of_homotopy,
  exact homotopy_shift h n,
end

def homotopy_category.shift_Œµ :
  ùü≠ _ ‚âÖ homotopy_category.shift_functor V 0 :=
begin
  refine nat_iso.of_components _ _,
  { rintro ‚ü®X‚ü©,
    refine (homotopy_category.quotient _ _).map_iso (hom.iso_of_components _ _),
    exact (Œª i, X.X_eq_to_iso (add_zero _).symm),
    { introv, dsimp, simp } },
  { rintro ‚ü®X‚ü© ‚ü®Y‚ü© f, dsimp,
    rw ‚Üê homotopy_category.quotient_map_out f,
    erw quotient.lift_map_functor_map,
    simp only [functor.comp_map, ‚Üê functor.map_comp],
    congr' 1, ext, dsimp, simp }
end

def homotopy_category.shift_functor_add (n m : ‚Ñ§) :
  homotopy_category.shift_functor V n ‚ãô homotopy_category.shift_functor V m ‚âÖ
    homotopy_category.shift_functor V (n + m) :=
begin
  refine nat_iso.of_components _ _,
  { rintro ‚ü®X‚ü©,
    refine (homotopy_category.quotient _ _).map_iso (hom.iso_of_components _ _),
    exact (Œª i, X.X_eq_to_iso (by rw [add_comm n m, add_assoc])),
    { introv r, dsimp [homotopy_category.shift_functor], simp [smul_smul, mul_comm] } },
  { rintro ‚ü®X‚ü© ‚ü®Y‚ü© f, dsimp,
    rw ‚Üê homotopy_category.quotient_map_out f,
    erw quotient.lift_map_functor_map,
    conv_rhs { erw quotient.lift_map_functor_map },
    simp only [functor.comp_map, ‚Üê functor.map_comp],
    congr' 1, ext, dsimp, simp }
end
.

@[simp]
lemma homotopy_category.shift_functor_obj_as {X : cochain_complex V ‚Ñ§} (n : ‚Ñ§) :
  (homotopy_category.shift_functor V n).obj ‚ü®X‚ü© = ‚ü®X‚ü¶n‚üß‚ü© := rfl

@[simp]
lemma homotopy_category.shift_functor_map_quotient (n : ‚Ñ§) {X Y : cochain_complex V ‚Ñ§} (f : X ‚ü∂ Y) :
  (homotopy_category.shift_functor V n).map ((homotopy_category.quotient V _).map f) =
  (homotopy_category.quotient V _).map (f‚ü¶n‚üß') := rfl

lemma quotient_eq_to_hom {X Y : homotopy_category V (complex_shape.up ‚Ñ§)} (h : X = Y) :
  eq_to_hom h = (homotopy_category.quotient V (complex_shape.up ‚Ñ§)).map (eq_to_hom (by rw h)) :=
by { subst h, simpa }

instance homotopy_category.has_shift : has_shift (homotopy_category V (complex_shape.up ‚Ñ§)) ‚Ñ§ :=
has_shift_mk _ _
{ F := homotopy_category.shift_functor V,
  Œµ := homotopy_category.shift_Œµ V,
  Œº := homotopy_category.shift_functor_add V,
  associativity := Œª m‚ÇÅ m‚ÇÇ m‚ÇÉ ‚ü®X‚ü©, by { dsimp [homotopy_category.shift_functor_add],
    rw quotient_eq_to_hom, simp only [‚Üê functor.map_comp], congr' 1, ext, simp [X_eq_to_iso] },
  left_unitality := Œª n ‚ü®X‚ü©, by { dsimp [homotopy_category.shift_Œµ,
    homotopy_category.shift_functor_add], rw quotient_eq_to_hom, simp only [‚Üê functor.map_comp],
    congr' 1, ext, simp [X_eq_to_iso] },
  right_unitality := Œª n ‚ü®X‚ü©, by { dsimp [homotopy_category.shift_Œµ,
    homotopy_category.shift_functor_add], rw quotient_eq_to_hom, simp only [‚Üê functor.map_comp],
    congr' 1, ext, simp [X_eq_to_iso] } }
.
@[simp] lemma homotopy_category.quotient_obj_shift (X : cochain_complex V ‚Ñ§) (n : ‚Ñ§) :
  ((homotopy_category.quotient V _).obj X)‚ü¶n‚üß = ‚ü®X‚ü¶n‚üß‚ü© := rfl

@[simp] lemma homotopy_category.shift_as (X : homotopy_category V (complex_shape.up ‚Ñ§)) (n : ‚Ñ§) :
  (X‚ü¶n‚üß).as = X.as‚ü¶n‚üß := rfl

@[simp] lemma homotopy_category.quotient_map_shift {X Y : cochain_complex V ‚Ñ§} (f : X ‚ü∂ Y) (n : ‚Ñ§) :
  ((homotopy_category.quotient V _).map f)‚ü¶n‚üß' = (homotopy_category.quotient V _).map (f‚ü¶n‚üß') := rfl


end homological_complex
