import category_theory.limits.preserves.shapes.zero
import category_theory.abelian.homology

noncomputable theory

open category_theory category_theory.category category_theory.limits
  category_theory.preadditive
open_locale zero_object

variables (C : Type*) [category C]

/-- A short complex in a category `C` with zero composition is the datum
of two composable morphisms `f : Xâ‚ âŸ¶ Xâ‚‚` and `g : Xâ‚‚ âŸ¶ Xâ‚ƒ` such that
`f â‰« g = 0`. -/
structure short_complex [has_zero_morphisms C] :=
{Xâ‚ Xâ‚‚ Xâ‚ƒ : C}
(f : Xâ‚ âŸ¶ Xâ‚‚)
(g : Xâ‚‚ âŸ¶ Xâ‚ƒ)
(zero : f â‰« g = 0)

variable {C}

namespace short_complex

section

variable [has_zero_morphisms C]

instance [has_zero_object C] : inhabited (short_complex C) :=
âŸ¨short_complex.mk (0 : 0 âŸ¶ 0) (0 : 0 âŸ¶ 0) comp_zeroâŸ©

attribute [simp, reassoc] zero

/-- Morphisms of short complexes are the commutative diagrams of the obvious shape. -/
@[ext]
structure hom (Sâ‚ Sâ‚‚ : short_complex C) :=
(Ï„â‚ : Sâ‚.Xâ‚ âŸ¶ Sâ‚‚.Xâ‚)
(Ï„â‚‚ : Sâ‚.Xâ‚‚ âŸ¶ Sâ‚‚.Xâ‚‚)
(Ï„â‚ƒ : Sâ‚.Xâ‚ƒ âŸ¶ Sâ‚‚.Xâ‚ƒ)
(commâ‚â‚‚ : Ï„â‚ â‰« Sâ‚‚.f = Sâ‚.f â‰« Ï„â‚‚)
(commâ‚‚â‚ƒ : Ï„â‚‚ â‰« Sâ‚‚.g = Sâ‚.g â‰« Ï„â‚ƒ)

attribute [reassoc] hom.commâ‚â‚‚ hom.commâ‚‚â‚ƒ

variables (S : short_complex C) {Sâ‚ Sâ‚‚ Sâ‚ƒ : short_complex C}

/-- The identity morphism of a short complex. -/
@[simps]
def hom.id : hom S S := âŸ¨ğŸ™ _, ğŸ™ _, ğŸ™ _, by simp, by simpâŸ©

instance : inhabited (hom S S) := âŸ¨hom.id SâŸ©

/-- The composition of morphisms of short complexes. -/
@[simps]
def hom.comp (Ï†â‚â‚‚ : hom Sâ‚ Sâ‚‚) (Ï†â‚‚â‚ƒ : hom Sâ‚‚ Sâ‚ƒ) : hom Sâ‚ Sâ‚ƒ :=
âŸ¨Ï†â‚â‚‚.Ï„â‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚, Ï†â‚â‚‚.Ï„â‚‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚‚, Ï†â‚â‚‚.Ï„â‚ƒ â‰« Ï†â‚‚â‚ƒ.Ï„â‚ƒ,
  by simp only [assoc, hom.commâ‚â‚‚, hom.commâ‚â‚‚_assoc],
  by simp only [assoc, hom.commâ‚‚â‚ƒ, hom.commâ‚‚â‚ƒ_assoc]âŸ©

instance : category (short_complex C) :=
{ hom := hom,
  id := hom.id,
  comp := Î» Sâ‚ Sâ‚‚ Sâ‚ƒ, hom.comp, }

@[simp] lemma id_Ï„â‚ : hom.Ï„â‚ (ğŸ™ S) = ğŸ™ _ := rfl
@[simp] lemma id_Ï„â‚‚ : hom.Ï„â‚‚ (ğŸ™ S) = ğŸ™ _ := rfl
@[simp] lemma id_Ï„â‚ƒ : hom.Ï„â‚ƒ (ğŸ™ S) = ğŸ™ _ := rfl
@[simp] lemma comp_Ï„â‚ (Ï†â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) : (Ï†â‚â‚‚ â‰« Ï†â‚‚â‚ƒ).Ï„â‚ = Ï†â‚â‚‚.Ï„â‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚ := rfl
@[simp] lemma comp_Ï„â‚‚ (Ï†â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) : (Ï†â‚â‚‚ â‰« Ï†â‚‚â‚ƒ).Ï„â‚‚ = Ï†â‚â‚‚.Ï„â‚‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚‚ := rfl
@[simp] lemma comp_Ï„â‚ƒ (Ï†â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) : (Ï†â‚â‚‚ â‰« Ï†â‚‚â‚ƒ).Ï„â‚ƒ = Ï†â‚â‚‚.Ï„â‚ƒ â‰« Ï†â‚‚â‚ƒ.Ï„â‚ƒ := rfl

/-- A constructor for isomorphisms in the category `short_complex C`-/
@[simps]
def mk_iso (eâ‚ : Sâ‚.Xâ‚ â‰… Sâ‚‚.Xâ‚) (eâ‚‚ : Sâ‚.Xâ‚‚ â‰… Sâ‚‚.Xâ‚‚) (eâ‚ƒ : Sâ‚.Xâ‚ƒ â‰… Sâ‚‚.Xâ‚ƒ)
  (commâ‚â‚‚ : eâ‚.hom â‰« Sâ‚‚.f = Sâ‚.f â‰« eâ‚‚.hom) (commâ‚‚â‚ƒ : eâ‚‚.hom â‰« Sâ‚‚.g = Sâ‚.g â‰« eâ‚ƒ.hom) :
  Sâ‚ â‰… Sâ‚‚ :=
{ hom := hom.mk eâ‚.hom eâ‚‚.hom eâ‚ƒ.hom commâ‚â‚‚ commâ‚‚â‚ƒ,
  inv := hom.mk eâ‚.inv eâ‚‚.inv eâ‚ƒ.inv
    (by simp only [â† cancel_mono eâ‚‚.hom, assoc, eâ‚‚.inv_hom_id, comp_id,
      â† commâ‚â‚‚, eâ‚.inv_hom_id_assoc])
    (by simp only [â† cancel_mono eâ‚ƒ.hom, assoc, eâ‚ƒ.inv_hom_id, comp_id,
      â† commâ‚‚â‚ƒ, eâ‚‚.inv_hom_id_assoc]), }

/-- The opposite short_complex in `Cáµ’áµ–` associated to a short complex in `C`. -/
@[simps]
def op : short_complex Cáµ’áµ– :=
mk S.g.op S.f.op (by simpa only [â† op_comp, S.zero])

/-- The opposite morphism in `short_complex Cáµ’áµ–` associated to a morphism in `short_complex C` -/
@[simps]
def op_map (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚‚.op âŸ¶ Sâ‚.op :=
âŸ¨Ï†.Ï„â‚ƒ.op, Ï†.Ï„â‚‚.op, Ï†.Ï„â‚.op ,
  (by { dsimp, simp only [â† op_comp, Ï†.commâ‚‚â‚ƒ], }),
  (by { dsimp, simp only [â† op_comp, Ï†.commâ‚â‚‚], })âŸ©

/-- The short_complex in `C` associated to a short complex in `Cáµ’áµ–`. -/
@[simps]
def unop (S : short_complex Cáµ’áµ–) : short_complex C :=
mk S.g.unop S.f.unop (by simpa only [â† unop_comp, S.zero])

/-- The morphism in `short_complex C` associated to a morphism in `short_complex Cáµ’áµ–` -/
@[simps]
def unop_map {Sâ‚ Sâ‚‚ : short_complex Cáµ’áµ–} (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚‚.unop âŸ¶ Sâ‚.unop :=
âŸ¨Ï†.Ï„â‚ƒ.unop, Ï†.Ï„â‚‚.unop, Ï†.Ï„â‚.unop ,
  (by { dsimp, simp only [â† unop_comp, Ï†.commâ‚‚â‚ƒ], }),
  (by { dsimp, simp only [â† unop_comp, Ï†.commâ‚â‚‚], })âŸ©

/-- The obvious isomorphism `S.op.unop â‰… S` for `S : short_complex C`. -/
@[simps]
def op_unop : S.op.unop â‰… S :=
mk_iso (iso.refl _) (iso.refl _) (iso.refl _) (by tidy) (by tidy)

/-- The obvious isomorphism `S.unop.op â‰… S` for `S : short_complex Cáµ’áµ–`. -/
@[simps]
def unop_op (S : short_complex Cáµ’áµ–) : S.unop.op â‰… S :=
mk_iso (iso.refl _) (iso.refl _) (iso.refl _) (by tidy) (by tidy)

variable (C)

/-- The obvious functor `(short_complex C)áµ’áµ– â¥¤ short_complex Cáµ’áµ–`. -/
@[simps]
def op_functor : (short_complex C)áµ’áµ– â¥¤ short_complex Cáµ’áµ– :=
{ obj := Î» S, (opposite.unop S).op,
  map := Î» Sâ‚ Sâ‚‚ Ï†, op_map Ï†.unop, }

/-- The obvious functor `short_complex Cáµ’áµ– â¥¤ (short_complex C)áµ’áµ–`. -/
@[simps]
def unop_functor : short_complex Cáµ’áµ– â¥¤ (short_complex C)áµ’áµ– :=
{ obj := Î» S, opposite.op (unop S),
  map := Î» Sâ‚ Sâ‚‚ Ï†, (unop_map Ï†).op, }

/-- The obvious equivalence of categories `(short_complex C)áµ’áµ– â‰Œ short_complex Cáµ’áµ–`. -/
def op_equiv : (short_complex C)áµ’áµ– â‰Œ short_complex Cáµ’áµ– :=
{ functor := op_functor C,
  inverse := unop_functor C,
  unit_iso := nat_iso.of_components (Î» S, (op_unop (opposite.unop S)).op)
  (Î» Sâ‚ Sâ‚‚ f, quiver.hom.unop_inj (by tidy)),
  counit_iso := nat_iso.of_components (unop_op) (by tidy), }

variables (Sâ‚ Sâ‚‚) {C}

/-- The zero morphism between two short complexes. -/
@[simps]
def hom.zero : Sâ‚ âŸ¶ Sâ‚‚ :=
âŸ¨0, 0, 0, by simp, by simpâŸ©

@[simps]
instance : has_zero (Sâ‚ âŸ¶ Sâ‚‚) := âŸ¨hom.zero _ _âŸ©

instance : has_zero_morphisms (short_complex C) := { }

end

section preadditive

variables [preadditive C] {Sâ‚ Sâ‚‚ : short_complex C}

/-- The negation of morphisms of short complexes in `C` is obtained by the
  taking the respective negations of morphisms in the preadditive category `C`. -/
@[simps]
def hom.neg (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚ âŸ¶ Sâ‚‚ :=
âŸ¨-Ï†.Ï„â‚, -Ï†.Ï„â‚‚, -Ï†.Ï„â‚ƒ,
    by simp only [neg_comp, comp_neg, neg_inj, hom.commâ‚â‚‚],
    by simp only [neg_comp, comp_neg, neg_inj, hom.commâ‚‚â‚ƒ]âŸ©

/-- The addition of morphisms in `short_complex C` is defined by adding
morphisms in the preadditive category `C`. -/
@[simps]
def hom.add (Ï† Ï†' : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚ âŸ¶ Sâ‚‚ :=
âŸ¨Ï†.Ï„â‚ + Ï†'.Ï„â‚, Ï†.Ï„â‚‚ + Ï†'.Ï„â‚‚, Ï†.Ï„â‚ƒ + Ï†'.Ï„â‚ƒ,
    by simp only [add_comp, comp_add, hom.commâ‚â‚‚],
    by simp only [add_comp, comp_add, hom.commâ‚‚â‚ƒ]âŸ©

@[simps]
instance : add_comm_group (Sâ‚ âŸ¶ Sâ‚‚) :=
{ add := hom.add,
  zero := hom.zero Sâ‚ Sâ‚‚,
  neg := hom.neg,
  add_assoc := Î» Ï† Ï†' Ï†'', by { ext; apply add_assoc, },
  zero_add := Î» Ï†, by { ext; apply zero_add, },
  add_zero := Î» Ï†, by { ext; apply add_zero, },
  add_left_neg := Î» Ï†, by { ext; apply add_left_neg, },
  add_comm := Î» Ï† Ï†', by { ext; apply add_comm, }, }

instance : preadditive (short_complex C) := { }

end preadditive

variables [has_zero_morphisms C] (S : short_complex C)

/-- If `S : short_complex C`, `h : homology_full_data S` consists of
various fields which expresses that `h.H` is the homology of `S`.
The datum includes `h.K` which is a kernel of the morphism `S.g`,
so that we may identify `h.K` as a *cycles* of the complex `S`.
Then, we require that `h.H` is a cokernel of the morphism
`S.Xâ‚ âŸ¶ h.K` induced by `f` (this morphism shall be denote `h.f'`).
This expresses `h.H` as the quotient of cycles by boundaries, i.e.
as a quotient of a subobject `h.K` of `S.Xâ‚‚`.
In order to make the notion of homology self-dual with respect
to taking the opposite category, we also require an object
`h.Q`, which is a cokernel of `S.f` and an identification
of `h.H` as a kernel of the morphism `h.g' : h.Q âŸ¶ S.Xâ‚ƒ`
induced by `S.g`. Then, the homology `h.H` is also expressed
a subject of the quotient `h.Q` of `S.Xâ‚‚`.

The primary use of this structure is for the internals of
homology API. In order to do computations, it is advisable
to use `homology_data` which involves only the expression
of the homology as a quotient of a subject. -/
structure homology_full_data :=
(K Q H : C)
(i : K âŸ¶ S.Xâ‚‚)
(p : S.Xâ‚‚ âŸ¶ Q)
(Ï€ : K âŸ¶ H)
(Î¹ : H âŸ¶ Q)
(Ï€_Î¹ : Ï€ â‰« Î¹ = i â‰« p)
(hiâ‚€ : i â‰« S.g = 0)
(hpâ‚€ : S.f â‰« p = 0)
(hi : is_limit (kernel_fork.of_Î¹ i hiâ‚€))
(hp : is_colimit (cokernel_cofork.of_Ï€ p hpâ‚€))
(hÏ€â‚€ : hi.lift (kernel_fork.of_Î¹ _ S.zero) â‰« Ï€ = 0)
(hÎ¹â‚€ : Î¹ â‰« hp.desc (cokernel_cofork.of_Ï€ _ S.zero) = 0)
(hÏ€ : is_colimit (cokernel_cofork.of_Ï€ Ï€ hÏ€â‚€))
(hÎ¹ : is_limit (kernel_fork.of_Î¹ Î¹ hÎ¹â‚€))

namespace homology_full_data

attribute [simp, reassoc] hiâ‚€ hpâ‚€ hÏ€â‚€ hÎ¹â‚€
attribute [reassoc] Ï€_Î¹
variables {S} (h : homology_full_data S)

/-- The morphism `S.Xâ‚ âŸ¶ h.K` induced by `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚` and the fact that
`h.K` is a kernel of `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ`. -/
def f' : S.Xâ‚ âŸ¶ h.K := h.hi.lift (kernel_fork.of_Î¹ _ S.zero)

/-- The morphism `h.Q âŸ¶ S.Xâ‚ƒ` induced by `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ` and the fact that
`h.Q` is a cokernel of `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚`. -/
def g' : h.Q âŸ¶ S.Xâ‚ƒ := h.hp.desc (cokernel_cofork.of_Ï€ _ S.zero)

@[simp, reassoc]
lemma f'_i : h.f' â‰« h.i = S.f := (kernel_fork.is_limit.lift' _ _ _).2

@[simp, reassoc]
lemma f'_Ï€ : h.f' â‰« h.Ï€ = 0 := h.hÏ€â‚€

@[simp, reassoc]
lemma Î¹_g' : h.Î¹ â‰« h.g' = 0 := h.hÎ¹â‚€

@[simp, reassoc]
lemma p_g' : h.p â‰« h.g' = S.g := (cokernel_cofork.is_colimit.desc' h.hp _ _).2

/-- For `h : homology_ful_data S`, this is a restatement of `h.hÏ€`, saying that
`Ï€ : h.K âŸ¶ h.H` is a cokernel of `h.f' : S.Xâ‚ âŸ¶ h.K`. -/
@[simp]
def hÏ€' : is_colimit (cokernel_cofork.of_Ï€ h.Ï€ h.f'_Ï€) := h.hÏ€

/-- For `h : homology_ful_data S`, this is a restatement of `h.hÎ¹`, saying that
`Î¹ : h.H âŸ¶ h.Q` is a kernel of `h.g' : h.Q âŸ¶ S.Xâ‚ƒ`. -/
@[simp]
def hÎ¹' : is_limit (kernel_fork.of_Î¹ h.Î¹ h.Î¹_g') := h.hÎ¹

instance : mono h.i :=
âŸ¨Î» Y lâ‚ lâ‚‚, fork.is_limit.hom_ext h.hiâŸ©

instance : mono h.Î¹ :=
âŸ¨Î» Y lâ‚ lâ‚‚, fork.is_limit.hom_ext h.hÎ¹âŸ©

instance : epi h.p :=
âŸ¨Î» Y lâ‚ lâ‚‚, cofork.is_colimit.hom_ext h.hpâŸ©

instance : epi h.Ï€ :=
âŸ¨Î» Y lâ‚ lâ‚‚, cofork.is_colimit.hom_ext h.hÏ€âŸ©

end homology_full_data

/-- We shall say that a short complex `S` has homology if
the type `homology_full_data S` is not empty. -/
class has_homology : Prop :=
(cond [] : nonempty (homology_full_data S))

/-- A choice of term of type `homology_full_data S` when `[has_homology S]`. -/
def some_homology_full_data [has_homology S] :
  homology_full_data S := (has_homology.cond S).some

/-- The homology of `S` is definition by taking the `H` field of
`S.some_homology_full_data`. -/
def homology [has_homology S] : C := S.some_homology_full_data.H

end short_complex

section

variables [has_zero_morphisms C] (C)

/-- In order to study the functoriality of the homology of short complexes,
and its behaviour with respect to different choices of `homology_full_data`,
the category `short_complex_with_homology C' is introduced, it consists
of short complexes `S` equipped with `ho : S.homology_full_data`. -/
@[ext]
structure short_complex_with_homology' :=
(S : short_complex C)
(ho : S.homology_full_data)

namespace short_complex_with_homology'

open short_complex

variables {C} (Z Zâ‚ Zâ‚‚ Zâ‚ƒ : short_complex_with_homology' C)

/-- A morphism in `short_complex_with_homology' C` consists of a
morphism of short complexes and morphisms on the `K`, `Q` and `H` fields
of the given `homology_full_data`, which satisfies the obvious
compatibilities. -/
@[ext]
structure hom :=
(Ï† : Zâ‚.S âŸ¶ Zâ‚‚.S)
(Ï†K : Zâ‚.ho.K âŸ¶ Zâ‚‚.ho.K)
(Ï†Q : Zâ‚.ho.Q âŸ¶ Zâ‚‚.ho.Q)
(Ï†H : Zâ‚.ho.H âŸ¶ Zâ‚‚.ho.H)
(commi : Zâ‚.ho.i â‰« short_complex.hom.Ï„â‚‚ Ï† = Ï†K â‰« Zâ‚‚.ho.i)
(commp : Zâ‚.ho.p â‰« Ï†Q = Ï†.Ï„â‚‚ â‰« Zâ‚‚.ho.p)
(commf' : Zâ‚.ho.f' â‰« Ï†K = Ï†.Ï„â‚ â‰« Zâ‚‚.ho.f')
(commg' : Zâ‚.ho.g' â‰« Ï†.Ï„â‚ƒ = Ï†Q â‰« Zâ‚‚.ho.g')
(commÏ€ : Zâ‚.ho.Ï€ â‰« Ï†H = Ï†K â‰« Zâ‚‚.ho.Ï€)
(commÎ¹ : Zâ‚.ho.Î¹ â‰« Ï†Q = Ï†H â‰« Zâ‚‚.ho.Î¹)

namespace hom

attribute [reassoc] commi commp commf' commg' commÏ€ commÎ¹

/-- The identity morphisms in `short_complex_with_homology' C`. -/
@[simps]
def id : hom Z Z :=
âŸ¨ğŸ™ _, ğŸ™ _, ğŸ™ _, ğŸ™ _, by simp, by simp, by simp, by simp, by simp, by simpâŸ©

instance : inhabited (hom Z Z) := âŸ¨hom.id ZâŸ©

variables {Zâ‚ Zâ‚‚ Zâ‚ƒ}

/-- The composition of morphisms in `short_complex_with_homology' C`. -/
@[simps]
def comp (Ïˆ : hom Zâ‚ Zâ‚‚) (Ïˆ' : hom Zâ‚‚ Zâ‚ƒ) : hom Zâ‚ Zâ‚ƒ :=
âŸ¨Ïˆ.Ï† â‰« Ïˆ'.Ï†, Ïˆ.Ï†K â‰« Ïˆ'.Ï†K, Ïˆ.Ï†Q â‰« Ïˆ'.Ï†Q, Ïˆ.Ï†H â‰« Ïˆ'.Ï†H,
  by simp only [comp_Ï„â‚‚, assoc, hom.commi_assoc, hom.commi],
  by simp only [comp_Ï„â‚‚, assoc, hom.commp_assoc, hom.commp],
  by simp only [assoc, comp_Ï„â‚, hom.commf'_assoc, hom.commf'],
  by simp only [comp_Ï„â‚ƒ, assoc, hom.commg'_assoc, hom.commg'],
  by simp only [assoc, hom.commÏ€_assoc, hom.commÏ€],
  by simp only [assoc, hom.commÎ¹_assoc, hom.commÎ¹]âŸ©

lemma congr_Ï† {Ïˆ Ïˆ' : hom Zâ‚ Zâ‚‚} (h : Ïˆ = Ïˆ') : Ïˆ.Ï† = Ïˆ'.Ï† := by rw h
lemma congr_Ï†K {Ïˆ Ïˆ' : hom Zâ‚ Zâ‚‚} (h : Ïˆ = Ïˆ') : Ïˆ.Ï†K = Ïˆ'.Ï†K := by rw h
lemma congr_Ï†Q {Ïˆ Ïˆ' : hom Zâ‚ Zâ‚‚} (h : Ïˆ = Ïˆ') : Ïˆ.Ï†Q = Ïˆ'.Ï†Q := by rw h
lemma congr_Ï†H {Ïˆ Ïˆ' : hom Zâ‚ Zâ‚‚} (h : Ïˆ = Ïˆ') : Ïˆ.Ï†H = Ïˆ'.Ï†H := by rw h

end hom

@[simps]
instance : category (short_complex_with_homology' C) :=
{ hom := hom,
  id := hom.id,
  comp := Î» Zâ‚ Zâ‚‚ Zâ‚ƒ, hom.comp, }

/-- The zero morphisms in `short_complex_with_homology' C` -/
@[simps]
def hom.zero : Zâ‚ âŸ¶ Zâ‚‚ :=
âŸ¨0, 0, 0, 0, by simp, by simp, by simp, by simp, by simp, by simpâŸ©

@[simps]
instance : has_zero (Zâ‚ âŸ¶ Zâ‚‚) := âŸ¨hom.zero _ _âŸ©

instance : has_zero_morphisms (short_complex_with_homology' C) := { }

variable (C)

/-- The obvious functor `short_complex_with_homology' C â¥¤ short_complex C` which
forgets the `homology_full_data`. -/
@[simps]
def forget : short_complex_with_homology' C â¥¤ short_complex C :=
{ obj := Î» Z, Z.S,
  map := Î» Zâ‚ Zâ‚‚ Ïˆ, Ïˆ.Ï†, }

instance : faithful (forget C) :=
âŸ¨Î» Zâ‚ Zâ‚‚ Ïˆ Ïˆ' (h : Ïˆ.Ï† = Ïˆ'.Ï†), begin
  have hK : Ïˆ.Ï†K = Ïˆ'.Ï†K := by simp only [â† cancel_mono Zâ‚‚.ho.i, â† hom.commi, h],
  have hQ : Ïˆ.Ï†Q = Ïˆ'.Ï†Q := by simp only [â† cancel_epi Zâ‚.ho.p, hom.commp, h],
  have hH : Ïˆ.Ï†H = Ïˆ'.Ï†H := by simp only [â† cancel_epi Zâ‚.ho.Ï€, hom.commÏ€, hK],
  ext1,
  exacts [h, hK, hQ, hH],
endâŸ©

instance : full (forget C) :=
{ preimage := Î» Zâ‚ Zâ‚‚ (Ï† : Zâ‚.S âŸ¶ Zâ‚‚.S), begin
    have hK : (Zâ‚.ho.i â‰« Ï†.Ï„â‚‚) â‰« Zâ‚‚.S.g = 0,
    { rw [assoc, Ï†.commâ‚‚â‚ƒ, Zâ‚.ho.hiâ‚€_assoc, zero_comp], },
    let Ï†K := Zâ‚‚.ho.hi.lift (kernel_fork.of_Î¹ (Zâ‚.ho.i â‰« Ï†.Ï„â‚‚) hK),
    have commi : Zâ‚.ho.i â‰« Ï†.Ï„â‚‚ = Ï†K â‰« Zâ‚‚.ho.i := (kernel_fork.is_limit.lift' _ _ hK).2.symm,
    have commf' : Zâ‚.ho.f' â‰« Ï†K = Ï†.Ï„â‚ â‰« Zâ‚‚.ho.f',
    { rw [â† cancel_mono (Zâ‚‚.ho.i), assoc, â† commi, Zâ‚.ho.f'_i_assoc, assoc, Zâ‚‚.ho.f'_i,
        Ï†.commâ‚â‚‚], },
    have hQ : Zâ‚.S.f â‰« Ï†.Ï„â‚‚ â‰« Zâ‚‚.ho.p = 0,
    { rw [â† Ï†.commâ‚â‚‚_assoc, Zâ‚‚.ho.hpâ‚€, comp_zero], },
    let Ï†Q := Zâ‚.ho.hp.desc (cokernel_cofork.of_Ï€ (Ï†.Ï„â‚‚ â‰« Zâ‚‚.ho.p) hQ),
    have commp : Zâ‚.ho.p â‰« Ï†Q = Ï†.Ï„â‚‚ â‰« Zâ‚‚.ho.p :=
      (cokernel_cofork.is_colimit.desc' Zâ‚.ho.hp _ _).2,
    have commg' : Zâ‚.ho.g' â‰« Ï†.Ï„â‚ƒ = Ï†Q â‰« Zâ‚‚.ho.g' ,
    { rw [â† cancel_epi (Zâ‚.ho.p), reassoc_of commp, Zâ‚.ho.p_g'_assoc, Zâ‚‚.ho.p_g', Ï†.commâ‚‚â‚ƒ], },
    have eqH : Zâ‚.ho.f' â‰« Ï†K â‰« Zâ‚‚.ho.Ï€ = 0,
    { rw [reassoc_of commf', Zâ‚‚.ho.f'_Ï€, comp_zero], },
    let Ï†H := Zâ‚.ho.hÏ€'.desc (cokernel_cofork.of_Ï€ (Ï†K â‰« Zâ‚‚.ho.Ï€) eqH),
    have eqH' : (Zâ‚.ho.Î¹ â‰« Ï†Q) â‰« Zâ‚‚.ho.g' = 0,
    { rw [assoc, â† commg', Zâ‚.ho.Î¹_g'_assoc, zero_comp], },
    let Ï†H' := Zâ‚‚.ho.hÎ¹'.lift (kernel_fork.of_Î¹ _ eqH'),
    have commÏ€ : Zâ‚.ho.Ï€ â‰« Ï†H = Ï†K â‰« Zâ‚‚.ho.Ï€ :=
      (cokernel_cofork.is_colimit.desc' Zâ‚.ho.hÏ€' _ eqH).2,
    have commÎ¹ : Zâ‚.ho.Î¹ â‰« Ï†Q = Ï†H' â‰« Zâ‚‚.ho.Î¹ :=
      (kernel_fork.is_limit.lift' Zâ‚‚.ho.hÎ¹' _ eqH').2.symm,
    have Ï†H_eq_Ï†H' : Ï†H = Ï†H',
    { rw [â† cancel_epi Zâ‚.ho.Ï€, â† cancel_mono Zâ‚‚.ho.Î¹, commÏ€, assoc, assoc, â† commÎ¹,
        Zâ‚.ho.Ï€_Î¹_assoc, Zâ‚‚.ho.Ï€_Î¹, commp, â† reassoc_of commi], },
    exact âŸ¨Ï†, Ï†K, Ï†Q, Ï†H, commi, commp, commf', commg', commÏ€, by rw [Ï†H_eq_Ï†H', commÎ¹]âŸ©,
  end,
  witness' := Î» Zâ‚ Zâ‚‚ Ï†, rfl, }

variable {C}

/-- A morphism in `Ï† : short_complex C` between objects that are equipped with
`homology_full_data` uniquely lifts as morphism in `short_complex_with_homology'`. -/
@[simp]
def forget_preimage {Sâ‚ Sâ‚‚ : short_complex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
  (Hâ‚ : Sâ‚.homology_full_data) (Hâ‚‚ : Sâ‚‚.homology_full_data) :
  mk Sâ‚ Hâ‚ âŸ¶ mk Sâ‚‚ Hâ‚‚ :=
(short_complex_with_homology'.forget C).preimage Ï†

lemma forget_preimage_id {S : short_complex C} (H : S.homology_full_data) :
  forget_preimage (ğŸ™ S) H H = ğŸ™ _ :=
by simpa only [forget_preimage] using preimage_id

lemma forget_preimage_comp {Sâ‚ Sâ‚‚ Sâ‚ƒ : short_complex C} (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (Ï†' : Sâ‚‚ âŸ¶ Sâ‚ƒ)
  (Hâ‚ : Sâ‚.homology_full_data) (Hâ‚‚ : Sâ‚‚.homology_full_data) (Hâ‚ƒ : Sâ‚ƒ.homology_full_data) :
  forget_preimage (Ï† â‰« Ï†') Hâ‚ Hâ‚ƒ = forget_preimage Ï† Hâ‚ Hâ‚‚ â‰« forget_preimage Ï†' Hâ‚‚ Hâ‚ƒ :=
(short_complex_with_homology'.forget C).map_injective
  (by simp only [forget_preimage, functor.image_preimage, functor.map_comp])

end short_complex_with_homology'

end

namespace short_complex

section

variables [has_zero_morphisms C] {C} (S : short_complex C) {Sâ‚ Sâ‚‚ Sâ‚ƒ : short_complex C}
  [has_homology S] [has_homology Sâ‚] [has_homology Sâ‚‚] [has_homology Sâ‚ƒ]

/-- The map in homology induced by a morphism of short complexes which have homology. -/
def homology_map (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚.homology âŸ¶ Sâ‚‚.homology :=
(short_complex_with_homology'.forget_preimage Ï† Sâ‚.some_homology_full_data
    Sâ‚‚.some_homology_full_data).Ï†H

@[simp]
lemma homology_id : homology_map (ğŸ™ S) = ğŸ™ _ :=
short_complex_with_homology'.hom.congr_Ï†H
  (short_complex_with_homology'.forget_preimage_id _)

@[simp]
lemma homology_map_comp (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (Ï†' : Sâ‚‚ âŸ¶ Sâ‚ƒ) :
  homology_map (Ï† â‰« Ï†') = homology_map Ï† â‰« homology_map Ï†' :=
short_complex_with_homology'.hom.congr_Ï†H
  (short_complex_with_homology'.forget_preimage_comp Ï† Ï†' _ _ _)

/-- Assuming that all short complex have homology, this is the homology functor. -/
@[simps]
def homology_functor [âˆ€ (S : short_complex C), has_homology S] :
  short_complex C â¥¤ C :=
{ obj := Î» S, S.homology,
  map := Î» Sâ‚ Sâ‚‚, homology_map, }

end

section abelian

/-- change kernel.lift to get better definitional properties -/
abbreviation kernel.lift' {C : Type*} [category C] [has_zero_morphisms C]
  {W X Y : C} (f : X âŸ¶ Y) [has_kernel f] (k : W âŸ¶ X) (h : k â‰« f = 0) : W âŸ¶ kernel f :=
(kernel_is_kernel f).lift (kernel_fork.of_Î¹ k h)

@[simp, reassoc]
lemma kernel.lift'_Î¹ {C : Type*} [category C] [has_zero_morphisms C]
  {W X Y : C} (f : X âŸ¶ Y) [has_kernel f] (k : W âŸ¶ X) (h : k â‰« f = 0) :
  kernel.lift' f k h â‰« kernel.Î¹ f = k :=
(kernel_is_kernel f).fac (kernel_fork.of_Î¹ k h) walking_parallel_pair.zero

/-- change cokernel.desc to get better definitional properties -/
abbreviation cokernel.desc' {C : Type*} [category C] [has_zero_morphisms C]
  {W X Y : C} (f : X âŸ¶ Y) [has_cokernel f] (k : Y âŸ¶ W) (h : f â‰« k = 0) : cokernel f âŸ¶ W :=
(cokernel_is_cokernel f).desc (cokernel_cofork.of_Ï€ k h)

@[simp, reassoc]
lemma cokernel.Ï€_desc' {C : Type*} [category C] [has_zero_morphisms C]
  {W X Y : C} (f : X âŸ¶ Y) [has_cokernel f] (k : Y âŸ¶ W) (h : f â‰« k = 0) :
  cokernel.Ï€ f â‰« cokernel.desc' f k h = k :=
(cokernel_is_cokernel f).fac (cokernel_cofork.of_Ï€ k h) walking_parallel_pair.one

@[priority 100]
instance abelian_has_homology [abelian C] : âˆ€ (S : short_complex C), has_homology S :=
Î» S, begin
  let K := kernel S.g,
  let Q := cokernel S.f,
  let f' : S.Xâ‚ âŸ¶ K := kernel.lift' _ _ S.zero,
  let g' : Q âŸ¶ S.Xâ‚ƒ := cokernel.desc' _ _ S.zero,
  let H := cokernel f',
  let i : K âŸ¶ S.Xâ‚‚ := kernel.Î¹ S.g,
  let p : S.Xâ‚‚ âŸ¶ Q := cokernel.Ï€ S.f,
  let Ï€ : K âŸ¶ H := cokernel.Ï€ f',
  let Î¹ : H âŸ¶ Q := cokernel.desc' _ (i â‰« p)
      (by simp only [kernel.lift'_Î¹_assoc, cokernel.condition]),
  have Ï€_Î¹ : Ï€ â‰« Î¹ = i â‰« p := cokernel.Ï€_desc' _ _ _,
  have hiâ‚€ : i â‰« S.g = 0 := kernel.condition _,
  have hpâ‚€ : S.f â‰« p = 0 := cokernel.condition _,
  let hi : is_limit (kernel_fork.of_Î¹ i hiâ‚€) := kernel_is_kernel _,
  let hp : is_colimit (cokernel_cofork.of_Ï€ p hpâ‚€) := cokernel_is_cokernel _,
  have hÏ€â‚€ : f' â‰« Ï€ = 0 := cokernel.condition _,
  have hÎ¹â‚€ : Î¹ â‰« g' = 0,
  { simp only [â† cancel_epi (cokernel.Ï€ (kernel.lift' S.g S.f S.zero)),
      cokernel.Ï€_desc'_assoc, assoc, cokernel.Ï€_desc', kernel.condition, comp_zero], },
  let hÏ€ : is_colimit (cokernel_cofork.of_Ï€ Ï€ hÏ€â‚€) := cokernel_is_cokernel _,
  /- The rest of the proof is the verification of `hÎ¹`.

    The main idea is to construct an isomorphism `e : H â‰… kernel g'`. (By definition,
    `H` is the cokernel of `f'`.), which is a composition of various isomorphisms
    `H â‰… cokernel Î±`, `eâ‚ : cokernel Î± â‰… abelian.coimage (i â‰« p)`,
    the isomorphism `abelian.coimage_iso_image (i â‰« p)`,
    `eâ‚‚ : abelian.image (i â‰« p) â‰… kernel Î²`, and `kernel Î² â‰… kernel g'`.

    Here `Î± : B âŸ¶ K` is the canonical map from `B := abelian.image S.f`,
    i.e. `Î±` is the inclusion of cycles in boundaries). The isomorphism
    `H â‰… cokernel Î±` (which is `cokernel f' â‰… cokernel Î±`) is easily obtained
    from the factorisation `facâ‚ : f' = f'' â‰« Î±` and the fact that `f''` is an epi).
    The isomorphism `eâ‚ : cokernel Î± â‰… abelian.coimage (i â‰« p)` follows from the
    definition of the coimage as a cokernel and the construction of an
    isomorphism `B â‰… kernel (i â‰« p)`.

    Similarly `Î² : Q âŸ¶ B'` is the canonical map to `B' := abelian.coimage S.g`, and
    all the arguments are dual. -/
  let B := abelian.image S.f,
  let B' := abelian.coimage S.g,
  let i' : B âŸ¶ S.Xâ‚‚ := abelian.image.Î¹ S.f,
  let p' : S.Xâ‚‚ âŸ¶ B' := abelian.coimage.Ï€ S.g,
  let f'' : S.Xâ‚ âŸ¶ B := abelian.factor_thru_image S.f,
  let g'' : B' âŸ¶ S.Xâ‚ƒ := abelian.factor_thru_coimage S.g,
  let Î± : B âŸ¶ K := kernel.lift' _ i'
    (by simp only [â† cancel_epi f'', abelian.image.fac_assoc, zero, comp_zero]),
  let Î² : Q âŸ¶ B' := cokernel.desc' _ p'
    (by simp only [â† cancel_mono g'', assoc, cokernel.Ï€_desc, zero, zero_comp]),
  have facâ‚ : f' = f'' â‰« Î±,
  { simp only [â† cancel_mono i, assoc, abelian.image.fac, kernel.lift'_Î¹], },
  have facâ‚‚ : Î² â‰« g'' = g',
  { simp only [â† cancel_epi p, cokernel.Ï€_desc', cokernel.Ï€_desc, cokernel.Ï€_desc'_assoc], },
  haveI : mono (Î± â‰« i) := by { rw [show Î± â‰« i = i', by simp], apply_instance, },
  haveI : epi (p â‰« Î²) := by { rw [show p â‰« Î² = p', by simp], apply_instance, },
  haveI : mono Î± := mono_of_mono Î± i,
  haveI : epi Î² := epi_of_epi p Î²,
  let hB : is_limit (kernel_fork.of_Î¹ Î± (show Î± â‰« i â‰« p = 0, by simp)) :=
    kernel_fork.is_limit.of_Î¹ _ _
      (Î» A k hk, kernel.lift' _ (k â‰« i) (by rw [assoc, hk]))
      (Î» A k hk, by simp only [â† cancel_mono i, assoc, kernel.lift'_Î¹])
      (Î» A k hk b hb, by simp only [â† cancel_mono Î±, â† cancel_mono i, hb, assoc, kernel.lift'_Î¹]),
  let hB' : is_colimit (cokernel_cofork.of_Ï€ Î² (show (i â‰« p) â‰« Î² = 0, by simp)) :=
    cokernel_cofork.is_colimit.of_Ï€ _ _
      (Î» A k hk, cokernel.desc' _ (p â‰« k) (by rw [â† assoc, hk]))
      (Î» A k hk, by simp only [â† cancel_epi p, cokernel.Ï€_desc'_assoc, cokernel.Ï€_desc'])
      (Î» A k hk b hb, by simp only [â† cancel_epi Î², â† cancel_epi p, hb,
          cokernel.Ï€_desc'_assoc, cokernel.Ï€_desc']),
  let eB : B â‰… kernel (i â‰« p) :=
    is_limit.cone_point_unique_up_to_iso hB (kernel_is_kernel (i â‰« p)),
  let eB' : cokernel (i â‰« p) â‰… B' :=
    is_colimit.cocone_point_unique_up_to_iso (cokernel_is_cokernel (i â‰« p)) hB',
  have facâ‚ƒ : eB.hom â‰« kernel.Î¹ (i â‰« p) = Î± :=
    is_limit.cone_point_unique_up_to_iso_hom_comp _ _ walking_parallel_pair.zero,
  have facâ‚„ : cokernel.Ï€ (i â‰« p) â‰« eB'.hom = Î² :=
    is_colimit.comp_cocone_point_unique_up_to_iso_hom
      (cokernel_is_cokernel _) _ walking_parallel_pair.one,
  let eâ‚ : cokernel Î± â‰… abelian.coimage (i â‰« p) :=
    cokernel_iso_of_eq facâ‚ƒ.symm â‰ªâ‰« cokernel_epi_comp _ _,
  let eâ‚‚ : abelian.image (i â‰« p) â‰… kernel Î² :=
    (kernel_comp_mono _ _).symm â‰ªâ‰« kernel_iso_of_eq facâ‚„,
  let e : H â‰… kernel g' := cokernel_iso_of_eq facâ‚ â‰ªâ‰« cokernel_epi_comp _ _ â‰ªâ‰« eâ‚ â‰ªâ‰«
    abelian.coimage_iso_image (i â‰« p) â‰ªâ‰« eâ‚‚ â‰ªâ‰«
    (kernel_comp_mono _ _ ).symm â‰ªâ‰« kernel_iso_of_eq facâ‚‚,
  have he : e.hom â‰« kernel.Î¹ _ = Î¹,
  { ext,
    dsimp,
    simp only [lift_comp_kernel_iso_of_eq_hom, cokernel_iso_of_eq_hom_comp_desc_assoc, assoc,
      kernel.lift_Î¹, cokernel.Ï€_desc_assoc, abelian.coimage_image_factorisation,
      cokernel.Ï€_desc'], },
  let hÎ¹ : is_limit (kernel_fork.of_Î¹ Î¹ hÎ¹â‚€) := is_limit.of_iso_limit (kernel_is_kernel _)
    (by { symmetry, exact fork.ext e he, }),
  exact âŸ¨nonempty.intro âŸ¨K, Q, H, i, p, Ï€, Î¹, Ï€_Î¹, hiâ‚€, hpâ‚€, hi, hp, hÏ€â‚€, hÎ¹â‚€, hÏ€, hÎ¹âŸ©âŸ©,
end

instance [abelian C] (S : short_complex C) : inhabited (S.homology_full_data) :=
âŸ¨(has_homology.cond S).someâŸ©

end abelian

end short_complex

namespace short_complex_with_homology'

instance [abelian C] : inhabited (short_complex_with_homology' C) := âŸ¨mk default defaultâŸ©

section preadditive

variables [preadditive C] (Zâ‚ Zâ‚‚ : short_complex_with_homology' C)

variables {Zâ‚ Zâ‚‚}

/-- The negation of morphisms in `short_complex_with_homology' C` is obtained
  by negatin the data. -/
@[simps]
def hom.neg (Ïˆ : Zâ‚ âŸ¶ Zâ‚‚) : Zâ‚ âŸ¶ Zâ‚‚ :=
âŸ¨-Ïˆ.Ï†, -Ïˆ.Ï†K, -Ïˆ.Ï†Q, -Ïˆ.Ï†H, by simp [Ïˆ.commi], by simp [Ïˆ.commp], by simp [Ïˆ.commf'],
  by simp [Ïˆ.commg'], by simp [Ïˆ.commÏ€], by simp [Ïˆ.commÎ¹]âŸ©

/-- The addition of morphisms in `short_complex_with_homology' C` is obtained
  by adding the data. -/
@[simps]
def hom.add (Ïˆ Ïˆ' : Zâ‚ âŸ¶ Zâ‚‚) : Zâ‚ âŸ¶ Zâ‚‚ :=
âŸ¨Ïˆ.Ï† + Ïˆ'.Ï†, Ïˆ.Ï†K + Ïˆ'.Ï†K, Ïˆ.Ï†Q + Ïˆ'.Ï†Q, Ïˆ.Ï†H + Ïˆ'.Ï†H, by simp [hom.commi], by simp [hom.commp],
  by simp [hom.commf'], by simp [hom.commg'], by simp [hom.commÏ€], by simp [hom.commÎ¹]âŸ©

@[simps]
instance : add_comm_group (Zâ‚ âŸ¶ Zâ‚‚) :=
{ add := hom.add,
  zero := hom.zero Zâ‚ Zâ‚‚,
  neg := hom.neg,
  add_assoc := Î» Ï† Ï†' Ï†'', by { ext; apply add_assoc, },
  zero_add := Î» Ï†, by { ext; apply zero_add, },
  add_zero := Î» Ï†, by { ext; apply add_zero, },
  add_left_neg := Î» Ï†, by { ext; apply add_left_neg, },
  add_comm := Î» Ï† Ï†', by { ext; apply add_comm, }, }

instance : preadditive (short_complex_with_homology' C) := { }

instance : functor.additive (short_complex_with_homology'.forget C) := { }

end preadditive

end short_complex_with_homology'
