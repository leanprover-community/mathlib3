import category_theory.limits.preserves.shapes.zero

/-!

# Short complexes

If `C` is a category that has zero morphisms, we introduce the category
`short_complex C`. Objects `S : short_complex C` consist of composable
morphisms `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚` and `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ` such that
the composition `S.f â‰« S.g` is zero.

TODO: refactor exact sequences and homology in mathlib using this category.

-/

namespace category_theory

open limits category

open_locale zero_object

variables (C D : Type*) [category C] [category D]

/-- A short complex in a category `C` with zero morphisms is the datum
of two composable morphisms `f : Xâ‚ âŸ¶ Xâ‚‚` and `g : Xâ‚‚ âŸ¶ Xâ‚ƒ` such that
`f â‰« g = 0`. -/
structure short_complex [has_zero_morphisms C] :=
{Xâ‚ Xâ‚‚ Xâ‚ƒ : C}
(f : Xâ‚ âŸ¶ Xâ‚‚)
(g : Xâ‚‚ âŸ¶ Xâ‚ƒ)
(zero : f â‰« g = 0)

variables {C} [has_zero_morphisms C] [has_zero_morphisms D]

namespace short_complex

noncomputable instance [has_zero_object C] : inhabited (short_complex C) :=
âŸ¨short_complex.mk (0 : 0 âŸ¶ 0) (0 : 0 âŸ¶ 0) comp_zeroâŸ©

attribute [simp, reassoc] zero

/-- Morphisms of short complexes are the commutative diagrams of the obvious shape. -/
@[ext]
structure hom (Sâ‚ Sâ‚‚ : short_complex C) :=
(Ï„â‚ : Sâ‚.Xâ‚ âŸ¶ Sâ‚‚.Xâ‚)
(Ï„â‚‚ : Sâ‚.Xâ‚‚ âŸ¶ Sâ‚‚.Xâ‚‚)
(Ï„â‚ƒ : Sâ‚.Xâ‚ƒ âŸ¶ Sâ‚‚.Xâ‚ƒ)
(commâ‚â‚‚' : Ï„â‚ â‰« Sâ‚‚.f = Sâ‚.f â‰« Ï„â‚‚ . obviously)
(commâ‚‚â‚ƒ' : Ï„â‚‚ â‰« Sâ‚‚.g = Sâ‚.g â‰« Ï„â‚ƒ . obviously)

restate_axiom hom.commâ‚â‚‚'
restate_axiom hom.commâ‚‚â‚ƒ'
attribute [reassoc] hom.commâ‚â‚‚ hom.commâ‚‚â‚ƒ
local attribute [simp] hom.commâ‚â‚‚ hom.commâ‚‚â‚ƒ hom.commâ‚â‚‚_assoc hom.commâ‚‚â‚ƒ_assoc

variables (S : short_complex C) {Sâ‚ Sâ‚‚ Sâ‚ƒ : short_complex C}

/-- The identity morphism of a short complex. -/
@[simps]
def hom.id : hom S S := { Ï„â‚ := ğŸ™ _, Ï„â‚‚ :=  ğŸ™ _, Ï„â‚ƒ := ğŸ™ _ }

instance : inhabited (hom S S) := âŸ¨hom.id SâŸ©

/-- The composition of morphisms of short complexes. -/
@[simps]
def hom.comp (Ï†â‚â‚‚ : hom Sâ‚ Sâ‚‚) (Ï†â‚‚â‚ƒ : hom Sâ‚‚ Sâ‚ƒ) : hom Sâ‚ Sâ‚ƒ :=
{ Ï„â‚ := Ï†â‚â‚‚.Ï„â‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚,
  Ï„â‚‚ := Ï†â‚â‚‚.Ï„â‚‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚‚,
  Ï„â‚ƒ := Ï†â‚â‚‚.Ï„â‚ƒ â‰« Ï†â‚‚â‚ƒ.Ï„â‚ƒ, }

instance : category (short_complex C) :=
{ hom := hom,
  id := hom.id,
  comp := Î» Sâ‚ Sâ‚‚ Sâ‚ƒ, hom.comp, }

/-- A constructor for morphisms in `short_complex C` when the commutativity conditions
are not obvious. -/
@[simps]
def hom.mk' {Sâ‚ Sâ‚‚ : short_complex C} (Ï„â‚ : Sâ‚.Xâ‚ âŸ¶ Sâ‚‚.Xâ‚) (Ï„â‚‚ : Sâ‚.Xâ‚‚ âŸ¶ Sâ‚‚.Xâ‚‚)
  (Ï„â‚ƒ : Sâ‚.Xâ‚ƒ âŸ¶ Sâ‚‚.Xâ‚ƒ) (commâ‚â‚‚ : Ï„â‚ â‰« Sâ‚‚.f = Sâ‚.f â‰« Ï„â‚‚)
  (commâ‚‚â‚ƒ : Ï„â‚‚ â‰« Sâ‚‚.g = Sâ‚.g â‰« Ï„â‚ƒ) : Sâ‚ âŸ¶ Sâ‚‚ :=
âŸ¨Ï„â‚, Ï„â‚‚, Ï„â‚ƒ, commâ‚â‚‚, commâ‚‚â‚ƒâŸ©

@[simp] lemma id_Ï„â‚ : hom.Ï„â‚ (ğŸ™ S) = ğŸ™ _ := rfl
@[simp] lemma id_Ï„â‚‚ : hom.Ï„â‚‚ (ğŸ™ S) = ğŸ™ _ := rfl
@[simp] lemma id_Ï„â‚ƒ : hom.Ï„â‚ƒ (ğŸ™ S) = ğŸ™ _ := rfl
@[reassoc] lemma comp_Ï„â‚ (Ï†â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) : (Ï†â‚â‚‚ â‰« Ï†â‚‚â‚ƒ).Ï„â‚ = Ï†â‚â‚‚.Ï„â‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚ := rfl
@[reassoc] lemma comp_Ï„â‚‚ (Ï†â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) : (Ï†â‚â‚‚ â‰« Ï†â‚‚â‚ƒ).Ï„â‚‚ = Ï†â‚â‚‚.Ï„â‚‚ â‰« Ï†â‚‚â‚ƒ.Ï„â‚‚ := rfl
@[reassoc] lemma comp_Ï„â‚ƒ (Ï†â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) : (Ï†â‚â‚‚ â‰« Ï†â‚‚â‚ƒ).Ï„â‚ƒ = Ï†â‚â‚‚.Ï„â‚ƒ â‰« Ï†â‚‚â‚ƒ.Ï„â‚ƒ := rfl
attribute [simp] comp_Ï„â‚ comp_Ï„â‚‚ comp_Ï„â‚ƒ

instance : has_zero (Sâ‚ âŸ¶ Sâ‚‚) := âŸ¨{ Ï„â‚ := 0, Ï„â‚‚ := 0, Ï„â‚ƒ := 0, }âŸ©

variables (Sâ‚ Sâ‚‚)
@[simp] lemma zero_Ï„â‚ : hom.Ï„â‚ (0 : Sâ‚ âŸ¶ Sâ‚‚) = 0 := rfl
@[simp] lemma zero_Ï„â‚‚ : hom.Ï„â‚‚ (0 : Sâ‚ âŸ¶ Sâ‚‚) = 0 := rfl
@[simp] lemma zero_Ï„â‚ƒ : hom.Ï„â‚ƒ (0 : Sâ‚ âŸ¶ Sâ‚‚) = 0 := rfl
variables {Sâ‚ Sâ‚‚}

instance : has_zero_morphisms (short_complex C) := { }

/-- The first projection functor `short_complex C â¥¤ C`. -/
@[simps]
def Ï€â‚ : short_complex C â¥¤ C :=
{ obj := Î» S, S.Xâ‚,
  map := Î» Sâ‚ Sâ‚‚ f, f.Ï„â‚, }

/-- The second projection functor `short_complex C â¥¤ C`. -/
@[simps]
def Ï€â‚‚ : short_complex C â¥¤ C :=
{ obj := Î» S, S.Xâ‚‚,
  map := Î» Sâ‚ Sâ‚‚ f, f.Ï„â‚‚, }

/-- The third projection functor `short_complex C â¥¤ C`. -/
@[simps]
def Ï€â‚ƒ : short_complex C â¥¤ C :=
{ obj := Î» S, S.Xâ‚ƒ,
  map := Î» Sâ‚ Sâ‚‚ f, f.Ï„â‚ƒ, }

instance Ï€â‚_preserves_zero_morphisms :
  functor.preserves_zero_morphisms (Ï€â‚ : _ â¥¤ C) := { }
instance Ï€â‚‚_preserves_zero_morphisms :
  functor.preserves_zero_morphisms (Ï€â‚‚ : _ â¥¤ C) := { }
instance Ï€â‚ƒ_preserves_zero_morphisms :
  functor.preserves_zero_morphisms (Ï€â‚ƒ : _ â¥¤ C) := { }

instance (f : Sâ‚ âŸ¶ Sâ‚‚) [is_iso f] : is_iso f.Ï„â‚ :=
by { change is_iso (Ï€â‚.map_iso (as_iso f)).hom, apply_instance, }
instance (f : Sâ‚ âŸ¶ Sâ‚‚) [is_iso f] : is_iso f.Ï„â‚‚ :=
by { change is_iso (Ï€â‚‚.map_iso (as_iso f)).hom, apply_instance, }
instance (f : Sâ‚ âŸ¶ Sâ‚‚) [is_iso f] : is_iso f.Ï„â‚ƒ :=
by { change is_iso (Ï€â‚ƒ.map_iso (as_iso f)).hom, apply_instance, }

/-- The natural transformation `Ï€â‚ âŸ¶ Ï€â‚‚` induced by `S.f` for all `S : short_complex C`. -/
@[simps]
def Ï€â‚_to_Ï€â‚‚ : (Ï€â‚ : _ â¥¤ C) âŸ¶ Ï€â‚‚ := { app := Î» S, S.f, }

/-- The natural transformation `Ï€â‚‚ âŸ¶ Ï€â‚ƒ` induced by `S.g` for all `S : short_complex C`. -/
@[simps]
def Ï€â‚‚_to_Ï€â‚ƒ : (Ï€â‚‚ : _ â¥¤ C) âŸ¶ Ï€â‚ƒ := { app := Î» S, S.g, }

@[simp, reassoc]
lemma Ï€â‚_to_Ï€â‚‚_comp_Ï€â‚‚_to_Ï€â‚ƒ : (Ï€â‚_to_Ï€â‚‚ : (_ : _ â¥¤ C) âŸ¶ _) â‰« Ï€â‚‚_to_Ï€â‚ƒ = 0 :=
by { ext, apply short_complex.zero, }

variables {C D}

/-- The short complex in `D` obtained by applying a functor `F : C â¥¤ D` to a
short complex in `C`, assuming that `F` preserves zero morphisms. -/
@[simps]
def map (F : C â¥¤ D) [F.preserves_zero_morphisms] : short_complex D :=
short_complex.mk (F.map S.f) (F.map S.g) (by rw [â† F.map_comp, S.zero, F.map_zero])

/-- The morphism of short complexes `S.map F âŸ¶ S.map G` induced by
a natural transformation `F âŸ¶ G`. -/
@[simps]
def map_nat_trans {F G : C â¥¤ D} [F.preserves_zero_morphisms]
  [G.preserves_zero_morphisms] (Ï„ : F âŸ¶ G) : S.map F âŸ¶ S.map G :=
{ Ï„â‚ := Ï„.app _,
  Ï„â‚‚ := Ï„.app _,
  Ï„â‚ƒ := Ï„.app _, }

/-- The isomorphism of short complexes `S.map F â‰… S.map G` induced by
a natural isomorphism `F â‰… G`. -/
--@[simps]
def map_nat_iso {F G : C â¥¤ D} [F.preserves_zero_morphisms]
  [G.preserves_zero_morphisms] (Ï„ : F â‰… G) : S.map F â‰… S.map G :=
{ hom := S.map_nat_trans Ï„.hom,
  inv := S.map_nat_trans Ï„.inv, }

/-- The functor `short_complex C â¥¤ short_complex D` induced by a functor `C â¥¤ D` which
preserves zero morphisms. -/
@[simps]
def _root_.category_theory.functor.map_short_complex
  (F : C â¥¤ D) [F.preserves_zero_morphisms] :
  short_complex C â¥¤ short_complex D :=
{ obj := Î» S, S.map F,
  map := Î» Sâ‚ Sâ‚‚ Ï†, âŸ¨F.map Ï†.Ï„â‚, F.map Ï†.Ï„â‚‚, F.map Ï†.Ï„â‚ƒ,
    by { dsimp, simp only [â† F.map_comp, Ï†.commâ‚â‚‚], },
    by { dsimp, simp only [â† F.map_comp, Ï†.commâ‚‚â‚ƒ], }âŸ© }

/-- A constructor for isomorphisms in the category `short_complex C`-/
@[simps]
def mk_iso (eâ‚ : Sâ‚.Xâ‚ â‰… Sâ‚‚.Xâ‚) (eâ‚‚ : Sâ‚.Xâ‚‚ â‰… Sâ‚‚.Xâ‚‚) (eâ‚ƒ : Sâ‚.Xâ‚ƒ â‰… Sâ‚‚.Xâ‚ƒ)
  (commâ‚â‚‚ : eâ‚.hom â‰« Sâ‚‚.f = Sâ‚.f â‰« eâ‚‚.hom) (commâ‚‚â‚ƒ : eâ‚‚.hom â‰« Sâ‚‚.g = Sâ‚.g â‰« eâ‚ƒ.hom) :
  Sâ‚ â‰… Sâ‚‚ :=
{ hom := âŸ¨eâ‚.hom, eâ‚‚.hom, eâ‚ƒ.hom, commâ‚â‚‚, commâ‚‚â‚ƒâŸ©,
  inv := hom.mk' eâ‚.inv eâ‚‚.inv eâ‚ƒ.inv
    (by simp only [â† cancel_mono eâ‚‚.hom, assoc, eâ‚‚.inv_hom_id, comp_id,
      â† commâ‚â‚‚, eâ‚.inv_hom_id_assoc])
    (by simp only [â† cancel_mono eâ‚ƒ.hom, assoc, eâ‚ƒ.inv_hom_id, comp_id,
      â† commâ‚‚â‚ƒ, eâ‚‚.inv_hom_id_assoc]), }

/-- The opposite short_complex in `Cáµ’áµ–` associated to a short complex in `C`. -/
@[simps]
def op : short_complex Cáµ’áµ– :=
mk S.g.op S.f.op (by simpa only [â† op_comp, S.zero])

/-- The opposite morphism in `short_complex Cáµ’áµ–` associated to a morphism in `short_complex C` -/
@[simps]
def op_map (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚‚.op âŸ¶ Sâ‚.op :=
âŸ¨Ï†.Ï„â‚ƒ.op, Ï†.Ï„â‚‚.op, Ï†.Ï„â‚.op,
  by { dsimp, simp only [â† op_comp, Ï†.commâ‚‚â‚ƒ], },
  by { dsimp, simp only [â† op_comp, Ï†.commâ‚â‚‚], }âŸ©

/-- The short_complex in `C` associated to a short complex in `Cáµ’áµ–`. -/
@[simps]
def unop (S : short_complex Cáµ’áµ–) : short_complex C :=
mk S.g.unop S.f.unop (by simpa only [â† unop_comp, S.zero])

/-- The morphism in `short_complex C` associated to a morphism in `short_complex Cáµ’áµ–` -/
@[simps]
def unop'_map {Sâ‚ Sâ‚‚ : short_complex Cáµ’áµ–} (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚‚.unop âŸ¶ Sâ‚.unop :=
âŸ¨Ï†.Ï„â‚ƒ.unop, Ï†.Ï„â‚‚.unop, Ï†.Ï„â‚.unop,
  by { dsimp, simp only [â† unop_comp, Ï†.commâ‚‚â‚ƒ], },
  by { dsimp, simp only [â† unop_comp, Ï†.commâ‚â‚‚], }âŸ©

/-- The morphism in `short_complex C` associated to a morphism in `short_complex Cáµ’áµ–` -/
@[simps]
def unop_map {Sâ‚ Sâ‚‚ : short_complex C} (Ï† : Sâ‚.op âŸ¶ Sâ‚‚.op) : Sâ‚‚ âŸ¶ Sâ‚ :=
âŸ¨Ï†.Ï„â‚ƒ.unop, Ï†.Ï„â‚‚.unop, Ï†.Ï„â‚.unop, quiver.hom.op_inj Ï†.commâ‚‚â‚ƒ.symm,
  quiver.hom.op_inj Ï†.commâ‚â‚‚.symmâŸ©

/-- The obvious isomorphism `S.op.unop â‰… S` for `S : short_complex C`. -/
@[simps]
def op_unop : S.op.unop â‰… S :=
mk_iso (iso.refl _) (iso.refl _) (iso.refl _) (by tidy) (by tidy)

/-- The obvious isomorphism `S.unop.op â‰… S` for `S : short_complex Cáµ’áµ–`. -/
@[simps]
def unop_op (S : short_complex Cáµ’áµ–) : S.unop.op â‰… S :=
mk_iso (iso.refl _) (iso.refl _) (iso.refl _) (by tidy) (by tidy)

variable (C)

/-- The obvious functor `(short_complex C)áµ’áµ– â¥¤ short_complex Cáµ’áµ–`. -/
@[simps]
def op_functor : (short_complex C)áµ’áµ– â¥¤ short_complex Cáµ’áµ– :=
{ obj := Î» S, (opposite.unop S).op,
  map := Î» Sâ‚ Sâ‚‚ Ï†, op_map Ï†.unop, }

/-- The obvious functor `short_complex Cáµ’áµ– â¥¤ (short_complex C)áµ’áµ–`. -/
@[simps]
def unop_functor : short_complex Cáµ’áµ– â¥¤ (short_complex C)áµ’áµ– :=
{ obj := Î» S, opposite.op (unop S),
  map := Î» Sâ‚ Sâ‚‚ Ï†, (unop'_map Ï†).op, }

/-- The obvious equivalence of categories `(short_complex C)áµ’áµ– â‰Œ short_complex Cáµ’áµ–`. -/
@[simps]
def op_equiv : (short_complex C)áµ’áµ– â‰Œ short_complex Cáµ’áµ– :=
{ functor := op_functor C,
  inverse := unop_functor C,
  unit_iso := nat_iso.of_components (Î» S, (op_unop (opposite.unop S)).op)
    (Î» Sâ‚ Sâ‚‚ f, quiver.hom.unop_inj (by tidy)),
  counit_iso := nat_iso.of_components (unop_op) (by tidy), }

end short_complex

end category_theory
