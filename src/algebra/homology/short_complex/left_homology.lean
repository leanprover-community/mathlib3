import algebra.homology.short_complex.basic
import category_theory.limits.preserves.finite
import category_theory.limits.shapes.finite_limits
import category_theory.limits.shapes.kernels
import tactic.equiv_rw

noncomputable theory

open category_theory category_theory.category category_theory.limits
open_locale zero_object

namespace category_theory.limits

variables {C : Type*} [category C] [has_zero_morphisms C]

/-- should be renamed `is_limit_id_kernel_fork` -/
@[simps]
def kernel_zero {X Y : C} (f : X âŸ¶ Y) (hf : f = 0) :
  is_limit (kernel_fork.of_Î¹ (ğŸ™ X) (show ğŸ™ X â‰« f = 0, by rw [hf, comp_zero])) :=
kernel_fork.is_limit.of_Î¹ _ _ (Î» A x hx, x) (Î» A x hx, comp_id _)
  (Î» A x hx b hb, by rw [â† hb, comp_id])

@[simps]
def cokernel_zero {X Y : C} (f : X âŸ¶ Y) (hf : f = 0) :
  is_colimit (cokernel_cofork.of_Ï€ (ğŸ™ Y) (show f â‰« ğŸ™ Y = 0, by rw [hf, zero_comp])) :=
cokernel_cofork.is_colimit.of_Ï€ _ _ (Î» A x hx, x) (Î» A x hx, id_comp _)
  (Î» A x hx b hb, by rw [â† hb, id_comp])

/-- fork.is_limit.lift_Î¹ has to be fixed -/
@[simp, reassoc]
lemma fork.is_limit.lift_Î¹' {X Y : C} {f g : X âŸ¶ Y} {c : fork f g} (hc : is_limit c)
  (c' : fork f g ) : hc.lift c' â‰« c.Î¹ = c'.Î¹ :=
by apply fork.is_limit.lift_Î¹

namespace kernel_fork

def is_limit.of_Î¹_op {K X Y : C} (i : K âŸ¶ X) {f : X âŸ¶ Y}
  (w : i â‰« f = 0) (h : is_limit (kernel_fork.of_Î¹ i w)) :
  is_colimit (cokernel_cofork.of_Ï€ i.op
    (show f.op â‰« i.op = 0, by simpa only [â† op_comp, w])) :=
cokernel_cofork.is_colimit.of_Ï€ _ _
  (Î» A x hx, (h.lift (kernel_fork.of_Î¹ x.unop (quiver.hom.op_inj hx))).op)
  (Î» A x hx, quiver.hom.unop_inj (fork.is_limit.lift_Î¹ h))
  (Î» A x hx b hb, quiver.hom.unop_inj (fork.is_limit.hom_ext h begin
    simp only [quiver.hom.unop_op, fork.is_limit.lift_Î¹],
    exact quiver.hom.op_inj hb,
  end))

def is_limit.of_Î¹_unop {K X Y : Cáµ’áµ–} (i : K âŸ¶ X) {f : X âŸ¶ Y}
  (w : i â‰« f = 0) (h : is_limit (kernel_fork.of_Î¹ i w)) :
  is_colimit (cokernel_cofork.of_Ï€ i.unop
    (show f.unop â‰« i.unop = 0, by simpa only [â† unop_comp, w])) :=
cokernel_cofork.is_colimit.of_Ï€ _ _
  (Î» A x hx, (h.lift (kernel_fork.of_Î¹ x.op (quiver.hom.unop_inj hx))).unop)
  (Î» A x hx, quiver.hom.op_inj (fork.is_limit.lift_Î¹ h))
  (Î» A x hx b hb, quiver.hom.op_inj (fork.is_limit.hom_ext h begin
    simp only [quiver.hom.op_unop, fork.is_limit.lift_Î¹],
    exact quiver.hom.unop_inj hb,
  end))

lemma is_limit.is_iso_Î¹_of_zero {X Y : C} {f : X âŸ¶ Y} (c : kernel_fork f)
  (hc : is_limit c) (hf : f = 0) : is_iso c.Î¹ :=
begin
  subst hf,
  let e : c.X â‰… X := is_limit.cone_point_unique_up_to_iso hc (kernel_zero (0 : X âŸ¶ Y) rfl),
  have eq : e.inv â‰« fork.Î¹ c  = ğŸ™ X := fork.is_limit.lift_Î¹ hc,
  haveI : is_iso (e.inv â‰« fork.Î¹ c),
  { rw eq, dsimp, apply_instance, },
  exact is_iso.of_is_iso_comp_left e.inv (fork.Î¹ c),
end

end kernel_fork

namespace cokernel_cofork

def is_colimit.of_Ï€_op {X Y Q : C} (p : Y âŸ¶ Q) {f : X âŸ¶ Y}
  (w : f â‰« p = 0) (h : is_colimit (cokernel_cofork.of_Ï€ p w)) :
  is_limit (kernel_fork.of_Î¹ p.op
    (show p.op â‰« f.op = 0, by simpa only [â† op_comp, w])) :=
kernel_fork.is_limit.of_Î¹ _ _
  (Î» A x hx, (h.desc (cokernel_cofork.of_Ï€ x.unop (quiver.hom.op_inj hx))).op)
  (Î» A x hx, quiver.hom.unop_inj (cofork.is_colimit.Ï€_desc h))
  (Î» A x hx b hb, quiver.hom.unop_inj (cofork.is_colimit.hom_ext h begin
    simp only [quiver.hom.unop_op, cofork.is_colimit.Ï€_desc],
    exact quiver.hom.op_inj hb,
  end))

def is_colimit.of_Ï€_unop {X Y Q : Cáµ’áµ–} (p : Y âŸ¶ Q) {f : X âŸ¶ Y}
  (w : f â‰« p = 0) (h : is_colimit (cokernel_cofork.of_Ï€ p w)) :
  is_limit (kernel_fork.of_Î¹ p.unop
    (show p.unop â‰« f.unop = 0, by simpa only [â† unop_comp, w])) :=
kernel_fork.is_limit.of_Î¹ _ _
  (Î» A x hx, (h.desc (cokernel_cofork.of_Ï€ x.op (quiver.hom.unop_inj hx))).unop)
  (Î» A x hx, quiver.hom.op_inj (cofork.is_colimit.Ï€_desc h))
  (Î» A x hx b hb, quiver.hom.op_inj (cofork.is_colimit.hom_ext h begin
    simp only [quiver.hom.op_unop, cofork.is_colimit.Ï€_desc],
    exact quiver.hom.unop_inj hb,
  end))

lemma is_colimit.is_iso_Ï€_of_zero {X Y : C} {f : X âŸ¶ Y} (c : cokernel_cofork f)
  (hc : is_colimit c) (hf : f = 0) : is_iso c.Ï€ :=
begin
  subst hf,
  let e : c.X â‰… Y := is_colimit.cocone_point_unique_up_to_iso hc (cokernel_zero (0 : X âŸ¶ Y) rfl),
  have eq : cofork.Ï€ c â‰« e.hom = ğŸ™ Y := cofork.is_colimit.Ï€_desc hc,
  haveI : is_iso (cofork.Ï€ c â‰« e.hom),
  { rw eq, dsimp, apply_instance, },
  exact is_iso.of_is_iso_comp_right (cofork.Ï€ c) e.hom,
end

end cokernel_cofork

end category_theory.limits

open category_theory.limits

namespace category_theory

namespace short_complex

variables {C D : Type*} [category C] [category D]
  [has_zero_morphisms C]
  (S : short_complex C) {Sâ‚ Sâ‚‚ Sâ‚ƒ : short_complex C}

@[nolint has_nonempty_instance]
structure left_homology_data :=
(K H : C)
(i : K âŸ¶ S.Xâ‚‚)
(Ï€ : K âŸ¶ H)
(wi : i â‰« S.g = 0)
(hi : is_limit (kernel_fork.of_Î¹ i wi))
(wÏ€ : hi.lift (kernel_fork.of_Î¹ _ S.zero) â‰« Ï€ = 0)
(hÏ€ : is_colimit (cokernel_cofork.of_Ï€ Ï€ wÏ€))

namespace left_homology_data

@[simps]
def of_ker_of_coker [has_kernel S.g] [has_cokernel (kernel.lift S.g S.f S.zero)] :
  S.left_homology_data :=
{ K := kernel S.g,
  H := cokernel (kernel.lift S.g S.f S.zero),
  i := kernel.Î¹ _,
  Ï€ := cokernel.Ï€ _,
  wi := kernel.condition _,
  hi := kernel_is_kernel _,
  wÏ€ := cokernel.condition _,
  hÏ€ := cokernel_is_cokernel _, }

attribute [simp, reassoc] wi wÏ€
variables {S} (h : left_homology_data S) {A : C}

instance : mono h.i :=
âŸ¨Î» Y lâ‚ lâ‚‚, fork.is_limit.hom_ext h.hiâŸ©

instance : epi h.Ï€ :=
âŸ¨Î» Y lâ‚ lâ‚‚, cofork.is_colimit.hom_ext h.hÏ€âŸ©

def lift_K (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) : A âŸ¶ h.K :=
h.hi.lift (kernel_fork.of_Î¹ k hk)

@[simp, reassoc]
lemma lift_K_i (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) :
  h.lift_K k hk â‰« h.i = k :=
h.hi.fac _ walking_parallel_pair.zero

@[simp]
def lift_H (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) : A âŸ¶ h.H :=
  h.lift_K k hk â‰« h.Ï€

/-- The morphism `S.Xâ‚ âŸ¶ h.K` induced by `S.f : S.Xâ‚ âŸ¶ S.Xâ‚‚` and the fact that
`h.K` is a kernel of `S.g : S.Xâ‚‚ âŸ¶ S.Xâ‚ƒ`. -/
def f' : S.Xâ‚ âŸ¶ h.K := h.lift_K S.f S.zero

@[simp, reassoc]
lemma f'_i : h.f' â‰« h.i = S.f :=
lift_K_i _ _ _

@[simp, reassoc]
lemma f'_Ï€ : h.f' â‰« h.Ï€ = 0 := h.wÏ€

lemma lift_K_Ï€_eq_zero_of_boundary (k : A âŸ¶ S.Xâ‚‚) (x : A âŸ¶ S.Xâ‚) (hx : k = x â‰« S.f) :
  h.lift_K k (by rw [hx, assoc, S.zero, comp_zero]) â‰« h.Ï€ = 0 :=
begin
  rw [show 0 = (x â‰« h.f') â‰« h.Ï€, by simp],
  congr' 1,
  simp only [â† cancel_mono h.i, hx, assoc, lift_K_i, f'_i],
end

/-- For `h : homology_ful_data S`, this is a restatement of `h.hÏ€`, saying that
`Ï€ : h.K âŸ¶ h.H` is a cokernel of `h.f' : S.Xâ‚ âŸ¶ h.K`. -/
def hÏ€' : is_colimit (cokernel_cofork.of_Ï€ h.Ï€ h.f'_Ï€) := h.hÏ€

def desc_H (k : h.K âŸ¶ A) (hk : h.f' â‰« k = 0) :
  h.H âŸ¶ A :=
h.hÏ€.desc (cokernel_cofork.of_Ï€ k hk)

@[simp, reassoc]
lemma Ï€_desc_H (k : h.K âŸ¶ A) (hk : h.f' â‰« k = 0) :
  h.Ï€ â‰« h.desc_H k hk = k :=
h.hÏ€.fac (cokernel_cofork.of_Ï€ k hk) walking_parallel_pair.one

variable (S)

@[simps]
def of_colimit_cokernel_cofork (hg : S.g = 0) (c : cokernel_cofork S.f) (hc : is_colimit c) :
  S.left_homology_data :=
{ K := S.Xâ‚‚,
  H := c.X,
  i := ğŸ™ _,
  Ï€ := c.Ï€,
  wi := by rw [id_comp, hg],
  hi := kernel_zero _ hg,
  wÏ€ := cokernel_cofork.condition _,
  hÏ€ := is_colimit.of_iso_colimit hc (cofork.ext (iso.refl _) (by tidy)), }

@[simp] lemma of_colimit_cokernel_cofork_f' (hg : S.g = 0) (c : cokernel_cofork S.f)
  (hc : is_colimit c) : (of_colimit_cokernel_cofork S hg c hc).f' = S.f :=
begin
  rw [â† cancel_mono (of_colimit_cokernel_cofork S hg c hc).i, f'_i,
    of_colimit_cokernel_cofork_i],
  dsimp,
  rw comp_id,
end

@[simp]
def of_has_cokernel [has_cokernel S.f] (hg : S.g = 0) : S.left_homology_data :=
of_colimit_cokernel_cofork S hg _ (cokernel_is_cokernel _)

@[simps]
def of_limit_kernel_fork (hf : S.f = 0) (c : kernel_fork S.g) (hc : is_limit c) :
  S.left_homology_data :=
{ K := c.X,
  H := c.X,
  i := c.Î¹,
  Ï€ := ğŸ™ _,
  wi := kernel_fork.condition _,
  hi := is_limit.of_iso_limit hc (fork.ext (iso.refl _) (by tidy)),
  wÏ€ := fork.is_limit.hom_ext hc begin
    dsimp, simp only [hf, comp_id, fork.is_limit.lift_Î¹, kernel_fork.Î¹_of_Î¹, zero_comp],
  end,
  hÏ€ := cokernel_zero _ begin
    apply fork.is_limit.hom_ext hc,
    dsimp,
    simp only [hf, comp_id, fork.is_limit.lift_Î¹, kernel_fork.Î¹_of_Î¹, zero_comp],
  end, }

@[simp] lemma of_limit_kernel_fork_f' (hf : S.f = 0) (c : kernel_fork S.g)
  (hc : is_limit c) : (of_limit_kernel_fork S hf c hc).f' = 0 :=
by rw [â† cancel_mono (of_limit_kernel_fork S hf c hc).i, f'_i, hf, zero_comp]

@[simp]
def of_has_kernel [has_kernel S.g] (hf : S.f = 0) : S.left_homology_data :=
of_limit_kernel_fork S hf _ (kernel_is_kernel _)

@[simps]
def of_zeros (hf : S.f = 0) (hg : S.g = 0) :
  S.left_homology_data :=
{ K := S.Xâ‚‚,
  H := S.Xâ‚‚,
  i := ğŸ™ _,
  Ï€ := ğŸ™ _,
  wi := by rw [id_comp, hg],
  hi := kernel_zero _ hg,
  wÏ€ := by { dsimp, rw [comp_id, hf], },
  hÏ€ := cokernel_zero _ hf, }

@[simp]
lemma of_zeros_f' (hf : S.f = 0) (hg : S.g = 0) :
  (of_zeros S hf hg).f' = S.f :=
begin
  rw [â† cancel_mono (of_zeros S hf hg).i, f'_i],
  dsimp,
  rw comp_id,
end

@[simps]
def kernel_sequence' {X Y : C} (f : X âŸ¶ Y) (c : kernel_fork f) (hc : is_limit c)
  [has_zero_object C] :
  left_homology_data (short_complex.mk c.Î¹ f (kernel_fork.condition c)) :=
{ K := c.X,
  H := 0,
  i := c.Î¹,
  Ï€ := 0,
  wi := kernel_fork.condition _,
  hi := is_limit.of_iso_limit hc (fork.ext (iso.refl _) (by simp)),
  wÏ€ := subsingleton.elim _ _,
  hÏ€ := begin
    let l := hc.lift (kernel_fork.of_Î¹ (fork.Î¹ c) (kernel_fork.condition c)),
    have hl : l = ğŸ™ c.X,
    { apply fork.is_limit.hom_ext hc,
      dsimp,
      simp only [fork.is_limit.lift_Î¹, kernel_fork.Î¹_of_Î¹, id_comp], },
    exact cokernel_cofork.is_colimit.of_Ï€ _ _ (Î» A x hx, 0)
      (Î» A x hx, begin
        change (l â‰« ğŸ™ _) â‰« x = 0 at hx,
        dsimp at hx,
        simpa only [hl, comp_id, id_comp, zero_comp] using hx.symm,
      end)
      (Î» A x hx b hb, subsingleton.elim _ _),
  end, }

@[simp]
def kernel_sequence {X Y : C} (f : X âŸ¶ Y) [has_kernel f] [has_zero_object C] :
  left_homology_data (short_complex.mk (kernel.Î¹ f) f (kernel.condition f)) :=
begin
  let h := kernel_sequence' f _ (kernel_is_kernel f),
  exact h,
end

section change

variables {S} {K H : C} {f' : S.Xâ‚ âŸ¶ K} {i : K âŸ¶ S.Xâ‚‚}
  (commf' : f' â‰« i = S.f) (e : K â‰… h.K) (commi : e.hom â‰« h.i = i)
  (Ï€ : K âŸ¶ H) (hÏ€â‚€ : f' â‰« Ï€ = 0) (hÏ€ : is_colimit (cokernel_cofork.of_Ï€ Ï€ hÏ€â‚€))

include commf' commi hÏ€

@[simps]
def change :
  left_homology_data S :=
begin
  have wi : i â‰« S.g = 0 := by rw [â† commi, assoc, h.wi, comp_zero],
  have hi : is_limit (kernel_fork.of_Î¹ i wi) :=
    is_limit.of_iso_limit h.hi (fork.ext e.symm (by simp [â† commi])),
  let f'' := hi.lift (kernel_fork.of_Î¹ S.f S.zero),
  have eq : f'' = f',
  { rw [â† cancel_mono e.hom, â† cancel_mono h.i, assoc, commi],
    dsimp,
    erw fork.is_limit.lift_Î¹,
    simp only [kernel_fork.Î¹_of_Î¹, assoc, commi, commf'], },
  have wÏ€' : f'' â‰« Ï€ = 0 := by rw [eq, hÏ€â‚€],
  have hÏ€' : is_colimit (cokernel_cofork.of_Ï€ Ï€ wÏ€'),
  { let e : parallel_pair f'' 0 â‰… parallel_pair f' 0 :=
      parallel_pair.ext (iso.refl _) (iso.refl _) (by simp [eq]) (by simp),
    equiv_rw (is_colimit.precompose_inv_equiv e _).symm,
    exact is_colimit.of_iso_colimit hÏ€ (cofork.ext (iso.refl _) (by tidy)), },
  exact âŸ¨K, H, i, Ï€, wi, hi, wÏ€', hÏ€'âŸ©,
end

@[simp] lemma change_f' : (h.change commf' e commi Ï€ hÏ€â‚€ hÏ€).f' = f' :=
by rw [â† cancel_mono (h.change commf' e commi Ï€ hÏ€â‚€ hÏ€).i, f'_i, change_i, commf']

end change

end left_homology_data

class has_left_homology : Prop :=
(cond : nonempty S.left_homology_data)

def some_left_homology_data [has_left_homology S] :
  S.left_homology_data := has_left_homology.cond.some

variable {S}

lemma has_left_homology.mk' (h : S.left_homology_data) : has_left_homology S :=
âŸ¨nonempty.intro hâŸ©

@[priority 100]
instance has_left_homology_of_ker_of_coker
  [has_kernel S.g] [has_cokernel (kernel.lift S.g S.f S.zero)] :
  S.has_left_homology := has_left_homology.mk' (left_homology_data.of_ker_of_coker S)

instance has_left_homology_of_has_cokernel {X Y : C} (f : X âŸ¶ Y) (Z : C)
  [has_cokernel f] :
  (short_complex.mk f (0 : Y âŸ¶ Z) comp_zero).has_left_homology :=
has_left_homology.mk' (left_homology_data.of_has_cokernel _ rfl)

instance has_left_homology_of_has_kernel {Y Z : C} (g : Y âŸ¶ Z) (X : C)
  [has_kernel g] :
  (short_complex.mk (0 : X âŸ¶ Y) g zero_comp).has_left_homology :=
has_left_homology.mk' (left_homology_data.of_has_kernel _ rfl)

instance has_left_homology_of_zeros (X Y Z : C) :
  (short_complex.mk (0 : X âŸ¶ Y) (0 : Y âŸ¶ Z) zero_comp).has_left_homology :=
has_left_homology.mk' (left_homology_data.of_zeros _ rfl rfl)

section

variables {Sâ‚ Sâ‚‚} (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data)

structure left_homology_map_data :=
(Ï†K : hâ‚.K âŸ¶ hâ‚‚.K)
(Ï†H : hâ‚.H âŸ¶ hâ‚‚.H)
(commi' : Ï†K â‰« hâ‚‚.i = hâ‚.i â‰« Ï†.Ï„â‚‚ . obviously)
(commf'' : hâ‚.f' â‰« Ï†K = Ï†.Ï„â‚ â‰« hâ‚‚.f' . obviously)
(commÏ€' : hâ‚.Ï€ â‰« Ï†H = Ï†K â‰« hâ‚‚.Ï€ . obviously)

namespace left_homology_map_data

restate_axiom commi'
restate_axiom commÏ€'
restate_axiom commf''
attribute [simp, reassoc] commi commf' commÏ€

@[simps]
def zero (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  left_homology_map_data 0 hâ‚ hâ‚‚ :=
{ Ï†K := 0,
  Ï†H := 0, }

@[simps]
def id (h : S.left_homology_data) : left_homology_map_data (ğŸ™ S) h h :=
{ Ï†K := ğŸ™ _,
  Ï†H := ğŸ™ _, }

@[simps]
def comp {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {Ï†' : Sâ‚‚ âŸ¶ Sâ‚ƒ} {hâ‚ : Sâ‚.left_homology_data}
  {hâ‚‚ : Sâ‚‚.left_homology_data} {hâ‚ƒ : Sâ‚ƒ.left_homology_data}
  (Ïˆ : left_homology_map_data Ï† hâ‚ hâ‚‚) (Ïˆ' : left_homology_map_data Ï†' hâ‚‚ hâ‚ƒ) :
  left_homology_map_data (Ï† â‰« Ï†') hâ‚ hâ‚ƒ :=
{ Ï†K := Ïˆ.Ï†K â‰« Ïˆ'.Ï†K,
  Ï†H := Ïˆ.Ï†H â‰« Ïˆ'.Ï†H, }

instance : subsingleton (left_homology_map_data Ï† hâ‚ hâ‚‚) :=
âŸ¨Î» Ïˆâ‚ Ïˆâ‚‚, begin
  have hK : Ïˆâ‚.Ï†K = Ïˆâ‚‚.Ï†K := by simp [â† cancel_mono hâ‚‚.i],
  have hH : Ïˆâ‚.Ï†H = Ïˆâ‚‚.Ï†H := by simp [â† cancel_epi hâ‚.Ï€, hK],
  cases Ïˆâ‚,
  cases Ïˆâ‚‚,
  simp only,
  tauto,
endâŸ©

instance : inhabited (left_homology_map_data Ï† hâ‚ hâ‚‚) :=
âŸ¨begin
  let Ï†K : hâ‚.K âŸ¶ hâ‚‚.K := hâ‚‚.lift_K (hâ‚.i â‰« Ï†.Ï„â‚‚)
    (by rw [assoc, Ï†.commâ‚‚â‚ƒ, hâ‚.wi_assoc, zero_comp]),
  have commf' : hâ‚.f' â‰« Ï†K = Ï†.Ï„â‚ â‰« hâ‚‚.f',
  { simp only [â† cancel_mono hâ‚‚.i, assoc, left_homology_data.lift_K_i,
      left_homology_data.f'_i_assoc, left_homology_data.f'_i, Ï†.commâ‚â‚‚], },
  let Ï†H : hâ‚.H âŸ¶ hâ‚‚.H := hâ‚.desc_H (Ï†K â‰« hâ‚‚.Ï€)
    (by rw [reassoc_of commf', hâ‚‚.f'_Ï€, comp_zero]),
  exact âŸ¨Ï†K, Ï†H, by simp, commf', by simpâŸ©,
endâŸ©

instance : unique (left_homology_map_data Ï† hâ‚ hâ‚‚) := unique.mk' _

def some : left_homology_map_data Ï† hâ‚ hâ‚‚ := default

variables {Ï† hâ‚ hâ‚‚}

lemma congr_Ï†H {Î³â‚ Î³â‚‚ : left_homology_map_data Ï† hâ‚ hâ‚‚} (eq : Î³â‚ = Î³â‚‚) :
  Î³â‚.Ï†H = Î³â‚‚.Ï†H := by rw eq
lemma congr_Ï†K {Î³â‚ Î³â‚‚ : left_homology_map_data Ï† hâ‚ hâ‚‚} (eq : Î³â‚ = Î³â‚‚) :
  Î³â‚.Ï†K = Î³â‚‚.Ï†K := by rw eq

@[simp]
def of_zeros (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hfâ‚ : Sâ‚.f = 0) (hgâ‚ : Sâ‚.g = 0) (hfâ‚‚ : Sâ‚‚.f = 0) (hgâ‚‚ : Sâ‚‚.g = 0) :
  left_homology_map_data Ï† (left_homology_data.of_zeros Sâ‚ hfâ‚ hgâ‚)
    (left_homology_data.of_zeros Sâ‚‚ hfâ‚‚ hgâ‚‚) :=
{ Ï†K := Ï†.Ï„â‚‚,
  Ï†H := Ï†.Ï„â‚‚,
  commf'' := by simp only [left_homology_data.of_zeros_f', Ï†.commâ‚â‚‚], }

@[simps]
def of_colimit_cokernel_coforks (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
  (hgâ‚ : Sâ‚.g = 0) (câ‚ : cokernel_cofork Sâ‚.f) (hcâ‚ : is_colimit câ‚)
  (hgâ‚‚ : Sâ‚‚.g = 0) (câ‚‚ : cokernel_cofork Sâ‚‚.f) (hcâ‚‚ : is_colimit câ‚‚) (f : câ‚.X âŸ¶ câ‚‚.X)
  (comm : Ï†.Ï„â‚‚ â‰« câ‚‚.Ï€ = câ‚.Ï€ â‰« f) :
  left_homology_map_data Ï† (left_homology_data.of_colimit_cokernel_cofork Sâ‚ hgâ‚ câ‚ hcâ‚)
    (left_homology_data.of_colimit_cokernel_cofork Sâ‚‚ hgâ‚‚ câ‚‚ hcâ‚‚) :=
{ Ï†K := Ï†.Ï„â‚‚,
  Ï†H := f,
  commi' := by { dsimp, simp only [comp_id, id_comp], },
  commf'' := by { simp only [left_homology_data.of_colimit_cokernel_cofork_f', Ï†.commâ‚â‚‚], },
  commÏ€' := comm.symm, }

@[simps]
def of_limit_kernel_forks (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
  (hfâ‚ : Sâ‚.f = 0) (câ‚ : kernel_fork Sâ‚.g) (hcâ‚ : is_limit câ‚)
  (hfâ‚‚ : Sâ‚‚.f = 0) (câ‚‚ : kernel_fork Sâ‚‚.g) (hcâ‚‚ : is_limit câ‚‚) (f : câ‚.X âŸ¶ câ‚‚.X)
  (comm : câ‚.Î¹ â‰« Ï†.Ï„â‚‚ = f â‰« câ‚‚.Î¹) :
  left_homology_map_data Ï† (left_homology_data.of_limit_kernel_fork Sâ‚ hfâ‚ câ‚ hcâ‚)
    (left_homology_data.of_limit_kernel_fork Sâ‚‚ hfâ‚‚ câ‚‚ hcâ‚‚) :=
{ Ï†K := f,
  Ï†H := f,
  commi' := comm.symm,
  commf'' := by simp only [left_homology_data.of_limit_kernel_fork_f', zero_comp, comp_zero], }

variable (S)

@[simps]
def compatibility_of_zeros_of_colimit_cokernel_cofork (hf : S.f = 0) (hg : S.g = 0)
  (c : cokernel_cofork S.f) (hc : is_colimit c) :
  left_homology_map_data (ğŸ™ S) (left_homology_data.of_zeros S hf hg)
    (left_homology_data.of_colimit_cokernel_cofork S hg c hc):=
{ Ï†K := ğŸ™ _,
  Ï†H := c.Ï€, }

@[simps]
def compatibility_of_zeros_of_limit_kernel_fork (hf : S.f = 0) (hg : S.g = 0)
  (c : kernel_fork S.g) (hc : is_limit c) :
  left_homology_map_data (ğŸ™ S)
    (left_homology_data.of_limit_kernel_fork S hf c hc)
    (left_homology_data.of_zeros S hf hg):=
{ Ï†K := c.Î¹,
  Ï†H := c.Î¹, }

end left_homology_map_data

end

variable (S)

def left_homology [has_left_homology S] : C := S.some_left_homology_data.H
def cycles [has_left_homology S] : C := S.some_left_homology_data.K
def left_homology_Ï€ [has_left_homology S] : S.cycles âŸ¶ S.left_homology :=
  S.some_left_homology_data.Ï€
def cycles_i [has_left_homology S] : S.cycles âŸ¶ S.Xâ‚‚ := S.some_left_homology_data.i
def to_cycles [has_left_homology S] : S.Xâ‚ âŸ¶ S.cycles := S.some_left_homology_data.f'

@[simp, reassoc] lemma cycles_i_g [has_left_homology S] : S.cycles_i â‰« S.g = 0 :=
S.some_left_homology_data.wi

@[simp, reassoc] lemma to_cycles_i [has_left_homology S] : S.to_cycles â‰« S.cycles_i = S.f :=
S.some_left_homology_data.f'_i

instance [has_left_homology S] : mono S.cycles_i :=
by { dsimp only [cycles_i], apply_instance, }

instance [has_left_homology S] : epi S.left_homology_Ï€ :=
by { dsimp only [left_homology_Ï€], apply_instance, }

variables {S Sâ‚ Sâ‚‚ Sâ‚ƒ}

def left_homology_map' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  hâ‚.H âŸ¶ hâ‚‚.H := (left_homology_map_data.some Ï† _ _).Ï†H

def cycles_map' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  hâ‚.K âŸ¶ hâ‚‚.K := (left_homology_map_data.some Ï† _ _).Ï†K

@[simp, reassoc]
lemma cycles_map'_i (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  cycles_map' Ï† hâ‚ hâ‚‚ â‰« hâ‚‚.i = hâ‚.i â‰« Ï†.Ï„â‚‚ :=
left_homology_map_data.commi _

@[simp, reassoc]
lemma left_homology_Ï€_naturality' (Ï† : Sâ‚ âŸ¶ Sâ‚‚)
  (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  hâ‚.Ï€ â‰« left_homology_map' Ï† hâ‚ hâ‚‚ = cycles_map' Ï† hâ‚ hâ‚‚ â‰« hâ‚‚.Ï€ :=
left_homology_map_data.commÏ€ _

def left_homology_map [has_left_homology Sâ‚] [has_left_homology Sâ‚‚]
  (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚.left_homology âŸ¶ Sâ‚‚.left_homology :=
left_homology_map' Ï† _ _

def cycles_map [has_left_homology Sâ‚] [has_left_homology Sâ‚‚]
  (Ï† : Sâ‚ âŸ¶ Sâ‚‚) : Sâ‚.cycles âŸ¶ Sâ‚‚.cycles :=
cycles_map' Ï† _ _

@[simp, reassoc]
lemma cycles_map_i (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.has_left_homology] [Sâ‚‚.has_left_homology] :
  cycles_map Ï† â‰« Sâ‚‚.cycles_i = Sâ‚.cycles_i â‰« Ï†.Ï„â‚‚ :=
cycles_map'_i _ _ _

@[simp, reassoc]
lemma to_cycles_naturality (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.has_left_homology] [Sâ‚‚.has_left_homology] :
  Sâ‚.to_cycles â‰« cycles_map Ï† = Ï†.Ï„â‚ â‰« Sâ‚‚.to_cycles :=
by simp only [â† cancel_mono Sâ‚‚.cycles_i, Ï†.commâ‚â‚‚, assoc, to_cycles_i,
  cycles_map_i, to_cycles_i_assoc]

@[simp, reassoc]
lemma left_homology_Ï€_naturality [has_left_homology Sâ‚] [has_left_homology Sâ‚‚]
  (Ï† : Sâ‚ âŸ¶ Sâ‚‚) :
  Sâ‚.left_homology_Ï€ â‰« left_homology_map Ï† = cycles_map Ï† â‰« Sâ‚‚.left_homology_Ï€ :=
left_homology_Ï€_naturality' _ _ _

namespace left_homology_map_data

variables {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {hâ‚ : Sâ‚.left_homology_data} {hâ‚‚ : Sâ‚‚.left_homology_data}
  (Î³ : left_homology_map_data Ï† hâ‚ hâ‚‚)

lemma left_homology_map'_eq : left_homology_map' Ï† hâ‚ hâ‚‚ = Î³.Ï†H :=
left_homology_map_data.congr_Ï†H (subsingleton.elim _ _)

lemma cycles_map'_eq : cycles_map' Ï† hâ‚ hâ‚‚ = Î³.Ï†K :=
left_homology_map_data.congr_Ï†K (subsingleton.elim _ _)

end left_homology_map_data

@[simp]
lemma left_homology_map'_id (h : S.left_homology_data) :
  left_homology_map' (ğŸ™ S) h h = ğŸ™ _ :=
(left_homology_map_data.id h).left_homology_map'_eq

@[simp]
lemma cycles_map'_id (h : S.left_homology_data) :
  cycles_map' (ğŸ™ S) h h = ğŸ™ _ :=
(left_homology_map_data.id h).cycles_map'_eq

variable (S)

@[simp]
lemma left_homology_map_id [has_left_homology S] :
  left_homology_map (ğŸ™ S) = ğŸ™ _ :=
left_homology_map'_id _

@[simp]
lemma cycles_map_id [has_left_homology S] :
  cycles_map (ğŸ™ S) = ğŸ™ _ :=
cycles_map'_id _

variables {Sâ‚ Sâ‚‚}

@[simp]
lemma left_homology_map'_zero (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  left_homology_map' 0 hâ‚ hâ‚‚ = 0 :=
(left_homology_map_data.zero hâ‚ hâ‚‚).left_homology_map'_eq

@[simp]
lemma cycles_map'_zero (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  cycles_map' 0 hâ‚ hâ‚‚ = 0 :=
(left_homology_map_data.zero hâ‚ hâ‚‚).cycles_map'_eq

variables (Sâ‚ Sâ‚‚)
@[simp]
lemma left_homology_map_zero [has_left_homology Sâ‚] [has_left_homology Sâ‚‚] :
  left_homology_map (0 : Sâ‚ âŸ¶ Sâ‚‚) = 0 :=
left_homology_map'_zero _ _

@[simp]
lemma cycles_map_zero [has_left_homology Sâ‚] [has_left_homology Sâ‚‚] :
  cycles_map (0 : Sâ‚ âŸ¶ Sâ‚‚) = 0 :=
cycles_map'_zero _ _

variables {Sâ‚ Sâ‚‚}

lemma left_homology_map'_comp (Ï†â‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚ : Sâ‚‚ âŸ¶ Sâ‚ƒ)
  (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) (hâ‚ƒ : Sâ‚ƒ.left_homology_data) :
  left_homology_map' (Ï†â‚ â‰« Ï†â‚‚) hâ‚ hâ‚ƒ = left_homology_map' Ï†â‚ hâ‚ hâ‚‚ â‰«
    left_homology_map' Ï†â‚‚ hâ‚‚ hâ‚ƒ :=
begin
  let Î³â‚ := left_homology_map_data.some Ï†â‚ _ _,
  let Î³â‚‚ := left_homology_map_data.some Ï†â‚‚ _ _,
  rw [Î³â‚.left_homology_map'_eq, Î³â‚‚.left_homology_map'_eq, (Î³â‚.comp Î³â‚‚).left_homology_map'_eq,
    left_homology_map_data.comp_Ï†H],
end

lemma cycles_map'_comp (Ï†â‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚ : Sâ‚‚ âŸ¶ Sâ‚ƒ)
  (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) (hâ‚ƒ : Sâ‚ƒ.left_homology_data) :
  cycles_map' (Ï†â‚ â‰« Ï†â‚‚) hâ‚ hâ‚ƒ = cycles_map' Ï†â‚ hâ‚ hâ‚‚ â‰«
    cycles_map' Ï†â‚‚ hâ‚‚ hâ‚ƒ :=
begin
  let Î³â‚ := left_homology_map_data.some Ï†â‚ _ _,
  let Î³â‚‚ := left_homology_map_data.some Ï†â‚‚ _ _,
  rw [Î³â‚.cycles_map'_eq, Î³â‚‚.cycles_map'_eq, (Î³â‚.comp Î³â‚‚).cycles_map'_eq,
    left_homology_map_data.comp_Ï†K],
end

@[simp]
lemma left_homology_map_comp [has_left_homology Sâ‚] [has_left_homology Sâ‚‚] [has_left_homology Sâ‚ƒ]
  (Ï†â‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚ : Sâ‚‚ âŸ¶ Sâ‚ƒ) :
  left_homology_map (Ï†â‚ â‰« Ï†â‚‚) = left_homology_map Ï†â‚ â‰« left_homology_map Ï†â‚‚ :=
left_homology_map'_comp _ _ _ _ _

@[simp]
lemma cycles_map_comp [has_left_homology Sâ‚] [has_left_homology Sâ‚‚] [has_left_homology Sâ‚ƒ]
  (Ï†â‚ : Sâ‚ âŸ¶ Sâ‚‚) (Ï†â‚‚ : Sâ‚‚ âŸ¶ Sâ‚ƒ) :
  cycles_map (Ï†â‚ â‰« Ï†â‚‚) = cycles_map Ï†â‚ â‰« cycles_map Ï†â‚‚ :=
cycles_map'_comp _ _ _ _ _

@[simps]
def left_homology_map_iso' (e : Sâ‚ â‰… Sâ‚‚) (hâ‚ : Sâ‚.left_homology_data)
  (hâ‚‚ : Sâ‚‚.left_homology_data) : hâ‚.H â‰… hâ‚‚.H :=
{ hom := left_homology_map' e.hom hâ‚ hâ‚‚,
  inv := left_homology_map' e.inv hâ‚‚ hâ‚,
  hom_inv_id' := by rw [â† left_homology_map'_comp, e.hom_inv_id, left_homology_map'_id],
  inv_hom_id' := by rw [â† left_homology_map'_comp, e.inv_hom_id, left_homology_map'_id], }

instance is_iso_left_homology_map'_of_iso (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [is_iso Ï†]
  (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  is_iso (left_homology_map' Ï† hâ‚ hâ‚‚) :=
by { change is_iso (left_homology_map_iso' (as_iso Ï†) hâ‚ hâ‚‚).hom, apply_instance, }

@[simps]
def cycles_map_iso' (e : Sâ‚ â‰… Sâ‚‚) (hâ‚ : Sâ‚.left_homology_data)
  (hâ‚‚ : Sâ‚‚.left_homology_data) : hâ‚.K â‰… hâ‚‚.K :=
{ hom := cycles_map' e.hom hâ‚ hâ‚‚,
  inv := cycles_map' e.inv hâ‚‚ hâ‚,
  hom_inv_id' := by rw [â† cycles_map'_comp, e.hom_inv_id, cycles_map'_id],
  inv_hom_id' := by rw [â† cycles_map'_comp, e.inv_hom_id, cycles_map'_id], }

instance is_iso_cycles_map'_of_iso (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [is_iso Ï†]
  (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data) :
  is_iso (cycles_map' Ï† hâ‚ hâ‚‚) :=
by { change is_iso (cycles_map_iso' (as_iso Ï†) hâ‚ hâ‚‚).hom, apply_instance, }

@[simps]
def left_homology_map_iso (e : Sâ‚ â‰… Sâ‚‚) [Sâ‚.has_left_homology]
  [Sâ‚‚.has_left_homology] : Sâ‚.left_homology â‰… Sâ‚‚.left_homology :=
{ hom := left_homology_map e.hom,
  inv := left_homology_map e.inv,
  hom_inv_id' := by rw [â† left_homology_map_comp, e.hom_inv_id, left_homology_map_id],
  inv_hom_id' := by rw [â† left_homology_map_comp, e.inv_hom_id, left_homology_map_id], }

instance is_iso_left_homology_map_of_iso (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [is_iso Ï†] [Sâ‚.has_left_homology]
  [Sâ‚‚.has_left_homology] :
  is_iso (left_homology_map Ï†) :=
by { change is_iso (left_homology_map_iso (as_iso Ï†)).hom, apply_instance, }

@[simps]
def cycles_map_iso (e : Sâ‚ â‰… Sâ‚‚) [Sâ‚.has_left_homology]
  [Sâ‚‚.has_left_homology] : Sâ‚.cycles â‰… Sâ‚‚.cycles :=
{ hom := cycles_map e.hom,
  inv := cycles_map e.inv,
  hom_inv_id' := by rw [â† cycles_map_comp, e.hom_inv_id, cycles_map_id],
  inv_hom_id' := by rw [â† cycles_map_comp, e.inv_hom_id, cycles_map_id], }

instance is_iso_cycles_map_of_iso (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [is_iso Ï†] [Sâ‚.has_left_homology]
  [Sâ‚‚.has_left_homology] :
  is_iso (cycles_map Ï†) :=
by { change is_iso (cycles_map_iso (as_iso Ï†)).hom, apply_instance, }

variable {S}

def left_homology_data.left_homology_iso (h : S.left_homology_data) [S.has_left_homology] :
  S.left_homology â‰… h.H := left_homology_map_iso' (iso.refl _) _ _

def left_homology_data.cycles_iso (h : S.left_homology_data) [S.has_left_homology] :
  S.cycles â‰… h.K := cycles_map_iso' (iso.refl _) _ _

@[simp, reassoc]
lemma left_homology_data.cycles_iso_hom_comp_i (h : S.left_homology_data) [S.has_left_homology] :
  h.cycles_iso.hom â‰« h.i = S.cycles_i :=
begin
  dsimp [cycles_i, left_homology_data.cycles_iso],
  simp only [cycles_map'_i, id_Ï„â‚‚, comp_id],
end

@[simp, reassoc]
lemma left_homology_data.cycles_iso_inv_comp_cycles_i (h : S.left_homology_data)
  [S.has_left_homology] :
  h.cycles_iso.inv â‰« S.cycles_i = h.i :=
by simp only [â† h.cycles_iso_hom_comp_i, iso.inv_hom_id_assoc]

namespace left_homology_map_data

variables {Ï† : Sâ‚ âŸ¶ Sâ‚‚} {hâ‚ : Sâ‚.left_homology_data} {hâ‚‚ : Sâ‚‚.left_homology_data}
  (Î³ : left_homology_map_data Ï† hâ‚ hâ‚‚)
lemma left_homology_map_eq [Sâ‚.has_left_homology] [Sâ‚‚.has_left_homology] :
  left_homology_map Ï† = hâ‚.left_homology_iso.hom â‰« Î³.Ï†H â‰« hâ‚‚.left_homology_iso.inv :=
begin
  dsimp [left_homology_data.left_homology_iso, left_homology_map_iso'],
  rw [â† Î³.left_homology_map'_eq, â† left_homology_map'_comp, â† left_homology_map'_comp, id_comp, comp_id],
  refl,
end

lemma cycles_map_eq [Sâ‚.has_left_homology] [Sâ‚‚.has_left_homology] :
  cycles_map Ï† = hâ‚.cycles_iso.hom â‰« Î³.Ï†K â‰« hâ‚‚.cycles_iso.inv :=
begin
  dsimp [left_homology_data.cycles_iso, cycles_map_iso'],
  rw [â† Î³.cycles_map'_eq, â† cycles_map'_comp, â† cycles_map'_comp, id_comp, comp_id],
  refl,
end

lemma left_homology_map_comm [Sâ‚.has_left_homology] [Sâ‚‚.has_left_homology] :
  left_homology_map Ï† â‰« hâ‚‚.left_homology_iso.hom = hâ‚.left_homology_iso.hom â‰« Î³.Ï†H :=
by simp only [Î³.left_homology_map_eq, assoc, iso.inv_hom_id, comp_id]

lemma cycles_map_comm [Sâ‚.has_left_homology] [Sâ‚‚.has_left_homology] :
  cycles_map Ï† â‰« hâ‚‚.cycles_iso.hom = hâ‚.cycles_iso.hom â‰« Î³.Ï†K :=
by simp only [Î³.cycles_map_eq, assoc, iso.inv_hom_id, comp_id]

end left_homology_map_data

variable (C)
/-- We shall say that a category with left homology is a category for which
all short complexes have left homology. -/
abbreviation _root_.category_with_left_homology := âˆ€ (S : short_complex C), S.has_left_homology

@[simps]
def left_homology_functor [category_with_left_homology C] :
  short_complex C â¥¤ C :=
{ obj := Î» S, S.left_homology,
  map := Î» Sâ‚ Sâ‚‚, left_homology_map, }

@[simps]
def cycles_functor [category_with_left_homology C] :
  short_complex C â¥¤ C :=
{ obj := Î» S, S.cycles,
  map := Î» Sâ‚ Sâ‚‚, cycles_map, }

@[simps]
def left_homology_Ï€_nat_trans [category_with_left_homology C] :
  cycles_functor C âŸ¶ left_homology_functor C :=
{ app := Î» S, left_homology_Ï€ S,
  naturality' := Î» Sâ‚ Sâ‚‚ Ï†, (left_homology_Ï€_naturality Ï†).symm, }

@[simps]
def cycles_i_nat_trans [category_with_left_homology C] :
  cycles_functor C âŸ¶ short_complex.Ï€â‚‚ :=
{ app := Î» S, S.cycles_i, }

@[simps]
def to_cycles_nat_trans [category_with_left_homology C] :
  Ï€â‚ âŸ¶ cycles_functor C :=
{ app := Î» S, S.to_cycles,
  naturality' := Î» Sâ‚ Sâ‚‚ Ï†, (to_cycles_naturality Ï†).symm, }

namespace left_homology_data

variable {C}

@[simps]
def of_epi_of_is_iso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : left_homology_data Sâ‚)
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] : left_homology_data Sâ‚‚ :=
begin
  let i : h.K âŸ¶ Sâ‚‚.Xâ‚‚ := h.i â‰« Ï†.Ï„â‚‚,
  have wi : i â‰« Sâ‚‚.g = 0 := by simp only [assoc, Ï†.commâ‚‚â‚ƒ, h.wi_assoc, zero_comp],
  have hi : is_limit (kernel_fork.of_Î¹ i wi) := kernel_fork.is_limit.of_Î¹ _ _
    (Î» A x hx, h.lift_K (x â‰« inv Ï†.Ï„â‚‚) (by simp only [assoc, â† cancel_mono Ï†.Ï„â‚ƒ,
      zero_comp, â† Ï†.commâ‚‚â‚ƒ, is_iso.inv_hom_id_assoc, hx]))
    (Î» A x hx, by simp only [assoc, lift_K_i_assoc, is_iso.inv_hom_id, comp_id])
    (Î» A x hx b hx, by simp only [â† cancel_mono h.i, â† cancel_mono Ï†.Ï„â‚‚,
        assoc, lift_K_i, is_iso.inv_hom_id, comp_id, hx]),
  let f' := hi.lift (kernel_fork.of_Î¹ Sâ‚‚.f Sâ‚‚.zero),
  have hf' : Ï†.Ï„â‚ â‰« f' = h.f',
  { have eq := @fork.is_limit.lift_Î¹ _ _ _ _ _ _ _ ((kernel_fork.of_Î¹ Sâ‚‚.f Sâ‚‚.zero)) hi,
    simp only [kernel_fork.Î¹_of_Î¹] at eq,
    simp only [â† cancel_mono h.i, â† cancel_mono Ï†.Ï„â‚‚, assoc, eq, f'_i_assoc, Ï†.commâ‚â‚‚], },
  have wÏ€ : f' â‰« h.Ï€ = 0,
  { rw [â† cancel_epi Ï†.Ï„â‚, comp_zero, reassoc_of hf', h.f'_Ï€], },
  have hÏ€ : is_colimit (cokernel_cofork.of_Ï€ h.Ï€ wÏ€) := cokernel_cofork.is_colimit.of_Ï€ _ _
    (Î» A x hx, h.desc_H x (by rw [â† hf', assoc, hx, comp_zero]))
    (Î» A x hx, Ï€_desc_H _ _ _)
    (Î» A x hx b hb, by simp only [â† cancel_epi h.Ï€, Ï€_desc_H, hb]),
  exact âŸ¨h.K, h.H, i, h.Ï€, wi, hi, wÏ€, hÏ€âŸ©,
end

@[simp]
lemma of_epi_of_is_iso_of_mono_Ï„â‚_f' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : left_homology_data Sâ‚)
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] : Ï†.Ï„â‚ â‰« (of_epi_of_is_iso_of_mono Ï† h).f' = h.f' :=
by rw [â† cancel_mono (of_epi_of_is_iso_of_mono Ï† h).i, assoc, f'_i,
    of_epi_of_is_iso_of_mono_i, f'_i_assoc, Ï†.commâ‚â‚‚]

@[simps]
def of_epi_of_is_iso_of_mono' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : left_homology_data Sâ‚‚)
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] : left_homology_data Sâ‚ :=
begin
  let i : h.K âŸ¶ Sâ‚.Xâ‚‚ := h.i â‰« inv Ï†.Ï„â‚‚,
  have wi : i â‰« Sâ‚.g = 0 := by simp only [assoc, â† cancel_mono Ï†.Ï„â‚ƒ, zero_comp,
    â† Ï†.commâ‚‚â‚ƒ, is_iso.inv_hom_id_assoc, h.wi],
  have hi : is_limit (kernel_fork.of_Î¹ i wi) := kernel_fork.is_limit.of_Î¹ _ _
    (Î» A x hx, h.lift_K (x â‰« Ï†.Ï„â‚‚) (by rw [assoc, Ï†.commâ‚‚â‚ƒ, reassoc_of hx, zero_comp]))
    (Î» A x hx, by simp only [assoc, lift_K_i_assoc, is_iso.hom_inv_id, comp_id])
    (Î» A x hx b hb, by simp only [â† cancel_mono h.i, lift_K_i, â† hb,
      assoc, is_iso.inv_hom_id, comp_id]),
  let f' := hi.lift (kernel_fork.of_Î¹ Sâ‚.f Sâ‚.zero),
  have hf' : f' â‰« i = Sâ‚.f := by simpa only [kernel_fork.Î¹_of_Î¹]
    using @fork.is_limit.lift_Î¹ _ _ _ _ _ _ _ ((kernel_fork.of_Î¹ Sâ‚.f Sâ‚.zero)) hi,
  have hf'' : f' = Ï†.Ï„â‚ â‰« h.f',
  { simpa only [â† cancel_mono h.i, â† cancel_mono (inv Ï†.Ï„â‚‚), assoc, f'_i_assoc, Ï†.commâ‚â‚‚_assoc,
      is_iso.hom_inv_id, comp_id] using fork.is_limit.lift_Î¹ _, },
  have wÏ€ : f' â‰« h.Ï€ = 0 := by simp only [hf'', assoc, f'_Ï€, comp_zero],
  have hÏ€ : is_colimit (cokernel_cofork.of_Ï€ h.Ï€ wÏ€) := cokernel_cofork.is_colimit.of_Ï€ _ _
    (Î» A x hx, h.desc_H x (by rw [â† cancel_epi Ï†.Ï„â‚, â† reassoc_of hf'', hx, comp_zero]))
    (Î» A x hx, Ï€_desc_H _ _ _)
    (Î» A x hx b hx, by simp only [â† cancel_epi h.Ï€, Ï€_desc_H, hx]),
  exact âŸ¨h.K, h.H, i, h.Ï€, wi, hi, wÏ€, hÏ€âŸ©,
end

@[simp]
lemma of_epi_of_is_iso_of_mono'_f' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : left_homology_data Sâ‚‚)
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] :
  (of_epi_of_is_iso_of_mono' Ï† h).f' = Ï†.Ï„â‚ â‰« h.f' :=
by rw [â† cancel_mono (of_epi_of_is_iso_of_mono' Ï† h).i, f'_i, of_epi_of_is_iso_of_mono'_i,
    assoc, f'_i_assoc, Ï†.commâ‚â‚‚_assoc, is_iso.hom_inv_id, comp_id]

def of_iso (e : Sâ‚ â‰… Sâ‚‚) (hâ‚ : left_homology_data Sâ‚) : left_homology_data Sâ‚‚ :=
hâ‚.of_epi_of_is_iso_of_mono e.hom

end left_homology_data

variables {C}

lemma has_left_homology_of_epi_of_is_iso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [has_left_homology Sâ‚]
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] : has_left_homology Sâ‚‚ :=
has_left_homology.mk' (left_homology_data.of_epi_of_is_iso_of_mono Ï† Sâ‚.some_left_homology_data)

lemma has_left_homology_of_epi_of_is_iso_of_mono' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [has_left_homology Sâ‚‚]
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] : has_left_homology Sâ‚ :=
has_left_homology.mk' (left_homology_data.of_epi_of_is_iso_of_mono' Ï† Sâ‚‚.some_left_homology_data)

lemma has_left_homology_of_iso {Sâ‚ Sâ‚‚ : short_complex C}
  (e : Sâ‚ â‰… Sâ‚‚) [has_left_homology Sâ‚] : has_left_homology Sâ‚‚ :=
has_left_homology_of_epi_of_is_iso_of_mono e.hom

namespace left_homology_map_data

@[simps]
def of_epi_of_is_iso_of_mono (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : left_homology_data Sâ‚)
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] :
    left_homology_map_data Ï† h (left_homology_data.of_epi_of_is_iso_of_mono Ï† h) :=
{ Ï†K := ğŸ™ _,
  Ï†H := ğŸ™ _,
  commf'' := by simp only [left_homology_data.of_epi_of_is_iso_of_mono_Ï„â‚_f' Ï† h, comp_id], }

@[simps]
def of_epi_of_is_iso_of_mono' (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (h : left_homology_data Sâ‚‚)
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] :
    left_homology_map_data Ï† (left_homology_data.of_epi_of_is_iso_of_mono' Ï† h) h :=
{ Ï†K := ğŸ™ _,
  Ï†H := ğŸ™ _, }

end left_homology_map_data

instance (Ï† : Sâ‚ âŸ¶ Sâ‚‚) (hâ‚ : Sâ‚.left_homology_data) (hâ‚‚ : Sâ‚‚.left_homology_data)
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] :
  is_iso (left_homology_map' Ï† hâ‚ hâ‚‚) :=
begin
  let hâ‚‚' := left_homology_data.of_epi_of_is_iso_of_mono Ï† hâ‚,
  haveI : is_iso (left_homology_map' Ï† hâ‚ hâ‚‚'),
  { let Î³ := left_homology_map_data.of_epi_of_is_iso_of_mono Ï† hâ‚,
    rw Î³.left_homology_map'_eq,
    dsimp,
    apply_instance, },
  have eq := left_homology_map'_comp Ï† (ğŸ™ Sâ‚‚) hâ‚ hâ‚‚' hâ‚‚,
  rw comp_id at eq,
  rw eq,
  apply_instance,
end

instance (Ï† : Sâ‚ âŸ¶ Sâ‚‚) [Sâ‚.has_left_homology] [Sâ‚‚.has_left_homology]
  [epi Ï†.Ï„â‚] [is_iso Ï†.Ï„â‚‚] [mono Ï†.Ï„â‚ƒ] :
  is_iso (left_homology_map Ï†) :=
by { dsimp only [left_homology_map], apply_instance, }

section

variables (S) (h : left_homology_data S)
  {A : C} (k : A âŸ¶ S.Xâ‚‚) (hk : k â‰« S.g = 0) [has_left_homology S]

def lift_cycles : A âŸ¶ S.cycles :=
S.some_left_homology_data.lift_K k hk

@[simp, reassoc]
lemma lift_cycles_i : S.lift_cycles k hk â‰« S.cycles_i = k :=
left_homology_data.lift_K_i _ k hk

@[reassoc]
lemma comp_lift_cycles {A' : C} (Î± : A' âŸ¶ A) :
  Î± â‰« S.lift_cycles k hk = S.lift_cycles (Î± â‰« k) (by rw [assoc, hk, comp_zero]) :=
by simp only [â† cancel_mono S.cycles_i, assoc, lift_cycles_i]

def cycles_is_kernel : is_limit (kernel_fork.of_Î¹ S.cycles_i S.cycles_i_g) :=
S.some_left_homology_data.hi

lemma is_iso_cycles_i_of (hg : S.g = 0) : is_iso (S.cycles_i) :=
kernel_fork.is_limit.is_iso_Î¹_of_zero _ S.cycles_is_kernel hg

@[simps]
def cycles_iso_kernel [has_kernel S.g] : S.cycles â‰… kernel S.g :=
{ hom := kernel.lift S.g S.cycles_i (by simp),
  inv := S.lift_cycles (kernel.Î¹ S.g) (by simp),
  hom_inv_id' := by simp only [â†  cancel_mono S.cycles_i, assoc, lift_cycles_i,
    kernel.lift_Î¹, id_comp],
  inv_hom_id' := by simp only [â† cancel_mono (kernel.Î¹ S.g), assoc, kernel.lift_Î¹,
    lift_cycles_i, id_comp], }

@[simp]
def lift_left_homology : A âŸ¶ S.left_homology :=
S.lift_cycles k hk â‰« S.left_homology_Ï€

lemma lift_cycles_Ï€_eq_zero_of_boundary (x : A âŸ¶ S.Xâ‚) (hx : k = x â‰« S.f) :
S.lift_cycles k (by rw [hx, assoc, S.zero, comp_zero])â‰« S.left_homology_Ï€ = 0 :=
left_homology_data.lift_K_Ï€_eq_zero_of_boundary _ k x hx

@[simp, reassoc]
lemma to_cycles_comp_left_homology_Ï€ :
  S.to_cycles â‰« S.left_homology_Ï€ = 0 :=
S.lift_cycles_Ï€_eq_zero_of_boundary S.f (ğŸ™ _) (by rw id_comp)

def left_homology_is_cokernel :
  is_colimit (cokernel_cofork.of_Ï€ S.left_homology_Ï€ S.to_cycles_comp_left_homology_Ï€) :=
S.some_left_homology_data.hÏ€

@[simp, reassoc]
lemma lift_cycles_comp_cycles_map (Ï† : S âŸ¶ Sâ‚) [Sâ‚.has_left_homology] :
  S.lift_cycles k hk â‰« cycles_map Ï† =
    Sâ‚.lift_cycles (k â‰« Ï†.Ï„â‚‚) (by rw [assoc, Ï†.commâ‚‚â‚ƒ, reassoc_of hk, zero_comp]) :=
by simp only [â† cancel_mono (Sâ‚.cycles_i), assoc, cycles_map_i, lift_cycles_i_assoc, lift_cycles_i]

variable {S}

@[simp, reassoc]
lemma left_homology_data.left_homology_Ï€_comp_left_homology_iso_hom :
  S.left_homology_Ï€ â‰« h.left_homology_iso.hom = h.cycles_iso.hom â‰« h.Ï€ :=
begin
  dsimp only [left_homology_Ï€, left_homology_data.left_homology_iso, left_homology_map_iso',
    iso.refl, left_homology_data.cycles_iso, cycles_map_iso'],
  rw â† left_homology_Ï€_naturality',
end

@[simp, reassoc]
lemma left_homology_data.Ï€_comp_left_homology_iso_inv :
  h.Ï€ â‰« h.left_homology_iso.inv = h.cycles_iso.inv â‰« S.left_homology_Ï€ :=
by simp only [â† cancel_epi h.cycles_iso.hom, â† cancel_mono h.left_homology_iso.hom, assoc,
  iso.inv_hom_id, comp_id, iso.hom_inv_id_assoc,
  left_homology_data.left_homology_Ï€_comp_left_homology_iso_hom]

@[simp, reassoc]
lemma left_homology_data.lift_cycles_comp_cycles_iso_hom :
  S.lift_cycles k hk â‰« h.cycles_iso.hom = h.lift_K k hk :=
by simp only [â†cancel_mono h.i, assoc, left_homology_data.cycles_iso_hom_comp_i,
  lift_cycles_i, left_homology_data.lift_K_i]

@[simp]
lemma left_homology_data.lift_K_comp_cycles_iso_inv :
  h.lift_K k hk â‰« h.cycles_iso.inv = S.lift_cycles k hk :=
by rw [â† h.lift_cycles_comp_cycles_iso_hom, assoc, iso.hom_inv_id, comp_id]

lemma left_homology_data.ext_iff' (fâ‚ fâ‚‚ : S.left_homology âŸ¶ A) :
  fâ‚ = fâ‚‚ â†” h.Ï€ â‰« h.left_homology_iso.inv â‰« fâ‚ = h.Ï€ â‰« h.left_homology_iso.inv â‰« fâ‚‚ :=
by rw [â† cancel_epi h.left_homology_iso.inv, cancel_epi h.Ï€]

end

namespace has_left_homology

variable (S)

@[protected]
lemma has_kernel [S.has_left_homology] : has_kernel S.g :=
âŸ¨âŸ¨âŸ¨_, S.some_left_homology_data.hiâŸ©âŸ©âŸ©

lemma has_cokernel [S.has_left_homology] [has_kernel S.g] :
  has_cokernel (kernel.lift S.g S.f S.zero) :=
begin
  let h := S.some_left_homology_data,
  haveI : has_colimit (parallel_pair h.f' 0) := âŸ¨âŸ¨âŸ¨_, h.hÏ€'âŸ©âŸ©âŸ©,
  let e : parallel_pair (kernel.lift S.g S.f S.zero) 0 â‰… parallel_pair h.f' 0 :=
    parallel_pair.ext (iso.refl _)
      (is_limit.cone_point_unique_up_to_iso (kernel_is_kernel S.g) h.hi) (by tidy) (by tidy),
  exact has_colimit_of_iso e,
end

end has_left_homology

def left_homology_iso_cokernel_lift [S.has_left_homology] [has_kernel S.g]
  [has_cokernel (kernel.lift S.g S.f S.zero)] :
  S.left_homology â‰… cokernel (kernel.lift S.g S.f S.zero) :=
(left_homology_data.of_ker_of_coker S).left_homology_iso

namespace left_homology_data

lemma is_iso_i_of_zero_g (h : left_homology_data S) (hg : S.g = 0) : is_iso h.i :=
âŸ¨âŸ¨h.lift_K (ğŸ™ S.Xâ‚‚) (by rw [hg, id_comp]),
    by simp only [â† cancel_mono h.i, id_comp, assoc, lift_K_i, comp_id], lift_K_i _ _ _âŸ©âŸ©

end left_homology_data

end short_complex

end category_theory
