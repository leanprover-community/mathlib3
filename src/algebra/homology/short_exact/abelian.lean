/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Andrew Yang
-/
import algebra.homology.short_exact.preadditive
import category_theory.abelian.diagram_lemmas.four

/-!
# Short exact sequences in abelian categories

In an abelian category, a left-split short exact sequence admits a splitting.
-/

noncomputable theory

open category_theory category_theory.limits category_theory.preadditive

variables {ğ’œ : Type*} [category ğ’œ]

namespace category_theory

variables {A B C A' B' C' : ğ’œ} {f : A âŸ¶ B} {g : B âŸ¶ C} {f' : A' âŸ¶ B'} {g' : B' âŸ¶ C'}
variables [abelian ğ’œ]
open_locale zero_object

lemma is_iso_of_short_exact_of_is_iso_of_is_iso (h : short_exact f g) (h' : short_exact f' g')
  (iâ‚ : A âŸ¶ A') (iâ‚‚ : B âŸ¶ B') (iâ‚ƒ : C âŸ¶ C')
  (commâ‚ : iâ‚ â‰« f' = f â‰« iâ‚‚) (commâ‚‚ : iâ‚‚ â‰« g' = g â‰« iâ‚ƒ) [is_iso iâ‚] [is_iso iâ‚ƒ] :
  is_iso iâ‚‚ :=
begin
  obtain âŸ¨_, _, _âŸ© := h,
  obtain âŸ¨_, _, _âŸ© := h',
  resetI,
  refine @abelian.is_iso_of_is_iso_of_is_iso_of_is_iso_of_is_iso ğ’œ _ _ 0 _ _ _ 0 _ _ _
    0 f g 0 f' g' 0 iâ‚ iâ‚‚ iâ‚ƒ _ commâ‚ commâ‚‚ 0 0 0 0 0 _ _ _ _ _ _ _ _ _ _ _;
  try { simp };
  try { apply exact_zero_left_of_mono };
  try { assumption };
  rwa â† epi_iff_exact_zero_right,
end

/-- To construct a splitting of `A -fâŸ¶ B -gâŸ¶ C` it suffices to supply
a *morphism* `i : B âŸ¶ A âŠ C` such that `f â‰« i` is the canonical map `biprod.inl : A âŸ¶ A âŠ C` and
`i â‰« q = g`, where `q` is the canonical map `biprod.snd : A âŠ C âŸ¶ C`,
together with proofs that `f` is mono and `g` is epi.

The morphism `i` is than automatically an isomorphism. -/
-- TODO: we may also want the version that supplies a morphism `A âŠ C âŸ¶ B`.
def splitting.mk' (h : short_exact f g) (i : B âŸ¶ A âŠ C)
  (h1 : f â‰« i = biprod.inl) (h2 : i â‰« biprod.snd = g) : splitting f g :=
{ iso :=
  begin
    refine @as_iso _ _ _ _ i (id _),
    refine is_iso_of_short_exact_of_is_iso_of_is_iso h _ _ _ _
      (h1.trans (category.id_comp _).symm).symm (h2.trans (category.comp_id _).symm),
    split,
    apply exact_inl_snd
  end,
  comp_iso_eq_inl := by { rwa as_iso_hom, },
  iso_comp_snd_eq := h2 }

/-- A short exact sequence that is left split admits a splitting. -/
def left_split.splitting {f : A âŸ¶ B} {g : B âŸ¶ C} (h : left_split f g) : splitting f g :=
splitting.mk' h.short_exact (biprod.lift h.left_split.some g)
(by { ext,
  { simpa only [biprod.inl_fst, biprod.lift_fst, category.assoc] using h.left_split.some_spec },
  { simp only [biprod.inl_snd, biprod.lift_snd, category.assoc, h.exact.w], } })
(by { simp only [biprod.lift_snd], })

end category_theory
