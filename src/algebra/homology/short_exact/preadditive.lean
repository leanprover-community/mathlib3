/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Andrew Yang
-/
import algebra.homology.exact
import category_theory.preadditive.additive_functor

/-!
# Short exact sequences, and splittings.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

`short_exact f g` is the proposition that `0 âŸ¶ A -fâŸ¶ B -gâŸ¶ C âŸ¶ 0` is an exact sequence.

We define when a short exact sequence is left-split, right-split, and split.

## See also
In `algebra.homology.short_exact.abelian` we show that in an abelian category
a left-split short exact sequences admits a splitting.
-/

noncomputable theory

open category_theory category_theory.limits category_theory.preadditive

variables {ğ’œ : Type*} [category ğ’œ]

namespace category_theory
variables {A B C A' B' C' : ğ’œ} (f : A âŸ¶ B) (g : B âŸ¶ C) (f' : A' âŸ¶ B') (g' : B' âŸ¶ C')

section has_zero_morphisms
variables [has_zero_morphisms ğ’œ] [has_kernels ğ’œ] [has_images ğ’œ]

/-- If `f : A âŸ¶ B` and `g : B âŸ¶ C` then `short_exact f g` is the proposition saying
  the resulting diagram `0 âŸ¶ A âŸ¶ B âŸ¶ C âŸ¶ 0` is an exact sequence. -/
structure short_exact : Prop :=
[mono  : mono f]
[epi   : epi g]
(exact : exact f g)

/-- An exact sequence `A -fâŸ¶ B -gâŸ¶ C` is *left split*
if there exists a morphism `Ï† : B âŸ¶ A` such that `f â‰« Ï† = ğŸ™ A` and `g` is epi.

Such a sequence is automatically short exact (i.e., `f` is mono). -/
structure left_split : Prop :=
(left_split : âˆƒ Ï† : B âŸ¶ A, f â‰« Ï† = ğŸ™ A)
[epi   : epi g]
(exact : exact f g)

lemma left_split.short_exact {f : A âŸ¶ B} {g : B âŸ¶ C} (h : left_split f g) : short_exact f g :=
{ mono :=
  begin
    obtain âŸ¨Ï†, hÏ†âŸ© := h.left_split,
    haveI : mono (f â‰« Ï†) := by { rw hÏ†, apply_instance },
    exact mono_of_mono f Ï†,
  end,
  epi := h.epi,
  exact := h.exact }

/-- An exact sequence `A -fâŸ¶ B -gâŸ¶ C` is *right split*
if there exists a morphism `Ï† : C âŸ¶ B` such that `f â‰« Ï† = ğŸ™ A` and `f` is mono.

Such a sequence is automatically short exact (i.e., `g` is epi). -/
structure right_split : Prop :=
(right_split : âˆƒ Ï‡ : C âŸ¶ B, Ï‡ â‰« g = ğŸ™ C)
[mono  : mono f]
(exact : exact f g)

lemma right_split.short_exact {f : A âŸ¶ B} {g : B âŸ¶ C} (h : right_split f g) : short_exact f g :=
{ epi :=
  begin
    obtain âŸ¨Ï‡, hÏ‡âŸ© := h.right_split,
    haveI : epi (Ï‡ â‰« g) := by { rw hÏ‡, apply_instance },
    exact epi_of_epi Ï‡ g,
  end,
  mono := h.mono,
  exact := h.exact }

end has_zero_morphisms

section preadditive
variables [preadditive ğ’œ]

/-- An exact sequence `A -fâŸ¶ B -gâŸ¶ C` is *split* if there exist
`Ï† : B âŸ¶ A` and `Ï‡ : C âŸ¶ B` such that:
* `f â‰« Ï† = ğŸ™ A`
* `Ï‡ â‰« g = ğŸ™ C`
* `f â‰« g = 0`
* `Ï‡ â‰« Ï† = 0`
* `Ï† â‰« f + g â‰« Ï‡ = ğŸ™ B`

Such a sequence is automatically short exact (i.e., `f` is mono and `g` is epi). -/
structure split : Prop :=
(split : âˆƒ (Ï† : B âŸ¶ A) (Ï‡ : C âŸ¶ B),
  f â‰« Ï† = ğŸ™ A âˆ§ Ï‡ â‰« g = ğŸ™ C âˆ§ f â‰« g = 0 âˆ§ Ï‡ â‰« Ï† = 0 âˆ§ Ï† â‰« f + g â‰« Ï‡ = ğŸ™ B)

variables [has_kernels ğ’œ] [has_images ğ’œ]

lemma exact_of_split {A B C : ğ’œ} {f : A âŸ¶ B} {g : B âŸ¶ C} {Ï‡ : C âŸ¶ B} {Ï† : B âŸ¶ A}
  (hfg : f â‰« g = 0) (H : Ï† â‰« f + g â‰« Ï‡ = ğŸ™ B) : exact f g :=
{ w := hfg,
  epi :=
  begin
    let Ïˆ : (kernel_subobject g : ğ’œ) âŸ¶ image_subobject f :=
      subobject.arrow _ â‰« Ï† â‰« factor_thru_image_subobject f,
    suffices : Ïˆ â‰« image_to_kernel f g hfg = ğŸ™ _,
    { convert epi_of_epi Ïˆ _, rw this, apply_instance },
    rw â† cancel_mono (subobject.arrow _), swap, { apply_instance },
    simp only [image_to_kernel_arrow, image_subobject_arrow_comp, category.id_comp, category.assoc],
    calc (kernel_subobject g).arrow â‰« Ï† â‰« f
        = (kernel_subobject g).arrow â‰« ğŸ™ B : _
    ... = (kernel_subobject g).arrow        : category.comp_id _,
    rw [â† H, preadditive.comp_add],
    simp only [add_zero, zero_comp, kernel_subobject_arrow_comp_assoc],
  end }

section

variables {f g}

lemma split.exact (h : split f g) : exact f g :=
by { obtain âŸ¨Ï†, Ï‡, -, -, h1, -, h2âŸ© := h, exact exact_of_split h1 h2 }

lemma split.left_split (h : split f g) : left_split f g :=
{ left_split := by { obtain âŸ¨Ï†, Ï‡, h1, -âŸ© := h, exact âŸ¨Ï†, h1âŸ©, },
  epi := begin
    obtain âŸ¨Ï†, Ï‡, -, h2, -âŸ© := h,
    have : epi (Ï‡ â‰« g), { rw h2, apply_instance },
    exactI epi_of_epi Ï‡ g,
  end,
  exact := h.exact }

lemma split.right_split (h : split f g) : right_split f g :=
{ right_split := by { obtain âŸ¨Ï†, Ï‡, -, h1, -âŸ© := h, exact âŸ¨Ï‡, h1âŸ©, },
  mono := begin
    obtain âŸ¨Ï†, Ï‡, h1, -âŸ© := h,
    have : mono (f â‰« Ï†), { rw h1, apply_instance },
    exactI mono_of_mono f Ï†,
  end,
  exact := h.exact }

lemma split.short_exact (h : split f g) : short_exact f g :=
h.left_split.short_exact

end

lemma split.map {ğ’œ â„¬ : Type*} [category ğ’œ] [preadditive ğ’œ] [category â„¬] [preadditive â„¬]
  (F : ğ’œ â¥¤ â„¬) [functor.additive F] {A B C : ğ’œ} {f : A âŸ¶ B} {g : B âŸ¶ C} (h : split f g) :
  split (F.map f) (F.map g) :=
begin
  obtain âŸ¨Ï†, Ï‡, h1, h2, h3, h4, h5âŸ© := h,
  refine âŸ¨âŸ¨F.map Ï†, F.map Ï‡, _âŸ©âŸ©,
  simp only [â† F.map_comp, â† F.map_id, â† F.map_add, F.map_zero, *, eq_self_iff_true, and_true],
end

/-- The sequence `A âŸ¶ A âŠ B âŸ¶ B` is exact. -/
lemma exact_inl_snd [has_binary_biproducts ğ’œ] (A B : ğ’œ) :
  exact (biprod.inl : A âŸ¶ A âŠ B) biprod.snd :=
exact_of_split biprod.inl_snd biprod.total

/-- The sequence `B âŸ¶ A âŠ B âŸ¶ A` is exact. -/
lemma exact_inr_fst [has_binary_biproducts ğ’œ] (A B : ğ’œ) :
  exact (biprod.inr : B âŸ¶ A âŠ B) biprod.fst :=
exact_of_split biprod.inr_fst ((add_comm _ _).trans biprod.total)

end preadditive

/-- A *splitting* of a sequence `A -fâŸ¶ B -gâŸ¶ C` is an isomorphism
to the short exact sequence `0 âŸ¶ A âŸ¶ A âŠ C âŸ¶ C âŸ¶ 0` such that
the vertical maps on the left and the right are the identity. -/
@[nolint has_nonempty_instance]
structure splitting [has_zero_morphisms ğ’œ] [has_binary_biproducts ğ’œ] :=
(iso : B â‰… A âŠ C)
(comp_iso_eq_inl : f â‰« iso.hom = biprod.inl)
(iso_comp_snd_eq : iso.hom â‰« biprod.snd = g)

variables {f g}

namespace splitting

section has_zero_morphisms
variables [has_zero_morphisms ğ’œ] [has_binary_biproducts ğ’œ]

attribute [simp, reassoc] comp_iso_eq_inl iso_comp_snd_eq

variables (h : splitting f g)

@[simp, reassoc] lemma inl_comp_iso_eq : biprod.inl â‰« h.iso.inv = f :=
by rw [iso.comp_inv_eq, h.comp_iso_eq_inl]

@[simp, reassoc] lemma iso_comp_eq_snd : h.iso.inv â‰« g = biprod.snd :=
by rw [iso.inv_comp_eq, h.iso_comp_snd_eq]

/-- If `h` is a splitting of `A -fâŸ¶ B -gâŸ¶ C`,
then `h.section : C âŸ¶ B` is the morphism satisfying `h.section â‰« g = ğŸ™ C`. -/
def _root_.category_theory.splitting.section : C âŸ¶ B := biprod.inr â‰« h.iso.inv

/-- If `h` is a splitting of `A -fâŸ¶ B -gâŸ¶ C`,
then `h.retraction : B âŸ¶ A` is the morphism satisfying `f â‰« h.retraction = ğŸ™ A`. -/
def retraction : B âŸ¶ A := h.iso.hom â‰« biprod.fst

@[simp, reassoc] lemma section_Ï€ : h.section â‰« g = ğŸ™ C := by { delta splitting.section, simp }

@[simp, reassoc] lemma Î¹_retraction : f â‰« h.retraction = ğŸ™ A := by { delta retraction, simp }

@[simp, reassoc] lemma section_retraction : h.section â‰« h.retraction = 0 :=
by { delta splitting.section retraction, simp }

/-- The retraction in a splitting is a split mono. -/
protected def split_mono : split_mono f := âŸ¨h.retraction, by simpâŸ©

/-- The section in a splitting is a split epi. -/
protected def split_epi : split_epi g := âŸ¨h.section, by simpâŸ©

@[simp, reassoc] lemma inr_iso_inv : biprod.inr â‰« h.iso.inv = h.section := rfl

@[simp, reassoc] lemma iso_hom_fst : h.iso.hom â‰« biprod.fst = h.retraction := rfl

/-- A short exact sequence of the form `X -fâŸ¶ Y -0âŸ¶ Z` where `f` is an iso and `Z` is zero
has a splitting. -/
def splitting_of_is_iso_zero {X Y Z : ğ’œ} (f : X âŸ¶ Y) [is_iso f] (hZ : is_zero Z) :
  splitting f (0 : Y âŸ¶ Z) :=
âŸ¨(as_iso f).symm â‰ªâ‰« iso_biprod_zero hZ, by simp [hZ.eq_of_tgt _ 0], by simpâŸ©

include h

protected lemma mono : mono f :=
begin
  apply mono_of_mono _ h.retraction,
  rw h.Î¹_retraction,
  apply_instance
end

protected lemma epi : epi g :=
begin
  apply_with (epi_of_epi h.section) { instances := ff },
  rw h.section_Ï€,
  apply_instance
end

instance : mono h.section :=
by { delta splitting.section, apply_instance }

instance : epi h.retraction :=
by { delta retraction, apply epi_comp }

end has_zero_morphisms

section preadditive
variables [preadditive ğ’œ] [has_binary_biproducts ğ’œ]
variables (h : splitting f g)

lemma split_add : h.retraction â‰« f + g â‰« h.section = ğŸ™ _ :=
begin
  delta splitting.section retraction,
  rw [â† cancel_mono h.iso.hom, â† cancel_epi h.iso.inv],
  simp only [category.comp_id, category.id_comp, category.assoc,
    iso.inv_hom_id_assoc, iso.inv_hom_id, limits.biprod.total,
    preadditive.comp_add, preadditive.add_comp,
    splitting.comp_iso_eq_inl, splitting.iso_comp_eq_snd_assoc]
end

@[reassoc]
lemma retraction_Î¹_eq_id_sub :
  h.retraction â‰« f = ğŸ™ _ - g â‰« h.section :=
eq_sub_iff_add_eq.mpr h.split_add

@[reassoc]
lemma Ï€_section_eq_id_sub :
  g â‰« h.section = ğŸ™ _ - h.retraction â‰« f :=
eq_sub_iff_add_eq.mpr ((add_comm _ _).trans h.split_add)

lemma splittings_comm (h h' : splitting f g) :
  h'.section â‰« h.retraction = - h.section â‰« h'.retraction :=
begin
  haveI := h.mono,
  rw â† cancel_mono f,
  simp [retraction_Î¹_eq_id_sub],
end

include h

lemma split : split f g :=
begin
  let Ï† := h.iso.hom â‰« biprod.fst,
  let Ï‡ := biprod.inr â‰« h.iso.inv,
  refine âŸ¨âŸ¨h.retraction, h.section, h.Î¹_retraction, h.section_Ï€, _,
    h.section_retraction, h.split_addâŸ©âŸ©,
  rw [â† h.inl_comp_iso_eq, category.assoc, h.iso_comp_eq_snd, biprod.inl_snd],
end

@[reassoc] lemma comp_eq_zero : f â‰« g = 0 :=
h.split.1.some_spec.some_spec.2.2.1

variables [has_kernels ğ’œ] [has_images ğ’œ] [has_zero_object ğ’œ] [has_cokernels ğ’œ]

protected lemma exact : exact f g :=
begin
  rw exact_iff_exact_of_iso f g (biprod.inl : A âŸ¶ A âŠ C) (biprod.snd : A âŠ C âŸ¶ C) _ _ _,
  { exact exact_inl_snd _ _ },
  { refine arrow.iso_mk (iso.refl _) h.iso _,
    simp only [iso.refl_hom, arrow.mk_hom, category.id_comp, comp_iso_eq_inl], },
  { refine arrow.iso_mk h.iso (iso.refl _) _,
    dsimp, simp, },
  { refl }
end

protected
lemma short_exact : short_exact f g :=
{ mono := h.mono, epi := h.epi, exact := h.exact }

end preadditive

end splitting

end category_theory
