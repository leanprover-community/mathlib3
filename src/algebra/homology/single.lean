/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.homology.homology
import algebra.homology.short_complex.exact

/-!
# Chain complexes supported in a single degree

We define `single V j c : V ‚•§ homological_complex V c`,
which constructs complexes in `V` of shape `c`, supported in degree `j`.

Similarly `single‚ÇÄ V : V ‚•§ chain_complex V ‚Ñï` is the special case for
`‚Ñï`-indexed chain complexes, with the object supported in degree `0`,
but with better definitional properties.

In `to_single‚ÇÄ_equiv` we characterize chain maps to a `‚Ñï`-indexed complex concentrated in degree 0;
they are equivalent to `{ f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 }`.
(This is useful translating between a projective resolution and
an augmented exact complex of projectives.)
-/

noncomputable theory

open category_theory
open category_theory.limits
open_locale zero_object

universes v u

variables (V : Type u) [category.{v} V] [has_zero_morphisms V] [has_zero_object V]

namespace homological_complex
variables {Œπ : Type*} [decidable_eq Œπ] (c : complex_shape Œπ)

/--
The functor `V ‚•§ homological_complex V c` creating a chain complex supported in a single degree.

See also `chain_complex.single‚ÇÄ : V ‚•§ chain_complex V ‚Ñï`,
which has better definitional properties,
if you are working with `‚Ñï`-indexed complexes.
-/
@[simps]
def single (j : Œπ) : V ‚•§ homological_complex V c :=
{ obj := Œª A,
  { X := Œª i, if i = j then A else 0,
    d := Œª i j, 0, },
  map := Œª A B f,
  { f := Œª i, if h : i = j then
      eq_to_hom (by { dsimp, rw if_pos h, }) ‚â´ f ‚â´ eq_to_hom (by { dsimp, rw if_pos h, })
    else
      0, },
  map_id' := Œª A, begin
    ext,
    dsimp,
    split_ifs with h,
    { subst h, simp, },
    { rw if_neg h, simp, },
  end,
  map_comp' := Œª A B C f g, begin
    ext,
    dsimp,
    split_ifs with h,
    { subst h, simp, },
    { simp, },
  end, }.

/--
The object in degree `j` of `(single V c h).obj A` is just `A`.
-/
@[simps]
def single_obj_X_self (j : Œπ) (A : V) : ((single V c j).obj A).X j ‚âÖ A :=
eq_to_iso (by simp)

@[simp]
lemma single_map_f_self (j : Œπ) {A B : V} (f : A ‚ü∂ B) :
  ((single V c j).map f).f j =
    (single_obj_X_self V c j A).hom ‚â´ f ‚â´ (single_obj_X_self V c j B).inv :=
by { simp, refl, }

instance (j : Œπ) : faithful (single V c j) :=
{ map_injective' := Œª X Y f g w, begin
    have := congr_hom w j,
    dsimp at this,
    simp only [dif_pos] at this,
    rw [‚Üêis_iso.inv_comp_eq, inv_eq_to_hom, eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp,
      ‚Üêis_iso.comp_inv_eq, category.assoc, inv_eq_to_hom, eq_to_hom_trans, eq_to_hom_refl,
      category.comp_id] at this,
    exact this,
  end, }

instance (j : Œπ) : full (single V c j) :=
{ preimage := Œª X Y f, eq_to_hom (by simp) ‚â´ f.f j ‚â´ eq_to_hom (by simp),
  witness' := Œª X Y f, begin
    ext i,
    dsimp,
    split_ifs,
    { subst h, simp, },
    { symmetry,
      apply zero_of_target_iso_zero,
      dsimp,
      rw [if_neg h], },
  end }

instance has_homology_single_obj (A : V) (j k : Œπ) :
  ((single V c j).obj A).has_homology k :=
begin
  rw has_homology.iff,
  dsimp [single],
  apply_instance,
end

def homology_single_self (A : V) (j : Œπ) :
  ((single V c j).obj A).homology j ‚âÖ A :=
(short_complex.homology_data.of_zeros
  (((single V c j).obj A).sc' j) rfl rfl).homology_iso ‚â™‚â´
  single_obj_X_self V c j A

def homology_single_other (A : V) (j k : Œπ) (hk : k ‚â† j):
  ((single V c j).obj A).homology k ‚âÖ 0 :=
is_zero.iso_zero
begin
  rw ‚Üê short_complex.exact_iff_is_zero_homology,
  apply short_complex.exact_of_is_zero_X‚ÇÇ,
  dsimp,
  split_ifs,
  { exfalso,
    exact hk h, },
  { apply limits.is_zero_zero, },
end

end homological_complex

open homological_complex

namespace chain_complex

/--
`chain_complex.single‚ÇÄ V` is the embedding of `V` into `chain_complex V ‚Ñï`
as chain complexes supported in degree 0.

This is naturally isomorphic to `single V _ 0`, but has better definitional properties.
-/
def single‚ÇÄ : V ‚•§ chain_complex V ‚Ñï :=
{ obj := Œª X,
  { X := Œª n, match n with
    | 0 := X
    | (n+1) := 0
    end,
    d := Œª i j, 0, },
  map := Œª X Y f,
  { f := Œª n, match n with
    | 0 := f
    | (n+1) := 0
    end, },
  map_id' := Œª X, by { ext n, cases n, refl, dsimp, unfold_aux, simp, },
  map_comp' := Œª X Y Z f g, by { ext n, cases n, refl, dsimp, unfold_aux, simp, } }

@[simp] lemma single‚ÇÄ_obj_X_0 (X : V) : ((single‚ÇÄ V).obj X).X 0 = X := rfl
@[simp] lemma single‚ÇÄ_obj_X_succ (X : V) (n : ‚Ñï) : ((single‚ÇÄ V).obj X).X (n+1) = 0 := rfl
@[simp] lemma single‚ÇÄ_obj_X_d (X : V) (i j : ‚Ñï) : ((single‚ÇÄ V).obj X).d i j = 0 := rfl
@[simp] lemma single‚ÇÄ_obj_X_d_to (X : V) (j : ‚Ñï) : ((single‚ÇÄ V).obj X).d_to j = 0 :=
by { rw [d_to_eq ((single‚ÇÄ V).obj X) rfl], simp, }
@[simp] lemma single‚ÇÄ_obj_X_d_from (X : V) (i : ‚Ñï) : ((single‚ÇÄ V).obj X).d_from i = 0 :=
begin
  cases i,
  { rw [d_from_eq_zero], simp, },
  { rw [d_from_eq ((single‚ÇÄ V).obj X) rfl], simp, },
end
@[simp] lemma single‚ÇÄ_map_f_0 {X Y : V} (f : X ‚ü∂ Y) : ((single‚ÇÄ V).map f).f 0 = f := rfl
@[simp] lemma single‚ÇÄ_map_f_succ {X Y : V} (f : X ‚ü∂ Y) (n : ‚Ñï) :
  ((single‚ÇÄ V).map f).f (n+1) = 0 := rfl

instance has_homology_single‚ÇÄ_obj (A : V) (j : ‚Ñï) :
  ((single‚ÇÄ V).obj A).has_homology j :=
begin
  rw has_homology.iff,
  dsimp [single‚ÇÄ],
  apply_instance,
end

variable {V}

@[simp]
def homology_data_single‚ÇÄ_zero (A : V) :
  ((single‚ÇÄ V).obj A).homology_data 0 :=
short_complex.homology_data.of_zeros _ rfl rfl

@[simp]
def homology_map_data_single‚ÇÄ_map_zero {A A' : V} (f : A ‚ü∂ A') :
  homology_map_data ((single‚ÇÄ V).map f) 0 (homology_data_single‚ÇÄ_zero A)
    (homology_data_single‚ÇÄ_zero A') :=
  short_complex.homology_map_data.of_zeros _ rfl rfl rfl rfl

@[simps]
def homology_single‚ÇÄ_zero (A : V) :
  ((single‚ÇÄ V).obj A).homology 0 ‚âÖ A :=
(homology_data_single‚ÇÄ_zero A).homology_iso

@[simps]
def homology_single‚ÇÄ_succ (A : V) (j : ‚Ñï):
  ((single‚ÇÄ V).obj A).homology (j+1) ‚âÖ 0 :=
is_zero.iso_zero
begin
  rw ‚Üê short_complex.exact_iff_is_zero_homology,
  apply short_complex.exact_of_is_zero_X‚ÇÇ,
  apply limits.is_zero_zero,
end


variable (V)

section

/--
Sending objects to chain complexes supported at `0` then taking `0`-th homology
is the same as doing nothing.
-/
noncomputable
def homology_functor_0_single‚ÇÄ [category_with_homology V] :
  single‚ÇÄ V ‚ãô homology_functor V _ 0 ‚âÖ (ùü≠ V) :=
nat_iso.of_components homology_single‚ÇÄ_zero
  (Œª X Y f, (homology_map_data_single‚ÇÄ_map_zero f).map_comm)

/--
Sending objects to chain complexes supported at `0` then taking `(n+1)`-st homology
is the same as the zero functor.
-/
noncomputable
def homology_functor_succ_single‚ÇÄ [category_with_homology V] (n : ‚Ñï) :
  single‚ÇÄ V ‚ãô homology_functor V _ (n+1) ‚âÖ (category_theory.functor.const _).obj 0 :=
nat_iso.of_components (Œª X, homology_single‚ÇÄ_succ X n) (by tidy)

end

variables {V}

/--
Morphisms from a `‚Ñï`-indexed chain complex `C`
to a single object chain complex with `X` concentrated in degree 0
are the same as morphisms `f : C.X 0 ‚ü∂ X` such that `C.d 1 0 ‚â´ f = 0`.
-/
@[simps]
def to_single‚ÇÄ_equiv (C : chain_complex V ‚Ñï) (X : V) :
  (C ‚ü∂ (single‚ÇÄ V).obj X) ‚âÉ { f : C.X 0 ‚ü∂ X // C.d 1 0 ‚â´ f = 0 } :=
{ to_fun := Œª f, ‚ü®f.f 0, by { rw ‚Üêf.comm 1 0, simp, }‚ü©,
  inv_fun := Œª f,
  { f := Œª i, match i with
    | 0 := f.1
    | (n+1) := 0
    end,
    comm' := Œª i j h, begin
      rcases i with _|_|i; cases j; unfold_aux; simp only [comp_zero, zero_comp, single‚ÇÄ_obj_X_d],
      { rw [C.shape, zero_comp], simp, },
      { exact f.2.symm, },
      { rw [C.shape, zero_comp], simp [i.succ_succ_ne_one.symm] },
    end, },
  left_inv := Œª f, begin
    ext i,
    rcases i,
    { refl, },
    { ext, },
  end,
  right_inv := by tidy, }

@[ext]
lemma to_single‚ÇÄ_ext {C : chain_complex V ‚Ñï} {X : V}
  (f g : (C ‚ü∂ (single‚ÇÄ V).obj X)) (h : f.f 0 = g.f 0) : f = g :=
(to_single‚ÇÄ_equiv C X).injective (by { ext, exact h, })

/--
Morphisms from a single object chain complex with `X` concentrated in degree 0
to a `‚Ñï`-indexed chain complex `C` are the same as morphisms `f : X ‚Üí C.X`.
-/
@[simps]
def from_single‚ÇÄ_equiv (C : chain_complex V ‚Ñï) (X : V) :
  ((single‚ÇÄ V).obj X ‚ü∂ C) ‚âÉ (X ‚ü∂ C.X 0) :=
{ to_fun := Œª f, f.f 0,
  inv_fun := Œª f,
  { f := Œª i, match i with
    | 0 := f
    | (n+1) := 0
    end,
    comm' := Œª i j h, begin
      cases i; cases j; unfold_aux;
      simp only [shape, complex_shape.down_rel, nat.one_ne_zero, not_false_iff,
        comp_zero, zero_comp, nat.succ_ne_zero, single‚ÇÄ_obj_X_d],
    end },
  left_inv := Œª f, begin
    ext i,
    cases i,
    { refl, },
    { ext, },
  end,
  right_inv := Œª g, rfl, }

variables (V)

/-- `single‚ÇÄ` is the same as `single V _ 0`. -/
def single‚ÇÄ_iso_single : single‚ÇÄ V ‚âÖ single V _ 0 :=
nat_iso.of_components
  (Œª X,
  { hom := { f := Œª i, by { cases i; simpa using ùüô _, } },
    inv := { f := Œª i, by { cases i; simpa using ùüô _, } },
    hom_inv_id' := by { ext (_|i); { dsimp, simp, }, },
    inv_hom_id' := begin
      ext (_|i),
      { apply category.id_comp, },
      { apply has_zero_object.to_zero_ext, },
    end, })
  (Œª X Y f, by { ext (_|i); { dsimp, simp, }, })

instance : faithful (single‚ÇÄ V) := faithful.of_iso (single‚ÇÄ_iso_single V).symm
instance : full (single‚ÇÄ V) := full.of_iso (single‚ÇÄ_iso_single V).symm

end chain_complex

namespace cochain_complex

local attribute [instance] has_zero_object.has_zero

/--
`cochain_complex.single‚ÇÄ V` is the embedding of `V` into `cochain_complex V ‚Ñï`
as cochain complexes supported in degree 0.

This is naturally isomorphic to `single V _ 0`, but has better definitional properties.
-/
def single‚ÇÄ : V ‚•§ cochain_complex V ‚Ñï :=
{ obj := Œª X,
  { X := Œª n, match n with
    | 0 := X
    | (n+1) := 0
    end,
    d := Œª i j, 0, },
  map := Œª X Y f,
  { f := Œª n, match n with
    | 0 := f
    | (n+1) := 0
    end, },
  map_id' := Œª X, by { ext n, cases n, refl, dsimp, unfold_aux, simp, },
  map_comp' := Œª X Y Z f g, by { ext n, cases n, refl, dsimp, unfold_aux, simp, } }

@[simp] lemma single‚ÇÄ_obj_X_0 (X : V) : ((single‚ÇÄ V).obj X).X 0 = X := rfl
@[simp] lemma single‚ÇÄ_obj_X_succ (X : V) (n : ‚Ñï) : ((single‚ÇÄ V).obj X).X (n+1) = 0 := rfl
@[simp] lemma single‚ÇÄ_obj_X_d (X : V) (i j : ‚Ñï) : ((single‚ÇÄ V).obj X).d i j = 0 := rfl
@[simp] lemma single‚ÇÄ_obj_X_d_from (X : V) (j : ‚Ñï) : ((single‚ÇÄ V).obj X).d_from j = 0 :=
by { rw [d_from_eq ((single‚ÇÄ V).obj X) rfl], simp, }
@[simp] lemma single‚ÇÄ_obj_X_d_to (X : V) (i : ‚Ñï) : ((single‚ÇÄ V).obj X).d_to i = 0 :=
begin
  cases i,
  { rw [d_to_eq_zero], simp, },
  { rw [d_to_eq ((single‚ÇÄ V).obj X) rfl], simp, },
end
@[simp] lemma single‚ÇÄ_map_f_0 {X Y : V} (f : X ‚ü∂ Y) : ((single‚ÇÄ V).map f).f 0 = f := rfl
@[simp] lemma single‚ÇÄ_map_f_succ {X Y : V} (f : X ‚ü∂ Y) (n : ‚Ñï) :
  ((single‚ÇÄ V).map f).f (n+1) = 0 := rfl

section

variable {V}

instance has_homology_single‚ÇÄ_obj (A : V) (j : ‚Ñï) :
  ((single‚ÇÄ V).obj A).has_homology j :=
begin
  rw has_homology.iff,
  dsimp [single‚ÇÄ],
  apply_instance,
end

def homology_data_single‚ÇÄ_zero (A : V) :
  ((single‚ÇÄ V).obj A).homology_data 0 :=
short_complex.homology_data.of_zeros _ rfl rfl

@[simp]
def homology_map_data_single‚ÇÄ_map_zero {A A' : V} (f : A ‚ü∂ A') :
  homology_map_data ((single‚ÇÄ V).map f) 0 (homology_data_single‚ÇÄ_zero A)
    (homology_data_single‚ÇÄ_zero A') :=
  short_complex.homology_map_data.of_zeros _ rfl rfl rfl rfl

def homology_single‚ÇÄ_zero (A : V) :
  ((single‚ÇÄ V).obj A).homology 0 ‚âÖ A :=
(homology_data_single‚ÇÄ_zero A).homology_iso

def homology_single‚ÇÄ_succ (A : V) (j : ‚Ñï):
  ((single‚ÇÄ V).obj A).homology (j+1) ‚âÖ 0 :=
is_zero.iso_zero
begin
  rw ‚Üê short_complex.exact_iff_is_zero_homology,
  apply short_complex.exact_of_is_zero_X‚ÇÇ,
  apply limits.is_zero_zero,
end

variable (V)

/--
Sending objects to cochain complexes supported at `0` then taking `0`-th homology
is the same as doing nothing.
-/
noncomputable
def homology_functor_0_single‚ÇÄ [category_with_homology V] :
  single‚ÇÄ V ‚ãô homology_functor V _ 0 ‚âÖ (ùü≠ V) :=
nat_iso.of_components homology_single‚ÇÄ_zero
  (Œª X Y f, begin
    dsimp [homology_single‚ÇÄ_zero],
    simp [(homology_map_data_single‚ÇÄ_map_zero f).homology_map_eq],
end)

/--
Sending objects to cochain complexes supported at `0` then taking `(n+1)`-st homology
is the same as the zero functor.
-/
noncomputable
def homology_functor_succ_single‚ÇÄ [category_with_homology V] (n : ‚Ñï) :
  single‚ÇÄ V ‚ãô homology_functor V _ (n+1) ‚âÖ (category_theory.functor.const _).obj 0 :=
nat_iso.of_components (Œª X, homology_single‚ÇÄ_succ X n) (by tidy)

end

variables {V}

/--
Morphisms from a single object cochain complex with `X` concentrated in degree 0
to a `‚Ñï`-indexed cochain complex `C`
are the same as morphisms `f : X ‚ü∂ C.X 0` such that `f ‚â´ C.d 0 1 = 0`.
-/
def from_single‚ÇÄ_equiv (C : cochain_complex V ‚Ñï) (X : V) :
  ((single‚ÇÄ V).obj X ‚ü∂ C) ‚âÉ { f : X ‚ü∂ C.X 0 // f ‚â´ C.d 0 1 = 0 } :=
{ to_fun := Œª f, ‚ü®f.f 0, by { rw f.comm 0 1, simp, }‚ü©,
  inv_fun := Œª f,
  { f := Œª i, match i with
    | 0 := f.1
    | (n+1) := 0
    end,
    comm' := Œª i j h, begin
      rcases j with _|_|j; cases i; unfold_aux; simp only [comp_zero, zero_comp, single‚ÇÄ_obj_X_d],
      { convert comp_zero, rw [C.shape], simp, },
      { exact f.2, },
      { convert comp_zero, rw [C.shape], simp only [complex_shape.up_rel, zero_add],
        exact (nat.one_lt_succ_succ j).ne },
    end, },
  left_inv := Œª f, begin
    ext i,
    rcases i,
    { refl, },
    { ext, },
  end,
  right_inv := by tidy, }

variables (V)

/-- `single‚ÇÄ` is the same as `single V _ 0`. -/
def single‚ÇÄ_iso_single : single‚ÇÄ V ‚âÖ single V _ 0 :=
nat_iso.of_components
  (Œª X,
  { hom := { f := Œª i, by { cases i; simpa using ùüô _, } },
    inv := { f := Œª i, by { cases i; simpa using ùüô _, } },
    hom_inv_id' := by { ext (_|i); { dsimp, simp, }, },
    inv_hom_id' := begin
      ext (_|i),
      { apply category.id_comp, },
      { apply has_zero_object.to_zero_ext, },
    end, })
  (Œª X Y f, by { ext (_|i); { dsimp, simp, }, })

instance : faithful (single‚ÇÄ V) := faithful.of_iso (single‚ÇÄ_iso_single V).symm
instance : full (single‚ÇÄ V) := full.of_iso (single‚ÇÄ_iso_single V).symm

end cochain_complex
