/-
Copyright (c) 2022 Alex J. Best, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex J. Best, YaÃ«l Dillies
-/
import algebra.big_operators.ring
import algebra.module.basic
import group_theory.group_action.basic
import group_theory.group_action.pi
import data.finset.locally_finite

/-!
# Incidence algebras


## TODOs
Here are some additions to this file that could be made in the future

- Generalize the construction of `mu` to invert any element of the incidence algebra `f` which has
  `f x x` a unit for all `x`.
- Give formulae for higher powers of zeta.
- A formula for the mÃ¶bius function on a pi type similar to the one for products
- More examples / applications to different posets.
- Connection with Galois insertions
- Finsum version of MÃ¶bius inversion that holds even when an order doesn't have top/bot?
-/

open finset
open_locale big_operators

namespace finset

section
variables {Î± Î² : Type*} [preorder Î±] [preorder Î²] [locally_finite_order Î±] [locally_finite_order Î²]
  [decidable_rel ((â‰¤) : Î± â†’ Î± â†’ Prop)] [decidable_rel ((â‰¤) : Î² â†’ Î² â†’ Prop)]
lemma prod_Icc (a b : Î± Ã— Î²) : Icc a b = (Icc a.fst b.fst).product (Icc a.snd b.snd) := rfl
end
section pre
variables {Î± : Type*} [preorder Î±] [locally_finite_order Î±] {a b c : Î±}

lemma Icc_ssubset_Icc_left (hab : a â‰¤ b) (h : c < b) : Icc a c âŠ‚ Icc a b :=
begin
  classical,
  rw finset.ssubset_iff,
  use b,
  simp only [hab, true_and, mem_Icc],
  refine âŸ¨Î» hh, lt_irrefl c (h.trans_le hh),
    insert_subset.mpr âŸ¨right_mem_Icc.mpr hab, finset.subset_iff.mpr (Î» x hx, _)âŸ©âŸ©,
  rw [mem_Icc] at âŠ¢ hx,
  exact âŸ¨hx.1, (hx.2.trans_lt h).leâŸ©,
end

lemma Icc_ssubset_Icc_right (hab : a â‰¤ b) (h : a < c) : Icc c b âŠ‚ Icc a b :=
@Icc_ssubset_Icc_left (order_dual Î±) _ _ _ _ _ hab h

lemma card_Icc_lt_card_Icc_left (hab : a â‰¤ b) (h : c < b) : (Icc a c).card < (Icc a b).card :=
card_lt_card (Icc_ssubset_Icc_left hab h)

lemma card_Icc_lt_card_Icc_right (hab : a â‰¤ b) (h : a < c) : (Icc c b).card < (Icc a b).card :=
@card_Icc_lt_card_Icc_left (order_dual Î±) _ _ _ _ _ hab h

end pre
variables {Î± : Type*} [partial_order Î±] [locally_finite_order Î±] {a b : Î±}

@[simp] lemma Ioc_insert_left [decidable_eq Î±] (h : a â‰¤ b) : insert a (Ioc a b) = Icc a b :=
@Ico_insert_right (order_dual Î±) _ _ _ _ _ h
local attribute [simp] Ico_insert_right

lemma Icc_eq_cons_Ioc (h : a â‰¤ b) : Icc a b = (Ioc a b).cons a left_not_mem_Ioc :=
finset.coe_inj.mp (by { classical, simp [h] })

lemma Icc_eq_cons_Ico (h : a â‰¤ b) : Icc a b = (Ico a b).cons b right_not_mem_Ico :=
finset.coe_inj.mp (by { classical, simp [h] })

section order_top
variables [order_top Î±]

@[simp]
lemma Ioi_insert [decidable_eq Î±] (a : Î±) : insert a (Ioi a) = Ici a := Ioc_insert_left le_top

lemma Ici_eq_cons_Ioi (a : Î±) : Ici a = (Ioi a).cons a left_not_mem_Ioc :=
finset.coe_inj.mp (by { classical, simp })

end order_top

section sum
variables {Î² : Type*} [add_comm_monoid Î²] {f : Î± â†’ Î²}

lemma sum_Icc_eq_add_sum_Ioc (h : a â‰¤ b) : âˆ‘ x in Icc a b, f x = f a + âˆ‘ x in Ioc a b, f x :=
by rw [Icc_eq_cons_Ioc h, sum_cons]

section order_top
variables [order_top Î±]

lemma sum_Ici_eq_add_sum_Ioi (a : Î±) : âˆ‘ x in Ici a, f x = f a + âˆ‘ x in Ioi a, f x :=
sum_Icc_eq_add_sum_Ioc le_top

end order_top
end sum
end finset

open finset
open_locale big_operators

variables (ð•„ F ð•œ ð• ð•ž Î± Î² : Type*)

/-- The `ð•œ`-incidence algebra over `Î±`. -/
structure incidence_algebra [has_zero ð•œ] [has_le Î±] :=
(to_fun : Î± â†’ Î± â†’ ð•œ)
(eq_zero_of_not_le' {a b : Î±} : Â¬ a â‰¤ b â†’ to_fun a b = 0)

namespace incidence_algebra
section zero
variables [has_zero ð•œ] [has_le Î±]

instance fun_like : fun_like (incidence_algebra ð•œ Î±) Î± (Î» _, Î± â†’ ð•œ) :=
âŸ¨to_fun, Î» f g h, by { cases f, cases g, congr' }âŸ©

variables {ð•œ Î±}

lemma eq_zero_of_not_le {a b : Î±} (h : Â¬ a â‰¤ b) (f : incidence_algebra ð•œ Î±) : f a b = 0 :=
eq_zero_of_not_le' _ h

lemma le_of_ne_zero {f : incidence_algebra ð•œ Î±} {a b : Î±} : f a b â‰  0 â†’ a â‰¤ b :=
not_imp_comm.1 $ eq_zero_of_not_le' _

-- completely uninteresting lemmas about coercion to function, that all homs need
section coes

-- Fallback `has_coe_to_fun` instance to help the elaborator
instance : has_coe_to_fun (incidence_algebra ð•œ Î±) (Î» _, Î± â†’ Î± â†’ ð•œ) := âŸ¨to_funâŸ©

-- this must come after the coe_to_fun definitions
initialize_simps_projections incidence_algebra (to_fun â†’ apply)

@[simp] lemma to_fun_eq_coe (f : incidence_algebra ð•œ Î±) : f.to_fun = f := rfl

@[simp] lemma coe_mk (f : Î± â†’ Î± â†’ ð•œ) (h) : (mk f h : Î± â†’ Î± â†’ ð•œ) = f := rfl

protected lemma congr_fun {f g : incidence_algebra ð•œ Î±} (h : f = g) (a b : Î±) : f a b = g a b :=
congr_arg (Î» f : incidence_algebra ð•œ Î±, f a b) h

protected lemma congr_arg (f : incidence_algebra ð•œ Î±) {aâ‚ aâ‚‚ bâ‚ bâ‚‚ : Î±} (ha : aâ‚ = aâ‚‚)
  (hb : bâ‚ = bâ‚‚) :
  f aâ‚ bâ‚ = f aâ‚‚ bâ‚‚ :=
congr_arg2 f ha hb

lemma coe_inj â¦ƒf g : incidence_algebra ð•œ Î±â¦„ (h : (f : Î± â†’ Î± â†’ ð•œ) = g) : f = g :=
by { cases f, cases g, cases h, refl }

@[ext] lemma ext â¦ƒf g : incidence_algebra ð•œ Î±â¦„ (h : âˆ€ a b (hab : a â‰¤ b), f a b = g a b) : f = g :=
begin
  refine coe_inj (funext $ Î» a, funext $ Î» b, _),
  by_cases hab : a â‰¤ b,
  { exact h _ _ hab },
  { rw [eq_zero_of_not_le hab, eq_zero_of_not_le hab] }
end

lemma ext_iff {f g : incidence_algebra ð•œ Î±} : f = g â†” âˆ€ a b, f a b = g a b :=
âŸ¨incidence_algebra.congr_fun, Î» h, ext $ Î» a b _, h _ _âŸ©

@[simp] lemma mk_coe (f : incidence_algebra ð•œ Î±) (h) : mk f h = f := ext $ Î» _ _ _, rfl

end coes

/-! ### Additive and multiplicative structure -/

variables {ð•œ Î±}

instance : has_zero (incidence_algebra ð•œ Î±) := âŸ¨âŸ¨Î» _ _, 0, Î» _ _ _, rflâŸ©âŸ©
instance : inhabited (incidence_algebra ð•œ Î±) := âŸ¨0âŸ©

@[simp] lemma zero_apply (a b : Î±) : (0 : incidence_algebra ð•œ Î±) a b = 0 := rfl

end zero

section add
variables [add_zero_class ð•œ] [has_le Î±]

instance : has_add (incidence_algebra ð•œ Î±) :=
âŸ¨Î» f g, âŸ¨f + g, Î» a b h, by simp_rw [pi.add_apply, eq_zero_of_not_le h, zero_add]âŸ©âŸ©

@[simp] lemma add_apply (f g : incidence_algebra ð•œ Î±) (a b : Î±) :
  (f + g) a b = f a b + g a b := rfl

end add

instance [add_monoid ð•œ] [has_le Î±] : add_monoid (incidence_algebra ð•œ Î±) :=
{ add := (+),
  add_assoc := Î» f g h, by { ext, exact add_assoc _ _ _ },
  zero := 0,
  zero_add := Î» f, by { ext, exact zero_add _ },
  add_zero := Î» f, by { ext, exact add_zero _ } }

instance [add_comm_monoid ð•œ] [has_le Î±] : add_comm_monoid (incidence_algebra ð•œ Î±) :=
{ add_comm := Î» f g, by { ext, exact add_comm _ _ },
  .. incidence_algebra.add_monoid ð•œ Î± }

section add_group
variables [add_group ð•œ] [has_le Î±]

instance : has_neg (incidence_algebra ð•œ Î±) :=
âŸ¨Î» f, âŸ¨-f, Î» a b h, by simp_rw [pi.neg_apply, eq_zero_of_not_le h, neg_zero]âŸ©âŸ©

instance : has_sub (incidence_algebra ð•œ Î±) :=
âŸ¨Î» f g, âŸ¨f - g, Î» a b h, by simp_rw [pi.sub_apply, eq_zero_of_not_le h, sub_zero]âŸ©âŸ©

@[simp] lemma neg_apply (f : incidence_algebra ð•œ Î±) (a b : Î±) : (-f) a b = -f a b := rfl

@[simp] lemma sub_apply (f g : incidence_algebra ð•œ Î±) (a b : Î±) : (f - g) a b = f a b - g a b := rfl

instance : add_group (incidence_algebra ð•œ Î±) :=
{ sub_eq_add_neg := Î» f g, by { ext, exact sub_eq_add_neg _ _ },
  add_left_neg := Î» f, by { ext, exact add_left_neg _ },
  .. incidence_algebra.add_monoid ð•œ Î±,
  .. incidence_algebra.has_neg ð•œ Î±,
  .. incidence_algebra.has_sub ð•œ Î± }

end add_group

instance [add_comm_group ð•œ] [has_le Î±] : add_comm_group (incidence_algebra ð•œ Î±) :=
{ .. incidence_algebra.add_group ð•œ Î±, .. incidence_algebra.add_comm_monoid ð•œ Î± }

-- section smul_with_zero
-- variables [has_zero ð•„] [has_zero ð•œ] [smul_with_zero ð•„ ð•œ] [has_le Î±]

-- instance : has_scalar ð•„ (incidence_algebra ð•œ Î±) :=
-- âŸ¨Î» c f, âŸ¨c â€¢ f, Î» a b h, by rw [pi.smul_apply, pi.smul_apply, eq_zero_of_not_le h, smul_zero']âŸ©âŸ©

-- @[simp] lemma smul_apply (c : ð•„) (f : incidence_algebra ð•œ Î±) (a b : Î±) : (c â€¢ f) a b
-- = c â€¢ f a b :=
-- rfl

-- instance : smul_with_zero ð•„ (incidence_algebra ð•œ Î±) :=
-- { smul := (â€¢),
--   smul_zero := Î» m, by { ext, exact smul_zero' _ _ },
--   zero_smul := Î» m, by { ext, exact zero_smul _ _ } }

-- end smul_with_zero

section one
variables [preorder Î±] [decidable_eq Î±] [has_zero ð•œ] [has_one ð•œ]

instance : has_one (incidence_algebra ð•œ Î±) :=
âŸ¨âŸ¨Î» a b, if a = b then 1 else 0, Î» a b h, ite_eq_right_iff.2 $ Î» H, (h H.le).elimâŸ©âŸ©

@[simp] lemma one_apply (a b : Î±) : (1 : incidence_algebra ð•œ Î±) a b = if a = b then 1 else 0 := rfl

end one

section co_union_lemmas
variables {Î±}
variables [partial_order Î±] [locally_finite_order Î±] [decidable_eq Î±]
-- TODO fix names of these lemmas
-- TODO copy more API from data.set.intervals.basic to finset
lemma Ici_eq_Ioi_union [order_top Î±] (x : Î±) : Ici x = Ioi x âˆª {x} := finset.coe_inj.mp (by simp)
lemma Iic_eq_Iio_union [order_bot Î±] (x : Î±) : Iic x = Iio x âˆª {x} := finset.coe_inj.mp (by simp)
lemma Icc_eq_Ico_union {x y : Î±} (hxy : x â‰¤ y) : Icc x y = Ico x y âˆª {y} :=
finset.coe_inj.mp (by simp [hxy, set.Ico_union_right])
lemma Icc_eq_Ioc_union {x y : Î±} (hxy : x â‰¤ y) : Icc x y = Ioc x y âˆª {x} :=
finset.coe_inj.mp (by simp [hxy, set.Ioc_union_left])
end co_union_lemmas

section mul
variables [preorder Î±] [locally_finite_order Î±] [add_comm_monoid ð•œ] [has_mul ð•œ]

instance : has_mul (incidence_algebra ð•œ Î±) :=
âŸ¨Î» f g, âŸ¨Î» a b, âˆ‘ x in Icc a b, f a x * g x b, Î» a b h, by rw [Icc_eq_empty h, sum_empty]âŸ©âŸ©

@[simp] lemma mul_apply (f g : incidence_algebra ð•œ Î±) (a b : Î±) :
  (f * g) a b = âˆ‘ x in Icc a b, f a x * g x b := rfl

end mul

instance [preorder Î±] [locally_finite_order Î±] [non_unital_non_assoc_semiring ð•œ] :
  non_unital_non_assoc_semiring (incidence_algebra ð•œ Î±) :=
{ mul := (*),
  zero := 0,
  zero_mul := Î» f, by { ext, exact sum_eq_zero (Î» x _, zero_mul _) },
  mul_zero := Î» f, by { ext, exact sum_eq_zero (Î» x _, mul_zero _) },
  left_distrib := Î» f g h,
    by { ext, exact eq.trans (sum_congr rfl $ Î» x _, left_distrib _ _ _) sum_add_distrib },
  right_distrib := Î» f g h,
    by { ext, exact eq.trans (sum_congr rfl $ Î» x _, right_distrib _ _ _) sum_add_distrib },
  .. incidence_algebra.add_comm_monoid ð•œ Î± }

instance [preorder Î±] [locally_finite_order Î±] [decidable_eq Î±] [non_assoc_semiring ð•œ] :
  non_assoc_semiring (incidence_algebra ð•œ Î±) :=
{ mul := (*),
  zero := 0,
  one := 1,
  one_mul := Î» f, begin
    ext a b,
    simp_rw [mul_apply, one_apply, sum_boole_mul],
    exact ite_eq_left_iff.2 (not_imp_comm.1 $ Î» h, left_mem_Icc.2 $ le_of_ne_zero $ ne.symm h),
  end,
  mul_one := Î» f, begin
    ext a b,
    simp_rw [mul_apply, one_apply, eq_comm, sum_mul_boole],
    convert (ite_eq_left_iff.2 $ not_imp_comm.1 $
      Î» h, right_mem_Icc.2 $ le_of_ne_zero $ ne.symm h).symm,
  end,
  .. incidence_algebra.non_unital_non_assoc_semiring ð•œ Î± }

instance [preorder Î±] [locally_finite_order Î±] [decidable_eq Î±] [semiring ð•œ] :
  semiring (incidence_algebra ð•œ Î±) :=
{ mul := (*),
  mul_assoc := Î» f g h, begin
    ext a b,
    simp only [mul_apply, sum_mul, mul_sum],
    rw [sum_sigma', sum_sigma'],
    dsimp,
    apply' sum_bij (Î» (x : Î£ i : Î±, Î±) hx, (sigma.mk x.snd x.fst : Î£ i : Î±, Î±)),
    { rintro c hc,
      simp only [mem_sigma, mem_Icc] at hc,
      simp only [mem_sigma, mem_Icc],
      exact âŸ¨âŸ¨hc.2.1, hc.2.2.trans hc.1.2âŸ©, hc.2.2, hc.1.2âŸ© },
    { rintro c hc,
      simp only [mul_assoc] },
    { rintro âŸ¨câ‚, câ‚‚âŸ© âŸ¨dâ‚, dâ‚‚âŸ© hc hd âŸ¨âŸ©,
      refl },
    { rintro c hc,
      simp only [exists_prop, sigma.exists, mem_sigma, heq_iff_eq, sigma.mk.inj_iff, mem_Icc] at *,
      exact âŸ¨c.2, c.1, âŸ¨âŸ¨hc.1.1.trans hc.2.1, hc.2.2âŸ©, hc.1.1, hc.2.1âŸ©, c.eta.symmâŸ© }
  end,
  one := 1,
  zero := 0,
  .. incidence_algebra.non_assoc_semiring ð•œ Î± }

instance [preorder Î±] [locally_finite_order Î±] [decidable_eq Î±] [ring ð•œ] :
  ring (incidence_algebra ð•œ Î±) :=
{ .. incidence_algebra.semiring ð•œ Î±, .. incidence_algebra.add_group ð•œ Î± }

/-! ### Scalar multiplication -/

section smul
variables [preorder Î±] [locally_finite_order Î±] [add_comm_monoid ð•œ] [add_comm_monoid ð•]
  [has_scalar ð•œ ð•]

instance : has_scalar (incidence_algebra ð•œ Î±) (incidence_algebra ð• Î±) :=
âŸ¨Î» f g, âŸ¨Î» a b, âˆ‘ x in Icc a b, f a x â€¢ g x b, Î» a b h, by rw [Icc_eq_empty h, sum_empty]âŸ©âŸ©

@[simp] lemma smul_apply (f : incidence_algebra ð•œ Î±) (g : incidence_algebra ð• Î±) (a b : Î±) :
  (f â€¢ g) a b = âˆ‘ x in Icc a b, f a x â€¢ g x b := rfl

end smul

instance [preorder Î±] [locally_finite_order Î±] [add_comm_monoid ð•œ] [monoid ð•œ] [semiring ð•]
  [add_comm_monoid ð•ž] [has_scalar ð•œ ð•] [module ð• ð•ž] [distrib_mul_action ð•œ ð•ž]
  [is_scalar_tower ð•œ ð• ð•ž] :
  is_scalar_tower (incidence_algebra ð•œ Î±) (incidence_algebra ð• Î±) (incidence_algebra ð•ž Î±) :=
âŸ¨Î» f g h, begin
  ext a b,
  simp only [smul_apply, sum_smul, smul_sum],
  rw [sum_sigma', sum_sigma'],
  dsimp,
  apply' sum_bij (Î» (x : Î£ i : Î±, Î±) hx, (sigma.mk x.snd x.fst : Î£ i : Î±, Î±)),
  { rintro c hc,
    simp only [mem_sigma, mem_Icc] at hc,
    simp only [mem_sigma, mem_Icc],
    exact âŸ¨âŸ¨hc.2.1, hc.2.2.trans hc.1.2âŸ©, hc.2.2, hc.1.2âŸ© },
  { rintro c hc,
    simp only [smul_assoc] },
  { rintro âŸ¨câ‚, câ‚‚âŸ© âŸ¨dâ‚, dâ‚‚âŸ© hc hd âŸ¨âŸ©,
    refl },
  { rintro c hc,
    simp only [exists_prop, sigma.exists, mem_sigma, heq_iff_eq, sigma.mk.inj_iff, mem_Icc] at *,
    exact âŸ¨c.2, c.1, âŸ¨âŸ¨hc.1.1.trans hc.2.1, hc.2.2âŸ©, hc.1.1, hc.2.1âŸ©, c.eta.symmâŸ© }
endâŸ©

instance [preorder Î±] [locally_finite_order Î±] [decidable_eq Î±] [semiring ð•œ] [semiring ð•]
  [module ð•œ ð•] :
  module (incidence_algebra ð•œ Î±) (incidence_algebra ð• Î±) :=
{ smul := (â€¢),
  one_smul := Î» f, begin
    ext a b,
    simp [ite_smul, hab],
  end,
  mul_smul := Î» f g h, begin
    convert smul_assoc _ _ _,
    ext a b,
    refl,
    apply_instance,
  end,
  smul_add := Î» f g h,
    by { ext, exact eq.trans (sum_congr rfl $ Î» x _, smul_add _ _ _) sum_add_distrib },
  add_smul := Î» f g h,
    by { ext, exact eq.trans (sum_congr rfl $ Î» x _, add_smul _ _ _) sum_add_distrib },
  zero_smul := Î» f, by { ext, exact sum_eq_zero (Î» x _, zero_smul _ _) },
  smul_zero := Î» f, by { ext, exact sum_eq_zero (Î» x _, smul_zero _) } }

/-! ### The Zeta and MÃ¶bius functions -/

section zeta
variables [has_zero ð•œ] [has_one ð•œ] [has_le Î±] [@decidable_rel Î± (â‰¤)]

/-- The zeta function of the incidence algebra is the function that assigns 1 to every nonempty
interval, convolution with this function sums functions over intervals. -/
def zeta : incidence_algebra ð•œ Î± := âŸ¨Î» a b, if a â‰¤ b then 1 else 0, Î» a b h, if_neg hâŸ©

variables {ð•œ Î±}

@[simp] lemma zeta_apply (a b : Î±) : zeta ð•œ Î± a b = if a â‰¤ b then 1 else 0 := rfl

lemma zeta_of_le {a b : Î±} (h : a â‰¤ b) : zeta ð•œ Î± a b = 1 := if_pos h

end zeta

lemma zeta_mul_zeta [add_comm_monoid ð•œ] [mul_one_class ð•œ] [preorder Î±] [locally_finite_order Î±]
  [@decidable_rel Î± (â‰¤)] (a b : Î±) :
  (zeta ð•œ Î± * zeta ð•œ Î±) a b = (Icc a b).card :=
begin
  rw [mul_apply, card_eq_sum_ones, nat.cast_sum, nat.cast_one],
  refine sum_congr rfl (Î» x hx, _),
  rw mem_Icc at hx,
  rw [zeta_of_le hx.1, zeta_of_le hx.2, one_mul],
end

section mu
variables [add_comm_group ð•œ] [has_one ð•œ] [preorder Î±] [locally_finite_order Î±] [decidable_eq Î±]

/-- The moebius function of the incidence algebra as a bare function defined recursively. -/
def mu_aux (a : Î±) : Î± â†’ ð•œ
| b := if h : a = b then 1 else
  -âˆ‘ x in (Ico a b).attach,
    have ha : a â‰¤ x, begin cases x, rw mem_Ico at x_property, exact x_property.1, end,
    have hb : â†‘x < b, begin cases x, rw mem_Ico at x_property, exact x_property.2, end,
    have (Icc a x).card < (Icc a b).card, from card_Icc_lt_card_Icc_left (ha.trans_lt hb).le hb,
    mu_aux x
using_well_founded { rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» b, (Icc a b).card)âŸ©] }

lemma mu_aux_apply (a b : Î±) :
  mu_aux ð•œ Î± a b = if a = b then 1 else -âˆ‘ x in (Ico a b).attach, mu_aux ð•œ Î± a x :=
by { convert has_well_founded.wf.fix_eq _ _, refl }

/-- The moebius function which inverts `zeta` as an element of the incidence algebra. -/
def mu : incidence_algebra ð•œ Î± := âŸ¨mu_aux ð•œ Î±, Î» a b, not_imp_comm.1 $ Î» h, begin
  rw mu_aux_apply at h,
  split_ifs at h with hab hab,
  { exact hab.le },
  { rw neg_eq_zero at h,
    obtain âŸ¨âŸ¨x, hxâŸ©, -âŸ© := exists_ne_zero_of_sum_ne_zero h,
    exact (nonempty_Ico.1 âŸ¨x, hxâŸ©).le }
endâŸ©

variables {ð•œ Î±}

lemma mu_apply (a b : Î±) : mu ð•œ Î± a b = if a = b then 1 else -âˆ‘ x in Ico a b, mu ð•œ Î± a x :=
by rw [mu, coe_mk, mu_aux_apply, sum_attach]

lemma mu_apply_of_eq {a b : Î±} (h : a = b) : mu ð•œ Î± a b = 1 :=
by rw [mu_apply, if_pos h]

@[simp]
lemma mu_apply_self (a : Î±) : mu ð•œ Î± a a = 1 := mu_apply_of_eq rfl

lemma mu_apply_of_ne {a b : Î±} (h : a â‰  b) : mu ð•œ Î± a b = -âˆ‘ x in Ico a b, mu ð•œ Î± a x :=
by rw [mu_apply, if_neg h]

end mu
section mu_spec
-- we need partial order for this
variables [add_comm_group ð•œ] [has_one ð•œ] [partial_order Î±] [locally_finite_order Î±] [decidable_eq Î±]
variables {ð•œ Î±}

lemma mu_spec_of_ne_right {a b : Î±} (h : a â‰  b) : âˆ‘ (x : Î±) in Icc a b, (mu ð•œ Î±) a x = 0 :=
begin
  have : mu ð•œ Î± a b = _ := mu_apply_of_ne h,
  by_cases hab : a â‰¤ b,
  { rw [Icc_eq_Ico_union hab, sum_union, sum_singleton, this, add_neg_self],
    simp, },
  { have : âˆ€ x âˆˆ Icc a b, Â¬ a â‰¤ x,
    { intros x hx hn, apply hab, rw [mem_Icc] at hx, exact le_trans hn hx.2 },
    conv in (mu _ _ _ _) { rw eq_zero_of_not_le (this x H), },
    exact sum_const_zero, },
end
end mu_spec

section mu'
variables [add_comm_group ð•œ] [has_one ð•œ] [preorder Î±] [locally_finite_order Î±] [decidable_eq Î±]

-- this is the reversed definition of mu, which is equal to mu but easiest to prove equal
-- by showing that zeta * mu = 1 and mu' * zeta = 1
-- therefore mu' should be an implementation detail and not used
private def mu'_aux (b : Î±) : Î± â†’ ð•œ
| a := if h : a = b then 1 else
  -âˆ‘ x in (Ioc a b).attach,
    have ha : a < x, begin cases x, rw mem_Ioc at x_property, exact x_property.1, end,
    have hb : â†‘x â‰¤ b, begin cases x, rw mem_Ioc at x_property, exact x_property.2, end,
    have (Icc â†‘x b).card < (Icc a b).card, from card_Icc_lt_card_Icc_right (ha.le.trans hb) ha,
    mu'_aux x
using_well_founded { rel_tac := Î» _ _, `[exact âŸ¨_, measure_wf (Î» a, (Icc a b).card)âŸ©] }

private lemma mu'_aux_apply (a b : Î±) :
  mu'_aux ð•œ Î± b a = if a = b then 1 else -âˆ‘ x in (Ioc a b).attach, mu'_aux ð•œ Î± b x :=
by { convert has_well_founded.wf.fix_eq _ _, refl }

private def mu' : incidence_algebra ð•œ Î± :=
âŸ¨Î» a b, mu'_aux ð•œ Î± b a, Î» a b, not_imp_comm.1 $ Î» h, begin
  rw mu'_aux_apply at h,
  split_ifs at h with hab hab,
  { exact hab.le },
  { rw neg_eq_zero at h,
    obtain âŸ¨âŸ¨x, hxâŸ©, -âŸ© := exists_ne_zero_of_sum_ne_zero h,
    exact (nonempty_Ioc.1 âŸ¨x, hxâŸ©).le }
endâŸ©
variables {ð•œ Î±}

lemma mu'_apply (a b : Î±) : mu' ð•œ Î± a b = if a = b then 1 else -âˆ‘ x in Ioc a b, mu' ð•œ Î± x b :=
by rw [mu', coe_mk, mu'_aux_apply, sum_attach]

lemma mu'_apply_of_ne {a b : Î±} (h : a â‰  b) : mu' ð•œ Î± a b = -âˆ‘ x in Ioc a b, mu' ð•œ Î± x b :=
by rw [mu'_apply, if_neg h]

lemma mu'_apply_of_eq {a b : Î±} (h : a = b) : mu' ð•œ Î± a b = 1 :=
by rw [mu'_apply, if_pos h]

@[simp]
lemma mu'_apply_self (a : Î±) : mu' ð•œ Î± a a = 1 := mu'_apply_of_eq rfl
end mu'

section mu'_spec
-- we need partial order for this
variables [add_comm_group ð•œ] [has_one ð•œ] [partial_order Î±] [locally_finite_order Î±] [decidable_eq Î±]
variables {ð•œ Î±}


lemma mu'_spec_of_ne_left {a b : Î±} (h : a â‰  b) : âˆ‘ (x : Î±) in Icc a b, (mu' ð•œ Î±) x b = 0 :=
begin
  have : mu' ð•œ Î± a b = _ := mu'_apply_of_ne h,
  by_cases hab : a â‰¤ b,
  { rw [Icc_eq_Ioc_union hab, sum_union, sum_singleton, this, add_right_neg],
    simp, },
  { have : âˆ€ x âˆˆ Icc a b, Â¬ x â‰¤ b,
    { intros x hx hn, apply hab, rw [mem_Icc] at hx, exact le_trans hx.1 hn },
    conv in (mu' _ _ _ _) { rw eq_zero_of_not_le (this x H), },
    exact sum_const_zero, },
end
end mu'_spec

section mu_zeta
variables [add_comm_group ð•œ] [mul_one_class ð•œ] [partial_order Î±] [locally_finite_order Î±]
  [decidable_eq Î±] [@decidable_rel Î± (â‰¤)]

lemma mu_mul_zeta : mu ð•œ Î± * zeta ð•œ Î± = 1 :=
begin
  ext a b,
  rw [mul_apply, one_apply],
  split_ifs with he,
  { simp [he] },
  { simp only [mul_one, zeta_apply, mul_ite],
    conv in (ite _ _ _) { rw [if_pos (mem_Icc.mp H).2] },
    rw mu_spec_of_ne_right he }
end

lemma zeta_mul_mu' : zeta ð•œ Î± * mu' ð•œ Î± = 1 :=
begin
  ext a b,
  rw [mul_apply, one_apply],
  split_ifs with he,
  { simp [he] },
  { simp only [zeta_apply, one_mul, ite_mul],
    conv in (ite _ _ _) { rw [if_pos (mem_Icc.mp H).1] },
    rw mu'_spec_of_ne_left he }
end

end mu_zeta

section mu_eq_mu'
variables [ring ð•œ] [partial_order Î±] [locally_finite_order Î±]
  [decidable_eq Î±]

lemma mu_eq_mu' : mu ð•œ Î± = mu' ð•œ Î± :=
begin
  letI : @decidable_rel Î± (â‰¤) := classical.dec_rel _,
  exact left_inv_eq_right_inv (mu_mul_zeta ð•œ Î±) (zeta_mul_mu' ð•œ Î±)
end

lemma mu_apply_of_ne' {a b : Î±} (h : a â‰  b) : mu ð•œ Î± a b = -âˆ‘ x in Ioc a b, mu ð•œ Î± x b :=
begin
  rw mu_eq_mu',
  exact mu'_apply_of_ne h,
end

lemma zeta_mul_mu [@decidable_rel Î± (â‰¤)] : zeta ð•œ Î± * mu ð•œ Î± = 1 :=
begin
  rw mu_eq_mu',
  exact zeta_mul_mu' ð•œ Î±,
end

lemma mu_spec_of_ne_left {a b : Î±} (h : a â‰  b) : âˆ‘ (x : Î±) in Icc a b, (mu ð•œ Î±) x b = 0 :=
by rw [mu_eq_mu', mu'_spec_of_ne_left h]

end mu_eq_mu'

section order_dual
variables [ring ð•œ] [partial_order Î±] [locally_finite_order Î±] [decidable_eq Î±]

open order_dual
lemma mu_dual (a b : Î±) : mu ð•œ (order_dual Î±) (to_dual a) (to_dual b) = mu ð•œ Î± b a :=
begin
  letI : @decidable_rel Î± (â‰¤) := classical.dec_rel _,
  let mud : incidence_algebra ð•œ (order_dual Î±) := { to_fun := Î» a b, mu ð•œ Î± b a,
    eq_zero_of_not_le' := Î» a b hab, eq_zero_of_not_le hab (mu ð•œ Î±) },
  suffices : mu ð•œ (order_dual Î±) = mud,
  { rw [this], refl, },
  suffices : mud * zeta ð•œ (order_dual Î±) = 1,
  { rw â† mu_mul_zeta at this,
    apply_fun (* (mu ð•œ (order_dual Î±))) at this,
    symmetry,
    simpa [mul_assoc, zeta_mul_mu] using this, },
  clear a b,
  ext a b,
  simp only [mul_boole, one_apply, mul_apply, coe_mk, zeta_apply],
  by_cases h : a = b,
  { simp [h], },
  { simp only [h, if_false],
    conv in (ite _ _ _)
    { rw if_pos (mem_Icc.mp H).2 },
    change âˆ‘ (x : Î±) in (Icc b a : finset Î±), (mu ð•œ Î±) x a = 0,
    exact mu_spec_of_ne_left _ _ (ne.symm h), },
end
end order_dual

section inversion_top
variables {Î±} [ring ð•œ] [partial_order Î±] [order_top Î±] [locally_finite_order Î±]
  [decidable_eq Î±] {a b : Î±}

/-- A general form of MÃ¶bius inversion. Based on Theorem 2.1.2 of Incidence Algebras by Spiegel and
O'Donnell. -/
lemma moebius_inversion_top (f g : Î± â†’ ð•œ) (h : âˆ€ x, g x = âˆ‘ y in Ici x, f y) (x : Î±) :
  f x = âˆ‘ y in Ici x, mu ð•œ Î± x y * g y :=
by letI : @decidable_rel Î± (â‰¤) := classical.dec_rel _; symmetry; calc
  âˆ‘ y in Ici x, mu ð•œ Î± x y * g y
      = âˆ‘ y in Ici x, mu ð•œ Î± x y * âˆ‘ z in Ici y, f z : by simp_rw [h]
  ... = âˆ‘ y in Ici x, mu ð•œ Î± x y * âˆ‘ z in Ici y, zeta ð•œ Î± y z * f z : by
      { simp_rw [zeta_apply],
        conv in (ite _ _ _)
        { rw if_pos (mem_Ici.mp H) },
        simp }
  ... = âˆ‘ y in Ici x, âˆ‘ z in Ici y, mu ð•œ Î± x y * zeta ð•œ Î± y z * f z : by simp [mul_sum]
  ... = âˆ‘ z in Ici x, âˆ‘ y in Icc x z, mu ð•œ Î± x y * zeta ð•œ Î± y z * f z : by
      { erw sum_sigma' (Ici x) (Î» y, Ici y),
        erw sum_sigma' (Ici x) (Î» z, Icc x z),
        simp only [mul_boole, zero_mul, ite_mul, zeta_apply],
        refine sum_bij (Î» X hX, âŸ¨X.snd, X.fstâŸ©) _ _ _ _,
        { intros X hX,
          simp only [mem_Ici, mem_sigma, mem_Icc] at *,
          exact âŸ¨hX.1.trans hX.2, hXâŸ©, },
        { intros X hX,
          simp only at *, },
        { intros X Y ha hb h,
          simp [sigma.ext_iff] at *,
          rwa and_comm, },
        { intros X hX,
          use [âŸ¨X.snd, X.fstâŸ©],
          simp only [and_true, mem_Ici, eq_self_iff_true, sigma.eta, mem_sigma, mem_Icc] at *,
          exact hX.2, }, }
  ... = âˆ‘ z in Ici x, (mu ð•œ Î± * zeta ð•œ Î±) x z * f z : by
      { conv in ((mu _ _ * zeta _ _) _ _) { rw [mul_apply] },
        simp_rw [sum_mul] }
  ... = âˆ‘ y in Ici x, âˆ‘ z in Ici y, (1 : incidence_algebra ð•œ Î±) x z * f z : by
      { simp [mu_mul_zeta ð•œ Î±, sum_Ici_eq_add_sum_Ioi],
        conv in (ite _ _ _) { rw if_neg (ne_of_lt $ mem_Ioi.mp H) },
        conv in (ite _ _ _) { rw if_neg (not_lt_of_le $ (mem_Ioi.mp H).le) },
        simp }
  ... = f x : by { simp [one_apply, sum_Ici_eq_add_sum_Ioi],
        conv in (ite _ _ _) { rw if_neg (ne_of_lt $ mem_Ioi.mp H) },
        conv in (ite _ _ _) { rw if_neg (not_lt_of_le $ (mem_Ioi.mp H).le) },
        simp }

end inversion_top

section inversion_bot
variables [ring ð•œ] [partial_order Î±] [order_bot Î±] [locally_finite_order Î±] [decidable_eq Î±]

/-- A general form of MÃ¶bius inversion. Based on Theorem 2.1.3 of Incidence Algebras by Spiegel and
O'Donnell. -/
lemma moebius_inversion_bot (f g : Î± â†’ ð•œ) (h : âˆ€ x, g x = âˆ‘ y in Iic x, f y) (x : Î±) :
  f x = âˆ‘ y in Iic x, mu ð•œ Î± y x * g y :=
begin
  convert @moebius_inversion_top ð•œ (order_dual Î±) _ _ _ _ _ f g h x,
  ext y,
  erw mu_dual,
end

end inversion_bot

section prod
section preorder
variables {Î± Î²} [ring ð•œ] [preorder Î±] [preorder Î²]

section decidable_le
variables [decidable_rel ((â‰¤) : Î± â†’ Î± â†’ Prop)] [decidable_rel ((â‰¤) : Î² â†’ Î² â†’ Prop)]

lemma zeta_prod_apply (a b : Î± Ã— Î²) : zeta ð•œ (Î± Ã— Î²) a b = zeta ð•œ Î± a.1 b.1 * zeta ð•œ Î² a.2 b.2 :=
by simp [ite_and, prod.le_def]

lemma zeta_prod_mk (aâ‚ aâ‚‚ : Î±) (bâ‚ bâ‚‚ : Î²) :
  zeta ð•œ (Î± Ã— Î²) (aâ‚, bâ‚) (aâ‚‚, bâ‚‚) = zeta ð•œ Î± aâ‚ aâ‚‚ * zeta ð•œ Î² bâ‚ bâ‚‚ :=
zeta_prod_apply _ _ _
end decidable_le

variables {Î± Î²}

variables [decidable_eq Î±] [decidable_eq Î²]
lemma one_prod_apply (a b : Î± Ã— Î²) :
  (1 : incidence_algebra ð•œ (Î± Ã— Î²)) a b =
  (1 : incidence_algebra ð•œ Î±) a.1 b.1 * (1 : incidence_algebra ð•œ Î²) a.2 b.2 :=
by simp [ite_and, prod.ext_iff]

lemma one_prod_mk (aâ‚ aâ‚‚ : Î±) (bâ‚ bâ‚‚ : Î²) :
  (1 : incidence_algebra ð•œ (Î± Ã— Î²)) (aâ‚, bâ‚) (aâ‚‚, bâ‚‚) =
    (1 : incidence_algebra ð•œ Î±) aâ‚ aâ‚‚ * (1 : incidence_algebra ð•œ Î²) bâ‚ bâ‚‚ :=
one_prod_apply _ _ _

variables (Î± Î²)
variables [locally_finite_order Î±] [locally_finite_order Î²]

/-- A description of `mu` in a product of incidence algebras -/
def mu_prod : incidence_algebra ð•œ (Î± Ã— Î²) :=
{ to_fun := Î» xu yv : Î± Ã— Î², mu ð•œ Î± xu.fst yv.fst * mu ð•œ Î² xu.snd yv.snd,
  eq_zero_of_not_le' := begin
    rintros âŸ¨aâŸ© âŸ¨bâŸ© hab,
    rw [prod.mk_le_mk, not_and_distrib] at hab,
    cases hab; simp [eq_zero_of_not_le hab],
end }

variables {Î± Î²}

lemma mu_prod_mk (x y : Î±) (u v : Î²) : mu_prod ð•œ Î± Î² (x, u) (y, v) = mu ð•œ Î± x y * mu ð•œ Î² u v := rfl
lemma mu_prod_apply (a b : Î± Ã— Î²) : mu_prod ð•œ Î± Î² a b = mu ð•œ Î± a.fst b.fst * mu ð•œ Î² a.snd b.snd :=
rfl


end preorder

section partial_order
variables {Î± Î²} [ring ð•œ] [partial_order Î±] [partial_order Î²] [locally_finite_order Î±]
  [locally_finite_order Î²] [decidable_eq Î±] [decidable_eq Î²] [decidable_rel ((â‰¤) : Î± â†’ Î± â†’ Prop)]
  [decidable_rel ((â‰¤) : Î² â†’ Î² â†’ Prop)]

/-- The MÃ¶bius function on a product order. Based on Theorem 2.1.13 of Incidence Algebras
by Spiegel and O'Donnell. -/
lemma mu_prod_eq (x y : Î±) (u v : Î²) : mu ð•œ (Î± Ã— Î²) (x, u) (y, v) = mu ð•œ Î± x y * mu ð•œ Î² u v :=
begin
  suffices : mu ð•œ (Î± Ã— Î²) = mu_prod ð•œ Î± Î²,
  { simp [this, mu_prod_apply] },
  suffices : mu_prod ð•œ Î± Î² * zeta ð•œ (Î± Ã— Î²) = 1,
  { rw â† mu_mul_zeta at this,
    apply_fun (* (mu ð•œ (Î± Ã— Î²))) at this,
    symmetry,
    simpa [mul_assoc, zeta_mul_mu] using this },
  clear x y u v,
  ext âŸ¨x, uâŸ© âŸ¨y, vâŸ©,
  simp_rw [mul_apply, zeta_prod_apply, mu_prod_apply, prod_Icc],
  convert_to âˆ‘ (x_1 : Î± Ã— Î²) in (Icc (x, u).fst (y, v).fst).product (Icc (x, u).snd (y, v).snd),
    (mu ð•œ Î±) x x_1.fst * (zeta ð•œ Î±) x_1.fst y * ((mu ð•œ Î²) u x_1.snd * (zeta ð•œ Î²) x_1.snd v) = _,
  { simp [mul_comm, mul_assoc] },
  rw â† sum_mul_sum (Icc x y) (Icc u v)
    (Î» x_1f, (mu ð•œ Î±) x x_1f * (zeta ð•œ Î±) x_1f y)
    (Î» x_1s, (mu ð•œ Î²) u x_1s * (zeta ð•œ Î²) x_1s v),
  rw one_prod_apply,
  congr; rw [â† mu_mul_zeta, mul_apply],
end

end partial_order
end prod

section euler
variables [add_comm_group ð•œ] [has_one ð•œ] [preorder Î±] [bounded_order Î±] [locally_finite_order Î±]
  [decidable_eq Î±]

/-- The Euler characteristic of a finite bounded order. -/
def euler_char : ð•œ := mu ð•œ Î± âŠ¥ âŠ¤

end euler
end incidence_algebra
