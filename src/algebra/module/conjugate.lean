/-
Copyright (c) 2020 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import algebra.module.basic
import ring_theory.ring_invo
import data.complex.is_R_or_C

/-!
# Conjugate semimodules

Given a `semimodule R E` with `R` a commutative semiring, we define its conjugate semimodule with
respect to a ring involution `I : R â‰ƒ+* Ráµ’áµ–`, in which the scalar product is defined as
`r â€¢ (to_conj I x) = to_conj ((I r) â€¢ x)`. This is a generalization of the complex conjugate
vector space, where the ring involution is the complex conjugation. Since this is an important
special case, we define also `conj_semimodule ğ•œ E` with `[is_R_or_C ğ•œ]` to avoid having to deal
with the ring involution explicitly.

## Implementation notes

The conjugate semimodule is defined as a copy of the original type, with conversions having to be
done explicitly via `to_conj` and `from_conj`, as for the opposite type. Facts that are specific
to normed spaces and inner product spaces are defined in `analysis/normed_space/conjugate.lean`.

## References

* https://en.wikipedia.org/wiki/Complex_conjugate_vector_space

## Tags

conjugate semimodule, conjugate vector space
-/

variables {R : Type*} [comm_semiring R]

/-- The conjugate of a semimodule `E` with respect to a ring involution `I`. -/
@[derive [add_comm_monoid], nolint unused_arguments]
def conjugate_semimodule (I : R â‰ƒ+* Ráµ’áµ–) (E : Type*) [add_comm_monoid E] := E

instance {I : R â‰ƒ+* Ráµ’áµ–} {E : Type*} [add_comm_monoid E] : inhabited (conjugate_semimodule I E) := âŸ¨0âŸ©

/-- The `is_R_or_C` complex conjugate semimodule of `E` -/
abbreviation conj_semimodule (ğ•œ : Type*) [is_R_or_C ğ•œ] (E : Type*) [add_comm_monoid E] :=
  conjugate_semimodule (is_R_or_C.conj_to_ring_equiv ğ•œ) E

namespace conjugate_semimodule

variables {E : Type*}  [add_comm_monoid E]

/-- The equivalence between `E` and its conjugate semimodule. -/
def conj_equiv (I : R â‰ƒ+* Ráµ’áµ–) : E â‰ƒ+ conjugate_semimodule I E :=
{ to_fun := id,
  inv_fun := id,
  left_inv := Î» x, rfl,
  right_inv := Î» x, rfl,
  map_add' := Î» x y, rfl }

variables {I : R â‰ƒ+* Ráµ’áµ–}

instance [has_scalar R E] : has_scalar R (conjugate_semimodule I E) :=
{ smul := Î» r x, conj_equiv I ((I r).unop â€¢ ((conj_equiv I).symm  x)) }

instance [mul_action R E] : mul_action R (conjugate_semimodule I E) :=
{ one_smul := Î» x, by { change conj_equiv I ((I 1).unop â€¢ ((conj_equiv I).symm x)) = x, simp },
  mul_smul := Î» x y z,
  begin
    change conj_equiv I( (I (x * y)).unop â€¢ ((conj_equiv I).symm z)) = conj_equiv I ((I x).unop â€¢ ((conj_equiv I).symm ( conj_equiv I ((I y).unop â€¢ ((conj_equiv I).symm z)) ))),
    simp [mul_comm, mul_smul],
  end }

instance [distrib_mul_action R E] : distrib_mul_action R (conjugate_semimodule I E) :=
{ smul_add := Î» r x y,
  begin
    change conj_equiv I ((I r).unop â€¢ ((conj_equiv I).symm (x + y)))
          = conj_equiv I ((I r).unop â€¢ ((conj_equiv I).symm x)) + conj_equiv I ((I r).unop â€¢ ((conj_equiv I).symm y)),
    simp [smul_add],
  end,
  smul_zero := Î» r, by { change conj_equiv I ((I r).unop â€¢ ((conj_equiv I).symm 0)) = 0, simp} }

instance [semimodule R E] : semimodule R (conjugate_semimodule I E) :=
{ add_smul := Î» r s x,
  begin
    change conj_equiv I ((I (r + s)).unop â€¢ ((conj_equiv I).symm x))
      = conj_equiv I ((I r).unop â€¢ ((conj_equiv I).symm x)) + conj_equiv I ((I s).unop â€¢ ((conj_equiv I).symm x)),
    simp [add_smul],
  end,
  zero_smul := Î» x, by { change conj_equiv I ((I 0).unop â€¢ ((conj_equiv I).symm x)) = 0,
                          simp [zero_smul] } }

end conjugate_semimodule

namespace conj_semimodule
open is_R_or_C

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [add_comm_monoid E] [semimodule ğ•œ E]
local postfix `â€ `:100 := @conj ğ•œ _
local notation `I` := conj_to_ring_equiv ğ•œ

variables (ğ•œ)
/-- The equivalence between `E` and its conjugate space -/
def conj_equiv : E â‰ƒ+ conj_semimodule ğ•œ E := conjugate_semimodule.conj_equiv I

variables {ğ•œ}

lemma smul_def {r : ğ•œ} {x : E} : conj_equiv ğ•œ (r â€¢ x) = râ€  â€¢ (conj_equiv ğ•œ x) :=
by simp [conj_equiv, has_scalar.smul]

lemma smul_def' {r : ğ•œ} {x : conj_semimodule ğ•œ E} : (conj_equiv ğ•œ).symm (r â€¢ x) = râ€  â€¢ ((conj_equiv ğ•œ).symm x) :=
by simp [conj_equiv, has_scalar.smul]

end conj_semimodule
