/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/

import ring_theory.graded_algebra.basic
import algebra.graded_mul_action
import algebra.direct_sum.decomposition
import algebra.module.big_operators

/-!
# Graded Module

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Given an `R`-algebra `A` graded by `ğ“`, a graded `A`-module `M` is expressed as
`direct_sum.decomposition ğ“œ` and `set_like.has_graded_smul ğ“ ğ“œ`.
Then `â¨ i, ğ“œ i` is an `A`-module and is isomorphic to `M`.

## Tags

graded module
-/


section

open_locale direct_sum

variables {Î¹ : Type*} (A : Î¹ â†’ Type*) (M : Î¹ â†’ Type*)

namespace direct_sum
open graded_monoid

/-- A graded version of `distrib_mul_action`. -/
class gdistrib_mul_action [add_monoid Î¹] [gmonoid A] [Î  i, add_monoid (M i)]
  extends gmul_action A M :=
(smul_add {i j} (a : A i) (b c : M j) : smul a (b + c) = smul a b + smul a c)
(smul_zero {i j} (a : A i) : smul a (0 : M j) = 0)

/-- A graded version of `module`. -/
class gmodule [add_monoid Î¹] [Î  i, add_monoid (A i)] [Î  i, add_monoid (M i)]
  [gmonoid A] extends gdistrib_mul_action A M :=
(add_smul {i j} (a a' : A i) (b : M j) : smul (a + a') b = smul a b + smul a' b)
(zero_smul {i j} (b : M j) : smul (0 : A i) b = 0)

/-- A graded version of `semiring.to_module`. -/
instance gsemiring.to_gmodule [decidable_eq Î¹] [add_monoid Î¹]
  [Î  (i : Î¹), add_comm_monoid (A i)] [gsemiring A] :
  gmodule A A :=
{ smul_add := Î» _ _, gsemiring.mul_add,
  smul_zero := Î» i j, gsemiring.mul_zero,
  add_smul := Î» i j, gsemiring.add_mul,
  zero_smul := Î» i j, gsemiring.zero_mul,
  ..gmonoid.to_gmul_action A }

variables [add_monoid Î¹] [Î  (i : Î¹), add_comm_monoid (A i)] [Î  i, add_comm_monoid (M i)]

/-- The piecewise multiplication from the `has_mul` instance, as a bundled homomorphism. -/
@[simps] def gsmul_hom [gmonoid A] [gmodule A M] {i j} :
  A i â†’+ M j â†’+ M (i + j) :=
{ to_fun := Î» a,
  { to_fun := Î» b, ghas_smul.smul a b,
    map_zero' := gdistrib_mul_action.smul_zero _,
    map_add' := gdistrib_mul_action.smul_add _ },
  map_zero' := add_monoid_hom.ext $ Î» a, gmodule.zero_smul a,
  map_add' := Î» aâ‚ aâ‚‚, add_monoid_hom.ext $ Î» b, gmodule.add_smul _ _ _}

namespace gmodule

/-- For graded monoid `A` and a graded module `M` over `A`. `gmodule.smul_add_monoid_hom` is the
`â¨áµ¢ Aáµ¢`-scalar multiplication on `â¨áµ¢ Máµ¢` induced by `gsmul_hom`. -/
def smul_add_monoid_hom
  [decidable_eq Î¹] [gmonoid A] [gmodule A M] :
  (â¨ i, A i) â†’+ (â¨ i, M i) â†’+ (â¨ i, M i) :=
to_add_monoid $ Î» i, add_monoid_hom.flip $
  to_add_monoid $ Î» j, add_monoid_hom.flip $
    (of M _).comp_hom.comp $ gsmul_hom A M

section

open graded_monoid direct_sum gmodule

instance [decidable_eq Î¹] [gmonoid A] [gmodule A M] :
  has_smul (â¨ i, A i) (â¨ i, M i) :=
{ smul := Î» x y, smul_add_monoid_hom A M x y }

@[simp] lemma smul_def [decidable_eq Î¹] [gmonoid A] [gmodule A M]
  (x : â¨ i, A i) (y : â¨ i, M i) : x â€¢ y = smul_add_monoid_hom _ _ x y := rfl

@[simp] lemma smul_add_monoid_hom_apply_of_of [decidable_eq Î¹] [gmonoid A] [gmodule A M]
  {i j} (x : A i) (y : M j) :
  smul_add_monoid_hom A M (direct_sum.of A i x) (of M j y) =
  of M (i + j) (ghas_smul.smul x y) :=
by simp [smul_add_monoid_hom]

@[simp] lemma of_smul_of [decidable_eq Î¹] [gmonoid A] [gmodule A M]
  {i j} (x : A i) (y : M j) :
  direct_sum.of A i x â€¢ of M j y = of M (i + j) (ghas_smul.smul x y) :=
smul_add_monoid_hom_apply_of_of _ _ _ _

open add_monoid_hom

-- Almost identical to the proof of `direct_sum.one_mul`
private lemma one_smul [decidable_eq Î¹] [gmonoid A] [gmodule A M] (x : â¨ i, M i) :
  (1 : â¨ i, A i) â€¢ x = x :=
suffices smul_add_monoid_hom A M 1 = add_monoid_hom.id (â¨ i, M i),
  from add_monoid_hom.congr_fun this x,
begin
  apply direct_sum.add_hom_ext, intros i xi,
  unfold has_one.one,
  rw smul_add_monoid_hom_apply_of_of,
  exact direct_sum.of_eq_of_graded_monoid_eq (one_smul (graded_monoid A) $ graded_monoid.mk i xi),
end

-- Almost identical to the proof of `direct_sum.mul_assoc`
private lemma mul_smul [decidable_eq Î¹] [gsemiring A] [gmodule A M]
  (a b : â¨ i, A i) (c : â¨ i, M i) : (a * b) â€¢ c = a â€¢ (b â€¢ c) :=
suffices (smul_add_monoid_hom A M).comp_hom.comp (direct_sum.mul_hom A)
      -- `Î» a b c, (a * b) â€¢ c` as a bundled hom
      = (add_monoid_hom.comp_hom add_monoid_hom.flip_hom $
          (smul_add_monoid_hom A M).flip.comp_hom.comp $ smul_add_monoid_hom A M).flip,
      -- `Î» a b c, a â€¢ (b â€¢ c)` as a bundled hom
  from add_monoid_hom.congr_fun (add_monoid_hom.congr_fun (add_monoid_hom.congr_fun this a) b) c,
begin
  ext ai ax bi bx ci cx : 6,
  dsimp only [coe_comp, function.comp_app, comp_hom_apply_apply, flip_apply, flip_hom_apply],
  rw [smul_add_monoid_hom_apply_of_of, smul_add_monoid_hom_apply_of_of,
    direct_sum.mul_hom_of_of, smul_add_monoid_hom_apply_of_of],
  exact direct_sum.of_eq_of_graded_monoid_eq
    (mul_smul (graded_monoid.mk ai ax) (graded_monoid.mk bi bx) (graded_monoid.mk ci cx)),
end

/-- The `module` derived from `gmodule A M`. -/
instance module [decidable_eq Î¹] [gsemiring A] [gmodule A M] :
  module (â¨ i, A i) (â¨ i, M i) :=
{ smul := (â€¢),
  one_smul := one_smul _ _,
  mul_smul := mul_smul _ _,
  smul_add := Î» r, (smul_add_monoid_hom A M r).map_add,
  smul_zero := Î» r, (smul_add_monoid_hom A M r).map_zero,
  add_smul := Î» r s x, by simp only [smul_def, map_add, add_monoid_hom.add_apply],
  zero_smul := Î» x, by simp only [smul_def, map_zero, add_monoid_hom.zero_apply] }

end

end gmodule

end direct_sum

end

open_locale direct_sum big_operators

variables {Î¹ R A M Ïƒ Ïƒ' : Type*}
variables [add_monoid Î¹] [comm_semiring R] [semiring A] [algebra R A]
variables (ğ“ : Î¹ â†’ Ïƒ') [set_like Ïƒ' A]
variables (ğ“œ : Î¹ â†’ Ïƒ)

namespace set_like

include Ïƒ' A Ïƒ M

instance gmul_action [add_monoid M] [distrib_mul_action A M]
  [set_like Ïƒ M] [set_like.graded_monoid ğ“] [set_like.has_graded_smul ğ“ ğ“œ] :
  graded_monoid.gmul_action (Î» i, ğ“ i) (Î» i, ğ“œ i) :=
{ one_smul := Î» âŸ¨i, mâŸ©, sigma.subtype_ext (zero_add _) (one_smul _ _),
  mul_smul := Î» âŸ¨i, aâŸ© âŸ¨j, a'âŸ© âŸ¨k, bâŸ©, sigma.subtype_ext (add_assoc _ _ _) (mul_smul _ _ _),
  ..set_like.ghas_smul ğ“ ğ“œ }

instance gdistrib_mul_action [add_monoid M] [distrib_mul_action A M]
  [set_like Ïƒ M] [add_submonoid_class Ïƒ M] [set_like.graded_monoid ğ“]
  [set_like.has_graded_smul ğ“ ğ“œ] :
  direct_sum.gdistrib_mul_action (Î» i, ğ“ i) (Î» i, ğ“œ i) :=
{ smul_add := Î» i j a b c, subtype.ext $ smul_add _ _ _,
  smul_zero := Î» i j a, subtype.ext $ smul_zero _,
  ..set_like.gmul_action ğ“ ğ“œ }

variables [add_comm_monoid M] [module A M] [set_like Ïƒ M] [add_submonoid_class Ïƒ' A]
  [add_submonoid_class Ïƒ M] [set_like.graded_monoid ğ“] [set_like.has_graded_smul ğ“ ğ“œ]

/-- `[set_like.graded_monoid ğ“] [set_like.has_graded_smul ğ“ ğ“œ]` is the internal version of graded
  module, the internal version can be translated into the external version `gmodule`. -/
instance gmodule : direct_sum.gmodule (Î» i, ğ“ i) (Î» i, ğ“œ i) :=
{ smul := Î» i j x y, âŸ¨(x : A) â€¢ (y : M), set_like.has_graded_smul.smul_mem x.2 y.2âŸ©,
  add_smul := Î» i j a a' b, subtype.ext $ add_smul _ _ _,
  zero_smul := Î» i j b, subtype.ext $ zero_smul _ _,
  ..set_like.gdistrib_mul_action ğ“ ğ“œ}

end set_like

namespace graded_module

include Ïƒ' A Ïƒ M

variables [add_comm_monoid M] [module A M] [set_like Ïƒ M] [add_submonoid_class Ïƒ' A]
  [add_submonoid_class Ïƒ M] [set_like.graded_monoid ğ“] [set_like.has_graded_smul ğ“ ğ“œ]

/--
The smul multiplication of `A` on `â¨ i, ğ“œ i` from `(â¨ i, ğ“ i) â†’+ (â¨ i, ğ“œ i) â†’+ â¨ i, ğ“œ i`
turns `â¨ i, ğ“œ i` into an `A`-module
-/
def is_module [decidable_eq Î¹] [graded_ring ğ“] :
  module A (â¨ i, ğ“œ i) :=
{ smul := Î» a b, direct_sum.decompose ğ“ a â€¢ b,
  .. module.comp_hom _ (direct_sum.decompose_ring_equiv ğ“ : A â‰ƒ+* â¨ i, ğ“ i).to_ring_hom }

local attribute [instance] graded_module.is_module

/--
`â¨ i, ğ“œ i` and `M` are isomorphic as `A`-modules.
"The internal version" and "the external version" are isomorphism as `A`-modules.
-/
def linear_equiv [decidable_eq Î¹] [graded_ring ğ“]
  [direct_sum.decomposition ğ“œ] :
  M â‰ƒâ‚—[A] â¨ i, ğ“œ i :=
{ to_fun := direct_sum.decompose_add_equiv ğ“œ,
  map_smul' := Î» x y, begin
    classical,
    rw [â† direct_sum.sum_support_decompose ğ“ x, map_sum, finset.sum_smul, map_sum,
      finset.sum_smul, finset.sum_congr rfl (Î» i hi, _)],
    rw [ring_hom.id_apply, â† direct_sum.sum_support_decompose ğ“œ y, map_sum, finset.smul_sum,
      map_sum, finset.smul_sum, finset.sum_congr rfl (Î» j hj, _)],
    simp only [(â€¢), direct_sum.decompose_add_equiv_apply, direct_sum.decompose_coe,
      direct_sum.gmodule.smul_add_monoid_hom_apply_of_of],
    convert direct_sum.decompose_coe ğ“œ _,
    refl,
  end,
  .. direct_sum.decompose_add_equiv ğ“œ }

end graded_module
