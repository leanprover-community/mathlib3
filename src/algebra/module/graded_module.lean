/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/

import ring_theory.graded_algebra.basic
import algebra.direct_sum.decomposition

/-!
# Graded Module

Given an `R`-algebra `A` graded by `ğ“`, a graded `A`-module `M` is expressed as
`direct_sum.decomposition ğ“œ` and `set_like.has_graded_scalar ğ“ ğ“œ`.
Then `â¨ i, ğ“œ i` is an `A`-module and is isomorphic to `M`.

## Tags

graded module
-/


open_locale direct_sum big_operators

variables {Î¹ R A M Ïƒ : Type*}
variables [add_monoid Î¹] [comm_semiring R] [semiring A] [algebra R A]
variables (ğ“ : Î¹ â†’ submodule R A)
variables [add_comm_monoid M] [module A M]
variables [set_like Ïƒ M] [add_submonoid_class Ïƒ M] (ğ“œ : Î¹ â†’ Ïƒ)

namespace graded_module

instance graded_algebra.to_graded_module [decidable_eq Î¹] [graded_algebra ğ“] :
  set_like.has_graded_scalar ğ“ (Î» i, (ğ“ i).to_add_submonoid) :=
{ smul_mem := Î» i j x y hi hj, set_like.graded_monoid.mul_mem hi hj }

lemma ghas_scalar.smul_zero
  [set_like.has_graded_scalar ğ“ ğ“œ]
  {i j} (a : ğ“ i) :
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j a 0 = 0 :=
subtype.ext_iff_val.2 $ (smul_zero _ : (a : A) â€¢ 0 = (0 : M))

lemma ghas_scalar.smul_add
  [set_like.has_graded_scalar ğ“ ğ“œ]
  {i j} (a : ğ“ i) (b c : ğ“œ j) :
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j a (b + c) =
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j a b +
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j a c :=
subtype.ext_iff_val.2 $ (smul_add _ _ _ : (a : A) â€¢ (b + c : M) = (a : A) â€¢ b + (a : A) â€¢ c)

lemma ghas_scalar.zero_smul
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  {i j} (b : ğ“œ j) :
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j 0 b = 0 :=
subtype.ext_iff_val.2 $ (zero_smul _ _ : (0 : A) â€¢ _ = 0)

lemma ghas_scalar.add_smul
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  {i j} (a b : ğ“ i) (c : ğ“œ j) :
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j (a + b) c =
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j a c +
  @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j b c :=
subtype.ext_iff_val.2 $ (add_smul _ _ _ : (a + b : A) â€¢ _ = (a : A) â€¢ _ + (b : A) â€¢ _)

def gscalar_hom
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ] {i j} :
  ğ“ i â†’+ ğ“œ j â†’+ ğ“œ (i + j) :=
{ to_fun := Î» a,
  { to_fun := Î» b, @graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j a b,
    map_zero' := ghas_scalar.smul_zero ğ“ ğ“œ _,
    map_add' := ghas_scalar.smul_add _ _ _ },
  map_zero' := add_monoid_hom.ext $ ghas_scalar.zero_smul _ _,
  map_add' := Î» x y, add_monoid_hom.ext $ ghas_scalar.add_smul _ _ _ _ }

def scalar_hom [decidable_eq Î¹]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ] :
  (â¨ i, ğ“ i) â†’+ (â¨ i, ğ“œ i) â†’+ â¨ i, ğ“œ i :=
direct_sum.to_add_monoid $ Î» i,
  add_monoid_hom.flip $ direct_sum.to_add_monoid $ Î» j, add_monoid_hom.flip $
    (direct_sum.of (Î» i, ğ“œ i) _).comp_hom.comp $ gscalar_hom ğ“ ğ“œ

lemma scalar_hom_of_of [decidable_eq Î¹]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  {i j} (a : ğ“ i) (b : ğ“œ j) :
  scalar_hom ğ“ ğ“œ (direct_sum.of _ i a) (direct_sum.of _ j b) =
  direct_sum.of _ (i + j)
    (@graded_monoid.ghas_scalar.smul Î¹ (Î» i, ğ“ i) (Î» i, ğ“œ i) _ _ i j a b) :=
begin
  dunfold scalar_hom,
  rw [direct_sum.to_add_monoid_of, add_monoid_hom.flip_apply, direct_sum.to_add_monoid_of,
    add_monoid_hom.flip_apply, add_monoid_hom.comp_apply],
  refl,
end

def has_scalar [decidable_eq Î¹] [direct_sum.decomposition ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ] :
  has_scalar A (â¨ i, ğ“œ i) :=
{ smul := Î» a b,
    (scalar_hom ğ“ ğ“œ).comp (direct_sum.decompose_add_equiv ğ“).to_add_monoid_hom a b }

local attribute [instance] graded_module.has_scalar

lemma one_smul [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  (b : â¨ i, ğ“œ i) :
  (1 : A) â€¢ b = b :=
have of_congr : âˆ€ {i i' : Î¹} (h1 : i = i') {x : ğ“œ i} {y : ğ“œ i'} (h2 : x.1 = y.1),
  direct_sum.of _ i x = direct_sum.of _ i' y,
from Î» _ _ h1 x y h2,
  direct_sum.of_congr _ h1 $ by subst h1; exact heq_of_eq (subtype.ext_iff_val.2 h2),
have eq0 : direct_sum.decompose_add_equiv ğ“ (1 : A) = direct_sum.of _ 0 _ :=
  direct_sum.decompose_coe ğ“ (âŸ¨1, set_like.graded_monoid.one_memâŸ© : ğ“ 0),
suffices h : (scalar_hom ğ“ ğ“œ).comp (direct_sum.decompose_add_equiv ğ“).to_add_monoid_hom 1 b = b,
from h, direct_sum.induction_on b (by rw [map_zero]) (Î» i b, begin
    rw [add_monoid_hom.comp_apply, add_equiv.coe_to_add_monoid_hom, eq0, scalar_hom_of_of],
    refine of_congr (zero_add _) _,
    change (1 : A) â€¢ b.1 = _,
    rw one_smul,
  end) $ Î» x y hx hy, by rw [map_add, hx, hy]

lemma mul_smul [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  (a b : A) (c : â¨ i, ğ“œ i) :
  (a * b) â€¢ c = a â€¢ (b â€¢ c) :=
have m : âˆ€ x, x âˆˆ supr ğ“,
from Î» x, (graded_algebra.is_internal ğ“).submodule_supr_eq_top.symm â–¸ submodule.mem_top,
have of_congr : âˆ€ {i i' : Î¹} (h1 : i = i') {x : ğ“œ i} {y : ğ“œ i'} (h2 : x.1 = y.1),
  direct_sum.of _ i x = direct_sum.of _ i' y,
from Î» _ _ h1 x y h2,
  direct_sum.of_congr _ h1 $ by subst h1; exact heq_of_eq (subtype.ext_iff_val.2 h2),
begin
  unfold has_scalar.smul,
  induction c using direct_sum.induction_on with i c x y hx hy,
  { rw [map_zero, map_zero, map_zero] },
  { rw [add_monoid_hom.comp_apply, add_equiv.coe_to_add_monoid_hom],
    refine submodule.supr_induction ğ“ (m a) _ _ _,
    { intros k a ha,
      refine submodule.supr_induction ğ“ (m b) _ _ _,
      { intros j b hb,
        rw [show direct_sum.decompose_add_equiv ğ“ (a * b) = _, from
          direct_sum.decompose_coe ğ“ (âŸ¨a * b, set_like.graded_monoid.mul_mem ha hbâŸ© : ğ“ (k + j)),
          scalar_hom_of_of, add_monoid_hom.comp_apply, add_equiv.coe_to_add_monoid_hom,
          add_monoid_hom.comp_apply, add_equiv.coe_to_add_monoid_hom,
          show direct_sum.decompose_add_equiv ğ“ b = _, from direct_sum.decompose_coe ğ“ âŸ¨b, hbâŸ©,
          scalar_hom_of_of,
          show direct_sum.decompose_add_equiv ğ“ a = _, from direct_sum.decompose_coe ğ“ âŸ¨a, haâŸ©,
          scalar_hom_of_of],
        refine of_congr (add_assoc _ _ _) _,
        change ((a : A) * b) â€¢ (c : M) = (a : A) â€¢ ((b : A) â€¢ c),
        rw mul_smul, },
      { simp only [map_zero, mul_zero, add_monoid_hom.zero_apply], },
      { intros x y hx hy,
        simp only [mul_add, map_add, add_monoid_hom.add_apply, hx, hy], } },
    { simp only [map_zero, zero_mul, add_monoid_hom.zero_apply], },
    { intros x y hx hy,
      simp only [add_mul, map_add, add_monoid_hom.add_apply, hx, hy], }, },
  { simp only [map_add, hx, hy], },
end

lemma smul_add [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  (a : A) (b c : â¨ i, ğ“œ i) :
  a â€¢ (b + c) = a â€¢ b + a â€¢ c :=
begin
  unfold has_scalar.smul,
  rw [map_add],
end

lemma smul_zero [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  (a : A) :
  a â€¢ (0 : â¨ i, ğ“œ i) = 0 :=
begin
  unfold has_scalar.smul,
  rw [map_zero],
end

def distrib_mul_action [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ] :
  distrib_mul_action A (â¨ i, ğ“œ i) :=
{ smul := (â€¢),
  one_smul := one_smul ğ“ ğ“œ,
  mul_smul := mul_smul ğ“ ğ“œ,
  smul_add := smul_add ğ“ ğ“œ,
  smul_zero := smul_zero ğ“ ğ“œ }

local attribute [instance] graded_module.distrib_mul_action

lemma add_smul [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  (a b : A) (c : â¨ i, ğ“œ i) :
  (a + b) â€¢ c = a â€¢ c + b â€¢ c :=
begin
  unfold has_scalar.smul,
  simp only [map_add, add_monoid_hom.add_apply],
end

lemma zero_smul [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  (a : â¨ i, ğ“œ i) :
  (0 : A) â€¢ a = 0 :=
begin
  unfold has_scalar.smul,
  simp only [map_zero, add_monoid_hom.zero_apply],
end

def is_module [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ] :
  module A (â¨ i, ğ“œ i) :=
{ add_smul := add_smul ğ“ ğ“œ,
  zero_smul := zero_smul ğ“ ğ“œ,
  ..(distrib_mul_action ğ“ ğ“œ)}

local attribute [instance] graded_module.is_module

def linear_equiv [decidable_eq Î¹] [graded_algebra ğ“]
  [@set_like.has_graded_scalar Î¹ _ A _ M _ _ _ _ ğ“ ğ“œ]
  [direct_sum.decomposition ğ“œ] :
  M â‰ƒâ‚—[A] â¨ i, ğ“œ i :=
have m : âˆ€ x, x âˆˆ supr ğ“,
from Î» x, (graded_algebra.is_internal ğ“).submodule_supr_eq_top.symm â–¸ submodule.mem_top,
{ to_fun := direct_sum.decompose_add_equiv ğ“œ,
  map_add' := Î» x y, map_add _ _ _,
  map_smul' := Î» x y, begin
    classical,
    rw [â† direct_sum.sum_support_decompose ğ“ x, map_sum, finset.sum_smul, map_sum,
      finset.sum_smul, finset.sum_congr rfl],
    intros i hi,
    rw [ring_hom.id_apply, â† direct_sum.sum_support_decompose ğ“œ y, map_sum, finset.smul_sum,
      map_sum, finset.smul_sum, finset.sum_congr rfl],
    intros j hj,
    unfold has_scalar.smul,
    rw [add_monoid_hom.comp_apply, add_equiv.coe_to_add_monoid_hom],
    simp only [direct_sum.decompose_add_equiv_apply, direct_sum.decompose_coe],
    rw [scalar_hom_of_of],
    convert direct_sum.decompose_coe ğ“œ _,
    refl,
  end,
  inv_fun := (direct_sum.decompose_add_equiv ğ“œ).symm,
  left_inv := add_equiv.apply_symm_apply _,
  right_inv := add_equiv.symm_apply_apply _ }

end graded_module
