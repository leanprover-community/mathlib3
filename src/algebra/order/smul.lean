/-
Copyright (c) 2020 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import algebra.module.pi
import algebra.module.prod
import algebra.order.monoid.prod
import algebra.order.pi
import data.set.pointwise.smul
import tactic.positivity

/-!
# Ordered scalar product

In this file we define

* `ordered_smul R M` : an ordered additive commutative monoid `M` is an `ordered_smul`
  over an `ordered_semiring` `R` if the scalar product respects the order relation on the
  monoid and on the ring. There is a correspondence between this structure and convex cones,
  which is proven in `analysis/convex/cone.lean`.

## Implementation notes

* We choose to define `ordered_smul` as a `Prop`-valued mixin, so that it can be
  used for actions, modules, and algebras
  (the axioms for an "ordered algebra" are exactly that the algebra is ordered as a module).
* To get ordered modules and ordered vector spaces, it suffices to replace the
  `order_add_comm_monoid` and the `ordered_semiring` as desired.

## References

* https://en.wikipedia.org/wiki/Ordered_module

## Tags

ordered module, ordered scalar, ordered smul, ordered action, ordered vector space
-/

open_locale pointwise

/--
The ordered scalar product property is when an ordered additive commutative monoid
with a partial order has a scalar multiplication which is compatible with the order.
-/
@[protect_proj]
class ordered_smul (R M : Type*)
  [ordered_semiring R] [ordered_add_comm_monoid M] [smul_with_zero R M] : Prop :=
(smul_lt_smul_of_pos : ‚àÄ {a b : M}, ‚àÄ {c : R}, a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b)
(lt_of_smul_lt_smul_of_pos : ‚àÄ {a b : M}, ‚àÄ {c : R}, c ‚Ä¢ a < c ‚Ä¢ b ‚Üí 0 < c ‚Üí a < b)

variables {Œπ ùïú R M N : Type*}

namespace order_dual

instance [has_zero R] [add_zero_class M] [h : smul_with_zero R M] : smul_with_zero R M·µí·µà :=
{ zero_smul := Œª m, order_dual.rec (zero_smul _) m,
  smul_zero := Œª r, order_dual.rec smul_zero r,
  ..order_dual.has_smul }

instance [monoid R] [mul_action R M] : mul_action R M·µí·µà :=
{ one_smul := Œª m, order_dual.rec (one_smul _) m,
  mul_smul := Œª r, order_dual.rec mul_smul r,
  ..order_dual.has_smul }

instance [monoid_with_zero R] [add_monoid M] [mul_action_with_zero R M] :
  mul_action_with_zero R M·µí·µà :=
{ ..order_dual.mul_action, ..order_dual.smul_with_zero }

instance [monoid_with_zero R] [add_monoid M] [distrib_mul_action R M] :
  distrib_mul_action R M·µí·µà :=
{ smul_add := Œª k a, order_dual.rec (Œª a' b, order_dual.rec (smul_add _ _) b) a,
  smul_zero := Œª r, order_dual.rec (@smul_zero _ M _ _) r }

instance [ordered_semiring R] [ordered_add_comm_monoid M] [smul_with_zero R M]
  [ordered_smul R M] :
  ordered_smul R M·µí·µà :=
{ smul_lt_smul_of_pos := Œª a b, @ordered_smul.smul_lt_smul_of_pos R M _ _ _ _ b a,
  lt_of_smul_lt_smul_of_pos := Œª a b,
    @ordered_smul.lt_of_smul_lt_smul_of_pos R M _ _ _ _ b a }

end order_dual

section ordered_smul
variables [ordered_semiring R] [ordered_add_comm_monoid M] [smul_with_zero R M] [ordered_smul R M]
  {s : set M} {a b : M} {c : R}

lemma smul_lt_smul_of_pos : a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b := ordered_smul.smul_lt_smul_of_pos

lemma smul_le_smul_of_nonneg (h‚ÇÅ : a ‚â§ b) (h‚ÇÇ : 0 ‚â§ c) : c ‚Ä¢ a ‚â§ c ‚Ä¢ b :=
begin
  rcases h‚ÇÅ.eq_or_lt with rfl|hab,
  { refl },
  { rcases h‚ÇÇ.eq_or_lt with rfl|hc,
    { rw [zero_smul, zero_smul] },
    { exact (smul_lt_smul_of_pos hab hc).le } }
end

lemma smul_nonneg (hc : 0 ‚â§ c) (ha : 0 ‚â§ a) : 0 ‚â§ c ‚Ä¢ a :=
calc (0 : M) = c ‚Ä¢ (0 : M) : (smul_zero c).symm
         ... ‚â§ c ‚Ä¢ a : smul_le_smul_of_nonneg ha hc

lemma smul_nonpos_of_nonneg_of_nonpos (hc : 0 ‚â§ c) (ha : a ‚â§ 0) : c ‚Ä¢ a ‚â§ 0 :=
@smul_nonneg R M·µí·µà _ _ _ _ _ _ hc ha

lemma eq_of_smul_eq_smul_of_pos_of_le (h‚ÇÅ : c ‚Ä¢ a = c ‚Ä¢ b) (hc : 0 < c) (hle : a ‚â§ b) :
  a = b :=
hle.lt_or_eq.resolve_left $ Œª hlt, (smul_lt_smul_of_pos hlt hc).ne h‚ÇÅ

lemma lt_of_smul_lt_smul_of_nonneg (h : c ‚Ä¢ a < c ‚Ä¢ b) (hc : 0 ‚â§ c) : a < b :=
hc.eq_or_lt.elim (Œª hc, false.elim $ lt_irrefl (0:M) $ by rwa [‚Üê hc, zero_smul, zero_smul] at h)
  (ordered_smul.lt_of_smul_lt_smul_of_pos h)

lemma smul_lt_smul_iff_of_pos (hc : 0 < c) : c ‚Ä¢ a < c ‚Ä¢ b ‚Üî a < b :=
‚ü®Œª h, lt_of_smul_lt_smul_of_nonneg h hc.le, Œª h, smul_lt_smul_of_pos h hc‚ü©

lemma smul_pos_iff_of_pos (hc : 0 < c) : 0 < c ‚Ä¢ a ‚Üî 0 < a :=
calc 0 < c ‚Ä¢ a ‚Üî c ‚Ä¢ 0 < c ‚Ä¢ a : by rw smul_zero
           ... ‚Üî 0 < a         : smul_lt_smul_iff_of_pos hc

alias smul_pos_iff_of_pos ‚Üî _ smul_pos

lemma monotone_smul_left (hc : 0 ‚â§ c) : monotone (has_smul.smul c : M ‚Üí M) :=
Œª a b h, smul_le_smul_of_nonneg h hc

lemma strict_mono_smul_left (hc : 0 < c) : strict_mono (has_smul.smul c : M ‚Üí M) :=
Œª a b h, smul_lt_smul_of_pos h hc

lemma smul_lower_bounds_subset_lower_bounds_smul (hc : 0 ‚â§ c) :
  c ‚Ä¢ lower_bounds s ‚äÜ lower_bounds (c ‚Ä¢ s) :=
(monotone_smul_left hc).image_lower_bounds_subset_lower_bounds_image

lemma smul_upper_bounds_subset_upper_bounds_smul (hc : 0 ‚â§ c) :
  c ‚Ä¢ upper_bounds s ‚äÜ upper_bounds (c ‚Ä¢ s) :=
(monotone_smul_left hc).image_upper_bounds_subset_upper_bounds_image

lemma bdd_below.smul_of_nonneg (hs : bdd_below s) (hc : 0 ‚â§ c) : bdd_below (c ‚Ä¢ s) :=
(monotone_smul_left hc).map_bdd_below hs

lemma bdd_above.smul_of_nonneg (hs : bdd_above s) (hc : 0 ‚â§ c) : bdd_above (c ‚Ä¢ s) :=
(monotone_smul_left hc).map_bdd_above hs

end ordered_smul

/-- To prove that a linear ordered monoid is an ordered module, it suffices to verify only the first
axiom of `ordered_smul`. -/
lemma ordered_smul.mk'' [ordered_semiring ùïú] [linear_ordered_add_comm_monoid M] [smul_with_zero ùïú M]
  (h : ‚àÄ ‚¶Éc : ùïú‚¶Ñ, 0 < c ‚Üí strict_mono (Œª a : M, c ‚Ä¢ a)) :
  ordered_smul ùïú M :=
{ smul_lt_smul_of_pos := Œª a b c hab hc, h hc hab,
  lt_of_smul_lt_smul_of_pos := Œª a b c hab hc, (h hc).lt_iff_lt.1 hab }

instance nat.ordered_smul [linear_ordered_cancel_add_comm_monoid M] : ordered_smul ‚Ñï M :=
ordered_smul.mk'' $ Œª n hn a b hab, begin
  cases n,
  { cases hn },
  induction n with n ih,
  { simp only [one_nsmul, hab], },
  { simp only [succ_nsmul _ n.succ, add_lt_add hab (ih n.succ_pos)] }
end

instance int.ordered_smul [linear_ordered_add_comm_group M] : ordered_smul ‚Ñ§ M :=
ordered_smul.mk'' $ Œª n hn, begin
  cases n,
  { simp only [int.of_nat_eq_coe, int.coe_nat_pos, coe_nat_zsmul] at ‚ä¢ hn,
    exact strict_mono_smul_left hn },
  { cases (int.neg_succ_not_pos _).1 hn }
end

-- TODO: `linear_ordered_field M ‚Üí ordered_smul ‚Ñö M`

instance linear_ordered_semiring.to_ordered_smul {R : Type*} [linear_ordered_semiring R] :
  ordered_smul R R :=
ordered_smul.mk'' $ Œª c, strict_mono_mul_left_of_pos

section linear_ordered_semifield
variables [linear_ordered_semifield ùïú] [ordered_add_comm_monoid M] [ordered_add_comm_monoid N]
  [mul_action_with_zero ùïú M] [mul_action_with_zero ùïú N]

/-- To prove that a vector space over a linear ordered field is ordered, it suffices to verify only
the first axiom of `ordered_smul`. -/
lemma ordered_smul.mk' (h : ‚àÄ ‚¶Éa b : M‚¶Ñ ‚¶Éc : ùïú‚¶Ñ, a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a ‚â§ c ‚Ä¢ b) :
  ordered_smul ùïú M :=
begin
  have hlt' : ‚àÄ ‚¶Éa b : M‚¶Ñ ‚¶Éc : ùïú‚¶Ñ, a < b ‚Üí 0 < c ‚Üí c ‚Ä¢ a < c ‚Ä¢ b,
  { refine Œª a b c hab hc, (h hab hc).lt_of_ne _,
    rw [ne.def, hc.ne'.is_unit.smul_left_cancel],
    exact hab.ne },
  refine { smul_lt_smul_of_pos := hlt', .. },
  intros a b c hab hc,
  obtain ‚ü®c, rfl‚ü© := hc.ne'.is_unit,
  rw [‚Üê inv_smul_smul c a, ‚Üê inv_smul_smul c b],
  refine hlt' hab (pos_of_mul_pos_right _ hc.le),
  simp only [c.mul_inv, zero_lt_one]
end

instance [ordered_smul ùïú M] [ordered_smul ùïú N] : ordered_smul ùïú (M √ó N) :=
ordered_smul.mk' $ Œª a b c h hc,
  ‚ü®smul_le_smul_of_nonneg h.1.1 hc.le, smul_le_smul_of_nonneg h.1.2 hc.le‚ü©

instance pi.ordered_smul {M : Œπ ‚Üí Type*} [Œ† i, ordered_add_comm_monoid (M i)]
  [Œ† i, mul_action_with_zero ùïú (M i)] [‚àÄ i, ordered_smul ùïú (M i)] :
  ordered_smul ùïú (Œ† i, M i) :=
ordered_smul.mk' $ Œª v u c h hc i, smul_le_smul_of_nonneg (h.le i) hc.le

/- Sometimes Lean fails to apply the dependent version to non-dependent functions, so we define
another instance. -/
instance pi.ordered_smul' [ordered_smul ùïú M] : ordered_smul ùïú (Œπ ‚Üí M) := pi.ordered_smul

/- Sometimes Lean fails to unify the module with the scalars, so we define another instance. -/
instance pi.ordered_smul'' : ordered_smul ùïú (Œπ ‚Üí ùïú) := @pi.ordered_smul' Œπ ùïú ùïú _ _ _ _

variables [ordered_smul ùïú M] {s : set M} {a b : M} {c : ùïú}

lemma smul_le_smul_iff_of_pos (hc : 0 < c) : c ‚Ä¢ a ‚â§ c ‚Ä¢ b ‚Üî a ‚â§ b :=
‚ü®Œª h, inv_smul_smul‚ÇÄ hc.ne' a ‚ñ∏ inv_smul_smul‚ÇÄ hc.ne' b ‚ñ∏
  smul_le_smul_of_nonneg h (inv_nonneg.2 hc.le),
  Œª h, smul_le_smul_of_nonneg h hc.le‚ü©

lemma inv_smul_le_iff (h : 0 < c) : c‚Åª¬π ‚Ä¢ a ‚â§ b ‚Üî a ‚â§ c ‚Ä¢ b :=
by { rw [‚Üêsmul_le_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne'], apply_instance }

lemma inv_smul_lt_iff (h : 0 < c) : c‚Åª¬π ‚Ä¢ a < b ‚Üî a < c ‚Ä¢ b :=
by { rw [‚Üêsmul_lt_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne'], apply_instance }

lemma le_inv_smul_iff (h : 0 < c) : a ‚â§ c‚Åª¬π ‚Ä¢ b ‚Üî c ‚Ä¢ a ‚â§ b :=
by { rw [‚Üêsmul_le_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne'], apply_instance }

lemma lt_inv_smul_iff (h : 0 < c) : a < c‚Åª¬π ‚Ä¢ b ‚Üî c ‚Ä¢ a < b :=
by { rw [‚Üêsmul_lt_smul_iff_of_pos h, smul_inv_smul‚ÇÄ h.ne'], apply_instance }

variables (M)

/-- Left scalar multiplication as an order isomorphism. -/
@[simps] def order_iso.smul_left (hc : 0 < c) : M ‚âÉo M :=
{ to_fun := Œª b, c ‚Ä¢ b,
  inv_fun := Œª b, c‚Åª¬π ‚Ä¢ b,
  left_inv := inv_smul_smul‚ÇÄ hc.ne',
  right_inv := smul_inv_smul‚ÇÄ hc.ne',
  map_rel_iff' := Œª b‚ÇÅ b‚ÇÇ, smul_le_smul_iff_of_pos hc }

variables {M}

@[simp] lemma lower_bounds_smul_of_pos (hc : 0 < c) : lower_bounds (c ‚Ä¢ s) = c ‚Ä¢ lower_bounds s :=
(order_iso.smul_left _ hc).lower_bounds_image

@[simp] lemma upper_bounds_smul_of_pos (hc : 0 < c) : upper_bounds (c ‚Ä¢ s) = c ‚Ä¢ upper_bounds s :=
(order_iso.smul_left _ hc).upper_bounds_image

@[simp] lemma bdd_below_smul_iff_of_pos (hc : 0 < c) : bdd_below (c ‚Ä¢ s) ‚Üî bdd_below s :=
(order_iso.smul_left _ hc).bdd_below_image

@[simp] lemma bdd_above_smul_iff_of_pos (hc : 0 < c) : bdd_above (c ‚Ä¢ s) ‚Üî bdd_above s :=
(order_iso.smul_left _ hc).bdd_above_image

end linear_ordered_semifield

namespace tactic
section ordered_smul
variables [ordered_semiring R] [ordered_add_comm_monoid M] [smul_with_zero R M] [ordered_smul R M]
  {a : R} {b : M}

private lemma smul_nonneg_of_pos_of_nonneg (ha : 0 < a) (hb : 0 ‚â§ b) : 0 ‚â§ a ‚Ä¢ b :=
smul_nonneg ha.le hb

private lemma smul_nonneg_of_nonneg_of_pos (ha : 0 ‚â§ a) (hb : 0 < b) : 0 ‚â§ a ‚Ä¢ b :=
smul_nonneg ha hb.le

end ordered_smul

section no_zero_smul_divisors
variables [has_zero R] [has_zero M] [has_smul R M] [no_zero_smul_divisors R M] {a : R} {b : M}

private lemma smul_ne_zero_of_pos_of_ne_zero [preorder R] (ha : 0 < a) (hb : b ‚â† 0) : a ‚Ä¢ b ‚â† 0 :=
smul_ne_zero ha.ne' hb

private lemma smul_ne_zero_of_ne_zero_of_pos [preorder M] (ha : a ‚â† 0) (hb : 0 < b) : a ‚Ä¢ b ‚â† 0 :=
smul_ne_zero ha hb.ne'

end no_zero_smul_divisors

open positivity

/-- Extension for the `positivity` tactic: scalar multiplication is nonnegative/positive/nonzero if
both sides are. -/
@[positivity]
meta def positivity_smul : expr ‚Üí tactic strictness
| e@`(%%a ‚Ä¢ %%b) := do
  strictness_a ‚Üê core a,
  strictness_b ‚Üê core b,
  match strictness_a, strictness_b with
  | positive pa, positive pb := positive <$> mk_app ``smul_pos [pa, pb]
  | positive pa, nonnegative pb := nonnegative <$> mk_app ``smul_nonneg_of_pos_of_nonneg [pa, pb]
  | nonnegative pa, positive pb := nonnegative <$> mk_app ``smul_nonneg_of_nonneg_of_pos [pa, pb]
  | nonnegative pa, nonnegative pb := nonnegative <$> mk_app ``smul_nonneg [pa, pb]
  | positive pa, nonzero pb := nonzero <$> to_expr ``(smul_ne_zero_of_pos_of_ne_zero %%pa %%pb)
  | nonzero pa, positive pb := nonzero <$> to_expr ``(smul_ne_zero_of_ne_zero_of_pos %%pa %%pb)
  | nonzero pa, nonzero pb := nonzero <$> to_expr ``(smul_ne_zero %%pa %%pb)
  | sa@_, sb@ _ := positivity_fail e a b sa sb
  end
| e := pp e >>= fail ‚àò format.bracket "The expression `" "` isn't of the form `a ‚Ä¢ b`"

end tactic
