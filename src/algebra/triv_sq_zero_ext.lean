/-
Copyright (c) 2020 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import algebra.algebra.basic
import linear_algebra.prod

/-!
# Trivial Square-Zero Extension

Given a module `M` over a ring `R`, the trivial square-zero extension of `M` over `R` is defined
to be the `R`-algebra `R ‚äï M` with multiplication given by
`(r‚ÇÅ + m‚ÇÅ) * (r‚ÇÇ + m‚ÇÇ) = r‚ÇÅ r‚ÇÇ + r‚ÇÅ m‚ÇÇ + r‚ÇÇ m‚ÇÅ`.

It is a square-zero extension because `M^2 = 0`.

## Main definitions

* `triv_sq_zero_ext.inl`, `triv_sq_zero_ext.inr`: the canonical inclusions into
  `triv_sq_zero_ext R M`.
* `triv_sq_zero_ext.fst`, `triv_sq_zero_ext.snd`: the canonical projections from
  `triv_sq_zero_ext R M`.
* `triv_sq_zero_ext.algebra`: the associated `R`-algebra structure.

-/

universes u v w

/--
"Trivial Square-Zero Extension".

Given a module `M` over a ring `R`, the trivial square-zero extension of `M` over `R` is defined
to be the `R`-algebra `R √ó M` with multiplication given by
`(r‚ÇÅ + m‚ÇÅ) * (r‚ÇÇ + m‚ÇÇ) = r‚ÇÅ r‚ÇÇ + r‚ÇÅ m‚ÇÇ + r‚ÇÇ m‚ÇÅ`.

It is a square-zero extension because `M^2 = 0`.
-/
def triv_sq_zero_ext (R : Type u) (M : Type v) :=
R √ó M

local notation `tsze` := triv_sq_zero_ext

namespace triv_sq_zero_ext

section basic
variables {R : Type u} {M : Type v}

/-- The canonical inclusion `R ‚Üí triv_sq_zero_ext R M`. -/
def inl [has_zero M] (r : R) : tsze R M :=
(r, 0)

/-- The canonical inclusion `M ‚Üí triv_sq_zero_ext R M`. -/
def inr [has_zero R] (m : M) : tsze R M :=
(0, m)

/-- The canonical projection `triv_sq_zero_ext R M ‚Üí R`. -/
def fst (x : tsze R M) : R :=
x.1

/-- The canonical projection `triv_sq_zero_ext R M ‚Üí M`. -/
def snd (x : tsze R M) : M :=
x.2

@[ext] lemma ext {x y : tsze R M} (h1 : x.fst = y.fst) (h2 : x.snd = y.snd) : x = y :=
prod.ext h1 h2

section
variables (M)
@[simp] lemma fst_inl [has_zero M] (r : R) : (inl r : tsze R M).fst = r := rfl
@[simp] lemma snd_inl [has_zero M] (r : R) : (inl r : tsze R M).snd = 0 := rfl
end

section
variables (R)
@[simp] lemma fst_inr [has_zero R] (m : M) : (inr m : tsze R M).fst = 0 := rfl
@[simp] lemma snd_inr [has_zero R] (m : M) : (inr m : tsze R M).snd = m := rfl
end

lemma inl_injective [has_zero M] : function.injective (inl : R ‚Üí tsze R M) :=
function.left_inverse.injective $ fst_inl _

lemma inr_injective [has_zero R] : function.injective (inr : M ‚Üí tsze R M) :=
function.left_inverse.injective $ snd_inr _

end basic

/-! ### Structures inherited from `prod`

Additive operators and scalar multiplication operate elementwise. -/

section additive

variables {T : Type*} {S : Type*} {R : Type u} {M : Type v}

instance [inhabited R] [inhabited M] : inhabited (tsze R M) :=
prod.inhabited

instance [has_zero R] [has_zero M] : has_zero (tsze R M) :=
prod.has_zero

instance [has_add R] [has_add M] : has_add (tsze R M) :=
prod.has_add

instance [has_neg R] [has_neg M] : has_neg (tsze R M) :=
prod.has_neg

instance [add_semigroup R] [add_semigroup M] : add_semigroup (tsze R M) :=
prod.add_semigroup

instance [add_zero_class R] [add_zero_class M] : add_zero_class (tsze R M) :=
prod.add_zero_class

instance [add_monoid R] [add_monoid M] : add_monoid (tsze R M) :=
prod.add_monoid

instance [add_group R] [add_group M] : add_group (tsze R M) :=
prod.add_group

instance [add_comm_semigroup R] [add_comm_semigroup M] : add_comm_semigroup (tsze R M) :=
prod.add_comm_semigroup

instance [add_comm_monoid R] [add_comm_monoid M] : add_comm_monoid (tsze R M) :=
prod.add_comm_monoid

instance [add_comm_group R] [add_comm_group M] : add_comm_group (tsze R M) :=
prod.add_comm_group

instance [has_scalar S R] [has_scalar S M] : has_scalar S (tsze R M) :=
prod.has_scalar

instance [has_scalar T R] [has_scalar T M] [has_scalar S R] [has_scalar S M] [has_scalar T S]
  [is_scalar_tower T S R] [is_scalar_tower T S M] : is_scalar_tower T S (tsze R M) :=
prod.is_scalar_tower

instance [has_scalar T R] [has_scalar T M] [has_scalar S R] [has_scalar S M]
  [smul_comm_class T S R] [smul_comm_class T S M] : smul_comm_class T S (tsze R M) :=
prod.smul_comm_class

instance [has_scalar S R] [has_scalar S M] [has_scalar S·µê·µí·µñ R] [has_scalar S·µê·µí·µñ M]
  [is_central_scalar S R] [is_central_scalar S M] : is_central_scalar S (tsze R M) :=
prod.is_central_scalar

instance [monoid S] [mul_action S R] [mul_action S M] : mul_action S (tsze R M) :=
prod.mul_action

instance [monoid S] [add_monoid R] [add_monoid M]
  [distrib_mul_action S R] [distrib_mul_action S M] : distrib_mul_action S (tsze R M) :=
prod.distrib_mul_action

instance [semiring S] [add_comm_monoid R] [add_comm_monoid M]
  [module S R] [module S M] : module S (tsze R M) :=
prod.module

@[simp] lemma fst_zero [has_zero R] [has_zero M] : (0 : tsze R M).fst = 0 := rfl
@[simp] lemma snd_zero [has_zero R] [has_zero M] : (0 : tsze R M).snd = 0 := rfl

@[simp] lemma fst_add [has_add R] [has_add M] (x‚ÇÅ x‚ÇÇ : tsze R M) :
  (x‚ÇÅ + x‚ÇÇ).fst = x‚ÇÅ.fst + x‚ÇÇ.fst := rfl
@[simp] lemma snd_add [has_add R] [has_add M] (x‚ÇÅ x‚ÇÇ : tsze R M) :
  (x‚ÇÅ + x‚ÇÇ).snd = x‚ÇÅ.snd + x‚ÇÇ.snd := rfl

@[simp] lemma fst_neg [has_neg R] [has_neg M] (x : tsze R M) : (-x).fst = -x.fst := rfl
@[simp] lemma snd_neg [has_neg R] [has_neg M] (x : tsze R M) : (-x).snd = -x.snd := rfl

@[simp] lemma fst_smul [has_scalar S R] [has_scalar S M] (s : S) (x : tsze R M) :
  (s ‚Ä¢ x).fst = s ‚Ä¢ x.fst := rfl
@[simp] lemma snd_smul [has_scalar S R] [has_scalar S M] (s : S) (x : tsze R M) :
  (s ‚Ä¢ x).snd = s ‚Ä¢ x.snd := rfl

section
variables (M)

@[simp] lemma inl_zero [has_zero R] [has_zero M] : (inl 0 : tsze R M) = 0 := rfl

@[simp] lemma inl_add [has_add R] [add_zero_class M] (r‚ÇÅ r‚ÇÇ : R) :
  (inl (r‚ÇÅ + r‚ÇÇ) : tsze R M) = inl r‚ÇÅ + inl r‚ÇÇ :=
ext rfl (add_zero 0).symm

@[simp] lemma inl_neg [has_neg R] [add_group M] (r : R) :
  (inl (-r) : tsze R M) = -inl r :=
ext rfl neg_zero.symm

@[simp] lemma inl_smul [monoid S] [add_monoid M] [has_scalar S R] [distrib_mul_action S M]
  (s : S) (r : R) : (inl (s ‚Ä¢ r) : tsze R M) = s ‚Ä¢ inl r :=
ext rfl (smul_zero s).symm

end

section
variables (R)

@[simp] lemma inr_zero [has_zero R] [has_zero M] : (inr 0 : tsze R M) = 0 := rfl

@[simp] lemma inr_add [add_zero_class R] [add_zero_class M] (m‚ÇÅ m‚ÇÇ : M) :
  (inr (m‚ÇÅ + m‚ÇÇ) : tsze R M) = inr m‚ÇÅ + inr m‚ÇÇ :=
ext (add_zero 0).symm rfl

@[simp] lemma inr_neg [add_group R] [has_neg M] (m : M) :
  (inr (-m) : tsze R M) = -inr m :=
ext neg_zero.symm rfl

@[simp] lemma inr_smul [has_zero R] [has_zero S] [smul_with_zero S R] [has_scalar S M]
  (r : S) (m : M) : (inr (r ‚Ä¢ m) : tsze R M) = r ‚Ä¢ inr m :=
ext (smul_zero' _ _).symm rfl

end

lemma inl_fst_add_inr_snd_eq [add_zero_class R] [add_zero_class M] (x : tsze R M) :
  inl x.fst + inr x.snd = x :=
ext (add_zero x.1) (zero_add x.2)

/-- To show a property hold on all `triv_sq_zero_ext R M` it suffices to show it holds
on terms of the form `inl r + inr m`.

This can be used as `induction x using triv_sq_zero_ext.ind`. -/
lemma ind {R M} [add_zero_class R] [add_zero_class M] {P : triv_sq_zero_ext R M ‚Üí Prop}
  (h : ‚àÄ r m, P (inl r + inr m)) (x) : P x :=
inl_fst_add_inr_snd_eq x ‚ñ∏ h x.1 x.2

@[ext]
lemma linear_map_ext {N} [semiring S] [add_comm_monoid R] [add_comm_monoid M] [add_comm_monoid N]
  [module S R] [module S M] [module S N] ‚¶Éf g : tsze R M ‚Üí‚Çó[S] N‚¶Ñ
  (hl : ‚àÄ r, f (inl r) = g (inl r)) (hr : ‚àÄ m, f (inr m) = g (inr m)) :
  f = g :=
linear_map.prod_ext (linear_map.ext hl) (linear_map.ext hr)

variables (R M)

/-- The canonical `R`-linear inclusion `M ‚Üí triv_sq_zero_ext R M`. -/
@[simps apply]
def inr_hom [semiring R] [add_comm_monoid M] [module R M] : M ‚Üí‚Çó[R] tsze R M :=
{ to_fun := inr, ..linear_map.inr _ _ _ }

/-- The canonical `R`-linear projection `triv_sq_zero_ext R M ‚Üí M`. -/
@[simps apply]
def snd_hom [semiring R] [add_comm_monoid M] [module R M] : tsze R M ‚Üí‚Çó[R] M :=
{ to_fun := snd, ..linear_map.snd _ _ _ }

end additive

/-! ### Multiplicative structure -/

section mul
variables {R : Type u} {M : Type v}

instance [has_one R] [has_zero M] : has_one (tsze R M) :=
‚ü®(1, 0)‚ü©

instance [has_mul R] [has_add M] [has_scalar R M] : has_mul (tsze R M) :=
‚ü®Œª x y, (x.1 * y.1, x.1 ‚Ä¢ y.2 + y.1 ‚Ä¢ x.2)‚ü©

@[simp] lemma fst_one [has_one R] [has_zero M] : (1 : tsze R M).fst = 1 := rfl
@[simp] lemma snd_one [has_one R] [has_zero M] : (1 : tsze R M).snd = 0 := rfl

@[simp] lemma fst_mul [has_mul R] [has_add M] [has_scalar R M] (x‚ÇÅ x‚ÇÇ : tsze R M) :
  (x‚ÇÅ * x‚ÇÇ).fst = x‚ÇÅ.fst * x‚ÇÇ.fst := rfl
@[simp] lemma snd_mul [has_mul R] [has_add M] [has_scalar R M] (x‚ÇÅ x‚ÇÇ : tsze R M) :
  (x‚ÇÅ * x‚ÇÇ).snd = x‚ÇÅ.fst ‚Ä¢ x‚ÇÇ.snd + x‚ÇÇ.fst ‚Ä¢ x‚ÇÅ.snd := rfl

section
variables (M)

@[simp] lemma inl_one [has_one R] [has_zero M] : (inl 1 : tsze R M) = 1 := rfl

@[simp] lemma inl_mul [monoid R] [add_monoid M] [distrib_mul_action R M] (r‚ÇÅ r‚ÇÇ : R) :
  (inl (r‚ÇÅ * r‚ÇÇ) : tsze R M) = inl r‚ÇÅ * inl r‚ÇÇ :=
ext rfl $ show (0 : M) = r‚ÇÅ ‚Ä¢ 0 + r‚ÇÇ ‚Ä¢ 0, by rw [smul_zero, zero_add, smul_zero]

lemma inl_mul_inl [monoid R] [add_monoid M] [distrib_mul_action R M] (r‚ÇÅ r‚ÇÇ : R) :
  (inl r‚ÇÅ * inl r‚ÇÇ : tsze R M) = inl (r‚ÇÅ * r‚ÇÇ) :=
(inl_mul M r‚ÇÅ r‚ÇÇ).symm

end

section
variables (R)

@[simp] lemma inr_mul_inr [semiring R] [add_comm_monoid M] [module R M] (m‚ÇÅ m‚ÇÇ : M) :
  (inr m‚ÇÅ * inr m‚ÇÇ : tsze R M) = 0 :=
ext (mul_zero _) $ show (0 : R) ‚Ä¢ m‚ÇÇ + (0 : R) ‚Ä¢ m‚ÇÅ = 0, by rw [zero_smul, zero_add, zero_smul]

end

lemma inl_mul_inr [semiring R] [add_comm_monoid M] [module R M] (r : R) (m : M) :
  (inl r * inr m : tsze R M) = inr (r ‚Ä¢ m) :=
ext (mul_zero r) $ show r ‚Ä¢ m + (0 : R) ‚Ä¢ 0 = r ‚Ä¢ m, by rw [smul_zero, add_zero]

lemma inr_mul_inl [semiring R] [add_comm_monoid M] [module R M] (r : R) (m : M) :
  (inr m * inl r : tsze R M) = inr (r ‚Ä¢ m) :=
ext (zero_mul r) $ show (0 : R) ‚Ä¢ 0 + r ‚Ä¢ m = r ‚Ä¢ m, by rw [smul_zero, zero_add]

instance [monoid R] [add_monoid M] [distrib_mul_action R M] : mul_one_class (tsze R M) :=
{ one_mul := Œª x, ext (one_mul x.1) $ show (1 : R) ‚Ä¢ x.2 + x.1 ‚Ä¢ 0 = x.2,
    by rw [one_smul, smul_zero, add_zero],
  mul_one := Œª x, ext (mul_one x.1) $ show (x.1 ‚Ä¢ 0 : M) + (1 : R) ‚Ä¢ x.2 = x.2,
    by rw [smul_zero, zero_add, one_smul],
  .. triv_sq_zero_ext.has_one,
  .. triv_sq_zero_ext.has_mul }

instance [semiring R] [add_comm_monoid M] [module R M] : non_assoc_semiring (tsze R M) :=
{ zero_mul := Œª x, ext (zero_mul x.1) $ show (0 : R) ‚Ä¢ x.2 + x.1 ‚Ä¢ 0 = 0,
    by rw [zero_smul, zero_add, smul_zero],
  mul_zero := Œª x, ext (mul_zero x.1) $ show (x.1 ‚Ä¢ 0 : M) + (0 : R) ‚Ä¢ x.2 = 0,
    by rw [smul_zero, zero_add, zero_smul],
  left_distrib := Œª x‚ÇÅ x‚ÇÇ x‚ÇÉ, ext (mul_add x‚ÇÅ.1 x‚ÇÇ.1 x‚ÇÉ.1) $
    show x‚ÇÅ.1 ‚Ä¢ (x‚ÇÇ.2 + x‚ÇÉ.2) + (x‚ÇÇ.1 + x‚ÇÉ.1) ‚Ä¢ x‚ÇÅ.2 =
      x‚ÇÅ.1 ‚Ä¢ x‚ÇÇ.2 + x‚ÇÇ.1 ‚Ä¢ x‚ÇÅ.2 + (x‚ÇÅ.1 ‚Ä¢ x‚ÇÉ.2 + x‚ÇÉ.1 ‚Ä¢ x‚ÇÅ.2),
    by simp_rw [smul_add, add_smul, add_add_add_comm],
  right_distrib := Œª x‚ÇÅ x‚ÇÇ x‚ÇÉ, ext (add_mul x‚ÇÅ.1 x‚ÇÇ.1 x‚ÇÉ.1) $
    show (x‚ÇÅ.1 + x‚ÇÇ.1) ‚Ä¢ x‚ÇÉ.2 + x‚ÇÉ.1 ‚Ä¢ (x‚ÇÅ.2 + x‚ÇÇ.2) =
      x‚ÇÅ.1 ‚Ä¢ x‚ÇÉ.2 + x‚ÇÉ.1 ‚Ä¢ x‚ÇÅ.2 + (x‚ÇÇ.1 ‚Ä¢ x‚ÇÉ.2 + x‚ÇÉ.1 ‚Ä¢ x‚ÇÇ.2),
    by simp_rw [add_smul, smul_add, add_add_add_comm],
  .. triv_sq_zero_ext.mul_one_class,
  .. triv_sq_zero_ext.add_comm_monoid }

instance [comm_monoid R] [add_monoid M] [distrib_mul_action R M] : monoid (tsze R M) :=
{ mul_assoc := Œª x y z, ext (mul_assoc x.1 y.1 z.1) $
    show (x.1 * y.1) ‚Ä¢ z.2 + z.1 ‚Ä¢ (x.1 ‚Ä¢ y.2 + y.1 ‚Ä¢ x.2) =
      x.1 ‚Ä¢ (y.1 ‚Ä¢ z.2 + z.1 ‚Ä¢ y.2) + (y.1 * z.1) ‚Ä¢ x.2,
    by simp_rw [smul_add, ‚Üê mul_smul, add_assoc, mul_comm],
  .. triv_sq_zero_ext.mul_one_class }

instance [comm_monoid R] [add_comm_monoid M] [distrib_mul_action R M] : comm_monoid (tsze R M) :=
{ mul_comm := Œª x‚ÇÅ x‚ÇÇ, ext (mul_comm x‚ÇÅ.1 x‚ÇÇ.1) $
    show x‚ÇÅ.1 ‚Ä¢ x‚ÇÇ.2 + x‚ÇÇ.1 ‚Ä¢ x‚ÇÅ.2 = x‚ÇÇ.1 ‚Ä¢ x‚ÇÅ.2 + x‚ÇÅ.1 ‚Ä¢ x‚ÇÇ.2, from add_comm _ _,
  .. triv_sq_zero_ext.monoid }

instance [comm_semiring R] [add_comm_monoid M] [module R M] : comm_semiring (tsze R M) :=
{ .. triv_sq_zero_ext.comm_monoid,
  .. triv_sq_zero_ext.non_assoc_semiring }

variables (R M)

/-- The canonical inclusion of rings `R ‚Üí triv_sq_zero_ext R M`. -/
@[simps apply]
def inl_hom [semiring R] [add_comm_monoid M] [module R M] : R ‚Üí+* tsze R M :=
{ to_fun := inl,
  map_one' := inl_one M,
  map_mul' := inl_mul M,
  map_zero' := inl_zero M,
  map_add' := inl_add M }

end mul

section algebra
variables (S : Type*) (R : Type u) (M : Type v)
variables [comm_semiring S] [comm_semiring R] [add_comm_monoid M]
variables [algebra S R] [module S M] [module R M] [is_scalar_tower S R M]

instance algebra'  : algebra S (tsze R M) :=
{ commutes' := Œª r x, mul_comm _ _,
  smul_def' := Œª r x, ext (algebra.smul_def _ _) $
    show r ‚Ä¢ x.2 = algebra_map S R r ‚Ä¢ x.2 + x.1 ‚Ä¢ 0, by rw [smul_zero, add_zero, algebra_map_smul],
  .. (triv_sq_zero_ext.inl_hom R M).comp (algebra_map S R) }

-- shortcut instance for the common case
instance : algebra R (tsze R M) := triv_sq_zero_ext.algebra' _ _ _

lemma algebra_map_eq_inl : ‚áë(algebra_map R (tsze R M)) = inl := rfl
lemma algebra_map_eq_inl_hom : algebra_map R (tsze R M) = inl_hom R M := rfl
lemma algebra_map_eq_inl' (s : S) : algebra_map S (tsze R M) s = inl (algebra_map S R s) := rfl

/-- The canonical `R`-algebra projection `triv_sq_zero_ext R M ‚Üí R`. -/
@[simps]
def fst_hom : tsze R M ‚Üí‚Çê[R] R :=
{ to_fun := fst,
  map_one' := fst_one,
  map_mul' := fst_mul,
  map_zero' := fst_zero,
  map_add' := fst_add,
  commutes' := fst_inl M }

variables {R S M}

lemma alg_hom_ext {A} [semiring A] [algebra R A] ‚¶Éf g : tsze R M ‚Üí‚Çê[R] A‚¶Ñ
  (h : ‚àÄ m, f (inr m) = g (inr m)) :
  f = g :=
alg_hom.to_linear_map_injective $ linear_map_ext (Œª r, (f.commutes _).trans (g.commutes _).symm) h

@[ext]
lemma alg_hom_ext' {A} [comm_semiring R] [add_comm_monoid M] [semiring A] [module R M]
  [algebra R A] ‚¶Éf g : tsze R M ‚Üí‚Çê[R] A‚¶Ñ
  (h : f.to_linear_map.comp (inr_hom R M) = g.to_linear_map.comp (inr_hom R M)) :
  f = g :=
alg_hom_ext $ linear_map.congr_fun h

variables {A : Type*} [semiring A] [algebra R A]

/-- There is an alg_hom from the trivial square zero extension to any `R`-algebra with an subalgebra
that squares to zero.

See `triv_sq_zero_ext.lift` for this as an equiv. -/
def lift_aux (f : M ‚Üí‚Çó[R] A) (hf : ‚àÄ x y, f x * f y = 0) : tsze R M ‚Üí‚Çê[R] A :=
alg_hom.of_linear_map
  ((algebra.linear_map _ _).comp (fst_hom R M).to_linear_map + f.comp (snd_hom R M))
  (show algebra_map R _ 1 + f (0 : M) = 1, by rw [map_zero, map_one, add_zero])
  (triv_sq_zero_ext.ind $ Œª r‚ÇÅ m‚ÇÅ, triv_sq_zero_ext.ind $ Œª r‚ÇÇ m‚ÇÇ, begin
    dsimp,
    simp only [add_zero, zero_add, add_mul, mul_add, smul_mul_smul, hf, smul_zero],
    rw [‚Üêring_hom.map_mul, linear_map.map_add, ‚Üêalgebra.commutes _ (f _), ‚Üêalgebra.smul_def,
        ‚Üêalgebra.smul_def, add_right_comm, add_assoc, linear_map.map_smul, linear_map.map_smul],
  end)

@[simp] lemma lift_aux_apply_inr (f : M ‚Üí‚Çó[R] A) (hf : ‚àÄ x y, f x * f y = 0) (m : M) :
  lift_aux f hf (inr m) = f m :=
show algebra_map R A 0 + f m = f m, by rw [ring_hom.map_zero, zero_add]

@[simp] lemma lift_aux_comp_inr_hom (f : M ‚Üí‚Çó[R] A) (hf : ‚àÄ x y, f x * f y = 0) :
  (lift_aux f hf).to_linear_map.comp (inr_hom R M) = f :=
linear_map.ext $ lift_aux_apply_inr f hf

/- When applied to `inr` itself, `lift_aux` is the identity. -/
@[simp]
lemma lift_aux_inr_hom : lift_aux (inr_hom R M) (inr_mul_inr R) = alg_hom.id R (tsze R M) :=
alg_hom_ext' $ lift_aux_comp_inr_hom _ _

/-- A universal property of the dual numbers, providing a unique `ùîª[R] ‚Üí‚Çê[R] A` for every element
of `A` which squares to `-1`.

This isomorphism is named to match the very similar `complex.lift`. -/
@[simps]
def lift : {f : M ‚Üí‚Çó[R] A // ‚àÄ x y, f x * f y = 0} ‚âÉ (tsze R M ‚Üí‚Çê[R] A) :=
{ to_fun := Œª f, lift_aux f f.prop,
  inv_fun := Œª F, ‚ü®F.to_linear_map.comp (inr_hom R M), Œª x y,
    (F.map_mul _ _).symm.trans $ (F.congr_arg $ inr_mul_inr _ _ _).trans F.map_zero‚ü©,
  left_inv := Œª f, subtype.ext $ lift_aux_comp_inr_hom _ _,
  right_inv := Œª F, alg_hom_ext' $ lift_aux_comp_inr_hom _ _, }

end algebra

end triv_sq_zero_ext
