/-
Copyright (c) 2021 David Kurniadi Angdinata. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Kurniadi Angdinata
-/

import data.zmod.quotient
import number_theory.class_number.number_field

import algebraic_geometry.EllipticCurve.fractional_ideal
import algebraic_geometry.EllipticCurve.group

/-!
# Kummer theory lemmas
-/

noncomputable theory
open_locale big_operators classical non_zero_divisors number_field

open is_dedekind_domain

universe u

variables {K : Type u} [field K]

----------------------------------------------------------------------------------------------------
/-! # Lemmas -/

private lemma is_localization.mk'_num_ne_zero_of_ne_zero {R : Type u} [comm_ring R] [algebra R K]
  {S : submonoid R} [is_localization S K] {z : K} {x : R} {y : S}
  (hxyz : z = is_localization.mk' K x y) (hz : z â‰  0) : x â‰  0 :=
by { rintro rfl, rw [is_localization.mk'_zero] at hxyz, exact hz hxyz }

private lemma is_integrally_closed.exists_algebra_map_eq_of_pow_mem {R : Type*} [comm_ring R]
  [algebra R K] {S : subalgebra R K} [is_integrally_closed S] [is_fraction_ring S K] {x : K} {n : â„•}
  (hn : 0 < n) (hx : x ^ n âˆˆ S) : âˆƒ y : S, algebra_map S K y = x :=
is_integrally_closed.is_integral_iff.mp âŸ¨polynomial.X ^ n - polynomial.C (âŸ¨x ^ n, hxâŸ© : S),
  âŸ¨polynomial.monic_X_pow_sub_C âŸ¨x ^ n, hxâŸ© $ ne_zero_of_lt hn,
   by simpa only [polynomial.evalâ‚‚_sub, polynomial.evalâ‚‚_X_pow, polynomial.evalâ‚‚_C, sub_eq_zero]âŸ©âŸ©

@[simp] private lemma fractional_ideal.pow_eq_one_of_pow_mul_eq_one {R : Type u} [comm_ring R]
  [is_domain R] [is_dedekind_domain R] [algebra R K] [is_fraction_ring R K] {I : ideal R} {z : â„¤}
  {n : â„•} (hn : 0 < n) (hI : (I : fractional_ideal Râ° K) ^ (z * n) = 1) :
  (I : fractional_ideal Râ° K) ^ z = 1 :=
begin
  rcases nat.exists_eq_succ_of_ne_zero (ne_zero_of_lt hn) with âŸ¨m, rflâŸ©,
  induction z using int.induction_on with w _ w _,
  { rwa [â† zero_mul] },
  all_goals { rw [zpow_mulâ‚€'] at hI },
  any_goals { rw [â† neg_add', zpow_negâ‚€, inv_eq_oneâ‚€] at hI âŠ¢ },
  all_goals { rw [zpow_coe_nat, â† fractional_ideal.coe_ideal_pow] at hI,
              rw [int.coe_nat_add_one_out, zpow_coe_nat, â† fractional_ideal.coe_ideal_pow,
                  fractional_ideal.coe_ideal_eq_one_iff, ideal.one_eq_top, ideal.eq_top_iff_one]
                at hI âŠ¢, rw [pow_succ I, mul_pow] at hI, exact ideal.mul_le_right hI }
end

private def fractional_ideal.units_of_factorization {R : Type u} [comm_ring R] [is_domain R]
  [is_dedekind_domain R] [algebra R K] [is_fraction_ring R K] (f : height_one_spectrum R â†’ â„¤) :
  (fractional_ideal Râ° K)Ë£ :=
units.mk0 (âˆá¶  p : height_one_spectrum R, â†‘p.as_ideal ^ f p)
begin
  rw [finprod_def],
  split_ifs,
  { exact finset.prod_ne_zero_iff.mpr
      (Î» p _, zpow_ne_zero _ $ fractional_ideal.coe_ideal_ne_zero p.ne_bot) },
  { exact one_ne_zero }
end

----------------------------------------------------------------------------------------------------

namespace number_field

----------------------------------------------------------------------------------------------------
/-! ## Primes and valuations -/

section valuation

variables [number_field K] {n : â„•}

/-- The multiplicative valuation of a non-zero element. -/
def val_of_ne_zero (p : height_one_spectrum $ ð“ž K) : KË£ â†’* multiplicative â„¤ :=
group.with_zero_units.to_monoid_hom.comp $ units.map $
  @height_one_spectrum.valuation _ _ _ _ K _ _ _ p

lemma associates.eq_val_of_ne_zero (p : height_one_spectrum $ ð“ž K) (x : KË£) :
  ((associates.mk p.as_ideal).count $ associates.factors $ associates.mk $ ideal.span
    {(is_localization.mk'_surjective (ð“ž K)â° (x : K)).some} : â„¤)
    - ((associates.mk p.as_ideal).count $ associates.factors $ associates.mk $ ideal.span
        {((is_localization.mk'_surjective (ð“ž K)â° (x : K)).some_spec.some : ð“ž K)} : â„¤)
  = -(val_of_ne_zero p x).to_add :=
begin
  change _ = -classical.some _,
  rw [â† neg_neg_sub_neg, neg_inj, â† with_zero.coe_inj,
      (with_zero.ne_zero_iff_exists.mp (_ : (with_zero $ multiplicative â„¤)Ë£).ne_zero).some_spec],
  change _ = ite _ _ _ / ite _ _ _,
  simpa only [if_neg (is_localization.mk'_num_ne_zero_of_ne_zero
                      (is_localization.mk'_surjective _ x.val).some_spec.some_spec.symm x.ne_zero),
              if_neg (non_zero_divisors.coe_ne_zero (_ : (ð“ž K)â°))]
end

lemma fractional_ideal.factorization_of_ne_zero (x : KË£) :
  âˆá¶  p : height_one_spectrum $ ð“ž K,
    (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ -(val_of_ne_zero p x).to_add
      = fractional_ideal.span_singleton (ð“ž K)â° x :=
begin
  simp_rw [â† Î» p : height_one_spectrum $ ð“ž K, associates.eq_val_of_ne_zero p x],
  exact fractional_ideal.factorization_principal (fractional_ideal.span_singleton (ð“ž K)â° x)
    (fractional_ideal.span_singleton_ne_zero_iff.mpr x.ne_zero) x rfl
end

lemma val_of_ne_zero_support_finite (x : KË£) :
  (function.mul_support $ Î» p : height_one_spectrum $ ð“ž K,
    (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ -(val_of_ne_zero p x).to_add).finite :=
begin
  simp_rw [â† associates.eq_val_of_ne_zero, Î» p : height_one_spectrum $ ð“ž K, zpow_subâ‚€
             (fractional_ideal.coe_ideal_ne_zero p.ne_bot : _ â‰  (0 : fractional_ideal (ð“ž K)â° K))],
  apply set.finite.subset (set.finite.union _ _) (function.mul_support_div _ _),
  all_goals { apply ideal.finite_mul_support_coe
                ((not_iff_not.mpr ideal.span_singleton_eq_bot).mpr _) },
  { exact is_localization.mk'_num_ne_zero_of_ne_zero
      (is_localization.mk'_surjective (ð“ž K)â° x.val).some_spec.some_spec.symm x.ne_zero },
  { exact non_zero_divisors.coe_ne_zero _ }
end

/-- The multiplicative valuation of a non-zero element modulo `n`-th powers. -/
def val_of_ne_zero_mod (p : height_one_spectrum $ ð“ž K) : KË£ â§¸ (nâ¬KË£) â†’* multiplicative (zmod n) :=
(int.quotient_zmultiples_nat_equiv_zmod n).to_multiplicative.to_monoid_hom.comp $
  quotient_group.map (nâ¬KË£) (add_subgroup.zmultiples (n : â„¤)).to_subgroup (val_of_ne_zero p) $
begin
  rintro _ âŸ¨y, rflâŸ©,
  exact âŸ¨val_of_ne_zero p y, by simpa only [pow_monoid_hom_apply, map_pow, int.to_add_pow]âŸ©
end

end valuation

----------------------------------------------------------------------------------------------------
/-! ## Unit group lemmas -/

section unit

/-- The canonical inclusion `ð“žË£ â†’* KË£`. -/
def ne_zero_of_unit : (ð“ž K)Ë£ â†’* KË£ :=
{ to_fun   := Î» âŸ¨âŸ¨v, _âŸ©, âŸ¨i, _âŸ©, vi, ivâŸ©, âŸ¨v, i, by injection vi, by injection ivâŸ©,
  map_one' := rfl,
  map_mul' := Î» âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, _, _âŸ© âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, _, _âŸ©, rfl }

variables [number_field K] {n : â„•}

@[simp] lemma val_of_unit (p : height_one_spectrum $ ð“ž K) (x : (ð“ž K)Ë£) :
  val_of_ne_zero p (ne_zero_of_unit x) = 1 :=
begin
  rcases x with âŸ¨âŸ¨v, hvâŸ©, âŸ¨i, hiâŸ©, vi, _âŸ©,
  rw [val_of_ne_zero, monoid_hom.comp_apply, mul_equiv.coe_to_monoid_hom, mul_equiv.map_eq_one_iff,
      units.map, monoid_hom.mk'_apply, units.ext_iff, eq_iff_le_not_lt],
  split,
  { exact p.valuation_le_one âŸ¨v, hvâŸ© },
  { injection vi with hvi,
    apply_fun p.valuation at hvi,
    rw [map_one, map_mul] at hvi,
    change Â¬p.valuation v < 1,
    rw [not_lt, â† hvi],
    nth_rewrite_rhs 0 [â† mul_one $ p.valuation v],
    exact @with_zero.mul_le_mul_left _ _ _
      âŸ¨group.covariant_iff_contravariant.mpr contravariant_class.elimâŸ© _ _
      (p.valuation_le_one âŸ¨i, hiâŸ©) _ }
end

@[simp] lemma val_of_unit_mod (p : height_one_spectrum $ ð“ž K) (x : (ð“ž K)Ë£) :
  val_of_ne_zero_mod p (ne_zero_of_unit x) = (0 : zmod n) :=
by simpa only [val_of_ne_zero_mod, monoid_hom.comp_apply, quotient_group.map_coe, val_of_unit]

-- Input : finite generation of unit group or Dirichlet's unit theorem
/-- `ð“žË£` is finitely generated. -/
instance : group.fg (ð“ž K)Ë£ := sorry

/-- `ð“žË£/(ð“žË£)â¿` is finite. -/
instance [fact $ 0 < n] : fintype $ (ð“ž K)Ë£ â§¸ (nâ¬(ð“ž K)Ë£) :=
@quotient_group.fintype_of_fg _ _ (by apply @units.group.fg K _) n _

end unit

----------------------------------------------------------------------------------------------------
/-! ## The subgroup `K(S, n)` -/

section K_S_n

variables [number_field K] {S S' : finset $ height_one_spectrum $ ð“ž K} {n : â„•}

/-- The subgroup `K(S, n) = {b(KË£)â¿ âˆˆ KË£/(KË£)â¿ | âˆ€ p âˆ‰ S, ord_p(b) â‰¡ 0 mod n}`. -/
def K_S_n : subgroup (KË£ â§¸ (nâ¬KË£)) :=
{ carrier  := {b : KË£ â§¸ (nâ¬KË£) | âˆ€ p âˆ‰ S, val_of_ne_zero_mod p b = 1},
  one_mem' := Î» _ _, by rw [map_one],
  mul_mem' := Î» _ _ hx hy p hp, by rw [map_mul, hx p hp, hy p hp, one_mul],
  inv_mem' := Î» _ hx p hp, by rw [map_inv, hx p hp, one_inv] }

notation KâŸ®S, nâŸ¯ := @K_S_n K _ _ S n

lemma K_S_n.monotone (hS : S' â‰¤ S) : KâŸ®S', nâŸ¯ â‰¤ KâŸ®S, nâŸ¯ := Î» _ hb p hp, hb p $ mt (@hS p) hp

/-- The multiplicative valuation on K_S_n. -/
def K_S_n.val : KâŸ®S, nâŸ¯ â†’* S â†’ multiplicative (zmod n) :=
{ to_fun   := Î» b p, val_of_ne_zero_mod (p : height_one_spectrum $ ð“ž K) (b : KË£ â§¸ (nâ¬KË£)),
  map_one' := funext $ Î» p, map_one $ val_of_ne_zero_mod p,
  map_mul' := Î» x y, funext $ Î» p, map_mul (val_of_ne_zero_mod p) x y }

lemma K_S_n.val_ker : K_S_n.val.ker = KâŸ®âˆ…, nâŸ¯.subgroup_of KâŸ®S, nâŸ¯ :=
begin
  ext âŸ¨_, hxâŸ©,
  split,
  { intros hx' p _,
    by_cases hp : p âˆˆ S,
    { exact congr_fun hx' âŸ¨p, hpâŸ© },
    { exact hx p hp } },
  { exact Î» hx', funext $ Î» p, hx' p $ finset.not_mem_empty p }
end

/-- A group homomorphism `ð“žË£ â†’ K(âˆ…, n)`. -/
def K_0_n.from_unit : (ð“ž K)Ë£ â†’* KâŸ®âˆ…, nâŸ¯ :=
{ to_fun   := Î» x, âŸ¨quotient_group.mk $ ne_zero_of_unit x, Î» p _, val_of_unit_mod p xâŸ©,
  map_one' := rfl,
  map_mul' := Î» âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, _, _âŸ© âŸ¨âŸ¨_, _âŸ©, âŸ¨_, _âŸ©, _, _âŸ©, rfl }

lemma K_0_n.from_unit_ker [fact $ 0 < n] : (@K_0_n.from_unit K _ _ n).ker = (nâ¬(ð“ž K)Ë£) :=
begin
  ext âŸ¨âŸ¨v, hvâŸ©, âŸ¨i, hiâŸ©, _, _âŸ©,
  split,
  { intro hx,
    cases (quotient_group.eq_one_iff _).mp (subtype.mk_eq_mk.mp hx) with x hx,
    have hv : ((x ^ (n : â„¤) : KË£) : K) = v := congr_arg units.val hx,
    have hi : (((x ^ (n : â„¤))â»Â¹ : KË£) : K) = i := congr_arg units.inv hx,
    substs hv hi,
    rw [â† inv_zpow] at hi,
    rw [zpow_coe_nat, units.coe_pow] at hv hi,
    cases is_integrally_closed.exists_algebra_map_eq_of_pow_mem _inst_3.elim hv with v' hv',
    cases is_integrally_closed.exists_algebra_map_eq_of_pow_mem _inst_3.elim hi with i' hi',
    existsi [(âŸ¨v', i', _, _âŸ© : (ð“ž K)Ë£)],
    { rw [units.ext_iff, subtype.ext_iff, pow_monoid_hom_apply, units.coe_pow, subalgebra.coe_pow],
      simp_rw [units.coe_zpowâ‚€, zpow_coe_nat],
      exact congr_arg (flip (^) n) hv' },
    all_goals { apply_fun (algebra_map (ð“ž K) K),
                any_goals { exact Î» âŸ¨_, _âŸ© âŸ¨_, _âŸ©, subtype.mk_eq_mk.mpr },
                rw [map_mul, hv', hi'] },
    { exact x.val_inv },
    { exact x.inv_val } },
  { rintro âŸ¨âŸ¨âŸ¨v', _âŸ©, âŸ¨i', _âŸ©, vi', iv'âŸ©, hxâŸ©,
    rw [units.ext_iff, subtype.ext_iff, pow_monoid_hom_apply, units.coe_pow, subalgebra.coe_pow]
      at hx,
    exact subtype.mk_eq_mk.mpr ((quotient_group.eq_one_iff _).mpr
      âŸ¨âŸ¨v', i', by injection vi', by injection iv'âŸ©,
       by rwa [units.ext_iff, pow_monoid_hom_apply, units.coe_pow]âŸ©) }
end

lemma K_0_n.val_exists_of_mk (p : height_one_spectrum $ ð“ž K) {x : KË£}
  (hx : quotient_group.mk x âˆˆ KâŸ®âˆ…, nâŸ¯) : âˆƒ z : â„¤, z * n = -(val_of_ne_zero p x).to_add :=
begin
  have hp : val_of_ne_zero_mod p x = 1 := hx p (finset.not_mem_empty p),
  rw [val_of_ne_zero_mod, monoid_hom.comp_apply, mul_equiv.coe_to_monoid_hom,
      mul_equiv.map_eq_one_iff, quotient_group.map_coe, quotient_group.eq_one_iff] at hp,
  cases hp with z hz,
  exact âŸ¨-z, by rwa [neg_mul, neg_inj]âŸ©
end

lemma K_0_n.val_support_finite [fact $ 0 < n] {x : KË£} (hx : quotient_group.mk x âˆˆ KâŸ®âˆ…, nâŸ¯) :
  (function.mul_support $ Î» p : height_one_spectrum $ ð“ž K,
    (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ (K_0_n.val_exists_of_mk p hx).some).finite :=
begin
  apply set.finite.subset (val_of_ne_zero_support_finite x),
  intros p hp,
  change (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ -(val_of_ne_zero p x).to_add â‰  1,
  rw [â† (K_0_n.val_exists_of_mk p hx).some_spec],
  exact not_imp_not.mpr (fractional_ideal.pow_eq_one_of_pow_mul_eq_one _inst_3.elim) hp
end

lemma K_0_n.val_exists (p : height_one_spectrum $ ð“ž K) (x : KâŸ®âˆ…, nâŸ¯) :
  âˆƒ z : â„¤, z * n = -(val_of_ne_zero p x.val.out').to_add :=
K_0_n.val_exists_of_mk p $ by simpa only [quotient_group.out_eq'] using x.property

/-- A set function `K(âˆ…, n) â†’ Cl(K)`. -/
def K_0_n.to_class.to_fun (x : KâŸ®âˆ…, nâŸ¯) : class_group (ð“ž K) K :=
quotient_group.mk' (to_principal_ideal (ð“ž K) K).range $ fractional_ideal.units_of_factorization $
  Î» p, (K_0_n.val_exists p x).some

variables [fact $ 0 < n]

@[simp] lemma K_0_n.to_class_of_mk {x : KË£} (hx : quotient_group.mk x âˆˆ KâŸ®âˆ…, nâŸ¯) :
  K_0_n.to_class.to_fun âŸ¨quotient_group.mk x, hxâŸ©
    = quotient_group.mk' (to_principal_ideal (ð“ž K) K).range
      (fractional_ideal.units_of_factorization $ Î» p, (K_0_n.val_exists_of_mk p hx).some) :=
begin
  rcases quotient_group.mk_out'_eq_mul (nâ¬KË£) x with âŸ¨âŸ¨_, âŸ¨z, hzâŸ©âŸ©, hyâŸ©,
  have val : âˆ€ p : height_one_spectrum $ ð“ž K,
    (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ (K_0_n.val_exists p âŸ¨quotient_group.mk x, hxâŸ©).some
      = p.as_ideal ^ (K_0_n.val_exists_of_mk p hx).some
        * p.as_ideal ^ -(val_of_ne_zero p z).to_add :=
  begin
    intro p,
    rw [â† zpow_addâ‚€
          (fractional_ideal.coe_ideal_ne_zero p.ne_bot : _ â‰  (0 : fractional_ideal (ð“ž K)â° K))],
    congr' 1,
    rw [â† mul_left_inj' $ int.coe_nat_ne_zero_iff_pos.mpr _inst_3.elim,
        (K_0_n.val_exists p âŸ¨_, hxâŸ©).some_spec, subtype.val_eq_coe, subtype.coe_mk, hy, map_mul,
        to_add_mul, add_mul, (K_0_n.val_exists_of_mk p hx).some_spec, neg_mul, â† int.to_add_pow,
        â† neg_add, neg_inj, add_right_inj],
    simp_rw [â† hz],
    exact map_pow (val_of_ne_zero p) z n
  end,
  rw [K_0_n.to_class.to_fun],
  simp_rw [quotient_group.mk'_apply, fractional_ideal.units_of_factorization, val,
           finprod_mul_distrib (K_0_n.val_support_finite hx) (val_of_ne_zero_support_finite z),
           fractional_ideal.factorization_of_ne_zero],
  rw [units.mk0_mul, quotient_group.coe_mul],
  exact mul_right_eq_self.mpr ((quotient_group.eq_one_iff _).mpr
                               âŸ¨z, by simpa only [to_principal_ideal_eq_iff]âŸ©)
end

/-- A group homomorphism `K(âˆ…, n) â†’ Cl(K)`. -/
def K_0_n.to_class : KâŸ®âˆ…, nâŸ¯ â†’* class_group (ð“ž K) K :=
{ to_fun   := K_0_n.to_class.to_fun,
  map_one' :=
  begin
    have val_one : âˆ€ p : height_one_spectrum $ ð“ž K,
      (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ (K_0_n.val_exists_of_mk p KâŸ®âˆ…, nâŸ¯.one_mem).some
        = 1 :=
    begin
      intro p,
      simp_rw [â† zpow_zero (p.as_ideal : fractional_ideal (ð“ž K)â° K)],
      congr' 1,
      rw [â† mul_left_inj' $ int.coe_nat_ne_zero_iff_pos.mpr _inst_3.elim,
          (K_0_n.val_exists_of_mk p KâŸ®âˆ…, nâŸ¯.one_mem).some_spec, map_one, zero_mul],
      refl
    end,
    change K_0_n.to_class.to_fun âŸ¨quotient_group.mk 1, _âŸ© = 1,
    rw [K_0_n.to_class_of_mk, quotient_group.mk'_apply, quotient_group.eq_one_iff,
        fractional_ideal.units_of_factorization],
    exact âŸ¨1, by { rw [to_principal_ideal_eq_iff], simp_rw [val_one, finprod_one],
                   exact fractional_ideal.span_singleton_one }âŸ©
  end,
  map_mul' := Î» âŸ¨x, hxâŸ© âŸ¨y, hyâŸ©,
  begin
    have hx' : quotient_group.mk x.out' âˆˆ KâŸ®âˆ…, nâŸ¯ := by rwa [quotient_group.out_eq'],
    have hy' : quotient_group.mk y.out' âˆˆ KâŸ®âˆ…, nâŸ¯ := by rwa [quotient_group.out_eq'],
    have hxy : quotient_group.mk (x.out' * y.out') âˆˆ KâŸ®âˆ…, nâŸ¯ :=
    by { change quotient_group.mk x.out' * quotient_group.mk y.out' âˆˆ KâŸ®âˆ…, nâŸ¯,
         simpa only [quotient_group.out_eq'] using (âŸ¨x, hxâŸ© * âŸ¨y, hyâŸ© : KâŸ®âˆ…, nâŸ¯).property },
    have x_rw : (âŸ¨x, hxâŸ© : KâŸ®âˆ…, nâŸ¯) = âŸ¨quotient_group.mk x.out', hx'âŸ© :=
    by simp_rw [quotient_group.out_eq'],
    have y_rw : (âŸ¨y, hyâŸ© : KâŸ®âˆ…, nâŸ¯) = âŸ¨quotient_group.mk y.out', hy'âŸ© :=
    by simp_rw [quotient_group.out_eq'],
    have xy_rw : (âŸ¨x, hxâŸ© * âŸ¨y, hyâŸ© : KâŸ®âˆ…, nâŸ¯) = âŸ¨quotient_group.mk (x.out' * y.out'), hxyâŸ© :=
    by { nth_rewrite_lhs 0 [x_rw], nth_rewrite_lhs 0 [y_rw], refl },
    have val_mul : âˆ€ p : height_one_spectrum $ ð“ž K,
      (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ (K_0_n.val_exists_of_mk p hxy).some
        = p.as_ideal ^ (K_0_n.val_exists_of_mk p hx').some
          * p.as_ideal ^ (K_0_n.val_exists_of_mk p hy').some :=
    begin
      intro p,
      rw [â† zpow_addâ‚€
            (fractional_ideal.coe_ideal_ne_zero p.ne_bot : _ â‰  (0 : fractional_ideal (ð“ž K)â° K))],
      congr' 1,
      rw [â† mul_left_inj' $ int.coe_nat_ne_zero_iff_pos.mpr _inst_3.elim,
          (K_0_n.val_exists_of_mk p hxy).some_spec, map_mul, to_add_mul, neg_add, add_mul,
          (K_0_n.val_exists_of_mk p hx').some_spec, (K_0_n.val_exists_of_mk p hy').some_spec],
    end,
    nth_rewrite_rhs 0 [x_rw],
    nth_rewrite_rhs 0 [y_rw],
    rw [xy_rw],
    simp_rw [K_0_n.to_class_of_mk, quotient_group.mk'_apply,
             fractional_ideal.units_of_factorization, val_mul,
             finprod_mul_distrib (K_0_n.val_support_finite hx') (K_0_n.val_support_finite hy')],
    rw [units.mk0_mul, quotient_group.coe_mul]
  end }

lemma K_0_n.to_class_ker : (@K_0_n.to_class K _ _ n _).ker = K_0_n.from_unit.range :=
begin
  ext âŸ¨x, hxâŸ©,
  split,
  { rw [â† quotient_group.out_eq' x] at hx,
    intro hx',
    cases (quotient_group.eq_one_iff _).mp hx' with y hy,
    rw [to_principal_ideal_eq_iff] at hy,
    apply_fun Î» x, x ^ n at hy,
    rw [fractional_ideal.span_singleton_pow, fractional_ideal.units_of_factorization, units.coe_mk0,
        finprod_pow $ K_0_n.val_support_finite hx] at hy,
    simp_rw [â† zpow_coe_nat, â† zpow_mulâ‚€, (K_0_n.val_exists_of_mk _ hx).some_spec] at hy,
    rw [fractional_ideal.factorization_of_ne_zero,
        fractional_ideal.span_singleton_eq_span_singleton] at hy,
    cases hy with y hy,
    existsi [y],
    rcases y with âŸ¨âŸ¨v, hvâŸ©, âŸ¨i, hiâŸ©, vi, ivâŸ©,
    change (âŸ¨quotient_group.mk' (nâ¬KË£) (âŸ¨v, i, _, _âŸ© : KË£), _âŸ© : KâŸ®âˆ…, nâŸ¯) = _,
    simp only,
    rw [â† quotient_group.out_eq' x],
    exact quotient_group.mk'_eq_mk'.mpr
      âŸ¨y ^ (n : â„¤), âŸ¨y, rflâŸ©, by rwa [units.ext_iff, units.coe_mul, units.coe_zpowâ‚€]âŸ© },
  { rintro âŸ¨y, hyâŸ©,
    injection hy with hy,
    have hx' : quotient_group.mk (ne_zero_of_unit y) âˆˆ KâŸ®âˆ…, nâŸ¯ := by rwa [hy],
    have x_rw : (âŸ¨x, hxâŸ© : KâŸ®âˆ…, nâŸ¯) = âŸ¨quotient_group.mk $ ne_zero_of_unit y, hx'âŸ© :=
    by simp_rw [hy],
    have val_unit : âˆ€ p : height_one_spectrum $ ð“ž K,
      (p.as_ideal : fractional_ideal (ð“ž K)â° K) ^ (K_0_n.val_exists_of_mk p hx').some = 1 :=
    begin
      intro p,
      simp_rw [â† zpow_zero (p.as_ideal : fractional_ideal (ð“ž K)â° K)],
      congr' 1,
      rw [â† mul_left_inj' $ int.coe_nat_ne_zero_iff_pos.mpr _inst_3.elim,
          (K_0_n.val_exists_of_mk p hx').some_spec, val_of_unit, zero_mul],
      refl
    end,
    rw [x_rw],
    change K_0_n.to_class.to_fun âŸ¨quotient_group.mk $ ne_zero_of_unit y, hx'âŸ© = 1,
    rw [K_0_n.to_class_of_mk, fractional_ideal.units_of_factorization],
    simp_rw [val_unit, finprod_one],
    rw [units.mk0_one],
    refl }
end

/-- `K(âˆ…, n)` is finite. -/
def K_0_n.fintype : fintype KâŸ®âˆ…, nâŸ¯ :=
@group.fintype_of_ker_of_codom _ _ _ _ (ring_of_integers.class_group.fintype K) _
begin
  rw [K_0_n.to_class_ker],
  apply fintype.of_equiv _ (quotient_group.quotient_ker_equiv_range K_0_n.from_unit).to_equiv,
  rw [K_0_n.from_unit_ker],
  exact has_quotient.quotient.fintype
end

/-- `K(S, n)` is finite. -/
instance : fintype KâŸ®S, nâŸ¯ :=
@group.fintype_of_ker_of_codom _ _ _ _ pi.fintype (@K_S_n.val K _ _ S n) $
by simpa only [K_S_n.val_ker]
   using @fintype.of_equiv _ KâŸ®âˆ…, nâŸ¯ K_0_n.fintype
         (subgroup.comap_subtype_equiv_of_le $ K_S_n.monotone $ finset.empty_subset S).symm.to_equiv

notation KâŸ®S, nâŸ¯`Â²` := (KâŸ®S, nâŸ¯.prod KâŸ®S, nâŸ¯).to_add_subgroup

/-- `K(S, n) Ã— K(S, n)` is finite. -/
instance : fintype KâŸ®S, nâŸ¯Â² := fintype.of_equiv _ (subgroup.prod_equiv _ _).symm.to_equiv

end K_S_n

----------------------------------------------------------------------------------------------------

end number_field
