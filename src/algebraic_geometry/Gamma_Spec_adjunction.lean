/-
Copyright (c) 2021 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import algebraic_geometry.Scheme
import category_theory.adjunction.limits
import category_theory.adjunction.reflective

/-!
# Adjunction between `Î“` and `Spec`

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define the adjunction `Î“_Spec.adjunction : Î“ âŠ£ Spec` by defining the unit (`to_Î“_Spec`,
in multiple steps in this file) and counit (done in Spec.lean) and checking that they satisfy
the left and right triangle identities. The constructions and proofs make use of
maps and lemmas defined and proved in structure_sheaf.lean extensively.

Notice that since the adjunction is between contravariant functors, you get to choose
one of the two categories to have arrows reversed, and it is equally valid to present
the adjunction as `Spec âŠ£ Î“` (`Spec.to_LocallyRingedSpace.right_op âŠ£ Î“`), in which
case the unit and the counit would switch to each other.

## Main definition

* `algebraic_geometry.identity_to_Î“_Spec` : The natural transformation `ğŸ­ _ âŸ¶ Î“ â‹™ Spec`.
* `algebraic_geometry.Î“_Spec.LocallyRingedSpace_adjunction` : The adjunction `Î“ âŠ£ Spec` from
  `CommRingáµ’áµ–` to `LocallyRingedSpace`.
* `algebraic_geometry.Î“_Spec.adjunction` : The adjunction `Î“ âŠ£ Spec` from
  `CommRingáµ’áµ–` to `Scheme`.

-/

noncomputable theory
universes u

open prime_spectrum

namespace algebraic_geometry
open opposite
open category_theory
open structure_sheaf Spec (structure_sheaf)
open topological_space
open algebraic_geometry.LocallyRingedSpace
open Top.presheaf
open Top.presheaf.sheaf_condition

namespace LocallyRingedSpace

variable (X : LocallyRingedSpace.{u})

/-- The map from the global sections to a stalk. -/
def Î“_to_stalk (x : X) : Î“.obj (op X) âŸ¶ X.presheaf.stalk x :=
X.presheaf.germ (âŸ¨x,trivialâŸ© : (âŠ¤ : opens X))

/-- The canonical map from the underlying set to the prime spectrum of `Î“(X)`. -/
def to_Î“_Spec_fun : X â†’ prime_spectrum (Î“.obj (op X)) :=
Î» x, comap (X.Î“_to_stalk x) (local_ring.closed_point (X.presheaf.stalk x))

lemma not_mem_prime_iff_unit_in_stalk (r : Î“.obj (op X)) (x : X) :
  r âˆ‰ (X.to_Î“_Spec_fun x).as_ideal â†” is_unit (X.Î“_to_stalk x r) :=
by erw [local_ring.mem_maximal_ideal, not_not]

/-- The preimage of a basic open in `Spec Î“(X)` under the unit is the basic
open in `X` defined by the same element (they are equal as sets). -/
lemma to_Î“_Spec_preim_basic_open_eq (r : Î“.obj (op X)) :
  X.to_Î“_Spec_funâ»Â¹' (basic_open r).1 = (X.to_RingedSpace.basic_open r).1 :=
by { ext, erw X.to_RingedSpace.mem_top_basic_open, apply not_mem_prime_iff_unit_in_stalk }

/-- `to_Î“_Spec_fun` is continuous. -/
lemma to_Î“_Spec_continuous : continuous X.to_Î“_Spec_fun :=
begin
  apply is_topological_basis_basic_opens.continuous,
  rintro _ âŸ¨r, rflâŸ©,
  erw X.to_Î“_Spec_preim_basic_open_eq r,
  exact (X.to_RingedSpace.basic_open r).2,
end

/-- The canonical (bundled) continuous map from the underlying topological
space of `X` to the prime spectrum of its global sections. -/
@[simps]
def to_Î“_Spec_base : X.to_Top âŸ¶ Spec.Top_obj (Î“.obj (op X)) :=
{ to_fun := X.to_Î“_Spec_fun,
  continuous_to_fun := X.to_Î“_Spec_continuous }

variable (r : Î“.obj (op X))

/-- The preimage in `X` of a basic open in `Spec Î“(X)` (as an open set). -/
abbreviation to_Î“_Spec_map_basic_open : opens X :=
(opens.map X.to_Î“_Spec_base).obj (basic_open r)

/-- The preimage is the basic open in `X` defined by the same element `r`. -/
lemma to_Î“_Spec_map_basic_open_eq : X.to_Î“_Spec_map_basic_open r = X.to_RingedSpace.basic_open r :=
opens.ext (X.to_Î“_Spec_preim_basic_open_eq r)

/-- The map from the global sections `Î“(X)` to the sections on the (preimage of) a basic open. -/
abbreviation to_to_Î“_Spec_map_basic_open :
  X.presheaf.obj (op âŠ¤) âŸ¶ X.presheaf.obj (op $ X.to_Î“_Spec_map_basic_open r) :=
X.presheaf.map (X.to_Î“_Spec_map_basic_open r).le_top.op

/-- `r` is a unit as a section on the basic open defined by `r`. -/
lemma is_unit_res_to_Î“_Spec_map_basic_open :
  is_unit (X.to_to_Î“_Spec_map_basic_open r r) :=
begin
  convert (X.presheaf.map $ (eq_to_hom $ X.to_Î“_Spec_map_basic_open_eq r).op)
    .is_unit_map (X.to_RingedSpace.is_unit_res_basic_open r),
  rw â† comp_apply,
  erw â† functor.map_comp,
  congr
end

/-- Define the sheaf hom on individual basic opens for the unit. -/
def to_Î“_Spec_c_app :
  (structure_sheaf $ Î“.obj $ op X).val.obj (op $ basic_open r) âŸ¶
    X.presheaf.obj (op $ X.to_Î“_Spec_map_basic_open r) :=
is_localization.away.lift r (is_unit_res_to_Î“_Spec_map_basic_open _ r)

/-- Characterization of the sheaf hom on basic opens,
    direction â† (next lemma) is used at various places, but â†’ is not used in this file. -/
lemma to_Î“_Spec_c_app_iff
  (f : (structure_sheaf $ Î“.obj $ op X).val.obj (op $ basic_open r) âŸ¶
    X.presheaf.obj (op $ X.to_Î“_Spec_map_basic_open r)) :
  to_open _ (basic_open r) â‰« f = X.to_to_Î“_Spec_map_basic_open r â†” f = X.to_Î“_Spec_c_app r :=
begin
  rw â† (is_localization.away.away_map.lift_comp r
    (X.is_unit_res_to_Î“_Spec_map_basic_open r)),
  swap 5, exact is_localization.to_basic_open _ r,
  split,
  { intro h, refine is_localization.ring_hom_ext _ _,
    swap 5, exact is_localization.to_basic_open _ r, exact h },
  apply congr_arg,
end

lemma to_Î“_Spec_c_app_spec :
  to_open _ (basic_open r) â‰« X.to_Î“_Spec_c_app r = X.to_to_Î“_Spec_map_basic_open r :=
(X.to_Î“_Spec_c_app_iff r _).2 rfl

/-- The sheaf hom on all basic opens, commuting with restrictions. -/
def to_Î“_Spec_c_basic_opens :
  (induced_functor basic_open).op â‹™ (structure_sheaf (Î“.obj (op X))).1 âŸ¶
  (induced_functor basic_open).op â‹™ ((Top.sheaf.pushforward X.to_Î“_Spec_base).obj X.ğ’ª).1 :=
{ app := Î» r, X.to_Î“_Spec_c_app r.unop,
  naturality' := Î» r s f, begin
    apply (structure_sheaf.to_basic_open_epi (Î“.obj (op X)) r.unop).1,
    simp only [â† category.assoc],
    erw X.to_Î“_Spec_c_app_spec r.unop,
    convert X.to_Î“_Spec_c_app_spec s.unop,
    symmetry,
    apply X.presheaf.map_comp
  end }

/-- The canonical morphism of sheafed spaces from `X` to the spectrum of its global sections. -/
@[simps]
def to_Î“_Spec_SheafedSpace : X.to_SheafedSpace âŸ¶ Spec.to_SheafedSpace.obj (op (Î“.obj (op X))) :=
{ base := X.to_Î“_Spec_base,
  c := Top.sheaf.restrict_hom_equiv_hom (structure_sheaf (Î“.obj (op X))).1 _
    is_basis_basic_opens X.to_Î“_Spec_c_basic_opens }

lemma to_Î“_Spec_SheafedSpace_app_eq :
  X.to_Î“_Spec_SheafedSpace.c.app (op (basic_open r)) = X.to_Î“_Spec_c_app r :=
Top.sheaf.extend_hom_app _ _ _ _ _

lemma to_Î“_Spec_SheafedSpace_app_spec (r : Î“.obj (op X)) :
  to_open _ (basic_open r) â‰« X.to_Î“_Spec_SheafedSpace.c.app (op (basic_open r)) =
    X.to_to_Î“_Spec_map_basic_open r :=
(X.to_Î“_Spec_SheafedSpace_app_eq r).symm â–¸ X.to_Î“_Spec_c_app_spec r

/-- The map on stalks induced by the unit commutes with maps from `Î“(X)` to
    stalks (in `Spec Î“(X)` and in `X`). -/
lemma to_stalk_stalk_map_to_Î“_Spec (x : X) : to_stalk _ _ â‰«
  PresheafedSpace.stalk_map X.to_Î“_Spec_SheafedSpace x = X.Î“_to_stalk x :=
begin
  rw PresheafedSpace.stalk_map,
  erw â† to_open_germ _ (basic_open (1 : Î“.obj (op X)))
    âŸ¨X.to_Î“_Spec_fun x, by rw basic_open_one; trivialâŸ©,
  rw [â† category.assoc, category.assoc (to_open _ _)],
  erw stalk_functor_map_germ,
  rw [â† category.assoc (to_open _ _), X.to_Î“_Spec_SheafedSpace_app_spec 1],
  unfold Î“_to_stalk,
  rw â† stalk_pushforward_germ _ X.to_Î“_Spec_base X.presheaf âŠ¤,
  congr' 1,
  change (X.to_Î“_Spec_base _* X.presheaf).map le_top.hom.op â‰« _ = _,
  apply germ_res,
end

/-- The canonical morphism from `X` to the spectrum of its global sections. -/
@[simps val_base]
def to_Î“_Spec : X âŸ¶ Spec.LocallyRingedSpace_obj (Î“.obj (op X)) :=
{ val := X.to_Î“_Spec_SheafedSpace,
  prop :=
  begin
    intro x,
    let p : prime_spectrum (Î“.obj (op X)) := X.to_Î“_Spec_fun x,
    constructor, /- show stalk map is local hom â†“ -/
    let S := (structure_sheaf _).presheaf.stalk p,
    rintros (t : S) ht,
    obtain âŸ¨âŸ¨r, sâŸ©, heâŸ© := is_localization.surj p.as_ideal.prime_compl t,
    dsimp at he,
    apply is_unit_of_mul_is_unit_left,
    rw he,
    refine is_localization.map_units S (âŸ¨r, _âŸ© : p.as_ideal.prime_compl),
    apply (not_mem_prime_iff_unit_in_stalk _ _ _).mpr,
    rw [â† to_stalk_stalk_map_to_Î“_Spec, comp_apply],
    erw â† he,
    rw ring_hom.map_mul,
    exact ht.mul ((is_localization.map_units S s : _).map
      (PresheafedSpace.stalk_map X.to_Î“_Spec_SheafedSpace x))
  end }

lemma comp_ring_hom_ext {X : LocallyRingedSpace} {R : CommRing}
  {f : R âŸ¶ Î“.obj (op X)} {Î² : X âŸ¶ Spec.LocallyRingedSpace_obj R}
  (w : X.to_Î“_Spec.1.base â‰« (Spec.LocallyRingedSpace_map f).1.base = Î².1.base)
  (h : âˆ€ r : R,
    f â‰« X.presheaf.map (hom_of_le le_top : (opens.map Î².1.base).obj (basic_open r) âŸ¶ _).op =
      to_open R (basic_open r) â‰« Î².1.c.app (op (basic_open r))) :
  X.to_Î“_Spec â‰« Spec.LocallyRingedSpace_map f = Î² :=
begin
  ext1,
  apply Spec.basic_open_hom_ext,
  { intros r _,
    rw LocallyRingedSpace.comp_val_c_app,
    erw to_open_comp_comap_assoc,
    rw category.assoc,
    erw [to_Î“_Spec_SheafedSpace_app_spec, â† X.presheaf.map_comp],
    convert h r },
  exact w,
end

/-- `to_Spec_Î“ _` is an isomorphism so these are mutually two-sided inverses. -/
lemma Î“_Spec_left_triangle : to_Spec_Î“ (Î“.obj (op X)) â‰« X.to_Î“_Spec.1.c.app (op âŠ¤) = ğŸ™ _ :=
begin
  unfold to_Spec_Î“,
  rw â† to_open_res _ (basic_open (1 : Î“.obj (op X))) âŠ¤ (eq_to_hom basic_open_one.symm),
  erw category.assoc,
  rw [nat_trans.naturality, â† category.assoc],
  erw [X.to_Î“_Spec_SheafedSpace_app_spec 1, â† functor.map_comp],
  convert eq_to_hom_map X.presheaf _, refl,
end

end LocallyRingedSpace

/-- The unit as a natural transformation. -/
def identity_to_Î“_Spec : ğŸ­ LocallyRingedSpace.{u} âŸ¶ Î“.right_op â‹™ Spec.to_LocallyRingedSpace :=
{ app := LocallyRingedSpace.to_Î“_Spec,
  naturality' := Î» X Y f, begin
    symmetry,
    apply LocallyRingedSpace.comp_ring_hom_ext,
    { ext1 x,
      dsimp [Spec.Top_map, LocallyRingedSpace.to_Î“_Spec_fun],
      rw [â† local_ring.comap_closed_point (PresheafedSpace.stalk_map _ x),
        â† prime_spectrum.comap_comp_apply, â† prime_spectrum.comap_comp_apply],
      congr' 2,
      exact (PresheafedSpace.stalk_map_germ f.1 âŠ¤ âŸ¨x,trivialâŸ©).symm,
      apply_instance },
    { intro r,
      rw [LocallyRingedSpace.comp_val_c_app, â† category.assoc],
      erw [Y.to_Î“_Spec_SheafedSpace_app_spec, f.1.c.naturality],
      refl },
  end }

namespace Î“_Spec

lemma left_triangle (X : LocallyRingedSpace) :
  Spec_Î“_identity.inv.app (Î“.obj (op X)) â‰« (identity_to_Î“_Spec.app X).val.c.app (op âŠ¤) = ğŸ™ _ :=
X.Î“_Spec_left_triangle


/-- `Spec_Î“_identity` is iso so these are mutually two-sided inverses. -/
lemma right_triangle (R : CommRing) :
  identity_to_Î“_Spec.app (Spec.to_LocallyRingedSpace.obj $ op R) â‰«
  Spec.to_LocallyRingedSpace.map (Spec_Î“_identity.inv.app R).op = ğŸ™ _ :=
begin
  apply LocallyRingedSpace.comp_ring_hom_ext,
  { ext (p : prime_spectrum R) x,
    erw â† is_localization.at_prime.to_map_mem_maximal_iff
      ((structure_sheaf R).presheaf.stalk p) p.as_ideal x,
    refl },
  { intro r, apply to_open_res },
end

-- Removing this makes the following definition time out.
local attribute [irreducible] Spec_Î“_identity identity_to_Î“_Spec Spec.to_LocallyRingedSpace

/-- The adjunction `Î“ âŠ£ Spec` from `CommRingáµ’áµ–` to `LocallyRingedSpace`. -/
@[simps unit counit] def LocallyRingedSpace_adjunction : Î“.right_op âŠ£ Spec.to_LocallyRingedSpace :=
adjunction.mk_of_unit_counit
{ unit := identity_to_Î“_Spec,
  counit := (nat_iso.op Spec_Î“_identity).inv,
  left_triangle' := by { ext X, erw category.id_comp,
    exact congr_arg quiver.hom.op (left_triangle X) },
  right_triangle' := by { ext1, ext1 R, erw category.id_comp,
    exact right_triangle R.unop } }

local attribute [semireducible] Spec.to_LocallyRingedSpace

/-- The adjunction `Î“ âŠ£ Spec` from `CommRingáµ’áµ–` to `Scheme`. -/
def adjunction : Scheme.Î“.right_op âŠ£ Scheme.Spec :=
LocallyRingedSpace_adjunction.restrict_fully_faithful
  Scheme.forget_to_LocallyRingedSpace (ğŸ­ _)
  (nat_iso.of_components (Î» X, iso.refl _) (Î» _ _ f, by simpa))
  (nat_iso.of_components (Î» X, iso.refl _) (Î» _ _ f, by simpa))

lemma adjunction_hom_equiv_apply {X : Scheme} {R : CommRingáµ’áµ–}
  (f : (op $ Scheme.Î“.obj $ op X) âŸ¶ R) :
  Î“_Spec.adjunction.hom_equiv X R f =
    LocallyRingedSpace_adjunction.hom_equiv X.1 R f :=
by { dsimp [adjunction, adjunction.restrict_fully_faithful], simp }

local attribute [irreducible] LocallyRingedSpace_adjunction Î“_Spec.adjunction

lemma adjunction_hom_equiv (X : Scheme) (R : CommRingáµ’áµ–) :
  Î“_Spec.adjunction.hom_equiv X R = LocallyRingedSpace_adjunction.hom_equiv X.1 R :=
equiv.ext $ Î» f, adjunction_hom_equiv_apply f

lemma adjunction_hom_equiv_symm_apply {X : Scheme} {R : CommRingáµ’áµ–}
  (f : X âŸ¶ Scheme.Spec.obj R) :
  (Î“_Spec.adjunction.hom_equiv X R).symm f =
    (LocallyRingedSpace_adjunction.hom_equiv X.1 R).symm f :=
by { congr' 2, exact adjunction_hom_equiv _ _ }

@[simp] lemma adjunction_counit_app {R : CommRingáµ’áµ–} :
  Î“_Spec.adjunction.counit.app R = LocallyRingedSpace_adjunction.counit.app R :=
by { rw [â† adjunction.hom_equiv_symm_id, â† adjunction.hom_equiv_symm_id,
  adjunction_hom_equiv_symm_apply], refl }

@[simp] lemma adjunction_unit_app {X : Scheme} :
  Î“_Spec.adjunction.unit.app X = LocallyRingedSpace_adjunction.unit.app X.1 :=
by { rw [â† adjunction.hom_equiv_id, â† adjunction.hom_equiv_id, adjunction_hom_equiv_apply], refl }

local attribute [semireducible] LocallyRingedSpace_adjunction Î“_Spec.adjunction

instance is_iso_LocallyRingedSpace_adjunction_counit :
  is_iso LocallyRingedSpace_adjunction.counit :=
is_iso.of_iso_inv _

instance is_iso_adjunction_counit : is_iso Î“_Spec.adjunction.counit :=
begin
  apply_with nat_iso.is_iso_of_is_iso_app { instances := ff },
  intro R,
  rw adjunction_counit_app,
  apply_instance,
end

-- This is just
-- `(Î“_Spec.adjunction.unit.app X).1.c.app (op âŠ¤) = Spec_Î“_identity.hom.app (X.presheaf.obj (op âŠ¤))`
-- But lean times out when trying to unify the types of the two sides.
lemma adjunction_unit_app_app_top (X : Scheme) :
  @eq ((Scheme.Spec.obj (op $ X.presheaf.obj (op âŠ¤))).presheaf.obj (op âŠ¤) âŸ¶
    ((Î“_Spec.adjunction.unit.app X).1.base _* X.presheaf).obj (op âŠ¤))
  ((Î“_Spec.adjunction.unit.app X).val.c.app (op âŠ¤))
    (Spec_Î“_identity.hom.app (X.presheaf.obj (op âŠ¤))) :=
begin
  have := congr_app Î“_Spec.adjunction.left_triangle X,
  dsimp at this,
  rw â† is_iso.eq_comp_inv at this,
  simp only [Î“_Spec.LocallyRingedSpace_adjunction_counit, nat_trans.op_app, category.id_comp,
    Î“_Spec.adjunction_counit_app] at this,
  rw [â† op_inv, nat_iso.inv_inv_app, quiver.hom.op_inj.eq_iff] at this,
  exact this
end

end Î“_Spec

/-! Immediate consequences of the adjunction. -/

/-- Spec preserves limits. -/
instance : limits.preserves_limits Spec.to_LocallyRingedSpace :=
Î“_Spec.LocallyRingedSpace_adjunction.right_adjoint_preserves_limits
instance Spec.preserves_limits : limits.preserves_limits Scheme.Spec :=
Î“_Spec.adjunction.right_adjoint_preserves_limits

/-- Spec is a full functor. -/
instance : full Spec.to_LocallyRingedSpace :=
R_full_of_counit_is_iso Î“_Spec.LocallyRingedSpace_adjunction
instance Spec.full : full Scheme.Spec :=
R_full_of_counit_is_iso Î“_Spec.adjunction

/-- Spec is a faithful functor. -/
instance : faithful Spec.to_LocallyRingedSpace :=
R_faithful_of_counit_is_iso Î“_Spec.LocallyRingedSpace_adjunction
instance Spec.faithful : faithful Scheme.Spec :=
R_faithful_of_counit_is_iso Î“_Spec.adjunction

instance : is_right_adjoint Spec.to_LocallyRingedSpace := âŸ¨_, Î“_Spec.LocallyRingedSpace_adjunctionâŸ©
instance : is_right_adjoint Scheme.Spec := âŸ¨_, Î“_Spec.adjunctionâŸ©

instance : reflective Spec.to_LocallyRingedSpace := âŸ¨âŸ©
instance Spec.reflective : reflective Scheme.Spec := âŸ¨âŸ©

end algebraic_geometry
