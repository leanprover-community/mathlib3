/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.presheafed_space.gluing

/-!
# Gluing Schemes

Given a family of gluing data of schemes, we may glue them together.

## Main definitions

* `algebraic_geometry.Scheme.glue_data`: A structure containing the family of gluing data.
* `algebraic_geometry.Scheme.glue_data.glued`: The glued scheme.
    This is defined as the multicoequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API
    can be used.
* `algebraic_geometry.Scheme.glue_data.Î¹`: The immersion `Î¹ i : U i âŸ¶ glued` for each `i : J`.
* `algebraic_geometry.Scheme.glue_data.iso_carrier`: The isomorphism between the underlying space
  of the glued scheme and the gluing of the underlying topological spaces.
* `algebraic_geometry.Scheme.open_cover.glue_data`: The glue data associated with an open cover.
* `algebraic_geometry.Scheme.open_cover.from_glue_data`: The canonical morphism
  `ğ’°.glue_data.glued âŸ¶ X`. This has an `is_iso` instance.
* `algebraic_geometry.Scheme.open_cover.glue_morphisms`: We may glue a family of compatible
  morphisms defined on an open cover of a scheme.

## Main results

* `algebraic_geometry.Scheme.glue_data.Î¹_is_open_immersion`: The map `Î¹ i : U i âŸ¶ glued`
  is an open immersion for each `i : J`.
* `algebraic_geometry.Scheme.glue_data.Î¹_jointly_surjective` : The underlying maps of
  `Î¹ i : U i âŸ¶ glued` are jointly surjective.
* `algebraic_geometry.Scheme.glue_data.V_pullback_cone_is_limit` : `V i j` is the pullback
  (intersection) of `U i` and `U j` over the glued space.
* `algebraic_geometry.Scheme.glue_data.Î¹_eq_iff_rel` : `Î¹ i x = Î¹ j y` if and only if they coincide
  when restricted to `V i i`.
* `algebraic_geometry.Scheme.glue_data.is_open_iff` : An subset of the glued scheme is open iff
  all its preimages in `U i` are open.

## Implementation details

All the hard work is done in `algebraic_geometry/presheafed_space/gluing.lean` where we glue
presheafed spaces, sheafed spaces, and locally ringed spaces.

-/

noncomputable theory

universe u

open topological_space category_theory opposite
open category_theory.limits algebraic_geometry.PresheafedSpace
open category_theory.glue_data

namespace algebraic_geometry

namespace Scheme

/--
A family of gluing data consists of
1. An index type `J`
2. An scheme `U i` for each `i : J`.
3. An scheme `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ Scheme` rather than `J â†’ J â†’ Scheme` to connect to the
  limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the schemes `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subschemes of the glued space.
-/
@[nolint has_nonempty_instance]
structure glue_data extends category_theory.glue_data Scheme :=
(f_open : âˆ€ i j, is_open_immersion (f i j))

attribute [instance] glue_data.f_open

namespace glue_data

variables (D : glue_data)

include D

local notation `ğ–£` := D.to_glue_data

/-- The glue data of locally ringed spaces spaces associated to a family of glue data of schemes. -/
abbreviation to_LocallyRingedSpace_glue_data : LocallyRingedSpace.glue_data :=
{ f_open := D.f_open,
  to_glue_data := ğ–£ .map_glue_data forget_to_LocallyRingedSpace }

/-- (Implementation). The glued scheme of a glue data.
This should not be used outside this file. Use `Scheme.glue_data.glued` instead. -/
def glued_Scheme : Scheme :=
begin
  apply LocallyRingedSpace.is_open_immersion.Scheme
    D.to_LocallyRingedSpace_glue_data.to_glue_data.glued,
  intro x,
  obtain âŸ¨i, y, rflâŸ© := D.to_LocallyRingedSpace_glue_data.Î¹_jointly_surjective x,
  refine âŸ¨_, _ â‰« D.to_LocallyRingedSpace_glue_data.to_glue_data.Î¹ i, _âŸ©,
  swap, exact (D.U i).affine_cover.map y,
  split,
  { dsimp [-set.mem_range],
    rw [coe_comp, set.range_comp],
    refine set.mem_image_of_mem _ _,
    exact (D.U i).affine_cover.covers y },
  { apply_instance },
end

instance : creates_colimit ğ–£ .diagram.multispan forget_to_LocallyRingedSpace :=
creates_colimit_of_fully_faithful_of_iso D.glued_Scheme
  (has_colimit.iso_of_nat_iso (ğ–£ .diagram_iso forget_to_LocallyRingedSpace).symm)

instance : preserves_colimit ğ–£ .diagram.multispan forget_to_Top :=
begin
  delta forget_to_Top LocallyRingedSpace.forget_to_Top,
  apply_instance,
end

instance : has_multicoequalizer ğ–£ .diagram :=
has_colimit_of_created _ forget_to_LocallyRingedSpace

/-- The glued scheme of a glued space. -/
abbreviation glued : Scheme := ğ–£ .glued

/-- The immersion from `D.U i` into the glued space. -/
abbreviation Î¹ (i : D.J) : D.U i âŸ¶ D.glued := ğ–£ .Î¹ i

/-- The gluing as sheafed spaces is isomorphic to the gluing as presheafed spaces. -/
abbreviation iso_LocallyRingedSpace :
  D.glued.to_LocallyRingedSpace â‰… D.to_LocallyRingedSpace_glue_data.to_glue_data.glued :=
ğ–£ .glued_iso forget_to_LocallyRingedSpace

lemma Î¹_iso_LocallyRingedSpace_inv (i : D.J) :
  D.to_LocallyRingedSpace_glue_data.to_glue_data.Î¹ i â‰« D.iso_LocallyRingedSpace.inv = ğ–£ .Î¹ i :=
ğ–£ .Î¹_glued_iso_inv forget_to_LocallyRingedSpace i

instance Î¹_is_open_immersion (i : D.J) :
  is_open_immersion (ğ–£ .Î¹ i) :=
by { rw â† D.Î¹_iso_LocallyRingedSpace_inv, apply_instance }

lemma Î¹_jointly_surjective (x : ğ–£ .glued.carrier) :
  âˆƒ (i : D.J) (y : (D.U i).carrier), (D.Î¹ i).1.base y = x :=
ğ–£ .Î¹_jointly_surjective (forget_to_Top â‹™ forget Top) x

@[simp, reassoc]
lemma glue_condition (i j : D.J) :
  D.t i j â‰« D.f j i â‰« D.Î¹ j = D.f i j â‰« D.Î¹ i :=
ğ–£ .glue_condition i j

/-- The pullback cone spanned by `V i j âŸ¶ U i` and `V i j âŸ¶ U j`.
This is a pullback diagram (`V_pullback_cone_is_limit`). -/
def V_pullback_cone (i j : D.J) : pullback_cone (D.Î¹ i) (D.Î¹ j) :=
pullback_cone.mk (D.f i j) (D.t i j â‰« D.f j i) (by simp)

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def V_pullback_cone_is_limit (i j : D.J) :
  is_limit (D.V_pullback_cone i j) :=
ğ–£ .V_pullback_cone_is_limit_of_map forget_to_LocallyRingedSpace i j
  (D.to_LocallyRingedSpace_glue_data.V_pullback_cone_is_limit _ _)

/-- The underlying topological space of the glued scheme is isomorphic to the gluing of the
underlying spacess -/
def iso_carrier :
  D.glued.carrier â‰… D.to_LocallyRingedSpace_glue_data.to_SheafedSpace_glue_data
    .to_PresheafedSpace_glue_data.to_Top_glue_data.to_glue_data.glued :=
begin
  refine (PresheafedSpace.forget _).map_iso _ â‰ªâ‰«
    glue_data.glued_iso _ (PresheafedSpace.forget _),
  refine SheafedSpace.forget_to_PresheafedSpace.map_iso _ â‰ªâ‰«
    SheafedSpace.glue_data.iso_PresheafedSpace _,
  refine LocallyRingedSpace.forget_to_SheafedSpace.map_iso _ â‰ªâ‰«
    LocallyRingedSpace.glue_data.iso_SheafedSpace _,
  exact Scheme.glue_data.iso_LocallyRingedSpace _
end

@[simp]
lemma Î¹_iso_carrier_inv (i : D.J) :
  D.to_LocallyRingedSpace_glue_data.to_SheafedSpace_glue_data
    .to_PresheafedSpace_glue_data.to_Top_glue_data.to_glue_data.Î¹ i â‰« D.iso_carrier.inv =
    (D.Î¹ i).1.base :=
begin
  delta iso_carrier,
  simp only [functor.map_iso_inv, iso.trans_inv, iso.trans_assoc,
    glue_data.Î¹_glued_iso_inv_assoc, functor.map_iso_trans, category.assoc],
  iterate 3 { erw â† comp_base },
  simp_rw â† category.assoc,
  rw D.to_LocallyRingedSpace_glue_data.to_SheafedSpace_glue_data.Î¹_iso_PresheafedSpace_inv i,
  erw D.to_LocallyRingedSpace_glue_data.Î¹_iso_SheafedSpace_inv i,
  change (_ â‰« D.iso_LocallyRingedSpace.inv).1.base = _,
  rw D.Î¹_iso_LocallyRingedSpace_inv i
end

/-- An equivalence relation on `Î£ i, D.U i` that holds iff `ğ–£ .Î¹ i x = ğ–£ .Î¹ j y`.
See `Scheme.gluing_data.Î¹_eq_iff`. -/
def rel (a b : Î£ i, ((D.U i).carrier : Type*)) : Prop :=
  a = b âˆ¨ âˆƒ (x : (D.V (a.1, b.1)).carrier),
    (D.f _ _).1.base x = a.2 âˆ§ (D.t _ _ â‰« D.f _ _).1.base x = b.2

lemma Î¹_eq_iff (i j : D.J) (x : (D.U i).carrier) (y : (D.U j).carrier) :
  (ğ–£ .Î¹ i).1.base x = (ğ–£ .Î¹ j).1.base y â†” D.rel âŸ¨i, xâŸ© âŸ¨j, yâŸ© :=
begin
  refine iff.trans _ (D.to_LocallyRingedSpace_glue_data.to_SheafedSpace_glue_data
      .to_PresheafedSpace_glue_data.to_Top_glue_data.Î¹_eq_iff_rel i j x y),
  rw â† ((Top.mono_iff_injective D.iso_carrier.inv).mp infer_instance).eq_iff,
    simp_rw [â† comp_apply, D.Î¹_iso_carrier_inv]
end

lemma is_open_iff (U : set D.glued.carrier) : is_open U â†” âˆ€ i, is_open ((D.Î¹ i).1.base â»Â¹' U) :=
begin
  rw â† (Top.homeo_of_iso D.iso_carrier.symm).is_open_preimage,
  rw Top.glue_data.is_open_iff,
  apply forall_congr,
  intro i,
  erw [â† set.preimage_comp, â† coe_comp, Î¹_iso_carrier_inv]
end

/-- The open cover of the glued space given by the glue data. -/
def open_cover (D : Scheme.glue_data) : open_cover D.glued :=
{ J := D.J,
  obj := D.U,
  map := D.Î¹,
  f := Î» x, (D.Î¹_jointly_surjective x).some,
  covers := Î» x, âŸ¨_, (D.Î¹_jointly_surjective x).some_spec.some_specâŸ© }

end glue_data

namespace open_cover

variables {X : Scheme.{u}} (ğ’° : open_cover.{u} X)

/-- (Implementation) the transition maps in the glue data associated with an open cover. -/
def glued_cover_t' (x y z : ğ’°.J) :
  pullback (pullback.fst : pullback (ğ’°.map x) (ğ’°.map y) âŸ¶ _)
    (pullback.fst : pullback (ğ’°.map x) (ğ’°.map z) âŸ¶ _) âŸ¶
  pullback (pullback.fst : pullback (ğ’°.map y) (ğ’°.map z) âŸ¶ _)
    (pullback.fst : pullback (ğ’°.map y) (ğ’°.map x) âŸ¶ _) :=
begin
  refine (pullback_right_pullback_fst_iso _ _ _).hom â‰« _,
  refine _ â‰« (pullback_symmetry _ _).hom,
  refine _ â‰« (pullback_right_pullback_fst_iso _ _ _).inv,
  refine pullback.map _ _ _ _ (pullback_symmetry _ _).hom (ğŸ™ _) (ğŸ™ _) _ _,
  { simp [pullback.condition] },
  { simp }
end

@[simp, reassoc]
lemma glued_cover_t'_fst_fst (x y z : ğ’°.J) :
   ğ’°.glued_cover_t' x y z â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd :=
by { delta glued_cover_t', simp }

@[simp, reassoc]
lemma glued_cover_t'_fst_snd (x y z : ğ’°.J) :
  glued_cover_t' ğ’° x y z â‰« pullback.fst â‰« pullback.snd = pullback.snd â‰« pullback.snd :=
by { delta glued_cover_t', simp }

@[simp, reassoc]
lemma glued_cover_t'_snd_fst (x y z : ğ’°.J) :
  glued_cover_t' ğ’° x y z â‰« pullback.snd â‰« pullback.fst = pullback.fst â‰« pullback.snd :=
by { delta glued_cover_t', simp }

@[simp, reassoc]
lemma glued_cover_t'_snd_snd (x y z : ğ’°.J) :
  glued_cover_t' ğ’° x y z â‰« pullback.snd â‰« pullback.snd = pullback.fst â‰« pullback.fst :=
by { delta glued_cover_t', simp }

lemma glued_cover_cocycle_fst (x y z : ğ’°.J) :
  glued_cover_t' ğ’° x y z â‰« glued_cover_t' ğ’° y z x â‰« glued_cover_t' ğ’° z x y â‰« pullback.fst =
    pullback.fst :=
by apply pullback.hom_ext; simp

lemma glued_cover_cocycle_snd (x y z : ğ’°.J) :
  glued_cover_t' ğ’° x y z â‰« glued_cover_t' ğ’° y z x â‰« glued_cover_t' ğ’° z x y â‰« pullback.snd =
    pullback.snd :=
by apply pullback.hom_ext; simp [pullback.condition]

lemma glued_cover_cocycle (x y z : ğ’°.J) :
  glued_cover_t' ğ’° x y z â‰« glued_cover_t' ğ’° y z x â‰« glued_cover_t' ğ’° z x y = ğŸ™ _ :=
begin
  apply pullback.hom_ext; simp_rw [category.id_comp, category.assoc],
  apply glued_cover_cocycle_fst,
  apply glued_cover_cocycle_snd,
end

/-- The glue data associated with an open cover.
The canonical isomorphism `ğ’°.glued_cover.glued âŸ¶ X` is provided by `ğ’°.from_glued`. -/
@[simps]
def glued_cover : Scheme.glue_data.{u} :=
{ J := ğ’°.J,
  U := ğ’°.obj,
  V := Î» âŸ¨x, yâŸ©, pullback (ğ’°.map x) (ğ’°.map y),
  f := Î» x y, pullback.fst,
  f_id := Î» x, infer_instance,
  t := Î» x y, (pullback_symmetry _ _).hom,
  t_id := Î» x, by simpa,
  t' := Î» x y z, glued_cover_t' ğ’° x y z,
  t_fac := Î» x y z, by apply pullback.hom_ext; simp,
  -- The `cocycle` field could have been `by tidy` but lean timeouts.
  cocycle := Î» x y z, glued_cover_cocycle ğ’° x y z,
  f_open := Î» x, infer_instance }

/-- The canonical morphism from the gluing of an open cover of `X` into `X`.
This is an isomorphism, as witnessed by an `is_iso` instance. -/
def from_glued : ğ’°.glued_cover.glued âŸ¶ X :=
begin
  fapply multicoequalizer.desc,
  exact Î» x, (ğ’°.map x),
  rintro âŸ¨x, yâŸ©,
  change pullback.fst â‰« _ = ((pullback_symmetry _ _).hom â‰« pullback.fst) â‰« _,
  simpa using pullback.condition
end

@[simp, reassoc]
lemma Î¹_from_glued (x : ğ’°.J) :
  ğ’°.glued_cover.Î¹ x â‰« ğ’°.from_glued = ğ’°.map x :=
multicoequalizer.Ï€_desc _ _ _ _ _

lemma from_glued_injective : function.injective ğ’°.from_glued.1.base :=
begin
  intros x y h,
  obtain âŸ¨i, x, rflâŸ© := ğ’°.glued_cover.Î¹_jointly_surjective x,
  obtain âŸ¨j, y, rflâŸ© :=  ğ’°.glued_cover.Î¹_jointly_surjective y,
  simp_rw [â† comp_apply, â† SheafedSpace.comp_base, â† LocallyRingedSpace.comp_val] at h,
  erw [Î¹_from_glued, Î¹_from_glued] at h,
  let e := (Top.pullback_cone_is_limit _ _).cone_point_unique_up_to_iso
    (is_limit_of_has_pullback_of_preserves_limit Scheme.forget_to_Top
      (ğ’°.map i) (ğ’°.map j)),
  rw ğ’°.glued_cover.Î¹_eq_iff,
  right,
  use e.hom âŸ¨âŸ¨x, yâŸ©, hâŸ©,
  simp_rw â† comp_apply,
  split,
  { erw is_limit.cone_point_unique_up_to_iso_hom_comp _ _ walking_cospan.left, refl },
  { erw [pullback_symmetry_hom_comp_fst,
      is_limit.cone_point_unique_up_to_iso_hom_comp _ _ walking_cospan.right], refl }
end

instance from_glued_stalk_iso (x : ğ’°.glued_cover.glued.carrier) :
  is_iso (PresheafedSpace.stalk_map ğ’°.from_glued.val x) :=
begin
  obtain âŸ¨i, x, rflâŸ© := ğ’°.glued_cover.Î¹_jointly_surjective x,
  have := PresheafedSpace.stalk_map.congr_hom _ _
    (congr_arg LocallyRingedSpace.hom.val $ ğ’°.Î¹_from_glued i) x,
  erw PresheafedSpace.stalk_map.comp at this,
  rw â† is_iso.eq_comp_inv at this,
  rw this,
  apply_instance,
end

lemma from_glued_open_map : is_open_map ğ’°.from_glued.1.base :=
begin
  intros U hU,
  rw is_open_iff_forall_mem_open,
  intros x hx,
  rw ğ’°.glued_cover.is_open_iff at hU,
  use ğ’°.from_glued.val.base '' U âˆ© set.range (ğ’°.map (ğ’°.f x)).1.base,
  use set.inter_subset_left _ _,
  split,
  { rw â† set.image_preimage_eq_inter_range,
    apply (show is_open_immersion (ğ’°.map (ğ’°.f x)), by apply_instance).base_open.is_open_map,
    convert hU (ğ’°.f x) using 1,
    rw â† Î¹_from_glued, erw coe_comp, rw set.preimage_comp,
    congr' 1,
    refine set.preimage_image_eq _ ğ’°.from_glued_injective },
  { exact âŸ¨hx, ğ’°.covers xâŸ© }
end

lemma from_glued_open_embedding : open_embedding ğ’°.from_glued.1.base :=
open_embedding_of_continuous_injective_open (by continuity) ğ’°.from_glued_injective
  ğ’°.from_glued_open_map

instance : epi ğ’°.from_glued.val.base :=
begin
  rw Top.epi_iff_surjective,
  intro x,
  obtain âŸ¨y, hâŸ© := ğ’°.covers x,
  use (ğ’°.glued_cover.Î¹ (ğ’°.f x)).1.base y,
  rw â† comp_apply,
  rw â† ğ’°.Î¹_from_glued (ğ’°.f x) at h,
  exact h
end

instance from_glued_open_immersion : is_open_immersion ğ’°.from_glued :=
SheafedSpace.is_open_immersion.of_stalk_iso _ ğ’°.from_glued_open_embedding

instance : is_iso ğ’°.from_glued :=
begin
  apply is_iso_of_reflects_iso _ (Scheme.forget_to_LocallyRingedSpace â‹™
    LocallyRingedSpace.forget_to_SheafedSpace â‹™ SheafedSpace.forget_to_PresheafedSpace),
  change @is_iso (PresheafedSpace _) _ _ _ ğ’°.from_glued.val,
  apply PresheafedSpace.is_open_immersion.to_iso,
end

/-- Given an open cover of `X`, and a morphism `ğ’°.obj x âŸ¶ Y` for each open subscheme in the cover,
such that these morphisms are compatible in the intersection (pullback), we may glue the morphisms
together into a morphism `X âŸ¶ Y`.

Note:
If `X` is exactly (defeq to) the gluing of `U i`, then using `multicoequalizer.desc` suffices.
-/
def glue_morphisms {Y : Scheme} (f : âˆ€ x, ğ’°.obj x âŸ¶ Y)
  (hf : âˆ€ x y, (pullback.fst : pullback (ğ’°.map x) (ğ’°.map y) âŸ¶ _) â‰« f x = pullback.snd â‰« f y) :
  X âŸ¶ Y :=
begin
  refine inv ğ’°.from_glued â‰« _,
  fapply multicoequalizer.desc,
  exact f,
  rintro âŸ¨i, jâŸ©,
  change pullback.fst â‰« f i = (_ â‰« _) â‰« f j,
  erw pullback_symmetry_hom_comp_fst,
  exact hf i j
end

@[simp, reassoc]
lemma Î¹_glue_morphisms {Y : Scheme} (f : âˆ€ x, ğ’°.obj x âŸ¶ Y)
  (hf : âˆ€ x y, (pullback.fst : pullback (ğ’°.map x) (ğ’°.map y) âŸ¶ _) â‰« f x = pullback.snd â‰« f y)
  (x : ğ’°.J) : (ğ’°.map x) â‰« ğ’°.glue_morphisms f hf = f x :=
begin
  rw [â† Î¹_from_glued, category.assoc],
  erw [is_iso.hom_inv_id_assoc, multicoequalizer.Ï€_desc],
end

lemma hom_ext {Y : Scheme} (fâ‚ fâ‚‚ : X âŸ¶ Y) (h : âˆ€ x, ğ’°.map x â‰« fâ‚ = ğ’°.map x â‰« fâ‚‚) : fâ‚ = fâ‚‚ :=
begin
  rw â† cancel_epi ğ’°.from_glued,
  apply multicoequalizer.hom_ext,
  intro x,
  erw multicoequalizer.Ï€_desc_assoc,
  erw multicoequalizer.Ï€_desc_assoc,
  exact h x,
end

end open_cover

end Scheme

end algebraic_geometry
