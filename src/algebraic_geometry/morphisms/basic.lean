/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.AffineScheme
import algebraic_geometry.pullbacks
import category_theory.adjunction.over

/-!
# Properties of morphisms

We provide the basic framework for talking about properties of morphisms.
The following meta-properties are defined

* `respects_iso`: `P` respects isomorphisms if `P f â†’ P (e â‰« f)` and `P f â†’ P (f â‰« e)`, where
  `e` is an isomorphism.
* `stable_under_composition`: `P` is stable under composition if `P f â†’ P g â†’ P (f â‰« g)`.
* `stable_under_base_change`: `P` is stable under base change if `P (Y âŸ¶ S) â†’ P (X Ã—[S] Y âŸ¶ X)`.

Special support is also provided for morphism properties defined for `X âŸ¶ Y` with `Y` affine
(`affine_target_morphism_property`). A morphism `f : X âŸ¶ Y` is locally `P`
(`target_affine_locally P`) if `P (f âˆ£_ U)` for each affine open set `U âŠ† Y`. Such a property `P` is
called local (`P.is_local`) if
1. `P` respects iso.
2. If `P` holds for some `f : X âŸ¶ Y`, `P` holds for `f âˆ£_ D (r)` for all `r : Î“(Y)`.
3. If `P` holds for `f âˆ£_ D (r)` for each `r` in a spanning set `s` of `Î“(Y)`, then `P` holds for
  `f`.

If `P` is local, then given a `f : X âŸ¶ Y`, then TFAE:
1. `f` is locally `P`.
2. `P` holds for all `f âˆ£_ Uáµ¢` for all affine covers `{ Uáµ¢ }` of `Y`.
3. `P` holds for all `f âˆ£_ Uáµ¢` for some affine cover `{ Uáµ¢ }` of `Y`.
4. locally `P` holds for all `f âˆ£_ Uáµ¢` for all open covers `{ Uáµ¢ }` of `Y`.
5. locally `P` holds for all `f âˆ£_ Uáµ¢` for some open cover `{ Uáµ¢ }` of `Y`.

Also, to check that "locally `P`" is stable under base change, it suffices to check that
  `P (Y âŸ¶ S) â†’ P (X Ã—[S] Y âŸ¶ X)` for affine `S` and `X`.

-/

universe u

open topological_space category_theory category_theory.limits opposite

noncomputable theory

namespace algebraic_geometry

def morphism_property := âˆ€ â¦ƒX Y : Schemeâ¦„ (f : X âŸ¶ Y), Prop

def affine_target_morphism_property := âˆ€ â¦ƒX Y : Schemeâ¦„ (f : X âŸ¶ Y) [is_affine Y], Prop

def affine_target_morphism_property.to_property (P : affine_target_morphism_property) :
  morphism_property :=
Î» X Y f, âˆƒ h, @@P f h

lemma affine_target_morphism_property.to_property_apply (P : affine_target_morphism_property)
  {X Y : Scheme} (f : X âŸ¶ Y) [is_affine Y] :
  P.to_property f â†” P f := by { delta affine_target_morphism_property.to_property, simp [*] }

def respects_iso (P : morphism_property) :=
  (âˆ€ {X Y Z} (e : X â‰… Y) (f : Y âŸ¶ Z), P f â†’ P (e.hom â‰« f)) âˆ§
  (âˆ€ {X Y Z} (e : Y â‰… Z) (f : X âŸ¶ Y), P f â†’ P (f â‰« e.hom))

def stable_under_composition (P : morphism_property) :=
  âˆ€ â¦ƒX Y Zâ¦„ (f : X âŸ¶ Y) (g : Y âŸ¶ Z), P f â†’ P g â†’ (P (f â‰« g))

def stable_under_base_change
  (P : morphism_property) : Prop :=
âˆ€ â¦ƒX Y S : Schemeâ¦„ (f : X âŸ¶ S) (g : Y âŸ¶ S), P g â†’ P (pullback.fst : pullback f g âŸ¶ X)

lemma stable_under_composition.respects_iso {P : morphism_property}
  (hP : stable_under_composition P) (hP' : âˆ€ {X Y} (e : X â‰… Y), P e.hom) : respects_iso P :=
âŸ¨Î» X Y Z e f hf, hP _ _ (hP' e) hf, Î» X Y Z e f hf, hP _ _ hf (hP' e)âŸ©

lemma respects_iso.cancel_left_is_iso {P : morphism_property}
  (hP : respects_iso P) {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [is_iso f] :
    P (f â‰« g) â†” P g :=
âŸ¨Î» h, by simpa using hP.1 (as_iso f).symm (f â‰« g) h, hP.1 (as_iso f) gâŸ©

lemma respects_iso.cancel_right_is_iso {P : morphism_property}
  (hP : respects_iso P) {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [is_iso g] :
    P (f â‰« g) â†” P f :=
âŸ¨Î» h, by simpa using hP.2 (as_iso g).symm (f â‰« g) h, hP.2 (as_iso g) fâŸ©

lemma stable_under_base_change.symmetry {P : morphism_property}
  (hP : stable_under_base_change P) (hP' : respects_iso P) {X Y S : Scheme} (f : X âŸ¶ S)
  (g : Y âŸ¶ S) (H : P f) : P (pullback.snd : pullback f g âŸ¶ Y) :=
begin
  rw [â† pullback_symmetry_hom_comp_fst, hP'.cancel_left_is_iso],
  apply hP,
  exact H
end

lemma stable_under_base_change.base_change_obj {P : morphism_property}
  (hP : stable_under_base_change P) (hP' : respects_iso P) {S S' : Scheme} (f : S' âŸ¶ S)
  (X : over S) (H : P X.hom) : P ((base_change f).obj X).hom :=
hP.symmetry hP' X.hom f H

def pullback.congr {C : Type*} [category C] {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z}
  (eâ‚ : fâ‚ = fâ‚‚) (eâ‚‚ : gâ‚ = gâ‚‚) [has_pullback fâ‚ gâ‚] [has_pullback fâ‚‚ gâ‚‚] :
    pullback fâ‚ gâ‚ â‰… pullback fâ‚‚ gâ‚‚ :=
as_iso (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) (by simpa using eâ‚) (by simpa using eâ‚‚))

@[simp, reassoc]
lemma pullback.congr_hom_fst {C : Type*} [category C] {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z}
  (eâ‚ : fâ‚ = fâ‚‚) (eâ‚‚ : gâ‚ = gâ‚‚) [has_pullback fâ‚ gâ‚] [has_pullback fâ‚‚ gâ‚‚] :
    (pullback.congr eâ‚ eâ‚‚).hom â‰« pullback.fst = pullback.fst :=
by { delta pullback.congr, simp }

@[simp, reassoc]
lemma pullback.congr_hom_snd {C : Type*} [category C] {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z}
  (eâ‚ : fâ‚ = fâ‚‚) (eâ‚‚ : gâ‚ = gâ‚‚) [has_pullback fâ‚ gâ‚] [has_pullback fâ‚‚ gâ‚‚] :
    (pullback.congr eâ‚ eâ‚‚).hom â‰« pullback.snd = pullback.snd :=
by { delta pullback.congr, simp }

@[simp, reassoc]
lemma pullback.congr_inv_fst {C : Type*} [category C] {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z}
  (eâ‚ : fâ‚ = fâ‚‚) (eâ‚‚ : gâ‚ = gâ‚‚) [has_pullback fâ‚ gâ‚] [has_pullback fâ‚‚ gâ‚‚] :
    (pullback.congr eâ‚ eâ‚‚).inv â‰« pullback.fst = pullback.fst :=
by rw [iso.inv_comp_eq, pullback.congr_hom_fst]

@[simp, reassoc]
lemma pullback.congr_inv_snd {C : Type*} [category C] {X Y Z : C} {fâ‚ fâ‚‚ : X âŸ¶ Z} {gâ‚ gâ‚‚ : Y âŸ¶ Z}
  (eâ‚ : fâ‚ = fâ‚‚) (eâ‚‚ : gâ‚ = gâ‚‚) [has_pullback fâ‚ gâ‚] [has_pullback fâ‚‚ gâ‚‚] :
    (pullback.congr eâ‚ eâ‚‚).inv â‰« pullback.snd = pullback.snd :=
by rw [iso.inv_comp_eq, pullback.congr_hom_snd]

lemma stable_under_base_change.base_change_map {P : morphism_property}
  (hP : stable_under_base_change P) (hP' : respects_iso P) {S S' : Scheme} (f : S' âŸ¶ S)
  {X Y : over S} (g : X âŸ¶ Y) (H : P g.left) : P ((base_change f).map g).left :=
begin
  let e := pullback_right_pullback_fst_iso Y.hom f g.left â‰ªâ‰«
    pullback.congr (g.w.trans (category.comp_id _)) rfl,
  have : e.inv â‰« pullback.snd = ((base_change f).map g).left,
  { apply pullback.hom_ext; dsimp; simp },
  rw [â† this, hP'.cancel_left_is_iso],
  apply hP.symmetry hP',
  exact H
end

lemma stable_under_base_change.pullback_map {P : morphism_property}
  (hP : stable_under_base_change P) (hP' : respects_iso P)
  (hP'' : stable_under_composition P) {S X X' Y Y' : Scheme}
  {f : X âŸ¶ S} {g : Y âŸ¶ S} {f' : X' âŸ¶ S} {g' : Y' âŸ¶ S} {iâ‚ : X âŸ¶ X'} {iâ‚‚ : Y âŸ¶ Y'}
  (hâ‚ : P iâ‚) (hâ‚‚ : P iâ‚‚) (eâ‚ : f = iâ‚ â‰« f') (eâ‚‚ : g = iâ‚‚ â‰« g') :
    P (pullback.map f g f' g' iâ‚ iâ‚‚ (ğŸ™ _)
      ((category.comp_id _).trans eâ‚) ((category.comp_id _).trans eâ‚‚)) :=
begin
  have : pullback.map f g f' g' iâ‚ iâ‚‚ (ğŸ™ _)
    ((category.comp_id _).trans eâ‚) ((category.comp_id _).trans eâ‚‚) =
      ((pullback_symmetry _ _).hom â‰«
      ((base_change _).map (over.hom_mk _ eâ‚‚.symm : over.mk g âŸ¶ over.mk g')).left) â‰«
      (pullback_symmetry _ _).hom â‰«
      ((base_change g').map (over.hom_mk _ eâ‚.symm : over.mk f âŸ¶ over.mk f')).left,
  { apply pullback.hom_ext; dsimp; simp },
  rw this,
  apply hP''; rw hP'.cancel_left_is_iso,
  exacts [hP.base_change_map hP' _ (over.hom_mk _ eâ‚‚.symm : over.mk g âŸ¶ over.mk g') hâ‚‚,
    hP.base_change_map hP' _ (over.hom_mk _ eâ‚.symm : over.mk f âŸ¶ over.mk f') hâ‚],
end

lemma congr_property_morphism_restrict_iff (P : morphism_property) (hP : respects_iso P)
  {X Y : Scheme} (f : X âŸ¶ Y) {U V : opens Y.carrier} (e : U = V) :
  P (f âˆ£_ U) â†” P (f âˆ£_ V) :=
by subst e

lemma property_iff_of_is_open_immersion (P : morphism_property) (hP : respects_iso P)
  {X Y U : Scheme} (f : X âŸ¶ Y) (g : U âŸ¶ Y) [hg : is_open_immersion g] :
  P (pullback.snd : pullback f g âŸ¶ _) â†” P (f âˆ£_ âŸ¨set.range g.1.base, hg.base_open.open_rangeâŸ©) :=
begin
  let V : opens Y.carrier := âŸ¨set.range g.1.base, hg.base_open.open_rangeâŸ©,
  let e := is_open_immersion.iso_of_range_eq g (Y.of_restrict V.open_embedding)
    (by exact subtype.range_coe.symm),
  let t : pullback f g âŸ¶ pullback f (Y.of_restrict V.open_embedding) :=
    pullback.map _ _ _ _ (ğŸ™ _) e.hom (ğŸ™ _) (by rw [category.comp_id, category.id_comp])
      (by rw [category.comp_id, is_open_immersion.iso_of_range_eq_hom, is_open_immersion.lift_fac]),
  have : t â‰« (pullback_restrict_iso_restrict f V).hom â‰« f âˆ£_ V â‰« e.inv = pullback.snd,
  { rw [â† cancel_mono g, is_open_immersion.iso_of_range_eq_inv, category.assoc, category.assoc,
      category.assoc, is_open_immersion.lift_fac, â† pullback.condition, morphism_restrict_Î¹,
      pullback_restrict_iso_restrict_hom_restrict_assoc,
      pullback.lift_fst_assoc, category.comp_id] },
  rw [â† this, hP.cancel_left_is_iso, hP.cancel_left_is_iso, hP.cancel_right_is_iso],
end

lemma property_restrict_restrict_iff (P : morphism_property) (hP : respects_iso P)
  {X Y : Scheme} (f : X âŸ¶ Y) (U : opens Y.carrier) (V : opens U) :
  P (f âˆ£_ U âˆ£_ V) â†” P (f âˆ£_ (U.open_embedding.is_open_map.functor.obj V)) :=
begin
  let : (f âˆ£_ U âˆ£_ V) = ((pullback_restrict_iso_restrict (f âˆ£_ U) V).inv â‰«
    (pullback_symmetry _ _).hom â‰« pullback.map _ _ _ _ (ğŸ™ _)
      ((pullback_restrict_iso_restrict f U).inv â‰« (pullback_symmetry _ _).hom) (ğŸ™ _)
    ((category.comp_id _).trans (category.id_comp _).symm) (by simpa) â‰«
    (pullback_right_pullback_fst_iso _ _ _).hom â‰« (pullback_symmetry _ _).hom) â‰« pullback.snd,
  { simpa },
  rw [this, hP.cancel_left_is_iso, property_iff_of_is_open_immersion _ hP],
  apply congr_property_morphism_restrict_iff P hP,
  ext1,
  dsimp,
  rw [coe_comp, set.range_comp],
  congr,
  exact subtype.range_coe,
end

lemma property_restrict_restrict_basic_open_iff (P : morphism_property) (hP : respects_iso P)
  {X Y : Scheme} (f : X âŸ¶ Y) (U : opens Y.carrier) (r : Y.presheaf.obj (op U)) :
  P (f âˆ£_ U âˆ£_ (Y.restrict _).basic_open
    (Y.presheaf.map (eq_to_hom U.open_embedding_obj_top).op r)) â†” P (f âˆ£_ Y.basic_open r) :=
begin
  rw property_restrict_restrict_iff _ hP,
  apply congr_property_morphism_restrict_iff _ hP,
  have e := Scheme.preimage_basic_open (Y.of_restrict U.open_embedding) r,
  erw [Scheme.of_restrict_coe_c_app, opens.adjunction_counit_app_self, eq_to_hom_op] at e,
  rw [â† (Y.restrict U.open_embedding).basic_open_res_eq _
    (eq_to_hom U.inclusion_map_eq_top).op, â† comp_apply],
  erw â† Y.presheaf.map_comp,
  rw [eq_to_hom_op, eq_to_hom_op, eq_to_hom_map, eq_to_hom_trans],
  erw â† e,
  ext1, dsimp [opens.map, opens.inclusion],
  rw [set.image_preimage_eq_inter_range, set.inter_eq_left_iff_subset, subtype.range_coe],
  exact Y.basic_open_subset r
end

def target_affine_locally (P : affine_target_morphism_property) : morphism_property :=
  Î» {X Y : Scheme} (f : X âŸ¶ Y), âˆ€ (U : Y.affine_opens), @@P (f âˆ£_ U) U.prop

lemma target_affine_locally_respects_iso {P : affine_target_morphism_property}
  (hP : respects_iso P.to_property) : respects_iso (target_affine_locally P) :=
begin
  split,
  { introv H U,
    rw [morphism_restrict_comp, â† P.to_property_apply, hP.cancel_left_is_iso],
    convert H U,
    rw â† P.to_property_apply },
  { introv H U,
    rw [morphism_restrict_comp, â† P.to_property_apply, hP.cancel_right_is_iso],
    convert H âŸ¨(opens.map e.hom.val.base).obj U.1, U.2.map_is_iso e.homâŸ©,
    rwa â† P.to_property_apply,
    refl }
end

structure affine_target_morphism_property.is_local (P : affine_target_morphism_property) : Prop :=
(respects_iso : respects_iso P.to_property)
(to_basic_open : âˆ€ {X Y : Scheme} [is_affine Y] (f : X âŸ¶ Y) (r : Y.presheaf.obj $ op âŠ¤),
  by exactI P f â†’
    @@P (f âˆ£_ (Y.basic_open r)) ((top_is_affine_open Y).basic_open_is_affine _))
(of_basic_open_cover : âˆ€ {X Y : Scheme} [is_affine Y] (f : X âŸ¶ Y)
  (s : finset (Y.presheaf.obj $ op âŠ¤)) (hs : ideal.span (s : set (Y.presheaf.obj $ op âŠ¤)) = âŠ¤),
  by exactI (âˆ€ (r : s), @@P (f âˆ£_ (Y.basic_open r.1))
    ((top_is_affine_open Y).basic_open_is_affine _)) â†’ P f)

lemma target_affine_locally_of_open_cover {P : affine_target_morphism_property}
  (hP : P.is_local)
  {X Y : Scheme} (f : X âŸ¶ Y) (ğ’° : Y.open_cover) [âˆ€ i, is_affine (ğ’°.obj i)]
  (hğ’° : âˆ€ i, P (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i)) :
    target_affine_locally P f :=
begin
  classical,
  let S := Î» i, (âŸ¨âŸ¨set.range (ğ’°.map i).1.base, (ğ’°.is_open i).base_open.open_rangeâŸ©,
    range_is_affine_open_of_open_immersion (ğ’°.map i)âŸ© : Y.affine_opens),
  apply of_affine_open_cover (Î» U, @@P (f âˆ£_ U.1) U.2) _ _ (set.range S); dsimp only,
  { rw set.eq_univ_iff_forall,
    simp only [set.mem_Union],
    intro x,
    exact âŸ¨âŸ¨_, âŸ¨ğ’°.f x, rflâŸ©âŸ©, ğ’°.covers xâŸ© },
  { rintro âŸ¨_, i, rflâŸ©,
    simp_rw â† P.to_property_apply at âŠ¢ hğ’°,
    exact (property_iff_of_is_open_immersion _ hP.1 _ _).mp (hğ’° i) },
  { intros U r h,
    haveI : is_affine _ := U.2,
    have := hP.2 (f âˆ£_ U.1),
    replace this := this (Y.presheaf.map (eq_to_hom U.1.open_embedding_obj_top).op r) h,
    rw â† P.to_property_apply at this âŠ¢,
    exact (property_restrict_restrict_basic_open_iff _ hP.1 _ _ r).mp this },
  { intros U s hs H,
    haveI : is_affine _ := U.2,
    apply hP.3 (f âˆ£_ U.1) (s.image (Y.presheaf.map (eq_to_hom U.1.open_embedding_obj_top).op)),
    { apply_fun ideal.comap (Y.presheaf.map (eq_to_hom U.1.open_embedding_obj_top.symm).op) at hs,
      rw ideal.comap_top at hs,
      rw â† hs,
      simp only [eq_to_hom_op, eq_to_hom_map, finset.coe_image],
      have : âˆ€ {R S : CommRing} (e : S = R) (s : set S),
        (by exactI ideal.span (eq_to_hom e '' s) = ideal.comap (eq_to_hom e.symm) (ideal.span s)),
      { intros, subst e, simpa },
      apply this },
    { rintro âŸ¨r, hrâŸ©,
      obtain âŸ¨r, hr', rflâŸ© := finset.mem_image.mp hr,
      simp_rw â† P.to_property_apply at âŠ¢ H,
      exact (property_restrict_restrict_basic_open_iff _ hP.1 f _ r).mpr (H âŸ¨r, hr'âŸ©) } }
end

lemma affine_target_morphism_property.is_local.affine_open_cover_tfae
  {P : affine_target_morphism_property}
  (hP : P.is_local) {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [target_affine_locally P f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)], âˆ€ (i : ğ’°.J),
      by exactI P (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)] (i : ğ’°.J),
      by exactI P (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_affine U] [is_open_immersion g],
      by exactI P (pullback.snd : pullback f g âŸ¶ U)] :=
begin
  tfae_have : 1 â†’ 4,
  { intros H U g hâ‚ hâ‚‚,
    resetI,
    replace H := H âŸ¨âŸ¨_, hâ‚‚.base_open.open_rangeâŸ©,
      range_is_affine_open_of_open_immersion gâŸ©,
    rw â† P.to_property_apply at H âŠ¢,
    rwa property_iff_of_is_open_immersion _ hP.1 },
  tfae_have : 4 â†’ 3,
  { intros H ğ’° hğ’° i,
    resetI,
    apply H },
  tfae_have : 3 â†’ 2,
  { exact Î» H, âŸ¨Y.affine_cover, infer_instance, H Y.affine_coverâŸ© },
  tfae_have : 2 â†’ 1,
  { rintro âŸ¨ğ’°, hğ’°, HâŸ©, exactI target_affine_locally_of_open_cover hP f ğ’° H },
  tfae_finish
end

lemma affine_target_morphism_property.is_local.open_cover_tfae
  {P : affine_target_morphism_property}
  (hP : P.is_local) {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [target_affine_locally P f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y), âˆ€ (i : ğ’°.J),
      target_affine_locally P (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) (i : ğ’°.J),
      target_affine_locally P (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (U : opens Y.carrier), target_affine_locally P (f âˆ£_ U),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_open_immersion g],
      target_affine_locally P (pullback.snd : pullback f g âŸ¶ U)] :=
begin
  tfae_have : 2 â†’ 1,
  { rintros âŸ¨ğ’°, hğ’°âŸ©,
    rw (hP.affine_open_cover_tfae f).out 0 1,
    refine âŸ¨ğ’°.bind (Î» _, Scheme.affine_cover _), _, _âŸ©,
    { intro i, dsimp [Scheme.open_cover.bind], apply_instance },
    { intro i,
      specialize hğ’° i.1,
      rw (hP.affine_open_cover_tfae (pullback.snd : pullback f (ğ’°.map i.fst) âŸ¶ _)).out 0 2
        at hğ’°,
      specialize hğ’° (Scheme.affine_cover _) i.2,
      let e : pullback f ((ğ’°.obj i.fst).affine_cover.map i.snd â‰« ğ’°.map i.fst) âŸ¶
        pullback (pullback.snd : pullback f (ğ’°.map i.fst) âŸ¶ _)
          ((ğ’°.obj i.fst).affine_cover.map i.snd),
      { refine (pullback_symmetry _ _).hom â‰« _,
        refine (pullback_right_pullback_fst_iso _ _ _).inv â‰« _,
        refine (pullback_symmetry _ _).hom â‰« _,
        refine pullback.map _ _ _ _ (pullback_symmetry _ _).hom (ğŸ™ _) (ğŸ™ _) _ _; simp },
      rw â† P.to_property_apply at âŠ¢ hğ’°,
      rw â† hP.1.cancel_left_is_iso e at hğ’°,
      convert hğ’°,
      simp } },
  tfae_have : 1 â†’ 4,
  { intros H U V,
    rw [â† P.to_property_apply, property_restrict_restrict_iff _ hP.1],
    convert H âŸ¨_, V.2.image_is_open_immersion (Y.of_restrict _)âŸ©,
    rw â† P.to_property_apply,
    refl },
  tfae_have : 4 â†’ 3,
  { intros H ğ’° i,
    have := H âŸ¨_, (ğ’°.is_open i).base_open.open_rangeâŸ©,
    rw property_iff_of_is_open_immersion _ (target_affine_locally_respects_iso hP.1),
    exact H âŸ¨_, (ğ’°.is_open i).base_open.open_rangeâŸ© },
  tfae_have : 3 â†’ 2,
  { exact Î» H, âŸ¨Y.affine_cover, H Y.affine_coverâŸ© },
  tfae_have : 4 â†’ 5,
  { intros H U g hg,
    resetI,
    rw property_iff_of_is_open_immersion _ (target_affine_locally_respects_iso hP.1),
    apply H },
  tfae_have : 5 â†’ 4,
  { intros H U,
    erw (target_affine_locally_respects_iso hP.1).cancel_left_is_iso,
    apply H },
  tfae_finish
end

lemma affine_target_morphism_property.is_local.affine_open_cover_iff
  {P : affine_target_morphism_property} (hP : P.is_local)
  {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.open_cover.{u} Y) [hğ’° : âˆ€ i, is_affine (ğ’°.obj i)] :
  target_affine_locally P f â†” âˆ€ i, @@P (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) (hğ’° i) :=
âŸ¨Î» H, let h := ((hP.affine_open_cover_tfae f).out 0 2).mp H in h ğ’°,
  Î» H, let h := ((hP.affine_open_cover_tfae f).out 1 0).mp in h âŸ¨ğ’°, infer_instance, HâŸ©âŸ©

lemma affine_target_morphism_property.is_local.open_cover_iff
  {P : affine_target_morphism_property} (hP : P.is_local)
  {X Y : Scheme.{u}} (f : X âŸ¶ Y) (ğ’° : Scheme.open_cover.{u} Y) :
  target_affine_locally P f â†”
    âˆ€ i, target_affine_locally P (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) :=
âŸ¨Î» H, let h := ((hP.open_cover_tfae f).out 0 2).mp H in h ğ’°,
  Î» H, let h := ((hP.open_cover_tfae f).out 1 0).mp in h âŸ¨ğ’°, HâŸ©âŸ©

universe v

/-- An isomorphism `X âŸ¶ Y` is an open cover of `Y`. -/
 @[simps J obj map]
 def open_cover_of_is_iso {X Y : Scheme.{u}} (f : X âŸ¶ Y) [is_iso f] :
   Y.open_cover :=
 { J := punit.{v+1},
   obj := Î» _, X,
   map := Î» _, f,
   f := Î» _, punit.star,
   covers := Î» x, by { rw set.range_iff_surjective.mpr, { trivial }, rw â† Top.epi_iff_surjective,
     apply_instance } }

lemma affine_target_morphism_property.is_local.affine_target_iff
  {P : affine_target_morphism_property} (hP : P.is_local)
  {X Y : Scheme.{u}} (f : X âŸ¶ Y) [is_affine Y] :
  target_affine_locally P f â†” P f :=
begin
  rw hP.affine_open_cover_iff f _,
  swap, { exact open_cover_of_is_iso (ğŸ™ Y) },
  swap, { intro _, dsimp, apply_instance },
  transitivity (P (pullback.snd : pullback f (ğŸ™ _) âŸ¶ _)),
  { exact âŸ¨Î» H, H punit.star, Î» H _, HâŸ© },
  rw [â† P.to_property_apply, â† P.to_property_apply, â† category.comp_id pullback.snd,
    â† pullback.condition, hP.1.cancel_left_is_iso],
end



-- @[simps]
-- def Scheme.open_cover.add {X : Scheme} (ğ’° : X.open_cover) {Y : Scheme} (f : Y âŸ¶ X)
--   [is_open_immersion f] : X.open_cover :=
-- { J := option ğ’°.J,
--   obj := Î» i, option.rec Y ğ’°.obj i,
--   map := Î» i, option.rec f ğ’°.map i,
--   f := Î» x, some (ğ’°.f x),
--   covers := ğ’°.covers,
--   is_open := by rintro (_|_); dsimp; apply_instance }

def affine_target_morphism_property.stable_under_base_change
  (P : affine_target_morphism_property) : Prop :=
âˆ€ â¦ƒX Y S : Schemeâ¦„ [is_affine S] [is_affine X] (f : X âŸ¶ S) (g : Y âŸ¶ S),
  by exactI P g â†’ P (pullback.fst : pullback f g âŸ¶ X)

lemma affine_target_morphism_property.is_local.affine_pullback_snd_of_left
  {P : affine_target_morphism_property} (hP : P.is_local) (hP' : P.stable_under_base_change)
  {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [is_affine S] (H : P g) :
  target_affine_locally P (pullback.fst : pullback f g âŸ¶ X) :=
begin
  rw (hP.affine_open_cover_tfae (pullback.fst : pullback f g âŸ¶ X)).out 0 1,
  use [X.affine_cover, infer_instance],
  intro i,
  let e := pullback_symmetry _ _ â‰ªâ‰« pullback_right_pullback_fst_iso f g (X.affine_cover.map i),
  have : e.hom â‰« pullback.fst = pullback.snd := by simp,
  rw [â† this, â† P.to_property_apply, hP.1.cancel_left_is_iso, P.to_property_apply],
  apply hP'; assumption,
end

lemma affine_target_morphism_property.is_local.stable_under_base_change
  {P : affine_target_morphism_property} (hP : P.is_local) (hP' : P.stable_under_base_change) :
  stable_under_base_change (target_affine_locally P) :=
begin
  introv X H,
  rw (hP.open_cover_tfae (pullback.fst : pullback f g âŸ¶ X)).out 0 1,
  use S.affine_cover.pullback_cover f,
  intro i,
  rw (hP.affine_open_cover_tfae g).out 0 3 at H,
  let e : pullback (pullback.fst : pullback f g âŸ¶ _) ((S.affine_cover.pullback_cover f).map i) â‰…
    _,
  { refine pullback_symmetry _ _ â‰ªâ‰« pullback_right_pullback_fst_iso f g _ â‰ªâ‰«
      _ â‰ªâ‰«
      (pullback_right_pullback_fst_iso (S.affine_cover.map i) g
        (pullback.snd : pullback f (S.affine_cover.map i) âŸ¶ _)).symm,
    exact as_iso (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _)
      (by simpa using pullback.condition) (by simp)) },
  have : e.hom â‰« pullback.fst = pullback.snd := by simp,
  rw [â† this, (target_affine_locally_respects_iso hP.1).cancel_left_is_iso],
  apply hP.affine_pullback_snd_of_left hP',
  rw [â† pullback_symmetry_hom_comp_snd, â† P.to_property_apply,
    hP.1.cancel_left_is_iso, P.to_property_apply],
  apply H
end


end algebraic_geometry
