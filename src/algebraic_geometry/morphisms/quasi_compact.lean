/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.morphisms.basic
import topology.spectral.hom
import algebraic_geometry.limits

/-!
# Quasi-compact morphisms

A morphism of schemes is quasi-compact if the preimages of quasi-compact open sets are
quasi-compact.

It suffices to check that preimages of affine open sets are compact
(`quasi_compact_iff_forall_affine`).

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

namespace algebraic_geometry

variables {X Y : Scheme.{u}} (f : X âŸ¶ Y)

/--
A morphism is `quasi-compact` if the underlying map of topological spaces is, i.e. if the preimages
of quasi-compact open sets are quasi-compact.
-/
@[mk_iff]
class quasi_compact (f : X âŸ¶ Y) : Prop :=
(is_compact_preimage : âˆ€ U : set Y.carrier, is_open U â†’ is_compact U â†’ is_compact (f.1.base â»Â¹' U))

lemma quasi_compact_iff_spectral : quasi_compact f â†” is_spectral_map f.1.base :=
âŸ¨Î» âŸ¨hâŸ©, âŸ¨by continuity, hâŸ©, Î» h, âŸ¨h.2âŸ©âŸ©

/-- The `affine_target_morphism_property` corresponding to `quasi_compact`, asserting that the
domain is a quasi-compact scheme. -/
def quasi_compact.affine_property : affine_target_morphism_property :=
Î» X Y f hf, compact_space X.carrier

@[priority 900]
instance quasi_compact_of_is_iso {X Y : Scheme} (f : X âŸ¶ Y) [is_iso f] : quasi_compact f :=
begin
  constructor,
  intros U hU hU',
  convert hU'.image (inv f.1.base).continuous_to_fun using 1,
  rw set.image_eq_preimage_of_inverse,
  delta function.left_inverse,
  exacts [is_iso.inv_hom_id_apply f.1.base, is_iso.hom_inv_id_apply f.1.base]
end

instance quasi_compact_comp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [quasi_compact f] [quasi_compact g] : quasi_compact (f â‰« g) :=
begin
  constructor,
  intros U hU hU',
  rw [Scheme.comp_val_base, coe_comp, set.preimage_comp],
  apply quasi_compact.is_compact_preimage,
  { exact continuous.is_open_preimage (by continuity) _ hU },
  apply quasi_compact.is_compact_preimage; assumption
end

lemma is_compact_open_iff_eq_finset_affine_union {X : Scheme} (U : set X.carrier) :
  is_compact U âˆ§ is_open U â†”
    âˆƒ (s : set X.affine_opens), s.finite âˆ§ U = â‹ƒ (i : X.affine_opens) (h : i âˆˆ s), i :=
begin
  apply opens.is_compact_open_iff_eq_finite_Union_of_is_basis
    (coe : X.affine_opens â†’ opens X.carrier),
  { rw subtype.range_coe, exact is_basis_affine_open X },
  { intro i, exact i.2.is_compact }
end

lemma is_compact_open_iff_eq_basic_open_union {X : Scheme} [is_affine X] (U : set X.carrier) :
  is_compact U âˆ§ is_open U â†”
    âˆƒ (s : set (X.presheaf.obj (op âŠ¤))), s.finite âˆ§
      U = â‹ƒ (i : X.presheaf.obj (op âŠ¤)) (h : i âˆˆ s), X.basic_open i :=
begin
  apply opens.is_compact_open_iff_eq_finite_Union_of_is_basis,
  { exact is_basis_basic_open X },
  { intro i, exact ((top_is_affine_open _).basic_open_is_affine _).is_compact }
end

lemma quasi_compact_iff_forall_affine : quasi_compact f â†”
  âˆ€ U : opens Y.carrier, is_affine_open U â†’ is_compact (f.1.base â»Â¹' (U : set Y.carrier)) :=
begin
  rw quasi_compact_iff,
  refine âŸ¨Î» H U hU, H U U.prop hU.is_compact, _âŸ©,
  intros H U hU hU',
  obtain âŸ¨S, hS, rflâŸ© := (is_compact_open_iff_eq_finset_affine_union U).mp âŸ¨hU', hUâŸ©,
  simp only [set.preimage_Union, subtype.val_eq_coe],
  exact hS.is_compact_bUnion (Î» i _, H i i.prop)
end

@[simp] lemma quasi_compact.affine_property_to_property {X Y : Scheme} (f : X âŸ¶ Y) :
  (quasi_compact.affine_property : _).to_property f â†”
    is_affine Y âˆ§ compact_space X.carrier :=
by { delta affine_target_morphism_property.to_property quasi_compact.affine_property, simp }

lemma quasi_compact_iff_affine_property :
  quasi_compact f â†” target_affine_locally quasi_compact.affine_property f :=
begin
  rw quasi_compact_iff_forall_affine,
  transitivity (âˆ€ U : Y.affine_opens, is_compact (f.1.base â»Â¹' (U : set Y.carrier))),
  { exact âŸ¨Î» h U, h U U.prop, Î» h U hU, h âŸ¨U, hUâŸ©âŸ© },
  apply forall_congr,
  exact Î» _, is_compact_iff_compact_space,
end

lemma quasi_compact_eq_affine_property :
  @quasi_compact = target_affine_locally quasi_compact.affine_property :=
by { ext, exact quasi_compact_iff_affine_property _ }

lemma is_compact_basic_open (X : Scheme) {U : opens X.carrier} (hU : is_compact (U : set X.carrier))
   (f : X.presheaf.obj (op U)) : is_compact (X.basic_open f : set X.carrier) :=
begin
  classical,
  refine ((is_compact_open_iff_eq_finset_affine_union _).mpr _).1,
  obtain âŸ¨s, hs, eâŸ© := (is_compact_open_iff_eq_finset_affine_union _).mp âŸ¨hU, U.propâŸ©,
  let g : s â†’ X.affine_opens,
  { intro V,
    use V.1 âŠ“ X.basic_open f,
    have : V.1.1 âŸ¶ U,
    { apply hom_of_le, change _ âŠ† (U : set X.carrier), rw e,
      convert @set.subset_Unionâ‚‚ _ _ _ (Î» (U : X.affine_opens) (h : U âˆˆ s), â†‘U) V V.prop using 1,
      refl },
    erw â† X.to_LocallyRingedSpace.to_RingedSpace.basic_open_res this.op,
    exact is_affine_open.basic_open_is_affine V.1.prop _ },
  haveI : finite s := hs.to_subtype,
  refine âŸ¨set.range g, set.finite_range g, _âŸ©,
  refine (set.inter_eq_right_iff_subset.mpr (RingedSpace.basic_open_le _ _)).symm.trans _,
  rw [e, set.Unionâ‚‚_inter],
  apply le_antisymm; apply set.Unionâ‚‚_subset,
  { intros i hi,
    refine set.subset.trans _ (set.subset_Unionâ‚‚ _ (set.mem_range_self âŸ¨i, hiâŸ©)),
    exact set.subset.rfl },
  { rintro âŸ¨i, hiâŸ© âŸ¨âŸ¨j, hjâŸ©, hj'âŸ©,
    rw â† hj',
    refine set.subset.trans _ (set.subset_Unionâ‚‚ j hj),
    exact set.subset.rfl }
end

lemma quasi_compact.affine_property_is_local :
  (quasi_compact.affine_property : _).is_local :=
begin
  split,
  { apply affine_target_morphism_property.respects_iso_mk; rintros X Y Z _ _ _ H,
    exacts [@@homeomorph.compact_space _ _ H (Top.homeo_of_iso (as_iso e.inv.1.base)), H] },
  { introv H,
    delta quasi_compact.affine_property at H âŠ¢,
    change compact_space ((opens.map f.val.base).obj (Y.basic_open r)),
    rw Scheme.preimage_basic_open f r,
    erw â† is_compact_iff_compact_space,
    rw â† is_compact_univ_iff at H,
    exact is_compact_basic_open X H _ },
  { rintros X Y H f S hS hS',
    resetI,
    rw â† is_affine_open.basic_open_union_eq_self_iff at hS,
    delta quasi_compact.affine_property,
    rw â† is_compact_univ_iff,
    change is_compact ((opens.map f.val.base).obj âŠ¤).1,
    rw â† hS,
    dsimp [opens.map],
    simp only [opens.coe_supr, set.preimage_Union, subtype.val_eq_coe],
    exacts [is_compact_Union (Î» i, is_compact_iff_compact_space.mpr (hS' i)),
      top_is_affine_open _] }
end

lemma quasi_compact.affine_open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_compact f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)],
      âˆ€ (i : ğ’°.J), compact_space (pullback f (ğ’°.map i)).carrier,
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)] (i : ğ’°.J),
      compact_space (pullback f (ğ’°.map i)).carrier,
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_affine U] [is_open_immersion g],
      compact_space (pullback f g).carrier,
    âˆƒ {Î¹ : Type u} (U : Î¹ â†’ opens Y.carrier) (hU : supr U = âŠ¤) (hU' : âˆ€ i, is_affine_open (U i)),
      âˆ€ i, compact_space (f.1.base â»Â¹' (U i).1)] :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact.affine_property_is_local.affine_open_cover_tfae f

lemma quasi_compact.is_local_at_target :
  property_is_local_at_target @quasi_compact :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact.affine_property_is_local.target_affine_locally_is_local

lemma quasi_compact.open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_compact f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y), âˆ€ (i : ğ’°.J),
      quasi_compact (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) (i : ğ’°.J),
      quasi_compact (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (U : opens Y.carrier), quasi_compact (f âˆ£_ U),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_open_immersion g],
      quasi_compact (pullback.snd : pullback f g âŸ¶ _),
    âˆƒ {Î¹ : Type u} (U : Î¹ â†’ opens Y.carrier) (hU : supr U = âŠ¤), âˆ€ i, quasi_compact (f âˆ£_ (U i))] :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact.affine_property_is_local.target_affine_locally_is_local.open_cover_tfae f

lemma quasi_compact_over_affine_iff {X Y : Scheme} (f : X âŸ¶ Y) [is_affine Y] :
  quasi_compact f â†” compact_space X.carrier :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact.affine_property_is_local.affine_target_iff f

lemma compact_space_iff_quasi_compact (X : Scheme) :
  compact_space X.carrier â†” quasi_compact (terminal.from X) :=
(quasi_compact_over_affine_iff _).symm

lemma quasi_compact.affine_open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y)
  [âˆ€ i, is_affine (ğ’°.obj i)] (f : X âŸ¶ Y) :
  quasi_compact f â†” âˆ€ i, compact_space (pullback f (ğ’°.map i)).carrier :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact.affine_property_is_local.affine_open_cover_iff f ğ’°

lemma quasi_compact.open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y) (f : X âŸ¶ Y) :
  quasi_compact f â†” âˆ€ i, quasi_compact (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact.affine_property_is_local.target_affine_locally_is_local.open_cover_iff f ğ’°

lemma quasi_compact_respects_iso : morphism_property.respects_iso @quasi_compact :=
quasi_compact_eq_affine_property.symm â–¸
  target_affine_locally_respects_iso quasi_compact.affine_property_is_local.1

lemma quasi_compact_stable_under_composition :
  morphism_property.stable_under_composition @quasi_compact :=
Î» _ _ _ _ _ _ _, by exactI infer_instance

local attribute [-simp] PresheafedSpace.as_coe SheafedSpace.as_coe

lemma quasi_compact.affine_property_stable_under_base_change :
  quasi_compact.affine_property.stable_under_base_change :=
begin
  intros X Y S _ _ f g h,
  rw quasi_compact.affine_property at h âŠ¢,
  resetI,
  let ğ’° := Scheme.pullback.open_cover_of_right Y.affine_cover.finite_subcover f g,
  haveI : finite ğ’°.J,
  { dsimp [ğ’°], apply_instance },
  haveI : âˆ€ i, compact_space (ğ’°.obj i).carrier,
  { intro i, dsimp, apply_instance },
  exact ğ’°.compact_space,
end

lemma quasi_compact_stable_under_base_change :
  morphism_property.stable_under_base_change @quasi_compact :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact.affine_property_is_local.stable_under_base_change
    quasi_compact.affine_property_stable_under_base_change

variables {Z : Scheme.{u}}

instance (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [quasi_compact g] :
  quasi_compact (pullback.fst : pullback f g âŸ¶ X) :=
quasi_compact_stable_under_base_change.fst f g infer_instance

instance (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [quasi_compact f] :
  quasi_compact (pullback.snd : pullback f g âŸ¶ Y) :=
quasi_compact_stable_under_base_change.snd f g infer_instance

@[elab_as_eliminator]
lemma compact_open_induction_on {P : opens X.carrier â†’ Prop} (S : opens X.carrier)
  (hS : is_compact S.1)
  (hâ‚ : P âŠ¥)
  (hâ‚‚ : âˆ€ (S : opens X.carrier) (hS : is_compact S.1) (U : X.affine_opens), P S â†’ P (S âŠ” U)) :
    P S :=
begin
  classical,
  obtain âŸ¨s, hs, hs'âŸ© := (is_compact_open_iff_eq_finset_affine_union S.1).mp âŸ¨hS, S.2âŸ©,
  replace hs' : S = supr (Î» i : s, (i : opens X.carrier)) := by { ext1, simpa using hs' },
  subst hs',
  apply hs.induction_on,
  { convert hâ‚, rw supr_eq_bot, rintro âŸ¨_, hâŸ©, exact h.elim },
  { intros x s hâ‚ƒ hs hâ‚„,
    have : is_compact (â¨† i : s, (i : opens X.carrier)).1,
    { refine ((is_compact_open_iff_eq_finset_affine_union _).mpr _).1, exact âŸ¨s, hs, by simpâŸ© },
    convert hâ‚‚ _ this x hâ‚„,
    simp only [coe_coe],
    rw [supr_subtype, sup_comm],
    conv_rhs { rw supr_subtype },
    exact supr_insert }
end

end algebraic_geometry
