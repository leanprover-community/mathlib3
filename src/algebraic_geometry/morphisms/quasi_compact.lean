/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.morphisms.basic

/-!
# Quasi-compact morphisms

A morphism of schemes is quasi-compact if the preimages of quasi-compact open sets are
quasi-compact.

It suffices to check that preimages of affine open sets are compact
(`quasi_compact_iff_forall_affine`).

We show that this property is local, and is stable under compositions and base-changes.

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

namespace algebraic_geometry

variables {X Y : Scheme.{u}} (f : X âŸ¶ Y)

/--
A morphism is `quasi-compact` if the underlying map of topological spaces is, i.e. if the preimages
of quasi-compact open sets are quasi-compact.
-/
@[mk_iff]
class quasi_compact (f : X âŸ¶ Y) : Prop :=
(is_compact_preimage : âˆ€ U : set Y.carrier, is_open U â†’ is_compact U â†’ is_compact (f.1.base â»Â¹' U))

def quasi_compact.affine_property : affine_target_morphism_property :=
Î» X Y f hf, compact_space X.carrier

@[simp] lemma quasi_compact_affine_property_to_property {X Y : Scheme} (f : X âŸ¶ Y) :
  affine_target_morphism_property.to_property quasi_compact.affine_property f â†”
    is_affine Y âˆ§ compact_space X.carrier :=
by { delta affine_target_morphism_property.to_property quasi_compact.affine_property, simp }

@[priority 900]
instance quasi_compact_of_is_iso {X Y : Scheme} (f : X âŸ¶ Y) [is_iso f] : quasi_compact f :=
begin
  constructor,
  intros U hU hU',
  convert hU'.image (inv f.1.base).continuous_to_fun using 1,
  rw set.image_eq_preimage_of_inverse,
  delta function.left_inverse,
  exacts [is_iso.inv_hom_id_apply f.1.base, is_iso.hom_inv_id_apply f.1.base]
end

instance quasi_compact_comp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [quasi_compact f] [quasi_compact g] : quasi_compact (f â‰« g) :=
begin
  constructor,
  intros U hU hU',
  rw [Scheme.comp_val_base, coe_comp, set.preimage_comp],
  apply quasi_compact.is_compact_preimage,
  { exact continuous.is_open_preimage (by continuity) _ hU },
  apply quasi_compact.is_compact_preimage; assumption
end

/-- If `Î±` has a basis consisting of compact opens, then an open set in `Î±` is compact open iff
  it is a finite union of some elements in the basis -/
lemma is_compact_open_iff_eq_finset_Union_of_is_topological_basis {Î± : Type*} [topological_space Î±]
  {Î¹ : Type*} (b : Î¹ â†’ set Î±) (hb : is_topological_basis (set.range b))
  (hb' : âˆ€ i, is_compact (b i)) (U : set Î±) :
  is_compact U âˆ§ is_open U â†” âˆƒ (s : finset Î¹), U = â‹ƒ i : s, b i :=
begin
  classical,
  split,
  { rintro âŸ¨hâ‚, hâ‚‚âŸ©,
    obtain âŸ¨Î², f, e, hfâŸ© := hb.open_eq_Union hâ‚‚,
    choose f' hf' using hf,
    have : b âˆ˜ f' = f := funext hf', subst this,
    obtain âŸ¨t, htâŸ© := hâ‚.elim_finite_subcover (b âˆ˜ f')
      (Î» i, hb.is_open (set.mem_range_self _)) (by rw e),
    use t.image f',
    apply le_antisymm,
    { refine set.subset.trans ht _,
      simp only [set.Union_subset_iff, coe_coe],
      intros i hi,
      exact set.subset_Union (Î» i : t.image f', b i) âŸ¨_, finset.mem_image_of_mem _ hiâŸ© },
    { apply set.Union_subset,
      rintro âŸ¨i, hiâŸ©,
      obtain âŸ¨j, hj, rflâŸ© := finset.mem_image.mp hi,
      rw e,
      exact set.subset_Union (b âˆ˜ f') j } },
  { rintro âŸ¨s, rflâŸ©,
    split,
    { convert @finset.compact_bUnion _ _ _ s.attach _ _,
      { ext y, simp },
      { exact Î» i _, hb' i } },
    { apply is_open_Union, rintro i, exact hb.is_open (set.mem_range_self _) } },
end


/-- If `Î±` has a basis consisting of compact opens, then an open set in `Î±` is compact open iff
  it is a finite union of some elements in the basis -/
lemma is_compact_open_iff_eq_finset_Union_of_opens_is_basis {Î± : Type*} [topological_space Î±]
  {Î¹ : Type*} (b : Î¹ â†’ opens Î±) (hb : opens.is_basis (set.range b))
  (hb' : âˆ€ i, is_compact (b i : set Î±)) (U : set Î±) :
  is_compact U âˆ§ is_open U â†” âˆƒ (s : finset Î¹), U = â‹ƒ i : s, b i :=
begin
  apply is_compact_open_iff_eq_finset_Union_of_is_topological_basis
    (Î» i : Î¹, (b i).1),
  { convert hb, ext, simp },
  { exact hb' }
end

lemma is_compact_open_iff_eq_finset_affine_union {X : Scheme} (U : set X.carrier) :
  is_compact U âˆ§ is_open U â†” âˆƒ (s : finset X.affine_opens), U = â‹ƒ (i : s), i :=
begin
  apply is_compact_open_iff_eq_finset_Union_of_opens_is_basis
    (coe : X.affine_opens â†’ opens X.carrier),
  { rw subtype.range_coe, exact is_basis_affine_open X },
  { intro i, exact i.2.is_compact }
end

lemma is_compact_open_iff_eq_basic_open_union {X : Scheme} [is_affine X] (U : set X.carrier) :
  is_compact U âˆ§ is_open U â†”
    âˆƒ (s : finset (X.presheaf.obj (op âŠ¤))), U = â‹ƒ (i : s), X.basic_open i.1 :=
begin
  apply is_compact_open_iff_eq_finset_Union_of_opens_is_basis,
  { exact is_basis_basic_open X },
  { intro i, exact ((top_is_affine_open _).basic_open_is_affine _).is_compact }
end

lemma quasi_compact_iff_forall_affine : quasi_compact f â†”
  âˆ€ U : opens Y.carrier, is_affine_open U â†’ is_compact (f.1.base â»Â¹' (U : set Y.carrier)) :=
begin
  rw quasi_compact_iff,
  refine âŸ¨Î» H U hU, H U U.prop hU.is_compact, _âŸ©,
  intros H U hU hU',
  obtain âŸ¨S, rflâŸ© := (is_compact_open_iff_eq_finset_affine_union U).mp âŸ¨hU', hUâŸ©,
  simp only [set.preimage_Union, subtype.val_eq_coe],
  convert S.compact_bUnion (Î» i _, H i i.prop) using 1,
  exact set.Union_subtype _ _
end

lemma quasi_compact_iff_affine_property :
  quasi_compact f â†” target_affine_locally quasi_compact.affine_property f :=
begin
  rw quasi_compact_iff_forall_affine,
  transitivity (âˆ€ U : Y.affine_opens, is_compact (f.1.base â»Â¹' (U : set Y.carrier))),
  { exact âŸ¨Î» h U, h U U.prop, Î» h U hU, h âŸ¨U, hUâŸ©âŸ© },
  apply forall_congr,
  exact Î» _, is_compact_iff_compact_space,
end

lemma quasi_compact_eq_affine_property :
  @quasi_compact = target_affine_locally quasi_compact.affine_property :=
by { ext, exact quasi_compact_iff_affine_property _ }

lemma is_compact_basic_open (X : Scheme) {U : opens X.carrier} (hU : is_compact (U : set X.carrier))
  (f : X.presheaf.obj (op U)) : is_compact (X.basic_open f : set X.carrier) :=
begin
  classical,
  refine ((is_compact_open_iff_eq_finset_affine_union _).mpr _).1,
  obtain âŸ¨s, hsâŸ© := (is_compact_open_iff_eq_finset_affine_union _).mp âŸ¨hU, U.propâŸ©,
  let g : s â†’ X.affine_opens,
  { intro V,
    use V.1 âˆ© X.basic_open f,
    have : V.1.1 âŸ¶ U,
    { apply hom_of_le, change _ âŠ† (U : set X.carrier), rw hs, exact set.subset_Union _ V },
    erw â† X.to_LocallyRingedSpace.to_RingedSpace.basic_open_res this.op,
    exact is_affine_open.basic_open_is_affine V.1.prop _ },
  use s.attach.image g,
  refine (set.inter_eq_right_iff_subset.mpr (RingedSpace.basic_open_subset _ _)).symm.trans _,
  rw [hs, set.Union_inter],
  apply le_antisymm; apply set.Union_subset,
  { intro i,
    refine set.subset.trans _
      (set.subset_Union _ (âŸ¨_, finset.mem_image_of_mem g (s.mem_attach i)âŸ© : s.attach.image g)),
    exact set.subset.rfl },
  { rintro âŸ¨i, hiâŸ©,
    obtain âŸ¨j : s, hj, rflâŸ© := finset.mem_image.mp hi,
    refine set.subset.trans _ (set.subset_Union _ j),
    exact set.subset.rfl }
end

lemma quasi_compact_affine_property_is_local :
  affine_target_morphism_property.is_local quasi_compact.affine_property :=
begin
  split,
  { split; intros X Y Z _ _ _ H,
    exacts [@@homeomorph.compact_space _ _ H (Top.homeo_of_iso (as_iso e.inv.1.base)), H] },
  { introv H,
    delta quasi_compact.affine_property at H âŠ¢,
    change compact_space ((opens.map f.val.base).obj (Y.basic_open r)),
    rw Scheme.preimage_basic_open f r,
    erw â† is_compact_iff_compact_space,
    rw â† is_compact_univ_iff at H,
    exact is_compact_basic_open X H _ },
  { rintros X Y H f S hS hS',
    resetI,
    rw â† is_affine_open.basic_open_union_eq_self_iff at hS,
    delta quasi_compact.affine_property,
    rw â† is_compact_univ_iff,
    change is_compact ((opens.map f.val.base).obj âŠ¤).1,
    rw â† hS,
    dsimp [opens.map],
    simp only [opens.supr_s, set.preimage_Union, subtype.val_eq_coe],
    exacts [compact_Union (Î» i, is_compact_iff_compact_space.mpr (hS' i)), top_is_affine_open _] }
end

lemma Scheme.open_cover.Union_range {X : Scheme} (ğ’° : X.open_cover) :
  (â‹ƒ i, set.range (ğ’°.map i).1.base) = set.univ :=
begin
  rw set.eq_univ_iff_forall,
  intros x,
  rw set.mem_Union,
  exact âŸ¨ğ’°.f x, ğ’°.covers xâŸ©
end

lemma Scheme.open_cover.compact_space {X : Scheme} (ğ’° : X.open_cover) [fintype ğ’°.J]
  [H : âˆ€ i, compact_space (ğ’°.obj i).carrier] : compact_space X.carrier :=
begin
  rw [â† is_compact_univ_iff, â† ğ’°.Union_range],
  apply compact_Union,
  intro i,
  rw is_compact_iff_compact_space,
  exact @@homeomorph.compact_space _ _ (H i)
    (Top.homeo_of_iso (as_iso (is_open_immersion.iso_of_range_eq (ğ’°.map i)
    (X.of_restrict (opens.open_embedding âŸ¨_, (ğ’°.is_open i).base_open.open_rangeâŸ©))
    subtype.range_coe.symm).hom.1.base))
end

lemma quasi_compact_affine_property_stable_under_base_change :
  affine_target_morphism_property.stable_under_base_change quasi_compact.affine_property :=
begin
  introv X H,
  delta quasi_compact.affine_property at H âŠ¢,
  resetI,
  apply_with Scheme.open_cover.compact_space { instances := ff },
  swap 3,
  { exact Scheme.pullback.open_cover_of_right Y.affine_cover.finite_subcover f g },
  { change fintype Y.affine_cover.finite_subcover.J,
    apply_instance },
  { intro i,
    haveI : is_affine (Y.affine_cover.finite_subcover.obj i),
    { dsimp, apply_instance },
    change compact_space (pullback f _).carrier,
    apply_instance }
end

lemma quasi_compact.affine_open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_compact f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)],
      âˆ€ (i : ğ’°.J), compact_space (pullback f (ğ’°.map i)).carrier,
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)] (i : ğ’°.J),
      compact_space (pullback f (ğ’°.map i)).carrier,
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_affine U] [is_open_immersion g],
      compact_space (pullback f g).carrier,
    âˆƒ {Î¹ : Type u} (U : Î¹ â†’ opens Y.carrier) (hU : supr U = âŠ¤) (hU' : âˆ€ i, is_affine_open (U i)),
      âˆ€ i, compact_space (f.1.base â»Â¹' (U i).1)] :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact_affine_property_is_local.affine_open_cover_tfae f

lemma quasi_compact.open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_compact f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y), âˆ€ (i : ğ’°.J),
      quasi_compact (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) (i : ğ’°.J),
      quasi_compact (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (U : opens Y.carrier), quasi_compact (f âˆ£_ U),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_open_immersion g],
      quasi_compact (pullback.snd : pullback f g âŸ¶ _),
    âˆƒ {Î¹ : Type u} (U : Î¹ â†’ opens Y.carrier) (hU : supr U = âŠ¤), âˆ€ i, quasi_compact (f âˆ£_ (U i))] :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact_affine_property_is_local.open_cover_tfae f

lemma quasi_compact_over_affine_iff {X Y : Scheme} (f : X âŸ¶ Y) [is_affine Y] :
  quasi_compact f â†” compact_space X.carrier :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact_affine_property_is_local.affine_target_iff f

lemma compact_space_iff_quasi_compact (X : Scheme) :
  compact_space X.carrier â†” quasi_compact (terminal.from X) :=
(quasi_compact_over_affine_iff _).symm

lemma quasi_compact.affine_open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y)
  [âˆ€ i, is_affine (ğ’°.obj i)] (f : X âŸ¶ Y) :
  quasi_compact f â†” âˆ€ i, compact_space (pullback f (ğ’°.map i)).carrier :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact_affine_property_is_local.affine_open_cover_iff f ğ’°

lemma quasi_compact.open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y)
  [âˆ€ i, is_affine (ğ’°.obj i)] (f : X âŸ¶ Y) :
  quasi_compact f â†” âˆ€ i, quasi_compact (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact_affine_property_is_local.open_cover_iff f ğ’°

lemma quasi_compact_stable_under_base_change :
  stable_under_base_change @quasi_compact :=
quasi_compact_eq_affine_property.symm â–¸
  quasi_compact_affine_property_is_local.stable_under_base_change
    quasi_compact_affine_property_stable_under_base_change

lemma quasi_compact_respects_iso : respects_iso @quasi_compact :=
quasi_compact_eq_affine_property.symm â–¸
  target_affine_locally_respects_iso quasi_compact_affine_property_is_local.1

lemma quasi_compact_stable_under_composition :
  stable_under_composition @quasi_compact :=
Î» _ _ _ _ _ _ _, by exactI infer_instance

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [quasi_compact g] :
  quasi_compact (pullback.fst : pullback f g âŸ¶ X) :=
quasi_compact_stable_under_base_change f g infer_instance

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [quasi_compact f] :
  quasi_compact (pullback.snd : pullback f g âŸ¶ Y) :=
quasi_compact_stable_under_base_change.symmetry quasi_compact_respects_iso f g infer_instance

lemma exists_power_mul_eq_zero_of_res_basic_open_eq_zero_of_is_affine_open (X : Scheme)
  {U : opens X.carrier} (hU : is_affine_open U) (x f : X.presheaf.obj (op U))
  (H : X.presheaf.map (hom_of_le $ X.basic_open_subset f : X.basic_open f âŸ¶ U).op x = 0) :
  âˆƒ n : â„•, f ^ n * x = 0 :=
begin
  rw â† map_zero (X.presheaf.map (hom_of_le $ X.basic_open_subset f : X.basic_open f âŸ¶ U).op) at H,
  have := (is_localization_basic_open hU f).3,
  obtain âŸ¨âŸ¨_, n, rflâŸ©, eâŸ© := this.mp H,
  exact âŸ¨n, by simpa [mul_comm x] using eâŸ©,
end

lemma exists_power_mul_eq_zero_of_res_basic_open_eq_zero_of_is_compact (X : Scheme)
  {U : opens X.carrier} (hU : is_compact U.1) (x f : X.presheaf.obj (op U))
  (H : X.presheaf.map (hom_of_le $ X.basic_open_subset f : X.basic_open f âŸ¶ U).op x = 0) :
  âˆƒ n : â„•, f ^ n * x = 0 :=
begin
  obtain âŸ¨s, hsâŸ© := (is_compact_open_iff_eq_finset_affine_union U.1).mp âŸ¨hU, U.2âŸ©,
  replace hs : U = supr (Î» i : s, (i : opens X.carrier)),
  { ext1, simpa using hs, },
  have hâ‚ : âˆ€ i : s, i.1.1 â‰¤ U,
  { intro i, change (i : opens X.carrier) â‰¤ U, rw hs, exact le_supr _ _ },
  have H' := Î» (i : s), exists_power_mul_eq_zero_of_res_basic_open_eq_zero_of_is_affine_open X i.1.2
    (X.presheaf.map (hom_of_le (hâ‚ i)).op x) (X.presheaf.map (hom_of_le (hâ‚ i)).op f) _,
  swap,
  { convert congr_arg (X.presheaf.map (hom_of_le _).op) H,
    { simp only [â† comp_apply, â† functor.map_comp], congr },
    { rw map_zero },
    { rw X.basic_open_res, exact set.inter_subset_right _ _ } },
  choose n hn using H',
  use s.attach.sup n,
  suffices : âˆ€ (i : s), X.presheaf.map (hom_of_le (hâ‚ i)).op (f ^ (s.attach.sup n) * x) = 0,
  { subst hs,
    apply X.sheaf.eq_of_locally_eq (Î» (i : s), (i : opens X.carrier)),
    intro i,
    rw map_zero,
    apply this },
  intro i,
  replace hn := congr_arg
    (Î» x, X.presheaf.map (hom_of_le (hâ‚ i)).op (f ^ (s.attach.sup n - n i)) * x) (hn i),
  dsimp at hn,
  simp only [â† map_mul, â† map_pow] at hn,
  rwa [mul_zero, â† mul_assoc, â† pow_add, tsub_add_cancel_of_le] at hn,
  apply finset.le_sup (s.mem_attach i)
end

lemma supr_insert {Î± Î² : Type*} (x : Î±) (s : set Î±) (f : Î± â†’ Î²) [complete_lattice Î²] :
  (â¨† a : insert x s, f a) = (â¨† a : s, f a) âŠ” f x :=
begin
  apply le_antisymm,
  { suffices : âˆ€ (a : s), f a â‰¤ (â¨† a : s, f a) âŠ” f x,
    { simpa using this },
    intros a,
    exact le_trans (le_supr (Î» x : s, f x) a : _) le_sup_left },
  { simp only [supr_le_iff, set_coe.forall, sup_le_iff], split,
    { intros a ha, exact le_supr (Î» a : insert x s, f a) âŸ¨_, set.mem_insert_of_mem _ haâŸ© },
    { exact le_supr (Î» a : insert x s, f a) âŸ¨_, set.mem_insert _ _âŸ© } }
end

lemma supr_finset_insert {Î± Î² : Type*} (x : Î±) (s : finset Î±) (f : Î± â†’ Î²) [complete_lattice Î²]
  [decidable_eq Î±] : (â¨† a : insert x s, f a) = (â¨† a : s, f a) âŠ” f x :=
begin
  convert supr_insert x s f using 1,
  rw â† finset.coe_insert,
  refl
end

lemma compact_open_induction_on (P : opens X.carrier â†’ Prop)
  (hâ‚ : P âŠ¥)
  (hâ‚‚ : âˆ€ (S : opens X.carrier) (hS : is_compact S.1) (U : X.affine_opens), P S â†’ P (S âŠ” U)) :
  âˆ€ (S : opens X.carrier) (hS : is_compact S.1), P S :=
begin
  classical,
  intros S hS,
  obtain âŸ¨s, hsâŸ© := (is_compact_open_iff_eq_finset_affine_union S.1).mp âŸ¨hS, S.2âŸ©,
  replace hs : S = supr (Î» i : s, (i : opens X.carrier)) := by { ext1, simpa using hs },
  subst hs,
  induction s using finset.induction with x s hâ‚ƒ hâ‚„,
  { convert hâ‚, rw supr_eq_bot, rintro âŸ¨_, hâŸ©, exact h.elim },
  { have : is_compact (â¨† i : s, (i : opens X.carrier)).1,
    { refine ((is_compact_open_iff_eq_finset_affine_union _).mpr _).1, use s, simp },
    convert hâ‚‚ _ this x (hâ‚„ this), simp only [coe_coe], rw supr_finset_insert, refl }
end

end algebraic_geometry
