/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.morphisms.quasi_compact
import topology.quasi_separated

/-!
# Quasi-separated morphisms

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A morphism of schemes `f : X âŸ¶ Y` is quasi-separated if the diagonal morphism `X âŸ¶ X Ã—[Y] X` is
quasi-compact.

A scheme is quasi-separated if the intersections of any two affine open sets is quasi-compact.
(`algebraic_geometry.quasi_separated_space_iff_affine`)

We show that a morphism is quasi-separated if the preimage of every affine open is quasi-separated.

We also show that this property is local at the target,
and is stable under compositions and base-changes.

## Main result
- `is_localization_basic_open_of_qcqs` (**Qcqs lemma**):
  If `U` is qcqs, then `Î“(X, D(f)) â‰ƒ Î“(X, U)_f` for every `f : Î“(X, U)`.

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

open_locale algebraic_geometry

namespace algebraic_geometry

variables {X Y : Scheme.{u}} (f : X âŸ¶ Y)

/-- A morphism is `quasi_separated` if diagonal map is quasi-compact. -/
@[mk_iff]
class quasi_separated (f : X âŸ¶ Y) : Prop :=
(diagonal_quasi_compact : quasi_compact (pullback.diagonal f))

/-- The `affine_target_morphism_property` corresponding to `quasi_separated`, asserting that the
domain is a quasi-separated scheme. -/
def quasi_separated.affine_property : affine_target_morphism_property :=
(Î» X Y f _, quasi_separated_space X.carrier)

lemma quasi_separated_space_iff_affine (X : Scheme) :
  quasi_separated_space X.carrier â†” âˆ€ (U V : X.affine_opens), is_compact (U âˆ© V : set X.carrier) :=
begin
  rw quasi_separated_space_iff,
  split,
  { intros H U V, exact H U V U.1.2 U.2.is_compact V.1.2 V.2.is_compact },
  { intros H,
    suffices : âˆ€ (U : opens X.carrier) (hU : is_compact U.1) (V : opens X.carrier)
      (hV : is_compact V.1), is_compact (U âŠ“ V).1,
    { intros U V hU hU' hV hV', exact this âŸ¨U, hUâŸ© hU' âŸ¨V, hVâŸ© hV' },
    intros U hU V hV,
    apply compact_open_induction_on V hV,
    { simp },
    { intros S hS V hV,
      change is_compact (U.1 âˆ© (S.1 âˆª V.1)),
      rw set.inter_union_distrib_left,
      apply hV.union,
      clear hV,
      apply compact_open_induction_on U hU,
      { simp },
      { intros S hS W hW,
      change is_compact ((S.1 âˆª W.1) âˆ© V.1),
        rw set.union_inter_distrib_right,
        apply hW.union,
        apply H } } }
end

lemma quasi_compact_affine_property_iff_quasi_separated_space {X Y : Scheme} [is_affine Y]
  (f : X âŸ¶ Y) :
  quasi_compact.affine_property.diagonal f â†” quasi_separated_space X.carrier :=
begin
  delta affine_target_morphism_property.diagonal,
  rw quasi_separated_space_iff_affine,
  split,
  { intros H U V,
    haveI : is_affine _ := U.2,
    haveI : is_affine _ := V.2,
    let g : pullback (X.of_restrict U.1.open_embedding) (X.of_restrict V.1.open_embedding) âŸ¶ X :=
      pullback.fst â‰« X.of_restrict _,
    have : is_open_immersion g := infer_instance,
    have e := homeomorph.of_embedding _ this.base_open.to_embedding,
    rw is_open_immersion.range_pullback_to_base_of_left at e,
    erw [subtype.range_coe, subtype.range_coe] at e,
    rw is_compact_iff_compact_space,
    exact @@homeomorph.compact_space _ _ (H _ _) e },
  { introv H hâ‚ hâ‚‚,
    resetI,
    let g : pullback fâ‚ fâ‚‚ âŸ¶ X := pullback.fst â‰« fâ‚,
    have : is_open_immersion g := infer_instance,
    have e := homeomorph.of_embedding _ this.base_open.to_embedding,
    rw is_open_immersion.range_pullback_to_base_of_left at e,
    simp_rw is_compact_iff_compact_space at H,
    exact @@homeomorph.compact_space _ _
      (H âŸ¨âŸ¨_, hâ‚.base_open.open_rangeâŸ©, range_is_affine_open_of_open_immersion _âŸ©
        âŸ¨âŸ¨_, hâ‚‚.base_open.open_rangeâŸ©, range_is_affine_open_of_open_immersion _âŸ©) e.symm },
end

lemma quasi_separated_eq_diagonal_is_quasi_compact :
  @quasi_separated = morphism_property.diagonal @quasi_compact :=
by { ext, exact quasi_separated_iff _ }

lemma quasi_compact_affine_property_diagonal_eq :
  quasi_compact.affine_property.diagonal = quasi_separated.affine_property :=
by { ext, rw quasi_compact_affine_property_iff_quasi_separated_space, refl }

lemma quasi_separated_eq_affine_property_diagonal :
  @quasi_separated =
    target_affine_locally quasi_compact.affine_property.diagonal :=
begin
  rw [quasi_separated_eq_diagonal_is_quasi_compact, quasi_compact_eq_affine_property],
  exact diagonal_target_affine_locally_eq_target_affine_locally
    _ quasi_compact.affine_property_is_local
end

lemma quasi_separated_eq_affine_property :
  @quasi_separated =
    target_affine_locally quasi_separated.affine_property :=
by rw [quasi_separated_eq_affine_property_diagonal, quasi_compact_affine_property_diagonal_eq]

lemma quasi_separated.affine_property_is_local :
  quasi_separated.affine_property.is_local :=
quasi_compact_affine_property_diagonal_eq â–¸
quasi_compact.affine_property_is_local.diagonal

@[priority 900]
instance quasi_separated_of_mono {X Y : Scheme} (f : X âŸ¶ Y) [mono f] : quasi_separated f :=
âŸ¨infer_instanceâŸ©

lemma quasi_separated_stable_under_composition :
  morphism_property.stable_under_composition @quasi_separated :=
quasi_separated_eq_diagonal_is_quasi_compact.symm â–¸
  quasi_compact_stable_under_composition.diagonal
    quasi_compact_respects_iso
    quasi_compact_stable_under_base_change

lemma quasi_separated_stable_under_base_change :
  morphism_property.stable_under_base_change @quasi_separated :=
quasi_separated_eq_diagonal_is_quasi_compact.symm â–¸
  quasi_compact_stable_under_base_change.diagonal
    quasi_compact_respects_iso

instance quasi_separated_comp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [quasi_separated f] [quasi_separated g] : quasi_separated (f â‰« g) :=
quasi_separated_stable_under_composition f g infer_instance infer_instance

lemma quasi_separated_respects_iso : morphism_property.respects_iso @quasi_separated :=
quasi_separated_eq_diagonal_is_quasi_compact.symm â–¸
  quasi_compact_respects_iso.diagonal

lemma quasi_separated.affine_open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_separated f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)],
      âˆ€ (i : ğ’°.J), quasi_separated_space (pullback f (ğ’°.map i)).carrier,
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)] (i : ğ’°.J),
      quasi_separated_space (pullback f (ğ’°.map i)).carrier,
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_affine U] [is_open_immersion g],
      quasi_separated_space (pullback f g).carrier,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)]
      (ğ’°' : Î  (i : ğ’°.J), Scheme.open_cover.{u} (pullback f (ğ’°.map i)))
      [âˆ€ i j, is_affine ((ğ’°' i).obj j)], by exactI âˆ€ (i : ğ’°.J) (j k : (ğ’°' i).J),
        compact_space (pullback ((ğ’°' i).map j) ((ğ’°' i).map k)).carrier] :=
begin
  have := quasi_compact.affine_property_is_local.diagonal_affine_open_cover_tfae f,
  simp_rw [â† quasi_compact_eq_affine_property,
    â† quasi_separated_eq_diagonal_is_quasi_compact,
    quasi_compact_affine_property_diagonal_eq] at this,
  exact this
end

lemma quasi_separated.is_local_at_target :
  property_is_local_at_target @quasi_separated :=
quasi_separated_eq_affine_property_diagonal.symm â–¸
  quasi_compact.affine_property_is_local.diagonal.target_affine_locally_is_local

lemma quasi_separated.open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_separated f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y), âˆ€ (i : ğ’°.J),
      quasi_separated (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) (i : ğ’°.J),
      quasi_separated (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (U : opens Y.carrier), quasi_separated (f âˆ£_ U),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_open_immersion g],
      quasi_separated (pullback.snd : pullback f g âŸ¶ _),
    âˆƒ {Î¹ : Type u} (U : Î¹ â†’ opens Y.carrier) (hU : supr U = âŠ¤),
      âˆ€ i, quasi_separated (f âˆ£_ (U i))] :=
quasi_separated.is_local_at_target.open_cover_tfae f

lemma quasi_separated_over_affine_iff {X Y : Scheme} (f : X âŸ¶ Y) [is_affine Y] :
  quasi_separated f â†” quasi_separated_space X.carrier :=
by rw [quasi_separated_eq_affine_property,
  quasi_separated.affine_property_is_local.affine_target_iff f,
  quasi_separated.affine_property]

lemma quasi_separated_space_iff_quasi_separated (X : Scheme) :
  quasi_separated_space X.carrier â†” quasi_separated (terminal.from X) :=
(quasi_separated_over_affine_iff _).symm

lemma quasi_separated.affine_open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y)
  [âˆ€ i, is_affine (ğ’°.obj i)] (f : X âŸ¶ Y) :
  quasi_separated f â†” âˆ€ i, quasi_separated_space (pullback f (ğ’°.map i)).carrier :=
begin
  rw [quasi_separated_eq_affine_property,
    quasi_separated.affine_property_is_local.affine_open_cover_iff f ğ’°],
  refl,
end

lemma quasi_separated.open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y)
  (f : X âŸ¶ Y) :
  quasi_separated f â†” âˆ€ i, quasi_separated (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) :=
quasi_separated.is_local_at_target.open_cover_iff f ğ’°

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [quasi_separated g] :
  quasi_separated (pullback.fst : pullback f g âŸ¶ X) :=
quasi_separated_stable_under_base_change.fst f g infer_instance

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [quasi_separated f] :
  quasi_separated (pullback.snd : pullback f g âŸ¶ Y) :=
quasi_separated_stable_under_base_change.snd f g infer_instance

instance {X Y Z: Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [quasi_separated f] [quasi_separated g] :
  quasi_separated (f â‰« g) :=
quasi_separated_stable_under_composition f g infer_instance infer_instance

lemma quasi_separated_space_of_quasi_separated {X Y : Scheme} (f : X âŸ¶ Y)
  [hY : quasi_separated_space Y.carrier] [quasi_separated f] : quasi_separated_space X.carrier :=
begin
  rw quasi_separated_space_iff_quasi_separated at hY âŠ¢,
  have : f â‰« terminal.from Y = terminal.from X := terminal_is_terminal.hom_ext _ _,
  rw â† this,
  resetI, apply_instance
end

instance quasi_separated_space_of_is_affine (X : Scheme) [is_affine X] :
  quasi_separated_space X.carrier :=
begin
  constructor,
  intros U V hU hU' hV hV',
  obtain âŸ¨s, hs, eâŸ© := (is_compact_open_iff_eq_basic_open_union _).mp âŸ¨hU', hUâŸ©,
  obtain âŸ¨s', hs', e'âŸ© := (is_compact_open_iff_eq_basic_open_union _).mp âŸ¨hV', hVâŸ©,
  rw [e, e', set.Unionâ‚‚_inter],
  simp_rw [set.inter_Unionâ‚‚],
  apply hs.is_compact_bUnion,
  { intros i hi,
    apply hs'.is_compact_bUnion,
    intros i' hi',
    change is_compact (X.basic_open i âŠ“ X.basic_open i').1,
    rw â† Scheme.basic_open_mul,
    exact ((top_is_affine_open _).basic_open_is_affine _).is_compact }
end

lemma is_affine_open.is_quasi_separated {X : Scheme} {U : opens X.carrier} (hU : is_affine_open U) :
  is_quasi_separated (U : set X.carrier)  :=
begin
  rw is_quasi_separated_iff_quasi_separated_space,
  exacts [@@algebraic_geometry.quasi_separated_space_of_is_affine _ hU, U.is_open],
end

lemma quasi_separated_of_comp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [H : quasi_separated (f â‰« g)] : quasi_separated f :=
begin
  rw (quasi_separated.affine_open_cover_tfae f).out 0 1,
  rw (quasi_separated.affine_open_cover_tfae (f â‰« g)).out 0 2 at H,
  use (Z.affine_cover.pullback_cover g).bind (Î» x, Scheme.affine_cover _),
  split, { intro i, dsimp, apply_instance },
  rintro âŸ¨i, jâŸ©, dsimp at *,
  specialize H _ i,
  refine @@quasi_separated_space_of_quasi_separated _ H _,
  { exact pullback.map _ _ _ _ (ğŸ™ _) _ _ (by simp) (category.comp_id _) â‰«
      (pullback_right_pullback_fst_iso g (Z.affine_cover.map i) f).hom },
  { apply algebraic_geometry.quasi_separated_of_mono }
end

lemma exists_eq_pow_mul_of_is_affine_open (X : Scheme) (U : opens X.carrier) (hU : is_affine_open U)
  (f : X.presheaf.obj (op U)) (x : X.presheaf.obj (op $ X.basic_open f)) :
  âˆƒ (n : â„•) (y : X.presheaf.obj (op U)),
    y |_ X.basic_open f = (f |_ X.basic_open f) ^ n * x :=
begin
  have := (is_localization_basic_open hU f).2,
  obtain âŸ¨âŸ¨y, _, n, rflâŸ©, dâŸ© := this x,
  use [n, y],
  delta Top.presheaf.restrict_open Top.presheaf.restrict,
  simpa [mul_comm x] using d.symm,
end

lemma exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux (X : Scheme)
  (S : X.affine_opens) (Uâ‚ Uâ‚‚ : opens X.carrier)
  {nâ‚ nâ‚‚ : â„•} {yâ‚ : X.presheaf.obj (op Uâ‚)}
  {yâ‚‚ : X.presheaf.obj (op Uâ‚‚)} {f : X.presheaf.obj (op $ Uâ‚ âŠ” Uâ‚‚)}
  {x : X.presheaf.obj (op $ X.basic_open f)}
  (hâ‚ : S.1 â‰¤ Uâ‚) (hâ‚‚ : S.1 â‰¤ Uâ‚‚)
  (eâ‚ : X.presheaf.map (hom_of_le $ X.basic_open_le
    (X.presheaf.map (hom_of_le le_sup_left).op f) : _ âŸ¶ Uâ‚).op yâ‚ =
      X.presheaf.map (hom_of_le (by { erw X.basic_open_res, exact inf_le_left })).op
        (X.presheaf.map (hom_of_le le_sup_left).op f) ^ nâ‚ *
      (X.presheaf.map (hom_of_le (by { erw X.basic_open_res, exact inf_le_right })).op) x)
  (eâ‚‚ : X.presheaf.map (hom_of_le $ X.basic_open_le
    (X.presheaf.map (hom_of_le le_sup_right).op f) : _ âŸ¶ Uâ‚‚).op yâ‚‚ =
      X.presheaf.map (hom_of_le (by { rw X.basic_open_res, exact inf_le_left })).op
        (X.presheaf.map (hom_of_le le_sup_right).op f) ^ nâ‚‚ *
      (X.presheaf.map (hom_of_le (by { rw X.basic_open_res, exact inf_le_right })).op) x) :
  âˆƒ n : â„•, X.presheaf.map (hom_of_le $ hâ‚).op
    ((X.presheaf.map (hom_of_le le_sup_left).op f) ^ (n + nâ‚‚) * yâ‚) =
    X.presheaf.map (hom_of_le $ hâ‚‚).op
      ((X.presheaf.map (hom_of_le le_sup_right).op f) ^ (n + nâ‚) * yâ‚‚) :=
begin
  have := (is_localization_basic_open S.2
    (X.presheaf.map (hom_of_le $ le_trans hâ‚ le_sup_left).op f)),
  obtain âŸ¨âŸ¨_, n, rflâŸ©, eâŸ© :=
    (@is_localization.eq_iff_exists _ _ _ _ _ _ this (X.presheaf.map (hom_of_le $ hâ‚).op
      ((X.presheaf.map (hom_of_le le_sup_left).op f) ^ nâ‚‚ * yâ‚))
    (X.presheaf.map (hom_of_le $ hâ‚‚).op
      ((X.presheaf.map (hom_of_le le_sup_right).op f) ^ nâ‚ * yâ‚‚))).mp _,
  swap,
  { simp only [map_pow, ring_hom.algebra_map_to_algebra, map_mul, â† comp_apply,
      â† functor.map_comp, â† op_comp, hom_of_le_comp],
    have hâ‚ƒ : X.basic_open ((X.presheaf.map (hom_of_le (hâ‚.trans le_sup_left)).op) f) â‰¤ S.val,
    { simpa only [X.basic_open_res] using inf_le_left, },
    transitivity
      X.presheaf.map (hom_of_le $ hâ‚ƒ.trans $ hâ‚.trans le_sup_left).op f ^ (nâ‚‚ + nâ‚) *
      X.presheaf.map (hom_of_le $ (X.basic_open_res f _).trans_le inf_le_right).op x,
    { rw [pow_add, mul_assoc], congr' 1,
      convert congr_arg (X.presheaf.map (hom_of_le _).op) eâ‚,
      { simp only [map_pow, map_mul, â† comp_apply, â† functor.map_comp, â† op_comp], congr },
      { simp only [map_pow, map_mul, â† comp_apply, â† functor.map_comp, â† op_comp], congr },
      { rw [X.basic_open_res, X.basic_open_res], rintros x âŸ¨Hâ‚, Hâ‚‚âŸ©, exact âŸ¨hâ‚ Hâ‚, Hâ‚‚âŸ© } },
    { rw [add_comm, pow_add, mul_assoc], congr' 1,
      convert congr_arg (X.presheaf.map (hom_of_le _).op) eâ‚‚.symm,
      { simp only [map_pow, map_mul, â† comp_apply, â† functor.map_comp, â† op_comp], congr },
      { simp only [map_pow, map_mul, â† comp_apply, â† functor.map_comp, â† op_comp], congr },
      { simp only [X.basic_open_res],
        rintros x âŸ¨Hâ‚, Hâ‚‚âŸ©, exact âŸ¨hâ‚‚ Hâ‚, Hâ‚‚âŸ© } } },
  use n,
  simp only [pow_add, map_pow, map_mul, â† comp_apply, â† mul_assoc,
    â† functor.map_comp, subtype.coe_mk] at e âŠ¢,
  exact e
end

lemma exists_eq_pow_mul_of_is_compact_of_is_quasi_separated (X : Scheme)
  (U : opens X.carrier) (hU : is_compact U.1) (hU' : is_quasi_separated U.1)
  (f : X.presheaf.obj (op U)) (x : X.presheaf.obj (op $ X.basic_open f)) :
  âˆƒ (n : â„•) (y : X.presheaf.obj (op U)), y |_ X.basic_open f = (f |_ X.basic_open f) ^ n * x :=
begin
  delta Top.presheaf.restrict_open Top.presheaf.restrict,
  revert hU' f x,
  apply compact_open_induction_on U hU,
  { intros hU' f x,
    use [0, f],
    refine @@subsingleton.elim (CommRing.subsingleton_of_is_terminal
      (X.sheaf.is_terminal_of_eq_empty _)) _ _,
    erw eq_bot_iff,
    exact X.basic_open_le f },
  { -- Given `f : ğ’ª(S âˆª U), x : ğ’ª(X_f)`, we need to show that `f ^ n * x` is the restriction of
    -- some `y : ğ’ª(S âˆª U)` for some `n : â„•`.
    intros S hS U hU hSU f x,
    -- We know that such `yâ‚, nâ‚` exists on `S` by the induction hypothesis.
    obtain âŸ¨nâ‚, yâ‚, hyâ‚âŸ© := hU (hSU.of_subset $ set.subset_union_left _ _)
      (X.presheaf.map (hom_of_le le_sup_left).op f) (X.presheaf.map (hom_of_le _).op x),
    swap, { rw X.basic_open_res, exact inf_le_right },
    -- We know that such `yâ‚‚, nâ‚‚` exists on `U` since `U` is affine.
    obtain âŸ¨nâ‚‚, yâ‚‚, hyâ‚‚âŸ© := exists_eq_pow_mul_of_is_affine_open X _ U.2
      (X.presheaf.map (hom_of_le le_sup_right).op f) (X.presheaf.map (hom_of_le _).op x),
    delta Top.presheaf.restrict_open Top.presheaf.restrict at hyâ‚‚,
    swap, { rw X.basic_open_res, exact inf_le_right },
    -- Since `S âˆª U` is quasi-separated, `S âˆ© U` can be covered by finite affine opens.
    obtain âŸ¨s, hs', hsâŸ© := (is_compact_open_iff_eq_finset_affine_union _).mp
      âŸ¨hSU _ _ (set.subset_union_left _ _) S.2 hS
        (set.subset_union_right _ _) U.1.2 U.2.is_compact, (S âŠ“ U.1).2âŸ©,
    haveI := hs'.to_subtype,
    casesI nonempty_fintype s,
    replace hs : S âŠ“ U.1 = supr (Î» i : s, (i : opens X.carrier)) := by { ext1, simpa using hs },
    have hsâ‚ : âˆ€ i : s, i.1.1 â‰¤ S,
    { intro i, change (i : opens X.carrier) â‰¤ S,
      refine le_trans _ inf_le_left, use U.1, erw hs, exact le_supr _ _ },
    have hsâ‚‚ : âˆ€ i : s, i.1.1 â‰¤ U.1,
    { intro i, change (i : opens X.carrier) â‰¤ U,
      refine le_trans _ inf_le_right, use S, erw hs, exact le_supr _ _ },
    -- On each affine open in the intersection, we have `f ^ (n + nâ‚‚) * yâ‚ = f ^ (n + nâ‚) * yâ‚‚`
    -- for some `n` since `f ^ nâ‚‚ * yâ‚ = f ^ (nâ‚ + nâ‚‚) * x = f ^ nâ‚ * yâ‚‚` on `X_f`.
    have : âˆ€ i : s, âˆƒ n : â„•,
      X.presheaf.map (hom_of_le $ hsâ‚ i).op
        ((X.presheaf.map (hom_of_le le_sup_left).op f) ^ (n + nâ‚‚) * yâ‚) =
      X.presheaf.map (hom_of_le $ hsâ‚‚ i).op
        ((X.presheaf.map (hom_of_le le_sup_right).op f) ^ (n + nâ‚) * yâ‚‚),
    { intro i,
      exact exists_eq_pow_mul_of_is_compact_of_quasi_separated_space_aux X i.1 S U (hsâ‚ i) (hsâ‚‚ i)
        hyâ‚ hyâ‚‚ },
    choose n hn using this,
    -- We can thus choose a big enough `n` such that `f ^ (n + nâ‚‚) * yâ‚ = f ^ (n + nâ‚) * yâ‚‚`
    -- on `S âˆ© U`.
    have : X.presheaf.map (hom_of_le $ inf_le_left).op
      ((X.presheaf.map (hom_of_le le_sup_left).op f) ^ (finset.univ.sup n + nâ‚‚) * yâ‚) =
        X.presheaf.map (hom_of_le $ inf_le_right).op
          ((X.presheaf.map (hom_of_le le_sup_right).op f) ^ (finset.univ.sup n + nâ‚) * yâ‚‚),
    { fapply X.sheaf.eq_of_locally_eq' (Î» i : s, i.1.1),
      { refine Î» i, hom_of_le _, erw hs, exact le_supr _ _ },
      { exact le_of_eq hs },
      { intro i,
        replace hn := congr_arg (Î» x, X.presheaf.map (hom_of_le
          (le_trans (hsâ‚ i) le_sup_left)).op f ^ (finset.univ.sup n - n i) * x) (hn i),
        dsimp only at hn,
        delta Scheme.sheaf SheafedSpace.sheaf,
        simp only [â† map_pow, map_mul, â† comp_apply, â† functor.map_comp, â† op_comp, â† mul_assoc]
          at hn âŠ¢,
        erw [â† map_mul, â† map_mul] at hn,
        rw [â† pow_add, â† pow_add, â† add_assoc, â† add_assoc, tsub_add_cancel_of_le] at hn,
        convert hn,
        exact finset.le_sup (finset.mem_univ _) } },
    use finset.univ.sup n + nâ‚ + nâ‚‚,
    -- By the sheaf condition, since `f ^ (n + nâ‚‚) * yâ‚ = f ^ (n + nâ‚) * yâ‚‚`, it can be glued into
    -- the desired section on `S âˆª U`.
    use (X.sheaf.obj_sup_iso_prod_eq_locus S U.1).inv âŸ¨âŸ¨_ * _, _ * _âŸ©, thisâŸ©,
    refine X.sheaf.eq_of_locally_eqâ‚‚
      (hom_of_le (_ : X.basic_open (X.presheaf.map (hom_of_le le_sup_left).op f) â‰¤ _))
      (hom_of_le (_ : X.basic_open (X.presheaf.map (hom_of_le le_sup_right).op f) â‰¤ _)) _ _ _ _ _,
    { rw X.basic_open_res, exact inf_le_right },
    { rw X.basic_open_res, exact inf_le_right },
    { rw [X.basic_open_res, X.basic_open_res],
      erw â† inf_sup_right,
      refine le_inf_iff.mpr âŸ¨X.basic_open_le f, le_of_eq rflâŸ© },
    { convert congr_arg (X.presheaf.map (hom_of_le _).op)
        (X.sheaf.obj_sup_iso_prod_eq_locus_inv_fst S U.1 âŸ¨âŸ¨_ * _, _ * _âŸ©, thisâŸ©) using 1,
      { delta Scheme.sheaf SheafedSpace.sheaf,
        simp only [â† comp_apply (X.presheaf.map _) (X.presheaf.map _),
          â† functor.map_comp, â† op_comp],
        congr },
      { delta Scheme.sheaf SheafedSpace.sheaf,
        simp only [map_pow, map_mul, â† comp_apply, â† functor.map_comp, â† op_comp, mul_assoc,
          pow_add], erw hyâ‚, congr' 1, rw [â† mul_assoc, â† mul_assoc], congr' 1,
        rw [mul_comm, â† comp_apply, â† functor.map_comp], congr } },
    { convert congr_arg (X.presheaf.map (hom_of_le _).op)
        (X.sheaf.obj_sup_iso_prod_eq_locus_inv_snd S U.1 âŸ¨âŸ¨_ * _, _ * _âŸ©, thisâŸ©) using 1,
      { delta Scheme.sheaf SheafedSpace.sheaf,
        simp only [â† comp_apply (X.presheaf.map _) (X.presheaf.map _),
          â† functor.map_comp, â† op_comp],
        congr },
      { delta Scheme.sheaf SheafedSpace.sheaf,
        simp only [map_pow, map_mul, â† comp_apply, â† functor.map_comp, â† op_comp, mul_assoc,
          pow_add], erw hyâ‚‚, rw [â† comp_apply, â† functor.map_comp], congr } } }
end

/-- If `U` is qcqs, then `Î“(X, D(f)) â‰ƒ Î“(X, U)_f` for every `f : Î“(X, U)`.
This is known as the **Qcqs lemma** in [R. Vakil, *The rising sea*][RisingSea]. -/
lemma is_localization_basic_open_of_qcqs {X : Scheme} {U : opens X.carrier}
  (hU : is_compact U.1) (hU' : is_quasi_separated U.1)
  (f : X.presheaf.obj (op U)) :
  is_localization.away f (X.presheaf.obj (op $ X.basic_open f)) :=
begin
  constructor,
  { rintro âŸ¨_, n, rflâŸ©,
    simp only [map_pow, subtype.coe_mk, ring_hom.algebra_map_to_algebra],
    exact is_unit.pow _ (RingedSpace.is_unit_res_basic_open _ f), },
  { intro z,
    obtain âŸ¨n, y, eâŸ© := exists_eq_pow_mul_of_is_compact_of_is_quasi_separated X U hU hU' f z,
    refine âŸ¨âŸ¨y, _, n, rflâŸ©, _âŸ©,
    simpa only [map_pow, subtype.coe_mk, ring_hom.algebra_map_to_algebra, mul_comm z]
      using e.symm },
  { intros x y,
    rw [â† sub_eq_zero, â† map_sub, ring_hom.algebra_map_to_algebra],
    simp_rw [â† @sub_eq_zero _ _ (_ * x) (_ * y), â† mul_sub],
    generalize : x - y = z,
    split,
    { intro H,
      obtain âŸ¨n, eâŸ© := exists_pow_mul_eq_zero_of_res_basic_open_eq_zero_of_is_compact X hU _ _ H,
      refine âŸ¨âŸ¨_, n, rflâŸ©, _âŸ©,
      simpa [mul_comm z] using e },
    { rintro âŸ¨âŸ¨_, n, rflâŸ©, e : f ^ n * z = 0âŸ©,
      rw [â† ((RingedSpace.is_unit_res_basic_open _ f).pow n).mul_right_inj, mul_zero, â† map_pow,
        â† map_mul, e, map_zero] } }
end

end algebraic_geometry
