/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.morphisms.quasi_compact

/-!
# Quasi-separated morphisms

A morphism of schemes `f : X âŸ¶ Y` is quasi-separated if the diagonal morphism `X âŸ¶ X Ã—[Y] X` is
quasi-compact.

A scheme is quasi-separated if the intersections of any two affine open sets is quasi-compact.

We show that a morphism is quasi-separated if the preimage of every affine open is quasi-separated.

We also show that this property is local, and is stable under compositions and base-changes.

-/

noncomputable theory

open category_theory category_theory.limits opposite topological_space

universe u

namespace algebraic_geometry

variables {X Y : Scheme.{u}} (f : X âŸ¶ Y)

/--
A morphism is `quasi-compact` if the underlying map of topological spaces is, i.e. if the preimages
of quasi-compact open sets are quasi-compact.
-/
@[mk_iff]
class quasi_separated (f : X âŸ¶ Y) : Prop :=
(diagonal_quasi_compact : quasi_compact (pullback.diagonal f))

@[mk_iff]
class is_quasi_separated (X : Scheme) : Prop :=
(inter_is_compact : âˆ€ (U V : X.affine_opens), is_compact (U âˆ© V : set X.carrier) )

lemma quasi_compact_affine_property_iff_is_quasi_separated {X Y : Scheme} [is_affine Y]
  (f : X âŸ¶ Y) :
  diagonal_is.affine_property quasi_compact.affine_property f â†” is_quasi_separated X :=
begin
  delta diagonal_is.affine_property,
  rw is_quasi_separated_iff,
  split,
  { intros H U V,
    haveI : is_affine _ := U.2,
    haveI : is_affine _ := V.2,
    let g : pullback (X.of_restrict U.1.open_embedding) (X.of_restrict V.1.open_embedding) âŸ¶ X :=
      pullback.fst â‰« X.of_restrict _,
    have : is_open_immersion g := infer_instance,
    have e := homeomorph.of_embedding _ this.base_open.to_embedding,
    rw is_open_immersion.range_pullback_one at e,
    erw [subtype.range_coe, subtype.range_coe] at e,
    rw is_compact_iff_compact_space,
    exact @@homeomorph.compact_space _ _ (H _ _) e },
  { introv H hâ‚ hâ‚‚,
    resetI,
    let g : pullback fâ‚ fâ‚‚ âŸ¶ X := pullback.fst â‰« fâ‚,
    have : is_open_immersion g := infer_instance,
    have e := homeomorph.of_embedding _ this.base_open.to_embedding,
    rw is_open_immersion.range_pullback_one at e,
    simp_rw is_compact_iff_compact_space at H,
    exact @@homeomorph.compact_space _ _
      (H âŸ¨âŸ¨_, hâ‚.base_open.open_rangeâŸ©, range_is_affine_open_of_open_immersion _âŸ©
        âŸ¨âŸ¨_, hâ‚‚.base_open.open_rangeâŸ©, range_is_affine_open_of_open_immersion _âŸ©) e.symm },
end

lemma quasi_separated_eq_diagonal_is_quasi_compact :
  @quasi_separated = diagonal_is @quasi_compact :=
by { ext, exact quasi_separated_iff _ }

lemma quasi_compact_affine_property_eq_is_quasi_separated :
  diagonal_is.affine_property quasi_compact.affine_property =
    (Î» X Y f _, is_quasi_separated X) :=
by { ext, rw quasi_compact_affine_property_iff_is_quasi_separated }

lemma quasi_separated_eq_affine_property :
  @quasi_separated =
    target_affine_locally (diagonal_is.affine_property quasi_compact.affine_property) :=
begin
  rw [quasi_separated_eq_diagonal_is_quasi_compact, quasi_compact_eq_affine_property],
  exact (diagonal_is_eq_diagonal_is_affine_property _ quasi_compact_affine_property_is_local)
end

lemma quasi_separated_affine_property_is_local :
  (diagonal_is.affine_property quasi_compact.affine_property).is_local :=
diagonal_is_affine_property.is_local _ quasi_compact_affine_property_is_local

@[priority 900]
instance quasi_separated_of_mono {X Y : Scheme} (f : X âŸ¶ Y) [mono f] : quasi_separated f :=
âŸ¨infer_instanceâŸ©

lemma quasi_separated_stable_under_composition :
  stable_under_composition @quasi_separated :=
quasi_separated_eq_diagonal_is_quasi_compact.symm â–¸
  diagonal_is_stable_under_composition @quasi_compact
    quasi_compact_stable_under_base_change
    quasi_compact_respects_iso
    quasi_compact_stable_under_composition

lemma quasi_separated_stable_under_base_change :
  stable_under_base_change @quasi_separated :=
quasi_separated_eq_diagonal_is_quasi_compact.symm â–¸
  diagonal_is_stable_under_base_change @quasi_compact
    quasi_compact_stable_under_base_change
    quasi_compact_respects_iso

instance quasi_separated_comp {X Y Z : Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [quasi_separated f] [quasi_separated g] : quasi_separated (f â‰« g) :=
quasi_separated_stable_under_composition f g infer_instance infer_instance

lemma quasi_separated_respects_iso : respects_iso @quasi_separated :=
quasi_separated_eq_diagonal_is_quasi_compact.symm â–¸
  diagonal_is_respects_iso @quasi_compact quasi_compact_respects_iso

lemma quasi_separated.affine_open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_separated f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)],
      âˆ€ (i : ğ’°.J), is_quasi_separated (pullback f (ğ’°.map i)),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) [âˆ€ i, is_affine (ğ’°.obj i)] (i : ğ’°.J),
      is_quasi_separated (pullback f (ğ’°.map i)),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_affine U] [is_open_immersion g],
      is_quasi_separated (pullback f g)] :=
begin
  have := quasi_separated_affine_property_is_local,
  rw quasi_separated_eq_affine_property,
  rw quasi_compact_affine_property_eq_is_quasi_separated at this âŠ¢,
  exact this.affine_open_cover_tfae f
end

lemma quasi_separated.open_cover_tfae {X Y : Scheme.{u}} (f : X âŸ¶ Y) :
  tfae [quasi_separated f,
    âˆƒ (ğ’° : Scheme.open_cover.{u} Y), âˆ€ (i : ğ’°.J),
      quasi_separated (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (ğ’° : Scheme.open_cover.{u} Y) (i : ğ’°.J),
      quasi_separated (pullback.snd : (ğ’°.pullback_cover f).obj i âŸ¶ ğ’°.obj i),
    âˆ€ (U : opens Y.carrier), quasi_separated (f âˆ£_ U),
    âˆ€ {U : Scheme} (g : U âŸ¶ Y) [is_open_immersion g],
      quasi_separated (pullback.snd : pullback f g âŸ¶ _)] :=
quasi_separated_eq_affine_property.symm â–¸
  quasi_separated_affine_property_is_local.open_cover_tfae f

lemma quasi_separated_over_affine_iff {X Y : Scheme} (f : X âŸ¶ Y) [is_affine Y] :
  quasi_separated f â†” is_quasi_separated X :=
by rw [quasi_separated_eq_affine_property,
  quasi_separated_affine_property_is_local.affine_target_iff f,
  quasi_compact_affine_property_eq_is_quasi_separated]

lemma compact_space_iff_quasi_separated (X : Scheme) :
  is_quasi_separated X â†” quasi_separated (terminal.from X) :=
(quasi_separated_over_affine_iff _).symm

lemma quasi_separated.affine_open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y)
  [âˆ€ i, is_affine (ğ’°.obj i)] (f : X âŸ¶ Y) :
  quasi_separated f â†” âˆ€ i, is_quasi_separated (pullback f (ğ’°.map i)) :=
begin
  rw [quasi_separated_eq_affine_property,
    quasi_separated_affine_property_is_local.affine_open_cover_iff f ğ’°],
  simp_rw quasi_compact_affine_property_eq_is_quasi_separated,
end

lemma quasi_separated.open_cover_iff {X Y : Scheme.{u}} (ğ’° : Scheme.open_cover.{u} Y)
  [âˆ€ i, is_affine (ğ’°.obj i)] (f : X âŸ¶ Y) :
  quasi_separated f â†” âˆ€ i, quasi_separated (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _) :=
quasi_separated_eq_affine_property.symm â–¸
  quasi_separated_affine_property_is_local.open_cover_iff f ğ’°

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [quasi_separated g] :
  quasi_separated (pullback.fst : pullback f g âŸ¶ X) :=
quasi_separated_stable_under_base_change f g infer_instance

instance {X Y S : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) [quasi_separated f] :
  quasi_separated (pullback.snd : pullback f g âŸ¶ Y) :=
quasi_separated_stable_under_base_change.symmetry quasi_separated_respects_iso f g infer_instance

instance {X Y Z: Scheme} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [quasi_separated f] [quasi_separated g] :
  quasi_separated (f â‰« g) :=
quasi_separated_stable_under_composition f g infer_instance infer_instance


end algebraic_geometry
