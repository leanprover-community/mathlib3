/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import topology.gluing
import algebraic_geometry.open_immersion
import algebraic_geometry.locally_ringed_space.has_colimits

/-!
# Gluing Structured spaces

Given a family of gluing data of structured spaces (presheafed spaces, sheafed spaces, or locally
ringed spaces), we may glue them together.

The construction should be "sealed" and considered as a black box, while only using the API
provided.

## Main definitions

* `algebraic_geometry.PresheafedSpace.glue_data`: A structure containing the family of gluing data.
* `category_theory.glue_data.glued`: The glued presheafed space.
    This is defined as the multicoequalizer of `âˆ V i j â‡‰ âˆ U i`, so that the general colimit API
    can be used.
* `category_theory.glue_data.Î¹`: The immersion `Î¹ i : U i âŸ¶ glued` for each `i : J`.

## Main results

* `algebraic_geometry.PresheafedSpace.glue_data.Î¹_is_open_immersion`: The map `Î¹ i : U i âŸ¶ glued`
  is an open immersion for each `i : J`.
* `algebraic_geometry.PresheafedSpace.glue_data.Î¹_jointly_surjective` : The underlying maps of
  `Î¹ i : U i âŸ¶ glued` are jointly surjective.
* `algebraic_geometry.PresheafedSpace.glue_data.V_pullback_cone_is_limit` : `V i j` is the pullback
  (intersection) of `U i` and `U j` over the glued space.

Analogous results are also provided for `SheafedSpace` and `LocallyRingedSpace`.

## Implementation details

Almost the whole file is dedicated to showing tht `Î¹ i` is an open immersion. The fact that
this is an open embedding of topological spaces follows from `topology.gluing.lean`, and it remains
to construct `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_X, Î¹ i '' U)` for each `U âŠ† U i`.
Since `Î“(ğ’ª_X, Î¹ i '' U)` is the the limit of `diagram_over_open`, the components of the structure
sheafs of the spaces in the gluing diagram, we need to construct a map
`Î¹_inv_app_Ï€_app : Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_V, U_V)` for each `V` in the gluing diagram.

We will refer to ![this diagram](https://i.imgur.com/P0phrwr.png) in the following doc strings.
The `X` is the glued space, and the dotted arrow is a partial inverse guaranteed by the fact
that it is an open immersion. The map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_{U_j}, _)` is given by the composition
of the red arrows, and the map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_{V_{jk}}, _)` is given by the composition of the
blue arrows. To lift this into a map from `Î“(ğ’ª_X, Î¹ i '' U)`, we also need to show that these
commute with the maps in the diagram (the green arrows), which is just a lengthy diagram-chasing.

-/

noncomputable theory

open topological_space category_theory opposite
open category_theory.limits algebraic_geometry.PresheafedSpace
open category_theory.glue_data

namespace algebraic_geometry

universes v u

variables (C : Type u) [category.{v} C]

namespace PresheafedSpace


/--
A family of gluing data consists of
1. An index type `J`
2. A presheafed space `U i` for each `i : J`.
3. A presheafed space `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ PresheafedSpace C` rather than `J â†’ J â†’ PresheafedSpace C` to
  connect to the limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.
-/
@[nolint has_nonempty_instance]
structure glue_data extends glue_data (PresheafedSpace.{v} C) :=
(f_open : âˆ€ i j, is_open_immersion (f i j))

attribute [instance] glue_data.f_open

namespace glue_data

variables {C} (D : glue_data C)

local notation `ğ–£` := D.to_glue_data
local notation `Ï€â‚ `i`, `j`, `k := @pullback.fst _ _ _ _ _ (D.f i j) (D.f i k) _
local notation `Ï€â‚‚ `i`, `j`, `k := @pullback.snd _ _ _ _ _ (D.f i j) (D.f i k) _
local notation `Ï€â‚â»Â¹ `i`, `j`, `k :=
(PresheafedSpace.is_open_immersion.pullback_fst_of_right (D.f i j) (D.f i k)).inv_app
local notation `Ï€â‚‚â»Â¹ `i`, `j`, `k :=
(PresheafedSpace.is_open_immersion.pullback_snd_of_left (D.f i j) (D.f i k)).inv_app

/-- The glue data of topological spaces associated to a family of glue data of PresheafedSpaces. -/
abbreviation to_Top_glue_data : Top.glue_data :=
{ f_open := Î» i j, (D.f_open i j).base_open,
  to_glue_data := ğ–£ .map_glue_data (forget C) }

lemma Î¹_open_embedding [has_limits C] (i : D.J) : open_embedding (ğ–£ .Î¹ i).base :=
begin
  rw â† (show _ = (ğ–£ .Î¹ i).base, from ğ–£ .Î¹_glued_iso_inv (PresheafedSpace.forget _) _),
  exact open_embedding.comp (Top.homeo_of_iso
    (ğ–£ .glued_iso (PresheafedSpace.forget _)).symm).open_embedding
    (D.to_Top_glue_data.Î¹_open_embedding i)
end

lemma pullback_base (i j k : D.J)  (S : set (D.V (i, j)).carrier) :
  (Ï€â‚‚ i, j, k) '' ((Ï€â‚ i, j, k) â»Â¹' S) = D.f i k â»Â¹' (D.f i j '' S) :=
begin
  have eqâ‚ : _ = (Ï€â‚ i, j, k).base := preserves_pullback.iso_hom_fst (forget C) _ _,
  have eqâ‚‚ : _ = (Ï€â‚‚ i, j, k).base := preserves_pullback.iso_hom_snd (forget C) _ _,
  rw [coe_to_fun_eq, coe_to_fun_eq, â† eqâ‚, â† eqâ‚‚, coe_comp, set.image_comp, coe_comp,
    set.preimage_comp, set.image_preimage_eq, Top.pullback_snd_image_fst_preimage],
  refl,
  rw â† Top.epi_iff_surjective,
  apply_instance
end

/-- The red and the blue arrows in ![this diagram](https://i.imgur.com/0GiBUh6.png) commute. -/
@[simp, reassoc]
lemma f_inv_app_f_app (i j k : D.J)  (U : (opens (D.V (i, j)).carrier)) :
  (D.f_open i j).inv_app U â‰« (D.f i k).c.app _ =
    (Ï€â‚ i, j, k).c.app (op U) â‰« (Ï€â‚‚â»Â¹ i, j, k) (unop _) â‰« (D.V _).presheaf.map (eq_to_hom
      (begin
        delta is_open_immersion.open_functor,
        dsimp only [functor.op, is_open_map.functor, opens.map, unop_op],
        congr,
        apply pullback_base,
      end)) :=
begin
  have := PresheafedSpace.congr_app (@pullback.condition _ _ _ _ _ (D.f i j) (D.f i k) _),
  dsimp only [comp_c_app] at this,
  rw [â† cancel_epi (inv ((D.f_open i j).inv_app U)), is_iso.inv_hom_id_assoc,
    is_open_immersion.inv_inv_app],
  simp_rw category.assoc,
  erw [(Ï€â‚ i, j, k).c.naturality_assoc,
    reassoc_of this, â† functor.map_comp_assoc, is_open_immersion.inv_naturality_assoc,
    is_open_immersion.app_inv_app_assoc, â† (D.V (i, k)).presheaf.map_comp,
    â† (D.V (i, k)).presheaf.map_comp],
  convert (category.comp_id _).symm,
  erw (D.V (i, k)).presheaf.map_id,
  refl
end

/--
We can prove the `eq` along with the lemma. Thus this is bundled together here, and the
lemma itself is separated below.
-/
lemma snd_inv_app_t_app' (i j k : D.J) (U : opens (pullback (D.f i j) (D.f i k)).carrier) :
  âˆƒ eq, (Ï€â‚‚â»Â¹ i, j, k) U â‰« (D.t k i).c.app _ â‰« (D.V (k, i)).presheaf.map (eq_to_hom eq) =
    (D.t' k i j).c.app _ â‰« (Ï€â‚â»Â¹ k, j, i) (unop _) :=
begin
  split,
  rw [â† is_iso.eq_inv_comp, is_open_immersion.inv_inv_app, category.assoc,
    (D.t' k i j).c.naturality_assoc],
  simp_rw â† category.assoc,
  erw â† comp_c_app,
  rw [congr_app (D.t_fac k i j), comp_c_app],
  simp_rw category.assoc,
  erw [is_open_immersion.inv_naturality, is_open_immersion.inv_naturality_assoc,
    is_open_immersion.app_inv_app'_assoc],
  simp_rw [â† (ğ–£ .V (k, i)).presheaf.map_comp,
    eq_to_hom_map (functor.op _), eq_to_hom_op, eq_to_hom_trans],
  rintros x âŸ¨y, hy, eqâŸ©,
  replace eq := concrete_category.congr_arg ((ğ–£ .t i k).base) eq,
  change ((Ï€â‚‚ i, j, k) â‰« D.t i k).base y = (D.t k i â‰« D.t i k).base x at eq,
  rw [ğ–£ .t_inv, id_base, Top.id_app] at eq,
  subst eq,
  use (inv (D.t' k i j)).base y,
  change ((inv (D.t' k i j)) â‰« (Ï€â‚ k, i, j)).base y = _,
  congr' 2,
  rw [is_iso.inv_comp_eq, ğ–£ .t_fac_assoc, ğ–£ .t_inv, category.comp_id]
end

/-- The red and the blue arrows in ![this diagram](https://i.imgur.com/q6X1GJ9.png) commute. -/
@[simp, reassoc]
lemma snd_inv_app_t_app (i j k : D.J) (U : opens (pullback (D.f i j) (D.f i k)).carrier) :
  (Ï€â‚‚â»Â¹ i, j, k) U â‰« (D.t k i).c.app _ = (D.t' k i j).c.app _ â‰« (Ï€â‚â»Â¹ k, j, i) (unop _) â‰«
    (D.V (k, i)).presheaf.map (eq_to_hom (D.snd_inv_app_t_app' i j k U).some.symm) :=
begin
  have e := (D.snd_inv_app_t_app' i j k U).some_spec,
  reassoc! e,
  rw â† e,
  simp [eq_to_hom_map],
end

variable [has_limits C]

lemma Î¹_image_preimage_eq (i j : D.J) (U : opens (D.U i).carrier) :
  (opens.map (ğ–£ .Î¹ j).base).obj ((D.Î¹_open_embedding i).is_open_map.functor.obj U) =
  (D.f_open j i).open_functor.obj ((opens.map (ğ–£ .t j i).base).obj
    ((opens.map (ğ–£ .f i j).base).obj U)) :=
begin
  ext1,
  dsimp only [opens.map_coe, is_open_map.functor_obj_coe],
  rw [â† (show _ = (ğ–£ .Î¹ i).base, from ğ–£ .Î¹_glued_iso_inv (PresheafedSpace.forget _) i),
    â† (show _ = (ğ–£ .Î¹ j).base, from ğ–£ .Î¹_glued_iso_inv (PresheafedSpace.forget _) j),
    coe_comp, coe_comp, set.image_comp, set.preimage_comp, set.preimage_image_eq],
  refine eq.trans (D.to_Top_glue_data.preimage_image_eq_image' _ _ _) _,
  rw [coe_comp, set.image_comp],
  congr' 1,
  erw set.eq_preimage_iff_image_eq,
  rw â† set.image_comp,
  change (D.t i j â‰« D.t j i).base '' _ = _,
  rw ğ–£ .t_inv,
  { simp },
  { change function.bijective (Top.homeo_of_iso (as_iso _)),
    exact homeomorph.bijective _,
    apply_instance },
  { rw â† Top.mono_iff_injective,
    apply_instance }
end

/-- (Implementation). The map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_{U_j}, ğ–£.Î¹ j â»Â¹' (ğ–£.Î¹ i '' U))` -/
def opens_image_preimage_map (i j : D.J) (U : opens (D.U i).carrier) :
  (D.U i).presheaf.obj (op U) âŸ¶ (D.U j).presheaf.obj _ :=
(D.f i j).c.app (op U) â‰« (D.t j i).c.app _ â‰« (D.f_open j i).inv_app (unop _) â‰«
  (ğ–£ .U j).presheaf.map (eq_to_hom (D.Î¹_image_preimage_eq i j U)).op

lemma opens_image_preimage_map_app' (i j k : D.J) (U : opens (D.U i).carrier) :
  âˆƒ eq, D.opens_image_preimage_map i j U â‰« (D.f j k).c.app _ =
    ((Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j).c.app (op U) â‰« (Ï€â‚‚â»Â¹ j, i, k) (unop _) â‰«
      (D.V (j, k)).presheaf.map (eq_to_hom eq) :=
begin
  split,
  delta opens_image_preimage_map,
  simp_rw category.assoc,
  rw [(D.f j k).c.naturality, f_inv_app_f_app_assoc],
  erw â† (D.V (j, k)).presheaf.map_comp,
  simp_rw â† category.assoc,
  erw [â† comp_c_app, â† comp_c_app],
  simp_rw category.assoc,
  dsimp only [functor.op, unop_op, quiver.hom.unop_op],
  rw [eq_to_hom_map (opens.map _), eq_to_hom_op, eq_to_hom_trans],
  congr
end

/-- The red and the blue arrows in ![this diagram](https://i.imgur.com/mBzV1Rx.png) commute. -/
lemma opens_image_preimage_map_app (i j k : D.J) (U : opens (D.U i).carrier) :
  D.opens_image_preimage_map i j U â‰« (D.f j k).c.app _ =
  ((Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j).c.app (op U) â‰« (Ï€â‚‚â»Â¹ j, i, k) (unop _) â‰«
    (D.V (j, k)).presheaf.map (eq_to_hom ((opens_image_preimage_map_app' D i j k U).some)) :=
(opens_image_preimage_map_app' D i j k U).some_spec

-- This is proved separately since `reassoc` somehow timeouts.
lemma opens_image_preimage_map_app_assoc (i j k : D.J) (U : opens (D.U i).carrier)
  {X' : C} (f' : _ âŸ¶ X') :
  D.opens_image_preimage_map i j U â‰« (D.f j k).c.app _ â‰« f' =
    ((Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j).c.app (op U) â‰« (Ï€â‚‚â»Â¹ j, i, k) (unop _) â‰«
    (D.V (j, k)).presheaf.map (eq_to_hom ((opens_image_preimage_map_app' D i j k U).some)) â‰« f' :=
by simpa only [category.assoc]
  using congr_arg (Î» g, g â‰« f') (opens_image_preimage_map_app D i j k U)

/-- (Implementation) Given an open subset of one of the spaces `U âŠ† Uáµ¢`, the sheaf component of
the image `Î¹ '' U` in the glued space is the limit of this diagram. -/
abbreviation diagram_over_open {i : D.J} (U : opens (D.U i).carrier) :
  (walking_multispan _ _)áµ’áµ– â¥¤ C :=
componentwise_diagram ğ–£ .diagram.multispan ((D.Î¹_open_embedding i).is_open_map.functor.obj U)

/-- (Implementation)
The projection from the limit of `diagram_over_open` to a component of `D.U j`. -/
abbreviation diagram_over_open_Ï€ {i : D.J} (U : opens (D.U i).carrier) (j : D.J) :=
limit.Ï€ (D.diagram_over_open U) (op (walking_multispan.right j))

/-- (Implementation) We construct the map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_V, U_V)` for each `V` in the gluing
diagram. We will lift these maps into `Î¹_inv_app`. -/
def Î¹_inv_app_Ï€_app {i : D.J} (U : opens (D.U i).carrier) (j) :
  (ğ–£ .U i).presheaf.obj (op U) âŸ¶ (D.diagram_over_open U).obj (op j) :=
begin
  rcases j with (âŸ¨j, kâŸ©|j),
  { refine D.opens_image_preimage_map i j U â‰« (D.f j k).c.app _ â‰«
      (D.V (j, k)).presheaf.map (eq_to_hom _),
    rw [functor.op_obj],
    congr' 1, ext1,
    dsimp only [functor.op_obj, opens.map_coe, unop_op, is_open_map.functor_obj_coe],
    rw set.preimage_preimage,
    change (D.f j k â‰« ğ–£ .Î¹ j).base â»Â¹' _ = _,
    congr' 3,
    exact colimit.w ğ–£ .diagram.multispan (walking_multispan.hom.fst (j, k)) },
  { exact D.opens_image_preimage_map i j U }
end

/-- (Implementation) The natural map `Î“(ğ’ª_{U_i}, U) âŸ¶ Î“(ğ’ª_X, ğ–£.Î¹ i '' U)`.
This forms the inverse of `(ğ–£.Î¹ i).c.app (op U)`. -/
def Î¹_inv_app {i : D.J} (U : opens (D.U i).carrier) :
  (D.U i).presheaf.obj (op U) âŸ¶ limit (D.diagram_over_open U) :=
limit.lift (D.diagram_over_open U)
{ X := (D.U i).presheaf.obj (op U),
  Ï€ := { app := Î» j, D.Î¹_inv_app_Ï€_app U (unop j),
  naturality' := Î» X Y f', begin
    induction X using opposite.rec,
    induction Y using opposite.rec,
    let f : Y âŸ¶ X := f'.unop, have : f' = f.op := rfl, clear_value f, subst this,
    rcases f with (_|âŸ¨j,kâŸ©|âŸ¨j,kâŸ©),
    { erw [category.id_comp, category_theory.functor.map_id],
      rw category.comp_id },
    { erw category.id_comp, congr' 1 },
    erw category.id_comp,
    -- It remains to show that the blue is equal to red + green in the original diagram.
    -- The proof strategy is illustrated in ![this diagram](https://i.imgur.com/mBzV1Rx.png)
    -- where we prove red = pink = light-blue = green = blue.
    change D.opens_image_preimage_map i j U â‰« (D.f j k).c.app _ â‰«
      (D.V (j, k)).presheaf.map (eq_to_hom _) = D.opens_image_preimage_map _ _ _ â‰«
      ((D.f k j).c.app _ â‰« (D.t j k).c.app _) â‰« (D.V (j, k)).presheaf.map (eq_to_hom _),
    erw opens_image_preimage_map_app_assoc,
    simp_rw category.assoc,
    erw [opens_image_preimage_map_app_assoc, (D.t j k).c.naturality_assoc],
    rw snd_inv_app_t_app_assoc,
    erw â† PresheafedSpace.comp_c_app_assoc,
    -- light-blue = green is relatively easy since the part that differs does not involve
    -- partial inverses.
    have : D.t' j k i â‰« (Ï€â‚ k, i, j) â‰« D.t k i â‰« ğ–£ .f i k =
      (pullback_symmetry _ _).hom â‰« (Ï€â‚ j, i, k) â‰« D.t j i â‰« D.f i j,
    { rw [â† ğ–£ .t_fac_assoc, ğ–£ .t'_comp_eq_pullback_symmetry_assoc,
        pullback_symmetry_hom_comp_snd_assoc, pullback.condition, ğ–£ .t_fac_assoc] },
    rw congr_app this,
    erw PresheafedSpace.comp_c_app_assoc (pullback_symmetry _ _).hom,
    simp_rw category.assoc,
    congr' 1,
    rw â† is_iso.eq_inv_comp,
    erw is_open_immersion.inv_inv_app,
    simp_rw category.assoc,
    erw [nat_trans.naturality_assoc, â† PresheafedSpace.comp_c_app_assoc,
      congr_app (pullback_symmetry_hom_comp_snd _ _)],
    simp_rw category.assoc,
    erw [is_open_immersion.inv_naturality_assoc, is_open_immersion.inv_naturality_assoc,
      is_open_immersion.inv_naturality_assoc, is_open_immersion.app_inv_app_assoc],
    repeat { erw â† (D.V (j, k)).presheaf.map_comp },
    congr,
  end } }

/-- `Î¹_inv_app` is the left inverse of `D.Î¹ i` on `U`. -/
lemma Î¹_inv_app_Ï€ {i : D.J} (U : opens (D.U i).carrier) :
  âˆƒ eq, D.Î¹_inv_app U â‰« D.diagram_over_open_Ï€ U i = (D.U i).presheaf.map (eq_to_hom eq) :=
begin
  split,
  delta Î¹_inv_app,
  rw limit.lift_Ï€,
  change D.opens_image_preimage_map i i U = _,
  dsimp [opens_image_preimage_map],
  rw [congr_app (D.t_id _), id_c_app, â† functor.map_comp],
  erw [is_open_immersion.inv_naturality_assoc, is_open_immersion.app_inv_app'_assoc],
  simp only [eq_to_hom_op, eq_to_hom_trans, eq_to_hom_map (functor.op _), â† functor.map_comp],
  rw set.range_iff_surjective.mpr _,
  { simp },
  { rw â† Top.epi_iff_surjective,
    apply_instance }
end

/-- The `eq_to_hom` given by `Î¹_inv_app_Ï€`. -/
abbreviation Î¹_inv_app_Ï€_eq_map {i : D.J} (U : opens (D.U i).carrier) :=
(D.U i).presheaf.map (eq_to_iso (D.Î¹_inv_app_Ï€ U).some).inv

/-- `Î¹_inv_app` is the right inverse of `D.Î¹ i` on `U`. -/
lemma Ï€_Î¹_inv_app_Ï€ (i j : D.J) (U : opens (D.U i).carrier) :
  D.diagram_over_open_Ï€ U i â‰« D.Î¹_inv_app_Ï€_eq_map U â‰« D.Î¹_inv_app U â‰«
    D.diagram_over_open_Ï€ U j = D.diagram_over_open_Ï€ U j :=
begin
  rw â† cancel_mono ((componentwise_diagram ğ–£ .diagram.multispan _).map
    (quiver.hom.op (walking_multispan.hom.snd (i, j))) â‰« (ğŸ™ _)),
  simp_rw category.assoc,
  rw limit.w_assoc,
  erw limit.lift_Ï€_assoc,
  rw [category.comp_id, category.comp_id],
  change _ â‰« _ â‰« (_ â‰« _) â‰« _ = _,
  rw [congr_app (D.t_id _), id_c_app],
  simp_rw category.assoc,
  rw [â† functor.map_comp_assoc, is_open_immersion.inv_naturality_assoc],
  erw is_open_immersion.app_inv_app_assoc,
  iterate 3 { rw â† functor.map_comp_assoc },
  rw nat_trans.naturality_assoc,
  erw â† (D.V (i, j)).presheaf.map_comp,
  convert limit.w (componentwise_diagram ğ–£ .diagram.multispan _)
    (quiver.hom.op (walking_multispan.hom.fst (i, j))),
  { rw category.comp_id,
    apply_with mono_comp { instances := ff },
    change mono ((_ â‰« D.f j i).c.app _),
    rw comp_c_app,
    apply_with mono_comp { instances := ff },
    erw D.Î¹_image_preimage_eq i j U,
    all_goals { apply_instance } },
end

/-- `Î¹_inv_app` is the inverse of `D.Î¹ i` on `U`. -/
lemma Ï€_Î¹_inv_app_eq_id (i : D.J) (U : opens (D.U i).carrier) :
  D.diagram_over_open_Ï€ U i â‰« D.Î¹_inv_app_Ï€_eq_map U â‰« D.Î¹_inv_app U = ğŸ™ _ :=
begin
  ext j,
  induction j using opposite.rec,
  rcases j with (âŸ¨j, kâŸ©|âŸ¨jâŸ©),
  { rw [â† limit.w (componentwise_diagram ğ–£ .diagram.multispan _)
      (quiver.hom.op (walking_multispan.hom.fst (j, k))), â† category.assoc, category.id_comp],
    congr' 1,
    simp_rw category.assoc,
    apply Ï€_Î¹_inv_app_Ï€ },
  { simp_rw category.assoc,
    rw category.id_comp,
    apply Ï€_Î¹_inv_app_Ï€ }
end

instance componentwise_diagram_Ï€_is_iso (i : D.J) (U : opens (D.U i).carrier) :
  is_iso (D.diagram_over_open_Ï€ U i) :=
begin
  use D.Î¹_inv_app_Ï€_eq_map U â‰« D.Î¹_inv_app U,
  split,
  { apply Ï€_Î¹_inv_app_eq_id },
  { rw [category.assoc, (D.Î¹_inv_app_Ï€ _).some_spec],
    exact iso.inv_hom_id ((D.to_glue_data.U i).presheaf.map_iso (eq_to_iso _)) }
end

instance Î¹_is_open_immersion (i : D.J) :
  is_open_immersion (ğ–£ .Î¹ i) :=
{ base_open := D.Î¹_open_embedding i,
  c_iso := Î» U, by { erw â† colimit_presheaf_obj_iso_componentwise_limit_hom_Ï€, apply_instance } }

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def V_pullback_cone_is_limit (i j : D.J) : is_limit (ğ–£ .V_pullback_cone i j) :=
pullback_cone.is_limit_aux' _ $ Î» s,
begin
  refine âŸ¨_, _, _, _âŸ©,
  { refine PresheafedSpace.is_open_immersion.lift (D.f i j) s.fst _,
    erw â† D.to_Top_glue_data.preimage_range j i,
    have : s.fst.base â‰« D.to_Top_glue_data.to_glue_data.Î¹ i =
      s.snd.base â‰« D.to_Top_glue_data.to_glue_data.Î¹ j,
    { rw [â† ğ–£ .Î¹_glued_iso_hom (PresheafedSpace.forget _) _,
        â† ğ–£ .Î¹_glued_iso_hom (PresheafedSpace.forget _) _],
      have := congr_arg PresheafedSpace.hom.base s.condition,
      rw [comp_base, comp_base] at this,
      reassoc! this,
      exact this _ },
    rw [â† set.image_subset_iff, â† set.image_univ, â† set.image_comp, set.image_univ,
      â† coe_comp, this, coe_comp, â† set.image_univ, set.image_comp],
    exact set.image_subset_range _ _ },
  { apply is_open_immersion.lift_fac },
  { rw [â† cancel_mono (ğ–£ .Î¹ j), category.assoc, â† (ğ–£ .V_pullback_cone i j).condition],
    conv_rhs { rw â† s.condition },
    erw is_open_immersion.lift_fac_assoc },
  { intros m eâ‚ eâ‚‚, rw â† cancel_mono (D.f i j), erw eâ‚, rw is_open_immersion.lift_fac }
end

lemma Î¹_jointly_surjective (x : ğ–£ .glued) :
  âˆƒ (i : D.J) (y : D.U i), (ğ–£ .Î¹ i).base y = x :=
ğ–£ .Î¹_jointly_surjective (PresheafedSpace.forget _ â‹™ category_theory.forget Top) x

end glue_data

end PresheafedSpace

namespace SheafedSpace

variables (C) [has_products.{v} C]

/--
A family of gluing data consists of
1. An index type `J`
2. A sheafed space `U i` for each `i : J`.
3. A sheafed space `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ SheafedSpace C` rather than `J â†’ J â†’ SheafedSpace C` to
  connect to the limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.
-/
@[nolint has_nonempty_instance]
structure glue_data extends glue_data (SheafedSpace.{v} C) :=
(f_open : âˆ€ i j, SheafedSpace.is_open_immersion (f i j))

attribute [instance] glue_data.f_open

namespace glue_data

variables {C} (D : glue_data C)

local notation `ğ–£` := D.to_glue_data

/-- The glue data of presheafed spaces associated to a family of glue data of sheafed spaces. -/
abbreviation to_PresheafedSpace_glue_data : PresheafedSpace.glue_data C :=
{ f_open := D.f_open,
  to_glue_data := ğ–£ .map_glue_data forget_to_PresheafedSpace }

variable [has_limits C]

/-- The gluing as sheafed spaces is isomorphic to the gluing as presheafed spaces. -/
abbreviation iso_PresheafedSpace : ğ–£ .glued.to_PresheafedSpace â‰…
  D.to_PresheafedSpace_glue_data.to_glue_data.glued :=
ğ–£ .glued_iso forget_to_PresheafedSpace

lemma Î¹_iso_PresheafedSpace_inv (i : D.J) :
  D.to_PresheafedSpace_glue_data.to_glue_data.Î¹ i â‰« D.iso_PresheafedSpace.inv = ğ–£ .Î¹ i :=
ğ–£ .Î¹_glued_iso_inv _ _

instance Î¹_is_open_immersion (i : D.J) :
  is_open_immersion (ğ–£ .Î¹ i) :=
by { rw â† D.Î¹_iso_PresheafedSpace_inv, apply_instance }

lemma Î¹_jointly_surjective (x : ğ–£ .glued) :
  âˆƒ (i : D.J) (y : D.U i), (ğ–£ .Î¹ i).base y = x :=
ğ–£ .Î¹_jointly_surjective (SheafedSpace.forget _ â‹™ category_theory.forget Top) x

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def V_pullback_cone_is_limit (i j : D.J) : is_limit (ğ–£ .V_pullback_cone i j) :=
ğ–£ .V_pullback_cone_is_limit_of_map forget_to_PresheafedSpace i j
  (D.to_PresheafedSpace_glue_data.V_pullback_cone_is_limit _ _)

end glue_data

end SheafedSpace

namespace LocallyRingedSpace

/--
A family of gluing data consists of
1. An index type `J`
2. A locally ringed space `U i` for each `i : J`.
3. A locally ringed space `V i j` for each `i j : J`.
  (Note that this is `J Ã— J â†’ LocallyRingedSpace` rather than `J â†’ J â†’ LocallyRingedSpace` to
  connect to the limits library easier.)
4. An open immersion `f i j : V i j âŸ¶ U i` for each `i j : Î¹`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : Î¹`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
9. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.

We can then glue the spaces `U i` together by identifying `V i j` with `V j i`, such
that the `U i`'s are open subspaces of the glued space.
-/
@[nolint has_nonempty_instance]
structure glue_data extends glue_data LocallyRingedSpace :=
(f_open : âˆ€ i j, LocallyRingedSpace.is_open_immersion (f i j))

attribute [instance] glue_data.f_open

namespace glue_data

variables (D : glue_data)

local notation `ğ–£` := D.to_glue_data

/-- The glue data of ringed spaces associated to a family of glue data of locally ringed spaces. -/
abbreviation to_SheafedSpace_glue_data : SheafedSpace.glue_data CommRing :=
{ f_open := D.f_open,
  to_glue_data := ğ–£ .map_glue_data forget_to_SheafedSpace }

/-- The gluing as locally ringed spaces is isomorphic to the gluing as ringed spaces. -/
abbreviation iso_SheafedSpace : ğ–£ .glued.to_SheafedSpace â‰…
  D.to_SheafedSpace_glue_data.to_glue_data.glued :=
ğ–£ .glued_iso forget_to_SheafedSpace

lemma Î¹_iso_SheafedSpace_inv (i : D.J) :
  D.to_SheafedSpace_glue_data.to_glue_data.Î¹ i â‰« D.iso_SheafedSpace.inv = (ğ–£ .Î¹ i).1 :=
ğ–£ .Î¹_glued_iso_inv forget_to_SheafedSpace i

instance Î¹_is_open_immersion (i : D.J) :
  is_open_immersion (ğ–£ .Î¹ i) :=
by { delta is_open_immersion, rw â† D.Î¹_iso_SheafedSpace_inv,
  apply PresheafedSpace.is_open_immersion.comp }

instance (i j k : D.J) :
  preserves_limit (cospan (ğ–£ .f i j) (ğ–£ .f i k)) forget_to_SheafedSpace :=
infer_instance

lemma Î¹_jointly_surjective (x : ğ–£ .glued) :
  âˆƒ (i : D.J) (y : D.U i), (ğ–£ .Î¹ i).1.base y = x :=
ğ–£ .Î¹_jointly_surjective ((LocallyRingedSpace.forget_to_SheafedSpace â‹™
  SheafedSpace.forget _) â‹™ forget Top) x

/-- The following diagram is a pullback, i.e. `Váµ¢â±¼` is the intersection of `Uáµ¢` and `Uâ±¼` in `X`.

Váµ¢â±¼ âŸ¶ Uáµ¢
 |      |
 â†“      â†“
 Uâ±¼ âŸ¶ X
-/
def V_pullback_cone_is_limit (i j : D.J) : is_limit (ğ–£ .V_pullback_cone i j) :=
ğ–£ .V_pullback_cone_is_limit_of_map forget_to_SheafedSpace i j
  (D.to_SheafedSpace_glue_data.V_pullback_cone_is_limit _ _)

end glue_data

end LocallyRingedSpace

end algebraic_geometry
