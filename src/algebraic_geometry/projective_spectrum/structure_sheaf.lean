/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import algebraic_geometry.projective_spectrum.topology
import algebra.category.CommRing.colimits
import algebra.category.CommRing.limits
import topology.sheaves.local_predicate
import ring_theory.localization

/-!
# The structure sheaf on `prime_spectrum R`.

We define the structure sheaf on `Top.of (prime_spectrum R)`, for a commutative ring `R` and prove
basic properties about it. We define this as a subsheaf of the sheaf of dependent functions into the
localizations, cut out by the condition that the function must be locally equal to a ratio of
elements of `R`.

Because the condition "is equal to a fraction" passes to smaller open subsets,
the subset of functions satisfying this condition is automatically a subpresheaf.
Because the condition "is locally equal to a fraction" is local,
it is also a subsheaf.

(It may be helpful to refer back to `topology.sheaves.sheaf_of_functions`,
where we show that dependent functions into any type family form a sheaf,
and also `topology.sheaves.local_predicate`, where we characterise the predicates
which pick out sub-presheaves and sub-sheaves of these sheaves.)

We also set up the ring structure, obtaining
`structure_sheaf R : sheaf CommRing (Top.of (prime_spectrum R))`.

We then construct two basic isomorphisms, relating the structure sheaf to the underlying ring `R`.
First, `structure_sheaf.stalk_iso` gives an isomorphism between the stalk of the structure sheaf
at a point `p` and the localization of `R` at the prime ideal `p`. Second,
`structure_sheaf.basic_open_iso` gives an isomorphism between the structure sheaf on `basic_open f`
and the localization of `R` at the submonoid of powers of `f`.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


noncomputable theory

open_locale classical direct_sum big_operators pointwise
open direct_sum set_like

variables {Î¹ R A: Type*} [linear_ordered_cancel_add_comm_monoid Î¹]
variables [comm_ring R] [comm_ring A] [algebra R A]
variables (ğ’œ : Î¹ â†’ submodule R A) [graded_algebra ğ’œ]

open Top
open topological_space
open category_theory
open opposite

namespace algebraic_geometry

/--
The prime spectrum, just as a topological space.
-/
def projective_spectrum.Top : Top := Top.of (projective_spectrum ğ’œ)

namespace projective_spectrum.structure_sheaf

/--
The type family over `prime_spectrum R` consisting of the localization over each point.
-/
@[derive [comm_ring]]
def localizations (P : projective_spectrum.Top ğ’œ) :=
localization.at_prime P.as_homogeneous_ideal.1

instance (P : projective_spectrum.Top ğ’œ) : inhabited (localizations ğ’œ P) :=
âŸ¨1âŸ©

instance (U : opens (projective_spectrum.Top ğ’œ)) (x : U) :
  algebra A (localizations ğ’œ x) :=
localization.algebra

instance (U : opens (projective_spectrum.Top ğ’œ)) (x : U) :
  is_localization.at_prime (localizations ğ’œ x)
  (x : projective_spectrum.Top ğ’œ).as_homogeneous_ideal.1 :=
localization.is_localization

variables {ğ’œ}

/--
The predicate saying that a dependent function on an open `U` is realised as a fixed fraction
`r / s` in each of the stalks (which are localizations at various prime ideals).
-/
def is_fraction {U : opens (projective_spectrum.Top ğ’œ)}
  (f : Î  x : U, localizations ğ’œ x) : Prop :=
âˆƒ (r s : A) (r_s_deg_same : âˆƒ (i : Î¹), r âˆˆ ğ’œ i âˆ§ s âˆˆ ğ’œ i),
  âˆ€ x : U, Â¬ (s âˆˆ x.1.as_homogeneous_ideal) âˆ§ f x * algebra_map _ _ s = algebra_map _ _ r

lemma is_fraction.eq_mk' {U : opens (projective_spectrum.Top ğ’œ)}
  {f : Î  x : U, localizations ğ’œ x}
  (hf : is_fraction f) :
  âˆƒ (r s : A) (r_s_deg_same : âˆƒ (i : Î¹), r âˆˆ ğ’œ i âˆ§ s âˆˆ ğ’œ i),
    âˆ€ x : U, âˆƒ (hs : s âˆ‰ x.1.as_homogeneous_ideal), f x =
    is_localization.mk' (localization.at_prime _) r
      (âŸ¨s, hsâŸ© : (x : projective_spectrum.Top ğ’œ).as_homogeneous_ideal.1.prime_compl) :=
begin
  rcases hf with âŸ¨r, s, r_s_deg_same, hâŸ©,
  refine âŸ¨r, s, r_s_deg_same,
    Î» x, âŸ¨(h x).1, (is_localization.mk'_eq_iff_eq_mul.mpr _).symmâŸ©âŸ©,
  exact (h x).2.symm,
end

variables (ğ’œ)

/--
The predicate `is_fraction` is "prelocal",
in the sense that if it holds on `U` it holds on any open subset `V` of `U`.
-/
def is_fraction_prelocal : prelocal_predicate (localizations ğ’œ) :=
{ pred := Î» U f, is_fraction f,
  res := by { rintro V U i f âŸ¨r, s, r_s_deg_same, wâŸ©,
    exact âŸ¨r, s, r_s_deg_same, Î» x, âŸ¨(w (i x)).1, (w (i x)).2âŸ©âŸ© } }

/--
We will define the structure sheaf as
the subsheaf of all dependent functions in `Î  x : U, localizations R x`
consisting of those functions which can locally be expressed as a ratio of
(the images in the localization of) elements of `R`.

Quoting Hartshorne:

For an open set $U âŠ† Spec A$, we define $ğ’ª(U)$ to be the set of functions
$s : U â†’ â¨†_{ğ”­ âˆˆ U} A_ğ”­$, such that $s(ğ”­) âˆˆ A_ğ”­$ for each $ğ”­$,
and such that $s$ is locally a quotient of elements of $A$:
to be precise, we require that for each $ğ”­ âˆˆ U$, there is a neighborhood $V$ of $ğ”­$,
contained in $U$, and elements $a, f âˆˆ A$, such that for each $ğ”® âˆˆ V, f âˆ‰ ğ”®$,
and $s(ğ”®) = a/f$ in $A_ğ”®$.

Now Hartshorne had the disadvantage of not knowing about dependent functions,
so we replace his circumlocution about functions into a disjoint union with
`Î  x : U, localizations x`.
-/
def is_locally_fraction : local_predicate (localizations ğ’œ) :=
(is_fraction_prelocal ğ’œ).sheafify

@[simp]
lemma is_locally_fraction_pred
  {U : opens (projective_spectrum.Top ğ’œ)} (f : Î  x : U, localizations ğ’œ x) :
  (is_locally_fraction ğ’œ).pred f =
  âˆ€ x : U, âˆƒ (V) (m : x.1 âˆˆ V) (i : V âŸ¶ U),
  âˆƒ (r s : A)  (r_s_deg_same : âˆƒ (i : Î¹), r âˆˆ ğ’œ i âˆ§ s âˆˆ ğ’œ i), âˆ€ y : V,
  Â¬ (s âˆˆ y.1.as_homogeneous_ideal) âˆ§
    f (i y : U) * algebra_map _ _ s = algebra_map _ _ r :=
rfl

/--
The functions satisfying `is_locally_fraction` form a subring.
-/
def sections_subring (U : (opens (projective_spectrum.Top ğ’œ))áµ’áµ–) :
  subring (Î  x : unop U, localizations ğ’œ x) :=
{ carrier := { f | (is_locally_fraction ğ’œ).pred f },
  zero_mem' :=
  begin
    refine Î» x, âŸ¨unop U, x.2, ğŸ™ _, 0, 1,
      âŸ¨0, submodule.zero_mem _, set_like.has_graded_one.one_memâŸ©, Î» y, âŸ¨_, _âŸ©âŸ©,
    { erw â†ideal.ne_top_iff_one ((y : projective_spectrum.Top ğ’œ).as_homogeneous_ideal.1),
      exact y.1.is_prime.1, },
    { simp, },
  end,
  one_mem' :=
  begin
    refine Î» x, âŸ¨unop U, x.2, ğŸ™ _, 1, 1,
      âŸ¨0, set_like.has_graded_one.one_mem, set_like.has_graded_one.one_memâŸ©, Î» y, âŸ¨_, _âŸ©âŸ©,
    { erw â†ideal.ne_top_iff_one ((y : projective_spectrum.Top ğ’œ).as_homogeneous_ideal.1),
      exact y.1.is_prime.1, },
    { simp, },
  end,
  add_mem' :=
  begin
    intros a b ha hb x,
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, âŸ¨ja, ra_sa_same_degâŸ©, waâŸ©,
    rcases hb x with âŸ¨Vb, mb, ib, rb, sb, âŸ¨jb, rb_sb_same_degâŸ©, wbâŸ©,
    refine âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, opens.inf_le_left _ _ â‰« ia, ra * sb + rb * sa, sa * sb,
      âŸ¨ja + jb, submodule.add_mem _ _ _,
        set_like.graded_monoid.mul_mem ra_sa_same_deg.2 rb_sb_same_deg.2âŸ©, Î» y, âŸ¨Î» h, _, _âŸ©âŸ©,
    { apply set_like.graded_monoid.mul_mem, exact ra_sa_same_deg.1,
      exact rb_sb_same_deg.2, },
    { rw add_comm, apply set_like.graded_monoid.mul_mem,
      exact rb_sb_same_deg.1, exact ra_sa_same_deg.2, },
    { have := (y : projective_spectrum.Top ğ’œ).is_prime.mem_or_mem h, cases this,
      apply (wa âŸ¨y, _âŸ©).1, exact this,
      suffices : y.1 âˆˆ Va, exact this,
      exact (opens.inf_le_left Va Vb y).2,
      apply (wb âŸ¨y, _âŸ©).1, exact this,
      suffices : y.1 âˆˆ Vb, exact this,
      exact (opens.inf_le_right Va Vb y).2, },
    { simp only [add_mul, ring_hom.map_add, pi.add_apply, ring_hom.map_mul],
      erw â†(wa (opens.inf_le_left Va Vb y)).2,
      erw â†(wb (opens.inf_le_right Va Vb y)).2,
      simp only [mul_assoc],
      congr' 2,
      rw [mul_comm], refl, }
  end,
  neg_mem' :=
  begin
    intros a ha x,
    rcases ha x with âŸ¨V, m, i, r, s, âŸ¨j, r_hom_j, s_hom_jâŸ©, wâŸ©,
    refine âŸ¨V, m, i, -r, s, âŸ¨j, submodule.neg_mem _ r_hom_j, s_hom_jâŸ©, Î» y, âŸ¨(w _).1, _âŸ©âŸ©,
    simp only [ring_hom.map_neg, pi.neg_apply],
      erw [â†(w _).2],
      simp only [neg_mul_eq_neg_mul_symm],
  end,
  mul_mem' :=
  begin
    intros a b ha hb x,
    rcases ha x with âŸ¨Va, ma, ia, ra, sa, âŸ¨ja, ra_hom_ja, sa_hom_jaâŸ©, waâŸ©,
    rcases hb x with âŸ¨Vb, mb, ib, rb, sb, âŸ¨jb, rb_hom_jb, sb_hom_jbâŸ©, wbâŸ©,
    refine âŸ¨Va âŠ“ Vb, âŸ¨ma, mbâŸ©, opens.inf_le_left _ _ â‰« ia, ra * rb, sa * sb,
      âŸ¨ja + jb, set_like.graded_monoid.mul_mem ra_hom_ja rb_hom_jb,
        set_like.graded_monoid.mul_mem sa_hom_ja sb_hom_jbâŸ©, Î» y, âŸ¨Î» h, _, _âŸ©âŸ©,
    { have := (y : projective_spectrum.Top ğ’œ).is_prime.mem_or_mem h, cases this,
      apply (wa âŸ¨y, _âŸ©).1, exact this,
      suffices : y.1 âˆˆ Va, exact this,
      exact (opens.inf_le_left Va Vb y).2,
      apply (wb âŸ¨y, _âŸ©).1, exact this,
      suffices : y.1 âˆˆ Vb, exact this,
      exact (opens.inf_le_right Va Vb y).2, },
    { simp only [pi.mul_apply, ring_hom.map_mul],
      erw â†(wa (opens.inf_le_left Va Vb y)).2,
      erw â†(wb (opens.inf_le_right Va Vb y)).2,
      simp only [mul_left_comm, mul_assoc, mul_comm],
      refl, }
  end, }

/--
The structure sheaf (valued in `Type`, not yet `CommRing`) is the subsheaf consisting of
functions satisfying `is_locally_fraction`.
-/
def structure_sheaf_in_Type : sheaf Type* (projective_spectrum.Top ğ’œ):=
subsheaf_to_Types (is_locally_fraction ğ’œ)

instance comm_ring_structure_sheaf_in_Type_obj
  (U : (opens (projective_spectrum.Top ğ’œ))áµ’áµ–) :
  comm_ring ((structure_sheaf_in_Type ğ’œ).1.obj U) :=
(sections_subring ğ’œ U).to_comm_ring

/--
The structure presheaf, valued in `CommRing`, constructed by dressing up the `Type` valued
structure presheaf.
-/
@[simps]
def structure_presheaf_in_CommRing : presheaf CommRing (projective_spectrum.Top ğ’œ) :=
{ obj := Î» U, CommRing.of ((structure_sheaf_in_Type ğ’œ).1.obj U),
  map := Î» U V i,
  { to_fun := ((structure_sheaf_in_Type ğ’œ).1.map i),
    map_zero' := rfl,
    map_add' := Î» x y, rfl,
    map_one' := rfl,
    map_mul' := Î» x y, rfl, }, }

/--
Some glue, verifying that that structure presheaf valued in `CommRing` agrees
with the `Type` valued structure presheaf.
-/
def structure_presheaf_comp_forget :
  structure_presheaf_in_CommRing ğ’œ â‹™ (forget CommRing) â‰… (structure_sheaf_in_Type ğ’œ).1 :=
nat_iso.of_components
  (Î» U, iso.refl _)
  (by tidy)

open Top.presheaf

/--
The structure sheaf on $Spec R$, valued in `CommRing`.

This is provided as a bundled `SheafedSpace` as `Spec.SheafedSpace R` later.
-/
def structure_sheaf : sheaf CommRing (projective_spectrum.Top ğ’œ) :=
âŸ¨structure_presheaf_in_CommRing ğ’œ,
  -- We check the sheaf condition under `forget CommRing`.
  (is_sheaf_iff_is_sheaf_comp _ _).mpr
    (is_sheaf_of_iso (structure_presheaf_comp_forget ğ’œ).symm
      (structure_sheaf_in_Type ğ’œ).property)âŸ©

end projective_spectrum.structure_sheaf

end algebraic_geometry
