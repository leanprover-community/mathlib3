/-
Copyright (c) 2020 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Johan Commelin
-/

import topology.opens
import ring_theory.graded_algebra.homogeneous_ideal

/-!
# Projective spectrum of a graded ring

The projective spectrum of a graded commutative ring is the subtype of all homogenous ideals that
are prime and not containing all the irrelevant ideal.
It is naturally endowed with a topology: the Zariski topology.

## Notation
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ğ’œ : â„• â†’ submodule R A` is the grading of `A`;

## Main definitions

* `projective_spectrum ğ’œ`: The projective spectrum of a graded ring `A`, or equivalently, the set of
  all homogeneous ideals of `A` that is both prime and relevant i.e. not containing all irrelevant
  ideal. Henceforth, we call elements of projective spectrum relevant prime homogeneous ideals.
* `zero_locus s`: The zero locus of a subset `s` of `A`
  is the subset of `projective_spectrum ğ’œ` consisting of all relevant prime homogeneous ideals
  that contain `s`.
* `vanishing_ideal t`: The vanishing ideal of a subset `t` of `projective_spectrum ğ’œ`
  is the intersection of points in `t` (viewed as relevant prime homogeneous ideals).

## Implementation note
The type of `vanishing_ideal` is an `ideal` but instead of `homogeneous_ideal` is to take advantage
of `ideal_gc` so that setting up a galois connection is easier.
-/

noncomputable theory
open_locale direct_sum big_operators pointwise
open direct_sum set_like

variables {R A: Type*}
variables [comm_semiring R] [comm_ring A] [algebra R A]
variables (ğ’œ : â„• â†’ submodule R A) [graded_algebra ğ’œ]

/--
The projective spectrum of a graded commutative ring is the subtype of all homogenous ideals that
are prime and not containing all the irrelevant ideal.
-/
@[nolint has_inhabited_instance]
def projective_spectrum :=
{I : homogeneous_ideal ğ’œ // I.1.is_prime âˆ§ Â¬(homogeneous_ideal.irrelevant ğ’œ â‰¤ I)}

namespace projective_spectrum


variable {ğ’œ}
/-- A method to view a point in the projective spectrum of a graded ring
as a homogeneous ideal of that ring. -/
abbreviation as_homogeneous_ideal (x : projective_spectrum ğ’œ) : homogeneous_ideal ğ’œ := x.val

lemma as_homogeneous_ideal.explicit (x : projective_spectrum ğ’œ) :
  x.as_homogeneous_ideal = x.1 := rfl

instance is_prime (x : projective_spectrum ğ’œ) :
  x.as_homogeneous_ideal.1.is_prime := x.2.1

@[ext] lemma ext {x y : projective_spectrum ğ’œ} :
  x = y â†” x.as_homogeneous_ideal = y.as_homogeneous_ideal :=
subtype.ext_iff_val

variable (ğ’œ)
/-- The zero locus of a set `s` of elements of a commutative ring `A`
is the set of all relevant prime homogeneous ideals of the ring that contain the set `s`.

An element `f` of `A` can be thought of as a dependent function on the projective spectrum of `ğ’œ`.
At a point `x` (a prime homogeneous ideal)
the function (i.e., element) `f` takes values in the quotient ring `A` modulo the prime ideal `x`.
In this manner, `zero_locus s` is exactly the subset of `projective_spectrum ğ’œ`
where all "functions" in `s` vanish simultaneously.
-/
def zero_locus (s : set A) : set (projective_spectrum ğ’œ) :=
{x | s âŠ† x.as_homogeneous_ideal}

@[simp] lemma mem_zero_locus (x : projective_spectrum ğ’œ) (s : set A) :
  x âˆˆ zero_locus ğ’œ s â†” s âŠ† x.as_homogeneous_ideal := iff.rfl

@[simp] lemma zero_locus_span (s : set A) :
  zero_locus ğ’œ (ideal.span s) = zero_locus ğ’œ s :=
by { ext x, exact (submodule.gi _ _).gc s x.as_homogeneous_ideal }

/-- The vanishing ideal of a set `t` of points
of the prime spectrum of a commutative ring `R`
is the intersection of all the prime ideals in the set `t`.

An element `f` of `R` can be thought of as a dependent function
on the prime spectrum of `R`.
At a point `x` (a prime ideal)
the function (i.e., element) `f` takes values in the quotient ring `R` modulo the prime ideal `x`.
In this manner, `vanishing_ideal t` is exactly the ideal of `R`
consisting of all "functions" that vanish on all of `t`.
-/
def vanishing_ideal (t : set (projective_spectrum ğ’œ)) : ideal A :=
â¨… (x : projective_spectrum ğ’œ) (h : x âˆˆ t), x.as_homogeneous_ideal.1

lemma vanishing_ideal.is_homogeneous (t : set (projective_spectrum ğ’œ)) :
  ideal.is_homogeneous ğ’œ $ vanishing_ideal ğ’œ t :=
ideal.is_homogeneous.Inf $ Î» I hI, begin
  obtain âŸ¨y, rflâŸ© := hI,
  apply ideal.is_homogeneous.Inf (Î» I hI, _),
  obtain âŸ¨_, rflâŸ© := hI,
  exact y.1.2,
end

lemma coe_vanishing_ideal (t : set (projective_spectrum ğ’œ)) :
  (vanishing_ideal ğ’œ t : set A) =
  {f | âˆ€ x : projective_spectrum ğ’œ, x âˆˆ t â†’ f âˆˆ x.as_homogeneous_ideal} :=
begin
  ext f,
  rw [vanishing_ideal, set_like.mem_coe, submodule.mem_infi],
  apply forall_congr, intro x,
  rw [submodule.mem_infi], refl,
end

lemma mem_vanishing_ideal (t : set (projective_spectrum ğ’œ)) (f : A) :
  f âˆˆ vanishing_ideal ğ’œ t â†”
  âˆ€ x : projective_spectrum ğ’œ, x âˆˆ t â†’ f âˆˆ x.as_homogeneous_ideal :=
by rw [â† set_like.mem_coe, coe_vanishing_ideal, set.mem_set_of_eq]

@[simp] lemma vanishing_ideal_singleton (x : projective_spectrum ğ’œ) :
  vanishing_ideal ğ’œ ({x} : set (projective_spectrum ğ’œ)) = x.as_homogeneous_ideal :=
by simp [vanishing_ideal]

lemma subset_zero_locus_iff_le_vanishing_ideal (t : set (projective_spectrum ğ’œ))
  (I : ideal A) :
  t âŠ† zero_locus ğ’œ I â†” I â‰¤ vanishing_ideal ğ’œ t :=
âŸ¨Î» h f k, (mem_vanishing_ideal _ _ _).mpr (Î» x j, (mem_zero_locus _ _ _).mpr (h j) k), Î» h,
  Î» x j, (mem_zero_locus _ _ _).mpr (le_trans h (Î» f h, ((mem_vanishing_ideal _ _ _).mp h) x j))âŸ©

section gc
variable (A)

/-- `zero_locus` and `vanishing_ideal` form a galois connection. -/
lemma gc : @galois_connection
  (ideal A) (order_dual (set (projective_spectrum ğ’œ))) _ _
  (Î» I, zero_locus ğ’œ I) (Î» t, vanishing_ideal ğ’œ t) :=
Î» I t, subset_zero_locus_iff_le_vanishing_ideal ğ’œ t I

/-- `zero_locus` and `vanishing_ideal` form a galois connection. -/
lemma gc_set : @galois_connection
  (set A) (order_dual (set (projective_spectrum ğ’œ))) _ _
  (Î» s, zero_locus ğ’œ s) (Î» t, vanishing_ideal ğ’œ t) :=
have ideal_gc : galois_connection (ideal.span) coe := (submodule.gi A _).gc,
by simpa [zero_locus_span, function.comp] using galois_connection.compose ideal_gc (gc _ ğ’œ)

lemma subset_zero_locus_iff_subset_vanishing_ideal (t : set (projective_spectrum ğ’œ))
  (s : set A) :
  t âŠ† zero_locus ğ’œ s â†” s âŠ† vanishing_ideal ğ’œ t :=
(gc_set _ _) s t

end gc

lemma subset_vanishing_ideal_zero_locus (s : set A) :
  s âŠ† vanishing_ideal ğ’œ (zero_locus ğ’œ s) :=
(gc_set _ _).le_u_l s

lemma le_vanishing_ideal_zero_locus (I : ideal A) :
  I â‰¤ vanishing_ideal ğ’œ (zero_locus ğ’œ I) :=
(gc _ _).le_u_l I

lemma subset_zero_locus_vanishing_ideal (t : set (projective_spectrum ğ’œ)) :
  t âŠ† zero_locus ğ’œ (vanishing_ideal ğ’œ t) :=
(gc A _).l_u_le t

lemma zero_locus_anti_mono {s t : set A} (h : s âŠ† t) : zero_locus ğ’œ t âŠ† zero_locus ğ’œ s :=
(gc_set A _).monotone_l h

lemma zero_locus_anti_mono_ideal {s t : ideal A} (h : s â‰¤ t) :
  zero_locus ğ’œ (t : set A) âŠ† zero_locus ğ’œ (s : set A) :=
(gc A _).monotone_l h

lemma vanishing_ideal_anti_mono {s t : set (projective_spectrum ğ’œ)} (h : s âŠ† t) :
  vanishing_ideal ğ’œ t â‰¤ vanishing_ideal ğ’œ s :=
(gc A _).monotone_u h

lemma zero_locus_bot :
  zero_locus ğ’œ ((âŠ¥ : ideal A) : set A) = set.univ :=
(@gc R A _ _ _ ğ’œ _).l_bot

@[simp] lemma zero_locus_singleton_zero :
  zero_locus ğ’œ ({0} : set A) = set.univ :=
zero_locus_bot _

@[simp] lemma zero_locus_empty :
  zero_locus ğ’œ (âˆ… : set A) = set.univ :=
(@gc_set R A _ _ _ ğ’œ _).l_bot

@[simp] lemma vanishing_ideal_univ :
  vanishing_ideal ğ’œ (âˆ… : set (projective_spectrum ğ’œ)) = âŠ¤ :=
by simpa using (gc A _).u_top

lemma zero_locus_empty_of_one_mem {s : set A} (h : (1:A) âˆˆ s) :
  zero_locus ğ’œ s = âˆ… :=
begin
  rw set.eq_empty_iff_forall_not_mem,
  intros x hx,
  rw mem_zero_locus at hx,
  have x_prime :x.as_homogeneous_ideal.1.is_prime := by apply_instance,
  have eq_top : x.as_homogeneous_ideal = âŠ¤,
  { rw [homogeneous_ideal.eq_top_iff, ideal.eq_top_iff_one], exact hx h },
  apply x_prime.ne_top,
  erw â†homogeneous_ideal.eq_top_iff,
  exact eq_top,
end

@[simp] lemma zero_locus_singleton_one :
  zero_locus ğ’œ ({1} : set A) = âˆ… :=
zero_locus_empty_of_one_mem ğ’œ (set.mem_singleton (1 : A))

@[simp] lemma zero_locus_univ :
  zero_locus ğ’œ (set.univ : set A) = âˆ… :=
zero_locus_empty_of_one_mem _ (set.mem_univ 1)

lemma zero_locus_sup (I J : ideal A) :
  zero_locus ğ’œ ((I âŠ” J : ideal A) : set A) = zero_locus _ I âˆ© zero_locus _ J :=
(@gc R A _ _ _ ğ’œ _).l_sup

lemma zero_locus_union (s s' : set A) :
  zero_locus ğ’œ (s âˆª s') = zero_locus _ s âˆ© zero_locus _ s' :=
(@gc_set R A _ _ _ ğ’œ _).l_sup

lemma vanishing_ideal_union (t t' : set (projective_spectrum ğ’œ)) :
  vanishing_ideal ğ’œ (t âˆª t') = vanishing_ideal ğ’œ t âŠ“ vanishing_ideal ğ’œ t' :=
(@gc R A _ _ _ ğ’œ _).u_inf

lemma zero_locus_supr {Î³ : Sort*} (I : Î³ â†’ ideal A) :
  zero_locus _ ((â¨† i, I i : ideal A) : set A) = (â‹‚ i, zero_locus ğ’œ (I i)) :=
(@gc R A _ _ _ ğ’œ _).l_supr

lemma zero_locus_Union {Î³ : Sort*} (s : Î³ â†’ set A) :
  zero_locus ğ’œ (â‹ƒ i, s i) = (â‹‚ i, zero_locus ğ’œ (s i)) :=
(@gc_set R A _ _ _ ğ’œ _).l_supr

lemma zero_locus_bUnion (s : set (set A)) :
  zero_locus ğ’œ (â‹ƒ s' âˆˆ s, s' : set A) = â‹‚ s' âˆˆ s, zero_locus ğ’œ s' :=
by simp only [zero_locus_Union]

lemma vanishing_ideal_Union {Î³ : Sort*} (t : Î³ â†’ set (projective_spectrum ğ’œ)) :
  vanishing_ideal ğ’œ (â‹ƒ i, t i) = (â¨… i, vanishing_ideal ğ’œ (t i)) :=
(@gc R A _ _ _ ğ’œ _).u_infi

lemma zero_locus_inf (I J : ideal A) :
  zero_locus ğ’œ ((I âŠ“ J : ideal A) : set A) = zero_locus ğ’œ I âˆª zero_locus ğ’œ J :=
set.ext $ Î» x, by simpa using x.2.1.inf_le

lemma union_zero_locus (s s' : set A) :
  zero_locus ğ’œ s âˆª zero_locus ğ’œ s' = zero_locus ğ’œ ((ideal.span s) âŠ“ (ideal.span s'): ideal A) :=
by { rw zero_locus_inf, simp }

lemma zero_locus_mul (I J : ideal A) :
  zero_locus ğ’œ ((I * J : ideal A) : set A) = zero_locus ğ’œ I âˆª zero_locus ğ’œ J :=
set.ext $ Î» x, by simpa using x.2.1.mul_le

lemma zero_locus_singleton_mul (f g : A) :
  zero_locus ğ’œ ({f * g} : set A) = zero_locus ğ’œ {f} âˆª zero_locus ğ’œ {g} :=
set.ext $ Î» x, by simpa using x.2.1.mul_mem_iff_mem_or_mem

@[simp] lemma zero_locus_singleton_pow (f : A) (n : â„•) (hn : 0 < n) :
  zero_locus ğ’œ ({f ^ n} : set A) = zero_locus ğ’œ {f} :=
set.ext $ Î» x, by simpa using x.2.1.pow_mem_iff_mem n hn

lemma sup_vanishing_ideal_le (t t' : set (projective_spectrum ğ’œ)) :
  vanishing_ideal ğ’œ t âŠ” vanishing_ideal ğ’œ t' â‰¤ vanishing_ideal ğ’œ (t âˆ© t') :=
begin
  intros r,
  rw [submodule.mem_sup, mem_vanishing_ideal],
  rintro âŸ¨f, hf, g, hg, rflâŸ© x âŸ¨hxt, hxt'âŸ©,
  rw mem_vanishing_ideal at hf hg,
  apply submodule.add_mem; solve_by_elim
end

lemma mem_compl_zero_locus_iff_not_mem {f : A} {I : projective_spectrum ğ’œ} :
  I âˆˆ (zero_locus ğ’œ {f} : set (projective_spectrum ğ’œ))á¶œ â†” f âˆ‰ I.as_homogeneous_ideal :=
by rw [set.mem_compl_eq, mem_zero_locus, set.singleton_subset_iff]; refl

/-- The Zariski topology on the prime spectrum of a commutative ring
is defined via the closed sets of the topology:
they are exactly those sets that are the zero locus of a subset of the ring. -/
instance zariski_topology : topological_space (projective_spectrum ğ’œ) :=
topological_space.of_closed (set.range (projective_spectrum.zero_locus ğ’œ))
  (âŸ¨set.univ, by simpâŸ©)
  begin
    intros Zs h,
    rw set.sInter_eq_Inter,
    let f : Zs â†’ set _ := Î» i, classical.some (h i.2),
    have hf : âˆ€ i : Zs, â†‘i = zero_locus ğ’œ (f i) := Î» i, (classical.some_spec (h i.2)).symm,
    simp only [hf],
    exact âŸ¨_, zero_locus_Union ğ’œ _âŸ©
  end
  (by { rintros _ âŸ¨s, rflâŸ© _ âŸ¨t, rflâŸ©, exact âŸ¨_, (union_zero_locus ğ’œ s t).symmâŸ© })

lemma is_open_iff (U : set (projective_spectrum ğ’œ)) :
  is_open U â†” âˆƒ s, Uá¶œ = zero_locus ğ’œ s :=
by simp only [@eq_comm _ Uá¶œ]; refl

lemma is_closed_iff_zero_locus (Z : set (projective_spectrum ğ’œ)) :
  is_closed Z â†” âˆƒ s, Z = zero_locus ğ’œ s :=
by rw [â† is_open_compl_iff, is_open_iff, compl_compl]

lemma is_closed_zero_locus (s : set A) :
  is_closed (zero_locus ğ’œ s) :=
by { rw [is_closed_iff_zero_locus], exact âŸ¨s, rflâŸ© }

lemma zero_locus_vanishing_ideal_eq_closure (t : set (projective_spectrum ğ’œ)) :
  zero_locus ğ’œ (vanishing_ideal ğ’œ t : set A) = closure t :=
begin
  apply set.subset.antisymm,
  { rintro x hx t' âŸ¨ht', htâŸ©,
    obtain âŸ¨fs, rflâŸ© : âˆƒ s, t' = zero_locus ğ’œ s,
    by rwa [is_closed_iff_zero_locus] at ht',
    rw [subset_zero_locus_iff_subset_vanishing_ideal] at ht,
    exact set.subset.trans ht hx },
  { rw (is_closed_zero_locus _ _).closure_subset_iff,
    exact subset_zero_locus_vanishing_ideal ğ’œ t }
end

lemma vanishing_ideal_closure (t : set (projective_spectrum ğ’œ)) :
  vanishing_ideal ğ’œ (closure t) = vanishing_ideal ğ’œ t :=
begin
  have := (gc A ğ’œ).u_l_u_eq_u t,
  dsimp only at this,
  rw zero_locus_vanishing_ideal_eq_closure ğ’œ t at this,
  exact this,
end

section basic_open

/-- `basic_open r` is the open subset containing all prime ideals not containing `r`. -/
def basic_open (r : A) : topological_space.opens (projective_spectrum ğ’œ) :=
{ val := { x | r âˆ‰ x.as_homogeneous_ideal },
  property := âŸ¨{r}, set.ext $ Î» x, set.singleton_subset_iff.trans $ not_not.symmâŸ© }

@[simp] lemma mem_basic_open (f : A) (x : projective_spectrum ğ’œ) :
  x âˆˆ basic_open ğ’œ f â†” f âˆ‰ x.as_homogeneous_ideal := iff.rfl

lemma mem_coe_basic_open (f : A) (x : projective_spectrum ğ’œ) :
  x âˆˆ (â†‘(basic_open ğ’œ f): set (projective_spectrum ğ’œ)) â†” f âˆ‰ x.as_homogeneous_ideal := iff.rfl

lemma is_open_basic_open {a : A} : is_open ((basic_open ğ’œ a) :
  set (projective_spectrum ğ’œ)) :=
(basic_open ğ’œ a).property

@[simp] lemma basic_open_eq_zero_locus_compl (r : A) :
  (basic_open ğ’œ r : set (projective_spectrum ğ’œ)) = (zero_locus ğ’œ {r})á¶œ :=
set.ext $ Î» x, by simpa only [set.mem_compl_eq, mem_zero_locus, set.singleton_subset_iff]

@[simp] lemma basic_open_one : basic_open ğ’œ (1 : A) = âŠ¤ :=
topological_space.opens.ext $ by simp

@[simp] lemma basic_open_zero : basic_open ğ’œ (0 : A) = âŠ¥ :=
topological_space.opens.ext $ by simp

lemma basic_open_mul (f g : A) : basic_open ğ’œ (f * g) = basic_open ğ’œ f âŠ“ basic_open ğ’œ g :=
topological_space.opens.ext $ by {simp [zero_locus_singleton_mul]}

lemma basic_open_mul_le_left (f g : A) : basic_open ğ’œ (f * g) â‰¤ basic_open ğ’œ f :=
by { rw basic_open_mul ğ’œ f g, exact inf_le_left }

lemma basic_open_mul_le_right (f g : A) : basic_open ğ’œ (f * g) â‰¤ basic_open ğ’œ g :=
by { rw basic_open_mul ğ’œ f g, exact inf_le_right }

@[simp] lemma basic_open_pow (f : A) (n : â„•) (hn : 0 < n) :
  basic_open ğ’œ (f ^ n) = basic_open ğ’œ f :=
topological_space.opens.ext $ by simpa using zero_locus_singleton_pow ğ’œ f n hn

lemma basic_open_as_union_of_projection (f : A) :
  basic_open ğ’œ f = â¨† (i : â„•), basic_open ğ’œ (graded_algebra.proj ğ’œ i f) :=
begin
  ext z, split; intro hz,
  { rw mem_coe_basic_open at hz,
    have : âˆƒ i, graded_algebra.proj ğ’œ i f âˆ‰ z.as_homogeneous_ideal,
    { contrapose! hz with H,
      haveI : Î  (i : â„•) (x : ğ’œ i), decidable (x â‰  0) := Î» _, classical.dec_pred _,
      rw â†graded_algebra.sum_support_decompose ğ’œ f,
      apply ideal.sum_mem _ (Î» i hi, H i) },
    obtain âŸ¨i, hiâŸ© := this,
    erw topological_space.opens.mem_Sup,
    exact âŸ¨basic_open ğ’œ (graded_algebra.proj ğ’œ i f), âŸ¨i, rflâŸ©, by rwa mem_basic_openâŸ© },
  { rw mem_coe_basic_open,
    erw topological_space.opens.mem_Sup at hz,
    obtain âŸ¨_, âŸ¨i, rflâŸ©, hzâŸ© := hz,
    intro rid,
    apply hz (z.1.2 i rid) },
end

lemma is_topological_basis_basic_opens : topological_space.is_topological_basis
  (set.range (Î» (r : A), (basic_open ğ’œ r : set (projective_spectrum ğ’œ)))) :=
begin
  apply topological_space.is_topological_basis_of_open_of_nhds,
  { rintros _ âŸ¨r, rflâŸ©,
    exact is_open_basic_open ğ’œ },
  { rintros p U hp âŸ¨s, hsâŸ©,
    rw [â† compl_compl U, set.mem_compl_eq, â† hs, mem_zero_locus, set.not_subset] at hp,
    obtain âŸ¨f, hfs, hfpâŸ© := hp,
    refine âŸ¨basic_open ğ’œ f, âŸ¨f, rflâŸ©, hfp, _âŸ©,
    rw [â† set.compl_subset_compl, â† hs, basic_open_eq_zero_locus_compl, compl_compl],
    exact zero_locus_anti_mono ğ’œ (set.singleton_subset_iff.mpr hfs) }
end

end basic_open

section order

/-!
## The specialization order

We endow `prime_spectrum R` with a partial order,
where `x â‰¤ y` if and only if `y âˆˆ closure {x}`.

TODO: maybe define sober topological spaces, and generalise this instance to those
-/

instance : partial_order (projective_spectrum ğ’œ) :=
subtype.partial_order _

@[simp] lemma as_ideal_le_as_ideal (x y : projective_spectrum ğ’œ) :
  x.as_homogeneous_ideal â‰¤ y.as_homogeneous_ideal â†” x â‰¤ y :=
subtype.coe_le_coe

@[simp] lemma as_ideal_lt_as_ideal (x y : projective_spectrum ğ’œ) :
  x.as_homogeneous_ideal < y.as_homogeneous_ideal â†” x < y :=
subtype.coe_lt_coe

lemma le_iff_mem_closure (x y : projective_spectrum ğ’œ) :
  x â‰¤ y â†” y âˆˆ closure ({x} : set (projective_spectrum ğ’œ)) :=
begin
  rw [â† as_ideal_le_as_ideal, â† zero_locus_vanishing_ideal_eq_closure,
    mem_zero_locus, vanishing_ideal_singleton],
  simp only [coe_subset_coe, subtype.coe_le_coe, coe_coe],
end

end order

end projective_spectrum
