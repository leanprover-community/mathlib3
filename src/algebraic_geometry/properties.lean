/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.Scheme
import ring_theory.nilpotent
import topology.sheaves.sheaf_condition.sites
import category_theory.limits.constructions.binary_products
import algebra.category.CommRing.constructions
import ring_theory.integral_domain

/-!
# Basic properties of schemes

We provide some basic properties of schemes

## Main definition
* `algebraic_geometry.is_integral`: A scheme is integral if it is nontrivial and all nontrivial
  components of the structure sheaf are integral domains.
* `algebraic_geometry.is_reduced`: A scheme is reduced if all the components of the structure sheaf
  is reduced.
-/

open topological_space opposite category_theory category_theory.limits Top

namespace algebraic_geometry

variable (X : Scheme)

/-- A scheme `X` is integral if its carrier is nonempty,
and `ùí™‚Çì(U)` is an integral domain for each `U ‚â† ‚àÖ`. -/
class is_integral : Prop :=
(nonempty : nonempty X.carrier . tactic.apply_instance)
(component_integral : ‚àÄ (U : opens X.carrier) [_root_.nonempty U],
  is_domain (X.presheaf.obj (op U)) . tactic.apply_instance)

attribute [instance] is_integral.component_integral is_integral.nonempty

/-- A scheme `X` is reduced if all `ùí™‚Çì(U)` are reduced. -/
class is_reduced : Prop :=
(component_reduced : ‚àÄ U, _root_.is_reduced (X.presheaf.obj (op U)) . tactic.apply_instance)

attribute [instance] is_reduced.component_reduced

@[priority 900]
instance is_reduced_of_is_integral [is_integral X] : is_reduced X :=
begin
  constructor,
  intro U,
  cases U.1.eq_empty_or_nonempty,
  { have : U = ‚àÖ := subtype.eq h,
    haveI := CommRing.subsingleton_of_is_terminal (X.sheaf.is_terminal_of_eq_empty this),
    change _root_.is_reduced (X.sheaf.val.obj (op U)),
    apply_instance },
  { haveI : nonempty U := by simpa, apply_instance }
end

instance is_irreducible_of_is_integral [is_integral X] : irreducible_space X.carrier :=
begin
  by_contradiction H,
  replace H : ¬¨ is_preirreducible (‚ä§ : set X.carrier) := Œª h,
    H { to_preirreducible_space := ‚ü®h‚ü©, to_nonempty := infer_instance },
  simp_rw [is_preirreducible_iff_closed_union_closed, not_forall, not_or_distrib] at H,
  rcases H with ‚ü®S, T, hS, hT, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©,
  erw not_forall at h‚ÇÇ h‚ÇÉ,
  simp_rw not_forall at h‚ÇÇ h‚ÇÉ,
  haveI : nonempty (‚ü®S·∂ú, hS.1‚ü© : opens X.carrier) := ‚ü®‚ü®_, h‚ÇÇ.some_spec.some_spec‚ü©‚ü©,
  haveI : nonempty (‚ü®T·∂ú, hT.1‚ü© : opens X.carrier) := ‚ü®‚ü®_, h‚ÇÉ.some_spec.some_spec‚ü©‚ü©,
  haveI : nonempty (‚ü®S·∂ú, hS.1‚ü© ‚äî ‚ü®T·∂ú, hT.1‚ü© : opens X.carrier) :=
    ‚ü®‚ü®_, or.inl h‚ÇÇ.some_spec.some_spec‚ü©‚ü©,
  let e : X.presheaf.obj _ ‚âÖ CommRing.of _ := (X.sheaf.is_product_of_disjoint ‚ü®_, hS.1‚ü© ‚ü®_, hT.1‚ü© _)
    .cone_point_unique_up_to_iso (CommRing.prod_fan_is_limit _ _),
  apply_with false_of_nontrivial_of_product_domain { instances := ff },
  { exact e.symm.CommRing_iso_to_ring_equiv.is_domain _ },
  { apply X.to_LocallyRingedSpace.component_nontrivial },
  { apply X.to_LocallyRingedSpace.component_nontrivial },
  { ext x,
    split,
    { rintros ‚ü®hS,hT‚ü©,
      cases h‚ÇÅ (show x ‚àà ‚ä§, by trivial),
      exacts [hS h, hT h] },
    { intro x, exact x.rec _ } }
end

lemma closed_eq_top_iff_nonempty_open_subset {Œ± : Type*} [topological_space Œ±]
  [preirreducible_space Œ±]
  {U Z : set Œ±} (hU : is_open U) (hU' : nonempty U) (hZ : is_closed Z) :
  Z = ‚ä§ ‚Üî U ‚â§ Z :=
begin
  split,
  { exact Œª h, h.symm ‚ñ∏ le_top },
  intro h,
  have := mt (nonempty_preirreducible_inter hU (is_open_compl_iff.mpr hZ)
    ((set.nonempty_coe_sort _).mp hU')),
  rw [set.inter_compl_nonempty_iff, set.not_nonempty_iff_eq_empty, not_not] at this,
  exact set.compl_empty_iff.mp (this h),
end

lemma has_generic_point [is_integral X] :
  ‚àÉ (x : X.carrier), closure ({x} : set X.carrier) = ‚ä§ :=
begin
  haveI : irreducible_space X.X := show irreducible_space X.carrier, by apply_instance,
  obtain ‚ü®‚ü®U, hU‚ü©, R, ‚ü®e‚ü©‚ü© := X.local_affine (nonempty.some infer_instance),
  haveI : nonempty (U.open_embedding.is_open_map.functor.obj ‚ä§) := ‚ü®‚ü®_, ‚ü®_, hU‚ü©, trivial, rfl‚ü©‚ü©,
  haveI := ((LocallyRingedSpace.Œì.map_iso e.op).symm ‚â™‚â´ Spec_Œì_identity.app R :
    X.presheaf.obj (op $ U.open_embedding.is_open_map.functor.obj ‚ä§) ‚âÖ R)
      .CommRing_iso_to_ring_equiv.symm.is_domain _,
  let e' : prime_spectrum R ‚âÉ‚Çú U.1 :=
    (homeo_of_iso $ LocallyRingedSpace.forget_to_Top.map_iso e).symm,
  let x := e' ‚ü®0, ideal.bot_prime‚ü©,
  use x.1,
  rw closed_eq_top_iff_nonempty_open_subset U.2 (nonempty.intro ‚ü®_, hU‚ü©) is_closed_closure,
  intros y hy,
  have : closure ({‚ü®0, ideal.bot_prime‚ü©} : set $ prime_spectrum R) = ‚ä§ :=
    eq_top_iff.mpr (Œª _ _, (prime_spectrum.le_iff_mem_closure _ _).mp bot_le),
  apply_fun (set.image e') at this,
  rw [e'.image_closure, set.image_singleton] at this,
  have h' := @closure_subtype _ _ U.1 ‚ü®y, hy‚ü© ({x} : set U),
  rw [set.image_singleton, subtype.coe_mk] at h',
  rw [subtype.val_eq_coe, ‚Üê h', this, set.top_eq_univ, set.image_univ,
    set.range_iff_surjective.mpr e'.surjective],
  trivial
end

noncomputable
def Scheme.generic_point [is_integral X] : X.carrier := (has_generic_point X).some

lemma Scheme.generic_point_closure [is_integral X] :
  closure ({X.generic_point} : set X.carrier) = ‚ä§ :=
(has_generic_point X).some_spec

lemma Scheme.generic_point_mem_nonempty_open [is_integral X]
  (U : opens X.carrier) (hU : nonempty U) : X.generic_point ‚àà U :=
begin
  by_contra,
  have := (is_closed.closure_subset_iff (is_closed_compl_iff.mpr U.prop)).mpr
    (set.singleton_subset_iff.mpr h),
  rw [X.generic_point_closure, set.top_eq_univ, set.univ_subset_iff] at this,
  have : hU.some.1 ‚àà (U : set X.carrier)·∂ú := by { rw this, trivial },
  exact this hU.some.2
end

end algebraic_geometry
