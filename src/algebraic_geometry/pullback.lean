/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.gluing
import category_theory.limits.opposites
import algebraic_geometry.Gamma_Spec_adjunction

/-!
# Fibred products of schemes

In this file we construct the fibred product of schemes via gluing.
We roughly follow [har77] Theorem 3.3.

-/
universes v u
noncomputable theory

open category_theory category_theory.limits algebraic_geometry
namespace algebraic_geometry.Scheme

variables {C : Type u} [category.{v} C]

variables {X Y Z : Scheme.{u}} (ğ’° : open_cover.{u} X) (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
variables [âˆ€ x, has_pullback (ğ’°.map x â‰« f) g]

/-- (Xáµ¢ Ã—[Z] Y) Ã—[X] Xâ±¼ -/
def V (x y : ğ’°.J) : Scheme :=
pullback ((pullback.fst : pullback ((ğ’°.map x) â‰« f) g âŸ¶ _) â‰« (ğ’°.map x)) (ğ’°.map y)

def t (x y : ğ’°.J) : V ğ’° f g x y âŸ¶ V ğ’° f g y x :=
begin
  haveI : has_pullback (pullback.snd â‰« ğ’°.map x â‰« f) g :=
    has_pullback_assoc_symm (ğ’°.map y) (ğ’°.map x) (ğ’°.map x â‰« f) g,
  haveI : has_pullback (pullback.snd â‰« ğ’°.map y â‰« f) g :=
    has_pullback_assoc_symm (ğ’°.map x) (ğ’°.map y) (ğ’°.map y â‰« f) g,
  refine (pullback_symmetry _ _).hom â‰« _,
  refine (pullback_assoc _ _ _ _).inv â‰« _,
  change pullback _ _ âŸ¶ pullback _ _,
  refine _ â‰« (pullback_symmetry _ _).hom,
  refine _ â‰« (pullback_assoc _ _ _ _).hom,
  refine pullback.map _ _ _ _ (pullback_symmetry _ _).hom (ğŸ™ _) (ğŸ™ _) _ _,
  rw [pullback_symmetry_hom_comp_snd_assoc, pullback.condition_assoc, category.comp_id],
  rw [category.comp_id, category.id_comp]
end

@[simp, reassoc]
lemma t_fst_fst (x y : ğ’°.J) : t ğ’° f g x y â‰« pullback.fst â‰« pullback.fst = pullback.snd :=
by { delta t, simp }

@[simp, reassoc]
lemma t_fst_snd (x y : ğ’°.J) :
  t ğ’° f g x y â‰« pullback.fst â‰« pullback.snd = pullback.fst â‰« pullback.snd :=
by { delta t, simp }

@[simp, reassoc]
lemma t_snd (x y : ğ’°.J) :
  t ğ’° f g x y â‰« pullback.snd = pullback.fst â‰« pullback.fst :=
by { delta t, simp }

lemma t_id (x : ğ’°.J) : t ğ’° f g x x = ğŸ™ _ :=
begin
  apply pullback.hom_ext; rw category.id_comp,
  apply pullback.hom_ext,
  { rw â† cancel_mono (ğ’°.map x),
    simp [pullback.condition] },
  { simp },
  { rw â† cancel_mono (ğ’°.map x),
    simp [pullback.condition] }
end

abbreviation fV (x y : ğ’°.J) : V ğ’° f g x y âŸ¶ pullback ((ğ’°.map x) â‰« f) g := pullback.fst

/-- (Xáµ¢ Ã—[Z] Y) Ã—[X] Xâ±¼ Ã—[Xáµ¢ Ã—[Z] Y] (Xáµ¢ Ã—[Z] Y) Ã—[X] Xâ‚–  -/
def t' (x y z : ğ’°.J) :
  pullback (fV ğ’° f g x y) (fV ğ’° f g x z) âŸ¶ pullback (fV ğ’° f g y z) (fV ğ’° f g y x) :=
begin
  refine (pullback_right_pullback_fst_iso _ _ _).hom â‰« _,
  refine _ â‰« (pullback_symmetry _ _).hom,
  refine _ â‰« (pullback_right_pullback_fst_iso _ _ _).inv,
  refine pullback.map _ _ _ _ (t ğ’° f g x y) (ğŸ™ _) (ğŸ™ _) _ _,
  { simp [â† pullback.condition] },
  { simp }
end

section end

@[simp, reassoc]
lemma t'_fst_fst_fst (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« pullback.fst â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd :=
by { delta t', simp }

@[simp, reassoc]
lemma t'_fst_fst_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« pullback.fst â‰« pullback.fst â‰« pullback.snd =
    pullback.fst â‰« pullback.fst â‰« pullback.snd :=
by { delta t', simp }

@[simp, reassoc]
lemma t'_fst_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« pullback.fst â‰« pullback.snd = pullback.snd â‰« pullback.snd :=
by { delta t', simp }

@[simp, reassoc]
lemma t'_snd_fst_fst (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« pullback.snd â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd :=
by { delta t', simp }

@[simp, reassoc]
lemma t'_snd_fst_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« pullback.snd â‰« pullback.fst â‰« pullback.snd =
    pullback.fst â‰« pullback.fst â‰« pullback.snd :=
by { delta t', simp }

@[simp, reassoc]
lemma t'_snd_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« pullback.snd â‰« pullback.snd = pullback.fst â‰« pullback.fst â‰« pullback.fst :=
by { delta t', simp, }

lemma cocycle_fst_fst_fst (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« t' ğ’° f g y z x â‰« t' ğ’° f g z x y â‰« pullback.fst â‰« pullback.fst â‰«
  pullback.fst = pullback.fst â‰« pullback.fst â‰« pullback.fst :=
by simp

lemma cocycle_fst_fst_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« t' ğ’° f g y z x â‰« t' ğ’° f g z x y â‰« pullback.fst â‰« pullback.fst â‰«
  pullback.snd = pullback.fst â‰« pullback.fst â‰« pullback.snd :=
by simp

lemma cocycle_fst_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« t' ğ’° f g y z x â‰« t' ğ’° f g z x y â‰« pullback.fst â‰« pullback.snd =
    pullback.fst â‰« pullback.snd :=
by simp

lemma cocycle_snd_fst_fst (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« t' ğ’° f g y z x â‰« t' ğ’° f g z x y â‰« pullback.snd â‰« pullback.fst â‰«
  pullback.fst = pullback.snd â‰« pullback.fst â‰« pullback.fst :=
by { rw â† cancel_mono (ğ’°.map x), simp [pullback.condition_assoc, pullback.condition] }

lemma cocycle_snd_fst_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« t' ğ’° f g y z x â‰« t' ğ’° f g z x y â‰« pullback.snd â‰« pullback.fst â‰«
  pullback.snd = pullback.snd â‰« pullback.fst â‰« pullback.snd :=
by { simp [pullback.condition_assoc, pullback.condition] }

lemma cocycle_snd_snd (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« t' ğ’° f g y z x â‰« t' ğ’° f g z x y â‰« pullback.snd â‰« pullback.snd =
    pullback.snd â‰« pullback.snd :=
by simp

lemma cocycle (x y z : ğ’°.J) :
  t' ğ’° f g x y z â‰« t' ğ’° f g y z x â‰« t' ğ’° f g z x y = ğŸ™ _ :=
begin
  apply pullback.hom_ext; rw category.id_comp,
  apply pullback.hom_ext,
  apply pullback.hom_ext,
  simp_rw category.assoc,
  exact cocycle_fst_fst_fst ğ’° f g x y z,
  simp_rw category.assoc,
  exact cocycle_fst_fst_snd ğ’° f g x y z,
  simp_rw category.assoc,
  exact cocycle_fst_snd ğ’° f g x y z,
  apply pullback.hom_ext,
  apply pullback.hom_ext,
  simp_rw category.assoc,
  exact cocycle_snd_fst_fst ğ’° f g x y z,
  simp_rw category.assoc,
  exact cocycle_snd_fst_snd ğ’° f g x y z,
  simp_rw category.assoc,
  exact cocycle_snd_snd ğ’° f g x y z
end

@[simps]
def gluing : Scheme.glue_data.{u} :=
{ J := ğ’°.J,
  U := Î» x, pullback ((ğ’°.map x) â‰« f) g,
  V := Î» âŸ¨x, yâŸ©, V ğ’° f g x y, -- pâ»Â¹(Xáµ¢ âˆ© Xâ±¼)
  f := Î» x y, pullback.fst,
  f_id := Î» x, infer_instance,
  f_open := infer_instance,
  t := Î» x y, t ğ’° f g x y,
  t_id := Î» x, t_id ğ’° f g x,
  t' := Î» x y z, t' ğ’° f g x y z,
  t_fac := Î» x y z, begin
    apply pullback.hom_ext,
    apply pullback.hom_ext,
    all_goals { simp }
  end,
  cocycle := Î» x y z, cocycle ğ’° f g x y z }

section end

def p1 : (gluing ğ’° f g).glued âŸ¶ X :=
begin
  fapply multicoequalizer.desc,
  exact Î» x, pullback.fst â‰« ğ’°.map x,
  rintro âŸ¨x,yâŸ©,
  change pullback.fst â‰« _ â‰« ğ’°.map x = (_ â‰« _) â‰« _ â‰« ğ’°.map y,
  rw pullback.condition,
  rw â† category.assoc,
  congr' 1,
  rw category.assoc,
  exact (t_fst_fst _ _ _ _ _).symm
end

def p2 : (gluing ğ’° f g).glued âŸ¶ Y :=
begin
  fapply multicoequalizer.desc,
  exact Î» x, pullback.snd,
  rintro âŸ¨x,yâŸ©,
  change pullback.fst â‰« _ = (_ â‰« _) â‰« _,
  rw category.assoc,
  exact (t_fst_snd _ _ _ _ _).symm
end

section end

lemma p_comm : p1 ğ’° f g â‰« f = p2 ğ’° f g â‰« g :=
begin
  apply multicoequalizer.hom_ext,
  intro x,
  erw [multicoequalizer.Ï€_desc_assoc, multicoequalizer.Ï€_desc_assoc],
  rw [category.assoc, pullback.condition]
end

section end

variable (s : pullback_cone f g)

def pullback_map (x y : ğ’°.J) :
  pullback ((ğ’°.pullback_cover s.fst).map x) ((ğ’°.pullback_cover s.fst).map y) âŸ¶
    (gluing ğ’° f g).V âŸ¨x, yâŸ© :=
begin
  change pullback pullback.fst pullback.fst âŸ¶ pullback _ _,
  refine (pullback_right_pullback_fst_iso _ _ _).hom â‰« _,
  refine pullback.map _ _ _ _ _ (ğŸ™ _) (ğŸ™ _) _ _,
  { exact (pullback_symmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (category.id_comp _).symm s.condition },
  { simpa using pullback.condition },
  { simp }
end

section end

@[reassoc]
lemma pullback_map_fst (x y : ğ’°.J) :
  pullback_map ğ’° f g s x y â‰« pullback.fst = pullback.fst â‰«
    (pullback_symmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (category.id_comp _).symm s.condition :=
by { delta pullback_map, simp }

@[reassoc]
lemma pullback_map_snd (x y : ğ’°.J) :
  pullback_map ğ’° f g s x y â‰« pullback.snd = pullback.snd â‰« pullback.snd  :=
by { delta pullback_map, simp }


def glued_lift : s.X âŸ¶ (gluing ğ’° f g).glued :=
begin
  fapply (ğ’°.pullback_cover s.fst).glue_morphisms,
  { exact Î» x, (pullback_symmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (category.id_comp _).symm s.condition â‰«
      (gluing ğ’° f g).Î¹ x },
  intros x y,
  rw â† pullback_map_fst_assoc,
  have : _ = pullback.fst â‰« _ :=
    (gluing ğ’° f g).glue_condition x y,
  rw â† this,
  rw [gluing_to_glue_data_t, gluing_to_glue_data_f],
  simp_rw â† category.assoc,
  congr' 1,
  apply pullback.hom_ext; simp_rw category.assoc,
  { rw t_fst_fst,
    rw pullback_map_snd,
    congr' 1,
    rw [â† iso.inv_comp_eq, pullback_symmetry_inv_comp_snd],
    erw pullback.lift_fst,
    rw category.comp_id },
  { rw t_fst_snd,
    rw pullback_map_fst_assoc,
    erw [pullback.lift_snd, pullback.lift_snd],
    rw [pullback_symmetry_hom_comp_snd_assoc, pullback_symmetry_hom_comp_snd_assoc],
    exact pullback.condition_assoc _ }
end

section end

lemma glued_lift_p1 : glued_lift ğ’° f g s â‰« p1 ğ’° f g = s.fst :=
begin
  rw â† cancel_epi (ğ’°.pullback_cover s.fst).from_glued,
  apply multicoequalizer.hom_ext,
  intro b,
  erw multicoequalizer.Ï€_desc_assoc,
  erw multicoequalizer.Ï€_desc_assoc,
  delta glued_lift,
  simp_rw â† category.assoc,
  rw (ğ’°.pullback_cover s.fst).Î¹_glue_morphisms,
  simp_rw category.assoc,
  erw [multicoequalizer.Ï€_desc, pullback.lift_fst_assoc, pullback.condition, category.comp_id],
  rw pullback_symmetry_hom_comp_fst_assoc,
end

lemma glued_lift_p2 : glued_lift ğ’° f g s â‰« p2 ğ’° f g = s.snd :=
begin
  rw â† cancel_epi (ğ’°.pullback_cover s.fst).from_glued,
  apply multicoequalizer.hom_ext,
  intro b,
  erw multicoequalizer.Ï€_desc_assoc,
  erw multicoequalizer.Ï€_desc_assoc,
  delta glued_lift,
  simp_rw â† category.assoc,
  rw (ğ’°.pullback_cover s.fst).Î¹_glue_morphisms,
  simp_rw category.assoc,
  erw [multicoequalizer.Ï€_desc, pullback.lift_snd],
  rw pullback_symmetry_hom_comp_snd_assoc,
  refl
end

section end

namespace open_cover
lemma hom_ext (fâ‚ fâ‚‚ : X âŸ¶ Y) (h : âˆ€ x, ğ’°.map x â‰« fâ‚ = ğ’°.map x â‰« fâ‚‚) : fâ‚ = fâ‚‚ :=
begin
  rw â† cancel_epi ğ’°.from_glued,
  apply multicoequalizer.hom_ext,
  intro x,
  erw multicoequalizer.Ï€_desc_assoc,
  erw multicoequalizer.Ï€_desc_assoc,
  exact h x,
end

end open_cover

def pullback_p1_Î¹_Î¹ (x y : ğ’°.J) :
  pullback (pullback.fst : pullback (p1 ğ’° f g) (ğ’°.map x) âŸ¶ _) ((gluing ğ’° f g).Î¹ y) âŸ¶
    V ğ’° f g y x :=
(pullback_symmetry _ _ â‰ªâ‰«
  (pullback_right_pullback_fst_iso (p1 ğ’° f g) (ğ’°.map x) _)).hom â‰«
    (pullback.congr_hom (multicoequalizer.Ï€_desc _ _ _ _ _) rfl).hom

@[simp, reassoc] lemma pullback_p1_Î¹_Î¹_fst (x y : ğ’°.J) :
  pullback_p1_Î¹_Î¹ ğ’° f g x y â‰« pullback.fst = pullback.snd :=
by { delta pullback_p1_Î¹_Î¹, simp }

@[simp, reassoc] lemma pullback_p1_Î¹_Î¹_snd (x y : ğ’°.J) :
  pullback_p1_Î¹_Î¹ ğ’° f g x y â‰« pullback.snd = pullback.fst â‰« pullback.snd :=
by { delta pullback_p1_Î¹_Î¹, simp }

lemma lift_p1_Î¹_Î¹_eq (x : ğ’°.J) : pullback.lift pullback.snd (pullback.fst â‰« p2 ğ’° f g)
  (by rw [â† pullback.condition_assoc, category.assoc, p_comm]) â‰«
  (gluing ğ’° f g).Î¹ x = (pullback.fst : pullback (p1 ğ’° f g) (ğ’°.map x) âŸ¶ _) :=
begin
  apply ((gluing ğ’° f g).open_cover.pullback_cover pullback.fst).hom_ext,
  intro y,
  dsimp only [open_cover.pullback_cover],
  transitivity pullback_p1_Î¹_Î¹ ğ’° f g x y â‰« fV ğ’° f g y x â‰« (gluing ğ’° f g).Î¹ _,
  { rw â† (show _ = fV ğ’° f g y x â‰« _, from (gluing ğ’° f g).glue_condition y x),
    simp_rw â† category.assoc,
    congr' 1,
    rw [gluing_to_glue_data_f, gluing_to_glue_data_t],
    apply pullback.hom_ext; simp_rw category.assoc,
    { rw [t_fst_fst, pullback.lift_fst, pullback_p1_Î¹_Î¹_snd] },
    { rw [t_fst_snd, pullback.lift_snd, pullback_p1_Î¹_Î¹_fst_assoc,
        pullback.condition_assoc], erw multicoequalizer.Ï€_desc } },
  { rw [pullback.condition, â† category.assoc],
    congr' 1,
    apply pullback.hom_ext,
    { simp },
    { simp } }
end

section end

def pullback_p1_iso (x : ğ’°.J) :
  pullback (p1 ğ’° f g) (ğ’°.map x) â‰… pullback (ğ’°.map x â‰« f) g :=
begin
  fsplit,
  exact pullback.lift pullback.snd (pullback.fst â‰« p2 ğ’° f g)
    (by rw [â† pullback.condition_assoc, category.assoc, p_comm]),
  refine pullback.lift ((gluing ğ’° f g).Î¹ x) pullback.fst
    (by erw multicoequalizer.Ï€_desc),
  { apply pullback.hom_ext,
    { simpa using lift_p1_Î¹_Î¹_eq ğ’° f g x },
    { simp } },
  { apply pullback.hom_ext,
    { simp },
    { simp, erw multicoequalizer.Ï€_desc } },
end

section end

@[simp, reassoc] lemma pullback_p1_iso_hom_fst (x : ğ’°.J) :
  (pullback_p1_iso ğ’° f g x).hom â‰« pullback.fst = pullback.snd :=
by { delta pullback_p1_iso, simp }

@[simp, reassoc] lemma pullback_p1_iso_hom_snd (x : ğ’°.J) :
  (pullback_p1_iso ğ’° f g x).hom â‰« pullback.snd = pullback.fst â‰« p2 ğ’° f g :=
by { delta pullback_p1_iso, simp, }

@[simp, reassoc] lemma pullback_p1_iso_inv_fst (x : ğ’°.J) :
  (pullback_p1_iso ğ’° f g x).inv â‰« pullback.fst = (gluing ğ’° f g).Î¹ x :=
by { delta pullback_p1_iso, simp }

@[simp, reassoc] lemma pullback_p1_iso_inv_snd (x : ğ’°.J) :
  (pullback_p1_iso ğ’° f g x).inv â‰« pullback.snd = pullback.fst :=
by { delta pullback_p1_iso, simp }

@[simp, reassoc]
lemma pullback_p1_iso_hom_Î¹ (x : ğ’°.J) :
  (pullback_p1_iso ğ’° f g x).hom â‰« (gluing ğ’° f g).Î¹ x = pullback.fst :=
by rw [â† pullback_p1_iso_inv_fst, iso.hom_inv_id_assoc]

lemma glued_is_limit : is_limit (pullback_cone.mk _ _ (p_comm ğ’° f g)) :=
begin
  apply pullback_cone.is_limit_aux',
  intro s,
  use glued_lift ğ’° f g s,
  use glued_lift_p1 ğ’° f g s,
  use glued_lift_p2 ğ’° f g s,
  intros m hâ‚ hâ‚‚,
  change m â‰« p1 ğ’° f g = _ at hâ‚,
  change m â‰« p2 ğ’° f g = _ at hâ‚‚,
  apply (ğ’°.pullback_cover s.fst).hom_ext,
  intro x,
  rw open_cover.pullback_cover_map,
  have := pullback_right_pullback_fst_iso (p1 ğ’° f g) (ğ’°.map x) m
    â‰ªâ‰« pullback.congr_hom hâ‚ rfl,
  erw (ğ’°.pullback_cover s.fst).Î¹_glue_morphisms,
  rw â† cancel_epi (pullback_right_pullback_fst_iso (p1 ğ’° f g) (ğ’°.map x) m
    â‰ªâ‰« pullback.congr_hom hâ‚ rfl).hom,
  rw [iso.trans_hom, category.assoc, pullback.congr_hom_hom, pullback.lift_fst_assoc,
    category.comp_id, pullback_right_pullback_fst_iso_hom_fst_assoc, pullback.condition],
  transitivity pullback.snd â‰« (pullback_p1_iso ğ’° f g _).hom â‰« (gluing ğ’° f g).Î¹ _,
  { congr' 1, rw â† pullback_p1_iso_hom_Î¹ },
  simp_rw â† category.assoc,
  congr' 1,
  apply pullback.hom_ext,
  { simp only [category.comp_id, pullback_right_pullback_fst_iso_hom_snd, category.assoc,
      pullback_p1_iso_hom_fst, pullback.lift_snd, pullback.lift_fst,
      pullback_symmetry_hom_comp_fst] },
  { simp only [category.comp_id, pullback_right_pullback_fst_iso_hom_fst_assoc,
    pullback_p1_iso_hom_snd, category.assoc, pullback.lift_fst_assoc,
    pullback_symmetry_hom_comp_snd_assoc, pullback.lift_snd],
    rw [â† pullback.condition_assoc, hâ‚‚] }
end

lemma has_pullback_of_cover : has_pullback f g := âŸ¨âŸ¨âŸ¨_, glued_is_limit ğ’° f gâŸ©âŸ©âŸ©

instance : has_limits CommRingáµ’áµ– := has_limits_op_of_has_colimits

instance affine_has_pullback {A B C : CommRing}
  (f : Spec.obj (opposite.op A) âŸ¶ Spec.obj (opposite.op C))
  (g : Spec.obj (opposite.op B) âŸ¶ Spec.obj (opposite.op C)) : has_pullback f g :=
begin
  rw [â† Spec.image_preimage f, â† Spec.image_preimage g],
  exact âŸ¨âŸ¨âŸ¨_,is_limit_of_has_pullback_of_preserves_limit
    Spec (Spec.preimage f) (Spec.preimage g)âŸ©âŸ©âŸ©
end

lemma affine_affine_has_pullback {B C : CommRing} {X : Scheme}
  (f : X âŸ¶ Spec.obj (opposite.op C))
  (g : Spec.obj (opposite.op B) âŸ¶ Spec.obj (opposite.op C)) : has_pullback f g :=
has_pullback_of_cover X.affine_cover f g

instance base_affine_has_pullback {C : CommRing} {X Y : Scheme}
  (f : X âŸ¶ Spec.obj (opposite.op C))
  (g : Y âŸ¶ Spec.obj (opposite.op C)) : has_pullback f g :=
@@has_pullback_symmetry _ _ _
  (@@has_pullback_of_cover Y.affine_cover g f
    (Î» x, @@has_pullback_symmetry _ _ _ $ affine_affine_has_pullback _ _))

instance left_affine_comp_pullback_has_pullback {X Y Z : Scheme}
  (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (x : Z.affine_cover.J) :
    has_pullback ((Z.affine_cover.pullback_cover f).map x â‰« f) g :=
begin
  let Xáµ¢ := pullback f (Z.affine_cover.map x),
  let Yáµ¢ := pullback g (Z.affine_cover.map x),
  let W := pullback (pullback.snd : Yáµ¢ âŸ¶ _) (pullback.snd : Xáµ¢ âŸ¶ _),
  have := big_square_is_pullback (pullback.fst : W âŸ¶ _) (pullback.fst : Yáµ¢ âŸ¶ _)
    (pullback.snd : Xáµ¢ âŸ¶ _) (Z.affine_cover.map x) pullback.snd pullback.snd g
    pullback.condition.symm pullback.condition.symm
      (pullback_cone.flip_is_limit $ pullback_is_pullback _ _)
      (pullback_cone.flip_is_limit $ pullback_is_pullback _ _),
  have : has_pullback (pullback.snd â‰« Z.affine_cover.map x : Xáµ¢ âŸ¶ _) g :=
    âŸ¨âŸ¨âŸ¨_,thisâŸ©âŸ©âŸ©,
  rw â† pullback.condition at this,
  exact this,
end

instance {X Y Z : Scheme} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : has_pullback f g :=
has_pullback_of_cover (Z.affine_cover.pullback_cover f) f g

end algebraic_geometry.Scheme
