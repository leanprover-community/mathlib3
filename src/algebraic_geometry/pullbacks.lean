/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.gluing
import category_theory.limits.opposites
import algebraic_geometry.AffineScheme
import category_theory.limits.shapes.diagonal

/-!
# Fibred products of schemes

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we construct the fibred product of schemes via gluing.
We roughly follow [har77] Theorem 3.3.

In particular, the main construction is to show that for an open cover `{ Uáµ¢ }` of `X`, if there
exist fibred products `Uáµ¢ Ã—[Z] Y` for each `i`, then there exists a fibred product `X Ã—[Z] Y`.

Then, for constructing the fibred product for arbitrary schemes `X, Y, Z`, we can use the
construction to reduce to the case where `X, Y, Z` are all affine, where fibred products are
constructed via tensor products.

-/
universes v u
noncomputable theory

open category_theory category_theory.limits algebraic_geometry
namespace algebraic_geometry.Scheme

namespace pullback

variables {C : Type u} [category.{v} C]

variables {X Y Z : Scheme.{u}} (ğ’° : open_cover.{u} X) (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
variables [âˆ€ i, has_pullback (ğ’°.map i â‰« f) g]

/-- The intersection of `Uáµ¢ Ã—[Z] Y` and `Uâ±¼ Ã—[Z] Y` is given by (Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼ -/
def V (i j : ğ’°.J) : Scheme :=
pullback ((pullback.fst : pullback ((ğ’°.map i) â‰« f) g âŸ¶ _) â‰« (ğ’°.map i)) (ğ’°.map j)

/-- The canonical transition map `(Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼ âŸ¶ (Uâ±¼ Ã—[Z] Y) Ã—[X] Uáµ¢` given by the fact
that pullbacks are associative and symmetric. -/
def t (i j : ğ’°.J) : V ğ’° f g i j âŸ¶ V ğ’° f g j i :=
begin
  haveI : has_pullback (pullback.snd â‰« ğ’°.map i â‰« f) g :=
    has_pullback_assoc_symm (ğ’°.map j) (ğ’°.map i) (ğ’°.map i â‰« f) g,
  haveI : has_pullback (pullback.snd â‰« ğ’°.map j â‰« f) g :=
    has_pullback_assoc_symm (ğ’°.map i) (ğ’°.map j) (ğ’°.map j â‰« f) g,
  refine (pullback_symmetry _ _).hom â‰« _,
  refine (pullback_assoc _ _ _ _).inv â‰« _,
  change pullback _ _ âŸ¶ pullback _ _,
  refine _ â‰« (pullback_symmetry _ _).hom,
  refine _ â‰« (pullback_assoc _ _ _ _).hom,
  refine pullback.map _ _ _ _ (pullback_symmetry _ _).hom (ğŸ™ _) (ğŸ™ _) _ _,
  rw [pullback_symmetry_hom_comp_snd_assoc, pullback.condition_assoc, category.comp_id],
  rw [category.comp_id, category.id_comp]
end

@[simp, reassoc]
lemma t_fst_fst (i j : ğ’°.J) : t ğ’° f g i j â‰« pullback.fst â‰« pullback.fst = pullback.snd :=
begin
  delta t,
  simp only [category.assoc, id.def, pullback_symmetry_hom_comp_fst_assoc,
    pullback_assoc_hom_snd_fst, pullback.lift_fst_assoc, pullback_symmetry_hom_comp_snd,
    pullback_assoc_inv_fst_fst, pullback_symmetry_hom_comp_fst],
end

@[simp, reassoc]
lemma t_fst_snd (i j : ğ’°.J) :
  t ğ’° f g i j â‰« pullback.fst â‰« pullback.snd = pullback.fst â‰« pullback.snd :=
begin
  delta t,
  simp only [pullback_symmetry_hom_comp_snd_assoc, category.comp_id, category.assoc, id.def,
    pullback_symmetry_hom_comp_fst_assoc, pullback_assoc_hom_snd_snd, pullback.lift_snd,
    pullback_assoc_inv_snd],
end

@[simp, reassoc]
lemma t_snd (i j : ğ’°.J) :
  t ğ’° f g i j â‰« pullback.snd = pullback.fst â‰« pullback.fst :=
begin
  delta t,
  simp only [pullback_symmetry_hom_comp_snd_assoc, category.assoc, id.def,
    pullback_symmetry_hom_comp_snd, pullback_assoc_hom_fst, pullback.lift_fst_assoc,
    pullback_symmetry_hom_comp_fst, pullback_assoc_inv_fst_snd],
end

lemma t_id (i : ğ’°.J) : t ğ’° f g i i = ğŸ™ _ :=
begin
  apply pullback.hom_ext; rw category.id_comp,
  apply pullback.hom_ext,
  { rw â† cancel_mono (ğ’°.map i), simp only [pullback.condition, category.assoc, t_fst_fst] },
  { simp only [category.assoc, t_fst_snd]},
  { rw â† cancel_mono (ğ’°.map i),simp only [pullback.condition, t_snd, category.assoc] }
end

/-- The inclusion map of `V i j = (Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼ âŸ¶ Uáµ¢ Ã—[Z] Y`-/
abbreviation fV (i j : ğ’°.J) : V ğ’° f g i j âŸ¶ pullback ((ğ’°.map i) â‰« f) g := pullback.fst

/-- The map `((Xáµ¢ Ã—[Z] Y) Ã—[X] Xâ±¼) Ã—[Xáµ¢ Ã—[Z] Y] ((Xáµ¢ Ã—[Z] Y) Ã—[X] Xâ‚–)` âŸ¶
  `((Xâ±¼ Ã—[Z] Y) Ã—[X] Xâ‚–) Ã—[Xâ±¼ Ã—[Z] Y] ((Xâ±¼ Ã—[Z] Y) Ã—[X] Xáµ¢)` needed for gluing   -/
def t' (i j k : ğ’°.J) :
  pullback (fV ğ’° f g i j) (fV ğ’° f g i k) âŸ¶ pullback (fV ğ’° f g j k) (fV ğ’° f g j i) :=
begin
  refine (pullback_right_pullback_fst_iso _ _ _).hom â‰« _,
  refine _ â‰« (pullback_symmetry _ _).hom,
  refine _ â‰« (pullback_right_pullback_fst_iso _ _ _).inv,
  refine pullback.map _ _ _ _ (t ğ’° f g i j) (ğŸ™ _) (ğŸ™ _) _ _,
  { simp only [â†pullback.condition, category.comp_id, t_fst_fst_assoc] },
  { simp only [category.comp_id, category.id_comp]}
end

section end

@[simp, reassoc]
lemma t'_fst_fst_fst (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« pullback.fst â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd :=
begin
  delta t',
  simp only [category.assoc, pullback_symmetry_hom_comp_fst_assoc,
    pullback_right_pullback_fst_iso_inv_snd_fst_assoc, pullback.lift_fst_assoc, t_fst_fst,
    pullback_right_pullback_fst_iso_hom_fst_assoc],
end

@[simp, reassoc]
lemma t'_fst_fst_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« pullback.fst â‰« pullback.fst â‰« pullback.snd =
    pullback.fst â‰« pullback.fst â‰« pullback.snd :=
begin
  delta t',
  simp only [category.assoc, pullback_symmetry_hom_comp_fst_assoc,
    pullback_right_pullback_fst_iso_inv_snd_fst_assoc, pullback.lift_fst_assoc, t_fst_snd,
    pullback_right_pullback_fst_iso_hom_fst_assoc],
end

@[simp, reassoc]
lemma t'_fst_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« pullback.fst â‰« pullback.snd = pullback.snd â‰« pullback.snd :=
begin
  delta t',
  simp only [category.comp_id, category.assoc, pullback_symmetry_hom_comp_fst_assoc,
    pullback_right_pullback_fst_iso_inv_snd_snd, pullback.lift_snd,
    pullback_right_pullback_fst_iso_hom_snd],
end

@[simp, reassoc]
lemma t'_snd_fst_fst (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« pullback.snd â‰« pullback.fst â‰« pullback.fst = pullback.fst â‰« pullback.snd :=
begin
  delta t',
  simp only [category.assoc, pullback_symmetry_hom_comp_snd_assoc,
    pullback_right_pullback_fst_iso_inv_fst_assoc, pullback.lift_fst_assoc, t_fst_fst,
    pullback_right_pullback_fst_iso_hom_fst_assoc],
end

@[simp, reassoc]
lemma t'_snd_fst_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« pullback.snd â‰« pullback.fst â‰« pullback.snd =
    pullback.fst â‰« pullback.fst â‰« pullback.snd :=
begin
  delta t',
  simp only [category.assoc, pullback_symmetry_hom_comp_snd_assoc,
    pullback_right_pullback_fst_iso_inv_fst_assoc, pullback.lift_fst_assoc, t_fst_snd,
    pullback_right_pullback_fst_iso_hom_fst_assoc],
end

@[simp, reassoc]
lemma t'_snd_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« pullback.snd â‰« pullback.snd = pullback.fst â‰« pullback.fst â‰« pullback.fst :=
begin
  delta t',
  simp only [category.assoc, pullback_symmetry_hom_comp_snd_assoc,
    pullback_right_pullback_fst_iso_inv_fst_assoc, pullback.lift_fst_assoc, t_snd,
    pullback_right_pullback_fst_iso_hom_fst_assoc],
end

lemma cocycle_fst_fst_fst (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.fst â‰« pullback.fst â‰«
  pullback.fst = pullback.fst â‰« pullback.fst â‰« pullback.fst :=
by simp only [t'_fst_fst_fst, t'_fst_snd, t'_snd_snd]

lemma cocycle_fst_fst_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.fst â‰« pullback.fst â‰«
  pullback.snd = pullback.fst â‰« pullback.fst â‰« pullback.snd :=
by simp only [t'_fst_fst_snd]

lemma cocycle_fst_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.fst â‰« pullback.snd =
    pullback.fst â‰« pullback.snd :=
by simp only [t'_fst_snd, t'_snd_snd, t'_fst_fst_fst]

lemma cocycle_snd_fst_fst (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.snd â‰« pullback.fst â‰«
  pullback.fst = pullback.snd â‰« pullback.fst â‰« pullback.fst :=
begin
  rw â† cancel_mono (ğ’°.map i),
  simp only [pullback.condition_assoc, t'_snd_fst_fst, t'_fst_snd, t'_snd_snd]
end

lemma cocycle_snd_fst_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.snd â‰« pullback.fst â‰«
  pullback.snd = pullback.snd â‰« pullback.fst â‰« pullback.snd :=
by simp only [pullback.condition_assoc, t'_snd_fst_snd]

lemma cocycle_snd_snd (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j â‰« pullback.snd â‰« pullback.snd =
    pullback.snd â‰« pullback.snd :=
by simp only [t'_snd_snd, t'_fst_fst_fst, t'_fst_snd]

-- `by tidy` should solve it, but it times out.
lemma cocycle (i j k : ğ’°.J) :
  t' ğ’° f g i j k â‰« t' ğ’° f g j k i â‰« t' ğ’° f g k i j = ğŸ™ _ :=
begin
  apply pullback.hom_ext; rw category.id_comp,
  { apply pullback.hom_ext,
    { apply pullback.hom_ext,
      { simp_rw category.assoc,
        exact cocycle_fst_fst_fst ğ’° f g i j k },
      { simp_rw category.assoc,
        exact cocycle_fst_fst_snd ğ’° f g i j k } },
    { simp_rw category.assoc,
      exact cocycle_fst_snd ğ’° f g i j k } },
  { apply pullback.hom_ext,
    { apply pullback.hom_ext,
      { simp_rw category.assoc,
        exact cocycle_snd_fst_fst ğ’° f g i j k },
      { simp_rw category.assoc,
        exact cocycle_snd_fst_snd ğ’° f g i j k } },
    { simp_rw category.assoc,
      exact cocycle_snd_snd ğ’° f g i j k } }
end

/-- Given `Uáµ¢ Ã—[Z] Y`, this is the glued fibered product `X Ã—[Z] Y`. -/
@[simps]
def gluing : Scheme.glue_data.{u} :=
{ J := ğ’°.J,
  U := Î» i, pullback ((ğ’°.map i) â‰« f) g,
  V := Î» âŸ¨i, jâŸ©, V ğ’° f g i j, -- `pâ»Â¹(Uáµ¢ âˆ© Uâ±¼)` where `p : Uáµ¢ Ã—[Z] Y âŸ¶ Uáµ¢ âŸ¶ X`.
  f := Î» i j, pullback.fst,
  f_id := Î» i, infer_instance,
  f_open := infer_instance,
  t := Î» i j, t ğ’° f g i j,
  t_id := Î» i, t_id ğ’° f g i,
  t' := Î» i j k, t' ğ’° f g i j k,
  t_fac := Î» i j k, begin
    apply pullback.hom_ext,
    apply pullback.hom_ext,
    all_goals { simp only [t'_snd_fst_fst, t'_snd_fst_snd, t'_snd_snd,
      t_fst_fst, t_fst_snd, t_snd, category.assoc] }
  end,
  cocycle := Î» i j k, cocycle ğ’° f g i j k }

/-- The first projection from the glued scheme into `X`. -/
def p1 : (gluing ğ’° f g).glued âŸ¶ X :=
begin
  fapply multicoequalizer.desc,
  exact Î» i, pullback.fst â‰« ğ’°.map i,
  rintro âŸ¨i, jâŸ©,
  change pullback.fst â‰« _ â‰« ğ’°.map i = (_ â‰« _) â‰« _ â‰« ğ’°.map j,
  rw pullback.condition,
  rw â† category.assoc,
  congr' 1,
  rw category.assoc,
  exact (t_fst_fst _ _ _ _ _).symm
end

/-- The second projection from the glued scheme into `Y`. -/
def p2 : (gluing ğ’° f g).glued âŸ¶ Y :=
begin
  fapply multicoequalizer.desc,
  exact Î» i, pullback.snd,
  rintro âŸ¨i, jâŸ©,
  change pullback.fst â‰« _ = (_ â‰« _) â‰« _,
  rw category.assoc,
  exact (t_fst_snd _ _ _ _ _).symm
end

lemma p_comm : p1 ğ’° f g â‰« f = p2 ğ’° f g â‰« g :=
begin
  apply multicoequalizer.hom_ext,
  intro i,
  erw [multicoequalizer.Ï€_desc_assoc, multicoequalizer.Ï€_desc_assoc],
  rw [category.assoc, pullback.condition]
end

variable (s : pullback_cone f g)

/-- (Implementation)
The canonical map `(s.X Ã—[X] Uáµ¢) Ã—[s.X] (s.X Ã—[X] Uâ±¼) âŸ¶ (Uáµ¢ Ã—[Z] Y) Ã—[X] Uâ±¼`

This is used in `glued_lift`. -/
def glued_lift_pullback_map (i j : ğ’°.J) :
  pullback ((ğ’°.pullback_cover s.fst).map i) ((ğ’°.pullback_cover s.fst).map j) âŸ¶
    (gluing ğ’° f g).V âŸ¨i, jâŸ© :=
begin
  change pullback pullback.fst pullback.fst âŸ¶ pullback _ _,
  refine (pullback_right_pullback_fst_iso _ _ _).hom â‰« _,
  refine pullback.map _ _ _ _ _ (ğŸ™ _) (ğŸ™ _) _ _,
  { exact (pullback_symmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (category.id_comp _).symm s.condition },
  { simpa using pullback.condition },
  { simp only [category.comp_id, category.id_comp] }
end

@[reassoc]
lemma glued_lift_pullback_map_fst (i j : ğ’°.J) :
  glued_lift_pullback_map ğ’° f g s i j â‰« pullback.fst = pullback.fst â‰«
    (pullback_symmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (category.id_comp _).symm s.condition :=
begin
  delta glued_lift_pullback_map,
  simp only [category.assoc, id.def, pullback.lift_fst,
    pullback_right_pullback_fst_iso_hom_fst_assoc],
end
@[reassoc]
lemma glued_lift_pullback_map_snd (i j : ğ’°.J) :
  glued_lift_pullback_map ğ’° f g s i j â‰« pullback.snd = pullback.snd â‰« pullback.snd :=
begin
  delta glued_lift_pullback_map,
  simp only [category.assoc, category.comp_id, id.def, pullback.lift_snd,
    pullback_right_pullback_fst_iso_hom_snd],
end

/--
The lifted map `s.X âŸ¶ (gluing ğ’° f g).glued` in order to show that `(gluing ğ’° f g).glued` is
indeed the pullback.

Given a pullback cone `s`, we have the maps `s.fst â»Â¹' Uáµ¢ âŸ¶ Uáµ¢` and
`s.fst â»Â¹' Uáµ¢ âŸ¶ s.X âŸ¶ Y` that we may lift to a map `s.fst â»Â¹' Uáµ¢ âŸ¶ Uáµ¢ Ã—[Z] Y`.

to glue these into a map `s.X âŸ¶ Uáµ¢ Ã—[Z] Y`, we need to show that the maps agree on
`(s.fst â»Â¹' Uáµ¢) Ã—[s.X] (s.fst â»Â¹' Uâ±¼) âŸ¶ Uáµ¢ Ã—[Z] Y`. This is achieved by showing that both of these
maps factors through `glued_lift_pullback_map`.
-/
def glued_lift : s.X âŸ¶ (gluing ğ’° f g).glued :=
begin
  fapply (ğ’°.pullback_cover s.fst).glue_morphisms,
  { exact Î» i, (pullback_symmetry _ _).hom â‰«
      pullback.map _ _ _ _ (ğŸ™ _) s.snd f (category.id_comp _).symm s.condition â‰«
      (gluing ğ’° f g).Î¹ i },
  intros i j,
  rw â† glued_lift_pullback_map_fst_assoc,
  have : _ = pullback.fst â‰« _ := (gluing ğ’° f g).glue_condition i j,
  rw [â† this, gluing_to_glue_data_t, gluing_to_glue_data_f],
  simp_rw â† category.assoc,
  congr' 1,
  apply pullback.hom_ext; simp_rw category.assoc,
  { rw [t_fst_fst, glued_lift_pullback_map_snd],
    congr' 1,
    rw [â† iso.inv_comp_eq, pullback_symmetry_inv_comp_snd],
    erw pullback.lift_fst,
    rw category.comp_id },
  { rw [t_fst_snd, glued_lift_pullback_map_fst_assoc],
    erw [pullback.lift_snd, pullback.lift_snd],
    rw [pullback_symmetry_hom_comp_snd_assoc, pullback_symmetry_hom_comp_snd_assoc],
    exact pullback.condition_assoc _ }
end

lemma glued_lift_p1 : glued_lift ğ’° f g s â‰« p1 ğ’° f g = s.fst :=
begin
  rw â† cancel_epi (ğ’°.pullback_cover s.fst).from_glued,
  apply multicoequalizer.hom_ext,
  intro b,
  erw [multicoequalizer.Ï€_desc_assoc, multicoequalizer.Ï€_desc_assoc],
  delta glued_lift,
  simp_rw â† category.assoc,
  rw (ğ’°.pullback_cover s.fst).Î¹_glue_morphisms,
  simp_rw category.assoc,
  erw [multicoequalizer.Ï€_desc, pullback.lift_fst_assoc, pullback.condition, category.comp_id],
  rw pullback_symmetry_hom_comp_fst_assoc,
end

lemma glued_lift_p2 : glued_lift ğ’° f g s â‰« p2 ğ’° f g = s.snd :=
begin
  rw â† cancel_epi (ğ’°.pullback_cover s.fst).from_glued,
  apply multicoequalizer.hom_ext,
  intro b,
  erw [multicoequalizer.Ï€_desc_assoc, multicoequalizer.Ï€_desc_assoc],
  delta glued_lift,
  simp_rw â† category.assoc,
  rw (ğ’°.pullback_cover s.fst).Î¹_glue_morphisms,
  simp_rw category.assoc,
  erw [multicoequalizer.Ï€_desc, pullback.lift_snd],
  rw pullback_symmetry_hom_comp_snd_assoc,
  refl
end

/-- (Implementation)
The canonical map `(W Ã—[X] Uáµ¢) Ã—[W] (Uâ±¼ Ã—[Z] Y) âŸ¶ (Uâ±¼ Ã—[Z] Y) Ã—[X] Uáµ¢ = V j i` where `W` is
the glued fibred product.

This is used in `lift_comp_Î¹`. -/
def pullback_fst_Î¹_to_V (i j : ğ’°.J) :
  pullback (pullback.fst : pullback (p1 ğ’° f g) (ğ’°.map i) âŸ¶ _) ((gluing ğ’° f g).Î¹ j) âŸ¶
    V ğ’° f g j i :=
(pullback_symmetry _ _ â‰ªâ‰«
  (pullback_right_pullback_fst_iso (p1 ğ’° f g) (ğ’°.map i) _)).hom â‰«
    (pullback.congr_hom (multicoequalizer.Ï€_desc _ _ _ _ _) rfl).hom

@[simp, reassoc] lemma pullback_fst_Î¹_to_V_fst (i j : ğ’°.J) :
  pullback_fst_Î¹_to_V ğ’° f g i j â‰« pullback.fst = pullback.snd :=
begin
  delta pullback_fst_Î¹_to_V,
  simp only [iso.trans_hom, pullback.congr_hom_hom, category.assoc, pullback.lift_fst,
    category.comp_id, pullback_right_pullback_fst_iso_hom_fst, pullback_symmetry_hom_comp_fst],
end

@[simp, reassoc] lemma pullback_fst_Î¹_to_V_snd (i j : ğ’°.J) :
  pullback_fst_Î¹_to_V ğ’° f g i j â‰« pullback.snd = pullback.fst â‰« pullback.snd :=
begin
  delta pullback_fst_Î¹_to_V,
  simp only [iso.trans_hom, pullback.congr_hom_hom, category.assoc, pullback.lift_snd,
    category.comp_id, pullback_right_pullback_fst_iso_hom_snd, pullback_symmetry_hom_comp_snd_assoc]
end
/-- We show that the map `W Ã—[X] Uáµ¢ âŸ¶ Uáµ¢ Ã—[Z] Y âŸ¶ W` is the first projection, where the
first map is given by the lift of `W Ã—[X] Uáµ¢ âŸ¶ Uáµ¢` and `W Ã—[X] Uáµ¢ âŸ¶ W âŸ¶ Y`.

It suffices to show that the two map agrees when restricted onto `Uâ±¼ Ã—[Z] Y`. In this case,
both maps factor through `V j i` via `pullback_fst_Î¹_to_V` -/
lemma lift_comp_Î¹ (i : ğ’°.J) : pullback.lift pullback.snd (pullback.fst â‰« p2 ğ’° f g)
  (by rw [â† pullback.condition_assoc, category.assoc, p_comm]) â‰«
  (gluing ğ’° f g).Î¹ i = (pullback.fst : pullback (p1 ğ’° f g) (ğ’°.map i) âŸ¶ _) :=
begin
  apply ((gluing ğ’° f g).open_cover.pullback_cover pullback.fst).hom_ext,
  intro j,
  dsimp only [open_cover.pullback_cover],
  transitivity pullback_fst_Î¹_to_V ğ’° f g i j â‰« fV ğ’° f g j i â‰« (gluing ğ’° f g).Î¹ _,
  { rw â† (show _ = fV ğ’° f g j i â‰« _, from (gluing ğ’° f g).glue_condition j i),
    simp_rw â† category.assoc,
    congr' 1,
    rw [gluing_to_glue_data_f, gluing_to_glue_data_t],
    apply pullback.hom_ext; simp_rw category.assoc,
    { rw [t_fst_fst, pullback.lift_fst, pullback_fst_Î¹_to_V_snd] },
    { rw [t_fst_snd, pullback.lift_snd, pullback_fst_Î¹_to_V_fst_assoc,
        pullback.condition_assoc], erw multicoequalizer.Ï€_desc } },
  { rw [pullback.condition, â† category.assoc],
    congr' 1,
    apply pullback.hom_ext,
    { simp only [pullback_fst_Î¹_to_V_fst] },
    { simp only [pullback_fst_Î¹_to_V_fst] } }
end

/-- The canonical isomorphism between `W Ã—[X] Uáµ¢` and `Uáµ¢ Ã—[X] Y`. That is, the preimage of `Uáµ¢` in
`W` along `p1` is indeed `Uáµ¢ Ã—[X] Y`. -/
def pullback_p1_iso (i : ğ’°.J) :
  pullback (p1 ğ’° f g) (ğ’°.map i) â‰… pullback (ğ’°.map i â‰« f) g :=
begin
  fsplit,
  exact pullback.lift pullback.snd (pullback.fst â‰« p2 ğ’° f g)
    (by rw [â† pullback.condition_assoc, category.assoc, p_comm]),
  refine pullback.lift ((gluing ğ’° f g).Î¹ i) pullback.fst
    (by erw multicoequalizer.Ï€_desc),
  { apply pullback.hom_ext,
    { simpa using lift_comp_Î¹ ğ’° f g i },
    { simp only [category.assoc, pullback.lift_snd, pullback.lift_fst, category.id_comp] } },
  { apply pullback.hom_ext,
    { simp only [category.assoc, pullback.lift_fst, pullback.lift_snd, category.id_comp] },
    { simp only [category.assoc, pullback.lift_snd, pullback.lift_fst_assoc, category.id_comp],
      erw multicoequalizer.Ï€_desc } },
end

@[simp, reassoc] lemma pullback_p1_iso_hom_fst (i : ğ’°.J) :
  (pullback_p1_iso ğ’° f g i).hom â‰« pullback.fst = pullback.snd :=
by { delta pullback_p1_iso, simp only [pullback.lift_fst] }

@[simp, reassoc] lemma pullback_p1_iso_hom_snd (i : ğ’°.J) :
  (pullback_p1_iso ğ’° f g i).hom â‰« pullback.snd = pullback.fst â‰« p2 ğ’° f g :=
by { delta pullback_p1_iso, simp only [pullback.lift_snd] }

@[simp, reassoc] lemma pullback_p1_iso_inv_fst (i : ğ’°.J) :
  (pullback_p1_iso ğ’° f g i).inv â‰« pullback.fst = (gluing ğ’° f g).Î¹ i :=
by { delta pullback_p1_iso, simp only [pullback.lift_fst] }

@[simp, reassoc] lemma pullback_p1_iso_inv_snd (i : ğ’°.J) :
  (pullback_p1_iso ğ’° f g i).inv â‰« pullback.snd = pullback.fst :=
by { delta pullback_p1_iso, simp only [pullback.lift_snd] }

@[simp, reassoc]
lemma pullback_p1_iso_hom_Î¹ (i : ğ’°.J) :
  (pullback_p1_iso ğ’° f g i).hom â‰« (gluing ğ’° f g).Î¹ i = pullback.fst :=
by rw [â† pullback_p1_iso_inv_fst, iso.hom_inv_id_assoc]

/-- The glued scheme (`(gluing ğ’° f g).glued`) is indeed the pullback of `f` and `g`. -/
def glued_is_limit : is_limit (pullback_cone.mk _ _ (p_comm ğ’° f g)) :=
begin
  apply pullback_cone.is_limit_aux',
  intro s,
  refine âŸ¨glued_lift ğ’° f g s, glued_lift_p1 ğ’° f g s, glued_lift_p2 ğ’° f g s, _âŸ©,
  intros m hâ‚ hâ‚‚,
  change m â‰« p1 ğ’° f g = _ at hâ‚,
  change m â‰« p2 ğ’° f g = _ at hâ‚‚,
  apply (ğ’°.pullback_cover s.fst).hom_ext,
  intro i,
  rw open_cover.pullback_cover_map,
  have := pullback_right_pullback_fst_iso (p1 ğ’° f g) (ğ’°.map i) m
    â‰ªâ‰« pullback.congr_hom hâ‚ rfl,
  erw (ğ’°.pullback_cover s.fst).Î¹_glue_morphisms,
  rw [â† cancel_epi (pullback_right_pullback_fst_iso (p1 ğ’° f g) (ğ’°.map i) m
    â‰ªâ‰« pullback.congr_hom hâ‚ rfl).hom, iso.trans_hom, category.assoc, pullback.congr_hom_hom,
    pullback.lift_fst_assoc, category.comp_id, pullback_right_pullback_fst_iso_hom_fst_assoc,
    pullback.condition],
  transitivity pullback.snd â‰« (pullback_p1_iso ğ’° f g _).hom â‰« (gluing ğ’° f g).Î¹ _,
  { congr' 1, rw â† pullback_p1_iso_hom_Î¹ },
  simp_rw â† category.assoc,
  congr' 1,
  apply pullback.hom_ext,
  { simp only [category.comp_id, pullback_right_pullback_fst_iso_hom_snd, category.assoc,
      pullback_p1_iso_hom_fst, pullback.lift_snd, pullback.lift_fst,
      pullback_symmetry_hom_comp_fst] },
  { simp only [category.comp_id, pullback_right_pullback_fst_iso_hom_fst_assoc,
    pullback_p1_iso_hom_snd, category.assoc, pullback.lift_fst_assoc,
    pullback_symmetry_hom_comp_snd_assoc, pullback.lift_snd],
    rw [â† pullback.condition_assoc, hâ‚‚] }
end

lemma has_pullback_of_cover : has_pullback f g := âŸ¨âŸ¨âŸ¨_, glued_is_limit ğ’° f gâŸ©âŸ©âŸ©

instance affine_has_pullback {A B C : CommRing}
  (f : Spec.obj (opposite.op A) âŸ¶ Spec.obj (opposite.op C))
  (g : Spec.obj (opposite.op B) âŸ¶ Spec.obj (opposite.op C)) : has_pullback f g :=
begin
  rw [â† Spec.image_preimage f, â† Spec.image_preimage g],
  exact âŸ¨âŸ¨âŸ¨_,is_limit_of_has_pullback_of_preserves_limit
    Spec (Spec.preimage f) (Spec.preimage g)âŸ©âŸ©âŸ©
end

lemma affine_affine_has_pullback {B C : CommRing} {X : Scheme}
  (f : X âŸ¶ Spec.obj (opposite.op C))
  (g : Spec.obj (opposite.op B) âŸ¶ Spec.obj (opposite.op C)) : has_pullback f g :=
has_pullback_of_cover X.affine_cover f g

instance base_affine_has_pullback {C : CommRing} {X Y : Scheme}
  (f : X âŸ¶ Spec.obj (opposite.op C))
  (g : Y âŸ¶ Spec.obj (opposite.op C)) : has_pullback f g :=
@@has_pullback_symmetry _ _ _
  (@@has_pullback_of_cover Y.affine_cover g f
    (Î» i, @@has_pullback_symmetry _ _ _ $ affine_affine_has_pullback _ _))

instance left_affine_comp_pullback_has_pullback {X Y Z : Scheme}
  (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (i : Z.affine_cover.J) :
    has_pullback ((Z.affine_cover.pullback_cover f).map i â‰« f) g :=
begin
  let Xáµ¢ := pullback f (Z.affine_cover.map i),
  let Yáµ¢ := pullback g (Z.affine_cover.map i),
  let W := pullback (pullback.snd : Yáµ¢ âŸ¶ _) (pullback.snd : Xáµ¢ âŸ¶ _),
  have := big_square_is_pullback (pullback.fst : W âŸ¶ _) (pullback.fst : Yáµ¢ âŸ¶ _)
    (pullback.snd : Xáµ¢ âŸ¶ _) (Z.affine_cover.map i) pullback.snd pullback.snd g
    pullback.condition.symm pullback.condition.symm
      (pullback_cone.flip_is_limit $ pullback_is_pullback _ _)
      (pullback_cone.flip_is_limit $ pullback_is_pullback _ _),
  have : has_pullback (pullback.snd â‰« Z.affine_cover.map i : Xáµ¢ âŸ¶ _) g :=
    âŸ¨âŸ¨âŸ¨_,thisâŸ©âŸ©âŸ©,
  rw â† pullback.condition at this,
  exact this,
end

instance {X Y Z : Scheme} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : has_pullback f g :=
has_pullback_of_cover (Z.affine_cover.pullback_cover f) f g

instance : has_pullbacks Scheme := has_pullbacks_of_has_limit_cospan _

instance {X Y Z : Scheme} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [is_affine X] [is_affine Y] [is_affine Z] :
  is_affine (pullback f g) :=
is_affine_of_iso (pullback.map f g (Spec.map (Î“.map f.op).op) (Spec.map (Î“.map g.op).op)
  (Î“_Spec.adjunction.unit.app X) (Î“_Spec.adjunction.unit.app Y) (Î“_Spec.adjunction.unit.app Z)
  (Î“_Spec.adjunction.unit.naturality f) (Î“_Spec.adjunction.unit.naturality g) â‰«
    (preserves_pullback.iso Spec _ _).inv)

/-- Given an open cover `{ Xáµ¢ }` of `X`, then `X Ã—[Z] Y` is covered by `Xáµ¢ Ã—[Z] Y`. -/
@[simps J obj map]
def open_cover_of_left (ğ’° : open_cover X) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : open_cover (pullback f g) :=
begin
  fapply ((gluing ğ’° f g).open_cover.pushforward_iso
    (limit.iso_limit_cone âŸ¨_, glued_is_limit ğ’° f gâŸ©).inv).copy ğ’°.J
    (Î» i, pullback (ğ’°.map i â‰« f) g)
    (Î» i, pullback.map _ _ _ _ (ğ’°.map i) (ğŸ™ _) (ğŸ™ _) (category.comp_id _) (by simp))
    (equiv.refl ğ’°.J) (Î» _, iso.refl _),
  rintro (i : ğ’°.J),
  change pullback.map _ _ _ _ _ _ _ _ _ = ğŸ™ _ â‰« (gluing ğ’° f g).Î¹ i â‰« _,
  refine eq.trans _ (category.id_comp _).symm,
  apply pullback.hom_ext,
  all_goals
  { dsimp,
    simp only [limit.iso_limit_cone_inv_Ï€, pullback_cone.mk_Ï€_app_left, category.comp_id,
      pullback_cone.mk_Ï€_app_right, category.assoc, pullback.lift_fst, pullback.lift_snd],
    symmetry,
    exact multicoequalizer.Ï€_desc _ _ _ _ _ },
end

/-- Given an open cover `{ Yáµ¢ }` of `Y`, then `X Ã—[Z] Y` is covered by `X Ã—[Z] Yáµ¢`. -/
@[simps J obj map]
def open_cover_of_right (ğ’° : open_cover Y) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : open_cover (pullback f g) :=
begin
  fapply ((open_cover_of_left ğ’° g f).pushforward_iso (pullback_symmetry _ _).hom).copy ğ’°.J
    (Î» i, pullback f (ğ’°.map i â‰« g))
    (Î» i, pullback.map _ _ _ _ (ğŸ™ _) (ğ’°.map i) (ğŸ™ _) (by simp) (category.comp_id _))
    (equiv.refl _) (Î» i, pullback_symmetry _ _),
  intro i,
  dsimp [open_cover.bind],
  apply pullback.hom_ext; simp,
end

/-- Given an open cover `{ Xáµ¢ }` of `X` and an open cover `{ Yâ±¼ }` of `Y`, then
`X Ã—[Z] Y` is covered by `Xáµ¢ Ã—[Z] Yâ±¼`. -/
@[simps J obj map]
def open_cover_of_left_right (ğ’°X : X.open_cover) (ğ’°Y : Y.open_cover)
  (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : (pullback f g).open_cover :=
begin
  fapply ((open_cover_of_left ğ’°X f g).bind (Î» x, open_cover_of_right ğ’°Y (ğ’°X.map x â‰« f) g)).copy
    (ğ’°X.J Ã— ğ’°Y.J)
    (Î» ij, pullback (ğ’°X.map ij.1 â‰« f) (ğ’°Y.map ij.2 â‰« g))
    (Î» ij, pullback.map _ _ _ _ (ğ’°X.map ij.1) (ğ’°Y.map ij.2) (ğŸ™ _)
      (category.comp_id _) (category.comp_id _))
    (equiv.sigma_equiv_prod _ _).symm
    (Î» _, iso.refl _),
  rintro âŸ¨i, jâŸ©,
  apply pullback.hom_ext; simpa,
end

/-- (Implementation). Use `open_cover_of_base` instead. -/
def open_cover_of_base' (ğ’° : open_cover Z) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : open_cover (pullback f g) :=
begin
  apply (open_cover_of_left (ğ’°.pullback_cover f) f g).bind,
  intro i,
  let Xáµ¢ := pullback f (ğ’°.map i),
  let Yáµ¢ := pullback g (ğ’°.map i),
  let W := pullback (pullback.snd : Yáµ¢ âŸ¶ _) (pullback.snd : Xáµ¢ âŸ¶ _),
  have := big_square_is_pullback (pullback.fst : W âŸ¶ _) (pullback.fst : Yáµ¢ âŸ¶ _)
    (pullback.snd : Xáµ¢ âŸ¶ _) (ğ’°.map i) pullback.snd pullback.snd g
    pullback.condition.symm pullback.condition.symm
      (pullback_cone.flip_is_limit $ pullback_is_pullback _ _)
      (pullback_cone.flip_is_limit $ pullback_is_pullback _ _),
  refine open_cover_of_is_iso
    ((pullback_symmetry _ _).hom â‰« (limit.iso_limit_cone âŸ¨_, thisâŸ©).inv â‰«
      pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) (ğŸ™ _) _ _),
  { simpa only [category.comp_id, category.id_comp, â† pullback.condition] },
  { simp only [category.comp_id, category.id_comp] },
  apply_instance
end

/-- Given an open cover `{ Záµ¢ }` of `Z`, then `X Ã—[Z] Y` is covered by `Xáµ¢ Ã—[Záµ¢] Yáµ¢`, where
  `Xáµ¢ = X Ã—[Z] Záµ¢` and `Yáµ¢ = Y Ã—[Z] Záµ¢` is the preimage of `Záµ¢` in `X` and `Y`. -/
@[simps J obj map]
def open_cover_of_base (ğ’° : open_cover Z) (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : open_cover (pullback f g) :=
begin
  apply (open_cover_of_base' ğ’° f g).copy
    ğ’°.J
    (Î» i, pullback (pullback.snd : pullback f (ğ’°.map i) âŸ¶ _)
      (pullback.snd : pullback g (ğ’°.map i) âŸ¶ _))
    (Î» i, pullback.map _ _ _ _ pullback.fst pullback.fst (ğ’°.map i)
      pullback.condition.symm pullback.condition.symm)
    ((equiv.prod_punit ğ’°.J).symm.trans (equiv.sigma_equiv_prod ğ’°.J punit).symm)
    (Î» _, iso.refl _),
  intro i,
  change _ = _ â‰« _ â‰« _,
  refine eq.trans _ (category.id_comp _).symm,
  apply pullback.hom_ext; simp only [category.comp_id, open_cover_of_left_map,
    open_cover.pullback_cover_map, pullback_cone.mk_Ï€_app_left, open_cover_of_is_iso_map,
    limit.iso_limit_cone_inv_Ï€_assoc, category.assoc, pullback.lift_fst_assoc,
    pullback_symmetry_hom_comp_snd_assoc, pullback.lift_fst, limit.iso_limit_cone_inv_Ï€,
    pullback_cone.mk_Ï€_app_right, pullback_symmetry_hom_comp_fst_assoc, pullback.lift_snd],
end

end pullback

end algebraic_geometry.Scheme

namespace algebraic_geometry

instance {X Y S X' Y' S' : Scheme} (f : X âŸ¶ S) (g : Y âŸ¶ S) (f' : X' âŸ¶ S')
  (g' : Y' âŸ¶ S') (iâ‚ : X âŸ¶ X') (iâ‚‚ : Y âŸ¶ Y') (iâ‚ƒ : S âŸ¶ S') (eâ‚ : f â‰« iâ‚ƒ = iâ‚ â‰« f')
  (eâ‚‚ : g â‰« iâ‚ƒ = iâ‚‚ â‰« g') [is_open_immersion iâ‚] [is_open_immersion iâ‚‚] [mono iâ‚ƒ] :
  is_open_immersion (pullback.map f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚) :=
begin
  rw pullback_map_eq_pullback_fst_fst_iso_inv,
  apply_instance
end

end algebraic_geometry
