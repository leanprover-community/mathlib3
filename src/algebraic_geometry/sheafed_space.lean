/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebraic_geometry.presheafed_space.has_colimits
import topology.sheaves.functors

/-!
# Sheafed spaces

Introduces the category of topological spaces equipped with a sheaf (taking values in an
arbitrary target category `C`.)

We further describe how to apply functors and natural transformations to the values of the
presheaves.
-/

universes v u

open category_theory
open Top
open topological_space
open opposite
open category_theory.limits
open category_theory.category category_theory.functor

variables (C : Type u) [category.{v} C] [limits.has_products C]

local attribute [tidy] tactic.op_induction'

namespace algebraic_geometry

/-- A `SheafedSpace C` is a topological space equipped with a sheaf of `C`s. -/
structure SheafedSpace extends PresheafedSpace C :=
(is_sheaf : presheaf.is_sheaf)

variables {C}

namespace SheafedSpace

instance coe_carrier : has_coe (SheafedSpace C) Top :=
{ coe := Œª X, X.carrier }

/-- Extract the `sheaf C (X : Top)` from a `SheafedSpace C`. -/
def sheaf (X : SheafedSpace C) : sheaf C (X : Top.{v}) := ‚ü®X.presheaf, X.is_sheaf‚ü©

@[simp] lemma as_coe (X : SheafedSpace C) : X.carrier = (X : Top.{v}) := rfl
@[simp] lemma mk_coe (carrier) (presheaf) (h) :
  (({ carrier := carrier, presheaf := presheaf, is_sheaf := h } : SheafedSpace.{v} C) :
  Top.{v}) = carrier :=
rfl

instance (X : SheafedSpace.{v} C) : topological_space X := X.carrier.str

/-- The trivial `punit` valued sheaf on any topological space. -/
def punit (X : Top) : SheafedSpace (discrete punit) :=
{ is_sheaf := presheaf.is_sheaf_punit _,
  ..@PresheafedSpace.const (discrete punit) _ X punit.star }

instance : inhabited (SheafedSpace (discrete _root_.punit)) := ‚ü®punit (Top.of pempty)‚ü©

instance : category (SheafedSpace C) :=
show category (induced_category (PresheafedSpace C) SheafedSpace.to_PresheafedSpace),
by apply_instance

/-- Forgetting the sheaf condition is a functor from `SheafedSpace C` to `PresheafedSpace C`. -/
@[derive [full, faithful]]
def forget_to_PresheafedSpace : (SheafedSpace C) ‚•§ (PresheafedSpace C) :=
induced_functor _

variables {C}

section
local attribute [simp] id comp

@[simp] lemma id_base (X : SheafedSpace C) :
  ((ùüô X) : X ‚ü∂ X).base = (ùüô (X : Top.{v})) := rfl

lemma id_c (X : SheafedSpace C) :
  ((ùüô X) : X ‚ü∂ X).c = eq_to_hom (presheaf.pushforward.id_eq X.presheaf).symm := rfl

@[simp] lemma id_c_app (X : SheafedSpace C) (U) :
  ((ùüô X) : X ‚ü∂ X).c.app U = eq_to_hom (by { induction U using opposite.rec, cases U, refl }) :=
by { induction U using opposite.rec, cases U, simp only [id_c], dsimp, simp, }

@[simp] lemma comp_base {X Y Z : SheafedSpace C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
  (f ‚â´ g).base = f.base ‚â´ g.base := rfl

@[simp] lemma comp_c_app {X Y Z : SheafedSpace C} (Œ± : X ‚ü∂ Y) (Œ≤ : Y ‚ü∂ Z) (U) :
  (Œ± ‚â´ Œ≤).c.app U = (Œ≤.c).app U ‚â´ (Œ±.c).app (op ((opens.map (Œ≤.base)).obj (unop U)))
:= rfl

lemma comp_c_app' {X Y Z : SheafedSpace C} (Œ± : X ‚ü∂ Y) (Œ≤ : Y ‚ü∂ Z) (U) :
  (Œ± ‚â´ Œ≤).c.app (op U) = (Œ≤.c).app (op U) ‚â´ (Œ±.c).app (op ((opens.map (Œ≤.base)).obj U))
:= rfl

lemma congr_app {X Y : SheafedSpace C} {Œ± Œ≤ : X ‚ü∂ Y} (h : Œ± = Œ≤) (U) :
  Œ±.c.app U = Œ≤.c.app U ‚â´ X.presheaf.map (eq_to_hom (by subst h)) :=
PresheafedSpace.congr_app h U

variables (C)

/-- The forgetful functor from `SheafedSpace` to `Top`. -/
def forget : SheafedSpace C ‚•§ Top :=
{ obj := Œª X, (X : Top.{v}),
  map := Œª X Y f, f.base }

end

open Top.presheaf

/--
The restriction of a sheafed space along an open embedding into the space.
-/
def restrict {U : Top} (X : SheafedSpace C)
  {f : U ‚ü∂ (X : Top.{v})} (h : open_embedding f) : SheafedSpace C :=
{ is_sheaf := Œª Œπ ùí∞, ‚ü®is_limit.of_iso_limit
    ((is_limit.postcompose_inv_equiv _ _).inv_fun (X.is_sheaf _).some)
    (sheaf_condition_equalizer_products.fork.iso_of_open_embedding h ùí∞).symm‚ü©,
  ..X.to_PresheafedSpace.restrict h }

/--
The restriction of a sheafed space `X` to the top subspace is isomorphic to `X` itself.
-/
def restrict_top_iso (X : SheafedSpace C) :
  X.restrict (opens.open_embedding ‚ä§) ‚âÖ X :=
@preimage_iso _ _ _ _ forget_to_PresheafedSpace _ _
  (X.restrict (opens.open_embedding ‚ä§)) _
  X.to_PresheafedSpace.restrict_top_iso

/--
The global sections, notated Gamma.
-/
def Œì : (SheafedSpace C)·µí·µñ ‚•§ C :=
forget_to_PresheafedSpace.op ‚ãô PresheafedSpace.Œì

lemma Œì_def : (Œì : _ ‚•§ C) = forget_to_PresheafedSpace.op ‚ãô PresheafedSpace.Œì := rfl

@[simp] lemma Œì_obj (X : (SheafedSpace C)·µí·µñ) : Œì.obj X = (unop X).presheaf.obj (op ‚ä§) := rfl

lemma Œì_obj_op (X : SheafedSpace C) : Œì.obj (op X) = X.presheaf.obj (op ‚ä§) := rfl

@[simp] lemma Œì_map {X Y : (SheafedSpace C)·µí·µñ} (f : X ‚ü∂ Y) :
  Œì.map f = f.unop.c.app (op ‚ä§) := rfl

lemma Œì_map_op {X Y : SheafedSpace C} (f : X ‚ü∂ Y) :
  Œì.map f.op = f.c.app (op ‚ä§) := rfl

noncomputable
instance [has_limits C] : creates_colimits (forget_to_PresheafedSpace : SheafedSpace C ‚•§ _) :=
‚ü®Œª J hJ, by exactI ‚ü®Œª K, creates_colimit_of_fully_faithful_of_iso
  ‚ü®(PresheafedSpace.colimit_cocone (K ‚ãô forget_to_PresheafedSpace)).X,
    limit_is_sheaf _ (Œª j, sheaf.pushforward_sheaf_of_sheaf _ (K.obj (unop j)).2)‚ü©
  (colimit.iso_colimit_cocone ‚ü®_, PresheafedSpace.colimit_cocone_is_colimit _‚ü©).symm‚ü©‚ü©

instance [has_limits C] : has_colimits (SheafedSpace C) :=
has_colimits_of_has_colimits_creates_colimits forget_to_PresheafedSpace

noncomputable instance [has_limits C] : preserves_colimits (forget C) :=
limits.comp_preserves_colimits forget_to_PresheafedSpace (PresheafedSpace.forget C)

end SheafedSpace

end algebraic_geometry
