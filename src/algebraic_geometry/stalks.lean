/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebraic_geometry.presheafed_space
import category_theory.limits.final
import topology.sheaves.stalks

/-!
# Stalks for presheaved spaces

This file lifts constructions of stalks and pushforwards of stalks to work with
the category of presheafed spaces. Additionally, we prove that restriction of
presheafed spaces does not change the stalks.
-/

noncomputable theory

universes v u v' u'

open category_theory
open category_theory.limits category_theory.category category_theory.functor
open algebraic_geometry
open topological_space
open opposite

variables {C : Type u} [category.{v} C] [has_colimits C]

local attribute [tidy] tactic.op_induction'

open Top.presheaf

namespace algebraic_geometry.PresheafedSpace

/--
The stalk at `x` of a `PresheafedSpace`.
-/
def stalk (X : PresheafedSpace C) (x : X) : C := X.presheaf.stalk x

/--
A morphism of presheafed spaces induces a morphism of stalks.
-/
def stalk_map {X Y : PresheafedSpace C} (Œ± : X ‚ü∂ Y) (x : X) : Y.stalk (Œ±.base x) ‚ü∂ X.stalk x :=
(stalk_functor C (Œ±.base x)).map (Œ±.c) ‚â´ X.presheaf.stalk_pushforward C Œ±.base x

@[simp, elementwise, reassoc]
lemma stalk_map_germ {X Y : PresheafedSpace C} (Œ± : X ‚ü∂ Y) (U : opens Y.carrier)
  (x : (opens.map Œ±.base).obj U) :
  Y.presheaf.germ ‚ü®Œ±.base x, x.2‚ü© ‚â´ stalk_map Œ± ‚Üëx = Œ±.c.app (op U) ‚â´ X.presheaf.germ x :=
by rw [stalk_map, stalk_functor_map_germ_assoc, stalk_pushforward_germ]

section restrict

/--
For an open embedding `f : U ‚ü∂ X` and a point `x : U`, we get an isomorphism between the stalk
of `X` at `f x` and the stalk of the restriction of `X` along `f` at t `x`.
-/
def restrict_stalk_iso {U : Top} (X : PresheafedSpace C)
  (f : U ‚ü∂ (X : Top.{v})) (h : open_embedding f) (x : U) :
  (X.restrict f h).stalk x ‚âÖ X.stalk (f x) :=
begin
  -- As a left adjoint, the functor `h.is_open_map.functor_nhds x` is initial.
  haveI := initial_of_adjunction (h.is_open_map.adjunction_nhds x),
  -- Typeclass resolution knows that the opposite of an initial functor is final. The result
  -- follows from the general fact that postcomposing with a final functor doesn't change colimits.
  exact final.colimit_iso (h.is_open_map.functor_nhds x).op
    ((open_nhds.inclusion (f x)).op ‚ãô X.presheaf),
end

@[simp, elementwise, reassoc]
lemma restrict_stalk_iso_hom_eq_germ {U : Top} (X : PresheafedSpace C) (f : U ‚ü∂ (X : Top.{v}))
  (h : open_embedding f) (V : opens U) (x : U) (hx : x ‚àà V) :
  (X.restrict f h).presheaf.germ ‚ü®x, hx‚ü© ‚â´ (restrict_stalk_iso X f h x).hom =
  X.presheaf.germ ‚ü®f x, show f x ‚àà h.is_open_map.functor.obj V, from ‚ü®x, hx, rfl‚ü©‚ü© :=
colimit.Œπ_pre ((open_nhds.inclusion (f x)).op ‚ãô X.presheaf)
  (h.is_open_map.functor_nhds x).op (op ‚ü®V, hx‚ü©)

@[simp, elementwise, reassoc]
lemma restrict_stalk_iso_inv_eq_germ {U : Top} (X : PresheafedSpace C) (f : U ‚ü∂ (X : Top.{v}))
  (h : open_embedding f) (V : opens U) (x : U) (hx : x ‚àà V) :
  X.presheaf.germ ‚ü®f x, show f x ‚àà h.is_open_map.functor.obj V, from ‚ü®x, hx, rfl‚ü©‚ü© ‚â´
  (restrict_stalk_iso X f h x).inv = (X.restrict f h).presheaf.germ ‚ü®x, hx‚ü© :=
by rw [‚Üê restrict_stalk_iso_hom_eq_germ, category.assoc, iso.hom_inv_id, category.comp_id]

end restrict

namespace stalk_map

@[simp] lemma id (X : PresheafedSpace C) (x : X) : stalk_map (ùüô X) x = ùüô (X.stalk x) :=
begin
  dsimp [stalk_map],
  simp only [stalk_pushforward.id],
  rw [‚Üêmap_comp],
  convert (stalk_functor C x).map_id X.presheaf,
  tidy,
end

-- TODO understand why this proof is still gross (i.e. requires using `erw`)
@[simp] lemma comp {X Y Z : PresheafedSpace C} (Œ± : X ‚ü∂ Y) (Œ≤ : Y ‚ü∂ Z) (x : X) :
  stalk_map (Œ± ‚â´ Œ≤) x =
    (stalk_map Œ≤ (Œ±.base x) : Z.stalk (Œ≤.base (Œ±.base x)) ‚ü∂ Y.stalk (Œ±.base x)) ‚â´
    (stalk_map Œ± x : Y.stalk (Œ±.base x) ‚ü∂ X.stalk x) :=
begin
  dsimp [stalk_map, stalk_functor, stalk_pushforward],
  ext U,
  induction U using opposite.rec,
  cases U,
  simp only [colimit.Œπ_map_assoc, colimit.Œπ_pre_assoc, colimit.Œπ_pre,
    whisker_left_app, whisker_right_app,
    assoc, id_comp, map_id, map_comp],
  dsimp,
  simp only [map_id, assoc, pushforward.comp_inv_app],
  -- FIXME Why doesn't simp do this:
  erw [category_theory.functor.map_id],
  erw [category_theory.functor.map_id],
  erw [id_comp, id_comp, id_comp],
end

/--
If `Œ± = Œ≤` and `x = x'`, we would like to say that `stalk_map Œ± x = stalk_map Œ≤ x'`.
Unfortunately, this equality is not well-formed, as their types are not _definitionally_ the same.
To get a proper congruence lemma, we therefore have to introduce these `eq_to_hom` arrows on
either side of the equality.
-/
lemma congr {X Y : PresheafedSpace C} (Œ± Œ≤ : X ‚ü∂ Y) (h‚ÇÅ : Œ± = Œ≤) (x x': X) (h‚ÇÇ : x = x') :
  stalk_map Œ± x ‚â´ eq_to_hom (show X.stalk x = X.stalk x', by rw h‚ÇÇ) =
  eq_to_hom (show Y.stalk (Œ±.base x) = Y.stalk (Œ≤.base x'), by rw [h‚ÇÅ, h‚ÇÇ]) ‚â´ stalk_map Œ≤ x' :=
stalk_hom_ext _ $ Œª U hx, by { subst h‚ÇÅ, subst h‚ÇÇ, simp }

lemma congr_hom {X Y : PresheafedSpace C} (Œ± Œ≤ : X ‚ü∂ Y) (h : Œ± = Œ≤) (x : X) :
  stalk_map Œ± x =
  eq_to_hom (show Y.stalk (Œ±.base x) = Y.stalk (Œ≤.base x), by rw h) ‚â´ stalk_map Œ≤ x :=
by rw [‚Üê stalk_map.congr Œ± Œ≤ h x x rfl, eq_to_hom_refl, category.comp_id]

lemma congr_point {X Y : PresheafedSpace C} (Œ± : X ‚ü∂ Y) (x x' : X) (h : x = x') :
  stalk_map Œ± x ‚â´ eq_to_hom (show X.stalk x = X.stalk x', by rw h) =
  eq_to_hom (show Y.stalk (Œ±.base x) = Y.stalk (Œ±.base x'), by rw h) ‚â´ stalk_map Œ± x' :=
by rw stalk_map.congr Œ± Œ± rfl x x' h

instance is_iso {X Y : PresheafedSpace C} (Œ± : X ‚ü∂ Y) [is_iso Œ±] (x : X) :
  is_iso (stalk_map Œ± x) :=
{ out := begin
  let Œ≤ : Y ‚ü∂ X := category_theory.inv Œ±,
  have h_eq : (Œ± ‚â´ Œ≤).base x = x,
  { rw [is_iso.hom_inv_id Œ±, id_base, Top.id_app] },
  -- Intuitively, the inverse of the stalk map of `Œ±` at `x` should just be the stalk map of `Œ≤`
  -- at `Œ± x`. Unfortunately, we have a problem with dependent type theory here: Because `x`
  -- is not *definitionally* equal to `Œ≤ (Œ± x)`, the map `stalk_map Œ≤ (Œ± x)` has not the correct
  -- type for an inverse.
  -- To get a proper inverse, we need to compose with the `eq_to_hom` arrow
  -- `X.stalk x ‚ü∂ X.stalk ((Œ± ‚â´ Œ≤).base x)`.
  refine ‚ü®eq_to_hom (show X.stalk x = X.stalk ((Œ± ‚â´ Œ≤).base x), by rw h_eq) ‚â´
    (stalk_map Œ≤ (Œ±.base x) : _), _, _‚ü©,
  { rw [‚Üê category.assoc, congr_point Œ± x ((Œ± ‚â´ Œ≤).base x) h_eq.symm, category.assoc],
    erw ‚Üê stalk_map.comp Œ≤ Œ± (Œ±.base x),
    rw [congr_hom _ _ (is_iso.inv_hom_id Œ±), stalk_map.id, eq_to_hom_trans_assoc,
      eq_to_hom_refl, category.id_comp] },
  { rw [category.assoc, ‚Üê stalk_map.comp, congr_hom _ _ (is_iso.hom_inv_id Œ±),
    stalk_map.id, eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp] },
end }

/--
An isomorphism between presheafed spaces induces an isomorphism of stalks.
-/
def stalk_iso {X Y : PresheafedSpace C} (Œ± : X ‚âÖ Y) (x : X) :
  Y.stalk (Œ±.hom.base x) ‚âÖ X.stalk x :=
as_iso (stalk_map Œ±.hom x)

end stalk_map

end algebraic_geometry.PresheafedSpace
