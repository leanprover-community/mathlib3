/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/

import algebraic_topology.simplicial_object
import category_theory.limits.shapes.wide_pullbacks
import category_theory.arrow

/-!

# The ÄŒech Nerve

This file provides a definition of the ÄŒech nerve associated to an arrow, provided
the base category has the correct wide pullbacks.

Several variants are provided, given `f : arrow C`:
1. `f.cech_nerve` is the ÄŒech nerve, considered as a simplicial object in `C`.
2. `f.augmented_cech_nerve` is the augmented ÄŒech nerve, considered as an
  augmented simplicial object in `C`.
3. `simplicial_object.cech_nerve` and `simplicial_object.augmented_cech_nerve` are
  functorial versions of 1 resp. 2.

-/

open category_theory
open category_theory.limits

noncomputable theory

universes v u

variables {C : Type u} [category.{v} C]

namespace category_theory.arrow

variables (f : arrow C)
variables [âˆ€ n : â„•, has_wide_pullback f.right (Î» i : ulift (fin (n+1)), f.left) (Î» i, f.hom)]

/-- The ÄŒech nerve associated to an arrow. -/
@[simps]
def cech_nerve : simplicial_object C :=
{ obj := Î» n, wide_pullback f.right
    (Î» i : ulift (fin (n.unop.len + 1)), f.left) (Î» i, f.hom),
  map := Î» m n g, wide_pullback.lift (wide_pullback.base _)
    (Î» i, wide_pullback.Ï€ (Î» i, f.hom) $ ulift.up $ g.unop.to_preorder_hom i.down) (by tidy) }

/-- The augmented ÄŒech nerve associated to an arrow. -/
@[simps]
def augmented_cech_nerve : simplicial_object.augmented C :=
{ left := f.cech_nerve,
  right := f.right,
  hom := { app := Î» i, wide_pullback.base _ } }

end category_theory.arrow

namespace category_theory
namespace simplicial_object

variables [âˆ€ (n : â„•) (f : arrow C),
  has_wide_pullback f.right (Î» i : ulift (fin (n+1)), f.left) (Î» i, f.hom)]

def cech_nerve_map {f g : arrow C} (F : f âŸ¶ g) : (f.cech_nerve âŸ¶ g.cech_nerve) :=
{ app := Î» n, wide_pullback.lift (wide_pullback.base _ â‰« F.right)
    (Î» i, wide_pullback.Ï€ _ i â‰« F.left)
    (Î» i, by simp only [eq_self_iff_true, arrow.w, category.assoc,
      limits.wide_pullback.Ï€_arrow_assoc]) }

/-- The ÄŒech nerve construction, as a functor from `arrow C`. -/
@[simps]
def cech_nerve : arrow C â¥¤ simplicial_object C :=
{ obj := Î» f, f.cech_nerve,
  map := Î» f g F, cech_nerve_map F,
  map_id' := by { intros i, ext;
    { simp only [cech_nerve_map, category.comp_id, category.id_comp, eq_self_iff_true,
        arrow.id_right, limits.wide_pullback.lift_Ï€, nat_trans.id_app, arrow.id_left,
        limits.wide_pullback.lift_base] } },
  map_comp' := begin
    intros f g h F G,
    ext,
    all_goals {
      simp only [cech_nerve_map, wide_pullback.lift_Ï€, eq_self_iff_true, wide_pullback.lift_Ï€_assoc,
        wide_pullback.Ï€_arrow, wide_pullback.lift_base_assoc, wide_pullback.lift_base,
        arrow.w, category.assoc, limits.wide_pullback.lift_base,
        limits.wide_pullback.lift_Ï€, limits.limit.lift_Ï€_assoc, limits.wide_pullback.lift_Ï€_assoc,
        nat_trans.comp_app, comma.comp_left, category.assoc, limits.wide_pullback.lift_Ï€,
        comma.comp_right] },
    simpa only [â†category.assoc],
  end }

lemma augmented_cech_nerve_w' {C : Type u}
  [category C]
  [âˆ€ (n : â„•) (f : arrow C),
     has_wide_pullback f.right (Î» (i : ulift (fin (n + 1))), f.left)
       (Î» (i : ulift (fin (n + 1))), f.hom)]
  (f g : arrow C)
  (F : f âŸ¶ g) :
  (ğŸ­ (simplicial_object C)).map (cech_nerve.map F) â‰«
      g.augmented_cech_nerve.hom =
    f.augmented_cech_nerve.hom â‰« (const C).map F.right :=
begin
  simp only [cech_nerve_map, functor.id_map, cech_nerve_map_2],
  apply nat_trans.ext,
  apply funext,
  intro x,
  simp only [arrow.augmented_cech_nerve_hom_app, functor.const.map_app, nat_trans.comp_app,
    eq_self_iff_true, limits.wide_pullback.lift_base],
end

def augmented_cech_nerve_map
  (f g : arrow C)
  (F : f âŸ¶ g) :
  f.augmented_cech_nerve âŸ¶ g.augmented_cech_nerve :=
{ left := cech_nerve.map F,
  right := F.right,
  w' := augmented_cech_nerve_w' f g F }


/-- The augmented ÄŒech nerve construction, as a functor from `arrow C`. -/
@[simps]
def augmented_cech_nerve : arrow C â¥¤ simplicial_object.augmented C :=
{ obj := Î» f, f.augmented_cech_nerve,
  map := Î» f g F, augmented_cech_nerve_map f g F,
  map_id' := by { simp [augmented_cech_nerve_map, cech_nerve_map], intros X,
    ext1,
    work_on_goal 0 {
      ext,
      work_on_goal 0 { simp only [category_theory.category.id_comp,
 eq_self_iff_true,
 category_theory.comma.id_left,
 category_theory.limits.wide_pullback.lift_Ï€,
 category_theory.nat_trans.id_app], },
      simp only [category_theory.category.id_comp,
 eq_self_iff_true,
 category_theory.comma.id_left,
 category_theory.limits.wide_pullback.lift_base,
 category_theory.nat_trans.id_app]}, refl },
  map_comp' := by { simp [augmented_cech_nerve_map, cech_nerve_map],
    tidy, } }

/-- A helper function used in defining the ÄŒech adjunction. -/
@[simps]
def equivalence_right_to_left (X : simplicial_object.augmented C) (F : arrow C)
  (G : X âŸ¶ F.augmented_cech_nerve) : augmented.to_arrow.obj X âŸ¶ F :=
{ left := G.left.app _ â‰« wide_pullback.Ï€ (Î» i, F.hom) âŸ¨0âŸ©,
  right := G.right,
  w' := begin
    have := G.w,
    apply_fun (Î» e, e.app (opposite.op $ simplex_category.mk 0)) at this,
    tidy,
  end }

/-- A helper function used in defining the ÄŒech adjunction. -/
@[simps]
def equivalence_left_to_right (X : simplicial_object.augmented C) (F : arrow C)
  (G : augmented.to_arrow.obj X âŸ¶ F) : X âŸ¶ F.augmented_cech_nerve :=
{ left :=
  { app := Î» x, limits.wide_pullback.lift (X.hom.app _ â‰« G.right)
      (Î» i, X.left.map (simplex_category.const x.unop i.down).op â‰« G.left) (by tidy),
    naturality' := begin
      intros x y f,
      ext,
      { dsimp,
        simp only [wide_pullback.lift_Ï€, category.assoc],
        rw [â† category.assoc, â† X.left.map_comp],
        refl },
      { dsimp,
        simp only [functor.const.obj_map, nat_trans.naturality_assoc,
          wide_pullback.lift_base, category.assoc],
        erw category.id_comp }
    end },
  right := G.right }

/-- A helper function used in defining the ÄŒech adjunction. -/
@[simps]
def cech_nerve_equiv (X : simplicial_object.augmented C) (F : arrow C) :
  (augmented.to_arrow.obj X âŸ¶ F) â‰ƒ (X âŸ¶ F.augmented_cech_nerve) :=
{ to_fun := equivalence_left_to_right _ _,
  inv_fun := equivalence_right_to_left _ _,
  left_inv := begin
    intro A,
    dsimp,
    ext,
    { dsimp,
      erw wide_pullback.lift_Ï€,
      nth_rewrite 1 â† category.id_comp A.left,
      congr' 1,
      convert X.left.map_id _,
      rw â† op_id,
      congr' 1,
      ext âŸ¨a,haâŸ©,
      change a < 1 at ha,
      change 0 = a,
      linarith },
    { refl, }
  end,
  right_inv := begin
    intro A,
    dsimp,
    ext _ âŸ¨jâŸ©,
    { dsimp,
      simp only [arrow.cech_nerve_map, wide_pullback.lift_Ï€, nat_trans.naturality_assoc],
      erw wide_pullback.lift_Ï€,
      refl },
    { dsimp,
      erw wide_pullback.lift_base,
      have := A.w,
      apply_fun (Î» e, e.app x) at this,
      rw nat_trans.comp_app at this,
      erw this,
      refl },
    { refl }
  end }

/-- The augmented ÄŒech nerve construction is right adjoint to the `to_arrow` functor. -/
abbreviation cech_nerve_adjunction :
  (augmented.to_arrow : _ â¥¤ arrow C) âŠ£ augmented_cech_nerve :=
adjunction.mk_of_hom_equiv { hom_equiv := cech_nerve_equiv }

end simplicial_object

end category_theory

namespace category_theory.arrow

variables (f : arrow C)
variables [âˆ€ n : â„•, has_wide_pushout f.left (Î» i : ulift (fin (n+1)), f.right) (Î» i, f.hom)]

/-- The ÄŒech conerve associated to an arrow. -/
@[simps]
def cech_conerve : cosimplicial_object C :=
{ obj := Î» n, wide_pushout f.left
    (Î» i : ulift (fin (n.len + 1)), f.right) (Î» i, f.hom),
  map := Î» m n g, wide_pushout.desc (wide_pushout.head _)
    (Î» i, wide_pushout.Î¹ (Î» i, f.hom) $ ulift.up $ g.to_preorder_hom i.down)
    begin
      rintros âŸ¨âŸ¨jâŸ©âŸ©,
      dsimp,
      rw [wide_pushout.arrow_Î¹ (Î» i, f.hom)],
    end }

/-- The augmented ÄŒech conerve associated to an arrow. -/
@[simps]
def augmented_cech_conerve : cosimplicial_object.augmented C :=
{ left := f.left,
  right := f.cech_conerve,
  hom := { app := Î» i, wide_pushout.head _ } }

end category_theory.arrow

namespace category_theory
namespace cosimplicial_object

variables [âˆ€ (n : â„•) (f : arrow C),
  has_wide_pushout f.left (Î» i : ulift (fin (n+1)), f.right) (Î» i, f.hom)]

/-- The ÄŒech conerve construction, as a functor from `arrow C`. -/
@[simps]
def cech_conerve : arrow C â¥¤ cosimplicial_object C :=
{ obj := Î» f, f.cech_conerve,
  map := Î» f g F,
  { app := Î» n, wide_pushout.desc (F.left â‰« wide_pushout.head _)
      (Î» i, F.right â‰« wide_pushout.Î¹ _ i)
      (Î» i, by { rw [â†arrow.w_assoc F, wide_pushout.arrow_Î¹ (Î» i, g.hom)], }) },
  -- tidy needs a bit of help here...
  map_id' := by { intros i, ext, tidy },
  map_comp' := begin
    intros f g h F G,
    ext,
    all_goals {
      dsimp,
      simp only [category.assoc, limits.wide_pushout.head_desc_assoc,
        limits.wide_pushout.Î¹_desc_assoc, limits.colimit.Î¹_desc],
      simpa only [â† category.assoc], },
  end }

/-- The augmented ÄŒech conerve construction, as a functor from `arrow C`. -/
@[simps]
def augmented_cech_conerve : arrow C â¥¤ cosimplicial_object.augmented C :=
{ obj := Î» f, f.augmented_cech_conerve,
  map := Î» f g F,
  { left := F.left,
    right := cech_conerve.map F, } }

/-- A helper function used in defining the ÄŒech conerve adjunction. -/
@[simps]
def equivalence_left_to_right (F : arrow C) (X : cosimplicial_object.augmented C)
  (G : F.augmented_cech_conerve âŸ¶ X) : F âŸ¶ augmented.to_arrow.obj X :=
{ left := G.left,
  right :=
    (wide_pushout.Î¹ (Î» i, F.hom) (_root_.ulift.up 0) â‰« G.right.app (simplex_category.mk 0) : _),
  w' := begin
    have := G.w,
    apply_fun (Î» e, e.app (simplex_category.mk 0)) at this,
    dsimp at this,
    simpa only [category_theory.functor.id_map, augmented.to_arrow_obj_hom,
      wide_pushout.arrow_Î¹_assoc (Î» i, F.hom)],
  end }

/-- A helper function used in defining the ÄŒech conerve adjunction. -/
@[simps]
def equivalence_right_to_left (F : arrow C) (X : cosimplicial_object.augmented C)
  (G : F âŸ¶ augmented.to_arrow.obj X) : F.augmented_cech_conerve âŸ¶ X :=
{ left := G.left,
  right := { app := Î» x, limits.wide_pushout.desc (G.left â‰« X.hom.app _)
      (Î» i, G.right â‰« X.right.map (simplex_category.const x i.down))
      begin
        rintros âŸ¨jâŸ©,
        rw â†arrow.w_assoc G,
        dsimp,
        have t := X.hom.naturality (x.const j),
        dsimp at t, simp only [category.id_comp] at t,
        rw â†t,
      end,
    naturality' := begin
      intros x y f,
      ext,
      { dsimp,
        simp only [wide_pushout.Î¹_desc_assoc, wide_pushout.Î¹_desc],
        rw [category.assoc, â†X.right.map_comp],
        refl },
      { dsimp,
        simp only [functor.const.obj_map, â†nat_trans.naturality,
          wide_pushout.head_desc_assoc, wide_pushout.head_desc, category.assoc],
        erw category.id_comp }
    end }, }

/-- A helper function used in defining the ÄŒech conerve adjunction. -/
@[simps]
def cech_conerve_equiv (F : arrow C) (X : cosimplicial_object.augmented C) :
  (F.augmented_cech_conerve âŸ¶ X) â‰ƒ (F âŸ¶ augmented.to_arrow.obj X) :=
{ to_fun := equivalence_left_to_right _ _,
  inv_fun := equivalence_right_to_left _ _,
  left_inv := begin
    intro A,
    dsimp,
    ext,
    { refl, },
    { cases j,
      dsimp,
      simp only [arrow.cech_conerve_map, wide_pushout.Î¹_desc, category.assoc,
        â†nat_trans.naturality, wide_pushout.Î¹_desc_assoc],
      refl },
    { dsimp,
      erw wide_pushout.head_desc,
      have := A.w,
      apply_fun (Î» e, e.app x) at this,
      rw nat_trans.comp_app at this,
      erw this,
      refl },
  end,
  right_inv := begin
    intro A,
    dsimp,
    ext,
    { refl, },
    { dsimp,
      erw wide_pushout.Î¹_desc,
      nth_rewrite 1 â† category.comp_id A.right,
      congr' 1,
      convert X.right.map_id _,
      ext âŸ¨a,haâŸ©,
      change a < 1 at ha,
      change 0 = a,
      linarith },
  end }

/-- The augmented ÄŒech conerve construction is left adjoint to the `to_arrow` functor. -/
abbreviation cech_conerve_adjunction :
  augmented_cech_conerve âŠ£ (augmented.to_arrow : _ â¥¤ arrow C) :=
adjunction.mk_of_hom_equiv { hom_equiv := cech_conerve_equiv }

end cosimplicial_object

end category_theory
