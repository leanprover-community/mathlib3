/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import category_theory.equivalence

/-!
> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.
 Tools for compatibilities between Dold-Kan equivalences

The purpose of this file is to introduce tools which will enable the
construction of the Dold-Kan equivalence `simplicial_object C â‰Œ chain_complex C â„•`
for a pseudoabelian category `C` from the equivalence
`karoubi (simplicial_object C) â‰Œ karoubi (chain_complex C â„•)` and the two
equivalences `simplicial_object C â‰… karoubi (simplicial_object C)` and
`chain_complex C â„• â‰… karoubi (chain_complex C â„•)`.

It is certainly possible to get an equivalence `simplicial_object C â‰Œ chain_complex C â„•`
using a compositions of the three equivalences above, but then neither the functor
nor the inverse would have good definitional properties. For example, it would be better
if the inverse functor of the equivalence was exactly the functor
`Î“â‚€ : simplicial_object C â¥¤ chain_complex C â„•` which was constructed in `functor_gamma.lean`.

In this file, given four categories `A`, `A'`, `B`, `B'`, equivalences `eA : A â‰… A'`,
`eB : B â‰… B'`, `e' : A' â‰… B'`, functors `F : A â¥¤ B'`, `G : B â¥¤ A` equipped with certain
compatibilities, we construct successive equivalences:
- `equivalenceâ‚€` from `A` to `B'`, which is the composition of `eA` and `e'`.
- `equivalenceâ‚` from `A` to `B'`, with the same inverse functor as `equivalenceâ‚€`,
but whose functor is `F`.
- `equivalenceâ‚‚` from `A` to `B`, which is the composition of `equivalenceâ‚` and the
inverse of `eB`:
- `equivalence` from `A` to `B`, which has the same functor `F â‹™ eB.inverse` as `equivalenceâ‚‚`,
but whose inverse functor is `G`.

When extra assumptions are given, we shall also provide simplification lemmas for the
unit and counit isomorphisms of `equivalence`. (TODO)

-/

open category_theory category_theory.category

namespace algebraic_topology

namespace dold_kan

namespace compatibility

variables {A A' B B' : Type*} [category A] [category A'] [category B] [category B']
  (eA : A â‰Œ A') (eB : B â‰Œ B') (e' : A' â‰Œ B')
  {F : A â¥¤ B'} (hF : eA.functor â‹™ e'.functor â‰… F)
  {G : B â¥¤ A} (hG : eB.functor â‹™ e'.inverse â‰… G â‹™ eA.functor)

/-- A basic equivalence `A â‰… B'` obtained by composing `eA : A â‰… A'` and `e' : A' â‰… B'`. -/
@[simps functor inverse unit_iso_hom_app]
def equivalenceâ‚€ : A â‰Œ B' := eA.trans e'

include hF
variables {eA} {e'}

/-- An intermediate equivalence `A â‰… B'` whose functor is `F` and whose inverse is
`e'.inverse â‹™ eA.inverse`. -/
@[simps functor]
def equivalenceâ‚ : A â‰Œ B' :=
begin
  letI : is_equivalence F :=
    is_equivalence.of_iso hF (is_equivalence.of_equivalence (equivalenceâ‚€ eA e')),
  exact F.as_equivalence,
end

lemma equivalenceâ‚_inverse : (equivalenceâ‚ hF).inverse = e'.inverse â‹™ eA.inverse := rfl

/-- The counit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps]
def equivalenceâ‚_counit_iso :
  (e'.inverse â‹™ eA.inverse) â‹™ F â‰… ğŸ­ B' :=
calc (e'.inverse â‹™ eA.inverse) â‹™ F
    â‰… (e'.inverse â‹™ eA.inverse) â‹™ (eA.functor â‹™ e'.functor) : iso_whisker_left _ hF.symm
... â‰… e'.inverse â‹™ (eA.inverse â‹™ eA.functor) â‹™ e'.functor : iso.refl _
... â‰… e'.inverse â‹™ ğŸ­ _ â‹™ e'.functor : iso_whisker_left _ (iso_whisker_right eA.counit_iso _)
... â‰… e'.inverse â‹™ e'.functor : iso.refl _
... â‰… ğŸ­ B' : e'.counit_iso

lemma equivalenceâ‚_counit_iso_eq : (equivalenceâ‚ hF).counit_iso = equivalenceâ‚_counit_iso hF :=
begin
  ext Y,
  dsimp [equivalenceâ‚€, equivalenceâ‚, is_equivalence.inverse, is_equivalence.of_equivalence],
  simp only [equivalenceâ‚_counit_iso_hom_app, category_theory.functor.map_id, comp_id],
end

/-- The unit isomorphism of the equivalence `equivalenceâ‚` between `A` and `B'`. -/
@[simps]
def equivalenceâ‚_unit_iso :
  ğŸ­ A â‰… F â‹™ (e'.inverse â‹™ eA.inverse) :=
calc ğŸ­ A â‰… eA.functor â‹™ eA.inverse : eA.unit_iso
... â‰… eA.functor â‹™ ğŸ­ A' â‹™ eA.inverse : iso.refl _
... â‰… eA.functor â‹™ (e'.functor â‹™ e'.inverse) â‹™ eA.inverse :
        iso_whisker_left _ (iso_whisker_right e'.unit_iso _)
... â‰… (eA.functor â‹™ e'.functor) â‹™ (e'.inverse â‹™ eA.inverse) : iso.refl _
... â‰… F â‹™ (e'.inverse â‹™ eA.inverse) : iso_whisker_right hF _

lemma equivalenceâ‚_unit_iso_eq : (equivalenceâ‚ hF).unit_iso = equivalenceâ‚_unit_iso hF :=
begin
  ext X,
  dsimp [equivalenceâ‚€, equivalenceâ‚, nat_iso.hcomp,
    is_equivalence.of_equivalence],
  simp only [id_comp, assoc, equivalenceâ‚_unit_iso_hom_app],
end

include eB

/-- An intermediate equivalence `A â‰… B` obtained as the composition of `equivalenceâ‚` and
the inverse of `eB : B â‰Œ B'`. -/
@[simps functor]
def equivalenceâ‚‚ : A â‰Œ B := (equivalenceâ‚ hF).trans eB.symm

lemma equivalenceâ‚‚_inverse : (equivalenceâ‚‚ eB hF).inverse =
  eB.functor â‹™ e'.inverse â‹™ eA.inverse := rfl

/-- The counit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps]
def equivalenceâ‚‚_counit_iso :
  (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ (F â‹™ eB.inverse) â‰… ğŸ­ B :=
calc (eB.functor â‹™ e'.inverse â‹™ eA.inverse) â‹™ (F â‹™ eB.inverse)
    â‰… eB.functor â‹™ (e'.inverse â‹™ eA.inverse â‹™ F) â‹™ eB.inverse : iso.refl _
... â‰… eB.functor â‹™ ğŸ­ _ â‹™ eB.inverse :
        iso_whisker_left _ (iso_whisker_right (equivalenceâ‚_counit_iso hF) _)
... â‰… eB.functor â‹™ eB.inverse : iso.refl _
... â‰… ğŸ­ B : eB.unit_iso.symm

lemma equivalenceâ‚‚_counit_iso_eq :
  (equivalenceâ‚‚ eB hF).counit_iso = equivalenceâ‚‚_counit_iso eB hF :=
begin
  ext Y',
  dsimp [equivalenceâ‚‚, iso.refl],
  simp only [equivalenceâ‚_counit_iso_eq, equivalenceâ‚‚_counit_iso_hom_app,
    equivalenceâ‚_counit_iso_hom_app, functor.map_comp, assoc],
end

/-- The unit isomorphism of the equivalence `equivalenceâ‚‚` between `A` and `B`. -/
@[simps]
def equivalenceâ‚‚_unit_iso :
  ğŸ­ A â‰… (F â‹™ eB.inverse) â‹™ (eB.functor â‹™ e'.inverse â‹™ eA.inverse) :=
calc ğŸ­ A â‰… F â‹™ e'.inverse â‹™ eA.inverse : equivalenceâ‚_unit_iso hF
... â‰… F â‹™ ğŸ­ B' â‹™ (e'.inverse â‹™ eA.inverse) : iso.refl _
... â‰… F â‹™ (eB.inverse â‹™ eB.functor) â‹™ e'.inverse â‹™ eA.inverse :
        iso_whisker_left _ (iso_whisker_right eB.counit_iso.symm _)
... â‰… (F â‹™ eB.inverse) â‹™ (eB.functor â‹™ e'.inverse â‹™ eA.inverse) : iso.refl _

lemma equivalenceâ‚‚_unit_iso_eq :
  (equivalenceâ‚‚ eB hF).unit_iso = equivalenceâ‚‚_unit_iso eB hF :=
begin
  ext X,
  dsimp [equivalenceâ‚‚],
  simpa only [equivalenceâ‚‚_unit_iso_hom_app, equivalenceâ‚_unit_iso_eq,
    equivalenceâ‚_unit_iso_hom_app, assoc, nat_iso.cancel_nat_iso_hom_left],
end

variable {eB}
include hG

/-- The equivalence `A â‰… B` whose functor is `F â‹™ eB.inverse` and
whose inverse is `G : B â‰… A`. -/
@[simps inverse]
def equivalence : A â‰Œ B :=
begin
  letI : is_equivalence G := begin
    refine is_equivalence.of_iso _ (is_equivalence.of_equivalence (equivalenceâ‚‚ eB hF).symm),
    calc eB.functor â‹™ e'.inverse â‹™ eA.inverse
        â‰… (eB.functor â‹™ e'.inverse) â‹™ eA.inverse : iso.refl _
    ... â‰… (G â‹™ eA.functor) â‹™ eA.inverse : iso_whisker_right hG _
    ... â‰… G â‹™ ğŸ­ A : iso_whisker_left _ eA.unit_iso.symm
    ... â‰… G : functor.right_unitor G,
  end,
  exact G.as_equivalence.symm,
end

lemma equivalence_functor : (equivalence hF hG).functor = F â‹™ eB.inverse := rfl

omit hG hF

/-- The isomorphism `eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor` deduced
from the counit isomorphism of `e'`. -/
@[simps hom_app]
def Ï„â‚€ : eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor :=
calc eB.functor â‹™ e'.inverse â‹™ e'.functor
        â‰… eB.functor â‹™ ğŸ­ _ : iso_whisker_left _ e'.counit_iso
...     â‰… eB.functor : functor.right_unitor _

include hF hG

/-- The isomorphism `eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor` deduced
from the isomorphisms `hF : eA.functor â‹™ e'.functor â‰… F`,
`hG : eB.functor â‹™ e'.inverse â‰… G â‹™ eA.functor` and the datum of
an isomorphism `Î· : G â‹™ F â‰… eB.functor`. -/
@[simps hom_app]
def Ï„â‚ (Î· : G â‹™ F â‰… eB.functor) :
  eB.functor â‹™ e'.inverse â‹™ e'.functor â‰… eB.functor :=
calc eB.functor â‹™ e'.inverse â‹™ e'.functor
    â‰… (eB.functor â‹™ e'.inverse) â‹™ e'.functor : iso.refl _
... â‰… (G â‹™ eA.functor) â‹™ e'.functor : iso_whisker_right hG _
... â‰… G â‹™ (eA.functor â‹™ e'.functor) : by refl
... â‰… G â‹™ F : iso_whisker_left _ hF
... â‰… eB.functor : Î·

variables (Î· : G â‹™ F â‰… eB.functor) (hÎ· : Ï„â‚€ = Ï„â‚ hF hG Î·)

omit hF hG
include Î·

/-- The counit isomorphism of `equivalence`. -/
@[simps]
def equivalence_counit_iso : G â‹™ (F â‹™ eB.inverse) â‰… ğŸ­ B :=
calc G â‹™ (F â‹™ eB.inverse) â‰… (G â‹™ F) â‹™ eB.inverse : iso.refl _
... â‰… eB.functor â‹™ eB.inverse : iso_whisker_right Î· _
... â‰… ğŸ­ B : eB.unit_iso.symm

variables {Î· hF hG}
include hÎ·

lemma equivalence_counit_iso_eq :
  (equivalence hF hG).counit_iso = equivalence_counit_iso Î· :=
begin
  ext1, apply nat_trans.ext, ext Y,
  dsimp [equivalence, equivalence_counit_iso, is_equivalence.of_equivalence],
  simp only [equivalenceâ‚‚_counit_iso_eq eB hF],
  erw [nat_trans.id_app, nat_trans.id_app],
  dsimp [equivalenceâ‚‚, equivalenceâ‚],
  simp only [assoc, comp_id, F.map_id, id_comp,
    equivalenceâ‚‚_counit_iso_hom_app, â† eB.inverse.map_comp_assoc,
    â† Ï„â‚€_hom_app, hÎ·, Ï„â‚_hom_app],
  erw hF.inv.naturality_assoc,
  congr' 2,
  dsimp,
  simp only [assoc, â† e'.functor.map_comp_assoc, eA.functor.map_comp,
    equivalence.fun_inv_map, iso.inv_hom_id_app_assoc, hG.inv_hom_id_app],
  dsimp,
  rw [comp_id, eA.functor_unit_iso_comp, e'.functor.map_id, id_comp, hF.inv_hom_id_app_assoc],
end

omit hÎ· Î· eB
include hF

variable (hF)

/-- The isomorphism `eA.functor â‰… F â‹™ e'.inverse` deduced from the
unit isomorphism of `e'` and the isomorphism `hF : eA.functor â‹™ e'.functor â‰… F`. -/
@[simps]
def Ï… : eA.functor â‰… F â‹™ e'.inverse :=
calc eA.functor â‰… eA.functor â‹™ ğŸ­ A' : (functor.left_unitor _).symm
... â‰… eA.functor â‹™ (e'.functor â‹™ e'.inverse) : iso_whisker_left _ e'.unit_iso
... â‰… (eA.functor â‹™ e'.functor) â‹™ e'.inverse : iso.refl _
... â‰… F â‹™ e'.inverse : iso_whisker_right hF _

variables (Îµ : eA.functor â‰… F â‹™ e'.inverse) (hÎµ : Ï… hF = Îµ)

include Îµ hG
omit hF

variable (hG)

/-- The unit isomorphism of `equivalence`. -/
@[simps]
def equivalence_unit_iso : ğŸ­ A â‰… (F â‹™ eB.inverse) â‹™ G :=
calc ğŸ­ A â‰… eA.functor â‹™ eA.inverse : eA.unit_iso
... â‰… (F â‹™ e'.inverse) â‹™ eA.inverse : iso_whisker_right Îµ _
... â‰… F â‹™ ğŸ­ B' â‹™ e'.inverse â‹™ eA.inverse : iso.refl _
... â‰… F â‹™ (eB.inverse â‹™ eB.functor) â‹™ (e'.inverse â‹™ eA.inverse) :
      iso_whisker_left _ (iso_whisker_right eB.counit_iso.symm _)
... â‰… (F â‹™ eB.inverse) â‹™ (eB.functor â‹™ e'.inverse) â‹™ eA.inverse : iso.refl _
... â‰… (F â‹™ eB.inverse) â‹™ (G â‹™ eA.functor) â‹™ eA.inverse :
      iso_whisker_left _ (iso_whisker_right hG _)
... â‰… (F â‹™ eB.inverse â‹™ G) â‹™ (eA.functor â‹™ eA.inverse) : iso.refl _
... â‰… (F â‹™ eB.inverse â‹™ G) â‹™ ğŸ­ A : iso_whisker_left _ eA.unit_iso.symm
... â‰… (F â‹™ eB.inverse) â‹™ G : iso.refl _

include hÎµ
variables {Îµ hF hG}

lemma equivalence_unit_iso_eq :
  (equivalence hF hG).unit_iso = equivalence_unit_iso hG Îµ :=
begin
  ext1, apply nat_trans.ext, ext X,
  dsimp [equivalence, iso.refl, nat_iso.hcomp, is_equivalence.inverse,
    is_equivalence.of_equivalence],
  erw [nat_trans.id_app, id_comp, G.map_id, comp_id, comp_id],
  simp only [equivalenceâ‚‚_unit_iso_eq eB hF, equivalenceâ‚‚_unit_iso_hom_app],
  dsimp [equivalenceâ‚‚, equivalenceâ‚],
  simp only [assoc, equivalence_unit_iso_hom_app, nat_iso.cancel_nat_iso_hom_left,
    â† eA.inverse.map_comp_assoc, â† hÎµ, Ï…_hom_app],
end

end compatibility

end dold_kan

end algebraic_topology
