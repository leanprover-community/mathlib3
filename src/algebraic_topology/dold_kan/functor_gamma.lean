/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import algebraic_topology.dold_kan.split_simplicial_object

/-!

# Construction of the inverse functor of the Dold-Kan equivalence

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.


In this file, we construct the functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`
which shall be the inverse functor of the Dold-Kan equivalence in the case of abelian categories,
and more generally pseudoabelian categories.

By definition, when `K` is a chain_complex, `Î“â‚€.obj K` is a simplicial object which
sends `Î” : simplex_categoryáµ’áµ–` to a certain coproduct indexed by the set
`splitting.index_set Î”` whose elements consists of epimorphisms `e : Î”.unop âŸ¶ Î”'.unop`
(with `Î”' : simplex_categoryáµ’áµ–`); the summand attached to such an `e` is `K.X Î”'.unop.len`.
By construction, `Î“â‚€.obj K` is a split simplicial object whose splitting is `Î“â‚€.splitting K`.

We also construct `Î“â‚‚ : karoubi (chain_complex C â„•) â¥¤ karoubi (simplicial_object C)`
which shall be an equivalence for any additive category `C`.

-/

noncomputable theory

open category_theory category_theory.category category_theory.limits
  simplex_category simplicial_object opposite category_theory.idempotents
open_locale simplicial dold_kan

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category C] [preadditive C] (K K' : chain_complex C â„•) (f : K âŸ¶ K')
  {Î”'' Î”' Î” : simplex_category} (i' : Î”'' âŸ¶ Î”') [mono i'] (i : Î”' âŸ¶ Î”) [mono i]

/-- `is_Î´â‚€ i` is a simple condition used to check whether a monomorphism `i` in
`simplex_category` identifies to the coface map `Î´ 0`. -/
@[nolint unused_arguments]
def is_Î´â‚€ {Î” Î”' : simplex_category} (i : Î”' âŸ¶ Î”) [mono i] : Prop :=
(Î”.len = Î”'.len+1) âˆ§ (i.to_order_hom 0 â‰  0)

namespace is_Î´â‚€

lemma iff {j : â„•} {i : fin (j+2)} : is_Î´â‚€ (simplex_category.Î´ i) â†” i = 0 :=
begin
  split,
  { rintro âŸ¨hâ‚, hâ‚‚âŸ©,
    by_contradiction,
    exact hâ‚‚ (fin.succ_above_ne_zero_zero h), },
  { rintro rfl,
    exact âŸ¨rfl, fin.succ_ne_zero _âŸ©, },
end

lemma eq_Î´â‚€ {n : â„•} {i : [n] âŸ¶ [n+1]} [mono i] (hi : is_Î´â‚€ i) :
  i = simplex_category.Î´ 0 :=
begin
  unfreezingI { obtain âŸ¨j, rflâŸ© := simplex_category.eq_Î´_of_mono i, },
  rw iff at hi,
  rw hi,
end

end is_Î´â‚€

namespace Î“â‚€

namespace obj

/-- In the definition of `(Î“â‚€.obj K).obj Î”` as a direct sum indexed by `A : splitting.index_set Î”`,
the summand `summand K Î” A` is `K.X A.1.len`. -/
def summand (Î” : simplex_categoryáµ’áµ–) (A : splitting.index_set Î”) : C := K.X A.1.unop.len

/-- The functor `Î“â‚€` sends a chain complex `K` to the simplicial object which
sends `Î”` to the direct sum of the objects `summand K Î” A` for all `A : splitting.index_set Î”` -/
def objâ‚‚ (K : chain_complex C â„•) (Î” : simplex_categoryáµ’áµ–) [has_finite_coproducts C] : C :=
âˆ (Î» (A : splitting.index_set Î”), summand K Î” A)

namespace termwise

/-- A monomorphism `i : Î”' âŸ¶ Î”` induces a morphism `K.X Î”.len âŸ¶ K.X Î”'.len` which
is the identity if `Î” = Î”'`, the differential on the complex `K` if `i = Î´ 0`, and
zero otherwise. -/
def map_mono (K : chain_complex C â„•) {Î”' Î” : simplex_category} (i : Î”' âŸ¶ Î”) [mono i] :
  K.X Î”.len âŸ¶ K.X Î”'.len :=
begin
  by_cases Î” = Î”',
  { exact eq_to_hom (by congr'), },
  { by_cases is_Î´â‚€ i,
    { exact K.d Î”.len Î”'.len, },
    { exact 0, }, },
end

variable (Î”)

lemma map_mono_id : map_mono K (ğŸ™ Î”) = ğŸ™ _ :=
by { unfold map_mono, simp only [eq_self_iff_true, eq_to_hom_refl, dite_eq_ite, if_true], }

variable {Î”}

lemma map_mono_Î´â‚€' (hi : is_Î´â‚€ i) : map_mono K i = K.d Î”.len Î”'.len :=
begin
  unfold map_mono,
  classical,
  rw [dif_neg, dif_pos hi],
  unfreezingI { rintro rfl, },
  simpa only [self_eq_add_right, nat.one_ne_zero] using hi.1,
end

@[simp]
lemma map_mono_Î´â‚€ {n : â„•} : map_mono K (Î´ (0 : fin (n+2))) = K.d (n+1) n :=
map_mono_Î´â‚€' K _ (by rw is_Î´â‚€.iff)

lemma map_mono_eq_zero (hâ‚ : Î” â‰  Î”') (hâ‚‚ : Â¬is_Î´â‚€ i) : map_mono K i = 0 :=
by { unfold map_mono, rw ne.def at hâ‚, split_ifs, refl, }

variables {K K'}

@[simp, reassoc]
lemma map_mono_naturality : map_mono K i â‰« f.f Î”'.len = f.f Î”.len â‰« map_mono K' i :=
begin
  unfold map_mono,
  split_ifs,
  { unfreezingI { subst h, },
    simp only [id_comp, eq_to_hom_refl, comp_id], },
  { rw homological_complex.hom.comm, },
  { rw [zero_comp, comp_zero], }
end

variable (K)

@[simp, reassoc]
lemma map_mono_comp : map_mono K i â‰« map_mono K i' = map_mono K (i' â‰« i) :=
begin
  /- case where i : Î”' âŸ¶ Î” is the identity -/
  by_cases hâ‚ : Î” = Î”',
  { unfreezingI { subst hâ‚, },
    simp only [simplex_category.eq_id_of_mono i,
      comp_id, id_comp, map_mono_id K, eq_to_hom_refl], },
  /- case where i' : Î”'' âŸ¶ Î”' is the identity -/
  by_cases hâ‚‚ : Î”' = Î”'',
  { unfreezingI { subst hâ‚‚, },
    simp only [simplex_category.eq_id_of_mono i',
      comp_id, id_comp, map_mono_id K, eq_to_hom_refl], },
  /- then the RHS is always zero -/
  obtain âŸ¨k, hkâŸ© := nat.exists_eq_add_of_lt (len_lt_of_mono i hâ‚),
  obtain âŸ¨k', hk'âŸ© := nat.exists_eq_add_of_lt (len_lt_of_mono i' hâ‚‚),
  have eq : Î”.len = Î”''.len + (k+k'+2) := by linarith,
  rw map_mono_eq_zero K (i' â‰« i) _ _, rotate,
  { by_contradiction,
    simpa only [self_eq_add_right, h] using eq, },
  { by_contradiction,
    simp only [h.1, add_right_inj] at eq,
    linarith, },
  /- in all cases, the LHS is also zero, either by definition, or because d â‰« d = 0 -/
  by_cases hâ‚ƒ : is_Î´â‚€ i,
  { by_cases hâ‚„ : is_Î´â‚€ i',
    { rw [map_mono_Î´â‚€' K i hâ‚ƒ, map_mono_Î´â‚€' K i' hâ‚„,
        homological_complex.d_comp_d], },
    { simp only [map_mono_eq_zero K i' hâ‚‚ hâ‚„, comp_zero], }, },
  { simp only [map_mono_eq_zero K i hâ‚ hâ‚ƒ, zero_comp], },
end

end termwise

variable [has_finite_coproducts C]

/-- The simplicial morphism on the simplicial object `Î“â‚€.obj K` induced by
a morphism `Î”' â†’ Î”` in `simplex_category` is defined on each summand
associated to an `A : Î“_index_set Î”` in terms of the epi-mono factorisation
of `Î¸ â‰« A.e`. -/
def map (K : chain_complex C â„•) {Î”' Î” : simplex_categoryáµ’áµ–} (Î¸ : Î” âŸ¶ Î”') :
  objâ‚‚ K Î” âŸ¶ objâ‚‚ K Î”' :=
sigma.desc (Î» A, termwise.map_mono K (image.Î¹ (Î¸.unop â‰« A.e)) â‰«
  (sigma.Î¹ (summand K Î”') (A.pull Î¸)))

@[reassoc]
lemma map_on_summandâ‚€ {Î” Î”' : simplex_categoryáµ’áµ–} (A : splitting.index_set Î”) {Î¸ : Î” âŸ¶ Î”'}
  {Î”'' : simplex_category} {e : Î”'.unop âŸ¶ Î”''} {i : Î”'' âŸ¶ A.1.unop} [epi e] [mono i]
  (fac : e â‰« i = Î¸.unop â‰« A.e) :
  (sigma.Î¹ (summand K Î”) A) â‰« map K Î¸ =
    termwise.map_mono K i â‰« sigma.Î¹ (summand K Î”') (splitting.index_set.mk e) :=
begin
  simp only [map, colimit.Î¹_desc, cofan.mk_Î¹_app],
  have h := simplex_category.image_eq fac,
  unfreezingI { subst h, },
  congr,
  { exact simplex_category.image_Î¹_eq fac, },
  { dsimp only [simplicial_object.splitting.index_set.pull],
    congr,
    exact simplex_category.factor_thru_image_eq fac, },
end

@[reassoc]
lemma map_on_summandâ‚€' {Î” Î”' : simplex_categoryáµ’áµ–} (A : splitting.index_set Î”) (Î¸ : Î” âŸ¶ Î”') :
  (sigma.Î¹ (summand K Î”) A) â‰« map K Î¸ =
    termwise.map_mono K (image.Î¹ (Î¸.unop â‰« A.e)) â‰« sigma.Î¹ (summand K _) (A.pull Î¸) :=
map_on_summandâ‚€ K A (A.fac_pull Î¸)

end obj

variable [has_finite_coproducts C]

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, on objects. -/
@[simps]
def obj (K : chain_complex C â„•) : simplicial_object C :=
{ obj := Î» Î”, obj.objâ‚‚ K Î”,
  map := Î» Î” Î”' Î¸, obj.map K Î¸,
  map_id' := Î» Î”, begin
    ext A,
    cases A,
    have fac : A.e â‰« ğŸ™ A.1.unop = (ğŸ™ Î”).unop â‰« A.e := by rw [unop_id, comp_id, id_comp],
    erw [obj.map_on_summandâ‚€ K A fac, obj.termwise.map_mono_id, id_comp, comp_id],
    unfreezingI { rcases A with âŸ¨Î”', âŸ¨e, heâŸ©âŸ©, },
    refl,
  end,
  map_comp' := Î» Î”'' Î”' Î” Î¸' Î¸, begin
    ext A,
    cases A,
    have fac : Î¸.unop â‰« Î¸'.unop â‰« A.e = (Î¸' â‰« Î¸).unop â‰« A.e := by rw [unop_comp, assoc],
    rw [â† image.fac (Î¸'.unop â‰« A.e), â† assoc,
      â† image.fac (Î¸.unop â‰« factor_thru_image (Î¸'.unop â‰« A.e)), assoc] at fac,
    simpa only [obj.map_on_summandâ‚€'_assoc K A Î¸', obj.map_on_summandâ‚€' K _ Î¸,
      obj.termwise.map_mono_comp_assoc, obj.map_on_summandâ‚€ K A fac],
  end }

lemma splitting_map_eq_id (Î” : simplex_categoryáµ’áµ–) :
  (simplicial_object.splitting.map (Î“â‚€.obj K)
    (Î» (n : â„•), sigma.Î¹ (Î“â‚€.obj.summand K (op [n])) (splitting.index_set.id (op [n]))) Î”)
    = ğŸ™ _ :=
begin
  ext A,
  discrete_cases,
  induction Î” using opposite.rec,
  induction Î” with n,
  dsimp,
  simp only [colimit.Î¹_desc, cofan.mk_Î¹_app, comp_id, Î“â‚€.obj_map],
  rw [Î“â‚€.obj.map_on_summandâ‚€ K
    (simplicial_object.splitting.index_set.id A.1) (show A.e â‰« ğŸ™ _ = A.e.op.unop â‰« ğŸ™ _, by refl),
    Î“â‚€.obj.termwise.map_mono_id, A.ext'],
  apply id_comp,
end

/-- By construction, the simplicial `Î“â‚€.obj K` is equipped with a splitting. -/
def splitting (K : chain_complex C â„•) : simplicial_object.splitting (Î“â‚€.obj K) :=
{ N := Î» n, K.X n,
  Î¹ := Î» n, sigma.Î¹ (Î“â‚€.obj.summand K (op [n])) (splitting.index_set.id (op [n])),
  map_is_iso' := Î» Î”, begin
    rw Î“â‚€.splitting_map_eq_id,
    apply is_iso.id,
  end, }

@[simp]
lemma splitting_iso_hom_eq_id (Î” : simplex_categoryáµ’áµ–) : ((splitting K).iso Î”).hom = ğŸ™ _ :=
splitting_map_eq_id K Î”

@[reassoc]
lemma obj.map_on_summand {Î” Î”' : simplex_categoryáµ’áµ–} (A : splitting.index_set Î”) (Î¸ : Î” âŸ¶ Î”')
  {Î”'' : simplex_category}
  {e : Î”'.unop âŸ¶ Î”''} {i : Î”'' âŸ¶ A.1.unop} [epi e] [mono i]
  (fac : e â‰« i = Î¸.unop â‰« A.e) : (Î“â‚€.splitting K).Î¹_summand A â‰« (Î“â‚€.obj K).map Î¸ =
  Î“â‚€.obj.termwise.map_mono K i â‰« (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.mk e) :=
begin
  dsimp only [simplicial_object.splitting.Î¹_summand,
    simplicial_object.splitting.Î¹_coprod],
  simp only [assoc, Î“â‚€.splitting_iso_hom_eq_id, id_comp, comp_id],
  exact Î“â‚€.obj.map_on_summandâ‚€ K A fac,
end

@[reassoc]
lemma obj.map_on_summand' {Î” Î”' : simplex_categoryáµ’áµ–} (A : splitting.index_set Î”) (Î¸ : Î” âŸ¶ Î”') :
  (splitting K).Î¹_summand A â‰« (obj K).map Î¸ =
    obj.termwise.map_mono K (image.Î¹ (Î¸.unop â‰« A.e)) â‰« (splitting K).Î¹_summand (A.pull Î¸) :=
by { apply obj.map_on_summand, apply image.fac, }

@[reassoc]
lemma obj.map_mono_on_summand_id {Î” Î”' : simplex_category} (i : Î”' âŸ¶ Î”) [mono i] :
  (splitting K).Î¹_summand (splitting.index_set.id (op Î”)) â‰« (obj K).map i.op =
  obj.termwise.map_mono K i â‰« (splitting K).Î¹_summand (splitting.index_set.id (op Î”')) :=
obj.map_on_summand K (splitting.index_set.id (op Î”)) i.op (rfl : ğŸ™ _ â‰« i = i â‰« ğŸ™ _)

@[reassoc]
lemma obj.map_epi_on_summand_id {Î” Î”' : simplex_category } (e : Î”' âŸ¶ Î”) [epi e] :
  (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op Î”)) â‰« (Î“â‚€.obj K).map e.op =
    (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.mk e) :=
by simpa only [Î“â‚€.obj.map_on_summand K (splitting.index_set.id (op Î”)) e.op
    (rfl : e â‰« ğŸ™ Î” = e â‰« ğŸ™ Î”), Î“â‚€.obj.termwise.map_mono_id] using id_comp _

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, on morphisms. -/
@[simps]
def map {K K' : chain_complex C â„•} (f : K âŸ¶ K') : obj K âŸ¶ obj K' :=
{ app := Î» Î”, (Î“â‚€.splitting K).desc Î” (Î» A, f.f A.1.unop.len â‰« (Î“â‚€.splitting K').Î¹_summand A),
  naturality' := Î» Î”' Î” Î¸, begin
    apply (Î“â‚€.splitting K).hom_ext',
    intro A,
    simp only [(splitting K).Î¹_desc_assoc, obj.map_on_summand'_assoc K _ Î¸,
      (splitting K).Î¹_desc, assoc, obj.map_on_summand' K' _ Î¸],
    apply obj.termwise.map_mono_naturality_assoc,
  end, }

end Î“â‚€

variable [has_finite_coproducts C]

/-- The functor `Î“â‚€' : chain_complex C â„• â¥¤ simplicial_object.split C`
that induces `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, which
shall be the inverse functor of the Dold-Kan equivalence for
abelian or pseudo-abelian categories. -/
@[simps]
def Î“â‚€' : chain_complex C â„• â¥¤ simplicial_object.split C :=
{ obj := Î» K, simplicial_object.split.mk' (Î“â‚€.splitting K),
  map := Î» K K' f,
  { F := Î“â‚€.map f,
    f := f.f,
    comm' := Î» n, by { dsimp, simpa only [â† splitting.Î¹_summand_id,
      (Î“â‚€.splitting K).Î¹_desc], }, }, }

/-- The functor `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`, which is
the inverse functor of the Dold-Kan equivalence when `C` is an abelian
category, or more generally a pseudoabelian category. -/
@[simps]
def Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C := Î“â‚€' â‹™ split.forget _


/-- The extension of `Î“â‚€ : chain_complex C â„• â¥¤ simplicial_object C`
on the idempotent completions. It shall be an equivalence of categories
for any additive category `C`. -/
@[simps]
def Î“â‚‚ : karoubi (chain_complex C â„•) â¥¤ karoubi (simplicial_object C) :=
(category_theory.idempotents.functor_extensionâ‚‚ _ _).obj Î“â‚€

lemma higher_faces_vanish.on_Î“â‚€_summand_id (K : chain_complex C â„•) (n : â„•) :
  higher_faces_vanish (n+1) ((Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [n+1]))) :=
begin
  intros j hj,
  have eq := Î“â‚€.obj.map_mono_on_summand_id K (simplex_category.Î´ j.succ),
  rw [Î“â‚€.obj.termwise.map_mono_eq_zero K, zero_comp] at eq, rotate,
  { intro h,
    exact (nat.succ_ne_self n) (congr_arg simplex_category.len h), },
  { exact Î» h, fin.succ_ne_zero j (by simpa only [is_Î´â‚€.iff] using h), },
  exact eq,
end

@[simp, reassoc]
lemma P_infty_on_Î“â‚€_splitting_summand_eq_self
  (K : chain_complex C â„•) {n : â„•} :
  (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [n])) â‰« (P_infty : K[Î“â‚€.obj K] âŸ¶ _).f n =
    (Î“â‚€.splitting K).Î¹_summand (splitting.index_set.id (op [n])) :=
begin
  rw P_infty_f,
  cases n,
  { simpa only [P_f_0_eq] using comp_id _, },
  { exact (higher_faces_vanish.on_Î“â‚€_summand_id K n).comp_P_eq_self, },
end

end dold_kan

end algebraic_topology
