/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import algebraic_topology.dold_kan.gamma_comp_n
import algebraic_topology.dold_kan.n_reflects_iso

/-!
> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.
 The unit isomorphism of the Dold-Kan equivalence

In order to construct the unit isomorphism of the Dold-Kan equivalence,
we first construct natural transformations
`Î“â‚‚Nâ‚.nat_trans : Nâ‚ â‹™ Î“â‚‚ âŸ¶ to_karoubi (simplicial_object C)` and
`Î“â‚‚Nâ‚‚.nat_trans : Nâ‚‚ â‹™ Î“â‚‚ âŸ¶ ğŸ­ (simplicial_object C)`.
It is then shown that `Î“â‚‚Nâ‚‚.nat_trans` is an isomorphism by using
that it becomes an isomorphism after the application of the functor
`Nâ‚‚ : karoubi (simplicial_object C) â¥¤ karoubi (chain_complex C â„•)`
which reflects isomorphisms.

-/

noncomputable theory

open category_theory category_theory.category category_theory.limits
  category_theory.idempotents simplex_category opposite simplicial_object
open_locale simplicial dold_kan

namespace algebraic_topology

namespace dold_kan

variables {C : Type*} [category C] [preadditive C]

lemma P_infty_comp_map_mono_eq_zero (X : simplicial_object C) {n : â„•}
  {Î”' : simplex_category} (i : Î”' âŸ¶ [n]) [hi : mono i] (hâ‚ : Î”'.len â‰  n) (hâ‚‚ : Â¬is_Î´â‚€ i) :
  P_infty.f n â‰« X.map i.op = 0 :=
begin
  unfreezingI { induction Î”' using simplex_category.rec with m, },
  obtain âŸ¨k, hkâŸ© := nat.exists_eq_add_of_lt (len_lt_of_mono i
    (Î» h, by { rw â† h at hâ‚,  exact hâ‚ rfl, })),
  simp only [len_mk] at hk,
  cases k,
  { change n = m + 1 at hk,
    unfreezingI { subst hk, obtain âŸ¨j, rflâŸ© := eq_Î´_of_mono i, },
    rw is_Î´â‚€.iff at hâ‚‚,
    have hâ‚ƒ : 1 â‰¤ (j : â„•),
    { by_contra,
      exact hâ‚‚ (by simpa only [fin.ext_iff, not_le, nat.lt_one_iff] using h), },
    exact (higher_faces_vanish.of_P (m+1) m).comp_Î´_eq_zero j hâ‚‚ (by linarith), },
  { simp only [nat.succ_eq_add_one, â† add_assoc] at hk,
    clear hâ‚‚ hi,
    subst hk,
    obtain âŸ¨jâ‚, i, rflâŸ© := eq_comp_Î´_of_not_surjective i (Î» h, begin
      have h' := len_le_of_epi (simplex_category.epi_iff_surjective.2 h),
      dsimp at h',
      linarith,
    end),
    obtain âŸ¨jâ‚‚, i, rflâŸ© := eq_comp_Î´_of_not_surjective i (Î» h, begin
      have h' := len_le_of_epi (simplex_category.epi_iff_surjective.2 h),
      dsimp at h',
      linarith,
    end),
    by_cases hjâ‚ : jâ‚ = 0,
    { unfreezingI { subst hjâ‚, },
      rw [assoc, â† simplex_category.Î´_comp_Î´'' (fin.zero_le _)],
      simp only [op_comp, X.map_comp, assoc, P_infty_f],
      erw [(higher_faces_vanish.of_P _ _).comp_Î´_eq_zero_assoc _ jâ‚‚.succ_ne_zero, zero_comp],
      rw fin.coe_succ,
      linarith, },
    { simp only [op_comp, X.map_comp, assoc, P_infty_f],
      erw [(higher_faces_vanish.of_P _ _).comp_Î´_eq_zero_assoc _ hjâ‚, zero_comp],
      by_contra,
      exact hjâ‚ (by { simp only [fin.ext_iff, fin.coe_zero], linarith, }), }, },
end

@[reassoc]
lemma Î“â‚€_obj_termwise_map_mono_comp_P_infty (X : simplicial_object C) {Î” Î”' : simplex_category}
  (i : Î” âŸ¶ Î”') [mono i] :
  Î“â‚€.obj.termwise.map_mono (alternating_face_map_complex.obj X) i â‰« P_infty.f (Î”.len) =
    P_infty.f (Î”'.len) â‰« X.map i.op :=
begin
  unfreezingI
  { induction Î” using simplex_category.rec with n,
    induction Î”' using simplex_category.rec with n', },
  dsimp,
  /- We start with the case `i` is an identity -/
  by_cases n = n',
  { unfreezingI { subst h, },
    simp only [simplex_category.eq_id_of_mono i, Î“â‚€.obj.termwise.map_mono_id, op_id, X.map_id],
    dsimp,
    simp only [id_comp, comp_id], },
  by_cases hi : is_Î´â‚€ i,
  /- The case `i = Î´ 0` -/
  { have h' : n' = n + 1 := hi.left,
    unfreezingI { subst h', },
    simp only [Î“â‚€.obj.termwise.map_mono_Î´â‚€' _ i hi],
    dsimp,
    rw [â† P_infty.comm' _ n rfl, alternating_face_map_complex.obj_d_eq],
    simp only [eq_self_iff_true, id_comp, if_true, preadditive.comp_sum],
    rw finset.sum_eq_single (0 : fin (n+2)), rotate,
    { intros b hb hb',
      rw preadditive.comp_zsmul,
      erw [P_infty_comp_map_mono_eq_zero X (simplex_category.Î´ b) h
        (by { rw is_Î´â‚€.iff, exact hb', }), zsmul_zero], },
    { simp only [finset.mem_univ, not_true, is_empty.forall_iff], },
    { simpa only [hi.eq_Î´â‚€, fin.coe_zero, pow_zero, one_zsmul], }, },
  /- The case `i â‰  Î´ 0` -/
  { rw [Î“â‚€.obj.termwise.map_mono_eq_zero _ i _ hi, zero_comp], swap,
    { by_contradiction h',
      exact h (congr_arg simplex_category.len h'.symm), },
    rw P_infty_comp_map_mono_eq_zero,
    { exact h, },
    { by_contradiction h',
      exact hi h', }, },
end

variable [has_finite_coproducts C]

namespace Î“â‚‚Nâ‚

/-- The natural transformation `Nâ‚ â‹™ Î“â‚‚ âŸ¶ to_karoubi (simplicial_object C)`. -/
@[simps]
def nat_trans : (Nâ‚ : simplicial_object C â¥¤ _) â‹™ Î“â‚‚ âŸ¶ to_karoubi _ :=
{ app := Î» X,
  { f :=
    { app := Î» Î”, (Î“â‚€.splitting K[X]).desc Î” (Î» A, P_infty.f A.1.unop.len â‰« X.map (A.e.op)),
      naturality' := Î» Î” Î”' Î¸, begin
        apply (Î“â‚€.splitting K[X]).hom_ext',
        intro A,
        change _ â‰« (Î“â‚€.obj K[X]).map Î¸  â‰« _ = _,
        simp only [splitting.Î¹_desc_assoc, assoc,
          Î“â‚€.obj.map_on_summand'_assoc, splitting.Î¹_desc],
        erw Î“â‚€_obj_termwise_map_mono_comp_P_infty_assoc X (image.Î¹ (Î¸.unop â‰« A.e)),
        dsimp only [to_karoubi],
        simp only [â† X.map_comp],
        congr' 2,
        simp only [eq_to_hom_refl, id_comp, comp_id, â† op_comp],
        exact quiver.hom.unop_inj (A.fac_pull Î¸),
      end, },
    comm := begin
      apply (Î“â‚€.splitting K[X]).hom_ext,
      intro n,
      dsimp [Nâ‚],
      simp only [â† splitting.Î¹_summand_id, splitting.Î¹_desc,
        comp_id, splitting.Î¹_desc_assoc, assoc, P_infty_f_idem_assoc],
    end, },
  naturality' := Î» X Y f, begin
    ext1,
    apply (Î“â‚€.splitting K[X]).hom_ext,
    intro n,
    dsimp [Nâ‚, to_karoubi],
    simpa only [â†splitting.Î¹_summand_id, splitting.Î¹_desc, splitting.Î¹_desc_assoc,
      assoc, P_infty_f_idem_assoc, karoubi.comp_f, nat_trans.comp_app, Î“â‚‚_map_f_app,
      homological_complex.comp_f, alternating_face_map_complex.map_f,
      P_infty_f_naturality_assoc, nat_trans.naturality],
  end, }

end Î“â‚‚Nâ‚

/-- The compatibility isomorphism relating `Nâ‚‚ â‹™ Î“â‚‚` and `Nâ‚ â‹™ Î“â‚‚`. -/
@[simps]
def compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚ : to_karoubi (simplicial_object C) â‹™ Nâ‚‚ â‹™ Î“â‚‚ â‰… Nâ‚ â‹™ Î“â‚‚ :=
eq_to_iso (functor.congr_obj (functor_extensionâ‚_comp_whiskering_left_to_karoubi _ _) (Nâ‚ â‹™ Î“â‚‚))

namespace Î“â‚‚Nâ‚‚

/-- The natural transformation `Nâ‚‚ â‹™ Î“â‚‚ âŸ¶ ğŸ­ (simplicial_object C)`. -/
def nat_trans : (Nâ‚‚ : karoubi (simplicial_object C) â¥¤ _) â‹™ Î“â‚‚ âŸ¶ ğŸ­ _ :=
((whiskering_left _ _ _).obj _).preimage (compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚.hom â‰« Î“â‚‚Nâ‚.nat_trans)

lemma nat_trans_app_f_app (P : karoubi (simplicial_object C)) :
  Î“â‚‚Nâ‚‚.nat_trans.app P = (Nâ‚‚ â‹™ Î“â‚‚).map P.decomp_id_i â‰«
    (compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚.hom â‰« Î“â‚‚Nâ‚.nat_trans).app P.X â‰« P.decomp_id_p :=
whiskering_left_obj_preimage_app ((compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚.hom â‰« Î“â‚‚Nâ‚.nat_trans)) P

end Î“â‚‚Nâ‚‚

lemma compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚_nat_trans (X : simplicial_object C) :
  Î“â‚‚Nâ‚.nat_trans.app X = (compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚.app X).inv â‰«
    Î“â‚‚Nâ‚‚.nat_trans.app ((to_karoubi _).obj X) :=
begin
  rw [â† cancel_epi (compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚.app X).hom, iso.hom_inv_id_assoc],
  exact congr_app (((whiskering_left _ _ _).obj _).image_preimage
    (compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚.hom â‰« Î“â‚‚Nâ‚.nat_trans : _ âŸ¶ to_karoubi _ â‹™ ğŸ­ _ )).symm X,
end

lemma identity_Nâ‚‚_objectwise (P : karoubi (simplicial_object C)) :
  Nâ‚‚Î“â‚‚.inv.app (Nâ‚‚.obj P) â‰« Nâ‚‚.map (Î“â‚‚Nâ‚‚.nat_trans.app P) = ğŸ™ (Nâ‚‚.obj P) :=
begin
  ext n,
  have eqâ‚ : (Nâ‚‚Î“â‚‚.inv.app (Nâ‚‚.obj P)).f.f n = P_infty.f n â‰« P.p.app (op [n]) â‰«
    (Î“â‚€.splitting (Nâ‚‚.obj P).X).Î¹_summand (splitting.index_set.id (op [n])),
  { simp only [Nâ‚‚Î“â‚‚_inv_app_f_f, Nâ‚‚_obj_p_f, assoc], },
  have eqâ‚‚ : (Î“â‚€.splitting (Nâ‚‚.obj P).X).Î¹_summand (splitting.index_set.id (op [n])) â‰«
    (Nâ‚‚.map (Î“â‚‚Nâ‚‚.nat_trans.app P)).f.f n = P_infty.f n â‰« P.p.app (op [n]),
  { dsimp [Nâ‚‚],
    simp only [Î“â‚‚Nâ‚‚.nat_trans_app_f_app, P_infty_on_Î“â‚€_splitting_summand_eq_self_assoc,
      functor.comp_map, compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚_hom, nat_trans.comp_app,
      eq_to_hom_app, assoc, karoubi.comp_f, karoubi.eq_to_hom_f, eq_to_hom_refl, comp_id,
      karoubi.decomp_id_p_f, karoubi.comp_p_assoc, Î“â‚‚_map_f_app,
      Nâ‚‚_map_f_f, karoubi.decomp_id_i_f, Î“â‚‚Nâ‚.nat_trans_app_f_app],
    erw [splitting.Î¹_desc_assoc, assoc, assoc, splitting.Î¹_desc_assoc],
    dsimp [splitting.index_set.id, splitting.index_set.e],
    simp only [assoc, nat_trans.naturality, P_infty_f_naturality_assoc,
      app_idem_assoc, P_infty_f_idem_assoc],
    erw [P.X.map_id, comp_id], },
  simp only [karoubi.comp_f, homological_complex.comp_f, karoubi.id_eq, Nâ‚‚_obj_p_f, assoc,
    eqâ‚, eqâ‚‚, P_infty_f_naturality_assoc, app_idem, P_infty_f_idem_assoc],
end

lemma identity_Nâ‚‚ :
  ((ğŸ™ (Nâ‚‚ : karoubi (simplicial_object C) â¥¤ _ ) â—« Nâ‚‚Î“â‚‚.inv) â‰«
    (Î“â‚‚Nâ‚‚.nat_trans â—« ğŸ™ Nâ‚‚) : Nâ‚‚ âŸ¶ Nâ‚‚) = ğŸ™ Nâ‚‚ :=
by { ext P : 2, dsimp, rw [Î“â‚‚.map_id, Nâ‚‚.map_id, comp_id, id_comp, identity_Nâ‚‚_objectwise P], }

instance : is_iso (Î“â‚‚Nâ‚‚.nat_trans : (Nâ‚‚ : karoubi (simplicial_object C) â¥¤ _ ) â‹™ _ âŸ¶ _) :=
begin
  haveI : âˆ€ (P : karoubi (simplicial_object C)), is_iso (Î“â‚‚Nâ‚‚.nat_trans.app P),
  { intro P,
    haveI : is_iso (Nâ‚‚.map (Î“â‚‚Nâ‚‚.nat_trans.app P)),
    { have h := identity_Nâ‚‚_objectwise P,
      erw hom_comp_eq_id at h,
      rw h,
      apply_instance, },
    exact is_iso_of_reflects_iso _ Nâ‚‚, },
  apply nat_iso.is_iso_of_is_iso_app,
end

instance : is_iso (Î“â‚‚Nâ‚.nat_trans : (Nâ‚ : simplicial_object C â¥¤ _ ) â‹™ _ âŸ¶ _) :=
begin
  haveI : âˆ€ (X : simplicial_object C), is_iso (Î“â‚‚Nâ‚.nat_trans.app X),
  { intro X,
    rw compatibility_Î“â‚‚Nâ‚_Î“â‚‚Nâ‚‚_nat_trans,
    apply_instance, },
  apply nat_iso.is_iso_of_is_iso_app,
end

/-- The unit isomorphism of the Dold-Kan equivalence. -/
@[simp]
def Î“â‚‚Nâ‚‚ : ğŸ­ _ â‰… (Nâ‚‚ : karoubi (simplicial_object C) â¥¤ _) â‹™ Î“â‚‚ :=
(as_iso Î“â‚‚Nâ‚‚.nat_trans).symm

/-- The natural isomorphism `to_karoubi (simplicial_object C) â‰… Nâ‚ â‹™ Î“â‚‚`. -/
@[simps]
def Î“â‚‚Nâ‚ : to_karoubi _  â‰… (Nâ‚ : simplicial_object C â¥¤ _) â‹™ Î“â‚‚ :=
(as_iso Î“â‚‚Nâ‚.nat_trans).symm

end dold_kan

end algebraic_topology
