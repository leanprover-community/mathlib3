/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import algebraic_topology.split_simplicial_object
import category_theory.preadditive.basic
import algebraic_topology.dold_kan.degeneracies

/-!

# Split simplicial objects in preadditive categories

TODO @joelriou: Define a functor `N' : simplicial_object.split C â¥¤ chain_complex C â„•`
when `C` is a preadditive category, and get an isomorphism
`N' â‹™ to_karoubi (chain_complex C â„•) â‰… forget C â‹™ dold_kan.Nâ‚`

-/

noncomputable theory

open category_theory category_theory.limits category_theory.category
  category_theory.preadditive opposite algebraic_topology.dold_kan

open_locale big_operators simplicial

namespace simplicial_object

namespace splitting

variables {C : Type*} [category C] [has_finite_coproducts C]
  {X : simplicial_object C} (s : splitting X)

/-- The projection on a summand of the coproduct decomposition given
by a splitting of a simplicial object. -/
def Ï€_summand [has_zero_morphisms C] {Î” : simplex_categoryáµ’áµ–} (A : index_set Î”) :
  X.obj Î” âŸ¶ s.N A.1.unop.len :=
begin
  refine (s.iso Î”).inv â‰« sigma.desc (Î» B, _),
  by_cases B = A,
  { exact eq_to_hom (by { subst h, refl, }), },
  { exact 0, },
end

@[simp, reassoc]
lemma Î¹_Ï€_summand_eq_id [has_zero_morphisms C] {Î” : simplex_categoryáµ’áµ–} (A : index_set Î”) :
  s.Î¹_summand A â‰« s.Ï€_summand A = ğŸ™ _ :=
begin
  dsimp [Î¹_summand, Ï€_summand],
  simp only [summand, assoc, is_iso.hom_inv_id_assoc],
  erw [colimit.Î¹_desc, cofan.mk_Î¹_app],
  dsimp,
  simp only [eq_self_iff_true, if_true],
end

@[simp, reassoc]
lemma Î¹_Ï€_summand_eq_zero [has_zero_morphisms C] {Î” : simplex_categoryáµ’áµ–} (A B : index_set Î”)
  (h : B â‰  A) : s.Î¹_summand A â‰« s.Ï€_summand B = 0 :=
begin
  dsimp [Î¹_summand, Ï€_summand],
  simp only [summand, assoc, is_iso.hom_inv_id_assoc],
  erw [colimit.Î¹_desc, cofan.mk_Î¹_app],
  apply dif_neg,
  exact h.symm,
end

variable [preadditive C]

lemma decomposition_id (Î” : simplex_categoryáµ’áµ–) :
  ğŸ™ (X.obj Î”) = âˆ‘ (A : index_set Î”), s.Ï€_summand A â‰« s.Î¹_summand A :=
begin
  apply s.hom_ext',
  intro A,
  rw [comp_id, comp_sum, finset.sum_eq_single A, Î¹_Ï€_summand_eq_id_assoc],
  { intros B hâ‚ hâ‚‚,
    rw [s.Î¹_Ï€_summand_eq_zero_assoc _ _ hâ‚‚, zero_comp], },
  { simp only [finset.mem_univ, not_true, is_empty.forall_iff], },
end

@[simp, reassoc]
lemma Ïƒ_comp_Ï€_summand_id_eq_zero {n : â„•} (i : fin (n+1)) :
  X.Ïƒ i â‰« s.Ï€_summand (index_set.id (op [n+1])) = 0 :=
begin
  apply s.hom_ext',
  intro A,
  dsimp only [simplicial_object.Ïƒ],
  rw [comp_zero, s.Î¹_summand_epi_naturality_assoc A (simplex_category.Ïƒ i).op,
    Î¹_Ï€_summand_eq_zero],
  symmetry,
  change Â¬ (A.epi_comp (simplex_category.Ïƒ i).op).eq_id,
  rw index_set.eq_id_iff_len_eq,
  have h := simplex_category.len_le_of_epi (infer_instance : epi A.e),
  dsimp at âŠ¢ h,
  linarith,
end

/-- If a simplicial object `X` in an additive category is split,
then `P_infty` vanishes on all the summands of `X _[n]` which do
not correspond to the identity of `[n]`. -/
lemma Î¹_summand_comp_P_infty_eq_zero {X : simplicial_object C}
  (s : simplicial_object.splitting X)
  {n : â„•} (A : simplicial_object.splitting.index_set (op [n]))
  (hA : Â¬ A.eq_id) :
  s.Î¹_summand A â‰« P_infty.f n = 0 :=
begin
  rw simplicial_object.splitting.index_set.eq_id_iff_mono at hA,
  rw [simplicial_object.splitting.Î¹_summand_eq, assoc,
    degeneracy_comp_P_infty X n A.e hA, comp_zero],
end

end splitting

end simplicial_object
