/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import algebraic_topology.alternating_face_map_complex
import algebraic_topology.simplicial_set
import algebraic_topology.cech_nerve
import algebra.homology.homotopy
import tactic.fin_cases

/-!

# Augmented simplicial objects with an extra degeneracy

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In simplicial homotopy theory, in order to prove that the connected components
of a simplicial set `X` are contractible, it suffices to construct an extra
degeneracy as it is defined in *Simplicial Homotopy Theory* by Goerss-Jardine p. 190.
It consists of a series of maps `œÄ‚ÇÄ X ‚Üí X _[0]` and `X _[n] ‚Üí X _[n+1]` which
behave formally like an extra degeneracy `œÉ (-1)`. It can be thought as a datum
associated to the augmented simplicial set `X ‚Üí œÄ‚ÇÄ X`.

In this file, we adapt this definition to the case of augmented
simplicial objects in any category.

## Main definitions

- the structure `extra_degeneracy X` for any `X : simplicial_object.augmented C`
- `extra_degeneracy.map`: extra degeneracies are preserved by the application of any
functor `C ‚•§ D`
- `sSet.augmented.standard_simplex.extra_degeneracy`: the standard `n`-simplex has
an extra degeneracy
- `arrow.augmented_cech_nerve.extra_degeneracy`: the ƒåech nerve of a split
epimorphism has an extra degeneracy
- `extra_degeneracy.homotopy_equiv`: in the case the category `C` is preadditive,
if we have an extra degeneracy on `X : simplicial_object.augmented C`, then
the augmentation on the alternating face map complex of `X` is a homotopy
equivalence.

## References
* [Paul G. Goerss, John F. Jardine, *Simplical Homotopy Theory*][goerss-jardine-2009]

-/

open category_theory category_theory.category
open category_theory.simplicial_object.augmented
open opposite
open_locale simplicial

namespace simplicial_object

namespace augmented

variables {C : Type*} [category C]

/-- The datum of an extra degeneracy is a technical condition on
augmented simplicial objects. The morphisms `s'` and `s n` of the
structure formally behave like extra degeneracies `œÉ (-1)`. -/
@[ext]
structure extra_degeneracy (X : simplicial_object.augmented C) :=
(s' : point.obj X ‚ü∂ (drop.obj X) _[0])
(s : Œ† (n : ‚Ñï), (drop.obj X) _[n] ‚ü∂ (drop.obj X) _[n+1])
(s'_comp_Œµ' : s' ‚â´ X.hom.app (op [0]) = ùüô _)
(s‚ÇÄ_comp_Œ¥‚ÇÅ' : s 0 ‚â´ (drop.obj X).Œ¥ 1 = X.hom.app (op [0]) ‚â´ s')
(s_comp_Œ¥‚ÇÄ' : Œ† (n : ‚Ñï), s n ‚â´ (drop.obj X).Œ¥ 0 = ùüô _)
(s_comp_Œ¥' : Œ† (n : ‚Ñï) (i : fin (n+2)), s (n+1) ‚â´ (drop.obj X).Œ¥ i.succ =
  (drop.obj X).Œ¥ i ‚â´ s n)
(s_comp_œÉ' : Œ† (n : ‚Ñï) (i : fin (n+1)), s n ‚â´ (drop.obj X).œÉ i.succ =
  (drop.obj X).œÉ i ‚â´ s (n+1))

namespace extra_degeneracy

restate_axiom s'_comp_Œµ'
restate_axiom s‚ÇÄ_comp_Œ¥‚ÇÅ'
restate_axiom s_comp_Œ¥‚ÇÄ'
restate_axiom s_comp_Œ¥'
restate_axiom s_comp_œÉ'
attribute [reassoc] s'_comp_Œµ s‚ÇÄ_comp_Œ¥‚ÇÅ s_comp_Œ¥‚ÇÄ s_comp_Œ¥ s_comp_œÉ
attribute [simp] s'_comp_Œµ s_comp_Œ¥‚ÇÄ

/-- If `ed` is an extra degeneracy for `X : simplicial_object.augmented C` and
`F : C ‚•§ D` is a functor, then `ed.map F` is an extra degeneracy for the
augmented simplical object in `D` obtained by applying `F` to `X`. -/
def map {D : Type*} [category D]
  {X : simplicial_object.augmented C} (ed : extra_degeneracy X) (F : C ‚•§ D) :
  extra_degeneracy (((whiskering _ _).obj F).obj X) :=
{ s' := F.map ed.s',
  s := Œª n, F.map (ed.s n),
  s'_comp_Œµ' := by { dsimp, erw [comp_id, ‚Üê F.map_comp, ed.s'_comp_Œµ, F.map_id], },
  s‚ÇÄ_comp_Œ¥‚ÇÅ' := by { dsimp, erw [comp_id, ‚Üê F.map_comp, ‚Üê F.map_comp, ed.s‚ÇÄ_comp_Œ¥‚ÇÅ], },
  s_comp_Œ¥‚ÇÄ' := Œª n, by { dsimp, erw [‚Üê F.map_comp, ed.s_comp_Œ¥‚ÇÄ, F.map_id], },
  s_comp_Œ¥' := Œª n i, by { dsimp, erw [‚Üê F.map_comp, ‚Üê F.map_comp, ed.s_comp_Œ¥], refl, },
  s_comp_œÉ' := Œª n i, by { dsimp, erw [‚Üê F.map_comp, ‚Üê F.map_comp, ed.s_comp_œÉ], refl, }, }

/-- If `X` and `Y` are isomorphic augmented simplicial objects, then an extra
degeneracy for `X` gives also an extra degeneracy for `Y` -/
def of_iso {X Y : simplicial_object.augmented C} (e : X ‚âÖ Y) (ed : extra_degeneracy X) :
  extra_degeneracy Y :=
{ s' := (point.map_iso e).inv ‚â´ ed.s' ‚â´ (drop.map_iso e).hom.app (op [0]),
  s := Œª n, (drop.map_iso e).inv.app (op [n]) ‚â´ ed.s n ‚â´ (drop.map_iso e).hom.app (op [n+1]),
  s'_comp_Œµ' := by simpa only [functor.map_iso, assoc, w‚ÇÄ, ed.s'_comp_Œµ_assoc]
    using (point.map_iso e).inv_hom_id,
  s‚ÇÄ_comp_Œ¥‚ÇÅ' := begin
    have h := w‚ÇÄ e.inv,
    dsimp at h ‚ä¢,
    simp only [assoc, ‚Üê simplicial_object.Œ¥_naturality, ed.s‚ÇÄ_comp_Œ¥‚ÇÅ_assoc, reassoc_of h],
  end,
  s_comp_Œ¥‚ÇÄ' := Œª n, begin
    have h := ed.s_comp_Œ¥‚ÇÄ',
    dsimp at ‚ä¢ h,
    simpa only [assoc, ‚Üê simplicial_object.Œ¥_naturality, reassoc_of h]
      using congr_app (drop.map_iso e).inv_hom_id (op [n]),
  end,
  s_comp_Œ¥' := Œª n i, begin
    have h := ed.s_comp_Œ¥' n i,
    dsimp at ‚ä¢ h,
    simp only [assoc, ‚Üê simplicial_object.Œ¥_naturality, reassoc_of h,
      ‚Üê simplicial_object.Œ¥_naturality_assoc],
  end,
  s_comp_œÉ' := Œª n i, begin
    have h := ed.s_comp_œÉ' n i,
    dsimp at ‚ä¢ h,
    simp only [assoc, ‚Üê simplicial_object.œÉ_naturality, reassoc_of h,
      ‚Üê simplicial_object.œÉ_naturality_assoc],
  end,}

end extra_degeneracy

end augmented

end simplicial_object

namespace sSet

namespace augmented

namespace standard_simplex

/-- When `[has_zero X]`, the shift of a map `f : fin n ‚Üí X`
is a map `fin (n+1) ‚Üí X` which sends `0` to `0` and `i.succ` to `f i`. -/
def shift_fun {n : ‚Ñï} {X : Type*} [has_zero X] (f : fin n ‚Üí X) (i : fin (n+1)) : X :=
dite (i = 0) (Œª h, 0) (Œª h, f (i.pred h))

@[simp]
lemma shift_fun_0 {n : ‚Ñï} {X : Type*} [has_zero X] (f : fin n ‚Üí X) : shift_fun f 0 = 0 := rfl

@[simp]
lemma shift_fun_succ {n : ‚Ñï} {X : Type*} [has_zero X] (f : fin n ‚Üí X)
  (i : fin n) : shift_fun f i.succ = f i :=
begin
  dsimp [shift_fun],
  split_ifs,
  { exfalso,
    simpa only [fin.ext_iff, fin.coe_succ] using h, },
  { simp only [fin.pred_succ], },
end

/-- The shift of a morphism `f : [n] ‚Üí Œî` in `simplex_category` corresponds to
the monotone map which sends `0` to `0` and `i.succ` to `f.to_order_hom i`. -/
@[simp]
def shift {n : ‚Ñï} {Œî : simplex_category} (f : [n] ‚ü∂ Œî) : [n+1] ‚ü∂ Œî := simplex_category.hom.mk
{ to_fun := shift_fun f.to_order_hom,
  monotone' := Œª i‚ÇÅ i‚ÇÇ hi, begin
    by_cases h‚ÇÅ : i‚ÇÅ = 0,
    { subst h‚ÇÅ,
      simp only [shift_fun_0, fin.zero_le], },
    { have h‚ÇÇ : i‚ÇÇ ‚â† 0 := by { intro h‚ÇÇ, subst h‚ÇÇ, exact h‚ÇÅ (le_antisymm hi (fin.zero_le _)), },
      cases fin.eq_succ_of_ne_zero h‚ÇÅ with j‚ÇÅ hj‚ÇÅ,
      cases fin.eq_succ_of_ne_zero h‚ÇÇ with j‚ÇÇ hj‚ÇÇ,
      substs hj‚ÇÅ hj‚ÇÇ,
      simpa only [shift_fun_succ] using f.to_order_hom.monotone (fin.succ_le_succ_iff.mp hi), },
  end, }

/-- The obvious extra degeneracy on the standard simplex. -/
@[protected]
def extra_degeneracy (Œî : simplex_category) :
  simplicial_object.augmented.extra_degeneracy (standard_simplex.obj Œî) :=
{ s' := Œª x, simplex_category.hom.mk (order_hom.const _ 0),
  s := Œª n f, shift f,
  s'_comp_Œµ' := by { ext1 j, fin_cases j, },
  s‚ÇÄ_comp_Œ¥‚ÇÅ' := by { ext x j, fin_cases j, refl, },
  s_comp_Œ¥‚ÇÄ' := Œª n, begin
    ext œÜ i : 4,
    dsimp [simplicial_object.Œ¥, simplex_category.Œ¥, sSet.standard_simplex],
    simp only [shift_fun_succ],
  end,
  s_comp_Œ¥' := Œª n i, begin
    ext œÜ j : 4,
    dsimp [simplicial_object.Œ¥, simplex_category.Œ¥, sSet.standard_simplex],
    by_cases j = 0,
    { subst h,
      simp only [fin.succ_succ_above_zero, shift_fun_0], },
    { cases fin.eq_succ_of_ne_zero h with k hk,
      subst hk,
      simp only [fin.succ_succ_above_succ, shift_fun_succ], },
  end,
  s_comp_œÉ' := Œª n i, begin
    ext œÜ j : 4,
    dsimp [simplicial_object.œÉ, simplex_category.œÉ, sSet.standard_simplex],
    by_cases j = 0,
    { subst h,
      simpa only [shift_fun_0] using shift_fun_0 œÜ.to_order_hom, },
    { cases fin.eq_succ_of_ne_zero h with k hk,
      subst hk,
      simp only [fin.succ_pred_above_succ, shift_fun_succ], },
  end, }

instance nonempty_extra_degeneracy_standard_simplex (Œî : simplex_category) :
  nonempty (simplicial_object.augmented.extra_degeneracy (standard_simplex.obj Œî)) :=
‚ü®standard_simplex.extra_degeneracy Œî‚ü©

end standard_simplex

end augmented

end sSet

namespace category_theory

open limits

namespace arrow

namespace augmented_cech_nerve

variables {C : Type*} [category C] (f : arrow C)
  [‚àÄ n : ‚Ñï, has_wide_pullback f.right (Œª i : fin (n+1), f.left) (Œª i, f.hom)]
  (S : split_epi f.hom)

include S

/-- The extra degeneracy map on the ƒåech nerve of a split epi. It is
given on the `0`-projection by the given section of the split epi,
and by shifting the indices on the other projections. -/
noncomputable def extra_degeneracy.s (n : ‚Ñï) :
  f.cech_nerve.obj (op [n]) ‚ü∂ f.cech_nerve.obj (op [n + 1]) :=
wide_pullback.lift (wide_pullback.base _)
  (Œª i, dite (i = 0) (Œª h, wide_pullback.base _ ‚â´ S.section_)
    (Œª h, wide_pullback.œÄ _ (i.pred h)))
  (Œª i, begin
    split_ifs,
    { subst h,
      simp only [assoc, split_epi.id, comp_id], },
    { simp only [wide_pullback.œÄ_arrow], },
  end)

@[simp]
lemma extra_degeneracy.s_comp_œÄ_0 (n : ‚Ñï) :
  extra_degeneracy.s f S n ‚â´ wide_pullback.œÄ _ 0 = wide_pullback.base _ ‚â´ S.section_ :=
by { dsimp [extra_degeneracy.s], simpa only [wide_pullback.lift_œÄ], }

@[simp]
lemma extra_degeneracy.s_comp_œÄ_succ (n : ‚Ñï) (i : fin (n+1)) :
  extra_degeneracy.s f S n ‚â´ wide_pullback.œÄ _ i.succ = wide_pullback.œÄ _ i :=
begin
  dsimp [extra_degeneracy.s],
  simp only [wide_pullback.lift_œÄ],
  split_ifs,
  { exfalso,
    simpa only [fin.ext_iff, fin.coe_succ, fin.coe_zero, nat.succ_ne_zero] using h, },
  { congr,
    apply fin.pred_succ, },
end

@[simp]
lemma extra_degeneracy.s_comp_base (n : ‚Ñï) :
  extra_degeneracy.s f S n ‚â´ wide_pullback.base _ = wide_pullback.base _ :=
by apply wide_pullback.lift_base

/-- The augmented ƒåech nerve associated to a split epimorphism has an extra degeneracy. -/
noncomputable def extra_degeneracy :
  simplicial_object.augmented.extra_degeneracy f.augmented_cech_nerve :=
{ s' := S.section_ ‚â´ wide_pullback.lift f.hom (Œª i, ùüô _) (Œª i, by rw id_comp),
  s := Œª n, extra_degeneracy.s f S n,
  s'_comp_Œµ' :=
    by simp only [augmented_cech_nerve_hom_app, assoc, wide_pullback.lift_base, split_epi.id],
  s‚ÇÄ_comp_Œ¥‚ÇÅ' := begin
    dsimp [cech_nerve, simplicial_object.Œ¥, simplex_category.Œ¥],
    ext j,
    { fin_cases j,
      simpa only [assoc, wide_pullback.lift_œÄ, comp_id] using extra_degeneracy.s_comp_œÄ_0 f S 0, },
    { simpa only [assoc, wide_pullback.lift_base, split_epi.id, comp_id]
        using extra_degeneracy.s_comp_base f S 0, },
  end,
  s_comp_Œ¥‚ÇÄ' := Œª n, begin
    dsimp [cech_nerve, simplicial_object.Œ¥, simplex_category.Œ¥],
    ext j,
    { simpa only [assoc, wide_pullback.lift_œÄ, id_comp]
        using extra_degeneracy.s_comp_œÄ_succ f S n j, },
    { simpa only [assoc, wide_pullback.lift_base, id_comp]
        using extra_degeneracy.s_comp_base f S n, },
  end,
  s_comp_Œ¥' := Œª n i, begin
    dsimp [cech_nerve, simplicial_object.Œ¥, simplex_category.Œ¥],
    ext j,
    { simp only [assoc, wide_pullback.lift_œÄ],
      by_cases j = 0,
      { subst h,
        erw [fin.succ_succ_above_zero, extra_degeneracy.s_comp_œÄ_0, extra_degeneracy.s_comp_œÄ_0],
        dsimp,
        simp only [wide_pullback.lift_base_assoc], },
      { cases fin.eq_succ_of_ne_zero h with k hk,
        subst hk,
        erw [fin.succ_succ_above_succ, extra_degeneracy.s_comp_œÄ_succ,
          extra_degeneracy.s_comp_œÄ_succ],
        dsimp,
        simp only [wide_pullback.lift_œÄ], }, },
    { simp only [assoc, wide_pullback.lift_base],
      erw [extra_degeneracy.s_comp_base, extra_degeneracy.s_comp_base],
      dsimp,
      simp only [wide_pullback.lift_base], },
  end,
  s_comp_œÉ' := Œª n i, begin
    dsimp [cech_nerve, simplicial_object.œÉ, simplex_category.œÉ],
    ext j,
    { simp only [assoc, wide_pullback.lift_œÄ],
      by_cases j = 0,
      { subst h,
        erw [extra_degeneracy.s_comp_œÄ_0, extra_degeneracy.s_comp_œÄ_0],
        dsimp,
        simp only [wide_pullback.lift_base_assoc], },
      { cases fin.eq_succ_of_ne_zero h with k hk,
        subst hk,
        erw [fin.succ_pred_above_succ, extra_degeneracy.s_comp_œÄ_succ,
          extra_degeneracy.s_comp_œÄ_succ],
        dsimp,
        simp only [wide_pullback.lift_œÄ], }, },
    { simp only [assoc, wide_pullback.lift_base],
      erw [extra_degeneracy.s_comp_base, extra_degeneracy.s_comp_base],
      dsimp,
      simp only [wide_pullback.lift_base], },
  end, }

end augmented_cech_nerve

end arrow

end category_theory

namespace simplicial_object

namespace augmented

namespace extra_degeneracy

open algebraic_topology category_theory category_theory.limits

/-- If `C` is a preadditive category and `X` is an augmented simplicial object
in `C` that has an extra degeneracy, then the augmentation on the alternating
face map complex of `X` is an homotopy equivalence. -/
noncomputable
def homotopy_equiv {C : Type*} [category C]
  [preadditive C] [has_zero_object C] {X : simplicial_object.augmented C}
  (ed : extra_degeneracy X) :
  homotopy_equiv (algebraic_topology.alternating_face_map_complex.obj (drop.obj X))
    ((chain_complex.single‚ÇÄ C).obj (point.obj X)) :=
{ hom := alternating_face_map_complex.Œµ.app X,
  inv := (chain_complex.from_single‚ÇÄ_equiv _ _).inv_fun ed.s',
  homotopy_inv_hom_id := homotopy.of_eq (by { ext, exact ed.s'_comp_Œµ, }),
  homotopy_hom_inv_id :=
  { hom := Œª i j, begin
      by_cases i+1 = j,
      { exact (-ed.s i) ‚â´ eq_to_hom (by congr'), },
      { exact 0, },
    end,
    zero' := Œª i j hij, begin
      split_ifs,
      { exfalso, exact hij h, },
      { simp only [eq_self_iff_true], },
    end,
    comm := Œª i, begin
      cases i,
      { rw [homotopy.prev_d_chain_complex, homotopy.d_next_zero_chain_complex, zero_add],
        dsimp [chain_complex.from_single‚ÇÄ_equiv, chain_complex.to_single‚ÇÄ_equiv],
        simp only [zero_add, eq_self_iff_true, preadditive.neg_comp, comp_id, if_true,
          alternating_face_map_complex.obj_d_eq, fin.sum_univ_two, fin.coe_zero, pow_zero,
          one_zsmul, fin.coe_one, pow_one, neg_smul, preadditive.comp_add, ‚Üê s‚ÇÄ_comp_Œ¥‚ÇÅ,
          s_comp_Œ¥‚ÇÄ, preadditive.comp_neg, neg_add_rev, neg_neg, neg_add_cancel_right,
          neg_add_cancel_comm], },
      { rw [homotopy.prev_d_chain_complex, homotopy.d_next_succ_chain_complex],
        dsimp [chain_complex.to_single‚ÇÄ_equiv, chain_complex.from_single‚ÇÄ_equiv],
        simp only [zero_comp, alternating_face_map_complex.obj_d_eq, eq_self_iff_true,
          preadditive.neg_comp, comp_id, if_true, preadditive.comp_neg,
          @fin.sum_univ_succ _ _ (i+2), preadditive.comp_add, fin.coe_zero, pow_zero, one_zsmul,
          s_comp_Œ¥‚ÇÄ, fin.coe_succ, pow_add, pow_one, mul_neg, neg_zsmul,
          preadditive.comp_sum, preadditive.sum_comp, neg_neg, mul_one,
          preadditive.comp_zsmul, preadditive.zsmul_comp, s_comp_Œ¥, zsmul_neg],
        rw [add_comm (-ùüô _), add_assoc, add_assoc, add_left_neg, add_zero,
          finset.sum_neg_distrib, add_left_neg], },
    end, }, }

end extra_degeneracy

end augmented

end simplicial_object
