/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import algebraic_topology.simplicial_set
import algebraic_topology.cech_nerve
import tactic.fin_cases

/-!

# Augmented simplicial objects with an extra degeneracy

In simplicial homotopy theory, in order to prove that the connected components
of a simplicial set `X` are contractible, it suffices to construct an extra
degeneracy as it is defined in *Simplicial Homotopy Theory* by Goerss-Jardine p. 190.
It consists of a series of maps `Ï€â‚€ X â†’ X _[0]` and `X _[n] â†’ X _[n+1]` which
behave formally like an extra degeneracy `Ïƒ (-1)`. It can be thought as a datum
associated to the augmented simplicial set `X â†’ Ï€â‚€ X`.

In this file, we adapt this definition to the case of augmented
simplicial objects in any category.

## Main definitions

- the structure `extra_degeneracy X` for any `X : simplicial_object.augmented C`
- `extra_degeneracy.map`: extra degeneracies are preserved by the application of any
functor `C â¥¤ D`
- `sSet.augmented.standard_simplex.extra_degeneracy`: the standard `n`-simplex has
an extra degeneracy

TODO @joelriou:
1) when the category `C` is preadditive and has a zero object, and
`X : simplicial_object.augmented C` has an extra degeneracy, then the augmentation
on the alternating face map complex of `X` is a homotopy equivalence of chain
complexes.

## References
* [Paul G. Goerss, John F. Jardine, *Simplical Homotopy Theory*][goerss-jardine-2009]

-/

open category_theory category_theory.category
open category_theory.simplicial_object.augmented
open opposite
open_locale simplicial

namespace simplicial_object

namespace augmented

variables {C : Type*} [category C]

/-- The datum of an extra degeneracy is a technical condition on
augmented simplicial objects. The morphisms `s'` and `s n` of the
structure formally behave like extra degeneracies `Ïƒ (-1)`. -/
@[ext]
structure extra_degeneracy (X : simplicial_object.augmented C) :=
(s' : point.obj X âŸ¶ (drop.obj X) _[0])
(s : Î  (n : â„•), (drop.obj X) _[n] âŸ¶ (drop.obj X) _[n+1])
(s'_comp_Îµ' : s' â‰« X.hom.app (op [0]) = ğŸ™ _)
(sâ‚€_comp_Î´â‚' : s 0 â‰« (drop.obj X).Î´ 1 = X.hom.app (op [0]) â‰« s')
(s_comp_Î´â‚€' : Î  (n : â„•), s n â‰« (drop.obj X).Î´ 0 = ğŸ™ _)
(s_comp_Î´' : Î  (n : â„•) (i : fin (n+2)), s (n+1) â‰« (drop.obj X).Î´ i.succ =
  (drop.obj X).Î´ i â‰« s n)
(s_comp_Ïƒ' : Î  (n : â„•) (i : fin (n+1)), s n â‰« (drop.obj X).Ïƒ i.succ =
  (drop.obj X).Ïƒ i â‰« s (n+1))

namespace extra_degeneracy

restate_axiom s'_comp_Îµ'
restate_axiom sâ‚€_comp_Î´â‚'
restate_axiom s_comp_Î´â‚€'
restate_axiom s_comp_Î´'
restate_axiom s_comp_Ïƒ'
attribute [reassoc] s'_comp_Îµ sâ‚€_comp_Î´â‚ s_comp_Î´â‚€ s_comp_Î´ s_comp_Ïƒ
attribute [simp] s'_comp_Îµ s_comp_Î´â‚€

/-- If `ed` is an extra degeneracy for `X : simplicial_object.augmented C` and
`F : C â¥¤ D` is a functor, then `ed.map F` is an extra degeneracy for the
augmented simplical object in `D` obtained by applying `F` to `X`. -/
def map {D : Type*} [category D]
  {X : simplicial_object.augmented C} (ed : extra_degeneracy X) (F : C â¥¤ D) :
  extra_degeneracy (((whiskering _ _).obj F).obj X) :=
{ s' := F.map ed.s',
  s := Î» n, F.map (ed.s n),
  s'_comp_Îµ' := by { dsimp, erw [comp_id, â† F.map_comp, ed.s'_comp_Îµ, F.map_id], },
  sâ‚€_comp_Î´â‚' := by { dsimp, erw [comp_id, â† F.map_comp, â† F.map_comp, ed.sâ‚€_comp_Î´â‚], },
  s_comp_Î´â‚€' := Î» n, by { dsimp, erw [â† F.map_comp, ed.s_comp_Î´â‚€, F.map_id], },
  s_comp_Î´' := Î» n i, by { dsimp, erw [â† F.map_comp, â† F.map_comp, ed.s_comp_Î´], refl, },
  s_comp_Ïƒ' := Î» n i, by { dsimp, erw [â† F.map_comp, â† F.map_comp, ed.s_comp_Ïƒ], refl, }, }

/-- If `X` and `Y` are isomorphic augmented simplicial objects, then an extra
degeneracy for `X` gives also an extra degeneracy for `Y` -/
def of_iso {X Y : simplicial_object.augmented C} (e : X â‰… Y) (ed : extra_degeneracy X) :
  extra_degeneracy Y :=
{ s' := (point.map_iso e).inv â‰« ed.s' â‰« (drop.map_iso e).hom.app (op [0]),
  s := Î» n, (drop.map_iso e).inv.app (op [n]) â‰« ed.s n â‰« (drop.map_iso e).hom.app (op [n+1]),
  s'_comp_Îµ' := by simpa only [functor.map_iso, assoc, wâ‚€, ed.s'_comp_Îµ_assoc]
    using (point.map_iso e).inv_hom_id,
  sâ‚€_comp_Î´â‚' := begin
    have h := wâ‚€ e.inv,
    dsimp at h âŠ¢,
    simp only [assoc, â† simplicial_object.Î´_naturality, ed.sâ‚€_comp_Î´â‚_assoc, reassoc_of h],
  end,
  s_comp_Î´â‚€' := Î» n, begin
    have h := ed.s_comp_Î´â‚€',
    dsimp at âŠ¢ h,
    simpa only [assoc, â† simplicial_object.Î´_naturality, reassoc_of h]
      using congr_app (drop.map_iso e).inv_hom_id (op [n]),
  end,
  s_comp_Î´' := Î» n i, begin
    have h := ed.s_comp_Î´' n i,
    dsimp at âŠ¢ h,
    simp only [assoc, â† simplicial_object.Î´_naturality, reassoc_of h,
      â† simplicial_object.Î´_naturality_assoc],
  end,
  s_comp_Ïƒ' := Î» n i, begin
    have h := ed.s_comp_Ïƒ' n i,
    dsimp at âŠ¢ h,
    simp only [assoc, â† simplicial_object.Ïƒ_naturality, reassoc_of h,
      â† simplicial_object.Ïƒ_naturality_assoc],
  end,}

end extra_degeneracy

end augmented

end simplicial_object

namespace sSet

namespace augmented

namespace standard_simplex

/-- When `[has_zero X]`, the shift of a map `f : fin n â†’ X`
is a map `fin (n+1) â†’ X` which sends `0` to `0` and `i.succ` to `f i`. -/
def shift_fun {n : â„•} {X : Type*} [has_zero X] (f : fin n â†’ X) (i : fin (n+1)) : X :=
dite (i = 0) (Î» h, 0) (Î» h, f (i.pred h))

@[simp]
lemma shift_fun_0 {n : â„•} {X : Type*} [has_zero X] (f : fin n â†’ X) : shift_fun f 0 = 0 := rfl

@[simp]
lemma shift_fun_succ {n : â„•} {X : Type*} [has_zero X] (f : fin n â†’ X)
  (i : fin n) : shift_fun f i.succ = f i :=
begin
  dsimp [shift_fun],
  split_ifs,
  { exfalso,
    simpa only [fin.ext_iff, fin.coe_succ] using h, },
  { simp only [fin.pred_succ], },
end

/-- The shift of a morphism `f : [n] â†’ Î”` in `simplex_category` corresponds to
the monotone map which sends `0` to `0` and `i.succ` to `f.to_order_hom i`. -/
@[simp]
def shift {n : â„•} {Î” : simplex_category} (f : [n] âŸ¶ Î”) : [n+1] âŸ¶ Î” := simplex_category.hom.mk
{ to_fun := shift_fun f.to_order_hom,
  monotone' := Î» iâ‚ iâ‚‚ hi, begin
    by_cases hâ‚ : iâ‚ = 0,
    { subst hâ‚,
      simp only [shift_fun_0, fin.zero_le], },
    { have hâ‚‚ : iâ‚‚ â‰  0 := by { intro hâ‚‚, subst hâ‚‚, exact hâ‚ (le_antisymm hi (fin.zero_le _)), },
      cases fin.eq_succ_of_ne_zero hâ‚ with jâ‚ hjâ‚,
      cases fin.eq_succ_of_ne_zero hâ‚‚ with jâ‚‚ hjâ‚‚,
      substs hjâ‚ hjâ‚‚,
      simpa only [shift_fun_succ] using f.to_order_hom.monotone (fin.succ_le_succ_iff.mp hi), },
  end, }

/-- The obvious extra degeneracy on the standard simplex. -/
@[protected]
def extra_degeneracy (Î” : simplex_category) :
  simplicial_object.augmented.extra_degeneracy (standard_simplex.obj Î”) :=
{ s' := Î» x, simplex_category.hom.mk (order_hom.const _ 0),
  s := Î» n f, shift f,
  s'_comp_Îµ' := by { ext1 j, fin_cases j, },
  sâ‚€_comp_Î´â‚' := by { ext x j, fin_cases j, refl, },
  s_comp_Î´â‚€' := Î» n, begin
    ext Ï† i : 4,
    dsimp [simplicial_object.Î´, simplex_category.Î´, sSet.standard_simplex],
    simp only [shift_fun_succ],
  end,
  s_comp_Î´' := Î» n i, begin
    ext Ï† j : 4,
    dsimp [simplicial_object.Î´, simplex_category.Î´, sSet.standard_simplex],
    by_cases j = 0,
    { subst h,
      simp only [fin.succ_succ_above_zero, shift_fun_0], },
    { cases fin.eq_succ_of_ne_zero h with k hk,
      subst hk,
      simp only [fin.succ_succ_above_succ, shift_fun_succ], },
  end,
  s_comp_Ïƒ' := Î» n i, begin
    ext Ï† j : 4,
    dsimp [simplicial_object.Ïƒ, simplex_category.Ïƒ, sSet.standard_simplex],
    by_cases j = 0,
    { subst h,
      simpa only [shift_fun_0] using shift_fun_0 Ï†.to_order_hom, },
    { cases fin.eq_succ_of_ne_zero h with k hk,
      subst hk,
      simp only [fin.succ_pred_above_succ, shift_fun_succ], },
  end, }

instance nonempty_extra_degeneracy_standard_simplex (Î” : simplex_category) :
  nonempty (simplicial_object.augmented.extra_degeneracy (standard_simplex.obj Î”)) :=
âŸ¨standard_simplex.extra_degeneracy Î”âŸ©

end standard_simplex

end augmented

end sSet

namespace category_theory

open limits

namespace arrow

namespace augmented_cech_nerve

variables {C : Type*} [category C] (f : arrow C)
  [âˆ€ n : â„•, has_wide_pullback f.right (Î» i : fin (n+1), f.left) (Î» i, f.hom)]
  (S : split_epi f.hom)

include S

/-- The extra degeneracy map on the ÄŒech nerve of a split epi. It is
given on the `0`-projection by the given section of the split epi,
and by shifting the indices on the other projections. -/
noncomputable def extra_degeneracy.s (n : â„•) :
  f.cech_nerve.obj (op [n]) âŸ¶ f.cech_nerve.obj (op [n + 1]) :=
wide_pullback.lift (wide_pullback.base _)
  (Î» i, dite (i = 0) (Î» h, wide_pullback.base _ â‰« S.section_)
    (Î» h, wide_pullback.Ï€ _ (i.pred h)))
  (Î» i, begin
    split_ifs,
    { subst h,
      simp only [assoc, split_epi.id, comp_id], },
    { simp only [wide_pullback.Ï€_arrow], },
  end)

@[simp]
lemma extra_degeneracy.s_comp_Ï€_0 (n : â„•) :
  extra_degeneracy.s f S n â‰« wide_pullback.Ï€ _ 0 = wide_pullback.base _ â‰« S.section_ :=
by { dsimp [extra_degeneracy.s], simpa only [wide_pullback.lift_Ï€], }

@[simp]
lemma extra_degeneracy.s_comp_Ï€_succ (n : â„•) (i : fin (n+1)) :
  extra_degeneracy.s f S n â‰« wide_pullback.Ï€ _ i.succ = wide_pullback.Ï€ _ i :=
begin
  dsimp [extra_degeneracy.s],
  simp only [wide_pullback.lift_Ï€],
  split_ifs,
  { exfalso,
    simpa only [fin.ext_iff, fin.coe_succ, fin.coe_zero, nat.succ_ne_zero] using h, },
  { congr,
    apply fin.pred_succ, },
end

@[simp]
lemma extra_degeneracy.s_comp_base (n : â„•) :
  extra_degeneracy.s f S n â‰« wide_pullback.base _ = wide_pullback.base _ :=
by apply wide_pullback.lift_base

/-- The augmented ÄŒech nerve associated to a split epimorphism has an extra degeneracy. -/
noncomputable def extra_degeneracy :
  simplicial_object.augmented.extra_degeneracy f.augmented_cech_nerve :=
{ s' := S.section_ â‰« wide_pullback.lift f.hom (Î» i, ğŸ™ _) (Î» i, by rw id_comp),
  s := Î» n, extra_degeneracy.s f S n,
  s'_comp_Îµ' :=
    by simp only [augmented_cech_nerve_hom_app, assoc, wide_pullback.lift_base, split_epi.id],
  sâ‚€_comp_Î´â‚' := begin
    dsimp [cech_nerve, simplicial_object.Î´, simplex_category.Î´],
    ext j,
    { fin_cases j,
      simpa only [assoc, wide_pullback.lift_Ï€, comp_id] using extra_degeneracy.s_comp_Ï€_0 f S 0, },
    { simpa only [assoc, wide_pullback.lift_base, split_epi.id, comp_id]
        using extra_degeneracy.s_comp_base f S 0, },
  end,
  s_comp_Î´â‚€' := Î» n, begin
    dsimp [cech_nerve, simplicial_object.Î´, simplex_category.Î´],
    ext j,
    { simpa only [assoc, wide_pullback.lift_Ï€, id_comp]
        using extra_degeneracy.s_comp_Ï€_succ f S n j, },
    { simpa only [assoc, wide_pullback.lift_base, id_comp]
        using extra_degeneracy.s_comp_base f S n, },
  end,
  s_comp_Î´' := Î» n i, begin
    dsimp [cech_nerve, simplicial_object.Î´, simplex_category.Î´],
    ext j,
    { simp only [assoc, wide_pullback.lift_Ï€],
      by_cases j = 0,
      { subst h,
        erw [fin.succ_succ_above_zero, extra_degeneracy.s_comp_Ï€_0, extra_degeneracy.s_comp_Ï€_0],
        dsimp,
        simp only [wide_pullback.lift_base_assoc], },
      { cases fin.eq_succ_of_ne_zero h with k hk,
        subst hk,
        erw [fin.succ_succ_above_succ, extra_degeneracy.s_comp_Ï€_succ,
          extra_degeneracy.s_comp_Ï€_succ],
        dsimp,
        simp only [wide_pullback.lift_Ï€], }, },
    { simp only [assoc, wide_pullback.lift_base],
      erw [extra_degeneracy.s_comp_base, extra_degeneracy.s_comp_base],
      dsimp,
      simp only [wide_pullback.lift_base], },
  end,
  s_comp_Ïƒ' := Î» n i, begin
    dsimp [cech_nerve, simplicial_object.Ïƒ, simplex_category.Ïƒ],
    ext j,
    { simp only [assoc, wide_pullback.lift_Ï€],
      by_cases j = 0,
      { subst h,
        erw [extra_degeneracy.s_comp_Ï€_0, extra_degeneracy.s_comp_Ï€_0],
        dsimp,
        simp only [wide_pullback.lift_base_assoc], },
      { cases fin.eq_succ_of_ne_zero h with k hk,
        subst hk,
        erw [fin.succ_pred_above_succ, extra_degeneracy.s_comp_Ï€_succ,
          extra_degeneracy.s_comp_Ï€_succ],
        dsimp,
        simp only [wide_pullback.lift_Ï€], }, },
    { simp only [assoc, wide_pullback.lift_base],
      erw [extra_degeneracy.s_comp_base, extra_degeneracy.s_comp_base],
      dsimp,
      simp only [wide_pullback.lift_base], },
  end, }

end augmented_cech_nerve

end arrow

end category_theory
