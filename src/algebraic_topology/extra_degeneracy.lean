/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import algebraic_topology.alternating_face_map_complex
import algebraic_topology.cech_nerve
import algebra.homology.homotopy
import tactic.equiv_rw
import tactic.fin_cases

noncomputable theory

open_locale simplicial

open category_theory category_theory.category category_theory.limits
open category_theory.simplicial_object.augmented
open opposite simplex_category

namespace algebraic_topology

variables {C : Type*} [category C]

/-- The datum of an extra degeneracy is a technical condition on
augmented simplicial objects. The morphisms `s'` and `s n` of the
structure formally behave like extra degeneracies `œÉ (-1)`. In
the case of augmented simplicial sets, the existence of an extra
degeneray implies the augmentation is an homotopy equivalence. -/
@[nolint has_inhabited_instance]
structure extra_degeneracy (X : simplicial_object.augmented C) :=
(s' : point.obj X ‚ü∂ (drop.obj X) _[0])
(s : Œ† (n : ‚Ñï), (drop.obj X) _[n] ‚ü∂ (drop.obj X) _[n+1])
(d‚ÇÄs' : s' ‚â´ X.hom.app (op [0]) = ùüô _)
(ds‚ÇÄ : s 0 ‚â´ (drop.obj X).Œ¥ 1 = X.hom.app (op [0]) ‚â´ s')
(d‚ÇÄs : Œ† (n : ‚Ñï), s n ‚â´ (drop.obj X).Œ¥ 0 = ùüô _)
(ds : Œ† (n : ‚Ñï) (i : fin (n+2)), s (n+1) ‚â´ (drop.obj X).Œ¥ i.succ =
  (drop.obj X).Œ¥ i ‚â´ s n)
(ss : Œ† (n : ‚Ñï) (i : fin (n+1)), s n ‚â´ (drop.obj X).œÉ i.succ =
  (drop.obj X).œÉ i ‚â´ s (n+1))

namespace extra_degeneracy

/-- If `ed` is an extra degeneracy for `X : simplicial_object.augmented C` and
`F : C ‚•§ D` is a functor, then `ed.map F` is an extra degeneracy for
augmented simplical objects in `D` obtained by applying `F` to `X`. -/
def map {D : Type*} [category D]
  {X : simplicial_object.augmented C} (ed : extra_degeneracy X) (F : C ‚•§ D) :
  extra_degeneracy (((whiskering _ _).obj F).obj X) :=
{ s' := F.map ed.s',
  s := Œª n, F.map (ed.s n),
  d‚ÇÄs' := by { dsimp, erw [comp_id, ‚Üê F.map_comp, ed.d‚ÇÄs', F.map_id], },
  ds‚ÇÄ := by { dsimp, erw [comp_id, ‚Üê F.map_comp, ‚Üê F.map_comp, ed.ds‚ÇÄ], },
  d‚ÇÄs := Œª n, by { dsimp, erw [‚Üê F.map_comp, ed.d‚ÇÄs, F.map_id], },
  ds := Œª n i, by { dsimp, erw [‚Üê F.map_comp, ‚Üê F.map_comp, ed.ds], refl, },
  ss := Œª n i, by { dsimp, erw [‚Üê F.map_comp, ‚Üê F.map_comp, ed.ss], refl, }, }

/-- If `X` and `Y` are isomorphism augmented simplicial objects, then an extra
degeneracy for `X` gives also an extra degeneracy for `Y` -/
def of_iso {X Y : simplicial_object.augmented C} (e : X ‚âÖ Y) (ed : extra_degeneracy X) :
  extra_degeneracy Y :=
{ s' := (point.map_iso e).inv ‚â´ ed.s' ‚â´ (drop.map_iso e).hom.app (op [0]),
  s := Œª n, (drop.map_iso e).inv.app (op [n]) ‚â´ ed.s n ‚â´ (drop.map_iso e).hom.app (op [n+1]),
  d‚ÇÄs' := begin
    simp only [assoc],
    erw w‚ÇÄ,
    slice_lhs 2 3 { rw ed.d‚ÇÄs', },
    rw id_comp,
    exact (point.map_iso e).inv_hom_id,
  end,
  ds‚ÇÄ := begin
    slice_rhs 1 2 { erw [‚Üê w‚ÇÄ], },
    slice_rhs 2 3 { rw [‚Üê ed.ds‚ÇÄ], },
    slice_rhs 3 4 { erw (drop.map e.hom).naturality, },
    simpa only [assoc],
  end,
  d‚ÇÄs := Œª n, begin
    slice_lhs 3 4 { erw ‚Üê (drop.map e.hom).naturality, },
    slice_lhs 2 3 { erw ed.d‚ÇÄs, },
    rw id_comp,
    convert congr_app (drop.map_iso e).inv_hom_id (op [n]),
  end,
  ds := Œª n i, begin
    slice_lhs 3 4 { erw ‚Üê (drop.map e.hom).naturality, },
    slice_lhs 2 3 { erw ed.ds, },
    slice_lhs 1 2 { erw ‚Üê (drop.map e.inv).naturality, },
    simpa only [assoc],
  end,
  ss := Œª n i, begin
    slice_lhs 3 4 { erw ‚Üê (drop.map e.hom).naturality, },
    slice_lhs 2 3 { erw ed.ss, },
    slice_lhs 1 2 { erw ‚Üê (drop.map e.inv).naturality, },
    simpa only [assoc],
  end, }

/-- In the (pre)additive case, if an augmented simplicial object `X` has an extra
degeneracy, then the augmentation `alternating_face_map_complex.Œµ.app X` is a
homotopy equivalence of chain complexes. -/
def homotopy_equivalence [preadditive C] [limits.has_zero_object C]
  {X : simplicial_object.augmented C} (ed : extra_degeneracy X) :
  homotopy_equiv (algebraic_topology.alternating_face_map_complex.obj (drop.obj X))
    ((chain_complex.single‚ÇÄ C).obj (point.obj X)) :=
{ hom := alternating_face_map_complex.Œµ.app X,
  inv := begin
    equiv_rw chain_complex.from_single‚ÇÄ_equiv _ _,
    exact ed.s',
  end,
  homotopy_hom_inv_id :=
  { hom := Œª i j, begin
      by_cases i+1 = j,
      { exact (-ed.s i) ‚â´ eq_to_hom (by congr'), },
      { exact 0, },
    end,
    zero' := Œª i j hij, begin
      split_ifs,
      { exfalso, exact hij h, },
      { simp only [eq_self_iff_true], },
    end,
    comm := Œª i, begin
      cases i,
      { dsimp [chain_complex.to_single‚ÇÄ_equiv, chain_complex.from_single‚ÇÄ_equiv],
        simp only [preadditive.neg_comp, homotopy.d_next_zero_chain_complex,
          homotopy.prev_d_chain_complex, eq_self_iff_true, eq_to_hom_refl, category.comp_id,
          dite_eq_ite, if_true, zero_add],
        erw [chain_complex.of_d],
        simp only [alternating_face_map_complex.obj_d, fin.sum_univ_two,
          fin.coe_zero, pow_zero, one_zsmul, fin.coe_one, pow_one, neg_smul, add_assoc,
          preadditive.comp_add, preadditive.comp_neg, neg_add_rev, neg_neg],
        erw [ed.d‚ÇÄs, ed.ds‚ÇÄ],
        convert (add_zero _).symm,
        apply neg_add_self, },
      { dsimp [chain_complex.to_single‚ÇÄ_equiv, chain_complex.from_single‚ÇÄ_equiv],
        simp only [limits.zero_comp, homotopy.d_next_succ_chain_complex, eq_self_iff_true,
          eq_to_hom_refl, category.comp_id, dite_eq_ite, if_true, homotopy.prev_d_chain_complex],
        erw [chain_complex.of_d, chain_complex.of_d],
        simp only [alternating_face_map_complex.obj_d, preadditive.comp_sum,
          preadditive.sum_comp, @fin.sum_univ_succ _ _ (i+2), fin.coe_zero, pow_zero,
          one_smul, preadditive.comp_add],
        have simplif : Œ† (a b c d : X.left _[i+1] ‚ü∂ X.left _[i+1])
          (h‚ÇÅ : a + c = 0) (h‚ÇÇ : b + d = 0), 0 = a + (b+c) + d,
        { intros a b c d h‚ÇÅ h‚ÇÇ,
          rw [add_comm b, ‚Üê add_assoc a, h‚ÇÅ, zero_add, h‚ÇÇ], },
        apply simplif,
        { simp only [‚Üê finset.sum_add_distrib],
          apply finset.sum_eq_zero,
          intros j hj,
          simp only [preadditive.zsmul_comp, preadditive.comp_zsmul, fin.coe_succ, pow_succ,
            preadditive.comp_neg, preadditive.neg_comp,
            neg_mul, one_mul, neg_smul, neg_neg],
          rw neg_add_eq_zero,
          congr' 1,
          exact (ed.ds i j).symm, },
        { erw [preadditive.neg_comp, ed.d‚ÇÄs i.succ],
          apply neg_add_self, }, },
    end, },
  homotopy_inv_hom_id := homotopy.of_eq begin
    ext n,
    cases n,
    { exact ed.d‚ÇÄs', },
    { tidy, },
  end, }

/-- The augmented ƒåech nerve associated to a split epimorphism has an extra degeneracy. -/
def for_cech_nerve_of_split_epi (f : arrow C)
  [‚àÄ n : ‚Ñï, has_wide_pullback f.right (Œª i : ulift (fin (n+1)), f.left) (Œª i, f.hom)]
  [split_epi f.hom] :
  extra_degeneracy (f.augmented_cech_nerve) :=
{ s' := section_ f.hom ‚â´ wide_pullback.lift f.hom (Œª i, ùüô _) (Œª i, by rw id_comp),
  s := Œª n, wide_pullback.lift (wide_pullback.base _)
  begin
    rintro ‚ü®i‚ü©,
    by_cases i = 0,
    { exact wide_pullback.base _ ‚â´ section_ f.hom, },
    { exact wide_pullback.œÄ _ (ulift.up ((œÉ 0).to_order_hom i)), },
  end
  begin
    intro j,
    cases j,
    dsimp,
    split_ifs,
    { subst h,
      simp only [assoc, split_epi.id, comp_id], },
    { simp only [wide_pullback.œÄ_arrow], },
  end,
  d‚ÇÄs' := by simp only [arrow.augmented_cech_nerve_hom_app, assoc,
    wide_pullback.lift_base, split_epi.id],
  ds‚ÇÄ := begin
    ext; dsimp [simplicial_object.Œ¥],
    { simp only [assoc, comp_id, wide_pullback.lift_œÄ, ite_eq_left_iff],
      intro h,
      exfalso,
      apply h,
      fin_cases j,
      refl, },
    { simp only [assoc, split_epi.id, comp_id, wide_pullback.lift_base], },
  end,
  d‚ÇÄs := Œª n, begin
    ext; dsimp [simplicial_object.Œ¥],
    { simp only [assoc, wide_pullback.lift_œÄ, id_comp],
      split_ifs,
      { exfalso,
        exact j.down.succ_ne_zero h, },
      { congr,
        cases j,
        ext1,
        have eq : Œ¥ 0 ‚â´ œÉ 0 = ùüô [n] := Œ¥_comp_œÉ_self,
        exact hom.congr_eval eq j, }, },
    { simp only [assoc, wide_pullback.lift_base, id_comp], },
  end,
  ds := Œª n i, begin
    ext,
    { cases j,
      dsimp [simplicial_object.Œ¥],
      simp only [assoc, wide_pullback.lift_œÄ],
      by_cases hj : j = 0,
      { subst hj,
        split_ifs,
        { simp only [wide_pullback.lift_base_assoc], },
        { exfalso,
          apply h,
          apply fin.succ_above_below i.succ 0,
          simp only [fin.cast_succ_zero, fin.succ_pos], }, },
      { split_ifs with h‚ÇÅ,
        { exfalso,
          have h‚ÇÇ : i.succ.succ_above j = 0 := h‚ÇÅ,
          by_cases h‚ÇÉ : fin.cast_succ j < i.succ,
          { apply hj,
            ext,
            erw fin.succ_above_below _ _ h‚ÇÉ at h‚ÇÅ,
            simpa only [fin.ext_iff] using h‚ÇÅ, },
          { simp only [not_lt] at h‚ÇÉ,
            rw fin.succ_above_above i.succ j h‚ÇÉ at h‚ÇÇ,
            exact (fin.succ_ne_zero j) h‚ÇÇ, }, },
        { simp only [wide_pullback.lift_œÄ],
          congr,
          cases nonzero_as_Œ¥‚ÇÄ hj with k hk,
          subst hk,
          have eq : Œ¥ 0 ‚â´ Œ¥ i.succ ‚â´ œÉ 0 = Œ¥ 0 ‚â´ œÉ 0 ‚â´ Œ¥ i,
          { slice_lhs 1 2 { rw Œ¥_comp_Œ¥ (fin.zero_le i), },
            slice_lhs 2 3 { rw Œ¥_comp_œÉ_self, },
            slice_rhs 1 2 { erw Œ¥_comp_œÉ_self, },
            rw [id_comp, comp_id], },
          simpa only [coe_coe, fin.coe_coe_eq_self] using hom.congr_eval eq k, }, }, },
    { dsimp [simplicial_object.Œ¥],
      simp only [assoc, wide_pullback.lift_base], },
  end,
  ss := Œª n i, begin
    ext,
    { cases j,
      dsimp [simplicial_object.œÉ],
      simp only [assoc, wide_pullback.lift_œÄ],
      by_cases hj : j = 0,
      { subst hj,
        split_ifs,
        { simp only [wide_pullback.lift_base_assoc], },
        { exfalso,
          apply h,
          refl, }, },
      { split_ifs with h‚ÇÅ,
        { exfalso,
          apply hj,
          ext,
          have h‚ÇÇ : i.succ.pred_above j = 0 := h‚ÇÅ,
          dsimp [fin.pred_above] at h‚ÇÇ,
          split_ifs at h‚ÇÇ with h‚ÇÉ,
          { rw [‚Üê fin.succ_pred j hj, h‚ÇÇ, fin.lt_iff_coe_lt_coe] at h‚ÇÉ,
            simpa only [fin.coe_cast_succ, fin.coe_succ, fin.succ_zero_eq_one,
              fin.coe_one, nat.lt_one_iff] using h‚ÇÉ, },
          { simpa only [fin.ext_iff] using h‚ÇÇ, }, },
        { simp only [wide_pullback.lift_œÄ],
          congr' 1,
          ext1,
          cases nonzero_as_Œ¥‚ÇÄ hj with k hk,
          subst hk,
          have eq : Œ¥ 0 ‚â´ œÉ i.succ ‚â´ œÉ 0 = Œ¥ 0 ‚â´ œÉ 0 ‚â´ œÉ i,
          { slice_lhs 1 2 { erw Œ¥_comp_œÉ_of_le (fin.cast_succ i).zero_le, },
            slice_lhs 2 3 { erw Œ¥_comp_œÉ_self, },
            slice_rhs 1 2 { erw Œ¥_comp_œÉ_self, },
            rw [id_comp, comp_id], },
          simpa only [coe_coe, fin.coe_coe_eq_self] using hom.congr_eval eq k, }, }, },
      { dsimp [simplicial_object.œÉ],
        simp only [assoc, wide_pullback.lift_base], },
  end, }

end extra_degeneracy

end algebraic_topology
