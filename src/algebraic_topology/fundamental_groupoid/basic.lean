/-
Copyright (c) 2021 Shing Tak Lam. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Shing Tak Lam
-/
import category_theory.category.Groupoid
import category_theory.groupoid
import topology.category.Top.basic
import topology.homotopy.path

/-!
# Fundamental groupoid of a space

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Given a topological space `X`, we can define the fundamental groupoid of `X` to be the category with
objects being points of `X`, and morphisms `x ‚ü∂ y` being paths from `x` to `y`, quotiented by
homotopy equivalence. With this, the fundamental group of `X` based at `x` is just the automorphism
group of `x`.
-/

universes u v

variables {X : Type u} {Y : Type v} [topological_space X] [topological_space Y]
variables {x‚ÇÄ x‚ÇÅ : X}

noncomputable theory

open_locale unit_interval

namespace path

namespace homotopy

section

/-- Auxilliary function for `refl_trans_symm` -/
def refl_trans_symm_aux (x : I √ó I) : ‚Ñù :=
if (x.2 : ‚Ñù) ‚â§ 1/2 then
  x.1 * 2 * x.2
else
  x.1 * (2 - 2 * x.2)

@[continuity]
lemma continuous_refl_trans_symm_aux : continuous refl_trans_symm_aux :=
begin
  refine continuous_if_le _ _ (continuous.continuous_on _) (continuous.continuous_on _) _,
  { continuity },
  { continuity },
  { continuity },
  { continuity },
  intros x hx,
  norm_num [hx, mul_assoc],
end

lemma refl_trans_symm_aux_mem_I (x : I √ó I) : refl_trans_symm_aux x ‚àà I :=
begin
  dsimp only [refl_trans_symm_aux],
  split_ifs,
  { split,
    { apply mul_nonneg,
      { apply mul_nonneg,
        { unit_interval },
        { norm_num } },
      { unit_interval } },
    { rw [mul_assoc],
      apply mul_le_one,
      { unit_interval },
      { apply mul_nonneg,
        { norm_num },
        { unit_interval } },
      { linarith } } },
  { split,
    { apply mul_nonneg,
      { unit_interval },
      linarith [unit_interval.nonneg x.2, unit_interval.le_one x.2] },
    { apply mul_le_one,
      { unit_interval },
      { linarith [unit_interval.nonneg x.2, unit_interval.le_one x.2] },
      { linarith [unit_interval.nonneg x.2, unit_interval.le_one x.2] } } }
end

/-- For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from the constant path based at `x‚ÇÄ` to
  `p.trans p.symm`. -/
def refl_trans_symm (p : path x‚ÇÄ x‚ÇÅ) : homotopy (path.refl x‚ÇÄ) (p.trans p.symm) :=
{ to_fun := Œª x, p ‚ü®refl_trans_symm_aux x, refl_trans_symm_aux_mem_I x‚ü©,
  continuous_to_fun := by continuity,
  map_zero_left' := by norm_num [refl_trans_symm_aux],
  map_one_left' := Œª x, begin
    dsimp only [refl_trans_symm_aux, path.coe_to_continuous_map, path.trans],
    change _ = ite _ _ _,
    split_ifs,
    { rw [path.extend, set.Icc_extend_of_mem],
      { norm_num },
      { rw unit_interval.mul_pos_mem_iff zero_lt_two,
        exact ‚ü®unit_interval.nonneg x, h‚ü© } },
    { rw [path.symm, path.extend, set.Icc_extend_of_mem],
      { congr' 1,
        ext,
        norm_num [sub_sub_eq_add_sub] },
      { rw unit_interval.two_mul_sub_one_mem_iff,
        exact ‚ü®(not_le.1 h).le, unit_interval.le_one x‚ü© } }
  end,
  prop' := Œª t x hx, begin
    cases hx,
    { rw hx, simp [refl_trans_symm_aux] },
    { rw set.mem_singleton_iff at hx,
      rw hx,
      norm_num [refl_trans_symm_aux] }
  end }

/-- For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from the constant path based at `x‚ÇÅ` to
  `p.symm.trans p`. -/
def refl_symm_trans (p : path x‚ÇÄ x‚ÇÅ) : homotopy (path.refl x‚ÇÅ) (p.symm.trans p) :=
(refl_trans_symm p.symm).cast rfl $ congr_arg _ path.symm_symm

end

section trans_refl

/-- Auxilliary function for `trans_refl_reparam` -/
def trans_refl_reparam_aux (t : I) : ‚Ñù :=
if (t : ‚Ñù) ‚â§ 1/2 then
  2 * t
else
  1

@[continuity]
lemma continuous_trans_refl_reparam_aux : continuous trans_refl_reparam_aux :=
begin
  refine continuous_if_le _ _ (continuous.continuous_on _) (continuous.continuous_on _) _;
  [continuity, continuity, continuity, continuity, skip],
  intros x hx,
  norm_num [hx]
end

lemma trans_refl_reparam_aux_mem_I (t : I) : trans_refl_reparam_aux t ‚àà I :=
begin
  unfold trans_refl_reparam_aux,
  split_ifs; split; linarith [unit_interval.le_one t, unit_interval.nonneg t]
end

lemma trans_refl_reparam_aux_zero : trans_refl_reparam_aux 0 = 0 :=
by norm_num [trans_refl_reparam_aux]

lemma trans_refl_reparam_aux_one : trans_refl_reparam_aux 1 = 1 :=
by norm_num [trans_refl_reparam_aux]

lemma trans_refl_reparam (p : path x‚ÇÄ x‚ÇÅ) : p.trans (path.refl x‚ÇÅ) =
  p.reparam (Œª t, ‚ü®trans_refl_reparam_aux t, trans_refl_reparam_aux_mem_I t‚ü©) (by continuity)
  (subtype.ext trans_refl_reparam_aux_zero) (subtype.ext trans_refl_reparam_aux_one) :=
begin
  ext,
  unfold trans_refl_reparam_aux,
  simp only [path.trans_apply, not_le, coe_to_fun, function.comp_app],
  split_ifs,
  { refl },
  { simp }
end

/--
For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from `p.trans (path.refl x‚ÇÅ)` to `p`.
-/
def trans_refl (p : path x‚ÇÄ x‚ÇÅ) : homotopy (p.trans (path.refl x‚ÇÅ)) p :=
((homotopy.reparam p (Œª t, ‚ü®trans_refl_reparam_aux t, trans_refl_reparam_aux_mem_I t‚ü©)
  (by continuity) (subtype.ext trans_refl_reparam_aux_zero)
  (subtype.ext trans_refl_reparam_aux_one)).cast rfl (trans_refl_reparam p).symm).symm

/--
For any path `p` from `x‚ÇÄ` to `x‚ÇÅ`, we have a homotopy from `(path.refl x‚ÇÄ).trans p` to `p`.
-/
def refl_trans (p : path x‚ÇÄ x‚ÇÅ) : homotopy ((path.refl x‚ÇÄ).trans p) p :=
(trans_refl p.symm).symm‚ÇÇ.cast (by simp) (by simp)

end trans_refl

section assoc

/-- Auxilliary function for `trans_assoc_reparam`. -/
def trans_assoc_reparam_aux (t : I) : ‚Ñù :=
if (t : ‚Ñù) ‚â§ 1/4 then
  2 * t
else if (t : ‚Ñù) ‚â§ 1/2 then
  t + 1/4
else
  1/2 * (t + 1)

@[continuity]
lemma continuous_trans_assoc_reparam_aux : continuous trans_assoc_reparam_aux :=
begin
  refine continuous_if_le _ _ (continuous.continuous_on _)
    (continuous_if_le _ _ (continuous.continuous_on _)
    (continuous.continuous_on _) _).continuous_on _;
    [continuity, continuity, continuity, continuity, continuity, continuity, continuity,
     skip, skip];
    { intros x hx,
      norm_num [hx], }
end

lemma trans_assoc_reparam_aux_mem_I (t : I) : trans_assoc_reparam_aux t ‚àà I :=
begin
  unfold trans_assoc_reparam_aux,
  split_ifs; split; linarith [unit_interval.le_one t, unit_interval.nonneg t]
end

lemma trans_assoc_reparam_aux_zero : trans_assoc_reparam_aux 0 = 0 :=
by norm_num [trans_assoc_reparam_aux]

lemma trans_assoc_reparam_aux_one : trans_assoc_reparam_aux 1 = 1 :=
by norm_num [trans_assoc_reparam_aux]

lemma trans_assoc_reparam {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X} (p : path x‚ÇÄ x‚ÇÅ) (q : path x‚ÇÅ x‚ÇÇ) (r : path x‚ÇÇ x‚ÇÉ) :
  (p.trans q).trans r = (p.trans (q.trans r)).reparam
    (Œª t, ‚ü®trans_assoc_reparam_aux t, trans_assoc_reparam_aux_mem_I t‚ü©)
    (by continuity) (subtype.ext trans_assoc_reparam_aux_zero)
    (subtype.ext trans_assoc_reparam_aux_one) :=
begin
  ext,
  simp only [trans_assoc_reparam_aux, path.trans_apply, mul_inv_cancel_left‚ÇÄ, not_le,
             function.comp_app, ne.def, not_false_iff, bit0_eq_zero, one_ne_zero, mul_ite,
             subtype.coe_mk, path.coe_to_fun],
  -- TODO: why does split_ifs not reduce the ifs??????
  split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ h‚ÇÖ,
  { simp [h‚ÇÇ, h‚ÇÉ, -one_div] },
  { exfalso, linarith },
  { exfalso, linarith },
  { have h : ¬¨ (x : ‚Ñù) + 1/4 ‚â§ 1/2, by linarith,
    have h' : 2 * ((x : ‚Ñù) + 1/4) - 1 ‚â§ 1/2, by linarith,
    have h'' : 2 * (2 * (x : ‚Ñù)) - 1 = 2 * (2 * (‚Üëx + 1/4) - 1), by linarith,
    simp only [h‚ÇÑ, h‚ÇÅ, h, h', h'',
      dif_neg (show ¬¨ false, from id), dif_pos true.intro, if_false, if_true] },
  { exfalso,
    linarith },
  { have h : ¬¨ (1 / 2 : ‚Ñù) * (x + 1) ‚â§ 1/2, by linarith,
    have h' : ¬¨ 2 * ((1 / 2 : ‚Ñù) * (x + 1)) - 1 ‚â§ 1/2, by linarith,
    simp only [h‚ÇÅ, h‚ÇÖ, h, h', if_false, dif_neg (show ¬¨ false, from id)],
    congr, ring }
end

/--
For paths `p q r`, we have a homotopy from `(p.trans q).trans r` to `p.trans (q.trans r)`.
-/
def trans_assoc {x‚ÇÄ x‚ÇÅ x‚ÇÇ x‚ÇÉ : X} (p : path x‚ÇÄ x‚ÇÅ) (q : path x‚ÇÅ x‚ÇÇ) (r : path x‚ÇÇ x‚ÇÉ) :
  homotopy ((p.trans q).trans r) (p.trans (q.trans r)) :=
((homotopy.reparam (p.trans (q.trans r))
  (Œª t, ‚ü®trans_assoc_reparam_aux t, trans_assoc_reparam_aux_mem_I t‚ü©)
  (by continuity) (subtype.ext trans_assoc_reparam_aux_zero)
  (subtype.ext trans_assoc_reparam_aux_one)).cast rfl (trans_assoc_reparam p q r).symm).symm

end assoc

end homotopy

end path

/--
The fundamental groupoid of a space `X` is defined to be a type synonym for `X`, and we subsequently
put a `category_theory.groupoid` structure on it.
-/
def fundamental_groupoid (X : Type u) := X

namespace fundamental_groupoid

instance {X : Type u} [h : inhabited X] : inhabited (fundamental_groupoid X) := h

local attribute [reducible] fundamental_groupoid
local attribute [instance] path.homotopic.setoid

instance : category_theory.groupoid (fundamental_groupoid X) :=
{ hom := Œª x y, path.homotopic.quotient x y,
  id := Œª x, ‚ü¶path.refl x‚üß,
  comp := Œª x y z, path.homotopic.quotient.comp,
  id_comp' := Œª x y f, quotient.induction_on f
    (Œª a, show ‚ü¶(path.refl x).trans a‚üß = ‚ü¶a‚üß,
          from quotient.sound ‚ü®path.homotopy.refl_trans a‚ü© ),
  comp_id' := Œª x y f, quotient.induction_on f
    (Œª a, show ‚ü¶a.trans (path.refl y)‚üß = ‚ü¶a‚üß,
          from quotient.sound ‚ü®path.homotopy.trans_refl a‚ü©),
  assoc' := Œª w x y z f g h, quotient.induction_on‚ÇÉ f g h
    (Œª p q r, show ‚ü¶(p.trans q).trans r‚üß = ‚ü¶p.trans (q.trans r)‚üß,
              from quotient.sound ‚ü®path.homotopy.trans_assoc p q r‚ü©),
  inv := Œª x y p, quotient.lift (Œª l : path x y, ‚ü¶l.symm‚üß) begin
    rintros a b ‚ü®h‚ü©,
    rw quotient.eq,
    exact ‚ü®h.symm‚ÇÇ‚ü©,
  end p,
  inv_comp' := Œª x y f, quotient.induction_on f
    (Œª a, show ‚ü¶a.symm.trans a‚üß = ‚ü¶path.refl y‚üß,
          from quotient.sound ‚ü®(path.homotopy.refl_symm_trans a).symm‚ü©),
  comp_inv' := Œª x y f, quotient.induction_on f
    (Œª a, show ‚ü¶a.trans a.symm‚üß = ‚ü¶path.refl x‚üß,
          from quotient.sound ‚ü®(path.homotopy.refl_trans_symm a).symm‚ü©) }

lemma comp_eq (x y z : fundamental_groupoid X) (p : x ‚ü∂ y) (q : y ‚ü∂ z) : p ‚â´ q = p.comp q := rfl

lemma id_eq_path_refl (x : fundamental_groupoid X) : ùüô x = ‚ü¶path.refl x‚üß := rfl

/--
The functor sending a topological space `X` to its fundamental groupoid.
-/
def fundamental_groupoid_functor : Top ‚•§ category_theory.Groupoid :=
{ obj := Œª X, { Œ± := fundamental_groupoid X },
  map := Œª X Y f,
  { obj := f,
    map := Œª x y p, p.map_fn f,
    map_id' := Œª X, rfl,
    map_comp' := Œª x y z p q, quotient.induction_on‚ÇÇ p q $ Œª a b,
      by simp [comp_eq, ‚Üê path.homotopic.map_lift, ‚Üê path.homotopic.comp_lift] },
  map_id' := begin
    intro X,
    change _ = (‚ü®_, _, _, _‚ü© : fundamental_groupoid X ‚•§ fundamental_groupoid X),
    congr',
    ext x y p,
    refine quotient.induction_on p (Œª q, _),
    rw [‚Üê path.homotopic.map_lift],
    conv_rhs { rw [‚Üêq.map_id] },
    refl,
  end,
  map_comp' := begin
    intros X Y Z f g,
    congr',
    ext x y p,
    refine quotient.induction_on p (Œª q, _),
    simp only [quotient.map_mk, path.map_map, quotient.eq],
    refl,
  end }

localized "notation (name := fundamental_groupoid_functor)
  `œÄ` := fundamental_groupoid.fundamental_groupoid_functor" in fundamental_groupoid
localized "notation (name := fundamental_groupoid_functor.obj)
  `œÄ‚Çì` := fundamental_groupoid.fundamental_groupoid_functor.obj" in fundamental_groupoid
localized "notation (name := fundamental_groupoid_functor.map)
  `œÄ‚Çò` := fundamental_groupoid.fundamental_groupoid_functor.map" in fundamental_groupoid

lemma map_eq {X Y : Top} {x‚ÇÄ x‚ÇÅ : X} (f : C(X, Y)) (p : path.homotopic.quotient x‚ÇÄ x‚ÇÅ) :
  (œÄ‚Çò f).map p = p.map_fn f := rfl

/-- Help the typechecker by converting a point in a groupoid back to a point in
the underlying topological space. -/
@[reducible]
def to_top {X : Top} (x : œÄ‚Çì X) : X := x

/-- Help the typechecker by converting a point in a topological space to a
point in the fundamental groupoid of that space -/
@[reducible]
def from_top {X : Top} (x : X) : œÄ‚Çì X := x

/-- Help the typechecker by converting an arrow in the fundamental groupoid of
a topological space back to a path in that space (i.e., `path.homotopic.quotient`). -/
@[reducible]
def to_path {X : Top} {x‚ÇÄ x‚ÇÅ : œÄ‚Çì X} (p : x‚ÇÄ ‚ü∂ x‚ÇÅ) :
  path.homotopic.quotient x‚ÇÄ x‚ÇÅ := p

/-- Help the typechecker by convering a path in a topological space to an arrow in the
fundamental groupoid of that space. -/
@[reducible]
def from_path {X : Top} {x‚ÇÄ x‚ÇÅ : X} (p : path.homotopic.quotient x‚ÇÄ x‚ÇÅ) : (x‚ÇÄ ‚ü∂ x‚ÇÅ) := p

end fundamental_groupoid
