/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison, Adam Topaz
-/
import algebraic_topology.simplex_category
import category_theory.category.ulift
import category_theory.limits.functor_category
import category_theory.opposites
import category_theory.adjunction.limits
import category_theory.arrow

/-!
# Simplicial objects in a category.

A simplicial object in a category `C` is a `C`-valued presheaf on `simplex_category`.
(Similarly a cosimplicial object is functor `simplex_category â¥¤ C`.)

Use the notation `X _[n]` in the `simplicial` locale to obtain the `n`-th term of a
(co)simplicial object `X`, where `n` is a natural number.

-/

open opposite
open category_theory
open category_theory.limits

universes v u

namespace category_theory

variables (C : Type u) [category.{v} C]

/-- The category of simplicial objects valued in a category `C`.
This is the category of contravariant functors from `simplex_category` to `C`. -/
@[derive category, nolint has_inhabited_instance]
def simplicial_object := simplex_category.{v}áµ’áµ– â¥¤ C

namespace simplicial_object

localized
  "notation X `_[`:1000 n `]` :=
    (X : category_theory.simplicial_object _).obj (opposite.op (simplex_category.mk n))"
  in simplicial

instance {J : Type v} [small_category J] [has_limits_of_shape J C] :
  has_limits_of_shape J (simplicial_object C) := by {dsimp [simplicial_object], apply_instance}

instance [has_limits C] : has_limits (simplicial_object C) := âŸ¨infer_instanceâŸ©

instance {J : Type v} [small_category J] [has_colimits_of_shape J C] :
  has_colimits_of_shape J (simplicial_object C) := by {dsimp [simplicial_object], apply_instance}

instance [has_colimits C] : has_colimits (simplicial_object C) := âŸ¨infer_instanceâŸ©

variables {C} (X : simplicial_object C)

/-- Face maps for a simplicial object. -/
def Î´ {n} (i : fin (n+2)) : X _[n+1] âŸ¶ X _[n] :=
X.map (simplex_category.Î´ i).op

/-- Degeneracy maps for a simplicial object. -/
def Ïƒ {n} (i : fin (n+1)) : X _[n] âŸ¶ X _[n+1] :=
X.map (simplex_category.Ïƒ i).op


/-- Isomorphisms from identities in â„•. -/
def eq_to_iso {n m : â„•} (h : n = m) : X _[n] â‰… X _[m] :=
X.map_iso (eq_to_iso (by rw h))

@[simp] lemma eq_to_iso_refl {n : â„•} (h : n = n) : X.eq_to_iso h = iso.refl _ :=
by { ext, simp [eq_to_iso], }


/-- The generic case of the first simplicial identity -/
lemma Î´_comp_Î´ {n} {i j : fin (n+2)} (H : i â‰¤ j) :
  X.Î´ j.succ â‰« X.Î´ i = X.Î´ i.cast_succ â‰« X.Î´ j :=
by { dsimp [Î´], simp only [â†X.map_comp, â†op_comp, simplex_category.Î´_comp_Î´ H] }

/-- The special case of the first simplicial identity -/
lemma Î´_comp_Î´_self {n} {i : fin (n+2)} : X.Î´ i.cast_succ â‰« X.Î´ i = X.Î´ i.succ â‰« X.Î´ i :=
by { dsimp [Î´], simp only [â†X.map_comp, â†op_comp, simplex_category.Î´_comp_Î´_self] }

/-- The second simplicial identity -/
lemma Î´_comp_Ïƒ_of_le {n} {i : fin (n+2)} {j : fin (n+1)} (H : i â‰¤ j.cast_succ) :
  X.Ïƒ j.succ â‰« X.Î´ i.cast_succ = X.Î´ i â‰« X.Ïƒ j :=
by { dsimp [Î´, Ïƒ], simp only [â†X.map_comp, â†op_comp, simplex_category.Î´_comp_Ïƒ_of_le H] }

/-- The first part of the third simplicial identity -/
lemma Î´_comp_Ïƒ_self {n} {i : fin (n+1)} :
  X.Ïƒ i â‰« X.Î´ i.cast_succ = ğŸ™ _ :=
begin
  dsimp [Î´, Ïƒ],
  simp only [â†X.map_comp, â†op_comp, simplex_category.Î´_comp_Ïƒ_self, op_id, X.map_id],
end

/-- The second part of the third simplicial identity -/
lemma Î´_comp_Ïƒ_succ {n} {i : fin (n+1)} :
  X.Ïƒ i â‰« X.Î´ i.succ = ğŸ™ _ :=
begin
  dsimp [Î´, Ïƒ],
  simp only [â†X.map_comp, â†op_comp, simplex_category.Î´_comp_Ïƒ_succ, op_id, X.map_id],
end

/-- The fourth simplicial identity -/
lemma Î´_comp_Ïƒ_of_gt {n} {i : fin (n+2)} {j : fin (n+1)} (H : j.cast_succ < i) :
  X.Ïƒ j.cast_succ â‰« X.Î´ i.succ = X.Î´ i â‰« X.Ïƒ j :=
by { dsimp [Î´, Ïƒ], simp only [â†X.map_comp, â†op_comp, simplex_category.Î´_comp_Ïƒ_of_gt H] }

/-- The fifth simplicial identity -/
lemma Ïƒ_comp_Ïƒ {n} {i j : fin (n+1)} (H : i â‰¤ j) :
  X.Ïƒ j â‰« X.Ïƒ i.cast_succ = X.Ïƒ i â‰« X.Ïƒ j.succ :=
by { dsimp [Î´, Ïƒ], simp only [â†X.map_comp, â†op_comp, simplex_category.Ïƒ_comp_Ïƒ H] }

variable (C)

/-- Functor composition induces a functor on simplicial objects. -/
@[simps]
def whiskering (D : Type*) [category.{v} D] :
  (C â¥¤ D) â¥¤ simplicial_object C â¥¤ simplicial_object D :=
whiskering_right _ _ _

/-- Truncated simplicial objects. -/
@[derive category, nolint has_inhabited_instance]
def truncated (n : â„•) := (simplex_category.truncated.{v} n)áµ’áµ– â¥¤ C

variable {C}

namespace truncated

instance {n} {J : Type v} [small_category J] [has_limits_of_shape J C] :
  has_limits_of_shape J (simplicial_object.truncated C n) := by {dsimp [truncated], apply_instance}

instance {n} [has_limits C] : has_limits (simplicial_object.truncated C n) := âŸ¨infer_instanceâŸ©

instance {n} {J : Type v} [small_category J] [has_colimits_of_shape J C] :
  has_colimits_of_shape J (simplicial_object.truncated C n) :=
by {dsimp [truncated], apply_instance}

instance {n} [has_colimits C] : has_colimits (simplicial_object.truncated C n) := âŸ¨infer_instanceâŸ©

variable (C)

/-- Functor composition induces a functor on truncated simplicial objects. -/
@[simps]
def whiskering {n} (D : Type*) [category.{v} D] :
  (C â¥¤ D) â¥¤ truncated C n â¥¤ truncated D n :=
whiskering_right _ _ _

variable {C}

end truncated

section skeleton

/-- The skeleton functor from simplicial objects to truncated simplicial objects. -/
def sk (n : â„•) : simplicial_object C â¥¤ simplicial_object.truncated C n :=
(whiskering_left _ _ _).obj simplex_category.truncated.inclusion.op

end skeleton

variable (C)

/-- The constant simplicial object is the constant functor. -/
abbreviation const : C â¥¤ simplicial_object C := category_theory.functor.const _

/-- The category of augmented simplicial objects, defined as a comma category. -/
@[derive category, nolint has_inhabited_instance]
def augmented := comma (ğŸ­ (simplicial_object C)) (const C)

variable {C}

namespace augmented

/-- Drop the augmentation. -/
@[simps]
def drop : augmented C â¥¤ simplicial_object C := comma.fst _ _

/-- The point of the augmentation. -/
@[simps]
def point : augmented C â¥¤ C := comma.snd _ _

/-- The functor from augmented objects to arrows. -/
@[simps]
def to_arrow : augmented C â¥¤ arrow C :=
{ obj := Î» X,
  { left := (drop.obj X) _[0],
    right := (point.obj X),
    hom := X.hom.app _ },
  map := Î» X Y Î·,
  { left := (drop.map Î·).app _,
    right := (point.map Î·),
    w' := begin
      dsimp,
      rw â† nat_trans.comp_app,
      erw Î·.w,
      refl,
    end } }

variable (C)

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simp]
def whiskering_obj (D : Type*) [category.{v} D] (F : C â¥¤ D) :
  augmented C â¥¤ augmented D :=
{ obj := Î» X,
  { left := ((whiskering _ _).obj F).obj (drop.obj X),
    right := F.obj (point.obj X),
    hom := whisker_right X.hom F â‰« (functor.const_comp _ _ _).hom },
  map := Î» X Y Î·,
  { left := whisker_right Î·.left _,
    right := F.map Î·.right,
    w' := begin
      ext,
      dsimp,
      erw [category.comp_id, category.comp_id, â† F.map_comp,
        â† F.map_comp, â† nat_trans.comp_app, Î·.w],
      refl,
    end } }

/-- Functor composition induces a functor on augmented simplicial objects. -/
@[simps]
def whiskering (D : Type*) [category.{v} D] :
  (C â¥¤ D) â¥¤ augmented C â¥¤ augmented D :=
{ obj := whiskering_obj _ _,
  map := Î» X Y Î·,
  { app := Î» A,
    { left := whisker_left _ Î·,
      right := Î·.app _ } } }

variable {C}

end augmented

open_locale simplicial

/-- Aaugment a simplicial object with an object. -/
@[simps]
def augment (X : simplicial_object C) (Xâ‚€ : C) (f : X _[0] âŸ¶ Xâ‚€)
  (w : âˆ€ (i : simplex_category) (gâ‚ gâ‚‚ : [0] âŸ¶ i),
    X.map gâ‚.op â‰« f = X.map gâ‚‚.op â‰« f) : simplicial_object.augmented C :=
{ left := X,
  right := Xâ‚€,
  hom :=
  { app := Î» i, X.map (simplex_category.const i.unop 0).op â‰« f,
    naturality' := begin
      intros i j g,
      dsimp,
      rw â† g.op_unop,
      simpa only [â† X.map_comp, â† category.assoc, category.comp_id, â† op_comp] using w _ _ _,
    end } }

@[simp]
lemma augment_hom_zero (X : simplicial_object C) (Xâ‚€ : C) (f : X _[0] âŸ¶ Xâ‚€) (w) :
  (X.augment Xâ‚€ f w).hom.app (op [0]) = f :=
by { dsimp, erw [simplex_category.hom_zero_zero ([0].const 0), X.map_id, category.id_comp] }

end simplicial_object

/-- Cosimplicial objects. -/
@[derive category, nolint has_inhabited_instance]
def cosimplicial_object := simplex_category.{v} â¥¤ C

namespace cosimplicial_object

localized
  "notation X `_[`:1000 n `]` :=
    (X : category_theory.cosimplicial_object _).obj (simplex_category.mk n)"
  in simplicial

instance {J : Type v} [small_category J] [has_limits_of_shape J C] :
  has_limits_of_shape J (cosimplicial_object C) := by {dsimp [cosimplicial_object], apply_instance}

instance [has_limits C] : has_limits (cosimplicial_object C) := âŸ¨infer_instanceâŸ©

instance {J : Type v} [small_category J] [has_colimits_of_shape J C] :
  has_colimits_of_shape J (cosimplicial_object C) :=
by {dsimp [cosimplicial_object], apply_instance}

instance [has_colimits C] : has_colimits (cosimplicial_object C) := âŸ¨infer_instanceâŸ©

variables {C} (X : cosimplicial_object C)

/-- Coface maps for a cosimplicial object. -/
def Î´ {n} (i : fin (n+2)) : X _[n] âŸ¶ X _[n+1] :=
X.map (simplex_category.Î´ i)

/-- Codegeneracy maps for a cosimplicial object. -/
def Ïƒ {n} (i : fin (n+1)) : X _[n+1] âŸ¶ X _[n] :=
X.map (simplex_category.Ïƒ i)

/-- Isomorphisms from identities in â„•. -/
def eq_to_iso {n m : â„•} (h : n = m) : X _[n] â‰… X _[m] :=
X.map_iso (eq_to_iso (by rw h))

@[simp] lemma eq_to_iso_refl {n : â„•} (h : n = n) : X.eq_to_iso h = iso.refl _ :=
by { ext, simp [eq_to_iso], }


/-- The generic case of the first cosimplicial identity -/
lemma Î´_comp_Î´ {n} {i j : fin (n+2)} (H : i â‰¤ j) :
  X.Î´ i â‰« X.Î´ j.succ = X.Î´ j â‰« X.Î´ i.cast_succ :=
by { dsimp [Î´], simp only [â†X.map_comp, simplex_category.Î´_comp_Î´ H], }

/-- The special case of the first cosimplicial identity -/
lemma Î´_comp_Î´_self {n} {i : fin (n+2)} : X.Î´ i â‰« X.Î´ i.cast_succ = X.Î´ i â‰« X.Î´ i.succ :=
by { dsimp [Î´], simp only [â†X.map_comp, simplex_category.Î´_comp_Î´_self] }

/-- The second cosimplicial identity -/
lemma Î´_comp_Ïƒ_of_le {n} {i : fin (n+2)} {j : fin (n+1)} (H : i â‰¤ j.cast_succ) :
  X.Î´ i.cast_succ â‰« X.Ïƒ j.succ = X.Ïƒ j â‰« X.Î´ i :=
by { dsimp [Î´, Ïƒ], simp only [â†X.map_comp, simplex_category.Î´_comp_Ïƒ_of_le H] }

/-- The first part of the third cosimplicial identity -/
lemma Î´_comp_Ïƒ_self {n} {i : fin (n+1)} :
  X.Î´ i.cast_succ â‰« X.Ïƒ i = ğŸ™ _ :=
begin
  dsimp [Î´, Ïƒ],
  simp only [â†X.map_comp, simplex_category.Î´_comp_Ïƒ_self, X.map_id],
end

/-- The second part of the third cosimplicial identity -/
lemma Î´_comp_Ïƒ_succ {n} {i : fin (n+1)} :
  X.Î´ i.succ â‰« X.Ïƒ i = ğŸ™ _ :=
begin
  dsimp [Î´, Ïƒ],
  simp only [â†X.map_comp, simplex_category.Î´_comp_Ïƒ_succ, X.map_id],
end

/-- The fourth cosimplicial identity -/
lemma Î´_comp_Ïƒ_of_gt {n} {i : fin (n+2)} {j : fin (n+1)} (H : j.cast_succ < i) :
  X.Î´ i.succ â‰« X.Ïƒ j.cast_succ = X.Ïƒ j â‰« X.Î´ i :=
by { dsimp [Î´, Ïƒ], simp only [â†X.map_comp, simplex_category.Î´_comp_Ïƒ_of_gt H] }

/-- The fifth cosimplicial identity -/
lemma Ïƒ_comp_Ïƒ {n} {i j : fin (n+1)} (H : i â‰¤ j) :
  X.Ïƒ i.cast_succ â‰« X.Ïƒ j = X.Ïƒ j.succ â‰« X.Ïƒ i :=
by { dsimp [Î´, Ïƒ], simp only [â†X.map_comp, simplex_category.Ïƒ_comp_Ïƒ H] }

variable (C)

/-- Functor composition induces a functor on cosimplicial objects. -/
@[simps]
def whiskering (D : Type*) [category.{v} D] :
  (C â¥¤ D) â¥¤ cosimplicial_object C â¥¤ cosimplicial_object D :=
whiskering_right _ _ _

/-- Truncated cosimplicial objects. -/
@[derive category, nolint has_inhabited_instance]
def truncated (n : â„•) := simplex_category.truncated.{v} n â¥¤ C

variable {C}

namespace truncated

instance {n} {J : Type v} [small_category J] [has_limits_of_shape J C] :
  has_limits_of_shape J (cosimplicial_object.truncated C n) :=
by {dsimp [truncated], apply_instance}

instance {n} [has_limits C] : has_limits (cosimplicial_object.truncated C n) := âŸ¨infer_instanceâŸ©

instance {n} {J : Type v} [small_category J] [has_colimits_of_shape J C] :
  has_colimits_of_shape J (cosimplicial_object.truncated C n) :=
by {dsimp [truncated], apply_instance}

instance {n} [has_colimits C] : has_colimits (cosimplicial_object.truncated C n) := âŸ¨infer_instanceâŸ©

variable (C)

/-- Functor composition induces a functor on truncated cosimplicial objects. -/
@[simps]
def whiskering {n} (D : Type*) [category.{v} D] :
  (C â¥¤ D) â¥¤ truncated C n â¥¤ truncated D n :=
whiskering_right _ _ _

variable {C}

end truncated

section skeleton

/-- The skeleton functor from cosimplicial objects to truncated cosimplicial objects. -/
def sk (n : â„•) : cosimplicial_object C â¥¤ cosimplicial_object.truncated C n :=
(whiskering_left _ _ _).obj simplex_category.truncated.inclusion

end skeleton

variable (C)

/-- The constant cosimplicial object. -/
abbreviation const : C â¥¤ cosimplicial_object C := category_theory.functor.const _

/-- Augmented cosimplicial objects. -/
@[derive category, nolint has_inhabited_instance]
def augmented := comma (const C) (ğŸ­ (cosimplicial_object C))

variable {C}

namespace augmented

/-- Drop the augmentation. -/
@[simps]
def drop : augmented C â¥¤ cosimplicial_object C := comma.snd _ _

/-- The point of the augmentation. -/
@[simps]
def point : augmented C â¥¤ C := comma.fst _ _

/-- The functor from augmented objects to arrows. -/
@[simps]
def to_arrow : augmented C â¥¤ arrow C :=
{ obj := Î» X,
  { left := (point.obj X),
    right := (drop.obj X) _[0],
    hom := X.hom.app _ },
  map := Î» X Y Î·,
  { left := (point.map Î·),
    right := (drop.map Î·).app _,
    w' := begin
      dsimp,
      rw â† nat_trans.comp_app,
      erw â† Î·.w,
      refl,
    end } }

variable (C)

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simp]
def whiskering_obj (D : Type*) [category.{v} D] (F : C â¥¤ D) :
  augmented C â¥¤ augmented D :=
{ obj := Î» X,
  { left := F.obj (point.obj X),
    right := ((whiskering _ _).obj F).obj (drop.obj X),
    hom := (functor.const_comp _ _ _).inv â‰« whisker_right X.hom F },
  map := Î» X Y Î·,
  { left := F.map Î·.left,
    right := whisker_right Î·.right _,
    w' := begin
      ext,
      dsimp,
      erw [category.id_comp, category.id_comp, â† F.map_comp,
        â† F.map_comp, â† nat_trans.comp_app, â† Î·.w],
      refl,
    end } }

/-- Functor composition induces a functor on augmented cosimplicial objects. -/
@[simps]
def whiskering (D : Type*) [category.{v} D] :
  (C â¥¤ D) â¥¤ augmented C â¥¤ augmented D :=
{ obj := whiskering_obj _ _,
  map := Î» X Y Î·,
  { app := Î» A,
    { left := Î·.app _,
      right := whisker_left _ Î· } } }

variable {C}

end augmented

open_locale simplicial

/-- Augment a cosimplicial object with an object. -/
@[simps]
def augment (X : cosimplicial_object C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj [0])
  (w : âˆ€ (i : simplex_category) (gâ‚ gâ‚‚ : [0] âŸ¶ i),
    f â‰« X.map gâ‚ = f â‰« X.map gâ‚‚) : cosimplicial_object.augmented C :=
{ left := Xâ‚€,
  right := X,
  hom :=
  { app := Î» i, f â‰« X.map (simplex_category.const i 0),
  naturality' := begin
    intros i j g,
    dsimp,
    simpa [â† X.map_comp] using w _ _ _,
  end } }

@[simp]
lemma augment_hom_zero (X : cosimplicial_object C) (Xâ‚€ : C) (f : Xâ‚€ âŸ¶ X.obj [0]) (w) :
  (X.augment Xâ‚€ f w).hom.app [0] = f :=
by { dsimp, rw [simplex_category.hom_zero_zero ([0].const 0), X.map_id, category.comp_id] }

end cosimplicial_object

/-- The anti-equivalence between simplicial objects and cosimplicial objects. -/
@[simps]
def simplicial_cosimplicial_equiv : (simplicial_object C)áµ’áµ– â‰Œ (cosimplicial_object Cáµ’áµ–) :=
functor.left_op_right_op_equiv _ _

variable {C}

/-- Construct an augmented cosimplicial object in the opposite
category from an augmented simplicial object. -/
@[simps]
def simplicial_object.augmented.right_op (X : simplicial_object.augmented C) :
  cosimplicial_object.augmented Cáµ’áµ– :=
{ left := opposite.op X.right,
  right := X.left.right_op,
  hom := X.hom.right_op }

/-- Construct an augmented simplicial object from an augmented cosimplicial
object in the opposite category. -/
@[simps]
def cosimplicial_object.augmented.left_op (X : cosimplicial_object.augmented Cáµ’áµ–) :
  simplicial_object.augmented C :=
{ left := X.right.left_op,
  right := X.left.unop,
  hom := X.hom.left_op }

/-- Converting an augmented simplicial object to an augmented cosimplicial
object and back is isomorphic to the given object. -/
@[simps]
def simplicial_object.augmented.right_op_left_op_iso (X : simplicial_object.augmented C) :
  X.right_op.left_op â‰… X :=
comma.iso_mk X.left.right_op_left_op_iso (eq_to_iso $ by simp) (by tidy)

/-- Converting an augmented cosimplicial object to an augmented simplicial
object and back is isomorphic to the given object. -/
@[simps]
def cosimplicial_object.augmented.left_op_right_op_iso (X : cosimplicial_object.augmented Cáµ’áµ–) :
  X.left_op.right_op â‰… X :=
comma.iso_mk (eq_to_iso $ by simp) X.right.left_op_right_op_iso (by tidy)

variable (C)

/-- A functorial version of `simplicial_object.augmented.right_op`. -/
@[simps]
def simplicial_to_cosimplicial_augmented :
  (simplicial_object.augmented C)áµ’áµ– â¥¤ cosimplicial_object.augmented Cáµ’áµ– :=
{ obj := Î» X, X.unop.right_op,
  map := Î» X Y f,
  { left := f.unop.right.op,
    right := f.unop.left.right_op,
    w' := begin
      ext x,
      dsimp,
      simp_rw â† op_comp,
      congr' 1,
      exact (congr_app f.unop.w (op x)).symm,
    end } }

/-- A functorial version of `cosimplicial_object.augmented.left_op`. -/
@[simps]
def cosimplicial_to_simplicial_augmented :
  cosimplicial_object.augmented Cáµ’áµ– â¥¤ (simplicial_object.augmented C)áµ’áµ– :=
{ obj := Î» X, opposite.op X.left_op,
  map := Î» X Y f, quiver.hom.op $
  { left := f.right.left_op,
    right := f.left.unop,
    w' := begin
      ext x,
      dsimp,
      simp_rw â† unop_comp,
      congr' 1,
      exact (congr_app f.w x.unop).symm,
    end} }

/-- The contravariant categorical equivalence between augmented simplicial
objects and augmented cosimplicial objects in the opposite category. -/
@[simps]
def simplicial_cosimplicial_augmented_equiv :
  (simplicial_object.augmented C)áµ’áµ– â‰Œ cosimplicial_object.augmented Cáµ’áµ– :=
{ functor := simplicial_to_cosimplicial_augmented _,
  inverse := cosimplicial_to_simplicial_augmented _,
  unit_iso := nat_iso.of_components
    (Î» X, X.unop.right_op_left_op_iso.op) begin
      intros X Y f,
      dsimp,
      rw (show f = f.unop.op, by simp),
      simp_rw â† op_comp,
      congr' 1,
      tidy,
    end,
  counit_iso := nat_iso.of_components
    (Î» X, X.left_op_right_op_iso) (by tidy) }

end category_theory
