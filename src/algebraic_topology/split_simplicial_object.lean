/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import algebraic_topology.simplicial_object
import category_theory.limits.shapes.finite_products

/-!

# Split simplicial objects

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file, we introduce the notion of split simplicial object.
If `C` is a category that has finite coproducts, a splitting
`s : splitting X` of a simplical object `X` in `C` consists
of the datum of a sequence of objects `s.N : â„• â†’ C` (which
we shall refer to as "nondegenerate simplices") and a
sequence of morphisms `s.Î¹ n : s.N n â†’ X _[n]` that have
the property that a certain canonical map identifies `X _[n]`
with the coproduct of objects `s.N i` indexed by all possible
epimorphisms `[n] âŸ¶ [i]` in `simplex_category`. (We do not
assume that the morphisms `s.Î¹ n` are monomorphisms: in the
most common categories, this would be a consequence of the
axioms.)

Simplicial objects equipped with a splitting form a category
`simplicial_object.split C`.

## References
* [Stacks: Splitting simplicial objects] https://stacks.math.columbia.edu/tag/017O

-/

noncomputable theory

open category_theory category_theory.category category_theory.limits
  opposite simplex_category
open_locale simplicial

universe u

variables {C : Type*} [category C]

namespace simplicial_object

namespace splitting

/-- The index set which appears in the definition of split simplicial objects. -/
def index_set (Î” : simplex_categoryáµ’áµ–) :=
Î£ (Î”' : simplex_categoryáµ’áµ–), { Î± : Î”.unop âŸ¶ Î”'.unop // epi Î± }

namespace index_set

/-- The element in `splitting.index_set Î”` attached to an epimorphism `f : Î” âŸ¶ Î”'`. -/
@[simps]
def mk {Î” Î”' : simplex_category} (f : Î” âŸ¶ Î”') [epi f] : index_set (op Î”) :=
âŸ¨op Î”', f, infer_instanceâŸ©

variables {Î”' Î” : simplex_categoryáµ’áµ–} (A : index_set Î”) (Î¸ : Î” âŸ¶ Î”')

/-- The epimorphism in `simplex_category` associated to `A : splitting.index_set Î”` -/
def e := A.2.1

instance : epi A.e := A.2.2

lemma ext' : A = âŸ¨A.1, âŸ¨A.e, A.2.2âŸ©âŸ© := by tidy

lemma ext (Aâ‚ Aâ‚‚ : index_set Î”) (hâ‚ : Aâ‚.1 = Aâ‚‚.1)
  (hâ‚‚ : Aâ‚.e â‰« eq_to_hom (by rw hâ‚) = Aâ‚‚.e) : Aâ‚ = Aâ‚‚ :=
begin
  rcases Aâ‚ with âŸ¨Î”â‚, âŸ¨Î±â‚, hÎ±â‚âŸ©âŸ©,
  rcases Aâ‚‚ with âŸ¨Î”â‚‚, âŸ¨Î±â‚‚, hÎ±â‚‚âŸ©âŸ©,
  simp only at hâ‚,
  subst hâ‚,
  simp only [eq_to_hom_refl, comp_id, index_set.e] at hâ‚‚,
  simp only [hâ‚‚],
end

instance : fintype (index_set Î”) :=
fintype.of_injective
  ((Î» A, âŸ¨âŸ¨A.1.unop.len, nat.lt_succ_iff.mpr
    (len_le_of_epi (infer_instance : epi A.e))âŸ©, A.e.to_order_homâŸ©) :
    index_set Î” â†’ (sigma (Î» (k : fin (Î”.unop.len+1)), (fin (Î”.unop.len+1) â†’ fin (k+1)))))
begin
  rintros âŸ¨Î”â‚, Î±â‚âŸ© âŸ¨Î”â‚‚, Î±â‚‚âŸ© hâ‚,
  induction Î”â‚ using opposite.rec,
  induction Î”â‚‚ using opposite.rec,
  simp only at hâ‚,
  have hâ‚‚ : Î”â‚ = Î”â‚‚ := by { ext1, simpa only [fin.mk_eq_mk] using hâ‚.1, },
  subst hâ‚‚,
  refine ext _ _ rfl _,
  ext : 2,
  exact eq_of_heq hâ‚.2,
end

variable (Î”)

/-- The distinguished element in `splitting.index_set Î”` which corresponds to the
identity of `Î”`. -/
def id : index_set Î” := âŸ¨Î”, âŸ¨ğŸ™ _, by apply_instance,âŸ©âŸ©

instance : inhabited (index_set Î”) := âŸ¨id Î”âŸ©

variable {Î”}

/-- The condition that an element `splitting.index_set Î”` is the distinguished
element `splitting.index_set.id Î”`. -/
@[simp]
def eq_id : Prop := A = id _

lemma eq_id_iff_eq : A.eq_id â†” A.1 = Î” :=
begin
  split,
  { intro h,
    dsimp at h,
    rw h,
    refl, },
  { intro h,
    rcases A with âŸ¨Î”', âŸ¨f, hfâŸ©âŸ©,
    simp only at h,
    subst h,
    refine ext _ _ rfl _,
    { haveI := hf,
      simp only [eq_to_hom_refl, comp_id],
      exact eq_id_of_epi f, }, },
end

lemma eq_id_iff_len_eq : A.eq_id â†” A.1.unop.len = Î”.unop.len :=
begin
  rw eq_id_iff_eq,
  split,
  { intro h,
    rw h, },
  { intro h,
    rw â† unop_inj_iff,
    ext,
    exact h, },
end

lemma eq_id_iff_len_le : A.eq_id â†” Î”.unop.len â‰¤ A.1.unop.len :=
begin
  rw eq_id_iff_len_eq,
  split,
  { intro h,
    rw h, },
  { exact le_antisymm (len_le_of_epi (infer_instance : epi A.e)), },
end

lemma eq_id_iff_mono : A.eq_id â†” mono A.e :=
begin
  split,
  { intro h,
    dsimp at h,
    subst h,
    dsimp only [id, e],
    apply_instance, },
  { intro h,
    rw eq_id_iff_len_le,
    exact len_le_of_mono h, }
end

/-- Given `A : index_set Î”â‚`, if `p.unop : unop Î”â‚‚ âŸ¶ unop Î”â‚` is an epi, this
is the obvious element in `A : index_set Î”â‚‚` associated to the composition
of epimorphisms `p.unop â‰« A.e`. -/
@[simps]
def epi_comp {Î”â‚ Î”â‚‚ : simplex_categoryáµ’áµ–} (A : index_set Î”â‚) (p : Î”â‚ âŸ¶ Î”â‚‚) [epi p.unop] :
  index_set Î”â‚‚ := âŸ¨A.1, âŸ¨p.unop â‰« A.e, epi_comp _ _âŸ©âŸ©

/--
When `A : index_set Î”` and `Î¸ : Î” â†’ Î”'` is a morphism in `simplex_categoryáµ’áµ–`,
an element in `index_set Î”'` can be defined by using the epi-mono factorisation
of `Î¸.unop â‰« A.e`. -/
def pull : index_set Î”' := mk (factor_thru_image (Î¸.unop â‰« A.e))

@[reassoc]
lemma fac_pull : (A.pull Î¸).e â‰« image.Î¹ (Î¸.unop â‰« A.e) = Î¸.unop â‰« A.e := image.fac _

end index_set

variables (N : â„• â†’ C) (Î” : simplex_categoryáµ’áµ–)
  (X : simplicial_object C) (Ï† : Î  n, N n âŸ¶ X _[n])

/-- Given a sequences of objects `N : â„• â†’ C` in a category `C`, this is
a family of objects indexed by the elements `A : splitting.index_set Î”`.
The `Î”`-simplices of a split simplicial objects shall identify to the
coproduct of objects in such a family. -/
@[simp, nolint unused_arguments]
def summand (A : index_set Î”) : C := N A.1.unop.len

variable [has_finite_coproducts C]

/-- The coproduct of the family `summand N Î”` -/
@[simp]
def coprod := âˆ summand N Î”

variable {Î”}

/-- The inclusion of a summand in the coproduct. -/
@[simp]
def Î¹_coprod (A : index_set Î”) : N A.1.unop.len âŸ¶ coprod N Î” := sigma.Î¹ _ A

variables {N}

/-- The canonical morphism `coprod N Î” âŸ¶ X.obj Î”` attached to a sequence
of objects `N` and a sequence of morphisms `N n âŸ¶ X _[n]`. -/
@[simp]
def map (Î” : simplex_categoryáµ’áµ–) : coprod N Î” âŸ¶ X.obj Î” :=
sigma.desc (Î» A, Ï† A.1.unop.len â‰« X.map A.e.op)

end splitting

variable [has_finite_coproducts C]

/-- A splitting of a simplicial object `X` consists of the datum of a sequence
of objects `N`, a sequence of morphisms `Î¹ : N n âŸ¶ X _[n]` such that
for all `Î” : simplex_categoryháµ’áµ–`, the canonical map `splitting.map X Î¹ Î”`
is an isomorphism. -/
@[nolint has_nonempty_instance]
structure splitting (X : simplicial_object C) :=
(N : â„• â†’ C)
(Î¹ : Î  n, N n âŸ¶ X _[n])
(map_is_iso' : âˆ€ (Î” : simplex_categoryáµ’áµ–), is_iso (splitting.map X Î¹ Î”))

namespace splitting

variables {X Y : simplicial_object C} (s : splitting X)

instance map_is_iso (Î” : simplex_categoryáµ’áµ–) : is_iso (splitting.map X s.Î¹ Î”) :=
s.map_is_iso' Î”

/-- The isomorphism on simplices given by the axiom `splitting.map_is_iso'` -/
@[simps]
def iso (Î” : simplex_categoryáµ’áµ–) : coprod s.N Î” â‰… X.obj Î” :=
as_iso (splitting.map X s.Î¹ Î”)

/-- Via the isomorphism `s.iso Î”`, this is the inclusion of a summand
in the direct sum decomposition given by the splitting `s : splitting X`. -/
def Î¹_summand {Î” : simplex_categoryáµ’áµ–} (A : index_set Î”) :
  s.N A.1.unop.len âŸ¶ X.obj Î” :=
splitting.Î¹_coprod s.N A â‰« (s.iso Î”).hom

@[reassoc]
lemma Î¹_summand_eq {Î” : simplex_categoryáµ’áµ–} (A : index_set Î”) :
  s.Î¹_summand A = s.Î¹ A.1.unop.len â‰« X.map A.e.op :=
begin
  dsimp only [Î¹_summand, iso.hom],
  erw [colimit.Î¹_desc, cofan.mk_Î¹_app],
end

lemma Î¹_summand_id (n : â„•) : s.Î¹_summand (index_set.id (op [n])) = s.Î¹ n :=
by { erw [Î¹_summand_eq, X.map_id, comp_id], refl, }

/-- As it is stated in `splitting.hom_ext`, a morphism `f : X âŸ¶ Y` from a split
simplicial object to any simplicial object is determined by its restrictions
`s.Ï† f n : s.N n âŸ¶ Y _[n]` to the distinguished summands in each degree `n`. -/
@[simp]
def Ï† (f : X âŸ¶ Y) (n : â„•) : s.N n âŸ¶ Y _[n] := s.Î¹ n â‰« f.app (op [n])

@[simp, reassoc]
lemma Î¹_summand_comp_app (f : X âŸ¶ Y) {Î” : simplex_categoryáµ’áµ–} (A : index_set Î”) :
  s.Î¹_summand A â‰« f.app Î” = s.Ï† f A.1.unop.len â‰« Y.map A.e.op :=
by simp only [Î¹_summand_eq_assoc, Ï†, nat_trans.naturality, assoc]

lemma hom_ext' {Z : C} {Î” : simplex_categoryáµ’áµ–} (f g : X.obj Î” âŸ¶ Z)
  (h : âˆ€ (A : index_set Î”), s.Î¹_summand A â‰« f = s.Î¹_summand A â‰« g) :
    f = g :=
begin
  rw â† cancel_epi (s.iso Î”).hom,
  ext A,
  discrete_cases,
  simpa only [Î¹_summand_eq, iso_hom, colimit.Î¹_desc_assoc, cofan.mk_Î¹_app, assoc] using h A,
end

lemma hom_ext (f g : X âŸ¶ Y) (h : âˆ€ n : â„•, s.Ï† f n = s.Ï† g n) : f = g :=
begin
  ext Î”,
  apply s.hom_ext',
  intro A,
  induction Î” using opposite.rec,
  induction Î” using simplex_category.rec with n,
  dsimp,
  simp only [s.Î¹_summand_comp_app, h],
end

/-- The map `X.obj Î” âŸ¶ Z` obtained by providing a family of morphisms on all the
terms of decomposition given by a splitting `s : splitting X`  -/
def desc {Z : C} (Î” : simplex_categoryáµ’áµ–)
  (F : Î  (A : index_set Î”), s.N A.1.unop.len âŸ¶ Z) : X.obj Î” âŸ¶ Z :=
(s.iso Î”).inv â‰« sigma.desc F

@[simp, reassoc]
lemma Î¹_desc {Z : C} (Î” : simplex_categoryáµ’áµ–)
  (F : Î  (A : index_set Î”), s.N A.1.unop.len âŸ¶ Z) (A : index_set Î”) :
  s.Î¹_summand A â‰« s.desc Î” F = F A :=
begin
  dsimp only [Î¹_summand, desc],
  simp only [assoc, iso.hom_inv_id_assoc, Î¹_coprod],
  erw [colimit.Î¹_desc, cofan.mk_Î¹_app],
end

/-- A simplicial object that is isomorphic to a split simplicial object is split. -/
@[simps]
def of_iso (e : X â‰… Y) : splitting Y :=
{ N := s.N,
  Î¹ := Î» n, s.Î¹ n â‰« e.hom.app (op [n]),
  map_is_iso' := Î» Î”, begin
    convert (infer_instance : is_iso ((s.iso Î”).hom â‰« e.hom.app Î”)),
    tidy,
  end, }

@[reassoc]
lemma Î¹_summand_epi_naturality {Î”â‚ Î”â‚‚ : simplex_categoryáµ’áµ–} (A : index_set Î”â‚)
  (p : Î”â‚ âŸ¶ Î”â‚‚) [epi p.unop] :
  s.Î¹_summand A â‰« X.map p = s.Î¹_summand (A.epi_comp p) :=
begin
  dsimp [Î¹_summand],
  erw [colimit.Î¹_desc, colimit.Î¹_desc, cofan.mk_Î¹_app, cofan.mk_Î¹_app],
  dsimp only [index_set.epi_comp, index_set.e],
  rw [op_comp, X.map_comp, assoc, quiver.hom.op_unop],
end

end splitting

variable (C)

/-- The category `simplicial_object.split C` is the category of simplicial objects
in `C` equipped with a splitting, and morphisms are morphisms of simplicial objects
which are compatible with the splittings. -/
@[ext, nolint has_nonempty_instance]
structure split := (X : simplicial_object C) (s : splitting X)

namespace split

variable {C}

/-- The object in `simplicial_object.split C` attached to a splitting `s : splitting X`
of a simplicial object `X`. -/
@[simps]
def mk' {X : simplicial_object C} (s : splitting X) : split C := âŸ¨X, sâŸ©

/-- Morphisms in `simplicial_object.split C` are morphisms of simplicial objects that
are compatible with the splittings. -/
@[nolint has_nonempty_instance]
structure hom (Sâ‚ Sâ‚‚ : split C) :=
(F : Sâ‚.X âŸ¶ Sâ‚‚.X)
(f : Î  (n : â„•), Sâ‚.s.N n âŸ¶ Sâ‚‚.s.N n)
(comm' : âˆ€ (n : â„•), Sâ‚.s.Î¹ n â‰« F.app (op [n]) = f n â‰« Sâ‚‚.s.Î¹ n)

@[ext]
lemma hom.ext {Sâ‚ Sâ‚‚ : split C} (Î¦â‚ Î¦â‚‚ : hom Sâ‚ Sâ‚‚) (h : âˆ€ (n : â„•), Î¦â‚.f n = Î¦â‚‚.f n) :
  Î¦â‚ = Î¦â‚‚ :=
begin
  rcases Î¦â‚ with âŸ¨Fâ‚, fâ‚, câ‚âŸ©,
  rcases Î¦â‚‚ with âŸ¨Fâ‚‚, fâ‚‚, câ‚‚âŸ©,
  have h' : fâ‚ = fâ‚‚ := by { ext, apply h, },
  subst h',
  simp only [eq_self_iff_true, and_true],
  apply Sâ‚.s.hom_ext,
  intro n,
  dsimp,
  rw [câ‚, câ‚‚],
end

restate_axiom hom.comm'
attribute [simp, reassoc] hom.comm

end split

instance : category (split C) :=
{ hom      := split.hom,
  id       := Î» S, { F := ğŸ™ _, f := Î» n, ğŸ™ _, comm' := by tidy, },
  comp     := Î» Sâ‚ Sâ‚‚ Sâ‚ƒ Î¦â‚â‚‚ Î¦â‚‚â‚ƒ,
    { F := Î¦â‚â‚‚.F â‰« Î¦â‚‚â‚ƒ.F, f := Î» n, Î¦â‚â‚‚.f n â‰« Î¦â‚‚â‚ƒ.f n, comm' := by tidy, }, }

variable {C}

namespace split

lemma congr_F {Sâ‚ Sâ‚‚ : split C} {Î¦â‚ Î¦â‚‚ : Sâ‚ âŸ¶ Sâ‚‚} (h : Î¦â‚ = Î¦â‚‚) : Î¦â‚.F = Î¦â‚‚.F := by rw h
lemma congr_f {Sâ‚ Sâ‚‚ : split C} {Î¦â‚ Î¦â‚‚ : Sâ‚ âŸ¶ Sâ‚‚} (h : Î¦â‚ = Î¦â‚‚) (n : â„•) :
  Î¦â‚.f n = Î¦â‚‚.f n := by rw h

@[simp]
lemma id_F (S : split C) : (ğŸ™ S : S âŸ¶ S).F = ğŸ™ (S.X) := rfl

@[simp]
lemma id_f (S : split C) (n : â„•) : (ğŸ™ S : S âŸ¶ S).f n = ğŸ™ (S.s.N n) := rfl

@[simp]
lemma comp_F {Sâ‚ Sâ‚‚ Sâ‚ƒ : split C} (Î¦â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Î¦â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) :
  (Î¦â‚â‚‚ â‰« Î¦â‚‚â‚ƒ).F = Î¦â‚â‚‚.F â‰« Î¦â‚‚â‚ƒ.F := rfl

@[simp]
lemma comp_f {Sâ‚ Sâ‚‚ Sâ‚ƒ : split C} (Î¦â‚â‚‚ : Sâ‚ âŸ¶ Sâ‚‚) (Î¦â‚‚â‚ƒ : Sâ‚‚ âŸ¶ Sâ‚ƒ) (n : â„•) :
  (Î¦â‚â‚‚ â‰« Î¦â‚‚â‚ƒ).f n = Î¦â‚â‚‚.f n â‰« Î¦â‚‚â‚ƒ.f n := rfl

@[simp, reassoc]
lemma Î¹_summand_naturality_symm {Sâ‚ Sâ‚‚ : split C} (Î¦ : Sâ‚ âŸ¶ Sâ‚‚)
  {Î” : simplex_categoryáµ’áµ–} (A : splitting.index_set Î”) :
  Sâ‚.s.Î¹_summand A â‰« Î¦.F.app Î” = Î¦.f A.1.unop.len â‰« Sâ‚‚.s.Î¹_summand A :=
by rw [Sâ‚.s.Î¹_summand_eq, Sâ‚‚.s.Î¹_summand_eq, assoc, Î¦.F.naturality, â† Î¦.comm_assoc]

variable (C)

/-- The functor `simplicial_object.split C â¥¤ simplicial_object C` which forgets
the splitting. -/
@[simps]
def forget : split C â¥¤ simplicial_object C :=
{ obj := Î» S, S.X,
  map := Î» Sâ‚ Sâ‚‚ Î¦, Î¦.F, }

/-- The functor `simplicial_object.split C â¥¤ C` which sends a simplicial object equipped
with a splitting to its nondegenerate `n`-simplices. -/
@[simps]
def eval_N (n : â„•) : split C â¥¤ C :=
{ obj := Î» S, S.s.N n,
  map := Î» Sâ‚ Sâ‚‚ Î¦, Î¦.f n, }

/-- The inclusion of each summand in the coproduct decomposition of simplices
in split simplicial objects is a natural transformation of functors
`simplicial_object.split C â¥¤ C` -/
@[simps]
def nat_trans_Î¹_summand {Î” : simplex_categoryáµ’áµ–} (A : splitting.index_set Î”) :
  eval_N C A.1.unop.len âŸ¶ forget C â‹™ (evaluation simplex_categoryáµ’áµ– C).obj Î” :=
{ app := Î» S, S.s.Î¹_summand A,
  naturality' := Î» Sâ‚ Sâ‚‚ Î¦, (Î¹_summand_naturality_symm Î¦ A).symm, }

end split

end simplicial_object
