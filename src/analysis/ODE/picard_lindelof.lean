/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Winston Yin
-/
import analysis.special_functions.integrals
import topology.metric_space.contracting

/-!
# Picard-Lindel√∂f (Cauchy-Lipschitz) Theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we prove that an ordinary differential equation $\dot x=v(t, x)$ such that $v$ is
Lipschitz continuous in $x$ and continuous in $t$ has a local solution, see
`exists_forall_deriv_within_Icc_eq_of_is_picard_lindelof`.

As a corollary, we prove that a time-independent locally continuously differentiable ODE has a
local solution.

## Implementation notes

In order to split the proof into small lemmas, we introduce a structure `picard_lindelof` that holds
all assumptions of the main theorem. This structure and lemmas in the `picard_lindelof` namespace
should be treated as private implementation details. This is not to be confused with the `Prop`-
valued structure `is_picard_lindelof`, which holds the long hypotheses of the Picard-Lindel√∂f
theorem for actual use as part of the public API.

We only prove existence of a solution in this file. For uniqueness see `ODE_solution_unique` and
related theorems in `analysis.ODE.gronwall`.

## Tags

differential equation
-/

open filter function set metric topological_space interval_integral measure_theory
open measure_theory.measure_space (volume)
open_locale filter topology nnreal ennreal nat interval

noncomputable theory

variables {E : Type*} [normed_add_comm_group E] [normed_space ‚Ñù E]

/-- `Prop` structure holding the hypotheses of the Picard-Lindel√∂f theorem.

The similarly named `picard_lindelof` structure is part of the internal API for convenience, so as
not to constantly invoke choice, but is not intended for public use. -/
structure is_picard_lindelof
  {E : Type*} [normed_add_comm_group E] (v : ‚Ñù ‚Üí E ‚Üí E) (t_min t‚ÇÄ t_max : ‚Ñù) (x‚ÇÄ : E)
  (L : ‚Ñù‚â•0) (R C : ‚Ñù) : Prop :=
(ht‚ÇÄ : t‚ÇÄ ‚àà Icc t_min t_max)
(hR : 0 ‚â§ R)
(lipschitz : ‚àÄ t ‚àà Icc t_min t_max, lipschitz_on_with L (v t) (closed_ball x‚ÇÄ R))
(cont : ‚àÄ x ‚àà closed_ball x‚ÇÄ R, continuous_on (Œª (t : ‚Ñù), v t x) (Icc t_min t_max))
(norm_le : ‚àÄ (t ‚àà Icc t_min t_max) (x ‚àà closed_ball x‚ÇÄ R), ‚Äñv t x‚Äñ ‚â§ C)
(C_mul_le_R : (C : ‚Ñù) * linear_order.max (t_max - t‚ÇÄ) (t‚ÇÄ - t_min) ‚â§ R)

/-- This structure holds arguments of the Picard-Lipschitz (Cauchy-Lipschitz) theorem. It is part of
the internal API for convenience, so as not to constantly invoke choice. Unless you want to use one
of the auxiliary lemmas, use `exists_forall_deriv_within_Icc_eq_of_lipschitz_of_continuous` instead
of using this structure.

The similarly named `is_picard_lindelof` is a bundled `Prop` holding the long hypotheses of the
Picard-Lindel√∂f theorem as named arguments. It is used as part of the public API.
-/
structure picard_lindelof (E : Type*) [normed_add_comm_group E] [normed_space ‚Ñù E] :=
(to_fun : ‚Ñù ‚Üí E ‚Üí E)
(t_min t_max : ‚Ñù)
(t‚ÇÄ : Icc t_min t_max)
(x‚ÇÄ : E)
(C R L : ‚Ñù‚â•0)
(is_pl : is_picard_lindelof to_fun t_min t‚ÇÄ t_max x‚ÇÄ L R C)

namespace picard_lindelof

variables (v : picard_lindelof E)

instance : has_coe_to_fun (picard_lindelof E) (Œª _, ‚Ñù ‚Üí E ‚Üí E) := ‚ü®to_fun‚ü©

instance : inhabited (picard_lindelof E) :=
‚ü®‚ü®0, 0, 0, ‚ü®0, le_rfl, le_rfl‚ü©, 0, 0, 0, 0,
  { ht‚ÇÄ := by { rw [subtype.coe_mk, Icc_self], exact mem_singleton _ },
    hR := by refl,
    lipschitz := Œª t ht, (lipschitz_with.const 0).lipschitz_on_with _,
    cont := Œª _ _, by simpa only [pi.zero_apply] using continuous_on_const,
    norm_le := Œª t ht x hx, norm_zero.le,
    C_mul_le_R := (zero_mul _).le }‚ü©‚ü©

lemma t_min_le_t_max : v.t_min ‚â§ v.t_max := v.t‚ÇÄ.2.1.trans v.t‚ÇÄ.2.2

protected lemma nonempty_Icc : (Icc v.t_min v.t_max).nonempty := nonempty_Icc.2 v.t_min_le_t_max

protected lemma lipschitz_on_with {t} (ht : t ‚àà Icc v.t_min v.t_max) :
  lipschitz_on_with v.L (v t) (closed_ball v.x‚ÇÄ v.R) :=
v.is_pl.lipschitz t ht

protected lemma continuous_on :
  continuous_on (uncurry v) (Icc v.t_min v.t_max √óÀ¢ closed_ball v.x‚ÇÄ v.R) :=
have continuous_on (uncurry (flip v)) (closed_ball v.x‚ÇÄ v.R √óÀ¢ Icc v.t_min v.t_max),
  from continuous_on_prod_of_continuous_on_lipschitz_on _ v.L v.is_pl.cont v.is_pl.lipschitz,
this.comp continuous_swap.continuous_on (preimage_swap_prod _ _).symm.subset

lemma norm_le {t : ‚Ñù} (ht : t ‚àà Icc v.t_min v.t_max) {x : E} (hx : x ‚àà closed_ball v.x‚ÇÄ v.R) :
  ‚Äñv t x‚Äñ ‚â§ v.C :=
v.is_pl.norm_le _ ht _ hx

/-- The maximum of distances from `t‚ÇÄ` to the endpoints of `[t_min, t_max]`. -/
def t_dist : ‚Ñù := max (v.t_max - v.t‚ÇÄ) (v.t‚ÇÄ - v.t_min)

lemma t_dist_nonneg : 0 ‚â§ v.t_dist := le_max_iff.2 $ or.inl $ sub_nonneg.2 v.t‚ÇÄ.2.2

lemma dist_t‚ÇÄ_le (t : Icc v.t_min v.t_max) : dist t v.t‚ÇÄ ‚â§ v.t_dist :=
begin
  rw [subtype.dist_eq, real.dist_eq],
  cases le_total t v.t‚ÇÄ with ht ht,
  { rw [abs_of_nonpos (sub_nonpos.2 $ subtype.coe_le_coe.2 ht), neg_sub],
    exact (sub_le_sub_left t.2.1 _).trans (le_max_right _ _) },
  { rw [abs_of_nonneg (sub_nonneg.2 $ subtype.coe_le_coe.2 ht)],
    exact (sub_le_sub_right t.2.2 _).trans (le_max_left _ _) }
end

/-- Projection $‚Ñù ‚Üí [t_{\min}, t_{\max}]$ sending $(-‚àû, t_{\min}]$ to $t_{\min}$ and $[t_{\max}, ‚àû)$
to $t_{\max}$. -/
def proj : ‚Ñù ‚Üí Icc v.t_min v.t_max := proj_Icc v.t_min v.t_max v.t_min_le_t_max

lemma proj_coe (t : Icc v.t_min v.t_max) : v.proj t = t := proj_Icc_coe _ _

lemma proj_of_mem {t : ‚Ñù} (ht : t ‚àà Icc v.t_min v.t_max) : ‚Üë(v.proj t) = t :=
by simp only [proj, proj_Icc_of_mem _ ht, subtype.coe_mk]

@[continuity] lemma continuous_proj : continuous v.proj := continuous_proj_Icc

/-- The space of curves $Œ≥ \colon [t_{\min}, t_{\max}] \to E$ such that $Œ≥(t‚ÇÄ) = x‚ÇÄ$ and $Œ≥$ is
Lipschitz continuous with constant $C$. The map sending $Œ≥$ to
$\mathbf PŒ≥(t)=x‚ÇÄ + ‚à´_{t‚ÇÄ}^{t} v(œÑ, Œ≥(œÑ))\,dœÑ$ is a contracting map on this space, and its fixed
point is a solution of the ODE $\dot x=v(t, x)$. -/
structure fun_space :=
(to_fun : Icc v.t_min v.t_max ‚Üí E)
(map_t‚ÇÄ' : to_fun v.t‚ÇÄ = v.x‚ÇÄ)
(lipschitz' : lipschitz_with v.C to_fun)

namespace fun_space

variables {v} (f : fun_space v)

instance : has_coe_to_fun (fun_space v) (Œª _, Icc v.t_min v.t_max ‚Üí E) := ‚ü®to_fun‚ü©

instance : inhabited v.fun_space :=
‚ü®‚ü®Œª _, v.x‚ÇÄ, rfl, (lipschitz_with.const _).weaken (zero_le _)‚ü©‚ü©

protected lemma lipschitz : lipschitz_with v.C f := f.lipschitz'

protected lemma continuous : continuous f := f.lipschitz.continuous

/-- Each curve in `picard_lindelof.fun_space` is continuous. -/
def to_continuous_map : v.fun_space ‚Ü™ C(Icc v.t_min v.t_max, E) :=
‚ü®Œª f, ‚ü®f, f.continuous‚ü©, Œª f g h, by { cases f, cases g, simpa using h }‚ü©

instance : metric_space v.fun_space :=
metric_space.induced to_continuous_map to_continuous_map.injective infer_instance

lemma uniform_inducing_to_continuous_map : uniform_inducing (@to_continuous_map _ _ _ v) := ‚ü®rfl‚ü©

lemma range_to_continuous_map :
  range to_continuous_map =
    {f : C(Icc v.t_min v.t_max, E) | f v.t‚ÇÄ = v.x‚ÇÄ ‚àß lipschitz_with v.C f} :=
begin
  ext f, split,
  { rintro ‚ü®‚ü®f, hf‚ÇÄ, hf_lip‚ü©, rfl‚ü©, exact ‚ü®hf‚ÇÄ, hf_lip‚ü© },
  { rcases f with ‚ü®f, hf‚ü©, rintro ‚ü®hf‚ÇÄ, hf_lip‚ü©, exact ‚ü®‚ü®f, hf‚ÇÄ, hf_lip‚ü©, rfl‚ü© }
end

lemma map_t‚ÇÄ : f v.t‚ÇÄ = v.x‚ÇÄ := f.map_t‚ÇÄ'

protected lemma mem_closed_ball (t : Icc v.t_min v.t_max) : f t ‚àà closed_ball v.x‚ÇÄ v.R :=
calc dist (f t) v.x‚ÇÄ = dist (f t) (f.to_fun v.t‚ÇÄ) : by rw f.map_t‚ÇÄ'
                 ... ‚â§ v.C * dist t v.t‚ÇÄ          : f.lipschitz.dist_le_mul _ _
                 ... ‚â§ v.C * v.t_dist             : mul_le_mul_of_nonneg_left (v.dist_t‚ÇÄ_le _) v.C.2
                 ... ‚â§ v.R                        : v.is_pl.C_mul_le_R

/-- Given a curve $Œ≥ \colon [t_{\min}, t_{\max}] ‚Üí E$, `v_comp` is the function
$F(t)=v(œÄ t, Œ≥(œÄ t))$, where `œÄ` is the projection $‚Ñù ‚Üí [t_{\min}, t_{\max}]$. The integral of this
function is the image of `Œ≥` under the contracting map we are going to define below. -/
def v_comp (t : ‚Ñù) : E := v (v.proj t) (f (v.proj t))

lemma v_comp_apply_coe (t : Icc v.t_min v.t_max) : f.v_comp t = v t (f t) :=
by simp only [v_comp, proj_coe]

lemma continuous_v_comp : continuous f.v_comp :=
begin
  have := (continuous_subtype_coe.prod_mk f.continuous).comp v.continuous_proj,
  refine continuous_on.comp_continuous v.continuous_on this (Œª x, _),
  exact ‚ü®(v.proj x).2, f.mem_closed_ball _‚ü©
end

lemma norm_v_comp_le (t : ‚Ñù) : ‚Äñf.v_comp t‚Äñ ‚â§ v.C :=
v.norm_le (v.proj t).2 $ f.mem_closed_ball _

lemma dist_apply_le_dist (f‚ÇÅ f‚ÇÇ : fun_space v) (t : Icc v.t_min v.t_max) :
  dist (f‚ÇÅ t) (f‚ÇÇ t) ‚â§ dist f‚ÇÅ f‚ÇÇ :=
@continuous_map.dist_apply_le_dist _ _ _ _ _ f‚ÇÅ.to_continuous_map f‚ÇÇ.to_continuous_map _

lemma dist_le_of_forall {f‚ÇÅ f‚ÇÇ : fun_space v} {d : ‚Ñù} (h : ‚àÄ t, dist (f‚ÇÅ t) (f‚ÇÇ t) ‚â§ d) :
  dist f‚ÇÅ f‚ÇÇ ‚â§ d :=
(@continuous_map.dist_le_iff_of_nonempty _ _ _ _ _ f‚ÇÅ.to_continuous_map f‚ÇÇ.to_continuous_map _
  v.nonempty_Icc.to_subtype).2 h

instance [complete_space E] : complete_space v.fun_space :=
begin
  refine (complete_space_iff_is_complete_range
    uniform_inducing_to_continuous_map).2 (is_closed.is_complete _),
  rw [range_to_continuous_map, set_of_and],
  refine (is_closed_eq (continuous_map.continuous_eval_const _) continuous_const).inter _,
  have : is_closed {f : Icc v.t_min v.t_max ‚Üí E | lipschitz_with v.C f} :=
    is_closed_set_of_lipschitz_with v.C,
  exact this.preimage continuous_map.continuous_coe
end

lemma interval_integrable_v_comp (t‚ÇÅ t‚ÇÇ : ‚Ñù) :
  interval_integrable f.v_comp volume t‚ÇÅ t‚ÇÇ :=
(f.continuous_v_comp).interval_integrable _ _

variables [complete_space E]

/-- The Picard-Lindel√∂f operator. This is a contracting map on `picard_lindelof.fun_space v` such
that the fixed point of this map is the solution of the corresponding ODE.

More precisely, some iteration of this map is a contracting map. -/
def next (f : fun_space v) : fun_space v :=
{ to_fun := Œª t, v.x‚ÇÄ + ‚à´ œÑ : ‚Ñù in v.t‚ÇÄ..t, f.v_comp œÑ,
  map_t‚ÇÄ' := by rw [integral_same, add_zero],
  lipschitz' := lipschitz_with.of_dist_le_mul $ Œª t‚ÇÅ t‚ÇÇ,
    begin
      rw [dist_add_left, dist_eq_norm,
        integral_interval_sub_left (f.interval_integrable_v_comp _ _)
          (f.interval_integrable_v_comp _ _)],
      exact norm_integral_le_of_norm_le_const (Œª t ht, f.norm_v_comp_le _),
    end }

lemma next_apply (t : Icc v.t_min v.t_max) : f.next t = v.x‚ÇÄ + ‚à´ œÑ : ‚Ñù in v.t‚ÇÄ..t, f.v_comp œÑ := rfl

lemma has_deriv_within_at_next (t : Icc v.t_min v.t_max) :
  has_deriv_within_at (f.next ‚àò v.proj) (v t (f t)) (Icc v.t_min v.t_max) t :=
begin
  haveI : fact ((t : ‚Ñù) ‚àà Icc v.t_min v.t_max) := ‚ü®t.2‚ü©,
  simp only [(‚àò), next_apply],
  refine has_deriv_within_at.const_add _ _,
  have : has_deriv_within_at (Œª t : ‚Ñù, ‚à´ œÑ in v.t‚ÇÄ..t, f.v_comp œÑ) (f.v_comp t)
    (Icc v.t_min v.t_max) t,
    from integral_has_deriv_within_at_right (f.interval_integrable_v_comp _ _)
      (f.continuous_v_comp.strongly_measurable_at_filter _ _)
      f.continuous_v_comp.continuous_within_at,
  rw v_comp_apply_coe at this,
  refine this.congr_of_eventually_eq_of_mem _ t.coe_prop,
  filter_upwards [self_mem_nhds_within] with _ ht',
  rw v.proj_of_mem ht'
end

lemma dist_next_apply_le_of_le {f‚ÇÅ f‚ÇÇ : fun_space v} {n : ‚Ñï} {d : ‚Ñù}
  (h : ‚àÄ t, dist (f‚ÇÅ t) (f‚ÇÇ t) ‚â§ (v.L * |t - v.t‚ÇÄ|) ^ n / n! * d) (t : Icc v.t_min v.t_max) :
  dist (next f‚ÇÅ t) (next f‚ÇÇ t) ‚â§ (v.L * |t - v.t‚ÇÄ|) ^ (n + 1) / (n + 1)! * d :=
begin
  simp only [dist_eq_norm, next_apply, add_sub_add_left_eq_sub,
    ‚Üê interval_integral.integral_sub (interval_integrable_v_comp _ _ _)
      (interval_integrable_v_comp _ _ _), norm_integral_eq_norm_integral_Ioc] at *,
  calc ‚Äñ‚à´ œÑ in Œô (v.t‚ÇÄ : ‚Ñù) t, f‚ÇÅ.v_comp œÑ - f‚ÇÇ.v_comp œÑ‚Äñ
      ‚â§ ‚à´ œÑ in Œô (v.t‚ÇÄ : ‚Ñù) t, v.L * ((v.L * |œÑ - v.t‚ÇÄ|) ^ n / n! * d) :
    begin
      refine norm_integral_le_of_norm_le (continuous.integrable_on_uIoc _) _,
      { continuity },
      { refine (ae_restrict_mem measurable_set_Ioc).mono (Œª œÑ hœÑ, _),
        refine (v.lipschitz_on_with (v.proj œÑ).2).norm_sub_le_of_le
          (f‚ÇÅ.mem_closed_ball _) (f‚ÇÇ.mem_closed_ball _) ((h _).trans_eq _),
        rw v.proj_of_mem,
        exact (uIcc_subset_Icc v.t‚ÇÄ.2 t.2 $ Ioc_subset_Icc_self hœÑ) }
    end
  ... = (v.L * |t - v.t‚ÇÄ|) ^ (n + 1) / (n + 1)! * d : _,
  simp_rw [mul_pow, div_eq_mul_inv, mul_assoc, measure_theory.integral_mul_left,
    measure_theory.integral_mul_right, integral_pow_abs_sub_uIoc, div_eq_mul_inv,
    pow_succ (v.L : ‚Ñù), nat.factorial_succ, nat.cast_mul, nat.cast_succ, mul_inv, mul_assoc]
end

lemma dist_iterate_next_apply_le (f‚ÇÅ f‚ÇÇ : fun_space v) (n : ‚Ñï) (t : Icc v.t_min v.t_max) :
  dist (next^[n] f‚ÇÅ t) (next^[n] f‚ÇÇ t) ‚â§ (v.L * |t - v.t‚ÇÄ|) ^ n / n! * dist f‚ÇÅ f‚ÇÇ :=
begin
  induction n with n ihn generalizing t,
  { rw [pow_zero, nat.factorial_zero, nat.cast_one, div_one, one_mul],
    exact dist_apply_le_dist f‚ÇÅ f‚ÇÇ t },
  { rw [iterate_succ_apply', iterate_succ_apply'],
    exact dist_next_apply_le_of_le ihn _ }
end

lemma dist_iterate_next_le (f‚ÇÅ f‚ÇÇ : fun_space v) (n : ‚Ñï) :
  dist (next^[n] f‚ÇÅ) (next^[n] f‚ÇÇ) ‚â§ (v.L * v.t_dist) ^ n / n! * dist f‚ÇÅ f‚ÇÇ :=
begin
  refine dist_le_of_forall (Œª t, (dist_iterate_next_apply_le _ _ _ _).trans _),
  have : 0 ‚â§ dist f‚ÇÅ f‚ÇÇ := dist_nonneg,
  have : |(t - v.t‚ÇÄ : ‚Ñù)| ‚â§ v.t_dist := v.dist_t‚ÇÄ_le t,
  mono*; simp only [nat.cast_nonneg, mul_nonneg, nnreal.coe_nonneg, abs_nonneg, *]
end

end fun_space

variables [complete_space E]

section

lemma exists_contracting_iterate :
  ‚àÉ (N : ‚Ñï) K, contracting_with K ((fun_space.next : v.fun_space ‚Üí v.fun_space)^[N]) :=
begin
  rcases ((real.tendsto_pow_div_factorial_at_top (v.L * v.t_dist)).eventually
    (gt_mem_nhds zero_lt_one)).exists with ‚ü®N, hN‚ü©,
  have : (0 : ‚Ñù) ‚â§ (v.L * v.t_dist) ^ N / N!,
    from div_nonneg (pow_nonneg (mul_nonneg v.L.2 v.t_dist_nonneg) _) (nat.cast_nonneg _),
  exact ‚ü®N, ‚ü®_, this‚ü©, hN,
    lipschitz_with.of_dist_le_mul (Œª f g, fun_space.dist_iterate_next_le f g N)‚ü©
end

lemma exists_fixed : ‚àÉ f : v.fun_space, f.next = f :=
let ‚ü®N, K, hK‚ü© := exists_contracting_iterate v in ‚ü®_, hK.is_fixed_pt_fixed_point_iterate‚ü©

end

/-- Picard-Lindel√∂f (Cauchy-Lipschitz) theorem. Use
`exists_forall_deriv_within_Icc_eq_of_is_picard_lindelof` instead for the public API. -/
lemma exists_solution :
  ‚àÉ f : ‚Ñù ‚Üí E, f v.t‚ÇÄ = v.x‚ÇÄ ‚àß ‚àÄ t ‚àà Icc v.t_min v.t_max,
    has_deriv_within_at f (v t (f t)) (Icc v.t_min v.t_max) t :=
begin
  rcases v.exists_fixed with ‚ü®f, hf‚ü©,
  refine ‚ü®f ‚àò v.proj, _, Œª t ht, _‚ü©,
  { simp only [(‚àò), proj_coe, f.map_t‚ÇÄ] },
  { simp only [(‚àò), v.proj_of_mem ht],
    lift t to Icc v.t_min v.t_max using ht,
    simpa only [hf, v.proj_coe] using f.has_deriv_within_at_next t }
end

end picard_lindelof

lemma is_picard_lindelof.norm_le‚ÇÄ {E : Type*} [normed_add_comm_group E]
  {v : ‚Ñù ‚Üí E ‚Üí E} {t_min t‚ÇÄ t_max : ‚Ñù} {x‚ÇÄ : E} {C R : ‚Ñù} {L : ‚Ñù‚â•0}
  (hpl : is_picard_lindelof v t_min t‚ÇÄ t_max x‚ÇÄ L R C) : ‚Äñv t‚ÇÄ x‚ÇÄ‚Äñ ‚â§ C :=
hpl.norm_le t‚ÇÄ hpl.ht‚ÇÄ x‚ÇÄ $ mem_closed_ball_self hpl.hR

/-- Picard-Lindel√∂f (Cauchy-Lipschitz) theorem. -/
theorem exists_forall_deriv_within_Icc_eq_of_is_picard_lindelof
  [complete_space E] {v : ‚Ñù ‚Üí E ‚Üí E} {t_min t‚ÇÄ t_max : ‚Ñù} (x‚ÇÄ : E) {C R : ‚Ñù} {L : ‚Ñù‚â•0}
  (hpl : is_picard_lindelof v t_min t‚ÇÄ t_max x‚ÇÄ L R C) :
  ‚àÉ f : ‚Ñù ‚Üí E, f t‚ÇÄ = x‚ÇÄ ‚àß ‚àÄ t ‚àà Icc t_min t_max,
    has_deriv_within_at f (v t (f t)) (Icc t_min t_max) t :=
begin
  lift C to ‚Ñù‚â•0 using (norm_nonneg _).trans hpl.norm_le‚ÇÄ,
  lift t‚ÇÄ to Icc t_min t_max using hpl.ht‚ÇÄ,
  exact picard_lindelof.exists_solution
    ‚ü®v, t_min, t_max, t‚ÇÄ, x‚ÇÄ, C, ‚ü®R, hpl.hR‚ü©, L, { ht‚ÇÄ := t‚ÇÄ.property, ..hpl }‚ü©
end

variables [proper_space E] {v : E ‚Üí E} (t‚ÇÄ : ‚Ñù) (x‚ÇÄ : E)

/-- A time-independent, locally continuously differentiable ODE satisfies the hypotheses of the
  Picard-Lindel√∂f theorem. -/
lemma exists_is_picard_lindelof_const_of_cont_diff_on_nhds
  {s : set E} (hv : cont_diff_on ‚Ñù 1 v s) (hs : s ‚àà ùìù x‚ÇÄ) :
  ‚àÉ (Œµ > (0 : ‚Ñù)) L R C, is_picard_lindelof (Œª t, v) (t‚ÇÄ - Œµ) t‚ÇÄ (t‚ÇÄ + Œµ) x‚ÇÄ L R C :=
begin
  -- extract Lipschitz constant
  obtain ‚ü®L, s', hs', hlip‚ü© := cont_diff_at.exists_lipschitz_on_with
    ((hv.cont_diff_within_at (mem_of_mem_nhds hs)).cont_diff_at hs),
  -- radius of closed ball in which v is bounded
  obtain ‚ü®r, hr : 0 < r, hball‚ü© := metric.mem_nhds_iff.mp (inter_sets (ùìù x‚ÇÄ) hs hs'),
  have hr' := (half_pos hr).le,
  obtain ‚ü®C, hC‚ü© := (is_compact_closed_ball x‚ÇÄ (r / 2)).bdd_above_image -- uses proper_space E
    (hv.continuous_on.norm.mono (subset_inter_iff.mp
        ((closed_ball_subset_ball (half_lt_self hr)).trans hball)).left),
  have hC' : 0 ‚â§ C,
  { apply (norm_nonneg (v x‚ÇÄ)).trans,
    apply hC,
    exact ‚ü®x‚ÇÄ, ‚ü®mem_closed_ball_self hr', rfl‚ü©‚ü© },
  set Œµ := if C = 0 then 1 else (r / 2 / C) with hŒµ,
  have hŒµ0 : 0 < Œµ,
  { rw hŒµ,
    split_ifs,
    { exact zero_lt_one },
    { exact div_pos (half_pos hr) (lt_of_le_of_ne hC' (ne.symm h)) } },
  refine ‚ü®Œµ, hŒµ0, L, r / 2, C, _‚ü©,
  exact { ht‚ÇÄ := by {rw ‚Üêreal.closed_ball_eq_Icc, exact mem_closed_ball_self hŒµ0.le},
    hR := (half_pos hr).le,
    lipschitz := Œª t ht, hlip.mono (subset_inter_iff.mp
      (subset_trans (closed_ball_subset_ball (half_lt_self hr)) hball)).2,
    cont := Œª x hx, continuous_on_const,
    norm_le := Œª t ht x hx, hC ‚ü®x, hx, rfl‚ü©,
    C_mul_le_R := begin
      rw [add_sub_cancel', sub_sub_cancel, max_self, mul_ite, mul_one],
      split_ifs,
      { rwa ‚Üê h at hr' },
      { exact (mul_div_cancel' (r / 2) h).le }
    end }
end

/-- A time-independent, locally continuously differentiable ODE admits a solution in some open
interval. -/
theorem exists_forall_deriv_at_Ioo_eq_of_cont_diff_on_nhds
  {s : set E} (hv : cont_diff_on ‚Ñù 1 v s) (hs : s ‚àà ùìù x‚ÇÄ) :
  ‚àÉ (Œµ > (0 : ‚Ñù)) (f : ‚Ñù ‚Üí E), f t‚ÇÄ = x‚ÇÄ ‚àß
    ‚àÄ t ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ), f t ‚àà s ‚àß has_deriv_at f (v (f t)) t :=
begin
  obtain ‚ü®Œµ, hŒµ, L, R, C, hpl‚ü© := exists_is_picard_lindelof_const_of_cont_diff_on_nhds t‚ÇÄ x‚ÇÄ hv hs,
  obtain ‚ü®f, hf1, hf2‚ü© := exists_forall_deriv_within_Icc_eq_of_is_picard_lindelof x‚ÇÄ hpl,
  have hf2' : ‚àÄ t ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ), has_deriv_at f (v (f t)) t :=
    Œª t ht, (hf2 t (Ioo_subset_Icc_self ht)).has_deriv_at (Icc_mem_nhds ht.1 ht.2),
  have h : (f ‚Åª¬π' s) ‚àà ùìù t‚ÇÄ,
  { have := (hf2' t‚ÇÄ (mem_Ioo.mpr ‚ü®sub_lt_self _ hŒµ, lt_add_of_pos_right _ hŒµ‚ü©)),
    apply continuous_at.preimage_mem_nhds this.continuous_at,
    rw hf1,
    exact hs },
  rw metric.mem_nhds_iff at h,
  obtain ‚ü®r, hr1, hr2‚ü© := h,
  refine ‚ü®min r Œµ, lt_min hr1 hŒµ, f, hf1, Œª t ht,
    ‚ü®_, hf2' t (mem_of_mem_of_subset ht (Ioo_subset_Ioo
      (sub_le_sub_left (min_le_right _ _) _) (add_le_add_left (min_le_right _ _) _)))‚ü©‚ü©,
  rw ‚Üêset.mem_preimage,
  apply set.mem_of_mem_of_subset _ hr2,
  apply set.mem_of_mem_of_subset ht,
  rw ‚Üêreal.ball_eq_Ioo,
  exact (metric.ball_subset_ball (min_le_left _ _))
end

/-- A time-independent, continuously differentiable ODE admits a solution in some open interval. -/
theorem exists_forall_deriv_at_Ioo_eq_of_cont_diff
  (hv : cont_diff ‚Ñù 1 v) : ‚àÉ (Œµ > (0 : ‚Ñù)) (f : ‚Ñù ‚Üí E), f t‚ÇÄ = x‚ÇÄ ‚àß
    ‚àÄ t ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ), has_deriv_at f (v (f t)) t :=
let ‚ü®Œµ, hŒµ, f, hf1, hf2‚ü© := exists_forall_deriv_at_Ioo_eq_of_cont_diff_on_nhds t‚ÇÄ x‚ÇÄ hv.cont_diff_on
  (is_open.mem_nhds is_open_univ (mem_univ _)) in ‚ü®Œµ, hŒµ, f, hf1, Œª t ht, (hf2 t ht).2‚ü©
