/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.calculus.formal_multilinear_series
import analysis.specific_limits

/-!
# Analytic functions

A function is analytic in one dimension around `0` if it can be written as a converging power series
`Î£ pâ‚™ zâ¿`. This definition can be extended to any dimension (even in infinite dimension) by
requiring that `pâ‚™` is a continuous `n`-multilinear map. In general, `pâ‚™` is not unique (in two
dimensions, taking `pâ‚‚ (x, y) (x', y') = x y'` or `y x'` gives the same map when applied to a
vector `(x, y) (x, y)`). A way to guarantee uniqueness is to take a symmetric `pâ‚™`, but this is not
always possible in nonzero characteristic (in characteristic 2, the previous example has no
symmetric representative). Therefore, we do not insist on symmetry or uniqueness in the definition,
and we only require the existence of a converging series.

The general framework is important to say that the exponential map on bounded operators on a Banach
space is analytic, as well as the inverse on invertible operators.

## Main definitions

Let `p` be a formal multilinear series from `E` to `F`, i.e., `p n` is a multilinear map on `E^n`
for `n : â„•`.

* `p.radius`: the largest `r : ennreal` such that `âˆ¥p nâˆ¥ * r^n` grows subexponentially, defined as
  a liminf.
* `p.le_radius_of_bound`, `p.bound_of_lt_radius`, `p.geometric_bound_of_lt_radius`: relating the
  value of the radius with the growth of `âˆ¥p nâˆ¥ * r^n`.
* `p.partial_sum n x`: the sum `âˆ‘_{i = 0}^{n-1} páµ¢ xâ±`.
* `p.sum x`: the sum `âˆ‘'_{i = 0}^{âˆ} páµ¢ xâ±`.

Additionally, let `f` be a function from `E` to `F`.

* `has_fpower_series_on_ball f p x r`: on the ball of center `x` with radius `r`,
  `f (x + y) = âˆ‘'_n pâ‚™ yâ¿`.
* `has_fpower_series_at f p x`: on some ball of center `x` with positive radius, holds
  `has_fpower_series_on_ball f p x r`.
* `analytic_at ğ•œ f x`: there exists a power series `p` such that holds
  `has_fpower_series_at f p x`.

We develop the basic properties of these notions, notably:
* If a function admits a power series, it is continuous (see
  `has_fpower_series_on_ball.continuous_on` and `has_fpower_series_at.continuous_at` and
  `analytic_at.continuous_at`).
* In a complete space, the sum of a formal power series with positive radius is well defined on the
  disk of convergence, see `formal_multilinear_series.has_fpower_series_on_ball`.
* If a function admits a power series in a ball, then it is analytic at any point `y` of this ball,
  and the power series there can be expressed in terms of the initial power series `p` as
  `p.change_origin y`. See `has_fpower_series_on_ball.change_origin`. It follows in particular that
  the set of points at which a given function is analytic is open, see `is_open_analytic_at`.

## Implementation details

We only introduce the radius of convergence of a power series, as `p.radius`.
For a power series in finitely many dimensions, there is a finer (directional, coordinate-dependent)
notion, describing the polydisk of convergence. This notion is more specific, and not necessary to
build the general theory. We do not define it here.
-/

noncomputable theory

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{F : Type*} [normed_group F] [normed_space ğ•œ F]
{G : Type*} [normed_group G] [normed_space ğ•œ G]

open_locale topological_space classical big_operators nnreal
open set filter asymptotics

/-! ### The radius of a formal multilinear series -/

namespace formal_multilinear_series

/-- The radius of a formal multilinear series is the largest `r` such that the sum `Î£ pâ‚™ yâ¿`
converges for all `âˆ¥yâˆ¥ < r`. -/
def radius (p : formal_multilinear_series ğ•œ E F) : ennreal :=
â¨† (r : â„â‰¥0) (hr : âˆƒ C, âˆ€ n, nnnorm (p n) * r ^ n â‰¤ C), (r : ennreal)

/-- If `âˆ¥pâ‚™âˆ¥ râ¿` is bounded in `n`, then the radius of `p` is at least `r`. -/
lemma le_radius_of_bound (p : formal_multilinear_series ğ•œ E F) (C : â„â‰¥0) {r : â„â‰¥0}
  (h : âˆ€ (n : â„•), nnnorm (p n) * r^n â‰¤ C) : (r : ennreal) â‰¤ p.radius :=
le_supr_of_le r (le_supr (Î» _, (r : ennreal)) $ Exists.intro C h)

/-- If `âˆ¥pâ‚™âˆ¥ râ¿` is bounded in `n`, then the radius of `p` is at least `r`. -/
lemma le_radius_of_is_O (p : formal_multilinear_series ğ•œ E F) {r : â„â‰¥0}
  (h : is_O (Î» n, âˆ¥p nâˆ¥ * r^n) (Î» n, (1 : â„)) at_top) : (r : ennreal) â‰¤ p.radius :=
begin
  rcases is_O_one_nat_at_top_iff.1 h with âŸ¨C, hCâŸ©,
  refine p.le_radius_of_bound (nnreal.of_real C)
    (Î» n, nnreal.coe_le_coe.1 (le_trans _ $ nnreal.le_coe_of_real _)),
  simpa using hC n
end

/-- For `r` strictly smaller than the radius of `p`, then `âˆ¥pâ‚™âˆ¥ râ¿` tends to zero exponentially. -/
lemma geometric_bound_of_lt_radius (p : formal_multilinear_series ğ•œ E F) {r : â„â‰¥0}
  (h : (r : ennreal) < p.radius) : âˆƒ (a < 1) C, âˆ€ n, nnnorm (p n) * r^n â‰¤ C * a^n :=
begin
  simp only [radius, lt_supr_iff] at h,
  rcases h with âŸ¨t, âŸ¨C, hCâŸ©, rtâŸ©,
  rw ennreal.coe_lt_coe at rt,
  refine âŸ¨r / t, nnreal.div_lt_one_of_lt rt, C, Î» n, _âŸ©,
  have tpos : t â‰  0 := ne_of_gt (lt_of_le_of_lt (zero_le _) rt),
  calc nnnorm (p n) * r ^ n = (nnnorm (p n) * t ^ n) * (r / t) ^ n :
    by field_simp [tpos, mul_right_comm]
  ... â‰¤ C * (r / t) ^ n : mul_le_mul_of_nonneg_right (hC n) (zero_le _)
end

lemma lt_radius_tfae' (p : formal_multilinear_series ğ•œ E F) (r : â„â‰¥0) :
  tfae [âˆƒ (a < 1) C, âˆ€ n, nnnorm (p n) * r ^ n â‰¤ C * a ^ n,
    âˆƒ (a âˆˆ Ioo (0 : â„â‰¥0) 1) (C > 0), âˆ€ n, nnnorm (p n) * r ^ n â‰¤ C * a ^ n,
    âˆƒ a < 1, âˆ€á¶  n in at_top, nnnorm (p n) * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„â‰¥0) 1, âˆ€á¶  n in at_top, nnnorm (p n) * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„â‰¥0) 1, âˆ€ C > 0, âˆ€á¶  n in at_top, nnnorm (p n) * r ^ n â‰¤ C * a ^ n,
    âˆƒ (a < 1) C, âˆ€ n, âˆ¥p nâˆ¥ * r ^ n â‰¤ C * a ^ n,
    âˆƒ (a âˆˆ Ioo (0 : â„) 1) (C > 0), âˆ€ n, âˆ¥p nâˆ¥ * r ^ n â‰¤ C * a ^ n,
    âˆƒ a < 1, âˆ€á¶  n in at_top, âˆ¥p nâˆ¥ * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆ€á¶  n in at_top, âˆ¥p nâˆ¥ * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„) 1, is_O (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top,
    âˆƒ a âˆˆ Ioo (-1 : â„) 1, is_O (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top,
    âˆƒ a âˆˆ Ioo (0 : â„) 1, is_o (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top,
    âˆƒ a âˆˆ Ioo (-1 : â„) 1, is_o (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top] :=
begin
  /- 6-13 come from `tfae_exists_lt_is_o_pow (Î» n, âˆ¥p nâˆ¥ * r ^ n) 1`. -/
  have H := tfae_exists_lt_is_o_pow (Î» n, âˆ¥p nâˆ¥ * r ^ n) 1,
  have A : âˆ€ n, abs (âˆ¥p nâˆ¥ * r ^ n) = âˆ¥p nâˆ¥ * r ^ n := Î» n, by simp [abs_mul],
  tfae_have : 6 â†” 7, by simpa only [exists_prop, or_true, true_and, zero_lt_one, A] using H.out 4 5,
  tfae_have : 7 â†” 8, by simpa only [A] using H.out 5 6,
  tfae_have : 8 â†” 9, by simpa only [A] using H.out 6 7,
  tfae_have : 9 â†” 10, by simpa only [A] using H.out 7 3,
  tfae_have : 10 â†” 11, from H.out 3 2,
  tfae_have : 11 â†” 12, from H.out 2 1,
  tfae_have : 12 â†” 13, from H.out 1 0,
  /- Add 1 and 2 -/
  tfae_have : 1 â†’ 6,
  { rintro âŸ¨a, ha, C, HâŸ©, exact âŸ¨a, ha, C, by exact_mod_cast HâŸ© },
  tfae_have : 7 â†’ 2,
  { rintro âŸ¨a, ha, C, hC, HâŸ©,
    lift C to â„â‰¥0 using hC.lt.le, lift a to â„â‰¥0 using ha.1.le,
    refine âŸ¨a, ha, C, hC, Î» n, _âŸ©,
    exact_mod_cast H n },
  tfae_have : 2 â†’ 1, { rintro âŸ¨a, ha, C, hC, HâŸ©, exact âŸ¨a, ha.2, C, HâŸ© },
  /- Add 3, 4, and 5 -/
  tfae_have : 3 â†’ 8, { rintro âŸ¨a, ha, HâŸ©, refine âŸ¨a, ha, _âŸ©, exact_mod_cast H },
  tfae_have : 12 â†’ 5,
  { rintro âŸ¨a, ha, HâŸ©,
    lift a to â„â‰¥0 using ha.1.le,
    refine âŸ¨a, ha, Î» C hC, (H.def hC).mono $ Î» n hn, nnreal.coe_le_coe.1 _âŸ©,
    rw [real.norm_eq_abs, A, real.norm_eq_abs] at hn,
    simpa using hn },
  tfae_have : 5 â†’ 4, from Î» âŸ¨a, ha, HâŸ©, âŸ¨a, ha, by simpa only [one_mul] using H 1 zero_lt_oneâŸ©,
  tfae_have : 4 â†’ 3, { rintro âŸ¨a, ha, HâŸ©, exact âŸ¨a, ha.2, HâŸ© },
  tfae_finish
end

lemma lt_radius_tfae (p : formal_multilinear_series ğ•œ E F) {r : â„â‰¥0} (hr : r â‰  0) :
  tfae [â†‘r < p.radius,
    âˆƒ (a < 1) C, âˆ€ n, nnnorm (p n) * r ^ n â‰¤ C * a ^ n,
    âˆƒ (a âˆˆ Ioo (0 : â„â‰¥0) 1) (C > 0), âˆ€ n, nnnorm (p n) * r ^ n â‰¤ C * a ^ n,
    âˆƒ a < 1, âˆ€á¶  n in at_top, nnnorm (p n) * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„â‰¥0) 1, âˆ€á¶  n in at_top, nnnorm (p n) * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„â‰¥0) 1, âˆ€ C > 0, âˆ€á¶  n in at_top, nnnorm (p n) * r ^ n â‰¤ C * a ^ n,
    âˆƒ (a < 1) C, âˆ€ n, âˆ¥p nâˆ¥ * r ^ n â‰¤ C * a ^ n,
    âˆƒ (a âˆˆ Ioo (0 : â„) 1) (C > 0), âˆ€ n, âˆ¥p nâˆ¥ * r ^ n â‰¤ C * a ^ n,
    âˆƒ a < 1, âˆ€á¶  n in at_top, âˆ¥p nâˆ¥ * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆ€á¶  n in at_top, âˆ¥p nâˆ¥ * r ^ n â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo (0 : â„) 1, is_O (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top,
    âˆƒ a âˆˆ Ioo (-1 : â„) 1, is_O (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top,
    âˆƒ a âˆˆ Ioo (0 : â„) 1, is_o (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top,
    âˆƒ a âˆˆ Ioo (-1 : â„) 1, is_o (Î» n, âˆ¥p nâˆ¥ * r ^ n) (pow a) at_top] :=
begin
  refine (list.tfae_cons_of_mem _).2 âŸ¨_, p.lt_radius_tfae' râŸ©,
  show _ âˆˆ _, from or.inl rfl,
  refine âŸ¨p.geometric_bound_of_lt_radius, Î» h, _âŸ©,
  rcases ((p.lt_radius_tfae' r).out 0 1).1 h with âŸ¨a, ha, C, hC, HâŸ©,
  simp only [â† nnreal.div_le_iff (pow_ne_zero _ ha.1.ne'), mul_div_assoc, â† div_pow] at H,
  refine lt_of_lt_of_le _ (p.le_radius_of_bound C H),
  rw [ennreal.coe_lt_coe, nnreal.lt_div_iff ha.1.ne'],
  calc r * a < r * 1 : mul_lt_mul_of_pos_left ha.2 (zero_lt_iff_ne_zero.2 hr)
  ... = r : mul_one r
end

/-- For `r` strictly smaller than the radius of `p`, then `âˆ¥pâ‚™âˆ¥ râ¿` is bounded. -/
lemma bound_of_lt_radius (p : formal_multilinear_series ğ•œ E F) {r : â„â‰¥0}
  (h : (r : ennreal) < p.radius) : âˆƒ (C : â„â‰¥0), âˆ€ n, nnnorm (p n) * r^n â‰¤ C :=
let âŸ¨a, ha, C, hâŸ© := p.geometric_bound_of_lt_radius h
in âŸ¨C, Î» n, (h n).trans $ mul_le_of_le_one_right (zero_le _) (pow_le_one _ (zero_le _) ha.le)âŸ©

/-- The radius of the sum of two formal series is at least the minimum of their two radii. -/
lemma min_radius_le_radius_add (p q : formal_multilinear_series ğ•œ E F) :
  min p.radius q.radius â‰¤ (p + q).radius :=
begin
  refine le_of_forall_ge_of_dense (Î» r hr, _),
  lift r to â„â‰¥0 using (ne_top_of_lt hr),
  obtain âŸ¨Cp, hCpâŸ© : âˆƒ (C : â„â‰¥0), âˆ€ n, nnnorm (p n) * r^n â‰¤ C :=
    p.bound_of_lt_radius (lt_of_lt_of_le hr (min_le_left _ _)),
  obtain âŸ¨Cq, hCqâŸ© : âˆƒ (C : â„â‰¥0), âˆ€ n, nnnorm (q n) * r^n â‰¤ C :=
    q.bound_of_lt_radius (lt_of_lt_of_le hr (min_le_right _ _)),
  refine (p + q).le_radius_of_bound (Cp + Cq) (Î» n, _),
  calc nnnorm (p n + q n) * r ^ n â‰¤ (nnnorm (p n) + nnnorm (q n)) * r ^ n :
    mul_le_mul_of_nonneg_right (nnnorm_add_le (p n) (q n)) (zero_le (r ^ n))
  ... â‰¤ Cp + Cq : by { rw add_mul, exact add_le_add (hCp n) (hCq n) }
end

lemma radius_neg (p : formal_multilinear_series ğ•œ E F) : (-p).radius = p.radius :=
by simp [formal_multilinear_series.radius, nnnorm_neg]

/-- Given a formal multilinear series `p` and a vector `x`, then `p.sum x` is the sum `Î£ pâ‚™ xâ¿`. A
priori, it only behaves well when `âˆ¥xâˆ¥ < p.radius`. -/
protected def sum (p : formal_multilinear_series ğ•œ E F) (x : E) : F := âˆ‘' n : â„• , p n (Î» i, x)

/-- Given a formal multilinear series `p` and a vector `x`, then `p.partial_sum n x` is the sum
`Î£ pâ‚– xáµ` for `k âˆˆ {0,..., n-1}`. -/
def partial_sum (p : formal_multilinear_series ğ•œ E F) (n : â„•) (x : E) : F :=
âˆ‘ k in finset.range n, p k (Î»(i : fin k), x)

/-- The partial sums of a formal multilinear series are continuous. -/
lemma partial_sum_continuous (p : formal_multilinear_series ğ•œ E F) (n : â„•) :
  continuous (p.partial_sum n) :=
by continuity

end formal_multilinear_series


/-! ### Expanding a function as a power series -/
section

variables {f g : E â†’ F} {p pf pg : formal_multilinear_series ğ•œ E F} {x : E} {r r' : ennreal}

/-- Given a function `f : E â†’ F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series on the ball of radius `r > 0` around `x` if `f (x + y) = âˆ‘' pâ‚™ yâ¿` for all `âˆ¥yâˆ¥ < r`. -/
structure has_fpower_series_on_ball
  (f : E â†’ F) (p : formal_multilinear_series ğ•œ E F) (x : E) (r : ennreal) : Prop :=
(r_le    : r â‰¤ p.radius)
(r_pos   : 0 < r)
(has_sum : âˆ€ {y}, y âˆˆ emetric.ball (0 : E) r â†’ has_sum (Î»n:â„•, p n (Î»(i : fin n), y)) (f (x + y)))

/-- Given a function `f : E â†’ F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series around `x` if `f (x + y) = âˆ‘' pâ‚™ yâ¿` for all `y` in a neighborhood of `0`. -/
def has_fpower_series_at (f : E â†’ F) (p : formal_multilinear_series ğ•œ E F) (x : E) :=
âˆƒ r, has_fpower_series_on_ball f p x r

variable (ğ•œ)
/-- Given a function `f : E â†’ F`, we say that `f` is analytic at `x` if it admits a convergent power
series expansion around `x`. -/
def analytic_at (f : E â†’ F) (x : E) :=
âˆƒ (p : formal_multilinear_series ğ•œ E F), has_fpower_series_at f p x

variable {ğ•œ}

lemma has_fpower_series_on_ball.has_fpower_series_at (hf : has_fpower_series_on_ball f p x r) :
  has_fpower_series_at f p x := âŸ¨r, hfâŸ©

lemma has_fpower_series_at.analytic_at (hf : has_fpower_series_at f p x) : analytic_at ğ•œ f x :=
âŸ¨p, hfâŸ©

lemma has_fpower_series_on_ball.analytic_at (hf : has_fpower_series_on_ball f p x r) :
  analytic_at ğ•œ f x :=
hf.has_fpower_series_at.analytic_at

lemma has_fpower_series_on_ball.radius_pos (hf : has_fpower_series_on_ball f p x r) :
  0 < p.radius :=
lt_of_lt_of_le hf.r_pos hf.r_le

lemma has_fpower_series_at.radius_pos (hf : has_fpower_series_at f p x) :
  0 < p.radius :=
let âŸ¨r, hrâŸ© := hf in hr.radius_pos

lemma has_fpower_series_on_ball.mono
  (hf : has_fpower_series_on_ball f p x r) (r'_pos : 0 < r') (hr : r' â‰¤ r) :
  has_fpower_series_on_ball f p x r' :=
âŸ¨le_trans hr hf.1, r'_pos, Î» y hy, hf.has_sum (emetric.ball_subset_ball hr hy)âŸ©

lemma has_fpower_series_on_ball.add
  (hf : has_fpower_series_on_ball f pf x r) (hg : has_fpower_series_on_ball g pg x r) :
  has_fpower_series_on_ball (f + g) (pf + pg) x r :=
{ r_le := le_trans (le_min_iff.2 âŸ¨hf.r_le, hg.r_leâŸ©) (pf.min_radius_le_radius_add pg),
  r_pos := hf.r_pos,
  has_sum := Î» y hy, (hf.has_sum hy).add (hg.has_sum hy) }

lemma has_fpower_series_at.add
  (hf : has_fpower_series_at f pf x) (hg : has_fpower_series_at g pg x) :
  has_fpower_series_at (f + g) (pf + pg) x :=
begin
  rcases hf with âŸ¨rf, hrfâŸ©,
  rcases hg with âŸ¨rg, hrgâŸ©,
  have P : 0 < min rf rg, by simp [hrf.r_pos, hrg.r_pos],
  exact âŸ¨min rf rg, (hrf.mono P (min_le_left _ _)).add (hrg.mono P (min_le_right _ _))âŸ©
end

lemma analytic_at.add (hf : analytic_at ğ•œ f x) (hg : analytic_at ğ•œ g x) :
  analytic_at ğ•œ (f + g) x :=
let âŸ¨pf, hpfâŸ© := hf, âŸ¨qf, hqfâŸ© := hg in (hpf.add hqf).analytic_at

lemma has_fpower_series_on_ball.neg (hf : has_fpower_series_on_ball f pf x r) :
  has_fpower_series_on_ball (-f) (-pf) x r :=
{ r_le    := by { rw pf.radius_neg, exact hf.r_le },
  r_pos   := hf.r_pos,
  has_sum := Î» y hy, (hf.has_sum hy).neg }

lemma has_fpower_series_at.neg
  (hf : has_fpower_series_at f pf x) : has_fpower_series_at (-f) (-pf) x :=
let âŸ¨rf, hrfâŸ© := hf in hrf.neg.has_fpower_series_at

lemma analytic_at.neg (hf : analytic_at ğ•œ f x) : analytic_at ğ•œ (-f) x :=
let âŸ¨pf, hpfâŸ© := hf in hpf.neg.analytic_at

lemma has_fpower_series_on_ball.sub
  (hf : has_fpower_series_on_ball f pf x r) (hg : has_fpower_series_on_ball g pg x r) :
  has_fpower_series_on_ball (f - g) (pf - pg) x r :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

lemma has_fpower_series_at.sub
  (hf : has_fpower_series_at f pf x) (hg : has_fpower_series_at g pg x) :
  has_fpower_series_at (f - g) (pf - pg) x :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

lemma analytic_at.sub (hf : analytic_at ğ•œ f x) (hg : analytic_at ğ•œ g x) :
  analytic_at ğ•œ (f - g) x :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

lemma has_fpower_series_on_ball.coeff_zero (hf : has_fpower_series_on_ball f pf x r)
  (v : fin 0 â†’ E) : pf 0 v = f x :=
begin
  have v_eq : v = (Î» i, 0), by { ext i, apply fin_zero_elim i },
  have zero_mem : (0 : E) âˆˆ emetric.ball (0 : E) r, by simp [hf.r_pos],
  have : âˆ€ i â‰  0, pf i (Î» j, 0) = 0,
  { assume i hi,
    have : 0 < i := bot_lt_iff_ne_bot.mpr hi,
    apply continuous_multilinear_map.map_coord_zero _ (âŸ¨0, thisâŸ© : fin i),
    refl },
  have A := (hf.has_sum zero_mem).unique (has_sum_single _ this),
  simpa [v_eq] using A.symm,
end

lemma has_fpower_series_at.coeff_zero (hf : has_fpower_series_at f pf x) (v : fin 0 â†’ E) :
  pf 0 v = f x :=
let âŸ¨rf, hrfâŸ© := hf in hrf.coeff_zero v

/-- If a function admits a power series expansion, then it is exponentially close to the partial
sums of this power series on strict subdisks of the disk of convergence. -/
lemma has_fpower_series_on_ball.uniform_geometric_approx {r' : â„â‰¥0}
  (hf : has_fpower_series_on_ball f p x r) (h : (r' : ennreal) < r) :
  âˆƒ (a C : â„â‰¥0), a < 1 âˆ§ (âˆ€ y âˆˆ metric.ball (0 : E) r', âˆ€ n,
  âˆ¥f (x + y) - p.partial_sum n yâˆ¥ â‰¤ C * a ^ n) :=
begin
  obtain âŸ¨a, ha, C, hCâŸ© : âˆƒ (a < 1) C, âˆ€ n, nnnorm (p n) * r' ^n â‰¤ C * a^n :=
    p.geometric_bound_of_lt_radius (h.trans_le hf.r_le),
  refine âŸ¨a, C / (1 - a), ha, Î» y hy n, _âŸ©,
  have yr' : âˆ¥yâˆ¥ < r', by { rw ball_0_eq at hy, exact hy },
  have : y âˆˆ emetric.ball (0 : E) r,
  { rw [emetric.mem_ball, edist_eq_coe_nnnorm],
    apply lt_trans _ h,
    exact_mod_cast yr' },
  push_cast [ha.le],
  rw [norm_sub_rev, â† mul_div_right_comm],
  apply norm_sub_le_of_geometric_bound_of_has_sum ha _ (hf.has_sum this),
  assume n,
  calc âˆ¥(p n) (Î» (i : fin n), y)âˆ¥ â‰¤ âˆ¥p nâˆ¥ * (âˆ i : fin n, âˆ¥yâˆ¥) :
      continuous_multilinear_map.le_op_norm _ _
    ... = nnnorm (p n) * (nnnorm y)^n : by simp
    ... â‰¤ nnnorm (p n) * r' ^ n :
      mul_le_mul_of_nonneg_left (pow_le_pow_of_le_left (nnreal.coe_nonneg _) (le_of_lt yr') _)
      (nnreal.coe_nonneg _)
    ... â‰¤ C * a ^ n : by exact_mod_cast hC n,
end

/-- If a function admits a power series expansion at `x`, then it is the uniform limit of the
partial sums of this power series on strict subdisks of the disk of convergence, i.e., `f (x + y)`
is the uniform limit of `p.partial_sum n y` there. -/
lemma has_fpower_series_on_ball.tendsto_uniformly_on {r' : â„â‰¥0}
  (hf : has_fpower_series_on_ball f p x r) (h : (r' : ennreal) < r) :
  tendsto_uniformly_on (Î» n y, p.partial_sum n y)
    (Î» y, f (x + y)) at_top (metric.ball (0 : E) r') :=
begin
  rcases hf.uniform_geometric_approx h with âŸ¨a, C, ha, hCâŸ©,
  refine metric.tendsto_uniformly_on_iff.2 (Î» Îµ Îµpos, _),
  have L : tendsto (Î» n, (C : â„) * a^n) at_top (ğ“ ((C : â„) * 0)) :=
    tendsto_const_nhds.mul (tendsto_pow_at_top_nhds_0_of_lt_1 a.2 ha),
  rw mul_zero at L,
  refine (L.eventually (gt_mem_nhds Îµpos)).mono (Î» n hn y hy, _),
  rw dist_eq_norm,
  exact (hC y hy n).trans_lt hn
end

/-- If a function admits a power series expansion at `x`, then it is the locally uniform limit of
the partial sums of this power series on the disk of convergence, i.e., `f (x + y)`
is the locally uniform limit of `p.partial_sum n y` there. -/
lemma has_fpower_series_on_ball.tendsto_locally_uniformly_on
  (hf : has_fpower_series_on_ball f p x r) :
  tendsto_locally_uniformly_on (Î» n y, p.partial_sum n y) (Î» y, f (x + y))
    at_top (emetric.ball (0 : E) r) :=
begin
  assume u hu x hx,
  rcases ennreal.lt_iff_exists_nnreal_btwn.1 hx with âŸ¨r', xr', hr'âŸ©,
  have : emetric.ball (0 : E) r' âˆˆ ğ“ x :=
    mem_nhds_sets emetric.is_open_ball xr',
  refine âŸ¨emetric.ball (0 : E) r', mem_nhds_within_of_mem_nhds this, _âŸ©,
  simpa [metric.emetric_ball_nnreal] using hf.tendsto_uniformly_on hr' u hu
end

/-- If a function admits a power series expansion at `x`, then it is the uniform limit of the
partial sums of this power series on strict subdisks of the disk of convergence, i.e., `f y`
is the uniform limit of `p.partial_sum n (y - x)` there. -/
lemma has_fpower_series_on_ball.tendsto_uniformly_on' {r' : â„â‰¥0}
  (hf : has_fpower_series_on_ball f p x r) (h : (r' : ennreal) < r) :
  tendsto_uniformly_on (Î» n y, p.partial_sum n (y - x)) f at_top (metric.ball (x : E) r') :=
begin
  convert (hf.tendsto_uniformly_on h).comp (Î» y, y - x),
  { ext z, simp },
  { ext z, simp [dist_eq_norm] }
end

/-- If a function admits a power series expansion at `x`, then it is the locally uniform limit of
the  partial sums of this power series on the disk of convergence, i.e., `f y`
is the locally uniform limit of `p.partial_sum n (y - x)` there. -/
lemma has_fpower_series_on_ball.tendsto_locally_uniformly_on'
  (hf : has_fpower_series_on_ball f p x r) :
  tendsto_locally_uniformly_on (Î» n y, p.partial_sum n (y - x)) f at_top (emetric.ball (x : E) r) :=
begin
  have A : continuous_on (Î» (y : E), y - x) (emetric.ball (x : E) r) :=
    (continuous_id.sub continuous_const).continuous_on,
  convert (hf.tendsto_locally_uniformly_on).comp (Î» (y : E), y - x) _ A,
  { ext z, simp },
  { assume z, simp [edist_eq_coe_nnnorm, edist_eq_coe_nnnorm_sub] }
end

/-- If a function admits a power series expansion on a disk, then it is continuous there. -/
lemma has_fpower_series_on_ball.continuous_on
  (hf : has_fpower_series_on_ball f p x r) : continuous_on f (emetric.ball x r) :=
hf.tendsto_locally_uniformly_on'.continuous_on $ Î» n,
  ((p.partial_sum_continuous n).comp (continuous_id.sub continuous_const)).continuous_on

lemma has_fpower_series_at.continuous_at (hf : has_fpower_series_at f p x) : continuous_at f x :=
let âŸ¨r, hrâŸ© := hf in hr.continuous_on.continuous_at (emetric.ball_mem_nhds x (hr.r_pos))

lemma analytic_at.continuous_at (hf : analytic_at ğ•œ f x) : continuous_at f x :=
let âŸ¨p, hpâŸ© := hf in hp.continuous_at

/-- In a complete space, the sum of a converging power series `p` admits `p` as a power series.
This is not totally obvious as we need to check the convergence of the series. -/
lemma formal_multilinear_series.has_fpower_series_on_ball [complete_space F]
  (p : formal_multilinear_series ğ•œ E F) (h : 0 < p.radius) :
  has_fpower_series_on_ball p.sum p 0 p.radius :=
{ r_le    := le_refl _,
  r_pos   := h,
  has_sum := Î» y hy, begin
    rw zero_add,
    replace hy : (nnnorm y : ennreal) < p.radius,
      by { convert hy, exact (edist_eq_coe_nnnorm _).symm },
    obtain âŸ¨a, ha, C, hCâŸ© : âˆƒ (a < 1) C, âˆ€ n, nnnorm (p n) * (nnnorm y)^n â‰¤ C * a^n :=
      p.geometric_bound_of_lt_radius hy,
    refine (summable_of_norm_bounded (Î» n, (C : â„) * a ^ n)
      ((summable_geometric_of_lt_1 a.2 ha).mul_left _) (Î» n, _)).has_sum,
    calc âˆ¥(p n) (Î» (i : fin n), y)âˆ¥
      â‰¤ âˆ¥p nâˆ¥ * (âˆ i : fin n, âˆ¥yâˆ¥) : continuous_multilinear_map.le_op_norm _ _
      ... = nnnorm (p n) * (nnnorm y)^n : by simp
      ... â‰¤ C * a ^ n : by exact_mod_cast hC n
  end }

lemma has_fpower_series_on_ball.sum [complete_space F] (h : has_fpower_series_on_ball f p x r)
  {y : E} (hy : y âˆˆ emetric.ball (0 : E) r) : f (x + y) = p.sum y :=
begin
  have A := h.has_sum hy,
  have B := (p.has_fpower_series_on_ball h.radius_pos).has_sum (lt_of_lt_of_le hy h.r_le),
  simpa using A.unique B
end

/-- The sum of a converging power series is continuous in its disk of convergence. -/
lemma formal_multilinear_series.continuous_on [complete_space F] :
  continuous_on p.sum (emetric.ball 0 p.radius) :=
begin
  by_cases h : 0 < p.radius,
  { exact (p.has_fpower_series_on_ball h).continuous_on },
  { simp at h,
    simp [h, continuous_on_empty] }
end

end

/-!
### Changing origin in a power series

If a function is analytic in a disk `D(x, R)`, then it is analytic in any disk contained in that
one. Indeed, one can write
$$
f (x + y + z) = \sum_{n} p_n (y + z)^n = \sum_{n, k} \binom{n}{k} p_n y^{n-k} z^k
= \sum_{k} \Bigl(\sum_{n} \binom{n}{k} p_n y^{n-k}\Bigr) z^k.
$$
The corresponding power series has thus a `k`-th coefficient equal to
$\sum_{n} \binom{n}{k} p_n y^{n-k}$. In the general case where `pâ‚™` is a multilinear map, this has
to be interpreted suitably: instead of having a binomial coefficient, one should sum over all
possible subsets `s` of `fin n` of cardinal `k`, and attribute `z` to the indices in `s` and
`y` to the indices outside of `s`.

In this paragraph, we implement this. The new power series is called `p.change_origin y`. Then, we
check its convergence and the fact that its sum coincides with the original sum. The outcome of this
discussion is that the set of points where a function is analytic is open.
-/

namespace formal_multilinear_series

variables (p : formal_multilinear_series ğ•œ E F) {x y : E} {r : â„â‰¥0}

/--
Changing the origin of a formal multilinear series `p`, so that
`p.sum (x+y) = (p.change_origin x).sum y` when this makes sense.

Here, we don't use the bracket notation `âŸ¨n, s, hsâŸ©` in place of the argument `i` in the lambda,
as this leads to a bad definition with auxiliary `_match` statements,
but we will try to use pattern matching in lambdas as much as possible in the proofs below
to increase readability.
-/
def change_origin (x : E) : formal_multilinear_series ğ•œ E F :=
Î» k, âˆ‘' i : Î£ (n : â„•), {s : finset (fin n) // finset.card s = k}, (p i.1).restr i.2 i.2.2 x

/-- Auxiliary lemma controlling the summability of the sequence appearing in the definition of
`p.change_origin`, first version. -/
-- Note here and below it is necessary to use `@` and provide implicit arguments using `_`,
-- so that it is possible to use pattern matching in the lambda.
-- Overall this seems a good trade-off in readability.
lemma change_origin_summable_aux1 (h : (nnnorm x + r : ennreal) < p.radius) :
  @summable â„ _ _ _ ((Î» âŸ¨n, sâŸ©, âˆ¥p nâˆ¥ * âˆ¥xâˆ¥ ^ (n - s.card) * r ^ s.card) :
    (Î£ (n : â„•), finset (fin n)) â†’ â„) :=
begin
  obtain âŸ¨a, ha, C, hCâŸ© : âˆƒ (a < 1) C, âˆ€ n, nnnorm (p n) * (nnnorm x + r) ^ n â‰¤ C * a^n :=
    p.geometric_bound_of_lt_radius h,
  let Bnnnorm : (Î£ (n : â„•), finset (fin n)) â†’ â„â‰¥0 :=
    Î» âŸ¨n, sâŸ©, nnnorm (p n) * (nnnorm x) ^ (n - s.card) * r ^ s.card,
  have : ((Î» âŸ¨n, sâŸ©, âˆ¥p nâˆ¥ * âˆ¥xâˆ¥ ^ (n - s.card) * r ^ s.card) :
    (Î£ (n : â„•), finset (fin n)) â†’ â„) = (Î» b, (Bnnnorm b : â„)),
    by { ext âŸ¨n, sâŸ©, simp [Bnnnorm, nnreal.coe_pow, coe_nnnorm] },
  rw [this, nnreal.summable_coe, â† ennreal.tsum_coe_ne_top_iff_summable],
  apply ne_of_lt,
  calc (âˆ‘' b, â†‘(Bnnnorm b))
  = (âˆ‘' n, (âˆ‘' s, â†‘(Bnnnorm âŸ¨n, sâŸ©))) : by exact ennreal.tsum_sigma' _
  ... â‰¤ (âˆ‘' n, (((nnnorm (p n) * (nnnorm x + r)^n) : â„â‰¥0) : ennreal)) :
    begin
      refine ennreal.tsum_le_tsum (Î» n, _),
      rw [tsum_fintype, â† ennreal.coe_finset_sum, ennreal.coe_le_coe],
      apply le_of_eq,
      calc âˆ‘ s : finset (fin n), Bnnnorm âŸ¨n, sâŸ©
      = âˆ‘ s : finset (fin n), nnnorm (p n) * ((nnnorm x) ^ (n - s.card) * r ^ s.card) :
        by simp [â† mul_assoc]
      ... = nnnorm (p n) * (nnnorm x + r) ^ n :
      by { rw [add_comm, â† finset.mul_sum, â† fin.sum_pow_mul_eq_add_pow], congr' with s : 1, ring }
    end
  ... â‰¤ (âˆ‘' (n : â„•), (C * a ^ n : ennreal)) :
    tsum_le_tsum (Î» n, by exact_mod_cast hC n) ennreal.summable ennreal.summable
  ... < âŠ¤ :
    by simp [ennreal.mul_eq_top, ha, ennreal.tsum_mul_left, ennreal.tsum_geometric,
              ennreal.lt_top_iff_ne_top]
end

/-- Auxiliary lemma controlling the summability of the sequence appearing in the definition of
`p.change_origin`, second version. -/
lemma change_origin_summable_aux2 (h : (nnnorm x + r : ennreal) < p.radius) :
  @summable â„ _ _ _ ((Î» âŸ¨k, n, s, hsâŸ©, âˆ¥(p n).restr s hs xâˆ¥ * â†‘r ^ k) :
    (Î£ (k : â„•) (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ â„) :=
begin
  let Bnorm : (Î£ (n : â„•), finset (fin n)) â†’ â„ := Î» âŸ¨n, sâŸ©, âˆ¥p nâˆ¥ * âˆ¥xâˆ¥ ^ (n - s.card) * r ^ s.card,
  have SBnorm : summable Bnorm := p.change_origin_summable_aux1 h,
  let Anorm : (Î£ (n : â„•), finset (fin n)) â†’ â„ := Î» âŸ¨n, sâŸ©, âˆ¥(p n).restr s rfl xâˆ¥ * r ^ s.card,
  have SAnorm : summable Anorm,
  { refine summable_of_norm_bounded _ SBnorm (Î» i, _),
    rcases i with âŸ¨n, sâŸ©,
    suffices H : âˆ¥(p n).restr s rfl xâˆ¥ * (r : â„) ^ s.card â‰¤
      (âˆ¥p nâˆ¥ * âˆ¥xâˆ¥ ^ (n - finset.card s) * r ^ s.card),
    { have : âˆ¥(r: â„)âˆ¥ = r, by rw [real.norm_eq_abs, abs_of_nonneg (nnreal.coe_nonneg _)],
      simpa [Anorm, Bnorm, this] using H },
    exact mul_le_mul_of_nonneg_right ((p n).norm_restr s rfl x)
      (pow_nonneg (nnreal.coe_nonneg _) _) },
  let e : (Î£ (n : â„•), finset (fin n)) â‰ƒ
      (Î£ (k : â„•) (n : â„•), {s : finset (fin n) // finset.card s = k}) :=
    { to_fun := Î» âŸ¨n, sâŸ©, âŸ¨s.card, n, s, rflâŸ©,
      inv_fun := Î» âŸ¨k, n, s, hsâŸ©, âŸ¨n, sâŸ©,
      left_inv := Î» âŸ¨n, sâŸ©, rfl,
      right_inv := Î» âŸ¨k, n, s, hsâŸ©, by { induction hs, refl } },
  rw â† e.summable_iff,
  convert SAnorm,
  ext âŸ¨n, sâŸ©,
  refl
end

/-- An auxiliary definition for `change_origin_radius`. -/
def change_origin_summable_aux_j (k : â„•) :
  (Î£ (n : â„•), {s : finset (fin n) // finset.card s = k})
    â†’ (Î£ (k : â„•) (n : â„•), {s : finset (fin n) // finset.card s = k}) :=
Î» âŸ¨n, s, hsâŸ©, âŸ¨k, n, s, hsâŸ©

lemma change_origin_summable_aux_j_injective (k : â„•) :
  function.injective (change_origin_summable_aux_j k) :=
begin
  rintros âŸ¨_, âŸ¨_, _âŸ©âŸ© âŸ¨_, âŸ¨_, _âŸ©âŸ© a,
  simp only [change_origin_summable_aux_j, true_and, eq_self_iff_true, heq_iff_eq, sigma.mk.inj_iff] at a,
  rcases a with âŸ¨rfl, aâŸ©,
  simpa using a,
end

/-- Auxiliary lemma controlling the summability of the sequence appearing in the definition of
`p.change_origin`, third version. -/
lemma change_origin_summable_aux3 (k : â„•) (h : (nnnorm x : ennreal) < p.radius) :
  @summable â„ _ _ _ (Î» âŸ¨n, s, hsâŸ©, âˆ¥(p n).restr s hs xâˆ¥ :
  (Î£ (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ â„) :=
begin
  obtain âŸ¨r, rpos, hrâŸ© : âˆƒ (r : â„â‰¥0), 0 < r âˆ§ ((nnnorm x + r) : ennreal) < p.radius :=
    ennreal.lt_iff_exists_add_pos_lt.mp h,
  have S : @summable â„ _ _ _ ((Î» âŸ¨n, s, hsâŸ©, âˆ¥(p n).restr s hs xâˆ¥ * (r : â„) ^ k) :
    (Î£ (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ â„),
  { convert (p.change_origin_summable_aux2 hr).comp_injective
      (change_origin_summable_aux_j_injective k),
    -- again, cleanup that could be done by `tidy`:
    ext âŸ¨_, âŸ¨_, _âŸ©âŸ©, refl },
  have : (r : â„)^k â‰  0, by simp [pow_ne_zero, nnreal.coe_eq_zero, ne_of_gt rpos],
  apply (summable_mul_right_iff this).2,
  convert S,
  -- again, cleanup that could be done by `tidy`:
  ext âŸ¨_, âŸ¨_, _âŸ©âŸ©, refl,
end

-- FIXME this causes a deterministic timeout with `-T50000`
/-- The radius of convergence of `p.change_origin x` is at least `p.radius - âˆ¥xâˆ¥`. In other words,
`p.change_origin x` is well defined on the largest ball contained in the original ball of
convergence.-/
lemma change_origin_radius : p.radius - nnnorm x â‰¤ (p.change_origin x).radius :=
begin
  by_cases h : p.radius â‰¤ nnnorm x,
  { have : radius p - â†‘(nnnorm x) = 0 := ennreal.sub_eq_zero_of_le h,
    rw this,
    exact zero_le _ },
  replace h : (nnnorm x : ennreal) < p.radius, by simpa using h,
  refine le_of_forall_ge_of_dense (Î» r hr, _),
  cases r, { simpa using hr },
  rw [ennreal.lt_sub_iff_add_lt, add_comm] at hr,
  let A : (Î£ (k : â„•) (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ â„ :=
    Î» âŸ¨k, n, s, hsâŸ©, âˆ¥(p n).restr s hs xâˆ¥ * (r : â„) ^ k,
  have SA : summable A := p.change_origin_summable_aux2 hr,
  have A_nonneg : âˆ€ i, 0 â‰¤ A i,
  { rintros âŸ¨k, n, s, hsâŸ©,
    change 0 â‰¤ âˆ¥(p n).restr s hs xâˆ¥ * (r : â„) ^ k,
    refine mul_nonneg (norm_nonneg _) (pow_nonneg (nnreal.coe_nonneg _) _) },
  have tsum_nonneg : 0 â‰¤ tsum A := tsum_nonneg A_nonneg,
  apply le_radius_of_bound _ (nnreal.of_real (tsum A)) (Î» k, _),
  rw [â† nnreal.coe_le_coe, nnreal.coe_mul, nnreal.coe_pow, coe_nnnorm,
      nnreal.coe_of_real _ tsum_nonneg],
  calc âˆ¥change_origin p x kâˆ¥ * â†‘r ^ k
  = âˆ¥@tsum (E [Ã—k]â†’L[ğ•œ] F) _ _ _ (Î» i, (p i.1).restr i.2.1 i.2.2 x :
    (Î£ (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ (E [Ã—k]â†’L[ğ•œ] F))âˆ¥ * â†‘r ^ k : rfl
  ... â‰¤ tsum (Î» i, âˆ¥(p i.1).restr i.2.1 i.2.2 xâˆ¥ :
    (Î£ (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ â„) * â†‘r ^ k :
      begin
        apply mul_le_mul_of_nonneg_right _ (pow_nonneg (nnreal.coe_nonneg _) _),
        apply norm_tsum_le_tsum_norm,
        convert p.change_origin_summable_aux3 k h,
        ext a,
        tidy
      end
  ... = tsum (Î» i, âˆ¥(p i.1).restr i.2.1 i.2.2 xâˆ¥ * â†‘r ^ k :
    (Î£ (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ â„) :
      by { rw tsum_mul_right, convert p.change_origin_summable_aux3 k h, tidy }
  ... = tsum (A âˆ˜ change_origin_summable_aux_j k) : by { congr, tidy }
  ... â‰¤ tsum A : tsum_comp_le_tsum_of_inj SA A_nonneg (change_origin_summable_aux_j_injective k)
end

-- From this point on, assume that the space is complete, to make sure that series that converge
-- in norm also converge in `F`.
variable [complete_space F]

/-- The `k`-th coefficient of `p.change_origin` is the sum of a summable series. -/
lemma change_origin_has_sum (k : â„•) (h : (nnnorm x : ennreal) < p.radius) :
  @has_sum (E [Ã—k]â†’L[ğ•œ] F) _ _ _  ((Î» i, (p i.1).restr i.2.1 i.2.2 x) :
    (Î£ (n : â„•), {s : finset (fin n) // finset.card s = k}) â†’ (E [Ã—k]â†’L[ğ•œ] F))
  (p.change_origin x k) :=
begin
  apply summable.has_sum,
  apply summable_of_summable_norm,
  convert p.change_origin_summable_aux3 k h,
  tidy
end

/-- Summing the series `p.change_origin x` at a point `y` gives back `p (x + y)`-/
theorem change_origin_eval (h : (nnnorm x + nnnorm y : ennreal) < p.radius) :
  has_sum ((Î»k:â„•, p.change_origin x k (Î» (i : fin k), y))) (p.sum (x + y)) :=
begin
  /- The series on the left is a series of series. If we order the terms differently, we get back
  to `p.sum (x + y)`, in which the `n`-th term is expanded by multilinearity. In the proof below,
  the term on the left is the sum of a series of terms `A`, the sum on the right is the sum of a
  series of terms `B`, and we show that they correspond to each other by reordering to conclude the
  proof. -/
  have radius_pos : 0 < p.radius := lt_of_le_of_lt (zero_le _) h,
  -- `A` is the terms of the series whose sum gives the series for `p.change_origin`
  let A : (Î£ (k : â„•) (n : â„•), {s : finset (fin n) // s.card = k}) â†’ F :=
    Î» âŸ¨k, n, s, hsâŸ©, (p n).restr s hs x (Î»(i : fin k), y),
  -- `B` is the terms of the series whose sum gives `p (x + y)`, after expansion by multilinearity.
  let B : (Î£ (n : â„•), finset (fin n)) â†’ F := Î» âŸ¨n, sâŸ©, (p n).restr s rfl x (Î» (i : fin s.card), y),
  let Bnorm : (Î£ (n : â„•), finset (fin n)) â†’ â„ := Î» âŸ¨n, sâŸ©, âˆ¥p nâˆ¥ * âˆ¥xâˆ¥ ^ (n - s.card) * âˆ¥yâˆ¥ ^ s.card,
  have SBnorm : summable Bnorm, by convert p.change_origin_summable_aux1 h,
  have SB : summable B,
  { refine summable_of_norm_bounded _ SBnorm _,
    rintros âŸ¨n, sâŸ©,
    calc âˆ¥(p n).restr s rfl x (Î» (i : fin s.card), y)âˆ¥
      â‰¤ âˆ¥(p n).restr s rfl xâˆ¥ * âˆ¥yâˆ¥ ^ s.card :
        begin
          convert ((p n).restr s rfl x).le_op_norm (Î» (i : fin s.card), y),
          simp [(finset.prod_const (âˆ¥yâˆ¥))],
        end
      ... â‰¤ (âˆ¥p nâˆ¥ * âˆ¥xâˆ¥ ^ (n - s.card)) * âˆ¥yâˆ¥ ^ s.card :
        mul_le_mul_of_nonneg_right ((p n).norm_restr _ _ _) (pow_nonneg (norm_nonneg _) _) },
  -- Check that indeed the sum of `B` is `p (x + y)`.
  have has_sum_B : has_sum B (p.sum (x + y)),
  { have K1 : âˆ€ n, has_sum (Î» (s : finset (fin n)), B âŸ¨n, sâŸ©) (p n (Î» (i : fin n), x + y)),
    { assume n,
      have : (p n) (Î» (i : fin n), y + x) = âˆ‘ s : finset (fin n),
        p n (finset.piecewise s (Î» (i : fin n), y) (Î» (i : fin n), x)) :=
        (p n).map_add_univ (Î» i, y) (Î» i, x),
      simp [add_comm y x] at this,
      rw this,
      exact has_sum_fintype _ },
    have K2 : has_sum (Î» (n : â„•), (p n) (Î» (i : fin n), x + y)) (p.sum (x + y)),
    { have : x + y âˆˆ emetric.ball (0 : E) p.radius,
      { apply lt_of_le_of_lt _ h,
        rw [edist_eq_coe_nnnorm, â† ennreal.coe_add, ennreal.coe_le_coe],
        exact norm_add_le x y },
      simpa using (p.has_fpower_series_on_ball radius_pos).has_sum this },
    exact has_sum.sigma_of_has_sum K2 K1 SB },
  -- Deduce that the sum of `A` is also `p (x + y)`, as the terms `A` and `B` are the same up to
  -- reordering
  have has_sum_A : has_sum A (p.sum (x + y)),
  { let e : (Î£ (n : â„•), finset (fin n)) â‰ƒ
      (Î£ (k : â„•) (n : â„•), {s : finset (fin n) // finset.card s = k}) :=
    { to_fun := Î» âŸ¨n, sâŸ©, âŸ¨s.card, n, s, rflâŸ©,
      inv_fun := Î» âŸ¨k, n, s, hsâŸ©, âŸ¨n, sâŸ©,
      left_inv := Î» âŸ¨n, sâŸ©, rfl,
      right_inv := Î» âŸ¨k, n, s, hsâŸ©, by { induction hs, refl } },
    have : A âˆ˜ e = B, by { ext âŸ¨âŸ©, refl },
    rw â† e.has_sum_iff,
    convert has_sum_B },
  -- Summing `A âŸ¨k, câŸ©` with fixed `k` and varying `c` is exactly the `k`-th term in the series
  -- defining `p.change_origin`, by definition
  have J : âˆ€k, has_sum (Î» c, A âŸ¨k, câŸ©) (p.change_origin x k (Î»(i : fin k), y)),
  { assume k,
    have : (nnnorm x : ennreal) < radius p := lt_of_le_of_lt (le_add_right (le_refl _)) h,
    convert continuous_multilinear_map.has_sum_eval (p.change_origin_has_sum k this)
      (Î»(i : fin k), y),
    ext i,
    tidy },
  exact has_sum_A.sigma J
end

end formal_multilinear_series

section

variables [complete_space F] {f : E â†’ F} {p : formal_multilinear_series ğ•œ E F} {x y : E}
{r : ennreal}

/-- If a function admits a power series expansion `p` on a ball `B (x, r)`, then it also admits a
power series on any subball of this ball (even with a different center), given by `p.change_origin`.
-/
theorem has_fpower_series_on_ball.change_origin
  (hf : has_fpower_series_on_ball f p x r) (h : (nnnorm y : ennreal) < r) :
  has_fpower_series_on_ball f (p.change_origin y) (x + y) (r - nnnorm y) :=
{ r_le := begin
    apply le_trans _ p.change_origin_radius,
    exact ennreal.sub_le_sub hf.r_le (le_refl _)
  end,
  r_pos := by simp [h],
  has_sum := begin
    assume z hz,
    have A : (nnnorm y : ennreal) + nnnorm z < r,
    { have : edist z 0 < r - â†‘(nnnorm y) := hz,
      rwa [edist_eq_coe_nnnorm, ennreal.lt_sub_iff_add_lt, add_comm] at this },
    convert p.change_origin_eval (lt_of_lt_of_le A hf.r_le),
    have : y + z âˆˆ emetric.ball (0 : E) r := calc
      edist (y + z) 0 â‰¤ â†‘(nnnorm y) + â†‘(nnnorm z) :
        by { rw [edist_eq_coe_nnnorm, â† ennreal.coe_add, ennreal.coe_le_coe], exact norm_add_le y z }
      ... < r : A,
    simpa only [add_assoc] using hf.sum this
  end }

lemma has_fpower_series_on_ball.analytic_at_of_mem
  (hf : has_fpower_series_on_ball f p x r) (h : y âˆˆ emetric.ball x r) :
  analytic_at ğ•œ f y :=
begin
  have : (nnnorm (y - x) : ennreal) < r, by simpa [edist_eq_coe_nnnorm_sub] using h,
  have := hf.change_origin this,
  rw [add_sub_cancel'_right] at this,
  exact this.analytic_at
end

variables (ğ•œ f)
lemma is_open_analytic_at : is_open {x | analytic_at ğ•œ f x} :=
begin
  rw is_open_iff_forall_mem_open,
  assume x hx,
  rcases hx with âŸ¨p, r, hrâŸ©,
  refine âŸ¨emetric.ball x r, Î» y hy, hr.analytic_at_of_mem hy, emetric.is_open_ball, _âŸ©,
  simp only [edist_self, emetric.mem_ball, hr.r_pos]
end
variables {ğ•œ f}

end
