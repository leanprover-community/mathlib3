/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/

import analysis.calculus.times_cont_diff tactic.omega analysis.complex.exponential
analysis.specific_limits

/-!
# Analytic functions

A function is analytic in one dimension around `0` if it can be written as a converging power series
`Î£ pâ‚™ zâ¿`. This definition can be extended to any dimension (even in infinite dimension) by
requiring that `pâ‚™` is a continuous `n`-multilinear map. In general, `pâ‚™` is not unique (in two
dimensions, taking `pâ‚‚ (x, y) (x', y') = x y'` or `y x'` gives the same map when applied to a
vector `(x, y) (x, y)`). A way to guarantee uniqueness is to take a symmetric `pâ‚™`, but this is not
always possible in nonzero characteristic (in characteristic 2, the previous example has no
symmetric representative). Therefore, we do not insist on symmetry or uniqueness in the definition,
and we only require the existence of a converging series.

The general framework is important to say that the exponential map on bounded operators on a Banach
space is analytic, as well as the inverse on invertible operators.

## Main definitions

Let `p` be a formal multilinear series from `E` to `F`, i.e., `p n` is a multilinear map on `E^n`
for `n : â„•`.

* `p.radius`: the largest `r : ennreal` such that `âˆ¥p nâˆ¥ * r^n` grows subexponentially, defined as
  a liminf.
* `p.le_radius_of_bound`, `p.bound_of_lt_radius`, `p.geometric_bound_of_lt_radius`: relating the
  value of the radius with the growth of `âˆ¥p nâˆ¥ * r^n`.
* `p.partial_sum n x`: the sum `âˆ‘_{i = 0}^{n-1} páµ¢ xâ±`.

Additionally, let `f` be a function from `E` to `F`.

* `has_fpower_series_on_ball f p x r`: on the ball of center `x` with radius `r`,
  `f (x + y) = âˆ‘_n pâ‚™ yâ¿`.
* `has_fpower_series_at f p x`: on some ball of center `x` with positive radius, holds
  `has_fpower_series_on_ball f p x r`.
* `analytic_at ğ•œ f x`: there exists a power series `p` such that holds
  `has_fpower_series_at f p x`.

We develop the basic properties of these notions, notably:
* If a function admits a power series, it is continuous (see
  `has_fpower_series_on_ball.continuous_on` and `has_fpower_series_at.continuous_at` and
  `analytic_at.continuous_at`).
* In a complete space, the sum of a formal power series with positive radius is well defined on the
  disk of convergence, see `formal_multilinear_series.has_fpower_series_on_ball`.

## Implementation details

We only introduce the radius of convergence of a power series, as `p.radius`.
For a power series in finitely many dimensions, there is a finer (directional, coordinate-dependent)
notion, describing the polydisk of convergence. This notion is more specific, and not necessary to
build the general theory. We do not define it here.
-/

noncomputable theory

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{F : Type*} [normed_group F] [normed_space ğ•œ F]
{G : Type*} [normed_group G] [normed_space ğ•œ G]

open_locale topological_space classical
open filter

/-! ### The radius of a formal multilinear series -/

namespace formal_multilinear_series

/-- The radius of a formal multilinear series is the largest `r` such that the sum `Î£ pâ‚™ yâ¿`
converges for all `âˆ¥yâˆ¥ < r`. -/
def radius (p : formal_multilinear_series ğ•œ E F) : ennreal :=
liminf at_top (Î» n, 1/((nnnorm (p n)) ^ (1 / (n : â„)) : nnreal))

/--If `âˆ¥pâ‚™âˆ¥ râ¿` is bounded in `n`, then the radius of `p` is at least `r`. -/
lemma le_radius_of_bound (p : formal_multilinear_series ğ•œ E F) (C : nnreal) {r : nnreal}
  (h : âˆ€ (n : â„•), nnnorm (p n) * r^n â‰¤ C) : (r : ennreal) â‰¤ p.radius :=
begin
  have L : tendsto (Î» n : â„•, (r : ennreal) / ((C + 1)^(1/(n : â„)) : nnreal))
    at_top (ğ“ ((r : ennreal) / ((C + 1)^(0 : â„) : nnreal))),
  { apply ennreal.tendsto.div tendsto_const_nhds,
    { simp },
    { rw ennreal.tendsto_coe,
      apply tendsto_const_nhds.nnrpow (tendsto_const_div_at_top_nhds_0_nat 1),
      simp },
    { simp } },
  have A : âˆ€ n : â„• , 0 < n â†’
    (r : ennreal) â‰¤ ((C + 1)^(1/(n : â„)) : nnreal) * (1 / (nnnorm (p n) ^ (1/(n:â„)) : nnreal)),
  { assume n npos,
    simp only [one_div_eq_inv, mul_assoc, mul_one, eq.symm ennreal.mul_div_assoc],
    rw [ennreal.le_div_iff_mul_le _ _, â† nnreal.pow_nat_rpow_nat_inv r npos, â† ennreal.coe_mul,
        ennreal.coe_le_coe, â† nnreal.mul_rpow, mul_comm],
    { exact nnreal.rpow_le_rpow (le_trans (h n) (le_add_right (le_refl _))) (by simp) },
    { simp },
    { simp } },
  have B : âˆ€á¶  (n : â„•) in at_top,
    (r : ennreal) / ((C + 1)^(1/(n : â„)) : nnreal) â‰¤ 1 / (nnnorm (p n) ^ (1/(n:â„)) : nnreal),
  { apply eventually_at_top.2 âŸ¨1, Î» n hn, _âŸ©,
    rw [ennreal.div_le_iff_le_mul, mul_comm],
    { apply A n hn },
    { simp },
    { simp } },
  have D : liminf at_top (Î» n : â„•, (r : ennreal) / ((C + 1)^(1/(n : â„)) : nnreal)) â‰¤ p.radius :=
    liminf_le_liminf B,
  rw liminf_eq_of_tendsto filter.at_top_ne_bot L at D,
  simpa using D
end

/-- For `r` strictly smaller than the radius of `p`, then `âˆ¥pâ‚™âˆ¥ râ¿` is bounded. -/
lemma bound_of_lt_radius (p : formal_multilinear_series ğ•œ E F) {r : nnreal}
  (h : (r : ennreal) < p.radius) : âˆƒ (C : nnreal), âˆ€ n, nnnorm (p n) * r^n â‰¤ C :=
begin
  obtain âŸ¨N, hNâŸ© : âˆƒ (N : â„•), âˆ€ n, n â‰¥ N â†’ (r : ennreal) < 1 / â†‘(nnnorm (p n) ^ (1 / (n : â„))) :=
    eventually.exists_forall_of_at_top (eventually_lt_of_lt_liminf h),
  obtain âŸ¨D, hDâŸ© : âˆƒD, âˆ€ x âˆˆ (â†‘((finset.range N.succ).image (Î» i, nnnorm (p i) * r^i))), x â‰¤ D :=
    finset.bdd_above _,
  refine âŸ¨max D 1, Î» n, _âŸ©,
  cases le_or_lt n N with hn hn,
  { refine le_trans _ (le_max_left D 1),
    apply hD,
    have : n âˆˆ finset.range N.succ := list.mem_range.mpr (nat.lt_succ_iff.mpr hn),
    exact finset.mem_image_of_mem _ this },
  { by_cases hpn : nnnorm (p n) = 0, { simp [hpn] },
    have A : nnnorm (p n) ^ (1 / (n : â„)) â‰  0, by simp [nnreal.rpow_eq_zero_iff, hpn],
    have B : r < (nnnorm (p n) ^ (1 / (n : â„)))â»Â¹,
    { have := hN n (le_of_lt hn),
      rwa [ennreal.div_def, â† ennreal.coe_inv A, one_mul, ennreal.coe_lt_coe] at this },
    rw [nnreal.lt_inv_iff_mul_lt A, mul_comm] at B,
    have : (nnnorm (p n) ^ (1 / (n : â„)) * r) ^ n â‰¤ 1 :=
      pow_le_one n (zero_le (nnnorm (p n) ^ (1 / â†‘n) * r)) (le_of_lt B),
    rw [mul_pow, one_div_eq_inv, nnreal.rpow_nat_inv_pow_nat _ (lt_of_le_of_lt (zero_le _) hn)]
      at this,
    exact le_trans this (le_max_right _ _) },
end

/-- For `r` strictly smaller than the radius of `p`, then `âˆ¥pâ‚™âˆ¥ râ¿` tends to zero exponentially. -/
lemma geometric_bound_of_lt_radius (p : formal_multilinear_series ğ•œ E F) {r : nnreal}
  (h : (r : ennreal) < p.radius) : âˆƒ a C, a < 1 âˆ§ âˆ€ n, nnnorm (p n) * r^n â‰¤ C * a^n :=
begin
  obtain âŸ¨t, rt, tpâŸ© : âˆƒ t, (r : ennreal) < t âˆ§ t < p.radius := dense h,
  cases t, { simpa using tp },
  simp only [ennreal.some_eq_coe, ennreal.coe_lt_coe] at rt,
  have tpos : t â‰  0 := ne_of_gt (lt_of_le_of_lt (zero_le _) rt),
  obtain âŸ¨C, hCâŸ© : âˆƒ (C : nnreal), âˆ€ n, nnnorm (p n) * t^n â‰¤ C := p.bound_of_lt_radius tp,
  refine âŸ¨r / t, C, nnreal.div_lt_one_of_lt rt, Î» n, _âŸ©,
  calc nnnorm (p n) * r ^ n
    = (nnnorm (p n) * t ^ n) * (r / t) ^ n : by { field_simp [tpos], ac_refl }
    ... â‰¤ C * (r / t) ^ n : mul_le_mul_of_nonneg_right (hC n) (zero_le _)
end

/-- The radius of the sum of two formal series is at least the minimum of their two radii. -/
lemma min_radius_le_radius_add (p q : formal_multilinear_series ğ•œ E F) :
  min p.radius q.radius â‰¤ (p + q).radius :=
begin
  refine le_of_forall_ge_of_dense (Î» r hr, _),
  cases r, { simpa using hr },
  obtain âŸ¨Cp, hCpâŸ© : âˆƒ (C : nnreal), âˆ€ n, nnnorm (p n) * r^n â‰¤ C :=
    p.bound_of_lt_radius (lt_of_lt_of_le hr (min_le_left _ _)),
  obtain âŸ¨Cq, hCqâŸ© : âˆƒ (C : nnreal), âˆ€ n, nnnorm (q n) * r^n â‰¤ C :=
    q.bound_of_lt_radius (lt_of_lt_of_le hr (min_le_right _ _)),
  have : âˆ€ n, nnnorm ((p + q) n) * r^n â‰¤ Cp + Cq,
  { assume n,
    calc nnnorm (p n + q n) * r ^ n
    â‰¤ (nnnorm (p n) + nnnorm (q n)) * r ^ n :
      mul_le_mul_of_nonneg_right (norm_add_le (p n) (q n)) (zero_le (r ^ n))
    ... â‰¤ Cp + Cq : by { rw add_mul, exact add_le_add (hCp n) (hCq n) } },
  exact (p + q).le_radius_of_bound _ this
end

lemma radius_neg (p : formal_multilinear_series ğ•œ E F) : (-p).radius = p.radius :=
by simp [formal_multilinear_series.radius, nnnorm_neg]

/-- Given a formal multilinear series `p` and a vector `x`, then `p.sum x` is the sum `Î£ pâ‚™ xâ¿`. A
priori, it only behaves well when `âˆ¥xâˆ¥ < p.radius`. -/
def sum (p : formal_multilinear_series ğ•œ E F) (x : E) : F :=
tsum (Î»n:â„•, p n (Î»(i : fin n), x))

/-- Given a formal multilinear series `p` and a vector `x`, then `p.partial_sum n x` is the sum
`Î£ pâ‚– xáµ` for `k âˆˆ {0,..., n-1}`. -/
def partial_sum (p : formal_multilinear_series ğ•œ E F) (n : â„•) (x : E) : F :=
(finset.range n).sum (Î» k, p k (Î»(i : fin k), x))

/-- The partial sums of a formal multilinear series are continuous. -/
lemma partial_sum_continuous (p : formal_multilinear_series ğ•œ E F) (n : â„•) :
  continuous (p.partial_sum n) :=
continuous_finset_sum (finset.range n) $ Î» k hk, (p k).cont.comp (continuous_pi (Î» i, continuous_id))

end formal_multilinear_series


/-! ### Expanding a function as a power series -/

variables {f g : E â†’ F} {p pf pg : formal_multilinear_series ğ•œ E F} {x : E} {r r' : ennreal}

/-- Given a function `f : E â†’ F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series on the ball of radius `r > 0` around `x` if `f (x + y) = âˆ‘ pâ‚™ yâ¿` for all `âˆ¥yâˆ¥ < r`. -/
structure has_fpower_series_on_ball
  (f : E â†’ F) (p : formal_multilinear_series ğ•œ E F) (x : E) (r : ennreal) : Prop :=
(r_le    : r â‰¤ p.radius)
(r_pos   : 0 < r)
(has_sum : âˆ€ {y}, y âˆˆ emetric.ball (0 : E) r â†’ has_sum (Î»n:â„•, p n (Î»(i : fin n), y)) (f (x + y)))

/-- Given a function `f : E â†’ F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series around `x` if `f (x + y) = âˆ‘ pâ‚™ yâ¿` for all `y` in a neighborhood of `0`. -/
def has_fpower_series_at (f : E â†’ F) (p : formal_multilinear_series ğ•œ E F) (x : E) :=
âˆƒ r, has_fpower_series_on_ball f p x r

variable (ğ•œ)
/-- Given a function `f : E â†’ F`, we say that `f` is analytic at `x` if it admits a convergent power
series expansion around `x`. -/
def analytic_at (f : E â†’ F) (x : E) :=
âˆƒ (p : formal_multilinear_series ğ•œ E F), has_fpower_series_at f p x

variable {ğ•œ}

lemma has_fpower_series_on_ball.has_fpower_series_at (hf : has_fpower_series_on_ball f p x r) :
  has_fpower_series_at f p x := âŸ¨r, hfâŸ©

lemma has_fpower_series_at.analytic_at (hf : has_fpower_series_at f p x) : analytic_at ğ•œ f x :=
âŸ¨p, hfâŸ©

lemma has_fpower_series_on_ball.analytic_at (hf : has_fpower_series_on_ball f p x r) :
  analytic_at ğ•œ f x :=
hf.has_fpower_series_at.analytic_at

lemma has_fpower_series_on_ball.radius_pos (hf : has_fpower_series_on_ball f p x r) :
  0 < p.radius :=
lt_of_lt_of_le hf.r_pos hf.r_le

lemma has_fpower_series_at.radius_pos (hf : has_fpower_series_at f p x) :
  0 < p.radius :=
let âŸ¨r, hrâŸ© := hf in hr.radius_pos

lemma has_fpower_series_on_ball.mono
  (hf : has_fpower_series_on_ball f p x r) (r'_pos : 0 < r') (hr : r' â‰¤ r) :
  has_fpower_series_on_ball f p x r' :=
âŸ¨le_trans hr hf.1, r'_pos, Î» y hy, hf.has_sum (emetric.ball_subset_ball hr hy)âŸ©

lemma has_fpower_series_on_ball.add
  (hf : has_fpower_series_on_ball f pf x r) (hg : has_fpower_series_on_ball g pg x r) :
  has_fpower_series_on_ball (f + g) (pf + pg) x r :=
{ r_le := le_trans (le_min_iff.2 âŸ¨hf.r_le, hg.r_leâŸ©) (pf.min_radius_le_radius_add pg),
  r_pos := hf.r_pos,
  has_sum := Î» y hy, (hf.has_sum hy).add (hg.has_sum hy) }

lemma has_fpower_series_at.add
  (hf : has_fpower_series_at f pf x) (hg : has_fpower_series_at g pg x) :
  has_fpower_series_at (f + g) (pf + pg) x :=
begin
  rcases hf with âŸ¨rf, hrfâŸ©,
  rcases hg with âŸ¨rg, hrgâŸ©,
  have P : 0 < min rf rg, by simp [hrf.r_pos, hrg.r_pos],
  exact âŸ¨min rf rg, (hrf.mono P (min_le_left _ _)).add (hrg.mono P (min_le_right _ _))âŸ©
end

lemma analytic_at.add (hf : analytic_at ğ•œ f x) (hg : analytic_at ğ•œ g x) :
  analytic_at ğ•œ (f + g) x :=
let âŸ¨pf, hpfâŸ© := hf, âŸ¨qf, hqfâŸ© := hg in (hpf.add hqf).analytic_at

lemma has_fpower_series_on_ball.neg (hf : has_fpower_series_on_ball f pf x r) :
  has_fpower_series_on_ball (-f) (-pf) x r :=
{ r_le    := by { rw pf.radius_neg, exact hf.r_le },
  r_pos   := hf.r_pos,
  has_sum := Î» y hy, (hf.has_sum hy).neg }

lemma has_fpower_series_at.neg
  (hf : has_fpower_series_at f pf x) : has_fpower_series_at (-f) (-pf) x :=
let âŸ¨rf, hrfâŸ© := hf in hrf.neg.has_fpower_series_at

lemma analytic_at.neg (hf : analytic_at ğ•œ f x) : analytic_at ğ•œ (-f) x :=
let âŸ¨pf, hpfâŸ© := hf in hpf.neg.analytic_at

lemma has_fpower_series_on_ball.sub
  (hf : has_fpower_series_on_ball f pf x r) (hg : has_fpower_series_on_ball g pg x r) :
  has_fpower_series_on_ball (f - g) (pf - pg) x r :=
hf.add hg.neg

lemma has_fpower_series_at.sub
  (hf : has_fpower_series_at f pf x) (hg : has_fpower_series_at g pg x) :
  has_fpower_series_at (f - g) (pf - pg) x :=
hf.add hg.neg

lemma analytic_at.sub (hf : analytic_at ğ•œ f x) (hg : analytic_at ğ•œ g x) :
  analytic_at ğ•œ (f - g) x :=
hf.add hg.neg

/-- If a function admits a power series expansion, then it is exponentially close to the partial
sums of this power series on strict subdisks of the disk of convergence. -/
lemma has_fpower_series_on_ball.uniform_limit {r' : nnreal}
  (hf : has_fpower_series_on_ball f p x r) (h : (r' : ennreal) < r) :
  âˆƒ (a C : nnreal), a < 1 âˆ§ (âˆ€ y âˆˆ metric.ball (0 : E) r', âˆ€ n,
  âˆ¥f (x + y) - p.partial_sum n yâˆ¥ â‰¤ C * a ^ n) :=
begin
  obtain âŸ¨a, C, ha, hCâŸ© : âˆƒ a C, a < 1 âˆ§ âˆ€ n, nnnorm (p n) * r' ^n â‰¤ C * a^n :=
    p.geometric_bound_of_lt_radius (lt_of_lt_of_le h hf.r_le),
  refine âŸ¨a, C / (1 - a), ha, Î» y hy n, _âŸ©,
  have yr' : âˆ¥yâˆ¥ < r', by { rw ball_0_eq at hy, exact hy },
  have : y âˆˆ emetric.ball (0 : E) r,
  { rw [emetric.mem_ball, edist_eq_coe_nnnorm],
    apply lt_trans _ h,
    rw [ennreal.coe_lt_coe, â† nnreal.coe_lt_coe],
    exact yr' },
  simp only [nnreal.coe_sub (le_of_lt ha), nnreal.coe_sub, nnreal.coe_div, nnreal.coe_one],
  rw [â† dist_eq_norm, dist_comm, dist_eq_norm, â† mul_div_right_comm],
  apply norm_sub_le_of_geometric_bound_of_has_sum ha _ (hf.has_sum this),
  assume n,
  calc âˆ¥(p n) (Î» (i : fin n), y)âˆ¥
    â‰¤ âˆ¥p nâˆ¥ * (finset.univ.prod (Î» i : fin n, âˆ¥yâˆ¥)) : continuous_multilinear_map.le_op_norm _ _
    ... = nnnorm (p n) * (nnnorm y)^n : by simp
    ... â‰¤ nnnorm (p n) * r' ^ n :
      mul_le_mul_of_nonneg_left (pow_le_pow_of_le_left (nnreal.coe_nonneg _) (le_of_lt yr') _)
      (nnreal.coe_nonneg _)
    ... â‰¤ C * a ^ n : by exact_mod_cast hC n,
end

/-- If a function admits a power series expansion on a disk, then it is continuous there. -/
lemma has_fpower_series_on_ball.continuous_on
  (hf : has_fpower_series_on_ball f p x r) : continuous_on f (emetric.ball x r) :=
begin
  have : âˆ€ n, continuous_on (Î» y, p.partial_sum n (y - x)) (emetric.ball x r) :=
    Î» n, ((p.partial_sum_continuous n).comp (continuous_id.sub continuous_const)).continuous_on,
  apply continuous_on_of_locally_uniform_limit_of_continuous_on (Î» y hy, _) this,
  have : (nnnorm (y - x) : ennreal) < r,
    by { rw â† edist_eq_coe_nnnorm_sub, exact hy },
  rcases dense this with âŸ¨r', xr', r'râŸ©,
  cases r', { simpa using r'r },
  replace xr' : nnnorm (y - x) < r', by simpa using xr',
  refine âŸ¨metric.ball x r', _, Î» Îµ Îµpos, _âŸ©,
  show metric.ball x r' âˆˆ nhds_within y (emetric.ball x r),
  { apply mem_nhds_within_of_mem_nhds,
    apply mem_nhds_sets metric.is_open_ball,
    change dist y x < r',
    rwa [dist_nndist, nnreal.coe_lt_coe, nndist_eq_nnnorm] },
  show âˆƒ (n : â„•),
    âˆ€ z âˆˆ metric.ball x â†‘r', dist (formal_multilinear_series.partial_sum p n (z - x)) (f z) â‰¤ Îµ,
  { obtain âŸ¨a, C, ha, hCâŸ© : âˆƒ (a C : nnreal), a < 1 âˆ§ (âˆ€ y âˆˆ metric.ball (0 : E) r', âˆ€ n,
      âˆ¥f (x + y) - p.partial_sum n yâˆ¥ â‰¤ C * a ^ n) := hf.uniform_limit r'r,
    have L : tendsto (Î» (n : â„•), (C : â„) * a ^ n) at_top (ğ“ ((C : â„) * 0)) :=
      tendsto_const_nhds.mul (tendsto_pow_at_top_nhds_0_of_lt_1 (a.2) ha),
    rw mul_zero at L,
    obtain âŸ¨n, hnâŸ© : âˆƒ (n : â„•), (C : â„) * a ^ n < Îµ :=
      eventually.exists ((tendsto_order.1 L).2 _ Îµpos) at_top_ne_bot,
    refine âŸ¨n, Î» z hz, _âŸ©,
    have : z - x âˆˆ metric.ball (0 : E) r',
      by { rwa [metric.mem_ball, dist_eq_norm, â† dist_zero_right] at hz },
    rw [dist_eq_norm, norm_sub_rev],
    convert le_trans (hC _ this n) (le_of_lt hn),
    abel }
end

lemma has_fpower_series_at.continuous_at (hf : has_fpower_series_at f p x) : continuous_at f x :=
let âŸ¨r, hrâŸ© := hf in hr.continuous_on.continuous_at (emetric.ball_mem_nhds x (hr.r_pos))

lemma analytic_at.continuous_at (hf : analytic_at ğ•œ f x) : continuous_at f x :=
let âŸ¨p, hpâŸ© := hf in hp.continuous_at

/-- In a complete space, the sum of a converging power series `p` admits `p` as a power series.
This is not totally obvious as we need to check the convergence of the series. -/
lemma formal_multilinear_series.has_fpower_series_on_ball [complete_space F]
  (p : formal_multilinear_series ğ•œ E F) (h : 0 < p.radius) :
  has_fpower_series_on_ball p.sum p 0 p.radius :=
{ r_le    := le_refl _,
  r_pos   := h,
  has_sum := Î» y hy, begin
    rw zero_add,
    replace hy : (nnnorm y : ennreal) < p.radius,
      by { convert hy, exact (edist_eq_coe_nnnorm _).symm },
    obtain âŸ¨a, C, ha, hCâŸ© : âˆƒ a C, a < 1 âˆ§ âˆ€ n, nnnorm (p n) * (nnnorm y)^n â‰¤ C * a^n :=
      p.geometric_bound_of_lt_radius hy,
    refine (summable_of_norm_bounded (Î» n, (C : â„) * a ^ n)
      ((summable_geometric a.2 ha).mul_left _) (Î» n, _)).has_sum,
    calc âˆ¥(p n) (Î» (i : fin n), y)âˆ¥
      â‰¤ âˆ¥p nâˆ¥ * (finset.univ.prod (Î» i : fin n, âˆ¥yâˆ¥)) : continuous_multilinear_map.le_op_norm _ _
      ... = nnnorm (p n) * (nnnorm y)^n : by simp
      ... â‰¤ C * a ^ n : by exact_mod_cast hC n
  end }

lemma has_fpower_series_on_ball.sum [complete_space F] (h : has_fpower_series_on_ball f p x r)
  {y : E} (hy : y âˆˆ emetric.ball (0 : E) r) : f (x + y) = p.sum y :=
begin
  have A := h.has_sum hy,
  have B := (p.has_fpower_series_on_ball h.radius_pos).has_sum (lt_of_lt_of_le hy h.r_le),
  simpa using has_sum_unique A B
end

/-- The sum of a converging power series is continuous in its disk of convergence. -/
lemma formal_multilinear_series.continuous_on [complete_space F] :
  continuous_on p.sum (emetric.ball 0 p.radius) :=
begin
  by_cases h : 0 < p.radius,
  { exact (p.has_fpower_series_on_ball h).continuous_on },
  { simp at h,
    simp [h, continuous_on_empty] }
end
