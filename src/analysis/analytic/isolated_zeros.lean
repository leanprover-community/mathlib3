/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
import analysis.calculus.dslope
import analysis.calculus.fderiv_analytic
import analysis.complex.basic

/-!
# Principle of isolated zeros

This file proves the fact that the zeros of a non-constant analytic function of one variable are
isolated. It also introduces a little bit of API in the `formal_multilinear_series` namespace that
is specific to this setup.

## Main definitions

When `p` is a formal multilinear series from `ğ•œ` to `E`:
* `p.coef n` is the `n`th coefficient of `p` seen as a power series in `ğ•œ`, which is equal to `p n
  (Î» i : fin n, 1)`;
* `p.order` (taking values in `with_top â„•`) is the index of the first non-zero coefficient of the
  series, or `âŠ¤` it `p n` is identically `0`. This is the order of the zero of an analytic function
  `f` at a point if `p` is the Taylor series of `f` at that point;
* `p.fslope` is the series obtained by dropping the term of order `0` and dividing by the parameter,
  corresponding to the Taylor series of `dslope f` if `p` is the Taylor series of `f`.

## Main results

* `has_fpower_series_at_iff` states that `has_fpower_series_at f p zâ‚€` is equivalent to `f` being
  locally the sum of `p`, in the sense that `âˆ€á¶  z in ğ“ 0, has_sum (Î» n, p n (Î» _, z)) (f (zâ‚€ + z))`
  (this version is easier to work with in some setups).
* `analytic_at.eventually_eq_zero_or_eventually_ne_zero` is the main statement that if a function is
  analytic at `zâ‚€`, then either it is identically zero in a neighborhood of `zâ‚€`, or it does not
  vanish in a punctured neighborhood of `zâ‚€`.
-/

open filter function nat
open_locale topological_space big_operators

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {s : E}
  {p : formal_multilinear_series ğ•œ ğ•œ E} {f : ğ•œ â†’ E}
  {n : â„•} {z zâ‚€ : ğ•œ} {y : fin n â†’ ğ•œ}

namespace formal_multilinear_series

/-- The `n`th coefficient of `p` when seen as a power series. -/
def coef (p : formal_multilinear_series ğ•œ ğ•œ E) (n : â„•) : E := p n 1

@[simp] lemma apply_eq_prod_smul_coef : p n y = (âˆ i, y i) â€¢ p.coef n :=
begin
  convert (p n).to_multilinear_map.map_smul_univ y 1,
  funext; simp only [pi.one_apply, algebra.id.smul_eq_mul, mul_one],
end

@[simp] lemma apply_eq_pow_smul_coef : p n (Î» _, z) = z ^ n â€¢ coef p n :=
by simp

@[simp] lemma norm_eq_norm_coef : âˆ¥p nâˆ¥ = âˆ¥coef p nâˆ¥ :=
begin
  apply le_antisymm,
  { refine (p n).op_norm_le_bound (norm_nonneg (coef p n)) (Î» y, _); simp [norm_smul, mul_comm] },
  { apply le_of_le_of_eq ((p n).le_op_norm 1); simp }
end

/-- The index of the first non-zero coefficient in `p`. -/
noncomputable def order (p : formal_multilinear_series ğ•œ ğ•œ E) : with_top â„• :=
by classical; exact dite (âˆ€ n, p.coef n = 0) (Î» _, âŠ¤) (Î» h, nat.find (not_forall.mp h))

lemma order_eq_zero_iff : p.order = 0 â†” p.coef 0 â‰  0 :=
by { by_cases (âˆ€ n, coef p n = 0); simp [order, h] }

lemma order_eq_top_iff : p.order = âŠ¤ â†” âˆ€ n, p.coef n = 0 :=
by { by_cases (âˆ€ n, coef p n = 0); simp [order, h] }

lemma coef_order_ne_zero {n : â„•} (ho : p.order = n) : p.coef n â‰  0 :=
begin
  by_cases (âˆ€ n, coef p n = 0),
  { simp [order, h] at ho; cases ho },
  { simp [order, h] at ho,
    norm_cast at ho,
    exact ((nat.find_eq_iff _).mp ho).1 }
end

lemma coef_eq_0_of_lt_order {n : â„•} (ho : p.order = n) â¦ƒk : â„•â¦„ (hk : k < n) : p.coef k = 0 :=
begin
  by_cases (âˆ€ n, coef p n = 0),
  { exact h k },
  { simp [order, h] at ho,
    norm_cast at ho,
    exact not_not.mp (((nat.find_eq_iff _).mp ho).2 k hk) }
end

/-- The formal counterpart of `dslope`, corresponding to the expansion of `(f z - f 0) / z`. If `f`
has `p` as a power series, then `dslope f` has `fslope p` as a power series. -/
noncomputable def fslope (p : formal_multilinear_series ğ•œ ğ•œ E) : formal_multilinear_series ğ•œ ğ•œ E :=
  Î» n, (p (n + 1)).curry_left 1

@[simp] lemma coef_fslope (n : â„•) :
  p.fslope.coef n = p.coef (n + 1) :=
begin
  have : @fin.cons n (Î» _, ğ•œ) 1 (1 : fin n â†’ ğ•œ) = 1 := fin.cons_self_tail 1,
  simp only [fslope, coef, continuous_multilinear_map.curry_left_apply, this],
end

@[simp] lemma coef_iterate_fslope (k n : â„•) :
  (fslope^[k] p).coef n = p.coef (n + k) :=
by induction k with k ih generalizing p; refl <|> simpa [ih]

@[simp] lemma order_fslope (ho : p.coef 0 = 0) : p.fslope.order = p.order - 1 :=
begin
  suffices : p.order = p.fslope.order + 1,
  { by_cases (p.fslope.order = âŠ¤); { rw [this, h] <|> rw [this, â†with_top.coe_untop _ h], refl } },
  by_cases (âˆ€ n, p.coef n = 0),
  { have h1 : âˆ€ n, p.fslope.coef n = 0 := by simp only [h, coef_fslope, forall_const],
    simp only [order, h, h1, forall_const, dif_pos, with_top.top_add] },
  { have h2 : Â¬âˆ€ n, p.coef (n + 1) = 0 := Î» hn, h (Î» n, nat.cases_on n ho hn),
    simp only [order, h, h2, not_false_iff, coef_fslope, dif_neg],
    norm_cast,
    exact find_comp_succ _ _ (not_not.mpr ho) }
end

end formal_multilinear_series

namespace has_sum

variables {a : â„• â†’ E}

lemma has_sum_at_zero (a : â„• â†’ E) : has_sum (Î» n, (0:ğ•œ) ^ n â€¢ a n) (a 0) :=
by convert has_sum_single 0 (Î» b h, _); simp [nat.pos_of_ne_zero h] <|> simp

/-- A variant of the `has_sum` predicate that states a property of the sum rather than its value.
`has_sum_in f {s}` is equivalent to `has_sum f s`. -/
def has_sum_in (a : â„• â†’ E) (S : set E) : Prop := âˆƒ s âˆˆ S, has_sum a s

lemma factor (hs : has_sum (Î» m, z ^ m â€¢ a m) s) (ha : âˆ€ k < n, a k = 0) :
  has_sum_in (Î» m, z ^ m â€¢ a (m + n)) {t | z ^ n â€¢ t = s} :=
begin
  refine dite (n = 0) (Î» hn, by { subst n; simpa [has_sum_in] }) (Î» hn, _),
  replace hn := nat.pos_of_ne_zero hn,
  by_cases (z = 0),
  { have : s = 0 := hs.unique (by simpa [ha 0 hn, h] using has_sum_at_zero a),
    exact âŸ¨a n, by simp [h, hn, this], by simpa [h] using has_sum_at_zero (Î» m, a (m + n))âŸ© },
  { refine âŸ¨(z ^ n)â»Â¹ â€¢ s, by field_simp [smul_smul], _âŸ©,
    have h1 : âˆ‘ i in finset.range n, z ^ i â€¢ a i = 0,
      from finset.sum_eq_zero (Î» k hk, by simp [ha k (finset.mem_range.mp hk)]),
    have h2 : has_sum (Î» m, z ^ (m + n) â€¢ a (m + n)) s,
      by simpa [h1] using (has_sum_nat_add_iff' n).mpr hs,
    convert @has_sum.const_smul E â„• ğ•œ _ _ _ _ _ _ _ (zâ»Â¹ ^ n) h2,
    field_simp [pow_add, smul_smul], simp only [inv_pow] }
end

end has_sum

namespace has_fpower_series_at

open formal_multilinear_series emetric

/-- A function `f : ğ•œ â†’ E` has `p` as power series expansion at a point `zâ‚€` iff it is the sum of
`p` in a neighborhood of `zâ‚€`. This makes some proofs easier by hiding the fact that
`has_fpower_series_at` depends on `p.radius`. -/
lemma _root_.has_fpower_series_at_iff : has_fpower_series_at f p zâ‚€ â†”
  âˆ€á¶  z in ğ“ 0, has_sum (Î» n, z ^ n â€¢ p.coef n) (f (zâ‚€ + z)) :=
begin
  refine âŸ¨Î» âŸ¨r, r_le, r_pos, hâŸ©, eventually_of_mem (ball_mem_nhds 0 r_pos)
    (Î» _, by simpa using h), _âŸ©,
  simp only [metric.eventually_nhds_iff],
  rintro âŸ¨r, r_pos, hâŸ©,
  refine âŸ¨p.radius âŠ“ r.to_nnreal, by simp, _, _âŸ©,
  { simp only [r_pos.lt, lt_inf_iff, ennreal.coe_pos, real.to_nnreal_pos, and_true],
    obtain âŸ¨z, z_pos, le_zâŸ© := normed_field.exists_norm_lt ğ•œ r_pos.lt,
    have : (âˆ¥zâˆ¥â‚Š : ennreal) â‰¤ p.radius,
    by { simp only [dist_zero_right] at h,
      apply formal_multilinear_series.le_radius_of_tendsto,
      convert tendsto_norm.comp (h le_z).summable.tendsto_at_top_zero,
      funext; simp [norm_smul, mul_comm] },
    refine lt_of_lt_of_le _ this,
    simp only [ennreal.coe_pos],
    exact zero_lt_iff.mpr (nnnorm_ne_zero_iff.mpr (norm_pos_iff.mp z_pos)) },
  { simp only [mem_ball, lt_inf_iff, edist_lt_coe, apply_eq_pow_smul_coef, and_imp,
      dist_zero_right] at h âŠ¢,
    refine Î» y hyp hyr, h _,
    simpa [nndist_eq_nnnorm, real.lt_to_nnreal_iff_coe_lt] using hyr }
end

lemma _root_.has_fpower_series_at_iff' : has_fpower_series_at f p zâ‚€ â†”
  âˆ€á¶  z in ğ“ zâ‚€, has_sum (Î» n, (z - zâ‚€) ^ n â€¢ p.coef n) (f z) :=
begin
  rw has_fpower_series_at_iff,
  split; intro h,
  { have : tendsto (Î» z, z - zâ‚€) (ğ“ zâ‚€) (ğ“ 0) := sub_self zâ‚€ â–¸ filter.tendsto_id.sub_const zâ‚€,
    simpa using this.eventually h },
  { have : tendsto (Î» z, z + zâ‚€) (ğ“ 0) (ğ“ (0 + zâ‚€)) := filter.tendsto_id.add_const zâ‚€,
    rw [zero_add] at this,
    simpa [add_comm] using this.eventually h }
end

lemma locally_zero_of_order_eq_top' (hp : has_fpower_series_at f p zâ‚€) (h : p.order = âŠ¤) :
  âˆ€á¶  z in ğ“ 0, f (zâ‚€ + z) = 0 :=
begin
  have : âˆ€á¶  z in ğ“ 0, has_sum (Î» n, (0:E)) (f (zâ‚€ + z)),
    by simpa [has_fpower_series_at_iff, order_eq_top_iff.mp h] using hp,
  exact this.mono (Î» x hx, has_sum.unique hx has_sum_zero)
end

lemma locally_zero_of_order_eq_top (hp : has_fpower_series_at f p zâ‚€) (h : p.order = âŠ¤) :
  âˆ€á¶  z in ğ“ zâ‚€, f z = 0 :=
begin
  have : tendsto (Î» z, z - zâ‚€) (ğ“ zâ‚€) (ğ“ 0) := sub_self zâ‚€ â–¸ filter.tendsto_id.sub_const zâ‚€,
  simpa using this.eventually (locally_zero_of_order_eq_top' hp h)
end

lemma has_fpower_series_dslope_fslope (hp : has_fpower_series_at f p zâ‚€) :
  has_fpower_series_at (dslope f zâ‚€) p.fslope zâ‚€ :=
begin
  have hpd : deriv f zâ‚€ = p.coef 1 := hp.deriv,
  have hp0 : p.coef 0 = f zâ‚€ := hp.coeff_zero 1,
  simp only [has_fpower_series_at_iff, apply_eq_pow_smul_coef, coef_fslope] at hp âŠ¢,
  refine hp.mono (Î» x hx, _),
  by_cases x = 0,
  { convert has_sum_single 0 _; intros; simp [*] },
  { have hxx : âˆ€ (n : â„•), xâ»Â¹ * x ^ (n + 1) = x ^ n := Î» n, by field_simp [h, pow_succ'],
    suffices : has_sum (Î» n, xâ»Â¹ â€¢ x ^ (n + 1) â€¢ p.coef (n + 1)) (xâ»Â¹ â€¢ (f (zâ‚€ + x) - f zâ‚€)),
    { simpa [dslope, slope, h, smul_smul, hxx] using this },
    { simpa [hp0] using ((has_sum_nat_add_iff' 1).mpr hx).const_smul } }
end

lemma has_fpower_series_iterate_dslope_fslope (n : â„•) (hp : has_fpower_series_at f p zâ‚€) :
  has_fpower_series_at ((swap dslope zâ‚€)^[n] f) (fslope^[n] p) zâ‚€ :=
begin
  induction n with n ih generalizing f p,
  { exact hp },
  { simpa using ih (has_fpower_series_dslope_fslope hp) }
end

lemma iterate_dslope_fslope_ne_zero (hp : has_fpower_series_at f p zâ‚€) {n : â„•} (h : p.order = n) :
  (swap dslope zâ‚€)^[n] f zâ‚€ â‰  0 :=
begin
  rw [â† coeff_zero (has_fpower_series_iterate_dslope_fslope n hp) 1],
  simpa using coef_order_ne_zero h,
end

lemma eq_pow_order_mul_iterate_dslope (hp : has_fpower_series_at f p zâ‚€) {n : â„•} (h : p.order = n) :
  âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ ((swap dslope zâ‚€)^[n] f z) :=
begin
  have hq := has_fpower_series_at_iff'.mp (has_fpower_series_iterate_dslope_fslope n hp),
  apply (hq.and (has_fpower_series_at_iff'.mp hp)).mono,
  rintro x âŸ¨hx1, hx2âŸ©,
  obtain âŸ¨s, hs1, hs2âŸ© := has_sum.factor hx2 (coef_eq_0_of_lt_order h),
  convert hs1.symm,
  simp only [coef_iterate_fslope] at hx1,
  exact hx1.unique hs2
end

lemma locally_ne_zero_aux (hp : has_fpower_series_at f p zâ‚€) {n : â„•} (h : p.order = n) :
  âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0 :=
begin
  have h1 := set.mem_compl_singleton_iff.mpr (iterate_dslope_fslope_ne_zero hp h),
  have h2 := (has_fpower_series_iterate_dslope_fslope n hp).continuous_at.tendsto,
  have h3 := h2.eventually (is_open_compl_singleton.eventually_mem h1),
  refine eventually_nhds_within_iff.mpr ((h3.and (eq_pow_order_mul_iterate_dslope hp h)).mono _),
  rintro x âŸ¨ha, hbâŸ© hc,
  simp only [â† @sub_eq_zero _ _ x zâ‚€, set.mem_compl_eq, set.mem_singleton_iff] at ha hc,
  simp [ha, hb, hc, pow_ne_zero],
end

end has_fpower_series_at

namespace analytic_at

/-- The *principle of isolated zeros* for an analytic function, local version: if a function is
analytic at `zâ‚€`, then either it is identically zero in a neighborhood of `zâ‚€`, or it does not
vanish in a punctured neighborhood of `zâ‚€`. -/
theorem eventually_eq_zero_or_eventually_ne_zero (hf : analytic_at ğ•œ f zâ‚€) :
  (âˆ€á¶  z in ğ“ zâ‚€, f z = 0) âˆ¨ (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0) :=
begin
  rcases hf with âŸ¨p, hpâŸ©,
  by_cases (p.order = âŠ¤),
  { simpa using or.inl (hp.locally_zero_of_order_eq_top h) },
  { let o := with_top.untop _ h,
    have : p.order = o := by simp only [with_top.coe_untop],
    exact or.inr (hp.locally_ne_zero_aux this) }
end

end analytic_at
