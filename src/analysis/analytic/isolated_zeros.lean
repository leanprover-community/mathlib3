/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
import analysis.analytic.basic
import analysis.calculus.dslope
import analysis.calculus.fderiv_analytic
import analysis.calculus.formal_multilinear_series
import analysis.analytic.uniqueness

/-!
# Principle of isolated zeros

This file proves the fact that the zeros of a non-constant analytic function of one variable are
isolated. It also introduces a little bit of API in the `has_fpower_series_at` namespace that is
useful in this setup.

## Main results

* `analytic_at.eventually_eq_zero_or_eventually_ne_zero` is the main statement that if a function is
  analytic at `zâ‚€`, then either it is identically zero in a neighborhood of `zâ‚€`, or it does not
  vanish in a punctured neighborhood of `zâ‚€`.
* `analytic_on.eq_on_of_preconnected_of_frequently_eq` is the identity theorem for analytic
  functions: if a function `f` is analytic on a connected set `U` and is zero on a set with an
  accumulation point in `U` then `f` is identically `0` on `U`.
-/

open_locale classical

open filter function nat formal_multilinear_series emetric set
open_locale topology big_operators

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E] {s : E}
  {p q : formal_multilinear_series ğ•œ ğ•œ E} {f g : ğ•œ â†’ E}
  {n : â„•} {z zâ‚€ : ğ•œ} {y : fin n â†’ ğ•œ}

namespace has_sum

variables {a : â„• â†’ E}

lemma has_sum_at_zero (a : â„• â†’ E) : has_sum (Î» n, (0:ğ•œ) ^ n â€¢ a n) (a 0) :=
by convert has_sum_single 0 (Î» b h, _); simp [nat.pos_of_ne_zero h] <|> simp

lemma exists_has_sum_smul_of_apply_eq_zero (hs : has_sum (Î» m, z ^ m â€¢ a m) s)
  (ha : âˆ€ k < n, a k = 0) :
  âˆƒ t : E, z ^ n â€¢ t = s âˆ§ has_sum (Î» m, z ^ m â€¢ a (m + n)) t :=
begin
  obtain rfl | hn := n.eq_zero_or_pos,
  { simpa },
  by_cases h : z = 0,
  { have : s = 0 := hs.unique (by simpa [ha 0 hn, h] using has_sum_at_zero a),
    exact âŸ¨a n, by simp [h, hn, this], by simpa [h] using has_sum_at_zero (Î» m, a (m + n))âŸ© },
  { refine âŸ¨(z ^ n)â»Â¹ â€¢ s, by field_simp [smul_smul], _âŸ©,
    have h1 : âˆ‘ i in finset.range n, z ^ i â€¢ a i = 0,
      from finset.sum_eq_zero (Î» k hk, by simp [ha k (finset.mem_range.mp hk)]),
    have h2 : has_sum (Î» m, z ^ (m + n) â€¢ a (m + n)) s,
      by simpa [h1] using (has_sum_nat_add_iff' n).mpr hs,
    convert h2.const_smul (zâ»Â¹ ^ n),
    { field_simp [pow_add, smul_smul] },
    { simp only [inv_pow] } }
end

end has_sum

namespace has_fpower_series_at

lemma has_fpower_series_dslope_fslope (hp : has_fpower_series_at f p zâ‚€) :
  has_fpower_series_at (dslope f zâ‚€) p.fslope zâ‚€ :=
begin
  have hpd : deriv f zâ‚€ = p.coeff 1 := hp.deriv,
  have hp0 : p.coeff 0 = f zâ‚€ := hp.coeff_zero 1,
  simp only [has_fpower_series_at_iff, apply_eq_pow_smul_coeff, coeff_fslope] at hp âŠ¢,
  refine hp.mono (Î» x hx, _),
  by_cases h : x = 0,
  { convert has_sum_single 0 _; intros; simp [*] },
  { have hxx : âˆ€ (n : â„•), xâ»Â¹ * x ^ (n + 1) = x ^ n := Î» n, by field_simp [h, pow_succ'],
    suffices : has_sum (Î» n, xâ»Â¹ â€¢ x ^ (n + 1) â€¢ p.coeff (n + 1)) (xâ»Â¹ â€¢ (f (zâ‚€ + x) - f zâ‚€)),
    { simpa [dslope, slope, h, smul_smul, hxx] using this },
    { simpa [hp0] using ((has_sum_nat_add_iff' 1).mpr hx).const_smul xâ»Â¹ } }
end

lemma has_fpower_series_iterate_dslope_fslope (n : â„•) (hp : has_fpower_series_at f p zâ‚€) :
  has_fpower_series_at ((swap dslope zâ‚€)^[n] f) (fslope^[n] p) zâ‚€ :=
begin
  induction n with n ih generalizing f p,
  { exact hp },
  { simpa using ih (has_fpower_series_dslope_fslope hp) }
end

lemma iterate_dslope_fslope_ne_zero (hp : has_fpower_series_at f p zâ‚€) (h : p â‰  0) :
  (swap dslope zâ‚€)^[p.order] f zâ‚€ â‰  0 :=
begin
  rw [â† coeff_zero (has_fpower_series_iterate_dslope_fslope p.order hp) 1],
  simpa [coeff_eq_zero] using apply_order_ne_zero h
end

lemma eq_pow_order_mul_iterate_dslope (hp : has_fpower_series_at f p zâ‚€) :
  âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ p.order â€¢ ((swap dslope zâ‚€)^[p.order] f z) :=
begin
  have hq := has_fpower_series_at_iff'.mp (has_fpower_series_iterate_dslope_fslope p.order hp),
  filter_upwards [hq, has_fpower_series_at_iff'.mp hp] with x hx1 hx2,
  have : âˆ€ k < p.order, p.coeff k = 0,
    from Î» k hk, by simpa [coeff_eq_zero] using apply_eq_zero_of_lt_order hk,
  obtain âŸ¨s, hs1, hs2âŸ© := has_sum.exists_has_sum_smul_of_apply_eq_zero hx2 this,
  convert hs1.symm,
  simp only [coeff_iterate_fslope] at hx1,
  exact hx1.unique hs2
end

lemma locally_ne_zero (hp : has_fpower_series_at f p zâ‚€) (h : p â‰  0) :
  âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0 :=
begin
  rw [eventually_nhds_within_iff],
  have h2 := (has_fpower_series_iterate_dslope_fslope p.order hp).continuous_at,
  have h3 := h2.eventually_ne (iterate_dslope_fslope_ne_zero hp h),
  filter_upwards [eq_pow_order_mul_iterate_dslope hp, h3] with z e1 e2 e3,
  simpa [e1, e2, e3] using pow_ne_zero p.order (sub_ne_zero.mpr e3),
end

lemma locally_zero_iff (hp : has_fpower_series_at f p zâ‚€) :
  (âˆ€á¶  z in ğ“ zâ‚€, f z = 0) â†” p = 0 :=
âŸ¨Î» hf, hp.eq_zero_of_eventually hf, Î» h, eventually_eq_zero (by rwa h at hp)âŸ©

end has_fpower_series_at

namespace analytic_at

/-- The *principle of isolated zeros* for an analytic function, local version: if a function is
analytic at `zâ‚€`, then either it is identically zero in a neighborhood of `zâ‚€`, or it does not
vanish in a punctured neighborhood of `zâ‚€`. -/
theorem eventually_eq_zero_or_eventually_ne_zero (hf : analytic_at ğ•œ f zâ‚€) :
  (âˆ€á¶  z in ğ“ zâ‚€, f z = 0) âˆ¨ (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0) :=
begin
  rcases hf with âŸ¨p, hpâŸ©,
  by_cases h : p = 0,
  { exact or.inl (has_fpower_series_at.eventually_eq_zero (by rwa h at hp)) },
  { exact or.inr (hp.locally_ne_zero h) }
end

lemma eventually_eq_or_eventually_ne (hf : analytic_at ğ•œ f zâ‚€) (hg : analytic_at ğ•œ g zâ‚€) :
  (âˆ€á¶  z in ğ“ zâ‚€, f z = g z) âˆ¨ (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  g z) :=
by simpa [sub_eq_zero] using (hf.sub hg).eventually_eq_zero_or_eventually_ne_zero

lemma frequently_zero_iff_eventually_zero {f : ğ•œ â†’ E} {w : ğ•œ} (hf : analytic_at ğ•œ f w) :
  (âˆƒá¶  z in ğ“[â‰ ] w, f z = 0) â†” (âˆ€á¶  z in ğ“ w, f z = 0) :=
âŸ¨hf.eventually_eq_zero_or_eventually_ne_zero.resolve_right,
  Î» h, (h.filter_mono nhds_within_le_nhds).frequentlyâŸ©

lemma frequently_eq_iff_eventually_eq (hf : analytic_at ğ•œ f zâ‚€) (hg : analytic_at ğ•œ g zâ‚€) :
  (âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = g z) â†” (âˆ€á¶  z in ğ“ zâ‚€, f z = g z) :=
by simpa [sub_eq_zero] using frequently_zero_iff_eventually_zero (hf.sub hg)

end analytic_at

namespace analytic_on

variables {U : set ğ•œ}

/-- The *principle of isolated zeros* for an analytic function, global version: if a function is
analytic on a connected set `U` and vanishes in arbitrary neighborhoods of a point `zâ‚€ âˆˆ U`, then
it is identically zero in `U`.
For higher-dimensional versions requiring that the function vanishes in a neighborhood of `zâ‚€`,
see `eq_on_zero_of_preconnected_of_eventually_eq_zero`. -/
theorem eq_on_zero_of_preconnected_of_frequently_eq_zero
  (hf : analytic_on ğ•œ f U) (hU : is_preconnected U)
  (hâ‚€ : zâ‚€ âˆˆ U) (hfw : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = 0) :
  eq_on f 0 U :=
hf.eq_on_zero_of_preconnected_of_eventually_eq_zero hU hâ‚€
  ((hf zâ‚€ hâ‚€).frequently_zero_iff_eventually_zero.1 hfw)

theorem eq_on_zero_of_preconnected_of_mem_closure (hf : analytic_on ğ•œ f U) (hU : is_preconnected U)
  (hâ‚€ : zâ‚€ âˆˆ U) (hfzâ‚€ : zâ‚€ âˆˆ closure ({z | f z = 0} \ {zâ‚€})) :
  eq_on f 0 U :=
hf.eq_on_zero_of_preconnected_of_frequently_eq_zero hU hâ‚€
  (mem_closure_ne_iff_frequently_within.mp hfzâ‚€)

/-- The *identity principle* for analytic functions, global version: if two functions are
analytic on a connected set `U` and coincide at points which accumulate to a point `zâ‚€ âˆˆ U`, then
they coincide globally in `U`.
For higher-dimensional versions requiring that the functions coincide in a neighborhood of `zâ‚€`,
see `eq_on_of_preconnected_of_eventually_eq`. -/
theorem eq_on_of_preconnected_of_frequently_eq (hf : analytic_on ğ•œ f U) (hg : analytic_on ğ•œ g U)
  (hU : is_preconnected U) (hâ‚€ : zâ‚€ âˆˆ U) (hfg : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = g z) :
  eq_on f g U :=
begin
  have hfg' : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, (f - g) z = 0 := hfg.mono (Î» z h, by rw [pi.sub_apply, h, sub_self]),
  simpa [sub_eq_zero] using
    Î» z hz, (hf.sub hg).eq_on_zero_of_preconnected_of_frequently_eq_zero hU hâ‚€ hfg' hz
end

theorem eq_on_of_preconnected_of_mem_closure (hf : analytic_on ğ•œ f U) (hg : analytic_on ğ•œ g U)
  (hU : is_preconnected U) (hâ‚€ : zâ‚€ âˆˆ U) (hfg : zâ‚€ âˆˆ closure ({z | f z = g z} \ {zâ‚€})) :
  eq_on f g U :=
hf.eq_on_of_preconnected_of_frequently_eq hg hU hâ‚€ (mem_closure_ne_iff_frequently_within.mp hfg)

end analytic_on
