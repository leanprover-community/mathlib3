/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury G. Kudryashov
-/
import analysis.analytic.basic

/-!
# Linear functions are analytic

In this file we prove that a `continuous_linear_map` defines an analytic function with
the formal power series `f x = f a + f (x - a)`.
-/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{F : Type*} [normed_group F] [normed_space ğ•œ F]

open_locale topological_space classical big_operators nnreal ennreal
open set filter asymptotics

noncomputable theory

namespace continuous_linear_map

/-- Formal power series of a continuous linear map `f : E â†’L[ğ•œ] F` at `x : E`:
`f y = f x + f (y - x)`. -/
@[simp] def fpower_series (f : E â†’L[ğ•œ] F) (x : E) : formal_multilinear_series ğ•œ E F
| 0 := continuous_multilinear_map.curry0 ğ•œ _ (f x)
| 1 := (continuous_multilinear_curry_fin1 ğ•œ _ _).symm f
| _ := 0

@[simp] lemma fpower_series_apply_add_two (f : E â†’L[ğ•œ] F) (x : E) (n : â„•) :
  f.fpower_series x (n + 2) = 0 := rfl

@[simp] lemma fpower_series_radius (f : E â†’L[ğ•œ] F) (x : E) : (f.fpower_series x).radius = âˆ :=
(f.fpower_series x).radius_eq_top_of_forall_image_add_eq_zero 2 $ Î» n, rfl

protected theorem has_fpower_series_on_ball (f : E â†’L[ğ•œ] F) (x : E) :
  has_fpower_series_on_ball f (f.fpower_series x) x âˆ :=
{ r_le := by simp,
  r_pos := ennreal.coe_lt_top,
  has_sum := Î» y _, (has_sum_nat_add_iff' 2).1 $
    by simp [finset.sum_range_succ, â† sub_sub, has_sum_zero] }

protected theorem has_fpower_series_at (f : E â†’L[ğ•œ] F) (x : E) :
  has_fpower_series_at f (f.fpower_series x) x :=
âŸ¨âˆ, f.has_fpower_series_on_ball xâŸ©

protected theorem analytic_at (f : E â†’L[ğ•œ] F) (x : E) : analytic_at ğ•œ f x :=
(f.has_fpower_series_at x).analytic_at

end continuous_linear_map
