/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury Kudryashov
-/
import analysis.analytic.basic
import analysis.special_functions.pow

/-!
# Representation of `formal_multilinear_series.radius` as a `liminf`

In this file we prove that the radius of convergence of a `formal_multilinear_series` is equal to
$\liminf_{n\to\infty} \frac{1}{\sqrt[n]{âˆ¥p nâˆ¥}}$. This lemma can't go to `basic.lean` because this
would create a circular dependency once we redefine `exp` using `formal_multilinear_series`.
-/
variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{F : Type*} [normed_group F] [normed_space ğ•œ F]

open_locale topological_space classical big_operators nnreal
open filter asymptotics

namespace formal_multilinear_series

variables (p : formal_multilinear_series ğ•œ E F)

/-- The radius of a formal multilinear series is equal to
$\liminf_{n\to\infty} \frac{1}{\sqrt[n]{âˆ¥p nâˆ¥}}$. The actual statement uses `â„â‰¥0` and some
coercions. -/
lemma radius_eq_liminf : p.radius = liminf at_top (Î» n, 1/((nnnorm (p n)) ^ (1 / (n : â„)) : â„â‰¥0)) :=
begin
  have : âˆ€ (r : â„â‰¥0) {n : â„•}, 0 < n â†’
    ((r : ennreal) â‰¤ 1 / â†‘(nnnorm (p n) ^ (1 / (n : â„))) â†” nnnorm (p n) * r ^ n â‰¤ 1),
  { intros r n hn,
    have : 0 < (n : â„) := nat.cast_pos.2 hn,
    conv_lhs {rw [ennreal.div_def, one_mul, ennreal.le_inv_iff_mul_le, â† ennreal.coe_mul,
      ennreal.coe_le_one_iff, one_div, â† nnreal.rpow_one r, â† mul_inv_cancel this.ne',
      nnreal.rpow_mul, â† nnreal.mul_rpow, â† nnreal.one_rpow (nâ»Â¹),
      nnreal.rpow_le_rpow_iff (inv_pos.2 this), mul_comm, nnreal.rpow_nat_cast] } },
  apply le_antisymm;
    refine le_of_forall_ge_of_dense (Î» r hr, _); lift r to â„â‰¥0 using (ne_top_of_lt hr),
  { rcases ((p.lt_radius_tfae' r).out 0 3).mp (p.geometric_bound_of_lt_radius hr) with âŸ¨a, ha, HâŸ©,
    refine le_Liminf_of_le (by apply_auto_param) (eventually_map.2 $ _),
    refine H.mp ((eventually_gt_at_top 0).mono $ Î» n hnâ‚€ hn, (this _ hnâ‚€).2 (hn.trans _)),
    exact pow_le_one _ ha.1.le ha.2.le },
  { refine p.le_radius_of_is_O (is_O.of_bound 1 _),
    refine (eventually_lt_of_lt_liminf hr).mp ((eventually_gt_at_top 0).mono (Î» n hnâ‚€ hn, _)),
    simpa using nnreal.coe_le_coe.2 ((this _ hnâ‚€).1 hn.le) }
end

end formal_multilinear_series
