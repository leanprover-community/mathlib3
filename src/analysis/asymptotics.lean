/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Jeremy Avigad

We introduce these relations:

  `is_O f g l` : "f is big O of g along l"
  `is_o f g l` : "f is little o of g along l"

Here `l` is any filter on the domain of `f` and `g`, which are assumed to be the same. The codomains
of `f` and `g` do not need to be the same; all that is needed that there is a norm associated with
these types, and it is the norm that is compared asymptotically.

Often the ranges of `f` and `g` will be the real numbers, in which case the norm is the absolute
value. In general, we have

  `is_O f g l ‚Üî is_O (Œª x, ‚à•f x‚à•) (Œª x, ‚à•g x‚à•) l`,

and similarly for `is_o`. But our setup allows us to use the notions e.g. with functions
to the integers, rationals, complex numbers, or any normed vector space without mentioning the
norm explicitly.

If `f` and `g` are functions to a normed field like the reals or complex numbers and `g` is always
nonzero, we have

  `is_o f g l ‚Üî tendsto (Œª x, f x / (g x)) (ùìù 0) l`.

In fact, the right-to-left direction holds without the hypothesis on `g`, and in the other direction
it suffices to assume that `f` is zero wherever `g` is. (This generalization is useful in defining
the Fr√©chet derivative.)
-/
import analysis.normed_space.basic
open filter
open_locale topological_space

namespace asymptotics

variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}

section
variables [has_norm Œ≤] [has_norm Œ≥] [has_norm Œ¥]

def is_O (f : Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≥) (l : filter Œ±) : Prop :=
‚àÉ c > 0, { x | ‚à• f x ‚à• ‚â§ c * ‚à• g x ‚à• } ‚àà l

def is_o (f : Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≥) (l : filter Œ±) : Prop :=
‚àÄ c > 0, { x | ‚à• f x ‚à• ‚â§  c * ‚à• g x ‚à• } ‚àà l

theorem is_O_refl (f : Œ± ‚Üí Œ≤) (l : filter Œ±) : is_O f f l :=
‚ü®1, zero_lt_one, by { filter_upwards [univ_mem_sets], intros x _, simp }‚ü©

theorem is_O.comp {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (hfg : is_O f g l)
    {Œ¥ : Type*} (k : Œ¥ ‚Üí Œ±) :
  is_O (f ‚àò k) (g ‚àò k) (l.comap k) :=
let ‚ü®c, cpos, hfgc‚ü© := hfg in
‚ü®c, cpos, mem_comap_sets.mpr ‚ü®_, hfgc, set.subset.refl _‚ü©‚ü©

theorem is_o.comp {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (hfg : is_o f g l)
    {Œ¥ : Type*} (k : Œ¥ ‚Üí Œ±) :
  is_o (f ‚àò k) (g ‚àò k) (l.comap k) :=
Œª c cpos, mem_comap_sets.mpr ‚ü®_, hfg c cpos, set.subset.refl _‚ü©

theorem is_O.mono {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l‚ÇÅ l‚ÇÇ : filter Œ±} (h : l‚ÇÅ ‚â§ l‚ÇÇ)
  (h' : is_O f g l‚ÇÇ) : is_O f g l‚ÇÅ :=
let ‚ü®c, cpos, h'c‚ü© := h' in ‚ü®c, cpos, h (h'c)‚ü©

theorem is_o.mono {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l‚ÇÅ l‚ÇÇ : filter Œ±} (h : l‚ÇÅ ‚â§ l‚ÇÇ)
  (h' : is_o f g l‚ÇÇ) : is_o f g l‚ÇÅ :=
Œª c cpos, h (h' c cpos)

theorem is_o.to_is_O {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (hgf : is_o f g l) : is_O f g l :=
‚ü®1, zero_lt_one, hgf 1 zero_lt_one‚ü©

theorem is_O.trans {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {k : Œ± ‚Üí Œ¥} {l : filter Œ±}
    (h‚ÇÅ : is_O f g l) (h‚ÇÇ : is_O g k l) :
  is_O f k l :=
let ‚ü®c,  cpos,  hc‚ü©  := h‚ÇÅ,
    ‚ü®c', c'pos, hc'‚ü© := h‚ÇÇ in
begin
  use [c * c', mul_pos cpos c'pos],
  filter_upwards [hc, hc'], dsimp,
  intros x h‚ÇÅx h‚ÇÇx, rw mul_assoc,
  exact le_trans h‚ÇÅx (mul_le_mul_of_nonneg_left h‚ÇÇx (le_of_lt cpos))
end

theorem is_o.trans_is_O {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {k : Œ± ‚Üí Œ¥} {l : filter Œ±}
    (h‚ÇÅ : is_o f g l) (h‚ÇÇ : is_O g k l) :
  is_o f k l :=
begin
  intros c cpos,
  rcases h‚ÇÇ with ‚ü®c', c'pos, hc'‚ü©,
  have cc'pos := div_pos cpos c'pos,
  filter_upwards [h‚ÇÅ (c / c') cc'pos, hc'], dsimp,
  intros x h‚ÇÅx h‚ÇÇx,
  refine le_trans h‚ÇÅx (le_trans (mul_le_mul_of_nonneg_left h‚ÇÇx (le_of_lt cc'pos)) _),
  rw [‚Üêmul_assoc, div_mul_cancel _ (ne_of_gt c'pos)]
end

theorem is_O.trans_is_o {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {k : Œ± ‚Üí Œ¥} {l : filter Œ±}
    (h‚ÇÅ : is_O f g l) (h‚ÇÇ : is_o g k l) :
  is_o f k l :=
begin
  intros c cpos,
  rcases h‚ÇÅ with ‚ü®c', c'pos, hc'‚ü©,
  have cc'pos := div_pos cpos c'pos,
  filter_upwards [hc', h‚ÇÇ (c / c') cc'pos], dsimp,
  intros x h‚ÇÅx h‚ÇÇx,
  refine le_trans h‚ÇÅx (le_trans (mul_le_mul_of_nonneg_left h‚ÇÇx (le_of_lt c'pos)) _),
  rw [‚Üêmul_assoc, mul_div_cancel' _ (ne_of_gt c'pos)]
end

theorem is_o.trans {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {k : Œ± ‚Üí Œ¥} {l : filter Œ±}
    (h‚ÇÅ : is_o f g l) (h‚ÇÇ : is_o g k l) :
  is_o f k l :=
h‚ÇÅ.to_is_O.trans_is_o h‚ÇÇ

theorem is_o_join {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l‚ÇÅ l‚ÇÇ : filter Œ±}
    (h‚ÇÅ : is_o f g l‚ÇÅ) (h‚ÇÇ : is_o f g l‚ÇÇ) :
  is_o f g (l‚ÇÅ ‚äî l‚ÇÇ) :=
begin
  intros c cpos,
  rw mem_sup_sets,
  exact ‚ü®h‚ÇÅ c cpos, h‚ÇÇ c cpos‚ü©
end

theorem is_O_congr {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (hf : {x | f‚ÇÅ x = f‚ÇÇ x} ‚àà l) (hg : {x | g‚ÇÅ x = g‚ÇÇ x} ‚àà l) :
  is_O f‚ÇÅ g‚ÇÅ l ‚Üî is_O f‚ÇÇ g‚ÇÇ l :=
bex_congr $ Œª c _, filter.congr_sets $
by filter_upwards [hf, hg] Œª x e‚ÇÅ e‚ÇÇ,
  by dsimp at e‚ÇÅ e‚ÇÇ ‚ä¢; rw [e‚ÇÅ, e‚ÇÇ]

theorem is_o_congr {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (hf : {x | f‚ÇÅ x = f‚ÇÇ x} ‚àà l) (hg : {x | g‚ÇÅ x = g‚ÇÇ x} ‚àà l) :
  is_o f‚ÇÅ g‚ÇÅ l ‚Üî is_o f‚ÇÇ g‚ÇÇ l :=
ball_congr $ Œª c _, filter.congr_sets $
by filter_upwards [hf, hg] Œª x e‚ÇÅ e‚ÇÇ,
  by dsimp at e‚ÇÅ e‚ÇÇ ‚ä¢; rw [e‚ÇÅ, e‚ÇÇ]

theorem is_O.congr {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (hf : ‚àÄ x, f‚ÇÅ x = f‚ÇÇ x) (hg : ‚àÄ x, g‚ÇÅ x = g‚ÇÇ x) :
  is_O f‚ÇÅ g‚ÇÅ l ‚Üí is_O f‚ÇÇ g‚ÇÇ l :=
(is_O_congr (univ_mem_sets' hf) (univ_mem_sets' hg)).1

theorem is_o.congr {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (hf : ‚àÄ x, f‚ÇÅ x = f‚ÇÇ x) (hg : ‚àÄ x, g‚ÇÅ x = g‚ÇÇ x) :
  is_o f‚ÇÅ g‚ÇÅ l ‚Üí is_o f‚ÇÇ g‚ÇÇ l :=
(is_o_congr (univ_mem_sets' hf) (univ_mem_sets' hg)).1

theorem is_O_congr_left {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h : {x | f‚ÇÅ x = f‚ÇÇ x} ‚àà l) :
  is_O f‚ÇÅ g l ‚Üî is_O f‚ÇÇ g l :=
is_O_congr h (univ_mem_sets' $ Œª _, rfl)

theorem is_o_congr_left {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h : {x | f‚ÇÅ x = f‚ÇÇ x} ‚àà l) :
  is_o f‚ÇÅ g l ‚Üî is_o f‚ÇÇ g l :=
is_o_congr h (univ_mem_sets' $ Œª _, rfl)

theorem is_O.congr_left {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
  (hf : ‚àÄ x, f‚ÇÅ x = f‚ÇÇ x) : is_O f‚ÇÅ g l ‚Üí is_O f‚ÇÇ g l :=
is_O.congr hf (Œª _, rfl)

theorem is_o.congr_left {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
  (hf : ‚àÄ x, f‚ÇÅ x = f‚ÇÇ x) : is_o f‚ÇÅ g l ‚Üí is_o f‚ÇÇ g l :=
is_o.congr hf (Œª _, rfl)

theorem is_O_congr_right {f : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h : {x | g‚ÇÅ x = g‚ÇÇ x} ‚àà l) :
  is_O f g‚ÇÅ l ‚Üî is_O f g‚ÇÇ l :=
is_O_congr (univ_mem_sets' $ Œª _, rfl) h

theorem is_o_congr_right {f : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h : {x | g‚ÇÅ x = g‚ÇÇ x} ‚àà l) :
  is_o f g‚ÇÅ l ‚Üî is_o f g‚ÇÇ l :=
is_o_congr (univ_mem_sets' $ Œª _, rfl) h

theorem is_O.congr_right {f : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
  (hg : ‚àÄ x, g‚ÇÅ x = g‚ÇÇ x) : is_O f g‚ÇÅ l ‚Üí is_O f g‚ÇÇ l :=
is_O.congr (Œª _, rfl) hg

theorem is_o.congr_right {f : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
  (hg : ‚àÄ x, g‚ÇÅ x = g‚ÇÇ x) : is_o f g‚ÇÅ l ‚Üí is_o f g‚ÇÇ l :=
is_o.congr (Œª _, rfl) hg

end

section
variables [has_norm Œ≤] [normed_group Œ≥] [normed_group Œ¥]

@[simp]
theorem is_O_norm_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O f (Œª x, ‚à•g x‚à•) l ‚Üî is_O f g l :=
by simp only [is_O, norm_norm]

@[simp]
theorem is_o_norm_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o f (Œª x, ‚à•g x‚à•) l ‚Üî is_o f g l :=
by simp only [is_o, norm_norm]

@[simp]
theorem is_O_neg_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O f (Œª x, -(g x)) l ‚Üî is_O f g l :=
by { rw ‚Üêis_O_norm_right, simp only [norm_neg], rw is_O_norm_right }

@[simp]
theorem is_o_neg_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o f (Œª x, -(g x)) l ‚Üî is_o f g l :=
by { rw ‚Üêis_o_norm_right, simp only [norm_neg], rw is_o_norm_right }

theorem is_O_iff {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O f g l ‚Üî ‚àÉ c, { x | ‚à•f x‚à• ‚â§ c * ‚à•g x‚à• } ‚àà l :=
suffices (‚àÉ c, { x | ‚à•f x‚à• ‚â§ c * ‚à•g x‚à• } ‚àà l) ‚Üí is_O f g l,
  from ‚ü®Œª ‚ü®c, cpos, hc‚ü©, ‚ü®c, hc‚ü©, this‚ü©,
assume ‚ü®c, hc‚ü©,
or.elim (lt_or_ge 0 c)
  (assume : c > 0, ‚ü®c, this, hc‚ü©)
  (assume h'c : c ‚â§ 0,
    have {x : Œ± | ‚à•f x‚à• ‚â§ 1 * ‚à•g x‚à•} ‚àà l,
      begin
        filter_upwards [hc], intros x,
        show ‚à•f x‚à• ‚â§ c * ‚à•g x‚à• ‚Üí ‚à•f x‚à• ‚â§ 1 * ‚à•g x‚à•,
        { intro hx, apply le_trans hx,
          apply mul_le_mul_of_nonneg_right _ (norm_nonneg _),
          show c ‚â§ 1, from le_trans h'c zero_le_one }
      end,
    ‚ü®1, zero_lt_one, this‚ü©)

theorem is_O_join {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l‚ÇÅ l‚ÇÇ : filter Œ±}
    (h‚ÇÅ : is_O f g l‚ÇÅ) (h‚ÇÇ : is_O f g l‚ÇÇ) :
  is_O f g (l‚ÇÅ ‚äî l‚ÇÇ) :=
begin
  rcases h‚ÇÅ with ‚ü®c‚ÇÅ, c‚ÇÅpos, hc‚ÇÅ‚ü©,
  rcases h‚ÇÇ with ‚ü®c‚ÇÇ, c‚ÇÇpos, hc‚ÇÇ‚ü©,
  have : 0 < max c‚ÇÅ c‚ÇÇ, by { rw lt_max_iff, left, exact c‚ÇÅpos },
  use [max c‚ÇÅ c‚ÇÇ, this],
  rw mem_sup_sets, split,
  { filter_upwards [hc‚ÇÅ], dsimp, intros x hx,
    exact le_trans hx (mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _)) },
  filter_upwards [hc‚ÇÇ], dsimp, intros x hx,
    exact le_trans hx (mul_le_mul_of_nonneg_right (le_max_right _ _) (norm_nonneg _))
end

lemma is_O.prod_rightl {f : Œ± ‚Üí Œ≤} {g‚ÇÅ : Œ± ‚Üí Œ≥} {g‚ÇÇ : Œ± ‚Üí Œ¥} {l : filter Œ±}
  (h : is_O f g‚ÇÅ l) : is_O f (Œªx, (g‚ÇÅ x, g‚ÇÇ x)) l :=
begin
  have : is_O g‚ÇÅ (Œªx, (g‚ÇÅ x, g‚ÇÇ x)) l :=
    ‚ü®1, zero_lt_one, filter.univ_mem_sets' (Œªx, by simp [norm, le_refl])‚ü©,
  exact is_O.trans h this
end

lemma is_O.prod_rightr {f : Œ± ‚Üí Œ≤} {g‚ÇÅ : Œ± ‚Üí Œ≥} {g‚ÇÇ : Œ± ‚Üí Œ¥} {l : filter Œ±}
  (h : is_O f g‚ÇÇ l) : is_O f (Œªx, (g‚ÇÅ x, g‚ÇÇ x)) l :=
begin
  have : is_O g‚ÇÇ (Œªx, (g‚ÇÅ x, g‚ÇÇ x)) l :=
    ‚ü®1, zero_lt_one, filter.univ_mem_sets' (Œªx, by simp [norm, le_refl])‚ü©,
  exact is_O.trans h this
end

lemma is_o.prod_rightl {f : Œ± ‚Üí Œ≤} {g‚ÇÅ : Œ± ‚Üí Œ≥} {g‚ÇÇ : Œ± ‚Üí Œ¥} {l : filter Œ±}
  (h : is_o f g‚ÇÅ l) : is_o f (Œªx, (g‚ÇÅ x, g‚ÇÇ x)) l :=
is_o.trans_is_O h (is_O.prod_rightl (is_O_refl g‚ÇÅ l))

lemma is_o.prod_rightr {f : Œ± ‚Üí Œ≤} {g‚ÇÅ : Œ± ‚Üí Œ≥} {g‚ÇÇ : Œ± ‚Üí Œ¥} {l : filter Œ±}
  (h : is_o f g‚ÇÇ l) : is_o f (Œªx, (g‚ÇÅ x, g‚ÇÇ x)) l :=
is_o.trans_is_O h (is_O.prod_rightr (is_O_refl g‚ÇÇ l))

end

section
variables [normed_group Œ≤] [normed_group Œ¥] [has_norm Œ≥]

@[simp] theorem is_O_norm_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O (Œª x, ‚à•f x‚à•) g l ‚Üî is_O f g l :=
by simp only [is_O, norm_norm]

@[simp] theorem is_o_norm_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o (Œª x, ‚à•f x‚à•) g l ‚Üî is_o f g l :=
by simp only [is_o, norm_norm]

@[simp] theorem is_O_neg_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O (Œª x, -f x) g l ‚Üî is_O f g l :=
by { rw ‚Üêis_O_norm_left, simp only [norm_neg], rw is_O_norm_left }

@[simp] theorem is_o_neg_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o (Œª x, -f x) g l ‚Üî is_o f g l :=
by { rw ‚Üêis_o_norm_left, simp only [norm_neg], rw is_o_norm_left }

theorem is_O.add {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h‚ÇÅ : is_O f‚ÇÅ g l) (h‚ÇÇ : is_O f‚ÇÇ g l) :
  is_O (Œª x, f‚ÇÅ x + f‚ÇÇ x) g l :=
let ‚ü®c‚ÇÅ, c‚ÇÅpos, hc‚ÇÅ‚ü© := h‚ÇÅ,
    ‚ü®c‚ÇÇ, c‚ÇÇpos, hc‚ÇÇ‚ü© := h‚ÇÇ in
begin
  use [c‚ÇÅ + c‚ÇÇ, add_pos c‚ÇÅpos c‚ÇÇpos],
  filter_upwards [hc‚ÇÅ, hc‚ÇÇ],
  intros x hx‚ÇÅ hx‚ÇÇ,
  show ‚à•f‚ÇÅ x + f‚ÇÇ x‚à• ‚â§ (c‚ÇÅ + c‚ÇÇ) * ‚à•g x‚à•,
  apply le_trans (norm_triangle _ _),
  rw add_mul,
  exact add_le_add hx‚ÇÅ hx‚ÇÇ
end

theorem is_o.add {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h‚ÇÅ : is_o f‚ÇÅ g l) (h‚ÇÇ : is_o f‚ÇÇ g l) :
  is_o (Œª x, f‚ÇÅ x + f‚ÇÇ x) g l :=
begin
  intros c cpos,
  filter_upwards [h‚ÇÅ (c / 2) (half_pos cpos), h‚ÇÇ (c / 2) (half_pos cpos)],
  intros x hx‚ÇÅ hx‚ÇÇ, dsimp at hx‚ÇÅ hx‚ÇÇ,
  apply le_trans (norm_triangle _ _),
  apply le_trans (add_le_add hx‚ÇÅ hx‚ÇÇ),
  rw [‚Üêmul_add, ‚Üêtwo_mul, ‚Üêmul_assoc, div_mul_cancel _ two_ne_zero]
end

theorem is_O.sub {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h‚ÇÅ : is_O f‚ÇÅ g l) (h‚ÇÇ : is_O f‚ÇÇ g l) :
  is_O (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l :=
h‚ÇÅ.add (is_O_neg_left.mpr h‚ÇÇ)

theorem is_o.sub {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h‚ÇÅ : is_o f‚ÇÅ g l) (h‚ÇÇ : is_o f‚ÇÇ g l) :
  is_o (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l :=
h‚ÇÅ.add (is_o_neg_left.mpr h‚ÇÇ)

theorem is_O_comm {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l ‚Üî is_O (Œª x, f‚ÇÇ x - f‚ÇÅ x) g l :=
by simpa using @is_O_neg_left _ _ _ _ _ (Œª x, f‚ÇÇ x - f‚ÇÅ x) g l

theorem is_O.symm {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l ‚Üí is_O (Œª x, f‚ÇÇ x - f‚ÇÅ x) g l :=
is_O_comm.1

theorem is_O.tri {f‚ÇÅ f‚ÇÇ f‚ÇÉ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h‚ÇÅ : is_O (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l)
    (h‚ÇÇ : is_O (Œª x, f‚ÇÇ x - f‚ÇÉ x) g l) :
  is_O (Œª x, f‚ÇÅ x - f‚ÇÉ x) g l :=
(h‚ÇÅ.add h‚ÇÇ).congr_left (by simp)

theorem is_O.congr_of_sub {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h : is_O (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l) :
  is_O f‚ÇÅ g l ‚Üî is_O f‚ÇÇ g l :=
‚ü®Œª h', (h'.sub h).congr_left (Œª x, sub_sub_cancel _ _),
 Œª h', (h.add h').congr_left (Œª x, sub_add_cancel _ _)‚ü©

theorem is_o_comm {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l ‚Üî is_o (Œª x, f‚ÇÇ x - f‚ÇÅ x) g l :=
by simpa using @is_o_neg_left _ _ _ _ _ (Œª x, f‚ÇÇ x - f‚ÇÅ x) g l

theorem is_o.symm {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l ‚Üí is_o (Œª x, f‚ÇÇ x - f‚ÇÅ x) g l :=
is_o_comm.1

theorem is_o.tri {f‚ÇÅ f‚ÇÇ f‚ÇÉ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h‚ÇÅ : is_o (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l)
    (h‚ÇÇ : is_o (Œª x, f‚ÇÇ x - f‚ÇÉ x) g l) :
  is_o (Œª x, f‚ÇÅ x - f‚ÇÉ x) g l :=
(h‚ÇÅ.add h‚ÇÇ).congr_left (by simp)

theorem is_o.congr_of_sub {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h : is_o (Œª x, f‚ÇÅ x - f‚ÇÇ x) g l) :
  is_o f‚ÇÅ g l ‚Üî is_o f‚ÇÇ g l :=
‚ü®Œª h', (h'.sub h).congr_left (Œª x, sub_sub_cancel _ _),
 Œª h', (h.add h').congr_left (Œª x, sub_add_cancel _ _)‚ü©

@[simp] theorem is_O_prod_left {f‚ÇÅ : Œ± ‚Üí Œ≤} {f‚ÇÇ : Œ± ‚Üí Œ¥} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O (Œªx, (f‚ÇÅ x, f‚ÇÇ x)) g l ‚Üî is_O f‚ÇÅ g l ‚àß is_O f‚ÇÇ g l :=
begin
  split,
  { assume h,
    split,
    { exact is_O.trans (is_O.prod_rightl (is_O_refl f‚ÇÅ l)) h },
    { exact is_O.trans (is_O.prod_rightr (is_O_refl f‚ÇÇ l)) h } },
  { rintros ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
    have : is_O (Œªx, ‚à•f‚ÇÅ x‚à• + ‚à•f‚ÇÇ x‚à•) g l :=
      is_O.add (is_O_norm_left.2 h‚ÇÅ) (is_O_norm_left.2 h‚ÇÇ),
    apply is_O.trans _ this,
    refine ‚ü®1, zero_lt_one, filter.univ_mem_sets' (Œªx, _)‚ü©,
    simp only [norm, max_le_iff, one_mul, set.mem_set_of_eq],
    split; exact le_trans (by simp) (le_abs_self _) }
end

@[simp] theorem is_o_prod_left {f‚ÇÅ : Œ± ‚Üí Œ≤} {f‚ÇÇ : Œ± ‚Üí Œ¥} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o (Œªx, (f‚ÇÅ x, f‚ÇÇ x)) g l ‚Üî is_o f‚ÇÅ g l ‚àß is_o f‚ÇÇ g l :=
begin
  split,
  { assume h,
    split,
    { exact is_O.trans_is_o (is_O.prod_rightl (is_O_refl f‚ÇÅ l)) h },
    { exact is_O.trans_is_o (is_O.prod_rightr (is_O_refl f‚ÇÇ l)) h } },
  { rintros ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
    have : is_o (Œªx, ‚à•f‚ÇÅ x‚à• + ‚à•f‚ÇÇ x‚à•) g l :=
      is_o.add (is_o_norm_left.2 h‚ÇÅ) (is_o_norm_left.2 h‚ÇÇ),
    apply is_O.trans_is_o _ this,
    refine ‚ü®1, zero_lt_one, filter.univ_mem_sets' (Œªx, _)‚ü©,
    simp only [norm, max_le_iff, one_mul, set.mem_set_of_eq],
    split; exact le_trans (by simp) (le_abs_self _) }
end

end

section
variables [normed_group Œ≤] [normed_group Œ≥]

theorem is_O_zero (g : Œ± ‚Üí Œ≥) (l : filter Œ±) :
  is_O (Œª x, (0 : Œ≤)) g l :=
‚ü®1, zero_lt_one, by { filter_upwards [univ_mem_sets], intros x _, simp }‚ü©

theorem is_O_refl_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_O (Œª x, f x - f x) g l :=
by simpa using is_O_zero g l

theorem is_O_zero_right_iff {f : Œ± ‚Üí Œ≤} {l : filter Œ±} :
  is_O f (Œª x, (0 : Œ≥)) l ‚Üî {x | f x = 0} ‚àà l :=
begin
  rw [is_O_iff], split,
  { rintros ‚ü®c, hc‚ü©,
    filter_upwards [hc], dsimp,
    intro x, rw [norm_zero, mul_zero], intro hx,
    rw ‚Üênorm_eq_zero,
    exact le_antisymm hx (norm_nonneg _) },
  intro h, use 0,
  filter_upwards [h], dsimp,
  intros x hx,
  rw [hx, norm_zero, zero_mul]
end

theorem is_o_zero (g : Œ± ‚Üí Œ≥) (l : filter Œ±) :
  is_o (Œª x, (0 : Œ≤)) g l :=
Œª c cpos,
by { filter_upwards [univ_mem_sets], intros x _, simp,
     exact mul_nonneg (le_of_lt cpos) (norm_nonneg _)}

theorem is_o_refl_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} :
  is_o (Œª x, f x - f x) g l :=
by simpa using is_o_zero g l

theorem is_o_zero_right_iff {f : Œ± ‚Üí Œ≤} (l : filter Œ±) :
  is_o f (Œª x, (0 : Œ≥)) l ‚Üî {x | f x = 0} ‚àà l :=
begin
  split,
  { intro h, exact is_O_zero_right_iff.mp h.to_is_O },
  intros h c cpos,
  filter_upwards [h], dsimp,
  intros x hx,
  rw [hx, norm_zero, norm_zero, mul_zero]
end

end

section
variables [has_norm Œ≤] [normed_field Œ≥]

open normed_field

theorem is_O_const_one (c : Œ≤) (l : filter Œ±) :
  is_O (Œª x : Œ±, c) (Œª x, (1 : Œ≥)) l :=
begin
  rw is_O_iff,
  refine ‚ü®‚à•c‚à•, _‚ü©,
  simp only [norm_one, mul_one],
  apply univ_mem_sets',
  simp [le_refl],
end

end

section
variables [normed_field Œ≤] [normed_group Œ≥]

theorem is_O_const_mul_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h : is_O f g l) (c : Œ≤) :
  is_O (Œª x, c * f x) g l :=
 begin
  cases classical.em (c = 0) with h'' h'',
  { simp [h''], apply is_O_zero },
  rcases h with ‚ü®c', c'pos, h'‚ü©,
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp h'',
  have cpos : ‚à•c‚à• > 0, from lt_of_le_of_ne (norm_nonneg _) (ne.symm cne0),
  refine ‚ü®‚à•c‚à• * c', mul_pos cpos c'pos, _‚ü©,
  filter_upwards [h'], dsimp,
  intros x h‚ÇÄ,
  rw [normed_field.norm_mul, mul_assoc],
  exact mul_le_mul_of_nonneg_left h‚ÇÄ (norm_nonneg _)
end

theorem is_O_const_mul_left_iff {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : Œ≤} (hc : c ‚â† 0) :
  is_O (Œª x, c * f x) g l ‚Üî is_O f g l :=
begin
  split,
  { intro h,
    convert is_O_const_mul_left h c‚Åª¬π, ext,
    rw [‚Üêmul_assoc, inv_mul_cancel hc, one_mul] },
  intro h, apply is_O_const_mul_left h
end

theorem is_o_const_mul_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h : is_o f g l) (c : Œ≤) :
  is_o (Œª x, c * f x) g l :=
begin
  cases classical.em (c = 0) with h'' h'',
  { simp [h''], apply is_o_zero },
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp h'',
  have cpos : ‚à•c‚à• > 0, from lt_of_le_of_ne (norm_nonneg _) (ne.symm cne0),
  intros c' c'pos, dsimp,
  filter_upwards [h (c' / ‚à•c‚à•) (div_pos c'pos cpos)], dsimp,
  intros x hx, rw [normed_field.norm_mul],
  apply le_trans (mul_le_mul_of_nonneg_left hx (le_of_lt cpos)),
  rw [‚Üêmul_assoc, mul_div_cancel' _ cne0]
end

theorem is_o_const_mul_left_iff {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : Œ≤} (hc : c ‚â† 0) :
  is_o (Œª x, c * f x) g l ‚Üî is_o f g l :=
begin
  split,
  { intro h,
    convert is_o_const_mul_left h c‚Åª¬π, ext,
    rw [‚Üêmul_assoc, inv_mul_cancel hc, one_mul] },
  intro h',
  apply is_o_const_mul_left h'
end

end

section
variables [normed_group Œ≤] [normed_field Œ≥]

theorem is_O_of_is_O_const_mul_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : Œ≥}
    (h : is_O f (Œª x, c * g x) l) :
  is_O f g l  :=
begin
  cases classical.em (c = 0) with h' h',
  { simp [h', is_O_zero_right_iff] at h, rw is_O_congr_left h, apply is_O_zero },
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp h',
  have cpos : ‚à•c‚à• > 0, from lt_of_le_of_ne (norm_nonneg _) (ne.symm cne0),
  rcases h with ‚ü®c', c'pos, h''‚ü©,
  refine ‚ü®c' * ‚à•c‚à•, mul_pos c'pos cpos, _‚ü©,
  convert h'', ext x, dsimp,
  rw [normed_field.norm_mul, mul_assoc]
end

theorem is_O_const_mul_right_iff {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : Œ≥} (hc : c ‚â† 0) :
  is_O f (Œª x, c * g x) l ‚Üî is_O f g l :=
begin
  split,
  { intro h, exact is_O_of_is_O_const_mul_right h },
  intro h,
  apply is_O_of_is_O_const_mul_right,
  show is_O f (Œª (x : Œ±), c‚Åª¬π * (c * g x)) l,
  convert h, ext, rw [‚Üêmul_assoc, inv_mul_cancel hc, one_mul]
end

theorem is_o_of_is_o_const_mul_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : Œ≥}
    (h : is_o f (Œª x, c * g x) l) :
  is_o f g l  :=
begin
  cases classical.em (c = 0) with h' h',
  { simp [h', is_o_zero_right_iff] at h, rw is_o_congr_left h, apply is_o_zero },
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp h',
  have cpos : ‚à•c‚à• > 0, from lt_of_le_of_ne (norm_nonneg _) (ne.symm cne0),
  intros c' c'pos,
  convert h (c' / ‚à•c‚à•) (div_pos c'pos cpos), dsimp,
  ext x, rw [normed_field.norm_mul, ‚Üêmul_assoc, div_mul_cancel _ cne0]
end

theorem is_o_const_mul_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : Œ≥} (hc : c ‚â† 0) :
  is_o f (Œª x, c * g x) l ‚Üî is_o f g l :=
begin
  split,
  { intro h, exact is_o_of_is_o_const_mul_right h },
  intro h,
  apply is_o_of_is_o_const_mul_right,
  show is_o f (Œª (x : Œ±), c‚Åª¬π * (c * g x)) l,
  convert h, ext, rw [‚Üêmul_assoc, inv_mul_cancel hc, one_mul]
end

theorem is_o_one_iff {f : Œ± ‚Üí Œ≤} {l : filter Œ±} :
  is_o f (Œª x, (1 : Œ≥)) l ‚Üî tendsto f l (ùìù 0) :=
begin
  rw [normed_group.tendsto_nhds_zero, is_o], split,
  { intros h e epos,
    filter_upwards [h (e / 2) (half_pos epos)], simp,
    intros x hx,
    exact lt_of_le_of_lt hx (half_lt_self epos) },
  intros h e epos,
  filter_upwards [h e epos], simp,
  intros x hx,
  exact le_of_lt hx
end

theorem is_O_one_of_tendsto {f : Œ± ‚Üí Œ≤} {l : filter Œ±} {y : Œ≤}
  (h : tendsto f l (ùìù y)) : is_O f (Œª x, (1 : Œ≥)) l :=
begin
  have Iy : ‚à•y‚à• < ‚à•y‚à• + 1 := lt_add_one _,
  refine ‚ü®‚à•y‚à• + 1, lt_of_le_of_lt (norm_nonneg _) Iy, _‚ü©,
  simp only [mul_one, normed_field.norm_one],
  have : tendsto (Œªx, ‚à•f x‚à•) l (ùìù ‚à•y‚à•) :=
    (continuous_norm.tendsto _).comp h,
  exact this (ge_mem_nhds Iy)
end

end

section
variables [normed_group Œ≤] [normed_group Œ≥]

theorem is_O.trans_tendsto {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h‚ÇÅ : is_O f g l) (h‚ÇÇ : tendsto g l (ùìù 0)) :
  tendsto f l (ùìù 0) :=
(@is_o_one_iff _ _ ‚Ñù _ _ _ _).1 $ h‚ÇÅ.trans_is_o $ is_o_one_iff.2 h‚ÇÇ

theorem is_o.trans_tendsto {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±}
  (h‚ÇÅ : is_o f g l) : tendsto g l (ùìù 0) ‚Üí tendsto f l (ùìù 0) :=
h‚ÇÅ.to_is_O.trans_tendsto

end

section
variables [normed_field Œ≤] [normed_field Œ≥]

theorem is_O_mul {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h‚ÇÅ : is_O f‚ÇÅ g‚ÇÅ l) (h‚ÇÇ : is_O f‚ÇÇ g‚ÇÇ l) :
  is_O (Œª x, f‚ÇÅ x * f‚ÇÇ x) (Œª x, g‚ÇÅ x * g‚ÇÇ x) l :=
begin
  rcases h‚ÇÅ with ‚ü®c‚ÇÅ, c‚ÇÅpos, hc‚ÇÅ‚ü©,
  rcases h‚ÇÇ with ‚ü®c‚ÇÇ, c‚ÇÇpos, hc‚ÇÇ‚ü©,
  refine ‚ü®c‚ÇÅ * c‚ÇÇ, mul_pos c‚ÇÅpos c‚ÇÇpos, _‚ü©,
  filter_upwards [hc‚ÇÅ, hc‚ÇÇ], dsimp,
  intros x hx‚ÇÅ hx‚ÇÇ,
  rw [normed_field.norm_mul, normed_field.norm_mul, mul_assoc, mul_left_comm c‚ÇÇ, ‚Üêmul_assoc],
  exact mul_le_mul hx‚ÇÅ hx‚ÇÇ (norm_nonneg _) (mul_nonneg (le_of_lt c‚ÇÅpos) (norm_nonneg _))
end

theorem is_o_mul_left {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h‚ÇÅ : is_O f‚ÇÅ g‚ÇÅ l) (h‚ÇÇ : is_o f‚ÇÇ g‚ÇÇ l) :
  is_o (Œª x, f‚ÇÅ x * f‚ÇÇ x) (Œª x, g‚ÇÅ x * g‚ÇÇ x) l :=
begin
  intros c cpos,
  rcases h‚ÇÅ with ‚ü®c‚ÇÅ, c‚ÇÅpos, hc‚ÇÅ‚ü©,
  filter_upwards [hc‚ÇÅ, h‚ÇÇ (c / c‚ÇÅ) (div_pos cpos c‚ÇÅpos)], dsimp,
  intros x hx‚ÇÅ hx‚ÇÇ,
  rw [normed_field.norm_mul, normed_field.norm_mul],
  apply le_trans (mul_le_mul hx‚ÇÅ hx‚ÇÇ (norm_nonneg _) (mul_nonneg (le_of_lt c‚ÇÅpos) (norm_nonneg _))),
  rw [mul_comm c‚ÇÅ, mul_assoc, mul_left_comm c‚ÇÅ, ‚Üêmul_assoc _ c‚ÇÅ, div_mul_cancel _ (ne_of_gt c‚ÇÅpos)],
  rw [mul_left_comm]
end

theorem is_o_mul_right {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h‚ÇÅ : is_o f‚ÇÅ g‚ÇÅ l) (h‚ÇÇ : is_O f‚ÇÇ g‚ÇÇ l) :
  is_o (Œª x, f‚ÇÅ x * f‚ÇÇ x) (Œª x, g‚ÇÅ x * g‚ÇÇ x) l :=
by convert is_o_mul_left h‚ÇÇ h‚ÇÅ; simp only [mul_comm]

theorem is_o_mul {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí Œ≤} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≥} {l : filter Œ±}
    (h‚ÇÅ : is_o f‚ÇÅ g‚ÇÅ l) (h‚ÇÇ : is_o f‚ÇÇ g‚ÇÇ l) :
  is_o (Œª x, f‚ÇÅ x * f‚ÇÇ x) (Œª x, g‚ÇÅ x * g‚ÇÇ x) l :=
is_o_mul_left h‚ÇÅ.to_is_O h‚ÇÇ

end

/-
Note: the theorems in the next two sections can also be used for the integers, since
scalar multiplication is multiplication.
-/

section
variables {K : Type*} [normed_field K] [normed_group Œ≤] [normed_space K Œ≤] [normed_group Œ≥]

set_option class.instance_max_depth 43

theorem is_O_const_smul_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h : is_O f g l) (c : K) :
  is_O (Œª x, c ‚Ä¢ f x) g l :=
begin
  rw [‚Üêis_O_norm_left], simp only [norm_smul],
  apply is_O_const_mul_left,
  rw [is_O_norm_left],
  apply h
end

theorem is_O_const_smul_left_iff {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : K} (hc : c ‚â† 0) :
  is_O (Œª x, c ‚Ä¢ f x) g l ‚Üî is_O f g l :=
begin
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp hc,
  rw [‚Üêis_O_norm_left], simp only [norm_smul],
  rw [is_O_const_mul_left_iff cne0, is_O_norm_left]
end

theorem is_o_const_smul_left {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h : is_o f g l) (c : K) :
  is_o (Œª x, c ‚Ä¢ f x) g l :=
begin
  rw [‚Üêis_o_norm_left], simp only [norm_smul],
  apply is_o_const_mul_left,
  rw [is_o_norm_left],
  apply h
end

theorem is_o_const_smul_left_iff {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : K} (hc : c ‚â† 0) :
  is_o (Œª x, c ‚Ä¢ f x) g l ‚Üî is_o f g l :=
begin
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp hc,
  rw [‚Üêis_o_norm_left], simp only [norm_smul],
  rw [is_o_const_mul_left_iff cne0, is_o_norm_left]
end

end

section
variables {K : Type*} [normed_group Œ≤] [normed_field K] [normed_group Œ≥] [normed_space K Œ≥]

set_option class.instance_max_depth 43

theorem is_O_const_smul_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : K} (hc : c ‚â† 0) :
  is_O f (Œª x, c ‚Ä¢ g x) l ‚Üî is_O f g l :=
begin
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp hc,
  rw [‚Üêis_O_norm_right], simp only [norm_smul],
  rw [is_O_const_mul_right_iff cne0, is_O_norm_right]
end

theorem is_o_const_smul_right {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} {c : K} (hc : c ‚â† 0) :
  is_o f (Œª x, c ‚Ä¢ g x) l ‚Üî is_o f g l :=
begin
  have cne0 : ‚à•c‚à• ‚â† 0, from mt (norm_eq_zero _).mp hc,
  rw [‚Üêis_o_norm_right], simp only [norm_smul],
  rw [is_o_const_mul_right cne0, is_o_norm_right]
end

end

section
variables {K : Type*} [normed_field K] [normed_group Œ≤] [normed_space K Œ≤]
[normed_group Œ≥] [normed_space K Œ≥]

set_option class.instance_max_depth 43

theorem is_O_smul {k : Œ± ‚Üí K} {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h : is_O f g l) :
  is_O (Œª x, k x ‚Ä¢ f x) (Œª x, k x ‚Ä¢ g x) l :=
begin
  rw [‚Üêis_O_norm_left, ‚Üêis_O_norm_right], simp only [norm_smul],
  apply is_O_mul (is_O_refl _ _),
  rw [is_O_norm_left, is_O_norm_right],
  exact h
end

theorem is_o_smul {k : Œ± ‚Üí K} {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {l : filter Œ±} (h : is_o f g l) :
  is_o (Œª x, k x ‚Ä¢ f x) (Œª x, k x ‚Ä¢ g x) l :=
begin
  rw [‚Üêis_o_norm_left, ‚Üêis_o_norm_right], simp only [norm_smul],
  apply is_o_mul_left (is_O_refl _ _),
  rw [is_o_norm_left, is_o_norm_right],
  exact h
end

end

section
variables [normed_field Œ≤]

theorem tendsto_nhds_zero_of_is_o {f g : Œ± ‚Üí Œ≤} {l : filter Œ±} (h : is_o f g l) :
  tendsto (Œª x, f x / (g x)) l (ùìù 0) :=
have eq‚ÇÅ : is_o (Œª x, f x / g x) (Œª x, g x / g x) l,
  from is_o_mul_right h (is_O_refl _ _),
have eq‚ÇÇ : is_O (Œª x, g x / g x) (Œª x, (1 : Œ≤)) l,
  begin
    use [1, zero_lt_one],
    filter_upwards [univ_mem_sets], simp,
    intro x,
    cases classical.em (‚à•g x‚à• = 0) with h' h'; simp [h'],
    exact zero_le_one
  end,
is_o_one_iff.mp (eq‚ÇÅ.trans_is_O eq‚ÇÇ)

private theorem is_o_of_tendsto {f g : Œ± ‚Üí Œ≤} {l : filter Œ±}
    (hgf : ‚àÄ x, g x = 0 ‚Üí f x = 0) (h : tendsto (Œª x, f x / (g x)) l (ùìù 0)) :
  is_o f g l :=
have eq‚ÇÅ : is_o (Œª x, f x / (g x)) (Œª x, (1 : Œ≤)) l,
  from is_o_one_iff.mpr h,
have eq‚ÇÇ : is_o (Œª x, f x / g x * g x) g l,
  by convert is_o_mul_right eq‚ÇÅ (is_O_refl _ _); simp,
have eq‚ÇÉ : is_O f (Œª x, f x / g x * g x) l,
  begin
    use [1, zero_lt_one],
    refine filter.univ_mem_sets' (assume x, _),
    suffices : ‚à•f x‚à• ‚â§ ‚à•f x‚à• / ‚à•g x‚à• * ‚à•g x‚à•, { simpa },
    by_cases g x = 0,
    { simp only [h, hgf x h, norm_zero, mul_zero] },
    { rw [div_mul_cancel], exact mt (norm_eq_zero _).1 h }
  end,
eq‚ÇÉ.trans_is_o eq‚ÇÇ

theorem is_o_iff_tendsto {f g : Œ± ‚Üí Œ≤} {l : filter Œ±}
    (hgf : ‚àÄ x, g x = 0 ‚Üí f x = 0) :
  is_o f g l ‚Üî tendsto (Œª x, f x / (g x)) l (ùìù 0) :=
iff.intro tendsto_nhds_zero_of_is_o (is_o_of_tendsto hgf)

end

end asymptotics
