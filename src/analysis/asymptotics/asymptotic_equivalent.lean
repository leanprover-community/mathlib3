/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.asymptotics.asymptotics
import analysis.normed_space.ordered

/-!
# Asymptotic equivalence

In this file, we define the relation `is_equivalent u v l`, which means that `u-v` is little o of
`v` along the filter `l`.

Unlike `is_[oO]` relations, this one requires `u` and `v` to have the same codomain `Î²`. While the
definition only requires `Î²` to be a `normed_group`, most interesting properties require it to be a
`normed_field`.

## Notations

We introduce the notation `u ~[l] v := is_equivalent u v l`, which you can use by opening the
`asymptotics` locale.

## Main results

If `Î²` is a `normed_group` :

- `_ ~[l] _` is an equivalence relation
- Equivalent statements for `u ~[l] const _ c` :
  - If `c â‰  0`, this is true iff `tendsto u l (ğ“ c)` (see `is_equivalent_const_iff_tendsto`)
  - For `c = 0`, this is true iff `u =á¶ [l] 0` (see `is_equivalent_zero_iff_eventually_zero`)

If `Î²` is a `normed_field` :

- Alternative characterization of the relation (see `is_equivalent_iff_exists_eq_mul`) :

  `u ~[l] v â†” âˆƒ (Ï† : Î± â†’ Î²) (hÏ† : tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v`

- Provided some non-vanishing hypothesis, this can be seen as `u ~[l] v â†” tendsto (u/v) l (ğ“ 1)`
  (see `is_equivalent_iff_tendsto_one`)
- For any constant `c`, `u ~[l] v` implies `tendsto u l (ğ“ c) â†” tendsto v l (ğ“ c)`
  (see `is_equivalent.tendsto_nhds_iff`)
- `*` and `/` are compatible with `_ ~[l] _` (see `is_equivalent.mul` and `is_equivalent.div`)

If `Î²` is a `normed_linear_ordered_field` :

- If `u ~[l] v`, we have `tendsto u l at_top â†” tendsto v l at_top`
  (see `is_equivalent.tendsto_at_top_iff`)

-/

namespace asymptotics

open filter function
open_locale topological_space

section normed_group

variables {Î± Î² : Type*} [normed_group Î²]

/-- Two functions `u` and `v` are said to be asymptotically equivalent along a filter `l` when
    `u x - v x = o(v x)` as x converges along `l`. -/
def is_equivalent (u v : Î± â†’ Î²) (l : filter Î±) := is_o (u - v) v l

localized "notation u ` ~[`:50 l:50 `] `:0 v:50 := asymptotics.is_equivalent u v l" in asymptotics

variables {u v w : Î± â†’ Î²} {l : filter Î±}

lemma is_equivalent.is_o (h : u ~[l] v) : is_o (u - v) v l := h

lemma is_equivalent.is_O (h : u ~[l] v) : is_O u v l :=
(is_O.congr_of_sub h.is_O.symm).mp (is_O_refl _ _)

lemma is_equivalent.is_O_symm (h : u ~[l] v) : is_O v u l :=
begin
  convert h.is_o.right_is_O_add,
  ext,
  simp
end

@[refl] lemma is_equivalent.refl : u ~[l] u :=
begin
  rw [is_equivalent, sub_self],
  exact is_o_zero _ _
end

@[symm] lemma is_equivalent.symm (h : u ~[l] v) : v ~[l] u :=
(h.is_o.trans_is_O h.is_O_symm).symm

@[trans] lemma is_equivalent.trans (huv : u ~[l] v) (hvw : v ~[l] w) : u ~[l] w :=
(huv.is_o.trans_is_O hvw.is_O).triangle hvw.is_o

lemma is_equivalent.congr_left {u v w : Î± â†’ Î²} {l : filter Î±} (huv : u ~[l] v)
  (huw : u =á¶ [l] w) : w ~[l] v :=
is_o.congr' (huw.sub (eventually_eq.refl _ _)) (eventually_eq.refl _ _) huv

lemma is_equivalent.congr_right {u v w : Î± â†’ Î²} {l : filter Î±} (huv : u ~[l] v)
  (hvw : v =á¶ [l] w) : u ~[l] w :=
(huv.symm.congr_left hvw).symm

lemma is_equivalent_zero_iff_eventually_zero : u ~[l] 0 â†” u =á¶ [l] 0 :=
begin
  rw [is_equivalent, sub_zero],
  exact is_o_zero_right_iff
end

lemma is_equivalent_zero_iff_is_O_zero : u ~[l] 0 â†” is_O u (0 : Î± â†’ Î²) l :=
begin
  refine âŸ¨is_equivalent.is_O, Î» h, _âŸ©,
  rw [is_equivalent_zero_iff_eventually_zero, eventually_eq_iff_exists_mem],
  exact âŸ¨{x : Î± | u x = 0}, is_O_zero_right_iff.mp h, Î» x hx, hxâŸ©,
end

lemma is_equivalent_const_iff_tendsto {c : Î²} (h : c â‰  0) : u ~[l] const _ c â†” tendsto u l (ğ“ c) :=
begin
  rw [is_equivalent, is_o_const_iff h],
  split; intro h;
  [ { have := h.sub tendsto_const_nhds, rw zero_sub (-c) at this },
    { have := h.sub tendsto_const_nhds, rw â† sub_self c} ];
  convert this; try { ext }; simp
end

lemma is_equivalent.tendsto_const {c : Î²} (hu : u ~[l] const _ c) : tendsto u l (ğ“ c) :=
begin
  rcases (em $ c = 0) with âŸ¨rfl, hâŸ©,
  { exact (tendsto_congr' $ is_equivalent_zero_iff_eventually_zero.mp hu).mpr tendsto_const_nhds },
  { exact (is_equivalent_const_iff_tendsto h).mp hu }
end

lemma is_equivalent.tendsto_nhds {c : Î²} (huv : u ~[l] v) (hu : tendsto u l (ğ“ c)) :
  tendsto v l (ğ“ c) :=
begin
  by_cases h : c = 0,
  { rw [h, â† is_o_one_iff â„] at *,
    convert (huv.symm.is_o.trans hu).add hu,
    simp },
  { rw â† is_equivalent_const_iff_tendsto h at hu âŠ¢,
    exact huv.symm.trans hu }
end

lemma is_equivalent.tendsto_nhds_iff {c : Î²} (huv : u ~[l] v) :
  tendsto u l (ğ“ c) â†” tendsto v l (ğ“ c) := âŸ¨huv.tendsto_nhds, huv.symm.tendsto_nhdsâŸ©

lemma is_equivalent.add_is_o (huv : u ~[l] v) (hwv : is_o w v l) : (w + u) ~[l] v :=
begin
  rw is_equivalent at *,
  convert hwv.add huv,
  ext,
  simp [add_sub],
end

lemma is_o.is_equivalent (huv : is_o (u - v) v l) : u ~[l] v := huv

lemma is_equivalent.neg (huv : u ~[l] v) : (Î» x, - u x) ~[l] (Î» x, - v x) :=
begin
  rw is_equivalent,
  convert huv.is_o.neg_left.neg_right,
  ext,
  simp,
end

end normed_group

open_locale asymptotics

section normed_field

variables {Î± Î² : Type*} [normed_field Î²] {t u v w : Î± â†’ Î²} {l : filter Î±}

lemma is_equivalent_iff_exists_eq_mul : u ~[l] v â†”
  âˆƒ (Ï† : Î± â†’ Î²) (hÏ† : tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v :=
begin
  rw [is_equivalent, is_o_iff_exists_eq_mul],
  split; rintros âŸ¨Ï†, hÏ†, hâŸ©; [use (Ï† + 1), use (Ï† - 1)]; split,
  { conv in (ğ“ _) { rw â† zero_add (1 : Î²) },
    exact hÏ†.add (tendsto_const_nhds) },
  { convert h.add (eventually_eq.refl l v); ext; simp [add_mul] },
  { conv in (ğ“ _) { rw â† sub_self (1 : Î²) },
    exact hÏ†.sub (tendsto_const_nhds) },
  { convert h.sub (eventually_eq.refl l v); ext; simp [sub_mul] }
end

lemma is_equivalent.exists_eq_mul (huv : u ~[l] v) :
  âˆƒ (Ï† : Î± â†’ Î²) (hÏ† : tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v :=
is_equivalent_iff_exists_eq_mul.mp huv

lemma is_equivalent_of_tendsto_one (hz : âˆ€á¶  x in l, v x = 0 â†’ u x = 0)
  (huv : tendsto (u/v) l (ğ“ 1)) : u ~[l] v :=
begin
  rw is_equivalent_iff_exists_eq_mul,
  refine âŸ¨u/v, huv, hz.mono $ Î» x hz', (div_mul_cancel_of_imp hz').symmâŸ©,
end

lemma is_equivalent_of_tendsto_one' (hz : âˆ€ x, v x = 0 â†’ u x = 0) (huv : tendsto (u/v) l (ğ“ 1)) :
  u ~[l] v :=
is_equivalent_of_tendsto_one (eventually_of_forall hz) huv

lemma is_equivalent_iff_tendsto_one (hz : âˆ€á¶  x in l, v x â‰  0) :
  u ~[l] v â†” tendsto (u/v) l (ğ“ 1) :=
begin
  split,
  { intro hequiv,
    have := hequiv.is_o.tendsto_0,
    simp only [pi.sub_apply, sub_div] at this,
    have key : tendsto (Î» x, v x / v x) l (ğ“ 1),
    { exact (tendsto_congr' $ hz.mono $ Î» x hnz, @div_self _ _ (v x) hnz).mpr tendsto_const_nhds },
    convert this.add key,
    { ext, simp },
    { norm_num } },
  { exact is_equivalent_of_tendsto_one (hz.mono $ Î» x hnvz hz, (hnvz hz).elim) }
end

end normed_field

section smul

lemma is_equivalent.smul {Î± E ğ•œ : Type*} [normed_field ğ•œ] [normed_group E]
  [normed_space ğ•œ E] {a b : Î± â†’ ğ•œ} {u v : Î± â†’ E} {l : filter Î±} (hab : a ~[l] b) (huv : u ~[l] v) :
  (Î» x, a x â€¢ u x) ~[l] (Î» x, b x â€¢ v x) :=
begin
  rcases hab.exists_eq_mul with âŸ¨Ï†, hÏ†, habÏ†âŸ©,
  have : (Î» (x : Î±), a x â€¢ u x) - (Î» (x : Î±), b x â€¢ v x) =á¶ [l] Î» x, b x â€¢ ((Ï† x â€¢ u x) - v x),
  { convert (habÏ†.compâ‚‚ (â€¢) $ eventually_eq.refl _ u).sub (eventually_eq.refl _ (Î» x, b x â€¢ v x)),
    ext,
    rw [pi.mul_apply, mul_comm, mul_smul, â† smul_sub] },
  refine (is_o_congr this.symm $ eventually_eq.rfl).mp ((is_O_refl b l).smul_is_o _),

  rcases huv.is_O.exists_pos with âŸ¨C, hC, hCuvâŸ©,
  rw is_equivalent at *,
  rw is_o_iff at *,
  rw is_O_with at hCuv,
  simp only [metric.tendsto_nhds, dist_eq_norm] at hÏ†,
  intros c hc,
  specialize hÏ† ((c/2)/C) (div_pos (by linarith) hC),
  specialize huv (show 0 < c/2, by linarith),
  refine hÏ†.mp (huv.mp $ hCuv.mono $ Î» x hCuvx huvx hÏ†x, _),

  have key :=
    calc âˆ¥Ï† x - 1âˆ¥ * âˆ¥u xâˆ¥
            â‰¤ (c/2) / C * âˆ¥u xâˆ¥ : mul_le_mul_of_nonneg_right hÏ†x.le (norm_nonneg $ u x)
        ... â‰¤ (c/2) / C * (C*âˆ¥v xâˆ¥) : mul_le_mul_of_nonneg_left hCuvx (div_pos (by linarith) hC).le
        ... = c/2 * âˆ¥v xâˆ¥ : by {field_simp [hC.ne.symm], ring},

  calc âˆ¥((Î» (x : Î±), Ï† x â€¢ u x) - v) xâˆ¥
          = âˆ¥(Ï† x - 1) â€¢ u x + (u x - v x)âˆ¥ : by simp [sub_smul, sub_add]
      ... â‰¤ âˆ¥(Ï† x - 1) â€¢ u xâˆ¥ + âˆ¥u x - v xâˆ¥ : norm_add_le _ _
      ... = âˆ¥Ï† x - 1âˆ¥ * âˆ¥u xâˆ¥ + âˆ¥u x - v xâˆ¥ : by rw norm_smul
      ... â‰¤ c / 2 * âˆ¥v xâˆ¥ + âˆ¥u x - v xâˆ¥ : add_le_add_right key _
      ... â‰¤ c / 2 * âˆ¥v xâˆ¥ + c / 2 * âˆ¥v xâˆ¥ : add_le_add_left huvx _
      ... = c * âˆ¥v xâˆ¥ : by ring,
end

end smul

section mul_inv

variables {Î± Î² : Type*} [normed_field Î²] {t u v w : Î± â†’ Î²} {l : filter Î±}

lemma is_equivalent.mul (htu : t ~[l] u) (hvw : v ~[l] w) : t * v ~[l] u * w :=
htu.smul hvw

lemma is_equivalent.inv (huv : u ~[l] v) : (Î» x, (u x)â»Â¹) ~[l] (Î» x, (v x)â»Â¹) :=
begin
  rw is_equivalent_iff_exists_eq_mul at *,
  rcases huv with âŸ¨Ï†, hÏ†, hâŸ©,
  rw â† inv_one,
  refine âŸ¨Î» x, (Ï† x)â»Â¹, tendsto.inv' hÏ† (by norm_num) , _âŸ©,
  convert h.inv,
  ext,
  simp [mul_inv']
end

lemma is_equivalent.div (htu : t ~[l] u) (hvw : v ~[l] w) :
  (Î» x, t x / v x) ~[l] (Î» x, u x / w x) :=
by simpa only [div_eq_mul_inv] using htu.mul hvw.inv

end mul_inv

section normed_linear_ordered_field

variables {Î± Î² : Type*} [normed_linear_ordered_field Î²] {u v : Î± â†’ Î²} {l : filter Î±}

lemma is_equivalent.tendsto_at_top [order_topology Î²] (huv : u ~[l] v) (hu : tendsto u l at_top) :
  tendsto v l at_top :=
let âŸ¨Ï†, hÏ†, hâŸ© := huv.symm.exists_eq_mul in
tendsto.congr' h.symm ((mul_comm u Ï†) â–¸ (hu.at_top_mul zero_lt_one hÏ†))

lemma is_equivalent.tendsto_at_top_iff [order_topology Î²] (huv : u ~[l] v) :
  tendsto u l at_top â†” tendsto v l at_top := âŸ¨huv.tendsto_at_top, huv.symm.tendsto_at_topâŸ©

lemma is_equivalent.tendsto_at_bot [order_topology Î²] (huv : u ~[l] v) (hu : tendsto u l at_bot) :
  tendsto v l at_bot :=
begin
  convert tendsto_neg_at_top_at_bot.comp
    (huv.neg.tendsto_at_top $ tendsto_neg_at_bot_at_top.comp hu),
  ext,
  simp
end

lemma is_equivalent.tendsto_at_bot_iff [order_topology Î²] (huv : u ~[l] v) :
  tendsto u l at_bot â†” tendsto v l at_bot := âŸ¨huv.tendsto_at_bot, huv.symm.tendsto_at_botâŸ©

end normed_linear_ordered_field

end asymptotics

open filter asymptotics
open_locale asymptotics

variables {Î± Î² : Type*} [normed_group Î²]

lemma filter.eventually_eq.is_equivalent {u v : Î± â†’ Î²} {l : filter Î±} (h : u =á¶ [l] v) : u ~[l] v :=
is_o.congr' h.sub_eq.symm (eventually_eq.refl _ _) (is_o_zero v l)
