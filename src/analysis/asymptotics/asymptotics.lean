/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Yury Kudryashov
-/
import analysis.normed_space.basic
import topology.local_homeomorph

/-!
# Asymptotics

We introduce these relations:

* `is_O_with c f g l` : "f is big O of g along l with constant c";
* `is_O f g l` : "f is big O of g along l";
* `is_o f g l` : "f is little o of g along l".

Here `l` is any filter on the domain of `f` and `g`, which are assumed to be the same. The codomains
of `f` and `g` do not need to be the same; all that is needed that there is a norm associated with
these types, and it is the norm that is compared asymptotically.

The relation `is_O_with c` is introduced to factor out common algebraic arguments in the proofs of
similar properties of `is_O` and `is_o`. Usually proofs outside of this file should use `is_O`
instead.

Often the ranges of `f` and `g` will be the real numbers, in which case the norm is the absolute
value. In general, we have

  `is_O f g l â†” is_O (Î» x, âˆ¥f xâˆ¥) (Î» x, âˆ¥g xâˆ¥) l`,

and similarly for `is_o`. But our setup allows us to use the notions e.g. with functions
to the integers, rationals, complex numbers, or any normed vector space without mentioning the
norm explicitly.

If `f` and `g` are functions to a normed field like the reals or complex numbers and `g` is always
nonzero, we have

  `is_o f g l â†” tendsto (Î» x, f x / (g x)) l (ğ“ 0)`.

In fact, the right-to-left direction holds without the hypothesis on `g`, and in the other direction
it suffices to assume that `f` is zero wherever `g` is. (This generalization is useful in defining
the FrÃ©chet derivative.)
-/

open filter set
open_locale topological_space big_operators classical filter nnreal

namespace asymptotics

variables {Î± : Type*} {Î² : Type*} {E : Type*} {F : Type*} {G : Type*}
  {E' : Type*} {F' : Type*} {G' : Type*} {R : Type*} {R' : Type*} {ğ•œ : Type*} {ğ•œ' : Type*}

variables [has_norm E] [has_norm F] [has_norm G] [normed_group E'] [normed_group F']
  [normed_group G'] [normed_ring R] [normed_ring R'] [normed_field ğ•œ] [normed_field ğ•œ']
  {c c' : â„} {f : Î± â†’ E} {g : Î± â†’ F} {k : Î± â†’ G} {f' : Î± â†’ E'} {g' : Î± â†’ F'} {k' : Î± â†’ G'}
  {l l' : filter Î±}

section defs

/-! ### Definitions -/

/-- This version of the Landau notation `is_O_with C f g l` where `f` and `g` are two functions on
a type `Î±` and `l` is a filter on `Î±`, means that eventually for `l`, `âˆ¥fâˆ¥` is bounded by `C * âˆ¥gâˆ¥`.
In other words, `âˆ¥fâˆ¥ / âˆ¥gâˆ¥` is eventually bounded by `C`, modulo division by zero issues that are
avoided by this definition. Probably you want to use `is_O` instead of this relation. -/
@[irreducible]
def is_O_with (c : â„) (f : Î± â†’ E) (g : Î± â†’ F) (l : filter Î±) : Prop :=
âˆ€á¶  x in l, âˆ¥ f x âˆ¥ â‰¤ c * âˆ¥ g x âˆ¥

/-- Definition of `is_O_with`. We record it in a lemma as we will set `is_O_with` to be irreducible
at the end of this file. -/
lemma is_O_with_iff {c : â„} {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} :
  is_O_with c f g l â†” âˆ€á¶  x in l, âˆ¥ f x âˆ¥ â‰¤ c * âˆ¥ g x âˆ¥ := by rw is_O_with

alias is_O_with_iff â†” asymptotics.is_O_with.bound asymptotics.is_O_with.of_bound

/-- The Landau notation `is_O f g l` where `f` and `g` are two functions on a type `Î±` and `l` is
a filter on `Î±`, means that eventually for `l`, `âˆ¥fâˆ¥` is bounded by a constant multiple of `âˆ¥gâˆ¥`.
In other words, `âˆ¥fâˆ¥ / âˆ¥gâˆ¥` is eventually bounded, modulo division by zero issues that are avoided
by this definition. -/
@[irreducible]
def is_O (f : Î± â†’ E) (g : Î± â†’ F) (l : filter Î±) : Prop := âˆƒ c : â„, is_O_with c f g l

/-- Definition of `is_O` in terms of `is_O_with`. We record it in a lemma as we will set
`is_O` to be irreducible at the end of this file. -/
lemma is_O_iff_is_O_with {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} :
  is_O f g l â†” âˆƒ c : â„, is_O_with c f g l := by rw is_O

/-- Definition of `is_O` in terms of filters. We record it in a lemma as we will set
`is_O` to be irreducible at the end of this file. -/
lemma is_O_iff {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} :
  is_O f g l â†” âˆƒ c : â„, âˆ€á¶  x in l, âˆ¥ f x âˆ¥ â‰¤ c * âˆ¥ g x âˆ¥ := by simp [is_O, is_O_with]

lemma is_O.of_bound (c : â„) {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±}
  (h : âˆ€á¶  x in l, âˆ¥ f x âˆ¥ â‰¤ c * âˆ¥ g x âˆ¥) : is_O f g l := is_O_iff.2 âŸ¨c, hâŸ©

lemma is_O.bound {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} :
  is_O f g l â†’ âˆƒ c : â„, âˆ€á¶  x in l, âˆ¥ f x âˆ¥ â‰¤ c * âˆ¥ g x âˆ¥ := is_O_iff.1

/-- The Landau notation `is_o f g l` where `f` and `g` are two functions on a type `Î±` and `l` is
a filter on `Î±`, means that eventually for `l`, `âˆ¥fâˆ¥` is bounded by an arbitrarily small constant
multiple of `âˆ¥gâˆ¥`. In other words, `âˆ¥fâˆ¥ / âˆ¥gâˆ¥` tends to `0` along `l`, modulo division by zero
issues that are avoided by this definition. -/
@[irreducible]
def is_o (f : Î± â†’ E) (g : Î± â†’ F) (l : filter Î±) : Prop := âˆ€ â¦ƒc : â„â¦„, 0 < c â†’ is_O_with c f g l

/-- Definition of `is_o` in terms of `is_O_with`. We record it in a lemma as we will set
`is_o` to be irreducible at the end of this file. -/
lemma is_o_iff_forall_is_O_with {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} :
  is_o f g l â†” âˆ€ â¦ƒc : â„â¦„, 0 < c â†’ is_O_with c f g l := by rw is_o

alias is_o_iff_forall_is_O_with â†” asymptotics.is_o.forall_is_O_with asymptotics.is_o.of_is_O_with

/-- Definition of `is_o` in terms of filters. We record it in a lemma as we will set
`is_o` to be irreducible at the end of this file. -/
lemma is_o_iff {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} :
  is_o f g l â†” âˆ€ â¦ƒc : â„â¦„, 0 < c â†’ âˆ€á¶  x in l, âˆ¥ f x âˆ¥ â‰¤ c * âˆ¥ g x âˆ¥ :=
by simp only [is_o, is_O_with]

alias is_o_iff â†” asymptotics.is_o.bound asymptotics.is_o.of_bound

lemma is_o.def {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} (h : is_o f g l) {c : â„} (hc : 0 < c) :
  âˆ€á¶  x in l, âˆ¥ f x âˆ¥ â‰¤ c * âˆ¥ g x âˆ¥ :=
is_o_iff.1 h hc

lemma is_o.def' {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} (h : is_o f g l) {c : â„} (hc : 0 < c) :
  is_O_with c f g l :=
is_O_with_iff.2 $ is_o_iff.1 h hc

end defs

/-! ### Conversions -/

theorem is_O_with.is_O (h : is_O_with c f g l) : is_O f g l := by rw is_O; exact âŸ¨c, hâŸ©

theorem is_o.is_O_with (hgf : is_o f g l) : is_O_with 1 f g l := hgf.def' zero_lt_one

theorem is_o.is_O (hgf : is_o f g l) : is_O f g l := hgf.is_O_with.is_O

lemma is_O.is_O_with {f : Î± â†’ E} {g : Î± â†’ F} {l : filter Î±} :
  is_O f g l â†’ âˆƒ c : â„, is_O_with c f g l := is_O_iff_is_O_with.1

theorem is_O_with.weaken (h : is_O_with c f g' l) (hc : c â‰¤ c') : is_O_with c' f g' l :=
is_O_with.of_bound $ mem_of_superset h.bound $ Î» x hx,
calc âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g' xâˆ¥ : hx
... â‰¤ _ : mul_le_mul_of_nonneg_right hc (norm_nonneg _)

theorem is_O_with.exists_pos (h : is_O_with c f g' l) :
  âˆƒ c' (H : 0 < c'), is_O_with c' f g' l :=
âŸ¨max c 1, lt_of_lt_of_le zero_lt_one (le_max_right c 1), h.weaken $ le_max_left c 1âŸ©

theorem is_O.exists_pos (h : is_O f g' l) : âˆƒ c (H : 0 < c), is_O_with c f g' l :=
let âŸ¨c, hcâŸ© := h.is_O_with in hc.exists_pos

theorem is_O_with.exists_nonneg (h : is_O_with c f g' l) :
  âˆƒ c' (H : 0 â‰¤ c'), is_O_with c' f g' l :=
let âŸ¨c, cpos, hcâŸ© := h.exists_pos in âŸ¨c, le_of_lt cpos, hcâŸ©

theorem is_O.exists_nonneg (h : is_O f g' l) :
  âˆƒ c (H : 0 â‰¤ c), is_O_with c f g' l :=
let âŸ¨c, hcâŸ© := h.is_O_with in hc.exists_nonneg

/-- `f = O(g)` if and only if `is_O_with c f g` for all sufficiently large `c`. -/
lemma is_O_iff_eventually_is_O_with : is_O f g' l â†” âˆ€á¶  c in at_top, is_O_with c f g' l :=
is_O_iff_is_O_with.trans
  âŸ¨Î» âŸ¨c, hcâŸ©, mem_at_top_sets.2 âŸ¨c, Î» c' hc', hc.weaken hc'âŸ©, Î» h, h.existsâŸ©

/-- `f = O(g)` if and only if `âˆ€á¶  x in l, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g xâˆ¥` for all sufficiently large `c`. -/
lemma is_O_iff_eventually : is_O f g' l â†” âˆ€á¶  c in at_top, âˆ€á¶  x in l, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g' xâˆ¥ :=
is_O_iff_eventually_is_O_with.trans $ by simp only [is_O_with]

/-! ### Subsingleton -/

@[nontriviality] lemma is_o_of_subsingleton [subsingleton E'] : is_o f' g' l :=
is_o.of_bound $ Î» c hc, by simp [subsingleton.elim (f' _) 0, mul_nonneg hc.le]

@[nontriviality] lemma is_O_of_subsingleton [subsingleton E'] : is_O f' g' l :=
is_o_of_subsingleton.is_O

/-! ### Congruence -/

theorem is_O_with_congr {câ‚ câ‚‚} {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
  (hc : câ‚ = câ‚‚) (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
  is_O_with câ‚ fâ‚ gâ‚ l â†” is_O_with câ‚‚ fâ‚‚ gâ‚‚ l :=
begin
  unfold is_O_with,
  subst câ‚‚,
  apply filter.eventually_congr,
  filter_upwards [hf, hg],
  assume x eâ‚ eâ‚‚,
  rw [eâ‚, eâ‚‚]
end

theorem is_O_with.congr' {câ‚ câ‚‚} {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
  (hc : câ‚ = câ‚‚) (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
  is_O_with câ‚ fâ‚ gâ‚ l â†’ is_O_with câ‚‚ fâ‚‚ gâ‚‚ l :=
(is_O_with_congr hc hf hg).mp

theorem is_O_with.congr {câ‚ câ‚‚} {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
  (hc : câ‚ = câ‚‚) (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
  is_O_with câ‚ fâ‚ gâ‚ l â†’ is_O_with câ‚‚ fâ‚‚ gâ‚‚ l :=
Î» h, h.congr' hc (univ_mem' hf) (univ_mem' hg)

theorem is_O_with.congr_left {fâ‚ fâ‚‚ : Î± â†’ E} {l : filter Î±} (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) :
  is_O_with c fâ‚ g l â†’ is_O_with c fâ‚‚ g l :=
is_O_with.congr rfl hf (Î» _, rfl)

theorem is_O_with.congr_right {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±} (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
  is_O_with c f gâ‚ l â†’ is_O_with c f gâ‚‚ l :=
is_O_with.congr rfl (Î» _, rfl) hg

theorem is_O_with.congr_const {câ‚ câ‚‚} {l : filter Î±} (hc : câ‚ = câ‚‚) :
  is_O_with câ‚ f g l â†’ is_O_with câ‚‚ f g l :=
is_O_with.congr hc (Î» _, rfl) (Î» _, rfl)

theorem is_O_congr {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
    (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
  is_O fâ‚ gâ‚ l â†” is_O fâ‚‚ gâ‚‚ l :=
by { unfold is_O, exact exists_congr (Î» c, is_O_with_congr rfl hf hg) }

theorem is_O.congr' {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
    (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
  is_O fâ‚ gâ‚ l â†’ is_O fâ‚‚ gâ‚‚ l :=
(is_O_congr hf hg).mp

theorem is_O.congr {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
    (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
  is_O fâ‚ gâ‚ l â†’ is_O fâ‚‚ gâ‚‚ l :=
Î» h, h.congr' (univ_mem' hf) (univ_mem' hg)

theorem is_O.congr_left {fâ‚ fâ‚‚ : Î± â†’ E} {l : filter Î±} (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) :
  is_O fâ‚ g l â†’ is_O fâ‚‚ g l :=
is_O.congr hf (Î» _, rfl)

theorem is_O.congr_right {gâ‚ gâ‚‚ : Î± â†’ E} {l : filter Î±} (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
  is_O f gâ‚ l â†’ is_O f gâ‚‚ l :=
is_O.congr (Î» _, rfl) hg

theorem is_o_congr {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
    (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
  is_o fâ‚ gâ‚ l â†” is_o fâ‚‚ gâ‚‚ l :=
by { unfold is_o, exact ball_congr (Î» c hc, is_O_with_congr (eq.refl c) hf hg) }

theorem is_o.congr' {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
    (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
  is_o fâ‚ gâ‚ l â†’ is_o fâ‚‚ gâ‚‚ l :=
(is_o_congr hf hg).mp

theorem is_o.congr {fâ‚ fâ‚‚ : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} {l : filter Î±}
    (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
  is_o fâ‚ gâ‚ l â†’ is_o fâ‚‚ gâ‚‚ l :=
Î» h, h.congr' (univ_mem' hf) (univ_mem' hg)

theorem is_o.congr_left {fâ‚ fâ‚‚ : Î± â†’ E} {l : filter Î±} (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) :
  is_o fâ‚ g l â†’ is_o fâ‚‚ g l :=
is_o.congr hf (Î» _, rfl)

theorem is_o.congr_right {gâ‚ gâ‚‚ : Î± â†’ E} {l : filter Î±} (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
  is_o f gâ‚ l â†’ is_o f gâ‚‚ l :=
is_o.congr (Î» _, rfl) hg

/-! ### Filter operations and transitivity -/

theorem is_O_with.comp_tendsto (hcfg : is_O_with c f g l)
  {k : Î² â†’ Î±} {l' : filter Î²} (hk : tendsto k l' l):
  is_O_with c (f âˆ˜ k) (g âˆ˜ k) l' :=
is_O_with.of_bound $ hk hcfg.bound

theorem is_O.comp_tendsto (hfg : is_O f g l) {k : Î² â†’ Î±} {l' : filter Î²} (hk : tendsto k l' l) :
  is_O (f âˆ˜ k) (g âˆ˜ k) l' :=
is_O_iff_is_O_with.2 $ hfg.is_O_with.imp (Î» c h, h.comp_tendsto hk)

theorem is_o.comp_tendsto (hfg : is_o f g l) {k : Î² â†’ Î±} {l' : filter Î²} (hk : tendsto k l' l) :
  is_o (f âˆ˜ k) (g âˆ˜ k) l' :=
is_o.of_is_O_with $ Î» c cpos, (hfg.forall_is_O_with cpos).comp_tendsto hk

@[simp] theorem is_O_with_map {k : Î² â†’ Î±} {l : filter Î²} :
  is_O_with c f g (map k l) â†” is_O_with c (f âˆ˜ k) (g âˆ˜ k) l :=
by { unfold is_O_with, exact mem_map }

@[simp] theorem is_O_map {k : Î² â†’ Î±} {l : filter Î²} :
  is_O f g (map k l) â†” is_O (f âˆ˜ k) (g âˆ˜ k) l :=
by simp only [is_O, is_O_with_map]

@[simp] theorem is_o_map {k : Î² â†’ Î±} {l : filter Î²} :
  is_o f g (map k l) â†” is_o (f âˆ˜ k) (g âˆ˜ k) l :=
by simp only [is_o, is_O_with_map]

theorem is_O_with.mono (h : is_O_with c f g l') (hl : l â‰¤ l') : is_O_with c f g l :=
is_O_with.of_bound $ hl h.bound

theorem is_O.mono (h : is_O f g l') (hl : l â‰¤ l') : is_O f g l :=
is_O_iff_is_O_with.2 $ h.is_O_with.imp (Î» c h, h.mono hl)

theorem is_o.mono (h : is_o f g l') (hl : l â‰¤ l') : is_o f g l :=
is_o.of_is_O_with $ Î» c cpos, (h.forall_is_O_with cpos).mono hl

theorem is_O_with.trans (hfg : is_O_with c f g l) (hgk : is_O_with c' g k l) (hc : 0 â‰¤ c) :
  is_O_with (c * c') f k l :=
begin
  unfold is_O_with at *,
  filter_upwards [hfg, hgk],
  assume x hx hx',
  calc âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g xâˆ¥ : hx
  ... â‰¤ c * (c' * âˆ¥k xâˆ¥) : mul_le_mul_of_nonneg_left hx' hc
  ... = c * c' * âˆ¥k xâˆ¥ : (mul_assoc _ _ _).symm
end

theorem is_O.trans (hfg : is_O f g' l) (hgk : is_O g' k l) : is_O f k l :=
let âŸ¨c, cnonneg, hcâŸ© := hfg.exists_nonneg, âŸ¨c', hc'âŸ© := hgk.is_O_with in
(hc.trans hc' cnonneg).is_O

theorem is_o.trans_is_O_with (hfg : is_o f g l) (hgk : is_O_with c g k l) (hc : 0 < c) :
  is_o f k l :=
begin
  unfold is_o at *,
  intros c' c'pos,
  have : 0 < c' / c, from div_pos c'pos hc,
  exact ((hfg this).trans hgk (le_of_lt this)).congr_const (div_mul_cancel _ (ne_of_gt hc))
end

theorem is_o.trans_is_O (hfg : is_o f g l) (hgk : is_O g k' l) : is_o f k' l :=
let âŸ¨c, cpos, hcâŸ© := hgk.exists_pos in hfg.trans_is_O_with hc cpos

theorem is_O_with.trans_is_o (hfg : is_O_with c f g l) (hgk : is_o g k l) (hc : 0 < c) :
  is_o f k l :=
begin
  unfold is_o at *,
  intros c' c'pos,
  have : 0 < c' / c, from div_pos c'pos hc,
  exact (hfg.trans (hgk this) (le_of_lt hc)).congr_const (mul_div_cancel' _ (ne_of_gt hc))
end

theorem is_O.trans_is_o (hfg : is_O f g' l) (hgk : is_o g' k l) : is_o f k l :=
let âŸ¨c, cpos, hcâŸ© := hfg.exists_pos in hc.trans_is_o hgk cpos

theorem is_o.trans (hfg : is_o f g l) (hgk : is_o g k' l) : is_o f k' l :=
hfg.trans_is_O hgk.is_O

theorem is_o.trans' (hfg : is_o f g' l) (hgk : is_o g' k l) : is_o f k l :=
hfg.is_O.trans_is_o hgk

section

variable (l)

theorem is_O_with_of_le' (hfg : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g xâˆ¥) : is_O_with c f g l :=
is_O_with.of_bound $ univ_mem' hfg

theorem is_O_with_of_le (hfg : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ âˆ¥g xâˆ¥) : is_O_with 1 f g l :=
is_O_with_of_le' l $ Î» x, by { rw one_mul, exact hfg x }

theorem is_O_of_le' (hfg : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g xâˆ¥) : is_O f g l :=
(is_O_with_of_le' l hfg).is_O

theorem is_O_of_le (hfg : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ âˆ¥g xâˆ¥) : is_O f g l :=
(is_O_with_of_le l hfg).is_O

end

theorem is_O_with_refl (f : Î± â†’ E) (l : filter Î±) : is_O_with 1 f f l :=
is_O_with_of_le l $ Î» _, le_refl _

theorem is_O_refl (f : Î± â†’ E) (l : filter Î±) : is_O f f l := (is_O_with_refl f l).is_O

theorem is_O_with.trans_le (hfg : is_O_with c f g l) (hgk : âˆ€ x, âˆ¥g xâˆ¥ â‰¤ âˆ¥k xâˆ¥) (hc : 0 â‰¤ c) :
  is_O_with c f k l :=
(hfg.trans (is_O_with_of_le l hgk) hc).congr_const $ mul_one c

theorem is_O.trans_le (hfg : is_O f g' l) (hgk : âˆ€ x, âˆ¥g' xâˆ¥ â‰¤ âˆ¥k xâˆ¥) :
  is_O f k l :=
hfg.trans (is_O_of_le l hgk)

theorem is_o.trans_le (hfg : is_o f g l) (hgk : âˆ€ x, âˆ¥g xâˆ¥ â‰¤ âˆ¥k xâˆ¥) :
  is_o f k l :=
hfg.trans_is_O_with (is_O_with_of_le _ hgk) zero_lt_one

section bot

variables (c f g)

@[simp] theorem is_O_with_bot : is_O_with c f g âŠ¥ := is_O_with.of_bound $ trivial

@[simp] theorem is_O_bot : is_O f g âŠ¥ := (is_O_with_bot 1 f g).is_O

@[simp] theorem is_o_bot : is_o f g âŠ¥ := is_o.of_is_O_with $ Î» c _, is_O_with_bot c f g

end bot

theorem is_O_with.join (h : is_O_with c f g l) (h' : is_O_with c f g l') :
  is_O_with c f g (l âŠ” l') :=
is_O_with.of_bound $ mem_sup.2 âŸ¨h.bound, h'.boundâŸ©

theorem is_O_with.join' (h : is_O_with c f g' l) (h' : is_O_with c' f g' l') :
  is_O_with (max c c') f g' (l âŠ” l') :=
is_O_with.of_bound $
mem_sup.2 âŸ¨(h.weaken $ le_max_left c c').bound, (h'.weaken $ le_max_right c c').boundâŸ©

theorem is_O.join (h : is_O f g' l) (h' : is_O f g' l') : is_O f g' (l âŠ” l') :=
let âŸ¨c, hcâŸ© := h.is_O_with, âŸ¨c', hc'âŸ© := h'.is_O_with in (hc.join' hc').is_O

theorem is_o.join (h : is_o f g l) (h' : is_o f g l') :
  is_o f g (l âŠ” l') :=
is_o.of_is_O_with $
Î» c cpos, (h.forall_is_O_with cpos).join (h'.forall_is_O_with cpos)

/-! ### Simplification : norm -/

@[simp] theorem is_O_with_norm_right : is_O_with c f (Î» x, âˆ¥g' xâˆ¥) l â†” is_O_with c f g' l :=
by simp only [is_O_with, norm_norm]

alias is_O_with_norm_right â†” asymptotics.is_O_with.of_norm_right asymptotics.is_O_with.norm_right

@[simp] theorem is_O_norm_right : is_O f (Î» x, âˆ¥g' xâˆ¥) l â†” is_O f g' l :=
by { unfold is_O, exact exists_congr (Î» _, is_O_with_norm_right) }

alias is_O_norm_right â†” asymptotics.is_O.of_norm_right asymptotics.is_O.norm_right

@[simp] theorem is_o_norm_right : is_o f (Î» x, âˆ¥g' xâˆ¥) l â†” is_o f g' l :=
by { unfold is_o, exact forall_congr (Î» _, forall_congr $ Î» _, is_O_with_norm_right) }

alias is_o_norm_right â†” asymptotics.is_o.of_norm_right asymptotics.is_o.norm_right

@[simp] theorem is_O_with_norm_left : is_O_with c (Î» x, âˆ¥f' xâˆ¥) g l â†” is_O_with c f' g l :=
by simp only [is_O_with, norm_norm]

alias is_O_with_norm_left â†” asymptotics.is_O_with.of_norm_left asymptotics.is_O_with.norm_left

@[simp] theorem is_O_norm_left : is_O (Î» x, âˆ¥f' xâˆ¥) g l â†” is_O f' g l :=
by { unfold is_O, exact exists_congr (Î» _, is_O_with_norm_left) }

alias is_O_norm_left â†” asymptotics.is_O.of_norm_left asymptotics.is_O.norm_left

@[simp] theorem is_o_norm_left : is_o (Î» x, âˆ¥f' xâˆ¥) g l â†” is_o f' g l :=
by { unfold is_o,  exact forall_congr (Î» _, forall_congr $ Î» _, is_O_with_norm_left) }

alias is_o_norm_left â†” asymptotics.is_o.of_norm_left asymptotics.is_o.norm_left

theorem is_O_with_norm_norm :
  is_O_with c (Î» x, âˆ¥f' xâˆ¥) (Î» x, âˆ¥g' xâˆ¥) l â†” is_O_with c f' g' l :=
is_O_with_norm_left.trans is_O_with_norm_right

alias is_O_with_norm_norm â†” asymptotics.is_O_with.of_norm_norm asymptotics.is_O_with.norm_norm

theorem is_O_norm_norm :
  is_O (Î» x, âˆ¥f' xâˆ¥) (Î» x, âˆ¥g' xâˆ¥) l â†” is_O f' g' l :=
is_O_norm_left.trans is_O_norm_right

alias is_O_norm_norm â†” asymptotics.is_O.of_norm_norm asymptotics.is_O.norm_norm

theorem is_o_norm_norm :
  is_o (Î» x, âˆ¥f' xâˆ¥) (Î» x, âˆ¥g' xâˆ¥) l â†” is_o f' g' l :=
is_o_norm_left.trans is_o_norm_right

alias is_o_norm_norm â†” asymptotics.is_o.of_norm_norm asymptotics.is_o.norm_norm

/-! ### Simplification: negate -/

@[simp] theorem is_O_with_neg_right : is_O_with c f (Î» x, -(g' x)) l â†” is_O_with c f g' l :=
by simp only [is_O_with, norm_neg]

alias is_O_with_neg_right â†” asymptotics.is_O_with.of_neg_right asymptotics.is_O_with.neg_right

@[simp] theorem is_O_neg_right : is_O f (Î» x, -(g' x)) l â†” is_O f g' l :=
by { unfold is_O, exact exists_congr (Î» _, is_O_with_neg_right) }

alias is_O_neg_right â†” asymptotics.is_O.of_neg_right asymptotics.is_O.neg_right

@[simp] theorem is_o_neg_right : is_o f (Î» x, -(g' x)) l â†” is_o f g' l :=
by { unfold is_o, exact forall_congr (Î» _, (forall_congr (Î» _, is_O_with_neg_right))) }

alias is_o_neg_right â†” asymptotics.is_o.of_neg_right asymptotics.is_o.neg_right

@[simp] theorem is_O_with_neg_left : is_O_with c (Î» x, -(f' x)) g l â†” is_O_with c f' g l :=
by simp only [is_O_with, norm_neg]

alias is_O_with_neg_left â†” asymptotics.is_O_with.of_neg_left asymptotics.is_O_with.neg_left

@[simp] theorem is_O_neg_left : is_O (Î» x, -(f' x)) g l â†” is_O f' g l :=
by { unfold is_O, exact exists_congr (Î» _, is_O_with_neg_left) }

alias is_O_neg_left â†” asymptotics.is_O.of_neg_left asymptotics.is_O.neg_left

@[simp] theorem is_o_neg_left : is_o (Î» x, -(f' x)) g l â†” is_o f' g l :=
by { unfold is_o, exact forall_congr (Î» _, (forall_congr (Î» _, is_O_with_neg_left))) }

alias is_o_neg_left â†” asymptotics.is_o.of_neg_right asymptotics.is_o.neg_left

/-! ### Product of functions (right) -/

lemma is_O_with_fst_prod : is_O_with 1 f' (Î» x, (f' x, g' x)) l :=
is_O_with_of_le l $ Î» x, le_max_left _ _

lemma is_O_with_snd_prod : is_O_with 1 g' (Î» x, (f' x, g' x)) l :=
is_O_with_of_le l $ Î» x, le_max_right _ _

lemma is_O_fst_prod : is_O f' (Î» x, (f' x, g' x)) l := is_O_with_fst_prod.is_O

lemma is_O_snd_prod : is_O g' (Î» x, (f' x, g' x)) l := is_O_with_snd_prod.is_O

lemma is_O_fst_prod' {f' : Î± â†’ E' Ã— F'} : is_O (Î» x, (f' x).1) f' l :=
by simpa [is_O, is_O_with] using is_O_fst_prod

lemma is_O_snd_prod' {f' : Î± â†’ E' Ã— F'} : is_O (Î» x, (f' x).2) f' l :=
by simpa [is_O, is_O_with] using is_O_snd_prod

section

variables (f' k')

lemma is_O_with.prod_rightl (h : is_O_with c f g' l) (hc : 0 â‰¤ c) :
  is_O_with c f (Î» x, (g' x, k' x)) l :=
(h.trans is_O_with_fst_prod hc).congr_const (mul_one c)

lemma is_O.prod_rightl (h : is_O f g' l) : is_O f (Î»x, (g' x, k' x)) l :=
let âŸ¨c, cnonneg, hcâŸ© := h.exists_nonneg in (hc.prod_rightl k' cnonneg).is_O

lemma is_o.prod_rightl (h : is_o f g' l) : is_o f (Î» x, (g' x, k' x)) l :=
is_o.of_is_O_with $
Î» c cpos, (h.forall_is_O_with cpos).prod_rightl k' (le_of_lt cpos)

lemma is_O_with.prod_rightr (h : is_O_with c f g' l) (hc : 0 â‰¤ c) :
  is_O_with c f (Î» x, (f' x, g' x)) l :=
(h.trans is_O_with_snd_prod hc).congr_const (mul_one c)

lemma is_O.prod_rightr (h : is_O f g' l) : is_O f (Î»x, (f' x, g' x)) l :=
let âŸ¨c, cnonneg, hcâŸ© := h.exists_nonneg in (hc.prod_rightr f' cnonneg).is_O

lemma is_o.prod_rightr (h : is_o f g' l) : is_o f (Î»x, (f' x, g' x)) l :=
is_o.of_is_O_with $
Î» c cpos, (h.forall_is_O_with cpos).prod_rightr f' (le_of_lt cpos)

end

lemma is_O_with.prod_left_same (hf : is_O_with c f' k' l) (hg : is_O_with c g' k' l) :
  is_O_with c (Î» x, (f' x, g' x)) k' l :=
by rw is_O_with_iff at *; filter_upwards [hf, hg] Î» x, max_le

lemma is_O_with.prod_left (hf : is_O_with c f' k' l) (hg : is_O_with c' g' k' l) :
  is_O_with (max c c') (Î» x, (f' x, g' x)) k' l :=
(hf.weaken $ le_max_left c c').prod_left_same (hg.weaken $ le_max_right c c')

lemma is_O_with.prod_left_fst (h : is_O_with c (Î» x, (f' x, g' x)) k' l) :
  is_O_with c f' k' l :=
(is_O_with_fst_prod.trans h zero_le_one).congr_const $ one_mul c

lemma is_O_with.prod_left_snd (h : is_O_with c (Î» x, (f' x, g' x)) k' l) :
  is_O_with c g' k' l :=
(is_O_with_snd_prod.trans h zero_le_one).congr_const $ one_mul c

lemma is_O_with_prod_left :
   is_O_with c (Î» x, (f' x, g' x)) k' l â†” is_O_with c f' k' l âˆ§ is_O_with c g' k' l :=
âŸ¨Î» h, âŸ¨h.prod_left_fst, h.prod_left_sndâŸ©, Î» h, h.1.prod_left_same h.2âŸ©

lemma is_O.prod_left (hf : is_O f' k' l) (hg : is_O g' k' l) : is_O (Î» x, (f' x, g' x)) k' l :=
let âŸ¨c, hfâŸ© := hf.is_O_with, âŸ¨c', hgâŸ© := hg.is_O_with in (hf.prod_left hg).is_O

lemma is_O.prod_left_fst (h : is_O (Î» x, (f' x, g' x)) k' l) : is_O f' k' l :=
is_O_fst_prod.trans h

lemma is_O.prod_left_snd (h : is_O (Î» x, (f' x, g' x)) k' l) : is_O g' k' l :=
is_O_snd_prod.trans h

@[simp] lemma is_O_prod_left :
  is_O (Î» x, (f' x, g' x)) k' l â†” is_O f' k' l âˆ§ is_O g' k' l :=
âŸ¨Î» h, âŸ¨h.prod_left_fst, h.prod_left_sndâŸ©, Î» h, h.1.prod_left h.2âŸ©

lemma is_o.prod_left (hf : is_o f' k' l) (hg : is_o g' k' l) : is_o (Î» x, (f' x, g' x)) k' l :=
is_o.of_is_O_with $
Î» c hc, (hf.forall_is_O_with hc).prod_left_same (hg.forall_is_O_with hc)

lemma is_o.prod_left_fst (h : is_o (Î» x, (f' x, g' x)) k' l) : is_o f' k' l :=
is_O_fst_prod.trans_is_o h

lemma is_o.prod_left_snd (h : is_o (Î» x, (f' x, g' x)) k' l) : is_o g' k' l :=
is_O_snd_prod.trans_is_o h

@[simp] lemma is_o_prod_left :
  is_o (Î» x, (f' x, g' x)) k' l â†” is_o f' k' l âˆ§ is_o g' k' l :=
âŸ¨Î» h, âŸ¨h.prod_left_fst, h.prod_left_sndâŸ©, Î» h, h.1.prod_left h.2âŸ©

lemma is_O_with.eq_zero_imp (h : is_O_with c f' g' l) : âˆ€á¶  x in l, g' x = 0 â†’ f' x = 0 :=
eventually.mono h.bound $ Î» x hx hg, norm_le_zero_iff.1 $ by simpa [hg] using hx

lemma is_O.eq_zero_imp (h : is_O f' g' l) : âˆ€á¶  x in l, g' x = 0 â†’ f' x = 0 :=
let âŸ¨C, hCâŸ© := h.is_O_with in hC.eq_zero_imp

/-! ### Addition and subtraction -/

section add_sub

variables {câ‚ câ‚‚ : â„} {fâ‚ fâ‚‚ : Î± â†’ E'}

theorem is_O_with.add (hâ‚ : is_O_with câ‚ fâ‚ g l) (hâ‚‚ : is_O_with câ‚‚ fâ‚‚ g l) :
  is_O_with (câ‚ + câ‚‚) (Î» x, fâ‚ x + fâ‚‚ x) g l :=
by rw is_O_with at *; filter_upwards [hâ‚, hâ‚‚] Î» x hxâ‚ hxâ‚‚,
calc âˆ¥fâ‚ x + fâ‚‚ xâˆ¥ â‰¤ câ‚ * âˆ¥g xâˆ¥ + câ‚‚ * âˆ¥g xâˆ¥ : norm_add_le_of_le hxâ‚ hxâ‚‚
               ... = (câ‚ + câ‚‚) * âˆ¥g xâˆ¥       : (add_mul _ _ _).symm

theorem is_O.add (hâ‚ : is_O fâ‚ g l) (hâ‚‚ : is_O fâ‚‚ g l) : is_O (Î» x, fâ‚ x + fâ‚‚ x) g l :=
let âŸ¨câ‚, hcâ‚âŸ© := hâ‚.is_O_with, âŸ¨câ‚‚, hcâ‚‚âŸ© := hâ‚‚.is_O_with in (hcâ‚.add hcâ‚‚).is_O

theorem is_o.add (hâ‚ : is_o fâ‚ g l) (hâ‚‚ : is_o fâ‚‚ g l) : is_o (Î» x, fâ‚ x + fâ‚‚ x) g l :=
is_o.of_is_O_with $ Î» c cpos, ((hâ‚.forall_is_O_with $ half_pos cpos).add
  (hâ‚‚.forall_is_O_with $ half_pos cpos)).congr_const (add_halves c)

theorem is_o.add_add {gâ‚ gâ‚‚ : Î± â†’ F'} (hâ‚ : is_o fâ‚ gâ‚ l) (hâ‚‚ : is_o fâ‚‚ gâ‚‚ l) :
  is_o (Î» x, fâ‚ x + fâ‚‚ x) (Î» x, âˆ¥gâ‚ xâˆ¥ + âˆ¥gâ‚‚ xâˆ¥) l :=
by refine (hâ‚.trans_le $ Î» x, _).add (hâ‚‚.trans_le _);
  simp [real.norm_eq_abs, abs_of_nonneg, add_nonneg]

theorem is_O.add_is_o (hâ‚ : is_O fâ‚ g l) (hâ‚‚ : is_o fâ‚‚ g l) : is_O (Î» x, fâ‚ x + fâ‚‚ x) g l :=
hâ‚.add hâ‚‚.is_O

theorem is_o.add_is_O (hâ‚ : is_o fâ‚ g l) (hâ‚‚ : is_O fâ‚‚ g l) : is_O (Î» x, fâ‚ x + fâ‚‚ x) g l :=
hâ‚.is_O.add hâ‚‚

theorem is_O_with.add_is_o (hâ‚ : is_O_with câ‚ fâ‚ g l) (hâ‚‚ : is_o fâ‚‚ g l) (hc : câ‚ < câ‚‚) :
  is_O_with câ‚‚ (Î»x, fâ‚ x + fâ‚‚ x) g l :=
(hâ‚.add (hâ‚‚.forall_is_O_with (sub_pos.2 hc))).congr_const (add_sub_cancel'_right _ _)

theorem is_o.add_is_O_with (hâ‚ : is_o fâ‚ g l) (hâ‚‚ : is_O_with câ‚ fâ‚‚ g l) (hc : câ‚ < câ‚‚) :
  is_O_with câ‚‚ (Î»x, fâ‚ x + fâ‚‚ x) g l :=
(hâ‚‚.add_is_o hâ‚ hc).congr_left $ Î» _, add_comm _ _

theorem is_O_with.sub (hâ‚ : is_O_with câ‚ fâ‚ g l) (hâ‚‚ : is_O_with câ‚‚ fâ‚‚ g l) :
  is_O_with (câ‚ + câ‚‚) (Î» x, fâ‚ x - fâ‚‚ x) g l :=
by simpa only [sub_eq_add_neg] using hâ‚.add hâ‚‚.neg_left

theorem is_O_with.sub_is_o (hâ‚ : is_O_with câ‚ fâ‚ g l) (hâ‚‚ : is_o fâ‚‚ g l) (hc : câ‚ < câ‚‚) :
  is_O_with câ‚‚ (Î» x, fâ‚ x - fâ‚‚ x) g l :=
by simpa only [sub_eq_add_neg] using hâ‚.add_is_o hâ‚‚.neg_left hc

theorem is_O.sub (hâ‚ : is_O fâ‚ g l) (hâ‚‚ : is_O fâ‚‚ g l) : is_O (Î» x, fâ‚ x - fâ‚‚ x) g l :=
by simpa only [sub_eq_add_neg] using hâ‚.add hâ‚‚.neg_left

theorem is_o.sub (hâ‚ : is_o fâ‚ g l) (hâ‚‚ : is_o fâ‚‚ g l) : is_o (Î» x, fâ‚ x - fâ‚‚ x) g l :=
by simpa only [sub_eq_add_neg] using hâ‚.add hâ‚‚.neg_left

end add_sub

/-! ### Lemmas about `is_O (fâ‚ - fâ‚‚) g l` / `is_o (fâ‚ - fâ‚‚) g l` treated as a binary relation -/

section is_oO_as_rel

variables {fâ‚ fâ‚‚ fâ‚ƒ : Î± â†’ E'}

theorem is_O_with.symm (h : is_O_with c (Î» x, fâ‚ x - fâ‚‚ x) g l) :
  is_O_with c (Î» x, fâ‚‚ x - fâ‚ x) g l :=
h.neg_left.congr_left $ Î» x, neg_sub _ _

theorem is_O_with_comm :
  is_O_with c (Î» x, fâ‚ x - fâ‚‚ x) g l â†” is_O_with c (Î» x, fâ‚‚ x - fâ‚ x) g l :=
âŸ¨is_O_with.symm, is_O_with.symmâŸ©

theorem is_O.symm (h : is_O (Î» x, fâ‚ x - fâ‚‚ x) g l) : is_O (Î» x, fâ‚‚ x - fâ‚ x) g l :=
h.neg_left.congr_left $ Î» x, neg_sub _ _

theorem is_O_comm : is_O (Î» x, fâ‚ x - fâ‚‚ x) g l â†” is_O (Î» x, fâ‚‚ x - fâ‚ x) g l :=
âŸ¨is_O.symm, is_O.symmâŸ©

theorem is_o.symm (h : is_o (Î» x, fâ‚ x - fâ‚‚ x) g l) : is_o (Î» x, fâ‚‚ x - fâ‚ x) g l :=
by simpa only [neg_sub] using h.neg_left

theorem is_o_comm : is_o (Î» x, fâ‚ x - fâ‚‚ x) g l â†” is_o (Î» x, fâ‚‚ x - fâ‚ x) g l :=
âŸ¨is_o.symm, is_o.symmâŸ©

theorem is_O_with.triangle (hâ‚ : is_O_with c (Î» x, fâ‚ x - fâ‚‚ x) g l)
  (hâ‚‚ : is_O_with c' (Î» x, fâ‚‚ x - fâ‚ƒ x) g l) :
  is_O_with (c + c') (Î» x, fâ‚ x - fâ‚ƒ x) g l :=
(hâ‚.add hâ‚‚).congr_left $ Î» x, sub_add_sub_cancel _ _ _

theorem is_O.triangle (hâ‚ : is_O (Î» x, fâ‚ x - fâ‚‚ x) g l) (hâ‚‚ : is_O (Î» x, fâ‚‚ x - fâ‚ƒ x) g l) :
  is_O (Î» x, fâ‚ x - fâ‚ƒ x) g l :=
(hâ‚.add hâ‚‚).congr_left $ Î» x, sub_add_sub_cancel _ _ _

theorem is_o.triangle (hâ‚ : is_o (Î» x, fâ‚ x - fâ‚‚ x) g l) (hâ‚‚ : is_o (Î» x, fâ‚‚ x - fâ‚ƒ x) g l) :
  is_o (Î» x, fâ‚ x - fâ‚ƒ x) g l :=
(hâ‚.add hâ‚‚).congr_left $ Î» x, sub_add_sub_cancel _ _ _

theorem is_O.congr_of_sub (h : is_O (Î» x, fâ‚ x - fâ‚‚ x) g l) :
  is_O fâ‚ g l â†” is_O fâ‚‚ g l :=
âŸ¨Î» h', (h'.sub h).congr_left (Î» x, sub_sub_cancel _ _),
 Î» h', (h.add h').congr_left (Î» x, sub_add_cancel _ _)âŸ©

theorem is_o.congr_of_sub (h : is_o (Î» x, fâ‚ x - fâ‚‚ x) g l) :
  is_o fâ‚ g l â†” is_o fâ‚‚ g l :=
âŸ¨Î» h', (h'.sub h).congr_left (Î» x, sub_sub_cancel _ _),
 Î» h', (h.add h').congr_left (Î» x, sub_add_cancel _ _)âŸ©

end is_oO_as_rel

/-! ### Zero, one, and other constants -/

section zero_const

variables (g g' l)

theorem is_o_zero : is_o (Î» x, (0 : E')) g' l :=
is_o.of_bound $ Î» c hc, univ_mem' $ Î» x,
by simpa using mul_nonneg (le_of_lt hc) (norm_nonneg $ g' x)

theorem is_O_with_zero (hc : 0 â‰¤ c) : is_O_with c (Î» x, (0 : E')) g' l :=
is_O_with.of_bound $ univ_mem' $ Î» x, by simpa using mul_nonneg hc (norm_nonneg $ g' x)

theorem is_O_with_zero' : is_O_with 0 (Î» x, (0 : E')) g l :=
is_O_with.of_bound $ univ_mem' $ Î» x, by simp

theorem is_O_zero : is_O (Î» x, (0 : E')) g l :=
is_O_iff_is_O_with.2 âŸ¨0, is_O_with_zero' _ _âŸ©

theorem is_O_refl_left : is_O (Î» x, f' x - f' x) g' l :=
(is_O_zero g' l).congr_left $ Î» x, (sub_self _).symm

theorem is_o_refl_left : is_o (Î» x, f' x - f' x) g' l :=
(is_o_zero g' l).congr_left $ Î» x, (sub_self _).symm

variables {g g' l}

@[simp] theorem is_O_with_zero_right_iff :
  is_O_with c f' (Î» x, (0 : F')) l â†” âˆ€á¶  x in l, f' x = 0 :=
by simp only [is_O_with, exists_prop, true_and, norm_zero, mul_zero, norm_le_zero_iff]

@[simp] theorem is_O_zero_right_iff : is_O f' (Î» x, (0 : F')) l â†” âˆ€á¶  x in l, f' x = 0 :=
âŸ¨Î» h, let âŸ¨c, hcâŸ© := h.is_O_with in is_O_with_zero_right_iff.1 hc,
  Î» h, (is_O_with_zero_right_iff.2 h : is_O_with 1 _ _ _).is_OâŸ©

@[simp] theorem is_o_zero_right_iff :
  is_o f' (Î» x, (0 : F')) l â†” âˆ€á¶  x in l, f' x = 0 :=
âŸ¨Î» h, is_O_zero_right_iff.1 h.is_O,
  Î» h, is_o.of_is_O_with $ Î» c hc, is_O_with_zero_right_iff.2 hâŸ©

theorem is_O_with_const_const (c : E) {c' : F'} (hc' : c' â‰  0) (l : filter Î±) :
  is_O_with (âˆ¥câˆ¥ / âˆ¥c'âˆ¥) (Î» x : Î±, c) (Î» x, c') l :=
begin
  unfold is_O_with,
  apply univ_mem',
  intro x,
  rw [mem_set_of_eq, div_mul_cancel],
  rwa [ne.def, norm_eq_zero]
end

theorem is_O_const_const (c : E) {c' : F'} (hc' : c' â‰  0) (l : filter Î±) :
  is_O (Î» x : Î±, c) (Î» x, c') l :=
(is_O_with_const_const c hc' l).is_O

end zero_const

@[simp] lemma is_O_with_top : is_O_with c f g âŠ¤ â†” âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g xâˆ¥ := by rw is_O_with; refl

@[simp] lemma is_O_top : is_O f g âŠ¤ â†” âˆƒ C, âˆ€ x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥g xâˆ¥ := by rw is_O_iff; refl

@[simp] lemma is_o_top : is_o f' g' âŠ¤ â†” âˆ€ x, f' x = 0 :=
begin
  refine âŸ¨_, Î» h, (is_o_zero g' âŠ¤).congr (Î» x, (h x).symm) (Î» x, rfl)âŸ©,
  simp only [is_o_iff, eventually_top],
  refine Î» h x, norm_le_zero_iff.1 _,
  have : tendsto (Î» c : â„, c * âˆ¥g' xâˆ¥) (ğ“[>] 0) (ğ“ 0) :=
    ((continuous_id.mul continuous_const).tendsto' _ _ (zero_mul _)).mono_left inf_le_left,
  exact le_of_tendsto_of_tendsto tendsto_const_nhds this
    (eventually_nhds_within_iff.2 $ eventually_of_forall $ Î» c hc, h hc x)
end

@[simp] lemma is_O_with_principal {s : set Î±} :
  is_O_with c f g (ğ“Ÿ s) â†” âˆ€ x âˆˆ s, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g xâˆ¥ :=
by rw is_O_with; refl

lemma is_O_principal {s : set Î±} :
  is_O f g (ğ“Ÿ s) â†” âˆƒ c, âˆ€ x âˆˆ s, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥g xâˆ¥ :=
by rw is_O_iff; refl

theorem is_O_with_const_one (c : E) (l : filter Î±) : is_O_with âˆ¥câˆ¥ (Î» x : Î±, c) (Î» x, (1 : ğ•œ)) l :=
begin
  refine (is_O_with_const_const c _ l).congr_const _,
  { rw [norm_one, div_one] },
  { exact one_ne_zero }
end

theorem is_O_const_one (c : E) (l : filter Î±) : is_O (Î» x : Î±, c) (Î» x, (1 : ğ•œ)) l :=
(is_O_with_const_one c l).is_O

section

variable (ğ•œ)

theorem is_o_const_iff_is_o_one {c : F'} (hc : c â‰  0) :
  is_o f (Î» x, c) l â†” is_o f (Î» x, (1:ğ•œ)) l :=
âŸ¨Î» h, h.trans_is_O $ is_O_const_one c l, Î» h, h.trans_is_O $ is_O_const_const _ hc _âŸ©

end

theorem is_o_const_iff {c : F'} (hc : c â‰  0) :
  is_o f' (Î» x, c) l â†” tendsto f' l (ğ“ 0) :=
(is_o_const_iff_is_o_one â„ hc).trans
begin
  clear hc c,
  simp only [is_o, is_O_with, norm_one, mul_one, metric.nhds_basis_closed_ball.tendsto_right_iff,
    metric.mem_closed_ball, dist_zero_right]
end

theorem is_o_const_const_iff [ne_bot l] {d : E'} {c : F'} (hc : c â‰  0) :
  is_o (Î» x, d) (Î» x, c) l â†” d = 0 :=
begin
  rw is_o_const_iff hc,
  refine âŸ¨Î» h, tendsto_nhds_unique tendsto_const_nhds h, _âŸ©,
  rintros rfl,
  exact tendsto_const_nhds,
end

lemma is_o_id_const {c : F'} (hc : c â‰  0) :
  is_o (Î» (x : E'), x) (Î» x, c) (ğ“ 0) :=
(is_o_const_iff hc).mpr (continuous_id.tendsto 0)

theorem is_O_const_of_tendsto {y : E'} (h : tendsto f' l (ğ“ y)) {c : F'} (hc : c â‰  0) :
  is_O f' (Î» x, c) l :=
begin
  refine is_O.trans _ (is_O_const_const (âˆ¥yâˆ¥ + 1) hc l),
  refine is_O.of_bound 1 _,
  simp only [is_O_with, one_mul],
  have : tendsto (Î»x, âˆ¥f' xâˆ¥) l (ğ“ âˆ¥yâˆ¥), from (continuous_norm.tendsto _).comp h,
  have Iy : âˆ¥yâˆ¥ < âˆ¥âˆ¥yâˆ¥ + 1âˆ¥, from lt_of_lt_of_le (lt_add_one _) (le_abs_self _),
  exact this (ge_mem_nhds Iy)
end

section

variable (ğ•œ)

theorem is_o_one_iff : is_o f' (Î» x, (1 : ğ•œ)) l â†” tendsto f' l (ğ“ 0) :=
is_o_const_iff one_ne_zero

theorem is_O_one_of_tendsto {y : E'} (h : tendsto f' l (ğ“ y)) :
  is_O f' (Î» x, (1:ğ•œ)) l :=
is_O_const_of_tendsto h one_ne_zero

theorem is_O.trans_tendsto_nhds (hfg : is_O f g' l) {y : F'} (hg : tendsto g' l (ğ“ y)) :
  is_O f (Î» x, (1:ğ•œ)) l :=
hfg.trans $ is_O_one_of_tendsto ğ•œ hg

end

theorem is_O.trans_tendsto (hfg : is_O f' g' l) (hg : tendsto g' l (ğ“ 0)) :
  tendsto f' l (ğ“ 0) :=
(is_o_one_iff â„).1 $ hfg.trans_is_o $ (is_o_one_iff â„).2 hg

theorem is_o.trans_tendsto (hfg : is_o f' g' l) (hg : tendsto g' l (ğ“ 0)) :
  tendsto f' l (ğ“ 0) :=
hfg.is_O.trans_tendsto hg

/-! ### Multiplication by a constant -/

theorem is_O_with_const_mul_self (c : R) (f : Î± â†’ R) (l : filter Î±) :
  is_O_with âˆ¥câˆ¥ (Î» x, c * f x) f l :=
is_O_with_of_le' _ $ Î» x, norm_mul_le _ _

theorem is_O_const_mul_self (c : R) (f : Î± â†’ R) (l : filter Î±) :
  is_O (Î» x, c * f x) f l :=
(is_O_with_const_mul_self c f l).is_O

theorem is_O_with.const_mul_left {f : Î± â†’ R} (h : is_O_with c f g l) (c' : R) :
  is_O_with (âˆ¥c'âˆ¥ * c) (Î» x, c' * f x) g l :=
(is_O_with_const_mul_self c' f l).trans h (norm_nonneg c')

theorem is_O.const_mul_left {f : Î± â†’ R} (h : is_O f g l) (c' : R) :
  is_O (Î» x, c' * f x) g l :=
let âŸ¨c, hcâŸ© := h.is_O_with in (hc.const_mul_left c').is_O

theorem is_O_with_self_const_mul' (u : units R) (f : Î± â†’ R) (l : filter Î±) :
  is_O_with âˆ¥(â†‘uâ»Â¹:R)âˆ¥ f (Î» x, â†‘u * f x) l :=
(is_O_with_const_mul_self â†‘uâ»Â¹ _ l).congr_left $ Î» x, u.inv_mul_cancel_left (f x)

theorem is_O_with_self_const_mul (c : ğ•œ) (hc : c â‰  0) (f : Î± â†’ ğ•œ) (l : filter Î±) :
  is_O_with âˆ¥câˆ¥â»Â¹ f (Î» x, c * f x) l :=
(is_O_with_self_const_mul' (units.mk0 c hc) f l).congr_const $
  normed_field.norm_inv c

theorem is_O_self_const_mul' {c : R} (hc : is_unit c) (f : Î± â†’ R) (l : filter Î±) :
  is_O f (Î» x, c * f x) l :=
let âŸ¨u, huâŸ© := hc in hu â–¸ (is_O_with_self_const_mul' u f l).is_O

theorem is_O_self_const_mul (c : ğ•œ) (hc : c â‰  0) (f : Î± â†’ ğ•œ) (l : filter Î±) :
  is_O f (Î» x, c * f x) l :=
is_O_self_const_mul' (is_unit.mk0 c hc) f l

theorem is_O_const_mul_left_iff' {f : Î± â†’ R} {c : R} (hc : is_unit c) :
  is_O (Î» x, c * f x) g l â†” is_O f g l :=
âŸ¨(is_O_self_const_mul' hc f l).trans, Î» h, h.const_mul_left câŸ©

theorem is_O_const_mul_left_iff {f : Î± â†’ ğ•œ} {c : ğ•œ} (hc : c â‰  0) :
  is_O (Î» x, c * f x) g l â†” is_O f g l :=
is_O_const_mul_left_iff' $ is_unit.mk0 c hc

theorem is_o.const_mul_left {f : Î± â†’ R} (h : is_o f g l) (c : R) :
  is_o (Î» x, c * f x) g l :=
(is_O_const_mul_self c f l).trans_is_o h

theorem is_o_const_mul_left_iff' {f : Î± â†’ R} {c : R} (hc : is_unit c) :
  is_o (Î» x, c * f x) g l â†” is_o f g l :=
âŸ¨(is_O_self_const_mul' hc f l).trans_is_o, Î» h, h.const_mul_left câŸ©

theorem is_o_const_mul_left_iff {f : Î± â†’ ğ•œ} {c : ğ•œ} (hc : c â‰  0) :
  is_o (Î» x, c * f x) g l â†” is_o f g l :=
is_o_const_mul_left_iff' $ is_unit.mk0 c hc

theorem is_O_with.of_const_mul_right {g : Î± â†’ R} {c : R} (hc' : 0 â‰¤ c')
  (h : is_O_with c' f (Î» x, c * g x) l) :
  is_O_with (c' * âˆ¥câˆ¥) f g l :=
h.trans (is_O_with_const_mul_self c g l) hc'

theorem is_O.of_const_mul_right {g : Î± â†’ R} {c : R}
  (h : is_O f (Î» x, c * g x) l) :
  is_O f g l :=
let âŸ¨c, cnonneg, hcâŸ© := h.exists_nonneg in (hc.of_const_mul_right cnonneg).is_O

theorem is_O_with.const_mul_right' {g : Î± â†’ R} {u : units R} {c' : â„} (hc' : 0 â‰¤ c')
  (h : is_O_with c' f g l) :
  is_O_with (c' * âˆ¥(â†‘uâ»Â¹:R)âˆ¥) f (Î» x, â†‘u * g x) l :=
h.trans (is_O_with_self_const_mul' _ _ _) hc'

theorem is_O_with.const_mul_right {g : Î± â†’ ğ•œ} {c : ğ•œ} (hc : c â‰  0)
  {c' : â„} (hc' : 0 â‰¤ c') (h : is_O_with c' f g l) :
  is_O_with (c' * âˆ¥câˆ¥â»Â¹) f (Î» x, c * g x) l :=
h.trans (is_O_with_self_const_mul c hc g l) hc'

theorem is_O.const_mul_right' {g : Î± â†’ R} {c : R} (hc : is_unit c) (h : is_O f g l) :
  is_O f (Î» x, c * g x) l :=
h.trans (is_O_self_const_mul' hc g l)

theorem is_O.const_mul_right {g : Î± â†’ ğ•œ} {c : ğ•œ} (hc : c â‰  0) (h : is_O f g l) :
  is_O f (Î» x, c * g x) l :=
h.const_mul_right' $ is_unit.mk0 c hc

theorem is_O_const_mul_right_iff' {g : Î± â†’ R} {c : R} (hc : is_unit c) :
  is_O f (Î» x, c * g x) l â†” is_O f g l :=
âŸ¨Î» h, h.of_const_mul_right, Î» h, h.const_mul_right' hcâŸ©

theorem is_O_const_mul_right_iff {g : Î± â†’ ğ•œ} {c : ğ•œ} (hc : c â‰  0) :
  is_O f (Î» x, c * g x) l â†” is_O f g l :=
is_O_const_mul_right_iff' $ is_unit.mk0 c hc

theorem is_o.of_const_mul_right {g : Î± â†’ R} {c : R} (h : is_o f (Î» x, c * g x) l) :
  is_o f g l :=
h.trans_is_O (is_O_const_mul_self c g l)

theorem is_o.const_mul_right' {g : Î± â†’ R} {c : R} (hc : is_unit c) (h : is_o f g l) :
  is_o f (Î» x, c * g x) l :=
h.trans_is_O (is_O_self_const_mul' hc g l)

theorem is_o.const_mul_right {g : Î± â†’ ğ•œ} {c : ğ•œ} (hc : c â‰  0) (h : is_o f g l) :
  is_o f (Î» x, c * g x) l :=
h.const_mul_right' $ is_unit.mk0 c hc

theorem is_o_const_mul_right_iff' {g : Î± â†’ R} {c : R} (hc : is_unit c) :
  is_o f (Î» x, c * g x) l â†” is_o f g l :=
âŸ¨Î» h, h.of_const_mul_right, Î» h, h.const_mul_right' hcâŸ©

theorem is_o_const_mul_right_iff {g : Î± â†’ ğ•œ} {c : ğ•œ} (hc : c â‰  0) :
  is_o f (Î» x, c * g x) l â†” is_o f g l :=
is_o_const_mul_right_iff' $ is_unit.mk0 c hc

/-! ### Multiplication -/

theorem is_O_with.mul {fâ‚ fâ‚‚ : Î± â†’ R} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ} {câ‚ câ‚‚ : â„}
  (hâ‚ : is_O_with câ‚ fâ‚ gâ‚ l) (hâ‚‚ : is_O_with câ‚‚ fâ‚‚ gâ‚‚ l) :
  is_O_with (câ‚ * câ‚‚) (Î» x, fâ‚ x * fâ‚‚ x) (Î» x, gâ‚ x * gâ‚‚ x) l :=
begin
  unfold is_O_with at *,
  filter_upwards [hâ‚, hâ‚‚],
  intros x hxâ‚ hxâ‚‚,
  apply le_trans (norm_mul_le _ _),
  convert mul_le_mul hxâ‚ hxâ‚‚ (norm_nonneg _) (le_trans (norm_nonneg _) hxâ‚) using 1,
  rw normed_field.norm_mul,
  ac_refl
end

theorem is_O.mul {fâ‚ fâ‚‚ : Î± â†’ R} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ}
  (hâ‚ : is_O fâ‚ gâ‚ l) (hâ‚‚ : is_O fâ‚‚ gâ‚‚ l) :
  is_O (Î» x, fâ‚ x * fâ‚‚ x) (Î» x, gâ‚ x * gâ‚‚ x) l :=
let âŸ¨c, hcâŸ© := hâ‚.is_O_with, âŸ¨c', hc'âŸ© := hâ‚‚.is_O_with in (hc.mul hc').is_O

theorem is_O.mul_is_o {fâ‚ fâ‚‚ : Î± â†’ R} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ}
  (hâ‚ : is_O fâ‚ gâ‚ l) (hâ‚‚ : is_o fâ‚‚ gâ‚‚ l) :
  is_o (Î» x, fâ‚ x * fâ‚‚ x) (Î» x, gâ‚ x * gâ‚‚ x) l :=
begin
  unfold is_o at *,
  intros c cpos,
  rcases hâ‚.exists_pos with âŸ¨c', c'pos, hc'âŸ©,
  exact (hc'.mul (hâ‚‚ (div_pos cpos c'pos))).congr_const (mul_div_cancel' _ (ne_of_gt c'pos))
end

theorem is_o.mul_is_O {fâ‚ fâ‚‚ : Î± â†’ R} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ}
  (hâ‚ : is_o fâ‚ gâ‚ l) (hâ‚‚ : is_O fâ‚‚ gâ‚‚ l) :
  is_o (Î» x, fâ‚ x * fâ‚‚ x) (Î» x, gâ‚ x * gâ‚‚ x) l :=
begin
  unfold is_o at *,
  intros c cpos,
  rcases hâ‚‚.exists_pos with âŸ¨c', c'pos, hc'âŸ©,
  exact ((hâ‚ (div_pos cpos c'pos)).mul hc').congr_const (div_mul_cancel _ (ne_of_gt c'pos))
end

theorem is_o.mul {fâ‚ fâ‚‚ : Î± â†’ R} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ} (hâ‚ : is_o fâ‚ gâ‚ l) (hâ‚‚ : is_o fâ‚‚ gâ‚‚ l) :
  is_o (Î» x, fâ‚ x * fâ‚‚ x) (Î» x, gâ‚ x * gâ‚‚ x) l :=
hâ‚.mul_is_O hâ‚‚.is_O

theorem is_O_with.pow' {f : Î± â†’ R} {g : Î± â†’ ğ•œ} (h : is_O_with c f g l) :
  âˆ€ n : â„•, is_O_with (nat.cases_on n âˆ¥(1 : R)âˆ¥ (Î» n, c ^ (n + 1))) (Î» x, f x ^ n) (Î» x, g x ^ n) l
| 0 := by simpa using is_O_with_const_const (1 : R) (@one_ne_zero ğ•œ _ _) l
| 1 := by simpa
| (n + 2) := by simpa [pow_succ] using h.mul (is_O_with.pow' (n + 1))

theorem is_O_with.pow [norm_one_class R] {f : Î± â†’ R} {g : Î± â†’ ğ•œ} (h : is_O_with c f g l) :
  âˆ€ n : â„•, is_O_with (c ^ n) (Î» x, f x ^ n) (Î» x, g x ^ n) l
| 0 := by simpa using h.pow' 0
| (n + 1) := h.pow' (n + 1)

theorem is_O.pow {f : Î± â†’ R} {g : Î± â†’ ğ•œ} (h : is_O f g l) (n : â„•) :
  is_O (Î» x, f x ^ n) (Î» x, g x ^ n) l :=
let âŸ¨C, hCâŸ© := h.is_O_with in is_O_iff_is_O_with.2 âŸ¨_, hC.pow' nâŸ©

theorem is_o.pow {f : Î± â†’ R} {g : Î± â†’ ğ•œ} (h : is_o f g l) {n : â„•} (hn : 0 < n) :
  is_o (Î» x, f x ^ n) (Î» x, g x ^ n) l :=
begin
  cases n, exact hn.false.elim, clear hn,
  induction n with n ihn, { simpa only [pow_one] },
  convert h.mul ihn; simp [pow_succ]
end

/-! ### Inverse -/

theorem is_O_with.inv_rev {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : is_O_with c f g l)
  (hâ‚€ : âˆ€á¶  x in l, f x â‰  0) : is_O_with c (Î» x, (g x)â»Â¹) (Î» x, (f x)â»Â¹) l :=
begin
  refine is_O_with.of_bound (h.bound.mp (hâ‚€.mono $ Î» x hâ‚€ hle, _)),
  cases le_or_lt c 0 with hc hc,
  { refine (hâ‚€ $ norm_le_zero_iff.1 _).elim,
    exact hle.trans (mul_nonpos_of_nonpos_of_nonneg hc $ norm_nonneg _) },
  { replace hle := inv_le_inv_of_le (norm_pos_iff.2 hâ‚€) hle,
    simpa only [normed_field.norm_inv, mul_invâ‚€, â† div_eq_inv_mul, div_le_iff hc] using hle }
end

theorem is_O.inv_rev {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : is_O f g l)
  (hâ‚€ : âˆ€á¶  x in l, f x â‰  0) : is_O (Î» x, (g x)â»Â¹) (Î» x, (f x)â»Â¹) l :=
let âŸ¨c, hcâŸ© := h.is_O_with in (hc.inv_rev hâ‚€).is_O

theorem is_o.inv_rev {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : is_o f g l)
  (hâ‚€ : âˆ€á¶  x in l, f x â‰  0) : is_o (Î» x, (g x)â»Â¹) (Î» x, (f x)â»Â¹) l :=
is_o.of_is_O_with $ Î» c hc, (h.def' hc).inv_rev hâ‚€

/-! ### Scalar multiplication -/

section smul_const
variables [normed_space ğ•œ E']

theorem is_O_with.const_smul_left (h : is_O_with c f' g l) (c' : ğ•œ) :
  is_O_with (âˆ¥c'âˆ¥ * c) (Î» x, c' â€¢ f' x) g l :=
by refine ((h.norm_left.const_mul_left (âˆ¥c'âˆ¥)).congr _ _ (Î» _, rfl)).of_norm_left;
    intros; simp only [norm_norm, norm_smul]

theorem is_O_const_smul_left_iff {c : ğ•œ} (hc : c â‰  0) :
  is_O (Î» x, c â€¢ f' x) g l â†” is_O f' g l :=
begin
  have cne0 : âˆ¥câˆ¥ â‰  0, from mt norm_eq_zero.mp hc,
  rw [â†is_O_norm_left], simp only [norm_smul],
  rw [is_O_const_mul_left_iff cne0, is_O_norm_left],
end

theorem is_o_const_smul_left (h : is_o f' g l) (c : ğ•œ) :
  is_o (Î» x, c â€¢ f' x) g l :=
begin
  refine ((h.norm_left.const_mul_left (âˆ¥câˆ¥)).congr_left _).of_norm_left,
  exact Î» x, (norm_smul _ _).symm
end

theorem is_o_const_smul_left_iff {c : ğ•œ} (hc : c â‰  0) :
  is_o (Î» x, c â€¢ f' x) g l â†” is_o f' g l :=
begin
  have cne0 : âˆ¥câˆ¥ â‰  0, from mt norm_eq_zero.mp hc,
  rw [â†is_o_norm_left], simp only [norm_smul],
  rw [is_o_const_mul_left_iff cne0, is_o_norm_left]
end

theorem is_O_const_smul_right {c : ğ•œ} (hc : c â‰  0) :
  is_O f (Î» x, c â€¢ f' x) l â†” is_O f f' l :=
begin
  have cne0 : âˆ¥câˆ¥ â‰  0, from mt norm_eq_zero.mp hc,
  rw [â†is_O_norm_right], simp only [norm_smul],
  rw [is_O_const_mul_right_iff cne0, is_O_norm_right]
end

theorem is_o_const_smul_right {c : ğ•œ} (hc : c â‰  0) :
  is_o f (Î» x, c â€¢ f' x) l â†” is_o f f' l :=
begin
  have cne0 : âˆ¥câˆ¥ â‰  0, from mt norm_eq_zero.mp hc,
  rw [â†is_o_norm_right], simp only [norm_smul],
  rw [is_o_const_mul_right_iff cne0, is_o_norm_right]
end

end smul_const

section smul

variables [normed_space ğ•œ E'] [normed_space ğ•œ F']

theorem is_O_with.smul {kâ‚ kâ‚‚ : Î± â†’ ğ•œ} (hâ‚ : is_O_with c kâ‚ kâ‚‚ l) (hâ‚‚ : is_O_with c' f' g' l) :
  is_O_with (c * c') (Î» x, kâ‚ x â€¢ f' x) (Î» x, kâ‚‚ x â€¢ g' x) l :=
by refine ((hâ‚.norm_norm.mul hâ‚‚.norm_norm).congr rfl _ _).of_norm_norm;
  by intros; simp only [norm_smul]

theorem is_O.smul {kâ‚ kâ‚‚ : Î± â†’ ğ•œ} (hâ‚ : is_O kâ‚ kâ‚‚ l) (hâ‚‚ : is_O f' g' l) :
  is_O (Î» x, kâ‚ x â€¢ f' x) (Î» x, kâ‚‚ x â€¢ g' x) l :=
by refine ((hâ‚.norm_norm.mul hâ‚‚.norm_norm).congr _ _).of_norm_norm;
  by intros; simp only [norm_smul]

theorem is_O.smul_is_o {kâ‚ kâ‚‚ : Î± â†’ ğ•œ} (hâ‚ : is_O kâ‚ kâ‚‚ l) (hâ‚‚ : is_o f' g' l) :
  is_o (Î» x, kâ‚ x â€¢ f' x) (Î» x, kâ‚‚ x â€¢ g' x) l :=
by refine ((hâ‚.norm_norm.mul_is_o hâ‚‚.norm_norm).congr _ _).of_norm_norm;
  by intros; simp only [norm_smul]

theorem is_o.smul_is_O {kâ‚ kâ‚‚ : Î± â†’ ğ•œ} (hâ‚ : is_o kâ‚ kâ‚‚ l) (hâ‚‚ : is_O f' g' l) :
  is_o (Î» x, kâ‚ x â€¢ f' x) (Î» x, kâ‚‚ x â€¢ g' x) l :=
by refine ((hâ‚.norm_norm.mul_is_O hâ‚‚.norm_norm).congr _ _).of_norm_norm;
  by intros; simp only [norm_smul]

theorem is_o.smul {kâ‚ kâ‚‚ : Î± â†’ ğ•œ} (hâ‚ : is_o kâ‚ kâ‚‚ l) (hâ‚‚ : is_o f' g' l) :
  is_o (Î» x, kâ‚ x â€¢ f' x) (Î» x, kâ‚‚ x â€¢ g' x) l :=
by refine ((hâ‚.norm_norm.mul hâ‚‚.norm_norm).congr _ _).of_norm_norm;
  by intros; simp only [norm_smul]

end smul

/-! ### Sum -/

section sum

variables {Î¹ : Type*} {A : Î¹ â†’ Î± â†’ E'} {C : Î¹ â†’ â„} {s : finset Î¹}

theorem is_O_with.sum (h : âˆ€ i âˆˆ s, is_O_with (C i) (A i) g l) :
  is_O_with (âˆ‘ i in s, C i) (Î» x, âˆ‘ i in s, A i x) g l :=
begin
  induction s using finset.induction_on with i s is IH,
  { simp only [is_O_with_zero', finset.sum_empty, forall_true_iff] },
  { simp only [is, finset.sum_insert, not_false_iff],
    exact (h _ (finset.mem_insert_self i s)).add (IH (Î» j hj, h _ (finset.mem_insert_of_mem hj))) }
end

theorem is_O.sum (h : âˆ€ i âˆˆ s, is_O (A i) g l) :
  is_O (Î» x, âˆ‘ i in s, A i x) g l :=
begin
  induction s using finset.induction_on with i s is IH,
  { simp only [is_O_zero, finset.sum_empty, forall_true_iff] },
  { simp only [is, finset.sum_insert, not_false_iff],
    exact (h _ (finset.mem_insert_self i s)).add (IH (Î» j hj, h _ (finset.mem_insert_of_mem hj))) }
end

theorem is_o.sum (h : âˆ€ i âˆˆ s, is_o (A i) g' l) :
  is_o (Î» x, âˆ‘ i in s, A i x) g' l :=
begin
  induction s using finset.induction_on with i s is IH,
  { simp only [is_o_zero, finset.sum_empty, forall_true_iff] },
  { simp only [is, finset.sum_insert, not_false_iff],
    exact (h _ (finset.mem_insert_self i s)).add (IH (Î» j hj, h _ (finset.mem_insert_of_mem hj))) }
end

end sum

/-! ### Relation between `f = o(g)` and `f / g â†’ 0` -/

theorem is_o.tendsto_0 {f g : Î± â†’ ğ•œ} {l : filter Î±} (h : is_o f g l) :
  tendsto (Î» x, f x / (g x)) l (ğ“ 0) :=
have eqâ‚ : is_o (Î» x, f x / g x) (Î» x, g x / g x) l,
  by simpa only [div_eq_mul_inv] using h.mul_is_O (is_O_refl _ _),
have eqâ‚‚ : is_O (Î» x, g x / g x) (Î» x, (1 : ğ•œ)) l,
  from is_O_of_le _ (Î» x, by by_cases h : âˆ¥g xâˆ¥ = 0; simp [h, zero_le_one]),
(is_o_one_iff ğ•œ).mp (eqâ‚.trans_is_O eqâ‚‚)

theorem is_o_iff_tendsto' {f g : Î± â†’ ğ•œ} {l : filter Î±}
    (hgf : âˆ€á¶  x in l, g x = 0 â†’ f x = 0) :
  is_o f g l â†” tendsto (Î» x, f x / (g x)) l (ğ“ 0) :=
iff.intro is_o.tendsto_0 $ Î» h,
  (((is_o_one_iff _).mpr h).mul_is_O (is_O_refl g l)).congr'
    (hgf.mono $ Î» x, div_mul_cancel_of_imp) (eventually_of_forall $ Î» x, one_mul _)

theorem is_o_iff_tendsto {f g : Î± â†’ ğ•œ} {l : filter Î±}
    (hgf : âˆ€ x, g x = 0 â†’ f x = 0) :
  is_o f g l â†” tendsto (Î» x, f x / (g x)) l (ğ“ 0) :=
âŸ¨Î» h, h.tendsto_0, (is_o_iff_tendsto' (eventually_of_forall hgf)).2âŸ©

alias is_o_iff_tendsto' â†” _ asymptotics.is_o_of_tendsto'
alias is_o_iff_tendsto â†” _ asymptotics.is_o_of_tendsto

/-!
### Eventually (u / v) * v = u

If `u` and `v` are linked by an `is_O_with` relation, then we
eventually have `(u / v) * v = u`, even if `v` vanishes.
-/

section eventually_mul_div_cancel

variables {u v : Î± â†’ ğ•œ}

lemma is_O_with.eventually_mul_div_cancel (h : is_O_with c u v l) :
  (u / v) * v =á¶ [l] u :=
eventually.mono h.bound (Î» y hy, div_mul_cancel_of_imp $ Î» hv, by simpa [hv] using hy)

/-- If `u = O(v)` along `l`, then `(u / v) * v = u` eventually at `l`. -/
lemma is_O.eventually_mul_div_cancel (h : is_O u v l) :  (u / v) * v =á¶ [l] u :=
let âŸ¨c, hcâŸ© := h.is_O_with in hc.eventually_mul_div_cancel

/-- If `u = o(v)` along `l`, then `(u / v) * v = u` eventually at `l`. -/
lemma is_o.eventually_mul_div_cancel (h : is_o u v l) : (u / v) * v =á¶ [l] u :=
(h.forall_is_O_with zero_lt_one).eventually_mul_div_cancel

end eventually_mul_div_cancel

/-! ### Equivalent definitions of the form `âˆƒ Ï†, u =á¶ [l] Ï† * v` in a `normed_field`. -/

section exists_mul_eq

variables {u v : Î± â†’ ğ•œ}

/-- If `âˆ¥Ï†âˆ¥` is eventually bounded by `c`, and `u =á¶ [l] Ï† * v`, then we have `is_O_with c u v l`.
    This does not require any assumptions on `c`, which is why we keep this version along with
    `is_O_with_iff_exists_eq_mul`. -/
lemma is_O_with_of_eq_mul (Ï† : Î± â†’ ğ•œ) (hÏ† : âˆ€á¶  x in l, âˆ¥Ï† xâˆ¥ â‰¤ c) (h : u =á¶ [l] Ï† * v) :
  is_O_with c u v l :=
begin
  unfold is_O_with,
  refine h.symm.rw (Î» x a, âˆ¥aâˆ¥ â‰¤ c * âˆ¥v xâˆ¥) (hÏ†.mono $ Î» x hx, _),
  simp only [normed_field.norm_mul, pi.mul_apply],
  exact mul_le_mul_of_nonneg_right hx (norm_nonneg _)
end

lemma is_O_with_iff_exists_eq_mul (hc : 0 â‰¤ c) :
  is_O_with c u v l â†” âˆƒ (Ï† : Î± â†’ ğ•œ) (hÏ† : âˆ€á¶  x in l, âˆ¥Ï† xâˆ¥ â‰¤ c), u =á¶ [l] Ï† * v :=
begin
  split,
  { intro h,
    use (Î» x, u x / v x),
    refine âŸ¨eventually.mono h.bound (Î» y hy, _), h.eventually_mul_div_cancel.symmâŸ©,
    simpa using div_le_of_nonneg_of_le_mul (norm_nonneg _) hc hy },
  { rintros âŸ¨Ï†, hÏ†, hâŸ©,
    exact is_O_with_of_eq_mul Ï† hÏ† h }
end

lemma is_O_with.exists_eq_mul (h : is_O_with c u v l) (hc : 0 â‰¤ c) :
  âˆƒ (Ï† : Î± â†’ ğ•œ) (hÏ† : âˆ€á¶  x in l, âˆ¥Ï† xâˆ¥ â‰¤ c), u =á¶ [l] Ï† * v :=
(is_O_with_iff_exists_eq_mul hc).mp h

lemma is_O_iff_exists_eq_mul :
  is_O u v l â†” âˆƒ (Ï† : Î± â†’ ğ•œ) (hÏ† : l.is_bounded_under (â‰¤) (norm âˆ˜ Ï†)), u =á¶ [l] Ï† * v :=
begin
  split,
  { rintros h,
    rcases h.exists_nonneg with âŸ¨c, hnnc, hcâŸ©,
    rcases hc.exists_eq_mul hnnc with âŸ¨Ï†, hÏ†, huvÏ†âŸ©,
    exact âŸ¨Ï†, âŸ¨c, hÏ†âŸ©, huvÏ†âŸ© },
  { rintros âŸ¨Ï†, âŸ¨c, hÏ†âŸ©, huvÏ†âŸ©,
    exact is_O_iff_is_O_with.2 âŸ¨c, is_O_with_of_eq_mul Ï† hÏ† huvÏ†âŸ© }
end

alias is_O_iff_exists_eq_mul â†” asymptotics.is_O.exists_eq_mul _

lemma is_o_iff_exists_eq_mul :
  is_o u v l â†” âˆƒ (Ï† : Î± â†’ ğ•œ) (hÏ† : tendsto Ï† l (ğ“ 0)), u =á¶ [l] Ï† * v :=
begin
  split,
  { exact Î» h, âŸ¨Î» x, u x / v x, h.tendsto_0, h.eventually_mul_div_cancel.symmâŸ© },
  { unfold is_o, rintros âŸ¨Ï†, hÏ†, huvÏ†âŸ© c hpos,
    rw normed_group.tendsto_nhds_zero at hÏ†,
    exact is_O_with_of_eq_mul _ ((hÏ† c hpos).mono $ Î» x, le_of_lt)  huvÏ† }
end

alias is_o_iff_exists_eq_mul â†” asymptotics.is_o.exists_eq_mul _

end exists_mul_eq

/-! ### Miscellanous lemmas -/

theorem div_is_bounded_under_of_is_O {Î± : Type*} {l : filter Î±}
  {f g : Î± â†’ ğ•œ} (h : is_O f g l) :
  is_bounded_under (â‰¤) l (Î» x, âˆ¥f x / g xâˆ¥) :=
begin
  obtain âŸ¨c, hcâŸ© := is_O_iff.mp h,
  refine âŸ¨max c 0, eventually_map.2 (filter.mem_of_superset hc (Î» x hx, _))âŸ©,
  simp only [mem_set_of_eq, normed_field.norm_div] at âŠ¢ hx,
  by_cases hgx : g x = 0,
  { rw [hgx, norm_zero, div_zero, le_max_iff],
    exact or.inr le_rfl },
  { exact le_max_iff.2 (or.inl ((div_le_iff (norm_pos_iff.2 hgx)).2 hx)) }
end

theorem is_O_iff_div_is_bounded_under {Î± : Type*} {l : filter Î±}
  {f g : Î± â†’ ğ•œ} (hgf : âˆ€á¶  x in l, g x = 0 â†’ f x = 0) :
  is_O f g l â†” is_bounded_under (â‰¤) l (Î» x, âˆ¥f x / g xâˆ¥) :=
begin
  refine âŸ¨div_is_bounded_under_of_is_O, Î» h, _âŸ©,
  obtain âŸ¨c, hcâŸ© := h,
  rw filter.eventually_iff at hgf hc,
  simp only [mem_set_of_eq, mem_map, normed_field.norm_div] at hc,
  refine is_O_iff.2 âŸ¨c, filter.eventually_of_mem (inter_mem hgf hc) (Î» x hx, _)âŸ©,
  by_cases hgx : g x = 0,
  { simp [hx.1 hgx, hgx] },
  { refine (div_le_iff (norm_pos_iff.2 hgx)).mp hx.2 },
end

theorem is_O_of_div_tendsto_nhds {Î± : Type*} {l : filter Î±}
  {f g : Î± â†’ ğ•œ} (hgf : âˆ€á¶  x in l, g x = 0 â†’ f x = 0)
  (c : ğ•œ) (H : filter.tendsto (f / g) l (ğ“ c)) :
  is_O f g l :=
(is_O_iff_div_is_bounded_under hgf).2 $ is_bounded_under_of_tendsto H

lemma is_o.tendsto_zero_of_tendsto {Î± E ğ•œ : Type*} [normed_group E] [normed_field ğ•œ] {u : Î± â†’ E}
  {v : Î± â†’ ğ•œ} {l : filter Î±} {y : ğ•œ} (huv : is_o u v l) (hv : tendsto v l (ğ“ y)) :
  tendsto u l (ğ“ 0) :=
begin
  suffices h : is_o u (Î» x, (1 : ğ•œ)) l,
  { rwa is_o_one_iff at h },
  exact huv.trans_is_O (is_O_one_of_tendsto ğ•œ hv),
end

theorem is_o_pow_pow {m n : â„•} (h : m < n) :
  is_o (Î»(x : ğ•œ), x^n) (Î»x, x^m) (ğ“ 0) :=
begin
  let p := n - m,
  have nmp : n = m + p := (add_tsub_cancel_of_le (le_of_lt h)).symm,
  have : (Î»(x : ğ•œ), x^m) = (Î»x, x^m * 1), by simp only [mul_one],
  simp only [this, pow_add, nmp],
  refine is_O.mul_is_o (is_O_refl _ _) ((is_o_one_iff _).2 _),
  convert (continuous_pow p).tendsto (0 : ğ•œ),
  exact (zero_pow (tsub_pos_of_lt h)).symm
end

theorem is_o_norm_pow_norm_pow {m n : â„•} (h : m < n) :
  is_o (Î»(x : E'), âˆ¥xâˆ¥^n) (Î»x, âˆ¥xâˆ¥^m) (ğ“ (0 : E')) :=
(is_o_pow_pow h).comp_tendsto tendsto_norm_zero

theorem is_o_pow_id {n : â„•} (h : 1 < n) :
  is_o (Î»(x : ğ•œ), x^n) (Î»x, x) (ğ“ 0) :=
by { convert is_o_pow_pow h, simp only [pow_one] }

theorem is_o_norm_pow_id {n : â„•} (h : 1 < n) :
  is_o (Î»(x : E'), âˆ¥xâˆ¥^n) (Î»x, x) (ğ“ 0) :=
by simpa only [pow_one, is_o_norm_right] using @is_o_norm_pow_norm_pow E' _ _ _ h

theorem is_O_with.right_le_sub_of_lt_1 {fâ‚ fâ‚‚ : Î± â†’ E'} (h : is_O_with c fâ‚ fâ‚‚ l) (hc : c < 1) :
  is_O_with (1 / (1 - c)) fâ‚‚ (Î»x, fâ‚‚ x - fâ‚ x) l :=
is_O_with.of_bound $ mem_of_superset h.bound $ Î» x hx,
begin
  simp only [mem_set_of_eq] at hx âŠ¢,
  rw [mul_comm, one_div, â† div_eq_mul_inv, le_div_iff, mul_sub, mul_one, mul_comm],
  { exact le_trans (sub_le_sub_left hx _) (norm_sub_norm_le _ _) },
  { exact sub_pos.2 hc }
end

theorem is_O_with.right_le_add_of_lt_1 {fâ‚ fâ‚‚ : Î± â†’ E'} (h : is_O_with c fâ‚ fâ‚‚ l) (hc : c < 1) :
  is_O_with (1 / (1 - c)) fâ‚‚ (Î»x, fâ‚ x + fâ‚‚ x) l :=
(h.neg_right.right_le_sub_of_lt_1 hc).neg_right.of_neg_left.congr rfl (Î» x, rfl)
  (Î» x, by rw [neg_sub, sub_neg_eq_add])

theorem is_o.right_is_O_sub {fâ‚ fâ‚‚ : Î± â†’ E'} (h : is_o fâ‚ fâ‚‚ l) :
  is_O fâ‚‚ (Î»x, fâ‚‚ x - fâ‚ x) l :=
((h.def' one_half_pos).right_le_sub_of_lt_1 one_half_lt_one).is_O

theorem is_o.right_is_O_add {fâ‚ fâ‚‚ : Î± â†’ E'} (h : is_o fâ‚ fâ‚‚ l) :
  is_O fâ‚‚ (Î»x, fâ‚ x + fâ‚‚ x) l :=
((h.def' one_half_pos).right_le_add_of_lt_1 one_half_lt_one).is_O

/-- If `f x = O(g x)` along `cofinite`, then there exists a positive constant `C` such that
`âˆ¥f xâˆ¥ â‰¤ C * âˆ¥g xâˆ¥` whenever `g x â‰  0`. -/
theorem bound_of_is_O_cofinite (h : is_O f g' cofinite) :
  âˆƒ C > 0, âˆ€ â¦ƒxâ¦„, g' x â‰  0 â†’ âˆ¥f xâˆ¥ â‰¤ C * âˆ¥g' xâˆ¥ :=
begin
  rcases h.exists_pos with âŸ¨C, Câ‚€, hCâŸ©,
  rw [is_O_with, eventually_cofinite] at hC,
  rcases (hC.to_finset.image (Î» x, âˆ¥f xâˆ¥ / âˆ¥g' xâˆ¥)).exists_le with âŸ¨C', hC'âŸ©,
  have : âˆ€ x, C * âˆ¥g' xâˆ¥ < âˆ¥f xâˆ¥ â†’ âˆ¥f xâˆ¥ / âˆ¥g' xâˆ¥ â‰¤ C', by simpa using hC',
  refine âŸ¨max C C', lt_max_iff.2 (or.inl Câ‚€), Î» x hâ‚€, _âŸ©,
  rw [max_mul_of_nonneg _ _ (norm_nonneg _), le_max_iff, or_iff_not_imp_left, not_le],
  exact Î» hx, (div_le_iff (norm_pos_iff.2 hâ‚€)).1 (this _ hx)
end

theorem is_O_cofinite_iff (h : âˆ€ x, g' x = 0 â†’ f' x = 0) :
  is_O f' g' cofinite â†” âˆƒ C, âˆ€ x, âˆ¥f' xâˆ¥ â‰¤ C * âˆ¥g' xâˆ¥ :=
âŸ¨Î» h', let âŸ¨C, Câ‚€, hCâŸ© := bound_of_is_O_cofinite h' in
  âŸ¨C, Î» x, if hx : g' x = 0 then by simp [h _ hx, hx] else hC hxâŸ©,
  Î» h, (is_O_top.2 h).mono le_topâŸ©

theorem bound_of_is_O_nat_at_top {f : â„• â†’ E} {g' : â„• â†’ E'} (h : is_O f g' at_top) :
  âˆƒ C > 0, âˆ€ â¦ƒxâ¦„, g' x â‰  0 â†’ âˆ¥f xâˆ¥ â‰¤ C * âˆ¥g' xâˆ¥ :=
bound_of_is_O_cofinite $ by rwa nat.cofinite_eq_at_top

theorem is_O_nat_at_top_iff {f : â„• â†’ E'} {g : â„• â†’ F'} (h : âˆ€ x, g x = 0 â†’ f x = 0) :
  is_O f g at_top â†” âˆƒ C, âˆ€ x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥g xâˆ¥ :=
by rw [â† nat.cofinite_eq_at_top, is_O_cofinite_iff h]

theorem is_O_one_nat_at_top_iff {f : â„• â†’ E'} :
  is_O f (Î» n, 1 : â„• â†’ â„) at_top â†” âˆƒ C, âˆ€ n, âˆ¥f nâˆ¥ â‰¤ C :=
iff.trans (is_O_nat_at_top_iff (Î» n h, (one_ne_zero h).elim)) $
  by simp only [norm_one, mul_one]

theorem is_O_with_pi {Î¹ : Type*} [fintype Î¹] {E' : Î¹ â†’ Type*} [Î  i, normed_group (E' i)]
  {f : Î± â†’ Î  i, E' i} {C : â„} (hC : 0 â‰¤ C) :
  is_O_with C f g' l â†” âˆ€ i, is_O_with C (Î» x, f x i) g' l :=
have âˆ€ x, 0 â‰¤ C * âˆ¥g' xâˆ¥, from Î» x, mul_nonneg hC (norm_nonneg _),
by simp only [is_O_with_iff, pi_norm_le_iff (this _), eventually_all]

@[simp] theorem is_O_pi {Î¹ : Type*} [fintype Î¹] {E' : Î¹ â†’ Type*} [Î  i, normed_group (E' i)]
  {f : Î± â†’ Î  i, E' i} :
  is_O f g' l â†” âˆ€ i, is_O (Î» x, f x i) g' l :=
begin
  simp only [is_O_iff_eventually_is_O_with, â† eventually_all],
  exact eventually_congr (eventually_at_top.2 âŸ¨0, Î» c, is_O_with_piâŸ©)
end

@[simp] theorem is_o_pi {Î¹ : Type*} [fintype Î¹] {E' : Î¹ â†’ Type*} [Î  i, normed_group (E' i)]
  {f : Î± â†’ Î  i, E' i} :
  is_o f g' l â†” âˆ€ i, is_o (Î» x, f x i) g' l :=
begin
  simp only [is_o, is_O_with_pi, le_of_lt] { contextual := tt },
  exact âŸ¨Î» h i c hc, h hc i, Î» h c hc i, h i hcâŸ©
end

end asymptotics

open asymptotics

lemma summable_of_is_O {Î¹ E} [normed_group E] [complete_space E] {f : Î¹ â†’ E} {g : Î¹ â†’ â„}
  (hg : summable g) (h : is_O f g cofinite) : summable f :=
let âŸ¨C, hCâŸ© := h.is_O_with in
summable_of_norm_bounded_eventually (Î» x, C * âˆ¥g xâˆ¥) (hg.abs.mul_left _) hC.bound

lemma summable_of_is_O_nat {E} [normed_group E] [complete_space E] {f : â„• â†’ E} {g : â„• â†’ â„}
  (hg : summable g) (h : is_O f g at_top) : summable f :=
summable_of_is_O hg $ nat.cofinite_eq_at_top.symm â–¸ h

namespace local_homeomorph

variables {Î± : Type*} {Î² : Type*} [topological_space Î±] [topological_space Î²]

variables {E : Type*} [has_norm E] {F : Type*} [has_norm F]

/-- Transfer `is_O_with` over a `local_homeomorph`. -/
lemma is_O_with_congr (e : local_homeomorph Î± Î²) {b : Î²} (hb : b âˆˆ e.target)
  {f : Î² â†’ E} {g : Î² â†’ F} {C : â„} :
  is_O_with C f g (ğ“ b) â†” is_O_with C (f âˆ˜ e) (g âˆ˜ e) (ğ“ (e.symm b)) :=
âŸ¨Î» h, h.comp_tendsto $
  by { convert e.continuous_at (e.map_target hb), exact (e.right_inv hb).symm },
  Î» h, (h.comp_tendsto (e.continuous_at_symm hb)).congr' rfl
    ((e.eventually_right_inverse hb).mono $ Î» x hx, congr_arg f hx)
    ((e.eventually_right_inverse hb).mono $ Î» x hx, congr_arg g hx)âŸ©

/-- Transfer `is_O` over a `local_homeomorph`. -/
lemma is_O_congr (e : local_homeomorph Î± Î²) {b : Î²} (hb : b âˆˆ e.target) {f : Î² â†’ E} {g : Î² â†’ F} :
  is_O f g (ğ“ b) â†” is_O (f âˆ˜ e) (g âˆ˜ e) (ğ“ (e.symm b)) :=
by { unfold is_O, exact exists_congr (Î» C, e.is_O_with_congr hb) }

/-- Transfer `is_o` over a `local_homeomorph`. -/
lemma is_o_congr (e : local_homeomorph Î± Î²) {b : Î²} (hb : b âˆˆ e.target) {f : Î² â†’ E} {g : Î² â†’ F} :
  is_o f g (ğ“ b) â†” is_o (f âˆ˜ e) (g âˆ˜ e) (ğ“ (e.symm b)) :=
by { unfold is_o, exact (forall_congr $ Î» c, forall_congr $ Î» hc, e.is_O_with_congr hb) }

end local_homeomorph

namespace homeomorph

variables {Î± : Type*} {Î² : Type*} [topological_space Î±] [topological_space Î²]

variables {E : Type*} [has_norm E] {F : Type*} [has_norm F]

open asymptotics

/-- Transfer `is_O_with` over a `homeomorph`. -/
lemma is_O_with_congr (e : Î± â‰ƒâ‚œ Î²) {b : Î²} {f : Î² â†’ E} {g : Î² â†’ F} {C : â„} :
  is_O_with C f g (ğ“ b) â†” is_O_with C (f âˆ˜ e) (g âˆ˜ e) (ğ“ (e.symm b)) :=
e.to_local_homeomorph.is_O_with_congr trivial

/-- Transfer `is_O` over a `homeomorph`. -/
lemma is_O_congr (e : Î± â‰ƒâ‚œ Î²) {b : Î²} {f : Î² â†’ E} {g : Î² â†’ F} :
  is_O f g (ğ“ b) â†” is_O (f âˆ˜ e) (g âˆ˜ e) (ğ“ (e.symm b)) :=
by { unfold is_O, exact exists_congr (Î» C, e.is_O_with_congr) }

/-- Transfer `is_o` over a `homeomorph`. -/
lemma is_o_congr (e : Î± â‰ƒâ‚œ Î²) {b : Î²} {f : Î² â†’ E} {g : Î² â†’ F} :
  is_o f g (ğ“ b) â†” is_o (f âˆ˜ e) (g âˆ˜ e) (ğ“ (e.symm b)) :=
by { unfold is_o, exact forall_congr (Î» c, forall_congr (Î» hc, e.is_O_with_congr)) }

end homeomorph
