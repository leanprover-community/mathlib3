/-
Copyright (c) 2021 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import analysis.asymptotics.asymptotics
import analysis.asymptotics.specific_asymptotics
import data.polynomial.eval

/-!
# Negligible Functions

This file defines a predicate `negligible f` on functions `f` from `‚Ñï` to a normed field `ùïú`.
A negligible function `f` is a function satisfying one of following equivalent definitions:

* `f` is `O(x ^ c)` for all (or equivalently sufficiently small) integers `c`
* `f` is `O(p(x)‚Åª¬π)` for all (or equivalently sufficiently large) polynomials `p`
* `p(x) * f` is bounded for all polynomials `p`
* `p(x) * f` tends to `ùìù 0` for all polynomials `p`

The definition used here is given in terms of the first condition.

The main theorem is `negligible_polynomial_mul` that says the product of a polynomial
  and a negligible function is still a negligible function.
-/

namespace asymptotics

open_locale topological_space
open filter

/-- Definition of negligible functions over an arbitrary `normed_field`.
  Note that the second function always has type `‚Ñï ‚Üí ‚Ñù`, which generally gives better lemmas. -/
def negligible {ùïú : Type*} [normed_ring ùïú] (f : ‚Ñï ‚Üí ùïú) :=
‚àÄ (c : ‚Ñ§), is_O f (Œª n, (n : ‚Ñù) ^ c) at_top

section normed_ring

variables {R : Type*} [normed_ring R]
variables {f g : ‚Ñï ‚Üí R}

lemma is_O.trans_negligible (h : is_O f g at_top)
  (hg : negligible g) : negligible f :=
Œª c, h.trans $ hg c

alias is_O.trans_negligible ‚Üê negligible.is_O_mono

lemma negligible.ext (hf : negligible f)
  (h : ‚àÄ x, f x = g x) : negligible g :=
(funext h : f = g) ‚ñ∏ hf

lemma negligible.mono (hf : negligible f)
  (h : ‚àÄ n, ‚à•g n‚à• ‚â§ ‚à•f n‚à•) : negligible g :=
(is_O_of_le at_top h).trans_negligible hf

lemma negligible.eventually_mono (hf : negligible f)
  (h : ‚àÄ·∂† n in at_top, ‚à•g n‚à• ‚â§ ‚à•f n‚à•) : negligible g :=
(is_O_iff.2 ‚ü®1, by simpa only [one_mul] using h‚ü©).trans_negligible hf

/-- It suffices to check the negligiblity condition for only sufficiently small exponents `c`.
  See `negligible_of_is_O_fpow_le` for a version with explicit bounds -/
lemma negligible_of_eventually_is_O (h : ‚àÄ·∂† (c : ‚Ñ§) in at_bot, is_O f (Œª n, (n : ‚Ñù) ^ c) at_top) :
  negligible f :=
begin
  obtain ‚ü®C, hC‚ü© := eventually_at_bot.mp h,
  intro c,
  by_cases hc : c ‚â§ C,
  { exact hC c hc },
  { refine (hC C le_rfl).trans (is_O.of_bound 1 (eventually_at_top.2 ‚ü®1, (Œª b hb, _)‚ü©)),
    simp_rw [one_mul, normed_field.norm_fpow, real.norm_coe_nat],
    have hb : 1 ‚â§ (b : ‚Ñù) := le_trans (le_of_eq nat.cast_one.symm) (nat.cast_le.2 hb),
    exact fpow_le_of_le hb (le_of_not_le hc) }
end

lemma negligible_of_is_O_fpow_le (C : ‚Ñ§)
  (h : ‚àÄ c ‚â§ C, is_O f (Œª n, (n : ‚Ñù) ^ c) at_top) :
  negligible f :=
negligible_of_eventually_is_O (eventually_at_bot.2 ‚ü®C, h‚ü©)

lemma negligible_of_is_O_fpow_lt (C : ‚Ñ§)
  (h : ‚àÄ c < C, is_O f (Œª n, (n : ‚Ñù) ^ c) at_top) :
  negligible f :=
negligible_of_is_O_fpow_le C.pred
  (Œª c hc, h c (lt_of_le_of_lt hc (int.pred_self_lt C)))

/-- A negligible function must tend to zero in the base ring (not just in norm) -/
lemma negligible.tendsto_zero (hf : negligible f) :
  tendsto f at_top (ùìù 0) :=
begin
  refine is_O.trans_tendsto (hf (-1)) _,
  have : (has_inv.inv : ‚Ñù ‚Üí ‚Ñù) ‚àò (coe : ‚Ñï ‚Üí ‚Ñù) = (Œª (n : ‚Ñï), (n : ‚Ñù) ^ (-1 : ‚Ñ§)),
  by simp only [gpow_one, fpow_neg],
  exact this ‚ñ∏ (tendsto_inv_at_top_zero).comp (coe_nat_tendsto_at_top ‚Ñù),
end

/-- A negligible function eventually has norm less than any positive bound -/
lemma negligible.eventually_le (hf : negligible f) (Œµ : ‚Ñù) (hŒµ : 0 < Œµ) :
  ‚àÄ·∂† (n : ‚Ñï) in at_top, ‚à•f n‚à• ‚â§ Œµ :=
by simpa only [dist_zero_right] using
  hf.tendsto_zero.eventually (metric.closed_ball_mem_nhds (0 : R) hŒµ)

@[simp]
lemma negligible_zero : negligible (0 : ‚Ñï ‚Üí R) :=
Œª c, is_O_zero _ _

lemma negligible.add (hf : negligible f) (hg : negligible g) :
  negligible (f + g) :=
Œª c, is_O.add (hf c) (hg c)

@[simp]
lemma negligible_const_iff [t1_space R] (x : R) :
  negligible (function.const ‚Ñï x) ‚Üî x = 0 :=
begin
  refine ‚ü®Œª h, not_not.1 (Œª hx, _), Œª h, by simp [h]‚ü©,
  have : (function.const ‚Ñï x ‚Åª¬π' {x}·∂ú) ‚àà at_top :=
    (tendsto_nhds.1 $ h.tendsto_zero) {x}·∂ú (is_open_ne) (ne.symm hx),
  rw [set.preimage_const_of_not_mem (by simp : x ‚àâ ({x} : set R)·∂ú)] at this,
  exact at_top.empty_not_mem this,
end

lemma negligible.const_mul (hf : negligible f) (c : R) :
  negligible (Œª n, c * f n) :=
(is_O_const_mul_self c f at_top).trans_negligible hf

lemma negligible_const_mul_iff_of_is_unit {c : R} (hc : is_unit c) :
  negligible (Œª n, c * f n) ‚Üî (negligible f) :=
begin
  refine ‚ü®Œª h, _, Œª h, _‚ü©,
  { exact (is_O_self_const_mul' hc f at_top).trans_negligible h },
  { exact h.const_mul c },
end

end normed_ring

section normed_field

variables {ùïú : Type*} [normed_field ùïú]
variables {f g : ‚Ñï ‚Üí ùïú}

@[simp]
lemma negligible_const_mul_iff (f : ‚Ñï ‚Üí ùïú) (c : ùïú) :
  negligible (Œª n, c * f n) ‚Üî (c = 0) ‚à® (negligible f) :=
begin
  by_cases hc0 : c = 0,
  { simp [hc0] },
  { exact (negligible_const_mul_iff_of_is_unit (is_unit.mk0 c hc0)).trans
      ‚ü®or.inr, or.rec (Œª hc0', absurd hc0' hc0) id‚ü© }
end

-- TODO: The lemmas below can be generalized to `iff` statements if `‚à•(n : ùïú)‚à•` doesn't tend to 0

lemma negligible.coe_nat_mul (hf : negligible f) :
  negligible (Œª n, (n : ùïú) * f n) :=
begin
  refine negligible_of_is_O_fpow_lt 0 (Œª c hc, _),
  refine is_O.trans (is_O.mul (coe_nat_is_O_coe_nat_real ùïú) (hf (c - 1)))
    (is_O_of_le _ (Œª x, le_of_eq (congr_arg _ _))),
  by_cases hx : (x : ‚Ñù) = 0,
  { simp_rw [hx, zero_mul],
    refine symm (zero_fpow c (ne_of_lt hc)) },
  { calc (x : ‚Ñù) * ‚Üëx ^ (c - 1) = (‚Üëx ^ (1 : ‚Ñ§)) * (‚Üëx ^ (c - 1)) : by rw gpow_one
      ... = ‚Üëx ^ (1 + (c - 1)) : (fpow_add hx 1 (c - 1)).symm
      ... = ‚Üëx ^ c : congr_arg (Œª g, gpow g (x : ‚Ñù)) (by linarith) }
end

lemma negligible.coe_nat_pow_mul (hf : negligible f) (p : ‚Ñï) :
  negligible (Œª n, (n : ùïú) ^ p * f n) :=
begin
  induction p with p hp,
  { simp_rw [pow_zero, one_mul],
    exact hf },
  { simp_rw [pow_succ, mul_assoc],
    exact hp.coe_nat_mul }
end

lemma negligible.nsmul (hf : negligible f) :
  negligible (Œª n, n ‚Ä¢ f n) :=
by simpa [nsmul_eq_mul] using hf.coe_nat_mul

lemma negligible.pow_nsmul (hf : negligible f) (p : ‚Ñï) :
  negligible (Œª n, (n ^ p) ‚Ä¢ f n) :=
by simpa [nsmul_eq_mul] using hf.coe_nat_pow_mul p

theorem negligible.polynomial_mul {ùïú : Type*} [normed_field ùïú]
  {f : ‚Ñï ‚Üí ùïú} (hf : negligible f) (p : polynomial ùïú) :
  negligible (Œª n, (p.eval n) * f n) :=
begin
  refine polynomial.induction_on' p (Œª p q hp hq, _) (Œª m x, _),
  { simp_rw [polynomial.eval_add, add_mul],
    exact hp.add hq },
  { simp_rw [polynomial.eval_monomial, mul_assoc],
    exact (hf.coe_nat_pow_mul m).const_mul x }
end

lemma negligible.mul_is_O_polynomial (hf : negligible f) (p : polynomial ùïú)
  (hg : is_O g (Œª n, p.eval n) filter.at_top) : negligible (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_negligible
  ((hf.polynomial_mul p).ext $ Œª x, mul_comm _ _)

lemma negligible.mul_is_O (hf : negligible f) (c : ‚Ñï)
  (hg : is_O g (Œª n, (n : ùïú) ^ c) at_top) : negligible (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_negligible
  ((hf.coe_nat_pow_mul c).ext $ Œª x, mul_comm _ _)

lemma negligible.mul (hf : negligible f) (hg : negligible g) :
  negligible (f * g) :=
begin
  refine hf.mul_is_O 0 (is_O_of_div_tendsto_nhds (by simp) 0 _),
  convert hg.tendsto_zero,
  exact funext (Œª x, by simp),
end

end normed_field

end asymptotics
