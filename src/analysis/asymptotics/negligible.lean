/-
Copyright (c) 2021 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import analysis.asymptotics.asymptotics
import analysis.asymptotics.specific_asymptotics
import data.polynomial.eval

/-!
# Negligible Functions

This file defines a predicate `negligible f` for a function satisfying
  one of following equivalent definitions (The definition is in terms of the first condition):

* `f` is `O(x ^ c)` for all (or equivalently sufficiently small) integers `c`
* `f` is `O(p(x)â»Â¹)` for all (or equivalently sufficiently large) polynomials `p`
* `p(x) * f` is bounded for all polynomials `p`
* `p(x) * f` tends to `ğ“ 0` for all polynomials `p`

The main theorem is `negligible_polynomial_mul` that says the product of a polynomial
  and a negligible function is still a negligible function.
-/

namespace asymptotics

open_locale topological_space
open filter

/-- Definition of negligible functions over an arbitrary `normed_field`.
  Note that the second function always has type `â„• â†’ â„`, which generally gives better lemmas. -/
def negligible {ğ•œ : Type*} [has_norm ğ•œ] (f : â„• â†’ ğ•œ) :=
âˆ€ (c : â„¤), is_O f (Î» n, (n : â„) ^ c) at_top

lemma negligible.ext {ğ•œ : Type*} [has_norm ğ•œ] {f g : â„• â†’ ğ•œ}
  (hf : negligible f) (h : âˆ€ x, f x = g x) : negligible g :=
(funext h : f = g) â–¸ hf

section normed_group

variables {R : Type*} [normed_group R]
variables {f g : â„• â†’ R}

lemma is_O.trans_negligible (h : is_O f g at_top)
  (hg : negligible g) : negligible f :=
Î» c, h.trans $ hg c

alias is_O.trans_negligible â† negligible.is_O_mono

lemma negligible.mono (hf : negligible f)
  (h : âˆ€ n, âˆ¥g nâˆ¥ â‰¤ âˆ¥f nâˆ¥) : negligible g :=
(is_O_of_le at_top h).trans_negligible hf

lemma negligible.eventually_mono (hf : negligible f)
  (h : âˆ€á¶  n in at_top, âˆ¥g nâˆ¥ â‰¤ âˆ¥f nâˆ¥) : negligible g :=
(is_O_iff.2 âŸ¨1, by simpa only [one_mul] using hâŸ©).trans_negligible hf

/-- It suffices to check the negligiblity condition for only sufficiently small exponents `c`.
  See `negligible_of_is_O_fpow_le` for a version with explicit bounds -/
lemma negligible_of_eventually_is_O (h : âˆ€á¶  (c : â„¤) in at_bot, is_O f (Î» n, (n : â„) ^ c) at_top) :
  negligible f :=
begin
  obtain âŸ¨C, hCâŸ© := eventually_at_bot.mp h,
  intro c,
  by_cases hc : c â‰¤ C,
  { exact hC c hc },
  { refine (hC C le_rfl).trans (is_O.of_bound 1 (eventually_at_top.2 âŸ¨1, (Î» b hb, _)âŸ©)),
    simp_rw [one_mul, normed_field.norm_fpow, real.norm_coe_nat],
    have hb : 1 â‰¤ (b : â„) := le_trans (le_of_eq nat.cast_one.symm) (nat.cast_le.2 hb),
    exact fpow_le_of_le hb (le_of_not_le hc) }
end

lemma negligible_of_is_O_fpow_le (C : â„¤)
  (h : âˆ€ c â‰¤ C, is_O f (Î» n, (n : â„) ^ c) at_top) :
  negligible f :=
negligible_of_eventually_is_O (eventually_at_bot.2 âŸ¨C, hâŸ©)

lemma negligible_of_is_O_fpow_lt (C : â„¤)
  (h : âˆ€ c < C, is_O f (Î» n, (n : â„) ^ c) at_top) :
  negligible f :=
negligible_of_is_O_fpow_le C.pred
  (Î» c hc, h c (lt_of_le_of_lt hc (int.pred_self_lt C)))

/-- A negligible function must tend to zero in the base ring (not just in norm) -/
lemma negligible.tendsto_zero (hf : negligible f) :
  tendsto f at_top (ğ“ 0) :=
begin
  refine is_O.trans_tendsto (hf (-1)) _,
  have : (has_inv.inv : â„ â†’ â„) âˆ˜ (coe : â„• â†’ â„) = (Î» (n : â„•), (n : â„) ^ (-1 : â„¤)),
  by simp only [gpow_one, fpow_neg],
  exact this â–¸ (tendsto_inv_at_top_zero).comp (coe_nat_tendsto_at_top â„),
end

/-- A negligible function eventually has norm less than any positive bound -/
lemma negligible.eventually_le (hf : negligible f) (Îµ : â„) (hÎµ : 0 < Îµ) :
  âˆ€á¶  (n : â„•) in at_top, âˆ¥f nâˆ¥ â‰¤ Îµ :=
by simpa only [dist_zero_right] using
  hf.tendsto_zero.eventually (metric.closed_ball_mem_nhds (0 : R) hÎµ)

@[simp]
lemma negligible_zero : negligible (0 : â„• â†’ R) :=
Î» c, is_O_zero _ _

lemma negligible.add (hf : negligible f) (hg : negligible g) :
  negligible (f + g) :=
Î» c, is_O.add (hf c) (hg c)

@[simp]
lemma negligible_const_iff [t1_space R] (x : R) :
  negligible (function.const â„• x) â†” x = 0 :=
begin
  refine âŸ¨Î» h, not_not.1 (Î» hx, _), Î» h, by simp [h]âŸ©,
  have : (function.const â„• x â»Â¹' {x}á¶œ) âˆˆ at_top :=
    (tendsto_nhds.1 $ h.tendsto_zero) {x}á¶œ (is_open_ne) (ne.symm hx),
  rw [set.preimage_const_of_not_mem (by simp : x âˆ‰ ({x} : set R)á¶œ)] at this,
  exact at_top.empty_not_mem this,
end

end normed_group

section normed_ring

variables {R : Type*} [normed_ring R]
variables {f g : â„• â†’ R}

lemma negligible.const_mul (hf : negligible f) (c : R) :
  negligible (Î» n, c * f n) :=
(is_O_const_mul_self c f at_top).trans_negligible hf

lemma negligible_const_mul_iff_of_is_unit {c : R} (hc : is_unit c) :
  negligible (Î» n, c * f n) â†” (negligible f) :=
begin
  refine âŸ¨Î» h, _, Î» h, _âŸ©,
  { exact (is_O_self_const_mul' hc f at_top).trans_negligible h },
  { exact h.const_mul c },
end

end normed_ring

section normed_field

variables {ğ•œ : Type*} [normed_field ğ•œ]
variables {f g : â„• â†’ ğ•œ}

@[simp]
lemma negligible_const_mul_iff (f : â„• â†’ ğ•œ) (c : ğ•œ) :
  negligible (Î» n, c * f n) â†” (c = 0) âˆ¨ (negligible f) :=
begin
  by_cases hc0 : c = 0,
  { simp [hc0] },
  { exact (negligible_const_mul_iff_of_is_unit (is_unit.mk0 c hc0)).trans
      âŸ¨or.inr, or.rec (Î» hc0', absurd hc0' hc0) idâŸ© }
end

-- TODO: The lemmas below can be generalized to `iff` statements if `âˆ¥(n : ğ•œ)âˆ¥` doesn't tend to 0

lemma negligible.coe_nat_mul (hf : negligible f) :
  negligible (Î» n, (n : ğ•œ) * f n) :=
begin
  refine negligible_of_is_O_fpow_lt 0 (Î» c hc, _),
  refine is_O.trans (is_O.mul (coe_nat_is_O_coe_nat_real ğ•œ) (hf (c - 1)))
    (is_O_of_le _ (Î» x, le_of_eq (congr_arg _ _))),
  by_cases hx : (x : â„) = 0,
  { simp_rw [hx, zero_mul],
    refine symm (zero_fpow c (ne_of_lt hc)) },
  { calc (x : â„) * â†‘x ^ (c - 1) = (â†‘x ^ (1 : â„¤)) * (â†‘x ^ (c - 1)) : by rw gpow_one
      ... = â†‘x ^ (1 + (c - 1)) : (fpow_add hx 1 (c - 1)).symm
      ... = â†‘x ^ c : congr_arg (Î» g, gpow g (x : â„)) (by linarith) }
end

lemma negligible.coe_nat_pow_mul (hf : negligible f) (p : â„•) :
  negligible (Î» n, (n : ğ•œ) ^ p * f n) :=
begin
  induction p with p hp,
  { simp_rw [pow_zero, one_mul],
    exact hf },
  { simp_rw [pow_succ, mul_assoc],
    exact hp.coe_nat_mul }
end

lemma negligible.nsmul (hf : negligible f) :
  negligible (Î» n, n â€¢ f n) :=
by simpa [nsmul_eq_mul] using hf.coe_nat_mul

lemma negligible.pow_nsmul (hf : negligible f) (p : â„•) :
  negligible (Î» n, (n ^ p) â€¢ f n) :=
by simpa [nsmul_eq_mul] using hf.coe_nat_pow_mul p

theorem negligible.polynomial_mul {ğ•œ : Type*} [normed_field ğ•œ]
  {f : â„• â†’ ğ•œ} (hf : negligible f) (p : polynomial ğ•œ) :
  negligible (Î» n, (p.eval n) * f n) :=
begin
  refine polynomial.induction_on' p (Î» p q hp hq, _) (Î» m x, _),
  { simp_rw [polynomial.eval_add, add_mul],
    exact hp.add hq },
  { simp_rw [polynomial.eval_monomial, mul_assoc],
    exact (hf.coe_nat_pow_mul m).const_mul x }
end

lemma negligible.mul_is_O_polynomial (hf : negligible f) (p : polynomial ğ•œ)
  (hg : is_O g (Î» n, p.eval n) filter.at_top) : negligible (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_negligible
  ((hf.polynomial_mul p).ext $ Î» x, mul_comm _ _)

lemma negligible.mul_is_O (hf : negligible f) (c : â„•)
  (hg : is_O g (Î» n, (n : ğ•œ) ^ c) at_top) : negligible (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_negligible
  ((hf.coe_nat_pow_mul c).ext $ Î» x, mul_comm _ _)

lemma negligible.mul (hf : negligible f) (hg : negligible g) :
  negligible (f * g) :=
begin
  refine hf.mul_is_O 0 (is_O_of_div_tendsto_nhds (by simp) 0 _),
  convert hg.tendsto_zero,
  exact funext (Î» x, by simp),
end

end normed_field

end asymptotics
