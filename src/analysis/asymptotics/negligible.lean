/-
Copyright (c) 2021 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import analysis.asymptotics.asymptotics
import analysis.asymptotics.specific_asymptotics
import data.polynomial.eval

/-!
# Negligible Functions

This file defines a predicate `negligible f` for a function satisfying
  one of following equivalent definitions (The definition is in terms of the first condition):

* `f` is `O(x ^ c)` for all (or equivalently sufficiently small) integers `c`
* `f` is `O(p(x)â»Â¹)` for all (or equivalently sufficiently large) polynomials `p`
* `p(x) * f` is bounded for all polynomials `p`
* `p(x) * f` tends to `ğ“ 0` for all polynomials `p`

-/

namespace asymptotics

open_locale topological_space
open filter

/-- A function `f` from an `ordered_comm_semiring` to a `normed_field` is negligible
  iff `f(x)` is `O(x ^ c)` for all integers `c`. -/
def negligible {Î± ğ•œ : Type*} [ordered_comm_semiring Î±] [normed_field ğ•œ] [algebra Î± ğ•œ]
  (f : Î± â†’ ğ•œ) :=
âˆ€ (c : â„¤), is_O f (Î» x, (algebra_map Î± ğ•œ x) ^ c) filter.at_top

section normed_field

variables {Î± ğ•œ : Type*} [ordered_comm_semiring Î±] [normed_field ğ•œ] [algebra Î± ğ•œ]
variables {f g : Î± â†’ ğ•œ}

lemma is_O.trans_negligible (h : is_O f g at_top)
  (hg : negligible g) : negligible f :=
Î» c, h.trans $ hg c

alias is_O.trans_negligible â† negligible.is_O_mono

lemma negligible.mono (hf : negligible f)
  (h : âˆ€ n, âˆ¥g nâˆ¥ â‰¤ âˆ¥f nâˆ¥) : negligible g :=
(is_O_of_le at_top h).trans_negligible hf

lemma negligible.eventually_mono (hf : negligible f)
  (h : âˆ€á¶  n in at_top, âˆ¥g nâˆ¥ â‰¤ âˆ¥f nâˆ¥) : negligible g :=
(is_O_iff.2 âŸ¨1, by simpa only [one_mul] using hâŸ©).trans_negligible hf

@[simp]
lemma negligible_zero : negligible (0 : Î± â†’ ğ•œ) :=
Î» c, is_O_zero _ _

@[simp]
lemma negligible_zero' : negligible (Î» (x : Î±), (0 : ğ•œ)) :=
negligible_zero

lemma negligible.add (hf : negligible f) (hg : negligible g) :
  negligible (f + g) :=
Î» c, is_O.add (hf c) (hg c)

lemma negligible.const_mul (hf : negligible f) (c : ğ•œ) :
  negligible (Î» n, c * f n) :=
(is_O_const_mul_self c f at_top).trans_negligible hf

lemma negligible.mul_const (hf : negligible f) (c : ğ•œ) :
  negligible (Î» n, f n * c) :=
by simpa [mul_comm _ c] using negligible.const_mul hf c

lemma negligible_const_mul_iff_of_ne_zero {c : ğ•œ} (hc : c â‰  0) :
  negligible (Î» n, c * f n) â†” negligible f :=
âŸ¨Î» h, (is_O_self_const_mul c hc f at_top).trans_negligible h, Î» h, h.const_mul c âŸ©

lemma negligible_mul_const_iff_of_ne_zero {c : ğ•œ} (hc : c â‰  0) :
  negligible (Î» n, f n * c) â†” negligible f :=
by simpa [mul_comm _ c] using negligible_const_mul_iff_of_ne_zero hc

@[simp]
lemma negligible_const_mul_iff (c : ğ•œ) :
  negligible (Î» n, c * f n) â†” c = 0 âˆ¨ negligible f :=
begin
  by_cases hc0 : c = 0,
  { simp [hc0] },
  { exact (negligible_const_mul_iff_of_ne_zero hc0).trans
      âŸ¨or.inr, or.rec (Î» hc0', absurd hc0' hc0) idâŸ© }
end

@[simp]
lemma negligible_mul_const_iff (c : ğ•œ) :
  negligible (Î» n, f n * c) â†” c = 0 âˆ¨ negligible f :=
by simp [mul_comm _ c]

section no_zero_smul_divisors

variables [nontrivial Î±] [no_zero_smul_divisors Î± ğ•œ]

lemma negligible.coe_nat_mul (hf : negligible f) :
  negligible (Î» n, (algebra_map Î± ğ•œ n) * f n) :=
begin
  refine Î» c, (is_O.mul (is_O_refl (algebra_map Î± ğ•œ) at_top) (hf (c - 1))).trans _,
  refine is_O_of_div_tendsto_nhds (eventually_of_forall
    (Î» x hx, mul_eq_zero_of_left (fpow_eq_zero hx) _)) 1 (tendsto_nhds.2 _),
  refine Î» s hs hs', at_top.sets_of_superset (mem_at_top 1) (Î» x hx, set.mem_preimage.2 _),
  have hx' : algebra_map Î± ğ•œ x â‰  0 := Î» hx', (ne_of_lt $ lt_of_lt_of_le zero_lt_one hx).symm
    (by simpa [algebra.algebra_map_eq_smul_one, smul_eq_zero] using hx'),
  convert hs',
  rw [pi.div_apply, div_eq_one_iff_eq (fpow_ne_zero c hx'), fpow_sub_one hx' c,
    mul_comm (algebra_map Î± ğ•œ x), mul_assoc, inv_mul_cancel hx', mul_one],
end

lemma negligible.coe_nat_pow_mul (hf : negligible f) (p : â„•) :
  negligible (Î» n, (algebra_map Î± ğ•œ n) ^ p * f n) :=
begin
  induction p with p hp,
  { simp_rw [pow_zero, one_mul],
    exact hf },
  { simp_rw [pow_succ, mul_assoc],
    exact hp.coe_nat_mul }
end

theorem negligible.polynomial_mul (hf : negligible f) (p : polynomial ğ•œ) :
  negligible (Î» n, (p.eval (algebra_map Î± ğ•œ n)) * f n) :=
begin
  refine polynomial.induction_on' p (Î» p q hp hq, _) (Î» m x, _),
  { simp_rw [polynomial.eval_add, add_mul],
    exact hp.add hq },
  { simp_rw [polynomial.eval_monomial, mul_assoc],
    exact (hf.coe_nat_pow_mul m).const_mul x }
end

/-- If `f` is negligible, and `g` is `O(p)` for some polynomial `p`, then `f * g` is negligible -/
lemma negligible.mul_is_O_polynomial (hf : negligible f) (p : polynomial ğ•œ)
  (hg : is_O g (Î» n, p.eval (algebra_map Î± ğ•œ n)) filter.at_top) : negligible (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_negligible
  ((hf.polynomial_mul p).mono $ Î» x, le_of_eq (congr_arg _ $ mul_comm _ _))

/-- If `f` is negligible, and `g` is `O(n ^ c)` for some integer `c`, then `f * g` is negligible-/
lemma negligible.mul_is_O (hf : negligible f) (c : â„•)
  (hg : is_O g (Î» n, (algebra_map Î± ğ•œ n) ^ c) at_top) : negligible (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_negligible
  ((hf.coe_nat_pow_mul c).mono $ Î» x, le_of_eq (congr_arg _ $ mul_comm _ _))

lemma negligible.mul (hf : negligible f) (hg : negligible g) :
  negligible (f * g) :=
hf.mul_is_O 0 (by simpa using hg 0)

end no_zero_smul_divisors

end normed_field

section normed_linear_ordered_field

variables {Î± ğ•œ : Type*} [ordered_comm_semiring Î±] [normed_linear_ordered_field ğ•œ] [algebra Î± ğ•œ]
variables {f g : Î± â†’ ğ•œ}

/-- It suffices to check the negligiblity condition for only sufficiently small exponents `c`,
  assuing algebra_map eventually has norm at least `1` -/
lemma negligible_of_eventually_is_O (hÎ± : âˆ€á¶  (x : Î±) in at_top, 1 â‰¤ âˆ¥algebra_map Î± ğ•œ xâˆ¥)
  (h : âˆ€á¶  (c : â„¤) in at_bot, is_O f (Î» x, (algebra_map Î± ğ•œ x) ^ c) at_top) :
  negligible f :=
begin
  obtain âŸ¨C, hCâŸ© := eventually_at_bot.mp h,
  intro c,
  by_cases hc : c â‰¤ C,
  { exact hC c hc },
  { refine (hC C le_rfl).trans (is_O.of_bound 1 (_)),
    refine at_top.sets_of_superset hÎ± (Î» x hx, _),
    simp only [one_mul, normed_field.norm_fpow, set.mem_set_of_eq],
    refine fpow_le_of_le hx (le_of_not_le hc) }
end

lemma negligible_of_is_O_fpow_le (hÎ± : âˆ€á¶  (x : Î±) in at_top, 1 â‰¤ âˆ¥algebra_map Î± ğ•œ xâˆ¥)
  (C : â„¤) (h : âˆ€ c â‰¤ C, is_O f (Î» n, (algebra_map Î± ğ•œ n) ^ c) at_top) :
  negligible f :=
negligible_of_eventually_is_O hÎ± (eventually_at_bot.2 âŸ¨C, hâŸ©)

lemma negligible_of_is_O_fpow_lt (hÎ± : âˆ€á¶  (x : Î±) in at_top, 1 â‰¤ âˆ¥algebra_map Î± ğ•œ xâˆ¥)
  (C : â„¤) (h : âˆ€ c < C, is_O f (Î» n, (algebra_map Î± ğ•œ n) ^ c) at_top) :
  negligible f :=
negligible_of_is_O_fpow_le hÎ± C.pred
  (Î» c hc, h c (lt_of_le_of_lt hc (int.pred_self_lt C)))

section order_topology

variable [order_topology ğ•œ]

/-- A negligible function must tend to zero in the base ring (not just in norm),
  assuming `algebra_map Î± ğ•œ` tends to `at_top` -/
lemma negligible.tendsto_zero (hÎ± : tendsto (algebra_map Î± ğ•œ) at_top at_top)
  (hf : negligible f) : tendsto f at_top (ğ“ 0) :=
begin
  refine is_O.trans_tendsto (hf (-1)) _,
  have : (has_inv.inv : ğ•œ â†’ ğ•œ) âˆ˜ (algebra_map Î± ğ•œ : Î± â†’ ğ•œ) = (Î» (n : Î±), (algebra_map Î± ğ•œ n) ^ (-1 : â„¤)),
  by simp only [gpow_one, fpow_neg],
  refine this â–¸ (tendsto_inv_at_top_zero).comp (hÎ±),
end

/-- A negligible function eventually has norm less than any positive bound,
  assuming the algebra map tendsto to `at_top` -/
lemma negligible.eventually_le (hÎ± : tendsto (algebra_map Î± ğ•œ) at_top at_top)
  (hf : negligible f) (Îµ : â„) (hÎµ : 0 < Îµ) : âˆ€á¶  (n : Î±) in at_top, âˆ¥f nâˆ¥ â‰¤ Îµ :=
by simpa only [dist_zero_right] using
  (hf.tendsto_zero hÎ±).eventually (metric.closed_ball_mem_nhds (0 : ğ•œ) hÎµ)

@[simp]
lemma negligible_const_iff [(at_top : filter Î±).ne_bot]
  (hÎ± : tendsto (algebra_map Î± ğ•œ) at_top at_top)
  (x : ğ•œ) : negligible (function.const Î± x) â†” x = 0 :=
begin
  refine âŸ¨Î» h, not_not.1 (Î» hx, _), Î» h, by simp [h]âŸ©,
  have : (function.const Î± x â»Â¹' {x}á¶œ) âˆˆ at_top :=
    (tendsto_nhds.1 $ h.tendsto_zero hÎ±) {x}á¶œ (is_open_ne) (ne.symm hx),
  rw [set.preimage_const_of_not_mem (by simp : x âˆ‰ ({x} : set ğ•œ)á¶œ)] at this,
  exact at_top.empty_not_mem this,
end

end order_topology

end normed_linear_ordered_field

end asymptotics
