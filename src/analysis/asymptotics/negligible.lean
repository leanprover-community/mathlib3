/-
Copyright (c) 2021 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import analysis.asymptotics.asymptotics
import analysis.asymptotics.specific_asymptotics
import data.polynomial.eval

/-!
# Negligible Functions

This file defines a predicate `negligible f` on functions `f` from `â„•` to a normed field.

The main theorem is `negligible_polynomial_mul` that says the product of a polynomial
  and a negligible function is still a negligible function
-/

namespace asymptotics

/-- Definition of negligible functions over an arbitrary `normed_field`.
  Note that the second function always has type `â„• â†’ â„`, which generally gives better lemmas. -/
def negligible {ğ•œ : Type*} [normed_field ğ•œ]
  (f : â„• â†’ ğ•œ) :=
âˆ€ (c : â„¤), is_O f (Î» n, (n : â„) ^ c) filter.at_top

variables {ğ•œ : Type*} [normed_field ğ•œ]
variables {f g : â„• â†’ ğ•œ}

lemma negligible_of_is_O (hg : negligible g)
  (h : is_O f g filter.at_top) : negligible f :=
Î» c, h.trans $ hg c

lemma negligible_of_eventually_le (hg : negligible g)
  (h : âˆ€á¶  n in filter.at_top, âˆ¥f nâˆ¥ â‰¤ âˆ¥g nâˆ¥) : negligible f :=
negligible_of_is_O hg $ is_O_iff.2 âŸ¨1, by simpa only [one_mul] using hâŸ©

/-- It suffices to check the negligiblity condition for only sufficiently small exponents `c`.
  See `negligible_of_is_O_fpow_le` for a version with explicit bounds -/
lemma negligible_of_eventually_is_O
  (h : âˆ€á¶  (c : â„¤) in filter.at_bot, is_O f (Î» n, (n : â„) ^ c) filter.at_top) :
  negligible f :=
begin
  obtain âŸ¨C, hCâŸ© := filter.eventually_at_bot.mp h,
  intro c,
  by_cases hc : c â‰¤ C,
  { exact hC c hc },
  { refine (hC C le_rfl).trans (is_O.of_bound 1 (filter.eventually_at_top.2 âŸ¨1, (Î» b hb, _)âŸ©)),
    simp_rw [one_mul, normed_field.norm_fpow, real.norm_coe_nat],
    have hb : 1 â‰¤ (b : â„) := le_trans (le_of_eq nat.cast_one.symm) (nat.cast_le.2 hb),
    exact fpow_le_of_le hb (le_of_not_le hc) }
end

lemma negligible_of_is_O_fpow_le (C : â„¤)
  (h : âˆ€ c â‰¤ C, is_O f (Î» n, (n : â„) ^ c) filter.at_top) :
  negligible f :=
negligible_of_eventually_is_O (filter.eventually_at_bot.2 âŸ¨C, hâŸ©)

lemma negligible_of_is_O_fpow_lt (C : â„¤)
  (h : âˆ€ c < C, is_O f (Î» n, (n : â„) ^ c) filter.at_top) :
  negligible f :=
negligible_of_is_O_fpow_le C.pred
  (Î» c hc, h c (lt_of_le_of_lt hc (int.pred_self_lt C)))

lemma tendsto_zero_of_negligible (hf : negligible f) :
  filter.tendsto f filter.at_top (nhds 0) :=
begin
  refine is_O.trans_tendsto (hf (-1)) _,
  have : (Î» (n : â„•), (n : â„) ^ (-1 : â„¤)) = (has_inv.inv : â„ â†’ â„) âˆ˜ (coe : â„• â†’ â„),
  by simp only [gpow_one, fpow_neg],
  rw this,
  refine filter.tendsto.comp (tendsto_inv_at_top_zero) (nat_coe_tendsto_at_top â„),
end

lemma norm_eventually_le_of_negligible
  (hf : negligible f) (xâ‚€ : â„) (hxâ‚€ : 0 < xâ‚€) :
  âˆ€á¶  (n : â„•) in filter.at_top, âˆ¥f nâˆ¥ â‰¤ xâ‚€ :=
begin
  obtain âŸ¨c, hcâŸ© := is_O_iff.1 (hf (-1)),
  have : âˆ€á¶  (n : â„•) in filter.at_top, c * âˆ¥(n : â„) ^ (-1 : â„¤)âˆ¥ â‰¤ xâ‚€,
  { obtain âŸ¨a, haâŸ© := exists_nat_ge (c * xâ‚€â»Â¹),
    refine filter.eventually_at_top.2 âŸ¨max a 1, Î» b hb, _âŸ©,
    have hb0 : 0 < (b : â„) := nat.cast_pos.2 (le_trans (le_max_right a 1) hb),
    have hba : (a : â„) â‰¤ (b : â„) := nat.cast_le.2 (le_trans (le_max_left a 1) hb),
    rw [fpow_neg, gpow_one, normed_field.norm_inv, real.norm_coe_nat,
      mul_inv_le_iff hb0, mul_comm _ xâ‚€],
    calc c â‰¤ xâ‚€ * (a : â„) : (mul_inv_le_iff hxâ‚€).1 ha
      ... â‰¤ xâ‚€ * (b : â„) : mul_le_mul le_rfl hba (nat.cast_nonneg a) (le_of_lt hxâ‚€) },
  refine filter.eventually.mp hc (filter.eventually.mono this (Î» x hx hx', le_trans hx' hx)),
end

@[simp]
lemma negligible_zero : negligible (function.const â„• (0 : ğ•œ)) :=
Î» c, is_O_zero _ _

lemma negligible_add (hf : negligible f) (hg : negligible g) :
  negligible (f + g) :=
Î» c, is_O.add (hf c) (hg c)

lemma negligible_mul (hf : negligible f) (hg : negligible g) :
  negligible (f * g) :=
begin
  suffices : is_O (f * g) f filter.at_top,
  from Î» c, this.trans (hf c),
  refine is_O.of_bound 1 ((norm_eventually_le_of_negligible hg 1 (zero_lt_one)).mono (Î» x hx, _)),
  rw [pi.mul_apply, normed_field.norm_mul, mul_comm 1 âˆ¥f xâˆ¥],
  exact mul_le_mul le_rfl hx (norm_nonneg $ g x) (norm_nonneg $ f x),
end

@[simp]
lemma negligible_const_iff [t1_space ğ•œ] (x : ğ•œ) :
  negligible (function.const â„• x) â†” x = 0 :=
begin
  refine âŸ¨Î» h, not_not.1 (Î» hx, _), Î» h, h.symm â–¸ negligible_zeroâŸ©,
  have : (function.const â„• x â»Â¹' {x}á¶œ) âˆˆ filter.at_top :=
    (tendsto_nhds.1 $ tendsto_zero_of_negligible h) {x}á¶œ (is_open_ne) (ne.symm hx),
  rw [set.preimage_const_of_not_mem (by simp : x âˆ‰ ({x} : set ğ•œ)á¶œ)] at this,
  exact filter.at_top.empty_not_mem this,
end

lemma negligible_const_mul (hf : negligible f) (c : ğ•œ) :
  negligible (Î» n, c * f n) :=
(negligible_of_is_O hf (is_O_const_mul_self c f filter.at_top))

@[simp]
lemma negligible_const_mul_iff (f : â„• â†’ ğ•œ) (c : ğ•œ) :
  negligible (Î» n, c * f n) â†” (c = 0) âˆ¨ (negligible f) :=
begin
  refine âŸ¨Î» h, _, Î» h, _âŸ©,
  { by_cases hc : c = 0,
    { exact or.inl hc },
    { exact or.inr (negligible_of_is_O h (is_O_self_const_mul c hc f filter.at_top)) } },
  { cases h,
    { simp only [h, zero_mul, negligible_const_iff] },
    { exact negligible_const_mul h c } }
end

-- TODO: The lemmas below can be generalized to `iff` statements if `âˆ¥(n : ğ•œ)âˆ¥` doesn't tend to 0

lemma negligible_coe_nat_mul (hf : negligible f) :
  negligible (Î» n, (n : ğ•œ) * f n) :=
begin
  refine negligible_of_is_O_fpow_lt 0 (Î» c hc, _),
  refine is_O.trans (is_O.mul (coe_nat_is_O_coe_nat_real ğ•œ) (hf (c - 1)))
    (is_O_of_le _ (Î» x, le_of_eq (congr_arg _ _))),
  by_cases hx : (x : â„) = 0,
  { simp_rw [hx, zero_mul],
    refine symm (zero_fpow c (ne_of_lt hc)) },
  { calc (x : â„) * â†‘x ^ (c - 1) = (â†‘x ^ (1 : â„¤)) * (â†‘x ^ (c - 1)) : by rw gpow_one
      ... = â†‘x ^ (1 + (c - 1)) : (fpow_add hx 1 (c - 1)).symm
      ... = â†‘x ^ c : congr_arg (Î» g, gpow g (x : â„)) (by linarith) }
end

lemma negligible_coe_nat_pow_mul (hf : negligible f) (p : â„•) :
  negligible (Î» n, (n : ğ•œ) ^ p * f n) :=
begin
  induction p with p hp,
  { simp_rw [pow_zero, one_mul],
    exact hf },
  { simp_rw [pow_succ, mul_assoc],
    exact negligible_coe_nat_mul hp }
end

lemma negligible_nsmul (hf : negligible f) :
  negligible (Î» n, n â€¢ f n) :=
by simpa [nsmul_eq_mul] using negligible_coe_nat_mul hf

lemma negligible_pow_nsmul (hf : negligible f) (p : â„•) :
  negligible (Î» n, (n ^ p) â€¢ f n) :=
by simpa [nsmul_eq_mul] using negligible_coe_nat_pow_mul hf p

theorem negligible_polynomial_mul {ğ•œ : Type*} [normed_field ğ•œ]
  {f : â„• â†’ ğ•œ} (hf : negligible f) (p : polynomial ğ•œ) :
  negligible (Î» n, (p.eval n) * f n) :=
begin
  refine polynomial.induction_on' p (Î» p q hp hq, _) (Î» m x, _),
  { simp_rw [polynomial.eval_add, add_mul],
    exact negligible_add hp hq },
  { simp_rw [polynomial.eval_monomial, mul_assoc],
    exact negligible_const_mul (negligible_coe_nat_pow_mul hf m) x }
end

end asymptotics
