/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.normed.order.basic
import analysis.asymptotics.asymptotics

/-!
# A collection of specific asymptotic results

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains specific lemmas about asymptotics which don't have their place in the general
theory developped in `analysis.asymptotics.asymptotics`.
-/

open filter asymptotics
open_locale topology

section normed_field

/-- If `f : ğ•œ â†’ E` is bounded in a punctured neighborhood of `a`, then `f(x) = o((x - a)â»Â¹)` as
`x â†’ a`, `x â‰  a`. -/
lemma filter.is_bounded_under.is_o_sub_self_inv {ğ•œ E : Type*} [normed_field ğ•œ] [has_norm E]
  {a : ğ•œ} {f : ğ•œ â†’ E} (h : is_bounded_under (â‰¤) (ğ“[â‰ ] a) (norm âˆ˜ f)) :
  f =o[ğ“[â‰ ] a] (Î» x, (x - a)â»Â¹) :=
begin
  refine (h.is_O_const (one_ne_zero' â„)).trans_is_o (is_o_const_left.2 $ or.inr _),
  simp only [(âˆ˜), norm_inv],
  exact (tendsto_norm_sub_self_punctured_nhds a).inv_tendsto_zero
end

end normed_field

section linear_ordered_field

variables {ğ•œ : Type*} [linear_ordered_field ğ•œ]

lemma pow_div_pow_eventually_eq_at_top {p q : â„•} :
  (Î» x : ğ•œ, x^p / x^q) =á¶ [at_top] (Î» x, x^((p : â„¤) -q)) :=
begin
  apply ((eventually_gt_at_top (0 : ğ•œ)).mono (Î» x hx, _)),
  simp [zpow_subâ‚€ hx.ne'],
end

lemma pow_div_pow_eventually_eq_at_bot {p q : â„•} :
  (Î» x : ğ•œ, x^p / x^q) =á¶ [at_bot] (Î» x, x^((p : â„¤) -q)) :=
begin
  apply ((eventually_lt_at_bot (0 : ğ•œ)).mono (Î» x hx, _)),
  simp [zpow_subâ‚€ hx.ne],
end

lemma tendsto_zpow_at_top_at_top {n : â„¤}
  (hn : 0 < n) : tendsto (Î» x : ğ•œ, x^n) at_top at_top :=
begin
  lift n to â„• using hn.le,
  simp only [zpow_coe_nat],
  exact tendsto_pow_at_top (nat.cast_pos.mp hn).ne'
end

lemma tendsto_pow_div_pow_at_top_at_top {p q : â„•}
  (hpq : q < p) : tendsto (Î» x : ğ•œ, x^p / x^q) at_top at_top :=
begin
  rw tendsto_congr' pow_div_pow_eventually_eq_at_top,
  apply tendsto_zpow_at_top_at_top,
  linarith
end

lemma tendsto_pow_div_pow_at_top_zero [topological_space ğ•œ] [order_topology ğ•œ] {p q : â„•}
  (hpq : p < q) : tendsto (Î» x : ğ•œ, x^p / x^q) at_top (ğ“ 0) :=
begin
  rw tendsto_congr' pow_div_pow_eventually_eq_at_top,
  apply tendsto_zpow_at_top_zero,
  linarith
end

end linear_ordered_field

section normed_linear_ordered_field

variables {ğ•œ : Type*} [normed_linear_ordered_field ğ•œ]

lemma asymptotics.is_o_pow_pow_at_top_of_lt
  [order_topology ğ•œ] {p q : â„•} (hpq : p < q) :
  (Î» x : ğ•œ, x^p) =o[at_top] (Î» x, x^q) :=
begin
  refine (is_o_iff_tendsto' _).mpr (tendsto_pow_div_pow_at_top_zero hpq),
  exact (eventually_gt_at_top 0).mono (Î» x hx hxq, (pow_ne_zero q hx.ne' hxq).elim),
end

lemma asymptotics.is_O.trans_tendsto_norm_at_top {Î± : Type*} {u v : Î± â†’ ğ•œ} {l : filter Î±}
  (huv : u =O[l] v) (hu : tendsto (Î» x, â€–u xâ€–) l at_top) : tendsto (Î» x, â€–v xâ€–) l at_top :=
begin
  rcases huv.exists_pos with âŸ¨c, hc, hcuvâŸ©,
  rw is_O_with at hcuv,
  convert tendsto.at_top_div_const hc (tendsto_at_top_mono' l hcuv hu),
  ext x,
  rw mul_div_cancel_left _ hc.ne.symm,
end

end normed_linear_ordered_field

section real

open_locale big_operators
open finset

lemma asymptotics.is_o.sum_range {Î± : Type*} [normed_add_comm_group Î±]
  {f : â„• â†’ Î±} {g : â„• â†’ â„} (h : f =o[at_top] g)
  (hg : 0 â‰¤ g) (h'g : tendsto (Î» n, âˆ‘ i in range n, g i) at_top at_top) :
  (Î» n, âˆ‘ i in range n, f i) =o[at_top] (Î» n, âˆ‘ i in range n, g i) :=
begin
  have A : âˆ€ i, â€–g iâ€– = g i := Î» i, real.norm_of_nonneg (hg i),
  have B : âˆ€ n, â€–âˆ‘ i in range n, g iâ€– = âˆ‘ i in range n, g i,
    from Î» n, by rwa [real.norm_eq_abs, abs_sum_of_nonneg'],
  apply is_o_iff.2 (Î» Îµ Îµpos, _),
  obtain âŸ¨N, hNâŸ© : âˆƒ (N : â„•), âˆ€ (b : â„•), N â‰¤ b â†’ â€–f bâ€– â‰¤ Îµ / 2 * g b,
    by simpa only [A, eventually_at_top] using is_o_iff.mp h (half_pos Îµpos),
  have : (Î» (n : â„•), âˆ‘ i in range N, f i) =o[at_top] (Î» (n : â„•), âˆ‘ i in range n, g i),
  { apply is_o_const_left.2,
    exact or.inr (h'g.congr (Î» n, (B n).symm)) },
  filter_upwards [is_o_iff.1 this (half_pos Îµpos), Ici_mem_at_top N] with n hn Nn,
  calc â€–âˆ‘ i in range n, f iâ€–
  = â€–âˆ‘ i in range N, f i + âˆ‘ i in Ico N n, f iâ€– :
    by rw sum_range_add_sum_Ico _ Nn
  ... â‰¤ â€–âˆ‘ i in range N, f iâ€– + â€–âˆ‘ i in Ico N n, f iâ€– :
    norm_add_le _ _
  ... â‰¤ â€–âˆ‘ i in range N, f iâ€– + âˆ‘ i in Ico N n, (Îµ / 2) * g i :
    add_le_add le_rfl (norm_sum_le_of_le _ (Î» i hi, hN _ (mem_Ico.1 hi).1))
  ... â‰¤ â€–âˆ‘ i in range N, f iâ€– + âˆ‘ i in range n, (Îµ / 2) * g i :
    begin
      refine add_le_add le_rfl _,
      apply sum_le_sum_of_subset_of_nonneg,
      { rw range_eq_Ico,
        exact Ico_subset_Ico (zero_le _) le_rfl },
      { assume i hi hident,
        exact mul_nonneg (half_pos Îµpos).le (hg i) }
    end
  ... â‰¤ (Îµ / 2) * â€–âˆ‘ i in range n, g iâ€– + (Îµ / 2) * (âˆ‘ i in range n, g i) :
    begin
      rw â† mul_sum,
      exact add_le_add hn (mul_le_mul_of_nonneg_left le_rfl (half_pos Îµpos).le),
    end
  ... = Îµ * â€–(âˆ‘ i in range n, g i)â€– : by { simp [B], ring }
end

lemma asymptotics.is_o_sum_range_of_tendsto_zero {Î± : Type*} [normed_add_comm_group Î±]
  {f : â„• â†’ Î±} (h : tendsto f at_top (ğ“ 0)) :
  (Î» n, âˆ‘ i in range n, f i) =o[at_top] (Î» n, (n : â„)) :=
begin
  have := ((is_o_one_iff â„).2 h).sum_range (Î» i, zero_le_one),
  simp only [sum_const, card_range, nat.smul_one_eq_coe] at this,
  exact this tendsto_coe_nat_at_top_at_top
end

/-- The Cesaro average of a converging sequence converges to the same limit. -/
lemma filter.tendsto.cesaro_smul {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
  {u : â„• â†’ E} {l : E} (h : tendsto u at_top (ğ“ l)) :
  tendsto (Î» (n : â„•), (n â»Â¹ : â„) â€¢ (âˆ‘ i in range n, u i)) at_top (ğ“ l) :=
begin
  rw [â† tendsto_sub_nhds_zero_iff, â† is_o_one_iff â„],
  have := asymptotics.is_o_sum_range_of_tendsto_zero (tendsto_sub_nhds_zero_iff.2 h),
  apply ((is_O_refl (Î» (n : â„•), (n : â„) â»Â¹) at_top).smul_is_o this).congr' _ _,
  { filter_upwards [Ici_mem_at_top 1] with n npos,
    have nposâ„ : (0 : â„) < n := nat.cast_pos.2 npos,
    simp only [smul_sub, sum_sub_distrib, sum_const, card_range, sub_right_inj],
    rw [nsmul_eq_smul_cast â„, smul_smul, inv_mul_cancel nposâ„.ne', one_smul] },
  { filter_upwards [Ici_mem_at_top 1] with n npos,
    have nposâ„ : (0 : â„) < n := nat.cast_pos.2 npos,
    rw [algebra.id.smul_eq_mul, inv_mul_cancel nposâ„.ne'] }
end

/-- The Cesaro average of a converging sequence converges to the same limit. -/
lemma filter.tendsto.cesaro {u : â„• â†’ â„} {l : â„} (h : tendsto u at_top (ğ“ l)) :
  tendsto (Î» (n : â„•), (n â»Â¹ : â„) * (âˆ‘ i in range n, u i)) at_top (ğ“ l) :=
h.cesaro_smul

end real
