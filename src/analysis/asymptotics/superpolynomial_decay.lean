/-
Copyright (c) 2021 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import analysis.asymptotics.asymptotics
import analysis.asymptotics.specific_asymptotics
import data.polynomial.eval

/-!
# Super-Polynomial Function Decay

This file defines a predicate `asymptotics.superpolynomial_decay f` for a function satisfying
  one of following equivalent definitions (The definition is in terms of the first condition):

* `f` is `O(x ^ c)` for all (or sufficiently small) integers `c`
* `x ^ c * f` is bounded for all (or sufficiently large) integers `c`
* `x ^ c * f` tends to `ğ“ 0` for all (or sufficiently large) integers `c`
* `f` is `o(x ^ c)` for all (or sufficiently small) integers `c`

The equivalence between the first two is given by in `superpolynomial_decay_iff_is_bounded_under`.
The equivalence between the first and third is given in `superpolynomial_decay_iff_tendsto_zero`.
The equivalence between the first and fourth is given in `superpolynomial_decay_iff_is_o`.

These conditions are all equivalent to conditions in terms of polynomials, replacing `x ^ c` with
  `p(x)` or `p(x)â»Â¹` as appropriate, since asymptotically `p(x)` behaves like `X ^ p.nat_degree`.
These further equivalences are not proven in mathlib but would be good future projects.

The definition of superpolynomial decay for a function `f : Î± â†’ ğ•œ`
  is made relative to an algebra structure `[algebra Î± ğ•œ]`.
Super-polynomial decay then means the function `f x` decays faster than
  `(p.eval (algebra_map Î± ğ•œ x))â»Â¹` for all polynomials `p : polynomial ğ•œ`.

When the algebra structure is given by `n â†¦ â†‘n : â„• â†’ â„` this defines negligible functions:
https://en.wikipedia.org/wiki/Negligible_function

When the algebra structure is given by `(râ‚,...,râ‚™) â†¦ râ‚*...*râ‚™ : â„â¿ â†’ â„` this is equivalent
  to the definition of rapidly decreasing functions given here:
https://ncatlab.org/nlab/show/rapidly+decreasing+function

# Main Theorems

* `superpolynomial_decay.polynomial_mul` says that if `f(x)` is negligible,
    then so is `p(x) * f(x)` for any polynomial `p`.
* `superpolynomial_decay_iff_is_bounded_under` says that `f` is negligible iff
    `p(x) * f(x)` has bounded norm for all polynomials `p(x)`.
* `superpolynomial_decay_of_eventually_is_O` says that it suffices to check `f(x)` is `O(x ^ c)`
    for only sufficiently small `c`, rather than all integers `c`.
-/

namespace asymptotics

open_locale topological_space
open filter

/-- A function `f` from an `ordered_comm_semiring` to a `normed_field` has superpolynomial decay
  iff `f(x)` is `O(x ^ c)` for all integers `c`. -/
def superpolynomial_decay {Î± ğ•œ : Type*} [ordered_comm_semiring Î±] [normed_field ğ•œ] [algebra Î± ğ•œ]
  (f : Î± â†’ ğ•œ) :=
âˆ€ (c : â„¤), is_O f (Î» x, (algebra_map Î± ğ•œ x) ^ c) filter.at_top

section normed_field

variables {Î± ğ•œ : Type*} [ordered_comm_semiring Î±] [normed_field ğ•œ] [algebra Î± ğ•œ]
variables {f g : Î± â†’ ğ•œ}

theorem superpolynomial_decay_iff_is_bounded_under (f : Î± â†’ ğ•œ)
  (hÎ± : âˆ€á¶  (x : Î±) in at_top, (algebra_map Î± ğ•œ x) â‰  0) :
  superpolynomial_decay f â†”
    âˆ€ (c : â„¤), is_bounded_under has_le.le at_top (Î» x, âˆ¥f x * (algebra_map Î± ğ•œ x) ^ câˆ¥) :=
begin
  split; intros h c; specialize h (-c),
  { simpa [div_eq_mul_inv] using div_is_bounded_under_of_is_O h },
  { refine (is_O_iff_div_is_bounded_under _).2 _,
    { exact hÎ±.mono (Î» x hx hx', absurd (zpow_eq_zero hx') hx) },
    { simpa [div_eq_mul_inv] using h } }
end

theorem superpolynomial_decay_iff_is_o (f : Î± â†’ ğ•œ)
  (hÎ± : tendsto (Î» x, âˆ¥algebra_map Î± ğ•œ xâˆ¥) at_top at_top) :
  superpolynomial_decay f â†”
    âˆ€ (c : â„¤), is_o f (Î» x, (algebra_map Î± ğ•œ x) ^ c) at_top :=
begin
  refine âŸ¨Î» h c, _, Î» h c, (h c).is_OâŸ©,
  have hÎ±' : âˆ€á¶  (x : Î±) in at_top, (algebra_map Î± ğ•œ x) â‰  0,
  from (eventually_ne_of_tendsto_norm_at_top hÎ± 0).mono (Î» x hx hx', absurd hx' hx),
  have : is_o (Î» x, 1 : Î± â†’ ğ•œ) (Î» x, (algebra_map Î± ğ•œ x)) at_top,
  { refine is_o_of_tendsto' (hÎ±'.mono $ Î» x hx hx', absurd hx' hx)
      (tendsto_zero_iff_norm_tendsto_zero.2 _),
    simp only [one_div, normed_field.norm_inv],
    exact tendsto.comp tendsto_inv_at_top_zero hÎ± },
  have := this.mul_is_O (h $ c - 1),
  simp only [one_mul] at this,
  refine this.trans_is_O (is_O.of_bound 1 (hÎ±'.mono (Î» x hx, le_of_eq _))),
  rw [zpow_sub_oneâ‚€ hx, mul_comm, mul_assoc, inv_mul_cancel hx, one_mul, mul_one]
end

theorem superpolynomial_decay_iff_norm_tendsto_zero (f : Î± â†’ ğ•œ)
  (hÎ± : tendsto (Î» x, âˆ¥algebra_map Î± ğ•œ xâˆ¥) at_top at_top) :
  superpolynomial_decay f â†”
    âˆ€ (c : â„¤), tendsto (Î» x, âˆ¥f x * (algebra_map Î± ğ•œ x) ^ câˆ¥) at_top (ğ“ 0) :=
begin
  refine âŸ¨Î» h c, _, Î» h, _âŸ©,
  { refine tendsto_zero_iff_norm_tendsto_zero.1 _,
    rw (superpolynomial_decay_iff_is_o f hÎ±) at h,
    simpa [div_eq_mul_inv] using (h $ -c).tendsto_0 },
  { have hÎ±' : âˆ€á¶  (x : Î±) in at_top, (algebra_map Î± ğ•œ x) â‰  0,
    from (eventually_ne_of_tendsto_norm_at_top hÎ± 0).mono (Î» x hx hx', absurd hx' hx),
    exact (superpolynomial_decay_iff_is_bounded_under f hÎ±').2
      (Î» c, is_bounded_under_of_tendsto (tendsto_zero_iff_norm_tendsto_zero.2 $ h c)) }
end

lemma superpolynomial_decay_iff_tendsto_zero (f : Î± â†’ ğ•œ)
  (hÎ± : tendsto (Î» x, âˆ¥algebra_map Î± ğ•œ xâˆ¥) at_top at_top) :
  superpolynomial_decay f â†”
    âˆ€ (c : â„¤), tendsto (Î» x, f x * (algebra_map Î± ğ•œ x) ^ c) at_top (ğ“ 0) :=
(superpolynomial_decay_iff_norm_tendsto_zero f hÎ±).trans
  (by simp [tendsto_zero_iff_norm_tendsto_zero])

lemma is_O.trans_superpolynomial_decay (h : is_O f g at_top)
  (hg : superpolynomial_decay g) : superpolynomial_decay f :=
Î» c, h.trans $ hg c

alias is_O.trans_superpolynomial_decay â† superpolynomial_decay.is_O_mono

lemma superpolynomial_decay.mono (hf : superpolynomial_decay f)
  (h : âˆ€ n, âˆ¥g nâˆ¥ â‰¤ âˆ¥f nâˆ¥) : superpolynomial_decay g :=
(is_O_of_le at_top h).trans_superpolynomial_decay hf

lemma superpolynomial_decay.eventually_mono (hf : superpolynomial_decay f)
  (h : âˆ€á¶  n in at_top, âˆ¥g nâˆ¥ â‰¤ âˆ¥f nâˆ¥) : superpolynomial_decay g :=
(is_O_iff.2 âŸ¨1, by simpa only [one_mul] using hâŸ©).trans_superpolynomial_decay hf

@[simp]
lemma superpolynomial_decay_zero : superpolynomial_decay (0 : Î± â†’ ğ•œ) :=
Î» c, is_O_zero _ _

@[simp]
lemma superpolynomial_decay_zero' : superpolynomial_decay (Î» (x : Î±), (0 : ğ•œ)) :=
superpolynomial_decay_zero

lemma superpolynomial_decay.add (hf : superpolynomial_decay f) (hg : superpolynomial_decay g) :
  superpolynomial_decay (f + g) :=
Î» c, is_O.add (hf c) (hg c)

lemma superpolynomial_decay.const_mul (hf : superpolynomial_decay f) (c : ğ•œ) :
  superpolynomial_decay (Î» n, c * f n) :=
(is_O_const_mul_self c f at_top).trans_superpolynomial_decay hf

lemma superpolynomial_decay.mul_const (hf : superpolynomial_decay f) (c : ğ•œ) :
  superpolynomial_decay (Î» n, f n * c) :=
by simpa [mul_comm _ c] using superpolynomial_decay.const_mul hf c

lemma superpolynomial_decay_const_mul_iff_of_ne_zero {c : ğ•œ} (hc : c â‰  0) :
  superpolynomial_decay (Î» n, c * f n) â†” superpolynomial_decay f :=
âŸ¨Î» h, (is_O_self_const_mul c hc f at_top).trans_superpolynomial_decay h, Î» h, h.const_mul c âŸ©

lemma superpolynomial_decay_mul_const_iff_of_ne_zero {c : ğ•œ} (hc : c â‰  0) :
  superpolynomial_decay (Î» n, f n * c) â†” superpolynomial_decay f :=
by simpa [mul_comm _ c] using superpolynomial_decay_const_mul_iff_of_ne_zero hc

@[simp]
lemma superpolynomial_decay_const_mul_iff (c : ğ•œ) :
  superpolynomial_decay (Î» n, c * f n) â†” c = 0 âˆ¨ superpolynomial_decay f :=
begin
  by_cases hc0 : c = 0,
  { simp [hc0] },
  { exact (superpolynomial_decay_const_mul_iff_of_ne_zero hc0).trans
      âŸ¨or.inr, or.rec (Î» hc0', absurd hc0' hc0) idâŸ© }
end

@[simp]
lemma superpolynomial_decay_mul_const_iff (c : ğ•œ) :
  superpolynomial_decay (Î» n, f n * c) â†” c = 0 âˆ¨ superpolynomial_decay f :=
by simp [mul_comm _ c]

section no_zero_smul_divisors

variables [no_zero_smul_divisors Î± ğ•œ]

lemma superpolynomial_decay.algebra_map_mul (hf : superpolynomial_decay f) :
  superpolynomial_decay (Î» n, (algebra_map Î± ğ•œ n) * f n) :=
begin
  haveI : nontrivial Î± := (algebra_map Î± ğ•œ).domain_nontrivial,
  refine Î» c, (is_O.mul (is_O_refl (algebra_map Î± ğ•œ) at_top) (hf (c - 1))).trans _,
  refine is_O_of_div_tendsto_nhds (eventually_of_forall
    (Î» x hx, mul_eq_zero_of_left (zpow_eq_zero hx) _)) 1 (tendsto_nhds.2 _),
  refine Î» s hs hs', at_top.sets_of_superset (mem_at_top 1) (Î» x hx, set.mem_preimage.2 _),
  have hx' : algebra_map Î± ğ•œ x â‰  0 := Î» hx', (ne_of_lt $ lt_of_lt_of_le zero_lt_one hx).symm
    (by simpa [algebra.algebra_map_eq_smul_one, smul_eq_zero] using hx'),
  convert hs',
  rw [pi.div_apply, div_eq_one_iff_eq (zpow_ne_zero c hx'), zpow_sub_oneâ‚€ hx' c,
    mul_comm (algebra_map Î± ğ•œ x), mul_assoc, inv_mul_cancel hx', mul_one],
end

lemma superpolynomial_decay.algebra_map_pow_mul (hf : superpolynomial_decay f) (p : â„•) :
  superpolynomial_decay (Î» n, (algebra_map Î± ğ•œ n) ^ p * f n) :=
begin
  induction p with p hp,
  { simp_rw [pow_zero, one_mul],
    exact hf },
  { simp_rw [pow_succ, mul_assoc],
    exact hp.algebra_map_mul }
end

theorem superpolynomial_decay.polynomial_mul (hf : superpolynomial_decay f) (p : polynomial ğ•œ) :
  superpolynomial_decay (Î» n, (p.eval (algebra_map Î± ğ•œ n)) * f n) :=
begin
  refine polynomial.induction_on' p (Î» p q hp hq, _) (Î» m x, _),
  { simp_rw [polynomial.eval_add, add_mul],
    exact hp.add hq },
  { simp_rw [polynomial.eval_monomial, mul_assoc],
    exact (hf.algebra_map_pow_mul m).const_mul x }
end

/-- If `f` has superpolynomial decay, and `g` is `O(p)` for some polynomial `p`,
  then `f * g` has superpolynomial decay -/
lemma superpolynomial_decay.mul_is_O_polynomial (hf : superpolynomial_decay f) (p : polynomial ğ•œ)
  (hg : is_O g (Î» n, p.eval (algebra_map Î± ğ•œ n)) filter.at_top) : superpolynomial_decay (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_superpolynomial_decay
  ((hf.polynomial_mul p).mono $ Î» x, le_of_eq (congr_arg _ $ mul_comm _ _))

/-- If `f` has superpolynomial decay, and `g` is `O(n ^ c)` for some integer `c`,
  then `f * g` has has superpolynomial decay-/
lemma superpolynomial_decay.mul_is_O (hf : superpolynomial_decay f) (c : â„•)
  (hg : is_O g (Î» n, (algebra_map Î± ğ•œ n) ^ c) at_top) : superpolynomial_decay (f * g) :=
(is_O.mul (is_O_refl f at_top) hg).trans_superpolynomial_decay
  ((hf.algebra_map_pow_mul c).mono $ Î» x, le_of_eq (congr_arg _ $ mul_comm _ _))

lemma superpolynomial_decay.mul (hf : superpolynomial_decay f) (hg : superpolynomial_decay g) :
  superpolynomial_decay (f * g) :=
hf.mul_is_O 0 (by simpa using hg 0)

end no_zero_smul_divisors

end normed_field

section normed_linear_ordered_field

variables {Î± ğ•œ : Type*} [ordered_comm_semiring Î±] [normed_linear_ordered_field ğ•œ] [algebra Î± ğ•œ]
variables {f g : Î± â†’ ğ•œ}

/-- It suffices to check the decay condition for only sufficiently small exponents `c`,
  assuing algebra_map eventually has norm at least `1` -/
lemma superpolynomial_decay_of_eventually_is_O (hÎ± : âˆ€á¶  (x : Î±) in at_top, 1 â‰¤ âˆ¥algebra_map Î± ğ•œ xâˆ¥)
  (h : âˆ€á¶  (c : â„¤) in at_bot, is_O f (Î» x, (algebra_map Î± ğ•œ x) ^ c) at_top) :
  superpolynomial_decay f :=
begin
  obtain âŸ¨C, hCâŸ© := eventually_at_bot.mp h,
  intro c,
  by_cases hc : c â‰¤ C,
  { exact hC c hc },
  { refine (hC C le_rfl).trans (is_O.of_bound 1 (_)),
    refine at_top.sets_of_superset hÎ± (Î» x hx, _),
    simp only [one_mul, normed_field.norm_zpow, set.mem_set_of_eq],
    exact zpow_le_of_le hx (le_of_not_le hc) }
end

lemma superpolynomial_decay_of_is_O_zpow_le (hÎ± : âˆ€á¶  (x : Î±) in at_top, 1 â‰¤ âˆ¥algebra_map Î± ğ•œ xâˆ¥)
  (C : â„¤) (h : âˆ€ c â‰¤ C, is_O f (Î» n, (algebra_map Î± ğ•œ n) ^ c) at_top) :
  superpolynomial_decay f :=
superpolynomial_decay_of_eventually_is_O hÎ± (eventually_at_bot.2 âŸ¨C, hâŸ©)

lemma superpolynomial_decay_of_is_O_zpow_lt (hÎ± : âˆ€á¶  (x : Î±) in at_top, 1 â‰¤ âˆ¥algebra_map Î± ğ•œ xâˆ¥)
  (C : â„¤) (h : âˆ€ c < C, is_O f (Î» n, (algebra_map Î± ğ•œ n) ^ c) at_top) :
  superpolynomial_decay f :=
superpolynomial_decay_of_is_O_zpow_le hÎ± C.pred
  (Î» c hc, h c (lt_of_le_of_lt hc (int.pred_self_lt C)))

section order_topology

variable [order_topology ğ•œ]

/-- A function with superpolynomial decay must tend to zero in the base ring (not just in norm),
  assuming `algebra_map Î± ğ•œ` tends to `at_top` -/
lemma superpolynomial_decay.tendsto_zero (hÎ± : tendsto (algebra_map Î± ğ•œ) at_top at_top)
  (hf : superpolynomial_decay f) : tendsto f at_top (ğ“ 0) :=
begin
  refine is_O.trans_tendsto (hf (-1)) _,
  have : (has_inv.inv : ğ•œ â†’ ğ•œ) âˆ˜ (algebra_map Î± ğ•œ : Î± â†’ ğ•œ)
    = (Î» (n : Î±), (algebra_map Î± ğ•œ n) ^ (-1 : â„¤)),
  by simp only [zpow_one, zpow_negâ‚€],
  exact this â–¸ (tendsto_inv_at_top_zero).comp (hÎ±)
end

/-- A function with superpolynomial decay eventually has norm less than any positive bound,
  assuming the algebra map tendsto to `at_top` -/
lemma superpolynomial_decay.eventually_le (hÎ± : tendsto (algebra_map Î± ğ•œ) at_top at_top)
  (hf : superpolynomial_decay f) (Îµ : â„) (hÎµ : 0 < Îµ) : âˆ€á¶  (n : Î±) in at_top, âˆ¥f nâˆ¥ â‰¤ Îµ :=
by simpa only [dist_zero_right] using
  (hf.tendsto_zero hÎ±).eventually (metric.closed_ball_mem_nhds (0 : ğ•œ) hÎµ)

lemma superpolynomial_decay_const_iff [(at_top : filter Î±).ne_bot]
  (hÎ± : tendsto (algebra_map Î± ğ•œ) at_top at_top)
  (x : ğ•œ) : superpolynomial_decay (function.const Î± x) â†” x = 0 :=
begin
  refine âŸ¨Î» h, not_not.1 (Î» hx, _), Î» h, by simp [h]âŸ©,
  have : (function.const Î± x â»Â¹' {x}á¶œ) âˆˆ at_top :=
    (tendsto_nhds.1 $ h.tendsto_zero hÎ±) {x}á¶œ (is_open_ne) (ne.symm hx),
  rw [set.preimage_const_of_not_mem (by simp : x âˆ‰ ({x} : set ğ•œ)á¶œ)] at this,
  exact at_top.empty_not_mem this
end

end order_topology

end normed_linear_ordered_field

end asymptotics
