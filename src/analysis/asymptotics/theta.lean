/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.asymptotics.asymptotics

/-!
# Asymptotic equivalence up to a constant

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define `asymptotics.is_Theta l f g` (notation: `f =Î˜[l] g`) as
`f =O[l] g âˆ§ g =O[l] f`, then prove basic properties of this equivalence relation.
-/

open filter
open_locale topology

namespace asymptotics

variables {Î± : Type*} {Î² : Type*} {E : Type*} {F : Type*} {G : Type*}
  {E' : Type*} {F' : Type*} {G' : Type*}
  {E'' : Type*} {F'' : Type*} {G'' : Type*}
  {R : Type*} {R' : Type*} {ğ•œ : Type*} {ğ•œ' : Type*}

variables [has_norm E] [has_norm F] [has_norm G]
variables [seminormed_add_comm_group E'] [seminormed_add_comm_group F']
  [seminormed_add_comm_group G'] [normed_add_comm_group E''] [normed_add_comm_group F'']
  [normed_add_comm_group G''] [semi_normed_ring R] [semi_normed_ring R']
variables [normed_field ğ•œ] [normed_field ğ•œ']
variables {c c' câ‚ câ‚‚ : â„} {f : Î± â†’ E} {g : Î± â†’ F} {k : Î± â†’ G}
variables {f' : Î± â†’ E'} {g' : Î± â†’ F'} {k' : Î± â†’ G'}
variables {f'' : Î± â†’ E''} {g'' : Î± â†’ F''}
variables {l l' : filter Î±}

/-- We say that `f` is `Î˜(g)` along a filter `l` (notation: `f =Î˜[l] g`) if `f =O[l] g` and
`g =O[l] f`. -/
def is_Theta (l : filter Î±) (f : Î± â†’ E) (g : Î± â†’ F) : Prop := is_O l f g âˆ§ is_O l g f

notation f ` =Î˜[`:100 l `] ` g:100 := is_Theta l f g

lemma is_O.antisymm (hâ‚ : f =O[l] g) (hâ‚‚ : g =O[l] f) : f =Î˜[l] g := âŸ¨hâ‚, hâ‚‚âŸ©

@[refl] lemma is_Theta_refl (f : Î± â†’ E) (l : filter Î±) : f =Î˜[l] f := âŸ¨is_O_refl _ _, is_O_refl _ _âŸ©
lemma is_Theta_rfl : f =Î˜[l] f := is_Theta_refl _ _
@[symm] lemma is_Theta.symm (h : f =Î˜[l] g) : g =Î˜[l] f := h.symm

lemma is_Theta_comm : f =Î˜[l] g â†” g =Î˜[l] f := âŸ¨Î» h, h.symm, Î» h, h.symmâŸ©

@[trans] lemma is_Theta.trans {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =Î˜[l] g)
  (hâ‚‚ : g =Î˜[l] k) : f =Î˜[l] k :=
âŸ¨hâ‚.1.trans hâ‚‚.1, hâ‚‚.2.trans hâ‚.2âŸ©

@[trans] lemma is_O.trans_is_Theta {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =O[l] g)
  (hâ‚‚ : g =Î˜[l] k) : f =O[l] k :=
hâ‚.trans hâ‚‚.1

@[trans] lemma is_Theta.trans_is_O {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =Î˜[l] g)
  (hâ‚‚ : g =O[l] k) : f =O[l] k :=
hâ‚.1.trans hâ‚‚

@[trans] lemma is_o.trans_is_Theta {f : Î± â†’ E} {g : Î± â†’ F} {k : Î± â†’ G'} (hâ‚ : f =o[l] g)
  (hâ‚‚ : g =Î˜[l] k) : f =o[l] k :=
hâ‚.trans_is_O hâ‚‚.1

@[trans] lemma is_Theta.trans_is_o {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =Î˜[l] g)
  (hâ‚‚ : g =o[l] k) : f =o[l] k :=
hâ‚.1.trans_is_o hâ‚‚

@[trans] lemma is_Theta.trans_eventually_eq {f : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} (h : f =Î˜[l] gâ‚)
  (hg : gâ‚ =á¶ [l] gâ‚‚) : f =Î˜[l] gâ‚‚ :=
âŸ¨h.1.trans_eventually_eq hg, hg.symm.trans_is_O h.2âŸ©

@[trans] lemma _root_.filter.eventually_eq.trans_is_Theta {fâ‚ fâ‚‚ : Î± â†’ E} {g : Î± â†’ F}
  (hf : fâ‚ =á¶ [l] fâ‚‚) (h : fâ‚‚ =Î˜[l] g) : fâ‚ =Î˜[l] g :=
âŸ¨hf.trans_is_O h.1, h.2.trans_eventually_eq hf.symmâŸ©

@[simp] lemma is_Theta_norm_left : (Î» x, â€–f' xâ€–) =Î˜[l] g â†” f' =Î˜[l] g := by simp [is_Theta]
@[simp] lemma is_Theta_norm_right : f =Î˜[l] (Î» x, â€–g' xâ€–) â†” f =Î˜[l] g' := by simp [is_Theta]

alias is_Theta_norm_left â†” is_Theta.of_norm_left is_Theta.norm_left
alias is_Theta_norm_right â†” is_Theta.of_norm_right is_Theta.norm_right

lemma is_Theta_of_norm_eventually_eq (h : (Î» x, â€–f xâ€–) =á¶ [l] (Î» x, â€–g xâ€–)) : f =Î˜[l] g :=
âŸ¨is_O.of_bound 1 $ by simpa only [one_mul] using h.le,
  is_O.of_bound 1 $ by simpa only [one_mul] using h.symm.leâŸ©

lemma is_Theta_of_norm_eventually_eq' {g : Î± â†’ â„} (h : (Î» x, â€–f' xâ€–) =á¶ [l] g) : f' =Î˜[l] g :=
is_Theta_of_norm_eventually_eq $ h.mono $ Î» x hx, by simp only [â† hx, norm_norm]

lemma is_Theta.is_o_congr_left (h : f' =Î˜[l] g') : f' =o[l] k â†” g' =o[l] k :=
âŸ¨h.symm.trans_is_o, h.trans_is_oâŸ©

lemma is_Theta.is_o_congr_right (h : g' =Î˜[l] k') : f =o[l] g' â†” f =o[l] k' :=
âŸ¨Î» H, H.trans_is_Theta h, Î» H, H.trans_is_Theta h.symmâŸ©

lemma is_Theta.is_O_congr_left (h : f' =Î˜[l] g') : f' =O[l] k â†” g' =O[l] k :=
âŸ¨h.symm.trans_is_O, h.trans_is_OâŸ©

lemma is_Theta.is_O_congr_right (h : g' =Î˜[l] k') : f =O[l] g' â†” f =O[l] k' :=
âŸ¨Î» H, H.trans_is_Theta h, Î» H, H.trans_is_Theta h.symmâŸ©

lemma is_Theta.mono (h : f =Î˜[l] g) (hl : l' â‰¤ l) : f =Î˜[l'] g := âŸ¨h.1.mono hl, h.2.mono hlâŸ©

lemma is_Theta.sup (h : f' =Î˜[l] g') (h' : f' =Î˜[l'] g') : f' =Î˜[l âŠ” l'] g' :=
âŸ¨h.1.sup h'.1, h.2.sup h'.2âŸ©

@[simp] lemma is_Theta_sup : f' =Î˜[l âŠ” l'] g' â†” f' =Î˜[l] g' âˆ§ f' =Î˜[l'] g' :=
âŸ¨Î» h, âŸ¨h.mono le_sup_left, h.mono le_sup_rightâŸ©, Î» h, h.1.sup h.2âŸ©

lemma is_Theta.eq_zero_iff (h : f'' =Î˜[l] g'') : âˆ€á¶  x in l, f'' x = 0 â†” g'' x = 0 :=
h.1.eq_zero_imp.mp $ h.2.eq_zero_imp.mono $ Î» x, iff.intro

lemma is_Theta.tendsto_zero_iff (h : f'' =Î˜[l] g'') : tendsto f'' l (ğ“ 0) â†” tendsto g'' l (ğ“ 0) :=
by simp only [â† is_o_one_iff â„, h.is_o_congr_left]

lemma is_Theta.tendsto_norm_at_top_iff (h : f' =Î˜[l] g') :
  tendsto (norm âˆ˜ f') l at_top â†” tendsto (norm âˆ˜ g') l at_top :=
by simp only [â† is_o_const_left_of_ne (one_ne_zero' â„), h.is_o_congr_right]

lemma is_Theta.is_bounded_under_le_iff (h : f' =Î˜[l] g') :
  is_bounded_under (â‰¤) l (norm âˆ˜ f') â†” is_bounded_under (â‰¤) l (norm âˆ˜ g') :=
by simp only [â† is_O_const_of_ne (one_ne_zero' â„), h.is_O_congr_left]

lemma is_Theta.smul [normed_space ğ•œ E'] [normed_space ğ•œ' F'] {fâ‚ : Î± â†’ ğ•œ} {fâ‚‚ : Î± â†’ ğ•œ'}
  {gâ‚ : Î± â†’ E'} {gâ‚‚ : Î± â†’ F'} (hf : fâ‚ =Î˜[l] fâ‚‚) (hg : gâ‚ =Î˜[l] gâ‚‚) :
  (Î» x, fâ‚ x â€¢ gâ‚ x) =Î˜[l] (Î» x, fâ‚‚ x â€¢ gâ‚‚ x) :=
âŸ¨hf.1.smul hg.1, hf.2.smul hg.2âŸ©

lemma is_Theta.mul {fâ‚ fâ‚‚ : Î± â†’ ğ•œ} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ'} (hâ‚ : fâ‚ =Î˜[l] gâ‚) (hâ‚‚ : fâ‚‚ =Î˜[l] gâ‚‚) :
  (Î» x, fâ‚ x * fâ‚‚ x) =Î˜[l] (Î» x, gâ‚ x * gâ‚‚ x) :=
hâ‚.smul hâ‚‚

lemma is_Theta.inv {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : f =Î˜[l] g) : (Î» x, (f x)â»Â¹) =Î˜[l] (Î» x, (g x)â»Â¹) :=
âŸ¨h.2.inv_rev h.1.eq_zero_imp, h.1.inv_rev h.2.eq_zero_impâŸ©

@[simp] lemma is_Theta_inv {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} :
  (Î» x, (f x)â»Â¹) =Î˜[l] (Î» x, (g x)â»Â¹) â†” f =Î˜[l] g :=
âŸ¨Î» h, by simpa only [inv_inv] using h.inv, is_Theta.invâŸ©

lemma is_Theta.div {fâ‚ fâ‚‚ : Î± â†’ ğ•œ} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ'} (hâ‚ : fâ‚ =Î˜[l] gâ‚) (hâ‚‚ : fâ‚‚ =Î˜[l] gâ‚‚) :
  (Î» x, fâ‚ x / fâ‚‚ x) =Î˜[l] (Î» x, gâ‚ x / gâ‚‚ x) :=
by simpa only [div_eq_mul_inv] using hâ‚.mul hâ‚‚.inv

lemma is_Theta.pow {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : f =Î˜[l] g) (n : â„•) :
  (Î» x, (f x) ^ n) =Î˜[l] (Î» x, (g x) ^ n) :=
âŸ¨h.1.pow n, h.2.pow nâŸ©

lemma is_Theta.zpow {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : f =Î˜[l] g) (n : â„¤) :
  (Î» x, (f x) ^ n) =Î˜[l] (Î» x, (g x) ^ n) :=
begin
  cases n,
  { simpa only [zpow_of_nat] using h.pow _ },
  { simpa only [zpow_neg_succ_of_nat] using (h.pow _).inv }
end

lemma is_Theta_const_const {câ‚ : E''} {câ‚‚ : F''} (hâ‚ : câ‚ â‰  0) (hâ‚‚ : câ‚‚ â‰  0) :
  (Î» x : Î±, câ‚) =Î˜[l] (Î» x, câ‚‚) :=
âŸ¨is_O_const_const _ hâ‚‚ _, is_O_const_const _ hâ‚ _âŸ©

@[simp] lemma is_Theta_const_const_iff [ne_bot l] {câ‚ : E''} {câ‚‚ : F''} :
  (Î» x : Î±, câ‚) =Î˜[l] (Î» x, câ‚‚) â†” (câ‚ = 0 â†” câ‚‚ = 0) :=
by simpa only [is_Theta, is_O_const_const_iff, â† iff_def] using iff.comm

@[simp] lemma is_Theta_zero_left : (Î» x, (0 : E')) =Î˜[l] g'' â†” g'' =á¶ [l] 0 :=
by simp only [is_Theta, is_O_zero, is_O_zero_right_iff, true_and]

@[simp] lemma is_Theta_zero_right : f'' =Î˜[l] (Î» x, (0 : F')) â†” f'' =á¶ [l] 0 :=
is_Theta_comm.trans is_Theta_zero_left

lemma is_Theta_const_smul_left [normed_space ğ•œ E'] {c : ğ•œ} (hc : c â‰  0) :
  (Î» x, c â€¢ f' x) =Î˜[l] g â†” f' =Î˜[l] g :=
and_congr (is_O_const_smul_left hc) (is_O_const_smul_right hc)

alias is_Theta_const_smul_left â†” is_Theta.of_const_smul_left is_Theta.const_smul_left

lemma is_Theta_const_smul_right [normed_space ğ•œ F'] {c : ğ•œ} (hc : c â‰  0) :
  f =Î˜[l] (Î» x, c â€¢ g' x) â†” f =Î˜[l] g' :=
and_congr (is_O_const_smul_right hc) (is_O_const_smul_left hc)

alias is_Theta_const_smul_right â†” is_Theta.of_const_smul_right is_Theta.const_smul_right

lemma is_Theta_const_mul_left {c : ğ•œ} {f : Î± â†’ ğ•œ} (hc : c â‰  0) :
  (Î» x, c * f x) =Î˜[l] g â†” f =Î˜[l] g :=
by simpa only [â† smul_eq_mul] using is_Theta_const_smul_left hc

alias is_Theta_const_mul_left â†” is_Theta.of_const_mul_left is_Theta.const_mul_left

lemma is_Theta_const_mul_right {c : ğ•œ} {g : Î± â†’ ğ•œ} (hc : c â‰  0) :
  f =Î˜[l] (Î» x, c * g x) â†” f =Î˜[l] g :=
by simpa only [â† smul_eq_mul] using is_Theta_const_smul_right hc

alias is_Theta_const_mul_right â†” is_Theta.of_const_mul_right is_Theta.const_mul_right

end asymptotics
