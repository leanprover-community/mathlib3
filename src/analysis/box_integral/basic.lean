/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.box_integral.partition.filter
import analysis.box_integral.partition.measure
import topology.uniform_space.compact_separated

/-!
# Integrals of Riemann, Henstock-Kurzweil, and McShane

In this file we define the integral of a function over a box in `â„â¿. The same definition works for
Riemann, Henstock-Kurzweil, and McShane integrals.

As usual, we represent `â„â¿` as the type of functions `Î¹ â†’ â„` for some finite type `Î¹`. A rectangular
box `(l, u]` in `â„â¿` is defined to be the set `{x : Î¹ â†’ â„ | âˆ€ i, l i < x i âˆ§ x i â‰¤ u i}`, see
`box_integral.box`.

Let `vol` be a box-additive function on boxes in `â„â¿` with codomain `E â†’L[â„] F`. Given a function
`f : â„â¿ â†’ E`, a box `I` and a tagged partition `Ï€` of this box, the *integral sum* of `f` over `Ï€`
with respect to the volume `vol` is the sum of `vol J (f (Ï€.tag J))` over all boxes of `Ï€`. Here
`Ï€.tag J` is the point (tag) in `â„â¿` associated with the box `J`.

The integral is defined as the limit of integral sums along a filter. Different filters correspond
to different integration theories. In order to avoid code duplication, all our definitions and
theorems take an argument `l : box_integral.integration_params`. This is a type that holds three
boolean values, and encodes eight filters including those corresponding to Riemann,
Henstock-Kurzweil, and McShane integrals.

Following the design of infinite sums (see `has_sum` and `tsum`), we define a predicate
`box_integral.has_integral` and a function `box_integral.integral` that returns a vector satisfying
the predicate or zero if the function is not integrable.

Then we prove some basic properties of box integrals (linearity, a formula for the integral of a
constant). We also prove a version of the Henstock-Sacks inequality (see
`box_integral.integrable.dist_integral_sum_le_of_mem_base_set` and
`box_integral.integrable.dist_integral_sum_sum_integral_le_of_mem_base_set_of_Union_eq`), prove
integrability of continuous functions, and provide a criterion for integrability w.r.t. a
non-Riemann filter (e.g., Henstock-Kurzweil and McShane).

## Notation

- `â„â¿`: local notation for `Î¹ â†’ â„`

## Tags

integral
-/

open_locale big_operators classical topological_space nnreal filter uniformity box_integral
open set finset function filter metric box_integral.integration_params

noncomputable theory

namespace box_integral

universes u v w

variables {Î¹ : Type u} {E : Type v} {F : Type w} [normed_group E] [normed_space â„ E]
  [normed_group F] [normed_space â„ F] {I J : box Î¹} {Ï€ : tagged_prepartition I}

open tagged_prepartition

local notation `â„â¿` := Î¹ â†’ â„

/-!
### Integral sum and its basic properties
-/

/-- The integral sum of `f : â„â¿ â†’ E` over a tagged prepartition `Ï€` w.r.t. box-additive volume `vol`
with codomain `E â†’L[â„] F` is the sum of `vol J (f (Ï€.tag J))` over all boxes of `Ï€`. -/
def integral_sum (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F)) (Ï€ : tagged_prepartition I) : F :=
âˆ‘ J in Ï€.boxes, vol J (f (Ï€.tag J))

lemma integral_sum_bUnion_tagged (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F)) (Ï€ : prepartition I)
  (Ï€i : Î  J, tagged_prepartition J) :
  integral_sum f vol (Ï€.bUnion_tagged Ï€i) = âˆ‘ J in Ï€.boxes, integral_sum f vol (Ï€i J) :=
begin
  refine (Ï€.sum_bUnion_boxes _ _).trans (sum_congr rfl $ Î» J hJ, sum_congr rfl $ Î» J' hJ', _),
  rw Ï€.tag_bUnion_tagged hJ hJ'
end

lemma integral_sum_bUnion_partition (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  (Ï€ : tagged_prepartition I) (Ï€i : Î  J, prepartition J) (hÏ€i : âˆ€ J âˆˆ Ï€, (Ï€i J).is_partition) :
  integral_sum f vol (Ï€.bUnion_prepartition Ï€i) = integral_sum f vol Ï€ :=
begin
  refine (Ï€.to_prepartition.sum_bUnion_boxes _ _).trans (sum_congr rfl $ Î» J hJ, _),
  calc âˆ‘ J' in (Ï€i J).boxes, vol J' (f (Ï€.tag $ Ï€.to_prepartition.bUnion_index Ï€i J'))
      = âˆ‘ J' in (Ï€i J).boxes, vol J' (f (Ï€.tag J)) :
    sum_congr rfl (Î» J' hJ', by rw [prepartition.bUnion_index_of_mem _ hJ hJ'])
  ... = vol J (f (Ï€.tag J)) :
    (vol.map âŸ¨Î» g : E â†’L[â„] F, g (f (Ï€.tag J)), rfl, Î» _ _, rflâŸ©).sum_partition_boxes
      le_top (hÏ€i J hJ)
end

lemma integral_sum_inf_partition (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  (Ï€ : tagged_prepartition I) {Ï€' : prepartition I} (h : Ï€'.is_partition) :
  integral_sum f vol (Ï€.inf_prepartition Ï€') = integral_sum f vol Ï€ :=
integral_sum_bUnion_partition f vol  Ï€ _ $ Î» J hJ, h.restrict (prepartition.le_of_mem _ hJ)

lemma integral_sum_fiberwise {Î±} (g : box Î¹ â†’ Î±) (f : â„â¿ â†’ E)
  (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F)) (Ï€ : tagged_prepartition I) :
  âˆ‘ y in Ï€.boxes.image g, integral_sum f vol (Ï€.filter (Î» x, g x = y)) = integral_sum f vol Ï€ :=
Ï€.to_prepartition.sum_fiberwise g (Î» J, vol J (f $ Ï€.tag J))

lemma integral_sum_sub_partitions (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  {Ï€â‚ Ï€â‚‚ : tagged_prepartition I} (hâ‚ : Ï€â‚.is_partition) (hâ‚‚ : Ï€â‚‚.is_partition) :
  integral_sum f vol Ï€â‚ - integral_sum f vol Ï€â‚‚ =
    âˆ‘ J in (Ï€â‚.to_prepartition âŠ“ Ï€â‚‚.to_prepartition).boxes,
      (vol J (f $ (Ï€â‚.inf_prepartition Ï€â‚‚.to_prepartition).tag J) -
        vol J (f $ (Ï€â‚‚.inf_prepartition Ï€â‚.to_prepartition).tag J)) :=
begin
  rw [â† integral_sum_inf_partition f vol Ï€â‚ hâ‚‚,
    â† integral_sum_inf_partition f vol Ï€â‚‚ hâ‚, integral_sum, integral_sum,
    finset.sum_sub_distrib],
  simp only [inf_prepartition_to_prepartition, inf_comm]
end

@[simp] lemma integral_sum_disj_union (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  {Ï€â‚ Ï€â‚‚ : tagged_prepartition I} (h : disjoint Ï€â‚.Union Ï€â‚‚.Union) :
  integral_sum f vol (Ï€â‚.disj_union Ï€â‚‚ h) = integral_sum f vol Ï€â‚ + integral_sum f vol Ï€â‚‚ :=
begin
  refine (prepartition.sum_disj_union_boxes h _).trans
    (congr_arg2 (+) (sum_congr rfl $ Î» J hJ, _) (sum_congr rfl $ Î» J hJ, _)),
  { rw disj_union_tag_of_mem_left _ hJ },
  { rw disj_union_tag_of_mem_right _ hJ }
end

@[simp] lemma integral_sum_add (f g : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  (Ï€ : tagged_prepartition I) :
  integral_sum (f + g) vol Ï€ = integral_sum f vol Ï€ + integral_sum g vol Ï€ :=
by simp only [integral_sum, pi.add_apply, (vol _).map_add, finset.sum_add_distrib]

@[simp] lemma integral_sum_neg (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  (Ï€ : tagged_prepartition I) :
  integral_sum (-f) vol Ï€ = -integral_sum f vol Ï€ :=
by simp only [integral_sum, pi.neg_apply, (vol _).map_neg, finset.sum_neg_distrib]

@[simp] lemma integral_sum_smul (c : â„) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  (Ï€ : tagged_prepartition I) :
  integral_sum (c â€¢ f) vol Ï€ = c â€¢ integral_sum f vol Ï€ :=
by simp only [integral_sum, finset.smul_sum, pi.smul_apply, continuous_linear_map.map_smul]

variables [fintype Î¹]

/-!
### Basic integrability theory
-/

/-- The predicate `has_integral I l f vol y` says that `y` is the integral of `f` over `I` along `l`
w.r.t. volume `vol`. This means that integral sums of `f` tend to `ğ“ y` along
`box_integral.integration_params.to_filter_Union I âŠ¤`. -/
def has_integral (I : box Î¹) (l : integration_params) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F))
  (y : F) : Prop :=
tendsto (integral_sum f vol) (l.to_filter_Union I âŠ¤) (ğ“ y)

/-- A function is integrable if there exists a vector that satisfies the `has_integral`
predicate. -/
def integrable (I : box Î¹) (l : integration_params) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F)) :=
âˆƒ y, has_integral I l f vol y

/-- The integral of a function `f` over a box `I` along a filter `l` w.r.t. a volume `vol`.  Returns
zero on non-integrable functions. -/
def integral (I : box Î¹) (l : integration_params) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F)) :=
if h : integrable I l f vol then h.some else 0

variables {l : integration_params} {f g : â„â¿ â†’ E} {vol : Î¹ â†’áµ‡áµƒ (E â†’L[â„] F)} {y y' : F}

/-- Reinterpret `box_integral.has_integral` as `filter.tendsto`, e.g., dot-notation theorems
that are shadowed in the `box_integral.has_integral` namespace. -/
lemma has_integral.tendsto (h : has_integral I l f vol y) :
  tendsto (integral_sum f vol) (l.to_filter_Union I âŠ¤) (ğ“ y) := h

/-- The `Îµ`-`Î´` definition of `box_integral.has_integral`. -/
lemma has_integral_iff : has_integral I l f vol y â†”
  âˆ€ Îµ > (0 : â„), âˆƒ r : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„), (âˆ€ c, l.r_cond (r c)) âˆ§
    âˆ€ c Ï€, l.mem_base_set I c (r c) Ï€ â†’ is_partition Ï€ â†’ dist (integral_sum f vol Ï€) y â‰¤ Îµ :=
((l.has_basis_to_filter_Union_top I).tendsto_iff nhds_basis_closed_ball).trans $
  by simp [@forall_swap â„â‰¥0 (tagged_prepartition I)]

/-- Quite often it is more natural to prove an estimate of the form `a * Îµ`, not `Îµ` in the RHS of
`box_integral.has_integral_iff`, so we provide this auxiliary lemma.  -/
lemma has_integral_of_mul (a : â„) (h : âˆ€ Îµ : â„, 0 < Îµ â†’
  âˆƒ r: â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„), (âˆ€ c, l.r_cond (r c)) âˆ§ âˆ€ c Ï€, l.mem_base_set I c (r c) Ï€ â†’
    is_partition Ï€ â†’ dist (integral_sum f vol Ï€) y â‰¤ a * Îµ) :
  has_integral I l f vol y :=
begin
  refine has_integral_iff.2 (Î» Îµ hÎµ, _),
  rcases exists_pos_mul_lt hÎµ a with âŸ¨Îµ', hÎµ', haâŸ©,
  rcases h Îµ' hÎµ' with âŸ¨r, hr, HâŸ©,
  exact âŸ¨r, hr, Î» c Ï€ hÏ€ hÏ€p, (H c Ï€ hÏ€ hÏ€p).trans ha.leâŸ©
end

lemma integrable_iff_cauchy [complete_space F] :
  integrable I l f vol â†” cauchy ((l.to_filter_Union I âŠ¤).map (integral_sum f vol)) :=
cauchy_map_iff_exists_tendsto.symm

/-- In a complete space, a function is integrable if and only if its integral sums form a Cauchy
net. Here we restate this fact in terms of `âˆ€ Îµ > 0, âˆƒ r, ...`. -/
lemma integrable_iff_cauchy_basis [complete_space F] :
  integrable I l f vol â†” âˆ€ Îµ > (0 : â„), âˆƒ r : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„), (âˆ€ c, l.r_cond (r c)) âˆ§
    âˆ€ câ‚ câ‚‚ Ï€â‚ Ï€â‚‚, l.mem_base_set I câ‚ (r câ‚) Ï€â‚ â†’ Ï€â‚.is_partition â†’ l.mem_base_set I câ‚‚ (r câ‚‚) Ï€â‚‚ â†’
      Ï€â‚‚.is_partition â†’ dist (integral_sum f vol Ï€â‚) (integral_sum f vol Ï€â‚‚) â‰¤ Îµ :=
begin
  rw [integrable_iff_cauchy, cauchy_map_iff',
    (l.has_basis_to_filter_Union_top _).prod_self.tendsto_iff uniformity_basis_dist_le],
  refine forall_congr (Î» Îµ, forall_congr $ Î» Îµ0, exists_congr $ Î» r, _),
  simp only [exists_prop, prod.forall, set.mem_Union, exists_imp_distrib,
    prod_mk_mem_set_prod_eq, and_imp, mem_inter_eq, mem_set_of_eq],
  exact and_congr iff.rfl âŸ¨Î» H câ‚ câ‚‚ Ï€â‚ Ï€â‚‚ hâ‚ hUâ‚ hâ‚‚ hUâ‚‚, H Ï€â‚ Ï€â‚‚ câ‚ hâ‚ hUâ‚ câ‚‚ hâ‚‚ hUâ‚‚,
    Î» H Ï€â‚ Ï€â‚‚ câ‚ hâ‚ hUâ‚ câ‚‚ hâ‚‚ hUâ‚‚, H câ‚ câ‚‚ Ï€â‚ Ï€â‚‚ hâ‚ hUâ‚ hâ‚‚ hUâ‚‚âŸ©
end

lemma has_integral.mono {lâ‚ lâ‚‚ : integration_params} (h : has_integral I lâ‚ f vol y)
  (hl : lâ‚‚ â‰¤ lâ‚) : has_integral I lâ‚‚ f vol y :=
h.mono_left $ integration_params.to_filter_Union_mono _ hl _

protected lemma integrable.has_integral (h : integrable I l f vol) :
  has_integral I l f vol (integral I l f vol) :=
by { rw [integral, dif_pos h], exact classical.some_spec h }

lemma integrable.mono {l'} (h : integrable I l f vol) (hle : l' â‰¤ l) : integrable I l' f vol :=
âŸ¨_, h.has_integral.mono hleâŸ©

lemma has_integral.unique (h : has_integral I l f vol y) (h' : has_integral I l f vol y') :
  y = y' :=
tendsto_nhds_unique h h'

lemma has_integral.integrable (h : has_integral I l f vol y) : integrable I l f vol := âŸ¨_, hâŸ©

lemma has_integral.integral_eq (h : has_integral I l f vol y) :
  integral I l f vol = y :=
h.integrable.has_integral.unique h

lemma has_integral.add (h : has_integral I l f vol y) (h' : has_integral I l g vol y') :
  has_integral I l (f + g) vol (y + y') :=
by simpa only [has_integral, â† integral_sum_add] using h.add h'

lemma integrable.add (hf : integrable I l f vol) (hg : integrable I l g vol) :
  integrable I l (f + g) vol :=
(hf.has_integral.add hg.has_integral).integrable

lemma integral_add (hf : integrable I l f vol) (hg : integrable I l g vol) :
  integral I l (f + g) vol = integral I l f vol + integral I l g vol :=
(hf.has_integral.add hg.has_integral).integral_eq

lemma has_integral.neg (hf : has_integral I l f vol y) : has_integral I l (-f) vol (-y) :=
by simpa only [has_integral, â† integral_sum_neg] using hf.neg

lemma integrable.neg (hf : integrable I l f vol) : integrable I l (-f) vol :=
hf.has_integral.neg.integrable

lemma integrable.of_neg (hf : integrable I l (-f) vol) : integrable I l f vol := neg_neg f â–¸ hf.neg

@[simp] lemma integrable_neg : integrable I l (-f) vol â†” integrable I l f vol :=
âŸ¨Î» h, h.of_neg, Î» h, h.negâŸ©

@[simp] lemma integral_neg : integral I l (-f) vol = -integral I l f vol :=
if h : integrable I l f vol then h.has_integral.neg.integral_eq
else by rw [integral, integral, dif_neg h, dif_neg (mt integrable.of_neg h), neg_zero]

lemma has_integral.sub (h : has_integral I l f vol y) (h' : has_integral I l g vol y') :
  has_integral I l (f - g) vol (y - y') :=
by simpa only [sub_eq_add_neg] using h.add h'.neg

lemma integrable.sub (hf : integrable I l f vol) (hg : integrable I l g vol) :
  integrable I l (f - g) vol :=
(hf.has_integral.sub hg.has_integral).integrable

lemma integral_sub (hf : integrable I l f vol) (hg : integrable I l g vol) :
  integral I l (f - g) vol = integral I l f vol - integral I l g vol :=
(hf.has_integral.sub hg.has_integral).integral_eq

lemma has_integral_const (c : E) : has_integral I l (Î» _, c) vol (vol I c) :=
tendsto_const_nhds.congr' $ (l.eventually_is_partition I).mono $ Î» Ï€ hÏ€,
  ((vol.map âŸ¨Î» g : E â†’L[â„] F, g c, rfl, Î» _ _, rflâŸ©).sum_partition_boxes le_top hÏ€).symm

@[simp] lemma integral_const (c : E) : integral I l (Î» _, c) vol = vol I c :=
(has_integral_const c).integral_eq

lemma integrable_const (c : E) : integrable I l (Î» _, c) vol :=
âŸ¨_, has_integral_const câŸ©

lemma has_integral_zero : has_integral I l (Î» _, (0:E)) vol 0 :=
by simpa only [â† (vol I).map_zero] using has_integral_const (0 : E)

lemma integrable_zero : integrable I l (Î» _, (0:E)) vol := âŸ¨0, has_integral_zeroâŸ©

lemma integral_zero : integral I l (Î» _, (0:E)) vol = 0 := has_integral_zero.integral_eq

lemma has_integral_sum {Î± : Type*} {s : finset Î±} {f : Î± â†’ â„â¿ â†’ E} {g : Î± â†’ F}
  (h : âˆ€ i âˆˆ s, has_integral I l (f i) vol (g i)) :
  has_integral I l (Î» x, âˆ‘ i in s, f i x) vol (âˆ‘ i in s, g i) :=
begin
  induction s using finset.induction_on with a s ha ihs, { simp [has_integral_zero] },
  simp only [finset.sum_insert ha], rw finset.forall_mem_insert at h,
  exact h.1.add (ihs h.2)
end

lemma has_integral.smul (hf : has_integral I l f vol y) (c : â„) :
  has_integral I l (c â€¢ f) vol (c â€¢ y) :=
by simpa only [has_integral, â† integral_sum_smul]
  using (tendsto_const_nhds : tendsto _ _ (ğ“ c)).smul hf

lemma integrable.smul (hf : integrable I l f vol) (c : â„) :
  integrable I l (c â€¢ f) vol :=
(hf.has_integral.smul c).integrable

lemma integrable.of_smul {c : â„} (hf : integrable I l (c â€¢ f) vol) (hc : c â‰  0) :
  integrable I l f vol :=
by { convert hf.smul câ»Â¹, ext x, simp only [pi.smul_apply, inv_smul_smulâ‚€ hc] }

@[simp] lemma integral_smul (c : â„) : integral I l (Î» x, c â€¢ f x) vol = c â€¢ integral I l f vol :=
begin
  rcases eq_or_ne c 0 with rfl | hc, { simp only [zero_smul, integral_zero] },
  by_cases hf : integrable I l f vol,
  { exact (hf.has_integral.smul c).integral_eq },
  { have : Â¬integrable I l (Î» x, c â€¢ f x) vol, from mt (Î» h, h.of_smul hc) hf,
    rw [integral, integral, dif_neg hf, dif_neg this, smul_zero] }
end

open measure_theory

/-- The integral of a nonnegative function w.r.t. a volume generated by a locally-finite measure is
nonnegative. -/
lemma integral_nonneg {g : â„â¿ â†’ â„} (hg : âˆ€ x âˆˆ I.Icc, 0 â‰¤ g x)
  (Î¼ : measure â„â¿) [is_locally_finite_measure Î¼] :
  0 â‰¤ integral I l g Î¼.to_box_additive.to_smul :=
begin
  by_cases hgi : integrable I l g Î¼.to_box_additive.to_smul,
  { refine ge_of_tendsto' hgi.has_integral (Î» Ï€, sum_nonneg $ Î» J hJ, _),
    exact mul_nonneg ennreal.to_real_nonneg (hg _ $ Ï€.tag_mem_Icc _) },
  { rw [integral, dif_neg hgi] }
end

/-- If `âˆ¥f xâˆ¥ â‰¤ g x` on `[l, u]` and `g` is integrable, then the norm of the integral of `f` is less
than or equal to the integral of `g`. -/
lemma norm_integral_le_of_norm_le {g : â„â¿ â†’ â„} (hle : âˆ€ x âˆˆ I.Icc, âˆ¥f xâˆ¥ â‰¤ g x)
  (Î¼ : measure â„â¿) [is_locally_finite_measure Î¼]
  (hg : integrable I l g Î¼.to_box_additive.to_smul) :
  âˆ¥(integral I l f Î¼.to_box_additive.to_smul : E)âˆ¥ â‰¤
    integral I l g Î¼.to_box_additive.to_smul :=
begin
  by_cases hfi : integrable.{u v v} I l f Î¼.to_box_additive.to_smul,
  { refine le_of_tendsto_of_tendsto' hfi.has_integral.norm hg.has_integral (Î» Ï€, _),
    refine norm_sum_le_of_le _ (Î» J hJ, _),
    simp only [box_additive_map.to_smul_apply, norm_smul, smul_eq_mul, real.norm_eq_abs,
      Î¼.to_box_additive_apply, abs_of_nonneg ennreal.to_real_nonneg],
    exact mul_le_mul_of_nonneg_left (hle _ $ Ï€.tag_mem_Icc _) ennreal.to_real_nonneg },
  { rw [integral, dif_neg hfi, norm_zero],
    exact integral_nonneg (Î» x hx, (norm_nonneg _).trans (hle x hx)) Î¼ }
end

lemma norm_integral_le_of_le_const {c : â„} (hc : âˆ€ x âˆˆ I.Icc, âˆ¥f xâˆ¥ â‰¤ c)
  (Î¼ : measure â„â¿) [is_locally_finite_measure Î¼] :
  âˆ¥(integral I l f Î¼.to_box_additive.to_smul : E)âˆ¥ â‰¤ (Î¼ I).to_real * c :=
by simpa only [integral_const]
  using norm_integral_le_of_norm_le hc Î¼ (integrable_const c)

/-!
# Henstock-Sacks inequality and integrability on subboxes

Henstock-Sacks inequality for Henstock-Kurzweil integral says the following. Let `f` be a function
integrable on a box `I`; let `r : â„â¿ â†’ (0, âˆ)` be a function such that for any tagged partition of
`I` subordinate to `r`, the integral sum over this partition is `Îµ`-close to the integral. Then for
any tagged prepartition (i.e. a finite collections of pairwise disjoint subboxes of `I` with tagged
points) `Ï€`, the integral sum over `Ï€` differs from the integral of `f` over the part of `I` covered
by `Ï€` by at most `Îµ`. The actual statement in the library is a bit more complicated to make it work
for any `box_integral.integration_params`. We formalize several versions of this inequality in
`box_integral.integrable.dist_integral_sum_le_of_mem_base_set`,
`box_integral.integrable.dist_integral_sum_sum_integral_le_of_mem_base_set_of_Union_eq`, and
`box_integral.integrable.dist_integral_sum_sum_integral_le_of_mem_base_set`.

Instead of using predicate assumptions on `r`, we define
`box_integral.integrable.convergence_r (h : integrable I l f vol) (Îµ : â„) (c : â„â‰¥0) : â„â¿ â†’ (0, âˆ)`
to be a function `r` such that

- if `l.bRiemann`, then `r` is a constant;
- if `Îµ > 0`, then for any tagged partition `Ï€` of `I` subordinate to `r` (more precisely,
  satisfying the predicate `l.mem_base_set I c r`), the integral sum of `f` over `Ï€` differs from
  the integral of `f` over `I` by at most `Îµ`.

The proof is mostly based on
[Russel A. Gordon, *The integrals of Lebesgue, Denjoy, Perron, and Henstock*][Gordon55].

-/

namespace integrable

/-- If `Îµ > 0`, then `box_integral.integrable.convergence_r` is a function `r : â„â‰¥0 â†’ â„â¿ â†’ (0, âˆ)`
such that for every `c : â„â‰¥0`, for every tagged partition `Ï€` subordinate to `r` (and satisfying
additional distortion estimates if `box_integral.integration_params.bDistortion l = tt`), the
corresponding integral sum is `Îµ`-close to the integral.

If `box.integral.integration_params.bRiemann = tt`, then `r c x` does not depend on `x`. If `Îµ â‰¤ 0`,
then we use `r c x = 1`.  -/
def convergence_r (h : integrable I l f vol) (Îµ : â„) : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„) :=
if hÎµ : 0 < Îµ then (has_integral_iff.1 h.has_integral Îµ hÎµ).some
else Î» _ _, âŸ¨1, set.mem_Ioi.2 zero_lt_oneâŸ©

variables {c câ‚ câ‚‚ : â„â‰¥0} {Îµ Îµâ‚ Îµâ‚‚ : â„} {Ï€â‚ Ï€â‚‚ : tagged_prepartition I}

lemma convergence_r_cond (h : integrable I l f vol) (Îµ : â„) (c : â„â‰¥0) :
  l.r_cond (h.convergence_r Îµ c) :=
begin
  rw convergence_r, split_ifs with hâ‚€,
  exacts [(has_integral_iff.1 h.has_integral Îµ hâ‚€).some_spec.1 _, Î» _ x, rfl]
end

lemma dist_integral_sum_integral_le_of_mem_base_set (h : integrable I l f vol) (hâ‚€ : 0 < Îµ)
  (hÏ€ : l.mem_base_set I c (h.convergence_r Îµ c) Ï€) (hÏ€p : Ï€.is_partition) :
  dist (integral_sum f vol Ï€) (integral I l f vol) â‰¤ Îµ :=
begin
  rw [convergence_r, dif_pos hâ‚€] at hÏ€,
  exact (has_integral_iff.1 h.has_integral Îµ hâ‚€).some_spec.2 c _ hÏ€ hÏ€p
end

/-- **Henstock-Sacks inequality**. Let `râ‚ râ‚‚ : â„â¿ â†’ (0, âˆ)` be function such that for any tagged
*partition* of `I` subordinate to `râ‚–`, `k=1,2`, the integral sum of `f` over this partition differs
from the integral of `f` by at most `Îµâ‚–`. Then for any two tagged *prepartition* `Ï€â‚ Ï€â‚‚` subordinate
to `râ‚` and `râ‚‚` respectively and covering the same part of `I`, the integral sums of `f` over these
prepartitions differ from each other by at most `Îµâ‚ + Îµâ‚‚`.

The actual statement

- uses `box_integral.integrable.convergence_r` instead of a predicate assumption on `r`;
- uses `box_integral.integration_params.mem_base_set` instead of â€œsubordinate to `r`â€ to
  account for additional requirements like being a Henstock partition or having a bounded
  distortion.

See also `box_integral.integrable.dist_integral_sum_sum_integral_le_of_mem_base_set_of_Union_eq` and
`box_integral.integrable.dist_integral_sum_sum_integral_le_of_mem_base_set`.
-/
lemma dist_integral_sum_le_of_mem_base_set (h : integrable I l f vol)
  (hposâ‚ : 0 < Îµâ‚) (hposâ‚‚ : 0 < Îµâ‚‚) (hâ‚ : l.mem_base_set I câ‚ (h.convergence_r Îµâ‚ câ‚) Ï€â‚)
  (hâ‚‚ : l.mem_base_set I câ‚‚ (h.convergence_r Îµâ‚‚ câ‚‚) Ï€â‚‚) (HU : Ï€â‚.Union = Ï€â‚‚.Union) :
  dist (integral_sum f vol Ï€â‚) (integral_sum f vol Ï€â‚‚) â‰¤ Îµâ‚ + Îµâ‚‚ :=
begin
  rcases hâ‚.exists_common_compl hâ‚‚ HU with âŸ¨Ï€, hÏ€U, hÏ€câ‚, hÏ€câ‚‚âŸ©,
  set r : â„â¿ â†’ Ioi (0 : â„) := Î» x, min (h.convergence_r Îµâ‚ câ‚ x) (h.convergence_r Îµâ‚‚ câ‚‚ x),
  have hr : l.r_cond r := (h.convergence_r_cond _ câ‚).min (h.convergence_r_cond _ câ‚‚),
  set Ï€r := Ï€.to_subordinate r,
  have Hâ‚ : dist (integral_sum f vol (Ï€â‚.union_compl_to_subordinate Ï€ hÏ€U r))
    (integral I l f vol) â‰¤ Îµâ‚,
  from h.dist_integral_sum_integral_le_of_mem_base_set hposâ‚
    (hâ‚.union_compl_to_subordinate (Î» _ _, min_le_left _ _) hÏ€U hÏ€câ‚)
    (is_partition_union_compl_to_subordinate _ _ _ _),
  rw HU at hÏ€U,
  have Hâ‚‚ : dist (integral_sum f vol (Ï€â‚‚.union_compl_to_subordinate Ï€ hÏ€U r))
    (integral I l f vol) â‰¤ Îµâ‚‚,
  from h.dist_integral_sum_integral_le_of_mem_base_set hposâ‚‚
    (hâ‚‚.union_compl_to_subordinate (Î» _ _, min_le_right _ _) hÏ€U hÏ€câ‚‚)
    (is_partition_union_compl_to_subordinate _ _ _ _),
  simpa [union_compl_to_subordinate] using (dist_triangle_right _ _ _).trans (add_le_add Hâ‚ Hâ‚‚)
end

/-- If `f` is integrable on `I` along `l`, then for two sufficiently fine tagged prepartitions
(in the sense of the filter `box_integral.integration_params.to_filter l I`) such that they cover
the same part of `I`, the integral sums of `f` over `Ï€â‚` and `Ï€â‚‚` are very close to each other.  -/
lemma tendsto_integral_sum_to_filter_prod_self_inf_Union_eq_uniformity (h : integrable I l f vol) :
  tendsto
    (Î» Ï€ : tagged_prepartition I Ã— tagged_prepartition I,
      (integral_sum f vol Ï€.1, integral_sum f vol Ï€.2))
    ((l.to_filter I Ã—á¶  l.to_filter I) âŠ“ ğ“Ÿ {Ï€ | Ï€.1.Union = Ï€.2.Union}) (ğ“¤ F) :=
begin
  refine (((l.has_basis_to_filter I).prod_self.inf_principal _).tendsto_iff
    uniformity_basis_dist_le).2 (Î» Îµ Îµ0, _),
  replace Îµ0 := half_pos Îµ0,
  use [h.convergence_r (Îµ / 2), h.convergence_r_cond (Îµ / 2)], rintro âŸ¨Ï€â‚, Ï€â‚‚âŸ© âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, hUâŸ©,
  rw â† add_halves Îµ,
  exact h.dist_integral_sum_le_of_mem_base_set Îµ0 Îµ0 hâ‚.some_spec hâ‚‚.some_spec hU
end

/-- If `f` is integrable on a box `I` along `l`, then for any fixed subset `s` of `I` that can be
represented as a finite union of boxes, the integral sums of `f` over tagged prepartitions that
cover exactly `s` form a Cauchy â€œsequenceâ€ along `l`. -/
lemma cauchy_map_integral_sum_to_filter_Union (h : integrable I l f vol) (Ï€â‚€ : prepartition I) :
  cauchy ((l.to_filter_Union I Ï€â‚€).map (integral_sum f vol)) :=
begin
  refine âŸ¨infer_instance, _âŸ©,
  rw [prod_map_map_eq, â† to_filter_inf_Union_eq, â† prod_inf_prod, prod_principal_principal],
  exact h.tendsto_integral_sum_to_filter_prod_self_inf_Union_eq_uniformity.mono_left
    (inf_le_inf_left _ $ principal_mono.2 $ Î» Ï€ h, h.1.trans h.2.symm)
end

variable [complete_space F]

lemma to_subbox_aux (h : integrable I l f vol) (hJ : J â‰¤ I) :
  âˆƒ y : F, has_integral J l f vol y âˆ§
    tendsto (integral_sum f vol) (l.to_filter_Union I (prepartition.single I J hJ)) (ğ“ y) :=
begin
  refine (cauchy_map_iff_exists_tendsto.1
    (h.cauchy_map_integral_sum_to_filter_Union (prepartition.single I J hJ))).imp (Î» y hy, âŸ¨_, hyâŸ©),
  convert hy.comp (l.tendsto_embed_box_to_filter_Union_top hJ) -- faster than `exact` here
end

/-- If `f` is integrable on a box `I`, then it is integrable on any subbox of `I`. -/
lemma to_subbox (h : integrable I l f vol) (hJ : J â‰¤ I) : integrable J l f vol :=
(h.to_subbox_aux hJ).imp $ Î» y, and.left

/-- If `f` is integrable on a box `I`, then integral sums of `f` over tagged prepartitions
that cover exactly a subbox `J â‰¤ I` tend to the integral of `f` over `J` along `l`. -/
lemma tendsto_integral_sum_to_filter_Union_single (h : integrable I l f vol) (hJ : J â‰¤ I) :
  tendsto (integral_sum f vol) (l.to_filter_Union I (prepartition.single I J hJ))
    (ğ“ $ integral J l f vol) :=
let âŸ¨y, hâ‚, hâ‚‚âŸ© := h.to_subbox_aux hJ in hâ‚.integral_eq.symm â–¸ hâ‚‚

/-- **Henstock-Sacks inequality**. Let `r : â„â¿ â†’ (0, âˆ)` be a function such that for any tagged
*partition* of `I` subordinate to `r`, the integral sum of `f` over this partition differs from the
integral of `f` by at most `Îµ`. Then for any tagged *prepartition* `Ï€` subordinate to `r`, the
integral sum of `f` over this prepartition differs from the integral of `f` over the part of `I`
covered by `Ï€` by at most `Îµ`.

The actual statement

- uses `box_integral.integrable.convergence_r` instead of a predicate assumption on `r`;
- uses `box_integral.integration_params.mem_base_set` instead of â€œsubordinate to `r`â€ to
  account for additional requirements like being a Henstock partition or having a bounded
  distortion;
- takes an extra argument `Ï€â‚€ : prepartition I` and an assumption `Ï€.Union = Ï€â‚€.Union` instead of
  using `Ï€.to_prepartition`.
-/
lemma dist_integral_sum_sum_integral_le_of_mem_base_set_of_Union_eq (h : integrable I l f vol)
  (h0 : 0 < Îµ) (hÏ€ : l.mem_base_set I c (h.convergence_r Îµ c) Ï€) {Ï€â‚€ : prepartition I}
  (hU : Ï€.Union = Ï€â‚€.Union) :
  dist (integral_sum f vol Ï€) (âˆ‘ J in Ï€â‚€.boxes, integral J l f vol) â‰¤ Îµ :=
begin
  /- Let us prove that the distance is less than or equal to `Îµ + Î´` for all positive `Î´`. -/
  refine le_of_forall_pos_le_add (Î» Î´ Î´0, _),
  /- First we choose some constants. -/
  set Î´' : â„ := Î´ / (Ï€â‚€.boxes.card + 1),
  have H0 : 0 < (Ï€â‚€.boxes.card + 1 : â„) := nat.cast_add_one_pos _,
  have Î´'0 : 0 < Î´' := div_pos Î´0 H0,
  set C := max Ï€â‚€.distortion Ï€â‚€.compl.distortion,
  /- Next we choose a tagged partition of each `J âˆˆ Ï€â‚€` such that the integral sum of `f` over this
  partition is `Î´'`-close to the integral of `f` over `J`. -/
  have : âˆ€ J âˆˆ Ï€â‚€, âˆƒ Ï€i : tagged_prepartition J, Ï€i.is_partition âˆ§
    dist (integral_sum f vol Ï€i) (integral J l f vol) â‰¤ Î´' âˆ§
    l.mem_base_set J C (h.convergence_r Î´' C) Ï€i,
  { intros J hJ,
    have Hle : J â‰¤ I := Ï€â‚€.le_of_mem hJ,
    have HJi : integrable J l f vol := h.to_subbox Hle,
    set r := Î» x, min (h.convergence_r Î´' C x) (HJi.convergence_r Î´' C x),
    have hr : l.r_cond r, from (h.convergence_r_cond _ C).min (HJi.convergence_r_cond _ C),
    have hJd : J.distortion â‰¤ C, from le_trans (finset.le_sup hJ) (le_max_left _ _),
    rcases l.exists_mem_base_set_is_partition J hJd r with âŸ¨Ï€J, hC, hpâŸ©,
    have hCâ‚ : l.mem_base_set J C (HJi.convergence_r Î´' C) Ï€J,
    { refine hC.mono J le_rfl le_rfl (Î» x hx, _), exact min_le_right _ _ },
    have hCâ‚‚ : l.mem_base_set J C (h.convergence_r Î´' C) Ï€J,
    { refine hC.mono J le_rfl le_rfl (Î» x hx, _), exact min_le_left _ _ },
    exact âŸ¨Ï€J, hp, HJi.dist_integral_sum_integral_le_of_mem_base_set Î´'0 hCâ‚ hp, hCâ‚‚âŸ© },
  /- Now we combine these tagged partitions into a tagged prepartition of `I` that covers the
  same part of `I` as `Ï€â‚€` and apply `box_integral.dist_integral_sum_le_of_mem_base_set` to
  `Ï€` and this prepartition. -/
  choose! Ï€i hÏ€ip hÏ€iÎ´' hÏ€iC,
  have : l.mem_base_set I C (h.convergence_r Î´' C) (Ï€â‚€.bUnion_tagged Ï€i),
    from bUnion_tagged_mem_base_set hÏ€iC hÏ€ip (Î» _, le_max_right _ _),
  have hU' : Ï€.Union = (Ï€â‚€.bUnion_tagged Ï€i).Union,
    from hU.trans (prepartition.Union_bUnion_partition _ hÏ€ip).symm,
  have := h.dist_integral_sum_le_of_mem_base_set h0 Î´'0 hÏ€ this hU',
  rw integral_sum_bUnion_tagged at this,
  calc dist (integral_sum f vol Ï€) (âˆ‘ J in Ï€â‚€.boxes, integral J l f vol)
      â‰¤ dist (integral_sum f vol Ï€) (âˆ‘ J in Ï€â‚€.boxes, integral_sum f vol (Ï€i J)) +
        dist (âˆ‘ J in Ï€â‚€.boxes, integral_sum f vol (Ï€i J)) (âˆ‘ J in Ï€â‚€.boxes, integral J l f vol) :
    dist_triangle _ _ _
  ... â‰¤ (Îµ + Î´') + âˆ‘ J in Ï€â‚€.boxes, Î´' : add_le_add this (dist_sum_sum_le_of_le _ hÏ€iÎ´')
  ... = Îµ + Î´ : by { field_simp [H0.ne'], ring }
end

/-- **Henstock-Sacks inequality**. Let `r : â„â¿ â†’ (0, âˆ)` be a function such that for any tagged
*partition* of `I` subordinate to `r`, the integral sum of `f` over this partition differs from the
integral of `f` by at most `Îµ`. Then for any tagged *prepartition* `Ï€` subordinate to `r`, the
integral sum of `f` over this prepartition differs from the integral of `f` over the part of `I`
covered by `Ï€` by at most `Îµ`.

The actual statement

- uses `box_integral.integrable.convergence_r` instead of a predicate assumption on `r`;
- uses `box_integral.integration_params.mem_base_set` instead of â€œsubordinate to `r`â€ to
  account for additional requirements like being a Henstock partition or having a bounded
  distortion;
-/
lemma dist_integral_sum_sum_integral_le_of_mem_base_set (h : integrable I l f vol)
  (h0 : 0 < Îµ) (hÏ€ : l.mem_base_set I c (h.convergence_r Îµ c) Ï€) :
  dist (integral_sum f vol Ï€) (âˆ‘ J in Ï€.boxes, integral J l f vol) â‰¤ Îµ :=
h.dist_integral_sum_sum_integral_le_of_mem_base_set_of_Union_eq h0 hÏ€ rfl

/-- Integral sum of `f` over a tagged prepartition `Ï€` such that `Ï€.Union = Ï€â‚€.Union` tends to the
sum of integrals of `f` over the boxes of `Ï€â‚€`. -/
lemma tendsto_integral_sum_sum_integral (h : integrable I l f vol) (Ï€â‚€ : prepartition I) :
  tendsto (integral_sum f vol) (l.to_filter_Union I Ï€â‚€) (ğ“ $ âˆ‘ J in Ï€â‚€.boxes, integral J l f vol) :=
begin
  refine ((l.has_basis_to_filter_Union I Ï€â‚€).tendsto_iff nhds_basis_closed_ball).2 (Î» Îµ Îµ0, _),
  refine âŸ¨h.convergence_r Îµ, h.convergence_r_cond Îµ, _âŸ©,
  simp only [mem_inter_eq, set.mem_Union, mem_set_of_eq],
  rintro Ï€ âŸ¨c, hc, hUâŸ©,
  exact h.dist_integral_sum_sum_integral_le_of_mem_base_set_of_Union_eq Îµ0 hc hU
end

/-- If `f` is integrable on `I`, then `Î» J, integral J l f vol` is box-additive on subboxes of `I`:
if `Ï€â‚`, `Ï€â‚‚` are two prepartitions of `I` covering the same part of `I`, then the sum of integrals
of `f` over the boxes of `Ï€â‚` is equal to the sum of integrals of `f` over the boxes of `Ï€â‚‚`.

See also `box_integral.integrable.to_box_additive` for a bundled version. -/
lemma sum_integral_congr (h : integrable I l f vol) {Ï€â‚ Ï€â‚‚ : prepartition I}
  (hU : Ï€â‚.Union = Ï€â‚‚.Union) :
  âˆ‘ J in Ï€â‚.boxes, integral J l f vol = âˆ‘ J in Ï€â‚‚.boxes, integral J l f vol :=
begin
  refine tendsto_nhds_unique (h.tendsto_integral_sum_sum_integral Ï€â‚) _,
  rw l.to_filter_Union_congr _ hU,
  exact h.tendsto_integral_sum_sum_integral Ï€â‚‚
end

/-- If `f` is integrable on `I`, then `Î» J, integral J l f vol` is box-additive on subboxes of `I`:
if `Ï€â‚`, `Ï€â‚‚` are two prepartitions of `I` covering the same part of `I`, then the sum of integrals
of `f` over the boxes of `Ï€â‚` is equal to the sum of integrals of `f` over the boxes of `Ï€â‚‚`.

See also `box_integral.integrable.sum_integral_congr` for an unbundled version. -/
@[simps] def to_box_additive (h : integrable I l f vol) : Î¹ â†’áµ‡áµƒ[I] F :=
{ to_fun := Î» J, integral J l f vol,
  sum_partition_boxes' := Î» J hJ Ï€ hÏ€,
    begin
      replace hÏ€ := hÏ€.Union_eq, rw â† prepartition.Union_top at hÏ€,
      rw [(h.to_subbox (with_top.coe_le_coe.1 hJ)).sum_integral_congr hÏ€,
        prepartition.top_boxes, sum_singleton]
    end }

end integrable

open measure_theory

/-!
### Integrability conditions
-/

variable (l)

/-- A continuous function is box-integrable with respect to any locally finite measure.

This is true for any volume with bounded variation. -/
lemma integrable_of_continuous_on [complete_space E] {I : box Î¹} {f : â„â¿ â†’ E}
  (hc : continuous_on f I.Icc) (Î¼ : measure â„â¿) [is_locally_finite_measure Î¼] :
  integrable.{u v v} I l f Î¼.to_box_additive.to_smul :=
begin
  have huc := I.is_compact_Icc.uniform_continuous_on_of_continuous hc,
  rw metric.uniform_continuous_on_iff_le at huc,
  refine integrable_iff_cauchy_basis.2 (Î» Îµ Îµ0, _),
  rcases exists_pos_mul_lt Îµ0 (Î¼.to_box_additive I) with âŸ¨Îµ', Îµ0', hÎµâŸ©,
  rcases huc Îµ' Îµ0' with âŸ¨Î´, Î´0 : 0 < Î´, HÎ´âŸ©,
  refine âŸ¨Î» _ _, âŸ¨Î´ / 2, half_pos Î´0âŸ©, Î» _ _ _, rfl, Î» câ‚ câ‚‚ Ï€â‚ Ï€â‚‚ hâ‚ hâ‚p hâ‚‚ hâ‚‚p, _âŸ©,
  simp only [dist_eq_norm, integral_sum_sub_partitions _ _ hâ‚p hâ‚‚p,
    box_additive_map.to_smul_apply, â† smul_sub],
  have : âˆ€ J âˆˆ Ï€â‚.to_prepartition âŠ“ Ï€â‚‚.to_prepartition,
    âˆ¥Î¼.to_box_additive J â€¢ (f ((Ï€â‚.inf_prepartition Ï€â‚‚.to_prepartition).tag J) -
      f ((Ï€â‚‚.inf_prepartition Ï€â‚.to_prepartition).tag J))âˆ¥ â‰¤ Î¼.to_box_additive J * Îµ',
  { intros J hJ,
    have : 0 â‰¤ Î¼.to_box_additive J, from ennreal.to_real_nonneg,
    rw [norm_smul, real.norm_eq_abs, abs_of_nonneg this, â† dist_eq_norm],
    refine mul_le_mul_of_nonneg_left _ this,
    refine HÎ´ _ _ (tagged_prepartition.tag_mem_Icc _ _) (tagged_prepartition.tag_mem_Icc _ _) _,
    rw [â† add_halves Î´],
    refine (dist_triangle_left _ _ J.upper).trans (add_le_add (hâ‚.1 _ _ _) (hâ‚‚.1 _ _ _)),
    { exact prepartition.bUnion_index_mem _ hJ },
    { exact box.le_iff_Icc.1 (prepartition.le_bUnion_index _ hJ) J.upper_mem_Icc },
    { rw inf_comm at hJ, exact prepartition.bUnion_index_mem _ hJ, },
    { rw inf_comm at hJ,
      exact box.le_iff_Icc.1 (prepartition.le_bUnion_index _ hJ) J.upper_mem_Icc } },
  refine (norm_sum_le_of_le _ this).trans _,
  rw [â† finset.sum_mul, Î¼.to_box_additive.sum_partition_boxes le_top (hâ‚p.inf hâ‚‚p)],
  exact hÎµ.le
end

variable {l}

/-- This is an auxiliary lemma used to prove two statements at once. Use one of the next two
lemmas instead. -/
lemma has_integral_of_bRiemann_eq_ff_of_forall_is_o (hl : l.bRiemann = ff)
  (B : Î¹ â†’áµ‡áµƒ[I] â„) (hB0 : âˆ€ J, 0 â‰¤ B J) (g : Î¹ â†’áµ‡áµƒ[I] F) (s : set â„â¿) (hs : s.countable)
  (hlH : s.nonempty â†’ l.bHenstock = tt)
  (Hâ‚ : âˆ€ (c : â„â‰¥0) (x âˆˆ I.Icc âˆ© s) (Îµ > (0 : â„)), âˆƒ Î´ > 0, âˆ€ J â‰¤ I,
    J.Icc âŠ† metric.closed_ball x Î´ â†’ x âˆˆ J.Icc â†’
    (l.bDistortion â†’ J.distortion â‰¤ c) â†’ dist (vol J (f x)) (g J) â‰¤ Îµ)
  (Hâ‚‚ : âˆ€ (c : â„â‰¥0) (x âˆˆ I.Icc \ s) (Îµ > (0 : â„)), âˆƒ Î´ > 0, âˆ€ J â‰¤ I,
    J.Icc âŠ† metric.closed_ball x Î´ â†’ (l.bHenstock â†’ x âˆˆ J.Icc) â†’
    (l.bDistortion â†’ J.distortion â‰¤ c) â†’ dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
  has_integral I l f vol (g I) :=
begin
  /- We choose `r x` differently for `x âˆˆ s` and `x âˆ‰ s`.

  For `x âˆˆ s`, we choose `Îµs` such that `âˆ‘' x : s, Îµs x < Îµ / 2 / 2 ^ #Î¹`, then choose `r x` so that
  `dist (vol J (f x)) (g J) â‰¤ Îµs x` for `J` in the `r x`-neighborhood of `x`. This guarantees that
  the sum of these distances over boxes `J` such that `Ï€.tag J âˆˆ s` is less than `Îµ / 2`. We need an
  additional multiplier `2 ^ #Î¹` because different boxes can have the same tag.

  For `x âˆ‰ s`, we choose `r x` so that `dist (vol (J (f x))) (g J) â‰¤ (Îµ / 2 / B I) * B J` for a box
  `J` in the `Î´`-neighborhood of `x`. -/
  refine ((l.has_basis_to_filter_Union_top _).tendsto_iff metric.nhds_basis_closed_ball).2 _,
  intros Îµ Îµ0,
  simp only [subtype.exists'] at Hâ‚ Hâ‚‚,
  choose! Î´â‚ HÎ´â‚ using Hâ‚,
  choose! Î´â‚‚ HÎ´â‚‚ using Hâ‚‚,
  have Îµ0' := half_pos Îµ0, have H0 : 0 < (2 ^ fintype.card Î¹ : â„), from pow_pos zero_lt_two _,
  rcases hs.exists_pos_forall_sum_le (div_pos Îµ0' H0) with âŸ¨Îµs, hÎµs0, hÎµsâŸ©,
  simp only [le_div_iff' H0, mul_sum] at hÎµs,
  rcases exists_pos_mul_lt Îµ0' (B I) with âŸ¨Îµ', Îµ'0, hÎµIâŸ©,
  set Î´ : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„) := Î» c x, if x âˆˆ s then Î´â‚ c x (Îµs x) else (Î´â‚‚ c) x Îµ',
  refine âŸ¨Î´, Î» c, l.r_cond_of_bRiemann_eq_ff hl, _âŸ©,
  simp only [set.mem_Union, mem_inter_eq, mem_set_of_eq],
  rintro Ï€ âŸ¨c, hÏ€Î´, hÏ€pâŸ©,
  /- Now we split the sum into two parts based on whether `Ï€.tag J` belongs to `s` or not. -/
  rw [â† g.sum_partition_boxes le_rfl hÏ€p, mem_closed_ball, integral_sum,
    â† sum_filter_add_sum_filter_not Ï€.boxes (Î» J, Ï€.tag J âˆˆ s),
    â† sum_filter_add_sum_filter_not Ï€.boxes (Î» J, Ï€.tag J âˆˆ s), â† add_halves Îµ],
  refine dist_add_add_le_of_le _ _,
  { unfreezingI { rcases s.eq_empty_or_nonempty with rfl|hsne }, { simp [Îµ0'.le] },
    /- For the boxes such that `Ï€.tag J âˆˆ s`, we use the fact that at most `2 ^ #Î¹` boxes have the
    same tag. -/
    specialize hlH hsne,
    have : âˆ€ J âˆˆ Ï€.boxes.filter (Î» J, Ï€.tag J âˆˆ s), dist (vol J (f $ Ï€.tag J)) (g J) â‰¤ Îµs (Ï€.tag J),
    { intros J hJ, rw finset.mem_filter at hJ, cases hJ with hJ hJs,
      refine HÎ´â‚ c _ âŸ¨Ï€.tag_mem_Icc _, hJsâŸ© _ (hÎµs0 _) _ (Ï€.le_of_mem' _ hJ) _
        (hÏ€Î´.2 hlH J hJ) (Î» hD, (finset.le_sup hJ).trans (hÏ€Î´.3 hD)),
      convert hÏ€Î´.1 J hJ, exact (dif_pos hJs).symm },
    refine (dist_sum_sum_le_of_le _ this).trans _,
    rw sum_comp,
    refine (sum_le_sum _).trans (hÎµs _ _),
    { rintro b -,
      rw [â† nat.cast_two, â† nat.cast_pow, â† nsmul_eq_mul],
      refine nsmul_le_nsmul (hÎµs0 _).le _,
      refine (finset.card_le_of_subset _).trans ((hÏ€Î´.is_Henstock hlH).card_filter_tag_eq_le b),
      exact filter_subset_filter _ (filter_subset _ _) },
    { rw [finset.coe_image, set.image_subset_iff],
      exact Î» J hJ, (finset.mem_filter.1 hJ).2 } },
  /- Now we deal with boxes such that `Ï€.tag J âˆ‰ s`.
  In this case the estimate is straightforward. -/
  have Hâ‚‚ : âˆ€ J âˆˆ Ï€.boxes.filter (Î» J, Ï€.tag J âˆ‰ s), dist (vol J (f $ Ï€.tag J)) (g J) â‰¤ Îµ' * B J,
  { intros J hJ, rw finset.mem_filter at hJ, cases hJ with hJ hJs,
    refine HÎ´â‚‚ c _ âŸ¨Ï€.tag_mem_Icc _, hJsâŸ© _ Îµ'0 _ (Ï€.le_of_mem' _ hJ) _ (Î» hH, hÏ€Î´.2 hH J hJ)
      (Î» hD, (finset.le_sup hJ).trans (hÏ€Î´.3 hD)),
    convert hÏ€Î´.1 J hJ, exact (dif_neg hJs).symm },
  refine (dist_sum_sum_le_of_le _ Hâ‚‚).trans
    ((sum_le_sum_of_subset_of_nonneg (filter_subset _ _) _).trans _),
  { exact Î» _ _ _, mul_nonneg Îµ'0.le (hB0 _) },
  { rw [â† mul_sum, B.sum_partition_boxes le_rfl hÏ€p, mul_comm],
    exact hÎµI.le }
end

/-- A function `f` has Henstock (or `âŠ¥`) integral over `I` is equal to the value of a box-additive
function `g` on `I` provided that `vol J (f x)` is sufficiently close to `g J` for sufficiently
small boxes `J âˆ‹ x`. This lemma is useful to prove, e.g., to prove the Divergence theorem for
integral along `âŠ¥`.

Let `l` be either `box_integral.integration_params.Henstock` or `âŠ¥`. Let `g` a box-additive function
on subboxes of `I`. Suppose that there exists a nonnegative box-additive function `B` and a
countable set `s` with the following property.

For every `c : â„â‰¥0`, a point `x âˆˆ I.Icc`, and a positive `Îµ` there exists `Î´ > 0` such that for any
box `J â‰¤ I` such that

- `x âˆˆ J.Icc âŠ† metric.closed_ball x Î´`;
- if `l.bDistortion` (i.e., `l = âŠ¥`), then the distortion of `J` is less than or equal to `c`,

the distance between the term `vol J (f x)` of an integral sum corresponding to `J` and `g J` is
less than or equal to `Îµ` if `x âˆˆ s` and is less than or equal to `Îµ * B J` otherwise.

Then `f` is integrable on `I along `l` with integral `g I`. -/
lemma has_integral_of_le_Henstock_of_forall_is_o (hl : l â‰¤ Henstock) (B : Î¹ â†’áµ‡áµƒ[I] â„)
  (hB0 : âˆ€ J, 0 â‰¤ B J) (g : Î¹ â†’áµ‡áµƒ[I] F) (s : set â„â¿) (hs : s.countable)
  (Hâ‚ : âˆ€ (c : â„â‰¥0) (x âˆˆ I.Icc âˆ© s) (Îµ > (0 : â„)), âˆƒ Î´ > 0, âˆ€ J â‰¤ I,
    J.Icc âŠ† metric.closed_ball x Î´ â†’ x âˆˆ J.Icc â†’ (l.bDistortion â†’ J.distortion â‰¤ c) â†’
    dist (vol J (f x)) (g J) â‰¤ Îµ)
  (Hâ‚‚ : âˆ€ (c : â„â‰¥0) (x âˆˆ I.Icc \ s) (Îµ > (0 : â„)), âˆƒ Î´ > 0, âˆ€ J â‰¤ I,
    J.Icc âŠ† metric.closed_ball x Î´ â†’ x âˆˆ J.Icc â†’ (l.bDistortion â†’ J.distortion â‰¤ c) â†’
    dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
  has_integral I l f vol (g I) :=
have A : l.bHenstock, from hl.2.1.resolve_left dec_trivial,
has_integral_of_bRiemann_eq_ff_of_forall_is_o (hl.1.resolve_right dec_trivial) B hB0 _ s hs (Î» _, A)
  Hâ‚ $ by simpa only [A, true_implies_iff] using Hâ‚‚

/-- Suppose that there exists a nonnegative box-additive function `B` with the following property.

For every `c : â„â‰¥0`, a point `x âˆˆ I.Icc`, and a positive `Îµ` there exists `Î´ > 0` such that for any
box `J â‰¤ I` such that

- `J.Icc âŠ† metric.closed_ball x Î´`;
- if `l.bDistortion` (i.e., `l = âŠ¥`), then the distortion of `J` is less than or equal to `c`,

the distance between the term `vol J (f x)` of an integral sum corresponding to `J` and `g J` is
less than or equal to `Îµ * B J`.

Then `f` is McShane integrable on `I` with integral `g I`. -/
lemma has_integral_McShane_of_forall_is_o (B : Î¹ â†’áµ‡áµƒ[I] â„) (hB0 : âˆ€ J, 0 â‰¤ B J)
  (g : Î¹ â†’áµ‡áµƒ[I] F) (H : âˆ€ (c : â„â‰¥0) (x âˆˆ I.Icc) (Îµ > (0 : â„)), âˆƒ Î´ > 0, âˆ€ J â‰¤ I,
    J.Icc âŠ† metric.closed_ball x Î´ â†’ dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
  has_integral I McShane f vol (g I) :=
has_integral_of_bRiemann_eq_ff_of_forall_is_o rfl B hB0 g âˆ… countable_empty (Î» âŸ¨x, hxâŸ©, hx.elim)
  (Î» c x hx, hx.2.elim) $
  by simpa only [McShane, coe_sort_ff, false_implies_iff, true_implies_iff, diff_empty] using H

end box_integral
