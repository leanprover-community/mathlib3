/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import data.set.intervals.monotone
import topology.algebra.order.monotone_convergence
import topology.metric_space.basic

/-!
# Rectangular boxes in `‚Ñù‚Åø`

In this file we define rectangular boxes in `‚Ñù‚Åø`. As usual, we represent `‚Ñù‚Åø` as the type of
functions `Œπ ‚Üí ‚Ñù` (usually `Œπ = fin n` for some `n`). When we need to interpret a box `[l, u]` as a
set, we use the product `{x | ‚àÄ i, l i < x i ‚àß x i ‚â§ u i}` of half-open intervals `(l i, u i]`. We
exclude `l i` because this way boxes of a partition are disjoint as sets in `‚Ñù‚Åø`.

Currently, the only use cases for these constructions are the definitions of Riemann-style integrals
(Riemann, Henstock-Kurzweil, McShane).

## Main definitions

We use the same structure `box_integral.box` both for ambient boxes and for elements of a partition.
Each box is stored as two points `lower upper : Œπ ‚Üí ‚Ñù` and a proof of `‚àÄ i, lower i < upper i`. We
define instances `has_mem (Œπ ‚Üí ‚Ñù) (box Œπ)` and `has_coe_t (box Œπ) (set $ Œπ ‚Üí ‚Ñù)` so that each box is
interpreted as the set `{x | ‚àÄ i, x i ‚àà set.Ioc (I.lower i) (I.upper i)}`. This way boxes of a
partition are pairwise disjoint and their union is exactly the original box.

We require boxes to be nonempty, because this way coercion to sets is injective. The empty box can
be represented as `‚ä• : with_bot (box_integral.box Œπ)`.

We define the following operations on boxes:

* coercion to `set (Œπ ‚Üí ‚Ñù)` and `has_mem (Œπ ‚Üí ‚Ñù) (box_integral.box Œπ)` as described above;
* `partial_order` and `semilattice_sup` instances such that `I ‚â§ J` is equivalent to
  `(I : set (Œπ ‚Üí ‚Ñù)) ‚äÜ J`;
* `lattice` instances on `with_bot (box_integral.box Œπ)`;
* `box_integral.box.Icc`: the closed box `set.Icc I.lower I.upper`; defined as a bundled monotone
  map from `box Œπ` to `set (Œπ ‚Üí ‚Ñù)`;
* `box_integral.box.face I i : box (fin n)`: a hyperface of `I : box_integral.box (fin (n + 1))`;
* `box_integral.box.distortion`: the maximal ratio of two lengths of edges of a box; defined as the
  supremum of `nndist I.lower I.upper / nndist (I.lower i) (I.upper i)`.

We also provide a convenience constructor `box_integral.box.mk' (l u : Œπ ‚Üí ‚Ñù) : with_bot (box Œπ)`
that returns the box `‚ü®l, u, _‚ü©` if it is nonempty and `‚ä•` otherwise.

## Tags

rectangular box
-/

open set function metric filter

noncomputable theory
open_locale nnreal classical topology

namespace box_integral

variables {Œπ : Type*}

/-!
### Rectangular box: definition and partial order
-/

/-- A nontrivial rectangular box in `Œπ ‚Üí ‚Ñù` with corners `lower` and `upper`. Repesents the product
of half-open intervals `(lower i, upper i]`. -/
structure box (Œπ : Type*) :=
(lower upper : Œπ ‚Üí ‚Ñù)
(lower_lt_upper : ‚àÄ i, lower i < upper i)

attribute [simp] box.lower_lt_upper

namespace box

variables (I J : box Œπ) {x y : Œπ ‚Üí ‚Ñù}

instance : inhabited (box Œπ) := ‚ü®‚ü®0, 1, Œª i, zero_lt_one‚ü©‚ü©

lemma lower_le_upper : I.lower ‚â§ I.upper := Œª i, (I.lower_lt_upper i).le
lemma lower_ne_upper (i) : I.lower i ‚â† I.upper i := (I.lower_lt_upper i).ne

instance : has_mem (Œπ ‚Üí ‚Ñù) (box Œπ) := ‚ü®Œª x I, ‚àÄ i, x i ‚àà Ioc (I.lower i) (I.upper i)‚ü©
instance : has_coe_t (box Œπ) (set $ Œπ ‚Üí ‚Ñù) := ‚ü®Œª I, {x | x ‚àà I}‚ü©

@[simp] lemma mem_mk {l u x : Œπ ‚Üí ‚Ñù} {H} : x ‚àà mk l u H ‚Üî ‚àÄ i, x i ‚àà Ioc (l i) (u i) := iff.rfl
@[simp, norm_cast] lemma mem_coe : x ‚àà (I : set (Œπ ‚Üí ‚Ñù)) ‚Üî x ‚àà I := iff.rfl

lemma mem_def : x ‚àà I ‚Üî ‚àÄ i, x i ‚àà Ioc (I.lower i) (I.upper i) := iff.rfl

lemma mem_univ_Ioc {I : box Œπ}  : x ‚àà pi univ (Œª i, Ioc (I.lower i) (I.upper i)) ‚Üî x ‚àà I :=
mem_univ_pi

lemma coe_eq_pi : (I : set (Œπ ‚Üí ‚Ñù)) = pi univ (Œª i, Ioc (I.lower i) (I.upper i)) :=
set.ext $ Œª x, mem_univ_Ioc.symm

@[simp] lemma upper_mem : I.upper ‚àà I := Œª i, right_mem_Ioc.2 $ I.lower_lt_upper i

lemma exists_mem : ‚àÉ x, x ‚àà I := ‚ü®_, I.upper_mem‚ü©
lemma nonempty_coe : set.nonempty (I : set (Œπ ‚Üí ‚Ñù)) := I.exists_mem
@[simp] lemma coe_ne_empty : (I : set (Œπ ‚Üí ‚Ñù)) ‚â† ‚àÖ := I.nonempty_coe.ne_empty
@[simp] lemma empty_ne_coe : ‚àÖ ‚â† (I : set (Œπ ‚Üí ‚Ñù)) := I.coe_ne_empty.symm

instance : has_le (box Œπ) := ‚ü®Œª I J, ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà I ‚Üí x ‚àà J‚ü©

lemma le_def : I ‚â§ J ‚Üî ‚àÄ x ‚àà I, x ‚àà J := iff.rfl

lemma le_tfae :
  tfae [I ‚â§ J,
    (I : set (Œπ ‚Üí ‚Ñù)) ‚äÜ J,
    Icc I.lower I.upper ‚äÜ Icc J.lower J.upper,
    J.lower ‚â§ I.lower ‚àß I.upper ‚â§ J.upper] :=
begin
  tfae_have : 1 ‚Üî 2, from iff.rfl,
  tfae_have : 2 ‚Üí 3,
  { intro h,
    simpa [coe_eq_pi, closure_pi_set, lower_ne_upper] using closure_mono h },
  tfae_have : 3 ‚Üî 4, from Icc_subset_Icc_iff I.lower_le_upper,
  tfae_have : 4 ‚Üí 2, from Œª h x hx i, Ioc_subset_Ioc (h.1 i) (h.2 i) (hx i),
  tfae_finish
end

variables {I J}

@[simp, norm_cast] lemma coe_subset_coe : (I : set (Œπ ‚Üí ‚Ñù)) ‚äÜ J ‚Üî I ‚â§ J := iff.rfl
lemma le_iff_bounds : I ‚â§ J ‚Üî J.lower ‚â§ I.lower ‚àß I.upper ‚â§ J.upper := (le_tfae I J).out 0 3

lemma injective_coe : injective (coe : box Œπ ‚Üí set (Œπ ‚Üí ‚Ñù)) :=
begin
  rintros ‚ü®l‚ÇÅ, u‚ÇÅ, h‚ÇÅ‚ü© ‚ü®l‚ÇÇ, u‚ÇÇ, h‚ÇÇ‚ü© h,
  simp only [subset.antisymm_iff, coe_subset_coe, le_iff_bounds] at h,
  congr,
  exacts [le_antisymm h.2.1 h.1.1, le_antisymm h.1.2 h.2.2]
end

@[simp, norm_cast] lemma coe_inj : (I : set (Œπ ‚Üí ‚Ñù)) = J ‚Üî I = J :=
injective_coe.eq_iff

@[ext] lemma ext (H : ‚àÄ x, x ‚àà I ‚Üî x ‚àà J) : I = J :=
injective_coe $ set.ext H

lemma ne_of_disjoint_coe (h : disjoint (I : set (Œπ ‚Üí ‚Ñù)) J) : I ‚â† J :=
mt coe_inj.2 $ h.ne I.coe_ne_empty

instance : partial_order (box Œπ) :=
{ le := (‚â§),
  .. partial_order.lift (coe : box Œπ ‚Üí set (Œπ ‚Üí ‚Ñù)) injective_coe }

/-- Closed box corresponding to `I : box_integral.box Œπ`. -/
protected def Icc : box Œπ ‚Ü™o set (Œπ ‚Üí ‚Ñù) :=
order_embedding.of_map_le_iff (Œª I : box Œπ, Icc I.lower I.upper) (Œª I J, (le_tfae I J).out 2 0)

lemma Icc_def : I.Icc = Icc I.lower I.upper := rfl

@[simp] lemma upper_mem_Icc (I : box Œπ) : I.upper ‚àà I.Icc := right_mem_Icc.2 I.lower_le_upper
@[simp] lemma lower_mem_Icc (I : box Œπ) : I.lower ‚àà I.Icc := left_mem_Icc.2 I.lower_le_upper

protected lemma is_compact_Icc (I : box Œπ) : is_compact I.Icc := is_compact_Icc

lemma Icc_eq_pi : I.Icc = pi univ (Œª i, Icc (I.lower i) (I.upper i)) := (pi_univ_Icc _ _).symm

lemma le_iff_Icc : I ‚â§ J ‚Üî I.Icc ‚äÜ J.Icc := (le_tfae I J).out 0 2

lemma antitone_lower : antitone (Œª I : box Œπ, I.lower) :=
Œª I J H, (le_iff_bounds.1 H).1

lemma monotone_upper : monotone (Œª I : box Œπ, I.upper) :=
Œª I J H, (le_iff_bounds.1 H).2

lemma coe_subset_Icc : ‚ÜëI ‚äÜ I.Icc := Œª x hx, ‚ü®Œª i, (hx i).1.le, Œª i, (hx i).2‚ü©

/-!
### Supremum of two boxes
-/

/-- `I ‚äî J` is the least box that includes both `I` and `J`. Since `‚ÜëI ‚à™ ‚ÜëJ` is usually not a box,
`‚Üë(I ‚äî J)` is larger than `‚ÜëI ‚à™ ‚ÜëJ`. -/
instance : has_sup (box Œπ) :=
‚ü®Œª I J, ‚ü®I.lower ‚äì J.lower, I.upper ‚äî J.upper,
  Œª i, (min_le_left _ _).trans_lt $ (I.lower_lt_upper i).trans_le (le_max_left _ _)‚ü©‚ü©

instance : semilattice_sup (box Œπ) :=
{ le_sup_left := Œª I J, le_iff_bounds.2 ‚ü®inf_le_left, le_sup_left‚ü©,
  le_sup_right := Œª I J, le_iff_bounds.2 ‚ü®inf_le_right, le_sup_right‚ü©,
  sup_le := Œª I‚ÇÅ I‚ÇÇ J h‚ÇÅ h‚ÇÇ, le_iff_bounds.2 ‚ü®le_inf (antitone_lower h‚ÇÅ) (antitone_lower h‚ÇÇ),
    sup_le (monotone_upper h‚ÇÅ) (monotone_upper h‚ÇÇ)‚ü©,
  .. box.partial_order, .. box.has_sup }


/-!
### `with_bot (box Œπ)`

In this section we define coercion from `with_bot (box Œπ)` to `set (Œπ ‚Üí ‚Ñù)` by sending `‚ä•` to `‚àÖ`.
-/

instance with_bot_coe : has_coe_t (with_bot (box Œπ)) (set (Œπ ‚Üí ‚Ñù)) := ‚ü®Œª o, o.elim ‚àÖ coe‚ü©

@[simp, norm_cast] lemma coe_bot : ((‚ä• : with_bot (box Œπ)) : set (Œπ ‚Üí ‚Ñù)) = ‚àÖ := rfl

@[simp, norm_cast] lemma coe_coe : ((I : with_bot (box Œπ)) : set (Œπ ‚Üí ‚Ñù)) = I := rfl

lemma is_some_iff : ‚àÄ {I : with_bot (box Œπ)}, I.is_some ‚Üî (I : set (Œπ ‚Üí ‚Ñù)).nonempty
| ‚ä• := by { erw option.is_some, simp }
| (I : box Œπ) := by { erw option.is_some, simp [I.nonempty_coe] }

lemma bUnion_coe_eq_coe (I : with_bot (box Œπ)) :
  (‚ãÉ (J : box Œπ) (hJ : ‚ÜëJ = I), (J : set (Œπ ‚Üí ‚Ñù))) = I :=
by induction I using with_bot.rec_bot_coe; simp [with_bot.coe_eq_coe]

@[simp, norm_cast] lemma with_bot_coe_subset_iff {I J : with_bot (box Œπ)} :
  (I : set (Œπ ‚Üí ‚Ñù)) ‚äÜ J ‚Üî I ‚â§ J :=
begin
  induction I using with_bot.rec_bot_coe, { simp },
  induction J using with_bot.rec_bot_coe, { simp [subset_empty_iff] },
  simp
end

@[simp, norm_cast] lemma with_bot_coe_inj {I J : with_bot (box Œπ)} :
  (I : set (Œπ ‚Üí ‚Ñù)) = J ‚Üî I = J :=
by simp only [subset.antisymm_iff, ‚Üê le_antisymm_iff,  with_bot_coe_subset_iff]

/-- Make a `with_bot (box Œπ)` from a pair of corners `l u : Œπ ‚Üí ‚Ñù`. If `l i < u i` for all `i`,
then the result is `‚ü®l, u, _‚ü© : box Œπ`, otherwise it is `‚ä•`. In any case, the result interpreted
as a set in `Œπ ‚Üí ‚Ñù` is the set `{x : Œπ ‚Üí ‚Ñù | ‚àÄ i, x i ‚àà Ioc (l i) (u i)}`.  -/
def mk' (l u : Œπ ‚Üí ‚Ñù) : with_bot (box Œπ) :=
if h : ‚àÄ i, l i < u i then ‚Üë(‚ü®l, u, h‚ü© : box Œπ) else ‚ä•

@[simp] lemma mk'_eq_bot {l u : Œπ ‚Üí ‚Ñù} : mk' l u = ‚ä• ‚Üî ‚àÉ i, u i ‚â§ l i :=
by { rw mk', split_ifs; simpa using h }

@[simp] lemma mk'_eq_coe {l u : Œπ ‚Üí ‚Ñù} : mk' l u = I ‚Üî l = I.lower ‚àß u = I.upper :=
begin
  cases I with lI uI hI, rw mk', split_ifs,
  { simp [with_bot.coe_eq_coe] },
  { suffices : l = lI ‚Üí u ‚â† uI, by simpa,
    rintro rfl rfl, exact h hI }
end

@[simp] lemma coe_mk' (l u : Œπ ‚Üí ‚Ñù) : (mk' l u : set (Œπ ‚Üí ‚Ñù)) = pi univ (Œª i, Ioc (l i) (u i)) :=
begin
  rw mk', split_ifs,
  { exact coe_eq_pi _ },
  { rcases not_forall.mp h with ‚ü®i, hi‚ü©,
    rw [coe_bot, univ_pi_eq_empty], exact Ioc_eq_empty hi }
end

instance : has_inf (with_bot (box Œπ)) :=
‚ü®Œª I, with_bot.rec_bot_coe (Œª J, ‚ä•) (Œª I J, with_bot.rec_bot_coe ‚ä•
  (Œª J, mk' (I.lower ‚äî J.lower) (I.upper ‚äì J.upper)) J) I‚ü©

@[simp] lemma coe_inf (I J : with_bot (box Œπ)) : (‚Üë(I ‚äì J) : set (Œπ ‚Üí ‚Ñù)) = I ‚à© J :=
begin
  induction I using with_bot.rec_bot_coe, { change ‚àÖ = _, simp },
  induction J using with_bot.rec_bot_coe, { change ‚àÖ = _, simp },
  change ‚Üë(mk' _ _) = _,
  simp only [coe_eq_pi, ‚Üê pi_inter_distrib, Ioc_inter_Ioc, pi.sup_apply, pi.inf_apply, coe_mk',
    coe_coe]
end

instance : lattice (with_bot (box Œπ)) :=
{ inf_le_left := Œª I J,
    begin
      rw [‚Üê with_bot_coe_subset_iff, coe_inf],
      exact inter_subset_left _ _
    end,
  inf_le_right := Œª I J,
    begin
      rw [‚Üê with_bot_coe_subset_iff, coe_inf],
      exact inter_subset_right _ _
    end,
  le_inf := Œª I J‚ÇÅ J‚ÇÇ h‚ÇÅ h‚ÇÇ,
    begin
      simp only [‚Üê with_bot_coe_subset_iff, coe_inf] at *,
      exact subset_inter h‚ÇÅ h‚ÇÇ
    end,
  .. with_bot.semilattice_sup, .. box.with_bot.has_inf }

@[simp, norm_cast] lemma disjoint_with_bot_coe {I J : with_bot (box Œπ)} :
  disjoint (I : set (Œπ ‚Üí ‚Ñù)) J ‚Üî disjoint I J :=
by { simp only [disjoint_iff_inf_le, ‚Üê with_bot_coe_subset_iff, coe_inf], refl }

lemma disjoint_coe : disjoint (I : with_bot (box Œπ)) J ‚Üî disjoint (I : set (Œπ ‚Üí ‚Ñù)) J :=
disjoint_with_bot_coe.symm

lemma not_disjoint_coe_iff_nonempty_inter :
  ¬¨disjoint (I : with_bot (box Œπ)) J ‚Üî (I ‚à© J : set (Œπ ‚Üí ‚Ñù)).nonempty :=
by rw [disjoint_coe, set.not_disjoint_iff_nonempty_inter]

/-!
### Hyperface of a box in `‚Ñù‚Åø‚Å∫¬π = fin (n + 1) ‚Üí ‚Ñù`
-/

/-- Face of a box in `‚Ñù‚Åø‚Å∫¬π = fin (n + 1) ‚Üí ‚Ñù`: the box in `‚Ñù‚Åø = fin n ‚Üí ‚Ñù` with corners at
`I.lower ‚àò fin.succ_above i` and `I.upper ‚àò fin.succ_above i`. -/
@[simps { simp_rhs := tt }] def face {n} (I : box (fin (n + 1))) (i : fin (n + 1)) : box (fin n) :=
‚ü®I.lower ‚àò fin.succ_above i, I.upper ‚àò fin.succ_above i, Œª j, I.lower_lt_upper _‚ü©

@[simp] lemma face_mk {n} (l u : fin (n + 1) ‚Üí ‚Ñù) (h : ‚àÄ i, l i < u i) (i : fin (n + 1)) :
  face ‚ü®l, u, h‚ü© i = ‚ü®l ‚àò fin.succ_above i, u ‚àò fin.succ_above i, Œª j, h _‚ü© :=
rfl

@[mono] lemma face_mono {n} {I J : box (fin (n + 1))} (h : I ‚â§ J) (i : fin (n + 1)) :
  face I i ‚â§ face J i :=
Œª x hx i, Ioc_subset_Ioc ((le_iff_bounds.1 h).1 _) ((le_iff_bounds.1 h).2 _) (hx _)

lemma monotone_face {n} (i : fin (n + 1)) : monotone (Œª I, face I i) := Œª I J h, face_mono h i

lemma maps_to_insert_nth_face_Icc {n} (I : box (fin (n + 1))) {i : fin (n + 1)} {x : ‚Ñù}
  (hx : x ‚àà Icc (I.lower i) (I.upper i)) :
  maps_to (i.insert_nth x) (I.face i).Icc I.Icc :=
Œª y hy, fin.insert_nth_mem_Icc.2 ‚ü®hx, hy‚ü©

lemma maps_to_insert_nth_face {n} (I : box (fin (n + 1))) {i : fin (n + 1)} {x : ‚Ñù}
  (hx : x ‚àà Ioc (I.lower i) (I.upper i)) :
  maps_to (i.insert_nth x) (I.face i) I :=
Œª y hy, by simpa only [mem_coe, mem_def, i.forall_iff_succ_above, hx, fin.insert_nth_apply_same,
  fin.insert_nth_apply_succ_above, true_and]

lemma continuous_on_face_Icc {X} [topological_space X] {n} {f : (fin (n + 1) ‚Üí ‚Ñù) ‚Üí X}
  {I : box (fin (n + 1))} (h : continuous_on f I.Icc) {i : fin (n + 1)} {x : ‚Ñù}
  (hx : x ‚àà Icc (I.lower i) (I.upper i)) :
  continuous_on (f ‚àò i.insert_nth x) (I.face i).Icc :=
h.comp (continuous_on_const.fin_insert_nth i continuous_on_id) (I.maps_to_insert_nth_face_Icc hx)

/-!
### Covering of the interior of a box by a monotone sequence of smaller boxes
-/

/-- The interior of a box. -/
protected def Ioo : box Œπ ‚Üío set (Œπ ‚Üí ‚Ñù) :=
{ to_fun := Œª I, pi univ (Œª i, Ioo (I.lower i) (I.upper i)),
  monotone' := Œª I J h, pi_mono $ Œª i hi, Ioo_subset_Ioo ((le_iff_bounds.1 h).1 i)
    ((le_iff_bounds.1 h).2 i) }

lemma Ioo_subset_coe (I : box Œπ) : I.Ioo ‚äÜ I := Œª x hx i, Ioo_subset_Ioc_self (hx i trivial)

protected lemma Ioo_subset_Icc (I : box Œπ) : I.Ioo ‚äÜ I.Icc := I.Ioo_subset_coe.trans coe_subset_Icc

lemma Union_Ioo_of_tendsto [finite Œπ] {I : box Œπ} {J : ‚Ñï ‚Üí box Œπ} (hJ : monotone J)
  (hl : tendsto (lower ‚àò J) at_top (ùìù I.lower)) (hu : tendsto (upper ‚àò J) at_top (ùìù I.upper)) :
  (‚ãÉ n, (J n).Ioo) = I.Ioo :=
have hl' : ‚àÄ i, antitone (Œª n, (J n).lower i),
  from Œª i, (monotone_eval i).comp_antitone (antitone_lower.comp_monotone hJ),
have hu' : ‚àÄ i, monotone (Œª n, (J n).upper i),
  from Œª i, (monotone_eval i).comp (monotone_upper.comp hJ),
calc (‚ãÉ n, (J n).Ioo) = pi univ (Œª i, ‚ãÉ n, Ioo ((J n).lower i) ((J n).upper i)) :
  Union_univ_pi_of_monotone (Œª i, (hl' i).Ioo (hu' i))
... = I.Ioo :
  pi_congr rfl (Œª i hi, Union_Ioo_of_mono_of_is_glb_of_is_lub (hl' i) (hu' i)
    (is_glb_of_tendsto_at_top (hl' i) (tendsto_pi_nhds.1 hl _))
    (is_lub_of_tendsto_at_top (hu' i) (tendsto_pi_nhds.1 hu _)))

lemma exists_seq_mono_tendsto (I : box Œπ) : ‚àÉ J : ‚Ñï ‚Üío box Œπ, (‚àÄ n, (J n).Icc ‚äÜ I.Ioo) ‚àß
  tendsto (lower ‚àò J) at_top (ùìù I.lower) ‚àß tendsto (upper ‚àò J) at_top (ùìù I.upper) :=
begin
  choose a b ha_anti hb_mono ha_mem hb_mem hab ha_tendsto hb_tendsto
    using Œª i, exists_seq_strict_anti_strict_mono_tendsto (I.lower_lt_upper i),
  exact ‚ü®‚ü®Œª k, ‚ü®flip a k, flip b k, Œª i, hab _ _ _‚ü©,
    Œª k l hkl, le_iff_bounds.2 ‚ü®Œª i, (ha_anti i).antitone hkl, Œª i, (hb_mono i).monotone hkl‚ü©‚ü©,
    Œª n x hx i hi, ‚ü®(ha_mem _ _).1.trans_le (hx.1 _), (hx.2 _).trans_lt (hb_mem _ _).2‚ü©,
    tendsto_pi_nhds.2 ha_tendsto, tendsto_pi_nhds.2 hb_tendsto‚ü©
end

section distortion

variable [fintype Œπ]

/-- The distortion of a box `I` is the maximum of the ratios of the lengths of its edges.
It is defined as the maximum of the ratios
`nndist I.lower I.upper / nndist (I.lower i) (I.upper i)`. -/
def distortion (I : box Œπ) : ‚Ñù‚â•0 :=
finset.univ.sup $ Œª i : Œπ, nndist I.lower I.upper / nndist (I.lower i) (I.upper i)

lemma distortion_eq_of_sub_eq_div {I J : box Œπ} {r : ‚Ñù}
  (h : ‚àÄ i, I.upper i - I.lower i = (J.upper i - J.lower i) / r) :
  distortion I = distortion J :=
begin
  simp only [distortion, nndist_pi_def, real.nndist_eq', h, map_div‚ÇÄ],
  congr' 1 with i,
  have : 0 < r,
  { by_contra hr,
    have := div_nonpos_of_nonneg_of_nonpos (sub_nonneg.2 $ J.lower_le_upper i) (not_lt.1 hr),
    rw ‚Üê h at this,
    exact this.not_lt (sub_pos.2 $ I.lower_lt_upper i) },
  simp_rw [nnreal.finset_sup_div, div_div_div_cancel_right _ ((map_ne_zero real.nnabs).2 this.ne')],
end

lemma nndist_le_distortion_mul (I : box Œπ) (i : Œπ) :
  nndist I.lower I.upper ‚â§ I.distortion * nndist (I.lower i) (I.upper i) :=
calc nndist I.lower I.upper =
  (nndist I.lower I.upper / nndist (I.lower i) (I.upper i)) *  nndist (I.lower i) (I.upper i) :
  (div_mul_cancel _ $ mt nndist_eq_zero.1 (I.lower_lt_upper i).ne).symm
... ‚â§ I.distortion * nndist (I.lower i) (I.upper i) :
  mul_le_mul_right' (finset.le_sup $ finset.mem_univ i) _

lemma dist_le_distortion_mul (I : box Œπ) (i : Œπ) :
  dist I.lower I.upper ‚â§ I.distortion * (I.upper i - I.lower i) :=
have A : I.lower i - I.upper i < 0, from sub_neg.2 (I.lower_lt_upper i),
by simpa only [‚Üê nnreal.coe_le_coe, ‚Üê dist_nndist, nnreal.coe_mul, real.dist_eq,
  abs_of_neg A, neg_sub] using I.nndist_le_distortion_mul i

lemma diam_Icc_le_of_distortion_le (I : box Œπ) (i : Œπ) {c : ‚Ñù‚â•0} (h : I.distortion ‚â§ c) :
  diam I.Icc ‚â§ c * (I.upper i - I.lower i) :=
have (0 : ‚Ñù) ‚â§ c * (I.upper i - I.lower i),
  from mul_nonneg c.coe_nonneg (sub_nonneg.2 $ I.lower_le_upper _),
diam_le_of_forall_dist_le this $ Œª x hx y hy,
  calc dist x y ‚â§ dist I.lower I.upper : real.dist_le_of_mem_pi_Icc hx hy
  ... ‚â§ I.distortion * (I.upper i - I.lower i) : I.dist_le_distortion_mul i
  ... ‚â§ c * (I.upper i - I.lower i) :
    mul_le_mul_of_nonneg_right h (sub_nonneg.2 (I.lower_le_upper i))

end distortion

end box

end box_integral
