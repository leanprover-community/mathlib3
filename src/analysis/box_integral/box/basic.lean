/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import data.set.intervals.monotone
import topology.algebra.order.monotone_convergence
import topology.metric_space.basic

/-!
# Rectangular boxes in `â„â¿`

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define rectangular boxes in `â„â¿`. As usual, we represent `â„â¿` as the type of
functions `Î¹ â†’ â„` (usually `Î¹ = fin n` for some `n`). When we need to interpret a box `[l, u]` as a
set, we use the product `{x | âˆ€ i, l i < x i âˆ§ x i â‰¤ u i}` of half-open intervals `(l i, u i]`. We
exclude `l i` because this way boxes of a partition are disjoint as sets in `â„â¿`.

Currently, the only use cases for these constructions are the definitions of Riemann-style integrals
(Riemann, Henstock-Kurzweil, McShane).

## Main definitions

We use the same structure `box_integral.box` both for ambient boxes and for elements of a partition.
Each box is stored as two points `lower upper : Î¹ â†’ â„` and a proof of `âˆ€ i, lower i < upper i`. We
define instances `has_mem (Î¹ â†’ â„) (box Î¹)` and `has_coe_t (box Î¹) (set $ Î¹ â†’ â„)` so that each box is
interpreted as the set `{x | âˆ€ i, x i âˆˆ set.Ioc (I.lower i) (I.upper i)}`. This way boxes of a
partition are pairwise disjoint and their union is exactly the original box.

We require boxes to be nonempty, because this way coercion to sets is injective. The empty box can
be represented as `âŠ¥ : with_bot (box_integral.box Î¹)`.

We define the following operations on boxes:

* coercion to `set (Î¹ â†’ â„)` and `has_mem (Î¹ â†’ â„) (box_integral.box Î¹)` as described above;
* `partial_order` and `semilattice_sup` instances such that `I â‰¤ J` is equivalent to
  `(I : set (Î¹ â†’ â„)) âŠ† J`;
* `lattice` instances on `with_bot (box_integral.box Î¹)`;
* `box_integral.box.Icc`: the closed box `set.Icc I.lower I.upper`; defined as a bundled monotone
  map from `box Î¹` to `set (Î¹ â†’ â„)`;
* `box_integral.box.face I i : box (fin n)`: a hyperface of `I : box_integral.box (fin (n + 1))`;
* `box_integral.box.distortion`: the maximal ratio of two lengths of edges of a box; defined as the
  supremum of `nndist I.lower I.upper / nndist (I.lower i) (I.upper i)`.

We also provide a convenience constructor `box_integral.box.mk' (l u : Î¹ â†’ â„) : with_bot (box Î¹)`
that returns the box `âŸ¨l, u, _âŸ©` if it is nonempty and `âŠ¥` otherwise.

## Tags

rectangular box
-/

open set function metric filter

noncomputable theory
open_locale nnreal classical topology

namespace box_integral

variables {Î¹ : Type*}

/-!
### Rectangular box: definition and partial order
-/

/-- A nontrivial rectangular box in `Î¹ â†’ â„` with corners `lower` and `upper`. Repesents the product
of half-open intervals `(lower i, upper i]`. -/
structure box (Î¹ : Type*) :=
(lower upper : Î¹ â†’ â„)
(lower_lt_upper : âˆ€ i, lower i < upper i)

attribute [simp] box.lower_lt_upper

namespace box

variables (I J : box Î¹) {x y : Î¹ â†’ â„}

instance : inhabited (box Î¹) := âŸ¨âŸ¨0, 1, Î» i, zero_lt_oneâŸ©âŸ©

lemma lower_le_upper : I.lower â‰¤ I.upper := Î» i, (I.lower_lt_upper i).le
lemma lower_ne_upper (i) : I.lower i â‰  I.upper i := (I.lower_lt_upper i).ne

instance : has_mem (Î¹ â†’ â„) (box Î¹) := âŸ¨Î» x I, âˆ€ i, x i âˆˆ Ioc (I.lower i) (I.upper i)âŸ©
instance : has_coe_t (box Î¹) (set $ Î¹ â†’ â„) := âŸ¨Î» I, {x | x âˆˆ I}âŸ©

@[simp] lemma mem_mk {l u x : Î¹ â†’ â„} {H} : x âˆˆ mk l u H â†” âˆ€ i, x i âˆˆ Ioc (l i) (u i) := iff.rfl
@[simp, norm_cast] lemma mem_coe : x âˆˆ (I : set (Î¹ â†’ â„)) â†” x âˆˆ I := iff.rfl

lemma mem_def : x âˆˆ I â†” âˆ€ i, x i âˆˆ Ioc (I.lower i) (I.upper i) := iff.rfl

lemma mem_univ_Ioc {I : box Î¹}  : x âˆˆ pi univ (Î» i, Ioc (I.lower i) (I.upper i)) â†” x âˆˆ I :=
mem_univ_pi

lemma coe_eq_pi : (I : set (Î¹ â†’ â„)) = pi univ (Î» i, Ioc (I.lower i) (I.upper i)) :=
set.ext $ Î» x, mem_univ_Ioc.symm

@[simp] lemma upper_mem : I.upper âˆˆ I := Î» i, right_mem_Ioc.2 $ I.lower_lt_upper i

lemma exists_mem : âˆƒ x, x âˆˆ I := âŸ¨_, I.upper_memâŸ©
lemma nonempty_coe : set.nonempty (I : set (Î¹ â†’ â„)) := I.exists_mem
@[simp] lemma coe_ne_empty : (I : set (Î¹ â†’ â„)) â‰  âˆ… := I.nonempty_coe.ne_empty
@[simp] lemma empty_ne_coe : âˆ… â‰  (I : set (Î¹ â†’ â„)) := I.coe_ne_empty.symm

instance : has_le (box Î¹) := âŸ¨Î» I J, âˆ€ â¦ƒxâ¦„, x âˆˆ I â†’ x âˆˆ JâŸ©

lemma le_def : I â‰¤ J â†” âˆ€ x âˆˆ I, x âˆˆ J := iff.rfl

lemma le_tfae :
  tfae [I â‰¤ J,
    (I : set (Î¹ â†’ â„)) âŠ† J,
    Icc I.lower I.upper âŠ† Icc J.lower J.upper,
    J.lower â‰¤ I.lower âˆ§ I.upper â‰¤ J.upper] :=
begin
  tfae_have : 1 â†” 2, from iff.rfl,
  tfae_have : 2 â†’ 3,
  { intro h,
    simpa [coe_eq_pi, closure_pi_set, lower_ne_upper] using closure_mono h },
  tfae_have : 3 â†” 4, from Icc_subset_Icc_iff I.lower_le_upper,
  tfae_have : 4 â†’ 2, from Î» h x hx i, Ioc_subset_Ioc (h.1 i) (h.2 i) (hx i),
  tfae_finish
end

variables {I J}

@[simp, norm_cast] lemma coe_subset_coe : (I : set (Î¹ â†’ â„)) âŠ† J â†” I â‰¤ J := iff.rfl
lemma le_iff_bounds : I â‰¤ J â†” J.lower â‰¤ I.lower âˆ§ I.upper â‰¤ J.upper := (le_tfae I J).out 0 3

lemma injective_coe : injective (coe : box Î¹ â†’ set (Î¹ â†’ â„)) :=
begin
  rintros âŸ¨lâ‚, uâ‚, hâ‚âŸ© âŸ¨lâ‚‚, uâ‚‚, hâ‚‚âŸ© h,
  simp only [subset.antisymm_iff, coe_subset_coe, le_iff_bounds] at h,
  congr,
  exacts [le_antisymm h.2.1 h.1.1, le_antisymm h.1.2 h.2.2]
end

@[simp, norm_cast] lemma coe_inj : (I : set (Î¹ â†’ â„)) = J â†” I = J :=
injective_coe.eq_iff

@[ext] lemma ext (H : âˆ€ x, x âˆˆ I â†” x âˆˆ J) : I = J :=
injective_coe $ set.ext H

lemma ne_of_disjoint_coe (h : disjoint (I : set (Î¹ â†’ â„)) J) : I â‰  J :=
mt coe_inj.2 $ h.ne I.coe_ne_empty

instance : partial_order (box Î¹) :=
{ le := (â‰¤),
  .. partial_order.lift (coe : box Î¹ â†’ set (Î¹ â†’ â„)) injective_coe }

/-- Closed box corresponding to `I : box_integral.box Î¹`. -/
protected def Icc : box Î¹ â†ªo set (Î¹ â†’ â„) :=
order_embedding.of_map_le_iff (Î» I : box Î¹, Icc I.lower I.upper) (Î» I J, (le_tfae I J).out 2 0)

lemma Icc_def : I.Icc = Icc I.lower I.upper := rfl

@[simp] lemma upper_mem_Icc (I : box Î¹) : I.upper âˆˆ I.Icc := right_mem_Icc.2 I.lower_le_upper
@[simp] lemma lower_mem_Icc (I : box Î¹) : I.lower âˆˆ I.Icc := left_mem_Icc.2 I.lower_le_upper

protected lemma is_compact_Icc (I : box Î¹) : is_compact I.Icc := is_compact_Icc

lemma Icc_eq_pi : I.Icc = pi univ (Î» i, Icc (I.lower i) (I.upper i)) := (pi_univ_Icc _ _).symm

lemma le_iff_Icc : I â‰¤ J â†” I.Icc âŠ† J.Icc := (le_tfae I J).out 0 2

lemma antitone_lower : antitone (Î» I : box Î¹, I.lower) :=
Î» I J H, (le_iff_bounds.1 H).1

lemma monotone_upper : monotone (Î» I : box Î¹, I.upper) :=
Î» I J H, (le_iff_bounds.1 H).2

lemma coe_subset_Icc : â†‘I âŠ† I.Icc := Î» x hx, âŸ¨Î» i, (hx i).1.le, Î» i, (hx i).2âŸ©

/-!
### Supremum of two boxes
-/

/-- `I âŠ” J` is the least box that includes both `I` and `J`. Since `â†‘I âˆª â†‘J` is usually not a box,
`â†‘(I âŠ” J)` is larger than `â†‘I âˆª â†‘J`. -/
instance : has_sup (box Î¹) :=
âŸ¨Î» I J, âŸ¨I.lower âŠ“ J.lower, I.upper âŠ” J.upper,
  Î» i, (min_le_left _ _).trans_lt $ (I.lower_lt_upper i).trans_le (le_max_left _ _)âŸ©âŸ©

instance : semilattice_sup (box Î¹) :=
{ le_sup_left := Î» I J, le_iff_bounds.2 âŸ¨inf_le_left, le_sup_leftâŸ©,
  le_sup_right := Î» I J, le_iff_bounds.2 âŸ¨inf_le_right, le_sup_rightâŸ©,
  sup_le := Î» Iâ‚ Iâ‚‚ J hâ‚ hâ‚‚, le_iff_bounds.2 âŸ¨le_inf (antitone_lower hâ‚) (antitone_lower hâ‚‚),
    sup_le (monotone_upper hâ‚) (monotone_upper hâ‚‚)âŸ©,
  .. box.partial_order, .. box.has_sup }


/-!
### `with_bot (box Î¹)`

In this section we define coercion from `with_bot (box Î¹)` to `set (Î¹ â†’ â„)` by sending `âŠ¥` to `âˆ…`.
-/

instance with_bot_coe : has_coe_t (with_bot (box Î¹)) (set (Î¹ â†’ â„)) := âŸ¨Î» o, o.elim âˆ… coeâŸ©

@[simp, norm_cast] lemma coe_bot : ((âŠ¥ : with_bot (box Î¹)) : set (Î¹ â†’ â„)) = âˆ… := rfl

@[simp, norm_cast] lemma coe_coe : ((I : with_bot (box Î¹)) : set (Î¹ â†’ â„)) = I := rfl

lemma is_some_iff : âˆ€ {I : with_bot (box Î¹)}, I.is_some â†” (I : set (Î¹ â†’ â„)).nonempty
| âŠ¥ := by { erw option.is_some, simp }
| (I : box Î¹) := by { erw option.is_some, simp [I.nonempty_coe] }

lemma bUnion_coe_eq_coe (I : with_bot (box Î¹)) :
  (â‹ƒ (J : box Î¹) (hJ : â†‘J = I), (J : set (Î¹ â†’ â„))) = I :=
by induction I using with_bot.rec_bot_coe; simp [with_bot.coe_eq_coe]

@[simp, norm_cast] lemma with_bot_coe_subset_iff {I J : with_bot (box Î¹)} :
  (I : set (Î¹ â†’ â„)) âŠ† J â†” I â‰¤ J :=
begin
  induction I using with_bot.rec_bot_coe, { simp },
  induction J using with_bot.rec_bot_coe, { simp [subset_empty_iff] },
  simp
end

@[simp, norm_cast] lemma with_bot_coe_inj {I J : with_bot (box Î¹)} :
  (I : set (Î¹ â†’ â„)) = J â†” I = J :=
by simp only [subset.antisymm_iff, â† le_antisymm_iff,  with_bot_coe_subset_iff]

/-- Make a `with_bot (box Î¹)` from a pair of corners `l u : Î¹ â†’ â„`. If `l i < u i` for all `i`,
then the result is `âŸ¨l, u, _âŸ© : box Î¹`, otherwise it is `âŠ¥`. In any case, the result interpreted
as a set in `Î¹ â†’ â„` is the set `{x : Î¹ â†’ â„ | âˆ€ i, x i âˆˆ Ioc (l i) (u i)}`.  -/
def mk' (l u : Î¹ â†’ â„) : with_bot (box Î¹) :=
if h : âˆ€ i, l i < u i then â†‘(âŸ¨l, u, hâŸ© : box Î¹) else âŠ¥

@[simp] lemma mk'_eq_bot {l u : Î¹ â†’ â„} : mk' l u = âŠ¥ â†” âˆƒ i, u i â‰¤ l i :=
by { rw mk', split_ifs; simpa using h }

@[simp] lemma mk'_eq_coe {l u : Î¹ â†’ â„} : mk' l u = I â†” l = I.lower âˆ§ u = I.upper :=
begin
  cases I with lI uI hI, rw mk', split_ifs,
  { simp [with_bot.coe_eq_coe] },
  { suffices : l = lI â†’ u â‰  uI, by simpa,
    rintro rfl rfl, exact h hI }
end

@[simp] lemma coe_mk' (l u : Î¹ â†’ â„) : (mk' l u : set (Î¹ â†’ â„)) = pi univ (Î» i, Ioc (l i) (u i)) :=
begin
  rw mk', split_ifs,
  { exact coe_eq_pi _ },
  { rcases not_forall.mp h with âŸ¨i, hiâŸ©,
    rw [coe_bot, univ_pi_eq_empty], exact Ioc_eq_empty hi }
end

instance : has_inf (with_bot (box Î¹)) :=
âŸ¨Î» I, with_bot.rec_bot_coe (Î» J, âŠ¥) (Î» I J, with_bot.rec_bot_coe âŠ¥
  (Î» J, mk' (I.lower âŠ” J.lower) (I.upper âŠ“ J.upper)) J) IâŸ©

@[simp] lemma coe_inf (I J : with_bot (box Î¹)) : (â†‘(I âŠ“ J) : set (Î¹ â†’ â„)) = I âˆ© J :=
begin
  induction I using with_bot.rec_bot_coe, { change âˆ… = _, simp },
  induction J using with_bot.rec_bot_coe, { change âˆ… = _, simp },
  change â†‘(mk' _ _) = _,
  simp only [coe_eq_pi, â† pi_inter_distrib, Ioc_inter_Ioc, pi.sup_apply, pi.inf_apply, coe_mk',
    coe_coe]
end

instance : lattice (with_bot (box Î¹)) :=
{ inf_le_left := Î» I J,
    begin
      rw [â† with_bot_coe_subset_iff, coe_inf],
      exact inter_subset_left _ _
    end,
  inf_le_right := Î» I J,
    begin
      rw [â† with_bot_coe_subset_iff, coe_inf],
      exact inter_subset_right _ _
    end,
  le_inf := Î» I Jâ‚ Jâ‚‚ hâ‚ hâ‚‚,
    begin
      simp only [â† with_bot_coe_subset_iff, coe_inf] at *,
      exact subset_inter hâ‚ hâ‚‚
    end,
  .. with_bot.semilattice_sup, .. box.with_bot.has_inf }

@[simp, norm_cast] lemma disjoint_with_bot_coe {I J : with_bot (box Î¹)} :
  disjoint (I : set (Î¹ â†’ â„)) J â†” disjoint I J :=
by { simp only [disjoint_iff_inf_le, â† with_bot_coe_subset_iff, coe_inf], refl }

lemma disjoint_coe : disjoint (I : with_bot (box Î¹)) J â†” disjoint (I : set (Î¹ â†’ â„)) J :=
disjoint_with_bot_coe.symm

lemma not_disjoint_coe_iff_nonempty_inter :
  Â¬disjoint (I : with_bot (box Î¹)) J â†” (I âˆ© J : set (Î¹ â†’ â„)).nonempty :=
by rw [disjoint_coe, set.not_disjoint_iff_nonempty_inter]

/-!
### Hyperface of a box in `â„â¿âºÂ¹ = fin (n + 1) â†’ â„`
-/

/-- Face of a box in `â„â¿âºÂ¹ = fin (n + 1) â†’ â„`: the box in `â„â¿ = fin n â†’ â„` with corners at
`I.lower âˆ˜ fin.succ_above i` and `I.upper âˆ˜ fin.succ_above i`. -/
@[simps { simp_rhs := tt }] def face {n} (I : box (fin (n + 1))) (i : fin (n + 1)) : box (fin n) :=
âŸ¨I.lower âˆ˜ fin.succ_above i, I.upper âˆ˜ fin.succ_above i, Î» j, I.lower_lt_upper _âŸ©

@[simp] lemma face_mk {n} (l u : fin (n + 1) â†’ â„) (h : âˆ€ i, l i < u i) (i : fin (n + 1)) :
  face âŸ¨l, u, hâŸ© i = âŸ¨l âˆ˜ fin.succ_above i, u âˆ˜ fin.succ_above i, Î» j, h _âŸ© :=
rfl

@[mono] lemma face_mono {n} {I J : box (fin (n + 1))} (h : I â‰¤ J) (i : fin (n + 1)) :
  face I i â‰¤ face J i :=
Î» x hx i, Ioc_subset_Ioc ((le_iff_bounds.1 h).1 _) ((le_iff_bounds.1 h).2 _) (hx _)

lemma monotone_face {n} (i : fin (n + 1)) : monotone (Î» I, face I i) := Î» I J h, face_mono h i

lemma maps_to_insert_nth_face_Icc {n} (I : box (fin (n + 1))) {i : fin (n + 1)} {x : â„}
  (hx : x âˆˆ Icc (I.lower i) (I.upper i)) :
  maps_to (i.insert_nth x) (I.face i).Icc I.Icc :=
Î» y hy, fin.insert_nth_mem_Icc.2 âŸ¨hx, hyâŸ©

lemma maps_to_insert_nth_face {n} (I : box (fin (n + 1))) {i : fin (n + 1)} {x : â„}
  (hx : x âˆˆ Ioc (I.lower i) (I.upper i)) :
  maps_to (i.insert_nth x) (I.face i) I :=
Î» y hy, by simpa only [mem_coe, mem_def, i.forall_iff_succ_above, hx, fin.insert_nth_apply_same,
  fin.insert_nth_apply_succ_above, true_and]

lemma continuous_on_face_Icc {X} [topological_space X] {n} {f : (fin (n + 1) â†’ â„) â†’ X}
  {I : box (fin (n + 1))} (h : continuous_on f I.Icc) {i : fin (n + 1)} {x : â„}
  (hx : x âˆˆ Icc (I.lower i) (I.upper i)) :
  continuous_on (f âˆ˜ i.insert_nth x) (I.face i).Icc :=
h.comp (continuous_on_const.fin_insert_nth i continuous_on_id) (I.maps_to_insert_nth_face_Icc hx)

/-!
### Covering of the interior of a box by a monotone sequence of smaller boxes
-/

/-- The interior of a box. -/
protected def Ioo : box Î¹ â†’o set (Î¹ â†’ â„) :=
{ to_fun := Î» I, pi univ (Î» i, Ioo (I.lower i) (I.upper i)),
  monotone' := Î» I J h, pi_mono $ Î» i hi, Ioo_subset_Ioo ((le_iff_bounds.1 h).1 i)
    ((le_iff_bounds.1 h).2 i) }

lemma Ioo_subset_coe (I : box Î¹) : I.Ioo âŠ† I := Î» x hx i, Ioo_subset_Ioc_self (hx i trivial)

protected lemma Ioo_subset_Icc (I : box Î¹) : I.Ioo âŠ† I.Icc := I.Ioo_subset_coe.trans coe_subset_Icc

lemma Union_Ioo_of_tendsto [finite Î¹] {I : box Î¹} {J : â„• â†’ box Î¹} (hJ : monotone J)
  (hl : tendsto (lower âˆ˜ J) at_top (ğ“ I.lower)) (hu : tendsto (upper âˆ˜ J) at_top (ğ“ I.upper)) :
  (â‹ƒ n, (J n).Ioo) = I.Ioo :=
have hl' : âˆ€ i, antitone (Î» n, (J n).lower i),
  from Î» i, (monotone_eval i).comp_antitone (antitone_lower.comp_monotone hJ),
have hu' : âˆ€ i, monotone (Î» n, (J n).upper i),
  from Î» i, (monotone_eval i).comp (monotone_upper.comp hJ),
calc (â‹ƒ n, (J n).Ioo) = pi univ (Î» i, â‹ƒ n, Ioo ((J n).lower i) ((J n).upper i)) :
  Union_univ_pi_of_monotone (Î» i, (hl' i).Ioo (hu' i))
... = I.Ioo :
  pi_congr rfl (Î» i hi, Union_Ioo_of_mono_of_is_glb_of_is_lub (hl' i) (hu' i)
    (is_glb_of_tendsto_at_top (hl' i) (tendsto_pi_nhds.1 hl _))
    (is_lub_of_tendsto_at_top (hu' i) (tendsto_pi_nhds.1 hu _)))

lemma exists_seq_mono_tendsto (I : box Î¹) : âˆƒ J : â„• â†’o box Î¹, (âˆ€ n, (J n).Icc âŠ† I.Ioo) âˆ§
  tendsto (lower âˆ˜ J) at_top (ğ“ I.lower) âˆ§ tendsto (upper âˆ˜ J) at_top (ğ“ I.upper) :=
begin
  choose a b ha_anti hb_mono ha_mem hb_mem hab ha_tendsto hb_tendsto
    using Î» i, exists_seq_strict_anti_strict_mono_tendsto (I.lower_lt_upper i),
  exact âŸ¨âŸ¨Î» k, âŸ¨flip a k, flip b k, Î» i, hab _ _ _âŸ©,
    Î» k l hkl, le_iff_bounds.2 âŸ¨Î» i, (ha_anti i).antitone hkl, Î» i, (hb_mono i).monotone hklâŸ©âŸ©,
    Î» n x hx i hi, âŸ¨(ha_mem _ _).1.trans_le (hx.1 _), (hx.2 _).trans_lt (hb_mem _ _).2âŸ©,
    tendsto_pi_nhds.2 ha_tendsto, tendsto_pi_nhds.2 hb_tendstoâŸ©
end

section distortion

variable [fintype Î¹]

/-- The distortion of a box `I` is the maximum of the ratios of the lengths of its edges.
It is defined as the maximum of the ratios
`nndist I.lower I.upper / nndist (I.lower i) (I.upper i)`. -/
def distortion (I : box Î¹) : â„â‰¥0 :=
finset.univ.sup $ Î» i : Î¹, nndist I.lower I.upper / nndist (I.lower i) (I.upper i)

lemma distortion_eq_of_sub_eq_div {I J : box Î¹} {r : â„}
  (h : âˆ€ i, I.upper i - I.lower i = (J.upper i - J.lower i) / r) :
  distortion I = distortion J :=
begin
  simp only [distortion, nndist_pi_def, real.nndist_eq', h, map_divâ‚€],
  congr' 1 with i,
  have : 0 < r,
  { by_contra hr,
    have := div_nonpos_of_nonneg_of_nonpos (sub_nonneg.2 $ J.lower_le_upper i) (not_lt.1 hr),
    rw â† h at this,
    exact this.not_lt (sub_pos.2 $ I.lower_lt_upper i) },
  simp_rw [nnreal.finset_sup_div, div_div_div_cancel_right _ ((map_ne_zero real.nnabs).2 this.ne')],
end

lemma nndist_le_distortion_mul (I : box Î¹) (i : Î¹) :
  nndist I.lower I.upper â‰¤ I.distortion * nndist (I.lower i) (I.upper i) :=
calc nndist I.lower I.upper =
  (nndist I.lower I.upper / nndist (I.lower i) (I.upper i)) *  nndist (I.lower i) (I.upper i) :
  (div_mul_cancel _ $ mt nndist_eq_zero.1 (I.lower_lt_upper i).ne).symm
... â‰¤ I.distortion * nndist (I.lower i) (I.upper i) :
  mul_le_mul_right' (finset.le_sup $ finset.mem_univ i) _

lemma dist_le_distortion_mul (I : box Î¹) (i : Î¹) :
  dist I.lower I.upper â‰¤ I.distortion * (I.upper i - I.lower i) :=
have A : I.lower i - I.upper i < 0, from sub_neg.2 (I.lower_lt_upper i),
by simpa only [â† nnreal.coe_le_coe, â† dist_nndist, nnreal.coe_mul, real.dist_eq,
  abs_of_neg A, neg_sub] using I.nndist_le_distortion_mul i

lemma diam_Icc_le_of_distortion_le (I : box Î¹) (i : Î¹) {c : â„â‰¥0} (h : I.distortion â‰¤ c) :
  diam I.Icc â‰¤ c * (I.upper i - I.lower i) :=
have (0 : â„) â‰¤ c * (I.upper i - I.lower i),
  from mul_nonneg c.coe_nonneg (sub_nonneg.2 $ I.lower_le_upper _),
diam_le_of_forall_dist_le this $ Î» x hx y hy,
  calc dist x y â‰¤ dist I.lower I.upper : real.dist_le_of_mem_pi_Icc hx hy
  ... â‰¤ I.distortion * (I.upper i - I.lower i) : I.dist_le_distortion_mul i
  ... â‰¤ c * (I.upper i - I.lower i) :
    mul_le_mul_of_nonneg_right h (sub_nonneg.2 (I.lower_le_upper i))

end distortion

end box

end box_integral
