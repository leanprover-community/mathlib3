/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.box_integral.partition.subbox_induction
import analysis.box_integral.partition.additive

/-!
# Filters used in box-based integrals

First we define a structure `box_integral.integration_params`. This structure will be used as an
argument in the definition of `box_integral.integral` in order to use the same definition for a few
well-known definitions of integrals based on partitions of a rectangular box into subboxes (Riemann
integral, Henstock-Kurzweil integral, and McShane integral).

This structure holds three boolean values (see below), and encodes eight different sets of
parameters; only four of these values are used somewhere in `mathlib`. Three of them correspond to
the integration theories listed above, and one is a generalization of the one-dimensional
Henstock-Kurzweil integral such that the divergence theorem works without additional integrability
assumptions.

Finally, for each set of parameters `l : box_integral.integration_params` and a rectangular box
`I : box_integral.box Î¹`, we define several `filter`s that will be used either in the definition of
the corresponding integral, or in the proofs of its properties. We equip
`box_integral.integration_params` with a `bounded_lattice` structure such that larger
`integration_params` produce larger filters.

## Main definitions

### Integration parameters

The structure `box_integral.integration_params` has 3 boolean fields with the following meaning:

* `bRiemann`: the value `tt` means that the filter corresponds to a Riemann-style integral, i.e. in
  the definition of integrability we require a constant upper estimate `r` on the size of boxes of a
  tagged partition; the value `ff` means that the estimate may depend on the position of the tag.

* `bHenstock`: the value `tt` means that we require that each tag belongs to its own closed box; the
  value `ff` means that we only require that tags belong to the ambient box.

* `bDistortion`: the value `tt` means that `r` can depend on the maximal ratio of sides of the same
  box of a partition. Presence of this case make quite a few proofs harder but we can prove the
  divergence theorem only for the filter `âŠ¥ = {bRiemann := ff, bHenstock := tt, bDistortion := tt}`.

### Well-known sets of parameters

Out of eight possible values of `box_integral.integration_params`, the following four are used in
the library.

* `box_integral.integration_params.Riemann` (`bRiemann = tt`, `bHenstock = tt`, `bDistortion = ff`):
  this value corresponds to the Riemann integral; in the corresponding filter, we require that the
  diameters of all boxes `J` of a tagged partition are bounded from above by a constant upper
  estimate that may not depend on the geometry of `J`, and each tag belongs to the corresponding
  closed box.

* `box_integral.integration_params.Henstock` (`bRiemann = ff`, `bHenstock = tt`,
  `bDistortion = ff`): this value corresponds to the most natural generalization of
  Henstock-Kurzweil integral to higher dimension; the only (but important!) difference between this
  theory and Riemann integral is that instead of a constant upper estimate on the size of all boxes
  of a partition, we require that the partition is *subordinate* to a possibly discontinuous
  function `r : (Î¹ â†’ â„) â†’ {x : â„ | 0 < x}`, i.e. each box `J` is included in a closed ball with
  center `Ï€.tag J` and radius `r J`.

* `box_integral.integration_params.McShane` (`bRiemann = ff`, `bHenstock = ff`, `bDistortion = ff`):
  this value corresponds to the McShane integral; the only difference with the Henstock integral is
  that we allow tags to be outside of their boxes; the tags still have to be in the ambient closed
  box, and the partition still has to be subordinate to a function.

* `âŠ¥` (`bRiemann = ff`, `bHenstock = tt`, `bDistortion = tt`): this is the least integration theory
  in our list, i.e., all functions integrable in any other theory is integrable in this one as well.
  This is a non-standard generalization of the Henstock-Kurzweil integral to higher dimension.
  In dimension one, it generates the same filter as `Henstock`. In higher dimension, this
  generalization defines an integration theory such that the divergence of any FrÃ©chet
  differentiable function `f` is integrable, and its integral is equal to the sum of integrals of
  `f` over the faces of the box, taken with appropriate signs.

  A function `f` is `âŠ¥`-integrable if for any `Îµ > 0` and `c : â„â‰¥0` there exists
  `r : (Î¹ â†’ â„) â†’ {x : â„ | 0 < x}` such that for any tagged partition `Ï€` subordinate to `r`, if each
  tag belongs to the corresponding closed box and for each box `J âˆˆ Ï€`, the maximal ratio of its
  sides is less than or equal to `c`, then the integral sum of `f` over `Ï€` is `Îµ`-close to the
  integral.

### Filters and predicates on `tagged_prepartition I`

For each value of `integration_params` and a rectangular box `I`, we define a few filters on
`tagged_prepartition I`. First, we define a predicate

```
structure box_integral.integration_params.mem_base_set (l : box_integral.integration_params)
  (I : box_integral.box Î¹) (c : â„â‰¥0) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„))
  (Ï€ : box_integral.tagged_prepartition I) : Prop :=
```

This predicate says that

* if `l.bHenstock`, then `Ï€` is a Henstock prepartition, i.e. each tag belongs to the corresponding
  closed box;
* `Ï€` is subordinate to `r`;
* if `l.bDistortion`, then the distortion of each box in `Ï€` is less than or equal to `c`;
* if `l.bDistortion`, then there exists a prepartition `Ï€'` with distortion `â‰¤ c` that covers
  exactly `I \ Ï€.Union`.

The last condition is always true for `c > 1`, see TODO section for more details.

Then we define a predicate `box_integral.integration_params.r_cond` on functions
`r : (Î¹ â†’ â„) â†’ {x : â„ | 0 < x}`. If `l.bRiemann`, then this predicate requires `r` to be a constant
function, otherwise it imposes no restrictions on `r`. We introduce this definition to prove a few
dot-notation lemmas: e.g., `box_integral.integration_params.r_cond.min` says that the pointwise
minimum of two functions that satisfy this condition satisfies this condition as well.

Then we define four filters on `box_integral.tagged_prepartition I`.

* `box_integral.integration_params.to_filter_distortion`: an auxiliary filter that takes parameters
  `(l : box_integral.integration_params) (I : box_integral.box Î¹) (c : â„â‰¥0)` and returns the
  filter generated by all sets `{Ï€ | mem_base_set l I c r Ï€}`, where `r` is a function satisfying
  the predicate `box_integral.integration_params.r_cond l`;

* `box_integral.integration_params.to_filter l I`: the supremum of `l.to_filter_distortion I c`
  over all `c : â„â‰¥0`;

* `box_integral.integration_params.to_filter_distortion_Union l I c Ï€â‚€`, where `Ï€â‚€` is a
  prepartition of `I`: the infimum of `l.to_filter_distortion I c` and the principal filter
  generated by `{Ï€ | Ï€.Union = Ï€â‚€.Union}`;

* `box_integral.integration_params.to_filter_Union l I Ï€â‚€`: the supremum of
  `l.to_filter_distortion_Union l I c Ï€â‚€` over all `c : â„â‰¥0`. This is the filter (in the case
  `Ï€â‚€ = âŠ¤` is the one-box partition of `I`) used in the definition of the integral of a function
  over a box.

## Implementation details

* Later we define the integral of a function over a rectangular box as the limit (if it exists) of
  the integral sums along `box_integral.integration_params.to_filter_Union l I âŠ¤`. While it is
  possible to define the integral with a general filter on `box_integral.tagged_prepartition I` as a
  parameter, many lemmas (e.g., Sacks-Henstock lemma and most results about integrability of
  functions) require the filter to have a predictable structure. So, instead of adding assumptions
  about the filter here and there, we define this auxiliary type that can encode all integration
  theories we need in practice.

* While the definition of the integral only uses the filter
  `box_integral.integration_params.to_filter_Union l I âŠ¤` and partitions of a box, some lemmas
  (e.g., the Henstock-Sacks lemmas) are best formulated in terms of the predicate `mem_base_set` and
  other filters defined above.

* We use `bool` instead of `Prop` for the fields of `integration_params` in order to have decidable
  equality and inequalities.

## TODO

Currently, `box_integral.integration_params.mem_base_set` explicitly requires that there exists a
partition of the complement `I \ Ï€.Union` with distortion `â‰¤ c`. For `c > 1`, this condition is
always true but the proof of this fact requires more API about
`box_integral.prepartition.split_many`. We should formalize this fact, then either require `c > 1`
everywhere, or replace `â‰¤ c` with `< c` so that we automatically get `c > 1` for a non-trivial
prepartition (and consider the special case `Ï€ = âŠ¥` separately if needed).

## Tags

integral, rectangular box, partition, filter
-/

open set function filter metric finset bool
open_locale classical topological_space filter nnreal
noncomputable theory

namespace box_integral

variables {Î¹ : Type*} [fintype Î¹] {I J : box Î¹} {c câ‚ câ‚‚ : â„â‰¥0} {r râ‚ râ‚‚ : (Î¹ â†’ â„) â†’ Ioi (0 : â„)}
  {Ï€ Ï€â‚ Ï€â‚‚ : tagged_prepartition I}

open tagged_prepartition

/-- An `integration_params` is a structure holding 3 boolean values used to define a filter to be
used in the definition of a box-integrable function.

* `bRiemann`: the value `tt` means that the filter corresponds to a Riemann-style integral, i.e. in
  the definition of integrability we require a constant upper estimate `r` on the size of boxes of a
  tagged partition; the value `ff` means that the estimate may depend on the position of the tag.

* `bHenstock`: the value `tt` means that we require that each tag belongs to its own closed box; the
  value `ff` means that we only require that tags belong to the ambient box.

* `bDistortion`: the value `tt` means that `r` can depend on the maximal ratio of sides of the same
  box of a partition. Presence of this case makes quite a few proofs harder but we can prove the
  divergence theorem only for the filter `âŠ¥ = {bRiemann := ff, bHenstock := tt, bDistortion := tt}`.
-/
@[ext] structure integration_params : Type :=
(bRiemann bHenstock bDistortion : bool)

variables {l lâ‚ lâ‚‚ : integration_params}

namespace integration_params

/-- Auxiliary equivalence with a product type used to lift an order. -/
def equiv_prod : integration_params â‰ƒ bool Ã— order_dual bool Ã— order_dual bool :=
{ to_fun := Î» l, âŸ¨l.1, order_dual.to_dual l.2, order_dual.to_dual l.3âŸ©,
  inv_fun := Î» l, âŸ¨l.1, order_dual.of_dual l.2.1, order_dual.of_dual l.2.2âŸ©,
  left_inv := Î» âŸ¨a, b, câŸ©, rfl,
  right_inv := Î» âŸ¨a, b, câŸ©, rfl }

instance : partial_order integration_params :=
partial_order.lift equiv_prod equiv_prod.injective

/-- Auxiliary `order_iso` with a product type used to lift a `bounded_lattice` structure. -/
def iso_prod : integration_params â‰ƒo bool Ã— order_dual bool Ã— order_dual bool :=
âŸ¨equiv_prod, Î» âŸ¨x, y, zâŸ©, iff.rflâŸ©

instance : bounded_lattice integration_params :=
iso_prod.symm.to_galois_insertion.lift_bounded_lattice

/-- The value `âŠ¥` (`bRiemann = ff`, `bHenstock = tt`, `bDistortion = tt`) corresponds to a
generalization of the Henstock integral such that the Divergence theorem holds true without
additional integrability assumptions, see the module docstring for details. -/
instance : inhabited integration_params := âŸ¨âŠ¥âŸ©

instance : decidable_rel ((â‰¤) : integration_params â†’ integration_params â†’ Prop) :=
Î» _ _, and.decidable

instance : decidable_eq integration_params := Î» x y, decidable_of_iff _ (ext_iff x y).symm

/-- The `box_integral.integration_params` corresponding to the Riemann integral. In the
corresponding filter, we require that the diameters of all boxes `J` of a tagged partition are
bounded from above by a constant upper estimate that may not depend on the geometry of `J`, and each
tag belongs to the corresponding closed box. -/
def Riemann : integration_params :=
{ bRiemann := tt,
  bHenstock := tt,
  bDistortion := ff }

/-- The `box_integral.integration_params` corresponding to the Henstock-Kurzweil integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r` and each tag belongs to the corresponding closed box. -/
def Henstock : integration_params := âŸ¨ff, tt, ffâŸ©

/-- The `box_integral.integration_params` corresponding to the McShane integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r`; the tags may be outside of the corresponding closed box
(but still inside the ambient closed box `I.Icc`). -/
def McShane : integration_params := âŸ¨ff, ff, ffâŸ©

lemma Henstock_le_Riemann : Henstock â‰¤ Riemann := dec_trivial

lemma Henstock_le_McShane : Henstock â‰¤ McShane := dec_trivial

/-- The predicate corresponding to a base set of the filter defined by an
`integration_params`. It says that

* if `l.bHenstock`, then `Ï€` is a Henstock prepartition, i.e. each tag belongs to the corresponding
  closed box;
* `Ï€` is subordinate to `r`;
* if `l.bDistortion`, then the distortion of each box in `Ï€` is less than or equal to `c`;
* if `l.bDistortion`, then there exists a prepartition `Ï€'` with distortion `â‰¤ c` that covers
  exactly `I \ Ï€.Union`.

The last condition is automatically verified for partitions, and is used in the proof of the
Sacks-Henstock inequality to compare two prepartitions covering the same part of the box.

It is also automatically satisfied for any `c > 1`, see TODO section of the module docstring for
details. -/
@[protect_proj] structure mem_base_set (l : integration_params) (I : box Î¹) (c : â„â‰¥0)
  (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) (Ï€ : tagged_prepartition I) : Prop :=
(is_subordinate : Ï€.is_subordinate r)
(is_Henstock : l.bHenstock â†’ Ï€.is_Henstock)
(distortion_le : l.bDistortion â†’ Ï€.distortion â‰¤ c)
(exists_compl : l.bDistortion â†’ âˆƒ Ï€' : prepartition I, Ï€'.Union = I \ Ï€.Union âˆ§ Ï€'.distortion â‰¤ c)

/-- A predicate saying that in case `l.bRiemann = tt`, the function `r` is a constant. -/
def r_cond {Î¹ : Type*} (l : integration_params) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) : Prop :=
l.bRiemann â†’ âˆ€ x, r x = r 0

/-- A set `s : set (tagged_prepartition I)` belongs to `l.to_filter_distortion I c` if there exists
a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = tt`) such that `s` contains each
prepartition `Ï€` such that `l.mem_base_set I c r Ï€`. -/
def to_filter_distortion (l : integration_params) (I : box Î¹) (c : â„â‰¥0) :
  filter (tagged_prepartition I) :=
â¨… (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) (hr : l.r_cond r), ğ“Ÿ {Ï€ | l.mem_base_set I c r Ï€}

/-- A set `s : set (tagged_prepartition I)` belongs to `l.to_filter I` if for any `c : â„â‰¥0` there
exists a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = tt`) such that
`s` contains each prepartition `Ï€` such that `l.mem_base_set I c r Ï€`. -/
def to_filter (l : integration_params) (I : box Î¹) :
  filter (tagged_prepartition I) :=
â¨† c : â„â‰¥0, l.to_filter_distortion I c

/-- A set `s : set (tagged_prepartition I)` belongs to `l.to_filter_distortion_Union I c Ï€â‚€` if
there exists a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = tt`) such that `s`
contains each prepartition `Ï€` such that `l.mem_base_set I c r Ï€` and `Ï€.Union = Ï€â‚€.Union`. -/
def to_filter_distortion_Union (l : integration_params) (I : box Î¹) (c : â„â‰¥0)
  (Ï€â‚€ : prepartition I) :=
l.to_filter_distortion I c âŠ“ ğ“Ÿ {Ï€ | Ï€.Union = Ï€â‚€.Union}

/-- A set `s : set (tagged_prepartition I)` belongs to `l.to_filter_Union I Ï€â‚€` if for any `c : â„â‰¥0`
there exists a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = tt`) such that `s`
contains each prepartition `Ï€` such that `l.mem_base_set I c r Ï€` and `Ï€.Union = Ï€â‚€.Union`. -/
def to_filter_Union (l : integration_params) (I : box Î¹) (Ï€â‚€ : prepartition I) :=
â¨† c : â„â‰¥0, l.to_filter_distortion_Union I c Ï€â‚€

lemma r_cond_of_bRiemann_eq_ff {Î¹} (l : integration_params) (hl : l.bRiemann = ff)
  {r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)} :
  l.r_cond r :=
by simp [r_cond, hl]

lemma to_filter_inf_Union_eq (l : integration_params) (I : box Î¹) (Ï€â‚€ : prepartition I) :
  l.to_filter I âŠ“ ğ“Ÿ {Ï€ | Ï€.Union = Ï€â‚€.Union} = l.to_filter_Union I Ï€â‚€ :=
(supr_inf_principal _ _).symm

lemma mem_base_set.mono' (I : box Î¹) (h : lâ‚ â‰¤ lâ‚‚) (hc : câ‚ â‰¤ câ‚‚) {Ï€ : tagged_prepartition I}
  (hr : âˆ€ J âˆˆ Ï€, râ‚ (Ï€.tag J) â‰¤ râ‚‚ (Ï€.tag J)) (hÏ€ : lâ‚.mem_base_set I câ‚ râ‚ Ï€) :
  lâ‚‚.mem_base_set I câ‚‚ râ‚‚ Ï€ :=
âŸ¨hÏ€.1.mono' hr, Î» hâ‚‚, hÏ€.2 (le_iff_imp.1 h.2.1 hâ‚‚),
  Î» hD, (hÏ€.3 (le_iff_imp.1 h.2.2 hD)).trans hc,
  Î» hD, (hÏ€.4 (le_iff_imp.1 h.2.2 hD)).imp $ Î» Ï€ hÏ€, âŸ¨hÏ€.1, hÏ€.2.trans hcâŸ©âŸ©

@[mono] lemma mem_base_set.mono (I : box Î¹) (h : lâ‚ â‰¤ lâ‚‚) (hc : câ‚ â‰¤ câ‚‚) {Ï€ : tagged_prepartition I}
  (hr : âˆ€ x âˆˆ I.Icc, râ‚ x â‰¤ râ‚‚ x) (hÏ€ : lâ‚.mem_base_set I câ‚ râ‚ Ï€) :
  lâ‚‚.mem_base_set I câ‚‚ râ‚‚ Ï€ :=
hÏ€.mono' I h hc $ Î» J hJ, hr _ $ Ï€.tag_mem_Icc J

lemma mem_base_set.exists_common_compl (hâ‚ : l.mem_base_set I câ‚ râ‚ Ï€â‚)
  (hâ‚‚ : l.mem_base_set I câ‚‚ râ‚‚ Ï€â‚‚) (hU : Ï€â‚.Union = Ï€â‚‚.Union) :
  âˆƒ Ï€ : prepartition I, Ï€.Union = I \ Ï€â‚.Union âˆ§
    (l.bDistortion â†’ Ï€.distortion â‰¤ câ‚) âˆ§ (l.bDistortion â†’ Ï€.distortion â‰¤ câ‚‚) :=
begin
  wlog hc : câ‚ â‰¤ câ‚‚ := le_total câ‚ câ‚‚ using [câ‚ câ‚‚ râ‚ râ‚‚ Ï€â‚ Ï€â‚‚, câ‚‚ câ‚ râ‚‚ râ‚ Ï€â‚‚ Ï€â‚] tactic.skip,
  { by_cases hD : (l.bDistortion : Prop),
    { rcases hâ‚.4 hD with âŸ¨Ï€, hÏ€U, hÏ€câŸ©,
      exact âŸ¨Ï€, hÏ€U, Î» _, hÏ€c, Î» _, hÏ€c.trans hcâŸ© },
    { exact âŸ¨Ï€â‚.to_prepartition.compl, Ï€â‚.to_prepartition.Union_compl,
        Î» h, (hD h).elim, Î» h, (hD h).elimâŸ© } },
  { intros hâ‚ hâ‚‚ hU,
    simpa [hU, and_comm] using this hâ‚‚ hâ‚ hU.symm }
end

protected lemma mem_base_set.union_compl_to_subordinate (hÏ€â‚ : l.mem_base_set I c râ‚ Ï€â‚)
  (hle : âˆ€ x âˆˆ I.Icc, râ‚‚ x â‰¤ râ‚ x) {Ï€â‚‚ : prepartition I}
  (hU : Ï€â‚‚.Union = I \ Ï€â‚.Union) (hc : l.bDistortion â†’ Ï€â‚‚.distortion â‰¤ c) :
  l.mem_base_set I c râ‚ (Ï€â‚.union_compl_to_subordinate Ï€â‚‚ hU râ‚‚) :=
âŸ¨hÏ€â‚.1.disj_union ((Ï€â‚‚.is_subordinate_to_subordinate râ‚‚).mono hle) _,
  Î» h, ((hÏ€â‚.2 h).disj_union (Ï€â‚‚.is_Henstock_to_subordinate _) _),
  Î» h, (distortion_union_compl_to_subordinate _ _ _ _).trans_le (max_le (hÏ€â‚.3 h) (hc h)),
  Î» _, âŸ¨âŠ¥, by simpâŸ©âŸ©

protected lemma mem_base_set.filter (hÏ€ : l.mem_base_set I c r Ï€) (p : box Î¹ â†’ Prop) :
  l.mem_base_set I c r (Ï€.filter p) :=
begin
  refine âŸ¨Î» J hJ, hÏ€.1 J (Ï€.mem_filter.1 hJ).1, Î» hH J hJ, hÏ€.2 hH J (Ï€.mem_filter.1 hJ).1,
    Î» hD, (distortion_filter_le _ _).trans (hÏ€.3 hD), Î» hD, _âŸ©,
  rcases hÏ€.4 hD with âŸ¨Ï€â‚, hÏ€â‚U, hcâŸ©,
  set Ï€â‚‚ := Ï€.filter (Î» J, Â¬p J),
  have : disjoint Ï€â‚.Union Ï€â‚‚.Union,
    by simpa [Ï€â‚‚, hÏ€â‚U] using (disjoint_diff.mono_left sdiff_le).symm,
  refine âŸ¨Ï€â‚.disj_union Ï€â‚‚.to_prepartition this, _, _âŸ©,
  { suffices : â†‘I \ Ï€.Union âˆª Ï€.Union \ (Ï€.filter p).Union = â†‘I \ (Ï€.filter p).Union, by simpa *,
    have : (Ï€.filter p).Union âŠ† Ï€.Union, from bUnion_subset_bUnion_left (finset.filter_subset _ _),
    ext x, fsplit,
    { rintro (âŸ¨hxI, hxÏ€âŸ©|âŸ¨hxÏ€, hxpâŸ©),
      exacts [âŸ¨hxI, mt (@this x) hxÏ€âŸ©, âŸ¨Ï€.Union_subset hxÏ€, hxpâŸ©] },
    { rintro âŸ¨hxI, hxpâŸ©, by_cases hxÏ€ : x âˆˆ Ï€.Union,
      exacts [or.inr âŸ¨hxÏ€, hxpâŸ©, or.inl âŸ¨hxI, hxÏ€âŸ©] } },
  { have : (Ï€.filter (Î» J, Â¬p J)).distortion â‰¤ c, from (distortion_filter_le _ _).trans (hÏ€.3 hD),
    simpa [hc] }
end

lemma bUnion_tagged_mem_base_set {Ï€ : prepartition I} {Ï€i : Î  J, tagged_prepartition J}
  (h : âˆ€ J âˆˆ Ï€, l.mem_base_set J c r (Ï€i J)) (hp : âˆ€ J âˆˆ Ï€, (Ï€i J).is_partition)
  (hc : l.bDistortion â†’ Ï€.compl.distortion â‰¤ c) :
  l.mem_base_set I c r (Ï€.bUnion_tagged Ï€i) :=
begin
  refine âŸ¨tagged_prepartition.is_subordinate_bUnion_tagged.2 $ Î» J hJ, (h J hJ).1,
    Î» hH, tagged_prepartition.is_Henstock_bUnion_tagged.2 $ Î» J hJ, (h J hJ).2 hH,
    Î» hD, _, Î» hD, _âŸ©,
  { rw [prepartition.distortion_bUnion_tagged, finset.sup_le_iff],
    exact Î» J hJ, (h J hJ).3 hD },
  { refine âŸ¨_, _, hc hDâŸ©,
    rw [Ï€.Union_compl, â† Ï€.Union_bUnion_partition hp], refl }
end

@[mono] lemma r_cond.mono {Î¹ : Type*} {r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)} (h : lâ‚ â‰¤ lâ‚‚)
  (hr :  lâ‚‚.r_cond r) : lâ‚.r_cond r :=
Î» hR, hr (le_iff_imp.1 h.1 hR)

lemma r_cond.min {Î¹ : Type*} {râ‚ râ‚‚ : (Î¹ â†’ â„) â†’ Ioi (0 : â„)} (hâ‚ : l.r_cond râ‚) (hâ‚‚ : l.r_cond râ‚‚) :
  l.r_cond (Î» x, min (râ‚ x) (râ‚‚ x)) :=
Î» hR x, congr_arg2 min (hâ‚ hR x) (hâ‚‚ hR x)

@[mono] lemma to_filter_distortion_mono (I : box Î¹) (h : lâ‚ â‰¤ lâ‚‚) (hc : câ‚ â‰¤ câ‚‚) :
  lâ‚.to_filter_distortion I câ‚ â‰¤ lâ‚‚.to_filter_distortion I câ‚‚ :=
infi_le_infi $ Î» r, infi_le_infi2 $ Î» hr,
  âŸ¨hr.mono h, principal_mono.2 $ Î» _, mem_base_set.mono I h hc (Î» _ _, le_rfl)âŸ©

@[mono] lemma to_filter_mono (I : box Î¹) {lâ‚ lâ‚‚ : integration_params} (h : lâ‚ â‰¤ lâ‚‚) :
  lâ‚.to_filter I â‰¤ lâ‚‚.to_filter I :=
supr_le_supr $ Î» c, to_filter_distortion_mono I h le_rfl

@[mono] lemma to_filter_Union_mono (I : box Î¹) {lâ‚ lâ‚‚ : integration_params} (h : lâ‚ â‰¤ lâ‚‚)
  (Ï€â‚€ : prepartition I) :
  lâ‚.to_filter_Union I Ï€â‚€ â‰¤ lâ‚‚.to_filter_Union I Ï€â‚€ :=
supr_le_supr $ Î» c, inf_le_inf_right _ $ to_filter_distortion_mono _ h le_rfl

lemma to_filter_Union_congr (I : box Î¹) (l : integration_params) {Ï€â‚ Ï€â‚‚ : prepartition I}
  (h : Ï€â‚.Union = Ï€â‚‚.Union) : l.to_filter_Union I Ï€â‚ = l.to_filter_Union I Ï€â‚‚ :=
by simp only [to_filter_Union, to_filter_distortion_Union, h]

lemma has_basis_to_filter_distortion (l : integration_params) (I : box Î¹) (c : â„â‰¥0) :
  (l.to_filter_distortion I c).has_basis l.r_cond (Î» r, {Ï€ | l.mem_base_set I c r Ï€}) :=
has_basis_binfi_principal'
  (Î» râ‚ hrâ‚ râ‚‚ hrâ‚‚, âŸ¨_, hrâ‚.min hrâ‚‚,
    Î» _, mem_base_set.mono _ le_rfl le_rfl (Î» x hx, min_le_left _ _),
    Î» _, mem_base_set.mono _ le_rfl le_rfl (Î» x hx, min_le_right _ _)âŸ©)
  âŸ¨Î» _, âŸ¨1, @zero_lt_one â„ _ _âŸ©, Î» _ _, rflâŸ©

lemma has_basis_to_filter_distortion_Union (l : integration_params) (I : box Î¹) (c : â„â‰¥0)
  (Ï€â‚€ : prepartition I) :
  (l.to_filter_distortion_Union I c Ï€â‚€).has_basis l.r_cond
    (Î» r, {Ï€ | l.mem_base_set I c r Ï€ âˆ§ Ï€.Union = Ï€â‚€.Union}) :=
(l.has_basis_to_filter_distortion I c).inf_principal _

lemma has_basis_to_filter_Union (l : integration_params) (I : box Î¹) (Ï€â‚€ : prepartition I) :
  (l.to_filter_Union I Ï€â‚€).has_basis (Î» r : â„â‰¥0 â†’ (Î¹ â†’ â„) â†’ Ioi (0 : â„), âˆ€ c, l.r_cond (r c))
    (Î» r, {Ï€ | âˆƒ c, l.mem_base_set I c (r c) Ï€ âˆ§ Ï€.Union = Ï€â‚€.Union}) :=
have _ := Î» c, l.has_basis_to_filter_distortion_Union I c Ï€â‚€,
by simpa only [set_of_and, set_of_exists] using has_basis_supr this

lemma has_basis_to_filter_Union_top (l : integration_params) (I : box Î¹) :
  (l.to_filter_Union I âŠ¤).has_basis (Î» r : â„â‰¥0 â†’ (Î¹ â†’ â„) â†’ Ioi (0 : â„), âˆ€ c, l.r_cond (r c))
    (Î» r, {Ï€ | âˆƒ c, l.mem_base_set I c (r c) Ï€ âˆ§ Ï€.is_partition}) :=
by simpa only [tagged_prepartition.is_partition_iff_Union_eq, prepartition.Union_top]
  using l.has_basis_to_filter_Union I âŠ¤

lemma has_basis_to_filter (l : integration_params) (I : box Î¹) :
  (l.to_filter I).has_basis (Î» r : â„â‰¥0 â†’ (Î¹ â†’ â„) â†’ Ioi (0 : â„), âˆ€ c, l.r_cond (r c))
    (Î» r, {Ï€ | âˆƒ c, l.mem_base_set I c (r c) Ï€}) :=
by simpa only [set_of_exists] using has_basis_supr (l.has_basis_to_filter_distortion I)

lemma tendsto_embed_box_to_filter_Union_top (l : integration_params) (h : I â‰¤ J) :
  tendsto (tagged_prepartition.embed_box I J h) (l.to_filter_Union I âŠ¤)
    (l.to_filter_Union J (prepartition.single J I h)) :=
begin
  simp only [to_filter_Union, tendsto_supr], intro c,
  set Ï€â‚€ := (prepartition.single J I h),
  refine le_supr_of_le (max c Ï€â‚€.compl.distortion) _,
  refine ((l.has_basis_to_filter_distortion_Union I c âŠ¤).tendsto_iff
    (l.has_basis_to_filter_distortion_Union J _ _)).2 (Î» r hr, _),
  refine âŸ¨r, hr, Î» Ï€ hÏ€, _âŸ©,
  rw [mem_set_of_eq, prepartition.Union_top] at hÏ€,
  refine âŸ¨âŸ¨hÏ€.1.1, hÏ€.1.2, Î» hD, le_trans (hÏ€.1.3 hD) (le_max_left _ _), Î» hD, _âŸ©, _âŸ©,
  { refine âŸ¨_, Ï€â‚€.Union_compl.trans _, le_max_right _ _âŸ©, congr' 1,
    exact (prepartition.Union_single h).trans hÏ€.2.symm },
  { exact hÏ€.2.trans (prepartition.Union_single _).symm }
end

lemma exists_mem_base_set_le_Union_eq (l : integration_params) (Ï€â‚€ : prepartition I)
  (hcâ‚ : Ï€â‚€.distortion â‰¤ c) (hcâ‚‚ : Ï€â‚€.compl.distortion â‰¤ c) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  âˆƒ Ï€, l.mem_base_set I c r Ï€ âˆ§ Ï€.to_prepartition â‰¤ Ï€â‚€ âˆ§ Ï€.Union = Ï€â‚€.Union :=
begin
  rcases Ï€â‚€.exists_tagged_le_is_Henstock_is_subordinate_Union_eq r
    with âŸ¨Ï€, hle, hH, hr, hd, hUâŸ©,
  refine âŸ¨Ï€, âŸ¨hr, Î» _, hH, Î» _, hd.trans_le hcâ‚, Î» hD, âŸ¨Ï€â‚€.compl, _, hcâ‚‚âŸ©âŸ©, âŸ¨hle, hUâŸ©âŸ©,
  exact prepartition.compl_congr hU â–¸ Ï€.to_prepartition.Union_compl
end

lemma exists_mem_base_set_is_partition (l : integration_params) (I : box Î¹)
  (hc : I.distortion â‰¤ c) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  âˆƒ Ï€, l.mem_base_set I c r Ï€ âˆ§ Ï€.is_partition :=
begin
  rw â† prepartition.distortion_top at hc,
  have hc' : (âŠ¤ : prepartition I).compl.distortion â‰¤ c, by simp,
  simpa [is_partition_iff_Union_eq] using l.exists_mem_base_set_le_Union_eq âŠ¤ hc hc' r
end

lemma to_filter_distortion_Union_ne_bot (l : integration_params) (I : box Î¹)
  (Ï€â‚€ : prepartition I) (hcâ‚ : Ï€â‚€.distortion â‰¤ c) (hcâ‚‚ : Ï€â‚€.compl.distortion â‰¤ c) :
  (l.to_filter_distortion_Union I c Ï€â‚€).ne_bot :=
((l.has_basis_to_filter_distortion I _).inf_principal _).ne_bot_iff.2 $ Î» r hr,
  (l.exists_mem_base_set_le_Union_eq Ï€â‚€ hcâ‚ hcâ‚‚ r).imp $ Î» Ï€ hÏ€, âŸ¨hÏ€.1, hÏ€.2.2âŸ©

instance to_filter_distortion_Union_ne_bot' (l : integration_params) (I : box Î¹)
  (Ï€â‚€ : prepartition I) :
  (l.to_filter_distortion_Union I (max Ï€â‚€.distortion Ï€â‚€.compl.distortion) Ï€â‚€).ne_bot :=
l.to_filter_distortion_Union_ne_bot I Ï€â‚€ (le_max_left _ _) (le_max_right _ _)

instance to_filter_distortion_ne_bot (l : integration_params) (I : box Î¹) :
  (l.to_filter_distortion I I.distortion).ne_bot :=
by simpa using (l.to_filter_distortion_Union_ne_bot' I âŠ¤).mono inf_le_left

instance to_filter_ne_bot (l : integration_params) (I : box Î¹) : (l.to_filter I).ne_bot :=
(l.to_filter_distortion_ne_bot I).mono $ le_supr _ _

instance to_filter_Union_ne_bot (l : integration_params) (I : box Î¹) (Ï€â‚€ : prepartition I) :
  (l.to_filter_Union I Ï€â‚€).ne_bot :=
(l.to_filter_distortion_Union_ne_bot' I Ï€â‚€).mono $
  le_supr (Î» c, l.to_filter_distortion_Union I c Ï€â‚€) _

lemma eventually_is_partition (l : integration_params) (I : box Î¹) :
  âˆ€á¶  Ï€ in l.to_filter_Union I âŠ¤, tagged_prepartition.is_partition Ï€ :=
eventually_supr.2 $ Î» c, eventually_inf_principal.2 $ eventually_of_forall $
  Î» Ï€ h, Ï€.is_partition_iff_Union_eq.2 (h.trans prepartition.Union_top)

end integration_params

end box_integral
