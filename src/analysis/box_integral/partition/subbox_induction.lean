/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.box_integral.box.subbox_induction
import analysis.box_integral.partition.tagged

/-!
# Induction on subboxes

In this file we prove (see
`box_integral.tagged_partition.exists_is_Henstock_is_subordinate_homothetic`) that for every box `I`
in `â„â¿` and a function `r : â„â¿ â†’ â„` positive on `I` there exists a tagged partition `Ï€` of `I` such
that

* `Ï€` is a Henstock partition;
* `Ï€` is subordinate to `r`;
* each box in `Ï€` is homothetic to `I` with coefficient of the form `1 / 2 ^ n`.

Later we will use this lemma to prove that the Henstock filter is nontrivial, hence the Henstock
integral is well-defined.

## Tags

partition, tagged partition, Henstock integral
-/

namespace box_integral

open set metric
open_locale classical topological_space
noncomputable theory

variables {Î¹ : Type*} [fintype Î¹] {I J : box Î¹}

namespace prepartition

/-- Split a box in `â„â¿` into `2 ^ n` boxes by hyperplanes passing through its center. -/
def split_center (I : box Î¹) : prepartition I :=
{ boxes := finset.univ.map (box.split_center_box_emb I),
  le_of_mem' := by simp [I.split_center_box_le],
  pairwise_disjoint :=
    begin
      rw [finset.coe_map, finset.coe_univ, image_univ],
      rintro _ âŸ¨s, rflâŸ© _ âŸ¨t, rflâŸ© Hne,
      exact I.disjoint_split_center_box (mt (congr_arg _) Hne)
    end }

@[simp] lemma mem_split_center : J âˆˆ split_center I â†” âˆƒ s, I.split_center_box s = J :=
by simp [split_center]

lemma is_partition_split_center (I : box Î¹) : is_partition (split_center I) :=
Î» x hx, by simp [hx]

lemma upper_sub_lower_of_mem_split_center (h : J âˆˆ split_center I) (i : Î¹) :
  J.upper i - J.lower i = (I.upper i - I.lower i) / 2 :=
let âŸ¨s, hsâŸ© := mem_split_center.1 h in hs â–¸ I.upper_sub_lower_split_center_box s i

end prepartition

namespace box

open prepartition tagged_prepartition

/-- Let `p` be a predicate on `box Î¹`, let `I` be a box. Suppose that the following two properties
hold true.

* Consider a smaller box `J â‰¤ I`. The hyperplanes passing through the center of `J` split it into
  `2 ^ n` boxes. If `p` holds true on each of these boxes, then it true on `J`.
* For each `z` in the closed box `I.Icc` there exists a neighborhood `U` of `z` within `I.Icc` such
  that for every box `J â‰¤ I` such that `z âˆˆ J.Icc âŠ† U`, if `J` is homothetic to `I` with a
  coefficient of the form `1 / 2 ^ m`, then `p` is true on `J`.

Then `p I` is true. See also `box_integral.box.subbox_induction_on'` for a version using
`box_integral.box.split_center_box` instead of `box_integral.prepartition.split_center`. -/
@[elab_as_eliminator]
lemma subbox_induction_on {p : box Î¹ â†’ Prop} (I : box Î¹)
  (H_ind : âˆ€ J â‰¤ I, (âˆ€ J' âˆˆ split_center J, p J') â†’ p J)
  (H_nhds : âˆ€ z âˆˆ I.Icc, âˆƒ (U âˆˆ ğ“[I.Icc] z), âˆ€ (J â‰¤ I) (m : â„•), z âˆˆ J.Icc â†’ J.Icc âŠ† U â†’
    (âˆ€ i, J.upper i - J.lower i = (I.upper i - I.lower i) / 2 ^ m) â†’ p J) :
  p I :=
begin
  refine subbox_induction_on' I (Î» J hle hs, H_ind J hle $ Î» J' h', _) H_nhds,
  rcases mem_split_center.1 h' with âŸ¨s, rflâŸ©,
  exact hs s
end

/-- Given a box `I` in `â„â¿` and a function `r : â„â¿ â†’ (0, âˆ)`, there exists a tagged partition `Ï€` of
`I` such that

* `Ï€` is a Henstock partition;
* `Ï€` is subordinate to `r`;
* each box in `Ï€` is homothetic to `I` with coefficient of the form `1 / 2 ^ m`.

This lemma implies that the Henstock filter is nontrivial, hence the Henstock integral is
well-defined. -/
lemma exists_tagged_partition_is_Henstock_is_subordinate_homothetic (I : box Î¹)
  (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  âˆƒ Ï€ : tagged_prepartition I, Ï€.is_partition âˆ§ Ï€.is_Henstock âˆ§ Ï€.is_subordinate r âˆ§
    (âˆ€ J âˆˆ Ï€, âˆƒ m : â„•, âˆ€ i, (J : _).upper i - J.lower i = (I.upper i - I.lower i) / 2 ^ m) âˆ§
    Ï€.distortion = I.distortion :=
begin
  refine subbox_induction_on I (Î» J hle hJ, _) (Î» z hz, _),
  { choose! Ï€i hP hHen hr Hn Hd using hJ, choose! n hn using Hn,
    have hP : ((split_center J).bUnion_tagged Ï€i).is_partition,
      from (is_partition_split_center _).bUnion_tagged hP,
    have hsub : âˆ€ (J' âˆˆ (split_center J).bUnion_tagged Ï€i), âˆƒ n : â„•, âˆ€ i,
      (J' : _).upper i - J'.lower i = (J.upper i - J.lower i) / 2 ^ n,
    { intros J' hJ',
      rcases (split_center J).mem_bUnion_tagged.1 hJ' with âŸ¨Jâ‚, hâ‚, hâ‚‚âŸ©,
      refine âŸ¨n Jâ‚ J' + 1, Î» i, _âŸ©,
      simp only [hn Jâ‚ hâ‚ J' hâ‚‚, upper_sub_lower_of_mem_split_center hâ‚, pow_succ,
        div_div_eq_div_mul] },
    refine âŸ¨_, hP, is_Henstock_bUnion_tagged.2 hHen, is_subordinate_bUnion_tagged.2 hr, hsub, _âŸ©,
    refine tagged_prepartition.distortion_of_const _ hP.nonempty_boxes (Î» J' h', _),
    rcases hsub J' h' with âŸ¨n, hnâŸ©,
    exact box.distortion_eq_of_sub_eq_div hn },
  { refine âŸ¨I.Icc âˆ© closed_ball z (r z),
      inter_mem_nhds_within _ (closed_ball_mem_nhds _ (r z).coe_prop), _âŸ©,
    intros J Hle n Hmem HIcc Hsub,
    rw set.subset_inter_iff at HIcc,
    refine âŸ¨single _ _ le_rfl _ Hmem, is_partition_single _, is_Henstock_single _,
      (is_subordinate_single _ _).2 HIcc.2, _, distortion_single _ _âŸ©,
    simp only [tagged_prepartition.mem_single, forall_eq],
    refine âŸ¨0, Î» i, _âŸ©, simp }
end

end box

namespace prepartition

open tagged_prepartition finset function

/-- Given a box `I` in `â„â¿`, a function `r : â„â¿ â†’ (0, âˆ)`, and a prepartition `Ï€` of `I`, there
exists a tagged prepartition `Ï€'` of `I` such that

* each box of `Ï€'` is included in some box of `Ï€`;
* `Ï€'` is a Henstock partition;
* `Ï€'` is subordinate to `r`;
* `Ï€'` covers exactly the same part of `I` as `Ï€`;
* the distortion of `Ï€'` is equal to the distortion of `Ï€`.
-/
lemma exists_tagged_le_is_Henstock_is_subordinate_Union_eq {I : box Î¹} (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„))
  (Ï€ : prepartition I) :
  âˆƒ Ï€' : tagged_prepartition I, Ï€'.to_prepartition â‰¤ Ï€ âˆ§
    Ï€'.is_Henstock âˆ§ Ï€'.is_subordinate r âˆ§ Ï€'.distortion = Ï€.distortion âˆ§
    Ï€'.Union = Ï€.Union :=
begin
  have := Î» J, box.exists_tagged_partition_is_Henstock_is_subordinate_homothetic J r,
  choose! Ï€i Ï€ip Ï€iH Ï€ir hsub Ï€id, clear hsub,
  refine âŸ¨Ï€.bUnion_tagged Ï€i, bUnion_le _ _, is_Henstock_bUnion_tagged.2 (Î» J _, Ï€iH J),
    is_subordinate_bUnion_tagged.2 (Î» J _, Ï€ir J), _, Ï€.Union_bUnion_partition (Î» J _, Ï€ip J)âŸ©,
  rw [distortion_bUnion_tagged],
  exact sup_congr rfl (Î» J _, Ï€id J)
end

/-- Given a prepartition `Ï€` of a box `I` and a function `r : â„â¿ â†’ (0, âˆ)`, `Ï€.to_subordinate r`
is a tagged partition `Ï€'` such that

* each box of `Ï€'` is included in some box of `Ï€`;
* `Ï€'` is a Henstock partition;
* `Ï€'` is subordinate to `r`;
* `Ï€'` covers exactly the same part of `I` as `Ï€`;
* the distortion of `Ï€'` is equal to the distortion of `Ï€`.
-/
def to_subordinate (Ï€ : prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) : tagged_prepartition I :=
(Ï€.exists_tagged_le_is_Henstock_is_subordinate_Union_eq r).some

lemma to_subordinate_to_prepartition_le (Ï€ : prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€.to_subordinate r).to_prepartition â‰¤ Ï€ :=
(Ï€.exists_tagged_le_is_Henstock_is_subordinate_Union_eq r).some_spec.1

lemma is_Henstock_to_subordinate (Ï€ : prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€.to_subordinate r).is_Henstock :=
(Ï€.exists_tagged_le_is_Henstock_is_subordinate_Union_eq r).some_spec.2.1

lemma is_subordinate_to_subordinate (Ï€ : prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€.to_subordinate r).is_subordinate r :=
(Ï€.exists_tagged_le_is_Henstock_is_subordinate_Union_eq r).some_spec.2.2.1

@[simp] lemma distortion_to_subordinate (Ï€ : prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€.to_subordinate r).distortion = Ï€.distortion :=
(Ï€.exists_tagged_le_is_Henstock_is_subordinate_Union_eq r).some_spec.2.2.2.1

@[simp] lemma Union_to_subordinate (Ï€ : prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€.to_subordinate r).Union = Ï€.Union :=
(Ï€.exists_tagged_le_is_Henstock_is_subordinate_Union_eq r).some_spec.2.2.2.2

end prepartition

namespace tagged_prepartition

/-- Given a tagged prepartition `Ï€â‚`, a prepartition `Ï€â‚‚` that covers exactly `I \ Ï€â‚.Union`, and
a function `r : â„â¿ â†’ (0, âˆ)`, returns the union of `Ï€â‚` and `Ï€â‚‚.to_subordinate r`. This partition
`Ï€` has the following properties:

* `Ï€` is a partition, i.e. it covers the whole `I`;
* `Ï€â‚.boxes âŠ† Ï€.boxes`;
* `Ï€.tag J = Ï€â‚.tag J` whenever `J âˆˆ Ï€â‚`;
* `Ï€` is Henstock outside of `Ï€â‚`: `Ï€.tag J âˆˆ J.Icc` whenever `J âˆˆ Ï€`, `J âˆ‰ Ï€â‚`;
* `Ï€` is subordinate to `r` outside of `Ï€â‚`;
* the distortion of `Ï€` is equal to the maximum of the distortions of `Ï€â‚` and `Ï€â‚‚`.
-/
def union_compl_to_subordinate (Ï€â‚ : tagged_prepartition I) (Ï€â‚‚ : prepartition I)
  (hU : Ï€â‚‚.Union = I \ Ï€â‚.Union) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  tagged_prepartition I :=
Ï€â‚.disj_union (Ï€â‚‚.to_subordinate r)
  (((Ï€â‚‚.Union_to_subordinate r).trans hU).symm â–¸ disjoint_diff)

lemma is_partition_union_compl_to_subordinate (Ï€â‚ : tagged_prepartition I) (Ï€â‚‚ : prepartition I)
  (hU : Ï€â‚‚.Union = I \ Ï€â‚.Union) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  is_partition (Ï€â‚.union_compl_to_subordinate Ï€â‚‚ hU r) :=
prepartition.is_partition_disj_union_of_eq_diff ((Ï€â‚‚.Union_to_subordinate r).trans hU)

@[simp] lemma union_compl_to_subordinate_boxes (Ï€â‚ : tagged_prepartition I) (Ï€â‚‚ : prepartition I)
  (hU : Ï€â‚‚.Union = I \ Ï€â‚.Union) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€â‚.union_compl_to_subordinate Ï€â‚‚ hU r).boxes = Ï€â‚.boxes âˆª (Ï€â‚‚.to_subordinate r).boxes :=
rfl

@[simp] lemma Union_union_compl_to_subordinate_boxes (Ï€â‚ : tagged_prepartition I)
  (Ï€â‚‚ : prepartition I) (hU : Ï€â‚‚.Union = I \ Ï€â‚.Union) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€â‚.union_compl_to_subordinate Ï€â‚‚ hU r).Union = I :=
(is_partition_union_compl_to_subordinate _ _ _ _).Union_eq

@[simp] lemma distortion_union_compl_to_subordinate (Ï€â‚ : tagged_prepartition I)
  (Ï€â‚‚ : prepartition I) (hU : Ï€â‚‚.Union = I \ Ï€â‚.Union) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
  (Ï€â‚.union_compl_to_subordinate Ï€â‚‚ hU r).distortion = max Ï€â‚.distortion Ï€â‚‚.distortion :=
by simp [union_compl_to_subordinate]

end tagged_prepartition

end box_integral
