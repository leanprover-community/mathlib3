/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.box_integral.basic
import analysis.box_integral.partition.additive
import analysis.calculus.fderiv

/-!
# Divergence integral for Henstock-Kurzweil integral

In this file we prove the Divergence Theorem for a Henstock-Kurzweil style integral. The theorem
says that the following. Let `f : â„â¿ â†’ Eâ¿` be a function differentiable on a closed rectangular box
`I` with derivative `f' x : â„â¿ â†’L[â„] Eâ¿` at `x âˆˆ I`. Then the divergence `Î» x, âˆ‘ k, f' x eâ‚– k`,
where `eâ‚– = pi.single k 1` is the `k`-th basis vector, is integrable on `I`, and its integral is
equal to the sum of integrals of `f` over the faces of `I` taken with appropriate signs.

To make the proof work, we had to ban tagged partitions with â€œlong and thinâ€ boxes. More precisely,
we use the following generalization of one-dimensional Henstock-Kurzweil integral to functions
defined on a box in `â„â¿` (it corresponds to the value `âŠ¥` of `box_integral.integration_filter` in
the definition of `box_integral.has_integral`).

We say that `f : â„â¿ â†’ E` has integral `y : E` over a box `I âŠ† â„â¿` if for an arbitrarily small
positive `Îµ` and an arbitrarily large `c`, there exists a function `r : â„â¿ â†’ (0, âˆ)` such that for
any tagged partition `Ï€` of `I` such that

* `Ï€` is a Henstock partition, i.e., each tag belongs to its box;
* `Ï€` is subordinate to `r`;
* for every box of `Ï€`, the maximum of the ratios of its sides is less than or equal to `c`,

the integral sum of `f` over `Ï€` is `Îµ`-close to `y`. In case of dimension one, the last condition
trivially holds for any `c â‰¥ 1`, so this definition is equivalent to the standard definition of
Henstock-Kurzweil integral.

## Tags

Henstock-Kurzweil integral, integral, Stokes theorem, divergence theorem
-/

open_locale classical big_operators nnreal ennreal topological_space
open continuous_linear_map (lsmul) filter set finset metric
noncomputable theory

universes u
variables {E : Type u} [normed_group E] [normed_space â„ E] {n : â„•}

namespace box_integral

local notation `â„â¿` := fin n â†’ â„
local notation `â„â¿âºÂ¹` := fin (n + 1) â†’ â„
local notation `Eâ¿âºÂ¹` := fin (n + 1) â†’ E

variables [complete_space E] (I : box (fin (n + 1))) {i : fin (n + 1)}

open measure_theory

/-- Auxiliary lemma for the divergence theorem. -/
lemma norm_volume_sub_integral_face_upper_sub_lower_smul_le
  {f : â„â¿âºÂ¹ â†’ E} {f' : â„â¿âºÂ¹ â†’L[â„] E} (hfc : continuous_on f I.Icc)
  {x : â„â¿âºÂ¹} (hxI : x âˆˆ I.Icc) {a : E} {Îµ : â„} (h0 : 0 < Îµ)
  (hÎµ : âˆ€ y âˆˆ I.Icc, âˆ¥f y - a - f' (y - x)âˆ¥ â‰¤ Îµ * âˆ¥y - xâˆ¥) {c : â„â‰¥0} (hc : I.distortion â‰¤ c) :
  âˆ¥(âˆ j, (I.upper j - I.lower j)) â€¢ f' (pi.single i 1) -
    (integral (I.face i) âŠ¥ (f âˆ˜ i.insert_nth (I.upper i)) box_additive_map.volume -
      integral (I.face i) âŠ¥ (f âˆ˜ i.insert_nth (I.lower i)) box_additive_map.volume)âˆ¥ â‰¤
    2 * Îµ * c * âˆ j, (I.upper j - I.lower j) :=
begin
  have Hl : I.lower i âˆˆ Icc (I.lower i) (I.upper i), from left_mem_Icc.2 (I.lower_le_upper i),
  have Hu : I.upper i âˆˆ Icc (I.lower i) (I.upper i), from right_mem_Icc.2 (I.lower_le_upper i),
  have Hi : âˆ€ x âˆˆ Icc (I.lower i) (I.upper i),
    integrable.{0 u u} (I.face i) âŠ¥ (f âˆ˜ i.insert_nth x) box_additive_map.volume,
    from Î» x hx, integrable_of_continuous_on _ (box.continuous_on_face_Icc hfc hx) volume,
  rw [â† integral_sub (Hi _ Hu) (Hi _ Hl), â† box.volume_face_mul i, mul_smul, â† box.volume_apply,
    â† box_additive_map.to_smul_apply, â† integral_const, â† box_additive_map.volume,
    â† integral_sub (integrable_const _) ((Hi _ Hu).sub (Hi _ Hl))],
  simp only [(âˆ˜), pi.sub_def, â† f'.map_smul, â† pi.single_smul', smul_eq_mul, mul_one],
  have : âˆ€ y âˆˆ (I.face i).Icc, âˆ¥f' (pi.single i (I.upper i - I.lower i)) -
    (f (i.insert_nth (I.upper i) y) - f (i.insert_nth (I.lower i) y))âˆ¥ â‰¤ 2 * Îµ * diam I.Icc,
  { intros y hy,
    set g := Î» y, f y - a - f' (y - x) with hg,
    change âˆ€ y âˆˆ I.Icc, âˆ¥g yâˆ¥ â‰¤ Îµ * âˆ¥y - xâˆ¥ at hÎµ,
    clear_value g, obtain rfl : f = Î» y, a + f' (y - x) + g y, by simp [hg],
    convert_to âˆ¥g (i.insert_nth (I.lower i) y) - g (i.insert_nth (I.upper i) y)âˆ¥ â‰¤ _,
    { congr' 1,
      have := fin.insert_nth_sub_same i (I.upper i) (I.lower i) y,
      simp only [â† this, f'.map_sub], abel },
    { have : âˆ€ z âˆˆ Icc (I.lower i) (I.upper i), i.insert_nth z y âˆˆ I.Icc,
        from Î» z hz, I.maps_to_insert_nth_face_Icc hz hy,
      replace hÎµ : âˆ€ y âˆˆ I.Icc, âˆ¥g yâˆ¥ â‰¤ Îµ * diam I.Icc,
      { intros y hy,
        refine (hÎµ y hy).trans (mul_le_mul_of_nonneg_left _ h0.le),
        rw â† dist_eq_norm,
        exact dist_le_diam_of_mem (is_compact_pi_Icc I.lower I.upper).bounded hy hxI },
      rw [two_mul, add_mul],
      exact norm_sub_le_of_le (hÎµ _ (this _ Hl)) (hÎµ _ (this _ Hu)) } },
  refine (norm_integral_le_of_le_const this volume).trans _,
  rw [mul_left_comm (volume _).to_real, mul_assoc (2 * Îµ), mul_left_comm (c : â„)],
  refine mul_le_mul_of_nonneg_left _ (mul_nonneg zero_le_two h0.le),
  refine mul_le_mul_of_nonneg_left _ ennreal.to_real_nonneg,
  exact (I.diam_Icc_le_distortion_mul i).trans
    (mul_le_mul_of_nonneg_right hc $ sub_nonneg.2 $ I.lower_le_upper i)
end

lemma has_integral_bot_pderiv (f : â„â¿âºÂ¹ â†’ E) (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] E) (s : set â„â¿âºÂ¹)
  (hs : countable s) (Hs : âˆ€ x âˆˆ s, continuous_within_at f I.Icc x)
  (Hd : âˆ€ x âˆˆ I.Icc \ s, has_fderiv_within_at f (f' x) I.Icc x) (i : fin (n + 1)) :
  has_integral.{0 u u} I âŠ¥ (Î» x, f' x (pi.single i 1)) box_additive_map.volume
    (integral.{0 u u} (I.face i) âŠ¥ (Î» x, f (i.insert_nth (I.upper i) x)) box_additive_map.volume -
      integral.{0 u u} (I.face i) âŠ¥ (Î» x, f (i.insert_nth (I.lower i) x))
        box_additive_map.volume) :=
begin
  have Hc : continuous_on f I.Icc,
  { intros x hx,
    by_cases hxs : x âˆˆ s,
    exacts [Hs x hxs, (Hd x âŸ¨hx, hxsâŸ©).continuous_within_at] },
  set fI : â„ â†’ box (fin n) â†’ E := Î» y J,
    integral.{0 u u} J âŠ¥ (Î» x, f (i.insert_nth y x)) box_additive_map.volume,
  set fb : Icc (I.lower i) (I.upper i) â†’ fin n â†’áµ‡áµƒ[â†‘(I.face i)] E :=
    Î» x, (integrable_of_continuous_on âŠ¥ (box.continuous_on_face_Icc Hc x.2) volume).to_box_additive,
  set F : fin (n + 1) â†’áµ‡áµƒ[I] E := box_additive_map.upper_sub_lower I i fI fb (Î» x hx J, rfl),
  change has_integral I âŠ¥ (Î» x, f' x (pi.single i 1)) _ (F I),
  refine has_integral_of_le_Henstock_of_forall_is_o bot_le _ _ _ s hs _ _,
  { exact (volume : measure â„â¿âºÂ¹).to_box_additive.restrict _ le_top },
  { exact Î» J, ennreal.to_real_nonneg },
  { intros c x hx Îµ Îµ0,
    have : âˆ€á¶  Î´ in ğ“[Ioi 0] (0 : â„), Î´ âˆˆ Ioc (0 : â„) (1 / 2) âˆ§
      (âˆ€ yâ‚ yâ‚‚ âˆˆ closed_ball x Î´ âˆ© I.Icc, âˆ¥f yâ‚ - f yâ‚‚âˆ¥ â‰¤ Îµ / 2) âˆ§
      ((2 * Î´) ^ (n + 1) * âˆ¥f' x (pi.single i 1)âˆ¥ â‰¤ Îµ / 2),
    { refine eventually.and _ (eventually.and _ _),
      { exact Ioc_mem_nhds_within_Ioi âŸ¨le_rfl, one_half_posâŸ© },
      { rcases ((nhds_within_has_basis nhds_basis_closed_ball _).tendsto_iff
          nhds_basis_closed_ball).1 (Hs x hx.2) _ (half_pos $ half_pos Îµ0) with âŸ¨Î´â‚, Î´â‚0, hÎ´â‚âŸ©,
        filter_upwards [Ioc_mem_nhds_within_Ioi âŸ¨le_rfl, Î´â‚0âŸ©],
        rintro Î´ hÎ´ yâ‚ yâ‚‚ hyâ‚ hyâ‚‚,
        have : closed_ball x Î´ âˆ© I.Icc âŠ† closed_ball x Î´â‚ âˆ© I.Icc,
          from inter_subset_inter_left _ (closed_ball_subset_closed_ball hÎ´.2),
        rw â† dist_eq_norm,
        calc dist (f yâ‚) (f yâ‚‚) â‰¤ dist (f yâ‚) (f x) + dist (f yâ‚‚) (f x) : dist_triangle_right _ _ _
        ... â‰¤ Îµ / 2 / 2 + Îµ / 2 / 2 : add_le_add (hÎ´â‚ _ $ this hyâ‚) (hÎ´â‚ _ $ this hyâ‚‚)
        ... = Îµ / 2 : add_halves _ },
      { have : continuous_within_at (Î» Î´, (2 * Î´) ^ (n + 1) * âˆ¥f' x (pi.single i 1)âˆ¥)
          (Ioi (0 : â„)) 0 := ((continuous_within_at_id.const_mul _).pow _).mul_const _,
        refine this.eventually (ge_mem_nhds _),
        simpa using half_pos Îµ0 } },
    rcases this.exists with âŸ¨Î´, âŸ¨hÎ´0, hÎ´12âŸ©, hdfÎ´, hÎ´âŸ©,
    refine âŸ¨Î´, hÎ´0, Î» J hJI hJÎ´ hxJ hJc, add_halves Îµ â–¸ _âŸ©,
    have Hl : J.lower i âˆˆ Icc (J.lower i) (J.upper i), from left_mem_Icc.2 (J.lower_le_upper i),
    have Hu : J.upper i âˆˆ Icc (J.lower i) (J.upper i), from right_mem_Icc.2 (J.lower_le_upper i),
    have Hi : âˆ€ x âˆˆ Icc (J.lower i) (J.upper i),
      integrable.{0 u u} (J.face i) âŠ¥ (Î» y, f (i.insert_nth x y)) box_additive_map.volume,
      from Î» x hx, integrable_of_continuous_on _
        (box.continuous_on_face_Icc (Hc.mono $ box.le_iff_Icc.1 hJI) hx) volume,
    have hJÎ´' : J.Icc âŠ† closed_ball x Î´ âˆ© I.Icc,
      from subset_inter hJÎ´ (box.le_iff_Icc.1 hJI),
    have Hmaps : âˆ€ z âˆˆ Icc (J.lower i) (J.upper i),
      maps_to (i.insert_nth z) (J.face i).Icc (closed_ball x Î´ âˆ© I.Icc),
      from Î» z hz, (J.maps_to_insert_nth_face_Icc hz).mono subset.rfl hJÎ´',
    simp only [dist_eq_norm, F, fI], dsimp,
    rw [â† integral_sub (Hi _ Hu) (Hi _ Hl)],
    refine (norm_sub_le _ _).trans (add_le_add _ _),
    { simp_rw [box_additive_map.volume_apply, norm_smul, real.norm_eq_abs, abs_prod],
      refine (mul_le_mul_of_nonneg_right _ $ norm_nonneg _).trans hÎ´,
      have : âˆ€ j, |J.upper j - J.lower j| â‰¤ 2 * Î´,
      { intro j,
        calc dist (J.upper j) (J.lower j) â‰¤ dist J.upper J.lower : dist_le_pi_dist _ _ _
        ... â‰¤ dist J.upper x + dist J.lower x : dist_triangle_right _ _ _
        ... â‰¤ Î´ + Î´ : add_le_add (hJÎ´ J.upper_mem_Icc) (hJÎ´ J.lower_mem_Icc)
        ... = 2 * Î´ : (two_mul Î´).symm },
      calc (âˆ j, |J.upper j - J.lower j|) â‰¤ âˆ j : fin (n + 1), (2 * Î´) :
        prod_le_prod (Î» _ _ , abs_nonneg _) (Î» j hj, this j)
      ... = (2 * Î´) ^ (n + 1) : by simp },
    { refine (norm_integral_le_of_le_const (Î» y hy,
        hdfÎ´ _ _ (Hmaps _ Hu hy) (Hmaps _ Hl hy)) _).trans _,
      refine (mul_le_mul_of_nonneg_right _ (half_pos Îµ0).le).trans_eq (one_mul _),
      rw [box.coe_eq_pi, real.volume_pi_Ioc_to_real (box.lower_le_upper _)],
      refine prod_le_one (Î» _ _, sub_nonneg.2 $ box.lower_le_upper _ _) (Î» j hj, _),
      calc J.upper (i.succ_above j) - J.lower (i.succ_above j)
          â‰¤ dist (J.upper (i.succ_above j)) (J.lower (i.succ_above j)) : le_abs_self _
      ... â‰¤ dist J.upper J.lower : dist_le_pi_dist J.upper J.lower (i.succ_above j)
      ... â‰¤ dist J.upper x + dist J.lower x : dist_triangle_right _ _ _
      ... â‰¤ Î´ + Î´ : add_le_add (hJÎ´ J.upper_mem_Icc) (hJÎ´ J.lower_mem_Icc)
      ... â‰¤ 1 / 2 + 1 / 2 : add_le_add hÎ´12 hÎ´12
      ... = 1 : add_halves 1 } },
  { intros c x hx Îµ Îµ0,
    rcases exists_pos_mul_lt Îµ0 (2 * c) with âŸ¨Îµ', Îµ'0, hltâŸ©,
    rcases (nhds_within_has_basis nhds_basis_closed_ball _).mem_iff.1 ((Hd x hx).def Îµ'0)
      with âŸ¨Î´, Î´0, HÎ´âŸ©,
    refine âŸ¨Î´, Î´0, Î» J hle hJÎ´ hxJ hJc, _âŸ©,
    simp only [box_additive_map.volume_apply, box.volume_apply, dist_eq_norm],
    refine (norm_volume_sub_integral_face_upper_sub_lower_smul_le _
      (Hc.mono $ box.le_iff_Icc.1 hle) hxJ Îµ'0 (Î» y hy, HÎ´ _) (hJc rfl)).trans _,
    { exact âŸ¨hJÎ´ hy, box.le_iff_Icc.1 hle hyâŸ© },
    { rw [mul_right_comm (2 : â„), â† box.volume_apply],
      exact mul_le_mul_of_nonneg_right hlt.le ennreal.to_real_nonneg } }
end

/-- Divergence theorem for a Henstock-Kurzweil style integral.

If `f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹` is differentiable on a closed rectangular box `I` with derivative `f'`, then
the divergence `âˆ‘ i, f' x (pi.single i 1) i` is Henstock-Kurzweil integrable with integral equal to
the sum of integrals of `f` over the faces of `I` taken with appropriate signs.

More precisely, we use a non-standard generalization of the Henstock-Kurzweil integral and
we allow `f` to be non-differentiable (but still continuous) at a countable set of points. -/
lemma has_integral_bot_divergence_of_forall_has_deriv_within_at
  (f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹) (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹) (s : set â„â¿âºÂ¹) (hs : countable s)
  (Hs : âˆ€ x âˆˆ s, continuous_within_at f I.Icc x)
  (Hd : âˆ€ x âˆˆ I.Icc \ s, has_fderiv_within_at f (f' x) I.Icc x) :
  has_integral.{0 u u} I âŠ¥ (Î» x, âˆ‘ i, f' x (pi.single i 1) i)
    box_additive_map.volume
    (âˆ‘ i, (integral.{0 u u} (I.face i) âŠ¥ (Î» x, f (i.insert_nth (I.upper i) x) i)
      box_additive_map.volume -
      integral.{0 u u} (I.face i) âŠ¥ (Î» x, f (i.insert_nth (I.lower i) x) i)
        box_additive_map.volume)) :=
begin
  refine has_integral_sum (Î» i hi, _), clear hi,
  simp only [has_fderiv_within_at_pi', continuous_within_at_pi] at Hd Hs,
  convert has_integral_bot_pderiv I _ _ s hs (Î» x hx, Hs x hx i) (Î» x hx, Hd x hx i) i
end

end box_integral
