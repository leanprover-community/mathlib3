import analysis.calculus.cont_diff
import analysis.calculus.diff_on_int_cont
import analysis.asymptotics.completion
import analysis.normed_space.completion

/-!
# Derivatives and completion of a normed space
-/

variables {ğ•œ E F : Type*} [nontrivially_normed_field ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E]
  [normed_add_comm_group F] [normed_space ğ•œ F]

local postfix `Ì‚`:100 := uniform_space.completion

open asymptotics uniform_space.completion

section fderiv

variables {f : E â†’ F} {x : E} {f' : E â†’L[ğ•œ] F} {s : set E} {L : filter E}
  {p : E â†’ formal_multilinear_series ğ•œ E F} {n : with_top â„•}

@[simp, norm_cast] lemma has_fderiv_at_filter_coe_completion :
  has_fderiv_at_filter (Î» x, f x : E â†’ FÌ‚) (to_complL.comp f') x L â†”
    has_fderiv_at_filter f f' x L :=
by simp only [has_fderiv_at_filter, continuous_linear_map.comp_apply, coe_to_complL, â† coe_sub,
  is_o_completion_left]

@[simp, norm_cast] lemma has_fderiv_within_at_coe_completion :
  has_fderiv_within_at (Î» x, f x : E â†’ FÌ‚) (to_complL.comp f') s x â†” has_fderiv_within_at f f' s x :=
has_fderiv_at_filter_coe_completion

@[simp, norm_cast] lemma has_fderiv_at_coe_completion :
  has_fderiv_at (Î» x, f x : E â†’ FÌ‚) (to_complL.comp f') x â†” has_fderiv_at f f' x :=
has_fderiv_at_filter_coe_completion

alias has_fderiv_at_filter_coe_completion â†” _ has_fderiv_at_filter.coe_completion
alias has_fderiv_within_at_coe_completion â†” _ has_fderiv_within_at.coe_completion
alias has_fderiv_at_coe_completion â†” _ has_fderiv_at.coe_completion

lemma differentiable_within_at.coe_completion (h : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î» x, f x : E â†’ FÌ‚) s x :=
h.has_fderiv_within_at.coe_completion.differentiable_within_at

lemma differentiable_at.coe_completion (h : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î» x, f x : E â†’ FÌ‚) x :=
h.has_fderiv_at.coe_completion.differentiable_at

lemma differentiable_on.coe_completion (h : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î» x, f x : E â†’ FÌ‚) s :=
Î» x hx, (h x hx).coe_completion

lemma differentiable.coe_completion (h : differentiable ğ•œ f) :
  differentiable ğ•œ (Î» x, f x : E â†’ FÌ‚) :=
Î» x, (h x).coe_completion

lemma diff_cont_on_cl.coe_completion (h : diff_cont_on_cl ğ•œ f s) :
  diff_cont_on_cl ğ•œ (Î» x, f x : E â†’ FÌ‚) s :=
(to_complL : F â†’L[ğ•œ] FÌ‚).differentiable.comp_diff_cont_on_cl h

lemma has_ftaylor_series_up_to_on.coe_completion (h : has_ftaylor_series_up_to_on n f p s) :
  has_ftaylor_series_up_to_on n (Î» x, f x : E â†’ FÌ‚)
    (Î» x k, to_complL.comp_continuous_multilinear_map (p x k)) s :=
h.continuous_linear_map_comp (to_complL : F â†’L[ğ•œ] FÌ‚)

lemma cont_diff_within_at.coe_completion (h : cont_diff_within_at ğ•œ n f s x) :
  cont_diff_within_at ğ•œ n (Î» x, f x : E â†’ FÌ‚) s x :=
h.continuous_linear_map_comp (to_complL : F â†’L[ğ•œ] FÌ‚)

lemma cont_diff_on.coe_completion (h : cont_diff_on ğ•œ n f s) :
  cont_diff_on ğ•œ n (Î» x, f x : E â†’ FÌ‚) s :=
h.continuous_linear_map_comp (to_complL : F â†’L[ğ•œ] FÌ‚)

lemma cont_diff.coe_completion (h : cont_diff ğ•œ n f) : cont_diff ğ•œ n (Î» x, f x : E â†’ FÌ‚) :=
h.continuous_linear_map_comp (to_complL : F â†’L[ğ•œ] FÌ‚)

end fderiv

section deriv

variables {f : ğ•œ â†’ E} {x : ğ•œ} {f' : E} {s : set ğ•œ} {L : filter ğ•œ}

@[simp, norm_cast] lemma has_deriv_at_filter_coe_completion :
  has_deriv_at_filter (Î» x, f x : ğ•œ â†’ EÌ‚) f' x L â†”
    has_deriv_at_filter f f' x L :=
by simp only [has_deriv_at_filter_iff_is_o, â† coe_sub, â† coe_smul, is_o_completion_left]

@[simp, norm_cast] lemma has_deriv_within_at_coe_completion :
  has_deriv_within_at (Î» x, f x : ğ•œ â†’ EÌ‚) f' s x â†” has_deriv_within_at f f' s x :=
has_deriv_at_filter_coe_completion

@[simp, norm_cast] lemma has_deriv_at_coe_completion :
  has_deriv_at (Î» x, f x : ğ•œ â†’ EÌ‚) f' x â†” has_deriv_at f f' x :=
has_deriv_at_filter_coe_completion

alias has_deriv_at_filter_coe_completion â†” _ has_deriv_at_filter.coe_completion
alias has_deriv_within_at_coe_completion â†” _ has_deriv_within_at.coe_completion
alias has_deriv_at_coe_completion â†” _ has_deriv_at.coe_completion

lemma differentiable_at.coe_completion_deriv (h : differentiable_at ğ•œ f x) :
  (â†‘(deriv f x) : EÌ‚) = deriv (Î» x, f x : ğ•œ â†’ EÌ‚) x :=
h.has_deriv_at.coe_completion.deriv.symm

end deriv
