/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.calculus.mean_value

/-!
# Darboux's theorem

In this file we prove that the derivative of a differentiable function on an interval takes all
intermediate values. The proof is based on the
[Wikipedia](https://en.wikipedia.org/wiki/Darboux%27s_theorem_(analysis)) page about this theorem.
-/

open filter set
open_locale topological_space classical

variables {a b : ‚Ñù} {f f' : ‚Ñù ‚Üí ‚Ñù}

/-- **Darboux's theorem**: if `a ‚â§ b` and `f' a < m < f' b`, then `f' c = m` for some
`c ‚àà [a, b]`. -/
theorem exists_has_deriv_within_at_eq_of_gt_of_lt
  (hab : a ‚â§ b) (hf : ‚àÄ x ‚àà (Icc a b), has_deriv_within_at f (f' x) (Icc a b) x)
  {m : ‚Ñù} (hma : f' a < m) (hmb : m < f' b) :
  m ‚àà f' '' (Icc a b) :=
begin
  have hab' : a < b,
  { refine lt_of_le_of_ne hab (Œª hab', _),
    subst b,
    exact lt_asymm hma hmb },
  set g : ‚Ñù ‚Üí ‚Ñù := Œª x, f x - m * x,
  have hg : ‚àÄ x ‚àà Icc a b, has_deriv_within_at g (f' x - m) (Icc a b) x,
  { intros x hx,
    simpa using (hf x hx).sub ((has_deriv_within_at_id x _).const_mul m) },
  obtain ‚ü®c, cmem, hc‚ü© : ‚àÉ c ‚àà Icc a b, is_min_on g (Icc a b) c,
    from is_compact_Icc.exists_forall_le (nonempty_Icc.2 $ hab)
      (Œª x hx, (hg x hx).continuous_within_at),
  have cmem' : c ‚àà Ioo a b,
  { cases eq_or_lt_of_le cmem.1 with hac hac,
    -- Show that `c` can't be equal to `a`
    { subst c,
      refine absurd (sub_nonneg.1 $ nonneg_of_mul_nonneg_left _ (sub_pos.2 hab'))
        (not_le_of_lt hma),
      have : b - a ‚àà pos_tangent_cone_at (Icc a b) a,
        from mem_pos_tangent_cone_at_of_segment_subset (segment_eq_Icc hab ‚ñ∏ subset.refl _),
      simpa [-sub_nonneg, -continuous_linear_map.map_sub]
        using hc.localize.has_fderiv_within_at_nonneg (hg a (left_mem_Icc.2 hab)) this },
    cases eq_or_lt_of_le cmem.2 with hbc hbc,
    -- Show that `c` can't be equal to `b`
    { subst c,
      refine absurd (sub_nonpos.1 $ nonpos_of_mul_nonneg_right _ (sub_lt_zero.2 hab'))
        (not_le_of_lt hmb),
      have : a - b ‚àà pos_tangent_cone_at (Icc a b) b,
        from mem_pos_tangent_cone_at_of_segment_subset (by rw [segment_symm, segment_eq_Icc hab]),
      simpa [-sub_nonneg, -continuous_linear_map.map_sub]
        using hc.localize.has_fderiv_within_at_nonneg (hg b (right_mem_Icc.2 hab)) this },
    exact ‚ü®hac, hbc‚ü© },
  use [c, cmem],
  rw [‚Üê sub_eq_zero],
  have : Icc a b ‚àà ùìù c, by rwa [‚Üê mem_interior_iff_mem_nhds, interior_Icc],
  exact (hc.is_local_min this).has_deriv_at_eq_zero ((hg c cmem).has_deriv_at this)
end

/-- **Darboux's theorem**: if `a ‚â§ b` and `f' a > m > f' b`, then `f' c = m` for some `c ‚àà [a, b]`.
-/
theorem exists_has_deriv_within_at_eq_of_lt_of_gt
  (hab : a ‚â§ b) (hf : ‚àÄ x ‚àà (Icc a b), has_deriv_within_at f (f' x) (Icc a b) x)
  {m : ‚Ñù} (hma : m < f' a) (hmb : f' b < m) :
  m ‚àà f' '' (Icc a b) :=
let ‚ü®c, cmem, hc‚ü© := exists_has_deriv_within_at_eq_of_gt_of_lt hab (Œª x hx, (hf x hx).neg)
  (neg_lt_neg hma) (neg_lt_neg hmb)
in ‚ü®c, cmem, neg_injective hc‚ü©

/-- **Darboux's theorem**: the image of a convex set under `f'` is a convex set. -/
theorem convex_image_has_deriv_at {s : set ‚Ñù} (hs : convex ‚Ñù s)
  (hf : ‚àÄ x ‚àà s, has_deriv_at f (f' x) x) :
  convex ‚Ñù (f' '' s) :=
begin
  refine ord_connected.convex ‚ü®_‚ü©,
  rintros _ ‚ü®a, ha, rfl‚ü© _ ‚ü®b, hb, rfl‚ü© m ‚ü®hma, hmb‚ü©,
  cases eq_or_lt_of_le hma with hma hma,
    by exact hma ‚ñ∏ mem_image_of_mem f' ha,
  cases eq_or_lt_of_le hmb with hmb hmb,
    by exact hmb.symm ‚ñ∏ mem_image_of_mem f' hb,
  cases le_total a b with hab hab,
  { have : Icc a b ‚äÜ s, from hs.ord_connected.out ha hb,
    rcases exists_has_deriv_within_at_eq_of_gt_of_lt hab
      (Œª x hx, (hf x $ this hx).has_deriv_within_at) hma hmb
      with ‚ü®c, cmem, hc‚ü©,
    exact ‚ü®c, this cmem, hc‚ü© },
  { have : Icc b a ‚äÜ s, from hs.ord_connected.out hb ha,
    rcases exists_has_deriv_within_at_eq_of_lt_of_gt hab
      (Œª x hx, (hf x $ this hx).has_deriv_within_at) hmb hma
      with ‚ü®c, cmem, hc‚ü©,
    exact ‚ü®c, this cmem, hc‚ü© }
end

/-- If the derivative of a function is never equal to `m`, then either
it is always greater than `m`, or it is always less than `m`. -/
theorem deriv_forall_lt_or_forall_gt_of_forall_ne {s : set ‚Ñù} (hs : convex ‚Ñù s)
  (hf : ‚àÄ x ‚àà s, has_deriv_at f (f' x) x) {m : ‚Ñù} (hf' : ‚àÄ x ‚àà s, f' x ‚â† m) :
  (‚àÄ x ‚àà s, f' x < m) ‚à® (‚àÄ x ‚àà s, m < f' x) :=
begin
  contrapose! hf',
  rcases hf' with ‚ü®‚ü®b, hb, hmb‚ü©, ‚ü®a, ha, hma‚ü©‚ü©,
  exact (convex_image_has_deriv_at hs hf).ord_connected.out (mem_image_of_mem f' ha)
    (mem_image_of_mem f' hb) ‚ü®hma, hmb‚ü©
end
