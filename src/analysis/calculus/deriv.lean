/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel

The FrÃ©chet derivative.

Let `E` and `F` be normed spaces, `f : E â†’ F`, and `f' : E â†’L[ğ•œ] F` a
continuous ğ•œ-linear map, where `ğ•œ` is a non-discrete normed field. Then

  `has_fderiv_within_at f f' s x`

says that `f` has derivative `f'` at `x`, where the domain of interest
is restricted to `s`. We also have

  `has_fderiv_at f f' x := has_fderiv_within_at f f' x univ`

The derivative is defined in terms of the `is_o` relation, but also
characterized in terms of the `tendsto` relation.

We also introduce predicates `differentiable_within_at ğ•œ f s x` (where `ğ•œ` is the base field,
`f` the function to be differentiated, `x` the point at which the derivative is asserted to exist,
and `s` the set along which the derivative is defined), as well as `differentiable_at ğ•œ f x`,
`differentiable_on ğ•œ f s` and `differentiable ğ•œ f` to express the existence of a derivative.

To be able to compute with derivatives, we write `fderiv_within ğ•œ f s x` and `fderiv ğ•œ f x`
for some choice of a derivative if it exists, and the zero function otherwise. This choice only
behaves well along sets for which the derivative is unique, i.e., those for which the tangent
directions span a dense subset of the whole space. The predicates `unique_diff_within_at s x` and
`unique_diff_on s`, defined in `tangent_cone.lean` express this property. We prove that indeed
they imply the uniqueness of the derivative. This is satisfied for open subsets, and in particular
for `univ`. This uniqueness only holds when the field is non-discrete, which we request at the very
beginning: otherwise, a derivative can be defined, but it has no interesting properties whatsoever.

In addition to the definition and basic properties of the derivative, this file contains the
usual formulas (and existence assertions) for the derivative of
* constants
* the identity
* bounded linear maps
* bounded bilinear maps
* sum of two functions
* multiplication of a function by a scalar constant
* negative of a function
* subtraction of two functions
* multiplication of a function by a scalar function
* multiplication of two scalar functions
* composition of functions (the chain rule)

-/

import analysis.asymptotics analysis.calculus.tangent_cone

open filter asymptotics continuous_linear_map set

noncomputable theory
local attribute [instance, priority 10] classical.decidable_inhabited classical.prop_decidable

set_option class.instance_max_depth 90

section

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_group F] [normed_space ğ•œ F]
variables {G : Type*} [normed_group G] [normed_space ğ•œ G]

def has_fderiv_at_filter (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) (L : filter E) :=
is_o (Î» x', f x' - f x - f' (x' - x)) (Î» x', x' - x) L

def has_fderiv_within_at (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (s : set E) (x : E) :=
has_fderiv_at_filter f f' x (nhds_within x s)

def has_fderiv_at (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) :=
has_fderiv_at_filter f f' x (nhds x)

variables (ğ•œ)

def differentiable_within_at (f : E â†’ F) (s : set E) (x : E) :=
âˆƒf' : E â†’L[ğ•œ] F, has_fderiv_within_at f f' s x

def differentiable_at (f : E â†’ F) (x : E) :=
âˆƒf' : E â†’L[ğ•œ] F, has_fderiv_at f f' x

def fderiv_within (f : E â†’ F) (s : set E) (x : E) : E â†’L[ğ•œ] F :=
if h : âˆƒf', has_fderiv_within_at f f' s x then classical.some h else 0

def fderiv (f : E â†’ F) (x : E) : E â†’L[ğ•œ] F :=
if h : âˆƒf', has_fderiv_at f f' x then classical.some h else 0

def differentiable_on (f : E â†’ F) (s : set E) :=
âˆ€x âˆˆ s, differentiable_within_at ğ•œ f s x

def differentiable (f : E â†’ F) :=
âˆ€x, differentiable_at ğ•œ f x

variables {ğ•œ}
variables {f fâ‚€ fâ‚ g : E â†’ F}
variables {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variables {x : E}
variables {s t : set E}
variables {L Lâ‚ Lâ‚‚ : filter E}


section derivative_uniqueness
/- In this section, we discuss the uniqueness of the derivative.
We prove that the definitions `unique_diff_within_at` and `unique_diff_on` indeed imply the
uniqueness of the derivative. -/

/-- `unique_diff_within_at` achieves its goal: it implies the uniqueness of the derivative. -/
theorem unique_diff_within_at.eq (H : unique_diff_within_at ğ•œ s x)
  (h : has_fderiv_within_at f f' s x) (hâ‚ : has_fderiv_within_at f fâ‚' s x) : f' = fâ‚' :=
begin
  have A : âˆ€y âˆˆ tangent_cone_at ğ•œ s x, f' y = fâ‚' y,
  { assume y hy,
    rcases hy with âŸ¨c, d, hd, hc, ylimâŸ©,
    have at_top_is_finer : at_top â‰¤ comap (Î» (n : â„•), x + d n) (nhds_within (x + 0) s),
    { rw [â†tendsto_iff_comap, nhds_within, tendsto_inf],
      split,
      { apply tendsto_add tendsto_const_nhds (tangent_cone_at.lim_zero hc ylim) },
      { rwa tendsto_principal } },
    rw add_zero at at_top_is_finer,
    have : is_o (Î» y, fâ‚' (y - x) - f' (y - x)) (Î» y, y - x) (nhds_within x s),
      by simpa using h.sub hâ‚,
    have : is_o (Î» n:â„•, fâ‚' ((x + d n) - x) - f' ((x + d n) - x)) (Î» n, (x + d n)  - x)
      ((nhds_within x s).comap (Î»n, x+ d n)) := is_o.comp this _,
    have L1 : is_o (Î» n:â„•, fâ‚' (d n) - f' (d n)) d
      ((nhds_within x s).comap (Î»n, x + d n)) := by simpa using this,
    have L2 : is_o (Î»n:â„•, fâ‚' (d n) - f' (d n)) d at_top :=
      is_o.mono at_top_is_finer L1,
    have L3 : is_o (Î»n:â„•, c n â€¢ (fâ‚' (d n) - f' (d n))) (Î»n, c n â€¢ d n) at_top :=
      is_o_smul L2,
    have L4 : is_o (Î»n:â„•, c n â€¢ (fâ‚' (d n) - f' (d n))) (Î»n, (1:â„)) at_top :=
      L3.trans_is_O (is_O_one_of_tendsto ylim),
    have L : tendsto (Î»n:â„•, c n â€¢ (fâ‚' (d n) - f' (d n))) at_top (nhds 0) :=
      is_o_one_iff.1 L4,
    have L' : tendsto (Î» (n : â„•), c n â€¢ (fâ‚' (d n) - f' (d n))) at_top (nhds (fâ‚' y - f' y)),
    { simp only [smul_sub, (continuous_linear_map.map_smul _ _ _).symm],
      apply tendsto_sub ((fâ‚'.continuous.tendsto _).comp ylim) ((f'.continuous.tendsto _).comp ylim) },
    have : fâ‚' y - f' y = 0 := tendsto_nhds_unique (by simp) L' L,
    exact (sub_eq_zero_iff_eq.1 this).symm },
  have B : âˆ€y âˆˆ submodule.span ğ•œ (tangent_cone_at ğ•œ s x), f' y = fâ‚' y,
  { assume y hy,
    apply submodule.span_induction hy,
    { exact Î»y hy, A y hy },
    { simp only [continuous_linear_map.map_zero] },
    { simp {contextual := tt} },
    { simp {contextual := tt} } },
  have C : âˆ€y âˆˆ closure ((submodule.span ğ•œ (tangent_cone_at ğ•œ s x)) : set E), f' y = fâ‚' y,
  { assume y hy,
    let K := {y | f' y = fâ‚' y},
    have : (submodule.span ğ•œ (tangent_cone_at ğ•œ s x) : set E) âŠ† K := B,
    have : closure (submodule.span ğ•œ (tangent_cone_at ğ•œ s x) : set E) âŠ† closure K :=
      closure_mono this,
    have : y âˆˆ closure K := this hy,
    rwa closure_eq_of_is_closed (is_closed_eq f'.continuous fâ‚'.continuous) at this },
  rw H.1 at C,
  ext y,
  exact C y (mem_univ _)
end

theorem unique_diff_on.eq (H : unique_diff_on ğ•œ s) (hx : x âˆˆ s)
  (h : has_fderiv_within_at f f' s x) (hâ‚ : has_fderiv_within_at f fâ‚' s x) : f' = fâ‚' :=
unique_diff_within_at.eq (H x hx) h hâ‚

end derivative_uniqueness

/- Basic properties of the derivative -/
section fderiv_properties

theorem has_fderiv_at_filter_iff_tendsto :
  has_fderiv_at_filter f f' x L â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) L (nhds 0) :=
have h : âˆ€ x', âˆ¥x' - xâˆ¥ = 0 â†’ âˆ¥f x' - f x - f' (x' - x)âˆ¥ = 0, from Î» x' hx',
  by { rw [sub_eq_zero.1 ((norm_eq_zero (x' - x)).1 hx')], simp },
begin
  unfold has_fderiv_at_filter,
  rw [â†is_o_norm_left, â†is_o_norm_right, is_o_iff_tendsto h],
  exact tendsto.congr'r (Î» _, div_eq_inv_mul),
end

theorem has_fderiv_within_at_iff_tendsto : has_fderiv_within_at f f' s x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) (nhds_within x s) (nhds 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_fderiv_at_iff_tendsto : has_fderiv_at f f' x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) (nhds x) (nhds 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_fderiv_at_filter.mono (h : has_fderiv_at_filter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
  has_fderiv_at_filter f f' x Lâ‚ :=
is_o.mono hst h

theorem has_fderiv_within_at.mono (h : has_fderiv_within_at f f' t x) (hst : s âŠ† t) :
  has_fderiv_within_at f f' s x :=
h.mono (nhds_within_mono _ hst)

theorem has_fderiv_at.has_fderiv_at_filter (h : has_fderiv_at f f' x) (hL : L â‰¤ nhds x) :
  has_fderiv_at_filter f f' x L :=
h.mono hL

theorem has_fderiv_at.has_fderiv_within_at
  (h : has_fderiv_at f f' x) : has_fderiv_within_at f f' s x :=
h.has_fderiv_at_filter lattice.inf_le_left

lemma has_fderiv_within_at.differentiable_within_at (h : has_fderiv_within_at f f' s x) :
  differentiable_within_at ğ•œ f s x :=
âŸ¨f', hâŸ©

lemma has_fderiv_at.differentiable_at (h : has_fderiv_at f f' x) : differentiable_at ğ•œ f x :=
âŸ¨f', hâŸ©

@[simp] lemma has_fderiv_within_at_univ :
  has_fderiv_within_at f f' univ x â†” has_fderiv_at f f' x :=
by { simp only [has_fderiv_within_at, nhds_within_univ], refl }

theorem has_fderiv_at_unique
  (hâ‚€ : has_fderiv_at f fâ‚€' x) (hâ‚ : has_fderiv_at f fâ‚' x) : fâ‚€' = fâ‚' :=
begin
  rw â† has_fderiv_within_at_univ at hâ‚€ hâ‚,
  exact unique_diff_within_at_univ.eq hâ‚€ hâ‚
end

lemma has_fderiv_within_at_inter' (h : t âˆˆ nhds_within x s) :
  has_fderiv_within_at f f' (s âˆ© t) x â†” has_fderiv_within_at f f' s x :=
by simp [has_fderiv_within_at, nhds_within_restrict'' s h]

lemma has_fderiv_within_at_inter (h : t âˆˆ nhds x) :
  has_fderiv_within_at f f' (s âˆ© t) x â†” has_fderiv_within_at f f' s x :=
by simp [has_fderiv_within_at, nhds_within_restrict' s h]

lemma differentiable_within_at.has_fderiv_within_at (h : differentiable_within_at ğ•œ f s x) :
  has_fderiv_within_at f (fderiv_within ğ•œ f s x) s x :=
begin
  dunfold fderiv_within,
  dunfold differentiable_within_at at h,
  rw dif_pos h,
  exact classical.some_spec h
end

lemma differentiable_at.has_fderiv_at (h : differentiable_at ğ•œ f x) :
  has_fderiv_at f (fderiv ğ•œ f x) x :=
begin
  dunfold fderiv,
  dunfold differentiable_at at h,
  rw dif_pos h,
  exact classical.some_spec h
end

lemma has_fderiv_at.fderiv (h : has_fderiv_at f f' x) : fderiv ğ•œ f x = f' :=
by { ext, rw has_fderiv_at_unique h h.differentiable_at.has_fderiv_at }

lemma has_fderiv_within_at.fderiv_within
  (h : has_fderiv_within_at f f' s x) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ f s x = f' :=
by { ext, rw hxs.eq h h.differentiable_within_at.has_fderiv_within_at }

lemma differentiable_within_at.mono (h : differentiable_within_at ğ•œ f t x) (st : s âŠ† t) :
  differentiable_within_at ğ•œ f s x :=
begin
  rcases h with âŸ¨f', hf'âŸ©,
  exact âŸ¨f', hf'.mono stâŸ©
end

lemma differentiable_within_at_univ :
  differentiable_within_at ğ•œ f univ x â†” differentiable_at ğ•œ f x :=
begin
  simp [differentiable_within_at, has_fderiv_within_at, nhds_within_univ],
  refl
end

lemma differentiable_within_at_inter (ht : t âˆˆ nhds x) :
  differentiable_within_at ğ•œ f (s âˆ© t) x â†” differentiable_within_at ğ•œ f s x :=
by simp only [differentiable_within_at, has_fderiv_within_at, has_fderiv_at_filter,
    nhds_within_restrict' s ht]

lemma differentiable_at.differentiable_within_at
  (h : differentiable_at ğ•œ f x) : differentiable_within_at ğ•œ f s x :=
(differentiable_within_at_univ.2 h).mono (subset_univ _)

lemma differentiable_within_at.differentiable_at
  (h : differentiable_within_at ğ•œ f s x) (hs : s âˆˆ nhds x) : differentiable_at ğ•œ f x :=
begin
  have : s = univ âˆ© s, by rw univ_inter,
  rwa [this, differentiable_within_at_inter hs, differentiable_within_at_univ] at h
end

lemma differentiable.fderiv_within
  (h : differentiable_at ğ•œ f x) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ f s x = fderiv ğ•œ f x :=
begin
  apply has_fderiv_within_at.fderiv_within _ hxs,
  exact h.has_fderiv_at.has_fderiv_within_at
end

lemma differentiable_on.mono (h : differentiable_on ğ•œ f t) (st : s âŠ† t) :
  differentiable_on ğ•œ f s :=
Î»x hx, (h x (st hx)).mono st

lemma differentiable_on_univ :
  differentiable_on ğ•œ f univ â†” differentiable ğ•œ f :=
by { simp [differentiable_on, differentiable_within_at_univ], refl }

lemma differentiable.differentiable_on (h : differentiable ğ•œ f) : differentiable_on ğ•œ f s :=
(differentiable_on_univ.2 h).mono (subset_univ _)

lemma differentiable_on_of_locally_differentiable_on
  (h : âˆ€xâˆˆs, âˆƒu, is_open u âˆ§ x âˆˆ u âˆ§ differentiable_on ğ•œ f (s âˆ© u)) : differentiable_on ğ•œ f s :=
begin
  assume x xs,
  rcases h x xs with âŸ¨t, t_open, xt, htâŸ©,
  exact (differentiable_within_at_inter (mem_nhds_sets t_open xt)).1 (ht x âŸ¨xs, xtâŸ©)
end

lemma fderiv_within_subset (st : s âŠ† t) (ht : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f t x) :
  fderiv_within ğ•œ f s x = fderiv_within ğ•œ f t x :=
((differentiable_within_at.has_fderiv_within_at h).mono st).fderiv_within ht

@[simp] lemma fderiv_within_univ : fderiv_within ğ•œ f univ = fderiv ğ•œ f :=
begin
  ext x : 1,
  by_cases h : differentiable_at ğ•œ f x,
  { apply has_fderiv_within_at.fderiv_within _ (is_open_univ.unique_diff_within_at (mem_univ _)),
    rw has_fderiv_within_at_univ,
    apply h.has_fderiv_at },
  { have : fderiv ğ•œ f x = 0,
      by { unfold differentiable_at at h, simp [fderiv, h] },
    rw this,
    have : Â¬(differentiable_within_at ğ•œ f univ x), by rwa differentiable_within_at_univ,
    unfold differentiable_within_at at this,
    simp [fderiv_within, this, -has_fderiv_within_at_univ] }
end

lemma fderiv_within_inter (ht : t âˆˆ nhds x) (hs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ f (s âˆ© t) x = fderiv_within ğ•œ f s x :=
begin
  by_cases h : differentiable_within_at ğ•œ f (s âˆ© t) x,
  { apply fderiv_within_subset (inter_subset_left _ _) _ ((differentiable_within_at_inter ht).1 h),
    apply hs.inter ht },
  { have : fderiv_within ğ•œ f (s âˆ© t) x = 0,
      by { unfold differentiable_within_at at h, simp [fderiv_within, h] },
    rw this,
    rw differentiable_within_at_inter ht at h,
    have : fderiv_within ğ•œ f s x = 0,
      by { unfold differentiable_within_at at h, simp [fderiv_within, h] },
    rw this }
end

end fderiv_properties

/- Congr -/
section congr

theorem has_fderiv_at_filter_congr_of_mem_sets
  (hx : fâ‚€ x = fâ‚ x) (hâ‚€ : {x | fâ‚€ x = fâ‚ x} âˆˆ L) (hâ‚ : âˆ€ x, fâ‚€' x = fâ‚' x) :
  has_fderiv_at_filter fâ‚€ fâ‚€' x L â†” has_fderiv_at_filter fâ‚ fâ‚' x L :=
by { rw (ext hâ‚), exact is_o_congr
  (by filter_upwards [hâ‚€] Î» x (h : _ = _), by simp [h, hx])
  (univ_mem_sets' $ Î» _, rfl) }

lemma has_fderiv_at_filter.congr_of_mem_sets (h : has_fderiv_at_filter f f' x L)
  (hL : {x | fâ‚ x = f x} âˆˆ L) (hx : fâ‚ x = f x) : has_fderiv_at_filter fâ‚ f' x L :=
begin
  apply (has_fderiv_at_filter_congr_of_mem_sets hx hL _).2 h,
  exact Î»x, rfl
end

lemma has_fderiv_within_at.congr_mono (h : has_fderiv_within_at f f' s x) (ht : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : has_fderiv_within_at fâ‚ f' t x :=
has_fderiv_at_filter.congr_of_mem_sets (h.mono hâ‚) (filter.mem_inf_sets_of_right ht) hx

lemma has_fderiv_within_at.congr_of_mem_nhds_within (h : has_fderiv_within_at f f' s x)
  (hâ‚ : {y | fâ‚ y = f y} âˆˆ nhds_within x s) (hx : fâ‚ x = f x) : has_fderiv_within_at fâ‚ f' s x :=
has_fderiv_at_filter.congr_of_mem_sets h hâ‚ hx

lemma has_fderiv_at.congr_of_mem_nhds (h : has_fderiv_at f f' x)
  (hâ‚ : {y | fâ‚ y = f y} âˆˆ nhds x) : has_fderiv_at fâ‚ f' x :=
has_fderiv_at_filter.congr_of_mem_sets h hâ‚ (mem_of_nhds hâ‚ : _)

lemma differentiable_within_at.congr_mono (h : differentiable_within_at ğ•œ f s x)
  (ht : âˆ€x âˆˆ t, fâ‚ x = f x) (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : differentiable_within_at ğ•œ fâ‚ t x :=
(has_fderiv_within_at.congr_mono h.has_fderiv_within_at ht hx hâ‚).differentiable_within_at

lemma differentiable_within_at.congr_of_mem_nhds_within
  (h : differentiable_within_at ğ•œ f s x) (hâ‚ : {y | fâ‚ y = f y} âˆˆ nhds_within x s)
  (hx : fâ‚ x = f x) : differentiable_within_at ğ•œ fâ‚ s x :=
(h.has_fderiv_within_at.congr_of_mem_nhds_within hâ‚ hx).differentiable_within_at

lemma differentiable_on.congr_mono (h : differentiable_on ğ•œ f s) (h' : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hâ‚ : t âŠ† s) : differentiable_on ğ•œ fâ‚ t :=
Î» x hx, (h x (hâ‚ hx)).congr_mono h' (h' x hx) hâ‚

lemma differentiable_at.congr_of_mem_nhds (h : differentiable_at ğ•œ f x)
  (hL : {y | fâ‚ y = f y} âˆˆ nhds x) : differentiable_at ğ•œ fâ‚ x :=
has_fderiv_at.differentiable_at (has_fderiv_at_filter.congr_of_mem_sets h.has_fderiv_at hL (mem_of_nhds hL : _))

lemma differentiable_within_at.fderiv_within_congr_mono (h : differentiable_within_at ğ•œ f s x)
  (hs : âˆ€x âˆˆ t, fâ‚ x = f x) (hx : fâ‚ x = f x) (hxt : unique_diff_within_at ğ•œ t x) (hâ‚ : t âŠ† s) :
  fderiv_within ğ•œ fâ‚ t x = fderiv_within ğ•œ f s x :=
(has_fderiv_within_at.congr_mono h.has_fderiv_within_at hs hx hâ‚).fderiv_within hxt

lemma fderiv_within_congr_of_mem_nhds_within (hs : unique_diff_within_at ğ•œ s x)
  (hL : {y | fâ‚ y = f y} âˆˆ nhds_within x s) (hx : fâ‚ x = f x) :
  fderiv_within ğ•œ fâ‚ s x = fderiv_within ğ•œ f s x :=
begin
  by_cases h : differentiable_within_at ğ•œ f s x âˆ¨ differentiable_within_at ğ•œ fâ‚ s x,
  { cases h,
    { apply has_fderiv_within_at.fderiv_within _ hs,
      exact has_fderiv_at_filter.congr_of_mem_sets h.has_fderiv_within_at hL hx },
    { symmetry,
      apply has_fderiv_within_at.fderiv_within _ hs,
      apply has_fderiv_at_filter.congr_of_mem_sets h.has_fderiv_within_at _ hx.symm,
      convert hL,
      ext y,
      exact eq_comm } },
  { push_neg at h,
    have A : fderiv_within ğ•œ f s x = 0,
      by { unfold differentiable_within_at at h, simp [fderiv_within, h] },
    have Aâ‚ : fderiv_within ğ•œ fâ‚ s x = 0,
      by { unfold differentiable_within_at at h, simp [fderiv_within, h] },
    rw [A, Aâ‚] }
end

lemma fderiv_within_congr (hs : unique_diff_within_at ğ•œ s x)
  (hL : âˆ€yâˆˆs, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  fderiv_within ğ•œ fâ‚ s x = fderiv_within ğ•œ f s x :=
begin
  apply fderiv_within_congr_of_mem_nhds_within hs _ hx,
  apply mem_sets_of_superset self_mem_nhds_within,
  exact hL
end

lemma fderiv_congr_of_mem_nhds (hL : {y | fâ‚ y = f y} âˆˆ nhds x) :
  fderiv ğ•œ fâ‚ x = fderiv ğ•œ f x :=
begin
  have A : fâ‚ x = f x := (mem_of_nhds hL : _),
  rw [â† fderiv_within_univ, â† fderiv_within_univ],
  rw â† nhds_within_univ at hL,
  exact fderiv_within_congr_of_mem_nhds_within unique_diff_within_at_univ hL A
end

end congr

/- id -/
section id

theorem has_fderiv_at_filter_id (x : E) (L : filter E) :
  has_fderiv_at_filter id (id : E â†’L[ğ•œ] E) x L :=
(is_o_zero _ _).congr_left $ by simp

theorem has_fderiv_within_at_id (x : E) (s : set E) :
  has_fderiv_within_at id (id : E â†’L[ğ•œ] E) s x :=
has_fderiv_at_filter_id _ _

theorem has_fderiv_at_id (x : E) : has_fderiv_at id (id : E â†’L[ğ•œ] E) x :=
has_fderiv_at_filter_id _ _

lemma differentiable_at_id : differentiable_at ğ•œ id x :=
(has_fderiv_at_id x).differentiable_at

lemma differentiable_within_at_id : differentiable_within_at ğ•œ id s x :=
differentiable_at_id.differentiable_within_at

lemma differentiable_id : differentiable ğ•œ (id : E â†’ E) :=
Î»x, differentiable_at_id

lemma differentiable_on_id : differentiable_on ğ•œ id s :=
differentiable_id.differentiable_on

lemma fderiv_id : fderiv ğ•œ id x = id :=
has_fderiv_at.fderiv (has_fderiv_at_id x)

lemma fderiv_within_id (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ id s x = id :=
begin
  rw differentiable.fderiv_within (differentiable_at_id) hxs,
  exact fderiv_id
end

end id

/- constants -/
section const

theorem has_fderiv_at_filter_const (c : F) (x : E) (L : filter E) :
  has_fderiv_at_filter (Î» x, c) (0 : E â†’L[ğ•œ] F) x L :=
(is_o_zero _ _).congr_left $ Î» _, by simp only [zero_apply, sub_self]

theorem has_fderiv_within_at_const (c : F) (x : E) (s : set E) :
  has_fderiv_within_at (Î» x, c) (0 : E â†’L[ğ•œ] F) s x :=
has_fderiv_at_filter_const _ _ _

theorem has_fderiv_at_const (c : F) (x : E) :
  has_fderiv_at (Î» x, c) (0 : E â†’L[ğ•œ] F) x :=
has_fderiv_at_filter_const _ _ _

lemma differentiable_at_const (c : F) : differentiable_at ğ•œ (Î»x, c) x :=
âŸ¨0, has_fderiv_at_const c xâŸ©

lemma differentiable_within_at_const (c : F) : differentiable_within_at ğ•œ (Î»x, c) s x :=
differentiable_at.differentiable_within_at (differentiable_at_const _)

lemma fderiv_const (c : F) : fderiv ğ•œ (Î»y, c) x = 0 :=
has_fderiv_at.fderiv (has_fderiv_at_const c x)

lemma fderiv_within_const (c : F) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»y, c) s x = 0 :=
begin
  rw differentiable.fderiv_within (differentiable_at_const _) hxs,
  exact fderiv_const _
end

lemma differentiable_const (c : F) : differentiable ğ•œ (Î»x : E, c) :=
Î»x, differentiable_at_const _

lemma differentiable_on_const (c : F) : differentiable_on ğ•œ (Î»x, c) s :=
(differentiable_const _).differentiable_on

end const

/- Bounded linear maps -/
section is_bounded_linear_map

lemma is_bounded_linear_map.has_fderiv_at_filter (h : is_bounded_linear_map ğ•œ f) :
  has_fderiv_at_filter f h.to_continuous_linear_map x L :=
begin
  have : (Î» (x' : E), f x' - f x - h.to_continuous_linear_map (x' - x)) = Î»x', 0,
  { ext,
    have : âˆ€a, h.to_continuous_linear_map a = f a := Î»a, rfl,
    simp,
    simp [this] },
  rw [has_fderiv_at_filter, this],
  exact asymptotics.is_o_zero _ _
end

lemma is_bounded_linear_map.has_fderiv_within_at (h : is_bounded_linear_map ğ•œ f) :
  has_fderiv_within_at f h.to_continuous_linear_map s x :=
h.has_fderiv_at_filter

lemma is_bounded_linear_map.has_fderiv_at (h : is_bounded_linear_map ğ•œ f) :
  has_fderiv_at f h.to_continuous_linear_map x  :=
h.has_fderiv_at_filter

lemma is_bounded_linear_map.differentiable_at (h : is_bounded_linear_map ğ•œ f) :
  differentiable_at ğ•œ f x :=
h.has_fderiv_at.differentiable_at

lemma is_bounded_linear_map.differentiable_within_at (h : is_bounded_linear_map ğ•œ f) :
  differentiable_within_at ğ•œ f s x :=
h.differentiable_at.differentiable_within_at

lemma is_bounded_linear_map.fderiv (h : is_bounded_linear_map ğ•œ f) :
  fderiv ğ•œ f x = h.to_continuous_linear_map :=
has_fderiv_at.fderiv (h.has_fderiv_at)

lemma is_bounded_linear_map.fderiv_within (h : is_bounded_linear_map ğ•œ f)
  (hxs : unique_diff_within_at ğ•œ s x) : fderiv_within ğ•œ f s x = h.to_continuous_linear_map :=
begin
  rw differentiable.fderiv_within h.differentiable_at hxs,
  exact h.fderiv
end

lemma is_bounded_linear_map.differentiable (h : is_bounded_linear_map ğ•œ f) :
  differentiable ğ•œ f :=
Î»x, h.differentiable_at

lemma is_bounded_linear_map.differentiable_on (h : is_bounded_linear_map ğ•œ f) :
  differentiable_on ğ•œ f s :=
h.differentiable.differentiable_on

end is_bounded_linear_map

/- multiplication by a constant -/
section smul_const

theorem has_fderiv_at_filter.smul (h : has_fderiv_at_filter f f' x L) (c : ğ•œ) :
  has_fderiv_at_filter (Î» x, c â€¢ f x) (c â€¢ f') x L :=
(is_o_const_smul_left h c).congr_left $ Î» x, by simp [smul_neg, smul_add]

theorem has_fderiv_within_at.smul (h : has_fderiv_within_at f f' s x) (c : ğ•œ) :
  has_fderiv_within_at (Î» x, c â€¢ f x) (c â€¢ f') s x :=
h.smul c

theorem has_fderiv_at.smul (h : has_fderiv_at f f' x) (c : ğ•œ) :
  has_fderiv_at (Î» x, c â€¢ f x) (c â€¢ f') x :=
h.smul c

lemma differentiable_within_at.smul (h : differentiable_within_at ğ•œ f s x) (c : ğ•œ) :
  differentiable_within_at ğ•œ (Î»y, c â€¢ f y) s x :=
(h.has_fderiv_within_at.smul c).differentiable_within_at

lemma differentiable_at.smul (h : differentiable_at ğ•œ f x) (c : ğ•œ) :
  differentiable_at ğ•œ (Î»y, c â€¢ f y) x :=
(h.has_fderiv_at.smul c).differentiable_at

lemma differentiable_on.smul (h : differentiable_on ğ•œ f s) (c : ğ•œ) :
  differentiable_on ğ•œ (Î»y, c â€¢ f y) s :=
Î»x hx, (h x hx).smul c

lemma differentiable.smul (h : differentiable ğ•œ f) (c : ğ•œ) :
  differentiable ğ•œ (Î»y, c â€¢ f y) :=
Î»x, (h x).smul c

lemma fderiv_within_smul (hxs : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f s x) (c : ğ•œ) :
  fderiv_within ğ•œ (Î»y, c â€¢ f y) s x = c â€¢ fderiv_within ğ•œ f s x :=
(h.has_fderiv_within_at.smul c).fderiv_within hxs

lemma fderiv_smul (h : differentiable_at ğ•œ f x) (c : ğ•œ) :
  fderiv ğ•œ (Î»y, c â€¢ f y) x = c â€¢ fderiv ğ•œ f x :=
(h.has_fderiv_at.smul c).fderiv

end smul_const

/- add -/
section add

theorem has_fderiv_at_filter.add
  (hf : has_fderiv_at_filter f f' x L) (hg : has_fderiv_at_filter g g' x L) :
  has_fderiv_at_filter (Î» y, f y + g y) (f' + g') x L :=
(hf.add hg).congr_left $ Î» _, by simp

theorem has_fderiv_within_at.add
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» y, f y + g y) (f' + g') s x :=
hf.add hg

theorem has_fderiv_at.add
  (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» x, f x + g x) (f' + g') x :=
hf.add hg

lemma differentiable_within_at.add
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  differentiable_within_at ğ•œ (Î» y, f y + g y) s x :=
(hf.has_fderiv_within_at.add hg.has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  differentiable_at ğ•œ (Î» y, f y + g y) x :=
(hf.has_fderiv_at.add hg.has_fderiv_at).differentiable_at

lemma differentiable_on.add
  (hf : differentiable_on ğ•œ f s) (hg : differentiable_on ğ•œ g s) :
  differentiable_on ğ•œ (Î»y, f y + g y) s :=
Î»x hx, (hf x hx).add (hg x hx)

lemma differentiable.add
  (hf : differentiable ğ•œ f) (hg : differentiable ğ•œ g) :
  differentiable ğ•œ (Î»y, f y + g y) :=
Î»x, (hf x).add (hg x)

lemma fderiv_within_add (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  fderiv_within ğ•œ (Î»y, f y + g y) s x = fderiv_within ğ•œ f s x + fderiv_within ğ•œ g s x :=
(hf.has_fderiv_within_at.add hg.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  fderiv ğ•œ (Î»y, f y + g y) x = fderiv ğ•œ f x + fderiv ğ•œ g x :=
(hf.has_fderiv_at.add hg.has_fderiv_at).fderiv

end add

/- neg -/
section neg

theorem has_fderiv_at_filter.neg (h : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (Î» x, -f x) (-f') x L :=
(h.smul (-1:ğ•œ)).congr (by simp) (by simp)

theorem has_fderiv_within_at.neg (h : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Î» x, -f x) (-f') s x :=
h.neg

theorem has_fderiv_at.neg (h : has_fderiv_at f f' x) :
  has_fderiv_at (Î» x, -f x) (-f') x :=
h.neg

lemma differentiable_within_at.neg (h : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î»y, -f y) s x :=
h.has_fderiv_within_at.neg.differentiable_within_at

lemma differentiable_at.neg (h : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î»y, -f y) x :=
h.has_fderiv_at.neg.differentiable_at

lemma differentiable_on.neg (h : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î»y, -f y) s :=
Î»x hx, (h x hx).neg

lemma differentiable.neg (h : differentiable ğ•œ f) :
  differentiable ğ•œ (Î»y, -f y) :=
Î»x, (h x).neg

lemma fderiv_within_neg (hxs : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f s x) :
  fderiv_within ğ•œ (Î»y, -f y) s x = - fderiv_within ğ•œ f s x :=
h.has_fderiv_within_at.neg.fderiv_within hxs

lemma fderiv_neg (h : differentiable_at ğ•œ f x) :
  fderiv ğ•œ (Î»y, -f y) x = - fderiv ğ•œ f x :=
h.has_fderiv_at.neg.fderiv

end neg

/- sub -/
section sub

theorem has_fderiv_at_filter.sub
  (hf : has_fderiv_at_filter f f' x L) (hg : has_fderiv_at_filter g g' x L) :
  has_fderiv_at_filter (Î» x, f x - g x) (f' - g') x L :=
hf.add hg.neg

theorem has_fderiv_within_at.sub
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» x, f x - g x) (f' - g') s x :=
hf.sub hg

theorem has_fderiv_at.sub
  (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» x, f x - g x) (f' - g') x :=
hf.sub hg

lemma differentiable_within_at.sub
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  differentiable_within_at ğ•œ (Î» y, f y - g y) s x :=
(hf.has_fderiv_within_at.sub hg.has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  differentiable_at ğ•œ (Î» y, f y - g y) x :=
(hf.has_fderiv_at.sub hg.has_fderiv_at).differentiable_at

lemma differentiable_on.sub
  (hf : differentiable_on ğ•œ f s) (hg : differentiable_on ğ•œ g s) :
  differentiable_on ğ•œ (Î»y, f y - g y) s :=
Î»x hx, (hf x hx).sub (hg x hx)

lemma differentiable.sub
  (hf : differentiable ğ•œ f) (hg : differentiable ğ•œ g) :
  differentiable ğ•œ (Î»y, f y - g y) :=
Î»x, (hf x).sub (hg x)

lemma fderiv_within_sub (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  fderiv_within ğ•œ (Î»y, f y - g y) s x = fderiv_within ğ•œ f s x - fderiv_within ğ•œ g s x :=
(hf.has_fderiv_within_at.sub hg.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  fderiv ğ•œ (Î»y, f y - g y) x = fderiv ğ•œ f x - fderiv ğ•œ g x :=
(hf.has_fderiv_at.sub hg.has_fderiv_at).fderiv

theorem has_fderiv_at_filter.is_O_sub (h : has_fderiv_at_filter f f' x L) :
is_O (Î» x', f x' - f x) (Î» x', x' - x) L :=
h.to_is_O.congr_of_sub.2 (f'.is_O_sub _ _)

end sub

/- Continuity -/
section continuous

theorem has_fderiv_at_filter.tendsto_nhds
  (hL : L â‰¤ nhds x) (h : has_fderiv_at_filter f f' x L) :
  tendsto f L (nhds (f x)) :=
begin
  have : tendsto (Î» x', f x' - f x) L (nhds 0),
  { refine h.is_O_sub.trans_tendsto (tendsto_le_left hL _),
    rw â† sub_self x, exact tendsto_sub tendsto_id tendsto_const_nhds },
  have := tendsto_add this tendsto_const_nhds,
  rw zero_add (f x) at this,
  exact this.congr (by simp)
end

theorem has_fderiv_within_at.continuous_within_at
  (h : has_fderiv_within_at f f' s x) : continuous_within_at f s x :=
has_fderiv_at_filter.tendsto_nhds lattice.inf_le_left h

theorem has_fderiv_at.continuous_at (h : has_fderiv_at f f' x) :
  continuous_at f x :=
has_fderiv_at_filter.tendsto_nhds (le_refl _) h

lemma differentiable_within_at.continuous_within_at (h : differentiable_within_at ğ•œ f s x) :
  continuous_within_at f s x :=
let âŸ¨f', hf'âŸ© := h in hf'.continuous_within_at

lemma differentiable_at.continuous_at (h : differentiable_at ğ•œ f x) : continuous_at f x :=
let âŸ¨f', hf'âŸ© := h in hf'.continuous_at

lemma differentiable_on.continuous_on (h : differentiable_on ğ•œ f s) : continuous_on f s :=
Î»x hx, (h x hx).continuous_within_at

lemma differentiable.continuous (h : differentiable ğ•œ f) : continuous f :=
continuous_iff_continuous_at.2 $ Î»x, (h x).continuous_at

end continuous

/- Bounded bilinear maps -/

section bilinear_map
variables {b : E Ã— F â†’ G} {u : set (E Ã— F) }

lemma is_bounded_bilinear_map.has_fderiv_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_fderiv_at b (h.deriv p) p :=
begin
  have : (Î» (x : E Ã— F), b x - b p - (h.deriv p) (x - p)) = (Î»x, b (x.1 - p.1, x.2 - p.2)),
  { ext x,
    delta is_bounded_bilinear_map.deriv,
    change b x - b p - (b (p.1, x.2-p.2) + b (x.1-p.1, p.2))
           = b (x.1 - p.1, x.2 - p.2),
    have : b x = b (x.1, x.2), by { cases x, refl },
    rw this,
    have : b p = b (p.1, p.2), by { cases p, refl },
    rw this,
    simp only [h.map_sub_left, h.map_sub_right],
    abel },
  rw [has_fderiv_at, has_fderiv_at_filter, this],
  rcases h.bound with âŸ¨C, Cpos, hCâŸ©,
  have A : asymptotics.is_O (Î»x : E Ã— F, b (x.1 - p.1, x.2 - p.2))
    (Î»x, âˆ¥x - pâˆ¥ * âˆ¥x - pâˆ¥) (nhds p) :=
  âŸ¨C, Cpos, filter.univ_mem_sets' (Î»x, begin
    simp only [mem_set_of_eq, norm_mul, norm_norm],
    calc âˆ¥b (x.1 - p.1, x.2 - p.2)âˆ¥ â‰¤ C * âˆ¥x.1 - p.1âˆ¥ * âˆ¥x.2 - p.2âˆ¥ : hC _ _
    ... â‰¤ C * âˆ¥x-pâˆ¥ * âˆ¥x-pâˆ¥ : by apply_rules [mul_le_mul, le_max_left, le_max_right, norm_nonneg,
      le_of_lt Cpos, le_refl, mul_nonneg, norm_nonneg, norm_nonneg]
    ... = C * (âˆ¥x-pâˆ¥ * âˆ¥x-pâˆ¥) : mul_assoc _ _ _ end)âŸ©,
  have B : asymptotics.is_o (Î» (x : E Ã— F), âˆ¥x - pâˆ¥ * âˆ¥x - pâˆ¥)
    (Î»x, 1 * âˆ¥x - pâˆ¥) (nhds p),
  { apply asymptotics.is_o_mul_right _ (asymptotics.is_O_refl _ _),
    rw [asymptotics.is_o_iff_tendsto],
    { simp only [div_one],
      have : 0 = âˆ¥p - pâˆ¥, by simp,
      rw this,
      have : continuous (Î»x, âˆ¥x-pâˆ¥) :=
        continuous_norm.comp (continuous_sub continuous_id continuous_const),
      exact this.tendsto p },
    simp only [forall_prop_of_false, not_false_iff, one_ne_zero, forall_true_iff] },
  simp only [one_mul, asymptotics.is_o_norm_right] at B,
  exact A.trans_is_o B
end

lemma is_bounded_bilinear_map.has_fderiv_within_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_fderiv_within_at b (h.deriv p) u p :=
(h.has_fderiv_at p).has_fderiv_within_at

lemma is_bounded_bilinear_map.differentiable_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  differentiable_at ğ•œ b p :=
(h.has_fderiv_at p).differentiable_at

lemma is_bounded_bilinear_map.differentiable_within_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  differentiable_within_at ğ•œ b u p :=
(h.differentiable_at p).differentiable_within_at

lemma is_bounded_bilinear_map.fderiv (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  fderiv ğ•œ b p = h.deriv p :=
has_fderiv_at.fderiv (h.has_fderiv_at p)

lemma is_bounded_bilinear_map.fderiv_within (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F)
  (hxs : unique_diff_within_at ğ•œ u p) : fderiv_within ğ•œ b u p = h.deriv p :=
begin
  rw differentiable.fderiv_within (h.differentiable_at p) hxs,
  exact h.fderiv p
end

lemma is_bounded_bilinear_map.differentiable (h : is_bounded_bilinear_map ğ•œ b) :
  differentiable ğ•œ b :=
Î»x, h.differentiable_at x

lemma is_bounded_bilinear_map.differentiable_on (h : is_bounded_bilinear_map ğ•œ b) :
  differentiable_on ğ•œ b u :=
h.differentiable.differentiable_on

lemma is_bounded_bilinear_map.continuous (h : is_bounded_bilinear_map ğ•œ b) :
  continuous b :=
h.differentiable.continuous

end bilinear_map


/- Cartesian products -/
section cartesian_product
variables {fâ‚‚ : E â†’ G} {fâ‚‚' : E â†’L[ğ•œ] G}

lemma has_fderiv_at_filter.prod
  (hfâ‚ : has_fderiv_at_filter fâ‚ fâ‚' x L) (hfâ‚‚ : has_fderiv_at_filter fâ‚‚ fâ‚‚' x L) :
  has_fderiv_at_filter (Î»x, (fâ‚ x, fâ‚‚ x)) (continuous_linear_map.prod fâ‚' fâ‚‚') x L :=
begin
  have : (Î» (x' : E), (fâ‚ x', fâ‚‚ x') - (fâ‚ x, fâ‚‚ x) - (continuous_linear_map.prod fâ‚' fâ‚‚') (x' -x)) =
           (Î» (x' : E), (fâ‚ x' - fâ‚ x - fâ‚' (x' - x), fâ‚‚ x' - fâ‚‚ x - fâ‚‚' (x' - x))) := rfl,
  rw [has_fderiv_at_filter, this],
  rw [asymptotics.is_o_prod_left],
  exact âŸ¨hfâ‚, hfâ‚‚âŸ©
end

lemma has_fderiv_within_at.prod
  (hfâ‚ : has_fderiv_within_at fâ‚ fâ‚' s x) (hfâ‚‚ : has_fderiv_within_at fâ‚‚ fâ‚‚' s x) :
  has_fderiv_within_at (Î»x, (fâ‚ x, fâ‚‚ x)) (continuous_linear_map.prod fâ‚' fâ‚‚') s x :=
hfâ‚.prod hfâ‚‚

lemma has_fderiv_at.prod (hfâ‚ : has_fderiv_at fâ‚ fâ‚' x) (hfâ‚‚ : has_fderiv_at fâ‚‚ fâ‚‚' x) :
  has_fderiv_at (Î»x, (fâ‚ x, fâ‚‚ x)) (continuous_linear_map.prod fâ‚' fâ‚‚') x :=
hfâ‚.prod hfâ‚‚

lemma differentiable_within_at.prod
  (hfâ‚ : differentiable_within_at ğ•œ fâ‚ s x) (hfâ‚‚ : differentiable_within_at ğ•œ fâ‚‚ s x) :
  differentiable_within_at ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) s x :=
(hfâ‚.has_fderiv_within_at.prod hfâ‚‚.has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.prod (hfâ‚ : differentiable_at ğ•œ fâ‚ x) (hfâ‚‚ : differentiable_at ğ•œ fâ‚‚ x) :
  differentiable_at ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) x :=
(hfâ‚.has_fderiv_at.prod hfâ‚‚.has_fderiv_at).differentiable_at

lemma differentiable_on.prod (hfâ‚ : differentiable_on ğ•œ fâ‚ s) (hfâ‚‚ : differentiable_on ğ•œ fâ‚‚ s) :
  differentiable_on ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) s :=
Î»x hx, differentiable_within_at.prod (hfâ‚ x hx) (hfâ‚‚ x hx)

lemma differentiable.prod (hfâ‚ : differentiable ğ•œ fâ‚) (hfâ‚‚ : differentiable ğ•œ fâ‚‚) :
  differentiable ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) :=
Î» x, differentiable_at.prod (hfâ‚ x) (hfâ‚‚ x)

lemma differentiable_at.fderiv_prod
  (hfâ‚ : differentiable_at ğ•œ fâ‚ x) (hfâ‚‚ : differentiable_at ğ•œ fâ‚‚ x) :
  fderiv ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) x =
    continuous_linear_map.prod (fderiv ğ•œ fâ‚ x) (fderiv ğ•œ fâ‚‚ x) :=
has_fderiv_at.fderiv (has_fderiv_at.prod hfâ‚.has_fderiv_at hfâ‚‚.has_fderiv_at)

lemma differentiable_at.fderiv_within_prod
  (hfâ‚ : differentiable_within_at ğ•œ fâ‚ s x) (hfâ‚‚ : differentiable_within_at ğ•œ fâ‚‚ s x)
  (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) s x =
    continuous_linear_map.prod (fderiv_within ğ•œ fâ‚ s x) (fderiv_within ğ•œ fâ‚‚ s x) :=
begin
  apply has_fderiv_within_at.fderiv_within _ hxs,
  exact has_fderiv_within_at.prod hfâ‚.has_fderiv_within_at hfâ‚‚.has_fderiv_within_at
end

end cartesian_product

/- Composition -/
section composition

/- For composition lemmas, we put x explicit to help the elaborator, as otherwise Lean tends to
get confused since there are too many possibilities for composition -/

variable (x)

theorem has_fderiv_at_filter.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at_filter g g' (f x) (L.map f))
  (hf : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (g âˆ˜ f) (g'.comp f') x L :=
let eqâ‚ := (g'.is_O_comp _ _).trans_is_o hf in
let eqâ‚‚ := ((hg.comp f).mono le_comap_map).trans_is_O hf.is_O_sub in
by { refine eqâ‚‚.tri (eqâ‚.congr_left (Î» x', _)), simp }

/- A readable version of the previous theorem,
   a general form of the chain rule. -/

example {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at_filter g g' (f x) (L.map f))
  (hf : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (g âˆ˜ f) (g'.comp f') x L :=
begin
  unfold has_fderiv_at_filter at hg,
  have : is_o (Î» x', g (f x') - g (f x) - g' (f x' - f x)) (Î» x', f x' - f x) L,
    from (hg.comp f).mono le_comap_map,
  have eqâ‚ : is_o (Î» x', g (f x') - g (f x) - g' (f x' - f x)) (Î» x', x' - x) L,
    from this.trans_is_O hf.is_O_sub,
  have eqâ‚‚ : is_o (Î» x', f x' - f x - f' (x' - x)) (Î» x', x' - x) L,
    from hf,
  have : is_O
    (Î» x', g' (f x' - f x - f' (x' - x))) (Î» x', f x' - f x - f' (x' - x)) L,
    from g'.is_O_comp _ _,
  have : is_o (Î» x', g' (f x' - f x - f' (x' - x))) (Î» x', x' - x) L,
    from this.trans_is_o eqâ‚‚,
  have eqâ‚ƒ : is_o (Î» x', g' (f x' - f x) - (g' (f' (x' - x)))) (Î» x', x' - x) L,
    by { refine this.congr_left _, simp},
  exact eqâ‚.tri eqâ‚ƒ
end

theorem has_fderiv_within_at.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_within_at g g' (f '' s) (f x))
  (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (g âˆ˜ f) (g'.comp f') s x :=
(has_fderiv_at_filter.mono hg
  hf.continuous_within_at.tendsto_nhds_within_image).comp x hf

/-- The chain rule. -/
theorem has_fderiv_at.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at g g' (f x)) (hf : has_fderiv_at f f' x) :
  has_fderiv_at (g âˆ˜ f) (g'.comp f') x :=
(hg.mono hf.continuous_at).comp x hf

theorem has_fderiv_at.comp_has_fderiv_within_at {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at g g' (f x)) (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (g âˆ˜ f) (g'.comp f') s x :=
begin
  rw â† has_fderiv_within_at_univ at hg,
  exact has_fderiv_within_at.comp x (hg.mono (subset_univ _)) hf
end

lemma differentiable_within_at.comp {g : F â†’ G} {t : set F}
  (hg : differentiable_within_at ğ•œ g t (f x)) (hf : differentiable_within_at ğ•œ f s x)
  (h : f '' s âŠ† t) : differentiable_within_at ğ•œ (g âˆ˜ f) s x :=
begin
  rcases hf with âŸ¨f', hf'âŸ©,
  rcases hg with âŸ¨g', hg'âŸ©,
  exact âŸ¨continuous_linear_map.comp g' f', (hg'.mono h).comp x hf'âŸ©
end

lemma differentiable_at.comp {g : F â†’ G}
  (hg : differentiable_at ğ•œ g (f x)) (hf : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (g âˆ˜ f) x :=
(hg.has_fderiv_at.comp x hf.has_fderiv_at).differentiable_at

lemma fderiv_within.comp {g : F â†’ G} {t : set F}
  (hg : differentiable_within_at ğ•œ g t (f x)) (hf : differentiable_within_at ğ•œ f s x)
  (h : f '' s âŠ† t) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (g âˆ˜ f) s x =
    continuous_linear_map.comp (fderiv_within ğ•œ g t (f x)) (fderiv_within ğ•œ f s x) :=
begin
  apply has_fderiv_within_at.fderiv_within _ hxs,
  apply has_fderiv_within_at.comp x _ (hf.has_fderiv_within_at),
  apply hg.has_fderiv_within_at.mono h
end

lemma fderiv.comp {g : F â†’ G}
  (hg : differentiable_at ğ•œ g (f x)) (hf : differentiable_at ğ•œ f x) :
  fderiv ğ•œ (g âˆ˜ f) x = continuous_linear_map.comp (fderiv ğ•œ g (f x)) (fderiv ğ•œ f x) :=
begin
  apply has_fderiv_at.fderiv,
  exact has_fderiv_at.comp x hg.has_fderiv_at hf.has_fderiv_at
end

lemma differentiable_on.comp {g : F â†’ G} {t : set F}
  (hg : differentiable_on ğ•œ g t) (hf : differentiable_on ğ•œ f s) (st : f '' s âŠ† t) :
  differentiable_on ğ•œ (g âˆ˜ f) s :=
Î»x hx, differentiable_within_at.comp x (hg (f x) (st (mem_image_of_mem _ hx))) (hf x hx) st

lemma differentiable.comp {g : F â†’ G} (hg : differentiable ğ•œ g) (hf : differentiable ğ•œ f) :
  differentiable ğ•œ (g âˆ˜ f) :=
Î»x, differentiable_at.comp x (hg (f x)) (hf x)

end composition

/- Multiplication by a scalar function -/
section smul
variables {c : E â†’ ğ•œ} {c' : E â†’L[ğ•œ] ğ•œ}

theorem has_fderiv_within_at.smul'
  (hc : has_fderiv_within_at c c' s x) (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.scalar_prod_space_iso (f x)) s x :=
begin
  have : is_bounded_bilinear_map ğ•œ (Î» (p : ğ•œ Ã— F), p.1 â€¢ p.2) := is_bounded_bilinear_map_smul,
  exact has_fderiv_at.comp_has_fderiv_within_at x (this.has_fderiv_at (c x, f x)) (hc.prod hf)
end

theorem has_fderiv_at.smul' (hc : has_fderiv_at c c' x) (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.scalar_prod_space_iso (f x)) x :=
begin
  have : is_bounded_bilinear_map ğ•œ (Î» (p : ğ•œ Ã— F), p.1 â€¢ p.2) := is_bounded_bilinear_map_smul,
  exact has_fderiv_at.comp x (this.has_fderiv_at (c x, f x)) (hc.prod hf)
end

lemma differentiable_within_at.smul'
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î» y, c y â€¢ f y) s x :=
(hc.has_fderiv_within_at.smul' hf.has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.smul' (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î» y, c y â€¢ f y) x :=
(hc.has_fderiv_at.smul' hf.has_fderiv_at).differentiable_at

lemma differentiable_on.smul' (hc : differentiable_on ğ•œ c s) (hf : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î» y, c y â€¢ f y) s :=
Î»x hx, (hc x hx).smul' (hf x hx)

lemma differentiable.smul' (hc : differentiable ğ•œ c) (hf : differentiable ğ•œ f) :
  differentiable ğ•œ (Î» y, c y â€¢ f y) :=
Î»x, (hc x).smul' (hf x)

lemma fderiv_within_smul' (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  fderiv_within ğ•œ (Î» y, c y â€¢ f y) s x =
    c x â€¢ fderiv_within ğ•œ f s x + (fderiv_within ğ•œ c s x).scalar_prod_space_iso (f x) :=
(hc.has_fderiv_within_at.smul' hf.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_smul' (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  fderiv ğ•œ (Î» y, c y â€¢ f y) x =
    c x â€¢ fderiv ğ•œ f x + (fderiv ğ•œ c x).scalar_prod_space_iso (f x) :=
(hc.has_fderiv_at.smul' hf.has_fderiv_at).fderiv

end smul


/- Multiplication of scalar functions -/

section mul
set_option class.instance_max_depth 120
variables {c d : E â†’ ğ•œ} {c' d' : E â†’L[ğ•œ] ğ•œ}

theorem has_fderiv_within_at.mul
  (hc : has_fderiv_within_at c c' s x) (hd : has_fderiv_within_at d d' s x) :
  has_fderiv_within_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') s x :=
begin
  have : is_bounded_bilinear_map ğ•œ (Î» (p : ğ•œ Ã— ğ•œ), p.1 * p.2) := is_bounded_bilinear_map_mul,
  convert has_fderiv_at.comp_has_fderiv_within_at x (this.has_fderiv_at (c x, d x)) (hc.prod hd),
  ext z,
  change c x * d' z + d x * c' z = c x * d' z + c' z * d x,
  ring
end

theorem has_fderiv_at.mul (hc : has_fderiv_at c c' x) (hd : has_fderiv_at d d' x) :
  has_fderiv_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') x :=
begin
  have : is_bounded_bilinear_map ğ•œ (Î» (p : ğ•œ Ã— ğ•œ), p.1 * p.2) := is_bounded_bilinear_map_mul,
  convert has_fderiv_at.comp x (this.has_fderiv_at (c x, d x)) (hc.prod hd),
  ext z,
  change c x * d' z + d x * c' z = c x * d' z + c' z * d x,
  ring
end

lemma differentiable_within_at.mul
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  differentiable_within_at ğ•œ (Î» y, c y * d y) s x :=
(hc.has_fderiv_within_at.mul hd.has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.mul (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  differentiable_at ğ•œ (Î» y, c y * d y) x :=
(hc.has_fderiv_at.mul hd.has_fderiv_at).differentiable_at

lemma differentiable_on.mul (hc : differentiable_on ğ•œ c s) (hd : differentiable_on ğ•œ d s) :
  differentiable_on ğ•œ (Î» y, c y * d y) s :=
Î»x hx, (hc x hx).mul (hd x hx)

lemma differentiable.mul (hc : differentiable ğ•œ c) (hd : differentiable ğ•œ d) :
  differentiable ğ•œ (Î» y, c y * d y) :=
Î»x, (hc x).mul (hd x)

lemma fderiv_within_mul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  fderiv_within ğ•œ (Î» y, c y * d y) s x =
    c x â€¢ fderiv_within ğ•œ d s x + d x â€¢ fderiv_within ğ•œ c s x :=
(hc.has_fderiv_within_at.mul hd.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_mul (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  fderiv ğ•œ (Î» y, c y * d y) x =
    c x â€¢ fderiv ğ•œ d x + d x â€¢ fderiv ğ•œ c x :=
(hc.has_fderiv_at.mul hd.has_fderiv_at).fderiv

end mul

end
/-
  In the special case of a normed space over the reals,
  we can use  scalar multiplication in the `tendsto` characterization
  of the FrÃ©chet derivative.
-/

section

variables {E : Type*} [normed_group E] [normed_space â„ E]
variables {F : Type*} [normed_group F] [normed_space â„ F]
variables {G : Type*} [normed_group G] [normed_space â„ G]

theorem has_fderiv_at_filter_real_equiv {f : E â†’ F} {f' : E â†’L[â„] F} {x : E} {L : filter E} :
  tendsto (Î» x' : E, âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) L (nhds 0) â†”
  tendsto (Î» x' : E, âˆ¥x' - xâˆ¥â»Â¹ â€¢ (f x' - f x - f' (x' - x))) L (nhds 0) :=
begin
  symmetry,
  rw [tendsto_iff_norm_tendsto_zero], refine tendsto.congr'r (Î» x', _),
  have : âˆ¥x' + -xâˆ¥â»Â¹ â‰¥ 0, from inv_nonneg.mpr (norm_nonneg _),
  simp [norm_smul, real.norm_eq_abs, abs_of_nonneg this]
end

end
