/-
Copyright (c) 2019 Gabriel Ebner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Ebner, SÃ©bastien GouÃ«zel
-/
import analysis.calculus.fderiv
import data.polynomial.derivative
import linear_algebra.affine_space.slope

/-!

# One-dimensional derivatives

This file defines the derivative of a function `f : ğ•œ â†’ F` where `ğ•œ` is a
normed field and `F` is a normed space over this field. The derivative of
such a function `f` at a point `x` is given by an element `f' : F`.

The theory is developed analogously to the [FrÃ©chet
derivatives](./fderiv.html). We first introduce predicates defined in terms
of the corresponding predicates for FrÃ©chet derivatives:

 - `has_deriv_at_filter f f' x L` states that the function `f` has the
    derivative `f'` at the point `x` as `x` goes along the filter `L`.

 - `has_deriv_within_at f f' s x` states that the function `f` has the
    derivative `f'` at the point `x` within the subset `s`.

 - `has_deriv_at f f' x` states that the function `f` has the derivative `f'`
    at the point `x`.

 - `has_strict_deriv_at f f' x` states that the function `f` has the derivative `f'`
    at the point `x` in the sense of strict differentiability, i.e.,
   `f y - f z = (y - z) â€¢ f' + o (y - z)` as `y, z â†’ x`.

For the last two notions we also define a functional version:

  - `deriv_within f s x` is a derivative of `f` at `x` within `s`. If the
    derivative does not exist, then `deriv_within f s x` equals zero.

  - `deriv f x` is a derivative of `f` at `x`. If the derivative does not
    exist, then `deriv f x` equals zero.

The theorems `fderiv_within_deriv_within` and `fderiv_deriv` show that the
one-dimensional derivatives coincide with the general FrÃ©chet derivatives.

We also show the existence and compute the derivatives of:
  - constants
  - the identity function
  - linear maps
  - addition
  - sum of finitely many functions
  - negation
  - subtraction
  - multiplication
  - inverse `x â†’ xâ»Â¹`
  - multiplication of two functions in `ğ•œ â†’ ğ•œ`
  - multiplication of a function in `ğ•œ â†’ ğ•œ` and of a function in `ğ•œ â†’ E`
  - composition of a function in `ğ•œ â†’ F` with a function in `ğ•œ â†’ ğ•œ`
  - composition of a function in `F â†’ E` with a function in `ğ•œ â†’ F`
  - inverse function (assuming that it exists; the inverse function theorem is in `inverse.lean`)
  - division
  - polynomials

For most binary operations we also define `const_op` and `op_const` theorems for the cases when
the first or second argument is a constant. This makes writing chains of `has_deriv_at`'s easier,
and they more frequently lead to the desired result.

We set up the simplifier so that it can compute the derivative of simple functions. For instance,
```lean
example (x : â„) : deriv (Î» x, cos (sin x) * exp x) x = (cos(sin(x))-sin(sin(x))*cos(x))*exp(x) :=
by { simp, ring }
```

## Implementation notes

Most of the theorems are direct restatements of the corresponding theorems
for FrÃ©chet derivatives.

The strategy to construct simp lemmas that give the simplifier the possibility to compute
derivatives is the same as the one for differentiability statements, as explained in `fderiv.lean`.
See the explanations there.
-/

universes u v w
noncomputable theory
open_locale classical topological_space big_operators filter ennreal polynomial
open filter asymptotics set
open continuous_linear_map (smul_right smul_right_one_eq_iff)


variables {ğ•œ : Type u} [nondiscrete_normed_field ğ•œ]

section
variables {F : Type v} [normed_group F] [normed_space ğ•œ F]
variables {E : Type w} [normed_group E] [normed_space ğ•œ E]

/--
`f` has the derivative `f'` at the point `x` as `x` goes along the filter `L`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges along the filter `L`.
-/
def has_deriv_at_filter (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) (L : filter ğ•œ) :=
has_fderiv_at_filter f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x L

/--
`f` has the derivative `f'` at the point `x` within the subset `s`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def has_deriv_within_at (f : ğ•œ â†’ F) (f' : F) (s : set ğ•œ) (x : ğ•œ) :=
has_deriv_at_filter f f' x (ğ“[s] x)

/--
`f` has the derivative `f'` at the point `x`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x`.
-/
def has_deriv_at (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) :=
has_deriv_at_filter f f' x (ğ“ x)

/-- `f` has the derivative `f'` at the point `x` in the sense of strict differentiability.

That is, `f y - f z = (y - z) â€¢ f' + o(y - z)` as `y, z â†’ x`. -/
def has_strict_deriv_at (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) :=
has_strict_fderiv_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x

/--
Derivative of `f` at the point `x` within the set `s`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', has_deriv_within_at f f' s x`), then
`f x' = f x + (x' - x) â€¢ deriv_within f s x + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def deriv_within (f : ğ•œ â†’ F) (s : set ğ•œ) (x : ğ•œ) :=
fderiv_within ğ•œ f s x 1

/--
Derivative of `f` at the point `x`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', has_deriv_at f f' x`), then
`f x' = f x + (x' - x) â€¢ deriv f x + o(x' - x)` where `x'` converges to `x`.
-/
def deriv (f : ğ•œ â†’ F) (x : ğ•œ) :=
fderiv ğ•œ f x 1

variables {f fâ‚€ fâ‚ g : ğ•œ â†’ F}
variables {f' fâ‚€' fâ‚' g' : F}
variables {x : ğ•œ}
variables {s t : set ğ•œ}
variables {L Lâ‚ Lâ‚‚ : filter ğ•œ}

/-- Expressing `has_fderiv_at_filter f f' x L` in terms of `has_deriv_at_filter` -/
lemma has_fderiv_at_filter_iff_has_deriv_at_filter {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at_filter f f' x L â†” has_deriv_at_filter f (f' 1) x L :=
by simp [has_deriv_at_filter]

lemma has_fderiv_at_filter.has_deriv_at_filter {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at_filter f f' x L â†’ has_deriv_at_filter f (f' 1) x L :=
has_fderiv_at_filter_iff_has_deriv_at_filter.mp

/-- Expressing `has_fderiv_within_at f f' s x` in terms of `has_deriv_within_at` -/
lemma has_fderiv_within_at_iff_has_deriv_within_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_within_at f f' s x â†” has_deriv_within_at f (f' 1) s x :=
has_fderiv_at_filter_iff_has_deriv_at_filter

/-- Expressing `has_deriv_within_at f f' s x` in terms of `has_fderiv_within_at` -/
lemma has_deriv_within_at_iff_has_fderiv_within_at {f' : F} :
  has_deriv_within_at f f' s x â†”
  has_fderiv_within_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') s x :=
iff.rfl

lemma has_fderiv_within_at.has_deriv_within_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_within_at f f' s x â†’ has_deriv_within_at f (f' 1) s x :=
has_fderiv_within_at_iff_has_deriv_within_at.mp

lemma has_deriv_within_at.has_fderiv_within_at {f' : F} :
  has_deriv_within_at f f' s x â†’ has_fderiv_within_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') s x :=
has_deriv_within_at_iff_has_fderiv_within_at.mp

/-- Expressing `has_fderiv_at f f' x` in terms of `has_deriv_at` -/
lemma has_fderiv_at_iff_has_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at f f' x â†” has_deriv_at f (f' 1) x :=
has_fderiv_at_filter_iff_has_deriv_at_filter

lemma has_fderiv_at.has_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at f f' x â†’ has_deriv_at f (f' 1) x :=
has_fderiv_at_iff_has_deriv_at.mp

lemma has_strict_fderiv_at_iff_has_strict_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_strict_fderiv_at f f' x â†” has_strict_deriv_at f (f' 1) x :=
by simp [has_strict_deriv_at, has_strict_fderiv_at]

protected lemma has_strict_fderiv_at.has_strict_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_strict_fderiv_at f f' x â†’ has_strict_deriv_at f (f' 1) x :=
has_strict_fderiv_at_iff_has_strict_deriv_at.mp

lemma has_strict_deriv_at_iff_has_strict_fderiv_at :
  has_strict_deriv_at f f' x â†” has_strict_fderiv_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x :=
iff.rfl

alias has_strict_deriv_at_iff_has_strict_fderiv_at â†” has_strict_deriv_at.has_strict_fderiv_at _

/-- Expressing `has_deriv_at f f' x` in terms of `has_fderiv_at` -/
lemma has_deriv_at_iff_has_fderiv_at {f' : F} :
  has_deriv_at f f' x â†”
  has_fderiv_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x :=
iff.rfl

alias has_deriv_at_iff_has_fderiv_at â†” has_deriv_at.has_fderiv_at _

lemma deriv_within_zero_of_not_differentiable_within_at
  (h : Â¬ differentiable_within_at ğ•œ f s x) : deriv_within f s x = 0 :=
by { unfold deriv_within, rw fderiv_within_zero_of_not_differentiable_within_at, simp, assumption }

lemma differentiable_within_at_of_deriv_within_ne_zero (h : deriv_within f s x â‰  0) :
  differentiable_within_at ğ•œ f s x :=
not_imp_comm.1 deriv_within_zero_of_not_differentiable_within_at h

lemma deriv_zero_of_not_differentiable_at (h : Â¬ differentiable_at ğ•œ f x) : deriv f x = 0 :=
by { unfold deriv, rw fderiv_zero_of_not_differentiable_at, simp, assumption }

lemma differentiable_at_of_deriv_ne_zero (h : deriv f x â‰  0) : differentiable_at ğ•œ f x :=
not_imp_comm.1 deriv_zero_of_not_differentiable_at h

theorem unique_diff_within_at.eq_deriv (s : set ğ•œ) (H : unique_diff_within_at ğ•œ s x)
  (h : has_deriv_within_at f f' s x) (hâ‚ : has_deriv_within_at f fâ‚' s x) : f' = fâ‚' :=
smul_right_one_eq_iff.mp $ unique_diff_within_at.eq H h hâ‚

theorem has_deriv_at_filter_iff_tendsto :
  has_deriv_at_filter f f' x L â†”
  tendsto (Î» x' : ğ•œ, âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - (x' - x) â€¢ f'âˆ¥) L (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_within_at_iff_tendsto : has_deriv_within_at f f' s x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - (x' - x) â€¢ f'âˆ¥) (ğ“[s] x) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_at_iff_tendsto : has_deriv_at f f' x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - (x' - x) â€¢ f'âˆ¥) (ğ“ x) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_strict_deriv_at.has_deriv_at (h : has_strict_deriv_at f f' x) :
  has_deriv_at f f' x :=
h.has_fderiv_at

/-- If the domain has dimension one, then FrÃ©chet derivative is equivalent to the classical
definition with a limit. In this version we have to take the limit along the subset `-{x}`,
because for `y=x` the slope equals zero due to the convention `0â»Â¹=0`. -/
lemma has_deriv_at_filter_iff_tendsto_slope {x : ğ•œ} {L : filter ğ•œ} :
  has_deriv_at_filter f f' x L â†” tendsto (slope f x) (L âŠ“ ğ“Ÿ {x}á¶œ) (ğ“ f') :=
begin
  conv_lhs { simp only [has_deriv_at_filter_iff_tendsto, (norm_inv _).symm,
    (norm_smul _ _).symm, tendsto_zero_iff_norm_tendsto_zero.symm] },
  conv_rhs { rw [â† nhds_translation_sub f', tendsto_comap_iff] },
  refine (tendsto_inf_principal_nhds_iff_of_forall_eq $ by simp).symm.trans (tendsto_congr' _),
  refine (eventually_principal.2 $ Î» z hz, _).filter_mono inf_le_right,
  simp only [(âˆ˜)],
  rw [smul_sub, â† mul_smul, inv_mul_cancel (sub_ne_zero.2 hz), one_smul, slope_def_module]
end

lemma has_deriv_within_at_iff_tendsto_slope :
  has_deriv_within_at f f' s x â†” tendsto (slope f x) (ğ“[s \ {x}] x) (ğ“ f') :=
begin
  simp only [has_deriv_within_at, nhds_within, diff_eq, inf_assoc.symm, inf_principal.symm],
  exact has_deriv_at_filter_iff_tendsto_slope
end

lemma has_deriv_within_at_iff_tendsto_slope' (hs : x âˆ‰ s) :
  has_deriv_within_at f f' s x â†” tendsto (slope f x) (ğ“[s] x) (ğ“ f') :=
begin
  convert â† has_deriv_within_at_iff_tendsto_slope,
  exact diff_singleton_eq_self hs
end

lemma has_deriv_at_iff_tendsto_slope :
  has_deriv_at f f' x â†” tendsto (slope f x) (ğ“[â‰ ] x) (ğ“ f') :=
has_deriv_at_filter_iff_tendsto_slope

theorem has_deriv_within_at_congr_set {s t u : set ğ•œ}
  (hu : u âˆˆ ğ“ x) (h : s âˆ© u = t âˆ© u) :
    has_deriv_within_at f f' s x â†” has_deriv_within_at f f' t x :=
by simp_rw [has_deriv_within_at, nhds_within_eq_nhds_within' hu h]

alias has_deriv_within_at_congr_set â†” has_deriv_within_at.congr_set _

@[simp] lemma has_deriv_within_at_diff_singleton :
  has_deriv_within_at f f' (s \ {x}) x â†” has_deriv_within_at f f' s x :=
by simp only [has_deriv_within_at_iff_tendsto_slope, sdiff_idem]

@[simp] lemma has_deriv_within_at_Ioi_iff_Ici [partial_order ğ•œ] :
  has_deriv_within_at f f' (Ioi x) x â†” has_deriv_within_at f f' (Ici x) x :=
by rw [â† Ici_diff_left, has_deriv_within_at_diff_singleton]

alias has_deriv_within_at_Ioi_iff_Ici â†”
  has_deriv_within_at.Ici_of_Ioi has_deriv_within_at.Ioi_of_Ici

@[simp] lemma has_deriv_within_at_Iio_iff_Iic [partial_order ğ•œ] :
  has_deriv_within_at f f' (Iio x) x â†” has_deriv_within_at f f' (Iic x) x :=
by rw [â† Iic_diff_right, has_deriv_within_at_diff_singleton]

alias has_deriv_within_at_Iio_iff_Iic â†”
  has_deriv_within_at.Iic_of_Iio has_deriv_within_at.Iio_of_Iic

theorem has_deriv_within_at.Ioi_iff_Ioo [linear_order ğ•œ] [order_closed_topology ğ•œ] {x y : ğ•œ}
  (h : x < y) :
  has_deriv_within_at f f' (Ioo x y) x â†” has_deriv_within_at f f' (Ioi x) x :=
has_deriv_within_at_congr_set (is_open_Iio.mem_nhds h) $
  by { rw [Ioi_inter_Iio, inter_eq_left_iff_subset], exact Ioo_subset_Iio_self }

alias has_deriv_within_at.Ioi_iff_Ioo â†”
  has_deriv_within_at.Ioi_of_Ioo has_deriv_within_at.Ioo_of_Ioi

theorem has_deriv_at_iff_is_o_nhds_zero : has_deriv_at f f' x â†”
  is_o (Î»h, f (x + h) - f x - h â€¢ f') (Î»h, h) (ğ“ 0) :=
has_fderiv_at_iff_is_o_nhds_zero

theorem has_deriv_at_filter.mono (h : has_deriv_at_filter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
  has_deriv_at_filter f f' x Lâ‚ :=
has_fderiv_at_filter.mono h hst

theorem has_deriv_within_at.mono (h : has_deriv_within_at f f' t x) (hst : s âŠ† t) :
  has_deriv_within_at f f' s x :=
has_fderiv_within_at.mono h hst

theorem has_deriv_at.has_deriv_at_filter (h : has_deriv_at f f' x) (hL : L â‰¤ ğ“ x) :
  has_deriv_at_filter f f' x L :=
has_fderiv_at.has_fderiv_at_filter h hL

theorem has_deriv_at.has_deriv_within_at
  (h : has_deriv_at f f' x) : has_deriv_within_at f f' s x :=
has_fderiv_at.has_fderiv_within_at h

lemma has_deriv_within_at.differentiable_within_at (h : has_deriv_within_at f f' s x) :
  differentiable_within_at ğ•œ f s x :=
has_fderiv_within_at.differentiable_within_at h

lemma has_deriv_at.differentiable_at (h : has_deriv_at f f' x) : differentiable_at ğ•œ f x :=
has_fderiv_at.differentiable_at h

@[simp] lemma has_deriv_within_at_univ : has_deriv_within_at f f' univ x â†” has_deriv_at f f' x :=
has_fderiv_within_at_univ

theorem has_deriv_at.unique
  (hâ‚€ : has_deriv_at f fâ‚€' x) (hâ‚ : has_deriv_at f fâ‚' x) : fâ‚€' = fâ‚' :=
smul_right_one_eq_iff.mp $ hâ‚€.has_fderiv_at.unique hâ‚

lemma has_deriv_within_at_inter' (h : t âˆˆ ğ“[s] x) :
  has_deriv_within_at f f' (s âˆ© t) x â†” has_deriv_within_at f f' s x :=
has_fderiv_within_at_inter' h

lemma has_deriv_within_at_inter (h : t âˆˆ ğ“ x) :
  has_deriv_within_at f f' (s âˆ© t) x â†” has_deriv_within_at f f' s x :=
has_fderiv_within_at_inter h

lemma has_deriv_within_at.union (hs : has_deriv_within_at f f' s x)
  (ht : has_deriv_within_at f f' t x) :
  has_deriv_within_at f f' (s âˆª t) x :=
begin
  simp only [has_deriv_within_at, nhds_within_union],
  exact hs.join ht,
end

lemma has_deriv_within_at.nhds_within (h : has_deriv_within_at f f' s x)
  (ht : s âˆˆ ğ“[t] x) : has_deriv_within_at f f' t x :=
(has_deriv_within_at_inter' ht).1 (h.mono (inter_subset_right _ _))

lemma has_deriv_within_at.has_deriv_at (h : has_deriv_within_at f f' s x) (hs : s âˆˆ ğ“ x) :
  has_deriv_at f f' x :=
has_fderiv_within_at.has_fderiv_at h hs

lemma differentiable_within_at.has_deriv_within_at (h : differentiable_within_at ğ•œ f s x) :
  has_deriv_within_at f (deriv_within f s x) s x :=
h.has_fderiv_within_at.has_deriv_within_at

lemma differentiable_at.has_deriv_at (h : differentiable_at ğ•œ f x) : has_deriv_at f (deriv f x) x :=
h.has_fderiv_at.has_deriv_at

@[simp] lemma has_deriv_at_deriv_iff : has_deriv_at f (deriv f x) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, h.differentiable_at, Î» h, h.has_deriv_atâŸ©

@[simp] lemma has_deriv_within_at_deriv_within_iff :
  has_deriv_within_at f (deriv_within f s x) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, h.differentiable_within_at, Î» h, h.has_deriv_within_atâŸ©

lemma differentiable_on.has_deriv_at (h : differentiable_on ğ•œ f s) (hs : s âˆˆ ğ“ x) :
  has_deriv_at f (deriv f x) x :=
(h.has_fderiv_at hs).has_deriv_at

lemma has_deriv_at.deriv (h : has_deriv_at f f' x) : deriv f x = f' :=
h.differentiable_at.has_deriv_at.unique h

lemma deriv_eq {f' : ğ•œ â†’ F} (h : âˆ€ x, has_deriv_at f (f' x) x) : deriv f = f' :=
funext $ Î» x, (h x).deriv

lemma has_deriv_within_at.deriv_within
  (h : has_deriv_within_at f f' s x) (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within f s x = f' :=
hxs.eq_deriv _ h.differentiable_within_at.has_deriv_within_at h

lemma fderiv_within_deriv_within : (fderiv_within ğ•œ f s x : ğ•œ â†’ F) 1 = deriv_within f s x :=
rfl

lemma deriv_within_fderiv_within :
  smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (deriv_within f s x) = fderiv_within ğ•œ f s x :=
by simp [deriv_within]

lemma fderiv_deriv : (fderiv ğ•œ f x : ğ•œ â†’ F) 1 = deriv f x :=
rfl

lemma deriv_fderiv :
  smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (deriv f x) = fderiv ğ•œ f x :=
by simp [deriv]

lemma differentiable_at.deriv_within (h : differentiable_at ğ•œ f x)
  (hxs : unique_diff_within_at ğ•œ s x) : deriv_within f s x = deriv f x :=
by { unfold deriv_within deriv, rw h.fderiv_within hxs }

lemma deriv_within_subset (st : s âŠ† t) (ht : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f t x) :
  deriv_within f s x = deriv_within f t x :=
((differentiable_within_at.has_deriv_within_at h).mono st).deriv_within ht

@[simp] lemma deriv_within_univ : deriv_within f univ = deriv f :=
by { ext, unfold deriv_within deriv, rw fderiv_within_univ }

lemma deriv_within_inter (ht : t âˆˆ ğ“ x) (hs : unique_diff_within_at ğ•œ s x) :
  deriv_within f (s âˆ© t) x = deriv_within f s x :=
by { unfold deriv_within, rw fderiv_within_inter ht hs }

lemma deriv_within_of_open (hs : is_open s) (hx : x âˆˆ s) :
  deriv_within f s x = deriv f x :=
by { unfold deriv_within, rw fderiv_within_of_open hs hx, refl }

section congr
/-! ### Congruence properties of derivatives -/

theorem filter.eventually_eq.has_deriv_at_filter_iff
  (hâ‚€ : fâ‚€ =á¶ [L] fâ‚) (hx : fâ‚€ x = fâ‚ x) (hâ‚ : fâ‚€' = fâ‚') :
  has_deriv_at_filter fâ‚€ fâ‚€' x L â†” has_deriv_at_filter fâ‚ fâ‚' x L :=
hâ‚€.has_fderiv_at_filter_iff hx (by simp [hâ‚])

lemma has_deriv_at_filter.congr_of_eventually_eq (h : has_deriv_at_filter f f' x L)
  (hL : fâ‚ =á¶ [L] f) (hx : fâ‚ x = f x) : has_deriv_at_filter fâ‚ f' x L :=
by rwa hL.has_deriv_at_filter_iff hx rfl

lemma has_deriv_within_at.congr_mono (h : has_deriv_within_at f f' s x) (ht : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : has_deriv_within_at fâ‚ f' t x :=
has_fderiv_within_at.congr_mono h ht hx hâ‚

lemma has_deriv_within_at.congr (h : has_deriv_within_at f f' s x) (hs : âˆ€x âˆˆ s, fâ‚ x = f x)
  (hx : fâ‚ x = f x) : has_deriv_within_at fâ‚ f' s x :=
h.congr_mono hs hx (subset.refl _)

lemma has_deriv_within_at.congr_of_eventually_eq (h : has_deriv_within_at f f' s x)
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : has_deriv_within_at fâ‚ f' s x :=
has_deriv_at_filter.congr_of_eventually_eq h hâ‚ hx

lemma has_deriv_within_at.congr_of_eventually_eq_of_mem (h : has_deriv_within_at f f' s x)
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : has_deriv_within_at fâ‚ f' s x :=
h.congr_of_eventually_eq hâ‚ (hâ‚.eq_of_nhds_within hx)

lemma has_deriv_at.congr_of_eventually_eq (h : has_deriv_at f f' x)
  (hâ‚ : fâ‚ =á¶ [ğ“ x] f) : has_deriv_at fâ‚ f' x :=
has_deriv_at_filter.congr_of_eventually_eq h hâ‚ (mem_of_mem_nhds hâ‚ : _)

lemma filter.eventually_eq.deriv_within_eq (hs : unique_diff_within_at ğ•œ s x)
  (hL : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  deriv_within fâ‚ s x = deriv_within f s x :=
by { unfold deriv_within, rw hL.fderiv_within_eq hs hx }

lemma deriv_within_congr (hs : unique_diff_within_at ğ•œ s x)
  (hL : âˆ€yâˆˆs, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  deriv_within fâ‚ s x = deriv_within f s x :=
by { unfold deriv_within, rw fderiv_within_congr hs hL hx }

lemma filter.eventually_eq.deriv_eq (hL : fâ‚ =á¶ [ğ“ x] f) : deriv fâ‚ x = deriv f x :=
by { unfold deriv, rwa filter.eventually_eq.fderiv_eq }

protected lemma filter.eventually_eq.deriv (h : fâ‚ =á¶ [ğ“ x] f) : deriv fâ‚ =á¶ [ğ“ x] deriv f :=
h.eventually_eq_nhds.mono $ Î» x h, h.deriv_eq

end congr

section id
/-! ### Derivative of the identity -/
variables (s x L)

theorem has_deriv_at_filter_id : has_deriv_at_filter id 1 x L :=
(has_fderiv_at_filter_id x L).has_deriv_at_filter

theorem has_deriv_within_at_id : has_deriv_within_at id 1 s x :=
has_deriv_at_filter_id _ _

theorem has_deriv_at_id : has_deriv_at id 1 x :=
has_deriv_at_filter_id _ _

theorem has_deriv_at_id' : has_deriv_at (Î» (x : ğ•œ), x) 1 x :=
has_deriv_at_filter_id _ _

theorem has_strict_deriv_at_id : has_strict_deriv_at id 1 x :=
(has_strict_fderiv_at_id x).has_strict_deriv_at

lemma deriv_id : deriv id x = 1 :=
has_deriv_at.deriv (has_deriv_at_id x)

@[simp] lemma deriv_id' : deriv (@id ğ•œ) = Î» _, 1 := funext deriv_id

@[simp] lemma deriv_id'' : deriv (Î» x : ğ•œ, x) = Î» _, 1 := deriv_id'

lemma deriv_within_id (hxs : unique_diff_within_at ğ•œ s x) : deriv_within id s x = 1 :=
(has_deriv_within_at_id x s).deriv_within hxs

end id

section const
/-! ### Derivative of constant functions -/
variables (c : F) (s x L)

theorem has_deriv_at_filter_const : has_deriv_at_filter (Î» x, c) 0 x L :=
(has_fderiv_at_filter_const c x L).has_deriv_at_filter

theorem has_strict_deriv_at_const : has_strict_deriv_at (Î» x, c) 0 x :=
(has_strict_fderiv_at_const c x).has_strict_deriv_at

theorem has_deriv_within_at_const : has_deriv_within_at (Î» x, c) 0 s x :=
has_deriv_at_filter_const _ _ _

theorem has_deriv_at_const : has_deriv_at (Î» x, c) 0 x :=
has_deriv_at_filter_const _ _ _

lemma deriv_const : deriv (Î» x, c) x = 0 :=
has_deriv_at.deriv (has_deriv_at_const x c)

@[simp] lemma deriv_const' : deriv (Î» x:ğ•œ, c) = Î» x, 0 :=
funext (Î» x, deriv_const x c)

lemma deriv_within_const (hxs : unique_diff_within_at ğ•œ s x) : deriv_within (Î» x, c) s x = 0 :=
(has_deriv_within_at_const _ _ _).deriv_within hxs

end const

section continuous_linear_map
/-! ### Derivative of continuous linear maps -/
variables (e : ğ•œ â†’L[ğ•œ] F)

protected lemma continuous_linear_map.has_deriv_at_filter : has_deriv_at_filter e (e 1) x L :=
e.has_fderiv_at_filter.has_deriv_at_filter

protected lemma continuous_linear_map.has_strict_deriv_at : has_strict_deriv_at e (e 1) x :=
e.has_strict_fderiv_at.has_strict_deriv_at

protected lemma continuous_linear_map.has_deriv_at : has_deriv_at e (e 1) x :=
e.has_deriv_at_filter

protected lemma continuous_linear_map.has_deriv_within_at : has_deriv_within_at e (e 1) s x :=
e.has_deriv_at_filter

@[simp] protected lemma continuous_linear_map.deriv : deriv e x = e 1 :=
e.has_deriv_at.deriv

protected lemma continuous_linear_map.deriv_within (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within e s x = e 1 :=
e.has_deriv_within_at.deriv_within hxs

end continuous_linear_map

section linear_map
/-! ### Derivative of bundled linear maps -/
variables (e : ğ•œ â†’â‚—[ğ•œ] F)

protected lemma linear_map.has_deriv_at_filter : has_deriv_at_filter e (e 1) x L :=
e.to_continuous_linear_mapâ‚.has_deriv_at_filter

protected lemma linear_map.has_strict_deriv_at : has_strict_deriv_at e (e 1) x :=
e.to_continuous_linear_mapâ‚.has_strict_deriv_at

protected lemma linear_map.has_deriv_at : has_deriv_at e (e 1) x :=
e.has_deriv_at_filter

protected lemma linear_map.has_deriv_within_at : has_deriv_within_at e (e 1) s x :=
e.has_deriv_at_filter

@[simp] protected lemma linear_map.deriv : deriv e x = e 1 :=
e.has_deriv_at.deriv

protected lemma linear_map.deriv_within (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within e s x = e 1 :=
e.has_deriv_within_at.deriv_within hxs

end linear_map

section add
/-! ### Derivative of the sum of two functions -/

theorem has_deriv_at_filter.add
  (hf : has_deriv_at_filter f f' x L) (hg : has_deriv_at_filter g g' x L) :
  has_deriv_at_filter (Î» y, f y + g y) (f' + g') x L :=
by simpa using (hf.add hg).has_deriv_at_filter

theorem has_strict_deriv_at.add
  (hf : has_strict_deriv_at f f' x) (hg : has_strict_deriv_at g g' x) :
  has_strict_deriv_at (Î» y, f y + g y) (f' + g') x :=
by simpa using (hf.add hg).has_strict_deriv_at

theorem has_deriv_within_at.add
  (hf : has_deriv_within_at f f' s x) (hg : has_deriv_within_at g g' s x) :
  has_deriv_within_at (Î» y, f y + g y) (f' + g') s x :=
hf.add hg

theorem has_deriv_at.add
  (hf : has_deriv_at f f' x) (hg : has_deriv_at g g' x) :
  has_deriv_at (Î» x, f x + g x) (f' + g') x :=
hf.add hg

lemma deriv_within_add (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  deriv_within (Î»y, f y + g y) s x = deriv_within f s x + deriv_within g s x :=
(hf.has_deriv_within_at.add hg.has_deriv_within_at).deriv_within hxs

@[simp] lemma deriv_add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  deriv (Î»y, f y + g y) x = deriv f x + deriv g x :=
(hf.has_deriv_at.add hg.has_deriv_at).deriv

theorem has_deriv_at_filter.add_const
  (hf : has_deriv_at_filter f f' x L) (c : F) :
  has_deriv_at_filter (Î» y, f y + c) f' x L :=
add_zero f' â–¸ hf.add (has_deriv_at_filter_const x L c)

theorem has_deriv_within_at.add_const
  (hf : has_deriv_within_at f f' s x) (c : F) :
  has_deriv_within_at (Î» y, f y + c) f' s x :=
hf.add_const c

theorem has_deriv_at.add_const
  (hf : has_deriv_at f f' x) (c : F) :
  has_deriv_at (Î» x, f x + c) f' x :=
hf.add_const c

lemma deriv_within_add_const (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  deriv_within (Î»y, f y + c) s x = deriv_within f s x :=
by simp only [deriv_within, fderiv_within_add_const hxs]

lemma deriv_add_const (c : F) : deriv (Î»y, f y + c) x = deriv f x :=
by simp only [deriv, fderiv_add_const]

@[simp] lemma deriv_add_const' (c : F) : deriv (Î» y, f y + c) = deriv f :=
funext $ Î» x, deriv_add_const c

theorem has_deriv_at_filter.const_add (c : F) (hf : has_deriv_at_filter f f' x L) :
  has_deriv_at_filter (Î» y, c + f y) f' x L :=
zero_add f' â–¸ (has_deriv_at_filter_const x L c).add hf

theorem has_deriv_within_at.const_add (c : F) (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Î» y, c + f y) f' s x :=
hf.const_add c

theorem has_deriv_at.const_add (c : F) (hf : has_deriv_at f f' x) :
  has_deriv_at (Î» x, c + f x) f' x :=
hf.const_add c

lemma deriv_within_const_add (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  deriv_within (Î»y, c + f y) s x = deriv_within f s x :=
by simp only [deriv_within, fderiv_within_const_add hxs]

lemma deriv_const_add (c : F)  : deriv (Î»y, c + f y) x = deriv f x :=
by simp only [deriv, fderiv_const_add]

@[simp] lemma deriv_const_add' (c : F) : deriv (Î» y, c + f y) = deriv f :=
funext $ Î» x, deriv_const_add c

end add

section sum
/-! ### Derivative of a finite sum of functions -/

open_locale big_operators

variables {Î¹ : Type*} {u : finset Î¹} {A : Î¹ â†’ (ğ•œ â†’ F)} {A' : Î¹ â†’ F}

theorem has_deriv_at_filter.sum (h : âˆ€ i âˆˆ u, has_deriv_at_filter (A i) (A' i) x L) :
  has_deriv_at_filter (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x L :=
by simpa [continuous_linear_map.sum_apply] using (has_fderiv_at_filter.sum h).has_deriv_at_filter

theorem has_strict_deriv_at.sum (h : âˆ€ i âˆˆ u, has_strict_deriv_at (A i) (A' i) x) :
  has_strict_deriv_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x :=
by simpa [continuous_linear_map.sum_apply] using (has_strict_fderiv_at.sum h).has_strict_deriv_at

theorem has_deriv_within_at.sum (h : âˆ€ i âˆˆ u, has_deriv_within_at (A i) (A' i) s x) :
  has_deriv_within_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) s x :=
has_deriv_at_filter.sum h

theorem has_deriv_at.sum (h : âˆ€ i âˆˆ u, has_deriv_at (A i) (A' i) x) :
  has_deriv_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x :=
has_deriv_at_filter.sum h

lemma deriv_within_sum (hxs : unique_diff_within_at ğ•œ s x)
  (h : âˆ€ i âˆˆ u, differentiable_within_at ğ•œ (A i) s x) :
  deriv_within (Î» y, âˆ‘ i in u, A i y) s x = âˆ‘ i in u, deriv_within (A i) s x :=
(has_deriv_within_at.sum (Î» i hi, (h i hi).has_deriv_within_at)).deriv_within hxs

@[simp] lemma deriv_sum (h : âˆ€ i âˆˆ u, differentiable_at ğ•œ (A i) x) :
  deriv (Î» y, âˆ‘ i in u, A i y) x = âˆ‘ i in u, deriv (A i) x :=
(has_deriv_at.sum (Î» i hi, (h i hi).has_deriv_at)).deriv

end sum

section pi

/-! ### Derivatives of functions `f : ğ•œ â†’ Î  i, E i` -/

variables {Î¹ : Type*} [fintype Î¹] {E' : Î¹ â†’ Type*} [Î  i, normed_group (E' i)]
  [Î  i, normed_space ğ•œ (E' i)] {Ï† : ğ•œ â†’ Î  i, E' i} {Ï†' : Î  i, E' i}

@[simp] lemma has_strict_deriv_at_pi :
  has_strict_deriv_at Ï† Ï†' x â†” âˆ€ i, has_strict_deriv_at (Î» x, Ï† x i) (Ï†' i) x :=
has_strict_fderiv_at_pi'

@[simp] lemma has_deriv_at_filter_pi :
  has_deriv_at_filter Ï† Ï†' x L â†”
    âˆ€ i, has_deriv_at_filter (Î» x, Ï† x i) (Ï†' i) x L :=
has_fderiv_at_filter_pi'

lemma has_deriv_at_pi :
  has_deriv_at Ï† Ï†' x â†” âˆ€ i, has_deriv_at (Î» x, Ï† x i) (Ï†' i) x:=
has_deriv_at_filter_pi

lemma has_deriv_within_at_pi :
  has_deriv_within_at Ï† Ï†' s x â†” âˆ€ i, has_deriv_within_at (Î» x, Ï† x i) (Ï†' i) s x:=
has_deriv_at_filter_pi

lemma deriv_within_pi (h : âˆ€ i, differentiable_within_at ğ•œ (Î» x, Ï† x i) s x)
  (hs : unique_diff_within_at ğ•œ s x) :
  deriv_within Ï† s x = Î» i, deriv_within (Î» x, Ï† x i) s x :=
(has_deriv_within_at_pi.2 (Î» i, (h i).has_deriv_within_at)).deriv_within hs

lemma deriv_pi (h : âˆ€ i, differentiable_at ğ•œ (Î» x, Ï† x i) x) :
  deriv Ï† x = Î» i, deriv (Î» x, Ï† x i) x :=
(has_deriv_at_pi.2 (Î» i, (h i).has_deriv_at)).deriv

end pi

section smul

/-! ### Derivative of the multiplication of a scalar function and a vector function -/

variables {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  [normed_space ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F] {c : ğ•œ â†’ ğ•œ'} {c' : ğ•œ'}

theorem has_deriv_within_at.smul
  (hc : has_deriv_within_at c c' s x) (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) s x :=
by simpa using (has_fderiv_within_at.smul hc hf).has_deriv_within_at

theorem has_deriv_at.smul
  (hc : has_deriv_at c c' x) (hf : has_deriv_at f f' x) :
  has_deriv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) x :=
begin
  rw [â† has_deriv_within_at_univ] at *,
  exact hc.smul hf
end

theorem has_strict_deriv_at.smul
  (hc : has_strict_deriv_at c c' x) (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) x :=
by simpa using (hc.smul hf).has_strict_deriv_at

lemma deriv_within_smul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  deriv_within (Î» y, c y â€¢ f y) s x = c x â€¢ deriv_within f s x + (deriv_within c s x) â€¢ f x :=
(hc.has_deriv_within_at.smul hf.has_deriv_within_at).deriv_within hxs

lemma deriv_smul (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  deriv (Î» y, c y â€¢ f y) x = c x â€¢ deriv f x + (deriv c x) â€¢ f x :=
(hc.has_deriv_at.smul hf.has_deriv_at).deriv

theorem has_deriv_within_at.smul_const
  (hc : has_deriv_within_at c c' s x) (f : F) :
  has_deriv_within_at (Î» y, c y â€¢ f) (c' â€¢ f) s x :=
begin
  have := hc.smul (has_deriv_within_at_const x s f),
  rwa [smul_zero, zero_add] at this
end

theorem has_deriv_at.smul_const
  (hc : has_deriv_at c c' x) (f : F) :
  has_deriv_at (Î» y, c y â€¢ f) (c' â€¢ f) x :=
begin
  rw [â† has_deriv_within_at_univ] at *,
  exact hc.smul_const f
end

lemma deriv_within_smul_const (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (f : F) :
  deriv_within (Î» y, c y â€¢ f) s x = (deriv_within c s x) â€¢ f :=
(hc.has_deriv_within_at.smul_const f).deriv_within hxs

lemma deriv_smul_const (hc : differentiable_at ğ•œ c x) (f : F) :
  deriv (Î» y, c y â€¢ f) x = (deriv c x) â€¢ f :=
(hc.has_deriv_at.smul_const f).deriv

end smul

section const_smul

variables {R : Type*} [semiring R] [module R F] [smul_comm_class ğ•œ R F]
  [has_continuous_const_smul R F]

theorem has_strict_deriv_at.const_smul
  (c : R) (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Î» y, c â€¢ f y) (c â€¢ f') x :=
by simpa using (hf.const_smul c).has_strict_deriv_at

theorem has_deriv_at_filter.const_smul
  (c : R) (hf : has_deriv_at_filter f f' x L) :
  has_deriv_at_filter (Î» y, c â€¢ f y) (c â€¢ f') x L :=
by simpa using (hf.const_smul c).has_deriv_at_filter

theorem has_deriv_within_at.const_smul
  (c : R) (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Î» y, c â€¢ f y) (c â€¢ f') s x :=
hf.const_smul c

theorem has_deriv_at.const_smul (c : R) (hf : has_deriv_at f f' x) :
  has_deriv_at (Î» y, c â€¢ f y) (c â€¢ f') x :=
hf.const_smul c

lemma deriv_within_const_smul (hxs : unique_diff_within_at ğ•œ s x)
  (c : R) (hf : differentiable_within_at ğ•œ f s x) :
  deriv_within (Î» y, c â€¢ f y) s x = c â€¢ deriv_within f s x :=
(hf.has_deriv_within_at.const_smul c).deriv_within hxs

lemma deriv_const_smul (c : R) (hf : differentiable_at ğ•œ f x) :
  deriv (Î» y, c â€¢ f y) x = c â€¢ deriv f x :=
(hf.has_deriv_at.const_smul c).deriv

end const_smul

section neg
/-! ### Derivative of the negative of a function -/

theorem has_deriv_at_filter.neg (h : has_deriv_at_filter f f' x L) :
  has_deriv_at_filter (Î» x, -f x) (-f') x L :=
by simpa using h.neg.has_deriv_at_filter

theorem has_deriv_within_at.neg (h : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Î» x, -f x) (-f') s x :=
h.neg

theorem has_deriv_at.neg (h : has_deriv_at f f' x) : has_deriv_at (Î» x, -f x) (-f') x :=
h.neg

theorem has_strict_deriv_at.neg (h : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Î» x, -f x) (-f') x :=
by simpa using h.neg.has_strict_deriv_at

lemma deriv_within.neg (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»y, -f y) s x = - deriv_within f s x :=
by simp only [deriv_within, fderiv_within_neg hxs, continuous_linear_map.neg_apply]

lemma deriv.neg : deriv (Î»y, -f y) x = - deriv f x :=
by simp only [deriv, fderiv_neg, continuous_linear_map.neg_apply]

@[simp] lemma deriv.neg' : deriv (Î»y, -f y) = (Î» x, - deriv f x) :=
funext $ Î» x, deriv.neg

end neg

section neg2
/-! ### Derivative of the negation function (i.e `has_neg.neg`) -/

variables (s x L)

theorem has_deriv_at_filter_neg : has_deriv_at_filter has_neg.neg (-1) x L :=
has_deriv_at_filter.neg $ has_deriv_at_filter_id _ _

theorem has_deriv_within_at_neg : has_deriv_within_at has_neg.neg (-1) s x :=
has_deriv_at_filter_neg _ _

theorem has_deriv_at_neg : has_deriv_at has_neg.neg (-1) x :=
has_deriv_at_filter_neg _ _

theorem has_deriv_at_neg' : has_deriv_at (Î» x, -x) (-1) x :=
has_deriv_at_filter_neg _ _

theorem has_strict_deriv_at_neg : has_strict_deriv_at has_neg.neg (-1) x :=
has_strict_deriv_at.neg $ has_strict_deriv_at_id _

lemma deriv_neg : deriv has_neg.neg x = -1 :=
has_deriv_at.deriv (has_deriv_at_neg x)

@[simp] lemma deriv_neg' : deriv (has_neg.neg : ğ•œ â†’ ğ•œ) = Î» _, -1 :=
funext deriv_neg

@[simp] lemma deriv_neg'' : deriv (Î» x : ğ•œ, -x) x = -1 :=
deriv_neg x

lemma deriv_within_neg (hxs : unique_diff_within_at ğ•œ s x) : deriv_within has_neg.neg s x = -1 :=
(has_deriv_within_at_neg x s).deriv_within hxs

lemma differentiable_neg : differentiable ğ•œ (has_neg.neg : ğ•œ â†’ ğ•œ) :=
differentiable.neg differentiable_id

lemma differentiable_on_neg : differentiable_on ğ•œ (has_neg.neg : ğ•œ â†’ ğ•œ) s :=
differentiable_on.neg differentiable_on_id

end neg2

section sub
/-! ### Derivative of the difference of two functions -/

theorem has_deriv_at_filter.sub
  (hf : has_deriv_at_filter f f' x L) (hg : has_deriv_at_filter g g' x L) :
  has_deriv_at_filter (Î» x, f x - g x) (f' - g') x L :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem has_deriv_within_at.sub
  (hf : has_deriv_within_at f f' s x) (hg : has_deriv_within_at g g' s x) :
  has_deriv_within_at (Î» x, f x - g x) (f' - g') s x :=
hf.sub hg

theorem has_deriv_at.sub
  (hf : has_deriv_at f f' x) (hg : has_deriv_at g g' x) :
  has_deriv_at (Î» x, f x - g x) (f' - g') x :=
hf.sub hg

theorem has_strict_deriv_at.sub
  (hf : has_strict_deriv_at f f' x) (hg : has_strict_deriv_at g g' x) :
  has_strict_deriv_at (Î» x, f x - g x) (f' - g') x :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

lemma deriv_within_sub (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  deriv_within (Î»y, f y - g y) s x = deriv_within f s x - deriv_within g s x :=
(hf.has_deriv_within_at.sub hg.has_deriv_within_at).deriv_within hxs

@[simp] lemma deriv_sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  deriv (Î» y, f y - g y) x = deriv f x - deriv g x :=
(hf.has_deriv_at.sub hg.has_deriv_at).deriv

theorem has_deriv_at_filter.is_O_sub (h : has_deriv_at_filter f f' x L) :
  is_O (Î» x', f x' - f x) (Î» x', x' - x) L :=
has_fderiv_at_filter.is_O_sub h

theorem has_deriv_at_filter.is_O_sub_rev (hf : has_deriv_at_filter f f' x L) (hf' : f' â‰  0) :
  is_O (Î» x', x' - x) (Î» x', f x' - f x) L :=
suffices antilipschitz_with âˆ¥f'âˆ¥â‚Šâ»Â¹ (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f'), from hf.is_O_sub_rev this,
(smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f').to_linear_map.antilipschitz_of_bound $
  Î» x, by simp [norm_smul, â† div_eq_inv_mul, mul_div_cancel _ (mt norm_eq_zero.1 hf')]

theorem has_deriv_at_filter.sub_const
  (hf : has_deriv_at_filter f f' x L) (c : F) :
  has_deriv_at_filter (Î» x, f x - c) f' x L :=
by simpa only [sub_eq_add_neg] using hf.add_const (-c)

theorem has_deriv_within_at.sub_const
  (hf : has_deriv_within_at f f' s x) (c : F) :
  has_deriv_within_at (Î» x, f x - c) f' s x :=
hf.sub_const c

theorem has_deriv_at.sub_const
  (hf : has_deriv_at f f' x) (c : F) :
  has_deriv_at (Î» x, f x - c) f' x :=
hf.sub_const c

lemma deriv_within_sub_const (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  deriv_within (Î»y, f y - c) s x = deriv_within f s x :=
by simp only [deriv_within, fderiv_within_sub_const hxs]

lemma deriv_sub_const (c : F) : deriv (Î» y, f y - c) x = deriv f x :=
by simp only [deriv, fderiv_sub_const]

theorem has_deriv_at_filter.const_sub (c : F) (hf : has_deriv_at_filter f f' x L) :
  has_deriv_at_filter (Î» x, c - f x) (-f') x L :=
by simpa only [sub_eq_add_neg] using hf.neg.const_add c

theorem has_deriv_within_at.const_sub (c : F) (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Î» x, c - f x) (-f') s x :=
hf.const_sub c

theorem has_strict_deriv_at.const_sub (c : F) (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (Î» x, c - f x) (-f') x :=
by simpa only [sub_eq_add_neg] using hf.neg.const_add c

theorem has_deriv_at.const_sub (c : F) (hf : has_deriv_at f f' x) :
  has_deriv_at (Î» x, c - f x) (-f') x :=
hf.const_sub c

lemma deriv_within_const_sub (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  deriv_within (Î»y, c - f y) s x = -deriv_within f s x :=
by simp [deriv_within, fderiv_within_const_sub hxs]

lemma deriv_const_sub (c : F) : deriv (Î» y, c - f y) x = -deriv f x :=
by simp only [â† deriv_within_univ,
  deriv_within_const_sub (unique_diff_within_at_univ : unique_diff_within_at ğ•œ _ _)]

end sub

section continuous
/-! ### Continuity of a function admitting a derivative -/

theorem has_deriv_at_filter.tendsto_nhds
  (hL : L â‰¤ ğ“ x) (h : has_deriv_at_filter f f' x L) :
  tendsto f L (ğ“ (f x)) :=
h.tendsto_nhds hL

theorem has_deriv_within_at.continuous_within_at
  (h : has_deriv_within_at f f' s x) : continuous_within_at f s x :=
has_deriv_at_filter.tendsto_nhds inf_le_left h

theorem has_deriv_at.continuous_at (h : has_deriv_at f f' x) : continuous_at f x :=
has_deriv_at_filter.tendsto_nhds le_rfl h

protected theorem has_deriv_at.continuous_on {f f' : ğ•œ â†’ F}
  (hderiv : âˆ€ x âˆˆ s, has_deriv_at f (f' x) x) : continuous_on f s :=
Î» x hx, (hderiv x hx).continuous_at.continuous_within_at

end continuous

section cartesian_product
/-! ### Derivative of the cartesian product of two functions -/

variables {G : Type w} [normed_group G] [normed_space ğ•œ G]
variables {fâ‚‚ : ğ•œ â†’ G} {fâ‚‚' : G}

lemma has_deriv_at_filter.prod
  (hfâ‚ : has_deriv_at_filter fâ‚ fâ‚' x L) (hfâ‚‚ : has_deriv_at_filter fâ‚‚ fâ‚‚' x L) :
  has_deriv_at_filter (Î» x, (fâ‚ x, fâ‚‚ x)) (fâ‚', fâ‚‚') x L :=
hfâ‚.prod hfâ‚‚

lemma has_deriv_within_at.prod
  (hfâ‚ : has_deriv_within_at fâ‚ fâ‚' s x) (hfâ‚‚ : has_deriv_within_at fâ‚‚ fâ‚‚' s x) :
  has_deriv_within_at (Î» x, (fâ‚ x, fâ‚‚ x)) (fâ‚', fâ‚‚') s x :=
hfâ‚.prod hfâ‚‚

lemma has_deriv_at.prod (hfâ‚ : has_deriv_at fâ‚ fâ‚' x) (hfâ‚‚ : has_deriv_at fâ‚‚ fâ‚‚' x) :
  has_deriv_at (Î» x, (fâ‚ x, fâ‚‚ x)) (fâ‚', fâ‚‚') x :=
hfâ‚.prod hfâ‚‚

lemma has_strict_deriv_at.prod (hfâ‚ : has_strict_deriv_at fâ‚ fâ‚' x)
  (hfâ‚‚ : has_strict_deriv_at fâ‚‚ fâ‚‚' x) :
  has_strict_deriv_at (Î» x, (fâ‚ x, fâ‚‚ x)) (fâ‚', fâ‚‚') x :=
hfâ‚.prod hfâ‚‚

end cartesian_product

section composition
/-!
### Derivative of the composition of a vector function and a scalar function

We use `scomp` in lemmas on composition of vector valued and scalar valued functions, and `comp`
in lemmas on composition of scalar valued functions, in analogy for `smul` and `mul` (and also
because the `comp` version with the shorter name will show up much more often in applications).
The formula for the derivative involves `smul` in `scomp` lemmas, which can be reduced to
usual multiplication in `comp` lemmas.
-/

/- For composition lemmas, we put x explicit to help the elaborator, as otherwise Lean tends to
get confused since there are too many possibilities for composition -/
variables {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  [normed_space ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F] {s' t' : set ğ•œ'}
  {h : ğ•œ â†’ ğ•œ'} {hâ‚ : ğ•œ â†’ ğ•œ} {hâ‚‚ : ğ•œ' â†’ ğ•œ'} {h' hâ‚‚' : ğ•œ'} {hâ‚' : ğ•œ}
  {gâ‚ : ğ•œ' â†’ F} {gâ‚' : F} {L' : filter ğ•œ'} (x)

theorem has_deriv_at_filter.scomp
  (hg : has_deriv_at_filter gâ‚ gâ‚' (h x) L')
  (hh : has_deriv_at_filter h h' x L) (hL : tendsto h L L'):
  has_deriv_at_filter (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x L :=
by simpa using ((hg.restrict_scalars ğ•œ).comp x hh hL).has_deriv_at_filter

theorem has_deriv_within_at.scomp_has_deriv_at
  (hg : has_deriv_within_at gâ‚ gâ‚' s' (h x))
  (hh : has_deriv_at h h' x) (hs : âˆ€ x, h x âˆˆ s') :
  has_deriv_at (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x :=
hg.scomp x hh $ tendsto_inf.2 âŸ¨hh.continuous_at, tendsto_principal.2 $ eventually_of_forall hsâŸ©

theorem has_deriv_within_at.scomp
  (hg : has_deriv_within_at gâ‚ gâ‚' t' (h x))
  (hh : has_deriv_within_at h h' s x) (hst : maps_to h s t') :
  has_deriv_within_at (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') s x :=
hg.scomp x hh $ hh.continuous_within_at.tendsto_nhds_within hst

/-- The chain rule. -/
theorem has_deriv_at.scomp
  (hg : has_deriv_at gâ‚ gâ‚' (h x)) (hh : has_deriv_at h h' x) :
  has_deriv_at (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x :=
hg.scomp x hh hh.continuous_at

theorem has_strict_deriv_at.scomp
  (hg : has_strict_deriv_at gâ‚ gâ‚' (h x)) (hh : has_strict_deriv_at h h' x) :
  has_strict_deriv_at (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x :=
by simpa using ((hg.restrict_scalars ğ•œ).comp x hh).has_strict_deriv_at

theorem has_deriv_at.scomp_has_deriv_within_at
  (hg : has_deriv_at gâ‚ gâ‚' (h x)) (hh : has_deriv_within_at h h' s x) :
  has_deriv_within_at (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') s x :=
has_deriv_within_at.scomp x hg.has_deriv_within_at hh (maps_to_univ _ _)

lemma deriv_within.scomp
  (hg : differentiable_within_at ğ•œ' gâ‚ t' (h x)) (hh : differentiable_within_at ğ•œ h s x)
  (hs : maps_to h s t') (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (gâ‚ âˆ˜ h) s x = deriv_within h s x â€¢ deriv_within gâ‚ t' (h x) :=
(has_deriv_within_at.scomp x hg.has_deriv_within_at hh.has_deriv_within_at hs).deriv_within hxs

lemma deriv.scomp
  (hg : differentiable_at ğ•œ' gâ‚ (h x)) (hh : differentiable_at ğ•œ h x) :
  deriv (gâ‚ âˆ˜ h) x = deriv h x â€¢ deriv gâ‚ (h x) :=
(has_deriv_at.scomp x hg.has_deriv_at hh.has_deriv_at).deriv

/-! ### Derivative of the composition of a scalar and vector functions -/

theorem has_deriv_at_filter.comp_has_fderiv_at_filter {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x)
  {L'' : filter E} (hhâ‚‚ : has_deriv_at_filter hâ‚‚ hâ‚‚' (f x) L')
  (hf : has_fderiv_at_filter f f' x L'') (hL : tendsto f L'' L') :
  has_fderiv_at_filter (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x L'' :=
by { convert (hhâ‚‚.restrict_scalars ğ•œ).comp x hf hL, ext x, simp [mul_comm] }

theorem has_strict_deriv_at.comp_has_strict_fderiv_at {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x)
  (hh : has_strict_deriv_at hâ‚‚ hâ‚‚' (f x)) (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x :=
begin
  rw has_strict_deriv_at at hh,
  convert (hh.restrict_scalars ğ•œ).comp x hf,
  ext x,
  simp [mul_comm]
end

theorem has_deriv_at.comp_has_fderiv_at {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x)
  (hh : has_deriv_at hâ‚‚ hâ‚‚' (f x)) (hf : has_fderiv_at f f' x) :
  has_fderiv_at (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x :=
hh.comp_has_fderiv_at_filter x hf hf.continuous_at

theorem has_deriv_at.comp_has_fderiv_within_at {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {s} (x)
  (hh : has_deriv_at hâ‚‚ hâ‚‚' (f x)) (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') s x :=
hh.comp_has_fderiv_at_filter x hf hf.continuous_within_at

theorem has_deriv_within_at.comp_has_fderiv_within_at {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {s t} (x)
  (hh : has_deriv_within_at hâ‚‚ hâ‚‚' t (f x)) (hf : has_fderiv_within_at f f' s x)
  (hst : maps_to f s t) :
  has_fderiv_within_at (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') s x :=
hh.comp_has_fderiv_at_filter x hf $ hf.continuous_within_at.tendsto_nhds_within hst

/-! ### Derivative of the composition of two scalar functions -/

theorem has_deriv_at_filter.comp
  (hhâ‚‚ : has_deriv_at_filter hâ‚‚ hâ‚‚' (h x) L')
  (hh : has_deriv_at_filter h h' x L) (hL : tendsto h L L') :
  has_deriv_at_filter (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x L :=
by { rw mul_comm, exact hhâ‚‚.scomp x hh hL }

theorem has_deriv_within_at.comp
  (hhâ‚‚ : has_deriv_within_at hâ‚‚ hâ‚‚' s' (h x))
  (hh : has_deriv_within_at h h' s x) (hst : maps_to h s s') :
  has_deriv_within_at (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') s x :=
by { rw mul_comm, exact hhâ‚‚.scomp x hh hst, }

/-- The chain rule. -/
theorem has_deriv_at.comp
  (hhâ‚‚ : has_deriv_at hâ‚‚ hâ‚‚' (h x)) (hh : has_deriv_at h h' x) :
  has_deriv_at (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x :=
hhâ‚‚.comp x hh hh.continuous_at

theorem has_strict_deriv_at.comp
  (hhâ‚‚ : has_strict_deriv_at hâ‚‚ hâ‚‚' (h x)) (hh : has_strict_deriv_at h h' x) :
  has_strict_deriv_at (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x :=
by { rw mul_comm, exact hhâ‚‚.scomp x hh }

theorem has_deriv_at.comp_has_deriv_within_at
  (hhâ‚‚ : has_deriv_at hâ‚‚ hâ‚‚' (h x)) (hh : has_deriv_within_at h h' s x) :
  has_deriv_within_at (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') s x :=
hhâ‚‚.has_deriv_within_at.comp x hh (maps_to_univ _ _)

lemma deriv_within.comp
  (hhâ‚‚ : differentiable_within_at ğ•œ' hâ‚‚ s' (h x)) (hh : differentiable_within_at ğ•œ h s x)
  (hs : maps_to h s s') (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (hâ‚‚ âˆ˜ h) s x = deriv_within hâ‚‚ s' (h x) * deriv_within h s x :=
(hhâ‚‚.has_deriv_within_at.comp x hh.has_deriv_within_at hs).deriv_within hxs

lemma deriv.comp
  (hhâ‚‚ : differentiable_at ğ•œ' hâ‚‚ (h x)) (hh : differentiable_at ğ•œ h x) :
  deriv (hâ‚‚ âˆ˜ h) x = deriv hâ‚‚ (h x) * deriv h x :=
(hhâ‚‚.has_deriv_at.comp x hh.has_deriv_at).deriv

protected lemma has_deriv_at_filter.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ}
  (hf : has_deriv_at_filter f f' x L) (hL : tendsto f L L) (hx : f x = x) (n : â„•) :
  has_deriv_at_filter (f^[n]) (f'^n) x L :=
begin
  have := hf.iterate hL hx n,
  rwa [continuous_linear_map.smul_right_one_pow] at this
end

protected lemma has_deriv_at.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ}
  (hf : has_deriv_at f f' x) (hx : f x = x) (n : â„•) :
  has_deriv_at (f^[n]) (f'^n) x :=
begin
  have := has_fderiv_at.iterate hf hx n,
  rwa [continuous_linear_map.smul_right_one_pow] at this
end

protected lemma has_deriv_within_at.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ}
  (hf : has_deriv_within_at f f' s x) (hx : f x = x) (hs : maps_to f s s) (n : â„•) :
  has_deriv_within_at (f^[n]) (f'^n) s x :=
begin
  have := has_fderiv_within_at.iterate hf hx hs n,
  rwa [continuous_linear_map.smul_right_one_pow] at this
end

protected lemma has_strict_deriv_at.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ}
  (hf : has_strict_deriv_at f f' x) (hx : f x = x) (n : â„•) :
  has_strict_deriv_at (f^[n]) (f'^n) x :=
begin
  have := hf.iterate hx n,
  rwa [continuous_linear_map.smul_right_one_pow] at this
end

end composition

section composition_vector
/-! ### Derivative of the composition of a function between vector spaces and a function on `ğ•œ` -/

open continuous_linear_map

variables {l : F â†’ E} {l' : F â†’L[ğ•œ] E}
variable (x)

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative within a set
equal to the FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem has_fderiv_within_at.comp_has_deriv_within_at {t : set F}
  (hl : has_fderiv_within_at l l' t (f x)) (hf : has_deriv_within_at f f' s x)
  (hst : maps_to f s t) :
  has_deriv_within_at (l âˆ˜ f) (l' f') s x :=
by simpa only [one_apply, one_smul, smul_right_apply, coe_comp', (âˆ˜)]
  using (hl.comp x hf.has_fderiv_within_at hst).has_deriv_within_at

theorem has_fderiv_at.comp_has_deriv_within_at
  (hl : has_fderiv_at l l' (f x)) (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (l âˆ˜ f) (l' f') s x :=
hl.has_fderiv_within_at.comp_has_deriv_within_at x hf (maps_to_univ _ _)

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative equal to the
FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem has_fderiv_at.comp_has_deriv_at (hl : has_fderiv_at l l' (f x)) (hf : has_deriv_at f f' x) :
  has_deriv_at (l âˆ˜ f) (l' f') x :=
has_deriv_within_at_univ.mp $ hl.comp_has_deriv_within_at x hf.has_deriv_within_at

theorem has_strict_fderiv_at.comp_has_strict_deriv_at
  (hl : has_strict_fderiv_at l l' (f x)) (hf : has_strict_deriv_at f f' x) :
  has_strict_deriv_at (l âˆ˜ f) (l' f') x :=
by simpa only [one_apply, one_smul, smul_right_apply, coe_comp', (âˆ˜)]
  using (hl.comp x hf.has_strict_fderiv_at).has_strict_deriv_at

lemma fderiv_within.comp_deriv_within {t : set F}
  (hl : differentiable_within_at ğ•œ l t (f x)) (hf : differentiable_within_at ğ•œ f s x)
  (hs : maps_to f s t) (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (l âˆ˜ f) s x = (fderiv_within ğ•œ l t (f x) : F â†’ E) (deriv_within f s x) :=
(hl.has_fderiv_within_at.comp_has_deriv_within_at x hf.has_deriv_within_at hs).deriv_within hxs

lemma fderiv.comp_deriv
  (hl : differentiable_at ğ•œ l (f x)) (hf : differentiable_at ğ•œ f x) :
  deriv (l âˆ˜ f) x = (fderiv ğ•œ l (f x) : F â†’ E) (deriv f x) :=
(hl.has_fderiv_at.comp_has_deriv_at x hf.has_deriv_at).deriv

end composition_vector

section mul
/-! ### Derivative of the multiplication of two functions -/
variables {ğ•œ' ğ”¸ : Type*} [normed_field ğ•œ'] [normed_ring ğ”¸] [normed_algebra ğ•œ ğ•œ']
  [normed_algebra ğ•œ ğ”¸] {c d : ğ•œ â†’ ğ”¸} {c' d' : ğ”¸} {u v : ğ•œ â†’ ğ•œ'}

theorem has_deriv_within_at.mul
  (hc : has_deriv_within_at c c' s x) (hd : has_deriv_within_at d d' s x) :
  has_deriv_within_at (Î» y, c y * d y) (c' * d x + c x * d') s x :=
begin
  have := (has_fderiv_within_at.mul' hc hd).has_deriv_within_at,
  rwa [continuous_linear_map.add_apply, continuous_linear_map.smul_apply,
      continuous_linear_map.smul_right_apply, continuous_linear_map.smul_right_apply,
      continuous_linear_map.smul_right_apply, continuous_linear_map.one_apply,
      one_smul, one_smul, add_comm] at this,
end

theorem has_deriv_at.mul (hc : has_deriv_at c c' x) (hd : has_deriv_at d d' x) :
  has_deriv_at (Î» y, c y * d y) (c' * d x + c x * d') x :=
begin
  rw [â† has_deriv_within_at_univ] at *,
  exact hc.mul hd
end

theorem has_strict_deriv_at.mul
  (hc : has_strict_deriv_at c c' x) (hd : has_strict_deriv_at d d' x) :
  has_strict_deriv_at (Î» y, c y * d y) (c' * d x + c x * d') x :=
begin
  have := (has_strict_fderiv_at.mul' hc hd).has_strict_deriv_at,
  rwa [continuous_linear_map.add_apply, continuous_linear_map.smul_apply,
      continuous_linear_map.smul_right_apply, continuous_linear_map.smul_right_apply,
      continuous_linear_map.smul_right_apply, continuous_linear_map.one_apply,
      one_smul, one_smul, add_comm] at this,
end

lemma deriv_within_mul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  deriv_within (Î» y, c y * d y) s x = deriv_within c s x * d x + c x * deriv_within d s x :=
(hc.has_deriv_within_at.mul hd.has_deriv_within_at).deriv_within hxs

@[simp] lemma deriv_mul (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  deriv (Î» y, c y * d y) x = deriv c x * d x + c x * deriv d x :=
(hc.has_deriv_at.mul hd.has_deriv_at).deriv

theorem has_deriv_within_at.mul_const (hc : has_deriv_within_at c c' s x) (d : ğ”¸) :
  has_deriv_within_at (Î» y, c y * d) (c' * d) s x :=
begin
  convert hc.mul (has_deriv_within_at_const x s d),
  rw [mul_zero, add_zero]
end

theorem has_deriv_at.mul_const (hc : has_deriv_at c c' x) (d : ğ”¸) :
  has_deriv_at (Î» y, c y * d) (c' * d) x :=
begin
  rw [â† has_deriv_within_at_univ] at *,
  exact hc.mul_const d
end

theorem has_deriv_at_mul_const (c : ğ•œ) : has_deriv_at (Î» x, x * c) c x :=
by simpa only [one_mul] using (has_deriv_at_id' x).mul_const c

theorem has_strict_deriv_at.mul_const (hc : has_strict_deriv_at c c' x) (d : ğ”¸) :
  has_strict_deriv_at (Î» y, c y * d) (c' * d) x :=
begin
  convert hc.mul (has_strict_deriv_at_const x d),
  rw [mul_zero, add_zero]
end

lemma deriv_within_mul_const (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (d : ğ”¸) :
  deriv_within (Î» y, c y * d) s x = deriv_within c s x * d :=
(hc.has_deriv_within_at.mul_const d).deriv_within hxs

lemma deriv_mul_const (hc : differentiable_at ğ•œ c x) (d : ğ”¸) :
  deriv (Î» y, c y * d) x = deriv c x * d :=
(hc.has_deriv_at.mul_const d).deriv

lemma deriv_mul_const_field (v : ğ•œ') :
  deriv (Î» y, u y * v) x = deriv u x * v :=
begin
  by_cases hu : differentiable_at ğ•œ u x,
  { exact deriv_mul_const hu v },
  { rw [deriv_zero_of_not_differentiable_at hu, zero_mul],
    rcases eq_or_ne v 0 with rfl|hd,
    { simp only [mul_zero, deriv_const] },
    { refine deriv_zero_of_not_differentiable_at (mt (Î» H, _) hu),
      simpa only [mul_inv_cancel_rightâ‚€ hd] using H.mul_const vâ»Â¹ } }
end

@[simp] lemma deriv_mul_const_field' (v : ğ•œ') : deriv (Î» x, u x * v) = Î» x, deriv u x * v :=
funext $ Î» _, deriv_mul_const_field v

theorem has_deriv_within_at.const_mul (c : ğ”¸) (hd : has_deriv_within_at d d' s x) :
  has_deriv_within_at (Î» y, c * d y) (c * d') s x :=
begin
  convert (has_deriv_within_at_const x s c).mul hd,
  rw [zero_mul, zero_add]
end

theorem has_deriv_at.const_mul (c : ğ”¸) (hd : has_deriv_at d d' x) :
  has_deriv_at (Î» y, c * d y) (c * d') x :=
begin
  rw [â† has_deriv_within_at_univ] at *,
  exact hd.const_mul c
end

theorem has_strict_deriv_at.const_mul (c : ğ”¸) (hd : has_strict_deriv_at d d' x) :
  has_strict_deriv_at (Î» y, c * d y) (c * d') x :=
begin
  convert (has_strict_deriv_at_const _ _).mul hd,
  rw [zero_mul, zero_add]
end

lemma deriv_within_const_mul (hxs : unique_diff_within_at ğ•œ s x)
  (c : ğ”¸) (hd : differentiable_within_at ğ•œ d s x) :
  deriv_within (Î» y, c * d y) s x = c * deriv_within d s x :=
(hd.has_deriv_within_at.const_mul c).deriv_within hxs

lemma deriv_const_mul (c : ğ”¸) (hd : differentiable_at ğ•œ d x) :
  deriv (Î» y, c * d y) x = c * deriv d x :=
(hd.has_deriv_at.const_mul c).deriv

lemma deriv_const_mul_field (u : ğ•œ') : deriv (Î» y, u * v y) x = u * deriv v x :=
by simp only [mul_comm u, deriv_mul_const_field]

@[simp] lemma deriv_const_mul_field' (u : ğ•œ') : deriv (Î» x, u * v x) = Î» x, u * deriv v x :=
funext (Î» x, deriv_const_mul_field u)

end mul

section inverse
/-! ### Derivative of `x â†¦ xâ»Â¹` -/

theorem has_strict_deriv_at_inv (hx : x â‰  0) : has_strict_deriv_at has_inv.inv (-(x^2)â»Â¹) x :=
begin
  suffices : is_o (Î» p : ğ•œ Ã— ğ•œ, (p.1 - p.2) * ((x * x)â»Â¹ - (p.1 * p.2)â»Â¹))
    (Î» (p : ğ•œ Ã— ğ•œ), (p.1 - p.2) * 1) (ğ“ (x, x)),
  { refine this.congr' _ (eventually_of_forall $ Î» _, mul_one _),
    refine eventually.mono (is_open.mem_nhds (is_open_ne.prod is_open_ne) âŸ¨hx, hxâŸ©) _,
    rintro âŸ¨y, zâŸ© âŸ¨hy, hzâŸ©,
    simp only [mem_set_of_eq] at hy hz, -- hy : y â‰  0, hz : z â‰  0
    field_simp [hx, hy, hz], ring, },
  refine (is_O_refl (Î» p : ğ•œ Ã— ğ•œ, p.1 - p.2) _).mul_is_o ((is_o_one_iff _).2 _),
  rw [â† sub_self (x * x)â»Â¹],
  exact tendsto_const_nhds.sub ((continuous_mul.tendsto (x, x)).invâ‚€ $ mul_ne_zero hx hx)
end

theorem has_deriv_at_inv (x_ne_zero : x â‰  0) :
  has_deriv_at (Î»y, yâ»Â¹) (-(x^2)â»Â¹) x :=
(has_strict_deriv_at_inv x_ne_zero).has_deriv_at

theorem has_deriv_within_at_inv (x_ne_zero : x â‰  0) (s : set ğ•œ) :
  has_deriv_within_at (Î»x, xâ»Â¹) (-(x^2)â»Â¹) s x :=
(has_deriv_at_inv x_ne_zero).has_deriv_within_at

lemma differentiable_at_inv :
  differentiable_at ğ•œ (Î»x, xâ»Â¹) x â†” x â‰  0:=
âŸ¨Î» H, normed_field.continuous_at_inv.1 H.continuous_at,
  Î» H, (has_deriv_at_inv H).differentiable_atâŸ©

lemma differentiable_within_at_inv (x_ne_zero : x â‰  0) :
  differentiable_within_at ğ•œ (Î»x, xâ»Â¹) s x :=
(differentiable_at_inv.2 x_ne_zero).differentiable_within_at

lemma differentiable_on_inv : differentiable_on ğ•œ (Î»x:ğ•œ, xâ»Â¹) {x | x â‰  0} :=
Î»x hx, differentiable_within_at_inv hx

lemma deriv_inv : deriv (Î»x, xâ»Â¹) x = -(x^2)â»Â¹ :=
begin
  rcases eq_or_ne x 0 with rfl|hne,
  { simp [deriv_zero_of_not_differentiable_at (mt differentiable_at_inv.1 (not_not.2 rfl))] },
  { exact (has_deriv_at_inv hne).deriv  }
end

@[simp] lemma deriv_inv' : deriv (Î» x : ğ•œ, xâ»Â¹) = Î» x, -(x ^ 2)â»Â¹ := funext (Î» x, deriv_inv)

lemma deriv_within_inv (x_ne_zero : x â‰  0) (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»x, xâ»Â¹) s x = -(x^2)â»Â¹ :=
begin
  rw differentiable_at.deriv_within (differentiable_at_inv.2 x_ne_zero) hxs,
  exact deriv_inv
end

lemma has_fderiv_at_inv (x_ne_zero : x â‰  0) :
  has_fderiv_at (Î»x, xâ»Â¹) (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x^2)â»Â¹) : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
has_deriv_at_inv x_ne_zero

lemma has_fderiv_within_at_inv (x_ne_zero : x â‰  0) :
  has_fderiv_within_at (Î»x, xâ»Â¹) (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x^2)â»Â¹) : ğ•œ â†’L[ğ•œ] ğ•œ) s x :=
(has_fderiv_at_inv x_ne_zero).has_fderiv_within_at

lemma fderiv_inv :
  fderiv ğ•œ (Î»x, xâ»Â¹) x = smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x^2)â»Â¹) :=
by rw [â† deriv_fderiv, deriv_inv]

lemma fderiv_within_inv (x_ne_zero : x â‰  0) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»x, xâ»Â¹) s x = smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x^2)â»Â¹) :=
begin
  rw differentiable_at.fderiv_within (differentiable_at_inv.2 x_ne_zero) hxs,
  exact fderiv_inv
end

variables {c : ğ•œ â†’ ğ•œ} {c' : ğ•œ}

lemma has_deriv_within_at.inv
  (hc : has_deriv_within_at c c' s x) (hx : c x â‰  0) :
  has_deriv_within_at (Î» y, (c y)â»Â¹) (- c' / (c x)^2) s x :=
begin
  convert (has_deriv_at_inv hx).comp_has_deriv_within_at x hc,
  field_simp
end

lemma has_deriv_at.inv (hc : has_deriv_at c c' x) (hx : c x â‰  0) :
  has_deriv_at (Î» y, (c y)â»Â¹) (- c' / (c x)^2) x :=
begin
  rw â† has_deriv_within_at_univ at *,
  exact hc.inv hx
end

lemma differentiable_within_at.inv (hc : differentiable_within_at ğ•œ c s x) (hx : c x â‰  0) :
  differentiable_within_at ğ•œ (Î»x, (c x)â»Â¹) s x :=
(hc.has_deriv_within_at.inv hx).differentiable_within_at

@[simp] lemma differentiable_at.inv (hc : differentiable_at ğ•œ c x) (hx : c x â‰  0) :
  differentiable_at ğ•œ (Î»x, (c x)â»Â¹) x :=
(hc.has_deriv_at.inv hx).differentiable_at

lemma differentiable_on.inv (hc : differentiable_on ğ•œ c s) (hx : âˆ€ x âˆˆ s, c x â‰  0) :
  differentiable_on ğ•œ (Î»x, (c x)â»Â¹) s :=
Î»x h, (hc x h).inv (hx x h)

@[simp] lemma differentiable.inv (hc : differentiable ğ•œ c) (hx : âˆ€ x, c x â‰  0) :
  differentiable ğ•œ (Î»x, (c x)â»Â¹) :=
Î»x, (hc x).inv (hx x)

lemma deriv_within_inv' (hc : differentiable_within_at ğ•œ c s x) (hx : c x â‰  0)
  (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»x, (c x)â»Â¹) s x = - (deriv_within c s x) / (c x)^2 :=
(hc.has_deriv_within_at.inv hx).deriv_within hxs

@[simp] lemma deriv_inv'' (hc : differentiable_at ğ•œ c x) (hx : c x â‰  0) :
  deriv (Î»x, (c x)â»Â¹) x = - (deriv c x) / (c x)^2 :=
(hc.has_deriv_at.inv hx).deriv

end inverse

section division
/-! ### Derivative of `x â†¦ c x / d x` -/

variables {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  {c d : ğ•œ â†’ ğ•œ'} {c' d' : ğ•œ'}

lemma has_deriv_within_at.div
  (hc : has_deriv_within_at c c' s x) (hd : has_deriv_within_at d d' s x) (hx : d x â‰  0) :
  has_deriv_within_at (Î» y, c y / d y) ((c' * d x - c x * d') / (d x)^2) s x :=
begin
  convert hc.mul ((has_deriv_at_inv hx).comp_has_deriv_within_at x hd),
  { simp only [div_eq_mul_inv] },
  { field_simp, ring }
end

lemma has_strict_deriv_at.div (hc : has_strict_deriv_at c c' x) (hd : has_strict_deriv_at d d' x)
  (hx : d x â‰  0) :
  has_strict_deriv_at (Î» y, c y / d y) ((c' * d x - c x * d') / (d x)^2) x :=
begin
  convert hc.mul ((has_strict_deriv_at_inv hx).comp x hd),
  { simp only [div_eq_mul_inv] },
  { field_simp, ring }
end

lemma has_deriv_at.div (hc : has_deriv_at c c' x) (hd : has_deriv_at d d' x) (hx : d x â‰  0) :
  has_deriv_at (Î» y, c y / d y) ((c' * d x - c x * d') / (d x)^2) x :=
begin
  rw â† has_deriv_within_at_univ at *,
  exact hc.div hd hx
end

lemma differentiable_within_at.div
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) (hx : d x â‰  0) :
  differentiable_within_at ğ•œ (Î»x, c x / d x) s x :=
((hc.has_deriv_within_at).div (hd.has_deriv_within_at) hx).differentiable_within_at

@[simp] lemma differentiable_at.div
  (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) (hx : d x â‰  0) :
  differentiable_at ğ•œ (Î»x, c x / d x) x :=
((hc.has_deriv_at).div (hd.has_deriv_at) hx).differentiable_at

lemma differentiable_on.div
  (hc : differentiable_on ğ•œ c s) (hd : differentiable_on ğ•œ d s) (hx : âˆ€ x âˆˆ s, d x â‰  0) :
  differentiable_on ğ•œ (Î»x, c x / d x) s :=
Î»x h, (hc x h).div (hd x h) (hx x h)

@[simp] lemma differentiable.div
  (hc : differentiable ğ•œ c) (hd : differentiable ğ•œ d) (hx : âˆ€ x, d x â‰  0) :
differentiable ğ•œ (Î»x, c x / d x) :=
Î»x, (hc x).div (hd x) (hx x)

lemma deriv_within_div
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) (hx : d x â‰  0)
  (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»x, c x / d x) s x
    = ((deriv_within c s x) * d x - c x * (deriv_within d s x)) / (d x)^2 :=
((hc.has_deriv_within_at).div (hd.has_deriv_within_at) hx).deriv_within hxs

@[simp] lemma deriv_div
  (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) (hx : d x â‰  0) :
  deriv (Î»x, c x / d x) x = ((deriv c x) * d x - c x * (deriv d x)) / (d x)^2 :=
((hc.has_deriv_at).div (hd.has_deriv_at) hx).deriv

lemma has_deriv_at.div_const (hc : has_deriv_at c c' x) (d : ğ•œ') :
  has_deriv_at (Î» x, c x / d) (c' / d) x :=
by simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹

lemma has_deriv_within_at.div_const (hc : has_deriv_within_at c c' s x) (d : ğ•œ') :
  has_deriv_within_at (Î» x, c x / d) (c' / d) s x :=
by simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹

lemma has_strict_deriv_at.div_const (hc : has_strict_deriv_at c c' x) (d : ğ•œ') :
  has_strict_deriv_at (Î» x, c x / d) (c' / d) x :=
by simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹

lemma differentiable_within_at.div_const (hc : differentiable_within_at ğ•œ c s x) {d : ğ•œ'} :
  differentiable_within_at ğ•œ (Î»x, c x / d) s x :=
(hc.has_deriv_within_at.div_const _).differentiable_within_at

@[simp] lemma differentiable_at.div_const (hc : differentiable_at ğ•œ c x) {d : ğ•œ'} :
  differentiable_at ğ•œ (Î» x, c x / d) x :=
(hc.has_deriv_at.div_const _).differentiable_at

lemma differentiable_on.div_const (hc : differentiable_on ğ•œ c s) {d : ğ•œ'} :
  differentiable_on ğ•œ (Î»x, c x / d) s :=
Î» x hx, (hc x hx).div_const

@[simp] lemma differentiable.div_const (hc : differentiable ğ•œ c) {d : ğ•œ'} :
  differentiable ğ•œ (Î»x, c x / d) :=
Î» x, (hc x).div_const

lemma deriv_within_div_const (hc : differentiable_within_at ğ•œ c s x) {d : ğ•œ'}
  (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»x, c x / d) s x = (deriv_within c s x) / d :=
by simp [div_eq_inv_mul, deriv_within_const_mul, hc, hxs]

@[simp] lemma deriv_div_const (d : ğ•œ') :
  deriv (Î»x, c x / d) x = (deriv c x) / d :=
by simp only [div_eq_mul_inv, deriv_mul_const_field]

end division

section clm_comp_apply
/-! ### Derivative of the pointwise composition/application of continuous linear maps -/

open continuous_linear_map

variables {G : Type*} [normed_group G] [normed_space ğ•œ G] {c : ğ•œ â†’ F â†’L[ğ•œ] G} {c' : F â†’L[ğ•œ] G}
  {d : ğ•œ â†’ E â†’L[ğ•œ] F} {d' : E â†’L[ğ•œ] F} {u : ğ•œ â†’ F} {u' : F}

lemma has_strict_deriv_at.clm_comp (hc : has_strict_deriv_at c c' x)
  (hd : has_strict_deriv_at d d' x) :
  has_strict_deriv_at (Î» y, (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') x :=
begin
  have := (hc.has_strict_fderiv_at.clm_comp hd.has_strict_fderiv_at).has_strict_deriv_at,
  rwa [add_apply, comp_apply, comp_apply, smul_right_apply, smul_right_apply, one_apply, one_smul,
      one_smul, add_comm] at this,
end

lemma has_deriv_within_at.clm_comp (hc : has_deriv_within_at c c' s x)
  (hd : has_deriv_within_at d d' s x) :
  has_deriv_within_at (Î» y, (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') s x :=
begin
  have := (hc.has_fderiv_within_at.clm_comp hd.has_fderiv_within_at).has_deriv_within_at,
  rwa [add_apply, comp_apply, comp_apply, smul_right_apply, smul_right_apply, one_apply, one_smul,
      one_smul, add_comm] at this,
end

lemma has_deriv_at.clm_comp (hc : has_deriv_at c c' x) (hd : has_deriv_at d d' x) :
  has_deriv_at (Î» y, (c y).comp (d y))
  (c'.comp (d x) + (c x).comp d') x :=
begin
  rw [â† has_deriv_within_at_univ] at *,
  exact hc.clm_comp hd
end

lemma deriv_within_clm_comp (hc : differentiable_within_at ğ•œ c s x)
  (hd : differentiable_within_at ğ•œ d s x) (hxs : unique_diff_within_at ğ•œ s x):
  deriv_within (Î» y, (c y).comp (d y)) s x =
    ((deriv_within c s x).comp (d x) + (c x).comp (deriv_within d s x)) :=
(hc.has_deriv_within_at.clm_comp hd.has_deriv_within_at).deriv_within hxs

lemma deriv_clm_comp (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  deriv (Î» y, (c y).comp (d y)) x =
    ((deriv c x).comp (d x) + (c x).comp (deriv d x)) :=
(hc.has_deriv_at.clm_comp hd.has_deriv_at).deriv

lemma has_strict_deriv_at.clm_apply (hc : has_strict_deriv_at c c' x)
  (hu : has_strict_deriv_at u u' x) :
  has_strict_deriv_at (Î» y, (c y) (u y)) (c' (u x) + c x u') x :=
begin
  have := (hc.has_strict_fderiv_at.clm_apply hu.has_strict_fderiv_at).has_strict_deriv_at,
  rwa [add_apply, comp_apply, flip_apply, smul_right_apply, smul_right_apply, one_apply, one_smul,
      one_smul, add_comm] at this,
end

lemma has_deriv_within_at.clm_apply (hc : has_deriv_within_at c c' s x)
  (hu : has_deriv_within_at u u' s x) :
  has_deriv_within_at (Î» y, (c y) (u y)) (c' (u x) + c x u') s x :=
begin
  have := (hc.has_fderiv_within_at.clm_apply hu.has_fderiv_within_at).has_deriv_within_at,
  rwa [add_apply, comp_apply, flip_apply, smul_right_apply, smul_right_apply, one_apply, one_smul,
      one_smul, add_comm] at this,
end

lemma has_deriv_at.clm_apply (hc : has_deriv_at c c' x) (hu : has_deriv_at u u' x) :
  has_deriv_at (Î» y, (c y) (u y)) (c' (u x) + c x u') x :=
begin
  have := (hc.has_fderiv_at.clm_apply hu.has_fderiv_at).has_deriv_at,
  rwa [add_apply, comp_apply, flip_apply, smul_right_apply, smul_right_apply, one_apply, one_smul,
      one_smul, add_comm] at this,
end

lemma deriv_within_clm_apply (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hu : differentiable_within_at ğ•œ u s x) :
  deriv_within (Î» y, (c y) (u y)) s x = (deriv_within c s x (u x) + c x (deriv_within u s x)) :=
(hc.has_deriv_within_at.clm_apply hu.has_deriv_within_at).deriv_within hxs

lemma deriv_clm_apply (hc : differentiable_at ğ•œ c x) (hu : differentiable_at ğ•œ u x) :
  deriv (Î» y, (c y) (u y)) x = (deriv c x (u x) + c x (deriv u x)) :=
(hc.has_deriv_at.clm_apply hu.has_deriv_at).deriv

end clm_comp_apply

theorem has_strict_deriv_at.has_strict_fderiv_at_equiv {f : ğ•œ â†’ ğ•œ} {f' x : ğ•œ}
  (hf : has_strict_deriv_at f f' x) (hf' : f' â‰  0) :
  has_strict_fderiv_at f
    (continuous_linear_equiv.units_equiv_aut ğ•œ (units.mk0 f' hf') : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
hf

theorem has_deriv_at.has_fderiv_at_equiv {f : ğ•œ â†’ ğ•œ} {f' x : ğ•œ} (hf : has_deriv_at f f' x)
  (hf' : f' â‰  0) :
  has_fderiv_at f (continuous_linear_equiv.units_equiv_aut ğ•œ (units.mk0 f' hf') : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
hf

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a` in the strict sense, then `g` has the derivative `f'â»Â¹` at `a`
in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem has_strict_deriv_at.of_local_left_inverse {f g : ğ•œ â†’ ğ•œ} {f' a : ğ•œ}
  (hg : continuous_at g a) (hf : has_strict_deriv_at f f' (g a)) (hf' : f' â‰  0)
  (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
  has_strict_deriv_at g f'â»Â¹ a :=
(hf.has_strict_fderiv_at_equiv hf').of_local_left_inverse hg hfg

/-- If `f` is a local homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has a
nonzero derivative `f'` at `f.symm a` in the strict sense, then `f.symm` has the derivative `f'â»Â¹`
at `a` in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
lemma local_homeomorph.has_strict_deriv_at_symm (f : local_homeomorph ğ•œ ğ•œ) {a f' : ğ•œ}
  (ha : a âˆˆ f.target) (hf' : f' â‰  0) (htff' : has_strict_deriv_at f f' (f.symm a)) :
  has_strict_deriv_at f.symm f'â»Â¹ a :=
htff'.of_local_left_inverse (f.symm.continuous_at ha) hf' (f.eventually_right_inverse ha)

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a`, then `g` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem has_deriv_at.of_local_left_inverse {f g : ğ•œ â†’ ğ•œ} {f' a : ğ•œ}
  (hg : continuous_at g a) (hf : has_deriv_at f f' (g a)) (hf' : f' â‰  0)
  (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
  has_deriv_at g f'â»Â¹ a :=
(hf.has_fderiv_at_equiv hf').of_local_left_inverse hg hfg

/-- If `f` is a local homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
nonzero derivative `f'` at `f.symm a`, then `f.symm` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
lemma local_homeomorph.has_deriv_at_symm (f : local_homeomorph ğ•œ ğ•œ) {a f' : ğ•œ}
  (ha : a âˆˆ f.target) (hf' : f' â‰  0) (htff' : has_deriv_at f f' (f.symm a)) :
  has_deriv_at f.symm f'â»Â¹ a :=
htff'.of_local_left_inverse (f.symm.continuous_at ha) hf' (f.eventually_right_inverse ha)

lemma has_deriv_at.eventually_ne (h : has_deriv_at f f' x) (hf' : f' â‰  0) :
  âˆ€á¶  z in ğ“[â‰ ] x, f z â‰  f x :=
(has_deriv_at_iff_has_fderiv_at.1 h).eventually_ne
  âŸ¨âˆ¥f'âˆ¥â»Â¹, Î» z, by field_simp [norm_smul, mt norm_eq_zero.1 hf']âŸ©

lemma has_deriv_at.tendsto_punctured_nhds (h : has_deriv_at f f' x) (hf' : f' â‰  0) :
  tendsto f (ğ“[â‰ ] x) (ğ“[â‰ ] (f x)) :=
tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _
  h.continuous_at.continuous_within_at (h.eventually_ne hf')

theorem not_differentiable_within_at_of_local_left_inverse_has_deriv_within_at_zero
  {f g : ğ•œ â†’ ğ•œ} {a : ğ•œ} {s t : set ğ•œ} (ha : a âˆˆ s) (hsu : unique_diff_within_at ğ•œ s a)
  (hf : has_deriv_within_at f 0 t (g a)) (hst : maps_to g s t) (hfg : f âˆ˜ g =á¶ [ğ“[s] a] id) :
  Â¬differentiable_within_at ğ•œ g s a :=
begin
  intro hg,
  have := (hf.comp a hg.has_deriv_within_at hst).congr_of_eventually_eq_of_mem hfg.symm ha,
  simpa using hsu.eq_deriv _ this (has_deriv_within_at_id _ _)
end

theorem not_differentiable_at_of_local_left_inverse_has_deriv_at_zero
  {f g : ğ•œ â†’ ğ•œ} {a : ğ•œ} (hf : has_deriv_at f 0 (g a)) (hfg : f âˆ˜ g =á¶ [ğ“ a] id) :
  Â¬differentiable_at ğ•œ g a :=
begin
  intro hg,
  have := (hf.comp a hg.has_deriv_at).congr_of_eventually_eq hfg.symm,
  simpa using this.unique (has_deriv_at_id a)
end

end

namespace polynomial
/-! ### Derivative of a polynomial -/

variables {x : ğ•œ} {s : set ğ•œ}
variable (p : ğ•œ[X])

/-- The derivative (in the analysis sense) of a polynomial `p` is given by `p.derivative`. -/
protected lemma has_strict_deriv_at (x : ğ•œ) :
  has_strict_deriv_at (Î»x, p.eval x) (p.derivative.eval x) x :=
begin
  apply p.induction_on,
  { simp [has_strict_deriv_at_const] },
  { assume p q hp hq,
    convert hp.add hq;
    simp },
  { assume n a h,
    convert h.mul (has_strict_deriv_at_id x),
    { ext y, simp [pow_add, mul_assoc] },
    { simp [pow_add], ring } }
end

/-- The derivative (in the analysis sense) of a polynomial `p` is given by `p.derivative`. -/
protected lemma has_deriv_at (x : ğ•œ) : has_deriv_at (Î»x, p.eval x) (p.derivative.eval x) x :=
(p.has_strict_deriv_at x).has_deriv_at

protected theorem has_deriv_within_at (x : ğ•œ) (s : set ğ•œ) :
  has_deriv_within_at (Î»x, p.eval x) (p.derivative.eval x) s x :=
(p.has_deriv_at x).has_deriv_within_at

protected lemma differentiable_at : differentiable_at ğ•œ (Î»x, p.eval x) x :=
(p.has_deriv_at x).differentiable_at

protected lemma differentiable_within_at : differentiable_within_at ğ•œ (Î»x, p.eval x) s x :=
p.differentiable_at.differentiable_within_at

protected lemma differentiable : differentiable ğ•œ (Î»x, p.eval x) :=
Î»x, p.differentiable_at

protected lemma differentiable_on : differentiable_on ğ•œ (Î»x, p.eval x) s :=
p.differentiable.differentiable_on

@[simp] protected lemma deriv : deriv (Î»x, p.eval x) x = p.derivative.eval x :=
(p.has_deriv_at x).deriv

protected lemma deriv_within (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»x, p.eval x) s x = p.derivative.eval x :=
begin
  rw differentiable_at.deriv_within p.differentiable_at hxs,
  exact p.deriv
end

protected lemma has_fderiv_at (x : ğ•œ) :
  has_fderiv_at (Î»x, p.eval x) (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (p.derivative.eval x)) x :=
p.has_deriv_at x

protected lemma has_fderiv_within_at (x : ğ•œ) :
  has_fderiv_within_at (Î»x, p.eval x) (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (p.derivative.eval x)) s x :=
(p.has_fderiv_at x).has_fderiv_within_at

@[simp] protected lemma fderiv :
  fderiv ğ•œ (Î»x, p.eval x) x = smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (p.derivative.eval x) :=
(p.has_fderiv_at x).fderiv

protected lemma fderiv_within (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»x, p.eval x) s x = smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (p.derivative.eval x) :=
(p.has_fderiv_within_at x).fderiv_within hxs

end polynomial

section pow
/-! ### Derivative of `x â†¦ x^n` for `n : â„•` -/
variables {x : ğ•œ} {s : set ğ•œ} {c : ğ•œ â†’ ğ•œ} {c' : ğ•œ}
variable {n : â„• }

lemma has_strict_deriv_at_pow (n : â„•) (x : ğ•œ) :
  has_strict_deriv_at (Î»x, x^n) ((n : ğ•œ) * x^(n-1)) x :=
begin
  convert (polynomial.C (1 : ğ•œ) * (polynomial.X)^n).has_strict_deriv_at x,
  { simp },
  { rw [polynomial.derivative_C_mul_X_pow], simp }
end

lemma has_deriv_at_pow (n : â„•) (x : ğ•œ) : has_deriv_at (Î»x, x^n) ((n : ğ•œ) * x^(n-1)) x :=
(has_strict_deriv_at_pow n x).has_deriv_at

theorem has_deriv_within_at_pow (n : â„•) (x : ğ•œ) (s : set ğ•œ) :
  has_deriv_within_at (Î»x, x^n) ((n : ğ•œ) * x^(n-1)) s x :=
(has_deriv_at_pow n x).has_deriv_within_at

lemma differentiable_at_pow : differentiable_at ğ•œ (Î»x, x^n) x :=
(has_deriv_at_pow n x).differentiable_at

lemma differentiable_within_at_pow : differentiable_within_at ğ•œ (Î»x, x^n) s x :=
differentiable_at_pow.differentiable_within_at

lemma differentiable_pow : differentiable ğ•œ (Î»x:ğ•œ, x^n) :=
Î»x, differentiable_at_pow

lemma differentiable_on_pow : differentiable_on ğ•œ (Î»x, x^n) s :=
differentiable_pow.differentiable_on

lemma deriv_pow : deriv (Î»x, x^n) x = (n : ğ•œ) * x^(n-1) :=
(has_deriv_at_pow n x).deriv

@[simp] lemma deriv_pow' : deriv (Î»x, x^n) = Î» x, (n : ğ•œ) * x^(n-1) :=
funext $ Î» x, deriv_pow

lemma deriv_within_pow (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»x, x^n) s x = (n : ğ•œ) * x^(n-1) :=
(has_deriv_within_at_pow n x s).deriv_within hxs

lemma has_deriv_within_at.pow (hc : has_deriv_within_at c c' s x) :
  has_deriv_within_at (Î» y, (c y)^n) ((n : ğ•œ) * (c x)^(n-1) * c') s x :=
(has_deriv_at_pow n (c x)).comp_has_deriv_within_at x hc

lemma has_deriv_at.pow (hc : has_deriv_at c c' x) :
  has_deriv_at (Î» y, (c y)^n) ((n : ğ•œ) * (c x)^(n-1) * c') x :=
by { rw â† has_deriv_within_at_univ at *, exact hc.pow }

lemma differentiable_within_at.pow (hc : differentiable_within_at ğ•œ c s x) :
  differentiable_within_at ğ•œ (Î»x, (c x)^n) s x :=
hc.has_deriv_within_at.pow.differentiable_within_at

@[simp] lemma differentiable_at.pow (hc : differentiable_at ğ•œ c x) :
  differentiable_at ğ•œ (Î»x, (c x)^n) x :=
hc.has_deriv_at.pow.differentiable_at

lemma differentiable_on.pow (hc : differentiable_on ğ•œ c s) :
  differentiable_on ğ•œ (Î»x, (c x)^n) s :=
Î»x h, (hc x h).pow

@[simp] lemma differentiable.pow (hc : differentiable ğ•œ c) :
  differentiable ğ•œ (Î»x, (c x)^n) :=
Î»x, (hc x).pow

lemma deriv_within_pow' (hc : differentiable_within_at ğ•œ c s x)
  (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (Î»x, (c x)^n) s x = (n : ğ•œ) * (c x)^(n-1) * (deriv_within c s x) :=
hc.has_deriv_within_at.pow.deriv_within hxs

@[simp] lemma deriv_pow'' (hc : differentiable_at ğ•œ c x) :
  deriv (Î»x, (c x)^n) x = (n : ğ•œ) * (c x)^(n-1) * (deriv c x) :=
hc.has_deriv_at.pow.deriv

end pow

section zpow
/-! ### Derivative of `x â†¦ x^m` for `m : â„¤` -/
variables {x : ğ•œ} {s : set ğ•œ} {m : â„¤}

lemma has_strict_deriv_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  has_strict_deriv_at (Î»x, x^m) ((m : ğ•œ) * x^(m-1)) x :=
begin
  have : âˆ€ m : â„¤, 0 < m â†’ has_strict_deriv_at (Î»x, x^m) ((m:ğ•œ) * x^(m-1)) x,
  { assume m hm,
    lift m to â„• using (le_of_lt hm),
    simp only [zpow_coe_nat, int.cast_coe_nat],
    convert has_strict_deriv_at_pow _ _ using 2,
    rw [â† int.coe_nat_one, â† int.coe_nat_sub, zpow_coe_nat],
    norm_cast at hm,
    exact nat.succ_le_of_lt hm },
  rcases lt_trichotomy m 0 with hm|hm|hm,
  { have hx : x â‰  0, from h.resolve_right hm.not_le,
    have := (has_strict_deriv_at_inv _).scomp _ (this (-m) (neg_pos.2 hm));
      [skip, exact zpow_ne_zero_of_ne_zero hx _],
    simp only [(âˆ˜), zpow_negâ‚€, one_div, inv_inv, smul_eq_mul] at this,
    convert this using 1,
    rw [sq, mul_inv, inv_inv, int.cast_neg, neg_mul, neg_mul_neg,
      â† zpow_addâ‚€ hx, mul_assoc, â† zpow_addâ‚€ hx], congr, abel },
  { simp only [hm, zpow_zero, int.cast_zero, zero_mul, has_strict_deriv_at_const] },
  { exact this m hm }
end

lemma has_deriv_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  has_deriv_at (Î»x, x^m) ((m : ğ•œ) * x^(m-1)) x :=
(has_strict_deriv_at_zpow m x h).has_deriv_at

theorem has_deriv_within_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) (s : set ğ•œ) :
  has_deriv_within_at (Î»x, x^m) ((m : ğ•œ) * x^(m-1)) s x :=
(has_deriv_at_zpow m x h).has_deriv_within_at

lemma differentiable_at_zpow : differentiable_at ğ•œ (Î»x, x^m) x â†” x â‰  0 âˆ¨ 0 â‰¤ m :=
âŸ¨Î» H, normed_field.continuous_at_zpow.1 H.continuous_at,
  Î» H, (has_deriv_at_zpow m x H).differentiable_atâŸ©

lemma differentiable_within_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  differentiable_within_at ğ•œ (Î»x, x^m) s x :=
(differentiable_at_zpow.mpr h).differentiable_within_at

lemma differentiable_on_zpow (m : â„¤) (s : set ğ•œ) (h : (0 : ğ•œ) âˆ‰ s âˆ¨ 0 â‰¤ m) :
  differentiable_on ğ•œ (Î»x, x^m) s :=
Î» x hxs, differentiable_within_at_zpow m x $ h.imp_left $ ne_of_mem_of_not_mem hxs

lemma deriv_zpow (m : â„¤) (x : ğ•œ) : deriv (Î» x, x ^ m) x = m * x ^ (m - 1) :=
begin
  by_cases H : x â‰  0 âˆ¨ 0 â‰¤ m,
  { exact (has_deriv_at_zpow m x H).deriv },
  { rw deriv_zero_of_not_differentiable_at (mt differentiable_at_zpow.1 H),
    push_neg at H, rcases H with âŸ¨rfl, hmâŸ©,
    rw [zero_zpow _ ((sub_one_lt _).trans hm).ne, mul_zero] }
end

@[simp] lemma deriv_zpow' (m : â„¤) : deriv (Î» x : ğ•œ, x ^ m) = Î» x, m * x ^ (m - 1) :=
funext $ deriv_zpow m

lemma deriv_within_zpow (hxs : unique_diff_within_at ğ•œ s x) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  deriv_within (Î»x, x^m) s x = (m : ğ•œ) * x^(m-1) :=
(has_deriv_within_at_zpow m x h s).deriv_within hxs

@[simp] lemma iter_deriv_zpow' (m : â„¤) (k : â„•) :
  deriv^[k] (Î» x : ğ•œ, x ^ m) = Î» x, (âˆ i in finset.range k, (m - i)) * x ^ (m - k) :=
begin
  induction k with k ihk,
  { simp only [one_mul, int.coe_nat_zero, id, sub_zero, finset.prod_range_zero,
      function.iterate_zero] },
  { simp only [function.iterate_succ_apply', ihk, deriv_const_mul_field', deriv_zpow',
      finset.prod_range_succ, int.coe_nat_succ, â† sub_sub, int.cast_sub, int.cast_coe_nat,
      mul_assoc], }
end

lemma iter_deriv_zpow (m : â„¤) (x : ğ•œ) (k : â„•) :
  deriv^[k] (Î» y, y ^ m) x = (âˆ i in finset.range k, (m - i)) * x ^ (m - k) :=
congr_fun (iter_deriv_zpow' m k) x

lemma iter_deriv_pow (n : â„•) (x : ğ•œ) (k : â„•) :
  deriv^[k] (Î»x:ğ•œ, x^n) x = (âˆ i in finset.range k, (n - i)) * x^(n-k) :=
begin
  simp only [â† zpow_coe_nat, iter_deriv_zpow, int.cast_coe_nat],
  cases le_or_lt k n with hkn hnk,
  { rw int.coe_nat_sub hkn },
  { have : âˆ i in finset.range k, (n - i : ğ•œ) = 0,
      from finset.prod_eq_zero (finset.mem_range.2 hnk) (sub_self _),
    simp only [this, zero_mul] }
end

@[simp] lemma iter_deriv_pow' (n k : â„•) :
  deriv^[k] (Î» x : ğ•œ, x ^ n) = Î» x, (âˆ i in finset.range k, (n - i)) * x ^ (n - k) :=
funext $ Î» x, iter_deriv_pow n x k

lemma iter_deriv_inv (k : â„•) (x : ğ•œ) :
  deriv^[k] has_inv.inv x = (âˆ i in finset.range k, (-1 - i)) * x ^ (-1 - k : â„¤) :=
by simpa only [zpow_neg_one, int.cast_neg, int.cast_one] using iter_deriv_zpow (-1) x k

@[simp] lemma iter_deriv_inv' (k : â„•) :
  deriv^[k] has_inv.inv = Î» x : ğ•œ, (âˆ i in finset.range k, (-1 - i)) * x ^ (-1 - k : â„¤) :=
funext (iter_deriv_inv k)

end zpow

/-! ### Support of derivatives -/

section support

open function
variables {F : Type*} [normed_group F] [normed_space ğ•œ F] {f : ğ•œ â†’ F}

lemma support_deriv_subset : support (deriv f) âŠ† tsupport f :=
begin
  intros x,
  rw [â† not_imp_not],
  intro h2x,
  rw [not_mem_closure_support_iff_eventually_eq] at h2x,
  exact nmem_support.mpr (h2x.deriv_eq.trans (deriv_const x 0))
end

lemma has_compact_support.deriv (hf : has_compact_support f) : has_compact_support (deriv f) :=
hf.mono' support_deriv_subset

end support

/-! ### Upper estimates on liminf and limsup -/

section real

variables {f : â„ â†’ â„} {f' : â„} {s : set â„} {x : â„} {r : â„}

lemma has_deriv_within_at.limsup_slope_le (hf : has_deriv_within_at f f' s x) (hr : f' < r) :
  âˆ€á¶  z in ğ“[s \ {x}] x, slope f x z < r :=
has_deriv_within_at_iff_tendsto_slope.1 hf (is_open.mem_nhds is_open_Iio hr)

lemma has_deriv_within_at.limsup_slope_le' (hf : has_deriv_within_at f f' s x)
  (hs : x âˆ‰ s) (hr : f' < r) :
  âˆ€á¶  z in ğ“[s] x, slope f x z < r :=
(has_deriv_within_at_iff_tendsto_slope' hs).1 hf (is_open.mem_nhds is_open_Iio hr)

lemma has_deriv_within_at.liminf_right_slope_le
  (hf : has_deriv_within_at f f' (Ici x) x) (hr : f' < r) :
  âˆƒá¶  z in ğ“[>] x, slope f x z < r :=
(hf.Ioi_of_Ici.limsup_slope_le' (lt_irrefl x) hr).frequently

end real

section real_space

open metric

variables {E : Type u} [normed_group E] [normed_space â„ E] {f : â„ â†’ E} {f' : E} {s : set â„}
  {x r : â„}

/-- If `f` has derivative `f'` within `s` at `x`, then for any `r > âˆ¥f'âˆ¥` the ratio
`âˆ¥f z - f xâˆ¥ / âˆ¥z - xâˆ¥` is less than `r` in some neighborhood of `x` within `s`.
In other words, the limit superior of this ratio as `z` tends to `x` along `s`
is less than or equal to `âˆ¥f'âˆ¥`. -/
lemma has_deriv_within_at.limsup_norm_slope_le
  (hf : has_deriv_within_at f f' s x) (hr : âˆ¥f'âˆ¥ < r) :
  âˆ€á¶  z in ğ“[s] x, âˆ¥z - xâˆ¥â»Â¹ * âˆ¥f z - f xâˆ¥ < r :=
begin
  have hrâ‚€ : 0 < r, from lt_of_le_of_lt (norm_nonneg f') hr,
  have A : âˆ€á¶  z in ğ“[s \ {x}] x, âˆ¥(z - x)â»Â¹ â€¢ (f z - f x)âˆ¥ âˆˆ Iio r,
    from (has_deriv_within_at_iff_tendsto_slope.1 hf).norm (is_open.mem_nhds is_open_Iio hr),
  have B : âˆ€á¶  z in ğ“[{x}] x, âˆ¥(z - x)â»Â¹ â€¢ (f z - f x)âˆ¥ âˆˆ Iio r,
    from mem_of_superset self_mem_nhds_within
      (singleton_subset_iff.2 $ by simp [hrâ‚€]),
  have C := mem_sup.2 âŸ¨A, BâŸ©,
  rw [â† nhds_within_union, diff_union_self, nhds_within_union, mem_sup] at C,
  filter_upwards [C.1],
  simp only [norm_smul, mem_Iio, norm_inv],
  exact Î» _, id
end

/-- If `f` has derivative `f'` within `s` at `x`, then for any `r > âˆ¥f'âˆ¥` the ratio
`(âˆ¥f zâˆ¥ - âˆ¥f xâˆ¥) / âˆ¥z - xâˆ¥` is less than `r` in some neighborhood of `x` within `s`.
In other words, the limit superior of this ratio as `z` tends to `x` along `s`
is less than or equal to `âˆ¥f'âˆ¥`.

This lemma is a weaker version of `has_deriv_within_at.limsup_norm_slope_le`
where `âˆ¥f zâˆ¥ - âˆ¥f xâˆ¥` is replaced by `âˆ¥f z - f xâˆ¥`. -/
lemma has_deriv_within_at.limsup_slope_norm_le
  (hf : has_deriv_within_at f f' s x) (hr : âˆ¥f'âˆ¥ < r) :
  âˆ€á¶  z in ğ“[s] x, âˆ¥z - xâˆ¥â»Â¹ * (âˆ¥f zâˆ¥ - âˆ¥f xâˆ¥) < r :=
begin
  apply (hf.limsup_norm_slope_le hr).mono,
  assume z hz,
  refine lt_of_le_of_lt (mul_le_mul_of_nonneg_left (norm_sub_norm_le _ _) _) hz,
  exact inv_nonneg.2 (norm_nonneg _)
end

/-- If `f` has derivative `f'` within `(x, +âˆ)` at `x`, then for any `r > âˆ¥f'âˆ¥` the ratio
`âˆ¥f z - f xâˆ¥ / âˆ¥z - xâˆ¥` is frequently less than `r` as `z â†’ x+0`.
In other words, the limit inferior of this ratio as `z` tends to `x+0`
is less than or equal to `âˆ¥f'âˆ¥`. See also `has_deriv_within_at.limsup_norm_slope_le`
for a stronger version using limit superior and any set `s`. -/
lemma has_deriv_within_at.liminf_right_norm_slope_le
  (hf : has_deriv_within_at f f' (Ici x) x) (hr : âˆ¥f'âˆ¥ < r) :
  âˆƒá¶  z in ğ“[>] x, âˆ¥z - xâˆ¥â»Â¹ * âˆ¥f z - f xâˆ¥ < r :=
(hf.Ioi_of_Ici.limsup_norm_slope_le hr).frequently

/-- If `f` has derivative `f'` within `(x, +âˆ)` at `x`, then for any `r > âˆ¥f'âˆ¥` the ratio
`(âˆ¥f zâˆ¥ - âˆ¥f xâˆ¥) / (z - x)` is frequently less than `r` as `z â†’ x+0`.
In other words, the limit inferior of this ratio as `z` tends to `x+0`
is less than or equal to `âˆ¥f'âˆ¥`.

See also

* `has_deriv_within_at.limsup_norm_slope_le` for a stronger version using
  limit superior and any set `s`;
* `has_deriv_within_at.liminf_right_norm_slope_le` for a stronger version using
  `âˆ¥f z - f xâˆ¥` instead of `âˆ¥f zâˆ¥ - âˆ¥f xâˆ¥`. -/
lemma has_deriv_within_at.liminf_right_slope_norm_le
  (hf : has_deriv_within_at f f' (Ici x) x) (hr : âˆ¥f'âˆ¥ < r) :
  âˆƒá¶  z in ğ“[>] x, (z - x)â»Â¹ * (âˆ¥f zâˆ¥ - âˆ¥f xâˆ¥) < r :=
begin
  have := (hf.Ioi_of_Ici.limsup_slope_norm_le hr).frequently,
  refine this.mp (eventually.mono self_mem_nhds_within _),
  assume z hxz hz,
  rwa [real.norm_eq_abs, abs_of_pos (sub_pos_of_lt hxz)] at hz
end

end real_space
