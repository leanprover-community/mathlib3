/-
Copyright (c) 2019 Gabriel Ebner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Ebner
-/
import analysis.calculus.fderiv

/-!

# One-dimensional derivatives

This file defines the derivative of a function `f : ğ•œ â†’ F` where `ğ•œ` is a
normed field and `F` is a normed space over this field. The derivative of
such a function `f` at a point `x` is given by an element `f' : F`.

The theory is developed analogously to the [FrÃ©chet
derivatives](./fderiv.lean). We first introduce predicates defined in terms
of the corresponding predicates for FrÃ©chet derivatives:

 - `has_deriv_at_filter f f' x L` states that the function `f` has the
    derivative `f'` at the point `x` as `x` goes along the filter `L`.

 - `has_deriv_within_at f f' s x` states that the function `f` has the
    derivative `f'` at the point `x` within the subset `s`.

 - `has_deriv_at f f' x` states that the function `f` has the derivative `f'`
    at the point `x`.

For the last two notions we also define a functional version:

  - `deriv_within f s x` is a derivative of `f` at `x` within `s`. If the
    derivative does not exist, then `deriv_within f s x` equals zero.

  - `deriv f x` is a derivative of `f` at `x`. If the derivative does not
    exist, then `deriv f x` equals zero.

The theorems `fderiv_within_deriv_within` and `fderiv_deriv` show that the
one-dimensional derivatives coincide with the general FrÃ©chet derivatives.

We also show the existence and compute the derivatives of:
  - constants
  - the identity function
  - linear maps
  - addition
  - subtraction
  - multiplication
  - negation
  - multiplication of two functions in `ğ•œ â†’ ğ•œ`
  - multiplication of a function in `ğ•œ â†’ ğ•œ` and of a function in `ğ•œ â†’ E`
  - composition of a function in `ğ•œ â†’ F` with a function in `ğ•œ â†’ ğ•œ`
  - composition of a function in `F â†’ E` with a function in `ğ•œ â†’ F`

## Implementation notes

Most of the theorems are direct restatements of the corresponding theorems
for FrÃ©chet derivatives.

-/

universes u v w
noncomputable theory
open_locale classical topological_space
open filter asymptotics set

set_option class.instance_max_depth 100

variables {ğ•œ : Type u} [nondiscrete_normed_field ğ•œ]
variables {F : Type v} [normed_group F] [normed_space ğ•œ F]
variables {E : Type w} [normed_group E] [normed_space ğ•œ E]

/--
`f` has the derivative `f'` at the point `x` as `x` goes along the filter `L`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges along the filter `L`.
-/
def has_deriv_at_filter (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) (L : filter ğ•œ) :=
has_fderiv_at_filter f (continuous_linear_map.smul_right 1 f' : ğ•œ â†’L[ğ•œ] F) x L

/--
`f` has the derivative `f'` at the point `x` within the subset `s`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def has_deriv_within_at (f : ğ•œ â†’ F) (f' : F) (s : set ğ•œ) (x : ğ•œ) :=
has_deriv_at_filter f f' x (nhds_within x s)

/--
`f` has the derivative `f'` at the point `x`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x`.
-/
def has_deriv_at (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) :=
has_deriv_at_filter f f' x (ğ“ x)

/--
Derivative of `f` at the point `x` within the set `s`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', has_deriv_within_at f f' s x`), then
`f x' = f x + (x' - x) â€¢ deriv_within f s x + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def deriv_within (f : ğ•œ â†’ F) (s : set ğ•œ) (x : ğ•œ) :=
(fderiv_within ğ•œ f s x : ğ•œ â†’L[ğ•œ] F) 1

/--
Derivative of `f` at the point `x`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', has_deriv_at f f' x`), then
`f x' = f x + (x' - x) â€¢ deriv f x + o(x' - x)` where `x'` converges to `x`.
-/
def deriv (f : ğ•œ â†’ F) (x : ğ•œ) :=
(fderiv ğ•œ f x : ğ•œ â†’L[ğ•œ] F) 1

variables {f fâ‚€ fâ‚ g : ğ•œ â†’ F}
variables {f' fâ‚€' fâ‚' g' : F}
variables {x : ğ•œ}
variables {s t : set ğ•œ}
variables {L Lâ‚ Lâ‚‚ : filter ğ•œ}

/-- Expressing `has_fderiv_at_filter f f' x L` in terms of `has_deriv_at_filter` -/
lemma has_fderiv_at_filter_iff_has_deriv_at_filter {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at_filter f f' x L â†” has_deriv_at_filter f (f' 1) x L :=
by simp [has_deriv_at_filter]

/-- Expressing `has_fderiv_within_at f f' s x` in terms of `has_deriv_within_at` -/
lemma has_fderiv_within_at_iff_has_deriv_within_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_within_at f f' s x â†” has_deriv_within_at f (f' 1) s x :=
by simp [has_deriv_within_at, has_deriv_at_filter, has_fderiv_within_at]

/-- Expressing `has_deriv_within_at f f' s x` in terms of `has_fderiv_within_at` -/
lemma has_deriv_within_at_iff_has_fderiv_within_at {f' : F} :
  has_deriv_within_at f f' s x â†”
  has_fderiv_within_at f (continuous_linear_map.smul_right 1 f' : ğ•œ â†’L[ğ•œ] F) s x :=
iff.rfl

/-- Expressing `has_fderiv_at f f' x` in terms of `has_deriv_at` -/
lemma has_fderiv_at_iff_has_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at f f' x â†” has_deriv_at f (f' 1) x :=
by simp [has_deriv_at, has_deriv_at_filter, has_fderiv_at]

/-- Expressing `has_deriv_at f f' x` in terms of `has_fderiv_at` -/
lemma has_deriv_at_iff_has_fderiv_at {f' : F} :
  has_deriv_at f f' x â†”
  has_fderiv_at f (continuous_linear_map.smul_right 1 f' : ğ•œ â†’L[ğ•œ] F) x :=
iff.rfl

lemma deriv_within_zero_of_not_differentiable_within_at
  (h : Â¬ differentiable_within_at ğ•œ f s x) : deriv_within f s x = 0 :=
by { unfold deriv_within, rw fderiv_within_zero_of_not_differentiable_within_at, simp, assumption }

lemma deriv_zero_of_not_differentiable_at (h : Â¬ differentiable_at ğ•œ f x) : deriv f x = 0 :=
by { unfold deriv, rw fderiv_zero_of_not_differentiable_at, simp, assumption }

theorem unique_diff_within_at.eq_deriv (s : set ğ•œ) (H : unique_diff_within_at ğ•œ s x)
  (h : has_deriv_within_at f f' s x) (hâ‚ : has_deriv_within_at f fâ‚' s x) : f' = fâ‚' :=
continuous_linear_map.smul_right_one_eq_iff.mp $ unique_diff_within_at.eq H h hâ‚

theorem has_deriv_at_filter_iff_tendsto :
  has_deriv_at_filter f f' x L â†”
  tendsto (Î» x' : ğ•œ, âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - (x' - x) â€¢ f'âˆ¥) L (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_within_at_iff_tendsto : has_deriv_within_at f f' s x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - (x' - x) â€¢ f'âˆ¥) (nhds_within x s) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_at_iff_tendsto : has_deriv_at f f' x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - (x' - x) â€¢ f'âˆ¥) (ğ“ x) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_at_iff_is_o_nhds_zero : has_deriv_at f f' x â†”
  is_o (Î»h, f (x + h) - f x - h â€¢ f') (Î»h, h) (ğ“ 0) :=
has_fderiv_at_iff_is_o_nhds_zero

theorem has_deriv_at_filter.mono (h : has_deriv_at_filter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
  has_deriv_at_filter f f' x Lâ‚ :=
has_fderiv_at_filter.mono h hst

theorem has_deriv_within_at.mono (h : has_deriv_within_at f f' t x) (hst : s âŠ† t) :
  has_deriv_within_at f f' s x :=
has_fderiv_within_at.mono h hst

theorem has_deriv_at.has_deriv_at_filter (h : has_deriv_at f f' x) (hL : L â‰¤ ğ“ x) :
  has_deriv_at_filter f f' x L :=
has_fderiv_at.has_fderiv_at_filter h hL

theorem has_deriv_at.has_deriv_within_at
  (h : has_deriv_at f f' x) : has_deriv_within_at f f' s x :=
has_fderiv_at.has_fderiv_within_at h

lemma has_deriv_within_at.differentiable_within_at (h : has_deriv_within_at f f' s x) :
  differentiable_within_at ğ•œ f s x :=
has_fderiv_within_at.differentiable_within_at h

lemma has_deriv_at.differentiable_at (h : has_deriv_at f f' x) : differentiable_at ğ•œ f x :=
has_fderiv_at.differentiable_at h

@[simp] lemma has_deriv_within_at_univ : has_deriv_within_at f f' univ x â†” has_deriv_at f f' x :=
has_fderiv_within_at_univ

theorem has_deriv_at_unique
  (hâ‚€ : has_deriv_at f fâ‚€' x) (hâ‚ : has_deriv_at f fâ‚' x) : fâ‚€' = fâ‚' :=
continuous_linear_map.smul_right_one_eq_iff.mp $ has_fderiv_at_unique hâ‚€ hâ‚

lemma has_deriv_within_at_inter' (h : t âˆˆ nhds_within x s) :
  has_deriv_within_at f f' (s âˆ© t) x â†” has_deriv_within_at f f' s x :=
has_fderiv_within_at_inter' h

lemma has_deriv_within_at_inter (h : t âˆˆ ğ“ x) :
  has_deriv_within_at f f' (s âˆ© t) x â†” has_deriv_within_at f f' s x :=
has_fderiv_within_at_inter h

lemma differentiable_within_at.has_deriv_within_at (h : differentiable_within_at ğ•œ f s x) :
  has_deriv_within_at f (deriv_within f s x) s x :=
show has_fderiv_within_at _ _ _ _, by { convert h.has_fderiv_within_at, simp [deriv_within] }

lemma differentiable_at.has_deriv_at (h : differentiable_at ğ•œ f x) : has_deriv_at f (deriv f x) x :=
show has_fderiv_at _ _ _, by { convert h.has_fderiv_at, simp [deriv] }

lemma has_deriv_at.deriv (h : has_deriv_at f f' x) : deriv f x = f' :=
has_deriv_at_unique h.differentiable_at.has_deriv_at h

lemma has_deriv_within_at.deriv_within
  (h : has_deriv_within_at f f' s x) (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within f s x = f' :=
hxs.eq_deriv _ h.differentiable_within_at.has_deriv_within_at h

lemma fderiv_within_deriv_within : (fderiv_within ğ•œ f s x : ğ•œ â†’ F) 1 = deriv_within f s x :=
rfl

lemma deriv_within_fderiv_within :
  continuous_linear_map.smul_right 1 (deriv_within f s x) = fderiv_within ğ•œ f s x :=
by simp [deriv_within]

lemma fderiv_deriv : (fderiv ğ•œ f x : ğ•œ â†’ F) 1 = deriv f x :=
rfl

lemma deriv_fderiv :
  continuous_linear_map.smul_right 1 (deriv f x) = fderiv ğ•œ f x :=
by simp [deriv]

lemma differentiable_at.deriv_within (h : differentiable_at ğ•œ f x)
  (hxs : unique_diff_within_at ğ•œ s x) : deriv_within f s x = deriv f x :=
by { unfold deriv_within deriv, rw h.fderiv_within hxs }

lemma deriv_within_subset (st : s âŠ† t) (ht : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f t x) :
  deriv_within f s x = deriv_within f t x :=
((differentiable_within_at.has_deriv_within_at h).mono st).deriv_within ht

@[simp] lemma deriv_within_univ : deriv_within f univ = deriv f :=
by { ext, unfold deriv_within deriv, rw fderiv_within_univ }

lemma deriv_within_inter (ht : t âˆˆ ğ“ x) (hs : unique_diff_within_at ğ•œ s x) :
  deriv_within f (s âˆ© t) x = deriv_within f s x :=
by { unfold deriv_within, rw fderiv_within_inter ht hs }

section congr
/-! ### Congruence properties of derivatives -/

theorem has_deriv_at_filter_congr_of_mem_sets
  (hx : fâ‚€ x = fâ‚ x) (hâ‚€ : {x | fâ‚€ x = fâ‚ x} âˆˆ L) (hâ‚ : fâ‚€' = fâ‚') :
  has_deriv_at_filter fâ‚€ fâ‚€' x L â†” has_deriv_at_filter fâ‚ fâ‚' x L :=
has_fderiv_at_filter_congr_of_mem_sets hx hâ‚€ (by simp [hâ‚])

lemma has_deriv_at_filter.congr_of_mem_sets (h : has_deriv_at_filter f f' x L)
  (hL : {x | fâ‚ x = f x} âˆˆ L) (hx : fâ‚ x = f x) : has_deriv_at_filter fâ‚ f' x L :=
by rwa has_deriv_at_filter_congr_of_mem_sets hx hL rfl

lemma has_deriv_within_at.congr_mono (h : has_deriv_within_at f f' s x) (ht : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : has_deriv_within_at fâ‚ f' t x :=
has_fderiv_within_at.congr_mono h ht hx hâ‚

lemma has_deriv_within_at.congr_of_mem_nhds_within (h : has_deriv_within_at f f' s x)
  (hâ‚ : {y | fâ‚ y = f y} âˆˆ nhds_within x s) (hx : fâ‚ x = f x) : has_deriv_within_at fâ‚ f' s x :=
has_deriv_at_filter.congr_of_mem_sets h hâ‚ hx

lemma has_deriv_at.congr_of_mem_nhds (h : has_deriv_at f f' x)
  (hâ‚ : {y | fâ‚ y = f y} âˆˆ ğ“ x) : has_deriv_at fâ‚ f' x :=
has_deriv_at_filter.congr_of_mem_sets h hâ‚ (mem_of_nhds hâ‚ : _)

lemma deriv_within_congr_of_mem_nhds_within (hs : unique_diff_within_at ğ•œ s x)
  (hL : {y | fâ‚ y = f y} âˆˆ nhds_within x s) (hx : fâ‚ x = f x) :
  deriv_within fâ‚ s x = deriv_within f s x :=
by { unfold deriv_within, rw fderiv_within_congr_of_mem_nhds_within hs hL hx }

lemma deriv_within_congr (hs : unique_diff_within_at ğ•œ s x)
  (hL : âˆ€yâˆˆs, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  deriv_within fâ‚ s x = deriv_within f s x :=
by { unfold deriv_within, rw fderiv_within_congr hs hL hx }

lemma deriv_congr_of_mem_nhds (hL : {y | fâ‚ y = f y} âˆˆ ğ“ x) : deriv fâ‚ x = deriv f x :=
by { unfold deriv, rwa fderiv_congr_of_mem_nhds }

end congr

section id
/-! ### Derivative of the identity -/
variables (s x L)

theorem has_deriv_at_filter_id : has_deriv_at_filter id 1 x L :=
(is_o_zero _ _).congr_left $ by simp

theorem has_deriv_within_at_id : has_deriv_within_at id 1 s x :=
has_deriv_at_filter_id _ _

theorem has_deriv_at_id : has_deriv_at id 1 x :=
has_deriv_at_filter_id _ _

@[simp] lemma deriv_id : deriv id x = 1 :=
has_deriv_at.deriv (has_deriv_at_id x)

lemma deriv_within_id (hxs : unique_diff_within_at ğ•œ s x) : deriv_within id s x = 1 :=
by { unfold deriv_within, rw fderiv_within_id, simp, assumption }

end id

section const
/-! ### Derivative of constant functions -/
variables (c : F) (s x L)

theorem has_deriv_at_filter_const : has_deriv_at_filter (Î» x, c) 0 x L :=
(is_o_zero _ _).congr_left $ Î» _, by simp [continuous_linear_map.zero_apply, sub_self]

theorem has_deriv_within_at_const : has_deriv_within_at (Î» x, c) 0 s x :=
has_deriv_at_filter_const _ _ _

theorem has_deriv_at_const : has_deriv_at (Î» x, c) 0 x :=
has_deriv_at_filter_const _ _ _

lemma deriv_const : deriv (Î» x, c) x = 0 :=
has_deriv_at.deriv (has_deriv_at_const x c)

lemma deriv_within_const (hxs : unique_diff_within_at ğ•œ s x) : deriv_within (Î» x, c) s x = 0 :=
by { rw (differentiable_at_const _).deriv_within hxs, apply deriv_const }

end const

section is_linear_map
/-! ### Derivative of linear maps -/
variables (s x L) [is_linear_map ğ•œ f]

lemma is_linear_map.has_deriv_at_filter : has_deriv_at_filter f (f 1) x L :=
(is_o_zero _ _).congr_left begin
  intro y,
  simp [add_smul],
  rw â† is_linear_map.smul f x,
  rw â† is_linear_map.smul f y,
  simp
end

lemma is_linear_map.has_deriv_within_at : has_deriv_within_at f (f 1) s x :=
is_linear_map.has_deriv_at_filter _ _

lemma is_linear_map.has_deriv_at : has_deriv_at f (f 1) x  :=
is_linear_map.has_deriv_at_filter _ _

lemma is_linear_map.differentiable_at : differentiable_at ğ•œ f x :=
(is_linear_map.has_deriv_at _).differentiable_at

lemma is_linear_map.differentiable_within_at : differentiable_within_at ğ•œ f s x :=
(is_linear_map.differentiable_at _).differentiable_within_at

@[simp] lemma is_linear_map.deriv : deriv f x = f 1 :=
has_deriv_at.deriv (is_linear_map.has_deriv_at _)

lemma is_linear_map.deriv_within (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within f s x = f 1 :=
begin
  rw differentiable_at.deriv_within (is_linear_map.differentiable_at _) hxs,
  apply is_linear_map.deriv,
  assumption
end

lemma is_linear_map.differentiable : differentiable ğ•œ f :=
Î» x, is_linear_map.differentiable_at _

lemma is_linear_map.differentiable_on : differentiable_on ğ•œ f s :=
is_linear_map.differentiable.differentiable_on

end is_linear_map

section add
/-! ### Derivative of the sum of two functions -/

theorem has_deriv_at_filter.add
  (hf : has_deriv_at_filter f f' x L) (hg : has_deriv_at_filter g g' x L) :
  has_deriv_at_filter (Î» y, f y + g y) (f' + g') x L :=
(hf.add hg).congr_left $ by simp [add_smul, smul_add]

theorem has_deriv_within_at.add
  (hf : has_deriv_within_at f f' s x) (hg : has_deriv_within_at g g' s x) :
  has_deriv_within_at (Î» y, f y + g y) (f' + g') s x :=
hf.add hg

theorem has_deriv_at.add
  (hf : has_deriv_at f f' x) (hg : has_deriv_at g g' x) :
  has_deriv_at (Î» x, f x + g x) (f' + g') x :=
hf.add hg

lemma deriv_within_add (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  deriv_within (Î»y, f y + g y) s x = deriv_within f s x + deriv_within g s x :=
(hf.has_deriv_within_at.add hg.has_deriv_within_at).deriv_within hxs

lemma deriv_add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  deriv (Î»y, f y + g y) x = deriv f x + deriv g x :=
(hf.has_deriv_at.add hg.has_deriv_at).deriv

end add

section neg
/-! ### Derivative of the negative of a function -/

theorem has_deriv_at_filter.neg (h : has_deriv_at_filter f f' x L) :
  has_deriv_at_filter (Î» x, -f x) (-f') x L :=
(h.smul (-1)).congr (by simp) (by simp)

theorem has_deriv_within_at.neg (h : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Î» x, -f x) (-f') s x :=
h.neg

theorem has_deriv_at.neg (h : has_deriv_at f f' x) : has_deriv_at (Î» x, -f x) (-f') x :=
h.neg

lemma deriv_within_neg (hxs : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f s x) :
  deriv_within (Î»y, -f y) s x = - deriv_within f s x :=
h.has_deriv_within_at.neg.deriv_within hxs

lemma deriv_neg (h : differentiable_at ğ•œ f x) : deriv (Î»y, -f y) x = - deriv f x :=
h.has_deriv_at.neg.deriv

end neg

section sub
/-! ### Derivative of the difference of two functions -/

theorem has_deriv_at_filter.sub
  (hf : has_deriv_at_filter f f' x L) (hg : has_deriv_at_filter g g' x L) :
  has_deriv_at_filter (Î» x, f x - g x) (f' - g') x L :=
hf.add hg.neg

theorem has_deriv_within_at.sub
  (hf : has_deriv_within_at f f' s x) (hg : has_deriv_within_at g g' s x) :
  has_deriv_within_at (Î» x, f x - g x) (f' - g') s x :=
hf.sub hg

theorem has_deriv_at.sub
  (hf : has_deriv_at f f' x) (hg : has_deriv_at g g' x) :
  has_deriv_at (Î» x, f x - g x) (f' - g') x :=
hf.sub hg

lemma deriv_within_sub (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  deriv_within (Î»y, f y - g y) s x = deriv_within f s x - deriv_within g s x :=
(hf.has_deriv_within_at.sub hg.has_deriv_within_at).deriv_within hxs

lemma deriv_sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  deriv (Î» y, f y - g y) x = deriv f x - deriv g x :=
(hf.has_deriv_at.sub hg.has_deriv_at).deriv

theorem has_deriv_at_filter.is_O_sub (h : has_deriv_at_filter f f' x L) :
  is_O (Î» x', f x' - f x) (Î» x', x' - x) L :=
has_fderiv_at_filter.is_O_sub h

end sub

section continuous
/-! ### Continuity of a function admitting a derivative -/

theorem has_deriv_at_filter.tendsto_nhds
  (hL : L â‰¤ ğ“ x) (h : has_deriv_at_filter f f' x L) :
  tendsto f L (ğ“ (f x)) :=
has_fderiv_at_filter.tendsto_nhds hL h

theorem has_deriv_within_at.continuous_within_at
  (h : has_deriv_within_at f f' s x) : continuous_within_at f s x :=
has_deriv_at_filter.tendsto_nhds lattice.inf_le_left h

theorem has_deriv_at.continuous_at (h : has_deriv_at f f' x) : continuous_at f x :=
has_deriv_at_filter.tendsto_nhds (le_refl _) h

end continuous

section cartesian_product
/-! ### Derivative of the cartesian product of two functions -/

variables {G : Type w} [normed_group G] [normed_space ğ•œ G]
variables {fâ‚‚ : ğ•œ â†’ G} {fâ‚‚' : G}

lemma has_deriv_at_filter.prod
  (hfâ‚ : has_deriv_at_filter fâ‚ fâ‚' x L) (hfâ‚‚ : has_deriv_at_filter fâ‚‚ fâ‚‚' x L) :
  has_deriv_at_filter (Î» x, (fâ‚ x, fâ‚‚ x)) (fâ‚', fâ‚‚') x L :=
show has_fderiv_at_filter _ _ _ _,
by convert has_fderiv_at_filter.prod hfâ‚ hfâ‚‚

lemma has_deriv_within_at.prod
  (hfâ‚ : has_deriv_within_at fâ‚ fâ‚' s x) (hfâ‚‚ : has_deriv_within_at fâ‚‚ fâ‚‚' s x) :
  has_deriv_within_at (Î» x, (fâ‚ x, fâ‚‚ x)) (fâ‚', fâ‚‚') s x :=
hfâ‚.prod hfâ‚‚

lemma has_deriv_at.prod (hfâ‚ : has_deriv_at fâ‚ fâ‚' x) (hfâ‚‚ : has_deriv_at fâ‚‚ fâ‚‚' x) :
  has_deriv_at (Î» x, (fâ‚ x, fâ‚‚ x)) (fâ‚', fâ‚‚') x :=
hfâ‚.prod hfâ‚‚

end cartesian_product

section composition
/-! ### Derivative of the composition of a vector valued function and a scalar function -/

variables {h : ğ•œ â†’ ğ•œ} {h' : ğ•œ}
/- For composition lemmas, we put x explicit to help the elaborator, as otherwise Lean tends to
get confused since there are too many possibilities for composition -/
variable (x)

theorem has_deriv_at_filter.comp
  (hg : has_deriv_at_filter g g' (h x) (L.map h))
  (hh : has_deriv_at_filter h h' x L) :
  has_deriv_at_filter (g âˆ˜ h) (h' â€¢ g') x L :=
have (continuous_linear_map.smul_right 1 g' : ğ•œ â†’L[ğ•œ] _).comp
      (continuous_linear_map.smul_right 1 h' : ğ•œ â†’L[ğ•œ] _) =
    continuous_linear_map.smul_right 1 (h' â€¢ g'), by { ext, simp [mul_smul] },
begin
  unfold has_deriv_at_filter,
  rw â† this,
  exact has_fderiv_at_filter.comp x hg hh,
end

theorem has_deriv_within_at.comp {t : set ğ•œ}
  (hg : has_deriv_within_at g g' t (h x))
  (hh : has_deriv_within_at h h' s x) (hst : s âŠ† h â»Â¹' t) :
  has_deriv_within_at (g âˆ˜ h) (h' â€¢ g') s x :=
begin
  apply has_deriv_at_filter.comp _ (has_deriv_at_filter.mono hg _) hh,
  calc map h (nhds_within x s)
      â‰¤ nhds_within (h x) (h '' s) : hh.continuous_within_at.tendsto_nhds_within_image
  ... â‰¤ nhds_within (h x) t        : nhds_within_mono _ (image_subset_iff.mpr hst)
end

/-- The chain rule. -/
theorem has_deriv_at.comp
  (hg : has_deriv_at g g' (h x)) (hh : has_deriv_at h h' x) :
  has_deriv_at (g âˆ˜ h) (h' â€¢ g') x :=
(hg.mono hh.continuous_at).comp x hh

theorem has_deriv_at.comp_has_deriv_within_at
  (hg : has_deriv_at g g' (h x)) (hh : has_deriv_within_at h h' s x) :
  has_deriv_within_at (g âˆ˜ h) (h' â€¢ g') s x :=
begin
  rw â† has_deriv_within_at_univ at hg,
  exact has_deriv_within_at.comp x hg hh subset_preimage_univ
end

lemma deriv_within.comp
  (hg : differentiable_within_at ğ•œ g t (h x)) (hh : differentiable_within_at ğ•œ h s x)
  (hs : s âŠ† h â»Â¹' t) (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (g âˆ˜ h) s x = deriv_within h s x â€¢ deriv_within g t (h x) :=
begin
  apply has_deriv_within_at.deriv_within _ hxs,
  exact has_deriv_within_at.comp x (hg.has_deriv_within_at) (hh.has_deriv_within_at) hs
end

lemma deriv.comp
  (hg : differentiable_at ğ•œ g (h x)) (hh : differentiable_at ğ•œ h x) :
  deriv (g âˆ˜ h) x = deriv h x â€¢ deriv g (h x) :=
begin
  apply has_deriv_at.deriv,
  exact has_deriv_at.comp x hg.has_deriv_at hh.has_deriv_at
end

end composition

section composition_vector
/-! ### Derivative of the composition of a function between vector spaces and of a function defined on `ğ•œ` -/

variables {l : F â†’ E} {l' : F â†’L[ğ•œ] E}
variable (x)

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative within a set
equal to the FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem has_fderiv_within_at.comp_has_deriv_within_at {t : set F}
  (hl : has_fderiv_within_at l l' t (f x)) (hf : has_deriv_within_at f f' s x) (hst : s âŠ† f â»Â¹' t) :
  has_deriv_within_at (l âˆ˜ f) (l' (f')) s x :=
begin
  rw has_deriv_within_at_iff_has_fderiv_within_at,
  convert has_fderiv_within_at.comp x hl hf hst,
  ext,
  simp
end

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative equal to the
FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem has_fderiv_at.comp_has_deriv_at
  (hl : has_fderiv_at l l' (f x)) (hf : has_deriv_at f f' x) :
  has_deriv_at (l âˆ˜ f) (l' (f')) x :=
begin
  rw has_deriv_at_iff_has_fderiv_at,
  convert has_fderiv_at.comp x hl hf,
  ext,
  simp
end

theorem has_fderiv_at.comp_has_deriv_within_at
  (hl : has_fderiv_at l l' (f x)) (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (l âˆ˜ f) (l' (f')) s x :=
begin
  rw â† has_fderiv_within_at_univ at hl,
  exact has_fderiv_within_at.comp_has_deriv_within_at x hl hf subset_preimage_univ
end

lemma fderiv_within.comp_deriv_within {t : set F}
  (hl : differentiable_within_at ğ•œ l t (f x)) (hf : differentiable_within_at ğ•œ f s x)
  (hs : s âŠ† f â»Â¹' t) (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within (l âˆ˜ f) s x = (fderiv_within ğ•œ l t (f x) : F â†’ E) (deriv_within f s x) :=
begin
  apply has_deriv_within_at.deriv_within _ hxs,
  exact (hl.has_fderiv_within_at).comp_has_deriv_within_at x (hf.has_deriv_within_at) hs
end

lemma fderiv.comp_deriv
  (hl : differentiable_at ğ•œ l (f x)) (hf : differentiable_at ğ•œ f x) :
  deriv (l âˆ˜ f) x = (fderiv ğ•œ l (f x) : F â†’ E) (deriv f x) :=
begin
  apply has_deriv_at.deriv _,
  exact (hl.has_fderiv_at).comp_has_deriv_at x (hf.has_deriv_at)
end

end composition_vector

section mul_vector
/-! ### Derivative of the multiplication of a scalar function and a vector function -/
variables {c : ğ•œ â†’ ğ•œ} {c' : ğ•œ}

theorem has_deriv_within_at.smul'
  (hc : has_deriv_within_at c c' s x) (hf : has_deriv_within_at f f' s x) :
  has_deriv_within_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) s x :=
begin
  show has_fderiv_within_at _ _ _ _,
  convert has_fderiv_within_at.smul' hc hf,
  ext,
  simp [smul_add, (mul_smul _ _ _).symm, mul_comm]
end

theorem has_deriv_at.smul'
  (hc : has_deriv_at c c' x) (hf : has_deriv_at f f' x) :
  has_deriv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) x :=
begin
  show has_fderiv_at _ _ _,
  convert has_fderiv_at.smul' hc hf,
  ext,
  simp [smul_add, (mul_smul _ _ _).symm, mul_comm]
end

lemma deriv_within_smul' (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  deriv_within (Î» y, c y â€¢ f y) s x = c x â€¢ deriv_within f s x + (deriv_within c s x) â€¢ f x :=
(hc.has_deriv_within_at.smul' hf.has_deriv_within_at).deriv_within hxs

lemma deriv_smul' (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  deriv (Î» y, c y â€¢ f y) x = c x â€¢ deriv f x + (deriv c x) â€¢ f x :=
(hc.has_deriv_at.smul' hf.has_deriv_at).deriv

end mul_vector

section mul
/-! ### Derivative of the multiplication of two scalar functions -/
variables {c d : ğ•œ â†’ ğ•œ} {c' d' : ğ•œ}

theorem has_deriv_within_at.mul
  (hc : has_deriv_within_at c c' s x) (hd : has_deriv_within_at d d' s x) :
  has_deriv_within_at (Î» y, c y * d y) (c x * d' + d x * c') s x :=
begin
  show has_fderiv_within_at _ _ _ _,
  convert has_fderiv_within_at.mul hc hd,
  ext, simp, ring,
end

theorem has_deriv_at.mul (hc : has_deriv_at c c' x) (hd : has_deriv_at d d' x) :
  has_deriv_at (Î» y, c y * d y) (c x * d' + d x * c') x :=
begin
  show has_fderiv_at _ _ _,
  convert has_fderiv_at.mul hc hd,
  ext, simp, ring,
end

lemma deriv_within_mul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  deriv_within (Î» y, c y * d y) s x = c x * deriv_within d s x + d x * deriv_within c s x :=
(hc.has_deriv_within_at.mul hd.has_deriv_within_at).deriv_within hxs

lemma deriv_mul (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  deriv (Î» y, c y * d y) x = c x * deriv d x + d x * deriv c x :=
(hc.has_deriv_at.mul hd.has_deriv_at).deriv

end mul
