/-
Copyright (c) 2019 Gabriel Ebner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Ebner, SÃ©bastien GouÃ«zel
-/
import analysis.calculus.fderiv.basic

/-!

# One-dimensional derivatives

This file defines the derivative of a function `f : ğ•œ â†’ F` where `ğ•œ` is a
normed field and `F` is a normed space over this field. The derivative of
such a function `f` at a point `x` is given by an element `f' : F`.

The theory is developed analogously to the [FrÃ©chet
derivatives](./fderiv.html). We first introduce predicates defined in terms
of the corresponding predicates for FrÃ©chet derivatives:

 - `has_deriv_at_filter f f' x L` states that the function `f` has the
    derivative `f'` at the point `x` as `x` goes along the filter `L`.

 - `has_deriv_within_at f f' s x` states that the function `f` has the
    derivative `f'` at the point `x` within the subset `s`.

 - `has_deriv_at f f' x` states that the function `f` has the derivative `f'`
    at the point `x`.

 - `has_strict_deriv_at f f' x` states that the function `f` has the derivative `f'`
    at the point `x` in the sense of strict differentiability, i.e.,
   `f y - f z = (y - z) â€¢ f' + o (y - z)` as `y, z â†’ x`.

For the last two notions we also define a functional version:

  - `deriv_within f s x` is a derivative of `f` at `x` within `s`. If the
    derivative does not exist, then `deriv_within f s x` equals zero.

  - `deriv f x` is a derivative of `f` at `x`. If the derivative does not
    exist, then `deriv f x` equals zero.

The theorems `fderiv_within_deriv_within` and `fderiv_deriv` show that the
one-dimensional derivatives coincide with the general FrÃ©chet derivatives.

We also show the existence and compute the derivatives of:
  - constants
  - the identity function
  - linear maps (in `linear.lean`)
  - addition (in `add.lean`)
  - sum of finitely many functions (in `add.lean`)
  - negation (in `add.lean`)
  - subtraction (in `add.lean`)
  - star  (in `star.lean`)
  - multiplication of two functions in `ğ•œ â†’ ğ•œ` (in `mul.lean`)
  - multiplication of a function in `ğ•œ â†’ ğ•œ` and of a function in `ğ•œ â†’ E` (in `mul.lean`)
  - powers of a function (in `pow.lean` and `zpow.lean`)
  - inverse `x â†’ xâ»Â¹` (in `inv.lean`)
  - division (in `inv.lean`)
  - composition of a function in `ğ•œ â†’ F` with a function in `ğ•œ â†’ ğ•œ` (in `comp.lean`)
  - composition of a function in `F â†’ E` with a function in `ğ•œ â†’ F` (in `comp.lean`)
  - inverse function (assuming that it exists; the inverse function theorem is in `inverse.lean`)
  - polynomials (in `polynomial.lean`)

For most binary operations we also define `const_op` and `op_const` theorems for the cases when
the first or second argument is a constant. This makes writing chains of `has_deriv_at`'s easier,
and they more frequently lead to the desired result.

We set up the simplifier so that it can compute the derivative of simple functions. For instance,
```lean
example (x : â„) : deriv (Î» x, cos (sin x) * exp x) x = (cos(sin(x))-sin(sin(x))*cos(x))*exp(x) :=
by { simp, ring }
```

## Implementation notes

Most of the theorems are direct restatements of the corresponding theorems
for FrÃ©chet derivatives.

The strategy to construct simp lemmas that give the simplifier the possibility to compute
derivatives is the same as the one for differentiability statements, as explained in `fderiv.lean`.
See the explanations there.
-/

universes u v w
noncomputable theory
open_locale classical topology big_operators filter ennreal
open filter asymptotics set
open continuous_linear_map (smul_right smul_right_one_eq_iff)


variables {ğ•œ : Type u} [nontrivially_normed_field ğ•œ]
variables {F : Type v} [normed_add_comm_group F] [normed_space ğ•œ F]
variables {E : Type w} [normed_add_comm_group E] [normed_space ğ•œ E]

/--
`f` has the derivative `f'` at the point `x` as `x` goes along the filter `L`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges along the filter `L`.
-/
def has_deriv_at_filter (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) (L : filter ğ•œ) :=
has_fderiv_at_filter f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x L

/--
`f` has the derivative `f'` at the point `x` within the subset `s`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def has_deriv_within_at (f : ğ•œ â†’ F) (f' : F) (s : set ğ•œ) (x : ğ•œ) :=
has_deriv_at_filter f f' x (ğ“[s] x)

/--
`f` has the derivative `f'` at the point `x`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x`.
-/
def has_deriv_at (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) :=
has_deriv_at_filter f f' x (ğ“ x)

/-- `f` has the derivative `f'` at the point `x` in the sense of strict differentiability.

That is, `f y - f z = (y - z) â€¢ f' + o(y - z)` as `y, z â†’ x`. -/
def has_strict_deriv_at (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) :=
has_strict_fderiv_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x

/--
Derivative of `f` at the point `x` within the set `s`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', has_deriv_within_at f f' s x`), then
`f x' = f x + (x' - x) â€¢ deriv_within f s x + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def deriv_within (f : ğ•œ â†’ F) (s : set ğ•œ) (x : ğ•œ) :=
fderiv_within ğ•œ f s x 1

/--
Derivative of `f` at the point `x`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', has_deriv_at f f' x`), then
`f x' = f x + (x' - x) â€¢ deriv f x + o(x' - x)` where `x'` converges to `x`.
-/
def deriv (f : ğ•œ â†’ F) (x : ğ•œ) :=
fderiv ğ•œ f x 1

variables {f fâ‚€ fâ‚ g : ğ•œ â†’ F}
variables {f' fâ‚€' fâ‚' g' : F}
variables {x : ğ•œ}
variables {s t : set ğ•œ}
variables {L Lâ‚ Lâ‚‚ : filter ğ•œ}

/-- Expressing `has_fderiv_at_filter f f' x L` in terms of `has_deriv_at_filter` -/
lemma has_fderiv_at_filter_iff_has_deriv_at_filter {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at_filter f f' x L â†” has_deriv_at_filter f (f' 1) x L :=
by simp [has_deriv_at_filter]

lemma has_fderiv_at_filter.has_deriv_at_filter {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at_filter f f' x L â†’ has_deriv_at_filter f (f' 1) x L :=
has_fderiv_at_filter_iff_has_deriv_at_filter.mp

/-- Expressing `has_fderiv_within_at f f' s x` in terms of `has_deriv_within_at` -/
lemma has_fderiv_within_at_iff_has_deriv_within_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_within_at f f' s x â†” has_deriv_within_at f (f' 1) s x :=
has_fderiv_at_filter_iff_has_deriv_at_filter

/-- Expressing `has_deriv_within_at f f' s x` in terms of `has_fderiv_within_at` -/
lemma has_deriv_within_at_iff_has_fderiv_within_at {f' : F} :
  has_deriv_within_at f f' s x â†”
  has_fderiv_within_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') s x :=
iff.rfl

lemma has_fderiv_within_at.has_deriv_within_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_within_at f f' s x â†’ has_deriv_within_at f (f' 1) s x :=
has_fderiv_within_at_iff_has_deriv_within_at.mp

lemma has_deriv_within_at.has_fderiv_within_at {f' : F} :
  has_deriv_within_at f f' s x â†’ has_fderiv_within_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') s x :=
has_deriv_within_at_iff_has_fderiv_within_at.mp

/-- Expressing `has_fderiv_at f f' x` in terms of `has_deriv_at` -/
lemma has_fderiv_at_iff_has_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at f f' x â†” has_deriv_at f (f' 1) x :=
has_fderiv_at_filter_iff_has_deriv_at_filter

lemma has_fderiv_at.has_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_fderiv_at f f' x â†’ has_deriv_at f (f' 1) x :=
has_fderiv_at_iff_has_deriv_at.mp

lemma has_strict_fderiv_at_iff_has_strict_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_strict_fderiv_at f f' x â†” has_strict_deriv_at f (f' 1) x :=
by simp [has_strict_deriv_at, has_strict_fderiv_at]

protected lemma has_strict_fderiv_at.has_strict_deriv_at {f' : ğ•œ â†’L[ğ•œ] F} :
  has_strict_fderiv_at f f' x â†’ has_strict_deriv_at f (f' 1) x :=
has_strict_fderiv_at_iff_has_strict_deriv_at.mp

lemma has_strict_deriv_at_iff_has_strict_fderiv_at :
  has_strict_deriv_at f f' x â†” has_strict_fderiv_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x :=
iff.rfl

alias has_strict_deriv_at_iff_has_strict_fderiv_at â†” has_strict_deriv_at.has_strict_fderiv_at _

/-- Expressing `has_deriv_at f f' x` in terms of `has_fderiv_at` -/
lemma has_deriv_at_iff_has_fderiv_at {f' : F} :
  has_deriv_at f f' x â†”
  has_fderiv_at f (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x :=
iff.rfl

alias has_deriv_at_iff_has_fderiv_at â†” has_deriv_at.has_fderiv_at _

lemma deriv_within_zero_of_not_differentiable_within_at
  (h : Â¬ differentiable_within_at ğ•œ f s x) : deriv_within f s x = 0 :=
by { unfold deriv_within, rw fderiv_within_zero_of_not_differentiable_within_at, simp, assumption }

lemma differentiable_within_at_of_deriv_within_ne_zero (h : deriv_within f s x â‰  0) :
  differentiable_within_at ğ•œ f s x :=
not_imp_comm.1 deriv_within_zero_of_not_differentiable_within_at h

lemma deriv_zero_of_not_differentiable_at (h : Â¬ differentiable_at ğ•œ f x) : deriv f x = 0 :=
by { unfold deriv, rw fderiv_zero_of_not_differentiable_at, simp, assumption }

lemma differentiable_at_of_deriv_ne_zero (h : deriv f x â‰  0) : differentiable_at ğ•œ f x :=
not_imp_comm.1 deriv_zero_of_not_differentiable_at h

theorem unique_diff_within_at.eq_deriv (s : set ğ•œ) (H : unique_diff_within_at ğ•œ s x)
  (h : has_deriv_within_at f f' s x) (hâ‚ : has_deriv_within_at f fâ‚' s x) : f' = fâ‚' :=
smul_right_one_eq_iff.mp $ unique_diff_within_at.eq H h hâ‚

theorem has_deriv_at_filter_iff_is_o :
  has_deriv_at_filter f f' x L â†” (Î» x' : ğ•œ, f x' - f x - (x' - x) â€¢ f') =o[L] (Î» x', x' - x) :=
iff.rfl

theorem has_deriv_at_filter_iff_tendsto :
  has_deriv_at_filter f f' x L â†”
  tendsto (Î» x' : ğ•œ, â€–x' - xâ€–â»Â¹ * â€–f x' - f x - (x' - x) â€¢ f'â€–) L (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_within_at_iff_is_o :
  has_deriv_within_at f f' s x
    â†” (Î» x' : ğ•œ, f x' - f x - (x' - x) â€¢ f') =o[ğ“[s] x] (Î» x', x' - x) :=
iff.rfl

theorem has_deriv_within_at_iff_tendsto : has_deriv_within_at f f' s x â†”
  tendsto (Î» x', â€–x' - xâ€–â»Â¹ * â€–f x' - f x - (x' - x) â€¢ f'â€–) (ğ“[s] x) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_at_iff_is_o :
  has_deriv_at f f' x â†” (Î» x' : ğ•œ, f x' - f x - (x' - x) â€¢ f') =o[ğ“ x] (Î» x', x' - x) :=
iff.rfl

theorem has_deriv_at_iff_tendsto : has_deriv_at f f' x â†”
  tendsto (Î» x', â€–x' - xâ€–â»Â¹ * â€–f x' - f x - (x' - x) â€¢ f'â€–) (ğ“ x) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_deriv_at_filter.is_O_sub (h : has_deriv_at_filter f f' x L) :
  (Î» x', f x' - f x) =O[L] (Î» x', x' - x) :=
has_fderiv_at_filter.is_O_sub h

theorem has_deriv_at_filter.is_O_sub_rev (hf : has_deriv_at_filter f f' x L) (hf' : f' â‰  0) :
  (Î» x', x' - x) =O[L] (Î» x', f x' - f x) :=
suffices antilipschitz_with â€–f'â€–â‚Šâ»Â¹ (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f'), from hf.is_O_sub_rev this,
add_monoid_hom_class.antilipschitz_of_bound (smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') $
  Î» x, by simp [norm_smul, â† div_eq_inv_mul, mul_div_cancel _ (mt norm_eq_zero.1 hf')]

theorem has_strict_deriv_at.has_deriv_at (h : has_strict_deriv_at f f' x) :
  has_deriv_at f f' x :=
h.has_fderiv_at
theorem has_deriv_within_at_congr_set' {s t : set ğ•œ} (y : ğ•œ) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    has_deriv_within_at f f' s x â†” has_deriv_within_at f f' t x :=
has_fderiv_within_at_congr_set' y h

theorem has_deriv_within_at_congr_set {s t : set ğ•œ} (h : s =á¶ [ğ“ x] t) :
    has_deriv_within_at f f' s x â†” has_deriv_within_at f f' t x :=
has_fderiv_within_at_congr_set h

alias has_deriv_within_at_congr_set â†” has_deriv_within_at.congr_set _

@[simp] lemma has_deriv_within_at_diff_singleton :
  has_deriv_within_at f f' (s \ {x}) x â†” has_deriv_within_at f f' s x :=
has_fderiv_within_at_diff_singleton _

@[simp] lemma has_deriv_within_at_Ioi_iff_Ici [partial_order ğ•œ] :
  has_deriv_within_at f f' (Ioi x) x â†” has_deriv_within_at f f' (Ici x) x :=
by rw [â† Ici_diff_left, has_deriv_within_at_diff_singleton]

alias has_deriv_within_at_Ioi_iff_Ici â†”
  has_deriv_within_at.Ici_of_Ioi has_deriv_within_at.Ioi_of_Ici

@[simp] lemma has_deriv_within_at_Iio_iff_Iic [partial_order ğ•œ] :
  has_deriv_within_at f f' (Iio x) x â†” has_deriv_within_at f f' (Iic x) x :=
by rw [â† Iic_diff_right, has_deriv_within_at_diff_singleton]

alias has_deriv_within_at_Iio_iff_Iic â†”
  has_deriv_within_at.Iic_of_Iio has_deriv_within_at.Iio_of_Iic

theorem has_deriv_within_at.Ioi_iff_Ioo [linear_order ğ•œ] [order_closed_topology ğ•œ] {x y : ğ•œ}
  (h : x < y) :
  has_deriv_within_at f f' (Ioo x y) x â†” has_deriv_within_at f f' (Ioi x) x :=
has_fderiv_within_at_inter $ Iio_mem_nhds h

alias has_deriv_within_at.Ioi_iff_Ioo â†”
  has_deriv_within_at.Ioi_of_Ioo has_deriv_within_at.Ioo_of_Ioi

theorem has_deriv_at_iff_is_o_nhds_zero : has_deriv_at f f' x â†”
  (Î»h, f (x + h) - f x - h â€¢ f') =o[ğ“ 0] (Î»h, h) :=
has_fderiv_at_iff_is_o_nhds_zero

theorem has_deriv_at_filter.mono (h : has_deriv_at_filter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
  has_deriv_at_filter f f' x Lâ‚ :=
has_fderiv_at_filter.mono h hst

theorem has_deriv_within_at.mono (h : has_deriv_within_at f f' t x) (hst : s âŠ† t) :
  has_deriv_within_at f f' s x :=
has_fderiv_within_at.mono h hst

theorem has_deriv_within_at.mono_of_mem (h : has_deriv_within_at f f' t x) (hst : t âˆˆ ğ“[s] x) :
  has_deriv_within_at f f' s x :=
has_fderiv_within_at.mono_of_mem h hst

theorem has_deriv_at.has_deriv_at_filter (h : has_deriv_at f f' x) (hL : L â‰¤ ğ“ x) :
  has_deriv_at_filter f f' x L :=
has_fderiv_at.has_fderiv_at_filter h hL

theorem has_deriv_at.has_deriv_within_at
  (h : has_deriv_at f f' x) : has_deriv_within_at f f' s x :=
has_fderiv_at.has_fderiv_within_at h

lemma has_deriv_within_at.differentiable_within_at (h : has_deriv_within_at f f' s x) :
  differentiable_within_at ğ•œ f s x :=
has_fderiv_within_at.differentiable_within_at h

lemma has_deriv_at.differentiable_at (h : has_deriv_at f f' x) : differentiable_at ğ•œ f x :=
has_fderiv_at.differentiable_at h

@[simp] lemma has_deriv_within_at_univ : has_deriv_within_at f f' univ x â†” has_deriv_at f f' x :=
has_fderiv_within_at_univ

theorem has_deriv_at.unique
  (hâ‚€ : has_deriv_at f fâ‚€' x) (hâ‚ : has_deriv_at f fâ‚' x) : fâ‚€' = fâ‚' :=
smul_right_one_eq_iff.mp $ hâ‚€.has_fderiv_at.unique hâ‚

lemma has_deriv_within_at_inter' (h : t âˆˆ ğ“[s] x) :
  has_deriv_within_at f f' (s âˆ© t) x â†” has_deriv_within_at f f' s x :=
has_fderiv_within_at_inter' h

lemma has_deriv_within_at_inter (h : t âˆˆ ğ“ x) :
  has_deriv_within_at f f' (s âˆ© t) x â†” has_deriv_within_at f f' s x :=
has_fderiv_within_at_inter h

lemma has_deriv_within_at.union (hs : has_deriv_within_at f f' s x)
  (ht : has_deriv_within_at f f' t x) :
  has_deriv_within_at f f' (s âˆª t) x :=
hs.has_fderiv_within_at.union ht.has_fderiv_within_at

lemma has_deriv_within_at.nhds_within (h : has_deriv_within_at f f' s x)
  (ht : s âˆˆ ğ“[t] x) : has_deriv_within_at f f' t x :=
(has_deriv_within_at_inter' ht).1 (h.mono (inter_subset_right _ _))

lemma has_deriv_within_at.has_deriv_at (h : has_deriv_within_at f f' s x) (hs : s âˆˆ ğ“ x) :
  has_deriv_at f f' x :=
has_fderiv_within_at.has_fderiv_at h hs

lemma differentiable_within_at.has_deriv_within_at (h : differentiable_within_at ğ•œ f s x) :
  has_deriv_within_at f (deriv_within f s x) s x :=
h.has_fderiv_within_at.has_deriv_within_at

lemma differentiable_at.has_deriv_at (h : differentiable_at ğ•œ f x) : has_deriv_at f (deriv f x) x :=
h.has_fderiv_at.has_deriv_at

@[simp] lemma has_deriv_at_deriv_iff : has_deriv_at f (deriv f x) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, h.differentiable_at, Î» h, h.has_deriv_atâŸ©

@[simp] lemma has_deriv_within_at_deriv_within_iff :
  has_deriv_within_at f (deriv_within f s x) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, h.differentiable_within_at, Î» h, h.has_deriv_within_atâŸ©

lemma differentiable_on.has_deriv_at (h : differentiable_on ğ•œ f s) (hs : s âˆˆ ğ“ x) :
  has_deriv_at f (deriv f x) x :=
(h.has_fderiv_at hs).has_deriv_at

lemma has_deriv_at.deriv (h : has_deriv_at f f' x) : deriv f x = f' :=
h.differentiable_at.has_deriv_at.unique h

lemma deriv_eq {f' : ğ•œ â†’ F} (h : âˆ€ x, has_deriv_at f (f' x) x) : deriv f = f' :=
funext $ Î» x, (h x).deriv

lemma has_deriv_within_at.deriv_within
  (h : has_deriv_within_at f f' s x) (hxs : unique_diff_within_at ğ•œ s x) :
  deriv_within f s x = f' :=
hxs.eq_deriv _ h.differentiable_within_at.has_deriv_within_at h

lemma fderiv_within_deriv_within : (fderiv_within ğ•œ f s x : ğ•œ â†’ F) 1 = deriv_within f s x :=
rfl

lemma deriv_within_fderiv_within :
  smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (deriv_within f s x) = fderiv_within ğ•œ f s x :=
by simp [deriv_within]

lemma fderiv_deriv : (fderiv ğ•œ f x : ğ•œ â†’ F) 1 = deriv f x :=
rfl

lemma deriv_fderiv :
  smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (deriv f x) = fderiv ğ•œ f x :=
by simp [deriv]

lemma differentiable_at.deriv_within (h : differentiable_at ğ•œ f x)
  (hxs : unique_diff_within_at ğ•œ s x) : deriv_within f s x = deriv f x :=
by { unfold deriv_within deriv, rw h.fderiv_within hxs }

theorem has_deriv_within_at.deriv_eq_zero (hd : has_deriv_within_at f 0 s x)
  (H : unique_diff_within_at ğ•œ s x) : deriv f x = 0 :=
(em' (differentiable_at ğ•œ f x)).elim deriv_zero_of_not_differentiable_at $
  Î» h, H.eq_deriv _ h.has_deriv_at.has_deriv_within_at hd

lemma deriv_within_of_mem (st : t âˆˆ ğ“[s] x) (ht : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f t x) :
  deriv_within f s x = deriv_within f t x :=
((differentiable_within_at.has_deriv_within_at h).mono_of_mem st).deriv_within ht

lemma deriv_within_subset (st : s âŠ† t) (ht : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f t x) :
  deriv_within f s x = deriv_within f t x :=
((differentiable_within_at.has_deriv_within_at h).mono st).deriv_within ht

lemma deriv_within_congr_set' (y : ğ•œ) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
  deriv_within f s x = deriv_within f t x :=
by simp only [deriv_within, fderiv_within_congr_set' y h]

lemma deriv_within_congr_set (h : s =á¶ [ğ“ x] t) : deriv_within f s x = deriv_within f t x :=
by simp only [deriv_within, fderiv_within_congr_set h]

@[simp] lemma deriv_within_univ : deriv_within f univ = deriv f :=
by { ext, unfold deriv_within deriv, rw fderiv_within_univ }

lemma deriv_within_inter (ht : t âˆˆ ğ“ x) :
  deriv_within f (s âˆ© t) x = deriv_within f s x :=
by { unfold deriv_within, rw fderiv_within_inter ht }

lemma deriv_within_of_open (hs : is_open s) (hx : x âˆˆ s) :
  deriv_within f s x = deriv f x :=
by { unfold deriv_within, rw fderiv_within_of_open hs hx, refl }

lemma deriv_mem_iff {f : ğ•œ â†’ F} {s : set F} {x : ğ•œ} :
  deriv f x âˆˆ s â†” (differentiable_at ğ•œ f x âˆ§ deriv f x âˆˆ s) âˆ¨
    (Â¬differentiable_at ğ•œ f x âˆ§ (0 : F) âˆˆ s) :=
by by_cases hx : differentiable_at ğ•œ f x; simp [deriv_zero_of_not_differentiable_at, *]

lemma deriv_within_mem_iff {f : ğ•œ â†’ F} {t : set ğ•œ} {s : set F} {x : ğ•œ} :
  deriv_within f t x âˆˆ s â†” (differentiable_within_at ğ•œ f t x âˆ§ deriv_within f t x âˆˆ s) âˆ¨
    (Â¬differentiable_within_at ğ•œ f t x âˆ§ (0 : F) âˆˆ s) :=
by by_cases hx : differentiable_within_at ğ•œ f t x;
  simp [deriv_within_zero_of_not_differentiable_within_at, *]

lemma differentiable_within_at_Ioi_iff_Ici [partial_order ğ•œ] :
  differentiable_within_at ğ•œ f (Ioi x) x â†” differentiable_within_at ğ•œ f (Ici x) x :=
âŸ¨Î» h, h.has_deriv_within_at.Ici_of_Ioi.differentiable_within_at,
Î» h, h.has_deriv_within_at.Ioi_of_Ici.differentiable_within_atâŸ©

-- Golfed while splitting the file
lemma deriv_within_Ioi_eq_Ici {E : Type*} [normed_add_comm_group E] [normed_space â„ E] (f : â„ â†’ E)
  (x : â„) :
  deriv_within f (Ioi x) x = deriv_within f (Ici x) x :=
begin
  by_cases H : differentiable_within_at â„ f (Ioi x) x,
  { have A := H.has_deriv_within_at.Ici_of_Ioi,
    have B := (differentiable_within_at_Ioi_iff_Ici.1 H).has_deriv_within_at,
    simpa using (unique_diff_on_Ici x).eq le_rfl A B },
  { rw [deriv_within_zero_of_not_differentiable_within_at H,
      deriv_within_zero_of_not_differentiable_within_at],
    rwa differentiable_within_at_Ioi_iff_Ici at H }
end

section congr
/-! ### Congruence properties of derivatives -/

theorem filter.eventually_eq.has_deriv_at_filter_iff
  (hâ‚€ : fâ‚€ =á¶ [L] fâ‚) (hx : fâ‚€ x = fâ‚ x) (hâ‚ : fâ‚€' = fâ‚') :
  has_deriv_at_filter fâ‚€ fâ‚€' x L â†” has_deriv_at_filter fâ‚ fâ‚' x L :=
hâ‚€.has_fderiv_at_filter_iff hx (by simp [hâ‚])

lemma has_deriv_at_filter.congr_of_eventually_eq (h : has_deriv_at_filter f f' x L)
  (hL : fâ‚ =á¶ [L] f) (hx : fâ‚ x = f x) : has_deriv_at_filter fâ‚ f' x L :=
by rwa hL.has_deriv_at_filter_iff hx rfl

lemma has_deriv_within_at.congr_mono (h : has_deriv_within_at f f' s x) (ht : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : has_deriv_within_at fâ‚ f' t x :=
has_fderiv_within_at.congr_mono h ht hx hâ‚

lemma has_deriv_within_at.congr (h : has_deriv_within_at f f' s x) (hs : âˆ€x âˆˆ s, fâ‚ x = f x)
  (hx : fâ‚ x = f x) : has_deriv_within_at fâ‚ f' s x :=
h.congr_mono hs hx (subset.refl _)

lemma has_deriv_within_at.congr_of_mem (h : has_deriv_within_at f f' s x) (hs : âˆ€x âˆˆ s, fâ‚ x = f x)
  (hx : x âˆˆ s) : has_deriv_within_at fâ‚ f' s x :=
h.congr hs (hs _ hx)

lemma has_deriv_within_at.congr_of_eventually_eq (h : has_deriv_within_at f f' s x)
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : has_deriv_within_at fâ‚ f' s x :=
has_deriv_at_filter.congr_of_eventually_eq h hâ‚ hx

lemma has_deriv_within_at.congr_of_eventually_eq_of_mem (h : has_deriv_within_at f f' s x)
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : has_deriv_within_at fâ‚ f' s x :=
h.congr_of_eventually_eq hâ‚ (hâ‚.eq_of_nhds_within hx)

lemma has_deriv_at.congr_of_eventually_eq (h : has_deriv_at f f' x)
  (hâ‚ : fâ‚ =á¶ [ğ“ x] f) : has_deriv_at fâ‚ f' x :=
has_deriv_at_filter.congr_of_eventually_eq h hâ‚ (mem_of_mem_nhds hâ‚ : _)

lemma filter.eventually_eq.deriv_within_eq (hL : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  deriv_within fâ‚ s x = deriv_within f s x :=
by { unfold deriv_within, rw hL.fderiv_within_eq hx }

lemma deriv_within_congr (hs : eq_on fâ‚ f s) (hx : fâ‚ x = f x) :
  deriv_within fâ‚ s x = deriv_within f s x :=
by { unfold deriv_within, rw fderiv_within_congr hs hx }

lemma filter.eventually_eq.deriv_eq (hL : fâ‚ =á¶ [ğ“ x] f) : deriv fâ‚ x = deriv f x :=
by { unfold deriv, rwa filter.eventually_eq.fderiv_eq }

protected lemma filter.eventually_eq.deriv (h : fâ‚ =á¶ [ğ“ x] f) : deriv fâ‚ =á¶ [ğ“ x] deriv f :=
h.eventually_eq_nhds.mono $ Î» x h, h.deriv_eq

end congr

section id
/-! ### Derivative of the identity -/
variables (s x L)

theorem has_deriv_at_filter_id : has_deriv_at_filter id 1 x L :=
(has_fderiv_at_filter_id x L).has_deriv_at_filter

theorem has_deriv_within_at_id : has_deriv_within_at id 1 s x :=
has_deriv_at_filter_id _ _

theorem has_deriv_at_id : has_deriv_at id 1 x :=
has_deriv_at_filter_id _ _

theorem has_deriv_at_id' : has_deriv_at (Î» (x : ğ•œ), x) 1 x :=
has_deriv_at_filter_id _ _

theorem has_strict_deriv_at_id : has_strict_deriv_at id 1 x :=
(has_strict_fderiv_at_id x).has_strict_deriv_at

lemma deriv_id : deriv id x = 1 :=
has_deriv_at.deriv (has_deriv_at_id x)

@[simp] lemma deriv_id' : deriv (@id ğ•œ) = Î» _, 1 := funext deriv_id

@[simp] lemma deriv_id'' : deriv (Î» x : ğ•œ, x) = Î» _, 1 := deriv_id'

lemma deriv_within_id (hxs : unique_diff_within_at ğ•œ s x) : deriv_within id s x = 1 :=
(has_deriv_within_at_id x s).deriv_within hxs

end id

section const
/-! ### Derivative of constant functions -/
variables (c : F) (s x L)

theorem has_deriv_at_filter_const : has_deriv_at_filter (Î» x, c) 0 x L :=
(has_fderiv_at_filter_const c x L).has_deriv_at_filter

theorem has_strict_deriv_at_const : has_strict_deriv_at (Î» x, c) 0 x :=
(has_strict_fderiv_at_const c x).has_strict_deriv_at

theorem has_deriv_within_at_const : has_deriv_within_at (Î» x, c) 0 s x :=
has_deriv_at_filter_const _ _ _

theorem has_deriv_at_const : has_deriv_at (Î» x, c) 0 x :=
has_deriv_at_filter_const _ _ _

lemma deriv_const : deriv (Î» x, c) x = 0 :=
has_deriv_at.deriv (has_deriv_at_const x c)

@[simp] lemma deriv_const' : deriv (Î» x:ğ•œ, c) = Î» x, 0 :=
funext (Î» x, deriv_const x c)

lemma deriv_within_const (hxs : unique_diff_within_at ğ•œ s x) : deriv_within (Î» x, c) s x = 0 :=
(has_deriv_within_at_const _ _ _).deriv_within hxs

end const

section continuous
/-! ### Continuity of a function admitting a derivative -/

theorem has_deriv_at_filter.tendsto_nhds
  (hL : L â‰¤ ğ“ x) (h : has_deriv_at_filter f f' x L) :
  tendsto f L (ğ“ (f x)) :=
h.tendsto_nhds hL

theorem has_deriv_within_at.continuous_within_at
  (h : has_deriv_within_at f f' s x) : continuous_within_at f s x :=
has_deriv_at_filter.tendsto_nhds inf_le_left h

theorem has_deriv_at.continuous_at (h : has_deriv_at f f' x) : continuous_at f x :=
has_deriv_at_filter.tendsto_nhds le_rfl h

protected theorem has_deriv_at.continuous_on {f f' : ğ•œ â†’ F}
  (hderiv : âˆ€ x âˆˆ s, has_deriv_at f (f' x) x) : continuous_on f s :=
Î» x hx, (hderiv x hx).continuous_at.continuous_within_at

end continuous

