/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import analysis.calculus.deriv.pow
import analysis.calculus.deriv.inv

/-!
# Derivatives of `x ^ m`, `m : â„¤`

In this file we prove theorems about (iterated) derivatives of `x ^ m`, `m : â„¤`.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`analysis/calculus/deriv/basic`.

## Keywords

derivative, power
-/

universes u v w
open_locale classical topology big_operators filter
open filter asymptotics set

variables {ğ•œ : Type u} [nontrivially_normed_field ğ•œ]
variables {E : Type v} [normed_add_comm_group E] [normed_space ğ•œ E]

variables {x : ğ•œ}
variables {s : set ğ•œ}
variables {m : â„¤}

/-! ### Derivative of `x â†¦ x^m` for `m : â„¤` -/

lemma has_strict_deriv_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  has_strict_deriv_at (Î»x, x^m) ((m : ğ•œ) * x^(m-1)) x :=
begin
  have : âˆ€ m : â„¤, 0 < m â†’ has_strict_deriv_at (Î»x, x^m) ((m:ğ•œ) * x^(m-1)) x,
  { assume m hm,
    lift m to â„• using (le_of_lt hm),
    simp only [zpow_coe_nat, int.cast_coe_nat],
    convert has_strict_deriv_at_pow _ _ using 2,
    rw [â† int.coe_nat_one, â† int.coe_nat_sub, zpow_coe_nat],
    norm_cast at hm,
    exact nat.succ_le_of_lt hm },
  rcases lt_trichotomy m 0 with hm|hm|hm,
  { have hx : x â‰  0, from h.resolve_right hm.not_le,
    have := (has_strict_deriv_at_inv _).scomp _ (this (-m) (neg_pos.2 hm));
      [skip, exact zpow_ne_zero_of_ne_zero hx _],
    simp only [(âˆ˜), zpow_neg, one_div, inv_inv, smul_eq_mul] at this,
    convert this using 1,
    rw [sq, mul_inv, inv_inv, int.cast_neg, neg_mul, neg_mul_neg,
      â† zpow_addâ‚€ hx, mul_assoc, â† zpow_addâ‚€ hx], congr, abel },
  { simp only [hm, zpow_zero, int.cast_zero, zero_mul, has_strict_deriv_at_const] },
  { exact this m hm }
end

lemma has_deriv_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  has_deriv_at (Î»x, x^m) ((m : ğ•œ) * x^(m-1)) x :=
(has_strict_deriv_at_zpow m x h).has_deriv_at

theorem has_deriv_within_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) (s : set ğ•œ) :
  has_deriv_within_at (Î»x, x^m) ((m : ğ•œ) * x^(m-1)) s x :=
(has_deriv_at_zpow m x h).has_deriv_within_at

lemma differentiable_at_zpow : differentiable_at ğ•œ (Î»x, x^m) x â†” x â‰  0 âˆ¨ 0 â‰¤ m :=
âŸ¨Î» H, normed_field.continuous_at_zpow.1 H.continuous_at,
  Î» H, (has_deriv_at_zpow m x H).differentiable_atâŸ©

lemma differentiable_within_at_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  differentiable_within_at ğ•œ (Î»x, x^m) s x :=
(differentiable_at_zpow.mpr h).differentiable_within_at

lemma differentiable_on_zpow (m : â„¤) (s : set ğ•œ) (h : (0 : ğ•œ) âˆ‰ s âˆ¨ 0 â‰¤ m) :
  differentiable_on ğ•œ (Î»x, x^m) s :=
Î» x hxs, differentiable_within_at_zpow m x $ h.imp_left $ ne_of_mem_of_not_mem hxs

lemma deriv_zpow (m : â„¤) (x : ğ•œ) : deriv (Î» x, x ^ m) x = m * x ^ (m - 1) :=
begin
  by_cases H : x â‰  0 âˆ¨ 0 â‰¤ m,
  { exact (has_deriv_at_zpow m x H).deriv },
  { rw deriv_zero_of_not_differentiable_at (mt differentiable_at_zpow.1 H),
    push_neg at H, rcases H with âŸ¨rfl, hmâŸ©,
    rw [zero_zpow _ ((sub_one_lt _).trans hm).ne, mul_zero] }
end

@[simp] lemma deriv_zpow' (m : â„¤) : deriv (Î» x : ğ•œ, x ^ m) = Î» x, m * x ^ (m - 1) :=
funext $ deriv_zpow m

lemma deriv_within_zpow (hxs : unique_diff_within_at ğ•œ s x) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
  deriv_within (Î»x, x^m) s x = (m : ğ•œ) * x^(m-1) :=
(has_deriv_within_at_zpow m x h s).deriv_within hxs

@[simp] lemma iter_deriv_zpow' (m : â„¤) (k : â„•) :
  deriv^[k] (Î» x : ğ•œ, x ^ m) = Î» x, (âˆ i in finset.range k, (m - i)) * x ^ (m - k) :=
begin
  induction k with k ihk,
  { simp only [one_mul, int.coe_nat_zero, id, sub_zero, finset.prod_range_zero,
      function.iterate_zero] },
  { simp only [function.iterate_succ_apply', ihk, deriv_const_mul_field', deriv_zpow',
      finset.prod_range_succ, int.coe_nat_succ, â† sub_sub, int.cast_sub, int.cast_coe_nat,
      mul_assoc], }
end

lemma iter_deriv_zpow (m : â„¤) (x : ğ•œ) (k : â„•) :
  deriv^[k] (Î» y, y ^ m) x = (âˆ i in finset.range k, (m - i)) * x ^ (m - k) :=
congr_fun (iter_deriv_zpow' m k) x

lemma iter_deriv_pow (n : â„•) (x : ğ•œ) (k : â„•) :
  deriv^[k] (Î»x:ğ•œ, x^n) x = (âˆ i in finset.range k, (n - i)) * x^(n-k) :=
begin
  simp only [â† zpow_coe_nat, iter_deriv_zpow, int.cast_coe_nat],
  cases le_or_lt k n with hkn hnk,
  { rw int.coe_nat_sub hkn },
  { have : âˆ i in finset.range k, (n - i : ğ•œ) = 0,
      from finset.prod_eq_zero (finset.mem_range.2 hnk) (sub_self _),
    simp only [this, zero_mul] }
end

@[simp] lemma iter_deriv_pow' (n k : â„•) :
  deriv^[k] (Î» x : ğ•œ, x ^ n) = Î» x, (âˆ i in finset.range k, (n - i)) * x ^ (n - k) :=
funext $ Î» x, iter_deriv_pow n x k

lemma iter_deriv_inv (k : â„•) (x : ğ•œ) :
  deriv^[k] has_inv.inv x = (âˆ i in finset.range k, (-1 - i)) * x ^ (-1 - k : â„¤) :=
by simpa only [zpow_neg_one, int.cast_neg, int.cast_one] using iter_deriv_zpow (-1) x k

@[simp] lemma iter_deriv_inv' (k : â„•) :
  deriv^[k] has_inv.inv = Î» x : ğ•œ, (âˆ i in finset.range k, (-1 - i)) * x ^ (-1 - k : â„¤) :=
funext (iter_deriv_inv k)

variables {f : E â†’ ğ•œ} {t : set E} {a : E}

lemma differentiable_within_at.zpow (hf : differentiable_within_at ğ•œ f t a) (h : f a â‰  0 âˆ¨ 0 â‰¤ m) :
  differentiable_within_at ğ•œ (Î» x, f x ^ m) t a :=
(differentiable_at_zpow.2 h).comp_differentiable_within_at a hf

lemma differentiable_at.zpow (hf : differentiable_at ğ•œ f a) (h : f a â‰  0 âˆ¨ 0 â‰¤ m) :
  differentiable_at ğ•œ (Î» x, f x ^ m) a :=
(differentiable_at_zpow.2 h).comp a hf

lemma differentiable_on.zpow (hf : differentiable_on ğ•œ f t) (h : (âˆ€ x âˆˆ t, f x â‰  0) âˆ¨ 0 â‰¤ m) :
  differentiable_on ğ•œ (Î» x, f x ^ m) t :=
Î» x hx, (hf x hx).zpow $ h.imp_left (Î» h, h x hx)

lemma differentiable.zpow (hf : differentiable ğ•œ f) (h : (âˆ€ x, f x â‰  0) âˆ¨ 0 â‰¤ m) :
  differentiable ğ•œ (Î» x, f x ^ m) :=
Î» x, (hf x).zpow $ h.imp_left (Î» h, h x)

