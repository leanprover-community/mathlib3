/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.calculus.deriv

/-!
# Functions continuous on a domain and differentiable on its interior

Many theorems in complex analysis assume that a function is continuous on a domain and is complex
differentiable on its interior. In this file we define a predicate `diff_on_int_cont` that expresses
this property and prove basic facts about this predicate.
-/

open set filter metric
open_locale topological_space

variables (ğ•œ : Type*) {E F G : Type*} [nondiscrete_normed_field ğ•œ] [normed_group E]
  [normed_group F] [normed_space ğ•œ E] [normed_space ğ•œ F] [normed_group G] [normed_space ğ•œ G]
  {f g : E â†’ F} {s t : set E} {x : E}

/-- A predicate saying that a function is continuous on a set and is differentiable on its interior.
This assumption naturally appears in many theorems in complex analysis. -/
@[protect_proj] structure diff_on_int_cont (f : E â†’ F) (s : set E) : Prop :=
(differentiable_on : differentiable_on ğ•œ f (interior s))
(continuous_on : continuous_on f s)

variable {ğ•œ}

lemma differentiable_on.diff_on_int_cont (h : differentiable_on ğ•œ f s) :
  diff_on_int_cont ğ•œ f s :=
âŸ¨h.mono interior_subset, h.continuous_onâŸ©

lemma differentiable.diff_on_int_cont (h : differentiable ğ•œ f) : diff_on_int_cont ğ•œ f s :=
h.differentiable_on.diff_on_int_cont

lemma diff_on_int_cont_open (hs : is_open s) :
  diff_on_int_cont ğ•œ f s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, hs.interior_eq â–¸ h.differentiable_on, Î» h, h.diff_on_int_contâŸ©

lemma diff_on_int_cont_univ : diff_on_int_cont ğ•œ f univ â†” differentiable ğ•œ f :=
(diff_on_int_cont_open is_open_univ).trans differentiable_on_univ

lemma diff_on_int_cont_const {c : F} :
  diff_on_int_cont ğ•œ (Î» x : E, c) s :=
âŸ¨differentiable_on_const c, continuous_on_constâŸ©

lemma differentiable_on.comp_diff_on_int_cont {g : G â†’ E} {t : set G}
  (hf : differentiable_on ğ•œ f s) (hg : diff_on_int_cont ğ•œ g t) (h : maps_to g t s) :
  diff_on_int_cont ğ•œ (f âˆ˜ g) t :=
âŸ¨hf.comp hg.differentiable_on $ h.mono_left interior_subset, hf.continuous_on.comp hg.2 hâŸ©

lemma differentiable.comp_diff_on_int_cont {g : G â†’ E} {t : set G}
  (hf : differentiable ğ•œ f) (hg : diff_on_int_cont ğ•œ g t) :
  diff_on_int_cont ğ•œ (f âˆ˜ g) t :=
hf.differentiable_on.comp_diff_on_int_cont hg (maps_to_image _ _)

namespace diff_on_int_cont

lemma comp {g : G â†’ E} {t : set G} (hf : diff_on_int_cont ğ•œ f s) (hg : diff_on_int_cont ğ•œ g t)
  (h : maps_to g t s) (h' : maps_to g (interior t) (interior s)) :
  diff_on_int_cont ğ•œ (f âˆ˜ g) t :=
âŸ¨hf.1.comp hg.1 h', hf.2.comp hg.2 hâŸ©

lemma differentiable_on_ball {x : E} {r : â„} (h : diff_on_int_cont ğ•œ f (closed_ball x r)) :
  differentiable_on ğ•œ f (ball x r) :=
h.differentiable_on.mono ball_subset_interior_closed_ball

lemma mk_ball [normed_space â„ E] {x : E} {r : â„} (hd : differentiable_on ğ•œ f (ball x r))
  (hc : continuous_on f (closed_ball x r)) : diff_on_int_cont ğ•œ f (closed_ball x r) :=
begin
  refine âŸ¨_, hcâŸ©,
  rcases eq_or_ne r 0 with rfl|hr,
  { rw [closed_ball_zero],
    exact (subsingleton_singleton.mono interior_subset).differentiable_on },
  { rwa interior_closed_ball x hr }
end

protected lemma differentiable_at (h : diff_on_int_cont ğ•œ f s) (hx : x âˆˆ interior s) :
  differentiable_at ğ•œ f x :=
h.differentiable_on.differentiable_at $ is_open_interior.mem_nhds hx

lemma differentiable_at' (h : diff_on_int_cont ğ•œ f s) (hx : s âˆˆ ğ“ x) :
  differentiable_at ğ•œ f x :=
h.differentiable_at (mem_interior_iff_mem_nhds.2 hx)

protected lemma mono (h : diff_on_int_cont ğ•œ f s) (ht : t âŠ† s) : diff_on_int_cont ğ•œ f t :=
âŸ¨h.differentiable_on.mono (interior_mono ht), h.continuous_on.mono htâŸ©

lemma add (hf : diff_on_int_cont ğ•œ f s) (hg : diff_on_int_cont ğ•œ g s) :
  diff_on_int_cont ğ•œ (f + g) s :=
âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©

lemma add_const (hf : diff_on_int_cont ğ•œ f s) (c : F) :
  diff_on_int_cont ğ•œ (Î» x, f x + c) s :=
hf.add diff_on_int_cont_const

lemma const_add (hf : diff_on_int_cont ğ•œ f s) (c : F) :
  diff_on_int_cont ğ•œ (Î» x, c + f x) s :=
diff_on_int_cont_const.add hf

lemma neg (hf : diff_on_int_cont ğ•œ f s) : diff_on_int_cont ğ•œ (-f) s := âŸ¨hf.1.neg, hf.2.negâŸ©

lemma sub (hf : diff_on_int_cont ğ•œ f s) (hg : diff_on_int_cont ğ•œ g s) :
  diff_on_int_cont ğ•œ (f - g) s :=
âŸ¨hf.1.sub hg.1, hf.2.sub hg.2âŸ©

lemma sub_const (hf : diff_on_int_cont ğ•œ f s) (c : F) : diff_on_int_cont ğ•œ (Î» x, f x - c) s :=
hf.sub diff_on_int_cont_const

lemma const_sub (hf : diff_on_int_cont ğ•œ f s) (c : F) : diff_on_int_cont ğ•œ (Î» x, c - f x) s :=
diff_on_int_cont_const.sub hf

lemma const_smul {R : Type*} [semiring R] [module R F] [smul_comm_class ğ•œ R F]
  [has_continuous_const_smul R F] (hf : diff_on_int_cont ğ•œ f s) (c : R) :
  diff_on_int_cont ğ•œ (c â€¢ f) s :=
âŸ¨hf.1.const_smul c, hf.2.const_smul câŸ©

lemma smul {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  [normed_space ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F] {c : E â†’ ğ•œ'} {f : E â†’ F} {s : set E}
  (hc : diff_on_int_cont ğ•œ c s) (hf : diff_on_int_cont ğ•œ f s) :
  diff_on_int_cont ğ•œ (Î» x, c x â€¢ f x) s :=
âŸ¨hc.1.smul hf.1, hc.2.smul hf.2âŸ©

lemma smul_const {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  [normed_space ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F] {c : E â†’ ğ•œ'} {s : set E}
  (hc : diff_on_int_cont ğ•œ c s) (y : F) :
  diff_on_int_cont ğ•œ (Î» x, c x â€¢ y) s :=
hc.smul diff_on_int_cont_const

lemma inv {f : E â†’ ğ•œ} (hf : diff_on_int_cont ğ•œ f s) (hâ‚€ : âˆ€ x âˆˆ s, f x â‰  0) :
  diff_on_int_cont ğ•œ fâ»Â¹ s :=
âŸ¨differentiable_on_inv.comp hf.1 $ Î» x hx, hâ‚€ _ (interior_subset hx), hf.2.invâ‚€ hâ‚€âŸ©

end diff_on_int_cont
