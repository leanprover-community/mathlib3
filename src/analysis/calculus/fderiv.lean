/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import analysis.calculus.tangent_cone
import analysis.normed_space.units
import analysis.asymptotics.asymptotic_equivalent
import analysis.analytic.basic

/-!
# The FrÃ©chet derivative

Let `E` and `F` be normed spaces, `f : E â†’ F`, and `f' : E â†’L[ğ•œ] F` a
continuous ğ•œ-linear map, where `ğ•œ` is a non-discrete normed field. Then

  `has_fderiv_within_at f f' s x`

says that `f` has derivative `f'` at `x`, where the domain of interest
is restricted to `s`. We also have

  `has_fderiv_at f f' x := has_fderiv_within_at f f' x univ`

Finally,

  `has_strict_fderiv_at f f' x`

means that `f : E â†’ F` has derivative `f' : E â†’L[ğ•œ] F` in the sense of strict differentiability,
i.e., `f y - f z - f'(y - z) = o(y - z)` as `y, z â†’ x`. This notion is used in the inverse
function theorem, and is defined here only to avoid proving theorems like
`is_bounded_bilinear_map.has_fderiv_at` twice: first for `has_fderiv_at`, then for
`has_strict_fderiv_at`.

## Main results

In addition to the definition and basic properties of the derivative, this file contains the
usual formulas (and existence assertions) for the derivative of
* constants
* the identity
* bounded linear maps
* bounded bilinear maps
* sum of two functions
* sum of finitely many functions
* multiplication of a function by a scalar constant
* negative of a function
* subtraction of two functions
* multiplication of a function by a scalar function
* multiplication of two scalar functions
* composition of functions (the chain rule)
* inverse function (assuming that it exists; the inverse function theorem is in `inverse.lean`)

For most binary operations we also define `const_op` and `op_const` theorems for the cases when
the first or second argument is a constant. This makes writing chains of `has_deriv_at`'s easier,
and they more frequently lead to the desired result.

One can also interpret the derivative of a function `f : ğ•œ â†’ E` as an element of `E` (by identifying
a linear function from `ğ•œ` to `E` with its value at `1`). Results on the FrÃ©chet derivative are
translated to this more elementary point of view on the derivative in the file `deriv.lean`. The
derivative of polynomials is handled there, as it is naturally one-dimensional.

The simplifier is set up to prove automatically that some functions are differentiable, or
differentiable at a point (but not differentiable on a set or within a set at a point, as checking
automatically that the good domains are mapped one to the other when using composition is not
something the simplifier can easily do). This means that one can write
`example (x : â„) : differentiable â„ (Î» x, sin (exp (3 + x^2)) - 5 * cos x) := by simp`.
If there are divisions, one needs to supply to the simplifier proofs that the denominators do
not vanish, as in
```lean
example (x : â„) (h : 1 + sin x â‰  0) : differentiable_at â„ (Î» x, exp x / (1 + sin x)) x :=
by simp [h]
```
Of course, these examples only work once `exp`, `cos` and `sin` have been shown to be
differentiable, in `analysis.special_functions.trigonometric`.

The simplifier is not set up to compute the FrÃ©chet derivative of maps (as these are in general
complicated multidimensional linear maps), but it will compute one-dimensional derivatives,
see `deriv.lean`.

## Implementation details

The derivative is defined in terms of the `is_o` relation, but also
characterized in terms of the `tendsto` relation.

We also introduce predicates `differentiable_within_at ğ•œ f s x` (where `ğ•œ` is the base field,
`f` the function to be differentiated, `x` the point at which the derivative is asserted to exist,
and `s` the set along which the derivative is defined), as well as `differentiable_at ğ•œ f x`,
`differentiable_on ğ•œ f s` and `differentiable ğ•œ f` to express the existence of a derivative.

To be able to compute with derivatives, we write `fderiv_within ğ•œ f s x` and `fderiv ğ•œ f x`
for some choice of a derivative if it exists, and the zero function otherwise. This choice only
behaves well along sets for which the derivative is unique, i.e., those for which the tangent
directions span a dense subset of the whole space. The predicates `unique_diff_within_at s x` and
`unique_diff_on s`, defined in `tangent_cone.lean` express this property. We prove that indeed
they imply the uniqueness of the derivative. This is satisfied for open subsets, and in particular
for `univ`. This uniqueness only holds when the field is non-discrete, which we request at the very
beginning: otherwise, a derivative can be defined, but it has no interesting properties whatsoever.

To make sure that the simplifier can prove automatically that functions are differentiable, we tag
many lemmas with the `simp` attribute, for instance those saying that the sum of differentiable
functions is differentiable, as well as their product, their cartesian product, and so on. A notable
exception is the chain rule: we do not mark as a simp lemma the fact that, if `f` and `g` are
differentiable, then their composition also is: `simp` would always be able to match this lemma,
by taking `f` or `g` to be the identity. Instead, for every reasonable function (say, `exp`),
we add a lemma that if `f` is differentiable then so is `(Î» x, exp (f x))`. This means adding
some boilerplate lemmas, but these can also be useful in their own right.

Tests for this ability of the simplifier (with more examples) are provided in
`tests/differentiable.lean`.

## Tags

derivative, differentiable, FrÃ©chet, calculus

-/

open filter asymptotics continuous_linear_map set metric
open_locale topological_space classical nnreal asymptotics filter ennreal

noncomputable theory


section

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_group F] [normed_space ğ•œ F]
variables {G : Type*} [normed_group G] [normed_space ğ•œ G]
variables {G' : Type*} [normed_group G'] [normed_space ğ•œ G']

/-- A function `f` has the continuous linear map `f'` as derivative along the filter `L` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` converges along the filter `L`. This definition
is designed to be specialized for `L = ğ“ x` (in `has_fderiv_at`), giving rise to the usual notion
of FrÃ©chet derivative, and for `L = ğ“[s] x` (in `has_fderiv_within_at`), giving rise to
the notion of FrÃ©chet derivative along the set `s`. -/
def has_fderiv_at_filter (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) (L : filter E) :=
is_o (Î» x', f x' - f x - f' (x' - x)) (Î» x', x' - x) L

/-- A function `f` has the continuous linear map `f'` as derivative at `x` within a set `s` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x` inside `s`. -/
def has_fderiv_within_at (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (s : set E) (x : E) :=
has_fderiv_at_filter f f' x (ğ“[s] x)

/-- A function `f` has the continuous linear map `f'` as derivative at `x` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x`. -/
def has_fderiv_at (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) :=
has_fderiv_at_filter f f' x (ğ“ x)

/-- A function `f` has derivative `f'` at `a` in the sense of *strict differentiability*
if `f x - f y - f' (x - y) = o(x - y)` as `x, y â†’ a`. This form of differentiability is required,
e.g., by the inverse function theorem. Any `C^1` function on a vector space over `â„` is strictly
differentiable but this definition works, e.g., for vector spaces over `p`-adic numbers. -/
def has_strict_fderiv_at (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) :=
is_o (Î» p : E Ã— E, f p.1 - f p.2 - f' (p.1 - p.2)) (Î» p : E Ã— E, p.1 - p.2) (ğ“ (x, x))

variables (ğ•œ)

/-- A function `f` is differentiable at a point `x` within a set `s` if it admits a derivative
there (possibly non-unique). -/
def differentiable_within_at (f : E â†’ F) (s : set E) (x : E) :=
âˆƒf' : E â†’L[ğ•œ] F, has_fderiv_within_at f f' s x

/-- A function `f` is differentiable at a point `x` if it admits a derivative there (possibly
non-unique). -/
def differentiable_at (f : E â†’ F) (x : E) :=
âˆƒf' : E â†’L[ğ•œ] F, has_fderiv_at f f' x

/-- If `f` has a derivative at `x` within `s`, then `fderiv_within ğ•œ f s x` is such a derivative.
Otherwise, it is set to `0`. -/
def fderiv_within (f : E â†’ F) (s : set E) (x : E) : E â†’L[ğ•œ] F :=
if h : âˆƒf', has_fderiv_within_at f f' s x then classical.some h else 0

/-- If `f` has a derivative at `x`, then `fderiv ğ•œ f x` is such a derivative. Otherwise, it is
set to `0`. -/
def fderiv (f : E â†’ F) (x : E) : E â†’L[ğ•œ] F :=
if h : âˆƒf', has_fderiv_at f f' x then classical.some h else 0

/-- `differentiable_on ğ•œ f s` means that `f` is differentiable within `s` at any point of `s`. -/
def differentiable_on (f : E â†’ F) (s : set E) :=
âˆ€x âˆˆ s, differentiable_within_at ğ•œ f s x

/-- `differentiable ğ•œ f` means that `f` is differentiable at any point. -/
def differentiable (f : E â†’ F) :=
âˆ€x, differentiable_at ğ•œ f x

variables {ğ•œ}
variables {f fâ‚€ fâ‚ g : E â†’ F}
variables {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variables (e : E â†’L[ğ•œ] F)
variables {x : E}
variables {s t : set E}
variables {L Lâ‚ Lâ‚‚ : filter E}

lemma fderiv_within_zero_of_not_differentiable_within_at
  (h : Â¬ differentiable_within_at ğ•œ f s x) : fderiv_within ğ•œ f s x = 0 :=
have Â¬ âˆƒ f', has_fderiv_within_at f f' s x, from h,
by simp [fderiv_within, this]

lemma fderiv_zero_of_not_differentiable_at (h : Â¬ differentiable_at ğ•œ f x) : fderiv ğ•œ f x = 0 :=
have Â¬ âˆƒ f', has_fderiv_at f f' x, from h,
by simp [fderiv, this]

section derivative_uniqueness
/- In this section, we discuss the uniqueness of the derivative.
We prove that the definitions `unique_diff_within_at` and `unique_diff_on` indeed imply the
uniqueness of the derivative. -/

/-- If a function f has a derivative f' at x, a rescaled version of f around x converges to f',
i.e., `n (f (x + (1/n) v) - f x)` converges to `f' v`. More generally, if `c n` tends to infinity
and `c n * d n` tends to `v`, then `c n * (f (x + d n) - f x)` tends to `f' v`. This lemma expresses
this fact, for functions having a derivative within a set. Its specific formulation is useful for
tangent cone related discussions. -/
theorem has_fderiv_within_at.lim (h : has_fderiv_within_at f f' s x) {Î± : Type*} (l : filter Î±)
  {c : Î± â†’ ğ•œ} {d : Î± â†’ E} {v : E} (dtop : âˆ€á¶  n in l, x + d n âˆˆ s)
  (clim : tendsto (Î» n, âˆ¥c nâˆ¥) l at_top)
  (cdlim : tendsto (Î» n, c n â€¢ d n) l (ğ“ v)) :
  tendsto (Î»n, c n â€¢ (f (x + d n) - f x)) l (ğ“ (f' v)) :=
begin
  have tendsto_arg : tendsto (Î» n, x + d n) l (ğ“[s] x),
  { conv in (ğ“[s] x) { rw â† add_zero x },
    rw [nhds_within, tendsto_inf],
    split,
    { apply tendsto_const_nhds.add (tangent_cone_at.lim_zero l clim cdlim) },
    { rwa tendsto_principal } },
  have : is_o (Î» y, f y - f x - f' (y - x)) (Î» y, y - x) (ğ“[s] x) := h,
  have : is_o (Î» n, f (x + d n) - f x - f' ((x + d n) - x)) (Î» n, (x + d n)  - x) l :=
    this.comp_tendsto tendsto_arg,
  have : is_o (Î» n, f (x + d n) - f x - f' (d n)) d l := by simpa only [add_sub_cancel'],
  have : is_o (Î»n, c n â€¢ (f (x + d n) - f x - f' (d n))) (Î»n, c n â€¢ d n) l :=
    (is_O_refl c l).smul_is_o this,
  have : is_o (Î»n, c n â€¢ (f (x + d n) - f x - f' (d n))) (Î»n, (1:â„)) l :=
    this.trans_is_O (is_O_one_of_tendsto â„ cdlim),
  have L1 : tendsto (Î»n, c n â€¢ (f (x + d n) - f x - f' (d n))) l (ğ“ 0) :=
    (is_o_one_iff â„).1 this,
  have L2 : tendsto (Î»n, f' (c n â€¢ d n)) l (ğ“ (f' v)) :=
    tendsto.comp f'.cont.continuous_at cdlim,
  have L3 : tendsto (Î»n, (c n â€¢ (f (x + d n) - f x - f' (d n)) +  f' (c n â€¢ d n)))
            l (ğ“ (0 + f' v)) :=
    L1.add L2,
  have : (Î»n, (c n â€¢ (f (x + d n) - f x - f' (d n)) +  f' (c n â€¢ d n)))
          = (Î»n, c n â€¢ (f (x + d n) - f x)),
    by { ext n, simp [smul_add, smul_sub] },
  rwa [this, zero_add] at L3
end

/-- If `f'` and `fâ‚'` are two derivatives of `f` within `s` at `x`, then they are equal on the
tangent cone to `s` at `x` -/
theorem has_fderiv_within_at.unique_on (hf : has_fderiv_within_at f f' s x)
  (hg : has_fderiv_within_at f fâ‚' s x) :
  eq_on f' fâ‚' (tangent_cone_at ğ•œ s x) :=
Î» y âŸ¨c, d, dtop, clim, cdlimâŸ©,
  tendsto_nhds_unique (hf.lim at_top dtop clim cdlim) (hg.lim at_top dtop clim cdlim)

/-- `unique_diff_within_at` achieves its goal: it implies the uniqueness of the derivative. -/
theorem unique_diff_within_at.eq (H : unique_diff_within_at ğ•œ s x)
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at f fâ‚' s x) : f' = fâ‚' :=
continuous_linear_map.ext_on H.1 (hf.unique_on hg)

theorem unique_diff_on.eq (H : unique_diff_on ğ•œ s) (hx : x âˆˆ s)
  (h : has_fderiv_within_at f f' s x) (hâ‚ : has_fderiv_within_at f fâ‚' s x) : f' = fâ‚' :=
(H x hx).eq h hâ‚

end derivative_uniqueness

section fderiv_properties
/-! ### Basic properties of the derivative -/

theorem has_fderiv_at_filter_iff_tendsto :
  has_fderiv_at_filter f f' x L â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) L (ğ“ 0) :=
have h : âˆ€ x', âˆ¥x' - xâˆ¥ = 0 â†’ âˆ¥f x' - f x - f' (x' - x)âˆ¥ = 0, from Î» x' hx',
  by { rw [sub_eq_zero.1 (norm_eq_zero.1 hx')], simp },
begin
  unfold has_fderiv_at_filter,
  rw [â†is_o_norm_left, â†is_o_norm_right, is_o_iff_tendsto h],
  exact tendsto_congr (Î» _, div_eq_inv_mul),
end

theorem has_fderiv_within_at_iff_tendsto : has_fderiv_within_at f f' s x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) (ğ“[s] x) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_fderiv_at_iff_tendsto : has_fderiv_at f f' x â†”
  tendsto (Î» x', âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) (ğ“ x) (ğ“ 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_fderiv_at_iff_is_o_nhds_zero : has_fderiv_at f f' x â†”
  is_o (Î»h, f (x + h) - f x - f' h) (Î»h, h) (ğ“ 0) :=
begin
  rw [has_fderiv_at, has_fderiv_at_filter, â† map_add_left_nhds_zero x, is_o_map],
  simp [(âˆ˜)]
end

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then it its derivative at `xâ‚€` has norm bounded by `C`. -/
lemma has_fderiv_at.le_of_lip {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {xâ‚€ : E} (hf : has_fderiv_at f f' xâ‚€)
  {s : set E} (hs : s âˆˆ ğ“ xâ‚€) {C : â„â‰¥0} (hlip : lipschitz_on_with C f s) : âˆ¥f'âˆ¥ â‰¤ C :=
begin
  refine le_of_forall_pos_le_add (Î» Îµ Îµ0, op_norm_le_of_nhds_zero _ _),
  exact add_nonneg C.coe_nonneg Îµ0.le,
  have hs' := hs, rw [â† map_add_left_nhds_zero xâ‚€, mem_map] at hs',
  filter_upwards [is_o_iff.1 (has_fderiv_at_iff_is_o_nhds_zero.1 hf) Îµ0, hs'], intros y hy hys,
  have := hlip.norm_sub_le hys (mem_of_nhds hs), rw add_sub_cancel' at this,
  calc âˆ¥f' yâˆ¥ â‰¤ âˆ¥f (xâ‚€ + y) - f xâ‚€âˆ¥ + âˆ¥f (xâ‚€ + y) - f xâ‚€ - f' yâˆ¥ : norm_le_insert _ _
          ... â‰¤ C * âˆ¥yâˆ¥ + Îµ * âˆ¥yâˆ¥                                : add_le_add this hy
          ... = (C + Îµ) * âˆ¥yâˆ¥                                    : (add_mul _ _ _).symm
end

theorem has_fderiv_at_filter.mono (h : has_fderiv_at_filter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
  has_fderiv_at_filter f f' x Lâ‚ :=
h.mono hst

theorem has_fderiv_within_at.mono (h : has_fderiv_within_at f f' t x) (hst : s âŠ† t) :
  has_fderiv_within_at f f' s x :=
h.mono (nhds_within_mono _ hst)

theorem has_fderiv_at.has_fderiv_at_filter (h : has_fderiv_at f f' x) (hL : L â‰¤ ğ“ x) :
  has_fderiv_at_filter f f' x L :=
h.mono hL

theorem has_fderiv_at.has_fderiv_within_at
  (h : has_fderiv_at f f' x) : has_fderiv_within_at f f' s x :=
h.has_fderiv_at_filter inf_le_left

lemma has_fderiv_within_at.differentiable_within_at (h : has_fderiv_within_at f f' s x) :
  differentiable_within_at ğ•œ f s x :=
âŸ¨f', hâŸ©

lemma has_fderiv_at.differentiable_at (h : has_fderiv_at f f' x) : differentiable_at ğ•œ f x :=
âŸ¨f', hâŸ©

@[simp] lemma has_fderiv_within_at_univ :
  has_fderiv_within_at f f' univ x â†” has_fderiv_at f f' x :=
by { simp only [has_fderiv_within_at, nhds_within_univ], refl }

lemma has_strict_fderiv_at.is_O_sub (hf : has_strict_fderiv_at f f' x) :
  is_O (Î» p : E Ã— E, f p.1 - f p.2) (Î» p : E Ã— E, p.1 - p.2) (ğ“ (x, x)) :=
hf.is_O.congr_of_sub.2 (f'.is_O_comp _ _)

lemma has_fderiv_at_filter.is_O_sub (h : has_fderiv_at_filter f f' x L) :
  is_O (Î» x', f x' - f x) (Î» x', x' - x) L :=
h.is_O.congr_of_sub.2 (f'.is_O_sub _ _)

protected lemma has_strict_fderiv_at.has_fderiv_at (hf : has_strict_fderiv_at f f' x) :
  has_fderiv_at f f' x :=
begin
  rw [has_fderiv_at, has_fderiv_at_filter, is_o_iff],
  exact (Î» c hc, tendsto_id.prod_mk_nhds tendsto_const_nhds (is_o_iff.1 hf hc))
end

protected lemma has_strict_fderiv_at.differentiable_at (hf : has_strict_fderiv_at f f' x) :
  differentiable_at ğ•œ f x :=
hf.has_fderiv_at.differentiable_at

/-- Directional derivative agrees with `has_fderiv`. -/
lemma has_fderiv_at.lim (hf : has_fderiv_at f f' x) (v : E) {Î± : Type*} {c : Î± â†’ ğ•œ}
  {l : filter Î±} (hc : tendsto (Î» n, âˆ¥c nâˆ¥) l at_top) :
  tendsto (Î» n, (c n) â€¢ (f (x + (c n)â»Â¹ â€¢ v) - f x)) l (ğ“ (f' v)) :=
begin
  refine (has_fderiv_within_at_univ.2 hf).lim _ (univ_mem_sets' (Î» _, trivial)) hc _,
  assume U hU,
  refine (eventually_ne_of_tendsto_norm_at_top hc (0:ğ•œ)).mono (Î» y hy, _),
  convert mem_of_nhds hU,
  dsimp only,
  rw [â† mul_smul, mul_inv_cancel hy, one_smul]
end

theorem has_fderiv_at.unique
  (hâ‚€ : has_fderiv_at f fâ‚€' x) (hâ‚ : has_fderiv_at f fâ‚' x) : fâ‚€' = fâ‚' :=
begin
  rw â† has_fderiv_within_at_univ at hâ‚€ hâ‚,
  exact unique_diff_within_at_univ.eq hâ‚€ hâ‚
end

lemma has_fderiv_within_at_inter' (h : t âˆˆ ğ“[s] x) :
  has_fderiv_within_at f f' (s âˆ© t) x â†” has_fderiv_within_at f f' s x :=
by simp [has_fderiv_within_at, nhds_within_restrict'' s h]

lemma has_fderiv_within_at_inter (h : t âˆˆ ğ“ x) :
  has_fderiv_within_at f f' (s âˆ© t) x â†” has_fderiv_within_at f f' s x :=
by simp [has_fderiv_within_at, nhds_within_restrict' s h]

lemma has_fderiv_within_at.union (hs : has_fderiv_within_at f f' s x)
  (ht : has_fderiv_within_at f f' t x) :
  has_fderiv_within_at f f' (s âˆª t) x :=
begin
  simp only [has_fderiv_within_at, nhds_within_union],
  exact hs.join ht,
end

lemma has_fderiv_within_at.nhds_within (h : has_fderiv_within_at f f' s x)
  (ht : s âˆˆ ğ“[t] x) : has_fderiv_within_at f f' t x :=
(has_fderiv_within_at_inter' ht).1 (h.mono (inter_subset_right _ _))

lemma has_fderiv_within_at.has_fderiv_at (h : has_fderiv_within_at f f' s x) (hs : s âˆˆ ğ“ x) :
  has_fderiv_at f f' x :=
by rwa [â† univ_inter s, has_fderiv_within_at_inter hs, has_fderiv_within_at_univ] at h

lemma differentiable_within_at.has_fderiv_within_at (h : differentiable_within_at ğ•œ f s x) :
  has_fderiv_within_at f (fderiv_within ğ•œ f s x) s x :=
begin
  dunfold fderiv_within,
  dunfold differentiable_within_at at h,
  rw dif_pos h,
  exact classical.some_spec h
end

lemma differentiable_at.has_fderiv_at (h : differentiable_at ğ•œ f x) :
  has_fderiv_at f (fderiv ğ•œ f x) x :=
begin
  dunfold fderiv,
  dunfold differentiable_at at h,
  rw dif_pos h,
  exact classical.some_spec h
end

lemma has_fderiv_at.fderiv (h : has_fderiv_at f f' x) : fderiv ğ•œ f x = f' :=
by { ext, rw h.unique h.differentiable_at.has_fderiv_at }

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then it its derivative at `xâ‚€` has norm bounded by `C`.
Version using `fderiv`. -/
lemma fderiv_at.le_of_lip {f : E â†’ F} {xâ‚€ : E} (hf : differentiable_at ğ•œ f xâ‚€)
  {s : set E} (hs : s âˆˆ ğ“ xâ‚€) {C : â„â‰¥0} (hlip : lipschitz_on_with C f s) : âˆ¥fderiv ğ•œ f xâ‚€âˆ¥ â‰¤ C :=
hf.has_fderiv_at.le_of_lip hs hlip

lemma has_fderiv_within_at.fderiv_within
  (h : has_fderiv_within_at f f' s x) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ f s x = f' :=
(hxs.eq h h.differentiable_within_at.has_fderiv_within_at).symm

/-- If `x` is not in the closure of `s`, then `f` has any derivative at `x` within `s`,
as this statement is empty. -/
lemma has_fderiv_within_at_of_not_mem_closure (h : x âˆ‰ closure s) :
  has_fderiv_within_at f f' s x :=
begin
  simp only [mem_closure_iff_nhds_within_ne_bot, ne_bot_iff, ne.def, not_not] at h,
  simp [has_fderiv_within_at, has_fderiv_at_filter, h, is_o, is_O_with],
end

lemma differentiable_within_at.mono (h : differentiable_within_at ğ•œ f t x) (st : s âŠ† t) :
  differentiable_within_at ğ•œ f s x :=
begin
  rcases h with âŸ¨f', hf'âŸ©,
  exact âŸ¨f', hf'.mono stâŸ©
end

lemma differentiable_within_at_univ :
  differentiable_within_at ğ•œ f univ x â†” differentiable_at ğ•œ f x :=
by simp only [differentiable_within_at, has_fderiv_within_at_univ, differentiable_at]

lemma differentiable_within_at_inter (ht : t âˆˆ ğ“ x) :
  differentiable_within_at ğ•œ f (s âˆ© t) x â†” differentiable_within_at ğ•œ f s x :=
by simp only [differentiable_within_at, has_fderiv_within_at, has_fderiv_at_filter,
    nhds_within_restrict' s ht]

lemma differentiable_within_at_inter' (ht : t âˆˆ ğ“[s] x) :
  differentiable_within_at ğ•œ f (s âˆ© t) x â†” differentiable_within_at ğ•œ f s x :=
by simp only [differentiable_within_at, has_fderiv_within_at, has_fderiv_at_filter,
    nhds_within_restrict'' s ht]

lemma differentiable_at.differentiable_within_at
  (h : differentiable_at ğ•œ f x) : differentiable_within_at ğ•œ f s x :=
(differentiable_within_at_univ.2 h).mono (subset_univ _)

lemma differentiable.differentiable_at (h : differentiable ğ•œ f) :
  differentiable_at ğ•œ f x :=
h x

lemma differentiable_within_at.differentiable_at
  (h : differentiable_within_at ğ•œ f s x) (hs : s âˆˆ ğ“ x) : differentiable_at ğ•œ f x :=
h.imp (Î» f' hf', hf'.has_fderiv_at hs)

lemma differentiable_at.fderiv_within
  (h : differentiable_at ğ•œ f x) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ f s x = fderiv ğ•œ f x :=
begin
  apply has_fderiv_within_at.fderiv_within _ hxs,
  exact h.has_fderiv_at.has_fderiv_within_at
end

lemma differentiable_on.mono (h : differentiable_on ğ•œ f t) (st : s âŠ† t) :
  differentiable_on ğ•œ f s :=
Î»x hx, (h x (st hx)).mono st

lemma differentiable_on_univ :
  differentiable_on ğ•œ f univ â†” differentiable ğ•œ f :=
by { simp [differentiable_on, differentiable_within_at_univ], refl }

lemma differentiable.differentiable_on (h : differentiable ğ•œ f) : differentiable_on ğ•œ f s :=
(differentiable_on_univ.2 h).mono (subset_univ _)

lemma differentiable_on_of_locally_differentiable_on
  (h : âˆ€xâˆˆs, âˆƒu, is_open u âˆ§ x âˆˆ u âˆ§ differentiable_on ğ•œ f (s âˆ© u)) : differentiable_on ğ•œ f s :=
begin
  assume x xs,
  rcases h x xs with âŸ¨t, t_open, xt, htâŸ©,
  exact (differentiable_within_at_inter (mem_nhds_sets t_open xt)).1 (ht x âŸ¨xs, xtâŸ©)
end

lemma fderiv_within_subset (st : s âŠ† t) (ht : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f t x) :
  fderiv_within ğ•œ f s x = fderiv_within ğ•œ f t x :=
((differentiable_within_at.has_fderiv_within_at h).mono st).fderiv_within ht

@[simp] lemma fderiv_within_univ : fderiv_within ğ•œ f univ = fderiv ğ•œ f :=
begin
  ext x : 1,
  by_cases h : differentiable_at ğ•œ f x,
  { apply has_fderiv_within_at.fderiv_within _ unique_diff_within_at_univ,
    rw has_fderiv_within_at_univ,
    apply h.has_fderiv_at },
  { have : Â¬ differentiable_within_at ğ•œ f univ x,
      by contrapose! h; rwa â† differentiable_within_at_univ,
    rw [fderiv_zero_of_not_differentiable_at h,
        fderiv_within_zero_of_not_differentiable_within_at this] }
end

lemma fderiv_within_inter (ht : t âˆˆ ğ“ x) (hs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ f (s âˆ© t) x = fderiv_within ğ•œ f s x :=
begin
  by_cases h : differentiable_within_at ğ•œ f (s âˆ© t) x,
  { apply fderiv_within_subset (inter_subset_left _ _) _ ((differentiable_within_at_inter ht).1 h),
    apply hs.inter ht },
  { have : Â¬ differentiable_within_at ğ•œ f s x,
      by contrapose! h; rw differentiable_within_at_inter; assumption,
    rw [fderiv_within_zero_of_not_differentiable_within_at h,
        fderiv_within_zero_of_not_differentiable_within_at this] }
end

lemma fderiv_within_of_mem_nhds (h : s âˆˆ ğ“ x) :
  fderiv_within ğ•œ f s x = fderiv ğ•œ f x :=
begin
  have : s = univ âˆ© s, by simp only [univ_inter],
  rw [this, â† fderiv_within_univ],
  exact fderiv_within_inter h (unique_diff_on_univ _ (mem_univ _))
end

lemma fderiv_within_of_open (hs : is_open s) (hx : x âˆˆ s) :
  fderiv_within ğ•œ f s x = fderiv ğ•œ f x :=
fderiv_within_of_mem_nhds (mem_nhds_sets hs hx)

lemma fderiv_within_eq_fderiv (hs : unique_diff_within_at ğ•œ s x) (h : differentiable_at ğ•œ f x) :
  fderiv_within ğ•œ f s x = fderiv ğ•œ f x :=
begin
  rw â† fderiv_within_univ,
  exact fderiv_within_subset (subset_univ _) hs h.differentiable_within_at
end

lemma fderiv_mem_iff {f : E â†’ F} {s : set (E â†’L[ğ•œ] F)} {x : E} :
  fderiv ğ•œ f x âˆˆ s â†” (differentiable_at ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ s) âˆ¨
    (0 : E â†’L[ğ•œ] F) âˆˆ s âˆ§ Â¬differentiable_at ğ•œ f x :=
begin
  split,
  { intro hfx,
    by_cases hx : differentiable_at ğ•œ f x,
    { exact or.inl âŸ¨hx, hfxâŸ© },
    { rw [fderiv_zero_of_not_differentiable_at hx] at hfx,
      exact or.inr âŸ¨hfx, hxâŸ© } },
  { rintro (âŸ¨hf, hf'âŸ©|âŸ¨hâ‚€, hxâŸ©),
    { exact hf' },
    { rwa [fderiv_zero_of_not_differentiable_at hx] } }
end

end fderiv_properties

section continuous
/-! ### Deducing continuity from differentiability -/

theorem has_fderiv_at_filter.tendsto_nhds
  (hL : L â‰¤ ğ“ x) (h : has_fderiv_at_filter f f' x L) :
  tendsto f L (ğ“ (f x)) :=
begin
  have : tendsto (Î» x', f x' - f x) L (ğ“ 0),
  { refine h.is_O_sub.trans_tendsto (tendsto.mono_left _ hL),
    rw â† sub_self x, exact tendsto_id.sub tendsto_const_nhds },
  have := tendsto.add this tendsto_const_nhds,
  rw zero_add (f x) at this,
  exact this.congr (by simp)
end

theorem has_fderiv_within_at.continuous_within_at
  (h : has_fderiv_within_at f f' s x) : continuous_within_at f s x :=
has_fderiv_at_filter.tendsto_nhds inf_le_left h

theorem has_fderiv_at.continuous_at (h : has_fderiv_at f f' x) :
  continuous_at f x :=
has_fderiv_at_filter.tendsto_nhds (le_refl _) h

lemma differentiable_within_at.continuous_within_at (h : differentiable_within_at ğ•œ f s x) :
  continuous_within_at f s x :=
let âŸ¨f', hf'âŸ© := h in hf'.continuous_within_at

lemma differentiable_at.continuous_at (h : differentiable_at ğ•œ f x) : continuous_at f x :=
let âŸ¨f', hf'âŸ© := h in hf'.continuous_at

lemma differentiable_on.continuous_on (h : differentiable_on ğ•œ f s) : continuous_on f s :=
Î»x hx, (h x hx).continuous_within_at

lemma differentiable.continuous (h : differentiable ğ•œ f) : continuous f :=
continuous_iff_continuous_at.2 $ Î»x, (h x).continuous_at

protected lemma has_strict_fderiv_at.continuous_at (hf : has_strict_fderiv_at f f' x) :
  continuous_at f x :=
hf.has_fderiv_at.continuous_at

lemma has_strict_fderiv_at.is_O_sub_rev {f' : E â‰ƒL[ğ•œ] F}
  (hf : has_strict_fderiv_at f (f' : E â†’L[ğ•œ] F) x) :
  is_O (Î» p : E Ã— E, p.1 - p.2) (Î» p : E Ã— E, f p.1 - f p.2) (ğ“ (x, x)) :=
((f'.is_O_comp_rev _ _).trans (hf.trans_is_O (f'.is_O_comp_rev _ _)).right_is_O_add).congr
(Î» _, rfl) (Î» _, sub_add_cancel _ _)

lemma has_fderiv_at_filter.is_O_sub_rev {f' : E â‰ƒL[ğ•œ] F}
  (hf : has_fderiv_at_filter f (f' : E â†’L[ğ•œ] F) x L) :
  is_O (Î» x', x' - x) (Î» x', f x' - f x) L :=
((f'.is_O_sub_rev _ _).trans (hf.trans_is_O (f'.is_O_sub_rev _ _)).right_is_O_add).congr
(Î» _, rfl) (Î» _, sub_add_cancel _ _)

end continuous

section congr
/-! ### congr properties of the derivative -/

theorem filter.eventually_eq.has_strict_fderiv_at_iff
  (h : fâ‚€ =á¶ [ğ“ x] fâ‚) (h' : âˆ€ y, fâ‚€' y = fâ‚' y) :
  has_strict_fderiv_at fâ‚€ fâ‚€' x â†” has_strict_fderiv_at fâ‚ fâ‚' x :=
begin
  refine is_o_congr ((h.prod_mk_nhds h).mono _) (eventually_of_forall $ Î» _, rfl),
  rintros p âŸ¨hpâ‚, hpâ‚‚âŸ©,
  simp only [*]
end

theorem has_strict_fderiv_at.congr_of_eventually_eq (h : has_strict_fderiv_at f f' x)
  (hâ‚ : f =á¶ [ğ“ x] fâ‚) : has_strict_fderiv_at fâ‚ f' x :=
(hâ‚.has_strict_fderiv_at_iff (Î» _, rfl)).1 h

theorem filter.eventually_eq.has_fderiv_at_filter_iff
  (hâ‚€ : fâ‚€ =á¶ [L] fâ‚) (hx : fâ‚€ x = fâ‚ x) (hâ‚ : âˆ€ x, fâ‚€' x = fâ‚' x) :
  has_fderiv_at_filter fâ‚€ fâ‚€' x L â†” has_fderiv_at_filter fâ‚ fâ‚' x L :=
is_o_congr (hâ‚€.mono $ Î» y hy, by simp only [hy, hâ‚, hx]) (eventually_of_forall $ Î» _, rfl)

lemma has_fderiv_at_filter.congr_of_eventually_eq (h : has_fderiv_at_filter f f' x L)
  (hL : fâ‚ =á¶ [L] f) (hx : fâ‚ x = f x) : has_fderiv_at_filter fâ‚ f' x L :=
(hL.has_fderiv_at_filter_iff hx $ Î» _, rfl).2 h

lemma has_fderiv_within_at.congr_mono (h : has_fderiv_within_at f f' s x) (ht : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : has_fderiv_within_at fâ‚ f' t x :=
has_fderiv_at_filter.congr_of_eventually_eq (h.mono hâ‚) (filter.mem_inf_sets_of_right ht) hx

lemma has_fderiv_within_at.congr (h : has_fderiv_within_at f f' s x) (hs : âˆ€x âˆˆ s, fâ‚ x = f x)
  (hx : fâ‚ x = f x) : has_fderiv_within_at fâ‚ f' s x :=
h.congr_mono hs hx (subset.refl _)

lemma has_fderiv_within_at.congr' (h : has_fderiv_within_at f f' s x) (hs : âˆ€x âˆˆ s, fâ‚ x = f x)
  (hx : x âˆˆ s) : has_fderiv_within_at fâ‚ f' s x :=
h.congr hs (hs x hx)

lemma has_fderiv_within_at.congr_of_eventually_eq (h : has_fderiv_within_at f f' s x)
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : has_fderiv_within_at fâ‚ f' s x :=
has_fderiv_at_filter.congr_of_eventually_eq h hâ‚ hx

lemma has_fderiv_at.congr_of_eventually_eq (h : has_fderiv_at f f' x)
  (hâ‚ : fâ‚ =á¶ [ğ“ x] f) : has_fderiv_at fâ‚ f' x :=
has_fderiv_at_filter.congr_of_eventually_eq h hâ‚ (mem_of_nhds hâ‚ : _)

lemma differentiable_within_at.congr_mono (h : differentiable_within_at ğ•œ f s x)
  (ht : âˆ€x âˆˆ t, fâ‚ x = f x) (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : differentiable_within_at ğ•œ fâ‚ t x :=
(has_fderiv_within_at.congr_mono h.has_fderiv_within_at ht hx hâ‚).differentiable_within_at

lemma differentiable_within_at.congr (h : differentiable_within_at ğ•œ f s x)
  (ht : âˆ€x âˆˆ s, fâ‚ x = f x) (hx : fâ‚ x = f x) : differentiable_within_at ğ•œ fâ‚ s x :=
differentiable_within_at.congr_mono h ht hx (subset.refl _)

lemma differentiable_within_at.congr_of_eventually_eq
  (h : differentiable_within_at ğ•œ f s x) (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f)
  (hx : fâ‚ x = f x) : differentiable_within_at ğ•œ fâ‚ s x :=
(h.has_fderiv_within_at.congr_of_eventually_eq hâ‚ hx).differentiable_within_at

lemma differentiable_on.congr_mono (h : differentiable_on ğ•œ f s) (h' : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hâ‚ : t âŠ† s) : differentiable_on ğ•œ fâ‚ t :=
Î» x hx, (h x (hâ‚ hx)).congr_mono h' (h' x hx) hâ‚

lemma differentiable_on.congr (h : differentiable_on ğ•œ f s) (h' : âˆ€x âˆˆ s, fâ‚ x = f x) :
  differentiable_on ğ•œ fâ‚ s :=
Î» x hx, (h x hx).congr h' (h' x hx)

lemma differentiable_on_congr (h' : âˆ€x âˆˆ s, fâ‚ x = f x) :
  differentiable_on ğ•œ fâ‚ s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, differentiable_on.congr h (Î»y hy, (h' y hy).symm),
Î» h, differentiable_on.congr h h'âŸ©

lemma differentiable_at.congr_of_eventually_eq (h : differentiable_at ğ•œ f x) (hL : fâ‚ =á¶ [ğ“ x] f) :
  differentiable_at ğ•œ fâ‚ x :=
has_fderiv_at.differentiable_at
  (has_fderiv_at_filter.congr_of_eventually_eq h.has_fderiv_at hL (mem_of_nhds hL : _))

lemma differentiable_within_at.fderiv_within_congr_mono (h : differentiable_within_at ğ•œ f s x)
  (hs : âˆ€x âˆˆ t, fâ‚ x = f x) (hx : fâ‚ x = f x) (hxt : unique_diff_within_at ğ•œ t x) (hâ‚ : t âŠ† s) :
  fderiv_within ğ•œ fâ‚ t x = fderiv_within ğ•œ f s x :=
(has_fderiv_within_at.congr_mono h.has_fderiv_within_at hs hx hâ‚).fderiv_within hxt

lemma filter.eventually_eq.fderiv_within_eq (hs : unique_diff_within_at ğ•œ s x)
  (hL : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  fderiv_within ğ•œ fâ‚ s x = fderiv_within ğ•œ f s x :=
if h : differentiable_within_at ğ•œ f s x
then has_fderiv_within_at.fderiv_within (h.has_fderiv_within_at.congr_of_eventually_eq hL hx) hs
else
  have h' : Â¬ differentiable_within_at ğ•œ fâ‚ s x,
  from mt (Î» h, h.congr_of_eventually_eq (hL.mono $ Î» x, eq.symm) hx.symm) h,
  by rw [fderiv_within_zero_of_not_differentiable_within_at h,
    fderiv_within_zero_of_not_differentiable_within_at h']

lemma fderiv_within_congr (hs : unique_diff_within_at ğ•œ s x)
  (hL : âˆ€yâˆˆs, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  fderiv_within ğ•œ fâ‚ s x = fderiv_within ğ•œ f s x :=
begin
  apply filter.eventually_eq.fderiv_within_eq hs _ hx,
  apply mem_sets_of_superset self_mem_nhds_within,
  exact hL
end

lemma filter.eventually_eq.fderiv_eq (hL : fâ‚ =á¶ [ğ“ x] f) :
  fderiv ğ•œ fâ‚ x = fderiv ğ•œ f x :=
begin
  have A : fâ‚ x = f x := hL.eq_of_nhds,
  rw [â† fderiv_within_univ, â† fderiv_within_univ],
  rw â† nhds_within_univ at hL,
  exact hL.fderiv_within_eq unique_diff_within_at_univ A
end

end congr

section id
/-! ### Derivative of the identity -/

theorem has_strict_fderiv_at_id (x : E) :
  has_strict_fderiv_at id (id ğ•œ E) x :=
(is_o_zero _ _).congr_left $ by simp

theorem has_fderiv_at_filter_id (x : E) (L : filter E) :
  has_fderiv_at_filter id (id ğ•œ E) x L :=
(is_o_zero _ _).congr_left $ by simp

theorem has_fderiv_within_at_id (x : E) (s : set E) :
  has_fderiv_within_at id (id ğ•œ E) s x :=
has_fderiv_at_filter_id _ _

theorem has_fderiv_at_id (x : E) : has_fderiv_at id (id ğ•œ E) x :=
has_fderiv_at_filter_id _ _

@[simp] lemma differentiable_at_id : differentiable_at ğ•œ id x :=
(has_fderiv_at_id x).differentiable_at

@[simp] lemma differentiable_at_id' : differentiable_at ğ•œ (Î» x, x) x :=
(has_fderiv_at_id x).differentiable_at

lemma differentiable_within_at_id : differentiable_within_at ğ•œ id s x :=
differentiable_at_id.differentiable_within_at

@[simp] lemma differentiable_id : differentiable ğ•œ (id : E â†’ E) :=
Î»x, differentiable_at_id

@[simp] lemma differentiable_id' : differentiable ğ•œ (Î» (x : E), x) :=
Î»x, differentiable_at_id

lemma differentiable_on_id : differentiable_on ğ•œ id s :=
differentiable_id.differentiable_on

lemma fderiv_id : fderiv ğ•œ id x = id ğ•œ E :=
has_fderiv_at.fderiv (has_fderiv_at_id x)

@[simp] lemma fderiv_id' : fderiv ğ•œ (Î» (x : E), x) x = continuous_linear_map.id ğ•œ E :=
fderiv_id

lemma fderiv_within_id (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ id s x = id ğ•œ E :=
begin
  rw differentiable_at.fderiv_within (differentiable_at_id) hxs,
  exact fderiv_id
end

lemma fderiv_within_id' (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î» (x : E), x) s x = continuous_linear_map.id ğ•œ E :=
fderiv_within_id hxs

end id

section const
/-! ### derivative of a constant function -/

theorem has_strict_fderiv_at_const (c : F) (x : E) :
  has_strict_fderiv_at (Î» _, c) (0 : E â†’L[ğ•œ] F) x :=
(is_o_zero _ _).congr_left $ Î» _, by simp only [zero_apply, sub_self]

theorem has_fderiv_at_filter_const (c : F) (x : E) (L : filter E) :
  has_fderiv_at_filter (Î» x, c) (0 : E â†’L[ğ•œ] F) x L :=
(is_o_zero _ _).congr_left $ Î» _, by simp only [zero_apply, sub_self]

theorem has_fderiv_within_at_const (c : F) (x : E) (s : set E) :
  has_fderiv_within_at (Î» x, c) (0 : E â†’L[ğ•œ] F) s x :=
has_fderiv_at_filter_const _ _ _

theorem has_fderiv_at_const (c : F) (x : E) :
  has_fderiv_at (Î» x, c) (0 : E â†’L[ğ•œ] F) x :=
has_fderiv_at_filter_const _ _ _

@[simp] lemma differentiable_at_const (c : F) : differentiable_at ğ•œ (Î»x, c) x :=
âŸ¨0, has_fderiv_at_const c xâŸ©

lemma differentiable_within_at_const (c : F) : differentiable_within_at ğ•œ (Î»x, c) s x :=
differentiable_at.differentiable_within_at (differentiable_at_const _)

lemma fderiv_const_apply (c : F) : fderiv ğ•œ (Î»y, c) x = 0 :=
has_fderiv_at.fderiv (has_fderiv_at_const c x)

@[simp] lemma fderiv_const (c : F) : fderiv ğ•œ (Î» (y : E), c) = 0 :=
by { ext m, rw fderiv_const_apply, refl }

lemma fderiv_within_const_apply (c : F) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»y, c) s x = 0 :=
begin
  rw differentiable_at.fderiv_within (differentiable_at_const _) hxs,
  exact fderiv_const_apply _
end

@[simp] lemma differentiable_const (c : F) : differentiable ğ•œ (Î»x : E, c) :=
Î»x, differentiable_at_const _

lemma differentiable_on_const (c : F) : differentiable_on ğ•œ (Î»x, c) s :=
(differentiable_const _).differentiable_on

end const

section continuous_linear_map
/-!
### Continuous linear maps

There are currently two variants of these in mathlib, the bundled version
(named `continuous_linear_map`, and denoted `E â†’L[ğ•œ] F`), and the unbundled version (with a
predicate `is_bounded_linear_map`). We give statements for both versions. -/

protected theorem continuous_linear_map.has_strict_fderiv_at {x : E} :
  has_strict_fderiv_at e e x :=
(is_o_zero _ _).congr_left $ Î» x, by simp only [e.map_sub, sub_self]

protected lemma continuous_linear_map.has_fderiv_at_filter :
  has_fderiv_at_filter e e x L :=
(is_o_zero _ _).congr_left $ Î» x, by simp only [e.map_sub, sub_self]

protected lemma continuous_linear_map.has_fderiv_within_at : has_fderiv_within_at e e s x :=
e.has_fderiv_at_filter

protected lemma continuous_linear_map.has_fderiv_at : has_fderiv_at e e x :=
e.has_fderiv_at_filter

@[simp] protected lemma continuous_linear_map.differentiable_at : differentiable_at ğ•œ e x :=
e.has_fderiv_at.differentiable_at

protected lemma continuous_linear_map.differentiable_within_at : differentiable_within_at ğ•œ e s x :=
e.differentiable_at.differentiable_within_at

@[simp] protected lemma continuous_linear_map.fderiv : fderiv ğ•œ e x = e :=
e.has_fderiv_at.fderiv

protected lemma continuous_linear_map.fderiv_within (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ e s x = e :=
begin
  rw differentiable_at.fderiv_within e.differentiable_at hxs,
  exact e.fderiv
end

@[simp] protected lemma continuous_linear_map.differentiable : differentiable ğ•œ e :=
Î»x, e.differentiable_at

protected lemma continuous_linear_map.differentiable_on : differentiable_on ğ•œ e s :=
e.differentiable.differentiable_on

lemma is_bounded_linear_map.has_fderiv_at_filter (h : is_bounded_linear_map ğ•œ f) :
  has_fderiv_at_filter f h.to_continuous_linear_map x L :=
h.to_continuous_linear_map.has_fderiv_at_filter

lemma is_bounded_linear_map.has_fderiv_within_at (h : is_bounded_linear_map ğ•œ f) :
  has_fderiv_within_at f h.to_continuous_linear_map s x :=
h.has_fderiv_at_filter

lemma is_bounded_linear_map.has_fderiv_at (h : is_bounded_linear_map ğ•œ f) :
  has_fderiv_at f h.to_continuous_linear_map x  :=
h.has_fderiv_at_filter

lemma is_bounded_linear_map.differentiable_at (h : is_bounded_linear_map ğ•œ f) :
  differentiable_at ğ•œ f x :=
h.has_fderiv_at.differentiable_at

lemma is_bounded_linear_map.differentiable_within_at (h : is_bounded_linear_map ğ•œ f) :
  differentiable_within_at ğ•œ f s x :=
h.differentiable_at.differentiable_within_at

lemma is_bounded_linear_map.fderiv (h : is_bounded_linear_map ğ•œ f) :
  fderiv ğ•œ f x = h.to_continuous_linear_map :=
has_fderiv_at.fderiv (h.has_fderiv_at)

lemma is_bounded_linear_map.fderiv_within (h : is_bounded_linear_map ğ•œ f)
  (hxs : unique_diff_within_at ğ•œ s x) : fderiv_within ğ•œ f s x = h.to_continuous_linear_map :=
begin
  rw differentiable_at.fderiv_within h.differentiable_at hxs,
  exact h.fderiv
end

lemma is_bounded_linear_map.differentiable (h : is_bounded_linear_map ğ•œ f) :
  differentiable ğ•œ f :=
Î»x, h.differentiable_at

lemma is_bounded_linear_map.differentiable_on (h : is_bounded_linear_map ğ•œ f) :
  differentiable_on ğ•œ f s :=
h.differentiable.differentiable_on

end continuous_linear_map

section analytic

variables {p : formal_multilinear_series ğ•œ E F} {r : â„â‰¥0âˆ}

lemma has_fpower_series_at.has_strict_fderiv_at (h : has_fpower_series_at f p x) :
  has_strict_fderiv_at f (continuous_multilinear_curry_fin1 ğ•œ E F (p 1)) x :=
begin
  refine h.is_O_image_sub_norm_mul_norm_sub.trans_is_o (is_o.of_norm_right _),
  refine is_o_iff_exists_eq_mul.2 âŸ¨Î» y, âˆ¥y - (x, x)âˆ¥, _, eventually_eq.rflâŸ©,
  refine (continuous_id.sub continuous_const).norm.tendsto' _ _ _,
  rw [_root_.id, sub_self, norm_zero]
end

lemma has_fpower_series_at.has_fderiv_at (h : has_fpower_series_at f p x) :
  has_fderiv_at f (continuous_multilinear_curry_fin1 ğ•œ E F (p 1)) x :=
h.has_strict_fderiv_at.has_fderiv_at

lemma has_fpower_series_at.differentiable_at (h : has_fpower_series_at f p x) :
  differentiable_at ğ•œ f x :=
h.has_fderiv_at.differentiable_at

lemma analytic_at.differentiable_at : analytic_at ğ•œ f x â†’ differentiable_at ğ•œ f x
| âŸ¨p, hpâŸ© := hp.differentiable_at

lemma analytic_at.differentiable_within_at (h : analytic_at ğ•œ f x) :
  differentiable_within_at ğ•œ f s x :=
h.differentiable_at.differentiable_within_at

lemma has_fpower_series_at.fderiv (h : has_fpower_series_at f p x) :
  fderiv ğ•œ f x = continuous_multilinear_curry_fin1 ğ•œ E F (p 1) :=
h.has_fderiv_at.fderiv

lemma has_fpower_series_on_ball.differentiable_on [complete_space F]
  (h : has_fpower_series_on_ball f p x r) :
  differentiable_on ğ•œ f (emetric.ball x r) :=
Î» y hy, (h.analytic_at_of_mem hy).differentiable_within_at

end analytic

section composition
/-!
### Derivative of the composition of two functions

For composition lemmas, we put x explicit to help the elaborator, as otherwise Lean tends to
get confused since there are too many possibilities for composition -/

variable (x)

theorem has_fderiv_at_filter.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at_filter g g' (f x) (L.map f))
  (hf : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (g âˆ˜ f) (g'.comp f') x L :=
let eqâ‚ := (g'.is_O_comp _ _).trans_is_o hf in
let eqâ‚‚ := (hg.comp_tendsto tendsto_map).trans_is_O hf.is_O_sub in
by { refine eqâ‚‚.triangle (eqâ‚.congr_left (Î» x', _)), simp }

/- A readable version of the previous theorem,
   a general form of the chain rule. -/

example {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at_filter g g' (f x) (L.map f))
  (hf : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (g âˆ˜ f) (g'.comp f') x L :=
begin
  unfold has_fderiv_at_filter at hg,
  have : is_o (Î» x', g (f x') - g (f x) - g' (f x' - f x)) (Î» x', f x' - f x) L,
    from hg.comp_tendsto (le_refl _),
  have eqâ‚ : is_o (Î» x', g (f x') - g (f x) - g' (f x' - f x)) (Î» x', x' - x) L,
    from this.trans_is_O hf.is_O_sub,
  have eqâ‚‚ : is_o (Î» x', f x' - f x - f' (x' - x)) (Î» x', x' - x) L,
    from hf,
  have : is_O
    (Î» x', g' (f x' - f x - f' (x' - x))) (Î» x', f x' - f x - f' (x' - x)) L,
    from g'.is_O_comp _ _,
  have : is_o (Î» x', g' (f x' - f x - f' (x' - x))) (Î» x', x' - x) L,
    from this.trans_is_o eqâ‚‚,
  have eqâ‚ƒ : is_o (Î» x', g' (f x' - f x) - (g' (f' (x' - x)))) (Î» x', x' - x) L,
    by { refine this.congr_left _, simp},
  exact eqâ‚.triangle eqâ‚ƒ
end

theorem has_fderiv_within_at.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G} {t : set F}
  (hg : has_fderiv_within_at g g' t (f x)) (hf : has_fderiv_within_at f f' s x)
  (hst : s âŠ† f â»Â¹' t) :
  has_fderiv_within_at (g âˆ˜ f) (g'.comp f') s x :=
begin
  apply has_fderiv_at_filter.comp _ (has_fderiv_at_filter.mono hg _) hf,
  calc map f (ğ“[s] x)
      â‰¤ ğ“[f '' s] (f x) : hf.continuous_within_at.tendsto_nhds_within_image
  ... â‰¤ ğ“[t] (f x)        : nhds_within_mono _ (image_subset_iff.mpr hst)
end

/-- The chain rule. -/
theorem has_fderiv_at.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at g g' (f x)) (hf : has_fderiv_at f f' x) :
  has_fderiv_at (g âˆ˜ f) (g'.comp f') x :=
(hg.mono hf.continuous_at).comp x hf

theorem has_fderiv_at.comp_has_fderiv_within_at {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_fderiv_at g g' (f x)) (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (g âˆ˜ f) (g'.comp f') s x :=
begin
  rw â† has_fderiv_within_at_univ at hg,
  exact has_fderiv_within_at.comp x hg hf subset_preimage_univ
end

lemma differentiable_within_at.comp {g : F â†’ G} {t : set F}
  (hg : differentiable_within_at ğ•œ g t (f x)) (hf : differentiable_within_at ğ•œ f s x)
  (h : s âŠ† f â»Â¹' t) : differentiable_within_at ğ•œ (g âˆ˜ f) s x :=
begin
  rcases hf with âŸ¨f', hf'âŸ©,
  rcases hg with âŸ¨g', hg'âŸ©,
  exact âŸ¨continuous_linear_map.comp g' f', hg'.comp x hf' hâŸ©
end

lemma differentiable_within_at.comp' {g : F â†’ G} {t : set F}
  (hg : differentiable_within_at ğ•œ g t (f x)) (hf : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (g âˆ˜ f) (s âˆ© fâ»Â¹' t) x :=
hg.comp x (hf.mono (inter_subset_left _ _)) (inter_subset_right _ _)

lemma differentiable_at.comp {g : F â†’ G}
  (hg : differentiable_at ğ•œ g (f x)) (hf : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (g âˆ˜ f) x :=
(hg.has_fderiv_at.comp x hf.has_fderiv_at).differentiable_at

lemma differentiable_at.comp_differentiable_within_at {g : F â†’ G}
  (hg : differentiable_at ğ•œ g (f x)) (hf : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (g âˆ˜ f) s x :=
(differentiable_within_at_univ.2 hg).comp x hf (by simp)

lemma fderiv_within.comp {g : F â†’ G} {t : set F}
  (hg : differentiable_within_at ğ•œ g t (f x)) (hf : differentiable_within_at ğ•œ f s x)
  (h : maps_to f s t) (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (g âˆ˜ f) s x = (fderiv_within ğ•œ g t (f x)).comp (fderiv_within ğ•œ f s x) :=
begin
  apply has_fderiv_within_at.fderiv_within _ hxs,
  exact has_fderiv_within_at.comp x (hg.has_fderiv_within_at) (hf.has_fderiv_within_at) h
end

lemma fderiv.comp {g : F â†’ G}
  (hg : differentiable_at ğ•œ g (f x)) (hf : differentiable_at ğ•œ f x) :
  fderiv ğ•œ (g âˆ˜ f) x = (fderiv ğ•œ g (f x)).comp (fderiv ğ•œ f x) :=
begin
  apply has_fderiv_at.fderiv,
  exact has_fderiv_at.comp x hg.has_fderiv_at hf.has_fderiv_at
end

lemma fderiv.comp_fderiv_within {g : F â†’ G}
  (hg : differentiable_at ğ•œ g (f x)) (hf : differentiable_within_at ğ•œ f s x)
  (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (g âˆ˜ f) s x = (fderiv ğ•œ g (f x)).comp (fderiv_within ğ•œ f s x) :=
begin
  apply has_fderiv_within_at.fderiv_within _ hxs,
  exact has_fderiv_at.comp_has_fderiv_within_at x (hg.has_fderiv_at) (hf.has_fderiv_within_at)
end

lemma differentiable_on.comp {g : F â†’ G} {t : set F}
  (hg : differentiable_on ğ•œ g t) (hf : differentiable_on ğ•œ f s) (st : s âŠ† f â»Â¹' t) :
  differentiable_on ğ•œ (g âˆ˜ f) s :=
Î»x hx, differentiable_within_at.comp x (hg (f x) (st hx)) (hf x hx) st

lemma differentiable.comp {g : F â†’ G} (hg : differentiable ğ•œ g) (hf : differentiable ğ•œ f) :
  differentiable ğ•œ (g âˆ˜ f) :=
Î»x, differentiable_at.comp x (hg (f x)) (hf x)

lemma differentiable.comp_differentiable_on {g : F â†’ G} (hg : differentiable ğ•œ g)
  (hf : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (g âˆ˜ f) s :=
(differentiable_on_univ.2 hg).comp hf (by simp)

/-- The chain rule for derivatives in the sense of strict differentiability. -/
protected lemma has_strict_fderiv_at.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
  (hg : has_strict_fderiv_at g g' (f x)) (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Î» x, g (f x)) (g'.comp f') x :=
((hg.comp_tendsto (hf.continuous_at.prod_map' hf.continuous_at)).trans_is_O hf.is_O_sub).triangle $
  by simpa only [g'.map_sub, f'.coe_comp'] using (g'.is_O_comp _ _).trans_is_o hf

protected lemma differentiable.iterate {f : E â†’ E} (hf : differentiable ğ•œ f) (n : â„•) :
  differentiable ğ•œ (f^[n]) :=
nat.rec_on n differentiable_id (Î» n ihn, ihn.comp hf)

protected lemma differentiable_on.iterate {f : E â†’ E} (hf : differentiable_on ğ•œ f s)
  (hs : maps_to f s s) (n : â„•) :
  differentiable_on ğ•œ (f^[n]) s :=
nat.rec_on n differentiable_on_id (Î» n ihn, ihn.comp hf hs)

variable {x}

protected lemma has_fderiv_at_filter.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E}
  (hf : has_fderiv_at_filter f f' x L) (hL : tendsto f L L) (hx : f x = x) (n : â„•) :
  has_fderiv_at_filter (f^[n]) (f'^n) x L :=
begin
  induction n with n ihn,
  { exact has_fderiv_at_filter_id x L },
  { change has_fderiv_at_filter (f^[n] âˆ˜ f) (f'^(n+1)) x L,
    rw [pow_succ'],
    refine has_fderiv_at_filter.comp x _ hf,
    rw hx,
    exact ihn.mono hL }
end

protected lemma has_fderiv_at.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E}
  (hf : has_fderiv_at f f' x) (hx : f x = x) (n : â„•) :
  has_fderiv_at (f^[n]) (f'^n) x :=
begin
  refine hf.iterate _ hx n,
  convert hf.continuous_at,
  exact hx.symm
end

protected lemma has_fderiv_within_at.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E}
  (hf : has_fderiv_within_at f f' s x) (hx : f x = x) (hs : maps_to f s s) (n : â„•) :
  has_fderiv_within_at (f^[n]) (f'^n) s x :=
begin
  refine hf.iterate _ hx n,
  convert tendsto_inf.2 âŸ¨hf.continuous_within_at, _âŸ©,
  exacts [hx.symm, (tendsto_principal_principal.2 hs).mono_left inf_le_right]
end

protected lemma has_strict_fderiv_at.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E}
  (hf : has_strict_fderiv_at f f' x) (hx : f x = x) (n : â„•) :
  has_strict_fderiv_at (f^[n]) (f'^n) x :=
begin
  induction n with n ihn,
  { exact has_strict_fderiv_at_id x },
  { change has_strict_fderiv_at (f^[n] âˆ˜ f) (f'^(n+1)) x,
    rw [pow_succ'],
    refine has_strict_fderiv_at.comp x _ hf,
    rwa hx }
end

protected lemma differentiable_at.iterate {f : E â†’ E} (hf : differentiable_at ğ•œ f x)
  (hx : f x = x) (n : â„•) :
  differentiable_at ğ•œ (f^[n]) x :=
exists.elim hf $ Î» f' hf, (hf.iterate hx n).differentiable_at

protected lemma differentiable_within_at.iterate {f : E â†’ E} (hf : differentiable_within_at ğ•œ f s x)
  (hx : f x = x) (hs : maps_to f s s) (n : â„•) :
  differentiable_within_at ğ•œ (f^[n]) s x :=
exists.elim hf $ Î» f' hf, (hf.iterate hx hs n).differentiable_within_at

end composition

section cartesian_product
/-! ### Derivative of the cartesian product of two functions -/

section prod
variables {fâ‚‚ : E â†’ G} {fâ‚‚' : E â†’L[ğ•œ] G}

protected lemma has_strict_fderiv_at.prod
  (hfâ‚ : has_strict_fderiv_at fâ‚ fâ‚' x) (hfâ‚‚ : has_strict_fderiv_at fâ‚‚ fâ‚‚' x) :
  has_strict_fderiv_at (Î»x, (fâ‚ x, fâ‚‚ x)) (fâ‚'.prod fâ‚‚') x :=
hfâ‚.prod_left hfâ‚‚

lemma has_fderiv_at_filter.prod
  (hfâ‚ : has_fderiv_at_filter fâ‚ fâ‚' x L) (hfâ‚‚ : has_fderiv_at_filter fâ‚‚ fâ‚‚' x L) :
  has_fderiv_at_filter (Î»x, (fâ‚ x, fâ‚‚ x)) (fâ‚'.prod fâ‚‚') x L :=
hfâ‚.prod_left hfâ‚‚

lemma has_fderiv_within_at.prod
  (hfâ‚ : has_fderiv_within_at fâ‚ fâ‚' s x) (hfâ‚‚ : has_fderiv_within_at fâ‚‚ fâ‚‚' s x) :
  has_fderiv_within_at (Î»x, (fâ‚ x, fâ‚‚ x)) (fâ‚'.prod fâ‚‚') s x :=
hfâ‚.prod hfâ‚‚

lemma has_fderiv_at.prod (hfâ‚ : has_fderiv_at fâ‚ fâ‚' x) (hfâ‚‚ : has_fderiv_at fâ‚‚ fâ‚‚' x) :
  has_fderiv_at (Î»x, (fâ‚ x, fâ‚‚ x)) (continuous_linear_map.prod fâ‚' fâ‚‚') x :=
hfâ‚.prod hfâ‚‚

lemma differentiable_within_at.prod
  (hfâ‚ : differentiable_within_at ğ•œ fâ‚ s x) (hfâ‚‚ : differentiable_within_at ğ•œ fâ‚‚ s x) :
  differentiable_within_at ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) s x :=
(hfâ‚.has_fderiv_within_at.prod hfâ‚‚.has_fderiv_within_at).differentiable_within_at

@[simp]
lemma differentiable_at.prod (hfâ‚ : differentiable_at ğ•œ fâ‚ x) (hfâ‚‚ : differentiable_at ğ•œ fâ‚‚ x) :
  differentiable_at ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) x :=
(hfâ‚.has_fderiv_at.prod hfâ‚‚.has_fderiv_at).differentiable_at

lemma differentiable_on.prod (hfâ‚ : differentiable_on ğ•œ fâ‚ s) (hfâ‚‚ : differentiable_on ğ•œ fâ‚‚ s) :
  differentiable_on ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) s :=
Î»x hx, differentiable_within_at.prod (hfâ‚ x hx) (hfâ‚‚ x hx)

@[simp]
lemma differentiable.prod (hfâ‚ : differentiable ğ•œ fâ‚) (hfâ‚‚ : differentiable ğ•œ fâ‚‚) :
  differentiable ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) :=
Î» x, differentiable_at.prod (hfâ‚ x) (hfâ‚‚ x)

lemma differentiable_at.fderiv_prod
  (hfâ‚ : differentiable_at ğ•œ fâ‚ x) (hfâ‚‚ : differentiable_at ğ•œ fâ‚‚ x) :
  fderiv ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) x = (fderiv ğ•œ fâ‚ x).prod (fderiv ğ•œ fâ‚‚ x) :=
(hfâ‚.has_fderiv_at.prod hfâ‚‚.has_fderiv_at).fderiv

lemma differentiable_at.fderiv_within_prod
  (hfâ‚ : differentiable_within_at ğ•œ fâ‚ s x) (hfâ‚‚ : differentiable_within_at ğ•œ fâ‚‚ s x)
  (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»x:E, (fâ‚ x, fâ‚‚ x)) s x =
    (fderiv_within ğ•œ fâ‚ s x).prod (fderiv_within ğ•œ fâ‚‚ s x) :=
begin
  apply has_fderiv_within_at.fderiv_within _ hxs,
  exact has_fderiv_within_at.prod hfâ‚.has_fderiv_within_at hfâ‚‚.has_fderiv_within_at
end

end prod

section fst

variables {fâ‚‚ : E â†’ F Ã— G} {fâ‚‚' : E â†’L[ğ•œ] F Ã— G} {p : E Ã— F}

lemma has_strict_fderiv_at_fst : has_strict_fderiv_at (fst ğ•œ E F) (fst ğ•œ E F) p :=
(fst ğ•œ E F).has_strict_fderiv_at

protected lemma has_strict_fderiv_at.fst (h : has_strict_fderiv_at fâ‚‚ fâ‚‚' x) :
  has_strict_fderiv_at (Î» x, (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') x :=
has_strict_fderiv_at_fst.comp x h

lemma has_fderiv_at_filter_fst {L : filter (E Ã— F)} :
  has_fderiv_at_filter (fst ğ•œ E F) (fst ğ•œ E F) p L :=
(fst ğ•œ E F).has_fderiv_at_filter

protected lemma has_fderiv_at_filter.fst (h : has_fderiv_at_filter fâ‚‚ fâ‚‚' x L) :
  has_fderiv_at_filter (Î» x, (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') x L :=
has_fderiv_at_filter_fst.comp x h

lemma has_fderiv_at_fst : has_fderiv_at (fst ğ•œ E F) (fst ğ•œ E F) p :=
has_fderiv_at_filter_fst

protected lemma has_fderiv_at.fst (h : has_fderiv_at fâ‚‚ fâ‚‚' x) :
  has_fderiv_at (Î» x, (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') x :=
h.fst

lemma has_fderiv_within_at_fst {s : set (E Ã— F)} :
  has_fderiv_within_at (fst ğ•œ E F) (fst ğ•œ E F) s p :=
has_fderiv_at_filter_fst

protected lemma has_fderiv_within_at.fst (h : has_fderiv_within_at fâ‚‚ fâ‚‚' s x) :
  has_fderiv_within_at (Î» x, (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') s x :=
h.fst

lemma differentiable_at_fst : differentiable_at ğ•œ prod.fst p :=
has_fderiv_at_fst.differentiable_at

@[simp] protected lemma differentiable_at.fst (h : differentiable_at ğ•œ fâ‚‚ x) :
  differentiable_at ğ•œ (Î» x, (fâ‚‚ x).1) x :=
differentiable_at_fst.comp x h

lemma differentiable_fst : differentiable ğ•œ (prod.fst : E Ã— F â†’ E) :=
Î» x, differentiable_at_fst

@[simp] protected lemma differentiable.fst (h : differentiable ğ•œ fâ‚‚) :
  differentiable ğ•œ (Î» x, (fâ‚‚ x).1) :=
differentiable_fst.comp h

lemma differentiable_within_at_fst {s : set (E Ã— F)} : differentiable_within_at ğ•œ prod.fst s p :=
differentiable_at_fst.differentiable_within_at

protected lemma differentiable_within_at.fst (h : differentiable_within_at ğ•œ fâ‚‚ s x) :
  differentiable_within_at ğ•œ (Î» x, (fâ‚‚ x).1) s x :=
differentiable_at_fst.comp_differentiable_within_at x h

lemma differentiable_on_fst {s : set (E Ã— F)} : differentiable_on ğ•œ prod.fst s :=
differentiable_fst.differentiable_on

protected lemma differentiable_on.fst (h : differentiable_on ğ•œ fâ‚‚ s) :
  differentiable_on ğ•œ (Î» x, (fâ‚‚ x).1) s :=
differentiable_fst.comp_differentiable_on h

lemma fderiv_fst : fderiv ğ•œ prod.fst p = fst ğ•œ E F := has_fderiv_at_fst.fderiv

lemma fderiv.fst (h : differentiable_at ğ•œ fâ‚‚ x) :
  fderiv ğ•œ (Î» x, (fâ‚‚ x).1) x = (fst ğ•œ F G).comp (fderiv ğ•œ fâ‚‚ x) :=
h.has_fderiv_at.fst.fderiv

lemma fderiv_within_fst {s : set (E Ã— F)} (hs : unique_diff_within_at ğ•œ s p) :
  fderiv_within ğ•œ prod.fst s p = fst ğ•œ E F :=
has_fderiv_within_at_fst.fderiv_within hs

lemma fderiv_within.fst (hs : unique_diff_within_at ğ•œ s x) (h : differentiable_within_at ğ•œ fâ‚‚ s x) :
  fderiv_within ğ•œ (Î» x, (fâ‚‚ x).1) s x = (fst ğ•œ F G).comp (fderiv_within ğ•œ fâ‚‚ s x) :=
h.has_fderiv_within_at.fst.fderiv_within hs

end fst

section snd

variables {fâ‚‚ : E â†’ F Ã— G} {fâ‚‚' : E â†’L[ğ•œ] F Ã— G} {p : E Ã— F}

lemma has_strict_fderiv_at_snd : has_strict_fderiv_at (@prod.snd E F) (snd ğ•œ E F) p :=
(snd ğ•œ E F).has_strict_fderiv_at

protected lemma has_strict_fderiv_at.snd (h : has_strict_fderiv_at fâ‚‚ fâ‚‚' x) :
  has_strict_fderiv_at (Î» x, (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') x :=
has_strict_fderiv_at_snd.comp x h

lemma has_fderiv_at_filter_snd {L : filter (E Ã— F)} :
  has_fderiv_at_filter (@prod.snd E F) (snd ğ•œ E F) p L :=
(snd ğ•œ E F).has_fderiv_at_filter

protected lemma has_fderiv_at_filter.snd (h : has_fderiv_at_filter fâ‚‚ fâ‚‚' x L) :
  has_fderiv_at_filter (Î» x, (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') x L :=
has_fderiv_at_filter_snd.comp x h

lemma has_fderiv_at_snd : has_fderiv_at (@prod.snd E F) (snd ğ•œ E F) p :=
has_fderiv_at_filter_snd

protected lemma has_fderiv_at.snd (h : has_fderiv_at fâ‚‚ fâ‚‚' x) :
  has_fderiv_at (Î» x, (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') x :=
h.snd

lemma has_fderiv_within_at_snd {s : set (E Ã— F)} :
  has_fderiv_within_at (@prod.snd E F) (snd ğ•œ E F) s p :=
has_fderiv_at_filter_snd

protected lemma has_fderiv_within_at.snd (h : has_fderiv_within_at fâ‚‚ fâ‚‚' s x) :
  has_fderiv_within_at (Î» x, (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') s x :=
h.snd

lemma differentiable_at_snd : differentiable_at ğ•œ prod.snd p :=
has_fderiv_at_snd.differentiable_at

@[simp] protected lemma differentiable_at.snd (h : differentiable_at ğ•œ fâ‚‚ x) :
  differentiable_at ğ•œ (Î» x, (fâ‚‚ x).2) x :=
differentiable_at_snd.comp x h

lemma differentiable_snd : differentiable ğ•œ (prod.snd : E Ã— F â†’ F) :=
Î» x, differentiable_at_snd

@[simp] protected lemma differentiable.snd (h : differentiable ğ•œ fâ‚‚) :
  differentiable ğ•œ (Î» x, (fâ‚‚ x).2) :=
differentiable_snd.comp h

lemma differentiable_within_at_snd {s : set (E Ã— F)} : differentiable_within_at ğ•œ prod.snd s p :=
differentiable_at_snd.differentiable_within_at

protected lemma differentiable_within_at.snd (h : differentiable_within_at ğ•œ fâ‚‚ s x) :
  differentiable_within_at ğ•œ (Î» x, (fâ‚‚ x).2) s x :=
differentiable_at_snd.comp_differentiable_within_at x h

lemma differentiable_on_snd {s : set (E Ã— F)} : differentiable_on ğ•œ prod.snd s :=
differentiable_snd.differentiable_on

protected lemma differentiable_on.snd (h : differentiable_on ğ•œ fâ‚‚ s) :
  differentiable_on ğ•œ (Î» x, (fâ‚‚ x).2) s :=
differentiable_snd.comp_differentiable_on h

lemma fderiv_snd : fderiv ğ•œ prod.snd p = snd ğ•œ E F := has_fderiv_at_snd.fderiv

lemma fderiv.snd (h : differentiable_at ğ•œ fâ‚‚ x) :
  fderiv ğ•œ (Î» x, (fâ‚‚ x).2) x = (snd ğ•œ F G).comp (fderiv ğ•œ fâ‚‚ x) :=
h.has_fderiv_at.snd.fderiv

lemma fderiv_within_snd {s : set (E Ã— F)} (hs : unique_diff_within_at ğ•œ s p) :
  fderiv_within ğ•œ prod.snd s p = snd ğ•œ E F :=
has_fderiv_within_at_snd.fderiv_within hs

lemma fderiv_within.snd (hs : unique_diff_within_at ğ•œ s x) (h : differentiable_within_at ğ•œ fâ‚‚ s x) :
  fderiv_within ğ•œ (Î» x, (fâ‚‚ x).2) s x = (snd ğ•œ F G).comp (fderiv_within ğ•œ fâ‚‚ s x) :=
h.has_fderiv_within_at.snd.fderiv_within hs

end snd

section prod_map

variables {fâ‚‚ : G â†’ G'} {fâ‚‚' : G â†’L[ğ•œ] G'} {y : G} (p : E Ã— G)

-- TODO (Lean 3.8): use `prod.map f fâ‚‚``

protected theorem has_strict_fderiv_at.prod_map (hf : has_strict_fderiv_at f f' p.1)
  (hfâ‚‚ : has_strict_fderiv_at fâ‚‚ fâ‚‚' p.2) :
  has_strict_fderiv_at (Î» p : E Ã— G, (f p.1, fâ‚‚ p.2)) (f'.prod_map fâ‚‚') p :=
(hf.comp p has_strict_fderiv_at_fst).prod (hfâ‚‚.comp p has_strict_fderiv_at_snd)

protected theorem has_fderiv_at.prod_map (hf : has_fderiv_at f f' p.1)
  (hfâ‚‚ : has_fderiv_at fâ‚‚ fâ‚‚' p.2) :
  has_fderiv_at (Î» p : E Ã— G, (f p.1, fâ‚‚ p.2)) (f'.prod_map fâ‚‚') p :=
(hf.comp p has_fderiv_at_fst).prod (hfâ‚‚.comp p has_fderiv_at_snd)

@[simp] protected theorem differentiable_at.prod_map (hf : differentiable_at ğ•œ f p.1)
  (hfâ‚‚ : differentiable_at ğ•œ fâ‚‚ p.2) :
  differentiable_at ğ•œ (Î» p : E Ã— G, (f p.1, fâ‚‚ p.2)) p :=
(hf.comp p differentiable_at_fst).prod (hfâ‚‚.comp p differentiable_at_snd)

end prod_map

end cartesian_product

section const_smul
/-! ### Derivative of a function multiplied by a constant -/

theorem has_strict_fderiv_at.const_smul (h : has_strict_fderiv_at f f' x) (c : ğ•œ) :
  has_strict_fderiv_at (Î» x, c â€¢ f x) (c â€¢ f') x :=
(c â€¢ (1 : F â†’L[ğ•œ] F)).has_strict_fderiv_at.comp x h

theorem has_fderiv_at_filter.const_smul (h : has_fderiv_at_filter f f' x L) (c : ğ•œ) :
  has_fderiv_at_filter (Î» x, c â€¢ f x) (c â€¢ f') x L :=
(c â€¢ (1 : F â†’L[ğ•œ] F)).has_fderiv_at_filter.comp x h

theorem has_fderiv_within_at.const_smul (h : has_fderiv_within_at f f' s x) (c : ğ•œ) :
  has_fderiv_within_at (Î» x, c â€¢ f x) (c â€¢ f') s x :=
h.const_smul c

theorem has_fderiv_at.const_smul (h : has_fderiv_at f f' x) (c : ğ•œ) :
  has_fderiv_at (Î» x, c â€¢ f x) (c â€¢ f') x :=
h.const_smul c

lemma differentiable_within_at.const_smul (h : differentiable_within_at ğ•œ f s x) (c : ğ•œ) :
  differentiable_within_at ğ•œ (Î»y, c â€¢ f y) s x :=
(h.has_fderiv_within_at.const_smul c).differentiable_within_at

lemma differentiable_at.const_smul (h : differentiable_at ğ•œ f x) (c : ğ•œ) :
  differentiable_at ğ•œ (Î»y, c â€¢ f y) x :=
(h.has_fderiv_at.const_smul c).differentiable_at

lemma differentiable_on.const_smul (h : differentiable_on ğ•œ f s) (c : ğ•œ) :
  differentiable_on ğ•œ (Î»y, c â€¢ f y) s :=
Î»x hx, (h x hx).const_smul c

lemma differentiable.const_smul (h : differentiable ğ•œ f) (c : ğ•œ) :
  differentiable ğ•œ (Î»y, c â€¢ f y) :=
Î»x, (h x).const_smul c

lemma fderiv_within_const_smul (hxs : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f s x) (c : ğ•œ) :
  fderiv_within ğ•œ (Î»y, c â€¢ f y) s x = c â€¢ fderiv_within ğ•œ f s x :=
(h.has_fderiv_within_at.const_smul c).fderiv_within hxs

lemma fderiv_const_smul (h : differentiable_at ğ•œ f x) (c : ğ•œ) :
  fderiv ğ•œ (Î»y, c â€¢ f y) x = c â€¢ fderiv ğ•œ f x :=
(h.has_fderiv_at.const_smul c).fderiv

end const_smul

section add
/-! ### Derivative of the sum of two functions -/

theorem has_strict_fderiv_at.add (hf : has_strict_fderiv_at f f' x)
  (hg : has_strict_fderiv_at g g' x) :
  has_strict_fderiv_at (Î» y, f y + g y) (f' + g') x :=
(hf.add hg).congr_left $ Î» y, by simp; abel

theorem has_fderiv_at_filter.add
  (hf : has_fderiv_at_filter f f' x L) (hg : has_fderiv_at_filter g g' x L) :
  has_fderiv_at_filter (Î» y, f y + g y) (f' + g') x L :=
(hf.add hg).congr_left $ Î» _, by simp; abel

theorem has_fderiv_within_at.add
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» y, f y + g y) (f' + g') s x :=
hf.add hg

theorem has_fderiv_at.add
  (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» x, f x + g x) (f' + g') x :=
hf.add hg

lemma differentiable_within_at.add
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  differentiable_within_at ğ•œ (Î» y, f y + g y) s x :=
(hf.has_fderiv_within_at.add hg.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  differentiable_at ğ•œ (Î» y, f y + g y) x :=
(hf.has_fderiv_at.add hg.has_fderiv_at).differentiable_at

lemma differentiable_on.add
  (hf : differentiable_on ğ•œ f s) (hg : differentiable_on ğ•œ g s) :
  differentiable_on ğ•œ (Î»y, f y + g y) s :=
Î»x hx, (hf x hx).add (hg x hx)

@[simp] lemma differentiable.add
  (hf : differentiable ğ•œ f) (hg : differentiable ğ•œ g) :
  differentiable ğ•œ (Î»y, f y + g y) :=
Î»x, (hf x).add (hg x)

lemma fderiv_within_add (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  fderiv_within ğ•œ (Î»y, f y + g y) s x = fderiv_within ğ•œ f s x + fderiv_within ğ•œ g s x :=
(hf.has_fderiv_within_at.add hg.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  fderiv ğ•œ (Î»y, f y + g y) x = fderiv ğ•œ f x + fderiv ğ•œ g x :=
(hf.has_fderiv_at.add hg.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.add_const (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» y, f y + c) f' x :=
add_zero f' â–¸ hf.add (has_strict_fderiv_at_const _ _)

theorem has_fderiv_at_filter.add_const
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» y, f y + c) f' x L :=
add_zero f' â–¸ hf.add (has_fderiv_at_filter_const _ _ _)

theorem has_fderiv_within_at.add_const
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» y, f y + c) f' s x :=
hf.add_const c

theorem has_fderiv_at.add_const (hf : has_fderiv_at f f' x) (c : F):
  has_fderiv_at (Î» x, f x + c) f' x :=
hf.add_const c

lemma differentiable_within_at.add_const
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y + c) s x :=
(hf.has_fderiv_within_at.add_const c).differentiable_within_at

@[simp] lemma differentiable_within_at_add_const_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y + c) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma differentiable_at.add_const
  (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, f y + c) x :=
(hf.has_fderiv_at.add_const c).differentiable_at

@[simp] lemma differentiable_at_add_const_iff (c : F) :
  differentiable_at ğ•œ (Î» y, f y + c) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma differentiable_on.add_const
  (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, f y + c) s :=
Î»x hx, (hf x hx).add_const c

@[simp] lemma differentiable_on_add_const_iff (c : F) :
  differentiable_on ğ•œ (Î» y, f y + c) s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma differentiable.add_const
  (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, f y + c) :=
Î»x, (hf x).add_const c

@[simp] lemma differentiable_add_const_iff (c : F) :
  differentiable ğ•œ (Î» y, f y + c) â†” differentiable ğ•œ f :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma fderiv_within_add_const (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, f y + c) s x = fderiv_within ğ•œ f s x :=
if hf : differentiable_within_at ğ•œ f s x
then (hf.has_fderiv_within_at.add_const c).fderiv_within hxs
else by { rw [fderiv_within_zero_of_not_differentiable_within_at hf,
  fderiv_within_zero_of_not_differentiable_within_at], simpa }

lemma fderiv_add_const (c : F) : fderiv ğ•œ (Î»y, f y + c) x = fderiv ğ•œ f x :=
by simp only [â† fderiv_within_univ, fderiv_within_add_const unique_diff_within_at_univ]

theorem has_strict_fderiv_at.const_add (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» y, c + f y) f' x :=
zero_add f' â–¸ (has_strict_fderiv_at_const _ _).add hf

theorem has_fderiv_at_filter.const_add
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» y, c + f y) f' x L :=
zero_add f' â–¸ (has_fderiv_at_filter_const _ _ _).add hf

theorem has_fderiv_within_at.const_add
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» y, c + f y) f' s x :=
hf.const_add c

theorem has_fderiv_at.const_add
  (hf : has_fderiv_at f f' x) (c : F):
  has_fderiv_at (Î» x, c + f x) f' x :=
hf.const_add c

lemma differentiable_within_at.const_add
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, c + f y) s x :=
(hf.has_fderiv_within_at.const_add c).differentiable_within_at

@[simp] lemma differentiable_within_at_const_add_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, c + f y) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma differentiable_at.const_add
  (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, c + f y) x :=
(hf.has_fderiv_at.const_add c).differentiable_at

@[simp] lemma differentiable_at_const_add_iff (c : F) :
  differentiable_at ğ•œ (Î» y, c + f y) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma differentiable_on.const_add (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, c + f y) s :=
Î»x hx, (hf x hx).const_add c

@[simp] lemma differentiable_on_const_add_iff (c : F) :
  differentiable_on ğ•œ (Î» y, c + f y) s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma differentiable.const_add (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, c + f y) :=
Î»x, (hf x).const_add c

@[simp] lemma differentiable_const_add_iff (c : F) :
  differentiable ğ•œ (Î» y, c + f y) â†” differentiable ğ•œ f :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma fderiv_within_const_add (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, c + f y) s x = fderiv_within ğ•œ f s x :=
by simpa only [add_comm] using fderiv_within_add_const hxs c

lemma fderiv_const_add (c : F) : fderiv ğ•œ (Î»y, c + f y) x = fderiv ğ•œ f x :=
by simp only [add_comm c, fderiv_add_const]

end add

section sum
/-! ### Derivative of a finite sum of functions -/

open_locale big_operators

variables {Î¹ : Type*} {u : finset Î¹} {A : Î¹ â†’ (E â†’ F)} {A' : Î¹ â†’ (E â†’L[ğ•œ] F)}

theorem has_strict_fderiv_at.sum (h : âˆ€ i âˆˆ u, has_strict_fderiv_at (A i) (A' i) x) :
  has_strict_fderiv_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x :=
begin
  dsimp [has_strict_fderiv_at] at *,
  convert is_o.sum h,
  simp [finset.sum_sub_distrib, continuous_linear_map.sum_apply]
end

theorem has_fderiv_at_filter.sum (h : âˆ€ i âˆˆ u, has_fderiv_at_filter (A i) (A' i) x L) :
  has_fderiv_at_filter (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x L :=
begin
  dsimp [has_fderiv_at_filter] at *,
  convert is_o.sum h,
  simp [continuous_linear_map.sum_apply]
end

theorem has_fderiv_within_at.sum (h : âˆ€ i âˆˆ u, has_fderiv_within_at (A i) (A' i) s x) :
  has_fderiv_within_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) s x :=
has_fderiv_at_filter.sum h

theorem has_fderiv_at.sum (h : âˆ€ i âˆˆ u, has_fderiv_at (A i) (A' i) x) :
  has_fderiv_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x :=
has_fderiv_at_filter.sum h

theorem differentiable_within_at.sum (h : âˆ€ i âˆˆ u, differentiable_within_at ğ•œ (A i) s x) :
  differentiable_within_at ğ•œ (Î» y, âˆ‘ i in u, A i y) s x :=
has_fderiv_within_at.differentiable_within_at $ has_fderiv_within_at.sum $
Î» i hi, (h i hi).has_fderiv_within_at

@[simp] theorem differentiable_at.sum (h : âˆ€ i âˆˆ u, differentiable_at ğ•œ (A i) x) :
  differentiable_at ğ•œ (Î» y, âˆ‘ i in u, A i y) x :=
has_fderiv_at.differentiable_at $ has_fderiv_at.sum $ Î» i hi, (h i hi).has_fderiv_at

theorem differentiable_on.sum (h : âˆ€ i âˆˆ u, differentiable_on ğ•œ (A i) s) :
  differentiable_on ğ•œ (Î» y, âˆ‘ i in u, A i y) s :=
Î» x hx, differentiable_within_at.sum $ Î» i hi, h i hi x hx

@[simp] theorem differentiable.sum (h : âˆ€ i âˆˆ u, differentiable ğ•œ (A i)) :
  differentiable ğ•œ (Î» y, âˆ‘ i in u, A i y) :=
Î» x, differentiable_at.sum $ Î» i hi, h i hi x

theorem fderiv_within_sum (hxs : unique_diff_within_at ğ•œ s x)
  (h : âˆ€ i âˆˆ u, differentiable_within_at ğ•œ (A i) s x) :
  fderiv_within ğ•œ (Î» y, âˆ‘ i in u, A i y) s x = (âˆ‘ i in u, fderiv_within ğ•œ (A i) s x) :=
(has_fderiv_within_at.sum (Î» i hi, (h i hi).has_fderiv_within_at)).fderiv_within hxs

theorem fderiv_sum (h : âˆ€ i âˆˆ u, differentiable_at ğ•œ (A i) x) :
  fderiv ğ•œ (Î» y, âˆ‘ i in u, A i y) x = (âˆ‘ i in u, fderiv ğ•œ (A i) x) :=
(has_fderiv_at.sum (Î» i hi, (h i hi).has_fderiv_at)).fderiv

end sum

section pi

/-!
### Derivatives of functions `f : E â†’ Î  i, F' i`

In this section we formulate `has_*fderiv*_pi` theorems as `iff`s, and provide two versions of each
theorem:

* the version without `'` deals with `Ï† : Î  i, E â†’ F' i` and `Ï†' : Î  i, E â†’L[ğ•œ] F' i`
  and is designed to deduce differentiability of `Î» x i, Ï† i x` from differentiability
  of each `Ï† i`;
* the version with `'` deals with `Î¦ : E â†’ Î  i, F' i` and `Î¦' : E â†’L[ğ•œ] Î  i, F' i`
  and is designed to deduce differentiability of the components `Î» x, Î¦ x i` from
  differentiability of `Î¦`.
-/

variables {Î¹ : Type*} [fintype Î¹] {F' : Î¹ â†’ Type*} [Î  i, normed_group (F' i)]
  [Î  i, normed_space ğ•œ (F' i)] {Ï† : Î  i, E â†’ F' i} {Ï†' : Î  i, E â†’L[ğ•œ] F' i}
  {Î¦ : E â†’ Î  i, F' i} {Î¦' : E â†’L[ğ•œ] Î  i, F' i}

@[simp] lemma has_strict_fderiv_at_pi' :
  has_strict_fderiv_at Î¦ Î¦' x â†”
    âˆ€ i, has_strict_fderiv_at (Î» x, Î¦ x i) ((proj i).comp Î¦') x :=
begin
  simp only [has_strict_fderiv_at, continuous_linear_map.coe_pi],
  exact is_o_pi
end

@[simp] lemma has_strict_fderiv_at_pi :
  has_strict_fderiv_at (Î» x i, Ï† i x) (continuous_linear_map.pi Ï†') x â†”
    âˆ€ i, has_strict_fderiv_at (Ï† i) (Ï†' i) x :=
has_strict_fderiv_at_pi'

@[simp] lemma has_fderiv_at_filter_pi' :
  has_fderiv_at_filter Î¦ Î¦' x L â†”
    âˆ€ i, has_fderiv_at_filter (Î» x, Î¦ x i) ((proj i).comp Î¦') x L :=
begin
  simp only [has_fderiv_at_filter, continuous_linear_map.coe_pi],
  exact is_o_pi
end

lemma has_fderiv_at_filter_pi :
  has_fderiv_at_filter (Î» x i, Ï† i x) (continuous_linear_map.pi Ï†') x L â†”
    âˆ€ i, has_fderiv_at_filter (Ï† i) (Ï†' i) x L :=
has_fderiv_at_filter_pi'

@[simp] lemma has_fderiv_at_pi' :
  has_fderiv_at Î¦ Î¦' x â†”
    âˆ€ i, has_fderiv_at (Î» x, Î¦ x i) ((proj i).comp Î¦') x :=
has_fderiv_at_filter_pi'

lemma has_fderiv_at_pi :
  has_fderiv_at (Î» x i, Ï† i x) (continuous_linear_map.pi Ï†') x â†”
    âˆ€ i, has_fderiv_at (Ï† i) (Ï†' i) x :=
has_fderiv_at_filter_pi

@[simp] lemma has_fderiv_within_at_pi' :
  has_fderiv_within_at Î¦ Î¦' s x â†”
    âˆ€ i, has_fderiv_within_at (Î» x, Î¦ x i) ((proj i).comp Î¦') s x :=
has_fderiv_at_filter_pi'

lemma has_fderiv_within_at_pi :
  has_fderiv_within_at (Î» x i, Ï† i x) (continuous_linear_map.pi Ï†') s x â†”
    âˆ€ i, has_fderiv_within_at (Ï† i) (Ï†' i) s x :=
has_fderiv_at_filter_pi

@[simp] lemma differentiable_within_at_pi :
  differentiable_within_at ğ•œ Î¦ s x â†”
   âˆ€ i, differentiable_within_at ğ•œ (Î» x, Î¦ x i) s x :=
âŸ¨Î» h i, (has_fderiv_within_at_pi'.1 h.has_fderiv_within_at i).differentiable_within_at,
  Î» h, (has_fderiv_within_at_pi.2 (Î» i, (h i).has_fderiv_within_at)).differentiable_within_atâŸ©

@[simp] lemma differentiable_at_pi :
  differentiable_at ğ•œ Î¦ x â†” âˆ€ i, differentiable_at ğ•œ (Î» x, Î¦ x i) x :=
âŸ¨Î» h i, (has_fderiv_at_pi'.1 h.has_fderiv_at i).differentiable_at,
  Î» h, (has_fderiv_at_pi.2 (Î» i, (h i).has_fderiv_at)).differentiable_atâŸ©

lemma differentiable_on_pi :
  differentiable_on ğ•œ Î¦ s â†” âˆ€ i, differentiable_on ğ•œ (Î» x, Î¦ x i) s :=
âŸ¨Î» h i x hx, differentiable_within_at_pi.1 (h x hx) i,
  Î» h x hx, differentiable_within_at_pi.2 (Î» i, h i x hx)âŸ©

lemma differentiable_pi :
  differentiable ğ•œ Î¦ â†” âˆ€ i, differentiable ğ•œ (Î» x, Î¦ x i) :=
âŸ¨Î» h i x, differentiable_at_pi.1 (h x) i, Î» h x, differentiable_at_pi.2 (Î» i, h i x)âŸ©

-- TODO: find out which version (`Ï†` or `Î¦`) works better with `rw`/`simp`
lemma fderiv_within_pi (h : âˆ€ i, differentiable_within_at ğ•œ (Ï† i) s x)
  (hs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î» x i, Ï† i x) s x = pi (Î» i, fderiv_within ğ•œ (Ï† i) s x) :=
(has_fderiv_within_at_pi.2 (Î» i, (h i).has_fderiv_within_at)).fderiv_within hs

lemma fderiv_pi (h : âˆ€ i, differentiable_at ğ•œ (Ï† i) x) :
  fderiv ğ•œ (Î» x i, Ï† i x) x = pi (Î» i, fderiv ğ•œ (Ï† i) x) :=
(has_fderiv_at_pi.2 (Î» i, (h i).has_fderiv_at)).fderiv

end pi

section neg
/-! ### Derivative of the negative of a function -/

theorem has_strict_fderiv_at.neg (h : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Î» x, -f x) (-f') x :=
(-1 : F â†’L[ğ•œ] F).has_strict_fderiv_at.comp x h

theorem has_fderiv_at_filter.neg (h : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (Î» x, -f x) (-f') x L :=
(-1 : F â†’L[ğ•œ] F).has_fderiv_at_filter.comp x h

theorem has_fderiv_within_at.neg (h : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Î» x, -f x) (-f') s x :=
h.neg

theorem has_fderiv_at.neg (h : has_fderiv_at f f' x) :
  has_fderiv_at (Î» x, -f x) (-f') x :=
h.neg

lemma differentiable_within_at.neg (h : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î»y, -f y) s x :=
h.has_fderiv_within_at.neg.differentiable_within_at

@[simp] lemma differentiable_within_at_neg_iff :
  differentiable_within_at ğ•œ (Î»y, -f y) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma differentiable_at.neg (h : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î»y, -f y) x :=
h.has_fderiv_at.neg.differentiable_at

@[simp] lemma differentiable_at_neg_iff :
  differentiable_at ğ•œ (Î»y, -f y) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma differentiable_on.neg (h : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î»y, -f y) s :=
Î»x hx, (h x hx).neg

@[simp] lemma differentiable_on_neg_iff :
  differentiable_on ğ•œ (Î»y, -f y) s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma differentiable.neg (h : differentiable ğ•œ f) :
  differentiable ğ•œ (Î»y, -f y) :=
Î»x, (h x).neg

@[simp] lemma differentiable_neg_iff : differentiable ğ•œ (Î»y, -f y) â†” differentiable ğ•œ f :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma fderiv_within_neg (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»y, -f y) s x = - fderiv_within ğ•œ f s x :=
if h : differentiable_within_at ğ•œ f s x
then h.has_fderiv_within_at.neg.fderiv_within hxs
else by { rw [fderiv_within_zero_of_not_differentiable_within_at h,
  fderiv_within_zero_of_not_differentiable_within_at, neg_zero], simpa }

@[simp] lemma fderiv_neg : fderiv ğ•œ (Î»y, -f y) x = - fderiv ğ•œ f x :=
by simp only [â† fderiv_within_univ, fderiv_within_neg unique_diff_within_at_univ]

end neg

section sub
/-! ### Derivative of the difference of two functions -/

theorem has_strict_fderiv_at.sub
  (hf : has_strict_fderiv_at f f' x) (hg : has_strict_fderiv_at g g' x) :
  has_strict_fderiv_at (Î» x, f x - g x) (f' - g') x :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem has_fderiv_at_filter.sub
  (hf : has_fderiv_at_filter f f' x L) (hg : has_fderiv_at_filter g g' x L) :
  has_fderiv_at_filter (Î» x, f x - g x) (f' - g') x L :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem has_fderiv_within_at.sub
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» x, f x - g x) (f' - g') s x :=
hf.sub hg

theorem has_fderiv_at.sub
  (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» x, f x - g x) (f' - g') x :=
hf.sub hg

lemma differentiable_within_at.sub
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  differentiable_within_at ğ•œ (Î» y, f y - g y) s x :=
(hf.has_fderiv_within_at.sub hg.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  differentiable_at ğ•œ (Î» y, f y - g y) x :=
(hf.has_fderiv_at.sub hg.has_fderiv_at).differentiable_at

lemma differentiable_on.sub
  (hf : differentiable_on ğ•œ f s) (hg : differentiable_on ğ•œ g s) :
  differentiable_on ğ•œ (Î»y, f y - g y) s :=
Î»x hx, (hf x hx).sub (hg x hx)

@[simp] lemma differentiable.sub
  (hf : differentiable ğ•œ f) (hg : differentiable ğ•œ g) :
  differentiable ğ•œ (Î»y, f y - g y) :=
Î»x, (hf x).sub (hg x)

lemma fderiv_within_sub (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  fderiv_within ğ•œ (Î»y, f y - g y) s x = fderiv_within ğ•œ f s x - fderiv_within ğ•œ g s x :=
(hf.has_fderiv_within_at.sub hg.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  fderiv ğ•œ (Î»y, f y - g y) x = fderiv ğ•œ f x - fderiv ğ•œ g x :=
(hf.has_fderiv_at.sub hg.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.sub_const
  (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» x, f x - c) f' x :=
by simpa only [sub_eq_add_neg] using hf.add_const (-c)

theorem has_fderiv_at_filter.sub_const
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» x, f x - c) f' x L :=
by simpa only [sub_eq_add_neg] using hf.add_const (-c)

theorem has_fderiv_within_at.sub_const
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» x, f x - c) f' s x :=
hf.sub_const c

theorem has_fderiv_at.sub_const
  (hf : has_fderiv_at f f' x) (c : F) :
  has_fderiv_at (Î» x, f x - c) f' x :=
hf.sub_const c

lemma differentiable_within_at.sub_const
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y - c) s x :=
(hf.has_fderiv_within_at.sub_const c).differentiable_within_at

@[simp] lemma differentiable_within_at_sub_const_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y - c) s x â†” differentiable_within_at ğ•œ f s x :=
by simp only [sub_eq_add_neg, differentiable_within_at_add_const_iff]

lemma differentiable_at.sub_const (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, f y - c) x :=
(hf.has_fderiv_at.sub_const c).differentiable_at

@[simp] lemma differentiable_at_sub_const_iff (c : F) :
  differentiable_at ğ•œ (Î» y, f y - c) x â†” differentiable_at ğ•œ f x :=
by simp only [sub_eq_add_neg, differentiable_at_add_const_iff]

lemma differentiable_on.sub_const (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, f y - c) s :=
Î»x hx, (hf x hx).sub_const c

@[simp] lemma differentiable_on_sub_const_iff (c : F) :
  differentiable_on ğ•œ (Î» y, f y - c) s â†” differentiable_on ğ•œ f s :=
by simp only [sub_eq_add_neg, differentiable_on_add_const_iff]

lemma differentiable.sub_const (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, f y - c) :=
Î»x, (hf x).sub_const c

@[simp] lemma differentiable_sub_const_iff (c : F) :
  differentiable ğ•œ (Î» y, f y - c) â†” differentiable ğ•œ f :=
by simp only [sub_eq_add_neg, differentiable_add_const_iff]

lemma fderiv_within_sub_const (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, f y - c) s x = fderiv_within ğ•œ f s x :=
by simp only [sub_eq_add_neg, fderiv_within_add_const hxs]

lemma fderiv_sub_const (c : F) : fderiv ğ•œ (Î»y, f y - c) x = fderiv ğ•œ f x :=
by simp only [sub_eq_add_neg, fderiv_add_const]

theorem has_strict_fderiv_at.const_sub
  (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» x, c - f x) (-f') x :=
by simpa only [sub_eq_add_neg] using hf.neg.const_add c

theorem has_fderiv_at_filter.const_sub
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» x, c - f x) (-f') x L :=
by simpa only [sub_eq_add_neg] using hf.neg.const_add c

theorem has_fderiv_within_at.const_sub
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» x, c - f x) (-f') s x :=
hf.const_sub c

theorem has_fderiv_at.const_sub
  (hf : has_fderiv_at f f' x) (c : F) :
  has_fderiv_at (Î» x, c - f x) (-f') x :=
hf.const_sub c

lemma differentiable_within_at.const_sub
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, c - f y) s x :=
(hf.has_fderiv_within_at.const_sub c).differentiable_within_at

@[simp] lemma differentiable_within_at_const_sub_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, c - f y) s x â†” differentiable_within_at ğ•œ f s x :=
by simp [sub_eq_add_neg]

lemma differentiable_at.const_sub
  (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, c - f y) x :=
(hf.has_fderiv_at.const_sub c).differentiable_at

@[simp] lemma differentiable_at_const_sub_iff (c : F) :
  differentiable_at ğ•œ (Î» y, c - f y) x â†” differentiable_at ğ•œ f x :=
by simp [sub_eq_add_neg]

lemma differentiable_on.const_sub (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, c - f y) s :=
Î»x hx, (hf x hx).const_sub c

@[simp] lemma differentiable_on_const_sub_iff (c : F) :
  differentiable_on ğ•œ (Î» y, c - f y) s â†” differentiable_on ğ•œ f s :=
by simp [sub_eq_add_neg]

lemma differentiable.const_sub (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, c - f y) :=
Î»x, (hf x).const_sub c

@[simp] lemma differentiable_const_sub_iff (c : F) :
  differentiable ğ•œ (Î» y, c - f y) â†” differentiable ğ•œ f :=
by simp [sub_eq_add_neg]

lemma fderiv_within_const_sub (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, c - f y) s x = -fderiv_within ğ•œ f s x :=
by simp only [sub_eq_add_neg, fderiv_within_const_add, fderiv_within_neg, hxs]

lemma fderiv_const_sub (c : F) : fderiv ğ•œ (Î»y, c - f y) x = -fderiv ğ•œ f x :=
by simp only [â† fderiv_within_univ, fderiv_within_const_sub unique_diff_within_at_univ]

end sub

section bilinear_map
/-! ### Derivative of a bounded bilinear map -/

variables {b : E Ã— F â†’ G} {u : set (E Ã— F) }

open normed_field

lemma is_bounded_bilinear_map.has_strict_fderiv_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_strict_fderiv_at b (h.deriv p) p :=
begin
  rw has_strict_fderiv_at,
  set T := (E Ã— F) Ã— (E Ã— F),
  have : is_o (Î» q : T, b (q.1 - q.2)) (Î» q : T, âˆ¥q.1 - q.2âˆ¥ * 1) (ğ“ (p, p)),
  { refine (h.is_O'.comp_tendsto le_top).trans_is_o _,
    simp only [(âˆ˜)],
    refine (is_O_refl (Î» q : T, âˆ¥q.1 - q.2âˆ¥) _).mul_is_o (is_o.norm_left $ (is_o_one_iff _).2 _),
    rw [â† sub_self p],
    exact continuous_at_fst.sub continuous_at_snd },
  simp only [mul_one, is_o_norm_right] at this,
  refine (is_o.congr_of_sub _).1 this, clear this,
  convert_to is_o (Î» q : T, h.deriv (p - q.2) (q.1 - q.2)) (Î» q : T, q.1 - q.2) (ğ“ (p, p)),
  { ext âŸ¨âŸ¨xâ‚, yâ‚âŸ©, âŸ¨xâ‚‚, yâ‚‚âŸ©âŸ©, rcases p with âŸ¨x, yâŸ©,
    simp only [is_bounded_bilinear_map_deriv_coe, prod.mk_sub_mk, h.map_sub_left, h.map_sub_right],
    abel },
  have : is_o (Î» q : T, p - q.2) (Î» q, (1:â„)) (ğ“ (p, p)),
    from (is_o_one_iff _).2 (sub_self p â–¸ tendsto_const_nhds.sub continuous_at_snd),
  apply is_bounded_bilinear_map_apply.is_O_comp.trans_is_o,
  refine is_o.trans_is_O _ (is_O_const_mul_self 1 _ _).of_norm_right,
  refine is_o.mul_is_O _ (is_O_refl _ _),
  exact (((h.is_bounded_linear_map_deriv.is_O_id âŠ¤).comp_tendsto le_top : _).trans_is_o
    this).norm_left
end

lemma is_bounded_bilinear_map.has_fderiv_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_fderiv_at b (h.deriv p) p :=
(h.has_strict_fderiv_at p).has_fderiv_at

lemma is_bounded_bilinear_map.has_fderiv_within_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_fderiv_within_at b (h.deriv p) u p :=
(h.has_fderiv_at p).has_fderiv_within_at

lemma is_bounded_bilinear_map.differentiable_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  differentiable_at ğ•œ b p :=
(h.has_fderiv_at p).differentiable_at

lemma is_bounded_bilinear_map.differentiable_within_at (h : is_bounded_bilinear_map ğ•œ b)
  (p : E Ã— F) :
  differentiable_within_at ğ•œ b u p :=
(h.differentiable_at p).differentiable_within_at

lemma is_bounded_bilinear_map.fderiv (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  fderiv ğ•œ b p = h.deriv p :=
has_fderiv_at.fderiv (h.has_fderiv_at p)

lemma is_bounded_bilinear_map.fderiv_within (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F)
  (hxs : unique_diff_within_at ğ•œ u p) : fderiv_within ğ•œ b u p = h.deriv p :=
begin
  rw differentiable_at.fderiv_within (h.differentiable_at p) hxs,
  exact h.fderiv p
end

lemma is_bounded_bilinear_map.differentiable (h : is_bounded_bilinear_map ğ•œ b) :
  differentiable ğ•œ b :=
Î»x, h.differentiable_at x

lemma is_bounded_bilinear_map.differentiable_on (h : is_bounded_bilinear_map ğ•œ b) :
  differentiable_on ğ•œ b u :=
h.differentiable.differentiable_on

lemma is_bounded_bilinear_map.continuous (h : is_bounded_bilinear_map ğ•œ b) :
  continuous b :=
h.differentiable.continuous

lemma is_bounded_bilinear_map.continuous_left (h : is_bounded_bilinear_map ğ•œ b) {f : F} :
  continuous (Î»e, b (e, f)) :=
h.continuous.comp (continuous_id.prod_mk continuous_const)

lemma is_bounded_bilinear_map.continuous_right (h : is_bounded_bilinear_map ğ•œ b) {e : E} :
  continuous (Î»f, b (e, f)) :=
h.continuous.comp (continuous_const.prod_mk continuous_id)

end bilinear_map

namespace continuous_linear_equiv

/-!
### The set of continuous linear equivalences between two Banach spaces is open

In this section we establish that the set of continuous linear equivalences between two Banach
spaces is an open subset of the space of linear maps between them.  These facts are placed here
because the proof uses `is_bounded_bilinear_map.continuous_left`, proved just above as a consequence
of its differentiability.
-/

protected lemma is_open [complete_space E] : is_open (range (coe : (E â‰ƒL[ğ•œ] F) â†’ (E â†’L[ğ•œ] F))) :=
begin
  nontriviality E,
  rw [is_open_iff_mem_nhds, forall_range_iff],
  refine Î» e, mem_nhds_sets _ (mem_range_self _),
  let O : (E â†’L[ğ•œ] F) â†’ (E â†’L[ğ•œ] E) := Î» f, (e.symm : F â†’L[ğ•œ] E).comp f,
  have h_O : continuous O := is_bounded_bilinear_map_comp.continuous_left,
  convert units.is_open.preimage h_O using 1,
  ext f',
  split,
  { rintros âŸ¨e', rflâŸ©,
    exact âŸ¨(e'.trans e.symm).to_unit, rflâŸ© },
  { rintros âŸ¨w, hwâŸ©,
    use (units_equiv ğ•œ E w).trans e,
    ext x,
    simp [hw] }
end

protected lemma nhds [complete_space E] (e : E â‰ƒL[ğ•œ] F) :
  (range (coe : (E â‰ƒL[ğ•œ] F) â†’ (E â†’L[ğ•œ] F))) âˆˆ ğ“ (e : E â†’L[ğ•œ] F) :=
mem_nhds_sets continuous_linear_equiv.is_open (by simp)

end continuous_linear_equiv

section smul
/-! ### Derivative of the product of a scalar-valued function and a vector-valued function

If `c` is a differentiable scalar-valued function and `f` is a differentiable vector-valued
function, then `Î» x, c x â€¢ f x` is differentiable as well. Lemmas in this section works for
function `c` taking values in the base field, as well as in a normed algebra over the base
field: e.g., they work for `c : E â†’ â„‚` and `f : E â†’ F` provided that `F` is a complex
normed vector space.
-/

variables {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  [normed_space ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F]
variables {c : E â†’ ğ•œ'} {c' : E â†’L[ğ•œ] ğ•œ'}

theorem has_strict_fderiv_at.smul (hc : has_strict_fderiv_at c c' x)
  (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.smul_right (f x)) x :=
(is_bounded_bilinear_map_smul.has_strict_fderiv_at (c x, f x)).comp x $
  hc.prod hf

theorem has_fderiv_within_at.smul
  (hc : has_fderiv_within_at c c' s x) (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.smul_right (f x)) s x :=
(is_bounded_bilinear_map_smul.has_fderiv_at (c x, f x)).comp_has_fderiv_within_at x $
  hc.prod hf

theorem has_fderiv_at.smul (hc : has_fderiv_at c c' x) (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.smul_right (f x)) x :=
(is_bounded_bilinear_map_smul.has_fderiv_at (c x, f x)).comp x $
  hc.prod hf

lemma differentiable_within_at.smul
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î» y, c y â€¢ f y) s x :=
(hc.has_fderiv_within_at.smul hf.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.smul (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î» y, c y â€¢ f y) x :=
(hc.has_fderiv_at.smul hf.has_fderiv_at).differentiable_at

lemma differentiable_on.smul (hc : differentiable_on ğ•œ c s) (hf : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î» y, c y â€¢ f y) s :=
Î»x hx, (hc x hx).smul (hf x hx)

@[simp] lemma differentiable.smul (hc : differentiable ğ•œ c) (hf : differentiable ğ•œ f) :
  differentiable ğ•œ (Î» y, c y â€¢ f y) :=
Î»x, (hc x).smul (hf x)

lemma fderiv_within_smul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  fderiv_within ğ•œ (Î» y, c y â€¢ f y) s x =
    c x â€¢ fderiv_within ğ•œ f s x + (fderiv_within ğ•œ c s x).smul_right (f x) :=
(hc.has_fderiv_within_at.smul hf.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_smul (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  fderiv ğ•œ (Î» y, c y â€¢ f y) x =
    c x â€¢ fderiv ğ•œ f x + (fderiv ğ•œ c x).smul_right (f x) :=
(hc.has_fderiv_at.smul hf.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.smul_const (hc : has_strict_fderiv_at c c' x) (f : F) :
  has_strict_fderiv_at (Î» y, c y â€¢ f) (c'.smul_right f) x :=
by simpa only [smul_zero, zero_add] using hc.smul (has_strict_fderiv_at_const f x)

theorem has_fderiv_within_at.smul_const (hc : has_fderiv_within_at c c' s x) (f : F) :
  has_fderiv_within_at (Î» y, c y â€¢ f) (c'.smul_right f) s x :=
by simpa only [smul_zero, zero_add] using hc.smul (has_fderiv_within_at_const f x s)

theorem has_fderiv_at.smul_const (hc : has_fderiv_at c c' x) (f : F) :
  has_fderiv_at (Î» y, c y â€¢ f) (c'.smul_right f) x :=
by simpa only [smul_zero, zero_add] using hc.smul (has_fderiv_at_const f x)

lemma differentiable_within_at.smul_const
  (hc : differentiable_within_at ğ•œ c s x) (f : F) :
  differentiable_within_at ğ•œ (Î» y, c y â€¢ f) s x :=
(hc.has_fderiv_within_at.smul_const f).differentiable_within_at

lemma differentiable_at.smul_const (hc : differentiable_at ğ•œ c x) (f : F) :
  differentiable_at ğ•œ (Î» y, c y â€¢ f) x :=
(hc.has_fderiv_at.smul_const f).differentiable_at

lemma differentiable_on.smul_const (hc : differentiable_on ğ•œ c s) (f : F) :
  differentiable_on ğ•œ (Î» y, c y â€¢ f) s :=
Î»x hx, (hc x hx).smul_const f

lemma differentiable.smul_const (hc : differentiable ğ•œ c) (f : F) :
  differentiable ğ•œ (Î» y, c y â€¢ f) :=
Î»x, (hc x).smul_const f

lemma fderiv_within_smul_const (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (f : F) :
  fderiv_within ğ•œ (Î» y, c y â€¢ f) s x =
    (fderiv_within ğ•œ c s x).smul_right f :=
(hc.has_fderiv_within_at.smul_const f).fderiv_within hxs

lemma fderiv_smul_const (hc : differentiable_at ğ•œ c x) (f : F) :
  fderiv ğ•œ (Î» y, c y â€¢ f) x = (fderiv ğ•œ c x).smul_right f :=
(hc.has_fderiv_at.smul_const f).fderiv

end smul

section mul
/-! ### Derivative of the product of two scalar-valued functions -/

variables {c d : E â†’ ğ•œ} {c' d' : E â†’L[ğ•œ] ğ•œ}

theorem has_strict_fderiv_at.mul
  (hc : has_strict_fderiv_at c c' x) (hd : has_strict_fderiv_at d d' x) :
  has_strict_fderiv_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') x :=
by { convert hc.smul hd, ext z, apply mul_comm }

theorem has_fderiv_within_at.mul
  (hc : has_fderiv_within_at c c' s x) (hd : has_fderiv_within_at d d' s x) :
  has_fderiv_within_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') s x :=
by { convert hc.smul hd, ext z, apply mul_comm }

theorem has_fderiv_at.mul (hc : has_fderiv_at c c' x) (hd : has_fderiv_at d d' x) :
  has_fderiv_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') x :=
by { convert hc.smul hd, ext z, apply mul_comm }

lemma differentiable_within_at.mul
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  differentiable_within_at ğ•œ (Î» y, c y * d y) s x :=
(hc.has_fderiv_within_at.mul hd.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.mul (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  differentiable_at ğ•œ (Î» y, c y * d y) x :=
(hc.has_fderiv_at.mul hd.has_fderiv_at).differentiable_at

lemma differentiable_on.mul (hc : differentiable_on ğ•œ c s) (hd : differentiable_on ğ•œ d s) :
  differentiable_on ğ•œ (Î» y, c y * d y) s :=
Î»x hx, (hc x hx).mul (hd x hx)

@[simp] lemma differentiable.mul (hc : differentiable ğ•œ c) (hd : differentiable ğ•œ d) :
  differentiable ğ•œ (Î» y, c y * d y) :=
Î»x, (hc x).mul (hd x)

lemma fderiv_within_mul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  fderiv_within ğ•œ (Î» y, c y * d y) s x =
    c x â€¢ fderiv_within ğ•œ d s x + d x â€¢ fderiv_within ğ•œ c s x :=
(hc.has_fderiv_within_at.mul hd.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_mul (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  fderiv ğ•œ (Î» y, c y * d y) x =
    c x â€¢ fderiv ğ•œ d x + d x â€¢ fderiv ğ•œ c x :=
(hc.has_fderiv_at.mul hd.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.mul_const (hc : has_strict_fderiv_at c c' x) (d : ğ•œ) :
  has_strict_fderiv_at (Î» y, c y * d) (d â€¢ c') x :=
by simpa only [smul_zero, zero_add] using hc.mul (has_strict_fderiv_at_const d x)

theorem has_fderiv_within_at.mul_const (hc : has_fderiv_within_at c c' s x) (d : ğ•œ) :
  has_fderiv_within_at (Î» y, c y * d) (d â€¢ c') s x :=
by simpa only [smul_zero, zero_add] using hc.mul (has_fderiv_within_at_const d x s)

theorem has_fderiv_at.mul_const (hc : has_fderiv_at c c' x) (d : ğ•œ) :
  has_fderiv_at (Î» y, c y * d) (d â€¢ c') x :=
begin
  rw [â† has_fderiv_within_at_univ] at *,
  exact hc.mul_const d
end

lemma differentiable_within_at.mul_const
  (hc : differentiable_within_at ğ•œ c s x) (d : ğ•œ) :
  differentiable_within_at ğ•œ (Î» y, c y * d) s x :=
(hc.has_fderiv_within_at.mul_const d).differentiable_within_at

lemma differentiable_at.mul_const (hc : differentiable_at ğ•œ c x) (d : ğ•œ) :
  differentiable_at ğ•œ (Î» y, c y * d) x :=
(hc.has_fderiv_at.mul_const d).differentiable_at

lemma differentiable_on.mul_const (hc : differentiable_on ğ•œ c s) (d : ğ•œ) :
  differentiable_on ğ•œ (Î» y, c y * d) s :=
Î»x hx, (hc x hx).mul_const d

lemma differentiable.mul_const (hc : differentiable ğ•œ c) (d : ğ•œ) :
  differentiable ğ•œ (Î» y, c y * d) :=
Î»x, (hc x).mul_const d

lemma fderiv_within_mul_const (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (d : ğ•œ) :
  fderiv_within ğ•œ (Î» y, c y * d) s x = d â€¢ fderiv_within ğ•œ c s x :=
(hc.has_fderiv_within_at.mul_const d).fderiv_within hxs

lemma fderiv_mul_const (hc : differentiable_at ğ•œ c x) (d : ğ•œ) :
  fderiv ğ•œ (Î» y, c y * d) x = d â€¢ fderiv ğ•œ c x :=
(hc.has_fderiv_at.mul_const d).fderiv

theorem has_strict_fderiv_at.const_mul (hc : has_strict_fderiv_at c c' x) (d : ğ•œ) :
  has_strict_fderiv_at (Î» y, d * c y) (d â€¢ c') x :=
begin
  simp only [mul_comm d],
  exact hc.mul_const d,
end

theorem has_fderiv_within_at.const_mul
  (hc : has_fderiv_within_at c c' s x) (d : ğ•œ) :
  has_fderiv_within_at (Î» y, d * c y) (d â€¢ c') s x :=
begin
  simp only [mul_comm d],
  exact hc.mul_const d,
end

theorem has_fderiv_at.const_mul (hc : has_fderiv_at c c' x) (d : ğ•œ) :
  has_fderiv_at (Î» y, d * c y) (d â€¢ c') x :=
begin
  simp only [mul_comm d],
  exact hc.mul_const d,
end

lemma differentiable_within_at.const_mul
  (hc : differentiable_within_at ğ•œ c s x) (d : ğ•œ) :
  differentiable_within_at ğ•œ (Î» y, d * c y) s x :=
(hc.has_fderiv_within_at.const_mul d).differentiable_within_at

lemma differentiable_at.const_mul (hc : differentiable_at ğ•œ c x) (d : ğ•œ) :
  differentiable_at ğ•œ (Î» y, d * c y) x :=
(hc.has_fderiv_at.const_mul d).differentiable_at

lemma differentiable_on.const_mul (hc : differentiable_on ğ•œ c s) (d : ğ•œ) :
  differentiable_on ğ•œ (Î» y, d * c y) s :=
Î»x hx, (hc x hx).const_mul d

lemma differentiable.const_mul (hc : differentiable ğ•œ c) (d : ğ•œ) :
  differentiable ğ•œ (Î» y, d * c y) :=
Î»x, (hc x).const_mul d

lemma fderiv_within_const_mul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (d : ğ•œ) :
  fderiv_within ğ•œ (Î» y, d * c y) s x = d â€¢ fderiv_within ğ•œ c s x :=
(hc.has_fderiv_within_at.const_mul d).fderiv_within hxs

lemma fderiv_const_mul (hc : differentiable_at ğ•œ c x) (d : ğ•œ) :
  fderiv ğ•œ (Î» y, d * c y) x = d â€¢ fderiv ğ•œ c x :=
(hc.has_fderiv_at.const_mul d).fderiv

end mul

section algebra_inverse
variables {R : Type*} [normed_ring R] [normed_algebra ğ•œ R] [complete_space R]
open normed_ring continuous_linear_map ring

/-- At an invertible element `x` of a normed algebra `R`, the FrÃ©chet derivative of the inversion
operation is the linear map `Î» t, - xâ»Â¹ * t * xâ»Â¹`. -/
lemma has_fderiv_at_ring_inverse (x : units R) :
  has_fderiv_at ring.inverse (-lmul_left_right ğ•œ R â†‘xâ»Â¹ â†‘xâ»Â¹) x :=
begin
  have h_is_o : is_o (Î» (t : R), inverse (â†‘x + t) - â†‘xâ»Â¹ + â†‘xâ»Â¹ * t * â†‘xâ»Â¹)
    (Î» (t : R), t) (ğ“ 0),
  { refine (inverse_add_norm_diff_second_order x).trans_is_o ((is_o_norm_norm).mp _),
    simp only [normed_field.norm_pow, norm_norm],
    have h12 : 1 < 2 := by norm_num,
    convert (asymptotics.is_o_pow_pow h12).comp_tendsto tendsto_norm_zero,
    ext, simp },
  have h_lim : tendsto (Î» (y:R), y - x) (ğ“ x) (ğ“ 0),
  { refine tendsto_zero_iff_norm_tendsto_zero.mpr _,
    exact tendsto_iff_norm_tendsto_zero.mp tendsto_id },
  simp only [has_fderiv_at, has_fderiv_at_filter],
  convert h_is_o.comp_tendsto h_lim,
  ext y,
  simp only [coe_comp', function.comp_app, lmul_left_right_apply, neg_apply, inverse_unit x,
    units.inv_mul, add_sub_cancel'_right, mul_sub, sub_mul, one_mul, sub_neg_eq_add]
end

lemma differentiable_at_inverse (x : units R) : differentiable_at ğ•œ (@ring.inverse R _) x :=
(has_fderiv_at_ring_inverse x).differentiable_at

lemma fderiv_inverse (x : units R) :
  fderiv ğ•œ (@ring.inverse R _) x = - lmul_left_right ğ•œ R â†‘xâ»Â¹ â†‘xâ»Â¹ :=
(has_fderiv_at_ring_inverse x).fderiv

end algebra_inverse

namespace continuous_linear_equiv
/-! ### Differentiability of linear equivs, and invariance of differentiability -/

variable (iso : E â‰ƒL[ğ•œ] F)

protected lemma has_strict_fderiv_at :
  has_strict_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
iso.to_continuous_linear_map.has_strict_fderiv_at

protected lemma has_fderiv_within_at :
  has_fderiv_within_at iso (iso : E â†’L[ğ•œ] F) s x :=
iso.to_continuous_linear_map.has_fderiv_within_at

protected lemma has_fderiv_at : has_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
iso.to_continuous_linear_map.has_fderiv_at_filter

protected lemma differentiable_at : differentiable_at ğ•œ iso x :=
iso.has_fderiv_at.differentiable_at

protected lemma differentiable_within_at :
  differentiable_within_at ğ•œ iso s x :=
iso.differentiable_at.differentiable_within_at

protected lemma fderiv : fderiv ğ•œ iso x = iso :=
iso.has_fderiv_at.fderiv

protected lemma fderiv_within (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ iso s x = iso :=
iso.to_continuous_linear_map.fderiv_within hxs

protected lemma differentiable : differentiable ğ•œ iso :=
Î»x, iso.differentiable_at

protected lemma differentiable_on : differentiable_on ğ•œ iso s :=
iso.differentiable.differentiable_on

lemma comp_differentiable_within_at_iff {f : G â†’ E} {s : set G} {x : G} :
  differentiable_within_at ğ•œ (iso âˆ˜ f) s x â†” differentiable_within_at ğ•œ f s x :=
begin
  refine âŸ¨Î» H, _, Î» H, iso.differentiable.differentiable_at.comp_differentiable_within_at x HâŸ©,
  have : differentiable_within_at ğ•œ (iso.symm âˆ˜ (iso âˆ˜ f)) s x :=
    iso.symm.differentiable.differentiable_at.comp_differentiable_within_at x H,
  rwa [â† function.comp.assoc iso.symm iso f, iso.symm_comp_self] at this,
end

lemma comp_differentiable_at_iff {f : G â†’ E} {x : G} :
  differentiable_at ğ•œ (iso âˆ˜ f) x â†” differentiable_at ğ•œ f x :=
by rw [â† differentiable_within_at_univ, â† differentiable_within_at_univ,
       iso.comp_differentiable_within_at_iff]

lemma comp_differentiable_on_iff {f : G â†’ E} {s : set G} :
  differentiable_on ğ•œ (iso âˆ˜ f) s â†” differentiable_on ğ•œ f s :=
begin
  rw [differentiable_on, differentiable_on],
  simp only [iso.comp_differentiable_within_at_iff],
end

lemma comp_differentiable_iff {f : G â†’ E} :
  differentiable ğ•œ (iso âˆ˜ f) â†” differentiable ğ•œ f :=
begin
  rw [â† differentiable_on_univ, â† differentiable_on_univ],
  exact iso.comp_differentiable_on_iff
end

lemma comp_has_fderiv_within_at_iff
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_within_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” has_fderiv_within_at f f' s x :=
begin
  refine âŸ¨Î» H, _, Î» H, iso.has_fderiv_at.comp_has_fderiv_within_at x HâŸ©,
  have A : f = iso.symm âˆ˜ (iso âˆ˜ f), by { rw [â† function.comp.assoc, iso.symm_comp_self], refl },
  have B : f' = (iso.symm : F â†’L[ğ•œ] E).comp ((iso : E â†’L[ğ•œ] F).comp f'),
    by rw [â† continuous_linear_map.comp_assoc, iso.coe_symm_comp_coe,
             continuous_linear_map.id_comp],
  rw [A, B],
  exact iso.symm.has_fderiv_at.comp_has_fderiv_within_at x H
end

lemma comp_has_strict_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_strict_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_strict_fderiv_at f f' x :=
begin
  refine âŸ¨Î» H, _, Î» H, iso.has_strict_fderiv_at.comp x HâŸ©,
  convert iso.symm.has_strict_fderiv_at.comp x H; ext z; apply (iso.symm_apply_apply _).symm
end

lemma comp_has_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_fderiv_at f f' x :=
by rw [â† has_fderiv_within_at_univ, â† has_fderiv_within_at_univ, iso.comp_has_fderiv_within_at_iff]

lemma comp_has_fderiv_within_at_iff'
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_within_at (iso âˆ˜ f) f' s x â†”
  has_fderiv_within_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x :=
by rw [â† iso.comp_has_fderiv_within_at_iff, â† continuous_linear_map.comp_assoc,
  iso.coe_comp_coe_symm, continuous_linear_map.id_comp]

lemma comp_has_fderiv_at_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_at (iso âˆ˜ f) f' x â†” has_fderiv_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') x :=
by rw [â† has_fderiv_within_at_univ, â† has_fderiv_within_at_univ, iso.comp_has_fderiv_within_at_iff']

lemma comp_fderiv_within {f : G â†’ E} {s : set G} {x : G}
  (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderiv_within ğ•œ f s x) :=
begin
  by_cases h : differentiable_within_at ğ•œ f s x,
  { rw [fderiv.comp_fderiv_within x iso.differentiable_at h hxs, iso.fderiv] },
  { have : Â¬differentiable_within_at ğ•œ (iso âˆ˜ f) s x,
      from mt iso.comp_differentiable_within_at_iff.1 h,
    rw [fderiv_within_zero_of_not_differentiable_within_at h,
        fderiv_within_zero_of_not_differentiable_within_at this,
        continuous_linear_map.comp_zero] }
end

lemma comp_fderiv {f : G â†’ E} {x : G} :
  fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) :=
begin
  rw [â† fderiv_within_univ, â† fderiv_within_univ],
  exact iso.comp_fderiv_within unique_diff_within_at_univ,
end

end continuous_linear_equiv

namespace linear_isometry_equiv
/-! ### Differentiability of linear isometry equivs, and invariance of differentiability -/

variable (iso : E â‰ƒâ‚—áµ¢[ğ•œ] F)

protected lemma has_strict_fderiv_at : has_strict_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
(iso : E â‰ƒL[ğ•œ] F).has_strict_fderiv_at

protected lemma has_fderiv_within_at : has_fderiv_within_at iso (iso : E â†’L[ğ•œ] F) s x :=
(iso : E â‰ƒL[ğ•œ] F).has_fderiv_within_at

protected lemma has_fderiv_at : has_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
(iso : E â‰ƒL[ğ•œ] F).has_fderiv_at

protected lemma differentiable_at : differentiable_at ğ•œ iso x :=
iso.has_fderiv_at.differentiable_at

protected lemma differentiable_within_at :
  differentiable_within_at ğ•œ iso s x :=
iso.differentiable_at.differentiable_within_at

protected lemma fderiv : fderiv ğ•œ iso x = iso := iso.has_fderiv_at.fderiv

protected lemma fderiv_within (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ iso s x = iso :=
(iso : E â‰ƒL[ğ•œ] F).fderiv_within hxs

protected lemma differentiable : differentiable ğ•œ iso :=
Î»x, iso.differentiable_at

protected lemma differentiable_on : differentiable_on ğ•œ iso s :=
iso.differentiable.differentiable_on

lemma comp_differentiable_within_at_iff {f : G â†’ E} {s : set G} {x : G} :
  differentiable_within_at ğ•œ (iso âˆ˜ f) s x â†” differentiable_within_at ğ•œ f s x :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_within_at_iff

lemma comp_differentiable_at_iff {f : G â†’ E} {x : G} :
  differentiable_at ğ•œ (iso âˆ˜ f) x â†” differentiable_at ğ•œ f x :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_at_iff

lemma comp_differentiable_on_iff {f : G â†’ E} {s : set G} :
  differentiable_on ğ•œ (iso âˆ˜ f) s â†” differentiable_on ğ•œ f s :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_on_iff

lemma comp_differentiable_iff {f : G â†’ E} :
  differentiable ğ•œ (iso âˆ˜ f) â†” differentiable ğ•œ f :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_iff

lemma comp_has_fderiv_within_at_iff
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_within_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” has_fderiv_within_at f f' s x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_within_at_iff

lemma comp_has_strict_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_strict_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_strict_fderiv_at f f' x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_strict_fderiv_at_iff

lemma comp_has_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_fderiv_at f f' x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_at_iff

lemma comp_has_fderiv_within_at_iff'
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_within_at (iso âˆ˜ f) f' s x â†”
  has_fderiv_within_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_within_at_iff'

lemma comp_has_fderiv_at_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_at (iso âˆ˜ f) f' x â†” has_fderiv_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_at_iff'

lemma comp_fderiv_within {f : G â†’ E} {s : set G} {x : G}
  (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderiv_within ğ•œ f s x) :=
(iso : E â‰ƒL[ğ•œ] F).comp_fderiv_within hxs

lemma comp_fderiv {f : G â†’ E} {x : G} :
  fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) :=
(iso : E â‰ƒL[ğ•œ] F).comp_fderiv

end linear_isometry_equiv

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a` in the strict sense, then `g` has the derivative `f'â»Â¹` at `a`
in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem has_strict_fderiv_at.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
  (hg : continuous_at g a) (hf : has_strict_fderiv_at f (f' : E â†’L[ğ•œ] F) (g a))
  (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
  has_strict_fderiv_at g (f'.symm : F â†’L[ğ•œ] E) a :=
begin
  replace hg := hg.prod_map' hg,
  replace hfg := hfg.prod_mk_nhds hfg,
  have : is_O (Î» p : F Ã— F, g p.1 - g p.2 - f'.symm (p.1 - p.2))
    (Î» p : F Ã— F, f' (g p.1 - g p.2) - (p.1 - p.2)) (ğ“ (a, a)),
  { refine ((f'.symm : F â†’L[ğ•œ] E).is_O_comp _ _).congr (Î» x, _) (Î» _, rfl),
    simp },
  refine this.trans_is_o _, clear this,
  refine ((hf.comp_tendsto hg).symm.congr' (hfg.mono _)
    (eventually_of_forall $ Î» _, rfl)).trans_is_O _,
  { rintros p âŸ¨hp1, hp2âŸ©,
    simp [hp1, hp2] },
  { refine (hf.is_O_sub_rev.comp_tendsto hg).congr'
      (eventually_of_forall $ Î» _, rfl) (hfg.mono _),
    rintros p âŸ¨hp1, hp2âŸ©,
    simp only [(âˆ˜), hp1, hp2] }
end

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a`, then `g` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem has_fderiv_at.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
  (hg : continuous_at g a) (hf : has_fderiv_at f (f' : E â†’L[ğ•œ] F) (g a))
  (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
  has_fderiv_at g (f'.symm : F â†’L[ğ•œ] E) a :=
begin
  have : is_O (Î» x : F, g x - g a - f'.symm (x - a)) (Î» x : F, f' (g x - g a) - (x - a)) (ğ“ a),
  { refine ((f'.symm : F â†’L[ğ•œ] E).is_O_comp _ _).congr (Î» x, _) (Î» _, rfl),
    simp },
  refine this.trans_is_o _, clear this,
  refine ((hf.comp_tendsto hg).symm.congr' (hfg.mono _)
    (eventually_of_forall $ Î» _, rfl)).trans_is_O _,
  { rintros p hp,
    simp [hp, hfg.self_of_nhds] },
  { refine (hf.is_O_sub_rev.comp_tendsto hg).congr'
      (eventually_of_forall $ Î» _, rfl) (hfg.mono _),
    rintros p hp,
    simp only [(âˆ˜), hp, hfg.self_of_nhds] }
end

/-- If `f` is a local homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` in the sense of strict differentiability at `f.symm a`, then `f.symm` has
the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
lemma local_homeomorph.has_strict_fderiv_at_symm (f : local_homeomorph E F) {f' : E â‰ƒL[ğ•œ] F} {a : F}
  (ha : a âˆˆ f.target) (htff' : has_strict_fderiv_at f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
  has_strict_fderiv_at f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
htff'.of_local_left_inverse (f.symm.continuous_at ha) (f.eventually_right_inverse ha)

/-- If `f` is a local homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` at `f.symm a`, then `f.symm` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
lemma local_homeomorph.has_fderiv_at_symm (f : local_homeomorph E F) {f' : E â‰ƒL[ğ•œ] F} {a : F}
  (ha : a âˆˆ f.target) (htff' : has_fderiv_at f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
  has_fderiv_at f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
htff'.of_local_left_inverse (f.symm.continuous_at ha) (f.eventually_right_inverse ha)

lemma has_fderiv_within_at.eventually_ne (h : has_fderiv_within_at f f' s x)
  (hf' : âˆƒ C, âˆ€ z, âˆ¥zâˆ¥ â‰¤ C * âˆ¥f' zâˆ¥) :
  âˆ€á¶  z in ğ“[s \ {x}] x, f z â‰  f x :=
begin
  rw [nhds_within, diff_eq, â† inf_principal, â† inf_assoc, eventually_inf_principal],
  have A : is_O (Î» z, z - x) (Î» z, f' (z - x)) (ğ“[s] x) :=
    (is_O_iff.2 $ hf'.imp $ Î» C hC, eventually_of_forall $ Î» z, hC _),
  have : (Î» z, f z - f x) ~[ğ“[s] x] (Î» z, f' (z - x)) := h.trans_is_O A,
  simpa [not_imp_not, sub_eq_zero] using (A.trans this.is_O_symm).eq_zero_imp
end

lemma has_fderiv_at.eventually_ne (h : has_fderiv_at f f' x) (hf' : âˆƒ C, âˆ€ z, âˆ¥zâˆ¥ â‰¤ C * âˆ¥f' zâˆ¥) :
  âˆ€á¶  z in ğ“[{x}á¶œ] x, f z â‰  f x :=
by simpa only [compl_eq_univ_diff] using (has_fderiv_within_at_univ.2 h).eventually_ne hf'

end

section
/-
  In the special case of a normed space over the reals,
  we can use  scalar multiplication in the `tendsto` characterization
  of the FrÃ©chet derivative.
-/


variables {E : Type*} [normed_group E] [normed_space â„ E]
variables {F : Type*} [normed_group F] [normed_space â„ F]
variables {f : E â†’ F} {f' : E â†’L[â„] F} {x : E}

theorem has_fderiv_at_filter_real_equiv {L : filter E} :
  tendsto (Î» x' : E, âˆ¥x' - xâˆ¥â»Â¹ * âˆ¥f x' - f x - f' (x' - x)âˆ¥) L (ğ“ 0) â†”
  tendsto (Î» x' : E, âˆ¥x' - xâˆ¥â»Â¹ â€¢ (f x' - f x - f' (x' - x))) L (ğ“ 0) :=
begin
  symmetry,
  rw [tendsto_iff_norm_tendsto_zero], refine tendsto_congr (Î» x', _),
  have : âˆ¥x' - xâˆ¥â»Â¹ â‰¥ 0, from inv_nonneg.mpr (norm_nonneg _),
  simp [norm_smul, real.norm_eq_abs, abs_of_nonneg this]
end

lemma has_fderiv_at.lim_real (hf : has_fderiv_at f f' x) (v : E) :
  tendsto (Î» (c:â„), c â€¢ (f (x + câ»Â¹ â€¢ v) - f x)) at_top (ğ“ (f' v)) :=
begin
  apply hf.lim v,
  rw tendsto_at_top_at_top,
  exact Î» b, âŸ¨b, Î» a ha, le_trans ha (le_abs_self _)âŸ©
end

end

section tangent_cone

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{F : Type*} [normed_group F] [normed_space ğ•œ F]
{f : E â†’ F} {s : set E} {f' : E â†’L[ğ•œ] F}

/-- The image of a tangent cone under the differential of a map is included in the tangent cone to
the image. -/
lemma has_fderiv_within_at.maps_to_tangent_cone {x : E} (h : has_fderiv_within_at f f' s x) :
  maps_to f' (tangent_cone_at ğ•œ s x) (tangent_cone_at ğ•œ (f '' s) (f x)) :=
begin
  rintros v âŸ¨c, d, dtop, clim, cdlimâŸ©,
  refine âŸ¨c, (Î»n, f (x + d n) - f x), mem_sets_of_superset dtop _, clim,
    h.lim at_top dtop clim cdlimâŸ©,
  simp [-mem_image, mem_image_of_mem] {contextual := tt}
end

/-- If a set has the unique differentiability property at a point x, then the image of this set
under a map with onto derivative has also the unique differentiability property at the image point.
-/
lemma has_fderiv_within_at.unique_diff_within_at {x : E} (h : has_fderiv_within_at f f' s x)
  (hs : unique_diff_within_at ğ•œ s x) (h' : dense_range f') :
  unique_diff_within_at ğ•œ (f '' s) (f x) :=
begin
  refine âŸ¨h'.dense_of_maps_to f'.continuous hs.1 _,
    h.continuous_within_at.mem_closure_image hs.2âŸ©,
  show submodule.span ğ•œ (tangent_cone_at ğ•œ s x) â‰¤
    (submodule.span ğ•œ (tangent_cone_at ğ•œ (f '' s) (f x))).comap f',
  rw [submodule.span_le],
  exact h.maps_to_tangent_cone.mono (subset.refl _) submodule.subset_span
end

lemma unique_diff_on.image {f' : E â†’ E â†’L[ğ•œ] F} (hs : unique_diff_on ğ•œ s)
  (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x) (hd : âˆ€ x âˆˆ s, dense_range (f' x)) :
  unique_diff_on ğ•œ (f '' s) :=
ball_image_iff.2 $ Î» x hx, (hf' x hx).unique_diff_within_at (hs x hx) (hd x hx)

lemma has_fderiv_within_at.unique_diff_within_at_of_continuous_linear_equiv
  {x : E} (e' : E â‰ƒL[ğ•œ] F) (h : has_fderiv_within_at f (e' : E â†’L[ğ•œ] F) s x)
  (hs : unique_diff_within_at ğ•œ s x) :
  unique_diff_within_at ğ•œ (f '' s) (f x) :=
h.unique_diff_within_at hs e'.surjective.dense_range

lemma continuous_linear_equiv.unique_diff_on_image (e : E â‰ƒL[ğ•œ] F) (h : unique_diff_on ğ•œ s) :
  unique_diff_on ğ•œ (e '' s) :=
h.image (Î» x _, e.has_fderiv_within_at) (Î» x hx, e.surjective.dense_range)

@[simp] lemma continuous_linear_equiv.unique_diff_on_image_iff (e : E â‰ƒL[ğ•œ] F) :
  unique_diff_on ğ•œ (e '' s) â†” unique_diff_on ğ•œ s :=
âŸ¨Î» h, e.symm_image_image s â–¸ e.symm.unique_diff_on_image h, e.unique_diff_on_imageâŸ©

@[simp] lemma continuous_linear_equiv.unique_diff_on_preimage_iff (e : F â‰ƒL[ğ•œ] E) :
  unique_diff_on ğ•œ (e â»Â¹' s) â†” unique_diff_on ğ•œ s :=
by rw [â† e.image_symm_eq_preimage, e.symm.unique_diff_on_image_iff]

end tangent_cone

section restrict_scalars
/-!
### Restricting from `â„‚` to `â„`, or generally from `ğ•œ'` to `ğ•œ`

If a function is differentiable over `â„‚`, then it is differentiable over `â„`. In this paragraph,
we give variants of this statement, in the general situation where `â„‚` and `â„` are replaced
respectively by `ğ•œ'` and `ğ•œ` where `ğ•œ'` is a normed algebra over `ğ•œ`.
-/

variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
variables {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
variables {E : Type*} [normed_group E] [normed_space ğ•œ E] [normed_space ğ•œ' E]
variables [is_scalar_tower ğ•œ ğ•œ' E]
variables {F : Type*} [normed_group F] [normed_space ğ•œ F] [normed_space ğ•œ' F]
variables [is_scalar_tower ğ•œ ğ•œ' F]
variables {f : E â†’ F} {f' : E â†’L[ğ•œ'] F} {s : set E} {x : E}

lemma has_strict_fderiv_at.restrict_scalars (h : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at f (f'.restrict_scalars ğ•œ) x := h

lemma has_fderiv_at.restrict_scalars (h : has_fderiv_at f f' x) :
  has_fderiv_at f (f'.restrict_scalars ğ•œ) x := h

lemma has_fderiv_within_at.restrict_scalars (h : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at f (f'.restrict_scalars ğ•œ) s x := h

lemma differentiable_at.restrict_scalars (h : differentiable_at ğ•œ' f x) :
  differentiable_at ğ•œ f x :=
(h.has_fderiv_at.restrict_scalars ğ•œ).differentiable_at

lemma differentiable_within_at.restrict_scalars (h : differentiable_within_at ğ•œ' f s x) :
  differentiable_within_at ğ•œ f s x :=
(h.has_fderiv_within_at.restrict_scalars ğ•œ).differentiable_within_at

lemma differentiable_on.restrict_scalars (h : differentiable_on ğ•œ' f s) :
  differentiable_on ğ•œ f s :=
Î»x hx, (h x hx).restrict_scalars ğ•œ

lemma differentiable.restrict_scalars (h : differentiable ğ•œ' f) :
  differentiable ğ•œ f :=
Î»x, (h x).restrict_scalars ğ•œ

end restrict_scalars
