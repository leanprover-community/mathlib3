/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, S√©bastien Gou√´zel, Yury Kudryashov
-/
import analysis.asymptotics.asymptotic_equivalent
import analysis.calculus.tangent_cone
import analysis.normed_space.bounded_linear_maps

/-!
# The Fr√©chet derivative

Let `E` and `F` be normed spaces, `f : E ‚Üí F`, and `f' : E ‚ÜíL[ùïú] F` a
continuous ùïú-linear map, where `ùïú` is a non-discrete normed field. Then

  `has_fderiv_within_at f f' s x`

says that `f` has derivative `f'` at `x`, where the domain of interest
is restricted to `s`. We also have

  `has_fderiv_at f f' x := has_fderiv_within_at f f' x univ`

Finally,

  `has_strict_fderiv_at f f' x`

means that `f : E ‚Üí F` has derivative `f' : E ‚ÜíL[ùïú] F` in the sense of strict differentiability,
i.e., `f y - f z - f'(y - z) = o(y - z)` as `y, z ‚Üí x`. This notion is used in the inverse
function theorem, and is defined here only to avoid proving theorems like
`is_bounded_bilinear_map.has_fderiv_at` twice: first for `has_fderiv_at`, then for
`has_strict_fderiv_at`.

## Main results

In addition to the definition and basic properties of the derivative,
the folder `analysis/calculus/fderiv/` contains the usual formulas
(and existence assertions) for the derivative of
* constants
* the identity
* bounded linear maps (`linear.lean`)
* bounded bilinear maps (`bilinear.lean`)
* sum of two functions (`add.lean`)
* sum of finitely many functions (`add.lean`)
* multiplication of a function by a scalar constant (`add.lean`)
* negative of a function (`add.lean`)
* subtraction of two functions (`add.lean`)
* multiplication of a function by a scalar function (`mul.lean`)
* multiplication of two scalar functions (`mul.lean`)
* composition of functions (the chain rule) (`comp.lean`)
* inverse function (`mul.lean`)
  (assuming that it exists; the inverse function theorem is in `../inverse.lean`)

For most binary operations we also define `const_op` and `op_const` theorems for the cases when
the first or second argument is a constant. This makes writing chains of `has_deriv_at`'s easier,
and they more frequently lead to the desired result.

One can also interpret the derivative of a function `f : ùïú ‚Üí E` as an element of `E` (by identifying
a linear function from `ùïú` to `E` with its value at `1`). Results on the Fr√©chet derivative are
translated to this more elementary point of view on the derivative in the file `deriv.lean`. The
derivative of polynomials is handled there, as it is naturally one-dimensional.

The simplifier is set up to prove automatically that some functions are differentiable, or
differentiable at a point (but not differentiable on a set or within a set at a point, as checking
automatically that the good domains are mapped one to the other when using composition is not
something the simplifier can easily do). This means that one can write
`example (x : ‚Ñù) : differentiable ‚Ñù (Œª x, sin (exp (3 + x^2)) - 5 * cos x) := by simp`.
If there are divisions, one needs to supply to the simplifier proofs that the denominators do
not vanish, as in
```lean
example (x : ‚Ñù) (h : 1 + sin x ‚â† 0) : differentiable_at ‚Ñù (Œª x, exp x / (1 + sin x)) x :=
by simp [h]
```
Of course, these examples only work once `exp`, `cos` and `sin` have been shown to be
differentiable, in `analysis.special_functions.trigonometric`.

The simplifier is not set up to compute the Fr√©chet derivative of maps (as these are in general
complicated multidimensional linear maps), but it will compute one-dimensional derivatives,
see `deriv.lean`.

## Implementation details

The derivative is defined in terms of the `is_o` relation, but also
characterized in terms of the `tendsto` relation.

We also introduce predicates `differentiable_within_at ùïú f s x` (where `ùïú` is the base field,
`f` the function to be differentiated, `x` the point at which the derivative is asserted to exist,
and `s` the set along which the derivative is defined), as well as `differentiable_at ùïú f x`,
`differentiable_on ùïú f s` and `differentiable ùïú f` to express the existence of a derivative.

To be able to compute with derivatives, we write `fderiv_within ùïú f s x` and `fderiv ùïú f x`
for some choice of a derivative if it exists, and the zero function otherwise. This choice only
behaves well along sets for which the derivative is unique, i.e., those for which the tangent
directions span a dense subset of the whole space. The predicates `unique_diff_within_at s x` and
`unique_diff_on s`, defined in `tangent_cone.lean` express this property. We prove that indeed
they imply the uniqueness of the derivative. This is satisfied for open subsets, and in particular
for `univ`. This uniqueness only holds when the field is non-discrete, which we request at the very
beginning: otherwise, a derivative can be defined, but it has no interesting properties whatsoever.

To make sure that the simplifier can prove automatically that functions are differentiable, we tag
many lemmas with the `simp` attribute, for instance those saying that the sum of differentiable
functions is differentiable, as well as their product, their cartesian product, and so on. A notable
exception is the chain rule: we do not mark as a simp lemma the fact that, if `f` and `g` are
differentiable, then their composition also is: `simp` would always be able to match this lemma,
by taking `f` or `g` to be the identity. Instead, for every reasonable function (say, `exp`),
we add a lemma that if `f` is differentiable then so is `(Œª x, exp (f x))`. This means adding
some boilerplate lemmas, but these can also be useful in their own right.

Tests for this ability of the simplifier (with more examples) are provided in
`tests/differentiable.lean`.

## Tags

derivative, differentiable, Fr√©chet, calculus

-/

open filter asymptotics continuous_linear_map set metric
open_locale topology classical nnreal filter asymptotics ennreal

noncomputable theory


section

variables {ùïú : Type*} [nontrivially_normed_field ùïú]
variables {E : Type*} [normed_add_comm_group E] [normed_space ùïú E]
variables {F : Type*} [normed_add_comm_group F] [normed_space ùïú F]
variables {G : Type*} [normed_add_comm_group G] [normed_space ùïú G]
variables {G' : Type*} [normed_add_comm_group G'] [normed_space ùïú G']

/-- A function `f` has the continuous linear map `f'` as derivative along the filter `L` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` converges along the filter `L`. This definition
is designed to be specialized for `L = ùìù x` (in `has_fderiv_at`), giving rise to the usual notion
of Fr√©chet derivative, and for `L = ùìù[s] x` (in `has_fderiv_within_at`), giving rise to
the notion of Fr√©chet derivative along the set `s`. -/
def has_fderiv_at_filter (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x : E) (L : filter E) :=
(Œª x', f x' - f x - f' (x' - x)) =o[L] (Œª x', x' - x)

/-- A function `f` has the continuous linear map `f'` as derivative at `x` within a set `s` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x` inside `s`. -/
def has_fderiv_within_at (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (s : set E) (x : E) :=
has_fderiv_at_filter f f' x (ùìù[s] x)

/-- A function `f` has the continuous linear map `f'` as derivative at `x` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x`. -/
def has_fderiv_at (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x : E) :=
has_fderiv_at_filter f f' x (ùìù x)

/-- A function `f` has derivative `f'` at `a` in the sense of *strict differentiability*
if `f x - f y - f' (x - y) = o(x - y)` as `x, y ‚Üí a`. This form of differentiability is required,
e.g., by the inverse function theorem. Any `C^1` function on a vector space over `‚Ñù` is strictly
differentiable but this definition works, e.g., for vector spaces over `p`-adic numbers. -/
def has_strict_fderiv_at (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x : E) :=
(Œª p : E √ó E, f p.1 - f p.2 - f' (p.1 - p.2)) =o[ùìù (x, x)] (Œª p : E √ó E, p.1 - p.2)

variables (ùïú)

/-- A function `f` is differentiable at a point `x` within a set `s` if it admits a derivative
there (possibly non-unique). -/
def differentiable_within_at (f : E ‚Üí F) (s : set E) (x : E) :=
‚àÉf' : E ‚ÜíL[ùïú] F, has_fderiv_within_at f f' s x

/-- A function `f` is differentiable at a point `x` if it admits a derivative there (possibly
non-unique). -/
def differentiable_at (f : E ‚Üí F) (x : E) :=
‚àÉf' : E ‚ÜíL[ùïú] F, has_fderiv_at f f' x

/-- If `f` has a derivative at `x` within `s`, then `fderiv_within ùïú f s x` is such a derivative.
Otherwise, it is set to `0`. -/
def fderiv_within (f : E ‚Üí F) (s : set E) (x : E) : E ‚ÜíL[ùïú] F :=
if h : ‚àÉf', has_fderiv_within_at f f' s x then classical.some h else 0

/-- If `f` has a derivative at `x`, then `fderiv ùïú f x` is such a derivative. Otherwise, it is
set to `0`. -/
def fderiv (f : E ‚Üí F) (x : E) : E ‚ÜíL[ùïú] F :=
if h : ‚àÉf', has_fderiv_at f f' x then classical.some h else 0

/-- `differentiable_on ùïú f s` means that `f` is differentiable within `s` at any point of `s`. -/
def differentiable_on (f : E ‚Üí F) (s : set E) :=
‚àÄx ‚àà s, differentiable_within_at ùïú f s x

/-- `differentiable ùïú f` means that `f` is differentiable at any point. -/
def differentiable (f : E ‚Üí F) :=
‚àÄx, differentiable_at ùïú f x

variables {ùïú}
variables {f f‚ÇÄ f‚ÇÅ g : E ‚Üí F}
variables {f' f‚ÇÄ' f‚ÇÅ' g' : E ‚ÜíL[ùïú] F}
variables (e : E ‚ÜíL[ùïú] F)
variables {x : E}
variables {s t : set E}
variables {L L‚ÇÅ L‚ÇÇ : filter E}

lemma fderiv_within_zero_of_not_differentiable_within_at
  (h : ¬¨ differentiable_within_at ùïú f s x) : fderiv_within ùïú f s x = 0 :=
have ¬¨ ‚àÉ f', has_fderiv_within_at f f' s x, from h,
by simp [fderiv_within, this]

lemma fderiv_zero_of_not_differentiable_at (h : ¬¨ differentiable_at ùïú f x) : fderiv ùïú f x = 0 :=
have ¬¨ ‚àÉ f', has_fderiv_at f f' x, from h,
by simp [fderiv, this]

section derivative_uniqueness
/- In this section, we discuss the uniqueness of the derivative.
We prove that the definitions `unique_diff_within_at` and `unique_diff_on` indeed imply the
uniqueness of the derivative. -/

/-- If a function f has a derivative f' at x, a rescaled version of f around x converges to f',
i.e., `n (f (x + (1/n) v) - f x)` converges to `f' v`. More generally, if `c n` tends to infinity
and `c n * d n` tends to `v`, then `c n * (f (x + d n) - f x)` tends to `f' v`. This lemma expresses
this fact, for functions having a derivative within a set. Its specific formulation is useful for
tangent cone related discussions. -/
theorem has_fderiv_within_at.lim (h : has_fderiv_within_at f f' s x) {Œ± : Type*} (l : filter Œ±)
  {c : Œ± ‚Üí ùïú} {d : Œ± ‚Üí E} {v : E} (dtop : ‚àÄ·∂† n in l, x + d n ‚àà s)
  (clim : tendsto (Œª n, ‚Äñc n‚Äñ) l at_top)
  (cdlim : tendsto (Œª n, c n ‚Ä¢ d n) l (ùìù v)) :
  tendsto (Œªn, c n ‚Ä¢ (f (x + d n) - f x)) l (ùìù (f' v)) :=
begin
  have tendsto_arg : tendsto (Œª n, x + d n) l (ùìù[s] x),
  { conv in (ùìù[s] x) { rw ‚Üê add_zero x },
    rw [nhds_within, tendsto_inf],
    split,
    { apply tendsto_const_nhds.add (tangent_cone_at.lim_zero l clim cdlim) },
    { rwa tendsto_principal } },
  have : (Œª y, f y - f x - f' (y - x)) =o[ùìù[s] x] (Œª y, y - x) := h,
  have : (Œª n, f (x + d n) - f x - f' ((x + d n) - x)) =o[l] (Œª n, (x + d n)  - x) :=
    this.comp_tendsto tendsto_arg,
  have : (Œª n, f (x + d n) - f x - f' (d n)) =o[l] d := by simpa only [add_sub_cancel'],
  have : (Œª n, c n ‚Ä¢ (f (x + d n) - f x - f' (d n))) =o[l] (Œª n, c n ‚Ä¢ d n) :=
    (is_O_refl c l).smul_is_o this,
  have : (Œª n, c n ‚Ä¢ (f (x + d n) - f x - f' (d n))) =o[l] (Œª n, (1:‚Ñù)) :=
    this.trans_is_O (cdlim.is_O_one ‚Ñù),
  have L1 : tendsto (Œªn, c n ‚Ä¢ (f (x + d n) - f x - f' (d n))) l (ùìù 0) :=
    (is_o_one_iff ‚Ñù).1 this,
  have L2 : tendsto (Œªn, f' (c n ‚Ä¢ d n)) l (ùìù (f' v)) :=
    tendsto.comp f'.cont.continuous_at cdlim,
  have L3 : tendsto (Œªn, (c n ‚Ä¢ (f (x + d n) - f x - f' (d n)) +  f' (c n ‚Ä¢ d n)))
            l (ùìù (0 + f' v)) :=
    L1.add L2,
  have : (Œªn, (c n ‚Ä¢ (f (x + d n) - f x - f' (d n)) +  f' (c n ‚Ä¢ d n)))
          = (Œªn, c n ‚Ä¢ (f (x + d n) - f x)),
    by { ext n, simp [smul_add, smul_sub] },
  rwa [this, zero_add] at L3
end

/-- If `f'` and `f‚ÇÅ'` are two derivatives of `f` within `s` at `x`, then they are equal on the
tangent cone to `s` at `x` -/
theorem has_fderiv_within_at.unique_on (hf : has_fderiv_within_at f f' s x)
  (hg : has_fderiv_within_at f f‚ÇÅ' s x) :
  eq_on f' f‚ÇÅ' (tangent_cone_at ùïú s x) :=
Œª y ‚ü®c, d, dtop, clim, cdlim‚ü©,
  tendsto_nhds_unique (hf.lim at_top dtop clim cdlim) (hg.lim at_top dtop clim cdlim)

/-- `unique_diff_within_at` achieves its goal: it implies the uniqueness of the derivative. -/
theorem unique_diff_within_at.eq (H : unique_diff_within_at ùïú s x)
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at f f‚ÇÅ' s x) : f' = f‚ÇÅ' :=
continuous_linear_map.ext_on H.1 (hf.unique_on hg)

theorem unique_diff_on.eq (H : unique_diff_on ùïú s) (hx : x ‚àà s)
  (h : has_fderiv_within_at f f' s x) (h‚ÇÅ : has_fderiv_within_at f f‚ÇÅ' s x) : f' = f‚ÇÅ' :=
(H x hx).eq h h‚ÇÅ

end derivative_uniqueness

section fderiv_properties
/-! ### Basic properties of the derivative -/

theorem has_fderiv_at_filter_iff_tendsto :
  has_fderiv_at_filter f f' x L ‚Üî
  tendsto (Œª x', ‚Äñx' - x‚Äñ‚Åª¬π * ‚Äñf x' - f x - f' (x' - x)‚Äñ) L (ùìù 0) :=
have h : ‚àÄ x', ‚Äñx' - x‚Äñ = 0 ‚Üí ‚Äñf x' - f x - f' (x' - x)‚Äñ = 0, from Œª x' hx',
  by { rw [sub_eq_zero.1 (norm_eq_zero.1 hx')], simp },
begin
  unfold has_fderiv_at_filter,
  rw [‚Üêis_o_norm_left, ‚Üêis_o_norm_right, is_o_iff_tendsto h],
  exact tendsto_congr (Œª _, div_eq_inv_mul _ _),
end

theorem has_fderiv_within_at_iff_tendsto : has_fderiv_within_at f f' s x ‚Üî
  tendsto (Œª x', ‚Äñx' - x‚Äñ‚Åª¬π * ‚Äñf x' - f x - f' (x' - x)‚Äñ) (ùìù[s] x) (ùìù 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_fderiv_at_iff_tendsto : has_fderiv_at f f' x ‚Üî
  tendsto (Œª x', ‚Äñx' - x‚Äñ‚Åª¬π * ‚Äñf x' - f x - f' (x' - x)‚Äñ) (ùìù x) (ùìù 0) :=
has_fderiv_at_filter_iff_tendsto

theorem has_fderiv_at_iff_is_o_nhds_zero : has_fderiv_at f f' x ‚Üî
  (Œª h : E, f (x + h) - f x - f' h) =o[ùìù 0] (Œªh, h) :=
begin
  rw [has_fderiv_at, has_fderiv_at_filter, ‚Üê map_add_left_nhds_zero x, is_o_map],
  simp [(‚àò)]
end

/-- Converse to the mean value inequality: if `f` is differentiable at `x‚ÇÄ` and `C`-lipschitz
on a neighborhood of `x‚ÇÄ` then it its derivative at `x‚ÇÄ` has norm bounded by `C`. This version
only assumes that `‚Äñf x - f x‚ÇÄ‚Äñ ‚â§ C * ‚Äñx - x‚ÇÄ‚Äñ` in a neighborhood of `x`. -/
lemma has_fderiv_at.le_of_lip' {f : E ‚Üí F} {f' : E ‚ÜíL[ùïú] F} {x‚ÇÄ : E} (hf : has_fderiv_at f f' x‚ÇÄ)
  {C : ‚Ñù} (hC‚ÇÄ : 0 ‚â§ C) (hlip : ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚Äñf x - f x‚ÇÄ‚Äñ ‚â§ C * ‚Äñx - x‚ÇÄ‚Äñ) : ‚Äñf'‚Äñ ‚â§ C :=
begin
  refine le_of_forall_pos_le_add (Œª Œµ Œµ0, op_norm_le_of_nhds_zero _ _),
  exact add_nonneg hC‚ÇÄ Œµ0.le,
  rw [‚Üê map_add_left_nhds_zero x‚ÇÄ, eventually_map] at hlip,
  filter_upwards [is_o_iff.1 (has_fderiv_at_iff_is_o_nhds_zero.1 hf) Œµ0, hlip] with y hy hyC,
  rw add_sub_cancel' at hyC,
  calc ‚Äñf' y‚Äñ ‚â§ ‚Äñf (x‚ÇÄ + y) - f x‚ÇÄ‚Äñ + ‚Äñf (x‚ÇÄ + y) - f x‚ÇÄ - f' y‚Äñ : norm_le_insert _ _
          ... ‚â§ C * ‚Äñy‚Äñ + Œµ * ‚Äñy‚Äñ                                : add_le_add hyC hy
          ... = (C + Œµ) * ‚Äñy‚Äñ                                    : (add_mul _ _ _).symm
end

/-- Converse to the mean value inequality: if `f` is differentiable at `x‚ÇÄ` and `C`-lipschitz
on a neighborhood of `x‚ÇÄ` then it its derivative at `x‚ÇÄ` has norm bounded by `C`. -/
lemma has_fderiv_at.le_of_lip {f : E ‚Üí F} {f' : E ‚ÜíL[ùïú] F} {x‚ÇÄ : E} (hf : has_fderiv_at f f' x‚ÇÄ)
  {s : set E} (hs : s ‚àà ùìù x‚ÇÄ) {C : ‚Ñù‚â•0} (hlip : lipschitz_on_with C f s) : ‚Äñf'‚Äñ ‚â§ C :=
begin
  refine hf.le_of_lip' C.coe_nonneg _,
  filter_upwards [hs] with x hx using hlip.norm_sub_le hx (mem_of_mem_nhds hs),
end

theorem has_fderiv_at_filter.mono (h : has_fderiv_at_filter f f' x L‚ÇÇ) (hst : L‚ÇÅ ‚â§ L‚ÇÇ) :
  has_fderiv_at_filter f f' x L‚ÇÅ :=
h.mono hst

theorem has_fderiv_within_at.mono_of_mem (h : has_fderiv_within_at f f' t x) (hst : t ‚àà ùìù[s] x) :
  has_fderiv_within_at f f' s x :=
h.mono $ nhds_within_le_iff.mpr hst

theorem has_fderiv_within_at.mono (h : has_fderiv_within_at f f' t x) (hst : s ‚äÜ t) :
  has_fderiv_within_at f f' s x :=
h.mono $ nhds_within_mono _ hst

theorem has_fderiv_at.has_fderiv_at_filter (h : has_fderiv_at f f' x) (hL : L ‚â§ ùìù x) :
  has_fderiv_at_filter f f' x L :=
h.mono hL

theorem has_fderiv_at.has_fderiv_within_at
  (h : has_fderiv_at f f' x) : has_fderiv_within_at f f' s x :=
h.has_fderiv_at_filter inf_le_left

lemma has_fderiv_within_at.differentiable_within_at (h : has_fderiv_within_at f f' s x) :
  differentiable_within_at ùïú f s x :=
‚ü®f', h‚ü©

lemma has_fderiv_at.differentiable_at (h : has_fderiv_at f f' x) : differentiable_at ùïú f x :=
‚ü®f', h‚ü©

@[simp] lemma has_fderiv_within_at_univ :
  has_fderiv_within_at f f' univ x ‚Üî has_fderiv_at f f' x :=
by { simp only [has_fderiv_within_at, nhds_within_univ], refl }

alias has_fderiv_within_at_univ ‚Üî has_fderiv_within_at.has_fderiv_at_of_univ _

lemma has_fderiv_within_at_insert {y : E} :
  has_fderiv_within_at f f' (insert y s) x ‚Üî has_fderiv_within_at f f' s x :=
begin
  rcases eq_or_ne x y with rfl|h,
  { simp_rw [has_fderiv_within_at, has_fderiv_at_filter],
    apply asymptotics.is_o_insert,
    simp only [sub_self, map_zero] },
  refine ‚ü®Œª h, h.mono $ subset_insert y s, Œª hf, hf.mono_of_mem _‚ü©,
  simp_rw [nhds_within_insert_of_ne h, self_mem_nhds_within]
end

alias has_fderiv_within_at_insert ‚Üî has_fderiv_within_at.of_insert has_fderiv_within_at.insert'

lemma has_fderiv_within_at.insert (h : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at f f' (insert x s) x :=
h.insert'

lemma has_fderiv_within_at_diff_singleton (y : E) :
  has_fderiv_within_at f f' (s \ {y}) x ‚Üî has_fderiv_within_at f f' s x :=
by rw [‚Üê has_fderiv_within_at_insert, insert_diff_singleton, has_fderiv_within_at_insert]

lemma has_strict_fderiv_at.is_O_sub (hf : has_strict_fderiv_at f f' x) :
  (Œª p : E √ó E, f p.1 - f p.2) =O[ùìù (x, x)] (Œª p : E √ó E, p.1 - p.2) :=
hf.is_O.congr_of_sub.2 (f'.is_O_comp _ _)

lemma has_fderiv_at_filter.is_O_sub (h : has_fderiv_at_filter f f' x L) :
  (Œª x', f x' - f x) =O[L] (Œª x', x' - x) :=
h.is_O.congr_of_sub.2 (f'.is_O_sub _ _)

protected lemma has_strict_fderiv_at.has_fderiv_at (hf : has_strict_fderiv_at f f' x) :
  has_fderiv_at f f' x :=
begin
  rw [has_fderiv_at, has_fderiv_at_filter, is_o_iff],
  exact (Œª c hc, tendsto_id.prod_mk_nhds tendsto_const_nhds (is_o_iff.1 hf hc))
end

protected lemma has_strict_fderiv_at.differentiable_at (hf : has_strict_fderiv_at f f' x) :
  differentiable_at ùïú f x :=
hf.has_fderiv_at.differentiable_at

/-- If `f` is strictly differentiable at `x` with derivative `f'` and `K > ‚Äñf'‚Äñ‚Çä`, then `f` is
`K`-Lipschitz in a neighborhood of `x`. -/
lemma has_strict_fderiv_at.exists_lipschitz_on_with_of_nnnorm_lt (hf : has_strict_fderiv_at f f' x)
  (K : ‚Ñù‚â•0) (hK : ‚Äñf'‚Äñ‚Çä < K) : ‚àÉ s ‚àà ùìù x, lipschitz_on_with K f s :=
begin
  have := hf.add_is_O_with (f'.is_O_with_comp _ _) hK,
  simp only [sub_add_cancel, is_O_with] at this,
  rcases exists_nhds_square this with ‚ü®U, Uo, xU, hU‚ü©,
  exact ‚ü®U, Uo.mem_nhds xU, lipschitz_on_with_iff_norm_sub_le.2 $
    Œª x hx y hy, hU (mk_mem_prod hx hy)‚ü©
end

/-- If `f` is strictly differentiable at `x` with derivative `f'`, then `f` is Lipschitz in a
neighborhood of `x`. See also `has_strict_fderiv_at.exists_lipschitz_on_with_of_nnnorm_lt` for a
more precise statement. -/
lemma has_strict_fderiv_at.exists_lipschitz_on_with (hf : has_strict_fderiv_at f f' x) :
  ‚àÉ K (s ‚àà ùìù x), lipschitz_on_with K f s :=
(exists_gt _).imp hf.exists_lipschitz_on_with_of_nnnorm_lt

/-- Directional derivative agrees with `has_fderiv`. -/
lemma has_fderiv_at.lim (hf : has_fderiv_at f f' x) (v : E) {Œ± : Type*} {c : Œ± ‚Üí ùïú}
  {l : filter Œ±} (hc : tendsto (Œª n, ‚Äñc n‚Äñ) l at_top) :
  tendsto (Œª n, (c n) ‚Ä¢ (f (x + (c n)‚Åª¬π ‚Ä¢ v) - f x)) l (ùìù (f' v)) :=
begin
  refine (has_fderiv_within_at_univ.2 hf).lim _ univ_mem hc _,
  assume U hU,
  refine (eventually_ne_of_tendsto_norm_at_top hc (0:ùïú)).mono (Œª y hy, _),
  convert mem_of_mem_nhds hU,
  dsimp only,
  rw [‚Üê mul_smul, mul_inv_cancel hy, one_smul]
end

theorem has_fderiv_at.unique
  (h‚ÇÄ : has_fderiv_at f f‚ÇÄ' x) (h‚ÇÅ : has_fderiv_at f f‚ÇÅ' x) : f‚ÇÄ' = f‚ÇÅ' :=
begin
  rw ‚Üê has_fderiv_within_at_univ at h‚ÇÄ h‚ÇÅ,
  exact unique_diff_within_at_univ.eq h‚ÇÄ h‚ÇÅ
end

lemma has_fderiv_within_at_inter' (h : t ‚àà ùìù[s] x) :
  has_fderiv_within_at f f' (s ‚à© t) x ‚Üî has_fderiv_within_at f f' s x :=
by simp [has_fderiv_within_at, nhds_within_restrict'' s h]

lemma has_fderiv_within_at_inter (h : t ‚àà ùìù x) :
  has_fderiv_within_at f f' (s ‚à© t) x ‚Üî has_fderiv_within_at f f' s x :=
by simp [has_fderiv_within_at, nhds_within_restrict' s h]

lemma has_fderiv_within_at.union (hs : has_fderiv_within_at f f' s x)
  (ht : has_fderiv_within_at f f' t x) :
  has_fderiv_within_at f f' (s ‚à™ t) x :=
begin
  simp only [has_fderiv_within_at, nhds_within_union],
  exact hs.sup ht,
end

lemma has_fderiv_within_at.nhds_within (h : has_fderiv_within_at f f' s x)
  (ht : s ‚àà ùìù[t] x) : has_fderiv_within_at f f' t x :=
(has_fderiv_within_at_inter' ht).1 (h.mono (inter_subset_right _ _))

lemma has_fderiv_within_at.has_fderiv_at (h : has_fderiv_within_at f f' s x) (hs : s ‚àà ùìù x) :
  has_fderiv_at f f' x :=
by rwa [‚Üê univ_inter s, has_fderiv_within_at_inter hs, has_fderiv_within_at_univ] at h

lemma differentiable_within_at.differentiable_at
  (h : differentiable_within_at ùïú f s x) (hs : s ‚àà ùìù x) : differentiable_at ùïú f x :=
h.imp (Œª f' hf', hf'.has_fderiv_at hs)

lemma differentiable_within_at.has_fderiv_within_at (h : differentiable_within_at ùïú f s x) :
  has_fderiv_within_at f (fderiv_within ùïú f s x) s x :=
begin
  dunfold fderiv_within,
  dunfold differentiable_within_at at h,
  rw dif_pos h,
  exact classical.some_spec h
end

lemma differentiable_at.has_fderiv_at (h : differentiable_at ùïú f x) :
  has_fderiv_at f (fderiv ùïú f x) x :=
begin
  dunfold fderiv,
  dunfold differentiable_at at h,
  rw dif_pos h,
  exact classical.some_spec h
end

lemma differentiable_on.has_fderiv_at (h : differentiable_on ùïú f s) (hs : s ‚àà ùìù x) :
  has_fderiv_at f (fderiv ùïú f x) x :=
((h x (mem_of_mem_nhds hs)).differentiable_at hs).has_fderiv_at

lemma differentiable_on.differentiable_at (h : differentiable_on ùïú f s) (hs : s ‚àà ùìù x) :
  differentiable_at ùïú f x :=
(h.has_fderiv_at hs).differentiable_at

lemma differentiable_on.eventually_differentiable_at (h : differentiable_on ùïú f s) (hs : s ‚àà ùìù x) :
  ‚àÄ·∂† y in ùìù x, differentiable_at ùïú f y :=
(eventually_eventually_nhds.2 hs).mono $ Œª y, h.differentiable_at

lemma has_fderiv_at.fderiv (h : has_fderiv_at f f' x) : fderiv ùïú f x = f' :=
by { ext, rw h.unique h.differentiable_at.has_fderiv_at }

lemma fderiv_eq {f' : E ‚Üí E ‚ÜíL[ùïú] F} (h : ‚àÄ x, has_fderiv_at f (f' x) x) : fderiv ùïú f = f' :=
funext $ Œª x, (h x).fderiv

/-- Converse to the mean value inequality: if `f` is differentiable at `x‚ÇÄ` and `C`-lipschitz
on a neighborhood of `x‚ÇÄ` then it its derivative at `x‚ÇÄ` has norm bounded by `C`.
Version using `fderiv`. -/
lemma fderiv_at.le_of_lip {f : E ‚Üí F} {x‚ÇÄ : E} (hf : differentiable_at ùïú f x‚ÇÄ)
  {s : set E} (hs : s ‚àà ùìù x‚ÇÄ) {C : ‚Ñù‚â•0} (hlip : lipschitz_on_with C f s) : ‚Äñfderiv ùïú f x‚ÇÄ‚Äñ ‚â§ C :=
hf.has_fderiv_at.le_of_lip hs hlip

lemma has_fderiv_within_at.fderiv_within
  (h : has_fderiv_within_at f f' s x) (hxs : unique_diff_within_at ùïú s x) :
  fderiv_within ùïú f s x = f' :=
(hxs.eq h h.differentiable_within_at.has_fderiv_within_at).symm

/-- If `x` is not in the closure of `s`, then `f` has any derivative at `x` within `s`,
as this statement is empty. -/
lemma has_fderiv_within_at_of_not_mem_closure (h : x ‚àâ closure s) :
  has_fderiv_within_at f f' s x :=
begin
  simp only [mem_closure_iff_nhds_within_ne_bot, ne_bot_iff, ne.def, not_not] at h,
  simp [has_fderiv_within_at, has_fderiv_at_filter, h, is_o, is_O_with],
end

lemma differentiable_within_at.mono (h : differentiable_within_at ùïú f t x) (st : s ‚äÜ t) :
  differentiable_within_at ùïú f s x :=
begin
  rcases h with ‚ü®f', hf'‚ü©,
  exact ‚ü®f', hf'.mono st‚ü©
end

lemma differentiable_within_at.mono_of_mem (h : differentiable_within_at ùïú f s x) {t : set E}
  (hst : s ‚àà ùìù[t] x) :
  differentiable_within_at ùïú f t x :=
(h.has_fderiv_within_at.mono_of_mem hst).differentiable_within_at

lemma differentiable_within_at_univ :
  differentiable_within_at ùïú f univ x ‚Üî differentiable_at ùïú f x :=
by simp only [differentiable_within_at, has_fderiv_within_at_univ, differentiable_at]

lemma differentiable_within_at_inter (ht : t ‚àà ùìù x) :
  differentiable_within_at ùïú f (s ‚à© t) x ‚Üî differentiable_within_at ùïú f s x :=
by simp only [differentiable_within_at, has_fderiv_within_at_inter ht]

lemma differentiable_within_at_inter' (ht : t ‚àà ùìù[s] x) :
  differentiable_within_at ùïú f (s ‚à© t) x ‚Üî differentiable_within_at ùïú f s x :=
by simp only [differentiable_within_at, has_fderiv_within_at_inter' ht]

lemma differentiable_at.differentiable_within_at
  (h : differentiable_at ùïú f x) : differentiable_within_at ùïú f s x :=
(differentiable_within_at_univ.2 h).mono (subset_univ _)

lemma differentiable.differentiable_at (h : differentiable ùïú f) :
  differentiable_at ùïú f x :=
h x

lemma differentiable_at.fderiv_within
  (h : differentiable_at ùïú f x) (hxs : unique_diff_within_at ùïú s x) :
  fderiv_within ùïú f s x = fderiv ùïú f x :=
h.has_fderiv_at.has_fderiv_within_at.fderiv_within hxs

lemma differentiable_on.mono (h : differentiable_on ùïú f t) (st : s ‚äÜ t) :
  differentiable_on ùïú f s :=
Œª x hx, (h x (st hx)).mono st

lemma differentiable_on_univ :
  differentiable_on ùïú f univ ‚Üî differentiable ùïú f :=
by simp only [differentiable_on, differentiable, differentiable_within_at_univ, mem_univ,
  forall_true_left]

lemma differentiable.differentiable_on (h : differentiable ùïú f) : differentiable_on ùïú f s :=
(differentiable_on_univ.2 h).mono (subset_univ _)

lemma differentiable_on_of_locally_differentiable_on
  (h : ‚àÄx‚ààs, ‚àÉu, is_open u ‚àß x ‚àà u ‚àß differentiable_on ùïú f (s ‚à© u)) : differentiable_on ùïú f s :=
begin
  assume x xs,
  rcases h x xs with ‚ü®t, t_open, xt, ht‚ü©,
  exact (differentiable_within_at_inter (is_open.mem_nhds t_open xt)).1 (ht x ‚ü®xs, xt‚ü©)
end

lemma fderiv_within_of_mem (st : t ‚àà ùìù[s] x) (ht : unique_diff_within_at ùïú s x)
  (h : differentiable_within_at ùïú f t x) :
  fderiv_within ùïú f s x = fderiv_within ùïú f t x :=
((differentiable_within_at.has_fderiv_within_at h).mono_of_mem st).fderiv_within ht

lemma fderiv_within_subset (st : s ‚äÜ t) (ht : unique_diff_within_at ùïú s x)
  (h : differentiable_within_at ùïú f t x) :
  fderiv_within ùïú f s x = fderiv_within ùïú f t x :=
fderiv_within_of_mem (nhds_within_mono _ st self_mem_nhds_within) ht h

lemma fderiv_within_inter (ht : t ‚àà ùìù x) :
  fderiv_within ùïú f (s ‚à© t) x = fderiv_within ùïú f s x :=
by simp only [fderiv_within, has_fderiv_within_at_inter ht]

lemma fderiv_within_of_mem_nhds (h : s ‚àà ùìù x) :
  fderiv_within ùïú f s x = fderiv ùïú f x :=
by simp only [fderiv, fderiv_within, has_fderiv_at, has_fderiv_within_at, nhds_within_eq_nhds.2 h]

@[simp] lemma fderiv_within_univ : fderiv_within ùïú f univ = fderiv ùïú f :=
funext $ Œª _, fderiv_within_of_mem_nhds univ_mem

lemma fderiv_within_of_open (hs : is_open s) (hx : x ‚àà s) :
  fderiv_within ùïú f s x = fderiv ùïú f x :=
fderiv_within_of_mem_nhds (hs.mem_nhds hx)

lemma fderiv_within_eq_fderiv (hs : unique_diff_within_at ùïú s x) (h : differentiable_at ùïú f x) :
  fderiv_within ùïú f s x = fderiv ùïú f x :=
begin
  rw ‚Üê fderiv_within_univ,
  exact fderiv_within_subset (subset_univ _) hs h.differentiable_within_at
end

lemma fderiv_mem_iff {f : E ‚Üí F} {s : set (E ‚ÜíL[ùïú] F)} {x : E} :
  fderiv ùïú f x ‚àà s ‚Üî (differentiable_at ùïú f x ‚àß fderiv ùïú f x ‚àà s) ‚à®
    (¬¨differentiable_at ùïú f x ‚àß (0 : E ‚ÜíL[ùïú] F) ‚àà s) :=
by by_cases hx : differentiable_at ùïú f x; simp [fderiv_zero_of_not_differentiable_at, *]

lemma fderiv_within_mem_iff {f : E ‚Üí F} {t : set E} {s : set (E ‚ÜíL[ùïú] F)} {x : E} :
  fderiv_within ùïú f t x ‚àà s ‚Üî (differentiable_within_at ùïú f t x ‚àß fderiv_within ùïú f t x ‚àà s) ‚à®
    (¬¨differentiable_within_at ùïú f t x ‚àß (0 : E ‚ÜíL[ùïú] F) ‚àà s) :=
by by_cases hx : differentiable_within_at ùïú f t x;
  simp [fderiv_within_zero_of_not_differentiable_within_at, *]

lemma asymptotics.is_O.has_fderiv_within_at {s : set E} {x‚ÇÄ : E} {n : ‚Ñï}
  (h : f =O[ùìù[s] x‚ÇÄ] Œª x, ‚Äñx - x‚ÇÄ‚Äñ^n) (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hn : 1 < n) :
  has_fderiv_within_at f (0 : E ‚ÜíL[ùïú] F) s x‚ÇÄ :=
by simp_rw [has_fderiv_within_at, has_fderiv_at_filter,
  h.eq_zero_of_norm_pow_within hx‚ÇÄ $ zero_lt_one.trans hn, zero_apply, sub_zero,
  h.trans_is_o ((is_o_pow_sub_sub x‚ÇÄ hn).mono nhds_within_le_nhds)]

lemma asymptotics.is_O.has_fderiv_at {x‚ÇÄ : E} {n : ‚Ñï}
  (h : f =O[ùìù x‚ÇÄ] Œª x, ‚Äñx - x‚ÇÄ‚Äñ^n) (hn : 1 < n) :
  has_fderiv_at f (0 : E ‚ÜíL[ùïú] F) x‚ÇÄ :=
begin
  rw [‚Üê nhds_within_univ] at h,
  exact (h.has_fderiv_within_at (mem_univ _) hn).has_fderiv_at_of_univ
end

lemma has_fderiv_within_at.is_O {f : E ‚Üí F} {s : set E} {x‚ÇÄ : E} {f' : E ‚ÜíL[ùïú] F}
  (h : has_fderiv_within_at f f' s x‚ÇÄ) :
  (Œª x, f x - f x‚ÇÄ) =O[ùìù[s] x‚ÇÄ] Œª x, x - x‚ÇÄ :=
by simpa only [sub_add_cancel] using h.is_O.add (is_O_sub f' (ùìù[s] x‚ÇÄ) x‚ÇÄ)

lemma has_fderiv_at.is_O {f : E ‚Üí F} {x‚ÇÄ : E} {f' : E ‚ÜíL[ùïú] F} (h : has_fderiv_at f f' x‚ÇÄ) :
  (Œª x, f x - f x‚ÇÄ) =O[ùìù x‚ÇÄ] Œª x, x - x‚ÇÄ :=
by simpa only [sub_add_cancel] using h.is_O.add (is_O_sub f' (ùìù x‚ÇÄ) x‚ÇÄ)

end fderiv_properties

section continuous
/-! ### Deducing continuity from differentiability -/

theorem has_fderiv_at_filter.tendsto_nhds
  (hL : L ‚â§ ùìù x) (h : has_fderiv_at_filter f f' x L) :
  tendsto f L (ùìù (f x)) :=
begin
  have : tendsto (Œª x', f x' - f x) L (ùìù 0),
  { refine h.is_O_sub.trans_tendsto (tendsto.mono_left _ hL),
    rw ‚Üê sub_self x, exact tendsto_id.sub tendsto_const_nhds },
  have := tendsto.add this tendsto_const_nhds,
  rw zero_add (f x) at this,
  exact this.congr (by simp only [sub_add_cancel, eq_self_iff_true, forall_const])
end

theorem has_fderiv_within_at.continuous_within_at
  (h : has_fderiv_within_at f f' s x) : continuous_within_at f s x :=
has_fderiv_at_filter.tendsto_nhds inf_le_left h

theorem has_fderiv_at.continuous_at (h : has_fderiv_at f f' x) :
  continuous_at f x :=
has_fderiv_at_filter.tendsto_nhds le_rfl h

lemma differentiable_within_at.continuous_within_at (h : differentiable_within_at ùïú f s x) :
  continuous_within_at f s x :=
let ‚ü®f', hf'‚ü© := h in hf'.continuous_within_at

lemma differentiable_at.continuous_at (h : differentiable_at ùïú f x) : continuous_at f x :=
let ‚ü®f', hf'‚ü© := h in hf'.continuous_at

lemma differentiable_on.continuous_on (h : differentiable_on ùïú f s) : continuous_on f s :=
Œªx hx, (h x hx).continuous_within_at

lemma differentiable.continuous (h : differentiable ùïú f) : continuous f :=
continuous_iff_continuous_at.2 $ Œªx, (h x).continuous_at

protected lemma has_strict_fderiv_at.continuous_at (hf : has_strict_fderiv_at f f' x) :
  continuous_at f x :=
hf.has_fderiv_at.continuous_at

lemma has_strict_fderiv_at.is_O_sub_rev {f' : E ‚âÉL[ùïú] F}
  (hf : has_strict_fderiv_at f (f' : E ‚ÜíL[ùïú] F) x) :
  (Œª p : E √ó E, p.1 - p.2) =O[ùìù (x, x)](Œª p : E √ó E, f p.1 - f p.2) :=
((f'.is_O_comp_rev _ _).trans (hf.trans_is_O (f'.is_O_comp_rev _ _)).right_is_O_add).congr
(Œª _, rfl) (Œª _, sub_add_cancel _ _)

lemma has_fderiv_at_filter.is_O_sub_rev (hf : has_fderiv_at_filter f f' x L) {C}
  (hf' : antilipschitz_with C f') :
  (Œª x', x' - x) =O[L] (Œª x', f x' - f x) :=
have (Œª x', x' - x) =O[L] (Œª x', f' (x' - x)),
  from is_O_iff.2 ‚ü®C, eventually_of_forall $ Œª x',
    add_monoid_hom_class.bound_of_antilipschitz f' hf' _‚ü©,
(this.trans (hf.trans_is_O this).right_is_O_add).congr (Œª _, rfl) (Œª _, sub_add_cancel _ _)

end continuous

section congr
/-! ### congr properties of the derivative -/

lemma has_fderiv_within_at_congr_set' (y : E) (h : s =·∂†[ùìù[{y}·∂ú] x] t) :
  has_fderiv_within_at f f' s x ‚Üî has_fderiv_within_at f f' t x :=
calc has_fderiv_within_at f f' s x ‚Üî has_fderiv_within_at f f' (s \ {y}) x :
  (has_fderiv_within_at_diff_singleton _).symm
... ‚Üî has_fderiv_within_at f f' (t \ {y}) x :
  suffices ùìù[s \ {y}] x = ùìù[t \ {y}] x, by simp only [has_fderiv_within_at, this],
  by simpa only [set_eventually_eq_iff_inf_principal, ‚Üê nhds_within_inter', diff_eq, inter_comm]
    using h
... ‚Üî has_fderiv_within_at f f' t x : has_fderiv_within_at_diff_singleton _

lemma has_fderiv_within_at_congr_set (h : s =·∂†[ùìù x] t) :
  has_fderiv_within_at f f' s x ‚Üî has_fderiv_within_at f f' t x :=
has_fderiv_within_at_congr_set' x $ h.filter_mono inf_le_left

lemma differentiable_within_at_congr_set' (y : E) (h : s =·∂†[ùìù[{y}·∂ú] x] t) :
  differentiable_within_at ùïú f s x ‚Üî differentiable_within_at ùïú f t x :=
exists_congr $ Œª _, has_fderiv_within_at_congr_set' _ h

lemma differentiable_within_at_congr_set (h : s =·∂†[ùìù x] t) :
  differentiable_within_at ùïú f s x ‚Üî differentiable_within_at ùïú f t x :=
exists_congr $ Œª _, has_fderiv_within_at_congr_set h

lemma fderiv_within_congr_set' (y : E) (h : s =·∂†[ùìù[{y}·∂ú] x] t) :
  fderiv_within ùïú f s x = fderiv_within ùïú f t x :=
by simp only [fderiv_within, has_fderiv_within_at_congr_set' y h]

lemma fderiv_within_congr_set (h : s =·∂†[ùìù x] t) :
  fderiv_within ùïú f s x = fderiv_within ùïú f t x :=
fderiv_within_congr_set' x $ h.filter_mono inf_le_left

lemma fderiv_within_eventually_congr_set' (y : E) (h : s =·∂†[ùìù[{y}·∂ú] x] t) :
  fderiv_within ùïú f s =·∂†[ùìù x] fderiv_within ùïú f t :=
(eventually_nhds_nhds_within.2 h).mono $ Œª _, fderiv_within_congr_set' y

lemma fderiv_within_eventually_congr_set (h : s =·∂†[ùìù x] t) :
  fderiv_within ùïú f s =·∂†[ùìù x] fderiv_within ùïú f t :=
fderiv_within_eventually_congr_set' x $ h.filter_mono inf_le_left

theorem filter.eventually_eq.has_strict_fderiv_at_iff
  (h : f‚ÇÄ =·∂†[ùìù x] f‚ÇÅ) (h' : ‚àÄ y, f‚ÇÄ' y = f‚ÇÅ' y) :
  has_strict_fderiv_at f‚ÇÄ f‚ÇÄ' x ‚Üî has_strict_fderiv_at f‚ÇÅ f‚ÇÅ' x :=
begin
  refine is_o_congr ((h.prod_mk_nhds h).mono _) (eventually_of_forall $ Œª _, rfl),
  rintros p ‚ü®hp‚ÇÅ, hp‚ÇÇ‚ü©,
  simp only [*]
end

theorem has_strict_fderiv_at.congr_of_eventually_eq (h : has_strict_fderiv_at f f' x)
  (h‚ÇÅ : f =·∂†[ùìù x] f‚ÇÅ) : has_strict_fderiv_at f‚ÇÅ f' x :=
(h‚ÇÅ.has_strict_fderiv_at_iff (Œª _, rfl)).1 h

theorem filter.eventually_eq.has_fderiv_at_filter_iff
  (h‚ÇÄ : f‚ÇÄ =·∂†[L] f‚ÇÅ) (hx : f‚ÇÄ x = f‚ÇÅ x) (h‚ÇÅ : ‚àÄ x, f‚ÇÄ' x = f‚ÇÅ' x) :
  has_fderiv_at_filter f‚ÇÄ f‚ÇÄ' x L ‚Üî has_fderiv_at_filter f‚ÇÅ f‚ÇÅ' x L :=
is_o_congr (h‚ÇÄ.mono $ Œª y hy, by simp only [hy, h‚ÇÅ, hx]) (eventually_of_forall $ Œª _, rfl)

lemma has_fderiv_at_filter.congr_of_eventually_eq (h : has_fderiv_at_filter f f' x L)
  (hL : f‚ÇÅ =·∂†[L] f) (hx : f‚ÇÅ x = f x) : has_fderiv_at_filter f‚ÇÅ f' x L :=
(hL.has_fderiv_at_filter_iff hx $ Œª _, rfl).2 h

theorem filter.eventually_eq.has_fderiv_at_iff (h : f‚ÇÄ =·∂†[ùìù x] f‚ÇÅ) :
  has_fderiv_at f‚ÇÄ f' x ‚Üî has_fderiv_at f‚ÇÅ f' x :=
h.has_fderiv_at_filter_iff h.eq_of_nhds (Œª _, rfl)

theorem filter.eventually_eq.differentiable_at_iff (h : f‚ÇÄ =·∂†[ùìù x] f‚ÇÅ) :
  differentiable_at ùïú f‚ÇÄ x ‚Üî differentiable_at ùïú f‚ÇÅ x :=
exists_congr $ Œª f', h.has_fderiv_at_iff

theorem filter.eventually_eq.has_fderiv_within_at_iff (h : f‚ÇÄ =·∂†[ùìù[s] x] f‚ÇÅ) (hx : f‚ÇÄ x = f‚ÇÅ x) :
  has_fderiv_within_at f‚ÇÄ f' s x ‚Üî has_fderiv_within_at f‚ÇÅ f' s x :=
h.has_fderiv_at_filter_iff hx (Œª _, rfl)

theorem filter.eventually_eq.has_fderiv_within_at_iff_of_mem (h : f‚ÇÄ =·∂†[ùìù[s] x] f‚ÇÅ) (hx : x ‚àà s) :
  has_fderiv_within_at f‚ÇÄ f' s x ‚Üî has_fderiv_within_at f‚ÇÅ f' s x :=
h.has_fderiv_within_at_iff (h.eq_of_nhds_within hx)

theorem filter.eventually_eq.differentiable_within_at_iff (h : f‚ÇÄ =·∂†[ùìù[s] x] f‚ÇÅ)
  (hx : f‚ÇÄ x = f‚ÇÅ x) :
  differentiable_within_at ùïú f‚ÇÄ s x ‚Üî differentiable_within_at ùïú f‚ÇÅ s x :=
exists_congr $ Œª f', h.has_fderiv_within_at_iff hx

theorem filter.eventually_eq.differentiable_within_at_iff_of_mem (h : f‚ÇÄ =·∂†[ùìù[s] x] f‚ÇÅ)
  (hx : x ‚àà s) :
  differentiable_within_at ùïú f‚ÇÄ s x ‚Üî differentiable_within_at ùïú f‚ÇÅ s x :=
h.differentiable_within_at_iff (h.eq_of_nhds_within hx)

lemma has_fderiv_within_at.congr_mono (h : has_fderiv_within_at f f' s x) (ht : eq_on f‚ÇÅ f t)
  (hx : f‚ÇÅ x = f x) (h‚ÇÅ : t ‚äÜ s) : has_fderiv_within_at f‚ÇÅ f' t x :=
has_fderiv_at_filter.congr_of_eventually_eq (h.mono h‚ÇÅ) (filter.mem_inf_of_right ht) hx

lemma has_fderiv_within_at.congr (h : has_fderiv_within_at f f' s x) (hs : eq_on f‚ÇÅ f s)
  (hx : f‚ÇÅ x = f x) : has_fderiv_within_at f‚ÇÅ f' s x :=
h.congr_mono hs hx (subset.refl _)

lemma has_fderiv_within_at.congr' (h : has_fderiv_within_at f f' s x) (hs : eq_on f‚ÇÅ f s)
  (hx : x ‚àà s) : has_fderiv_within_at f‚ÇÅ f' s x :=
h.congr hs (hs hx)

lemma has_fderiv_within_at.congr_of_eventually_eq (h : has_fderiv_within_at f f' s x)
  (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù[s] x] f) (hx : f‚ÇÅ x = f x) : has_fderiv_within_at f‚ÇÅ f' s x :=
has_fderiv_at_filter.congr_of_eventually_eq h h‚ÇÅ hx

lemma has_fderiv_at.congr_of_eventually_eq (h : has_fderiv_at f f' x)
  (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù x] f) : has_fderiv_at f‚ÇÅ f' x :=
has_fderiv_at_filter.congr_of_eventually_eq h h‚ÇÅ (mem_of_mem_nhds h‚ÇÅ : _)

lemma differentiable_within_at.congr_mono (h : differentiable_within_at ùïú f s x)
  (ht : eq_on f‚ÇÅ f t) (hx : f‚ÇÅ x = f x) (h‚ÇÅ : t ‚äÜ s) : differentiable_within_at ùïú f‚ÇÅ t x :=
(h.has_fderiv_within_at.congr_mono ht hx h‚ÇÅ).differentiable_within_at

lemma differentiable_within_at.congr (h : differentiable_within_at ùïú f s x)
  (ht : ‚àÄx ‚àà s, f‚ÇÅ x = f x) (hx : f‚ÇÅ x = f x) : differentiable_within_at ùïú f‚ÇÅ s x :=
differentiable_within_at.congr_mono h ht hx (subset.refl _)

lemma differentiable_within_at.congr_of_eventually_eq
  (h : differentiable_within_at ùïú f s x) (h‚ÇÅ : f‚ÇÅ =·∂†[ùìù[s] x] f)
  (hx : f‚ÇÅ x = f x) : differentiable_within_at ùïú f‚ÇÅ s x :=
(h.has_fderiv_within_at.congr_of_eventually_eq h‚ÇÅ hx).differentiable_within_at

lemma differentiable_on.congr_mono (h : differentiable_on ùïú f s) (h' : ‚àÄx ‚àà t, f‚ÇÅ x = f x)
  (h‚ÇÅ : t ‚äÜ s) : differentiable_on ùïú f‚ÇÅ t :=
Œª x hx, (h x (h‚ÇÅ hx)).congr_mono h' (h' x hx) h‚ÇÅ

lemma differentiable_on.congr (h : differentiable_on ùïú f s) (h' : ‚àÄx ‚àà s, f‚ÇÅ x = f x) :
  differentiable_on ùïú f‚ÇÅ s :=
Œª x hx, (h x hx).congr h' (h' x hx)

lemma differentiable_on_congr (h' : ‚àÄx ‚àà s, f‚ÇÅ x = f x) :
  differentiable_on ùïú f‚ÇÅ s ‚Üî differentiable_on ùïú f s :=
‚ü®Œª h, differentiable_on.congr h (Œªy hy, (h' y hy).symm),
Œª h, differentiable_on.congr h h'‚ü©

lemma differentiable_at.congr_of_eventually_eq (h : differentiable_at ùïú f x) (hL : f‚ÇÅ =·∂†[ùìù x] f) :
  differentiable_at ùïú f‚ÇÅ x :=
hL.differentiable_at_iff.2 h

lemma differentiable_within_at.fderiv_within_congr_mono (h : differentiable_within_at ùïú f s x)
  (hs : eq_on f‚ÇÅ f t) (hx : f‚ÇÅ x = f x) (hxt : unique_diff_within_at ùïú t x) (h‚ÇÅ : t ‚äÜ s) :
  fderiv_within ùïú f‚ÇÅ t x = fderiv_within ùïú f s x :=
(has_fderiv_within_at.congr_mono h.has_fderiv_within_at hs hx h‚ÇÅ).fderiv_within hxt

lemma filter.eventually_eq.fderiv_within_eq (hs : f‚ÇÅ =·∂†[ùìù[s] x] f) (hx : f‚ÇÅ x = f x) :
  fderiv_within ùïú f‚ÇÅ s x = fderiv_within ùïú f s x :=
by simp only [fderiv_within, hs.has_fderiv_within_at_iff hx]

lemma filter.eventually_eq.fderiv_within' (hs : f‚ÇÅ =·∂†[ùìù[s] x] f) (ht : t ‚äÜ s) :
  fderiv_within ùïú f‚ÇÅ t =·∂†[ùìù[s] x] fderiv_within ùïú f t :=
(eventually_nhds_within_nhds_within.2 hs).mp $ eventually_mem_nhds_within.mono $ Œª y hys hs,
  filter.eventually_eq.fderiv_within_eq (hs.filter_mono $ nhds_within_mono _ ht)
    (hs.self_of_nhds_within hys)

protected lemma filter.eventually_eq.fderiv_within (hs : f‚ÇÅ =·∂†[ùìù[s] x] f) :
  fderiv_within ùïú f‚ÇÅ s =·∂†[ùìù[s] x] fderiv_within ùïú f s :=
hs.fderiv_within' subset.rfl

lemma filter.eventually_eq.fderiv_within_eq_nhds (h : f‚ÇÅ =·∂†[ùìù x] f) :
  fderiv_within ùïú f‚ÇÅ s x = fderiv_within ùïú f s x :=
(h.filter_mono nhds_within_le_nhds).fderiv_within_eq h.self_of_nhds

lemma fderiv_within_congr (hs : eq_on f‚ÇÅ f s) (hx : f‚ÇÅ x = f x) :
  fderiv_within ùïú f‚ÇÅ s x = fderiv_within ùïú f s x :=
(hs.eventually_eq.filter_mono inf_le_right).fderiv_within_eq hx

lemma fderiv_within_congr' (hs : eq_on f‚ÇÅ f s) (hx : x ‚àà s) :
  fderiv_within ùïú f‚ÇÅ s x = fderiv_within ùïú f s x :=
fderiv_within_congr hs (hs hx)

lemma filter.eventually_eq.fderiv_eq (h : f‚ÇÅ =·∂†[ùìù x] f) :
  fderiv ùïú f‚ÇÅ x = fderiv ùïú f x :=
by rw [‚Üê fderiv_within_univ, ‚Üê fderiv_within_univ, h.fderiv_within_eq_nhds]

protected lemma filter.eventually_eq.fderiv (h : f‚ÇÅ =·∂†[ùìù x] f) :
  fderiv ùïú f‚ÇÅ =·∂†[ùìù x] fderiv ùïú f :=
h.eventually_eq_nhds.mono $ Œª x h, h.fderiv_eq

end congr

section id
/-! ### Derivative of the identity -/

theorem has_strict_fderiv_at_id (x : E) :
  has_strict_fderiv_at id (id ùïú E) x :=
(is_o_zero _ _).congr_left $ by simp

theorem has_fderiv_at_filter_id (x : E) (L : filter E) :
  has_fderiv_at_filter id (id ùïú E) x L :=
(is_o_zero _ _).congr_left $ by simp

theorem has_fderiv_within_at_id (x : E) (s : set E) :
  has_fderiv_within_at id (id ùïú E) s x :=
has_fderiv_at_filter_id _ _

theorem has_fderiv_at_id (x : E) : has_fderiv_at id (id ùïú E) x :=
has_fderiv_at_filter_id _ _

@[simp] lemma differentiable_at_id : differentiable_at ùïú id x :=
(has_fderiv_at_id x).differentiable_at

@[simp] lemma differentiable_at_id' : differentiable_at ùïú (Œª x, x) x :=
(has_fderiv_at_id x).differentiable_at

lemma differentiable_within_at_id : differentiable_within_at ùïú id s x :=
differentiable_at_id.differentiable_within_at

@[simp] lemma differentiable_id : differentiable ùïú (id : E ‚Üí E) :=
Œªx, differentiable_at_id

@[simp] lemma differentiable_id' : differentiable ùïú (Œª (x : E), x) :=
Œªx, differentiable_at_id

lemma differentiable_on_id : differentiable_on ùïú id s :=
differentiable_id.differentiable_on

lemma fderiv_id : fderiv ùïú id x = id ùïú E :=
has_fderiv_at.fderiv (has_fderiv_at_id x)

@[simp] lemma fderiv_id' : fderiv ùïú (Œª (x : E), x) x = continuous_linear_map.id ùïú E :=
fderiv_id

lemma fderiv_within_id (hxs : unique_diff_within_at ùïú s x) :
  fderiv_within ùïú id s x = id ùïú E :=
begin
  rw differentiable_at.fderiv_within (differentiable_at_id) hxs,
  exact fderiv_id
end

lemma fderiv_within_id' (hxs : unique_diff_within_at ùïú s x) :
  fderiv_within ùïú (Œª (x : E), x) s x = continuous_linear_map.id ùïú E :=
fderiv_within_id hxs

end id

section const
/-! ### derivative of a constant function -/

theorem has_strict_fderiv_at_const (c : F) (x : E) :
  has_strict_fderiv_at (Œª _, c) (0 : E ‚ÜíL[ùïú] F) x :=
(is_o_zero _ _).congr_left $ Œª _, by simp only [zero_apply, sub_self]

theorem has_fderiv_at_filter_const (c : F) (x : E) (L : filter E) :
  has_fderiv_at_filter (Œª x, c) (0 : E ‚ÜíL[ùïú] F) x L :=
(is_o_zero _ _).congr_left $ Œª _, by simp only [zero_apply, sub_self]

theorem has_fderiv_within_at_const (c : F) (x : E) (s : set E) :
  has_fderiv_within_at (Œª x, c) (0 : E ‚ÜíL[ùïú] F) s x :=
has_fderiv_at_filter_const _ _ _

theorem has_fderiv_at_const (c : F) (x : E) :
  has_fderiv_at (Œª x, c) (0 : E ‚ÜíL[ùïú] F) x :=
has_fderiv_at_filter_const _ _ _

@[simp] lemma differentiable_at_const (c : F) : differentiable_at ùïú (Œªx, c) x :=
‚ü®0, has_fderiv_at_const c x‚ü©

lemma differentiable_within_at_const (c : F) : differentiable_within_at ùïú (Œªx, c) s x :=
differentiable_at.differentiable_within_at (differentiable_at_const _)

lemma fderiv_const_apply (c : F) : fderiv ùïú (Œªy, c) x = 0 :=
has_fderiv_at.fderiv (has_fderiv_at_const c x)

@[simp] lemma fderiv_const (c : F) : fderiv ùïú (Œª (y : E), c) = 0 :=
by { ext m, rw fderiv_const_apply, refl }

lemma fderiv_within_const_apply (c : F) (hxs : unique_diff_within_at ùïú s x) :
  fderiv_within ùïú (Œªy, c) s x = 0 :=
begin
  rw differentiable_at.fderiv_within (differentiable_at_const _) hxs,
  exact fderiv_const_apply _
end

@[simp] lemma differentiable_const (c : F) : differentiable ùïú (Œªx : E, c) :=
Œªx, differentiable_at_const _

lemma differentiable_on_const (c : F) : differentiable_on ùïú (Œªx, c) s :=
(differentiable_const _).differentiable_on

lemma has_fderiv_within_at_singleton (f : E ‚Üí F) (x : E) :
  has_fderiv_within_at f (0 : E ‚ÜíL[ùïú] F) {x} x :=
by simp only [has_fderiv_within_at, nhds_within_singleton, has_fderiv_at_filter, is_o_pure,
  continuous_linear_map.zero_apply, sub_self]

lemma has_fderiv_at_of_subsingleton [h : subsingleton E] (f : E ‚Üí F) (x : E) :
  has_fderiv_at f (0 : E ‚ÜíL[ùïú] F) x :=
begin
  rw [‚Üê has_fderiv_within_at_univ, subsingleton_univ.eq_singleton_of_mem (mem_univ x)],
  exact has_fderiv_within_at_singleton f x
end

lemma differentiable_on_empty : differentiable_on ùïú f ‚àÖ := Œª x, false.elim

lemma differentiable_on_singleton : differentiable_on ùïú f {x} :=
forall_eq.2 (has_fderiv_within_at_singleton f x).differentiable_within_at

lemma set.subsingleton.differentiable_on (hs : s.subsingleton) : differentiable_on ùïú f s :=
hs.induction_on differentiable_on_empty (Œª x, differentiable_on_singleton)

lemma has_fderiv_at_zero_of_eventually_const
  (c : F) (hf : f =·∂†[ùìù x] (Œª y, c)) :
  has_fderiv_at f (0 : E ‚ÜíL[ùïú] F) x :=
(has_fderiv_at_const _ _).congr_of_eventually_eq hf

end const

end

/-! ### Support of derivatives -/

section support

open function
variables (ùïú : Type*) {E F : Type*} [nontrivially_normed_field ùïú] [normed_add_comm_group E]
  [normed_space ùïú E] [normed_add_comm_group F] [normed_space ùïú F] {f : E ‚Üí F}

lemma support_fderiv_subset : support (fderiv ùïú f) ‚äÜ tsupport f :=
begin
  intros x,
  rw [‚Üê not_imp_not, not_mem_tsupport_iff_eventually_eq, nmem_support],
  exact Œª hx, (hx.fderiv_eq.trans $ fderiv_const_apply 0),
end

lemma tsupport_fderiv_subset : tsupport (fderiv ùïú f) ‚äÜ tsupport f :=
closure_minimal (support_fderiv_subset ùïú) is_closed_closure

lemma has_compact_support.fderiv (hf : has_compact_support f) : has_compact_support (fderiv ùïú f) :=
hf.mono' $ support_fderiv_subset ùïú

end support
