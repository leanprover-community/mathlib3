/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import analysis.calculus.fderiv.basic
import analysis.calculus.fderiv.linear
import analysis.calculus.fderiv.comp
import analysis.calculus.fderiv.prod

open filter asymptotics continuous_linear_map set metric
open_locale topology classical nnreal filter asymptotics ennreal

noncomputable theory


section

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
variables {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
variables {G : Type*} [normed_add_comm_group G] [normed_space ğ•œ G]
variables {G' : Type*} [normed_add_comm_group G'] [normed_space ğ•œ G']

variables {f fâ‚€ fâ‚ g : E â†’ F}
variables {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variables (e : E â†’L[ğ•œ] F)
variables {x : E}
variables {s t : set E}
variables {L Lâ‚ Lâ‚‚ : filter E}

section const_smul

variables {R : Type*} [semiring R] [module R F] [smul_comm_class ğ•œ R F]
  [has_continuous_const_smul R F]

/-! ### Derivative of a function multiplied by a constant -/
theorem has_strict_fderiv_at.const_smul (h : has_strict_fderiv_at f f' x) (c : R) :
  has_strict_fderiv_at (Î» x, c â€¢ f x) (c â€¢ f') x :=
(c â€¢ (1 : F â†’L[ğ•œ] F)).has_strict_fderiv_at.comp x h

theorem has_fderiv_at_filter.const_smul (h : has_fderiv_at_filter f f' x L) (c : R) :
  has_fderiv_at_filter (Î» x, c â€¢ f x) (c â€¢ f') x L :=
(c â€¢ (1 : F â†’L[ğ•œ] F)).has_fderiv_at_filter.comp x h tendsto_map

theorem has_fderiv_within_at.const_smul (h : has_fderiv_within_at f f' s x) (c : R) :
  has_fderiv_within_at (Î» x, c â€¢ f x) (c â€¢ f') s x :=
h.const_smul c

theorem has_fderiv_at.const_smul (h : has_fderiv_at f f' x) (c : R) :
  has_fderiv_at (Î» x, c â€¢ f x) (c â€¢ f') x :=
h.const_smul c

lemma differentiable_within_at.const_smul (h : differentiable_within_at ğ•œ f s x) (c : R) :
  differentiable_within_at ğ•œ (Î»y, c â€¢ f y) s x :=
(h.has_fderiv_within_at.const_smul c).differentiable_within_at

lemma differentiable_at.const_smul (h : differentiable_at ğ•œ f x) (c : R) :
  differentiable_at ğ•œ (Î»y, c â€¢ f y) x :=
(h.has_fderiv_at.const_smul c).differentiable_at

lemma differentiable_on.const_smul (h : differentiable_on ğ•œ f s) (c : R) :
  differentiable_on ğ•œ (Î»y, c â€¢ f y) s :=
Î»x hx, (h x hx).const_smul c

lemma differentiable.const_smul (h : differentiable ğ•œ f) (c : R) :
  differentiable ğ•œ (Î»y, c â€¢ f y) :=
Î»x, (h x).const_smul c

lemma fderiv_within_const_smul (hxs : unique_diff_within_at ğ•œ s x)
  (h : differentiable_within_at ğ•œ f s x) (c : R) :
  fderiv_within ğ•œ (Î»y, c â€¢ f y) s x = c â€¢ fderiv_within ğ•œ f s x :=
(h.has_fderiv_within_at.const_smul c).fderiv_within hxs

lemma fderiv_const_smul (h : differentiable_at ğ•œ f x) (c : R) :
  fderiv ğ•œ (Î»y, c â€¢ f y) x = c â€¢ fderiv ğ•œ f x :=
(h.has_fderiv_at.const_smul c).fderiv

end const_smul

section add

/-! ### Derivative of the sum of two functions -/

theorem has_strict_fderiv_at.add (hf : has_strict_fderiv_at f f' x)
  (hg : has_strict_fderiv_at g g' x) :
  has_strict_fderiv_at (Î» y, f y + g y) (f' + g') x :=
(hf.add hg).congr_left $ Î» y,
  by { simp only [linear_map.sub_apply, linear_map.add_apply, map_sub, map_add, add_apply], abel }

theorem has_fderiv_at_filter.add
  (hf : has_fderiv_at_filter f f' x L) (hg : has_fderiv_at_filter g g' x L) :
  has_fderiv_at_filter (Î» y, f y + g y) (f' + g') x L :=
(hf.add hg).congr_left $ Î» _,
  by { simp only [linear_map.sub_apply, linear_map.add_apply, map_sub, map_add, add_apply], abel }

theorem has_fderiv_within_at.add
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» y, f y + g y) (f' + g') s x :=
hf.add hg

theorem has_fderiv_at.add
  (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» x, f x + g x) (f' + g') x :=
hf.add hg

lemma differentiable_within_at.add
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  differentiable_within_at ğ•œ (Î» y, f y + g y) s x :=
(hf.has_fderiv_within_at.add hg.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  differentiable_at ğ•œ (Î» y, f y + g y) x :=
(hf.has_fderiv_at.add hg.has_fderiv_at).differentiable_at

lemma differentiable_on.add
  (hf : differentiable_on ğ•œ f s) (hg : differentiable_on ğ•œ g s) :
  differentiable_on ğ•œ (Î»y, f y + g y) s :=
Î»x hx, (hf x hx).add (hg x hx)

@[simp] lemma differentiable.add
  (hf : differentiable ğ•œ f) (hg : differentiable ğ•œ g) :
  differentiable ğ•œ (Î»y, f y + g y) :=
Î»x, (hf x).add (hg x)

lemma fderiv_within_add (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  fderiv_within ğ•œ (Î»y, f y + g y) s x = fderiv_within ğ•œ f s x + fderiv_within ğ•œ g s x :=
(hf.has_fderiv_within_at.add hg.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_add
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  fderiv ğ•œ (Î»y, f y + g y) x = fderiv ğ•œ f x + fderiv ğ•œ g x :=
(hf.has_fderiv_at.add hg.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.add_const (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» y, f y + c) f' x :=
add_zero f' â–¸ hf.add (has_strict_fderiv_at_const _ _)

theorem has_fderiv_at_filter.add_const
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» y, f y + c) f' x L :=
add_zero f' â–¸ hf.add (has_fderiv_at_filter_const _ _ _)

theorem has_fderiv_within_at.add_const
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» y, f y + c) f' s x :=
hf.add_const c

theorem has_fderiv_at.add_const (hf : has_fderiv_at f f' x) (c : F):
  has_fderiv_at (Î» x, f x + c) f' x :=
hf.add_const c

lemma differentiable_within_at.add_const
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y + c) s x :=
(hf.has_fderiv_within_at.add_const c).differentiable_within_at

@[simp] lemma differentiable_within_at_add_const_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y + c) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma differentiable_at.add_const
  (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, f y + c) x :=
(hf.has_fderiv_at.add_const c).differentiable_at

@[simp] lemma differentiable_at_add_const_iff (c : F) :
  differentiable_at ğ•œ (Î» y, f y + c) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma differentiable_on.add_const
  (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, f y + c) s :=
Î»x hx, (hf x hx).add_const c

@[simp] lemma differentiable_on_add_const_iff (c : F) :
  differentiable_on ğ•œ (Î» y, f y + c) s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma differentiable.add_const
  (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, f y + c) :=
Î»x, (hf x).add_const c

@[simp] lemma differentiable_add_const_iff (c : F) :
  differentiable ğ•œ (Î» y, f y + c) â†” differentiable ğ•œ f :=
âŸ¨Î» h, by simpa using h.add_const (-c), Î» h, h.add_const câŸ©

lemma fderiv_within_add_const (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, f y + c) s x = fderiv_within ğ•œ f s x :=
if hf : differentiable_within_at ğ•œ f s x
then (hf.has_fderiv_within_at.add_const c).fderiv_within hxs
else by { rw [fderiv_within_zero_of_not_differentiable_within_at hf,
  fderiv_within_zero_of_not_differentiable_within_at], simpa }

lemma fderiv_add_const (c : F) : fderiv ğ•œ (Î»y, f y + c) x = fderiv ğ•œ f x :=
by simp only [â† fderiv_within_univ, fderiv_within_add_const unique_diff_within_at_univ]

theorem has_strict_fderiv_at.const_add (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» y, c + f y) f' x :=
zero_add f' â–¸ (has_strict_fderiv_at_const _ _).add hf

theorem has_fderiv_at_filter.const_add
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» y, c + f y) f' x L :=
zero_add f' â–¸ (has_fderiv_at_filter_const _ _ _).add hf

theorem has_fderiv_within_at.const_add
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» y, c + f y) f' s x :=
hf.const_add c

theorem has_fderiv_at.const_add
  (hf : has_fderiv_at f f' x) (c : F):
  has_fderiv_at (Î» x, c + f x) f' x :=
hf.const_add c

lemma differentiable_within_at.const_add
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, c + f y) s x :=
(hf.has_fderiv_within_at.const_add c).differentiable_within_at

@[simp] lemma differentiable_within_at_const_add_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, c + f y) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma differentiable_at.const_add
  (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, c + f y) x :=
(hf.has_fderiv_at.const_add c).differentiable_at

@[simp] lemma differentiable_at_const_add_iff (c : F) :
  differentiable_at ğ•œ (Î» y, c + f y) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma differentiable_on.const_add (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, c + f y) s :=
Î»x hx, (hf x hx).const_add c

@[simp] lemma differentiable_on_const_add_iff (c : F) :
  differentiable_on ğ•œ (Î» y, c + f y) s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma differentiable.const_add (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, c + f y) :=
Î»x, (hf x).const_add c

@[simp] lemma differentiable_const_add_iff (c : F) :
  differentiable ğ•œ (Î» y, c + f y) â†” differentiable ğ•œ f :=
âŸ¨Î» h, by simpa using h.const_add (-c), Î» h, h.const_add câŸ©

lemma fderiv_within_const_add (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, c + f y) s x = fderiv_within ğ•œ f s x :=
by simpa only [add_comm] using fderiv_within_add_const hxs c

lemma fderiv_const_add (c : F) : fderiv ğ•œ (Î»y, c + f y) x = fderiv ğ•œ f x :=
by simp only [add_comm c, fderiv_add_const]

end add

section sum
/-! ### Derivative of a finite sum of functions -/

open_locale big_operators

variables {Î¹ : Type*} {u : finset Î¹} {A : Î¹ â†’ (E â†’ F)} {A' : Î¹ â†’ (E â†’L[ğ•œ] F)}

theorem has_strict_fderiv_at.sum (h : âˆ€ i âˆˆ u, has_strict_fderiv_at (A i) (A' i) x) :
  has_strict_fderiv_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x :=
begin
  dsimp [has_strict_fderiv_at] at *,
  convert is_o.sum h,
  simp [finset.sum_sub_distrib, continuous_linear_map.sum_apply]
end

theorem has_fderiv_at_filter.sum (h : âˆ€ i âˆˆ u, has_fderiv_at_filter (A i) (A' i) x L) :
  has_fderiv_at_filter (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x L :=
begin
  dsimp [has_fderiv_at_filter] at *,
  convert is_o.sum h,
  simp [continuous_linear_map.sum_apply]
end

theorem has_fderiv_within_at.sum (h : âˆ€ i âˆˆ u, has_fderiv_within_at (A i) (A' i) s x) :
  has_fderiv_within_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) s x :=
has_fderiv_at_filter.sum h

theorem has_fderiv_at.sum (h : âˆ€ i âˆˆ u, has_fderiv_at (A i) (A' i) x) :
  has_fderiv_at (Î» y, âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x :=
has_fderiv_at_filter.sum h

theorem differentiable_within_at.sum (h : âˆ€ i âˆˆ u, differentiable_within_at ğ•œ (A i) s x) :
  differentiable_within_at ğ•œ (Î» y, âˆ‘ i in u, A i y) s x :=
has_fderiv_within_at.differentiable_within_at $ has_fderiv_within_at.sum $
Î» i hi, (h i hi).has_fderiv_within_at

@[simp] theorem differentiable_at.sum (h : âˆ€ i âˆˆ u, differentiable_at ğ•œ (A i) x) :
  differentiable_at ğ•œ (Î» y, âˆ‘ i in u, A i y) x :=
has_fderiv_at.differentiable_at $ has_fderiv_at.sum $ Î» i hi, (h i hi).has_fderiv_at

theorem differentiable_on.sum (h : âˆ€ i âˆˆ u, differentiable_on ğ•œ (A i) s) :
  differentiable_on ğ•œ (Î» y, âˆ‘ i in u, A i y) s :=
Î» x hx, differentiable_within_at.sum $ Î» i hi, h i hi x hx

@[simp] theorem differentiable.sum (h : âˆ€ i âˆˆ u, differentiable ğ•œ (A i)) :
  differentiable ğ•œ (Î» y, âˆ‘ i in u, A i y) :=
Î» x, differentiable_at.sum $ Î» i hi, h i hi x

theorem fderiv_within_sum (hxs : unique_diff_within_at ğ•œ s x)
  (h : âˆ€ i âˆˆ u, differentiable_within_at ğ•œ (A i) s x) :
  fderiv_within ğ•œ (Î» y, âˆ‘ i in u, A i y) s x = (âˆ‘ i in u, fderiv_within ğ•œ (A i) s x) :=
(has_fderiv_within_at.sum (Î» i hi, (h i hi).has_fderiv_within_at)).fderiv_within hxs

theorem fderiv_sum (h : âˆ€ i âˆˆ u, differentiable_at ğ•œ (A i) x) :
  fderiv ğ•œ (Î» y, âˆ‘ i in u, A i y) x = (âˆ‘ i in u, fderiv ğ•œ (A i) x) :=
(has_fderiv_at.sum (Î» i hi, (h i hi).has_fderiv_at)).fderiv

end sum

section neg
/-! ### Derivative of the negative of a function -/

theorem has_strict_fderiv_at.neg (h : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Î» x, -f x) (-f') x :=
(-1 : F â†’L[ğ•œ] F).has_strict_fderiv_at.comp x h

theorem has_fderiv_at_filter.neg (h : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (Î» x, -f x) (-f') x L :=
(-1 : F â†’L[ğ•œ] F).has_fderiv_at_filter.comp x h tendsto_map

theorem has_fderiv_within_at.neg (h : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Î» x, -f x) (-f') s x :=
h.neg

theorem has_fderiv_at.neg (h : has_fderiv_at f f' x) :
  has_fderiv_at (Î» x, -f x) (-f') x :=
h.neg

lemma differentiable_within_at.neg (h : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î»y, -f y) s x :=
h.has_fderiv_within_at.neg.differentiable_within_at

@[simp] lemma differentiable_within_at_neg_iff :
  differentiable_within_at ğ•œ (Î»y, -f y) s x â†” differentiable_within_at ğ•œ f s x :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma differentiable_at.neg (h : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î»y, -f y) x :=
h.has_fderiv_at.neg.differentiable_at

@[simp] lemma differentiable_at_neg_iff :
  differentiable_at ğ•œ (Î»y, -f y) x â†” differentiable_at ğ•œ f x :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma differentiable_on.neg (h : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î»y, -f y) s :=
Î»x hx, (h x hx).neg

@[simp] lemma differentiable_on_neg_iff :
  differentiable_on ğ•œ (Î»y, -f y) s â†” differentiable_on ğ•œ f s :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma differentiable.neg (h : differentiable ğ•œ f) :
  differentiable ğ•œ (Î»y, -f y) :=
Î»x, (h x).neg

@[simp] lemma differentiable_neg_iff : differentiable ğ•œ (Î»y, -f y) â†” differentiable ğ•œ f :=
âŸ¨Î» h, by simpa only [neg_neg] using h.neg, Î» h, h.negâŸ©

lemma fderiv_within_neg (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î»y, -f y) s x = - fderiv_within ğ•œ f s x :=
if h : differentiable_within_at ğ•œ f s x
then h.has_fderiv_within_at.neg.fderiv_within hxs
else by { rw [fderiv_within_zero_of_not_differentiable_within_at h,
  fderiv_within_zero_of_not_differentiable_within_at, neg_zero], simpa }

@[simp] lemma fderiv_neg : fderiv ğ•œ (Î»y, -f y) x = - fderiv ğ•œ f x :=
by simp only [â† fderiv_within_univ, fderiv_within_neg unique_diff_within_at_univ]

end neg

section sub
/-! ### Derivative of the difference of two functions -/

theorem has_strict_fderiv_at.sub
  (hf : has_strict_fderiv_at f f' x) (hg : has_strict_fderiv_at g g' x) :
  has_strict_fderiv_at (Î» x, f x - g x) (f' - g') x :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem has_fderiv_at_filter.sub
  (hf : has_fderiv_at_filter f f' x L) (hg : has_fderiv_at_filter g g' x L) :
  has_fderiv_at_filter (Î» x, f x - g x) (f' - g') x L :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem has_fderiv_within_at.sub
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» x, f x - g x) (f' - g') s x :=
hf.sub hg

theorem has_fderiv_at.sub
  (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» x, f x - g x) (f' - g') x :=
hf.sub hg

lemma differentiable_within_at.sub
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  differentiable_within_at ğ•œ (Î» y, f y - g y) s x :=
(hf.has_fderiv_within_at.sub hg.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  differentiable_at ğ•œ (Î» y, f y - g y) x :=
(hf.has_fderiv_at.sub hg.has_fderiv_at).differentiable_at

lemma differentiable_on.sub
  (hf : differentiable_on ğ•œ f s) (hg : differentiable_on ğ•œ g s) :
  differentiable_on ğ•œ (Î»y, f y - g y) s :=
Î»x hx, (hf x hx).sub (hg x hx)

@[simp] lemma differentiable.sub
  (hf : differentiable ğ•œ f) (hg : differentiable ğ•œ g) :
  differentiable ğ•œ (Î»y, f y - g y) :=
Î»x, (hf x).sub (hg x)

lemma fderiv_within_sub (hxs : unique_diff_within_at ğ•œ s x)
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x) :
  fderiv_within ğ•œ (Î»y, f y - g y) s x = fderiv_within ğ•œ f s x - fderiv_within ğ•œ g s x :=
(hf.has_fderiv_within_at.sub hg.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_sub
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  fderiv ğ•œ (Î»y, f y - g y) x = fderiv ğ•œ f x - fderiv ğ•œ g x :=
(hf.has_fderiv_at.sub hg.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.sub_const
  (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» x, f x - c) f' x :=
by simpa only [sub_eq_add_neg] using hf.add_const (-c)

theorem has_fderiv_at_filter.sub_const
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» x, f x - c) f' x L :=
by simpa only [sub_eq_add_neg] using hf.add_const (-c)

theorem has_fderiv_within_at.sub_const
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» x, f x - c) f' s x :=
hf.sub_const c

theorem has_fderiv_at.sub_const
  (hf : has_fderiv_at f f' x) (c : F) :
  has_fderiv_at (Î» x, f x - c) f' x :=
hf.sub_const c

lemma differentiable_within_at.sub_const
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y - c) s x :=
(hf.has_fderiv_within_at.sub_const c).differentiable_within_at

@[simp] lemma differentiable_within_at_sub_const_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, f y - c) s x â†” differentiable_within_at ğ•œ f s x :=
by simp only [sub_eq_add_neg, differentiable_within_at_add_const_iff]

lemma differentiable_at.sub_const (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, f y - c) x :=
(hf.has_fderiv_at.sub_const c).differentiable_at

@[simp] lemma differentiable_at_sub_const_iff (c : F) :
  differentiable_at ğ•œ (Î» y, f y - c) x â†” differentiable_at ğ•œ f x :=
by simp only [sub_eq_add_neg, differentiable_at_add_const_iff]

lemma differentiable_on.sub_const (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, f y - c) s :=
Î»x hx, (hf x hx).sub_const c

@[simp] lemma differentiable_on_sub_const_iff (c : F) :
  differentiable_on ğ•œ (Î» y, f y - c) s â†” differentiable_on ğ•œ f s :=
by simp only [sub_eq_add_neg, differentiable_on_add_const_iff]

lemma differentiable.sub_const (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, f y - c) :=
Î»x, (hf x).sub_const c

@[simp] lemma differentiable_sub_const_iff (c : F) :
  differentiable ğ•œ (Î» y, f y - c) â†” differentiable ğ•œ f :=
by simp only [sub_eq_add_neg, differentiable_add_const_iff]

lemma fderiv_within_sub_const (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, f y - c) s x = fderiv_within ğ•œ f s x :=
by simp only [sub_eq_add_neg, fderiv_within_add_const hxs]

lemma fderiv_sub_const (c : F) : fderiv ğ•œ (Î»y, f y - c) x = fderiv ğ•œ f x :=
by simp only [sub_eq_add_neg, fderiv_add_const]

theorem has_strict_fderiv_at.const_sub
  (hf : has_strict_fderiv_at f f' x) (c : F) :
  has_strict_fderiv_at (Î» x, c - f x) (-f') x :=
by simpa only [sub_eq_add_neg] using hf.neg.const_add c

theorem has_fderiv_at_filter.const_sub
  (hf : has_fderiv_at_filter f f' x L) (c : F) :
  has_fderiv_at_filter (Î» x, c - f x) (-f') x L :=
by simpa only [sub_eq_add_neg] using hf.neg.const_add c

theorem has_fderiv_within_at.const_sub
  (hf : has_fderiv_within_at f f' s x) (c : F) :
  has_fderiv_within_at (Î» x, c - f x) (-f') s x :=
hf.const_sub c

theorem has_fderiv_at.const_sub
  (hf : has_fderiv_at f f' x) (c : F) :
  has_fderiv_at (Î» x, c - f x) (-f') x :=
hf.const_sub c

lemma differentiable_within_at.const_sub
  (hf : differentiable_within_at ğ•œ f s x) (c : F) :
  differentiable_within_at ğ•œ (Î» y, c - f y) s x :=
(hf.has_fderiv_within_at.const_sub c).differentiable_within_at

@[simp] lemma differentiable_within_at_const_sub_iff (c : F) :
  differentiable_within_at ğ•œ (Î» y, c - f y) s x â†” differentiable_within_at ğ•œ f s x :=
by simp [sub_eq_add_neg]

lemma differentiable_at.const_sub
  (hf : differentiable_at ğ•œ f x) (c : F) :
  differentiable_at ğ•œ (Î» y, c - f y) x :=
(hf.has_fderiv_at.const_sub c).differentiable_at

@[simp] lemma differentiable_at_const_sub_iff (c : F) :
  differentiable_at ğ•œ (Î» y, c - f y) x â†” differentiable_at ğ•œ f x :=
by simp [sub_eq_add_neg]

lemma differentiable_on.const_sub (hf : differentiable_on ğ•œ f s) (c : F) :
  differentiable_on ğ•œ (Î»y, c - f y) s :=
Î»x hx, (hf x hx).const_sub c

@[simp] lemma differentiable_on_const_sub_iff (c : F) :
  differentiable_on ğ•œ (Î» y, c - f y) s â†” differentiable_on ğ•œ f s :=
by simp [sub_eq_add_neg]

lemma differentiable.const_sub (hf : differentiable ğ•œ f) (c : F) :
  differentiable ğ•œ (Î»y, c - f y) :=
Î»x, (hf x).const_sub c

@[simp] lemma differentiable_const_sub_iff (c : F) :
  differentiable ğ•œ (Î» y, c - f y) â†” differentiable ğ•œ f :=
by simp [sub_eq_add_neg]

lemma fderiv_within_const_sub (hxs : unique_diff_within_at ğ•œ s x) (c : F) :
  fderiv_within ğ•œ (Î»y, c - f y) s x = -fderiv_within ğ•œ f s x :=
by simp only [sub_eq_add_neg, fderiv_within_const_add, fderiv_within_neg, hxs]

lemma fderiv_const_sub (c : F) : fderiv ğ•œ (Î»y, c - f y) x = -fderiv ğ•œ f x :=
by simp only [â† fderiv_within_univ, fderiv_within_const_sub unique_diff_within_at_univ]

end sub

section bilinear_map
/-! ### Derivative of a bounded bilinear map -/

variables {b : E Ã— F â†’ G} {u : set (E Ã— F)}

open normed_field

lemma is_bounded_bilinear_map.has_strict_fderiv_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_strict_fderiv_at b (h.deriv p) p :=
begin
  rw has_strict_fderiv_at,
  set T := (E Ã— F) Ã— (E Ã— F),
  have : (Î» q : T, b (q.1 - q.2)) =o[ğ“ (p, p)] (Î» q : T, â€–q.1 - q.2â€– * 1),
  { refine (h.is_O'.comp_tendsto le_top).trans_is_o _,
    simp only [(âˆ˜)],
    refine (is_O_refl (Î» q : T, â€–q.1 - q.2â€–) _).mul_is_o (is_o.norm_left $ (is_o_one_iff _).2 _),
    rw [â† sub_self p],
    exact continuous_at_fst.sub continuous_at_snd },
  simp only [mul_one, is_o_norm_right] at this,
  refine (is_o.congr_of_sub _).1 this, clear this,
  convert_to (Î» q : T, h.deriv (p - q.2) (q.1 - q.2)) =o[ğ“ (p, p)] (Î» q : T, q.1 - q.2),
  { ext âŸ¨âŸ¨xâ‚, yâ‚âŸ©, âŸ¨xâ‚‚, yâ‚‚âŸ©âŸ©, rcases p with âŸ¨x, yâŸ©,
    simp only [is_bounded_bilinear_map_deriv_coe, prod.mk_sub_mk, h.map_sub_left, h.map_sub_right],
    abel },
  have : (Î» q : T, p - q.2) =o[ğ“ (p, p)] (Î» q, (1:â„)),
    from (is_o_one_iff _).2 (sub_self p â–¸ tendsto_const_nhds.sub continuous_at_snd),
  apply is_bounded_bilinear_map_apply.is_O_comp.trans_is_o,
  refine is_o.trans_is_O _ (is_O_const_mul_self 1 _ _).of_norm_right,
  refine is_o.mul_is_O _ (is_O_refl _ _),
  exact (((h.is_bounded_linear_map_deriv.is_O_id âŠ¤).comp_tendsto le_top : _).trans_is_o
    this).norm_left
end

lemma is_bounded_bilinear_map.has_fderiv_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_fderiv_at b (h.deriv p) p :=
(h.has_strict_fderiv_at p).has_fderiv_at

lemma is_bounded_bilinear_map.has_fderiv_within_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  has_fderiv_within_at b (h.deriv p) u p :=
(h.has_fderiv_at p).has_fderiv_within_at

lemma is_bounded_bilinear_map.differentiable_at (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  differentiable_at ğ•œ b p :=
(h.has_fderiv_at p).differentiable_at

lemma is_bounded_bilinear_map.differentiable_within_at (h : is_bounded_bilinear_map ğ•œ b)
  (p : E Ã— F) :
  differentiable_within_at ğ•œ b u p :=
(h.differentiable_at p).differentiable_within_at

lemma is_bounded_bilinear_map.fderiv (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F) :
  fderiv ğ•œ b p = h.deriv p :=
has_fderiv_at.fderiv (h.has_fderiv_at p)

lemma is_bounded_bilinear_map.fderiv_within (h : is_bounded_bilinear_map ğ•œ b) (p : E Ã— F)
  (hxs : unique_diff_within_at ğ•œ u p) : fderiv_within ğ•œ b u p = h.deriv p :=
begin
  rw differentiable_at.fderiv_within (h.differentiable_at p) hxs,
  exact h.fderiv p
end

lemma is_bounded_bilinear_map.differentiable (h : is_bounded_bilinear_map ğ•œ b) :
  differentiable ğ•œ b :=
Î»x, h.differentiable_at x

lemma is_bounded_bilinear_map.differentiable_on (h : is_bounded_bilinear_map ğ•œ b) :
  differentiable_on ğ•œ b u :=
h.differentiable.differentiable_on

variable (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G)

lemma continuous_linear_map.has_fderiv_within_at_of_bilinear
  {f : G' â†’ E} {g : G' â†’ F} {f' : G' â†’L[ğ•œ] E} {g' : G' â†’L[ğ•œ] F} {x : G'} {s : set G'}
  (hf : has_fderiv_within_at f f' s x) (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» y, B (f y) (g y)) (B.precompR G' (f x) g' + B.precompL G' f' (g x)) s x :=
(B.is_bounded_bilinear_map.has_fderiv_at (f x, g x)).comp_has_fderiv_within_at x (hf.prod hg)

lemma continuous_linear_map.has_fderiv_at_of_bilinear
  {f : G' â†’ E} {g : G' â†’ F} {f' : G' â†’L[ğ•œ] E} {g' : G' â†’L[ğ•œ] F} {x : G'}
  (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» y, B (f y) (g y)) (B.precompR G' (f x) g' + B.precompL G' f' (g x)) x :=
(B.is_bounded_bilinear_map.has_fderiv_at (f x, g x)).comp x (hf.prod hg)

lemma continuous_linear_map.fderiv_within_of_bilinear
  {f : G' â†’ E} {g : G' â†’ F} {x : G'} {s : set G'}
  (hf : differentiable_within_at ğ•œ f s x) (hg : differentiable_within_at ğ•œ g s x)
  (hs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î» y, B (f y) (g y)) s x =
    (B.precompR G' (f x) (fderiv_within ğ•œ g s x) + B.precompL G' (fderiv_within ğ•œ f s x) (g x)) :=
(B.has_fderiv_within_at_of_bilinear hf.has_fderiv_within_at hg.has_fderiv_within_at).fderiv_within
  hs

lemma continuous_linear_map.fderiv_of_bilinear {f : G' â†’ E} {g : G' â†’ F} {x : G'}
  (hf : differentiable_at ğ•œ f x) (hg : differentiable_at ğ•œ g x) :
  fderiv ğ•œ (Î» y, B (f y) (g y)) x =
    (B.precompR G' (f x) (fderiv ğ•œ g x) + B.precompL G' (fderiv ğ•œ f x) (g x)) :=
(B.has_fderiv_at_of_bilinear hf.has_fderiv_at hg.has_fderiv_at).fderiv

end bilinear_map

section clm_comp_apply
/-! ### Derivative of the pointwise composition/application of continuous linear maps -/

variables {H : Type*} [normed_add_comm_group H] [normed_space ğ•œ H] {c : E â†’ G â†’L[ğ•œ] H}
  {c' : E â†’L[ğ•œ] G â†’L[ğ•œ] H} {d : E â†’ F â†’L[ğ•œ] G} {d' : E â†’L[ğ•œ] F â†’L[ğ•œ] G} {u : E â†’ G}
  {u' : E â†’L[ğ•œ] G}

lemma has_strict_fderiv_at.clm_comp (hc : has_strict_fderiv_at c c' x)
  (hd : has_strict_fderiv_at d d' x) : has_strict_fderiv_at (Î» y, (c y).comp (d y))
  ((compL ğ•œ F G H (c x)).comp d' + ((compL ğ•œ F G H).flip (d x)).comp c') x :=
(is_bounded_bilinear_map_comp.has_strict_fderiv_at (c x, d x)).comp x $ hc.prod hd

lemma has_fderiv_within_at.clm_comp (hc : has_fderiv_within_at c c' s x)
  (hd : has_fderiv_within_at d d' s x) : has_fderiv_within_at (Î» y, (c y).comp (d y))
  ((compL ğ•œ F G H (c x)).comp d' + ((compL ğ•œ F G H).flip (d x)).comp c') s x :=
(is_bounded_bilinear_map_comp.has_fderiv_at (c x, d x)).comp_has_fderiv_within_at x $ hc.prod hd

lemma has_fderiv_at.clm_comp (hc : has_fderiv_at c c' x)
  (hd : has_fderiv_at d d' x) : has_fderiv_at (Î» y, (c y).comp (d y))
  ((compL ğ•œ F G H (c x)).comp d' + ((compL ğ•œ F G H).flip (d x)).comp c') x :=
(is_bounded_bilinear_map_comp.has_fderiv_at (c x, d x)).comp x $ hc.prod hd

lemma differentiable_within_at.clm_comp
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  differentiable_within_at ğ•œ (Î» y, (c y).comp (d y)) s x :=
(hc.has_fderiv_within_at.clm_comp hd.has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.clm_comp (hc : differentiable_at ğ•œ c x)
  (hd : differentiable_at ğ•œ d x) : differentiable_at ğ•œ (Î» y, (c y).comp (d y)) x :=
(hc.has_fderiv_at.clm_comp hd.has_fderiv_at).differentiable_at

lemma differentiable_on.clm_comp (hc : differentiable_on ğ•œ c s) (hd : differentiable_on ğ•œ d s) :
  differentiable_on ğ•œ (Î» y, (c y).comp (d y)) s :=
Î»x hx, (hc x hx).clm_comp (hd x hx)

lemma differentiable.clm_comp (hc : differentiable ğ•œ c) (hd : differentiable ğ•œ d) :
  differentiable ğ•œ (Î» y, (c y).comp (d y)) :=
Î»x, (hc x).clm_comp (hd x)

lemma fderiv_within_clm_comp (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  fderiv_within ğ•œ (Î» y, (c y).comp (d y)) s x =
    (compL ğ•œ F G H (c x)).comp (fderiv_within ğ•œ d s x) +
    ((compL ğ•œ F G H).flip (d x)).comp (fderiv_within ğ•œ c s x) :=
(hc.has_fderiv_within_at.clm_comp hd.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_clm_comp (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  fderiv ğ•œ (Î» y, (c y).comp (d y)) x =
    (compL ğ•œ F G H (c x)).comp (fderiv ğ•œ d x) +
    ((compL ğ•œ F G H).flip (d x)).comp (fderiv ğ•œ c x) :=
(hc.has_fderiv_at.clm_comp hd.has_fderiv_at).fderiv

lemma has_strict_fderiv_at.clm_apply (hc : has_strict_fderiv_at c c' x)
  (hu : has_strict_fderiv_at u u' x) :
  has_strict_fderiv_at (Î» y, (c y) (u y)) ((c x).comp u' + c'.flip (u x)) x :=
(is_bounded_bilinear_map_apply.has_strict_fderiv_at (c x, u x)).comp x (hc.prod hu)

lemma has_fderiv_within_at.clm_apply (hc : has_fderiv_within_at c c' s x)
  (hu : has_fderiv_within_at u u' s x) :
  has_fderiv_within_at (Î» y, (c y) (u y)) ((c x).comp u' + c'.flip (u x)) s x :=
(is_bounded_bilinear_map_apply.has_fderiv_at (c x, u x)).comp_has_fderiv_within_at x (hc.prod hu)

lemma has_fderiv_at.clm_apply (hc : has_fderiv_at c c' x) (hu : has_fderiv_at u u' x) :
  has_fderiv_at (Î» y, (c y) (u y)) ((c x).comp u' + c'.flip (u x)) x :=
(is_bounded_bilinear_map_apply.has_fderiv_at (c x, u x)).comp x (hc.prod hu)

lemma differentiable_within_at.clm_apply
  (hc : differentiable_within_at ğ•œ c s x) (hu : differentiable_within_at ğ•œ u s x) :
  differentiable_within_at ğ•œ (Î» y, (c y) (u y)) s x :=
(hc.has_fderiv_within_at.clm_apply hu.has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.clm_apply (hc : differentiable_at ğ•œ c x)
  (hu : differentiable_at ğ•œ u x) : differentiable_at ğ•œ (Î» y, (c y) (u y)) x :=
(hc.has_fderiv_at.clm_apply hu.has_fderiv_at).differentiable_at

lemma differentiable_on.clm_apply (hc : differentiable_on ğ•œ c s) (hu : differentiable_on ğ•œ u s) :
  differentiable_on ğ•œ (Î» y, (c y) (u y)) s :=
Î»x hx, (hc x hx).clm_apply (hu x hx)

lemma differentiable.clm_apply (hc : differentiable ğ•œ c) (hu : differentiable ğ•œ u) :
  differentiable ğ•œ (Î» y, (c y) (u y)) :=
Î»x, (hc x).clm_apply (hu x)

lemma fderiv_within_clm_apply (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hu : differentiable_within_at ğ•œ u s x) :
  fderiv_within ğ•œ (Î» y, (c y) (u y)) s x =
    ((c x).comp (fderiv_within ğ•œ u s x) + (fderiv_within ğ•œ c s x).flip (u x)) :=
(hc.has_fderiv_within_at.clm_apply hu.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_clm_apply (hc : differentiable_at ğ•œ c x) (hu : differentiable_at ğ•œ u x) :
  fderiv ğ•œ (Î» y, (c y) (u y)) x = ((c x).comp (fderiv ğ•œ u x) + (fderiv ğ•œ c x).flip (u x)) :=
(hc.has_fderiv_at.clm_apply hu.has_fderiv_at).fderiv

end clm_comp_apply

section smul
/-! ### Derivative of the product of a scalar-valued function and a vector-valued function

If `c` is a differentiable scalar-valued function and `f` is a differentiable vector-valued
function, then `Î» x, c x â€¢ f x` is differentiable as well. Lemmas in this section works for
function `c` taking values in the base field, as well as in a normed algebra over the base
field: e.g., they work for `c : E â†’ â„‚` and `f : E â†’ F` provided that `F` is a complex
normed vector space.
-/

variables {ğ•œ' : Type*} [nontrivially_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  [normed_space ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F]
variables {c : E â†’ ğ•œ'} {c' : E â†’L[ğ•œ] ğ•œ'}

theorem has_strict_fderiv_at.smul (hc : has_strict_fderiv_at c c' x)
  (hf : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.smul_right (f x)) x :=
(is_bounded_bilinear_map_smul.has_strict_fderiv_at (c x, f x)).comp x $
  hc.prod hf

theorem has_fderiv_within_at.smul
  (hc : has_fderiv_within_at c c' s x) (hf : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.smul_right (f x)) s x :=
(is_bounded_bilinear_map_smul.has_fderiv_at (c x, f x)).comp_has_fderiv_within_at x $
  hc.prod hf

theorem has_fderiv_at.smul (hc : has_fderiv_at c c' x) (hf : has_fderiv_at f f' x) :
  has_fderiv_at (Î» y, c y â€¢ f y) (c x â€¢ f' + c'.smul_right (f x)) x :=
(is_bounded_bilinear_map_smul.has_fderiv_at (c x, f x)).comp x $
  hc.prod hf

lemma differentiable_within_at.smul
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î» y, c y â€¢ f y) s x :=
(hc.has_fderiv_within_at.smul hf.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.smul (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î» y, c y â€¢ f y) x :=
(hc.has_fderiv_at.smul hf.has_fderiv_at).differentiable_at

lemma differentiable_on.smul (hc : differentiable_on ğ•œ c s) (hf : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î» y, c y â€¢ f y) s :=
Î»x hx, (hc x hx).smul (hf x hx)

@[simp] lemma differentiable.smul (hc : differentiable ğ•œ c) (hf : differentiable ğ•œ f) :
  differentiable ğ•œ (Î» y, c y â€¢ f y) :=
Î»x, (hc x).smul (hf x)

lemma fderiv_within_smul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hf : differentiable_within_at ğ•œ f s x) :
  fderiv_within ğ•œ (Î» y, c y â€¢ f y) s x =
    c x â€¢ fderiv_within ğ•œ f s x + (fderiv_within ğ•œ c s x).smul_right (f x) :=
(hc.has_fderiv_within_at.smul hf.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_smul (hc : differentiable_at ğ•œ c x) (hf : differentiable_at ğ•œ f x) :
  fderiv ğ•œ (Î» y, c y â€¢ f y) x =
    c x â€¢ fderiv ğ•œ f x + (fderiv ğ•œ c x).smul_right (f x) :=
(hc.has_fderiv_at.smul hf.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.smul_const (hc : has_strict_fderiv_at c c' x) (f : F) :
  has_strict_fderiv_at (Î» y, c y â€¢ f) (c'.smul_right f) x :=
by simpa only [smul_zero, zero_add] using hc.smul (has_strict_fderiv_at_const f x)

theorem has_fderiv_within_at.smul_const (hc : has_fderiv_within_at c c' s x) (f : F) :
  has_fderiv_within_at (Î» y, c y â€¢ f) (c'.smul_right f) s x :=
by simpa only [smul_zero, zero_add] using hc.smul (has_fderiv_within_at_const f x s)

theorem has_fderiv_at.smul_const (hc : has_fderiv_at c c' x) (f : F) :
  has_fderiv_at (Î» y, c y â€¢ f) (c'.smul_right f) x :=
by simpa only [smul_zero, zero_add] using hc.smul (has_fderiv_at_const f x)

lemma differentiable_within_at.smul_const
  (hc : differentiable_within_at ğ•œ c s x) (f : F) :
  differentiable_within_at ğ•œ (Î» y, c y â€¢ f) s x :=
(hc.has_fderiv_within_at.smul_const f).differentiable_within_at

lemma differentiable_at.smul_const (hc : differentiable_at ğ•œ c x) (f : F) :
  differentiable_at ğ•œ (Î» y, c y â€¢ f) x :=
(hc.has_fderiv_at.smul_const f).differentiable_at

lemma differentiable_on.smul_const (hc : differentiable_on ğ•œ c s) (f : F) :
  differentiable_on ğ•œ (Î» y, c y â€¢ f) s :=
Î»x hx, (hc x hx).smul_const f

lemma differentiable.smul_const (hc : differentiable ğ•œ c) (f : F) :
  differentiable ğ•œ (Î» y, c y â€¢ f) :=
Î»x, (hc x).smul_const f

lemma fderiv_within_smul_const (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (f : F) :
  fderiv_within ğ•œ (Î» y, c y â€¢ f) s x =
    (fderiv_within ğ•œ c s x).smul_right f :=
(hc.has_fderiv_within_at.smul_const f).fderiv_within hxs

lemma fderiv_smul_const (hc : differentiable_at ğ•œ c x) (f : F) :
  fderiv ğ•œ (Î» y, c y â€¢ f) x = (fderiv ğ•œ c x).smul_right f :=
(hc.has_fderiv_at.smul_const f).fderiv

end smul

section mul
/-! ### Derivative of the product of two functions -/

variables {ğ”¸ ğ”¸' : Type*} [normed_ring ğ”¸] [normed_comm_ring ğ”¸'] [normed_algebra ğ•œ ğ”¸]
  [normed_algebra ğ•œ ğ”¸'] {a b : E â†’ ğ”¸} {a' b' : E â†’L[ğ•œ] ğ”¸} {c d : E â†’ ğ”¸'} {c' d' : E â†’L[ğ•œ] ğ”¸'}

theorem has_strict_fderiv_at.mul' {x : E} (ha : has_strict_fderiv_at a a' x)
  (hb : has_strict_fderiv_at b b' x) :
  has_strict_fderiv_at (Î» y, a y * b y) (a x â€¢ b' + a'.smul_right (b x)) x :=
((continuous_linear_map.mul ğ•œ ğ”¸).is_bounded_bilinear_map.has_strict_fderiv_at (a x, b x)).comp x
  (ha.prod hb)

theorem has_strict_fderiv_at.mul
  (hc : has_strict_fderiv_at c c' x) (hd : has_strict_fderiv_at d d' x) :
  has_strict_fderiv_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') x :=
by { convert hc.mul' hd, ext z, apply mul_comm }

theorem has_fderiv_within_at.mul'
  (ha : has_fderiv_within_at a a' s x) (hb : has_fderiv_within_at b b' s x) :
  has_fderiv_within_at (Î» y, a y * b y) (a x â€¢ b' + a'.smul_right (b x)) s x :=
((continuous_linear_map.mul ğ•œ ğ”¸).is_bounded_bilinear_map.has_fderiv_at
  (a x, b x)).comp_has_fderiv_within_at x (ha.prod hb)

theorem has_fderiv_within_at.mul
  (hc : has_fderiv_within_at c c' s x) (hd : has_fderiv_within_at d d' s x) :
  has_fderiv_within_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') s x :=
by { convert hc.mul' hd, ext z, apply mul_comm }

theorem has_fderiv_at.mul'
  (ha : has_fderiv_at a a' x) (hb : has_fderiv_at b b' x) :
  has_fderiv_at (Î» y, a y * b y) (a x â€¢ b' + a'.smul_right (b x)) x :=
((continuous_linear_map.mul ğ•œ ğ”¸).is_bounded_bilinear_map.has_fderiv_at (a x, b x)).comp x
  (ha.prod hb)

theorem has_fderiv_at.mul (hc : has_fderiv_at c c' x) (hd : has_fderiv_at d d' x) :
  has_fderiv_at (Î» y, c y * d y) (c x â€¢ d' + d x â€¢ c') x :=
by { convert hc.mul' hd, ext z, apply mul_comm }

lemma differentiable_within_at.mul
  (ha : differentiable_within_at ğ•œ a s x) (hb : differentiable_within_at ğ•œ b s x) :
  differentiable_within_at ğ•œ (Î» y, a y * b y) s x :=
(ha.has_fderiv_within_at.mul' hb.has_fderiv_within_at).differentiable_within_at

@[simp] lemma differentiable_at.mul (ha : differentiable_at ğ•œ a x) (hb : differentiable_at ğ•œ b x) :
  differentiable_at ğ•œ (Î» y, a y * b y) x :=
(ha.has_fderiv_at.mul' hb.has_fderiv_at).differentiable_at

lemma differentiable_on.mul (ha : differentiable_on ğ•œ a s) (hb : differentiable_on ğ•œ b s) :
  differentiable_on ğ•œ (Î» y, a y * b y) s :=
Î»x hx, (ha x hx).mul (hb x hx)

@[simp] lemma differentiable.mul (ha : differentiable ğ•œ a) (hb : differentiable ğ•œ b) :
  differentiable ğ•œ (Î» y, a y * b y) :=
Î»x, (ha x).mul (hb x)

lemma differentiable_within_at.pow (ha : differentiable_within_at ğ•œ a s x) :
  âˆ€ n : â„•, differentiable_within_at ğ•œ (Î» x, a x ^ n) s x
| 0 := by simp only [pow_zero, differentiable_within_at_const]
| (n + 1) := by simp only [pow_succ, differentiable_within_at.pow n, ha.mul]

@[simp] lemma differentiable_at.pow (ha : differentiable_at ğ•œ a x) (n : â„•) :
  differentiable_at ğ•œ (Î» x, a x ^ n) x :=
differentiable_within_at_univ.mp $ ha.differentiable_within_at.pow n

lemma differentiable_on.pow (ha : differentiable_on ğ•œ a s) (n : â„•) :
  differentiable_on ğ•œ (Î» x, a x ^ n) s :=
Î» x h, (ha x h).pow n

@[simp] lemma differentiable.pow (ha : differentiable ğ•œ a) (n : â„•) :
  differentiable ğ•œ (Î» x, a x ^ n) :=
Î»x, (ha x).pow n

lemma fderiv_within_mul' (hxs : unique_diff_within_at ğ•œ s x)
  (ha : differentiable_within_at ğ•œ a s x) (hb : differentiable_within_at ğ•œ b s x) :
  fderiv_within ğ•œ (Î» y, a y * b y) s x =
    a x â€¢ fderiv_within ğ•œ b s x + (fderiv_within ğ•œ a s x).smul_right (b x) :=
(ha.has_fderiv_within_at.mul' hb.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_within_mul (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (hd : differentiable_within_at ğ•œ d s x) :
  fderiv_within ğ•œ (Î» y, c y * d y) s x =
    c x â€¢ fderiv_within ğ•œ d s x + d x â€¢ fderiv_within ğ•œ c s x :=
(hc.has_fderiv_within_at.mul hd.has_fderiv_within_at).fderiv_within hxs

lemma fderiv_mul' (ha : differentiable_at ğ•œ a x) (hb : differentiable_at ğ•œ b x) :
  fderiv ğ•œ (Î» y, a y * b y) x =
    a x â€¢ fderiv ğ•œ b x + (fderiv ğ•œ a x).smul_right (b x) :=
(ha.has_fderiv_at.mul' hb.has_fderiv_at).fderiv

lemma fderiv_mul (hc : differentiable_at ğ•œ c x) (hd : differentiable_at ğ•œ d x) :
  fderiv ğ•œ (Î» y, c y * d y) x =
    c x â€¢ fderiv ğ•œ d x + d x â€¢ fderiv ğ•œ c x :=
(hc.has_fderiv_at.mul hd.has_fderiv_at).fderiv

theorem has_strict_fderiv_at.mul_const' (ha : has_strict_fderiv_at a a' x) (b : ğ”¸) :
  has_strict_fderiv_at (Î» y, a y * b) (a'.smul_right b) x :=
(((continuous_linear_map.mul ğ•œ ğ”¸).flip b).has_strict_fderiv_at).comp x ha

theorem has_strict_fderiv_at.mul_const (hc : has_strict_fderiv_at c c' x) (d : ğ”¸') :
  has_strict_fderiv_at (Î» y, c y * d) (d â€¢ c') x :=
by { convert hc.mul_const' d, ext z, apply mul_comm }

theorem has_fderiv_within_at.mul_const' (ha : has_fderiv_within_at a a' s x) (b : ğ”¸) :
  has_fderiv_within_at (Î» y, a y * b) (a'.smul_right b) s x :=
(((continuous_linear_map.mul ğ•œ ğ”¸).flip b).has_fderiv_at).comp_has_fderiv_within_at x ha

theorem has_fderiv_within_at.mul_const (hc : has_fderiv_within_at c c' s x) (d : ğ”¸') :
  has_fderiv_within_at (Î» y, c y * d) (d â€¢ c') s x :=
by { convert hc.mul_const' d, ext z, apply mul_comm }

theorem has_fderiv_at.mul_const' (ha : has_fderiv_at a a' x) (b : ğ”¸) :
  has_fderiv_at (Î» y, a y * b) (a'.smul_right b) x :=
(((continuous_linear_map.mul ğ•œ ğ”¸).flip b).has_fderiv_at).comp x ha

theorem has_fderiv_at.mul_const (hc : has_fderiv_at c c' x) (d : ğ”¸') :
  has_fderiv_at (Î» y, c y * d) (d â€¢ c') x :=
by { convert hc.mul_const' d, ext z, apply mul_comm }

lemma differentiable_within_at.mul_const
  (ha : differentiable_within_at ğ•œ a s x) (b : ğ”¸) :
  differentiable_within_at ğ•œ (Î» y, a y * b) s x :=
(ha.has_fderiv_within_at.mul_const' b).differentiable_within_at

lemma differentiable_at.mul_const (ha : differentiable_at ğ•œ a x) (b : ğ”¸) :
  differentiable_at ğ•œ (Î» y, a y * b) x :=
(ha.has_fderiv_at.mul_const' b).differentiable_at

lemma differentiable_on.mul_const (ha : differentiable_on ğ•œ a s) (b : ğ”¸) :
  differentiable_on ğ•œ (Î» y, a y * b) s :=
Î»x hx, (ha x hx).mul_const b

lemma differentiable.mul_const (ha : differentiable ğ•œ a) (b : ğ”¸) :
  differentiable ğ•œ (Î» y, a y * b) :=
Î»x, (ha x).mul_const b

lemma fderiv_within_mul_const' (hxs : unique_diff_within_at ğ•œ s x)
  (ha : differentiable_within_at ğ•œ a s x) (b : ğ”¸) :
  fderiv_within ğ•œ (Î» y, a y * b) s x = (fderiv_within ğ•œ a s x).smul_right b :=
(ha.has_fderiv_within_at.mul_const' b).fderiv_within hxs

lemma fderiv_within_mul_const (hxs : unique_diff_within_at ğ•œ s x)
  (hc : differentiable_within_at ğ•œ c s x) (d : ğ”¸') :
  fderiv_within ğ•œ (Î» y, c y * d) s x = d â€¢ fderiv_within ğ•œ c s x :=
(hc.has_fderiv_within_at.mul_const d).fderiv_within hxs

lemma fderiv_mul_const' (ha : differentiable_at ğ•œ a x) (b : ğ”¸) :
  fderiv ğ•œ (Î» y, a y * b) x = (fderiv ğ•œ a x).smul_right b :=
(ha.has_fderiv_at.mul_const' b).fderiv

lemma fderiv_mul_const (hc : differentiable_at ğ•œ c x) (d : ğ”¸') :
  fderiv ğ•œ (Î» y, c y * d) x = d â€¢ fderiv ğ•œ c x :=
(hc.has_fderiv_at.mul_const d).fderiv

theorem has_strict_fderiv_at.const_mul (ha : has_strict_fderiv_at a a' x) (b : ğ”¸) :
  has_strict_fderiv_at (Î» y, b * a y) (b â€¢ a') x :=
(((continuous_linear_map.mul ğ•œ ğ”¸) b).has_strict_fderiv_at).comp x ha

theorem has_fderiv_within_at.const_mul
  (ha : has_fderiv_within_at a a' s x) (b : ğ”¸) :
  has_fderiv_within_at (Î» y, b * a y) (b â€¢ a') s x :=
(((continuous_linear_map.mul ğ•œ ğ”¸) b).has_fderiv_at).comp_has_fderiv_within_at x ha

theorem has_fderiv_at.const_mul (ha : has_fderiv_at a a' x) (b : ğ”¸) :
  has_fderiv_at (Î» y, b * a y) (b â€¢ a') x :=
(((continuous_linear_map.mul ğ•œ ğ”¸) b).has_fderiv_at).comp x ha

lemma differentiable_within_at.const_mul
  (ha : differentiable_within_at ğ•œ a s x) (b : ğ”¸) :
  differentiable_within_at ğ•œ (Î» y, b * a y) s x :=
(ha.has_fderiv_within_at.const_mul b).differentiable_within_at

lemma differentiable_at.const_mul (ha : differentiable_at ğ•œ a x) (b : ğ”¸) :
  differentiable_at ğ•œ (Î» y, b * a y) x :=
(ha.has_fderiv_at.const_mul b).differentiable_at

lemma differentiable_on.const_mul (ha : differentiable_on ğ•œ a s) (b : ğ”¸) :
  differentiable_on ğ•œ (Î» y, b * a y) s :=
Î»x hx, (ha x hx).const_mul b

lemma differentiable.const_mul (ha : differentiable ğ•œ a) (b : ğ”¸) :
  differentiable ğ•œ (Î» y, b * a y) :=
Î»x, (ha x).const_mul b

lemma fderiv_within_const_mul (hxs : unique_diff_within_at ğ•œ s x)
  (ha : differentiable_within_at ğ•œ a s x) (b : ğ”¸) :
  fderiv_within ğ•œ (Î» y, b * a y) s x = b â€¢ fderiv_within ğ•œ a s x :=
(ha.has_fderiv_within_at.const_mul b).fderiv_within hxs

lemma fderiv_const_mul (ha : differentiable_at ğ•œ a x) (b : ğ”¸) :
  fderiv ğ•œ (Î» y, b * a y) x = b â€¢ fderiv ğ•œ a x :=
(ha.has_fderiv_at.const_mul b).fderiv

end mul

section algebra_inverse
variables {R : Type*} [normed_ring R] [normed_algebra ğ•œ R] [complete_space R]
open normed_ring continuous_linear_map ring

/-- At an invertible element `x` of a normed algebra `R`, the FrÃ©chet derivative of the inversion
operation is the linear map `Î» t, - xâ»Â¹ * t * xâ»Â¹`. -/
lemma has_fderiv_at_ring_inverse (x : RË£) :
  has_fderiv_at ring.inverse (-mul_left_right ğ•œ R â†‘xâ»Â¹ â†‘xâ»Â¹) x :=
begin
  have h_is_o : (Î» (t : R), inverse (â†‘x + t) - â†‘xâ»Â¹ + â†‘xâ»Â¹ * t * â†‘xâ»Â¹) =o[ğ“ 0] (Î» (t : R), t),
  { refine (inverse_add_norm_diff_second_order x).trans_is_o ((is_o_norm_norm).mp _),
    simp only [norm_pow, norm_norm],
    have h12 : 1 < 2 := by norm_num,
    convert (asymptotics.is_o_pow_pow h12).comp_tendsto tendsto_norm_zero,
    ext, simp },
  have h_lim : tendsto (Î» (y:R), y - x) (ğ“ x) (ğ“ 0),
  { refine tendsto_zero_iff_norm_tendsto_zero.mpr _,
    exact tendsto_iff_norm_tendsto_zero.mp tendsto_id },
  simp only [has_fderiv_at, has_fderiv_at_filter],
  convert h_is_o.comp_tendsto h_lim,
  ext y,
  simp only [coe_comp', function.comp_app, mul_left_right_apply, neg_apply, inverse_unit x,
    units.inv_mul, add_sub_cancel'_right, mul_sub, sub_mul, one_mul, sub_neg_eq_add]
end

lemma differentiable_at_inverse (x : RË£) : differentiable_at ğ•œ (@ring.inverse R _) x :=
(has_fderiv_at_ring_inverse x).differentiable_at

lemma fderiv_inverse (x : RË£) :
  fderiv ğ•œ (@ring.inverse R _) x = - mul_left_right ğ•œ R â†‘xâ»Â¹ â†‘xâ»Â¹ :=
(has_fderiv_at_ring_inverse x).fderiv

end algebra_inverse

namespace continuous_linear_equiv
/-! ### Differentiability of linear equivs, and invariance of differentiability -/

variable (iso : E â‰ƒL[ğ•œ] F)

protected lemma has_strict_fderiv_at :
  has_strict_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
iso.to_continuous_linear_map.has_strict_fderiv_at

protected lemma has_fderiv_within_at :
  has_fderiv_within_at iso (iso : E â†’L[ğ•œ] F) s x :=
iso.to_continuous_linear_map.has_fderiv_within_at

protected lemma has_fderiv_at : has_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
iso.to_continuous_linear_map.has_fderiv_at_filter

protected lemma differentiable_at : differentiable_at ğ•œ iso x :=
iso.has_fderiv_at.differentiable_at

protected lemma differentiable_within_at :
  differentiable_within_at ğ•œ iso s x :=
iso.differentiable_at.differentiable_within_at

protected lemma fderiv : fderiv ğ•œ iso x = iso :=
iso.has_fderiv_at.fderiv

protected lemma fderiv_within (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ iso s x = iso :=
iso.to_continuous_linear_map.fderiv_within hxs

protected lemma differentiable : differentiable ğ•œ iso :=
Î»x, iso.differentiable_at

protected lemma differentiable_on : differentiable_on ğ•œ iso s :=
iso.differentiable.differentiable_on

lemma comp_differentiable_within_at_iff {f : G â†’ E} {s : set G} {x : G} :
  differentiable_within_at ğ•œ (iso âˆ˜ f) s x â†” differentiable_within_at ğ•œ f s x :=
begin
  refine âŸ¨Î» H, _, Î» H, iso.differentiable.differentiable_at.comp_differentiable_within_at x HâŸ©,
  have : differentiable_within_at ğ•œ (iso.symm âˆ˜ (iso âˆ˜ f)) s x :=
    iso.symm.differentiable.differentiable_at.comp_differentiable_within_at x H,
  rwa [â† function.comp.assoc iso.symm iso f, iso.symm_comp_self] at this,
end

lemma comp_differentiable_at_iff {f : G â†’ E} {x : G} :
  differentiable_at ğ•œ (iso âˆ˜ f) x â†” differentiable_at ğ•œ f x :=
by rw [â† differentiable_within_at_univ, â† differentiable_within_at_univ,
       iso.comp_differentiable_within_at_iff]

lemma comp_differentiable_on_iff {f : G â†’ E} {s : set G} :
  differentiable_on ğ•œ (iso âˆ˜ f) s â†” differentiable_on ğ•œ f s :=
begin
  rw [differentiable_on, differentiable_on],
  simp only [iso.comp_differentiable_within_at_iff],
end

lemma comp_differentiable_iff {f : G â†’ E} :
  differentiable ğ•œ (iso âˆ˜ f) â†” differentiable ğ•œ f :=
begin
  rw [â† differentiable_on_univ, â† differentiable_on_univ],
  exact iso.comp_differentiable_on_iff
end

lemma comp_has_fderiv_within_at_iff
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_within_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” has_fderiv_within_at f f' s x :=
begin
  refine âŸ¨Î» H, _, Î» H, iso.has_fderiv_at.comp_has_fderiv_within_at x HâŸ©,
  have A : f = iso.symm âˆ˜ (iso âˆ˜ f), by { rw [â† function.comp.assoc, iso.symm_comp_self], refl },
  have B : f' = (iso.symm : F â†’L[ğ•œ] E).comp ((iso : E â†’L[ğ•œ] F).comp f'),
    by rw [â† continuous_linear_map.comp_assoc, iso.coe_symm_comp_coe,
             continuous_linear_map.id_comp],
  rw [A, B],
  exact iso.symm.has_fderiv_at.comp_has_fderiv_within_at x H
end

lemma comp_has_strict_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_strict_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_strict_fderiv_at f f' x :=
begin
  refine âŸ¨Î» H, _, Î» H, iso.has_strict_fderiv_at.comp x HâŸ©,
  convert iso.symm.has_strict_fderiv_at.comp x H; ext z; apply (iso.symm_apply_apply _).symm
end

lemma comp_has_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_fderiv_at f f' x :=
by simp_rw [â† has_fderiv_within_at_univ, iso.comp_has_fderiv_within_at_iff]

lemma comp_has_fderiv_within_at_iff'
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_within_at (iso âˆ˜ f) f' s x â†”
  has_fderiv_within_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x :=
by rw [â† iso.comp_has_fderiv_within_at_iff, â† continuous_linear_map.comp_assoc,
  iso.coe_comp_coe_symm, continuous_linear_map.id_comp]

lemma comp_has_fderiv_at_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_at (iso âˆ˜ f) f' x â†” has_fderiv_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') x :=
by simp_rw [â† has_fderiv_within_at_univ, iso.comp_has_fderiv_within_at_iff']

lemma comp_fderiv_within {f : G â†’ E} {s : set G} {x : G}
  (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderiv_within ğ•œ f s x) :=
begin
  by_cases h : differentiable_within_at ğ•œ f s x,
  { rw [fderiv.comp_fderiv_within x iso.differentiable_at h hxs, iso.fderiv] },
  { have : Â¬differentiable_within_at ğ•œ (iso âˆ˜ f) s x,
      from mt iso.comp_differentiable_within_at_iff.1 h,
    rw [fderiv_within_zero_of_not_differentiable_within_at h,
        fderiv_within_zero_of_not_differentiable_within_at this,
        continuous_linear_map.comp_zero] }
end

lemma comp_fderiv {f : G â†’ E} {x : G} :
  fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) :=
begin
  rw [â† fderiv_within_univ, â† fderiv_within_univ],
  exact iso.comp_fderiv_within unique_diff_within_at_univ,
end

lemma comp_right_differentiable_within_at_iff {f : F â†’ G} {s : set F} {x : E} :
  differentiable_within_at ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x â†” differentiable_within_at ğ•œ f s (iso x) :=
begin
  refine âŸ¨Î» H, _, Î» H, H.comp x iso.differentiable_within_at (maps_to_preimage _ s)âŸ©,
  have : differentiable_within_at ğ•œ ((f âˆ˜ iso) âˆ˜ iso.symm) s (iso x),
  { rw â† iso.symm_apply_apply x at H,
    apply H.comp (iso x) iso.symm.differentiable_within_at,
    assume y hy,
    simpa only [mem_preimage, apply_symm_apply] using hy },
  rwa [function.comp.assoc, iso.self_comp_symm] at this,
end

lemma comp_right_differentiable_at_iff {f : F â†’ G} {x : E} :
  differentiable_at ğ•œ (f âˆ˜ iso) x â†” differentiable_at ğ•œ f (iso x) :=
by simp only [â† differentiable_within_at_univ, â† iso.comp_right_differentiable_within_at_iff,
  preimage_univ]

lemma comp_right_differentiable_on_iff {f : F â†’ G} {s : set F} :
  differentiable_on ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) â†” differentiable_on ğ•œ f s :=
begin
  refine âŸ¨Î» H y hy, _, Î» H y hy, iso.comp_right_differentiable_within_at_iff.2 (H _ hy)âŸ©,
  rw [â† iso.apply_symm_apply y, â† comp_right_differentiable_within_at_iff],
  apply H,
  simpa only [mem_preimage, apply_symm_apply] using hy,
end

lemma comp_right_differentiable_iff {f : F â†’ G} :
  differentiable ğ•œ (f âˆ˜ iso) â†” differentiable ğ•œ f :=
by simp only [â† differentiable_on_univ, â† iso.comp_right_differentiable_on_iff, preimage_univ]

lemma comp_right_has_fderiv_within_at_iff
  {f : F â†’ G} {s : set F} {x : E} {f' : F â†’L[ğ•œ] G} :
  has_fderiv_within_at (f âˆ˜ iso) (f'.comp (iso : E â†’L[ğ•œ] F)) (iso â»Â¹' s) x â†”
    has_fderiv_within_at f f' s (iso x) :=
begin
  refine âŸ¨Î» H, _, Î» H, H.comp x iso.has_fderiv_within_at (maps_to_preimage _ s)âŸ©,
  rw [â† iso.symm_apply_apply x] at H,
  have A : f = (f âˆ˜ iso) âˆ˜ iso.symm, by { rw [function.comp.assoc, iso.self_comp_symm], refl },
  have B : f' =  (f'.comp (iso : E â†’L[ğ•œ] F)).comp (iso.symm : F â†’L[ğ•œ] E),
    by rw [continuous_linear_map.comp_assoc, iso.coe_comp_coe_symm,
             continuous_linear_map.comp_id],
  rw [A, B],
  apply H.comp (iso x) iso.symm.has_fderiv_within_at,
  assume y hy,
  simpa only [mem_preimage, apply_symm_apply] using hy
end

lemma comp_right_has_fderiv_at_iff {f : F â†’ G} {x : E} {f' : F â†’L[ğ•œ] G} :
  has_fderiv_at (f âˆ˜ iso) (f'.comp (iso : E â†’L[ğ•œ] F)) x â†” has_fderiv_at f f' (iso x) :=
by simp only [â† has_fderiv_within_at_univ, â† comp_right_has_fderiv_within_at_iff, preimage_univ]

lemma comp_right_has_fderiv_within_at_iff'
  {f : F â†’ G} {s : set F} {x : E} {f' : E â†’L[ğ•œ] G} :
  has_fderiv_within_at (f âˆ˜ iso) f' (iso â»Â¹' s) x â†”
  has_fderiv_within_at f (f'.comp (iso.symm : F â†’L[ğ•œ] E)) s (iso x) :=
by rw [â† iso.comp_right_has_fderiv_within_at_iff, continuous_linear_map.comp_assoc,
    iso.coe_symm_comp_coe, continuous_linear_map.comp_id]

lemma comp_right_has_fderiv_at_iff' {f : F â†’ G} {x : E} {f' : E â†’L[ğ•œ] G} :
  has_fderiv_at (f âˆ˜ iso) f' x â†” has_fderiv_at f (f'.comp (iso.symm : F â†’L[ğ•œ] E)) (iso x) :=
by simp only [â† has_fderiv_within_at_univ, â† iso.comp_right_has_fderiv_within_at_iff',
  preimage_univ]

lemma comp_right_fderiv_within {f : F â†’ G} {s : set F} {x : E}
  (hxs : unique_diff_within_at ğ•œ (iso â»Â¹' s) x) :
  fderiv_within ğ•œ (f âˆ˜ iso) (iso â»Â¹'s) x = (fderiv_within ğ•œ f s (iso x)).comp (iso : E â†’L[ğ•œ] F) :=
begin
  by_cases h : differentiable_within_at ğ•œ f s (iso x),
  { exact (iso.comp_right_has_fderiv_within_at_iff.2 (h.has_fderiv_within_at)).fderiv_within hxs },
  { have : Â¬ differentiable_within_at ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x,
    { assume h', exact h (iso.comp_right_differentiable_within_at_iff.1 h') },
    rw [fderiv_within_zero_of_not_differentiable_within_at h,
        fderiv_within_zero_of_not_differentiable_within_at this, continuous_linear_map.zero_comp] }
end

lemma comp_right_fderiv {f : F â†’ G} {x : E} :
  fderiv ğ•œ (f âˆ˜ iso) x = (fderiv ğ•œ f (iso x)).comp (iso : E â†’L[ğ•œ] F) :=
begin
  rw [â† fderiv_within_univ, â† fderiv_within_univ, â† iso.comp_right_fderiv_within, preimage_univ],
  exact unique_diff_within_at_univ,
end

end continuous_linear_equiv

namespace linear_isometry_equiv
/-! ### Differentiability of linear isometry equivs, and invariance of differentiability -/

variable (iso : E â‰ƒâ‚—áµ¢[ğ•œ] F)

protected lemma has_strict_fderiv_at : has_strict_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
(iso : E â‰ƒL[ğ•œ] F).has_strict_fderiv_at

protected lemma has_fderiv_within_at : has_fderiv_within_at iso (iso : E â†’L[ğ•œ] F) s x :=
(iso : E â‰ƒL[ğ•œ] F).has_fderiv_within_at

protected lemma has_fderiv_at : has_fderiv_at iso (iso : E â†’L[ğ•œ] F) x :=
(iso : E â‰ƒL[ğ•œ] F).has_fderiv_at

protected lemma differentiable_at : differentiable_at ğ•œ iso x :=
iso.has_fderiv_at.differentiable_at

protected lemma differentiable_within_at :
  differentiable_within_at ğ•œ iso s x :=
iso.differentiable_at.differentiable_within_at

protected lemma fderiv : fderiv ğ•œ iso x = iso := iso.has_fderiv_at.fderiv

protected lemma fderiv_within (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ iso s x = iso :=
(iso : E â‰ƒL[ğ•œ] F).fderiv_within hxs

protected lemma differentiable : differentiable ğ•œ iso :=
Î»x, iso.differentiable_at

protected lemma differentiable_on : differentiable_on ğ•œ iso s :=
iso.differentiable.differentiable_on

lemma comp_differentiable_within_at_iff {f : G â†’ E} {s : set G} {x : G} :
  differentiable_within_at ğ•œ (iso âˆ˜ f) s x â†” differentiable_within_at ğ•œ f s x :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_within_at_iff

lemma comp_differentiable_at_iff {f : G â†’ E} {x : G} :
  differentiable_at ğ•œ (iso âˆ˜ f) x â†” differentiable_at ğ•œ f x :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_at_iff

lemma comp_differentiable_on_iff {f : G â†’ E} {s : set G} :
  differentiable_on ğ•œ (iso âˆ˜ f) s â†” differentiable_on ğ•œ f s :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_on_iff

lemma comp_differentiable_iff {f : G â†’ E} :
  differentiable ğ•œ (iso âˆ˜ f) â†” differentiable ğ•œ f :=
(iso : E â‰ƒL[ğ•œ] F).comp_differentiable_iff

lemma comp_has_fderiv_within_at_iff
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_within_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” has_fderiv_within_at f f' s x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_within_at_iff

lemma comp_has_strict_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_strict_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_strict_fderiv_at f f' x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_strict_fderiv_at_iff

lemma comp_has_fderiv_at_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
  has_fderiv_at (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” has_fderiv_at f f' x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_at_iff

lemma comp_has_fderiv_within_at_iff'
  {f : G â†’ E} {s : set G} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_within_at (iso âˆ˜ f) f' s x â†”
  has_fderiv_within_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_within_at_iff'

lemma comp_has_fderiv_at_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
  has_fderiv_at (iso âˆ˜ f) f' x â†” has_fderiv_at f ((iso.symm : F â†’L[ğ•œ] E).comp f') x :=
(iso : E â‰ƒL[ğ•œ] F).comp_has_fderiv_at_iff'

lemma comp_fderiv_within {f : G â†’ E} {s : set G} {x : G}
  (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderiv_within ğ•œ f s x) :=
(iso : E â‰ƒL[ğ•œ] F).comp_fderiv_within hxs

lemma comp_fderiv {f : G â†’ E} {x : G} :
  fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) :=
(iso : E â‰ƒL[ğ•œ] F).comp_fderiv

end linear_isometry_equiv

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a` in the strict sense, then `g` has the derivative `f'â»Â¹` at `a`
in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem has_strict_fderiv_at.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
  (hg : continuous_at g a) (hf : has_strict_fderiv_at f (f' : E â†’L[ğ•œ] F) (g a))
  (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
  has_strict_fderiv_at g (f'.symm : F â†’L[ğ•œ] E) a :=
begin
  replace hg := hg.prod_map' hg,
  replace hfg := hfg.prod_mk_nhds hfg,
  have : (Î» p : F Ã— F, g p.1 - g p.2 - f'.symm (p.1 - p.2)) =O[ğ“ (a, a)]
    (Î» p : F Ã— F, f' (g p.1 - g p.2) - (p.1 - p.2)),
  { refine ((f'.symm : F â†’L[ğ•œ] E).is_O_comp _ _).congr (Î» x, _) (Î» _, rfl),
    simp },
  refine this.trans_is_o _, clear this,
  refine ((hf.comp_tendsto hg).symm.congr' (hfg.mono _)
    (eventually_of_forall $ Î» _, rfl)).trans_is_O _,
  { rintros p âŸ¨hp1, hp2âŸ©,
    simp [hp1, hp2] },
  { refine (hf.is_O_sub_rev.comp_tendsto hg).congr'
      (eventually_of_forall $ Î» _, rfl) (hfg.mono _),
    rintros p âŸ¨hp1, hp2âŸ©,
    simp only [(âˆ˜), hp1, hp2] }
end

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a`, then `g` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem has_fderiv_at.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
  (hg : continuous_at g a) (hf : has_fderiv_at f (f' : E â†’L[ğ•œ] F) (g a))
  (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
  has_fderiv_at g (f'.symm : F â†’L[ğ•œ] E) a :=
begin
  have : (Î» x : F, g x - g a - f'.symm (x - a)) =O[ğ“ a] (Î» x : F, f' (g x - g a) - (x - a)),
  { refine ((f'.symm : F â†’L[ğ•œ] E).is_O_comp _ _).congr (Î» x, _) (Î» _, rfl),
    simp },
  refine this.trans_is_o _, clear this,
  refine ((hf.comp_tendsto hg).symm.congr' (hfg.mono _)
    (eventually_of_forall $ Î» _, rfl)).trans_is_O _,
  { rintros p hp,
    simp [hp, hfg.self_of_nhds] },
  { refine ((hf.is_O_sub_rev f'.antilipschitz).comp_tendsto hg).congr'
      (eventually_of_forall $ Î» _, rfl) (hfg.mono _),
    rintros p hp,
    simp only [(âˆ˜), hp, hfg.self_of_nhds] }
end

/-- If `f` is a local homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` in the sense of strict differentiability at `f.symm a`, then `f.symm` has
the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
lemma local_homeomorph.has_strict_fderiv_at_symm (f : local_homeomorph E F) {f' : E â‰ƒL[ğ•œ] F} {a : F}
  (ha : a âˆˆ f.target) (htff' : has_strict_fderiv_at f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
  has_strict_fderiv_at f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
htff'.of_local_left_inverse (f.symm.continuous_at ha) (f.eventually_right_inverse ha)

/-- If `f` is a local homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` at `f.symm a`, then `f.symm` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
lemma local_homeomorph.has_fderiv_at_symm (f : local_homeomorph E F) {f' : E â‰ƒL[ğ•œ] F} {a : F}
  (ha : a âˆˆ f.target) (htff' : has_fderiv_at f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
  has_fderiv_at f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
htff'.of_local_left_inverse (f.symm.continuous_at ha) (f.eventually_right_inverse ha)

lemma has_fderiv_within_at.eventually_ne (h : has_fderiv_within_at f f' s x)
  (hf' : âˆƒ C, âˆ€ z, â€–zâ€– â‰¤ C * â€–f' zâ€–) :
  âˆ€á¶  z in ğ“[s \ {x}] x, f z â‰  f x :=
begin
  rw [nhds_within, diff_eq, â† inf_principal, â† inf_assoc, eventually_inf_principal],
  have A : (Î» z, z - x) =O[ğ“[s] x] (Î» z, f' (z - x)) :=
    (is_O_iff.2 $ hf'.imp $ Î» C hC, eventually_of_forall $ Î» z, hC _),
  have : (Î» z, f z - f x) ~[ğ“[s] x] (Î» z, f' (z - x)) := h.trans_is_O A,
  simpa [not_imp_not, sub_eq_zero] using (A.trans this.is_O_symm).eq_zero_imp
end

lemma has_fderiv_at.eventually_ne (h : has_fderiv_at f f' x) (hf' : âˆƒ C, âˆ€ z, â€–zâ€– â‰¤ C * â€–f' zâ€–) :
  âˆ€á¶  z in ğ“[â‰ ] x, f z â‰  f x :=
by simpa only [compl_eq_univ_diff] using (has_fderiv_within_at_univ.2 h).eventually_ne hf'

end

section
/-
  In the special case of a normed space over the reals,
  we can use  scalar multiplication in the `tendsto` characterization
  of the FrÃ©chet derivative.
-/


variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
variables {F : Type*} [normed_add_comm_group F] [normed_space â„ F]
variables {f : E â†’ F} {f' : E â†’L[â„] F} {x : E}

theorem has_fderiv_at_filter_real_equiv {L : filter E} :
  tendsto (Î» x' : E, â€–x' - xâ€–â»Â¹ * â€–f x' - f x - f' (x' - x)â€–) L (ğ“ 0) â†”
  tendsto (Î» x' : E, â€–x' - xâ€–â»Â¹ â€¢ (f x' - f x - f' (x' - x))) L (ğ“ 0) :=
begin
  symmetry,
  rw [tendsto_iff_norm_tendsto_zero], refine tendsto_congr (Î» x', _),
  have : â€–x' - xâ€–â»Â¹ â‰¥ 0, from inv_nonneg.mpr (norm_nonneg _),
  simp [norm_smul, abs_of_nonneg this]
end

lemma has_fderiv_at.lim_real (hf : has_fderiv_at f f' x) (v : E) :
  tendsto (Î» (c:â„), c â€¢ (f (x + câ»Â¹ â€¢ v) - f x)) at_top (ğ“ (f' v)) :=
begin
  apply hf.lim v,
  rw tendsto_at_top_at_top,
  exact Î» b, âŸ¨b, Î» a ha, le_trans ha (le_abs_self _)âŸ©
end

end

section tangent_cone

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
{f : E â†’ F} {s : set E} {f' : E â†’L[ğ•œ] F}

/-- The image of a tangent cone under the differential of a map is included in the tangent cone to
the image. -/
lemma has_fderiv_within_at.maps_to_tangent_cone {x : E} (h : has_fderiv_within_at f f' s x) :
  maps_to f' (tangent_cone_at ğ•œ s x) (tangent_cone_at ğ•œ (f '' s) (f x)) :=
begin
  rintros v âŸ¨c, d, dtop, clim, cdlimâŸ©,
  refine âŸ¨c, (Î»n, f (x + d n) - f x), mem_of_superset dtop _, clim,
    h.lim at_top dtop clim cdlimâŸ©,
  simp [-mem_image, mem_image_of_mem] {contextual := tt}
end

/-- If a set has the unique differentiability property at a point x, then the image of this set
under a map with onto derivative has also the unique differentiability property at the image point.
-/
lemma has_fderiv_within_at.unique_diff_within_at {x : E} (h : has_fderiv_within_at f f' s x)
  (hs : unique_diff_within_at ğ•œ s x) (h' : dense_range f') :
  unique_diff_within_at ğ•œ (f '' s) (f x) :=
begin
  refine âŸ¨h'.dense_of_maps_to f'.continuous hs.1 _,
    h.continuous_within_at.mem_closure_image hs.2âŸ©,
  show submodule.span ğ•œ (tangent_cone_at ğ•œ s x) â‰¤
    (submodule.span ğ•œ (tangent_cone_at ğ•œ (f '' s) (f x))).comap f',
  rw [submodule.span_le],
  exact h.maps_to_tangent_cone.mono (subset.refl _) submodule.subset_span
end

lemma unique_diff_on.image {f' : E â†’ E â†’L[ğ•œ] F} (hs : unique_diff_on ğ•œ s)
  (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x) (hd : âˆ€ x âˆˆ s, dense_range (f' x)) :
  unique_diff_on ğ•œ (f '' s) :=
ball_image_iff.2 $ Î» x hx, (hf' x hx).unique_diff_within_at (hs x hx) (hd x hx)

lemma has_fderiv_within_at.unique_diff_within_at_of_continuous_linear_equiv
  {x : E} (e' : E â‰ƒL[ğ•œ] F) (h : has_fderiv_within_at f (e' : E â†’L[ğ•œ] F) s x)
  (hs : unique_diff_within_at ğ•œ s x) :
  unique_diff_within_at ğ•œ (f '' s) (f x) :=
h.unique_diff_within_at hs e'.surjective.dense_range

lemma continuous_linear_equiv.unique_diff_on_image (e : E â‰ƒL[ğ•œ] F) (h : unique_diff_on ğ•œ s) :
  unique_diff_on ğ•œ (e '' s) :=
h.image (Î» x _, e.has_fderiv_within_at) (Î» x hx, e.surjective.dense_range)

@[simp] lemma continuous_linear_equiv.unique_diff_on_image_iff (e : E â‰ƒL[ğ•œ] F) :
  unique_diff_on ğ•œ (e '' s) â†” unique_diff_on ğ•œ s :=
âŸ¨Î» h, e.symm_image_image s â–¸ e.symm.unique_diff_on_image h, e.unique_diff_on_imageâŸ©

@[simp] lemma continuous_linear_equiv.unique_diff_on_preimage_iff (e : F â‰ƒL[ğ•œ] E) :
  unique_diff_on ğ•œ (e â»Â¹' s) â†” unique_diff_on ğ•œ s :=
by rw [â† e.image_symm_eq_preimage, e.symm.unique_diff_on_image_iff]

end tangent_cone

section restrict_scalars
/-!
### Restricting from `â„‚` to `â„`, or generally from `ğ•œ'` to `ğ•œ`

If a function is differentiable over `â„‚`, then it is differentiable over `â„`. In this paragraph,
we give variants of this statement, in the general situation where `â„‚` and `â„` are replaced
respectively by `ğ•œ'` and `ğ•œ` where `ğ•œ'` is a normed algebra over `ğ•œ`.
-/

variables (ğ•œ : Type*) [nontrivially_normed_field ğ•œ]
variables {ğ•œ' : Type*} [nontrivially_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
variables {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E] [normed_space ğ•œ' E]
variables [is_scalar_tower ğ•œ ğ•œ' E]
variables {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F] [normed_space ğ•œ' F]
variables [is_scalar_tower ğ•œ ğ•œ' F]
variables {f : E â†’ F} {f' : E â†’L[ğ•œ'] F} {s : set E} {x : E}

lemma has_strict_fderiv_at.restrict_scalars (h : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at f (f'.restrict_scalars ğ•œ) x := h

lemma has_fderiv_at_filter.restrict_scalars {L} (h : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter f (f'.restrict_scalars ğ•œ) x L := h

lemma has_fderiv_at.restrict_scalars (h : has_fderiv_at f f' x) :
  has_fderiv_at f (f'.restrict_scalars ğ•œ) x := h

lemma has_fderiv_within_at.restrict_scalars (h : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at f (f'.restrict_scalars ğ•œ) s x := h

lemma differentiable_at.restrict_scalars (h : differentiable_at ğ•œ' f x) :
  differentiable_at ğ•œ f x :=
(h.has_fderiv_at.restrict_scalars ğ•œ).differentiable_at

lemma differentiable_within_at.restrict_scalars (h : differentiable_within_at ğ•œ' f s x) :
  differentiable_within_at ğ•œ f s x :=
(h.has_fderiv_within_at.restrict_scalars ğ•œ).differentiable_within_at

lemma differentiable_on.restrict_scalars (h : differentiable_on ğ•œ' f s) :
  differentiable_on ğ•œ f s :=
Î»x hx, (h x hx).restrict_scalars ğ•œ

lemma differentiable.restrict_scalars (h : differentiable ğ•œ' f) :
  differentiable ğ•œ f :=
Î»x, (h x).restrict_scalars ğ•œ

lemma has_fderiv_within_at_of_restrict_scalars
  {g' : E â†’L[ğ•œ] F} (h : has_fderiv_within_at f g' s x)
  (H : f'.restrict_scalars ğ•œ = g') : has_fderiv_within_at f f' s x :=
by { rw â† H at h, exact h }

lemma has_fderiv_at_of_restrict_scalars {g' : E â†’L[ğ•œ] F} (h : has_fderiv_at f g' x)
  (H : f'.restrict_scalars ğ•œ = g') : has_fderiv_at f f' x :=
by { rw â† H at h, exact h }

lemma differentiable_at.fderiv_restrict_scalars (h : differentiable_at ğ•œ' f x) :
  fderiv ğ•œ f x = (fderiv ğ•œ' f x).restrict_scalars ğ•œ :=
(h.has_fderiv_at.restrict_scalars ğ•œ).fderiv

lemma differentiable_within_at_iff_restrict_scalars
  (hf : differentiable_within_at ğ•œ f s x) (hs : unique_diff_within_at ğ•œ s x) :
  differentiable_within_at ğ•œ' f s x â†”
  âˆƒ (g' : E â†’L[ğ•œ'] F), g'.restrict_scalars ğ•œ = fderiv_within ğ•œ f s x :=
begin
  split,
  { rintros âŸ¨g', hg'âŸ©,
    exact âŸ¨g', hs.eq (hg'.restrict_scalars ğ•œ) hf.has_fderiv_within_atâŸ©, },
  { rintros âŸ¨f', hf'âŸ©,
    exact âŸ¨f', has_fderiv_within_at_of_restrict_scalars ğ•œ hf.has_fderiv_within_at hf'âŸ©, },
end

lemma differentiable_at_iff_restrict_scalars (hf : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ' f x â†” âˆƒ (g' : E â†’L[ğ•œ'] F), g'.restrict_scalars ğ•œ = fderiv ğ•œ f x :=
begin
  rw [â† differentiable_within_at_univ, â† fderiv_within_univ],
  exact differentiable_within_at_iff_restrict_scalars ğ•œ
    hf.differentiable_within_at unique_diff_within_at_univ,
end

end restrict_scalars

/-! ### Support of derivatives -/

section support

open function
variables (ğ•œ : Type*) {E F : Type*} [nontrivially_normed_field ğ•œ] [normed_add_comm_group E]
  [normed_space ğ•œ E] [normed_add_comm_group F] [normed_space ğ•œ F] {f : E â†’ F}

lemma support_fderiv_subset : support (fderiv ğ•œ f) âŠ† tsupport f :=
begin
  intros x,
  rw [â† not_imp_not],
  intro h2x,
  rw [not_mem_tsupport_iff_eventually_eq] at h2x,
  exact nmem_support.mpr (h2x.fderiv_eq.trans $ fderiv_const_apply 0),
end

lemma has_compact_support.fderiv (hf : has_compact_support f) : has_compact_support (fderiv ğ•œ f) :=
hf.mono' $ support_fderiv_subset ğ•œ

end support
