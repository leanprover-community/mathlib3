/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import analysis.calculus.fderiv.linear
import analysis.calculus.fderiv.comp
import analysis.calculus.fderiv.equiv
import analysis.normed_space.star.basic

/-!
# Star operations on derivatives

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `analysis/calculus/fderiv/basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of the star
operation. Note that these only apply when the field that the derivative is respect to has a trivial
star operation; which as should be expected rules out `ğ•œ = â„‚`.
-/

open_locale classical


variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ] [star_ring ğ•œ] [has_trivial_star ğ•œ]
variables {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_add_comm_group F] [star_add_monoid F] [normed_space ğ•œ F]
  [star_module ğ•œ F] [has_continuous_star F]

variables {f : E â†’ F}
variables {f' : E â†’L[ğ•œ] F}
variables (e : E â†’L[ğ•œ] F)
variables {x : E}
variables {s : set E}
variables {L : filter E}

theorem has_strict_fderiv_at.star (h : has_strict_fderiv_at f f' x) :
  has_strict_fderiv_at (Î» x, star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') x :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).to_continuous_linear_map.has_strict_fderiv_at.comp x h

theorem has_fderiv_at_filter.star (h : has_fderiv_at_filter f f' x L) :
  has_fderiv_at_filter (Î» x, star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') x L :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).to_continuous_linear_map.has_fderiv_at_filter.comp x h filter.tendsto_map

theorem has_fderiv_within_at.star (h : has_fderiv_within_at f f' s x) :
  has_fderiv_within_at (Î» x, star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') s x :=
h.star

theorem has_fderiv_at.star (h : has_fderiv_at f f' x) :
  has_fderiv_at (Î» x, star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') x :=
h.star

lemma differentiable_within_at.star (h : differentiable_within_at ğ•œ f s x) :
  differentiable_within_at ğ•œ (Î» y, star (f y)) s x :=
h.has_fderiv_within_at.star.differentiable_within_at

@[simp] lemma differentiable_within_at_star_iff :
  differentiable_within_at ğ•œ (Î» y, star (f y)) s x â†” differentiable_within_at ğ•œ f s x :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiable_within_at_iff

lemma differentiable_at.star (h : differentiable_at ğ•œ f x) :
  differentiable_at ğ•œ (Î» y, star (f y)) x :=
h.has_fderiv_at.star.differentiable_at

@[simp] lemma differentiable_at_star_iff :
  differentiable_at ğ•œ (Î» y, star (f y)) x â†” differentiable_at ğ•œ f x :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiable_at_iff

lemma differentiable_on.star (h : differentiable_on ğ•œ f s) :
  differentiable_on ğ•œ (Î» y, star (f y)) s :=
Î» x hx, (h x hx).star

@[simp] lemma differentiable_on_star_iff :
  differentiable_on ğ•œ (Î» y, star (f y)) s â†” differentiable_on ğ•œ f s :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiable_on_iff

lemma differentiable.star (h : differentiable ğ•œ f) :
  differentiable ğ•œ (Î» y, star (f y)) :=
Î»x, (h x).star

@[simp] lemma differentiable_star_iff : differentiable ğ•œ (Î» y, star (f y)) â†” differentiable ğ•œ f :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiable_iff

lemma fderiv_within_star (hxs : unique_diff_within_at ğ•œ s x) :
  fderiv_within ğ•œ (Î» y, star (f y)) s x =
    ((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L fderiv_within ğ•œ f s x :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_fderiv_within hxs

@[simp] lemma fderiv_star :
  fderiv ğ•œ (Î» y, star (f y)) x = ((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L fderiv ğ•œ f x :=
(starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_fderiv
