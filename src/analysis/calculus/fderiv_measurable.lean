/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import analysis.calculus.deriv
import measure_theory.constructions.borel_space

/-!
# Derivative is measurable

In this file we prove that the derivative of any function with complete codomain is a measurable
function. Namely, we prove:

* `measurable_set_of_differentiable_at`: the set `{x | differentiable_at ğ•œ f x}` is measurable;
* `measurable_fderiv`: the function `fderiv ğ•œ f` is measurable;
* `measurable_fderiv_apply_const`: for a fixed vector `y`, the function `Î» x, fderiv ğ•œ f x y`
  is measurable;
* `measurable_deriv`: the function `deriv f` is measurable (for `f : ğ•œ â†’ F`).

## Implementation

We give a proof that avoids second-countability issues, by expressing the differentiability set
as a function of open sets in the following way. Define `A (L, r, Îµ)` to be the set of points
where, on a ball of radius roughly `r` around `x`, the function is uniformly approximated by the
linear map `L`, up to `Îµ r`. It is an open set.
Let also `B (L, r, s, Îµ) = A (L, r, Îµ) âˆ© A (L, s, Îµ)`: we require that at two possibly different
scales `r` and `s`, the function is well approximated by the linear map `L`. It is also open.

We claim that the differentiability set of `f` is exactly
`D = â‹‚ Îµ > 0, â‹ƒ Î´ > 0, â‹‚ r, s < Î´, â‹ƒ L, B (L, r, s, Îµ)`.
In other words, for any `Îµ > 0`, we require that there is a size `Î´` such that, for any two scales
below this size, the function is well approximated by a linear map, common to the two scales.

The set `â‹ƒ L, B (L, r, s, Îµ)` is open, as a union of open sets. Converting the intersections and
unions to countable ones (using real numbers of the form `2 ^ (-n)`), it follows that the
differentiability set is measurable.

To prove the claim, there are two inclusions. One is trivial: if the function is differentiable
at `x`, then `x` belongs to `D` (just take `L` to be the derivative, and use that the
differentiability exactly says that the map is well approximated by `L`). This is proved in
`mem_A_of_differentiable` and `differentiable_set_subset_D`.

For the other direction, the difficulty is that `L` in the union may depend on `Îµ, r, s`. The key
point is that, in fact, it doesn't depend too much on them. First, if `x` belongs both to
`A (L, r, Îµ)` and `A (L', r, Îµ)`, then `L` and `L'` have to be close on a shell, and thus
`âˆ¥L - L'âˆ¥` is bounded by `Îµ` (see `norm_sub_le_of_mem_A`). Assume now `x âˆˆ D`. If one has two maps
`L` and `L'` such that `x` belongs to `A (L, r, Îµ)` and to `A (L', r', Îµ')`, one deduces that `L` is
close to `L'` by arguing as follows. Consider another scale `s` smaller than `r` and `r'`. Take a
linear map `Lâ‚` that approximates `f` around `x` both at scales `r` and `s` w.r.t. `Îµ` (it exists as
`x` belongs to `D`). Take also `Lâ‚‚` that approximates `f` around `x` both at scales `r'` and `s`
w.r.t. `Îµ'`. Then `Lâ‚` is close to `L` (as they are close on a shell of radius `r`), and `Lâ‚‚` is
close to `Lâ‚` (as they are close on a shell of radius `s`), and `L'` is close to `Lâ‚‚` (as they are
close on a shell of radius `r'`). It follows that `L` is close to `L'`, as we claimed.

It follows that the different approximating linear maps that show up form a Cauchy sequence when
`Îµ` tends to `0`. When the target space is complete, this sequence converges, to a limit `f'`.
With the same kind of arguments, one checks that `f` is differentiable with derivative `f'`.

To show that the derivative itself is measurable, add in the definition of `B` and `D` a set
`K` of continuous linear maps to which `L` should belong. Then, when `K` is complete, the set `D K`
is exactly the set of points where `f` is differentiable with a derivative in `K`.

## Tags

derivative, measurable function, Borel Ïƒ-algebra
-/

noncomputable theory

open set metric asymptotics filter continuous_linear_map
open topological_space (second_countable_topology)
open_locale topological_space

namespace continuous_linear_map

variables {ğ•œ E F : Type*} [nondiscrete_normed_field ğ•œ]
  [normed_group E] [normed_space ğ•œ E] [normed_group F] [normed_space ğ•œ F]

lemma measurable_applyâ‚‚ [measurable_space E] [opens_measurable_space E]
  [second_countable_topology E] [second_countable_topology (E â†’L[ğ•œ] F)]
  [measurable_space F] [borel_space F] :
  measurable (Î» p : (E â†’L[ğ•œ] F) Ã— E, p.1 p.2) :=
is_bounded_bilinear_map_apply.continuous.measurable

end continuous_linear_map

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_group F] [normed_space ğ•œ F]
variables {f : E â†’ F} (K : set (E â†’L[ğ•œ] F))

namespace fderiv_measurable_aux

/-- The set `A f L r Îµ` is the set of points `x` around which the function `f` is well approximated
at scale `r` by the linear map `L`, up to an error `Îµ`. We tweak the definition to make sure that
this is an open set.-/
def A (f : E â†’ F) (L : E â†’L[ğ•œ] F) (r Îµ : â„) : set E :=
{x | âˆƒ r' âˆˆ Ioc (r/2) r, âˆ€ y z âˆˆ ball x r', âˆ¥f z - f y - L (z-y)âˆ¥ â‰¤ Îµ * r}

/-- The set `B f K r s Îµ` is the set of points `x` around which there exists a continuous linear map
`L` belonging to `K` (a given set of continuous linear maps) that approximates well the
function `f` (up to an error `Îµ`), simultaneously at scales `r` and `s`. -/
def B (f : E â†’ F) (K : set (E â†’L[ğ•œ] F)) (r s Îµ : â„) : set E :=
â‹ƒ (L âˆˆ K), (A f L r Îµ) âˆ© (A f L s Îµ)

/-- The set `D f K` is a complicated set constructed using countable intersections and unions. Its
main use is that, when `K` is complete, it is exactly the set of points where `f` is differentiable,
with a derivative in `K`. -/
def D (f : E â†’ F) (K : set (E â†’L[ğ•œ] F)) : set E :=
â‹‚ (e : â„•), â‹ƒ (n : â„•), â‹‚ (p â‰¥ n) (q â‰¥ n), B f K ((1/2) ^ p) ((1/2) ^ q) ((1/2) ^ e)

lemma is_open_A (L : E â†’L[ğ•œ] F) (r Îµ : â„) : is_open (A f L r Îµ) :=
begin
  rw metric.is_open_iff,
  rintros x âŸ¨r', r'_mem, hr'âŸ©,
  obtain âŸ¨s, s_gt, s_ltâŸ© : âˆƒ (s : â„), r / 2 < s âˆ§ s < r' := exists_between r'_mem.1,
  have : s âˆˆ Ioc (r/2) r := âŸ¨s_gt, le_of_lt (s_lt.trans_le r'_mem.2)âŸ©,
  refine âŸ¨r' - s, by linarith, Î» x' hx', âŸ¨s, this, _âŸ©âŸ©,
  have B : ball x' s âŠ† ball x r' := ball_subset (le_of_lt hx'),
  assume y z hy hz,
  exact hr' y z (B hy) (B hz)
end

lemma is_open_B {K : set (E â†’L[ğ•œ] F)} {r s Îµ : â„} : is_open (B f K r s Îµ) :=
by simp [B, is_open_Union, is_open.inter, is_open_A]

lemma A_mono (L : E â†’L[ğ•œ] F) (r : â„) {Îµ Î´ : â„} (h : Îµ â‰¤ Î´) :
  A f L r Îµ âŠ† A f L r Î´ :=
begin
  rintros x âŸ¨r', r'r, hr'âŸ©,
  refine âŸ¨r', r'r, Î» y z hy hz, _âŸ©,
  apply le_trans (hr' y z hy hz),
  apply mul_le_mul_of_nonneg_right h,
  linarith [mem_ball.1 hy, r'r.2, @dist_nonneg _ _ y x],
end

lemma le_of_mem_A {r Îµ : â„} {L : E â†’L[ğ•œ] F} {x : E} (hx : x âˆˆ A f L r Îµ)
  {y z : E} (hy : y âˆˆ closed_ball x (r/2)) (hz : z âˆˆ closed_ball x (r/2)) :
  âˆ¥f z - f y - L (z-y)âˆ¥ â‰¤ Îµ * r :=
begin
  rcases hx with âŸ¨r', r'mem, hr'âŸ©,
  exact hr' _ _ (lt_of_le_of_lt (mem_closed_ball.1 hy) r'mem.1)
    (lt_of_le_of_lt (mem_closed_ball.1 hz) r'mem.1)
end

lemma mem_A_of_differentiable {Îµ : â„} (hÎµ : 0 < Îµ) {x : E} (hx : differentiable_at ğ•œ f x) :
  âˆƒ R > 0, âˆ€ r âˆˆ Ioo (0 : â„) R, x âˆˆ A f (fderiv ğ•œ f x) r Îµ :=
begin
  have := hx.has_fderiv_at,
  simp only [has_fderiv_at, has_fderiv_at_filter, is_o_iff] at this,
  rcases eventually_nhds_iff_ball.1 (this (half_pos hÎµ)) with âŸ¨R, R_pos, hRâŸ©,
  refine âŸ¨R, R_pos, Î» r hr, _âŸ©,
  have : r âˆˆ Ioc (r/2) r := âŸ¨half_lt_self hr.1, le_refl _âŸ©,
  refine âŸ¨r, this, Î» y z hy hz, _âŸ©,
  calc  âˆ¥f z - f y - (fderiv ğ•œ f x) (z - y)âˆ¥
      = âˆ¥(f z - f x - (fderiv ğ•œ f x) (z - x)) - (f y - f x - (fderiv ğ•œ f x) (y - x))âˆ¥ :
    by { congr' 1, simp only [continuous_linear_map.map_sub], abel }
  ... â‰¤ âˆ¥(f z - f x - (fderiv ğ•œ f x) (z - x))âˆ¥ + âˆ¥f y - f x - (fderiv ğ•œ f x) (y - x)âˆ¥ :
    norm_sub_le _ _
  ... â‰¤ Îµ / 2 * âˆ¥z - xâˆ¥ + Îµ / 2 * âˆ¥y - xâˆ¥ :
    add_le_add (hR _ (lt_trans (mem_ball.1 hz) hr.2)) (hR _ (lt_trans (mem_ball.1 hy) hr.2))
  ... â‰¤ Îµ / 2 * r + Îµ / 2 * r :
    add_le_add
      (mul_le_mul_of_nonneg_left (le_of_lt (mem_ball_iff_norm.1 hz)) (le_of_lt (half_pos hÎµ)))
      (mul_le_mul_of_nonneg_left (le_of_lt (mem_ball_iff_norm.1 hy)) (le_of_lt (half_pos hÎµ)))
  ... = Îµ * r : by ring
end

lemma norm_sub_le_of_mem_A {c : ğ•œ} (hc : 1 < âˆ¥câˆ¥)
  {r Îµ : â„} (hÎµ : 0 < Îµ) (hr : 0 < r) {x : E} {Lâ‚ Lâ‚‚ : E â†’L[ğ•œ] F}
  (hâ‚ : x âˆˆ A f Lâ‚ r Îµ) (hâ‚‚ : x âˆˆ A f Lâ‚‚ r Îµ) : âˆ¥Lâ‚ - Lâ‚‚âˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * Îµ :=
begin
  have : 0 â‰¤ 4 * âˆ¥câˆ¥ * Îµ :=
    mul_nonneg (mul_nonneg (by norm_num : (0 : â„) â‰¤ 4) (norm_nonneg _)) hÎµ.le,
  apply op_norm_le_of_shell (half_pos hr) this hc,
  assume y ley ylt,
  rw [div_div_eq_div_mul,
      div_le_iff' (mul_pos (by norm_num : (0 : â„) < 2) (zero_lt_one.trans hc))] at ley,
  calc âˆ¥(Lâ‚ - Lâ‚‚) yâˆ¥
        = âˆ¥(f (x + y) - f x - Lâ‚‚ ((x + y) - x)) - (f (x + y) - f x - Lâ‚ ((x + y) - x))âˆ¥ : by simp
    ... â‰¤ âˆ¥(f (x + y) - f x - Lâ‚‚ ((x + y) - x))âˆ¥ + âˆ¥(f (x + y) - f x - Lâ‚ ((x + y) - x))âˆ¥ :
      norm_sub_le _ _
    ... â‰¤ Îµ * r + Îµ * r :
      begin
        apply add_le_add,
        { apply le_of_mem_A hâ‚‚,
          { simp only [le_of_lt (half_pos hr), mem_closed_ball, dist_self] },
          { simp only [dist_eq_norm, add_sub_cancel', mem_closed_ball, ylt.le], } },
        { apply le_of_mem_A hâ‚,
          { simp only [le_of_lt (half_pos hr), mem_closed_ball, dist_self] },
          { simp only [dist_eq_norm, add_sub_cancel', mem_closed_ball, ylt.le] } },
      end
    ... = 2 * Îµ * r : by ring
    ... â‰¤ 2 * Îµ * (2 * âˆ¥câˆ¥ * âˆ¥yâˆ¥) : mul_le_mul_of_nonneg_left ley (mul_nonneg (by norm_num) hÎµ.le)
    ... = 4 * âˆ¥câˆ¥ * Îµ * âˆ¥yâˆ¥ : by ring
end

/-- Easy inclusion: a differentiability point with derivative in `K` belongs to `D f K`. -/
lemma differentiable_set_subset_D : {x | differentiable_at ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K} âŠ† D f K :=
begin
  assume x hx,
  rw [D, mem_Inter],
  assume e,
  have : (0 : â„) < (1/2) ^ e := pow_pos (by norm_num) _,
  rcases mem_A_of_differentiable this hx.1 with âŸ¨R, R_pos, hRâŸ©,
  obtain âŸ¨n, hnâŸ© : âˆƒ (n : â„•), (1/2) ^ n < R :=
    exists_pow_lt_of_lt_one R_pos (by norm_num : (1 : â„)/2 < 1),
  simp only [mem_Union, mem_Inter, B, mem_inter_eq],
  refine âŸ¨n, Î» p hp q hq, âŸ¨fderiv ğ•œ f x, hx.2, âŸ¨_, _âŸ©âŸ©âŸ©;
  { refine hR _ âŸ¨pow_pos (by norm_num) _, lt_of_le_of_lt _ hnâŸ©,
    exact pow_le_pow_of_le_one (by norm_num) (by norm_num) (by assumption) }
end

/-- Harder inclusion: at a point in `D f K`, the function `f` has a derivative, in `K`. -/
lemma D_subset_differentiable_set {K : set (E â†’L[ğ•œ] F)} (hK : is_complete K) :
  D f K âŠ† {x | differentiable_at ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K} :=
begin
  have P : âˆ€ {n : â„•}, (0 : â„) < (1/2) ^ n := pow_pos (by norm_num),
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  have cpos : 0 < âˆ¥câˆ¥ := lt_trans zero_lt_one hc,
  assume x hx,
  have : âˆ€ (e : â„•), âˆƒ (n : â„•), âˆ€ p q, n â‰¤ p â†’ n â‰¤ q â†’ âˆƒ L âˆˆ K,
    x âˆˆ A f L ((1/2) ^ p) ((1/2) ^ e) âˆ© A f L ((1/2) ^ q) ((1/2) ^ e),
  { assume e,
    have := mem_Inter.1 hx e,
    rcases mem_Union.1 this with âŸ¨n, hnâŸ©,
    refine âŸ¨n, Î» p q hp hq, _âŸ©,
    simp only [mem_Inter, ge_iff_le] at hn,
    rcases mem_Union.1 (hn p hp q hq) with âŸ¨L, hLâŸ©,
    exact âŸ¨L, mem_Union.1 hLâŸ©, },
  /- Recast the assumptions: for each `e`, there exist `n e` and linear maps `L e p q` in `K`
  such that, for `p, q â‰¥ n e`, then `f` is well approximated by `L e p q` at scale `2 ^ (-p)` and
  `2 ^ (-q)`, with an error `2 ^ (-e)`. -/
  choose! n L hn using this,
  /- All the operators `L e p q` that show up are close to each other. To prove this, we argue
    that `L e p q` is close to `L e p r` (where `r` is large enough), as both approximate `f` at
    scale `2 ^(- p)`. And `L e p r` is close to `L e' p' r` as both approximate `f` at scale
    `2 ^ (- r)`. And `L e' p' r` is close to `L e' p' q'` as both approximate `f` at scale
    `2 ^ (- p')`. -/
  have M : âˆ€ e p q e' p' q', n e â‰¤ p â†’ n e â‰¤ q â†’ n e' â‰¤ p' â†’ n e' â‰¤ q' â†’ e â‰¤ e' â†’
    âˆ¥L e p q - L e' p' q'âˆ¥ â‰¤ 12 * âˆ¥câˆ¥ * (1/2) ^ e,
  { assume e p q e' p' q' hp hq hp' hq' he',
    let r := max (n e) (n e'),
    have I : ((1:â„)/2)^e' â‰¤ (1/2)^e := pow_le_pow_of_le_one (by norm_num) (by norm_num) he',
    have J1 : âˆ¥L e p q - L e p râˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * (1/2)^e,
    { have I1 : x âˆˆ A f (L e p q) ((1 / 2) ^ p) ((1/2)^e) :=
        (hn e p q hp hq).2.1,
      have I2 : x âˆˆ A f (L e p r) ((1 / 2) ^ p) ((1/2)^e) :=
        (hn e p r hp (le_max_left _ _)).2.1,
      exact norm_sub_le_of_mem_A hc P P I1 I2 },
    have J2 : âˆ¥L e p r - L e' p' râˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * (1/2)^e,
    { have I1 : x âˆˆ A f (L e p r) ((1 / 2) ^ r) ((1/2)^e) :=
        (hn e p r hp (le_max_left _ _)).2.2,
      have I2 : x âˆˆ A f (L e' p' r) ((1 / 2) ^ r) ((1/2)^e') :=
        (hn e' p' r hp' (le_max_right _ _)).2.2,
      exact norm_sub_le_of_mem_A hc P P I1 (A_mono _ _ I I2) },
    have J3 : âˆ¥L e' p' r - L e' p' q'âˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * (1/2)^e,
    { have I1 : x âˆˆ A f (L e' p' r) ((1 / 2) ^ p') ((1/2)^e') :=
        (hn e' p' r hp' (le_max_right _ _)).2.1,
      have I2 : x âˆˆ A f (L e' p' q') ((1 / 2) ^ p') ((1/2)^e') :=
        (hn e' p' q' hp' hq').2.1,
      exact norm_sub_le_of_mem_A hc P P (A_mono _ _ I I1) (A_mono _ _ I I2) },
    calc âˆ¥L e p q - L e' p' q'âˆ¥
          = âˆ¥(L e p q - L e p r) + (L e p r - L e' p' r) + (L e' p' r - L e' p' q')âˆ¥ :
        by { congr' 1, abel }
      ... â‰¤ âˆ¥L e p q - L e p râˆ¥ + âˆ¥L e p r - L e' p' râˆ¥ + âˆ¥L e' p' r - L e' p' q'âˆ¥ :
        le_trans (norm_add_le _ _) (add_le_add_right (norm_add_le _ _) _)
      ... â‰¤ 4 * âˆ¥câˆ¥ * (1/2)^e + 4 * âˆ¥câˆ¥ * (1/2)^e + 4 * âˆ¥câˆ¥ * (1/2)^e :
        by apply_rules [add_le_add]
      ... = 12 * âˆ¥câˆ¥ * (1/2)^e : by ring },
  /- For definiteness, use `L0 e = L e (n e) (n e)`, to have a single sequence. We claim that this
  is a Cauchy sequence. -/
  let L0 : â„• â†’ (E â†’L[ğ•œ] F) := Î» e, L e (n e) (n e),
  have : cauchy_seq L0,
  { rw metric.cauchy_seq_iff',
    assume Îµ Îµpos,
    obtain âŸ¨e, heâŸ© : âˆƒ (e : â„•), (1/2) ^ e < Îµ / (12 * âˆ¥câˆ¥) :=
      exists_pow_lt_of_lt_one (div_pos Îµpos (mul_pos (by norm_num) cpos)) (by norm_num),
    refine âŸ¨e, Î» e' he', _âŸ©,
    rw [dist_comm, dist_eq_norm],
    calc âˆ¥L0 e - L0 e'âˆ¥
          â‰¤ 12 * âˆ¥câˆ¥ * (1/2)^e : M _ _ _ _ _ _ (le_refl _) (le_refl _) (le_refl _) (le_refl _) he'
      ... < 12 * âˆ¥câˆ¥ * (Îµ / (12 * âˆ¥câˆ¥)) :
        mul_lt_mul' (le_refl _) he (le_of_lt P) (mul_pos (by norm_num) cpos)
      ... = Îµ : by { field_simp [(by norm_num : (12 : â„) â‰  0), ne_of_gt cpos], ring } },
  /- As it is Cauchy, the sequence `L0` converges, to a limit `f'` in `K`.-/
  obtain âŸ¨f', f'K, hf'âŸ© : âˆƒ f' âˆˆ K, tendsto L0 at_top (ğ“ f') :=
    cauchy_seq_tendsto_of_is_complete hK (Î» e, (hn e (n e) (n e) (le_refl _) (le_refl _)).1) this,
  have Lf' : âˆ€ e p, n e â‰¤ p â†’ âˆ¥L e (n e) p - f'âˆ¥ â‰¤ 12 * âˆ¥câˆ¥ * (1/2)^e,
  { assume e p hp,
    apply le_of_tendsto (tendsto_const_nhds.sub hf').norm,
    rw eventually_at_top,
    exact âŸ¨e, Î» e' he', M _ _ _ _ _ _ (le_refl _) hp (le_refl _) (le_refl _) he'âŸ© },
  /- Let us show that `f` has derivative `f'` at `x`. -/
  have : has_fderiv_at f f' x,
  { simp only [has_fderiv_at_iff_is_o_nhds_zero, is_o_iff],
    /- to get an approximation with a precision `Îµ`, we will replace `f` with `L e (n e) m` for
    some large enough `e` (yielding a small error by uniform approximation). As one can vary `m`,
    this makes it possible to cover all scales, and thus to obtain a good linear approximation in
    the whole ball of radius `(1/2)^(n e)`. -/
    assume Îµ Îµpos,
    have pos : 0 < 4 + 12 * âˆ¥câˆ¥ :=
      add_pos_of_pos_of_nonneg (by norm_num) (mul_nonneg (by norm_num) (norm_nonneg _)),
    obtain âŸ¨e, heâŸ© : âˆƒ (e : â„•), (1 / 2) ^ e < Îµ / (4 + 12 * âˆ¥câˆ¥) :=
      exists_pow_lt_of_lt_one (div_pos Îµpos pos) (by norm_num),
    rw eventually_nhds_iff_ball,
    refine âŸ¨(1/2) ^ (n e + 1), P, Î» y hy, _âŸ©,
    -- We need to show that `f (x + y) - f x - f' y` is small. For this, we will work at scale
    -- `k` where `k` is chosen with `âˆ¥yâˆ¥ âˆ¼ 2 ^ (-k)`.
    by_cases y_pos : y = 0, {simp [y_pos] },
    have yzero : 0 < âˆ¥yâˆ¥ := norm_pos_iff.mpr y_pos,
    have y_lt : âˆ¥yâˆ¥ < (1/2) ^ (n e + 1), by simpa using mem_ball_iff_norm.1 hy,
    have yone : âˆ¥yâˆ¥ â‰¤ 1 :=
      le_trans (y_lt.le) (pow_le_one _ (by norm_num) (by norm_num)),
    -- define the scale `k`.
    obtain âŸ¨k, hk, h'kâŸ© : âˆƒ (k : â„•), (1/2) ^ (k + 1) < âˆ¥yâˆ¥ âˆ§ âˆ¥yâˆ¥ â‰¤ (1/2) ^ k :=
      exists_nat_pow_near_of_lt_one yzero yone (by norm_num : (0 : â„) < 1/2)
      (by norm_num : (1 : â„)/2 < 1),
    -- the scale is large enough (as `y` is small enough)
    have k_gt : n e < k,
    { have : ((1:â„)/2) ^ (k + 1) < (1/2) ^ (n e + 1) := lt_trans hk y_lt,
      rw pow_lt_pow_iff_of_lt_one (by norm_num : (0 : â„) < 1/2) (by norm_num) at this,
      linarith },
    set m := k - 1 with hl,
    have m_ge : n e â‰¤ m := nat.le_pred_of_lt k_gt,
    have km : k = m + 1 := (nat.succ_pred_eq_of_pos (lt_of_le_of_lt (zero_le _) k_gt)).symm,
    rw km at hk h'k,
    -- `f` is well approximated by `L e (n e) k` at the relevant scale
    -- (in fact, we use `m = k - 1` instead of `k` because of the precise definition of `A`).
    have J1 : âˆ¥f (x + y) - f x - L e (n e) m ((x + y) - x)âˆ¥ â‰¤ (1/2) ^ e * (1/2) ^ m,
    { apply le_of_mem_A (hn e (n e) m (le_refl _) m_ge).2.2,
      { simp only [mem_closed_ball, dist_self],
        exact div_nonneg (le_of_lt P) (zero_le_two) },
      { simp [dist_eq_norm],
        convert h'k,
        field_simp,
        ring_exp } },
    have J2 : âˆ¥f (x + y) - f x - L e (n e) m yâˆ¥ â‰¤ 4 * (1/2) ^ e * âˆ¥yâˆ¥ := calc
      âˆ¥f (x + y) - f x - L e (n e) m yâˆ¥ â‰¤ (1/2) ^ e * (1/2) ^ m :
        by simpa only [add_sub_cancel'] using J1
      ... = 4 * (1/2) ^ e * (1/2) ^ (m + 2) : by { field_simp, ring_exp }
      ... â‰¤ 4 * (1/2) ^ e * âˆ¥yâˆ¥ :
        mul_le_mul_of_nonneg_left (le_of_lt hk) (mul_nonneg (by norm_num) (le_of_lt P)),
    -- use the previous estimates to see that `f (x + y) - f x - f' y` is small.
    calc âˆ¥f (x + y) - f x - f' yâˆ¥
        = âˆ¥(f (x + y) - f x - L e (n e) m y) + (L e (n e) m - f') yâˆ¥ :
      by { congr' 1, simp, abel }
    ... â‰¤ âˆ¥f (x + y) - f x - L e (n e) m yâˆ¥ + âˆ¥(L e (n e) m - f') yâˆ¥ :
      norm_add_le _ _
    ... â‰¤ 4 * (1/2) ^ e * âˆ¥yâˆ¥ + 12 * âˆ¥câˆ¥ * (1/2) ^ e * âˆ¥yâˆ¥ :
      add_le_add J2
        (le_trans (le_op_norm _ _) (mul_le_mul_of_nonneg_right (Lf' _ _ m_ge) (norm_nonneg _)))
    ... = (4 + 12 * âˆ¥câˆ¥) * âˆ¥yâˆ¥ * (1/2) ^ e : by ring
    ... â‰¤ (4 + 12 * âˆ¥câˆ¥) * âˆ¥yâˆ¥ * (Îµ / (4 + 12 * âˆ¥câˆ¥)) :
      mul_le_mul_of_nonneg_left he.le
        (mul_nonneg (add_nonneg (by norm_num) (mul_nonneg (by norm_num) (norm_nonneg _)))
          (norm_nonneg _))
    ... = Îµ * âˆ¥yâˆ¥ : by { field_simp [ne_of_gt pos], ring } },
  rw â† this.fderiv at f'K,
  exact âŸ¨this.differentiable_at, f'KâŸ©
end

theorem differentiable_set_eq_D (hK : is_complete K) :
  {x | differentiable_at ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K} = D f K :=
subset.antisymm (differentiable_set_subset_D _) (D_subset_differentiable_set hK)

end fderiv_measurable_aux

open fderiv_measurable_aux

variables [measurable_space E] [opens_measurable_space E]
variables (ğ•œ f)

/-- The set of differentiability points of a function, with derivative in a given complete set,
is Borel-measurable. -/
theorem measurable_set_of_differentiable_at_of_is_complete
  {K : set (E â†’L[ğ•œ] F)} (hK : is_complete K) :
  measurable_set {x | differentiable_at ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K} :=
by simp [differentiable_set_eq_D K hK, D, is_open_B.measurable_set, measurable_set.Inter_Prop,
         measurable_set.Inter, measurable_set.Union]

variable [complete_space F]

/-- The set of differentiability points of a function taking values in a complete space is
Borel-measurable. -/
theorem measurable_set_of_differentiable_at :
  measurable_set {x | differentiable_at ğ•œ f x} :=
begin
  have : is_complete (univ : set (E â†’L[ğ•œ] F)) := complete_univ,
  convert measurable_set_of_differentiable_at_of_is_complete ğ•œ f this,
  simp
end

lemma measurable_fderiv : measurable (fderiv ğ•œ f) :=
begin
  refine measurable_of_is_closed (Î» s hs, _),
  have : fderiv ğ•œ f â»Â¹' s = {x | differentiable_at ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ s} âˆª
    {x | (0 : E â†’L[ğ•œ] F) âˆˆ s} âˆ© {x | Â¬differentiable_at ğ•œ f x} :=
    set.ext (Î» x, mem_preimage.trans fderiv_mem_iff),
  rw this,
  exact (measurable_set_of_differentiable_at_of_is_complete _ _ hs.is_complete).union
    ((measurable_set.const _).inter (measurable_set_of_differentiable_at _ _).compl)
end

lemma measurable_fderiv_apply_const [measurable_space F] [borel_space F] (y : E) :
  measurable (Î» x, fderiv ğ•œ f x y) :=
(continuous_linear_map.measurable_apply y).comp (measurable_fderiv ğ•œ f)

variable {ğ•œ}

lemma measurable_deriv [measurable_space ğ•œ] [opens_measurable_space ğ•œ] [measurable_space F]
  [borel_space F] (f : ğ•œ â†’ F) : measurable (deriv f) :=
by simpa only [fderiv_deriv] using measurable_fderiv_apply_const ğ•œ f 1
