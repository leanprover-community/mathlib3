/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/

import analysis.calculus.fderiv measure_theory.borel_space

/-!
# Measurability of the derivative

Consider a function between `ğ•œ`-vector spaces, where the target space is complete. We prove that
the set of its differentiability points is Borel-measurable, in `is_measurable_differentiable`.

TODO: show that the derivative itself (defined to be `0` where the function is not differentiable)
is measurable.

## Implementation

We give a proof that avoids second-countability issues, by expressing the differentiability set
as a function of open sets in the following way. Define `A (L, r, Îµ)` to be the set of points
where, on a ball of radius roughly `r` around `x`, the function is uniformly approximated by the
linear map, up to `Îµ r`. It is an open set. Let also `B (L, r, s, Îµ) = A (L, r, Îµ) âˆ© A (L, s, Îµ)`:
we require that at two possibly different scales `r` and `s`, the function is well approximated by
the linear map.

We claim that the differentiability set of `f` is exactly
`â‹‚ Îµ > 0, â‹ƒ Î´ > 0, â‹‚ r, s < Î´, â‹ƒ L, B (L, r, s, Îµ)`.
In other words, for any `Îµ > 0`, there is a size such that, for any two scales below this size, the
function is well approximated by a linear map, common to the two scales.

The set `â‹ƒ L, B (L, r, s, Îµ)` is open, as a union of open sets. Converting the intersections and
unions to countable ones (using real numbers of the form `2 ^ (-n)`), it follows that the
differentiability set is measurable.

To prove the claim, there are two inclusions. One is trivial: if the function is differentiable
at `x`, then `x` belongs to the above set (just take `L` to be the derivative, and use that the
differentiability exactly says that the map is well approximated by `L`). This is proved in
`mem_A_of_differentiable` and `differentiable_subset_B`.

For the other direction, the difficulty is that `L` in the union may depend on `Îµ, r, s`. The key
point is that, in fact, it doesn't depend too much on them. First, if `x` belongs both to
`A (L, r, Îµ)` and `A (L', r, Îµ)`, then `L` and `L'` have to be close on a shell, and thus
`âˆ¥L - L'âˆ¥` is bounded by `Îµ` (see `norm_sub_le_of_mem_A`). If one has two maps `L` and `L'` such
that `x` belongs to `A (L, r, Îµ)` and to `A (L', r', Îµ')`, one deduces that `L` is close
to `L'` by arguing as follows. Consider another scale `s` smaller than `r` and `r'`. Take a linear
map `Lâ‚` that approximates `f` around `x` both at scales `r` and `s` w.r.t. `Îµ` (it exists as
`x` belongs to our set). Take also `Lâ‚‚` that approximates `f` around `x` both at scales `r'` and `s`
w.r.t. `Îµ'`. Then `Lâ‚` is close to `L` (as they are close on a shell of radius `r`), and `Lâ‚‚` is
close to `Lâ‚` (as they are close on a shell of radius `s`), and `L'` is close to `Lâ‚‚` (as they are
close on a shell of radius `r'`). It follows that `L` is close to `L'`, as we claimed.

It follows that the different approximating linear maps that show up form a Cauchy sequence when
`Îµ` tends to `0`. When the target space is complete, this sequence converges, to a limit `f'`.
With the same kind of arguments, one checks that `f` is differentiable with derivative `f'`.
-/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_group F] [normed_space ğ•œ F]
variable (f : E â†’ F)

open set metric asymptotics filter continuous_linear_map
open_locale topological_space

namespace fderiv_measurable_aux

/-- The set `A f L r Îµ` is the set of points `x` around which the function `f` is well approximated
at scale `r` by the linear map `L`, up to an error `Îµ`. We tweak the definition to make sure that
this is an open set.-/
def A (L : E â†’L[ğ•œ] F) (r Îµ : â„) : set E :=
{x | âˆƒ r' âˆˆ Ioc (r/2) r, âˆ€ y z âˆˆ ball x r', âˆ¥f z - f y - L (z-y)âˆ¥ â‰¤ Îµ * r}

lemma A_mono (L : E â†’L[ğ•œ] F) (r : â„) {Îµ Î´ : â„} (h : Îµ â‰¤ Î´) :
  A f L r Îµ âŠ† A f L r Î´ :=
begin
  rintros x âŸ¨r', r'r, hr'âŸ©,
  refine âŸ¨r', r'r, Î» y z hy hz, _âŸ©,
  apply le_trans (hr' y z hy hz),
  apply mul_le_mul_of_nonneg_right h,
  linarith [mem_ball.1 hy, r'r.2, @dist_nonneg _ _ y x],
end

variable {f}
lemma le_of_mem_A {r Îµ : â„} {L : E â†’L[ğ•œ] F} {x : E} (hx : x âˆˆ A f L r Îµ)
  {y z : E} (hy : y âˆˆ closed_ball x (r/2)) (hz : z âˆˆ closed_ball x (r/2)) :
  âˆ¥f z - f y - L (z-y)âˆ¥ â‰¤ Îµ * r :=
begin
  rcases hx with âŸ¨r', r'mem, hr'âŸ©,
  exact hr' _ _ (lt_of_le_of_lt (mem_closed_ball.1 hy) r'mem.1)
    (lt_of_le_of_lt (mem_closed_ball.1 hz) r'mem.1)
end
variable (f)

lemma is_open_A (L : E â†’L[ğ•œ] F) (r Îµ : â„) : is_open (A f L r Îµ) :=
begin
  rw metric.is_open_iff,
  rintros x âŸ¨r', r'_mem, hr'âŸ©,
  obtain âŸ¨s, s_gt, s_ltâŸ© : âˆƒ (s : â„), r / 2 < s âˆ§ s < r' := exists_between r'_mem.1,
  have : s âˆˆ Ioc (r/2) r := âŸ¨s_gt, le_of_lt (s_lt.trans_le r'_mem.2)âŸ©,
  refine âŸ¨r' - s, by linarith, Î» x' hx', âŸ¨s, this, _âŸ©âŸ©,
  have B : ball x' s âŠ† ball x r' := ball_subset (le_of_lt hx'),
  assume y z hy hz,
  exact hr' y z (B hy) (B hz)
end

variable {f}

lemma mem_A_of_differentiable {Îµ : â„} (hÎµ : 0 < Îµ) {x : E} (hx : differentiable_at ğ•œ f x) :
  âˆƒ R > 0, âˆ€ r âˆˆ Ioo (0 : â„) R, x âˆˆ A f (fderiv ğ•œ f x) r Îµ :=
begin
  have := hx.has_fderiv_at,
  simp only [has_fderiv_at, has_fderiv_at_filter, is_o_iff] at this,
  rcases eventually_nhds_iff_ball.1 (this (half_pos hÎµ)) with âŸ¨R, R_pos, hRâŸ©,
  refine âŸ¨R, R_pos, Î» r hr, _âŸ©,
  have : r âˆˆ Ioc (r/2) r := âŸ¨half_lt_self hr.1, le_refl _âŸ©,
  refine âŸ¨r, this, Î» y z hy hz, _âŸ©,
  calc  âˆ¥f z - f y - (fderiv ğ•œ f x) (z - y)âˆ¥
      = âˆ¥(f z - f x - (fderiv ğ•œ f x) (z - x)) - (f y - f x - (fderiv ğ•œ f x) (y - x))âˆ¥ :
    by { congr' 1, simp only [continuous_linear_map.map_sub], abel }
  ... â‰¤ âˆ¥(f z - f x - (fderiv ğ•œ f x) (z - x))âˆ¥ + âˆ¥f y - f x - (fderiv ğ•œ f x) (y - x)âˆ¥ :
    norm_sub_le _ _
  ... â‰¤ Îµ / 2 * âˆ¥z - xâˆ¥ + Îµ / 2 * âˆ¥y - xâˆ¥ :
    add_le_add (hR _ (lt_trans (mem_ball.1 hz) hr.2)) (hR _ (lt_trans (mem_ball.1 hy) hr.2))
  ... â‰¤ Îµ / 2 * r + Îµ / 2 * r :
    add_le_add
      (mul_le_mul_of_nonneg_left (le_of_lt (mem_ball_iff_norm.1 hz)) (le_of_lt (half_pos hÎµ)))
      (mul_le_mul_of_nonneg_left (le_of_lt (mem_ball_iff_norm.1 hy)) (le_of_lt (half_pos hÎµ)))
  ... = Îµ * r : by ring
end

lemma norm_sub_le_of_mem_A {c : ğ•œ} (hc : 1 < âˆ¥câˆ¥)
  {r Îµ : â„} (hÎµ : 0 < Îµ) (hr : 0 < r) {x : E} {Lâ‚ Lâ‚‚ : E â†’L[ğ•œ] F}
  (hâ‚ : x âˆˆ A f Lâ‚ r Îµ) (hâ‚‚ : x âˆˆ A f Lâ‚‚ r Îµ) : âˆ¥Lâ‚ - Lâ‚‚âˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * Îµ :=
begin
  apply continuous_linear_map.op_norm_le_bound _
    (mul_nonneg (mul_nonneg (by norm_num : (0 : â„) â‰¤ 4) (norm_nonneg _)) (le_of_lt hÎµ)),
  assume y,
  by_cases hy : y = 0, { simp [hy] },
  rcases rescale_to_shell hc (half_pos hr) hy with âŸ¨d, d_pos, dy_le, le_dy, dinvâŸ©,
  have M : âˆ¥(Lâ‚ - Lâ‚‚) (d â€¢ y)âˆ¥ â‰¤ 2 * Îµ * r := calc
    âˆ¥(Lâ‚ - Lâ‚‚) (d â€¢ y)âˆ¥
        = âˆ¥(f (x + d â€¢ y) - f x - Lâ‚‚ ((x + d â€¢ y) - x))
            - (f (x + d â€¢ y) - f x - Lâ‚ ((x + d â€¢ y) - x))âˆ¥ : by simp
    ... â‰¤ âˆ¥(f (x + d â€¢ y) - f x - Lâ‚‚ ((x + d â€¢ y) - x))âˆ¥
          + âˆ¥(f (x + d â€¢ y) - f x - Lâ‚ ((x + d â€¢ y) - x))âˆ¥ : norm_sub_le _ _
    ... â‰¤ Îµ * r + Îµ * r :
      begin
        apply add_le_add,
        { apply le_of_mem_A hâ‚‚,
          { simp only [le_of_lt (half_pos hr), mem_closed_ball, dist_self] },
          { simp only [dist_eq_norm, add_sub_cancel', mem_closed_ball, dy_le] } },
        { apply le_of_mem_A hâ‚,
          { simp only [le_of_lt (half_pos hr), mem_closed_ball, dist_self] },
          { simp only [dist_eq_norm, add_sub_cancel', mem_closed_ball, dy_le] } },
      end
    ... = 2 * Îµ * r : by ring,
  calc âˆ¥(Lâ‚ - Lâ‚‚) yâˆ¥
      = âˆ¥(Lâ‚ - Lâ‚‚) (dâ»Â¹ â€¢ (d â€¢ y))âˆ¥ : by rw [smul_smul, inv_mul_cancel d_pos, one_smul]
  ... = âˆ¥dâˆ¥â»Â¹ * âˆ¥(Lâ‚ - Lâ‚‚) (d â€¢ y)âˆ¥ :
    by simp [-continuous_linear_map.coe_sub', norm_smul]
  ... â‰¤ ((r / 2)â»Â¹ * âˆ¥câˆ¥ * âˆ¥yâˆ¥) * (2 * Îµ * r) :
    mul_le_mul dinv M (norm_nonneg _) (le_trans (inv_nonneg.2 (norm_nonneg _)) dinv)
  ... = 4 * âˆ¥câˆ¥ * Îµ * âˆ¥yâˆ¥ :
    by { field_simp [ne_of_gt hr], ring }
end

variables (ğ•œ f)


/-- The set `B ğ•œ f r s Îµ` is the set of points `x` around which there exists a continuous linear map
`L` that approximates well the function `f` (up to an error `Îµ`), simultaneously at scales
`r` and `s`. -/
def B (r s Îµ : â„) : set E := â‹ƒ (L : E â†’L[ğ•œ] F), (A f L r Îµ) âˆ© (A f L s Îµ)

lemma is_open_B (r s Îµ : â„) : is_open (B ğ•œ f r s Îµ) :=
by simp [B, is_open_Union, is_open_inter, is_open_A]

/-- Easy inclusion: a differentiability point is included in an explicit set defined in terms
of `B` with countable operations. -/
lemma differentiable_subset_B :
  {x | differentiable_at ğ•œ f x} âŠ†
    â‹‚ (e : â„•), â‹ƒ (n : â„•), â‹‚ (p â‰¥ n) (q â‰¥ n), B ğ•œ f ((1/2) ^ p) ((1/2) ^ q) ((1/2) ^ e) :=
begin
  assume x hx,
  rw mem_Inter,
  assume e,
  have : (0 : â„) < (1/2) ^ e, by { apply pow_pos, norm_num },
  rcases mem_A_of_differentiable this hx with âŸ¨R, R_pos, hRâŸ©,
  obtain âŸ¨n, hnâŸ© : âˆƒ (n : â„•), (1/2) ^ n < R :=
    exists_nat_pow_lt R_pos (by norm_num : (1 : â„)/2 < 1),
  apply mem_Union.2 âŸ¨n, _âŸ©,
  simp only [mem_Inter],
  assume p hp q hq,
  apply mem_Union.2 âŸ¨fderiv ğ•œ f x, _âŸ©,
  split;
  { refine hR _ âŸ¨pow_pos (by norm_num) _, lt_of_le_of_lt _ hnâŸ©,
    exact pow_le_pow_of_le_one (by norm_num) (by norm_num) (by assumption) }
end

/-- Hard inclusion: if a point belongs to an explicit set defined in terms of `B` with countable
operations, then the function `f` is differentiable there. -/
lemma B_subset_differentiable [complete_space F]:
  (â‹‚ (e : â„•), â‹ƒ (n : â„•), â‹‚ (p â‰¥ n) (q â‰¥ n), B ğ•œ f ((1/2) ^ p) ((1/2) ^ q) ((1/2) ^ e))
    âŠ† {x | differentiable_at ğ•œ f x} :=
begin
  have P : âˆ€ {n : â„•}, (0 : â„) < (1/2) ^ n := pow_pos (by norm_num),
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  have cpos : 0 < âˆ¥câˆ¥ := lt_trans zero_lt_one hc,
  assume x hx,
  have : âˆ€ (e : â„•), âˆƒ (n : â„•), âˆ€ p q, n â‰¤ p â†’ n â‰¤ q â†’ âˆƒ (L : E â†’L[ğ•œ] F),
    x âˆˆ A f L ((1/2) ^ p) ((1/2) ^ e) âˆ© A f L ((1/2) ^ q) ((1/2) ^ e),
  { assume e,
    have := mem_Inter.1 hx e,
    rcases mem_Union.1 this with âŸ¨n, hnâŸ©,
    refine âŸ¨n, Î» p q hp hq, _âŸ©,
    simp only [mem_Inter, ge_iff_le] at hn,
    exact mem_Union.1 (hn p hp q hq), },
  /- Recast the assumptions: for each `e`, there exist `n e` and linear maps `L e p q`
  such that, for `p, q â‰¥ n e`, then `f` is well approximated by `L e p q` at scale `2 ^ (-p)` and
  `2 ^ (-q)`, with an error `2 ^ (-e)`. -/
  choose! n L hn using this,
  /- We will show that all the `L e p q` are close to each other when `e` is large enough. For
  definiteness, use `L0 e = L e (n e) (n e)`, to have a single sequence. We claim that this is
  a Cauchy sequence. -/
  let L0 : â„• â†’ (E â†’L[ğ•œ] F) := Î» e, L e (n e) (n e),
  have : cauchy_seq L0,
  { rw cauchy_seq_iff',
    assume Îµ Îµpos,
    obtain âŸ¨e, heâŸ© : âˆƒ (e : â„•), (1/2) ^ e < Îµ / (12 * âˆ¥câˆ¥):=
      exists_nat_pow_lt (div_pos Îµpos (mul_pos (by norm_num) cpos)) (by norm_num),
    use e,
    set Î´ : â„ := (1/2) ^ e with hÎ´,
    assume e' he',
    rw [ge_iff_le] at he',
    set Î´' : â„ := (1/2) ^ e' with hÎ´',
    have Î´'le : Î´' â‰¤ Î´ := pow_le_pow_of_le_one (by norm_num) (by norm_num) he',
    let p := max (n e) (n e'),
    /- To show that `L0 e` and `L0 e'` are close, argue that `L0 e` is close to `L e (n e) p`
    (where `p` is large enough), as both approach `f` at scale `2 ^(- n e)`. And `L e (n e) p`
    is close to `L e' (n e') p` as both approach `f` at scale `2 ^ (-p)`. And `L e' (n e') p` is
    close to `L0 e'` as both approach `f` at scale `2 ^ (- n e')`. -/
    have J1 : âˆ¥L0 e - L e (n e) pâˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * Î´,
    { have I1 : x âˆˆ A f (L0 e) ((1 / 2) ^ (n e)) Î´ :=
        (hn e (n e) (n e) (le_refl _) (le_refl _)).1,
      have I2 : x âˆˆ A f (L e (n e) p) ((1 / 2) ^ (n e)) Î´ :=
        (hn e (n e) p (le_refl _) (le_max_left _ _)).1,
      exact norm_sub_le_of_mem_A hc P P I1 I2 },
    have J2 : âˆ¥L e' (n e') p - L0 e'âˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * Î´,
    { have I1 : x âˆˆ A f (L0 e') ((1 / 2) ^ (n e')) Î´' :=
        (hn e' (n e') (n e') (le_refl _) (le_refl _)).1,
      have I2 : x âˆˆ A f (L e' (n e') p) ((1 / 2) ^ (n e')) Î´' :=
        (hn e' (n e') p (le_refl _) (le_max_right _ _)).1,
      exact norm_sub_le_of_mem_A hc P P
        (A_mono _ _ _ Î´'le I2) (A_mono _ _ _ Î´'le I1) },
    have J3 : âˆ¥L e (n e) p - L e' (n e') pâˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * Î´,
    { have I1 : x âˆˆ A f (L e (n e) p) ((1 / 2) ^ p) Î´ :=
        (hn e (n e) p (le_refl _) (le_max_left _ _)).2,
      have I2 : x âˆˆ A f (L e' (n e') p) ((1 / 2) ^ p) Î´' :=
        (hn e' (n e') p (le_refl _) (le_max_right _ _)).2,
      exact norm_sub_le_of_mem_A hc P P I1 (A_mono _ _ _ Î´'le I2) },
    rw [dist_comm, dist_eq_norm],
    calc
      âˆ¥L0 e - L0 e'âˆ¥
          = âˆ¥(L0 e - L e (n e) p) + (L e (n e) p - L e' (n e') p) + (L e' (n e') p - L0 e')âˆ¥ :
        by { congr' 1, abel }
      ... â‰¤ âˆ¥L0 e - L e (n e) pâˆ¥ + âˆ¥L e (n e) p - L e' (n e') pâˆ¥ + âˆ¥L e' (n e') p - L0 e'âˆ¥ :
        le_trans (norm_add_le _ _) (add_le_add_right (norm_add_le _ _) _)
      ... â‰¤ 4 * âˆ¥câˆ¥ * Î´ + 4 * âˆ¥câˆ¥ * Î´ + 4 * âˆ¥câˆ¥ * Î´ :
        by apply_rules [add_le_add]
      ... = 12 * âˆ¥câˆ¥ * Î´ : by ring
      ... < 12 * âˆ¥câˆ¥ * (Îµ / (12 * âˆ¥câˆ¥)) :
        mul_lt_mul' (le_refl _) he (le_of_lt P) (mul_pos (by norm_num) cpos)
      ... = Îµ :
        by { field_simp [(by norm_num : (12 : â„) â‰  0), ne_of_gt cpos], ring } },
  /- As it is Cauchy, the sequence `L0` converges, to a limit `f'`.-/
  obtain âŸ¨f', hf'âŸ© : âˆƒ f' : E â†’L[ğ•œ] F, tendsto L0 at_top (ğ“ f') :=
    cauchy_seq_tendsto_of_complete this,
  /- We will show that `f` has derivative `f'` at `x`. -/
  have : has_fderiv_at f f' x,
  { simp only [has_fderiv_at_iff_is_o_nhds_zero, is_o_iff],
    /- to get an approximation with a precision `Îµ`, we will use `L0 e` for large enough (but fixed)
    `e`, and then argue that it works as an approximation at any scale `2 ^ (-k)` as it is close to
    `L e (n e) k` which, by definition, is a good approximation at scale `k`. Both linear maps are
    close as they are close on a shell of size `2 ^ (-n e)`, by definition.
    -/
    assume Îµ Îµpos,
    have pos : 0 < 8 + 8 * âˆ¥câˆ¥ :=
      add_pos_of_pos_of_nonneg (by norm_num) (mul_nonneg (by norm_num) (norm_nonneg _)),
    obtain âŸ¨e, heâŸ© : âˆƒ (e : â„•), âˆ¥L0 e - f'âˆ¥ < Îµ / 2 âˆ§ (1 / 2) ^ e < Îµ / (8 + 8 * âˆ¥câˆ¥) :=
    begin
      have Eâ‚ := (tendsto_order.1 (tendsto_iff_norm_tendsto_zero.1 hf')).2 (Îµ/2) (half_pos Îµpos),
      have : tendsto (Î» (n : â„•), ((1 : â„)/2)^n) at_top (ğ“ 0) :=
        tendsto_pow_at_top_nhds_0_of_lt_1 (by norm_num) (by norm_num),
      have Eâ‚‚ := (tendsto_order.1 this).2 _ (div_pos Îµpos pos),
      exact (Eâ‚.and Eâ‚‚).exists
    end,
    rw eventually_nhds_iff_ball,
    refine âŸ¨(1/2) ^ (n e + 1), P, Î» y hy, _âŸ©,
    -- We need to show that `f (x + y) - f x - f' y` is small. For this, we will work at scale
    -- `k` where `k` is chosen with `âˆ¥yâˆ¥ âˆ¼ 2 ^ (-k)`.
    by_cases y_pos : y = 0, {simp [y_pos] },
    have yzero : 0 < âˆ¥yâˆ¥ := norm_pos_iff.mpr y_pos,
    have y_lt : âˆ¥yâˆ¥ < (1/2) ^ (n e + 1), by simpa using mem_ball_iff_norm.1 hy,
    have yone : âˆ¥yâˆ¥ â‰¤ 1 :=
      le_trans (y_lt.le) (pow_le_one _ (by norm_num) (by norm_num)),
    -- define the scale `k`.
    obtain âŸ¨k, hk, h'kâŸ© : âˆƒ (k : â„•), (1/2) ^ (k + 1) < âˆ¥yâˆ¥ âˆ§ âˆ¥yâˆ¥ â‰¤ (1/2) ^ k :=
      exists_nat_pow_near_of_lt_one yzero yone (by norm_num : (0 : â„) < 1/2)
      (by norm_num : (1 : â„)/2 < 1),
    -- the scale is large enough (as `y` is small enough)
    have k_gt : n e < k,
    { have : ((1:â„)/2) ^ (k + 1) < (1/2) ^ (n e + 1) := lt_trans hk y_lt,
      rw pow_lt_pow_iff_of_lt_one (by norm_num : (0 : â„) < 1/2) (by norm_num) at this,
      linarith },
    set m := k - 1 with hl,
    have m_ge : n e â‰¤ m := nat.le_pred_of_lt k_gt,
    have km : k = m + 1 := (nat.succ_pred_eq_of_pos (lt_of_le_of_lt (zero_le _) k_gt)).symm,
    rw km at hk h'k,
    -- `f` is well approximated by `L e (n e) k` at the relevant scale
    -- (in fact, we use `m = k - 1` instead of `k` because of the precise definition of `A`).
    have J1 : âˆ¥f (x + y) - f x - L e (n e) m ((x + y) - x)âˆ¥ â‰¤ (1/2) ^ e * (1/2) ^ m,
    { apply le_of_mem_A (hn e (n e) m (le_refl _) m_ge).2,
      { simp only [mem_closed_ball, dist_self],
        exact div_nonneg (le_of_lt P) (zero_le_two) },
      { simp [dist_eq_norm],
        convert h'k,
        field_simp,
        ring_exp } },
    have J2 : âˆ¥f (x + y) - f x - L e (n e) m yâˆ¥ â‰¤ 4 * (1/2) ^ e * âˆ¥yâˆ¥ := calc
      âˆ¥f (x + y) - f x - L e (n e) m yâˆ¥ â‰¤ (1/2) ^ e * (1/2) ^ m :
        by simpa only [add_sub_cancel'] using J1
      ... = 4 * (1/2) ^ e * (1/2) ^ (m + 2) : by { field_simp, ring_exp }
      ... â‰¤ 4 * (1/2) ^ e * âˆ¥yâˆ¥ :
        mul_le_mul_of_nonneg_left (le_of_lt hk) (mul_nonneg (by norm_num) (le_of_lt P)),
    -- The operator `L e (n e) m` is close to `L0`, as they are close on a shell of
    -- scale `2 ^ (- n e)`.
    have J3 : âˆ¥L e (n e) m - L0 eâˆ¥ â‰¤ 4 * âˆ¥câˆ¥ * (1/2)^e,
    { have I1 : x âˆˆ A f (L0 e) ((1 / 2) ^ (n e)) ((1/2)^e) :=
        (hn e (n e) (n e) (le_refl _) (le_refl _)).1,
      have I2 : x âˆˆ A f (L e (n e) m) ((1 / 2) ^ (n e)) ((1/2)^e) :=
        (hn e (n e) m (le_refl _) m_ge).1,
      exact norm_sub_le_of_mem_A hc P P I2 I1, },
    -- combine all the previous estimates to see that `f (x + y) - f x - f' y` is small.
    calc âˆ¥f (x + y) - f x - f' yâˆ¥
    = âˆ¥(f (x + y) - f x - L e (n e) m y) + (L e (n e) m - L0 e) y + (L0 e - f') yâˆ¥ :
      by { congr' 1, simp, abel }
    ... â‰¤ âˆ¥f (x + y) - f x - L e (n e) m yâˆ¥ + âˆ¥(L e (n e) m - L0 e) yâˆ¥ + âˆ¥(L0 e - f') yâˆ¥ :
      le_trans (norm_add_le _ _) (add_le_add_right (norm_add_le _ _) _)
    ... â‰¤ 4 * (1/2) ^ e * âˆ¥yâˆ¥ + 4 * âˆ¥câˆ¥ * (1/2) ^ e * âˆ¥yâˆ¥ + (Îµ / 2) * âˆ¥yâˆ¥ :
      begin
        apply add_le_add (add_le_add J2 _) _,
        { exact le_trans (le_op_norm _ _) (mul_le_mul_of_nonneg_right J3 (norm_nonneg _)) },
        { exact le_trans (le_op_norm _ _) (mul_le_mul_of_nonneg_right he.1.le (norm_nonneg _)) }
      end
    ... = (4 + 4 * âˆ¥câˆ¥) * âˆ¥yâˆ¥ * (1/2) ^ e + (Îµ / 2) * âˆ¥yâˆ¥ : by ring
    ... â‰¤ (4 + 4 * âˆ¥câˆ¥) * âˆ¥yâˆ¥ * (Îµ / (8 + 8 * âˆ¥câˆ¥)) + (Îµ / 2) * âˆ¥yâˆ¥ :
      begin
        apply add_le_add_right,
        apply mul_le_mul_of_nonneg_left (le_of_lt he.2),
        exact mul_nonneg (add_nonneg (by norm_num) (mul_nonneg (by norm_num) (norm_nonneg _)))
          (norm_nonneg _)
      end
    ... = Îµ * âˆ¥yâˆ¥ : by { field_simp [ne_of_gt pos], ring } },
  exact this.differentiable_at,
end

theorem differentiable_eq_B [complete_space F] :
  {x | differentiable_at ğ•œ f x} =
  â‹‚ (e : â„•), â‹ƒ (n : â„•), â‹‚ (p â‰¥ n) (q â‰¥ n), B ğ•œ f ((1/2) ^ p) ((1/2) ^ q) ((1/2) ^ e) :=
subset.antisymm (differentiable_subset_B _ _) (B_subset_differentiable _ _)

end fderiv_measurable_aux

open fderiv_measurable_aux

/-- The set of differentiability points of a function taking values in a complete space is
Borel-measurable. -/
theorem is_measurable_differentiable
  [complete_space F] [measurable_space E] [opens_measurable_space E] :
  is_measurable {x | differentiable_at ğ•œ f x} :=
begin
  rw differentiable_eq_B,
  refine is_measurable.Inter (Î» e, _),
  refine is_measurable.Union (Î» n, _),
  refine is_measurable.Inter (Î» p, _),
  refine is_measurable.Inter_Prop (Î» hp, _),
  refine is_measurable.Inter (Î» q, _),
  refine is_measurable.Inter_Prop (Î» hq, _),
  apply is_open.is_measurable,
  apply is_open_B
end
