/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.normed_space.multilinear

/-!
# Formal multilinear series

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define `formal_multilinear_series ğ•œ E F` to be a family of `n`-multilinear maps for
all `n`, designed to model the sequence of derivatives of a function. In other files we use this
notion to define `C^n` functions (called `cont_diff` in `mathlib`) and analytic functions.

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

## Tags

multilinear, formal series
-/

noncomputable theory

open set fin
open_locale topology

variables {ğ•œ ğ•œ' E F G : Type*}

section
variables [comm_ring ğ•œ]
  [add_comm_group E] [module ğ•œ E] [topological_space E] [topological_add_group E]
  [has_continuous_const_smul ğ•œ E]
  [add_comm_group F] [module ğ•œ F] [topological_space F] [topological_add_group F]
  [has_continuous_const_smul ğ•œ F]
  [add_comm_group G] [module ğ•œ G] [topological_space G] [topological_add_group G]
  [has_continuous_const_smul ğ•œ G]


/-- A formal multilinear series over a field `ğ•œ`, from `E` to `F`, is given by a family of
multilinear maps from `E^n` to `F` for all `n`. -/
@[derive add_comm_group, nolint unused_arguments]
def formal_multilinear_series (ğ•œ : Type*) (E : Type*) (F : Type*)
  [ring ğ•œ]
  [add_comm_group E] [module ğ•œ E] [topological_space E] [topological_add_group E]
    [has_continuous_const_smul ğ•œ E]
  [add_comm_group F] [module ğ•œ F] [topological_space F] [topological_add_group F]
    [has_continuous_const_smul ğ•œ F] :=
Î  (n : â„•), (E [Ã—n]â†’L[ğ•œ] F)

instance : inhabited (formal_multilinear_series ğ•œ E F) := âŸ¨0âŸ©

section module
/- `derive` is not able to find the module structure, probably because Lean is confused by the
dependent types. We register it explicitly. -/

instance : module ğ•œ (formal_multilinear_series ğ•œ E F) :=
begin
  letI : Î  n, module ğ•œ (continuous_multilinear_map ğ•œ (Î» (i : fin n), E) F) :=
    Î» n, by apply_instance,
  refine pi.module _ _ _,
end

end module

namespace formal_multilinear_series

protected lemma ext_iff {p q : formal_multilinear_series ğ•œ E F} : p = q â†” âˆ€ n, p n = q n :=
function.funext_iff

protected lemma ne_iff {p q : formal_multilinear_series ğ•œ E F} : p â‰  q â†” âˆƒ n, p n â‰  q n :=
function.ne_iff

/-- Killing the zeroth coefficient in a formal multilinear series -/
def remove_zero (p : formal_multilinear_series ğ•œ E F) : formal_multilinear_series ğ•œ E F
| 0       := 0
| (n + 1) := p (n + 1)

@[simp] lemma remove_zero_coeff_zero (p : formal_multilinear_series ğ•œ E F) :
  p.remove_zero 0 = 0 := rfl

@[simp] lemma remove_zero_coeff_succ (p : formal_multilinear_series ğ•œ E F) (n : â„•) :
  p.remove_zero (n+1) = p (n+1) := rfl

lemma remove_zero_of_pos (p : formal_multilinear_series ğ•œ E F) {n : â„•} (h : 0 < n) :
  p.remove_zero n = p n :=
by { rw â† nat.succ_pred_eq_of_pos h, refl }

/-- Convenience congruence lemma stating in a dependent setting that, if the arguments to a formal
multilinear series are equal, then the values are also equal. -/
lemma congr (p : formal_multilinear_series ğ•œ E F) {m n : â„•} {v : fin m â†’ E} {w : fin n â†’ E}
  (h1 : m = n) (h2 : âˆ€ (i : â„•) (him : i < m) (hin : i < n), v âŸ¨i, himâŸ© = w âŸ¨i, hinâŸ©) :
  p m v = p n w :=
by { cases h1, congr' with âŸ¨i, hiâŸ©, exact h2 i hi hi }

/-- Composing each term `pâ‚™` in a formal multilinear series with `(u, ..., u)` where `u` is a fixed
continuous linear map, gives a new formal multilinear series `p.comp_continuous_linear_map u`. -/
def comp_continuous_linear_map (p : formal_multilinear_series ğ•œ F G) (u : E â†’L[ğ•œ] F) :
  formal_multilinear_series ğ•œ E G :=
Î» n, (p n).comp_continuous_linear_map (Î» (i : fin n), u)

@[simp] lemma comp_continuous_linear_map_apply
  (p : formal_multilinear_series ğ•œ F G) (u : E â†’L[ğ•œ] F) (n : â„•) (v : fin n â†’ E) :
  (p.comp_continuous_linear_map u) n v = p n (u âˆ˜ v) := rfl

variables (ğ•œ) [comm_ring ğ•œ'] [has_smul ğ•œ ğ•œ']
variables [module ğ•œ' E] [has_continuous_const_smul ğ•œ' E] [is_scalar_tower ğ•œ ğ•œ' E]
variables [module ğ•œ' F] [has_continuous_const_smul ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F]

/-- Reinterpret a formal `ğ•œ'`-multilinear series as a formal `ğ•œ`-multilinear series. -/
@[simp] protected def restrict_scalars (p : formal_multilinear_series ğ•œ' E F) :
  formal_multilinear_series ğ•œ E F :=
Î» n, (p n).restrict_scalars ğ•œ

end formal_multilinear_series

end

namespace formal_multilinear_series

variables [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group E] [normed_space ğ•œ E]
  [normed_add_comm_group F] [normed_space ğ•œ F]
  [normed_add_comm_group G] [normed_space ğ•œ G]

variables (p : formal_multilinear_series ğ•œ E F)

/-- Forgetting the zeroth term in a formal multilinear series, and interpreting the following terms
as multilinear maps into `E â†’L[ğ•œ] F`. If `p` corresponds to the Taylor series of a function, then
`p.shift` is the Taylor series of the derivative of the function. -/
def shift : formal_multilinear_series ğ•œ E (E â†’L[ğ•œ] F) :=
Î»n, (p n.succ).curry_right

/-- Adding a zeroth term to a formal multilinear series taking values in `E â†’L[ğ•œ] F`. This
corresponds to starting from a Taylor series for the derivative of a function, and building a Taylor
series for the function itself. -/
def unshift (q : formal_multilinear_series ğ•œ E (E â†’L[ğ•œ] F)) (z : F) :
  formal_multilinear_series ğ•œ E F
| 0       := (continuous_multilinear_curry_fin0 ğ•œ E F).symm z
| (n + 1) := continuous_multilinear_curry_right_equiv' ğ•œ n E F (q n)

end formal_multilinear_series

namespace continuous_linear_map
variables [comm_ring ğ•œ]
  [add_comm_group E] [module ğ•œ E] [topological_space E] [topological_add_group E]
  [has_continuous_const_smul ğ•œ E]
  [add_comm_group F] [module ğ•œ F] [topological_space F] [topological_add_group F]
  [has_continuous_const_smul ğ•œ F]
  [add_comm_group G] [module ğ•œ G] [topological_space G] [topological_add_group G]
  [has_continuous_const_smul ğ•œ G]

/-- Composing each term `pâ‚™` in a formal multilinear series with a continuous linear map `f` on the
left gives a new formal multilinear series `f.comp_formal_multilinear_series p` whose general term
is `f âˆ˜ pâ‚™`. -/
def comp_formal_multilinear_series (f : F â†’L[ğ•œ] G) (p : formal_multilinear_series ğ•œ E F) :
  formal_multilinear_series ğ•œ E G :=
Î» n, f.comp_continuous_multilinear_map (p n)

@[simp] lemma comp_formal_multilinear_series_apply
  (f : F â†’L[ğ•œ] G) (p : formal_multilinear_series ğ•œ E F) (n : â„•) :
  (f.comp_formal_multilinear_series p) n = f.comp_continuous_multilinear_map (p n) :=
rfl

lemma comp_formal_multilinear_series_apply'
  (f : F â†’L[ğ•œ] G) (p : formal_multilinear_series ğ•œ E F) (n : â„•) (v : fin n â†’ E) :
  (f.comp_formal_multilinear_series p) n v = f (p n v) :=
rfl

end continuous_linear_map

namespace formal_multilinear_series

section order

variables [comm_ring ğ•œ] {n : â„•}
  [add_comm_group E] [module ğ•œ E] [topological_space E] [topological_add_group E]
  [has_continuous_const_smul ğ•œ E]
  [add_comm_group F] [module ğ•œ F] [topological_space F] [topological_add_group F]
  [has_continuous_const_smul ğ•œ F]
  {p : formal_multilinear_series ğ•œ E F}

/-- The index of the first non-zero coefficient in `p` (or `0` if all coefficients are zero). This
  is the order of the isolated zero of an analytic function `f` at a point if `p` is the Taylor
  series of `f` at that point. -/
noncomputable def order (p : formal_multilinear_series ğ•œ E F) : â„• :=
Inf { n | p n â‰  0 }

@[simp] lemma order_zero : (0 : formal_multilinear_series ğ•œ E F).order = 0 := by simp [order]

lemma ne_zero_of_order_ne_zero (hp : p.order â‰  0) : p â‰  0 :=
Î» h, by simpa [h] using hp

lemma order_eq_find [decidable_pred (Î» n, p n â‰  0)] (hp : âˆƒ n, p n â‰  0) :
  p.order = nat.find hp :=
by simp [order, Inf, hp]

lemma order_eq_find' [decidable_pred (Î» n, p n â‰  0)] (hp : p â‰  0) :
  p.order = nat.find (formal_multilinear_series.ne_iff.mp hp) :=
order_eq_find _

lemma order_eq_zero_iff (hp : p â‰  0) : p.order = 0 â†” p 0 â‰  0 :=
begin
  classical,
  have : âˆƒ n, p n â‰  0 := formal_multilinear_series.ne_iff.mp hp,
  simp [order_eq_find this, hp]
end

lemma order_eq_zero_iff' : p.order = 0 â†” p = 0 âˆ¨ p 0 â‰  0 :=
by { by_cases h : p = 0; simp [h, order_eq_zero_iff] }

lemma apply_order_ne_zero (hp : p â‰  0) : p p.order â‰  0 :=
begin
  classical,
  let h := formal_multilinear_series.ne_iff.mp hp,
  exact (order_eq_find h).symm â–¸ nat.find_spec h
end

lemma apply_order_ne_zero' (hp : p.order â‰  0) : p p.order â‰  0 :=
apply_order_ne_zero (ne_zero_of_order_ne_zero hp)

lemma apply_eq_zero_of_lt_order (hp : n < p.order) : p n = 0 :=
begin
  by_cases p = 0,
  { simp [h] },
  { classical,
    rw [order_eq_find' h] at hp,
    simpa using nat.find_min _ hp }
end

end order

section coef

variables [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group E] [normed_space ğ•œ E] {s : E}
  {p : formal_multilinear_series ğ•œ ğ•œ E} {f : ğ•œ â†’ E}
  {n : â„•} {z zâ‚€ : ğ•œ} {y : fin n â†’ ğ•œ}

open_locale big_operators

/-- The `n`th coefficient of `p` when seen as a power series. -/
def coeff (p : formal_multilinear_series ğ•œ ğ•œ E) (n : â„•) : E := p n 1

lemma mk_pi_field_coeff_eq (p : formal_multilinear_series ğ•œ ğ•œ E) (n : â„•) :
  continuous_multilinear_map.mk_pi_field ğ•œ (fin n) (p.coeff n) = p n :=
(p n).mk_pi_field_apply_one_eq_self

@[simp] lemma apply_eq_prod_smul_coeff : p n y = (âˆ i, y i) â€¢ p.coeff n :=
begin
  convert (p n).to_multilinear_map.map_smul_univ y 1,
  funext; simp only [pi.one_apply, algebra.id.smul_eq_mul, mul_one],
end

lemma coeff_eq_zero : p.coeff n = 0 â†” p n = 0 :=
by rw [â† mk_pi_field_coeff_eq p, continuous_multilinear_map.mk_pi_field_eq_zero_iff]

@[simp] lemma apply_eq_pow_smul_coeff : p n (Î» _, z) = z ^ n â€¢ p.coeff n :=
by simp

@[simp] lemma norm_apply_eq_norm_coef : â€–p nâ€– = â€–coeff p nâ€– :=
by rw [â† mk_pi_field_coeff_eq p, continuous_multilinear_map.norm_mk_pi_field]

end coef

section fslope

variables [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group E] [normed_space ğ•œ E]
  {p : formal_multilinear_series ğ•œ ğ•œ E} {n : â„•}

/-- The formal counterpart of `dslope`, corresponding to the expansion of `(f z - f 0) / z`. If `f`
has `p` as a power series, then `dslope f` has `fslope p` as a power series. -/
noncomputable def fslope (p : formal_multilinear_series ğ•œ ğ•œ E) : formal_multilinear_series ğ•œ ğ•œ E :=
  Î» n, (p (n + 1)).curry_left 1

@[simp] lemma coeff_fslope : p.fslope.coeff n = p.coeff (n + 1) :=
begin
  have : @fin.cons n (Î» _, ğ•œ) 1 (1 : fin n â†’ ğ•œ) = 1 := fin.cons_self_tail 1,
  simp only [fslope, coeff, continuous_multilinear_map.curry_left_apply, this],
end

@[simp] lemma coeff_iterate_fslope (k n : â„•) :
  (fslope^[k] p).coeff n = p.coeff (n + k) :=
by induction k with k ih generalizing p; refl <|> simpa [ih]

end fslope

end formal_multilinear_series

section const

/-- The formal multilinear series where all terms of positive degree are equal to zero, and the term
of degree zero is `c`. It is the power series expansion of the constant function equal to `c`
everywhere. -/
def const_formal_multilinear_series (ğ•œ : Type*) [nontrivially_normed_field ğ•œ]
  (E : Type*) [normed_add_comm_group E] [normed_space ğ•œ E] [has_continuous_const_smul ğ•œ E]
  [topological_add_group E] {F : Type*} [normed_add_comm_group F] [topological_add_group F]
  [normed_space ğ•œ F]  [has_continuous_const_smul ğ•œ F] (c : F) : formal_multilinear_series ğ•œ E F
| 0 := continuous_multilinear_map.curry0 _ _ c
| _ := 0

@[simp] lemma const_formal_multilinear_series_apply [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group E] [normed_add_comm_group F] [normed_space ğ•œ E] [normed_space ğ•œ F]
  {c : F} {n : â„•} (hn : n â‰  0) :
  const_formal_multilinear_series ğ•œ E c n = 0 :=
nat.cases_on n (Î» hn, (hn rfl).elim) (Î» _ _, rfl) hn

end const
