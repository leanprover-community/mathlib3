/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.calculus.inverse
import analysis.normed_space.complemented

/-!
# Implicit function theorem

We prove three versions of the implicit function theorem. First we define a structure
`implicit_function_data` that holds arguments for the most general version of the implicit function
theorem, see `implicit_function_data.implicit_function`
and `implicit_function_data.to_implicit_function`. This version allows a user to choose
a specific implicit function but provides only a little convenience over the inverse function
theorem.

Then we define `implicit_function_of_complemented`: implicit function defined by `f (g z y) = z`,
where `f : E â†’ F` is a function strictly differentiable at `a` such that its derivative `f'`
is surjective and has a `complemented` kernel.

Finally, if the codomain of `f` is a finite dimensional space, then we can automatically prove
that the kernel of `f'` is complemented, hence the only assumptions are `has_strict_fderiv_at`
and `f'.range = âŠ¤`. This version is named `implicit_function`.

## TODO

* Add a version for a function `f : E Ã— F â†’ G` such that $$\frac{\partial f}{\partial y}$$ is
  invertible.
* Add a version for `f : ğ•œ Ã— ğ•œ â†’ ğ•œ` proving `has_strict_deriv_at` and `deriv Ï† = ...`.
* Prove that in a real vector space the implicit function has the same smoothness as the original
  one.
* If the original function is differentiable in a neighborhood, then the implicit function is
  differentiable in a neighborhood as well. Current setup only proves differentiability at one
  point for the implicit function constructed in this file (as opposed to an unspecified implicit
  function). One of the ways to overcome this difficulty is to use uniqueness of the implicit
  function in the general version of the theorem. Another way is to prove that *any* implicit
  function satisfying some predicate is strictly differentiable.

## Tags

implicit function, inverse function
-/

noncomputable theory

open_locale topological_space
open filter
open continuous_linear_map (fst snd subtype_val smul_right ker_prod)
open continuous_linear_equiv (of_bijective)

/-!
### General version

Consider two functions `f : E â†’ F` and `g : E â†’ G` and a point `a` such that

* both functions are strictly differentiable at `a`;
* the derivatives are surjective;
* the kernels of the derivatives are complementary subspaces of `E`.

Note that the map `x â†¦ (f x, g x)` has a bijective derivative, hence it is a local homeomorphism
between `E` and `F Ã— G`. We use this fact to define a function `Ï† : F â†’ G â†’ E`
(see `implicit_function_data.implicit_function`) such that for `(y, z)` close enough to `(f a, g a)`
we have `f (Ï† y z) = y` and `g (Ï† y z) = z`.

We also prove a formula for $$\frac{\partial\varphi}{\partial z}.$$

Though this statement is almost symmetric with respect to `F`, `G`, we interpret it in the following
way. Consider a family of surfaces `{x | f x = y}`, `y âˆˆ ğ“ (f a)`. Each of these surfaces is
parametrized by `Ï† y`.

There are many ways to choose a (differentiable) function `Ï†` such that `f (Ï† y z) = y` but the
extra condition `g (Ï† y z) = z` allows a user to select one of these functions. If we imagine
that the level surfaces `f = const` form a local horizontal foliation, then the choice of
`g` fixes a transverse foliation `g = const`, and `Ï†` is the inverse function of the projection
of `{x | f x = y}` along this transverse foliation.

This version of the theorem is used to prove the other versions and can be used if a user
needs to have a complete control over the choice of the implicit function.
-/

/-- Data for the general version of the implicit function theorem. It holds two functions
`f : E â†’ F` and `g : E â†’ G` (named `left_fun` and `right_fun`) and a point `a` (named `pt`)
such that

* both functions are strictly differentiable at `a`;
* the derivatives are surjective;
* the kernels of the derivatives are complementary subspaces of `E`. -/
@[nolint has_inhabited_instance]
structure implicit_function_data (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  (E : Type*) [normed_group E] [normed_space ğ•œ E] [complete_space E]
  (F : Type*) [normed_group F] [normed_space ğ•œ F] [complete_space F]
  (G : Type*) [normed_group G] [normed_space ğ•œ G] [complete_space G] :=
(left_fun : E â†’ F)
(left_deriv : E â†’L[ğ•œ] F)
(right_fun : E â†’ G)
(right_deriv : E â†’L[ğ•œ] G)
(pt : E)
(left_has_deriv : has_strict_fderiv_at left_fun left_deriv pt)
(right_has_deriv : has_strict_fderiv_at right_fun right_deriv pt)
(left_range : left_deriv.range = âŠ¤)
(right_range : right_deriv.range = âŠ¤)
(is_compl_ker : is_compl left_deriv.ker right_deriv.ker)

namespace implicit_function_data

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] [complete_space E]
  {F : Type*} [normed_group F] [normed_space ğ•œ F] [complete_space F]
  {G : Type*} [normed_group G] [normed_space ğ•œ G] [complete_space G]
  (Ï† : implicit_function_data ğ•œ E F G)

/-- The function given by `x â†¦ (left_fun x, right_fun x)`. -/
def prod_fun (x : E) : F Ã— G := (Ï†.left_fun x, Ï†.right_fun x)

@[simp] lemma prod_fun_apply (x : E) : Ï†.prod_fun x = (Ï†.left_fun x, Ï†.right_fun x) := rfl

protected lemma has_strict_fderiv_at :
  has_strict_fderiv_at Ï†.prod_fun
    (Ï†.left_deriv.equiv_prod_of_surjective_of_is_compl Ï†.right_deriv Ï†.left_range Ï†.right_range
       Ï†.is_compl_ker : E â†’L[ğ•œ] F Ã— G) Ï†.pt :=
Ï†.left_has_deriv.prod Ï†.right_has_deriv

/-- Implicit function theorem. If `f : E â†’ F` and `g : E â†’ G` are two maps strictly differentiable
at `a`, their derivatives `f'`, `g'` are surjective, and the kernels of these derivatives are
complementary subspaces of `E`, then `x â†¦ (f x, g x)` defines a local homeomorphism between
`E` and `F Ã— G`. In particular, `{x | f x = f a}` is locally homeomorphic to `G`. -/
def to_local_homeomorph : local_homeomorph E (F Ã— G) :=
Ï†.has_strict_fderiv_at.to_local_homeomorph _

/-- Implicit function theorem. If `f : E â†’ F` and `g : E â†’ G` are two maps strictly differentiable
at `a`, their derivatives `f'`, `g'` are surjective, and the kernels of these derivatives are
complementary subspaces of `E`, then `implicit_function_of_is_compl_ker` is the unique (germ of a)
map `Ï† : F â†’ G â†’ E` such that `f (Ï† y z) = y` and `g (Ï† y z) = z`. -/
def implicit_function : F â†’ G â†’ E := function.curry $ Ï†.to_local_homeomorph.symm

@[simp] lemma to_local_homeomorph_coe : â‡‘(Ï†.to_local_homeomorph) = Ï†.prod_fun := rfl

lemma to_local_homeomorph_apply (x : E) :
  Ï†.to_local_homeomorph x = (Ï†.left_fun x, Ï†.right_fun x) :=
rfl

lemma pt_mem_to_local_homeomorph_source :
  Ï†.pt âˆˆ Ï†.to_local_homeomorph.source :=
Ï†.has_strict_fderiv_at.mem_to_local_homeomorph_source

lemma map_pt_mem_to_local_homeomorph_target :
  (Ï†.left_fun Ï†.pt, Ï†.right_fun Ï†.pt) âˆˆ Ï†.to_local_homeomorph.target :=
Ï†.to_local_homeomorph.map_source $ Ï†.pt_mem_to_local_homeomorph_source

lemma prod_map_implicit_function :
  âˆ€á¶  (p : F Ã— G) in ğ“ (Ï†.prod_fun Ï†.pt), Ï†.prod_fun (Ï†.implicit_function p.1 p.2) = p :=
Ï†.has_strict_fderiv_at.eventually_right_inverse.mono $ Î» âŸ¨z, yâŸ© h, h

lemma left_map_implicit_function :
  âˆ€á¶  (p : F Ã— G) in ğ“ (Ï†.prod_fun Ï†.pt), Ï†.left_fun (Ï†.implicit_function p.1 p.2) = p.1 :=
Ï†.prod_map_implicit_function.mono $ Î» z, congr_arg prod.fst

lemma right_map_implicit_function :
  âˆ€á¶  (p : F Ã— G) in ğ“ (Ï†.prod_fun Ï†.pt), Ï†.right_fun (Ï†.implicit_function p.1 p.2) = p.2 :=
Ï†.prod_map_implicit_function.mono $ Î» z, congr_arg prod.snd

lemma implicit_function_apply_image :
  âˆ€á¶  x in ğ“ Ï†.pt, Ï†.implicit_function (Ï†.left_fun x) (Ï†.right_fun x) = x :=
Ï†.has_strict_fderiv_at.eventually_left_inverse

lemma map_nhds_eq : map Ï†.left_fun (ğ“ Ï†.pt) = ğ“ (Ï†.left_fun Ï†.pt) :=
show map (prod.fst âˆ˜ Ï†.prod_fun) (ğ“ Ï†.pt) = ğ“ (Ï†.prod_fun Ï†.pt).1,
by rw [â† map_map, Ï†.has_strict_fderiv_at.map_nhds_eq_of_equiv, map_fst_nhds]

lemma implicit_function_has_strict_fderiv_at
  (g'inv : G â†’L[ğ•œ] E) (hg'inv : Ï†.right_deriv.comp g'inv = continuous_linear_map.id ğ•œ G)
  (hg'invf : Ï†.left_deriv.comp g'inv = 0) :
  has_strict_fderiv_at (Ï†.implicit_function (Ï†.left_fun Ï†.pt)) g'inv (Ï†.right_fun Ï†.pt) :=
begin
  have := Ï†.has_strict_fderiv_at.to_local_inverse,
  simp only [prod_fun] at this,
  convert this.comp (Ï†.right_fun Ï†.pt)
    ((has_strict_fderiv_at_const _ _).prod (has_strict_fderiv_at_id _)),
  simp only [continuous_linear_map.ext_iff, continuous_linear_map.coe_comp', function.comp_app]
    at hg'inv hg'invf âŠ¢,
  simp [continuous_linear_equiv.eq_symm_apply, *]
end

end implicit_function_data

namespace has_strict_fderiv_at

section complemented

/-!
### Case of a complemented kernel

In this section we prove the following version of the implicit function theorem. Consider a map
`f : E â†’ F` and a point `a : E` such that `f` is strictly differentiable at `a`, its derivative `f'`
is surjective and the kernel of `f'` is a complemented subspace of `E` (i.e., it has a closed
complementary subspace). Then there exists a function `Ï† : F â†’ ker f' â†’ E` such that for `(y, z)`
close to `(f a, 0)` we have `f (Ï† y z) = y` and the derivative of `Ï† (f a)` at zero is the
embedding `ker f' â†’ E`.

Note that a map with these properties is not unique. E.g., different choices of a subspace
complementary to `ker f'` lead to different maps `Ï†`.
-/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] [complete_space E]
  {F : Type*} [normed_group F] [normed_space ğ•œ F] [complete_space F]
  {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {a : E}

section defs

variables (f f')

/-- Data used to apply the generic implicit function theorem to the case of a strictly
differentiable map such that its derivative is surjective and has a complemented kernel. -/
@[simp] def implicit_function_data_of_complemented (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  implicit_function_data ğ•œ E F f'.ker :=
{ left_fun := f,
  left_deriv := f',
  right_fun := Î» x, classical.some hker (x - a),
  right_deriv := classical.some hker,
  pt := a,
  left_has_deriv := hf,
  right_has_deriv := (classical.some hker).has_strict_fderiv_at.comp a
    ((has_strict_fderiv_at_id a).sub_const a),
  left_range := hf',
  right_range := linear_map.range_eq_of_proj (classical.some_spec hker),
  is_compl_ker := linear_map.is_compl_of_proj (classical.some_spec hker) }

/-- A local homeomorphism between `E` and `F Ã— f'.ker` sending level surfaces of `f`
to vertical subspaces. -/
def implicit_to_local_homeomorph_of_complemented (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  local_homeomorph E (F Ã— f'.ker) :=
(implicit_function_data_of_complemented f f' hf hf' hker).to_local_homeomorph

/-- Implicit function `g` defined by `f (g z y) = z`. -/
def implicit_function_of_complemented (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  F â†’ f'.ker â†’ E :=
(implicit_function_data_of_complemented f f' hf hf' hker).implicit_function

end defs

@[simp] lemma implicit_to_local_homeomorph_of_complemented_fst (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) (x : E) :
  (hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker x).fst = f x :=
rfl

lemma implicit_to_local_homeomorph_of_complemented_apply
  (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤)
  (hker : f'.ker.closed_complemented) (y : E) :
  hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker y =
    (f y, classical.some hker (y - a)) :=
rfl

@[simp] lemma implicit_to_local_homeomorph_of_complemented_apply_ker
  (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤)
  (hker : f'.ker.closed_complemented) (y : f'.ker) :
  hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker (y + a) = (f (y + a), y) :=
by simp only [implicit_to_local_homeomorph_of_complemented_apply, add_sub_cancel,
  classical.some_spec hker]

@[simp] lemma implicit_to_local_homeomorph_of_complemented_self
  (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker a = (f a, 0) :=
by simp [hf.implicit_to_local_homeomorph_of_complemented_apply]

lemma mem_implicit_to_local_homeomorph_of_complemented_source (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  a âˆˆ (hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker).source :=
mem_to_local_homeomorph_source _

lemma mem_implicit_to_local_homeomorph_of_complemented_target (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  (f a, (0 : f'.ker)) âˆˆ (hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker).target :=
by simpa only [implicit_to_local_homeomorph_of_complemented_self] using
  ((hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker).map_source $
    (hf.mem_implicit_to_local_homeomorph_of_complemented_source hf' hker))

/-- `implicit_function_of_complemented` sends `(z, y)` to a point in `f â»Â¹' z`. -/
lemma map_implicit_function_of_complemented_eq (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  âˆ€á¶  (p : F Ã— f'.ker) in ğ“ (f a, 0),
    f (hf.implicit_function_of_complemented f f' hf' hker p.1 p.2) = p.1 :=
((hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker).eventually_right_inverse $
  hf.mem_implicit_to_local_homeomorph_of_complemented_target hf' hker).mono $ Î» âŸ¨z, yâŸ© h,
    congr_arg prod.fst h

/-- Any point in some neighborhood of `a` can be represented as `implicit_function`
of some point. -/
lemma eq_implicit_function_of_complemented (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  âˆ€á¶  x in ğ“ a, hf.implicit_function_of_complemented f f' hf' hker (f x)
    (hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker x).snd = x :=
(implicit_function_data_of_complemented f f' hf hf' hker).implicit_function_apply_image

@[simp] lemma implicit_function_of_complemented_apply_image (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  hf.implicit_function_of_complemented f f' hf' hker (f a) 0 = a :=
begin
  convert (hf.implicit_to_local_homeomorph_of_complemented f f' hf' hker).left_inv
    (hf.mem_implicit_to_local_homeomorph_of_complemented_source hf' hker),
  exact congr_arg prod.snd (hf.implicit_to_local_homeomorph_of_complemented_self hf' hker).symm
end

lemma to_implicit_function_of_complemented (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (hker : f'.ker.closed_complemented) :
  has_strict_fderiv_at (hf.implicit_function_of_complemented f f' hf' hker (f a))
    (subtype_val f'.ker) 0 :=
by convert (implicit_function_data_of_complemented f f' hf hf'
  hker).implicit_function_has_strict_fderiv_at (subtype_val f'.ker) _ _;
    [skip, ext, ext]; simp [classical.some_spec hker]

end complemented

/-!
### Finite dimensional case

In this section we prove the following version of the implicit function theorem. Consider a map
`f : E â†’ F` from a Banach normed space to a finite dimensional space.
Take a point `a : E` such that `f` is strictly differentiable at `a` and its derivative `f'`
is surjective. Then there exists a function `Ï† : F â†’ ker f' â†’ E` such that for `(y, z)`
close to `(f a, 0)` we have `f (Ï† y z) = y` and the derivative of `Ï† (f a)` at zero is the
embedding `ker f' â†’ E`.

This version deduces that `ker f'` is a complemented subspace from the fact that `F` is a finite
dimensional space, then applies the previous version.

Note that a map with these properties is not unique. E.g., different choices of a subspace
complementary to `ker f'` lead to different maps `Ï†`.
-/

section finite_dimensional

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] [complete_space ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] [complete_space E]
  {F : Type*} [normed_group F] [normed_space ğ•œ F] [finite_dimensional ğ•œ F]
  (f : E â†’ F) (f' : E â†’L[ğ•œ] F) {a : E}

/-- Given a map `f : E â†’ F` to a finite dimensional space with a surjective derivative `f'`,
returns a local homeomorphism between `E` and `F Ã— ker f'`. -/
def implicit_to_local_homeomorph (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) :
  local_homeomorph E (F Ã— f'.ker) :=
by haveI := finite_dimensional.complete ğ•œ F; exact
hf.implicit_to_local_homeomorph_of_complemented f f' hf'
  f'.ker_closed_complemented_of_finite_dimensional_range

/-- Implicit function `g` defined by `f (g z y) = z`. -/
def implicit_function (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) :
  F â†’ f'.ker â†’ E :=
function.curry $ (hf.implicit_to_local_homeomorph f f' hf').symm

variables {f f'}

@[simp] lemma implicit_to_local_homeomorph_fst (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) (x : E) :
  (hf.implicit_to_local_homeomorph f f' hf' x).fst = f x :=
rfl

@[simp] lemma implicit_to_local_homeomorph_apply_ker
  (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) (y : f'.ker) :
  hf.implicit_to_local_homeomorph f f' hf' (y + a) = (f (y + a), y) :=
by apply implicit_to_local_homeomorph_of_complemented_apply_ker

@[simp] lemma implicit_to_local_homeomorph_self
  (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) :
  hf.implicit_to_local_homeomorph f f' hf' a = (f a, 0) :=
by apply implicit_to_local_homeomorph_of_complemented_self

lemma mem_implicit_to_local_homeomorph_source (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) :
  a âˆˆ (hf.implicit_to_local_homeomorph f f' hf').source :=
mem_to_local_homeomorph_source _

lemma mem_implicit_to_local_homeomorph_target (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) :
  (f a, (0 : f'.ker)) âˆˆ (hf.implicit_to_local_homeomorph f f' hf').target :=
by apply mem_implicit_to_local_homeomorph_of_complemented_target

lemma tendsto_implicit_function (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) {Î± : Type*} {l : filter Î±} {gâ‚ : Î± â†’ F} {gâ‚‚ : Î± â†’ f'.ker}
  (hâ‚ : tendsto gâ‚ l (ğ“ $ f a)) (hâ‚‚ : tendsto gâ‚‚ l (ğ“ 0)) :
  tendsto (Î» t, hf.implicit_function f f' hf' (gâ‚ t) (gâ‚‚ t)) l (ğ“ a) :=
begin
  refine ((hf.implicit_to_local_homeomorph f f' hf').tendsto_symm
    (hf.mem_implicit_to_local_homeomorph_source hf')).comp _,
  rw [implicit_to_local_homeomorph_self],
  exact hâ‚.prod_mk_nhds hâ‚‚
end

alias tendsto_implicit_function â† filter.tendsto.implicit_function

/-- `implicit_function` sends `(z, y)` to a point in `f â»Â¹' z`. -/
lemma map_implicit_function_eq (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) :
  âˆ€á¶  (p : F Ã— f'.ker) in ğ“ (f a, 0), f (hf.implicit_function f f' hf' p.1 p.2) = p.1 :=
by apply map_implicit_function_of_complemented_eq

@[simp] lemma implicit_function_apply_image (hf : has_strict_fderiv_at f f' a)
  (hf' : f'.range = âŠ¤) :
  hf.implicit_function f f' hf' (f a) 0 = a :=
by apply implicit_function_of_complemented_apply_image

/-- Any point in some neighborhood of `a` can be represented as `implicit_function`
of some point. -/
lemma eq_implicit_function (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) :
  âˆ€á¶  x in ğ“ a, hf.implicit_function f f' hf' (f x)
    (hf.implicit_to_local_homeomorph f f' hf' x).snd = x :=
by apply eq_implicit_function_of_complemented

lemma to_implicit_function (hf : has_strict_fderiv_at f f' a) (hf' : f'.range = âŠ¤) :
  has_strict_fderiv_at (hf.implicit_function f f' hf' (f a))
    (subtype_val f'.ker) 0 :=
by apply to_implicit_function_of_complemented

end finite_dimensional

end has_strict_fderiv_at
