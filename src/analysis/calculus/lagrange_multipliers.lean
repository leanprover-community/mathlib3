/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.calculus.inverse
import linear_algebra.dual

/-!
# Lagrange multipliers

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we formalize the
[Lagrange multipliers](https://en.wikipedia.org/wiki/Lagrange_multiplier) method of solving
conditional extremum problems: if a function `Ï†` has a local extremum at `xâ‚€` on the set
`f â»Â¹' {f xâ‚€}`, `f x = (fâ‚€ x, ..., fâ‚™â‚‹â‚ x)`, then the differentials of `fâ‚–` and `Ï†` are linearly
dependent. First we formulate a geometric version of this theorem which does not rely on the
target space being `â„â¿`, then restate it in terms of coordinates.

## TODO

Formalize Karush-Kuhn-Tucker theorem

## Tags

lagrange multiplier, local extremum

-/

open filter set
open_locale topology filter big_operators
variables {E F : Type*} [normed_add_comm_group E] [normed_space â„ E] [complete_space E]
  [normed_add_comm_group F] [normed_space â„ F] [complete_space F]
  {f : E â†’ F} {Ï† : E â†’ â„} {xâ‚€ : E} {f' : E â†’L[â„] F} {Ï†' : E â†’L[â„] â„}

/-- Lagrange multipliers theorem: if `Ï† : E â†’ â„` has a local extremum on the set `{x | f x = f xâ‚€}`
at `xâ‚€`, both `f : E â†’ F` and `Ï†` are strictly differentiable at `xâ‚€`, and the codomain of `f` is
a complete space, then the linear map `x â†¦ (f' x, Ï†' x)` is not surjective. -/
lemma is_local_extr_on.range_ne_top_of_has_strict_fderiv_at
  (hextr : is_local_extr_on Ï† {x | f x = f xâ‚€} xâ‚€) (hf' : has_strict_fderiv_at f f' xâ‚€)
  (hÏ†' : has_strict_fderiv_at Ï† Ï†' xâ‚€) :
  linear_map.range (f'.prod Ï†') â‰  âŠ¤ :=
begin
  intro htop,
  set fÏ† := Î» x, (f x, Ï† x),
  have A : map Ï† (ğ“[f â»Â¹' {f xâ‚€}] xâ‚€) = ğ“ (Ï† xâ‚€),
  { change map (prod.snd âˆ˜ fÏ†) (ğ“[fÏ† â»Â¹' {p | p.1 = f xâ‚€}] xâ‚€) = ğ“ (Ï† xâ‚€),
    rw [â† map_map, nhds_within, map_inf_principal_preimage,
      (hf'.prod hÏ†').map_nhds_eq_of_surj htop],
    exact map_snd_nhds_within _ },
  exact hextr.not_nhds_le_map A.ge
end

/-- Lagrange multipliers theorem: if `Ï† : E â†’ â„` has a local extremum on the set `{x | f x = f xâ‚€}`
at `xâ‚€`, both `f : E â†’ F` and `Ï†` are strictly differentiable at `xâ‚€`, and the codomain of `f` is
a complete space, then there exist `Î› : dual â„ F` and `Î›â‚€ : â„` such that `(Î›, Î›â‚€) â‰  0` and
`Î› (f' x) + Î›â‚€ â€¢ Ï†' x = 0` for all `x`. -/
lemma is_local_extr_on.exists_linear_map_of_has_strict_fderiv_at
  (hextr : is_local_extr_on Ï† {x | f x = f xâ‚€} xâ‚€) (hf' : has_strict_fderiv_at f f' xâ‚€)
  (hÏ†' : has_strict_fderiv_at Ï† Ï†' xâ‚€) :
  âˆƒ (Î› : module.dual â„ F) (Î›â‚€ : â„), (Î›, Î›â‚€) â‰  0 âˆ§ âˆ€ x, Î› (f' x) + Î›â‚€ â€¢ Ï†' x = 0 :=
begin
  rcases submodule.exists_le_ker_of_lt_top _
    (lt_top_iff_ne_top.2 $ hextr.range_ne_top_of_has_strict_fderiv_at hf' hÏ†') with âŸ¨Î›', h0, hÎ›'âŸ©,
  set e : ((F â†’â‚—[â„] â„) Ã— â„) â‰ƒâ‚—[â„] (F Ã— â„ â†’â‚—[â„] â„) :=
    ((linear_equiv.refl â„ (F â†’â‚—[â„] â„)).prod (linear_map.ring_lmap_equiv_self â„ â„ â„).symm).trans
      (linear_map.coprod_equiv â„),
  rcases e.surjective Î›' with âŸ¨âŸ¨Î›, Î›â‚€âŸ©, rflâŸ©,
  refine âŸ¨Î›, Î›â‚€, e.map_ne_zero_iff.1 h0, Î» x, _âŸ©,
  convert linear_map.congr_fun (linear_map.range_le_ker_iff.1 hÎ›') x using 1,
  -- squeezed `simp [mul_comm]` to speed up elaboration
  simp only [mul_comm, algebra.id.smul_eq_mul, linear_equiv.trans_apply, linear_equiv.prod_apply,
             linear_equiv.refl_apply, linear_map.ring_lmap_equiv_self_symm_apply,
             linear_map.coprod_equiv_apply, continuous_linear_map.to_linear_map_eq_coe,
             continuous_linear_map.coe_prod, linear_map.coprod_comp_prod, linear_map.add_apply,
             linear_map.coe_comp, continuous_linear_map.coe_coe, linear_map.coe_smul_right,
             linear_map.one_apply]
end

/-- Lagrange multipliers theorem: if `Ï† : E â†’ â„` has a local extremum on the set `{x | f x = f xâ‚€}`
at `xâ‚€`, and both `f : E â†’ â„` and `Ï†` are strictly differentiable at `xâ‚€`, then there exist
`a b : â„` such that `(a, b) â‰  0` and `a â€¢ f' + b â€¢ Ï†' = 0`. -/
lemma is_local_extr_on.exists_multipliers_of_has_strict_fderiv_at_1d
  {f : E â†’ â„} {f' : E â†’L[â„] â„}
  (hextr : is_local_extr_on Ï† {x | f x = f xâ‚€} xâ‚€) (hf' : has_strict_fderiv_at f f' xâ‚€)
  (hÏ†' : has_strict_fderiv_at Ï† Ï†' xâ‚€) :
  âˆƒ (a b : â„), (a, b) â‰  0 âˆ§ a â€¢ f' + b â€¢ Ï†' = 0 :=
begin
  obtain âŸ¨Î›, Î›â‚€, hÎ›, hfÎ›âŸ© := hextr.exists_linear_map_of_has_strict_fderiv_at hf' hÏ†',
  refine âŸ¨Î› 1, Î›â‚€, _, _âŸ©,
  { contrapose! hÎ›,
    simp only [prod.mk_eq_zero] at âŠ¢ hÎ›,
    refine âŸ¨linear_map.ext (Î» x, _), hÎ›.2âŸ©,
    simpa [hÎ›.1] using Î›.map_smul x 1 },
  { ext x,
    have Hâ‚ : Î› (f' x) = f' x * Î› 1,
    { simpa only [mul_one, algebra.id.smul_eq_mul] using Î›.map_smul (f' x) 1 },
    have Hâ‚‚ : f' x * Î› 1  + Î›â‚€ * Ï†' x = 0,
    { simpa only [algebra.id.smul_eq_mul, Hâ‚] using hfÎ› x },
    simpa [mul_comm] using Hâ‚‚ }
end

/-- Lagrange multipliers theorem, 1d version. Let `f : Î¹ â†’ E â†’ â„` be a finite family of functions.
Suppose that `Ï† : E â†’ â„` has a local extremum on the set `{x | âˆ€ i, f i x = f i xâ‚€}` at `xâ‚€`.
Suppose that all functions `f i` as well as `Ï†` are strictly differentiable at `xâ‚€`.
Then the derivatives `f' i : E â†’ L[â„] â„` and `Ï†' : E â†’L[â„] â„` are linearly dependent:
there exist `Î› : Î¹ â†’ â„` and `Î›â‚€ : â„`, `(Î›, Î›â‚€) â‰  0`, such that `âˆ‘ i, Î› i â€¢ f' i + Î›â‚€ â€¢ Ï†' = 0`.

See also `is_local_extr_on.linear_dependent_of_has_strict_fderiv_at` for a version that
states `Â¬linear_independent â„ _` instead of existence of `Î›` and `Î›â‚€`. -/
lemma is_local_extr_on.exists_multipliers_of_has_strict_fderiv_at {Î¹ : Type*} [fintype Î¹]
  {f : Î¹ â†’ E â†’ â„} {f' : Î¹ â†’ E â†’L[â„] â„}
  (hextr : is_local_extr_on Ï† {x | âˆ€ i, f i x = f i xâ‚€} xâ‚€)
  (hf' : âˆ€ i, has_strict_fderiv_at (f i) (f' i) xâ‚€)
  (hÏ†' : has_strict_fderiv_at Ï† Ï†' xâ‚€) :
  âˆƒ (Î› : Î¹ â†’ â„) (Î›â‚€ : â„), (Î›, Î›â‚€) â‰  0 âˆ§ âˆ‘ i, Î› i â€¢ f' i + Î›â‚€ â€¢ Ï†' = 0 :=
begin
  letI := classical.dec_eq Î¹,
  replace hextr : is_local_extr_on Ï† {x | (Î» i, f i x) = (Î» i, f i xâ‚€)} xâ‚€,
    by simpa only [function.funext_iff] using hextr,
  rcases hextr.exists_linear_map_of_has_strict_fderiv_at
    (has_strict_fderiv_at_pi.2 (Î» i, hf' i)) hÏ†'
    with âŸ¨Î›, Î›â‚€, h0, hsumâŸ©,
  rcases (linear_equiv.pi_ring â„ â„ Î¹ â„).symm.surjective Î› with âŸ¨Î›, rflâŸ©,
  refine âŸ¨Î›, Î›â‚€, _, _âŸ©,
  { simpa only [ne.def, prod.ext_iff, linear_equiv.map_eq_zero_iff, prod.fst_zero] using h0 },
  { ext x, simpa [mul_comm] using hsum x }
end

/-- Lagrange multipliers theorem. Let `f : Î¹ â†’ E â†’ â„` be a finite family of functions.
Suppose that `Ï† : E â†’ â„` has a local extremum on the set `{x | âˆ€ i, f i x = f i xâ‚€}` at `xâ‚€`.
Suppose that all functions `f i` as well as `Ï†` are strictly differentiable at `xâ‚€`.
Then the derivatives `f' i : E â†’ L[â„] â„` and `Ï†' : E â†’L[â„] â„` are linearly dependent.

See also `is_local_extr_on.exists_multipliers_of_has_strict_fderiv_at` for a version that
that states existence of Lagrange multipliers `Î›` and `Î›â‚€` instead of using
`Â¬linear_independent â„ _` -/
lemma is_local_extr_on.linear_dependent_of_has_strict_fderiv_at {Î¹ : Type*} [finite Î¹]
  {f : Î¹ â†’ E â†’ â„} {f' : Î¹ â†’ E â†’L[â„] â„}
  (hextr : is_local_extr_on Ï† {x | âˆ€ i, f i x = f i xâ‚€} xâ‚€)
  (hf' : âˆ€ i, has_strict_fderiv_at (f i) (f' i) xâ‚€)
  (hÏ†' : has_strict_fderiv_at Ï† Ï†' xâ‚€) :
  Â¬linear_independent â„ (option.elim Ï†' f' : option Î¹ â†’ E â†’L[â„] â„) :=
begin
  casesI nonempty_fintype Î¹,
  rw [fintype.linear_independent_iff], push_neg,
  rcases hextr.exists_multipliers_of_has_strict_fderiv_at hf' hÏ†' with âŸ¨Î›, Î›â‚€, hÎ›, hÎ›fâŸ©,
  refine âŸ¨option.elim Î›â‚€ Î›, _, _âŸ©,
  { simpa [add_comm] using hÎ›f },
  { simpa [function.funext_iff, not_and_distrib, or_comm, option.exists] using hÎ› }
end
