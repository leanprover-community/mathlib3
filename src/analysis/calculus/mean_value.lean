/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov
-/
import analysis.calculus.local_extr
import analysis.convex.topology

/-!
# The mean value inequality and equalities

In this file we prove the following facts:

* `convex.norm_image_sub_le_of_norm_deriv_le` : if `f` is differentiable on a convex set `s`
  and the norm of its derivative is bounded by `C`, then `f` is Lipschitz continuous on `s` with
  constant `C`; also a variant in which what is bounded by `C` is the norm of the difference of the
  derivative from a fixed linear map.

* `image_le_of*`, `image_norm_le_of_*` : several similar lemmas deducing `f x ‚â§ B x` or
  `‚à•f x‚à• ‚â§ B x` from upper estimates on `f'` or `‚à•f'‚à•`, respectively. These lemmas differ by
  their assumptions:

  * `of_liminf_*` lemmas assume that limit inferior of some ratio is less than `B' x`;
  * `of_deriv_right_*`, `of_norm_deriv_right_*` lemmas assume that the right derivative
    or its norm is less than `B' x`;
  * `of_*_lt_*` lemmas assume a strict inequality whenever `f x = B x` or `‚à•f x‚à• = B x`;
  * `of_*_le_*` lemmas assume a non-strict inequality everywhere on `[a, b)`;
  * name of a lemma ends with `'` if (1) it assumes that `B` is continuous on `[a, b]`
    and has a right derivative at every point of `[a, b)`, and (2) the lemma has
    a counterpart assuming that `B` is differentiable everywhere on `‚Ñù`

* `norm_image_sub_le_*_segment` : if derivative of `f` on `[a, b]` is bounded above
  by a constant `C`, then `‚à•f x - f a‚à• ‚â§ C * ‚à•x - a‚à•`; several versions deal with
  right derivative and derivative within `[a, b]` (`has_deriv_within_at` or `deriv_within`).

* `convex.is_const_of_fderiv_within_eq_zero` : if a function has derivative `0` on a convex set `s`,
  then it is a constant on `s`.

* `exists_ratio_has_deriv_at_eq_ratio_slope` and `exists_ratio_deriv_eq_ratio_slope` :
  Cauchy's Mean Value Theorem.

* `exists_has_deriv_at_eq_slope` and `exists_deriv_eq_slope` : Lagrange's Mean Value Theorem.

* `domain_mvt` : Lagrange's Mean Value Theorem, applied to a segment in a convex domain.

* `convex.image_sub_lt_mul_sub_of_deriv_lt`, `convex.mul_sub_lt_image_sub_of_lt_deriv`,
  `convex.image_sub_le_mul_sub_of_deriv_le`, `convex.mul_sub_le_image_sub_of_le_deriv`,
  if `‚àÄ x, C (</‚â§/>/‚â•) (f' x)`, then `C * (y - x) (</‚â§/>/‚â•) (f y - f x)` whenever `x < y`.

* `convex.mono_of_deriv_nonneg`, `convex.antimono_of_deriv_nonpos`,
  `convex.strict_mono_of_deriv_pos`, `convex.strict_antimono_of_deriv_neg` :
  if the derivative of a function is non-negative/non-positive/positive/negative, then
  the function is monotone/monotonically decreasing/strictly monotone/strictly monotonically
  decreasing.

* `convex_on_of_deriv_mono`, `convex_on_of_deriv2_nonneg` : if the derivative of a function
  is increasing or its second derivative is nonnegative, then the original function is convex.

* `strict_fderiv_of_cont_diff` : a C^1 function over the reals is strictly differentiable.  (This
  is a corollary of the mean value inequality.)
-/


variables {E : Type*} [normed_group E] [normed_space ‚Ñù E]
          {F : Type*} [normed_group F] [normed_space ‚Ñù F]

open metric set asymptotics continuous_linear_map filter
open_locale classical topological_space

/-! ### One-dimensional fencing inequalities -/

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
lemma image_le_of_liminf_slope_right_lt_deriv_boundary' {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
  (hf : continuous_on f (Icc a b))
  -- `hf'` actually says `liminf (z - x)‚Åª¬π * (f z - f x) ‚â§ f' x`
  (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí
    ‚àÉ·∂† z in nhds_within x (Ioi x), (z - x)‚Åª¬π * (f z - f x) < r)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : continuous_on B (Icc a b))
  (hB' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at B (B' x) (Ioi x) x)
  (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
begin
  change Icc a b ‚äÜ {x | f x ‚â§ B x},
  set s := {x | f x ‚â§ B x} ‚à© Icc a b,
  have A : continuous_on (Œª x, (f x, B x)) (Icc a b), from hf.prod hB,
  have : is_closed s,
  { simp only [s, inter_comm],
    exact A.preimage_closed_of_closed is_closed_Icc order_closed_topology.is_closed_le' },
  apply this.Icc_subset_of_forall_exists_gt ha,
  rintros x ‚ü®hxB, xab‚ü© y hy,
  change f x ‚â§ B x at hxB,
  cases lt_or_eq_of_le hxB with hxB hxB,
  { -- If `f x < B x`, then all we need is continuity of both sides
    apply @nonempty_of_mem_sets _ (nhds_within x (Ioi x)),
    refine inter_mem_sets _ (Ioc_mem_nhds_within_Ioi ‚ü®le_refl x, hy‚ü©),
    have : ‚àÄ·∂† x in nhds_within x (Icc a b), f x < B x,
      from A x (Ico_subset_Icc_self xab)
        (mem_nhds_sets (is_open_lt continuous_fst continuous_snd) hxB),
    have : ‚àÄ·∂† x in nhds_within x (Ioi x), f x < B x,
      from nhds_within_le_of_mem (Icc_mem_nhds_within_Ioi xab) this,
    refine mem_sets_of_superset this (set_of_subset_set_of.2 $ Œª y, le_of_lt) },
  { rcases dense (bound x xab hxB) with ‚ü®r, hfr, hrB‚ü©,
    specialize hf' x xab r hfr,
    have HB : ‚àÄ·∂† z in nhds_within x (Ioi x), r < (z - x)‚Åª¬π * (B z - B x),
      from (has_deriv_within_at_iff_tendsto_slope' $ lt_irrefl x).1 (hB' x xab)
        (mem_nhds_sets is_open_Ioi hrB),
    obtain ‚ü®z, ‚ü®hfz, hzB‚ü©, hz‚ü© :
      ‚àÉ z, ((z - x)‚Åª¬π * (f z - f x) < r ‚àß r < (z - x)‚Åª¬π * (B z - B x)) ‚àß z ‚àà Ioc x y,
      from ((hf'.and_eventually HB).and_eventually
        (Ioc_mem_nhds_within_Ioi ‚ü®le_refl _, hy‚ü©)).exists,
    have := le_of_lt (lt_trans hfz hzB),
    refine ‚ü®z, _, hz‚ü©,
    rw [mul_le_mul_left (inv_pos.2 $ sub_pos.2 hz.1), hxB, sub_le_sub_iff_right] at this,
    exact this }
end

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
lemma image_le_of_liminf_slope_right_lt_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
  (hf : continuous_on f (Icc a b))
  -- `hf'` actually says `liminf (z - x)‚Åª¬π * (f z - f x) ‚â§ f' x`
  (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí
    ‚àÉ·∂† z in nhds_within x (Ioi x), (z - x)‚Åª¬π * (f z - f x) < r)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ‚àÄ x, has_deriv_at B (B' x) x)
  (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
image_le_of_liminf_slope_right_lt_deriv_boundary' hf hf' ha
  (Œª x hx, (hB x).continuous_at.continuous_within_at)
  (Œª x hx, (hB x).has_deriv_within_at) bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by `B'`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
lemma image_le_of_liminf_slope_right_le_deriv_boundary {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
  (hf : continuous_on f (Icc a b))
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : continuous_on B (Icc a b))
  (hB' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at B (B' x) (Ioi x) x)
  -- `bound` actually says `liminf (z - x)‚Åª¬π * (f z - f x) ‚â§ B' x`
  (bound : ‚àÄ x ‚àà Ico a b, ‚àÄ r, B' x < r ‚Üí
    ‚àÉ·∂† z in nhds_within x (Ioi x), (z - x)‚Åª¬π * (f z - f x) < r) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
begin
  have Hr : ‚àÄ x ‚àà Icc a b, ‚àÄ r ‚àà Ioi (0:‚Ñù), f x ‚â§ B x + r * (x - a),
  { intros x hx r hr,
    apply image_le_of_liminf_slope_right_lt_deriv_boundary' hf bound,
    { rwa [sub_self, mul_zero, add_zero] },
    { exact hB.add (continuous_on_const.mul
        (continuous_id.continuous_on.sub continuous_on_const)) },
    { assume x hx,
      exact (hB' x hx).add (((has_deriv_within_at_id x (Ioi x)).sub_const a).const_mul r) },
    { assume x hx _,
      rw [mul_one],
      exact (lt_add_iff_pos_right _).2 hr },
    exact hx },
  assume x hx,
  have : continuous_within_at (Œª r, B x + r * (x - a)) (Ioi 0) 0,
    from continuous_within_at_const.add (continuous_within_at_id.mul continuous_within_at_const),
  convert continuous_within_at_const.closure_le _ this (Hr x hx); simp [closure_Ioi]
end

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
lemma image_le_of_deriv_right_lt_deriv_boundary' {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : continuous_on B (Icc a b))
  (hB' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at B (B' x) (Ioi x) x)
  (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
image_le_of_liminf_slope_right_lt_deriv_boundary' hf
  (Œª x hx r hr, (hf' x hx).liminf_right_slope_le hr) ha hB hB' bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
lemma image_le_of_deriv_right_lt_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ‚àÄ x, has_deriv_at B (B' x) x)
  (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
image_le_of_deriv_right_lt_deriv_boundary' hf hf' ha
  (Œª x hx, (hB x).continuous_at.continuous_within_at)
  (Œª x hx, (hB x).has_deriv_within_at) bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x ‚â§ B' x` on `[a, b)`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
lemma image_le_of_deriv_right_le_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : continuous_on B (Icc a b))
  (hB' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at B (B' x) (Ioi x) x)
  (bound : ‚àÄ x ‚àà Ico a b, f' x ‚â§ B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
image_le_of_liminf_slope_right_le_deriv_boundary hf ha hB hB' $
assume x hx r hr, (hf' x hx).liminf_right_slope_le (lt_of_le_of_lt (bound x hx) hr)

/-! ### Vector-valued functions `f : ‚Ñù ‚Üí E` -/

section

variables {f : ‚Ñù ‚Üí E} {a b : ‚Ñù}

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚à•f a‚à• ‚â§ B a`;
* `B` has right derivative at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(‚à•f z‚à• - ‚à•f x‚à•) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `‚à•f x‚à• = B x`.

Then `‚à•f x‚à• ‚â§ B x` everywhere on `[a, b]`. -/
lemma image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary {E : Type*} [normed_group E]
  {f : ‚Ñù ‚Üí E} {f' : ‚Ñù ‚Üí ‚Ñù} (hf : continuous_on f (Icc a b))
  -- `hf'` actually says `liminf ‚à•z - x‚à•‚Åª¬π * (‚à•f z‚à• - ‚à•f x‚à•) ‚â§ f' x`
  (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí
    ‚àÉ·∂† z in nhds_within x (Ioi x), (z - x)‚Åª¬π * (‚à•f z‚à• - ‚à•f x‚à•) < r)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚à•f a‚à• ‚â§ B a) (hB : continuous_on B (Icc a b))
  (hB' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at B (B' x) (Ioi x) x)
  (bound : ‚àÄ x ‚àà Ico a b, ‚à•f x‚à• = B x ‚Üí f' x < B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚à•f x‚à• ‚â§ B x :=
image_le_of_liminf_slope_right_lt_deriv_boundary' (continuous_norm.comp_continuous_on hf) hf'
    ha hB hB' bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚à•f a‚à• ‚â§ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* the norm of `f'` is strictly less than `B'` whenever `‚à•f x‚à• = B x`.

Then `‚à•f x‚à• ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
lemma image_norm_le_of_norm_deriv_right_lt_deriv_boundary' {f' : ‚Ñù ‚Üí E}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚à•f a‚à• ‚â§ B a) (hB : continuous_on B (Icc a b))
  (hB' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at B (B' x) (Ioi x) x)
  (bound : ‚àÄ x ‚àà Ico a b, ‚à•f x‚à• = B x ‚Üí ‚à•f' x‚à• < B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚à•f x‚à• ‚â§ B x :=
image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary hf
  (Œª x hx r hr, (hf' x hx).liminf_right_slope_norm_le hr) ha hB hB' bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚à•f a‚à• ‚â§ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* the norm of `f'` is strictly less than `B'` whenever `‚à•f x‚à• = B x`.

Then `‚à•f x‚à• ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
lemma image_norm_le_of_norm_deriv_right_lt_deriv_boundary {f' : ‚Ñù ‚Üí E}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚à•f a‚à• ‚â§ B a) (hB : ‚àÄ x, has_deriv_at B (B' x) x)
  (bound : ‚àÄ x ‚àà Ico a b, ‚à•f x‚à• = B x ‚Üí ‚à•f' x‚à• < B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚à•f x‚à• ‚â§ B x :=
image_norm_le_of_norm_deriv_right_lt_deriv_boundary' hf hf' ha
  (Œª x hx, (hB x).continuous_at.continuous_within_at)
  (Œª x hx, (hB x).has_deriv_within_at) bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚à•f a‚à• ‚â§ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* we have `‚à•f' x‚à• ‚â§ B x` everywhere on `[a, b)`.

Then `‚à•f x‚à• ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
lemma image_norm_le_of_norm_deriv_right_le_deriv_boundary' {f' : ‚Ñù ‚Üí E}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚à•f a‚à• ‚â§ B a) (hB : continuous_on B (Icc a b))
  (hB' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at B (B' x) (Ioi x) x)
  (bound : ‚àÄ x ‚àà Ico a b, ‚à•f' x‚à• ‚â§ B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚à•f x‚à• ‚â§ B x :=
image_le_of_liminf_slope_right_le_deriv_boundary (continuous_norm.comp_continuous_on hf) ha hB hB' $
  (Œª x hx r hr, (hf' x hx).liminf_right_slope_norm_le (lt_of_le_of_lt (bound x hx) hr))

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚à•f a‚à• ‚â§ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* we have `‚à•f' x‚à• ‚â§ B x` everywhere on `[a, b)`.

Then `‚à•f x‚à• ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
lemma image_norm_le_of_norm_deriv_right_le_deriv_boundary {f' : ‚Ñù ‚Üí E}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚à•f a‚à• ‚â§ B a) (hB : ‚àÄ x, has_deriv_at B (B' x) x)
  (bound : ‚àÄ x ‚àà Ico a b, ‚à•f' x‚à• ‚â§ B' x) :
  ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚à•f x‚à• ‚â§ B x :=
image_norm_le_of_norm_deriv_right_le_deriv_boundary' hf hf' ha
  (Œª x hx, (hB x).continuous_at.continuous_within_at)
  (Œª x hx, (hB x).has_deriv_within_at) bound

/-- A function on `[a, b]` with the norm of the right derivative bounded by `C`
satisfies `‚à•f x - f a‚à• ‚â§ C * (x - a)`. -/
theorem norm_image_sub_le_of_norm_deriv_right_le_segment {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
  (hf : continuous_on f (Icc a b))
  (hf' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at f (f' x) (Ioi x) x)
  (bound : ‚àÄx ‚àà Ico a b, ‚à•f' x‚à• ‚â§ C) :
  ‚àÄ x ‚àà Icc a b, ‚à•f x - f a‚à• ‚â§ C * (x - a) :=
begin
  let g := Œª x, f x - f a,
  have hg : continuous_on g (Icc a b), from hf.sub continuous_on_const,
  have hg' : ‚àÄ x ‚àà Ico a b, has_deriv_within_at g (f' x) (Ioi x) x,
  { assume x hx,
    simpa using (hf' x hx).sub (has_deriv_within_at_const _ _ _) },
  let B := Œª x, C * (x - a),
  have hB : ‚àÄ x, has_deriv_at B C x,
  { assume x,
    simpa using (has_deriv_at_const x C).mul ((has_deriv_at_id x).sub (has_deriv_at_const x a)) },
  convert image_norm_le_of_norm_deriv_right_le_deriv_boundary hg hg' _ hB bound,
  { simp only [g, B] },
  { simp only [g, B], rw [sub_self, _root_.norm_zero, sub_self, mul_zero] }
end

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `‚à•f x - f a‚à• ‚â§ C * (x - a)`, `has_deriv_within_at`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment' {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
  (hf : ‚àÄ x ‚àà Icc a b, has_deriv_within_at f (f' x) (Icc a b) x)
  (bound : ‚àÄx ‚àà Ico a b, ‚à•f' x‚à• ‚â§ C) :
  ‚àÄ x ‚àà Icc a b, ‚à•f x - f a‚à• ‚â§ C * (x - a) :=
begin
  refine norm_image_sub_le_of_norm_deriv_right_le_segment
    (Œª x hx, (hf x hx).continuous_within_at) (Œª x hx, _) bound,
  apply (hf x $ Ico_subset_Icc_self hx).nhds_within,
  exact Icc_mem_nhds_within_Ioi hx
end

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `‚à•f x - f a‚à• ‚â§ C * (x - a)`, `deriv_within`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment {C : ‚Ñù} (hf : differentiable_on ‚Ñù f (Icc a b))
  (bound : ‚àÄx ‚àà Ico a b, ‚à•deriv_within f (Icc a b) x‚à• ‚â§ C) :
  ‚àÄ x ‚àà Icc a b, ‚à•f x - f a‚à• ‚â§ C * (x - a) :=
begin
  refine norm_image_sub_le_of_norm_deriv_le_segment' _ bound,
  exact Œª x hx, (hf x  hx).has_deriv_within_at
end

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `‚à•f 1 - f 0‚à• ‚â§ C`, `has_deriv_within_at`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01' {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
  (hf : ‚àÄ x ‚àà Icc (0:‚Ñù) 1, has_deriv_within_at f (f' x) (Icc (0:‚Ñù) 1) x)
  (bound : ‚àÄx ‚àà Ico (0:‚Ñù) 1, ‚à•f' x‚à• ‚â§ C) :
  ‚à•f 1 - f 0‚à• ‚â§ C :=
by simpa only [sub_zero, mul_one]
  using norm_image_sub_le_of_norm_deriv_le_segment' hf bound 1 (right_mem_Icc.2 zero_le_one)

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `‚à•f 1 - f 0‚à• ‚â§ C`, `deriv_within` version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01 {C : ‚Ñù}
  (hf : differentiable_on ‚Ñù f (Icc (0:‚Ñù) 1))
  (bound : ‚àÄx ‚àà Ico (0:‚Ñù) 1, ‚à•deriv_within f (Icc (0:‚Ñù) 1) x‚à• ‚â§ C) :
  ‚à•f 1 - f 0‚à• ‚â§ C :=
by simpa only [sub_zero, mul_one]
  using norm_image_sub_le_of_norm_deriv_le_segment hf bound 1 (right_mem_Icc.2 zero_le_one)

end

/-! ### Vector-valued functions `f : E ‚Üí F` -/

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`, then
the function is `C`-Lipschitz. Version with `has_fderiv_within`. -/
theorem convex.norm_image_sub_le_of_norm_has_fderiv_within_le
  {f : E ‚Üí F} {C : ‚Ñù} {s : set E} {x y : E} {f' : E ‚Üí (E ‚ÜíL[‚Ñù] F)}
  (hf : ‚àÄ x ‚àà s, has_fderiv_within_at f (f' x) s x) (bound : ‚àÄx‚ààs, ‚à•f' x‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x‚à• ‚â§ C * ‚à•y - x‚à• :=
begin
  /- By composition with `t ‚Ü¶ x + t ‚Ä¢ (y-x)`, we reduce to a statement for functions defined
  on `[0,1]`, for which it is proved in `norm_image_sub_le_of_norm_deriv_le_segment`.
  We just have to check the differentiability of the composition and bounds on its derivative,
  which is straightforward but tedious for lack of automation. -/
  have C0 : 0 ‚â§ C := le_trans (norm_nonneg _) (bound x xs),
  set g : ‚Ñù ‚Üí E := Œª t, x + t ‚Ä¢ (y - x),
  have Dg : ‚àÄ t, has_deriv_at g (y-x) t,
  { assume t,
    simpa only [one_smul] using ((has_deriv_at_id t).smul_const (y - x)).const_add x },
  have segm : Icc 0 1 ‚äÜ g ‚Åª¬π' s,
  { rw [‚Üê image_subset_iff, ‚Üê segment_eq_image'],
    apply hs.segment_subset xs ys },
  have : f x = f (g 0), by { simp only [g], rw [zero_smul, add_zero] },
  rw this,
  have : f y = f (g 1), by { simp only [g], rw [one_smul, add_sub_cancel'_right] },
  rw this,
  have D2: ‚àÄ t ‚àà Icc (0:‚Ñù) 1, has_deriv_within_at (f ‚àò g)
    ((f' (g t) : E ‚Üí F) (y-x)) (Icc (0:‚Ñù) 1) t,
  { intros t ht,
    exact (hf (g t) $ segm ht).comp_has_deriv_within_at _
      (Dg t).has_deriv_within_at segm },
  apply norm_image_sub_le_of_norm_deriv_le_segment_01' D2,
  assume t ht,
  refine le_trans (le_op_norm _ _) (mul_le_mul_of_nonneg_right _ (norm_nonneg _)),
  exact bound (g t) (segm $ Ico_subset_Icc_self ht)
end

/-- The mean value theorem on a convex set: if the derivative of a function within this set is
bounded by `C`, then the function is `C`-Lipschitz. Version with `fderiv_within`. -/
theorem convex.norm_image_sub_le_of_norm_fderiv_within_le {f : E ‚Üí F} {C : ‚Ñù} {s : set E} {x y : E}
  (hf : differentiable_on ‚Ñù f s) (bound : ‚àÄx‚ààs, ‚à•fderiv_within ‚Ñù f s x‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x‚à• ‚â§ C * ‚à•y - x‚à• :=
hs.norm_image_sub_le_of_norm_has_fderiv_within_le (Œª x hx, (hf x hx).has_fderiv_within_at)
bound xs ys

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`,
then the function is `C`-Lipschitz. Version with `fderiv`. -/
theorem convex.norm_image_sub_le_of_norm_fderiv_le {f : E ‚Üí F} {C : ‚Ñù} {s : set E} {x y : E}
  (hf : ‚àÄ x ‚àà s, differentiable_at ‚Ñù f x) (bound : ‚àÄx‚ààs, ‚à•fderiv ‚Ñù f x‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x‚à• ‚â§ C * ‚à•y - x‚à• :=
hs.norm_image_sub_le_of_norm_has_fderiv_within_le
(Œª x hx, (hf x hx).has_fderiv_at.has_fderiv_within_at) bound xs ys

/-- Variant of the mean value inequality on a convex set, using a bound on the difference between
the derivative and a fixed linear map, rather than a bound on the derivative itself. Version with
`has_fderiv_within`. -/
theorem convex.norm_image_sub_le_of_norm_has_fderiv_within_le'
  {f : E ‚Üí F} {C : ‚Ñù} {s : set E} {x y : E} {f' : E ‚Üí (E ‚ÜíL[‚Ñù] F)} {œÜ : E ‚ÜíL[‚Ñù] F}
  (hf : ‚àÄ x ‚àà s, has_fderiv_within_at f (f' x) s x) (bound : ‚àÄx‚ààs, ‚à•f' x - œÜ‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x - œÜ (y - x)‚à• ‚â§ C * ‚à•y - x‚à• :=
begin
  /- We subtract `œÜ` to define a new function `g` for which `g' = 0`, for which the previous theorem
  applies, `convex.norm_image_sub_le_of_norm_has_fderiv_within_le`. Then, we just need to glue
  together the pieces, expressing back `f` in terms of `g`. -/
  let g := Œªy, f y - œÜ y,
  have hg : ‚àÄ x ‚àà s, has_fderiv_within_at g (f' x - œÜ) s x :=
    Œª x xs, (hf x xs).sub œÜ.has_fderiv_within_at,
  calc ‚à•f y - f x - œÜ (y - x)‚à• = ‚à•f y - f x - (œÜ y - œÜ x)‚à• : by simp
  ... = ‚à•(f y - œÜ y) - (f x - œÜ x)‚à• : by abel
  ... = ‚à•g y - g x‚à• : by simp
  ... ‚â§ C * ‚à•y - x‚à• : convex.norm_image_sub_le_of_norm_has_fderiv_within_le hg bound hs xs ys,
end

/-- Variant of the mean value inequality on a convex set. Version with `fderiv_within`. -/
theorem convex.norm_image_sub_le_of_norm_fderiv_within_le' {f : E ‚Üí F} {C : ‚Ñù} {s : set E} {x y : E}
  {œÜ : E ‚ÜíL[‚Ñù] F} (hf : differentiable_on ‚Ñù f s) (bound : ‚àÄx‚ààs, ‚à•fderiv_within ‚Ñù f s x - œÜ‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x - œÜ (y - x)‚à• ‚â§ C * ‚à•y - x‚à• :=
hs.norm_image_sub_le_of_norm_has_fderiv_within_le' (Œª x hx, (hf x hx).has_fderiv_within_at)
bound xs ys

/-- Variant of the mean value inequality on a convex set. Version with `fderiv`. -/
theorem convex.norm_image_sub_le_of_norm_fderiv_le' {f : E ‚Üí F} {C : ‚Ñù} {s : set E} {x y : E}
  {œÜ : E ‚ÜíL[‚Ñù] F} (hf : ‚àÄ x ‚àà s, differentiable_at ‚Ñù f x) (bound : ‚àÄx‚ààs, ‚à•fderiv ‚Ñù f x - œÜ‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x - œÜ (y - x)‚à• ‚â§ C * ‚à•y - x‚à• :=
hs.norm_image_sub_le_of_norm_has_fderiv_within_le'
(Œª x hx, (hf x hx).has_fderiv_at.has_fderiv_within_at) bound xs ys

/-- If a function has zero Fr√©chet derivative at every point of a convex set,
then it is a constant on this set. -/
theorem convex.is_const_of_fderiv_within_eq_zero {s : set E} (hs : convex s)
  {f : E ‚Üí F} (hf : differentiable_on ‚Ñù f s) (hf' : ‚àÄ x ‚àà s, fderiv_within ‚Ñù f s x = 0)
  {x y : E} (hx : x ‚àà s) (hy : y ‚àà s) :
  f x = f y :=
have bound : ‚àÄ x ‚àà s, ‚à•fderiv_within ‚Ñù f s x‚à• ‚â§ 0,
  from Œª x hx, by simp only [hf' x hx, _root_.norm_zero],
by simpa only [(dist_eq_norm _ _).symm, zero_mul, dist_le_zero, eq_comm]
  using hs.norm_image_sub_le_of_norm_fderiv_within_le hf bound hx hy

theorem is_const_of_fderiv_eq_zero {f : E ‚Üí F} (hf : differentiable ‚Ñù f)
  (hf' : ‚àÄ x, fderiv ‚Ñù f x = 0) (x y : E) :
  f x = f y :=
convex_univ.is_const_of_fderiv_within_eq_zero hf.differentiable_on
  (Œª x _, by rw fderiv_within_univ; exact hf' x) trivial trivial

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `has_deriv_within`. -/
theorem convex.norm_image_sub_le_of_norm_has_deriv_within_le
  {f f' : ‚Ñù ‚Üí F} {C : ‚Ñù} {s : set ‚Ñù} {x y : ‚Ñù}
  (hf : ‚àÄ x ‚àà s, has_deriv_within_at f (f' x) s x) (bound : ‚àÄx‚ààs, ‚à•f' x‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x‚à• ‚â§ C * ‚à•y - x‚à• :=
convex.norm_image_sub_le_of_norm_has_fderiv_within_le (Œª x hx, (hf x hx).has_fderiv_within_at)
(Œª x hx, le_trans (by simp) (bound x hx)) hs xs ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function within
this set is bounded by `C`, then the function is `C`-Lipschitz. Version with `deriv_within` -/
theorem convex.norm_image_sub_le_of_norm_deriv_within_le
  {f : ‚Ñù ‚Üí F} {C : ‚Ñù} {s : set ‚Ñù} {x y : ‚Ñù}
  (hf : differentiable_on ‚Ñù f s) (bound : ‚àÄx‚ààs, ‚à•deriv_within f s x‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x‚à• ‚â§ C * ‚à•y - x‚à• :=
hs.norm_image_sub_le_of_norm_has_deriv_within_le (Œª x hx, (hf x hx).has_deriv_within_at)
bound xs ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `deriv`. -/
theorem convex.norm_image_sub_le_of_norm_deriv_le {f : ‚Ñù ‚Üí F} {C : ‚Ñù} {s : set ‚Ñù} {x y : ‚Ñù}
  (hf : ‚àÄ x ‚àà s, differentiable_at ‚Ñù f x) (bound : ‚àÄx‚ààs, ‚à•deriv f x‚à• ‚â§ C)
  (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚à•f y - f x‚à• ‚â§ C * ‚à•y - x‚à• :=
hs.norm_image_sub_le_of_norm_has_deriv_within_le
(Œª x hx, (hf x hx).has_deriv_at.has_deriv_within_at) bound xs ys

/-! ### Functions `[a, b] ‚Üí ‚Ñù`. -/

section interval

-- Declare all variables here to make sure they come in a correct order
variables (f f' : ‚Ñù ‚Üí ‚Ñù) {a b : ‚Ñù} (hab : a < b) (hfc : continuous_on f (Icc a b))
  (hff' : ‚àÄ x ‚àà Ioo a b, has_deriv_at f (f' x) x) (hfd : differentiable_on ‚Ñù f (Ioo a b))
  (g g' : ‚Ñù ‚Üí ‚Ñù) (hgc : continuous_on g (Icc a b)) (hgg' : ‚àÄ x ‚àà Ioo a b, has_deriv_at g (g' x) x)
  (hgd : differentiable_on ‚Ñù g (Ioo a b))

include hab hfc hff' hgc hgg'

/-- Cauchy's Mean Value Theorem, `has_deriv_at` version. -/
lemma exists_ratio_has_deriv_at_eq_ratio_slope :
  ‚àÉ c ‚àà Ioo a b, (g b - g a) * f' c = (f b - f a) * g' c :=
begin
  let h := Œª x, (g b - g a) * f x - (f b - f a) * g x,
  have hI : h a = h b,
  { simp only [h], ring },
  let h' := Œª x, (g b - g a) * f' x - (f b - f a) * g' x,
  have hhh' : ‚àÄ x ‚àà Ioo a b, has_deriv_at h (h' x) x,
    from Œª x hx, ((hff' x hx).const_mul (g b - g a)).sub ((hgg' x hx).const_mul (f b - f a)),
  have hhc : continuous_on h (Icc a b),
    from (continuous_on_const.mul hfc).sub (continuous_on_const.mul hgc),
  rcases exists_has_deriv_at_eq_zero h h' hab hhc hI hhh' with ‚ü®c, cmem, hc‚ü©,
  exact ‚ü®c, cmem, sub_eq_zero.1 hc‚ü©
end

omit hgc hgg'

/-- Lagrange's Mean Value Theorem, `has_deriv_at` version -/
lemma exists_has_deriv_at_eq_slope : ‚àÉ c ‚àà Ioo a b, f' c = (f b - f a) / (b - a) :=
begin
  rcases exists_ratio_has_deriv_at_eq_ratio_slope f f' hab hfc hff'
    id 1 continuous_id.continuous_on (Œª x hx, has_deriv_at_id x) with ‚ü®c, cmem, hc‚ü©,
  use [c, cmem],
  simp only [_root_.id, pi.one_apply, mul_one] at hc,
  rw [‚Üê hc, mul_div_cancel_left],
  exact ne_of_gt (sub_pos.2 hab)
end

omit hff'

/-- Cauchy's Mean Value Theorem, `deriv` version. -/
lemma exists_ratio_deriv_eq_ratio_slope :
  ‚àÉ c ‚àà Ioo a b, (g b - g a) * (deriv f c) = (f b - f a) * (deriv g c) :=
exists_ratio_has_deriv_at_eq_ratio_slope f (deriv f) hab hfc
  (Œª x hx, ((hfd x hx).differentiable_at $ mem_nhds_sets is_open_Ioo hx).has_deriv_at)
  g (deriv g) hgc (Œª x hx, ((hgd x hx).differentiable_at $ mem_nhds_sets is_open_Ioo hx).has_deriv_at)

/-- Lagrange's Mean Value Theorem, `deriv` version. -/
lemma exists_deriv_eq_slope : ‚àÉ c ‚àà Ioo a b, deriv f c = (f b - f a) / (b - a) :=
exists_has_deriv_at_eq_slope f (deriv f) hab hfc
  (Œª x hx, ((hfd x hx).differentiable_at $ mem_nhds_sets is_open_Ioo hx).has_deriv_at)

end interval

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `C < f'`, then
`f` grows faster than `C * x` on `D`, i.e., `C * (y - x) < f y - f x` whenever `x, y ‚àà D`,
`x < y`. -/
theorem convex.mul_sub_lt_image_sub_of_lt_deriv {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  {C} (hf'_gt : ‚àÄ x ‚àà interior D, C < deriv f x) :
  ‚àÄ x y ‚àà D, x < y ‚Üí C * (y - x) < f y - f x :=
begin
  assume x y hx hy hxy,
  have hxyD : Icc x y ‚äÜ D, from convex_real_iff.1 hD hx hy,
  have hxyD' : Ioo x y ‚äÜ interior D,
    from subset_sUnion_of_mem ‚ü®is_open_Ioo, subset.trans Ioo_subset_Icc_self hxyD‚ü©,
  obtain ‚ü®a, a_mem, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x),
    from exists_deriv_eq_slope f hxy (hf.mono hxyD) (hf'.mono hxyD'),
  have : C < (f y - f x) / (y - x), by { rw [‚Üê ha], exact hf'_gt _ (hxyD' a_mem) },
  exact (lt_div_iff (sub_pos.2 hxy)).1 this
end

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `C < f'`, then `f` grows faster than
`C * x`, i.e., `C * (y - x) < f y - f x` whenever `x < y`. -/
theorem mul_sub_lt_image_sub_of_lt_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f)
  {C} (hf'_gt : ‚àÄ x, C < deriv f x) ‚¶Éx y‚¶Ñ (hxy : x < y) :
  C * (y - x) < f y - f x :=
convex_univ.mul_sub_lt_image_sub_of_lt_deriv hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, hf'_gt x) x y trivial trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `C ‚â§ f'`, then
`f` grows at least as fast as `C * x` on `D`, i.e., `C * (y - x) ‚â§ f y - f x` whenever `x, y ‚àà D`,
`x ‚â§ y`. -/
theorem convex.mul_sub_le_image_sub_of_le_deriv {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  {C} (hf'_ge : ‚àÄ x ‚àà interior D, C ‚â§ deriv f x) :
  ‚àÄ x y ‚àà D, x ‚â§ y ‚Üí C * (y - x) ‚â§ f y - f x :=
begin
  assume x y hx hy hxy,
  cases eq_or_lt_of_le hxy with hxy' hxy', by rw [hxy', sub_self, sub_self, mul_zero],
  have hxyD : Icc x y ‚äÜ D, from convex_real_iff.1 hD hx hy,
  have hxyD' : Ioo x y ‚äÜ interior D,
    from subset_sUnion_of_mem ‚ü®is_open_Ioo, subset.trans Ioo_subset_Icc_self hxyD‚ü©,
  obtain ‚ü®a, a_mem, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x),
    from exists_deriv_eq_slope f hxy' (hf.mono hxyD) (hf'.mono hxyD'),
  have : C ‚â§ (f y - f x) / (y - x), by { rw [‚Üê ha], exact hf'_ge _ (hxyD' a_mem) },
  exact (le_div_iff (sub_pos.2 hxy')).1 this
end

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `C ‚â§ f'`, then `f` grows at least as fast
as `C * x`, i.e., `C * (y - x) ‚â§ f y - f x` whenever `x ‚â§ y`. -/
theorem mul_sub_le_image_sub_of_le_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f)
  {C} (hf'_ge : ‚àÄ x, C ‚â§ deriv f x) ‚¶Éx y‚¶Ñ (hxy : x ‚â§ y) :
  C * (y - x) ‚â§ f y - f x :=
convex_univ.mul_sub_le_image_sub_of_le_deriv hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, hf'_ge x) x y trivial trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f' < C`, then
`f` grows slower than `C * x` on `D`, i.e., `f y - f x < C * (y - x)` whenever `x, y ‚àà D`,
`x < y`. -/
theorem convex.image_sub_lt_mul_sub_of_deriv_lt {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  {C} (lt_hf' : ‚àÄ x ‚àà interior D, deriv f x < C) :
  ‚àÄ x y ‚àà D, x < y ‚Üí f y - f x < C * (y - x) :=
begin
  assume x y hx hy hxy,
  have hf'_gt : ‚àÄ x ‚àà interior D, -C < deriv (Œª y, -f y) x,
  { assume x hx,
    rw [deriv.neg, neg_lt_neg_iff],
    exact lt_hf' x hx },
  simpa [-neg_lt_neg_iff]
    using neg_lt_neg (hD.mul_sub_lt_image_sub_of_lt_deriv hf.neg hf'.neg hf'_gt x y hx hy hxy)
end

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f' < C`, then `f` grows slower than
`C * x` on `D`, i.e., `f y - f x < C * (y - x)` whenever `x < y`. -/
theorem image_sub_lt_mul_sub_of_deriv_lt {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f)
  {C} (lt_hf' : ‚àÄ x, deriv f x < C) ‚¶Éx y‚¶Ñ (hxy : x < y) :
  f y - f x < C * (y - x) :=
convex_univ.image_sub_lt_mul_sub_of_deriv_lt hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, lt_hf' x) x y trivial trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f' ‚â§ C`, then
`f` grows at most as fast as `C * x` on `D`, i.e., `f y - f x ‚â§ C * (y - x)` whenever `x, y ‚àà D`,
`x ‚â§ y`. -/
theorem convex.image_sub_le_mul_sub_of_deriv_le {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  {C} (le_hf' : ‚àÄ x ‚àà interior D, deriv f x ‚â§ C) :
  ‚àÄ x y ‚àà D, x ‚â§ y ‚Üí f y - f x ‚â§ C * (y - x) :=
begin
  assume x y hx hy hxy,
  have hf'_ge : ‚àÄ x ‚àà interior D, -C ‚â§ deriv (Œª y, -f y) x,
  { assume x hx,
    rw [deriv.neg, neg_le_neg_iff],
    exact le_hf' x hx },
  simpa [-neg_le_neg_iff]
    using neg_le_neg (hD.mul_sub_le_image_sub_of_le_deriv hf.neg hf'.neg hf'_ge x y hx hy hxy)
end

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f' ‚â§ C`, then `f` grows at most as fast
as `C * x`, i.e., `f y - f x ‚â§ C * (y - x)` whenever `x ‚â§ y`. -/
theorem image_sub_le_mul_sub_of_deriv_le {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f)
  {C} (le_hf' : ‚àÄ x, deriv f x ‚â§ C) ‚¶Éx y‚¶Ñ (hxy : x ‚â§ y) :
  f y - f x ‚â§ C * (y - x) :=
convex_univ.image_sub_le_mul_sub_of_deriv_le hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, le_hf' x) x y trivial trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is positive, then
`f` is a strictly monotonically increasing function on `D`. -/
theorem convex.strict_mono_of_deriv_pos {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  (hf'_pos : ‚àÄ x ‚àà interior D, 0 < deriv f x) :
  ‚àÄ x y ‚àà D, x < y ‚Üí f x < f y :=
by simpa only [zero_mul, sub_pos] using hD.mul_sub_lt_image_sub_of_lt_deriv hf hf' hf'_pos

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is positive, then
`f` is a strictly monotonically increasing function. -/
theorem strict_mono_of_deriv_pos {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f)
  (hf'_pos : ‚àÄ x, 0 < deriv f x) :
  strict_mono f :=
Œª x y hxy, convex_univ.strict_mono_of_deriv_pos hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, hf'_pos x) x y trivial trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonnegative, then
`f` is a monotonically increasing function on `D`. -/
theorem convex.mono_of_deriv_nonneg {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  (hf'_nonneg : ‚àÄ x ‚àà interior D, 0 ‚â§ deriv f x) :
  ‚àÄ x y ‚àà D, x ‚â§ y ‚Üí f x ‚â§ f y :=
by simpa only [zero_mul, sub_nonneg] using hD.mul_sub_le_image_sub_of_le_deriv hf hf' hf'_nonneg

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is nonnegative, then
`f` is a monotonically increasing function. -/
theorem mono_of_deriv_nonneg {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f) (hf' : ‚àÄ x, 0 ‚â§ deriv f x) :
  monotone f :=
Œª x y hxy, convex_univ.mono_of_deriv_nonneg hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, hf' x) x y trivial trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is negative, then
`f` is a strictly monotonically decreasing function on `D`. -/
theorem convex.strict_antimono_of_deriv_neg {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  (hf'_neg : ‚àÄ x ‚àà interior D, deriv f x < 0) :
  ‚àÄ x y ‚àà D, x < y ‚Üí f y < f x :=
by simpa only [zero_mul, sub_lt_zero] using hD.image_sub_lt_mul_sub_of_deriv_lt hf hf' hf'_neg

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is negative, then
`f` is a strictly monotonically decreasing function. -/
theorem strict_antimono_of_deriv_neg {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f)
  (hf' : ‚àÄ x, deriv f x < 0) :
  ‚àÄ ‚¶Éx y‚¶Ñ, x < y ‚Üí f y < f x :=
Œª x y hxy, convex_univ.strict_antimono_of_deriv_neg hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, hf' x) x y trivial trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonpositive, then
`f` is a monotonically decreasing function on `D`. -/
theorem convex.antimono_of_deriv_nonpos {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  (hf'_nonpos : ‚àÄ x ‚àà interior D, deriv f x ‚â§ 0) :
  ‚àÄ x y ‚àà D, x ‚â§ y ‚Üí f y ‚â§ f x :=
by simpa only [zero_mul, sub_nonpos] using hD.image_sub_le_mul_sub_of_deriv_le hf hf' hf'_nonpos

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is nonpositive, then
`f` is a monotonically decreasing function. -/
theorem antimono_of_deriv_nonpos {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f) (hf' : ‚àÄ x, deriv f x ‚â§ 0) :
  ‚àÄ ‚¶Éx y‚¶Ñ, x ‚â§ y ‚Üí f y ‚â§ f x :=
Œª x y hxy, convex_univ.antimono_of_deriv_nonpos hf.continuous.continuous_on hf.differentiable_on
  (Œª x _, hf' x) x y trivial trivial hxy

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is differentiable on its interior,
and `f'` is monotone on the interior, then `f` is convex on `D`. -/
theorem convex_on_of_deriv_mono {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  (hf'_mono : ‚àÄ x y ‚àà interior D, x ‚â§ y ‚Üí deriv f x ‚â§ deriv f y) :
  convex_on D f :=
convex_on_real_of_slope_mono_adjacent hD
begin
  intros x y z hx hz hxy hyz,
  -- First we prove some trivial inclusions
  have hxzD : Icc x z ‚äÜ D, from convex_real_iff.1 hD hx hz,
  have hxyD : Icc x y ‚äÜ D, from subset.trans (Icc_subset_Icc_right $ le_of_lt hyz) hxzD,
  have hxyD' : Ioo x y ‚äÜ interior D,
    from subset_sUnion_of_mem ‚ü®is_open_Ioo, subset.trans Ioo_subset_Icc_self hxyD‚ü©,
  have hyzD : Icc y z ‚äÜ D, from subset.trans (Icc_subset_Icc_left $ le_of_lt hxy) hxzD,
  have hyzD' : Ioo y z ‚äÜ interior D,
    from subset_sUnion_of_mem ‚ü®is_open_Ioo, subset.trans Ioo_subset_Icc_self hyzD‚ü©,
  -- Then we apply MVT to both `[x, y]` and `[y, z]`
  obtain ‚ü®a, ‚ü®hxa, hay‚ü©, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x),
    from exists_deriv_eq_slope f hxy (hf.mono hxyD) (hf'.mono hxyD'),
  obtain ‚ü®b, ‚ü®hyb, hbz‚ü©, hb‚ü© : ‚àÉ b ‚àà Ioo y z, deriv f b = (f z - f y) / (z - y),
    from exists_deriv_eq_slope f hyz (hf.mono hyzD) (hf'.mono hyzD'),
  rw [‚Üê ha, ‚Üê hb],
  exact hf'_mono a b (hxyD' ‚ü®hxa, hay‚ü©) (hyzD' ‚ü®hyb, hbz‚ü©) (le_of_lt $ lt_trans hay hyb)
end

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is differentiable on its interior,
and `f'` is monotone on the interior, then `f` is convex on `‚Ñù`. -/
theorem convex_on_univ_of_deriv_mono {f : ‚Ñù ‚Üí ‚Ñù} (hf : differentiable ‚Ñù f)
  (hf'_mono : monotone (deriv f)) : convex_on univ f :=
convex_on_of_deriv_mono convex_univ hf.continuous.continuous_on hf.differentiable_on
  (Œª x y _ _ h, hf'_mono h)

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is twice differentiable on its interior,
and `f''` is nonnegative on the interior, then `f` is convex on `D`. -/
theorem convex_on_of_deriv2_nonneg {D : set ‚Ñù} (hD : convex D) {f : ‚Ñù ‚Üí ‚Ñù}
  (hf : continuous_on f D) (hf' : differentiable_on ‚Ñù f (interior D))
  (hf'' : differentiable_on ‚Ñù (deriv f) (interior D))
  (hf''_nonneg : ‚àÄ x ‚àà interior D, 0 ‚â§ (deriv^[2] f x)) :
  convex_on D f :=
convex_on_of_deriv_mono hD hf hf' $
assume x y hx hy hxy,
hD.interior.mono_of_deriv_nonneg hf''.continuous_on (by rwa [interior_interior])
  (by rwa [interior_interior]) _ _ hx hy hxy

/-- If a function `f` is twice differentiable on `‚Ñù`, and `f''` is nonnegative on `‚Ñù`,
then `f` is convex on `‚Ñù`. -/
theorem convex_on_univ_of_deriv2_nonneg {f : ‚Ñù ‚Üí ‚Ñù} (hf' : differentiable ‚Ñù f)
  (hf'' : differentiable ‚Ñù (deriv f)) (hf''_nonneg : ‚àÄ x, 0 ‚â§ (deriv^[2] f x)) :
  convex_on univ f :=
convex_on_of_deriv2_nonneg convex_univ hf'.continuous.continuous_on hf'.differentiable_on
  hf''.differentiable_on (Œª x _, hf''_nonneg x)

/-! ### Functions `f : E ‚Üí ‚Ñù` -/

/-- Lagrange's Mean Value Theorem, applied to convex domains. -/
theorem domain_mvt
  {f : E ‚Üí ‚Ñù} {s : set E} {x y : E} {f' : E ‚Üí (E ‚ÜíL[‚Ñù] ‚Ñù)}
  (hf : ‚àÄ x ‚àà s, has_fderiv_within_at f (f' x) s x) (hs : convex s) (xs : x ‚àà s) (ys : y ‚àà s) :
  ‚àÉ z ‚àà segment x y, f y - f x = f' z (y - x) :=
begin
  have hIccIoo := @Ioo_subset_Icc_self ‚Ñù _ 0 1,
-- parametrize segment
  set g : ‚Ñù ‚Üí E := Œª t, x + t ‚Ä¢ (y - x),
  have hseg : ‚àÄ t ‚àà Icc (0:‚Ñù) 1, g t ‚àà segment x y,
  { rw segment_eq_image',
    simp only [mem_image, and_imp, add_right_inj],
    intros t ht, exact ‚ü®t, ht, rfl‚ü© },
  have hseg' : Icc 0 1 ‚äÜ g ‚Åª¬π' s,
  { rw ‚Üê image_subset_iff, unfold image, change ‚àÄ _, _,
    intros z Hz, rw mem_set_of_eq at Hz, rcases Hz with ‚ü®t, Ht, hgt‚ü©,
    rw ‚Üê hgt, exact hs.segment_subset xs ys (hseg t Ht) },
-- derivative of pullback of f under parametrization
  have hfg: ‚àÄ t ‚àà Icc (0:‚Ñù) 1, has_deriv_within_at (f ‚àò g)
    ((f' (g t) : E ‚Üí ‚Ñù) (y-x)) (Icc (0:‚Ñù) 1) t,
  { intros t Ht,
    have hg : has_deriv_at g (y-x) t,
    { have := ((has_deriv_at_id t).smul_const (y - x)).const_add x,
      rwa one_smul at this },
    exact (hf (g t) $ hseg' Ht).comp_has_deriv_within_at _ hg.has_deriv_within_at hseg' },
-- apply 1-variable mean value theorem to pullback
  have hMVT : ‚àÉ (t ‚àà Ioo (0:‚Ñù) 1), ((f' (g t) : E ‚Üí ‚Ñù) (y-x)) = (f (g 1) - f (g 0)) / (1 - 0),
  { refine exists_has_deriv_at_eq_slope (f ‚àò g) _ (by norm_num) _ _,
    { unfold continuous_on,
      exact Œª t Ht, (hfg t Ht).continuous_within_at },
    { refine Œª t Ht, (hfg t $ hIccIoo Ht).has_deriv_at _,
      refine mem_nhds_sets_iff.mpr _,
      use (Ioo (0:‚Ñù) 1),
      refine ‚ü®hIccIoo, _, Ht‚ü©,
      simp [real.Ioo_eq_ball, is_open_ball] } },
-- reinterpret on domain
  rcases hMVT with ‚ü®t, Ht, hMVT'‚ü©,
  use g t, refine ‚ü®hseg t $ hIccIoo Ht, _‚ü©,
  simp [g, hMVT'],
end

/-! ### Vector-valued functions `f : E ‚Üí F`.  Strict differentiability. -/

/-- Over the reals, a continuously differentiable function is strictly differentiable. -/
lemma strict_fderiv_of_cont_diff
  {f : E ‚Üí F} {s : set E}  {x : E} {f' : E ‚Üí (E ‚ÜíL[‚Ñù] F)}
  (hf : ‚àÄ x ‚àà s, has_fderiv_within_at f (f' x) s x) (hcont : continuous_on f' s) (hs : s ‚àà ùìù x) :
  has_strict_fderiv_at f (f' x) x :=
begin
-- turn little-o definition of strict_fderiv into an epsilon-delta statement
  apply is_o_iff_forall_is_O_with.mpr,
  intros c hc,
  refine is_O_with.of_bound (eventually_iff.mpr (mem_nhds_iff.mpr _)),
-- the correct Œµ is the modulus of continuity of f', shrunk to be inside s
  rcases (metric.continuous_on_iff.mp hcont x (mem_of_nhds hs) c hc) with ‚ü®Œµ‚ÇÅ, H‚ÇÅ, hcont'‚ü©,
  rcases (mem_nhds_iff.mp hs) with ‚ü®Œµ‚ÇÇ, H‚ÇÇ, hŒµ‚ÇÇ‚ü©,
  refine ‚ü®min Œµ‚ÇÅ Œµ‚ÇÇ, lt_min H‚ÇÅ H‚ÇÇ, _‚ü©,
-- mess with Œµ construction
  set t := ball x (min Œµ‚ÇÅ Œµ‚ÇÇ),
  have hts : t ‚äÜ s := Œª _ hy, hŒµ‚ÇÇ (ball_subset_ball (min_le_right Œµ‚ÇÅ Œµ‚ÇÇ) hy),
  have Hf : ‚àÄ y ‚àà t, has_fderiv_within_at f (f' y) t y :=
    Œª y yt, has_fderiv_within_at.mono (hf y (hts yt)) hts,
  have hconv := convex_ball x (min Œµ‚ÇÅ Œµ‚ÇÇ),
-- simplify formulas involving the product E √ó E
  rintros ‚ü®a, b‚ü© h,
  simp only [mem_set_of_eq, map_sub],
  have hab : a ‚àà t ‚àß b ‚àà t := by rwa [mem_ball, prod.dist_eq, max_lt_iff] at h,
-- exploit the choice of Œµ as the modulus of continuity of f'
  have hf' : ‚àÄ x' ‚àà t, ‚à•f' x' - f' x‚à• ‚â§ c,
  { intros x' H',
    refine le_of_lt (hcont' x' (hts H') _),
    exact ball_subset_ball (min_le_left Œµ‚ÇÅ Œµ‚ÇÇ) H' },
-- apply mean value theorem
  simpa using convex.norm_image_sub_le_of_norm_has_fderiv_within_le' Hf hf' hconv hab.2 hab.1,
end
