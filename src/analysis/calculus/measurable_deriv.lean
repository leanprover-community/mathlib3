/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury Kudryashov
-/
import analysis.calculus.deriv
import measure_theory.borel_space

/-!
# Derivative is measurable

In this file we prove that the derivative of any function with complete codomain is a measurable
function. Namely, we prove:

* `is_measurable_set_of_differentiable_at`: the set `{x | differentiable_at ğ•œ f x}` is measurable;
* `measurable_fderiv`: the function `fderiv ğ•œ f` is measurable;
* `measurable_fderiv_apply`: for a fixed vector `y`, the function `Î» x, fderiv ğ•œ f x y`
  is measurable;
* `measurable_deriv`: the function `deriv f` is measurable.

## Tags

derivative, measurable function, Borel Ïƒ-algebra
-/

noncomputable theory

open topological_space (second_countable_topology) set asymptotics filter
open_locale topological_space filter

namespace continuous_linear_map

variables {ğ•œ E F : Type*} [nondiscrete_normed_field ğ•œ]
  [normed_group E] [normed_space ğ•œ E] [normed_group F] [normed_space ğ•œ F]

instance : measurable_space (E â†’L[ğ•œ] F) := borel _

instance : borel_space (E â†’L[ğ•œ] F) := âŸ¨rflâŸ©

lemma measurable_apply [measurable_space F] [borel_space F] (x : E) :
  measurable (Î» f : E â†’L[ğ•œ] F, f x) :=
(apply ğ•œ F x).continuous.measurable

lemma measurable_apply' [measurable_space E] [opens_measurable_space E]
  [measurable_space F] [borel_space F] :
  measurable (Î» (x : E) (f : E â†’L[ğ•œ] F), f x) :=
measurable_pi_lambda _ $ Î» f, f.measurable

lemma measurable_applyâ‚‚ [measurable_space E] [opens_measurable_space E]
  [second_countable_topology E] [second_countable_topology (E â†’L[ğ•œ] F)]
  [measurable_space F] [borel_space F] :
  measurable (Î» p : (E â†’L[ğ•œ] F) Ã— E, p.1 p.2) :=
is_bounded_bilinear_map_apply.continuous.measurable

lemma measurable_coe [measurable_space F] [borel_space F] :
  measurable (Î» (f : E â†’L[ğ•œ] F) (x : E), f x) :=
measurable_pi_lambda _ measurable_apply

end continuous_linear_map

variables {ğ•œ E F : Type*} [nondiscrete_normed_field ğ•œ] [normed_group E] [normed_space ğ•œ E]
  [normed_group F] [normed_space ğ•œ F] {f : E â†’ F} {f' g' : E â†’L[ğ•œ] F} {x : E} {r R Îµ : â„}

/-- A function `f` has an approximate derivative `f'` in the shell `r â‰¤ dist y x < R` with accuracy
`Îµ` if for each `y` in this shell we have `âˆ¥f y - f x - f' (y - x)âˆ¥ â‰¤ Îµ * âˆ¥y - xâˆ¥`.

This is an auxiliary definition for the proof of measurability of `fderiv ğ•œ f`. -/
def has_approx_fderiv_at_in_shell (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) (r R Îµ : â„) :=
âˆ€ y, r â‰¤ dist y x â†’ dist y x < R â†’ âˆ¥f y - f x - f' (y - x)âˆ¥ â‰¤ Îµ * âˆ¥y - xâˆ¥

lemma has_fderiv_at.has_approx_fderiv_at_in_shell (h : has_fderiv_at f f' x) (hÎµ : 0 < Îµ) :
  âˆƒ Râ‚€ > 0, âˆ€ R < Râ‚€, âˆ€ r, has_approx_fderiv_at_in_shell f f' x r R Îµ :=
begin
  rcases metric.eventually_nhds_iff.1 (is_o_iff.1 h hÎµ) with âŸ¨Râ‚€, Râ‚€_pos, hRâ‚€âŸ©,
  use [Râ‚€, Râ‚€_pos],
  rintros R hR r y hyr hy,
  exact hRâ‚€ (hy.trans hR)
end

lemma has_approx_fderiv_at_in_shell.mono (hf : has_approx_fderiv_at_in_shell f f' x r R Îµ)
  {r' R' Îµ' : â„} (hr : r â‰¤ r') (hR : R' â‰¤ R) (hÎµ : Îµ â‰¤ Îµ') :
  has_approx_fderiv_at_in_shell f f' x r' R' Îµ' :=
Î» y hâ‚ hâ‚‚, (hf y (hr.trans hâ‚) (hâ‚‚.trans_le hR)).trans $
  mul_le_mul_of_nonneg_right hÎµ (norm_nonneg _)

lemma has_approx_fderiv_at_in_shell.mono_pow {a : ğ•œ} (ha : âˆ¥aâˆ¥ < 1) {m n m' n' : â„•}
  (hf : has_approx_fderiv_at_in_shell f f' x (âˆ¥aâˆ¥^m) (âˆ¥aâˆ¥^n) Îµ) (hm : m' â‰¤ m) (hn : n â‰¤ n') :
  has_approx_fderiv_at_in_shell f f' x (âˆ¥aâˆ¥^m') (âˆ¥aâˆ¥^n') Îµ :=
hf.mono (pow_le_pow_of_le_one (norm_nonneg a) ha.le hm)
  (pow_le_pow_of_le_one (norm_nonneg a) ha.le hn) le_rfl

lemma has_approx_fderiv_at_in_shell.dist_le (hf : has_approx_fderiv_at_in_shell f f' x r R Îµ)
  {Îµ'} (hg : has_approx_fderiv_at_in_shell f g' x r R Îµ') (hR : 0 < R) (hÎµ : 0 â‰¤ Îµ) (hÎµ' : 0 â‰¤ Îµ')
  {a : ğ•œ} (haâ‚ : âˆ¥aâˆ¥ < 1) (hca : r â‰¤ âˆ¥aâˆ¥ * R) :
  dist f' g' â‰¤ Îµ + Îµ' :=
begin
  rw [dist_eq_norm],
  refine continuous_linear_map.op_norm_le_of_shell' hR (add_nonneg hÎµ hÎµ') haâ‚ _,
  intros y hay hy,
  have hâ‚ : dist (x + y) x < R, by simpa [dist_eq_norm],
  have hâ‚‚ : r â‰¤ dist (x + y) x,
    calc r â‰¤ âˆ¥aâˆ¥ * R : hca
    ... â‰¤ dist (x + y) x : by rwa [dist_eq_norm, add_sub_cancel', mul_comm],
  calc âˆ¥f' y - g' yâˆ¥ = âˆ¥(f (x + y) - f x - g' (x + y - x)) - (f (x + y) - f x - f' (x + y - x))âˆ¥ :
    by simp
  ... â‰¤ _ : norm_sub_le _ _
  ... â‰¤ Îµ' * âˆ¥x + y - xâˆ¥ + Îµ * âˆ¥x + y - xâˆ¥ : add_le_add (hg _ hâ‚‚ hâ‚) (hf _ hâ‚‚ hâ‚)
  ... = (Îµ + Îµ') * âˆ¥yâˆ¥ : by rw [add_sub_cancel', add_mul, add_comm]
end

lemma has_approx_fderiv_at_in_shell.dist_le' {a : ğ•œ} (ha : âˆ¥aâˆ¥ < 1) (haâ‚€ : a â‰  0)
  {m n m' n' k k': â„•}
  (hf : has_approx_fderiv_at_in_shell f f' x (âˆ¥aâˆ¥^m) (âˆ¥aâˆ¥^n) (1 / 2 ^ k))
  (hg : has_approx_fderiv_at_in_shell f g' x (âˆ¥aâˆ¥^m') (âˆ¥aâˆ¥^n') (1 / 2 ^ k'))
  (h : max n n' < min m m') :
  dist f' g' â‰¤ 1 / 2 ^ k + 1 / 2 ^ k' :=
(hf.mono_pow ha (min_le_left _ _) (le_max_left _ _)).dist_le
  (hg.mono_pow ha (min_le_right _ _) (le_max_right _ _)) (pow_pos (norm_pos_iff.2 haâ‚€) _)
  (div_nonneg zero_le_one (pow_nonneg zero_le_two _))
  (div_nonneg zero_le_one (pow_nonneg zero_le_two _)) ha $
  by { rw â† pow_succ, exact pow_le_pow_of_le_one (norm_nonneg a) ha.le h }

lemma has_fderiv_at_of_forall_shell {a : ğ•œ} (ha : âˆ¥aâˆ¥ < 1) (hâ‚€ : a â‰  0)
  {t : set (E â†’L[ğ•œ] F)} (ht : is_complete t)
  (hf : âˆ€ k : â„•, âˆƒ n : â„•, âˆ€ m : â„•,
    âˆƒ f' âˆˆ t, has_approx_fderiv_at_in_shell f f' x (âˆ¥aâˆ¥^m) (âˆ¥aâˆ¥^n) (1 / 2 ^ k)) :
  âˆƒ f' âˆˆ t, has_fderiv_at f f' x :=
begin
  choose! n f' hf't H using hf,
  /- First we prove estimates on the distances between the approximate derivatives. -/
  have Hâ‚ : âˆ€ â¦ƒK k k'â¦„, K + 3 â‰¤ k â†’ K + 3 â‰¤ k' â†’ âˆ€ m m',
    dist (f' k (m + n k + 1)) (f' k' (m' + n k' + 1)) < (1 / 2) ^ K,
  { intro K,
    have : âˆ€ â¦ƒk : â„•â¦„, K + 3 â‰¤ k â†’ (1 / 2 ^ k : â„) â‰¤ (1 / 2) ^ K / 8,
    { intros k h,
      rw [div_pow, one_pow, div_div_eq_div_mul],
      refine iff.mpr (one_div_le_one_div (pow_pos zero_lt_two _) (by norm_num)) _,
      have : (2 ^ 3 : â„) = 8, by norm_num,
      rw [â† this, â† pow_add],
      exact pow_le_pow one_le_two h },
    set Îµ := (1 / 2 : â„) ^ K,
    have Îµ0 : 0 < Îµ := pow_pos one_half_pos _,
    have : âˆ€ â¦ƒk k' m m'â¦„, K + 3 â‰¤ k â†’ K + 3 â‰¤ k' â†’ max (n k) (n k') < min m m' â†’
      dist (f' k m) (f' k' m') â‰¤ Îµ / 4,
    { intros k k' m m' hk hk' h,
      calc dist (f' k m) (f' k' m') â‰¤ 1 / 2 ^ k + 1 / 2 ^ k' :
        (H _ _).dist_le' ha hâ‚€ (H _ _) h
      ... â‰¤ Îµ / 8 + Îµ / 8 : add_le_add (this hk) (this hk')
      ... = _ : _,
      rw [â† add_div, â† two_mul, bit0_eq_two_mul (4:â„), mul_div_mul_left],
      exact two_ne_zero },
    intros k k' hk hk' m m',
    have A : dist (f' k (m + n k + 1)) (f' k (n k + n k' + 1)) â‰¤ Îµ / 4 :=
      this hk hk (by { simp only [max_self, lt_min_iff], omega }),
    have B : dist (f' k (n k + n k' + 1)) (f' k' (n k + n k' + 1)) â‰¤ Îµ / 4 :=
      this hk hk' (by { simp only [min_self, max_lt_iff], omega }),
    have C : dist (f' k' (n k + n k' + 1)) (f' k' (m' + n k' + 1)) â‰¤ Îµ / 4 :=
      this hk' hk' (by simp only [max_self, lt_min_iff]; omega),
    calc dist (f' k (m + n k + 1)) (f' k' (m' + n k' + 1)) â‰¤ _ + _ + _ :
      dist_triangle4 _ _ _ _
    ... â‰¤ Îµ / 4 + Îµ / 4 + Îµ / 4 :
      add_le_add (add_le_add A B) C
    ... < _ : _,
    rw [â† add_div, â† add_div, div_lt_iff],
    linarith,
    norm_num },
  /- These estimates imply that `Î» p, f' p.1 (p.2 + n p.1 + 1)` is a Cauchy sequence.
  We add `n p.1 + 1` to the second argument to ensure that the inner radius is less
  than the outer radius. -/
  have Hâ‚‚ : cauchy_seq (Î» p : â„• Ã— â„•, f' p.1 (p.2 + n p.1 + 1)) :=
    (uniformity_basis_dist_pow_of_lt_1 one_half_pos one_half_lt_one).cauchy_seq_iff.2
      (Î» K _, âŸ¨âŸ¨K + 3, 0âŸ©, Î» p p' hp hp', Hâ‚ hp.1 hp'.1 _ _âŸ©),
  /- Now take the limit `F'` of this sequence. -/
  rcases cauchy_seq_tendsto_of_is_complete ht _ Hâ‚‚ with âŸ¨F', hF't, hF'âŸ©,
  show âˆ€ p : â„• Ã— â„•, f' _ _ âˆˆ t, from Î» p, hf't _ _,
  /- This limit will be the derivative we are looking for. -/
  use [F', hF't],
  /- The estimate on the distances between `f' k m` imply an estimate on the distance
  between `f' (k + 3) m` and `F'`. -/
  have Hâ‚ƒ : âˆ€ k m, dist (f' (k + 3) (m + n (k + 3) + 1)) F' â‰¤ (1 / 2) ^ k :=
    Î» k m, le_of_tendsto (tendsto_const_nhds.dist hF') $
      eventually_at_top.2 âŸ¨(k + 3, 0), Î» p hp, (Hâ‚ le_rfl hp.1 m p.2).leâŸ©,
  /- Take `Îµ > 0`. -/
  refine is_o_iff.2 (Î» Îµ Îµ0, _),
  /- Without loss of generality, `Îµ = (1 / 2) ^ K`, `K : â„•`. -/
  rcases ((tendsto_pow_at_top_nhds_0_of_lt_1 one_half_pos.le one_half_lt_one).eventually
    (gt_mem_nhds Îµ0)).exists with âŸ¨K, hKâŸ©,
  suffices : âˆ€á¶  y in ğ“ x, âˆ¥f y - f x - F' (y - x)âˆ¥ â‰¤ (1 / 2) ^ K * âˆ¥y - xâˆ¥,
    from this.mono (Î» y hy, hy.trans (mul_le_mul_of_nonneg_right hK.le (norm_nonneg _))),
  clear hK Îµ0 Îµ,
  have hâ‚€' : 0 < âˆ¥aâˆ¥ := norm_pos_iff.2 hâ‚€,
  /- The estimate will hold in the ball of radius `âˆ¥aâˆ¥ ^ n (K + 4)` around `x`. -/
  refine metric.eventually_nhds_iff.2 âŸ¨âˆ¥aâˆ¥ ^ n (K + 1 + 3), pow_pos hâ‚€' _, Î» y hy, _âŸ©,
  /- Take a point `y` in this ball. If `y = x`, then the inequality is trivial. -/
  by_cases hyx : y = x, { simp [hyx] },
  /- Otherwise there exists `m` such that `âˆ¥aâˆ¥^m < dist y x < âˆ¥aâˆ¥ ^ n (K + 4)`. -/
  replace hyx : 0 < dist y x := dist_pos.2 hyx,
  rcases ((tendsto_pow_at_top_nhds_0_of_lt_1 hâ‚€'.le ha).eventually (gt_mem_nhds hyx)).exists
    with âŸ¨m, hmâŸ©,
  have Hâ‚„ := (H (K + 1 + 3) (m + n (K + 1 + 3) + 1)).mono_pow ha
    (le_add_right (le_add_right le_rfl)) le_rfl y hm.le hy,
  have Hâ‚… := Hâ‚ƒ (K + 1) m,
  rw [dist_eq_norm] at Hâ‚…,
  replace Hâ‚… := continuous_linear_map.le_of_op_norm_le _ Hâ‚… (y - x),
  rw [continuous_linear_map.sub_apply] at Hâ‚…,
  rw [â† dist_eq_norm] at Hâ‚„ Hâ‚… âŠ¢,
  refine (dist_triangle _ _ _).trans ((add_le_add Hâ‚„ Hâ‚…).trans _),
  rw [â† add_mul, â† one_div_pow, pow_add, pow_add, mul_assoc, â† mul_add],
  refine mul_le_mul_of_nonneg_right _ (norm_nonneg _),
  refine mul_le_of_le_one_right (pow_nonneg _ _) _; norm_num
end

lemma set_of_has_fderiv_at_mem_set_eq {a : ğ•œ} (ha : âˆ¥aâˆ¥ < 1) (hâ‚€ : a â‰  0)
  {t : set (E â†’L[ğ•œ] F)} (ht : is_complete t) :
  {x | âˆƒ f' âˆˆ t, has_fderiv_at f f' x} =
    â‹‚ k : â„•, â‹ƒ n : â„•, â‹‚ m : â„•, {x | continuous_at f x} âˆ©
      â‹ƒ (r : â„š) (hr : (0 : â„) < r âˆ§ â†‘r < âˆ¥aâˆ¥^m) (R : â„š) (hR : âˆ¥aâˆ¥^n < R)
          (Îµ : â„š) (hÎµ : (0 : â„) < Îµ âˆ§ (Îµ : â„) < 1 / 2 ^ k),
        {x | âˆƒ f' âˆˆ t, has_approx_fderiv_at_in_shell f f' x r R Îµ} :=
begin
  have hâ‚€' : 0 < âˆ¥aâˆ¥ := norm_pos_iff.2 hâ‚€,
  ext x : 1,
  simp only [mem_set_of_eq, mem_Inter, mem_Union, mem_inter_iff],
  split,
  { rintros âŸ¨f', hf't, hf'âŸ© k,
    rcases exists_rat_btwn (one_div_pos.2 (pow_pos (@zero_lt_two â„ _ _) k)) with âŸ¨Îµ, hÎµâŸ©,
    rcases hf'.has_approx_fderiv_at_in_shell hÎµ.1 with âŸ¨Râ‚€, hRâ‚€âŸ©,
    rcases exists_pow_lt_of_lt_1 hRâ‚€.fst (norm_nonneg a) ha with âŸ¨n, hnâŸ©,
    rcases exists_rat_btwn hn with âŸ¨R, hRâŸ©,
    refine âŸ¨n, Î» m, _âŸ©,
    rcases exists_rat_btwn (pow_pos hâ‚€' m) with âŸ¨r, hrâŸ©,
    exact âŸ¨hf'.continuous_at, r, hr, R, hR.1, Îµ, hÎµ, f', hf't, hRâ‚€.snd _ hR.2 _âŸ© },
  { intro H,
    choose n hc r hr R hR Îµ hÎµ f' Hf't Hf' using H,
    refine has_fderiv_at_of_forall_shell ha hâ‚€ ht (Î» k, âŸ¨n k, Î» m, âŸ¨f' k m, Hf't _ _, _âŸ©âŸ©),
    exact (Hf' k m).mono (hr _ _).2.le (hR _ _).le (hÎµ _ _).2.le }
end

lemma fderiv_mem_iff {f : E â†’ F} {s : set (E â†’L[ğ•œ] F)} {x : E} :
  fderiv ğ•œ f x âˆˆ s â†” (âˆƒ f' âˆˆ s, has_fderiv_at f f' x) âˆ¨
    (0 : E â†’L[ğ•œ] F) âˆˆ s âˆ§ Â¬differentiable_at ğ•œ f x :=
begin
  split,
  { intro hfx,
    by_cases hx : differentiable_at ğ•œ f x,
    { exact or.inl âŸ¨fderiv ğ•œ f x, hfx, hx.has_fderiv_atâŸ© },
    { rw [fderiv_zero_of_not_differentiable_at hx] at hfx,
      exact or.inr âŸ¨hfx, hxâŸ© } },
  { rintro (âŸ¨f', hf's, hf'âŸ©|âŸ¨hâ‚€, hxâŸ©),
    { rwa hf'.fderiv },
    { rwa [fderiv_zero_of_not_differentiable_at hx] } }
end

variables [measurable_space E] [opens_measurable_space E] (ğ•œ f)

lemma is_measurable_set_of_has_fderiv_at_mem_set {t : set (E â†’L[ğ•œ] F)} (ht : is_complete t) :
  is_measurable {x : E | âˆƒ f' âˆˆ t, has_fderiv_at f f' x} :=
begin
  rcases normed_field.exists_norm_lt_one ğ•œ with âŸ¨a, hâ‚€', haâŸ©,
  have hâ‚€ : a â‰  0 := norm_pos_iff.1 hâ‚€',
  rw [set_of_has_fderiv_at_mem_set_eq ha hâ‚€ ht],
  refine is_measurable.Inter (Î» k, is_measurable.Union $ Î» n, is_measurable.Inter $
    Î» m, _),
  rw [â† inter_interior_eq_of_forall_mem_nhds],
  show is_measurable _,
    from (is_measurable_set_of_continuous_at _).inter is_open_interior.is_measurable,
  simp only [mem_set_of_eq, mem_inter_eq, mem_Union, â† set_of_exists],
  rintros x âŸ¨hfc, r, hr, R, hR, Îµ, hÎµ, f', hf't, hf'âŸ©,
  rcases exists_rat_btwn hr.2 with âŸ¨r', hr'âŸ©,
  rcases exists_rat_btwn hR with âŸ¨R', hR'âŸ©,
  rcases exists_rat_btwn hÎµ.2 with âŸ¨Îµ', hÎµ'âŸ©,
  suffices : âˆ€á¶  x' in ğ“ x, has_approx_fderiv_at_in_shell f f' x' r' R' Îµ',
  { exact this.mono (Î» x' hx', âŸ¨r', âŸ¨hr.1.trans hr'.1, hr'.2âŸ©, R', hR'.1,
      Îµ', âŸ¨hÎµ.1.trans hÎµ'.1, hÎµ'.2âŸ©, f', hf't, hx'âŸ©) },
  have hr'_subset : âˆ€á¶  x' in ğ“ x, âˆ€ y, â†‘r' â‰¤ dist y x' â†’ â†‘r â‰¤ dist y x,
  { refine metric.eventually_nhds_iff.2 âŸ¨r' - r, sub_pos.2 hr'.1, Î» x' hx' y hy, _âŸ©,
    calc (r : â„) = r' - (r' - r) : (sub_sub_cancel _ _).symm
    ... â‰¤ dist y x' - dist x' x : sub_le_sub hy hx'.le
    ... â‰¤ _ : sub_le_iff_le_add.2 (dist_triangle_right _ _ _) },
  have hR'_subset : âˆ€á¶  x' in ğ“ x, âˆ€ y, dist y x' < R' â†’ dist y x < R :=
    metric.eventually_nhds_iff.2 âŸ¨R - R', sub_pos.2 hR'.2, Î» x' hx' y hy,
      metric.ball_subset hx'.le hyâŸ©,
  rcases exists_between hÎµ'.1 with âŸ¨Îµ'', hÎµÎµ'', hÎµ''Îµ'âŸ©,
  have hÎµ''0 : 0 < Îµ'' := hÎµ.1.trans hÎµÎµ'',
  suffices : âˆ€á¶  x' in ğ“ x, âˆ€ y, â†‘r' â‰¤ dist y x' â†’ dist y x' < R' â†’
    âˆ¥f y - f x' - f' (y - x')âˆ¥ â‰¤ Îµ'' * âˆ¥y - xâˆ¥,
  { have H : 0 < (â†‘Îµ' - Îµ'') * r' / Îµ'' :=
      div_pos (mul_pos (sub_pos.2 hÎµ''Îµ') (hr.1.trans hr'.1)) hÎµ''0,
    filter_upwards [this, metric.ball_mem_nhds _ H],
    intros x' Hâ‚ Hâ‚‚ y hyr hyR,
    calc _ â‰¤ Îµ'' * âˆ¥y - xâˆ¥ : Hâ‚ y hyr hyR
    ... = Îµ'' * dist y x : by rw dist_eq_norm
    ... â‰¤ Îµ'' * (dist y x' + dist x' x) :
      mul_le_mul_of_nonneg_left (dist_triangle _ _ _) hÎµ''0.le
    ... â‰¤ â†‘Îµ' * dist y x' : _
    ... = â†‘Îµ' * âˆ¥y - x'âˆ¥ : by rw dist_eq_norm,
    rw [mul_add, â† le_sub_iff_add_le', â† sub_mul, â† le_div_iff' hÎµ''0],
    refine (le_of_lt $ metric.mem_ball.1 Hâ‚‚).trans _,
    exact (div_le_div_right hÎµ''0).2 (mul_le_mul_of_nonneg_left hyr (sub_nonneg.2 hÎµ''Îµ'.le)) },
  clear hÎµ''Îµ' hÎµ' Îµ',
  have : continuous_at (Î» x', f x' - f' x') x := hfc.sub f'.continuous.continuous_at,
  rw [continuous_at, tendsto_iff_norm_tendsto_zero] at this,
  filter_upwards [hr'_subset, hR'_subset,
    this.eventually (gt_mem_nhds $ mul_pos (sub_pos.2 hÎµÎµ'') hr.1)],
  intros x' hr_impl hR_impl hlt y hyr' hyR,
  have hyr : â†‘r â‰¤ dist y x := hr_impl _ hyr',
  calc âˆ¥f y - f x' - f' (y - x')âˆ¥ = âˆ¥(f y - f x - f' (y - x)) - (f x' - f' x' - (f x - f' x))âˆ¥ :
    by { simp only [f'.map_sub], congr' 1, abel }
  ... â‰¤ âˆ¥f y - f x - f' (y - x)âˆ¥ + âˆ¥f x' - f' x' - (f x - f' x)âˆ¥ : norm_sub_le _ _
  ... â‰¤ â†‘Îµ * âˆ¥y - xâˆ¥ + (Îµ'' - Îµ) * r : add_le_add (hf' y hyr (hR_impl _ hyR)) hlt.le
  ... â‰¤ Îµ'' * âˆ¥y - xâˆ¥ : _,
  rw [â† le_sub_iff_add_le', â† sub_mul],
  exact mul_le_mul_of_nonneg_left (by rwa â† dist_eq_norm) (sub_nonneg.2 hÎµÎµ''.le)
end

variable [complete_space F]

lemma is_measurable_set_of_differentiable_at : is_measurable {x : E | differentiable_at ğ•œ f x} :=
by simpa [differentiable_at] using is_measurable_set_of_has_fderiv_at_mem_set ğ•œ f complete_univ

lemma measurable_fderiv : measurable (fderiv ğ•œ f) :=
begin
  refine measurable_of_is_closed (Î» s hs, _),
  have : fderiv ğ•œ f â»Â¹' s = {x | âˆƒ f' âˆˆ s, has_fderiv_at f f' x} âˆª
    {x | (0 : E â†’L[ğ•œ] F) âˆˆ s} âˆ© {x | Â¬differentiable_at ğ•œ f x} :=
    set.ext (Î» x, mem_preimage.trans fderiv_mem_iff),
  rw this,
  exact (is_measurable_set_of_has_fderiv_at_mem_set _ _ hs.is_complete).union
    ((is_measurable.const _).inter (is_measurable_set_of_differentiable_at _ _).compl)
end

lemma measurable_fderiv_apply_const [measurable_space F] [borel_space F] (y : E) :
  measurable (Î» x, fderiv ğ•œ f x y) :=
(continuous_linear_map.measurable_apply y).comp (measurable_fderiv ğ•œ f)

variable {ğ•œ}

lemma measurable_deriv [measurable_space ğ•œ] [opens_measurable_space ğ•œ] [measurable_space F]
  [borel_space F] (f : ğ•œ â†’ F) : measurable (deriv f) :=
by simpa only [fderiv_deriv] using measurable_fderiv_apply_const ğ•œ f 1
