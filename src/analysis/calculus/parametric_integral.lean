/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import measure_theory.integral.set_integral
import analysis.calculus.mean_value

/-!
# Derivatives of integrals depending on parameters

A parametric integral is a function with shape `f = Î» x : H, âˆ« a : Î±, F x a âˆ‚Î¼` for some
`F : H â†’ Î± â†’ E`, where `H` and `E` are normed spaces and `Î±` is a measured space with measure `Î¼`.

We already know from `continuous_of_dominated` in `measure_theory.integral.bochner` how to
guarantee that `f` is continuous using the dominated convergence theorem. In this file,
we want to express the derivative of `f` as the integral of the derivative of `F` with respect
to `x`.


## Main results

As explained above, all results express the derivative of a parametric integral as the integral of
a derivative. The variations come from the assumptions and from the different ways of expressing
derivative, especially FrÃ©chet derivatives vs elementary derivative of function of one real
variable.

* `has_fderiv_at_integral_of_dominated_loc_of_lip`: this version assumes that
  - `F x` is ae-measurable for x near `xâ‚€`,
  - `F xâ‚€` is integrable,
  - `Î» x, F x a` has derivative `F' a : H â†’L[â„] E` at `xâ‚€` which is ae-measurable,
  - `Î» x, F x a` is locally Lipschitz near `xâ‚€` for almost every `a`, with a Lipschitz bound which
    is integrable with respect to `a`.

  A subtle point is that the "near xâ‚€" in the last condition has to be uniform in `a`. This is
  controlled by a positive number `Îµ`.

* `has_fderiv_at_integral_of_dominated_of_fderiv_le`: this version assume `Î» x, F x a` has
   derivative `F' x a` for `x` near `xâ‚€` and `F' x` is bounded by an integrable function independent
   from `x` near `xâ‚€`.


`has_deriv_at_integral_of_dominated_loc_of_lip` and
`has_deriv_at_integral_of_dominated_loc_of_deriv_le` are versions of the above two results that
assume `H = â„` or `H = â„‚` and use the high-school derivative `deriv` instead of FrÃ©chet derivative
`fderiv`.

We also provide versions of these theorems for set integrals.

## Tags
integral, derivative
-/

noncomputable theory

open topological_space measure_theory filter metric
open_locale topology filter

variables {Î± : Type*} [measurable_space Î±] {Î¼ : measure Î±} {ğ•œ : Type*} [is_R_or_C ğ•œ]
          {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [normed_space ğ•œ E]
          [complete_space E]
          {H : Type*} [normed_add_comm_group H] [normed_space ğ•œ H]

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming `F xâ‚€` is
integrable, `â€–F x a - F xâ‚€ aâ€– â‰¤ bound a * â€–x - xâ‚€â€–` for `x` in a ball around `xâ‚€` for ae `a` with
integrable Lipschitz bound `bound` (with a ball radius independent of `a`), and `F x` is
ae-measurable for `x` in the same ball. See `has_fderiv_at_integral_of_dominated_loc_of_lip` for a
slightly less general but usually more useful version. -/
lemma has_fderiv_at_integral_of_dominated_loc_of_lip' {F : H â†’ Î± â†’ E} {F' : Î± â†’ (H â†’L[ğ•œ] E)}
  {xâ‚€ : H} {bound : Î± â†’ â„}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€ x âˆˆ ball xâ‚€ Îµ, ae_strongly_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_strongly_measurable F' Î¼)
  (h_lipsch : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F x a - F xâ‚€ aâ€– â‰¤ bound a * â€–x - xâ‚€â€–)
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, has_fderiv_at (Î» x, F x a) (F' a) xâ‚€) :
  integrable F' Î¼ âˆ§ has_fderiv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ :=
begin
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos,
  have nneg : âˆ€ x, 0 â‰¤ â€–x - xâ‚€â€–â»Â¹ := Î» x, inv_nonneg.mpr (norm_nonneg _) ,
  set b : Î± â†’ â„ := Î» a, |bound a|,
  have b_int : integrable b Î¼ := bound_integrable.norm,
  have b_nonneg : âˆ€ a, 0 â‰¤ b a := Î» a, abs_nonneg _,
  replace h_lipsch : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F x a - F xâ‚€ aâ€– â‰¤ b a * â€–x - xâ‚€â€–,
    from h_lipsch.mono (Î» a ha x hx, (ha x hx).trans $
      mul_le_mul_of_nonneg_right (le_abs_self _) (norm_nonneg _)),
  have hF_int' : âˆ€ x âˆˆ ball xâ‚€ Îµ, integrable (F x) Î¼,
  { intros x x_in,
    have : âˆ€áµ a âˆ‚Î¼, â€–F xâ‚€ a - F x aâ€– â‰¤ Îµ * b a,
    { simp only [norm_sub_rev (F xâ‚€ _)],
      refine h_lipsch.mono (Î» a ha, (ha x x_in).trans _),
      rw mul_comm Îµ,
      rw [mem_ball, dist_eq_norm] at x_in,
      exact mul_le_mul_of_nonneg_left x_in.le (b_nonneg _) },
    exact integrable_of_norm_sub_le (hF_meas x x_in) hF_int
      (integrable.const_mul bound_integrable.norm Îµ) this },
  have hF'_int : integrable F' Î¼,
  { have : âˆ€áµ a âˆ‚Î¼, â€–F' aâ€– â‰¤ b a,
    { apply (h_diff.and h_lipsch).mono,
      rintros a âŸ¨ha_diff, ha_lipâŸ©,
      refine ha_diff.le_of_lip' (b_nonneg a) (mem_of_superset (ball_mem_nhds _ Îµ_pos) $ ha_lip) },
    exact b_int.mono' hF'_meas this },
  refine âŸ¨hF'_int, _âŸ©,
  have h_ball: ball xâ‚€ Îµ âˆˆ ğ“ xâ‚€ := ball_mem_nhds xâ‚€ Îµ_pos,
  have : âˆ€á¶  x in ğ“ xâ‚€,
      â€–x - xâ‚€â€–â»Â¹ * â€–âˆ« a, F x a âˆ‚Î¼ - âˆ« a, F xâ‚€ a âˆ‚Î¼ - (âˆ« a, F' a âˆ‚Î¼) (x - xâ‚€)â€– =
       â€–âˆ« a, â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a  - F' a (x - xâ‚€)) âˆ‚Î¼â€–,
  { apply mem_of_superset (ball_mem_nhds _ Îµ_pos),
    intros x x_in,
    rw [set.mem_set_of_eq, â† norm_smul_of_nonneg (nneg _), integral_smul,
        integral_sub, integral_sub, â† continuous_linear_map.integral_apply hF'_int],
    exacts [hF_int' x x_in, hF_int, (hF_int' x x_in).sub hF_int,
            hF'_int.apply_continuous_linear_map _] },
  rw [has_fderiv_at_iff_tendsto, tendsto_congr' this, â† tendsto_zero_iff_norm_tendsto_zero,
      â† show âˆ« (a : Î±), â€–xâ‚€ - xâ‚€â€–â»Â¹ â€¢ (F xâ‚€ a - F xâ‚€ a - (F' a) (xâ‚€ - xâ‚€)) âˆ‚Î¼ = 0, by simp],
  apply tendsto_integral_filter_of_dominated_convergence,
  { filter_upwards [h_ball] with _ x_in,
    apply ae_strongly_measurable.const_smul,
    exact ((hF_meas _ x_in).sub (hF_meas _ xâ‚€_in)).sub (hF'_meas.apply_continuous_linear_map _) },
  { apply mem_of_superset h_ball,
    intros x hx,
    apply (h_diff.and h_lipsch).mono,
    rintros a âŸ¨ha_deriv, ha_boundâŸ©,
    show â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))â€– â‰¤ b a + â€–F' aâ€–,
    replace ha_bound : â€–F x a - F xâ‚€ aâ€– â‰¤ b a * â€–x - xâ‚€â€– := ha_bound x hx,
    calc â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))â€–
    = â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a) - â€–x - xâ‚€â€–â»Â¹ â€¢ F' a (x - xâ‚€)â€– : by rw smul_sub
    ... â‰¤  â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a)â€– + â€–â€–x - xâ‚€â€–â»Â¹ â€¢ F' a (x - xâ‚€)â€– : norm_sub_le _ _
    ... =  â€–x - xâ‚€â€–â»Â¹ * â€–F x a - F xâ‚€ aâ€– + â€–x - xâ‚€â€–â»Â¹ * â€–F' a (x - xâ‚€)â€– :
                                 by { rw [norm_smul_of_nonneg, norm_smul_of_nonneg] ; exact nneg _}
    ... â‰¤  â€–x - xâ‚€â€–â»Â¹ * (b a * â€–x - xâ‚€â€–) + â€–x - xâ‚€â€–â»Â¹ * (â€–F' aâ€– * â€–x - xâ‚€â€–) : add_le_add _ _
    ... â‰¤ b a + â€–F' aâ€– : _,
    exact mul_le_mul_of_nonneg_left ha_bound (nneg _),
    apply mul_le_mul_of_nonneg_left ((F' a).le_op_norm _) (nneg _),
    by_cases h : â€–x - xâ‚€â€– = 0,
    { simpa [h] using add_nonneg (b_nonneg a) (norm_nonneg (F' a)) },
    { field_simp [h] } },
  { exact b_int.add hF'_int.norm },
  { apply h_diff.mono,
    intros a ha,
    suffices : tendsto (Î» x, â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))) (ğ“ xâ‚€) (ğ“ 0),
    by simpa,
    rw tendsto_zero_iff_norm_tendsto_zero,
    have : (Î» x, â€–x - xâ‚€â€–â»Â¹ * â€–F x a - F xâ‚€ a - F' a (x - xâ‚€)â€–) =
            Î» x, â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))â€–,
    { ext x,
      rw norm_smul_of_nonneg (nneg _) },
    rwa [has_fderiv_at_iff_tendsto, this] at ha },
end

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with a ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is ae-measurable
for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
lemma has_fderiv_at_integral_of_dominated_loc_of_lip {F : H â†’ Î± â†’ E} {F' : Î± â†’ (H â†’L[ğ•œ] E)} {xâ‚€ : H}
  {bound : Î± â†’ â„}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_strongly_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_strongly_measurable F' Î¼)
  (h_lip : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs $ bound a) (Î» x, F x a) (ball xâ‚€ Îµ))
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, has_fderiv_at (Î» x, F x a) (F' a) xâ‚€) :
  integrable F' Î¼ âˆ§ has_fderiv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ :=
begin
  obtain âŸ¨Î´, Î´_pos, hÎ´âŸ© : âˆƒ Î´ > 0, âˆ€ x âˆˆ ball xâ‚€ Î´, ae_strongly_measurable (F x) Î¼ âˆ§ x âˆˆ ball xâ‚€ Îµ,
    from eventually_nhds_iff_ball.mp (hF_meas.and (ball_mem_nhds xâ‚€ Îµ_pos)),
  choose hÎ´_meas hÎ´Îµ using hÎ´,
  replace h_lip : âˆ€áµ (a : Î±) âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Î´, â€–F x a - F xâ‚€ aâ€– â‰¤ |bound a| * â€–x - xâ‚€â€–,
    from h_lip.mono (Î» a lip x hx, lip.norm_sub_le (hÎ´Îµ x hx) (mem_ball_self Îµ_pos)),
  replace bound_integrable := bound_integrable.norm,
  apply has_fderiv_at_integral_of_dominated_loc_of_lip' Î´_pos; assumption
end

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on a ball around `xâ‚€` for ae `a` with
derivative norm uniformly bounded by an integrable function (the ball radius is independent of `a`),
and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
lemma has_fderiv_at_integral_of_dominated_of_fderiv_le {F : H â†’ Î± â†’ E} {F' : H â†’ Î± â†’ (H â†’L[ğ•œ] E)}
  {xâ‚€ : H} {bound : Î± â†’ â„}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_strongly_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_strongly_measurable (F' xâ‚€) Î¼)
  (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F' x aâ€– â‰¤ bound a)
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, has_fderiv_at (Î» x, F x a) (F' x a) x) :
  has_fderiv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' xâ‚€ a âˆ‚Î¼) xâ‚€ :=
begin
  letI : normed_space â„ H := normed_space.restrict_scalars â„ ğ•œ H,
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos,
  have diff_xâ‚€ : âˆ€áµ a âˆ‚Î¼, has_fderiv_at (Î» x, F x a) (F' xâ‚€ a) xâ‚€ :=
    h_diff.mono (Î» a ha, ha xâ‚€ xâ‚€_in),
  have : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs (bound a)) (Î» x, F x a) (ball xâ‚€ Îµ),
  { apply (h_diff.and h_bound).mono,
    rintros a âŸ¨ha_deriv, ha_boundâŸ©,
    refine (convex_ball _ _).lipschitz_on_with_of_nnnorm_has_fderiv_within_le
      (Î» x x_in, (ha_deriv x x_in).has_fderiv_within_at) (Î» x x_in, _),
    rw [â† nnreal.coe_le_coe, coe_nnnorm, real.coe_nnabs],
    exact (ha_bound x x_in).trans (le_abs_self _) },
  exact (has_fderiv_at_integral_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int
                                               hF'_meas this bound_integrable diff_xâ‚€).2
end

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is
ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
lemma has_deriv_at_integral_of_dominated_loc_of_lip {F : ğ•œ â†’ Î± â†’ E} {F' : Î± â†’ E} {xâ‚€ : ğ•œ}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_strongly_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_strongly_measurable F' Î¼) {bound : Î± â†’ â„}
  (h_lipsch : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs $ bound a) (Î» x, F x a) (ball xâ‚€ Îµ))
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, has_deriv_at (Î» x, F x a) (F' a) xâ‚€) :
  (integrable F' Î¼) âˆ§ has_deriv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ :=
begin
  set L : E â†’L[ğ•œ] (ğ•œ â†’L[ğ•œ] E) := (continuous_linear_map.smul_rightL ğ•œ ğ•œ E 1),
  replace h_diff : âˆ€áµ a âˆ‚Î¼, has_fderiv_at (Î» x, F x a) (L (F' a)) xâ‚€ :=
    h_diff.mono (Î» x hx, hx.has_fderiv_at),
  have hm : ae_strongly_measurable (L âˆ˜ F') Î¼ := L.continuous.comp_ae_strongly_measurable hF'_meas,
  cases has_fderiv_at_integral_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hm h_lipsch
    bound_integrable h_diff with hF'_int key,
  replace hF'_int : integrable F' Î¼,
  { rw [â† integrable_norm_iff hm] at hF'_int,
    simpa only [L, (âˆ˜), integrable_norm_iff, hF'_meas, one_mul, norm_one,
      continuous_linear_map.comp_apply, continuous_linear_map.coe_restrict_scalarsL',
      continuous_linear_map.norm_restrict_scalars, continuous_linear_map.norm_smul_rightL_apply]
      using hF'_int },
  refine âŸ¨hF'_int, _âŸ©,
  simp_rw has_deriv_at_iff_has_fderiv_at at h_diff âŠ¢,
  rwa continuous_linear_map.integral_comp_comm _ hF'_int at key,
  all_goals { apply_instance, },
end

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : â„`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on an interval around `xâ‚€` for ae `a`
(with interval radius independent of `a`) with derivative uniformly bounded by an integrable
function, and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
lemma has_deriv_at_integral_of_dominated_loc_of_deriv_le {F : ğ•œ â†’ Î± â†’ E} {F' : ğ•œ â†’ Î± â†’ E} {xâ‚€ : ğ•œ}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_strongly_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_strongly_measurable (F' xâ‚€) Î¼)
  {bound : Î± â†’ â„}
  (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F' x aâ€– â‰¤ bound a)
  (bound_integrable : integrable bound Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, has_deriv_at (Î» x, F x a) (F' x a) x) :
  (integrable (F' xâ‚€) Î¼) âˆ§ has_deriv_at (Î»n, âˆ« a, F n a âˆ‚Î¼) (âˆ« a, F' xâ‚€ a âˆ‚Î¼) xâ‚€ :=
begin
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos,
  have diff_xâ‚€ : âˆ€áµ a âˆ‚Î¼, has_deriv_at (Î» x, F x a) (F' xâ‚€ a) xâ‚€ :=
    h_diff.mono (Î» a ha, ha xâ‚€ xâ‚€_in),
  have : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs (bound a)) (Î» (x : ğ•œ), F x a) (ball xâ‚€ Îµ),
  { apply (h_diff.and h_bound).mono,
    rintros a âŸ¨ha_deriv, ha_boundâŸ©,
    refine (convex_ball _ _).lipschitz_on_with_of_nnnorm_has_deriv_within_le
      (Î» x x_in, (ha_deriv x x_in).has_deriv_within_at) (Î» x x_in, _),
    rw [â† nnreal.coe_le_coe, coe_nnnorm, real.coe_nnabs],
    exact (ha_bound x x_in).trans (le_abs_self _) },
  exact has_deriv_at_integral_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hF'_meas this
        bound_integrable diff_xâ‚€
end
