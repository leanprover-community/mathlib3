/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.calculus.uniform_limits_deriv
import analysis.calculus.cont_diff
import data.nat.cast.with_top

/-!
# Smoothness of series

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We show that series of functions are continuous, or differentiable, or smooth, when each individual
function in the series is and additionally suitable uniform summable bounds are satisfied.

More specifically,
* `continuous_tsum` ensures that a series of continuous functions is continuous.
* `differentiable_tsum` ensures that a series of differentiable functions is differentiable.
* `cont_diff_tsum` ensures that a series of smooth functions is smooth.

We also give versions of these statements which are localized to a set.
-/

open set metric topological_space function asymptotics filter
open_locale topology nnreal big_operators

variables {Î± Î² ğ•œ E F : Type*}
  [is_R_or_C ğ•œ]
  [normed_add_comm_group E] [normed_space ğ•œ E]
  [normed_add_comm_group F] [complete_space F]
  {u : Î± â†’ â„}

/-! ### Continuity -/

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version relative to a set, with general index set. -/
lemma tendsto_uniformly_on_tsum {f : Î± â†’ Î² â†’ F} (hu : summable u) {s : set Î²}
  (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) :
  tendsto_uniformly_on (Î» (t : finset Î±), (Î» x, âˆ‘ n in t, f n x)) (Î» x, âˆ‘' n, f n x) at_top s :=
begin
  refine tendsto_uniformly_on_iff.2 (Î» Îµ Îµpos, _),
  filter_upwards [(tendsto_order.1 (tendsto_tsum_compl_at_top_zero u)).2 _ Îµpos] with t ht x hx,
  have A : summable (Î» n, â€–f n xâ€–),
    from summable_of_nonneg_of_le (Î» n, norm_nonneg _) (Î» n, hfu n x hx) hu,
  rw [dist_eq_norm, â† sum_add_tsum_subtype_compl (summable_of_summable_norm A) t, add_sub_cancel'],
  apply lt_of_le_of_lt _ ht,
  apply (norm_tsum_le_tsum_norm (A.subtype _)).trans,
  exact tsum_le_tsum (Î» n, hfu _ _ hx) (A.subtype _) (hu.subtype _)
end

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version relative to a set, with index set `â„•`. -/
lemma tendsto_uniformly_on_tsum_nat {f : â„• â†’ Î² â†’ F} {u : â„• â†’ â„} (hu : summable u) {s : set Î²}
  (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) :
  tendsto_uniformly_on (Î» N, (Î» x, âˆ‘ n in finset.range N, f n x)) (Î» x, âˆ‘' n, f n x) at_top s :=
Î» v hv, tendsto_finset_range.eventually (tendsto_uniformly_on_tsum hu hfu v hv)

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version with general index set. -/
lemma tendsto_uniformly_tsum {f : Î± â†’ Î² â†’ F} (hu : summable u)
  (hfu : âˆ€ n x, â€–f n xâ€– â‰¤ u n) :
  tendsto_uniformly (Î» (t : finset Î±), (Î» x, âˆ‘ n in t, f n x)) (Î» x, âˆ‘' n, f n x) at_top :=
by { rw â† tendsto_uniformly_on_univ, exact tendsto_uniformly_on_tsum hu (Î» n x hx, hfu n x) }

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version with index set `â„•`. -/
lemma tendsto_uniformly_tsum_nat {f : â„• â†’ Î² â†’ F} {u : â„• â†’ â„} (hu : summable u)
  (hfu : âˆ€ n x, â€–f n xâ€– â‰¤ u n) :
  tendsto_uniformly (Î» N, (Î» x, âˆ‘ n in finset.range N, f n x)) (Î» x, âˆ‘' n, f n x) at_top :=
Î» v hv, tendsto_finset_range.eventually (tendsto_uniformly_tsum hu hfu v hv)

/-- An infinite sum of functions with summable sup norm is continuous on a set if each individual
function is. -/
lemma continuous_on_tsum [topological_space Î²]
  {f : Î± â†’ Î² â†’ F} {s : set Î²} (hf : âˆ€ i, continuous_on (f i) s) (hu : summable u)
  (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) :
  continuous_on (Î» x, âˆ‘' n, f n x) s :=
begin
  classical,
  refine (tendsto_uniformly_on_tsum hu hfu).continuous_on (eventually_of_forall _),
  assume t,
  exact continuous_on_finset_sum _ (Î» i hi, hf i),
end

/-- An infinite sum of functions with summable sup norm is continuous if each individual
function is. -/
lemma continuous_tsum [topological_space Î²]
  {f : Î± â†’ Î² â†’ F} (hf : âˆ€ i, continuous (f i)) (hu : summable u)
  (hfu : âˆ€ n x, â€–f n xâ€– â‰¤ u n) :
  continuous (Î» x, âˆ‘' n, f n x) :=
begin
  simp_rw [continuous_iff_continuous_on_univ] at hf âŠ¢,
  exact continuous_on_tsum hf hu (Î» n x hx, hfu n x),
end


/-! ### Differentiability -/

variables [normed_space ğ•œ F]
variables {f : Î± â†’ E â†’ F} {f' : Î± â†’ E â†’ (E â†’L[ğ•œ] F)} {v : â„• â†’ Î± â†’ â„}
{s : set E} {xâ‚€ x : E} {N : â„•âˆ}

/-- Consider a series of functions `âˆ‘' n, f n x` on a preconnected open set. If the series converges
at a point, and all functions in the series are differentiable with a summable bound on the
derivatives, then the series converges everywhere on the set. -/
lemma summable_of_summable_has_fderiv_at_of_is_preconnected
  (hu : summable u) (hs : is_open s) (h's : is_preconnected s)
  (hf : âˆ€ n x, x âˆˆ s â†’ has_fderiv_at (f n) (f' n x) x)
  (hf' : âˆ€ n x, x âˆˆ s â†’ â€–f' n xâ€– â‰¤ u n)
  (hxâ‚€ : xâ‚€ âˆˆ s) (hf0 : summable (Î» n, f n xâ‚€)) {x : E} (hx : x âˆˆ s) :
  summable (Î» n, f n x) :=
begin
  rw summable_iff_cauchy_seq_finset at hf0 âŠ¢,
  have A : uniform_cauchy_seq_on (Î» (t : finset Î±), (Î» x, âˆ‘ i in t, f' i x)) at_top s,
    from (tendsto_uniformly_on_tsum hu hf').uniform_cauchy_seq_on,
  apply cauchy_map_of_uniform_cauchy_seq_on_fderiv hs h's A (Î» t y hy, _) hxâ‚€ hx hf0,
  exact has_fderiv_at.sum (Î» i hi, hf i y hy),
end

/-- Consider a series of functions `âˆ‘' n, f n x` on a preconnected open set. If the series converges
at a point, and all functions in the series are differentiable with a summable bound on the
derivatives, then the series is differentiable on the set and its derivative is the sum of the
derivatives. -/
lemma has_fderiv_at_tsum_of_is_preconnected
  (hu : summable u) (hs : is_open s) (h's : is_preconnected s)
  (hf : âˆ€ n x, x âˆˆ s â†’ has_fderiv_at (f n) (f' n x) x)
  (hf' : âˆ€ n x, x âˆˆ s â†’ â€–f' n xâ€– â‰¤ u n)
  (hxâ‚€ : xâ‚€ âˆˆ s) (hf0 : summable (Î» n, f n xâ‚€)) (hx : x âˆˆ s) :
  has_fderiv_at (Î» y, âˆ‘' n, f n y) (âˆ‘' n, f' n x) x :=
begin
  classical,
  have A : âˆ€ (x : E), x âˆˆ s â†’ tendsto (Î» (t : finset Î±), âˆ‘ n in t, f n x) at_top (ğ“ (âˆ‘' n, f n x)),
  { assume y hy,
    apply summable.has_sum,
    exact summable_of_summable_has_fderiv_at_of_is_preconnected hu hs h's hf hf' hxâ‚€ hf0 hy },
  apply has_fderiv_at_of_tendsto_uniformly_on hs
    (tendsto_uniformly_on_tsum hu hf') (Î» t y hy, _) A _ hx,
  exact has_fderiv_at.sum (Î» n hn, hf n y hy),
end

/-- Consider a series of functions `âˆ‘' n, f n x`. If the series converges at a
point, and all functions in the series are differentiable with a summable bound on the derivatives,
then the series converges everywhere. -/
lemma summable_of_summable_has_fderiv_at
  (hu : summable u) (hf : âˆ€ n x, has_fderiv_at (f n) (f' n x) x) (hf' : âˆ€ n x, â€–f' n xâ€– â‰¤ u n)
  (hf0 : summable (Î» n, f n xâ‚€)) (x : E) :
  summable (Î» n, f n x) :=
begin
  letI : normed_space â„ E, from normed_space.restrict_scalars â„ ğ•œ _,
  apply summable_of_summable_has_fderiv_at_of_is_preconnected hu is_open_univ
    is_connected_univ.is_preconnected (Î» n x hx, hf n x)
    (Î» n x hx, hf' n x) (mem_univ _) hf0 (mem_univ _),
end

/-- Consider a series of functions `âˆ‘' n, f n x`. If the series converges at a
point, and all functions in the series are differentiable with a summable bound on the derivatives,
then the series is differentiable and its derivative is the sum of the derivatives. -/
lemma has_fderiv_at_tsum
  (hu : summable u) (hf : âˆ€ n x, has_fderiv_at (f n) (f' n x) x) (hf' : âˆ€ n x, â€–f' n xâ€– â‰¤ u n)
  (hf0 : summable (Î» n, f n xâ‚€)) (x : E) :
  has_fderiv_at (Î» y, âˆ‘' n, f n y) (âˆ‘' n, f' n x) x :=
begin
  letI : normed_space â„ E, from normed_space.restrict_scalars â„ ğ•œ _,
  exact has_fderiv_at_tsum_of_is_preconnected hu is_open_univ
    is_connected_univ.is_preconnected (Î» n x hx, hf n x)
    (Î» n x hx, hf' n x) (mem_univ _) hf0 (mem_univ _),
end

/-- Consider a series of functions `âˆ‘' n, f n x`. If all functions in the series are differentiable
with a summable bound on the derivatives, then the series is differentiable.
Note that our assumptions do not ensure the pointwise convergence, but if there is no pointwise
convergence then the series is zero everywhere so the result still holds. -/
lemma differentiable_tsum
  (hu : summable u) (hf : âˆ€ n x, has_fderiv_at (f n) (f' n x) x) (hf' : âˆ€ n x, â€–f' n xâ€– â‰¤ u n) :
  differentiable ğ•œ (Î» y, âˆ‘' n, f n y) :=
begin
  by_cases h : âˆƒ xâ‚€, summable (Î» n, f n xâ‚€),
  { rcases h with âŸ¨xâ‚€, hf0âŸ©,
    assume x,
    exact (has_fderiv_at_tsum hu hf hf' hf0 x).differentiable_at },
  { push_neg at h,
    have : (Î» x, âˆ‘' n, f n x) = 0,
    { ext1 x, exact tsum_eq_zero_of_not_summable (h x) },
    rw this,
    exact differentiable_const 0 }
end

lemma fderiv_tsum_apply
  (hu : summable u) (hf : âˆ€ n, differentiable ğ•œ (f n)) (hf' : âˆ€ n x, â€–fderiv ğ•œ (f n) xâ€– â‰¤ u n)
  (hf0 : summable (Î» n, f n xâ‚€)) (x : E) :
  fderiv ğ•œ (Î» y, âˆ‘' n, f n y) x = âˆ‘' n, fderiv ğ•œ (f n) x :=
(has_fderiv_at_tsum hu (Î» n x, (hf n x).has_fderiv_at) hf' hf0 _).fderiv

lemma fderiv_tsum
  (hu : summable u) (hf : âˆ€ n, differentiable ğ•œ (f n)) (hf' : âˆ€ n x, â€–fderiv ğ•œ (f n) xâ€– â‰¤ u n)
  {xâ‚€ : E} (hf0 : summable (Î» n, f n xâ‚€)) :
  fderiv ğ•œ (Î» y, âˆ‘' n, f n y) = (Î» x, âˆ‘' n, fderiv ğ•œ (f n) x) :=
by { ext1 x, exact fderiv_tsum_apply hu hf hf' hf0 x}


/-! ### Higher smoothness -/

/-- Consider a series of smooth functions, with summable uniform bounds on the successive
derivatives. Then the iterated derivative of the sum is the sum of the iterated derivative. -/
lemma iterated_fderiv_tsum
  (hf : âˆ€ i, cont_diff ğ•œ N (f i)) (hv : âˆ€ (k : â„•), (k : â„•âˆ) â‰¤ N â†’ summable (v k))
  (h'f : âˆ€ (k : â„•) (i : Î±) (x : E), (k : â„•âˆ) â‰¤ N â†’ â€–iterated_fderiv ğ•œ k (f i) xâ€– â‰¤ v k i)
  {k : â„•} (hk : (k : â„•âˆ) â‰¤ N) :
  iterated_fderiv ğ•œ k (Î» y, âˆ‘' n, f n y) = (Î» x, âˆ‘' n, iterated_fderiv ğ•œ k (f n) x) :=
begin
  induction k with k IH,
  { ext1 x,
    simp_rw [iterated_fderiv_zero_eq_comp],
    exact (continuous_multilinear_curry_fin0 ğ•œ E F).symm.to_continuous_linear_equiv.map_tsum },
  { have h'k : (k : â„•âˆ) < N,
      from lt_of_lt_of_le (with_top.coe_lt_coe.2 (nat.lt_succ_self _)) hk,
    have A : summable (Î» n, iterated_fderiv ğ•œ k (f n) 0),
      from summable_of_norm_bounded (v k) (hv k h'k.le) (Î» n, h'f k n 0 h'k.le),
    simp_rw [iterated_fderiv_succ_eq_comp_left, IH h'k.le],
    rw fderiv_tsum (hv _ hk) (Î» n, (hf n).differentiable_iterated_fderiv h'k) _ A,
    { ext1 x,
      exact (continuous_multilinear_curry_left_equiv ğ•œ (Î» (i : fin (k + 1)), E) F)
        .to_continuous_linear_equiv.map_tsum },
    { assume n x,
      simpa only [iterated_fderiv_succ_eq_comp_left, linear_isometry_equiv.norm_map]
        using h'f k.succ n x hk } }
end

/-- Consider a series of smooth functions, with summable uniform bounds on the successive
derivatives. Then the iterated derivative of the sum is the sum of the iterated derivative. -/
lemma iterated_fderiv_tsum_apply
  (hf : âˆ€ i, cont_diff ğ•œ N (f i)) (hv : âˆ€ (k : â„•), (k : â„•âˆ) â‰¤ N â†’ summable (v k))
  (h'f : âˆ€ (k : â„•) (i : Î±) (x : E), (k : â„•âˆ) â‰¤ N â†’ â€–iterated_fderiv ğ•œ k (f i) xâ€– â‰¤ v k i)
  {k : â„•} (hk : (k : â„•âˆ) â‰¤ N) (x : E) :
  iterated_fderiv ğ•œ k (Î» y, âˆ‘' n, f n y) x = âˆ‘' n, iterated_fderiv ğ•œ k (f n) x :=
by rw iterated_fderiv_tsum hf hv h'f hk

/-- Consider a series of functions `âˆ‘' i, f i x`. Assume that each individual function `f i` is of
class `C^N`, and moreover there is a uniform summable upper bound on the `k`-th derivative
for each `k â‰¤ N`. Then the series is also `C^N`. -/
lemma cont_diff_tsum
  (hf : âˆ€ i, cont_diff ğ•œ N (f i)) (hv : âˆ€ (k : â„•), (k : â„•âˆ) â‰¤ N â†’ summable (v k))
  (h'f : âˆ€ (k : â„•) (i : Î±) (x : E), (k : â„•âˆ) â‰¤ N â†’ â€–iterated_fderiv ğ•œ k (f i) xâ€– â‰¤ v k i) :
  cont_diff ğ•œ N (Î» x, âˆ‘' i, f i x) :=
begin
  rw cont_diff_iff_continuous_differentiable,
  split,
  { assume m hm,
    rw iterated_fderiv_tsum hf hv h'f hm,
    refine continuous_tsum _ (hv m hm) _,
    { assume i,
      exact cont_diff.continuous_iterated_fderiv hm (hf i) },
    { assume n x,
      exact h'f _ _ _ hm } },
  { assume m hm,
    have h'm : ((m+1 : â„•) : â„•âˆ) â‰¤ N,
      by simpa only [enat.coe_add, nat.cast_with_bot, enat.coe_one] using enat.add_one_le_of_lt hm,
    rw iterated_fderiv_tsum hf hv h'f hm.le,
    have A : âˆ€ n x, has_fderiv_at (iterated_fderiv ğ•œ m (f n))
      (fderiv ğ•œ (iterated_fderiv ğ•œ m (f n)) x) x, from Î» n x,
        (cont_diff.differentiable_iterated_fderiv hm (hf n)).differentiable_at.has_fderiv_at,
    apply differentiable_tsum (hv _ h'm) A (Î» n x, _),
    rw [fderiv_iterated_fderiv, linear_isometry_equiv.norm_map],
    exact h'f _ _ _ h'm }
end

/-- Consider a series of functions `âˆ‘' i, f i x`. Assume that each individual function `f i` is of
class `C^N`, and moreover there is a uniform summable upper bound on the `k`-th derivative
for each `k â‰¤ N` (except maybe for finitely many `i`s). Then the series is also `C^N`. -/
lemma cont_diff_tsum_of_eventually
  (hf : âˆ€ i, cont_diff ğ•œ N (f i)) (hv : âˆ€ (k : â„•), (k : â„•âˆ) â‰¤ N â†’ summable (v k))
  (h'f : âˆ€ (k : â„•), (k : â„•âˆ) â‰¤ N â†’ âˆ€á¶  i in (filter.cofinite : filter Î±), âˆ€ (x : E),
     â€–iterated_fderiv ğ•œ k (f i) xâ€– â‰¤ v k i) :
  cont_diff ğ•œ N (Î» x, âˆ‘' i, f i x) :=
begin
  classical,
  apply cont_diff_iff_forall_nat_le.2 (Î» m hm, _),
  let t : set Î± :=
    {i : Î± | Â¬âˆ€ (k : â„•), k âˆˆ finset.range (m + 1) â†’ âˆ€ x, â€–iterated_fderiv ğ•œ k (f i) xâ€– â‰¤ v k i},
  have ht : set.finite t,
  { have A : âˆ€á¶  i in (filter.cofinite : filter Î±), âˆ€ (k : â„•), k âˆˆ finset.range (m+1) â†’
      âˆ€ (x : E), â€–iterated_fderiv ğ•œ k (f i) xâ€– â‰¤ v k i,
    { rw eventually_all_finset,
      assume i hi,
      apply h'f,
      simp only [finset.mem_range_succ_iff] at hi,
      exact (with_top.coe_le_coe.2 hi).trans hm },
    exact eventually_cofinite.2 A },
  let T : finset Î± := ht.to_finset,
  have : (Î» x, âˆ‘' i, f i x) = (Î» x, âˆ‘ i in T, f i x) + (Î» x, âˆ‘' i : {i // i âˆ‰ T}, f i x),
  { ext1 x,
    refine (sum_add_tsum_subtype_compl _ T).symm,
    refine summable_of_norm_bounded_eventually _ (hv 0 (zero_le _)) _,
    filter_upwards [h'f 0 (zero_le _)] with i hi,
    simpa only [norm_iterated_fderiv_zero] using hi x },
  rw this,
  apply (cont_diff.sum (Î» i hi, (hf i).of_le hm)).add,
  have h'u : âˆ€ (k : â„•), (k : â„•âˆ) â‰¤ m â†’ summable ((v k) âˆ˜ (coe : {i // i âˆ‰ T} â†’ Î±)),
    from Î» k hk, (hv k (hk.trans hm)).subtype _,
  refine cont_diff_tsum (Î» i, (hf i).of_le hm) h'u _,
  rintros k âŸ¨i, hiâŸ© x hk,
  dsimp,
  simp only [finite.mem_to_finset, mem_set_of_eq, finset.mem_range, not_forall, not_le, exists_prop,
    not_exists, not_and, not_lt] at hi,
  exact hi k (nat.lt_succ_iff.2 (with_top.coe_le_coe.1 hk)) x,
end
