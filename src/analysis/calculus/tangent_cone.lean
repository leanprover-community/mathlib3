/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.convex.basic
import analysis.specific_limits

/-!
# Tangent cone

In this file, we define two predicates `unique_diff_within_at ğ•œ s x` and `unique_diff_on ğ•œ s`
ensuring that, if a function has two derivatives, then they have to coincide. As a direct
definition of this fact (quantifying on all target types and all functions) would depend on
universes, we use a more intrinsic definition: if all the possible tangent directions to the set
`s` at the point `x` span a dense subset of the whole subset, it is easy to check that the
derivative has to be unique.

Therefore, we introduce the set of all tangent directions, named `tangent_cone_at`,
and express `unique_diff_within_at` and `unique_diff_on` in terms of it.
One should however think of this definition as an implementation detail: the only reason to
introduce the predicates `unique_diff_within_at` and `unique_diff_on` is to ensure the uniqueness
of the derivative. This is why their names reflect their uses, and not how they are defined.

## Implementation details

Note that this file is imported by `fderiv.lean`. Hence, derivatives are not defined yet. The
property of uniqueness of the derivative is therefore proved in `fderiv.lean`, but based on the
properties of the tangent cone we prove here.
-/

variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]

open filter set
open_locale topological_space

section tangent_cone

variables {E : Type*} [add_comm_monoid E] [module ğ•œ E] [topological_space E]

/-- The set of all tangent directions to the set `s` at the point `x`. -/
def tangent_cone_at (s : set E) (x : E) : set E :=
{y : E | âˆƒ(c : â„• â†’ ğ•œ) (d : â„• â†’ E), (âˆ€á¶  n in at_top, x + d n âˆˆ s) âˆ§
  (tendsto (Î»n, âˆ¥c nâˆ¥) at_top at_top) âˆ§ (tendsto (Î»n, c n â€¢ d n) at_top (ğ“ y))}

/-- A property ensuring that the tangent cone to `s` at `x` spans a dense subset of the whole space.
The main role of this property is to ensure that the differential within `s` at `x` is unique,
hence this name. The uniqueness it asserts is proved in `unique_diff_within_at.eq` in `fderiv.lean`.
To avoid pathologies in dimension 0, we also require that `x` belongs to the closure of `s` (which
is automatic when `E` is not `0`-dimensional).
 -/
@[mk_iff] structure unique_diff_within_at (s : set E) (x : E) : Prop :=
(dense_tangent_cone : dense ((submodule.span ğ•œ (tangent_cone_at ğ•œ s x)) : set E))
(mem_closure : x âˆˆ closure s)

/-- A property ensuring that the tangent cone to `s` at any of its points spans a dense subset of
the whole space.  The main role of this property is to ensure that the differential along `s` is
unique, hence this name. The uniqueness it asserts is proved in `unique_diff_on.eq` in
`fderiv.lean`. -/
def unique_diff_on (s : set E) : Prop :=
âˆ€x âˆˆ s, unique_diff_within_at ğ•œ s x

end tangent_cone

variables {E : Type*} [normed_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_group F] [normed_space ğ•œ F]
variables {G : Type*} [normed_group G] [normed_space â„ G]
variables {ğ•œ} {x y : E} {s t : set E}

section tangent_cone
/- This section is devoted to the properties of the tangent cone. -/

open normed_field

lemma tangent_cone_univ : tangent_cone_at ğ•œ univ x = univ :=
begin
  refine univ_subset_iff.1 (Î»y hy, _),
  rcases exists_one_lt_norm ğ•œ with âŸ¨w, hwâŸ©,
  refine âŸ¨Î»n, w^n, Î»n, (w^n)â»Â¹ â€¢ y, univ_mem' (Î»n, mem_univ _),  _, _âŸ©,
  { simp only [norm_pow],
    exact tendsto_pow_at_top_at_top_of_one_lt hw },
  { convert tendsto_const_nhds,
    ext n,
    have : w ^ n * (w ^ n)â»Â¹ = 1,
    { apply mul_inv_cancel,
      apply pow_ne_zero,
      simpa [norm_eq_zero] using (ne_of_lt (lt_trans zero_lt_one hw)).symm },
    rw [smul_smul, this, one_smul] }
end

lemma tangent_cone_mono (h : s âŠ† t) :
  tangent_cone_at ğ•œ s x âŠ† tangent_cone_at ğ•œ t x :=
begin
  rintros y âŸ¨c, d, ds, ctop, climâŸ©,
  exact âŸ¨c, d, mem_of_superset ds (Î»n hn, h hn), ctop, climâŸ©
end

/-- Auxiliary lemma ensuring that, under the assumptions defining the tangent cone,
the sequence `d` tends to 0 at infinity. -/
lemma tangent_cone_at.lim_zero {Î± : Type*} (l : filter Î±) {c : Î± â†’ ğ•œ} {d : Î± â†’ E}
  (hc : tendsto (Î»n, âˆ¥c nâˆ¥) l at_top) (hd : tendsto (Î»n, c n â€¢ d n) l (ğ“ y)) :
  tendsto d l (ğ“ 0) :=
begin
  have A : tendsto (Î»n, âˆ¥c nâˆ¥â»Â¹) l (ğ“ 0) := tendsto_inv_at_top_zero.comp hc,
  have B : tendsto (Î»n, âˆ¥c n â€¢ d nâˆ¥) l (ğ“ âˆ¥yâˆ¥) :=
    (continuous_norm.tendsto _).comp hd,
  have C : tendsto (Î»n, âˆ¥c nâˆ¥â»Â¹ * âˆ¥c n â€¢ d nâˆ¥) l (ğ“ (0 * âˆ¥yâˆ¥)) := A.mul B,
  rw zero_mul at C,
  have : âˆ€á¶  n in l, âˆ¥c nâˆ¥â»Â¹ * âˆ¥c n â€¢ d nâˆ¥ = âˆ¥d nâˆ¥,
  { apply (eventually_ne_of_tendsto_norm_at_top hc 0).mono (Î»n hn, _),
    rw [norm_smul, â† mul_assoc, inv_mul_cancel, one_mul],
    rwa [ne.def, norm_eq_zero] },
  have D : tendsto (Î» n, âˆ¥d nâˆ¥) l (ğ“ 0) :=
    tendsto.congr' this C,
  rw tendsto_zero_iff_norm_tendsto_zero,
  exact D
end

lemma tangent_cone_mono_nhds (h : ğ“[s] x â‰¤ ğ“[t] x) :
  tangent_cone_at ğ•œ s x âŠ† tangent_cone_at ğ•œ t x :=
begin
  rintros y âŸ¨c, d, ds, ctop, climâŸ©,
  refine âŸ¨c, d, _, ctop, climâŸ©,
  suffices : tendsto (Î» n, x + d n) at_top (ğ“[t] x),
    from tendsto_principal.1 (tendsto_inf.1 this).2,
  refine (tendsto_inf.2 âŸ¨_, tendsto_principal.2 dsâŸ©).mono_right h,
  simpa only [add_zero] using tendsto_const_nhds.add (tangent_cone_at.lim_zero at_top ctop clim)
end

/-- Tangent cone of `s` at `x` depends only on `ğ“[s] x`. -/
lemma tangent_cone_congr (h : ğ“[s] x = ğ“[t] x) :
  tangent_cone_at ğ•œ s x = tangent_cone_at ğ•œ t x :=
subset.antisymm
  (tangent_cone_mono_nhds $ le_of_eq h)
  (tangent_cone_mono_nhds $ le_of_eq h.symm)

/-- Intersecting with a neighborhood of the point does not change the tangent cone. -/
lemma tangent_cone_inter_nhds (ht : t âˆˆ ğ“ x) :
  tangent_cone_at ğ•œ (s âˆ© t) x = tangent_cone_at ğ•œ s x :=
tangent_cone_congr (nhds_within_restrict' _ ht).symm

/-- The tangent cone of a product contains the tangent cone of its left factor. -/
lemma subset_tangent_cone_prod_left {t : set F} {y : F} (ht : y âˆˆ closure t) :
  linear_map.inl ğ•œ E F '' (tangent_cone_at ğ•œ s x) âŠ† tangent_cone_at ğ•œ (set.prod s t) (x, y) :=
begin
  rintros _ âŸ¨v, âŸ¨c, d, hd, hc, hyâŸ©, rflâŸ©,
  have : âˆ€n, âˆƒd', y + d' âˆˆ t âˆ§ âˆ¥c n â€¢ d'âˆ¥ < ((1:â„)/2)^n,
  { assume n,
    rcases mem_closure_iff_nhds.1 ht _ (eventually_nhds_norm_smul_sub_lt (c n) y
      (pow_pos one_half_pos n)) with âŸ¨z, hz, hztâŸ©,
    exact âŸ¨z - y, by simpa using hzt, by simpa using hzâŸ© },
  choose d' hd' using this,
  refine âŸ¨c, Î»n, (d n, d' n), _, hc, _âŸ©,
  show âˆ€á¶  n in at_top, (x, y) + (d n, d' n) âˆˆ set.prod s t,
  { filter_upwards [hd],
    assume n hn,
    simp [hn, (hd' n).1] },
  { apply tendsto.prod_mk_nhds hy _,
    refine squeeze_zero_norm (Î»n, (hd' n).2.le) _,
    exact tendsto_pow_at_top_nhds_0_of_lt_1 one_half_pos.le one_half_lt_one }
end

/-- The tangent cone of a product contains the tangent cone of its right factor. -/
lemma subset_tangent_cone_prod_right {t : set F} {y : F}
  (hs : x âˆˆ closure s) :
  linear_map.inr ğ•œ E F '' (tangent_cone_at ğ•œ t y) âŠ† tangent_cone_at ğ•œ (set.prod s t) (x, y) :=
begin
  rintros _ âŸ¨w, âŸ¨c, d, hd, hc, hyâŸ©, rflâŸ©,
  have : âˆ€n, âˆƒd', x + d' âˆˆ s âˆ§ âˆ¥c n â€¢ d'âˆ¥ < ((1:â„)/2)^n,
  { assume n,
    rcases mem_closure_iff_nhds.1 hs _ (eventually_nhds_norm_smul_sub_lt (c n) x
      (pow_pos one_half_pos n)) with âŸ¨z, hz, hzsâŸ©,
    exact âŸ¨z - x, by simpa using hzs, by simpa using hzâŸ© },
  choose d' hd' using this,
  refine âŸ¨c, Î»n, (d' n, d n), _, hc, _âŸ©,
  show âˆ€á¶  n in at_top, (x, y) + (d' n, d n) âˆˆ set.prod s t,
  { filter_upwards [hd],
    assume n hn,
    simp [hn, (hd' n).1] },
  { apply tendsto.prod_mk_nhds _ hy,
    refine squeeze_zero_norm (Î»n, (hd' n).2.le) _,
    exact tendsto_pow_at_top_nhds_0_of_lt_1 one_half_pos.le one_half_lt_one }
end

/-- The tangent cone of a product contains the tangent cone of each factor. -/
lemma maps_to_tangent_cone_pi {Î¹ : Type*} [decidable_eq Î¹] {E : Î¹ â†’ Type*}
  [Î  i, normed_group (E i)] [Î  i, normed_space ğ•œ (E i)]
  {s : Î  i, set (E i)} {x : Î  i, E i} {i : Î¹} (hi : âˆ€ j â‰  i, x j âˆˆ closure (s j)) :
  maps_to (linear_map.single i : E i â†’â‚—[ğ•œ] Î  j, E j) (tangent_cone_at ğ•œ (s i) (x i))
    (tangent_cone_at ğ•œ (set.pi univ s) x) :=
begin
  rintros w âŸ¨c, d, hd, hc, hyâŸ©,
  have : âˆ€ n (j â‰  i), âˆƒ d', x j + d' âˆˆ s j âˆ§ âˆ¥c n â€¢ d'âˆ¥ < (1 / 2 : â„) ^ n,
  { assume n j hj,
    rcases mem_closure_iff_nhds.1 (hi j hj) _ (eventually_nhds_norm_smul_sub_lt (c n) (x j)
      (pow_pos one_half_pos n)) with âŸ¨z, hz, hzsâŸ©,
    exact âŸ¨z - x j, by simpa using hzs, by simpa using hzâŸ© },
  choose! d' hd's hcd',
  refine âŸ¨c, Î» n, function.update (d' n) i (d n), hd.mono (Î» n hn j hj', _), hc,
    tendsto_pi.2 $ Î» j, _âŸ©,
  { rcases em (j = i) with rfl|hj; simp * },
  { rcases em (j = i) with rfl|hj,
    { simp [hy] },
    { suffices : tendsto (Î» n, c n â€¢ d' n j) at_top (ğ“ 0), by simpa [hj],
      refine squeeze_zero_norm (Î» n, (hcd' n j hj).le) _,
      exact tendsto_pow_at_top_nhds_0_of_lt_1 one_half_pos.le one_half_lt_one } }
end

/-- If a subset of a real vector space contains a segment, then the direction of this
segment belongs to the tangent cone at its endpoints. -/
lemma mem_tangent_cone_of_segment_subset {s : set G} {x y : G} (h : segment â„ x y âŠ† s) :
  y - x âˆˆ tangent_cone_at â„ s x :=
begin
  let c := Î»n:â„•, (2:â„)^n,
  let d := Î»n:â„•, (c n)â»Â¹ â€¢ (y-x),
  refine âŸ¨c, d, filter.univ_mem' (Î»n, h _), _, _âŸ©,
  show x + d n âˆˆ segment â„ x y,
  { rw segment_eq_image,
    refine âŸ¨(c n)â»Â¹, âŸ¨_, _âŸ©, _âŸ©,
    { rw inv_nonneg, apply pow_nonneg, norm_num },
    { apply inv_le_one, apply one_le_pow_of_one_le, norm_num },
    { simp only [d, sub_smul, smul_sub, one_smul], abel } },
  show filter.tendsto (Î» (n : â„•), âˆ¥c nâˆ¥) filter.at_top filter.at_top,
  { have : (Î» (n : â„•), âˆ¥c nâˆ¥) = c,
      by { ext n, exact abs_of_nonneg (pow_nonneg (by norm_num) _) },
    rw this,
    exact tendsto_pow_at_top_at_top_of_one_lt (by norm_num) },
  show filter.tendsto (Î» (n : â„•), c n â€¢ d n) filter.at_top (ğ“ (y - x)),
  { have : (Î» (n : â„•), c n â€¢ d n) = (Î»n, y - x),
    { ext n,
      simp only [d, smul_smul],
      rw [mul_inv_cancel, one_smul],
      exact pow_ne_zero _ (by norm_num) },
    rw this,
    apply tendsto_const_nhds }
end

end tangent_cone

section unique_diff
/-!
### Properties of `unique_diff_within_at` and `unique_diff_on`

This section is devoted to properties of the predicates
`unique_diff_within_at` and `unique_diff_on`. -/

lemma unique_diff_on.unique_diff_within_at {s : set E} {x} (hs : unique_diff_on ğ•œ s) (h : x âˆˆ s) :
  unique_diff_within_at ğ•œ s x :=
hs x h

lemma unique_diff_within_at_univ : unique_diff_within_at ğ•œ univ x :=
by { rw [unique_diff_within_at_iff, tangent_cone_univ], simp }

lemma unique_diff_on_univ : unique_diff_on ğ•œ (univ : set E) :=
Î»x hx, unique_diff_within_at_univ

lemma unique_diff_on_empty : unique_diff_on ğ•œ (âˆ… : set E) :=
Î» x hx, hx.elim

lemma unique_diff_within_at.mono_nhds (h : unique_diff_within_at ğ•œ s x)
  (st : ğ“[s] x â‰¤ ğ“[t] x) :
  unique_diff_within_at ğ•œ t x :=
begin
  simp only [unique_diff_within_at_iff] at *,
  rw [mem_closure_iff_nhds_within_ne_bot] at h âŠ¢,
  exact âŸ¨h.1.mono $ submodule.span_mono $ tangent_cone_mono_nhds st,
    h.2.mono stâŸ©
end

lemma unique_diff_within_at.mono (h : unique_diff_within_at ğ•œ s x) (st : s âŠ† t) :
  unique_diff_within_at ğ•œ t x :=
h.mono_nhds $ nhds_within_mono _ st

lemma unique_diff_within_at_congr (st : ğ“[s] x = ğ“[t] x) :
  unique_diff_within_at ğ•œ s x â†” unique_diff_within_at ğ•œ t x :=
âŸ¨Î» h, h.mono_nhds $ le_of_eq st, Î» h, h.mono_nhds $ le_of_eq st.symmâŸ©

lemma unique_diff_within_at_inter (ht : t âˆˆ ğ“ x) :
  unique_diff_within_at ğ•œ (s âˆ© t) x â†” unique_diff_within_at ğ•œ s x :=
unique_diff_within_at_congr $ (nhds_within_restrict' _ ht).symm

lemma unique_diff_within_at.inter (hs : unique_diff_within_at ğ•œ s x) (ht : t âˆˆ ğ“ x) :
  unique_diff_within_at ğ•œ (s âˆ© t) x :=
(unique_diff_within_at_inter ht).2 hs

lemma unique_diff_within_at_inter' (ht : t âˆˆ ğ“[s] x) :
  unique_diff_within_at ğ•œ (s âˆ© t) x â†” unique_diff_within_at ğ•œ s x :=
unique_diff_within_at_congr $ (nhds_within_restrict'' _ ht).symm

lemma unique_diff_within_at.inter' (hs : unique_diff_within_at ğ•œ s x) (ht : t âˆˆ ğ“[s] x) :
  unique_diff_within_at ğ•œ (s âˆ© t) x :=
(unique_diff_within_at_inter' ht).2 hs

lemma unique_diff_within_at_of_mem_nhds (h : s âˆˆ ğ“ x) : unique_diff_within_at ğ•œ s x :=
by simpa only [univ_inter] using unique_diff_within_at_univ.inter h

lemma is_open.unique_diff_within_at (hs : is_open s) (xs : x âˆˆ s) : unique_diff_within_at ğ•œ s x :=
unique_diff_within_at_of_mem_nhds (is_open.mem_nhds hs xs)

lemma unique_diff_on.inter (hs : unique_diff_on ğ•œ s) (ht : is_open t) : unique_diff_on ğ•œ (s âˆ© t) :=
Î»x hx, (hs x hx.1).inter (is_open.mem_nhds ht hx.2)

lemma is_open.unique_diff_on (hs : is_open s) : unique_diff_on ğ•œ s :=
Î»x hx, is_open.unique_diff_within_at hs hx

/-- The product of two sets of unique differentiability at points `x` and `y` has unique
differentiability at `(x, y)`. -/
lemma unique_diff_within_at.prod {t : set F} {y : F}
  (hs : unique_diff_within_at ğ•œ s x) (ht : unique_diff_within_at ğ•œ t y) :
  unique_diff_within_at ğ•œ (set.prod s t) (x, y) :=
begin
  rw [unique_diff_within_at_iff] at âŠ¢ hs ht,
  rw [closure_prod_eq],
  refine âŸ¨_, hs.2, ht.2âŸ©,
  have : _ â‰¤ submodule.span ğ•œ (tangent_cone_at ğ•œ (s.prod t) (x, y)) :=
    submodule.span_mono (union_subset (subset_tangent_cone_prod_left ht.2)
      (subset_tangent_cone_prod_right hs.2)),
  rw [linear_map.span_inl_union_inr, set_like.le_def] at this,
  exact (hs.1.prod ht.1).mono this
end

lemma unique_diff_within_at.univ_pi (Î¹ : Type*) [fintype Î¹] (E : Î¹ â†’ Type*)
  [Î  i, normed_group (E i)] [Î  i, normed_space ğ•œ (E i)]
  (s : Î  i, set (E i)) (x : Î  i, E i) (h : âˆ€ i, unique_diff_within_at ğ•œ (s i) (x i)) :
  unique_diff_within_at ğ•œ (set.pi univ s) x :=
begin
  classical,
  simp only [unique_diff_within_at_iff, closure_pi_set] at h âŠ¢,
  refine âŸ¨(dense_pi univ (Î» i _, (h i).1)).mono _, Î» i _, (h i).2âŸ©,
  norm_cast,
  simp only [â† submodule.supr_map_single, supr_le_iff, linear_map.map_span, submodule.span_le,
    â† maps_to'],
  exact Î» i, (maps_to_tangent_cone_pi $ Î» j hj, (h j).2).mono subset.rfl submodule.subset_span
end

lemma unique_diff_within_at.pi (Î¹ : Type*) [fintype Î¹] (E : Î¹ â†’ Type*)
  [Î  i, normed_group (E i)] [Î  i, normed_space ğ•œ (E i)]
  (s : Î  i, set (E i)) (x : Î  i, E i) (I : set Î¹)
  (h : âˆ€ i âˆˆ I, unique_diff_within_at ğ•œ (s i) (x i)) :
  unique_diff_within_at ğ•œ (set.pi I s) x :=
begin
  classical,
  rw [â† set.univ_pi_piecewise],
  refine unique_diff_within_at.univ_pi _ _ _ _ (Î» i, _),
  by_cases hi : i âˆˆ I; simp [*, unique_diff_within_at_univ],
end

/-- The product of two sets of unique differentiability is a set of unique differentiability. -/
lemma unique_diff_on.prod {t : set F} (hs : unique_diff_on ğ•œ s) (ht : unique_diff_on ğ•œ t) :
  unique_diff_on ğ•œ (set.prod s t) :=
Î» âŸ¨x, yâŸ© h, unique_diff_within_at.prod (hs x h.1) (ht y h.2)

/-- The finite product of a family of sets of unique differentiability is a set of unique
differentiability. -/
lemma unique_diff_on.pi (Î¹ : Type*) [fintype Î¹] (E : Î¹ â†’ Type*)
  [Î  i, normed_group (E i)] [Î  i, normed_space ğ•œ (E i)]
  (s : Î  i, set (E i)) (I : set Î¹) (h : âˆ€ i âˆˆ I, unique_diff_on ğ•œ (s i)) :
  unique_diff_on ğ•œ (set.pi I s) :=
Î» x hx, unique_diff_within_at.pi _ _ _ _ _ $ Î» i hi, h i hi (x i) (hx i hi)

/-- The finite product of a family of sets of unique differentiability is a set of unique
differentiability. -/
lemma unique_diff_on.univ_pi (Î¹ : Type*) [fintype Î¹] (E : Î¹ â†’ Type*)
  [Î  i, normed_group (E i)] [Î  i, normed_space ğ•œ (E i)]
  (s : Î  i, set (E i)) (h : âˆ€ i, unique_diff_on ğ•œ (s i)) :
  unique_diff_on ğ•œ (set.pi univ s) :=
unique_diff_on.pi _ _ _ _ $ Î» i _, h i

/-- In a real vector space, a convex set with nonempty interior is a set of unique
differentiability. -/
theorem unique_diff_on_convex {s : set G} (conv : convex â„ s) (hs : (interior s).nonempty) :
  unique_diff_on â„ s :=
begin
  assume x xs,
  rcases hs with âŸ¨y, hyâŸ©,
  suffices : y - x âˆˆ interior (tangent_cone_at â„ s x),
  { refine âŸ¨dense.of_closure _, subset_closure xsâŸ©,
    simp [(submodule.span â„ (tangent_cone_at â„ s x)).eq_top_of_nonempty_interior'
      âŸ¨y - x, interior_mono submodule.subset_span thisâŸ©] },
  rw [mem_interior_iff_mem_nhds] at hy âŠ¢,
  apply mem_of_superset ((is_open_map_sub_right x).image_mem_nhds hy),
  rintros _ âŸ¨z, zs, rflâŸ©,
  exact mem_tangent_cone_of_segment_subset (conv.segment_subset xs zs)
end

lemma unique_diff_on_Ici (a : â„) : unique_diff_on â„ (Ici a) :=
unique_diff_on_convex (convex_Ici a) $ by simp only [interior_Ici, nonempty_Ioi]

lemma unique_diff_on_Iic (a : â„) : unique_diff_on â„ (Iic a) :=
unique_diff_on_convex (convex_Iic a) $ by simp only [interior_Iic, nonempty_Iio]

lemma unique_diff_on_Ioi (a : â„) : unique_diff_on â„ (Ioi a) :=
is_open_Ioi.unique_diff_on

lemma unique_diff_on_Iio (a : â„) : unique_diff_on â„ (Iio a) :=
is_open_Iio.unique_diff_on

lemma unique_diff_on_Icc {a b : â„} (hab : a < b) : unique_diff_on â„ (Icc a b) :=
unique_diff_on_convex (convex_Icc a b) $ by simp only [interior_Icc, nonempty_Ioo, hab]

lemma unique_diff_on_Ico (a b : â„) : unique_diff_on â„ (Ico a b) :=
if hab : a < b
then unique_diff_on_convex (convex_Ico a b) $ by simp only [interior_Ico, nonempty_Ioo, hab]
else by simp only [Ico_eq_empty hab, unique_diff_on_empty]

lemma unique_diff_on_Ioc (a b : â„) : unique_diff_on â„ (Ioc a b) :=
if hab : a < b
then unique_diff_on_convex (convex_Ioc a b) $ by simp only [interior_Ioc, nonempty_Ioo, hab]
else by simp only [Ioc_eq_empty hab, unique_diff_on_empty]

lemma unique_diff_on_Ioo (a b : â„) : unique_diff_on â„ (Ioo a b) :=
is_open_Ioo.unique_diff_on

/-- The real interval `[0, 1]` is a set of unique differentiability. -/
lemma unique_diff_on_Icc_zero_one : unique_diff_on â„ (Icc (0:â„) 1) :=
unique_diff_on_Icc zero_lt_one

end unique_diff
