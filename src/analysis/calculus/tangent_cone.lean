/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.convex.basic
import analysis.normed_space.bounded_linear_maps
import analysis.specific_limits

/-!
# Tangent cone

In this file, we define two predicates `unique_diff_within_at ğ•œ s x` and `unique_diff_on ğ•œ s`
ensuring that, if a function has two derivatives, then they have to coincide. As a direct
definition of this fact (quantifying on all target types and all functions) would depend on
universes, we use a more intrinsic definition: if all the possible tangent directions to the set
`s` at the point `x` span a dense subset of the whole subset, it is easy to check that the
derivative has to be unique.

Therefore, we introduce the set of all tangent directions, named `tangent_cone_at`,
and express `unique_diff_within_at` and `unique_diff_on` in terms of it.
One should however think of this definition as an implementation detail: the only reason to
introduce the predicates `unique_diff_within_at` and `unique_diff_on` is to ensure the uniqueness
of the derivative. This is why their names reflect their uses, and not how they are defined.

## Implementation details

Note that this file is imported by `fderiv.lean`. Hence, derivatives are not defined yet. The
property of uniqueness of the derivative is therefore proved in `fderiv.lean`, but based on the
properties of the tangent cone we prove here.
-/

variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_group F] [normed_space ğ•œ F]
variables {G : Type*} [normed_group G] [normed_space â„ G]

open filter set
open_locale topological_space

/-- The set of all tangent directions to the set `s` at the point `x`. -/
def tangent_cone_at (s : set E) (x : E) : set E :=
{y : E | âˆƒ(c : â„• â†’ ğ•œ) (d : â„• â†’ E), (âˆ€á¶  n in at_top, x + d n âˆˆ s) âˆ§
  (tendsto (Î»n, âˆ¥c nâˆ¥) at_top at_top) âˆ§ (tendsto (Î»n, c n â€¢ d n) at_top (ğ“ y))}

/-- A property ensuring that the tangent cone to `s` at `x` spans a dense subset of the whole space.
The main role of this property is to ensure that the differential within `s` at `x` is unique,
hence this name. The uniqueness it asserts is proved in `unique_diff_within_at.eq` in `fderiv.lean`.
To avoid pathologies in dimension 0, we also require that `x` belongs to the closure of `s` (which
is automatic when `E` is not `0`-dimensional).
 -/
def unique_diff_within_at (s : set E) (x : E) : Prop :=
closure ((submodule.span ğ•œ (tangent_cone_at ğ•œ s x)) : set E) = univ âˆ§ x âˆˆ closure s

/-- A property ensuring that the tangent cone to `s` at any of its points spans a dense subset of
the whole space.  The main role of this property is to ensure that the differential along `s` is
unique, hence this name. The uniqueness it asserts is proved in `unique_diff_on.eq` in
`fderiv.lean`. -/
def unique_diff_on (s : set E) : Prop :=
âˆ€x âˆˆ s, unique_diff_within_at ğ•œ s x

variables {ğ•œ} {x y : E} {s t : set E}

section tangent_cone
/- This section is devoted to the properties of the tangent cone. -/

open normed_field

lemma tangent_cone_univ : tangent_cone_at ğ•œ univ x = univ :=
begin
  refine univ_subset_iff.1 (Î»y hy, _),
  rcases exists_one_lt_norm ğ•œ with âŸ¨w, hwâŸ©,
  refine âŸ¨Î»n, w^n, Î»n, (w^n)â»Â¹ â€¢ y, univ_mem_sets' (Î»n, mem_univ _),  _, _âŸ©,
  { simp only [norm_pow],
    exact tendsto_pow_at_top_at_top_of_one_lt hw },
  { convert tendsto_const_nhds,
    ext n,
    have : w ^ n * (w ^ n)â»Â¹ = 1,
    { apply mul_inv_cancel,
      apply pow_ne_zero,
      simpa [norm_eq_zero] using (ne_of_lt (lt_trans zero_lt_one hw)).symm },
    rw [smul_smul, this, one_smul] }
end

lemma tangent_cone_mono (h : s âŠ† t) :
  tangent_cone_at ğ•œ s x âŠ† tangent_cone_at ğ•œ t x :=
begin
  rintros y âŸ¨c, d, ds, ctop, climâŸ©,
  exact âŸ¨c, d, mem_sets_of_superset ds (Î»n hn, h hn), ctop, climâŸ©
end

/-- Auxiliary lemma ensuring that, under the assumptions defining the tangent cone,
the sequence `d` tends to 0 at infinity. -/
lemma tangent_cone_at.lim_zero {Î± : Type*} (l : filter Î±) {c : Î± â†’ ğ•œ} {d : Î± â†’ E}
  (hc : tendsto (Î»n, âˆ¥c nâˆ¥) l at_top) (hd : tendsto (Î»n, c n â€¢ d n) l (ğ“ y)) :
  tendsto d l (ğ“ 0) :=
begin
  have A : tendsto (Î»n, âˆ¥c nâˆ¥â»Â¹) l (ğ“ 0) := tendsto_inv_at_top_zero.comp hc,
  have B : tendsto (Î»n, âˆ¥c n â€¢ d nâˆ¥) l (ğ“ âˆ¥yâˆ¥) :=
    (continuous_norm.tendsto _).comp hd,
  have C : tendsto (Î»n, âˆ¥c nâˆ¥â»Â¹ * âˆ¥c n â€¢ d nâˆ¥) l (ğ“ (0 * âˆ¥yâˆ¥)) := A.mul B,
  rw zero_mul at C,
  have : âˆ€á¶  n in l, âˆ¥c nâˆ¥â»Â¹ * âˆ¥c n â€¢ d nâˆ¥ = âˆ¥d nâˆ¥,
  { apply (eventually_ne_of_tendsto_norm_at_top hc 0).mono (Î»n hn, _),
    rw [norm_smul, â† mul_assoc, inv_mul_cancel, one_mul],
    rwa [ne.def, norm_eq_zero] },
  have D : tendsto (Î» n, âˆ¥d nâˆ¥) l (ğ“ 0) :=
    tendsto.congr' this C,
  rw tendsto_zero_iff_norm_tendsto_zero,
  exact D
end

lemma tangent_cone_mono_nhds (h : nhds_within x s â‰¤ nhds_within x t) :
  tangent_cone_at ğ•œ s x âŠ† tangent_cone_at ğ•œ t x :=
begin
  rintros y âŸ¨c, d, ds, ctop, climâŸ©,
  refine âŸ¨c, d, _, ctop, climâŸ©,
  suffices : tendsto (Î» n, x + d n) at_top (nhds_within x t),
    from tendsto_principal.1 (tendsto_inf.1 this).2,
  apply tendsto_le_right h,
  refine tendsto_inf.2 âŸ¨_, tendsto_principal.2 dsâŸ©,
  simpa only [add_zero] using tendsto_const_nhds.add (tangent_cone_at.lim_zero at_top ctop clim)
end

/-- Tangent cone of `s` at `x` depends only on `nhds_within x s`. -/
lemma tangent_cone_congr (h : nhds_within x s = nhds_within x t) :
  tangent_cone_at ğ•œ s x = tangent_cone_at ğ•œ t x :=
subset.antisymm
  (tangent_cone_mono_nhds $ le_of_eq h)
  (tangent_cone_mono_nhds $ le_of_eq h.symm)

/-- Intersecting with a neighborhood of the point does not change the tangent cone. -/
lemma tangent_cone_inter_nhds (ht : t âˆˆ ğ“ x) :
  tangent_cone_at ğ•œ (s âˆ© t) x = tangent_cone_at ğ•œ s x :=
tangent_cone_congr (nhds_within_restrict' _ ht).symm

/-- The tangent cone of a product contains the tangent cone of its left factor. -/
lemma subset_tangent_cone_prod_left {t : set F} {y : F} (ht : y âˆˆ closure t) :
  prod.inl '' (tangent_cone_at ğ•œ s x) âŠ† tangent_cone_at ğ•œ (set.prod s t) (x, y) :=
begin
  rintros _ âŸ¨v, âŸ¨c, d, hd, hc, hyâŸ©, rflâŸ©,
  have : âˆ€n, âˆƒd', y + d' âˆˆ t âˆ§ âˆ¥c n â€¢ d'âˆ¥ â‰¤ ((1:â„)/2)^n,
  { assume n,
    have c_pos : 0 < 1 + âˆ¥c nâˆ¥ :=
      add_pos_of_pos_of_nonneg zero_lt_one (norm_nonneg _),
    rcases metric.mem_closure_iff.1 ht ((1 + âˆ¥c nâˆ¥)â»Â¹ * (1/2)^n) _ with âŸ¨z, z_pos, hzâŸ©,
    refine âŸ¨z - y, _, _âŸ©,
    { convert z_pos, abel },
    { rw [norm_smul, â† dist_eq_norm, dist_comm],
      calc âˆ¥c nâˆ¥ * dist y z â‰¤ (1 + âˆ¥c nâˆ¥) * ((1 + âˆ¥c nâˆ¥)â»Â¹ * (1/2)^n) :
      begin
        apply mul_le_mul _ (le_of_lt hz) dist_nonneg (le_of_lt c_pos),
        simp only [zero_le_one, le_add_iff_nonneg_left]
      end
      ... = (1/2)^n :
      begin
        rw [â† mul_assoc, mul_inv_cancel, one_mul],
        exact ne_of_gt c_pos
      end },
    { apply mul_pos (inv_pos.2 c_pos) (pow_pos _ _),
      norm_num } },
  choose d' hd' using this,
  refine âŸ¨c, Î»n, (d n, d' n), _, hc, _âŸ©,
  show âˆ€á¶  n in at_top, (x, y) + (d n, d' n) âˆˆ set.prod s t,
  { apply filter.mem_sets_of_superset hd,
    assume n hn,
    simp at hn,
    simp [hn, (hd' n).1] },
  { apply hy.prod_mk_nhds,
    change tendsto (Î» (n : â„•), c n â€¢ d' n) at_top (ğ“ 0),
    rw tendsto_zero_iff_norm_tendsto_zero,
    refine squeeze_zero (Î»n, norm_nonneg _) (Î»n, (hd' n).2) _,
    apply tendsto_pow_at_top_nhds_0_of_lt_1; norm_num }
end

/-- The tangent cone of a product contains the tangent cone of its right factor. -/
lemma subset_tangent_cone_prod_right {t : set F} {y : F}
  (hs : x âˆˆ closure s) :
  prod.inr '' (tangent_cone_at ğ•œ t y) âŠ† tangent_cone_at ğ•œ (set.prod s t) (x, y) :=
begin
  rintros _ âŸ¨w, âŸ¨c, d, hd, hc, hyâŸ©, rflâŸ©,
  have : âˆ€n, âˆƒd', x + d' âˆˆ s âˆ§ âˆ¥c n â€¢ d'âˆ¥ â‰¤ ((1:â„)/2)^n,
  { assume n,
    have c_pos : 0 < 1 + âˆ¥c nâˆ¥ :=
      add_pos_of_pos_of_nonneg zero_lt_one (norm_nonneg _),
    rcases metric.mem_closure_iff.1 hs ((1 + âˆ¥c nâˆ¥)â»Â¹ * (1/2)^n) _ with âŸ¨z, z_pos, hzâŸ©,
    refine âŸ¨z - x, _, _âŸ©,
    { convert z_pos, abel },
    { rw [norm_smul, â† dist_eq_norm, dist_comm],
      calc âˆ¥c nâˆ¥ * dist x z â‰¤ (1 + âˆ¥c nâˆ¥) * ((1 + âˆ¥c nâˆ¥)â»Â¹ * (1/2)^n) :
      begin
        apply mul_le_mul _ (le_of_lt hz) dist_nonneg (le_of_lt c_pos),
        simp only [zero_le_one, le_add_iff_nonneg_left]
      end
      ... = (1/2)^n :
      begin
        rw [â† mul_assoc, mul_inv_cancel, one_mul],
        exact ne_of_gt c_pos
      end },
    { apply mul_pos (inv_pos.2 c_pos) (pow_pos _ _),
      norm_num } },
  choose d' hd' using this,
  refine âŸ¨c, Î»n, (d' n, d n), _, hc, _âŸ©,
  show âˆ€á¶  n in at_top, (x, y) + (d' n, d n) âˆˆ set.prod s t,
  { apply filter.mem_sets_of_superset hd,
    assume n hn,
    simp at hn,
    simp [hn, (hd' n).1] },
  { apply tendsto.prod_mk_nhds _ hy,
    change tendsto (Î» (n : â„•), c n â€¢ d' n) at_top (ğ“ 0),
    rw tendsto_zero_iff_norm_tendsto_zero,
    refine squeeze_zero (Î»n, norm_nonneg _) (Î»n, (hd' n).2) _,
    apply tendsto_pow_at_top_nhds_0_of_lt_1; norm_num }
end

/-- If a subset of a real vector space contains a segment, then the direction of this
segment belongs to the tangent cone at its endpoints. -/
lemma mem_tangent_cone_of_segment_subset {s : set G} {x y : G} (h : segment x y âŠ† s) :
  y - x âˆˆ tangent_cone_at â„ s x :=
begin
  let w : â„ := 2,
  let c := Î»n:â„•, (2:â„)^n,
  let d := Î»n:â„•, (c n)â»Â¹ â€¢ (y-x),
  refine âŸ¨c, d, filter.univ_mem_sets' (Î»n, h _), _, _âŸ©,
  show x + d n âˆˆ segment x y,
  { rw segment_eq_image,
    refine âŸ¨(c n)â»Â¹, âŸ¨_, _âŸ©, _âŸ©,
    { rw inv_nonneg, apply pow_nonneg, norm_num },
    { apply inv_le_one, apply one_le_pow_of_one_le, norm_num },
    { simp only [d, sub_smul, smul_sub, one_smul], abel } },
  show filter.tendsto (Î» (n : â„•), âˆ¥c nâˆ¥) filter.at_top filter.at_top,
  { have : (Î» (n : â„•), âˆ¥c nâˆ¥) = c,
      by { ext n, exact abs_of_nonneg (pow_nonneg (by norm_num) _) },
    rw this,
    exact tendsto_pow_at_top_at_top_of_one_lt (by norm_num) },
  show filter.tendsto (Î» (n : â„•), c n â€¢ d n) filter.at_top (ğ“ (y - x)),
  { have : (Î» (n : â„•), c n â€¢ d n) = (Î»n, y - x),
    { ext n,
      simp only [d, smul_smul],
      rw [mul_inv_cancel, one_smul],
      exact pow_ne_zero _ (by norm_num) },
    rw this,
    apply tendsto_const_nhds }
end

end tangent_cone

section unique_diff
/- This section is devoted to properties of the predicates `unique_diff_within_at` and
`unique_diff_on`. -/

lemma unique_diff_within_at_univ : unique_diff_within_at ğ•œ univ x :=
by { rw [unique_diff_within_at, tangent_cone_univ], simp }

lemma unique_diff_on_univ : unique_diff_on ğ•œ (univ : set E) :=
Î»x hx, unique_diff_within_at_univ

lemma unique_diff_on_empty : unique_diff_on ğ•œ (âˆ… : set E) :=
Î» x hx, hx.elim

lemma unique_diff_within_at.mono_nhds (h : unique_diff_within_at ğ•œ s x)
  (st : nhds_within x s â‰¤ nhds_within x t) :
  unique_diff_within_at ğ•œ t x :=
begin
  unfold unique_diff_within_at at *,
  rw [â† univ_subset_iff, â† h.1],
  rw [mem_closure_iff_nhds_within_ne_bot] at h âŠ¢,
  exact âŸ¨closure_mono (submodule.span_mono (tangent_cone_mono_nhds st)),
    ne_bot_of_le_ne_bot h.2 stâŸ©
end

lemma unique_diff_within_at.mono (h : unique_diff_within_at ğ•œ s x) (st : s âŠ† t) :
  unique_diff_within_at ğ•œ t x :=
h.mono_nhds $ nhds_within_mono _ st

lemma unique_diff_within_at_congr (st : nhds_within x s = nhds_within x t) :
  unique_diff_within_at ğ•œ s x â†” unique_diff_within_at ğ•œ t x :=
âŸ¨Î» h, h.mono_nhds $ le_of_eq st, Î» h, h.mono_nhds $ le_of_eq st.symmâŸ©

lemma unique_diff_within_at_inter (ht : t âˆˆ ğ“ x) :
  unique_diff_within_at ğ•œ (s âˆ© t) x â†” unique_diff_within_at ğ•œ s x :=
unique_diff_within_at_congr $ (nhds_within_restrict' _ ht).symm

lemma unique_diff_within_at.inter (hs : unique_diff_within_at ğ•œ s x) (ht : t âˆˆ ğ“ x) :
  unique_diff_within_at ğ•œ (s âˆ© t) x :=
(unique_diff_within_at_inter ht).2 hs

lemma unique_diff_within_at_inter' (ht : t âˆˆ nhds_within x s) :
  unique_diff_within_at ğ•œ (s âˆ© t) x â†” unique_diff_within_at ğ•œ s x :=
unique_diff_within_at_congr $ (nhds_within_restrict'' _ ht).symm

lemma unique_diff_within_at.inter' (hs : unique_diff_within_at ğ•œ s x) (ht : t âˆˆ nhds_within x s) :
  unique_diff_within_at ğ•œ (s âˆ© t) x :=
(unique_diff_within_at_inter' ht).2 hs

lemma is_open.unique_diff_within_at (hs : is_open s) (xs : x âˆˆ s) : unique_diff_within_at ğ•œ s x :=
begin
  have := unique_diff_within_at_univ.inter (mem_nhds_sets hs xs),
  rwa univ_inter at this
end

lemma unique_diff_on.inter (hs : unique_diff_on ğ•œ s) (ht : is_open t) : unique_diff_on ğ•œ (s âˆ© t) :=
Î»x hx, (hs x hx.1).inter (mem_nhds_sets ht hx.2)

lemma is_open.unique_diff_on (hs : is_open s) : unique_diff_on ğ•œ s :=
Î»x hx, is_open.unique_diff_within_at hs hx

/-- The product of two sets of unique differentiability at points `x` and `y` has unique
differentiability at `(x, y)`. -/
lemma unique_diff_within_at.prod {t : set F} {y : F}
  (hs : unique_diff_within_at ğ•œ s x) (ht : unique_diff_within_at ğ•œ t y) :
  unique_diff_within_at ğ•œ (set.prod s t) (x, y) :=
begin
  rw [unique_diff_within_at] at âŠ¢ hs ht,
  rw [â† univ_subset_iff, closure_prod_eq],
  refine âŸ¨_, hs.2, ht.2âŸ©,
  have : _ âŠ† tangent_cone_at ğ•œ (s.prod t) (x, y) :=
    union_subset (subset_tangent_cone_prod_left ht.2) (subset_tangent_cone_prod_right hs.2),
  refine subset.trans _ (closure_mono $ submodule.span_mono this),
  erw [linear_map.span_inl_union_inr, submodule.prod_coe, closure_prod_eq,
    hs.1, ht.1, univ_prod_univ]
end

/-- The product of two sets of unique differentiability is a set of unique differentiability. -/
lemma unique_diff_on.prod {t : set F} (hs : unique_diff_on ğ•œ s) (ht : unique_diff_on ğ•œ t) :
  unique_diff_on ğ•œ (set.prod s t) :=
Î» âŸ¨x, yâŸ© h, unique_diff_within_at.prod (hs x h.1) (ht y h.2)

/-- In a real vector space, a convex set with nonempty interior is a set of unique
differentiability. -/
theorem unique_diff_on_convex {s : set G} (conv : convex s) (hs : (interior s).nonempty) :
  unique_diff_on â„ s :=
begin
  assume x xs,
  rcases hs with âŸ¨y, hyâŸ©,
  suffices : y - x âˆˆ interior (tangent_cone_at â„ s x),
  { refine âŸ¨_, subset_closure xsâŸ©,
    rw [submodule.eq_top_of_nonempty_interior' _ âŸ¨y - x, interior_mono submodule.subset_span thisâŸ©,
      submodule.top_coe, closure_univ]; apply_instance },
  rw [mem_interior_iff_mem_nhds] at hy âŠ¢,
  apply mem_sets_of_superset ((is_open_map_add_right (-x)).image_mem_nhds hy),
  rintros _ âŸ¨z, zs, rflâŸ©,
  exact mem_tangent_cone_of_segment_subset (conv.segment_subset xs zs)
end

lemma unique_diff_on_Ici (a : â„) : unique_diff_on â„ (Ici a) :=
unique_diff_on_convex (convex_Ici a) $ by simp only [interior_Ici, nonempty_Ioi]

lemma unique_diff_on_Iic (a : â„) : unique_diff_on â„ (Iic a) :=
unique_diff_on_convex (convex_Iic a) $ by simp only [interior_Iic, nonempty_Iio]

lemma unique_diff_on_Ioi (a : â„) : unique_diff_on â„ (Ioi a) :=
is_open_Ioi.unique_diff_on

lemma unique_diff_on_Iio (a : â„) : unique_diff_on â„ (Iio a) :=
is_open_Iio.unique_diff_on

lemma unique_diff_on_Icc {a b : â„} (hab : a < b) : unique_diff_on â„ (Icc a b) :=
unique_diff_on_convex (convex_Icc a b) $ by simp only [interior_Icc, nonempty_Ioo, hab]

lemma unique_diff_on_Ico (a b : â„) : unique_diff_on â„ (Ico a b) :=
if hab : a < b
then unique_diff_on_convex (convex_Ico a b) $ by simp only [interior_Ico, nonempty_Ioo, hab]
else by simp only [Ico_eq_empty (le_of_not_lt hab), unique_diff_on_empty]

lemma unique_diff_on_Ioc (a b : â„) : unique_diff_on â„ (Ioc a b) :=
if hab : a < b
then unique_diff_on_convex (convex_Ioc a b) $ by simp only [interior_Ioc, nonempty_Ioo, hab]
else by simp only [Ioc_eq_empty (le_of_not_lt hab), unique_diff_on_empty]

lemma unique_diff_on_Ioo (a b : â„) : unique_diff_on â„ (Ioo a b) :=
is_open_Ioo.unique_diff_on

/-- The real interval `[0, 1]` is a set of unique differentiability. -/
lemma unique_diff_on_Icc_zero_one : unique_diff_on â„ (Icc (0:â„) 1) :=
unique_diff_on_Icc zero_lt_one

end unique_diff
