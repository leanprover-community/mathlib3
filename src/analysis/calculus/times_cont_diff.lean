/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.calculus.mean_value
import analysis.normed_space.multilinear

/-!
# Higher differentiability

A function is `C^1` on a domain if it is differentiable there, and its derivative is continuous.
By induction, it is `C^n` if it is `C^{n-1}` and its (n-1)-th derivative is `C^1` there or,
equivalently, if it is `C^1` and its derivative is `C^{n-1}`.
Finally, it is `C^âˆ` if it is `C^n` for all n.

We formalize these notions by defining iteratively the `n+1`-th derivative of a function as the
derivative of the `n`-th derivative. It is called `iterated_fderiv ğ•œ n f x` where `ğ•œ` is the
field, `n` is the number of iterations, `f` is the function and `x` is the point, and it is given
as an `n`-multilinear map. We also define a version `iterated_fderiv_within` relative to a domain,
as well as predicates `times_cont_diff_within_at`, `times_cont_diff_at`, `times_cont_diff_on` and
`times_cont_diff` saying that the function is `C^n` within a set at a point, at a point, on a set
and on the whole space respectively.

To avoid the issue of choice when choosing a derivative in sets where the derivative is not
necessarily unique, `times_cont_diff_on` is not defined directly in terms of the
regularity of the specific choice `iterated_fderiv_within ğ•œ n f s` inside `s`, but in terms of the
existence of a nice sequence of derivatives, expressed with a predicate
`has_ftaylor_series_up_to_on`.

We prove basic properties of these notions.

## Main definitions and results
Let `f : E â†’ F` be a map between normed vector spaces over a nondiscrete normed field `ğ•œ`.

* `has_ftaylor_series_up_to n f p`: expresses that the formal multilinear series `p` is a sequence
  of iterated derivatives of `f`, up to the `n`-th term (where `n` is a natural number or `âˆ`).
* `has_ftaylor_series_up_to_on n f p s`: same thing, but inside a set `s`. The notion of derivative
  is now taken inside `s`. In particular, derivatives don't have to be unique.
* `times_cont_diff ğ•œ n f`: expresses that `f` is `C^n`, i.e., it admits a Taylor series up to
  rank `n`.
* `times_cont_diff_on ğ•œ n f s`: expresses that `f` is `C^n` in `s`.
* `times_cont_diff_at ğ•œ n f x`: expresses that `f` is `C^n` around `x`.
* `times_cont_diff_within_at ğ•œ n f s x`: expresses that `f` is `C^n` around `x` within the set `s`.
* `iterated_fderiv_within ğ•œ n f s x` is an `n`-th derivative of `f` over the field `ğ•œ` on the
  set `s` at the point `x`. It is a continuous multilinear map from `E^n` to `F`, defined as a
  derivative within `s` of `iterated_fderiv_within ğ•œ (n-1) f s` if one exists, and `0` otherwise.
* `iterated_fderiv ğ•œ n f x` is the `n`-th derivative of `f` over the field `ğ•œ` at the point `x`.
  It is a continuous multilinear map from `E^n` to `F`, defined as a derivative of
  `iterated_fderiv ğ•œ (n-1) f` if one exists, and `0` otherwise.

In sets of unique differentiability, `times_cont_diff_on ğ•œ n f s` can be expressed in terms of the
properties of `iterated_fderiv_within ğ•œ m f s` for `m â‰¤ n`. In the whole space,
`times_cont_diff ğ•œ n f` can be expressed in terms of the properties of `iterated_fderiv ğ•œ m f`
for `m â‰¤ n`.

We also prove that the usual operations (addition, multiplication, difference, composition, and
so on) preserve `C^n` functions.

## Implementation notes

The definitions in this file are designed to work on any field `ğ•œ`. They are sometimes slightly more
complicated than the naive definitions one would guess from the intuition over the real or complex
numbers, but they are designed to circumvent the lack of gluing properties and partitions of unity
in general. In the usual situations, they coincide with the usual definitions.

### Definition of `C^n` functions in domains

One could define `C^n` functions in a domain `s` by fixing an arbitrary choice of derivatives (this
is what we do with `iterated_fderiv_within`) and requiring that all these derivatives up to `n` are
continuous. If the derivative is not unique, this could lead to strange behavior like two `C^n`
functions `f` and `g` on `s` whose sum is not `C^n`. A better definition is thus to say that a
function is `C^n` inside `s` if it admits a sequence of derivatives up to `n` inside `s`.

This definition still has the problem that a function which is locally `C^n` would not need to
be `C^n`, as different choices of sequences of derivatives around different points might possibly
not be glued together to give a globally defined sequence of derivatives. (Note that this issue
can not happen over reals, thanks to partition of unity, but the behavior over a general field is
not so clear, and we want a definition for general fields). Also, there are locality
problems for the order parameter: one could image a function which, for each `n`, has a nice
sequence of derivatives up to order `n`, but they do not coincide for varying `n` and can therefore
not be  glued to give rise to an infinite sequence of derivatives. This would give a function
which is `C^n` for all `n`, but not `C^âˆ`. We solve this issue by putting locality conditions
in space and order in our definition of `times_cont_diff_within_at` and `times_cont_diff_on`.
The resulting definition is slightly more complicated to work with (in fact not so much), but it
gives rise to completely satisfactory theorems.

For instance, with this definition, a real function which is `C^m` (but not better) on `(-1/m, 1/m)`
for each natural `m` is by definition `C^âˆ` at `0`.

There is another issue with the definition of `times_cont_diff_within_at ğ•œ n f s x`. We can
require the existence and good behavior of derivatives up to order `n` on a neighborhood of `x`
within `s`. However, this does not imply continuity or differentiability within `s` of the function
at `x` when `x` does not belong to `s`. Therefore, we require such existence and good behavior on
a neighborhood of `x` within `s âˆª {x}` (which appears as `insert x s` in this file).

### Side of the composition, and universe issues

With a naÃ¯ve direct definition, the `n`-th derivative of a function belongs to the space
`E â†’L[ğ•œ] (E â†’L[ğ•œ] (E ... F)...)))` where there are n iterations of `E â†’L[ğ•œ]`. This space
may also be seen as the space of continuous multilinear functions on `n` copies of `E` with
values in `F`, by uncurrying. This is the point of view that is usually adopted in textbooks,
and that we also use. This means that the definition and the first proofs are slightly involved,
as one has to keep track of the uncurrying operation. The uncurrying can be done from the
left or from the right, amounting to defining the `n+1`-th derivative either as the derivative of
the `n`-th derivative, or as the `n`-th derivative of the derivative.
For proofs, it would be more convenient to use the latter approach (from the right),
as it means to prove things at the `n+1`-th step we only need to understand well enough the
derivative in `E â†’L[ğ•œ] F` (contrary to the approach from the left, where one would need to know
enough on the `n`-th derivative to deduce things on the `n+1`-th derivative).

However, the definition from the right leads to a universe polymorphism problem: if we define
`iterated_fderiv ğ•œ (n + 1) f x = iterated_fderiv ğ•œ n (fderiv ğ•œ f) x` by induction, we need to
generalize over all spaces (as `f` and `fderiv ğ•œ f` don't take values in the same space). It is
only possible to generalize over all spaces in some fixed universe in an inductive definition.
For `f : E â†’ F`, then `fderiv ğ•œ f` is a map `E â†’ (E â†’L[ğ•œ] F)`. Therefore, the definition will only
work if `F` and `E â†’L[ğ•œ] F` are in the same universe.

This issue does not appear with the definition from the left, where one does not need to generalize
over all spaces. Therefore, we use the definition from the left. This means some proofs later on
become a little bit more complicated: to prove that a function is `C^n`, the most efficient approach
is to exhibit a formula for its `n`-th derivative and prove it is continuous (contrary to the
inductive approach where one would prove smoothness statements without giving a formula for the
derivative). In the end, this approach is still satisfactory as it is good to have formulas for the
iterated derivatives in various constructions.

One point where we depart from this explicit approach is in the proof of smoothness of a
composition: there is a formula for the `n`-th derivative of a composition (FaÃ  di Bruno's formula),
but it is very complicated and barely usable, while the inductive proof is very simple. Thus, we
give the inductive proof. As explained above, it works by generalizing over the target space, hence
it only works well if all spaces belong to the same universe. To get the general version, we lift
things to a common universe using a trick.

### Variables management

The textbook definitions and proofs use various identifications and abuse of notations, for instance
when saying that the natural space in which the derivative lives, i.e.,
`E â†’L[ğ•œ] (E â†’L[ğ•œ] ( ... â†’L[ğ•œ] F))`, is the same as a space of multilinear maps. When doing things
formally, we need to provide explicit maps for these identifications, and chase some diagrams to see
everything is compatible with the identifications. In particular, one needs to check that taking the
derivative and then doing the identification, or first doing the identification and then taking the
derivative, gives the same result. The key point for this is that taking the derivative commutes
with continuous linear equivalences. Therefore, we need to implement all our identifications with
continuous linear equivs.

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

In this file, we denote `âŠ¤ : with_top â„•` with `âˆ`.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

noncomputable theory
open_locale classical big_operators nnreal

local notation `âˆ` := (âŠ¤ : with_top â„•)

universes u v w

local attribute [instance, priority 1001]
normed_group.to_add_comm_group normed_space.to_module add_comm_group.to_add_comm_monoid

open set fin filter
open_locale topological_space

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{F : Type*} [normed_group F] [normed_space ğ•œ F]
{G : Type*} [normed_group G] [normed_space ğ•œ G]
{s sâ‚ t u : set E} {f fâ‚ : E â†’ F} {g : F â†’ G} {x : E} {c : F}
{b : E Ã— F â†’ G}

/-! ### Functions with a Taylor series on a domain -/

variable {p : E â†’ formal_multilinear_series ğ•œ E F}

/-- `has_ftaylor_series_up_to_on n f p s` registers the fact that `p 0 = f` and `p (m+1)` is a
derivative of `p m` for `m < n`, and is continuous for `m â‰¤ n`. This is a predicate analogous to
`has_fderiv_within_at` but for higher order derivatives. -/
structure has_ftaylor_series_up_to_on (n : with_top â„•)
  (f : E â†’ F) (p : E â†’ formal_multilinear_series ğ•œ E F) (s : set E) : Prop :=
(zero_eq       : âˆ€ x âˆˆ s, (p x 0).uncurry0 = f x)
(fderiv_within : âˆ€ (m : â„•) (hm : (m : with_top â„•) < n), âˆ€ x âˆˆ s,
   has_fderiv_within_at (Î» y, p y m) (p x m.succ).curry_left s x)
(cont          : âˆ€ (m : â„•) (hm : (m : with_top â„•) â‰¤ n), continuous_on (Î» x, p x m) s)

lemma has_ftaylor_series_up_to_on.zero_eq' {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) {x : E} (hx : x âˆˆ s) :
  p x 0 = (continuous_multilinear_curry_fin0 ğ•œ E F).symm (f x) :=
by { rw â† h.zero_eq x hx, symmetry, exact continuous_multilinear_map.uncurry0_curry0 _ }

/-- If two functions coincide on a set `s`, then a Taylor series for the first one is as well a
Taylor series for the second one. -/
lemma has_ftaylor_series_up_to_on.congr {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) :
  has_ftaylor_series_up_to_on n fâ‚ p s :=
begin
  refine âŸ¨Î» x hx, _, h.fderiv_within, h.contâŸ©,
  rw hâ‚ x hx,
  exact h.zero_eq x hx
end

lemma has_ftaylor_series_up_to_on.mono {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) {t : set E} (hst : t âŠ† s) :
  has_ftaylor_series_up_to_on n f p t :=
âŸ¨Î» x hx, h.zero_eq x (hst hx),
Î» m hm x hx, (h.fderiv_within m hm x (hst hx)).mono hst,
Î» m hm, (h.cont m hm).mono hstâŸ©

lemma has_ftaylor_series_up_to_on.of_le {m n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) (hmn : m â‰¤ n) :
  has_ftaylor_series_up_to_on m f p s :=
âŸ¨h.zero_eq,
Î» k hk x hx, h.fderiv_within k (lt_of_lt_of_le hk hmn) x hx,
Î» k hk, h.cont k (le_trans hk hmn)âŸ©

lemma has_ftaylor_series_up_to_on.continuous_on {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) : continuous_on f s :=
begin
  have := (h.cont 0 bot_le).congr (Î» x hx, (h.zero_eq' hx).symm),
  rwa linear_isometry_equiv.comp_continuous_on_iff at this
end

lemma has_ftaylor_series_up_to_on_zero_iff :
  has_ftaylor_series_up_to_on 0 f p s â†” continuous_on f s âˆ§ (âˆ€ x âˆˆ s, (p x 0).uncurry0 = f x) :=
begin
  refine âŸ¨Î» H, âŸ¨H.continuous_on, H.zero_eqâŸ©,
          Î» H, âŸ¨H.2, Î» m hm, false.elim (not_le.2 hm bot_le), _âŸ©âŸ©,
  assume m hm,
  obtain rfl : m = 0, by exact_mod_cast (hm.antisymm (zero_le _)),
  have : âˆ€ x âˆˆ s, p x 0 = (continuous_multilinear_curry_fin0 ğ•œ E F).symm (f x),
    by { assume x hx, rw â† H.2 x hx, symmetry, exact continuous_multilinear_map.uncurry0_curry0 _ },
  rw [continuous_on_congr this, linear_isometry_equiv.comp_continuous_on_iff],
  exact H.1
end

lemma has_ftaylor_series_up_to_on_top_iff :
  (has_ftaylor_series_up_to_on âˆ f p s) â†” (âˆ€ (n : â„•), has_ftaylor_series_up_to_on n f p s) :=
begin
  split,
  { assume H n, exact H.of_le le_top },
  { assume H,
    split,
    { exact (H 0).zero_eq },
    { assume m hm,
      apply (H m.succ).fderiv_within m (with_top.coe_lt_coe.2 (lt_add_one m)) },
    { assume m hm,
      apply (H m).cont m (le_refl _) } }
end

/-- If a function has a Taylor series at order at least `1`, then the term of order `1` of this
series is a derivative of `f`. -/
lemma has_ftaylor_series_up_to_on.has_fderiv_within_at {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) (hn : 1 â‰¤ n) (hx : x âˆˆ s) :
  has_fderiv_within_at f (continuous_multilinear_curry_fin1 ğ•œ E F (p x 1)) s x :=
begin
  have A : âˆ€ y âˆˆ s, f y = (continuous_multilinear_curry_fin0 ğ•œ E F) (p y 0),
  { assume y hy, rw â† h.zero_eq y hy, refl },
  suffices H : has_fderiv_within_at
      (Î» y, continuous_multilinear_curry_fin0 ğ•œ E F (p y 0))
      (continuous_multilinear_curry_fin1 ğ•œ E F (p x 1)) s x,
    by exact H.congr A (A x hx),
  rw linear_isometry_equiv.comp_has_fderiv_within_at_iff',
  have : ((0 : â„•) : with_top â„•) < n :=
    lt_of_lt_of_le (with_top.coe_lt_coe.2 nat.zero_lt_one) hn,
  convert h.fderiv_within _ this x hx,
  ext y v,
  change (p x 1) (snoc 0 y) = (p x 1) (cons y v),
  unfold_coes,
  congr' with i,
  rw unique.eq_default i,
  refl
end

lemma has_ftaylor_series_up_to_on.differentiable_on {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) (hn : 1 â‰¤ n) : differentiable_on ğ•œ f s :=
Î» x hx, (h.has_fderiv_within_at hn hx).differentiable_within_at

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then the term
of order `1` of this series is a derivative of `f` at `x`. -/
lemma has_ftaylor_series_up_to_on.has_fderiv_at {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) (hn : 1 â‰¤ n) (hx : s âˆˆ ğ“ x) :
  has_fderiv_at f (continuous_multilinear_curry_fin1 ğ•œ E F (p x 1)) x :=
(h.has_fderiv_within_at hn (mem_of_mem_nhds hx)).has_fderiv_at hx

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then
in a neighborhood of `x`, the term of order `1` of this series is a derivative of `f`. -/
lemma has_ftaylor_series_up_to_on.eventually_has_fderiv_at {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) (hn : 1 â‰¤ n) (hx : s âˆˆ ğ“ x) :
  âˆ€á¶  y in ğ“ x, has_fderiv_at f (continuous_multilinear_curry_fin1 ğ•œ E F (p y 1)) y :=
(eventually_eventually_nhds.2 hx).mono $ Î» y hy, h.has_fderiv_at hn hy

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then
it is differentiable at `x`. -/
lemma has_ftaylor_series_up_to_on.differentiable_at {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s) (hn : 1 â‰¤ n) (hx : s âˆˆ ğ“ x) :
  differentiable_at ğ•œ f x :=
(h.has_fderiv_at hn hx).differentiable_at

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p` is a Taylor series up to `n`, and
`p (n + 1)` is a derivative of `p n`. -/
theorem has_ftaylor_series_up_to_on_succ_iff_left {n : â„•} :
  has_ftaylor_series_up_to_on (n + 1) f p s â†”
  has_ftaylor_series_up_to_on n f p s
  âˆ§ (âˆ€ x âˆˆ s, has_fderiv_within_at (Î» y, p y n) (p x n.succ).curry_left s x)
  âˆ§ continuous_on (Î» x, p x (n + 1)) s :=
begin
  split,
  { assume h,
    exact âŸ¨h.of_le (with_top.coe_le_coe.2 (nat.le_succ n)),
           h.fderiv_within _ (with_top.coe_lt_coe.2 (lt_add_one n)),
           h.cont (n + 1) (le_refl _)âŸ© },
  { assume h,
    split,
    { exact h.1.zero_eq },
    { assume m hm,
      by_cases h' : m < n,
      { exact h.1.fderiv_within m (with_top.coe_lt_coe.2 h') },
      { have : m = n := nat.eq_of_lt_succ_of_not_lt (with_top.coe_lt_coe.1 hm) h',
        rw this,
        exact h.2.1 } },
    { assume m hm,
      by_cases h' : m â‰¤ n,
      { apply h.1.cont m (with_top.coe_le_coe.2 h') },
      { have : m = (n + 1) := le_antisymm (with_top.coe_le_coe.1 hm) (not_le.1 h'),
        rw this,
        exact h.2.2 } } }
end

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p.shift` is a Taylor series up to `n`
for `p 1`, which is a derivative of `f`. -/
theorem has_ftaylor_series_up_to_on_succ_iff_right {n : â„•} :
  has_ftaylor_series_up_to_on ((n + 1) : â„•) f p s â†”
  (âˆ€ x âˆˆ s, (p x 0).uncurry0 = f x)
  âˆ§ (âˆ€ x âˆˆ s, has_fderiv_within_at (Î» y, p y 0) (p x 1).curry_left s x)
  âˆ§ has_ftaylor_series_up_to_on n
    (Î» x, continuous_multilinear_curry_fin1 ğ•œ E F (p x 1)) (Î» x, (p x).shift) s :=
begin
  split,
  { assume H,
    refine âŸ¨H.zero_eq, H.fderiv_within 0 (with_top.coe_lt_coe.2 (nat.succ_pos n)), _âŸ©,
    split,
    { assume x hx, refl },
    { assume m (hm : (m : with_top â„•) < n) x (hx : x âˆˆ s),
      have A : (m.succ : with_top â„•) < n.succ,
        by { rw with_top.coe_lt_coe at âŠ¢ hm, exact nat.lt_succ_iff.mpr hm },
      change has_fderiv_within_at
        ((continuous_multilinear_curry_right_equiv' ğ•œ m E F).symm
           âˆ˜ (Î» (y : E), p y m.succ))
        (p x m.succ.succ).curry_right.curry_left s x,
      rw linear_isometry_equiv.comp_has_fderiv_within_at_iff',
      convert H.fderiv_within _ A x hx,
      ext y v,
      change (p x m.succ.succ) (snoc (cons y (init v)) (v (last _)))
        = (p x (nat.succ (nat.succ m))) (cons y v),
      rw [â† cons_snoc_eq_snoc_cons, snoc_init_self] },
    { assume m (hm : (m : with_top â„•) â‰¤ n),
      have A : (m.succ : with_top â„•) â‰¤ n.succ,
        by { rw with_top.coe_le_coe at âŠ¢ hm, exact nat.pred_le_iff.mp hm },
      change continuous_on ((continuous_multilinear_curry_right_equiv' ğ•œ m E F).symm
           âˆ˜ (Î» (y : E), p y m.succ)) s,
      rw linear_isometry_equiv.comp_continuous_on_iff,
      exact H.cont _ A } },
  { rintros âŸ¨Hzero_eq, Hfderiv_zero, HtaylorâŸ©,
    split,
    { exact Hzero_eq },
    { assume m (hm : (m : with_top â„•) < n.succ) x (hx : x âˆˆ s),
      cases m,
      { exact Hfderiv_zero x hx },
      { have A : (m : with_top â„•) < n,
          by { rw with_top.coe_lt_coe at hm âŠ¢, exact nat.lt_of_succ_lt_succ hm },
        have : has_fderiv_within_at ((continuous_multilinear_curry_right_equiv' ğ•œ m E F).symm
           âˆ˜ (Î» (y : E), p y m.succ)) ((p x).shift m.succ).curry_left s x :=
          Htaylor.fderiv_within _ A x hx,
        rw linear_isometry_equiv.comp_has_fderiv_within_at_iff' at this,
        convert this,
        ext y v,
        change (p x (nat.succ (nat.succ m))) (cons y v)
          = (p x m.succ.succ) (snoc (cons y (init v)) (v (last _))),
        rw [â† cons_snoc_eq_snoc_cons, snoc_init_self] } },
    { assume m (hm : (m : with_top â„•) â‰¤ n.succ),
      cases m,
      { have : differentiable_on ğ•œ (Î» x, p x 0) s :=
          Î» x hx, (Hfderiv_zero x hx).differentiable_within_at,
        exact this.continuous_on },
      { have A : (m : with_top â„•) â‰¤ n,
          by { rw with_top.coe_le_coe at hm âŠ¢, exact nat.lt_succ_iff.mp hm },
        have : continuous_on ((continuous_multilinear_curry_right_equiv' ğ•œ m E F).symm
           âˆ˜ (Î» (y : E), p y m.succ)) s :=
        Htaylor.cont _ A,
        rwa linear_isometry_equiv.comp_continuous_on_iff at this } } }
end

/-! ### Smooth functions within a set around a point -/

variable (ğ•œ)

/-- A function is continuously differentiable up to order `n` within a set `s` at a point `x` if
it admits continuous derivatives up to order `n` in a neighborhood of `x` in `s âˆª {x}`.
For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).

For instance, a real function which is `C^m` on `(-1/m, 1/m)` for each natural `m`, but not
better, is `C^âˆ` at `0` within `univ`.
-/
def times_cont_diff_within_at (n : with_top â„•) (f : E â†’ F) (s : set E) (x : E) :=
âˆ€ (m : â„•), (m : with_top â„•) â‰¤ n â†’
  âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ p : E â†’ formal_multilinear_series ğ•œ E F,
    has_ftaylor_series_up_to_on m f p u

variable {ğ•œ}

lemma times_cont_diff_within_at_nat {n : â„•} :
  times_cont_diff_within_at ğ•œ n f s x â†”
  âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ p : E â†’ formal_multilinear_series ğ•œ E F,
  has_ftaylor_series_up_to_on n f p u :=
âŸ¨Î» H, H n (le_refl _), Î» âŸ¨u, hu, p, hpâŸ© m hm, âŸ¨u, hu, p, hp.of_le hmâŸ©âŸ©

lemma times_cont_diff_within_at.of_le {m n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hmn : m â‰¤ n) :
  times_cont_diff_within_at ğ•œ m f s x :=
Î» k hk, h k (le_trans hk hmn)

lemma times_cont_diff_within_at_iff_forall_nat_le {n : with_top â„•} :
  times_cont_diff_within_at ğ•œ n f s x â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ times_cont_diff_within_at ğ•œ m f s x :=
âŸ¨Î» H m hm, H.of_le hm, Î» H m hm, H m hm _ le_rflâŸ©

lemma times_cont_diff_within_at_top :
  times_cont_diff_within_at ğ•œ âˆ f s x â†” âˆ€ (n : â„•), times_cont_diff_within_at ğ•œ n f s x :=
times_cont_diff_within_at_iff_forall_nat_le.trans $ by simp only [forall_prop_of_true, le_top]

lemma times_cont_diff_within_at.continuous_within_at {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) : continuous_within_at f s x :=
begin
  rcases h 0 bot_le with âŸ¨u, hu, p, HâŸ©,
  rw [mem_nhds_within_insert] at hu,
  exact (H.continuous_on.continuous_within_at hu.1).mono_of_mem hu.2
end

lemma times_cont_diff_within_at.congr_of_eventually_eq {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  times_cont_diff_within_at ğ•œ n fâ‚ s x :=
Î» m hm, let âŸ¨u, hu, p, HâŸ© := h m hm in
âŸ¨{x âˆˆ u | fâ‚ x = f x}, filter.inter_mem hu (mem_nhds_within_insert.2 âŸ¨hx, hâ‚âŸ©), p,
  (H.mono (sep_subset _ _)).congr (Î» _, and.right)âŸ©

lemma times_cont_diff_within_at.congr_of_eventually_eq' {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) :
  times_cont_diff_within_at ğ•œ n fâ‚ s x :=
h.congr_of_eventually_eq hâ‚ $ hâ‚.self_of_nhds_within hx

lemma filter.eventually_eq.times_cont_diff_within_at_iff {n : with_top â„•}
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  times_cont_diff_within_at ğ•œ n fâ‚ s x â†” times_cont_diff_within_at ğ•œ n f s x :=
âŸ¨Î» H, times_cont_diff_within_at.congr_of_eventually_eq H hâ‚.symm hx.symm,
Î» H, H.congr_of_eventually_eq hâ‚ hxâŸ©

lemma times_cont_diff_within_at.congr {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  times_cont_diff_within_at ğ•œ n fâ‚ s x :=
h.congr_of_eventually_eq (filter.eventually_eq_of_mem self_mem_nhds_within hâ‚) hx

lemma times_cont_diff_within_at.congr' {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : x âˆˆ s) :
  times_cont_diff_within_at ğ•œ n fâ‚ s x :=
h.congr hâ‚ (hâ‚ _ hx)

lemma times_cont_diff_within_at.mono_of_mem {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) {t : set E} (hst : s âˆˆ ğ“[t] x) :
  times_cont_diff_within_at ğ•œ n f t x :=
begin
  assume m hm,
  rcases h m hm with âŸ¨u, hu, p, HâŸ©,
  exact âŸ¨u, nhds_within_le_of_mem (insert_mem_nhds_within_insert hst) hu, p, HâŸ©
end

lemma times_cont_diff_within_at.mono {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) {t : set E} (hst : t âŠ† s) :
  times_cont_diff_within_at ğ•œ n f t x :=
h.mono_of_mem $ filter.mem_of_superset self_mem_nhds_within hst

lemma times_cont_diff_within_at.congr_nhds {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) {t : set E} (hst : ğ“[s] x = ğ“[t] x) :
  times_cont_diff_within_at ğ•œ n f t x :=
h.mono_of_mem $ hst â–¸ self_mem_nhds_within

lemma times_cont_diff_within_at_congr_nhds {n : with_top â„•} {t : set E} (hst : ğ“[s] x = ğ“[t] x) :
  times_cont_diff_within_at ğ•œ n f s x â†” times_cont_diff_within_at ğ•œ n f t x :=
âŸ¨Î» h, h.congr_nhds hst, Î» h, h.congr_nhds hst.symmâŸ©

lemma times_cont_diff_within_at_inter' {n : with_top â„•} (h : t âˆˆ ğ“[s] x) :
  times_cont_diff_within_at ğ•œ n f (s âˆ© t) x â†” times_cont_diff_within_at ğ•œ n f s x :=
times_cont_diff_within_at_congr_nhds $ eq.symm $ nhds_within_restrict'' _ h

lemma times_cont_diff_within_at_inter {n : with_top â„•} (h : t âˆˆ ğ“ x) :
  times_cont_diff_within_at ğ•œ n f (s âˆ© t) x â†” times_cont_diff_within_at ğ•œ n f s x :=
times_cont_diff_within_at_inter' (mem_nhds_within_of_mem_nhds h)

/-- If a function is `C^n` within a set at a point, with `n â‰¥ 1`, then it is differentiable
within this set at this point. -/
lemma times_cont_diff_within_at.differentiable_within_at' {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hn : 1 â‰¤ n) :
  differentiable_within_at ğ•œ f (insert x s) x :=
begin
  rcases h 1 hn with âŸ¨u, hu, p, HâŸ©,
  rcases mem_nhds_within.1 hu with âŸ¨t, t_open, xt, tuâŸ©,
  rw inter_comm at tu,
  have := ((H.mono tu).differentiable_on (le_refl _)) x âŸ¨mem_insert x s, xtâŸ©,
  exact (differentiable_within_at_inter (is_open.mem_nhds t_open xt)).1 this,
end

lemma times_cont_diff_within_at.differentiable_within_at {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hn : 1 â‰¤ n) :
  differentiable_within_at ğ•œ f s x :=
(h.differentiable_within_at' hn).mono  (subset_insert x s)

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`. -/
theorem times_cont_diff_within_at_succ_iff_has_fderiv_within_at {n : â„•} :
  times_cont_diff_within_at ğ•œ ((n + 1) : â„•) f s x
  â†” âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ f' : E â†’ (E â†’L[ğ•œ] F),
    (âˆ€ x âˆˆ u, has_fderiv_within_at f (f' x) u x) âˆ§ (times_cont_diff_within_at ğ•œ n f' u x) :=
begin
  split,
  { assume h,
    rcases h n.succ (le_refl _) with âŸ¨u, hu, p, HpâŸ©,
    refine âŸ¨u, hu, Î» y, (continuous_multilinear_curry_fin1 ğ•œ E F) (p y 1),
      Î» y hy, Hp.has_fderiv_within_at (with_top.coe_le_coe.2 (nat.le_add_left 1 n)) hy, _âŸ©,
    assume m hm,
    refine âŸ¨u, _, Î» (y : E), (p y).shift, _âŸ©,
    { convert self_mem_nhds_within,
      have : x âˆˆ insert x s, by simp,
      exact (insert_eq_of_mem (mem_of_mem_nhds_within this hu)) },
    { rw has_ftaylor_series_up_to_on_succ_iff_right at Hp,
      exact Hp.2.2.of_le hm } },
  { rintros âŸ¨u, hu, f', f'_eq_deriv, Hf'âŸ©,
    rw times_cont_diff_within_at_nat,
    rcases Hf' n (le_refl _) with âŸ¨v, hv, p', Hp'âŸ©,
    refine âŸ¨v âˆ© u, _, Î» x, (p' x).unshift (f x), _âŸ©,
    { apply filter.inter_mem _ hu,
      apply nhds_within_le_of_mem hu,
      exact nhds_within_mono _ (subset_insert x u) hv },
    { rw has_ftaylor_series_up_to_on_succ_iff_right,
      refine âŸ¨Î» y hy, rfl, Î» y hy, _, _âŸ©,
      { change has_fderiv_within_at (Î» z, (continuous_multilinear_curry_fin0 ğ•œ E F).symm (f z))
          ((formal_multilinear_series.unshift (p' y) (f y) 1).curry_left) (v âˆ© u) y,
        rw linear_isometry_equiv.comp_has_fderiv_within_at_iff',
        convert (f'_eq_deriv y hy.2).mono (inter_subset_right v u),
        rw â† Hp'.zero_eq y hy.1,
        ext z,
        change ((p' y 0) (init (@cons 0 (Î» i, E) z 0))) (@cons 0 (Î» i, E) z 0 (last 0))
          = ((p' y 0) 0) z,
        unfold_coes,
        congr },
      { convert (Hp'.mono (inter_subset_left v u)).congr (Î» x hx, Hp'.zero_eq x hx.1),
        { ext x y,
          change p' x 0 (init (@snoc 0 (Î» i : fin 1, E) 0 y)) y = p' x 0 0 y,
          rw init_snoc },
        { ext x k v y,
          change p' x k (init (@snoc k (Î» i : fin k.succ, E) v y))
            (@snoc k (Î» i : fin k.succ, E) v y (last k)) = p' x k v y,
          rw [snoc_last, init_snoc] } } } }
end

/-! ### Smooth functions within a set -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` on `s` if, for any point `x` in `s`, it
admits continuous derivatives up to order `n` on a neighborhood of `x` in `s`.

For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).
-/
definition times_cont_diff_on (n : with_top â„•) (f : E â†’ F) (s : set E) :=
âˆ€ x âˆˆ s, times_cont_diff_within_at ğ•œ n f s x

variable {ğ•œ}

lemma times_cont_diff_on.times_cont_diff_within_at {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hx : x âˆˆ s) :
  times_cont_diff_within_at ğ•œ n f s x :=
h x hx

lemma times_cont_diff_within_at.times_cont_diff_on {n : with_top â„•} {m : â„•}
  (hm : (m : with_top â„•) â‰¤ n) (h : times_cont_diff_within_at ğ•œ n f s x) :
  âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ times_cont_diff_on ğ•œ m f u :=
begin
  rcases h m hm with âŸ¨u, u_nhd, p, hpâŸ©,
  refine âŸ¨u âˆ© insert x s, filter.inter_mem u_nhd self_mem_nhds_within,
    inter_subset_right _ _, _âŸ©,
  assume y hy m' hm',
  refine âŸ¨u âˆ© insert x s, _, p, (hp.mono (inter_subset_left _ _)).of_le hm'âŸ©,
  convert self_mem_nhds_within,
  exact insert_eq_of_mem hy
end

lemma times_cont_diff_on.of_le {m n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hmn : m â‰¤ n) :
  times_cont_diff_on ğ•œ m f s :=
Î» x hx, (h x hx).of_le hmn

lemma times_cont_diff_on_iff_forall_nat_le {n : with_top â„•} :
  times_cont_diff_on ğ•œ n f s â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ times_cont_diff_on ğ•œ m f s :=
âŸ¨Î» H m hm, H.of_le hm, Î» H x hx m hm, H m hm x hx m le_rflâŸ©

lemma times_cont_diff_on_top :
  times_cont_diff_on ğ•œ âˆ f s â†” âˆ€ (n : â„•), times_cont_diff_on ğ•œ n f s :=
times_cont_diff_on_iff_forall_nat_le.trans $ by simp only [le_top, forall_prop_of_true]

lemma times_cont_diff_on_all_iff_nat :
  (âˆ€ n, times_cont_diff_on ğ•œ n f s) â†” (âˆ€ n : â„•, times_cont_diff_on ğ•œ n f s) :=
begin
  refine âŸ¨Î» H n, H n, _âŸ©,
  rintro H (_|n),
  exacts [times_cont_diff_on_top.2 H, H n]
end

lemma times_cont_diff_on.continuous_on {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) : continuous_on f s :=
Î» x hx, (h x hx).continuous_within_at

lemma times_cont_diff_on.congr {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) :
  times_cont_diff_on ğ•œ n fâ‚ s :=
Î» x hx, (h x hx).congr hâ‚ (hâ‚ x hx)

lemma times_cont_diff_on_congr {n : with_top â„•} (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) :
  times_cont_diff_on ğ•œ n fâ‚ s â†” times_cont_diff_on ğ•œ n f s :=
âŸ¨Î» H, H.congr (Î» x hx, (hâ‚ x hx).symm), Î» H, H.congr hâ‚âŸ©

lemma times_cont_diff_on.mono {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) {t : set E} (hst : t âŠ† s) :
  times_cont_diff_on ğ•œ n f t :=
Î» x hx, (h x (hst hx)).mono hst

lemma times_cont_diff_on.congr_mono {n : with_top â„•}
  (hf : times_cont_diff_on ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ sâ‚, fâ‚ x = f x) (hs : sâ‚ âŠ† s) :
  times_cont_diff_on ğ•œ n fâ‚ sâ‚ :=
(hf.mono hs).congr hâ‚

/-- If a function is `C^n` on a set with `n â‰¥ 1`, then it is differentiable there. -/
lemma times_cont_diff_on.differentiable_on {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hn : 1 â‰¤ n) : differentiable_on ğ•œ f s :=
Î» x hx, (h x hx).differentiable_within_at hn

/-- If a function is `C^n` around each point in a set, then it is `C^n` on the set. -/
lemma times_cont_diff_on_of_locally_times_cont_diff_on {n : with_top â„•}
  (h : âˆ€ x âˆˆ s, âˆƒu, is_open u âˆ§ x âˆˆ u âˆ§ times_cont_diff_on ğ•œ n f (s âˆ© u)) :
  times_cont_diff_on ğ•œ n f s :=
begin
  assume x xs,
  rcases h x xs with âŸ¨u, u_open, xu, huâŸ©,
  apply (times_cont_diff_within_at_inter _).1 (hu x âŸ¨xs, xuâŸ©),
  exact is_open.mem_nhds u_open xu
end

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`. -/
theorem times_cont_diff_on_succ_iff_has_fderiv_within_at {n : â„•} :
  times_cont_diff_on ğ•œ ((n + 1) : â„•) f s
  â†” âˆ€ x âˆˆ s, âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ f' : E â†’ (E â†’L[ğ•œ] F),
    (âˆ€ x âˆˆ u, has_fderiv_within_at f (f' x) u x) âˆ§ (times_cont_diff_on ğ•œ n f' u) :=
begin
  split,
  { assume h x hx,
    rcases (h x hx) n.succ (le_refl _) with âŸ¨u, hu, p, HpâŸ©,
    refine âŸ¨u, hu, Î» y, (continuous_multilinear_curry_fin1 ğ•œ E F) (p y 1),
      Î» y hy, Hp.has_fderiv_within_at (with_top.coe_le_coe.2 (nat.le_add_left 1 n)) hy, _âŸ©,
    rw has_ftaylor_series_up_to_on_succ_iff_right at Hp,
    assume z hz m hm,
    refine âŸ¨u, _, Î» (x : E), (p x).shift, Hp.2.2.of_le hmâŸ©,
    convert self_mem_nhds_within,
    exact insert_eq_of_mem hz, },
  { assume h x hx,
    rw times_cont_diff_within_at_succ_iff_has_fderiv_within_at,
    rcases h x hx with âŸ¨u, u_nhbd, f', hu, hf'âŸ©,
    have : x âˆˆ u := mem_of_mem_nhds_within (mem_insert _ _) u_nhbd,
    exact âŸ¨u, u_nhbd, f', hu, hf' x thisâŸ© }
end

/-! ### Iterated derivative within a set -/
variable (ğ•œ)

/--
The `n`-th derivative of a function along a set, defined inductively by saying that the `n+1`-th
derivative of `f` is the derivative of the `n`-th derivative of `f` along this set, together with
an uncurrying step to see it as a multilinear map in `n+1` variables..
-/
noncomputable def iterated_fderiv_within (n : â„•) (f : E â†’ F) (s : set E) :
  E â†’ (E [Ã—n]â†’L[ğ•œ] F) :=
nat.rec_on n
  (Î» x, continuous_multilinear_map.curry0 ğ•œ E (f x))
  (Î» n rec x, continuous_linear_map.uncurry_left (fderiv_within ğ•œ rec s x))

/-- Formal Taylor series associated to a function within a set. -/
def ftaylor_series_within (f : E â†’ F) (s : set E) (x : E) : formal_multilinear_series ğ•œ E F :=
Î» n, iterated_fderiv_within ğ•œ n f s x

variable {ğ•œ}

@[simp] lemma iterated_fderiv_within_zero_apply (m : (fin 0) â†’ E) :
  (iterated_fderiv_within ğ•œ 0 f s x : ((fin 0) â†’  E) â†’ F) m = f x := rfl

lemma iterated_fderiv_within_zero_eq_comp :
  iterated_fderiv_within ğ•œ 0 f s = (continuous_multilinear_curry_fin0 ğ•œ E F).symm âˆ˜ f := rfl

lemma iterated_fderiv_within_succ_apply_left {n : â„•} (m : fin (n + 1) â†’ E):
  (iterated_fderiv_within ğ•œ (n + 1) f s x : (fin (n + 1) â†’ E) â†’ F) m
  = (fderiv_within ğ•œ (iterated_fderiv_within ğ•œ n f s) s x : E â†’ (E [Ã—n]â†’L[ğ•œ] F))
      (m 0) (tail m) := rfl

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the derivative of the `n`-th derivative. -/
lemma iterated_fderiv_within_succ_eq_comp_left {n : â„•} :
  iterated_fderiv_within ğ•œ (n + 1) f s =
  (continuous_multilinear_curry_left_equiv ğ•œ (Î»(i : fin (n + 1)), E) F)
    âˆ˜ (fderiv_within ğ•œ (iterated_fderiv_within ğ•œ n f s) s) := rfl

theorem iterated_fderiv_within_succ_apply_right {n : â„•}
  (hs : unique_diff_on ğ•œ s) (hx : x âˆˆ s) (m : fin (n + 1) â†’ E) :
  (iterated_fderiv_within ğ•œ (n + 1) f s x : (fin (n + 1) â†’ E) â†’ F) m
    = iterated_fderiv_within ğ•œ n (Î»y, fderiv_within ğ•œ f s y) s x (init m) (m (last n)) :=
begin
  induction n with n IH generalizing x,
  { rw [iterated_fderiv_within_succ_eq_comp_left, iterated_fderiv_within_zero_eq_comp,
        iterated_fderiv_within_zero_apply,
        function.comp_apply, linear_isometry_equiv.comp_fderiv_within _ (hs x hx)],
    refl },
  { let I := continuous_multilinear_curry_right_equiv' ğ•œ n E F,
    have A : âˆ€ y âˆˆ s, iterated_fderiv_within ğ•œ n.succ f s y
        = (I âˆ˜ (iterated_fderiv_within ğ•œ n (Î»y, fderiv_within ğ•œ f s y) s)) y,
      by { assume y hy, ext m, rw @IH m y hy, refl },
    calc
    (iterated_fderiv_within ğ•œ (n+2) f s x : (fin (n+2) â†’ E) â†’ F) m =
    (fderiv_within ğ•œ (iterated_fderiv_within ğ•œ n.succ f s) s x
              : E â†’ (E [Ã—(n + 1)]â†’L[ğ•œ] F)) (m 0) (tail m) : rfl
    ... = (fderiv_within ğ•œ (I âˆ˜ (iterated_fderiv_within ğ•œ n (fderiv_within ğ•œ f s) s)) s x
              : E â†’ (E [Ã—(n + 1)]â†’L[ğ•œ] F)) (m 0) (tail m) :
      by rw fderiv_within_congr (hs x hx) A (A x hx)
    ... = (I âˆ˜ fderiv_within ğ•œ ((iterated_fderiv_within ğ•œ n (fderiv_within ğ•œ f s) s)) s x
              : E â†’ (E [Ã—(n + 1)]â†’L[ğ•œ] F)) (m 0) (tail m) :
      by { rw linear_isometry_equiv.comp_fderiv_within _ (hs x hx), refl }
    ... = (fderiv_within ğ•œ ((iterated_fderiv_within ğ•œ n (Î» y, fderiv_within ğ•œ f s y) s)) s x
              : E â†’ (E [Ã—n]â†’L[ğ•œ] (E â†’L[ğ•œ] F))) (m 0) (init (tail m)) ((tail m) (last n)) : rfl
    ... = iterated_fderiv_within ğ•œ (nat.succ n) (Î» y, fderiv_within ğ•œ f s y) s x
              (init m) (m (last (n + 1))) :
      by { rw [iterated_fderiv_within_succ_apply_left, tail_init_eq_init_tail], refl } }
end

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the `n`-th derivative of the derivative. -/
lemma iterated_fderiv_within_succ_eq_comp_right {n : â„•} (hs : unique_diff_on ğ•œ s) (hx : x âˆˆ s) :
  iterated_fderiv_within ğ•œ (n + 1) f s x =
  ((continuous_multilinear_curry_right_equiv' ğ•œ n E F)
    âˆ˜ (iterated_fderiv_within ğ•œ n (Î»y, fderiv_within ğ•œ f s y) s)) x :=
by { ext m, rw iterated_fderiv_within_succ_apply_right hs hx, refl }

@[simp] lemma iterated_fderiv_within_one_apply
  (hs : unique_diff_on ğ•œ s) (hx : x âˆˆ s) (m : (fin 1) â†’ E) :
  (iterated_fderiv_within ğ•œ 1 f s x : ((fin 1) â†’ E) â†’ F) m
  = (fderiv_within ğ•œ f s x : E â†’ F) (m 0) :=
by { rw [iterated_fderiv_within_succ_apply_right hs hx, iterated_fderiv_within_zero_apply], refl }

/-- If two functions coincide on a set `s` of unique differentiability, then their iterated
differentials within this set coincide. -/
lemma iterated_fderiv_within_congr {n : â„•}
  (hs : unique_diff_on ğ•œ s) (hL : âˆ€yâˆˆs, fâ‚ y = f y) (hx : x âˆˆ s) :
  iterated_fderiv_within ğ•œ n fâ‚ s x = iterated_fderiv_within ğ•œ n f s x :=
begin
  induction n with n IH generalizing x,
  { ext m, simp [hL x hx] },
  { have : fderiv_within ğ•œ (Î» y, iterated_fderiv_within ğ•œ n fâ‚ s y) s x
           = fderiv_within ğ•œ (Î» y, iterated_fderiv_within ğ•œ n f s y) s x :=
      fderiv_within_congr (hs x hx) (Î» y hy, IH hy) (IH hx),
    ext m,
    rw [iterated_fderiv_within_succ_apply_left, iterated_fderiv_within_succ_apply_left, this] }
end

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with an open set containing `x`. -/
lemma iterated_fderiv_within_inter_open {n : â„•} (hu : is_open u)
  (hs : unique_diff_on ğ•œ (s âˆ© u)) (hx : x âˆˆ s âˆ© u) :
  iterated_fderiv_within ğ•œ n f (s âˆ© u) x = iterated_fderiv_within ğ•œ n f s x :=
begin
  induction n with n IH generalizing x,
  { ext m, simp },
  { have A : fderiv_within ğ•œ (Î» y, iterated_fderiv_within ğ•œ n f (s âˆ© u) y) (s âˆ© u) x
           = fderiv_within ğ•œ (Î» y, iterated_fderiv_within ğ•œ n f s y) (s âˆ© u) x :=
      fderiv_within_congr (hs x hx) (Î» y hy, IH hy) (IH hx),
    have B : fderiv_within ğ•œ (Î» y, iterated_fderiv_within ğ•œ n f s y) (s âˆ© u) x
           = fderiv_within ğ•œ (Î» y, iterated_fderiv_within ğ•œ n f s y) s x :=
      fderiv_within_inter (is_open.mem_nhds hu hx.2)
        ((unique_diff_within_at_inter (is_open.mem_nhds hu hx.2)).1 (hs x hx)),
    ext m,
    rw [iterated_fderiv_within_succ_apply_left, iterated_fderiv_within_succ_apply_left, A, B] }
end

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with a neighborhood of `x` within `s`. -/
lemma iterated_fderiv_within_inter' {n : â„•}
  (hu : u âˆˆ ğ“[s] x) (hs : unique_diff_on ğ•œ s) (xs : x âˆˆ s) :
  iterated_fderiv_within ğ•œ n f (s âˆ© u) x = iterated_fderiv_within ğ•œ n f s x :=
begin
  obtain âŸ¨v, v_open, xv, vuâŸ© : âˆƒ v, is_open v âˆ§ x âˆˆ v âˆ§ v âˆ© s âŠ† u := mem_nhds_within.1 hu,
  have A : (s âˆ© u) âˆ© v = s âˆ© v,
  { apply subset.antisymm (inter_subset_inter (inter_subset_left _ _) (subset.refl _)),
    exact Î» y âŸ¨ys, yvâŸ©, âŸ¨âŸ¨ys, vu âŸ¨yv, ysâŸ©âŸ©, yvâŸ© },
  have : iterated_fderiv_within ğ•œ n f (s âˆ© v) x = iterated_fderiv_within ğ•œ n f s x :=
    iterated_fderiv_within_inter_open v_open (hs.inter v_open) âŸ¨xs, xvâŸ©,
  rw â† this,
  have : iterated_fderiv_within ğ•œ n f ((s âˆ© u) âˆ© v) x = iterated_fderiv_within ğ•œ n f (s âˆ© u) x,
  { refine iterated_fderiv_within_inter_open v_open  _ âŸ¨âŸ¨xs, vu âŸ¨xv, xsâŸ©âŸ©, xvâŸ©,
    rw A,
    exact hs.inter v_open },
  rw A at this,
  rw â† this
end

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with a neighborhood of `x`. -/
lemma iterated_fderiv_within_inter {n : â„•}
  (hu : u âˆˆ ğ“ x) (hs : unique_diff_on ğ•œ s) (xs : x âˆˆ s) :
  iterated_fderiv_within ğ•œ n f (s âˆ© u) x = iterated_fderiv_within ğ•œ n f s x :=
iterated_fderiv_within_inter' (mem_nhds_within_of_mem_nhds hu) hs xs

@[simp] lemma times_cont_diff_on_zero :
  times_cont_diff_on ğ•œ 0 f s â†” continuous_on f s :=
begin
  refine âŸ¨Î» H, H.continuous_on, Î» H, _âŸ©,
  assume x hx m hm,
  have : (m : with_top â„•) = 0 := le_antisymm hm bot_le,
  rw this,
  refine âŸ¨insert x s, self_mem_nhds_within, ftaylor_series_within ğ•œ f s, _âŸ©,
  rw has_ftaylor_series_up_to_on_zero_iff,
  exact âŸ¨by rwa insert_eq_of_mem hx, Î» x hx, by simp [ftaylor_series_within]âŸ©
end

lemma times_cont_diff_within_at_zero (hx : x âˆˆ s) :
  times_cont_diff_within_at ğ•œ 0 f s x â†” âˆƒ u âˆˆ ğ“[s] x, continuous_on f (s âˆ© u) :=
begin
  split,
  { intros h,
    obtain âŸ¨u, H, p, hpâŸ© := h 0 (by norm_num),
    refine âŸ¨u, _, _âŸ©,
    { simpa [hx] using H },
    { simp only [with_top.coe_zero, has_ftaylor_series_up_to_on_zero_iff] at hp,
      exact hp.1.mono (inter_subset_right s u) } },
  { rintros âŸ¨u, H, huâŸ©,
    rw â† times_cont_diff_within_at_inter' H,
    have h' : x âˆˆ s âˆ© u := âŸ¨hx, mem_of_mem_nhds_within hx HâŸ©,
    exact (times_cont_diff_on_zero.mpr hu).times_cont_diff_within_at h' }
end

/-- On a set with unique differentiability, any choice of iterated differential has to coincide
with the one we have chosen in `iterated_fderiv_within ğ•œ m f s`. -/
theorem has_ftaylor_series_up_to_on.eq_ftaylor_series_of_unique_diff_on {n : with_top â„•}
  (h : has_ftaylor_series_up_to_on n f p s)
  {m : â„•} (hmn : (m : with_top â„•) â‰¤ n) (hs : unique_diff_on ğ•œ s) (hx : x âˆˆ s) :
  p x m = iterated_fderiv_within ğ•œ m f s x :=
begin
  induction m with m IH generalizing x,
  { rw [h.zero_eq' hx, iterated_fderiv_within_zero_eq_comp] },
  { have A : (m : with_top â„•) < n := lt_of_lt_of_le (with_top.coe_lt_coe.2 (lt_add_one m)) hmn,
    have : has_fderiv_within_at (Î» (y : E), iterated_fderiv_within ğ•œ m f s y)
      (continuous_multilinear_map.curry_left (p x (nat.succ m))) s x :=
    (h.fderiv_within m A x hx).congr (Î» y hy, (IH (le_of_lt A) hy).symm) (IH (le_of_lt A) hx).symm,
    rw [iterated_fderiv_within_succ_eq_comp_left, function.comp_apply,
      this.fderiv_within (hs x hx)],
    exact (continuous_multilinear_map.uncurry_curry_left _).symm }
end

/-- When a function is `C^n` in a set `s` of unique differentiability, it admits
`ftaylor_series_within ğ•œ f s` as a Taylor series up to order `n` in `s`. -/
theorem times_cont_diff_on.ftaylor_series_within {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hs : unique_diff_on ğ•œ s) :
  has_ftaylor_series_up_to_on n f (ftaylor_series_within ğ•œ f s) s :=
begin
  split,
  { assume x hx,
    simp only [ftaylor_series_within, continuous_multilinear_map.uncurry0_apply,
               iterated_fderiv_within_zero_apply] },
  { assume m hm x hx,
    rcases (h x hx) m.succ (with_top.add_one_le_of_lt hm) with âŸ¨u, hu, p, HpâŸ©,
    rw insert_eq_of_mem hx at hu,
    rcases mem_nhds_within.1 hu with âŸ¨o, o_open, xo, hoâŸ©,
    rw inter_comm at ho,
    have : p x m.succ = ftaylor_series_within ğ•œ f s x m.succ,
    { change p x m.succ = iterated_fderiv_within ğ•œ m.succ f s x,
      rw â† iterated_fderiv_within_inter (is_open.mem_nhds o_open xo) hs hx,
      exact (Hp.mono ho).eq_ftaylor_series_of_unique_diff_on (le_refl _)
        (hs.inter o_open) âŸ¨hx, xoâŸ© },
    rw [â† this, â† has_fderiv_within_at_inter (is_open.mem_nhds o_open xo)],
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylor_series_within ğ•œ f s y m,
    { rintros y âŸ¨hy, yoâŸ©,
      change p y m = iterated_fderiv_within ğ•œ m f s y,
      rw â† iterated_fderiv_within_inter (is_open.mem_nhds o_open yo) hs hy,
      exact (Hp.mono ho).eq_ftaylor_series_of_unique_diff_on (with_top.coe_le_coe.2 (nat.le_succ m))
        (hs.inter o_open) âŸ¨hy, yoâŸ© },
    exact ((Hp.mono ho).fderiv_within m (with_top.coe_lt_coe.2 (lt_add_one m)) x âŸ¨hx, xoâŸ©).congr
      (Î» y hy, (A y hy).symm) (A x âŸ¨hx, xoâŸ©).symm },
  { assume m hm,
    apply continuous_on_of_locally_continuous_on,
    assume x hx,
    rcases h x hx m hm with âŸ¨u, hu, p, HpâŸ©,
    rcases mem_nhds_within.1 hu with âŸ¨o, o_open, xo, hoâŸ©,
    rw insert_eq_of_mem hx at ho,
    rw inter_comm at ho,
    refine âŸ¨o, o_open, xo, _âŸ©,
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylor_series_within ğ•œ f s y m,
    { rintros y âŸ¨hy, yoâŸ©,
      change p y m = iterated_fderiv_within ğ•œ m f s y,
      rw â† iterated_fderiv_within_inter (is_open.mem_nhds o_open yo) hs hy,
      exact (Hp.mono ho).eq_ftaylor_series_of_unique_diff_on (le_refl _)
        (hs.inter o_open) âŸ¨hy, yoâŸ© },
    exact ((Hp.mono ho).cont m (le_refl _)).congr (Î» y hy, (A y hy).symm) }
end

lemma times_cont_diff_on_of_continuous_on_differentiable_on {n : with_top â„•}
  (Hcont : âˆ€ (m : â„•), (m : with_top â„•) â‰¤ n â†’
    continuous_on (Î» x, iterated_fderiv_within ğ•œ m f s x) s)
  (Hdiff : âˆ€ (m : â„•), (m : with_top â„•) < n â†’
    differentiable_on ğ•œ (Î» x, iterated_fderiv_within ğ•œ m f s x) s) :
  times_cont_diff_on ğ•œ n f s :=
begin
  assume x hx m hm,
  rw insert_eq_of_mem hx,
  refine âŸ¨s, self_mem_nhds_within, ftaylor_series_within ğ•œ f s, _âŸ©,
  split,
  { assume y hy,
    simp only [ftaylor_series_within, continuous_multilinear_map.uncurry0_apply,
                iterated_fderiv_within_zero_apply] },
  { assume k hk y hy,
    convert (Hdiff k (lt_of_lt_of_le hk hm) y hy).has_fderiv_within_at,
    simp only [ftaylor_series_within, iterated_fderiv_within_succ_eq_comp_left,
                continuous_linear_equiv.coe_apply, function.comp_app, coe_fn_coe_base],
    exact continuous_linear_map.curry_uncurry_left _ },
  { assume k hk,
    exact Hcont k (le_trans hk hm) }
end

lemma times_cont_diff_on_of_differentiable_on {n : with_top â„•}
  (h : âˆ€(m : â„•), (m : with_top â„•) â‰¤ n â†’ differentiable_on ğ•œ (iterated_fderiv_within ğ•œ m f s) s) :
  times_cont_diff_on ğ•œ n f s :=
times_cont_diff_on_of_continuous_on_differentiable_on
  (Î» m hm, (h m hm).continuous_on) (Î» m hm, (h m (le_of_lt hm)))

lemma times_cont_diff_on.continuous_on_iterated_fderiv_within {n : with_top â„•} {m : â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hmn : (m : with_top â„•) â‰¤ n) (hs : unique_diff_on ğ•œ s) :
  continuous_on (iterated_fderiv_within ğ•œ m f s) s :=
(h.ftaylor_series_within hs).cont m hmn

lemma times_cont_diff_on.differentiable_on_iterated_fderiv_within {n : with_top â„•} {m : â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hmn : (m : with_top â„•) < n) (hs : unique_diff_on ğ•œ s) :
  differentiable_on ğ•œ (iterated_fderiv_within ğ•œ m f s) s :=
Î» x hx, ((h.ftaylor_series_within hs).fderiv_within m hmn x hx).differentiable_within_at

lemma times_cont_diff_on_iff_continuous_on_differentiable_on {n : with_top â„•}
  (hs : unique_diff_on ğ•œ s) :
  times_cont_diff_on ğ•œ n f s â†”
  (âˆ€ (m : â„•), (m : with_top â„•) â‰¤ n â†’
    continuous_on (Î» x, iterated_fderiv_within ğ•œ m f s x) s)
  âˆ§ (âˆ€ (m : â„•), (m : with_top â„•) < n â†’
    differentiable_on ğ•œ (Î» x, iterated_fderiv_within ğ•œ m f s x) s) :=
begin
  split,
  { assume h,
    split,
    { assume m hm, exact h.continuous_on_iterated_fderiv_within hm hs },
    { assume m hm, exact h.differentiable_on_iterated_fderiv_within hm hs } },
  { assume h,
    exact times_cont_diff_on_of_continuous_on_differentiable_on h.1 h.2 }
end

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative (expressed with `fderiv_within`) is `C^n`. -/
theorem times_cont_diff_on_succ_iff_fderiv_within {n : â„•} (hs : unique_diff_on ğ•œ s) :
  times_cont_diff_on ğ•œ ((n + 1) : â„•) f s â†”
  differentiable_on ğ•œ f s âˆ§ times_cont_diff_on ğ•œ n (Î» y, fderiv_within ğ•œ f s y) s :=
begin
  split,
  { assume H,
    refine âŸ¨H.differentiable_on (with_top.coe_le_coe.2 (nat.le_add_left 1 n)), Î» x hx, _âŸ©,
    rcases times_cont_diff_within_at_succ_iff_has_fderiv_within_at.1 (H x hx)
      with âŸ¨u, hu, f', hff', hf'âŸ©,
    rcases mem_nhds_within.1 hu with âŸ¨o, o_open, xo, hoâŸ©,
    rw [inter_comm, insert_eq_of_mem hx] at ho,
    have := hf'.mono ho,
    rw times_cont_diff_within_at_inter' (mem_nhds_within_of_mem_nhds (is_open.mem_nhds o_open xo))
      at this,
    apply this.congr_of_eventually_eq' _ hx,
    have : o âˆ© s âˆˆ ğ“[s] x := mem_nhds_within.2 âŸ¨o, o_open, xo, subset.refl _âŸ©,
    rw inter_comm at this,
    apply filter.eventually_eq_of_mem this (Î» y hy, _),
    have A : fderiv_within ğ•œ f (s âˆ© o) y = f' y :=
      ((hff' y (ho hy)).mono ho).fderiv_within (hs.inter o_open y hy),
    rwa fderiv_within_inter (is_open.mem_nhds o_open hy.2) (hs y hy.1) at A, },
  { rintros âŸ¨hdiff, hâŸ© x hx,
    rw [times_cont_diff_within_at_succ_iff_has_fderiv_within_at, insert_eq_of_mem hx],
    exact âŸ¨s, self_mem_nhds_within, fderiv_within ğ•œ f s,
      Î» y hy, (hdiff y hy).has_fderiv_within_at, h x hxâŸ© }
end

/-- A function is `C^(n + 1)` on an open domain if and only if it is
differentiable there, and its derivative (expressed with `fderiv`) is `C^n`. -/
theorem times_cont_diff_on_succ_iff_fderiv_of_open {n : â„•} (hs : is_open s) :
  times_cont_diff_on ğ•œ ((n + 1) : â„•) f s â†”
  differentiable_on ğ•œ f s âˆ§ times_cont_diff_on ğ•œ n (Î» y, fderiv ğ•œ f y) s :=
begin
  rw times_cont_diff_on_succ_iff_fderiv_within hs.unique_diff_on,
  congr' 2,
  rw â† iff_iff_eq,
  apply times_cont_diff_on_congr,
  assume x hx,
  exact fderiv_within_of_open hs hx
end

/-- A function is `C^âˆ` on a domain with unique derivatives if and only if it is differentiable
there, and its derivative (expressed with `fderiv_within`) is `C^âˆ`. -/
theorem times_cont_diff_on_top_iff_fderiv_within (hs : unique_diff_on ğ•œ s) :
  times_cont_diff_on ğ•œ âˆ f s â†”
  differentiable_on ğ•œ f s âˆ§ times_cont_diff_on ğ•œ âˆ (Î» y, fderiv_within ğ•œ f s y) s :=
begin
  split,
  { assume h,
    refine âŸ¨h.differentiable_on le_top, _âŸ©,
    apply times_cont_diff_on_top.2 (Î» n, ((times_cont_diff_on_succ_iff_fderiv_within hs).1 _).2),
    exact h.of_le le_top },
  { assume h,
    refine times_cont_diff_on_top.2 (Î» n, _),
    have A : (n : with_top â„•) â‰¤ âˆ := le_top,
    apply ((times_cont_diff_on_succ_iff_fderiv_within hs).2 âŸ¨h.1, h.2.of_le AâŸ©).of_le,
    exact with_top.coe_le_coe.2 (nat.le_succ n) }
end

/-- A function is `C^âˆ` on an open domain if and only if it is differentiable there, and its
derivative (expressed with `fderiv`) is `C^âˆ`. -/
theorem times_cont_diff_on_top_iff_fderiv_of_open (hs : is_open s) :
  times_cont_diff_on ğ•œ âˆ f s â†”
  differentiable_on ğ•œ f s âˆ§ times_cont_diff_on ğ•œ âˆ (Î» y, fderiv ğ•œ f y) s :=
begin
  rw times_cont_diff_on_top_iff_fderiv_within hs.unique_diff_on,
  congr' 2,
  rw â† iff_iff_eq,
  apply times_cont_diff_on_congr,
  assume x hx,
  exact fderiv_within_of_open hs hx
end

lemma times_cont_diff_on.fderiv_within {m n : with_top â„•}
  (hf : times_cont_diff_on ğ•œ n f s) (hs : unique_diff_on ğ•œ s) (hmn : m + 1 â‰¤ n) :
  times_cont_diff_on ğ•œ m (Î» y, fderiv_within ğ•œ f s y) s :=
begin
  cases m,
  { change âˆ + 1 â‰¤ n at hmn,
    have : n = âˆ, by simpa using hmn,
    rw this at hf,
    exact ((times_cont_diff_on_top_iff_fderiv_within hs).1 hf).2 },
  { change (m.succ : with_top â„•) â‰¤ n at hmn,
    exact ((times_cont_diff_on_succ_iff_fderiv_within hs).1 (hf.of_le hmn)).2 }
end

lemma times_cont_diff_on.fderiv_of_open {m n : with_top â„•}
  (hf : times_cont_diff_on ğ•œ n f s) (hs : is_open s) (hmn : m + 1 â‰¤ n) :
  times_cont_diff_on ğ•œ m (Î» y, fderiv ğ•œ f y) s :=
(hf.fderiv_within hs.unique_diff_on hmn).congr (Î» x hx, (fderiv_within_of_open hs hx).symm)

lemma times_cont_diff_on.continuous_on_fderiv_within {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hs : unique_diff_on ğ•œ s) (hn : 1 â‰¤ n) :
  continuous_on (Î» x, fderiv_within ğ•œ f s x) s :=
((times_cont_diff_on_succ_iff_fderiv_within hs).1 (h.of_le hn)).2.continuous_on

lemma times_cont_diff_on.continuous_on_fderiv_of_open {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n f s) (hs : is_open s) (hn : 1 â‰¤ n) :
  continuous_on (Î» x, fderiv ğ•œ f x) s :=
((times_cont_diff_on_succ_iff_fderiv_of_open hs).1 (h.of_le hn)).2.continuous_on

/-- If a function is at least `C^1`, its bundled derivative (mapping `(x, v)` to `Df(x) v`) is
continuous. -/
lemma times_cont_diff_on.continuous_on_fderiv_within_apply
  {n : with_top â„•} (h : times_cont_diff_on ğ•œ n f s) (hs : unique_diff_on ğ•œ s) (hn : 1 â‰¤ n) :
  continuous_on (Î»p : E Ã— E, (fderiv_within ğ•œ f s p.1 : E â†’ F) p.2) (set.prod s univ) :=
begin
  have A : continuous (Î»q : (E â†’L[ğ•œ] F) Ã— E, q.1 q.2) := is_bounded_bilinear_map_apply.continuous,
  have B : continuous_on (Î»p : E Ã— E, (fderiv_within ğ•œ f s p.1, p.2)) (set.prod s univ),
  { apply continuous_on.prod _ continuous_snd.continuous_on,
    exact continuous_on.comp (h.continuous_on_fderiv_within hs hn) continuous_fst.continuous_on
      (prod_subset_preimage_fst _ _) },
  exact A.comp_continuous_on B
end

/-! ### Functions with a Taylor series on the whole space -/

/-- `has_ftaylor_series_up_to n f p` registers the fact that `p 0 = f` and `p (m+1)` is a
derivative of `p m` for `m < n`, and is continuous for `m â‰¤ n`. This is a predicate analogous to
`has_fderiv_at` but for higher order derivatives. -/
structure has_ftaylor_series_up_to (n : with_top â„•)
  (f : E â†’ F) (p : E â†’ formal_multilinear_series ğ•œ E F) : Prop :=
(zero_eq : âˆ€ x, (p x 0).uncurry0 = f x)
(fderiv  : âˆ€ (m : â„•) (hm : (m : with_top â„•) < n), âˆ€ x,
    has_fderiv_at (Î» y, p y m) (p x m.succ).curry_left x)
(cont    : âˆ€ (m : â„•) (hm : (m : with_top â„•) â‰¤ n), continuous (Î» x, p x m))

lemma has_ftaylor_series_up_to.zero_eq' {n : with_top â„•}
  (h : has_ftaylor_series_up_to n f p) (x : E) :
  p x 0 = (continuous_multilinear_curry_fin0 ğ•œ E F).symm (f x) :=
by { rw â† h.zero_eq x, symmetry, exact continuous_multilinear_map.uncurry0_curry0 _ }

lemma has_ftaylor_series_up_to_on_univ_iff {n : with_top â„•} :
  has_ftaylor_series_up_to_on n f p univ â†” has_ftaylor_series_up_to n f p :=
begin
  split,
  { assume H,
    split,
    { exact Î» x, H.zero_eq x (mem_univ x) },
    { assume m hm x,
      rw â† has_fderiv_within_at_univ,
      exact H.fderiv_within m hm x (mem_univ x) },
    { assume m hm,
      rw continuous_iff_continuous_on_univ,
      exact H.cont m hm } },
  { assume H,
    split,
    { exact Î» x hx, H.zero_eq x },
    { assume m hm x hx,
      rw has_fderiv_within_at_univ,
      exact H.fderiv m hm x },
    { assume m hm,
      rw â† continuous_iff_continuous_on_univ,
      exact H.cont m hm } }
end

lemma has_ftaylor_series_up_to.has_ftaylor_series_up_to_on {n : with_top â„•}
  (h : has_ftaylor_series_up_to n f p) (s : set E) :
  has_ftaylor_series_up_to_on n f p s :=
(has_ftaylor_series_up_to_on_univ_iff.2 h).mono (subset_univ _)

lemma has_ftaylor_series_up_to.of_le {m n : with_top â„•}
  (h : has_ftaylor_series_up_to n f p) (hmn : m â‰¤ n) :
  has_ftaylor_series_up_to m f p :=
by { rw â† has_ftaylor_series_up_to_on_univ_iff at h âŠ¢, exact h.of_le hmn }

lemma has_ftaylor_series_up_to.continuous {n : with_top â„•}
  (h : has_ftaylor_series_up_to n f p) : continuous f :=
begin
  rw â† has_ftaylor_series_up_to_on_univ_iff at h,
  rw continuous_iff_continuous_on_univ,
  exact h.continuous_on
end

lemma has_ftaylor_series_up_to_zero_iff :
  has_ftaylor_series_up_to 0 f p â†” continuous f âˆ§ (âˆ€ x, (p x 0).uncurry0 = f x) :=
by simp [has_ftaylor_series_up_to_on_univ_iff.symm, continuous_iff_continuous_on_univ,
         has_ftaylor_series_up_to_on_zero_iff]

/-- If a function has a Taylor series at order at least `1`, then the term of order `1` of this
series is a derivative of `f`. -/
lemma has_ftaylor_series_up_to.has_fderiv_at {n : with_top â„•}
  (h : has_ftaylor_series_up_to n f p) (hn : 1 â‰¤ n) (x : E) :
  has_fderiv_at f (continuous_multilinear_curry_fin1 ğ•œ E F (p x 1)) x :=
begin
  rw [â† has_fderiv_within_at_univ],
  exact (has_ftaylor_series_up_to_on_univ_iff.2 h).has_fderiv_within_at hn (mem_univ _)
end

lemma has_ftaylor_series_up_to.differentiable {n : with_top â„•}
  (h : has_ftaylor_series_up_to n f p) (hn : 1 â‰¤ n) : differentiable ğ•œ f :=
Î» x, (h.has_fderiv_at hn x).differentiable_at

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p.shift` is a Taylor series up to `n`
for `p 1`, which is a derivative of `f`. -/
theorem has_ftaylor_series_up_to_succ_iff_right {n : â„•} :
  has_ftaylor_series_up_to ((n + 1) : â„•) f p â†”
  (âˆ€ x, (p x 0).uncurry0 = f x)
  âˆ§ (âˆ€ x, has_fderiv_at (Î» y, p y 0) (p x 1).curry_left x)
  âˆ§ has_ftaylor_series_up_to n
    (Î» x, continuous_multilinear_curry_fin1 ğ•œ E F (p x 1)) (Î» x, (p x).shift) :=
by simp [has_ftaylor_series_up_to_on_succ_iff_right, has_ftaylor_series_up_to_on_univ_iff.symm,
         -add_comm, -with_zero.coe_add]

/-! ### Smooth functions at a point -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` at a point `x` if, for any integer `k â‰¤ n`,
there is a neighborhood of `x` where `f` admits derivatives up to order `n`, which are continuous.
-/
def times_cont_diff_at (n : with_top â„•) (f : E â†’ F) (x : E) :=
times_cont_diff_within_at ğ•œ n f univ x

variable {ğ•œ}

theorem times_cont_diff_within_at_univ {n : with_top â„•} :
  times_cont_diff_within_at ğ•œ n f univ x â†” times_cont_diff_at ğ•œ n f x :=
iff.rfl

lemma times_cont_diff_at_top :
  times_cont_diff_at ğ•œ âˆ f x â†” âˆ€ (n : â„•), times_cont_diff_at ğ•œ n f x :=
by simp [â† times_cont_diff_within_at_univ, times_cont_diff_within_at_top]

lemma times_cont_diff_at.times_cont_diff_within_at {n : with_top â„•}
  (h : times_cont_diff_at ğ•œ n f x) : times_cont_diff_within_at ğ•œ n f s x :=
h.mono (subset_univ _)

lemma times_cont_diff_within_at.times_cont_diff_at {n : with_top â„•}
  (h : times_cont_diff_within_at ğ•œ n f s x) (hx : s âˆˆ ğ“ x) :
  times_cont_diff_at ğ•œ n f x :=
by rwa [times_cont_diff_at, â† times_cont_diff_within_at_inter hx, univ_inter]

lemma times_cont_diff_at.congr_of_eventually_eq {n : with_top â„•}
  (h : times_cont_diff_at ğ•œ n f x) (hg : fâ‚ =á¶ [ğ“ x] f) :
  times_cont_diff_at ğ•œ n fâ‚ x :=
h.congr_of_eventually_eq' (by rwa nhds_within_univ) (mem_univ x)

lemma times_cont_diff_at.of_le {m n : with_top â„•}
  (h : times_cont_diff_at ğ•œ n f x) (hmn : m â‰¤ n) :
  times_cont_diff_at ğ•œ m f x :=
h.of_le hmn

lemma times_cont_diff_at.continuous_at {n : with_top â„•}
  (h : times_cont_diff_at ğ•œ n f x) : continuous_at f x :=
by simpa [continuous_within_at_univ] using h.continuous_within_at

/-- If a function is `C^n` with `n â‰¥ 1` at a point, then it is differentiable there. -/
lemma times_cont_diff_at.differentiable_at {n : with_top â„•}
  (h : times_cont_diff_at ğ•œ n f x) (hn : 1 â‰¤ n) : differentiable_at ğ•œ f x :=
by simpa [hn, differentiable_within_at_univ] using h.differentiable_within_at

/-- A function is `C^(n + 1)` at a point iff locally, it has a derivative which is `C^n`. -/
theorem times_cont_diff_at_succ_iff_has_fderiv_at {n : â„•} :
  times_cont_diff_at ğ•œ ((n + 1) : â„•) f x
  â†” (âˆƒ f' : E â†’ (E â†’L[ğ•œ] F), (âˆƒ u âˆˆ ğ“ x, (âˆ€ x âˆˆ u, has_fderiv_at f (f' x) x))
      âˆ§ (times_cont_diff_at ğ•œ n f' x)) :=
begin
  rw [â† times_cont_diff_within_at_univ, times_cont_diff_within_at_succ_iff_has_fderiv_within_at],
  simp only [nhds_within_univ, exists_prop, mem_univ, insert_eq_of_mem],
  split,
  { rintros âŸ¨u, H, f', h_fderiv, h_times_cont_diffâŸ©,
    rcases mem_nhds_iff.mp H with âŸ¨t, htu, ht, hxtâŸ©,
    refine âŸ¨f', âŸ¨t, _âŸ©, h_times_cont_diff.times_cont_diff_at HâŸ©,
    refine âŸ¨mem_nhds_iff.mpr âŸ¨t, subset.rfl, ht, hxtâŸ©, _âŸ©,
    intros y hyt,
    refine (h_fderiv y (htu hyt)).has_fderiv_at _,
    exact mem_nhds_iff.mpr âŸ¨t, htu, ht, hytâŸ© },
  { rintros âŸ¨f', âŸ¨u, H, h_fderivâŸ©, h_times_cont_diffâŸ©,
    refine âŸ¨u, H, f', _, h_times_cont_diff.times_cont_diff_within_atâŸ©,
    intros x hxu,
    exact (h_fderiv x hxu).has_fderiv_within_at }
end

/-! ### Smooth functions -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` if it admits derivatives up to
order `n`, which are continuous. Contrary to the case of definitions in domains (where derivatives
might not be unique) we do not need to localize the definition in space or time.
-/
definition times_cont_diff (n : with_top â„•) (f : E â†’ F)  :=
âˆƒ p : E â†’ formal_multilinear_series ğ•œ E F, has_ftaylor_series_up_to n f p

variable {ğ•œ}

theorem times_cont_diff_on_univ {n : with_top â„•} :
  times_cont_diff_on ğ•œ n f univ â†” times_cont_diff ğ•œ n f :=
begin
  split,
  { assume H,
    use ftaylor_series_within ğ•œ f univ,
    rw â† has_ftaylor_series_up_to_on_univ_iff,
    exact H.ftaylor_series_within unique_diff_on_univ },
  { rintros âŸ¨p, hpâŸ© x hx m hm,
    exact âŸ¨univ, filter.univ_sets _, p, (hp.has_ftaylor_series_up_to_on univ).of_le hmâŸ© }
end

lemma times_cont_diff_iff_times_cont_diff_at {n : with_top â„•} :
  times_cont_diff ğ•œ n f â†” âˆ€ x, times_cont_diff_at ğ•œ n f x :=
by simp [â† times_cont_diff_on_univ, times_cont_diff_on, times_cont_diff_at]

lemma times_cont_diff.times_cont_diff_at {n : with_top â„•} (h : times_cont_diff ğ•œ n f) :
  times_cont_diff_at ğ•œ n f x :=
times_cont_diff_iff_times_cont_diff_at.1 h x

lemma times_cont_diff.times_cont_diff_within_at {n : with_top â„•} (h : times_cont_diff ğ•œ n f) :
  times_cont_diff_within_at ğ•œ n f s x :=
h.times_cont_diff_at.times_cont_diff_within_at

lemma times_cont_diff_top :
  times_cont_diff ğ•œ âˆ f â†” âˆ€ (n : â„•), times_cont_diff ğ•œ n f :=
by simp [times_cont_diff_on_univ.symm, times_cont_diff_on_top]

lemma times_cont_diff_all_iff_nat :
  (âˆ€ n, times_cont_diff ğ•œ n f) â†” (âˆ€ n : â„•, times_cont_diff ğ•œ n f) :=
by simp only [â† times_cont_diff_on_univ, times_cont_diff_on_all_iff_nat]

lemma times_cont_diff.times_cont_diff_on {n : with_top â„•}
  (h : times_cont_diff ğ•œ n f) : times_cont_diff_on ğ•œ n f s :=
(times_cont_diff_on_univ.2 h).mono (subset_univ _)

@[simp] lemma times_cont_diff_zero :
  times_cont_diff ğ•œ 0 f â†” continuous f :=
begin
  rw [â† times_cont_diff_on_univ, continuous_iff_continuous_on_univ],
  exact times_cont_diff_on_zero
end

lemma times_cont_diff_at_zero :
  times_cont_diff_at ğ•œ 0 f x â†” âˆƒ u âˆˆ ğ“ x, continuous_on f u :=
by { rw â† times_cont_diff_within_at_univ, simp [times_cont_diff_within_at_zero, nhds_within_univ] }

lemma times_cont_diff.of_le {m n : with_top â„•}
  (h : times_cont_diff ğ•œ n f) (hmn : m â‰¤ n) :
  times_cont_diff ğ•œ m f :=
times_cont_diff_on_univ.1 $ (times_cont_diff_on_univ.2 h).of_le hmn

lemma times_cont_diff.continuous {n : with_top â„•}
  (h : times_cont_diff ğ•œ n f) : continuous f :=
times_cont_diff_zero.1 (h.of_le bot_le)

/-- If a function is `C^n` with `n â‰¥ 1`, then it is differentiable. -/
lemma times_cont_diff.differentiable {n : with_top â„•}
  (h : times_cont_diff ğ•œ n f) (hn : 1 â‰¤ n) : differentiable ğ•œ f :=
differentiable_on_univ.1 $ (times_cont_diff_on_univ.2 h).differentiable_on hn


/-! ### Iterated derivative -/

variable (ğ•œ)

/-- The `n`-th derivative of a function, as a multilinear map, defined inductively. -/
noncomputable def iterated_fderiv (n : â„•) (f : E â†’ F) :
  E â†’ (E [Ã—n]â†’L[ğ•œ] F) :=
nat.rec_on n
  (Î» x, continuous_multilinear_map.curry0 ğ•œ E (f x))
  (Î» n rec x, continuous_linear_map.uncurry_left (fderiv ğ•œ rec x))

/-- Formal Taylor series associated to a function within a set. -/
def ftaylor_series (f : E â†’ F) (x : E) : formal_multilinear_series ğ•œ E F :=
Î» n, iterated_fderiv ğ•œ n f x

variable {ğ•œ}

@[simp] lemma iterated_fderiv_zero_apply (m : (fin 0) â†’ E) :
  (iterated_fderiv ğ•œ 0 f x : ((fin 0) â†’  E) â†’ F) m = f x := rfl

lemma iterated_fderiv_zero_eq_comp :
  iterated_fderiv ğ•œ 0 f = (continuous_multilinear_curry_fin0 ğ•œ E F).symm âˆ˜ f := rfl

lemma iterated_fderiv_succ_apply_left {n : â„•} (m : fin (n + 1) â†’ E):
  (iterated_fderiv ğ•œ (n + 1) f x : (fin (n + 1) â†’ E) â†’ F) m
  = (fderiv ğ•œ (iterated_fderiv ğ•œ n f) x : E â†’ (E [Ã—n]â†’L[ğ•œ] F)) (m 0) (tail m) := rfl

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the derivative of the `n`-th derivative. -/
lemma iterated_fderiv_succ_eq_comp_left {n : â„•} :
  iterated_fderiv ğ•œ (n + 1) f =
  (continuous_multilinear_curry_left_equiv ğ•œ (Î»(i : fin (n + 1)), E) F)
    âˆ˜ (fderiv ğ•œ (iterated_fderiv ğ•œ n f)) := rfl

lemma iterated_fderiv_within_univ {n : â„•} :
  iterated_fderiv_within ğ•œ n f univ = iterated_fderiv ğ•œ n f :=
begin
  induction n with n IH,
  { ext x, simp },
  { ext x m,
    rw [iterated_fderiv_succ_apply_left, iterated_fderiv_within_succ_apply_left, IH,
        fderiv_within_univ] }
end

lemma ftaylor_series_within_univ :
  ftaylor_series_within ğ•œ f univ = ftaylor_series ğ•œ f :=
begin
  ext1 x, ext1 n,
  change iterated_fderiv_within ğ•œ n f univ x = iterated_fderiv ğ•œ n f x,
  rw iterated_fderiv_within_univ
end

theorem iterated_fderiv_succ_apply_right {n : â„•} (m : fin (n + 1) â†’ E) :
  (iterated_fderiv ğ•œ (n + 1) f x : (fin (n + 1) â†’ E) â†’ F) m
    = iterated_fderiv ğ•œ n (Î»y, fderiv ğ•œ f y) x (init m) (m (last n)) :=
begin
  rw [â† iterated_fderiv_within_univ, â† iterated_fderiv_within_univ, â† fderiv_within_univ],
  exact iterated_fderiv_within_succ_apply_right unique_diff_on_univ (mem_univ _) _
end

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the `n`-th derivative of the derivative. -/
lemma iterated_fderiv_succ_eq_comp_right {n : â„•} :
  iterated_fderiv ğ•œ (n + 1) f x =
  ((continuous_multilinear_curry_right_equiv' ğ•œ n E F)
    âˆ˜ (iterated_fderiv ğ•œ n (Î»y, fderiv ğ•œ f y))) x :=
by { ext m, rw iterated_fderiv_succ_apply_right, refl }

@[simp] lemma iterated_fderiv_one_apply (m : (fin 1) â†’ E) :
  (iterated_fderiv ğ•œ 1 f x : ((fin 1) â†’ E) â†’ F) m
  = (fderiv ğ•œ f x : E â†’ F) (m 0) :=
by { rw [iterated_fderiv_succ_apply_right, iterated_fderiv_zero_apply], refl }

/-- When a function is `C^n` in a set `s` of unique differentiability, it admits
`ftaylor_series_within ğ•œ f s` as a Taylor series up to order `n` in `s`. -/
theorem times_cont_diff_on_iff_ftaylor_series {n : with_top â„•} :
  times_cont_diff ğ•œ n f â†” has_ftaylor_series_up_to n f (ftaylor_series ğ•œ f) :=
begin
  split,
  { rw [â† times_cont_diff_on_univ, â† has_ftaylor_series_up_to_on_univ_iff,
        â† ftaylor_series_within_univ],
    exact Î» h, times_cont_diff_on.ftaylor_series_within h unique_diff_on_univ },
  { assume h, exact âŸ¨ftaylor_series ğ•œ f, hâŸ© }
end

lemma times_cont_diff_iff_continuous_differentiable {n : with_top â„•} :
  times_cont_diff ğ•œ n f â†”
  (âˆ€ (m : â„•), (m : with_top â„•) â‰¤ n â†’ continuous (Î» x, iterated_fderiv ğ•œ m f x))
  âˆ§ (âˆ€ (m : â„•), (m : with_top â„•) < n â†’ differentiable ğ•œ (Î» x, iterated_fderiv ğ•œ m f x)) :=
by simp [times_cont_diff_on_univ.symm, continuous_iff_continuous_on_univ,
    differentiable_on_univ.symm, iterated_fderiv_within_univ,
    times_cont_diff_on_iff_continuous_on_differentiable_on unique_diff_on_univ]

lemma times_cont_diff_of_differentiable_iterated_fderiv {n : with_top â„•}
  (h : âˆ€(m : â„•), (m : with_top â„•) â‰¤ n â†’ differentiable ğ•œ (iterated_fderiv ğ•œ m f)) :
  times_cont_diff ğ•œ n f :=
times_cont_diff_iff_continuous_differentiable.2
âŸ¨Î» m hm, (h m hm).continuous, Î» m hm, (h m (le_of_lt hm))âŸ©

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if
it is differentiable there, and its derivative is `C^n`. -/
theorem times_cont_diff_succ_iff_fderiv {n : â„•} :
  times_cont_diff ğ•œ ((n + 1) : â„•) f â†”
  differentiable ğ•œ f âˆ§ times_cont_diff ğ•œ n (Î» y, fderiv ğ•œ f y) :=
by simp [times_cont_diff_on_univ.symm, differentiable_on_univ.symm, fderiv_within_univ.symm,
         - fderiv_within_univ, times_cont_diff_on_succ_iff_fderiv_within unique_diff_on_univ,
         -with_zero.coe_add, -add_comm]

/-- A function is `C^âˆ` on a domain with unique derivatives if and only if it is differentiable
there, and its derivative is `C^âˆ`. -/
theorem times_cont_diff_top_iff_fderiv :
  times_cont_diff ğ•œ âˆ f â†”
  differentiable ğ•œ f âˆ§ times_cont_diff ğ•œ âˆ (Î» y, fderiv ğ•œ f y) :=
begin
  simp [times_cont_diff_on_univ.symm, differentiable_on_univ.symm, fderiv_within_univ.symm,
        - fderiv_within_univ],
  rw times_cont_diff_on_top_iff_fderiv_within unique_diff_on_univ,
end

lemma times_cont_diff.continuous_fderiv {n : with_top â„•}
  (h : times_cont_diff ğ•œ n f) (hn : 1 â‰¤ n) :
  continuous (Î» x, fderiv ğ•œ f x) :=
((times_cont_diff_succ_iff_fderiv).1 (h.of_le hn)).2.continuous

/-- If a function is at least `C^1`, its bundled derivative (mapping `(x, v)` to `Df(x) v`) is
continuous. -/
lemma times_cont_diff.continuous_fderiv_apply {n : with_top â„•}
  (h : times_cont_diff ğ•œ n f) (hn : 1 â‰¤ n) :
  continuous (Î»p : E Ã— E, (fderiv ğ•œ f p.1 : E â†’ F) p.2) :=
begin
  have A : continuous (Î»q : (E â†’L[ğ•œ] F) Ã— E, q.1 q.2) := is_bounded_bilinear_map_apply.continuous,
  have B : continuous (Î»p : E Ã— E, (fderiv ğ•œ f p.1, p.2)),
  { apply continuous.prod_mk _ continuous_snd,
    exact continuous.comp (h.continuous_fderiv hn) continuous_fst },
  exact A.comp B
end

/-! ### Constants -/

lemma iterated_fderiv_within_zero_fun {n : â„•} :
  iterated_fderiv ğ•œ n (Î» x : E, (0 : F)) = 0 :=
begin
  induction n with n IH,
  { ext m, simp },
  { ext x m,
    rw [iterated_fderiv_succ_apply_left, IH],
    change (fderiv ğ•œ (Î» (x : E), (0 : (E [Ã—n]â†’L[ğ•œ] F))) x : E â†’ (E [Ã—n]â†’L[ğ•œ] F)) (m 0) (tail m) = _,
    rw fderiv_const,
    refl }
end

lemma times_cont_diff_zero_fun {n : with_top â„•} :
  times_cont_diff ğ•œ n (Î» x : E, (0 : F)) :=
begin
  apply times_cont_diff_of_differentiable_iterated_fderiv (Î»m hm, _),
  rw iterated_fderiv_within_zero_fun,
  apply differentiable_const (0 : (E [Ã—m]â†’L[ğ•œ] F))
end

/--
Constants are `C^âˆ`.
-/
lemma times_cont_diff_const {n : with_top â„•} {c : F} : times_cont_diff ğ•œ n (Î»x : E, c) :=
begin
  suffices h : times_cont_diff ğ•œ âˆ (Î»x : E, c), by exact h.of_le le_top,
  rw times_cont_diff_top_iff_fderiv,
  refine âŸ¨differentiable_const c, _âŸ©,
  rw fderiv_const,
  exact times_cont_diff_zero_fun
end

lemma times_cont_diff_on_const {n : with_top â„•} {c : F} {s : set E} :
  times_cont_diff_on ğ•œ n (Î»x : E, c) s :=
times_cont_diff_const.times_cont_diff_on

lemma times_cont_diff_at_const {n : with_top â„•} {c : F} :
  times_cont_diff_at ğ•œ n (Î»x : E, c) x :=
times_cont_diff_const.times_cont_diff_at

lemma times_cont_diff_within_at_const {n : with_top â„•} {c : F} :
  times_cont_diff_within_at ğ•œ n (Î»x : E, c) s x :=
times_cont_diff_at_const.times_cont_diff_within_at

@[nontriviality] lemma times_cont_diff_of_subsingleton [subsingleton F] {n : with_top â„•} :
  times_cont_diff ğ•œ n f :=
by { rw [subsingleton.elim f (Î» _, 0)], exact times_cont_diff_const }

@[nontriviality] lemma times_cont_diff_at_of_subsingleton [subsingleton F] {n : with_top â„•} :
  times_cont_diff_at ğ•œ n f x :=
by { rw [subsingleton.elim f (Î» _, 0)], exact times_cont_diff_at_const }

@[nontriviality] lemma times_cont_diff_within_at_of_subsingleton [subsingleton F] {n : with_top â„•} :
  times_cont_diff_within_at ğ•œ n f s x :=
by { rw [subsingleton.elim f (Î» _, 0)], exact times_cont_diff_within_at_const }

@[nontriviality] lemma times_cont_diff_on_of_subsingleton [subsingleton F] {n : with_top â„•} :
  times_cont_diff_on ğ•œ n f s :=
by { rw [subsingleton.elim f (Î» _, 0)], exact times_cont_diff_on_const }

/-! ### Linear functions -/

/--
Unbundled bounded linear functions are `C^âˆ`.
-/
lemma is_bounded_linear_map.times_cont_diff {n : with_top â„•} (hf : is_bounded_linear_map ğ•œ f) :
  times_cont_diff ğ•œ n f :=
begin
  suffices h : times_cont_diff ğ•œ âˆ f, by exact h.of_le le_top,
  rw times_cont_diff_top_iff_fderiv,
  refine âŸ¨hf.differentiable, _âŸ©,
  simp [hf.fderiv],
  exact times_cont_diff_const
end

lemma continuous_linear_map.times_cont_diff {n : with_top â„•} (f : E â†’L[ğ•œ] F) :
  times_cont_diff ğ•œ n f :=
f.is_bounded_linear_map.times_cont_diff

lemma continuous_linear_equiv.times_cont_diff {n : with_top â„•} (f : E â‰ƒL[ğ•œ] F) :
  times_cont_diff ğ•œ n f :=
(f : E â†’L[ğ•œ] F).times_cont_diff

lemma linear_isometry_map.times_cont_diff {n : with_top â„•} (f : E â†’â‚—áµ¢[ğ•œ] F) :
  times_cont_diff ğ•œ n f :=
f.to_continuous_linear_map.times_cont_diff

lemma linear_isometry_equiv.times_cont_diff {n : with_top â„•} (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
  times_cont_diff ğ•œ n f :=
(f : E â†’L[ğ•œ] F).times_cont_diff

/--
The first projection in a product is `C^âˆ`.
-/
lemma times_cont_diff_fst {n : with_top â„•} : times_cont_diff ğ•œ n (prod.fst : E Ã— F â†’ E) :=
is_bounded_linear_map.times_cont_diff is_bounded_linear_map.fst

/--
The first projection on a domain in a product is `C^âˆ`.
-/
lemma times_cont_diff_on_fst {s : set (EÃ—F)} {n : with_top â„•} :
  times_cont_diff_on ğ•œ n (prod.fst : E Ã— F â†’ E) s :=
times_cont_diff.times_cont_diff_on times_cont_diff_fst

/--
The first projection at a point in a product is `C^âˆ`.
-/
lemma times_cont_diff_at_fst {p : E Ã— F} {n : with_top â„•} :
  times_cont_diff_at ğ•œ n (prod.fst : E Ã— F â†’ E) p :=
times_cont_diff_fst.times_cont_diff_at

/--
The first projection within a domain at a point in a product is `C^âˆ`.
-/
lemma times_cont_diff_within_at_fst {s : set (E Ã— F)} {p : E Ã— F} {n : with_top â„•} :
  times_cont_diff_within_at ğ•œ n (prod.fst : E Ã— F â†’ E) s p :=
times_cont_diff_fst.times_cont_diff_within_at

/--
The second projection in a product is `C^âˆ`.
-/
lemma times_cont_diff_snd {n : with_top â„•} : times_cont_diff ğ•œ n (prod.snd : E Ã— F â†’ F) :=
is_bounded_linear_map.times_cont_diff is_bounded_linear_map.snd

/--
The second projection on a domain in a product is `C^âˆ`.
-/
lemma times_cont_diff_on_snd {s : set (EÃ—F)} {n : with_top â„•} :
  times_cont_diff_on ğ•œ n (prod.snd : E Ã— F â†’ F) s :=
times_cont_diff.times_cont_diff_on times_cont_diff_snd

/--
The second projection at a point in a product is `C^âˆ`.
-/
lemma times_cont_diff_at_snd {p : E Ã— F} {n : with_top â„•} :
  times_cont_diff_at ğ•œ n (prod.snd : E Ã— F â†’ F) p :=
times_cont_diff_snd.times_cont_diff_at

/--
The second projection within a domain at a point in a product is `C^âˆ`.
-/
lemma times_cont_diff_within_at_snd {s : set (E Ã— F)} {p : E Ã— F} {n : with_top â„•} :
  times_cont_diff_within_at ğ•œ n (prod.snd : E Ã— F â†’ F) s p :=
times_cont_diff_snd.times_cont_diff_within_at

/--
The identity is `C^âˆ`.
-/
lemma times_cont_diff_id {n : with_top â„•} : times_cont_diff ğ•œ n (id : E â†’ E) :=
is_bounded_linear_map.id.times_cont_diff

lemma times_cont_diff_within_at_id {n : with_top â„•} {s x} :
  times_cont_diff_within_at ğ•œ n (id : E â†’ E) s x :=
times_cont_diff_id.times_cont_diff_within_at

lemma times_cont_diff_at_id {n : with_top â„•} {x} :
  times_cont_diff_at ğ•œ n (id : E â†’ E) x :=
times_cont_diff_id.times_cont_diff_at

lemma times_cont_diff_on_id {n : with_top â„•} {s} :
  times_cont_diff_on ğ•œ n (id : E â†’ E) s :=
times_cont_diff_id.times_cont_diff_on

/--
Bilinear functions are `C^âˆ`.
-/
lemma is_bounded_bilinear_map.times_cont_diff {n : with_top â„•} (hb : is_bounded_bilinear_map ğ•œ b) :
  times_cont_diff ğ•œ n b :=
begin
  suffices h : times_cont_diff ğ•œ âˆ b, by exact h.of_le le_top,
  rw times_cont_diff_top_iff_fderiv,
  refine âŸ¨hb.differentiable, _âŸ©,
  simp [hb.fderiv],
  exact hb.is_bounded_linear_map_deriv.times_cont_diff
end

/-- If `f` admits a Taylor series `p` in a set `s`, and `g` is linear, then `g âˆ˜ f` admits a Taylor
series whose `k`-th term is given by `g âˆ˜ (p k)`. -/
lemma has_ftaylor_series_up_to_on.continuous_linear_map_comp {n : with_top â„•} (g : F â†’L[ğ•œ] G)
  (hf : has_ftaylor_series_up_to_on n f p s) :
  has_ftaylor_series_up_to_on n (g âˆ˜ f) (Î» x k, g.comp_continuous_multilinear_map (p x k)) s :=
begin
  set L : Î  m : â„•, (E [Ã—m]â†’L[ğ•œ] F) â†’L[ğ•œ] (E [Ã—m]â†’L[ğ•œ] G) :=
    Î» m, continuous_linear_map.comp_continuous_multilinear_mapL g,
  split,
  { exact Î» x hx, congr_arg g (hf.zero_eq x hx) },
  { intros m hm x hx,
    convert (L m).has_fderiv_at.comp_has_fderiv_within_at x (hf.fderiv_within m hm x hx) },
  { intros m hm,
    convert (L m).continuous.comp_continuous_on (hf.cont m hm) }
end

/-- Composition by continuous linear maps on the left preserves `C^n` functions in a domain
at a point. -/
lemma times_cont_diff_within_at.continuous_linear_map_comp {n : with_top â„•} (g : F â†’L[ğ•œ] G)
  (hf : times_cont_diff_within_at ğ•œ n f s x) :
  times_cont_diff_within_at ğ•œ n (g âˆ˜ f) s x :=
begin
  assume m hm,
  rcases hf m hm with âŸ¨u, hu, p, hpâŸ©,
  exact âŸ¨u, hu, _, hp.continuous_linear_map_comp gâŸ©,
end

/-- Composition by continuous linear maps on the left preserves `C^n` functions in a domain
at a point. -/
lemma times_cont_diff_at.continuous_linear_map_comp {n : with_top â„•} (g : F â†’L[ğ•œ] G)
  (hf : times_cont_diff_at ğ•œ n f x) :
  times_cont_diff_at ğ•œ n (g âˆ˜ f) x :=
times_cont_diff_within_at.continuous_linear_map_comp g hf

/-- Composition by continuous linear maps on the left preserves `C^n` functions on domains. -/
lemma times_cont_diff_on.continuous_linear_map_comp {n : with_top â„•} (g : F â†’L[ğ•œ] G)
  (hf : times_cont_diff_on ğ•œ n f s) :
  times_cont_diff_on ğ•œ n (g âˆ˜ f) s :=
Î» x hx, (hf x hx).continuous_linear_map_comp g

/-- Composition by continuous linear maps on the left preserves `C^n` functions. -/
lemma times_cont_diff.continuous_linear_map_comp {n : with_top â„•} {f : E â†’ F} (g : F â†’L[ğ•œ] G)
  (hf : times_cont_diff ğ•œ n f) : times_cont_diff ğ•œ n (Î»x, g (f x)) :=
times_cont_diff_on_univ.1 $ times_cont_diff_on.continuous_linear_map_comp
  _ (times_cont_diff_on_univ.2 hf)

/-- Composition by continuous linear equivs on the left respects higher differentiability on
domains. -/
lemma continuous_linear_equiv.comp_times_cont_diff_within_at_iff
  {n : with_top â„•} (e : F â‰ƒL[ğ•œ] G) :
  times_cont_diff_within_at ğ•œ n (e âˆ˜ f) s x â†” times_cont_diff_within_at ğ•œ n f s x :=
begin
  split,
  { assume H,
    have : f = e.symm âˆ˜ (e âˆ˜ f),
      by { ext y, simp only [function.comp_app], rw e.symm_apply_apply (f y) },
    rw this,
    exact H.continuous_linear_map_comp _ },
  { assume H,
    exact H.continuous_linear_map_comp _ }
end

/-- Composition by continuous linear equivs on the left respects higher differentiability on
domains. -/
lemma continuous_linear_equiv.comp_times_cont_diff_on_iff
  {n : with_top â„•} (e : F â‰ƒL[ğ•œ] G) :
  times_cont_diff_on ğ•œ n (e âˆ˜ f) s â†” times_cont_diff_on ğ•œ n f s :=
by simp [times_cont_diff_on, e.comp_times_cont_diff_within_at_iff]

/-- If `f` admits a Taylor series `p` in a set `s`, and `g` is linear, then `f âˆ˜ g` admits a Taylor
series in `g â»Â¹' s`, whose `k`-th term is given by `p k (g vâ‚, ..., g vâ‚–)` . -/
lemma has_ftaylor_series_up_to_on.comp_continuous_linear_map {n : with_top â„•}
  (hf : has_ftaylor_series_up_to_on n f p s) (g : G â†’L[ğ•œ] E) :
  has_ftaylor_series_up_to_on n (f âˆ˜ g)
    (Î» x k, (p (g x) k).comp_continuous_linear_map (Î» _, g)) (g â»Â¹' s) :=
begin
  let A : Î  m : â„•, (E [Ã—m]â†’L[ğ•œ] F) â†’ (G [Ã—m]â†’L[ğ•œ] F) :=
    Î» m h, h.comp_continuous_linear_map (Î» _, g),
  have hA : âˆ€ m, is_bounded_linear_map ğ•œ (A m) :=
    Î» m, is_bounded_linear_map_continuous_multilinear_map_comp_linear g,
  split,
  { assume x hx,
    simp only [(hf.zero_eq (g x) hx).symm, function.comp_app],
    change p (g x) 0 (Î» (i : fin 0), g 0) = p (g x) 0 0,
    rw continuous_linear_map.map_zero,
    refl },
  { assume m hm x hx,
    convert ((hA m).has_fderiv_at).comp_has_fderiv_within_at x
      ((hf.fderiv_within m hm (g x) hx).comp x (g.has_fderiv_within_at) (subset.refl _)),
    ext y v,
    change p (g x) (nat.succ m) (g âˆ˜ (cons y v)) = p (g x) m.succ (cons (g y) (g âˆ˜ v)),
    rw comp_cons },
  { assume m hm,
    exact (hA m).continuous.comp_continuous_on
      ((hf.cont m hm).comp g.continuous.continuous_on (subset.refl _)) }
end

/-- Composition by continuous linear maps on the right preserves `C^n` functions at a point on
a domain. -/
lemma times_cont_diff_within_at.comp_continuous_linear_map {n : with_top â„•} {x : G}
  (g : G â†’L[ğ•œ] E) (hf : times_cont_diff_within_at ğ•œ n f s (g x)) :
  times_cont_diff_within_at ğ•œ n (f âˆ˜ g) (g â»Â¹' s) x :=
begin
  assume m hm,
  rcases hf m hm with âŸ¨u, hu, p, hpâŸ©,
  refine âŸ¨g â»Â¹' u, _, _, hp.comp_continuous_linear_map gâŸ©,
  apply continuous_within_at.preimage_mem_nhds_within',
  { exact g.continuous.continuous_within_at },
  { apply nhds_within_mono (g x) _ hu,
    rw image_insert_eq,
    exact insert_subset_insert (image_preimage_subset g s) }
end

/-- Composition by continuous linear maps on the right preserves `C^n` functions on domains. -/
lemma times_cont_diff_on.comp_continuous_linear_map {n : with_top â„•}
  (hf : times_cont_diff_on ğ•œ n f s) (g : G â†’L[ğ•œ] E) :
  times_cont_diff_on ğ•œ n (f âˆ˜ g) (g â»Â¹' s) :=
Î» x hx, (hf (g x) hx).comp_continuous_linear_map g

/-- Composition by continuous linear maps on the right preserves `C^n` functions. -/
lemma times_cont_diff.comp_continuous_linear_map {n : with_top â„•} {f : E â†’ F} {g : G â†’L[ğ•œ] E}
  (hf : times_cont_diff ğ•œ n f) : times_cont_diff ğ•œ n (f âˆ˜ g) :=
times_cont_diff_on_univ.1 $
times_cont_diff_on.comp_continuous_linear_map (times_cont_diff_on_univ.2 hf) _

/-- Composition by continuous linear equivs on the right respects higher differentiability at a
point in a domain. -/
lemma continuous_linear_equiv.times_cont_diff_within_at_comp_iff {n : with_top â„•} (e : G â‰ƒL[ğ•œ] E) :
  times_cont_diff_within_at ğ•œ n (f âˆ˜ e) (e â»Â¹' s) (e.symm x) â†”
  times_cont_diff_within_at ğ•œ n f s x :=
begin
  split,
  { assume H,
    have A : f = (f âˆ˜ e) âˆ˜ e.symm,
      by { ext y, simp only [function.comp_app], rw e.apply_symm_apply y },
    have B : e.symm â»Â¹' (e â»Â¹' s) = s,
      by { rw [â† preimage_comp, e.self_comp_symm], refl },
    rw [A, â† B],
    exact H.comp_continuous_linear_map _},
  { assume H,
    have : x = e (e.symm x), by simp,
    rw this at H,
    exact H.comp_continuous_linear_map _ },
end


/-- Composition by continuous linear equivs on the right respects higher differentiability on
domains. -/
lemma continuous_linear_equiv.times_cont_diff_on_comp_iff {n : with_top â„•} (e : G â‰ƒL[ğ•œ] E) :
  times_cont_diff_on ğ•œ n (f âˆ˜ e) (e â»Â¹' s) â†” times_cont_diff_on ğ•œ n f s :=
begin
  refine âŸ¨Î» H, _, Î» H, H.comp_continuous_linear_map _âŸ©,
  have A : f = (f âˆ˜ e) âˆ˜ e.symm,
    by { ext y, simp only [function.comp_app], rw e.apply_symm_apply y },
  have B : e.symm â»Â¹' (e â»Â¹' s) = s,
    by { rw [â† preimage_comp, e.self_comp_symm], refl },
  rw [A, â† B],
  exact H.comp_continuous_linear_map _
end

/-- If two functions `f` and `g` admit Taylor series `p` and `q` in a set `s`, then the cartesian
product of `f` and `g` admits the cartesian product of `p` and `q` as a Taylor series. -/
lemma has_ftaylor_series_up_to_on.prod {n : with_top â„•} (hf : has_ftaylor_series_up_to_on n f p s)
  {g : E â†’ G} {q : E â†’ formal_multilinear_series ğ•œ E G} (hg : has_ftaylor_series_up_to_on n g q s) :
  has_ftaylor_series_up_to_on n (Î» y, (f y, g y)) (Î» y k, (p y k).prod (q y k)) s :=
begin
  set L := Î» m, continuous_multilinear_map.prodL ğ•œ (Î» i : fin m, E) F G,
  split,
  { assume x hx, rw [â† hf.zero_eq x hx, â† hg.zero_eq x hx], refl },
  { assume m hm x hx,
    convert (L m).has_fderiv_at.comp_has_fderiv_within_at x
      ((hf.fderiv_within m hm x hx).prod (hg.fderiv_within m hm x hx)) },
  { assume m hm,
    exact (L m).continuous.comp_continuous_on ((hf.cont m hm).prod (hg.cont m hm)) }
end

/-- The cartesian product of `C^n` functions at a point in a domain is `C^n`. -/
lemma times_cont_diff_within_at.prod {n : with_top â„•} {s : set E} {f : E â†’ F} {g : E â†’ G}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (hg : times_cont_diff_within_at ğ•œ n g s x) :
  times_cont_diff_within_at ğ•œ n (Î»x:E, (f x, g x)) s x :=
begin
  assume m hm,
  rcases hf m hm with âŸ¨u, hu, p, hpâŸ©,
  rcases hg m hm with âŸ¨v, hv, q, hqâŸ©,
  exact âŸ¨u âˆ© v, filter.inter_mem hu hv, _,
        (hp.mono (inter_subset_left u v)).prod (hq.mono (inter_subset_right u v))âŸ©
end

/-- The cartesian product of `C^n` functions on domains is `C^n`. -/
lemma times_cont_diff_on.prod {n : with_top â„•} {s : set E} {f : E â†’ F} {g : E â†’ G}
  (hf : times_cont_diff_on ğ•œ n f s) (hg : times_cont_diff_on ğ•œ n g s) :
  times_cont_diff_on ğ•œ n (Î»x:E, (f x, g x)) s :=
Î» x hx, (hf x hx).prod (hg x hx)

/-- The cartesian product of `C^n` functions at a point is `C^n`. -/
lemma times_cont_diff_at.prod {n : with_top â„•} {f : E â†’ F} {g : E â†’ G}
  (hf : times_cont_diff_at ğ•œ n f x) (hg : times_cont_diff_at ğ•œ n g x) :
  times_cont_diff_at ğ•œ n (Î»x:E, (f x, g x)) x :=
times_cont_diff_within_at_univ.1 $ times_cont_diff_within_at.prod
  (times_cont_diff_within_at_univ.2 hf)
  (times_cont_diff_within_at_univ.2 hg)

/--
The cartesian product of `C^n` functions is `C^n`.
-/
lemma times_cont_diff.prod {n : with_top â„•} {f : E â†’ F} {g : E â†’ G}
  (hf : times_cont_diff ğ•œ n f) (hg : times_cont_diff ğ•œ n g) :
  times_cont_diff ğ•œ n (Î»x:E, (f x, g x)) :=
times_cont_diff_on_univ.1 $ times_cont_diff_on.prod (times_cont_diff_on_univ.2 hf)
  (times_cont_diff_on_univ.2 hg)

/-!
### Smoothness of functions `f : E â†’ Î  i, F' i`
-/

section pi

variables {Î¹ : Type*} [fintype Î¹] {F' : Î¹ â†’ Type*} [Î  i, normed_group (F' i)]
  [Î  i, normed_space ğ•œ (F' i)] {Ï† : Î  i, E â†’ F' i}
  {p' : Î  i, E â†’ formal_multilinear_series ğ•œ E (F' i)}
  {Î¦ : E â†’ Î  i, F' i} {P' : E â†’ formal_multilinear_series ğ•œ E (Î  i, F' i)}
  {n : with_top â„•}

lemma has_ftaylor_series_up_to_on_pi :
  has_ftaylor_series_up_to_on n (Î» x i, Ï† i x)
    (Î» x m, continuous_multilinear_map.pi (Î» i, p' i x m)) s â†”
    âˆ€ i, has_ftaylor_series_up_to_on n (Ï† i) (p' i) s :=
begin
  set pr := @continuous_linear_map.proj ğ•œ _ Î¹ F' _ _ _,
  letI : Î  (m : â„•) (i : Î¹), normed_space ğ•œ (E [Ã—m]â†’L[ğ•œ] (F' i)) := Î» m i, infer_instance,
  set L : Î  m : â„•, (Î  i, E [Ã—m]â†’L[ğ•œ] (F' i)) â‰ƒâ‚—áµ¢[ğ•œ] (E [Ã—m]â†’L[ğ•œ] (Î  i, F' i)) :=
    Î» m, continuous_multilinear_map.piâ‚—áµ¢ _ _,
  refine âŸ¨Î» h i, _, Î» h, âŸ¨Î» x hx, _, _, _âŸ©âŸ©,
  { convert h.continuous_linear_map_comp (pr i),
    ext, refl },
  { ext1 i,
    exact (h i).zero_eq x hx },
  { intros m hm x hx,
    have := has_fderiv_within_at_pi.2 (Î» i, (h i).fderiv_within m hm x hx),
    convert (L m).has_fderiv_at.comp_has_fderiv_within_at x this },
  { intros m hm,
    have := continuous_on_pi.2 (Î» i, (h i).cont m hm),
    convert (L m).continuous.comp_continuous_on this }
end

@[simp] lemma has_ftaylor_series_up_to_on_pi' :
  has_ftaylor_series_up_to_on n Î¦ P' s â†”
    âˆ€ i, has_ftaylor_series_up_to_on n (Î» x, Î¦ x i)
      (Î» x m, (@continuous_linear_map.proj ğ•œ _ Î¹ F' _ _ _ i).comp_continuous_multilinear_map
        (P' x m)) s :=
by { convert has_ftaylor_series_up_to_on_pi, ext, refl }

lemma times_cont_diff_within_at_pi :
  times_cont_diff_within_at ğ•œ n Î¦ s x â†”
    âˆ€ i, times_cont_diff_within_at ğ•œ n (Î» x, Î¦ x i) s x :=
begin
  set pr := @continuous_linear_map.proj ğ•œ _ Î¹ F' _ _ _,
  refine âŸ¨Î» h i, h.continuous_linear_map_comp (pr i), Î» h m hm, _âŸ©,
  choose u hux p hp using Î» i, h i m hm,
  exact âŸ¨â‹‚ i, u i, filter.Inter_mem.2 hux, _,
    has_ftaylor_series_up_to_on_pi.2 (Î» i, (hp i).mono $ Inter_subset _ _)âŸ©,
end

lemma times_cont_diff_on_pi :
  times_cont_diff_on ğ•œ n Î¦ s â†” âˆ€ i, times_cont_diff_on ğ•œ n (Î» x, Î¦ x i) s :=
âŸ¨Î» h i x hx, times_cont_diff_within_at_pi.1 (h x hx) _,
  Î» h x hx, times_cont_diff_within_at_pi.2 (Î» i, h i x hx)âŸ©

lemma times_cont_diff_at_pi :
  times_cont_diff_at ğ•œ n Î¦ x â†” âˆ€ i, times_cont_diff_at ğ•œ n (Î» x, Î¦ x i) x :=
times_cont_diff_within_at_pi

lemma times_cont_diff_pi :
  times_cont_diff ğ•œ n Î¦ â†” âˆ€ i, times_cont_diff ğ•œ n (Î» x, Î¦ x i) :=
by simp only [â† times_cont_diff_on_univ, times_cont_diff_on_pi]

end pi

/-!
### Composition of `C^n` functions

We show that the composition of `C^n` functions is `C^n`. One way to prove it would be to write
the `n`-th derivative of the composition (this is FaÃ  di Bruno's formula) and check its continuity,
but this is very painful. Instead, we go for a simple inductive proof. Assume it is done for `n`.
Then, to check it for `n+1`, one needs to check that the derivative of `g âˆ˜ f` is `C^n`, i.e.,
that `Dg(f x) â¬ Df(x)` is `C^n`. The term `Dg (f x)` is the composition of two `C^n` functions, so
it is `C^n` by the inductive assumption. The term `Df(x)` is also `C^n`. Then, the matrix
multiplication is the application of a bilinear map (which is `C^âˆ`, and therefore `C^n`) to
`x â†¦ (Dg(f x), Df x)`. As the composition of two `C^n` maps, it is again `C^n`, and we are done.

There is a subtlety in this argument: we apply the inductive assumption to functions on other Banach
spaces. In maths, one would say: prove by induction over `n` that, for all `C^n` maps between all
pairs of Banach spaces, their composition is `C^n`. In Lean, this is fine as long as the spaces
stay in the same universe. This is not the case in the above argument: if `E` lives in universe `u`
and `F` lives in universe `v`, then linear maps from `E` to `F` (to which the derivative of `f`
belongs) is in universe `max u v`. If one could quantify over finitely many universes, the above
proof would work fine, but this is not the case. One could still write the proof considering spaces
in any universe in `u, v, w, max u v, max v w, max u v w`, but it would be extremely tedious and
lead to a lot of duplication. Instead, we formulate the above proof when all spaces live in the same
universe (where everything is fine), and then we deduce the general result by lifting all our spaces
to a common universe. We use the trick that any space `H` is isomorphic through a continuous linear
equiv to `continuous_multilinear_map (Î» (i : fin 0), E Ã— F Ã— G) H` to change the universe level,
and then argue that composing with such a linear equiv does not change the fact of being `C^n`,
which we have already proved previously.
-/

/-- Auxiliary lemma proving that the composition of `C^n` functions on domains is `C^n` when all
spaces live in the same universe. Use instead `times_cont_diff_on.comp` which removes the universe
assumption (but is deduced from this one). -/
private lemma times_cont_diff_on.comp_same_univ
  {Eu : Type u} [normed_group Eu] [normed_space ğ•œ Eu]
  {Fu : Type u} [normed_group Fu] [normed_space ğ•œ Fu]
  {Gu : Type u} [normed_group Gu] [normed_space ğ•œ Gu]
  {n : with_top â„•} {s : set Eu} {t : set Fu} {g : Fu â†’ Gu} {f : Eu â†’ Fu}
  (hg : times_cont_diff_on ğ•œ n g t) (hf : times_cont_diff_on ğ•œ n f s) (st : s âŠ† f â»Â¹' t) :
  times_cont_diff_on ğ•œ n (g âˆ˜ f) s :=
begin
  unfreezingI { induction n using with_top.nat_induction with n IH Itop generalizing Eu Fu Gu },
  { rw times_cont_diff_on_zero at hf hg âŠ¢,
    exact continuous_on.comp hg hf st },
  { rw times_cont_diff_on_succ_iff_has_fderiv_within_at at hg âŠ¢,
    assume x hx,
    rcases (times_cont_diff_on_succ_iff_has_fderiv_within_at.1 hf) x hx
      with âŸ¨u, hu, f', hf', f'_diffâŸ©,
    rcases hg (f x) (st hx) with âŸ¨v, hv, g', hg', g'_diffâŸ©,
    rw insert_eq_of_mem hx at hu âŠ¢,
    have xu : x âˆˆ u := mem_of_mem_nhds_within hx hu,
    let w := s âˆ© (u âˆ© fâ»Â¹' v),
    have wv : w âŠ† f â»Â¹' v := Î» y hy, hy.2.2,
    have wu : w âŠ† u := Î» y hy, hy.2.1,
    have ws : w âŠ† s := Î» y hy, hy.1,
    refine âŸ¨w, _, Î» y, (g' (f y)).comp (f' y), _, _âŸ©,
    show w âˆˆ ğ“[s] x,
    { apply filter.inter_mem self_mem_nhds_within,
      apply filter.inter_mem hu,
      apply continuous_within_at.preimage_mem_nhds_within',
      { rw â† continuous_within_at_inter' hu,
        exact (hf' x xu).differentiable_within_at.continuous_within_at.mono
          (inter_subset_right _ _) },
      { apply nhds_within_mono _ _ hv,
        exact subset.trans (image_subset_iff.mpr st) (subset_insert (f x) t) } },
    show âˆ€ y âˆˆ w,
      has_fderiv_within_at (g âˆ˜ f) ((g' (f y)).comp (f' y)) w y,
    { rintros y âŸ¨ys, yu, yvâŸ©,
      exact (hg' (f y) yv).comp y ((hf' y yu).mono wu) wv },
    show times_cont_diff_on ğ•œ n (Î» y, (g' (f y)).comp (f' y)) w,
    { have A : times_cont_diff_on ğ•œ n (Î» y, g' (f y)) w :=
        IH g'_diff ((hf.of_le (with_top.coe_le_coe.2 (nat.le_succ n))).mono ws) wv,
      have B : times_cont_diff_on ğ•œ n f' w := f'_diff.mono wu,
      have C : times_cont_diff_on ğ•œ n (Î» y, (f' y, g' (f y))) w :=
        times_cont_diff_on.prod B A,
      have D : times_cont_diff_on ğ•œ n (Î»(p : (Eu â†’L[ğ•œ] Fu) Ã— (Fu â†’L[ğ•œ] Gu)), p.2.comp p.1) univ :=
        is_bounded_bilinear_map_comp.times_cont_diff.times_cont_diff_on,
      exact IH D C (subset_univ _) } },
  { rw times_cont_diff_on_top at hf hg âŠ¢,
    assume n,
    apply Itop n (hg n) (hf n) st }
end

/-- The composition of `C^n` functions on domains is `C^n`. -/
lemma times_cont_diff_on.comp
  {n : with_top â„•} {s : set E} {t : set F} {g : F â†’ G} {f : E â†’ F}
  (hg : times_cont_diff_on ğ•œ n g t) (hf : times_cont_diff_on ğ•œ n f s) (st : s âŠ† f â»Â¹' t) :
  times_cont_diff_on ğ•œ n (g âˆ˜ f) s :=
begin
  /- we lift all the spaces to a common universe, as we have already proved the result in this
  situation. For the lift, we use the trick that `H` is isomorphic through a
  continuous linear equiv to `continuous_multilinear_map ğ•œ (Î» (i : fin 0), (E Ã— F Ã— G)) H`, and
  continuous linear equivs respect smoothness classes. -/
  let Eu := continuous_multilinear_map ğ•œ (Î» (i : fin 0), (E Ã— F Ã— G)) E,
  letI : normed_group Eu := by apply_instance,
  letI : normed_space ğ•œ Eu := by apply_instance,
  let Fu := continuous_multilinear_map ğ•œ (Î» (i : fin 0), (E Ã— F Ã— G)) F,
  letI : normed_group Fu := by apply_instance,
  letI : normed_space ğ•œ Fu := by apply_instance,
  let Gu := continuous_multilinear_map ğ•œ (Î» (i : fin 0), (E Ã— F Ã— G)) G,
  letI : normed_group Gu := by apply_instance,
  letI : normed_space ğ•œ Gu := by apply_instance,
  -- declare the isomorphisms
  let isoE : Eu â‰ƒL[ğ•œ] E := continuous_multilinear_curry_fin0 ğ•œ (E Ã— F Ã— G) E,
  let isoF : Fu â‰ƒL[ğ•œ] F := continuous_multilinear_curry_fin0 ğ•œ (E Ã— F Ã— G) F,
  let isoG : Gu â‰ƒL[ğ•œ] G := continuous_multilinear_curry_fin0 ğ•œ (E Ã— F Ã— G) G,
  -- lift the functions to the new spaces, check smoothness there, and then go back.
  let fu : Eu â†’ Fu := (isoF.symm âˆ˜ f) âˆ˜ isoE,
  have fu_diff : times_cont_diff_on ğ•œ n fu (isoE â»Â¹' s),
    by rwa [isoE.times_cont_diff_on_comp_iff, isoF.symm.comp_times_cont_diff_on_iff],
  let gu : Fu â†’ Gu := (isoG.symm âˆ˜ g) âˆ˜ isoF,
  have gu_diff : times_cont_diff_on ğ•œ n gu (isoF â»Â¹' t),
    by rwa [isoF.times_cont_diff_on_comp_iff, isoG.symm.comp_times_cont_diff_on_iff],
  have main : times_cont_diff_on ğ•œ n (gu âˆ˜ fu) (isoE â»Â¹' s),
  { apply times_cont_diff_on.comp_same_univ gu_diff fu_diff,
    assume y hy,
    simp only [fu, continuous_linear_equiv.coe_apply, function.comp_app, mem_preimage],
    rw isoF.apply_symm_apply (f (isoE y)),
    exact st hy },
  have : gu âˆ˜ fu = (isoG.symm âˆ˜ (g âˆ˜ f)) âˆ˜ isoE,
  { ext y,
    simp only [function.comp_apply, gu, fu],
    rw isoF.apply_symm_apply (f (isoE y)) },
  rwa [this, isoE.times_cont_diff_on_comp_iff, isoG.symm.comp_times_cont_diff_on_iff] at main
end

/-- The composition of `C^n` functions on domains is `C^n`. -/
lemma times_cont_diff_on.comp'
  {n : with_top â„•} {s : set E} {t : set F} {g : F â†’ G} {f : E â†’ F}
  (hg : times_cont_diff_on ğ•œ n g t) (hf : times_cont_diff_on ğ•œ n f s) :
  times_cont_diff_on ğ•œ n (g âˆ˜ f) (s âˆ© fâ»Â¹' t) :=
hg.comp (hf.mono (inter_subset_left _ _)) (inter_subset_right _ _)

/-- The composition of a `C^n` function on a domain with a `C^n` function is `C^n`. -/
lemma times_cont_diff.comp_times_cont_diff_on {n : with_top â„•} {s : set E} {g : F â†’ G} {f : E â†’ F}
  (hg : times_cont_diff ğ•œ n g) (hf : times_cont_diff_on ğ•œ n f s) :
  times_cont_diff_on ğ•œ n (g âˆ˜ f) s :=
(times_cont_diff_on_univ.2 hg).comp hf subset_preimage_univ

/-- The composition of `C^n` functions is `C^n`. -/
lemma times_cont_diff.comp {n : with_top â„•} {g : F â†’ G} {f : E â†’ F}
  (hg : times_cont_diff ğ•œ n g) (hf : times_cont_diff ğ•œ n f) :
  times_cont_diff ğ•œ n (g âˆ˜ f) :=
times_cont_diff_on_univ.1 $ times_cont_diff_on.comp (times_cont_diff_on_univ.2 hg)
  (times_cont_diff_on_univ.2 hf) (subset_univ _)

/-- The composition of `C^n` functions at points in domains is `C^n`. -/
lemma times_cont_diff_within_at.comp
  {n : with_top â„•} {s : set E} {t : set F} {g : F â†’ G} {f : E â†’ F} (x : E)
  (hg : times_cont_diff_within_at ğ•œ n g t (f x))
  (hf : times_cont_diff_within_at ğ•œ n f s x) (st : s âŠ† f â»Â¹' t) :
  times_cont_diff_within_at ğ•œ n (g âˆ˜ f) s x :=
begin
  assume m hm,
  rcases hg.times_cont_diff_on hm with âŸ¨u, u_nhd, ut, huâŸ©,
  rcases hf.times_cont_diff_on hm with âŸ¨v, v_nhd, vs, hvâŸ©,
  have xmem : x âˆˆ f â»Â¹' u âˆ© v :=
    âŸ¨(mem_of_mem_nhds_within (mem_insert (f x) _) u_nhd : _),
    mem_of_mem_nhds_within (mem_insert x s) v_nhdâŸ©,
  have : f â»Â¹' u âˆˆ ğ“[insert x s] x,
  { apply hf.continuous_within_at.insert_self.preimage_mem_nhds_within',
    apply nhds_within_mono _ _ u_nhd,
    rw image_insert_eq,
    exact insert_subset_insert (image_subset_iff.mpr st) },
  have Z := ((hu.comp (hv.mono (inter_subset_right (f â»Â¹' u) v)) (inter_subset_left _ _))
    .times_cont_diff_within_at) xmem m (le_refl _),
  have : ğ“[f â»Â¹' u âˆ© v] x = ğ“[insert x s] x,
  { have A : f â»Â¹' u âˆ© v = (insert x s) âˆ© (f â»Â¹' u âˆ© v),
    { apply subset.antisymm _ (inter_subset_right _ _),
      rintros y âŸ¨hy1, hy2âŸ©,
      simp [hy1, hy2, vs hy2] },
    rw [A, â† nhds_within_restrict''],
    exact filter.inter_mem this v_nhd },
  rwa [insert_eq_of_mem xmem, this] at Z,
end

/-- The composition of `C^n` functions at points in domains is `C^n`. -/
lemma times_cont_diff_within_at.comp' {n : with_top â„•} {s : set E} {t : set F} {g : F â†’ G}
  {f : E â†’ F} (x : E)
  (hg : times_cont_diff_within_at ğ•œ n g t (f x)) (hf : times_cont_diff_within_at ğ•œ n f s x) :
  times_cont_diff_within_at ğ•œ n (g âˆ˜ f) (s âˆ© fâ»Â¹' t) x :=
hg.comp x (hf.mono (inter_subset_left _ _)) (inter_subset_right _ _)

lemma times_cont_diff_at.comp_times_cont_diff_within_at {n} (x : E)
  (hg : times_cont_diff_at ğ•œ n g (f x)) (hf : times_cont_diff_within_at ğ•œ n f s x) :
  times_cont_diff_within_at ğ•œ n (g âˆ˜ f) s x :=
hg.comp x hf (maps_to_univ _ _)

/-- The composition of `C^n` functions at points is `C^n`. -/
lemma times_cont_diff_at.comp {n : with_top â„•} (x : E)
  (hg : times_cont_diff_at ğ•œ n g (f x))
  (hf : times_cont_diff_at ğ•œ n f x) :
  times_cont_diff_at ğ•œ n (g âˆ˜ f) x :=
hg.comp x hf subset_preimage_univ

lemma times_cont_diff.comp_times_cont_diff_within_at
  {n : with_top â„•} {g : F â†’ G} {f : E â†’ F} (h : times_cont_diff ğ•œ n g)
  (hf : times_cont_diff_within_at ğ•œ n f t x) :
  times_cont_diff_within_at ğ•œ n (g âˆ˜ f) t x :=
begin
  have : times_cont_diff_within_at ğ•œ n g univ (f x) :=
    h.times_cont_diff_at.times_cont_diff_within_at,
  exact this.comp x hf (subset_univ _),
end

lemma times_cont_diff.comp_times_cont_diff_at
  {n : with_top â„•} {g : F â†’ G} {f : E â†’ F} (x : E)
  (hg : times_cont_diff ğ•œ n g)
  (hf : times_cont_diff_at ğ•œ n f x) :
  times_cont_diff_at ğ•œ n (g âˆ˜ f) x :=
hg.comp_times_cont_diff_within_at hf

/-- The bundled derivative of a `C^{n+1}` function is `C^n`. -/
lemma times_cont_diff_on_fderiv_within_apply {m n : with_top  â„•} {s : set E}
  {f : E â†’ F} (hf : times_cont_diff_on ğ•œ n f s) (hs : unique_diff_on ğ•œ s) (hmn : m + 1 â‰¤ n) :
  times_cont_diff_on ğ•œ m (Î»p : E Ã— E, (fderiv_within ğ•œ f s p.1 : E â†’L[ğ•œ] F) p.2)
  (set.prod s (univ : set E)) :=
begin
  have A : times_cont_diff ğ•œ m (Î»p : (E â†’L[ğ•œ] F) Ã— E, p.1 p.2),
  { apply is_bounded_bilinear_map.times_cont_diff,
    exact is_bounded_bilinear_map_apply },
  have B : times_cont_diff_on ğ•œ m
    (Î» (p : E Ã— E), ((fderiv_within ğ•œ f s p.fst), p.snd)) (set.prod s univ),
  { apply times_cont_diff_on.prod _ _,
    { have I : times_cont_diff_on ğ•œ m (Î» (x : E), fderiv_within ğ•œ f s x) s :=
        hf.fderiv_within hs hmn,
      have J : times_cont_diff_on ğ•œ m (Î» (x : E Ã— E), x.1) (set.prod s univ) :=
        times_cont_diff_fst.times_cont_diff_on,
      exact times_cont_diff_on.comp I J (prod_subset_preimage_fst _ _) },
    { apply times_cont_diff.times_cont_diff_on _ ,
      apply is_bounded_linear_map.snd.times_cont_diff } },
  exact A.comp_times_cont_diff_on B
end

/-- The bundled derivative of a `C^{n+1}` function is `C^n`. -/
lemma times_cont_diff.times_cont_diff_fderiv_apply {n m : with_top â„•} {f : E â†’ F}
  (hf : times_cont_diff ğ•œ n f) (hmn : m + 1 â‰¤ n) :
  times_cont_diff ğ•œ m (Î»p : E Ã— E, (fderiv ğ•œ f p.1 : E â†’L[ğ•œ] F) p.2) :=
begin
  rw â† times_cont_diff_on_univ at âŠ¢ hf,
  rw [â† fderiv_within_univ, â† univ_prod_univ],
  exact times_cont_diff_on_fderiv_within_apply hf unique_diff_on_univ hmn
end

/-! ### Sum of two functions -/

/- The sum is smooth. -/
lemma times_cont_diff_add {n : with_top â„•} :
  times_cont_diff ğ•œ n (Î»p : F Ã— F, p.1 + p.2) :=
(is_bounded_linear_map.fst.add is_bounded_linear_map.snd).times_cont_diff

/-- The sum of two `C^n` functions within a set at a point is `C^n` within this set
at this point. -/
lemma times_cont_diff_within_at.add {n : with_top â„•} {s : set E} {f g : E â†’ F}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (hg : times_cont_diff_within_at ğ•œ n g s x) :
  times_cont_diff_within_at ğ•œ n (Î»x, f x + g x) s x :=
times_cont_diff_add.times_cont_diff_within_at.comp x (hf.prod hg) subset_preimage_univ

/-- The sum of two `C^n` functions at a point is `C^n` at this point. -/
lemma times_cont_diff_at.add {n : with_top â„•} {f g : E â†’ F}
  (hf : times_cont_diff_at ğ•œ n f x) (hg : times_cont_diff_at ğ•œ n g x) :
  times_cont_diff_at ğ•œ n (Î»x, f x + g x) x :=
by rw [â† times_cont_diff_within_at_univ] at *; exact hf.add hg

/-- The sum of two `C^n`functions is `C^n`. -/
lemma times_cont_diff.add {n : with_top â„•} {f g : E â†’ F}
  (hf : times_cont_diff ğ•œ n f) (hg : times_cont_diff ğ•œ n g) :
  times_cont_diff ğ•œ n (Î»x, f x + g x) :=
times_cont_diff_add.comp (hf.prod hg)

/-- The sum of two `C^n` functions on a domain is `C^n`. -/
lemma times_cont_diff_on.add {n : with_top â„•} {s : set E} {f g : E â†’ F}
  (hf : times_cont_diff_on ğ•œ n f s) (hg : times_cont_diff_on ğ•œ n g s) :
  times_cont_diff_on ğ•œ n (Î»x, f x + g x) s :=
Î» x hx, (hf x hx).add (hg x hx)

/-! ### Negative -/

/- The negative is smooth. -/
lemma times_cont_diff_neg {n : with_top â„•} :
  times_cont_diff ğ•œ n (Î»p : F, -p) :=
is_bounded_linear_map.id.neg.times_cont_diff

/-- The negative of a `C^n` function within a domain at a point is `C^n` within this domain at
this point. -/
lemma times_cont_diff_within_at.neg {n : with_top â„•} {s : set E} {f : E â†’ F}
  (hf : times_cont_diff_within_at ğ•œ n f s x) : times_cont_diff_within_at ğ•œ n (Î»x, -f x) s x :=
times_cont_diff_neg.times_cont_diff_within_at.comp x hf subset_preimage_univ

/-- The negative of a `C^n` function at a point is `C^n` at this point. -/
lemma times_cont_diff_at.neg {n : with_top â„•} {f : E â†’ F}
  (hf : times_cont_diff_at ğ•œ n f x) : times_cont_diff_at ğ•œ n (Î»x, -f x) x :=
by rw â† times_cont_diff_within_at_univ at *; exact hf.neg

/-- The negative of a `C^n`function is `C^n`. -/
lemma times_cont_diff.neg {n : with_top â„•} {f : E â†’ F} (hf : times_cont_diff ğ•œ n f) :
  times_cont_diff ğ•œ n (Î»x, -f x) :=
times_cont_diff_neg.comp hf

/-- The negative of a `C^n` function on a domain is `C^n`. -/
lemma times_cont_diff_on.neg {n : with_top â„•} {s : set E} {f : E â†’ F}
  (hf : times_cont_diff_on ğ•œ n f s) : times_cont_diff_on ğ•œ n (Î»x, -f x) s :=
Î» x hx, (hf x hx).neg

/-! ### Subtraction -/

/-- The difference of two `C^n` functions within a set at a point is `C^n` within this set
at this point. -/
lemma times_cont_diff_within_at.sub {n : with_top â„•} {s : set E} {f g : E â†’ F}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (hg : times_cont_diff_within_at ğ•œ n g s x) :
  times_cont_diff_within_at ğ•œ n (Î»x, f x - g x) s x :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

/-- The difference of two `C^n` functions at a point is `C^n` at this point. -/
lemma times_cont_diff_at.sub {n : with_top â„•} {f g : E â†’ F}
  (hf : times_cont_diff_at ğ•œ n f x) (hg : times_cont_diff_at ğ•œ n g x) :
  times_cont_diff_at ğ•œ n (Î»x, f x - g x) x :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

/-- The difference of two `C^n` functions on a domain is `C^n`. -/
lemma times_cont_diff_on.sub {n : with_top â„•} {s : set E} {f g : E â†’ F}
  (hf : times_cont_diff_on ğ•œ n f s) (hg : times_cont_diff_on ğ•œ n g s) :
  times_cont_diff_on ğ•œ n (Î»x, f x - g x) s :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

/-- The difference of two `C^n` functions is `C^n`. -/
lemma times_cont_diff.sub {n : with_top â„•} {f g : E â†’ F}
  (hf : times_cont_diff ğ•œ n f) (hg : times_cont_diff ğ•œ n g) : times_cont_diff ğ•œ n (Î»x, f x - g x) :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

/-! ### Sum of finitely many functions -/

lemma times_cont_diff_within_at.sum
  {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : finset Î¹} {n : with_top â„•} {t : set E} {x : E}
  (h : âˆ€ i âˆˆ s, times_cont_diff_within_at ğ•œ n (Î» x, f i x) t x) :
  times_cont_diff_within_at ğ•œ n (Î» x, (âˆ‘ i in s, f i x)) t x :=
begin
  classical,
  induction s using finset.induction_on with i s is IH,
  { simp [times_cont_diff_within_at_const] },
  { simp only [is, finset.sum_insert, not_false_iff],
    exact (h _ (finset.mem_insert_self i s)).add (IH (Î» j hj, h _ (finset.mem_insert_of_mem hj))) }
end

lemma times_cont_diff_at.sum
  {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : finset Î¹} {n : with_top â„•} {x : E}
  (h : âˆ€ i âˆˆ s, times_cont_diff_at ğ•œ n (Î» x, f i x) x) :
  times_cont_diff_at ğ•œ n (Î» x, (âˆ‘ i in s, f i x)) x :=
by rw [â† times_cont_diff_within_at_univ] at *; exact times_cont_diff_within_at.sum h

lemma times_cont_diff_on.sum
  {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : finset Î¹} {n : with_top â„•} {t : set E}
  (h : âˆ€ i âˆˆ s, times_cont_diff_on ğ•œ n (Î» x, f i x) t) :
  times_cont_diff_on ğ•œ n (Î» x, (âˆ‘ i in s, f i x)) t :=
Î» x hx, times_cont_diff_within_at.sum (Î» i hi, h i hi x hx)

lemma times_cont_diff.sum
  {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : finset Î¹} {n : with_top â„•}
  (h : âˆ€ i âˆˆ s, times_cont_diff ğ•œ n (Î» x, f i x)) :
  times_cont_diff ğ•œ n (Î» x, (âˆ‘ i in s, f i x)) :=
by simp [â† times_cont_diff_on_univ] at *; exact times_cont_diff_on.sum h

/-! ### Product of two functions -/

/- The product is smooth. -/
lemma times_cont_diff_mul {n : with_top â„•} :
  times_cont_diff ğ•œ n (Î» p : ğ•œ Ã— ğ•œ, p.1 * p.2) :=
is_bounded_bilinear_map_mul.times_cont_diff

/-- The product of two `C^n` functions within a set at a point is `C^n` within this set
at this point. -/
lemma times_cont_diff_within_at.mul {n : with_top â„•} {s : set E} {f g : E â†’ ğ•œ}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (hg : times_cont_diff_within_at ğ•œ n g s x) :
  times_cont_diff_within_at ğ•œ n (Î» x, f x * g x) s x :=
times_cont_diff_mul.times_cont_diff_within_at.comp x (hf.prod hg) subset_preimage_univ

/-- The product of two `C^n` functions at a point is `C^n` at this point. -/
lemma times_cont_diff_at.mul {n : with_top â„•} {f g : E â†’ ğ•œ}
  (hf : times_cont_diff_at ğ•œ n f x) (hg : times_cont_diff_at ğ•œ n g x) :
  times_cont_diff_at ğ•œ n (Î» x, f x * g x) x :=
by rw [â† times_cont_diff_within_at_univ] at *; exact hf.mul hg

/-- The product of two `C^n` functions on a domain is `C^n`. -/
lemma times_cont_diff_on.mul {n : with_top â„•} {s : set E} {f g : E â†’ ğ•œ}
  (hf : times_cont_diff_on ğ•œ n f s) (hg : times_cont_diff_on ğ•œ n g s) :
  times_cont_diff_on ğ•œ n (Î» x, f x * g x) s :=
Î» x hx, (hf x hx).mul (hg x hx)

/-- The product of two `C^n`functions is `C^n`. -/
lemma times_cont_diff.mul {n : with_top â„•} {f g : E â†’ ğ•œ}
  (hf : times_cont_diff ğ•œ n f) (hg : times_cont_diff ğ•œ n g) :
  times_cont_diff ğ•œ n (Î» x, f x * g x) :=
times_cont_diff_mul.comp (hf.prod hg)

lemma times_cont_diff_within_at.div_const {f : E â†’ ğ•œ} {n} {c : ğ•œ}
  (hf : times_cont_diff_within_at ğ•œ n f s x) :
  times_cont_diff_within_at ğ•œ n (Î» x, f x / c) s x :=
by simpa only [div_eq_mul_inv] using hf.mul times_cont_diff_within_at_const

lemma times_cont_diff_at.div_const {f : E â†’ ğ•œ} {n} {c : ğ•œ} (hf : times_cont_diff_at ğ•œ n f x) :
  times_cont_diff_at ğ•œ n (Î» x, f x / c) x :=
by simpa only [div_eq_mul_inv] using hf.mul times_cont_diff_at_const

lemma times_cont_diff_on.div_const {f : E â†’ ğ•œ} {n} {c : ğ•œ} (hf : times_cont_diff_on ğ•œ n f s) :
  times_cont_diff_on ğ•œ n (Î» x, f x / c) s :=
by simpa only [div_eq_mul_inv] using hf.mul times_cont_diff_on_const

lemma times_cont_diff.div_const {f : E â†’ ğ•œ} {n} {c : ğ•œ} (hf : times_cont_diff ğ•œ n f) :
  times_cont_diff ğ•œ n (Î» x, f x / c) :=
by simpa only [div_eq_mul_inv] using hf.mul times_cont_diff_const

lemma times_cont_diff.pow {n : with_top â„•} {f : E â†’ ğ•œ}
  (hf : times_cont_diff ğ•œ n f) :
  âˆ€ m : â„•, times_cont_diff ğ•œ n (Î» x, (f x) ^ m)
| 0       := by simpa using times_cont_diff_const
| (m + 1) := by simpa [pow_succ] using hf.mul (times_cont_diff.pow m)

lemma times_cont_diff_at.pow {n : with_top â„•} {f : E â†’ ğ•œ} (hf : times_cont_diff_at ğ•œ n f x)
  (m : â„•) : times_cont_diff_at ğ•œ n (Î» y, f y ^ m) x :=
(times_cont_diff_id.pow m).times_cont_diff_at.comp x hf

lemma times_cont_diff_within_at.pow {n : with_top â„•} {f : E â†’ ğ•œ}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (m : â„•) :
  times_cont_diff_within_at ğ•œ n (Î» y, f y ^ m) s x :=
(times_cont_diff_id.pow m).times_cont_diff_at.comp_times_cont_diff_within_at x hf

lemma times_cont_diff_on.pow {n : with_top â„•} {f : E â†’ ğ•œ}
  (hf : times_cont_diff_on ğ•œ n f s) (m : â„•) :
  times_cont_diff_on ğ•œ n (Î» y, f y ^ m) s :=
Î» y hy, (hf y hy).pow m

/-! ### Scalar multiplication -/

/- The scalar multiplication is smooth. -/
lemma times_cont_diff_smul {n : with_top â„•} :
  times_cont_diff ğ•œ n (Î» p : ğ•œ Ã— F, p.1 â€¢ p.2) :=
is_bounded_bilinear_map_smul.times_cont_diff

/-- The scalar multiplication of two `C^n` functions within a set at a point is `C^n` within this
set at this point. -/
lemma times_cont_diff_within_at.smul {n : with_top â„•} {s : set E} {f : E â†’ ğ•œ} {g : E â†’ F}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (hg : times_cont_diff_within_at ğ•œ n g s x) :
  times_cont_diff_within_at ğ•œ n (Î» x, f x â€¢ g x) s x :=
times_cont_diff_smul.times_cont_diff_within_at.comp x (hf.prod hg) subset_preimage_univ

/-- The scalar multiplication of two `C^n` functions at a point is `C^n` at this point. -/
lemma times_cont_diff_at.smul {n : with_top â„•} {f : E â†’ ğ•œ} {g : E â†’ F}
  (hf : times_cont_diff_at ğ•œ n f x) (hg : times_cont_diff_at ğ•œ n g x) :
  times_cont_diff_at ğ•œ n (Î» x, f x â€¢ g x) x :=
by rw [â† times_cont_diff_within_at_univ] at *; exact hf.smul hg

/-- The scalar multiplication of two `C^n` functions is `C^n`. -/
lemma times_cont_diff.smul {n : with_top â„•} {f : E â†’ ğ•œ} {g : E â†’ F}
  (hf : times_cont_diff ğ•œ n f) (hg : times_cont_diff ğ•œ n g) :
  times_cont_diff ğ•œ n (Î» x, f x â€¢ g x) :=
times_cont_diff_smul.comp (hf.prod hg)

/-- The scalar multiplication of two `C^n` functions on a domain is `C^n`. -/
lemma times_cont_diff_on.smul {n : with_top â„•} {s : set E} {f : E â†’ ğ•œ} {g : E â†’ F}
  (hf : times_cont_diff_on ğ•œ n f s) (hg : times_cont_diff_on ğ•œ n g s) :
  times_cont_diff_on ğ•œ n (Î» x, f x â€¢ g x) s :=
Î» x hx, (hf x hx).smul (hg x hx)

/-! ### Cartesian product of two functions-/

section prod_map
variables {E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{F' : Type*} [normed_group F'] [normed_space ğ•œ F']
{n : with_top â„•}

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
lemma times_cont_diff_within_at.prod_map'
  {s : set E} {t : set E'} {f : E â†’ F} {g : E' â†’ F'} {p : E Ã— E'}
  (hf : times_cont_diff_within_at ğ•œ n f s p.1) (hg : times_cont_diff_within_at ğ•œ n g t p.2) :
  times_cont_diff_within_at ğ•œ n (prod.map f g) (set.prod s t) p :=
(hf.comp p times_cont_diff_within_at_fst (prod_subset_preimage_fst _ _)).prod
  (hg.comp p times_cont_diff_within_at_snd (prod_subset_preimage_snd _ _))

lemma times_cont_diff_within_at.prod_map
  {s : set E} {t : set E'} {f : E â†’ F} {g : E' â†’ F'} {x : E} {y : E'}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (hg : times_cont_diff_within_at ğ•œ n g t y) :
  times_cont_diff_within_at ğ•œ n (prod.map f g) (set.prod s t) (x, y) :=
times_cont_diff_within_at.prod_map' hf hg

/-- The product map of two `C^n` functions on a set is `C^n` on the product set. -/
lemma times_cont_diff_on.prod_map {E' : Type*} [normed_group E'] [normed_space ğ•œ E']
  {F' : Type*} [normed_group F'] [normed_space ğ•œ F']
  {s : set E} {t : set E'} {n : with_top â„•} {f : E â†’ F} {g : E' â†’ F'}
  (hf : times_cont_diff_on ğ•œ n f s) (hg : times_cont_diff_on ğ•œ n g t) :
  times_cont_diff_on ğ•œ n (prod.map f g) (set.prod s t) :=
(hf.comp times_cont_diff_on_fst (prod_subset_preimage_fst _ _)).prod
  (hg.comp (times_cont_diff_on_snd) (prod_subset_preimage_snd _ _))

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
lemma times_cont_diff_at.prod_map {f : E â†’ F} {g : E' â†’ F'} {x : E} {y : E'}
  (hf : times_cont_diff_at ğ•œ n f x) (hg : times_cont_diff_at ğ•œ n g y) :
  times_cont_diff_at ğ•œ n (prod.map f g) (x, y) :=
begin
  rw times_cont_diff_at at *,
  convert hf.prod_map hg,
  simp only [univ_prod_univ]
end

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
lemma times_cont_diff_at.prod_map' {f : E â†’ F} {g : E' â†’ F'} {p : E Ã— E'}
  (hf : times_cont_diff_at ğ•œ n f p.1) (hg : times_cont_diff_at ğ•œ n g p.2) :
  times_cont_diff_at ğ•œ n (prod.map f g) p :=
begin
  rcases p,
  exact times_cont_diff_at.prod_map hf hg
end

/-- The product map of two `C^n` functions is `C^n`. -/
lemma times_cont_diff.prod_map
  {f : E â†’ F} {g : E' â†’ F'}
  (hf : times_cont_diff ğ•œ n f) (hg : times_cont_diff ğ•œ n g) :
  times_cont_diff ğ•œ n (prod.map f g) :=
begin
  rw times_cont_diff_iff_times_cont_diff_at at *,
  exact Î» âŸ¨x, yâŸ©, (hf x).prod_map (hg y)
end

end prod_map

/-! ### Inversion in a complete normed algebra -/

section algebra_inverse
variables (ğ•œ) {R : Type*} [normed_ring R] [normed_algebra ğ•œ R]
open normed_ring continuous_linear_map ring

/-- In a complete normed algebra, the operation of inversion is `C^n`, for all `n`, at each
invertible element.  The proof is by induction, bootstrapping using an identity expressing the
derivative of inversion as a bilinear map of inversion itself. -/
lemma times_cont_diff_at_ring_inverse [complete_space R] {n : with_top â„•} (x : units R) :
  times_cont_diff_at ğ•œ n ring.inverse (x : R) :=
begin
  induction n using with_top.nat_induction with n IH Itop,
  { intros m hm,
    refine âŸ¨{y : R | is_unit y}, _, _âŸ©,
    { simp [nhds_within_univ],
      exact x.nhds },
    { use (ftaylor_series_within ğ•œ inverse univ),
      rw [le_antisymm hm bot_le, has_ftaylor_series_up_to_on_zero_iff],
      split,
      { rintros _ âŸ¨x', rflâŸ©,
        exact (inverse_continuous_at x').continuous_within_at },
      { simp [ftaylor_series_within] } } },
  { apply times_cont_diff_at_succ_iff_has_fderiv_at.mpr,
    refine âŸ¨Î» (x : R), - lmul_left_right ğ•œ R (inverse x) (inverse x), _, _âŸ©,
    { refine âŸ¨{y : R | is_unit y}, x.nhds, _âŸ©,
      rintros _ âŸ¨y, rflâŸ©,
      rw [inverse_unit],
      exact has_fderiv_at_ring_inverse y },
    { convert (lmul_left_right_is_bounded_bilinear ğ•œ R).times_cont_diff.neg.comp_times_cont_diff_at
        (x : R) (IH.prod IH) } },
  { exact times_cont_diff_at_top.mpr Itop }
end

variables (ğ•œ) {ğ•œ' : Type*} [normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ'] [complete_space ğ•œ']

lemma times_cont_diff_at_inv {x : ğ•œ'} (hx : x â‰  0) {n} :
  times_cont_diff_at ğ•œ n has_inv.inv x :=
by simpa only [inverse_eq_has_inv] using times_cont_diff_at_ring_inverse ğ•œ (units.mk0 x hx)

lemma times_cont_diff_on_inv {n} : times_cont_diff_on ğ•œ n (has_inv.inv : ğ•œ' â†’ ğ•œ') {0}á¶œ :=
Î» x hx, (times_cont_diff_at_inv ğ•œ hx).times_cont_diff_within_at

variable {ğ•œ}

-- TODO: the next few lemmas don't need `ğ•œ` or `ğ•œ'` to be complete
-- A good way to show this is to generalize `times_cont_diff_at_ring_inverse` to the setting
-- of a function `f` such that `âˆ€á¶  x in ğ“ a, x * f x = 1`.

lemma times_cont_diff_within_at.inv {f : E â†’ ğ•œ'} {n} (hf : times_cont_diff_within_at ğ•œ n f s x)
  (hx : f x â‰  0) :
  times_cont_diff_within_at ğ•œ n (Î» x, (f x)â»Â¹) s x :=
(times_cont_diff_at_inv ğ•œ hx).comp_times_cont_diff_within_at x hf

lemma times_cont_diff_on.inv {f : E â†’ ğ•œ'} {n} (hf : times_cont_diff_on ğ•œ n f s)
  (h : âˆ€ x âˆˆ s, f x â‰  0) :
  times_cont_diff_on ğ•œ n (Î» x, (f x)â»Â¹) s :=
Î» x hx, (hf.times_cont_diff_within_at hx).inv (h x hx)

lemma times_cont_diff_at.inv {f : E â†’ ğ•œ'} {n} (hf : times_cont_diff_at ğ•œ n f x) (hx : f x â‰  0) :
  times_cont_diff_at ğ•œ n (Î» x, (f x)â»Â¹) x :=
hf.inv hx

lemma times_cont_diff.inv {f : E â†’ ğ•œ'} {n} (hf : times_cont_diff ğ•œ n f) (h : âˆ€ x, f x â‰  0) :
  times_cont_diff ğ•œ n (Î» x, (f x)â»Â¹) :=
by { rw times_cont_diff_iff_times_cont_diff_at, exact Î» x, hf.times_cont_diff_at.inv (h x) }

-- TODO: generalize to `f g : E â†’ ğ•œ'`
lemma times_cont_diff_within_at.div [complete_space ğ•œ] {f g : E â†’ ğ•œ} {n}
  (hf : times_cont_diff_within_at ğ•œ n f s x) (hg : times_cont_diff_within_at ğ•œ n g s x)
  (hx : g x â‰  0) :
  times_cont_diff_within_at ğ•œ n (Î» x, f x / g x) s x :=
by simpa only [div_eq_mul_inv] using hf.mul (hg.inv hx)

lemma times_cont_diff_on.div [complete_space ğ•œ] {f g : E â†’ ğ•œ} {n}
  (hf : times_cont_diff_on ğ•œ n f s) (hg : times_cont_diff_on ğ•œ n g s) (hâ‚€ : âˆ€ x âˆˆ s, g x â‰  0) :
  times_cont_diff_on ğ•œ n (f / g) s :=
Î» x hx, (hf x hx).div (hg x hx) (hâ‚€ x hx)

lemma times_cont_diff_at.div [complete_space ğ•œ] {f g : E â†’ ğ•œ} {n}
  (hf : times_cont_diff_at ğ•œ n f x) (hg : times_cont_diff_at ğ•œ n g x)
  (hx : g x â‰  0) :
  times_cont_diff_at ğ•œ n (Î» x, f x / g x) x :=
hf.div hg hx

lemma times_cont_diff.div [complete_space ğ•œ] {f g : E â†’ ğ•œ} {n}
  (hf : times_cont_diff ğ•œ n f) (hg : times_cont_diff ğ•œ n g)
  (h0 : âˆ€ x, g x â‰  0) :
  times_cont_diff ğ•œ n (Î» x, f x / g x) :=
begin
  simp only [times_cont_diff_iff_times_cont_diff_at] at *,
  exact Î» x, (hf x).div (hg x) (h0 x)
end

end algebra_inverse

/-! ### Inversion of continuous linear maps between Banach spaces -/

section map_inverse
open continuous_linear_map

/-- At a continuous linear equivalence `e : E â‰ƒL[ğ•œ] F` between Banach spaces, the operation of
inversion is `C^n`, for all `n`. -/
lemma times_cont_diff_at_map_inverse [complete_space E] {n : with_top â„•} (e : E â‰ƒL[ğ•œ] F) :
  times_cont_diff_at ğ•œ n inverse (e : E â†’L[ğ•œ] F) :=
begin
  nontriviality E,
  -- first, we use the lemma `to_ring_inverse` to rewrite in terms of `ring.inverse` in the ring
  -- `E â†’L[ğ•œ] E`
  let Oâ‚ : (E â†’L[ğ•œ] E) â†’ (F â†’L[ğ•œ] E) := Î» f, f.comp (e.symm : (F â†’L[ğ•œ] E)),
  let Oâ‚‚ : (E â†’L[ğ•œ] F) â†’ (E â†’L[ğ•œ] E) := Î» f, (e.symm : (F â†’L[ğ•œ] E)).comp f,
  have : continuous_linear_map.inverse = Oâ‚ âˆ˜ ring.inverse âˆ˜ Oâ‚‚ :=
    funext (to_ring_inverse e),
  rw this,
  -- `Oâ‚` and `Oâ‚‚` are `times_cont_diff`,
  -- so we reduce to proving that `ring.inverse` is `times_cont_diff`
  have hâ‚ : times_cont_diff ğ•œ n Oâ‚,
    from is_bounded_bilinear_map_comp.times_cont_diff.comp
      (times_cont_diff_const.prod times_cont_diff_id),
  have hâ‚‚ : times_cont_diff ğ•œ n Oâ‚‚,
    from is_bounded_bilinear_map_comp.times_cont_diff.comp
      (times_cont_diff_id.prod times_cont_diff_const),
  refine hâ‚.times_cont_diff_at.comp _ (times_cont_diff_at.comp _ _ hâ‚‚.times_cont_diff_at),
  convert times_cont_diff_at_ring_inverse ğ•œ (1 : units (E â†’L[ğ•œ] E)),
  simp [Oâ‚‚, one_def]
end

end map_inverse

section function_inverse
open continuous_linear_map

/-- If `f` is a local homeomorphism and the point `a` is in its target,
and if `f` is `n` times continuously differentiable at `f.symm a`,
and if the derivative at `f.symm a` is a continuous linear equivalence,
then `f.symm` is `n` times continuously differentiable at the point `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem local_homeomorph.times_cont_diff_at_symm [complete_space E] {n : with_top â„•}
  (f : local_homeomorph E F) {fâ‚€' : E â‰ƒL[ğ•œ] F} {a : F} (ha : a âˆˆ f.target)
  (hfâ‚€' : has_fderiv_at f (fâ‚€' : E â†’L[ğ•œ] F) (f.symm a)) (hf : times_cont_diff_at ğ•œ n f (f.symm a)) :
  times_cont_diff_at ğ•œ n f.symm a :=
begin
  -- We prove this by induction on `n`
  induction n using with_top.nat_induction with n IH Itop,
  { rw times_cont_diff_at_zero,
    exact âŸ¨f.target, is_open.mem_nhds f.open_target ha, f.continuous_inv_funâŸ© },
  { obtain âŸ¨f', âŸ¨u, hu, hff'âŸ©, hf'âŸ© := times_cont_diff_at_succ_iff_has_fderiv_at.mp hf,
    apply times_cont_diff_at_succ_iff_has_fderiv_at.mpr,
    -- For showing `n.succ` times continuous differentiability (the main inductive step), it
    -- suffices to produce the derivative and show that it is `n` times continuously differentiable
    have eq_fâ‚€' : f' (f.symm a) = fâ‚€',
    { exact (hff' (f.symm a) (mem_of_mem_nhds hu)).unique hfâ‚€' },
    -- This follows by a bootstrapping formula expressing the derivative as a function of `f` itself
    refine âŸ¨inverse âˆ˜ f' âˆ˜ f.symm, _, _âŸ©,
    { -- We first check that the derivative of `f` is that formula
      have h_nhds : {y : E | âˆƒ (e : E â‰ƒL[ğ•œ] F), â†‘e = f' y} âˆˆ ğ“ ((f.symm) a),
      { have hfâ‚€' := fâ‚€'.nhds,
        rw â† eq_fâ‚€' at hfâ‚€',
        exact hf'.continuous_at.preimage_mem_nhds hfâ‚€' },
      obtain âŸ¨t, htu, ht, htfâŸ© := mem_nhds_iff.mp (filter.inter_mem hu h_nhds),
      use f.target âˆ© (f.symm) â»Â¹' t,
      refine âŸ¨is_open.mem_nhds _ _, _âŸ©,
      { exact f.preimage_open_of_open_symm ht },
      { exact mem_inter ha (mem_preimage.mpr htf) },
      intros x hx,
      obtain âŸ¨hxu, e, heâŸ© := htu hx.2,
      have h_deriv : has_fderiv_at f â†‘e ((f.symm) x),
      { rw he,
        exact hff' (f.symm x) hxu },
      convert f.has_fderiv_at_symm hx.1 h_deriv,
      simp [â† he] },
    { -- Then we check that the formula, being a composition of `times_cont_diff` pieces, is
      -- itself `times_cont_diff`
      have h_derivâ‚ : times_cont_diff_at ğ•œ n inverse (f' (f.symm a)),
      { rw eq_fâ‚€',
        exact times_cont_diff_at_map_inverse _ },
      have h_derivâ‚‚ : times_cont_diff_at ğ•œ n f.symm a,
      { refine IH (hf.of_le _),
        norm_cast,
        exact nat.le_succ n },
      exact (h_derivâ‚.comp _ hf').comp _ h_derivâ‚‚ } },
  { refine times_cont_diff_at_top.mpr _,
    intros n,
    exact Itop n (times_cont_diff_at_top.mp hf n) }
end

/-- Let `f` be a local homeomorphism of a nondiscrete normed field, let `a` be a point in its
target. if `f` is `n` times continuously differentiable at `f.symm a`, and if the derivative at
`f.symm a` is nonzero, then `f.symm` is `n` times continuously differentiable at the point `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem local_homeomorph.times_cont_diff_at_symm_deriv [complete_space ğ•œ] {n : with_top â„•}
  (f : local_homeomorph ğ•œ ğ•œ) {fâ‚€' a : ğ•œ} (hâ‚€ : fâ‚€' â‰  0) (ha : a âˆˆ f.target)
  (hfâ‚€' : has_deriv_at f fâ‚€' (f.symm a)) (hf : times_cont_diff_at ğ•œ n f (f.symm a)) :
  times_cont_diff_at ğ•œ n f.symm a :=
f.times_cont_diff_at_symm ha (hfâ‚€'.has_fderiv_at_equiv hâ‚€) hf

end function_inverse

section real
/-!
### Results over `â„` or `â„‚`
  The results in this section rely on the Mean Value Theorem, and therefore hold only over `â„` (and
  its extension fields such as `â„‚`).
-/

variables
{ğ•‚ : Type*} [is_R_or_C ğ•‚]
{E' : Type*} [normed_group E'] [normed_space ğ•‚ E']
{F' : Type*} [normed_group F'] [normed_space ğ•‚ F']

/-- If a function has a Taylor series at order at least 1, then at points in the interior of the
    domain of definition, the term of order 1 of this series is a strict derivative of `f`. -/
lemma has_ftaylor_series_up_to_on.has_strict_fderiv_at
  {s : set E'} {f : E' â†’ F'} {x : E'} {p : E' â†’ formal_multilinear_series ğ•‚ E' F'} {n : with_top â„•}
  (hf : has_ftaylor_series_up_to_on n f p s) (hn : 1 â‰¤ n) (hs : s âˆˆ ğ“ x) :
  has_strict_fderiv_at f ((continuous_multilinear_curry_fin1 ğ•‚ E' F') (p x 1)) x :=
has_strict_fderiv_at_of_has_fderiv_at_of_continuous_at (hf.eventually_has_fderiv_at hn hs) $
  (continuous_multilinear_curry_fin1 ğ•‚ E' F').continuous_at.comp $
    (hf.cont 1 hn).continuous_at hs

/-- If a function is `C^n` with `1 â‰¤ n` around a point, and its derivative at that point is given to
us as `f'`, then `f'` is also a strict derivative. -/
lemma times_cont_diff_at.has_strict_fderiv_at'
  {f : E' â†’ F'} {f' : E' â†’L[ğ•‚] F'} {x : E'}
  {n : with_top â„•} (hf : times_cont_diff_at ğ•‚ n f x) (hf' : has_fderiv_at f f' x) (hn : 1 â‰¤ n) :
  has_strict_fderiv_at f f' x :=
begin
  rcases hf 1 hn with âŸ¨u, H, p, hpâŸ©,
  simp only [nhds_within_univ, mem_univ, insert_eq_of_mem] at H,
  have := hp.has_strict_fderiv_at le_rfl H,
  rwa hf'.unique this.has_fderiv_at
end

/-- If a function is `C^n` with `1 â‰¤ n` around a point, and its derivative at that point is given to
us as `f'`, then `f'` is also a strict derivative. -/
lemma times_cont_diff_at.has_strict_deriv_at' {f : ğ•‚ â†’ F'} {f' : F'} {x : ğ•‚}
  {n : with_top â„•} (hf : times_cont_diff_at ğ•‚ n f x) (hf' : has_deriv_at f f' x) (hn : 1 â‰¤ n) :
  has_strict_deriv_at f f' x :=
hf.has_strict_fderiv_at' hf' hn

/-- If a function is `C^n` with `1 â‰¤ n` around a point, then the derivative of `f` at this point
is also a strict derivative. -/
lemma times_cont_diff_at.has_strict_fderiv_at {f : E' â†’ F'} {x : E'} {n : with_top â„•}
  (hf : times_cont_diff_at ğ•‚ n f x) (hn : 1 â‰¤ n) :
  has_strict_fderiv_at f (fderiv ğ•‚ f x) x :=
hf.has_strict_fderiv_at' (hf.differentiable_at hn).has_fderiv_at hn

/-- If a function is `C^n` with `1 â‰¤ n` around a point, then the derivative of `f` at this point
is also a strict derivative. -/
lemma times_cont_diff_at.has_strict_deriv_at {f : ğ•‚ â†’ F'} {x : ğ•‚} {n : with_top â„•}
  (hf : times_cont_diff_at ğ•‚ n f x) (hn : 1 â‰¤ n) :
  has_strict_deriv_at f (deriv f x) x :=
(hf.has_strict_fderiv_at hn).has_strict_deriv_at

/-- If a function is `C^n` with `1 â‰¤ n`, then the derivative of `f` is also a strict derivative. -/
lemma times_cont_diff.has_strict_fderiv_at
  {f : E' â†’ F'} {x : E'} {n : with_top â„•} (hf : times_cont_diff ğ•‚ n f) (hn : 1 â‰¤ n) :
  has_strict_fderiv_at f (fderiv ğ•‚ f x) x :=
hf.times_cont_diff_at.has_strict_fderiv_at hn

/-- If a function is `C^n` with `1 â‰¤ n`, then the derivative of `f` is also a strict derivative. -/
lemma times_cont_diff.has_strict_deriv_at
  {f : ğ•‚ â†’ F'} {x : ğ•‚} {n : with_top â„•} (hf : times_cont_diff ğ•‚ n f) (hn : 1 â‰¤ n) :
  has_strict_deriv_at f (deriv f x) x :=
hf.times_cont_diff_at.has_strict_deriv_at hn

/-- If `f` has a formal Taylor series `p` up to order `1` on `{x} âˆª s`, where `s` is a convex set,
and `âˆ¥p x 1âˆ¥â‚Š < K`, then `f` is `K`-Lipschitz in a neighborhood of `x` within `s`. -/
lemma has_ftaylor_series_up_to_on.exists_lipschitz_on_with_of_nnnorm_lt {E F : Type*}
  [normed_group E] [normed_space â„ E] [normed_group F] [normed_space â„ F] {f : E â†’ F}
  {p : E â†’ formal_multilinear_series â„ E F} {s : set E} {x : E}
  (hf : has_ftaylor_series_up_to_on 1 f p (insert x s)) (hs : convex s) (K : â„â‰¥0)
  (hK : âˆ¥p x 1âˆ¥â‚Š < K) :
  âˆƒ t âˆˆ ğ“[s] x, lipschitz_on_with K f t :=
begin
  set f' := Î» y, continuous_multilinear_curry_fin1 â„ E F (p y 1),
  have hder : âˆ€ y âˆˆ s, has_fderiv_within_at f (f' y) s y,
    from Î» y hy, (hf.has_fderiv_within_at le_rfl (subset_insert x s hy)).mono (subset_insert x s),
  have hcont : continuous_within_at f' s x,
    from (continuous_multilinear_curry_fin1 â„ E F).continuous_at.comp_continuous_within_at
      ((hf.cont _ le_rfl _ (mem_insert _ _)).mono (subset_insert x s)),
  replace hK : âˆ¥f' xâˆ¥â‚Š < K, by simpa only [linear_isometry_equiv.nnnorm_map],
  exact hs.exists_nhds_within_lipschitz_on_with_of_has_fderiv_within_at_of_nnnorm_lt
    (eventually_nhds_within_iff.2 $ eventually_of_forall hder) hcont K hK
end

/-- If `f` has a formal Taylor series `p` up to order `1` on `{x} âˆª s`, where `s` is a convex set,
then `f` is Lipschitz in a neighborhood of `x` within `s`. -/
lemma has_ftaylor_series_up_to_on.exists_lipschitz_on_with {E F : Type*}
  [normed_group E] [normed_space â„ E] [normed_group F] [normed_space â„ F] {f : E â†’ F}
  {p : E â†’ formal_multilinear_series â„ E F} {s : set E} {x : E}
  (hf : has_ftaylor_series_up_to_on 1 f p (insert x s)) (hs : convex s) :
  âˆƒ K (t âˆˆ ğ“[s] x), lipschitz_on_with K f t :=
(no_top _).imp $ hf.exists_lipschitz_on_with_of_nnnorm_lt hs

/-- If `f` is `C^1` within a conves set `s` at `x`, then it is Lipschitz on a neighborhood of `x`
within `s`. -/
lemma times_cont_diff_within_at.exists_lipschitz_on_with {E F : Type*} [normed_group E]
  [normed_space â„ E] [normed_group F] [normed_space â„ F] {f : E â†’ F} {s : set E}
  {x : E} (hf : times_cont_diff_within_at â„ 1 f s x) (hs : convex s) :
  âˆƒ (K : â„â‰¥0) (t âˆˆ ğ“[s] x), lipschitz_on_with K f t :=
begin
  rcases hf 1 le_rfl with âŸ¨t, hst, p, hpâŸ©,
  rcases metric.mem_nhds_within_iff.mp hst with âŸ¨Îµ, Îµ0, hÎµâŸ©,
  replace hp : has_ftaylor_series_up_to_on 1 f p (metric.ball x Îµ âˆ© insert x s) := hp.mono hÎµ,
  clear hst hÎµ t,
  rw [â† insert_eq_of_mem (metric.mem_ball_self Îµ0), â† insert_inter] at hp,
  rcases hp.exists_lipschitz_on_with ((convex_ball _ _).inter hs) with âŸ¨K, t, hst, hftâŸ©,
  rw [inter_comm, â† nhds_within_restrict' _ (metric.ball_mem_nhds _ Îµ0)] at hst,
  exact âŸ¨K, t, hst, hftâŸ©
end

/-- If `f` is `C^1` at `x` and `K > âˆ¥fderiv ğ•‚ f xâˆ¥`, then `f` is `K`-Lipschitz in a neighborhood of
`x`. -/
lemma times_cont_diff_at.exists_lipschitz_on_with_of_nnnorm_lt {f : E' â†’ F'} {x : E'}
  (hf : times_cont_diff_at ğ•‚ 1 f x) (K : â„â‰¥0) (hK : âˆ¥fderiv ğ•‚ f xâˆ¥â‚Š < K) :
  âˆƒ t âˆˆ ğ“ x, lipschitz_on_with K f t :=
(hf.has_strict_fderiv_at le_rfl).exists_lipschitz_on_with_of_nnnorm_lt K hK

/-- If `f` is `C^1` at `x`, then `f` is Lipschitz in a neighborhood of `x`. -/
lemma times_cont_diff_at.exists_lipschitz_on_with {f : E' â†’ F'} {x : E'}
  (hf : times_cont_diff_at ğ•‚ 1 f x) :
  âˆƒ K (t âˆˆ ğ“ x), lipschitz_on_with K f t :=
(hf.has_strict_fderiv_at le_rfl).exists_lipschitz_on_with

end real

section deriv
/-!
### One dimension

All results up to now have been expressed in terms of the general FrÃ©chet derivative `fderiv`. For
maps defined on the field, the one-dimensional derivative `deriv` is often easier to use. In this
paragraph, we reformulate some higher smoothness results in terms of `deriv`.
-/

variables {fâ‚‚ : ğ•œ â†’ F} {sâ‚‚ : set ğ•œ}
open continuous_linear_map (smul_right)

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative (formulated with `deriv_within`) is `C^n`. -/
theorem times_cont_diff_on_succ_iff_deriv_within {n : â„•} (hs : unique_diff_on ğ•œ sâ‚‚) :
  times_cont_diff_on ğ•œ ((n + 1) : â„•) fâ‚‚ sâ‚‚ â†”
  differentiable_on ğ•œ fâ‚‚ sâ‚‚ âˆ§ times_cont_diff_on ğ•œ n (deriv_within fâ‚‚ sâ‚‚) sâ‚‚ :=
begin
  rw times_cont_diff_on_succ_iff_fderiv_within hs,
  congr' 2,
  apply le_antisymm,
  { assume h,
    have : deriv_within fâ‚‚ sâ‚‚ = (Î» u : ğ•œ â†’L[ğ•œ] F, u 1) âˆ˜ (fderiv_within ğ•œ fâ‚‚ sâ‚‚),
      by { ext x, refl },
    simp only [this],
    apply times_cont_diff.comp_times_cont_diff_on _ h,
    exact (is_bounded_bilinear_map_apply.is_bounded_linear_map_left _).times_cont_diff },
  { assume h,
    have : fderiv_within ğ•œ fâ‚‚ sâ‚‚ = smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) âˆ˜ deriv_within fâ‚‚ sâ‚‚,
      by { ext x, simp [deriv_within] },
    simp only [this],
    apply times_cont_diff.comp_times_cont_diff_on _ h,
    exact (is_bounded_bilinear_map_smul_right.is_bounded_linear_map_right _).times_cont_diff }
end

/-- A function is `C^(n + 1)` on an open domain if and only if it is
differentiable there, and its derivative (formulated with `deriv`) is `C^n`. -/
theorem times_cont_diff_on_succ_iff_deriv_of_open {n : â„•} (hs : is_open sâ‚‚) :
  times_cont_diff_on ğ•œ ((n + 1) : â„•) fâ‚‚ sâ‚‚ â†”
  differentiable_on ğ•œ fâ‚‚ sâ‚‚ âˆ§ times_cont_diff_on ğ•œ n (deriv fâ‚‚) sâ‚‚ :=
begin
  rw times_cont_diff_on_succ_iff_deriv_within hs.unique_diff_on,
  congr' 2,
  rw â† iff_iff_eq,
  apply times_cont_diff_on_congr,
  assume x hx,
  exact deriv_within_of_open hs hx
end

/-- A function is `C^âˆ` on a domain with unique derivatives if and only if it is differentiable
there, and its derivative (formulated with `deriv_within`) is `C^âˆ`. -/
theorem times_cont_diff_on_top_iff_deriv_within (hs : unique_diff_on ğ•œ sâ‚‚) :
  times_cont_diff_on ğ•œ âˆ fâ‚‚ sâ‚‚ â†”
  differentiable_on ğ•œ fâ‚‚ sâ‚‚ âˆ§ times_cont_diff_on ğ•œ âˆ (deriv_within fâ‚‚ sâ‚‚) sâ‚‚ :=
begin
  split,
  { assume h,
    refine âŸ¨h.differentiable_on le_top, _âŸ©,
    apply times_cont_diff_on_top.2 (Î» n, ((times_cont_diff_on_succ_iff_deriv_within hs).1 _).2),
    exact h.of_le le_top },
  { assume h,
    refine times_cont_diff_on_top.2 (Î» n, _),
    have A : (n : with_top â„•) â‰¤ âˆ := le_top,
    apply ((times_cont_diff_on_succ_iff_deriv_within hs).2 âŸ¨h.1, h.2.of_le AâŸ©).of_le,
    exact with_top.coe_le_coe.2 (nat.le_succ n) }
end

/-- A function is `C^âˆ` on an open domain if and only if it is differentiable
there, and its derivative (formulated with `deriv`) is `C^âˆ`. -/
theorem times_cont_diff_on_top_iff_deriv_of_open (hs : is_open sâ‚‚) :
  times_cont_diff_on ğ•œ âˆ fâ‚‚ sâ‚‚ â†”
  differentiable_on ğ•œ fâ‚‚ sâ‚‚ âˆ§ times_cont_diff_on ğ•œ âˆ (deriv fâ‚‚) sâ‚‚ :=
begin
  rw times_cont_diff_on_top_iff_deriv_within hs.unique_diff_on,
  congr' 2,
  rw â† iff_iff_eq,
  apply times_cont_diff_on_congr,
  assume x hx,
  exact deriv_within_of_open hs hx
end

lemma times_cont_diff_on.deriv_within {m n : with_top â„•}
  (hf : times_cont_diff_on ğ•œ n fâ‚‚ sâ‚‚) (hs : unique_diff_on ğ•œ sâ‚‚) (hmn : m + 1 â‰¤ n) :
  times_cont_diff_on ğ•œ m (deriv_within fâ‚‚ sâ‚‚) sâ‚‚ :=
begin
  cases m,
  { change âˆ + 1 â‰¤ n at hmn,
    have : n = âˆ, by simpa using hmn,
    rw this at hf,
    exact ((times_cont_diff_on_top_iff_deriv_within hs).1 hf).2 },
  { change (m.succ : with_top â„•) â‰¤ n at hmn,
    exact ((times_cont_diff_on_succ_iff_deriv_within hs).1 (hf.of_le hmn)).2 }
end

lemma times_cont_diff_on.deriv_of_open {m n : with_top â„•}
  (hf : times_cont_diff_on ğ•œ n fâ‚‚ sâ‚‚) (hs : is_open sâ‚‚) (hmn : m + 1 â‰¤ n) :
  times_cont_diff_on ğ•œ m (deriv fâ‚‚) sâ‚‚ :=
(hf.deriv_within hs.unique_diff_on hmn).congr (Î» x hx, (deriv_within_of_open hs hx).symm)

lemma times_cont_diff_on.continuous_on_deriv_within {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n fâ‚‚ sâ‚‚) (hs : unique_diff_on ğ•œ sâ‚‚) (hn : 1 â‰¤ n) :
  continuous_on (deriv_within fâ‚‚ sâ‚‚) sâ‚‚ :=
((times_cont_diff_on_succ_iff_deriv_within hs).1 (h.of_le hn)).2.continuous_on

lemma times_cont_diff_on.continuous_on_deriv_of_open {n : with_top â„•}
  (h : times_cont_diff_on ğ•œ n fâ‚‚ sâ‚‚) (hs : is_open sâ‚‚) (hn : 1 â‰¤ n) :
  continuous_on (deriv fâ‚‚) sâ‚‚ :=
((times_cont_diff_on_succ_iff_deriv_of_open hs).1 (h.of_le hn)).2.continuous_on

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative is `C^n`. -/
theorem times_cont_diff_succ_iff_deriv {n : â„•} :
  times_cont_diff ğ•œ ((n + 1) : â„•) fâ‚‚ â†”
    differentiable ğ•œ fâ‚‚ âˆ§ times_cont_diff ğ•œ n (deriv fâ‚‚) :=
by simp only [â† times_cont_diff_on_univ, times_cont_diff_on_succ_iff_deriv_of_open, is_open_univ,
  differentiable_on_univ]

end deriv

section restrict_scalars
/-!
### Restricting from `â„‚` to `â„`, or generally from `ğ•œ'` to `ğ•œ`

If a function is `n` times continuously differentiable over `â„‚`, then it is `n` times continuously
differentiable over `â„`. In this paragraph, we give variants of this statement, in the general
situation where `â„‚` and `â„` are replaced respectively by `ğ•œ'` and `ğ•œ` where `ğ•œ'` is a normed algebra
over `ğ•œ`.
-/

variables (ğ•œ) {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
variables [normed_space ğ•œ' E] [is_scalar_tower ğ•œ ğ•œ' E]
variables [normed_space ğ•œ' F] [is_scalar_tower ğ•œ ğ•œ' F]
variables {p' : E â†’ formal_multilinear_series ğ•œ' E F} {n : with_top â„•}

lemma has_ftaylor_series_up_to_on.restrict_scalars
  (h : has_ftaylor_series_up_to_on n f p' s) :
  has_ftaylor_series_up_to_on n f (Î» x, (p' x).restrict_scalars ğ•œ) s :=
{ zero_eq := Î» x hx, h.zero_eq x hx,
  fderiv_within :=
    begin
      intros m hm x hx,
      convert ((continuous_multilinear_map.restrict_scalars_linear ğ•œ).has_fderiv_at)
        .comp_has_fderiv_within_at _ ((h.fderiv_within m hm x hx).restrict_scalars ğ•œ),
    end,
  cont := Î» m hm, continuous_multilinear_map.continuous_restrict_scalars.comp_continuous_on
    (h.cont m hm) }

lemma times_cont_diff_within_at.restrict_scalars (h : times_cont_diff_within_at ğ•œ' n f s x) :
  times_cont_diff_within_at ğ•œ n f s x :=
begin
  intros m hm,
  rcases h m hm with âŸ¨u, u_mem, p', hp'âŸ©,
  exact âŸ¨u, u_mem, _, hp'.restrict_scalars _âŸ©
end

lemma times_cont_diff_on.restrict_scalars (h : times_cont_diff_on ğ•œ' n f s) :
  times_cont_diff_on ğ•œ n f s :=
Î» x hx, (h x hx).restrict_scalars _

lemma times_cont_diff_at.restrict_scalars (h : times_cont_diff_at ğ•œ' n f x) :
  times_cont_diff_at ğ•œ n f x :=
times_cont_diff_within_at_univ.1 $ h.times_cont_diff_within_at.restrict_scalars _

lemma times_cont_diff.restrict_scalars (h : times_cont_diff ğ•œ' n f) :
  times_cont_diff ğ•œ n f :=
times_cont_diff_iff_times_cont_diff_at.2 $ Î» x, h.times_cont_diff_at.restrict_scalars _

end restrict_scalars
