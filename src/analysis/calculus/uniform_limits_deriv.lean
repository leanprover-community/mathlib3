/-
Copyright (c) 2022 Kevin H. Wilson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin H. Wilson
-/
import analysis.calculus.mean_value
import analysis.normed_space.is_R_or_C
import order.filter.curry

/-!
# Swapping limits and derivatives via uniform convergence

The purpose of this file is to prove that the derivative of the pointwise limit of a sequence of
functions is the pointwise limit of the functions' derivatives when the derivatives converge
_uniformly_. The formal statement appears as `has_fderiv_at_of_tendsto_locally_uniformly_at`.

## Main statements

* `uniform_cauchy_seq_on_filter_of_fderiv`: If
    1. `f : â„• â†’ E â†’ G` is a sequence of functions which have derivatives
       `f' : â„• â†’ E â†’ (E â†’L[ğ•œ] G)` on a neighborhood of `x`,
    2. the functions `f` converge at `x`, and
    3. the derivatives `f'` form a Cauchy sequence uniformly on a neighborhood of `x`,
  then the `f` form a Cauchy sequence _uniformly_ on a neighborhood of `x`
* `has_fderiv_at_of_tendsto_uniformly_on_filter` : Suppose (1), (2), and (3) above are true. Let
  `g` (resp. `g'`) be the limiting function of the `f` (resp. `g'`). Then `f'` is the derivative of
  `g` on a neighborhood of `x`
* `has_fderiv_at_of_tendsto_uniformly_on`: An often-easier-to-use version of the above theorem when
  *all* the derivatives exist and functions converge on a common open set and the derivatives
  converge uniformly there.

Each of the above statements also has variations that support `deriv` instead of `fderiv`.

## Implementation notes

Our technique for proving the main result is the famous "`Îµ / 3` proof." In words, you can find it
explained, for instance, at [this StackExchange post](https://math.stackexchange.com/questions/214218/uniform-convergence-of-derivatives-tao-14-2-7).
The subtlety is that we want to prove that the difference quotients of the `g` converge to the `g'`.
That is, we want to prove something like:

```
âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y âˆˆ B_Î´(x), |y - x|â»Â¹ * |(g y - g x) - g' x (y - x)| < Îµ.
```

To do so, we will need to introduce a pair of quantifers

```lean
âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, âˆƒ Î´ > 0, âˆ€ y âˆˆ B_Î´(x), |y - x|â»Â¹ * |(g y - g x) - g' x (y - x)| < Îµ.
```

So how do we write this in terms of filters? Well, the initial definition of the derivative is

```lean
tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (ğ“ x) (ğ“ 0)
```

There are two ways we might introduce `n`. We could do:

```lean
âˆ€á¶  (n : â„•) in at_top, tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (ğ“ x) (ğ“ 0)
```

but this is equivalent to the quantifier order `âˆƒ N, âˆ€ n â‰¥ N, âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y âˆˆ B_Î´(x)`,
which _implies_ our desired `âˆ€ âˆƒ âˆ€ âˆƒ âˆ€` but is _not_ equivalent to it. On the other hand, we might
try

```lean
tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (at_top Ã—á¶  ğ“ x) (ğ“ 0)
```

but this is equivalent to the quantifer order `âˆ€ Îµ > 0, âˆƒ N, âˆƒ Î´ > 0, âˆ€ n â‰¥ N, âˆ€ y âˆˆ B_Î´(x)`, which
again _implies_ our desired `âˆ€ âˆƒ âˆ€ âˆƒ âˆ€` but is not equivalent to it.

So to get the quantifier order we want, we need to introduce a new filter construction, which we
call a "curried filter"

```lean
tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (at_top.curry (ğ“ x)) (ğ“ 0)
```

Then the above implications are `filter.tendsto.curry` and
`filter.tendsto.mono_left filter.curry_le_prod`. We will use both of these deductions as part of
our proof.

We note that if you loosen the assumptions of the main theorem then the proof becomes quite a bit
easier. In particular, if you assume there is a common neighborhood `s` where all of the three
assumptions of `has_fderiv_at_of_tendsto_uniformly_on_filter` hold and that the `f'` are
continuous, then you can avoid the mean value theorem and much of the work around curried filters.

## Tags

uniform convergence, limits of derivatives
-/

open filter
open_locale uniformity filter topology

section limits_of_derivatives

variables {Î¹ : Type*} {l : filter Î¹}
  {E : Type*} [normed_add_comm_group E]
  {ğ•œ : Type*} [is_R_or_C ğ•œ] [normed_space ğ•œ E]
  {G : Type*} [normed_add_comm_group G] [normed_space ğ•œ G]
  {f : Î¹ â†’ E â†’ G} {g : E â†’ G} {f' : Î¹ â†’ (E â†’ (E â†’L[ğ•œ] G))} {g' : E â†’ (E â†’L[ğ•œ] G)}
  {x : E}

/-- If a sequence of functions real or complex functions are eventually differentiable on a
neighborhood of `x`, they are Cauchy _at_ `x`, and their derivatives
are a uniform Cauchy sequence in a neighborhood of `x`, then the functions form a uniform Cauchy
sequence in a neighborhood of `x`. -/
lemma uniform_cauchy_seq_on_filter_of_fderiv
  (hf' : uniform_cauchy_seq_on_filter f' l (ğ“ x))
  (hf : âˆ€á¶  (n : Î¹ Ã— E) in (l Ã—á¶  ğ“ x), has_fderiv_at (f n.1) (f' n.1 n.2) n.2)
  (hfg : cauchy (map (Î» n, f n x) l)) :
  uniform_cauchy_seq_on_filter f l (ğ“ x) :=
begin
  letI : normed_space â„ E, from normed_space.restrict_scalars â„ ğ•œ _,
  rw seminormed_add_group.uniform_cauchy_seq_on_filter_iff_tendsto_uniformly_on_filter_zero at
    hf' âŠ¢,

  suffices : tendsto_uniformly_on_filter
    (Î» (n : Î¹ Ã— Î¹) (z : E), f n.1 z - f n.2 z - (f n.1 x - f n.2 x)) 0 (l Ã—á¶  l) (ğ“ x) âˆ§
    tendsto_uniformly_on_filter (Î» (n : Î¹ Ã— Î¹) (z : E), f n.1 x - f n.2 x) 0 (l Ã—á¶  l) (ğ“ x),
  { have := this.1.add this.2,
    rw add_zero at this,
    exact this.congr (by simp), },
  split,
  { -- This inequality follows from the mean value theorem. To apply it, we will need to shrink our
    -- neighborhood to small enough ball
    rw metric.tendsto_uniformly_on_filter_iff at hf' âŠ¢,
    intros Îµ hÎµ,
    have := (tendsto_swap4_prod.eventually (hf.prod_mk hf)).diag_of_prod_right,
    obtain âŸ¨a, b, c, d, eâŸ© := eventually_prod_iff.1 ((hf' Îµ hÎµ).and this),
    obtain âŸ¨R, hR, hR'âŸ© := metric.nhds_basis_ball.eventually_iff.mp d,
    let r := min 1 R,
    have hr : 0 < r, { simp [hR], },
    have hr' : âˆ€ â¦ƒy : Eâ¦„, y âˆˆ metric.ball x r â†’ c y,
    { exact (Î» y hy, hR' (lt_of_lt_of_le (metric.mem_ball.mp hy) (min_le_right _ _))), },
    have hxy : âˆ€ (y : E), y âˆˆ metric.ball x r â†’ â€–y - xâ€– < 1,
    { intros y hy,
      rw [metric.mem_ball, dist_eq_norm] at hy,
      exact lt_of_lt_of_le hy (min_le_left _ _), },
    have hxyÎµ : âˆ€ (y : E), y âˆˆ metric.ball x r â†’ Îµ * â€–y - xâ€– < Îµ,
    { intros y hy,
      exact (mul_lt_iff_lt_one_right hÎµ.lt).mpr (hxy y hy), },

    -- With a small ball in hand, apply the mean value theorem
    refine eventually_prod_iff.mpr âŸ¨_, b, (Î» e : E, metric.ball x r e),
      eventually_mem_set.mpr (metric.nhds_basis_ball.mem_of_mem hr), (Î» n hn y hy, _)âŸ©,
    simp only [pi.zero_apply, dist_zero_left] at e âŠ¢,
    refine lt_of_le_of_lt _ (hxyÎµ y hy),
    exact convex.norm_image_sub_le_of_norm_has_fderiv_within_le
      (Î» y hy, ((e hn (hr' hy)).2.1.sub (e hn (hr' hy)).2.2).has_fderiv_within_at)
      (Î» y hy, (e hn (hr' hy)).1.le)
      (convex_ball x r) (metric.mem_ball_self hr) hy, },
  { -- This is just `hfg` run through `eventually_prod_iff`
    refine metric.tendsto_uniformly_on_filter_iff.mpr (Î» Îµ hÎµ, _),
    obtain âŸ¨t, ht, ht'âŸ© := (metric.cauchy_iff.mp hfg).2 Îµ hÎµ,
    exact eventually_prod_iff.mpr
    âŸ¨ (Î» (n : Î¹ Ã— Î¹), (f n.1 x âˆˆ t) âˆ§ (f n.2 x âˆˆ t)),
      eventually_prod_iff.mpr âŸ¨_, ht, _, ht, (Î» n hn n' hn', âŸ¨hn, hn'âŸ©)âŸ©,
      (Î» y, true),
      (by simp),
      (Î» n hn y hy, by simpa [norm_sub_rev, dist_eq_norm] using ht' _ hn.1 _ hn.2)âŸ©, },
end

/-- A variant of the second fundamental theorem of calculus (FTC-2): If a sequence of functions
between real or complex normed spaces are differentiable on a ball centered at `x`, they
form a Cauchy sequence _at_ `x`, and their derivatives are Cauchy uniformly on the ball, then the
functions form a uniform Cauchy sequence on the ball.

NOTE: The fact that we work on a ball is typically all that is necessary to work with power series
and Dirichlet series (our primary use case). However, this can be generalized by replacing the ball
with any connected, bounded, open set and replacing uniform convergence with local uniform
convergence. See `cauchy_map_of_uniform_cauchy_seq_on_fderiv`.
-/
lemma uniform_cauchy_seq_on_ball_of_fderiv
  {r : â„} (hf' : uniform_cauchy_seq_on f' l (metric.ball x r))
  (hf : âˆ€ n : Î¹, âˆ€ y : E, y âˆˆ metric.ball x r â†’ has_fderiv_at (f n) (f' n y) y)
  (hfg : cauchy (map (Î» n, f n x) l)) :
  uniform_cauchy_seq_on f l (metric.ball x r) :=
begin
  letI : normed_space â„ E, from normed_space.restrict_scalars â„ ğ•œ _,
  haveI : ne_bot l, from (cauchy_map_iff.1 hfg).1,
  rcases le_or_lt r 0 with hr|hr,
  { simp only [metric.ball_eq_empty.2 hr, uniform_cauchy_seq_on, set.mem_empty_iff_false,
      is_empty.forall_iff, eventually_const, implies_true_iff] },
  rw seminormed_add_group.uniform_cauchy_seq_on_iff_tendsto_uniformly_on_zero at hf' âŠ¢,
  suffices : tendsto_uniformly_on
    (Î» (n : Î¹ Ã— Î¹) (z : E), f n.1 z - f n.2 z - (f n.1 x - f n.2 x)) 0
      (l Ã—á¶  l) (metric.ball x r) âˆ§
    tendsto_uniformly_on (Î» (n : Î¹ Ã— Î¹) (z : E), f n.1 x - f n.2 x) 0
      (l Ã—á¶  l) (metric.ball x r),
  { have := this.1.add this.2,
    rw add_zero at this,
    refine this.congr _,
    apply eventually_of_forall,
    intros n z hz,
    simp, },
  split,
  { -- This inequality follows from the mean value theorem
    rw metric.tendsto_uniformly_on_iff at hf' âŠ¢,
    intros Îµ hÎµ,
    obtain âŸ¨q, hqpos, hqâŸ© : âˆƒ q : â„, 0 < q âˆ§ q * r < Îµ,
    { simp_rw mul_comm,
      exact exists_pos_mul_lt hÎµ.lt r, },
    apply (hf' q hqpos.gt).mono,
    intros n hn y hy,
    simp_rw [dist_eq_norm, pi.zero_apply, zero_sub, norm_neg] at hn âŠ¢,
    have mvt := convex.norm_image_sub_le_of_norm_has_fderiv_within_le
      (Î» z hz, ((hf n.1 z hz).sub (hf n.2 z hz)).has_fderiv_within_at)
      (Î» z hz, (hn z hz).le) (convex_ball x r) (metric.mem_ball_self hr) hy,
    refine lt_of_le_of_lt mvt _,
    have : q * â€–y - xâ€– < q * r,
    { exact mul_lt_mul' rfl.le (by simpa only [dist_eq_norm] using metric.mem_ball.mp hy)
        (norm_nonneg _) hqpos, },
    exact this.trans hq, },
  { -- This is just `hfg` run through `eventually_prod_iff`
    refine metric.tendsto_uniformly_on_iff.mpr (Î» Îµ hÎµ, _),
    obtain âŸ¨t, ht, ht'âŸ© := (metric.cauchy_iff.mp hfg).2 Îµ hÎµ,
    rw eventually_prod_iff,
    refine âŸ¨(Î» n, f n x âˆˆ t), ht, (Î» n, f n x âˆˆ t), ht, _âŸ©,
    intros n hn n' hn' z hz,
    rw [dist_eq_norm, pi.zero_apply, zero_sub, norm_neg, â†dist_eq_norm],
    exact (ht' _ hn _ hn'), },
end

/-- If a sequence of functions between real or complex normed spaces are differentiable on a
preconnected open set, they form a Cauchy sequence _at_ `x`, and their derivatives are Cauchy
uniformly on the set, then the functions form a Cauchy sequence at any point in the set. -/
lemma cauchy_map_of_uniform_cauchy_seq_on_fderiv
  {s : set E} (hs : is_open s) (h's : is_preconnected s)
  (hf' : uniform_cauchy_seq_on f' l s)
  (hf : âˆ€ n : Î¹, âˆ€ y : E, y âˆˆ s â†’ has_fderiv_at (f n) (f' n y) y)
  {xâ‚€ x : E} (hxâ‚€ : xâ‚€ âˆˆ s) (hx : x âˆˆ s)
  (hfg : cauchy (map (Î» n, f n xâ‚€) l)) :
  cauchy (map (Î» n, f n x) l) :=
begin
  haveI : ne_bot l, from (cauchy_map_iff.1 hfg).1,
  let t := {y | y âˆˆ s âˆ§ cauchy (map (Î» n, f n y) l)},
  suffices H : s âŠ† t, from (H hx).2,
  have A : âˆ€ x Îµ, x âˆˆ t â†’ metric.ball x Îµ âŠ† s â†’ metric.ball x Îµ âŠ† t,
  from Î» x Îµ xt hx y hy, âŸ¨hx hy, (uniform_cauchy_seq_on_ball_of_fderiv (hf'.mono hx)
    (Î» n y hy, hf n y (hx hy)) xt.2).cauchy_map hyâŸ©,
  have open_t : is_open t,
  { rw metric.is_open_iff,
    assume x hx,
    rcases metric.is_open_iff.1 hs x hx.1 with âŸ¨Îµ, Îµpos, hÎµâŸ©,
    exact âŸ¨Îµ, Îµpos, A x Îµ hx hÎµâŸ© },
  have st_nonempty : (s âˆ© t).nonempty, from âŸ¨xâ‚€, hxâ‚€, âŸ¨hxâ‚€, hfgâŸ©âŸ©,
  suffices H : closure t âˆ© s âŠ† t, from h's.subset_of_closure_inter_subset open_t st_nonempty H,
  rintros x âŸ¨xt, xsâŸ©,
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ (Îµ : â„) (H : Îµ > 0), metric.ball x Îµ âŠ† s,
    from metric.is_open_iff.1 hs x xs,
  obtain âŸ¨y, yt, hxyâŸ© : âˆƒ (y : E) (yt : y âˆˆ t), dist x y < Îµ / 2,
    from metric.mem_closure_iff.1 xt _ (half_pos Îµpos),
  have B : metric.ball y (Îµ / 2) âŠ† metric.ball x Îµ,
  { apply metric.ball_subset_ball', rw dist_comm, linarith },
  exact A y (Îµ / 2) yt (B.trans hÎµ) (metric.mem_ball.2 hxy)
end

/-- If `f_n â†’ g` pointwise and the derivatives `(f_n)' â†’ h` _uniformly_ converge, then
in fact for a fixed `y`, the difference quotients `â€–z - yâ€–â»Â¹ â€¢ (f_n z - f_n y)` converge
_uniformly_ to `â€–z - yâ€–â»Â¹ â€¢ (g z - g y)` -/
lemma difference_quotients_converge_uniformly
  (hf' : tendsto_uniformly_on_filter f' g' l (ğ“ x))
  (hf : âˆ€á¶  (n : Î¹ Ã— E) in (l Ã—á¶  ğ“ x), has_fderiv_at (f n.1) (f' n.1 n.2) n.2)
  (hfg : âˆ€á¶  (y : E) in ğ“ x, tendsto (Î» n, f n y) l (ğ“ (g y))) :
  tendsto_uniformly_on_filter
    (Î» n : Î¹, Î» y : E, (â€–y - xâ€–â»Â¹ : ğ•œ) â€¢ (f n y - f n x))
    (Î» y : E, (â€–y - xâ€–â»Â¹ : ğ•œ) â€¢ (g y - g x))
    l (ğ“ x) :=
begin
  letI : normed_space â„ E, from normed_space.restrict_scalars â„ ğ•œ _,
  rcases eq_or_ne l âŠ¥ with hl|hl,
  { simp only [hl, tendsto_uniformly_on_filter, bot_prod, eventually_bot, implies_true_iff] },
  haveI : ne_bot l := âŸ¨hlâŸ©,
  refine uniform_cauchy_seq_on_filter.tendsto_uniformly_on_filter_of_tendsto _
    ((hfg.and (eventually_const.mpr hfg.self_of_nhds)).mono (Î» y hy, (hy.1.sub hy.2).const_smul _)),
  rw seminormed_add_group.uniform_cauchy_seq_on_filter_iff_tendsto_uniformly_on_filter_zero,
  rw metric.tendsto_uniformly_on_filter_iff,

  have hfg' := hf'.uniform_cauchy_seq_on_filter,
  rw seminormed_add_group.uniform_cauchy_seq_on_filter_iff_tendsto_uniformly_on_filter_zero at hfg',
  rw metric.tendsto_uniformly_on_filter_iff at hfg',
  intros Îµ hÎµ,
  obtain âŸ¨q, hqpos, hqÎµâŸ© := exists_pos_rat_lt hÎµ,
  specialize hfg' (q : â„) (by simp [hqpos]),

  have := (tendsto_swap4_prod.eventually (hf.prod_mk hf)).diag_of_prod_right,
  obtain âŸ¨a, b, c, d, eâŸ© := eventually_prod_iff.1 (hfg'.and this),
  obtain âŸ¨r, hr, hr'âŸ© := metric.nhds_basis_ball.eventually_iff.mp d,

  rw eventually_prod_iff,
  refine âŸ¨_, b, (Î» e : E, metric.ball x r e),
    eventually_mem_set.mpr (metric.nhds_basis_ball.mem_of_mem hr), (Î» n hn y hy, _)âŸ©,
  simp only [pi.zero_apply, dist_zero_left],
  rw [â† smul_sub, norm_smul, norm_inv, is_R_or_C.norm_coe_norm],
  refine lt_of_le_of_lt _ hqÎµ,
  by_cases hyz' : x = y, { simp [hyz', hqpos.le], },
  have hyz : 0 < â€–y - xâ€–,
  {rw norm_pos_iff, intros hy', exact hyz' (eq_of_sub_eq_zero hy').symm, },
  rw [inv_mul_le_iff hyz, mul_comm, sub_sub_sub_comm],
  simp only [pi.zero_apply, dist_zero_left] at e,
  refine convex.norm_image_sub_le_of_norm_has_fderiv_within_le
    (Î» y hy, ((e hn (hr' hy)).2.1.sub (e hn (hr' hy)).2.2).has_fderiv_within_at)
    (Î» y hy, (e hn (hr' hy)).1.le)
    (convex_ball x r) (metric.mem_ball_self hr) hy,
end

/-- `(d/dx) lim_{n â†’ âˆ} f n x = lim_{n â†’ âˆ} f' n x` when the `f' n` converge
_uniformly_ to their limit at `x`.

In words the assumptions mean the following:
  * `hf'`: The `f'` converge "uniformly at" `x` to `g'`. This does not mean that the `f' n` even
    converge away from `x`!
  * `hf`: For all `(y, n)` with `y` sufficiently close to `x` and `n` sufficiently large, `f' n` is
    the derivative of `f n`
  * `hfg`: The `f n` converge pointwise to `g` on a neighborhood of `x` -/
lemma has_fderiv_at_of_tendsto_uniformly_on_filter [ne_bot l]
  (hf' : tendsto_uniformly_on_filter f' g' l (ğ“ x))
  (hf : âˆ€á¶  (n : Î¹ Ã— E) in (l Ã—á¶  ğ“ x), has_fderiv_at (f n.1) (f' n.1 n.2) n.2)
  (hfg : âˆ€á¶  y in ğ“ x, tendsto (Î» n, f n y) l (ğ“ (g y))) :
  has_fderiv_at g (g' x) x :=
begin
  -- The proof strategy follows several steps:
  --   1. The quantifiers in the definition of the derivative are
  --      `âˆ€ Îµ > 0, âˆƒÎ´ > 0, âˆ€y âˆˆ B_Î´(x)`. We will introduce a quantifier in the middle:
  --      `âˆ€ Îµ > 0, âˆƒN, âˆ€n â‰¥ N, âˆƒÎ´ > 0, âˆ€y âˆˆ B_Î´(x)` which will allow us to introduce the `f(') n`
  --   2. The order of the quantifiers `hfg` are opposite to what we need. We will be able to swap
  --      the quantifiers using the uniform convergence assumption
  rw has_fderiv_at_iff_tendsto,

  -- Introduce extra quantifier via curried filters
  suffices : tendsto
    (Î» (y : Î¹ Ã— E), â€–y.2 - xâ€–â»Â¹ * â€–g y.2 - g x - (g' x) (y.2 - x)â€–) (l.curry (ğ“ x)) (ğ“ 0),
  { rw metric.tendsto_nhds at this âŠ¢,
    intros Îµ hÎµ,
    specialize this Îµ hÎµ,
    rw eventually_curry_iff at this,
    simp only at this,
    exact (eventually_const.mp this).mono (by simp only [imp_self, forall_const]), },

  -- With the new quantifier in hand, we can perform the famous `Îµ/3` proof. Specifically,
  -- we will break up the limit (the difference functions minus the derivative go to 0) into 3:
  --   * The difference functions of the `f n` converge *uniformly* to the difference functions
  --     of the `g n`
  --   * The `f' n` are the derivatives of the `f n`
  --   * The `f' n` converge to `g'` at `x`
  conv
  { congr, funext,
    rw [â†norm_norm, â†norm_inv,â†@is_R_or_C.norm_of_real ğ•œ _ _,
      is_R_or_C.of_real_inv, â†norm_smul], },
  rw â†tendsto_zero_iff_norm_tendsto_zero,
  have : (Î» a : Î¹ Ã— E, (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ (g a.2 - g x - (g' x) (a.2 - x))) =
    (Î» a : Î¹ Ã— E, (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ (g a.2 - g x - (f a.1 a.2 - f a.1 x))) +
    (Î» a : Î¹ Ã— E, (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ ((f a.1 a.2 - f a.1 x) -
      ((f' a.1 x) a.2 - (f' a.1 x) x))) +
    (Î» a : Î¹ Ã— E, (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ ((f' a.1 x - g' x) (a.2 - x))),
  { ext, simp only [pi.add_apply], rw [â†smul_add, â†smul_add], congr,
  simp only [map_sub, sub_add_sub_cancel, continuous_linear_map.coe_sub', pi.sub_apply], },
  simp_rw this,
  have : ğ“ (0 : G) = ğ“ (0 + 0 + 0), simp only [add_zero],
  rw this,
  refine tendsto.add (tendsto.add _ _) _,
  simp only,
  { have := difference_quotients_converge_uniformly hf' hf hfg,
    rw metric.tendsto_uniformly_on_filter_iff at this,
    rw metric.tendsto_nhds,
    intros Îµ hÎµ,
    apply ((this Îµ hÎµ).filter_mono curry_le_prod).mono,
    intros n hn,
    rw dist_eq_norm at hn âŠ¢,
    rw â† smul_sub at hn,
    rwa sub_zero, },
  { -- (Almost) the definition of the derivatives
    rw metric.tendsto_nhds,
    intros Îµ hÎµ,
    rw eventually_curry_iff,
    refine hf.curry.mono (Î» n hn, _),
    have := hn.self_of_nhds,
    rw [has_fderiv_at_iff_tendsto, metric.tendsto_nhds] at this,
    refine (this Îµ hÎµ).mono (Î» y hy, _),
    rw dist_eq_norm at hy âŠ¢,
    simp only [sub_zero, map_sub, norm_mul, norm_inv, norm_norm] at hy âŠ¢,
    rw [norm_smul, norm_inv, is_R_or_C.norm_coe_norm],
    exact hy, },
  { -- hfg' after specializing to `x` and applying the definition of the operator norm
    refine tendsto.mono_left _ curry_le_prod,
    have h1: tendsto (Î» n : Î¹ Ã— E, g' n.2 - f' n.1 n.2) (l Ã—á¶  ğ“ x) (ğ“ 0),
    { rw metric.tendsto_uniformly_on_filter_iff at hf',
      exact metric.tendsto_nhds.mpr (Î» Îµ hÎµ, by simpa using hf' Îµ hÎµ), },
    have h2: tendsto (Î» n : Î¹, g' x - f' n x) l (ğ“ 0),
    { rw metric.tendsto_nhds at h1 âŠ¢,
      exact (Î» Îµ hÎµ, (h1 Îµ hÎµ).curry.mono (Î» n hn, hn.self_of_nhds)), },
    have := (tendsto_fst.comp (h2.prod_map tendsto_id)),
    refine squeeze_zero_norm _ (tendsto_zero_iff_norm_tendsto_zero.mp this),
    intros n,
    simp_rw [norm_smul, norm_inv, is_R_or_C.norm_coe_norm],
    by_cases hx : x = n.2, { simp [hx], },
    have hnx : 0 < â€–n.2 - xâ€–,
    { rw norm_pos_iff, intros hx', exact hx (eq_of_sub_eq_zero hx').symm, },
    rw [inv_mul_le_iff hnx, mul_comm],
    simp only [function.comp_app, prod_map],
    rw norm_sub_rev,
    exact (f' n.1 x - g' x).le_op_norm (n.2 - x), },
end

lemma has_fderiv_at_of_tendsto_locally_uniformly_on [ne_bot l] {s : set E} (hs : is_open s)
  (hf' : tendsto_locally_uniformly_on f' g' l s)
  (hf : âˆ€ n, âˆ€ x âˆˆ s, has_fderiv_at (f n) (f' n x) x)
  (hfg : âˆ€ x âˆˆ s, tendsto (Î» n, f n x) l (ğ“ (g x)))
  (hx : x âˆˆ s) :
  has_fderiv_at g (g' x) x :=
begin
  have h1 : s âˆˆ ğ“ x := hs.mem_nhds hx,
  have h3 : set.univ Ã—Ë¢ s âˆˆ l Ã—á¶  ğ“ x := by simp only [h1, prod_mem_prod_iff, univ_mem, and_self],
  have h4 : âˆ€á¶  (n : Î¹ Ã— E) in l Ã—á¶  ğ“ x, has_fderiv_at (f n.1) (f' n.1 n.2) n.2,
    from eventually_of_mem h3 (Î» âŸ¨n, zâŸ© âŸ¨hn, hzâŸ©, hf n z hz),
  refine has_fderiv_at_of_tendsto_uniformly_on_filter _ h4 (eventually_of_mem h1 hfg),
  simpa [is_open.nhds_within_eq hs hx] using tendsto_locally_uniformly_on_iff_filter.mp hf' x hx,
end

/-- A slight variant of `has_fderiv_at_of_tendsto_locally_uniformly_on` with the assumption stated
in terms of `differentiable_on` rather than `has_fderiv_at`. This makes a few proofs nicer in
complex analysis where holomorphicity is assumed but the derivative is not known a priori. -/
lemma has_fderiv_at_of_tendsto_locally_uniformly_on' [ne_bot l] {s : set E} (hs : is_open s)
  (hf' : tendsto_locally_uniformly_on (fderiv ğ•œ âˆ˜ f) g' l s)
  (hf : âˆ€ n, differentiable_on ğ•œ (f n) s)
  (hfg : âˆ€ x âˆˆ s, tendsto (Î» n, f n x) l (ğ“ (g x)))
  (hx : x âˆˆ s) :
  has_fderiv_at g (g' x) x :=
begin
  refine has_fderiv_at_of_tendsto_locally_uniformly_on hs hf' (Î» n z hz, _) hfg hx,
  exact ((hf n z hz).differentiable_at (hs.mem_nhds hz)).has_fderiv_at
end

/-- `(d/dx) lim_{n â†’ âˆ} f n x = lim_{n â†’ âˆ} f' n x` when the `f' n` converge
_uniformly_ to their limit on an open set containing `x`. -/
lemma has_fderiv_at_of_tendsto_uniformly_on [ne_bot l]
  {s : set E} (hs : is_open s)
  (hf' : tendsto_uniformly_on f' g' l s)
  (hf : âˆ€ (n : Î¹), âˆ€ (x : E), x âˆˆ s â†’ has_fderiv_at (f n) (f' n x) x)
  (hfg : âˆ€ (x : E), x âˆˆ s â†’ tendsto (Î» n, f n x) l (ğ“ (g x))) :
  âˆ€ (x : E), x âˆˆ s â†’ has_fderiv_at g (g' x) x :=
Î» x, has_fderiv_at_of_tendsto_locally_uniformly_on hs hf'.tendsto_locally_uniformly_on hf hfg

/-- `(d/dx) lim_{n â†’ âˆ} f n x = lim_{n â†’ âˆ} f' n x` when the `f' n` converge
_uniformly_ to their limit. -/
lemma has_fderiv_at_of_tendsto_uniformly [ne_bot l]
  (hf' : tendsto_uniformly f' g' l)
  (hf : âˆ€ (n : Î¹), âˆ€ (x : E), has_fderiv_at (f n) (f' n x) x)
  (hfg : âˆ€ (x : E), tendsto (Î» n, f n x) l (ğ“ (g x))) :
  âˆ€ (x : E), has_fderiv_at g (g' x) x :=
begin
  intros x,
  have hf : âˆ€ (n : Î¹), âˆ€ (x : E), x âˆˆ set.univ â†’ has_fderiv_at (f n) (f' n x) x, { simp [hf], },
  have hfg : âˆ€ (x : E), x âˆˆ set.univ â†’ tendsto (Î» n, f n x) l (ğ“ (g x)), { simp [hfg], },
  have hf' : tendsto_uniformly_on f' g' l set.univ, { rwa tendsto_uniformly_on_univ, },
  refine has_fderiv_at_of_tendsto_uniformly_on is_open_univ hf' hf hfg x (set.mem_univ x),
end

end limits_of_derivatives

section deriv

/-! ### `deriv` versions of above theorems

In this section, we provide `deriv` equivalents of the `fderiv` lemmas in the previous section.
The protected function `promote_deriv` provides the translation between derivatives and FrÃ©chet
derivatives
-/

variables {Î¹ : Type*} {l : filter Î¹}
  {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {G : Type*} [normed_add_comm_group G] [normed_space ğ•œ G]
  {f : Î¹ â†’ ğ•œ â†’ G} {g : ğ•œ â†’ G} {f' : Î¹ â†’ ğ•œ â†’ G} {g' : ğ•œ â†’ G}
  {x : ğ•œ}

/-- If our derivatives converge uniformly, then the FrÃ©chet derivatives converge uniformly -/
lemma uniform_cauchy_seq_on_filter.one_smul_right {l' : filter ğ•œ}
  (hf' : uniform_cauchy_seq_on_filter f' l l') :
  uniform_cauchy_seq_on_filter (Î» n, Î» z, (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smul_right (f' n z)) l l' :=
begin
  -- The tricky part of this proof is that operator norms are written in terms of `â‰¤` whereas
  -- metrics are written in terms of `<`. So we need to shrink `Îµ` utilizing the archimedean
  -- property of `â„`

  rw [seminormed_add_group.uniform_cauchy_seq_on_filter_iff_tendsto_uniformly_on_filter_zero,
      metric.tendsto_uniformly_on_filter_iff] at hf' âŠ¢,
  intros Îµ hÎµ,
  obtain âŸ¨q, hq, hq'âŸ© := exists_between hÎµ.lt,
  apply (hf' q hq).mono,
  intros n hn,
  refine lt_of_le_of_lt _ hq',
  simp only [dist_eq_norm, pi.zero_apply, zero_sub, norm_neg] at hn âŠ¢,
  refine continuous_linear_map.op_norm_le_bound _ hq.le _,
  intros z,
  simp only [continuous_linear_map.coe_sub', pi.sub_apply, continuous_linear_map.smul_right_apply,
    continuous_linear_map.one_apply],
  rw [â†smul_sub, norm_smul, mul_comm],
  exact mul_le_mul hn.le rfl.le (norm_nonneg _) hq.le,
end

lemma uniform_cauchy_seq_on_filter_of_deriv
  (hf' : uniform_cauchy_seq_on_filter f' l (ğ“ x))
  (hf : âˆ€á¶  (n : Î¹ Ã— ğ•œ) in (l Ã—á¶  ğ“ x), has_deriv_at (f n.1) (f' n.1 n.2) n.2)
  (hfg : cauchy (map (Î» n, f n x) l)) :
  uniform_cauchy_seq_on_filter f l (ğ“ x) :=
begin
  simp_rw has_deriv_at_iff_has_fderiv_at at hf,
  exact uniform_cauchy_seq_on_filter_of_fderiv
    hf'.one_smul_right hf hfg,
end

lemma uniform_cauchy_seq_on_ball_of_deriv
  {r : â„} (hf' : uniform_cauchy_seq_on f' l (metric.ball x r))
  (hf : âˆ€ n : Î¹, âˆ€ y : ğ•œ, y âˆˆ metric.ball x r â†’ has_deriv_at (f n) (f' n y) y)
  (hfg : cauchy (map (Î» n, f n x) l)) :
  uniform_cauchy_seq_on f l (metric.ball x r) :=
begin
  simp_rw has_deriv_at_iff_has_fderiv_at at hf,
  rw uniform_cauchy_seq_on_iff_uniform_cauchy_seq_on_filter at hf',
  have hf' : uniform_cauchy_seq_on (Î» n, Î» z, (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smul_right (f' n z)) l
    (metric.ball x r),
  { rw uniform_cauchy_seq_on_iff_uniform_cauchy_seq_on_filter,
    exact hf'.one_smul_right, },
  exact uniform_cauchy_seq_on_ball_of_fderiv hf' hf hfg,
end

lemma has_deriv_at_of_tendsto_uniformly_on_filter [ne_bot l]
  (hf' : tendsto_uniformly_on_filter f' g' l (ğ“ x))
  (hf : âˆ€á¶  (n : Î¹ Ã— ğ•œ) in (l Ã—á¶  ğ“ x), has_deriv_at (f n.1) (f' n.1 n.2) n.2)
  (hfg : âˆ€á¶  y in ğ“ x, tendsto (Î» n, f n y) l (ğ“ (g y))) :
  has_deriv_at g (g' x) x :=
begin
  -- The first part of the proof rewrites `hf` and the goal to be functions so that Lean
  -- can recognize them when we apply `has_fderiv_at_of_tendsto_uniformly_on_filter`
  let F' := (Î» n, Î» z, (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smul_right (f' n z)),
  let G' := Î» z, (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smul_right (g' z),
  simp_rw has_deriv_at_iff_has_fderiv_at at hf âŠ¢,

  -- Now we need to rewrite hf' in terms of continuous_linear_maps. The tricky part is that
  -- operator norms are written in terms of `â‰¤` whereas metrics are written in terms of `<`. So we
  -- need to shrink `Îµ` utilizing the archimedean property of `â„`
  have hf' : tendsto_uniformly_on_filter F' G' l (ğ“ x),
  { rw metric.tendsto_uniformly_on_filter_iff at hf' âŠ¢,
    intros Îµ hÎµ,
    obtain âŸ¨q, hq, hq'âŸ© := exists_between hÎµ.lt,
    apply (hf' q hq).mono,
    intros n hn,
    refine lt_of_le_of_lt _ hq',
    simp only [F', G', dist_eq_norm] at hn âŠ¢,
    refine continuous_linear_map.op_norm_le_bound _ hq.le _,
    intros z,
    simp only [continuous_linear_map.coe_sub', pi.sub_apply, continuous_linear_map.smul_right_apply,
      continuous_linear_map.one_apply],
    rw [â†smul_sub, norm_smul, mul_comm],
    exact mul_le_mul hn.le rfl.le (norm_nonneg _) hq.le, },
  exact has_fderiv_at_of_tendsto_uniformly_on_filter hf' hf hfg,
end

lemma has_deriv_at_of_tendsto_locally_uniformly_on [ne_bot l] {s : set ğ•œ} (hs : is_open s)
  (hf' : tendsto_locally_uniformly_on f' g' l s)
  (hf : âˆ€á¶  n in l, âˆ€ x âˆˆ s, has_deriv_at (f n) (f' n x) x)
  (hfg : âˆ€ x âˆˆ s, tendsto (Î» n, f n x) l (ğ“ (g x)))
  (hx : x âˆˆ s) :
  has_deriv_at g (g' x) x :=
begin
  have h1 : s âˆˆ ğ“ x := hs.mem_nhds hx,
  have h2 : âˆ€á¶  (n : Î¹ Ã— ğ•œ) in l Ã—á¶  ğ“ x, has_deriv_at (f n.1) (f' n.1 n.2) n.2,
    from eventually_prod_iff.2 âŸ¨_, hf, Î» x, x âˆˆ s, h1, Î» n, idâŸ©,
  refine has_deriv_at_of_tendsto_uniformly_on_filter _ h2 (eventually_of_mem h1 hfg),
  simpa [is_open.nhds_within_eq hs hx] using tendsto_locally_uniformly_on_iff_filter.mp hf' x hx,
end

/-- A slight variant of `has_deriv_at_of_tendsto_locally_uniformly_on` with the assumption stated in
terms of `differentiable_on` rather than `has_deriv_at`. This makes a few proofs nicer in complex
analysis where holomorphicity is assumed but the derivative is not known a priori. -/
lemma has_deriv_at_of_tendsto_locally_uniformly_on' [ne_bot l] {s : set ğ•œ} (hs : is_open s)
  (hf' : tendsto_locally_uniformly_on (deriv âˆ˜ f) g' l s)
  (hf : âˆ€á¶  n in l, differentiable_on ğ•œ (f n) s)
  (hfg : âˆ€ x âˆˆ s, tendsto (Î» n, f n x) l (ğ“ (g x)))
  (hx : x âˆˆ s) :
  has_deriv_at g (g' x) x :=
begin
  refine has_deriv_at_of_tendsto_locally_uniformly_on hs hf' _ hfg hx,
  filter_upwards [hf] with n h z hz using ((h z hz).differentiable_at (hs.mem_nhds hz)).has_deriv_at
end

lemma has_deriv_at_of_tendsto_uniformly_on [ne_bot l]
  {s : set ğ•œ} (hs : is_open s)
  (hf' : tendsto_uniformly_on f' g' l s)
  (hf : âˆ€á¶  n in l, âˆ€ (x : ğ•œ), x âˆˆ s â†’ has_deriv_at (f n) (f' n x) x)
  (hfg : âˆ€ (x : ğ•œ), x âˆˆ s â†’ tendsto (Î» n, f n x) l (ğ“ (g x))) :
  âˆ€ (x : ğ•œ), x âˆˆ s â†’ has_deriv_at g (g' x) x :=
Î» x, has_deriv_at_of_tendsto_locally_uniformly_on hs hf'.tendsto_locally_uniformly_on hf hfg

lemma has_deriv_at_of_tendsto_uniformly [ne_bot l]
  (hf' : tendsto_uniformly f' g' l)
  (hf : âˆ€á¶  n in l, âˆ€ (x : ğ•œ), has_deriv_at (f n) (f' n x) x)
  (hfg : âˆ€ (x : ğ•œ), tendsto (Î» n, f n x) l (ğ“ (g x))) :
  âˆ€ (x : ğ•œ), has_deriv_at g (g' x) x :=
begin
  intros x,
  have hf : âˆ€á¶  n in l, âˆ€ (x : ğ•œ), x âˆˆ set.univ â†’ has_deriv_at (f n) (f' n x) x,
    by filter_upwards [hf] with n h x hx using h x,
  have hfg : âˆ€ (x : ğ•œ), x âˆˆ set.univ â†’ tendsto (Î» n, f n x) l (ğ“ (g x)), { simp [hfg], },
  have hf' : tendsto_uniformly_on f' g' l set.univ, { rwa tendsto_uniformly_on_univ, },
  exact has_deriv_at_of_tendsto_uniformly_on is_open_univ hf' hf hfg x (set.mem_univ x),
end

end deriv
