/-
Copyright (c) 2022 Kevin H. Wilson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin H. Wilson
-/
import analysis.complex.cauchy_integral
import analysis.calculus.fderiv_analytic
import analysis.calculus.uniform_limits_deriv
import topology.uniform_space.complete_separated

/-!
# Uniform limits of holomorphic functions are holomorphic

The purpose of this file is to prove that a uniform limit of holomorphic functions is holomorphic,
a critical component of many theories, notably that of Dirichlet series.

## Definitions

* `formal_multilinear_series.antideriv` : The formal antiderivative of a power series with a one
  dimensional domain
* `has_fpower_series_on_ball.antideriv` : The formal antiderivative of an analytic function on a
  ball

## Main statements

* `has_fpower_series_on_ball.antideriv_has_deriv_at` : Morera's Theorem. A function on `â„` or `â„‚`
  that is analytic on a ball admits an antiderivative on that ball.
* `complex.analytic_at_of_tendsto_uniformly_on` : If `f : â„• â†’ â„‚ â†’ â„‚` is a sequence functions which
  are analytic on a shared neighborhood of `x` and they converge _uniformly_ to `g : â„‚ â†’ â„‚` on that
  neighborhood, then `f` is also analytic at `x`.
* `complex.analytic_on_of_tendsto_uniformly_on` : Same as above, but if the shared neighborhood `s`
  is _open_, then in fact `f` is analytic on all of `s`.


## Implementation notes

The steps to our proof are:
  * Develop a language which lets us translate between the vast complexity of formal multilinear
    series that form the foundation of analyticity in mathlib, and more prosaic sums when we're in
    one dimension
  * Given an analytic function `f : ğ•œ â†’ ğ•œ` on _any_ nontrivially normed field, define an
    antiderivative `F : ğ•œ â†’ ğ•œ`
  * Now when `ğ•œ` is either `â„` or `â„‚`, use the mean value theorem to show that given a sequence of
    analytic functions `f : â„• â†’ ğ•œ â†’ ğ•œ`, the sequence of antiderivatives `F : â„• â†’ ğ•œ â†’ ğ•œ` form a
    uniform Cauchy sequence and thus converge to some function `G`
  * Apply `has_fderiv_at_of_tendsto_uniformly_on` to show that `G' = g` and so, when
    `ğ•œ = â„‚`, we have that `G` is analytic (`differentiable_on.analytic_on`) and thus so is `g`
    (`analytic_on.fderiv`)

## Tags

uniform convergence, holomorphic functions, morera's theorem
-/

open filter nat
open_locale big_operators topological_space uniformity

variables {Î¹ ğ•œ E F : Type*} [fintype Î¹] [decidable_eq Î¹]

section general

@[simp] lemma norm_const_zero {Î¹ M : Type*} [fintype Î¹] [nonempty Î¹] [normed_add_comm_group M] :
  âˆ¥(0 : Î¹ â†’ M)âˆ¥ = 0 :=
by { convert pi_norm_const (0 : M), rw norm_zero, apply_instance }

@[simp] lemma norm_const_one {Î¹ M : Type*} [fintype Î¹] [nonempty Î¹] [has_one M]
  [normed_add_comm_group M] [norm_one_class M] :
  âˆ¥(1 : Î¹ â†’ M)âˆ¥ = 1 :=
by { convert pi_norm_const (1 : M), rw norm_one, apply_instance }

@[simp] lemma norm_is_empty {Î¹ M : Type*} [is_empty Î¹] [normed_add_comm_group M] (f : Î¹ â†’ M) :
  âˆ¥fâˆ¥ = 0 :=
by { rw subsingleton.elim f 0, refl }

lemma fooâ‚ [comm_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E] (f : multilinear_map ğ•œ (Î» i : Î¹, ğ•œ) E)
  (x : Î¹ â†’ ğ•œ) : f x = (âˆ i, x i) â€¢ (f 1) :=
begin
  rw â† multilinear_map.map_smul_univ,
  exact congr_arg f (funext $ Î» i, by simp)
end

lemma barâ‚ [comm_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E]
  [topological_space ğ•œ] [topological_space E]
  (f : continuous_multilinear_map ğ•œ (Î» i : Î¹, ğ•œ) E)
  (x : Î¹ â†’ ğ•œ) : f x = (âˆ i, x i) â€¢ (f 1) :=
fooâ‚ f.to_multilinear_map x

lemma partial_sumâ‚ [comm_ring ğ•œ] [add_comm_group E] [module ğ•œ E]
  [topological_space ğ•œ] [topological_add_group ğ•œ] [has_continuous_const_smul ğ•œ ğ•œ]
  [topological_space E] [topological_add_group E] [has_continuous_const_smul ğ•œ E]
  (Ï† : formal_multilinear_series ğ•œ ğ•œ E) (x : ğ•œ) (n : â„•) :
  Ï†.partial_sum n x = âˆ‘ k in finset.range n, x^k â€¢ (Ï† k 1) :=
begin
  rw formal_multilinear_series.partial_sum,
  congr,
  ext n,
  rw [barâ‚, fin.prod_const],
end

lemma partial_sum5 [comm_ring ğ•œ] [add_comm_group E] [module ğ•œ E]
  [topological_space ğ•œ] [topological_add_group ğ•œ] [has_continuous_const_smul ğ•œ ğ•œ]
  [topological_space E] [topological_add_group E] [has_continuous_const_smul ğ•œ E]
  (Ï† : formal_multilinear_series ğ•œ ğ•œ E) (n : â„•) :
  Ï†.partial_sum n = (Î» x : ğ•œ, âˆ‘ k in finset.range n, x^k â€¢ (Ï† k 1)) :=
begin
  ext,
  exact partial_sumâ‚ Ï† x n,
end

end general

section normed_field
variables [normed_field ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E]

/-- The formal antiderivative of a multilinear power series with a one-dimensional domain. Note
that while we have defined this for any `normed_field`, it really only makes sense when that
field is characterisitic 0. -/
def formal_multilinear_series.antideriv (Ï† : formal_multilinear_series ğ•œ ğ•œ E) :
  formal_multilinear_series ğ•œ ğ•œ E
| 0 := 0
| (n + 1) := ((n + 1) : ğ•œ)â»Â¹ â€¢
  (continuous_multilinear_map.mk_pi_algebra_fin ğ•œ (n + 1) ğ•œ).smul_right (Ï† n 1)

end normed_field

section nontrivially_normed_field

variables [nontrivially_normed_field ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E]

lemma continuous_multilinear_map.norm_one_dim {f : continuous_multilinear_map ğ•œ (Î» i : Î¹, ğ•œ) E} :
  âˆ¥fâˆ¥ = âˆ¥f 1âˆ¥ :=
begin
  refine le_antisymm _ _,
  convert continuous_multilinear_map.op_norm_le_bound _ (norm_nonneg _) _,
  { intros m,
    apply le_of_eq,
    have : f m = (âˆ i, m i) â€¢ (f 1),
    { convert barâ‚ _ m, },
    rw [this, norm_smul, mul_comm, norm_prod], },

  { convert continuous_multilinear_map.unit_le_op_norm _ 1 _,
    refl,
    casesI is_empty_or_nonempty Î¹,
    { refine le_of_eq_of_le _ zero_le_one,
      simp only [norm_eq_zero, eq_iff_true_of_subsingleton], },
    { exact norm_const_one.le, }, },
end

lemma continuous_multilinear_map.norm_smul_right {f : continuous_multilinear_map ğ•œ (Î» i : Î¹, ğ•œ) ğ•œ}
  {x : E} : âˆ¥f.smul_right xâˆ¥ = âˆ¥fâˆ¥ * âˆ¥xâˆ¥ :=
by rw [continuous_multilinear_map.norm_one_dim, continuous_multilinear_map.norm_one_dim,
  continuous_multilinear_map.smul_right_apply, norm_smul]

end nontrivially_normed_field

section is_R_or_C

variables [is_R_or_C ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E]
  (Ï† : formal_multilinear_series ğ•œ ğ•œ E)

lemma formal_multilinear_series.antideriv_radius_mono_aux {r : nnreal}
  (hr : â†‘r < Ï†.radius) : â†‘r â‰¤ Ï†.antideriv.radius :=
begin
  obtain âŸ¨C, hC, hmâŸ© := Ï†.norm_mul_pow_le_of_lt_radius hr,
  refine formal_multilinear_series.le_radius_of_bound _ (C * r) _,
  intros n,
  induction n with n hn,
  { simp only [formal_multilinear_series.antideriv, norm_zero, zero_mul],
    exact mul_nonneg hC.lt.le nnreal.zero_le_coe, },

  have : n.succ = n + 1, refl,
  rw this,
  dunfold formal_multilinear_series.antideriv,
  rw [norm_smul, continuous_multilinear_map.norm_smul_right, norm_inv,
    continuous_multilinear_map.norm_mk_pi_algebra_fin, one_mul, pow_add (r : â„) n 1,
    â† mul_assoc, pow_one, â† continuous_multilinear_map.norm_one_dim],

  refine mul_le_mul _ rfl.le nnreal.zero_le_coe hC.lt.le,
  have : C = 1 * C, simp,
  rw [this, mul_assoc],
  refine mul_le_mul _ (hm n)
    (mul_nonneg (norm_nonneg _) (by simp only [pow_nonneg, nnreal.zero_le_coe])) zero_le_one,

  norm_cast,
  rw [is_R_or_C.norm_eq_abs, is_R_or_C.abs_cast_nat],
  rw inv_le _ zero_lt_one,
  { simp, },
  { norm_cast, simp, },
  { apply_instance, },
end

lemma formal_multilinear_series.antideriv_radius_mono :
  Ï†.radius â‰¤ Ï†.antideriv.radius :=
begin
  by_contradiction h,
  push_neg at h,
  obtain âŸ¨r, hr, hr'âŸ© := ennreal.lt_iff_exists_nnreal_btwn.mp h,
  exact not_lt_of_le rfl.le
    (lt_of_lt_of_le hr (Ï†.antideriv_radius_mono_aux hr')),
end

lemma formal_multilinear_series.antideriv_has_deriv_at_parial_sum {y : ğ•œ} {n : â„•} :
  has_deriv_at (Ï†.antideriv.partial_sum n.succ) (Ï†.partial_sum n y) y :=
begin
  -- Proof is by induction and the fact that d/dx (x^n) = n x^(n - 1)
  rw partial_sumâ‚,
  rw partial_sum5,
  induction n with n hn,
  { -- base case is trivial as it's an empty sum
    simp only [finset.range_one, finset.sum_singleton, pow_zero, one_smul,
    finset.range_zero, finset.sum_empty],
    exact has_deriv_at_const y _, },

  -- Inductive case's main difficulty is cancelling (n + 1)â»Â¹ through a bunch of casts
  rw finset.sum_range_succ,
  conv { congr, funext, rw finset.sum_range_succ, },
  refine has_deriv_at.add hn _,
  simp only [formal_multilinear_series.antideriv, continuous_multilinear_map.smul_apply,
    continuous_multilinear_map.smul_right_apply, continuous_multilinear_map.mk_pi_algebra_fin_apply,
    list.of_fn_succ, pi.one_apply, list.of_fn_const, list.prod_cons, list.prod_repeat, one_pow,
    mul_one, one_smul],
  conv { congr, funext, rw â† smul_assoc, },
  refine has_deriv_at.smul_const _ (Ï† n 1),
  have aa := (has_deriv_at_pow (n + 1) y).const_mul ((n : ğ•œ) + 1)â»Â¹,
  simp only [cast_add, cast_one, add_succ_sub_one, add_zero] at aa,
  have : (((n : ğ•œ) + 1)â»Â¹ * (((n : ğ•œ) + 1) * y ^ n)) = y ^ n,
  { rw â†mul_assoc,
    conv { congr, skip, rw â† one_mul (y ^ n), },
    congr,
    rw inv_mul_cancel,
    norm_cast,
    simp, },
  rw this at aa,
  apply aa.congr_of_eventually_eq,
  simp only [eventually_eq, algebra.id.smul_eq_mul, mul_eq_mul_left_iff, inv_eq_zero],
  exact eventually_of_forall (Î» y, by rw mul_comm),
end

lemma formal_multilinear_series.antideriv_has_deriv_at_sum [complete_space E] {y : ğ•œ}
  (hÏ† : 0 < Ï†.radius) (hy' : y âˆˆ emetric.ball (0 : ğ•œ) Ï†.radius) :
  has_deriv_at Ï†.antideriv.sum (Ï†.sum y) y :=
begin
  -- For technical reasons involving uniform convergence, we need to shrink our radius
  obtain âŸ¨r, hr, hr'âŸ© : âˆƒ (r : nnreal), nndist y 0 < r âˆ§ â†‘r < Ï†.radius,
  { suffices : âˆƒ (r : nnreal), ((nndist y 0) : ennreal) < r âˆ§ â†‘r < Ï†.radius,
    { obtain âŸ¨r, hr, hr'âŸ© := this,
      refine âŸ¨r, (by simpa using hr), hr'âŸ©, },
    rw [emetric.mem_ball, edist_nndist] at hy',
    exact ennreal.lt_iff_exists_nnreal_btwn.mp hy', },

  -- Ultimately, we'll use the fact that you can swap limits and derivatives when
  -- the derivatives converge uniformly
  have h3 : âˆ€ z : ğ•œ, z âˆˆ metric.ball (0 : ğ•œ) r â†’
    tendsto (Î» n : â„•, Ï†.antideriv.partial_sum n.succ z) at_top (ğ“ (Ï†.antideriv.sum z)),
    { intros z hz,
      suffices ha : tendsto (Î» (n : â„•), Ï†.antideriv.partial_sum n z) at_top
        (ğ“ (Ï†.antideriv.sum z)),
      { exact ha.comp
        (tendsto_at_top_at_top_of_monotone (Î» b c, succ_le_succ) (Î» b, âŸ¨b, le_succ bâŸ©)), },
      have h1 := lt_of_lt_of_le hÏ† Ï†.antideriv_radius_mono,
      have h2 := lt_of_lt_of_le hr' Ï†.antideriv_radius_mono,
      have h3 := ((Ï†.antideriv.has_fpower_series_on_ball h1).tendsto_uniformly_on h2).tendsto_at hz,
      simpa using h3, },

  refine has_deriv_at_of_tendsto_uniformly_on metric.is_open_ball _ h3
    (by simpa using (Ï†.has_fpower_series_on_ball hÏ†).tendsto_uniformly_on hr') y hr,
  { intros n z hz, exact Ï†.antideriv_has_deriv_at_parial_sum, },
end

end is_R_or_C

section is_R_or_C_fpower_series
variables [is_R_or_C ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E] [complete_space E]
  {f : ğ•œ â†’ E} {Ï† : formal_multilinear_series ğ•œ ğ•œ E} {x : ğ•œ} {r : ennreal}

/-- The antiderivative of an analytic funciton -/
noncomputable def has_fpower_series_on_ball.antideriv
  (h : has_fpower_series_on_ball f Ï† x r) : ğ•œ â†’ E :=
Î» z, Ï†.antideriv.sum (z - x)

lemma has_fpower_series_on_ball.antideriv_has_fpower_series_on_ball
  (h : has_fpower_series_on_ball f Ï† x r) :
  has_fpower_series_on_ball h.antideriv Ï†.antideriv x r :=
begin
  have : x = 0 + x, simp,
  conv {congr, skip, skip, rw this,},
  dunfold has_fpower_series_on_ball.antideriv,
  apply has_fpower_series_on_ball.comp_sub,
  refine has_fpower_series_on_ball.mono _ h.r_pos (h.r_le.trans Ï†.antideriv_radius_mono),
  refine Ï†.antideriv.has_fpower_series_on_ball _,
  calc 0 < r : h.r_pos
    ... â‰¤ Ï†.radius : h.r_le
    ... â‰¤ Ï†.antideriv.radius : Ï†.antideriv_radius_mono,
end

/-- **Morera's Theorem**: An analytic function over `â„` or `â„‚` admits an antiderivative -/
lemma has_fpower_series_on_ball.antideriv_has_deriv_at
  (h : has_fpower_series_on_ball f Ï† x r) {y : ğ•œ} (hy : y âˆˆ emetric.ball x r) :
  has_deriv_at h.antideriv (f y) y :=
begin
  let recenter : ğ•œ â†’ ğ•œ := (Î» z, z - x),
  have : h.antideriv = Ï†.antideriv.sum âˆ˜ recenter,
  { funext,
    simp [has_fpower_series_on_ball.antideriv, formal_multilinear_series.antideriv, recenter], },
  rw this,
  have hyr : y - x âˆˆ emetric.ball (0 : ğ•œ) r,
  { rw [emetric.mem_ball, edist_dist, dist_eq_norm] at hy âŠ¢,
    rw sub_zero,
    exact hy, },
  have hyÏ† : y - x âˆˆ emetric.ball (0 : ğ•œ) Ï†.radius,
  { exact set.mem_of_mem_of_subset hyr (emetric.ball_subset_ball h.r_le), },
  have := Ï†.antideriv_has_deriv_at_sum (lt_of_lt_of_le h.r_pos h.r_le) hyÏ†,
  have aa := has_deriv_at.scomp y this ((has_deriv_at_id y).sub_const x),
  have bb : f y = Ï†.sum (y - x), { simpa using h.sum hyr, },
  rw â†bb at aa,
  simpa using aa,
end

/-- **Morera's Theorem**: An analytic function over `â„` or `â„‚` admits an antiderivative -/
lemma has_fpower_series_at.antideriv_has_deriv_at
  (h : has_fpower_series_at f Ï† x) :
  has_deriv_at (classical.some_spec h).antideriv (f x) x :=
begin
  refine has_fpower_series_on_ball.antideriv_has_deriv_at _ _,
  rw [emetric.mem_ball, edist_self],
  exact (classical.some_spec h).r_pos,
end

end is_R_or_C_fpower_series

section complex
variables {Î· : Type*} {l : filter Î·} [ne_bot l]
  [normed_add_comm_group E] [normed_space â„‚ E] [complete_space E]
  {f : Î· â†’ â„‚ â†’ E} {g : â„‚ â†’ E} {Ï† : formal_multilinear_series â„‚ â„‚ E} {x : â„‚}
  {r : ennreal} {s : set â„‚}

/-- If a sequence of holomorphic functions converges uniformly on a neighborhhod of `x`, then the
limit is also holomorphic at `x`. -/
theorem complex.analytic_at_of_tendsto_uniformly_on (hs : s âˆˆ ğ“ x)
  (hf : âˆ€ (n : Î·), analytic_on â„‚ (f n) s)
  (hfg : tendsto_uniformly_on f g l s) : analytic_at â„‚ g x :=
begin
  -- Proof strategy: We will use the fact that the complex derivative of a complex function is
  -- analytic. To do so, we first construct antiderivatives of `f n` and `g` by shrinking to a
  -- small ball around `x` and applying the above machinery
  obtain âŸ¨_r, h_r, h_r'âŸ© := metric.nhds_basis_closed_ball.mem_iff.mp hs,
  let r : nnreal := _r.to_nnreal,
  have hr : 0 < r, exact real.to_nnreal_pos.mpr h_r,
  have : max _r 0 = _r, { exact max_eq_left_of_lt h_r, },
  have hr' : metric.closed_ball x r âŠ† s, {simp [this, h_r'], },

  -- Our first use of `â„‚` instead of `â„`: An analytic function has a power series which converges on
  -- the largest ball on which the function is differentiable. We use this to get a _common_ radius
  -- of convergence.
  have hfp : âˆ€ n, has_fpower_series_on_ball (f n) (cauchy_power_series (f n) x r) x r,
  { intros n,
    refine differentiable_on.has_fpower_series_on_ball _ hr,
    intros y hy,
    exact (hf n y (set.mem_of_mem_of_subset hy hr')).differentiable_at.differentiable_within_at, },

  -- Construct the antiderivatives
  let F : Î· â†’ â„‚ â†’ E := (Î» n, (hfp n).antideriv),
  let G : â„‚ â†’ E := (Î» z, lim l (Î» n, F n z)),

  -- Show that the `F` converge (necessarily to `G`) via
  -- `uniform_cauchy_seq_on_ball_of_tendsto_uniformly_on_ball_deriv`
  have hF : âˆ€ n y, y âˆˆ metric.ball x r â†’ has_deriv_at (F n) (f n y) y,
  { intros n y hy,
    have : y âˆˆ emetric.ball x r,
    { rw [emetric.mem_ball, edist_nndist],
      rw [metric.mem_ball, dist_nndist] at hy,
      norm_cast at hy âŠ¢,
      exact hy, },
    exact (hfp n).antideriv_has_deriv_at this, },
  have hFG : tendsto (Î» n, F n x) l (ğ“ (G x)),
  { refine tendsto_nhds_lim âŸ¨0, _âŸ©,
    have : âˆ€ n, F n x = 0,
    { intros n,
      have := (hfp n).antideriv_has_fpower_series_on_ball.coeff_zero,
      simp only [formal_multilinear_series.antideriv, real.coe_to_nnreal',
        continuous_multilinear_map.zero_apply, fin.forall_fin_zero_pi] at this,
      exact this.symm, },
    simp_rw this,
    exact tendsto_const_nhds, },
  have hFG' := hfg.mono (metric.ball_subset_closed_ball.trans hr'),
  have hFG : âˆ€ y, y âˆˆ metric.ball x r â†’ tendsto (Î» n, F n y) l (ğ“ (G y)),
  { intros y hy,
    have := uniform_cauchy_seq_on_ball_of_tendsto_uniformly_on_ball_deriv hr hF hFG
      hFG'.uniform_cauchy_seq_on,
    have : cauchy (map (Î» n, F n y) l),
    { rw metric.cauchy_iff,
      split,
      { exact filter.map_ne_bot, },
      intros Îµ hÎµ,
      obtain âŸ¨N, hN, hNmâŸ© := (metric.uniform_cauchy_seq_on_iff'.mp this) Îµ hÎµ,
      refine âŸ¨_, image_mem_map hN, Î» m hm n hn, _âŸ©,
      obtain âŸ¨m', hm'âŸ© := hm,
      obtain âŸ¨n', hn'âŸ© := hn,
      simp only at hm' hn',
      rw [â†hm'.2, â†hn'.2],
      exact hNm m' hm'.1 n' hn'.1 y hy, },
    rw cauchy_map_iff_exists_tendsto at this,
    simpa using tendsto_nhds_lim this, },

  -- Since the `F` converge to `G`, we can use `has_deriv_at_of_tendsto_uniformly_on` to show that
  -- the derivative of `G` is `g` at `x`
  have : is_open (metric.ball x r), exact metric.is_open_ball,
  have hfin := has_deriv_at_of_tendsto_uniformly_on this hF hFG
    (hfg.mono (metric.ball_subset_closed_ball.trans hr')),

  -- Our second use of `â„‚`: differentiability implies analyticity
  have : analytic_on â„‚ G (metric.ball x r),
  { intros y hy,
    have : metric.ball x r âˆˆ ğ“ y,
    { exact mem_nhds_iff.mpr âŸ¨metric.ball x r, rfl.subset, metric.is_open_ball, hyâŸ©, },
    refine differentiable_on.analytic_at _ this,
    intros z hz,
    exact (hfin z hz).differentiable_at.differentiable_within_at, },

  -- Analyticity implies the derivative is analytic
  obtain âŸ¨p, âŸ¨R, hRâŸ©âŸ© := (this.deriv x (metric.mem_ball_self hr)),

  -- The `congr` for replacing `deriv G` with `g` requires us to show that `deriv G` and
  -- `g` match on a small ball around `x`. So shrink radii further so we can apply `hfin`
  obtain âŸ¨R', hlR', hrR'âŸ© := ennreal.lt_iff_exists_nnreal_btwn.mp hR.r_pos,
  use [p, min R' r],
  have hR' := hR.mono hlR' hrR'.le,
  refine (hR'.mono (by simp [lt_min, hR'.r_pos, hr]) (min_le_left R' r)).congr _,

  -- Finally, apply `hfin` on this small ball
  intros y hy,
  simp only [emetric.mem_ball, lt_min_iff, edist_lt_coe] at hy,
  exact (hfin y hy.2).deriv,
end

/-- If a sequence of holomorphic functions converges uniformly on a domain, then the
limit is also holomorphic on the domain -/
theorem complex.analytic_on_of_tendsto_uniformly_on (hs : is_open s)
  (hf : âˆ€ (n : Î·), analytic_on â„‚ (f n) s)
  (hfg : tendsto_uniformly_on f g l s) : analytic_on â„‚ g s :=
Î» x hx, complex.analytic_at_of_tendsto_uniformly_on
  (mem_nhds_iff.mpr âŸ¨s, rfl.subset, hs, hxâŸ©) hf hfg

end complex
