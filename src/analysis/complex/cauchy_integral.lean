/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import measure_theory.measure.complex_lebesgue
import measure_theory.integral.divergence_theorem
import measure_theory.integral.circle_integral
import analysis.analytic.basic
import data.real.cardinality

/-!
# Cauchy integral formula

In this file we prove Cauchy theorem and Cauchy integral formula for integrals over circles. Most
results are formulated for a function `f : â„‚ â†’ E` that takes values in a complex Banach space with
second countable topology.

## Main statements

* `complex.integral_boundary_rect_of_has_fderiv_within_at_real_off_countable`: If a function
  `f : â„‚ â†’ E` is *real* differentiable on a rectangle, then its integral over the boundary of this
  rectangle is equal to the integral of `I â€¢ f' (x + y * I) 1 - f' (x + y * I) I` over the
  rectangle, where `f' z w : E` is the derivative of `f` at `z` in the direction `w` and
  `I = complex.I` is the imaginary unit.

* `complex.integral_boundary_rect_eq_zero_of_differentiable_on_off_countable`: If a function
  `f : â„‚ â†’ E` is *complex* differentiable on a rectangle, then its integral over the boundary of
  this rectangle is equal to zero.

* `complex.circle_integral_sub_center_inv_smul_eq_of_differentiable_on_annulus_off_countable`:
  If a function `f : â„‚ â†’ E` is complex differentiable on an annulus `{z | r â‰¤ |z - c| â‰¤ R}`,
  then the integrals of `(z - c)â»Â¹ â€¢ f z` over the outer boundary and over the inner boundary are
  equal.

* `complex.circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable_of_tendsto`,
  `complex.circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable`:
  If a function `f : â„‚ â†’ E` is complex differentiable on a punctured closed disc
  `{z | |z - c| â‰¤ R âˆ§ z â‰  c}` and tends to `y` as `z â†’ c`, `z â‰  c`, then the integral of
  `(z - c)â»Â¹ â€¢ f z` over the circle `|z - c| = R` is equal to `2Ï€iy`. In particular, if `f`
  is differentiable on the whole closed disc, then this integral is equal to `2Ï€if(c)`.

* `complex.circle_integral_sub_inv_smul_of_differentiable_on`, **Cauchy integral formula**: if
  `f : â„‚ â†’ E` is complex differentiable on a closed disc of radius `R`, then for any `w` in the
  corresponding open disc the integral of `(z - w)â»Â¹ â€¢ f z` over the boundary of the disc is equal
  to `2Ï€if(w)`.

* `differentiable_on.has_fpower_series_on_ball`: If `f : â„‚ â†’ E` is complex differentiable on a
  closed disc of positive radius, then it is analytic on the corresponding open disc, and the
  coefficients of the power series are given by Cauchy integral formulas.

* `differentiable_on.analytic_at`, `differentiable.analytic_at`: If `f : â„‚ â†’ E` is differentiable
  on a neighborhood of a point, then it is analytic at this point. In particular, if `f : â„‚ â†’ E`
  is differentiable on the whole `â„‚`, then it is analytic at every point `z : â„‚`.

## Tags

Cauchy theorem, Cauchy integral formula
-/

open topological_space set measure_theory interval_integral metric filter function
open_locale interval real nnreal ennreal topological_space big_operators

noncomputable theory

universes u v

variables {E : Type u} [normed_group E] [normed_space â„‚ E] [measurable_space E] [borel_space E]
  [second_countable_topology E] [complete_space E]

namespace complex

/-- Suppose that a function `f : â„‚ â†’ E` is real differentiable on a rectangle with vertices
`z w : â„‚` and $\frac{\partial f}{\partial \bar z}$ is integrable on this rectangle. Then
the integral of `f` over the boundary of the rectangle is equal to the integral of
$2i\frac{\partial f}{\partial \bar z}=i\frac{\partial f}{\partial x}-\frac{\partial f}{\partial y}$
over the rectangle.

Moreover, the same is true if `f` is only differentiable at points outside of a countable set `s`
and is continuous at the points of this set. -/
lemma integral_boundary_rect_of_has_fderiv_within_at_real_off_countable (f : â„‚ â†’ E)
  (f' : â„‚ â†’ â„‚ â†’L[â„] E) (z w : â„‚) (s : set â„‚) (hs : countable s)
  (Hc : continuous_on f (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]))
  (Hd : âˆ€ x âˆˆ (re â»Â¹' (Ioo (min z.re w.re) (max z.re w.re)) âˆ©
    im â»Â¹' (Ioo (min z.im w.im) (max z.im w.im))) \ s, has_fderiv_at f (f' x) x)
  (Hi : integrable_on (Î» z, I â€¢ f' z 1 - f' z I) (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im])) :
  (âˆ« x : â„ in z.re..w.re, f (x + z.im * I)) - (âˆ« x : â„ in z.re..w.re, f (x + w.im * I)) +
    (I â€¢ âˆ« y : â„ in z.im..w.im, f (re w + y * I)) - I â€¢ âˆ« y : â„ in z.im..w.im, f (re z + y * I) =
    âˆ« x : â„ in z.re..w.re, âˆ« y : â„ in z.im..w.im, I â€¢ f' (x + y * I) 1 - f' (x + y * I) I :=
begin
  set e : (â„ Ã— â„) â‰ƒL[â„] â„‚ := equiv_real_prodâ‚—.symm,
  have he : âˆ€ x y : â„, â†‘x + â†‘y * I = e (x, y), from Î» x y, (mk_eq_add_mul_I x y).symm,
  have heâ‚ : e (1, 0) = 1 := rfl, have heâ‚‚ : e (0, 1) = I := rfl,
  simp only [he] at *,
  set F : (â„ Ã— â„) â†’ E := f âˆ˜ e,
  set F' : (â„ Ã— â„) â†’ (â„ Ã— â„) â†’L[â„] E := Î» p, (f' (e p)).comp (e : (â„ Ã— â„) â†’L[â„] â„‚),
  have hF' : âˆ€ p : â„ Ã— â„, (-(I â€¢ F' p)) (1, 0) + F' p (0, 1) = -(I â€¢ f' (e p) 1 - f' (e p) I),
  { rintro âŸ¨x, yâŸ©, simp [F', heâ‚, heâ‚‚, â† sub_eq_neg_add], },
  set R : set (â„ Ã— â„) := [z.re, w.re].prod [w.im, z.im],
  set t : set (â„ Ã— â„) := e â»Â¹' s,
  rw [interval_swap z.im] at Hc Hi, rw [min_comm z.im, max_comm z.im] at Hd,
  have hR : e â»Â¹' (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [w.im, z.im]) = R := rfl,
  have htc : continuous_on F R, from Hc.comp e.continuous_on hR.ge,
  have htd : âˆ€ p âˆˆ (Ioo (min z.re w.re) (max z.re w.re)).prod
    (Ioo (min w.im z.im) (max w.im z.im)) \ t, has_fderiv_at F (F' p) p,
    from Î» p hp, (Hd (e p) hp).comp p e.has_fderiv_at,
  simp_rw [â† interval_integral.integral_smul, interval_integral.integral_symm w.im z.im,
    â† interval_integral.integral_neg, â† hF'],
  refine (integral2_divergence_prod_of_has_fderiv_within_at_off_countable
      (Î» p, -(I â€¢ F p)) F (Î» p, - (I â€¢ F' p)) F' z.re w.im w.re z.im t (hs.preimage e.injective)
      (continuous_on_const.smul htc).neg htc (Î» p hp, ((htd p hp).const_smul I).neg) htd _).symm,
  rw [â† volume_preserving_equiv_real_prod.symm.integrable_on_comp_preimage
    (measurable_equiv.measurable_embedding _)] at Hi,
  simpa only [hF'] using Hi.neg
end

/-- **Cauchy theorem**: the integral of a complex differentiable function over the boundary of a
rectangle equals zero.

Moreover, the same is true if `f` is only differentiable at points outside of a countable set `s`
and is continuous at the points of this set. -/
lemma integral_boundary_rect_eq_zero_of_differentiable_on_off_countable (f : â„‚ â†’ E)
  (z w : â„‚) (s : set â„‚) (hs : countable s)
  (Hc : continuous_on f (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]))
  (Hd : âˆ€ x âˆˆ (re â»Â¹' (Ioo (min z.re w.re) (max z.re w.re)) âˆ©
    im â»Â¹' (Ioo (min z.im w.im) (max z.im w.im))) \ s, differentiable_at â„‚ f x) :
  (âˆ« x : â„ in z.re..w.re, f (x + z.im * I)) - (âˆ« x : â„ in z.re..w.re, f (x + w.im * I)) +
    (I â€¢ âˆ« y : â„ in z.im..w.im, f (re w + y * I)) -
      I â€¢ âˆ« y : â„ in z.im..w.im, f (re z + y * I) = 0 :=
by refine (integral_boundary_rect_of_has_fderiv_within_at_real_off_countable f
  (Î» z, (fderiv â„‚ f z).restrict_scalars â„) z w s hs Hc
  (Î» x hx, (Hd x hx).has_fderiv_at.restrict_scalars â„) _).trans _;
    simp [â† continuous_linear_map.map_smul]

/-- If `f : â„‚ â†’ E` is complex differentiable on the closed annulus `r â‰¤ âˆ¥z - câˆ¥ â‰¤ R`, `0 < r â‰¤ R`,
then the integrals of `f z / (z - c)` (formally, `(z - c)â»Â¹ â€¢ f z`) over the circles
`âˆ¥z - câˆ¥ = r` and `âˆ¥z - câˆ¥ = R` are equal to each other.

Moreover, the same is true if `f` is differentiable at points of the annulus outside of a countable
set `s` and is continuous at points of this set.  -/
lemma circle_integral_sub_center_inv_smul_eq_of_differentiable_on_annulus_off_countable
  {c : â„‚} {r R : â„} (h0 : 0 < r) (hle : r â‰¤ R) {f : â„‚ â†’ E} {s : set â„‚} (hs : countable s)
  (hc : continuous_on f (closed_ball c R \ ball c r))
  (hd : âˆ€ z âˆˆ ball c R \ closed_ball c r \ s, differentiable_at â„‚ f z) :
  âˆ® z in C(c, R), (z - c)â»Â¹ â€¢ f z = âˆ® z in C(c, r), (z - c)â»Â¹ â€¢ f z :=
begin
  /- We apply the previous lemma to `Î» z, f (c + exp z)` on the rectangle
  `[log r, log R] Ã— [0, 2 * Ï€]`. -/
  set A := closed_ball c R \ ball c r,
  obtain âŸ¨a, rflâŸ© : âˆƒ a, real.exp a = r, from âŸ¨real.log r, real.exp_log h0âŸ©,
  obtain âŸ¨b, rflâŸ© : âˆƒ b, real.exp b = R, from âŸ¨real.log R, real.exp_log (h0.trans_le hle)âŸ©,
  rw [real.exp_le_exp] at hle,
  -- Unfold definition of `circle_integral` and cancel some terms.
  suffices : âˆ« Î¸ in 0..2 * Ï€, I â€¢ f (circle_map c (real.exp b) Î¸) =
    âˆ« Î¸ in 0..2 * Ï€, I â€¢ f (circle_map c (real.exp a) Î¸),
    by simpa only [circle_integral, add_sub_cancel', of_real_exp, â† exp_add, smul_smul,
      â† div_eq_mul_inv, mul_div_cancel_left _ (circle_map_ne_center (real.exp_pos _).ne'),
      circle_map_sub_center, deriv_circle_map],
  set R := re â»Â¹' [a, b] âˆ© im â»Â¹' [0, 2 * Ï€],
  set g : â„‚ â†’ â„‚ := (+) c âˆ˜ exp,
  have hdg : differentiable â„‚ g := differentiable_exp.const_add _,
  replace hs : countable (g â»Â¹' s) := (hs.preimage (add_right_injective c)).preimage_cexp,
  have h_maps : maps_to g R A,
  { rintro z âŸ¨h, -âŸ©, simpa [dist_eq, g, abs_exp, hle] using h.symm },
  replace hc : continuous_on (f âˆ˜ g) R, from hc.comp hdg.continuous.continuous_on h_maps,
  replace hd : âˆ€ z âˆˆ re â»Â¹' (Ioo (min a b) (max a b)) âˆ©
    im â»Â¹' (Ioo (min 0 (2 * Ï€)) (max 0 (2 * Ï€))) \ g â»Â¹' s, differentiable_at â„‚ (f âˆ˜ g) z,
  { refine Î» z hz, (hd (g z) âŸ¨_, hz.2âŸ©).comp z (hdg _),
    simpa [g, dist_eq, abs_exp, hle, and.comm] using hz.1.1 },
  simpa [g, circle_map, exp_periodic _, sub_eq_zero, â† exp_add]
    using integral_boundary_rect_eq_zero_of_differentiable_on_off_countable _ âŸ¨a, 0âŸ© âŸ¨b, 2 * Ï€âŸ©
      _ hs hc hd
end

/-- **Cauchy integral formula** for the value at the center of a disc. If `f` is differentiable on a
punctured closed disc of radius `R` and has a limit `y` at the center of the disc, then the integral
$\int_{|z|=R} f(z)\,d(\arg z)=-i\int_{|z|=R}\frac{f(z)\,dz}{z}$ is equal to $2Ï€y`. Moreover, the
same is true if at the points of some countable set, `f` is only continuous, not differentiable. -/
lemma circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable_of_tendsto
  {c : â„‚} {R : â„} (h0 : 0 < R) {f : â„‚ â†’ E} {y : E} {s : set â„‚} (hs : countable s)
  (hc : continuous_on f (closed_ball c R \ {c}))
  (hd : âˆ€ z âˆˆ ball c R \ {c} \ s, differentiable_at â„‚ f z)
  (hy : tendsto f (ğ“[{c}á¶œ] c) (ğ“ y)) :
  âˆ® z in C(c, R), (z - c)â»Â¹ â€¢ f z = (2 * Ï€ * I : â„‚) â€¢ y :=
begin
  rw [â† sub_eq_zero, â† norm_le_zero_iff],
  refine le_of_forall_le_of_dense (Î» Îµ Îµ0, _),
  obtain âŸ¨Î´, Î´0, hÎ´âŸ© :
    âˆƒ Î´ > (0 : â„), âˆ€ z âˆˆ closed_ball c Î´ \ {c}, dist (f z) y < Îµ / (2 * Ï€),
    from ((nhds_within_has_basis nhds_basis_closed_ball _).tendsto_iff nhds_basis_ball).1 hy _
      (div_pos Îµ0 real.two_pi_pos),
  obtain âŸ¨r, hr0, hrÎ´, hrRâŸ© : âˆƒ r, 0 < r âˆ§ r â‰¤ Î´ âˆ§ r â‰¤ R :=
    âŸ¨min Î´ R, lt_min Î´0 h0, min_le_left _ _, min_le_right _ _âŸ©,
  have hsub : closed_ball c R \ ball c r âŠ† closed_ball c R \ {c},
    from diff_subset_diff_right (singleton_subset_iff.2 $ mem_ball_self hr0),
  have hsub' : ball c R \ closed_ball c r âŠ† ball c R \ {c},
    from diff_subset_diff_right (singleton_subset_iff.2 $ mem_closed_ball_self hr0.le),
  have hzne : âˆ€ z âˆˆ sphere c r, z â‰  c,
    from Î» z hz, ne_of_mem_of_not_mem hz (Î» h, hr0.ne' $ dist_self c â–¸ eq.symm h),
  /- The integral `âˆ® z in C(c, r), f z / (z - c)` does not depend on `0 < r â‰¤ R` and tends to
  `2Ï€Iy` as `r â†’ 0`. -/
  calc âˆ¥(âˆ® z in C(c, R), (z - c)â»Â¹ â€¢ f z) - (2 * â†‘Ï€ * I) â€¢ yâˆ¥
      = âˆ¥(âˆ® z in C(c, r), (z - c)â»Â¹ â€¢ f z) - âˆ® z in C(c, r), (z - c)â»Â¹ â€¢ yâˆ¥ :
    begin
      congr' 2,
      { exact circle_integral_sub_center_inv_smul_eq_of_differentiable_on_annulus_off_countable
          hr0 hrR hs (hc.mono hsub) (Î» z hz, hd z âŸ¨hsub' hz.1, hz.2âŸ©) },
      { simp [hr0.ne'] }
    end
  ... = âˆ¥âˆ® z in C(c, r), (z - c)â»Â¹ â€¢ (f z - y)âˆ¥ :
    begin
      simp only [smul_sub],
      have hc' : continuous_on (Î» z, (z - c)â»Â¹) (sphere c r),
        from (continuous_on_id.sub continuous_on_const).invâ‚€ (Î» z hz, sub_ne_zero.2 $ hzne _ hz),
      rw circle_integral.integral_sub; refine (hc'.smul _).circle_integrable hr0.le,
      { exact hc.mono (subset_inter (sphere_subset_closed_ball.trans $
          closed_ball_subset_closed_ball hrR) hzne) },
      { exact continuous_on_const }
    end
  ... â‰¤ 2 * Ï€ * r * (râ»Â¹ * (Îµ / (2 * Ï€))) :
    begin
      refine circle_integral.norm_integral_le_of_norm_le_const hr0.le (Î» z hz, _),
      specialize hzne z hz,
      rw [mem_sphere, dist_eq_norm] at hz,
      rw [norm_smul, normed_field.norm_inv, hz, â† dist_eq_norm],
      refine mul_le_mul_of_nonneg_left (hÎ´ _ âŸ¨_, hzneâŸ©).le (inv_nonneg.2 hr0.le),
      rwa [mem_closed_ball_iff_norm, hz]
    end
  ... = Îµ : by { field_simp [hr0.ne', real.two_pi_pos.ne'], ac_refl }
end

/-- **Cauchy integral formula** for the value at the center of a disc. If `f` is differentiable on a
closed disc of radius `R`, then the integral
$\int_{|z|=R} f(z)\,d(\arg z)=-i\int_{|z|=R}\frac{f(z)\,dz}{z}$ is equal to $2Ï€y`. Moreover, the
same is true if at the points of some countable set, `f` is only continuous, not differentiable. -/
lemma circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable {R : â„} (h0 : 0 < R)
  {f : â„‚ â†’ E} {c : â„‚} {s : set â„‚} (hs : countable s)
  (hc : continuous_on f (closed_ball c R)) (hd : âˆ€ z âˆˆ ball c R \ s, differentiable_at â„‚ f z) :
  âˆ® z in C(c, R), (z - c)â»Â¹ â€¢ f z = (2 * Ï€ * I : â„‚) â€¢ f c :=
circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable_of_tendsto h0 hs
  (hc.mono $ diff_subset _ _) (Î» z hz, hd z âŸ¨hz.1.1, hz.2âŸ©)
  (hc.continuous_at $ closed_ball_mem_nhds _ h0).continuous_within_at

/-- **Cauchy theorem**: the integral of a complex differentiable function over the boundary of a
disc equals zero. Moreover, the same is true if at the points of some countable set, `f` is only
continuous. -/
lemma circle_integral_eq_zero_of_differentiable_on_off_countable {R : â„} (h0 : 0 â‰¤ R) {f : â„‚ â†’ E}
  {c : â„‚} {s : set â„‚} (hs : countable s) (hc : continuous_on f (closed_ball c R))
  (hd : âˆ€ z âˆˆ ball c R \ s, differentiable_at â„‚ f z) :
  âˆ® z in C(c, R), f z = 0 :=
begin
  rcases h0.eq_or_lt with rfl|h0, { apply circle_integral.integral_radius_zero },
  calc âˆ® z in C(c, R), f z = âˆ® z in C(c, R), (z - c)â»Â¹ â€¢ (z - c) â€¢ f z :
    begin
      refine circle_integral.integral_congr h0.le (Î» z hz, (inv_smul_smulâ‚€ (Î» hâ‚€, _) _).symm),
      rw [mem_sphere, dist_eq, hâ‚€, abs_zero] at hz,
      exact h0.ne hz
    end
  ... = (2 * â†‘Ï€ * I : â„‚) â€¢ (c - c) â€¢ f c :
    circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable h0 hs
      ((continuous_on_id.sub continuous_on_const).smul hc)
      (Î» z hz, (differentiable_at_id.sub_const _).smul (hd z hz))
  ... = 0 : by rw [sub_self, zero_smul, smul_zero]
end

/-- **Cauchy integral formula**: if `f : â„‚ â†’ E` is complex differentiable on a closed disc of radius
`R`, then for any `w` in the corresponding open disc we have
$\oint_{|z-c|=R}(z-w)^{-1}f(z)\,dz=2\pi i\,f(w)$.
-/
lemma circle_integral_sub_inv_smul_of_differentiable_on_off_countable_aux {R : â„} {c w : â„‚}
  {f : â„‚ â†’ E} {s : set â„‚} (hs : countable s) (hw : w âˆˆ ball c R \ s)
  (hc : continuous_on f (closed_ball c R)) (hd : âˆ€ x âˆˆ ball c R \ s, differentiable_at â„‚ f x) :
  âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z = (2 * Ï€ * I : â„‚) â€¢ f w :=
begin
  have hR : 0 < R := dist_nonneg.trans_lt hw.1,
  set F : â„‚ â†’ E := update (Î» z, (z - w)â»Â¹ â€¢ (f z - f w)) w (deriv f w),
  have hws : countable (insert w s) := hs.insert _,
  have hnhds : closed_ball c R âˆˆ ğ“ w, from closed_ball_mem_nhds_of_mem hw.1,
  have hcF : continuous_on F (closed_ball c R),
  { refine continuous_on_update_iff.2 âŸ¨_, _âŸ©,
    { refine ((continuous_on_id.sub continuous_on_const).invâ‚€ $ Î» z hz, sub_ne_zero.2 _).smul
        ((hc.mono $ diff_subset _ _).sub continuous_on_const), exact hz.2 },
    { have := has_deriv_at_iff_tendsto_slope.1 (hd _ hw).has_deriv_at,
      exact Î» _, this.mono_left (nhds_within_mono _ (inter_subset_right _ _)) } },
  have hdF : âˆ€ z âˆˆ ball (c : â„‚) R \ (insert w s), differentiable_at â„‚ F z,
  { rintro z âŸ¨hzR, hzwsâŸ©,
    rw [mem_insert_iff, not_or_distrib] at hzws,
    refine (((differentiable_at_id.sub_const w).inv $ sub_ne_zero.2 hzws.1).smul
      ((hd z âŸ¨hzR, hzws.2âŸ©).sub_const (f w))).congr_of_eventually_eq _,
    filter_upwards [is_open_ne.mem_nhds hzws.1],
    exact Î» x hx, update_noteq hx _ _ },
  have HI := circle_integral_eq_zero_of_differentiable_on_off_countable hR.le hws hcF hdF,
  have hne : âˆ€ z âˆˆ sphere c R, z â‰  w, from Î» z hz, ne_of_mem_of_not_mem hz (ne_of_lt hw.1),
  have hFeq : eq_on F (Î» z, (z - w)â»Â¹ â€¢ f z - (z - w)â»Â¹ â€¢ f w) (sphere c R),
  { intros z hz,
    calc F z = (z - w)â»Â¹ â€¢ (f z - f w) : update_noteq (hne z hz) _ _
    ... = (z - w)â»Â¹ â€¢ f z - (z - w)â»Â¹ â€¢ f w : smul_sub _ _ _ },
  have hc' : continuous_on (Î» z, (z - w)â»Â¹) (sphere c R),
    from (continuous_on_id.sub continuous_on_const).invâ‚€ (Î» z hz, sub_ne_zero.2 $ hne z hz),
  rw [â† circle_integral.integral_sub_inv_of_mem_ball hw.1, â† circle_integral.integral_smul_const,
    â† sub_eq_zero, â† circle_integral.integral_sub, â† circle_integral.integral_congr hR.le hFeq, HI],
  exacts [(hc'.smul (hc.mono sphere_subset_closed_ball)).circle_integrable hR.le,
    (hc'.smul continuous_on_const).circle_integrable hR.le]
end

lemma two_pi_I_inv_smul_circle_integral_sub_inv_smul_of_differentiable_on_off_countable
  {R : â„} {c w : â„‚} {f : â„‚ â†’ E} {s : set â„‚} (hs : countable s) (hw : w âˆˆ ball c R)
  (hc : continuous_on f (closed_ball c R)) (hd : âˆ€ x âˆˆ ball c R \ s, differentiable_at â„‚ f x) :
  (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z = f w :=
begin
  have hR : 0 < R := dist_nonneg.trans_lt hw,
  suffices : w âˆˆ closure (ball c R \ s),
  { lift R to â„â‰¥0 using hR.le,
    have A : continuous_at (Î» w, (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z) w,
    { have := has_fpower_series_on_cauchy_integral
      ((hc.mono sphere_subset_closed_ball).circle_integrable R.coe_nonneg) hR,
      refine this.continuous_on.continuous_at (emetric.is_open_ball.mem_nhds _),
      rwa metric.emetric_ball_nnreal },
    have B : continuous_at f w, from hc.continuous_at (closed_ball_mem_nhds_of_mem hw),
    refine tendsto_nhds_unique_of_frequently_eq A B ((mem_closure_iff_frequently.1 this).mono _),
    intros z hz,
    rw [circle_integral_sub_inv_smul_of_differentiable_on_off_countable_aux hs hz hc hd,
      inv_smul_smulâ‚€],
    simp [real.pi_ne_zero, I_ne_zero] },
  refine mem_closure_iff_nhds.2 (Î» t ht, _),
  -- TODO: generalize to any vector space over `â„`
  set g : â„ â†’ â„‚ := Î» x, w + x,
  have : tendsto g (ğ“ 0) (ğ“ w),
    from (continuous_const.add continuous_of_real).tendsto' 0 w (add_zero _),
  rcases mem_nhds_iff_exists_Ioo_subset.1 (this $ inter_mem ht $ is_open_ball.mem_nhds hw)
    with âŸ¨l, u, hluâ‚€, hlu_subâŸ©,
  obtain âŸ¨x, hxâŸ© : (Ioo l u \ g â»Â¹' s).nonempty,
  { refine nonempty_diff.2 (Î» hsub, _),
    have : countable (Ioo l u),
      from (hs.preimage ((add_right_injective w).comp of_real_injective)).mono hsub,
    rw [â† cardinal.mk_set_le_omega, cardinal.mk_Ioo_real (hluâ‚€.1.trans hluâ‚€.2)] at this,
    exact this.not_lt cardinal.omega_lt_continuum },
  exact âŸ¨g x, (hlu_sub hx.1).1, (hlu_sub hx.1).2, hx.2âŸ©
end

lemma circle_integral_sub_inv_smul_of_differentiable_on_off_countable
  {R : â„} {c w : â„‚} {f : â„‚ â†’ E} {s : set â„‚} (hs : countable s) (hw : w âˆˆ ball c R)
  (hc : continuous_on f (closed_ball c R)) (hd : âˆ€ x âˆˆ ball c R \ s, differentiable_at â„‚ f x) :
  âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z = (2 * Ï€ * I : â„‚) â€¢ f w :=
by { rw [â† two_pi_I_inv_smul_circle_integral_sub_inv_smul_of_differentiable_on_off_countable
  hs hw hc hd, smul_inv_smulâ‚€], simp [real.pi_ne_zero, I_ne_zero] }

/-- **Cauchy integral formula**: if `f : â„‚ â†’ â„‚` is complex differentiable on a closed disc of radius
`R`, then for any `w` in the corresponding open disc we have
$\oint_{|z-c|=R}\frac{f(z)}{z-w}dz=2\pi i\,f(w)$. -/
lemma circle_integral_div_sub_of_differentiable_on_off_countable {R : â„} {c w : â„‚} {s : set â„‚}
  (hs : countable s) (hw : w âˆˆ ball c R) {f : â„‚ â†’ â„‚} (hc : continuous_on f (closed_ball c R))
  (hd : âˆ€ z âˆˆ ball c R \ s, differentiable_at â„‚ f z) :
  âˆ® z in C(c, R), f z / (z - w) = 2 * Ï€ * I * f w :=
by simpa only [smul_eq_mul, div_eq_inv_mul]
  using circle_integral_sub_inv_smul_of_differentiable_on_off_countable hs hw hc hd

lemma has_fpower_series_on_ball_of_differentiable_off_countable {R : â„â‰¥0} {c : â„‚} {f : â„‚ â†’ E}
  {s : set â„‚} (hs : countable s) (hc : continuous_on f (closed_ball c R))
  (hd : âˆ€ z âˆˆ ball c R \ s, differentiable_at â„‚ f z) (hR : 0 < R) :
  has_fpower_series_on_ball f (cauchy_power_series f c R) c R :=
{ r_le := le_radius_cauchy_power_series _ _ _,
  r_pos := ennreal.coe_pos.2 hR,
  has_sum := Î» w hw,
    begin
      have hw' : c + w âˆˆ ball c R,
        by simpa only [add_mem_ball_iff_norm, â† coe_nnnorm, mem_emetric_ball_zero_iff,
          nnreal.coe_lt_coe, ennreal.coe_lt_coe] using hw,
      rw â† two_pi_I_inv_smul_circle_integral_sub_inv_smul_of_differentiable_on_off_countable hs
        hw' hc hd,
      exact (has_fpower_series_on_cauchy_integral
        ((hc.mono sphere_subset_closed_ball).circle_integrable R.2) hR).has_sum hw
    end }

/-- If `f : â„‚ â†’ E` is complex differentiable on a closed disc of positive radius, then it is
analytic on the corresponding open disc, and the coefficients of the power series are given by
Cauchy integral formulas. -/
protected lemma _root_.differentiable_on.has_fpower_series_on_ball {R : â„â‰¥0} {c : â„‚} {f : â„‚ â†’ E}
  (hd : differentiable_on â„‚ f (closed_ball c R)) (hR : 0 < R) :
  has_fpower_series_on_ball f (cauchy_power_series f c R) c R :=
has_fpower_series_on_ball_of_differentiable_off_countable countable_empty hd.continuous_on
  (Î» z hz, hd.differentiable_at $ closed_ball_mem_nhds_of_mem hz.1) hR

/-- If `f : â„‚ â†’ E` is complex differentiable on some set `s`, then it is analytic at any point `z`
such that `s âˆˆ ğ“ z` (equivalently, `z âˆˆ interior s`). -/
protected lemma _root_.differentiable_on.analytic_at {s : set â„‚} {f : â„‚ â†’ E} {z : â„‚}
  (hd : differentiable_on â„‚ f s) (hz : s âˆˆ ğ“ z) : analytic_at â„‚ f z :=
begin
  rcases nhds_basis_closed_ball.mem_iff.1 hz with âŸ¨R, hR0, hRsâŸ©,
  lift R to â„â‰¥0 using hR0.le,
  exact ((hd.mono hRs).has_fpower_series_on_ball hR0).analytic_at
end

/-- A complex differentiable function `f : â„‚ â†’ E` is analytic at every point. -/
protected lemma differentiable.analytic_at {f : â„‚ â†’ E} (hf : differentiable â„‚ f) (z : â„‚) :
  analytic_at â„‚ f z :=
hf.differentiable_on.analytic_at univ_mem

end complex

