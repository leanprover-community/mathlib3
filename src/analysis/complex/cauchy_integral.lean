/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import measure_theory.measure.complex_lebesgue
import measure_theory.integral.divergence_theorem
import measure_theory.integral.circle_integral
import analysis.calculus.fderiv_analytic

/-!
# Cauchy integral formula

In this file we prove Cauchy theorem and Cauchy integral formula.
-/

open topological_space set measure_theory interval_integral metric filter function
open_locale interval real nnreal ennreal topological_space big_operators

noncomputable theory

universes u

variables {E : Type u} [normed_group E] [normed_space â„‚ E] [measurable_space E] [borel_space E]
  [second_countable_topology E] [complete_space E]

namespace complex

/-- Suppose that a function `f : â„‚ â†’ E` be is real differentiable on a rectangle with vertices
`z w : â„‚` and $\frac{\partial f}{\partial \bar z}$ is integrable on this rectangle. Then
the integral of `f` over the boundary of the rectangle is equal to the integral of
$2i\frac{\partial f}{\partial \bar z}=i\frac{\partial f}{\partial x}-\frac{\partial f}{\partial y}$
over the rectangle.

Moreover, the same is true if `f` is only differentiable at points outside of a countable set `s`
and is continuous at the points of this set. -/
lemma integral_boundary_rect_of_has_fderiv_within_at_real_off_countable (f : â„‚ â†’ E)
  (f' : â„‚ â†’ â„‚ â†’L[â„] E) (z w : â„‚) (s : set â„‚) (hs : countable s)
  (Hc : âˆ€ x âˆˆ s, continuous_within_at f (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]) x)
  (Hd : âˆ€ x âˆˆ (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]) \ s, has_fderiv_within_at f (f' x)
    (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]) x)
  (Hi : integrable_on (Î» z, I â€¢ f' z 1 - f' z I) (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im])) :
  (âˆ« x : â„ in z.re..w.re, f (x + z.im * I)) - (âˆ« x : â„ in z.re..w.re, f (x + w.im * I)) +
    (I â€¢ âˆ« y : â„ in z.im..w.im, f (re w + y * I)) - I â€¢ âˆ« y : â„ in z.im..w.im, f (re z + y * I) =
    âˆ« x : â„ in z.re..w.re, âˆ« y : â„ in z.im..w.im, I â€¢ f' (x + y * I) 1 - f' (x + y * I) I :=
begin
  set e : (â„ Ã— â„) â‰ƒL[â„] â„‚ := equiv_real_prodâ‚—.symm,
  have he : âˆ€ x y : â„, â†‘x + â†‘y * I = e (x, y), from Î» x y, (mk_eq_add_mul_I x y).symm,
  have heâ‚ : e (1, 0) = 1 := rfl, have heâ‚‚ : e (0, 1) = I := rfl,
  simp only [he] at *,
  set F : (â„ Ã— â„) â†’ E := f âˆ˜ e,
  set F' : (â„ Ã— â„) â†’ (â„ Ã— â„) â†’L[â„] E := Î» p, (f' (e p)).comp (e : (â„ Ã— â„) â†’L[â„] â„‚),
  have hF' : âˆ€ p : â„ Ã— â„, (-(I â€¢ F' p)) (1, 0) + F' p (0, 1) = -(I â€¢ f' (e p) 1 - f' (e p) I),
  { rintro âŸ¨x, yâŸ©, simp [F', heâ‚, heâ‚‚, â† sub_eq_neg_add], },
  set R : set (â„ Ã— â„) := [z.re, w.re].prod [w.im, z.im],
  set t : set (â„ Ã— â„) := e â»Â¹' s,
  rw [interval_swap z.im] at Hc Hd Hi,
  have hR : e â»Â¹' (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [w.im, z.im]) = R := rfl,
  have htc : âˆ€ p âˆˆ t, continuous_within_at F R p,
    from Î» p hp, (Hc (e p) hp).comp e.continuous_within_at hR.ge,
  have htd : âˆ€ p âˆˆ R \ t, has_fderiv_within_at F (F' p) R p,
    from Î» p hp, (Hd (e p) hp).comp p e.has_fderiv_within_at hR.ge,
  simp_rw [â† interval_integral.integral_smul, interval_integral.integral_symm w.im z.im,
    â† interval_integral.integral_neg, â† hF'],
  refine (integral2_divergence_prod_of_has_fderiv_within_at_off_countable
      (Î» p, -(I â€¢ F p)) F (Î» p, - (I â€¢ F' p)) F' z.re w.im w.re z.im t (hs.preimage e.injective)
      (Î» p hp, (continuous_within_at_const.smul (htc p hp)).neg) htc
      (Î» p hp, ((htd p hp).const_smul I).neg) htd _).symm,
  rw [â† volume_preserving_equiv_real_prod.symm.integrable_on_comp_preimage
    (measurable_equiv.measurable_embedding _)] at Hi,
  simpa only [hF'] using Hi.neg
end

/-- **Cauchy theorem**: the integral of a complex differentiable function over the boundary of a
rectangle equals zero.

Moreover, the same is true if `f` is only differentiable at points outside of a countable set `s`
and is continuous at the points of this set. -/
lemma integral_boundary_rect_eq_zero_of_differentiable_on_off_countable (f : â„‚ â†’ E)
  (z w : â„‚) (s : set â„‚) (hs : countable s)
  (Hc : âˆ€ x âˆˆ s, continuous_within_at f (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]) x)
  (Hd : âˆ€ x âˆˆ (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]) \ s, differentiable_within_at â„‚ f
    (re â»Â¹' [z.re, w.re] âˆ© im â»Â¹' [z.im, w.im]) x) :
  (âˆ« x : â„ in z.re..w.re, f (x + z.im * I)) - (âˆ« x : â„ in z.re..w.re, f (x + w.im * I)) +
    (I â€¢ âˆ« y : â„ in z.im..w.im, f (re w + y * I)) -
      I â€¢ âˆ« y : â„ in z.im..w.im, f (re z + y * I) = 0 :=
by refine (integral_boundary_rect_of_has_fderiv_within_at_real_off_countable f
  (Î» z, (fderiv_within â„‚ f _ z).restrict_scalars â„) z w s hs Hc
  (Î» x hx, (Hd x hx).has_fderiv_within_at.restrict_scalars â„) _).trans _;
    simp [â† continuous_linear_map.map_smul]

/-- If `f : â„‚ â†’ E` is complex differentiable on the closed annulus `r â‰¤ âˆ¥z - zâ‚€âˆ¥ â‰¤ R`, `0 < r â‰¤ R`,
then the integrals of `f z / (z - zâ‚€)` (formally, `(z - zâ‚€)â»Â¹ â€¢ f z`) over the circles
`âˆ¥z - zâ‚€âˆ¥ = r` and `âˆ¥z - zâ‚€âˆ¥ = R` are equal to each other.

Moreover, the same is true if `f` is differentiable at points of the annulus outside of a countable
set `s` and is continuous at points of this set.  -/
lemma circle_integral_sub_center_inv_smul_eq_of_differentiable_on_annulus_off_countable
  {zâ‚€ : â„‚} {r R : â„} (h0 : 0 < r) (hle : r â‰¤ R) {f : â„‚ â†’ E} {s : set â„‚} (hs : countable s)
  (hc : âˆ€ z âˆˆ s, continuous_within_at f (closed_ball zâ‚€ R \ ball zâ‚€ r) z)
  (hd : âˆ€ z âˆˆ (closed_ball zâ‚€ R \ ball zâ‚€ r) \ s,
    differentiable_within_at â„‚ f (closed_ball zâ‚€ R \ ball zâ‚€ r) z) :
  âˆ® z in C(zâ‚€, R), (z - zâ‚€)â»Â¹ â€¢ f z = âˆ® z in C(zâ‚€, r), (z - zâ‚€)â»Â¹ â€¢ f z :=
begin
  set A := closed_ball zâ‚€ R \ ball zâ‚€ r,
  obtain âŸ¨a, rflâŸ© : âˆƒ a, real.exp a = r, from âŸ¨real.log r, real.exp_log h0âŸ©,
  obtain âŸ¨b, rflâŸ© : âˆƒ b, real.exp b = R, from âŸ¨real.log R, real.exp_log (h0.trans_le hle)âŸ©,
  rw [real.exp_le_exp] at hle,
  simp only [circle_integral, add_sub_cancel', of_real_exp, â† exp_add, smul_smul,
    â† div_eq_mul_inv, mul_div_cancel_left _ (exp_ne_zero _)],
  set R := re â»Â¹' [a, b] âˆ© im â»Â¹' [0, 2 * Ï€],
  set g : â„‚ â†’ â„‚ := (+) zâ‚€ âˆ˜ exp,
  have hdg : âˆ€ {z t}, differentiable_within_at â„‚ g t z :=
    Î» z t, differentiable_at_exp.differentiable_within_at.const_add _,
  replace hs : countable (g â»Â¹' s) := (hs.preimage (add_right_injective zâ‚€)).preimage_cexp,
  have h_maps : maps_to g R A,
  { rintro z âŸ¨h, -âŸ©, simpa [dist_eq, g, abs_exp, hle] using h.symm },
  replace hc : âˆ€ z âˆˆ g â»Â¹' s, continuous_within_at (f âˆ˜ g) R z,
    from Î» z hz, (hc (g z) hz).comp hdg.continuous_within_at h_maps,
  replace hd : âˆ€ z âˆˆ R \ g â»Â¹' s, differentiable_within_at â„‚ (f âˆ˜ g) R z,
  { intros z hz,
    exact (hd (g z) âŸ¨h_maps hz.1, hz.2âŸ©).comp z hdg h_maps },
  simp only [circle_map_sub_center, deriv_circle_map,
    mul_div_cancel_left _ (circle_map_ne_center (real.exp_ne_zero _))],
  simpa [g, circle_map, exp_periodic _, sub_eq_zero, â† exp_add]
    using integral_boundary_rect_eq_zero_of_differentiable_on_off_countable _ âŸ¨a, 0âŸ© âŸ¨b, 2 * Ï€âŸ©
      _ hs hc hd
end

/-- **Cauchy integral formula** for the value at the center of a disc. If `f` is differentiable on a
punctured closed disc of radius `R` and has a limit `y` at the center of the disc, then the integral
$\int_{|z|=R} f(z)\,d(\arg z)=-i\int_{|z|=R}\frac{f(z)\,dz}{z}$ is equal to $2Ï€y`. Moreover, the
same is true if at the points of some countable set, `f` is only continuous, not differentiable. -/
lemma circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable_of_tendsto
  {zâ‚€ : â„‚} {R : â„} (h0 : 0 < R) {f : â„‚ â†’ E} {y : E} {s : set â„‚} (hs : countable s)
  (hc : âˆ€ z âˆˆ s, continuous_within_at f (closed_ball zâ‚€ R \ {zâ‚€}) z)
  (hd : âˆ€ z âˆˆ closed_ball zâ‚€ R \ {zâ‚€} \ s,
    differentiable_within_at â„‚ f (closed_ball zâ‚€ R \ {zâ‚€}) z)
  (hy : tendsto f (ğ“[{zâ‚€}á¶œ] zâ‚€) (ğ“ y)) :
  âˆ® z in C(zâ‚€, R), (z - zâ‚€)â»Â¹ â€¢ f z = (2 * Ï€ * I : â„‚) â€¢ y :=
begin
  rw [â† sub_eq_zero, â† norm_le_zero_iff],
  refine le_of_forall_le_of_dense (Î» Îµ Îµ0, _),
  obtain âŸ¨Î´, Î´0, hÎ´âŸ© :
    âˆƒ Î´ > (0 : â„), âˆ€ z âˆˆ closed_ball zâ‚€ Î´ \ {zâ‚€}, dist (f z) y < Îµ / (2 * Ï€),
    from ((nhds_within_has_basis nhds_basis_closed_ball _).tendsto_iff nhds_basis_ball).1 hy _
      (div_pos Îµ0 real.two_pi_pos),
  obtain âŸ¨r, hr0, hrÎ´, hrRâŸ© : âˆƒ r, 0 < r âˆ§ r â‰¤ Î´ âˆ§ r â‰¤ R :=
    âŸ¨min Î´ R, lt_min Î´0 h0, min_le_left _ _, min_le_right _ _âŸ©,
  have hsub : closed_ball zâ‚€ R \ ball zâ‚€ r âŠ† closed_ball zâ‚€ R \ {zâ‚€},
    from diff_subset_diff_right (singleton_subset_iff.2 $ mem_ball_self hr0),
  have hzne : âˆ€ z âˆˆ sphere zâ‚€ r, z â‰  zâ‚€,
  { rintro z hz rfl,
    apply hr0.ne,
    rwa [mem_sphere, dist_self] at hz },
  calc âˆ¥(âˆ® z in C(zâ‚€, R), (z - zâ‚€)â»Â¹ â€¢ f z) - (2 * â†‘Ï€ * I) â€¢ yâˆ¥
      = âˆ¥(âˆ® z in C(zâ‚€, r), (z - zâ‚€)â»Â¹ â€¢ f z) - âˆ® z in C(zâ‚€, r), (z - zâ‚€)â»Â¹ â€¢ yâˆ¥ :
    begin
      congr' 2,
      { refine circle_integral_sub_center_inv_smul_eq_of_differentiable_on_annulus_off_countable
          hr0 hrR hs _ _,
        exacts [Î» z hz, (hc z hz).mono hsub, Î» z hz, (hd z âŸ¨hsub hz.1, hz.2âŸ©).mono hsub] },
      { simp [hr0.ne'] }
    end
  ... = âˆ¥âˆ® z in C(zâ‚€, r), (z - zâ‚€)â»Â¹ â€¢ (f z - y)âˆ¥ :
    begin
      simp only [smul_sub],
      have hc' : continuous_on (Î» z, (z - zâ‚€)â»Â¹) (sphere zâ‚€ r),
        from (continuous_on_id.sub continuous_on_const).invâ‚€ (Î» z hz, sub_ne_zero.2 $ hzne _ hz),
      rw circle_integral.integral_sub; refine (hc'.smul _).circle_integrable hr0.le,
      { have H : sphere zâ‚€ r âŠ† closed_ball zâ‚€ R \ {zâ‚€},
        { refine Î» z hz, âŸ¨_, hzne z hzâŸ©,
          rw [mem_sphere, dist_eq_norm] at hz,
          rwa [mem_closed_ball_iff_norm, hz] },
        intros z hz,
        by_cases hzs : z âˆˆ s,
        exacts [(hc z hzs).mono H, (hd z âŸ¨H hz, hzsâŸ©).continuous_within_at.mono H] },
      { exact continuous_on_const }
    end
  ... â‰¤ 2 * Ï€ * r * (râ»Â¹ * (Îµ / (2 * Ï€))) :
    begin
      refine circle_integral.norm_integral_le_of_norm_le_const hr0.le (Î» z hz, _),
      specialize hzne z hz,
      rw [mem_sphere, dist_eq_norm] at hz,
      rw [norm_smul, normed_field.norm_inv, hz, â† dist_eq_norm],
      refine mul_le_mul_of_nonneg_left (hÎ´ _ âŸ¨_, hzneâŸ©).le (inv_nonneg.2 hr0.le),
      rwa [mem_closed_ball_iff_norm, hz]
    end
  ... = Îµ : by { field_simp [hr0.ne', real.two_pi_pos.ne'], ac_refl }
end

/-- **Cauchy integral formula** for the value at the center of a disc. If `f` is differentiable on a
closed disc of radius `R`, then the integral
$\int_{|z|=R} f(z)\,d(\arg z)=-i\int_{|z|=R}\frac{f(z)\,dz}{z}$ is equal to $2Ï€y`. Moreover, the
same is true if at the points of some countable set, `f` is only continuous, not differentiable. -/
lemma circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable {R : â„} (h0 : 0 < R)
  {f : â„‚ â†’ E} {zâ‚€ : â„‚} {s : set â„‚} (hs : countable s)
  (hc : âˆ€ x âˆˆ s, continuous_within_at f (closed_ball zâ‚€ R) x)
  (hd : âˆ€ z âˆˆ closed_ball zâ‚€ R \ s, differentiable_within_at â„‚ f (closed_ball zâ‚€ R) z) :
  âˆ® z in C(zâ‚€, R), (z - zâ‚€)â»Â¹ â€¢ f z = (2 * Ï€ * I : â„‚) â€¢ f zâ‚€ :=
begin
  refine circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable_of_tendsto h0 hs
    (Î» z hz, (hc z hz).mono $ diff_subset _ _)
    (Î» z hz, (hd z âŸ¨hz.1.1, hz.2âŸ©).mono $ diff_subset _ _) _,
  suffices : continuous_within_at f (closed_ball zâ‚€ R) zâ‚€,
    from (this.continuous_at (closed_ball_mem_nhds _ h0)).continuous_within_at,
  by_cases h : (zâ‚€ : â„‚) âˆˆ s,
  exacts [hc _ h, (hd _ âŸ¨mem_closed_ball_self h0.le, hâŸ©).continuous_within_at]
end

/-- **Cauchy theorem**: the integral of a complex differentiable function over the boundary of a
disc equals zero. Moreover, the same is true if at the points of some countable set, `f` is only
continuous. -/
lemma circle_integral_eq_zero_of_differentiable_on_off_countable {R : â„} (h0 : 0 â‰¤ R) {f : â„‚ â†’ E}
  {zâ‚€ : â„‚}
  {s : set â„‚} (hs : countable s) (hc : âˆ€ x âˆˆ s, continuous_within_at f (closed_ball zâ‚€ R) x)
  (hd : âˆ€ z âˆˆ closed_ball zâ‚€ R \ s, differentiable_within_at â„‚ f (closed_ball zâ‚€ R) z) :
  âˆ® z in C(zâ‚€, R), f z = 0 :=
begin
  rcases h0.eq_or_lt with rfl|h0, { apply circle_integral.integral_radius_zero },
  calc âˆ® z in C(zâ‚€, R), f z = âˆ® z in C(zâ‚€, R), (z - zâ‚€)â»Â¹ â€¢ (z - zâ‚€) â€¢ f z :
    begin
      refine circle_integral.integral_congr h0.le (Î» z hz, (inv_smul_smulâ‚€ (Î» hâ‚€, _) _).symm),
      rw [mem_sphere, dist_eq, hâ‚€, abs_zero] at hz,
      exact h0.ne hz
    end
  ... = (2 * â†‘Ï€ * I : â„‚) â€¢ (zâ‚€ - zâ‚€) â€¢ f zâ‚€ :
    circle_integral_sub_center_inv_smul_of_differentiable_on_off_countable h0 hs
      (Î» z hz, (continuous_within_at_id.sub continuous_within_at_const).smul (hc z hz))
      (Î» z hz, (differentiable_within_at_id.sub_const _).smul (hd z hz))
  ... = 0 : by rw [sub_self, zero_smul, smul_zero]
end
    
lemma circle_integral_sub_inv_smul_of_differentiable_on {R : â„} {c w : â„‚} (hw : w âˆˆ ball c R)
  {f : â„‚ â†’ E} (hd : differentiable_on â„‚ f (closed_ball c R)) :
  âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z = (2 * Ï€ * I : â„‚) â€¢ f w :=
begin
  have hR : 0 < R := dist_nonneg.trans_lt hw,
  set F : â„‚ â†’ E := update (Î» z, (z - w)â»Â¹ â€¢ (f z - f w)) w (deriv f w),
  set s : set â„‚ := {w},
  have hnhds : closed_ball c R âˆˆ ğ“ w, from closed_ball_mem_nhds_of_mem hw,
  have hc : âˆ€ z âˆˆ s, continuous_within_at F (closed_ball c R) z,
  { rintro z (rfl|_),
    have := has_deriv_at_iff_tendsto_slope.1 (hd.has_deriv_at hnhds),
    rw continuous_within_at_update_same,
    exact this.mono_left (nhds_within_mono _ (inter_subset_right _ _)) },
  have hdF : âˆ€ z âˆˆ closed_ball (c : â„‚) R \ s, differentiable_within_at â„‚ F (closed_ball c R) z,
  { rintro z âŸ¨hzR, hzw : z â‰  wâŸ©,
    refine (((differentiable_within_at_id.sub_const w).inv $ sub_ne_zero.2 hzw).smul
      ((hd z hzR).sub_const (f w))).congr_of_eventually_eq _ _,
    { filter_upwards [mem_nhds_within_of_mem_nhds (is_open_ne.mem_nhds hzw)],
      exact Î» x hx, update_noteq hx _ _ },
    { exact update_noteq hzw _ _ } },
  have HI := circle_integral_eq_zero_of_differentiable_on_off_countable hR.le
    (countable_singleton w) hc hdF,
  have hne : âˆ€ z âˆˆ sphere c R, z â‰  w, from Î» z hz, ne_of_mem_of_not_mem hz (ne_of_lt hw),
  have hFeq : eq_on F (Î» z, (z - w)â»Â¹ â€¢ f z - (z - w)â»Â¹ â€¢ f w) (sphere c R),
  { intros z hz,
    calc F z = (z - w)â»Â¹ â€¢ (f z - f w) : update_noteq (hne z hz) _ _
    ... = (z - w)â»Â¹ â€¢ f z - (z - w)â»Â¹ â€¢ f w : smul_sub _ _ _ },
  have hc : continuous_on (Î» z, (z - w)â»Â¹) (sphere c R),
    from (continuous_on_id.sub continuous_on_const).invâ‚€ (Î» z hz, sub_ne_zero.2 $ hne z hz),
  rw [â† circle_integral.integral_sub_inv_of_mem_ball hw, â† circle_integral.integral_smul_const,
    â† sub_eq_zero, â† circle_integral.integral_sub, â† circle_integral.integral_congr hR.le hFeq, HI],
  exacts [(hc.smul (hd.continuous_on.mono sphere_subset_closed_ball)).circle_integrable hR.le,
    (hc.smul continuous_on_const).circle_integrable hR.le]
end

lemma circle_integral_div_sub_of_differentiable_on {R : â„} {c w : â„‚} (hw : w âˆˆ ball c R)
  {f : â„‚ â†’ â„‚} (hd : differentiable_on â„‚ f (closed_ball c R)) :
  âˆ® z in C(c, R), f z / (z - w) = 2 * Ï€ * I * f w :=
by simpa only [smul_eq_mul, div_eq_inv_mul]
  using circle_integral_sub_inv_smul_of_differentiable_on hw hd

protected lemma _root_.differentiable_on.has_fpower_series_on_ball {R : â„â‰¥0} {c : â„‚} {f : â„‚ â†’ E}
  (hd : differentiable_on â„‚ f (closed_ball c R)) (hR : 0 < R) :
  has_fpower_series_on_ball f (cauchy_power_series f c R) c R :=
{ r_le := le_radius_cauchy_power_series _ _ _,
  r_pos := ennreal.coe_pos.2 hR,
  has_sum := Î» w hw,
    begin
      have hw' : c + w âˆˆ ball c R,
        by simpa only [add_mem_ball_iff_norm, â† coe_nnnorm, mem_emetric_ball_zero_iff,
          nnreal.coe_lt_coe, ennreal.coe_lt_coe] using hw,
      convert (has_fpower_series_on_cauchy_integral _ hR).has_sum hw,
      { rw [circle_integral_sub_inv_smul_of_differentiable_on hw' hd, inv_smul_smulâ‚€],
        simp [real.pi_ne_zero, I_ne_zero] },
      { exact (hd.mono sphere_subset_closed_ball).continuous_on.circle_integrable R.2 }
    end }

protected lemma _root_.differentiable_on.analytic_at {s : set â„‚} {f : â„‚ â†’ E} {z : â„‚}
  (hd : differentiable_on â„‚ f s) (hz : s âˆˆ ğ“ z) : analytic_at â„‚ f z :=
begin
  rcases nhds_basis_closed_ball.mem_iff.1 hz with âŸ¨R, hR0, hRsâŸ©,
  lift R to â„â‰¥0 using hR0.le,
  exact ((hd.mono hRs).has_fpower_series_on_ball hR0).analytic_at
end

protected lemma differentiable.analytic_at {f : â„‚ â†’ E} (hf : differentiable â„‚ f) (z : â„‚) :
  analytic_at â„‚ f z :=
hf.differentiable_on.analytic_at univ_mem

/-- If `f` is complex differentiable on a closed disc with center `c` and radius `R > 0`, then
`f' c` can be represented as an integral over the corresponding circle.

TODO: add a version for `w âˆˆ metric.ball c R`.

TODO: add a version for higher derivatives. -/
lemma deriv_eq_smul_circle_integral {R : â„} {c : â„‚} {f : â„‚ â†’ E} (hR : 0 < R)
  (hd : differentiable_on â„‚ f (closed_ball c R)) :
  deriv f c = (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - c) ^ (-2 : â„¤) â€¢ f z :=
begin
  lift R to â„â‰¥0 using hR.le,
  refine (hd.has_fpower_series_on_ball hR).has_fpower_series_at.deriv.trans _,
  simp only [cauchy_power_series_apply, one_div, zpow_negâ‚€, pow_one, smul_smul,
    zpow_two, mul_invâ‚€]
end

/-- If `f` is complex differentiable on a closed disc of radius `R`, and its values on the boundary
circle of this disc are bounded from above by `C`, then the norm of its derivative at the center
is at most `C / R`. -/
lemma norm_deriv_le_of_forall_mem_sphere_norm_le {c : â„‚} {R C : â„} {f : â„‚ â†’ E} (hR : 0 < R)
  (hd : differentiable_on â„‚ f (closed_ball c R)) (hC : âˆ€ z âˆˆ sphere c R, âˆ¥f zâˆ¥ â‰¤ C) :
  âˆ¥deriv f câˆ¥ â‰¤ C / R :=
have âˆ€ z âˆˆ sphere c R, âˆ¥(z - c) ^ (-2 : â„¤) â€¢ f zâˆ¥ â‰¤ C / (R * R),
  from Î» z (hz : abs (z - c) = R), by simpa [norm_smul, hz, zpow_two, â† div_eq_inv_mul]
    using (div_le_div_right (mul_pos hR hR)).2 (hC z hz),
calc âˆ¥deriv f câˆ¥ = âˆ¥(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - c) ^ (-2 : â„¤) â€¢ f zâˆ¥ :
  congr_arg norm (deriv_eq_smul_circle_integral hR hd)
... â‰¤ R * (C / (R * R)) :
  circle_integral.norm_two_pi_I_inv_smul_integral_le_of_norm_le_const hR.le this
... = C / R : by rw [mul_div_comm, div_self_mul_self', div_eq_mul_inv]

/-- A complex differentiable bounded function is a constant. -/
lemma apply_eq_apply_of_differentiable_of_bounded {f : â„‚ â†’ E} (hf : differentiable â„‚ f)
  (hb : bounded (range f)) (z w : â„‚) : f z = f w :=
begin
  suffices : âˆ€ c, deriv f c = 0, from is_const_of_deriv_eq_zero hf this z w,
  clear z w, intro c,
  obtain âŸ¨C, Câ‚€, hCâŸ© : âˆƒ C > (0 : â„), âˆ€ z, âˆ¥f zâˆ¥ â‰¤ C,
  { rcases bounded_iff_forall_norm_le.1 hb with âŸ¨C, hCâŸ©,
    exact âŸ¨max C 1, lt_max_iff.2 (or.inr zero_lt_one),
      Î» z, (hC (f z) (mem_range_self _)).trans (le_max_left _ _)âŸ© },
  refine norm_le_zero_iff.1 (le_of_forall_le_of_dense $ Î» Îµ Îµâ‚€, _),
  calc âˆ¥deriv f câˆ¥ â‰¤ C / (C / Îµ) :
    norm_deriv_le_of_forall_mem_sphere_norm_le (div_pos Câ‚€ Îµâ‚€) hf.differentiable_on (Î» z _, hC z)
  ... = Îµ : div_div_cancel' Câ‚€.lt.ne'
end

/-- A complex differentiable bounded function is a constant. -/
lemma exists_const_forall_eq_of_differentiable_of_bounded {f : â„‚ â†’ E} (hf : differentiable â„‚ f)
  (hb : bounded (range f)) : âˆƒ c, âˆ€ z, f z = c :=
âŸ¨f 0, Î» z, apply_eq_apply_of_differentiable_of_bounded hf hb _ _âŸ©

/-- A complex differentiable bounded function is a constant. -/
lemma exists_eq_const_of_differentiable_of_bounded {f : â„‚ â†’ E} (hf : differentiable â„‚ f)
  (hb : bounded (range f)) : âˆƒ c, f = const â„‚ c :=
(exists_const_forall_eq_of_differentiable_of_bounded hf hb).imp $ Î» c, funext

lemma norm_eq_norm_of_differentiable_on_of_is_max_on_closed_ball_of_mem_closed_ball {f : â„‚ â†’ E}
  {c w : â„‚} {R : â„} (hd : differentiable_on â„‚ f (closed_ball c R))
  (hn : is_max_on (norm âˆ˜ f) (closed_ball c R) c) (hw : w âˆˆ closed_ball c R) :
  âˆ¥f wâˆ¥ = âˆ¥f câˆ¥ :=
begin
  refine (is_max_on_iff.1 hn _ hw).antisymm (not_lt.1 _),
  rintro hw_lt : âˆ¥f wâˆ¥ < âˆ¥f câˆ¥,
  set r := dist w c,
  have hr : 0 < r, from dist_pos.2 (Î» h, hw_lt.ne $ h â–¸ rfl),
  have hsub' : closed_ball c r âŠ† closed_ball c R, from closed_ball_subset_closed_ball hw,
  have hsub : sphere c r âŠ† closed_ball c R, from sphere_subset_closed_ball.trans hsub',
  have hne : âˆ€ z âˆˆ sphere c r, z â‰  c,
    from Î» z hz, ne_of_mem_of_not_mem hz (ne_of_lt $ (dist_self c).symm â–¸ hr),
  have hcont : continuous_on (Î» z, (z - c)â»Â¹ â€¢ f z) (sphere c r),
    from ((continuous_on_id.sub continuous_on_const).invâ‚€ $
      Î» z hz, sub_ne_zero.2 (hne z hz)).smul (hd.continuous_on.mono hsub),
  have hle : âˆ€ z âˆˆ sphere c r, âˆ¥(z - c)â»Â¹ â€¢ f zâˆ¥ â‰¤ âˆ¥f câˆ¥ / r,
  { rintros z (hz : abs (z - c) = r),
    simpa [norm_smul, hz, â† div_eq_inv_mul] using (div_le_div_right hr).2 (hn (hsub hz)) },
  have hlt : âˆ¥(w - c)â»Â¹ â€¢ f wâˆ¥ < âˆ¥f câˆ¥ / r,
    by simpa [norm_smul, â† div_eq_inv_mul] using (div_lt_div_right hr).2 hw_lt,
  have : âˆ¥âˆ® z in C(c, r), (z - c)â»Â¹ â€¢ f zâˆ¥ < 2 * Ï€ * r * (âˆ¥f câˆ¥ / r),
    from circle_integral.norm_integral_lt_of_norm_le_const_of_lt hr hcont hle âŸ¨w, rfl, hltâŸ©,
  refine this.ne _,
  rw circle_integral_sub_inv_smul_of_differentiable_on (mem_ball_self hr) (hd.mono hsub'),
  field_simp [norm_smul, hr.ne', abs_of_pos real.pi_pos],
  ac_refl
end

lemma norm_eventually_eq_of_eventually_differentiable_at_of_is_local_max {f : â„‚ â†’ E} {c : â„‚}
  (hd : âˆ€á¶  z in ğ“ c, differentiable_at â„‚ f z) (hc : is_local_max (norm âˆ˜ f) c) :
  âˆ€á¶  y in ğ“ c, âˆ¥f yâˆ¥ = âˆ¥f câˆ¥ :=
begin
  rcases nhds_basis_closed_ball.eventually_iff.1 (hd.and hc) with âŸ¨r, hrâ‚€, hrâŸ©,
  exact nhds_basis_closed_ball.eventually_iff.2 âŸ¨r, hrâ‚€, Î» w hw,
    norm_eq_norm_of_differentiable_on_of_is_max_on_closed_ball_of_mem_closed_ball
      (Î» z hz, (hr hz).1.differentiable_within_at) (Î» z hz, (hr hz).2) hwâŸ©
end

lemma is_open_set_of_mem_nhds_and_is_max_on_norm {f : â„‚ â†’ E} {s : set â„‚}
  (hd : differentiable_on â„‚ f s) :
  is_open {z | s âˆˆ ğ“ z âˆ§ is_max_on (norm âˆ˜ f) s z} :=
begin
  refine is_open_iff_mem_nhds.2 (Î» z hz, (eventually_eventually_nhds.2 hz.1).and _),
  replace hd : âˆ€á¶  w in ğ“ z, differentiable_at â„‚ f w, from hd.eventually_differentiable_at hz.1,
  exact (norm_eventually_eq_of_eventually_differentiable_at_of_is_local_max hd $
    (hz.2.is_local_max hz.1)).mono (Î» x hx y hy, le_trans (hz.2 hy) hx.ge)
end

end complex
