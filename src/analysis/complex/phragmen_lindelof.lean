/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.complex.abs_max

/-!
-/

open set function filter asymptotics complex metric
open_locale topological_space filter real

local notation `expR` := real.exp

namespace phragmen_lindelof

variables {Œπ E : Type*} [normed_group E] [normed_space ‚ÑÇ E]

lemma horizontal_strip_pi_div_two {C : ‚Ñù} {f : ‚ÑÇ ‚Üí E}
  (hd : diff_cont_on_cl ‚ÑÇ f (im ‚Åª¬π' Ioo (-(œÄ / 2)) (œÄ / 2)))
  (hB : ‚àÉ (c ‚àà Ico 0 (1 : ‚Ñù)) A, ‚àÄ z : ‚ÑÇ, |z.im| < œÄ / 2 ‚Üí ‚à•f z‚à• ‚â§ expR (A * expR (c * |z.re|)))
  (hle : ‚àÄ z : ‚ÑÇ, |z.im| = (œÄ / 2) ‚Üí ‚à•f z‚à• ‚â§ C) {z : ‚ÑÇ} (hz : |z.im| ‚â§ œÄ / 2) :
  ‚à•f z‚à• ‚â§ C :=
begin
  -- WLOG, `0 < C`.
  have hœÄ2 : 0 < œÄ / 2, from div_pos real.pi_pos two_pos,
  have hœÄ : -(œÄ / 2) < œÄ / 2, from neg_lt_self hœÄ2,
  suffices : ‚àÄ C' : ‚Ñù, 0 < C' ‚Üí (‚àÄ w : ‚ÑÇ, |w.im| = (œÄ / 2) ‚Üí ‚à•f w‚à• ‚â§ C') ‚Üí ‚à•f z‚à• ‚â§ C',
  { refine le_of_forall_le_of_dense (Œª C' hC', this C' _ $ Œª w hw, (hle w hw).trans hC'.le),
    refine ((norm_nonneg (f (‚Üë(œÄ / 2) * I))).trans (hle _ _)).trans_lt hC',
    rwa [of_real_mul_im, I_im, mul_one, abs_of_pos] },
  clear_dependent C, intros C hC‚ÇÄ hle,
  -- Choose some `c A : ‚Ñù` satisfying `hB`, then choose `b ‚àà (c, 1)`.
  rcases hB with ‚ü®c, ‚ü®hc‚ÇÄ, hc‚ÇÅ‚ü©, A, Hle‚ü©,
  rcases exists_between hc‚ÇÅ with ‚ü®b, hcb, hb‚ÇÅ‚ü©,
  have hb‚ÇÄ : 0 < b, from hc‚ÇÄ.trans_lt hcb,
  have hbœÄ : 0 < b * (œÄ / 2), from mul_pos hb‚ÇÄ hœÄ2,
  have hbœÄ' : b * (œÄ / 2) < œÄ / 2, from (mul_lt_iff_lt_one_left hœÄ2).2 hb‚ÇÅ,
  /- Put `g Œµ w = exp (-Œµ * (exp (b * w) + exp (-b * w)))`. We're only interested in `Œµ > 0`
  and `w` from our strip. -/
  set g : ‚Ñù ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ := Œª Œµ w, exp (-Œµ * (exp (b * w) + exp (-b * w))),
  /- Since `g Œµ z ‚Üí 1` as `Œµ ‚Üí 0`, it suffices to prove that `‚à•g Œµ z ‚Ä¢ f z‚à• ‚â§ C`
  for all positive `Œµ`. -/
  suffices : ‚àÄ·∂† Œµ : ‚Ñù in ùìù[>] 0, ‚à•g Œµ z ‚Ä¢ f z‚à• ‚â§ C,
  { refine le_of_tendsto (tendsto.mono_left _ nhds_within_le_nhds) this,
    refine ((continuous_of_real.neg.mul continuous_const).cexp.smul
      continuous_const).norm.tendsto' _ _ _,
    simp },
  filter_upwards [self_mem_nhds_within] with Œµ Œµ‚ÇÄ, change 0 < Œµ at Œµ‚ÇÄ,
  -- An upper estimate on `‚à•g Œµ w‚à•` that will be used in two branches of the proof.
  obtain ‚ü®Œ¥, Œ¥‚ÇÄ, hŒ¥‚ü© : ‚àÉ Œ¥ : ‚Ñù, 0 < Œ¥ ‚àß ‚àÄ {w : ‚ÑÇ}, |w.im| ‚â§ œÄ / 2 ‚Üí
    abs (g Œµ w) ‚â§ expR (-Œ¥ * expR (b * |w.re|)),
  { have hcos‚ÇÄ : 0 < real.cos (b * (œÄ / 2)),
      from real.cos_pos_of_mem_Ioo (abs_lt.1 $ (abs_of_pos hbœÄ).symm ‚ñ∏ hbœÄ'),
    refine ‚ü®Œµ * real.cos (b * (œÄ / 2)), mul_pos Œµ‚ÇÄ hcos‚ÇÄ, _‚ü©,
    intros w hw,
    calc abs (g Œµ w)
        = expR (-Œµ * (expR (b * w.re) + expR (-b * w.re)) * real.cos (b * w.im)) :
      by simp only [abs_exp, ‚Üê of_real_neg, of_real_mul_re, add_re, exp_re, of_real_mul_im,
        neg_mul b w.im, real.cos_neg, mul_assoc, add_mul]
    ... ‚â§ expR (-(Œµ * real.cos (b * (œÄ / 2))) * expR (b * |w.re|)) : _,
    simp only [real.exp_le_exp, neg_mul, neg_le_neg_iff, mul_assoc, mul_le_mul_left Œµ‚ÇÄ],
    rw mul_comm,
    have hexp : expR (b * |w.re|) ‚â§ expR (b * w.re) + expR (-(b * w.re)),
    { cases le_total w.re 0 with hw hw,
      { rw [abs_of_nonpos hw, mul_neg],
        exact le_add_of_nonneg_left (real.exp_pos _).le },
      { rw [_root_.abs_of_nonneg hw],
        exact le_add_of_nonneg_right (real.exp_pos _).le } },
    have hcos : real.cos (b * (œÄ / 2)) ‚â§ real.cos (b * w.im),
    { rw [‚Üê real.cos_abs (b * w.im)],
      refine real.cos_le_cos_of_nonneg_of_le_pi (_root_.abs_nonneg _) _ _,
      { exact hbœÄ'.le.trans (half_le_self real.pi_pos.le) },
      { rw [_root_.abs_mul, abs_of_pos hb‚ÇÄ],
        exact mul_le_mul_of_nonneg_left hw hb‚ÇÄ.le } },
    exact mul_le_mul hexp hcos hcos‚ÇÄ.le ((real.exp_pos _).le.trans hexp) },
  -- `abs (g Œµ w) ‚â§ 1` whenever `Œµ` is nonnegative and `|w.im| = œÄ / 2`
  have hg‚ÇÅ : ‚àÄ (w : ‚ÑÇ), |w.im| = œÄ / 2 ‚Üí abs (g Œµ w) ‚â§ 1,
  { intros w hw,
    refine (hŒ¥ hw.le).trans (real.exp_le_one_iff.2 _),
    exact mul_nonpos_of_nonpos_of_nonneg (neg_nonpos.2 Œ¥‚ÇÄ.le) (real.exp_pos _).le },
  obtain ‚ü®R, hzR, hR‚ü© : ‚àÉ R : ‚Ñù, |z.re| < R ‚àß ‚àÄ w : ‚ÑÇ, |w.re| = R ‚Üí |w.im| < œÄ / 2 ‚Üí
    ‚à•g Œµ w ‚Ä¢ f w‚à• ‚â§ C,
  { refine ((eventually_gt_at_top _).and _).exists,
    suffices : tendsto (Œª R, expR (-Œ¥ * expR (b * R) + A * expR (c * R))) at_top (ùìù 0),
    { refine (this.eventually (ge_mem_nhds hC‚ÇÄ)).mono (Œª R hR w hre him, _),
      calc ‚à•g Œµ w ‚Ä¢ f w‚à• ‚â§ expR (-Œ¥ * expR (b * R) + A * expR (c * R)) : _
      ... ‚â§ C : hR,
      rw [norm_smul, real.exp_add, ‚Üê hre],
      exact mul_le_mul (hŒ¥ him.le) (Hle _ him) (norm_nonneg _) (real.exp_pos _).le },
    refine real.tendsto_exp_at_bot.comp _,
    clear_dependent C z g f,
    obtain ‚ü®c, hc, rfl‚ü© : ‚àÉ c' : ‚Ñù, 0 < c' ‚àß b - c' = c,
      from ‚ü®b - c, sub_pos.2 hcb, sub_sub_cancel _ _‚ü©,
    simp only [sub_mul, real.exp_sub, div_eq_inv_mul, real.exp_add, ‚Üê mul_assoc, ‚Üê add_mul],
    suffices : tendsto (Œª R, -Œ¥ + A * (expR (c * R))‚Åª¬π) at_top (ùìù (-Œ¥ + A * 0)),
    { rw [mul_zero, add_zero] at this,
      exact tendsto.neg_mul_at_top (neg_lt_zero.2 Œ¥‚ÇÄ) this
        (real.tendsto_exp_at_top.comp $ tendsto_const_nhds.mul_at_top hb‚ÇÄ tendsto_id) },
    refine tendsto_const_nhds.add (tendsto_const_nhds.mul _),
    exact tendsto_inv_at_top_zero.comp (real.tendsto_exp_at_top.comp $
      tendsto_const_nhds.mul_at_top hc tendsto_id) },
  have hR‚ÇÄ : 0 < R, from (_root_.abs_nonneg _).trans_lt hzR,
  have hgd : differentiable ‚ÑÇ (g Œµ),
    from (((differentiable_id.const_mul _).cexp.add
      (differentiable_id.const_mul _).cexp).const_mul _).cexp,
  replace hd : diff_cont_on_cl ‚ÑÇ (Œª w, g Œµ w ‚Ä¢ f w) ((Ioo (-R) R) √ó‚ÑÇ Ioo (-(œÄ / 2)) (œÄ / 2)),
    from (hgd.diff_cont_on_cl.smul hd).mono (Œª w hw, hw.2),
  convert norm_le_of_forall_mem_frontier_norm_le
    ((bounded_Ioo _ _).re_prod_im (bounded_Ioo _ _)) hd (Œª w hw, _) _,
  { have hwc := frontier_subset_closure hw,
    rw [frontier_re_prod_im, closure_Ioo (neg_lt_self hR‚ÇÄ).ne, frontier_Ioo hœÄ,
      closure_Ioo hœÄ.ne, frontier_Ioo (neg_lt_self hR‚ÇÄ)] at hw,
    cases eq_or_ne (|w.im|) (œÄ / 2) with him him,
    { rw [closure_re_prod_im, closure_Ioo (neg_lt_self hR‚ÇÄ).ne] at hwc,
      rw [norm_smul, ‚Üê one_mul C],
      exact mul_le_mul (hg‚ÇÅ _ him) (hle _ him) (norm_nonneg _) zero_le_one },
    { replace hw : w ‚àà {-R, R} √ó‚ÑÇ Icc (-(œÄ / 2)) (œÄ / 2),
      { rw [ne.def, abs_eq hœÄ2.le] at him,
        exact hw.resolve_left (Œª h, him (or.symm h.right)) },
      exact hR _ ((abs_eq hR‚ÇÄ.le).2 (or.symm hw.1)) ((abs_le.2 hw.2).lt_of_ne him) } },
  { rw [closure_re_prod_im, closure_Ioo hœÄ.ne, closure_Ioo (neg_lt_self hR‚ÇÄ).ne],
    exact ‚ü®abs_le.1 hzR.le, abs_le.1 hz‚ü© }
end

lemma horizontal_strip {a b C : ‚Ñù} {f : ‚ÑÇ ‚Üí E}
  (hd : diff_cont_on_cl ‚ÑÇ f (im ‚Åª¬π' Ioo a b))
  (hB : ‚àÉ (c ‚àà Ico 0 (œÄ / (b - a))) A, ‚àÄ z : ‚ÑÇ, z.im ‚àà Ioo a b ‚Üí
    ‚à•f z‚à• ‚â§ expR (A * expR (c * |z.re|)))
  (hle : ‚àÄ z : ‚ÑÇ, (z.im = a ‚à® z.im = b) ‚Üí ‚à•f z‚à• ‚â§ C) {z : ‚ÑÇ} (hz : z.im ‚àà Icc a b) :
  ‚à•f z‚à• ‚â§ C :=
begin
  -- If `z.im = a` or `z.im = b`, then apply `hle`, otherwise `z.im ‚àà Ioo a b`
  rcases eq_endpoints_or_mem_Ioo_of_mem_Icc hz with (hz|hz|hz'),
  { exact hle z (or.inl hz) }, { exact hle z (or.inr hz) }, clear hz, rename hz' hz,
  obtain ‚ü®a, b, ha, rfl, rfl‚ü© :
    ‚àÉ a' b' : ‚Ñù, 0 < a' ‚àß a' * -(œÄ / 2) + b' = a ‚àß a' * (œÄ / 2) + b' = b,
  { refine ‚ü®(b - a) / œÄ, (a + b) / 2, div_pos (sub_pos.2 (hz.1.trans hz.2)) real.pi_pos, _, _‚ü©;
      { field_simp [real.pi_pos.ne'], ring } },
  have hlt_iff : ‚àÄ {w : ‚ÑÇ}, |w.im| < œÄ / 2 ‚Üî
    (a * w + I * b : ‚ÑÇ).im ‚àà Ioo (a * -(œÄ / 2) + b) (a * (œÄ / 2) + b),
  { intro w,
    rw [add_im, mul_comm I, of_real_mul_im, of_real_mul_im, I_im, mul_one, add_mem_Ioo_iff_left,
      add_sub_cancel, add_sub_cancel, mem_Ioo, mul_lt_mul_left ha, mul_lt_mul_left ha, abs_lt] },
  have heq_iff : ‚àÄ {w : ‚ÑÇ}, |w.im| = œÄ / 2 ‚Üî
    (a * w + I * b : ‚ÑÇ).im ‚àà ({a * -(œÄ / 2) + b, a * (œÄ / 2) + b} : set ‚Ñù),
  { intro w,
    rw [add_im, mul_comm I, of_real_mul_im, of_real_mul_im, I_im, mul_one, mem_insert_iff,
      mem_singleton_iff, add_left_inj, add_left_inj, mul_right_inj' ha.ne', mul_right_inj' ha.ne',
      abs_eq (div_pos real.pi_pos two_pos).le, or_comm] },
  have hle_iff : ‚àÄ {w : ‚ÑÇ}, |w.im| ‚â§ œÄ / 2 ‚Üî
    (a * w + I * b : ‚ÑÇ).im ‚àà Icc (a * -(œÄ / 2) + b) (a * (œÄ / 2) + b),
  { intro w,
    rw [add_im, mul_comm I, of_real_mul_im, of_real_mul_im, I_im, mul_one, add_mem_Icc_iff_left,
      add_sub_cancel, add_sub_cancel, mem_Icc, mul_le_mul_left ha, mul_le_mul_left ha, abs_le] },
  obtain ‚ü®z, rfl‚ü© : ‚àÉ z' : ‚ÑÇ, ‚Üëa * z' + I * b = z,
  { use (z - I * b) / a,
    rw [mul_div_cancel' _ (of_real_ne_zero.2 ha.ne'), sub_add_cancel] },
  replace hz : |z.im| ‚â§ œÄ / 2, from hle_iff.2 (Ioo_subset_Icc_self hz),
  set g : ‚ÑÇ ‚Üí E := Œª w, f (a * w + I * b),
  change ‚à•g z‚à• ‚â§ C,
  refine horizontal_strip_pi_div_two
    (hd.comp ((differentiable_id.const_mul _).add_const _).diff_cont_on_cl
      (Œª z hz, hlt_iff.1 $ abs_lt.2 hz)) _ (Œª w hw, hle _ (heq_iff.1 hw)) hz,
  rcases hB with ‚ü®c, hc, A, Hle‚ü©,
  rw [add_sub_add_right_eq_sub, mul_neg, sub_neg_eq_add, ‚Üê mul_add, add_halves,
    div_mul_left real.pi_pos.ne'] at hc,
  refine ‚ü®a * c, ‚ü®mul_nonneg ha.le hc.1, _‚ü©, A, Œª w hw, _‚ü©,
  { rw [mem_Ico, lt_div_iff' ha] at hc, exact hc.2 },
  { convert Hle _ (hlt_iff.1 hw) using 4,
    rw [add_re, mul_comm I, of_real_mul_re, of_real_mul_re, I_re, mul_zero, add_zero,
      _root_.abs_mul, abs_of_pos ha, ‚Üê mul_assoc, mul_comm a] }
end

lemma eq_zero_on_horizontal_strip {a b : ‚Ñù} {f : ‚ÑÇ ‚Üí E}
  (hd : diff_cont_on_cl ‚ÑÇ f (im ‚Åª¬π' Ioo a b))
  (hB : ‚àÉ (c ‚àà Ico 0 (œÄ / (b - a))) A, ‚àÄ z : ‚ÑÇ, z.im ‚àà Ioo a b ‚Üí
    ‚à•f z‚à• ‚â§ expR (A * expR (c * |z.re|)))
  (h‚ÇÄ : ‚àÄ z : ‚ÑÇ, (z.im = a ‚à® z.im = b) ‚Üí f z = 0) {z : ‚ÑÇ} (hz : z.im ‚àà Icc a b) :
  f z = 0 :=
norm_le_zero_iff.1 $ horizontal_strip hd hB (Œª z hz, norm_le_zero_iff.2 $ h‚ÇÄ z hz) hz

lemma quadrant_I {C : ‚Ñù} {f : ‚ÑÇ ‚Üí E} (hd : diff_cont_on_cl ‚ÑÇ f (Ioi 0 √ó‚ÑÇ Ioi 0))
  (hB : ‚àÉ (c ‚àà Ico (0 : ‚Ñù) 2) A, ‚àÄ z : ‚ÑÇ, 0 < z.re ‚Üí 0 < z.im ‚Üí ‚à•f z‚à• ‚â§ expR (A * (abs z) ^ c))
  (hre : ‚àÄ x : ‚Ñù, 0 ‚â§ x ‚Üí ‚à•f x‚à• ‚â§ C) (him : ‚àÄ x : ‚Ñù, 0 ‚â§ x ‚Üí ‚à•f (x * I)‚à• ‚â§ C) {z : ‚ÑÇ}
  (hz : 0 ‚â§ z.re ‚àß 0 ‚â§ z.im) :
  ‚à•f z‚à• ‚â§ C :=
begin
  rcases eq_or_ne z 0 with rfl|hzne, { exact hre 0 le_rfl },
  obtain ‚ü®z, hz, rfl‚ü© : ‚àÉ Œ∂ : ‚ÑÇ, Œ∂.im ‚àà Icc 0 (œÄ / 2) ‚àß exp Œ∂ = z,
  { refine ‚ü®log z, _, exp_log hzne‚ü©,
    rw log_im,
    exact ‚ü®arg_nonneg_iff.2 hz.2, (arg_mem_Icc_neg_pi_div_two_pi_div_two.2 hz.1).2‚ü© },
  clear hz hzne,
  change ‚à•(f ‚àò exp) z‚à• ‚â§ C,
  have : maps_to exp (im ‚Åª¬π' Ioo 0 (œÄ / 2)) (Ioi 0 √ó‚ÑÇ Ioi 0),
  { intros z hz,
    rw [mem_re_prod_im, exp_re, exp_im, mem_Ioi, mem_Ioi],
    refine ‚ü®mul_pos (real.exp_pos _)
      (real.cos_pos_of_mem_Ioo ‚ü®(neg_lt_zero.2 $ div_pos real.pi_pos two_pos).trans hz.1, hz.2‚ü©),
      mul_pos (real.exp_pos _)
        (real.sin_pos_of_mem_Ioo ‚ü®hz.1, hz.2.trans (half_lt_self real.pi_pos)‚ü©)‚ü© },
  refine horizontal_strip (hd.comp differentiable_exp.diff_cont_on_cl this) _ (Œª w hw, _) hz,
  { rw [sub_zero, div_div_cancel' real.pi_pos.ne'],
    rcases hB with ‚ü®c, hc, A, Hle‚ü©,
    refine ‚ü®c, hc, max A 0, Œª w hw, (Hle _ (this hw).1 (this hw).2).trans _‚ü©,
    rw [abs_exp, ‚Üê real.exp_mul, real.exp_le_exp, mul_comm _ c],
    exact mul_le_mul (le_max_left _ _)
      (real.exp_le_exp.2 $ mul_le_mul_of_nonneg_left (le_abs_self _) hc.1)
      (real.exp_pos _).le (le_max_right _ _) },
  { cases w with x y, rcases hw with (rfl : y = 0)|(rfl : y = œÄ / 2),
    { rw [‚Üê of_real_def, comp_app, ‚Üê of_real_exp],
      exact hre _ (real.exp_pos _).le },
    { rw [mk_eq_add_mul_I, comp_app, exp_add_mul_I, ‚Üê of_real_cos, ‚Üê of_real_sin,
        real.cos_pi_div_two, real.sin_pi_div_two, of_real_zero, of_real_one, one_mul, zero_add,
        ‚Üê of_real_exp],
      exact him _ (real.exp_pos _).le } }
end

lemma quadrant_II {C : ‚Ñù} {f : ‚ÑÇ ‚Üí E} (hd : diff_cont_on_cl ‚ÑÇ f (Iio 0 √ó‚ÑÇ Ioi 0))
  (hB : ‚àÉ (c ‚àà Ico (0 : ‚Ñù) 2) A, ‚àÄ z : ‚ÑÇ, z.re < 0 ‚Üí 0 < z.im ‚Üí ‚à•f z‚à• ‚â§ expR (A * (abs z) ^ c))
  (hre : ‚àÄ x : ‚Ñù, x ‚â§ 0 ‚Üí ‚à•f x‚à• ‚â§ C) (him : ‚àÄ x : ‚Ñù, 0 ‚â§ x ‚Üí ‚à•f (x * I)‚à• ‚â§ C) {z : ‚ÑÇ}
  (hz : z.re ‚â§ 0 ‚àß 0 ‚â§ z.im) :
  ‚à•f z‚à• ‚â§ C :=
begin
  obtain ‚ü®z, rfl‚ü© : ‚àÉ z', z' * I = z, from ‚ü®z / I, div_mul_cancel _ I_ne_zero‚ü©,
  rw [mul_I_re, mul_I_im, neg_nonpos] at hz,
  change ‚à•(f ‚àò (* I)) z‚à• ‚â§ C,
  have H : maps_to (* I) (Ioi 0 √ó‚ÑÇ Ioi 0) (Iio 0 √ó‚ÑÇ Ioi 0),
  { intros w hw,
    simpa only [mem_re_prod_im, mul_I_re, mul_I_im, neg_lt_zero, mem_Iio] using hw.symm },
  refine quadrant_I (hd.comp (differentiable_id.mul_const _).diff_cont_on_cl H)
    _ him (Œª x hx, _) hz.symm,
  { rcases hB with ‚ü®c, hc, A, Hle‚ü©,
    refine ‚ü®c, hc, A, Œª w h‚ÇÅ h‚ÇÇ, _‚ü©,
    simpa only [complex.abs_mul, abs_I, mul_one] using Hle _ (H ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©).1 (H ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©).2 },
  { rw [comp_app, mul_assoc, I_mul_I, mul_neg_one, ‚Üê of_real_neg],
    exact hre _ (neg_nonpos.2 hx) }
end

lemma quadrant_III {C : ‚Ñù} {f : ‚ÑÇ ‚Üí E} (hd : diff_cont_on_cl ‚ÑÇ f (Iio 0 √ó‚ÑÇ Iio 0))
  (hB : ‚àÉ (c ‚àà Ico (0 : ‚Ñù) 2) A, ‚àÄ z : ‚ÑÇ, z.re < 0 ‚Üí z.im < 0 ‚Üí ‚à•f z‚à• ‚â§ expR (A * (abs z) ^ c))
  (hre : ‚àÄ x : ‚Ñù, x ‚â§ 0 ‚Üí ‚à•f x‚à• ‚â§ C) (him : ‚àÄ x : ‚Ñù, x ‚â§ 0 ‚Üí ‚à•f (x * I)‚à• ‚â§ C) {z : ‚ÑÇ}
  (hz : z.re ‚â§ 0 ‚àß z.im ‚â§ 0) :
  ‚à•f z‚à• ‚â§ C :=
begin
  obtain ‚ü®z, rfl‚ü© : ‚àÉ z', -z' = z, from ‚ü®-z, neg_neg z‚ü©,
  rw [neg_re, neg_im, neg_nonpos, neg_nonpos] at hz,
  change ‚à•(f ‚àò has_neg.neg) z‚à• ‚â§ C,
  have H : maps_to has_neg.neg (Ioi 0 √ó‚ÑÇ Ioi 0) (Iio 0 √ó‚ÑÇ Iio 0),
  { intros w hw,
    simpa only [mem_re_prod_im, neg_re, neg_im, neg_lt_zero, mem_Iio] using hw },
  refine quadrant_I (hd.comp differentiable_neg.diff_cont_on_cl H) _ (Œª x hx, _) (Œª x hx, _) hz,
  { rcases hB with ‚ü®c, hc, A, Hle‚ü©,
    refine ‚ü®c, hc, A, Œª w h‚ÇÅ h‚ÇÇ, _‚ü©,
    simpa only [comp_app, complex.abs_neg] using Hle _ (H ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©).1 (H ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©).2 },
  { rw [comp_app, ‚Üê of_real_neg],
    exact hre (-x) (neg_nonpos.2 hx) },
  { rw [comp_app, ‚Üê neg_mul, ‚Üê of_real_neg],
    exact him (-x) (neg_nonpos.2 hx) }
end

lemma quadrant_IV {C : ‚Ñù} {f : ‚ÑÇ ‚Üí E} (hd : diff_cont_on_cl ‚ÑÇ f (Ioi 0 √ó‚ÑÇ Iio 0))
  (hB : ‚àÉ (c ‚àà Ico (0 : ‚Ñù) 2) A, ‚àÄ z : ‚ÑÇ, 0 < z.re ‚Üí z.im < 0 ‚Üí ‚à•f z‚à• ‚â§ expR (A * (abs z) ^ c))
  (hre : ‚àÄ x : ‚Ñù, 0 ‚â§ x ‚Üí ‚à•f x‚à• ‚â§ C) (him : ‚àÄ x : ‚Ñù, x ‚â§ 0 ‚Üí ‚à•f (x * I)‚à• ‚â§ C) {z : ‚ÑÇ}
  (hz : 0 ‚â§ z.re ‚àß z.im ‚â§ 0) :
  ‚à•f z‚à• ‚â§ C :=
begin
  obtain ‚ü®z, rfl‚ü© : ‚àÉ z', -z' = z, from ‚ü®-z, neg_neg z‚ü©,
  rw [neg_re, neg_im, neg_nonpos, neg_nonneg] at hz,
  change ‚à•(f ‚àò has_neg.neg) z‚à• ‚â§ C,
  have H : maps_to has_neg.neg (Iio 0 √ó‚ÑÇ Ioi 0) (Ioi 0 √ó‚ÑÇ Iio 0),
  { intros w hw,
    simpa only [mem_re_prod_im, neg_re, neg_im, neg_lt_zero, neg_pos, mem_Ioi, mem_Iio] using hw },
  refine quadrant_II (hd.comp differentiable_neg.diff_cont_on_cl H) _ (Œª x hx, _) (Œª x hx, _) hz,
  { rcases hB with ‚ü®c, hc, A, Hle‚ü©,
    refine ‚ü®c, hc, A, Œª w h‚ÇÅ h‚ÇÇ, _‚ü©,
    simpa only [comp_app, complex.abs_neg] using Hle _ (H ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©).1 (H ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©).2 },
  { rw [comp_app, ‚Üê of_real_neg],
    exact hre (-x) (neg_nonneg.2 hx) },
  { rw [comp_app, ‚Üê neg_mul, ‚Üê of_real_neg],
    exact him (-x) (neg_nonpos.2 hx) }
end

end phragmen_lindelof
