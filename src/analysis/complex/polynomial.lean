/-
Copyright (c) 2019 Chris Hughes All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes
-/
import analysis.special_functions.pow
import field_theory.is_alg_closed.basic
import topology.algebra.polynomial

/-!
# The fundamental theorem of algebra

This file proves that every nonconstant complex polynomial has a root.

As a consequence, the complex numbers are algebraically closed.
-/

open complex polynomial metric filter set
open_locale polynomial topological_space

namespace complex

/- The following proof uses the method given at
<https://ncatlab.org/nlab/show/fundamental+theorem+of+algebra#classical_fta_via_advanced_calculus>
-/
/-- **Fundamental theorem of algebra**: every non constant complex polynomial
  has a root -/
lemma exists_root {f : â„‚[X]} (hf : 0 < degree f) : âˆƒ z : â„‚, is_root f z :=
begin
  /- Choose a global minimum `zâ‚€` of `âˆ¥f zâˆ¥`. -/
  refine f.exists_forall_norm_le.imp (Î» zâ‚€ hzâ‚€, by_contra $ Î» (hf0 : eval zâ‚€ f â‰  0), _),
  /- Represent `f` as `g * (X - C zâ‚€) ^ k + C a`. -/
  obtain âŸ¨a, k, g, ha0, hk0, hg0, rflâŸ© :
    âˆƒ (a : â„‚) (k : â„•) (g : â„‚[X]), a â‰  0 âˆ§ 0 < k âˆ§ eval zâ‚€ g â‰  0 âˆ§ g * (X - C zâ‚€) ^ k + C a = f,
  { set a := f.eval zâ‚€,
    set n := root_multiplicity zâ‚€ (f - C a),
    have hfa : f - C a â‰  0, from mt sub_eq_zero.1 (Î» h, hf.not_le (h.symm â–¸ degree_C_le)),
    refine âŸ¨a, n, (f - C a) /â‚˜ ((X - C zâ‚€) ^ n), hf0, (root_multiplicity_pos hfa).2 _, _, _âŸ©,
    { rw [is_root, eval_sub, eval_C, sub_self] },
    { exact eval_div_by_monic_pow_root_multiplicity_ne_zero _ hfa },
    { exact eq_sub_iff_add_eq.1 (div_by_monic_mul_pow_root_multiplicity_eq _ _) } },
  clear hf0 hf,
  /- Choose `k`-th root of $-\frac{a}{g(z_0)}$. -/
  obtain âŸ¨w, hwâŸ© : âˆƒ w, w ^ k = -a / eval zâ‚€ g, from âŸ¨_, cpow_nat_inv_pow _ hk0.ne'âŸ©,
  /- It suffices to show that $âˆ¥f(zâ‚€+Îµw)âˆ¥ < âˆ¥f(zâ‚€)âˆ¥$ for sufficiently small positive `Îµ`. We
  substitute `f = g * (X - C zâ‚€) ^ k + C a` and reorder terms in this inequality. -/
  suffices : âˆ€á¶  Îµ : â„ in ð“[>] 0,
    abs (1 - Îµ ^ k + Îµ ^ k * ((eval zâ‚€ g - eval (zâ‚€ + Îµ * w) g) / eval zâ‚€ g)) < 1,
  { rcases this.exists with âŸ¨Îµ, hÎµâŸ©,
    rw [â† mul_lt_mul_left (abs.pos ha0), â† map_mul, mul_one] at hÎµ,
    refine hÎµ.not_le _,
    convert hzâ‚€ (zâ‚€ + Îµ * w),
    { rw [eval_add, eval_mul, eval_pow, eval_sub, eval_X, eval_C, eval_C, sub_self, zero_pow hk0,
        mul_zero, zero_add] },
    { rw [eval_add, eval_C, eval_mul, eval_pow, eval_sub, eval_X, eval_C, add_sub_cancel', mul_pow,
        hw, sub_div, div_self hg0, div_eq_mul_inv, div_eq_mul_inv],
      ring } },
  /- Since `g` is continuous, the fraction `(eval zâ‚€ g - eval (zâ‚€ + â†‘Îµ * w) g) / eval zâ‚€ g` tends
  to zero as `Îµ â†’ 0`. -/
  have hg : tendsto (Î» Îµ : â„, âˆ¥(eval zâ‚€ g - eval (zâ‚€ + â†‘Îµ * w) g) / eval zâ‚€ gâˆ¥) (ð“ 0) (ð“ 0),
  { refine (continuous_const.sub _).div_const.norm.tendsto' _ _ _,
    { exact g.continuous.comp (continuous_const.add $ continuous_of_real.mul continuous_const) },
    { simp } },
  /- Choose `Îµ âˆˆ (0, 1)` such that `(eval zâ‚€ g - eval (zâ‚€ + â†‘Îµ * w) g) / eval zâ‚€ g` has norm less
  than one. It is easy to see that $âˆ¥f (zâ‚€ + Îµw)âˆ¥ < âˆ¥f(zâ‚€)âˆ¥$. -/
  filter_upwards [(hg.eventually $ gt_mem_nhds one_pos).filter_mono nhds_within_le_nhds,
    Ioo_mem_nhds_within_Ioi (left_mem_Ico.2 (zero_lt_one' â„))] with Îµ hgÎµ hÎµ,
  refine (abs.add_le _ _).trans_lt _,
  have hÎµ0 : 0 < Îµ ^ k, from pow_pos hÎµ.1 k,
  have hÎµ1 : Îµ ^ k < 1, from pow_lt_one hÎµ.1.le hÎµ.2 hk0.ne',
  rw [â† of_real_pow, â† of_real_one, â† of_real_sub, abs_of_real, abs_of_pos (sub_pos.2 hÎµ1),
    sub_add_eq_add_sub, add_sub_assoc, add_lt_iff_neg_left, sub_lt_zero, map_mul, abs_of_real,
    abs_of_pos hÎµ0],
  exact mul_lt_of_lt_one_right hÎµ0 hgÎµ
end

instance is_alg_closed : is_alg_closed â„‚ :=
is_alg_closed.of_exists_root _ $ Î» p _ hp, complex.exists_root $ degree_pos_of_irreducible hp

end complex
