/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.calculus.fderiv_analytic
import analysis.asymptotics.specific_asymptotics
import analysis.complex.cauchy_integral

/-!
# Removable singularity theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we prove Riemann's removable singularity theorem: if `f : â„‚ â†’ E` is complex
differentiable in a punctured neighborhood of a point `c` and is bounded in a punctured neighborhood
of `c` (or, more generally, $f(z) - f(c)=o((z-c)^{-1})$), then it has a limit at `c` and the
function `function.update f c (lim (ğ“[â‰ ] c) f)` is complex differentiable in a neighborhood of `c`.
-/

open topological_space metric set filter asymptotics function
open_locale topology filter nnreal real

universe u
variables {E : Type u} [normed_add_comm_group E] [normed_space â„‚ E] [complete_space E]

namespace complex

/-- **Removable singularity** theorem, weak version. If `f : â„‚ â†’ E` is differentiable in a punctured
neighborhood of a point and is continuous at this point, then it is analytic at this point. -/
lemma analytic_at_of_differentiable_on_punctured_nhds_of_continuous_at {f : â„‚ â†’ E} {c : â„‚}
  (hd : âˆ€á¶  z in ğ“[â‰ ] c, differentiable_at â„‚ f z) (hc : continuous_at f c) :
  analytic_at â„‚ f c :=
begin
  rcases (nhds_within_has_basis nhds_basis_closed_ball _).mem_iff.1 hd with âŸ¨R, hR0, hRsâŸ©,
  lift R to â„â‰¥0 using hR0.le,
  replace hc : continuous_on f (closed_ball c R),
  { refine Î» z hz, continuous_at.continuous_within_at _,
    rcases eq_or_ne z c with rfl | hne,
    exacts [hc, (hRs âŸ¨hz, hneâŸ©).continuous_at] },
  exact (has_fpower_series_on_ball_of_differentiable_off_countable (countable_singleton c) hc
    (Î» z hz, hRs (diff_subset_diff_left ball_subset_closed_ball hz)) hR0).analytic_at
end

lemma differentiable_on_compl_singleton_and_continuous_at_iff {f : â„‚ â†’ E} {s : set â„‚} {c : â„‚}
  (hs : s âˆˆ ğ“ c) : differentiable_on â„‚ f (s \ {c}) âˆ§ continuous_at f c â†” differentiable_on â„‚ f s :=
begin
  refine âŸ¨_, Î» hd, âŸ¨hd.mono (diff_subset _ _), (hd.differentiable_at hs).continuous_atâŸ©âŸ©,
  rintro âŸ¨hd, hcâŸ© x hx,
  rcases eq_or_ne x c with rfl | hne,
  { refine (analytic_at_of_differentiable_on_punctured_nhds_of_continuous_at _ hc)
      .differentiable_at.differentiable_within_at,
    refine eventually_nhds_within_iff.2 ((eventually_mem_nhds.2 hs).mono $ Î» z hz hzx, _),
    exact hd.differentiable_at (inter_mem hz (is_open_ne.mem_nhds hzx)) },
  { simpa only [differentiable_within_at, has_fderiv_within_at, hne.nhds_within_diff_singleton]
      using hd x âŸ¨hx, hneâŸ© }
end

lemma differentiable_on_dslope {f : â„‚ â†’ E} {s : set â„‚} {c : â„‚} (hc : s âˆˆ ğ“ c) :
  differentiable_on â„‚ (dslope f c) s â†” differentiable_on â„‚ f s :=
âŸ¨Î» h, h.of_dslope, Î» h, (differentiable_on_compl_singleton_and_continuous_at_iff hc).mp $
  âŸ¨iff.mpr (differentiable_on_dslope_of_nmem $ Î» h, h.2 rfl) (h.mono $ diff_subset _ _),
    continuous_at_dslope_same.2 $ h.differentiable_at hcâŸ©âŸ©

/-- **Removable singularity** theorem: if `s` is a neighborhood of `c : â„‚`, a function `f : â„‚ â†’ E`
is complex differentiable on `s \ {c}`, and $f(z) - f(c)=o((z-c)^{-1})$, then `f` redefined to be
equal to `lim (ğ“[â‰ ] c) f` at `c` is complex differentiable on `s`. -/
lemma differentiable_on_update_lim_of_is_o {f : â„‚ â†’ E} {s : set â„‚} {c : â„‚}
  (hc : s âˆˆ ğ“ c) (hd : differentiable_on â„‚ f (s \ {c}))
  (ho : (Î» z, f z - f c) =o[ğ“[â‰ ] c] (Î» z, (z - c)â»Â¹)) :
  differentiable_on â„‚ (update f c (lim (ğ“[â‰ ] c) f)) s :=
begin
  set F : â„‚ â†’ E := Î» z, (z - c) â€¢ f z with hF,
  suffices : differentiable_on â„‚ F (s \ {c}) âˆ§ continuous_at F c,
  { rw [differentiable_on_compl_singleton_and_continuous_at_iff hc, â† differentiable_on_dslope hc,
      dslope_sub_smul] at this; try { apply_instance },
    have hc : tendsto f (ğ“[â‰ ] c) (ğ“ (deriv F c)),
      from continuous_at_update_same.mp (this.continuous_on.continuous_at hc),
    rwa hc.lim_eq },
  refine âŸ¨(differentiable_on_id.sub_const _).smul hd, _âŸ©,
  rw â† continuous_within_at_compl_self,
  have H := ho.tendsto_inv_smul_nhds_zero,
  have H' : tendsto (Î» z, (z - c) â€¢ f c) (ğ“[â‰ ] c) (ğ“ (F c)),
    from (continuous_within_at_id.tendsto.sub tendsto_const_nhds).smul tendsto_const_nhds,
  simpa [â† smul_add, continuous_within_at] using H.add H'
end

/-- **Removable singularity** theorem: if `s` is a punctured neighborhood of `c : â„‚`, a function
`f : â„‚ â†’ E` is complex differentiable on `s`, and $f(z) - f(c)=o((z-c)^{-1})$, then `f` redefined to
be equal to `lim (ğ“[â‰ ] c) f` at `c` is complex differentiable on `{c} âˆª s`. -/
lemma differentiable_on_update_lim_insert_of_is_o {f : â„‚ â†’ E} {s : set â„‚} {c : â„‚}
  (hc : s âˆˆ ğ“[â‰ ] c) (hd : differentiable_on â„‚ f s)
  (ho : (Î» z, f z - f c) =o[ğ“[â‰ ] c] (Î» z, (z - c)â»Â¹)) :
  differentiable_on â„‚ (update f c (lim (ğ“[â‰ ] c) f)) (insert c s) :=
differentiable_on_update_lim_of_is_o (insert_mem_nhds_iff.2 hc)
  (hd.mono $ Î» z hz, hz.1.resolve_left hz.2) ho

/-- **Removable singularity** theorem: if `s` is a neighborhood of `c : â„‚`, a function `f : â„‚ â†’ E`
is complex differentiable and is bounded on `s \ {c}`, then `f` redefined to be equal to
`lim (ğ“[â‰ ] c) f` at `c` is complex differentiable on `s`. -/
lemma differentiable_on_update_lim_of_bdd_above {f : â„‚ â†’ E} {s : set â„‚} {c : â„‚}
  (hc : s âˆˆ ğ“ c) (hd : differentiable_on â„‚ f (s \ {c}))
  (hb : bdd_above (norm âˆ˜ f '' (s \ {c}))) :
  differentiable_on â„‚ (update f c (lim (ğ“[â‰ ] c) f)) s :=
differentiable_on_update_lim_of_is_o hc hd $ is_bounded_under.is_o_sub_self_inv $
  let âŸ¨C, hCâŸ© := hb in âŸ¨C + â€–f câ€–, eventually_map.2 $ mem_nhds_within_iff_exists_mem_nhds_inter.2
    âŸ¨s, hc, Î» z hz, norm_sub_le_of_le (hC $ mem_image_of_mem _ hz) le_rflâŸ©âŸ©

/-- **Removable singularity** theorem: if a function `f : â„‚ â†’ E` is complex differentiable on a
punctured neighborhood of `c` and $f(z) - f(c)=o((z-c)^{-1})$, then `f` has a limit at `c`. -/
lemma tendsto_lim_of_differentiable_on_punctured_nhds_of_is_o {f : â„‚ â†’ E} {c : â„‚}
  (hd : âˆ€á¶  z in ğ“[â‰ ] c, differentiable_at â„‚ f z)
  (ho : (Î» z, f z - f c) =o[ğ“[â‰ ] c] (Î» z, (z - c)â»Â¹)) :
  tendsto f (ğ“[â‰ ] c) (ğ“ $ lim (ğ“[â‰ ] c) f) :=
begin
  rw eventually_nhds_within_iff at hd,
  have : differentiable_on â„‚ f ({z | z â‰  c â†’ differentiable_at â„‚ f z} \ {c}),
    from Î» z hz, (hz.1 hz.2).differentiable_within_at,
  have H := differentiable_on_update_lim_of_is_o hd this ho,
  exact continuous_at_update_same.1 (H.differentiable_at hd).continuous_at
end

/-- **Removable singularity** theorem: if a function `f : â„‚ â†’ E` is complex differentiable and
bounded on a punctured neighborhood of `c`, then `f` has a limit at `c`. -/
lemma tendsto_lim_of_differentiable_on_punctured_nhds_of_bounded_under {f : â„‚ â†’ E}
  {c : â„‚} (hd : âˆ€á¶  z in ğ“[â‰ ] c, differentiable_at â„‚ f z)
  (hb : is_bounded_under (â‰¤) (ğ“[â‰ ] c) (Î» z, â€–f z - f câ€–)) :
  tendsto f (ğ“[â‰ ] c) (ğ“ $ lim (ğ“[â‰ ] c) f) :=
tendsto_lim_of_differentiable_on_punctured_nhds_of_is_o hd hb.is_o_sub_self_inv

/-- The Cauchy formula for the derivative of a holomorphic function. -/
lemma two_pi_I_inv_smul_circle_integral_sub_sq_inv_smul_of_differentiable
  {U : set â„‚} (hU : is_open U) {c wâ‚€ : â„‚} {R : â„} {f : â„‚ â†’ E}
  (hc : closed_ball c R âŠ† U) (hf : differentiable_on â„‚ f U) (hwâ‚€ : wâ‚€ âˆˆ ball c R) :
  (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), ((z - wâ‚€) ^ 2)â»Â¹ â€¢ f z = deriv f wâ‚€ :=
begin
  -- We apply the removable singularity theorem and the Cauchy formula to `dslope f wâ‚€`
  have hR : 0 < R := not_le.mp (ball_eq_empty.not.mp (nonempty_of_mem hwâ‚€).ne_empty),
  have hf' : differentiable_on â„‚ (dslope f wâ‚€) U,
    from (differentiable_on_dslope (hU.mem_nhds ((ball_subset_closed_ball.trans hc) hwâ‚€))).mpr hf,
  have h0 := (hf'.diff_cont_on_cl_ball hc).two_pi_I_inv_smul_circle_integral_sub_inv_smul hwâ‚€,
  rw [â† dslope_same, â† h0],
  congr' 1,
  transitivity âˆ® z in C(c, R), ((z - wâ‚€) ^ 2)â»Â¹ â€¢ (f z - f wâ‚€),
  { have h1 : continuous_on (Î» (z : â„‚), ((z - wâ‚€) ^ 2)â»Â¹) (sphere c R),
    { refine ((continuous_id'.sub continuous_const).pow 2).continuous_on.invâ‚€ (Î» w hw h, _),
      exact sphere_disjoint_ball.ne_of_mem hw hwâ‚€ (sub_eq_zero.mp (sq_eq_zero_iff.mp h)) },
    have h2 : circle_integrable (Î» (z : â„‚), ((z - wâ‚€) ^ 2)â»Â¹ â€¢ f z) c R,
    { refine continuous_on.circle_integrable (pos_of_mem_ball hwâ‚€).le _,
      exact h1.smul (hf.continuous_on.mono (sphere_subset_closed_ball.trans hc)) },
    have h3 : circle_integrable (Î» (z : â„‚), ((z - wâ‚€) ^ 2)â»Â¹ â€¢ f wâ‚€) c R,
      from continuous_on.circle_integrable (pos_of_mem_ball hwâ‚€).le (h1.smul continuous_on_const),
    have h4 : âˆ® (z : â„‚) in C(c, R), ((z - wâ‚€) ^ 2)â»Â¹ = 0,
      by simpa using circle_integral.integral_sub_zpow_of_ne (dec_trivial : (-2 : â„¤) â‰  -1) c wâ‚€ R,
    simp only [smul_sub, circle_integral.integral_sub h2 h3, h4,
      circle_integral.integral_smul_const, zero_smul, sub_zero] },
  { refine circle_integral.integral_congr (pos_of_mem_ball hwâ‚€).le (Î» z hz, _),
    simp only [dslope_of_ne, metric.sphere_disjoint_ball.ne_of_mem hz hwâ‚€, slope, â† smul_assoc, sq,
      mul_inv, ne.def, not_false_iff, vsub_eq_sub, algebra.id.smul_eq_mul] }
end

end complex
