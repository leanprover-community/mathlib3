/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.complex.upper_half_plane.basic
import analysis.convex.contractible
import analysis.convex.complex
import analysis.complex.re_im_topology
import geometry.manifold.cont_mdiff_mfderiv

/-!
# Topology on the upper half plane

In this file we introduce a `topological_space` structure on the upper half plane and provide
various instances.
-/

noncomputable theory
open set filter function topological_space complex
open_locale filter topology upper_half_plane manifold

namespace upper_half_plane

instance : topological_space â„ := subtype.topological_space

lemma open_embedding_coe : open_embedding (coe : â„ â†’ â„‚) :=
is_open.open_embedding_subtype_coe $ is_open_lt continuous_const complex.continuous_im

lemma embedding_coe : embedding (coe : â„ â†’ â„‚) := embedding_subtype_coe
lemma continuous_coe : continuous (coe : â„ â†’ â„‚) := embedding_coe.continuous

lemma continuous_re : continuous re := complex.continuous_re.comp continuous_coe
lemma continuous_im : continuous im := complex.continuous_im.comp continuous_coe

instance : topological_space.second_countable_topology â„ :=
topological_space.subtype.second_countable_topology _ _

instance : t3_space â„ := subtype.t3_space
instance : normal_space â„ := normal_space_of_t3_second_countable â„

instance : contractible_space â„ :=
(convex_halfspace_im_gt 0).contractible_space âŸ¨I, one_pos.trans_eq I_im.symmâŸ©

instance : loc_path_connected_space â„ :=
loc_path_connected_of_is_open $ is_open_lt continuous_const complex.continuous_im

instance : noncompact_space â„ :=
begin
  refine âŸ¨Î» h, _âŸ©,
  have : is_compact (complex.im â»Â¹' Ioi 0), from is_compact_iff_is_compact_univ.2 h,
  replace := this.is_closed.closure_eq,
  rw [closure_preimage_im, closure_Ioi, set.ext_iff] at this,
  exact absurd ((this 0).1 left_mem_Ici) (lt_irrefl _)
end

instance : locally_compact_space â„ := open_embedding_coe.locally_compact_space

instance upper_half_plane.charted_space : charted_space â„‚ â„ :=
upper_half_plane.open_embedding_coe.singleton_charted_space

instance upper_half_plane.smooth_manifold_with_corners : smooth_manifold_with_corners ğ“˜(â„‚) â„ :=
upper_half_plane.open_embedding_coe.singleton_smooth_manifold_with_corners ğ“˜(â„‚)

/-- The inclusion map `â„ â†’ â„‚` is a smooth map of manifolds. -/
lemma smooth_coe : smooth ğ“˜(â„‚) ğ“˜(â„‚) (coe : â„ â†’ â„‚) :=
Î» x, cont_mdiff_at_ext_chart_at

/-- The inclusion map `â„ â†’ â„‚` is a differentiable map of manifolds. -/
lemma mdifferentiable_coe : mdifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (coe : â„ â†’ â„‚) :=
smooth_coe.mdifferentiable

end upper_half_plane
