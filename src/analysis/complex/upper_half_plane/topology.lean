/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.complex.upper_half_plane.basic
import analysis.convex.contractible
import analysis.convex.normed
import analysis.convex.complex
import analysis.complex.re_im_topology
import topology.homotopy.contractible
import geometry.manifold.mfderiv

/-!
# Topology on the upper half plane

In this file we introduce a `topological_space` structure on the upper half plane and provide
various instances.
-/

noncomputable theory
open set filter function topological_space complex
open_locale filter topology upper_half_plane manifold

namespace upper_half_plane

instance : topological_space ‚Ñç := subtype.topological_space

lemma open_embedding_coe : open_embedding (coe : ‚Ñç ‚Üí ‚ÑÇ) :=
is_open.open_embedding_subtype_coe $ is_open_lt continuous_const complex.continuous_im

lemma embedding_coe : embedding (coe : ‚Ñç ‚Üí ‚ÑÇ) := embedding_subtype_coe
lemma continuous_coe : continuous (coe : ‚Ñç ‚Üí ‚ÑÇ) := embedding_coe.continuous

lemma continuous_re : continuous re := complex.continuous_re.comp continuous_coe
lemma continuous_im : continuous im := complex.continuous_im.comp continuous_coe

instance : topological_space.second_countable_topology ‚Ñç :=
topological_space.subtype.second_countable_topology _ _

instance : t3_space ‚Ñç := subtype.t3_space
instance : normal_space ‚Ñç := normal_space_of_t3_second_countable ‚Ñç

instance : contractible_space ‚Ñç :=
(convex_halfspace_im_gt 0).contractible_space ‚ü®I, one_pos.trans_eq I_im.symm‚ü©

instance : loc_path_connected_space ‚Ñç :=
loc_path_connected_of_is_open $ is_open_lt continuous_const complex.continuous_im

instance : noncompact_space ‚Ñç :=
begin
  refine ‚ü®Œª h, _‚ü©,
  have : is_compact (complex.im ‚Åª¬π' Ioi 0), from is_compact_iff_is_compact_univ.2 h,
  replace := this.is_closed.closure_eq,
  rw [closure_preimage_im, closure_Ioi, set.ext_iff] at this,
  exact absurd ((this 0).1 left_mem_Ici) (lt_irrefl _)
end

instance : locally_compact_space ‚Ñç := open_embedding_coe.locally_compact_space

instance upper_half_plane.charted_space : charted_space ‚ÑÇ ‚Ñç :=
upper_half_plane.open_embedding_coe.singleton_charted_space

instance upper_half_plane.smooth_manifold_with_corners : smooth_manifold_with_corners ùìò(‚ÑÇ) ‚Ñç :=
upper_half_plane.open_embedding_coe.singleton_smooth_manifold_with_corners ùìò(‚ÑÇ)

lemma mdifferentiable_coe : mdifferentiable ùìò(‚ÑÇ) ùìò(‚ÑÇ) (coe : ‚Ñç ‚Üí ‚ÑÇ) :=
mdifferentiable_open_embedding ùìò(‚ÑÇ) upper_half_plane.open_embedding_coe

end upper_half_plane
