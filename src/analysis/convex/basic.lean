/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudriashov, YaÃ«l Dillies
-/
import algebra.order.smul
import data.set.intervals.image_preimage
import linear_algebra.affine_space.affine_map
import order.closure

/-!
# Convex sets and functions in vector spaces

In a ğ•œ-vector space, we define the following objects and properties.
* `segment ğ•œ x y`: Closed segment joining `x` and `y`.
* `open_segment ğ•œ x y`: Open segment joining `x` and `y`.
* `convex ğ•œ s`: A set `s` is convex if for any two points `x y âˆˆ s` it includes `segment ğ•œ x y`.
* `convex_hull ğ•œ s`: The minimal convex set that includes `s`. In order theory speak, this is a
  closure operator.
* Standard simplex `std_simplex Î¹ [fintype Î¹]` is the intersection of the positive quadrant with
  the hyperplane `s.sum = 1` in the space `Î¹ â†’ ğ•œ`.

We also provide various equivalent versions of the definitions above, prove that some specific sets
are convex.

## Notations

We provide the following notation:
* `[x -[ğ•œ] y] = segment ğ•œ x y` in locale `convex`

## Implementation notes

`convex_hull` is defined as a closure operator. This gives access to the `closure_operator` API
while the impact on writing code is minimal as `convex_hull s` is automatically elaborated as
`â‡‘convex_hull s`.

## TODO

Generalize all this file to affine spaces.

Should we rename `segment` and `open_segment` to `convex.Icc` and `convex.Ioo`? Should we also
define `clopen_segment`/`convex.Ico`/`convex.Ioc`?
-/

universes u u'
variables (ğ•œ : Type*) {E F : Type*}

open linear_map set
open_locale big_operators classical pointwise

/-! ### Segment -/

/-- Segments in a vector space. -/
def segment [add_comm_monoid E] [ordered_semiring ğ•œ] [has_scalar ğ•œ E] (x y : E) : set E :=
{z : E | âˆƒ (a b : ğ•œ) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1), a â€¢ x + b â€¢ y = z}

/-- Open segment in a vector space. Note that `open_segment ğ•œ x x = {x}` instead of being `âˆ…` when
the base semiring has some element between `0` and `1`. -/
def open_segment [add_comm_monoid E] [ordered_semiring ğ•œ] [has_scalar ğ•œ E] (x y : E) : set E :=
{z : E | âˆƒ (a b : ğ•œ) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1), a â€¢ x + b â€¢ y = z}

localized "notation `[` x ` -[` ğ•œ `] ` y `]` := segment ğ•œ x y" in convex

section ordered_semiring
variables [add_comm_monoid E] [ordered_semiring ğ•œ] [module ğ•œ E]

lemma segment_symm (x y : E) : [x -[ğ•œ] y] = [y -[ğ•œ] x] :=
set.ext $ Î» z,
âŸ¨Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
  Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©

lemma open_segment_symm (x y : E) :
  open_segment ğ•œ x y = open_segment ğ•œ y x :=
set.ext $ Î» z,
âŸ¨Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
  Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©

lemma left_mem_segment (x y : E) : x âˆˆ [x -[ğ•œ] y] :=
âŸ¨1, 0, zero_le_one, le_refl 0, add_zero 1, by rw [zero_smul, one_smul, add_zero]âŸ©

lemma right_mem_segment (x y : E) : y âˆˆ [x -[ğ•œ] y] :=
segment_symm ğ•œ y x â–¸ left_mem_segment ğ•œ y x

lemma segment_same (x : E) : [x -[ğ•œ] x] = {x} :=
set.ext $ Î» z, âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
  by simpa only [(add_smul _ _ _).symm, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
  Î» h, mem_singleton_iff.1 h â–¸ left_mem_segment ğ•œ z zâŸ©

lemma open_segment_subset_segment (x y : E) :
  open_segment ğ•œ x y âŠ† [x -[ğ•œ] y] :=
Î» z âŸ¨a, b, ha, hb, hab, hzâŸ©, âŸ¨a, b, ha.le, hb.le, hab, hzâŸ©

lemma mem_open_segment_of_ne_left_right {x y z : E} (hx : x â‰  z) (hy : y â‰  z)
  (hz : z âˆˆ [x -[ğ•œ] y]) :
  z âˆˆ open_segment ğ•œ x y :=
begin
  obtain âŸ¨a, b, ha, hb, hab, hzâŸ© := hz,
    by_cases ha' : a = 0,
  { rw [ha', zero_add] at hab,
    rw [ha', hab, zero_smul, one_smul, zero_add] at hz,
    exact (hy hz).elim },
  by_cases hb' : b = 0,
  { rw [hb', add_zero] at hab,
    rw [hb', hab, zero_smul, one_smul, add_zero] at hz,
    exact (hx hz).elim },
  exact âŸ¨a, b, ha.lt_of_ne (ne.symm ha'), hb.lt_of_ne (ne.symm hb'), hab, hzâŸ©,
end

variables {ğ•œ}

lemma open_segment_subset_iff_segment_subset {x y : E} {s : set E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
  open_segment ğ•œ x y âŠ† s â†” [x -[ğ•œ] y] âŠ† s :=
begin
  refine âŸ¨Î» h z hz, _, (open_segment_subset_segment ğ•œ x y).transâŸ©,
  obtain rfl | hxz := eq_or_ne x z,
  { exact hx },
  obtain rfl | hyz := eq_or_ne y z,
  { exact hy },
  exact h (mem_open_segment_of_ne_left_right ğ•œ hxz hyz hz),
end

lemma convex.combo_self {x y : ğ•œ} (h : x + y = 1) (a : E) : x â€¢ a + y â€¢ a = a :=
by rw [â†add_smul, h, one_smul]

end ordered_semiring

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [module ğ•œ E] [add_comm_monoid F] [module ğ•œ F]

section densely_ordered
variables [nontrivial ğ•œ] [densely_ordered ğ•œ]

@[simp] lemma open_segment_same (x : E) :
  open_segment ğ•œ x x = {x} :=
set.ext $ Î» z, âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
  by simpa only [â† add_smul, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
  Î» (h : z = x), begin
    obtain âŸ¨a, haâ‚€, haâ‚âŸ© := densely_ordered.dense (0 : ğ•œ) 1 zero_lt_one,
    refine âŸ¨a, 1 - a, haâ‚€, sub_pos_of_lt haâ‚, add_sub_cancel'_right _ _, _âŸ©,
    rw [â†add_smul, add_sub_cancel'_right, one_smul, h],
  endâŸ©

end densely_ordered

lemma segment_eq_image (x y : E) : [x -[ğ•œ] y] = (Î» Î¸ : ğ•œ, (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Icc (0 : ğ•œ) 1 :=
set.ext $ Î» z,
  âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
    âŸ¨b, âŸ¨hb, hab â–¸ le_add_of_nonneg_left haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel]âŸ©,
    Î» âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ©, âŸ¨1-Î¸, Î¸, sub_nonneg.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©

lemma segment_eq_imageâ‚‚ (x y : E) :
  [x -[ğ•œ] y] = (Î» p : ğ•œ Ã— ğ•œ, p.1 â€¢ x + p.2 â€¢ y) '' {p | 0 â‰¤ p.1 âˆ§ 0 â‰¤ p.2 âˆ§ p.1 + p.2 = 1} :=
by simp only [segment, image, prod.exists, mem_set_of_eq, exists_prop, and_assoc]

lemma open_segment_eq_image (x y : E) :
  open_segment ğ•œ x y = (Î» (Î¸ : ğ•œ), (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Ioo (0 : ğ•œ) 1 :=
set.ext $ Î» z,
  âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
    âŸ¨b, âŸ¨hb, hab â–¸ lt_add_of_pos_left _ haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel]âŸ©,
    Î» âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ©, âŸ¨1 - Î¸, Î¸, sub_pos.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©

lemma open_segment_eq_imageâ‚‚ (x y : E) :
  open_segment ğ•œ x y =
    (Î» p : ğ•œ Ã— ğ•œ, p.1 â€¢ x + p.2 â€¢ y) '' {p | 0 < p.1 âˆ§ 0 < p.2 âˆ§ p.1 + p.2 = 1} :=
by simp only [open_segment, image, prod.exists, mem_set_of_eq, exists_prop, and_assoc]

lemma segment_image (f : E â†’â‚—[ğ•œ] F) (a b : E) : f '' [a -[ğ•œ] b] = [f a -[ğ•œ] f b] :=
set.ext (Î» x, by simp_rw [segment_eq_image, mem_image, exists_exists_and_eq_and, map_add, map_smul])

@[simp] lemma open_segment_image (f : E â†’â‚—[ğ•œ] F) (a b : E) :
  f '' open_segment ğ•œ a b = open_segment ğ•œ (f a) (f b) :=
set.ext (Î» x, by simp_rw [open_segment_eq_image, mem_image, exists_exists_and_eq_and, map_add,
  map_smul])

end add_comm_monoid

section add_comm_group
variables [add_comm_group E] [module ğ•œ E]

lemma segment_eq_image' (x y : E) :
  [x -[ğ•œ] y] = (Î» (Î¸ : ğ•œ), x + Î¸ â€¢ (y - x)) '' Icc (0 : ğ•œ) 1 :=
by { convert segment_eq_image ğ•œ x y, ext Î¸, simp only [smul_sub, sub_smul, one_smul], abel }

lemma open_segment_eq_image' (x y : E) :
  open_segment ğ•œ x y = (Î» (Î¸ : ğ•œ), x + Î¸ â€¢ (y - x)) '' Ioo (0 : ğ•œ) 1 :=
by { convert open_segment_eq_image ğ•œ x y, ext Î¸, simp only [smul_sub, sub_smul, one_smul], abel }

lemma mem_segment_translate (a : E) {x b c} : a + x âˆˆ [a + b -[ğ•œ] a + c] â†” x âˆˆ [b -[ğ•œ] c] :=
begin
  rw [segment_eq_image', segment_eq_image'],
  refine exists_congr (Î» Î¸, and_congr iff.rfl _),
  simp only [add_sub_add_left_eq_sub, add_assoc, add_right_inj],
end

@[simp] lemma mem_open_segment_translate (a : E) {x b c : E} :
  a + x âˆˆ open_segment ğ•œ (a + b) (a + c) â†” x âˆˆ open_segment ğ•œ b c :=
begin
  rw [open_segment_eq_image', open_segment_eq_image'],
  refine exists_congr (Î» Î¸, and_congr iff.rfl _),
  simp only [add_sub_add_left_eq_sub, add_assoc, add_right_inj],
end

lemma segment_translate_preimage (a b c : E) : (Î» x, a + x) â»Â¹' [a + b -[ğ•œ] a + c] = [b -[ğ•œ] c] :=
set.ext $ Î» x, mem_segment_translate ğ•œ a

lemma open_segment_translate_preimage (a b c : E) :
  (Î» x, a + x) â»Â¹' open_segment ğ•œ (a + b) (a + c) = open_segment ğ•œ b c :=
set.ext $ Î» x, mem_open_segment_translate ğ•œ a

lemma segment_translate_image (a b c : E) : (Î» x, a + x) '' [b -[ğ•œ] c] = [a + b -[ğ•œ] a + c] :=
segment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ $ add_left_surjective a

lemma open_segment_translate_image (a b c : E) :
  (Î» x, a + x) '' open_segment ğ•œ b c = open_segment ğ•œ (a + b) (a + c) :=
open_segment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ $ add_left_surjective a

end add_comm_group
end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] [add_comm_group F] [module ğ•œ F] {ğ•œ}

@[simp] lemma left_mem_open_segment_iff [no_zero_smul_divisors ğ•œ E] {x y : E} :
  x âˆˆ open_segment ğ•œ x y â†” x = y :=
begin
  split,
  { rintro âŸ¨a, b, ha, hb, hab, hxâŸ©,
    refine smul_right_injective _ hb.ne' ((add_right_inj (a â€¢ x)).1 _),
    rw [hx, â†add_smul, hab, one_smul] },
  { rintro rfl,
    rw open_segment_same,
    exact mem_singleton _ }
end

@[simp] lemma right_mem_open_segment_iff {x y : E} :
  y âˆˆ open_segment ğ•œ x y â†” x = y :=
by rw [open_segment_symm, left_mem_open_segment_iff, eq_comm]

end add_comm_group
end linear_ordered_field

/-!
#### Segments in an ordered space
Relates `segment`, `open_segment` and `set.Icc`, `set.Ico`, `set.Ioc`, `set.Ioo`
-/
section ordered_semiring
variables [ordered_semiring ğ•œ]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E] {ğ•œ}

lemma segment_subset_Icc {x y : E} (h : x â‰¤ y) : [x -[ğ•œ] y] âŠ† Icc x y :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  split,
  calc
    x   = a â€¢ x + b â€¢ x : by rw [â†add_smul, hab, one_smul]
    ... â‰¤ a â€¢ x + b â€¢ y : add_le_add_left (smul_le_smul_of_nonneg h hb) _,
  calc
    a â€¢ x + b â€¢ y
        â‰¤ a â€¢ y + b â€¢ y : add_le_add_right (smul_le_smul_of_nonneg h ha) _
    ... = y : by rw [â†add_smul, hab, one_smul],
end

end ordered_add_comm_monoid

section ordered_cancel_add_comm_monoid
variables [ordered_cancel_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E] {ğ•œ}

lemma open_segment_subset_Ioo {x y : E} (h : x < y) : open_segment ğ•œ x y âŠ† Ioo x y :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  split,
  calc
    x   = a â€¢ x + b â€¢ x : by rw [â†add_smul, hab, one_smul]
    ... < a â€¢ x + b â€¢ y : add_lt_add_left (smul_lt_smul_of_pos h hb) _,
  calc
    a â€¢ x + b â€¢ y
        < a â€¢ y + b â€¢ y : add_lt_add_right (smul_lt_smul_of_pos h ha) _
    ... = y : by rw [â†add_smul, hab, one_smul],
end

end ordered_cancel_add_comm_monoid
end ordered_semiring

section linear_ordered_field
variables [linear_ordered_field ğ•œ] {ğ•œ}

lemma Icc_subset_segment {x y : ğ•œ} : Icc x y âŠ† [x -[ğ•œ] y] :=
begin
  rintro z âŸ¨hxz, hyzâŸ©,
  obtain rfl | h := (hxz.trans hyz).eq_or_lt,
  { rw segment_same,
    exact hyz.antisymm hxz },
  rw â†sub_nonneg at hxz hyz,
  rw â†sub_pos at h,
  refine âŸ¨(y - z) / (y - x), (z - x) / (y - x), div_nonneg hyz h.le, div_nonneg hxz h.le, _, _âŸ©,
  { rw [â†add_div, sub_add_sub_cancel, div_self h.ne'] },
  { rw [smul_eq_mul, smul_eq_mul, â†mul_div_right_comm, â†mul_div_right_comm, â†add_div,
      div_eq_iff h.ne', add_comm, sub_mul, sub_mul, mul_comm x, sub_add_sub_cancel, mul_sub] }
end

@[simp] lemma segment_eq_Icc {x y : ğ•œ} (h : x â‰¤ y) : [x -[ğ•œ] y] = Icc x y :=
(segment_subset_Icc h).antisymm Icc_subset_segment

lemma Ioo_subset_open_segment {x y : ğ•œ} : Ioo x y âŠ† open_segment ğ•œ x y :=
Î» z hz, mem_open_segment_of_ne_left_right _ hz.1.ne hz.2.ne'
    (Icc_subset_segment $ Ioo_subset_Icc_self hz)

@[simp] lemma open_segment_eq_Ioo {x y : ğ•œ} (h : x < y) : open_segment ğ•œ x y = Ioo x y :=
(open_segment_subset_Ioo h).antisymm Ioo_subset_open_segment

lemma segment_eq_Icc' (x y : ğ•œ) : [x -[ğ•œ] y] = Icc (min x y) (max x y) :=
begin
  cases le_total x y,
  { rw [segment_eq_Icc h, max_eq_right h, min_eq_left h] },
  { rw [segment_symm, segment_eq_Icc h, max_eq_left h, min_eq_right h] }
end

lemma open_segment_eq_Ioo' {x y : ğ•œ} (hxy : x â‰  y) :
  open_segment ğ•œ x y = Ioo (min x y) (max x y) :=
begin
  cases hxy.lt_or_lt,
  { rw [open_segment_eq_Ioo h, max_eq_right h.le, min_eq_left h.le] },
  { rw [open_segment_symm, open_segment_eq_Ioo h, max_eq_left h.le, min_eq_right h.le] }
end

lemma segment_eq_interval (x y : ğ•œ) : [x -[ğ•œ] y] = interval x y :=
segment_eq_Icc' _ _

/-- A point is in an `Icc` iff it can be expressed as a convex combination of the endpoints. -/
lemma convex.mem_Icc {x y : ğ•œ} (h : x â‰¤ y) {z : ğ•œ} :
  z âˆˆ Icc x y â†” âˆƒ (a b : ğ•œ), 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  rw â†segment_eq_Icc h,
  simp_rw [â†exists_prop],
  refl,
end

/-- A point is in an `Ioo` iff it can be expressed as a strict convex combination of the endpoints.
-/
lemma convex.mem_Ioo {x y : ğ•œ} (h : x < y) {z : ğ•œ} :
  z âˆˆ Ioo x y â†” âˆƒ (a b : ğ•œ), 0 < a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  rw â†open_segment_eq_Ioo h,
  simp_rw [â†exists_prop],
  refl,
end

/-- A point is in an `Ioc` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
lemma convex.mem_Ioc {x y : ğ•œ} (h : x < y) {z : ğ•œ} :
  z âˆˆ Ioc x y â†” âˆƒ (a b : ğ•œ), 0 â‰¤ a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  split,
  { rintro hz,
    obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (convex.mem_Icc h.le).1 (Ioc_subset_Icc_self hz),
    obtain rfl | hb' := hb.eq_or_lt,
    { rw add_zero at hab,
      rw [hab, one_mul, zero_mul, add_zero] at hz,
      exact (hz.1.ne rfl).elim },
    { exact âŸ¨a, b, ha, hb', hab, rflâŸ© } },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    obtain rfl | ha' := ha.eq_or_lt,
    { rw zero_add at hab,
      rwa [hab, one_mul, zero_mul, zero_add, right_mem_Ioc] },
    { exact Ioo_subset_Ioc_self ((convex.mem_Ioo h).2 âŸ¨a, b, ha', hb, hab, rflâŸ©) } }
end

/-- A point is in an `Ico` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
lemma convex.mem_Ico {x y : ğ•œ} (h : x < y) {z : ğ•œ} :
  z âˆˆ Ico x y â†” âˆƒ (a b : ğ•œ), 0 < a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  split,
  { rintro hz,
    obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (convex.mem_Icc h.le).1 (Ico_subset_Icc_self hz),
    obtain rfl | ha' := ha.eq_or_lt,
    { rw zero_add at hab,
      rw [hab, one_mul, zero_mul, zero_add] at hz,
      exact (hz.2.ne rfl).elim },
    { exact âŸ¨a, b, ha', hb, hab, rflâŸ© } },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    obtain rfl | hb' := hb.eq_or_lt,
    { rw add_zero at hab,
      rwa [hab, one_mul, zero_mul, add_zero, left_mem_Ico] },
    { exact Ioo_subset_Ico_self ((convex.mem_Ioo h).2 âŸ¨a, b, ha, hb', hab, rflâŸ©) } }
end

end linear_ordered_field

/-! ### Convexity of sets -/

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E]

/-- Convexity of sets. -/
def convex [has_scalar ğ•œ E] (s : set E) :=
âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
  a â€¢ x + b â€¢ y âˆˆ s

variables {ğ•œ} [module ğ•œ E] [add_comm_monoid F] [module ğ•œ F] {s : set E}

lemma convex_iff_forall_pos :
  convex ğ•œ s â†” âˆ€ â¦ƒx yâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1
  â†’ a â€¢ x + b â€¢ y âˆˆ s :=
begin
  refine âŸ¨Î» h x y hx hy a b ha hb hab, h hx hy ha.le hb.le hab, _âŸ©,
  intros h x y hx hy a b ha hb hab,
  cases ha.eq_or_lt with ha ha,
  { subst a, rw [zero_add] at hab, simp [hab, hy] },
  cases hb.eq_or_lt with hb hb,
  { subst b, rw [add_zero] at hab, simp [hab, hx] },
  exact h hx hy ha hb hab
end

lemma convex_iff_segment_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒx yâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ [x -[ğ•œ] y] âŠ† s :=
begin
  split,
  { rintro h x y hx hy z âŸ¨a, b, ha, hb, hab, rflâŸ©,
    exact h hx hy ha hb hab },
  { rintro h x y hx hy a b ha hb hab,
    exact h hx hy âŸ¨a, b, ha, hb, hab, rflâŸ© }
end

lemma convex_iff_open_segment_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒx yâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ open_segment ğ•œ x y âŠ† s :=
begin
  rw convex_iff_segment_subset,
  exact forallâ‚‚_congr (Î» x y, forallâ‚‚_congr $ Î» hx hy,
    (open_segment_subset_iff_segment_subset hx hy).symm),
end

lemma convex.segment_subset (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
  [x -[ğ•œ] y] âŠ† s :=
convex_iff_segment_subset.1 h hx hy

lemma convex.open_segment_subset (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
  open_segment ğ•œ x y âŠ† s :=
convex_iff_open_segment_subset.1 h hx hy

/-- Alternative definition of set convexity, in terms of pointwise set operations. -/
lemma convex_iff_pointwise_add_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ s + b â€¢ s âŠ† s :=
iff.intro
  begin
    rintro hA a b ha hb hab w âŸ¨au, bv, âŸ¨u, hu, rflâŸ©, âŸ¨v, hv, rflâŸ©, rflâŸ©,
    exact hA hu hv ha hb hab
  end
  (Î» h x y hx hy a b ha hb hab,
    (h ha hb hab) (set.add_mem_add âŸ¨_, hx, rflâŸ© âŸ¨_, hy, rflâŸ©))

lemma convex_empty : convex ğ•œ (âˆ… : set E) := by finish

lemma convex_singleton (c : E) : convex ğ•œ ({c} : set E) :=
begin
  intros x y hx hy a b ha hb hab,
  rw [set.eq_of_mem_singleton hx, set.eq_of_mem_singleton hy, â†add_smul, hab, one_smul],
  exact mem_singleton c
end

lemma convex_univ : convex ğ•œ (set.univ : set E) := Î» _ _ _ _ _ _ _ _ _, trivial

lemma convex.inter {t : set E} (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s âˆ© t) :=
Î» x y (hx : x âˆˆ s âˆ© t) (hy : y âˆˆ s âˆ© t) a b (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1),
  âŸ¨hs hx.left hy.left ha hb hab, ht hx.right hy.right ha hb habâŸ©

lemma convex_sInter {S : set (set E)} (h : âˆ€ s âˆˆ S, convex ğ•œ s) : convex ğ•œ (â‹‚â‚€ S) :=
assume x y hx hy a b ha hb hab s hs,
h s hs (hx s hs) (hy s hs) ha hb hab

lemma convex_Inter {Î¹ : Sort*} {s : Î¹ â†’ set E} (h : âˆ€ i : Î¹, convex ğ•œ (s i)) :
  convex ğ•œ (â‹‚ i, s i) :=
(sInter_range s) â–¸ convex_sInter $ forall_range_iff.2 h

lemma convex.prod {s : set E} {t : set F} (hs : convex ğ•œ s) (ht : convex ğ•œ t) :
  convex ğ•œ (s.prod t) :=
begin
  intros x y hx hy a b ha hb hab,
  apply mem_prod.2,
  exact âŸ¨hs (mem_prod.1 hx).1 (mem_prod.1 hy).1 ha hb hab,
        ht (mem_prod.1 hx).2 (mem_prod.1 hy).2 ha hb habâŸ©
end

lemma directed.convex_Union {Î¹ : Sort*} {s : Î¹ â†’ set E} (hdir : directed has_subset.subset s)
  (hc : âˆ€ â¦ƒi : Î¹â¦„, convex ğ•œ (s i)) :
  convex ğ•œ (â‹ƒ i, s i) :=
begin
  rintro x y hx hy a b ha hb hab,
  rw mem_Union at âŠ¢ hx hy,
  obtain âŸ¨i, hxâŸ© := hx,
  obtain âŸ¨j, hyâŸ© := hy,
  obtain âŸ¨k, hik, hjkâŸ© := hdir i j,
  exact âŸ¨k, hc (hik hx) (hjk hy) ha hb habâŸ©,
end

lemma directed_on.convex_sUnion {c : set (set E)} (hdir : directed_on has_subset.subset c)
  (hc : âˆ€ â¦ƒA : set Eâ¦„, A âˆˆ c â†’ convex ğ•œ A) :
  convex ğ•œ (â‹ƒâ‚€c) :=
begin
  rw sUnion_eq_Union,
  exact (directed_on_iff_directed.1 hdir).convex_Union (Î» A, hc A.2),
end

lemma convex.linear_image (hs : convex ğ•œ s) (f : E â†’â‚—[ğ•œ]  F) : convex ğ•œ (s.image f) :=
begin
  intros x y hx hy a b ha hb hab,
  obtain âŸ¨x', hx', rflâŸ© := mem_image_iff_bex.1 hx,
  obtain âŸ¨y', hy', rflâŸ© := mem_image_iff_bex.1 hy,
  exact âŸ¨a â€¢ x' + b â€¢ y', hs hx' hy' ha hb hab, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©,
end

lemma convex.is_linear_image (hs : convex ğ•œ s) {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  convex ğ•œ (f '' s) :=
hs.linear_image $ hf.mk' f

lemma convex.linear_preimage {s : set F} (hs : convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) :
  convex ğ•œ (s.preimage f) :=
begin
  intros x y hx hy a b ha hb hab,
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul],
  exact hs hx hy ha hb hab,
end

lemma convex.is_linear_preimage {s : set F} (hs : convex ğ•œ s) {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  convex ğ•œ (preimage f s) :=
hs.linear_preimage $ hf.mk' f

lemma convex.add {t : set E}  (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s + t) :=
by { rw â† add_image_prod, exact (hs.prod ht).is_linear_image is_linear_map.is_linear_map_add }

lemma convex.translate (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, z + x) '' s) :=
begin
  intros x y hx hy a b ha hb hab,
  obtain âŸ¨x', hx', rflâŸ© := mem_image_iff_bex.1 hx,
  obtain âŸ¨y', hy', rflâŸ© := mem_image_iff_bex.1 hy,
  refine âŸ¨a â€¢ x' + b â€¢ y', hs hx' hy' ha hb hab, _âŸ©,
  rw [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul],
end

/-- The translation of a convex set is also convex. -/
lemma convex.translate_preimage_right (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, z + x) â»Â¹' s) :=
begin
  intros x y hx hy a b ha hb hab,
  have h := hs hx hy ha hb hab,
  rwa [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul] at h,
end

/-- The translation of a convex set is also convex. -/
lemma convex.translate_preimage_left (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, x + z) â»Â¹' s) :=
by simpa only [add_comm] using hs.translate_preimage_right z

lemma convex_Iio (r : ğ•œ) : convex ğ•œ (Iio r) :=
begin
  intros x y hx hy a b ha hb hab,
  obtain rfl | ha' := ha.eq_or_lt,
  { rw zero_add at hab,
    rwa [zero_smul, zero_add, hab, one_smul] },
  rw [smul_eq_mul, smul_eq_mul],
  rw mem_Iio at hx hy,
  calc
    a * x + b * y
        < a * r + b * r : add_lt_add_of_lt_of_le (mul_lt_mul_of_pos_left hx ha')
          (mul_le_mul_of_nonneg_left hy.le hb)
  ... = r : by rw [â†add_mul, hab, one_mul]
end

lemma convex_Ioi (r : ğ•œ) : convex ğ•œ (Ioi r) :=
begin
  intros x y hx hy a b ha hb hab,
  obtain rfl | ha' := ha.eq_or_lt,
  { rw zero_add at hab,
    rwa [zero_smul, zero_add, hab, one_smul] },
  rw [smul_eq_mul, smul_eq_mul],
  rw mem_Ioi at hx hy,
  calc
    r   = a * r + b * r : by rw [â†add_mul, hab, one_mul]
    ... < a * x + b * y : add_lt_add_of_lt_of_le (mul_lt_mul_of_pos_left hx ha')
          (mul_le_mul_of_nonneg_left hy.le hb),
end

lemma convex_Iic (r : ğ•œ) : convex ğ•œ (Iic r) :=
Î» x y hx hy a b ha hb hab,
calc
  a * x + b * y
      â‰¤ a * r + b * r
      : add_le_add (mul_le_mul_of_nonneg_left hx ha) (mul_le_mul_of_nonneg_left hy hb)
  ... = r : by rw [â†add_mul, hab, one_mul]

lemma convex_Ici (r : ğ•œ) : convex ğ•œ (Ici r) :=
Î» x y hx hy a b ha hb hab,
calc
  r   = a * r + b * r : by rw [â†add_mul, hab, one_mul]
  ... â‰¤ a * x + b * y
      : add_le_add (mul_le_mul_of_nonneg_left hx ha) (mul_le_mul_of_nonneg_left hy hb)

lemma convex_Ioo (r s : ğ•œ) : convex ğ•œ (Ioo r s) :=
Ioi_inter_Iio.subst ((convex_Ioi r).inter $ convex_Iio s)

lemma convex_Ico (r s : ğ•œ) : convex ğ•œ (Ico r s) :=
Ici_inter_Iio.subst ((convex_Ici r).inter $ convex_Iio s)

lemma convex_Ioc (r s : ğ•œ) : convex ğ•œ (Ioc r s) :=
Ioi_inter_Iic.subst ((convex_Ioi r).inter $ convex_Iic s)

lemma convex_Icc (r s : ğ•œ) : convex ğ•œ (Icc r s) :=
Ici_inter_Iic.subst ((convex_Ici r).inter $ convex_Iic s)

end add_comm_monoid

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] {ğ•œ} {s t : set E}

lemma convex.combo_eq_vadd {a b : ğ•œ} {x y : E} (h : a + b = 1) :
  a â€¢ x + b â€¢ y = b â€¢ (y - x) + x :=
calc
  a â€¢ x + b â€¢ y = (b â€¢ y - b â€¢ x) + (a â€¢ x + b â€¢ x) : by abel
            ... = b â€¢ (y - x) + x                   : by rw [smul_sub, â†add_smul, h, one_smul]

lemma convex.sub (hs : convex ğ•œ s) (ht : convex ğ•œ t) :
  convex ğ•œ ((Î» x : E Ã— E, x.1 - x.2) '' (s.prod t)) :=
(hs.prod ht).is_linear_image is_linear_map.is_linear_map_sub

lemma convex_segment (x y : E) : convex ğ•œ [x -[ğ•œ]  y] :=
begin
  rintro p q âŸ¨ap, bp, hap, hbp, habp, rflâŸ© âŸ¨aq, bq, haq, hbq, habq, rflâŸ© a b ha hb hab,
  refine âŸ¨a * ap + b * aq, a * bp + b * bq,
    add_nonneg (mul_nonneg ha hap) (mul_nonneg hb haq),
    add_nonneg (mul_nonneg ha hbp) (mul_nonneg hb hbq), _, _âŸ©,
  { rw [add_add_add_comm, â†mul_add, â†mul_add, habp, habq, mul_one, mul_one, hab] },
  { simp_rw [add_smul, mul_smul, smul_add],
    exact add_add_add_comm _ _ _ _ }
end

lemma convex_open_segment (a b : E) : convex ğ•œ (open_segment ğ•œ a b) :=
begin
  rw convex_iff_open_segment_subset,
  rintro p q âŸ¨ap, bp, hap, hbp, habp, rflâŸ© âŸ¨aq, bq, haq, hbq, habq, rflâŸ© z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  refine âŸ¨a * ap + b * aq, a * bp + b * bq,
    add_pos (mul_pos ha hap) (mul_pos hb haq),
    add_pos (mul_pos ha hbp) (mul_pos hb hbq), _, _âŸ©,
  { rw [add_add_add_comm, â†mul_add, â†mul_add, habp, habq, mul_one, mul_one, hab] },
  { simp_rw [add_smul, mul_smul, smul_add],
    exact add_add_add_comm _ _ _ _ }
end

lemma convex_halfspace_lt {f : E â†’ ğ•œ} (h : is_linear_map ğ•œ f) (r : ğ•œ) :
  convex ğ•œ {w | f w < r} :=
(convex_Iio r).is_linear_preimage h

lemma convex_halfspace_le {f : E â†’ ğ•œ} (h : is_linear_map ğ•œ f) (r : ğ•œ) :
  convex ğ•œ {w | f w â‰¤ r} :=
(convex_Iic r).is_linear_preimage h

lemma convex_halfspace_gt {f : E â†’ ğ•œ} (h : is_linear_map ğ•œ f) (r : ğ•œ) :
  convex ğ•œ {w | r < f w} :=
(convex_Ioi r).is_linear_preimage h

lemma convex_halfspace_ge {f : E â†’ ğ•œ} (h : is_linear_map ğ•œ f) (r : ğ•œ) :
  convex ğ•œ {w | r â‰¤ f w} :=
(convex_Ici r).is_linear_preimage h

lemma convex_hyperplane {f : E â†’ ğ•œ} (h : is_linear_map ğ•œ f) (r : ğ•œ) :
  convex ğ•œ {w | f w = r} :=
begin
  simp_rw le_antisymm_iff,
  exact (convex_halfspace_le h r).inter (convex_halfspace_ge h r),
end

end add_comm_group
end ordered_semiring

section linear_ordered_semiring
variables [linear_ordered_semiring ğ•œ] {ğ•œ}

lemma convex_interval (r s : ğ•œ) : convex ğ•œ (interval r s) :=
convex_Icc _ _

end linear_ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [module ğ•œ E] [add_comm_monoid F] [module ğ•œ F] {ğ•œ} {s : set E}

lemma convex.smul (hs : convex ğ•œ s) (c : ğ•œ) : convex ğ•œ (c â€¢ s) :=
hs.linear_image (linear_map.lsmul _ _ c)

lemma convex.smul_preimage (hs : convex ğ•œ s) (c : ğ•œ) : convex ğ•œ ((Î» z, c â€¢ z) â»Â¹' s) :=
hs.linear_preimage (linear_map.lsmul _ _ c)

lemma convex.affinity (hs : convex ğ•œ s) (z : E) (c : ğ•œ) : convex ğ•œ ((Î» x, z + c â€¢ x) '' s) :=
begin
  have h := (hs.smul c).translate z,
  rwa [â†image_smul, image_image] at h,
end

end add_comm_monoid
end ordered_comm_semiring

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [module ğ•œ E] [add_comm_monoid F] [module ğ•œ F] {ğ•œ} {s : set E}

lemma convex.add_smul_mem (hs : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : x + y âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ y âˆˆ s :=
begin
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y),
  { rw [smul_add, â†add_assoc, â†add_smul, sub_add_cancel, one_smul] },
  rw h,
  exact hs hx hy (sub_nonneg_of_le ht.2) ht.1 (sub_add_cancel _ _),
end

lemma convex.smul_mem_of_zero_mem (hs : convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s) (hx : x âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : t â€¢ x âˆˆ s :=
by simpa using hs.add_smul_mem zero_mem (by simpa using hx) ht

end add_comm_monoid

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] [add_comm_group F] [module ğ•œ F] {ğ•œ} {s : set E}

lemma convex.add_smul_sub_mem (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ (y - x) âˆˆ s :=
begin
  apply h.segment_subset hx hy,
  rw segment_eq_image',
  exact mem_image_of_mem _ ht,
end

/--
Applying an affine map to an affine combination of two points yields
an affine combination of the images.
-/
lemma convex.combo_affine_apply {a b : ğ•œ} {x y : E} {f : E â†’áµƒ[ğ•œ]  F} (h : a + b = 1) :
  f (a â€¢ x + b â€¢ y) = a â€¢ f x + b â€¢ f y :=
begin
  simp only [convex.combo_eq_vadd h, â† vsub_eq_sub],
  exact f.apply_line_map _ _ _,
end

/-- The preimage of a convex set under an affine map is convex. -/
lemma convex.affine_preimage (f : E â†’áµƒ[ğ•œ]  F) {s : set F} (hs : convex ğ•œ s) :
  convex ğ•œ (f â»Â¹' s) :=
begin
  intros x y xs ys a b ha hb hab,
  rw [mem_preimage, convex.combo_affine_apply hab],
  exact hs xs ys ha hb hab,
end

/-- The image of a convex set under an affine map is convex. -/
lemma convex.affine_image (f : E â†’áµƒ[ğ•œ]  F) {s : set E} (hs : convex ğ•œ s) :
  convex ğ•œ (f '' s) :=
begin
  rintro x y âŸ¨x', âŸ¨hx', hx'fâŸ©âŸ© âŸ¨y', âŸ¨hy', hy'fâŸ©âŸ© a b ha hb hab,
  refine âŸ¨a â€¢ x' + b â€¢ y', âŸ¨hs hx' hy' ha hb hab, _âŸ©âŸ©,
  rw [convex.combo_affine_apply hab, hx'f, hy'f]
end

lemma convex.neg (hs : convex ğ•œ s) : convex ğ•œ ((Î» z, -z) '' s) :=
hs.is_linear_image is_linear_map.is_linear_map_neg

lemma convex.neg_preimage (hs : convex ğ•œ s) : convex ğ•œ ((Î» z, -z) â»Â¹' s) :=
hs.is_linear_preimage is_linear_map.is_linear_map_neg

end add_comm_group

end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [module ğ•œ E] [add_comm_monoid F] [module ğ•œ F] {ğ•œ} {s : set E}

/-- Alternative definition of set convexity, using division. -/
lemma convex_iff_div :
  convex ğ•œ s â†” âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„,
    0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’ (a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y âˆˆ s :=
âŸ¨Î» h x y hx hy a b ha hb hab, begin
  apply h hx hy,
  { have ha', from mul_le_mul_of_nonneg_left ha (inv_pos.2 hab).le,
    rwa [mul_zero, â†div_eq_inv_mul] at ha' },
  { have hb', from mul_le_mul_of_nonneg_left hb (inv_pos.2 hab).le,
    rwa [mul_zero, â†div_eq_inv_mul] at hb' },
  { rw â†add_div,
    exact div_self hab.ne' }
end, Î» h x y hx hy a b ha hb hab,
begin
  have h', from h hx hy ha hb,
  rw [hab, div_one, div_one] at h',
  exact h' zero_lt_one
endâŸ©

lemma convex.mem_smul_of_zero_mem (h : convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s)
  (hx : x âˆˆ s) {t : ğ•œ} (ht : 1 â‰¤ t) :
  x âˆˆ t â€¢ s :=
begin
  rw mem_smul_set_iff_inv_smul_mem' (zero_lt_one.trans_le ht).ne',
  exact h.smul_mem_of_zero_mem zero_mem hx âŸ¨inv_nonneg.2 (zero_le_one.trans ht), inv_le_one htâŸ©,
end

lemma convex.add_smul (h_conv : convex ğ•œ s) {p q : ğ•œ} (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) :
  (p + q) â€¢ s = p â€¢ s + q â€¢ s :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { simp_rw [smul_set_empty, add_empty] },
  obtain rfl | hp' := hp.eq_or_lt,
  { rw [zero_add, zero_smul_set hs, zero_add] },
  obtain rfl | hq' := hq.eq_or_lt,
  { rw [add_zero, zero_smul_set hs, add_zero] },
  ext,
  split,
  { rintro âŸ¨v, hv, rflâŸ©,
    exact âŸ¨p â€¢ v, q â€¢ v, smul_mem_smul_set hv, smul_mem_smul_set hv, (add_smul _ _ _).symmâŸ© },
  { rintro âŸ¨vâ‚, vâ‚‚, âŸ¨vâ‚â‚, hâ‚â‚‚, rflâŸ©, âŸ¨vâ‚‚â‚, hâ‚‚â‚‚, rflâŸ©, rflâŸ©,
    have hpq := add_pos hp' hq',
    exact mem_smul_set.2 âŸ¨_, h_conv hâ‚â‚‚ hâ‚‚â‚‚ (div_pos hp' hpq).le (div_pos hq' hpq).le
      (by rw [â†div_self hpq.ne', add_div] : p / (p + q) + q / (p + q) = 1),
      by simp only [â† mul_smul, smul_add, mul_div_cancel' _ hpq.ne']âŸ© }
end

end add_comm_monoid
end linear_ordered_field

/-!
#### Convex sets in an ordered space
Relates `convex` and `ord_connected`.
-/

section
variables {ğ•œ}

lemma set.ord_connected.convex_of_chain [ordered_add_comm_monoid E] [ordered_semiring ğ•œ]
  [module ğ•œ E] [ordered_smul ğ•œ E] {s : set E} (hs : s.ord_connected) (h : zorn.chain (â‰¤) s) :
  convex ğ•œ s :=
begin
  intros x y hx hy a b ha hb hab,
  obtain hxy | hyx := h.total_of_refl hx hy,
  { refine hs.out hx hy (mem_Icc.2 âŸ¨_, _âŸ©),
    calc
      x   = a â€¢ x + b â€¢ x : by rw [â†add_smul, hab, one_smul]
      ... â‰¤ a â€¢ x + b â€¢ y : add_le_add_left (smul_le_smul_of_nonneg hxy hb) _,
    calc
      a â€¢ x + b â€¢ y
          â‰¤ a â€¢ y + b â€¢ y : add_le_add_right (smul_le_smul_of_nonneg hxy ha) _
      ... = y : by rw [â†add_smul, hab, one_smul] },
  { refine hs.out hy hx (mem_Icc.2 âŸ¨_, _âŸ©),
    calc
      y   = a â€¢ y + b â€¢ y : by rw [â†add_smul, hab, one_smul]
      ... â‰¤ a â€¢ x + b â€¢ y : add_le_add_right (smul_le_smul_of_nonneg hyx ha) _,
    calc
      a â€¢ x + b â€¢ y
          â‰¤ a â€¢ x + b â€¢ x : add_le_add_left (smul_le_smul_of_nonneg hyx hb) _
      ... = x : by rw [â†add_smul, hab, one_smul] }
end

lemma set.ord_connected.convex [linear_ordered_add_comm_monoid E] [ordered_semiring ğ•œ]
  [module ğ•œ E] [ordered_smul ğ•œ E] {s : set E} (hs : s.ord_connected) :
  convex ğ•œ s :=
hs.convex_of_chain (zorn.chain_of_trichotomous s)

lemma convex_iff_ord_connected [linear_ordered_field ğ•œ] {s : set ğ•œ} :
  convex ğ•œ s â†” s.ord_connected :=
begin
  simp_rw [convex_iff_segment_subset, segment_eq_interval, ord_connected_iff_interval_subset],
  exact forall_congr (Î» x, forall_swap)
end

alias convex_iff_ord_connected â†” convex.ord_connected _

end

/-! #### Convexity of submodules/subspaces -/

section submodule
variables {ğ•œ}
open submodule

lemma submodule.convex [ordered_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E] (K : submodule ğ•œ E) :
  convex ğ•œ (â†‘K : set E) :=
by { repeat {intro}, refine add_mem _ (smul_mem _ _ _) (smul_mem _ _ _); assumption }

lemma subspace.convex [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E] (K : subspace ğ•œ E) :
  convex ğ•œ (â†‘K : set E) :=
K.convex

end submodule

/-! ### Convex hull -/

section convex_hull
section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [module ğ•œ E] [add_comm_monoid F] [module ğ•œ F]

/-- The convex hull of a set `s` is the minimal convex set that includes `s`. -/
def convex_hull : closure_operator (set E) :=
closure_operator.mkâ‚ƒ
  (Î» s, â‹‚ (t : set E) (hst : s âŠ† t) (ht : convex ğ•œ t), t)
  (convex ğ•œ)
  (Î» s, set.subset_Inter (Î» t, set.subset_Inter $ Î» hst, set.subset_Inter $ Î» ht, hst))
  (Î» s, convex_Inter $ Î» t, convex_Inter $ Î» ht, convex_Inter id)
  (Î» s t hst ht, set.Inter_subset_of_subset t $ set.Inter_subset_of_subset hst $
  set.Inter_subset _ ht)

variables (s : set E)

lemma subset_convex_hull : s âŠ† convex_hull ğ•œ s :=
(convex_hull ğ•œ).le_closure s

lemma convex_convex_hull : convex ğ•œ (convex_hull ğ•œ s) :=
closure_operator.closure_mem_mkâ‚ƒ s

variables {s ğ•œ} {t : set E}

lemma convex_hull_min (hst : s âŠ† t) (ht : convex ğ•œ t) : convex_hull ğ•œ s âŠ† t :=
closure_operator.closure_le_mkâ‚ƒ_iff (show s â‰¤ t, from hst) ht

lemma convex_hull_mono (hst : s âŠ† t) : convex_hull ğ•œ s âŠ† convex_hull ğ•œ t :=
(convex_hull ğ•œ).monotone hst

lemma convex.convex_hull_eq {s : set E} (hs : convex ğ•œ s) : convex_hull ğ•œ s = s :=
closure_operator.mem_mkâ‚ƒ_closed hs

@[simp]
lemma convex_hull_empty :
  convex_hull ğ•œ (âˆ… : set E) = âˆ… :=
convex_empty.convex_hull_eq

@[simp]
lemma convex_hull_empty_iff :
  convex_hull ğ•œ s = âˆ… â†” s = âˆ… :=
begin
  split,
  { intro h,
    rw [â†set.subset_empty_iff, â†h],
    exact subset_convex_hull ğ•œ _ },
  { rintro rfl,
    exact convex_hull_empty }
end

@[simp] lemma convex_hull_nonempty_iff :
  (convex_hull ğ•œ s).nonempty â†” s.nonempty :=
begin
  rw [â†ne_empty_iff_nonempty, â†ne_empty_iff_nonempty, ne.def, ne.def],
  exact not_congr convex_hull_empty_iff,
end

@[simp]
lemma convex_hull_singleton {x : E} : convex_hull ğ•œ ({x} : set E) = {x} :=
(convex_singleton x).convex_hull_eq

lemma convex.convex_remove_iff_not_mem_convex_hull_remove {s : set E} (hs : convex ğ•œ s) (x : E) :
  convex ğ•œ (s \ {x}) â†” x âˆ‰ convex_hull ğ•œ (s \ {x}) :=
begin
  split,
  { rintro hsx hx,
    rw hsx.convex_hull_eq at hx,
    exact hx.2 (mem_singleton _) },
  rintro hx,
  suffices h : s \ {x} = convex_hull ğ•œ (s \ {x}), { convert convex_convex_hull ğ•œ _ },
  exact subset.antisymm (subset_convex_hull ğ•œ _) (Î» y hy, âŸ¨convex_hull_min (diff_subset _ _) hs hy,
    by { rintro (rfl : y = x), exact hx hy }âŸ©),
end

lemma is_linear_map.image_convex_hull {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  f '' (convex_hull ğ•œ s) = convex_hull ğ•œ (f '' s) :=
begin
  apply set.subset.antisymm ,
  { rw set.image_subset_iff,
    exact convex_hull_min (set.image_subset_iff.1 $ subset_convex_hull ğ•œ $ f '' s)
      ((convex_convex_hull ğ•œ (f '' s)).is_linear_preimage hf) },
  { exact convex_hull_min (set.image_subset _ $ subset_convex_hull ğ•œ s)
     ((convex_convex_hull ğ•œ s).is_linear_image hf) }
end

lemma linear_map.image_convex_hull (f : E â†’â‚—[ğ•œ] F) :
  f '' (convex_hull ğ•œ s) = convex_hull ğ•œ (f '' s) :=
f.is_linear.image_convex_hull

lemma is_linear_map.convex_hull_image {f : E â†’ F} (hf : is_linear_map ğ•œ f) (s : set E) :
  convex_hull ğ•œ (f '' s) = f '' convex_hull ğ•œ s :=
set.subset.antisymm (convex_hull_min (image_subset _ (subset_convex_hull ğ•œ s)) $
  (convex_convex_hull ğ•œ s).is_linear_image hf)
  (image_subset_iff.2 $ convex_hull_min
    (image_subset_iff.1 $ subset_convex_hull ğ•œ _)
    ((convex_convex_hull ğ•œ _).is_linear_preimage hf))

lemma linear_map.convex_hull_image (f : E â†’â‚—[ğ•œ] F) (s : set E) :
  convex_hull ğ•œ (f '' s) = f '' convex_hull ğ•œ s :=
f.is_linear.convex_hull_image s

end add_comm_monoid
end ordered_semiring

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_monoid
variables {ğ•œ} [add_comm_group E] [module ğ•œ E] [add_comm_group F] [module ğ•œ F] {s : set E}

lemma affine_map.image_convex_hull (f : E â†’áµƒ[ğ•œ] F) :
  f '' (convex_hull ğ•œ s) = convex_hull ğ•œ (f '' s) :=
begin
  apply set.subset.antisymm,
  { rw set.image_subset_iff,
    refine convex_hull_min _ ((convex_convex_hull ğ•œ (â‡‘f '' s)).affine_preimage f),
    rw â† set.image_subset_iff,
    exact subset_convex_hull ğ•œ (f '' s) },
  { exact convex_hull_min (set.image_subset _ (subset_convex_hull ğ•œ s))
    ((convex_convex_hull ğ•œ s).affine_image f) }
end

end add_comm_monoid
end ordered_ring
end convex_hull

/-! ### Simplex -/

section simplex

variables (Î¹ : Type*) [fintype Î¹]

/-- The standard simplex in the space of functions `Î¹ â†’ â„` is the set
of vectors with non-negative coordinates with total sum `1`. -/
def std_simplex (Î¹ : Type*) [fintype Î¹] (R : Type*) [ordered_semiring R] :
  set (Î¹ â†’ R) :=
{f | (âˆ€ x, 0 â‰¤ f x) âˆ§ âˆ‘ x, f x = 1}

variables (R : Type*) [ordered_semiring R] {f : Î¹ â†’ R}

lemma std_simplex_eq_inter :
  std_simplex Î¹ R = (â‹‚ x, {f | 0 â‰¤ f x}) âˆ© {f | âˆ‘ x, f x = 1} :=
by { ext f, simp only [std_simplex, set.mem_inter_eq, set.mem_Inter, set.mem_set_of_eq] }

lemma convex_std_simplex : convex R (std_simplex Î¹ R) :=
begin
  refine Î» f g hf hg a b ha hb hab, âŸ¨Î» x, _, _âŸ©,
  { apply_rules [add_nonneg, mul_nonneg, hf.1, hg.1] },
  { erw [finset.sum_add_distrib, â† finset.smul_sum, â† finset.smul_sum, hf.2, hg.2,
      smul_eq_mul, smul_eq_mul, mul_one, mul_one],
    exact hab }
end

variable {Î¹}

lemma ite_eq_mem_std_simplex (i : Î¹) : (Î» j, ite (i = j) (1:R) 0) âˆˆ std_simplex Î¹ R :=
âŸ¨Î» j, by simp only; split_ifs; norm_num, by rw [finset.sum_ite_eq, if_pos (finset.mem_univ _)]âŸ©

end simplex
