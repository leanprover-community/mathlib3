/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudriashov, YaÃ«l Dillies
-/
import algebra.order.invertible
import algebra.order.module
import linear_algebra.affine_space.midpoint
import linear_algebra.affine_space.affine_subspace
import linear_algebra.ray

/-!
# Convex sets and functions in vector spaces

In a ğ•œ-vector space, we define the following objects and properties.
* `segment ğ•œ x y`: Closed segment joining `x` and `y`.
* `open_segment ğ•œ x y`: Open segment joining `x` and `y`.
* `convex ğ•œ s`: A set `s` is convex if for any two points `x y âˆˆ s` it includes `segment ğ•œ x y`.
* `std_simplex ğ•œ Î¹`: The standard simplex in `Î¹ â†’ ğ•œ` (currently requires `fintype Î¹`). It is the
  intersection of the positive quadrant with the hyperplane `s.sum = 1`.

We also provide various equivalent versions of the definitions above, prove that some specific sets
are convex.

## Notations

We provide the following notation:
* `[x -[ğ•œ] y] = segment ğ•œ x y` in locale `convex`

## TODO

Generalize all this file to affine spaces.

Should we rename `segment` and `open_segment` to `convex.Icc` and `convex.Ioo`? Should we also
define `clopen_segment`/`convex.Ico`/`convex.Ioc`?
-/

variables {ğ•œ E F Î² : Type*}

open linear_map set
open_locale big_operators classical pointwise

/-! ### Segment -/

section ordered_semiring
variables [ordered_semiring ğ•œ] [add_comm_monoid E]

section has_scalar
variables (ğ•œ) [has_scalar ğ•œ E]

/-- Segments in a vector space. -/
def segment (x y : E) : set E :=
{z : E | âˆƒ (a b : ğ•œ) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1), a â€¢ x + b â€¢ y = z}

/-- Open segment in a vector space. Note that `open_segment ğ•œ x x = {x}` instead of being `âˆ…` when
the base semiring has some element between `0` and `1`. -/
def open_segment (x y : E) : set E :=
{z : E | âˆƒ (a b : ğ•œ) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1), a â€¢ x + b â€¢ y = z}

localized "notation `[` x ` -[` ğ•œ `] ` y `]` := segment ğ•œ x y" in convex

lemma segment_eq_imageâ‚‚ (x y : E) :
  [x -[ğ•œ] y] = (Î» p : ğ•œ Ã— ğ•œ, p.1 â€¢ x + p.2 â€¢ y) '' {p | 0 â‰¤ p.1 âˆ§ 0 â‰¤ p.2 âˆ§ p.1 + p.2 = 1} :=
by simp only [segment, image, prod.exists, mem_set_of_eq, exists_prop, and_assoc]

lemma open_segment_eq_imageâ‚‚ (x y : E) :
  open_segment ğ•œ x y =
    (Î» p : ğ•œ Ã— ğ•œ, p.1 â€¢ x + p.2 â€¢ y) '' {p | 0 < p.1 âˆ§ 0 < p.2 âˆ§ p.1 + p.2 = 1} :=
by simp only [open_segment, image, prod.exists, mem_set_of_eq, exists_prop, and_assoc]

lemma segment_symm (x y : E) : [x -[ğ•œ] y] = [y -[ğ•œ] x] :=
set.ext $ Î» z,
âŸ¨Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
  Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©

lemma open_segment_symm (x y : E) :
  open_segment ğ•œ x y = open_segment ğ•œ y x :=
set.ext $ Î» z,
âŸ¨Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
  Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©

lemma open_segment_subset_segment (x y : E) :
  open_segment ğ•œ x y âŠ† [x -[ğ•œ] y] :=
Î» z âŸ¨a, b, ha, hb, hab, hzâŸ©, âŸ¨a, b, ha.le, hb.le, hab, hzâŸ©

lemma segment_subset_iff {x y : E} {s : set E} :
  [x -[ğ•œ] y] âŠ† s â†” âˆ€ a b : ğ•œ, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
âŸ¨Î» H a b ha hb hab, H âŸ¨a, b, ha, hb, hab, rflâŸ©,
  Î» H z âŸ¨a, b, ha, hb, hab, hzâŸ©, hz â–¸ H a b ha hb habâŸ©

lemma open_segment_subset_iff {x y : E} {s : set E} :
  open_segment ğ•œ x y âŠ† s â†” âˆ€ a b : ğ•œ, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
âŸ¨Î» H a b ha hb hab, H âŸ¨a, b, ha, hb, hab, rflâŸ©,
  Î» H z âŸ¨a, b, ha, hb, hab, hzâŸ©, hz â–¸ H a b ha hb habâŸ©

end has_scalar

open_locale convex

section mul_action_with_zero
variables (ğ•œ) [mul_action_with_zero ğ•œ E]

lemma left_mem_segment (x y : E) : x âˆˆ [x -[ğ•œ] y] :=
âŸ¨1, 0, zero_le_one, le_refl 0, add_zero 1, by rw [zero_smul, one_smul, add_zero]âŸ©

lemma right_mem_segment (x y : E) : y âˆˆ [x -[ğ•œ] y] :=
segment_symm ğ•œ y x â–¸ left_mem_segment ğ•œ y x

end mul_action_with_zero

section module
variables (ğ•œ) [module ğ•œ E] {x y z : E} {s : set E}

@[simp] lemma segment_same (x : E) : [x -[ğ•œ] x] = {x} :=
set.ext $ Î» z, âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
  by simpa only [(add_smul _ _ _).symm, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
  Î» h, mem_singleton_iff.1 h â–¸ left_mem_segment ğ•œ z zâŸ©

lemma insert_endpoints_open_segment (x y : E) :
  insert x (insert y (open_segment ğ•œ x y)) = [x -[ğ•œ] y] :=
begin
  simp only [subset_antisymm_iff, insert_subset, left_mem_segment, right_mem_segment,
    open_segment_subset_segment, true_and],
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  refine hb.eq_or_gt.imp _ (Î» hb', ha.eq_or_gt.imp _ _),
  { rintro rfl,
    rw add_zero at hab,
    rw [hab, one_smul, zero_smul, add_zero] },
  { rintro rfl,
    rw zero_add at hab,
    rw [hab, one_smul, zero_smul, zero_add] },
  { exact Î» ha', âŸ¨a, b, ha', hb', hab, rflâŸ© }
end

variables {ğ•œ}

lemma mem_open_segment_of_ne_left_right (hx : x â‰  z) (hy : y â‰  z) (hz : z âˆˆ [x -[ğ•œ] y]) :
  z âˆˆ open_segment ğ•œ x y :=
begin
  rw [â† insert_endpoints_open_segment] at hz,
  exact ((hz.resolve_left hx.symm).resolve_left hy.symm)
end

lemma open_segment_subset_iff_segment_subset (hx : x âˆˆ s) (hy : y âˆˆ s) :
  open_segment ğ•œ x y âŠ† s â†” [x -[ğ•œ] y] âŠ† s :=
by simp only [â† insert_endpoints_open_segment, insert_subset, *, true_and]

end module
end ordered_semiring

open_locale convex

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_group
variables (ğ•œ) [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F]

section densely_ordered
variables [nontrivial ğ•œ] [densely_ordered ğ•œ]

@[simp] lemma open_segment_same (x : E) :
  open_segment ğ•œ x x = {x} :=
set.ext $ Î» z, âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
  by simpa only [â† add_smul, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
  Î» (h : z = x), begin
    obtain âŸ¨a, haâ‚€, haâ‚âŸ© := densely_ordered.dense (0 : ğ•œ) 1 zero_lt_one,
    refine âŸ¨a, 1 - a, haâ‚€, sub_pos_of_lt haâ‚, add_sub_cancel'_right _ _, _âŸ©,
    rw [â†add_smul, add_sub_cancel'_right, one_smul, h],
  endâŸ©

end densely_ordered

lemma segment_eq_image (x y : E) : [x -[ğ•œ] y] = (Î» Î¸ : ğ•œ, (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Icc (0 : ğ•œ) 1 :=
set.ext $ Î» z,
  âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
    âŸ¨b, âŸ¨hb, hab â–¸ le_add_of_nonneg_left haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel]âŸ©,
    Î» âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ©, âŸ¨1-Î¸, Î¸, sub_nonneg.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©

lemma open_segment_eq_image (x y : E) :
  open_segment ğ•œ x y = (Î» (Î¸ : ğ•œ), (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Ioo (0 : ğ•œ) 1 :=
set.ext $ Î» z,
  âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
    âŸ¨b, âŸ¨hb, hab â–¸ lt_add_of_pos_left _ haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel]âŸ©,
    Î» âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ©, âŸ¨1 - Î¸, Î¸, sub_pos.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©

lemma segment_eq_image' (x y : E) :
  [x -[ğ•œ] y] = (Î» (Î¸ : ğ•œ), x + Î¸ â€¢ (y - x)) '' Icc (0 : ğ•œ) 1 :=
by { convert segment_eq_image ğ•œ x y, ext Î¸, simp only [smul_sub, sub_smul, one_smul], abel }

lemma open_segment_eq_image' (x y : E) :
  open_segment ğ•œ x y = (Î» (Î¸ : ğ•œ), x + Î¸ â€¢ (y - x)) '' Ioo (0 : ğ•œ) 1 :=
by { convert open_segment_eq_image ğ•œ x y, ext Î¸, simp only [smul_sub, sub_smul, one_smul], abel }

lemma segment_eq_image_line_map (x y : E) :
  [x -[ğ•œ] y] = affine_map.line_map x y '' Icc (0 : ğ•œ) 1 :=
by { convert segment_eq_image ğ•œ x y, ext, exact affine_map.line_map_apply_module _ _ _ }

lemma open_segment_eq_image_line_map (x y : E) :
  open_segment ğ•œ x y = affine_map.line_map x y '' Ioo (0 : ğ•œ) 1 :=
by { convert open_segment_eq_image ğ•œ x y, ext, exact affine_map.line_map_apply_module _ _ _ }

lemma segment_image (f : E â†’â‚—[ğ•œ] F) (a b : E) : f '' [a -[ğ•œ] b] = [f a -[ğ•œ] f b] :=
set.ext (Î» x, by simp_rw [segment_eq_image, mem_image, exists_exists_and_eq_and, map_add, map_smul])

@[simp] lemma open_segment_image (f : E â†’â‚—[ğ•œ] F) (a b : E) :
  f '' open_segment ğ•œ a b = open_segment ğ•œ (f a) (f b) :=
set.ext (Î» x, by simp_rw [open_segment_eq_image, mem_image, exists_exists_and_eq_and, map_add,
  map_smul])

lemma mem_segment_translate (a : E) {x b c} : a + x âˆˆ [a + b -[ğ•œ] a + c] â†” x âˆˆ [b -[ğ•œ] c] :=
begin
  rw [segment_eq_image', segment_eq_image'],
  refine exists_congr (Î» Î¸, and_congr iff.rfl _),
  simp only [add_sub_add_left_eq_sub, add_assoc, add_right_inj],
end

@[simp] lemma mem_open_segment_translate (a : E) {x b c : E} :
  a + x âˆˆ open_segment ğ•œ (a + b) (a + c) â†” x âˆˆ open_segment ğ•œ b c :=
begin
  rw [open_segment_eq_image', open_segment_eq_image'],
  refine exists_congr (Î» Î¸, and_congr iff.rfl _),
  simp only [add_sub_add_left_eq_sub, add_assoc, add_right_inj],
end

lemma segment_translate_preimage (a b c : E) : (Î» x, a + x) â»Â¹' [a + b -[ğ•œ] a + c] = [b -[ğ•œ] c] :=
set.ext $ Î» x, mem_segment_translate ğ•œ a

lemma open_segment_translate_preimage (a b c : E) :
  (Î» x, a + x) â»Â¹' open_segment ğ•œ (a + b) (a + c) = open_segment ğ•œ b c :=
set.ext $ Î» x, mem_open_segment_translate ğ•œ a

lemma segment_translate_image (a b c : E) : (Î» x, a + x) '' [b -[ğ•œ] c] = [a + b -[ğ•œ] a + c] :=
segment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ $ add_left_surjective a

lemma open_segment_translate_image (a b c : E) :
  (Î» x, a + x) '' open_segment ğ•œ b c = open_segment ğ•œ (a + b) (a + c) :=
open_segment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ $ add_left_surjective a

end add_comm_group
end ordered_ring

lemma same_ray_of_mem_segment [ordered_comm_ring ğ•œ] [add_comm_group E] [module ğ•œ E]
  {x y z : E} (h : x âˆˆ [y -[ğ•œ] z]) : same_ray ğ•œ (x - y) (z - x) :=
begin
  rw segment_eq_image' at h,
  rcases h with âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, rflâŸ©,
  simpa only [add_sub_cancel', â† sub_sub, sub_smul, one_smul]
    using (same_ray_nonneg_smul_left (z - y) hÎ¸â‚€).nonneg_smul_right (sub_nonneg.2 hÎ¸â‚)
end

section linear_ordered_ring
variables [linear_ordered_ring ğ•œ]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F]

lemma midpoint_mem_segment [invertible (2 : ğ•œ)] (x y : E) :
  midpoint ğ•œ x y âˆˆ [x -[ğ•œ] y] :=
begin
  rw segment_eq_image_line_map,
  exact âŸ¨â…Ÿ2, âŸ¨inv_of_nonneg.mpr zero_le_two, inv_of_le_one one_le_twoâŸ©, rflâŸ©,
end

lemma mem_segment_sub_add [invertible (2 : ğ•œ)] (x y : E) :
  x âˆˆ [x-y -[ğ•œ] x+y] :=
begin
  convert @midpoint_mem_segment ğ•œ _ _ _ _ _ _ _,
  rw midpoint_sub_add
end

lemma mem_segment_add_sub [invertible (2 : ğ•œ)] (x y : E) :
  x âˆˆ [x+y -[ğ•œ] x-y] :=
begin
  convert @midpoint_mem_segment ğ•œ _ _ _ _ _ _ _,
  rw midpoint_add_sub
end

@[simp] lemma left_mem_open_segment_iff [densely_ordered ğ•œ] [no_zero_smul_divisors ğ•œ E] {x y : E} :
  x âˆˆ open_segment ğ•œ x y â†” x = y :=
begin
  split,
  { rintro âŸ¨a, b, ha, hb, hab, hxâŸ©,
    refine smul_right_injective _ hb.ne' ((add_right_inj (a â€¢ x)).1 _),
    rw [hx, â†add_smul, hab, one_smul] },
  { rintro rfl,
    rw open_segment_same,
    exact mem_singleton _ }
end

@[simp] lemma right_mem_open_segment_iff [densely_ordered ğ•œ] [no_zero_smul_divisors ğ•œ E] {x y : E} :
  y âˆˆ open_segment ğ•œ x y â†” x = y :=
by rw [open_segment_symm, left_mem_open_segment_iff, eq_comm]

end add_comm_group
end linear_ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {x y z : E}

lemma mem_segment_iff_same_ray : x âˆˆ [y -[ğ•œ] z] â†” same_ray ğ•œ (x - y) (z - x) :=
begin
  refine âŸ¨same_ray_of_mem_segment, Î» h, _âŸ©,
  rcases h.exists_eq_smul_add with âŸ¨a, b, ha, hb, hab, hxy, hzxâŸ©,
  rw [add_comm, sub_add_sub_cancel] at hxy hzx,
  rw [â† mem_segment_translate _ (-x), neg_add_self],
  refine âŸ¨b, a, hb, ha, add_comm a b â–¸ hab, _âŸ©,
  rw [â† sub_eq_neg_add, â† neg_sub, hxy, â† sub_eq_neg_add, hzx, smul_neg, smul_comm, neg_add_self]
end

lemma mem_segment_iff_div : x âˆˆ [y -[ğ•œ] z] â†”
  âˆƒ a b : ğ•œ, 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ 0 < a + b âˆ§ (a / (a + b)) â€¢ y + (b / (a + b)) â€¢ z = x :=
begin
  split,
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    use [a, b, ha, hb],
    simp * },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    refine âŸ¨a / (a + b), b / (a + b), div_nonneg ha hab.le, div_nonneg hb hab.le, _, rflâŸ©,
    rw [â† add_div, div_self hab.ne'] }
end

lemma mem_open_segment_iff_div : x âˆˆ open_segment ğ•œ y z â†”
  âˆƒ a b : ğ•œ, 0 < a âˆ§ 0 < b âˆ§ (a / (a + b)) â€¢ y + (b / (a + b)) â€¢ z = x :=
begin
  split,
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    use [a, b, ha, hb],
    rw [hab, div_one, div_one] },
  { rintro âŸ¨a, b, ha, hb, rflâŸ©,
    have hab : 0 < a + b, from add_pos ha hb,
    refine âŸ¨a / (a + b), b / (a + b), div_pos ha hab, div_pos hb hab, _, rflâŸ©,
    rw [â† add_div, div_self hab.ne'] }
end

end add_comm_group
end linear_ordered_field

/-!
#### Segments in an ordered space
Relates `segment`, `open_segment` and `set.Icc`, `set.Ico`, `set.Ioc`, `set.Ioo`
-/
section ordered_semiring
variables [ordered_semiring ğ•œ]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E]

lemma segment_subset_Icc {x y : E} (h : x â‰¤ y) : [x -[ğ•œ] y] âŠ† Icc x y :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  split,
  calc
    x   = a â€¢ x + b â€¢ x :(convex.combo_self hab _).symm
    ... â‰¤ a â€¢ x + b â€¢ y : add_le_add_left (smul_le_smul_of_nonneg h hb) _,
  calc
    a â€¢ x + b â€¢ y
        â‰¤ a â€¢ y + b â€¢ y : add_le_add_right (smul_le_smul_of_nonneg h ha) _
    ... = y : convex.combo_self hab _,
end

end ordered_add_comm_monoid

section ordered_cancel_add_comm_monoid
variables [ordered_cancel_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E]

lemma open_segment_subset_Ioo {x y : E} (h : x < y) : open_segment ğ•œ x y âŠ† Ioo x y :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  split,
  calc
    x   = a â€¢ x + b â€¢ x : (convex.combo_self hab _).symm
    ... < a â€¢ x + b â€¢ y : add_lt_add_left (smul_lt_smul_of_pos h hb) _,
  calc
    a â€¢ x + b â€¢ y
        < a â€¢ y + b â€¢ y : add_lt_add_right (smul_lt_smul_of_pos h ha) _
    ... = y : convex.combo_self hab _,
end

end ordered_cancel_add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E] {ğ•œ}

lemma segment_subset_interval (x y : E) : [x -[ğ•œ] y] âŠ† interval x y :=
begin
  cases le_total x y,
  { rw interval_of_le h,
    exact segment_subset_Icc h },
  { rw [interval_of_ge h, segment_symm],
    exact segment_subset_Icc h }
end

lemma convex.min_le_combo (x y : E) {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  min x y â‰¤ a â€¢ x + b â€¢ y :=
(segment_subset_interval x y âŸ¨_, _, ha, hb, hab, rflâŸ©).1

lemma convex.combo_le_max (x y : E) {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  a â€¢ x + b â€¢ y â‰¤ max x y :=
(segment_subset_interval x y âŸ¨_, _, ha, hb, hab, rflâŸ©).2

end linear_ordered_add_comm_monoid
end ordered_semiring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

lemma Icc_subset_segment {x y : ğ•œ} : Icc x y âŠ† [x -[ğ•œ] y] :=
begin
  rintro z âŸ¨hxz, hyzâŸ©,
  obtain rfl | h := (hxz.trans hyz).eq_or_lt,
  { rw segment_same,
    exact hyz.antisymm hxz },
  rw â†sub_nonneg at hxz hyz,
  rw â†sub_pos at h,
  refine âŸ¨(y - z) / (y - x), (z - x) / (y - x), div_nonneg hyz h.le, div_nonneg hxz h.le, _, _âŸ©,
  { rw [â†add_div, sub_add_sub_cancel, div_self h.ne'] },
  { rw [smul_eq_mul, smul_eq_mul, â†mul_div_right_comm, â†mul_div_right_comm, â†add_div,
      div_eq_iff h.ne', add_comm, sub_mul, sub_mul, mul_comm x, sub_add_sub_cancel, mul_sub] }
end

@[simp] lemma segment_eq_Icc {x y : ğ•œ} (h : x â‰¤ y) : [x -[ğ•œ] y] = Icc x y :=
(segment_subset_Icc h).antisymm Icc_subset_segment

lemma Ioo_subset_open_segment {x y : ğ•œ} : Ioo x y âŠ† open_segment ğ•œ x y :=
Î» z hz, mem_open_segment_of_ne_left_right hz.1.ne hz.2.ne'
    (Icc_subset_segment $ Ioo_subset_Icc_self hz)

@[simp] lemma open_segment_eq_Ioo {x y : ğ•œ} (h : x < y) : open_segment ğ•œ x y = Ioo x y :=
(open_segment_subset_Ioo h).antisymm Ioo_subset_open_segment

lemma segment_eq_Icc' (x y : ğ•œ) : [x -[ğ•œ] y] = Icc (min x y) (max x y) :=
begin
  cases le_total x y,
  { rw [segment_eq_Icc h, max_eq_right h, min_eq_left h] },
  { rw [segment_symm, segment_eq_Icc h, max_eq_left h, min_eq_right h] }
end

lemma open_segment_eq_Ioo' {x y : ğ•œ} (hxy : x â‰  y) :
  open_segment ğ•œ x y = Ioo (min x y) (max x y) :=
begin
  cases hxy.lt_or_lt,
  { rw [open_segment_eq_Ioo h, max_eq_right h.le, min_eq_left h.le] },
  { rw [open_segment_symm, open_segment_eq_Ioo h, max_eq_left h.le, min_eq_right h.le] }
end

lemma segment_eq_interval (x y : ğ•œ) : [x -[ğ•œ] y] = interval x y :=
segment_eq_Icc' _ _

/-- A point is in an `Icc` iff it can be expressed as a convex combination of the endpoints. -/
lemma convex.mem_Icc {x y : ğ•œ} (h : x â‰¤ y) {z : ğ•œ} :
  z âˆˆ Icc x y â†” âˆƒ (a b : ğ•œ), 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  rw â†segment_eq_Icc h,
  simp_rw [â†exists_prop],
  refl,
end

/-- A point is in an `Ioo` iff it can be expressed as a strict convex combination of the endpoints.
-/
lemma convex.mem_Ioo {x y : ğ•œ} (h : x < y) {z : ğ•œ} :
  z âˆˆ Ioo x y â†” âˆƒ (a b : ğ•œ), 0 < a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  rw â†open_segment_eq_Ioo h,
  simp_rw [â†exists_prop],
  refl,
end

/-- A point is in an `Ioc` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
lemma convex.mem_Ioc {x y : ğ•œ} (h : x < y) {z : ğ•œ} :
  z âˆˆ Ioc x y â†” âˆƒ (a b : ğ•œ), 0 â‰¤ a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  split,
  { rintro hz,
    obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (convex.mem_Icc h.le).1 (Ioc_subset_Icc_self hz),
    obtain rfl | hb' := hb.eq_or_lt,
    { rw add_zero at hab,
      rw [hab, one_mul, zero_mul, add_zero] at hz,
      exact (hz.1.ne rfl).elim },
    { exact âŸ¨a, b, ha, hb', hab, rflâŸ© } },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    obtain rfl | ha' := ha.eq_or_lt,
    { rw zero_add at hab,
      rwa [hab, one_mul, zero_mul, zero_add, right_mem_Ioc] },
    { exact Ioo_subset_Ioc_self ((convex.mem_Ioo h).2 âŸ¨a, b, ha', hb, hab, rflâŸ©) } }
end

/-- A point is in an `Ico` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
lemma convex.mem_Ico {x y : ğ•œ} (h : x < y) {z : ğ•œ} :
  z âˆˆ Ico x y â†” âˆƒ (a b : ğ•œ), 0 < a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  split,
  { rintro hz,
    obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (convex.mem_Icc h.le).1 (Ico_subset_Icc_self hz),
    obtain rfl | ha' := ha.eq_or_lt,
    { rw zero_add at hab,
      rw [hab, one_mul, zero_mul, zero_add] at hz,
      exact (hz.2.ne rfl).elim },
    { exact âŸ¨a, b, ha', hb, hab, rflâŸ© } },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    obtain rfl | hb' := hb.eq_or_lt,
    { rw add_zero at hab,
      rwa [hab, one_mul, zero_mul, add_zero, left_mem_Ico] },
    { exact Ioo_subset_Ico_self ((convex.mem_Ioo h).2 âŸ¨a, b, ha, hb', hab, rflâŸ©) } }
end

end linear_ordered_field

/-! ### Convexity of sets -/

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F]

section has_scalar
variables (ğ•œ) [has_scalar ğ•œ E] [has_scalar ğ•œ F] (s : set E)

/-- Convexity of sets. -/
def convex : Prop :=
âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
  a â€¢ x + b â€¢ y âˆˆ s

variables {ğ•œ s}

lemma convex_iff_segment_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒx yâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ [x -[ğ•œ] y] âŠ† s :=
forallâ‚„_congr $ Î» x y hx hy, (segment_subset_iff _).symm

lemma convex.segment_subset (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
  [x -[ğ•œ] y] âŠ† s :=
convex_iff_segment_subset.1 h hx hy

lemma convex.open_segment_subset (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
  open_segment ğ•œ x y âŠ† s :=
(open_segment_subset_segment ğ•œ x y).trans (h.segment_subset hx hy)

/-- Alternative definition of set convexity, in terms of pointwise set operations. -/
lemma convex_iff_pointwise_add_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ s + b â€¢ s âŠ† s :=
iff.intro
  begin
    rintro hA a b ha hb hab w âŸ¨au, bv, âŸ¨u, hu, rflâŸ©, âŸ¨v, hv, rflâŸ©, rflâŸ©,
    exact hA hu hv ha hb hab
  end
  (Î» h x y hx hy a b ha hb hab,
    (h ha hb hab) (set.add_mem_add âŸ¨_, hx, rflâŸ© âŸ¨_, hy, rflâŸ©))

alias convex_iff_pointwise_add_subset â†” convex.set_combo_subset _

lemma convex_empty : convex ğ•œ (âˆ… : set E) :=
Î» x y, false.elim

lemma convex_univ : convex ğ•œ (set.univ : set E) := Î» _ _ _ _ _ _ _ _ _, trivial

lemma convex.inter {t : set E} (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s âˆ© t) :=
Î» x y (hx : x âˆˆ s âˆ© t) (hy : y âˆˆ s âˆ© t) a b (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1),
  âŸ¨hs hx.left hy.left ha hb hab, ht hx.right hy.right ha hb habâŸ©

lemma convex_sInter {S : set (set E)} (h : âˆ€ s âˆˆ S, convex ğ•œ s) : convex ğ•œ (â‹‚â‚€ S) :=
assume x y hx hy a b ha hb hab s hs,
h s hs (hx s hs) (hy s hs) ha hb hab

lemma convex_Inter {Î¹ : Sort*} {s : Î¹ â†’ set E} (h : âˆ€ i, convex ğ•œ (s i)) : convex ğ•œ (â‹‚ i, s i) :=
(sInter_range s) â–¸ convex_sInter $ forall_range_iff.2 h

lemma convex_Interâ‚‚ {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*} {s : Î  i, Îº i â†’ set E}
  (h : âˆ€ i j, convex ğ•œ (s i j)) :
  convex ğ•œ (â‹‚ i j, s i j) :=
convex_Inter $ Î» i, convex_Inter $ h i

lemma convex.prod {s : set E} {t : set F} (hs : convex ğ•œ s) (ht : convex ğ•œ t) :
  convex ğ•œ (s Ã—Ë¢ t) :=
begin
  intros x y hx hy a b ha hb hab,
  apply mem_prod.2,
  exact âŸ¨hs (mem_prod.1 hx).1 (mem_prod.1 hy).1 ha hb hab,
        ht (mem_prod.1 hx).2 (mem_prod.1 hy).2 ha hb habâŸ©
end

lemma convex_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [Î  i, add_comm_monoid (E i)]
  [Î  i, has_scalar ğ•œ (E i)] {s : set Î¹} {t : Î  i, set (E i)} (ht : âˆ€ i, convex ğ•œ (t i)) :
  convex ğ•œ (s.pi t) :=
Î» x y hx hy a b ha hb hab i hi, ht i (hx i hi) (hy i hi) ha hb hab

lemma directed.convex_Union {Î¹ : Sort*} {s : Î¹ â†’ set E} (hdir : directed (âŠ†) s)
  (hc : âˆ€ â¦ƒi : Î¹â¦„, convex ğ•œ (s i)) :
  convex ğ•œ (â‹ƒ i, s i) :=
begin
  rintro x y hx hy a b ha hb hab,
  rw mem_Union at âŠ¢ hx hy,
  obtain âŸ¨i, hxâŸ© := hx,
  obtain âŸ¨j, hyâŸ© := hy,
  obtain âŸ¨k, hik, hjkâŸ© := hdir i j,
  exact âŸ¨k, hc (hik hx) (hjk hy) ha hb habâŸ©,
end

lemma directed_on.convex_sUnion {c : set (set E)} (hdir : directed_on (âŠ†) c)
  (hc : âˆ€ â¦ƒA : set Eâ¦„, A âˆˆ c â†’ convex ğ•œ A) :
  convex ğ•œ (â‹ƒâ‚€c) :=
begin
  rw sUnion_eq_Union,
  exact (directed_on_iff_directed.1 hdir).convex_Union (Î» A, hc A.2),
end

end has_scalar

section module
variables [module ğ•œ E] [module ğ•œ F] {s : set E}

lemma convex_iff_open_segment_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒx yâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ open_segment ğ•œ x y âŠ† s :=
convex_iff_segment_subset.trans $ forallâ‚„_congr $ Î» x y hx hy,
  (open_segment_subset_iff_segment_subset hx hy).symm

lemma convex_iff_forall_pos :
  convex ğ•œ s â†” âˆ€ â¦ƒx yâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1
  â†’ a â€¢ x + b â€¢ y âˆˆ s :=
convex_iff_open_segment_subset.trans $ forallâ‚„_congr $ Î» x y hx hy,
  open_segment_subset_iff ğ•œ

lemma convex_iff_pairwise_pos :
  convex ğ•œ s â†” s.pairwise (Î» x y, âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s) :=
begin
  refine convex_iff_forall_pos.trans âŸ¨Î» h x hx y hy _, h hx hy, _âŸ©,
  intros h x y hx hy a b ha hb hab,
  obtain rfl | hxy := eq_or_ne x y,
  { rwa convex.combo_self hab },
  { exact h hx hy hxy ha hb hab },
end

protected lemma set.subsingleton.convex {s : set E} (h : s.subsingleton) : convex ğ•œ s :=
convex_iff_pairwise_pos.mpr (h.pairwise _)

lemma convex_singleton (c : E) : convex ğ•œ ({c} : set E) :=
subsingleton_singleton.convex

lemma convex.linear_image (hs : convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) : convex ğ•œ (f '' s) :=
begin
  intros x y hx hy a b ha hb hab,
  obtain âŸ¨x', hx', rflâŸ© := mem_image_iff_bex.1 hx,
  obtain âŸ¨y', hy', rflâŸ© := mem_image_iff_bex.1 hy,
  exact âŸ¨a â€¢ x' + b â€¢ y', hs hx' hy' ha hb hab, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©,
end

lemma convex.is_linear_image (hs : convex ğ•œ s) {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  convex ğ•œ (f '' s) :=
hs.linear_image $ hf.mk' f

lemma convex.linear_preimage {s : set F} (hs : convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) :
  convex ğ•œ (f â»Â¹' s) :=
begin
  intros x y hx hy a b ha hb hab,
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul],
  exact hs hx hy ha hb hab,
end

lemma convex.is_linear_preimage {s : set F} (hs : convex ğ•œ s) {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  convex ğ•œ (f â»Â¹' s) :=
hs.linear_preimage $ hf.mk' f

lemma convex.add {t : set E} (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s + t) :=
by { rw â† add_image_prod, exact (hs.prod ht).is_linear_image is_linear_map.is_linear_map_add }

lemma convex.vadd (hs : convex ğ•œ s) (z : E) : convex ğ•œ (z +áµ¥ s) :=
by { simp_rw [â†image_vadd, vadd_eq_add, â†singleton_add], exact (convex_singleton _).add hs }

lemma convex.translate (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, z + x) '' s) := hs.vadd _

/-- The translation of a convex set is also convex. -/
lemma convex.translate_preimage_right (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, z + x) â»Â¹' s) :=
begin
  intros x y hx hy a b ha hb hab,
  have h := hs hx hy ha hb hab,
  rwa [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul] at h,
end

/-- The translation of a convex set is also convex. -/
lemma convex.translate_preimage_left (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, x + z) â»Â¹' s) :=
by simpa only [add_comm] using hs.translate_preimage_right z

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]

lemma convex_Iic (r : Î²) : convex ğ•œ (Iic r) :=
Î» x y hx hy a b ha hb hab,
calc
  a â€¢ x + b â€¢ y
      â‰¤ a â€¢ r + b â€¢ r
      : add_le_add (smul_le_smul_of_nonneg hx ha) (smul_le_smul_of_nonneg hy hb)
  ... = r : convex.combo_self hab _

lemma convex_Ici (r : Î²) : convex ğ•œ (Ici r) := @convex_Iic ğ•œ Î²áµ’áµˆ _ _ _ _ r

lemma convex_Icc (r s : Î²) : convex ğ•œ (Icc r s) :=
Ici_inter_Iic.subst ((convex_Ici r).inter $ convex_Iic s)

lemma convex_halfspace_le {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | f w â‰¤ r} :=
(convex_Iic r).is_linear_preimage h

lemma convex_halfspace_ge {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | r â‰¤ f w} :=
(convex_Ici r).is_linear_preimage h

lemma convex_hyperplane {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | f w = r} :=
begin
  simp_rw le_antisymm_iff,
  exact (convex_halfspace_le h r).inter (convex_halfspace_ge h r),
end

end ordered_add_comm_monoid

section ordered_cancel_add_comm_monoid
variables [ordered_cancel_add_comm_monoid Î²] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]

lemma convex_Iio (r : Î²) : convex ğ•œ (Iio r) :=
begin
  intros x y hx hy a b ha hb hab,
  obtain rfl | ha' := ha.eq_or_lt,
  { rw zero_add at hab,
    rwa [zero_smul, zero_add, hab, one_smul] },
  rw mem_Iio at hx hy,
  calc
    a â€¢ x + b â€¢ y
        < a â€¢ r + b â€¢ r
        : add_lt_add_of_lt_of_le (smul_lt_smul_of_pos hx ha') (smul_le_smul_of_nonneg hy.le hb)
    ... = r : convex.combo_self hab _
end

lemma convex_Ioi (r : Î²) : convex ğ•œ (Ioi r) := @convex_Iio ğ•œ Î²áµ’áµˆ _ _ _ _ r

lemma convex_Ioo (r s : Î²) : convex ğ•œ (Ioo r s) :=
Ioi_inter_Iio.subst ((convex_Ioi r).inter $ convex_Iio s)

lemma convex_Ico (r s : Î²) : convex ğ•œ (Ico r s) :=
Ici_inter_Iio.subst ((convex_Ici r).inter $ convex_Iio s)

lemma convex_Ioc (r s : Î²) : convex ğ•œ (Ioc r s) :=
Ioi_inter_Iic.subst ((convex_Ioi r).inter $ convex_Iic s)

lemma convex_halfspace_lt {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | f w < r} :=
(convex_Iio r).is_linear_preimage h

lemma convex_halfspace_gt {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | r < f w} :=
(convex_Ioi r).is_linear_preimage h

end ordered_cancel_add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid Î²] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]

lemma convex_interval (r s : Î²) : convex ğ•œ (interval r s) :=
convex_Icc _ _

end linear_ordered_add_comm_monoid
end module
end add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid E] [ordered_add_comm_monoid Î²] [module ğ•œ E]
  [ordered_smul ğ•œ E] {s : set E} {f : E â†’ Î²}

lemma monotone_on.convex_le (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x â‰¤ r} :=
Î» x y hx hy a b ha hb hab, âŸ¨hs hx.1 hy.1 ha hb hab,
  (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1) (convex.combo_le_max x y ha hb hab)).trans
    (max_rec' _ hx.2 hy.2)âŸ©

lemma monotone_on.convex_lt (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x < r} :=
Î» x y hx hy a b ha hb hab, âŸ¨hs hx.1 hy.1 ha hb hab,
  (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1) (convex.combo_le_max x y ha hb hab)).trans_lt
    (max_rec' _ hx.2 hy.2)âŸ©

lemma monotone_on.convex_ge (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r â‰¤ f x} :=
@monotone_on.convex_le ğ•œ Eáµ’áµˆ Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual hs r

lemma monotone_on.convex_gt (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r < f x} :=
@monotone_on.convex_lt ğ•œ Eáµ’áµˆ Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual hs r

lemma antitone_on.convex_le (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x â‰¤ r} :=
@monotone_on.convex_ge ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma antitone_on.convex_lt (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x < r} :=
@monotone_on.convex_gt ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma antitone_on.convex_ge (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r â‰¤ f x} :=
@monotone_on.convex_le ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma antitone_on.convex_gt (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r < f x} :=
@monotone_on.convex_lt ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma monotone.convex_le (hf : monotone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_le convex_univ r)

lemma monotone.convex_lt (hf : monotone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_le convex_univ r)

lemma monotone.convex_ge (hf : monotone f ) (r : Î²) :
  convex ğ•œ {x | r â‰¤ f x} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_ge convex_univ r)

lemma monotone.convex_gt (hf : monotone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_le convex_univ r)

lemma antitone.convex_le (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_le convex_univ r)

lemma antitone.convex_lt (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | f x < r} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_lt convex_univ r)

lemma antitone.convex_ge (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | r â‰¤ f x} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_ge convex_univ r)

lemma antitone.convex_gt (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | r < f x} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_gt convex_univ r)

end linear_ordered_add_comm_monoid

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] {s t : set E}

lemma convex.combo_eq_vadd {a b : ğ•œ} {x y : E} (h : a + b = 1) :
  a â€¢ x + b â€¢ y = b â€¢ (y - x) + x :=
calc
  a â€¢ x + b â€¢ y = (b â€¢ y - b â€¢ x) + (a â€¢ x + b â€¢ x) : by abel
            ... = b â€¢ (y - x) + x                   : by rw [smul_sub, convex.combo_self h]

lemma convex_segment (x y : E) : convex ğ•œ [x -[ğ•œ] y] :=
begin
  rintro p q âŸ¨ap, bp, hap, hbp, habp, rflâŸ© âŸ¨aq, bq, haq, hbq, habq, rflâŸ© a b ha hb hab,
  refine âŸ¨a * ap + b * aq, a * bp + b * bq,
    add_nonneg (mul_nonneg ha hap) (mul_nonneg hb haq),
    add_nonneg (mul_nonneg ha hbp) (mul_nonneg hb hbq), _, _âŸ©,
  { rw [add_add_add_comm, â†mul_add, â†mul_add, habp, habq, mul_one, mul_one, hab] },
  { simp_rw [add_smul, mul_smul, smul_add],
    exact add_add_add_comm _ _ _ _ }
end

lemma convex_open_segment (a b : E) : convex ğ•œ (open_segment ğ•œ a b) :=
begin
  rw convex_iff_open_segment_subset,
  rintro p q âŸ¨ap, bp, hap, hbp, habp, rflâŸ© âŸ¨aq, bq, haq, hbq, habq, rflâŸ© z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  refine âŸ¨a * ap + b * aq, a * bp + b * bq,
    add_pos (mul_pos ha hap) (mul_pos hb haq),
    add_pos (mul_pos ha hbp) (mul_pos hb hbq), _, _âŸ©,
  { rw [add_add_add_comm, â†mul_add, â†mul_add, habp, habq, mul_one, mul_one, hab] },
  { simp_rw [add_smul, mul_smul, smul_add],
    exact add_add_add_comm _ _ _ _ }
end

end add_comm_group
end ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F] [module ğ•œ E] [module ğ•œ F] {s : set E}

lemma convex.smul (hs : convex ğ•œ s) (c : ğ•œ) : convex ğ•œ (c â€¢ s) :=
hs.linear_image (linear_map.lsmul _ _ c)

lemma convex.smul_preimage (hs : convex ğ•œ s) (c : ğ•œ) : convex ğ•œ ((Î» z, c â€¢ z) â»Â¹' s) :=
hs.linear_preimage (linear_map.lsmul _ _ c)

lemma convex.affinity (hs : convex ğ•œ s) (z : E) (c : ğ•œ) : convex ğ•œ ((Î» x, z + c â€¢ x) '' s) :=
by simpa only [â†image_smul, â†image_vadd, image_image] using (hs.smul c).vadd z

end add_comm_monoid
end ordered_comm_semiring

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {s t : set E}

lemma convex.add_smul_mem (hs : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : x + y âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ y âˆˆ s :=
begin
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y),
  { rw [smul_add, â†add_assoc, â†add_smul, sub_add_cancel, one_smul] },
  rw h,
  exact hs hx hy (sub_nonneg_of_le ht.2) ht.1 (sub_add_cancel _ _),
end

lemma convex.smul_mem_of_zero_mem (hs : convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s) (hx : x âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : t â€¢ x âˆˆ s :=
by simpa using hs.add_smul_mem zero_mem (by simpa using hx) ht

lemma convex.add_smul_sub_mem (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ (y - x) âˆˆ s :=
begin
  apply h.segment_subset hx hy,
  rw segment_eq_image',
  exact mem_image_of_mem _ ht,
end

/-- Affine subspaces are convex. -/
lemma affine_subspace.convex (Q : affine_subspace ğ•œ E) : convex ğ•œ (Q : set E) :=
begin
  intros x y hx hy a b ha hb hab,
  rw [eq_sub_of_add_eq hab, â† affine_map.line_map_apply_module],
  exact affine_map.line_map_mem b hx hy,
end

/--
Applying an affine map to an affine combination of two points yields
an affine combination of the images.
-/
lemma convex.combo_affine_apply {a b : ğ•œ} {x y : E} {f : E â†’áµƒ[ğ•œ] F} (h : a + b = 1) :
  f (a â€¢ x + b â€¢ y) = a â€¢ f x + b â€¢ f y :=
begin
  simp only [convex.combo_eq_vadd h, â† vsub_eq_sub],
  exact f.apply_line_map _ _ _,
end

/-- The preimage of a convex set under an affine map is convex. -/
lemma convex.affine_preimage (f : E â†’áµƒ[ğ•œ] F) {s : set F} (hs : convex ğ•œ s) :
  convex ğ•œ (f â»Â¹' s) :=
begin
  intros x y xs ys a b ha hb hab,
  rw [mem_preimage, convex.combo_affine_apply hab],
  exact hs xs ys ha hb hab,
end

/-- The image of a convex set under an affine map is convex. -/
lemma convex.affine_image (f : E â†’áµƒ[ğ•œ] F) (hs : convex ğ•œ s) : convex ğ•œ (f '' s) :=
begin
  rintro x y âŸ¨x', âŸ¨hx', hx'fâŸ©âŸ© âŸ¨y', âŸ¨hy', hy'fâŸ©âŸ© a b ha hb hab,
  refine âŸ¨a â€¢ x' + b â€¢ y', âŸ¨hs hx' hy' ha hb hab, _âŸ©âŸ©,
  rw [convex.combo_affine_apply hab, hx'f, hy'f]
end

lemma convex.neg (hs : convex ğ•œ s) : convex ğ•œ (-s) :=
hs.is_linear_preimage is_linear_map.is_linear_map_neg

lemma convex.sub (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s - t) :=
by { rw sub_eq_add_neg, exact hs.add ht.neg }

end add_comm_group
end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {s : set E}

/-- Alternative definition of set convexity, using division. -/
lemma convex_iff_div :
  convex ğ•œ s â†” âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„,
    0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’ (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ s :=
begin
  simp only [convex_iff_segment_subset, subset_def, mem_segment_iff_div],
  refine forallâ‚„_congr (Î» x y hx hy, âŸ¨Î» H a b ha hb hab, H _ âŸ¨a, b, ha, hb, hab, rflâŸ©, _âŸ©),
  rintro H _ âŸ¨a, b, ha, hb, hab, rflâŸ©,
  exact H ha hb hab
end

lemma convex.mem_smul_of_zero_mem (h : convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s)
  (hx : x âˆˆ s) {t : ğ•œ} (ht : 1 â‰¤ t) :
  x âˆˆ t â€¢ s :=
begin
  rw mem_smul_set_iff_inv_smul_memâ‚€ (zero_lt_one.trans_le ht).ne',
  exact h.smul_mem_of_zero_mem zero_mem hx âŸ¨inv_nonneg.2 (zero_le_one.trans ht), inv_le_one htâŸ©,
end

lemma convex.add_smul (h_conv : convex ğ•œ s) {p q : ğ•œ} (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) :
  (p + q) â€¢ s = p â€¢ s + q â€¢ s :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { simp_rw [smul_set_empty, add_empty] },
  obtain rfl | hp' := hp.eq_or_lt,
  { rw [zero_add, zero_smul_set hs, zero_add] },
  obtain rfl | hq' := hq.eq_or_lt,
  { rw [add_zero, zero_smul_set hs, add_zero] },
  ext,
  split,
  { rintro âŸ¨v, hv, rflâŸ©,
    exact âŸ¨p â€¢ v, q â€¢ v, smul_mem_smul_set hv, smul_mem_smul_set hv, (add_smul _ _ _).symmâŸ© },
  { rintro âŸ¨vâ‚, vâ‚‚, âŸ¨vâ‚â‚, hâ‚â‚‚, rflâŸ©, âŸ¨vâ‚‚â‚, hâ‚‚â‚‚, rflâŸ©, rflâŸ©,
    have hpq := add_pos hp' hq',
    exact mem_smul_set.2 âŸ¨_, h_conv hâ‚â‚‚ hâ‚‚â‚‚ (div_pos hp' hpq).le (div_pos hq' hpq).le
      (by rw [â†div_self hpq.ne', add_div] : p / (p + q) + q / (p + q) = 1),
      by simp only [â† mul_smul, smul_add, mul_div_cancel' _ hpq.ne']âŸ© }
end

end add_comm_group
end linear_ordered_field

/-!
#### Convex sets in an ordered space
Relates `convex` and `ord_connected`.
-/

section

lemma set.ord_connected.convex_of_chain [ordered_semiring ğ•œ] [ordered_add_comm_monoid E]
  [module ğ•œ E] [ordered_smul ğ•œ E] {s : set E} (hs : s.ord_connected) (h : is_chain (â‰¤) s) :
  convex ğ•œ s :=
begin
  refine convex_iff_segment_subset.mpr (Î» x y hx hy, _),
  obtain hxy | hyx := h.total hx hy,
  { exact (segment_subset_Icc hxy).trans (hs.out hx hy) },
  { rw segment_symm,
    exact (segment_subset_Icc hyx).trans (hs.out hy hx) }
end

lemma set.ord_connected.convex [ordered_semiring ğ•œ] [linear_ordered_add_comm_monoid E] [module ğ•œ E]
  [ordered_smul ğ•œ E] {s : set E} (hs : s.ord_connected) :
  convex ğ•œ s :=
hs.convex_of_chain $ is_chain_of_trichotomous s

lemma convex_iff_ord_connected [linear_ordered_field ğ•œ] {s : set ğ•œ} :
  convex ğ•œ s â†” s.ord_connected :=
begin
  simp_rw [convex_iff_segment_subset, segment_eq_interval, ord_connected_iff_interval_subset],
  exact forall_congr (Î» x, forall_swap)
end

alias convex_iff_ord_connected â†” convex.ord_connected _

end

/-! #### Convexity of submodules/subspaces -/

section submodule
open submodule

lemma submodule.convex [ordered_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E] (K : submodule ğ•œ E) :
  convex ğ•œ (â†‘K : set E) :=
by { repeat {intro}, refine add_mem (smul_mem _ _ _) (smul_mem _ _ _); assumption }

lemma subspace.convex [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E] (K : subspace ğ•œ E) :
  convex ğ•œ (â†‘K : set E) :=
K.convex

end submodule

/-! ### Simplex -/

section simplex

variables (ğ•œ) (Î¹ : Type*) [ordered_semiring ğ•œ] [fintype Î¹]

/-- The standard simplex in the space of functions `Î¹ â†’ ğ•œ` is the set of vectors with non-negative
coordinates with total sum `1`. This is the free object in the category of convex spaces. -/
def std_simplex : set (Î¹ â†’ ğ•œ) :=
{f | (âˆ€ x, 0 â‰¤ f x) âˆ§ âˆ‘ x, f x = 1}

lemma std_simplex_eq_inter :
  std_simplex ğ•œ Î¹ = (â‹‚ x, {f | 0 â‰¤ f x}) âˆ© {f | âˆ‘ x, f x = 1} :=
by { ext f, simp only [std_simplex, set.mem_inter_eq, set.mem_Inter, set.mem_set_of_eq] }

lemma convex_std_simplex : convex ğ•œ (std_simplex ğ•œ Î¹) :=
begin
  refine Î» f g hf hg a b ha hb hab, âŸ¨Î» x, _, _âŸ©,
  { apply_rules [add_nonneg, mul_nonneg, hf.1, hg.1] },
  { erw [finset.sum_add_distrib, â† finset.smul_sum, â† finset.smul_sum, hf.2, hg.2,
      smul_eq_mul, smul_eq_mul, mul_one, mul_one],
    exact hab }
end

variable {Î¹}

lemma ite_eq_mem_std_simplex (i : Î¹) : (Î» j, ite (i = j) (1:ğ•œ) 0) âˆˆ std_simplex ğ•œ Î¹ :=
âŸ¨Î» j, by simp only; split_ifs; norm_num, by rw [finset.sum_ite_eq, if_pos (finset.mem_univ _)]âŸ©

end simplex
