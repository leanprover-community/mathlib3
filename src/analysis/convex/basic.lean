/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudriashov, YaÃ«l Dillies
-/
import algebra.order.module
import analysis.convex.star
import linear_algebra.affine_space.affine_subspace

/-!
# Convex sets and functions in vector spaces

In a ğ•œ-vector space, we define the following objects and properties.
* `convex ğ•œ s`: A set `s` is convex if for any two points `x y âˆˆ s` it includes `segment ğ•œ x y`.
* `std_simplex ğ•œ Î¹`: The standard simplex in `Î¹ â†’ ğ•œ` (currently requires `fintype Î¹`). It is the
  intersection of the positive quadrant with the hyperplane `s.sum = 1`.

We also provide various equivalent versions of the definitions above, prove that some specific sets
are convex.

## TODO

Generalize all this file to affine spaces.
-/

variables {ğ•œ E F Î² : Type*}

open linear_map set
open_locale big_operators classical convex pointwise

/-! ### Convexity of sets -/

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F]

section has_smul
variables (ğ•œ) [has_smul ğ•œ E] [has_smul ğ•œ F] (s : set E) {x : E}

/-- Convexity of sets. -/
def convex : Prop := âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ star_convex ğ•œ x s

variables {ğ•œ s}

lemma convex.star_convex (hs : convex ğ•œ s) (hx : x âˆˆ s) : star_convex ğ•œ x s := hs hx

lemma convex_iff_segment_subset : convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ [x -[ğ•œ] y] âŠ† s :=
forallâ‚‚_congr $ Î» x hx, star_convex_iff_segment_subset

lemma convex.segment_subset (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
  [x -[ğ•œ] y] âŠ† s :=
convex_iff_segment_subset.1 h hx hy

lemma convex.open_segment_subset (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
  open_segment ğ•œ x y âŠ† s :=
(open_segment_subset_segment ğ•œ x y).trans (h.segment_subset hx hy)

/-- Alternative definition of set convexity, in terms of pointwise set operations. -/
lemma convex_iff_pointwise_add_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ s + b â€¢ s âŠ† s :=
iff.intro
  begin
    rintro hA a b ha hb hab w âŸ¨au, bv, âŸ¨u, hu, rflâŸ©, âŸ¨v, hv, rflâŸ©, rflâŸ©,
    exact hA hu hv ha hb hab
  end
  (Î» h x hx y hy a b ha hb hab,
    (h ha hb hab) (set.add_mem_add âŸ¨_, hx, rflâŸ© âŸ¨_, hy, rflâŸ©))

alias convex_iff_pointwise_add_subset â†” convex.set_combo_subset _

lemma convex_empty : convex ğ•œ (âˆ… : set E) := Î» x, false.elim

lemma convex_univ : convex ğ•œ (set.univ : set E) := Î» _ _, star_convex_univ _

lemma convex.inter {t : set E} (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s âˆ© t) :=
Î» x hx, (hs hx.1).inter (ht hx.2)

lemma convex_sInter {S : set (set E)} (h : âˆ€ s âˆˆ S, convex ğ•œ s) : convex ğ•œ (â‹‚â‚€ S) :=
Î» x hx, star_convex_sInter $ Î» s hs, h _ hs $ hx _ hs

lemma convex_Inter {Î¹ : Sort*} {s : Î¹ â†’ set E} (h : âˆ€ i, convex ğ•œ (s i)) : convex ğ•œ (â‹‚ i, s i) :=
(sInter_range s) â–¸ convex_sInter $ forall_range_iff.2 h

lemma convex_Interâ‚‚ {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*} {s : Î  i, Îº i â†’ set E}
  (h : âˆ€ i j, convex ğ•œ (s i j)) :
  convex ğ•œ (â‹‚ i j, s i j) :=
convex_Inter $ Î» i, convex_Inter $ h i

lemma convex.prod {s : set E} {t : set F} (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s Ã—Ë¢ t) :=
Î» x hx, (hs hx.1).prod (ht hx.2)

lemma convex_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [Î  i, add_comm_monoid (E i)]
  [Î  i, has_smul ğ•œ (E i)] {s : set Î¹} {t : Î  i, set (E i)} (ht : âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ convex ğ•œ (t i)) :
  convex ğ•œ (s.pi t) :=
Î» x hx, star_convex_pi $ Î» i hi, ht hi $ hx _ hi

lemma directed.convex_Union {Î¹ : Sort*} {s : Î¹ â†’ set E} (hdir : directed (âŠ†) s)
  (hc : âˆ€ â¦ƒi : Î¹â¦„, convex ğ•œ (s i)) :
  convex ğ•œ (â‹ƒ i, s i) :=
begin
  rintro x hx y hy a b ha hb hab,
  rw mem_Union at âŠ¢ hx hy,
  obtain âŸ¨i, hxâŸ© := hx,
  obtain âŸ¨j, hyâŸ© := hy,
  obtain âŸ¨k, hik, hjkâŸ© := hdir i j,
  exact âŸ¨k, hc (hik hx) (hjk hy) ha hb habâŸ©,
end

lemma directed_on.convex_sUnion {c : set (set E)} (hdir : directed_on (âŠ†) c)
  (hc : âˆ€ â¦ƒA : set Eâ¦„, A âˆˆ c â†’ convex ğ•œ A) :
  convex ğ•œ (â‹ƒâ‚€c) :=
begin
  rw sUnion_eq_Union,
  exact (directed_on_iff_directed.1 hdir).convex_Union (Î» A, hc A.2),
end

end has_smul

section module
variables [module ğ•œ E] [module ğ•œ F] {s : set E} {x : E}

lemma convex_iff_open_segment_subset :
  convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ open_segment ğ•œ x y âŠ† s :=
forallâ‚‚_congr $ Î» x, star_convex_iff_open_segment_subset

lemma convex_iff_forall_pos :
  convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1
  â†’ a â€¢ x + b â€¢ y âˆˆ s :=
forallâ‚‚_congr $ Î» x, star_convex_iff_forall_pos

lemma convex_iff_pairwise_pos :
  convex ğ•œ s â†” s.pairwise (Î» x y, âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s) :=
begin
  refine convex_iff_forall_pos.trans âŸ¨Î» h x hx y hy _, h hx hy, _âŸ©,
  intros h x hx y hy a b ha hb hab,
  obtain rfl | hxy := eq_or_ne x y,
  { rwa convex.combo_self hab },
  { exact h hx hy hxy ha hb hab },
end

lemma convex.star_convex_iff (hs : convex ğ•œ s) (h : s.nonempty) : star_convex ğ•œ x s â†” x âˆˆ s :=
âŸ¨Î» hxs, hxs.mem h, hs.star_convexâŸ©

protected lemma set.subsingleton.convex {s : set E} (h : s.subsingleton) : convex ğ•œ s :=
convex_iff_pairwise_pos.mpr (h.pairwise _)

lemma convex_singleton (c : E) : convex ğ•œ ({c} : set E) :=
subsingleton_singleton.convex

lemma convex_segment (x y : E) : convex ğ•œ [x -[ğ•œ] y] :=
begin
  rintro p âŸ¨ap, bp, hap, hbp, habp, rflâŸ© q âŸ¨aq, bq, haq, hbq, habq, rflâŸ© a b ha hb hab,
  refine âŸ¨a * ap + b * aq, a * bp + b * bq,
    add_nonneg (mul_nonneg ha hap) (mul_nonneg hb haq),
    add_nonneg (mul_nonneg ha hbp) (mul_nonneg hb hbq), _, _âŸ©,
  { rw [add_add_add_comm, â†mul_add, â†mul_add, habp, habq, mul_one, mul_one, hab] },
  { simp_rw [add_smul, mul_smul, smul_add],
    exact add_add_add_comm _ _ _ _ }
end

lemma convex.linear_image (hs : convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) : convex ğ•œ (f '' s) :=
begin
  intros x hx y hy a b ha hb hab,
  obtain âŸ¨x', hx', rflâŸ© := mem_image_iff_bex.1 hx,
  obtain âŸ¨y', hy', rflâŸ© := mem_image_iff_bex.1 hy,
  exact âŸ¨a â€¢ x' + b â€¢ y', hs hx' hy' ha hb hab, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©,
end

lemma convex.is_linear_image (hs : convex ğ•œ s) {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  convex ğ•œ (f '' s) :=
hs.linear_image $ hf.mk' f

lemma convex.linear_preimage {s : set F} (hs : convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) :
  convex ğ•œ (f â»Â¹' s) :=
begin
  intros x hx y hy a b ha hb hab,
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul],
  exact hs hx hy ha hb hab,
end

lemma convex.is_linear_preimage {s : set F} (hs : convex ğ•œ s) {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  convex ğ•œ (f â»Â¹' s) :=
hs.linear_preimage $ hf.mk' f

lemma convex.add {t : set E} (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s + t) :=
by { rw â† add_image_prod, exact (hs.prod ht).is_linear_image is_linear_map.is_linear_map_add }

lemma convex.vadd (hs : convex ğ•œ s) (z : E) : convex ğ•œ (z +áµ¥ s) :=
by { simp_rw [â†image_vadd, vadd_eq_add, â†singleton_add], exact (convex_singleton _).add hs }

lemma convex.translate (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, z + x) '' s) := hs.vadd _

/-- The translation of a convex set is also convex. -/
lemma convex.translate_preimage_right (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, z + x) â»Â¹' s) :=
begin
  intros x hx y hy a b ha hb hab,
  have h := hs hx hy ha hb hab,
  rwa [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul] at h,
end

/-- The translation of a convex set is also convex. -/
lemma convex.translate_preimage_left (hs : convex ğ•œ s) (z : E) : convex ğ•œ ((Î» x, x + z) â»Â¹' s) :=
by simpa only [add_comm] using hs.translate_preimage_right z

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]

lemma convex_Iic (r : Î²) : convex ğ•œ (Iic r) :=
Î» x hx y hy a b ha hb hab,
calc
  a â€¢ x + b â€¢ y
      â‰¤ a â€¢ r + b â€¢ r
      : add_le_add (smul_le_smul_of_nonneg hx ha) (smul_le_smul_of_nonneg hy hb)
  ... = r : convex.combo_self hab _

lemma convex_Ici (r : Î²) : convex ğ•œ (Ici r) := @convex_Iic ğ•œ Î²áµ’áµˆ _ _ _ _ r

lemma convex_Icc (r s : Î²) : convex ğ•œ (Icc r s) :=
Ici_inter_Iic.subst ((convex_Ici r).inter $ convex_Iic s)

lemma convex_halfspace_le {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | f w â‰¤ r} :=
(convex_Iic r).is_linear_preimage h

lemma convex_halfspace_ge {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | r â‰¤ f w} :=
(convex_Ici r).is_linear_preimage h

lemma convex_hyperplane {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | f w = r} :=
begin
  simp_rw le_antisymm_iff,
  exact (convex_halfspace_le h r).inter (convex_halfspace_ge h r),
end

end ordered_add_comm_monoid

section ordered_cancel_add_comm_monoid
variables [ordered_cancel_add_comm_monoid Î²] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]

lemma convex_Iio (r : Î²) : convex ğ•œ (Iio r) :=
begin
  intros x hx y hy a b ha hb hab,
  obtain rfl | ha' := ha.eq_or_lt,
  { rw zero_add at hab,
    rwa [zero_smul, zero_add, hab, one_smul] },
  rw mem_Iio at hx hy,
  calc
    a â€¢ x + b â€¢ y
        < a â€¢ r + b â€¢ r
        : add_lt_add_of_lt_of_le (smul_lt_smul_of_pos hx ha') (smul_le_smul_of_nonneg hy.le hb)
    ... = r : convex.combo_self hab _
end

lemma convex_Ioi (r : Î²) : convex ğ•œ (Ioi r) := @convex_Iio ğ•œ Î²áµ’áµˆ _ _ _ _ r

lemma convex_Ioo (r s : Î²) : convex ğ•œ (Ioo r s) :=
Ioi_inter_Iio.subst ((convex_Ioi r).inter $ convex_Iio s)

lemma convex_Ico (r s : Î²) : convex ğ•œ (Ico r s) :=
Ici_inter_Iio.subst ((convex_Ici r).inter $ convex_Iio s)

lemma convex_Ioc (r s : Î²) : convex ğ•œ (Ioc r s) :=
Ioi_inter_Iic.subst ((convex_Ioi r).inter $ convex_Iic s)

lemma convex_halfspace_lt {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | f w < r} :=
(convex_Iio r).is_linear_preimage h

lemma convex_halfspace_gt {f : E â†’ Î²} (h : is_linear_map ğ•œ f) (r : Î²) :
  convex ğ•œ {w | r < f w} :=
(convex_Ioi r).is_linear_preimage h

end ordered_cancel_add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid Î²] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]

lemma convex_uIcc (r s : Î²) : convex ğ•œ (uIcc r s) := convex_Icc _ _

end linear_ordered_add_comm_monoid
end module
end add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid E] [ordered_add_comm_monoid Î²] [module ğ•œ E]
  [ordered_smul ğ•œ E] {s : set E} {f : E â†’ Î²}

lemma monotone_on.convex_le (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x â‰¤ r} :=
Î» x hx y hy a b ha hb hab, âŸ¨hs hx.1 hy.1 ha hb hab,
  (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1) (convex.combo_le_max x y ha hb hab)).trans
    (max_rec' _ hx.2 hy.2)âŸ©

lemma monotone_on.convex_lt (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x < r} :=
Î» x hx y hy a b ha hb hab, âŸ¨hs hx.1 hy.1 ha hb hab,
  (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1) (convex.combo_le_max x y ha hb hab)).trans_lt
    (max_rec' _ hx.2 hy.2)âŸ©

lemma monotone_on.convex_ge (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r â‰¤ f x} :=
@monotone_on.convex_le ğ•œ Eáµ’áµˆ Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual hs r

lemma monotone_on.convex_gt (hf : monotone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r < f x} :=
@monotone_on.convex_lt ğ•œ Eáµ’áµˆ Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual hs r

lemma antitone_on.convex_le (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x â‰¤ r} :=
@monotone_on.convex_ge ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma antitone_on.convex_lt (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x < r} :=
@monotone_on.convex_gt ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma antitone_on.convex_ge (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r â‰¤ f x} :=
@monotone_on.convex_le ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma antitone_on.convex_gt (hf : antitone_on f s) (hs : convex ğ•œ s) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r < f x} :=
@monotone_on.convex_lt ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r

lemma monotone.convex_le (hf : monotone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_le convex_univ r)

lemma monotone.convex_lt (hf : monotone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_le convex_univ r)

lemma monotone.convex_ge (hf : monotone f ) (r : Î²) :
  convex ğ•œ {x | r â‰¤ f x} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_ge convex_univ r)

lemma monotone.convex_gt (hf : monotone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.monotone_on univ).convex_le convex_univ r)

lemma antitone.convex_le (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | f x â‰¤ r} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_le convex_univ r)

lemma antitone.convex_lt (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | f x < r} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_lt convex_univ r)

lemma antitone.convex_ge (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | r â‰¤ f x} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_ge convex_univ r)

lemma antitone.convex_gt (hf : antitone f) (r : Î²) :
  convex ğ•œ {x | r < f x} :=
set.sep_univ.subst ((hf.antitone_on univ).convex_gt convex_univ r)

end linear_ordered_add_comm_monoid
end ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F] [module ğ•œ E] [module ğ•œ F] {s : set E}

lemma convex.smul (hs : convex ğ•œ s) (c : ğ•œ) : convex ğ•œ (c â€¢ s) :=
hs.linear_image (linear_map.lsmul _ _ c)

lemma convex.smul_preimage (hs : convex ğ•œ s) (c : ğ•œ) : convex ğ•œ ((Î» z, c â€¢ z) â»Â¹' s) :=
hs.linear_preimage (linear_map.lsmul _ _ c)

lemma convex.affinity (hs : convex ğ•œ s) (z : E) (c : ğ•œ) : convex ğ•œ ((Î» x, z + c â€¢ x) '' s) :=
by simpa only [â†image_smul, â†image_vadd, image_image] using (hs.smul c).vadd z

end add_comm_monoid
end ordered_comm_semiring

section strict_ordered_comm_semiring
variables [strict_ordered_comm_semiring ğ•œ] [add_comm_group E] [module ğ•œ E]

lemma convex_open_segment (a b : E) : convex ğ•œ (open_segment ğ•œ a b) :=
begin
  rw convex_iff_open_segment_subset,
  rintro p âŸ¨ap, bp, hap, hbp, habp, rflâŸ© q âŸ¨aq, bq, haq, hbq, habq, rflâŸ© z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  refine âŸ¨a * ap + b * aq, a * bp + b * bq, by positivity, by positivity, _, _âŸ©,
  { rw [add_add_add_comm, â†mul_add, â†mul_add, habp, habq, mul_one, mul_one, hab] },
  { simp_rw [add_smul, mul_smul, smul_add, add_add_add_comm] }
end

end strict_ordered_comm_semiring

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {s t : set E}

lemma convex.add_smul_mem (hs : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : x + y âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ y âˆˆ s :=
begin
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y),
  { rw [smul_add, â†add_assoc, â†add_smul, sub_add_cancel, one_smul] },
  rw h,
  exact hs hx hy (sub_nonneg_of_le ht.2) ht.1 (sub_add_cancel _ _),
end

lemma convex.smul_mem_of_zero_mem (hs : convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s) (hx : x âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : t â€¢ x âˆˆ s :=
by simpa using hs.add_smul_mem zero_mem (by simpa using hx) ht

lemma convex.add_smul_sub_mem (h : convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ (y - x) âˆˆ s :=
begin
  apply h.segment_subset hx hy,
  rw segment_eq_image',
  exact mem_image_of_mem _ ht,
end

/-- Affine subspaces are convex. -/
lemma affine_subspace.convex (Q : affine_subspace ğ•œ E) : convex ğ•œ (Q : set E) :=
begin
  intros x hx y hy a b ha hb hab,
  rw [eq_sub_of_add_eq hab, â† affine_map.line_map_apply_module],
  exact affine_map.line_map_mem b hx hy,
end

/-- The preimage of a convex set under an affine map is convex. -/
lemma convex.affine_preimage (f : E â†’áµƒ[ğ•œ] F) {s : set F} (hs : convex ğ•œ s) :
  convex ğ•œ (f â»Â¹' s) :=
Î» x hx, (hs hx).affine_preimage _

/-- The image of a convex set under an affine map is convex. -/
lemma convex.affine_image (f : E â†’áµƒ[ğ•œ] F) (hs : convex ğ•œ s) : convex ğ•œ (f '' s) :=
by { rintro _ âŸ¨x, hx, rflâŸ©, exact (hs hx).affine_image _ }

lemma convex.neg (hs : convex ğ•œ s) : convex ğ•œ (-s) :=
hs.is_linear_preimage is_linear_map.is_linear_map_neg

lemma convex.sub (hs : convex ğ•œ s) (ht : convex ğ•œ t) : convex ğ•œ (s - t) :=
by { rw sub_eq_add_neg, exact hs.add ht.neg }

end add_comm_group
end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {s : set E}

/-- Alternative definition of set convexity, using division. -/
lemma convex_iff_div :
  convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„,
    0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’ (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ s :=
forallâ‚‚_congr $ Î» x hx, star_convex_iff_div

lemma convex.mem_smul_of_zero_mem (h : convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s)
  (hx : x âˆˆ s) {t : ğ•œ} (ht : 1 â‰¤ t) :
  x âˆˆ t â€¢ s :=
begin
  rw mem_smul_set_iff_inv_smul_memâ‚€ (zero_lt_one.trans_le ht).ne',
  exact h.smul_mem_of_zero_mem zero_mem hx âŸ¨inv_nonneg.2 (zero_le_one.trans ht), inv_le_one htâŸ©,
end

lemma convex.add_smul (h_conv : convex ğ•œ s) {p q : ğ•œ} (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) :
  (p + q) â€¢ s = p â€¢ s + q â€¢ s :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { simp_rw [smul_set_empty, add_empty] },
  obtain rfl | hp' := hp.eq_or_lt,
  { rw [zero_add, zero_smul_set hs, zero_add] },
  obtain rfl | hq' := hq.eq_or_lt,
  { rw [add_zero, zero_smul_set hs, add_zero] },
  ext,
  split,
  { rintro âŸ¨v, hv, rflâŸ©,
    exact âŸ¨p â€¢ v, q â€¢ v, smul_mem_smul_set hv, smul_mem_smul_set hv, (add_smul _ _ _).symmâŸ© },
  { rintro âŸ¨vâ‚, vâ‚‚, âŸ¨vâ‚â‚, hâ‚â‚‚, rflâŸ©, âŸ¨vâ‚‚â‚, hâ‚‚â‚‚, rflâŸ©, rflâŸ©,
    have hpq := add_pos hp' hq',
    refine mem_smul_set.2 âŸ¨_, h_conv hâ‚â‚‚ hâ‚‚â‚‚ _ _
      (by rw [â†div_self hpq.ne', add_div] : p / (p + q) + q / (p + q) = 1),
      by simp only [â† mul_smul, smul_add, mul_div_cancel' _ hpq.ne']âŸ©; positivity }
end

end add_comm_group
end linear_ordered_field

/-!
#### Convex sets in an ordered space
Relates `convex` and `ord_connected`.
-/

section

lemma set.ord_connected.convex_of_chain [ordered_semiring ğ•œ] [ordered_add_comm_monoid E]
  [module ğ•œ E] [ordered_smul ğ•œ E] {s : set E} (hs : s.ord_connected) (h : is_chain (â‰¤) s) :
  convex ğ•œ s :=
begin
  refine convex_iff_segment_subset.mpr (Î» x hx y hy, _),
  obtain hxy | hyx := h.total hx hy,
  { exact (segment_subset_Icc hxy).trans (hs.out hx hy) },
  { rw segment_symm,
    exact (segment_subset_Icc hyx).trans (hs.out hy hx) }
end

lemma set.ord_connected.convex [ordered_semiring ğ•œ] [linear_ordered_add_comm_monoid E] [module ğ•œ E]
  [ordered_smul ğ•œ E] {s : set E} (hs : s.ord_connected) :
  convex ğ•œ s :=
hs.convex_of_chain $ is_chain_of_trichotomous s

lemma convex_iff_ord_connected [linear_ordered_field ğ•œ] {s : set ğ•œ} :
  convex ğ•œ s â†” s.ord_connected :=
by simp_rw [convex_iff_segment_subset, segment_eq_uIcc, ord_connected_iff_uIcc_subset]

alias convex_iff_ord_connected â†” convex.ord_connected _

end

/-! #### Convexity of submodules/subspaces -/

namespace submodule
variables [ordered_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E]

protected lemma convex (K : submodule ğ•œ E) : convex ğ•œ (â†‘K : set E) :=
by { repeat {intro}, refine add_mem (smul_mem _ _ _) (smul_mem _ _ _); assumption }

protected lemma star_convex (K : submodule ğ•œ E) : star_convex ğ•œ (0 : E) K := K.convex K.zero_mem

end submodule

/-! ### Simplex -/

section simplex

variables (ğ•œ) (Î¹ : Type*) [ordered_semiring ğ•œ] [fintype Î¹]

/-- The standard simplex in the space of functions `Î¹ â†’ ğ•œ` is the set of vectors with non-negative
coordinates with total sum `1`. This is the free object in the category of convex spaces. -/
def std_simplex : set (Î¹ â†’ ğ•œ) :=
{f | (âˆ€ x, 0 â‰¤ f x) âˆ§ âˆ‘ x, f x = 1}

lemma std_simplex_eq_inter :
  std_simplex ğ•œ Î¹ = (â‹‚ x, {f | 0 â‰¤ f x}) âˆ© {f | âˆ‘ x, f x = 1} :=
by { ext f, simp only [std_simplex, set.mem_inter_iff, set.mem_Inter, set.mem_set_of_eq] }

lemma convex_std_simplex : convex ğ•œ (std_simplex ğ•œ Î¹) :=
begin
  refine Î» f hf g hg a b ha hb hab, âŸ¨Î» x, _, _âŸ©,
  { apply_rules [add_nonneg, mul_nonneg, hf.1, hg.1] },
  { erw [finset.sum_add_distrib, â† finset.smul_sum, â† finset.smul_sum, hf.2, hg.2,
      smul_eq_mul, smul_eq_mul, mul_one, mul_one],
    exact hab }
end

variable {Î¹}

lemma ite_eq_mem_std_simplex (i : Î¹) : (Î» j, ite (i = j) (1:ğ•œ) 0) âˆˆ std_simplex ğ•œ Î¹ :=
âŸ¨Î» j, by simp only; split_ifs; norm_num, by rw [finset.sum_ite_eq, if_pos (finset.mem_univ _)]âŸ©

end simplex
