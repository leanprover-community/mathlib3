/-
Copyright (c) 2019 Yury Kudriashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudriashov
-/
import algebra.big_operators.order
import analysis.convex.hull
import linear_algebra.affine_space.basis

/-!
# Convex combinations

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines convex combinations of points in a vector space.

## Main declarations

* `finset.center_mass`: Center of mass of a finite family of points.

## Implementation notes

We divide by the sum of the weights in the definition of `finset.center_mass` because of the way
mathematical arguments go: one doesn't change weights, but merely adds some. This also makes a few
lemmas unconditional on the sum of the weights being `1`.
-/

open set function
open_locale big_operators classical pointwise

universes u u'
variables {R E F Î¹ Î¹' Î± : Type*} [linear_ordered_field R] [add_comm_group E] [add_comm_group F]
  [linear_ordered_add_comm_group Î±] [module R E] [module R F] [module R Î±] [ordered_smul R Î±]
  {s : set E}

/-- Center of mass of a finite collection of points with prescribed weights.
Note that we require neither `0 â‰¤ w i` nor `âˆ‘ w = 1`. -/
def finset.center_mass (t : finset Î¹) (w : Î¹ â†’ R) (z : Î¹ â†’ E) : E :=
(âˆ‘ i in t, w i)â»Â¹ â€¢ (âˆ‘ i in t, w i â€¢ z i)

variables (i j : Î¹) (c : R) (t : finset Î¹) (w : Î¹ â†’ R) (z : Î¹ â†’ E)

open finset

lemma finset.center_mass_empty : (âˆ… : finset Î¹).center_mass w z = 0 :=
by simp only [center_mass, sum_empty, smul_zero]

lemma finset.center_mass_pair (hne : i â‰  j) :
  ({i, j} : finset Î¹).center_mass w z = (w i / (w i + w j)) â€¢ z i + (w j / (w i + w j)) â€¢ z j :=
by simp only [center_mass, sum_pair hne, smul_add, (mul_smul _ _ _).symm, div_eq_inv_mul]

variable {w}

lemma finset.center_mass_insert (ha : i âˆ‰ t) (hw : âˆ‘ j in t, w j â‰  0) :
  (insert i t).center_mass w z = (w i / (w i + âˆ‘ j in t, w j)) â€¢ z i +
    ((âˆ‘ j in t, w j) / (w i + âˆ‘ j in t, w j)) â€¢ t.center_mass w z :=
begin
  simp only [center_mass, sum_insert ha, smul_add, (mul_smul _ _ _).symm, â† div_eq_inv_mul],
  congr' 2,
  rw [div_mul_eq_mul_div, mul_inv_cancel hw, one_div]
end

lemma finset.center_mass_singleton (hw : w i â‰  0) : ({i} : finset Î¹).center_mass w z = z i :=
by rw [center_mass, sum_singleton, sum_singleton, â† mul_smul, inv_mul_cancel hw, one_smul]

lemma finset.center_mass_eq_of_sum_1 (hw : âˆ‘ i in t, w i = 1) :
  t.center_mass w z = âˆ‘ i in t, w i â€¢ z i :=
by simp only [finset.center_mass, hw, inv_one, one_smul]

lemma finset.center_mass_smul : t.center_mass w (Î» i, c â€¢ z i) = c â€¢ t.center_mass w z :=
by simp only [finset.center_mass, finset.smul_sum, (mul_smul _ _ _).symm, mul_comm c, mul_assoc]

/-- A convex combination of two centers of mass is a center of mass as well. This version
deals with two different index types. -/
lemma finset.center_mass_segment'
  (s : finset Î¹) (t : finset Î¹') (ws : Î¹ â†’ R) (zs : Î¹ â†’ E) (wt : Î¹' â†’ R) (zt : Î¹' â†’ E)
  (hws : âˆ‘ i in s, ws i = 1) (hwt : âˆ‘ i in t, wt i = 1) (a b : R) (hab : a + b = 1) :
  a â€¢ s.center_mass ws zs + b â€¢ t.center_mass wt zt =
    (s.disj_sum t).center_mass (sum.elim (Î» i, a * ws i) (Î» j, b * wt j)) (sum.elim zs zt) :=
begin
  rw [s.center_mass_eq_of_sum_1 _ hws, t.center_mass_eq_of_sum_1 _ hwt,
    smul_sum, smul_sum, â† finset.sum_sum_elim, finset.center_mass_eq_of_sum_1],
  { congr' with âŸ¨âŸ©; simp only [sum.elim_inl, sum.elim_inr, mul_smul] },
  { rw [sum_sum_elim, â† mul_sum, â† mul_sum, hws, hwt, mul_one, mul_one, hab] }
end

/-- A convex combination of two centers of mass is a center of mass as well. This version
works if two centers of mass share the set of original points. -/
lemma finset.center_mass_segment
  (s : finset Î¹) (wâ‚ wâ‚‚ : Î¹ â†’ R) (z : Î¹ â†’ E)
  (hwâ‚ : âˆ‘ i in s, wâ‚ i = 1) (hwâ‚‚ : âˆ‘ i in s, wâ‚‚ i = 1) (a b : R) (hab : a + b = 1) :
  a â€¢ s.center_mass wâ‚ z + b â€¢ s.center_mass wâ‚‚ z =
    s.center_mass (Î» i, a * wâ‚ i + b * wâ‚‚ i) z :=
have hw : âˆ‘ i in s, (a * wâ‚ i + b * wâ‚‚ i) = 1,
  by simp only [mul_sum.symm, sum_add_distrib, mul_one, *],
by simp only [finset.center_mass_eq_of_sum_1, smul_sum, sum_add_distrib, add_smul, mul_smul, *]

lemma finset.center_mass_ite_eq (hi : i âˆˆ t) :
  t.center_mass (Î» j, if (i = j) then (1 : R) else 0) z = z i :=
begin
  rw [finset.center_mass_eq_of_sum_1],
  transitivity âˆ‘ j in t, if (i = j) then z i else 0,
  { congr' with i, split_ifs, exacts [h â–¸ one_smul _ _, zero_smul _ _] },
  { rw [sum_ite_eq, if_pos hi] },
  { rw [sum_ite_eq, if_pos hi] }
end

variables {t w}

lemma finset.center_mass_subset {t' : finset Î¹} (ht : t âŠ† t')
  (h : âˆ€ i âˆˆ t', i âˆ‰ t â†’ w i = 0) :
  t.center_mass w z = t'.center_mass w z :=
begin
  rw [center_mass, sum_subset ht h, smul_sum, center_mass, smul_sum],
  apply sum_subset ht,
  assume i hit' hit,
  rw [h i hit' hit, zero_smul, smul_zero]
end

lemma finset.center_mass_filter_ne_zero :
  (t.filter (Î» i, w i â‰  0)).center_mass w z = t.center_mass w z :=
finset.center_mass_subset z (filter_subset _ _) $ Î» i hit hit',
  by simpa only [hit, mem_filter, true_and, ne.def, not_not] using hit'

namespace finset

lemma center_mass_le_sup {s : finset Î¹} {f : Î¹ â†’ Î±} {w : Î¹ â†’ R}
  (hwâ‚€ : âˆ€ i âˆˆ s, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i in s, w i) :
  s.center_mass w f â‰¤ s.sup' (nonempty_of_ne_empty $ by { rintro rfl, simpa using hwâ‚ }) f :=
begin
  rw [center_mass, inv_smul_le_iff hwâ‚, sum_smul],
  exact sum_le_sum (Î» i hi, smul_le_smul_of_nonneg (le_sup' _ hi) $ hwâ‚€ i hi),
  apply_instance,
end

lemma inf_le_center_mass {s : finset Î¹} {f : Î¹ â†’ Î±} {w : Î¹ â†’ R}
  (hwâ‚€ : âˆ€ i âˆˆ s, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i in s, w i) :
  s.inf' (nonempty_of_ne_empty $ by { rintro rfl, simpa using hwâ‚ }) f â‰¤ s.center_mass w f :=
@center_mass_le_sup R _ Î±áµ’áµˆ _ _ _ _ _ _ _ hwâ‚€ hwâ‚

end finset


variable {z}

/-- The center of mass of a finite subset of a convex set belongs to the set
provided that all weights are non-negative, and the total weight is positive. -/
lemma convex.center_mass_mem (hs : convex R s) :
  (âˆ€ i âˆˆ t, 0 â‰¤ w i) â†’ (0 < âˆ‘ i in t, w i) â†’ (âˆ€ i âˆˆ t, z i âˆˆ s) â†’ t.center_mass w z âˆˆ s :=
begin
  induction t using finset.induction with i t hi ht, { simp [lt_irrefl] },
  intros hâ‚€ hpos hmem,
  have zi : z i âˆˆ s, from hmem _ (mem_insert_self _ _),
  have hsâ‚€ : âˆ€ j âˆˆ t, 0 â‰¤ w j, from Î» j hj, hâ‚€ j $ mem_insert_of_mem hj,
  rw [sum_insert hi] at hpos,
  by_cases hsum_t : âˆ‘ j in t, w j = 0,
  { have ws : âˆ€ j âˆˆ t, w j = 0, from (sum_eq_zero_iff_of_nonneg hsâ‚€).1 hsum_t,
    have wz : âˆ‘ j in t, w j â€¢ z j = 0, from sum_eq_zero (Î» i hi, by simp [ws i hi]),
    simp only [center_mass, sum_insert hi, wz, hsum_t, add_zero],
    simp only [hsum_t, add_zero] at hpos,
    rw [â† mul_smul, inv_mul_cancel (ne_of_gt hpos), one_smul],
    exact zi },
  { rw [finset.center_mass_insert _ _ _ hi hsum_t],
    refine convex_iff_div.1 hs zi (ht hsâ‚€ _ _) _ (sum_nonneg hsâ‚€) hpos,
    { exact lt_of_le_of_ne (sum_nonneg hsâ‚€) (ne.symm hsum_t) },
    { intros j hj, exact hmem j (mem_insert_of_mem hj) },
    { exact hâ‚€ _ (mem_insert_self _ _) } }
end

lemma convex.sum_mem (hs : convex R s) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i in t, w i = 1)
  (hz : âˆ€ i âˆˆ t, z i âˆˆ s) :
  âˆ‘ i in t, w i â€¢ z i âˆˆ s :=
by simpa only [hâ‚, center_mass, inv_one, one_smul] using
  hs.center_mass_mem hâ‚€ (hâ‚.symm â–¸ zero_lt_one) hz

/-- A version of `convex.sum_mem` for `finsum`s. If `s` is a convex set, `w : Î¹ â†’ R` is a family of
nonnegative weights with sum one and `z : Î¹ â†’ E` is a family of elements of a module over `R` such
that `z i âˆˆ s` whenever `w i â‰  0``, then the sum `âˆ‘á¶  i, w i â€¢ z i` belongs to `s`. See also
`partition_of_unity.finsum_smul_mem_convex`. -/
lemma convex.finsum_mem {Î¹ : Sort*} {w : Î¹ â†’ R} {z : Î¹ â†’ E} {s : set E}
  (hs : convex R s) (hâ‚€ : âˆ€ i, 0 â‰¤ w i) (hâ‚ : âˆ‘á¶  i, w i = 1) (hz : âˆ€ i, w i â‰  0 â†’ z i âˆˆ s) :
  âˆ‘á¶  i, w i â€¢ z i âˆˆ s :=
begin
  have hfin_w : (support (w âˆ˜ plift.down)).finite,
  { by_contra H,
    rw [finsum, dif_neg H] at hâ‚,
    exact zero_ne_one hâ‚ },
  have hsub : support ((Î» i, w i â€¢ z i) âˆ˜ plift.down) âŠ† hfin_w.to_finset,
    from (support_smul_subset_left _ _).trans hfin_w.coe_to_finset.ge,
  rw [finsum_eq_sum_plift_of_support_subset hsub],
  refine hs.sum_mem (Î» _ _, hâ‚€ _) _ (Î» i hi, hz _ _),
  { rwa [finsum, dif_pos hfin_w] at hâ‚ },
  { rwa [hfin_w.mem_to_finset] at hi }
end

lemma convex_iff_sum_mem :
  convex R s â†”
    (âˆ€ (t : finset E) (w : E â†’ R),
      (âˆ€ i âˆˆ t, 0 â‰¤ w i) â†’ âˆ‘ i in t, w i = 1 â†’ (âˆ€ x âˆˆ t, x âˆˆ s) â†’ âˆ‘ x in t, w x â€¢ x âˆˆ s ) :=
begin
  refine âŸ¨Î» hs t w hwâ‚€ hwâ‚ hts, hs.sum_mem hwâ‚€ hwâ‚ hts, _âŸ©,
  intros h x hx y hy a b ha hb hab,
  by_cases h_cases: x = y,
  { rw [h_cases, â†add_smul, hab, one_smul], exact hy },
  { convert h {x, y} (Î» z, if z = y then b else a) _ _ _,
    { simp only [sum_pair h_cases, if_neg h_cases, if_pos rfl] },
    { simp_intros i hi,
      cases hi; subst i; simp [ha, hb, if_neg h_cases] },
    { simp only [sum_pair h_cases, if_neg h_cases, if_pos rfl, hab] },
    { simp_intros i hi,
      cases hi; subst i; simp [hx, hy, if_neg h_cases] } }
end

lemma finset.center_mass_mem_convex_hull (t : finset Î¹) {w : Î¹ â†’ R} (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
  (hws : 0 < âˆ‘ i in t, w i) {z : Î¹ â†’ E} (hz : âˆ€ i âˆˆ t, z i âˆˆ s) :
  t.center_mass w z âˆˆ convex_hull R s :=
(convex_convex_hull R s).center_mass_mem hwâ‚€ hws (Î» i hi, subset_convex_hull R s $ hz i hi)

/-- A refinement of `finset.center_mass_mem_convex_hull` when the indexed family is a `finset` of
the space. -/
lemma finset.center_mass_id_mem_convex_hull (t : finset E) {w : E â†’ R} (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
  (hws : 0 < âˆ‘ i in t, w i) :
  t.center_mass w id âˆˆ convex_hull R (t : set E) :=
t.center_mass_mem_convex_hull hwâ‚€ hws (Î» i, mem_coe.2)

lemma affine_combination_eq_center_mass {Î¹ : Type*} {t : finset Î¹} {p : Î¹ â†’ E} {w : Î¹ â†’ R}
  (hwâ‚‚ : âˆ‘ i in t, w i = 1) :
  t.affine_combination R p w = center_mass t w p :=
begin
  rw [affine_combination_eq_weighted_vsub_of_point_vadd_of_sum_eq_one _ w _ hwâ‚‚ (0 : E),
    finset.weighted_vsub_of_point_apply, vadd_eq_add, add_zero, t.center_mass_eq_of_sum_1 _ hwâ‚‚],
  simp_rw [vsub_eq_sub, sub_zero],
end

lemma affine_combination_mem_convex_hull
  {s : finset Î¹} {v : Î¹ â†’ E} {w : Î¹ â†’ R} (hwâ‚€ : âˆ€ i âˆˆ s, 0 â‰¤ w i) (hwâ‚ : s.sum w = 1) :
  s.affine_combination R v w âˆˆ convex_hull R (range v) :=
begin
  rw affine_combination_eq_center_mass hwâ‚,
  apply s.center_mass_mem_convex_hull hwâ‚€,
  { simp [hwâ‚], },
  { simp, },
end

/-- The centroid can be regarded as a center of mass. -/
@[simp] lemma finset.centroid_eq_center_mass (s : finset Î¹) (hs : s.nonempty) (p : Î¹ â†’ E) :
  s.centroid R p = s.center_mass (s.centroid_weights R) p :=
affine_combination_eq_center_mass (s.sum_centroid_weights_eq_one_of_nonempty R hs)

lemma finset.centroid_mem_convex_hull (s : finset E) (hs : s.nonempty) :
  s.centroid R id âˆˆ convex_hull R (s : set E) :=
begin
  rw s.centroid_eq_center_mass hs,
  apply s.center_mass_id_mem_convex_hull,
  { simp only [inv_nonneg, implies_true_iff, nat.cast_nonneg, finset.centroid_weights_apply], },
  { have hs_card : (s.card : R) â‰  0, { simp [finset.nonempty_iff_ne_empty.mp hs] },
    simp only [hs_card, finset.sum_const, nsmul_eq_mul, mul_inv_cancel, ne.def, not_false_iff,
      finset.centroid_weights_apply, zero_lt_one] }
end

lemma convex_hull_range_eq_exists_affine_combination (v : Î¹ â†’ E) :
  convex_hull R (range v) = { x | âˆƒ (s : finset Î¹) (w : Î¹ â†’ R)
    (hwâ‚€ : âˆ€ i âˆˆ s, 0 â‰¤ w i) (hwâ‚ : s.sum w = 1), s.affine_combination R v w = x } :=
begin
  refine subset.antisymm (convex_hull_min _ _) _,
  { intros x hx,
    obtain âŸ¨i, hiâŸ© := set.mem_range.mp hx,
    refine âŸ¨{i}, function.const Î¹ (1 : R), by simp, by simp, by simp [hi]âŸ©, },
  { rintro x âŸ¨s, w, hwâ‚€, hwâ‚, rflâŸ© y âŸ¨s', w', hwâ‚€', hwâ‚', rflâŸ© a b ha hb hab,
    let W : Î¹ â†’ R := Î» i, (if i âˆˆ s then a * w i else 0) + (if i âˆˆ s' then b * w' i else 0),
    have hWâ‚ : (s âˆª s').sum W = 1,
    { rw [sum_add_distrib, â† sum_subset (subset_union_left s s'),
        â† sum_subset (subset_union_right s s'), sum_ite_of_true _ _ (Î» i hi, hi),
        sum_ite_of_true _ _ (Î» i hi, hi), â† mul_sum, â† mul_sum, hwâ‚, hwâ‚', â† add_mul, hab, mul_one];
      intros i hi hi';
      simp [hi'], },
    refine âŸ¨s âˆª s', W, _, hWâ‚, _âŸ©,
    { rintros i -,
      by_cases hi : i âˆˆ s;
      by_cases hi' : i âˆˆ s';
      simp [hi, hi', add_nonneg, mul_nonneg ha (hwâ‚€ i _), mul_nonneg hb (hwâ‚€' i _)], },
    { simp_rw [affine_combination_eq_linear_combination (s âˆª s') v _ hWâ‚,
        affine_combination_eq_linear_combination s v w hwâ‚,
        affine_combination_eq_linear_combination s' v w' hwâ‚', add_smul, sum_add_distrib],
      rw [â† sum_subset (subset_union_left s s'), â† sum_subset (subset_union_right s s')],
      { simp only [ite_smul, sum_ite_of_true _ _ (Î» i hi, hi), mul_smul, â† smul_sum], },
      { intros i hi hi', simp [hi'], },
      { intros i hi hi', simp [hi'], }, }, },
  { rintros x âŸ¨s, w, hwâ‚€, hwâ‚, rflâŸ©,
    exact affine_combination_mem_convex_hull hwâ‚€ hwâ‚, },
end

/-- Convex hull of `s` is equal to the set of all centers of masses of `finset`s `t`, `z '' t âŠ† s`.
This version allows finsets in any type in any universe. -/
lemma convex_hull_eq (s : set E) :
  convex_hull R s = {x : E | âˆƒ (Î¹ : Type u') (t : finset Î¹) (w : Î¹ â†’ R) (z : Î¹ â†’ E)
    (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : âˆ‘ i in t, w i = 1) (hz : âˆ€ i âˆˆ t, z i âˆˆ s),
    t.center_mass w z = x} :=
begin
  refine subset.antisymm (convex_hull_min _ _) _,
  { intros x hx,
    use [punit, {punit.star}, Î» _, 1, Î» _, x, Î» _ _, zero_le_one,
      finset.sum_singleton, Î» _ _, hx],
    simp only [finset.center_mass, finset.sum_singleton, inv_one, one_smul] },
  { rintros x âŸ¨Î¹, sx, wx, zx, hwxâ‚€, hwxâ‚, hzx, rflâŸ© y âŸ¨Î¹', sy, wy, zy, hwyâ‚€, hwyâ‚, hzy, rflâŸ©
      a b ha hb hab,
    rw [finset.center_mass_segment' _ _ _ _ _ _ hwxâ‚ hwyâ‚ _ _ hab],
    refine âŸ¨_, _, _, _, _, _, _, rflâŸ©,
    { rintros i hi,
      rw [finset.mem_disj_sum] at hi,
      rcases hi with âŸ¨j, hj, rflâŸ©|âŸ¨j, hj, rflâŸ©;
        simp only [sum.elim_inl, sum.elim_inr];
        apply_rules [mul_nonneg, hwxâ‚€, hwyâ‚€] },
    { simp [finset.sum_sum_elim, finset.mul_sum.symm, *], },
    { intros i hi,
      rw [finset.mem_disj_sum] at hi,
      rcases hi with âŸ¨j, hj, rflâŸ©|âŸ¨j, hj, rflâŸ©; apply_rules [hzx, hzy] } },
  { rintros _ âŸ¨Î¹, t, w, z, hwâ‚€, hwâ‚, hz, rflâŸ©,
    exact t.center_mass_mem_convex_hull hwâ‚€ (hwâ‚.symm â–¸ zero_lt_one) hz }
end

lemma finset.convex_hull_eq (s : finset E) :
  convex_hull R â†‘s = {x : E | âˆƒ (w : E â†’ R) (hwâ‚€ : âˆ€ y âˆˆ s, 0 â‰¤ w y) (hwâ‚ : âˆ‘ y in s, w y = 1),
    s.center_mass w id = x} :=
begin
  refine subset.antisymm (convex_hull_min _ _) _,
  { intros x hx,
    rw [finset.mem_coe] at hx,
    refine âŸ¨_, _, _, finset.center_mass_ite_eq _ _ _ hxâŸ©,
    { intros, split_ifs, exacts [zero_le_one, le_refl 0] },
    { rw [finset.sum_ite_eq, if_pos hx] } },
  { rintro x âŸ¨wx, hwxâ‚€, hwxâ‚, rflâŸ© y âŸ¨wy, hwyâ‚€, hwyâ‚, rflâŸ© a b ha hb hab,
    rw [finset.center_mass_segment _ _ _ _ hwxâ‚ hwyâ‚ _ _ hab],
    refine âŸ¨_, _, _, rflâŸ©,
    { rintros i hi,
      apply_rules [add_nonneg, mul_nonneg, hwxâ‚€, hwyâ‚€], },
    { simp only [finset.sum_add_distrib, finset.mul_sum.symm, mul_one, *] } },
  { rintros _ âŸ¨w, hwâ‚€, hwâ‚, rflâŸ©,
    exact s.center_mass_mem_convex_hull (Î» x hx, hwâ‚€ _ hx)
      (hwâ‚.symm â–¸ zero_lt_one) (Î» x hx, hx) }
end

lemma finset.mem_convex_hull {s : finset E} {x : E} :
  x âˆˆ convex_hull R (s : set E) â†”
    âˆƒ (w : E â†’ R) (hwâ‚€ : âˆ€ y âˆˆ s, 0 â‰¤ w y) (hwâ‚ : âˆ‘ y in s, w y = 1), s.center_mass w id = x :=
by rw [finset.convex_hull_eq, set.mem_set_of_eq]

lemma set.finite.convex_hull_eq {s : set E} (hs : s.finite) :
  convex_hull R s = {x : E | âˆƒ (w : E â†’ R) (hwâ‚€ : âˆ€ y âˆˆ s, 0 â‰¤ w y)
    (hwâ‚ : âˆ‘ y in hs.to_finset, w y = 1), hs.to_finset.center_mass w id = x} :=
by simpa only [set.finite.coe_to_finset, set.finite.mem_to_finset, exists_prop]
  using hs.to_finset.convex_hull_eq

/-- A weak version of CarathÃ©odory's theorem. -/
lemma convex_hull_eq_union_convex_hull_finite_subsets (s : set E) :
  convex_hull R s = â‹ƒ (t : finset E) (w : â†‘t âŠ† s), convex_hull R â†‘t :=
begin
  refine subset.antisymm _ _,
  { rw convex_hull_eq,
    rintros x âŸ¨Î¹, t, w, z, hwâ‚€, hwâ‚, hz, rflâŸ©,
    simp only [mem_Union],
    refine âŸ¨t.image z, _, _âŸ©,
    { rw [coe_image, set.image_subset_iff],
      exact hz },
    { apply t.center_mass_mem_convex_hull hwâ‚€,
      { simp only [hwâ‚, zero_lt_one] },
      { exact Î» i hi, finset.mem_coe.2 (finset.mem_image_of_mem _ hi) } } },
  { exact Union_subset (Î» i, Union_subset convex_hull_mono), },
end

lemma mk_mem_convex_hull_prod {t : set F} {x : E} {y : F} (hx : x âˆˆ convex_hull R s)
  (hy : y âˆˆ convex_hull R t) :
  (x, y) âˆˆ convex_hull R (s Ã—Ë¢ t) :=
begin
  rw convex_hull_eq at âŠ¢ hx hy,
  obtain âŸ¨Î¹, a, w, S, hw, hw', hS, hSpâŸ© := hx,
  obtain âŸ¨Îº, b, v, T, hv, hv', hT, hTpâŸ© := hy,
  have h_sum : âˆ‘ (i : Î¹ Ã— Îº) in a Ã—Ë¢ b, w i.fst * v i.snd = 1,
  { rw [finset.sum_product, â† hw'],
    congr,
    ext i,
    have : âˆ‘ (y : Îº) in b, w i * v y = âˆ‘ (y : Îº) in b, v y * w i,
    { congr, ext, simp [mul_comm] },
    rw [this, â† finset.sum_mul, hv'],
    simp },
  refine âŸ¨Î¹ Ã— Îº, a Ã—Ë¢ b, Î» p, (w p.1) * (v p.2), Î» p, (S p.1, T p.2),
    Î» p hp, _, h_sum, Î» p hp, _, _âŸ©,
  { rw mem_product at hp,
    exact mul_nonneg (hw p.1 hp.1) (hv p.2 hp.2) },
  { rw mem_product at hp,
    exact âŸ¨hS p.1 hp.1, hT p.2 hp.2âŸ© },
  ext,
  { rw [â†hSp, finset.center_mass_eq_of_sum_1 _ _ hw', finset.center_mass_eq_of_sum_1 _ _ h_sum],
    simp_rw [prod.fst_sum, prod.smul_mk],
    rw finset.sum_product,
    congr,
    ext i,
    have : âˆ‘ (j : Îº) in b, (w i * v j) â€¢ S i = âˆ‘ (j : Îº) in b, v j â€¢ w i â€¢ S i,
    { congr, ext, rw [mul_smul, smul_comm] },
    rw [this, â†finset.sum_smul, hv', one_smul] },
  { rw [â†hTp, finset.center_mass_eq_of_sum_1 _ _ hv', finset.center_mass_eq_of_sum_1 _ _ h_sum],
    simp_rw [prod.snd_sum, prod.smul_mk],
    rw [finset.sum_product, finset.sum_comm],
    congr,
    ext j,
    simp_rw mul_smul,
    rw [â†finset.sum_smul, hw', one_smul] }
end

@[simp] lemma convex_hull_prod (s : set E) (t : set F) :
  convex_hull R (s Ã—Ë¢ t) = convex_hull R s Ã—Ë¢ convex_hull R t :=
subset.antisymm (convex_hull_min (prod_mono (subset_convex_hull _ _) $ subset_convex_hull _ _) $
  (convex_convex_hull _ _).prod $ convex_convex_hull _ _) $
    prod_subset_iff.2 $ Î» x hx y, mk_mem_convex_hull_prod hx

lemma convex_hull_add (s t : set E) : convex_hull R (s + t) = convex_hull R s + convex_hull R t :=
by simp_rw [â†image2_add, â†image_prod, is_linear_map.is_linear_map_add.convex_hull_image,
  convex_hull_prod]

lemma convex_hull_sub (s t : set E) : convex_hull R (s - t) = convex_hull R s - convex_hull R t :=
by simp_rw [sub_eq_add_neg, convex_hull_add, convex_hull_neg]

/-! ### `std_simplex` -/

variables (Î¹) [fintype Î¹] {f : Î¹ â†’ R}

/-- `std_simplex ğ•œ Î¹` is the convex hull of the canonical basis in `Î¹ â†’ ğ•œ`. -/
lemma convex_hull_basis_eq_std_simplex :
  convex_hull R (range $ Î»(i j:Î¹), if i = j then (1:R) else 0) = std_simplex R Î¹ :=
begin
  refine subset.antisymm (convex_hull_min _ (convex_std_simplex R Î¹)) _,
  { rintros _ âŸ¨i, rflâŸ©,
    exact ite_eq_mem_std_simplex R i },
  { rintros w âŸ¨hwâ‚€, hwâ‚âŸ©,
    rw [pi_eq_sum_univ w, â† finset.univ.center_mass_eq_of_sum_1 _ hwâ‚],
    exact finset.univ.center_mass_mem_convex_hull (Î» i hi, hwâ‚€ i)
      (hwâ‚.symm â–¸ zero_lt_one) (Î» i hi, mem_range_self i) }
end

variable {Î¹}

/-- The convex hull of a finite set is the image of the standard simplex in `s â†’ â„`
under the linear map sending each function `w` to `âˆ‘ x in s, w x â€¢ x`.

Since we have no sums over finite sets, we use sum over `@finset.univ _ hs.fintype`.
The map is defined in terms of operations on `(s â†’ â„) â†’â‚—[â„] â„` so that later we will not need
to prove that this map is linear. -/
lemma set.finite.convex_hull_eq_image {s : set E} (hs : s.finite) :
  convex_hull R s = by haveI := hs.fintype; exact
    (â‡‘(âˆ‘ x : s, (@linear_map.proj R s _ (Î» i, R) _ _ x).smul_right x.1)) '' (std_simplex R s) :=
begin
  rw [â† convex_hull_basis_eq_std_simplex, â† linear_map.convex_hull_image, â† set.range_comp, (âˆ˜)],
  apply congr_arg,
  convert subtype.range_coe.symm,
  ext x,
  simp [linear_map.sum_apply, ite_smul, finset.filter_eq]
end

/-- All values of a function `f âˆˆ std_simplex ğ•œ Î¹` belong to `[0, 1]`. -/
lemma mem_Icc_of_mem_std_simplex (hf : f âˆˆ std_simplex R Î¹) (x) :
  f x âˆˆ Icc (0 : R) 1 :=
âŸ¨hf.1 x, hf.2 â–¸ finset.single_le_sum (Î» y hy, hf.1 y) (finset.mem_univ x)âŸ©

/-- The convex hull of an affine basis is the intersection of the half-spaces defined by the
corresponding barycentric coordinates. -/
lemma affine_basis.convex_hull_eq_nonneg_coord {Î¹ : Type*} (b : affine_basis Î¹ R E) :
  convex_hull R (range b) = {x | âˆ€ i, 0 â‰¤ b.coord i x} :=
begin
  rw convex_hull_range_eq_exists_affine_combination,
  ext x,
  refine âŸ¨_, Î» hx, _âŸ©,
  { rintros âŸ¨s, w, hwâ‚€, hwâ‚, rflâŸ© i,
    by_cases hi : i âˆˆ s,
    { rw b.coord_apply_combination_of_mem hi hwâ‚,
      exact hwâ‚€ i hi, },
    { rw b.coord_apply_combination_of_not_mem hi hwâ‚, }, },
  { have hx' : x âˆˆ affine_span R (range b),
    { rw b.tot, exact affine_subspace.mem_top R E x, },
    obtain âŸ¨s, w, hwâ‚, rflâŸ© := (mem_affine_span_iff_eq_affine_combination R E).mp hx',
    refine âŸ¨s, w, _, hwâ‚, rflâŸ©,
    intros i hi,
    specialize hx i,
    rw b.coord_apply_combination_of_mem hi hwâ‚ at hx,
    exact hx, },
end
