/-
Copyright (c) 2020 Yury Kudryashov All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, FrÃ©dÃ©ric Dupuis
-/
import analysis.convex.hull
import analysis.inner_product_space.basic

/-!
# Convex cones

In a `ğ•œ`-module `E`, we define a convex cone as a set `s` such that `a â€¢ x + b â€¢ y âˆˆ s` whenever
`x, y âˆˆ s` and `a, b > 0`. We prove that convex cones form a `complete_lattice`, and define their
images (`convex_cone.map`) and preimages (`convex_cone.comap`) under linear maps.

We define pointed, blunt, flat and salient cones, and prove the correspondence between
convex cones and ordered modules.

We also define `convex.to_cone` to be the minimal cone that includes a given convex set.

We define `set.inner_dual_cone` to be the cone consisting of all points `y` such that for
all points `x` in a given set `0 â‰¤ âŸª x, y âŸ«`.

## Main statements

We prove two extension theorems:
* `riesz_extension`:
  [M. Riesz extension theorem](https://en.wikipedia.org/wiki/M._Riesz_extension_theorem) says that
  if `s` is a convex cone in a real vector space `E`, `p` is a submodule of `E`
  such that `p + s = E`, and `f` is a linear function `p â†’ â„` which is
  nonnegative on `p âˆ© s`, then there exists a globally defined linear function
  `g : E â†’ â„` that agrees with `f` on `p`, and is nonnegative on `s`.
* `exists_extension_of_le_sublinear`:
  Hahn-Banach theorem: if `N : E â†’ â„` is a sublinear map, `f` is a linear map
  defined on a subspace of `E`, and `f x â‰¤ N x` for all `x` in the domain of `f`,
  then `f` can be extended to the whole space to a linear map `g` such that `g x â‰¤ N x`
  for all `x`

## Implementation notes

While `convex ğ•œ` is a predicate on sets, `convex_cone ğ•œ E` is a bundled convex cone.

## References

* https://en.wikipedia.org/wiki/Convex_cone
-/


open set linear_map
open_locale classical pointwise

variables {ğ•œ E F G : Type*}

/-! ### Definition of `convex_cone` and basic properties -/

section definitions
variables (ğ•œ E) [ordered_semiring ğ•œ]

/-- A convex cone is a subset `s` of a `ğ•œ`-module such that `a â€¢ x + b â€¢ y âˆˆ s` whenever `a, b > 0`
and `x, y âˆˆ s`. -/
structure convex_cone [add_comm_monoid E] [has_scalar ğ•œ E] :=
(carrier : set E)
(smul_mem' : âˆ€ â¦ƒc : ğ•œâ¦„, 0 < c â†’ âˆ€ â¦ƒx : Eâ¦„, x âˆˆ carrier â†’ c â€¢ x âˆˆ carrier)
(add_mem' : âˆ€ â¦ƒxâ¦„ (hx : x âˆˆ carrier) â¦ƒyâ¦„ (hy : y âˆˆ carrier), x + y âˆˆ carrier)

end definitions

variables {ğ•œ E}

namespace convex_cone
section ordered_semiring
variables [ordered_semiring ğ•œ] [add_comm_monoid E]

section has_scalar
variables [has_scalar ğ•œ E] (S T : convex_cone ğ•œ E)

instance : has_coe (convex_cone ğ•œ E) (set E) := âŸ¨convex_cone.carrierâŸ©

instance : has_mem E (convex_cone ğ•œ E) := âŸ¨Î» m S, m âˆˆ S.carrierâŸ©

instance : has_le (convex_cone ğ•œ E) := âŸ¨Î» S T, S.carrier âŠ† T.carrierâŸ©

instance : has_lt (convex_cone ğ•œ E) := âŸ¨Î» S T, S.carrier âŠ‚ T.carrierâŸ©

@[simp, norm_cast] lemma mem_coe {x : E} : x âˆˆ (S : set E) â†” x âˆˆ S := iff.rfl

@[simp] lemma mem_mk {s : set E} {hâ‚ hâ‚‚ x} : x âˆˆ @mk ğ•œ _ _ _ _ s hâ‚ hâ‚‚ â†” x âˆˆ s := iff.rfl

/-- Two `convex_cone`s are equal if the underlying sets are equal. -/
theorem ext' {S T : convex_cone ğ•œ E} (h : (S : set E) = T) : S = T :=
by cases S; cases T; congr'

/-- Two `convex_cone`s are equal if and only if the underlying sets are equal. -/
protected theorem ext'_iff {S T : convex_cone ğ•œ E}  : (S : set E) = T â†” S = T :=
âŸ¨ext', Î» h, h â–¸ rflâŸ©

/-- Two `convex_cone`s are equal if they have the same elements. -/
@[ext] theorem ext {S T : convex_cone ğ•œ E} (h : âˆ€ x, x âˆˆ S â†” x âˆˆ T) : S = T := ext' $ set.ext h

lemma smul_mem {c : ğ•œ} {x : E} (hc : 0 < c) (hx : x âˆˆ S) : c â€¢ x âˆˆ S := S.smul_mem' hc hx

lemma add_mem â¦ƒxâ¦„ (hx : x âˆˆ S) â¦ƒyâ¦„ (hy : y âˆˆ S) : x + y âˆˆ S := S.add_mem' hx hy

instance : has_inf (convex_cone ğ•œ E) :=
âŸ¨Î» S T, âŸ¨S âˆ© T, Î» c hc x hx, âŸ¨S.smul_mem hc hx.1, T.smul_mem hc hx.2âŸ©,
  Î» x hx y hy, âŸ¨S.add_mem hx.1 hy.1, T.add_mem hx.2 hy.2âŸ©âŸ©âŸ©

lemma coe_inf : ((S âŠ“ T : convex_cone ğ•œ E) : set E) = â†‘S âˆ© â†‘T := rfl

lemma mem_inf {x} : x âˆˆ S âŠ“ T â†” x âˆˆ S âˆ§ x âˆˆ T := iff.rfl

instance : has_Inf (convex_cone ğ•œ E) :=
âŸ¨Î» S, âŸ¨â‹‚ s âˆˆ S, â†‘s,
  Î» c hc x hx, mem_bInter $ Î» s hs, s.smul_mem hc $ by apply mem_bInter_iff.1 hx s hs,
  Î» x hx y hy, mem_bInter $ Î» s hs, s.add_mem (by apply mem_bInter_iff.1 hx s hs)
    (by apply mem_bInter_iff.1 hy s hs)âŸ©âŸ©

lemma mem_Inf {x : E} {S : set (convex_cone ğ•œ E)} : x âˆˆ Inf S â†” âˆ€ s âˆˆ S, x âˆˆ s := mem_bInter_iff

variables (ğ•œ)

instance : has_bot (convex_cone ğ•œ E) := âŸ¨âŸ¨âˆ…, Î» c hc x, false.elim, Î» x, false.elimâŸ©âŸ©

lemma mem_bot (x : E) : x âˆˆ (âŠ¥ : convex_cone ğ•œ E) = false := rfl

instance : has_top (convex_cone ğ•œ E) := âŸ¨âŸ¨univ, Î» c hc x hx, mem_univ _, Î» x hx y hy, mem_univ _âŸ©âŸ©

lemma mem_top (x : E) : x âˆˆ (âŠ¤ : convex_cone ğ•œ E) := mem_univ x

instance : complete_lattice (convex_cone ğ•œ E) :=
{ le           := (â‰¤),
  lt           := (<),
  bot          := (âŠ¥),
  bot_le       := Î» S x, false.elim,
  top          := (âŠ¤),
  le_top       := Î» S x hx, mem_top ğ•œ x,
  inf          := (âŠ“),
  Inf          := has_Inf.Inf,
  sup          := Î» a b, Inf {x | a â‰¤ x âˆ§ b â‰¤ x},
  Sup          := Î» s, Inf {T | âˆ€ S âˆˆ s, S â‰¤ T},
  le_sup_left  := Î» a b, Î» x hx, mem_Inf.2 $ Î» s hs, hs.1 hx,
  le_sup_right := Î» a b, Î» x hx, mem_Inf.2 $ Î» s hs, hs.2 hx,
  sup_le       := Î» a b c ha hb x hx, mem_Inf.1 hx c âŸ¨ha, hbâŸ©,
  le_inf       := Î» a b c ha hb x hx, âŸ¨ha hx, hb hxâŸ©,
  inf_le_left  := Î» a b x, and.left,
  inf_le_right := Î» a b x, and.right,
  le_Sup       := Î» s p hs x hx, mem_Inf.2 $ Î» t ht, ht p hs hx,
  Sup_le       := Î» s p hs x hx, mem_Inf.1 hx p hs,
  le_Inf       := Î» s a ha x hx, mem_Inf.2 $ Î» t ht, ha t ht hx,
  Inf_le       := Î» s a ha x hx, mem_Inf.1 hx _ ha,
  .. partial_order.lift (coe : convex_cone ğ•œ E â†’ set E) (Î» a b, ext') }

instance : inhabited (convex_cone ğ•œ E) := âŸ¨âŠ¥âŸ©

end has_scalar

section module
variables [module ğ•œ E] (S : convex_cone ğ•œ E)

protected lemma convex : convex ğ•œ (S : set E) :=
convex_iff_forall_pos.2 $ Î» x y hx hy a b ha hb hab,
  S.add_mem (S.smul_mem ha hx) (S.smul_mem hb hy)

end module
end ordered_semiring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F] [add_comm_monoid G]

section mul_action
variables [mul_action ğ•œ E] (S : convex_cone ğ•œ E)

lemma smul_mem_iff {c : ğ•œ} (hc : 0 < c) {x : E} :
  c â€¢ x âˆˆ S â†” x âˆˆ S :=
âŸ¨Î» h, inv_smul_smulâ‚€ hc.ne' x â–¸ S.smul_mem (inv_pos.2 hc) h, S.smul_mem hcâŸ©

end mul_action

section module
variables [module ğ•œ E] [module ğ•œ F] [module ğ•œ G]

/-- The image of a convex cone under a `ğ•œ`-linear map is a convex cone. -/
def map (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ E) : convex_cone ğ•œ F :=
{ carrier := f '' S,
  smul_mem' := Î» c hc y âŸ¨x, hx, hyâŸ©, hy â–¸ f.map_smul c x â–¸ mem_image_of_mem f (S.smul_mem hc hx),
  add_mem' := Î» yâ‚ âŸ¨xâ‚, hxâ‚, hyâ‚âŸ© yâ‚‚ âŸ¨xâ‚‚, hxâ‚‚, hyâ‚‚âŸ©, hyâ‚ â–¸ hyâ‚‚ â–¸ f.map_add xâ‚ xâ‚‚ â–¸
    mem_image_of_mem f (S.add_mem hxâ‚ hxâ‚‚) }

lemma map_map (g : F â†’â‚—[ğ•œ] G) (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ E) :
  (S.map f).map g = S.map (g.comp f) :=
ext' $ image_image g f S

@[simp] lemma map_id (S : convex_cone ğ•œ E) : S.map linear_map.id = S := ext' $ image_id _

/-- The preimage of a convex cone under a `ğ•œ`-linear map is a convex cone. -/
def comap (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ F) : convex_cone ğ•œ E :=
{ carrier := f â»Â¹' S,
  smul_mem' := Î» c hc x hx, by { rw [mem_preimage, f.map_smul c], exact S.smul_mem hc hx },
  add_mem' := Î» x hx y hy, by { rw [mem_preimage, f.map_add], exact S.add_mem hx hy } }

@[simp] lemma comap_id (S : convex_cone ğ•œ E) : S.comap linear_map.id = S := ext' preimage_id

lemma comap_comap (g : F â†’â‚—[ğ•œ] G) (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ G) :
  (S.comap g).comap f = S.comap (g.comp f) :=
ext' $ preimage_comp.symm

@[simp] lemma mem_comap {f : E â†’â‚—[ğ•œ] F} {S : convex_cone ğ•œ F} {x : E} : x âˆˆ S.comap f â†” f x âˆˆ S :=
iff.rfl

end module
end add_comm_monoid

section ordered_add_comm_group
variables [ordered_add_comm_group E] [module ğ•œ E]

/--
Constructs an ordered module given an `ordered_add_comm_group`, a cone, and a proof that
the order relation is the one defined by the cone.
-/
lemma to_ordered_smul (S : convex_cone ğ•œ E) (h : âˆ€ x y : E, x â‰¤ y â†” y - x âˆˆ S) :
  ordered_smul ğ•œ E :=
ordered_smul.mk'
begin
  intros x y z xy hz,
  rw [h (z â€¢ x) (z â€¢ y), â†smul_sub z y x],
  exact smul_mem S hz ((h x y).mp xy.le),
end

end ordered_add_comm_group
end linear_ordered_field

/-! ### Convex cones with extra properties -/

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [has_scalar ğ•œ E] (S : convex_cone ğ•œ E)

/-- A convex cone is pointed if it includes `0`. -/
def pointed (S : convex_cone ğ•œ E) : Prop := (0 : E) âˆˆ S

/-- A convex cone is blunt if it doesn't include `0`. -/
def blunt (S : convex_cone ğ•œ E) : Prop := (0 : E) âˆ‰ S

lemma pointed_iff_not_blunt (S : convex_cone ğ•œ E) : S.pointed â†” Â¬S.blunt :=
âŸ¨Î» hâ‚ hâ‚‚, hâ‚‚ hâ‚, not_not.mpâŸ©

lemma blunt_iff_not_pointed (S : convex_cone ğ•œ E) : S.blunt â†” Â¬S.pointed :=
by rw [pointed_iff_not_blunt, not_not]

end add_comm_monoid

section add_comm_group
variables [add_comm_group E] [has_scalar ğ•œ E] (S : convex_cone ğ•œ E)

/-- A convex cone is flat if it contains some nonzero vector `x` and its opposite `-x`. -/
def flat : Prop := âˆƒ x âˆˆ S, x â‰  (0 : E) âˆ§ -x âˆˆ S

/-- A convex cone is salient if it doesn't include `x` and `-x` for any nonzero `x`. -/
def salient : Prop := âˆ€ x âˆˆ S, x â‰  (0 : E) â†’ -x âˆ‰ S

lemma salient_iff_not_flat (S : convex_cone ğ•œ E) : S.salient â†” Â¬S.flat :=
begin
  split,
  { rintros hâ‚ âŸ¨x, xs, Hâ‚, Hâ‚‚âŸ©,
    exact hâ‚ x xs Hâ‚ Hâ‚‚ },
  { intro h,
    unfold flat at h,
    push_neg at h,
    exact h }
end

/-- A flat cone is always pointed (contains `0`). -/
lemma flat.pointed {S : convex_cone ğ•œ E} (hS : S.flat) : S.pointed :=
begin
  obtain âŸ¨x, hx, _, hxnegâŸ© := hS,
  rw [pointed, â†add_neg_self x],
  exact add_mem S hx hxneg,
end

/-- A blunt cone (one not containing `0`) is always salient. -/
lemma blunt.salient {S : convex_cone ğ•œ E} : S.blunt â†’ S.salient :=
begin
  rw [salient_iff_not_flat, blunt_iff_not_pointed],
  exact mt flat.pointed,
end

/-- A pointed convex cone defines a preorder. -/
def to_preorder (hâ‚ : S.pointed) : preorder E :=
{ le := Î» x y, y - x âˆˆ S,
  le_refl := Î» x, by change x - x âˆˆ S; rw [sub_self x]; exact hâ‚,
  le_trans := Î» x y z xy zy, by simpa using add_mem S zy xy }

/-- A pointed and salient cone defines a partial order. -/
def to_partial_order (hâ‚ : S.pointed) (hâ‚‚ : S.salient) : partial_order E :=
{ le_antisymm :=
    begin
      intros a b ab ba,
      by_contradiction h,
      have h' : b - a â‰  0 := Î» h'', h (eq_of_sub_eq_zero h'').symm,
      have H := hâ‚‚ (b-a) ab h',
      rw neg_sub b a at H,
      exact H ba,
    end,
  ..to_preorder S hâ‚ }

/-- A pointed and salient cone defines an `ordered_add_comm_group`. -/
def to_ordered_add_comm_group (hâ‚ : S.pointed) (hâ‚‚ : S.salient) :
  ordered_add_comm_group E :=
{ add_le_add_left :=
    begin
      intros a b hab c,
      change c + b - (c + a) âˆˆ S,
      rw add_sub_add_left_eq_sub,
      exact hab,
    end,
  ..to_partial_order S hâ‚ hâ‚‚,
  ..show add_comm_group E, by apply_instance }

end add_comm_group
end ordered_semiring

/-! ### Positive cone of an ordered module -/

section positive_cone
variables (ğ•œ E) [ordered_semiring ğ•œ] [ordered_add_comm_group E] [module ğ•œ E] [ordered_smul ğ•œ E]

/--
The positive cone is the convex cone formed by the set of nonnegative elements in an ordered
module.
-/
def positive_cone : convex_cone ğ•œ E :=
{ carrier := {x | 0 â‰¤ x},
  smul_mem' :=
    begin
      rintro c hc x (hx : _ â‰¤ _),
      rw â†smul_zero c,
      exact smul_le_smul_of_nonneg hx hc.le,
    end,
  add_mem' := Î» x (hx : _ â‰¤ _) y (hy : _ â‰¤ _), add_nonneg hx hy }

/-- The positive cone of an ordered module is always salient. -/
lemma salient_positive_cone : salient (positive_cone ğ•œ E) :=
Î» x xs hx hx', lt_irrefl (0 : E)
  (calc
    0   < x         : lt_of_le_of_ne xs hx.symm
    ... â‰¤ x + (-x)  : le_add_of_nonneg_right hx'
    ... = 0         : add_neg_self x)

/-- The positive cone of an ordered module is always pointed. -/
lemma pointed_positive_cone : pointed (positive_cone ğ•œ E) := le_refl 0

end positive_cone
end convex_cone

/-! ### Cone over a convex set -/

section cone_from_convex
variables [linear_ordered_field ğ•œ] [ordered_add_comm_group E] [module ğ•œ E]

namespace convex

/-- The set of vectors proportional to those in a convex set forms a convex cone. -/
def to_cone (s : set E) (hs : convex ğ•œ s) : convex_cone ğ•œ E :=
begin
  apply convex_cone.mk (â‹ƒ (c : ğ•œ) (H : 0 < c), c â€¢ s);
    simp only [mem_Union, mem_smul_set],
  { rintros c c_pos _ âŸ¨c', c'_pos, x, hx, rflâŸ©,
    exact âŸ¨c * c', mul_pos c_pos c'_pos, x, hx, (smul_smul _ _ _).symmâŸ© },
  { rintros _ âŸ¨cx, cx_pos, x, hx, rflâŸ© _ âŸ¨cy, cy_pos, y, hy, rflâŸ©,
    have : 0 < cx + cy, from add_pos cx_pos cy_pos,
    refine âŸ¨_, this, _, convex_iff_div.1 hs hx hy cx_pos.le cy_pos.le this, _âŸ©,
    simp only [smul_add, smul_smul, mul_div_assoc', mul_div_cancel_left _ this.ne'] }
end

variables {s : set E} (hs : convex ğ•œ s) {x : E}

lemma mem_to_cone : x âˆˆ hs.to_cone s â†” âˆƒ (c : ğ•œ), 0 < c âˆ§ âˆƒ y âˆˆ s, c â€¢ y = x :=
by simp only [to_cone, convex_cone.mem_mk, mem_Union, mem_smul_set, eq_comm, exists_prop]

lemma mem_to_cone' : x âˆˆ hs.to_cone s â†” âˆƒ (c : ğ•œ), 0 < c âˆ§ c â€¢ x âˆˆ s :=
begin
  refine hs.mem_to_cone.trans âŸ¨_, _âŸ©,
  { rintros âŸ¨c, hc, y, hy, rflâŸ©,
    exact âŸ¨câ»Â¹, inv_pos.2 hc, by rwa [smul_smul, inv_mul_cancel hc.ne', one_smul]âŸ© },
  { rintros âŸ¨c, hc, hcxâŸ©,
    exact âŸ¨câ»Â¹, inv_pos.2 hc, _, hcx, by rw [smul_smul, inv_mul_cancel hc.ne', one_smul]âŸ© }
end

lemma subset_to_cone : s âŠ† hs.to_cone s :=
Î» x hx, hs.mem_to_cone'.2 âŸ¨1, zero_lt_one, by rwa one_smulâŸ©

/-- `hs.to_cone s` is the least cone that includes `s`. -/
lemma to_cone_is_least : is_least { t : convex_cone ğ•œ E | s âŠ† t } (hs.to_cone s) :=
begin
  refine âŸ¨hs.subset_to_cone, Î» t ht x hx, _âŸ©,
  rcases hs.mem_to_cone.1 hx with âŸ¨c, hc, y, hy, rflâŸ©,
  exact t.smul_mem hc (ht hy)
end

lemma to_cone_eq_Inf : hs.to_cone s = Inf { t : convex_cone ğ•œ E | s âŠ† t } :=
hs.to_cone_is_least.is_glb.Inf_eq.symm

end convex

lemma convex_hull_to_cone_is_least (s : set E) :
  is_least {t : convex_cone ğ•œ E | s âŠ† t} ((convex_convex_hull ğ•œ s).to_cone _) :=
begin
  convert (convex_convex_hull ğ•œ s).to_cone_is_least,
  ext t,
  exact âŸ¨Î» h, convex_hull_min h t.convex, (subset_convex_hull ğ•œ s).transâŸ©,
end

lemma convex_hull_to_cone_eq_Inf (s : set E) :
  (convex_convex_hull ğ•œ s).to_cone _ = Inf {t : convex_cone ğ•œ E | s âŠ† t} :=
(convex_hull_to_cone_is_least s).is_glb.Inf_eq.symm

end cone_from_convex

/-!
### M. Riesz extension theorem

Given a convex cone `s` in a vector space `E`, a submodule `p`, and a linear `f : p â†’ â„`, assume
that `f` is nonnegative on `p âˆ© s` and `p + s = E`. Then there exists a globally defined linear
function `g : E â†’ â„` that agrees with `f` on `p`, and is nonnegative on `s`.

We prove this theorem using Zorn's lemma. `riesz_extension.step` is the main part of the proof.
It says that if the domain `p` of `f` is not the whole space, then `f` can be extended to a larger
subspace `p âŠ” span â„ {y}` without breaking the non-negativity condition.

In `riesz_extension.exists_top` we use Zorn's lemma to prove that we can extend `f`
to a linear map `g` on `âŠ¤ : submodule E`. Mathematically this is the same as a linear map on `E`
but in Lean `âŠ¤ : submodule E` is isomorphic but is not equal to `E`. In `riesz_extension`
we use this isomorphism to prove the theorem.
-/

variables [add_comm_group E] [module â„ E]

namespace riesz_extension
open submodule
variables (s : convex_cone â„ E) (f : linear_pmap â„ E â„)

/-- Induction step in M. Riesz extension theorem. Given a convex cone `s` in a vector space `E`,
a partially defined linear map `f : f.domain â†’ â„`, assume that `f` is nonnegative on `f.domain âˆ© p`
and `p + s = E`. If `f` is not defined on the whole `E`, then we can extend it to a larger
submodule without breaking the non-negativity condition. -/
lemma step (nonneg : âˆ€ x : f.domain, (x : E) âˆˆ s â†’ 0 â‰¤ f x)
  (dense : âˆ€ y, âˆƒ x : f.domain, (x : E) + y âˆˆ s) (hdom : f.domain â‰  âŠ¤) :
  âˆƒ g, f < g âˆ§ âˆ€ x : g.domain, (x : E) âˆˆ s â†’ 0 â‰¤ g x :=
begin
  rcases set_like.exists_of_lt (lt_top_iff_ne_top.2 hdom) with âŸ¨y, hy', hyâŸ©, clear hy',
  obtain âŸ¨c, le_c, c_leâŸ© :
    âˆƒ c, (âˆ€ x : f.domain, -(x:E) - y âˆˆ s â†’ f x â‰¤ c) âˆ§ (âˆ€ x : f.domain, (x:E) + y âˆˆ s â†’ c â‰¤ f x),
  { set Sp := f '' {x : f.domain | (x:E) + y âˆˆ s},
    set Sn := f '' {x : f.domain | -(x:E) - y âˆˆ s},
    suffices : (upper_bounds Sn âˆ© lower_bounds Sp).nonempty,
      by simpa only [set.nonempty, upper_bounds, lower_bounds, ball_image_iff] using this,
    refine exists_between_of_forall_le (nonempty.image f _) (nonempty.image f (dense y)) _,
    { rcases (dense (-y)) with âŸ¨x, hxâŸ©,
      rw [â† neg_neg x, coe_neg, â† sub_eq_add_neg] at hx,
      exact âŸ¨_, hxâŸ© },
    rintros a âŸ¨xn, hxn, rflâŸ© b âŸ¨xp, hxp, rflâŸ©,
    have := s.add_mem hxp hxn,
    rw [add_assoc, add_sub_cancel'_right, â† sub_eq_add_neg, â† coe_sub] at this,
    replace := nonneg _ this,
    rwa [f.map_sub, sub_nonneg] at this },
  have hy' : y â‰  0, from Î» hyâ‚€, hy (hyâ‚€.symm â–¸ zero_mem _),
  refine âŸ¨f.sup_span_singleton y (-c) hy, _, _âŸ©,
  { refine lt_iff_le_not_le.2 âŸ¨f.left_le_sup _ _, Î» H, _âŸ©,
    replace H := linear_pmap.domain_mono.monotone H,
    rw [linear_pmap.domain_sup_span_singleton, sup_le_iff, span_le, singleton_subset_iff] at H,
    exact hy H.2 },
  { rintros âŸ¨z, hzâŸ© hzs,
    rcases mem_sup.1 hz with âŸ¨x, hx, y', hy', rflâŸ©,
    rcases mem_span_singleton.1 hy' with âŸ¨r, rflâŸ©,
    simp only [subtype.coe_mk] at hzs,
    erw [linear_pmap.sup_span_singleton_apply_mk _ _ _ _ _ hx, smul_neg,
      â† sub_eq_add_neg, sub_nonneg],
    rcases lt_trichotomy r 0 with hr|hr|hr,
    { have : -(râ»Â¹ â€¢ x) - y âˆˆ s,
        by rwa [â† s.smul_mem_iff (neg_pos.2 hr), smul_sub, smul_neg, neg_smul, neg_neg, smul_smul,
          mul_inv_cancel hr.ne, one_smul, sub_eq_add_neg, neg_smul, neg_neg],
      replace := le_c (râ»Â¹ â€¢ âŸ¨x, hxâŸ©) this,
      rwa [â† mul_le_mul_left (neg_pos.2 hr), â† neg_mul_eq_neg_mul, â† neg_mul_eq_neg_mul,
        neg_le_neg_iff, f.map_smul, smul_eq_mul, â† mul_assoc, mul_inv_cancel hr.ne,
        one_mul] at this },
    { subst r,
      simp only [zero_smul, add_zero] at hzs âŠ¢,
      apply nonneg,
      exact hzs },
    { have : râ»Â¹ â€¢ x + y âˆˆ s,
        by rwa [â† s.smul_mem_iff hr, smul_add, smul_smul, mul_inv_cancel hr.ne', one_smul],
      replace := c_le (râ»Â¹ â€¢ âŸ¨x, hxâŸ©) this,
      rwa [â† mul_le_mul_left hr, f.map_smul, smul_eq_mul, â† mul_assoc,
        mul_inv_cancel hr.ne', one_mul] at this } }
end

theorem exists_top (p : linear_pmap â„ E â„)
  (hp_nonneg : âˆ€ x : p.domain, (x : E) âˆˆ s â†’ 0 â‰¤ p x)
  (hp_dense : âˆ€ y, âˆƒ x : p.domain, (x : E) + y âˆˆ s) :
  âˆƒ q â‰¥ p, q.domain = âŠ¤ âˆ§ âˆ€ x : q.domain, (x : E) âˆˆ s â†’ 0 â‰¤ q x :=
begin
  replace hp_nonneg : p âˆˆ { p | _ }, by { rw mem_set_of_eq, exact hp_nonneg },
  obtain âŸ¨q, hqs, hpq, hqâŸ© := zorn.zorn_nonempty_partial_orderâ‚€ _ _ _ hp_nonneg,
  { refine âŸ¨q, hpq, _, hqsâŸ©,
    contrapose! hq,
    rcases step s q hqs _ hq with âŸ¨r, hqr, hrâŸ©,
    { exact âŸ¨r, hr, hqr.le, hqr.ne'âŸ© },
    { exact Î» y, let âŸ¨x, hxâŸ© := hp_dense y in âŸ¨of_le hpq.left x, hxâŸ© } },
  { intros c hcs c_chain y hy,
    clear hp_nonneg hp_dense p,
    have cne : c.nonempty := âŸ¨y, hyâŸ©,
    refine âŸ¨linear_pmap.Sup c c_chain.directed_on, _, Î» _, linear_pmap.le_Sup c_chain.directed_onâŸ©,
    rintros âŸ¨x, hxâŸ© hxs,
    have hdir : directed_on (â‰¤) (linear_pmap.domain '' c),
      from directed_on_image.2 (c_chain.directed_on.mono linear_pmap.domain_mono.monotone),
    rcases (mem_Sup_of_directed (cne.image _) hdir).1 hx with âŸ¨_, âŸ¨f, hfc, rflâŸ©, hfxâŸ©,
    have : f â‰¤ linear_pmap.Sup c c_chain.directed_on, from linear_pmap.le_Sup _ hfc,
    convert â† hcs hfc âŸ¨x, hfxâŸ© hxs,
    apply this.2, refl }
end

end riesz_extension

/-- M. **Riesz extension theorem**: given a convex cone `s` in a vector space `E`, a submodule `p`,
and a linear `f : p â†’ â„`, assume that `f` is nonnegative on `p âˆ© s` and `p + s = E`. Then
there exists a globally defined linear function `g : E â†’ â„` that agrees with `f` on `p`,
and is nonnegative on `s`. -/
theorem riesz_extension (s : convex_cone â„ E) (f : linear_pmap â„ E â„)
  (nonneg : âˆ€ x : f.domain, (x : E) âˆˆ s â†’ 0 â‰¤ f x) (dense : âˆ€ y, âˆƒ x : f.domain, (x : E) + y âˆˆ s) :
  âˆƒ g : E â†’â‚—[â„] â„, (âˆ€ x : f.domain, g x = f x) âˆ§ (âˆ€ x âˆˆ s, 0 â‰¤ g x) :=
begin
  rcases riesz_extension.exists_top s f nonneg dense with âŸ¨âŸ¨g_dom, gâŸ©, âŸ¨hpg, hfgâŸ©, htop, hgsâŸ©,
  clear hpg,
  refine âŸ¨g âˆ˜â‚— â†‘(linear_equiv.of_top _ htop).symm, _, _âŸ©;
    simp only [comp_apply, linear_equiv.coe_coe, linear_equiv.of_top_symm_apply],
  { exact Î» x, (hfg (submodule.coe_mk _ _).symm).symm },
  { exact Î» x hx, hgs âŸ¨x, _âŸ© hx }
end

/-- **Hahn-Banach theorem**: if `N : E â†’ â„` is a sublinear map, `f` is a linear map
defined on a subspace of `E`, and `f x â‰¤ N x` for all `x` in the domain of `f`,
then `f` can be extended to the whole space to a linear map `g` such that `g x â‰¤ N x`
for all `x`. -/
theorem exists_extension_of_le_sublinear (f : linear_pmap â„ E â„) (N : E â†’ â„)
  (N_hom : âˆ€ (c : â„), 0 < c â†’ âˆ€ x, N (c â€¢ x) = c * N x)
  (N_add : âˆ€ x y, N (x + y) â‰¤ N x + N y)
  (hf : âˆ€ x : f.domain, f x â‰¤ N x) :
  âˆƒ g : E â†’â‚—[â„] â„, (âˆ€ x : f.domain, g x = f x) âˆ§ (âˆ€ x, g x â‰¤ N x) :=
begin
  let s : convex_cone â„ (E Ã— â„) :=
  { carrier := {p : E Ã— â„ | N p.1 â‰¤ p.2 },
    smul_mem' := Î» c hc p hp,
      calc N (c â€¢ p.1) = c * N p.1 : N_hom c hc p.1
      ... â‰¤ c * p.2 : mul_le_mul_of_nonneg_left hp hc.le,
    add_mem' := Î» x hx y hy, (N_add _ _).trans (add_le_add hx hy) },
  obtain âŸ¨g, g_eq, g_nonnegâŸ© :=
    riesz_extension s ((-f).coprod (linear_map.id.to_pmap âŠ¤)) _ _;
    try { simp only [linear_pmap.coprod_apply, to_pmap_apply, id_apply,
            linear_pmap.neg_apply, â† sub_eq_neg_add, sub_nonneg, subtype.coe_mk] at * },
  replace g_eq : âˆ€ (x : f.domain) (y : â„), g (x, y) = y - f x,
  { intros x y,
    simpa only [subtype.coe_mk, subtype.coe_eta] using g_eq âŸ¨(x, y), âŸ¨x.2, trivialâŸ©âŸ© },
  { refine âŸ¨-g.comp (inl â„ E â„), _, _âŸ©; simp only [neg_apply, inl_apply, comp_apply],
    { intro x, simp [g_eq x 0] },
    { intro x,
      have A : (x, N x) = (x, 0) + (0, N x), by simp,
      have B := g_nonneg âŸ¨x, N xâŸ© (le_refl (N x)),
      rw [A, map_add, â† neg_le_iff_add_nonneg'] at B,
      have C := g_eq 0 (N x),
      simp only [submodule.coe_zero, f.map_zero, sub_zero] at C,
      rwa â† C } },
  { exact Î» x hx, le_trans (hf _) hx },
  { rintros âŸ¨x, yâŸ©,
    refine âŸ¨âŸ¨(0, N x - y), âŸ¨f.domain.zero_mem, trivialâŸ©âŸ©, _âŸ©,
    simp only [convex_cone.mem_mk, mem_set_of_eq, subtype.coe_mk, prod.fst_add, prod.snd_add,
      zero_add, sub_add_cancel] }
end

/-! ### The dual cone -/

section dual
variables {H : Type*} [inner_product_space â„ H] (s t : set H)
open_locale real_inner_product_space

/-- The dual cone is the cone consisting of all points `y` such that for
all points `x` in a given set `0 â‰¤ âŸª x, y âŸ«`. -/
noncomputable def set.inner_dual_cone (s : set H) : convex_cone â„ H :=
{ carrier := { y | âˆ€ x âˆˆ s, 0 â‰¤ âŸª x, y âŸ« },
  smul_mem' := Î» c hc y hy x hx,
  begin
    rw real_inner_smul_right,
    exact mul_nonneg hc.le (hy x hx)
  end,
  add_mem' := Î» u hu v hv x hx,
  begin
    rw inner_add_right,
    exact add_nonneg (hu x hx) (hv x hx)
  end }

lemma mem_inner_dual_cone (y : H) (s : set H) :
  y âˆˆ s.inner_dual_cone â†” âˆ€ x âˆˆ s, 0 â‰¤ âŸª x, y âŸ« := by refl

@[simp] lemma inner_dual_cone_empty : (âˆ… : set H).inner_dual_cone = âŠ¤ :=
convex_cone.ext' (eq_univ_of_forall
  (Î» x y hy, false.elim (set.not_mem_empty _ hy)))

lemma inner_dual_cone_le_inner_dual_cone (h : t âŠ† s) :
  s.inner_dual_cone â‰¤ t.inner_dual_cone :=
Î» y hy x hx, hy x (h hx)

lemma pointed_inner_dual_cone : s.inner_dual_cone.pointed :=
Î» x hx, by rw inner_zero_right

end dual
