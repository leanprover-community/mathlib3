/-
Copyright (c) 2020 Yury Kudryashov All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, FrÃ©dÃ©ric Dupuis
-/
import analysis.inner_product_space.projection

/-!
# Convex cones

In a `ğ•œ`-module `E`, we define a convex cone as a set `s` such that `a â€¢ x + b â€¢ y âˆˆ s` whenever
`x, y âˆˆ s` and `a, b > 0`. We prove that convex cones form a `complete_lattice`, and define their
images (`convex_cone.map`) and preimages (`convex_cone.comap`) under linear maps.

We define pointed, blunt, flat and salient cones, and prove the correspondence between
convex cones and ordered modules.

We define `convex.to_cone` to be the minimal cone that includes a given convex set.

We define `set.inner_dual_cone` to be the cone consisting of all points `y` such that for
all points `x` in a given set `0 â‰¤ âŸª x, y âŸ«`.

## Main statements

We prove two extension theorems:
* `riesz_extension`:
  [M. Riesz extension theorem](https://en.wikipedia.org/wiki/M._Riesz_extension_theorem) says that
  if `s` is a convex cone in a real vector space `E`, `p` is a submodule of `E`
  such that `p + s = E`, and `f` is a linear function `p â†’ â„` which is
  nonnegative on `p âˆ© s`, then there exists a globally defined linear function
  `g : E â†’ â„` that agrees with `f` on `p`, and is nonnegative on `s`.
* `exists_extension_of_le_sublinear`:
  Hahn-Banach theorem: if `N : E â†’ â„` is a sublinear map, `f` is a linear map
  defined on a subspace of `E`, and `f x â‰¤ N x` for all `x` in the domain of `f`,
  then `f` can be extended to the whole space to a linear map `g` such that `g x â‰¤ N x`
  for all `x`

We prove the following theorems:
* `convex_cone.hyperplane_separation_of_nonempty_of_is_closed_of_nmem`:
  This variant of the
  [hyperplane separation theorem](https://en.wikipedia.org/wiki/Hyperplane_separation_theorem)
  states that given a nonempty, closed, convex cone `K` in a complete, real inner product space `H`
  and a point `b` disjoint from it, there is a vector `y` which separates `b` from `K` in the sense
  that for all points `x` in `K`, `0 â‰¤ âŸªx, yâŸ«_â„` and `âŸªy, bâŸ«_â„ < 0`. This is also a geometric
  interpretation of the
  [Farkas lemma](https://en.wikipedia.org/wiki/Farkas%27_lemma#Geometric_interpretation).
* `convex_cone.inner_dual_cone_of_inner_dual_cone_eq_self`:
  The `inner_dual_cone` of the `inner_dual_cone` of a nonempty, closed, convex cone is itself.

## Implementation notes

While `convex ğ•œ` is a predicate on sets, `convex_cone ğ•œ E` is a bundled convex cone.

## References

* https://en.wikipedia.org/wiki/Convex_cone
* [Stephen P. Boyd and Lieven Vandenberghe, *Convex Optimization*][boydVandenberghe2004]
* [Emo Welzl and Bernd GÃ¤rtner, *Cone Programming*][welzl_garter]
-/


open set linear_map
open_locale classical pointwise

variables {ğ•œ E F G : Type*}

/-! ### Definition of `convex_cone` and basic properties -/

section definitions
variables (ğ•œ E) [ordered_semiring ğ•œ]

/-- A convex cone is a subset `s` of a `ğ•œ`-module such that `a â€¢ x + b â€¢ y âˆˆ s` whenever `a, b > 0`
and `x, y âˆˆ s`. -/
structure convex_cone [add_comm_monoid E] [has_smul ğ•œ E] :=
(carrier : set E)
(smul_mem' : âˆ€ â¦ƒc : ğ•œâ¦„, 0 < c â†’ âˆ€ â¦ƒx : Eâ¦„, x âˆˆ carrier â†’ c â€¢ x âˆˆ carrier)
(add_mem' : âˆ€ â¦ƒxâ¦„ (hx : x âˆˆ carrier) â¦ƒyâ¦„ (hy : y âˆˆ carrier), x + y âˆˆ carrier)

end definitions

variables {ğ•œ E}

namespace convex_cone
section ordered_semiring
variables [ordered_semiring ğ•œ] [add_comm_monoid E]

section has_smul
variables [has_smul ğ•œ E] (S T : convex_cone ğ•œ E)

instance : set_like (convex_cone ğ•œ E) E :=
{ coe := carrier,
  coe_injective' := Î» S T h, by cases S; cases T; congr' }

@[simp] lemma coe_mk {s : set E} {hâ‚ hâ‚‚} : â†‘(@mk ğ•œ _ _ _ _ s hâ‚ hâ‚‚) = s := rfl

@[simp] lemma mem_mk {s : set E} {hâ‚ hâ‚‚ x} : x âˆˆ @mk ğ•œ _ _ _ _ s hâ‚ hâ‚‚ â†” x âˆˆ s := iff.rfl

/-- Two `convex_cone`s are equal if they have the same elements. -/
@[ext] theorem ext {S T : convex_cone ğ•œ E} (h : âˆ€ x, x âˆˆ S â†” x âˆˆ T) : S = T := set_like.ext h

lemma smul_mem {c : ğ•œ} {x : E} (hc : 0 < c) (hx : x âˆˆ S) : c â€¢ x âˆˆ S := S.smul_mem' hc hx

lemma add_mem â¦ƒxâ¦„ (hx : x âˆˆ S) â¦ƒyâ¦„ (hy : y âˆˆ S) : x + y âˆˆ S := S.add_mem' hx hy

instance : add_mem_class (convex_cone ğ•œ E) E :=
{ add_mem := Î» c a b ha hb, add_mem c ha hb }

instance : has_inf (convex_cone ğ•œ E) :=
âŸ¨Î» S T, âŸ¨S âˆ© T, Î» c hc x hx, âŸ¨S.smul_mem hc hx.1, T.smul_mem hc hx.2âŸ©,
  Î» x hx y hy, âŸ¨S.add_mem hx.1 hy.1, T.add_mem hx.2 hy.2âŸ©âŸ©âŸ©

@[simp] lemma coe_inf : ((S âŠ“ T : convex_cone ğ•œ E) : set E) = â†‘S âˆ© â†‘T := rfl

lemma mem_inf {x} : x âˆˆ S âŠ“ T â†” x âˆˆ S âˆ§ x âˆˆ T := iff.rfl

instance : has_Inf (convex_cone ğ•œ E) :=
âŸ¨Î» S, âŸ¨â‹‚ s âˆˆ S, â†‘s,
  Î» c hc x hx, mem_bInter $ Î» s hs, s.smul_mem hc $ mem_Interâ‚‚.1 hx s hs,
  Î» x hx y hy, mem_bInter $ Î» s hs, s.add_mem (mem_Interâ‚‚.1 hx s hs) (mem_Interâ‚‚.1 hy s hs)âŸ©âŸ©

@[simp] lemma coe_Inf (S : set (convex_cone ğ•œ E)) : â†‘(Inf S) = â‹‚ s âˆˆ S, (s : set E) := rfl

lemma mem_Inf {x : E} {S : set (convex_cone ğ•œ E)} : x âˆˆ Inf S â†” âˆ€ s âˆˆ S, x âˆˆ s := mem_Interâ‚‚

@[simp] lemma coe_infi {Î¹ : Sort*} (f : Î¹ â†’ convex_cone ğ•œ E) : â†‘(infi f) = â‹‚ i, (f i : set E) :=
by simp [infi]

lemma mem_infi {Î¹ : Sort*} {x : E} {f : Î¹ â†’ convex_cone ğ•œ E} : x âˆˆ infi f â†” âˆ€ i, x âˆˆ f i :=
mem_Interâ‚‚.trans $ by simp

variables (ğ•œ)

instance : has_bot (convex_cone ğ•œ E) := âŸ¨âŸ¨âˆ…, Î» c hc x, false.elim, Î» x, false.elimâŸ©âŸ©

lemma mem_bot (x : E) : x âˆˆ (âŠ¥ : convex_cone ğ•œ E) = false := rfl

@[simp] lemma coe_bot : â†‘(âŠ¥ : convex_cone ğ•œ E) = (âˆ… : set E) := rfl

instance : has_top (convex_cone ğ•œ E) := âŸ¨âŸ¨univ, Î» c hc x hx, mem_univ _, Î» x hx y hy, mem_univ _âŸ©âŸ©

lemma mem_top (x : E) : x âˆˆ (âŠ¤ : convex_cone ğ•œ E) := mem_univ x

@[simp] lemma coe_top : â†‘(âŠ¤ : convex_cone ğ•œ E) = (univ : set E) := rfl

instance : complete_lattice (convex_cone ğ•œ E) :=
{ le           := (â‰¤),
  lt           := (<),
  bot          := (âŠ¥),
  bot_le       := Î» S x, false.elim,
  top          := (âŠ¤),
  le_top       := Î» S x hx, mem_top ğ•œ x,
  inf          := (âŠ“),
  Inf          := has_Inf.Inf,
  sup          := Î» a b, Inf {x | a â‰¤ x âˆ§ b â‰¤ x},
  Sup          := Î» s, Inf {T | âˆ€ S âˆˆ s, S â‰¤ T},
  le_sup_left  := Î» a b, Î» x hx, mem_Inf.2 $ Î» s hs, hs.1 hx,
  le_sup_right := Î» a b, Î» x hx, mem_Inf.2 $ Î» s hs, hs.2 hx,
  sup_le       := Î» a b c ha hb x hx, mem_Inf.1 hx c âŸ¨ha, hbâŸ©,
  le_inf       := Î» a b c ha hb x hx, âŸ¨ha hx, hb hxâŸ©,
  inf_le_left  := Î» a b x, and.left,
  inf_le_right := Î» a b x, and.right,
  le_Sup       := Î» s p hs x hx, mem_Inf.2 $ Î» t ht, ht p hs hx,
  Sup_le       := Î» s p hs x hx, mem_Inf.1 hx p hs,
  le_Inf       := Î» s a ha x hx, mem_Inf.2 $ Î» t ht, ha t ht hx,
  Inf_le       := Î» s a ha x hx, mem_Inf.1 hx _ ha,
  .. set_like.partial_order }

instance : inhabited (convex_cone ğ•œ E) := âŸ¨âŠ¥âŸ©

end has_smul

section module
variables [module ğ•œ E] (S : convex_cone ğ•œ E)

protected lemma convex : convex ğ•œ (S : set E) :=
convex_iff_forall_pos.2 $ Î» x hx y hy a b ha hb _, S.add_mem (S.smul_mem ha hx) (S.smul_mem hb hy)

end module
end ordered_semiring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F] [add_comm_monoid G]

section mul_action
variables [mul_action ğ•œ E] (S : convex_cone ğ•œ E)

lemma smul_mem_iff {c : ğ•œ} (hc : 0 < c) {x : E} :
  c â€¢ x âˆˆ S â†” x âˆˆ S :=
âŸ¨Î» h, inv_smul_smulâ‚€ hc.ne' x â–¸ S.smul_mem (inv_pos.2 hc) h, S.smul_mem hcâŸ©

end mul_action

section module
variables [module ğ•œ E] [module ğ•œ F] [module ğ•œ G]

/-- The image of a convex cone under a `ğ•œ`-linear map is a convex cone. -/
def map (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ E) : convex_cone ğ•œ F :=
{ carrier := f '' S,
  smul_mem' := Î» c hc y âŸ¨x, hx, hyâŸ©, hy â–¸ f.map_smul c x â–¸ mem_image_of_mem f (S.smul_mem hc hx),
  add_mem' := Î» yâ‚ âŸ¨xâ‚, hxâ‚, hyâ‚âŸ© yâ‚‚ âŸ¨xâ‚‚, hxâ‚‚, hyâ‚‚âŸ©, hyâ‚ â–¸ hyâ‚‚ â–¸ f.map_add xâ‚ xâ‚‚ â–¸
    mem_image_of_mem f (S.add_mem hxâ‚ hxâ‚‚) }

@[simp] lemma mem_map {f : E â†’â‚—[ğ•œ] F} {S : convex_cone ğ•œ E} {y : F} :
  y âˆˆ S.map f â†” âˆƒ x âˆˆ S, f x = y :=
mem_image_iff_bex

lemma map_map (g : F â†’â‚—[ğ•œ] G) (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ E) :
  (S.map f).map g = S.map (g.comp f) :=
set_like.coe_injective $ image_image g f S

@[simp] lemma map_id (S : convex_cone ğ•œ E) : S.map linear_map.id = S :=
set_like.coe_injective $ image_id _

/-- The preimage of a convex cone under a `ğ•œ`-linear map is a convex cone. -/
def comap (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ F) : convex_cone ğ•œ E :=
{ carrier := f â»Â¹' S,
  smul_mem' := Î» c hc x hx, by { rw [mem_preimage, f.map_smul c], exact S.smul_mem hc hx },
  add_mem' := Î» x hx y hy, by { rw [mem_preimage, f.map_add], exact S.add_mem hx hy } }

@[simp] lemma coe_comap (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ F) : (S.comap f : set E) = f â»Â¹' S := rfl

@[simp] lemma comap_id (S : convex_cone ğ•œ E) : S.comap linear_map.id = S :=
set_like.coe_injective preimage_id

lemma comap_comap (g : F â†’â‚—[ğ•œ] G) (f : E â†’â‚—[ğ•œ] F) (S : convex_cone ğ•œ G) :
  (S.comap g).comap f = S.comap (g.comp f) :=
set_like.coe_injective $ preimage_comp.symm

@[simp] lemma mem_comap {f : E â†’â‚—[ğ•œ] F} {S : convex_cone ğ•œ F} {x : E} : x âˆˆ S.comap f â†” f x âˆˆ S :=
iff.rfl

end module
end add_comm_monoid

section ordered_add_comm_group
variables [ordered_add_comm_group E] [module ğ•œ E]

/--
Constructs an ordered module given an `ordered_add_comm_group`, a cone, and a proof that
the order relation is the one defined by the cone.
-/
lemma to_ordered_smul (S : convex_cone ğ•œ E) (h : âˆ€ x y : E, x â‰¤ y â†” y - x âˆˆ S) :
  ordered_smul ğ•œ E :=
ordered_smul.mk'
begin
  intros x y z xy hz,
  rw [h (z â€¢ x) (z â€¢ y), â†smul_sub z y x],
  exact smul_mem S hz ((h x y).mp xy.le),
end

end ordered_add_comm_group
end linear_ordered_field

/-! ### Convex cones with extra properties -/

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [has_smul ğ•œ E] (S : convex_cone ğ•œ E)

/-- A convex cone is pointed if it includes `0`. -/
def pointed (S : convex_cone ğ•œ E) : Prop := (0 : E) âˆˆ S

/-- A convex cone is blunt if it doesn't include `0`. -/
def blunt (S : convex_cone ğ•œ E) : Prop := (0 : E) âˆ‰ S

lemma pointed_iff_not_blunt (S : convex_cone ğ•œ E) : S.pointed â†” Â¬S.blunt :=
âŸ¨Î» hâ‚ hâ‚‚, hâ‚‚ hâ‚, not_not.mpâŸ©

lemma blunt_iff_not_pointed (S : convex_cone ğ•œ E) : S.blunt â†” Â¬S.pointed :=
by rw [pointed_iff_not_blunt, not_not]

lemma pointed.mono {S T : convex_cone ğ•œ E} (h : S â‰¤ T) : S.pointed â†’ T.pointed := @h _

lemma blunt.anti {S T : convex_cone ğ•œ E} (h : T â‰¤ S) : S.blunt â†’ T.blunt := (âˆ˜ @@h)

end add_comm_monoid

section add_comm_group
variables [add_comm_group E] [has_smul ğ•œ E] (S : convex_cone ğ•œ E)

/-- A convex cone is flat if it contains some nonzero vector `x` and its opposite `-x`. -/
def flat : Prop := âˆƒ x âˆˆ S, x â‰  (0 : E) âˆ§ -x âˆˆ S

/-- A convex cone is salient if it doesn't include `x` and `-x` for any nonzero `x`. -/
def salient : Prop := âˆ€ x âˆˆ S, x â‰  (0 : E) â†’ -x âˆ‰ S

lemma salient_iff_not_flat (S : convex_cone ğ•œ E) : S.salient â†” Â¬S.flat :=
begin
  split,
  { rintros hâ‚ âŸ¨x, xs, Hâ‚, Hâ‚‚âŸ©,
    exact hâ‚ x xs Hâ‚ Hâ‚‚ },
  { intro h,
    unfold flat at h,
    push_neg at h,
    exact h }
end

lemma flat.mono {S T : convex_cone ğ•œ E} (h : S â‰¤ T) : S.flat â†’ T.flat
| âŸ¨x, hxS, hx, hnxSâŸ© := âŸ¨x, h hxS, hx, h hnxSâŸ©

lemma salient.anti {S T : convex_cone ğ•œ E} (h : T â‰¤ S) : S.salient â†’ T.salient :=
Î» hS x hxT hx hnT, hS x (h hxT) hx (h hnT)

/-- A flat cone is always pointed (contains `0`). -/
lemma flat.pointed {S : convex_cone ğ•œ E} (hS : S.flat) : S.pointed :=
begin
  obtain âŸ¨x, hx, _, hxnegâŸ© := hS,
  rw [pointed, â†add_neg_self x],
  exact add_mem S hx hxneg,
end

/-- A blunt cone (one not containing `0`) is always salient. -/
lemma blunt.salient {S : convex_cone ğ•œ E} : S.blunt â†’ S.salient :=
begin
  rw [salient_iff_not_flat, blunt_iff_not_pointed],
  exact mt flat.pointed,
end

/-- A pointed convex cone defines a preorder. -/
def to_preorder (hâ‚ : S.pointed) : preorder E :=
{ le := Î» x y, y - x âˆˆ S,
  le_refl := Î» x, by change x - x âˆˆ S; rw [sub_self x]; exact hâ‚,
  le_trans := Î» x y z xy zy, by simpa using add_mem S zy xy }

/-- A pointed and salient cone defines a partial order. -/
def to_partial_order (hâ‚ : S.pointed) (hâ‚‚ : S.salient) : partial_order E :=
{ le_antisymm :=
    begin
      intros a b ab ba,
      by_contradiction h,
      have h' : b - a â‰  0 := Î» h'', h (eq_of_sub_eq_zero h'').symm,
      have H := hâ‚‚ (b-a) ab h',
      rw neg_sub b a at H,
      exact H ba,
    end,
  ..to_preorder S hâ‚ }

/-- A pointed and salient cone defines an `ordered_add_comm_group`. -/
def to_ordered_add_comm_group (hâ‚ : S.pointed) (hâ‚‚ : S.salient) :
  ordered_add_comm_group E :=
{ add_le_add_left :=
    begin
      intros a b hab c,
      change c + b - (c + a) âˆˆ S,
      rw add_sub_add_left_eq_sub,
      exact hab,
    end,
  ..to_partial_order S hâ‚ hâ‚‚,
  ..show add_comm_group E, by apply_instance }

end add_comm_group

section module
variables [add_comm_monoid E] [module ğ•œ E]

instance : has_zero (convex_cone ğ•œ E) := âŸ¨âŸ¨0, Î» _ _, by simp, Î» _, by simpâŸ©âŸ©

@[simp] lemma mem_zero (x : E) : x âˆˆ (0 : convex_cone ğ•œ E) â†” x = 0 := iff.rfl
@[simp] lemma coe_zero : ((0 : convex_cone ğ•œ E) : set E) = 0 := rfl

lemma pointed_zero : (0 : convex_cone ğ•œ E).pointed := by rw [pointed, mem_zero]

instance : has_add (convex_cone ğ•œ E) := âŸ¨ Î» Kâ‚ Kâ‚‚,
{ carrier := {z | âˆƒ (x y : E), x âˆˆ Kâ‚ âˆ§ y âˆˆ Kâ‚‚ âˆ§ x + y = z},
  smul_mem' :=
  begin
    rintro c hc _ âŸ¨x, y, hx, hy, rflâŸ©,
    rw smul_add,
    use [c â€¢ x, c â€¢ y, Kâ‚.smul_mem hc hx, Kâ‚‚.smul_mem hc hy],
  end,
  add_mem' :=
  begin
    rintro _ âŸ¨xâ‚, xâ‚‚, hxâ‚, hxâ‚‚, rflâŸ© y âŸ¨yâ‚, yâ‚‚, hyâ‚, hyâ‚‚, rflâŸ©,
    use [xâ‚ + yâ‚, xâ‚‚ + yâ‚‚, Kâ‚.add_mem hxâ‚ hyâ‚, Kâ‚‚.add_mem hxâ‚‚ hyâ‚‚],
    abel,
  end } âŸ©

@[simp] lemma mem_add {Kâ‚ Kâ‚‚ : convex_cone ğ•œ E} {a : E} :
  a âˆˆ Kâ‚ + Kâ‚‚ â†” âˆƒ (x y : E), x âˆˆ Kâ‚ âˆ§ y âˆˆ Kâ‚‚ âˆ§ x + y = a := iff.rfl

instance : add_zero_class (convex_cone ğ•œ E) :=
âŸ¨0, has_add.add, Î» _, by {ext, simp}, Î» _, by {ext, simp}âŸ©

instance : add_comm_semigroup (convex_cone ğ•œ E) :=
{ add := has_add.add,
  add_assoc := Î» _ _ _, set_like.coe_injective $ set.add_comm_semigroup.add_assoc _ _ _,
  add_comm := Î» _ _, set_like.coe_injective $ set.add_comm_semigroup.add_comm _ _ }

end module
end ordered_semiring

end convex_cone

namespace submodule

/-! ### Submodules are cones -/

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [module ğ•œ E]

/-- Every submodule is trivially a convex cone. -/
def to_convex_cone (S : submodule ğ•œ E) : convex_cone ğ•œ E :=
{ carrier := S,
  smul_mem' := Î» c hc x hx, S.smul_mem c hx,
  add_mem' := Î» x hx y hy, S.add_mem hx hy }

@[simp] lemma coe_to_convex_cone (S : submodule ğ•œ E) : â†‘S.to_convex_cone = (S : set E) := rfl

@[simp] lemma mem_to_convex_cone {x : E} {S : submodule ğ•œ E} : x âˆˆ S.to_convex_cone â†” x âˆˆ S :=
iff.rfl

@[simp] lemma to_convex_cone_le_iff {S T : submodule ğ•œ E} :
  S.to_convex_cone â‰¤ T.to_convex_cone â†” S â‰¤ T :=
iff.rfl

@[simp] lemma to_convex_cone_bot : (âŠ¥ : submodule ğ•œ E).to_convex_cone = 0 := rfl
@[simp] lemma to_convex_cone_top : (âŠ¤ : submodule ğ•œ E).to_convex_cone = âŠ¤ := rfl

@[simp] lemma to_convex_cone_inf (S T : submodule ğ•œ E) :
  (S âŠ“ T).to_convex_cone = S.to_convex_cone âŠ“ T.to_convex_cone :=
rfl

@[simp] lemma pointed_to_convex_cone (S : submodule ğ•œ E) : S.to_convex_cone.pointed := S.zero_mem

end add_comm_monoid
end ordered_semiring

end submodule

namespace convex_cone

/-! ### Positive cone of an ordered module -/

section positive_cone
variables (ğ•œ E) [ordered_semiring ğ•œ] [ordered_add_comm_group E] [module ğ•œ E] [ordered_smul ğ•œ E]

/--
The positive cone is the convex cone formed by the set of nonnegative elements in an ordered
module.
-/
def positive : convex_cone ğ•œ E :=
{ carrier := set.Ici 0,
  smul_mem' := Î» c hc x (hx : _ â‰¤ _), smul_nonneg hc.le hx,
  add_mem' := Î» x (hx : _ â‰¤ _) y (hy : _ â‰¤ _), add_nonneg hx hy }

@[simp] lemma mem_positive {x : E} : x âˆˆ positive ğ•œ E â†” 0 â‰¤ x := iff.rfl
@[simp] lemma coe_positive : â†‘(positive ğ•œ E) = set.Ici (0 : E) := rfl

/-- The positive cone of an ordered module is always salient. -/
lemma salient_positive : salient (positive ğ•œ E) :=
Î» x xs hx hx', lt_irrefl (0 : E)
  (calc
    0   < x         : lt_of_le_of_ne xs hx.symm
    ... â‰¤ x + (-x)  : le_add_of_nonneg_right hx'
    ... = 0         : add_neg_self x)

/-- The positive cone of an ordered module is always pointed. -/
lemma pointed_positive : pointed (positive ğ•œ E) := le_refl 0

/-- The cone of strictly positive elements.

Note that this naming diverges from the mathlib convention of `pos` and `nonneg` due to "positive
cone" (`convex_cone.positive`) being established terminology for the non-negative elements. -/
def strictly_positive : convex_cone ğ•œ E :=
{ carrier := set.Ioi 0,
  smul_mem' := Î» c hc x (hx : _ < _), smul_pos hc hx,
  add_mem' := Î» x hx y hy, add_pos hx hy }

@[simp] lemma mem_strictly_positive {x : E} : x âˆˆ strictly_positive ğ•œ E â†” 0 < x := iff.rfl
@[simp] lemma coe_strictly_positive : â†‘(strictly_positive ğ•œ E) = set.Ioi (0 : E) := rfl

lemma positive_le_strictly_positive : strictly_positive ğ•œ E â‰¤ positive ğ•œ E := Î» x, le_of_lt

/-- The strictly positive cone of an ordered module is always salient. -/
lemma salient_strictly_positive : salient (strictly_positive ğ•œ E) :=
(salient_positive ğ•œ E).anti $ positive_le_strictly_positive ğ•œ E

/-- The strictly positive cone of an ordered module is always blunt. -/
lemma blunt_strictly_positive : blunt (strictly_positive ğ•œ E) := lt_irrefl 0

end positive_cone
end convex_cone

/-! ### Cone over a convex set -/

section cone_from_convex
variables [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E]

namespace convex

/-- The set of vectors proportional to those in a convex set forms a convex cone. -/
def to_cone (s : set E) (hs : convex ğ•œ s) : convex_cone ğ•œ E :=
begin
  apply convex_cone.mk (â‹ƒ (c : ğ•œ) (H : 0 < c), c â€¢ s);
    simp only [mem_Union, mem_smul_set],
  { rintros c c_pos _ âŸ¨c', c'_pos, x, hx, rflâŸ©,
    exact âŸ¨c * c', mul_pos c_pos c'_pos, x, hx, (smul_smul _ _ _).symmâŸ© },
  { rintros _ âŸ¨cx, cx_pos, x, hx, rflâŸ© _ âŸ¨cy, cy_pos, y, hy, rflâŸ©,
    have : 0 < cx + cy, from add_pos cx_pos cy_pos,
    refine âŸ¨_, this, _, convex_iff_div.1 hs hx hy cx_pos.le cy_pos.le this, _âŸ©,
    simp only [smul_add, smul_smul, mul_div_assoc', mul_div_cancel_left _ this.ne'] }
end

variables {s : set E} (hs : convex ğ•œ s) {x : E}

lemma mem_to_cone : x âˆˆ hs.to_cone s â†” âˆƒ (c : ğ•œ), 0 < c âˆ§ âˆƒ y âˆˆ s, c â€¢ y = x :=
by simp only [to_cone, convex_cone.mem_mk, mem_Union, mem_smul_set, eq_comm, exists_prop]

lemma mem_to_cone' : x âˆˆ hs.to_cone s â†” âˆƒ (c : ğ•œ), 0 < c âˆ§ c â€¢ x âˆˆ s :=
begin
  refine hs.mem_to_cone.trans âŸ¨_, _âŸ©,
  { rintros âŸ¨c, hc, y, hy, rflâŸ©,
    exact âŸ¨câ»Â¹, inv_pos.2 hc, by rwa [smul_smul, inv_mul_cancel hc.ne', one_smul]âŸ© },
  { rintros âŸ¨c, hc, hcxâŸ©,
    exact âŸ¨câ»Â¹, inv_pos.2 hc, _, hcx, by rw [smul_smul, inv_mul_cancel hc.ne', one_smul]âŸ© }
end

lemma subset_to_cone : s âŠ† hs.to_cone s :=
Î» x hx, hs.mem_to_cone'.2 âŸ¨1, zero_lt_one, by rwa one_smulâŸ©

/-- `hs.to_cone s` is the least cone that includes `s`. -/
lemma to_cone_is_least : is_least { t : convex_cone ğ•œ E | s âŠ† t } (hs.to_cone s) :=
begin
  refine âŸ¨hs.subset_to_cone, Î» t ht x hx, _âŸ©,
  rcases hs.mem_to_cone.1 hx with âŸ¨c, hc, y, hy, rflâŸ©,
  exact t.smul_mem hc (ht hy)
end

lemma to_cone_eq_Inf : hs.to_cone s = Inf { t : convex_cone ğ•œ E | s âŠ† t } :=
hs.to_cone_is_least.is_glb.Inf_eq.symm

end convex

lemma convex_hull_to_cone_is_least (s : set E) :
  is_least {t : convex_cone ğ•œ E | s âŠ† t} ((convex_convex_hull ğ•œ s).to_cone _) :=
begin
  convert (convex_convex_hull ğ•œ s).to_cone_is_least,
  ext t,
  exact âŸ¨Î» h, convex_hull_min h t.convex, (subset_convex_hull ğ•œ s).transâŸ©,
end

lemma convex_hull_to_cone_eq_Inf (s : set E) :
  (convex_convex_hull ğ•œ s).to_cone _ = Inf {t : convex_cone ğ•œ E | s âŠ† t} :=
eq.symm $ is_glb.Inf_eq $ is_least.is_glb $ convex_hull_to_cone_is_least s

end cone_from_convex

/-!
### M. Riesz extension theorem

Given a convex cone `s` in a vector space `E`, a submodule `p`, and a linear `f : p â†’ â„`, assume
that `f` is nonnegative on `p âˆ© s` and `p + s = E`. Then there exists a globally defined linear
function `g : E â†’ â„` that agrees with `f` on `p`, and is nonnegative on `s`.

We prove this theorem using Zorn's lemma. `riesz_extension.step` is the main part of the proof.
It says that if the domain `p` of `f` is not the whole space, then `f` can be extended to a larger
subspace `p âŠ” span â„ {y}` without breaking the non-negativity condition.

In `riesz_extension.exists_top` we use Zorn's lemma to prove that we can extend `f`
to a linear map `g` on `âŠ¤ : submodule E`. Mathematically this is the same as a linear map on `E`
but in Lean `âŠ¤ : submodule E` is isomorphic but is not equal to `E`. In `riesz_extension`
we use this isomorphism to prove the theorem.
-/

variables [add_comm_group E] [module â„ E]

namespace riesz_extension
open submodule
variables (s : convex_cone â„ E) (f : E â†’â‚—.[â„] â„)

/-- Induction step in M. Riesz extension theorem. Given a convex cone `s` in a vector space `E`,
a partially defined linear map `f : f.domain â†’ â„`, assume that `f` is nonnegative on `f.domain âˆ© p`
and `p + s = E`. If `f` is not defined on the whole `E`, then we can extend it to a larger
submodule without breaking the non-negativity condition. -/
lemma step (nonneg : âˆ€ x : f.domain, (x : E) âˆˆ s â†’ 0 â‰¤ f x)
  (dense : âˆ€ y, âˆƒ x : f.domain, (x : E) + y âˆˆ s) (hdom : f.domain â‰  âŠ¤) :
  âˆƒ g, f < g âˆ§ âˆ€ x : g.domain, (x : E) âˆˆ s â†’ 0 â‰¤ g x :=
begin
  obtain âŸ¨y, -, hyâŸ© : âˆƒ (y : E) (h : y âˆˆ âŠ¤), y âˆ‰ f.domain,
    { exact @set_like.exists_of_lt (submodule â„ E) _ _ _ _ (lt_top_iff_ne_top.2 hdom) },
  obtain âŸ¨c, le_c, c_leâŸ© :
    âˆƒ c, (âˆ€ x : f.domain, -(x:E) - y âˆˆ s â†’ f x â‰¤ c) âˆ§ (âˆ€ x : f.domain, (x:E) + y âˆˆ s â†’ c â‰¤ f x),
  { set Sp := f '' {x : f.domain | (x:E) + y âˆˆ s},
    set Sn := f '' {x : f.domain | -(x:E) - y âˆˆ s},
    suffices : (upper_bounds Sn âˆ© lower_bounds Sp).nonempty,
      by simpa only [set.nonempty, upper_bounds, lower_bounds, ball_image_iff] using this,
    refine exists_between_of_forall_le (nonempty.image f _) (nonempty.image f (dense y)) _,
    { rcases (dense (-y)) with âŸ¨x, hxâŸ©,
      rw [â† neg_neg x, add_subgroup_class.coe_neg, â† sub_eq_add_neg] at hx,
      exact âŸ¨_, hxâŸ© },
    rintros a âŸ¨xn, hxn, rflâŸ© b âŸ¨xp, hxp, rflâŸ©,
    have := s.add_mem hxp hxn,
    rw [add_assoc, add_sub_cancel'_right, â† sub_eq_add_neg, â† add_subgroup_class.coe_sub] at this,
    replace := nonneg _ this,
    rwa [f.map_sub, sub_nonneg] at this },
  have hy' : y â‰  0, from Î» hyâ‚€, hy (hyâ‚€.symm â–¸ zero_mem _),
  refine âŸ¨f.sup_span_singleton y (-c) hy, _, _âŸ©,
  { refine lt_iff_le_not_le.2 âŸ¨f.left_le_sup _ _, Î» H, _âŸ©,
    replace H := linear_pmap.domain_mono.monotone H,
    rw [linear_pmap.domain_sup_span_singleton, sup_le_iff, span_le, singleton_subset_iff] at H,
    exact hy H.2 },
  { rintros âŸ¨z, hzâŸ© hzs,
    rcases mem_sup.1 hz with âŸ¨x, hx, y', hy', rflâŸ©,
    rcases mem_span_singleton.1 hy' with âŸ¨r, rflâŸ©,
    simp only [subtype.coe_mk] at hzs,
    erw [linear_pmap.sup_span_singleton_apply_mk _ _ _ _ _ hx, smul_neg,
      â† sub_eq_add_neg, sub_nonneg],
    rcases lt_trichotomy r 0 with hr|hr|hr,
    { have : -(râ»Â¹ â€¢ x) - y âˆˆ s,
        by rwa [â† s.smul_mem_iff (neg_pos.2 hr), smul_sub, smul_neg, neg_smul, neg_neg, smul_smul,
          mul_inv_cancel hr.ne, one_smul, sub_eq_add_neg, neg_smul, neg_neg],
      replace := le_c (râ»Â¹ â€¢ âŸ¨x, hxâŸ©) this,
      rwa [â† mul_le_mul_left (neg_pos.2 hr), neg_mul, neg_mul,
        neg_le_neg_iff, f.map_smul, smul_eq_mul, â† mul_assoc, mul_inv_cancel hr.ne,
        one_mul] at this },
    { subst r,
      simp only [zero_smul, add_zero] at hzs âŠ¢,
      apply nonneg,
      exact hzs },
    { have : râ»Â¹ â€¢ x + y âˆˆ s,
        by rwa [â† s.smul_mem_iff hr, smul_add, smul_smul, mul_inv_cancel hr.ne', one_smul],
      replace := c_le (râ»Â¹ â€¢ âŸ¨x, hxâŸ©) this,
      rwa [â† mul_le_mul_left hr, f.map_smul, smul_eq_mul, â† mul_assoc,
        mul_inv_cancel hr.ne', one_mul] at this } }
end

theorem exists_top (p : E â†’â‚—.[â„] â„)
  (hp_nonneg : âˆ€ x : p.domain, (x : E) âˆˆ s â†’ 0 â‰¤ p x)
  (hp_dense : âˆ€ y, âˆƒ x : p.domain, (x : E) + y âˆˆ s) :
  âˆƒ q â‰¥ p, q.domain = âŠ¤ âˆ§ âˆ€ x : q.domain, (x : E) âˆˆ s â†’ 0 â‰¤ q x :=
begin
  replace hp_nonneg : p âˆˆ { p | _ }, by { rw mem_set_of_eq, exact hp_nonneg },
  obtain âŸ¨q, hqs, hpq, hqâŸ© := zorn_nonempty_partial_orderâ‚€ _ _ _ hp_nonneg,
  { refine âŸ¨q, hpq, _, hqsâŸ©,
    contrapose! hq,
    rcases step s q hqs _ hq with âŸ¨r, hqr, hrâŸ©,
    { exact âŸ¨r, hr, hqr.le, hqr.ne'âŸ© },
    { exact Î» y, let âŸ¨x, hxâŸ© := hp_dense y in âŸ¨of_le hpq.left x, hxâŸ© } },
  { intros c hcs c_chain y hy,
    clear hp_nonneg hp_dense p,
    have cne : c.nonempty := âŸ¨y, hyâŸ©,
    refine âŸ¨linear_pmap.Sup c c_chain.directed_on, _, Î» _, linear_pmap.le_Sup c_chain.directed_onâŸ©,
    rintros âŸ¨x, hxâŸ© hxs,
    have hdir : directed_on (â‰¤) (linear_pmap.domain '' c),
      from directed_on_image.2 (c_chain.directed_on.mono linear_pmap.domain_mono.monotone),
    rcases (mem_Sup_of_directed (cne.image _) hdir).1 hx with âŸ¨_, âŸ¨f, hfc, rflâŸ©, hfxâŸ©,
    have : f â‰¤ linear_pmap.Sup c c_chain.directed_on, from linear_pmap.le_Sup _ hfc,
    convert â† hcs hfc âŸ¨x, hfxâŸ© hxs,
    apply this.2, refl }
end

end riesz_extension

/-- M. **Riesz extension theorem**: given a convex cone `s` in a vector space `E`, a submodule `p`,
and a linear `f : p â†’ â„`, assume that `f` is nonnegative on `p âˆ© s` and `p + s = E`. Then
there exists a globally defined linear function `g : E â†’ â„` that agrees with `f` on `p`,
and is nonnegative on `s`. -/
theorem riesz_extension (s : convex_cone â„ E) (f : E â†’â‚—.[â„] â„)
  (nonneg : âˆ€ x : f.domain, (x : E) âˆˆ s â†’ 0 â‰¤ f x) (dense : âˆ€ y, âˆƒ x : f.domain, (x : E) + y âˆˆ s) :
  âˆƒ g : E â†’â‚—[â„] â„, (âˆ€ x : f.domain, g x = f x) âˆ§ (âˆ€ x âˆˆ s, 0 â‰¤ g x) :=
begin
  rcases riesz_extension.exists_top s f nonneg dense with âŸ¨âŸ¨g_dom, gâŸ©, âŸ¨hpg, hfgâŸ©, htop, hgsâŸ©,
  clear hpg,
  refine âŸ¨g âˆ˜â‚— â†‘(linear_equiv.of_top _ htop).symm, _, _âŸ©;
    simp only [comp_apply, linear_equiv.coe_coe, linear_equiv.of_top_symm_apply],
  { exact Î» x, (hfg (submodule.coe_mk _ _).symm).symm },
  { exact Î» x hx, hgs âŸ¨x, _âŸ© hx }
end

/-- **Hahn-Banach theorem**: if `N : E â†’ â„` is a sublinear map, `f` is a linear map
defined on a subspace of `E`, and `f x â‰¤ N x` for all `x` in the domain of `f`,
then `f` can be extended to the whole space to a linear map `g` such that `g x â‰¤ N x`
for all `x`. -/
theorem exists_extension_of_le_sublinear (f : E â†’â‚—.[â„] â„) (N : E â†’ â„)
  (N_hom : âˆ€ (c : â„), 0 < c â†’ âˆ€ x, N (c â€¢ x) = c * N x)
  (N_add : âˆ€ x y, N (x + y) â‰¤ N x + N y)
  (hf : âˆ€ x : f.domain, f x â‰¤ N x) :
  âˆƒ g : E â†’â‚—[â„] â„, (âˆ€ x : f.domain, g x = f x) âˆ§ (âˆ€ x, g x â‰¤ N x) :=
begin
  let s : convex_cone â„ (E Ã— â„) :=
  { carrier := {p : E Ã— â„ | N p.1 â‰¤ p.2 },
    smul_mem' := Î» c hc p hp,
      calc N (c â€¢ p.1) = c * N p.1 : N_hom c hc p.1
      ... â‰¤ c * p.2 : mul_le_mul_of_nonneg_left hp hc.le,
    add_mem' := Î» x hx y hy, (N_add _ _).trans (add_le_add hx hy) },
  obtain âŸ¨g, g_eq, g_nonnegâŸ© :=
    riesz_extension s ((-f).coprod (linear_map.id.to_pmap âŠ¤)) _ _;
    try { simp only [linear_pmap.coprod_apply, to_pmap_apply, id_apply,
            linear_pmap.neg_apply, â† sub_eq_neg_add, sub_nonneg, subtype.coe_mk] at * },
  replace g_eq : âˆ€ (x : f.domain) (y : â„), g (x, y) = y - f x,
  { intros x y,
    simpa only [subtype.coe_mk, subtype.coe_eta] using g_eq âŸ¨(x, y), âŸ¨x.2, trivialâŸ©âŸ© },
  { refine âŸ¨-g.comp (inl â„ E â„), _, _âŸ©; simp only [neg_apply, inl_apply, comp_apply],
    { intro x, simp [g_eq x 0] },
    { intro x,
      have A : (x, N x) = (x, 0) + (0, N x), by simp,
      have B := g_nonneg âŸ¨x, N xâŸ© (le_refl (N x)),
      rw [A, map_add, â† neg_le_iff_add_nonneg'] at B,
      have C := g_eq 0 (N x),
      simp only [submodule.coe_zero, f.map_zero, sub_zero] at C,
      rwa â† C } },
  { exact Î» x hx, le_trans (hf _) hx },
  { rintros âŸ¨x, yâŸ©,
    refine âŸ¨âŸ¨(0, N x - y), âŸ¨f.domain.zero_mem, trivialâŸ©âŸ©, _âŸ©,
    simp only [convex_cone.mem_mk, mem_set_of_eq, subtype.coe_mk, prod.fst_add, prod.snd_add,
      zero_add, sub_add_cancel] }
end

/-! ### The dual cone -/

section dual
variables {H : Type*} [inner_product_space â„ H] (s t : set H)
open_locale real_inner_product_space

/-- The dual cone is the cone consisting of all points `y` such that for
all points `x` in a given set `0 â‰¤ âŸª x, y âŸ«`. -/
def set.inner_dual_cone (s : set H) : convex_cone â„ H :=
{ carrier := { y | âˆ€ x âˆˆ s, 0 â‰¤ âŸª x, y âŸ« },
  smul_mem' := Î» c hc y hy x hx,
  begin
    rw real_inner_smul_right,
    exact mul_nonneg hc.le (hy x hx)
  end,
  add_mem' := Î» u hu v hv x hx,
  begin
    rw inner_add_right,
    exact add_nonneg (hu x hx) (hv x hx)
  end }

@[simp] lemma mem_inner_dual_cone (y : H) (s : set H) :
  y âˆˆ s.inner_dual_cone â†” âˆ€ x âˆˆ s, 0 â‰¤ âŸª x, y âŸ« := iff.rfl

@[simp] lemma inner_dual_cone_empty : (âˆ… : set H).inner_dual_cone = âŠ¤ :=
eq_top_iff.mpr $ Î» x hy y, false.elim

/-- Dual cone of the convex cone {0} is the total space. -/
@[simp] lemma inner_dual_cone_zero : (0 : set H).inner_dual_cone = âŠ¤ :=
eq_top_iff.mpr $ Î» x hy y (hy : y = 0), hy.symm â–¸ inner_zero_left.ge

/-- Dual cone of the total space is the convex cone {0}. -/
@[simp] lemma inner_dual_cone_univ : (univ : set H).inner_dual_cone = 0 :=
begin
  suffices : âˆ€ x : H, x âˆˆ (univ : set H).inner_dual_cone â†’ x = 0,
  { apply set_like.coe_injective,
    exact eq_singleton_iff_unique_mem.mpr âŸ¨Î» x hx, inner_zero_right.ge, thisâŸ© },
  exact Î» x hx, by simpa [â†real_inner_self_nonpos] using hx (-x) (mem_univ _),
end

lemma inner_dual_cone_le_inner_dual_cone (h : t âŠ† s) :
  s.inner_dual_cone â‰¤ t.inner_dual_cone :=
Î» y hy x hx, hy x (h hx)

lemma pointed_inner_dual_cone : s.inner_dual_cone.pointed :=
Î» x hx, by rw inner_zero_right

/-- The inner dual cone of a singleton is given by the preimage of the positive cone under the
linear map `Î» y, âŸªx, yâŸ«`. -/
lemma inner_dual_cone_singleton (x : H) :
  ({x} : set H).inner_dual_cone = (convex_cone.positive â„ â„).comap (innerâ‚›â‚— x) :=
convex_cone.ext $ Î» i, forall_eq

lemma inner_dual_cone_union (s t : set H) :
  (s âˆª t).inner_dual_cone = s.inner_dual_cone âŠ“ t.inner_dual_cone :=
le_antisymm
  (le_inf (Î» x hx y hy, hx _ $ or.inl hy) (Î» x hx y hy, hx _ $ or.inr hy))
  (Î» x hx y, or.rec (hx.1 _) (hx.2 _))

lemma inner_dual_cone_insert (x : H) (s : set H) :
  (insert x s).inner_dual_cone = set.inner_dual_cone {x} âŠ“ s.inner_dual_cone :=
by rw [insert_eq, inner_dual_cone_union]

lemma inner_dual_cone_Union {Î¹ : Sort*} (f : Î¹ â†’ set H) :
  (â‹ƒ i, f i).inner_dual_cone = â¨… i, (f i).inner_dual_cone :=
begin
  refine le_antisymm (le_infi $ Î» i x hx y hy, hx _ $ mem_Union_of_mem _ hy) _,
  intros x hx y hy,
  rw [convex_cone.mem_infi] at hx,
  obtain âŸ¨j, hjâŸ© := mem_Union.mp hy,
  exact hx _ _ hj,
end

lemma inner_dual_cone_sUnion (S : set (set H)) :
  (â‹ƒâ‚€ S).inner_dual_cone = Inf (set.inner_dual_cone '' S) :=
by simp_rw [Inf_image, sUnion_eq_bUnion, inner_dual_cone_Union]

/-- The dual cone of `s` equals the intersection of dual cones of the points in `s`. -/
lemma inner_dual_cone_eq_Inter_inner_dual_cone_singleton :
  (s.inner_dual_cone : set H) = â‹‚ i : s, (({i} : set H).inner_dual_cone : set H) :=
by rw [â†convex_cone.coe_infi, â†inner_dual_cone_Union, Union_of_singleton_coe]

lemma is_closed_inner_dual_cone : is_closed (s.inner_dual_cone : set H) :=
begin
  -- reduce the problem to showing that dual cone of a singleton `{x}` is closed
  rw inner_dual_cone_eq_Inter_inner_dual_cone_singleton,
  apply is_closed_Inter,
  intros x,

  -- the dual cone of a singleton `{x}` is the preimage of `[0, âˆ)` under `inner x`
  have h : â†‘({x} : set H).inner_dual_cone = (inner x : H â†’ â„) â»Â¹' set.Ici 0,
  { rw [inner_dual_cone_singleton, convex_cone.coe_comap, convex_cone.coe_positive,
      innerâ‚›â‚—_apply_coe] },

  -- the preimage is closed as `inner x` is continuous and `[0, âˆ)` is closed
  rw h,
  exact is_closed_Ici.preimage (by continuity),
end

lemma convex_cone.pointed_of_nonempty_of_is_closed (K : convex_cone â„ H)
  (ne : (K : set H).nonempty) (hc : is_closed (K : set H)) : K.pointed :=
begin
  obtain âŸ¨x, hxâŸ© := ne,
  let f : â„ â†’ H := (â€¢ x),

  -- f (0, âˆ) is a subset of K
  have fI : f '' set.Ioi 0 âŠ† (K : set H),
  { rintro _ âŸ¨_, h, rflâŸ©,
    exact K.smul_mem (set.mem_Ioi.1 h) hx },

  -- closure of f (0, âˆ) is a subset of K
  have clf : closure (f '' set.Ioi 0) âŠ† (K : set H) := hc.closure_subset_iff.2 fI,

  -- f is continuous at 0 from the right
  have fc : continuous_within_at f (set.Ioi (0 : â„)) 0 :=
    (continuous_id.smul continuous_const).continuous_within_at,

  -- 0 belongs to the closure of the f (0, âˆ)
  have memâ‚€ := fc.mem_closure_image (by rw [closure_Ioi (0 : â„), mem_Ici]),

  -- as 0 âˆˆ closure f (0, âˆ) and closure f (0, âˆ) âŠ† K, 0 âˆˆ K.
  have fâ‚€ : f 0 = 0 := zero_smul â„ x,
  simpa only [fâ‚€, convex_cone.pointed, â† set_like.mem_coe] using mem_of_subset_of_mem clf memâ‚€,
end

section complete_space
variables [complete_space H]

/-- This is a stronger version of the Hahn-Banach separation theorem for closed convex cones. This
is also the geometric interpretation of Farkas' lemma. -/
theorem convex_cone.hyperplane_separation_of_nonempty_of_is_closed_of_nmem (K : convex_cone â„ H)
  (ne : (K : set H).nonempty) (hc : is_closed (K : set H)) {b : H} (disj : b âˆ‰ K) :
  âˆƒ (y : H), (âˆ€ x : H, x âˆˆ K â†’ 0 â‰¤ âŸªx, yâŸ«_â„) âˆ§ âŸªy, bâŸ«_â„ < 0 :=
begin
  -- let `z` be the point in `K` closest to `b`
  obtain âŸ¨z, hzK, infiâŸ© := exists_norm_eq_infi_of_complete_convex ne hc.is_complete K.convex b,

  -- for any `w` in `K`, we have `âŸªb - z, w - zâŸ«_â„ â‰¤ 0`
  have hinner := (norm_eq_infi_iff_real_inner_le_zero K.convex hzK).1 infi,

  -- set `y := z - b`
  use z - b,

  split,
  { -- the rest of the proof is a straightforward calculation
    rintros x hxK,
    specialize hinner _ (K.add_mem hxK hzK),
    rwa [add_sub_cancel, real_inner_comm, â† neg_nonneg, neg_eq_neg_one_mul,
         â† real_inner_smul_right, neg_smul, one_smul, neg_sub] at hinner },
  { -- as `K` is closed and non-empty, it is pointed
    have hinnerâ‚€ := hinner 0 (K.pointed_of_nonempty_of_is_closed ne hc),

    -- the rest of the proof is a straightforward calculation
    rw [zero_sub, inner_neg_right, right.neg_nonpos_iff] at hinnerâ‚€,
    have hbz : b - z â‰  0 := by { rw sub_ne_zero, contrapose! hzK, rwa â† hzK },
    rw [â† neg_zero, lt_neg, â† neg_one_mul, â† real_inner_smul_left, smul_sub, neg_smul, one_smul,
      neg_smul, neg_sub_neg, one_smul],
    calc 0 < âŸªb - z, b - zâŸ«_â„ : lt_of_not_le ((iff.not real_inner_self_nonpos).2 hbz)
    ... = âŸªb - z, b - zâŸ«_â„ + 0 : (add_zero _).symm
    ... â‰¤ âŸªb - z, b - zâŸ«_â„ + âŸªb - z, zâŸ«_â„ : add_le_add rfl.ge hinnerâ‚€
    ... = âŸªb - z, b - z + zâŸ«_â„ : inner_add_right.symm
    ... = âŸªb - z, bâŸ«_â„ : by rw sub_add_cancel },
end

/-- The inner dual of inner dual of a non-empty, closed convex cone is itself.  -/
theorem convex_cone.inner_dual_cone_of_inner_dual_cone_eq_self (K : convex_cone â„ H)
  (ne : (K : set H).nonempty) (hc : is_closed (K : set H)) :
  ((K : set H).inner_dual_cone : set H).inner_dual_cone = K :=
begin
  ext x,
  split,
  { rw [mem_inner_dual_cone, â† set_like.mem_coe],
    contrapose!,
    exact K.hyperplane_separation_of_nonempty_of_is_closed_of_nmem ne hc },
  { rintro hxK y h,
    specialize h x hxK,
    rwa real_inner_comm },
end

end complete_space
end dual
