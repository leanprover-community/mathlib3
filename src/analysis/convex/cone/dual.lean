/-
Copyright (c) 2021 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import analysis.convex.cone.basic
import analysis.inner_product_space.projection

/-!
# Convex cones in inner product spaces

We define `set.inner_dual_cone` to be the cone consisting of all points `y` such that for
all points `x` in a given set `0 ‚â§ ‚ü™ x, y ‚ü´`.

## Main statements

We prove the following theorems:
* `convex_cone.inner_dual_cone_of_inner_dual_cone_eq_self`:
  The `inner_dual_cone` of the `inner_dual_cone` of a nonempty, closed, convex cone is itself.

-/


open set linear_map
open_locale classical pointwise

variables {ùïú E F G : Type*}


/-! ### The dual cone -/

section dual
variables {H : Type*} [normed_add_comm_group H] [inner_product_space ‚Ñù H] (s t : set H)
open_locale real_inner_product_space

/-- The dual cone is the cone consisting of all points `y` such that for
all points `x` in a given set `0 ‚â§ ‚ü™ x, y ‚ü´`. -/
def set.inner_dual_cone (s : set H) : convex_cone ‚Ñù H :=
{ carrier := { y | ‚àÄ x ‚àà s, 0 ‚â§ ‚ü™ x, y ‚ü´ },
  smul_mem' := Œª c hc y hy x hx,
  begin
    rw real_inner_smul_right,
    exact mul_nonneg hc.le (hy x hx)
  end,
  add_mem' := Œª u hu v hv x hx,
  begin
    rw inner_add_right,
    exact add_nonneg (hu x hx) (hv x hx)
  end }

@[simp] lemma mem_inner_dual_cone (y : H) (s : set H) :
  y ‚àà s.inner_dual_cone ‚Üî ‚àÄ x ‚àà s, 0 ‚â§ ‚ü™ x, y ‚ü´ := iff.rfl

@[simp] lemma inner_dual_cone_empty : (‚àÖ : set H).inner_dual_cone = ‚ä§ :=
eq_top_iff.mpr $ Œª x hy y, false.elim

/-- Dual cone of the convex cone {0} is the total space. -/
@[simp] lemma inner_dual_cone_zero : (0 : set H).inner_dual_cone = ‚ä§ :=
eq_top_iff.mpr $ Œª x hy y (hy : y = 0), hy.symm ‚ñ∏ (inner_zero_left _).ge

/-- Dual cone of the total space is the convex cone {0}. -/
@[simp] lemma inner_dual_cone_univ : (univ : set H).inner_dual_cone = 0 :=
begin
  suffices : ‚àÄ x : H, x ‚àà (univ : set H).inner_dual_cone ‚Üí x = 0,
  { apply set_like.coe_injective,
    exact eq_singleton_iff_unique_mem.mpr ‚ü®Œª x hx, (inner_zero_right _).ge, this‚ü© },
  exact Œª x hx, by simpa [‚Üêreal_inner_self_nonpos] using hx (-x) (mem_univ _),
end

lemma inner_dual_cone_le_inner_dual_cone (h : t ‚äÜ s) :
  s.inner_dual_cone ‚â§ t.inner_dual_cone :=
Œª y hy x hx, hy x (h hx)

lemma pointed_inner_dual_cone : s.inner_dual_cone.pointed :=
Œª x hx, by rw inner_zero_right

/-- The inner dual cone of a singleton is given by the preimage of the positive cone under the
linear map `Œª y, ‚ü™x, y‚ü´`. -/
lemma inner_dual_cone_singleton (x : H) :
  ({x} : set H).inner_dual_cone = (convex_cone.positive ‚Ñù ‚Ñù).comap (inner‚Çõ‚Çó ‚Ñù x) :=
convex_cone.ext $ Œª i, forall_eq

lemma inner_dual_cone_union (s t : set H) :
  (s ‚à™ t).inner_dual_cone = s.inner_dual_cone ‚äì t.inner_dual_cone :=
le_antisymm
  (le_inf (Œª x hx y hy, hx _ $ or.inl hy) (Œª x hx y hy, hx _ $ or.inr hy))
  (Œª x hx y, or.rec (hx.1 _) (hx.2 _))

lemma inner_dual_cone_insert (x : H) (s : set H) :
  (insert x s).inner_dual_cone = set.inner_dual_cone {x} ‚äì s.inner_dual_cone :=
by rw [insert_eq, inner_dual_cone_union]

lemma inner_dual_cone_Union {Œπ : Sort*} (f : Œπ ‚Üí set H) :
  (‚ãÉ i, f i).inner_dual_cone = ‚®Ö i, (f i).inner_dual_cone :=
begin
  refine le_antisymm (le_infi $ Œª i x hx y hy, hx _ $ mem_Union_of_mem _ hy) _,
  intros x hx y hy,
  rw [convex_cone.mem_infi] at hx,
  obtain ‚ü®j, hj‚ü© := mem_Union.mp hy,
  exact hx _ _ hj,
end

lemma inner_dual_cone_sUnion (S : set (set H)) :
  (‚ãÉ‚ÇÄ S).inner_dual_cone = Inf (set.inner_dual_cone '' S) :=
by simp_rw [Inf_image, sUnion_eq_bUnion, inner_dual_cone_Union]

/-- The dual cone of `s` equals the intersection of dual cones of the points in `s`. -/
lemma inner_dual_cone_eq_Inter_inner_dual_cone_singleton :
  (s.inner_dual_cone : set H) = ‚ãÇ i : s, (({i} : set H).inner_dual_cone : set H) :=
by rw [‚Üêconvex_cone.coe_infi, ‚Üêinner_dual_cone_Union, Union_of_singleton_coe]

lemma is_closed_inner_dual_cone : is_closed (s.inner_dual_cone : set H) :=
begin
  -- reduce the problem to showing that dual cone of a singleton `{x}` is closed
  rw inner_dual_cone_eq_Inter_inner_dual_cone_singleton,
  apply is_closed_Inter,
  intros x,

  -- the dual cone of a singleton `{x}` is the preimage of `[0, ‚àû)` under `inner x`
  have h : ‚Üë({x} : set H).inner_dual_cone = (inner x : H ‚Üí ‚Ñù) ‚Åª¬π' set.Ici 0,
  { rw [inner_dual_cone_singleton, convex_cone.coe_comap, convex_cone.coe_positive,
      inner‚Çõ‚Çó_apply_coe] },

  -- the preimage is closed as `inner x` is continuous and `[0, ‚àû)` is closed
  rw h,
  exact is_closed_Ici.preimage (by continuity),
end

lemma convex_cone.pointed_of_nonempty_of_is_closed (K : convex_cone ‚Ñù H)
  (ne : (K : set H).nonempty) (hc : is_closed (K : set H)) : K.pointed :=
begin
  obtain ‚ü®x, hx‚ü© := ne,
  let f : ‚Ñù ‚Üí H := (‚Ä¢ x),

  -- f (0, ‚àû) is a subset of K
  have fI : f '' set.Ioi 0 ‚äÜ (K : set H),
  { rintro _ ‚ü®_, h, rfl‚ü©,
    exact K.smul_mem (set.mem_Ioi.1 h) hx },

  -- closure of f (0, ‚àû) is a subset of K
  have clf : closure (f '' set.Ioi 0) ‚äÜ (K : set H) := hc.closure_subset_iff.2 fI,

  -- f is continuous at 0 from the right
  have fc : continuous_within_at f (set.Ioi (0 : ‚Ñù)) 0 :=
    (continuous_id.smul continuous_const).continuous_within_at,

  -- 0 belongs to the closure of the f (0, ‚àû)
  have mem‚ÇÄ := fc.mem_closure_image (by rw [closure_Ioi (0 : ‚Ñù), mem_Ici]),

  -- as 0 ‚àà closure f (0, ‚àû) and closure f (0, ‚àû) ‚äÜ K, 0 ‚àà K.
  have f‚ÇÄ : f 0 = 0 := zero_smul ‚Ñù x,
  simpa only [f‚ÇÄ, convex_cone.pointed, ‚Üê set_like.mem_coe] using mem_of_subset_of_mem clf mem‚ÇÄ,
end

section complete_space
variables [complete_space H]

/-- This is a stronger version of the Hahn-Banach separation theorem for closed convex cones. This
is also the geometric interpretation of Farkas' lemma. -/
theorem convex_cone.hyperplane_separation_of_nonempty_of_is_closed_of_nmem (K : convex_cone ‚Ñù H)
  (ne : (K : set H).nonempty) (hc : is_closed (K : set H)) {b : H} (disj : b ‚àâ K) :
  ‚àÉ (y : H), (‚àÄ x : H, x ‚àà K ‚Üí 0 ‚â§ ‚ü™x, y‚ü´_‚Ñù) ‚àß ‚ü™y, b‚ü´_‚Ñù < 0 :=
begin
  -- let `z` be the point in `K` closest to `b`
  obtain ‚ü®z, hzK, infi‚ü© := exists_norm_eq_infi_of_complete_convex ne hc.is_complete K.convex b,

  -- for any `w` in `K`, we have `‚ü™b - z, w - z‚ü´_‚Ñù ‚â§ 0`
  have hinner := (norm_eq_infi_iff_real_inner_le_zero K.convex hzK).1 infi,

  -- set `y := z - b`
  use z - b,

  split,
  { -- the rest of the proof is a straightforward calculation
    rintros x hxK,
    specialize hinner _ (K.add_mem hxK hzK),
    rwa [add_sub_cancel, real_inner_comm, ‚Üê neg_nonneg, neg_eq_neg_one_mul,
         ‚Üê real_inner_smul_right, neg_smul, one_smul, neg_sub] at hinner },
  { -- as `K` is closed and non-empty, it is pointed
    have hinner‚ÇÄ := hinner 0 (K.pointed_of_nonempty_of_is_closed ne hc),

    -- the rest of the proof is a straightforward calculation
    rw [zero_sub, inner_neg_right, right.neg_nonpos_iff] at hinner‚ÇÄ,
    have hbz : b - z ‚â† 0 := by { rw sub_ne_zero, contrapose! hzK, rwa ‚Üê hzK },
    rw [‚Üê neg_zero, lt_neg, ‚Üê neg_one_mul, ‚Üê real_inner_smul_left, smul_sub, neg_smul, one_smul,
      neg_smul, neg_sub_neg, one_smul],
    calc 0 < ‚ü™b - z, b - z‚ü´_‚Ñù : lt_of_not_le ((iff.not real_inner_self_nonpos).2 hbz)
    ... = ‚ü™b - z, b - z‚ü´_‚Ñù + 0 : (add_zero _).symm
    ... ‚â§ ‚ü™b - z, b - z‚ü´_‚Ñù + ‚ü™b - z, z‚ü´_‚Ñù : add_le_add rfl.ge hinner‚ÇÄ
    ... = ‚ü™b - z, b - z + z‚ü´_‚Ñù : (inner_add_right _ _ _).symm
    ... = ‚ü™b - z, b‚ü´_‚Ñù : by rw sub_add_cancel },
end

/-- The inner dual of inner dual of a non-empty, closed convex cone is itself.  -/
theorem convex_cone.inner_dual_cone_of_inner_dual_cone_eq_self (K : convex_cone ‚Ñù H)
  (ne : (K : set H).nonempty) (hc : is_closed (K : set H)) :
  ((K : set H).inner_dual_cone : set H).inner_dual_cone = K :=
begin
  ext x,
  split,
  { rw [mem_inner_dual_cone, ‚Üê set_like.mem_coe],
    contrapose!,
    exact K.hyperplane_separation_of_nonempty_of_is_closed_of_nmem ne hc },
  { rintro hxK y h,
    specialize h x hxK,
    rwa real_inner_comm },
end

end complete_space
end dual
