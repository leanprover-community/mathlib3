/-
Copyright (c) 2022 Apurva Nakade All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Apurva Nakade
-/
import analysis.inner_product_space.adjoint

/-!

# Proper cones

We define a proper cone as a nonempty, closed, convex cone. Proper cones are used in defining conic
programs which generalize linear programs. A linear program is a conic program for the positive
cone. We then prove Farkas' lemma for conic programs following the proof in the reference below.
Farkas' lemma is equivalent to strong duality. So, once have the definitions of conic programs and
linear programs, the results from this file can be used to prove duality theorems.

## TODO

The next steps are:
- Prove the cone version of Farkas' lemma (2.3.4 in the reference).
- Define the positive cone as a proper cone.
- Define primal and dual cone programs and prove weak duality.
- Prove regular and strong duality for cone programs using Farkas' lemma (see reference).
- Define linear programs and prove LP duality as a special case of cone duality.
- Find a better reference (textbook instead of lecture notes).

## References

- [B. Gartner and J. Matousek, Cone Programming][gartnerMatousek]

-/

open continuous_linear_map filter

namespace convex_cone

variables {E : Type*} [add_comm_monoid E] [has_smul ‚Ñù E] [topological_space E]
  [has_continuous_const_smul ‚Ñù E] [has_continuous_add E]

/-- The closure of a convex cone inside a real inner product space is a convex cone. This
construction is mainly used for defining maps between proper cones. -/
protected def closure (K : convex_cone ‚Ñù E) : convex_cone ‚Ñù E :=
{ carrier := closure ‚ÜëK,
  smul_mem' :=
    Œª c hc _ h‚ÇÅ, map_mem_closure (continuous_id'.const_smul c) h‚ÇÅ (Œª _ h‚ÇÇ, K.smul_mem hc h‚ÇÇ),
  add_mem' := Œª _ h‚ÇÅ _ h‚ÇÇ, map_mem_closure‚ÇÇ continuous_add h‚ÇÅ h‚ÇÇ K.add_mem }

@[simp, norm_cast] lemma coe_closure (K : convex_cone ‚Ñù E) : (K.closure : set E) = closure K := rfl

@[simp] protected lemma mem_closure {K : convex_cone ‚Ñù E} {a : E} :
  a ‚àà K.closure ‚Üî a ‚àà closure (K : set E) := iff.rfl

lemma closure_eq_iff_is_closed {K : convex_cone ‚Ñù E} : K.closure = K ‚Üî is_closed (K : set E) :=
‚ü® (Œª h, by rw [‚Üê closure_eq_iff_is_closed, ‚Üê coe_closure, h]),
  (Œª h, set_like.coe_injective $ closure_eq_iff_is_closed.2 h) ‚ü©

end convex_cone

/-- A proper cone is a convex cone `K` that is nonempty and closed. Proper cones have the nice
property that the dual of the dual of a proper cone is itself. This makes them useful for defining
cone programs and proving duality theorems. -/
structure proper_cone (ùïú : Type*) (E : Type*)
  [is_R_or_C ùïú] [ordered_semiring ùïú] [normed_add_comm_group E] [inner_product_space ùïú E]
  extends convex_cone ùïú E :=
(nonempty'  : (carrier : set E).nonempty)
(is_closed' : is_closed (carrier : set E))

namespace proper_cone

section inner_product_space

variables {E : Type*} [normed_add_comm_group E] [inner_product_space ‚Ñù E]
variables {F : Type*} [normed_add_comm_group F] [inner_product_space ‚Ñù F]

noncomputable instance : has_coe (proper_cone ‚Ñù E) (convex_cone ‚Ñù E) := ‚ü®Œª K, K.1‚ü©

@[simp] lemma to_convex_cone_eq_coe (K : proper_cone ‚Ñù E) : K.to_convex_cone = K := rfl

noncomputable instance : has_zero (proper_cone ‚Ñù E) :=
‚ü® { to_convex_cone := (0 : convex_cone ‚Ñù E),
    nonempty' := ‚ü®0, rfl‚ü©,
    is_closed' := is_closed_singleton } ‚ü©

noncomputable instance : inhabited (proper_cone ‚Ñù E) := ‚ü®0‚ü©

lemma ext' : function.injective (coe : proper_cone ‚Ñù E ‚Üí convex_cone ‚Ñù E) :=
Œª S T h, by cases S; cases T; congr'

instance : set_like (proper_cone ‚Ñù E) E :=
{ coe := Œª K, K.carrier,
  coe_injective' := Œª _ _ h, proper_cone.ext' (set_like.coe_injective h) }

@[ext] lemma ext {S T : proper_cone ‚Ñù E} (h : ‚àÄ x, x ‚àà S ‚Üî x ‚àà T) : S = T := set_like.ext h

@[simp] lemma mem_coe {x : E} {K : proper_cone ‚Ñù E} : x ‚àà (K : convex_cone ‚Ñù E) ‚Üî x ‚àà K := iff.rfl

protected lemma nonempty (K : proper_cone ‚Ñù E) : (K : set E).nonempty := K.nonempty'

protected lemma is_closed (K : proper_cone ‚Ñù E) : is_closed (K : set E) := K.is_closed'

protected lemma pointed (K : proper_cone ‚Ñù E) : (K : convex_cone ‚Ñù E).pointed :=
(K : convex_cone ‚Ñù E).pointed_of_nonempty_of_is_closed K.nonempty K.is_closed

-- TODO: Replace map with a bundled version: proper_cone ‚Ñù E ‚ÜíL[‚Ñù] proper_cone ‚Ñù F
/-- The closure of image of a proper cone under a continuous `‚Ñù`-linear map is a proper cone. We
use continuous maps here so that the adjoint of f is also a map between proper cones. -/
noncomputable def map (f : E ‚ÜíL[‚Ñù] F) (K : proper_cone ‚Ñù E) : proper_cone ‚Ñù F :=
{ to_convex_cone := convex_cone.closure (convex_cone.map (f : E ‚Üí‚Çó[‚Ñù] F) ‚ÜëK),
  nonempty' := ‚ü® 0, subset_closure $ set_like.mem_coe.2 $ convex_cone.mem_map.2
    ‚ü®0, K.pointed, map_zero _‚ü© ‚ü©,
  is_closed' := is_closed_closure }

@[simp, norm_cast] lemma coe_map (f : E ‚ÜíL[‚Ñù] F) (K : proper_cone ‚Ñù E) :
  ‚Üë(K.map f) = (convex_cone.map (f : E ‚Üí‚Çó[‚Ñù] F) ‚ÜëK).closure := rfl

@[simp] lemma mem_map {f : E ‚ÜíL[‚Ñù] F} {K : proper_cone ‚Ñù E} {y : F} :
  y ‚àà K.map f ‚Üî y ‚àà (convex_cone.map (f : E ‚Üí‚Çó[‚Ñù] F) ‚ÜëK).closure := iff.rfl

@[simp] lemma map_id (K : proper_cone ‚Ñù E) : K.map (continuous_linear_map.id ‚Ñù E) = K :=
proper_cone.ext' $ by simpa using convex_cone.closure_eq_iff_is_closed.2 K.is_closed

/-- The inner dual cone of a proper cone is a proper cone. -/
def dual (K : proper_cone ‚Ñù E): (proper_cone ‚Ñù E) :=
{ to_convex_cone := (K : set E).inner_dual_cone,
  nonempty' := ‚ü®0, pointed_inner_dual_cone _‚ü©,
  is_closed' := is_closed_inner_dual_cone _ }

@[simp, norm_cast]
lemma coe_dual (K : proper_cone ‚Ñù E) : ‚Üë(dual K) = (K : set E).inner_dual_cone := rfl

@[simp] lemma mem_dual {K : proper_cone ‚Ñù E} {y : E} :
  y ‚àà dual K ‚Üî ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà K ‚Üí 0 ‚â§ ‚ü™x, y‚ü´_‚Ñù :=
by {rw [‚Üê mem_coe, coe_dual, mem_inner_dual_cone _ _], refl}

-- TODO: add comap, adjoint

end inner_product_space

section complete_space

variables {E : Type*} [normed_add_comm_group E] [inner_product_space ‚Ñù E] [complete_space E]

/-- The dual of the dual of a proper cone is itself. -/
theorem dual_dual (K : proper_cone ‚Ñù E) : K.dual.dual = K := proper_cone.ext' $
  (K : convex_cone ‚Ñù E).inner_dual_cone_of_inner_dual_cone_eq_self K.nonempty K.is_closed

end complete_space

end proper_cone
