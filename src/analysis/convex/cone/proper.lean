/-
Copyright (c) 2022 Apurva Nakade All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Apurva Nakade
-/
import analysis.inner_product_space.adjoint

/-!

# Proper cones

We define a proper cone as a nonempty, closed, convex cone. Proper cones are used in defining conic
programs which generalize linear programs. A linear program is a conic program for the positive
cone. We then prove Farkas' lemma for conic programs following the proof in the reference below.
Farkas' lemma is equivalent to strong duality. So, once have the definitions of conic programs and
linear programs, the results from this file can be used to prove duality theorems.

## TODO

The next steps are:
- Add convex_cone_class that extends set_like and replace the below instance
- Replace map with a bundled version: proper_cone â„ E â†’L[â„] proper_cone â„ F
- Define the positive cone as a proper cone.
- Define primal and dual cone programs and prove weak duality.
- Prove regular and strong duality for cone programs using Farkas' lemma (see reference).
- Define linear programs and prove LP duality as a special case of cone duality.
- Find a better reference (textbook instead of lecture notes).
- Show submodules are (proper) cones.

## References

- [B. Gartner and J. Matousek, Cone Programming][gartnerMatousek]

-/

open continuous_linear_map filter set

namespace convex_cone

section has_smul

variables {ğ•œ : Type*} [ordered_semiring ğ•œ]
variables {E : Type*} [add_comm_monoid E] [topological_space E] [has_continuous_add E]
  [has_smul ğ•œ E] [has_continuous_const_smul ğ•œ E]

/-- The closure of a convex cone inside a topological space is a convex cone. This
construction is mainly used for defining maps between proper cones. -/
protected def closure (K : convex_cone ğ•œ E) : convex_cone ğ•œ E :=
{ carrier := closure â†‘K,
  smul_mem' :=
    Î» c hc _ hâ‚, map_mem_closure (continuous_id'.const_smul c) hâ‚ (Î» _ hâ‚‚, K.smul_mem hc hâ‚‚),
  add_mem' := Î» _ hâ‚ _ hâ‚‚, map_mem_closureâ‚‚ continuous_add hâ‚ hâ‚‚ K.add_mem }

@[simp, norm_cast] lemma coe_closure (K : convex_cone ğ•œ E) : (K.closure : set E) = closure K := rfl

@[simp] protected lemma mem_closure {K : convex_cone ğ•œ E} {a : E} :
  a âˆˆ K.closure â†” a âˆˆ closure (K : set E) := iff.rfl

lemma closure_eq_iff_is_closed {K : convex_cone ğ•œ E} : K.closure = K â†” is_closed (K : set E) :=
âŸ¨ (Î» h, by rw [â† closure_eq_iff_is_closed, â† coe_closure, h]),
  (Î» h, set_like.coe_injective $ closure_eq_iff_is_closed.2 h) âŸ©

end has_smul

section complete_space

variables {E : Type*} [normed_add_comm_group E] [inner_product_space â„ E] [complete_space E]
variables {F : Type*} [normed_add_comm_group F] [inner_product_space â„ F] [complete_space F]

/-- The image of a convex cone under an adjoint is a convex cone. -/
protected def adjoint (f : E â†’L[â„] F) (S : convex_cone â„ F) : convex_cone â„ E :=
{ carrier := (adjoint f)''S,
  smul_mem' := Î» c hc _ âŸ¨y, h1, h2âŸ©,
    âŸ¨c â€¢ y, S.smul_mem hc h1, by rw [â† h2,continuous_linear_map.map_smul] âŸ©,
  add_mem' := Î» x âŸ¨a, ha1, ha2âŸ© y âŸ¨b, hb1, hb2âŸ©,
    âŸ¨a + b, S.add_mem ha1 hb1, by rw [â† ha2, â† hb2, continuous_linear_map.map_add ] âŸ© }

@[simp] lemma coe_adjoint (f : E â†’L[â„] F) (S : convex_cone â„ F) :
  (convex_cone.adjoint f S : set E) = (adjoint f)''S := rfl

@[simp] lemma mem_adjoint {f : E â†’L[â„] F} {S : convex_cone â„ F} {x : E} :
  x âˆˆ convex_cone.adjoint f S â†” x âˆˆ (adjoint f)''S := iff.rfl

@[simp] lemma adjoint_id (S : convex_cone â„ E) :
  convex_cone.adjoint (continuous_linear_map.id â„ E) S = S := set_like.coe_injective $ by simp

end complete_space

end convex_cone

/-- A proper cone is a convex cone `K` that is nonempty and closed. Proper cones have the nice
property that the dual of the dual of a proper cone is itself. This makes them useful for defining
cone programs and proving duality theorems. -/
structure proper_cone (ğ•œ : Type*) (E : Type*)
  [ordered_semiring ğ•œ] [add_comm_monoid E] [topological_space E] [has_smul ğ•œ E]
  extends convex_cone ğ•œ E :=
(nonempty'  : (carrier : set E).nonempty)
(is_closed' : is_closed (carrier : set E))

namespace proper_cone

section has_smul

variables {ğ•œ : Type*} [ordered_semiring ğ•œ]
variables {E : Type*} [add_comm_monoid E] [topological_space E] [has_smul ğ•œ E]

instance : has_coe (proper_cone ğ•œ E) (convex_cone ğ•œ E) := âŸ¨Î» K, K.1âŸ©

@[simp] lemma to_convex_cone_eq_coe (K : proper_cone ğ•œ E) : K.to_convex_cone = K := rfl

lemma ext' : function.injective (coe : proper_cone ğ•œ E â†’ convex_cone ğ•œ E) :=
Î» S T h, by cases S; cases T; congr'

-- TODO: add convex_cone_class that extends set_like and replace the below instance
instance : set_like (proper_cone ğ•œ E) E :=
{ coe := Î» K, K.carrier,
  coe_injective' := Î» _ _ h, proper_cone.ext' (set_like.coe_injective h) }

@[ext] lemma ext {S T : proper_cone ğ•œ E} (h : âˆ€ x, x âˆˆ S â†” x âˆˆ T) : S = T := set_like.ext h

@[simp] lemma mem_coe {x : E} {K : proper_cone ğ•œ E} : x âˆˆ (K : convex_cone ğ•œ E) â†” x âˆˆ K := iff.rfl

protected lemma nonempty (K : proper_cone ğ•œ E) : (K : set E).nonempty := K.nonempty'

protected lemma is_closed (K : proper_cone ğ•œ E) : is_closed (K : set E) := K.is_closed'

end has_smul

section module

variables {ğ•œ : Type*} [ordered_semiring ğ•œ]
variables {E : Type*} [add_comm_monoid E] [topological_space E] [t1_space E] [module ğ•œ E]

instance : has_zero (proper_cone ğ•œ E) :=
âŸ¨ { to_convex_cone := 0,
    nonempty' := âŸ¨0, rflâŸ©,
    is_closed' := is_closed_singleton } âŸ©

instance : inhabited (proper_cone ğ•œ E) := âŸ¨0âŸ©

end module

section inner_product_space

variables {E : Type*} [normed_add_comm_group E] [inner_product_space â„ E]
variables {F : Type*} [normed_add_comm_group F] [inner_product_space â„ F]
variables {G : Type*} [normed_add_comm_group G] [inner_product_space â„ G]

protected lemma pointed (K : proper_cone â„ E) : (K : convex_cone â„ E).pointed :=
(K : convex_cone â„ E).pointed_of_nonempty_of_is_closed K.nonempty K.is_closed

-- TODO: Replace map with a bundled version: proper_cone â„ E â†’L[â„] proper_cone â„ F
/-- The closure of image of a proper cone under a continuous `â„`-linear map is a proper cone. We
use continuous maps here so that the comap of f is also a map between proper cones. -/
noncomputable def map (f : E â†’L[â„] F) (K : proper_cone â„ E) : proper_cone â„ F :=
{ to_convex_cone := convex_cone.closure (convex_cone.map (f : E â†’â‚—[â„] F) â†‘K),
  nonempty' := âŸ¨ 0, subset_closure $ set_like.mem_coe.2 $ convex_cone.mem_map.2
    âŸ¨0, K.pointed, map_zero _âŸ© âŸ©,
  is_closed' := is_closed_closure }

@[simp, norm_cast] lemma coe_map (f : E â†’L[â„] F) (K : proper_cone â„ E) :
  â†‘(K.map f) = (convex_cone.map (f : E â†’â‚—[â„] F) â†‘K).closure := rfl

@[simp] lemma mem_map {f : E â†’L[â„] F} {K : proper_cone â„ E} {y : F} :
  y âˆˆ K.map f â†” y âˆˆ (convex_cone.map (f : E â†’â‚—[â„] F) â†‘K).closure := iff.rfl

@[simp] lemma map_id (K : proper_cone â„ E) : K.map (continuous_linear_map.id â„ E) = K :=
proper_cone.ext' $ by simpa using convex_cone.closure_eq_iff_is_closed.2 K.is_closed

/-- The inner dual cone of a proper cone is a proper cone. -/
def dual (K : proper_cone â„ E): (proper_cone â„ E) :=
{ to_convex_cone := (K : set E).inner_dual_cone,
  nonempty' := âŸ¨0, pointed_inner_dual_cone _âŸ©,
  is_closed' := is_closed_inner_dual_cone _ }

@[simp, norm_cast]
lemma coe_dual (K : proper_cone â„ E) : â†‘(dual K) = (K : set E).inner_dual_cone := rfl

@[simp] lemma mem_dual {K : proper_cone â„ E} {y : E} :
  y âˆˆ dual K â†” âˆ€ â¦ƒxâ¦„, x âˆˆ K â†’ 0 â‰¤ âŸªx, yâŸ«_â„ :=
by {rw [â† mem_coe, coe_dual, mem_inner_dual_cone _ _], refl}

/-- The preimage of a convex cone under a continuous `â„`-linear map is a convex cone. -/
noncomputable def comap (f : E â†’L[â„] F) (S : proper_cone â„ F) : proper_cone â„ E :=
{ to_convex_cone := convex_cone.comap (f : E â†’â‚—[â„] F) S,
  nonempty' := âŸ¨ 0,
  begin
    simp only [convex_cone.comap, mem_preimage, map_zero, set_like.mem_coe, mem_coe],
    apply proper_cone.pointed,
  end âŸ©,
  is_closed' :=
  begin
    simp only [convex_cone.comap, continuous_linear_map.coe_coe],
    apply is_closed.preimage f.2 S.is_closed,
  end }

@[simp] lemma coe_comap (f : E â†’L[â„] F) (S : proper_cone â„ F) : (S.comap f : set E) = f â»Â¹' S := rfl

@[simp] lemma comap_id (S : convex_cone â„ E) : S.comap linear_map.id = S :=
set_like.coe_injective preimage_id

lemma comap_comap (g : F â†’L[â„] G) (f : E â†’L[â„] F) (S : proper_cone â„ G) :
  (S.comap g).comap f = S.comap (g.comp f) :=
set_like.coe_injective $ preimage_comp.symm

@[simp] lemma mem_comap {f : E â†’L[â„] F} {S : proper_cone â„ F} {x : E} : x âˆˆ S.comap f â†” f x âˆˆ S :=
iff.rfl

end inner_product_space

section complete_space

variables {E : Type*} [normed_add_comm_group E] [inner_product_space â„ E] [complete_space E]
variables {F : Type*} [normed_add_comm_group F] [inner_product_space â„ F] [complete_space F]

/-- The dual of the dual of a proper cone is itself. -/
theorem dual_dual (K : proper_cone â„ E) : K.dual.dual = K := proper_cone.ext' $
  (K : convex_cone â„ E).inner_dual_cone_of_inner_dual_cone_eq_self K.nonempty K.is_closed

/-- This is a relative version of
`convex_cone.hyperplane_separation_of_nonempty_of_is_closed_of_nmem`. This reduces to the previous
theorem when `f` is the identity map. This is a geometric interpretation of the Farkas' lemma
(2.3.4 in the reference) stated using proper cones. -/
theorem hyperplane_separation (K : proper_cone â„ E) {f : E â†’L[â„] F} {b : F} :
  b âˆˆ K.map f â†” âˆ€ y : F, (adjoint f y) âˆˆ K.dual â†’ 0 â‰¤ âŸªy, bâŸ«_â„ := iff.intro
begin
  -- suppose `b âˆˆ K.map f`
  simp only [proper_cone.mem_map, proper_cone.mem_dual, adjoint_inner_right, convex_cone.mem_closure, mem_closure_iff_seq_limit],

  -- there is a sequence `seq : â„• â†’ F` in the image of `f` that converges to `b`
  rintros âŸ¨seq, hmem, htendsâŸ© y hinner,

  suffices h : âˆ€ n, 0 â‰¤ âŸªy, seq nâŸ«_â„, from ge_of_tendsto' (continuous.seq_continuous
    (continuous.inner (@continuous_const _ _ _ _ y) continuous_id) htends) h,

  intro n,
  obtain âŸ¨_, h, hseqâŸ© := hmem n,
  simpa only [â† hseq, real_inner_comm] using (hinner h),
end
begin
  -- proof by contradiction
  -- suppose `b âˆ‰ K.map f`
  intro h,
  contrapose! h,

  -- as `b âˆ‰ K.map f`, there is a hyperplane `y` separating `b` from `K.map f`
  obtain âŸ¨y, hxy, hybâŸ© := convex_cone.hyperplane_separation_of_nonempty_of_is_closed_of_nmem _
    (K.map f).nonempty (K.map f).is_closed h,

  -- the rest of the proof is a straightforward algebraic manipulation
  refine âŸ¨y, _, hybâŸ©,
  simp_rw [proper_cone.mem_dual, adjoint_inner_right],
  intros x hxK,
  apply hxy (f x),
  rw [to_convex_cone_eq_coe, proper_cone.coe_map],
  apply subset_closure,
  rw [set_like.mem_coe, convex_cone.mem_map],
  use âŸ¨x, hxK, rflâŸ©,
end

theorem hyperplane_separation_of_nmem (K : proper_cone â„ E) {f : E â†’L[â„] F} {b : F}
  (disj : b âˆ‰ K.map f) : âˆƒ y : F, (adjoint f y) âˆˆ K.dual âˆ§ âŸªy, bâŸ«_â„ < 0 :=
by { contrapose! disj, rwa K.hyperplane_separation }

end complete_space

end proper_cone
