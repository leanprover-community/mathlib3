/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import analysis.convex.extreme
import analysis.convex.function
import analysis.normed_space.ordered

/-!
# Exposed sets

This file defines exposed sets and exposed points for sets in a real vector space.

An exposed subset of `A` is a subset of `A` that is the set of all maximal points of a functional
(a continuous linear map `E â†’ ğ•œ`) over `A`. By convention, `âˆ…` is an exposed subset of all sets.
This allows for better functioriality of the definition (the intersection of two exposed subsets is
exposed, faces of a polytope form a bounded lattice).
This is an analytic notion of "being on the side of". It is stronger than being extreme (see
`is_exposed.is_extreme`), but weaker (for exposed points) than being a vertex.

An exposed set of `A` is sometimes called a "face of `A`", but we decided to reserve this
terminology to the more specific notion of a face of a polytope (sometimes hopefully soon out
on mathlib!).

## Main declarations

* `is_exposed ğ•œ A B`: States that `B` is an exposed set of `A` (in the literature, `A` is often
  implicit).
* `is_exposed.is_extreme`: An exposed set is also extreme.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011]

## TODO

Define intrinsic frontier/interior and prove the lemmas related to exposed sets and points.

Generalise to Locally Convex Topological Vector Spacesâ„¢

More not-yet-PRed stuff is available on the branch `sperner_again`.
-/

open_locale classical affine big_operators
open set

variables (ğ•œ : Type*) {E : Type*} [normed_linear_ordered_field ğ•œ] [normed_group E]
  [normed_space ğ•œ E] {l : E â†’L[ğ•œ] ğ•œ} {A B C : set E} {X : finset E} {x : E}

/-- A set `B` is exposed with respect to `A` iff it maximizes some functional over `A` (and contains
all points maximizing it). Written `is_exposed ğ•œ A B`. -/
def is_exposed (A B : set E) : Prop :=
B.nonempty â†’ âˆƒ l : E â†’L[ğ•œ] ğ•œ, B = {x âˆˆ A | âˆ€ y âˆˆ A, l y â‰¤ l x}

variables {ğ•œ}

/-- A useful way to build exposed sets from intersecting `A` with halfspaces (modelled by an
inequality with a functional). -/
def continuous_linear_map.to_exposed (l : E â†’L[ğ•œ] ğ•œ) (A : set E) : set E :=
{x âˆˆ A | âˆ€ y âˆˆ A, l y â‰¤ l x}

lemma continuous_linear_map.to_exposed.is_exposed : is_exposed ğ•œ A (l.to_exposed A) := Î» h, âŸ¨l, rflâŸ©

lemma is_exposed_empty : is_exposed ğ•œ A âˆ… :=
Î» âŸ¨x, hxâŸ©, by { exfalso, exact hx }

namespace is_exposed

protected lemma subset (hAB : is_exposed ğ•œ A B) : B âŠ† A :=
begin
  rintro x hx,
  obtain âŸ¨_, rflâŸ© := hAB âŸ¨x, hxâŸ©,
  exact hx.1,
end

@[refl] lemma refl (A : set E) : is_exposed ğ•œ A A :=
Î» âŸ¨w, hwâŸ©, âŸ¨0, subset.antisymm (Î» x hx, âŸ¨hx, Î» y hy, by exact le_refl 0âŸ©) (Î» x hx, hx.1)âŸ©

lemma antisymm (hB : is_exposed ğ•œ A B) (hA : is_exposed ğ•œ B A) :
  A = B :=
hA.subset.antisymm hB.subset

/- `is_exposed` is *not* transitive: Consider a (topologically) open cube with vertices
`Aâ‚€â‚€â‚€, ..., Aâ‚â‚â‚` and add to it the triangle `Aâ‚€â‚€â‚€Aâ‚€â‚€â‚Aâ‚€â‚â‚€`. Then `Aâ‚€â‚€â‚Aâ‚€â‚â‚€` is an exposed subset
of `Aâ‚€â‚€â‚€Aâ‚€â‚€â‚Aâ‚€â‚â‚€` which is an exposed subset of the cube, but `Aâ‚€â‚€â‚Aâ‚€â‚â‚€` is not itself an exposed
subset of the cube. -/

protected lemma mono (hC : is_exposed ğ•œ A C) (hBA : B âŠ† A) (hCB : C âŠ† B) :
  is_exposed ğ•œ B C :=
begin
  rintro âŸ¨w, hwâŸ©,
  obtain âŸ¨l, rflâŸ© := hC âŸ¨w, hwâŸ©,
  exact âŸ¨l, subset.antisymm (Î» x hx, âŸ¨hCB hx, Î» y hy, hx.2 y (hBA hy)âŸ©)
    (Î» x hx, âŸ¨hBA hx.1, Î» y hy, (hw.2 y hy).trans (hx.2 w (hCB hw))âŸ©)âŸ©,
end

/-- If `B` is an exposed subset of `A`, then `B` is the intersection of `A` with some closed
halfspace. The converse is *not* true. It would require that the corresponding open halfspace
doesn't intersect `A`. -/
lemma eq_inter_halfspace (hAB : is_exposed ğ•œ A B) :
  âˆƒ l : E â†’L[ğ•œ] ğ•œ, âˆƒ a, B = {x âˆˆ A | a â‰¤ l x} :=
begin
  obtain hB | hB := B.eq_empty_or_nonempty,
  { refine âŸ¨0, 1, _âŸ©,
    rw [hB, eq_comm, eq_empty_iff_forall_not_mem],
    rintro x âŸ¨-, hâŸ©,
    rw continuous_linear_map.zero_apply at h,
    linarith },
  obtain âŸ¨l, rflâŸ© := hAB hB,
  obtain âŸ¨w, hwâŸ© := hB,
  exact âŸ¨l, l w, subset.antisymm (Î» x hx, âŸ¨hx.1, hx.2 w hw.1âŸ©)
    (Î» x hx, âŸ¨hx.1, Î» y hy, (hw.2 y hy).trans hx.2âŸ©)âŸ©,
end

lemma inter (hB : is_exposed ğ•œ A B) (hC : is_exposed ğ•œ A C) :
  is_exposed ğ•œ A (B âˆ© C) :=
begin
  rintro âŸ¨w, hwB, hwCâŸ©,
  obtain âŸ¨lâ‚, rflâŸ© := hB âŸ¨w, hwBâŸ©,
  obtain âŸ¨lâ‚‚, rflâŸ© := hC âŸ¨w, hwCâŸ©,
  refine âŸ¨lâ‚ + lâ‚‚, subset.antisymm _ _âŸ©,
  { rintro x âŸ¨âŸ¨hxA, hxBâŸ©, âŸ¨-, hxCâŸ©âŸ©,
    exact âŸ¨hxA, Î» z hz, add_le_add (hxB z hz) (hxC z hz)âŸ© },
  rintro x âŸ¨hxA, hxâŸ©,
  refine âŸ¨âŸ¨hxA, Î» y hy, _âŸ©, hxA, Î» y hy, _âŸ©,
  { exact (add_le_add_iff_right (lâ‚‚ x)).1 ((add_le_add (hwB.2 y hy) (hwC.2 x hxA)).trans
      (hx w hwB.1)) },
  { exact (add_le_add_iff_left (lâ‚ x)).1 (le_trans (add_le_add (hwB.2 x hxA) (hwC.2 y hy))
    (hx w hwB.1)) }
end

lemma sInter {F : finset (set E)} (hF : F.nonempty)
  (hAF : âˆ€ B âˆˆ F, is_exposed ğ•œ A B) :
  is_exposed ğ•œ A (â‹‚â‚€ F) :=
begin
  revert hF F,
  refine finset.induction _ _,
  { rintro h,
    exfalso,
    exact empty_not_nonempty h },
  rintro C F _ hF _ hCF,
  rw [finset.coe_insert, sInter_insert],
  obtain rfl | hFnemp := F.eq_empty_or_nonempty,
  { rw [finset.coe_empty, sInter_empty, inter_univ],
    exact hCF C (finset.mem_singleton_self C) },
  exact (hCF C (finset.mem_insert_self C F)).inter (hF hFnemp (Î» B hB,
    hCF B(finset.mem_insert_of_mem hB))),
end

lemma inter_left (hC : is_exposed ğ•œ A C) (hCB : C âŠ† B) :
  is_exposed ğ•œ (A âˆ© B) C :=
begin
  rintro âŸ¨w, hwâŸ©,
  obtain âŸ¨l, rflâŸ© := hC âŸ¨w, hwâŸ©,
  exact âŸ¨l, subset.antisymm (Î» x hx, âŸ¨âŸ¨hx.1, hCB hxâŸ©, Î» y hy, hx.2 y hy.1âŸ©)
    (Î» x âŸ¨âŸ¨hxC, _âŸ©, hxâŸ©, âŸ¨hxC, Î» y hy, (hw.2 y hy).trans (hx w âŸ¨hC.subset hw, hCB hwâŸ©)âŸ©)âŸ©,
end

lemma inter_right (hC : is_exposed ğ•œ B C) (hCA : C âŠ† A) :
  is_exposed ğ•œ (A âˆ© B) C :=
begin
  rw inter_comm,
  exact hC.inter_left hCA,
end

protected lemma is_extreme [normed_space â„ E]  (hAB : is_exposed â„ A B) :
  is_extreme â„ A B :=
begin
  refine âŸ¨hAB.subset, Î» xâ‚ xâ‚‚ hxâ‚A hxâ‚‚A x hxB hx, _âŸ©,
  obtain âŸ¨l, rflâŸ© := hAB âŸ¨x, hxBâŸ©,
  have hl : convex_on univ l := l.to_linear_map.convex_on convex_univ,
  have hlxâ‚ := hxB.2 xâ‚ hxâ‚A,
  have hlxâ‚‚ := hxB.2 xâ‚‚ hxâ‚‚A,
  refine âŸ¨âŸ¨hxâ‚A, Î» y hy, _âŸ©, âŸ¨hxâ‚‚A, Î» y hy, _âŸ©âŸ©,
  { rw hlxâ‚.antisymm (hl.le_left_of_right_le (mem_univ _) (mem_univ _) hx hlxâ‚‚),
    exact hxB.2 y hy },
  { rw hlxâ‚‚.antisymm (hl.le_right_of_left_le (mem_univ _) (mem_univ _) hx hlxâ‚),
    exact hxB.2 y hy }
end

protected lemma convex [normed_space â„ E] (hAB : is_exposed â„ A B) (hA : convex â„ A) :
  convex â„ B :=
begin
  obtain rfl | hB := B.eq_empty_or_nonempty,
  { exact convex_empty },
  obtain âŸ¨l, rflâŸ© := hAB hB,
  exact Î» xâ‚ xâ‚‚ hxâ‚ hxâ‚‚ a b ha hb hab, âŸ¨hA hxâ‚.1 hxâ‚‚.1 ha hb hab, Î» y hy,
    ((l.to_linear_map.concave_on convex_univ).concave_le _
    âŸ¨mem_univ _, hxâ‚.2 y hyâŸ© âŸ¨mem_univ _, hxâ‚‚.2 y hyâŸ© ha hb hab).2âŸ©,
end

lemma is_closed [normed_space â„ E] (hAB : is_exposed â„ A B) (hA : is_closed A) :
  is_closed B :=
begin
  obtain âŸ¨l, a, rflâŸ© := hAB.eq_inter_halfspace,
  exact hA.is_closed_le continuous_on_const l.continuous.continuous_on,
end

lemma is_compact [normed_space â„ E] (hAB : is_exposed â„ A B) (hA : is_compact A) :
  is_compact B :=
compact_of_is_closed_subset hA (hAB.is_closed hA.is_closed) hAB.subset

end is_exposed

variables (ğ•œ)

/-- A point is exposed with respect to `A` iff there exists an hyperplane whose intersection with
`A` is exactly that point. -/
def set.exposed_points (A : set E) :
  set E :=
{x âˆˆ A | âˆƒ l : E â†’L[ğ•œ] ğ•œ, âˆ€ y âˆˆ A, l y â‰¤ l x âˆ§ (l x â‰¤ l y â†’ y = x)}

variables {ğ•œ}

lemma exposed_point_def :
  x âˆˆ A.exposed_points ğ•œ â†” x âˆˆ A âˆ§ âˆƒ l : E â†’L[ğ•œ] ğ•œ, âˆ€ y âˆˆ A, l y â‰¤ l x âˆ§ (l x â‰¤ l y â†’ y = x) :=
iff.rfl

lemma exposed_points_subset :
  A.exposed_points ğ•œ âŠ† A :=
Î» x hx, hx.1

@[simp] lemma exposed_points_empty :
  (âˆ… : set E).exposed_points ğ•œ = âˆ… :=
subset_empty_iff.1 exposed_points_subset

/-- Exposed points exactly correspond to exposed singletons. -/
lemma mem_exposed_points_iff_exposed_singleton :
  x âˆˆ A.exposed_points ğ•œ â†” is_exposed ğ•œ A {x} :=
begin
  use Î» âŸ¨hxA, l, hlâŸ© h, âŸ¨l, eq.symm $ eq_singleton_iff_unique_mem.2 âŸ¨âŸ¨hxA, Î» y hy, (hl y hy).1âŸ©,
    Î» z hz, (hl z hz.1).2 (hz.2 x hxA)âŸ©âŸ©,
  rintro h,
  obtain âŸ¨l, hlâŸ© := h âŸ¨x, mem_singleton _âŸ©,
  rw [eq_comm, eq_singleton_iff_unique_mem] at hl,
  exact âŸ¨hl.1.1, l, Î» y hy, âŸ¨hl.1.2 y hy, Î» hxy, hl.2 y âŸ¨hy, Î» z hz, (hl.1.2 z hz).trans hxyâŸ©âŸ©âŸ©,
end

lemma exposed_points_subset_extreme_points [normed_space â„ E] :
  A.exposed_points â„ âŠ† A.extreme_points â„ :=
Î» x hx, mem_extreme_points_iff_extreme_singleton.2
  (mem_exposed_points_iff_exposed_singleton.1 hx).is_extreme
