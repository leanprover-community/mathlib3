/-
Copyright (c) 2020 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/
import analysis.convex.basic
import topology.algebra.affine
import topology.local_extr

/-!
# Minima and maxima of convex functions

We show that if a function `f : E â†’ Î²` is convex, then a local minimum is also
a global minimum, and likewise for concave functions.
-/

variables {ğ•œ E Î² : Type*} [linear_ordered_semiring ğ•œ] [densely_ordered ğ•œ] [topological_space ğ•œ] [order_topology ğ•œ]
  [add_comm_group E]
  [topological_space E] [module ğ•œ E] [topological_add_group E] [has_continuous_smul ğ•œ E]
  [linear_ordered_add_comm_group Î²] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]
  {s : set E}

open set filter
open_locale classical

/--
Helper lemma for the more general case: `is_min_on.of_is_local_min_on_of_convex_on`.
-/
lemma is_min_on.of_is_local_min_on_of_convex_on_Icc {f : ğ•œ â†’ Î²} {a b : ğ•œ} (a_lt_b : a < b)
  (h_local_min : is_local_min_on f (Icc a b) a) (h_conv : convex_on ğ•œ (Icc a b) f) :
  âˆ€ x âˆˆ Icc a b, f a â‰¤ f x :=
begin
  by_contradiction H_cont,
  push_neg at H_cont,
  rcases H_cont with âŸ¨x, âŸ¨h_ax, h_xbâŸ©, fx_lt_faâŸ©,
  obtain âŸ¨z, hz, ge_on_nhdâŸ© : âˆƒ z > a, âˆ€ y âˆˆ (Icc a z), f y â‰¥ f a,
  { rcases eventually_iff_exists_mem.mp h_local_min with âŸ¨U, U_in_nhds_within, fy_ge_faâŸ©,
    rw [nhds_within_Icc_eq_nhds_within_Ici a_lt_b, mem_nhds_within_Ici_iff_exists_Icc_subset]
        at U_in_nhds_within,
    rcases U_in_nhds_within with âŸ¨Îµ, Îµ_in_Ioi, Ioc_in_UâŸ©,
    exact âŸ¨Îµ, mem_Ioi.mp Îµ_in_Ioi, Î» y y_in_Ioc, fy_ge_fa y $ Ioc_in_U y_in_IocâŸ© },
  have a_lt_x : a < x := lt_of_le_of_ne h_ax (Î» H, by subst H; exact lt_irrefl (f a) fx_lt_fa),
  have lt_on_nhd : âˆ€ y âˆˆ Ioc a x, f y < f a,
  { intros y y_in_Ioc,
    rcases (convex.mem_Ioc a_lt_x).mp y_in_Ioc with âŸ¨ya, yx, ya_pos, yx_pos, yax, y_comboâŸ©,
    calc
      f y = f (ya * a + yx * x)       : by rw [y_combo]
      ... â‰¤ ya â€¢ f a + yx â€¢ f x
                : h_conv.2 (left_mem_Icc.mpr (le_of_lt a_lt_b)) âŸ¨h_ax, h_xbâŸ© (ya_pos)
                    (le_of_lt yx_pos) yax
      ... < ya â€¢ f a + yx â€¢ f a       : add_lt_add_left (smul_lt_smul_of_pos fx_lt_fa yx_pos) _
      ... = f a                       : by rw [â†add_smul, yax, one_smul] },
  by_cases h_xz : x â‰¤ z,
  { exact not_lt_of_ge (ge_on_nhd x (show x âˆˆ Icc a z, by exact âŸ¨h_ax, h_xzâŸ©)) fx_lt_fa, },
  { have hâ‚ : z âˆˆ Ioc a x := âŸ¨hz, le_of_not_ge h_xzâŸ©,
    have hâ‚‚ : z âˆˆ Icc a z := âŸ¨le_of_lt hz, le_refl zâŸ©,
    exact not_lt_of_ge (ge_on_nhd z hâ‚‚) (lt_on_nhd z hâ‚) }
end

/--
A local minimum of a convex function is a global minimum, restricted to a set `s`.
-/
lemma is_min_on.of_is_local_min_on_of_convex_on {f : E â†’ Î²} {a : E}
  (a_in_s : a âˆˆ s) (h_localmin : is_local_min_on f s a) (h_conv : convex_on ğ•œ s f) :
  âˆ€ x âˆˆ s, f a â‰¤ f x :=
begin
  by_contradiction H_cont,
  push_neg at H_cont,
  rcases H_cont with âŸ¨x, âŸ¨x_in_s, fx_lt_faâŸ©âŸ©,
  let g : ğ•œ â†’áµƒ[ğ•œ] E := affine_map.line_map a x,
  have hg0 : g 0 = a := affine_map.line_map_apply_zero a x,
  have hg1 : g 1 = x := affine_map.line_map_apply_one a x,
  have fg_local_min_on : is_local_min_on (f âˆ˜ g) (g â»Â¹' s) 0,
  { rw â†hg0 at h_localmin,
    refine is_local_min_on.comp_continuous_on h_localmin subset.rfl
      (continuous.continuous_on (affine_map.line_map_continuous)) _,
    simp [mem_preimage, hg0, a_in_s] },
  have fg_min_on : âˆ€ x âˆˆ (Icc 0 1 : set ğ•œ), (f âˆ˜ g) 0 â‰¤ (f âˆ˜ g) x,
  { have Icc_in_s' : Icc 0 1 âŠ† (g â»Â¹' s),
    { have h0 : (0 : ğ•œ) âˆˆ (g â»Â¹' s) := by simp [mem_preimage, a_in_s],
      have h1 : (1 : ğ•œ) âˆˆ (g â»Â¹' s) := by simp [mem_preimage, hg1, x_in_s],
      rw â†segment_eq_Icc (show (0 : ğ•œ) â‰¤ 1, by linarith),
      exact (convex.affine_preimage g h_conv.1).segment_subset
        (by simp [mem_preimage, hg0, a_in_s]) (by simp [mem_preimage, hg1, x_in_s]) },
    have fg_local_min_on' : is_local_min_on (f âˆ˜ g) (Icc 0 1) 0 :=
      is_local_min_on.on_subset fg_local_min_on Icc_in_s',
    refine is_min_on.of_is_local_min_on_of_convex_on_Icc (by linarith) fg_local_min_on' _,
    exact (convex_on.comp_affine_map g h_conv).subset Icc_in_s' (convex_Icc 0 1) },
  have gx_lt_ga : (f âˆ˜ g) 1 < (f âˆ˜ g) 0 := by simp [hg1, fx_lt_fa, hg0],
  exact not_lt_of_ge (fg_min_on 1 (mem_Icc.mpr âŸ¨zero_le_one, le_refl 1âŸ©)) gx_lt_ga,
end

/-- A local maximum of a concave function is a global maximum, restricted to a set `s`. -/
lemma is_max_on.of_is_local_max_on_of_concave_on {f : E â†’ Î²} {a : E}
  (a_in_s : a âˆˆ s) (h_localmax: is_local_max_on f s a) (h_conc : concave_on ğ•œ s f) :
  âˆ€ x âˆˆ s, f x â‰¤ f a :=
@is_min_on.of_is_local_min_on_of_convex_on _
  _ _ (order_dual Î²) _ _ _ _ _ _ _ s f a a_in_s h_localmax h_conc

/-- A local minimum of a convex function is a global minimum. -/
lemma is_min_on.of_is_local_min_of_convex_univ {f : E â†’ Î²} {a : E}
  (h_local_min : is_local_min f a) (h_conv : convex_on ğ•œ univ f) : âˆ€ x, f a â‰¤ f x :=
Î» x, (is_min_on.of_is_local_min_on_of_convex_on (mem_univ a)
        (is_local_min.on h_local_min univ) h_conv) x (mem_univ x)

/-- A local maximum of a concave function is a global maximum. -/
lemma is_max_on.of_is_local_max_of_convex_univ {f : E â†’ Î²} {a : E}
  (h_local_max : is_local_max f a) (h_conc : concave_on ğ•œ univ f) : âˆ€ x, f x â‰¤ f a :=
@is_min_on.of_is_local_min_of_convex_univ _ (order_dual Î²) _ _ _ _ _ _ _ _ f a h_local_max h_conc
