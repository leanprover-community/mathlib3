/-
Copyright (c) 2021 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import analysis.convex.basic

/-!
# Extreme sets

This file defines extreme sets and extreme points for sets in a real vector space.

An extreme set of `A` is a subset of `A` that is as far as it can get in any outward direction: If
point `x` is in it and point `y ‚àà A`, then the line passing through `x` and `y` leaves `A` at `x`.
This is an analytic notion of "being on the side of". It is weaker than being exposed (see
`is_exposed.is_extreme`).

## Main declarations

* `is_extreme A B`: States that `B` is an extreme set of `A` (in the literature, `A` is often
  implicit).
* `set.extreme_points A`: Set of extreme points of `A` (corresponding to extreme singletons).
* `convex.mem_extreme_points_iff_convex_remove`: A useful equivalent condition to being an extreme
  point: `x` is an extreme point iff `A \ {x}` is convex.

## Implementation notes

The exact definition of extremeness has been carefully chosen so as to make as many lemmas
unconditional. In practice, `A` is often assumed to be a convex set.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011]

## TODO

* define convex independence, intrinsic frontier and prove lemmas related to extreme sets and
  points.
* generalise to Locally Convex Topological Vector Spaces‚Ñ¢

More not-yet-PRed stuff is available on the branch `sperner_again`.
-/

open_locale classical affine
open set

variables {E : Type*} [add_comm_group E] [module ‚Ñù E] {x : E} {A B C : set E}

/-- A set `B` is an extreme subset of `A` if `B ‚äÜ A` and all points of `B` only belong to open
segments whose ends are in `B`. -/
def is_extreme (A B : set E) : Prop :=
B ‚äÜ A ‚àß ‚àÄ x‚ÇÅ x‚ÇÇ ‚àà A, ‚àÄ x ‚àà B, x ‚àà open_segment ‚Ñù x‚ÇÅ x‚ÇÇ ‚Üí x‚ÇÅ ‚àà B ‚àß x‚ÇÇ ‚àà B

namespace is_extreme

@[refl] lemma refl (A : set E) :
  is_extreme A A :=
‚ü®subset.refl _, Œª x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxA hx, ‚ü®hx‚ÇÅA, hx‚ÇÇA‚ü©‚ü©

@[trans] lemma trans (hAB : is_extreme A B) (hBC : is_extreme B C) :
  is_extreme A C :=
begin
  use subset.trans hBC.1 hAB.1,
  rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxC hx,
  obtain ‚ü®hx‚ÇÅB, hx‚ÇÇB‚ü© := hAB.2 x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x (hBC.1 hxC) hx,
  exact hBC.2 x‚ÇÅ x‚ÇÇ hx‚ÇÅB hx‚ÇÇB x hxC hx,
end

lemma antisymm :
  anti_symmetric (is_extreme : set E ‚Üí set E ‚Üí Prop) :=
Œª A B hAB hBA, subset.antisymm hBA.1 hAB.1

instance : is_partial_order (set E) is_extreme :=
{ refl := refl,
  trans := Œª A B C, trans,
  antisymm := antisymm }

lemma convex_diff (hA : convex ùïú A) (hAB : is_extreme A B) :
  convex ùïú (A \ B) :=
convex_iff_open_segment_subset.2 (Œª x‚ÇÅ x‚ÇÇ ‚ü®hx‚ÇÅA, hx‚ÇÅB‚ü© ‚ü®hx‚ÇÇA, hx‚ÇÇB‚ü© x hx,
    ‚ü®hA.open_segment_subset hx‚ÇÅA hx‚ÇÇA hx, Œª hxB, hx‚ÇÅB (hAB.2 x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxB hx).1‚ü©)

lemma inter (hAB : is_extreme A B) (hAC : is_extreme A C) :
  is_extreme A (B ‚à© C) :=
begin
  use subset.trans (inter_subset_left _ _) hAB.1,
  rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x ‚ü®hxB, hxC‚ü© hx,
  obtain ‚ü®hx‚ÇÅB, hx‚ÇÇB‚ü© := hAB.2 x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxB hx,
  obtain ‚ü®hx‚ÇÅC, hx‚ÇÇC‚ü© := hAC.2 x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxC hx,
  exact ‚ü®‚ü®hx‚ÇÅB, hx‚ÇÅC‚ü©, hx‚ÇÇB, hx‚ÇÇC‚ü©,
end

lemma Inter {Œπ : Type*} [nonempty Œπ] {F : Œπ ‚Üí set E}
  (hAF : ‚àÄ i : Œπ, is_extreme A (F i)) :
  is_extreme A (‚ãÇ i : Œπ, F i) :=
begin
  obtain i := classical.arbitrary Œπ,
  use Inter_subset_of_subset i (hAF i).1,
  rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxF hx,
  simp_rw mem_Inter at ‚ä¢ hxF,
  have h := Œª i, (hAF i).2 x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x (hxF i) hx,
  exact ‚ü®Œª i, (h i).1, Œª i, (h i).2‚ü©,
end

lemma bInter {F : set (set E)} (hF : F.nonempty)
  (hAF : ‚àÄ B ‚àà F, is_extreme A B) :
  is_extreme A (‚ãÇ B ‚àà F, B) :=
begin
  obtain ‚ü®B, hB‚ü© := hF,
  use subset.trans (bInter_subset_of_mem hB) (hAF B hB).1,
  rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxF hx,
  rw mem_bInter_iff at ‚ä¢ hxF,
  rw mem_bInter_iff,
  have h := Œª B hB, (hAF B hB).2 x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x (hxF B hB) hx,
  exact ‚ü®Œª B hB, (h B hB).1, Œª B hB, (h B hB).2‚ü©,
end

lemma sInter {F : set (set E)} (hF : F.nonempty)
  (hAF : ‚àÄ B ‚àà F, is_extreme A B) :
  is_extreme A (‚ãÇ‚ÇÄ F) :=
begin
  obtain ‚ü®B, hB‚ü© := hF,
  use subset.trans (sInter_subset_of_mem hB) (hAF B hB).1,
  rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x hxF hx,
  simp_rw mem_sInter at ‚ä¢ hxF,
  have h := Œª B hB, (hAF B hB).2 x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA x (hxF B hB) hx,
  exact ‚ü®Œª B hB, (h B hB).1, Œª B hB, (h B hB).2‚ü©,
end

lemma mono (hAC : is_extreme A C) (hBA : B ‚äÜ A) (hCB : C ‚äÜ B) :
  is_extreme B C :=
‚ü®hCB, Œª x‚ÇÅ x‚ÇÇ hx‚ÇÅB hx‚ÇÇB x hxC hx, hAC.2 x‚ÇÅ x‚ÇÇ (hBA hx‚ÇÅB) (hBA hx‚ÇÇB) x hxC hx‚ü©

end is_extreme

/-- A point `x` is an extreme point of a set `A` if `x` belongs to no open segment with ends in
`A`, except for the obvious `open_segment x x`. -/
def set.extreme_points (A : set E) : set E :=
{x ‚àà A | ‚àÄ (x‚ÇÅ x‚ÇÇ ‚àà A), x ‚àà open_segment ‚Ñù x‚ÇÅ x‚ÇÇ ‚Üí x‚ÇÅ = x ‚àß x‚ÇÇ = x}

lemma extreme_points_def :
  x ‚àà A.extreme_points ‚Üî x ‚àà A ‚àß ‚àÄ (x‚ÇÅ x‚ÇÇ ‚àà A), x ‚àà open_segment ‚Ñù x‚ÇÅ x‚ÇÇ ‚Üí x‚ÇÅ = x ‚àß x‚ÇÇ = x :=
iff.rfl

/-- A useful restatement using `segment`: `x` is an extreme point iff the only (closed) segments
that contain it are those with `x` as one of their endpoints. -/
lemma mem_extreme_points_iff_forall_segment :
  x ‚àà A.extreme_points ‚Üî x ‚àà A ‚àß ‚àÄ (x‚ÇÅ x‚ÇÇ ‚àà A), x ‚àà segment ‚Ñù x‚ÇÅ x‚ÇÇ ‚Üí x‚ÇÅ = x ‚à® x‚ÇÇ = x :=
begin
  split,
  { rintro ‚ü®hxA, hAx‚ü©,
    use hxA,
    rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ hx,
    by_contra,
    push_neg at h,
    exact h.1 (hAx _ _ hx‚ÇÅ hx‚ÇÇ (mem_open_segment_of_ne_left_right ‚Ñù h.1 h.2 hx)).1 },
  rintro ‚ü®hxA, hAx‚ü©,
  use hxA,
  rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ hx,
  obtain rfl | rfl := hAx x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ (open_segment_subset_segment ‚Ñù _ _ hx),
  { exact ‚ü®rfl, (left_mem_open_segment_iff.1 hx).symm‚ü© },
  exact ‚ü®right_mem_open_segment_iff.1 hx, rfl‚ü©,
end

/-- x is an extreme point to A iff {x} is an extreme set of A. -/
lemma mem_extreme_points_iff_extreme_singleton :
  x ‚àà A.extreme_points ‚Üî is_extreme A {x} :=
begin
  split,
  { rintro ‚ü®hxA, hAx‚ü©,
    use singleton_subset_iff.2 hxA,
    rintro x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA y (rfl : y = x),
    exact hAx x‚ÇÅ x‚ÇÇ hx‚ÇÅA hx‚ÇÇA },
  exact Œª hx, ‚ü®singleton_subset_iff.1 hx.1, Œª x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ, hx.2 x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ x rfl‚ü©,
end

lemma extreme_points_subset : A.extreme_points ‚äÜ A := Œª x hx, hx.1

@[simp] lemma extreme_points_empty :
  (‚àÖ : set E).extreme_points = ‚àÖ :=
subset_empty_iff.1 extreme_points_subset

@[simp] lemma extreme_points_singleton :
  ({x} : set E).extreme_points = {x} :=
extreme_points_subset.antisymm $ singleton_subset_iff.2
  ‚ü®mem_singleton x, Œª x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ _, ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©‚ü©

lemma convex.mem_extreme_points_iff_convex_remove (hA : convex ùïú A) :
  x ‚àà A.extreme_points ‚Üî x ‚àà A ‚àß convex ùïú (A \ {x}) :=
begin
  use Œª hx, ‚ü®hx.1, (mem_extreme_points_iff_extreme_singleton.1 hx).convex_diff hA‚ü©,
  rintro ‚ü®hxA, hAx‚ü©,
  refine mem_extreme_points_iff_forall_segment.2 ‚ü®hxA, Œª x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ hx, _‚ü©,
  rw convex_iff_segment_subset at hAx,
  by_contra,
  push_neg at h,
  exact (hAx ‚ü®hx‚ÇÅ, Œª hx‚ÇÅ, h.1 (mem_singleton_iff.2 hx‚ÇÅ)‚ü©
    ‚ü®hx‚ÇÇ, Œª hx‚ÇÇ, h.2 (mem_singleton_iff.2 hx‚ÇÇ)‚ü© hx).2 rfl,
end

lemma convex.mem_extreme_points_iff_mem_diff_convex_hull_remove (hA : convex ùïú A) :
  x ‚àà A.extreme_points ‚Üî x ‚àà A \ convex_hull ùïú (A \ {x}) :=
by rw [hA.mem_extreme_points_iff_convex_remove, hA.convex_remove_iff_not_mem_convex_hull_remove,
  mem_diff]

lemma inter_extreme_points_subset_extreme_points_of_subset (hBA : B ‚äÜ A) :
  B ‚à© A.extreme_points ‚äÜ B.extreme_points :=
Œª x ‚ü®hxB, hxA‚ü©, ‚ü®hxB, Œª x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ hx, hxA.2 x‚ÇÅ x‚ÇÇ (hBA hx‚ÇÅ) (hBA hx‚ÇÇ) hx‚ü©

namespace is_extreme

lemma extreme_points_subset_extreme_points (hAB : is_extreme A B) :
  B.extreme_points ‚äÜ A.extreme_points :=
Œª x hx, mem_extreme_points_iff_extreme_singleton.2 (hAB.trans
  (mem_extreme_points_iff_extreme_singleton.1 hx))

lemma extreme_points_eq (hAB : is_extreme A B) :
  B.extreme_points = B ‚à© A.extreme_points :=
subset.antisymm (Œª x hx, ‚ü®hx.1, hAB.extreme_points_subset_extreme_points hx‚ü©)
  (inter_extreme_points_subset_extreme_points_of_subset hAB.1)

end is_extreme

lemma extreme_points_convex_hull_subset :
  (convex_hull ùïú A).extreme_points ‚äÜ A :=
begin
  rintro x hx,
  rw (convex_convex_hull ùïú _).mem_extreme_points_iff_convex_remove at hx,
  by_contra,
  exact (convex_hull_min (subset_diff.2 ‚ü®subset_convex_hull _, disjoint_singleton_right.2 h‚ü©) hx.2
    hx.1).2 rfl,
end
