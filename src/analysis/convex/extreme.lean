/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import analysis.convex.hull

/-!
# Extreme sets

This file defines extreme sets and extreme points for sets in a module.

An extreme set of `A` is a subset of `A` that is as far as it can get in any outward direction: If
point `x` is in it and point `y âˆˆ A`, then the line passing through `x` and `y` leaves `A` at `x`.
This is an analytic notion of "being on the side of". It is weaker than being exposed (see
`is_exposed.is_extreme`).

## Main declarations

* `is_extreme ğ•œ A B`: States that `B` is an extreme set of `A` (in the literature, `A` is often
  implicit).
* `set.extreme_points ğ•œ A`: Set of extreme points of `A` (corresponding to extreme singletons).
* `convex.mem_extreme_points_iff_convex_diff`: A useful equivalent condition to being an extreme
  point: `x` is an extreme point iff `A \ {x}` is convex.

## Implementation notes

The exact definition of extremeness has been carefully chosen so as to make as many lemmas
unconditional (in particular, the Krein-Milman theorem doesn't need the set to be convex!).
In practice, `A` is often assumed to be a convex set.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011]

## TODO

Prove lemmas relating extreme sets and points to the intrinsic frontier.

More not-yet-PRed stuff is available on the branch `sperner_again`.
-/

open function set
open_locale affine classical

variables {ğ•œ E F Î¹ : Type*} {Ï€ : Î¹ â†’ Type*}

section has_smul
variables (ğ•œ) [ordered_semiring ğ•œ] [add_comm_monoid E] [has_smul ğ•œ E]

/-- A set `B` is an extreme subset of `A` if `B âŠ† A` and all points of `B` only belong to open
segments whose ends are in `B`. -/
def is_extreme (A B : set E) : Prop :=
B âŠ† A âˆ§ âˆ€ â¦ƒxâ‚â¦„, xâ‚ âˆˆ A â†’ âˆ€ â¦ƒxâ‚‚â¦„, xâ‚‚ âˆˆ A â†’ âˆ€ â¦ƒxâ¦„, x âˆˆ B â†’ x âˆˆ open_segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ âˆˆ B âˆ§ xâ‚‚ âˆˆ B

/-- A point `x` is an extreme point of a set `A` if `x` belongs to no open segment with ends in
`A`, except for the obvious `open_segment x x`. -/
def set.extreme_points (A : set E) : set E :=
{x âˆˆ A | âˆ€ â¦ƒxâ‚â¦„, xâ‚ âˆˆ A â†’ âˆ€ â¦ƒxâ‚‚â¦„, xâ‚‚ âˆˆ A â†’ x âˆˆ open_segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x}

@[refl] protected lemma is_extreme.refl (A : set E) :
  is_extreme ğ•œ A A :=
âŸ¨subset.rfl, Î» xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxA hx, âŸ¨hxâ‚A, hxâ‚‚AâŸ©âŸ©

variables {ğ•œ} {A B C : set E} {x : E}

protected lemma is_extreme.rfl :
  is_extreme ğ•œ A A :=
is_extreme.refl ğ•œ A

@[trans] protected lemma is_extreme.trans (hAB : is_extreme ğ•œ A B) (hBC : is_extreme ğ•œ B C) :
  is_extreme ğ•œ A C :=
begin
  refine âŸ¨subset.trans hBC.1 hAB.1, Î» xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxC hx, _âŸ©,
  obtain âŸ¨hxâ‚B, hxâ‚‚BâŸ© := hAB.2 hxâ‚A hxâ‚‚A (hBC.1 hxC) hx,
  exact hBC.2 hxâ‚B hxâ‚‚B hxC hx,
end

protected lemma is_extreme.antisymm :
  anti_symmetric (is_extreme ğ•œ : set E â†’ set E â†’ Prop) :=
Î» A B hAB hBA, subset.antisymm hBA.1 hAB.1

instance : is_partial_order (set E) (is_extreme ğ•œ) :=
{ refl := is_extreme.refl ğ•œ,
  trans := Î» A B C, is_extreme.trans,
  antisymm := is_extreme.antisymm }

lemma is_extreme.inter (hAB : is_extreme ğ•œ A B) (hAC : is_extreme ğ•œ A C) :
  is_extreme ğ•œ A (B âˆ© C) :=
begin
  use subset.trans (inter_subset_left _ _) hAB.1,
  rintro xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x âŸ¨hxB, hxCâŸ© hx,
  obtain âŸ¨hxâ‚B, hxâ‚‚BâŸ© := hAB.2 hxâ‚A hxâ‚‚A hxB hx,
  obtain âŸ¨hxâ‚C, hxâ‚‚CâŸ© := hAC.2 hxâ‚A hxâ‚‚A hxC hx,
  exact âŸ¨âŸ¨hxâ‚B, hxâ‚CâŸ©, hxâ‚‚B, hxâ‚‚CâŸ©,
end

protected lemma is_extreme.mono (hAC : is_extreme ğ•œ A C) (hBA : B âŠ† A) (hCB : C âŠ† B) :
  is_extreme ğ•œ B C :=
âŸ¨hCB, Î» xâ‚ hxâ‚B xâ‚‚ hxâ‚‚B x hxC hx, hAC.2 (hBA hxâ‚B) (hBA hxâ‚‚B) hxC hxâŸ©

lemma is_extreme_Inter {Î¹ : Sort*} [nonempty Î¹] {F : Î¹ â†’ set E}
  (hAF : âˆ€ i : Î¹, is_extreme ğ•œ A (F i)) :
  is_extreme ğ•œ A (â‹‚ i : Î¹, F i) :=
begin
  obtain i := classical.arbitrary Î¹,
  refine âŸ¨Inter_subset_of_subset i (hAF i).1, Î» xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxF hx, _âŸ©,
  simp_rw mem_Inter at âŠ¢ hxF,
  have h := Î» i, (hAF i).2 hxâ‚A hxâ‚‚A (hxF i) hx,
  exact âŸ¨Î» i, (h i).1, Î» i, (h i).2âŸ©,
end

lemma is_extreme_bInter {F : set (set E)} (hF : F.nonempty) (hA : âˆ€ B âˆˆ F, is_extreme ğ•œ A B) :
  is_extreme ğ•œ A (â‹‚ B âˆˆ F, B) :=
by { haveI := hF.to_subtype, simpa only [Inter_subtype] using is_extreme_Inter (Î» i : F, hA _ i.2) }

lemma is_extreme_sInter {F : set (set E)} (hF : F.nonempty)
  (hAF : âˆ€ B âˆˆ F, is_extreme ğ•œ A B) :
  is_extreme ğ•œ A (â‹‚â‚€ F) :=
begin
  obtain âŸ¨B, hBâŸ© := hF,
  refine âŸ¨(sInter_subset_of_mem hB).trans (hAF B hB).1, Î» xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxF hx, _âŸ©,
  simp_rw mem_sInter at âŠ¢ hxF,
  have h := Î» B hB, (hAF B hB).2 hxâ‚A hxâ‚‚A (hxF B hB) hx,
  exact âŸ¨Î» B hB, (h B hB).1, Î» B hB, (h B hB).2âŸ©,
end

lemma mem_extreme_points :
  x âˆˆ A.extreme_points ğ•œ â†” x âˆˆ A âˆ§ âˆ€ (xâ‚ xâ‚‚ âˆˆ A), x âˆˆ open_segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x :=
iff.rfl

/-- x is an extreme point to A iff {x} is an extreme set of A. -/
lemma mem_extreme_points_iff_extreme_singleton :
  x âˆˆ A.extreme_points ğ•œ â†” is_extreme ğ•œ A {x} :=
begin
  refine âŸ¨_, Î» hx, âŸ¨singleton_subset_iff.1 hx.1, Î» xâ‚ hxâ‚ xâ‚‚ hxâ‚‚, hx.2 hxâ‚ hxâ‚‚ rflâŸ©âŸ©,
  rintro âŸ¨hxA, hAxâŸ©,
  use singleton_subset_iff.2 hxA,
  rintro xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A y (rfl : y = x),
  exact hAx hxâ‚A hxâ‚‚A,
end

lemma extreme_points_subset : A.extreme_points ğ•œ âŠ† A := Î» x hx, hx.1

@[simp] lemma extreme_points_empty :
  (âˆ… : set E).extreme_points ğ•œ = âˆ… :=
subset_empty_iff.1 extreme_points_subset

@[simp] lemma extreme_points_singleton :
  ({x} : set E).extreme_points ğ•œ = {x} :=
extreme_points_subset.antisymm $ singleton_subset_iff.2
  âŸ¨mem_singleton x, Î» xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ _, âŸ¨hxâ‚, hxâ‚‚âŸ©âŸ©

lemma inter_extreme_points_subset_extreme_points_of_subset (hBA : B âŠ† A) :
  B âˆ© A.extreme_points ğ•œ âŠ† B.extreme_points ğ•œ :=
Î» x âŸ¨hxB, hxAâŸ©, âŸ¨hxB, Î» xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx, hxA.2 (hBA hxâ‚) (hBA hxâ‚‚) hxâŸ©

lemma is_extreme.extreme_points_subset_extreme_points (hAB : is_extreme ğ•œ A B) :
  B.extreme_points ğ•œ âŠ† A.extreme_points ğ•œ :=
Î» x hx, mem_extreme_points_iff_extreme_singleton.2 (hAB.trans
  (mem_extreme_points_iff_extreme_singleton.1 hx))

lemma is_extreme.extreme_points_eq (hAB : is_extreme ğ•œ A B) :
  B.extreme_points ğ•œ = B âˆ© A.extreme_points ğ•œ :=
subset.antisymm (Î» x hx, âŸ¨hx.1, hAB.extreme_points_subset_extreme_points hxâŸ©)
  (inter_extreme_points_subset_extreme_points_of_subset hAB.1)

end has_smul

section ordered_semiring
variables [ordered_semiring ğ•œ] [add_comm_group E] [add_comm_group F] [Î  i, add_comm_group (Ï€ i)]
  [module ğ•œ E] [module ğ•œ F] [Î  i, module ğ•œ (Ï€ i)] {A B : set E} {x : E}

lemma is_extreme.convex_diff (hA : convex ğ•œ A) (hAB : is_extreme ğ•œ A B) :
  convex ğ•œ (A \ B) :=
convex_iff_open_segment_subset.2 (Î» xâ‚ âŸ¨hxâ‚A, hxâ‚BâŸ© xâ‚‚ âŸ¨hxâ‚‚A, hxâ‚‚BâŸ© x hx,
    âŸ¨hA.open_segment_subset hxâ‚A hxâ‚‚A hx, Î» hxB, hxâ‚B (hAB.2 hxâ‚A hxâ‚‚A hxB hx).1âŸ©)

@[simp] lemma extreme_points_prod (s : set E) (t : set F) :
  (s Ã—Ë¢ t).extreme_points ğ•œ = s.extreme_points ğ•œ Ã—Ë¢ t.extreme_points ğ•œ :=
begin
  ext,
  refine (and_congr_right $ Î» hx, âŸ¨Î» h, _, Î» h, _âŸ©).trans (and_and_and_comm _ _ _ _),
  split,
  { rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx_fst,
    refine (h (mk_mem_prod hxâ‚ hx.2) (mk_mem_prod hxâ‚‚ hx.2) _).imp
      (congr_arg prod.fst) (congr_arg prod.fst),
    rw â†prod.image_mk_open_segment_left,
    exact âŸ¨_, hx_fst, prod.mk.etaâŸ© },
  { rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx_snd,
    refine (h (mk_mem_prod hx.1 hxâ‚) (mk_mem_prod hx.1 hxâ‚‚) _).imp
      (congr_arg prod.snd) (congr_arg prod.snd),
    rw â†prod.image_mk_open_segment_right,
    exact âŸ¨_, hx_snd, prod.mk.etaâŸ© },
  { rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ âŸ¨a, b, ha, hb, hab, hx'âŸ©,
    simp_rw prod.ext_iff,
    exact (and_and_and_comm _ _ _ _).1
     âŸ¨h.1 hxâ‚.1 hxâ‚‚.1 âŸ¨a, b, ha, hb, hab, congr_arg prod.fst hx'âŸ©,
      h.2 hxâ‚.2 hxâ‚‚.2 âŸ¨a, b, ha, hb, hab, congr_arg prod.snd hx'âŸ©âŸ© }
end

@[simp] lemma extreme_points_pi (s : Î  i, set (Ï€ i)) :
  (univ.pi s).extreme_points ğ•œ = univ.pi (Î» i, (s i).extreme_points ğ•œ) :=
begin
  ext,
  simp only [mem_extreme_points, mem_pi, mem_univ, true_implies_iff, @forall_and_distrib Î¹],
  refine and_congr_right (Î» hx, âŸ¨Î» h i, _, Î» h, _âŸ©),
  { rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hi,
    refine (h (update x i xâ‚) _ (update x i xâ‚‚) _ _).imp (Î» hâ‚, by rw [â†hâ‚, update_same])
      (Î» hâ‚‚, by rw [â†hâ‚‚, update_same]),
    iterate 2
    { rintro j,
      obtain rfl | hji := eq_or_ne j i,
      { rwa update_same },
      { rw update_noteq hji,
        exact hx _ } },
    rw â†pi.image_update_open_segment,
    exact âŸ¨_, hi, update_eq_self _ _âŸ© },
  { rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ âŸ¨a, b, ha, hb, hab, hx'âŸ©,
    simp_rw [funext_iff, â†forall_and_distrib],
    exact Î» i, h _ _ (hxâ‚ _) _ (hxâ‚‚ _) âŸ¨a, b, ha, hb, hab, congr_fun hx' _âŸ© }
end

end ordered_semiring

section linear_ordered_ring
variables {ğ•œ} [linear_ordered_ring ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [densely_ordered ğ•œ] [no_zero_smul_divisors ğ•œ E] {A B : set E} {x : E}

/-- A useful restatement using `segment`: `x` is an extreme point iff the only (closed) segments
that contain it are those with `x` as one of their endpoints. -/
lemma mem_extreme_points_iff_forall_segment :
  x âˆˆ A.extreme_points ğ•œ â†” x âˆˆ A âˆ§ âˆ€ (xâ‚ xâ‚‚ âˆˆ A), x âˆˆ segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ¨ xâ‚‚ = x :=
begin
  refine and_congr_right (Î» hxA, forallâ‚„_congr $ Î» xâ‚ hâ‚ xâ‚‚ hâ‚‚, _),
  split,
  { rw â† insert_endpoints_open_segment,
    rintro H (rfl|rfl|hx),
    exacts [or.inl rfl, or.inr rfl, or.inl $ (H hx).1] },
  { intros H hx,
    rcases H (open_segment_subset_segment _ _ _ hx) with rfl | rfl,
    exacts [âŸ¨rfl, (left_mem_open_segment_iff.1 hx).symmâŸ©, âŸ¨right_mem_open_segment_iff.1 hx, rflâŸ©] }
end

lemma convex.mem_extreme_points_iff_convex_diff (hA : convex ğ•œ A) :
  x âˆˆ A.extreme_points ğ•œ â†” x âˆˆ A âˆ§ convex ğ•œ (A \ {x}) :=
begin
  use Î» hx, âŸ¨hx.1, (mem_extreme_points_iff_extreme_singleton.1 hx).convex_diff hAâŸ©,
  rintro âŸ¨hxA, hAxâŸ©,
  refine mem_extreme_points_iff_forall_segment.2 âŸ¨hxA, Î» xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx, _âŸ©,
  rw convex_iff_segment_subset at hAx,
  by_contra' h,
  exact (hAx âŸ¨hxâ‚, Î» hxâ‚, h.1 (mem_singleton_iff.2 hxâ‚)âŸ©
    âŸ¨hxâ‚‚, Î» hxâ‚‚, h.2 (mem_singleton_iff.2 hxâ‚‚)âŸ© hx).2 rfl,
end

lemma convex.mem_extreme_points_iff_mem_diff_convex_hull_diff (hA : convex ğ•œ A) :
  x âˆˆ A.extreme_points ğ•œ â†” x âˆˆ A \ convex_hull ğ•œ (A \ {x}) :=
by rw [hA.mem_extreme_points_iff_convex_diff, hA.convex_remove_iff_not_mem_convex_hull_remove,
  mem_diff]

lemma extreme_points_convex_hull_subset :
  (convex_hull ğ•œ A).extreme_points ğ•œ âŠ† A :=
begin
  rintro x hx,
  rw (convex_convex_hull ğ•œ _).mem_extreme_points_iff_convex_diff at hx,
  by_contra,
  exact (convex_hull_min (subset_diff.2 âŸ¨subset_convex_hull ğ•œ _, disjoint_singleton_right.2 hâŸ©) hx.2
    hx.1).2 rfl,
  apply_instance
end

end linear_ordered_ring
