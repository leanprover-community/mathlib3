/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, FranÃ§ois Dupuis
-/
import analysis.convex.combination
import data.real.basic
import algebra.module.ordered

/-!
# Convex and concave functions

This file defines convex and concave functions in vector spaces and proves the finite Jensen
inequality. The integral version can be found in `analysis.convex.integral`.

A function `f : E â†’ Î²` is `convex_on` a set `s` if `s` is itself a convex set, and for any two
points `x y âˆˆ s`, the segment joining `(x, f x)` to `(y, f y)` is above the graph of `f`.
Equivalently, `convex_on ğ•œ f s` means that the epigraph `{p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2}` is
a convex set.

## Main declarations

* `convex_on ğ•œ s f`: The function `f` is convex on `s` with scalars `ğ•œ`.
* `concave_on ğ•œ s f`: The function `f` is concave on `s` with scalars `ğ•œ`.
* `strict_convex_on ğ•œ s f`: The function `f` is strictly convex on `s` with scalars `ğ•œ`.
* `strict_concave_on ğ•œ s f`: The function `f` is strictly concave on `s` with scalars `ğ•œ`.
* `convex_on.map_center_mass_le` `convex_on.map_sum_le`: Convex Jensen's inequality.
-/

open finset linear_map set
open_locale big_operators classical convex pointwise

variables {ğ•œ E F Î² Î¹ : Type*}

section ordered_semiring
variables [ordered_semiring ğ•œ] [add_comm_monoid E] [add_comm_monoid F]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section has_scalar
variables (ğ•œ) [has_scalar ğ•œ E] [has_scalar ğ•œ Î²] (s : set E) (f : E â†’ Î²)

/-- Convexity of functions -/
def convex_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y

/-- Concavity of functions -/
def concave_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)

/-- Strict convexity of functions -/
def strict_convex_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y

/-- Strict concavity of functions -/
def strict_concave_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ 
    a â€¢ f x + b â€¢ f y < f (a â€¢ x + b â€¢ y)

variables {ğ•œ s f}

lemma convex_on_id {s : set ğ•œ} (hs : convex ğ•œ s) : convex_on ğ•œ s id := âŸ¨hs, by { intros, refl }âŸ©

lemma concave_on_id {s : set ğ•œ} (hs : convex ğ•œ s) : concave_on ğ•œ s id := âŸ¨hs, by { intros, refl }âŸ©

lemma convex_on.subset {f : E â†’ Î²} {t : set E} (hf : convex_on ğ•œ t f) (hst : s âŠ† t)
  (hs : convex ğ•œ s) : convex_on ğ•œ s f :=
âŸ¨hs, Î» x y hx hy, hf.2 (hst hx) (hst hy)âŸ©

lemma concave_on.subset {f : E â†’ Î²} {t : set E} (hf : concave_on ğ•œ t f) (hst : s âŠ† t)
  (hs : convex ğ•œ s) : concave_on ğ•œ s f :=
âŸ¨hs, Î» x y hx hy, hf.2 (hst hx) (hst hy)âŸ©

end has_scalar

section distrib_mul_action
variables [has_scalar ğ•œ E] [distrib_mul_action ğ•œ Î²] {s : set E}

lemma convex_on.add {f g : E â†’ Î²} (hf : convex_on ğ•œ s f) (hg : convex_on ğ•œ s g) :
  convex_on ğ•œ s (Î» x, f x + g x) :=
âŸ¨hf.1, Î» x y hx hy a b ha hb hab,
  calc
    f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) â‰¤ (a â€¢ f x + b â€¢ f y) + (a â€¢ g x + b â€¢ g y)
      : add_le_add (hf.2 hx hy ha hb hab) (hg.2 hx hy ha hb hab)
    ... = a â€¢ f x + a â€¢ g x + b â€¢ f y + b â€¢ g y : by abel
    ... = a â€¢ (f x + g x) + b â€¢ (f y + g y) : by simp only [smul_add, add_assoc]âŸ©

lemma concave_on.add {f g : E â†’ Î²} (hf : concave_on ğ•œ s f) (hg : concave_on ğ•œ s g) :
  concave_on ğ•œ s (Î» x, f x + g x) :=
@convex_on.add _ _ (order_dual Î²) _ _ _ _ _ _ f g hf hg

end distrib_mul_action

section module
variables [has_scalar ğ•œ E] [module ğ•œ Î²] {s : set E}

lemma convex_on_const (c : Î²) (hs : convex ğ•œ s) : convex_on ğ•œ s (Î» x:E, c) :=
âŸ¨hs, Î» x y _ _ a b _ _ hab, (convex.combo_self hab c).geâŸ©

lemma concave_on_const (c : Î²) (hs : convex ğ•œ s) : concave_on ğ•œ s (Î» x:E, c) :=
@convex_on_const _ _ (order_dual Î²) _ _ _ _ _ _ c hs

end module

section ordered_smul
variables [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma convex_on.convex_le (hf : convex_on ğ•œ s f) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x â‰¤ r} :=
Î» x y hx hy a b ha hb hab, âŸ¨hf.1 hx.1 hy.1 ha hb hab,
  calc
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ (f x) + b â€¢ (f y) : hf.2 hx.1 hy.1 ha hb hab
                  ... â‰¤ a â€¢ r + b â€¢ r         : add_le_add (smul_le_smul_of_nonneg hx.2 ha)
                                                  (smul_le_smul_of_nonneg hy.2 hb)
                  ... = r                     : convex.combo_self hab râŸ©

lemma concave_on.concave_ge (hf : concave_on ğ•œ s f) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r â‰¤ f x} :=
@convex_on.convex_le ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf r

lemma convex_on.convex_epigraph (hf : convex_on ğ•œ s f) :
  convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} :=
begin
  rintro âŸ¨x, râŸ© âŸ¨y, tâŸ© âŸ¨hx, hrâŸ© âŸ¨hy, htâŸ© a b ha hb hab,
  refine âŸ¨hf.1 hx hy ha hb hab, _âŸ©,
  calc f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha hb hab
  ... â‰¤ a â€¢ r + b â€¢ t : add_le_add (smul_le_smul_of_nonneg hr ha)
                            (smul_le_smul_of_nonneg ht hb)
end

lemma concave_on.convex_hypograph (hf : concave_on ğ•œ s f) :
  convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1} :=
@convex_on.convex_epigraph ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf

lemma convex_on_iff_convex_epigraph :
  convex_on ğ•œ s f â†” convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} :=
âŸ¨convex_on.convex_epigraph, Î» h,
  âŸ¨Î» x y hx hy a b ha hb hab, (@h (x, f x) (y, f y) âŸ¨hx, le_rflâŸ© âŸ¨hy, le_rflâŸ© a b ha hb hab).1,
  Î» x y hx hy a b ha hb hab, (@h (x, f x) (y, f y) âŸ¨hx, le_rflâŸ© âŸ¨hy, le_rflâŸ© a b ha hb hab).2âŸ©âŸ©

lemma concave_on_iff_convex_hypograph :
  concave_on ğ•œ s f â†” convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1} :=
@convex_on_iff_convex_epigraph ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f

end ordered_smul

section module
variables [module ğ•œ E] [module ğ•œ Î²]

/-- A linear map is convex. -/
lemma linear_map.convex_on (f : E â†’â‚—[ğ•œ] Î²) {s : set E} (hs : convex ğ•œ s) : convex_on ğ•œ s f :=
âŸ¨hs, Î» _ _ _ _ _ _ _ _ _, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

/-- A linear map is concave. -/
lemma linear_map.concave_on (f : E â†’â‚—[ğ•œ] Î²) {s : set E} (hs : convex ğ•œ s) : concave_on ğ•œ s f :=
âŸ¨hs, Î» _ _ _ _ _ _ _ _ _, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

/-- If a function is convex on `s`, it remains convex after a translation. -/
lemma convex_on.translate_right {f : E â†’ Î²} {s : set E} {c : E} (hf : convex_on ğ•œ s f) :
  convex_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, c + z)) :=
âŸ¨hf.1.translate_preimage_right _, Î» x y hx hy a b ha hb hab,
  calc
    f (c + (a â€¢ x + b â€¢ y)) = f (a â€¢ (c + x) + b â€¢ (c + y))
        : by rw [smul_add, smul_add, add_add_add_comm, convex.combo_self hab]
    ... â‰¤ a â€¢ f (c + x) + b â€¢ f (c + y) : hf.2 hx hy ha hb habâŸ©

/-- If a function is concave on `s`, it remains concave after a translation. -/
lemma concave_on.translate_right {f : E â†’ Î²} {s : set E} {a : E} (hf : concave_on ğ•œ s f) :
  concave_on ğ•œ ((Î» z, a + z) â»Â¹' s) (f âˆ˜ (Î» z, a + z)) :=
@convex_on.translate_right ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ _ hf

/-- If a function is convex on `s`, it remains convex after a translation. -/
lemma convex_on.translate_left {f : E â†’ Î²} {s : set E} {a : E} (hf : convex_on ğ•œ s f) :
  convex_on ğ•œ ((Î» z, a + z) â»Â¹' s) (f âˆ˜ (Î» z, z + a)) :=
by simpa only [add_comm] using hf.translate_right

/-- If a function is concave on `s`, it remains concave after a translation. -/
lemma concave_on.translate_left {f : E â†’ Î²} {s : set E} {a : E} (hf : concave_on ğ•œ s f) :
  concave_on ğ•œ ((Î» z, a + z) â»Â¹' s) (f âˆ˜ (Î» z, z + a)) :=
by simpa only [add_comm] using hf.translate_right

variables [linear_order E] {s : set E} {f : E â†’ Î²}

/-- For a function on a convex set in a linear ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is convex, it suffices to
verify the inequality `f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y` only for `x < y` and positive `a`,
`b`. The main use case is `E = ğ•œ` however one can apply it, e.g., to `ğ•œ^n` with lexicographic order.
-/
lemma linear_order.convex_on_of_lt (hs : convex ğ•œ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y) : convex_on ğ•œ s f :=
begin
  refine âŸ¨hs, Î» x y hx hy a b ha hb hab, _âŸ©,
  wlog hxy : x â‰¤ y using [x y a b, y x b a],
  { exact le_total _ _ },
  obtain rfl | hxy := hxy.eq_or_lt,
  { rw [convex.combo_self hab, convex.combo_self hab] },
  obtain rfl | ha' := ha.eq_or_lt,
  { rw [zero_add] at hab, subst b, simp_rw [zero_smul, zero_add, one_smul] },
  obtain rfl | hb' := hb.eq_or_lt,
  { rw [add_zero] at hab, subst a, simp_rw [zero_smul, add_zero, one_smul] },
  exact hf hx hy hxy ha' hb' hab,
end

/-- For a function on a convex set in a linear ordered space, in order to prove that it is concave
it suffices to verify the inequality `a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)` only for `x < y`
and positive `a`, `b`. The main use case is `E = â„` however one can apply it, e.g., to `â„^n` with
lexicographic order. -/
lemma linear_order.concave_on_of_lt (hs : convex ğ•œ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
     a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)) : concave_on ğ•œ s f :=
@linear_order.convex_on_of_lt _ _ (order_dual Î²) _ _ _ _ _ _ s f hs hf

end module

section module
variables [module ğ•œ E] [module ğ•œ F] [has_scalar ğ•œ Î²]

/-- If `g` is convex on `s`, so is `(f âˆ˜ g)` on `f â»Â¹' s` for a linear `f`. -/
lemma convex_on.comp_linear_map {f : F â†’ Î²} {s : set F} (hf : convex_on ğ•œ s f) (g : E â†’â‚—[ğ•œ] F) :
  convex_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
âŸ¨hf.1.linear_preimage _, Î» x y hx hy a b ha hb hab,
  calc
    f (g (a â€¢ x + b â€¢ y)) = f (a â€¢ (g x) + b â€¢ (g y)) : by rw [g.map_add, g.map_smul, g.map_smul]
                      ... â‰¤ a â€¢ f (g x) + b â€¢ f (g y) : hf.2 hx hy ha hb habâŸ©

/-- If `g` is concave on `s`, so is `(g âˆ˜ f)` on `f â»Â¹' s` for a linear `f`. -/
lemma concave_on.comp_linear_map {f : F â†’ Î²} {s : set F} (hf : concave_on ğ•œ s f) (g : E â†’â‚—[ğ•œ] F) :
  concave_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
@convex_on.comp_linear_map ğ•œ E F (order_dual Î²) _ _ _ _ _ _ _ f s hf g

end module
end ordered_add_comm_monoid

section ordered_cancel_add_comm_monoid
variables [ordered_cancel_add_comm_monoid Î²]

section module
variables [module ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f g : E â†’ Î²}

lemma convex_on.convex_lt (hf : convex_on ğ•œ s f) (r : Î²) : convex ğ•œ {x âˆˆ s | f x < r} :=
begin
  refine Î» x y hx hy a b ha hb hab, âŸ¨hf.1 hx.1 hy.1 ha hb hab, _âŸ©,
  obtain rfl | ha' := ha.eq_or_lt,
  { rw zero_add at hab,
    rw [hab, zero_smul, one_smul, zero_add],
    exact hy.2 },
  { calc
      f (a â€¢ x + b â€¢ y)
          â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx.1 hy.1 ha hb hab
      ... < a â€¢ r + b â€¢ r     : add_lt_add_of_lt_of_le (smul_lt_smul_of_pos hx.2 ha')
                                  (smul_le_smul_of_nonneg hy.2.le hb)
      ... = r                 : convex.combo_self hab _ }
end

lemma concave_on.convex_lt (hf : concave_on ğ•œ s f) (r : Î²) : convex ğ•œ {x âˆˆ s | r < f x} :=
@convex_on.convex_lt ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf r

end module

end ordered_cancel_add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid Î²] [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]
  {s : set E} {f g : E â†’ Î²}

/-- The pointwise maximum of convex functions is convex. -/
lemma convex_on.sup (hf : convex_on ğ•œ s f) (hg : convex_on ğ•œ s g) :
  convex_on ğ•œ s (f âŠ” g) :=
begin
   refine âŸ¨hf.left, Î» x y hx hy a b ha hb hab, sup_le _ _âŸ©,
   { calc f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.right hx hy ha hb hab
      ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
      (smul_le_smul_of_nonneg le_sup_left ha)
      (smul_le_smul_of_nonneg le_sup_left hb) },
   { calc g (a â€¢ x + b â€¢ y) â‰¤ a â€¢ g x + b â€¢ g y : hg.right hx hy ha hb hab
      ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
      (smul_le_smul_of_nonneg le_sup_right ha)
      (smul_le_smul_of_nonneg le_sup_right hb) }
end

/-- The pointwise minimum of concave functions is concave. -/
lemma concave_on.inf (hf : concave_on ğ•œ s f) (hg : concave_on ğ•œ s g) :
  concave_on ğ•œ s (f âŠ“ g) :=
@convex_on.sup ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f g hf hg

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
lemma convex_on.le_on_segment' (hf : convex_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  f (a â€¢ x + b â€¢ y) â‰¤ max (f x) (f y) :=
calc
  f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha hb hab
  ... â‰¤ a â€¢ max (f x) (f y) + b â€¢ max (f x) (f y) :
    add_le_add (smul_le_smul_of_nonneg (le_max_left _ _) ha)
      (smul_le_smul_of_nonneg (le_max_right _ _) hb)
  ... = max (f x) (f y) : by rw [â†add_smul, hab, one_smul]

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
lemma concave_on.le_on_segment' (hf : concave_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
 {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  min (f x) (f y) â‰¤ f (a â€¢ x + b â€¢ y) :=
@convex_on.le_on_segment' ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf x y hx hy a b ha hb hab

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
lemma convex_on.le_on_segment (hf : convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  (hz : z âˆˆ [x -[ğ•œ] y]) :
  f z â‰¤ max (f x) (f y) :=
let âŸ¨a, b, ha, hb, hab, hzâŸ© := hz in hz â–¸ hf.le_on_segment' hx hy ha hb hab

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
lemma concave_on.le_on_segment (hf : concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  (hz : z âˆˆ [x -[ğ•œ] y]) :
  min (f x) (f y) â‰¤ f z :=
@convex_on.le_on_segment ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf x y z hx hy hz

end linear_ordered_add_comm_monoid

section linear_ordered_cancel_add_comm_monoid
variables [linear_ordered_cancel_add_comm_monoid Î²]

section ordered_smul
variables [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f g : E â†’ Î²}

lemma convex_on.le_left_of_right_le' (hf : convex_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
 {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) (hxy : f y â‰¤ f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) â‰¤ f x :=
le_of_not_lt $ Î» h, lt_irrefl (f (a â€¢ x + b â€¢ y)) $
  calc
    f (a â€¢ x + b â€¢ y)
        â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha.le hb hab
    ... < a â€¢ f (a â€¢ x + b â€¢ y) + b â€¢ f (a â€¢ x + b â€¢ y)
        : add_lt_add_of_lt_of_le (smul_lt_smul_of_pos h ha) (smul_le_smul_of_nonneg hxy hb)
    ... = f (a â€¢ x + b â€¢ y) : by rw [â†add_smul, hab, one_smul]

lemma concave_on.left_le_of_le_right' (hf : concave_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) (hxy : f (a â€¢ x + b â€¢ y) â‰¤ f y) :
  f x â‰¤ f (a â€¢ x + b â€¢ y) :=
@convex_on.le_left_of_right_le' ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf x y hx hy a b ha hb hab hxy

lemma convex_on.le_right_of_left_le' (hf : convex_on ğ•œ s f) {x y : E} {a b : ğ•œ}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1)
  (hxy : f x â‰¤ f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) â‰¤ f y :=
begin
  rw add_comm at âŠ¢ hab hxy,
  exact hf.le_left_of_right_le' hy hx hb ha hab hxy,
end

lemma concave_on.le_right_of_left_le' (hf : concave_on ğ•œ s f) {x y : E} {a b : ğ•œ}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1)
  (hxy : f (a â€¢ x + b â€¢ y) â‰¤ f x) :
  f y â‰¤ f (a â€¢ x + b â€¢ y) :=
@convex_on.le_right_of_left_le' ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf x y a b hx hy ha hb hab hxy

lemma convex_on.le_left_of_right_le (hf : convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hyz : f y â‰¤ f z) :
  f z â‰¤ f x :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.le_left_of_right_le' hx hy ha hb.le hab hyz,
end

lemma concave_on.left_le_of_le_right (hf : concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hyz : f z â‰¤ f y) :
  f x â‰¤ f z :=
@convex_on.le_left_of_right_le ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf x y z hx hy hz hyz

lemma convex_on.le_right_of_left_le (hf : convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hxz : f x â‰¤ f z) :
  f z â‰¤ f y :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.le_right_of_left_le' hx hy ha.le hb hab hxz,
end

lemma concave_on.le_right_of_left_le (hf : concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hxz : f z â‰¤ f x) :
  f y â‰¤ f z :=
@convex_on.le_right_of_left_le ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ f hf x y z hx hy hz hxz

end ordered_smul
end linear_ordered_cancel_add_comm_monoid

section ordered_add_comm_group
variables [ordered_add_comm_group Î²] [has_scalar ğ•œ E] [module ğ•œ Î²] {s : set E} {f : E â†’ Î²}

/-- A function `-f` is convex iff `f` is concave. -/
@[simp] lemma neg_convex_on_iff :
  convex_on ğ•œ s (-f) â†” concave_on ğ•œ s f :=
begin
  split,
  { rintro âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, Î» x y hx hy a b ha hb hab, _âŸ©,
    simp [neg_apply, neg_le, add_comm] at h,
    exact h hx hy ha hb hab },
  { rintro âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, Î» x y hx hy a b ha hb hab, _âŸ©,
    rw â†neg_le_neg_iff,
    simp_rw [neg_add, pi.neg_apply, smul_neg, neg_neg],
    exact h hx hy ha hb hab }
end

/-- A function `-f` is concave iff `f` is convex. -/
@[simp] lemma neg_concave_on_iff : concave_on ğ•œ s (-f) â†” convex_on ğ•œ s f:=
by rw [â† neg_convex_on_iff, neg_neg f]

alias neg_convex_on_iff â†” _ concave_on.neg
alias neg_concave_on_iff â†” _ convex_on.neg

end ordered_add_comm_group
end ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ] [add_comm_monoid E]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section module
variables [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma convex_on.smul {c : ğ•œ} (hc : 0 â‰¤ c)
  (hf : convex_on ğ•œ s f) : convex_on ğ•œ s (Î» x, c â€¢ f x) :=
âŸ¨hf.1, Î» x y hx hy a b ha hb hab,
  calc
    c â€¢ f (a â€¢ x + b â€¢ y) â‰¤ c â€¢ (a â€¢ f x + b â€¢ f y)
      : smul_le_smul_of_nonneg (hf.2 hx hy ha hb hab) hc
    ... = a â€¢ (c â€¢ f x) + b â€¢ (c â€¢ f y)
      : by rw [smul_add, smul_comm c, smul_comm c]; apply_instanceâŸ©

lemma concave_on.smul {c : ğ•œ} (hc : 0 â‰¤ c)
  (hf : concave_on ğ•œ s f) : concave_on ğ•œ s (Î» x, c â€¢ f x) :=
@convex_on.smul _ _ (order_dual Î²) _ _ _ _ _ _ _ f c hc hf

end module
end ordered_add_comm_monoid
end ordered_comm_semiring

section ordered_ring
variables [linear_ordered_field ğ•œ] [add_comm_group E] [add_comm_group F]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section module
variables [module ğ•œ E] [module ğ•œ F] [has_scalar ğ•œ Î²]

/-- If a function is convex on `s`, it remains convex when precomposed by an affine map. -/
lemma convex_on.comp_affine_map {f : F â†’ Î²} (g : E â†’áµƒ[ğ•œ] F) {s : set F} (hf : convex_on ğ•œ s f) :
  convex_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
âŸ¨hf.1.affine_preimage _, Î» x y hx hy a b ha hb hab,
  calc
    (f âˆ˜ g) (a â€¢ x + b â€¢ y) = f (g (a â€¢ x + b â€¢ y))         : rfl
                       ...  = f (a â€¢ (g x) + b â€¢ (g y))     : by rw [convex.combo_affine_apply hab]
                       ...  â‰¤ a â€¢ f (g x) + b â€¢ f (g y)     : hf.2 hx hy ha hb habâŸ©

/-- If a function is concave on `s`, it remains concave when precomposed by an affine map. -/
lemma concave_on.comp_affine_map {f : F â†’ Î²} (g : E â†’áµƒ[ğ•œ] F) {s : set F} (hf : concave_on ğ•œ s f) :
  concave_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
@convex_on.comp_affine_map _ _ _ (order_dual Î²) _ _ _ _ _ _ _ f g s hf

end module
end ordered_add_comm_monoid
end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ] [add_comm_monoid E]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section has_scalar
variables [has_scalar ğ•œ E] [has_scalar ğ•œ Î²] {s : set E}

lemma convex_on_iff_div {f : E â†’ Î²} :
  convex_on ğ•œ s f â†” convex ğ•œ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b
  â†’ f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) â‰¤ (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y :=
and_congr iff.rfl
âŸ¨begin
  intros h x y hx hy a b ha hb hab,
  apply h hx hy (div_nonneg ha hab.le) (div_nonneg hb hab.le),
  rw [â†add_div, div_self hab.ne'],
end,
begin
  intros h x y hx hy a b ha hb hab,
  simpa [hab, zero_lt_one] using h hx hy ha hb,
endâŸ©

lemma concave_on_iff_div {f : E â†’ Î²} :
  concave_on ğ•œ s f â†” convex ğ•œ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b
  â†’ 0 < a + b â†’ (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y â‰¤ f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) :=
@convex_on_iff_div _ _ (order_dual Î²) _ _ _ _ _ _ _

/-- For a function `f` defined on a convex subset `D` of `ğ•œ`, if for any three points `x < y < z`
the slope of the secant line of `f` on `[x, y]` is less than or equal to the slope
of the secant line of `f` on `[x, z]`, then `f` is convex on `D`. This way of proving convexity
of a function is used in the proof of convexity of a function with a monotone derivative. -/
lemma convex_on_of_slope_mono_adjacent {s : set ğ•œ} (hs : convex ğ•œ s) {f : ğ•œ â†’ ğ•œ}
  (hf : âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)) :
  convex_on ğ•œ s f :=
linear_order.convex_on_of_lt hs
begin
  assume x z hx hz hxz a b ha hb hab,
  let y := a * x + b * z,
  have hxy : x < y,
  { rw [â† one_mul x, â† hab, add_mul],
    exact add_lt_add_left ((mul_lt_mul_left hb).2 hxz) _ },
  have hyz : y < z,
  { rw [â† one_mul z, â† hab, add_mul],
    exact add_lt_add_right ((mul_lt_mul_left ha).2 hxz) _ },
  have : (f y - f x) * (z - y) â‰¤ (f z - f y) * (y - x),
    from (div_le_div_iff (sub_pos.2 hxy) (sub_pos.2 hyz)).1 (hf hx hz hxy hyz),
  have hxz : 0 < z - x, from sub_pos.2 (hxy.trans hyz),
  have ha : (z - y) / (z - x) = a,
  { rw [eq_comm, â† sub_eq_iff_eq_add'] at hab,
    simp_rw [div_eq_iff hxz.ne', y, â†hab], ring },
  have hb : (y - x) / (z - x) = b,
  { rw [eq_comm, â† sub_eq_iff_eq_add] at hab,
    simp_rw [div_eq_iff hxz.ne', y, â†hab], ring },
  rwa [sub_mul, sub_mul, sub_le_iff_le_add', â† add_sub_assoc, le_sub_iff_add_le, â† mul_add,
    sub_add_sub_cancel, â† le_div_iff hxz, add_div, mul_div_assoc, mul_div_assoc, mul_comm (f x),
    mul_comm (f z), ha, hb] at this,
end

/-- For a function `f` defined on a subset `D` of `ğ•œ`, if `f` is convex on `D`, then for any three
points `x < y < z`, the slope of the secant line of `f` on `[x, y]` is less than or equal to the
slope of the secant line of `f` on `[x, z]`. -/
lemma convex_on.slope_mono_adjacent {s : set ğ•œ} {f : ğ•œ â†’ ğ•œ} (hf : convex_on ğ•œ s f)
  {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y) :=
begin
  have hâ‚ : 0 < y - x := by linarith,
  have hâ‚‚ : 0 < z - y := by linarith,
  have hâ‚ƒ : 0 < z - x := by linarith,
  suffices : f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y),
  { ring_nf at this âŠ¢, linarith },
  set a := (z - y) / (z - x),
  set b := (y - x) / (z - x),
  have heqz : a â€¢ x + b â€¢ z = y, by { field_simp, rw div_eq_iff; [ring, linarith] },
  have key, from
    hf.2 hx hz
      (show 0 â‰¤ a, by apply div_nonneg; linarith)
      (show 0 â‰¤ b, by apply div_nonneg; linarith)
      (show a + b = 1, by { field_simp, rw div_eq_iff; [ring, linarith] }),
  rw heqz at key,
  replace key := mul_le_mul_of_nonneg_left key hâ‚ƒ.le,
  field_simp [hâ‚.ne', hâ‚‚.ne', hâ‚ƒ.ne', mul_comm (z - x) _] at key âŠ¢,
  rw div_le_div_right,
  { linarith },
  { nlinarith }
end

/-- For a function `f` defined on a convex subset `D` of `ğ•œ`, `f` is convex on `D` iff, for any
three points `x < y < z` the slope of the secant line of `f` on `[x, y]` is less than or equal to
the slope,of the secant line of `f` on `[x, z]`. -/
lemma convex_on_iff_slope_mono_adjacent {s : set ğ•œ} (hs : convex ğ•œ s) {f : ğ•œ â†’ ğ•œ} :
  convex_on ğ•œ s f â†”
  (âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)) :=
âŸ¨convex_on.slope_mono_adjacent, convex_on_of_slope_mono_adjacent hsâŸ©

/-- For a function `f` defined on a convex subset `D` of `ğ•œ`, if for any three points `x < y < z`
the slope of the secant line of `f` on `[x, y]` is greater than or equal to the slope
of the secant line of `f` on `[x, z]`, then `f` is concave on `D`. -/
lemma concave_on_of_slope_mono_adjacent {s : set ğ•œ} (hs : convex ğ•œ s) {f : ğ•œ â†’ ğ•œ}
  (hf : âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)) : concave_on ğ•œ s f :=
begin
  rw â†neg_convex_on_iff,
  refine convex_on_of_slope_mono_adjacent hs (Î» x y z hx hz hxy hyz, _),
  rw â†neg_le_neg_iff,
  simp_rw [â†neg_div, neg_sub, pi.neg_apply, neg_sub_neg],
  exact hf hx hz hxy hyz,
end

/-- For a function `f` defined on a subset `D` of `ğ•œ`, if `f` is concave on `D`, then for any three
points `x < y < z`, the slope of the secant line of `f` on `[x, y]` is greater than or equal to the
slope of the secant line of `f` on `[x, z]`. -/
lemma concave_on.slope_mono_adjacent {s : set ğ•œ} {f : ğ•œ â†’ ğ•œ} (hf : concave_on ğ•œ s f)
  {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x) :=
begin
  rw [â†neg_le_neg_iff, â†neg_sub_neg (f x), â†neg_sub_neg (f y)],
  simp_rw [â†pi.neg_apply, â†neg_div, neg_sub],
  exact convex_on.slope_mono_adjacent hf.neg hx hz hxy hyz,
end

/-- For a function `f` defined on a convex subset `D` of `ğ•œ`, `f` is concave on `D` iff for any
three points `x < y < z` the slope of the secant line of `f` on `[x, y]` is greater than or equal to
the slope of the secant line of `f` on `[x, z]`. -/
lemma concave_on_iff_slope_mono_adjacent {s : set ğ•œ} (hs : convex ğ•œ s) {f : ğ•œ â†’ ğ•œ} :
  concave_on ğ•œ s f â†”
  (âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)) :=
âŸ¨concave_on.slope_mono_adjacent, concave_on_of_slope_mono_adjacent hsâŸ©

end has_scalar
end ordered_add_comm_monoid
end linear_ordered_field







/-! ### Jensen's inequality -/

section jensen
variables [linear_ordered_field ğ•œ] [add_comm_group E] [ordered_add_comm_group Î²] [module ğ•œ E]
  [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²} {t : finset Î¹} {w : Î¹ â†’ ğ•œ} {p : Î¹ â†’ E}

/-- Convex **Jensen's inequality**, `finset.center_mass` version. -/
lemma convex_on.map_center_mass_le (hf : convex_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
  (hâ‚ : 0 < âˆ‘ i in t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  f (t.center_mass w p) â‰¤ t.center_mass w (f âˆ˜ p) :=
begin
  have hmem' : âˆ€ i âˆˆ t, (p i, (f âˆ˜ p) i) âˆˆ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2},
    from Î» i hi, âŸ¨hmem i hi, le_rflâŸ©,
  convert (hf.convex_epigraph.center_mass_mem hâ‚€ hâ‚ hmem').2;
    simp only [center_mass, function.comp, prod.smul_fst, prod.fst_sum,
      prod.smul_snd, prod.snd_sum],
end

/-- Concave **Jensen's inequality**, `finset.center_mass` version. -/
lemma concave_on.le_map_center_mass (hf : concave_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
  (hâ‚ : 0 < âˆ‘ i in t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  t.center_mass w (f âˆ˜ p) â‰¤ f (t.center_mass w p) :=
@convex_on.map_center_mass_le ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ _ _ _ _ _ hf hâ‚€ hâ‚ hmem

/-- Convex **Jensen's inequality**, `finset.sum` version. -/
lemma convex_on.map_sum_le (hf : convex_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i in t, w i = 1)
  (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  f (âˆ‘ i in t, w i â€¢ p i) â‰¤ âˆ‘ i in t, w i â€¢ f (p i) :=
by simpa only [center_mass, hâ‚, inv_one, one_smul]
  using hf.map_center_mass_le hâ‚€ (hâ‚.symm â–¸ zero_lt_one) hmem

/-- Concave **Jensen's inequality**, `finset.sum` version. -/
lemma concave_on.le_map_sum (hf : concave_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i in t, w i = 1)
  (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  âˆ‘ i in t, w i â€¢ f (p i) â‰¤ f (âˆ‘ i in t, w i â€¢ p i) :=
@convex_on.map_sum_le ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ _ _ _ _ _ hf hâ‚€ hâ‚ hmem

end jensen

/-! ### Maximum principle -/

section maximum_principle
variables [linear_ordered_field ğ•œ] [add_comm_group E] [linear_ordered_add_comm_group Î²]
  [module ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²} {t : finset Î¹} {w : Î¹ â†’ ğ•œ}
  {p : Î¹ â†’ E}

/-- If a function `f` is convex on `s`, then the value it takes at some center of mass of points of
`s` is less than the value it takes on one of those points. -/
lemma convex_on.exists_ge_of_center_mass (h : convex_on ğ•œ s f)
  (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i in t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) :
  âˆƒ i âˆˆ t, f (t.center_mass w p) â‰¤ f (p i) :=
begin
  set y := t.center_mass w p,
  suffices h : âˆƒ i âˆˆ t.filter (Î» i, w i â‰  0), w i â€¢ f y â‰¤ w i â€¢ (f âˆ˜ p) i,
  { obtain âŸ¨i, hi, hfiâŸ© := h,
    rw mem_filter at hi,
    exact âŸ¨i, hi.1, (smul_le_smul_iff_of_pos $ (hwâ‚€ i hi.1).lt_of_ne hi.2.symm).1 hfiâŸ© },
  have hw' : (0 : ğ•œ) < âˆ‘ i in filter (Î» i, w i â‰  0) t, w i := by rwa sum_filter_ne_zero,
  refine exists_le_of_sum_le (nonempty_of_sum_ne_zero hw'.ne') _,
  rw [â†sum_smul, â†smul_le_smul_iff_of_pos (inv_pos.2 hw'), inv_smul_smul' hw'.ne',
    â†finset.center_mass, finset.center_mass_filter_ne_zero],
  exact h.map_center_mass_le hwâ‚€ hwâ‚ hp,
  apply_instance,
end

/-- If a function `f` is concave on `s`, then the value it takes at some center of mass of points of
`s` is greater than the value it takes on one of those points. -/
lemma concave_on.exists_le_of_center_mass (h : concave_on ğ•œ s f)
  (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i in t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) :
  âˆƒ i âˆˆ t, f (p i) â‰¤ f (t.center_mass w p) :=
@convex_on.exists_ge_of_center_mass ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ _ _ _ _ _ h hwâ‚€ hwâ‚ hp

/-- Maximum principle for convex functions. If a function `f` is convex on the convex hull of `s`,
then the eventual maximum of `f` on `convex_hull ğ•œ s` lies in `s`. -/
lemma convex_on.exists_ge_of_mem_convex_hull (hf : convex_on ğ•œ (convex_hull ğ•œ s) f) {x}
  (hx : x âˆˆ convex_hull ğ•œ s) : âˆƒ y âˆˆ s, f x â‰¤ f y :=
begin
  rw _root_.convex_hull_eq at hx,
  obtain âŸ¨Î±, t, w, p, hwâ‚€, hwâ‚, hp, rflâŸ© := hx,
  rcases hf.exists_ge_of_center_mass hwâ‚€ (hwâ‚.symm â–¸ zero_lt_one)
    (Î» i hi, subset_convex_hull ğ•œ s (hp i hi)) with âŸ¨i, hit, HiâŸ©,
  exact âŸ¨p i, hp i hit, HiâŸ©
end

/-- Minimum principle for concave functions. If a function `f` is concave on the convex hull of `s`,
then the eventual minimum of `f` on `convex_hull ğ•œ s` lies in `s`. -/
lemma concave_on.exists_le_of_mem_convex_hull (hf : concave_on ğ•œ (convex_hull ğ•œ s) f) {x}
  (hx : x âˆˆ convex_hull ğ•œ s) : âˆƒ y âˆˆ s, f y â‰¤ f x :=
@convex_on.exists_ge_of_mem_convex_hull ğ•œ E (order_dual Î²) _ _ _ _ _ _ _ _ hf _ hx

end maximum_principle
