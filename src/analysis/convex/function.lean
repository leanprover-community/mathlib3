/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, FranÃ§ois Dupuis
-/
import analysis.convex.basic
import tactic.field_simp
import tactic.linarith
import tactic.ring

/-!
# Convex and concave functions

This file defines convex and concave functions in vector spaces and proves the finite Jensen
inequality. The integral version can be found in `analysis.convex.integral`.

A function `f : E â†’ Î²` is `convex_on` a set `s` if `s` is itself a convex set, and for any two
points `x y âˆˆ s`, the segment joining `(x, f x)` to `(y, f y)` is above the graph of `f`.
Equivalently, `convex_on ğ•œ f s` means that the epigraph `{p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2}` is
a convex set.

## Main declarations

* `convex_on ğ•œ s f`: The function `f` is convex on `s` with scalars `ğ•œ`.
* `concave_on ğ•œ s f`: The function `f` is concave on `s` with scalars `ğ•œ`.
* `strict_convex_on ğ•œ s f`: The function `f` is strictly convex on `s` with scalars `ğ•œ`.
* `strict_concave_on ğ•œ s f`: The function `f` is strictly concave on `s` with scalars `ğ•œ`.
-/

open finset linear_map set
open_locale big_operators classical convex pointwise

variables {ğ•œ E F Î² Î¹ : Type*}

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section has_scalar
variables (ğ•œ) [has_scalar ğ•œ E] [has_scalar ğ•œ Î²] (s : set E) (f : E â†’ Î²)

/-- Convexity of functions -/
def convex_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y

/-- Concavity of functions -/
def concave_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)

/-- Strict convexity of functions -/
def strict_convex_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y

/-- Strict concavity of functions -/
def strict_concave_on : Prop :=
convex ğ•œ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    a â€¢ f x + b â€¢ f y < f (a â€¢ x + b â€¢ y)

variables {ğ•œ s f}

open order_dual (to_dual of_dual)

lemma convex_on.dual (hf : convex_on ğ•œ s f) : concave_on ğ•œ s (to_dual âˆ˜ f) := hf

lemma concave_on.dual (hf : concave_on ğ•œ s f) : convex_on ğ•œ s (to_dual âˆ˜ f) := hf

lemma strict_convex_on.dual (hf : strict_convex_on ğ•œ s f) : strict_concave_on ğ•œ s (to_dual âˆ˜ f) :=
hf

lemma strict_concave_on.dual (hf : strict_concave_on ğ•œ s f) : strict_convex_on ğ•œ s (to_dual âˆ˜ f) :=
hf

lemma convex_on_id {s : set Î²} (hs : convex ğ•œ s) : convex_on ğ•œ s id := âŸ¨hs, by { intros, refl }âŸ©

lemma concave_on_id {s : set Î²} (hs : convex ğ•œ s) : concave_on ğ•œ s id := âŸ¨hs, by { intros, refl }âŸ©

lemma convex_on.subset {t : set E} (hf : convex_on ğ•œ t f) (hst : s âŠ† t) (hs : convex ğ•œ s) :
  convex_on ğ•œ s f :=
âŸ¨hs, Î» x y hx hy, hf.2 (hst hx) (hst hy)âŸ©

lemma concave_on.subset {t : set E} (hf : concave_on ğ•œ t f) (hst : s âŠ† t) (hs : convex ğ•œ s) :
  concave_on ğ•œ s f :=
âŸ¨hs, Î» x y hx hy, hf.2 (hst hx) (hst hy)âŸ©

lemma strict_convex_on.subset {t : set E} (hf : strict_convex_on ğ•œ t f) (hst : s âŠ† t)
  (hs : convex ğ•œ s) :
  strict_convex_on ğ•œ s f :=
âŸ¨hs, Î» x y hx hy, hf.2 (hst hx) (hst hy)âŸ©

lemma strict_concave_on.subset {t : set E} (hf : strict_concave_on ğ•œ t f) (hst : s âŠ† t)
  (hs : convex ğ•œ s) :
  strict_concave_on ğ•œ s f :=
âŸ¨hs, Î» x y hx hy, hf.2 (hst hx) (hst hy)âŸ©

end has_scalar

section distrib_mul_action
variables [has_scalar ğ•œ E] [distrib_mul_action ğ•œ Î²] {s : set E} {f g : E â†’ Î²}

lemma convex_on.add (hf : convex_on ğ•œ s f) (hg : convex_on ğ•œ s g) :
  convex_on ğ•œ s (f + g) :=
âŸ¨hf.1, Î» x y hx hy a b ha hb hab,
  calc
    f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) â‰¤ (a â€¢ f x + b â€¢ f y) + (a â€¢ g x + b â€¢ g y)
      : add_le_add (hf.2 hx hy ha hb hab) (hg.2 hx hy ha hb hab)
    ... = a â€¢ (f x + g x) + b â€¢ (f y + g y) : by rw [smul_add, smul_add, add_add_add_comm]âŸ©

lemma concave_on.add (hf : concave_on ğ•œ s f) (hg : concave_on ğ•œ s g) :
  concave_on ğ•œ s (f + g) :=
hf.dual.add hg

end distrib_mul_action

section module
variables [has_scalar ğ•œ E] [module ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma convex_on_const (c : Î²) (hs : convex ğ•œ s) : convex_on ğ•œ s (Î» x:E, c) :=
âŸ¨hs, Î» x y _ _ a b _ _ hab, (convex.combo_self hab c).geâŸ©

lemma concave_on_const (c : Î²) (hs : convex ğ•œ s) : concave_on ğ•œ s (Î» x:E, c) :=
@convex_on_const _ _ Î²áµ’áµˆ _ _ _ _ _ _ c hs

lemma convex_on_of_convex_epigraph (h : convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2}) :
  convex_on ğ•œ s f :=
âŸ¨Î» x y hx hy a b ha hb hab, (@h (x, f x) (y, f y) âŸ¨hx, le_rflâŸ© âŸ¨hy, le_rflâŸ© a b ha hb hab).1,
  Î» x y hx hy a b ha hb hab, (@h (x, f x) (y, f y) âŸ¨hx, le_rflâŸ© âŸ¨hy, le_rflâŸ© a b ha hb hab).2âŸ©

lemma concave_on_of_convex_hypograph (h : convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1}) :
  concave_on ğ•œ s f :=
@convex_on_of_convex_epigraph ğ•œ  E Î²áµ’áµˆ _ _ _ _ _ _ _ h

end module

section ordered_smul
variables [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma convex_on.convex_le (hf : convex_on ğ•œ s f) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x â‰¤ r} :=
Î» x y hx hy a b ha hb hab, âŸ¨hf.1 hx.1 hy.1 ha hb hab,
  calc
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx.1 hy.1 ha hb hab
                  ... â‰¤ a â€¢ r + b â€¢ r     : add_le_add (smul_le_smul_of_nonneg hx.2 ha)
                                              (smul_le_smul_of_nonneg hy.2 hb)
                  ... = r                 : convex.combo_self hab râŸ©

lemma concave_on.convex_ge (hf : concave_on ğ•œ s f) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r â‰¤ f x} :=
hf.dual.convex_le r

lemma convex_on.convex_epigraph (hf : convex_on ğ•œ s f) :
  convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} :=
begin
  rintro âŸ¨x, râŸ© âŸ¨y, tâŸ© âŸ¨hx, hrâŸ© âŸ¨hy, htâŸ© a b ha hb hab,
  refine âŸ¨hf.1 hx hy ha hb hab, _âŸ©,
  calc f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha hb hab
  ... â‰¤ a â€¢ r + b â€¢ t : add_le_add (smul_le_smul_of_nonneg hr ha)
                            (smul_le_smul_of_nonneg ht hb)
end

lemma concave_on.convex_hypograph (hf : concave_on ğ•œ s f) :
  convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1} :=
hf.dual.convex_epigraph

lemma convex_on_iff_convex_epigraph :
  convex_on ğ•œ s f â†” convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} :=
âŸ¨convex_on.convex_epigraph, convex_on_of_convex_epigraphâŸ©

lemma concave_on_iff_convex_hypograph :
  concave_on ğ•œ s f â†” convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1} :=
@convex_on_iff_convex_epigraph ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ f

end ordered_smul

section module
variables [module ğ•œ E] [has_scalar ğ•œ Î²] {s : set E} {f : E â†’ Î²}

/-- Right translation preserves convexity. -/
lemma convex_on.translate_right (hf : convex_on ğ•œ s f) (c : E) :
  convex_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, c + z)) :=
âŸ¨hf.1.translate_preimage_right _, Î» x y hx hy a b ha hb hab,
  calc
    f (c + (a â€¢ x + b â€¢ y)) = f (a â€¢ (c + x) + b â€¢ (c + y))
        : by rw [smul_add, smul_add, add_add_add_comm, convex.combo_self hab]
    ... â‰¤ a â€¢ f (c + x) + b â€¢ f (c + y) : hf.2 hx hy ha hb habâŸ©

/-- Right translation preserves concavity. -/
lemma concave_on.translate_right (hf : concave_on ğ•œ s f) (c : E) :
  concave_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, c + z)) :=
hf.dual.translate_right _

/-- Left translation preserves convexity. -/
lemma convex_on.translate_left (hf : convex_on ğ•œ s f) (c : E) :
  convex_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, z + c)) :=
by simpa only [add_comm] using hf.translate_right _

/-- Left translation preserves concavity. -/
lemma concave_on.translate_left (hf : concave_on ğ•œ s f) (c : E) :
  concave_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, z + c)) :=
hf.dual.translate_left _

end module

section module
variables [module ğ•œ E] [module ğ•œ Î²]

lemma convex_on_iff_forall_pos {s : set E} {f : E â†’ Î²} :
  convex_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1
    â†’ f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y :=
begin
  refine and_congr_right' âŸ¨Î» h x y hx hy a b ha hb hab, h hx hy ha.le hb.le hab,
    Î» h x y hx hy a b ha hb hab, _âŸ©,
  obtain rfl | ha' := ha.eq_or_lt,
  { rw [zero_add] at hab, subst b, simp_rw [zero_smul, zero_add, one_smul] },
  obtain rfl | hb' := hb.eq_or_lt,
  { rw [add_zero] at hab, subst a, simp_rw [zero_smul, add_zero, one_smul] },
  exact h hx hy ha' hb' hab,
end

lemma concave_on_iff_forall_pos {s : set E} {f : E â†’ Î²} :
  concave_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1
    â†’ a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y) :=
@convex_on_iff_forall_pos ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _

lemma convex_on_iff_pairwise_pos {s : set E} {f : E â†’ Î²} :
  convex_on ğ•œ s f â†” convex ğ•œ s âˆ§
    s.pairwise (Î» x y, âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1
    â†’ f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y) :=
begin
  rw convex_on_iff_forall_pos,
  refine and_congr_right' âŸ¨Î» h x hx y hy _ a b ha hb hab, h hx hy ha hb hab,
    Î» h x y hx hy a b ha hb hab, _âŸ©,
  obtain rfl | hxy := eq_or_ne x y,
  { rw [convex.combo_self hab, convex.combo_self hab] },
  exact h hx hy hxy ha hb hab,
end

lemma concave_on_iff_pairwise_pos {s : set E} {f : E â†’ Î²} :
  concave_on ğ•œ s f â†” convex ğ•œ s âˆ§
   s.pairwise (Î» x y, âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1
    â†’ a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)) :=
@convex_on_iff_pairwise_pos ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _

/-- A linear map is convex. -/
lemma linear_map.convex_on (f : E â†’â‚—[ğ•œ] Î²) {s : set E} (hs : convex ğ•œ s) : convex_on ğ•œ s f :=
âŸ¨hs, Î» _ _ _ _ _ _ _ _ _, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

/-- A linear map is concave. -/
lemma linear_map.concave_on (f : E â†’â‚—[ğ•œ] Î²) {s : set E} (hs : convex ğ•œ s) : concave_on ğ•œ s f :=
âŸ¨hs, Î» _ _ _ _ _ _ _ _ _, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

lemma strict_convex_on.convex_on {s : set E} {f : E â†’ Î²} (hf : strict_convex_on ğ•œ s f) :
  convex_on ğ•œ s f :=
convex_on_iff_pairwise_pos.mpr âŸ¨hf.1, Î» x hx y hy hxy a b ha hb hab, (hf.2 hx hy hxy ha hb hab).leâŸ©

lemma strict_concave_on.concave_on {s : set E} {f : E â†’ Î²} (hf : strict_concave_on ğ•œ s f) :
  concave_on ğ•œ s f :=
hf.dual.convex_on

section ordered_smul
variables [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma strict_convex_on.convex_lt (hf : strict_convex_on ğ•œ s f) (r : Î²) :
  convex ğ•œ {x âˆˆ s | f x < r} :=
convex_iff_pairwise_pos.2 $ Î» x hx y hy hxy a b ha hb hab, âŸ¨hf.1 hx.1 hy.1 ha.le hb.le hab,
  calc
    f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y : hf.2 hx.1 hy.1 hxy ha hb hab
                  ... â‰¤ a â€¢ r + b â€¢ r     : add_le_add (smul_lt_smul_of_pos hx.2 ha).le
                                              (smul_lt_smul_of_pos hy.2 hb).le
                  ... = r                 : convex.combo_self hab râŸ©

lemma strict_concave_on.convex_gt (hf : strict_concave_on ğ•œ s f) (r : Î²) :
  convex ğ•œ {x âˆˆ s | r < f x} :=
hf.dual.convex_lt r

end ordered_smul

section linear_order
variables [linear_order E] {s : set E} {f : E â†’ Î²}

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is convex, it suffices to
verify the inequality `f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y` only for `x < y` and positive `a`,
`b`. The main use case is `E = ğ•œ` however one can apply it, e.g., to `ğ•œ^n` with lexicographic order.
-/
lemma linear_order.convex_on_of_lt (hs : convex ğ•œ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y) : convex_on ğ•œ s f :=
begin
  refine convex_on_iff_pairwise_pos.2 âŸ¨hs, Î» x hx y hy hxy a b ha hb hab, _âŸ©,
  wlog h : x â‰¤ y using [x y a b, y x b a],
  { exact le_total _ _ },
  exact hf hx hy (h.lt_of_ne hxy) ha hb hab,
end

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is concave it suffices to
verify the inequality `a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)` for `x < y` and positive `a`, `b`. The
main use case is `E = â„` however one can apply it, e.g., to `â„^n` with lexicographic order. -/
lemma linear_order.concave_on_of_lt (hs : convex ğ•œ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
     a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)) : concave_on ğ•œ s f :=
@linear_order.convex_on_of_lt _ _ Î²áµ’áµˆ _ _ _ _ _ _ s f hs hf

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is convex, it suffices to
verify the inequality `f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y` for `x < y` and positive `a`, `b`. The
main use case is `E = ğ•œ` however one can apply it, e.g., to `ğ•œ^n` with lexicographic order. -/
lemma linear_order.strict_convex_on_of_lt (hs : convex ğ•œ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y) : strict_convex_on ğ•œ s f :=
begin
  refine âŸ¨hs, Î» x y hx hy hxy a b ha hb hab, _âŸ©,
  wlog h : x â‰¤ y using [x y a b, y x b a],
  { exact le_total _ _ },
  exact hf hx hy (h.lt_of_ne hxy) ha hb hab,
end

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is concave it suffices to
verify the inequality `a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)` for `x < y` and positive `a`, `b`. The
main use case is `E = ğ•œ` however one can apply it, e.g., to `ğ•œ^n` with lexicographic order. -/
lemma linear_order.strict_concave_on_of_lt (hs : convex ğ•œ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
     a â€¢ f x + b â€¢ f y < f (a â€¢ x + b â€¢ y)) : strict_concave_on ğ•œ s f :=
@linear_order.strict_convex_on_of_lt _ _ Î²áµ’áµˆ _ _ _ _ _ _ _ _ hs hf

end linear_order
end module

section module
variables [module ğ•œ E] [module ğ•œ F] [has_scalar ğ•œ Î²]

/-- If `g` is convex on `s`, so is `(f âˆ˜ g)` on `f â»Â¹' s` for a linear `f`. -/
lemma convex_on.comp_linear_map {f : F â†’ Î²} {s : set F} (hf : convex_on ğ•œ s f) (g : E â†’â‚—[ğ•œ] F) :
  convex_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
âŸ¨hf.1.linear_preimage _, Î» x y hx hy a b ha hb hab,
  calc
    f (g (a â€¢ x + b â€¢ y)) = f (a â€¢ (g x) + b â€¢ (g y)) : by rw [g.map_add, g.map_smul, g.map_smul]
                      ... â‰¤ a â€¢ f (g x) + b â€¢ f (g y) : hf.2 hx hy ha hb habâŸ©

/-- If `g` is concave on `s`, so is `(g âˆ˜ f)` on `f â»Â¹' s` for a linear `f`. -/
lemma concave_on.comp_linear_map {f : F â†’ Î²} {s : set F} (hf : concave_on ğ•œ s f) (g : E â†’â‚—[ğ•œ] F) :
  concave_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
hf.dual.comp_linear_map g

end module
end ordered_add_comm_monoid

section ordered_cancel_add_comm_monoid
variables [ordered_cancel_add_comm_monoid Î²]

section distrib_mul_action
variables [has_scalar ğ•œ E] [distrib_mul_action ğ•œ Î²] {s : set E} {f g : E â†’ Î²}

lemma strict_convex_on.add_convex_on (hf : strict_convex_on ğ•œ s f) (hg : convex_on ğ•œ s g) :
  strict_convex_on ğ•œ s (f + g) :=
âŸ¨hf.1, Î» x y hx hy hxy a b ha hb hab,
  calc
    f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) < (a â€¢ f x + b â€¢ f y) + (a â€¢ g x + b â€¢ g y)
      : add_lt_add_of_lt_of_le (hf.2 hx hy hxy ha hb hab) (hg.2 hx hy ha.le hb.le hab)
    ... = a â€¢ (f x + g x) + b â€¢ (f y + g y) : by rw [smul_add, smul_add, add_add_add_comm]âŸ©

lemma convex_on.add_strict_convex_on (hf : convex_on ğ•œ s f) (hg : strict_convex_on ğ•œ s g) :
  strict_convex_on ğ•œ s (f + g) :=
(add_comm g f) â–¸ hg.add_convex_on hf

lemma strict_convex_on.add (hf : strict_convex_on ğ•œ s f) (hg : strict_convex_on ğ•œ s g) :
  strict_convex_on ğ•œ s (f + g) :=
âŸ¨hf.1, Î» x y hx hy hxy a b ha hb hab,
  calc
    f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) < (a â€¢ f x + b â€¢ f y) + (a â€¢ g x + b â€¢ g y)
      : add_lt_add (hf.2 hx hy hxy ha hb hab) (hg.2 hx hy hxy ha hb hab)
    ... = a â€¢ (f x + g x) + b â€¢ (f y + g y) : by rw [smul_add, smul_add, add_add_add_comm]âŸ©

lemma strict_concave_on.add_concave_on (hf : strict_concave_on ğ•œ s f) (hg : concave_on ğ•œ s g) :
  strict_concave_on ğ•œ s (f + g) :=
hf.dual.add_convex_on hg.dual

lemma concave_on.add_strict_concave_on (hf : concave_on ğ•œ s f) (hg : strict_concave_on ğ•œ s g) :
  strict_concave_on ğ•œ s (f + g) :=
hf.dual.add_strict_convex_on hg.dual

lemma strict_concave_on.add (hf : strict_concave_on ğ•œ s f) (hg : strict_concave_on ğ•œ s g) :
  strict_concave_on ğ•œ s (f + g) :=
hf.dual.add hg

end distrib_mul_action

section module
variables [module ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma convex_on.convex_lt (hf : convex_on ğ•œ s f) (r : Î²) : convex ğ•œ {x âˆˆ s | f x < r} :=
convex_iff_forall_pos.2 $ Î» x y hx hy a b ha hb hab, âŸ¨hf.1 hx.1 hy.1 ha.le hb.le hab,
  calc
    f (a â€¢ x + b â€¢ y)
        â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx.1 hy.1 ha.le hb.le hab
    ... < a â€¢ r + b â€¢ r     : add_lt_add_of_lt_of_le (smul_lt_smul_of_pos hx.2 ha)
                                (smul_le_smul_of_nonneg hy.2.le hb.le)
    ... = r                 : convex.combo_self hab _âŸ©

lemma concave_on.convex_gt (hf : concave_on ğ•œ s f) (r : Î²) : convex ğ•œ {x âˆˆ s | r < f x} :=
hf.dual.convex_lt r

lemma convex_on.open_segment_subset_strict_epigraph (hf : convex_on ğ•œ s f) (p q : E Ã— Î²)
  (hp : p.1 âˆˆ s âˆ§ f p.1 < p.2) (hq : q.1 âˆˆ s âˆ§ f q.1 â‰¤ q.2) :
  open_segment ğ•œ p q âŠ† {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 < p.2} :=
begin
  rintro _ âŸ¨a, b, ha, hb, hab, rflâŸ©,
  refine âŸ¨hf.1 hp.1 hq.1 ha.le hb.le hab, _âŸ©,
  calc f (a â€¢ p.1 + b â€¢ q.1) â‰¤ a â€¢ f p.1 + b â€¢ f q.1 : hf.2 hp.1 hq.1 ha.le hb.le hab
  ... < a â€¢ p.2 + b â€¢ q.2 :
    add_lt_add_of_lt_of_le (smul_lt_smul_of_pos hp.2 ha) (smul_le_smul_of_nonneg hq.2 hb.le)
end

lemma concave_on.open_segment_subset_strict_hypograph (hf : concave_on ğ•œ s f) (p q : E Ã— Î²)
  (hp : p.1 âˆˆ s âˆ§ p.2 < f p.1) (hq : q.1 âˆˆ s âˆ§ q.2 â‰¤ f q.1) :
  open_segment ğ•œ p q âŠ† {p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 < f p.1} :=
hf.dual.open_segment_subset_strict_epigraph p q hp hq

lemma convex_on.convex_strict_epigraph (hf : convex_on ğ•œ s f) :
  convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 < p.2} :=
convex_iff_open_segment_subset.mpr $
  Î» p q hp hq, hf.open_segment_subset_strict_epigraph p q hp âŸ¨hq.1, hq.2.leâŸ©

lemma concave_on.convex_strict_hypograph (hf : concave_on ğ•œ s f) :
  convex ğ•œ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 < f p.1} :=
hf.dual.convex_strict_epigraph

end module
end ordered_cancel_add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid Î²] [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²]
  {s : set E} {f g : E â†’ Î²}

/-- The pointwise maximum of convex functions is convex. -/
lemma convex_on.sup (hf : convex_on ğ•œ s f) (hg : convex_on ğ•œ s g) :
  convex_on ğ•œ s (f âŠ” g) :=
begin
  refine âŸ¨hf.left, Î» x y hx hy a b ha hb hab, sup_le _ _âŸ©,
  { calc f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.right hx hy ha hb hab
     ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
     (smul_le_smul_of_nonneg le_sup_left ha)
     (smul_le_smul_of_nonneg le_sup_left hb) },
  { calc g (a â€¢ x + b â€¢ y) â‰¤ a â€¢ g x + b â€¢ g y : hg.right hx hy ha hb hab
     ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
     (smul_le_smul_of_nonneg le_sup_right ha)
     (smul_le_smul_of_nonneg le_sup_right hb) }
end

/-- The pointwise minimum of concave functions is concave. -/
lemma concave_on.inf (hf : concave_on ğ•œ s f) (hg : concave_on ğ•œ s g) :
  concave_on ğ•œ s (f âŠ“ g) :=
hf.dual.sup hg

/-- The pointwise maximum of strictly convex functions is strictly convex. -/
lemma strict_convex_on.sup (hf : strict_convex_on ğ•œ s f) (hg : strict_convex_on ğ•œ s g) :
  strict_convex_on ğ•œ s (f âŠ” g) :=
âŸ¨hf.left, Î» x y hx hy hxy a b ha hb hab, max_lt
  (calc f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y : hf.2 hx hy hxy ha hb hab
    ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
    (smul_le_smul_of_nonneg le_sup_left ha.le)
    (smul_le_smul_of_nonneg le_sup_left hb.le))
  (calc g (a â€¢ x + b â€¢ y) < a â€¢ g x + b â€¢ g y : hg.2 hx hy hxy ha hb hab
    ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
    (smul_le_smul_of_nonneg le_sup_right ha.le)
    (smul_le_smul_of_nonneg le_sup_right hb.le))âŸ©

/-- The pointwise minimum of strictly concave functions is strictly concave. -/
lemma strict_concave_on.inf (hf : strict_concave_on ğ•œ s f) (hg : strict_concave_on ğ•œ s g) :
   strict_concave_on ğ•œ s (f âŠ“ g) :=
hf.dual.sup hg

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
lemma convex_on.le_on_segment' (hf : convex_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  f (a â€¢ x + b â€¢ y) â‰¤ max (f x) (f y) :=
calc
  f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha hb hab
  ... â‰¤ a â€¢ max (f x) (f y) + b â€¢ max (f x) (f y) :
    add_le_add (smul_le_smul_of_nonneg (le_max_left _ _) ha)
      (smul_le_smul_of_nonneg (le_max_right _ _) hb)
  ... = max (f x) (f y) : convex.combo_self hab _

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
lemma concave_on.ge_on_segment' (hf : concave_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  min (f x) (f y) â‰¤ f (a â€¢ x + b â€¢ y) :=
hf.dual.le_on_segment' hx hy ha hb hab

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
lemma convex_on.le_on_segment (hf : convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  (hz : z âˆˆ [x -[ğ•œ] y]) :
  f z â‰¤ max (f x) (f y) :=
let âŸ¨a, b, ha, hb, hab, hzâŸ© := hz in hz â–¸ hf.le_on_segment' hx hy ha hb hab

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
lemma concave_on.ge_on_segment (hf : concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  (hz : z âˆˆ [x -[ğ•œ] y]) :
  min (f x) (f y) â‰¤ f z :=
hf.dual.le_on_segment hx hy hz

/-- A strictly convex function on an open segment is strictly upper-bounded by the max of its
endpoints. -/
lemma strict_convex_on.lt_on_open_segment' (hf : strict_convex_on ğ•œ s f) {x y : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hxy : x â‰  y) {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) :
  f (a â€¢ x + b â€¢ y) < max (f x) (f y) :=
calc
  f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y : hf.2 hx hy hxy ha hb hab
  ... â‰¤ a â€¢ max (f x) (f y) + b â€¢ max (f x) (f y) :
    add_le_add (smul_le_smul_of_nonneg (le_max_left _ _) ha.le)
      (smul_le_smul_of_nonneg (le_max_right _ _) hb.le)
  ... = max (f x) (f y) : convex.combo_self hab _

/-- A strictly concave function on an open segment is strictly lower-bounded by the min of its
endpoints. -/
lemma strict_concave_on.lt_on_open_segment' (hf : strict_concave_on ğ•œ s f) {x y : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hxy : x â‰  y) {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) :
  min (f x) (f y) < f (a â€¢ x + b â€¢ y) :=
hf.dual.lt_on_open_segment' hx hy hxy ha hb hab

/-- A strictly convex function on an open segment is strictly upper-bounded by the max of its
endpoints. -/
lemma strict_convex_on.lt_on_open_segment (hf : strict_convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hxy : x â‰  y) (hz : z âˆˆ open_segment ğ•œ x y) :
  f z < max (f x) (f y) :=
let âŸ¨a, b, ha, hb, hab, hzâŸ© := hz in hz â–¸ hf.lt_on_open_segment' hx hy hxy ha hb hab

/-- A strictly concave function on an open segment is strictly lower-bounded by the min of its
endpoints. -/
lemma strict_concave_on.lt_on_open_segment (hf : strict_concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hxy : x â‰  y) (hz : z âˆˆ open_segment ğ•œ x y) :
  min (f x) (f y) < f z :=
hf.dual.lt_on_open_segment hx hy hxy hz

end linear_ordered_add_comm_monoid

section linear_ordered_cancel_add_comm_monoid
variables [linear_ordered_cancel_add_comm_monoid Î²]

section ordered_smul
variables [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f g : E â†’ Î²}

lemma convex_on.le_left_of_right_le' (hf : convex_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) (hfy : f y â‰¤ f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) â‰¤ f x :=
le_of_not_lt $ Î» h, lt_irrefl (f (a â€¢ x + b â€¢ y)) $
  calc
    f (a â€¢ x + b â€¢ y)
        â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha.le hb hab
    ... < a â€¢ f (a â€¢ x + b â€¢ y) + b â€¢ f (a â€¢ x + b â€¢ y)
        : add_lt_add_of_lt_of_le (smul_lt_smul_of_pos h ha) (smul_le_smul_of_nonneg hfy hb)
    ... = f (a â€¢ x + b â€¢ y) : convex.combo_self hab _

lemma concave_on.left_le_of_le_right' (hf : concave_on ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
  {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) (hfy : f (a â€¢ x + b â€¢ y) â‰¤ f y) :
  f x â‰¤ f (a â€¢ x + b â€¢ y) :=
hf.dual.le_left_of_right_le' hx hy ha hb hab hfy

lemma convex_on.le_right_of_left_le' (hf : convex_on ğ•œ s f) {x y : E} {a b : ğ•œ}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1)
  (hfx : f x â‰¤ f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) â‰¤ f y :=
begin
  rw add_comm at âŠ¢ hab hfx,
  exact hf.le_left_of_right_le' hy hx hb ha hab hfx,
end

lemma concave_on.le_right_of_left_le' (hf : concave_on ğ•œ s f) {x y : E} {a b : ğ•œ}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1)
  (hfx : f (a â€¢ x + b â€¢ y) â‰¤ f x) :
  f y â‰¤ f (a â€¢ x + b â€¢ y) :=
hf.dual.le_right_of_left_le' hx hy ha hb hab hfx

lemma convex_on.le_left_of_right_le (hf : convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hyz : f y â‰¤ f z) :
  f z â‰¤ f x :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.le_left_of_right_le' hx hy ha hb.le hab hyz,
end

lemma concave_on.left_le_of_le_right (hf : concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hyz : f z â‰¤ f y) :
  f x â‰¤ f z :=
hf.dual.le_left_of_right_le hx hy hz hyz

lemma convex_on.le_right_of_left_le (hf : convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hxz : f x â‰¤ f z) :
  f z â‰¤ f y :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.le_right_of_left_le' hx hy ha.le hb hab hxz,
end

lemma concave_on.le_right_of_left_le (hf : concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hxz : f z â‰¤ f x) :
  f y â‰¤ f z :=
hf.dual.le_right_of_left_le hx hy hz hxz

end ordered_smul

section module
variables [module ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f g : E â†’ Î²}

/- The following lemmas don't require `module ğ•œ E` if you add the hypothesis `x â‰  y`. At the time of
the writing, we decided the resulting lemmas wouldn't be useful. Feel free to reintroduce them. -/
lemma strict_convex_on.lt_left_of_right_lt' (hf : strict_convex_on ğ•œ s f) {x y : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1)
  (hfy : f y < f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) < f x :=
not_le.1 $ Î» h, lt_irrefl (f (a â€¢ x + b â€¢ y)) $
  calc
    f (a â€¢ x + b â€¢ y)
        < a â€¢ f x + b â€¢ f y : hf.2 hx hy begin
            rintro rfl,
            rw convex.combo_self hab at hfy,
            exact lt_irrefl _ hfy,
          end ha hb hab
    ... < a â€¢ f (a â€¢ x + b â€¢ y) + b â€¢ f (a â€¢ x + b â€¢ y)
        : add_lt_add_of_le_of_lt (smul_le_smul_of_nonneg h ha.le) (smul_lt_smul_of_pos hfy hb)
    ... = f (a â€¢ x + b â€¢ y) : convex.combo_self hab _

lemma strict_concave_on.left_lt_of_lt_right' (hf : strict_concave_on ğ•œ s f) {x y : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1)
  (hfy : f (a â€¢ x + b â€¢ y) < f y) :
  f x < f (a â€¢ x + b â€¢ y) :=
hf.dual.lt_left_of_right_lt' hx hy ha hb hab hfy

lemma strict_convex_on.lt_right_of_left_lt' (hf : strict_convex_on ğ•œ s f) {x y : E} {a b : ğ•œ}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1)
  (hfx : f x < f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) < f y :=
begin
  rw add_comm at âŠ¢ hab hfx,
  exact hf.lt_left_of_right_lt' hy hx hb ha hab hfx,
end

lemma strict_concave_on.lt_right_of_left_lt' (hf : strict_concave_on ğ•œ s f) {x y : E} {a b : ğ•œ}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1)
  (hfx : f (a â€¢ x + b â€¢ y) < f x) :
  f y < f (a â€¢ x + b â€¢ y) :=
hf.dual.lt_right_of_left_lt' hx hy ha hb hab hfx

lemma strict_convex_on.lt_left_of_right_lt (hf : strict_convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hyz : f y < f z) :
  f z < f x :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.lt_left_of_right_lt' hx hy ha hb hab hyz,
end

lemma strict_concave_on.left_lt_of_lt_right (hf : strict_concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hyz : f z < f y) :
  f x < f z :=
hf.dual.lt_left_of_right_lt hx hy hz hyz

lemma strict_convex_on.lt_right_of_left_lt (hf : strict_convex_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hxz : f x < f z) :
  f z < f y :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.lt_right_of_left_lt' hx hy ha hb hab hxz,
end

lemma strict_concave_on.lt_right_of_left_lt (hf : strict_concave_on ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment ğ•œ x y) (hxz : f z < f x) :
  f y < f z :=
hf.dual.lt_right_of_left_lt hx hy hz hxz

end module
end linear_ordered_cancel_add_comm_monoid

section ordered_add_comm_group
variables [ordered_add_comm_group Î²] [has_scalar ğ•œ E] [module ğ•œ Î²] {s : set E} {f g : E â†’ Î²}

/-- A function `-f` is convex iff `f` is concave. -/
@[simp] lemma neg_convex_on_iff : convex_on ğ•œ s (-f) â†” concave_on ğ•œ s f :=
begin
  split,
  { rintro âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, Î» x y hx hy a b ha hb hab, _âŸ©,
    simp [neg_apply, neg_le, add_comm] at h,
    exact h hx hy ha hb hab },
  { rintro âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, Î» x y hx hy a b ha hb hab, _âŸ©,
    rw â†neg_le_neg_iff,
    simp_rw [neg_add, pi.neg_apply, smul_neg, neg_neg],
    exact h hx hy ha hb hab }
end

/-- A function `-f` is concave iff `f` is convex. -/
@[simp] lemma neg_concave_on_iff : concave_on ğ•œ s (-f) â†” convex_on ğ•œ s f:=
by rw [â† neg_convex_on_iff, neg_neg f]

/-- A function `-f` is strictly convex iff `f` is strictly concave. -/
@[simp] lemma neg_strict_convex_on_iff : strict_convex_on ğ•œ s (-f) â†” strict_concave_on ğ•œ s f :=
begin
  split,
  { rintro âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, Î» x y hx hy hxy a b ha hb hab, _âŸ©,
    simp [neg_apply, neg_lt, add_comm] at h,
    exact h hx hy hxy ha hb hab },
  { rintro âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, Î» x y hx hy hxy a b ha hb hab, _âŸ©,
    rw â†neg_lt_neg_iff,
    simp_rw [neg_add, pi.neg_apply, smul_neg, neg_neg],
    exact h hx hy hxy ha hb hab }
end

/-- A function `-f` is strictly concave iff `f` is strictly convex. -/
@[simp] lemma neg_strict_concave_on_iff : strict_concave_on ğ•œ s (-f) â†” strict_convex_on ğ•œ s f :=
by rw [â† neg_strict_convex_on_iff, neg_neg f]

alias neg_convex_on_iff â†” _ concave_on.neg
alias neg_concave_on_iff â†” _ convex_on.neg
alias neg_strict_convex_on_iff â†” _ strict_concave_on.neg
alias neg_strict_concave_on_iff â†” _ strict_convex_on.neg

lemma convex_on.sub (hf : convex_on ğ•œ s f) (hg : concave_on ğ•œ s g) : convex_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

lemma concave_on.sub (hf : concave_on ğ•œ s f) (hg : convex_on ğ•œ s g) : concave_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

lemma strict_convex_on.sub (hf : strict_convex_on ğ•œ s f) (hg : strict_concave_on ğ•œ s g) :
  strict_convex_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

lemma strict_concave_on.sub (hf : strict_concave_on ğ•œ s f) (hg : strict_convex_on ğ•œ s g) :
  strict_concave_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

lemma convex_on.sub_strict_concave_on (hf : convex_on ğ•œ s f) (hg : strict_concave_on ğ•œ s g) :
  strict_convex_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add_strict_convex_on hg.neg

lemma concave_on.sub_strict_convex_on (hf : concave_on ğ•œ s f) (hg : strict_convex_on ğ•œ s g) :
  strict_concave_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add_strict_concave_on hg.neg

lemma strict_convex_on.sub_concave_on (hf : strict_convex_on ğ•œ s f) (hg : concave_on ğ•œ s g) :
  strict_convex_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add_convex_on hg.neg

lemma strict_concave_on.sub_convex_on (hf : strict_concave_on ğ•œ s f) (hg : convex_on ğ•œ s g) :
  strict_concave_on ğ•œ s (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add_concave_on hg.neg

end ordered_add_comm_group
end add_comm_monoid

section add_cancel_comm_monoid
variables [add_cancel_comm_monoid E] [ordered_add_comm_monoid Î²] [module ğ•œ E] [has_scalar ğ•œ Î²]
  {s : set E} {f : E â†’ Î²}

/-- Right translation preserves strict convexity. -/
lemma strict_convex_on.translate_right (hf : strict_convex_on ğ•œ s f) (c : E) :
  strict_convex_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, c + z)) :=
âŸ¨hf.1.translate_preimage_right _, Î» x y hx hy hxy a b ha hb hab,
  calc
    f (c + (a â€¢ x + b â€¢ y)) = f (a â€¢ (c + x) + b â€¢ (c + y))
        : by rw [smul_add, smul_add, add_add_add_comm, convex.combo_self hab]
    ... < a â€¢ f (c + x) + b â€¢ f (c + y) : hf.2 hx hy ((add_right_injective c).ne hxy) ha hb habâŸ©

/-- Right translation preserves strict concavity. -/
lemma strict_concave_on.translate_right (hf : strict_concave_on ğ•œ s f) (c : E) :
  strict_concave_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, c + z)) :=
hf.dual.translate_right _

/-- Left translation preserves strict convexity. -/
lemma strict_convex_on.translate_left (hf : strict_convex_on ğ•œ s f) (c : E) :
  strict_convex_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, z + c)) :=
by simpa only [add_comm] using hf.translate_right _

/-- Left translation preserves strict concavity. -/
lemma strict_concave_on.translate_left (hf : strict_concave_on ğ•œ s f) (c : E) :
  strict_concave_on ğ•œ ((Î» z, c + z) â»Â¹' s) (f âˆ˜ (Î» z, z + c)) :=
by simpa only [add_comm] using hf.translate_right _

end add_cancel_comm_monoid
end ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ] [add_comm_monoid E]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section module
variables [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma convex_on.smul {c : ğ•œ} (hc : 0 â‰¤ c) (hf : convex_on ğ•œ s f) : convex_on ğ•œ s (Î» x, c â€¢ f x) :=
âŸ¨hf.1, Î» x y hx hy a b ha hb hab,
  calc
    c â€¢ f (a â€¢ x + b â€¢ y) â‰¤ c â€¢ (a â€¢ f x + b â€¢ f y)
      : smul_le_smul_of_nonneg (hf.2 hx hy ha hb hab) hc
    ... = a â€¢ (c â€¢ f x) + b â€¢ (c â€¢ f y)
      : by rw [smul_add, smul_comm c, smul_comm c]; apply_instanceâŸ©

lemma concave_on.smul {c : ğ•œ} (hc : 0 â‰¤ c) (hf : concave_on ğ•œ s f) :
  concave_on ğ•œ s (Î» x, c â€¢ f x) :=
hf.dual.smul hc

end module
end ordered_add_comm_monoid
end ordered_comm_semiring

section ordered_ring
variables [linear_ordered_field ğ•œ] [add_comm_group E] [add_comm_group F]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section module
variables [module ğ•œ E] [module ğ•œ F] [has_scalar ğ•œ Î²]

/-- If a function is convex on `s`, it remains convex when precomposed by an affine map. -/
lemma convex_on.comp_affine_map {f : F â†’ Î²} (g : E â†’áµƒ[ğ•œ] F) {s : set F} (hf : convex_on ğ•œ s f) :
  convex_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
âŸ¨hf.1.affine_preimage _, Î» x y hx hy a b ha hb hab,
  calc
    (f âˆ˜ g) (a â€¢ x + b â€¢ y) = f (g (a â€¢ x + b â€¢ y))         : rfl
                       ...  = f (a â€¢ (g x) + b â€¢ (g y))     : by rw [convex.combo_affine_apply hab]
                       ...  â‰¤ a â€¢ f (g x) + b â€¢ f (g y)     : hf.2 hx hy ha hb habâŸ©

/-- If a function is concave on `s`, it remains concave when precomposed by an affine map. -/
lemma concave_on.comp_affine_map {f : F â†’ Î²} (g : E â†’áµƒ[ğ•œ] F) {s : set F} (hf : concave_on ğ•œ s f) :
  concave_on ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
hf.dual.comp_affine_map g

end module
end ordered_add_comm_monoid
end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ] [add_comm_monoid E]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid Î²]

section has_scalar
variables [has_scalar ğ•œ E] [has_scalar ğ•œ Î²] {s : set E}

lemma convex_on_iff_div {f : E â†’ Î²} :
  convex_on ğ•œ s f â†” convex ğ•œ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b
  â†’ f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) â‰¤ (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y :=
and_congr iff.rfl
âŸ¨begin
  intros h x y hx hy a b ha hb hab,
  apply h hx hy (div_nonneg ha hab.le) (div_nonneg hb hab.le),
  rw [â†add_div, div_self hab.ne'],
end,
begin
  intros h x y hx hy a b ha hb hab,
  simpa [hab, zero_lt_one] using h hx hy ha hb,
endâŸ©

lemma concave_on_iff_div {f : E â†’ Î²} :
  concave_on ğ•œ s f â†” convex ğ•œ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b
  â†’ 0 < a + b â†’ (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y â‰¤ f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) :=
@convex_on_iff_div _ _ Î²áµ’áµˆ _ _ _ _ _ _ _

lemma strict_convex_on_iff_div {f : E â†’ Î²} :
  strict_convex_on ğ•œ s f â†” convex ğ•œ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a
    â†’ 0 < b â†’ f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) < (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y :=
and_congr iff.rfl
âŸ¨begin
  intros h x y hx hy hxy a b ha hb,
  have hab := add_pos ha hb,
  apply h hx hy hxy (div_pos ha hab) (div_pos hb hab),
  rw [â†add_div, div_self hab.ne'],
end,
begin
  intros h x y hx hy hxy a b ha hb hab,
  simpa [hab, zero_lt_one] using h hx hy hxy ha hb,
endâŸ©

lemma strict_concave_on_iff_div {f : E â†’ Î²} :
  strict_concave_on ğ•œ s f â†” convex ğ•œ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a
    â†’ 0 < b â†’ (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y < f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) :=
@strict_convex_on_iff_div _ _ Î²áµ’áµˆ _ _ _ _ _ _ _

end has_scalar
end ordered_add_comm_monoid
end linear_ordered_field
