/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, FranÃ§ois Dupuis
-/
import analysis.convex.combination
import data.real.basic
import algebra.module.ordered

/-!
# Convex and concave functions

This file defines convex and concave functions in vector spaces and proves the finite Jensen
inequality. The integral version can be found in `analysis.convex.integral`.

A function `f : E â†’ Î²` is `convex_on` a set `s` if `s` is itself a convex set, and for any two
points `x y âˆˆ s`, the segment joining `(x, f x)` to `(y, f y)` is above the graph of `f`.
Equivalently, `convex_on ğ•œ f s` means that the epigraph `{p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2}` is
a convex set.

## Main declarations

* `convex_on ğ•œ s f`: The function `f` is convex on `s` with scalars `ğ•œ`.
* `concave_on ğ•œ s f`: The function `f` is concave on `s` with scalars `ğ•œ`.
* `convex_on.map_center_mass_le` `convex_on.map_sum_le`: Convex Jensen's inequality.
-/

open finset linear_map set
open_locale big_operators classical convex pointwise

variables {ğ•œ E F Î¹ Î¹' Î² : Type*} [add_comm_group E] [module â„ E] [add_comm_group F] [module â„ F]
  [ordered_add_comm_monoid Î²] [module â„ Î²] {s : set E}

/-- Convexity of functions -/
def convex_on (s : set E) (f : E â†’ Î²) : Prop :=
  convex â„ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : â„â¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y

/-- Concavity of functions -/
def concave_on (s : set E) (f : E â†’ Î²) : Prop :=
  convex â„ s âˆ§
  âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : â„â¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)

section
variables [ordered_smul â„ Î²]

/-- A function `f` is concave iff `-f` is convex. -/
@[simp] lemma neg_convex_on_iff {Î³ : Type*} [ordered_add_comm_group Î³] [module â„ Î³]
  (s : set E) (f : E â†’ Î³) : convex_on s (-f) â†” concave_on s f :=
begin
  split,
  { rintros âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, _âŸ©,
    intros x y xs ys a b ha hb hab,
    specialize h xs ys ha hb hab,
    simp [neg_apply, neg_le, add_comm] at h,
    exact h },
  { rintros âŸ¨hconv, hâŸ©,
    refine âŸ¨hconv, _âŸ©,
    intros x y xs ys a b ha hb hab,
    specialize h xs ys ha hb hab,
    simp [neg_apply, neg_le, add_comm, h] }
end

/-- A function `f` is concave iff `-f` is convex. -/
@[simp] lemma neg_concave_on_iff {Î³ : Type*} [ordered_add_comm_group Î³] [module â„ Î³]
  (s : set E) (f : E â†’ Î³) : concave_on s (-f) â†” convex_on s f:=
by rw [â† neg_convex_on_iff s (-f), neg_neg f]

end

lemma convex_on_id {s : set â„} (hs : convex â„ s) : convex_on s id := âŸ¨hs, by { intros, refl }âŸ©

lemma concave_on_id {s : set â„} (hs : convex â„ s) : concave_on s id := âŸ¨hs, by { intros, refl }âŸ©

lemma convex_on_const (c : Î²) (hs : convex â„ s) : convex_on s (Î» x:E, c) :=
âŸ¨hs, by { intros, simp only [â† add_smul, *, one_smul] }âŸ©

lemma concave_on_const (c : Î²) (hs : convex â„ s) : concave_on s (Î» x:E, c) :=
@convex_on_const _ (order_dual Î²) _ _ _ _ _ c hs

lemma convex_on_iff_div {f : E â†’ Î²} :
  convex_on s f â†” convex â„ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : â„â¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b
  â†’ f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) â‰¤ (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y :=
and_congr iff.rfl
âŸ¨begin
  intros h x y hx hy a b ha hb hab,
  apply h hx hy (div_nonneg ha $ le_of_lt hab) (div_nonneg hb $ le_of_lt hab),
  rw [â†add_div],
  exact div_self (ne_of_gt hab)
end,
begin
  intros h x y hx hy a b ha hb hab,
  simpa [hab, zero_lt_one] using h hx hy ha hb,
endâŸ©

lemma concave_on_iff_div {f : E â†’ Î²} :
  concave_on s f â†” convex â„ s âˆ§ âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : â„â¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b
  â†’ (a/(a+b)) â€¢ f x + (b/(a+b)) â€¢ f y â‰¤ f ((a/(a+b)) â€¢ x + (b/(a+b)) â€¢ y) :=
@convex_on_iff_div _ (order_dual Î²) _ _ _ _ _ _

/-- For a function on a convex set in a linear ordered space, in order to prove that it is convex
it suffices to verify the inequality `f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y` only for `x < y`
and positive `a`, `b`. The main use case is `E = â„` however one can apply it, e.g., to `â„^n` with
lexicographic order. -/
lemma linear_order.convex_on_of_lt {f : E â†’ Î²} [linear_order E] (hs : convex â„ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : â„â¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y) : convex_on s f :=
begin
  use hs,
  intros x y hx hy a b ha hb hab,
  wlog hxy : x<=y using [x y a b, y x b a],
  { exact le_total _ _ },
  { cases eq_or_lt_of_le hxy with hxy hxy,
      by { subst y, rw [â† add_smul, â† add_smul, hab, one_smul, one_smul] },
    cases eq_or_lt_of_le ha with ha ha,
      by { subst a, rw [zero_add] at hab, subst b, simp },
    cases eq_or_lt_of_le hb with hb hb,
      by { subst b, rw [add_zero] at hab, subst a, simp },
    exact hf hx hy hxy ha hb hab }
end

/-- For a function on a convex set in a linear ordered space, in order to prove that it is concave
it suffices to verify the inequality `a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)` only for `x < y`
and positive `a`, `b`. The main use case is `E = â„` however one can apply it, e.g., to `â„^n` with
lexicographic order. -/
lemma linear_order.concave_on_of_lt {f : E â†’ Î²} [linear_order E] (hs : convex â„ s)
  (hf : âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : â„â¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
     a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)) : concave_on s f :=
@linear_order.convex_on_of_lt _ (order_dual Î²) _ _ _ _ _ f _ hs hf

/-- For a function `f` defined on a convex subset `D` of `â„`, if for any three points `x < y < z`
the slope of the secant line of `f` on `[x, y]` is less than or equal to the slope
of the secant line of `f` on `[x, z]`, then `f` is convex on `D`. This way of proving convexity
of a function is used in the proof of convexity of a function with a monotone derivative. -/
lemma convex_on_real_of_slope_mono_adjacent {s : set â„} (hs : convex â„ s) {f : â„ â†’ â„}
  (hf : âˆ€ {x y z : â„}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)) :
  convex_on s f :=
linear_order.convex_on_of_lt hs
begin
  assume x z hx hz hxz a b ha hb hab,
  let y := a * x + b * z,
  have hxy : x < y,
  { rw [â† one_mul x, â† hab, add_mul],
    exact add_lt_add_left ((mul_lt_mul_left hb).2 hxz) _ },
  have hyz : y < z,
  { rw [â† one_mul z, â† hab, add_mul],
    exact add_lt_add_right ((mul_lt_mul_left ha).2 hxz) _ },
  have : (f y - f x) * (z - y) â‰¤ (f z - f y) * (y - x),
    from (div_le_div_iff (sub_pos.2 hxy) (sub_pos.2 hyz)).1 (hf hx hz hxy hyz),
  have A : z - y + (y - x) = z - x, by abel,
  have B : 0 < z - x, from sub_pos.2 (lt_trans hxy hyz),
  rw [sub_mul, sub_mul, sub_le_iff_le_add', â† add_sub_assoc, le_sub_iff_add_le, â† mul_add, A,
    â† le_div_iff B, add_div, mul_div_assoc, mul_div_assoc,
    mul_comm (f x), mul_comm (f z)] at this,
  rw [eq_comm, â† sub_eq_iff_eq_add] at hab; subst a,
  convert this; symmetry; simp only [div_eq_iff (ne_of_gt B), y]; ring
end

/-- For a function `f` defined on a subset `D` of `â„`, if `f` is convex on `D`, then for any three
points `x < y < z`, the slope of the secant line of `f` on `[x, y]` is less than or equal to the
slope of the secant line of `f` on `[x, z]`. -/
lemma convex_on.slope_mono_adjacent {s : set â„} {f : â„ â†’ â„} (hf : convex_on s f)
  {x y z : â„} (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y) :=
begin
  have hâ‚ : 0 < y - x := by linarith,
  have hâ‚‚ : 0 < z - y := by linarith,
  have hâ‚ƒ : 0 < z - x := by linarith,
  suffices : f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y),
    by { ring_nf at this âŠ¢, linarith },
  set a := (z - y) / (z - x),
  set b := (y - x) / (z - x),
  have heqz : a â€¢ x + b â€¢ z = y, by { field_simp, rw div_eq_iff; [ring, linarith], },
  have key, from
    hf.2 hx hz
      (show 0 â‰¤ a, by apply div_nonneg; linarith)
      (show 0 â‰¤ b, by apply div_nonneg; linarith)
      (show a + b = 1, by { field_simp, rw div_eq_iff; [ring, linarith], }),
  rw heqz at key,
  replace key := mul_le_mul_of_nonneg_left key (le_of_lt hâ‚ƒ),
  field_simp [ne_of_gt hâ‚, ne_of_gt hâ‚‚, ne_of_gt hâ‚ƒ, mul_comm (z - x) _] at key âŠ¢,
  rw div_le_div_right,
  { linarith, },
  { nlinarith, },
end

/-- For a function `f` defined on a convex subset `D` of `â„`, `f` is convex on `D` iff, for any
three points `x < y < z` the slope of the secant line of `f` on `[x, y]` is less than or equal to
the slope,of the secant line of `f` on `[x, z]`. -/
lemma convex_on_real_iff_slope_mono_adjacent {s : set â„} (hs : convex â„ s) {f : â„ â†’ â„} :
  convex_on s f â†”
  (âˆ€ {x y z : â„}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)) :=
âŸ¨convex_on.slope_mono_adjacent, convex_on_real_of_slope_mono_adjacent hsâŸ©

/-- For a function `f` defined on a convex subset `D` of `â„`, if for any three points `x < y < z`
the slope of the secant line of `f` on `[x, y]` is greater than or equal to the slope
of the secant line of `f` on `[x, z]`, then `f` is concave on `D`. -/
lemma concave_on_real_of_slope_mono_adjacent {s : set â„} (hs : convex â„ s) {f : â„ â†’ â„}
  (hf : âˆ€ {x y z : â„}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)) : concave_on s f :=
begin
  rw [â†neg_convex_on_iff],
  apply convex_on_real_of_slope_mono_adjacent hs,
  intros x y z xs zs xy yz,
  rw [â†neg_le_neg_iff, â†neg_div, â†neg_div, neg_sub, neg_sub],
  simp only [hf xs zs xy yz, neg_sub_neg, pi.neg_apply],
end

/-- For a function `f` defined on a subset `D` of `â„`, if `f` is concave on `D`, then for any three
points `x < y < z`, the slope of the secant line of `f` on `[x, y]` is greater than or equal to the
slope of the secant line of `f` on `[x, z]`. -/
lemma concave_on.slope_mono_adjacent {s : set â„} {f : â„ â†’ â„} (hf : concave_on s f)
  {x y z : â„} (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x) :=
begin
  rw [â†neg_le_neg_iff, â†neg_div, â†neg_div, neg_sub, neg_sub],
  rw [â†neg_sub_neg (f y), â†neg_sub_neg (f z)],
  simp_rw [â†pi.neg_apply],
  rw [â†neg_convex_on_iff] at hf,
  apply convex_on.slope_mono_adjacent hf; assumption,
end

/-- For a function `f` defined on a convex subset `D` of `â„`, `f` is concave on `D` iff for any
three points `x < y < z` the slope of the secant line of `f` on `[x, y]` is greater than or equal to
the slope of the secant line of `f` on `[x, z]`. -/
lemma concave_on_real_iff_slope_mono_adjacent {s : set â„} (hs : convex â„ s) {f : â„ â†’ â„} :
  concave_on s f â†”
  (âˆ€ {x y z : â„}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)) :=
âŸ¨concave_on.slope_mono_adjacent, concave_on_real_of_slope_mono_adjacent hsâŸ©

lemma convex_on.subset {f : E â†’ Î²} {t : set E} (h_convex_on : convex_on t f)
  (h_subset : s âŠ† t) (h_convex : convex â„ s) : convex_on s f :=
begin
  apply and.intro h_convex,
  intros x y hx hy,
  exact h_convex_on.2 (h_subset hx) (h_subset hy),
end

lemma concave_on.subset {f : E â†’ Î²} {t : set E} (h_concave_on : concave_on t f)
  (h_subset : s âŠ† t) (h_convex : convex â„ s) : concave_on s f :=
@convex_on.subset _ (order_dual Î²) _ _ _ _ _ f t h_concave_on h_subset h_convex

lemma convex_on.add {f g : E â†’ Î²} (hf : convex_on s f) (hg : convex_on s g) :
  convex_on s (Î»x, f x + g x) :=
begin
  apply and.intro hf.1,
  intros x y hx hy a b ha hb hab,
  calc
    f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) â‰¤ (a â€¢ f x + b â€¢ f y) + (a â€¢ g x + b â€¢ g y)
      : add_le_add (hf.2 hx hy ha hb hab) (hg.2 hx hy ha hb hab)
    ... = a â€¢ f x + a â€¢ g x + b â€¢ f y + b â€¢ g y : by abel
    ... = a â€¢ (f x + g x) + b â€¢ (f y + g y) : by simp [smul_add, add_assoc]
end

lemma concave_on.add {f g : E â†’ Î²} (hf : concave_on s f) (hg : concave_on s g) :
  concave_on s (Î»x, f x + g x) :=
@convex_on.add _ (order_dual Î²) _ _ _ _ _ f g hf hg

lemma convex_on.smul [ordered_smul â„ Î²] {f : E â†’ Î²} {c : â„} (hc : 0 â‰¤ c)
  (hf : convex_on s f) : convex_on s (Î»x, c â€¢ f x) :=
begin
  apply and.intro hf.1,
  intros x y hx hy a b ha hb hab,
  calc
    c â€¢ f (a â€¢ x + b â€¢ y) â‰¤ c â€¢ (a â€¢ f x + b â€¢ f y)
      : smul_le_smul_of_nonneg (hf.2 hx hy ha hb hab) hc
    ... = a â€¢ (c â€¢ f x) + b â€¢ (c â€¢ f y) : by simp only [smul_add, smul_comm c]
end

lemma concave_on.smul [ordered_smul â„ Î²] {f : E â†’ Î²} {c : â„} (hc : 0 â‰¤ c)
  (hf : concave_on s f) : concave_on s (Î»x, c â€¢ f x) :=
@convex_on.smul _ (order_dual Î²) _ _ _ _ _ _ f c hc hf

section linear_order
section monoid

variables {Î³ : Type*} [linear_ordered_add_comm_monoid Î³] [module â„ Î³] [ordered_smul â„ Î³]
  {f g : E â†’ Î³}

/-- The pointwise maximum of convex functions is convex. -/
lemma convex_on.sup (hf : convex_on s f) (hg : convex_on s g) :
  convex_on s (f âŠ” g) :=
begin
   refine âŸ¨hf.left, Î» x y hx hy a b ha hb hab, sup_le _ _âŸ©,
   { calc f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.right hx hy ha hb hab
      ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
      (smul_le_smul_of_nonneg le_sup_left ha)
      (smul_le_smul_of_nonneg le_sup_left hb) },
   { calc g (a â€¢ x + b â€¢ y) â‰¤ a â€¢ g x + b â€¢ g y : hg.right hx hy ha hb hab
      ...                   â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) : add_le_add
      (smul_le_smul_of_nonneg le_sup_right ha)
      (smul_le_smul_of_nonneg le_sup_right hb) }
end

/-- The pointwise minimum of concave functions is concave. -/
lemma concave_on.inf (hf : concave_on s f) (hg : concave_on s g) :
  concave_on s (f âŠ“ g) :=
@convex_on.sup _ _ _ _ (order_dual Î³) _ _ _ _ _ hf hg

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
lemma convex_on.le_on_segment' (hf : convex_on s f) {x y : E} {a b : â„}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  f (a â€¢ x + b â€¢ y) â‰¤ max (f x) (f y) :=
calc
  f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha hb hab
  ... â‰¤ a â€¢ max (f x) (f y) + b â€¢ max (f x) (f y) :
    add_le_add (smul_le_smul_of_nonneg (le_max_left _ _) ha)
      (smul_le_smul_of_nonneg (le_max_right _ _) hb)
  ... = max (f x) (f y) : by rw [â†add_smul, hab, one_smul]

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
lemma concave_on.le_on_segment' (hf : concave_on s f) {x y : E} {a b : â„}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  min (f x) (f y) â‰¤ f (a â€¢ x + b â€¢ y) :=
@convex_on.le_on_segment' _ _ _ _ (order_dual Î³) _ _ _ f hf x y a b hx hy ha hb hab

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
lemma convex_on.le_on_segment (hf : convex_on s f) {x y z : E}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (hz : z âˆˆ [x -[â„] y]) :
  f z â‰¤ max (f x) (f y) :=
let âŸ¨a, b, ha, hb, hab, hzâŸ© := hz in hz â–¸ hf.le_on_segment' hx hy ha hb hab

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
lemma concave_on.le_on_segment {f : E â†’ Î³} (hf : concave_on s f) {x y z : E}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (hz : z âˆˆ [x -[â„] y]) :
    min (f x) (f y) â‰¤ f z :=
@convex_on.le_on_segment _ _ _ _ (order_dual Î³) _ _ _ f hf x y z hx hy hz

end monoid

variables {Î³ : Type*} [linear_ordered_cancel_add_comm_monoid Î³] [module â„ Î³] [ordered_smul â„ Î³]
  {f : E â†’ Î³}

-- could be shown without contradiction but yeah
lemma convex_on.le_left_of_right_le' (hf : convex_on s f) {x y : E} {a b : â„}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1)
  (hxy : f y â‰¤ f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) â‰¤ f x :=
begin
  apply le_of_not_lt (Î» h, lt_irrefl (f (a â€¢ x + b â€¢ y)) _),
  calc
    f (a â€¢ x + b â€¢ y)
        â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha.le hb hab
    ... < a â€¢ f (a â€¢ x + b â€¢ y) + b â€¢ f (a â€¢ x + b â€¢ y)
        : add_lt_add_of_lt_of_le (smul_lt_smul_of_pos h ha) (smul_le_smul_of_nonneg hxy hb)
    ... = f (a â€¢ x + b â€¢ y) : by rw [â†add_smul, hab, one_smul],
end

lemma concave_on.left_le_of_le_right' (hf : concave_on s f) {x y : E} {a b : â„}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1)
  (hxy : f (a â€¢ x + b â€¢ y) â‰¤ f y) :
  f x â‰¤ f (a â€¢ x + b â€¢ y) :=
@convex_on.le_left_of_right_le' _ _ _ _ (order_dual Î³) _ _ _ f hf x y a b hx hy ha hb hab hxy

lemma convex_on.le_right_of_left_le' (hf : convex_on s f) {x y : E} {a b : â„}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1)
  (hxy : f x â‰¤ f (a â€¢ x + b â€¢ y)) :
  f (a â€¢ x + b â€¢ y) â‰¤ f y :=
begin
  rw add_comm at âŠ¢ hab hxy,
  exact hf.le_left_of_right_le' hy hx hb ha hab hxy,
end

lemma concave_on.le_right_of_left_le' (hf : concave_on s f) {x y : E} {a b : â„}
  (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1)
  (hxy : f (a â€¢ x + b â€¢ y) â‰¤ f x) :
  f y â‰¤ f (a â€¢ x + b â€¢ y) :=
@convex_on.le_right_of_left_le' _ _ _ _ (order_dual Î³) _ _ _ f hf x y a b hx hy ha hb hab hxy

lemma convex_on.le_left_of_right_le (hf : convex_on s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment â„ x y) (hyz : f y â‰¤ f z) :
  f z â‰¤ f x :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.le_left_of_right_le' hx hy ha hb.le hab hyz,
end

lemma concave_on.left_le_of_le_right (hf : concave_on s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment â„ x y) (hyz : f z â‰¤ f y) :
  f x â‰¤ f z :=
@convex_on.le_left_of_right_le _ _ _ _ (order_dual Î³) _ _ _ f hf x y z hx hy hz hyz

lemma convex_on.le_right_of_left_le (hf : convex_on s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment â„ x y) (hxz : f x â‰¤ f z) :
  f z â‰¤ f y :=
begin
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz,
  exact hf.le_right_of_left_le' hx hy ha.le hb hab hxz,
end

lemma concave_on.le_right_of_left_le (hf : concave_on s f) {x y z : E} (hx : x âˆˆ s)
  (hy : y âˆˆ s) (hz : z âˆˆ open_segment â„ x y) (hxz : f z â‰¤ f x) :
  f y â‰¤ f z :=
@convex_on.le_right_of_left_le _ _ _ _ (order_dual Î³) _ _ _ f hf x y z hx hy hz hxz

end linear_order

lemma convex_on.convex_le [ordered_smul â„ Î²] {f : E â†’ Î²} (hf : convex_on s f) (r : Î²) :
  convex â„ {x âˆˆ s | f x â‰¤ r} :=
Î» x y hx hy a b ha hb hab,
begin
  refine âŸ¨hf.1 hx.1 hy.1 ha hb hab, _âŸ©,
  calc
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ (f x) + b â€¢ (f y) : hf.2 hx.1 hy.1 ha hb hab
                  ... â‰¤ a â€¢ r + b â€¢ r         : add_le_add (smul_le_smul_of_nonneg hx.2 ha)
                                                  (smul_le_smul_of_nonneg hy.2 hb)
                  ... â‰¤ r                     : by simp [â†add_smul, hab]
end

lemma concave_on.concave_le [ordered_smul â„ Î²] {f : E â†’ Î²} (hf : concave_on s f) (r : Î²) :
  convex â„ {x âˆˆ s | r â‰¤ f x} :=
@convex_on.convex_le _ (order_dual Î²) _ _ _ _ _ _ f hf r

lemma convex_on.convex_lt {Î³ : Type*} [ordered_cancel_add_comm_monoid Î³]
  [module â„ Î³] [ordered_smul â„ Î³]
  {f : E â†’ Î³} (hf : convex_on s f) (r : Î³) : convex â„ {x âˆˆ s | f x < r} :=
begin
  intros a b as bs xa xb hxa hxb hxaxb,
  refine âŸ¨hf.1 as.1 bs.1 hxa hxb hxaxb, _âŸ©,
  by_cases H : xa = 0,
  { have H' : xb = 1 := by rwa [H, zero_add] at hxaxb,
    rw [H, H', zero_smul, one_smul, zero_add],
    exact bs.2 },
  { calc
      f (xa â€¢ a + xb â€¢ b) â‰¤ xa â€¢ (f a) + xb â€¢ (f b) : hf.2 as.1 bs.1 hxa hxb hxaxb
                      ... < xa â€¢ r + xb â€¢ (f b)     : (add_lt_add_iff_right (xb â€¢ (f b))).mpr
                                                        (smul_lt_smul_of_pos as.2
                                                          (lt_of_le_of_ne hxa (ne.symm H)))
                      ... â‰¤ xa â€¢ r + xb â€¢ r         : (add_le_add_iff_left (xa â€¢ r)).mpr
                                                        (smul_le_smul_of_nonneg bs.2.le hxb)
                      ... = r                       : by simp only [â†add_smul, hxaxb, one_smul] }
end

lemma concave_on.convex_lt {Î³ : Type*} [ordered_cancel_add_comm_monoid Î³]
  [module â„ Î³] [ordered_smul â„ Î³]
  {f : E â†’ Î³} (hf : concave_on s f) (r : Î³) : convex â„ {x âˆˆ s | r < f x} :=
@convex_on.convex_lt _ _ _ _ (order_dual Î³) _ _ _ f hf r

lemma convex_on.convex_epigraph {Î³ : Type*} [ordered_add_comm_group Î³]
  [module â„ Î³] [ordered_smul â„ Î³]
  {f : E â†’ Î³} (hf : convex_on s f) :
  convex â„ {p : E Ã— Î³ | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} :=
begin
  rintros âŸ¨x, râŸ© âŸ¨y, tâŸ© âŸ¨hx, hrâŸ© âŸ¨hy, htâŸ© a b ha hb hab,
  refine âŸ¨hf.1 hx hy ha hb hab, _âŸ©,
  calc f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y : hf.2 hx hy ha hb hab
  ... â‰¤ a â€¢ r + b â€¢ t : add_le_add (smul_le_smul_of_nonneg hr ha)
                            (smul_le_smul_of_nonneg ht hb)
end

lemma concave_on.convex_hypograph {Î³ : Type*} [ordered_add_comm_group Î³]
  [module â„ Î³] [ordered_smul â„ Î³]
  {f : E â†’ Î³} (hf : concave_on s f) :
  convex â„ {p : E Ã— Î³ | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1} :=
@convex_on.convex_epigraph _ _ _ _ (order_dual Î³) _ _ _ f hf

lemma convex_on_iff_convex_epigraph {Î³ : Type*} [ordered_add_comm_group Î³]
  [module â„ Î³] [ordered_smul â„ Î³]
  {f : E â†’ Î³} :
  convex_on s f â†” convex â„ {p : E Ã— Î³ | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2} :=
begin
  refine âŸ¨convex_on.convex_epigraph, Î» h, âŸ¨_, _âŸ©âŸ©,
  { assume x y hx hy a b ha hb hab,
    exact (@h (x, f x) (y, f y) âŸ¨hx, le_refl _âŸ© âŸ¨hy, le_refl _âŸ© a b ha hb hab).1 },
  { assume x y hx hy a b ha hb hab,
    exact (@h (x, f x) (y, f y) âŸ¨hx, le_refl _âŸ© âŸ¨hy, le_refl _âŸ© a b ha hb hab).2 }
end

lemma concave_on_iff_convex_hypograph {Î³ : Type*} [ordered_add_comm_group Î³]
  [module â„ Î³] [ordered_smul â„ Î³]
  {f : E â†’ Î³} :
  concave_on s f â†” convex â„ {p : E Ã— Î³ | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1} :=
@convex_on_iff_convex_epigraph _ _ _ _ (order_dual Î³) _ _ _ f

/- A linear map is convex. -/
lemma linear_map.convex_on (f : E â†’â‚—[â„] Î²) {s : set E} (hs : convex â„ s) : convex_on s f :=
âŸ¨hs, Î» _ _ _ _ _ _ _ _ _, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

/- A linear map is concave. -/
lemma linear_map.concave_on (f : E â†’â‚—[â„] Î²) {s : set E} (hs : convex â„ s) : concave_on s f :=
âŸ¨hs, Î» _ _ _ _ _ _ _ _ _, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

/-- If a function is convex on `s`, it remains convex when precomposed by an affine map. -/
lemma convex_on.comp_affine_map {f : F â†’ Î²} (g : E â†’áµƒ[â„] F) {s : set F}
  (hf : convex_on s f) : convex_on (g â»Â¹' s) (f âˆ˜ g) :=
begin
  refine âŸ¨hf.1.affine_preimage  _,_âŸ©,
  intros x y xs ys a b ha hb hab,
  calc
    (f âˆ˜ g) (a â€¢ x + b â€¢ y) = f (g (a â€¢ x + b â€¢ y))         : rfl
                       ...  = f (a â€¢ (g x) + b â€¢ (g y))     : by rw [convex.combo_affine_apply hab]
                       ...  â‰¤ a â€¢ f (g x) + b â€¢ f (g y)     : hf.2 xs ys ha hb hab
                       ...  = a â€¢ (f âˆ˜ g) x + b â€¢ (f âˆ˜ g) y : rfl
end

/-- If a function is concave on `s`, it remains concave when precomposed by an affine map. -/
lemma concave_on.comp_affine_map {f : F â†’ Î²} (g : E â†’áµƒ[â„] F) {s : set F}
  (hf : concave_on s f) : concave_on (g â»Â¹' s) (f âˆ˜ g) :=
@convex_on.comp_affine_map _ _ (order_dual Î²) _ _ _ _ _ _ f g s hf

/-- If `g` is convex on `s`, so is `(g âˆ˜ f)` on `f â»Â¹' s` for a linear `f`. -/
lemma convex_on.comp_linear_map {g : F â†’ Î²} {s : set F} (hg : convex_on s g) (f : E â†’â‚—[â„] F) :
  convex_on (f â»Â¹' s) (g âˆ˜ f) :=
hg.comp_affine_map f.to_affine_map

/-- If `g` is concave on `s`, so is `(g âˆ˜ f)` on `f â»Â¹' s` for a linear `f`. -/
lemma concave_on.comp_linear_map {g : F â†’ Î²} {s : set F} (hg : concave_on s g) (f : E â†’â‚—[â„] F) :
  concave_on (f â»Â¹' s) (g âˆ˜ f) :=
hg.comp_affine_map f.to_affine_map

/-- If a function is convex on `s`, it remains convex after a translation. -/
lemma convex_on.translate_right {f : E â†’ Î²} {s : set E} {a : E} (hf : convex_on s f) :
  convex_on ((Î» z, a + z) â»Â¹' s) (f âˆ˜ (Î» z, a + z)) :=
hf.comp_affine_map $ affine_map.const â„ E a +áµ¥ affine_map.id â„ E

/-- If a function is concave on `s`, it remains concave after a translation. -/
lemma concave_on.translate_right {f : E â†’ Î²} {s : set E} {a : E} (hf : concave_on s f) :
  concave_on ((Î» z, a + z) â»Â¹' s) (f âˆ˜ (Î» z, a + z)) :=
hf.comp_affine_map $ affine_map.const â„ E a +áµ¥ affine_map.id â„ E

/-- If a function is convex on `s`, it remains convex after a translation. -/
lemma convex_on.translate_left {f : E â†’ Î²} {s : set E} {a : E} (hf : convex_on s f) :
  convex_on ((Î» z, a + z) â»Â¹' s) (f âˆ˜ (Î» z, z + a)) :=
by simpa only [add_comm] using hf.translate_right

/-- If a function is concave on `s`, it remains concave after a translation. -/
lemma concave_on.translate_left {f : E â†’ Î²} {s : set E} {a : E} (hf : concave_on s f) :
  concave_on ((Î» z, a + z) â»Â¹' s) (f âˆ˜ (Î» z, z + a)) :=
by simpa only [add_comm] using hf.translate_right

/-! ### Jensen's inequality -/

variables {i j : Î¹} {c : â„} {t : finset Î¹} {w : Î¹ â†’ â„} {z : Î¹ â†’ E}

/-- Convex **Jensen's inequality**, `finset.center_mass` version. -/
lemma convex_on.map_center_mass_le {f : E â†’ â„} (hf : convex_on s f)
  (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hpos : 0 < âˆ‘ i in t, w i)
  (hmem : âˆ€ i âˆˆ t, z i âˆˆ s) : f (t.center_mass w z) â‰¤ t.center_mass w (f âˆ˜ z) :=
begin
  have hmem' : âˆ€ i âˆˆ t, (z i, (f âˆ˜ z) i) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2},
    from Î» i hi, âŸ¨hmem i hi, le_rflâŸ©,
  convert (hf.convex_epigraph.center_mass_mem hâ‚€ hpos hmem').2;
    simp only [center_mass, function.comp, prod.smul_fst, prod.fst_sum, prod.smul_snd, prod.snd_sum]
end

/-- Convex **Jensen's inequality**, `finset.sum` version. -/
lemma convex_on.map_sum_le {f : E â†’ â„} (hf : convex_on s f)
  (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i in t, w i = 1)
  (hmem : âˆ€ i âˆˆ t, z i âˆˆ s) : f (âˆ‘ i in t, w i â€¢ z i) â‰¤ âˆ‘ i in t, w i * (f (z i)) :=
by simpa only [center_mass, hâ‚, inv_one, one_smul]
  using hf.map_center_mass_le hâ‚€ (hâ‚.symm â–¸ zero_lt_one) hmem

/-! ### Maximal principle -/

/-- If a function `f` is convex on `s` takes value `y` at the center of mass of some points
`z i âˆˆ s`, then for some `i` we have `y â‰¤ f (z i)`. -/
lemma convex_on.exists_ge_of_center_mass {f : E â†’ â„} (h : convex_on s f)
  (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hws : 0 < âˆ‘ i in t, w i) (hz : âˆ€ i âˆˆ t, z i âˆˆ s) :
  âˆƒ i âˆˆ t, f (t.center_mass w z) â‰¤ f (z i) :=
begin
  set y := t.center_mass w z,
  have : f y â‰¤ t.center_mass w (f âˆ˜ z) := h.map_center_mass_le hwâ‚€ hws hz,
  rw â† sum_filter_ne_zero at hws,
  rw [â† finset.center_mass_filter_ne_zero (f âˆ˜ z), center_mass, smul_eq_mul,
    â† div_eq_inv_mul, le_div_iff hws, mul_sum] at this,
  replace : âˆƒ i âˆˆ t.filter (Î» i, w i â‰  0), f y * w i â‰¤ w i â€¢ (f âˆ˜ z) i :=
    exists_le_of_sum_le (nonempty_of_sum_ne_zero (ne_of_gt hws)) this,
  rcases this with âŸ¨i, hi, HâŸ©,
  rw [mem_filter] at hi,
  use [i, hi.1],
  simp only [smul_eq_mul, mul_comm (w i)] at H,
  refine (mul_le_mul_right _).1 H,
  exact lt_of_le_of_ne (hwâ‚€ i hi.1) hi.2.symm
end

/-- Maximum principle for convex functions. If a function `f` is convex on the convex hull of `s`,
then `f` can't have a maximum on `convex_hull s` outside of `s`. -/
lemma convex_on.exists_ge_of_mem_convex_hull {f : E â†’ â„} (hf : convex_on (convex_hull â„ s) f)
  {x} (hx : x âˆˆ convex_hull â„ s) : âˆƒ y âˆˆ s, f x â‰¤ f y :=
begin
  rw _root_.convex_hull_eq at hx,
  rcases hx with âŸ¨Î±, t, w, z, hwâ‚€, hwâ‚, hz, rflâŸ©,
  rcases hf.exists_ge_of_center_mass hwâ‚€ (hwâ‚.symm â–¸ zero_lt_one)
    (Î» i hi, subset_convex_hull â„ s (hz i hi)) with âŸ¨i, hit, HiâŸ©,
  exact âŸ¨z i, hz i hit, HiâŸ©
end
