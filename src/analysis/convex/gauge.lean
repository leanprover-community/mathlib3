/-
Copyright (c) 2021 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import analysis.convex.basic
import analysis.normed_space.pointwise
import analysis.seminorm
import data.is_R_or_C.basic
import tactic.congrm

/-!
# The Minkowksi functional

This file defines the Minkowski functional, aka gauge.

The Minkowski functional of a set `s` is the function which associates each point to how much you
need to scale `s` for `x` to be inside it. When `s` is symmetric, convex and absorbent, its gauge is
a seminorm. Reciprocally, any seminorm arises as the gauge of some set, namely its unit ball. This
induces the equivalence of seminorms and locally convex topological vector spaces.

## Main declarations

For a real vector space,
* `gauge`: Aka Minkowksi functional. `gauge s x` is the least (actually, an infimum) `r` such
  that `x ‚àà r ‚Ä¢ s`.
* `gauge_seminorm`: The Minkowski functional as a seminorm, when `s` is symmetric, convex and
  absorbent.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

Minkowski functional, gauge
-/

open normed_field set
open_locale pointwise

noncomputable theory

variables {ùïú E F : Type*}

section add_comm_group
variables [add_comm_group E] [module ‚Ñù E]

/--The Minkowski functional. Given a set `s` in a real vector space, `gauge s` is the functional
which sends `x : E` to the smallest `r : ‚Ñù` such that `x` is in `s` scaled by `r`. -/
def gauge (s : set E) (x : E) : ‚Ñù := Inf {r : ‚Ñù | 0 < r ‚àß x ‚àà r ‚Ä¢ s}

variables {s t : set E} {a : ‚Ñù} {x : E}

lemma gauge_def : gauge s x = Inf {r ‚àà set.Ioi 0 | x ‚àà r ‚Ä¢ s} := rfl

/-- An alternative definition of the gauge using scalar multiplication on the element rather than on
the set. -/
lemma gauge_def' : gauge s x = Inf {r ‚àà set.Ioi 0 | r‚Åª¬π ‚Ä¢ x ‚àà s} :=
begin
  congrm Inf (Œª r, _),
  exact and_congr_right (Œª hr, mem_smul_set_iff_inv_smul_mem‚ÇÄ hr.ne' _ _),
end

private lemma gauge_set_bdd_below : bdd_below {r : ‚Ñù | 0 < r ‚àß x ‚àà r ‚Ä¢ s} := ‚ü®0, Œª r hr, hr.1.le‚ü©

/-- If the given subset is `absorbent` then the set we take an infimum over in `gauge` is nonempty,
which is useful for proving many properties about the gauge.  -/
lemma absorbent.gauge_set_nonempty (absorbs : absorbent ‚Ñù s) :
  {r : ‚Ñù | 0 < r ‚àß x ‚àà r ‚Ä¢ s}.nonempty :=
let ‚ü®r, hr‚ÇÅ, hr‚ÇÇ‚ü© := absorbs x in ‚ü®r, hr‚ÇÅ, hr‚ÇÇ r (real.norm_of_nonneg hr‚ÇÅ.le).ge‚ü©

lemma gauge_mono (hs : absorbent ‚Ñù s) (h : s ‚äÜ t) : gauge t ‚â§ gauge s :=
Œª x, cInf_le_cInf gauge_set_bdd_below hs.gauge_set_nonempty $ Œª r hr, ‚ü®hr.1, smul_set_mono h hr.2‚ü©

lemma exists_lt_of_gauge_lt (absorbs : absorbent ‚Ñù s) (h : gauge s x < a) :
  ‚àÉ b, 0 < b ‚àß b < a ‚àß x ‚àà b ‚Ä¢ s :=
begin
  obtain ‚ü®b, ‚ü®hb, hx‚ü©, hba‚ü© := exists_lt_of_cInf_lt absorbs.gauge_set_nonempty h,
  exact ‚ü®b, hb, hba, hx‚ü©,
end

/-- The gauge evaluated at `0` is always zero (mathematically this requires `0` to be in the set `s`
but, the real infimum of the empty set in Lean being defined as `0`, it holds unconditionally). -/
@[simp] lemma gauge_zero : gauge s 0 = 0 :=
begin
  rw gauge_def',
  by_cases (0 : E) ‚àà s,
  { simp only [smul_zero, sep_true, h, cInf_Ioi] },
  { simp only [smul_zero, sep_false, h, real.Inf_empty] }
end

@[simp] lemma gauge_zero' : gauge (0 : set E) = 0 :=
begin
  ext,
  rw gauge_def',
  obtain rfl | hx := eq_or_ne x 0,
  { simp only [cInf_Ioi, mem_zero, pi.zero_apply, eq_self_iff_true, sep_true, smul_zero] },
  { simp only [mem_zero, pi.zero_apply, inv_eq_zero, smul_eq_zero],
    convert real.Inf_empty,
    exact eq_empty_iff_forall_not_mem.2 (Œª r hr, hr.2.elim (ne_of_gt hr.1) hx) }
end

@[simp] lemma gauge_empty : gauge (‚àÖ : set E) = 0 :=
by { ext, simp only [gauge_def', real.Inf_empty, mem_empty_iff_false, pi.zero_apply, sep_false] }

lemma gauge_of_subset_zero (h : s ‚äÜ 0) : gauge s = 0 :=
by { obtain rfl | rfl := subset_singleton_iff_eq.1 h, exacts [gauge_empty, gauge_zero'] }

/-- The gauge is always nonnegative. -/
lemma gauge_nonneg (x : E) : 0 ‚â§ gauge s x := real.Inf_nonneg _ $ Œª x hx, hx.1.le

lemma gauge_neg (symmetric : ‚àÄ x ‚àà s, -x ‚àà s) (x : E) : gauge s (-x) = gauge s x :=
begin
  have : ‚àÄ x, -x ‚àà s ‚Üî x ‚àà s := Œª x, ‚ü®Œª h, by simpa using symmetric _ h, symmetric x‚ü©,
  simp_rw [gauge_def', smul_neg, this]
end

lemma gauge_neg_set_neg (x : E) : gauge (-s) (-x) = gauge s x :=
by simp_rw [gauge_def', smul_neg, neg_mem_neg]

lemma gauge_neg_set_eq_gauge_neg (x : E) : gauge (-s) x = gauge s (-x) :=
by rw [‚Üê gauge_neg_set_neg, neg_neg]

lemma gauge_le_of_mem (ha : 0 ‚â§ a) (hx : x ‚àà a ‚Ä¢ s) : gauge s x ‚â§ a :=
begin
  obtain rfl | ha' := ha.eq_or_lt,
  { rw [mem_singleton_iff.1 (zero_smul_set_subset _ hx), gauge_zero] },
  { exact cInf_le gauge_set_bdd_below ‚ü®ha', hx‚ü© }
end

lemma gauge_le_eq (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÄ : (0 : E) ‚àà s) (hs‚ÇÇ : absorbent ‚Ñù s) (ha : 0 ‚â§ a) :
  {x | gauge s x ‚â§ a} = ‚ãÇ (r : ‚Ñù) (H : a < r), r ‚Ä¢ s :=
begin
  ext,
  simp_rw [set.mem_Inter, set.mem_set_of_eq],
  refine ‚ü®Œª h r hr, _, Œª h, le_of_forall_pos_lt_add (Œª Œµ hŒµ, _)‚ü©,
  { have hr' := ha.trans_lt hr,
    rw mem_smul_set_iff_inv_smul_mem‚ÇÄ hr'.ne',
    obtain ‚ü®Œ¥, Œ¥_pos, hŒ¥r, hŒ¥‚ü© := exists_lt_of_gauge_lt hs‚ÇÇ (h.trans_lt hr),
    suffices : (r‚Åª¬π * Œ¥) ‚Ä¢ Œ¥‚Åª¬π ‚Ä¢ x ‚àà s,
    { rwa [smul_smul, mul_inv_cancel_right‚ÇÄ Œ¥_pos.ne'] at this },
    rw mem_smul_set_iff_inv_smul_mem‚ÇÄ Œ¥_pos.ne' at hŒ¥,
    refine hs‚ÇÅ.smul_mem_of_zero_mem hs‚ÇÄ hŒ¥ ‚ü®by positivity, _‚ü©,
    rw [inv_mul_le_iff hr', mul_one],
    exact hŒ¥r.le },
  { have hŒµ' := (lt_add_iff_pos_right a).2 (half_pos hŒµ),
    exact (gauge_le_of_mem (ha.trans hŒµ'.le) $ h _ hŒµ').trans_lt
      (add_lt_add_left (half_lt_self hŒµ) _) }
end

lemma gauge_lt_eq' (absorbs : absorbent ‚Ñù s) (a : ‚Ñù) :
  {x | gauge s x < a} = ‚ãÉ (r : ‚Ñù) (H : 0 < r) (H : r < a), r ‚Ä¢ s :=
begin
  ext,
  simp_rw [mem_set_of_eq, mem_Union, exists_prop],
  exact ‚ü®exists_lt_of_gauge_lt absorbs,
    Œª ‚ü®r, hr‚ÇÄ, hr‚ÇÅ, hx‚ü©, (gauge_le_of_mem hr‚ÇÄ.le hx).trans_lt hr‚ÇÅ‚ü©,
end

lemma gauge_lt_eq (absorbs : absorbent ‚Ñù s) (a : ‚Ñù) :
  {x | gauge s x < a} = ‚ãÉ (r ‚àà set.Ioo 0 (a : ‚Ñù)), r ‚Ä¢ s :=
begin
  ext,
  simp_rw [mem_set_of_eq, mem_Union, exists_prop, mem_Ioo, and_assoc],
  exact ‚ü®exists_lt_of_gauge_lt absorbs,
    Œª ‚ü®r, hr‚ÇÄ, hr‚ÇÅ, hx‚ü©, (gauge_le_of_mem hr‚ÇÄ.le hx).trans_lt hr‚ÇÅ‚ü©,
end

lemma gauge_lt_one_subset_self (hs : convex ‚Ñù s) (h‚ÇÄ : (0 : E) ‚àà s) (absorbs : absorbent ‚Ñù s) :
  {x | gauge s x < 1} ‚äÜ s :=
begin
  rw gauge_lt_eq absorbs,
  refine set.Union‚ÇÇ_subset (Œª r hr _, _),
  rintro ‚ü®y, hy, rfl‚ü©,
  exact hs.smul_mem_of_zero_mem h‚ÇÄ hy (Ioo_subset_Icc_self hr),
end

lemma gauge_le_one_of_mem {x : E} (hx : x ‚àà s) : gauge s x ‚â§ 1 :=
gauge_le_of_mem zero_le_one $ by rwa one_smul

lemma self_subset_gauge_le_one : s ‚äÜ {x | gauge s x ‚â§ 1} := Œª x, gauge_le_one_of_mem

lemma convex.gauge_le (hs : convex ‚Ñù s) (h‚ÇÄ : (0 : E) ‚àà s) (absorbs : absorbent ‚Ñù s) (a : ‚Ñù) :
  convex ‚Ñù {x | gauge s x ‚â§ a} :=
begin
  by_cases ha : 0 ‚â§ a,
  { rw gauge_le_eq hs h‚ÇÄ absorbs ha,
    exact convex_Inter (Œª i, convex_Inter (Œª hi, hs.smul _)) },
  { convert convex_empty,
    exact eq_empty_iff_forall_not_mem.2 (Œª x hx, ha $ (gauge_nonneg _).trans hx) }
end

lemma balanced.star_convex (hs : balanced ‚Ñù s) : star_convex ‚Ñù 0 s :=
star_convex_zero_iff.2 $ Œª x hx a ha‚ÇÄ ha‚ÇÅ,
  hs _ (by rwa real.norm_of_nonneg ha‚ÇÄ) (smul_mem_smul_set hx)

lemma le_gauge_of_not_mem (hs‚ÇÄ : star_convex ‚Ñù 0 s) (hs‚ÇÇ : absorbs ‚Ñù s {x}) (hx : x ‚àâ a ‚Ä¢ s) :
  a ‚â§ gauge s x :=
begin
  rw star_convex_zero_iff at hs‚ÇÄ,
  obtain ‚ü®r, hr, h‚ü© := hs‚ÇÇ,
  refine le_cInf ‚ü®r, hr, singleton_subset_iff.1 $ h _ (real.norm_of_nonneg hr.le).ge‚ü© _,
  rintro b ‚ü®hb, x, hx', rfl‚ü©,
  refine not_lt.1 (Œª hba, hx _),
  have ha := hb.trans hba,
  refine ‚ü®(a‚Åª¬π * b) ‚Ä¢ x, hs‚ÇÄ hx' (by positivity) _, _‚ü©,
  { rw ‚Üêdiv_eq_inv_mul,
    exact div_le_one_of_le hba.le ha.le },
  { rw [‚Üêmul_smul, mul_inv_cancel_left‚ÇÄ ha.ne'] }
end

lemma one_le_gauge_of_not_mem (hs‚ÇÅ : star_convex ‚Ñù 0 s) (hs‚ÇÇ : absorbs ‚Ñù s {x}) (hx : x ‚àâ s) :
  1 ‚â§ gauge s x :=
le_gauge_of_not_mem hs‚ÇÅ hs‚ÇÇ $ by rwa one_smul

section linear_ordered_field
variables {Œ± : Type*} [linear_ordered_field Œ±] [mul_action_with_zero Œ± ‚Ñù] [ordered_smul Œ± ‚Ñù]

lemma gauge_smul_of_nonneg [mul_action_with_zero Œ± E] [is_scalar_tower Œ± ‚Ñù (set E)] {s : set E}
  {a : Œ±} (ha : 0 ‚â§ a) (x : E) :
  gauge s (a ‚Ä¢ x) = a ‚Ä¢ gauge s x :=
begin
  obtain rfl | ha' := ha.eq_or_lt,
  { rw [zero_smul, gauge_zero, zero_smul] },
  rw [gauge_def', gauge_def', ‚Üêreal.Inf_smul_of_nonneg ha],
  congr' 1,
  ext r,
  simp_rw [set.mem_smul_set, set.mem_sep_iff],
  split,
  { rintro ‚ü®hr, hx‚ü©,
    simp_rw mem_Ioi at ‚ä¢ hr,
    rw ‚Üêmem_smul_set_iff_inv_smul_mem‚ÇÄ hr.ne' at hx,
    have := smul_pos (inv_pos.2 ha') hr,
    refine ‚ü®a‚Åª¬π ‚Ä¢ r, ‚ü®this, _‚ü©, smul_inv_smul‚ÇÄ ha'.ne' _‚ü©,
    rwa [‚Üêmem_smul_set_iff_inv_smul_mem‚ÇÄ this.ne', smul_assoc,
      mem_smul_set_iff_inv_smul_mem‚ÇÄ (inv_ne_zero ha'.ne'), inv_inv] },
  { rintro ‚ü®r, ‚ü®hr, hx‚ü©, rfl‚ü©,
    rw mem_Ioi at ‚ä¢ hr,
    rw ‚Üêmem_smul_set_iff_inv_smul_mem‚ÇÄ hr.ne' at hx,
    have := smul_pos ha' hr,
    refine ‚ü®this, _‚ü©,
    rw [‚Üêmem_smul_set_iff_inv_smul_mem‚ÇÄ this.ne', smul_assoc],
    exact smul_mem_smul_set hx }
end

lemma gauge_smul_left_of_nonneg [mul_action_with_zero Œ± E] [smul_comm_class Œ± ‚Ñù ‚Ñù]
  [is_scalar_tower Œ± ‚Ñù ‚Ñù] [is_scalar_tower Œ± ‚Ñù E] {s : set E} {a : Œ±} (ha : 0 ‚â§ a) :
  gauge (a ‚Ä¢ s) = a‚Åª¬π ‚Ä¢ gauge s :=
begin
  obtain rfl | ha' := ha.eq_or_lt,
  { rw [inv_zero, zero_smul, gauge_of_subset_zero (zero_smul_set_subset _)] },
  ext,
  rw [gauge_def', pi.smul_apply, gauge_def', ‚Üêreal.Inf_smul_of_nonneg (inv_nonneg.2 ha)],
  congr' 1,
  ext r,
  simp_rw [set.mem_smul_set, set.mem_sep_iff],
  split,
  { rintro ‚ü®hr, y, hy, h‚ü©,
    simp_rw [mem_Ioi] at ‚ä¢ hr,
    refine ‚ü®a ‚Ä¢ r, ‚ü®smul_pos ha' hr, _‚ü©, inv_smul_smul‚ÇÄ ha'.ne' _‚ü©,
    rwa [smul_inv‚ÇÄ, smul_assoc, ‚Üêh, inv_smul_smul‚ÇÄ ha'.ne'] },
  { rintro ‚ü®r, ‚ü®hr, hx‚ü©, rfl‚ü©,
    rw mem_Ioi at ‚ä¢ hr,
    have := smul_pos ha' hr,
    refine ‚ü®smul_pos (inv_pos.2 ha') hr, r‚Åª¬π ‚Ä¢ x, hx, _‚ü©,
    rw [smul_inv‚ÇÄ, smul_assoc, inv_inv] }
end

lemma gauge_smul_left [module Œ± E] [smul_comm_class Œ± ‚Ñù ‚Ñù] [is_scalar_tower Œ± ‚Ñù ‚Ñù]
  [is_scalar_tower Œ± ‚Ñù E] {s : set E} (symmetric : ‚àÄ x ‚àà s, -x ‚àà s) (a : Œ±) :
  gauge (a ‚Ä¢ s) = |a|‚Åª¬π ‚Ä¢ gauge s :=
begin
  rw ‚Üêgauge_smul_left_of_nonneg (abs_nonneg a),
  obtain h | h := abs_choice a,
  { rw h },
  { rw [h, set.neg_smul_set, ‚Üêset.smul_set_neg],
    congr,
    ext y,
    refine ‚ü®symmetric _, Œª hy, _‚ü©,
    rw ‚Üêneg_neg y,
    exact symmetric _ hy },
  { apply_instance }
end

end linear_ordered_field

section is_R_or_C
variables [is_R_or_C ùïú] [module ùïú E] [is_scalar_tower ‚Ñù ùïú E]

lemma gauge_norm_smul (hs : balanced ùïú s) (r : ùïú) (x : E) : gauge s (‚Äñr‚Äñ ‚Ä¢ x) = gauge s (r ‚Ä¢ x) :=
begin
  rw @is_R_or_C.real_smul_eq_coe_smul ùïú,
  obtain rfl | hr := eq_or_ne r 0,
  { simp only [norm_zero, is_R_or_C.of_real_zero] },
  unfold gauge,
  congr' with Œ∏,
  refine and_congr_right (Œª hŒ∏, (hs.smul _).mem_smul_iff _),
  rw [is_R_or_C.norm_of_real, norm_norm],
end

/-- If `s` is balanced, then the Minkowski functional is ‚ÑÇ-homogeneous. -/
lemma gauge_smul (hs : balanced ùïú s) (r : ùïú) (x : E) : gauge s (r ‚Ä¢ x) = ‚Äñr‚Äñ * gauge s x :=
by { rw [‚Üêsmul_eq_mul, ‚Üêgauge_smul_of_nonneg (norm_nonneg r), gauge_norm_smul hs], apply_instance }

end is_R_or_C

section topological_space
variables [topological_space E] [has_continuous_smul ‚Ñù E]

lemma interior_subset_gauge_lt_one (s : set E) : interior s ‚äÜ {x | gauge s x < 1} :=
begin
  intros x hx,
  let f : ‚Ñù ‚Üí E := Œª t, t ‚Ä¢ x,
  have hf : continuous f,
  { continuity },
  let s' := f ‚Åª¬π' (interior s),
  have hs' : is_open s' := hf.is_open_preimage _ is_open_interior,
  have one_mem : (1 : ‚Ñù) ‚àà s',
  { simpa only [s', f, set.mem_preimage, one_smul] },
  obtain ‚ü®Œµ, hŒµ‚ÇÄ, hŒµ‚ü© := (metric.nhds_basis_closed_ball.1 _).1
    (is_open_iff_mem_nhds.1 hs' 1 one_mem),
  rw real.closed_ball_eq_Icc at hŒµ,
  have hŒµ‚ÇÅ : 0 < 1 + Œµ := hŒµ‚ÇÄ.trans (lt_one_add Œµ),
  have : (1 + Œµ)‚Åª¬π < 1,
  { rw inv_lt_one_iff,
    right,
    linarith },
  refine (gauge_le_of_mem (inv_nonneg.2 hŒµ‚ÇÅ.le) _).trans_lt this,
  rw mem_inv_smul_set_iff‚ÇÄ hŒµ‚ÇÅ.ne',
  exact interior_subset
    (hŒµ ‚ü®(sub_le_self _ hŒµ‚ÇÄ.le).trans ((le_add_iff_nonneg_right _).2 hŒµ‚ÇÄ.le), le_rfl‚ü©),
end

lemma gauge_lt_one_eq_self_of_open (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÄ : (0 : E) ‚àà s) (hs‚ÇÇ : is_open s) :
  {x | gauge s x < 1} = s :=
begin
  refine (gauge_lt_one_subset_self hs‚ÇÅ ‚Äπ_‚Ä∫ $ absorbent_nhds_zero $ hs‚ÇÇ.mem_nhds hs‚ÇÄ).antisymm _,
  convert interior_subset_gauge_lt_one s,
  exact hs‚ÇÇ.interior_eq.symm,
end

lemma gauge_lt_one_of_mem_of_open (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÄ : (0 : E) ‚àà s) (hs‚ÇÇ : is_open s)
  {x : E} (hx : x ‚àà s) :
  gauge s x < 1 :=
by rwa ‚Üêgauge_lt_one_eq_self_of_open hs‚ÇÅ hs‚ÇÄ hs‚ÇÇ at hx

lemma gauge_lt_of_mem_smul (x : E) (Œµ : ‚Ñù) (hŒµ : 0 < Œµ) (hs‚ÇÄ : (0 : E) ‚àà s)
  (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÇ : is_open s) (hx : x ‚àà Œµ ‚Ä¢ s) :
  gauge s x < Œµ :=
begin
  have : Œµ‚Åª¬π ‚Ä¢ x ‚àà s,
  { rwa ‚Üêmem_smul_set_iff_inv_smul_mem‚ÇÄ hŒµ.ne' },
  have h_gauge_lt := gauge_lt_one_of_mem_of_open hs‚ÇÅ hs‚ÇÄ hs‚ÇÇ this,
  rwa [gauge_smul_of_nonneg (inv_nonneg.2 hŒµ.le), smul_eq_mul, inv_mul_lt_iff hŒµ, mul_one]
    at h_gauge_lt,
  apply_instance
end

end topological_space

lemma gauge_add_le (hs : convex ‚Ñù s) (absorbs : absorbent ‚Ñù s) (x y : E) :
  gauge s (x + y) ‚â§ gauge s x + gauge s y :=
begin
  refine le_of_forall_pos_lt_add (Œª Œµ hŒµ, _),
  obtain ‚ü®a, ha, ha', hx‚ü© := exists_lt_of_gauge_lt absorbs
    (lt_add_of_pos_right (gauge s x) (half_pos hŒµ)),
  obtain ‚ü®b, hb, hb', hy‚ü© := exists_lt_of_gauge_lt absorbs
    (lt_add_of_pos_right (gauge s y) (half_pos hŒµ)),
  rw mem_smul_set_iff_inv_smul_mem‚ÇÄ ha.ne' at hx,
  rw mem_smul_set_iff_inv_smul_mem‚ÇÄ hb.ne' at hy,
  suffices : gauge s (x + y) ‚â§ a + b,
  { linarith },
  have hab : 0 < a + b := add_pos ha hb,
  apply gauge_le_of_mem hab.le,
  have := convex_iff_div.1 hs hx hy ha.le hb.le hab,
  rwa [smul_smul, smul_smul, ‚Üêmul_div_right_comm, ‚Üêmul_div_right_comm, mul_inv_cancel ha.ne',
    mul_inv_cancel hb.ne', ‚Üêsmul_add, one_div, ‚Üêmem_smul_set_iff_inv_smul_mem‚ÇÄ hab.ne'] at this,
end

section is_R_or_C
variables [is_R_or_C ùïú] [module ùïú E] [is_scalar_tower ‚Ñù ùïú E]

/-- `gauge s` as a seminorm when `s` is  balanced, convex and absorbent. -/
@[simps] def gauge_seminorm (hs‚ÇÄ : balanced ùïú s)  (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÇ : absorbent ‚Ñù s) :
  seminorm ùïú E :=
seminorm.of (gauge s) (gauge_add_le hs‚ÇÅ hs‚ÇÇ) (gauge_smul hs‚ÇÄ)

variables {hs‚ÇÄ : balanced ùïú s} {hs‚ÇÅ : convex ‚Ñù s} {hs‚ÇÇ : absorbent ‚Ñù s} [topological_space E]
  [has_continuous_smul ‚Ñù E]

lemma gauge_seminorm_lt_one_of_open (hs : is_open s) {x : E} (hx : x ‚àà s) :
  gauge_seminorm hs‚ÇÄ hs‚ÇÅ hs‚ÇÇ x < 1 :=
gauge_lt_one_of_mem_of_open hs‚ÇÅ hs‚ÇÇ.zero_mem hs hx

lemma gauge_seminorm_ball_one (hs : is_open s) :
  (gauge_seminorm hs‚ÇÄ hs‚ÇÅ hs‚ÇÇ).ball 0 1 = s :=
begin
  rw seminorm.ball_zero_eq,
  exact gauge_lt_one_eq_self_of_open hs‚ÇÅ hs‚ÇÇ.zero_mem hs
end

end is_R_or_C

/-- Any seminorm arises as the gauge of its unit ball. -/
@[simp] protected lemma seminorm.gauge_ball (p : seminorm ‚Ñù E) : gauge (p.ball 0 1) = p :=
begin
  ext,
  obtain hp | hp := {r : ‚Ñù | 0 < r ‚àß x ‚àà r ‚Ä¢ p.ball 0 1}.eq_empty_or_nonempty,
  { rw [gauge, hp, real.Inf_empty],
    by_contra,
    have hpx : 0 < p x := (map_nonneg _ _).lt_of_ne h,
    have hpx‚ÇÇ : 0 < 2 * p x := mul_pos zero_lt_two hpx,
    refine hp.subset ‚ü®hpx‚ÇÇ, (2 * p x)‚Åª¬π ‚Ä¢ x, _, smul_inv_smul‚ÇÄ hpx‚ÇÇ.ne' _‚ü©,
    rw [p.mem_ball_zero, map_smul_eq_mul, real.norm_eq_abs, abs_of_pos (inv_pos.2 hpx‚ÇÇ),
      inv_mul_lt_iff hpx‚ÇÇ, mul_one],
    exact lt_mul_of_one_lt_left hpx one_lt_two },
  refine is_glb.cInf_eq ‚ü®Œª r, _, Œª r hr, le_of_forall_pos_le_add $ Œª Œµ hŒµ, _‚ü© hp,
  { rintro ‚ü®hr, y, hy, rfl‚ü©,
    rw p.mem_ball_zero at hy,
    rw [map_smul_eq_mul, real.norm_eq_abs, abs_of_pos hr],
    exact mul_le_of_le_one_right hr.le hy.le },
  { have hpŒµ : 0 < p x + Œµ := by positivity,
    refine hr ‚ü®hpŒµ, (p x + Œµ)‚Åª¬π ‚Ä¢ x, _, smul_inv_smul‚ÇÄ hpŒµ.ne' _‚ü©,
    rw [p.mem_ball_zero, map_smul_eq_mul, real.norm_eq_abs, abs_of_pos (inv_pos.2 hpŒµ),
      inv_mul_lt_iff hpŒµ, mul_one],
    exact lt_add_of_pos_right _ hŒµ }
end

lemma seminorm.gauge_seminorm_ball (p : seminorm ‚Ñù E) :
  gauge_seminorm (p.balanced_ball_zero 1) (p.convex_ball 0 1)
    (p.absorbent_ball_zero zero_lt_one) = p := fun_like.coe_injective p.gauge_ball

end add_comm_group

section norm
variables [seminormed_add_comm_group E] [normed_space ‚Ñù E] {s : set E} {r : ‚Ñù} {x : E}

lemma gauge_unit_ball (x : E) : gauge (metric.ball (0 : E) 1) x = ‚Äñx‚Äñ :=
begin
  obtain rfl | hx := eq_or_ne x 0,
  { rw [norm_zero, gauge_zero] },
  refine (le_of_forall_pos_le_add $ Œª Œµ hŒµ, _).antisymm _,
  { have : 0 < ‚Äñx‚Äñ + Œµ := by positivity,
    refine gauge_le_of_mem this.le _,
    rw [smul_ball this.ne', smul_zero, real.norm_of_nonneg this.le, mul_one, mem_ball_zero_iff],
    exact lt_add_of_pos_right _ hŒµ },
  refine le_gauge_of_not_mem balanced_ball_zero.star_convex
    (absorbent_ball_zero zero_lt_one).absorbs (Œª h, _),
  obtain hx' | hx' := eq_or_ne (‚Äñx‚Äñ) 0,
  { rw hx' at h,
    exact hx (zero_smul_set_subset _ h) },
  { rw [mem_smul_set_iff_inv_smul_mem‚ÇÄ hx', mem_ball_zero_iff, norm_smul, norm_inv, norm_norm,
      inv_mul_cancel hx'] at h,
    exact lt_irrefl _ h }
end

lemma gauge_ball (hr : 0 < r) (x : E) : gauge (metric.ball (0 : E) r) x = ‚Äñx‚Äñ / r :=
begin
  rw [‚Üêsmul_unit_ball_of_pos hr, gauge_smul_left, pi.smul_apply, gauge_unit_ball, smul_eq_mul,
    abs_of_nonneg hr.le, div_eq_inv_mul],
  simp_rw [mem_ball_zero_iff, norm_neg],
  exact Œª _, id,
end

lemma mul_gauge_le_norm (hs : metric.ball (0 : E) r ‚äÜ s) : r * gauge s x ‚â§ ‚Äñx‚Äñ :=
begin
  obtain hr | hr := le_or_lt r 0,
  { exact (mul_nonpos_of_nonpos_of_nonneg hr $ gauge_nonneg _).trans (norm_nonneg _) },
  rw [mul_comm, ‚Üêle_div_iff hr, ‚Üêgauge_ball hr],
  exact gauge_mono (absorbent_ball_zero hr) hs x,
end

end norm
