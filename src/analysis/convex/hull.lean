/-
Copyright (c) 2020 Yury Kudriashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudriashov, YaÃ«l Dillies
-/
import analysis.convex.basic
import order.closure

/-!
# Convex hull

This file defines the convex hull of a set `s` in a module. `convex_hull ğ•œ s` is the smallest convex
set containing `s`. In order theory speak, this is a closure operator.

## Implementation notes

`convex_hull` is defined as a closure operator. This gives access to the `closure_operator` API
while the impact on writing code is minimal as `convex_hull ğ•œ s` is automatically elaborated as
`â‡‘(convex_hull ğ•œ) s`.
-/

open set
open_locale pointwise

variables {ğ•œ E F : Type*}

section convex_hull
section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables (ğ•œ) [add_comm_monoid E] [add_comm_monoid F] [module ğ•œ E] [module ğ•œ F]

/-- The convex hull of a set `s` is the minimal convex set that includes `s`. -/
def convex_hull : closure_operator (set E) :=
closure_operator.mkâ‚ƒ
  (Î» s, â‹‚ (t : set E) (hst : s âŠ† t) (ht : convex ğ•œ t), t)
  (convex ğ•œ)
  (Î» s, set.subset_Inter (Î» t, set.subset_Inter $ Î» hst, set.subset_Inter $ Î» ht, hst))
  (Î» s, convex_Inter $ Î» t, convex_Inter $ Î» ht, convex_Inter id)
  (Î» s t hst ht, set.Inter_subset_of_subset t $ set.Inter_subset_of_subset hst $
  set.Inter_subset _ ht)

variables (s : set E)

lemma subset_convex_hull : s âŠ† convex_hull ğ•œ s := (convex_hull ğ•œ).le_closure s

lemma convex_convex_hull : convex ğ•œ (convex_hull ğ•œ s) := closure_operator.closure_mem_mkâ‚ƒ s

lemma convex_hull_eq_Inter : convex_hull ğ•œ s = â‹‚ (t : set E) (hst : s âŠ† t) (ht : convex ğ•œ t), t :=
rfl

variables {ğ•œ s} {t : set E} {x y : E}

lemma mem_convex_hull_iff : x âˆˆ convex_hull ğ•œ s â†” âˆ€ t, s âŠ† t â†’ convex ğ•œ t â†’ x âˆˆ t :=
by simp_rw [convex_hull_eq_Inter, mem_Inter]

lemma convex_hull_min (hst : s âŠ† t) (ht : convex ğ•œ t) : convex_hull ğ•œ s âŠ† t :=
closure_operator.closure_le_mkâ‚ƒ_iff (show s â‰¤ t, from hst) ht

lemma convex.convex_hull_subset_iff (ht : convex ğ•œ t) : convex_hull ğ•œ s âŠ† t â†” s âŠ† t :=
âŸ¨(subset_convex_hull _ _).trans, Î» h, convex_hull_min h htâŸ©

@[mono] lemma convex_hull_mono (hst : s âŠ† t) : convex_hull ğ•œ s âŠ† convex_hull ğ•œ t :=
(convex_hull ğ•œ).monotone hst

lemma convex.convex_hull_eq (hs : convex ğ•œ s) : convex_hull ğ•œ s = s :=
closure_operator.mem_mkâ‚ƒ_closed hs

@[simp] lemma convex_hull_univ : convex_hull ğ•œ (univ : set E) = univ :=
closure_operator.closure_top (convex_hull ğ•œ)

@[simp] lemma convex_hull_empty : convex_hull ğ•œ (âˆ… : set E) = âˆ… := convex_empty.convex_hull_eq

@[simp] lemma convex_hull_empty_iff : convex_hull ğ•œ s = âˆ… â†” s = âˆ… :=
begin
  split,
  { intro h,
    rw [â†set.subset_empty_iff, â†h],
    exact subset_convex_hull ğ•œ _ },
  { rintro rfl,
    exact convex_hull_empty }
end

@[simp] lemma convex_hull_nonempty_iff : (convex_hull ğ•œ s).nonempty â†” s.nonempty :=
begin
  rw [nonempty_iff_ne_empty, nonempty_iff_ne_empty, ne.def, ne.def],
  exact not_congr convex_hull_empty_iff,
end

alias convex_hull_nonempty_iff â†” _ set.nonempty.convex_hull

attribute [protected] set.nonempty.convex_hull

lemma segment_subset_convex_hull (hx : x âˆˆ s) (hy : y âˆˆ s) : segment ğ•œ x y âŠ† convex_hull ğ•œ s :=
(convex_convex_hull _ _).segment_subset (subset_convex_hull _ _ hx) (subset_convex_hull _ _ hy)

@[simp] lemma convex_hull_singleton (x : E) : convex_hull ğ•œ ({x} : set E) = {x} :=
(convex_singleton x).convex_hull_eq

@[simp] lemma convex_hull_pair (x y : E) : convex_hull ğ•œ {x, y} = segment ğ•œ x y :=
begin
  refine (convex_hull_min _ $ convex_segment _ _).antisymm
    (segment_subset_convex_hull (mem_insert _ _) $ mem_insert_of_mem _ $ mem_singleton _),
  rw [insert_subset, singleton_subset_iff],
  exact âŸ¨left_mem_segment _ _ _, right_mem_segment _ _ _âŸ©,
end

lemma convex_hull_convex_hull_union_left (s t : set E) :
  convex_hull ğ•œ (convex_hull ğ•œ s âˆª t) = convex_hull ğ•œ (s âˆª t) :=
closure_operator.closure_sup_closure_left _ _ _

lemma convex_hull_convex_hull_union_right (s t : set E) :
  convex_hull ğ•œ (s âˆª convex_hull ğ•œ t) = convex_hull ğ•œ (s âˆª t) :=
closure_operator.closure_sup_closure_right _ _ _

lemma convex.convex_remove_iff_not_mem_convex_hull_remove {s : set E} (hs : convex ğ•œ s) (x : E) :
  convex ğ•œ (s \ {x}) â†” x âˆ‰ convex_hull ğ•œ (s \ {x}) :=
begin
  split,
  { rintro hsx hx,
    rw hsx.convex_hull_eq at hx,
    exact hx.2 (mem_singleton _) },
  rintro hx,
  suffices h : s \ {x} = convex_hull ğ•œ (s \ {x}), { convert convex_convex_hull ğ•œ _ },
  exact subset.antisymm (subset_convex_hull ğ•œ _) (Î» y hy, âŸ¨convex_hull_min (diff_subset _ _) hs hy,
    by { rintro (rfl : y = x), exact hx hy }âŸ©),
end

lemma is_linear_map.convex_hull_image {f : E â†’ F} (hf : is_linear_map ğ•œ f) (s : set E) :
  convex_hull ğ•œ (f '' s) = f '' convex_hull ğ•œ s :=
set.subset.antisymm (convex_hull_min (image_subset _ (subset_convex_hull ğ•œ s)) $
  (convex_convex_hull ğ•œ s).is_linear_image hf)
  (image_subset_iff.2 $ convex_hull_min
    (image_subset_iff.1 $ subset_convex_hull ğ•œ _)
    ((convex_convex_hull ğ•œ _).is_linear_preimage hf))

lemma linear_map.convex_hull_image (f : E â†’â‚—[ğ•œ] F) (s : set E) :
  convex_hull ğ•œ (f '' s) = f '' convex_hull ğ•œ s :=
f.is_linear.convex_hull_image s

end add_comm_monoid
end ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E]

lemma convex_hull_smul (a : ğ•œ) (s : set E) : convex_hull ğ•œ (a â€¢ s) = a â€¢ convex_hull ğ•œ s :=
(linear_map.lsmul _ _ a).convex_hull_image _

end ordered_comm_semiring

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] (s : set E)

lemma affine_map.image_convex_hull (f : E â†’áµƒ[ğ•œ] F) :
  f '' convex_hull ğ•œ s = convex_hull ğ•œ (f '' s) :=
begin
  apply set.subset.antisymm,
  { rw set.image_subset_iff,
    refine convex_hull_min _ ((convex_convex_hull ğ•œ (â‡‘f '' s)).affine_preimage f),
    rw â† set.image_subset_iff,
    exact subset_convex_hull ğ•œ (f '' s) },
  { exact convex_hull_min (set.image_subset _ (subset_convex_hull ğ•œ s))
    ((convex_convex_hull ğ•œ s).affine_image f) }
end

lemma convex_hull_subset_affine_span : convex_hull ğ•œ s âŠ† (affine_span ğ•œ s : set E) :=
convex_hull_min (subset_affine_span ğ•œ s) (affine_span ğ•œ s).convex

@[simp] lemma affine_span_convex_hull : affine_span ğ•œ (convex_hull ğ•œ s) = affine_span ğ•œ s :=
begin
  refine le_antisymm _ (affine_span_mono ğ•œ (subset_convex_hull ğ•œ s)),
  rw affine_span_le,
  exact convex_hull_subset_affine_span s,
end

lemma convex_hull_neg (s : set E) : convex_hull ğ•œ (-s) = -convex_hull ğ•œ s :=
by { simp_rw â†image_neg, exact (affine_map.image_convex_hull _ $ -1).symm }

end add_comm_group
end ordered_ring
end convex_hull
