/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import analysis.convex.caratheodory
import analysis.convex.extreme
import linear_algebra.affine_space.independent

/-!
# Convex independence

This file defines convex independent families of points.

Convex independence is closely related to affine independence. In both cases, no point can be
written as a combination of others. When the combination is affine (that is, any coefficients), this
yields affine independence. When the combination is convex (that is, all coefficients are
nonnegative), then this yields convex independence. In particular, affine independence implies
convex independence.

## Main declarations

* `convex_independent p`: Convex independence of the indexed family `p : Î¹ â†’ E`. Every point of the
  family only belongs to convex hulls of sets of the family containing it.
* `convex_independent_iff_finset`: CarathÃ©odory's theorem allows us to only check finsets to
  conclude convex independence.
* `convex.extreme_points_convex_independent`: Extreme points of a convex set are convex independent.

## References

* https://en.wikipedia.org/wiki/Convex_position

## TODO

Prove `affine_independent.convex_independent`. This requires some glue between `affine_combination`
and `finset.center_mass`.

## Tags

independence, convex position
-/

open_locale affine big_operators classical
open finset function
variables {ğ•œ E Î¹ : Type*}

section ordered_semiring
variables (ğ•œ) [ordered_semiring ğ•œ] [add_comm_group E] [module ğ•œ E] {s t : set E}

/-- An indexed family is said to be convex independent if every point only belongs to convex hulls
of sets containing it. -/
def convex_independent (p : Î¹ â†’ E) : Prop :=
âˆ€ (s : set Î¹) (x : Î¹), p x âˆˆ convex_hull ğ•œ (p '' s) â†’ x âˆˆ s

variables {ğ•œ}

/-- A family with at most one point is convex independent. -/
lemma subsingleton.convex_independent [subsingleton Î¹] (p : Î¹ â†’ E) :
  convex_independent ğ•œ p :=
Î» s x hx, begin
  have : (convex_hull ğ•œ (p '' s)).nonempty := âŸ¨p x, hxâŸ©,
  rw [convex_hull_nonempty_iff, set.nonempty_image_iff] at this,
  rwa subsingleton.mem_iff_nonempty,
end

/-- A convex independent family is injective. -/
protected lemma convex_independent.injective {p : Î¹ â†’ E} (hc : convex_independent ğ•œ p) :
  function.injective p :=
begin
  refine Î» i j hij, hc {j} i _,
  rw [hij, set.image_singleton, convex_hull_singleton],
  exact set.mem_singleton _,
end

/-- If a family is convex independent, so is any subfamily given by composition of an embedding into
index type with the original family. -/
lemma convex_independent.comp_embedding {Î¹' : Type*} (f : Î¹' â†ª Î¹) {p : Î¹ â†’ E}
  (hc : convex_independent ğ•œ p) :
  convex_independent ğ•œ (p âˆ˜ f) :=
begin
  intros s x hx,
  rw â†f.injective.mem_set_image,
  exact hc _ _ (by rwa set.image_image),
end

/-- If a family is convex independent, so is any subfamily indexed by a subtype of the index type.
-/
protected lemma convex_independent.subtype {p : Î¹ â†’ E} (hc : convex_independent ğ•œ p) (s : set Î¹) :
  convex_independent ğ•œ (Î» i : s, p i) :=
hc.comp_embedding (embedding.subtype _)

/-- If an indexed family of points is convex independent, so is the corresponding set of points. -/
protected lemma convex_independent.range {p : Î¹ â†’ E} (hc : convex_independent ğ•œ p) :
  convex_independent ğ•œ (Î» x, x : set.range p â†’ E) :=
begin
  let f : set.range p â†’ Î¹ := Î» x, x.property.some,
  have hf : âˆ€ x, p (f x) = x := Î» x, x.property.some_spec,
  let fe : set.range p â†ª Î¹ := âŸ¨f, Î» xâ‚ xâ‚‚ he, subtype.ext (hf xâ‚ â–¸ hf xâ‚‚ â–¸ he â–¸ rfl)âŸ©,
  convert hc.comp_embedding fe,
  ext,
  rw [embedding.coe_fn_mk, comp_app, hf],
end

/-- A subset of a convex independent set of points is convex independent as well. -/
protected lemma convex_independent.mono {s t : set E} (hc : convex_independent ğ•œ (Î» x, x : t â†’ E))
  (hs : s âŠ† t) :
  convex_independent ğ•œ (Î» x, x : s â†’ E) :=
hc.comp_embedding (s.embedding_of_subset t hs)

/-- The range of an injective indexed family of points is convex independent iff that family is. -/
lemma function.injective.convex_independent_iff_set {p : Î¹ â†’ E}
  (hi : function.injective p) :
  convex_independent ğ•œ (Î» x, x : set.range p â†’ E) â†” convex_independent ğ•œ p :=
âŸ¨Î» hc, hc.comp_embedding
  (âŸ¨Î» i, âŸ¨p i, set.mem_range_self _âŸ©, Î» x y h, hi (subtype.mk_eq_mk.1 h)âŸ© : Î¹ â†ª set.range p),
  convex_independent.rangeâŸ©

/-- If a family is convex independent, a point in the family is in the convex hull of some of the
points given by a subset of the index type if and only if the point's index is in this subset. -/
@[simp] protected lemma convex_independent.mem_convex_hull_iff {p : Î¹ â†’ E}
    (hc : convex_independent ğ•œ p) (s : set Î¹) (i : Î¹) :
  p i âˆˆ convex_hull ğ•œ (p '' s) â†” i âˆˆ s :=
âŸ¨hc _ _, Î» hi, subset_convex_hull ğ•œ _ (set.mem_image_of_mem p hi)âŸ©

/-- If a family is convex independent, a point in the family is not in the convex hull of the other
points. See `convex_independent_set_iff_not_mem_convex_hull_diff` for the `set` version.  -/
lemma convex_independent_iff_not_mem_convex_hull_diff {p : Î¹ â†’ E} :
  convex_independent ğ•œ p â†” âˆ€ i s, p i âˆ‰ convex_hull ğ•œ (p '' (s \ {i})) :=
begin
  refine âŸ¨Î» hc i s h, _, Î» h s i hi, _âŸ©,
  { rw hc.mem_convex_hull_iff at h,
    exact h.2 (set.mem_singleton _) },
  { by_contra H,
    refine h i s _,
    rw set.diff_singleton_eq_self H,
    exact hi }
end

lemma convex_independent_set_iff_inter_convex_hull_subset {s : set E} :
  convex_independent ğ•œ (Î» x, x : s â†’ E) â†” âˆ€ t, t âŠ† s â†’ s âˆ© convex_hull ğ•œ t âŠ† t :=
begin
  split,
  { rintro hc t h x âŸ¨hxs, hxtâŸ©,
    refine hc {x | â†‘x âˆˆ t} âŸ¨x, hxsâŸ© _,
    rw subtype.coe_image_of_subset h,
    exact hxt },
  { intros hc t x h,
    rw â†subtype.coe_injective.mem_set_image,
    exact hc (t.image coe) (subtype.coe_image_subset s t) âŸ¨x.prop, hâŸ© }
end

/-- If a set is convex independent, a point in the set is not in the convex hull of the other
points. See `convex_independent_iff_not_mem_convex_hull_diff` for the indexed family version.  -/
lemma convex_independent_set_iff_not_mem_convex_hull_diff {s : set E} :
  convex_independent ğ•œ (Î» x, x : s â†’ E) â†” âˆ€ x âˆˆ s, x âˆ‰ convex_hull ğ•œ (s \ {x}) :=
begin
  rw convex_independent_set_iff_inter_convex_hull_subset,
  split,
  { rintro hs x hxs hx,
    exact (hs _ (set.diff_subset _ _) âŸ¨hxs, hxâŸ©).2 (set.mem_singleton _) },
  { rintro hs t ht x âŸ¨hxs, hxtâŸ©,
    by_contra h,
    exact hs _ hxs (convex_hull_mono (set.subset_diff_singleton ht h) hxt) }
end

end ordered_semiring

section linear_ordered_field
variables [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E] {s : set E}

/-- To check convex independence, one only has to check finsets thanks to CarathÃ©odory's theorem. -/
lemma convex_independent_iff_finset {p : Î¹ â†’ E} :
  convex_independent ğ•œ p
  â†” âˆ€ (s : finset Î¹) (x : Î¹), p x âˆˆ convex_hull ğ•œ (s.image p : set E) â†’ x âˆˆ s :=
begin
  refine âŸ¨Î» hc s x hx, hc s x _, Î» h s x hx, _âŸ©,
  { rwa finset.coe_image at hx },
  have hp : injective p,
  { rintro a b hab,
    rw â†mem_singleton,
    refine h {b} a _,
    rw [hab, image_singleton, coe_singleton, convex_hull_singleton],
    exact set.mem_singleton _ },
  let s' : finset Î¹ :=
    (caratheodory.min_card_finset_of_mem_convex_hull hx).preimage p (hp.inj_on _),
  suffices hs' : x âˆˆ s',
  { rw â†hp.mem_set_image,
    rw [finset.mem_preimage, â†mem_coe] at hs',
    exact caratheodory.min_card_finset_of_mem_convex_hull_subseteq hx hs' },
  refine h s' x _,
  have : s'.image p = caratheodory.min_card_finset_of_mem_convex_hull hx,
  { rw [image_preimage, filter_true_of_mem],
    exact Î» y hy, set.image_subset_range _ s
      (caratheodory.min_card_finset_of_mem_convex_hull_subseteq hx $ mem_coe.2 hy) },
  rw this,
  exact caratheodory.mem_min_card_finset_of_mem_convex_hull hx,
end

/-! ### Extreme points -/

lemma convex.convex_independent_extreme_points (hs : convex ğ•œ s) :
  convex_independent ğ•œ (Î» p, p : s.extreme_points ğ•œ â†’ E) :=
convex_independent_set_iff_not_mem_convex_hull_diff.2 $ Î» x hx h,
  (extreme_points_convex_hull_subset
  (inter_extreme_points_subset_extreme_points_of_subset (convex_hull_min
  ((set.diff_subset _ _).trans extreme_points_subset) hs) âŸ¨h, hxâŸ©)).2 (set.mem_singleton _)

end linear_ordered_field
