/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.convex.function
import measure_theory.integral.set_integral

/-!
# Jensen's inequality for integrals

In this file we prove several versions of Jensen's inequality. Here we list key differences between
these lemmas and explain how they affect names of the lemmas.

- We prove inequalities for convex functions (in the namespaces `convex_on` and `strict_convex_on`):
  `g ((Î¼ univ)â»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼) â‰¤ (Î¼ univ)â»Â¹ â€¢ âˆ« x, g (f x) âˆ‚Î¼`, and for convex sets (int the
  namespace `convex`): if `âˆ€áµ x âˆ‚Î¼, f x âˆˆ s`, then `(Î¼ univ)â»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s`.

- We prove inequalities for average values over the whole space w.r.t. to a finite measure
  (`...smul_integral...`), to a probability measure (`...integral...`), or over a set
  (`...smul_set_integral...`).

- We prove strict inequality (has `lt` in the name, all versions but one are in the
  `strict_convex_on` namespace) and non-strict inequalities.

## Tags

convex, integral, center mass, Jensen's inequality
-/

open measure_theory metric set filter topological_space
open_locale topological_space big_operators ennreal

variables {Î± E : Type*} [measurable_space Î±] {Î¼ : measure Î±}
  [normed_group E] [normed_space â„ E] [complete_space E]
  [topological_space.second_countable_topology E] [measurable_space E] [borel_space E]

/-!
### Non-strict Jensen's inequality
-/

/-- An auxiliary lemma for `convex.smul_integral_mem`. -/
protected lemma convex.smul_integral_mem_of_measurable
  [is_finite_measure Î¼] {s : set E} (hs : convex â„ s) (hsc : is_closed s)
  (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hfm : measurable f) :
  (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s :=
begin
  unfreezingI { rcases eq_empty_or_nonempty s with rfl|âŸ¨yâ‚€, hâ‚€âŸ© },
  { refine (hÎ¼ _).elim, simpa using hfs },
  rw â† hsc.closure_eq at hfs,
  have hc : integrable (Î» _, yâ‚€) Î¼ := integrable_const _,
  set F : â„• â†’ simple_func Î± E := simple_func.approx_on f hfm s yâ‚€ hâ‚€,
  have : tendsto (Î» n, (F n).integral Î¼) at_top (ğ“ $ âˆ« x, f x âˆ‚Î¼),
  { simp only [simple_func.integral_eq_integral _
      (simple_func.integrable_approx_on hfm hfi hâ‚€ hc _)],
    exact tendsto_integral_of_L1 _ hfi
      (eventually_of_forall $ simple_func.integrable_approx_on hfm hfi hâ‚€ hc)
      (simple_func.tendsto_approx_on_L1_nnnorm hfm hâ‚€ hfs (hfi.sub hc).2) },
  refine hsc.mem_of_tendsto (tendsto_const_nhds.smul this) (eventually_of_forall $ Î» n, _),
  have : âˆ‘ y in (F n).range, (Î¼ ((F n) â»Â¹' {y})).to_real = (Î¼ univ).to_real,
    by rw [â† (F n).sum_range_measure_preimage_singleton, @ennreal.to_real_sum _ _
      (Î» y, Î¼ ((F n) â»Â¹' {y})) (Î» _ _, (measure_ne_top _ _))],
  rw [â† this, simple_func.integral],
  refine hs.center_mass_mem (Î» _ _, ennreal.to_real_nonneg) _ _,
  { rw this,
    exact ennreal.to_real_pos (mt measure.measure_univ_eq_zero.mp hÎ¼) (measure_ne_top _ _) },
  { simp only [simple_func.mem_range],
    rintros _ âŸ¨x, rflâŸ©,
    exact simple_func.approx_on_mem hfm hâ‚€ n x }
end

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`(Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.center_mass_mem` for a finite sum version
of this lemma. -/
lemma convex.smul_integral_mem
  [is_finite_measure Î¼] {s : set E} (hs : convex â„ s) (hsc : is_closed s)
  (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) :
  (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s :=
begin
  have : âˆ€áµ (x : Î±) âˆ‚Î¼, hfi.ae_measurable.mk f x âˆˆ s,
  { filter_upwards [hfs, hfi.ae_measurable.ae_eq_mk],
    assume a ha h,
    rwa â† h },
  convert convex.smul_integral_mem_of_measurable hs hsc hÎ¼ this
    (hfi.congr hfi.ae_measurable.ae_eq_mk) (hfi.ae_measurable.measurable_mk) using 2,
  apply integral_congr_ae,
  exact hfi.ae_measurable.ae_eq_mk
end

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`(Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.center_mass_mem` for a finite sum version
of this lemma. -/
lemma convex.smul_set_integral_mem {t : set Î±} {s : set E} (hs : convex â„ s) (hsc : is_closed s)
  (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s)
  (hfi : integrable_on f t Î¼) :
  (Î¼ t).to_realâ»Â¹ â€¢ âˆ« x in t, f x âˆ‚Î¼ âˆˆ s :=
begin
  haveI : fact (Î¼ t < âˆ) := âŸ¨ht.lt_topâŸ©,
  rw â† measure.restrict_apply_univ t at h0 âŠ¢,
  refine hs.smul_integral_mem hsc _ hfs hfi,
  rwa [ne.def, â† measure.measure_univ_eq_zero]
end

/-- If `Î¼` is a probability measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the expected value of `f` belongs to `s`:
`âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.sum_mem` for a finite sum version of this lemma. -/
lemma convex.integral_mem [is_probability_measure Î¼] {s : set E} (hs : convex â„ s)
  (hsc : is_closed s) {f : Î± â†’ E} (hf : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ âˆˆ s :=
by simpa [measure_univ] using hs.smul_integral_mem hsc (is_probability_measure.ne_zero Î¼) hf hfi

/-- Jensen's inequality: if a function `g : E â†’ â„` is convex and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
to `s`, then the value of `g` at the average value of `f` is less than or equal to the average value
of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also `convex.map_center_mass_le`
for a finite sum version of this lemma. -/
lemma convex_on.map_smul_integral_le [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  g ((Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼) â‰¤ (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, g (f x) âˆ‚Î¼ :=
begin
  set t := {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2},
  have ht_conv : convex â„ t := hg.convex_epigraph,
  have ht_closed : is_closed t :=
    (hsc.preimage continuous_fst).is_closed_le (hgc.comp continuous_on_fst (subset.refl _))
      continuous_on_snd,
  have ht_mem : âˆ€áµ x âˆ‚Î¼, (f x, g (f x)) âˆˆ t := hfs.mono (Î» x hx, âŸ¨hx, le_rflâŸ©),
  simpa [integral_pair hfi hgi]
    using (ht_conv.smul_integral_mem ht_closed hÎ¼ ht_mem (hfi.prod_mk hgi)).2
end

/-- Jensen's inequality: if a function `g : E â†’ â„` is convex and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
of a set `t` to `s`, then the value of `g` at the average value of `f` over `t` is less than or
equal to the average value of `g âˆ˜ f` over `t` provided that both `f` and `g âˆ˜ f` are
integrable. -/
lemma convex_on.map_smul_set_integral_le [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) {t : set Î±} (h0 : Î¼ t â‰  0)
  (ht : Î¼ t â‰  âˆ) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s) (hfi : integrable_on f t Î¼)
  (hgi : integrable_on (g âˆ˜ f) t Î¼) :
  g ((Î¼ t).to_realâ»Â¹ â€¢ âˆ« x in t, f x âˆ‚Î¼) â‰¤ (Î¼ t).to_realâ»Â¹ â€¢ âˆ« x in t, g (f x) âˆ‚Î¼ :=
begin
  haveI : fact (Î¼ t < âˆ) := âŸ¨ht.lt_topâŸ©,
  rw â† measure.restrict_apply_univ t at h0 âŠ¢,
  refine hg.map_smul_integral_le hgc hsc _ hfs hfi hgi,
  rwa [ne.def, â† measure.measure_univ_eq_zero]
end

/-- Convex **Jensen's inequality**: if a function `g : E â†’ â„` is convex and continuous on a convex
closed set `s`, `Î¼` is a probability measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e.
points to `s`, then the value of `g` at the expected value of `f` is less than or equal to the
expected value of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also
`convex_on.map_center_mass_le` for a finite sum version of this lemma. -/
lemma convex_on.map_integral_le [is_probability_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  g (âˆ« x, f x âˆ‚Î¼) â‰¤ âˆ« x, g (f x) âˆ‚Î¼ :=
by simpa [measure_univ]
  using hg.map_smul_integral_le hgc hsc (is_probability_measure.ne_zero Î¼) hfs hfi hgi

/-- Strict **Jensen's inequality**. Suppose that a function `g : E â†’ â„` is convex and continuous on
a convex closed set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function
sending `Î¼`-a.e. points to `s`. Also assume that for some set `t` of nonzero measure, the value of
`g` at the average value of `f` over `t` is strictly less than the average value of `g âˆ˜ f` over
`t`. Then the value of `g` at the average value of `f` over the whole space is strictly less than
the average value of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. -/
lemma convex_on.map_smul_integral_lt_of_exists_set [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼)
  (H : âˆƒ t, Î¼ t â‰  0 âˆ§
    g ((Î¼ t).to_realâ»Â¹ â€¢ âˆ« x in t, f x âˆ‚Î¼) < (Î¼ t).to_realâ»Â¹ * âˆ« x in t, g (f x) âˆ‚Î¼) :
  g ((Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼) < (Î¼ univ).to_realâ»Â¹ * âˆ« x, g (f x) âˆ‚Î¼ :=
begin
  obtain âŸ¨t, htm, htâ‚€, ht_ltâŸ© : âˆƒ t, measurable_set t âˆ§ Î¼ t â‰  0 âˆ§
    g ((Î¼ t).to_realâ»Â¹ â€¢ âˆ« x in t, f x âˆ‚Î¼) < (Î¼ t).to_realâ»Â¹ â€¢ âˆ« x in t, g (f x) âˆ‚Î¼,
  { rcases H with âŸ¨t, htâŸ©,
    refine âŸ¨to_measurable Î¼ t, measurable_set_to_measurable _ _, _âŸ©,
    rwa [measure_to_measurable, measure.restrict_to_measurable (measure_ne_top Î¼ t)] },
  clear H,
  set Î½ : set Î± â†’ â„ := Î» u, (Î¼ u).to_real,
  set I : set Î± â†’ E := Î» u, (Î½ u)â»Â¹ â€¢ âˆ« x in u, f x âˆ‚Î¼,
  set J : set Î± â†’ â„ := Î» u, (Î½ u)â»Â¹ * âˆ« x in u, g (f x) âˆ‚Î¼,
  have hÎ½â‚€ : 0 < Î½ t, from ennreal.to_real_pos htâ‚€ (measure_ne_top _ _),
  cases (@ennreal.to_real_nonneg (Î¼ tá¶œ)).eq_or_lt with hÎ½â‚€' hÎ½â‚€',
  { have A : t =áµ[Î¼] univ,
      by simpa only [eventually_eq_univ, mem_ae_iff, ennreal.to_real_eq_zero_iff,
        measure_ne_top, or_false] using hÎ½â‚€'.symm,
    simpa only [measure_congr A, set_integral_congr_set_ae A, integral_univ] using ht_lt },
  have htâ‚€' : Î¼ tá¶œ â‰  0, from Î» H, hÎ½â‚€'.ne' ((ennreal.to_real_eq_zero_iff _).2 $ or.inl H),
  have hÎ½t_add : Î½ t + Î½ tá¶œ = Î½ univ,
    by rw [â† ennreal.to_real_add (measure_ne_top Î¼ _) (measure_ne_top Î¼ _),
      measure_add_measure_compl htm],
  have hÎ½â‚€_add : 0 < Î½ t + Î½ tá¶œ, from add_pos hÎ½â‚€ hÎ½â‚€',
  calc g ((Î½ univ)â»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼)
      = g ((Î½ t / (Î½ t + Î½ tá¶œ)) â€¢ I t + (Î½ tá¶œ / (Î½ t + Î½ tá¶œ)) â€¢ I tá¶œ) :
    by rw [smul_smul, smul_smul, hÎ½t_add, â† mul_div_right_comm, mul_inv_cancel hÎ½â‚€.ne',
      â† mul_div_right_comm, mul_inv_cancel hÎ½â‚€'.ne', one_div, â† smul_add,
      integral_add_compl htm hfi]
  ... â‰¤ (Î½ t / (Î½ t + Î½ tá¶œ)) * g (I t) + (Î½ tá¶œ / (Î½ t + Î½ tá¶œ)) * g (I tá¶œ) :
    (convex_on_iff_div.1 hg).2
      (hg.1.smul_set_integral_mem hsc htâ‚€ (measure_ne_top _ _) (ae_restrict_of_ae hfs)
        hfi.integrable_on)
      (hg.1.smul_set_integral_mem hsc htâ‚€' (measure_ne_top _ _) (ae_restrict_of_ae hfs)
        hfi.integrable_on) ennreal.to_real_nonneg ennreal.to_real_nonneg hÎ½â‚€_add
  ... < (Î½ t / (Î½ t + Î½ tá¶œ)) * J t + (Î½ tá¶œ / (Î½ t + Î½ tá¶œ)) * J tá¶œ :
    add_lt_add_of_lt_of_le ((mul_lt_mul_left $ div_pos hÎ½â‚€ hÎ½â‚€_add).2 ht_lt) $
      flip mul_le_mul_of_nonneg_left (div_pos hÎ½â‚€' hÎ½â‚€_add).le $
        hg.map_smul_set_integral_le hgc hsc htâ‚€' (measure_ne_top _ _) (ae_restrict_of_ae hfs)
          hfi.integrable_on hgi.integrable_on
  ... = (Î¼ univ).to_realâ»Â¹ * âˆ« x, g (f x) âˆ‚Î¼ :
    by rw [â† mul_assoc, â† mul_assoc, hÎ½t_add, â† mul_div_right_comm, mul_inv_cancel hÎ½â‚€.ne', one_div,
      â† mul_div_right_comm, mul_inv_cancel hÎ½â‚€'.ne', one_div, â† mul_add,
      integral_add_compl htm hgi]
end

/-- Strict **Jensen's inequality**. Suppose that a function `g : E â†’ â„` is strictly convex and
continuous on a convex closed set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a
function sending `Î¼`-a.e. points to `s`. Also assume that both `f` and `g âˆ˜ f` are integrable. Then
either `f` is a.e. constant, or the value of `g` at the average value of `f` over the whole space is
strictly less than the average value of `g âˆ˜ f`. -/
lemma strict_convex_on.ae_eq_const_or_map_smul_integral_lt [is_finite_measure Î¼] {s : set E}
  {g : E â†’ â„} (hg : strict_convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s)
  {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼)
  (hgi : integrable (g âˆ˜ f) Î¼) :
  (âˆƒ c : E, f =áµ[Î¼] function.const Î± c) âˆ¨
    g ((Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼) < (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, g (f x) âˆ‚Î¼ :=
begin
  cases eq_or_ne Î¼ 0 with hÎ¼ hÎ¼, { refine or.inl âŸ¨0, _âŸ©, simp [hÎ¼] },
  refine or_iff_not_imp_left.mpr (Î» H, _),
  apply hg.convex_on.map_smul_integral_lt_of_exists_set hgc hsc hÎ¼ hfs hfi hgi,
  simp only [not_exists, eventually_eq, not_eventually] at H,
  rcases exists_ne_forall_mem_nhds_pos_measure_preimage H
    with âŸ¨a, b, hne, ha : âˆ€ s âˆˆ ğ“ a, 0 < Î¼ (f â»Â¹' s), hb : âˆ€ s âˆˆ ğ“ b, 0 < Î¼ (f â»Â¹' s)âŸ©,
  obtain âŸ¨r, hrâ‚€, hdâŸ© : âˆƒ r : â„, 0 < r âˆ§ disjoint (closed_ball a r) (closed_ball b r),
  { rcases exists_pos_mul_lt (dist_pos.2 hne) 2 with âŸ¨r, hrâ‚€, hrâŸ©,
    exact âŸ¨r, hrâ‚€, closed_ball_disjoint_closed_ball $ two_mul r â–¸ hrâŸ© },
  set Ba := f â»Â¹' closed_ball a r, set Bb := f â»Â¹' closed_ball b r,
  have hBa : 0 < Î¼ Ba, from ha _ (closed_ball_mem_nhds _ hrâ‚€),
  have hBb : 0 < Î¼ Bb, from hb _ (closed_ball_mem_nhds _ hrâ‚€),
  have hBa' : 0 < (Î¼ Ba).to_real, from ennreal.to_real_pos hBa.ne' (measure_ne_top _ _),
  have hBb' : 0 < (Î¼ Bb).to_real, from ennreal.to_real_pos hBb.ne' (measure_ne_top _ _),
  have hBab : 0 < Î¼ (Ba âˆª Bb), from hBa.trans_le (measure_mono $ subset_union_left _ _),
  refine âŸ¨Ba âˆª Bb, hBab.ne', _âŸ©,
  have hBd : ae_disjoint Î¼ Ba Bb, from (hd.preimage f).ae_disjoint,
  have hBam : null_measurable_set Ba Î¼, from hfi.1.null_measurable measurable_set_closed_ball,
  have hBbm : null_measurable_set Bb Î¼, from hfi.1.null_measurable measurable_set_closed_ball,
  obtain âŸ¨has, harâŸ©  : (Î¼ Ba).to_realâ»Â¹ â€¢ âˆ« x in Ba, f x âˆ‚Î¼ âˆˆ s âˆ© closed_ball a r,
    from (hg.1.inter (convex_closed_ball _ _)).smul_set_integral_mem (hsc.inter is_closed_ball)
      hBa.ne' (measure_ne_top _ _) ((ae_restrict_of_ae hfs).and (ae_restrict_memâ‚€ hBam))
      hfi.integrable_on,
  obtain âŸ¨hbs, hbrâŸ© : (Î¼ Bb).to_realâ»Â¹ â€¢ âˆ« x in Bb, f x âˆ‚Î¼ âˆˆ s âˆ© closed_ball b r,
    from (hg.1.inter (convex_closed_ball _ _)).smul_set_integral_mem (hsc.inter is_closed_ball)
      hBb.ne' (measure_ne_top _ _) ((ae_restrict_of_ae hfs).and (ae_restrict_memâ‚€ hBbm))
      hfi.integrable_on,
  rw [measure_unionâ‚€ hBbm hBd, ennreal.to_real_add (measure_ne_top Î¼ _) (measure_ne_top Î¼ _),
      integral_union_ae hBd hBbm hfi.integrable_on hfi.integrable_on, smul_add,
      integral_union_ae hBd hBbm hgi.integrable_on hgi.integrable_on, mul_add],
  set ca := (Î¼ Ba).to_real, set cb := (Î¼ Bb).to_real,
  have hca : (ca / (ca + cb)) * caâ»Â¹ = (ca + cb)â»Â¹,
    by rw [â† mul_div_right_comm, mul_inv_cancel hBa'.ne', one_div],
  have hcb : (cb / (ca + cb)) * cbâ»Â¹ = (ca + cb)â»Â¹,
    by rw [â† mul_div_right_comm, mul_inv_cancel hBb'.ne', one_div],
  calc g ((ca + cb)â»Â¹ â€¢ âˆ« x in Ba, f x âˆ‚Î¼ + (ca + cb)â»Â¹ â€¢ âˆ« x in Bb, f x âˆ‚Î¼)
      = g ((ca / (ca + cb)) â€¢ caâ»Â¹ â€¢ âˆ« x in Ba, f x âˆ‚Î¼ +
          (cb / (ca + cb)) â€¢ cbâ»Â¹ â€¢ âˆ« x in Bb, f x âˆ‚Î¼) :
    by rw [smul_smul, smul_smul, hca, hcb]
  ... < (ca / (ca + cb)) * g (caâ»Â¹ â€¢ âˆ« x in Ba, f x âˆ‚Î¼) +
          (cb / (ca + cb)) * g (cbâ»Â¹ â€¢ âˆ« x in Bb, f x âˆ‚Î¼) :
    (strict_convex_on_iff_div.1 hg).2 has hbs (hd.ne_of_mem har hbr) hBa' hBb'
  ... â‰¤ (ca / (ca + cb)) * (caâ»Â¹ * âˆ« x in Ba, g (f x) âˆ‚Î¼) +
          (cb / (ca + cb)) * (cbâ»Â¹ * âˆ« x in Bb, g (f x) âˆ‚Î¼) :
    add_le_add
      (mul_le_mul_of_nonneg_left (hg.convex_on.map_smul_set_integral_le hgc hsc hBa.ne'
        (measure_ne_top _ _) (ae_restrict_of_ae hfs) hfi.integrable_on hgi.integrable_on)
        (div_pos hBa' (add_pos hBa' hBb')).le)
      (mul_le_mul_of_nonneg_left (hg.convex_on.map_smul_set_integral_le hgc hsc hBb.ne'
        (measure_ne_top _ _) (ae_restrict_of_ae hfs) hfi.integrable_on hgi.integrable_on)
        (div_pos hBb' (add_pos hBa' hBb')).le)
  ... = (ca + cb)â»Â¹ * âˆ« x in Ba, g (f x) âˆ‚Î¼ + (ca + cb)â»Â¹ * âˆ« x in Bb, g (f x) âˆ‚Î¼ :
    by simp only [â† mul_assoc, hca, hcb]
end

/-- If the norm of a function `f : Î± â†’ E` taking values in a strictly convex normed space is
a.e. less than or equal to `C`, then either this function is a constant, or the norm of its integral
is strictly less than `Î¼ univ * C`. -/
lemma ae_eq_const_or_norm_integral_lt_of_norm_le_const [is_finite_measure Î¼] {f : Î± â†’ E} {C : â„}
  (h_le : âˆ€áµ x âˆ‚Î¼, âˆ¥f xâˆ¥ â‰¤ C)
  (h_convex : strict_convex_on â„ (closed_ball (0 : E) C) (norm : E â†’ â„)) :
  (âˆƒ c : E, f =áµ[Î¼] function.const Î± c) âˆ¨ âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ < (Î¼ univ).to_real * C :=
begin
  cases le_or_lt C 0 with hC0 hC0,
  { exact or.inl âŸ¨0, h_le.mono $ Î» x hx, norm_le_zero_iff.1 $ hx.trans hC0âŸ© },
  cases eq_or_ne Î¼ 0 with hÎ¼ hÎ¼,
  { rw hÎ¼, exact or.inl âŸ¨0, rflâŸ© },
  by_cases hfi : integrable f Î¼, swap,
  { right,
    simpa [integral_undef hfi, hC0, measure_lt_top, ennreal.to_real_pos_iff, pos_iff_ne_zero]
      using hÎ¼ },
  refine (h_convex.ae_eq_const_or_map_smul_integral_lt continuous_norm.continuous_on is_closed_ball
    _ hfi hfi.norm).imp_right (Î» h, _),
  { rw [norm_smul, normed_field.norm_inv, real.norm_eq_abs, abs_of_nonneg ennreal.to_real_nonneg,
      smul_eq_mul, mul_lt_mul_left] at h,
    { calc âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ < âˆ« x, âˆ¥f xâˆ¥ âˆ‚Î¼ : h
      ... â‰¤ âˆ« x, C âˆ‚Î¼ : integral_mono_ae hfi.norm (integrable_const _) h_le
      ... = _ : integral_const _ },
    { refine inv_pos.2 (ennreal.to_real_pos _ (measure_ne_top _ _)),
      rwa [ne.def, measure.measure_univ_eq_zero] } },
  { simpa }
end
