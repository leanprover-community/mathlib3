/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.convex.basic
import measure_theory.integral.set_integral

/-!
# Jensen's inequality for integrals

In this file we prove four theorems:

* `convex.smul_integral_mem`: if `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set
  in `E`, and `f` is an integrable function sending `Î¼`-a.e. points to `s`, then the average value
  of `f` belongs to `s`: `(Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.center_mass_mem`
  for a finite sum version of this lemma.

* `convex.integral_mem`: if `Î¼` is a probability measure on `Î±`, `s` is a convex closed set in `E`,
  and `f` is an integrable function sending `Î¼`-a.e. points to `s`, then the expected value of `f`
  belongs to `s`: `âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.sum_mem` for a finite sum version of this
  lemma.

* `convex_on.map_smul_integral_le`: Jensen's inequality: if a function `g : E â†’ â„` is convex and
  continuous on a convex closed set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is
  a function sending `Î¼`-a.e. points to `s`, then the value of `g` at the average value of `f` is
  less than or equal to the average value of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are
  integrable. See also `convex.map_center_mass_le` for a finite sum version of this lemma.

* `convex_on.map_integral_le`: Jensen's inequality: if a function `g : E â†’ â„` is convex and
  continuous on a convex closed set `s`, `Î¼` is a probability measure on `Î±`, and `f : Î± â†’ E` is a
  function sending `Î¼`-a.e. points to `s`, then the value of `g` at the expected value of `f` is
  less than or equal to the expected value of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are
  integrable. See also `convex.map_sum_le` for a finite sum version of this lemma.

## Tags

convex, integral, center mass, Jensen's inequality
-/

open measure_theory set filter
open_locale topological_space big_operators

variables {Î± E : Type*} [measurable_space Î±] {Î¼ : measure Î±}
  [normed_group E] [normed_space â„ E] [complete_space E]
  [topological_space.second_countable_topology E] [measurable_space E] [borel_space E]

private lemma convex.smul_integral_mem_of_measurable
  [finite_measure Î¼] {s : set E} (hs : convex s) (hsc : is_closed s)
  (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hfm : measurable f) :
  (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s :=
begin
  unfreezingI { rcases eq_empty_or_nonempty s with rfl|âŸ¨yâ‚€, hâ‚€âŸ© },
  { refine (hÎ¼ _).elim, simpa using hfs },
  rw â† hsc.closure_eq at hfs,
  have hc : integrable (Î» _, yâ‚€) Î¼ := integrable_const _,
  set F : â„• â†’ simple_func Î± E := simple_func.approx_on f hfm s yâ‚€ hâ‚€,
  have : tendsto (Î» n, (F n).integral Î¼) at_top (ğ“ $ âˆ« x, f x âˆ‚Î¼),
  { simp only [simple_func.integral_eq_integral _
      (simple_func.integrable_approx_on hfm hfi hâ‚€ hc _)],
    exact tendsto_integral_of_L1 _ hfi
      (eventually_of_forall $ simple_func.integrable_approx_on hfm hfi hâ‚€ hc)
      (simple_func.tendsto_approx_on_L1_nnnorm hfm hâ‚€ hfs (hfi.sub hc).2) },
  refine hsc.mem_of_tendsto (tendsto_const_nhds.smul this) (eventually_of_forall $ Î» n, _),
  have : âˆ‘ y in (F n).range, (Î¼ ((F n) â»Â¹' {y})).to_real = (Î¼ univ).to_real,
    by rw [â† (F n).sum_range_measure_preimage_singleton, @ennreal.to_real_sum _ _
      (Î» y, Î¼ ((F n) â»Â¹' {y})) (Î» _ _, (measure_lt_top _ _))],
  rw [â† this, simple_func.integral],
  refine hs.center_mass_mem (Î» _ _, ennreal.to_real_nonneg) _ _,
  { rw [this, ennreal.to_real_pos_iff, pos_iff_ne_zero, ne.def, measure.measure_univ_eq_zero],
    exact âŸ¨hÎ¼, measure_ne_top _ _âŸ© },
  { simp only [simple_func.mem_range],
    rintros _ âŸ¨x, rflâŸ©,
    exact simple_func.approx_on_mem hfm hâ‚€ n x }
end

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`(Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.center_mass_mem` for a finite sum version
of this lemma. -/
lemma convex.smul_integral_mem
  [finite_measure Î¼] {s : set E} (hs : convex s) (hsc : is_closed s)
  (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) :
  (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ âˆˆ s :=
begin
  have : âˆ€áµ (x : Î±) âˆ‚Î¼, hfi.ae_measurable.mk f x âˆˆ s,
  { filter_upwards [hfs, hfi.ae_measurable.ae_eq_mk],
    assume a ha h,
    rwa â† h },
  convert convex.smul_integral_mem_of_measurable hs hsc hÎ¼ this
    (hfi.congr hfi.ae_measurable.ae_eq_mk) (hfi.ae_measurable.measurable_mk) using 2,
  apply integral_congr_ae,
  exact hfi.ae_measurable.ae_eq_mk
end

/-- If `Î¼` is a probability measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the expected value of `f` belongs to `s`:
`âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.sum_mem` for a finite sum version of this lemma. -/
lemma convex.integral_mem [probability_measure Î¼] {s : set E} (hs : convex s) (hsc : is_closed s)
  {f : Î± â†’ E} (hf : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ âˆˆ s :=
by simpa [measure_univ] using hs.smul_integral_mem hsc (probability_measure.ne_zero Î¼) hf hfi

/-- Jensen's inequality: if a function `g : E â†’ â„` is convex and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
to `s`, then the value of `g` at the average value of `f` is less than or equal to the average value
of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also `convex.map_center_mass_le`
for a finite sum version of this lemma. -/
lemma convex_on.map_smul_integral_le [finite_measure Î¼] {s : set E} {g : E â†’ â„} (hg : convex_on s g)
  (hgc : continuous_on g s) (hsc : is_closed s) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s)
  (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  g ((Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼) â‰¤ (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, g (f x) âˆ‚Î¼ :=
begin
  set t := {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2},
  have ht_conv : convex t := hg.convex_epigraph,
  have ht_closed : is_closed t :=
    (hsc.preimage continuous_fst).is_closed_le (hgc.comp continuous_on_fst (subset.refl _))
      continuous_on_snd,
  have ht_mem : âˆ€áµ x âˆ‚Î¼, (f x, g (f x)) âˆˆ t := hfs.mono (Î» x hx, âŸ¨hx, le_rflâŸ©),
  simpa [integral_pair hfi hgi]
    using (ht_conv.smul_integral_mem ht_closed hÎ¼ ht_mem (hfi.prod_mk hgi)).2
end

/-- Jensen's inequality: if a function `g : E â†’ â„` is convex and continuous on a convex closed set
`s`, `Î¼` is a probability measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points to
`s`, then the value of `g` at the expected value of `f` is less than or equal to the expected value
of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also `convex.map_sum_le` for a
finite sum version of this lemma. -/
lemma convex_on.map_integral_le [probability_measure Î¼] {s : set E} {g : E â†’ â„} (hg : convex_on s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s)
  (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  g (âˆ« x, f x âˆ‚Î¼) â‰¤ âˆ« x, g (f x) âˆ‚Î¼ :=
by simpa [measure_univ]
  using hg.map_smul_integral_le hgc hsc (probability_measure.ne_zero Î¼) hfs hfi hgi
