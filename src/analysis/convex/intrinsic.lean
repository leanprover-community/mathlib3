/-
Copyright (c) 2023 Paul Reichert. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Reichert, YaÃ«l Dillies
-/
import analysis.normed_space.add_torsor_bases
import analysis.normed_space.linear_isometry

/-!
# Intrinsic frontier and interior

This file defines the intrinsic frontier, interior and closure of a set in a normed additive torsor.
These are also known as relative frontier, interior, closure.

The intrinsic frontier/interior/closure of a set `s` is the frontier/interior/closure of `s`
considered as a set in its affine span.

The intrinsic interior is in general greater than the topological interior, the intrinsic frontier
in general less than the topological frontier, and the intrinsic closure in cases of interest the
same as the topological closure.

## Definitions

* `intrinsic_interior`: Intrinsic interior
* `intrinsic_frontier`: Intrinsic frontier
* `intrinsic_closure`: Intrinsic closure

## Results

The main results are:
* `affine_isometry.image_intrinsic_interior`/`affine_isometry.image_intrinsic_frontier`/
  `affine_isometry.image_intrinsic_closure`: Intrinsic interiors/frontiers/closures commute with
  taking the image under an affine isometry.
* `set.nonempty.intrinsic_interior`: The intrinsic interior of a nonempty convex set is nonempty.

## References

* Chapter 8 of [Barry Simon, *Convexity*][simon2011]
* Chapter 1 of [Rolf Schneider, *Convex Bodies: The Brunn-Minkowski theory*][schneider2013].

## TODO

* `is_closed s â†’ is_extreme ğ•œ s (intrinsic_frontier ğ•œ s)`
* `x âˆˆ s â†’ y âˆˆ intrinsic_interior ğ•œ s â†’ open_segment ğ•œ x y âŠ† intrinsic_interior ğ•œ s`
-/

open affine_subspace set
open_locale pointwise

variables {ğ•œ V W Q P : Type*}

section add_torsor
variables (ğ•œ) [ring ğ•œ] [add_comm_group V] [module ğ•œ V] [topological_space P] [add_torsor V P]
  {s t : set P} {x : P}
include V

/-- The intrinsic interior of a set is its interior considered as a set in its affine span. -/
def intrinsic_interior (s : set P) : set P := coe '' interior (coe â»Â¹' s : set $ affine_span ğ•œ s)

/-- The intrinsic frontier of a set is its frontier considered as a set in its affine span. -/
def intrinsic_frontier (s : set P) : set P := coe '' frontier (coe â»Â¹' s : set $ affine_span ğ•œ s)

/-- The intrinsic closure of a set is its closure considered as a set in its affine span. -/
def intrinsic_closure (s : set P) : set P := coe '' closure (coe â»Â¹' s : set $ affine_span ğ•œ s)

variables {ğ•œ}

@[simp] lemma mem_intrinsic_interior :
  x âˆˆ intrinsic_interior ğ•œ s â†” âˆƒ y, y âˆˆ interior (coe â»Â¹' s : set $ affine_span ğ•œ s) âˆ§ â†‘y = x :=
mem_image _ _ _

@[simp] lemma mem_intrinsic_frontier :
  x âˆˆ intrinsic_frontier ğ•œ s â†” âˆƒ y, y âˆˆ frontier (coe â»Â¹' s : set $ affine_span ğ•œ s) âˆ§ â†‘y = x :=
mem_image _ _ _

@[simp] lemma mem_intrinsic_closure :
  x âˆˆ intrinsic_closure ğ•œ s â†” âˆƒ y, y âˆˆ closure (coe â»Â¹' s : set $ affine_span ğ•œ s) âˆ§ â†‘y = x :=
mem_image _ _ _

lemma intrinsic_interior_subset : intrinsic_interior ğ•œ s âŠ† s := image_subset_iff.2 interior_subset

lemma intrinsic_frontier_subset (hs : is_closed s) : intrinsic_frontier ğ•œ s âŠ† s :=
image_subset_iff.2 (hs.preimage continuous_induced_dom).frontier_subset

lemma intrinsic_frontier_subset_intrinsic_closure :
  intrinsic_frontier ğ•œ s âŠ† intrinsic_closure ğ•œ s :=
image_subset _ frontier_subset_closure

lemma subset_intrinsic_closure : s âŠ† intrinsic_closure ğ•œ s :=
Î» x hx, âŸ¨âŸ¨x, subset_affine_span _ _ hxâŸ©, subset_closure hx, rflâŸ©

@[simp] lemma intrinsic_interior_empty : intrinsic_interior ğ•œ (âˆ… : set P) = âˆ… :=
by simp [intrinsic_interior]

@[simp] lemma intrinsic_frontier_empty : intrinsic_frontier ğ•œ (âˆ… : set P) = âˆ… :=
by simp [intrinsic_frontier]

@[simp] lemma intrinsic_closure_empty : intrinsic_closure ğ•œ (âˆ… : set P) = âˆ… :=
by simp [intrinsic_closure]

@[simp] lemma intrinsic_closure_nonempty : (intrinsic_closure ğ•œ s).nonempty â†” s.nonempty :=
âŸ¨by { simp_rw nonempty_iff_ne_empty, rintro h rfl, exact h intrinsic_closure_empty },
  nonempty.mono subset_intrinsic_closureâŸ©

alias intrinsic_closure_nonempty â†” set.nonempty.of_intrinsic_closure set.nonempty.intrinsic_closure

attribute [protected] set.nonempty.intrinsic_closure

@[simp] lemma intrinsic_interior_singleton (x : P) : intrinsic_interior ğ•œ ({x} : set P) = {x} :=
by simpa only [intrinsic_interior, preimage_coe_affine_span_singleton, interior_univ, image_univ,
  subtype.range_coe] using coe_affine_span_singleton _ _ _

@[simp] lemma intrinsic_frontier_singleton (x : P) : intrinsic_frontier ğ•œ ({x} : set P) = âˆ… :=
by rw [intrinsic_frontier, preimage_coe_affine_span_singleton, frontier_univ, image_empty]

@[simp] lemma intrinsic_closure_singleton (x : P) : intrinsic_closure ğ•œ ({x} : set P) = {x} :=
by simpa only [intrinsic_closure, preimage_coe_affine_span_singleton, closure_univ, image_univ,
  subtype.range_coe] using coe_affine_span_singleton _ _ _

/-!
Note that neither `intrinsic_interior` nor `intrinsic_frontier` is monotone.
-/

lemma intrinsic_closure_mono (h : s âŠ† t) : intrinsic_closure ğ•œ s âŠ† intrinsic_closure ğ•œ t :=
begin
  refine image_subset_iff.2 (Î» x hx, âŸ¨set.inclusion (affine_span_mono _ h) x,
    (continuous_inclusion _).closure_preimage_subset _ $ closure_mono _ hx, rflâŸ©),
  exact Î» y hy, h hy,
end

lemma interior_subset_intrinsic_interior : interior s âŠ† intrinsic_interior ğ•œ s :=
Î» x hx, âŸ¨âŸ¨x, subset_affine_span _ _ $ interior_subset hxâŸ©,
  preimage_interior_subset_interior_preimage continuous_subtype_coe hx, rflâŸ©

lemma intrinsic_closure_subset_closure : intrinsic_closure ğ•œ s âŠ† closure s :=
image_subset_iff.2 $ continuous_subtype_coe.closure_preimage_subset _

lemma intrinsic_frontier_subset_frontier : intrinsic_frontier ğ•œ s âŠ† frontier s :=
image_subset_iff.2 $ continuous_subtype_coe.frontier_preimage_subset _

lemma intrinsic_closure_subset_affine_span : intrinsic_closure ğ•œ s âŠ† affine_span ğ•œ s :=
(image_subset_range _ _).trans subtype.range_coe.subset

@[simp] lemma intrinsic_closure_diff_intrinsic_frontier (s : set P) :
  intrinsic_closure ğ•œ s \ intrinsic_frontier ğ•œ s = intrinsic_interior ğ•œ s :=
(image_diff subtype.coe_injective _ _).symm.trans $
  by rw [closure_diff_frontier, intrinsic_interior]

@[simp] lemma intrinsic_closure_diff_intrinsic_interior (s : set P) :
  intrinsic_closure ğ•œ s \ intrinsic_interior ğ•œ s = intrinsic_frontier ğ•œ s :=
(image_diff subtype.coe_injective _ _).symm

@[simp] lemma intrinsic_interior_union_intrinsic_frontier (s : set P) :
  intrinsic_interior ğ•œ s âˆª intrinsic_frontier ğ•œ s = intrinsic_closure ğ•œ s :=
by simp [intrinsic_closure, intrinsic_interior, intrinsic_frontier,
  closure_eq_interior_union_frontier, image_union]

@[simp] lemma intrinsic_frontier_union_intrinsic_interior (s : set P) :
  intrinsic_frontier ğ•œ s âˆª intrinsic_interior ğ•œ s = intrinsic_closure ğ•œ s :=
by rw [union_comm, intrinsic_interior_union_intrinsic_frontier]

lemma is_closed_intrinsic_closure (hs : is_closed (affine_span ğ•œ s : set P)) :
  is_closed (intrinsic_closure ğ•œ s) :=
(closed_embedding_subtype_coe hs).is_closed_map _ is_closed_closure

lemma is_closed_intrinsic_frontier (hs : is_closed (affine_span ğ•œ s : set P)) :
  is_closed (intrinsic_frontier ğ•œ s) :=
(closed_embedding_subtype_coe hs).is_closed_map _ is_closed_frontier

@[simp] lemma affine_span_intrinsic_closure (s : set P) :
  affine_span ğ•œ (intrinsic_closure ğ•œ s) = affine_span ğ•œ s :=
(affine_span_le.2 intrinsic_closure_subset_affine_span).antisymm $
  affine_span_mono _ subset_intrinsic_closure

protected lemma is_closed.intrinsic_closure (hs : is_closed (coe â»Â¹' s : set $ affine_span ğ•œ s)) :
  intrinsic_closure ğ•œ s = s :=
begin
  rw [intrinsic_closure, hs.closure_eq, image_preimage_eq_of_subset],
  exact (subset_affine_span _ _).trans subtype.range_coe.superset,
end

@[simp] lemma intrinsic_closure_idem (s : set P) :
  intrinsic_closure ğ•œ (intrinsic_closure ğ•œ s) = intrinsic_closure ğ•œ s :=
begin
  refine is_closed.intrinsic_closure _,
  set t := affine_span ğ•œ (intrinsic_closure ğ•œ s) with ht,
  clear_value t,
  obtain rfl := ht.trans (affine_span_intrinsic_closure _),
  rw [intrinsic_closure, preimage_image_eq _ subtype.coe_injective],
  exact is_closed_closure,
end

end add_torsor

namespace affine_isometry
variables [normed_field ğ•œ] [seminormed_add_comm_group V] [seminormed_add_comm_group W]
  [normed_space ğ•œ V] [normed_space ğ•œ W] [metric_space P] [pseudo_metric_space Q]
  [normed_add_torsor V P] [normed_add_torsor W Q]
include V W

local attribute [instance, nolint fails_quickly] affine_subspace.to_normed_add_torsor
  affine_subspace.nonempty_map

@[simp] lemma image_intrinsic_interior (Ï† : P â†’áµƒâ±[ğ•œ] Q) (s : set P) :
  intrinsic_interior ğ•œ (Ï† '' s) = Ï† '' intrinsic_interior ğ•œ s :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { simp only [intrinsic_interior_empty, image_empty] },
  haveI : nonempty s := hs.to_subtype,
  let f := ((affine_span ğ•œ s).isometry_equiv_map Ï†).to_homeomorph,
  have : Ï†.to_affine_map âˆ˜ coe âˆ˜ f.symm = coe := funext isometry_equiv_map.apply_symm_apply,
  rw [intrinsic_interior, intrinsic_interior, â†Ï†.coe_to_affine_map, â†map_span Ï†.to_affine_map s,
    â†this, â†function.comp.assoc, image_comp, image_comp, f.symm.image_interior, f.image_symm,
    â†preimage_comp, function.comp.assoc, f.symm_comp_self, affine_isometry.coe_to_affine_map,
    function.comp.right_id, preimage_comp, Ï†.injective.preimage_image],
end

@[simp] lemma image_intrinsic_frontier (Ï† : P â†’áµƒâ±[ğ•œ] Q) (s : set P) :
  intrinsic_frontier ğ•œ (Ï† '' s) = Ï† '' intrinsic_frontier ğ•œ s :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { simp },
  haveI : nonempty s := hs.to_subtype,
  let f := ((affine_span ğ•œ s).isometry_equiv_map Ï†).to_homeomorph,
  have : Ï†.to_affine_map âˆ˜ coe âˆ˜ f.symm = coe := funext isometry_equiv_map.apply_symm_apply,
  rw [intrinsic_frontier, intrinsic_frontier, â†Ï†.coe_to_affine_map, â†map_span Ï†.to_affine_map s,
    â†this, â†function.comp.assoc, image_comp, image_comp, f.symm.image_frontier, f.image_symm,
    â†preimage_comp, function.comp.assoc, f.symm_comp_self, affine_isometry.coe_to_affine_map,
    function.comp.right_id, preimage_comp, Ï†.injective.preimage_image],
end

@[simp] lemma image_intrinsic_closure (Ï† : P â†’áµƒâ±[ğ•œ] Q) (s : set P) :
  intrinsic_closure ğ•œ (Ï† '' s) = Ï† '' intrinsic_closure ğ•œ s :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { simp },
  haveI : nonempty s := hs.to_subtype,
  let f := ((affine_span ğ•œ s).isometry_equiv_map Ï†).to_homeomorph,
  have : Ï†.to_affine_map âˆ˜ coe âˆ˜ f.symm = coe := funext isometry_equiv_map.apply_symm_apply,
  rw [intrinsic_closure, intrinsic_closure, â†Ï†.coe_to_affine_map, â†map_span Ï†.to_affine_map s,
    â†this, â†function.comp.assoc, image_comp, image_comp, f.symm.image_closure, f.image_symm,
    â†preimage_comp, function.comp.assoc, f.symm_comp_self, affine_isometry.coe_to_affine_map,
    function.comp.right_id, preimage_comp, Ï†.injective.preimage_image],
end

end affine_isometry

section normed_add_torsor
variables (ğ•œ) [nontrivially_normed_field ğ•œ] [complete_space ğ•œ] [normed_add_comm_group V]
  [normed_space ğ•œ V] [finite_dimensional ğ•œ V] [metric_space P] [normed_add_torsor V P] (s : set P)
include V

@[simp] lemma intrinsic_closure_eq_closure : intrinsic_closure ğ•œ s = closure s :=
begin
  ext x,
  simp only [mem_closure_iff, mem_intrinsic_closure],
  refine âŸ¨_, Î» h, âŸ¨âŸ¨x, _âŸ©, _, subtype.coe_mk _ _âŸ©âŸ©,
  { rintro âŸ¨x, h, rflâŸ© t ht hx,
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h _ (continuous_induced_dom.is_open_preimage t ht) hx,
    exact âŸ¨z, hzâ‚, hzâ‚‚âŸ© },
  { by_contradiction hc,
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h _ (affine_span ğ•œ s).closed_of_finite_dimensional.is_open_compl hc,
    exact hzâ‚ (subset_affine_span ğ•œ s hzâ‚‚) },
  { rintro _ âŸ¨t, ht, rflâŸ© hx,
    obtain âŸ¨y, hyt, hysâŸ© := h _ ht hx,
    exact âŸ¨âŸ¨_, subset_affine_span ğ•œ s hysâŸ©, hyt, hysâŸ© }
end

variables {ğ•œ}

@[simp] lemma closure_diff_intrinsic_interior (s : set P) :
  closure s \ intrinsic_interior ğ•œ s = intrinsic_frontier ğ•œ s :=
intrinsic_closure_eq_closure ğ•œ s â–¸ intrinsic_closure_diff_intrinsic_interior s

@[simp] lemma closure_diff_intrinsic_frontier (s : set P) :
  closure s \ intrinsic_frontier ğ•œ s = intrinsic_interior ğ•œ s :=
intrinsic_closure_eq_closure ğ•œ s â–¸ intrinsic_closure_diff_intrinsic_frontier s

end normed_add_torsor

private lemma aux {Î± Î² : Type*} [topological_space Î±] [topological_space Î²] (Ï† : Î± â‰ƒâ‚œ Î²)
  (s : set Î²) :
  (interior s).nonempty â†” (interior (Ï† â»Â¹' s)).nonempty :=
by rw [â†Ï†.image_symm, â†Ï†.symm.image_interior, nonempty_image_iff]

variables [normed_add_comm_group V] [normed_space â„ V] [finite_dimensional â„ V] {s : set V}

/-- The intrinsic interior of a nonempty convex set is nonempty. -/
protected lemma set.nonempty.intrinsic_interior (hscv : convex â„ s) (hsne : s.nonempty) :
  (intrinsic_interior â„ s).nonempty :=
begin
  haveI := hsne.coe_sort,
  obtain âŸ¨p, hpâŸ© := hsne,
  let p' : affine_span â„ s := âŸ¨p, subset_affine_span _ _ hpâŸ©,
  rw [intrinsic_interior, nonempty_image_iff,
    aux (affine_isometry_equiv.const_vsub â„ p').symm.to_homeomorph,
    convex.interior_nonempty_iff_affine_span_eq_top, affine_isometry_equiv.coe_to_homeomorph,
    â†affine_isometry_equiv.coe_to_affine_equiv, â†comap_span, affine_span_coe_preimage_eq_top,
    comap_top],
  exact hscv.affine_preimage ((affine_span â„ s).subtype.comp
    (affine_isometry_equiv.const_vsub â„ p').symm.to_affine_equiv.to_affine_map),
end

lemma intrinsic_interior_nonempty (hs : convex â„ s) :
  (intrinsic_interior â„ s).nonempty â†” s.nonempty :=
âŸ¨by { simp_rw nonempty_iff_ne_empty, rintro h rfl, exact h intrinsic_interior_empty },
  set.nonempty.intrinsic_interior hsâŸ©
