/-
Copyright (c) 2022 Paul Reichert. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Reichert, YaÃ«l Dillies
-/
import analysis.convex.basic
import analysis.normed_space.add_torsor_bases
import analysis.normed_space.basic
import analysis.normed_space.linear_isometry
import data.real.basic
import data.set.pointwise.basic
import linear_algebra.affine_space.pointwise

/-!
# Intrinsic frontier and interior

This file defines the intrinsic frontier and intrinsic interior of a set.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011] or chapter 1 of
[Rolf Schneider, *Convex Bodies: The Brunn-Minkowski theory*][schneider2013].
-/

open_locale pointwise

/-- The intrinsic interior of a set is its interior considered as a set in its affine span. -/
def intrinsic_interior (R : Type*) {V P : Type*} [ring R] [seminormed_add_comm_group V] [module R V]
  [pseudo_metric_space P] [normed_add_torsor V P] -- have to redeclare variables to ensure that
                                                  -- all typeclasses are used
  (A : set P) := (coe : affine_span R A â†’ P) '' interior ((coe : affine_span R A â†’ P) â»Â¹' A)

/-- The intrinsic frontier of a set is its frontier considered as a set in its affine span. -/
def intrinsic_frontier (R : Type*) {V P : Type*} [ring R] [seminormed_add_comm_group V] [module R V]
  [pseudo_metric_space P] [normed_add_torsor V P] (A : set P) :=
(coe : affine_span R A â†’ P) '' frontier ((coe : affine_span R A â†’ P) â»Â¹' A)

/-- The intrinsic closure of a set is its closure considered as a set in its affine span. -/
def intrinsic_closure (R : Type*) {V P : Type*} [ring R] [seminormed_add_comm_group V] [module R V]
  [pseudo_metric_space P] [normed_add_torsor V P] (A : set P) :=
(coe : affine_span R A â†’ P) '' closure ((coe : affine_span R A â†’ P) â»Â¹' A)

lemma intrinsic_interior_def (R : Type*) {V P : Type*} [ring R] [seminormed_add_comm_group V]
  [module R V] [pseudo_metric_space P] [normed_add_torsor V P] (A : set P) :
intrinsic_interior R A =
  (coe : affine_span R A â†’ P) '' interior ((coe : affine_span R A â†’ P) â»Â¹' A) := rfl

lemma intrinsic_frontier_def (R : Type*) {V P : Type*} [ring R] [seminormed_add_comm_group V]
  [module R V] [pseudo_metric_space P] [normed_add_torsor V P] (A : set P) :
intrinsic_frontier R A =
  (coe : affine_span R A â†’ P) '' frontier ((coe : affine_span R A â†’ P) â»Â¹' A) := rfl

lemma intrinsic_closure_def (R : Type*) {V P : Type*} [ring R] [seminormed_add_comm_group V]
  [module R V] [pseudo_metric_space P] [normed_add_torsor V P] (A : set P) :
intrinsic_closure R A =
  (coe : affine_span R A â†’ P) '' closure ((coe : affine_span R A â†’ P) â»Â¹' A) := rfl

lemma intrinsic_interior_subset {R : Type*} {V P : Type*} [ring R] [seminormed_add_comm_group V]
  [module R V] [pseudo_metric_space P] [normed_add_torsor V P] (A : set P) :
intrinsic_interior R A âŠ† A :=
set.image_subset_iff.mpr interior_subset

lemma intrinsic_frontier_subset {R : Type*} {V P : Type*} [ring R] [seminormed_add_comm_group V]
  [module R V] [pseudo_metric_space P] [normed_add_torsor V P] {A : set P} (hA : is_closed A) :
intrinsic_frontier R A âŠ† A :=
set.image_subset_iff.mpr (hA.preimage continuous_induced_dom).frontier_subset

@[simp]
lemma intrinsic_interior_empty {R : Type*} {V P : Type*} [ring R] [seminormed_add_comm_group V]
  [module R V] [pseudo_metric_space P] [normed_add_torsor V P] :
intrinsic_interior R (âˆ… : set P) = âˆ… :=
set.subset_empty_iff.mp $ intrinsic_interior_subset _

@[simp]
lemma intrinsic_frontier_empty {R : Type*} {V P : Type*} [ring R] [seminormed_add_comm_group V]
  [module R V] [pseudo_metric_space P] [normed_add_torsor V P] :
intrinsic_frontier R (âˆ… : set P) = âˆ… :=
set.subset_empty_iff.mp $ intrinsic_frontier_subset is_closed_empty

lemma preimage_singleton_eq_univ {R : Type*} {V P : Type*} [ring R]
  [seminormed_add_comm_group V] [module R V] [pseudo_metric_space P] [normed_add_torsor V P]
  (x : P) : (coe : affine_span R ({x} : set P) â†’ P) â»Â¹' {x} = set.univ :=
begin
  refine subset_antisymm (set.subset_univ _) _,
  rintro âŸ¨y, hyâŸ© -,
  obtain rfl := (affine_subspace.mem_affine_span_singleton _ _ _ _).mp hy,
  exact subtype.coe_mk _ _,
end

@[simp] lemma intrinsic_interior_singleton {R : Type*} {V P : Type*} [ring R]
  [seminormed_add_comm_group V] [module R V] [pseudo_metric_space P] [normed_add_torsor V P]
  (x : P) : intrinsic_interior R ({x} : set P) = {x} :=
begin
  rw [intrinsic_interior_def, interior_eq_iff_is_open.mpr], swap,
  { convert is_open_univ,
    exact preimage_singleton_eq_univ x },
  { rw [set.eq_singleton_iff_unique_mem],
    refine âŸ¨âŸ¨âŸ¨x, _âŸ©, subtype.coe_mk _ _, subtype.coe_mk _ _âŸ©, _âŸ©,
    { exact (affine_subspace.mem_affine_span_singleton _ _ _ _).mpr rfl },
    { rintro - âŸ¨âŸ¨y, hyâ‚âŸ©, hyâ‚‚, rflâŸ©,
      simpa only [set.mem_preimage, subtype.coe_mk, set.mem_singleton_iff] using hyâ‚‚ } },
end

@[simp] lemma intrinsic_frontier_singleton  {R : Type*} {V P : Type*} [ring R]
  [seminormed_add_comm_group V] [module R V] [pseudo_metric_space P] [normed_add_torsor V P]
  (x : P) : intrinsic_frontier R ({x} : set P) = âˆ… :=
begin
  rw [intrinsic_frontier_def, set.image_eq_empty],
  convert frontier_univ,
  exact preimage_singleton_eq_univ x,
end

@[simp] lemma intrinsic_closure_diff_intrinsic_interior {R : Type*} {V P : Type*} [ring R]
  [seminormed_add_comm_group V] [module R V] [pseudo_metric_space P] [normed_add_torsor V P]
  (A : set P) :
intrinsic_closure R A \ intrinsic_interior R A = intrinsic_frontier R A :=
begin
  rw [intrinsic_frontier_def, intrinsic_closure_def, intrinsic_interior_def,
    â†set.image_diff subtype.coe_injective],
  refl,
end

section local_instances

local attribute [instance, nolint fails_quickly] affine_subspace.to_normed_add_torsor
local attribute [instance, nolint fails_quickly] affine_subspace.nonempty_map

/--
The image of the intrinsic interior under an affine isometry is
the relative interior of the image.
-/
@[simp] -- not sure whether this is the correct direction for simp
lemma affine_isometry.image_intrinsic_interior {ğ•œ V Vâ‚‚ P Pâ‚‚: Type*}
  [normed_field ğ•œ] [seminormed_add_comm_group V] [seminormed_add_comm_group Vâ‚‚] [normed_space ğ•œ V]
  [normed_space ğ•œ Vâ‚‚] [metric_space P] [pseudo_metric_space Pâ‚‚] [normed_add_torsor V P]
  [normed_add_torsor Vâ‚‚ Pâ‚‚]
  (Ï† : P â†’áµƒâ±[ğ•œ] Pâ‚‚) (A : set P) :
intrinsic_interior ğ•œ (Ï† '' A) = Ï† '' intrinsic_interior ğ•œ A :=
begin
  rcases A.eq_empty_or_nonempty with rfl | hc,
  { simp only [intrinsic_interior_empty, set.image_empty] },
  haveI : nonempty A := hc.to_subtype,
  let f := (affine_span ğ•œ A).isometry_equiv_map Ï†,
  let f' := f.to_homeomorph,
  have : Ï†.to_affine_map âˆ˜ (coe : affine_span ğ•œ A â†’ P) âˆ˜ f'.symm =
    (coe : (affine_span ğ•œ A).map Ï†.to_affine_map â†’ Pâ‚‚),
  { funext x,
    exact affine_subspace.isometry_equiv_map.apply_symm_apply _ },
  simp only [intrinsic_interior_def, â†Ï†.coe_to_affine_map],
  rw [intrinsic_interior_def],
  rw [â†affine_subspace.map_span Ï†.to_affine_map A, â†this,
    â†function.comp.assoc, set.image_comp _ f'.symm,
    set.image_comp _ (coe : affine_span ğ•œ A â†’ P), f'.symm.image_interior, f'.image_symm,
    â†set.preimage_comp, function.comp.assoc, f'.symm_comp_self, affine_isometry.coe_to_affine_map,
    function.comp.right_id, @set.preimage_comp _ P, Ï†.injective.preimage_image],
end

end local_instances

@[simp] lemma intrinsic_closure_eq_closure (ğ•œ : Type*)
  [nontrivially_normed_field ğ•œ] [complete_space ğ•œ]
  {V P : Type} [normed_add_comm_group V] [normed_space ğ•œ V]
  [metric_space P] [normed_add_torsor V P]
  (A : set P) [finite_dimensional ğ•œ V] :
intrinsic_closure ğ•œ A = closure A :=
begin
  simp only [intrinsic_closure_def],
  ext x,
  simp only [mem_closure_iff, set.mem_image],
  split,
  { rintro âŸ¨x, h, rflâŸ© o ho hxo,
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h ((coe : affine_span ğ•œ A â†’ P) â»Â¹' o)
                   (continuous_induced_dom.is_open_preimage o ho) hxo,
    exact âŸ¨z, hzâ‚, hzâ‚‚âŸ© },
  { intro h,
    refine âŸ¨âŸ¨x, _âŸ©, _âŸ©,
    { by_contradiction hc,
      obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h
        (affine_span ğ•œ A)á¶œ
        (affine_subspace.closed_of_finite_dimensional (affine_span ğ•œ A)).is_open_compl
        hc,
      exact hzâ‚ (subset_affine_span ğ•œ A hzâ‚‚) },
    refine âŸ¨_, subtype.coe_mk _ _âŸ©,
    intros o ho hxo,
    have ho' := ho,
    rw [is_open_induced_iff] at ho,
    obtain âŸ¨o, ho, rflâŸ© := ho,
    rw [set.mem_preimage, subtype.coe_mk] at hxo,
    obtain âŸ¨w, hwo, hwAâŸ© := h _ ho hxo,
    have : w âˆˆ affine_span ğ•œ A := subset_affine_span ğ•œ A hwA,
    refine âŸ¨âŸ¨w, subset_affine_span ğ•œ A hwAâŸ©, hwo, hwAâŸ© },
end

@[simp] lemma closure_diff_intrinsic_interior {ğ•œ : Type*}
  [nontrivially_normed_field ğ•œ] [complete_space ğ•œ]
  {V P : Type} [normed_add_comm_group V] [normed_space ğ•œ V] [finite_dimensional ğ•œ V]
  [metric_space P] [normed_add_torsor V P]
  (A : set P) :
closure A \ intrinsic_interior ğ•œ A = intrinsic_frontier ğ•œ A :=
(intrinsic_closure_eq_closure ğ•œ A) â–¸ intrinsic_closure_diff_intrinsic_interior A

lemma nonempty_intrinsic_interior_of_nonempty_of_convex.aux {Î± Î² : Type*}
  [topological_space Î±] [topological_space Î²] (Ï† : Î± â‰ƒâ‚œ Î²) (A : set Î²) :
(interior A).nonempty â†” (interior (Ï† â»Â¹' A)).nonempty :=
begin
  rw [â†Ï†.image_symm, â†Ï†.symm.image_interior, set.nonempty_image_iff],
end

lemma nonempty_intrinsic_interior_of_nonempty_of_convex.aux_2 {ğ•œ Vâ‚ Pâ‚ Vâ‚‚ Pâ‚‚ : Type*}
  [normed_field ğ•œ] [normed_add_comm_group Vâ‚] [normed_add_comm_group Vâ‚‚]
  [pseudo_metric_space Pâ‚] [pseudo_metric_space Pâ‚‚] [normed_space ğ•œ Vâ‚] [normed_space ğ•œ Vâ‚‚]
  [normed_add_torsor Vâ‚ Pâ‚] [normed_add_torsor Vâ‚‚ Pâ‚‚]
  (f : Pâ‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) (A : set Pâ‚‚) :
affine_subspace.comap f.to_affine_equiv.to_affine_map (affine_span ğ•œ A) =
  affine_span ğ•œ (f â»Â¹' A) :=
f.to_affine_equiv.comap_span A

lemma nonempty_intrinsic_interior_of_nonempty_of_convex
  {V : Type*} [normed_add_comm_group V] [normed_space â„ V] [finite_dimensional â„ V]
  {A : set V} (Ane : A.nonempty) (Acv : convex â„ A) :
(intrinsic_interior â„ A).nonempty :=
begin
  haveI : nonempty A := set.nonempty_coe_sort.mpr Ane,
  rw [intrinsic_interior_def, set.nonempty_image_iff],
  obtain âŸ¨p, hpâŸ© := Ane,
  let p' : affine_span â„ A := âŸ¨p, subset_affine_span _ _ hpâŸ©,
  rw [nonempty_intrinsic_interior_of_nonempty_of_convex.aux
    (affine_isometry_equiv.const_vsub â„ p').symm.to_homeomorph,
    convex.interior_nonempty_iff_affine_span_eq_top],
  { rw [affine_isometry_equiv.coe_to_homeomorph,
        â†nonempty_intrinsic_interior_of_nonempty_of_convex.aux_2
          (affine_isometry_equiv.const_vsub â„ p').symm,
        affine_span_coe_preimage_eq_top A],
    exact affine_subspace.comap_top },
  { exact convex.affine_preimage (((affine_span â„ A).subtype).comp
    (affine_isometry_equiv.const_vsub â„ p').symm.to_affine_equiv.to_affine_map) Acv },
end
