/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudriashov
-/
import analysis.convex.combination
import analysis.convex.function

/-!
# Jensen's inequality and maximum principle for convex functions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file, we prove the finite Jensen inequality and the finite maximum principle for convex
functions. The integral versions are to be found in `analysis.convex.integral`.

## Main declarations

Jensen's inequalities:
* `convex_on.map_center_mass_le`, `convex_on.map_sum_le`: Convex Jensen's inequality. The image of a
  convex combination of points under a convex function is less than the convex combination of the
  images.
* `concave_on.le_map_center_mass`, `concave_on.le_map_sum`: Concave Jensen's inequality.

As corollaries, we get:
* `convex_on.exists_ge_of_mem_convex_hull `: Maximum principle for convex functions.
* `concave_on.exists_le_of_mem_convex_hull`: Minimum principle for concave functions.
-/

open finset linear_map set
open_locale big_operators classical convex pointwise

variables {ğ•œ E F Î² Î¹ : Type*}

/-! ### Jensen's inequality -/

section jensen
variables [linear_ordered_field ğ•œ] [add_comm_group E] [ordered_add_comm_group Î²] [module ğ•œ E]
  [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²} {t : finset Î¹} {w : Î¹ â†’ ğ•œ} {p : Î¹ â†’ E}

/-- Convex **Jensen's inequality**, `finset.center_mass` version. -/
lemma convex_on.map_center_mass_le (hf : convex_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
  (hâ‚ : 0 < âˆ‘ i in t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  f (t.center_mass w p) â‰¤ t.center_mass w (f âˆ˜ p) :=
begin
  have hmem' : âˆ€ i âˆˆ t, (p i, (f âˆ˜ p) i) âˆˆ {p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2},
    from Î» i hi, âŸ¨hmem i hi, le_rflâŸ©,
  convert (hf.convex_epigraph.center_mass_mem hâ‚€ hâ‚ hmem').2;
    simp only [center_mass, function.comp, prod.smul_fst, prod.fst_sum,
      prod.smul_snd, prod.snd_sum],
end

/-- Concave **Jensen's inequality**, `finset.center_mass` version. -/
lemma concave_on.le_map_center_mass (hf : concave_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
  (hâ‚ : 0 < âˆ‘ i in t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  t.center_mass w (f âˆ˜ p) â‰¤ f (t.center_mass w p) :=
@convex_on.map_center_mass_le ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ _ _ _ _ _ hf hâ‚€ hâ‚ hmem

/-- Convex **Jensen's inequality**, `finset.sum` version. -/
lemma convex_on.map_sum_le (hf : convex_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i in t, w i = 1)
  (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  f (âˆ‘ i in t, w i â€¢ p i) â‰¤ âˆ‘ i in t, w i â€¢ f (p i) :=
by simpa only [center_mass, hâ‚, inv_one, one_smul]
  using hf.map_center_mass_le hâ‚€ (hâ‚.symm â–¸ zero_lt_one) hmem

/-- Concave **Jensen's inequality**, `finset.sum` version. -/
lemma concave_on.le_map_sum (hf : concave_on ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i in t, w i = 1)
  (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
  âˆ‘ i in t, w i â€¢ f (p i) â‰¤ f (âˆ‘ i in t, w i â€¢ p i) :=
@convex_on.map_sum_le ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ _ _ _ _ _ hf hâ‚€ hâ‚ hmem

end jensen

/-! ### Maximum principle -/

section maximum_principle
variables [linear_ordered_field ğ•œ] [add_comm_group E] [linear_ordered_add_comm_group Î²]
  [module ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²} {t : finset Î¹} {w : Î¹ â†’ ğ•œ}
  {p : Î¹ â†’ E} {x : E}

lemma le_sup_of_mem_convex_hull {s : finset E} (hf : convex_on ğ•œ (convex_hull ğ•œ (s : set E)) f)
  (hx : x âˆˆ convex_hull ğ•œ (s : set E)) :
  f x â‰¤ s.sup' (coe_nonempty.1 $ convex_hull_nonempty_iff.1 âŸ¨x, hxâŸ©) f :=
begin
  obtain âŸ¨w, hwâ‚€, hwâ‚, rflâŸ© := mem_convex_hull.1 hx,
  exact (hf.map_center_mass_le hwâ‚€ (by positivity) $ subset_convex_hull _ _).trans
    (center_mass_le_sup hwâ‚€ $ by positivity),
end

lemma inf_le_of_mem_convex_hull {s : finset E} (hf : concave_on ğ•œ (convex_hull ğ•œ (s : set E)) f)
  (hx : x âˆˆ convex_hull ğ•œ (s : set E)) :
  s.inf' (coe_nonempty.1 $ convex_hull_nonempty_iff.1 âŸ¨x, hxâŸ©) f â‰¤ f x :=
le_sup_of_mem_convex_hull hf.dual hx

/-- If a function `f` is convex on `s`, then the value it takes at some center of mass of points of
`s` is less than the value it takes on one of those points. -/
lemma convex_on.exists_ge_of_center_mass (h : convex_on ğ•œ s f)
  (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i in t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) :
  âˆƒ i âˆˆ t, f (t.center_mass w p) â‰¤ f (p i) :=
begin
  set y := t.center_mass w p,
  rsuffices âŸ¨i, hi, hfiâŸ© : âˆƒ i âˆˆ t.filter (Î» i, w i â‰  0), w i â€¢ f y â‰¤ w i â€¢ (f âˆ˜ p) i,
  { rw mem_filter at hi,
    exact âŸ¨i, hi.1, (smul_le_smul_iff_of_pos $ (hwâ‚€ i hi.1).lt_of_ne hi.2.symm).1 hfiâŸ© },
  have hw' : (0 : ğ•œ) < âˆ‘ i in filter (Î» i, w i â‰  0) t, w i := by rwa sum_filter_ne_zero,
  refine exists_le_of_sum_le (nonempty_of_sum_ne_zero hw'.ne') _,
  rw [â†sum_smul, â†smul_le_smul_iff_of_pos (inv_pos.2 hw'), inv_smul_smulâ‚€ hw'.ne',
    â†finset.center_mass, finset.center_mass_filter_ne_zero],
  exact h.map_center_mass_le hwâ‚€ hwâ‚ hp,
  apply_instance,
end

/-- If a function `f` is concave on `s`, then the value it takes at some center of mass of points of
`s` is greater than the value it takes on one of those points. -/
lemma concave_on.exists_le_of_center_mass (h : concave_on ğ•œ s f)
  (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i in t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) :
  âˆƒ i âˆˆ t, f (p i) â‰¤ f (t.center_mass w p) :=
@convex_on.exists_ge_of_center_mass ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ _ _ _ _ _ h hwâ‚€ hwâ‚ hp

/-- Maximum principle for convex functions. If a function `f` is convex on the convex hull of `s`,
then the eventual maximum of `f` on `convex_hull ğ•œ s` lies in `s`. -/
lemma convex_on.exists_ge_of_mem_convex_hull (hf : convex_on ğ•œ (convex_hull ğ•œ s) f) {x}
  (hx : x âˆˆ convex_hull ğ•œ s) : âˆƒ y âˆˆ s, f x â‰¤ f y :=
begin
  rw _root_.convex_hull_eq at hx,
  obtain âŸ¨Î±, t, w, p, hwâ‚€, hwâ‚, hp, rflâŸ© := hx,
  rcases hf.exists_ge_of_center_mass hwâ‚€ (hwâ‚.symm â–¸ zero_lt_one)
    (Î» i hi, subset_convex_hull ğ•œ s (hp i hi)) with âŸ¨i, hit, HiâŸ©,
  exact âŸ¨p i, hp i hit, HiâŸ©
end

/-- Minimum principle for concave functions. If a function `f` is concave on the convex hull of `s`,
then the eventual minimum of `f` on `convex_hull ğ•œ s` lies in `s`. -/
lemma concave_on.exists_le_of_mem_convex_hull (hf : concave_on ğ•œ (convex_hull ğ•œ s) f) {x}
  (hx : x âˆˆ convex_hull ğ•œ s) : âˆƒ y âˆˆ s, f y â‰¤ f x :=
@convex_on.exists_ge_of_mem_convex_hull ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ _ hf _ hx

end maximum_principle
