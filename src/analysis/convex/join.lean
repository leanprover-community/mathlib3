/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import analysis.convex.combination

/-!
# Convex join

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the convex join of two sets. The convex join of `s` and `t` is the union of the
segments with one end in `s` and the other in `t`. This is notably a useful gadget to deal with
convex hulls of finite sets.
-/

open set
open_locale big_operators

variables {Î¹ : Sort*} {ğ•œ E : Type*}

section ordered_semiring
variables (ğ•œ) [ordered_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E] {s t sâ‚ sâ‚‚ tâ‚ tâ‚‚ u : set E}
  {x y : E}

/-- The join of two sets is the union of the segments joining them. This can be interpreted as the
topological join, but within the original space. -/
def convex_join (s t : set E) : set E := â‹ƒ (x âˆˆ s) (y âˆˆ t), segment ğ•œ x y

variables {ğ•œ}

lemma mem_convex_join : x âˆˆ convex_join ğ•œ s t â†” âˆƒ (a âˆˆ s) (b âˆˆ t), x âˆˆ segment ğ•œ a b :=
by simp [convex_join]

lemma convex_join_comm (s t : set E) : convex_join ğ•œ s t = convex_join ğ•œ t s :=
(Unionâ‚‚_comm _).trans $ by simp_rw [convex_join, segment_symm]

lemma convex_join_mono (hs : sâ‚ âŠ† sâ‚‚) (ht : tâ‚ âŠ† tâ‚‚) : convex_join ğ•œ sâ‚ tâ‚ âŠ† convex_join ğ•œ sâ‚‚ tâ‚‚ :=
bUnion_mono hs $ Î» x hx, bUnion_mono ht $ Î» y hy, subset.rfl

lemma convex_join_mono_left (hs : sâ‚ âŠ† sâ‚‚) : convex_join ğ•œ sâ‚ t âŠ† convex_join ğ•œ sâ‚‚ t :=
convex_join_mono hs subset.rfl

lemma convex_join_mono_right (ht : tâ‚ âŠ† tâ‚‚) : convex_join ğ•œ s tâ‚ âŠ† convex_join ğ•œ s tâ‚‚ :=
convex_join_mono subset.rfl ht

@[simp] lemma convex_join_empty_left (t : set E) : convex_join ğ•œ âˆ… t = âˆ… := by simp [convex_join]
@[simp] lemma convex_join_empty_right (s : set E) : convex_join ğ•œ s âˆ… = âˆ… := by simp [convex_join]

@[simp] lemma convex_join_singleton_left (t : set E) (x : E) :
  convex_join ğ•œ {x} t = â‹ƒ (y âˆˆ t), segment ğ•œ x y := by simp [convex_join]

@[simp] lemma convex_join_singleton_right (s : set E) (y : E) :
  convex_join ğ•œ s {y} = â‹ƒ (x âˆˆ s), segment ğ•œ x y := by simp [convex_join]

@[simp] lemma convex_join_singletons (x : E) : convex_join ğ•œ {x} {y} = segment ğ•œ x y :=
by simp [convex_join]

@[simp] lemma convex_join_union_left (sâ‚ sâ‚‚ t : set E) :
  convex_join ğ•œ (sâ‚ âˆª sâ‚‚) t = convex_join ğ•œ sâ‚ t âˆª convex_join ğ•œ sâ‚‚ t :=
by simp_rw [convex_join, mem_union, Union_or, Union_union_distrib]

@[simp] lemma convex_join_union_right (s tâ‚ tâ‚‚ : set E) :
  convex_join ğ•œ s (tâ‚ âˆª tâ‚‚) = convex_join ğ•œ s tâ‚ âˆª convex_join ğ•œ s tâ‚‚ :=
by simp_rw [convex_join, mem_union, Union_or, Union_union_distrib]

@[simp] lemma convex_join_Union_left (s : Î¹ â†’ set E) (t : set E) :
  convex_join ğ•œ (â‹ƒ i, s i) t = â‹ƒ i, convex_join ğ•œ (s i) t :=
by { simp_rw [convex_join, mem_Union, Union_exists], exact Union_comm _ }

@[simp] lemma convex_join_Union_right (s : set E) (t : Î¹ â†’ set E) :
  convex_join ğ•œ s (â‹ƒ i, t i) = â‹ƒ i, convex_join ğ•œ s (t i) :=
by simp_rw [convex_join_comm s, convex_join_Union_left]

lemma segment_subset_convex_join (hx : x âˆˆ s) (hy : y âˆˆ t) : segment ğ•œ x y âŠ† convex_join ğ•œ s t :=
(subset_Unionâ‚‚ y hy).trans (subset_Unionâ‚‚ x hx)

lemma subset_convex_join_left (h : t.nonempty) : s âŠ† convex_join ğ•œ s t :=
Î» x hx, let âŸ¨y, hyâŸ© := h in segment_subset_convex_join hx hy $ left_mem_segment _ _ _

lemma subset_convex_join_right (h : s.nonempty) : t âŠ† convex_join ğ•œ s t :=
Î» y hy, let âŸ¨x, hxâŸ© := h in segment_subset_convex_join hx hy $ right_mem_segment _ _ _

lemma convex_join_subset (hs : s âŠ† u) (ht : t âŠ† u) (hu : convex ğ•œ u) : convex_join ğ•œ s t âŠ† u :=
Unionâ‚‚_subset $ Î» x hx, Unionâ‚‚_subset $ Î» y hy, hu.segment_subset (hs hx) (ht hy)

lemma convex_join_subset_convex_hull (s t : set E) : convex_join ğ•œ s t âŠ† convex_hull ğ•œ (s âˆª t) :=
convex_join_subset ((subset_union_left _ _).trans $ subset_convex_hull _ _)
  ((subset_union_right _ _).trans $ subset_convex_hull _ _) $ convex_convex_hull _ _

end ordered_semiring

section linear_ordered_field
variables [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E] {s t u : set E} {x y : E}

lemma convex_join_assoc_aux (s t u : set E) :
  convex_join ğ•œ (convex_join ğ•œ s t) u âŠ† convex_join ğ•œ s (convex_join ğ•œ t u) :=
begin
  simp_rw [subset_def, mem_convex_join],
  rintro _ âŸ¨z, âŸ¨x, hx, y, hy, aâ‚, bâ‚, haâ‚, hbâ‚, habâ‚, rflâŸ©, z, hz, aâ‚‚, bâ‚‚, haâ‚‚, hbâ‚‚, habâ‚‚, rflâŸ©,
  obtain rfl | hbâ‚‚ := hbâ‚‚.eq_or_lt,
  { refine âŸ¨x, hx, y, âŸ¨y, hy, z, hz, left_mem_segment _ _ _âŸ©, aâ‚, bâ‚, haâ‚, hbâ‚, habâ‚, _âŸ©,
    rw add_zero at habâ‚‚,
    rw [habâ‚‚, one_smul, zero_smul, add_zero] },
  have haâ‚‚bâ‚ : 0 â‰¤ aâ‚‚ * bâ‚ := mul_nonneg haâ‚‚ hbâ‚,
  have hab : 0 < aâ‚‚ * bâ‚ + bâ‚‚ := add_pos_of_nonneg_of_pos haâ‚‚bâ‚ hbâ‚‚,
  refine âŸ¨x, hx, ((aâ‚‚ * bâ‚) / (aâ‚‚ * bâ‚ + bâ‚‚)) â€¢ y + (bâ‚‚ / (aâ‚‚ * bâ‚ + bâ‚‚)) â€¢ z,
    âŸ¨y, hy, z, hz, _, _, _, _, _, rflâŸ©, aâ‚‚ * aâ‚, aâ‚‚ * bâ‚ + bâ‚‚, mul_nonneg haâ‚‚ haâ‚, hab.le, _, _âŸ©,
  { exact div_nonneg haâ‚‚bâ‚ hab.le },
  { exact div_nonneg hbâ‚‚.le hab.le },
  { rw [â†add_div, div_self hab.ne'] },
  { rw [â†add_assoc, â†mul_add, habâ‚, mul_one, habâ‚‚] },
  { simp_rw [smul_add, â†mul_smul, mul_div_cancel' _ hab.ne', add_assoc] }
end

lemma convex_join_assoc (s t u : set E) :
  convex_join ğ•œ (convex_join ğ•œ s t) u = convex_join ğ•œ s (convex_join ğ•œ t u) :=
begin
  refine (convex_join_assoc_aux _ _ _).antisymm _,
  simp_rw [convex_join_comm s, convex_join_comm _ u],
  exact convex_join_assoc_aux _ _ _,
end

lemma convex_join_left_comm (s t u : set E) :
  convex_join ğ•œ s (convex_join ğ•œ t u) = convex_join ğ•œ t (convex_join ğ•œ s u) :=
by simp_rw [â†convex_join_assoc, convex_join_comm]

lemma convex_join_right_comm (s t u : set E) :
  convex_join ğ•œ (convex_join ğ•œ s t) u = convex_join ğ•œ (convex_join ğ•œ s u) t :=
by simp_rw [convex_join_assoc, convex_join_comm]

lemma convex_join_convex_join_convex_join_comm (s t u v : set E) :
  convex_join ğ•œ (convex_join ğ•œ s t) (convex_join ğ•œ u v) =
    convex_join ğ•œ (convex_join ğ•œ s u) (convex_join ğ•œ t v) :=
by simp_rw [â†convex_join_assoc, convex_join_right_comm]

lemma convex_hull_insert (hs : s.nonempty) :
  convex_hull ğ•œ (insert x s) = convex_join ğ•œ {x} (convex_hull ğ•œ s) :=
begin
  classical,
  refine (convex_join_subset ((singleton_subset_iff.2 $ mem_insert _ _).trans $ subset_convex_hull
    _ _) (convex_hull_mono $ subset_insert _ _) $ convex_convex_hull _ _).antisymm' (Î» x hx, _),
  rw convex_hull_eq at hx,
  obtain âŸ¨Î¹, t, w, z, hwâ‚€, hwâ‚, hz, rflâŸ© := hx,
  have : (âˆ‘ i in t.filter (Î» i, z i = x), w i) â€¢ x + âˆ‘ i in t.filter (Î» i, z i â‰  x), w i â€¢ z i =
    t.center_mass w z,
  { rw [finset.center_mass_eq_of_sum_1 _ _ hwâ‚, finset.sum_smul],
    convert finset.sum_filter_add_sum_filter_not _ _ (w â€¢ z) using 2,
    refine finset.sum_congr rfl (Î» i hi, _),
    rw [pi.smul_apply', (finset.mem_filter.1 hi).2] },
  rw â†this,
  have hwâ‚€' : âˆ€ i âˆˆ t.filter (Î» i, z i â‰  x), 0 â‰¤ w i := Î» i hi, hwâ‚€ _ $ finset.filter_subset _ _ hi,
  obtain hw | hw := (finset.sum_nonneg hwâ‚€').eq_or_gt,
  { rw [â†finset.sum_filter_add_sum_filter_not _ (Î» i, z i = x), hw, add_zero] at hwâ‚,
    rw [hwâ‚, one_smul, finset.sum_eq_zero, add_zero],
    { exact subset_convex_join_left hs.convex_hull (mem_singleton _) },
    simp_rw finset.sum_eq_zero_iff_of_nonneg hwâ‚€' at hw,
    rintro i hi,
    rw [hw _ hi, zero_smul] },
  refine mem_convex_join.2 âŸ¨x, mem_singleton _, (t.filter $ Î» i, z i â‰  x).center_mass w z,
    finset.center_mass_mem_convex_hull _ hwâ‚€' hw (Î» i hi, _),
    âˆ‘ i in t.filter (Î» i, z i = x), w i, âˆ‘ i in t.filter (Î» i, z i â‰  x), w i,
    finset.sum_nonneg (Î» i hi, hwâ‚€ _ $ finset.filter_subset _ _ hi), finset.sum_nonneg hwâ‚€', _, _âŸ©,
  { rw finset.mem_filter at hi,
    exact mem_of_mem_insert_of_ne (hz _ hi.1) hi.2 },
  { rw [finset.sum_filter_add_sum_filter_not, hwâ‚] },
  { rw [finset.center_mass, smul_inv_smulâ‚€ hw.ne', finset.sum_smul] }
end

lemma convex_join_segments (a b c d : E) :
  convex_join ğ•œ (segment ğ•œ a b) (segment ğ•œ c d) = convex_hull ğ•œ {a, b, c, d} :=
by simp only [convex_hull_insert, insert_nonempty, singleton_nonempty, convex_hull_pair,
    â†convex_join_assoc, convex_join_singletons]

lemma convex_join_segment_singleton (a b c : E) :
  convex_join ğ•œ (segment ğ•œ a b) {c} = convex_hull ğ•œ {a, b, c} :=
by rw [â†pair_eq_singleton, â†convex_join_segments, segment_same, pair_eq_singleton]

lemma convex_join_singleton_segment (a b c : E) :
  convex_join ğ•œ {a} (segment ğ•œ b c) = convex_hull ğ•œ {a, b, c} :=
by rw [â†segment_same ğ•œ, convex_join_segments, insert_idem]

protected lemma convex.convex_join (hs : convex ğ•œ s) (ht : convex ğ•œ t) :
  convex ğ•œ (convex_join ğ•œ s t) :=
begin
  rw convex_iff_segment_subset at âŠ¢ ht hs,
  simp_rw mem_convex_join,
  rintro x âŸ¨xa, hxa, xb, hxb, hxâŸ© y âŸ¨ya, hya, yb, hyb, hyâŸ©,
  refine (segment_subset_convex_join hx hy).trans _,
  have triv : ({xa, xb, ya, yb} : set E) = {xa, ya, xb, yb} := by simp only [set.insert_comm],
  rw [convex_join_segments, triv, â†convex_join_segments],
  exact convex_join_mono (hs hxa hya) (ht hxb hyb),
end

protected lemma convex.convex_hull_union (hs : convex ğ•œ s) (ht : convex ğ•œ t) (hsâ‚€ : s.nonempty)
  (htâ‚€ : t.nonempty) :
  convex_hull ğ•œ (s âˆª t) = convex_join ğ•œ s t :=
(convex_hull_min (union_subset (subset_convex_join_left htâ‚€) $ subset_convex_join_right hsâ‚€) $
  hs.convex_join ht).antisymm $ convex_join_subset_convex_hull _ _

lemma convex_hull_union (hs : s.nonempty) (ht : t.nonempty) :
  convex_hull ğ•œ (s âˆª t) = convex_join ğ•œ (convex_hull ğ•œ s) (convex_hull ğ•œ t) :=
begin
  rw [â†convex_hull_convex_hull_union_left, â†convex_hull_convex_hull_union_right],
  exact (convex_convex_hull ğ•œ s).convex_hull_union (convex_convex_hull ğ•œ t)
    hs.convex_hull ht.convex_hull,
end

end linear_ordered_field
