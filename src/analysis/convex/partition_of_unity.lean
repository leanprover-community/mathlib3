/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.partition_of_unity
import analysis.convex.combination

/-!
# Partition of unity and convex sets

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we prove the following lemma, see `exists_continuous_forall_mem_convex_of_local`. Let
`X` be a normal paracompact topological space (e.g., any extended metric space). Let `E` be a
topological real vector space. Let `t : X ‚Üí set E` be a family of convex sets. Suppose that for each
point `x : X`, there exists a neighborhood `U ‚àà ùìù X` and a function `g : X ‚Üí E` that is continuous
on `U` and sends each `y ‚àà U` to a point of `t y`. Then there exists a continuous map `g : C(X, E)`
such that `g x ‚àà t x` for all `x`.

We also formulate a useful corollary, see `exists_continuous_forall_mem_convex_of_local_const`, that
assumes that local functions `g` are constants.

## Tags

partition of unity
-/

open set function
open_locale big_operators topology

variables {Œπ X E : Type*} [topological_space X] [add_comm_group E] [module ‚Ñù E]

lemma partition_of_unity.finsum_smul_mem_convex {s : set X} (f : partition_of_unity Œπ X s)
  {g : Œπ ‚Üí X ‚Üí E} {t : set E} {x : X} (hx : x ‚àà s) (hg : ‚àÄ i, f i x ‚â† 0 ‚Üí g i x ‚àà t)
  (ht : convex ‚Ñù t) :
  ‚àë·∂† i, f i x ‚Ä¢ g i x ‚àà t :=
ht.finsum_mem (Œª i, f.nonneg _ _) (f.sum_eq_one hx) hg

variables [normal_space X] [paracompact_space X] [topological_space E] [has_continuous_add E]
  [has_continuous_smul ‚Ñù E] {t : X ‚Üí set E}

/-- Let `X` be a normal paracompact topological space (e.g., any extended metric space). Let `E` be
a topological real vector space. Let `t : X ‚Üí set E` be a family of convex sets. Suppose that for
each point `x : X`, there exists a neighborhood `U ‚àà ùìù X` and a function `g : X ‚Üí E` that is
continuous on `U` and sends each `y ‚àà U` to a point of `t y`. Then there exists a continuous map
`g : C(X, E)` such that `g x ‚àà t x` for all `x`. See also
`exists_continuous_forall_mem_convex_of_local_const`. -/
lemma exists_continuous_forall_mem_convex_of_local (ht : ‚àÄ x, convex ‚Ñù (t x))
  (H : ‚àÄ x : X, ‚àÉ (U ‚àà ùìù x) (g : X ‚Üí E), continuous_on g U ‚àß ‚àÄ y ‚àà U, g y ‚àà t y) : ‚àÉ
  g : C(X, E), ‚àÄ x, g x ‚àà t x :=
begin
  choose U hU g hgc hgt using H,
  obtain ‚ü®f, hf‚ü© := partition_of_unity.exists_is_subordinate is_closed_univ (Œª x, interior (U x))
    (Œª x, is_open_interior) (Œª x hx, mem_Union.2 ‚ü®x, mem_interior_iff_mem_nhds.2 (hU x)‚ü©),
  refine ‚ü®‚ü®Œª x, ‚àë·∂† i, f i x ‚Ä¢ g i x,
    hf.continuous_finsum_smul (Œª i, is_open_interior) $ Œª i, (hgc i).mono interior_subset‚ü©,
    Œª x, f.finsum_smul_mem_convex (mem_univ x) (Œª i hi, hgt _ _ _) (ht _)‚ü©,
  exact interior_subset (hf _ $ subset_closure hi)
end

/-- Let `X` be a normal paracompact topological space (e.g., any extended metric space). Let `E` be
a topological real vector space. Let `t : X ‚Üí set E` be a family of convex sets. Suppose that for
each point `x : X`, there exists a vector `c : E` that belongs to `t y` for all `y` in a
neighborhood of `x`. Then there exists a continuous map `g : C(X, E)` such that `g x ‚àà t x` for all
`x`. See also `exists_continuous_forall_mem_convex_of_local`. -/
lemma exists_continuous_forall_mem_convex_of_local_const (ht : ‚àÄ x, convex ‚Ñù (t x))
  (H : ‚àÄ x : X, ‚àÉ c : E, ‚àÄ·∂† y in ùìù x, c ‚àà t y) :
  ‚àÉ g : C(X, E), ‚àÄ x, g x ‚àà t x :=
exists_continuous_forall_mem_convex_of_local ht $ Œª x,
  let ‚ü®c, hc‚ü© := H x in ‚ü®_, hc, Œª _, c, continuous_on_const, Œª y, id‚ü©
