/-
Copyright (c) 2021 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import analysis.convex.function

/-!
# Quasiconvex and quasiconcave functions

This file defines quasiconvexity, quasiconcavity and quasilinearity of functions, which are
generalizations of unimodality and monotonicity. Convexity implies quasiconvexity, concavity implies
quasiconcavity, and monotonicity implies quasilinearity.

## Main declarations

* `quasiconvex_on ğ•œ s f`: Quasiconvexity of the function `f` on the set `s` with scalars `ğ•œ`. This
  means that, for all `r`, `{x âˆˆ s | f x â‰¤ r}` is `ğ•œ`-convex.
* `quasiconcave_on ğ•œ s f`: Quasiconcavity of the function `f` on the set `s` with scalars `ğ•œ`. This
  means that, for all `r`, `{x âˆˆ s | r â‰¤ f x}` is `ğ•œ`-convex.
* `quasilinear_on ğ•œ s f`: Quasilinearity of the function `f` on the set `s` with scalars `ğ•œ`. This
  means that `f` is both quasiconvex and quasiconcave.

## TODO

Prove that a quasilinear function between two linear orders is either monotone or antitone. This is
not hard but quite a pain to go about as there are many cases to consider.

## References

* https://en.wikipedia.org/wiki/Quasiconvex_function
-/

open set

variables {ğ•œ E F Î² : Type*}

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F]

section ordered_add_comm_monoid
variables (ğ•œ) [ordered_add_comm_monoid Î²] [has_scalar ğ•œ E] (s : set E) (f : E â†’ Î²)

/-- A function is quasiconvex if all its sublevels are convex.
This means that, for all `r`, `{x âˆˆ s | f x â‰¤ r}` is `ğ•œ`-convex. -/
def quasiconvex_on : Prop :=
âˆ€ r, convex ğ•œ {x âˆˆ s | f x â‰¤ r}

/-- A function is quasiconcave if all its superlevels are convex.
This means that, for all `r`, `{x âˆˆ s | r â‰¤ f x}` is `ğ•œ`-convex. -/
def quasiconcave_on : Prop :=
âˆ€ r, convex ğ•œ {x âˆˆ s | r â‰¤ f x}

/-- A function is quasilinear if it is both quasiconvex and quasiconcave.
This means that, for all `r`,
the sets `{x âˆˆ s | f x â‰¤ r}` and `{x âˆˆ s | r â‰¤ f x}` are `ğ•œ`-convex. -/
def quasilinear_on : Prop :=
quasiconvex_on ğ•œ s f âˆ§ quasiconcave_on ğ•œ s f

variables {ğ•œ s f}

lemma quasiconvex_on.dual (hf : quasiconvex_on ğ•œ s f) :
  @quasiconcave_on ğ•œ E (order_dual Î²) _ _ _ _ s f :=
hf

lemma quasiconcave_on.dual (hf : quasiconcave_on ğ•œ s f) :
  @quasiconvex_on ğ•œ E (order_dual Î²) _ _ _ _ s f :=
hf

lemma quasilinear_on.dual (hf : quasilinear_on ğ•œ s f) :
  @quasilinear_on ğ•œ E (order_dual Î²) _ _ _ _ s f :=
âŸ¨hf.2, hf.1âŸ©

lemma convex.quasiconvex_on_of_convex_le (hs : convex ğ•œ s) (h : âˆ€ r, convex ğ•œ {x | f x â‰¤ r}) :
  quasiconvex_on ğ•œ s f :=
Î» r, hs.inter (h r)

lemma convex.quasiconcave_on_of_convex_ge (hs : convex ğ•œ s) (h : âˆ€ r, convex ğ•œ {x | r â‰¤ f x}) :
  quasiconcave_on ğ•œ s f :=
@convex.quasiconvex_on_of_convex_le ğ•œ E (order_dual Î²) _ _ _ _ _ _ hs h

end ordered_add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid Î²]

section has_scalar
variables [has_scalar ğ•œ E] {s : set E} {f g : E â†’ Î²}

-- This only requires `directed_order Î²` but we don't have `directed_ordered_add_comm_monoid`
lemma quasiconvex_on.convex (hf : quasiconvex_on ğ•œ s f) : convex ğ•œ s :=
Î» x y hx hy a b ha hb hab,  (hf _ âŸ¨hx, le_max_left _ _âŸ© âŸ¨hy, le_max_right _ _âŸ© ha hb hab).1

lemma quasiconcave_on.convex (hf : quasiconcave_on ğ•œ s f) : convex ğ•œ s :=
hf.dual.convex

lemma quasiconvex_on.sup (hf : quasiconvex_on ğ•œ s f) (hg : quasiconvex_on ğ•œ s g) :
  quasiconvex_on ğ•œ s (f âŠ” g) :=
begin
  intro r,
  simp_rw [pi.sup_def, sup_le_iff, â†set.sep_inter_sep],
  exact (hf r).inter (hg r),
end

lemma quasiconcave_on.inf (hf : quasiconcave_on ğ•œ s f) (hg : quasiconcave_on ğ•œ s g) :
  quasiconcave_on ğ•œ s (f âŠ“ g) :=
hf.dual.sup hg

lemma quasiconvex_on_iff_le_max :
  quasiconvex_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
      f (a â€¢ x + b â€¢ y) â‰¤ max (f x) (f y) :=
âŸ¨Î» hf, âŸ¨hf.convex, Î» x y hx hy a b ha hb hab,
  (hf _ âŸ¨hx, le_max_left _ _âŸ© âŸ¨hy, le_max_right _ _âŸ© ha hb hab).2âŸ©,
  Î» hf r x y hx hy a b ha hb hab,
  âŸ¨hf.1 hx.1 hy.1 ha hb hab, (hf.2 hx.1 hy.1 ha hb hab).trans $ max_le hx.2 hy.2âŸ©âŸ©

lemma quasiconcave_on_iff_min_le :
  quasiconcave_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
      min (f x) (f y) â‰¤ f (a â€¢ x + b â€¢ y) :=
@quasiconvex_on_iff_le_max ğ•œ E (order_dual Î²) _ _ _ _ _ _

lemma quasilinear_on_iff_mem_interval :
  quasilinear_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y : Eâ¦„, x âˆˆ s â†’ y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
      f (a â€¢ x + b â€¢ y) âˆˆ interval (f x) (f y) :=
begin
  rw [quasilinear_on, quasiconvex_on_iff_le_max, quasiconcave_on_iff_min_le, and_and_and_comm,
    and_self],
  apply and_congr_right',
  simp_rw [â†forall_and_distrib, interval, mem_Icc, and_comm],
end

lemma quasiconvex_on.convex_lt (hf : quasiconvex_on ğ•œ s f) (r : Î²) : convex ğ•œ {x âˆˆ s | f x < r} :=
begin
  refine Î» x y hx hy a b ha hb hab, _,
  have h := hf _ âŸ¨hx.1, le_max_left _ _âŸ© âŸ¨hy.1, le_max_right _ _âŸ© ha hb hab,
  exact âŸ¨h.1, h.2.trans_lt $ max_lt hx.2 hy.2âŸ©,
end

lemma quasiconcave_on.convex_gt (hf : quasiconcave_on ğ•œ s f) (r : Î²) : convex ğ•œ {x âˆˆ s | r < f x} :=
hf.dual.convex_lt r

end has_scalar

section ordered_smul
variables [has_scalar ğ•œ E] [module ğ•œ Î²] [ordered_smul ğ•œ Î²] {s : set E} {f : E â†’ Î²}

lemma convex_on.quasiconvex_on (hf : convex_on ğ•œ s f) : quasiconvex_on ğ•œ s f :=
hf.convex_le

lemma concave_on.quasiconcave_on (hf : concave_on ğ•œ s f) : quasiconcave_on ğ•œ s f :=
hf.convex_ge

end ordered_smul
end linear_ordered_add_comm_monoid
end add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid E] [ordered_add_comm_monoid Î²] [module ğ•œ E]
  [ordered_smul ğ•œ E] {s : set E} {f : E â†’ Î²}

lemma monotone_on.quasiconvex_on (hf : monotone_on f s) (hs : convex ğ•œ s) : quasiconvex_on ğ•œ s f :=
hf.convex_le hs

lemma monotone_on.quasiconcave_on (hf : monotone_on f s) (hs : convex ğ•œ s) :
  quasiconcave_on ğ•œ s f :=
hf.convex_ge hs

lemma monotone_on.quasilinear_on (hf : monotone_on f s) (hs : convex ğ•œ s) : quasilinear_on ğ•œ s f :=
âŸ¨hf.quasiconvex_on hs, hf.quasiconcave_on hsâŸ©

lemma antitone_on.quasiconvex_on (hf : antitone_on f s) (hs : convex ğ•œ s) : quasiconvex_on ğ•œ s f :=
hf.convex_le hs

lemma antitone_on.quasiconcave_on (hf : antitone_on f s) (hs : convex ğ•œ s) :
  quasiconcave_on ğ•œ s f :=
hf.convex_ge hs

lemma antitone_on.quasilinear_on (hf : antitone_on f s) (hs : convex ğ•œ s) : quasilinear_on ğ•œ s f :=
âŸ¨hf.quasiconvex_on hs, hf.quasiconcave_on hsâŸ©

lemma monotone.quasiconvex_on (hf : monotone f) : quasiconvex_on ğ•œ univ f :=
(hf.monotone_on _).quasiconvex_on convex_univ

lemma monotone.quasiconcave_on (hf : monotone f) : quasiconcave_on ğ•œ univ f :=
(hf.monotone_on _).quasiconcave_on convex_univ

lemma monotone.quasilinear_on (hf : monotone f) : quasilinear_on ğ•œ univ f :=
âŸ¨hf.quasiconvex_on, hf.quasiconcave_onâŸ©

lemma antitone.quasiconvex_on (hf : antitone f) : quasiconvex_on ğ•œ univ f :=
(hf.antitone_on _).quasiconvex_on convex_univ

lemma antitone.quasiconcave_on (hf : antitone f) : quasiconcave_on ğ•œ univ f :=
(hf.antitone_on _).quasiconcave_on convex_univ

lemma antitone.quasilinear_on (hf : antitone f) : quasilinear_on ğ•œ univ f :=
âŸ¨hf.quasiconvex_on, hf.quasiconcave_onâŸ©

end linear_ordered_add_comm_monoid
end ordered_semiring
