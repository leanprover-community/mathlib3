/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov, YaÃ«l Dillies
-/
import algebra.order.invertible
import algebra.order.smul
import linear_algebra.affine_space.midpoint
import linear_algebra.ray
import tactic.positivity

/-!
# Segments in vector spaces

In a ğ•œ-vector space, we define the following objects and properties.
* `segment ğ•œ x y`: Closed segment joining `x` and `y`.
* `open_segment ğ•œ x y`: Open segment joining `x` and `y`.

## Notations

We provide the following notation:
* `[x -[ğ•œ] y] = segment ğ•œ x y` in locale `convex`

## TODO

Generalize all this file to affine spaces.

Should we rename `segment` and `open_segment` to `convex.Icc` and `convex.Ioo`? Should we also
define `clopen_segment`/`convex.Ico`/`convex.Ioc`?
-/

variables {ğ•œ E F G Î¹ : Type*} {Ï€ : Î¹ â†’ Type*}

open function set
open_locale pointwise

section ordered_semiring
variables [ordered_semiring ğ•œ] [add_comm_monoid E]

section has_smul
variables (ğ•œ) [has_smul ğ•œ E] {s : set E} {x y : E}

/-- Segments in a vector space. -/
def segment (x y : E) : set E :=
{z : E | âˆƒ (a b : ğ•œ) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1), a â€¢ x + b â€¢ y = z}

/-- Open segment in a vector space. Note that `open_segment ğ•œ x x = {x}` instead of being `âˆ…` when
the base semiring has some element between `0` and `1`. -/
def open_segment (x y : E) : set E :=
{z : E | âˆƒ (a b : ğ•œ) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1), a â€¢ x + b â€¢ y = z}

localized "notation (name := segment) `[` x ` -[` ğ•œ `] ` y `]` := segment ğ•œ x y" in convex

lemma segment_eq_imageâ‚‚ (x y : E) :
  [x -[ğ•œ] y] = (Î» p : ğ•œ Ã— ğ•œ, p.1 â€¢ x + p.2 â€¢ y) '' {p | 0 â‰¤ p.1 âˆ§ 0 â‰¤ p.2 âˆ§ p.1 + p.2 = 1} :=
by simp only [segment, image, prod.exists, mem_set_of_eq, exists_prop, and_assoc]

lemma open_segment_eq_imageâ‚‚ (x y : E) :
  open_segment ğ•œ x y =
    (Î» p : ğ•œ Ã— ğ•œ, p.1 â€¢ x + p.2 â€¢ y) '' {p | 0 < p.1 âˆ§ 0 < p.2 âˆ§ p.1 + p.2 = 1} :=
by simp only [open_segment, image, prod.exists, mem_set_of_eq, exists_prop, and_assoc]

lemma segment_symm (x y : E) : [x -[ğ•œ] y] = [y -[ğ•œ] x] :=
set.ext $ Î» z,
âŸ¨Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
  Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©

lemma open_segment_symm (x y : E) : open_segment ğ•œ x y = open_segment ğ•œ y x :=
set.ext $ Î» z,
âŸ¨Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
  Î» âŸ¨a, b, ha, hb, hab, HâŸ©, âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©

lemma open_segment_subset_segment (x y : E) : open_segment ğ•œ x y âŠ† [x -[ğ•œ] y] :=
Î» z âŸ¨a, b, ha, hb, hab, hzâŸ©, âŸ¨a, b, ha.le, hb.le, hab, hzâŸ©

lemma segment_subset_iff :
  [x -[ğ•œ] y] âŠ† s â†” âˆ€ a b : ğ•œ, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
âŸ¨Î» H a b ha hb hab, H âŸ¨a, b, ha, hb, hab, rflâŸ©,
  Î» H z âŸ¨a, b, ha, hb, hab, hzâŸ©, hz â–¸ H a b ha hb habâŸ©

lemma open_segment_subset_iff :
  open_segment ğ•œ x y âŠ† s â†” âˆ€ a b : ğ•œ, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
âŸ¨Î» H a b ha hb hab, H âŸ¨a, b, ha, hb, hab, rflâŸ©,
  Î» H z âŸ¨a, b, ha, hb, hab, hzâŸ©, hz â–¸ H a b ha hb habâŸ©

end has_smul

open_locale convex

section mul_action_with_zero
variables (ğ•œ) [mul_action_with_zero ğ•œ E]

lemma left_mem_segment (x y : E) : x âˆˆ [x -[ğ•œ] y] :=
âŸ¨1, 0, zero_le_one, le_refl 0, add_zero 1, by rw [zero_smul, one_smul, add_zero]âŸ©

lemma right_mem_segment (x y : E) : y âˆˆ [x -[ğ•œ] y] := segment_symm ğ•œ y x â–¸ left_mem_segment ğ•œ y x

end mul_action_with_zero

section module
variables (ğ•œ) [module ğ•œ E] {s : set E} {x y z : E}

@[simp] lemma segment_same (x : E) : [x -[ğ•œ] x] = {x} :=
set.ext $ Î» z, âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
  by simpa only [(add_smul _ _ _).symm, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
  Î» h, mem_singleton_iff.1 h â–¸ left_mem_segment ğ•œ z zâŸ©

lemma insert_endpoints_open_segment (x y : E) :
  insert x (insert y (open_segment ğ•œ x y)) = [x -[ğ•œ] y] :=
begin
  simp only [subset_antisymm_iff, insert_subset, left_mem_segment, right_mem_segment,
    open_segment_subset_segment, true_and],
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  refine hb.eq_or_gt.imp _ (Î» hb', ha.eq_or_gt.imp _ $ Î» ha', _),
  { rintro rfl,
    rw [â† add_zero a, hab, one_smul, zero_smul, add_zero] },
  { rintro rfl,
    rw [â† zero_add b, hab, one_smul, zero_smul, zero_add] },
  { exact âŸ¨a, b, ha', hb', hab, rflâŸ© }
end

variables {ğ•œ}

lemma mem_open_segment_of_ne_left_right (hx : x â‰  z) (hy : y â‰  z) (hz : z âˆˆ [x -[ğ•œ] y]) :
  z âˆˆ open_segment ğ•œ x y :=
begin
  rw [â†insert_endpoints_open_segment] at hz,
  exact ((hz.resolve_left hx.symm).resolve_left hy.symm)
end

lemma open_segment_subset_iff_segment_subset (hx : x âˆˆ s) (hy : y âˆˆ s) :
  open_segment ğ•œ x y âŠ† s â†” [x -[ğ•œ] y] âŠ† s :=
by simp only [â†insert_endpoints_open_segment, insert_subset, *, true_and]

end module
end ordered_semiring

open_locale convex

section ordered_ring
variables (ğ•œ) [ordered_ring ğ•œ] [add_comm_group E] [add_comm_group F] [add_comm_group G] [module ğ•œ E]
  [module ğ•œ F]

section densely_ordered
variables [nontrivial ğ•œ] [densely_ordered ğ•œ]

@[simp] lemma open_segment_same (x : E) : open_segment ğ•œ x x = {x} :=
set.ext $ Î» z, âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
  by simpa only [â†add_smul, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
  Î» (h : z = x), begin
    obtain âŸ¨a, haâ‚€, haâ‚âŸ© := densely_ordered.dense (0 : ğ•œ) 1 zero_lt_one,
    refine âŸ¨a, 1 - a, haâ‚€, sub_pos_of_lt haâ‚, add_sub_cancel'_right _ _, _âŸ©,
    rw [â†add_smul, add_sub_cancel'_right, one_smul, h],
  endâŸ©

end densely_ordered

lemma segment_eq_image (x y : E) : [x -[ğ•œ] y] = (Î» Î¸ : ğ•œ, (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Icc (0 : ğ•œ) 1 :=
set.ext $ Î» z,
  âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
    âŸ¨b, âŸ¨hb, hab â–¸ le_add_of_nonneg_left haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel]âŸ©,
    Î» âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ©, âŸ¨1-Î¸, Î¸, sub_nonneg.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©

lemma open_segment_eq_image (x y : E) :
  open_segment ğ•œ x y = (Î» (Î¸ : ğ•œ), (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Ioo (0 : ğ•œ) 1 :=
set.ext $ Î» z,
  âŸ¨Î» âŸ¨a, b, ha, hb, hab, hzâŸ©,
    âŸ¨b, âŸ¨hb, hab â–¸ lt_add_of_pos_left _ haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel]âŸ©,
    Î» âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ©, âŸ¨1 - Î¸, Î¸, sub_pos.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©

lemma segment_eq_image' (x y : E) : [x -[ğ•œ] y] = (Î» (Î¸ : ğ•œ), x + Î¸ â€¢ (y - x)) '' Icc (0 : ğ•œ) 1 :=
by { convert segment_eq_image ğ•œ x y, ext Î¸, simp only [smul_sub, sub_smul, one_smul], abel }

lemma open_segment_eq_image' (x y : E) :
  open_segment ğ•œ x y = (Î» (Î¸ : ğ•œ), x + Î¸ â€¢ (y - x)) '' Ioo (0 : ğ•œ) 1 :=
by { convert open_segment_eq_image ğ•œ x y, ext Î¸, simp only [smul_sub, sub_smul, one_smul], abel }

lemma segment_eq_image_line_map (x y : E) : [x -[ğ•œ] y] = affine_map.line_map x y '' Icc (0 : ğ•œ) 1 :=
by { convert segment_eq_image ğ•œ x y, ext, exact affine_map.line_map_apply_module _ _ _ }

lemma open_segment_eq_image_line_map (x y : E) :
  open_segment ğ•œ x y = affine_map.line_map x y '' Ioo (0 : ğ•œ) 1 :=
by { convert open_segment_eq_image ğ•œ x y, ext, exact affine_map.line_map_apply_module _ _ _ }

@[simp] lemma image_segment (f : E â†’áµƒ[ğ•œ] F) (a b : E) : f '' [a -[ğ•œ] b] = [f a -[ğ•œ] f b] :=
set.ext $ Î» x, by simp_rw [segment_eq_image_line_map, mem_image, exists_exists_and_eq_and,
  affine_map.apply_line_map]

@[simp] lemma image_open_segment (f : E â†’áµƒ[ğ•œ] F) (a b : E) :
  f '' open_segment ğ•œ a b = open_segment ğ•œ (f a) (f b) :=
set.ext $ Î» x, by simp_rw [open_segment_eq_image_line_map, mem_image, exists_exists_and_eq_and,
  affine_map.apply_line_map]

@[simp] lemma vadd_segment [add_torsor G E] [vadd_comm_class G E E] (a : G) (b c : E) :
  a +áµ¥ [b -[ğ•œ] c] = [a +áµ¥ b -[ğ•œ] a +áµ¥ c] :=
image_segment ğ•œ âŸ¨_, linear_map.id, Î» _ _, vadd_comm _ _ _âŸ© b c

@[simp] lemma vadd_open_segment [add_torsor G E] [vadd_comm_class G E E] (a : G) (b c : E) :
  a +áµ¥ open_segment ğ•œ b c = open_segment ğ•œ (a +áµ¥ b) (a +áµ¥ c) :=
image_open_segment ğ•œ âŸ¨_, linear_map.id, Î» _ _, vadd_comm _ _ _âŸ© b c

@[simp] lemma mem_segment_translate (a : E) {x b c} : a + x âˆˆ [a + b -[ğ•œ] a + c] â†” x âˆˆ [b -[ğ•œ] c] :=
by simp_rw [â†vadd_eq_add, â†vadd_segment, vadd_mem_vadd_set_iff]

@[simp] lemma mem_open_segment_translate (a : E) {x b c : E} :
  a + x âˆˆ open_segment ğ•œ (a + b) (a + c) â†” x âˆˆ open_segment ğ•œ b c :=
by simp_rw [â†vadd_eq_add, â†vadd_open_segment, vadd_mem_vadd_set_iff]

lemma segment_translate_preimage (a b c : E) : (Î» x, a + x) â»Â¹' [a + b -[ğ•œ] a + c] = [b -[ğ•œ] c] :=
set.ext $ Î» x, mem_segment_translate ğ•œ a

lemma open_segment_translate_preimage (a b c : E) :
  (Î» x, a + x) â»Â¹' open_segment ğ•œ (a + b) (a + c) = open_segment ğ•œ b c :=
set.ext $ Î» x, mem_open_segment_translate ğ•œ a

lemma segment_translate_image (a b c : E) : (Î» x, a + x) '' [b -[ğ•œ] c] = [a + b -[ğ•œ] a + c] :=
segment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ $ add_left_surjective a

lemma open_segment_translate_image (a b c : E) :
  (Î» x, a + x) '' open_segment ğ•œ b c = open_segment ğ•œ (a + b) (a + c) :=
open_segment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ $ add_left_surjective a

end ordered_ring

lemma same_ray_of_mem_segment [strict_ordered_comm_ring ğ•œ] [add_comm_group E] [module ğ•œ E]
  {x y z : E} (h : x âˆˆ [y -[ğ•œ] z]) : same_ray ğ•œ (x - y) (z - x) :=
begin
  rw segment_eq_image' at h,
  rcases h with âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, rflâŸ©,
  simpa only [add_sub_cancel', â†sub_sub, sub_smul, one_smul]
    using (same_ray_nonneg_smul_left (z - y) hÎ¸â‚€).nonneg_smul_right (sub_nonneg.2 hÎ¸â‚)
end

section linear_ordered_ring
variables [linear_ordered_ring ğ•œ] [add_comm_group E] [module ğ•œ E] {x y : E}

lemma midpoint_mem_segment [invertible (2 : ğ•œ)] (x y : E) : midpoint ğ•œ x y âˆˆ [x -[ğ•œ] y] :=
begin
  rw segment_eq_image_line_map,
  exact âŸ¨â…Ÿ2, âŸ¨inv_of_nonneg.mpr zero_le_two, inv_of_le_one one_le_twoâŸ©, rflâŸ©,
end

lemma mem_segment_sub_add [invertible (2 : ğ•œ)] (x y : E) : x âˆˆ [x - y -[ğ•œ] x + y] :=
by { convert @midpoint_mem_segment ğ•œ _ _ _ _ _ _ _, rw midpoint_sub_add }

lemma mem_segment_add_sub [invertible (2 : ğ•œ)] (x y : E) : x âˆˆ [x + y -[ğ•œ] x - y] :=
by { convert @midpoint_mem_segment ğ•œ _ _ _ _ _ _ _, rw midpoint_add_sub }

@[simp] lemma left_mem_open_segment_iff [densely_ordered ğ•œ] [no_zero_smul_divisors ğ•œ E] :
  x âˆˆ open_segment ğ•œ x y â†” x = y :=
begin
  split,
  { rintro âŸ¨a, b, ha, hb, hab, hxâŸ©,
    refine smul_right_injective _ hb.ne' ((add_right_inj (a â€¢ x)).1 _),
    rw [hx, â†add_smul, hab, one_smul] },
  { rintro rfl,
    rw open_segment_same,
    exact mem_singleton _ }
end

@[simp] lemma right_mem_open_segment_iff [densely_ordered ğ•œ] [no_zero_smul_divisors ğ•œ E] :
  y âˆˆ open_segment ğ•œ x y â†” x = y :=
by rw [open_segment_symm, left_mem_open_segment_iff, eq_comm]

end linear_ordered_ring

section linear_ordered_semifield
variables [linear_ordered_semifield ğ•œ] [add_comm_group E] [module ğ•œ E] {x y z : E}

lemma mem_segment_iff_div : x âˆˆ [y -[ğ•œ] z] â†”
  âˆƒ a b : ğ•œ, 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ 0 < a + b âˆ§ (a / (a + b)) â€¢ y + (b / (a + b)) â€¢ z = x :=
begin
  split,
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    use [a, b, ha, hb],
    simp * },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    refine âŸ¨a / (a + b), b / (a + b), div_nonneg ha hab.le, div_nonneg hb hab.le, _, rflâŸ©,
    rw [â†add_div, div_self hab.ne'] }
end

lemma mem_open_segment_iff_div : x âˆˆ open_segment ğ•œ y z â†”
  âˆƒ a b : ğ•œ, 0 < a âˆ§ 0 < b âˆ§ (a / (a + b)) â€¢ y + (b / (a + b)) â€¢ z = x :=
begin
  split,
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    use [a, b, ha, hb],
    rw [hab, div_one, div_one] },
  { rintro âŸ¨a, b, ha, hb, rflâŸ©,
    have hab : 0 < a + b := by positivity,
    refine âŸ¨a / (a + b), b / (a + b), by positivity, by positivity, _, rflâŸ©,
    rw [â†add_div, div_self hab.ne'] }
end

end linear_ordered_semifield

section linear_ordered_field
variables [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E] {x y z : E}

lemma mem_segment_iff_same_ray : x âˆˆ [y -[ğ•œ] z] â†” same_ray ğ•œ (x - y) (z - x) :=
begin
  refine âŸ¨same_ray_of_mem_segment, Î» h, _âŸ©,
  rcases h.exists_eq_smul_add with âŸ¨a, b, ha, hb, hab, hxy, hzxâŸ©,
  rw [add_comm, sub_add_sub_cancel] at hxy hzx,
  rw [â†mem_segment_translate _ (-x), neg_add_self],
  refine âŸ¨b, a, hb, ha, add_comm a b â–¸ hab, _âŸ©,
  rw [â†sub_eq_neg_add, â†neg_sub, hxy, â†sub_eq_neg_add, hzx, smul_neg, smul_comm, neg_add_self]
end

open affine_map

/-- If `z = line_map x y c` is a point on the line passing through `x` and `y`, then the open
segment `open_segment ğ•œ x y` is included in the union of the open segments `open_segment ğ•œ x z`,
`open_segment ğ•œ z y`, and the point `z`. Informally, `(x, y) âŠ† {z} âˆª (x, z) âˆª (z, y)`. -/
lemma open_segment_subset_union (x y : E) {z : E} (hz : z âˆˆ range (line_map x y : ğ•œ â†’ E)) :
  open_segment ğ•œ x y âŠ† insert z (open_segment ğ•œ x z âˆª open_segment ğ•œ z y) :=
begin
  rcases hz with âŸ¨c, rflâŸ©,
  simp only [open_segment_eq_image_line_map, â† maps_to'],
  rintro a âŸ¨hâ‚€, hâ‚âŸ©,
  rcases lt_trichotomy a c with hac|rfl|hca,
  { right, left,
    have hc : 0 < c, from hâ‚€.trans hac,
    refine âŸ¨a / c, âŸ¨div_pos hâ‚€ hc, (div_lt_one hc).2 hacâŸ©, _âŸ©,
    simp only [â† homothety_eq_line_map, â† homothety_mul_apply, div_mul_cancel _ hc.ne'] },
  { left, refl },
  { right, right,
    have hc : 0 < 1 - c, from sub_pos.2 (hca.trans hâ‚),
    simp only [â† line_map_apply_one_sub y],
    refine âŸ¨(a - c) / (1 - c), âŸ¨div_pos (sub_pos.2 hca) hc,
      (div_lt_one hc).2 $ sub_lt_sub_right hâ‚ _âŸ©, _âŸ©,
    simp only [â† homothety_eq_line_map, â† homothety_mul_apply, sub_mul, one_mul,
      div_mul_cancel _ hc.ne', sub_sub_sub_cancel_right] }
end

end linear_ordered_field

/-!
#### Segments in an ordered space

Relates `segment`, `open_segment` and `set.Icc`, `set.Ico`, `set.Ioc`, `set.Ioo`
-/
section ordered_semiring
variables [ordered_semiring ğ•œ]

section ordered_add_comm_monoid
variables [ordered_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E] {x y : E}

lemma segment_subset_Icc (h : x â‰¤ y) : [x -[ğ•œ] y] âŠ† Icc x y :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  split,
  calc
    x   = a â€¢ x + b â€¢ x :(convex.combo_self hab _).symm
    ... â‰¤ a â€¢ x + b â€¢ y : add_le_add_left (smul_le_smul_of_nonneg h hb) _,
  calc
    a â€¢ x + b â€¢ y
        â‰¤ a â€¢ y + b â€¢ y : add_le_add_right (smul_le_smul_of_nonneg h ha) _
    ... = y : convex.combo_self hab _,
end

end ordered_add_comm_monoid

section ordered_cancel_add_comm_monoid
variables [ordered_cancel_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E] {x y : E}

lemma open_segment_subset_Ioo (h : x < y) : open_segment ğ•œ x y âŠ† Ioo x y :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©,
  split,
  calc
    x   = a â€¢ x + b â€¢ x : (convex.combo_self hab _).symm
    ... < a â€¢ x + b â€¢ y : add_lt_add_left (smul_lt_smul_of_pos h hb) _,
  calc
    a â€¢ x + b â€¢ y
        < a â€¢ y + b â€¢ y : add_lt_add_right (smul_lt_smul_of_pos h ha) _
    ... = y : convex.combo_self hab _,
end

end ordered_cancel_add_comm_monoid

section linear_ordered_add_comm_monoid
variables [linear_ordered_add_comm_monoid E] [module ğ•œ E] [ordered_smul ğ•œ E] {ğ•œ} {a b : ğ•œ}

lemma segment_subset_uIcc (x y : E) : [x -[ğ•œ] y] âŠ† uIcc x y :=
begin
  cases le_total x y,
  { rw uIcc_of_le h,
    exact segment_subset_Icc h },
  { rw [uIcc_of_ge h, segment_symm],
    exact segment_subset_Icc h }
end

lemma convex.min_le_combo (x y : E) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  min x y â‰¤ a â€¢ x + b â€¢ y :=
(segment_subset_uIcc x y âŸ¨_, _, ha, hb, hab, rflâŸ©).1

lemma convex.combo_le_max (x y : E) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  a â€¢ x + b â€¢ y â‰¤ max x y :=
(segment_subset_uIcc x y âŸ¨_, _, ha, hb, hab, rflâŸ©).2

end linear_ordered_add_comm_monoid
end ordered_semiring

section linear_ordered_field
variables [linear_ordered_field ğ•œ] {x y z : ğ•œ}

lemma Icc_subset_segment : Icc x y âŠ† [x -[ğ•œ] y] :=
begin
  rintro z âŸ¨hxz, hyzâŸ©,
  obtain rfl | h := (hxz.trans hyz).eq_or_lt,
  { rw segment_same,
    exact hyz.antisymm hxz },
  rw â†sub_nonneg at hxz hyz,
  rw â†sub_pos at h,
  refine âŸ¨(y - z) / (y - x), (z - x) / (y - x), div_nonneg hyz h.le, div_nonneg hxz h.le, _, _âŸ©,
  { rw [â†add_div, sub_add_sub_cancel, div_self h.ne'] },
  { rw [smul_eq_mul, smul_eq_mul, â†mul_div_right_comm, â†mul_div_right_comm, â†add_div,
      div_eq_iff h.ne', add_comm, sub_mul, sub_mul, mul_comm x, sub_add_sub_cancel, mul_sub] }
end

@[simp] lemma segment_eq_Icc (h : x â‰¤ y) : [x -[ğ•œ] y] = Icc x y :=
(segment_subset_Icc h).antisymm Icc_subset_segment

lemma Ioo_subset_open_segment : Ioo x y âŠ† open_segment ğ•œ x y :=
Î» z hz, mem_open_segment_of_ne_left_right hz.1.ne hz.2.ne' $ Icc_subset_segment $
  Ioo_subset_Icc_self hz

@[simp] lemma open_segment_eq_Ioo (h : x < y) : open_segment ğ•œ x y = Ioo x y :=
(open_segment_subset_Ioo h).antisymm Ioo_subset_open_segment

lemma segment_eq_Icc' (x y : ğ•œ) : [x -[ğ•œ] y] = Icc (min x y) (max x y) :=
begin
  cases le_total x y,
  { rw [segment_eq_Icc h, max_eq_right h, min_eq_left h] },
  { rw [segment_symm, segment_eq_Icc h, max_eq_left h, min_eq_right h] }
end

lemma open_segment_eq_Ioo' (hxy : x â‰  y) : open_segment ğ•œ x y = Ioo (min x y) (max x y) :=
begin
  cases hxy.lt_or_lt,
  { rw [open_segment_eq_Ioo h, max_eq_right h.le, min_eq_left h.le] },
  { rw [open_segment_symm, open_segment_eq_Ioo h, max_eq_left h.le, min_eq_right h.le] }
end

lemma segment_eq_uIcc (x y : ğ•œ) : [x -[ğ•œ] y] = uIcc x y := segment_eq_Icc' _ _

/-- A point is in an `Icc` iff it can be expressed as a convex combination of the endpoints. -/
lemma convex.mem_Icc (h : x â‰¤ y) :
  z âˆˆ Icc x y â†” âˆƒ a b, 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
by { rw â†segment_eq_Icc h, simp_rw [â†exists_prop], refl }

/-- A point is in an `Ioo` iff it can be expressed as a strict convex combination of the endpoints.
-/
lemma convex.mem_Ioo (h : x < y) :
  z âˆˆ Ioo x y â†” âˆƒ a b, 0 < a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
by { rw â†open_segment_eq_Ioo h, simp_rw [â†exists_prop], refl }

/-- A point is in an `Ioc` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
lemma convex.mem_Ioc (h : x < y) :
  z âˆˆ Ioc x y â†” âˆƒ a b, 0 â‰¤ a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  refine âŸ¨Î» hz, _, _âŸ©,
  { obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (convex.mem_Icc h.le).1 (Ioc_subset_Icc_self hz),
    obtain rfl | hb' := hb.eq_or_lt,
    { rw add_zero at hab,
      rw [hab, one_mul, zero_mul, add_zero] at hz,
      exact (hz.1.ne rfl).elim },
    { exact âŸ¨a, b, ha, hb', hab, rflâŸ© } },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    obtain rfl | ha' := ha.eq_or_lt,
    { rw zero_add at hab,
      rwa [hab, one_mul, zero_mul, zero_add, right_mem_Ioc] },
    { exact Ioo_subset_Ioc_self ((convex.mem_Ioo h).2 âŸ¨a, b, ha', hb, hab, rflâŸ©) } }
end

/-- A point is in an `Ico` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
lemma convex.mem_Ico (h : x < y) :
  z âˆˆ Ico x y â†” âˆƒ a b, 0 < a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z :=
begin
  refine âŸ¨Î» hz, _, _âŸ©,
  { obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (convex.mem_Icc h.le).1 (Ico_subset_Icc_self hz),
    obtain rfl | ha' := ha.eq_or_lt,
    { rw zero_add at hab,
      rw [hab, one_mul, zero_mul, zero_add] at hz,
      exact (hz.2.ne rfl).elim },
    { exact âŸ¨a, b, ha', hb, hab, rflâŸ© } },
  { rintro âŸ¨a, b, ha, hb, hab, rflâŸ©,
    obtain rfl | hb' := hb.eq_or_lt,
    { rw add_zero at hab,
      rwa [hab, one_mul, zero_mul, add_zero, left_mem_Ico] },
    { exact Ioo_subset_Ico_self ((convex.mem_Ioo h).2 âŸ¨a, b, ha, hb', hab, rflâŸ©) } }
end

end linear_ordered_field

namespace prod
variables [ordered_semiring ğ•œ] [add_comm_monoid E] [add_comm_monoid F] [module ğ•œ E] [module ğ•œ F]

lemma segment_subset (x y : E Ã— F) : segment ğ•œ x y âŠ† segment ğ•œ x.1 y.1 Ã—Ë¢ segment ğ•œ x.2 y.2 :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, hzâŸ©,
  exact âŸ¨âŸ¨a, b, ha, hb, hab, congr_arg prod.fst hzâŸ©, a, b, ha, hb, hab, congr_arg prod.snd hzâŸ©,
end

lemma open_segment_subset (x y : E Ã— F) :
  open_segment ğ•œ x y âŠ† open_segment ğ•œ x.1 y.1 Ã—Ë¢ open_segment ğ•œ x.2 y.2 :=
begin
  rintro z âŸ¨a, b, ha, hb, hab, hzâŸ©,
  exact âŸ¨âŸ¨a, b, ha, hb, hab, congr_arg prod.fst hzâŸ©, a, b, ha, hb, hab, congr_arg prod.snd hzâŸ©,
end

lemma image_mk_segment_left (xâ‚ xâ‚‚ : E) (y : F) :
  (Î» x, (x, y)) '' [xâ‚ -[ğ•œ] xâ‚‚] = [(xâ‚, y) -[ğ•œ] (xâ‚‚, y)] :=
begin
  ext âŸ¨x', y'âŸ©,
  simp_rw [set.mem_image, segment, set.mem_set_of, prod.smul_mk, prod.mk_add_mk,
    prod.mk.inj_iff, â†exists_and_distrib_right, @exists_comm E, exists_eq_left'],
  refine existsâ‚…_congr (Î» a b ha hb hab, _),
  rw convex.combo_self hab,
end

lemma image_mk_segment_right (x : E) (yâ‚ yâ‚‚ : F) :
  (Î» y, (x, y)) '' [yâ‚ -[ğ•œ] yâ‚‚] = [(x, yâ‚) -[ğ•œ] (x, yâ‚‚)] :=
begin
  ext âŸ¨x', y'âŸ©,
  simp_rw [set.mem_image, segment, set.mem_set_of, prod.smul_mk, prod.mk_add_mk,
    prod.mk.inj_iff, â†exists_and_distrib_right, @exists_comm F, exists_eq_left'],
  refine existsâ‚…_congr (Î» a b ha hb hab, _),
  rw convex.combo_self hab,
end

lemma image_mk_open_segment_left (xâ‚ xâ‚‚ : E) (y : F) :
  (Î» x, (x, y)) '' open_segment ğ•œ xâ‚ xâ‚‚ = open_segment ğ•œ (xâ‚, y) (xâ‚‚, y) :=
begin
  ext âŸ¨x', y'âŸ©,
  simp_rw [set.mem_image, open_segment, set.mem_set_of, prod.smul_mk, prod.mk_add_mk,
    prod.mk.inj_iff, â†exists_and_distrib_right, @exists_comm E, exists_eq_left'],
  refine existsâ‚…_congr (Î» a b ha hb hab, _),
  rw convex.combo_self hab,
end

@[simp] lemma image_mk_open_segment_right (x : E) (yâ‚ yâ‚‚ : F) :
  (Î» y, (x, y)) '' open_segment ğ•œ yâ‚ yâ‚‚ = open_segment ğ•œ (x, yâ‚) (x, yâ‚‚) :=
begin
  ext âŸ¨x', y'âŸ©,
  simp_rw [set.mem_image, open_segment, set.mem_set_of, prod.smul_mk, prod.mk_add_mk,
    prod.mk.inj_iff, â†exists_and_distrib_right, @exists_comm F, exists_eq_left'],
  refine existsâ‚…_congr (Î» a b ha hb hab, _),
  rw convex.combo_self hab,
end

end prod

namespace pi
variables [ordered_semiring ğ•œ] [Î  i, add_comm_monoid (Ï€ i)] [Î  i, module ğ•œ (Ï€ i)] {s : set Î¹}

lemma segment_subset (x y : Î  i, Ï€ i) : segment ğ•œ x y âŠ† s.pi (Î» i, segment ğ•œ (x i) (y i)) :=
by { rintro z âŸ¨a, b, ha, hb, hab, hzâŸ© i -, exact âŸ¨a, b, ha, hb, hab, congr_fun hz iâŸ© }

lemma open_segment_subset (x y : Î  i, Ï€ i) :
  open_segment ğ•œ x y âŠ† s.pi (Î» i, open_segment ğ•œ (x i) (y i)) :=
by { rintro z âŸ¨a, b, ha, hb, hab, hzâŸ© i -, exact âŸ¨a, b, ha, hb, hab, congr_fun hz iâŸ© }

variables [decidable_eq Î¹]

lemma image_update_segment (i : Î¹) (xâ‚ xâ‚‚ : Ï€ i) (y : Î  i, Ï€ i) :
  update y i '' [xâ‚ -[ğ•œ] xâ‚‚] = [update y i xâ‚ -[ğ•œ] update y i xâ‚‚] :=
begin
  ext z,
  simp_rw [set.mem_image, segment, set.mem_set_of, â†update_smul, â†update_add, update_eq_iff,
    â†exists_and_distrib_right, @exists_comm (Ï€ i), exists_eq_left'],
  refine existsâ‚…_congr (Î» a b ha hb hab, _),
  rw convex.combo_self hab,
end

lemma image_update_open_segment (i : Î¹) (xâ‚ xâ‚‚ : Ï€ i) (y : Î  i, Ï€ i) :
  update y i '' open_segment ğ•œ xâ‚ xâ‚‚ = open_segment ğ•œ (update y i xâ‚) (update y i xâ‚‚) :=
begin
  ext z,
  simp_rw [set.mem_image, open_segment, set.mem_set_of, â†update_smul, â†update_add, update_eq_iff,
    â†exists_and_distrib_right, @exists_comm (Ï€ i), exists_eq_left'],
  refine existsâ‚…_congr (Î» a b ha hb hab, _),
  rw convex.combo_self hab,
end

end pi
