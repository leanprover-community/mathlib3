/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import analysis.convex.hull
import linear_algebra.affine_space.independent

/-!
# Simplicial complexes

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file, we define simplicial complexes in `ğ•œ`-modules. A simplicial complex is a collection
of simplices closed by inclusion (of vertices) and intersection (of underlying sets).

We model them by a downward-closed set of affine independent finite sets whose convex hulls "glue
nicely", each finite set and its convex hull corresponding respectively to the vertices and the
underlying set of a simplex.

## Main declarations

* `simplicial_complex ğ•œ E`: A simplicial complex in the `ğ•œ`-module `E`.
* `simplicial_complex.vertices`: The zero dimensional faces of a simplicial complex.
* `simplicial_complex.facets`: The maximal faces of a simplicial complex.

## Notation

`s âˆˆ K` means that `s` is a face of `K`.

`K â‰¤ L` means that the faces of `K` are faces of `L`.

## Implementation notes

"glue nicely" usually means that the intersection of two faces (as sets in the ambient space) is a
face. Given that we store the vertices, not the faces, this would be a bit awkward to spell.
Instead, `simplicial_complex.inter_subset_convex_hull` is an equivalent condition which works on the
vertices.

## TODO

Simplicial complexes can be generalized to affine spaces once `convex_hull` has been ported.
-/

open finset set

variables (ğ•œ E : Type*) {Î¹ : Type*} [ordered_ring ğ•œ] [add_comm_group E] [module ğ•œ E]

namespace geometry

/-- A simplicial complex in a `ğ•œ`-module is a collection of simplices which glue nicely together.
Note that the textbook meaning of "glue nicely" is given in
`geometry.simplicial_complex.disjoint_or_exists_inter_eq_convex_hull`. It is mostly useless, as
`geometry.simplicial_complex.convex_hull_inter_convex_hull` is enough for all purposes. -/
-- TODO: update to new binder order? not sure what binder order is correct for `down_closed`.
@[ext] structure simplicial_complex :=
(faces : set (finset E))
(not_empty_mem : âˆ… âˆ‰ faces)
(indep : âˆ€ {s}, s âˆˆ faces â†’ affine_independent ğ•œ (coe : (s : set E) â†’ E))
(down_closed : âˆ€ {s t}, s âˆˆ faces â†’ t âŠ† s â†’ t â‰  âˆ… â†’ t âˆˆ faces)
(inter_subset_convex_hull : âˆ€ {s t}, s âˆˆ faces â†’ t âˆˆ faces â†’
  convex_hull ğ•œ â†‘s âˆ© convex_hull ğ•œ â†‘t âŠ† convex_hull ğ•œ (s âˆ© t : set E))

namespace simplicial_complex
variables {ğ•œ E} {K : simplicial_complex ğ•œ E} {s t : finset E} {x : E}

/-- A `finset` belongs to a `simplicial_complex` if it's a face of it. -/
instance : has_mem (finset E) (simplicial_complex ğ•œ E) := âŸ¨Î» s K, s âˆˆ K.facesâŸ©

/-- The underlying space of a simplicial complex is the union of its faces. -/
def space (K : simplicial_complex ğ•œ E) : set E := â‹ƒ s âˆˆ K.faces, convex_hull ğ•œ (s : set E)

lemma mem_space_iff : x âˆˆ K.space â†” âˆƒ s âˆˆ K.faces, x âˆˆ convex_hull ğ•œ (s : set E) := mem_Unionâ‚‚

lemma convex_hull_subset_space (hs : s âˆˆ K.faces) : convex_hull ğ•œ â†‘s âŠ† K.space :=
subset_bUnion_of_mem hs

protected lemma subset_space (hs : s âˆˆ K.faces) : (s : set E) âŠ† K.space :=
(subset_convex_hull ğ•œ _).trans $ convex_hull_subset_space hs

lemma convex_hull_inter_convex_hull (hs : s âˆˆ K.faces) (ht : t âˆˆ K.faces) :
  convex_hull ğ•œ â†‘s âˆ© convex_hull ğ•œ â†‘t = convex_hull ğ•œ (s âˆ© t : set E) :=
(K.inter_subset_convex_hull hs ht).antisymm $ subset_inter
  (convex_hull_mono $ set.inter_subset_left _ _) $ convex_hull_mono $ set.inter_subset_right _ _

/-- The conclusion is the usual meaning of "glue nicely" in textbooks. It turns out to be quite
unusable, as it's about faces as sets in space rather than simplices. Further,  additional structure
on `ğ•œ` means the only choice of `u` is `s âˆ© t` (but it's hard to prove). -/
lemma disjoint_or_exists_inter_eq_convex_hull (hs : s âˆˆ K.faces) (ht : t âˆˆ K.faces) :
  disjoint (convex_hull ğ•œ (s : set E)) (convex_hull ğ•œ â†‘t) âˆ¨
  âˆƒ u âˆˆ K.faces, convex_hull ğ•œ (s : set E) âˆ© convex_hull ğ•œ â†‘t = convex_hull ğ•œ â†‘u :=
begin
  classical,
  by_contra' h,
  refine h.2 (s âˆ© t) (K.down_closed hs (inter_subset_left _ _) $ Î» hst, h.1 $
    disjoint_iff_inf_le.mpr $ (K.inter_subset_convex_hull hs ht).trans _) _,
  { rw [â†coe_inter, hst, coe_empty, convex_hull_empty],
    refl },
  { rw [coe_inter, convex_hull_inter_convex_hull hs ht] }
end

/-- Construct a simplicial complex by removing the empty face for you. -/
@[simps] def of_erase
  (faces : set (finset E))
  (indep : âˆ€ s âˆˆ faces, affine_independent ğ•œ (coe : (s : set E) â†’ E))
  (down_closed : âˆ€ s âˆˆ faces, âˆ€ t âŠ† s, t âˆˆ faces)
  (inter_subset_convex_hull : âˆ€ s t âˆˆ faces,
    convex_hull ğ•œ â†‘s âˆ© convex_hull ğ•œ â†‘t âŠ† convex_hull ğ•œ (s âˆ© t : set E)) :
  simplicial_complex ğ•œ E :=
{ faces := faces \ {âˆ…},
  not_empty_mem := Î» h, h.2 (mem_singleton _),
  indep := Î» s hs, indep _ hs.1,
  down_closed := Î» s t hs hts ht, âŸ¨down_closed _ hs.1 _ hts, htâŸ©,
  inter_subset_convex_hull := Î» s t hs ht, inter_subset_convex_hull _ hs.1 _ ht.1 }

/-- Construct a simplicial complex as a subset of a given simplicial complex. -/
@[simps] def of_subcomplex (K : simplicial_complex ğ•œ E)
  (faces : set (finset E))
  (subset : faces âŠ† K.faces)
  (down_closed : âˆ€ {s t}, s âˆˆ faces â†’ t âŠ† s â†’ t âˆˆ faces) :
  simplicial_complex ğ•œ E :=
{ faces := faces,
  not_empty_mem := Î» h, K.not_empty_mem (subset h),
  indep := Î» s hs, K.indep (subset hs),
  down_closed := Î» s t hs hts _, down_closed hs hts,
  inter_subset_convex_hull := Î» s t hs ht, K.inter_subset_convex_hull (subset hs) (subset ht) }

/-! ### Vertices -/

/-- The vertices of a simplicial complex are its zero dimensional faces. -/
def vertices (K : simplicial_complex ğ•œ E) : set E := {x | {x} âˆˆ K.faces}

lemma mem_vertices : x âˆˆ K.vertices â†” {x} âˆˆ K.faces := iff.rfl

lemma vertices_eq : K.vertices = â‹ƒ k âˆˆ K.faces, (k : set E) :=
begin
  ext x,
  refine âŸ¨Î» h, mem_bUnion h $ mem_coe.2 $ mem_singleton_self x, Î» h, _âŸ©,
  obtain âŸ¨s, hs, hxâŸ© := mem_Unionâ‚‚.1 h,
  exact K.down_closed hs (finset.singleton_subset_iff.2 $ mem_coe.1 hx) (singleton_ne_empty _),
end

lemma vertices_subset_space : K.vertices âŠ† K.space :=
vertices_eq.subset.trans $ Unionâ‚‚_mono $ Î» x hx, subset_convex_hull ğ•œ x

lemma vertex_mem_convex_hull_iff (hx : x âˆˆ K.vertices) (hs : s âˆˆ K.faces) :
  x âˆˆ convex_hull ğ•œ (s : set E) â†” x âˆˆ s :=
begin
  refine âŸ¨Î» h, _, Î» h, subset_convex_hull _ _ hâŸ©,
  classical,
  have h := K.inter_subset_convex_hull hx hs âŸ¨by simp, hâŸ©,
  by_contra H,
  rwa [â†coe_inter, finset.disjoint_iff_inter_eq_empty.1
    (finset.disjoint_singleton_right.2 H).symm, coe_empty, convex_hull_empty] at h,
end

/-- A face is a subset of another one iff its vertices are.  -/
lemma face_subset_face_iff (hs : s âˆˆ K.faces) (ht : t âˆˆ K.faces) :
  convex_hull ğ•œ (s : set E) âŠ† convex_hull ğ•œ â†‘t â†” s âŠ† t :=
âŸ¨Î» h x hxs, (vertex_mem_convex_hull_iff (K.down_closed hs (finset.singleton_subset_iff.2 hxs) $
  singleton_ne_empty _) ht).1 (h (subset_convex_hull ğ•œ â†‘s hxs)), convex_hull_monoâŸ©

/-! ### Facets -/

/-- A facet of a simplicial complex is a maximal face. -/
def facets (K : simplicial_complex ğ•œ E) : set (finset E) :=
{s âˆˆ K.faces | âˆ€ â¦ƒtâ¦„, t âˆˆ K.faces â†’ s âŠ† t â†’ s = t}

lemma mem_facets : s âˆˆ K.facets â†” s âˆˆ K.faces âˆ§ âˆ€ t âˆˆ K.faces, s âŠ† t â†’ s = t := mem_sep_iff

lemma facets_subset : K.facets âŠ† K.faces := Î» s hs, hs.1

lemma not_facet_iff_subface (hs : s âˆˆ K.faces) : (s âˆ‰ K.facets â†” âˆƒ t, t âˆˆ K.faces âˆ§ s âŠ‚ t) :=
begin
  refine âŸ¨Î» (hs' : Â¬ (_ âˆ§ _)), _, _âŸ©,
  { push_neg at hs',
    obtain âŸ¨t, htâŸ© := hs' hs,
    exact âŸ¨t, ht.1, âŸ¨ht.2.1, (Î» hts, ht.2.2 (subset.antisymm ht.2.1 hts))âŸ©âŸ© },
  { rintro âŸ¨t, htâŸ© âŸ¨hs, hs'âŸ©,
    have := hs' ht.1 ht.2.1,
    rw this at ht,
    exact ht.2.2 (subset.refl t) } -- `has_ssubset.ssubset.ne` would be handy here
end

/-!
### The semilattice of simplicial complexes

`K â‰¤ L` means that `K.faces âŠ† L.faces`.
-/

variables (ğ•œ E)

/-- The complex consisting of only the faces present in both of its arguments. -/
instance : has_inf (simplicial_complex ğ•œ E) :=
âŸ¨Î» K L, { faces := K.faces âˆ© L.faces,
  not_empty_mem := Î» h, K.not_empty_mem (set.inter_subset_left _ _ h),
  indep := Î» s hs, K.indep hs.1,
  down_closed := Î» s t hs hst ht, âŸ¨K.down_closed hs.1 hst ht, L.down_closed hs.2 hst htâŸ©,
  inter_subset_convex_hull := Î» s t hs ht, K.inter_subset_convex_hull hs.1 ht.1 }âŸ©

instance : semilattice_inf (simplicial_complex ğ•œ E) :=
{ inf := (âŠ“),
  inf_le_left := Î» K L s hs, hs.1,
  inf_le_right := Î» K L s hs, hs.2,
  le_inf := Î» K L M hKL hKM s hs, âŸ¨hKL hs, hKM hsâŸ©,
  .. (partial_order.lift faces $ Î» x y, ext _ _) }

instance : has_bot (simplicial_complex ğ•œ E) :=
âŸ¨{ faces := âˆ…,
  not_empty_mem := set.not_mem_empty âˆ…,
  indep := Î» s hs, (set.not_mem_empty _ hs).elim,
  down_closed := Î» s _ hs, (set.not_mem_empty _ hs).elim,
  inter_subset_convex_hull := Î» s _ hs, (set.not_mem_empty _ hs).elim }âŸ©

instance : order_bot (simplicial_complex ğ•œ E) :=
{ bot_le := Î» K, set.empty_subset _, .. simplicial_complex.has_bot ğ•œ E }

instance : inhabited (simplicial_complex ğ•œ E) := âŸ¨âŠ¥âŸ©

variables {ğ•œ E}

lemma faces_bot : (âŠ¥ : simplicial_complex ğ•œ E).faces = âˆ… := rfl

lemma space_bot : (âŠ¥ : simplicial_complex ğ•œ E).space = âˆ… := set.bUnion_empty _

lemma facets_bot : (âŠ¥ : simplicial_complex ğ•œ E).facets = âˆ… := eq_empty_of_subset_empty facets_subset

end simplicial_complex
end geometry
