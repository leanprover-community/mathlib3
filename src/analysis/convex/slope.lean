/-
Copyright (c) 2021 Yury Kudriashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudriashov, Malo JaffrÃ©
-/
import analysis.convex.function
import tactic.field_simp
import tactic.linarith

/-!
# Slopes of convex functions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file relates convexity/concavity of functions in a linearly ordered field and the monotonicity
of their slopes.

The main use is to show convexity/concavity from monotonicity of the derivative.
-/

variables {ğ•œ : Type*} [linear_ordered_field ğ•œ] {s : set ğ•œ} {f : ğ•œ â†’ ğ•œ}

/-- If `f : ğ•œ â†’ ğ•œ` is convex, then for any three points `x < y < z` the slope of the secant line of
`f` on `[x, y]` is less than the slope of the secant line of `f` on `[x, z]`. -/
lemma convex_on.slope_mono_adjacent (hf : convex_on ğ•œ s f)
  {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y) :=
begin
  have hxz := hxy.trans hyz,
  rw â†sub_pos at hxy hxz hyz,
  suffices : f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y),
  { ring_nf at this âŠ¢, linarith },
  set a := (z - y) / (z - x),
  set b := (y - x) / (z - x),
  have hy : a â€¢ x + b â€¢ z = y, by { field_simp, rw div_eq_iff; [ring, linarith] },
  have key, from
    hf.2 hx hz
      (show 0 â‰¤ a, by apply div_nonneg; linarith)
      (show 0 â‰¤ b, by apply div_nonneg; linarith)
      (show a + b = 1, by { field_simp, rw div_eq_iff; [ring, linarith] }),
  rw hy at key,
  replace key := mul_le_mul_of_nonneg_left key hxz.le,
  field_simp [hxy.ne', hyz.ne', hxz.ne', mul_comm (z - x) _] at key âŠ¢,
  rw div_le_div_right,
  { linarith },
  { nlinarith }
end

/-- If `f : ğ•œ â†’ ğ•œ` is concave, then for any three points `x < y < z` the slope of the secant line of
`f` on `[x, y]` is greater than the slope of the secant line of `f` on `[x, z]`. -/
lemma concave_on.slope_anti_adjacent (hf : concave_on ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s)
  (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x) :=
begin
  rw [â†neg_le_neg_iff, â†neg_sub_neg (f x), â†neg_sub_neg (f y)],
  simp_rw [â†pi.neg_apply, â†neg_div, neg_sub],
  exact convex_on.slope_mono_adjacent hf.neg hx hz hxy hyz,
end

/-- If `f : ğ•œ â†’ ğ•œ` is strictly convex, then for any three points `x < y < z` the slope of the
secant line of `f` on `[x, y]` is strictly less than the slope of the secant line of `f` on
`[x, z]`. -/
lemma strict_convex_on.slope_strict_mono_adjacent (hf : strict_convex_on ğ•œ s f)
  {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f y - f x) / (y - x) < (f z - f y) / (z - y) :=
begin
  have hxz := hxy.trans hyz,
  have hxz' := hxz.ne,
  rw â†sub_pos at hxy hxz hyz,
  suffices : f y / (y - x) + f y / (z - y) < f x / (y - x) + f z / (z - y),
  { ring_nf at this âŠ¢, linarith },
  set a := (z - y) / (z - x),
  set b := (y - x) / (z - x),
  have hy : a â€¢ x + b â€¢ z = y, by { field_simp, rw div_eq_iff; [ring, linarith] },
  have key, from
    hf.2 hx hz hxz' (div_pos hyz hxz) (div_pos hxy hxz)
      (show a + b = 1, by { field_simp, rw div_eq_iff; [ring, linarith] }),
  rw hy at key,
  replace key := mul_lt_mul_of_pos_left key hxz,
  field_simp [hxy.ne', hyz.ne', hxz.ne', mul_comm (z - x) _] at key âŠ¢,
  rw div_lt_div_right,
  { linarith },
  { nlinarith }
end

/-- If `f : ğ•œ â†’ ğ•œ` is strictly concave, then for any three points `x < y < z` the slope of the
secant line of `f` on `[x, y]` is strictly greater than the slope of the secant line of `f` on
`[x, z]`. -/
lemma strict_concave_on.slope_anti_adjacent (hf : strict_concave_on ğ•œ s f)
  {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
  (f z - f y) / (z - y) < (f y - f x) / (y - x) :=
begin
  rw [â†neg_lt_neg_iff, â†neg_sub_neg (f x), â†neg_sub_neg (f y)],
  simp_rw [â†pi.neg_apply, â†neg_div, neg_sub],
  exact strict_convex_on.slope_strict_mono_adjacent hf.neg hx hz hxy hyz,
end

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
less than the slope of the secant line of `f` on `[x, z]`, then `f` is convex. -/
lemma convex_on_of_slope_mono_adjacent (hs : convex ğ•œ s)
  (hf : âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)) :
  convex_on ğ•œ s f :=
linear_order.convex_on_of_lt hs $ Î» x hx z hz hxz a b ha hb hab,
begin
  let y := a * x + b * z,
  have hxy : x < y,
  { rw [â† one_mul x, â† hab, add_mul],
    exact add_lt_add_left ((mul_lt_mul_left hb).2 hxz) _ },
  have hyz : y < z,
  { rw [â† one_mul z, â† hab, add_mul],
    exact add_lt_add_right ((mul_lt_mul_left ha).2 hxz) _ },
  have : (f y - f x) * (z - y) â‰¤ (f z - f y) * (y - x),
    from (div_le_div_iff (sub_pos.2 hxy) (sub_pos.2 hyz)).1 (hf hx hz hxy hyz),
  have hxz : 0 < z - x, from sub_pos.2 (hxy.trans hyz),
  have ha : (z - y) / (z - x) = a,
  { rw [eq_comm, â† sub_eq_iff_eq_add'] at hab,
    simp_rw [div_eq_iff hxz.ne', y, â†hab], ring },
  have hb : (y - x) / (z - x) = b,
  { rw [eq_comm, â† sub_eq_iff_eq_add] at hab,
    simp_rw [div_eq_iff hxz.ne', y, â†hab], ring },
  rwa [sub_mul, sub_mul, sub_le_iff_le_add', â† add_sub_assoc, le_sub_iff_add_le, â† mul_add,
    sub_add_sub_cancel, â† le_div_iff hxz, add_div, mul_div_assoc, mul_div_assoc, mul_comm (f x),
    mul_comm (f z), ha, hb] at this,
end

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
greater than the slope of the secant line of `f` on `[x, z]`, then `f` is concave. -/
lemma concave_on_of_slope_anti_adjacent (hs : convex ğ•œ s)
  (hf : âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)) : concave_on ğ•œ s f :=
begin
  rw â†neg_convex_on_iff,
  refine convex_on_of_slope_mono_adjacent hs (Î» x y z hx hz hxy hyz, _),
  rw â†neg_le_neg_iff,
  simp_rw [â†neg_div, neg_sub, pi.neg_apply, neg_sub_neg],
  exact hf hx hz hxy hyz,
end

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
strictly less than the slope of the secant line of `f` on `[x, z]`, then `f` is strictly convex. -/
lemma strict_convex_on_of_slope_strict_mono_adjacent (hs : convex ğ•œ s)
  (hf : âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f y - f x) / (y - x) < (f z - f y) / (z - y)) :
  strict_convex_on ğ•œ s f :=
linear_order.strict_convex_on_of_lt hs $ Î» x hx z hz hxz a b ha hb hab,
begin
  let y := a * x + b * z,
  have hxy : x < y,
  { rw [â† one_mul x, â† hab, add_mul],
    exact add_lt_add_left ((mul_lt_mul_left hb).2 hxz) _ },
  have hyz : y < z,
  { rw [â† one_mul z, â† hab, add_mul],
    exact add_lt_add_right ((mul_lt_mul_left ha).2 hxz) _ },
  have : (f y - f x) * (z - y) < (f z - f y) * (y - x),
    from (div_lt_div_iff (sub_pos.2 hxy) (sub_pos.2 hyz)).1 (hf hx hz hxy hyz),
  have hxz : 0 < z - x, from sub_pos.2 (hxy.trans hyz),
  have ha : (z - y) / (z - x) = a,
  { rw [eq_comm, â† sub_eq_iff_eq_add'] at hab,
    simp_rw [div_eq_iff hxz.ne', y, â†hab], ring },
  have hb : (y - x) / (z - x) = b,
  { rw [eq_comm, â† sub_eq_iff_eq_add] at hab,
    simp_rw [div_eq_iff hxz.ne', y, â†hab], ring },
  rwa [sub_mul, sub_mul, sub_lt_iff_lt_add', â† add_sub_assoc, lt_sub_iff_add_lt, â† mul_add,
    sub_add_sub_cancel, â† lt_div_iff hxz, add_div, mul_div_assoc, mul_div_assoc, mul_comm (f x),
    mul_comm (f z), ha, hb] at this,
end

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
strictly greater than the slope of the secant line of `f` on `[x, z]`, then `f` is strictly concave.
-/
lemma strict_concave_on_of_slope_strict_anti_adjacent (hs : convex ğ•œ s)
  (hf : âˆ€ {x y z : ğ•œ}, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
    (f z - f y) / (z - y) < (f y - f x) / (y - x)) : strict_concave_on ğ•œ s f :=
begin
  rw â†neg_strict_convex_on_iff,
  refine strict_convex_on_of_slope_strict_mono_adjacent hs (Î» x y z hx hz hxy hyz, _),
  rw â†neg_lt_neg_iff,
  simp_rw [â†neg_div, neg_sub, pi.neg_apply, neg_sub_neg],
  exact hf hx hz hxy hyz,
end

/-- A function `f : ğ•œ â†’ ğ•œ` is convex iff for any three points `x < y < z` the slope of the secant
line of `f` on `[x, y]` is less than the slope of the secant line of `f` on `[x, z]`. -/
lemma convex_on_iff_slope_mono_adjacent :
  convex_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y z : ğ•œâ¦„, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
      (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y) :=
âŸ¨Î» h, âŸ¨h.1, Î» x y z, h.slope_mono_adjacentâŸ©, Î» h, convex_on_of_slope_mono_adjacent h.1 h.2âŸ©

/-- A function `f : ğ•œ â†’ ğ•œ` is concave iff for any three points `x < y < z` the slope of the secant
line of `f` on `[x, y]` is greater than the slope of the secant line of `f` on `[x, z]`. -/
lemma concave_on_iff_slope_anti_adjacent :
  concave_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y z : ğ•œâ¦„, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
      (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x) :=
âŸ¨Î» h, âŸ¨h.1, Î» x y z, h.slope_anti_adjacentâŸ©, Î» h, concave_on_of_slope_anti_adjacent h.1 h.2âŸ©

/-- A function `f : ğ•œ â†’ ğ•œ` is strictly convex iff for any three points `x < y < z` the slope of
the secant line of `f` on `[x, y]` is strictly less than the slope of the secant line of `f` on
`[x, z]`. -/
lemma strict_convex_on_iff_slope_strict_mono_adjacent :
  strict_convex_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y z : ğ•œâ¦„, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
      (f y - f x) / (y - x) < (f z - f y) / (z - y) :=
âŸ¨Î» h, âŸ¨h.1, Î» x y z, h.slope_strict_mono_adjacentâŸ©,
  Î» h, strict_convex_on_of_slope_strict_mono_adjacent h.1 h.2âŸ©

/-- A function `f : ğ•œ â†’ ğ•œ` is strictly concave iff for any three points `x < y < z` the slope of
the secant line of `f` on `[x, y]` is strictly greater than the slope of the secant line of `f` on
`[x, z]`. -/
lemma strict_concave_on_iff_slope_strict_anti_adjacent :
  strict_concave_on ğ•œ s f â†” convex ğ•œ s âˆ§
    âˆ€ â¦ƒx y z : ğ•œâ¦„, x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’
      (f z - f y) / (z - y) < (f y - f x) / (y - x) :=
âŸ¨Î» h, âŸ¨h.1, Î» x y z, h.slope_anti_adjacentâŸ©,
  Î» h, strict_concave_on_of_slope_strict_anti_adjacent h.1 h.2âŸ©

/-- If `f` is convex on a set `s` in a linearly ordered field, and `f x < f y` for two points
`x < y` in `s`, then `f` is strictly monotone on `s âˆ© [y, âˆ)`. -/
lemma convex_on.strict_mono_of_lt (hf : convex_on ğ•œ s f)
  {x y : ğ•œ} (hx : x âˆˆ s) (hxy : x < y) (hxy' : f x < f y) :
  strict_mono_on f (s âˆ© set.Ici y) :=
begin
  intros u hu v hv huv,
  have step1 : âˆ€ {z : ğ•œ}, z âˆˆ s âˆ© set.Ioi y â†’ f y < f z,
  { refine Î» z hz, hf.lt_right_of_left_lt hx hz.1 _ hxy',
    rw open_segment_eq_Ioo (hxy.trans hz.2),
    exact âŸ¨hxy, hz.2âŸ© },
  rcases eq_or_lt_of_le hu.2 with rfl | hu2,
  { exact step1 âŸ¨hv.1, huvâŸ© },
  { refine hf.lt_right_of_left_lt _ hv.1 _ (step1 âŸ¨hu.1, hu2âŸ©),
    { apply hf.1.segment_subset hx hu.1,
      rw segment_eq_Icc (hxy.le.trans hu.2),
      exact âŸ¨hxy.le, hu.2âŸ© },
    { rw open_segment_eq_Ioo (hu2.trans huv),
      exact âŸ¨hu2, huvâŸ© } },
end
