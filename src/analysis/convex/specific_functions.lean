/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, S√©bastien Gou√´zel
-/
import analysis.calculus.mean_value
import analysis.special_functions.pow

/-!
# Collection of convex functions

In this file we prove that the following functions are convex:

* `convex_on_exp` : the exponential function is convex on $(-‚àû, +‚àû)$;
* `convex_on_pow_of_even` : given an even natural number $n$, the function $f(x)=x^n$
  is convex on $(-‚àû, +‚àû)$;
* `convex_on_pow` : for a natural $n$, the function $f(x)=x^n$ is convex on $[0, +‚àû)$;
* `convex_on_fpow` : for an integer $m$, the function $f(x)=x^m$ is convex on $(0, +‚àû)$.
* `convex_on_rpow : ‚àÄ p : ‚Ñù, 1 ‚â§ p ‚Üí convex_on ùïú (Ici 0) (Œª x, x ^ p)`
* `concave_on_log_Ioi` and `concave_on_log_Iio`: log is concave on `Ioi 0` and `Iio 0` respectively.
-/

open real set
open_locale big_operators

/-- `exp` is convex on the whole real line -/
lemma convex_on_exp : convex_on ùïú univ exp :=
convex_on_univ_of_deriv2_nonneg differentiable_exp (by simp)
  (assume x, (iter_deriv_exp 2).symm ‚ñ∏ le_of_lt (exp_pos x))

/-- `x^n`, `n : ‚Ñï` is convex on the whole real line whenever `n` is even -/
lemma convex_on_pow_of_even {n : ‚Ñï} (hn : even n) : convex_on ùïú set.univ (Œª x : ‚Ñù, x^n) :=
begin
  apply convex_on_univ_of_deriv2_nonneg differentiable_pow,
  { simp only [deriv_pow', differentiable.mul, differentiable_const, differentiable_pow] },
  { intro x,
    rcases nat.even.sub_even hn (nat.even_bit0 1) with ‚ü®k, hk‚ü©,
    rw [iter_deriv_pow, finset.prod_range_cast_nat_sub, hk, pow_mul'],
    exact mul_nonneg (nat.cast_nonneg _) (pow_two_nonneg _) }
end

/-- `x^n`, `n : ‚Ñï` is convex on `[0, +‚àû)` for all `n` -/
lemma convex_on_pow (n : ‚Ñï) : convex_on ùïú (Ici 0) (Œª x : ‚Ñù, x^n) :=
begin
  apply convex_on_of_deriv2_nonneg (convex_Ici _) (continuous_pow n).continuous_on
    differentiable_on_pow,
  { simp only [deriv_pow'], exact differentiable_on_pow.const_mul _ },
  { intros x hx,
    rw [iter_deriv_pow, finset.prod_range_cast_nat_sub],
    exact mul_nonneg (nat.cast_nonneg _) (pow_nonneg (interior_subset hx) _) }
end

lemma finset.prod_nonneg_of_card_nonpos_even
  {Œ± Œ≤ : Type*} [linear_ordered_comm_ring Œ≤]
  {f : Œ± ‚Üí Œ≤} [decidable_pred (Œª x, f x ‚â§ 0)]
  {s : finset Œ±} (h0 : even (s.filter (Œª x, f x ‚â§ 0)).card) :
  0 ‚â§ ‚àè x in s, f x :=
calc 0 ‚â§ (‚àè x in s, ((if f x ‚â§ 0 then (-1:Œ≤) else 1) * f x)) :
  finset.prod_nonneg (Œª x _, by
    { split_ifs with hx hx, by simp [hx], simp at hx ‚ä¢, exact le_of_lt hx })
... = _ : by rw [finset.prod_mul_distrib, finset.prod_ite, finset.prod_const_one,
  mul_one, finset.prod_const, neg_one_pow_eq_pow_mod_two, nat.even_iff.1 h0, pow_zero, one_mul]

lemma int_prod_range_nonneg (m : ‚Ñ§) (n : ‚Ñï) (hn : even n) :
  0 ‚â§ ‚àè k in finset.range n, (m - k) :=
begin
  rcases hn with ‚ü®n, rfl‚ü©,
  induction n with n ihn, { simp },
  rw [nat.succ_eq_add_one, mul_add, mul_one, bit0, ‚Üê add_assoc, finset.prod_range_succ,
    finset.prod_range_succ, mul_assoc],
  refine mul_nonneg ihn _, generalize : (1 + 1) * n = k,
  cases le_or_lt m k with hmk hmk,
  { have : m ‚â§ k + 1, from hmk.trans (lt_add_one ‚Üëk).le,
    exact mul_nonneg_of_nonpos_of_nonpos (sub_nonpos.2 hmk) (sub_nonpos.2 this) },
  { exact mul_nonneg (sub_nonneg.2 hmk.le) (sub_nonneg.2 hmk) }
end

/-- `x^m`, `m : ‚Ñ§` is convex on `(0, +‚àû)` for all `m` -/
lemma convex_on_fpow (m : ‚Ñ§) : convex_on ùïú (Ioi 0) (Œª x : ‚Ñù, x^m) :=
begin
  have : ‚àÄ n : ‚Ñ§, differentiable_on ‚Ñù (Œª x, x ^ n) (Ioi (0 : ‚Ñù)),
    from Œª n, differentiable_on_fpow _ _ (or.inl $ lt_irrefl _),
  apply convex_on_of_deriv2_nonneg (convex_Ioi 0);
    try { simp only [interior_Ioi, deriv_fpow'] },
  { exact (this _).continuous_on },
  { exact this _ },
  { exact (this _).const_mul _ },
  { intros x hx,
    simp only [iter_deriv_fpow, ‚Üê int.cast_coe_nat, ‚Üê int.cast_sub, ‚Üê int.cast_prod],
    refine mul_nonneg (int.cast_nonneg.2 _) (fpow_nonneg (le_of_lt hx) _),
    exact int_prod_range_nonneg _ _ (nat.even_bit0 1) }
end

lemma convex_on_rpow {p : ‚Ñù} (hp : 1 ‚â§ p) : convex_on ùïú (Ici 0) (Œª x : ‚Ñù, x^p) :=
begin
  have A : deriv (Œª (x : ‚Ñù), x ^ p) = Œª x, p * x^(p-1), by { ext x, simp [hp] },
  apply convex_on_of_deriv2_nonneg (convex_Ici 0),
  { exact continuous_on_id.rpow_const (Œª x _, or.inr (zero_le_one.trans hp)) },
  { exact (differentiable_rpow_const hp).differentiable_on },
  { rw A,
    assume x hx,
    replace hx : x ‚â† 0, by { simp at hx, exact ne_of_gt hx },
    simp [differentiable_at.differentiable_within_at, hx] },
  { assume x hx,
    replace hx : 0 < x, by simpa using hx,
    suffices : 0 ‚â§ p * ((p - 1) * x ^ (p - 1 - 1)), by simpa [ne_of_gt hx, A],
    apply mul_nonneg (le_trans zero_le_one hp),
    exact mul_nonneg (sub_nonneg_of_le hp) (rpow_nonneg_of_nonneg (le_of_lt hx) _) }
end

lemma concave_on_log_Ioi : concave_on ùïú (Ioi 0) log :=
begin
  have h‚ÇÅ : Ioi 0 ‚äÜ ({0} : set ‚Ñù)·∂ú,
  { intros x hx hx',
    rw [mem_singleton_iff] at hx',
    rw [hx'] at hx,
    exact lt_irrefl 0 hx },
  refine concave_on_open_of_deriv2_nonpos (convex_Ioi 0) is_open_Ioi _ _ _,
  { exact differentiable_on_log.mono h‚ÇÅ },
  { refine ((times_cont_diff_on_log.deriv_of_open _ le_top).differentiable_on le_top).mono h‚ÇÅ,
    exact is_open_compl_singleton },
  { intros x hx,
    rw [function.iterate_succ, function.iterate_one],
    change (deriv (deriv log)) x ‚â§ 0,
    rw [deriv_log', deriv_inv],
    exact neg_nonpos.mpr (inv_nonneg.mpr (sq_nonneg x)) }
end

lemma concave_on_log_Iio : concave_on ùïú (Iio 0) log :=
begin
  have h‚ÇÅ : Iio 0 ‚äÜ ({0} : set ‚Ñù)·∂ú,
  { intros x hx hx',
    rw [mem_singleton_iff] at hx',
    rw [hx'] at hx,
    exact lt_irrefl 0 hx },
  refine concave_on_open_of_deriv2_nonpos (convex_Iio 0) is_open_Iio _ _ _,
  { exact differentiable_on_log.mono h‚ÇÅ },
  { refine ((times_cont_diff_on_log.deriv_of_open _ le_top).differentiable_on le_top).mono h‚ÇÅ,
    exact is_open_compl_singleton },
  { intros x hx,
    rw [function.iterate_succ, function.iterate_one],
    change (deriv (deriv log)) x ‚â§ 0,
    rw [deriv_log', deriv_inv],
    exact neg_nonpos.mpr (inv_nonneg.mpr (sq_nonneg x)) }
end
