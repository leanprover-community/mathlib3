/-
Copyright (c) 2021 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import analysis.convex.basic

/-!
# Star-convex sets

This files defines star-convex sets (aka star domains, star-shaped set, radially convex set).

A set is star-convex at `x` if every segment from `x` to a point in the set is contained in the set.

This is the prototypical example of a contractible set in homotopy theory (by scaling every point
towards `x`), but has wider uses.

Note that this has nothing to do with star rings, `has_star` and co.

## Main declarations

* `star_convex ğ•œ x s`: `s` is star-convex at `x` with scalars `ğ•œ`.

## Implementation notes

Instead of saying that a set is star-convex, we say a set is star-convex *at a point*. This has the
advantage of allowing us to talk about convexity as being "everywhere star-convexity" and of making
the union of star-convex sets be star-convex.

Incidentally, this choice means we don't need to assume a set is nonempty for it to be star-convex.
Concretely, the empty set is star-convex at every point.

## TODO

Balanced sets are star-convex.

The closure of a star-convex set is star-convex.

Star-convex sets are contractible.

A nonempty open star-convex set in `â„^n` is diffeomorphic to the entire space.
-/

open set
open_locale convex pointwise

variables {ğ•œ E F Î² : Type*}

section ordered_semiring
variables [ordered_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F]

section has_scalar
variables (ğ•œ) [has_scalar ğ•œ E] [has_scalar ğ•œ F] (x : E) (s : set E)

/-- Star-convexity of sets. `s` is star-convex at `x` if every segment from `x` to a point in `s` is
contained in `s`. -/
def star_convex : Prop :=
âˆ€ â¦ƒy : Eâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s

variables {ğ•œ x s} {t : set E}

lemma convex_iff_forall_star_convex : convex ğ•œ s â†” âˆ€ x âˆˆ s, star_convex ğ•œ x s :=
forall_congr $ Î» x, forall_swap

lemma convex.star_convex (h : convex ğ•œ s) (hx : x âˆˆ s) : star_convex ğ•œ x s :=
convex_iff_forall_star_convex.1 h _ hx

lemma star_convex_iff_segment_subset : star_convex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ [x -[ğ•œ] y] âŠ† s :=
begin
  split,
  { rintro h y hy z âŸ¨a, b, ha, hb, hab, rflâŸ©,
    exact h hy ha hb hab },
  { rintro h y hy a b ha hb hab,
    exact h hy âŸ¨a, b, ha, hb, hab, rflâŸ© }
end

lemma star_convex.segment_subset (h : star_convex ğ•œ x s) {y : E} (hy : y âˆˆ s) : [x -[ğ•œ] y] âŠ† s :=
star_convex_iff_segment_subset.1 h hy

lemma star_convex.open_segment_subset (h : star_convex ğ•œ x s) {y : E} (hy : y âˆˆ s) :
  open_segment ğ•œ x y âŠ† s :=
(open_segment_subset_segment ğ•œ x y).trans (h.segment_subset hy)

/-- Alternative definition of star-convexity, in terms of pointwise set operations. -/
lemma star_convex_iff_pointwise_add_subset :
  star_convex ğ•œ x s â†” âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ {x} + b â€¢ s âŠ† s :=
begin
  refine âŸ¨_, Î» h y hy a b ha hb hab,
    h ha hb hab (add_mem_add (smul_mem_smul_set $ mem_singleton _) âŸ¨_, hy, rflâŸ©)âŸ©,
  rintro hA a b ha hb hab w âŸ¨au, bv, âŸ¨u, (rfl : u = x), rflâŸ©, âŸ¨v, hv, rflâŸ©, rflâŸ©,
  exact hA hv ha hb hab,
end

lemma star_convex_empty (x : E) : star_convex ğ•œ x âˆ… := Î» y hy, hy.elim

lemma star_convex_univ (x : E) : star_convex ğ•œ x univ := Î» _ _ _ _ _ _ _, trivial

lemma star_convex.inter (hs : star_convex ğ•œ x s) (ht : star_convex ğ•œ x t) :
  star_convex ğ•œ x (s âˆ© t) :=
Î» y hy a b ha hb hab, âŸ¨hs hy.left ha hb hab, ht hy.right ha hb habâŸ©

lemma star_convex_sInter {S : set (set E)} (h : âˆ€ s âˆˆ S, star_convex ğ•œ x s) :
  star_convex ğ•œ x (â‹‚â‚€ S) :=
Î» y hy a b ha hb hab s hs, h s hs (hy s hs) ha hb hab

lemma star_convex_Inter {Î¹ : Sort*} {s : Î¹ â†’ set E} (h : âˆ€ i, star_convex ğ•œ x (s i)) :
  star_convex ğ•œ x (â‹‚ i, s i) :=
(sInter_range s) â–¸ star_convex_sInter $ forall_range_iff.2 h

lemma star_convex.union (hs : star_convex ğ•œ x s) (ht : star_convex ğ•œ x t) :
  star_convex ğ•œ x (s âˆª t) :=
begin
  rintro y (hy | hy) a b ha hb hab,
  { exact or.inl (hs hy ha hb hab) },
  { exact or.inr (ht hy ha hb hab) }
end

lemma star_convex_Union {Î¹ : Sort*} {s : Î¹ â†’ set E} (hs : âˆ€ i, star_convex ğ•œ x (s i)) :
  star_convex ğ•œ x (â‹ƒ i, s i) :=
begin
  rintro y hy a b ha hb hab,
  rw mem_Union at âŠ¢ hy,
  obtain âŸ¨i, hyâŸ© := hy,
  exact âŸ¨i, hs i hy ha hb habâŸ©,
end

lemma star_convex_sUnion {S : set (set E)} (hS : âˆ€ s âˆˆ S, star_convex ğ•œ x s) :
  star_convex ğ•œ x (â‹ƒâ‚€ S) :=
begin
  rw sUnion_eq_Union,
  exact star_convex_Union (Î» s, hS _ s.2),
end

lemma star_convex.prod {y : F} {s : set E} {t : set F} (hs : star_convex ğ•œ x s)
  (ht : star_convex ğ•œ y t) :
  star_convex ğ•œ (x, y) (s Ã—Ë¢ t) :=
Î» y hy a b ha hb hab, âŸ¨hs hy.1 ha hb hab, ht hy.2 ha hb habâŸ©

lemma star_convex_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [Î  i, add_comm_monoid (E i)]
  [Î  i, has_scalar ğ•œ (E i)] {x : Î  i, E i} {s : set Î¹} {t : Î  i, set (E i)}
  (ht : âˆ€ i, star_convex ğ•œ (x i) (t i)) :
  star_convex ğ•œ x (s.pi t) :=
Î» y hy a b ha hb hab i hi, ht i (hy i hi) ha hb hab

end has_scalar

section module
variables [module ğ•œ E] [module ğ•œ F] {x y z : E} {s : set E}

lemma star_convex.mem (hs : star_convex ğ•œ x s) (h : s.nonempty) : x âˆˆ s :=
begin
  obtain âŸ¨y, hyâŸ© := h,
  convert hs hy zero_le_one le_rfl (add_zero 1),
  rw [one_smul, zero_smul, add_zero],
end

lemma convex.star_convex_iff (hs : convex ğ•œ s) (h : s.nonempty) : star_convex ğ•œ x s â†” x âˆˆ s :=
âŸ¨Î» hxs, hxs.mem h, hs.star_convexâŸ©

lemma star_convex_iff_forall_pos (hx : x âˆˆ s) :
  star_convex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
begin
  refine âŸ¨Î» h y hy a b ha hb hab, h hy ha.le hb.le hab, _âŸ©,
  intros h y hy a b ha hb hab,
  obtain rfl | ha := ha.eq_or_lt,
  { rw zero_add at hab,
    rwa [hab, one_smul, zero_smul, zero_add] },
  obtain rfl | hb := hb.eq_or_lt,
  { rw add_zero at hab,
    rwa [hab, one_smul, zero_smul, add_zero] },
  exact h hy ha hb hab,
end

lemma star_convex_iff_forall_ne_pos (hx : x âˆˆ s) :
  star_convex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    a â€¢ x + b â€¢ y âˆˆ s :=
begin
  refine âŸ¨Î» h y hy _ a b ha hb hab, h hy ha.le hb.le hab, _âŸ©,
  intros h y hy a b ha hb hab,
  obtain rfl | ha' := ha.eq_or_lt,
  { rw [zero_add] at hab, rwa [hab, zero_smul, one_smul, zero_add] },
  obtain rfl | hb' := hb.eq_or_lt,
  { rw [add_zero] at hab, rwa [hab, zero_smul, one_smul, add_zero] },
  obtain rfl | hxy := eq_or_ne x y,
  { rwa convex.combo_self hab },
  exact h hy hxy ha' hb' hab,
end

lemma star_convex_iff_open_segment_subset (hx : x âˆˆ s) :
  star_convex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ open_segment ğ•œ x y âŠ† s :=
star_convex_iff_segment_subset.trans $ forallâ‚‚_congr $ Î» y hy,
  (open_segment_subset_iff_segment_subset hx hy).symm

lemma star_convex_singleton (x : E) : star_convex ğ•œ x {x} :=
begin
  rintro y (rfl : y = x) a b ha hb hab,
  exact convex.combo_self hab _,
end

lemma star_convex.linear_image (hs : star_convex ğ•œ x s) (f : E â†’â‚—[ğ•œ] F) :
  star_convex ğ•œ (f x) (s.image f) :=
begin
  intros y hy a b ha hb hab,
  obtain âŸ¨y', hy', rflâŸ© := hy,
  exact âŸ¨a â€¢ x + b â€¢ y', hs hy' ha hb hab, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©,
end

lemma star_convex.is_linear_image (hs : star_convex ğ•œ x s) {f : E â†’ F} (hf : is_linear_map ğ•œ f) :
  star_convex ğ•œ (f x) (f '' s) :=
hs.linear_image $ hf.mk' f

lemma star_convex.linear_preimage {s : set F} (f : E â†’â‚—[ğ•œ] F) (hs : star_convex ğ•œ (f x) s) :
  star_convex ğ•œ x (s.preimage f) :=
begin
  intros y hy a b ha hb hab,
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul],
  exact hs hy ha hb hab,
end

lemma star_convex.is_linear_preimage {s : set F} {f : E â†’ F} (hs : star_convex ğ•œ (f x) s)
  (hf : is_linear_map ğ•œ f) :
  star_convex ğ•œ x (preimage f s) :=
hs.linear_preimage $ hf.mk' f

lemma star_convex.add {t : set E} (hs : star_convex ğ•œ x s) (ht : star_convex ğ•œ y t) :
  star_convex ğ•œ (x + y) (s + t) :=
by { rw â†add_image_prod, exact (hs.prod ht).is_linear_image is_linear_map.is_linear_map_add }

lemma star_convex.add_left (hs : star_convex ğ•œ x s) (z : E) :
  star_convex ğ•œ (z + x) ((Î» x, z + x) '' s) :=
begin
  intros y hy a b ha hb hab,
  obtain âŸ¨y', hy', rflâŸ© := hy,
  refine âŸ¨a â€¢ x + b â€¢ y', hs hy' ha hb hab, _âŸ©,
  rw [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul],
end

lemma star_convex.add_right (hs : star_convex ğ•œ x s) (z : E) :
  star_convex ğ•œ (x + z) ((Î» x, x + z) '' s) :=
begin
  intros y hy a b ha hb hab,
  obtain âŸ¨y', hy', rflâŸ© := hy,
  refine âŸ¨a â€¢ x + b â€¢ y', hs hy' ha hb hab, _âŸ©,
  rw [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul],
end

/-- The translation of a star-convex set is also star-convex. -/
lemma star_convex.preimage_add_right (hs : star_convex ğ•œ (z + x) s) :
  star_convex ğ•œ x ((Î» x, z + x) â»Â¹' s) :=
begin
  intros y hy a b ha hb hab,
  have h := hs hy ha hb hab,
  rwa [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul] at h,
end

/-- The translation of a star-convex set is also star-convex. -/
lemma star_convex.preimage_add_left (hs : star_convex ğ•œ (x + z) s) :
  star_convex ğ•œ x ((Î» x, x + z) â»Â¹' s) :=
begin
  rw add_comm at hs,
  simpa only [add_comm] using hs.preimage_add_right,
end

end module
end add_comm_monoid

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] {x y : E}

lemma star_convex.sub' {s : set (E Ã— E)} (hs : star_convex ğ•œ (x, y) s) :
  star_convex ğ•œ (x - y) ((Î» x : E Ã— E, x.1 - x.2) '' s) :=
hs.is_linear_image is_linear_map.is_linear_map_sub

end add_comm_group
end ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F] [module ğ•œ E] [module ğ•œ F] {x : E} {s : set E}

lemma star_convex.smul (hs : star_convex ğ•œ x s) (c : ğ•œ) : star_convex ğ•œ (c â€¢ x) (c â€¢ s) :=
hs.linear_image $ linear_map.lsmul _ _ c

lemma star_convex.preimage_smul {c : ğ•œ} (hs : star_convex ğ•œ (c â€¢ x) s) :
  star_convex ğ•œ x ((Î» z, c â€¢ z) â»Â¹' s) :=
hs.linear_preimage (linear_map.lsmul _ _ c)

lemma star_convex.affinity (hs : star_convex ğ•œ x s) (z : E) (c : ğ•œ) :
  star_convex ğ•œ (z + c â€¢ x) ((Î» x, z + c â€¢ x) '' s) :=
begin
  have h := (hs.smul c).add_left z,
  rwa [â†image_smul, image_image] at h,
end

end add_comm_monoid
end ordered_comm_semiring

section ordered_ring
variables [ordered_ring ğ•œ]

section add_comm_monoid
variables [add_comm_monoid E] [smul_with_zero ğ•œ E]{s : set E}

lemma star_convex_zero_iff :
  star_convex ğ•œ 0 s â†” âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒa : ğ•œâ¦„, 0 â‰¤ a â†’ a â‰¤ 1 â†’ a â€¢ x âˆˆ s :=
begin
  refine forall_congr (Î» x, forall_congr $ Î» hx, âŸ¨Î» h a haâ‚€ haâ‚, _, Î» h a b ha hb hab, _âŸ©),
  { simpa only [sub_add_cancel, eq_self_iff_true, forall_true_left, zero_add, smul_zero'] using
      h (sub_nonneg_of_le haâ‚) haâ‚€ },
  { rw [smul_zero', zero_add],
    exact h hb (by { rw â†hab, exact le_add_of_nonneg_left ha }) }
end

end add_comm_monoid

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {x y : E} {s t : set E}

lemma star_convex.add_smul_mem (hs : star_convex ğ•œ x s) (hy : x + y âˆˆ s) {t : ğ•œ} (htâ‚€ : 0 â‰¤ t)
  (htâ‚ : t â‰¤ 1) :
  x + t â€¢ y âˆˆ s :=
begin
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y),
  { rw [smul_add, â†add_assoc, â†add_smul, sub_add_cancel, one_smul] },
  rw h,
  exact hs hy (sub_nonneg_of_le htâ‚) htâ‚€ (sub_add_cancel _ _),
end

lemma star_convex.smul_mem (hs : star_convex ğ•œ 0 s) (hx : x âˆˆ s) {t : ğ•œ} (htâ‚€ : 0 â‰¤ t)
  (htâ‚ : t â‰¤ 1) :
  t â€¢ x âˆˆ s :=
by simpa using hs.add_smul_mem (by simpa using hx) htâ‚€ htâ‚

lemma star_convex.add_smul_sub_mem (hs : star_convex ğ•œ x s) (hy : y âˆˆ s) {t : ğ•œ} (htâ‚€ : 0 â‰¤ t)
  (htâ‚ : t â‰¤ 1) :
  x + t â€¢ (y - x) âˆˆ s :=
begin
  apply hs.segment_subset hy,
  rw segment_eq_image',
  exact mem_image_of_mem _ âŸ¨htâ‚€, htâ‚âŸ©,
end

/-- The preimage of a star-convex set under an affine map is star-convex. -/
lemma star_convex.affine_preimage (f : E â†’áµƒ[ğ•œ] F) {s : set F} (hs : star_convex ğ•œ (f x) s) :
  star_convex ğ•œ x (f â»Â¹' s) :=
begin
  intros y hy a b ha hb hab,
  rw [mem_preimage, convex.combo_affine_apply hab],
  exact hs hy ha hb hab,
end

/-- The image of a star-convex set under an affine map is star-convex. -/
lemma star_convex.affine_image (f : E â†’áµƒ[ğ•œ] F) {s : set E} (hs : star_convex ğ•œ x s) :
  star_convex ğ•œ (f x) (f '' s) :=
begin
  rintro y âŸ¨y', âŸ¨hy', hy'fâŸ©âŸ© a b ha hb hab,
  refine âŸ¨a â€¢ x + b â€¢ y', âŸ¨hs hy' ha hb hab, _âŸ©âŸ©,
  rw [convex.combo_affine_apply hab, hy'f],
end

lemma star_convex.neg (hs : star_convex ğ•œ x s) : star_convex ğ•œ (-x) (-s) :=
by { rw â†image_neg, exact hs.is_linear_image is_linear_map.is_linear_map_neg }

lemma star_convex.sub (hs : star_convex ğ•œ x s) (ht : star_convex ğ•œ y t) :
  star_convex ğ•œ (x - y) (s - t) :=
by { simp_rw sub_eq_add_neg, exact hs.add ht.neg }

end add_comm_group
end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ]

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] {x : E} {s : set E}

/-- Alternative definition of star-convexity, using division. -/
lemma star_convex_iff_div :
  star_convex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’
    (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ s :=
âŸ¨Î» h y hy a b ha hb hab, begin
  apply h hy,
  { have ha', from mul_le_mul_of_nonneg_left ha (inv_pos.2 hab).le,
    rwa [mul_zero, â†div_eq_inv_mul] at ha' },
  { have hb', from mul_le_mul_of_nonneg_left hb (inv_pos.2 hab).le,
    rwa [mul_zero, â†div_eq_inv_mul] at hb' },
  { rw â†add_div,
    exact div_self hab.ne' }
end, Î» h y hy a b ha hb hab,
begin
  have h', from h hy ha hb,
  rw [hab, div_one, div_one] at h',
  exact h' zero_lt_one
endâŸ©

lemma star_convex.mem_smul (hs : star_convex ğ•œ 0 s) (hx : x âˆˆ s) {t : ğ•œ} (ht : 1 â‰¤ t) :
  x âˆˆ t â€¢ s :=
begin
  rw mem_smul_set_iff_inv_smul_memâ‚€ (zero_lt_one.trans_le ht).ne',
  exact hs.smul_mem hx (inv_nonneg.2 $ zero_le_one.trans ht) (inv_le_one ht),
end

end add_comm_group
end linear_ordered_field

/-!
#### Star-convex sets in an ordered space

Relates `star_convex` and `set.ord_connected`.
-/

section ord_connected

lemma set.ord_connected.star_convex [ordered_semiring ğ•œ] [ordered_add_comm_monoid E]
  [module ğ•œ E] [ordered_smul ğ•œ E] {x : E} {s : set E} (hs : s.ord_connected) (hx : x âˆˆ s)
  (h : âˆ€ y âˆˆ s, x â‰¤ y âˆ¨ y â‰¤ x) :
  star_convex ğ•œ x s :=
begin
  intros y hy a b ha hb hab,
  obtain hxy | hyx := h _ hy,
  { refine hs.out hx hy (mem_Icc.2 âŸ¨_, _âŸ©),
    calc
      x   = a â€¢ x + b â€¢ x : (convex.combo_self hab _).symm
      ... â‰¤ a â€¢ x + b â€¢ y : add_le_add_left (smul_le_smul_of_nonneg hxy hb) _,
    calc
      a â€¢ x + b â€¢ y
          â‰¤ a â€¢ y + b â€¢ y : add_le_add_right (smul_le_smul_of_nonneg hxy ha) _
      ... = y             : convex.combo_self hab _ },
  { refine hs.out hy hx (mem_Icc.2 âŸ¨_, _âŸ©),
    calc
      y   = a â€¢ y + b â€¢ y : (convex.combo_self hab _).symm
      ... â‰¤ a â€¢ x + b â€¢ y : add_le_add_right (smul_le_smul_of_nonneg hyx ha) _,
    calc
      a â€¢ x + b â€¢ y
          â‰¤ a â€¢ x + b â€¢ x : add_le_add_left (smul_le_smul_of_nonneg hyx hb) _
      ... = x             : convex.combo_self hab _ }
end

lemma star_convex_iff_ord_connected [linear_ordered_field ğ•œ] {x : ğ•œ} {s : set ğ•œ} (hx : x âˆˆ s) :
  star_convex ğ•œ x s â†” s.ord_connected :=
by simp_rw [ord_connected_iff_interval_subset_left hx, star_convex_iff_segment_subset,
  segment_eq_interval]

alias star_convex_iff_ord_connected â†” star_convex.ord_connected _

end ord_connected

/-! #### Star-convexity of submodules/subspaces -/

section submodule
open submodule

lemma submodule.star_convex [ordered_semiring ğ•œ] [add_comm_monoid E] [module ğ•œ E]
  (K : submodule ğ•œ E) :
  star_convex ğ•œ (0 : E) K :=
K.convex.star_convex K.zero_mem

lemma subspace.star_convex [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E]
  (K : subspace ğ•œ E) :
  star_convex ğ•œ (0 : E) K :=
K.convex.star_convex K.zero_mem

end submodule
