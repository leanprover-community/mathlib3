/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import analysis.convex.join

/-!
# Stone's separation theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file prove Stone's separation theorem. This tells us that any two disjoint convex sets can be
separated by a convex set whose complement is also convex.

In locally convex real topological vector spaces, the Hahn-Banach separation theorems provide
stronger statements: one may find a separating hyperplane, instead of merely a convex set whose
complement is convex.
-/

open set
open_locale big_operators

variables {ğ•œ E Î¹ : Type*} [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E] {s t : set E}

/-- In a tetrahedron with vertices `x`, `y`, `p`, `q`, any segment `[u, v]` joining the opposite
edges `[x, p]` and `[y, q]` passes through any triangle of vertices `p`, `q`, `z` where
`z âˆˆ [x, y]`. -/
lemma not_disjoint_segment_convex_hull_triple {p q u v x y z : E}
  (hz : z âˆˆ segment ğ•œ x y) (hu : u âˆˆ segment ğ•œ x p) (hv : v âˆˆ segment ğ•œ y q) :
  Â¬ disjoint (segment ğ•œ u v) (convex_hull ğ•œ {p, q, z}) :=
begin
  rw not_disjoint_iff,
  obtain âŸ¨az, bz, haz, hbz, habz, rflâŸ© := hz,
  obtain rfl | haz' := haz.eq_or_lt,
  { rw zero_add at habz,
    rw [zero_smul, zero_add, habz, one_smul],
    refine âŸ¨v, right_mem_segment _ _ _, segment_subset_convex_hull _ _ hvâŸ©; simp },
  obtain âŸ¨av, bv, hav, hbv, habv, rflâŸ© := hv,
  obtain rfl | hav' := hav.eq_or_lt,
  { rw zero_add at habv,
    rw [zero_smul, zero_add, habv, one_smul],
    exact âŸ¨q, right_mem_segment _ _ _, subset_convex_hull _ _ $ by simpâŸ© },
  obtain âŸ¨au, bu, hau, hbu, habu, rflâŸ© := hu,
  have hab : 0 < az * av + bz * au :=
    add_pos_of_pos_of_nonneg (mul_pos haz' hav') (mul_nonneg hbz hau),
  refine âŸ¨(az * av / (az * av + bz * au)) â€¢ (au â€¢ x + bu â€¢ p) +
          (bz * au / (az * av + bz * au)) â€¢ (av â€¢ y + bv â€¢ q), âŸ¨_, _, _, _, _, rflâŸ©, _âŸ©,
  { exact div_nonneg (mul_nonneg haz hav) hab.le },
  { exact div_nonneg (mul_nonneg hbz hau) hab.le },
  { rw [â†add_div, div_self hab.ne'] },
  rw [smul_add, smul_add, add_add_add_comm, add_comm, â†mul_smul, â†mul_smul],
  classical,
  let w : fin 3 â†’ ğ•œ := ![az * av * bu, bz * au * bv, au * av],
  let z : fin 3 â†’ E := ![p, q, az â€¢ x + bz â€¢ y],
  have hwâ‚€ : âˆ€ i, 0 â‰¤ w i,
  { rintro i,
    fin_cases i,
    { exact mul_nonneg (mul_nonneg haz hav) hbu },
    { exact mul_nonneg (mul_nonneg hbz hau) hbv },
    { exact mul_nonneg hau hav } },
  have hw : âˆ‘ i, w i = az * av + bz * au,
  { transitivity az * av * bu + (bz * au * bv + au * av),
    { simp [w, fin.sum_univ_succ, fin.sum_univ_zero] },
    rw [â†one_mul (au * av), â†habz, add_mul, â†add_assoc, add_add_add_comm, mul_assoc, â†mul_add,
      mul_assoc, â†mul_add, mul_comm av, â†add_mul, â†mul_add, add_comm bu, add_comm bv, habu, habv,
      one_mul, mul_one] },
  have hz : âˆ€ i, z i âˆˆ ({p, q, az â€¢ x + bz â€¢ y} : set E),
  { rintro i,
    fin_cases i; simp [z] },
  convert finset.center_mass_mem_convex_hull (finset.univ : finset (fin 3)) (Î» i _, hwâ‚€ i)
    (by rwa hw) (Î» i _, hz i),
  rw finset.center_mass,
  simp_rw [div_eq_inv_mul, hw, mul_assoc, mul_smul (az * av + bz * au)â»Â¹, â†smul_add, add_assoc,
    â†mul_assoc],
  congr' 3,
  rw [â†mul_smul, â†mul_rotate, mul_right_comm, mul_smul, â†mul_smul _ av, mul_rotate, mul_smul _ bz,
    â†smul_add],
  simp only [list.map, list.pmap, nat.add_def, add_zero, fin.mk_bit0,
    fin.mk_one, list.foldr_cons, list.foldr_nil],
  refl,
end

/-- **Stone's Separation Theorem** -/
lemma exists_convex_convex_compl_subset (hs : convex ğ•œ s) (ht : convex ğ•œ t) (hst : disjoint s t) :
  âˆƒ C : set E, convex ğ•œ C âˆ§ convex ğ•œ Cá¶œ âˆ§ s âŠ† C âˆ§ t âŠ† Cá¶œ :=
begin
  let S : set (set E) := {C | convex ğ•œ C âˆ§ disjoint C t},
  obtain âŸ¨C, hC, hsC, hCmaxâŸ© := zorn_subset_nonempty S
    (Î» c hcS hc âŸ¨t, htâŸ©, âŸ¨â‹ƒâ‚€ c, âŸ¨hc.directed_on.convex_sUnion (Î» s hs, (hcS hs).1),
     disjoint_sUnion_left.2 $ Î» c hc, (hcS hc).2âŸ©, Î» s, subset_sUnion_of_memâŸ©) s âŸ¨hs, hstâŸ©,
  refine âŸ¨C, hC.1, convex_iff_segment_subset.2 $ Î» x hx y hy z hz hzC, _, hsC,
     hC.2.subset_compl_leftâŸ©,
  suffices h : âˆ€ c âˆˆ Cá¶œ, âˆƒ a âˆˆ C, (segment ğ•œ c a âˆ© t).nonempty,
  { obtain âŸ¨p, hp, u, hu, hutâŸ© := h x hx,
    obtain âŸ¨q, hq, v, hv, hvtâŸ© := h y hy,
    refine not_disjoint_segment_convex_hull_triple hz hu hv
      (hC.2.symm.mono (ht.segment_subset hut hvt) $ convex_hull_min _ hC.1),
    simp [insert_subset, hp, hq, singleton_subset_iff.2 hzC] },
  rintro c hc,
  by_contra' h,
  suffices h : disjoint (convex_hull ğ•œ (insert c C)) t,
  { rw â†hCmax _ âŸ¨convex_convex_hull _ _, hâŸ©
      ((subset_insert _ _).trans $ subset_convex_hull _ _) at hc,
    exact hc (subset_convex_hull _ _ $ mem_insert _ _) },
  rw [convex_hull_insert âŸ¨z, hzCâŸ©, convex_join_singleton_left],
  refine disjoint_Unionâ‚‚_left.2 (Î» a ha, disjoint_iff_inf_le.mpr $ Î» b hb, h a _ âŸ¨b, hbâŸ©),
  rwa â†hC.1.convex_hull_eq,
end
