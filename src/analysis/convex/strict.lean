/-
Copyright (c) 2021 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import analysis.convex.basic
import topology.algebra.order.basic

/-!
# Strictly convex sets

This file defines strictly convex sets.

A set is strictly convex if the open segment between any two distinct points lies in its interior.
-/

open set
open_locale convex pointwise

variables {ğ•œ ğ• E F Î² : Type*}

open function set
open_locale convex

section ordered_semiring
variables [ordered_semiring ğ•œ] [topological_space E] [topological_space F]

section add_comm_monoid
variables [add_comm_monoid E] [add_comm_monoid F]

section has_scalar
variables (ğ•œ) [has_scalar ğ•œ E] [has_scalar ğ•œ F] (s : set E)

/-- A set is strictly convex if the open segment between any two distinct points lies is in its
interior. This basically means "convex and not flat on the boundary". -/
def strict_convex : Prop :=
s.pairwise $ Î» x y, âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ interior s

variables {ğ•œ s} {x y : E} {a b : ğ•œ}

lemma strict_convex_iff_open_segment_subset :
  strict_convex ğ•œ s â†” s.pairwise (Î» x y, open_segment ğ•œ x y âŠ† interior s) :=
forallâ‚…_congr $ Î» x hx y hy hxy, (open_segment_subset_iff ğ•œ).symm

lemma strict_convex.open_segment_subset (hs : strict_convex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s)
  (h : x â‰  y) :
  open_segment ğ•œ x y âŠ† interior s :=
strict_convex_iff_open_segment_subset.1 hs hx hy h

lemma strict_convex_empty : strict_convex ğ•œ (âˆ… : set E) := pairwise_empty _

lemma strict_convex_univ : strict_convex ğ•œ (univ : set E) :=
begin
  intros x hx y hy hxy a b ha hb hab,
  rw interior_univ,
  exact mem_univ _,
end

protected lemma strict_convex.eq (hs : strict_convex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s) (ha : 0 < a)
  (hb : 0 < b) (hab : a + b = 1) (h : a â€¢ x + b â€¢ y âˆ‰ interior s) : x = y :=
hs.eq hx hy $ Î» H, h $ H ha hb hab

protected lemma strict_convex.inter {t : set E} (hs : strict_convex ğ•œ s) (ht : strict_convex ğ•œ t) :
  strict_convex ğ•œ (s âˆ© t) :=
begin
  intros x hx y hy hxy a b ha hb hab,
  rw interior_inter,
  exact âŸ¨hs hx.1 hy.1 hxy ha hb hab, ht hx.2 hy.2 hxy ha hb habâŸ©,
end

lemma directed.strict_convex_Union {Î¹ : Sort*} {s : Î¹ â†’ set E} (hdir : directed (âŠ†) s)
  (hs : âˆ€ â¦ƒi : Î¹â¦„, strict_convex ğ•œ (s i)) :
  strict_convex ğ•œ (â‹ƒ i, s i) :=
begin
  rintro x hx y hy hxy a b ha hb hab,
  rw mem_Union at hx hy,
  obtain âŸ¨i, hxâŸ© := hx,
  obtain âŸ¨j, hyâŸ© := hy,
  obtain âŸ¨k, hik, hjkâŸ© := hdir i j,
  exact interior_mono (subset_Union s k) (hs (hik hx) (hjk hy) hxy ha hb hab),
end

lemma directed_on.strict_convex_sUnion {S : set (set E)} (hdir : directed_on (âŠ†) S)
  (hS : âˆ€ s âˆˆ S, strict_convex ğ•œ s) :
  strict_convex ğ•œ (â‹ƒâ‚€ S) :=
begin
  rw sUnion_eq_Union,
  exact (directed_on_iff_directed.1 hdir).strict_convex_Union (Î» s, hS _ s.2),
end

end has_scalar

section module
variables [module ğ•œ E] [module ğ•œ F] {s : set E}

protected lemma strict_convex.convex (hs : strict_convex ğ•œ s) : convex ğ•œ s :=
convex_iff_pairwise_pos.2 $ Î» x hx y hy hxy a b ha hb hab, interior_subset $ hs hx hy hxy ha hb hab

/-- An open convex set is strictly convex. -/
protected lemma convex.strict_convex (h : is_open s) (hs : convex ğ•œ s) : strict_convex ğ•œ s :=
Î» x hx y hy _ a b ha hb hab, h.interior_eq.symm â–¸ hs hx hy ha.le hb.le hab

lemma is_open.strict_convex_iff (h : is_open s) : strict_convex ğ•œ s â†” convex ğ•œ s :=
âŸ¨strict_convex.convex, convex.strict_convex hâŸ©

lemma strict_convex_singleton (c : E) : strict_convex ğ•œ ({c} : set E) := pairwise_singleton _ _

lemma set.subsingleton.strict_convex (hs : s.subsingleton) : strict_convex ğ•œ s := hs.pairwise _

lemma strict_convex.linear_image [semiring ğ•] [module ğ• E] [module ğ• F]
  [linear_map.compatible_smul E F ğ•œ ğ•] (hs : strict_convex ğ•œ s) (f : E â†’â‚—[ğ•] F)
  (hf : is_open_map f) :
  strict_convex ğ•œ (f '' s) :=
begin
  rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© hxy a b ha hb hab,
  refine hf.image_interior_subset _ âŸ¨a â€¢ x + b â€¢ y, hs hx hy (ne_of_apply_ne _ hxy) ha hb hab, _âŸ©,
  rw [map_add, f.map_smul_of_tower a, f.map_smul_of_tower b]
end

lemma strict_convex.is_linear_image (hs : strict_convex ğ•œ s) {f : E â†’ F} (h : is_linear_map ğ•œ f)
  (hf : is_open_map f) :
  strict_convex ğ•œ (f '' s) :=
hs.linear_image (h.mk' f) hf

lemma strict_convex.linear_preimage {s : set F} (hs : strict_convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F)
  (hf : continuous f) (hfinj : injective f) :
  strict_convex ğ•œ (s.preimage f) :=
begin
  intros x hx y hy hxy a b ha hb hab,
  refine preimage_interior_subset_interior_preimage hf _,
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul],
  exact hs hx hy (hfinj.ne hxy) ha hb hab,
end

lemma strict_convex.is_linear_preimage {s : set F} (hs : strict_convex ğ•œ s) {f : E â†’ F}
  (h : is_linear_map ğ•œ f) (hf : continuous f) (hfinj : injective f) :
  strict_convex ğ•œ (s.preimage f) :=
hs.linear_preimage (h.mk' f) hf hfinj

section linear_ordered_cancel_add_comm_monoid
variables [topological_space Î²] [linear_ordered_cancel_add_comm_monoid Î²] [order_topology Î²]
  [module ğ•œ Î²] [ordered_smul ğ•œ Î²]

lemma strict_convex_Iic (r : Î²) : strict_convex ğ•œ (Iic r) :=
begin
  rintro x (hx : x â‰¤ r) y (hy : y â‰¤ r) hxy a b ha hb hab,
  refine (subset_interior_iff_subset_of_open is_open_Iio).2 Iio_subset_Iic_self _,
  rw â†convex.combo_self hab r,
  obtain rfl | hx := hx.eq_or_lt,
  { exact add_lt_add_left (smul_lt_smul_of_pos (hy.lt_of_ne hxy.symm) hb) _ },
  obtain rfl | hy := hy.eq_or_lt,
  { exact add_lt_add_right (smul_lt_smul_of_pos hx ha) _ },
  { exact add_lt_add (smul_lt_smul_of_pos hx ha) (smul_lt_smul_of_pos hy hb) }
end

lemma strict_convex_Ici (r : Î²) : strict_convex ğ•œ (Ici r) := @strict_convex_Iic ğ•œ Î²áµ’áµˆ _ _ _ _ _ _ r

lemma strict_convex_Icc (r s : Î²) : strict_convex ğ•œ (Icc r s) :=
(strict_convex_Ici r).inter $ strict_convex_Iic s

lemma strict_convex_Iio (r : Î²) : strict_convex ğ•œ (Iio r) :=
(convex_Iio r).strict_convex is_open_Iio

lemma strict_convex_Ioi (r : Î²) : strict_convex ğ•œ (Ioi r) :=
(convex_Ioi r).strict_convex is_open_Ioi

lemma strict_convex_Ioo (r s : Î²) : strict_convex ğ•œ (Ioo r s) :=
(strict_convex_Ioi r).inter $ strict_convex_Iio s

lemma strict_convex_Ico (r s : Î²) : strict_convex ğ•œ (Ico r s) :=
(strict_convex_Ici r).inter $ strict_convex_Iio s

lemma strict_convex_Ioc (r s : Î²) : strict_convex ğ•œ (Ioc r s) :=
(strict_convex_Ioi r).inter $ strict_convex_Iic s

lemma strict_convex_interval (r s : Î²) : strict_convex ğ•œ (interval r s) :=
strict_convex_Icc _ _

end linear_ordered_cancel_add_comm_monoid
end module
end add_comm_monoid

section add_cancel_comm_monoid
variables [add_cancel_comm_monoid E] [has_continuous_add E] [module ğ•œ E] {s : set E}

/-- The translation of a strictly convex set is also strictly convex. -/
lemma strict_convex.preimage_add_right (hs : strict_convex ğ•œ s) (z : E) :
  strict_convex ğ•œ ((Î» x, z + x) â»Â¹' s) :=
begin
  intros x hx y hy hxy a b ha hb hab,
  refine preimage_interior_subset_interior_preimage (continuous_add_left _) _,
  have h := hs hx hy ((add_right_injective _).ne hxy) ha hb hab,
  rwa [smul_add, smul_add, add_add_add_comm, â†add_smul, hab, one_smul] at h,
end

/-- The translation of a strictly convex set is also strictly convex. -/
lemma strict_convex.preimage_add_left (hs : strict_convex ğ•œ s) (z : E) :
  strict_convex ğ•œ ((Î» x, x + z) â»Â¹' s) :=
by simpa only [add_comm] using hs.preimage_add_right z

end add_cancel_comm_monoid

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F]

section continuous_add
variables [has_continuous_add E] {s t : set E}

lemma strict_convex.add (hs : strict_convex ğ•œ s) (ht : strict_convex ğ•œ t) :
  strict_convex ğ•œ (s + t) :=
begin
  rintro _ âŸ¨v, w, hv, hw, rflâŸ© _ âŸ¨x, y, hx, hy, rflâŸ© h a b ha hb hab,
  rw [smul_add, smul_add, add_add_add_comm],
  obtain rfl | hvx := eq_or_ne v x,
  { refine interior_mono (add_subset_add (singleton_subset_iff.2 hv) subset.rfl) _,
    rw [convex.combo_self hab, singleton_add],
    exact (is_open_map_add_left _).image_interior_subset _
      (mem_image_of_mem _ $ ht hw hy (ne_of_apply_ne _ h) ha hb hab) },
  exact subset_interior_add_left (add_mem_add (hs hv hx hvx ha hb hab) $
    ht.convex hw hy ha.le hb.le hab)
end

lemma strict_convex.add_left (hs : strict_convex ğ•œ s) (z : E) :
  strict_convex ğ•œ ((Î» x, z + x) '' s) :=
by simpa only [singleton_add] using (strict_convex_singleton z).add hs

lemma strict_convex.add_right (hs : strict_convex ğ•œ s) (z : E) :
  strict_convex ğ•œ ((Î» x, x + z) '' s) :=
by simpa only [add_comm] using hs.add_left z

/-- The translation of a strictly convex set is also strictly convex. -/
lemma strict_convex.vadd (hs : strict_convex ğ•œ s) (x : E) : strict_convex ğ•œ (x +áµ¥ s) :=
hs.add_left x

end continuous_add

section continuous_smul
variables [linear_ordered_field ğ•] [module ğ• E] [has_continuous_const_smul ğ• E]
  [linear_map.compatible_smul E E ğ•œ ğ•] {s : set E} {x : E}

lemma strict_convex.smul (hs : strict_convex ğ•œ s) (c : ğ•) : strict_convex ğ•œ (c â€¢ s) :=
begin
  obtain rfl | hc := eq_or_ne c 0,
  { exact (subsingleton_zero_smul_set _).strict_convex },
  { exact hs.linear_image (linear_map.lsmul _ _ c) (is_open_map_smulâ‚€ hc) }
end

lemma strict_convex.affinity [has_continuous_add E] (hs : strict_convex ğ•œ s) (z : E) (c : ğ•) :
  strict_convex ğ•œ (z +áµ¥ c â€¢ s) :=
(hs.smul c).vadd z

end continuous_smul
end add_comm_group
end ordered_semiring

section ordered_comm_semiring
variables [ordered_comm_semiring ğ•œ] [topological_space E]

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] [no_zero_smul_divisors ğ•œ E]
  [has_continuous_const_smul ğ•œ E] {s : set E}

lemma strict_convex.preimage_smul (hs : strict_convex ğ•œ s) (c : ğ•œ) :
  strict_convex ğ•œ ((Î» z, c â€¢ z) â»Â¹' s) :=
begin
  classical,
  obtain rfl | hc := eq_or_ne c 0,
  { simp_rw [zero_smul, preimage_const],
    split_ifs,
    { exact strict_convex_univ },
    { exact strict_convex_empty } },
  refine hs.linear_preimage (linear_map.lsmul _ _ c) _ (smul_right_injective E hc),
  unfold linear_map.lsmul linear_map.mkâ‚‚ linear_map.mkâ‚‚' linear_map.mkâ‚‚'â‚›â‚—,
  exact continuous_const_smul _,
end

end add_comm_group
end ordered_comm_semiring

section ordered_ring
variables [ordered_ring ğ•œ] [topological_space E] [topological_space F]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {s t : set E} {x y : E}

lemma strict_convex.eq_of_open_segment_subset_frontier [nontrivial ğ•œ] [densely_ordered ğ•œ]
  (hs : strict_convex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s) (h : open_segment ğ•œ x y âŠ† frontier s) :
  x = y :=
begin
  obtain âŸ¨a, haâ‚€, haâ‚âŸ© := densely_ordered.dense (0 : ğ•œ) 1 zero_lt_one,
  classical,
  by_contra hxy,
  exact (h âŸ¨a, 1 - a, haâ‚€, sub_pos_of_lt haâ‚, add_sub_cancel'_right _ _, rflâŸ©).2
    (hs hx hy hxy haâ‚€ (sub_pos_of_lt haâ‚) $ add_sub_cancel'_right _ _),
end

lemma strict_convex.add_smul_mem (hs : strict_convex ğ•œ s) (hx : x âˆˆ s) (hxy : x + y âˆˆ s)
  (hy : y â‰  0) {t : ğ•œ} (htâ‚€ : 0 < t) (htâ‚ : t < 1) :
  x + t â€¢ y âˆˆ interior s :=
begin
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y),
  { rw [smul_add, â†add_assoc, â†add_smul, sub_add_cancel, one_smul] },
  rw h,
  refine hs hx hxy (Î» h, hy $ add_left_cancel _) (sub_pos_of_lt htâ‚) htâ‚€ (sub_add_cancel _ _),
  exact x,
  rw [â†h, add_zero],
end

lemma strict_convex.smul_mem_of_zero_mem (hs : strict_convex ğ•œ s) (zero_mem : (0 : E) âˆˆ s)
  (hx : x âˆˆ s) (hxâ‚€ : x â‰  0) {t : ğ•œ} (htâ‚€ : 0 < t) (htâ‚ : t < 1) :
  t â€¢ x âˆˆ interior s :=
by simpa using hs.add_smul_mem zero_mem (by simpa using hx) hxâ‚€ htâ‚€ htâ‚

lemma strict_convex.add_smul_sub_mem (h : strict_convex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s) (hxy : x â‰  y)
  {t : ğ•œ} (htâ‚€ : 0 < t) (htâ‚ : t < 1) : x + t â€¢ (y - x) âˆˆ interior s :=
begin
  apply h.open_segment_subset hx hy hxy,
  rw open_segment_eq_image',
  exact mem_image_of_mem _ âŸ¨htâ‚€, htâ‚âŸ©,
end

/-- The preimage of a strictly convex set under an affine map is strictly convex. -/
lemma strict_convex.affine_preimage {s : set F} (hs : strict_convex ğ•œ s) {f : E â†’áµƒ[ğ•œ] F}
  (hf : continuous f) (hfinj : injective f) :
  strict_convex ğ•œ (f â»Â¹' s) :=
begin
  intros x hx y hy hxy a b ha hb hab,
  refine preimage_interior_subset_interior_preimage hf _,
  rw [mem_preimage, convex.combo_affine_apply hab],
  exact hs hx hy (hfinj.ne hxy) ha hb hab,
end

/-- The image of a strictly convex set under an affine map is strictly convex. -/
lemma strict_convex.affine_image (hs : strict_convex ğ•œ s) {f : E â†’áµƒ[ğ•œ] F} (hf : is_open_map f) :
  strict_convex ğ•œ (f '' s) :=
begin
  rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© hxy a b ha hb hab,
  exact hf.image_interior_subset _ âŸ¨a â€¢ x + b â€¢ y, âŸ¨hs hx hy (ne_of_apply_ne _ hxy) ha hb hab,
    convex.combo_affine_apply habâŸ©âŸ©,
end

variables [topological_add_group E]

lemma strict_convex.neg (hs : strict_convex ğ•œ s) : strict_convex ğ•œ (-s) :=
hs.is_linear_preimage is_linear_map.is_linear_map_neg continuous_id.neg neg_injective

lemma strict_convex.sub (hs : strict_convex ğ•œ s) (ht : strict_convex ğ•œ t) :
  strict_convex ğ•œ (s - t) :=
(sub_eq_add_neg s t).symm â–¸ hs.add ht.neg

end add_comm_group
end ordered_ring

section linear_ordered_field
variables [linear_ordered_field ğ•œ] [topological_space E]

section add_comm_group
variables [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F] {s : set E} {x : E}

/-- Alternative definition of set strict convexity, using division. -/
lemma strict_convex_iff_div :
  strict_convex ğ•œ s â†” s.pairwise
    (Î» x y, âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ interior s) :=
âŸ¨Î» h x hx y hy hxy a b ha hb, begin
  apply h hx hy hxy (div_pos ha $ add_pos ha hb) (div_pos hb $ add_pos ha hb),
  rw â†add_div,
  exact div_self (add_pos ha hb).ne',
end, Î» h x hx y hy hxy a b ha hb hab, by convert h hx hy hxy ha hb; rw [hab, div_one] âŸ©

lemma strict_convex.mem_smul_of_zero_mem (hs : strict_convex ğ•œ s) (zero_mem : (0 : E) âˆˆ s)
  (hx : x âˆˆ s) (hxâ‚€ : x â‰  0) {t : ğ•œ} (ht : 1 < t) :
  x âˆˆ t â€¢ interior s :=
begin
  rw mem_smul_set_iff_inv_smul_memâ‚€ (zero_lt_one.trans ht).ne',
  exact hs.smul_mem_of_zero_mem zero_mem hx hxâ‚€ (inv_pos.2 $ zero_lt_one.trans ht)  (inv_lt_one ht),
end

end add_comm_group
end linear_ordered_field

/-!
#### Convex sets in an ordered space

Relates `convex` and `set.ord_connected`.
-/

section
variables [topological_space E]

/-- A set in a linear ordered field is strictly convex if and only if it is convex. -/
@[simp] lemma strict_convex_iff_convex [linear_ordered_field ğ•œ] [topological_space ğ•œ]
  [order_topology ğ•œ] {s : set ğ•œ} :
  strict_convex ğ•œ s â†” convex ğ•œ s :=
begin
  refine âŸ¨strict_convex.convex, Î» hs, strict_convex_iff_open_segment_subset.2 (Î» x hx y hy hxy, _)âŸ©,
  obtain h | h := hxy.lt_or_lt,
  { refine (open_segment_subset_Ioo h).trans _,
    rw â†interior_Icc,
    exact interior_mono (Icc_subset_segment.trans $ hs.segment_subset hx hy) },
  { rw open_segment_symm,
    refine (open_segment_subset_Ioo h).trans _,
    rw â†interior_Icc,
    exact interior_mono (Icc_subset_segment.trans $ hs.segment_subset hy hx) }
end

lemma strict_convex_iff_ord_connected [linear_ordered_field ğ•œ] [topological_space ğ•œ]
  [order_topology ğ•œ] {s : set ğ•œ} :
  strict_convex ğ•œ s â†” s.ord_connected :=
strict_convex_iff_convex.trans convex_iff_ord_connected

alias strict_convex_iff_ord_connected â†” strict_convex.ord_connected _

end
