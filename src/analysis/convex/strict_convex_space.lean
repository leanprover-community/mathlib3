/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import analysis.convex.strict
import analysis.convex.topology
import analysis.normed_space.ordered
import analysis.normed_space.pointwise

/-!
# Strictly convex spaces

This file defines strictly convex spaces. A normed space is strictly convex if all closed balls are
strictly convex. This does **not** mean that the norm is strictly convex (in fact, it never is).
-/

open metric
open_locale convex pointwise

/-- A *strictly convex space* is a normed space where the closed balls are strictly convex. We only
require balls of positive radius with center at the origin to be strictly convex in the definition,
then prove that any closed ball is strictly convex in `strict_convex_closed_ball` below.

See also `strict_convex_space.of_strict_convex_closed_unit_ball`. -/
class strict_convex_space (ğ•œ E : Type*) [normed_linear_ordered_field ğ•œ] [normed_group E]
  [normed_space ğ•œ E] : Prop :=
(strict_convex_closed_ball : âˆ€ r : â„, 0 < r â†’ strict_convex ğ•œ (closed_ball (0 : E) r))

variables (ğ•œ : Type*) {E : Type*} [normed_linear_ordered_field ğ•œ]

variables [normed_group E] [normed_space ğ•œ E]

/-- A closed ball in a strictly convex space is strictly convex. -/
lemma strict_convex_closed_ball [strict_convex_space ğ•œ E] (x : E) (r : â„) :
  strict_convex ğ•œ (closed_ball x r) :=
begin
  cases le_or_lt r 0 with hr hr,
  { exact (subsingleton_closed_ball x hr).strict_convex },
  rw â†vadd_closed_ball_zero,
  exact (strict_convex_space.strict_convex_closed_ball r hr).vadd _,
end

variables [normed_space â„ E]

/-- A real normed vector space is strictly convex provided that the unit ball is strictly convex. -/
lemma strict_convex_space.of_strict_convex_closed_unit_ball
  [linear_map.compatible_smul E E ğ•œ â„] (h : strict_convex ğ•œ (closed_ball (0 : E) 1)) :
  strict_convex_space ğ•œ E :=
âŸ¨Î» r hr, by simpa only [smul_closed_unit_ball_of_nonneg hr.le] using h.smul râŸ©

lemma strict_convex_space.of_norm_add
  (h : âˆ€ x y : E, x â‰  0 â†’ y â‰  0 â†’ âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ + âˆ¥yâˆ¥ â†’ âˆ¥xâˆ¥ â€¢ y = âˆ¥yâˆ¥ â€¢ x) :
  strict_convex_space â„ E :=
begin
  refine strict_convex_space.of_strict_convex_closed_unit_ball â„ (Î» x hx y hy hne a b ha hb hab, _),
  have hx' := hx, have hy' := hy,
  rw [â†closure_closed_ball, closure_eq_interior_union_frontier,
    frontier_closed_ball (0 : E) one_ne_zero] at hx hy,
  cases hx, { exact (convex_closed_ball _ _).combo_mem_interior_left hx hy' ha hb.le hab },
  cases hy, { exact (convex_closed_ball _ _).combo_mem_interior_right hx' hy ha.le hb hab },
  rw [interior_closed_ball (0 : E) one_ne_zero, mem_ball_zero_iff],
  have hxâ‚ : âˆ¥xâˆ¥ = 1, from mem_sphere_zero_iff_norm.1 hx,
  have hyâ‚ : âˆ¥yâˆ¥ = 1, from mem_sphere_zero_iff_norm.1 hy,
  have ha' : âˆ¥aâˆ¥ = a, from real.norm_of_nonneg ha.le,
  have hb' : âˆ¥bâˆ¥ = b, from real.norm_of_nonneg hb.le,
  calc âˆ¥a â€¢ x + b â€¢ yâˆ¥ < âˆ¥a â€¢ xâˆ¥ + âˆ¥b â€¢ yâˆ¥ : (norm_add_le _ _).lt_of_ne (Î» H, hne _)
  ... = 1 : by simpa only [norm_smul, hxâ‚, hyâ‚, mul_one, ha', hb'],
  have : âˆ¥a â€¢ xâˆ¥ â€¢ b â€¢ y = âˆ¥b â€¢ yâˆ¥ â€¢ a â€¢ x,
    from h (a â€¢ x) (b â€¢ y) (smul_ne_zero.2 âŸ¨ha.ne', ne_of_mem_sphere hx one_ne_zeroâŸ©)
      (smul_ne_zero.2 âŸ¨hb.ne', ne_of_mem_sphere hy one_ne_zeroâŸ©) H,
  simpa only [norm_smul, hxâ‚, hyâ‚, ha', hb', mul_one, smul_comm a, smul_right_inj ha.ne',
    smul_right_inj hb.ne'] using this.symm
end

variables [strict_convex_space â„ E] {x y z : E} {a b r : â„}

lemma norm_combo_lt_of_ne (hx : âˆ¥xâˆ¥ â‰¤ r) (hy : âˆ¥yâˆ¥ â‰¤ r) (hxy : x â‰  y) (ha : 0 < a) (hb : 0 < b)
  (hab : a + b = 1) : âˆ¥a â€¢ x + b â€¢ yâˆ¥ < r :=
begin
  have hr : r â‰  0,
  { rintro rfl,
    rw [norm_le_zero_iff] at hx hy,
    exact hxy (hx.trans hy.symm) },
  simp only [â†mem_closed_ball_zero_iff, â†mem_ball_zero_iff, â†interior_closed_ball _ hr]
    at hx hy âŠ¢,
  exact strict_convex_closed_ball â„ (0 : E) r hx hy hxy ha hb hab
end

/-- In a strictly convex space, if `x` and `y` do not have the same direction, then
`âˆ¥x + yâˆ¥ < âˆ¥xâˆ¥ + âˆ¥yâˆ¥`. See also `norm_add_lt_of_ne`. -/
lemma norm_add_lt_of_div_norm_ne (hx : x â‰  0) (hy : y â‰  0) (h : âˆ¥xâˆ¥â»Â¹ â€¢ x â‰  âˆ¥yâˆ¥â»Â¹ â€¢ y) :
  âˆ¥x + yâˆ¥ < âˆ¥xâˆ¥ + âˆ¥yâˆ¥ :=
begin
  rw â†norm_pos_iff at hx hy,
  rw [â†div_lt_one (add_pos hx hy)],
  simpa [interior_closed_ball _ one_ne_zero, smul_smul, div_eq_inv_mul,
    mul_inv_cancel_rightâ‚€ hx.ne', mul_inv_cancel_rightâ‚€ hy.ne', â†smul_add, norm_smul,
    real.norm_of_nonneg (add_pos hx hy).le]
    using strict_convex_iff_div.1 (strict_convex_closed_ball â„ (0 : E) 1)
      (inv_norm_smul_mem_closed_unit_ball x) (inv_norm_smul_mem_closed_unit_ball y) h hx hy,
end

/-- In a strictly convex space, if `x` and `y` do not have the same direction, then
`âˆ¥x + yâˆ¥ < âˆ¥xâˆ¥ + âˆ¥yâˆ¥`. See also `norm_add_lt_of_div_norm_ne`. -/
lemma norm_add_lt_of_ne (h : âˆ¥xâˆ¥ â€¢ y â‰  âˆ¥yâˆ¥ â€¢ x) : âˆ¥x + yâˆ¥ < âˆ¥xâˆ¥ + âˆ¥yâˆ¥ :=
begin
  have hx : x â‰  0, { rintro rfl, simpa using h },
  have hy : y â‰  0, { rintro rfl, simpa using h },
  refine norm_add_lt_of_div_norm_ne hx hy _,
  rw â†norm_ne_zero_iff at hx hy,
  rwa [ne.def, â†smul_right_inj hx, smul_inv_smulâ‚€ hx, smul_comm, â†smul_right_inj hy,
    smul_inv_smulâ‚€ hy, eq_comm]; apply_instance
end

/-- In a strictly convex space, `âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ + âˆ¥yâˆ¥` if and only if `x` and `y` are -/
lemma norm_add_eq_iff : âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ + âˆ¥yâˆ¥ â†” âˆ¥xâˆ¥ â€¢ y = âˆ¥yâˆ¥ â€¢ x :=
âŸ¨not_imp_not.1 $ Î» h, (norm_add_lt_of_ne h).ne, norm_add_eq_of_norm_smul_eqâŸ©

/-- In a strictly convex space, the triangle inequality turns into an equality if and only if the
middle point belongs to the segment joining two other points. -/
lemma dist_add_dist_eq_iff : dist x y + dist y z = dist x z â†” y âˆˆ [x -[â„] z] :=
begin
  refine âŸ¨_, dist_add_dist_of_mem_segmentâŸ©, intro h,
  simp only [dist_eq_norm, â†sub_add_sub_cancel x y z, eq_comm.trans norm_add_eq_iff] at h,
  rcases eq_or_ne x y with rfl|hx, { apply left_mem_segment },
  rcases eq_or_ne y z with rfl|hy, { apply right_mem_segment },
  rw [â†sub_ne_zero, â†norm_pos_iff] at hx hy,
  rw [â†mem_segment_translate â„ (-y), add_left_neg, â†sub_eq_neg_add, â†sub_eq_neg_add],
  refine mem_segment_iff_div.2 âŸ¨âˆ¥y - zâˆ¥, âˆ¥x - yâˆ¥, hy.le, hx.le, add_pos hy hx, _âŸ©,
  simp only [div_eq_inv_mul, mul_smul, â†h, â†smul_add, sub_add_sub_cancel', sub_self, smul_zero]
end
