/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov
-/
import analysis.convex.combination
import analysis.convex.strict
import topology.path_connected
import topology.algebra.affine
import topology.algebra.module.basic

/-!
# Topological properties of convex sets

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We prove the following facts:

* `convex.interior` : interior of a convex set is convex;
* `convex.closure` : closure of a convex set is convex;
* `set.finite.compact_convex_hull` : convex hull of a finite set is compact;
* `set.finite.is_closed_convex_hull` : convex hull of a finite set is closed.
-/

assert_not_exists has_norm

open metric set
open_locale pointwise convex

variables {Î¹ ğ•œ E : Type*}

lemma real.convex_iff_is_preconnected {s : set â„} : convex â„ s â†” is_preconnected s :=
convex_iff_ord_connected.trans is_preconnected_iff_ord_connected.symm

alias real.convex_iff_is_preconnected â†” _ is_preconnected.convex

/-! ### Standard simplex -/

section std_simplex

variables [fintype Î¹]

/-- Every vector in `std_simplex ğ•œ Î¹` has `max`-norm at most `1`. -/
lemma std_simplex_subset_closed_ball :
  std_simplex â„ Î¹ âŠ† metric.closed_ball 0 1 :=
begin
  assume f hf,
  rw [metric.mem_closed_ball, dist_pi_le_iff zero_le_one],
  intros x,
  rw [pi.zero_apply, real.dist_0_eq_abs, abs_of_nonneg $ hf.1 x],
  exact (mem_Icc_of_mem_std_simplex hf x).2,
end

variable (Î¹)

/-- `std_simplex â„ Î¹` is bounded. -/
lemma bounded_std_simplex : metric.bounded (std_simplex â„ Î¹) :=
(metric.bounded_iff_subset_ball 0).2 âŸ¨1, std_simplex_subset_closed_ballâŸ©

/-- `std_simplex â„ Î¹` is closed. -/
lemma is_closed_std_simplex : is_closed (std_simplex â„ Î¹) :=
(std_simplex_eq_inter â„ Î¹).symm â–¸ is_closed.inter
  (is_closed_Inter $ Î» i, is_closed_le continuous_const (continuous_apply i))
  (is_closed_eq (continuous_finset_sum _ $ Î» x _, continuous_apply x) continuous_const)

/-- `std_simplex â„ Î¹` is compact. -/
lemma is_compact_std_simplex : is_compact (std_simplex â„ Î¹) :=
metric.is_compact_iff_is_closed_bounded.2 âŸ¨is_closed_std_simplex Î¹, bounded_std_simplex Î¹âŸ©

end std_simplex

/-! ### Topological vector space -/

section topological_space
variables [linear_ordered_ring ğ•œ] [densely_ordered ğ•œ] [topological_space ğ•œ] [order_topology ğ•œ]
  [add_comm_group E] [topological_space E] [has_continuous_add E] [module ğ•œ E]
  [has_continuous_smul ğ•œ E] {x y : E}

lemma segment_subset_closure_open_segment : [x -[ğ•œ] y] âŠ† closure (open_segment ğ•œ x y) :=
begin
  rw [segment_eq_image, open_segment_eq_image, â†closure_Ioo (zero_ne_one' ğ•œ)],
  exact image_closure_subset_closure_image (by continuity),
end

end topological_space

section pseudo_metric_space
variables [linear_ordered_ring ğ•œ] [densely_ordered ğ•œ] [pseudo_metric_space ğ•œ] [order_topology ğ•œ]
  [proper_space ğ•œ] [compact_Icc_space ğ•œ] [add_comm_group E] [topological_space E] [t2_space E]
  [has_continuous_add E] [module ğ•œ E] [has_continuous_smul ğ•œ E]

@[simp] lemma closure_open_segment (x y : E) : closure (open_segment ğ•œ x y) = [x -[ğ•œ] y] :=
begin
  rw [segment_eq_image, open_segment_eq_image, â†closure_Ioo (zero_ne_one' ğ•œ)],
  exact (image_closure_of_is_compact (bounded_Ioo _ _).is_compact_closure $
    continuous.continuous_on $ by continuity).symm,
end

end pseudo_metric_space

section has_continuous_const_smul

variables [linear_ordered_field ğ•œ] [add_comm_group E] [module ğ•œ E] [topological_space E]
  [topological_add_group E] [has_continuous_const_smul ğ•œ E]

/-- If `s` is a convex set, then `a â€¢ interior s + b â€¢ closure s âŠ† interior s` for all `0 < a`,
`0 â‰¤ b`, `a + b = 1`. See also `convex.combo_interior_self_subset_interior` for a weaker version. -/
lemma convex.combo_interior_closure_subset_interior {s : set E} (hs : convex ğ•œ s) {a b : ğ•œ}
  (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  a â€¢ interior s + b â€¢ closure s âŠ† interior s :=
interior_smulâ‚€ ha.ne' s â–¸
  calc interior (a â€¢ s) + b â€¢ closure s âŠ† interior (a â€¢ s) + closure (b â€¢ s) :
    add_subset_add subset.rfl (smul_closure_subset b s)
  ... = interior (a â€¢ s) + b â€¢ s : by rw is_open_interior.add_closure (b â€¢ s)
  ... âŠ† interior (a â€¢ s + b â€¢ s) : subset_interior_add_left
  ... âŠ† interior s : interior_mono $ hs.set_combo_subset ha.le hb hab

/-- If `s` is a convex set, then `a â€¢ interior s + b â€¢ s âŠ† interior s` for all `0 < a`, `0 â‰¤ b`,
`a + b = 1`. See also `convex.combo_interior_closure_subset_interior` for a stronger version. -/
lemma convex.combo_interior_self_subset_interior {s : set E} (hs : convex ğ•œ s) {a b : ğ•œ}
  (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  a â€¢ interior s + b â€¢ s âŠ† interior s :=
calc a â€¢ interior s + b â€¢ s âŠ† a â€¢ interior s + b â€¢ closure s :
  add_subset_add subset.rfl $ image_subset _ subset_closure
... âŠ† interior s : hs.combo_interior_closure_subset_interior ha hb hab

/-- If `s` is a convex set, then `a â€¢ closure s + b â€¢ interior s âŠ† interior s` for all `0 â‰¤ a`,
`0 < b`, `a + b = 1`. See also `convex.combo_self_interior_subset_interior` for a weaker version. -/
lemma convex.combo_closure_interior_subset_interior {s : set E} (hs : convex ğ•œ s) {a b : ğ•œ}
  (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) :
  a â€¢ closure s + b â€¢ interior s âŠ† interior s :=
by { rw add_comm, exact hs.combo_interior_closure_subset_interior hb ha (add_comm a b â–¸ hab) }

/-- If `s` is a convex set, then `a â€¢ s + b â€¢ interior s âŠ† interior s` for all `0 â‰¤ a`, `0 < b`,
`a + b = 1`. See also `convex.combo_closure_interior_subset_interior` for a stronger version. -/
lemma convex.combo_self_interior_subset_interior {s : set E} (hs : convex ğ•œ s) {a b : ğ•œ}
  (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) :
  a â€¢ s + b â€¢ interior s âŠ† interior s :=
by { rw add_comm, exact hs.combo_interior_self_subset_interior hb ha (add_comm a b â–¸ hab) }

lemma convex.combo_interior_closure_mem_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ interior s) (hy : y âˆˆ closure s) {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  a â€¢ x + b â€¢ y âˆˆ interior s :=
hs.combo_interior_closure_subset_interior ha hb hab $
  add_mem_add (smul_mem_smul_set hx) (smul_mem_smul_set hy)

lemma convex.combo_interior_self_mem_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ interior s) (hy : y âˆˆ s) {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  a â€¢ x + b â€¢ y âˆˆ interior s :=
hs.combo_interior_closure_mem_interior hx (subset_closure hy) ha hb hab

lemma convex.combo_closure_interior_mem_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ closure s) (hy : y âˆˆ interior s) {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) :
  a â€¢ x + b â€¢ y âˆˆ interior s :=
hs.combo_closure_interior_subset_interior ha hb hab $
  add_mem_add (smul_mem_smul_set hx) (smul_mem_smul_set hy)

lemma convex.combo_self_interior_mem_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ s) (hy : y âˆˆ interior s) {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) :
  a â€¢ x + b â€¢ y âˆˆ interior s :=
hs.combo_closure_interior_mem_interior (subset_closure hx) hy ha hb hab

lemma convex.open_segment_interior_closure_subset_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ interior s) (hy : y âˆˆ closure s) : open_segment ğ•œ x y âŠ† interior s :=
begin
  rintro _ âŸ¨a, b, ha, hb, hab, rflâŸ©,
  exact hs.combo_interior_closure_mem_interior hx hy ha hb.le hab
end

lemma convex.open_segment_interior_self_subset_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ interior s) (hy : y âˆˆ s) : open_segment ğ•œ x y âŠ† interior s :=
hs.open_segment_interior_closure_subset_interior hx (subset_closure hy)

lemma convex.open_segment_closure_interior_subset_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ closure s) (hy : y âˆˆ interior s) : open_segment ğ•œ x y âŠ† interior s :=
begin
  rintro _ âŸ¨a, b, ha, hb, hab, rflâŸ©,
  exact hs.combo_closure_interior_mem_interior hx hy ha.le hb hab
end

lemma convex.open_segment_self_interior_subset_interior {s : set E} (hs : convex ğ•œ s) {x y : E}
  (hx : x âˆˆ s) (hy : y âˆˆ interior s) : open_segment ğ•œ x y âŠ† interior s :=
hs.open_segment_closure_interior_subset_interior (subset_closure hx) hy

/-- If `x âˆˆ closure s` and `y âˆˆ interior s`, then the segment `(x, y]` is included in `interior s`.
-/
lemma convex.add_smul_sub_mem_interior' {s : set E} (hs : convex ğ•œ s)
  {x y : E} (hx : x âˆˆ closure s) (hy : y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) :
  x + t â€¢ (y - x) âˆˆ interior s :=
by simpa only [sub_smul, smul_sub, one_smul, add_sub, add_comm]
  using hs.combo_interior_closure_mem_interior hy hx ht.1 (sub_nonneg.mpr ht.2)
    (add_sub_cancel'_right _ _)

/-- If `x âˆˆ s` and `y âˆˆ interior s`, then the segment `(x, y]` is included in `interior s`. -/
lemma convex.add_smul_sub_mem_interior {s : set E} (hs : convex ğ•œ s)
  {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) :
  x + t â€¢ (y - x) âˆˆ interior s :=
hs.add_smul_sub_mem_interior' (subset_closure hx) hy ht

/-- If `x âˆˆ closure s` and `x + y âˆˆ interior s`, then `x + t y âˆˆ interior s` for `t âˆˆ (0, 1]`. -/
lemma convex.add_smul_mem_interior' {s : set E} (hs : convex ğ•œ s)
  {x y : E} (hx : x âˆˆ closure s) (hy : x + y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) :
  x + t â€¢ y âˆˆ interior s :=
by simpa only [add_sub_cancel'] using hs.add_smul_sub_mem_interior' hx hy ht

/-- If `x âˆˆ s` and `x + y âˆˆ interior s`, then `x + t y âˆˆ interior s` for `t âˆˆ (0, 1]`. -/
lemma convex.add_smul_mem_interior {s : set E} (hs : convex ğ•œ s)
  {x y : E} (hx : x âˆˆ s) (hy : x + y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) :
  x + t â€¢ y âˆˆ interior s :=
hs.add_smul_mem_interior' (subset_closure hx) hy ht

/-- In a topological vector space, the interior of a convex set is convex. -/
protected lemma convex.interior {s : set E} (hs : convex ğ•œ s) : convex ğ•œ (interior s) :=
convex_iff_open_segment_subset.mpr $ Î» x hx y hy,
  hs.open_segment_closure_interior_subset_interior (interior_subset_closure hx) hy

/-- In a topological vector space, the closure of a convex set is convex. -/
protected lemma convex.closure {s : set E} (hs : convex ğ•œ s) : convex ğ•œ (closure s) :=
Î» x hx y hy a b ha hb hab,
let f : E â†’ E â†’ E := Î» x' y', a â€¢ x' + b â€¢ y' in
have hf : continuous (function.uncurry f),
  from (continuous_fst.const_smul _).add (continuous_snd.const_smul _),
show f x y âˆˆ closure s,
  from map_mem_closureâ‚‚ hf hx hy (Î» x' hx' y' hy', hs hx' hy' ha hb hab)

open affine_map

/-- A convex set `s` is strictly convex provided that for any two distinct points of
`s \ interior s`, the line passing through these points has nonempty intersection with
`interior s`. -/
protected lemma convex.strict_convex' {s : set E} (hs : convex ğ•œ s)
  (h : (s \ interior s).pairwise $ Î» x y, âˆƒ c : ğ•œ, line_map x y c âˆˆ interior s) :
  strict_convex ğ•œ s :=
begin
  refine strict_convex_iff_open_segment_subset.2 _,
  intros x hx y hy hne,
  by_cases hx' : x âˆˆ interior s, { exact hs.open_segment_interior_self_subset_interior hx' hy },
  by_cases hy' : y âˆˆ interior s, { exact hs.open_segment_self_interior_subset_interior hx hy' },
  rcases h âŸ¨hx, hx'âŸ© âŸ¨hy, hy'âŸ© hne with âŸ¨c, hcâŸ©,
  refine (open_segment_subset_union x y âŸ¨c, rflâŸ©).trans (insert_subset.2 âŸ¨hc, union_subset _ _âŸ©),
  exacts [hs.open_segment_self_interior_subset_interior hx hc,
    hs.open_segment_interior_self_subset_interior hc hy]
end

/-- A convex set `s` is strictly convex provided that for any two distinct points `x`, `y` of
`s \ interior s`, the segment with endpoints `x`, `y` has nonempty intersection with
`interior s`. -/
protected lemma convex.strict_convex {s : set E} (hs : convex ğ•œ s)
  (h : (s \ interior s).pairwise $ Î» x y, ([x -[ğ•œ] y] \ frontier s).nonempty) :
  strict_convex ğ•œ s :=
begin
  refine (hs.strict_convex' $ h.imp_on $ Î» x hx y hy hne, _),
  simp only [segment_eq_image_line_map, â† self_diff_frontier],
  rintro âŸ¨_, âŸ¨âŸ¨c, hc, rflâŸ©, hcsâŸ©âŸ©,
  refine âŸ¨c, hs.segment_subset hx.1 hy.1 _, hcsâŸ©,
  exact (segment_eq_image_line_map ğ•œ x y).symm â–¸ mem_image_of_mem _ hc
end

end has_continuous_const_smul

section has_continuous_smul

variables [add_comm_group E] [module â„ E] [topological_space E]
  [topological_add_group E] [has_continuous_smul â„ E]

/-- Convex hull of a finite set is compact. -/
lemma set.finite.compact_convex_hull {s : set E} (hs : s.finite) :
  is_compact (convex_hull â„ s) :=
begin
  rw [hs.convex_hull_eq_image],
  apply (is_compact_std_simplex _).image,
  haveI := hs.fintype,
  apply linear_map.continuous_on_pi
end

/-- Convex hull of a finite set is closed. -/
lemma set.finite.is_closed_convex_hull [t2_space E] {s : set E} (hs : s.finite) :
  is_closed (convex_hull â„ s) :=
hs.compact_convex_hull.is_closed

open affine_map

/-- If we dilate the interior of a convex set about a point in its interior by a scale `t > 1`,
the result includes the closure of the original set.

TODO Generalise this from convex sets to sets that are balanced / star-shaped about `x`. -/
lemma convex.closure_subset_image_homothety_interior_of_one_lt {s : set E} (hs : convex â„ s)
  {x : E} (hx : x âˆˆ interior s) (t : â„) (ht : 1 < t) :
  closure s âŠ† homothety x t '' interior s :=
begin
  intros y hy,
  have hne : t â‰  0, from (one_pos.trans ht).ne',
  refine âŸ¨homothety x tâ»Â¹ y, hs.open_segment_interior_closure_subset_interior hx hy _,
    (affine_equiv.homothety_units_mul_hom x (units.mk0 t hne)).apply_symm_apply yâŸ©,
  rw [open_segment_eq_image_line_map, â† inv_one, â† inv_Ioi (zero_lt_one' â„), â† image_inv,
    image_image, homothety_eq_line_map],
  exact mem_image_of_mem _ ht
end

/-- If we dilate a convex set about a point in its interior by a scale `t > 1`, the interior of
the result includes the closure of the original set.

TODO Generalise this from convex sets to sets that are balanced / star-shaped about `x`. -/
lemma convex.closure_subset_interior_image_homothety_of_one_lt {s : set E} (hs : convex â„ s)
  {x : E} (hx : x âˆˆ interior s) (t : â„) (ht : 1 < t) :
  closure s âŠ† interior (homothety x t '' s) :=
(hs.closure_subset_image_homothety_interior_of_one_lt hx t ht).trans $
  (homothety_is_open_map x t (one_pos.trans ht).ne').image_interior_subset _

/-- If we dilate a convex set about a point in its interior by a scale `t > 1`, the interior of
the result includes the closure of the original set.

TODO Generalise this from convex sets to sets that are balanced / star-shaped about `x`. -/
lemma convex.subset_interior_image_homothety_of_one_lt {s : set E} (hs : convex â„ s)
  {x : E} (hx : x âˆˆ interior s) (t : â„) (ht : 1 < t) :
  s âŠ† interior (homothety x t '' s) :=
subset_closure.trans $ hs.closure_subset_interior_image_homothety_of_one_lt hx t ht

/-- A nonempty convex set is path connected. -/
protected lemma convex.is_path_connected {s : set E} (hconv : convex â„ s) (hne : s.nonempty) :
  is_path_connected s :=
begin
  refine is_path_connected_iff.mpr âŸ¨hne, _âŸ©,
  intros x x_in y y_in,
  have H := hconv.segment_subset x_in y_in,
  rw segment_eq_image_line_map at H,
  exact joined_in.of_line affine_map.line_map_continuous.continuous_on (line_map_apply_zero _ _)
    (line_map_apply_one _ _) H
end

/-- A nonempty convex set is connected. -/
protected lemma convex.is_connected {s : set E} (h : convex â„ s) (hne : s.nonempty) :
  is_connected s :=
(h.is_path_connected hne).is_connected

/-- A convex set is preconnected. -/
protected lemma convex.is_preconnected {s : set E} (h : convex â„ s) : is_preconnected s :=
s.eq_empty_or_nonempty.elim (Î» h, h.symm â–¸ is_preconnected_empty)
  (Î» hne, (h.is_connected hne).is_preconnected)

/--
Every topological vector space over â„ is path connected.

Not an instance, because it creates enormous TC subproblems (turn on `pp.all`).
-/
protected lemma topological_add_group.path_connected : path_connected_space E :=
path_connected_space_iff_univ.mpr $ convex_univ.is_path_connected âŸ¨(0 : E), trivialâŸ©

end has_continuous_smul
