/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import measure_theory.group.integration
import measure_theory.group.prod
import measure_theory.function.locally_integrable
import analysis.calculus.bump_function_inner
import measure_theory.integral.interval_integral
import analysis.calculus.parametric_integral

/-!
# Convolution of functions

This file defines the convolution on two functions, i.e. `x ‚Ü¶ ‚à´ f(t)g(x - t) ‚àÇt`.
In the general case, these functions can be vector-valued, and have an arbitrary (additive)
group as domain. We use a continuous bilinear operation `L` on these function values as
"multiplication". The domain must be equipped with a Haar measure `Œº`
(though many individual results have weaker conditions on `Œº`).

For many applications we can take `L = lsmul ‚Ñù ‚Ñù` or `L = mul ‚Ñù ‚Ñù`.

We also define `convolution_exists` and `convolution_exists_at` to state that the convolution is
well-defined (everywhere or at a single point). These conditions are needed for pointwise
computations (e.g. `convolution_exists_at.distrib_add`), but are generally not stong enough for any
local (or global) properties of the convolution. For this we need stronger assumptions on `f`
and/or `g`, and generally if we impose stronger conditions on one of the functions, we can impose
weaker conditions on the other.
We have proven many of the properties of the convolution assuming one of these functions
has compact support (in which case the other function only needs to be locally integrable).
We still need to prove the properties for other pairs of conditions (e.g. both functions are
rapidly decreasing)

# Design Decisions

We use a bilinear map `L` to "multiply" the two functions in the integrand.
This generality has several advantages

* This allows us to compute the total derivative of the convolution, in case the functions are
  multivariate. The total derivative is again a convolution, but where the codomains of the
  functions can be higher-dimensional. See `has_compact_support.has_fderiv_at_convolution_right`.
* This allows us to use `@[to_additive]` everywhere (which would not be possible if we would use
  `mul`/`smul` in the integral, since `@[to_additive]` will incorrectly also try to additivize
  those definitions).
* We need to support the case where at least one of the functions is vector-valued, but if we use
  `smul` to multiply the functions, that would be an asymmetric definition.

# Main Definitions
* `convolution f g L Œº x = (f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº` is the convolution of
  `f` and `g` w.r.t. the continuous bilinear map `L` and measure `Œº`.
* `convolution_exists_at f g x L Œº` states that the convolution `(f ‚ãÜ[L, Œº] g) x` is well-defined
  (i.e. the integral exists).
* `convolution_exists f g L Œº` states that the convolution `f ‚ãÜ[L, Œº] g` is well-defined at each
  point.

# Main Results
* `has_compact_support.has_fderiv_at_convolution_right` and
  `has_compact_support.has_fderiv_at_convolution_left`: we can compute the total derivative
  of the convolution as a convolution with the total derivative of the right (left) function.
* `has_compact_support.cont_diff_convolution_right` and
  `has_compact_support.cont_diff_convolution_left`: the convolution is `ùíû‚Åø` if one of the functions
  is `ùíû‚Åø` with compact support and the other function in locally integrable.

Versions of these statements for functions depending on a parameter are also given.

* `convolution_tendsto_right`: Given a sequence of nonnegative normalized functions whose support
  tends to a small neighborhood around `0`, the convolution tends to the right argument.
  This is specialized to bump functions in `cont_diff_bump.convolution_tendsto_right`.

# Notation
The following notations are localized in the locale `convolution`:
* `f ‚ãÜ[L, Œº] g` for the convolution. Note: you have to use parentheses to apply the convolution
  to an argument: `(f ‚ãÜ[L, Œº] g) x`.
* `f ‚ãÜ[L] g := f ‚ãÜ[L, volume] g`
* `f ‚ãÜ g := f ‚ãÜ[lsmul ‚Ñù ‚Ñù] g`

# To do
* Existence and (uniform) continuity of the convolution if
  one of the maps is in `‚Ñí^p` and the other in `‚Ñí^q` with `1 / p + 1 / q = 1`.
  This might require a generalization of `measure_theory.mem_‚Ñíp.smul` where `smul` is generalized
  to a continuous bilinear map.
  (see e.g. [Fremlin, *Measure Theory* (volume 2)][fremlin_vol2], 255K)
* The convolution is a `ae_strongly_measurable` function
  (see e.g. [Fremlin, *Measure Theory* (volume 2)][fremlin_vol2], 255I).
* Prove properties about the convolution if both functions are rapidly decreasing.
* Use `@[to_additive]` everywhere
-/

open set function filter measure_theory measure_theory.measure topological_space
open continuous_linear_map metric
open_locale pointwise topology nnreal filter

universes uùïú uG uE uE' uE'' uF uF' uF'' uP

variables {ùïú : Type uùïú} {G : Type uG} {E : Type uE} {E' : Type uE'} {E'' : Type uE''}
{F : Type uF} {F' : Type uF'} {F'' : Type uF''} {P : Type uP}

variables [normed_add_comm_group E] [normed_add_comm_group E'] [normed_add_comm_group E'']
  [normed_add_comm_group F]
  {f f' : G ‚Üí E} {g g' : G ‚Üí E'} {x x' : G} {y y' : E}

section nontrivially_normed_field

variables [nontrivially_normed_field ùïú]
variables [normed_space ùïú E] [normed_space ùïú E'] [normed_space ùïú E''] [normed_space ùïú F]
variables (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)

section no_measurability

variables [add_group G] [topological_space G]

lemma convolution_integrand_bound_right_of_le_of_subset
  {C : ‚Ñù} (hC : ‚àÄ i, ‚Äñg i‚Äñ ‚â§ C) {x t : G} {s u : set G} (hx : x ‚àà s) (hu : - tsupport g + s ‚äÜ u) :
  ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ u.indicator (Œª t, ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * C) t :=
begin
  refine le_indicator (Œª t ht, _) (Œª t ht, _) t,
  { refine (L.le_op_norm‚ÇÇ _ _).trans _,
    apply mul_le_mul_of_nonneg_left (hC _) (mul_nonneg (norm_nonneg _) (norm_nonneg _)) },
  { have : x - t ‚àâ support g,
    { refine mt (Œª hxt, _) ht,
      apply hu,
      refine ‚ü®_, _, set.neg_mem_neg.mpr (subset_closure hxt), hx, _‚ü©,
      rw [neg_sub, sub_add_cancel] },
    rw [nmem_support.mp this, (L _).map_zero, norm_zero] }
end

lemma has_compact_support.convolution_integrand_bound_right_of_subset (hcg : has_compact_support g)
  (hg : continuous g) {x t : G} {s u : set G} (hx : x ‚àà s) (hu : - tsupport g + s ‚äÜ u) :
  ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ u.indicator (Œª t, ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * (‚®Ü i, ‚Äñg i‚Äñ)) t :=
begin
  apply convolution_integrand_bound_right_of_le_of_subset _ (Œª i, _) hx hu,
  exact le_csupr (hg.norm.bdd_above_range_of_has_compact_support hcg.norm) _,
end

lemma has_compact_support.convolution_integrand_bound_right (hcg : has_compact_support g)
  (hg : continuous g) {x t : G} {s : set G} (hx : x ‚àà s) :
  ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ (- tsupport g + s).indicator (Œª t, ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * (‚®Ü i, ‚Äñg i‚Äñ)) t :=
hcg.convolution_integrand_bound_right_of_subset L hg hx subset.rfl

lemma continuous.convolution_integrand_fst [has_continuous_sub G] (hg : continuous g) (t : G) :
  continuous (Œª x, L (f t) (g (x - t))) :=
L.continuous‚ÇÇ.comp‚ÇÇ continuous_const $ hg.comp $ continuous_id.sub continuous_const

lemma has_compact_support.convolution_integrand_bound_left (hcf : has_compact_support f)
  (hf : continuous f) {x t : G} {s : set G} (hx : x ‚àà s) :
  ‚ÄñL (f (x - t)) (g t)‚Äñ ‚â§ (- tsupport f + s).indicator (Œª t, ‚ÄñL‚Äñ * (‚®Ü i, ‚Äñf i‚Äñ) * ‚Äñg t‚Äñ) t :=
by { convert hcf.convolution_integrand_bound_right L.flip hf hx,
  simp_rw [L.op_norm_flip, mul_right_comm] }

end no_measurability

section measurability

variables [measurable_space G] {Œº ŒΩ : measure G}

/-- The convolution of `f` and `g` exists at `x` when the function `t ‚Ü¶ L (f t) (g (x - t))` is
integrable. There are various conditions on `f` and `g` to prove this. -/
def convolution_exists_at [has_sub G] (f : G ‚Üí E) (g : G ‚Üí E') (x : G) (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
  (Œº : measure G . volume_tac) : Prop :=
integrable (Œª t, L (f t) (g (x - t))) Œº

/-- The convolution of `f` and `g` exists when the function `t ‚Ü¶ L (f t) (g (x - t))` is integrable
for all `x : G`. There are various conditions on `f` and `g` to prove this. -/
def convolution_exists [has_sub G] (f : G ‚Üí E) (g : G ‚Üí E') (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
  (Œº : measure G . volume_tac) : Prop :=
‚àÄ x : G, convolution_exists_at f g x L Œº

section convolution_exists

variables {L}
lemma convolution_exists_at.integrable [has_sub G] {x : G} (h : convolution_exists_at f g x L Œº) :
  integrable (Œª t, L (f t) (g (x - t))) Œº :=
h

variables (L)

section group

variables [add_group G]

lemma measure_theory.ae_strongly_measurable.convolution_integrand'
  [has_measurable_add‚ÇÇ G] [has_measurable_neg G] [sigma_finite ŒΩ]
  (hf : ae_strongly_measurable f ŒΩ)
  (hg : ae_strongly_measurable g $ map (Œª (p : G √ó G), p.1 - p.2) (Œº.prod ŒΩ)) :
  ae_strongly_measurable (Œª p : G √ó G, L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) :=
L.ae_strongly_measurable_comp‚ÇÇ hf.snd $ hg.comp_measurable measurable_sub


section

variables [has_measurable_add G] [has_measurable_neg G]

lemma measure_theory.ae_strongly_measurable.convolution_integrand_snd'
  (hf : ae_strongly_measurable f Œº) {x : G}
  (hg : ae_strongly_measurable g $ map (Œª t, x - t) Œº) :
  ae_strongly_measurable (Œª t, L (f t) (g (x - t))) Œº :=
L.ae_strongly_measurable_comp‚ÇÇ hf $ hg.comp_measurable $ measurable_id.const_sub x

lemma measure_theory.ae_strongly_measurable.convolution_integrand_swap_snd'
  {x : G} (hf : ae_strongly_measurable f $ map (Œª t, x - t) Œº)
  (hg : ae_strongly_measurable g Œº) : ae_strongly_measurable (Œª t, L (f (x - t)) (g t)) Œº :=
L.ae_strongly_measurable_comp‚ÇÇ (hf.comp_measurable $ measurable_id.const_sub x) hg

/-- A sufficient condition to prove that `f ‚ãÜ[L, Œº] g` exists.
We assume that `f` is integrable on a set `s` and `g` is bounded and ae strongly measurable
on `x‚ÇÄ - s` (note that both properties hold if `g` is continuous with compact support). -/
lemma bdd_above.convolution_exists_at' {x‚ÇÄ : G}
  {s : set G} (hbg : bdd_above ((Œª i, ‚Äñg i‚Äñ) '' ((Œª t, - t + x‚ÇÄ) ‚Åª¬π' s)))
  (hs : measurable_set s) (h2s : support (Œª t, L (f t) (g (x‚ÇÄ - t))) ‚äÜ s)
  (hf : integrable_on f s Œº) (hmg : ae_strongly_measurable g $ map (Œª t, x‚ÇÄ - t) (Œº.restrict s)) :
  convolution_exists_at f g x‚ÇÄ L Œº :=
begin
  rw [convolution_exists_at, ‚Üê integrable_on_iff_integrable_of_support_subset h2s],
  set s' := (Œª t, - t + x‚ÇÄ) ‚Åª¬π' s,
  have : ‚àÄ·µê (t : G) ‚àÇ(Œº.restrict s),
    ‚ÄñL (f t) (g (x‚ÇÄ - t))‚Äñ ‚â§ s.indicator (Œª t, ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * ‚®Ü i : s', ‚Äñg i‚Äñ) t,
  { refine eventually_of_forall _,
    refine le_indicator (Œª t ht, _) (Œª t ht, _),
    { refine (L.le_op_norm‚ÇÇ _ _).trans _,
      refine mul_le_mul_of_nonneg_left
        (le_csupr_set hbg $ mem_preimage.mpr _)
        (mul_nonneg (norm_nonneg _) (norm_nonneg _)),
      rwa [neg_sub, sub_add_cancel] },
    { have : t ‚àâ support (Œª t, L (f t) (g (x‚ÇÄ - t))) := mt (Œª h, h2s h) ht,
      rw [nmem_support.mp this, norm_zero] } },
  refine integrable.mono' _ _ this,
  { rw [integrable_indicator_iff hs], exact ((hf.norm.const_mul _).mul_const _).integrable_on },
  { exact hf.ae_strongly_measurable.convolution_integrand_snd' L hmg }
end

/-- If `‚Äñf‚Äñ *[Œº] ‚Äñg‚Äñ` exists, then `f *[L, Œº] g` exists. -/
lemma convolution_exists_at.of_norm' {x‚ÇÄ : G}
  (h : convolution_exists_at (Œª x, ‚Äñf x‚Äñ) (Œª x, ‚Äñg x‚Äñ) x‚ÇÄ (mul ‚Ñù ‚Ñù) Œº)
  (hmf : ae_strongly_measurable f Œº)
  (hmg : ae_strongly_measurable g $ map (Œª t, x‚ÇÄ - t) Œº) :
  convolution_exists_at f g x‚ÇÄ L Œº :=
begin
  refine (h.const_mul ‚ÄñL‚Äñ).mono' (hmf.convolution_integrand_snd' L hmg)
    (eventually_of_forall $ Œª x, _),
  rw [mul_apply', ‚Üê mul_assoc],
  apply L.le_op_norm‚ÇÇ,
end

end

section left
variables [has_measurable_add‚ÇÇ G] [has_measurable_neg G] [sigma_finite Œº] [is_add_right_invariant Œº]

lemma measure_theory.ae_strongly_measurable.convolution_integrand_snd
  (hf : ae_strongly_measurable f Œº) (hg : ae_strongly_measurable g Œº)
  (x : G) : ae_strongly_measurable (Œª t, L (f t) (g (x - t))) Œº :=
hf.convolution_integrand_snd' L $ hg.mono' $
  (quasi_measure_preserving_sub_left_of_right_invariant Œº x).absolutely_continuous

lemma measure_theory.ae_strongly_measurable.convolution_integrand_swap_snd
  (hf : ae_strongly_measurable f Œº) (hg : ae_strongly_measurable g Œº)
  (x : G) : ae_strongly_measurable (Œª t, L (f (x - t)) (g t)) Œº :=
(hf.mono' (quasi_measure_preserving_sub_left_of_right_invariant Œº x).absolutely_continuous)
  .convolution_integrand_swap_snd' L hg

/-- If `‚Äñf‚Äñ *[Œº] ‚Äñg‚Äñ` exists, then `f *[L, Œº] g` exists. -/
lemma convolution_exists_at.of_norm {x‚ÇÄ : G}
  (h : convolution_exists_at (Œª x, ‚Äñf x‚Äñ) (Œª x, ‚Äñg x‚Äñ) x‚ÇÄ (mul ‚Ñù ‚Ñù) Œº)
  (hmf : ae_strongly_measurable f Œº)
  (hmg : ae_strongly_measurable g Œº) :
  convolution_exists_at f g x‚ÇÄ L Œº :=
h.of_norm' L hmf $ hmg.mono'
  (quasi_measure_preserving_sub_left_of_right_invariant Œº x‚ÇÄ).absolutely_continuous

end left

section right

variables [has_measurable_add‚ÇÇ G] [has_measurable_neg G]
[sigma_finite Œº] [is_add_right_invariant Œº] [sigma_finite ŒΩ]

lemma measure_theory.ae_strongly_measurable.convolution_integrand
  (hf : ae_strongly_measurable f ŒΩ) (hg : ae_strongly_measurable g Œº) :
  ae_strongly_measurable (Œª p : G √ó G, L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) :=
hf.convolution_integrand' L $ hg.mono'
  (quasi_measure_preserving_sub_of_right_invariant Œº ŒΩ).absolutely_continuous

lemma measure_theory.integrable.convolution_integrand (hf : integrable f ŒΩ) (hg : integrable g Œº) :
  integrable (Œª p : G √ó G, L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) :=
begin
  have h_meas : ae_strongly_measurable (Œª (p : G √ó G), L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) :=
    hf.ae_strongly_measurable.convolution_integrand L hg.ae_strongly_measurable,
  have h2_meas : ae_strongly_measurable (Œª (y : G), ‚à´ (x : G), ‚ÄñL (f y) (g (x - y))‚Äñ ‚àÇŒº) ŒΩ :=
    h_meas.prod_swap.norm.integral_prod_right',
  simp_rw [integrable_prod_iff' h_meas],
  refine ‚ü®eventually_of_forall (Œª t, (L (f t)).integrable_comp (hg.comp_sub_right t)), _‚ü©,
  refine integrable.mono' _ h2_meas (eventually_of_forall $
    Œª t, (_ : _ ‚â§ ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * ‚à´ x, ‚Äñg (x - t)‚Äñ ‚àÇŒº)),
  { simp_rw [integral_sub_right_eq_self (Œª t, ‚Äñ g t ‚Äñ)],
    exact (hf.norm.const_mul _).mul_const _ },
  { simp_rw [‚Üê integral_mul_left],
    rw [real.norm_of_nonneg],
    { exact integral_mono_of_nonneg (eventually_of_forall $ Œª t, norm_nonneg _)
        ((hg.comp_sub_right t).norm.const_mul _) (eventually_of_forall $ Œª t, L.le_op_norm‚ÇÇ _ _) },
    exact integral_nonneg (Œª x, norm_nonneg _) }
end

lemma measure_theory.integrable.ae_convolution_exists (hf : integrable f ŒΩ) (hg : integrable g Œº) :
  ‚àÄ·µê x ‚àÇŒº, convolution_exists_at f g x L ŒΩ :=
((integrable_prod_iff $ hf.ae_strongly_measurable.convolution_integrand L
  hg.ae_strongly_measurable).mp $ hf.convolution_integrand L hg).1

end right

variables [topological_space G] [topological_add_group G] [borel_space G]

lemma has_compact_support.convolution_exists_at {x‚ÇÄ : G}
  (h : has_compact_support (Œª t, L (f t) (g (x‚ÇÄ - t)))) (hf : locally_integrable f Œº)
  (hg : continuous g) : convolution_exists_at f g x‚ÇÄ L Œº :=
begin
  let u := (homeomorph.neg G).trans (homeomorph.add_right x‚ÇÄ),
  let v := (homeomorph.neg G).trans (homeomorph.add_left x‚ÇÄ),
  apply ((u.is_compact_preimage.mpr h).bdd_above_image hg.norm.continuous_on).convolution_exists_at'
    L is_closed_closure.measurable_set subset_closure (hf.integrable_on_is_compact h),
  have A : ae_strongly_measurable (g ‚àò ‚áëv)
    (Œº.restrict (tsupport (Œª (t : G), (L (f t)) (g (x‚ÇÄ - t))))),
  { apply (hg.comp v.continuous).continuous_on.ae_strongly_measurable_of_is_compact h,
    exact (is_closed_tsupport _).measurable_set },
  convert ((v.continuous.measurable.measure_preserving
    (Œº.restrict (tsupport (Œª t, L (f t) (g (x‚ÇÄ - t)))))).ae_strongly_measurable_comp_iff
    v.to_measurable_equiv.measurable_embedding).1 A,
  ext x,
  simp only [homeomorph.neg, sub_eq_add_neg, coe_to_add_units, homeomorph.trans_apply,
    equiv.neg_apply, equiv.to_fun_as_coe, homeomorph.homeomorph_mk_coe, equiv.coe_fn_mk,
    homeomorph.coe_add_left],
end

lemma has_compact_support.convolution_exists_right
  (hcg : has_compact_support g) (hf : locally_integrable f Œº) (hg : continuous g) :
  convolution_exists f g L Œº :=
begin
  intro x‚ÇÄ,
  refine has_compact_support.convolution_exists_at L _ hf hg,
  refine (hcg.comp_homeomorph (homeomorph.sub_left x‚ÇÄ)).mono _,
  refine Œª t, mt (Œª ht : g (x‚ÇÄ - t) = 0, _),
  simp_rw [ht, (L _).map_zero]
end

lemma has_compact_support.convolution_exists_left_of_continuous_right
  (hcf : has_compact_support f) (hf : locally_integrable f Œº) (hg : continuous g) :
  convolution_exists f g L Œº :=
begin
  intro x‚ÇÄ,
  refine has_compact_support.convolution_exists_at L _ hf hg,
  refine hcf.mono _,
  refine Œª t, mt (Œª ht : f t = 0, _),
  simp_rw [ht, L.map_zero‚ÇÇ]
end

end group

section comm_group

variables [add_comm_group G]

section measurable_group

variables [has_measurable_neg G] [is_add_left_invariant Œº]

/-- A sufficient condition to prove that `f ‚ãÜ[L, Œº] g` exists.
We assume that the integrand has compact support and `g` is bounded on this support (note that
both properties hold if `g` is continuous with compact support). We also require that `f` is
integrable on the support of the integrand, and that both functions are strongly measurable.

This is a variant of `bdd_above.convolution_exists_at'` in an abelian group with a left-invariant
measure. This allows us to state the boundedness and measurability of `g` in a more natural way. -/
lemma bdd_above.convolution_exists_at [has_measurable_add‚ÇÇ G] [sigma_finite Œº] {x‚ÇÄ : G}
  {s : set G} (hbg : bdd_above ((Œª i, ‚Äñg i‚Äñ) '' ((Œª t, x‚ÇÄ - t) ‚Åª¬π' s)))
  (hs : measurable_set s) (h2s : support (Œª t, L (f t) (g (x‚ÇÄ - t))) ‚äÜ s)
  (hf : integrable_on f s Œº) (hmg : ae_strongly_measurable g Œº) :
    convolution_exists_at f g x‚ÇÄ L Œº :=
begin
  refine bdd_above.convolution_exists_at' L _ hs h2s hf _,
  { simp_rw [‚Üê sub_eq_neg_add, hbg] },
  { have : ae_strongly_measurable g (map (Œª (t : G), x‚ÇÄ - t) Œº), from hmg.mono'
      (quasi_measure_preserving_sub_left_of_right_invariant Œº x‚ÇÄ).absolutely_continuous,
    apply this.mono_measure,
    exact map_mono_of_ae_measurable restrict_le_self
      (measurable_const.sub measurable_id').ae_measurable }
end

variables {L} [has_measurable_add G] [is_neg_invariant Œº]

lemma convolution_exists_at_flip :
  convolution_exists_at g f x L.flip Œº ‚Üî convolution_exists_at f g x L Œº :=
by simp_rw [convolution_exists_at, ‚Üê integrable_comp_sub_left (Œª t, L (f t) (g (x - t))) x,
  sub_sub_cancel, flip_apply]

lemma convolution_exists_at.integrable_swap (h : convolution_exists_at f g x L Œº) :
  integrable (Œª t, L (f (x - t)) (g t)) Œº :=
by { convert h.comp_sub_left x, simp_rw [sub_sub_self] }

lemma convolution_exists_at_iff_integrable_swap :
  convolution_exists_at f g x L Œº ‚Üî integrable (Œª t, L (f (x - t)) (g t)) Œº :=
convolution_exists_at_flip.symm

end measurable_group

variables [topological_space G] [topological_add_group G] [borel_space G]
 [is_add_left_invariant Œº] [is_neg_invariant Œº]

lemma has_compact_support.convolution_exists_left
  (hcf : has_compact_support f) (hf : continuous f) (hg : locally_integrable g Œº) :
  convolution_exists f g L Œº :=
Œª x‚ÇÄ, convolution_exists_at_flip.mp $ hcf.convolution_exists_right L.flip hg hf x‚ÇÄ

lemma has_compact_support.convolution_exists_right_of_continuous_left
  (hcg : has_compact_support g) (hf : continuous f) (hg : locally_integrable g Œº) :
  convolution_exists f g L Œº :=
Œª x‚ÇÄ, convolution_exists_at_flip.mp $
  hcg.convolution_exists_left_of_continuous_right L.flip hg hf x‚ÇÄ

end comm_group

end convolution_exists

variables [normed_space ‚Ñù F] [complete_space F]

/-- The convolution of two functions `f` and `g` with respect to a continuous bilinear map `L` and
measure `Œº`. It is defined to be `(f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº`. -/
noncomputable def convolution [has_sub G] (f : G ‚Üí E) (g : G ‚Üí E') (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
  (Œº : measure G . volume_tac) : G ‚Üí F :=
Œª x, ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº

localized "notation (name := convolution) f ` ‚ãÜ[`:67 L:67 `, ` Œº:67 `] `:0 g:66 :=
  convolution f g L Œº" in convolution
localized "notation (name := convolution.volume) f ` ‚ãÜ[`:67 L:67 `]`:0 g:66 :=
  convolution f g L measure_theory.measure_space.volume" in convolution
localized "notation (name := convolution.lsmul) f ` ‚ãÜ `:67 g:66 :=
  convolution f g (continuous_linear_map.lsmul ‚Ñù ‚Ñù) measure_theory.measure_space.volume"
  in convolution

lemma convolution_def [has_sub G] : (f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº := rfl

/-- The definition of convolution where the bilinear operator is scalar multiplication.
Note: it often helps the elaborator to give the type of the convolution explicitly. -/
lemma convolution_lsmul [has_sub G] {f : G ‚Üí ùïú} {g : G ‚Üí F} :
  (f ‚ãÜ[lsmul ùïú ùïú, Œº] g : G ‚Üí F) x = ‚à´ t, f t ‚Ä¢ g (x - t) ‚àÇŒº := rfl

/-- The definition of convolution where the bilinear operator is multiplication. -/
lemma convolution_mul [has_sub G] [normed_space ‚Ñù ùïú] [complete_space ùïú] {f : G ‚Üí ùïú} {g : G ‚Üí ùïú} :
  (f ‚ãÜ[mul ùïú ùïú, Œº] g) x = ‚à´ t, f t * g (x - t) ‚àÇŒº := rfl

section group

variables {L} [add_group G]

lemma smul_convolution [smul_comm_class ‚Ñù ùïú F]
  {y : ùïú} : (y ‚Ä¢ f) ‚ãÜ[L, Œº] g = y ‚Ä¢ (f ‚ãÜ[L, Œº] g) :=
by { ext, simp only [pi.smul_apply, convolution_def, ‚Üê integral_smul, L.map_smul‚ÇÇ] }

lemma convolution_smul [smul_comm_class ‚Ñù ùïú F]
  {y : ùïú} : f ‚ãÜ[L, Œº] (y ‚Ä¢ g) = y ‚Ä¢ (f ‚ãÜ[L, Œº] g) :=
by { ext, simp only [pi.smul_apply, convolution_def, ‚Üê integral_smul, (L _).map_smul] }

@[simp] lemma zero_convolution : 0 ‚ãÜ[L, Œº] g = 0 :=
by { ext, simp_rw [convolution_def, pi.zero_apply, L.map_zero‚ÇÇ, integral_zero] }

@[simp] lemma convolution_zero : f ‚ãÜ[L, Œº] 0 = 0 :=
by { ext, simp_rw [convolution_def, pi.zero_apply, (L _).map_zero, integral_zero] }

lemma convolution_exists_at.distrib_add {x : G} (hfg : convolution_exists_at f g x L Œº)
  (hfg' : convolution_exists_at f g' x L Œº) :
  (f ‚ãÜ[L, Œº] (g + g')) x = (f ‚ãÜ[L, Œº] g) x + (f ‚ãÜ[L, Œº] g') x :=
by simp only [convolution_def, (L _).map_add, pi.add_apply, integral_add hfg hfg']

lemma convolution_exists.distrib_add (hfg : convolution_exists f g L Œº)
  (hfg' : convolution_exists f g' L Œº) : f ‚ãÜ[L, Œº] (g + g') = f ‚ãÜ[L, Œº] g + f ‚ãÜ[L, Œº] g' :=
by { ext, exact (hfg x).distrib_add (hfg' x) }

lemma convolution_exists_at.add_distrib {x : G} (hfg : convolution_exists_at f g x L Œº)
  (hfg' : convolution_exists_at f' g x L Œº) :
  ((f + f') ‚ãÜ[L, Œº] g) x = (f ‚ãÜ[L, Œº] g) x + (f' ‚ãÜ[L, Œº] g) x :=
by simp only [convolution_def, L.map_add‚ÇÇ, pi.add_apply, integral_add hfg hfg']

lemma convolution_exists.add_distrib (hfg : convolution_exists f g L Œº)
  (hfg' : convolution_exists f' g L Œº) : (f + f') ‚ãÜ[L, Œº] g = f ‚ãÜ[L, Œº] g + f' ‚ãÜ[L, Œº] g :=
by { ext, exact (hfg x).add_distrib (hfg' x) }

lemma convolution_mono_right {f g g' : G ‚Üí ‚Ñù}
  (hfg : convolution_exists_at f g x (lsmul ‚Ñù ‚Ñù) Œº)
  (hfg' : convolution_exists_at f g' x (lsmul ‚Ñù ‚Ñù) Œº)
  (hf : ‚àÄ x, 0 ‚â§ f x) (hg : ‚àÄ x, g x ‚â§ g' x) :
  (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x ‚â§ (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g') x :=
begin
  apply integral_mono hfg hfg',
  simp only [lsmul_apply, algebra.id.smul_eq_mul],
  assume t,
  apply mul_le_mul_of_nonneg_left (hg _) (hf _),
end

lemma convolution_mono_right_of_nonneg {f g g' : G ‚Üí ‚Ñù}
  (hfg' : convolution_exists_at f g' x (lsmul ‚Ñù ‚Ñù) Œº)
  (hf : ‚àÄ x, 0 ‚â§ f x) (hg : ‚àÄ x, g x ‚â§ g' x) (hg' : ‚àÄ x, 0 ‚â§ g' x) :
  (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x ‚â§ (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g') x :=
begin
  by_cases H : convolution_exists_at f g x (lsmul ‚Ñù ‚Ñù) Œº,
  { exact convolution_mono_right H hfg' hf hg },
  have : (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x = 0 := integral_undef H,
  rw this,
  exact integral_nonneg (Œª y, mul_nonneg (hf y) (hg' (x - y))),
end

variables (L)

lemma convolution_congr [has_measurable_add‚ÇÇ G] [has_measurable_neg G]
  [sigma_finite Œº] [is_add_right_invariant Œº] (h1 : f =·µê[Œº] f') (h2 : g =·µê[Œº] g') :
  f ‚ãÜ[L, Œº] g = f' ‚ãÜ[L, Œº] g' :=
begin
  ext x,
  apply integral_congr_ae,
  exact (h1.prod_mk $ h2.comp_tendsto
    (quasi_measure_preserving_sub_left_of_right_invariant Œº x).tendsto_ae).fun_comp ‚Üø(Œª x y, L x y)
end

lemma support_convolution_subset_swap : support (f ‚ãÜ[L, Œº] g) ‚äÜ support g + support f :=
begin
  intros x h2x,
  by_contra hx,
  apply h2x,
  simp_rw [set.mem_add, not_exists, not_and_distrib, nmem_support] at hx,
  rw [convolution_def],
  convert integral_zero G F,
  ext t,
  rcases hx (x - t) t with h|h|h,
  { rw [h, (L _).map_zero] },
  { rw [h, L.map_zero‚ÇÇ] },
  { exact (h $ sub_add_cancel x t).elim }
end

section
variables [has_measurable_add‚ÇÇ G] [has_measurable_neg G] [sigma_finite Œº] [is_add_right_invariant Œº]

lemma measure_theory.integrable.integrable_convolution (hf : integrable f Œº) (hg : integrable g Œº) :
  integrable (f ‚ãÜ[L, Œº] g) Œº :=
(hf.convolution_integrand L hg).integral_prod_left

end

variables [topological_space G]
variables [topological_add_group G]

lemma has_compact_support.convolution [t2_space G] (hcf : has_compact_support f)
  (hcg : has_compact_support g) : has_compact_support (f ‚ãÜ[L, Œº] g) :=
is_compact_of_is_closed_subset (hcg.is_compact.add hcf) is_closed_closure $ closure_minimal
  ((support_convolution_subset_swap L).trans $ add_subset_add subset_closure subset_closure)
  (hcg.is_compact.add hcf).is_closed

variables [borel_space G] [first_countable_topology G]
[topological_space P] [first_countable_topology P]

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in a subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
not assuming `t2_space G`. -/
lemma continuous_on_convolution_right_with_param'
  {g : P ‚Üí G ‚Üí E'} {s : set P} {k : set G} (hk : is_compact k) (h'k : is_closed k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : continuous_on (‚Üøg) (s √óÀ¢ univ)) :
  continuous_on (Œª (q : P √ó G), (f ‚ãÜ[L, Œº] g q.1) q.2) (s √óÀ¢ univ) :=
begin
  assume q‚ÇÄ hq‚ÇÄ,
  replace hq‚ÇÄ : q‚ÇÄ.1 ‚àà s, by simpa only [mem_prod, mem_univ, and_true] using hq‚ÇÄ,
  have A : ‚àÄ p ‚àà s, continuous (g p),
  { assume p hp,
    apply hg.comp_continuous (continuous_const.prod_mk continuous_id') (Œª x, _),
    simpa only [prod_mk_mem_set_prod_eq, mem_univ, and_true] using hp },
  have B : ‚àÄ p ‚àà s, tsupport (g p) ‚äÜ k :=
    Œª p hp, closure_minimal (support_subset_iff'.2 (Œª z hz, hgs _ _ hp hz)) h'k,
  /- We find a small neighborhood of `{q‚ÇÄ.1} √ó k` on which the function is uniformly bounded.
    This follows from the continuity at all points of the compact set `k`. -/
  obtain ‚ü®w, C, w_open, q‚ÇÄw, Cnonneg, hw‚ü© :
    ‚àÉ w C, is_open w ‚àß q‚ÇÄ.1 ‚àà w ‚àß 0 ‚â§ C ‚àß ‚àÄ p x, p ‚àà w ‚à© s ‚Üí ‚Äñg p x‚Äñ ‚â§ C,
  { have A : is_compact ({q‚ÇÄ.1} √óÀ¢ k), from is_compact_singleton.prod hk,
    obtain ‚ü®t, kt, t_open, ht‚ü© :
      ‚àÉ t, {q‚ÇÄ.1} √óÀ¢ k ‚äÜ t ‚àß is_open t ‚àß bounded (‚Üøg '' (t ‚à© s √óÀ¢ univ)),
    { apply exists_is_open_bounded_image_inter_of_is_compact_of_continuous_on A _ hg,
      simp only [prod_subset_prod_iff, hq‚ÇÄ, singleton_subset_iff, subset_univ, and_self, true_or] },
    obtain ‚ü®C, Cpos, hC‚ü© : ‚àÉ C, 0 < C ‚àß (‚Üøg) '' (t ‚à© s √óÀ¢ univ) ‚äÜ closed_ball (0 : E') C,
      from ht.subset_ball_lt 0 0,
    obtain ‚ü®w, w_open, q‚ÇÄw, hw‚ü© : ‚àÉ w, is_open w ‚àß q‚ÇÄ.1 ‚àà w ‚àß w √óÀ¢ k ‚äÜ t,
    { obtain ‚ü®w, v, w_open, v_open, hw, hv, hvw‚ü© :
        ‚àÉ (w : set P) (v : set G), is_open w ‚àß is_open v ‚àß {q‚ÇÄ.fst} ‚äÜ w ‚àß k ‚äÜ v ‚àß w √óÀ¢ v ‚äÜ t,
        from generalized_tube_lemma is_compact_singleton hk t_open kt,
      exact ‚ü®w, w_open, singleton_subset_iff.1 hw,
        subset.trans (set.prod_mono subset.rfl hv) hvw‚ü© },
    refine ‚ü®w, C, w_open, q‚ÇÄw, Cpos.le, _‚ü©,
    rintros p x ‚ü®hp, hps‚ü©,
    by_cases hx : x ‚àà k,
    { have H : (p, x) ‚àà t,
      { apply hw,
        simp only [prod_mk_mem_set_prod_eq, hp, hx, and_true], },
      have H' : (p, x) ‚àà (s √óÀ¢ univ : set (P √ó G)),
        by simpa only [prod_mk_mem_set_prod_eq, mem_univ, and_true] using hps,
      have : g p x ‚àà closed_ball (0 : E') C, from hC (mem_image_of_mem _ ‚ü®H, H'‚ü©),
      rwa mem_closed_ball_zero_iff at this },
    { have : g p x = 0, from hgs _ _ hps hx,
      rw this,
      simpa only [norm_zero] using Cpos.le } },
  have I1 : ‚àÄ·∂† (q : P √ó G) in ùìù[s √óÀ¢ univ] q‚ÇÄ,
    ae_strongly_measurable (Œª (a : G), L (f a) (g q.1 (q.2 - a))) Œº,
  { filter_upwards [self_mem_nhds_within],
    rintros ‚ü®p, x‚ü© ‚ü®hp, hx‚ü©,
    refine (has_compact_support.convolution_exists_right L _ hf (A _ hp) _).1,
    exact is_compact_of_is_closed_subset hk (is_closed_tsupport _) (B p hp) },
  let K' := - k + {q‚ÇÄ.2},
  have hK' : is_compact K' := hk.neg.add is_compact_singleton,
  obtain ‚ü®U, U_open, K'U, hU‚ü© : ‚àÉ U, is_open U ‚àß K' ‚äÜ U ‚àß integrable_on f U Œº,
    from hf.integrable_on_nhds_is_compact hK',
  let bound : G ‚Üí ‚Ñù := indicator U (Œª a, ‚ÄñL‚Äñ * ‚Äñf a‚Äñ * C),
  have I2 : ‚àÄ·∂† (q : P √ó G) in ùìù[s √óÀ¢ univ] q‚ÇÄ, ‚àÄ·µê a ‚àÇŒº, ‚ÄñL (f a) (g q.1 (q.2 - a))‚Äñ ‚â§ bound a,
  { obtain ‚ü®V, V_mem, hV‚ü© : ‚àÉ (V : set G) (H : V ‚àà ùìù (0 : G)), K' + V ‚äÜ U,
      from compact_open_separated_add_right hK' U_open K'U,
    have : ((w ‚à© s) √óÀ¢ ({q‚ÇÄ.2} + V) : set (P √ó G)) ‚àà ùìù[s √óÀ¢ univ] q‚ÇÄ,
    { conv_rhs { rw [‚Üê @prod.mk.eta _ _ q‚ÇÄ, nhds_within_prod_eq, nhds_within_univ] },
      refine filter.prod_mem_prod _ (singleton_add_mem_nhds_of_nhds_zero q‚ÇÄ.2 V_mem),
      exact mem_nhds_within_iff_exists_mem_nhds_inter.2 ‚ü®w, w_open.mem_nhds q‚ÇÄw, subset.rfl‚ü© },
    filter_upwards [this],
    rintros ‚ü®p, x‚ü© hpx,
    simp only [prod_mk_mem_set_prod_eq] at hpx,
    apply eventually_of_forall (Œª a, _),
    apply convolution_integrand_bound_right_of_le_of_subset _ _ hpx.2 _,
    { assume x,
      exact hw _ _ hpx.1 },
    { rw ‚Üê add_assoc,
      apply subset.trans (add_subset_add_right (add_subset_add_right _)) hV,
      rw neg_subset_neg,
      exact B p hpx.1.2 } },
  have I3 : integrable bound Œº,
  { rw [integrable_indicator_iff U_open.measurable_set],
    exact (hU.norm.const_mul _).mul_const _ },
  have I4 : ‚àÄ·µê (a : G) ‚àÇŒº, continuous_within_at (Œª (q : P √ó G), L (f a) (g q.1 (q.2 - a)))
    (s √óÀ¢ univ) q‚ÇÄ,
  { apply eventually_of_forall (Œª a, _),
    suffices H : continuous_within_at (Œª (q : P √ó G), (f a, g q.1 (q.2 - a))) (s √óÀ¢ univ) q‚ÇÄ,
      from L.continuous‚ÇÇ.continuous_at.comp_continuous_within_at H,
    apply continuous_within_at_const.prod,
    change continuous_within_at (Œª (q : P √ó G), ‚Üøg (q.1, q.2 - a)) (s √óÀ¢ univ) q‚ÇÄ,
    have : continuous_at (Œª (q : P √ó G), (q.1, q.2 - a)) (q‚ÇÄ.1, q‚ÇÄ.2),
      from (continuous_fst.prod_mk (continuous_snd.sub continuous_const)).continuous_at,
    rw ‚Üê @prod.mk.eta _ _ q‚ÇÄ,
    have h'q‚ÇÄ : (q‚ÇÄ.1, q‚ÇÄ.2 - a) ‚àà (s √óÀ¢ univ : set (P √ó G)) := ‚ü®hq‚ÇÄ, mem_univ _‚ü©,
    refine continuous_within_at.comp (hg _ h'q‚ÇÄ) this.continuous_within_at _,
    rintros ‚ü®q, x‚ü© ‚ü®hq, hx‚ü©,
    exact ‚ü®hq, mem_univ _‚ü© },
  exact continuous_within_at_of_dominated I1 I2 I3 I4,
end

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in a subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
lemma continuous_on_convolution_right_with_param
  [t2_space G] {g : P ‚Üí G ‚Üí E'} {s : set P} {k : set G} (hk : is_compact k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : continuous_on (‚Üøg) (s √óÀ¢ univ)) :
  continuous_on (Œª (q : P √ó G), (f ‚ãÜ[L, Œº] g q.1) q.2) (s √óÀ¢ univ) :=
continuous_on_convolution_right_with_param' L hk hk.is_closed hgs hf hg

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in an open subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of compositions with an additional continuous map.
Version not assuming `t2_space G`. -/
lemma continuous_on_convolution_right_with_param_comp'
  {s : set P} {v : P ‚Üí G} (hv : continuous_on v s)
  {g : P ‚Üí G ‚Üí E'} {k : set G}
  (hk : is_compact k) (h'k : is_closed k) (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : continuous_on (‚Üøg) (s √óÀ¢ univ)) :
  continuous_on (Œª x, (f ‚ãÜ[L, Œº] g x) (v x)) s :=
begin
  apply (continuous_on_convolution_right_with_param' L hk h'k hgs hf hg).comp
    (continuous_on_id.prod hv),
  assume x hx,
  simp only [hx, prod_mk_mem_set_prod_eq, mem_univ, and_self, id.def],
end

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in an open subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of compositions with an additional continuous map. -/
lemma continuous_on_convolution_right_with_param_comp [t2_space G]
  {s : set P} {v : P ‚Üí G} (hv : continuous_on v s)
  {g : P ‚Üí G ‚Üí E'} {k : set G}
  (hk : is_compact k) (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : continuous_on (‚Üøg) (s √óÀ¢ univ)) :
  continuous_on (Œª x, (f ‚ãÜ[L, Œº] g x) (v x)) s :=
continuous_on_convolution_right_with_param_comp' L hv hk hk.is_closed hgs hf hg

/-- The convolution is continuous if one function is locally integrable and the other has compact
support and is continuous. -/
lemma has_compact_support.continuous_convolution_right
  (hcg : has_compact_support g) (hf : locally_integrable f Œº)
  (hg : continuous g) : continuous (f ‚ãÜ[L, Œº] g) :=
begin
  rw continuous_iff_continuous_on_univ,
  let g' : G ‚Üí G ‚Üí E' := Œª p q, g q,
  have : continuous_on (‚Üøg') (univ √óÀ¢ univ) := (hg.comp continuous_snd).continuous_on,
  exact continuous_on_convolution_right_with_param_comp' L
    (continuous_iff_continuous_on_univ.1 continuous_id) hcg (is_closed_tsupport _)
    (Œª p x hp hx, image_eq_zero_of_nmem_tsupport hx) hf this,
end

/-- The convolution is continuous if one function is integrable and the other is bounded and
continuous. -/
lemma bdd_above.continuous_convolution_right_of_integrable [second_countable_topology G]
  (hbg : bdd_above (range (Œª x, ‚Äñg x‚Äñ))) (hf : integrable f Œº) (hg : continuous g) :
    continuous (f ‚ãÜ[L, Œº] g) :=
begin
  refine continuous_iff_continuous_at.mpr (Œª x‚ÇÄ, _),
  have : ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚àÄ·µê (t : G) ‚àÇŒº,
    ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * (‚®Ü i, ‚Äñg i‚Äñ),
  { refine eventually_of_forall (Œª x, eventually_of_forall $ Œª t, _),
    refine (L.le_op_norm‚ÇÇ _ _).trans _,
    exact mul_le_mul_of_nonneg_left (le_csupr hbg $ x - t)
      (mul_nonneg (norm_nonneg _) (norm_nonneg _)) },
  refine continuous_at_of_dominated _ this _ _,
  { exact eventually_of_forall
      (Œª x, hf.ae_strongly_measurable.convolution_integrand_snd' L hg.ae_strongly_measurable) },
  { exact (hf.norm.const_mul _).mul_const _ },
  { exact eventually_of_forall (Œª t, (L.continuous‚ÇÇ.comp‚ÇÇ continuous_const $
      hg.comp $ continuous_id.sub $ by apply continuous_const).continuous_at) }
end

end group

section comm_group

variables [add_comm_group G]

lemma support_convolution_subset : support (f ‚ãÜ[L, Œº] g) ‚äÜ support f + support g :=
(support_convolution_subset_swap L).trans (add_comm _ _).subset

variables [is_add_left_invariant Œº] [is_neg_invariant Œº]

section measurable
variables [has_measurable_neg G]
variables [has_measurable_add G]

variable (L)
/-- Commutativity of convolution -/
lemma convolution_flip : g ‚ãÜ[L.flip, Œº] f = f ‚ãÜ[L, Œº] g :=
begin
  ext1 x,
  simp_rw [convolution_def],
  rw [‚Üê integral_sub_left_eq_self _ Œº x],
  simp_rw [sub_sub_self, flip_apply]
end

/-- The symmetric definition of convolution. -/
lemma convolution_eq_swap : (f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f (x - t)) (g t) ‚àÇŒº :=
by { rw [‚Üê convolution_flip], refl }

/-- The symmetric definition of convolution where the bilinear operator is scalar multiplication. -/
lemma convolution_lsmul_swap {f : G ‚Üí ùïú} {g : G ‚Üí F}:
  (f ‚ãÜ[lsmul ùïú ùïú, Œº] g : G ‚Üí F) x = ‚à´ t, f (x - t) ‚Ä¢ g t ‚àÇŒº :=
convolution_eq_swap _

/-- The symmetric definition of convolution where the bilinear operator is multiplication. -/
lemma convolution_mul_swap [normed_space ‚Ñù ùïú] [complete_space ùïú] {f : G ‚Üí ùïú} {g : G ‚Üí ùïú} :
  (f ‚ãÜ[mul ùïú ùïú, Œº] g) x = ‚à´ t, f (x - t) * g t ‚àÇŒº :=
convolution_eq_swap _

/-- The convolution of two even functions is also even. -/
lemma convolution_neg_of_neg_eq (h1 : ‚àÄ·µê x ‚àÇŒº, f (-x) = f x) (h2 : ‚àÄ·µê x ‚àÇŒº, g (-x) = g x) :
  (f ‚ãÜ[L, Œº] g) (-x) = (f ‚ãÜ[L, Œº] g) x :=
calc ‚à´ (t : G), (L (f t)) (g (-x - t)) ‚àÇŒº
    = ‚à´ (t : G), (L (f (-t))) (g (x + t)) ‚àÇŒº :
  begin
    apply integral_congr_ae,
    filter_upwards [h1, (eventually_add_left_iff Œº x).2 h2] with t ht h't,
    simp_rw [ht, ‚Üê h't, neg_add'],
  end
... = ‚à´ (t : G), (L (f t)) (g (x - t)) ‚àÇŒº :
  by { rw ‚Üê integral_neg_eq_self, simp only [neg_neg, ‚Üê sub_eq_add_neg] }

end measurable

variables [topological_space G]
variables [topological_add_group G]
variables [borel_space G]

lemma has_compact_support.continuous_convolution_left [first_countable_topology G]
  (hcf : has_compact_support f) (hf : continuous f) (hg : locally_integrable g Œº) :
  continuous (f ‚ãÜ[L, Œº] g) :=
by { rw [‚Üê convolution_flip], exact hcf.continuous_convolution_right L.flip hg hf }

lemma bdd_above.continuous_convolution_left_of_integrable [second_countable_topology G]
  (hbf : bdd_above (range (Œª x, ‚Äñf x‚Äñ))) (hf : continuous f) (hg : integrable g Œº) :
  continuous (f ‚ãÜ[L, Œº] g) :=
by { rw [‚Üê convolution_flip], exact hbf.continuous_convolution_right_of_integrable L.flip hg hf }

end comm_group

section normed_add_comm_group

variables [seminormed_add_comm_group G]

/-- Compute `(f ‚ãÜ g) x‚ÇÄ` if the support of the `f` is within `metric.ball 0 R`, and `g` is constant
on `metric.ball x‚ÇÄ R`.

We can simplify the RHS further if we assume `f` is integrable, but also if `L = (‚Ä¢)` or more
generally if `L` has a `antilipschitz_with`-condition. -/
lemma convolution_eq_right' {x‚ÇÄ : G} {R : ‚Ñù}
  (hf : support f ‚äÜ ball (0 : G) R)
  (hg : ‚àÄ x ‚àà ball x‚ÇÄ R, g x = g x‚ÇÄ) : (f ‚ãÜ[L, Œº] g) x‚ÇÄ = ‚à´ t, L (f t) (g x‚ÇÄ) ‚àÇŒº :=
begin
  have h2 : ‚àÄ t, L (f t) (g (x‚ÇÄ - t)) = L (f t) (g x‚ÇÄ),
  { intro t, by_cases ht : t ‚àà support f,
    { have h2t := hf ht,
      rw [mem_ball_zero_iff] at h2t,
      specialize hg (x‚ÇÄ - t),
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, ‚Üê sub_eq_add_neg] at hg,
      rw [hg h2t] },
    { rw [nmem_support] at ht,
      simp_rw [ht, L.map_zero‚ÇÇ] } },
  simp_rw [convolution_def, h2],
end

variables [borel_space G] [second_countable_topology G]
variables [is_add_left_invariant Œº] [sigma_finite Œº]

/-- Approximate `(f ‚ãÜ g) x‚ÇÄ` if the support of the `f` is bounded within a ball, and `g` is near
`g x‚ÇÄ` on a ball with the same radius around `x‚ÇÄ`. See `dist_convolution_le` for a special case.

We can simplify the second argument of `dist` further if we add some extra type-classes on `E`
and `ùïú` or if `L` is scalar multiplication. -/
lemma dist_convolution_le' {x‚ÇÄ : G} {R Œµ : ‚Ñù} {z‚ÇÄ : E'}
  (hŒµ : 0 ‚â§ Œµ)
  (hif : integrable f Œº)
  (hf : support f ‚äÜ ball (0 : G) R)
  (hmg : ae_strongly_measurable g Œº)
  (hg : ‚àÄ x ‚àà ball x‚ÇÄ R, dist (g x) z‚ÇÄ ‚â§ Œµ) :
  dist ((f ‚ãÜ[L, Œº] g : G ‚Üí F) x‚ÇÄ) (‚à´ t, L (f t) z‚ÇÄ ‚àÇŒº) ‚â§ ‚ÄñL‚Äñ * ‚à´ x, ‚Äñf x‚Äñ ‚àÇŒº * Œµ :=
begin
  have hfg : convolution_exists_at f g x‚ÇÄ L Œº,
  { refine bdd_above.convolution_exists_at L _ metric.is_open_ball.measurable_set
    (subset_trans _ hf) hif.integrable_on hmg,
    swap, { refine Œª t, mt (Œª ht : f t = 0, _), simp_rw [ht, L.map_zero‚ÇÇ] },
    rw [bdd_above_def],
    refine ‚ü®‚Äñz‚ÇÄ‚Äñ + Œµ, _‚ü©,
    rintro _ ‚ü®x, hx, rfl‚ü©,
    refine norm_le_norm_add_const_of_dist_le (hg x _),
    rwa [mem_ball_iff_norm, norm_sub_rev, ‚Üê mem_ball_zero_iff] },
  have h2 : ‚àÄ t, dist (L (f t) (g (x‚ÇÄ - t))) (L (f t) z‚ÇÄ) ‚â§ ‚ÄñL (f t)‚Äñ * Œµ,
  { intro t, by_cases ht : t ‚àà support f,
    { have h2t := hf ht,
      rw [mem_ball_zero_iff] at h2t,
      specialize hg (x‚ÇÄ - t),
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, ‚Üê sub_eq_add_neg] at hg,
      refine ((L (f t)).dist_le_op_norm _ _).trans _,
      exact mul_le_mul_of_nonneg_left (hg h2t) (norm_nonneg _) },
    { rw [nmem_support] at ht,
      simp_rw [ht, L.map_zero‚ÇÇ, L.map_zero, norm_zero, zero_mul, dist_self] } },
  simp_rw [convolution_def],
  simp_rw [dist_eq_norm] at h2 ‚ä¢,
  rw [‚Üê integral_sub hfg.integrable], swap, { exact (L.flip z‚ÇÄ).integrable_comp hif },
  refine (norm_integral_le_of_norm_le ((L.integrable_comp hif).norm.mul_const Œµ)
    (eventually_of_forall h2)).trans _,
  rw [integral_mul_right],
  refine mul_le_mul_of_nonneg_right _ hŒµ,
  have h3 : ‚àÄ t, ‚ÄñL (f t)‚Äñ ‚â§ ‚ÄñL‚Äñ * ‚Äñf t‚Äñ,
  { intros t,
    exact L.le_op_norm (f t) },
  refine (integral_mono (L.integrable_comp hif).norm (hif.norm.const_mul _) h3).trans_eq _,
  rw [integral_mul_left]
end

variables [normed_space ‚Ñù E] [normed_space ‚Ñù E'] [complete_space E']

/-- Approximate `f ‚ãÜ g` if the support of the `f` is bounded within a ball, and `g` is near `g x‚ÇÄ`
on a ball with the same radius around `x‚ÇÄ`.

This is a special case of `dist_convolution_le'` where `L` is `(‚Ä¢)`, `f` has integral 1 and `f` is
nonnegative. -/
lemma dist_convolution_le {f : G ‚Üí ‚Ñù} {x‚ÇÄ : G} {R Œµ : ‚Ñù} {z‚ÇÄ : E'}
  (hŒµ : 0 ‚â§ Œµ)
  (hf : support f ‚äÜ ball (0 : G) R)
  (hnf : ‚àÄ x, 0 ‚â§ f x)
  (hintf : ‚à´ x, f x ‚àÇŒº = 1)
  (hmg : ae_strongly_measurable g Œº)
  (hg : ‚àÄ x ‚àà ball x‚ÇÄ R, dist (g x) z‚ÇÄ ‚â§ Œµ) :
  dist ((f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ) z‚ÇÄ ‚â§ Œµ :=
begin
  have hif : integrable f Œº,
  { by_contra hif, exact zero_ne_one ((integral_undef hif).symm.trans hintf) },
  convert (dist_convolution_le' _ hŒµ hif hf hmg hg).trans _,
  { simp_rw [lsmul_apply, integral_smul_const, hintf, one_smul] },
  { simp_rw [real.norm_of_nonneg (hnf _), hintf, mul_one],
    exact (mul_le_mul_of_nonneg_right op_norm_lsmul_le hŒµ).trans_eq (one_mul Œµ) }
end

/-- `(œÜ i ‚ãÜ g i) (k i)` tends to `z‚ÇÄ` as `i` tends to some filter `l` if
* `œÜ` is a sequence of nonnegative functions with integral `1` as `i` tends to `l`;
* The support of `œÜ` tends to small neighborhoods around `(0 : G)` as `i` tends to `l`;
* `g i` is `mu`-a.e. strongly measurable as `i` tends to `l`;
* `g i x` tends to `z‚ÇÄ` as `(i, x)` tends to `l √ó·∂† ùìù x‚ÇÄ`;
* `k i` tends to `x‚ÇÄ`.

See also `cont_diff_bump.convolution_tendsto_right`.
-/
lemma convolution_tendsto_right
  {Œπ} {g : Œπ ‚Üí G ‚Üí E'} {l : filter Œπ} {x‚ÇÄ : G} {z‚ÇÄ : E'}
  {œÜ : Œπ ‚Üí G ‚Üí ‚Ñù} {k : Œπ ‚Üí G}
  (hnœÜ : ‚àÄ·∂† i in l, ‚àÄ x, 0 ‚â§ œÜ i x)
  (hiœÜ : ‚àÄ·∂† i in l, ‚à´ x, œÜ i x ‚àÇŒº = 1) -- todo: we could weaken this to "the integral tends to 1"
  (hœÜ : tendsto (Œª n, support (œÜ n)) l (ùìù 0).small_sets)
  (hmg : ‚àÄ·∂† i in l, ae_strongly_measurable (g i) Œº)
  (hcg : tendsto (uncurry g) (l √ó·∂† ùìù x‚ÇÄ) (ùìù z‚ÇÄ))
  (hk : tendsto k l (ùìù x‚ÇÄ)) :
  tendsto (Œª i : Œπ, (œÜ i ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g i : G ‚Üí E') (k i)) l (ùìù z‚ÇÄ) :=
begin
  simp_rw [tendsto_small_sets_iff] at hœÜ,
  rw [metric.tendsto_nhds] at hcg ‚ä¢,
  simp_rw [metric.eventually_prod_nhds_iff] at hcg,
  intros Œµ hŒµ,
  have h2Œµ : 0 < Œµ / 3 := div_pos hŒµ (by norm_num),
  obtain ‚ü®p, hp, Œ¥, hŒ¥, hgŒ¥‚ü© := hcg _ h2Œµ,
  dsimp only [uncurry] at hgŒ¥,
  have h2k := hk.eventually (ball_mem_nhds x‚ÇÄ $ half_pos hŒ¥),
  have h2œÜ := (hœÜ (ball (0 : G) _) $ ball_mem_nhds _ (half_pos hŒ¥)),
  filter_upwards [hp, h2k, h2œÜ, hnœÜ, hiœÜ, hmg] with i hpi hki hœÜi hnœÜi hiœÜi hmgi,
  have hgi : dist (g i (k i)) z‚ÇÄ < Œµ / 3 := hgŒ¥ hpi (hki.trans $ half_lt_self hŒ¥),
  have h1 : ‚àÄ x' ‚àà ball (k i) (Œ¥ / 2), dist (g i x') (g i (k i)) ‚â§ Œµ / 3 + Œµ / 3,
  { intros x' hx',
    refine (dist_triangle_right _ _ _).trans (add_le_add (hgŒ¥ hpi _).le hgi.le),
    exact ((dist_triangle _ _ _).trans_lt (add_lt_add hx'.out hki)).trans_eq (add_halves Œ¥) },
  have := dist_convolution_le (add_pos h2Œµ h2Œµ).le hœÜi hnœÜi hiœÜi hmgi h1,
  refine ((dist_triangle _ _ _).trans_lt (add_lt_add_of_le_of_lt this hgi)).trans_eq _,
  field_simp, ring_nf
end

end normed_add_comm_group

namespace cont_diff_bump

variables {n : ‚Ñï‚àû}
variables [normed_space ‚Ñù E']
variables [normed_add_comm_group G] [normed_space ‚Ñù G] [has_cont_diff_bump G]
variables [complete_space E']
variables {a : G} {œÜ : cont_diff_bump (0 : G)}

/-- If `œÜ` is a bump function, compute `(œÜ ‚ãÜ g) x‚ÇÄ` if `g` is constant on `metric.ball x‚ÇÄ œÜ.R`. -/
lemma convolution_eq_right {x‚ÇÄ : G}
  (hg : ‚àÄ x ‚àà ball x‚ÇÄ œÜ.R, g x = g x‚ÇÄ) : (œÜ ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ = integral Œº œÜ ‚Ä¢ g x‚ÇÄ :=
by simp_rw [convolution_eq_right' _ œÜ.support_eq.subset hg, lsmul_apply, integral_smul_const]

variables [borel_space G]
variables [is_locally_finite_measure Œº] [is_open_pos_measure Œº]
variables [finite_dimensional ‚Ñù G]

/-- If `œÜ` is a normed bump function, compute `œÜ ‚ãÜ g` if `g` is constant on `metric.ball x‚ÇÄ œÜ.R`. -/
lemma normed_convolution_eq_right {x‚ÇÄ : G}
  (hg : ‚àÄ x ‚àà ball x‚ÇÄ œÜ.R, g x = g x‚ÇÄ) : (œÜ.normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ = g x‚ÇÄ :=
by { simp_rw [convolution_eq_right' _ œÜ.support_normed_eq.subset hg, lsmul_apply],
  exact integral_normed_smul œÜ Œº (g x‚ÇÄ) }

variables [is_add_left_invariant Œº]

/-- If `œÜ` is a normed bump function, approximate `(œÜ ‚ãÜ g) x‚ÇÄ` if `g` is near `g x‚ÇÄ` on a ball with
radius `œÜ.R` around `x‚ÇÄ`. -/
lemma dist_normed_convolution_le {x‚ÇÄ : G} {Œµ : ‚Ñù}
  (hmg : ae_strongly_measurable g Œº)
  (hg : ‚àÄ x ‚àà ball x‚ÇÄ œÜ.R, dist (g x) (g x‚ÇÄ) ‚â§ Œµ) :
  dist ((œÜ.normed Œº ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ) (g x‚ÇÄ) ‚â§ Œµ :=
dist_convolution_le (by simp_rw [‚Üê dist_self (g x‚ÇÄ), hg x‚ÇÄ (mem_ball_self œÜ.R_pos)])
  œÜ.support_normed_eq.subset œÜ.nonneg_normed œÜ.integral_normed hmg hg

/-- `(œÜ i ‚ãÜ g i) (k i)` tends to `z‚ÇÄ` as `i` tends to some filter `l` if
* `œÜ` is a sequence of normed bump functions such that `(œÜ i).R` tends to `0` as `i` tends to `l`;
* `g i` is `mu`-a.e. strongly measurable as `i` tends to `l`;
* `g i x` tends to `z‚ÇÄ` as `(i, x)` tends to `l √ó·∂† ùìù x‚ÇÄ`;
* `k i` tends to `x‚ÇÄ`. -/
lemma convolution_tendsto_right {Œπ} {œÜ : Œπ ‚Üí cont_diff_bump (0 : G)}
  {g : Œπ ‚Üí G ‚Üí E'} {k : Œπ ‚Üí G} {x‚ÇÄ : G} {z‚ÇÄ : E'} {l : filter Œπ}
  (hœÜ : tendsto (Œª i, (œÜ i).R) l (ùìù 0))
  (hig : ‚àÄ·∂† i in l, ae_strongly_measurable (g i) Œº)
  (hcg : tendsto (uncurry g) (l √ó·∂† ùìù x‚ÇÄ) (ùìù z‚ÇÄ))
  (hk : tendsto k l (ùìù x‚ÇÄ)) :
  tendsto (Œª i, ((Œª x, (œÜ i).normed Œº x) ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g i : G ‚Üí E') (k i)) l (ùìù z‚ÇÄ) :=
convolution_tendsto_right (eventually_of_forall $ Œª i, (œÜ i).nonneg_normed)
  (eventually_of_forall $ Œª i, (œÜ i).integral_normed)
  (tendsto_support_normed_small_sets hœÜ) hig hcg hk

/-- Special case of `cont_diff_bump.convolution_tendsto_right` where `g` is continuous,
  and the limit is taken only in the first function. -/
lemma convolution_tendsto_right_of_continuous {Œπ} {œÜ : Œπ ‚Üí cont_diff_bump (0 : G)}
  {l : filter Œπ} (hœÜ : tendsto (Œª i, (œÜ i).R) l (ùìù 0))
  (hg : continuous g) (x‚ÇÄ : G) :
  tendsto (Œª i, ((Œª x, (œÜ i).normed Œº x) ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ) l (ùìù (g x‚ÇÄ)) :=
convolution_tendsto_right hœÜ (eventually_of_forall $ Œª _, hg.ae_strongly_measurable)
  ((hg.tendsto x‚ÇÄ).comp tendsto_snd) tendsto_const_nhds

end cont_diff_bump

end measurability

end nontrivially_normed_field

open_locale convolution

section is_R_or_C

variables [is_R_or_C ùïú]
variables [normed_space ùïú E]
variables [normed_space ùïú E']
variables [normed_space ùïú E'']
variables [normed_space ‚Ñù F] [normed_space ùïú F]
variables {n : ‚Ñï‚àû}
variables [complete_space F]
variables [measurable_space G] {Œº ŒΩ : measure G}
variables (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)

section assoc
variables [normed_add_comm_group F'] [normed_space ‚Ñù F'] [normed_space ùïú F'] [complete_space F']
variables [normed_add_comm_group F''] [normed_space ‚Ñù F''] [normed_space ùïú F''] [complete_space F'']
variables {k : G ‚Üí E''}
variables (L‚ÇÇ : F ‚ÜíL[ùïú] E'' ‚ÜíL[ùïú] F')
variables (L‚ÇÉ : E ‚ÜíL[ùïú] F'' ‚ÜíL[ùïú] F')
variables (L‚ÇÑ : E' ‚ÜíL[ùïú] E'' ‚ÜíL[ùïú] F'')
variables [add_group G]
variables [sigma_finite Œº] [sigma_finite ŒΩ] [is_add_right_invariant Œº]

lemma integral_convolution
  [has_measurable_add‚ÇÇ G] [has_measurable_neg G]
  [normed_space ‚Ñù E] [normed_space ‚Ñù E']
  [complete_space E] [complete_space E']
  (hf : integrable f ŒΩ) (hg : integrable g Œº) :
  ‚à´ x, (f ‚ãÜ[L, ŒΩ] g) x ‚àÇŒº = L (‚à´ x, f x ‚àÇŒΩ) (‚à´ x, g x ‚àÇŒº) :=
begin
  refine (integral_integral_swap (by apply hf.convolution_integrand L hg)).trans _,
  simp_rw [integral_comp_comm _ (hg.comp_sub_right _), integral_sub_right_eq_self],
  exact (L.flip (‚à´ x, g x ‚àÇŒº)).integral_comp_comm hf
end

variables [has_measurable_add‚ÇÇ G] [is_add_right_invariant ŒΩ] [has_measurable_neg G]

/-- Convolution is associative. This has a weak but inconvenient integrability condition.
See also `convolution_assoc`. -/
lemma convolution_assoc' (hL : ‚àÄ (x : E) (y : E') (z : E''), L‚ÇÇ (L x y) z = L‚ÇÉ x (L‚ÇÑ y z))
  {x‚ÇÄ : G}
  (hfg : ‚àÄ·µê y ‚àÇŒº, convolution_exists_at f g y L ŒΩ)
  (hgk : ‚àÄ·µê x ‚àÇŒΩ, convolution_exists_at g k x L‚ÇÑ Œº)
  (hi : integrable (uncurry (Œª x y, (L‚ÇÉ (f y)) ((L‚ÇÑ (g (x - y))) (k (x‚ÇÄ - x))))) (Œº.prod ŒΩ)) :
  ((f ‚ãÜ[L, ŒΩ] g) ‚ãÜ[L‚ÇÇ, Œº] k) x‚ÇÄ = (f ‚ãÜ[L‚ÇÉ, ŒΩ] (g ‚ãÜ[L‚ÇÑ, Œº] k)) x‚ÇÄ :=
calc    ((f ‚ãÜ[L, ŒΩ] g) ‚ãÜ[L‚ÇÇ, Œº] k) x‚ÇÄ
      = ‚à´ t, L‚ÇÇ (‚à´ s, L (f s) (g (t - s)) ‚àÇŒΩ) (k (x‚ÇÄ - t)) ‚àÇŒº : rfl
  ... = ‚à´ t, ‚à´ s, L‚ÇÇ (L (f s) (g (t - s))) (k (x‚ÇÄ - t)) ‚àÇŒΩ ‚àÇŒº :
    integral_congr_ae (hfg.mono $ Œª t ht, ((L‚ÇÇ.flip (k (x‚ÇÄ - t))).integral_comp_comm ht).symm)
  ... = ‚à´ t, ‚à´ s, L‚ÇÉ (f s) (L‚ÇÑ (g (t - s)) (k (x‚ÇÄ - t))) ‚àÇŒΩ ‚àÇŒº : by simp_rw hL
  ... = ‚à´ s, ‚à´ t, L‚ÇÉ (f s) (L‚ÇÑ (g (t - s)) (k (x‚ÇÄ - t))) ‚àÇŒº ‚àÇŒΩ : by rw [integral_integral_swap hi]
  ... = ‚à´ s, ‚à´ u, L‚ÇÉ (f s) (L‚ÇÑ (g u) (k ((x‚ÇÄ - s) - u))) ‚àÇŒº ‚àÇŒΩ : begin
    congr', ext t,
    rw [eq_comm, ‚Üê integral_sub_right_eq_self _ t],
    { simp_rw [sub_sub_sub_cancel_right] },
    { apply_instance },
  end
  ... = ‚à´ s, L‚ÇÉ (f s) (‚à´ u, L‚ÇÑ (g u) (k ((x‚ÇÄ - s) - u)) ‚àÇŒº) ‚àÇŒΩ : begin
    refine integral_congr_ae _,
    refine ((quasi_measure_preserving_sub_left_of_right_invariant ŒΩ x‚ÇÄ).ae hgk).mono (Œª t ht, _),
    exact (L‚ÇÉ (f t)).integral_comp_comm ht,
  end
  ... = (f ‚ãÜ[L‚ÇÉ, ŒΩ] (g ‚ãÜ[L‚ÇÑ, Œº] k)) x‚ÇÄ : rfl

/-- Convolution is associative. This requires that
* all maps are a.e. strongly measurable w.r.t one of the measures
* `f ‚ãÜ[L, ŒΩ] g` exists almost everywhere
* `‚Äñg‚Äñ ‚ãÜ[Œº] ‚Äñk‚Äñ` exists almost everywhere
* `‚Äñf‚Äñ ‚ãÜ[ŒΩ] (‚Äñg‚Äñ ‚ãÜ[Œº] ‚Äñk‚Äñ)` exists at `x‚ÇÄ` -/
lemma convolution_assoc (hL : ‚àÄ (x : E) (y : E') (z : E''), L‚ÇÇ (L x y) z = L‚ÇÉ x (L‚ÇÑ y z))
  {x‚ÇÄ : G}
  (hf : ae_strongly_measurable f ŒΩ)
  (hg : ae_strongly_measurable g Œº)
  (hk : ae_strongly_measurable k Œº)
  (hfg : ‚àÄ·µê y ‚àÇŒº, convolution_exists_at f g y L ŒΩ)
  (hgk : ‚àÄ·µê x ‚àÇŒΩ, convolution_exists_at (Œª x, ‚Äñg x‚Äñ) (Œª x, ‚Äñk x‚Äñ) x (mul ‚Ñù ‚Ñù) Œº)
  (hfgk : convolution_exists_at (Œª x, ‚Äñf x‚Äñ) ((Œª x, ‚Äñg x‚Äñ) ‚ãÜ[mul ‚Ñù ‚Ñù, Œº] (Œª x, ‚Äñk x‚Äñ))
    x‚ÇÄ (mul ‚Ñù ‚Ñù) ŒΩ) :
  ((f ‚ãÜ[L, ŒΩ] g) ‚ãÜ[L‚ÇÇ, Œº] k) x‚ÇÄ = (f ‚ãÜ[L‚ÇÉ, ŒΩ] (g ‚ãÜ[L‚ÇÑ, Œº] k)) x‚ÇÄ :=
begin
  refine convolution_assoc' L L‚ÇÇ L‚ÇÉ L‚ÇÑ hL hfg (hgk.mono $ Œª x hx, hx.of_norm L‚ÇÑ hg hk) _,
  /- the following is similar to `integrable.convolution_integrand` -/
  have h_meas : ae_strongly_measurable
      (uncurry (Œª x y, L‚ÇÉ (f y) (L‚ÇÑ (g x) (k (x‚ÇÄ - y - x))))) (Œº.prod ŒΩ),
  { refine L‚ÇÉ.ae_strongly_measurable_comp‚ÇÇ hf.snd _,
    refine L‚ÇÑ.ae_strongly_measurable_comp‚ÇÇ hg.fst _,
    refine (hk.mono' _).comp_measurable ((measurable_const.sub measurable_snd).sub measurable_fst),
    refine quasi_measure_preserving.absolutely_continuous _,
    refine quasi_measure_preserving.prod_of_left
      ((measurable_const.sub measurable_snd).sub measurable_fst) (eventually_of_forall $ Œª y, _),
    dsimp only,
    exact quasi_measure_preserving_sub_left_of_right_invariant Œº _ },
  have h2_meas : ae_strongly_measurable (Œª y, ‚à´ x, ‚ÄñL‚ÇÉ (f y) (L‚ÇÑ (g x) (k (x‚ÇÄ - y - x)))‚Äñ ‚àÇŒº) ŒΩ :=
    h_meas.prod_swap.norm.integral_prod_right',
  have h3 : map (Œª z : G √ó G, (z.1 - z.2, z.2)) (Œº.prod ŒΩ) = Œº.prod ŒΩ :=
    (measure_preserving_sub_prod Œº ŒΩ).map_eq,
  suffices : integrable (uncurry (Œª x y, L‚ÇÉ (f y) (L‚ÇÑ (g x) (k (x‚ÇÄ - y - x))))) (Œº.prod ŒΩ),
  { rw [‚Üê h3] at this,
    convert this.comp_measurable (measurable_sub.prod_mk measurable_snd),
    ext ‚ü®x, y‚ü©,
    simp_rw [uncurry, function.comp_apply, sub_sub_sub_cancel_right] },
  simp_rw [integrable_prod_iff' h_meas],
  refine ‚ü®((quasi_measure_preserving_sub_left_of_right_invariant ŒΩ x‚ÇÄ).ae hgk).mono
    (Œª t ht, (L‚ÇÉ (f t)).integrable_comp $ ht.of_norm L‚ÇÑ hg hk), _‚ü©,
  refine (hfgk.const_mul (‚ÄñL‚ÇÉ‚Äñ * ‚ÄñL‚ÇÑ‚Äñ)).mono' h2_meas
    (((quasi_measure_preserving_sub_left_of_right_invariant ŒΩ x‚ÇÄ).ae hgk).mono $ Œª t ht, _),
  { simp_rw [convolution_def, mul_apply', mul_mul_mul_comm ‚ÄñL‚ÇÉ‚Äñ ‚ÄñL‚ÇÑ‚Äñ, ‚Üê integral_mul_left],
    rw [real.norm_of_nonneg],
    { refine integral_mono_of_nonneg (eventually_of_forall $ Œª t, norm_nonneg _)
        ((ht.const_mul _).const_mul _) (eventually_of_forall $ Œª s, _),
      refine (L‚ÇÉ.le_op_norm‚ÇÇ _ _).trans _,
      refine mul_le_mul_of_nonneg_left _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)),
      rw [‚Üê mul_assoc],
      apply L‚ÇÑ.le_op_norm‚ÇÇ },
    exact integral_nonneg (Œª x, norm_nonneg _) }
end

end assoc

variables [normed_add_comm_group G] [borel_space G]

lemma convolution_precompR_apply {g : G ‚Üí E'' ‚ÜíL[ùïú] E'}
  (hf : locally_integrable f Œº) (hcg : has_compact_support g) (hg : continuous g)
  (x‚ÇÄ : G) (x : E'') : (f ‚ãÜ[L.precompR E'', Œº] g) x‚ÇÄ x = (f ‚ãÜ[L, Œº] (Œª a, g a x)) x‚ÇÄ  :=
begin
  have := hcg.convolution_exists_right (L.precompR E'' : _) hf hg x‚ÇÄ,
  simp_rw [convolution_def, continuous_linear_map.integral_apply this],
  refl,
end

variables [normed_space ùïú G] [sigma_finite Œº] [is_add_left_invariant Œº]

/-- Compute the total derivative of `f ‚ãÜ g` if `g` is `C^1` with compact support and `f` is locally
integrable. To write down the total derivative as a convolution, we use
`continuous_linear_map.precompR`. -/
lemma has_compact_support.has_fderiv_at_convolution_right
  (hcg : has_compact_support g) (hf : locally_integrable f Œº) (hg : cont_diff ùïú 1 g) (x‚ÇÄ : G) :
  has_fderiv_at (f ‚ãÜ[L, Œº] g) ((f ‚ãÜ[L.precompR G, Œº] fderiv ùïú g) x‚ÇÄ) x‚ÇÄ :=
begin
  rcases hcg.eq_zero_or_finite_dimensional ùïú hg.continuous with rfl|fin_dim,
  { have : fderiv ùïú (0 : G ‚Üí E') = 0, from fderiv_const (0 : E'),
    simp only [this, convolution_zero, pi.zero_apply],
    exact has_fderiv_at_const (0 : F) x‚ÇÄ },
  resetI,
  haveI : proper_space G, from finite_dimensional.proper_is_R_or_C ùïú G,
  set L' := L.precompR G,
  have h1 : ‚àÄ·∂† x in ùìù x‚ÇÄ, ae_strongly_measurable (Œª t, L (f t) (g (x - t))) Œº :=
  eventually_of_forall
    (hf.ae_strongly_measurable.convolution_integrand_snd L hg.continuous.ae_strongly_measurable),
  have h2 : ‚àÄ x, ae_strongly_measurable (Œª t, L' (f t) (fderiv ùïú g (x - t))) Œº,
  { exact hf.ae_strongly_measurable.convolution_integrand_snd L'
      (hg.continuous_fderiv le_rfl).ae_strongly_measurable },
  have h3 : ‚àÄ x t, has_fderiv_at (Œª x, g (x - t)) (fderiv ùïú g (x - t)) x,
  { intros x t,
    simpa using (hg.differentiable le_rfl).differentiable_at.has_fderiv_at.comp x
      ((has_fderiv_at_id x).sub (has_fderiv_at_const t x)) },
  let K' := - tsupport (fderiv ùïú g) + closed_ball x‚ÇÄ 1,
  have hK' : is_compact K' := (hcg.fderiv ùïú).neg.add (is_compact_closed_ball x‚ÇÄ 1),
  refine has_fderiv_at_integral_of_dominated_of_fderiv_le
    zero_lt_one h1 _ (h2 x‚ÇÄ) _ _ _,
  { exact K'.indicator (Œª t, ‚ÄñL'‚Äñ * ‚Äñf t‚Äñ * (‚®Ü x, ‚Äñfderiv ùïú g x‚Äñ)) },
  { exact hcg.convolution_exists_right L hf hg.continuous x‚ÇÄ },
  { refine eventually_of_forall (Œª t x hx, _),
    exact (hcg.fderiv ùïú).convolution_integrand_bound_right L'
      (hg.continuous_fderiv le_rfl) (ball_subset_closed_ball hx) },
  { rw [integrable_indicator_iff hK'.measurable_set],
    exact ((hf.integrable_on_is_compact hK').norm.const_mul _).mul_const _ },
  { exact eventually_of_forall (Œª t x hx, (L _).has_fderiv_at.comp x (h3 x t)) },
end

lemma has_compact_support.has_fderiv_at_convolution_left [is_neg_invariant Œº]
  (hcf : has_compact_support f) (hf : cont_diff ùïú 1 f) (hg : locally_integrable g Œº) (x‚ÇÄ : G) :
  has_fderiv_at (f ‚ãÜ[L, Œº] g) ((fderiv ùïú f ‚ãÜ[L.precompL G, Œº] g) x‚ÇÄ) x‚ÇÄ :=
begin
  simp only [‚Üê convolution_flip] {single_pass := tt},
  exact hcf.has_fderiv_at_convolution_right L.flip hg hf x‚ÇÄ,
end

end is_R_or_C

section real
/-! The one-variable case -/

variables [is_R_or_C ùïú]
variables [normed_space ùïú E]
variables [normed_space ùïú E']
variables [normed_space ‚Ñù F] [normed_space ùïú F]
variables {f‚ÇÄ : ùïú ‚Üí E} {g‚ÇÄ : ùïú ‚Üí E'}
variables {n : ‚Ñï‚àû}
variables (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
variables [complete_space F]
variables {Œº : measure ùïú}
variables [is_add_left_invariant Œº] [sigma_finite Œº]

lemma has_compact_support.has_deriv_at_convolution_right
  (hf : locally_integrable f‚ÇÄ Œº) (hcg : has_compact_support g‚ÇÄ) (hg : cont_diff ùïú 1 g‚ÇÄ)
  (x‚ÇÄ : ùïú) :
  has_deriv_at (f‚ÇÄ ‚ãÜ[L, Œº] g‚ÇÄ) ((f‚ÇÄ ‚ãÜ[L, Œº] deriv g‚ÇÄ) x‚ÇÄ) x‚ÇÄ :=
begin
  convert (hcg.has_fderiv_at_convolution_right L hf hg x‚ÇÄ).has_deriv_at,
  rw [convolution_precompR_apply L hf (hcg.fderiv ùïú) (hg.continuous_fderiv le_rfl)],
  refl,
end

lemma has_compact_support.has_deriv_at_convolution_left [is_neg_invariant Œº]
  (hcf : has_compact_support f‚ÇÄ) (hf : cont_diff ùïú 1 f‚ÇÄ)
  (hg : locally_integrable g‚ÇÄ Œº) (x‚ÇÄ : ùïú) :
  has_deriv_at (f‚ÇÄ ‚ãÜ[L, Œº] g‚ÇÄ) ((deriv f‚ÇÄ ‚ãÜ[L, Œº] g‚ÇÄ) x‚ÇÄ) x‚ÇÄ :=
begin
  simp only [‚Üê convolution_flip] {single_pass := tt},
  exact hcf.has_deriv_at_convolution_right L.flip hg hf x‚ÇÄ,
end

end real

section with_param

variables [is_R_or_C ùïú] [normed_space ùïú E] [normed_space ùïú E'] [normed_space ùïú E'']
[normed_space ‚Ñù F] [normed_space ùïú F] [complete_space F] [measurable_space G]
[normed_add_comm_group G] [borel_space G] [normed_space ùïú G]
[normed_add_comm_group P] [normed_space ùïú P]
{Œº : measure G} (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)

/-- The derivative of the convolution `f * g` is given by `f * Dg`, when `f` is locally integrable
and `g` is `C^1` and compactly supported. Version where `g` depends on an additional parameter in an
open subset `s` of a parameter space `P` (and the compact support `k` is independent of the
parameter in `s`). -/
lemma has_fderiv_at_convolution_right_with_param
  {g : P ‚Üí G ‚Üí E'} {s : set P} {k : set G} (hs : is_open s) (hk : is_compact k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : cont_diff_on ùïú 1 ‚Üøg (s √óÀ¢ univ))
  (q‚ÇÄ : P √ó G) (hq‚ÇÄ : q‚ÇÄ.1 ‚àà s) :
  has_fderiv_at (Œª (q : P √ó G), (f ‚ãÜ[L, Œº] g q.1) q.2)
    ((f ‚ãÜ[L.precompR (P √ó G), Œº] (Œª (x : G), fderiv ùïú ‚Üøg (q‚ÇÄ.1, x))) q‚ÇÄ.2) q‚ÇÄ :=
begin
  let g' := fderiv ùïú ‚Üøg,
  have A : ‚àÄ p ‚àà s, continuous (g p),
  { assume p hp,
    apply hg.continuous_on.comp_continuous (continuous_const.prod_mk continuous_id') (Œª x, _),
    simpa only [prod_mk_mem_set_prod_eq, mem_univ, and_true] using hp },
  have A' : ‚àÄ (q : P √ó G), q.1 ‚àà s ‚Üí s √óÀ¢ univ ‚àà ùìù q,
  { assume q hq,
    apply (hs.prod is_open_univ).mem_nhds,
    simpa only [mem_prod, mem_univ, and_true] using hq },
  /- The derivative of `g` vanishes away from `k`. -/
  have g'_zero : ‚àÄ p x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g' (p, x) = 0,
  { assume p x hp hx,
    refine (has_fderiv_at_zero_of_eventually_const 0 _).fderiv,
    have M2 : k·∂ú ‚àà ùìù x, from is_open.mem_nhds hk.is_closed.is_open_compl hx,
    have M1 : s ‚àà ùìù p, from hs.mem_nhds hp,
    rw nhds_prod_eq,
    filter_upwards [prod_mem_prod M1 M2],
    rintros ‚ü®p, y‚ü© ‚ü®hp, hy‚ü©,
    exact hgs p y hp hy },
  /- We find a small neighborhood of `{q‚ÇÄ.1} √ó k` on which the derivative is uniformly bounded. This
  follows from the continuity at all points of the compact set `k`. -/
  obtain ‚ü®Œµ, C, Œµpos, Cnonneg, h‚ÇÄŒµ, hŒµ‚ü© :
    ‚àÉ Œµ C, 0 < Œµ ‚àß 0 ‚â§ C ‚àß ball q‚ÇÄ.1 Œµ ‚äÜ s ‚àß ‚àÄ p x, ‚Äñp - q‚ÇÄ.1‚Äñ < Œµ ‚Üí ‚Äñg' (p, x)‚Äñ ‚â§ C,
  { have A : is_compact ({q‚ÇÄ.1} √óÀ¢ k), from is_compact_singleton.prod hk,
    obtain ‚ü®t, kt, t_open, ht‚ü© : ‚àÉ t, {q‚ÇÄ.1} √óÀ¢ k ‚äÜ t ‚àß is_open t ‚àß bounded (g' '' t),
    { have B : continuous_on g' (s √óÀ¢ univ),
        from hg.continuous_on_fderiv_of_open (hs.prod is_open_univ) le_rfl,
      apply exists_is_open_bounded_image_of_is_compact_of_continuous_on A
        (hs.prod is_open_univ) _ B,
      simp only [prod_subset_prod_iff, hq‚ÇÄ, singleton_subset_iff, subset_univ, and_self, true_or] },
    obtain ‚ü®Œµ, Œµpos, hŒµ, h'Œµ‚ü© :
      ‚àÉ (Œµ : ‚Ñù), 0 < Œµ ‚àß thickening Œµ ({q‚ÇÄ.fst} √óÀ¢ k) ‚äÜ t ‚àß ball q‚ÇÄ.1 Œµ ‚äÜ s,
    { obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ (Œµ : ‚Ñù), 0 < Œµ ‚àß thickening Œµ ({q‚ÇÄ.fst} √óÀ¢ k) ‚äÜ t,
        from A.exists_thickening_subset_open t_open kt,
      obtain ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü© : ‚àÉ (Œ¥ : ‚Ñù) (H : 0 < Œ¥), ball q‚ÇÄ.1 Œ¥ ‚äÜ s,
        from metric.is_open_iff.1 hs _ hq‚ÇÄ,
      refine ‚ü®min Œµ Œ¥, lt_min Œµpos Œ¥pos, _, _‚ü©,
      { exact subset.trans (thickening_mono (min_le_left _ _) _) hŒµ },
      { exact subset.trans (ball_subset_ball (min_le_right _ _)) hŒ¥ } },
    obtain ‚ü®C, Cpos, hC‚ü© : ‚àÉ C, 0 < C ‚àß g' '' t ‚äÜ closed_ball 0 C, from ht.subset_ball_lt 0 0,
    refine ‚ü®Œµ, C, Œµpos, Cpos.le, h'Œµ, Œª p x hp, _‚ü©,
    have hps : p ‚àà s, from h'Œµ (mem_ball_iff_norm.2 hp),
    by_cases hx : x ‚àà k,
    { have H : (p, x) ‚àà t,
      { apply hŒµ,
        refine mem_thickening_iff.2 ‚ü®(q‚ÇÄ.1, x), _, _‚ü©,
        { simp only [hx, singleton_prod, mem_image, prod.mk.inj_iff, eq_self_iff_true, true_and,
            exists_eq_right] },
        { rw ‚Üê dist_eq_norm at hp,
          simpa only [prod.dist_eq, Œµpos, dist_self, max_lt_iff, and_true] using hp } },
      have : g' (p, x) ‚àà closed_ball (0 : P √ó G ‚ÜíL[ùïú] E') C, from hC (mem_image_of_mem _ H),
      rwa mem_closed_ball_zero_iff at this },
    { have : g' (p, x) = 0, from g'_zero _ _ hps hx,
      rw this,
      simpa only [norm_zero] using Cpos.le } },
  /- Now, we wish to apply a theorem on differentiation of integrals. For this, we need to check
  trivial measurability or integrability assumptions (in `I1`, `I2`, `I3`), as well as a uniform
  integrability assumption over the derivative (in `I4` and `I5`) and pointwise differentiability
  in `I6`. -/
  have I1 : ‚àÄ·∂† (x : P √ó G) in ùìù q‚ÇÄ,
    ae_strongly_measurable (Œª (a : G), L (f a) (g x.1 (x.2 - a))) Œº,
  { filter_upwards [A' q‚ÇÄ hq‚ÇÄ],
    rintros ‚ü®p, x‚ü© ‚ü®hp, hx‚ü©,
    refine (has_compact_support.convolution_exists_right L _ hf (A _ hp) _).1,
    apply is_compact_of_is_closed_subset hk (is_closed_tsupport _),
    exact closure_minimal (support_subset_iff'.2 (Œª z hz, hgs _ _ hp hz)) hk.is_closed, },
  have I2 : integrable (Œª (a : G), L (f a) (g q‚ÇÄ.1 (q‚ÇÄ.2 - a))) Œº,
  { have M : has_compact_support (g q‚ÇÄ.1),
      from has_compact_support.intro hk (Œª x hx, hgs q‚ÇÄ.1 x hq‚ÇÄ hx),
    apply M.convolution_exists_right L hf (A q‚ÇÄ.1 hq‚ÇÄ) q‚ÇÄ.2 },
  have I3 : ae_strongly_measurable (Œª (a : G), (L (f a)).comp (g' (q‚ÇÄ.fst, q‚ÇÄ.snd - a))) Œº,
  { have T : has_compact_support (Œª y, g' (q‚ÇÄ.1, y)),
      from has_compact_support.intro hk (Œª x hx, g'_zero q‚ÇÄ.1 x hq‚ÇÄ hx),
    apply (has_compact_support.convolution_exists_right (L.precompR (P √ó G) : _) T hf _ q‚ÇÄ.2).1,
    have : continuous_on g' (s √óÀ¢ univ),
        from hg.continuous_on_fderiv_of_open (hs.prod is_open_univ) le_rfl,
    apply this.comp_continuous (continuous_const.prod_mk continuous_id'),
    assume x,
    simpa only [prod_mk_mem_set_prod_eq, mem_univ, and_true] using hq‚ÇÄ },
  set K' := - k + {q‚ÇÄ.2} with K'_def,
  have hK' : is_compact K' := hk.neg.add is_compact_singleton,
  obtain ‚ü®U, U_open, K'U, hU‚ü© : ‚àÉ U, is_open U ‚àß K' ‚äÜ U ‚àß integrable_on f U Œº,
    from hf.integrable_on_nhds_is_compact hK',
  obtain ‚ü®Œ¥, Œ¥pos, Œ¥Œµ, hŒ¥‚ü© : ‚àÉ Œ¥, (0 : ‚Ñù) < Œ¥ ‚àß Œ¥ ‚â§ Œµ ‚àß K' + ball 0 Œ¥ ‚äÜ U,
  { obtain ‚ü®V, V_mem, hV‚ü© : ‚àÉ (V : set G) (V_mem : V ‚àà ùìù (0 : G)), K' + V ‚äÜ U,
      from compact_open_separated_add_right hK' U_open K'U,
    rcases metric.mem_nhds_iff.1 V_mem with ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü©,
    refine ‚ü®min Œ¥ Œµ, lt_min Œ¥pos Œµpos, min_le_right _ _, _‚ü©,
    exact (add_subset_add_left ((ball_subset_ball (min_le_left _ _)).trans hŒ¥)).trans hV },
  let bound : G ‚Üí ‚Ñù := indicator U (Œª a, ‚ÄñL.precompR (P √ó G)‚Äñ * ‚Äñf a‚Äñ * C),
  have I4 : ‚àÄ·µê (a : G) ‚àÇŒº, ‚àÄ (x : P √ó G), dist x q‚ÇÄ < Œ¥ ‚Üí
    ‚ÄñL.precompR (P √ó G) (f a) (g' (x.fst, x.snd - a))‚Äñ ‚â§ bound a,
  { apply eventually_of_forall,
    assume a x hx,
    rw [prod.dist_eq, dist_eq_norm, dist_eq_norm] at hx,
    have : -tsupport (Œª a, g' (x.1, a)) + ball q‚ÇÄ.2 Œ¥ ‚äÜ U,
    { apply subset.trans _ hŒ¥,
      rw [K'_def, add_assoc],
      apply add_subset_add,
      { rw neg_subset_neg,
        apply closure_minimal (support_subset_iff'.2 (Œª z hz, _)) hk.is_closed,
        apply g'_zero x.1 z (h‚ÇÄŒµ _) hz,
        rw mem_ball_iff_norm,
        exact ((le_max_left _ _).trans_lt hx).trans_le Œ¥Œµ },
      { simp only [add_ball, thickening_singleton, zero_vadd] } },
    apply convolution_integrand_bound_right_of_le_of_subset _ _ _ this,
    { assume y,
      exact hŒµ _ _ (((le_max_left _ _).trans_lt hx).trans_le Œ¥Œµ) },
    { rw mem_ball_iff_norm,
      exact (le_max_right _ _).trans_lt hx } },
  have I5 : integrable bound Œº,
  { rw [integrable_indicator_iff U_open.measurable_set],
    exact (hU.norm.const_mul _).mul_const _ },
  have I6 : ‚àÄ·µê (a : G) ‚àÇŒº, ‚àÄ (x : P √ó G), dist x q‚ÇÄ < Œ¥ ‚Üí
    has_fderiv_at (Œª (x : P √ó G), L (f a) (g x.1 (x.2 - a)))
      ((L (f a)).comp (g' (x.fst, x.snd - a))) x,
  { apply eventually_of_forall,
    assume a x hx,
    apply (L _).has_fderiv_at.comp x,
    have N : s √óÀ¢ univ ‚àà ùìù (x.1, x.2 - a),
    { apply A',
      apply h‚ÇÄŒµ,
      rw prod.dist_eq at hx,
      exact lt_of_lt_of_le (lt_of_le_of_lt (le_max_left _ _) hx) Œ¥Œµ },
    have Z := ((hg.differentiable_on le_rfl).differentiable_at N).has_fderiv_at,
    have Z' : has_fderiv_at (Œª (x : P √ó G), (x.1, x.2 - a)) (continuous_linear_map.id ùïú (P √ó G)) x,
    { have : (Œª (x : P √ó G), (x.1, x.2 - a)) = id - (Œª x, (0, a)),
      { ext x; simp only [pi.sub_apply, id.def, prod.fst_sub, sub_zero, prod.snd_sub] },
      simp_rw [this],
      exact (has_fderiv_at_id x).sub_const (0, a) },
    exact Z.comp x Z' },
  exact has_fderiv_at_integral_of_dominated_of_fderiv_le Œ¥pos I1 I2 I3 I4 I5 I6,
end

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`).
In this version, all the types belong to the same universe (to get an induction working in the
proof). Use instead `cont_diff_on_convolution_right_with_param`, which removes this restriction. -/
lemma cont_diff_on_convolution_right_with_param_aux
  {G : Type uP} {E' : Type uP} {F : Type uP} {P : Type uP}
  [normed_add_comm_group E'] [normed_add_comm_group F]
  [normed_space ùïú E'] [normed_space ‚Ñù F] [normed_space ùïú F] [complete_space F]
  [measurable_space G] {Œº : measure G} [normed_add_comm_group G] [borel_space G] [normed_space ùïú G]
  [normed_add_comm_group P] [normed_space ùïú P]
  {f : G ‚Üí E} {n : ‚Ñï‚àû} (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
  {g : P ‚Üí G ‚Üí E'}
  {s : set P} {k : set G} (hs : is_open s) (hk : is_compact k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : cont_diff_on ùïú n ‚Üøg (s √óÀ¢ univ)) :
  cont_diff_on ùïú n (Œª (q : P √ó G), (f ‚ãÜ[L, Œº] g q.1) q.2) (s √óÀ¢ univ) :=
begin
  /- We have a formula for the derivation of `f * g`, which is of the same form, thanks to
  `has_fderiv_at_convolution_right_with_param`. Therefore, we can prove the result by induction on
  `n` (but for this we need the spaces at the different steps of the induction to live in the same
  universe, which is why we make the assumption in the lemma that all the relevant spaces
  come from the same universe). -/
  unfreezingI { induction n using enat.nat_induction with n ih ih generalizing g E' F },
  { rw [cont_diff_on_zero] at hg ‚ä¢,
    exact continuous_on_convolution_right_with_param L hk hgs hf hg },
  { let f' : P ‚Üí G ‚Üí (P √ó G ‚ÜíL[ùïú] F) := Œª p a,
      (f ‚ãÜ[L.precompR (P √ó G), Œº] (Œª (x : G), fderiv ùïú (uncurry g) (p, x))) a,
    have A : ‚àÄ (q‚ÇÄ : P √ó G), q‚ÇÄ.1 ‚àà s ‚Üí
      has_fderiv_at (Œª (q : P √ó G), (f ‚ãÜ[L, Œº] g q.1) q.2) (f' q‚ÇÄ.1 q‚ÇÄ.2) q‚ÇÄ,
        from has_fderiv_at_convolution_right_with_param L hs hk hgs hf hg.one_of_succ,
    rw cont_diff_on_succ_iff_fderiv_of_open (hs.prod (@is_open_univ G _)) at ‚ä¢ hg,
    split,
    { rintros ‚ü®p, x‚ü© ‚ü®hp, hx‚ü©,
      exact (A (p, x) hp).differentiable_at.differentiable_within_at, },
    { suffices H : cont_diff_on ùïú n ‚Üøf' (s √óÀ¢ univ),
      { apply H.congr,
        rintros ‚ü®p, x‚ü© ‚ü®hp, hx‚ü©,
        exact (A (p, x) hp).fderiv },
      have B : ‚àÄ (p : P) (x : G), p ‚àà s ‚Üí x ‚àâ k ‚Üí fderiv ùïú (uncurry g) (p, x) = 0,
      { assume p x hp hx,
        apply (has_fderiv_at_zero_of_eventually_const (0 : E') _).fderiv,
        have M2 : k·∂ú ‚àà ùìù x, from is_open.mem_nhds hk.is_closed.is_open_compl hx,
        have M1 : s ‚àà ùìù p, from hs.mem_nhds hp,
        rw nhds_prod_eq,
        filter_upwards [prod_mem_prod M1 M2],
        rintros ‚ü®p, y‚ü© ‚ü®hp, hy‚ü©,
        exact hgs p y hp hy },
      apply ih (L.precompR (P √ó G) : _) B,
      convert hg.2,
      apply funext,
      rintros ‚ü®p, x‚ü©,
      refl } },
  { rw [cont_diff_on_top] at hg ‚ä¢,
    assume n,
    exact ih n L hgs (hg n) }
end

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
lemma cont_diff_on_convolution_right_with_param
  {f : G ‚Üí E} {n : ‚Ñï‚àû} (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F) {g : P ‚Üí G ‚Üí E'}
  {s : set P} {k : set G} (hs : is_open s) (hk : is_compact k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : cont_diff_on ùïú n ‚Üøg (s √óÀ¢ univ)) :
  cont_diff_on ùïú n (Œª (q : P √ó G), (f ‚ãÜ[L, Œº] g q.1) q.2) (s √óÀ¢ univ) :=
begin
  /- The result is known when all the universes are the same, from
  `cont_diff_on_convolution_right_with_param_aux`. We reduce to this situation by pushing
  everything through `ulift` continuous linear equivalences. -/
  let eG : Type (max uG uE' uF uP) := ulift G,
  borelize eG,
  let eE' : Type (max uE' uG uF uP) := ulift E',
  let eF : Type (max uF uG uE' uP) := ulift F,
  let eP : Type (max uP uG uE' uF) := ulift P,
  have isoG : eG ‚âÉL[ùïú] G := continuous_linear_equiv.ulift,
  have isoE' : eE' ‚âÉL[ùïú] E' := continuous_linear_equiv.ulift,
  have isoF : eF ‚âÉL[ùïú] F := continuous_linear_equiv.ulift,
  have isoP : eP ‚âÉL[ùïú] P := continuous_linear_equiv.ulift,
  let ef := f ‚àò isoG,
  let eŒº : measure eG := measure.map isoG.symm Œº,
  let eg : eP ‚Üí eG ‚Üí eE' := Œª ep ex, isoE'.symm (g (isoP ep) (isoG ex)),
  let eL := continuous_linear_map.comp
    ((continuous_linear_equiv.arrow_congr isoE' isoF).symm : (E' ‚ÜíL[ùïú] F) ‚ÜíL[ùïú] eE' ‚ÜíL[ùïú] eF) L,
  let R := (Œª (q : eP √ó eG), (ef ‚ãÜ[eL, eŒº] eg q.1) q.2),
  have R_contdiff : cont_diff_on ùïú n R ((isoP ‚Åª¬π' s) √óÀ¢ univ),
  { have hek : is_compact (isoG ‚Åª¬π' k),
      from isoG.to_homeomorph.closed_embedding.is_compact_preimage hk,
    have hes : is_open (isoP ‚Åª¬π' s), from isoP.continuous.is_open_preimage _ hs,
    refine cont_diff_on_convolution_right_with_param_aux eL hes hek _ _ _,
    { assume p x hp hx,
      simp only [comp_app, continuous_linear_equiv.prod_apply, linear_isometry_equiv.coe_coe,
        continuous_linear_equiv.map_eq_zero_iff],
      exact hgs _ _ hp hx },
    { apply (locally_integrable_map_homeomorph isoG.symm.to_homeomorph).2,
      convert hf,
      ext1 x,
      simp only [ef, continuous_linear_equiv.coe_to_homeomorph, comp_app,
        continuous_linear_equiv.apply_symm_apply], },
    { apply isoE'.symm.cont_diff.comp_cont_diff_on,
      apply hg.comp ((isoP.prod isoG).cont_diff).cont_diff_on,
      rintros ‚ü®p, x‚ü© ‚ü®hp, hx‚ü©,
      simpa only [mem_preimage, continuous_linear_equiv.prod_apply, prod_mk_mem_set_prod_eq,
        mem_univ, and_true] using hp } },
  have A : cont_diff_on ùïú n (isoF ‚àò R ‚àò (isoP.prod isoG).symm) (s √óÀ¢ univ),
  { apply isoF.cont_diff.comp_cont_diff_on,
    apply R_contdiff.comp (continuous_linear_equiv.cont_diff _).cont_diff_on,
    rintros ‚ü®p, x‚ü© ‚ü®hp, hx‚ü©,
    simpa only [mem_preimage, mem_prod, mem_univ, and_true, continuous_linear_equiv.prod_symm,
      continuous_linear_equiv.prod_apply, continuous_linear_equiv.apply_symm_apply] using hp },
  have : isoF ‚àò R ‚àò (isoP.prod isoG).symm = (Œª (q : P √ó G), (f ‚ãÜ[L, Œº] g q.1) q.2),
  { apply funext,
    rintros ‚ü®p, x‚ü©,
    simp only [R, linear_isometry_equiv.coe_coe, comp_app, continuous_linear_equiv.prod_symm,
      continuous_linear_equiv.prod_apply],
    simp only [convolution, eL, coe_comp', continuous_linear_equiv.coe_coe, comp_app, eŒº],
    rw [closed_embedding.integral_map, ‚Üê isoF.integral_comp_comm],
    swap, { exact isoG.symm.to_homeomorph.closed_embedding },
    congr' 1,
    ext1 a,
    simp only [ef, eg, comp_app, continuous_linear_equiv.apply_symm_apply, coe_comp',
      continuous_linear_equiv.prod_apply, continuous_linear_equiv.map_sub,
      continuous_linear_equiv.arrow_congr, continuous_linear_equiv.arrow_congrSL_symm_apply,
      continuous_linear_equiv.coe_coe, comp_app, continuous_linear_equiv.apply_symm_apply] },
  simp_rw [this] at A,
  exact A,
end

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of composition with an additional smooth function. -/
lemma cont_diff_on_convolution_right_with_param_comp
  {n : ‚Ñï‚àû} (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
  {s : set P} {v : P ‚Üí G} (hv : cont_diff_on ùïú n v s)
  {f : G ‚Üí E} {g : P ‚Üí G ‚Üí E'} {k : set G} (hs : is_open s) (hk : is_compact k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : cont_diff_on ùïú n ‚Üøg (s √óÀ¢ univ)) :
  cont_diff_on ùïú n (Œª x, (f ‚ãÜ[L, Œº] g x) (v x)) s :=
begin
  apply (cont_diff_on_convolution_right_with_param L hs hk hgs hf hg).comp
    (cont_diff_on_id.prod hv),
  assume x hx,
  simp only [hx, mem_preimage, prod_mk_mem_set_prod_eq, mem_univ, and_self, id.def],
end

/-- The convolution `g * f` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
lemma cont_diff_on_convolution_left_with_param [Œº.is_add_left_invariant] [Œº.is_neg_invariant]
  (L : E' ‚ÜíL[ùïú] E ‚ÜíL[ùïú] F) {f : G ‚Üí E} {n : ‚Ñï‚àû}
  {g : P ‚Üí G ‚Üí E'} {s : set P} {k : set G} (hs : is_open s) (hk : is_compact k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : cont_diff_on ùïú n ‚Üøg (s √óÀ¢ univ)) :
  cont_diff_on ùïú n (Œª (q : P √ó G), (g q.1 ‚ãÜ[L, Œº] f) q.2) (s √óÀ¢ univ) :=
by simpa only [convolution_flip]
  using cont_diff_on_convolution_right_with_param L.flip hs hk hgs hf hg

/-- The convolution `g * f` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of composition with additional smooth functions. -/
lemma cont_diff_on_convolution_left_with_param_comp [Œº.is_add_left_invariant] [Œº.is_neg_invariant]
  (L : E' ‚ÜíL[ùïú] E ‚ÜíL[ùïú] F) {s : set P} {n : ‚Ñï‚àû} {v : P ‚Üí G} (hv : cont_diff_on ùïú n v s)
  {f : G ‚Üí E} {g : P ‚Üí G ‚Üí E'} {k : set G} (hs : is_open s) (hk : is_compact k)
  (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
  (hf : locally_integrable f Œº) (hg : cont_diff_on ùïú n ‚Üøg (s √óÀ¢ univ)) :
  cont_diff_on ùïú n (Œª x, (g x ‚ãÜ[L, Œº] f) (v x)) s :=
begin
  apply (cont_diff_on_convolution_left_with_param L hs hk hgs hf hg).comp (cont_diff_on_id.prod hv),
  assume x hx,
  simp only [hx, mem_preimage, prod_mk_mem_set_prod_eq, mem_univ, and_self, id.def],
end

lemma has_compact_support.cont_diff_convolution_right {n : ‚Ñï‚àû}
  (hcg : has_compact_support g) (hf : locally_integrable f Œº) (hg : cont_diff ùïú n g) :
  cont_diff ùïú n (f ‚ãÜ[L, Œº] g) :=
begin
  rcases exists_compact_iff_has_compact_support.2 hcg with ‚ü®k, hk, h'k‚ü©,
  rw ‚Üê cont_diff_on_univ,
  exact cont_diff_on_convolution_right_with_param_comp L cont_diff_on_id is_open_univ hk
    (Œª p x hp hx, h'k x hx) hf (hg.comp cont_diff_snd).cont_diff_on,
end

lemma has_compact_support.cont_diff_convolution_left [Œº.is_add_left_invariant] [Œº.is_neg_invariant]
  {n : ‚Ñï‚àû} (hcf : has_compact_support f) (hf : cont_diff ùïú n f) (hg : locally_integrable g Œº) :
  cont_diff ùïú n (f ‚ãÜ[L, Œº] g) :=
by { rw [‚Üê convolution_flip], exact hcf.cont_diff_convolution_right L.flip hg hf }

end with_param

section nonneg

variables [normed_space ‚Ñù E] [normed_space ‚Ñù E'] [normed_space ‚Ñù F] [complete_space F]

/-- The forward convolution of two functions `f` and `g` on `‚Ñù`, with respect to a continuous
bilinear map `L` and measure `ŒΩ`. It is defined to be the function mapping `x` to
`‚à´ t in 0..x, L (f t) (g (x - t)) ‚àÇŒΩ` if `0 < x`, and 0 otherwise. -/
noncomputable def pos_convolution
  (f : ‚Ñù ‚Üí E) (g : ‚Ñù ‚Üí E') (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F) (ŒΩ : measure ‚Ñù . volume_tac) : ‚Ñù ‚Üí F :=
indicator (Ioi (0:‚Ñù)) (Œª x, ‚à´ t in 0..x, L (f t) (g (x - t)) ‚àÇŒΩ)

lemma pos_convolution_eq_convolution_indicator
  (f : ‚Ñù ‚Üí E) (g : ‚Ñù ‚Üí E') (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F) (ŒΩ : measure ‚Ñù . volume_tac) [has_no_atoms ŒΩ] :
  pos_convolution f g L ŒΩ = convolution (indicator (Ioi 0) f) (indicator (Ioi 0) g) L ŒΩ :=
begin
  ext1 x,
  rw [convolution, pos_convolution, indicator],
  split_ifs,
  { rw [interval_integral.integral_of_le (le_of_lt h),
      integral_Ioc_eq_integral_Ioo,
      ‚Üêintegral_indicator (measurable_set_Ioo : measurable_set (Ioo 0 x))],
    congr' 1 with t : 1,
    have : (t ‚â§ 0) ‚à® (t ‚àà Ioo 0 x) ‚à® (x ‚â§ t),
    { rcases le_or_lt t 0 with h | h,
      { exact or.inl h },
      { rcases lt_or_le t x with h' | h',
        exacts [or.inr (or.inl ‚ü®h, h'‚ü©), or.inr (or.inr h')] } },
    rcases this with ht|ht|ht,
    { rw [indicator_of_not_mem (not_mem_Ioo_of_le ht), indicator_of_not_mem (not_mem_Ioi.mpr ht),
        continuous_linear_map.map_zero, continuous_linear_map.zero_apply] },
    { rw [indicator_of_mem ht, indicator_of_mem (mem_Ioi.mpr ht.1),
        indicator_of_mem (mem_Ioi.mpr $ sub_pos.mpr ht.2)] },
    { rw [indicator_of_not_mem (not_mem_Ioo_of_ge ht),
        indicator_of_not_mem (not_mem_Ioi.mpr (sub_nonpos_of_le ht)),
        continuous_linear_map.map_zero] } },
  { convert (integral_zero ‚Ñù F).symm,
    ext1 t,
    by_cases ht : 0 < t,
    { rw [indicator_of_not_mem (_ : x - t ‚àâ Ioi 0), continuous_linear_map.map_zero],
      rw not_mem_Ioi at h ‚ä¢,
      exact sub_nonpos.mpr (h.trans ht.le) },
    { rw [indicator_of_not_mem (mem_Ioi.not.mpr ht), continuous_linear_map.map_zero,
        continuous_linear_map.zero_apply] } }
end

lemma integrable_pos_convolution {f : ‚Ñù ‚Üí E} {g : ‚Ñù ‚Üí E'} {Œº ŒΩ : measure ‚Ñù}
  [sigma_finite Œº] [sigma_finite ŒΩ] [is_add_right_invariant Œº] [has_no_atoms ŒΩ]
  (hf : integrable_on f (Ioi 0) ŒΩ) (hg : integrable_on g (Ioi 0) Œº) (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F) :
  integrable (pos_convolution f g L ŒΩ) Œº :=
begin
  rw ‚Üêintegrable_indicator_iff (measurable_set_Ioi : measurable_set (Ioi (0:‚Ñù))) at hf hg,
  rw pos_convolution_eq_convolution_indicator f g L ŒΩ,
  exact (hf.convolution_integrand L hg).integral_prod_left,
end

/-- The integral over `Ioi 0` of a forward convolution of two functions is equal to the product
of their integrals over this set. (Compare `integral_convolution` for the two-sided convolution.) -/
lemma integral_pos_convolution [complete_space E] [complete_space E'] {Œº ŒΩ : measure ‚Ñù}
  [sigma_finite Œº] [sigma_finite ŒΩ] [is_add_right_invariant Œº] [has_no_atoms ŒΩ]
  {f : ‚Ñù ‚Üí E} {g : ‚Ñù ‚Üí E'} (hf : integrable_on f (Ioi 0) ŒΩ)
  (hg : integrable_on g (Ioi 0) Œº) (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F)  :
  ‚à´ x:‚Ñù in Ioi 0, (‚à´ t:‚Ñù in 0..x, L (f t) (g (x - t)) ‚àÇŒΩ) ‚àÇŒº =
    L (‚à´ x:‚Ñù in Ioi 0, f x ‚àÇŒΩ) (‚à´ x:‚Ñù in Ioi 0, g x ‚àÇŒº) :=
begin
  rw ‚Üêintegrable_indicator_iff (measurable_set_Ioi : measurable_set (Ioi (0:‚Ñù))) at hf hg,
  simp_rw ‚Üêintegral_indicator measurable_set_Ioi,
  convert integral_convolution L hf hg using 2,
  apply pos_convolution_eq_convolution_indicator,
end

end nonneg
