/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import analysis.special_functions.complex.circle
import measure_theory.group.integration

/-!
# The Fourier transform

We set up the Fourier transform for complex-valued functions on finite-dimensional spaces.

## Design choices

We define Fourier transforms in the following context:
* `ğ•œ` is a commutative ring.
* `V` and `W` are `ğ•œ`-modules.
* `e` is a unitary additive character of `ğ•œ`, i.e. a homomorphism `(multiplicative ğ•œ) â†’* circle`.
* `Î¼` is a measure on `V`.
* `L` is a `ğ•œ`-bilinear form `V Ã— W â†’ ğ•œ`.
* `E` is a complete normed `â„‚`-vector space.

With these definitions, the Fourier transform is a map from functions `V â†’ E` to
functions `W â†’ E`, defined by sending `f` to

`Î» w, âˆ« v in V, e [-L v w] â€¢ f v âˆ‚Î¼`,

where `e [x]` is notational sugar for `(e (multiplicative.of_add x) : â„‚)` (available in locale
`fourier_transform`).

The most familiar case, of course, is
* `ğ•œ = V = W = â„`
* `e = fourier_char` (defined below), i.e. the character `Î» x, exp (2 * Ï€ * I * x)`
* `Î¼ = volume`
* `L = linear_map.mul â„ â„`

The notation `ğ“•` is available in the locale `fourier_transform` as a shortcut for this case.
However, we set things up much more generally (number theorists care about the case when `ğ•œ` is an
adele ring, for instance).

## Main results

At present the only nontrivial lemma we prove is `continuous_fourier_transform`, stating that the
Fourier transform of an integrable function is continuous (under mild assumptions).
-/

noncomputable theory

local notation `ğ•Š` := circle

open measure_theory filter

open_locale topology

-- To avoid messing around with multiplicative vs. additive characters, we make a notation.
localized "notation e `[` x `]` := (e (multiplicative.of_add x) : â„‚)" in fourier_transform

section defs

variables
  {ğ•œ : Type*} [comm_ring ğ•œ]
  {V : Type*} [add_comm_group V] [module ğ•œ V] [measurable_space V]
  {W : Type*} [add_comm_group W] [module ğ•œ W]
  {E : Type*} [normed_add_comm_group E] [complete_space E] [normed_space â„‚ E]

/-- The Fourier transform of `f : V â†’ E`, with respect to a bilinear form `L : V Ã— W â†’ ğ•œ` and an
additive character `e`. -/
def fourier_transform
  (e : (multiplicative ğ•œ) â†’* ğ•Š) (Î¼ : measure V) (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ)
  (f : V â†’ E) (w : W) : E :=
âˆ« v, e [-L v w] â€¢ f v âˆ‚Î¼

lemma fourier_transform.smul_const
  (e : (multiplicative ğ•œ) â†’* ğ•Š) (Î¼ : measure V) (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ)
  (f : V â†’ E) (r : â„‚) :
  fourier_transform e Î¼ L (r â€¢ f) = r â€¢ (fourier_transform e Î¼ L f) :=
begin
  ext1 w,
  simp only [pi.smul_apply, fourier_transform, smul_comm _ r, integral_smul],
end

/-- The uniform norm of the Fourier transform of `f` is bounded by the `LÂ¹` norm of `f`. -/
lemma fourier_transform_norm_le {e : (multiplicative ğ•œ) â†’* ğ•Š} {Î¼ : measure V}
  (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) {f : V â†’ E} (hf : integrable f Î¼) (w : W) :
  â€–fourier_transform e Î¼ L f wâ€– â‰¤ â€–mem_â„’p.to_Lp f (mem_â„’p_one_iff_integrable.mpr hf)â€– :=
begin
  rw [Lp.norm_to_Lp, mem_â„’p.snorm_eq_integral_rpow_norm one_ne_zero ennreal.one_ne_top
    (mem_â„’p_one_iff_integrable.mpr hf)],
  have : (1 : ennreal).to_real = 1 := by refl,
  simp_rw [this, inv_one, real.rpow_one],
  rw ennreal.to_real_of_real,
  swap, exact (integral_nonneg (Î» _, norm_nonneg _)),
  refine le_trans (norm_integral_le_integral_norm _) (le_of_eq _),
  congr' 1 with x:1,
  rw [norm_smul, complex.norm_eq_abs, abs_coe_circle, one_mul],
end

/-- The Fourier transform converts right-translation into scalar multiplication by a phase factor.-/
lemma fourier_transform.comp_add_right [has_measurable_add V]
  (e : (multiplicative ğ•œ) â†’* ğ•Š) (Î¼ : measure V) [Î¼.is_add_right_invariant]
  (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E) (vâ‚€ : V) :
  fourier_transform e Î¼ L (f âˆ˜ (Î» v, v + vâ‚€)) = Î» w, e [L vâ‚€ w] â€¢ fourier_transform e Î¼ L f w :=
begin
  ext1 w,
  dsimp only [fourier_transform, function.comp_apply],
  conv in (L _) { rw â†add_sub_cancel v vâ‚€ },
  rw integral_add_right_eq_self (Î» (v : V), e [-L (v - vâ‚€) w] â€¢ f v),
  swap, apply_instance,
  dsimp only,
  rw â†integral_smul,
  congr' 1 with v,
  rw [â†smul_assoc, smul_eq_mul, â†submonoid.coe_mul, â†e.map_mul, â†of_add_add, â†linear_map.neg_apply,
    â†sub_eq_add_neg, â†linear_map.sub_apply, linear_map.map_sub, neg_sub]
end

end defs

section continuous
/- In this section we assume ğ•œ, V, W have topologies, and L, e are continuous (but f needn't be).
   This is used to ensure that `e [-L v w]` is (ae strongly) measurable. We could get away with
   imposing only a measurable-space structure on ğ•œ (it doesn't have to be the Borel sigma-algebra of
   a topology); but it seems hard to imagine cases where this extra generality would be useful, and
   allowing it would complicate matters in the most important use cases.
-/

variables
  {ğ•œ : Type*} [comm_ring ğ•œ] [topological_space ğ•œ] [topological_ring ğ•œ]
  {V : Type*} [add_comm_group V] [module ğ•œ V] [measurable_space V] [topological_space V]
    [borel_space V]
  {W : Type*} [add_comm_group W] [module ğ•œ W] [topological_space W]
  {E : Type*} [normed_add_comm_group E] [normed_space â„‚ E]
  {e : (multiplicative ğ•œ) â†’* ğ•Š} {Î¼ : measure V} {L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ}

/-- If `f` is integrable, then the Fourier transform integral is convergent for all `w`. -/
lemma fourier_integral_convergent
  (he : continuous e) (hL : continuous (Î» p : V Ã— W, L p.1 p.2))
  {f : V â†’ E} (hf : integrable f Î¼) (w : W) :
  integrable (Î» (v : V), (e [-L v w]) â€¢ f v) Î¼ :=
begin
  rw continuous_induced_rng at he,
  have c : continuous (Î» v, e[-L v w]),
  { refine he.comp (continuous_of_add.comp (continuous.neg _)),
    exact hL.comp (continuous_prod_mk.mpr âŸ¨continuous_id, continuous_constâŸ©) },
  rw â†integrable_norm_iff (c.ae_strongly_measurable.smul hf.1),
  convert hf.norm,
  ext1 v,
  rw [norm_smul, complex.norm_eq_abs, abs_coe_circle, one_mul]
end

variables [complete_space E]

lemma fourier_transform.add
  (he : continuous e) (hL : continuous (Î» p : V Ã— W, L p.1 p.2))
  {f g : V â†’ E} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  (fourier_transform e Î¼ L f) + (fourier_transform e Î¼ L g) = fourier_transform e Î¼ L (f + g) :=
begin
  ext1 w,
  dsimp only [pi.add_apply, fourier_transform],
  simp_rw smul_add,
  rw integral_add,
  { exact fourier_integral_convergent he hL hf w },
  { exact fourier_integral_convergent he hL hg w },
end

/-- The Fourier transform of an `L^1` function is a continuous function. -/
lemma fourier_transform_continuous [topological_space.first_countable_topology W]
  (he : continuous e) (hL : continuous (Î» p : V Ã— W, L p.1 p.2))
  {f : V â†’ E} (hf : integrable f Î¼) :
  continuous (fourier_transform e Î¼ L f) :=
begin
  apply continuous_of_dominated,
  { exact Î» w, (fourier_integral_convergent he hL hf w).1 },
  { refine Î» w, ae_of_all _ (Î» v, _),
    { exact Î» v, â€–f vâ€– },
    { rw [norm_smul, complex.norm_eq_abs, abs_coe_circle, one_mul] } },
  { exact hf.norm },
  { rw continuous_induced_rng at he,
    refine ae_of_all _ (Î» v, (he.comp (continuous_of_add.comp _)).smul continuous_const),
    refine (hL.comp (continuous_prod_mk.mpr âŸ¨continuous_const, continuous_idâŸ©)).neg }
end

end continuous

section real
open_locale real

/-- The standard additive character of `â„`, given by `Î» x, exp (2 * Ï€ * x * I)`. -/
def fourier_char : (multiplicative â„) â†’* ğ•Š :=
{ to_fun := Î» z, exp_map_circle (2 * Ï€ * z.to_add),
  map_one' := by rw [to_add_one, mul_zero, exp_map_circle_zero],
  map_mul' := Î» x y, by rw [to_add_mul, mul_add, exp_map_circle_add] }

lemma fourier_char_apply (x : â„) :
  fourier_char [x]  = complex.exp (2 * Ï€ * x * complex.I) :=
begin
  rw [fourier_char, monoid_hom.coe_mk, to_add_of_add, exp_map_circle_apply],
  push_cast,
end

@[continuity]
lemma continuous_fourier_char : continuous fourier_char :=
(map_continuous exp_map_circle).comp (continuous_const.mul continuous_to_add)

lemma fourier_transform_eq_integral_exp_smul
  {V : Type*} [add_comm_group V] [module â„ V] [measurable_space V]
  {W : Type*} [add_comm_group W] [module â„ W]
  {E : Type*} [normed_add_comm_group E] [complete_space E] [normed_space â„‚ E]
  (L : V â†’â‚—[â„] W â†’â‚—[â„] â„) (Î¼ : measure V) (f : V â†’ E) (w : W) :
  fourier_transform fourier_char Î¼ L f w
  = âˆ« (v : V), complex.exp (-2 * Ï€ * L v w * complex.I) â€¢ f v âˆ‚Î¼ :=
by simp_rw [fourier_transform, fourier_char_apply, complex.of_real_neg, mul_neg, neg_mul]

lemma fourier_transform_mul_eq_integral_exp_smul
  {E : Type*} [normed_add_comm_group E] [complete_space E] [normed_space â„‚ E]
  (Î¼ : measure â„) (f : â„ â†’ E) (w : â„) :
  fourier_transform fourier_char Î¼ (linear_map.mul â„ â„) f w
  = âˆ« (v : â„), complex.exp (-2 * Ï€ * v * w * complex.I) â€¢ f v âˆ‚Î¼ :=
by simp_rw [fourier_transform_eq_integral_exp_smul, linear_map.mul_apply', complex.of_real_mul,
  â†mul_assoc]

localized "notation `ğ“•` := fourier_transform fourier_char measure_theory.measure_space.volume
  (linear_map.mul â„ â„)" in fourier_transform

end real
