/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import analysis.fourier.add_circle

/-!
# The Fourier transform

We set up the Fourier transform for complex-valued functions on finite-dimensional spaces.

## Design choices

We define Fourier transforms in the following context:
* `ğ•œ` is a commutative ring.
* `V` and `W` are `ğ•œ`-modules.
* `e` is an additive character of `ğ•œ`, i.e. a homomorphism `(multiplicative ğ•œ) â†’* â„‚`.
* `Î¼` is a measure on `V`.
* `L` is a `ğ•œ`-bilinear form `V Ã— W â†’ ğ•œ`.
* `E` is a complete normed `â„‚`-vector space.

With these definitions, the Fourier transform is a map from functions `V â†’ E` to
functions `W â†’ E`, defined by sending `f` to

`Î» w, âˆ« v in V, e (multiplicative.of_add (-L(v, w)) â€¢ f v âˆ‚Î¼`.

The most familiar case, of course, is
* `ğ•œ = V = W = â„`
* `L = linear_map.mul â„ â„`
* `Î¼ = volume`
* `e = fourier_char` (defined below), i.e. the character `Î» x, exp (2 * Ï€ * I * x)`.

The notation `ğ“•` is available in the locale `fourier_transform` as a shortcut for this case.
However, we set things up much more generally (number theorists care about the case when `ğ•œ` is an
adele ring, for instance).

## Main results

At present the only nontrivial lemma we prove is `continuous_fourier_transform`, stating that the
Fourier transform of an integrable function is continuous (under mild assumptions).
-/

noncomputable theory

open measure_theory filter

open_locale topology

section defs

variables
  {ğ•œ : Type*} [comm_ring ğ•œ]
  {V : Type*} [add_comm_group V] [module ğ•œ V] [measurable_space V]
  {W : Type*} [add_comm_group W] [module ğ•œ W]
  {E : Type*} [normed_add_comm_group E] [complete_space E] [normed_space â„‚ E]

/-- The Fourier transform of `f : V â†’ E`, with respect to a bilinear form `L : V Ã— W â†’ ğ•œ` and an
additive character `e`. -/
def fourier_transform
  (e : (multiplicative ğ•œ) â†’* â„‚)
  (Î¼ : measure V)
  (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ)
  (f : V â†’ E) (w : W) : E :=
âˆ« v, e (multiplicative.of_add (-L v w)) â€¢ f v âˆ‚Î¼

lemma fourier_transform.smul
  (e : (multiplicative ğ•œ) â†’* â„‚) (Î¼ : measure V) (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ)
  (f : V â†’ E) (r : â„‚) :
  fourier_transform e Î¼ L (r â€¢ f) = r â€¢ (fourier_transform e Î¼ L f) :=
begin
  ext1 w,
  dsimp only [pi.smul_apply, fourier_transform],
  simp_rw [smul_comm _ r, integral_smul]
end

/-- A map to a normed space is *unitary* if its values have norm 1. -/
def is_unitary {Î± Î² : Type*} [has_norm Î²] (e : Î± â†’ Î²) : Prop :=
âˆ€ (x : Î±), â€–e xâ€– = 1

/-- The uniform norm of the Fourier transform of `f` is bounded by the `LÂ¹` norm of `f`. -/
lemma fourier_transform_norm_le {e : (multiplicative ğ•œ) â†’* â„‚} {Î¼ : measure V}
  (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (he' : is_unitary e) {f : V â†’ E} (hf : integrable f Î¼) (w : W) :
  â€–fourier_transform e Î¼ L f wâ€– â‰¤ â€–mem_â„’p.to_Lp f (mem_â„’p_one_iff_integrable.mpr hf)â€– :=
begin
  rw [Lp.norm_to_Lp, mem_â„’p.snorm_eq_integral_rpow_norm one_ne_zero ennreal.one_ne_top
    (mem_â„’p_one_iff_integrable.mpr hf)],
  have : (1 : ennreal).to_real = 1 := by refl,
  simp_rw [this, inv_one, real.rpow_one],
  rw ennreal.to_real_of_real,
  swap, exact (integral_nonneg (Î» _, norm_nonneg _)),
  refine le_trans (norm_integral_le_integral_norm _) (le_of_eq _),
  congr' 1 with x:1,
  rw [norm_smul, he', one_mul],
end

end defs

section continuous
-- In this section we assume ğ•œ, V, W have topologies, and L, e are continuous (but f needn't be).

variables
  {ğ•œ : Type*} [comm_ring ğ•œ] [topological_space ğ•œ] [topological_ring ğ•œ]
  {V : Type*} [add_comm_group V] [module ğ•œ V] [measurable_space V]
    [topological_space V] [opens_measurable_space V]
  {W : Type*} [add_comm_group W] [module ğ•œ W] [topological_space W]
  {E : Type*} [normed_add_comm_group E] [normed_space â„‚ E]
  {e : (multiplicative ğ•œ) â†’* â„‚} {Î¼ : measure V} {L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ}

/-- If `f` is integrable (and the additive character is unitary), then the Fourier
transform integral is convergent, for all `w`. -/
lemma fourier_integral_convergent
  (he : continuous e) (he' : is_unitary e)
  (hL : continuous (Î» p : V Ã— W, L p.1 p.2))
  {f : V â†’ E} (hf : integrable f Î¼) (w : W) :
  integrable (Î» (v : V), e (multiplicative.of_add (-L v w)) â€¢ f v) Î¼ :=
begin
  have c : continuous (Î» v, e (multiplicative.of_add (-L v w))),
  { refine he.comp (continuous_of_add.comp (continuous.neg _)),
    exact hL.comp (continuous_prod_mk.mpr âŸ¨continuous_id, continuous_constâŸ©) },
  rw â†integrable_norm_iff (c.ae_strongly_measurable.smul hf.1),
  convert hf.norm,
  ext1 v,
  rw [norm_smul, he' _, one_mul]
end

variables [complete_space E]

lemma fourier_transform.add
  (he : continuous e) (he' : is_unitary e) (hL : continuous (Î» p : V Ã— W, L p.1 p.2))
  {f g : V â†’ E} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  (fourier_transform e Î¼ L f) + (fourier_transform e Î¼ L g) = fourier_transform e Î¼ L (f + g) :=
begin
  ext1 w,
  dsimp only [pi.add_apply, fourier_transform],
  simp_rw smul_add,
  rw integral_add,
  { exact fourier_integral_convergent he he' hL hf w },
  { exact fourier_integral_convergent he he' hL hg w },
end

/-- The Fourier transform of an `L^1` function is a continuous function. -/
lemma fourier_transform_continuous [topological_space.first_countable_topology W]
  (he : continuous e) (he' : is_unitary e) (hL : continuous (Î» p : V Ã— W, L p.1 p.2))
  {f : V â†’ E} (hf : integrable f Î¼) :
  continuous (fourier_transform e Î¼ L f) :=
begin
  apply continuous_of_dominated,
  { exact Î» w, (fourier_integral_convergent he he' hL hf w).1 },
  { refine Î» w, ae_of_all _ (Î» v, _),
    { exact Î» v, â€–f vâ€– },
    { rw [norm_smul, he', one_mul] } },
  { exact hf.norm },
  { refine ae_of_all _ (Î» v, (he.comp (continuous_of_add.comp _)).smul continuous_const),
    refine (hL.comp (continuous_prod_mk.mpr âŸ¨continuous_const, continuous_idâŸ©)).neg }
end

end continuous

section real
open_locale real

/-- The standard additive character of `â„`, given by `Î» x, exp (2 * Ï€ * I * x)`. -/
def fourier_char : (multiplicative â„) â†’* â„‚ :=
{ to_fun := (Î» z, fourier 1 (z.to_add : unit_add_circle)),
  map_one' := by rw [to_add_one, quotient_add_group.coe_zero, fourier_eval_zero],
  map_mul' := Î» x y, by rw [to_add_mul, fourier_coe_apply, complex.of_real_add, mul_add, add_div,
    complex.exp_add, â†fourier_coe_apply, â†fourier_coe_apply] }

lemma fourier_char_apply (x : â„) :
  fourier_char (multiplicative.of_add x) = complex.exp (2 * Ï€ * complex.I * x) :=
by rw [fourier_char, monoid_hom.coe_mk, to_add_of_add, fourier_coe_apply,
    complex.of_real_one, int.cast_one, mul_one, div_one]

lemma fourier_transform_eq_integral_exp_smul
  {V : Type*} [add_comm_group V] [module â„ V] [measurable_space V]
  {W : Type*} [add_comm_group W] [module â„ W]
  {E : Type*} [normed_add_comm_group E] [complete_space E] [normed_space â„‚ E]
  (L : V â†’â‚—[â„] W â†’â‚—[â„] â„) (Î¼ : measure V) (f : V â†’ E) (w : W) :
  fourier_transform fourier_char Î¼ L f w
  = âˆ« (v : V), complex.exp (-2 * Ï€ * complex.I * L v w) â€¢ f v âˆ‚Î¼ :=
begin
  simp_rw [fourier_transform, fourier_char_apply, complex.of_real_neg],
  congr' 1 with v:1,
  congr' 2,
  ring,
end

lemma fourier_transform_mul_eq_integral_exp_smul
  {E : Type*} [normed_add_comm_group E] [complete_space E] [normed_space â„‚ E]
  (Î¼ : measure â„) (f : â„ â†’ E) (w : â„) :
  fourier_transform fourier_char Î¼ (linear_map.mul â„ â„) f w
  = âˆ« (v : â„), complex.exp (-2 * Ï€ * complex.I * v * w) â€¢ f v âˆ‚Î¼ :=
by simp_rw [fourier_transform_eq_integral_exp_smul, linear_map.mul_apply', complex.of_real_mul,
  â†mul_assoc]

localized "notation `ğ“•` := fourier_transform fourier_char measure_theory.measure_space.volume
  (linear_map.mul â„ â„)" in fourier_transform

lemma is_unitary_fourier_char : is_unitary fourier_char :=
Î» x, by rw [fourier_char, monoid_hom.coe_mk, fourier_apply_norm]

@[continuity]
lemma continuous_fourier_char : continuous fourier_char :=
(map_continuous (fourier 1)).comp (continuous_quotient_mk.comp continuous_to_add)

end real
