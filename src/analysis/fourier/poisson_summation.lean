/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import analysis.fourier.add_circle
import analysis.fourier.fourier_transform
import analysis.p_series
import analysis.schwartz_space

/-!
# Poisson's summation formula

We prove Poisson's summation formula `âˆ‘ (n : â„¤), f n = âˆ‘ (n : â„¤), ğ“• f n`, where `ğ“• f` is the
Fourier transform of `f`, under the following hypotheses:
* `f` is a continuous function `â„ â†’ â„‚`.
* The sum `âˆ‘ (n : â„¤), ğ“• f n` is convergent.
* For all compacts `K âŠ‚ â„`, the sum `âˆ‘ (n : â„¤), sup { â€–f(x + n)â€– | x âˆˆ K }` is convergent.
See `real.tsum_eq_tsum_fourier_integral` for this formulation.

These hypotheses are potentially a little awkward to apply, so we also provide the less general but
easier-to-use result `real.tsum_eq_tsum_fourier_integral_of_rpow_decay`, in which we assume `f` and
`ğ“• f` both decay as `|x| ^ (-b)` for some `b > 1`, and the even more specific result
`schwartz_map.tsum_eq_tsum_fourier_integral`, where we assume that both `f` and `ğ“• f` are Schwartz
functions.

## TODO

At the moment `schwartz_map.tsum_eq_tsum_fourier_integral` requires separate proofs that both `f`
and `ğ“• f` are Schwartz functions. In fact, `ğ“• f` is automatically Schwartz if `f` is; and once
we have this lemma in the library, we should adjust the hypotheses here accordingly.
-/

noncomputable theory

open function (hiding comp_apply) complex (hiding abs_of_nonneg) real set (hiding restrict_apply)
  topological_space filter measure_theory asymptotics

open_locale real big_operators filter fourier_transform

local attribute [instance] real.fact_zero_lt_one

open continuous_map

/-- The key lemma for Poisson summation: the `m`-th Fourier coefficient of the periodic function
`âˆ‘' n : â„¤, f (x + n)` is the value at `m` of the Fourier transform of `f`. -/
lemma real.fourier_coeff_tsum_comp_add {f : C(â„, â„‚)}
  (hf : âˆ€ (K : compacts â„), summable (Î» n : â„¤, â€–(f.comp (continuous_map.add_right n)).restrict Kâ€–))
  (m : â„¤) :
  fourier_coeff (periodic.lift $ f.periodic_tsum_comp_add_zsmul 1) m = ğ“• f m :=
begin
  -- NB: This proof can be shortened somewhat by telescoping together some of the steps in the calc
  -- block, but I think it's more legible this way. We start with preliminaries about the integrand.
  let e : C(â„, â„‚) := (fourier (-m)).comp âŸ¨(coe : â„ â†’ unit_add_circle), continuous_quotient_mkâŸ©,
  have neK : âˆ€ (K : compacts â„) (g : C(â„, â„‚)), â€–(e * g).restrict Kâ€– = â€–g.restrict Kâ€–,
  { have : âˆ€ (x : â„), â€–e xâ€– = 1, from Î» x, abs_coe_circle _,
    intros K g,
    simp_rw [norm_eq_supr_norm, restrict_apply, mul_apply, norm_mul, this, one_mul] },
  have eadd : âˆ€ (n : â„¤), e.comp (continuous_map.add_right n) = e,
  { intro n, ext1 x,
    have : periodic e 1, from periodic.comp (Î» x, add_circle.coe_add_period 1 x) _,
    simpa only [mul_one] using this.int_mul n x },
  -- Now the main argument. First unwind some definitions.
  calc fourier_coeff (periodic.lift $ f.periodic_tsum_comp_add_zsmul 1) m
  = âˆ« x in 0..1, e x * (âˆ‘' n : â„¤, f.comp (continuous_map.add_right n)) x :
    by simp_rw [fourier_coeff_eq_interval_integral _ m 0, div_one, one_smul, zero_add, comp_apply,
      coe_mk, periodic.lift_coe, zsmul_one, smul_eq_mul]
  -- Transform sum in C(â„, â„‚) evaluated at x into pointwise sum of values.
  ... = âˆ« x in 0..1, (âˆ‘' n : â„¤, (e * f.comp (continuous_map.add_right n)) x) :
    by simp_rw [coe_mul, pi.mul_apply, â†tsum_apply (summable_of_locally_summable_norm hf),
      tsum_mul_left]
  -- Swap sum and integral.
  ... = âˆ‘' n : â„¤, âˆ« x in 0..1, (e * f.comp (continuous_map.add_right n)) x :
    begin
      refine (interval_integral.tsum_interval_integral_eq_of_summable_norm _).symm,
      convert hf âŸ¨uIcc 0 1, is_compact_uIccâŸ©,
      exact funext (Î» n, neK _ _)
    end
  ... = âˆ‘' n : â„¤, âˆ« x in 0..1, (e * f).comp (continuous_map.add_right n) x :
    begin
      simp only [continuous_map.comp_apply, mul_comp] at eadd âŠ¢,
      simp_rw eadd,
    end
  -- Rearrange sum of interval integrals into an integral over `â„`.
  ... = âˆ« x, e x * f x :
    begin
      suffices : integrable â‡‘(e * f), from this.has_sum_interval_integral_comp_add_int.tsum_eq,
      apply integrable_of_summable_norm_Icc,
      convert hf âŸ¨Icc 0 1, is_compact_IccâŸ©,
      simp_rw [continuous_map.comp_apply, mul_comp] at eadd âŠ¢,
      simp_rw eadd,
      exact funext (Î» n, neK âŸ¨Icc 0 1, is_compact_IccâŸ© _),
    end
  -- Minor tidying to finish
  ... = ğ“• f m :
    begin
      rw fourier_integral_eq_integral_exp_smul,
      congr' 1 with x : 1,
      rw [smul_eq_mul, comp_apply, coe_mk, fourier_coe_apply],
      congr' 2,
      push_cast,
      ring
    end
end

/-- **Poisson's summation formula**, most general form. -/
theorem real.tsum_eq_tsum_fourier_integral {f : C(â„, â„‚)}
  (h_norm : âˆ€ (K : compacts â„),
    summable (Î» n : â„¤, â€–(f.comp $ continuous_map.add_right n).restrict Kâ€–))
  (h_sum : summable (Î» n : â„¤, ğ“• f n)) :
  âˆ‘' (n : â„¤), f n = âˆ‘' (n : â„¤), ğ“• f n :=
begin
  let F : C(unit_add_circle, â„‚) := âŸ¨(f.periodic_tsum_comp_add_zsmul 1).lift,
    continuous_coinduced_dom.mpr (map_continuous _)âŸ©,
  have : summable (fourier_coeff F),
  { convert h_sum,
    exact funext (Î» n, real.fourier_coeff_tsum_comp_add h_norm n) },
  convert (has_pointwise_sum_fourier_series_of_summable this 0).tsum_eq.symm using 1,
  { have := (has_sum_apply (summable_of_locally_summable_norm h_norm).has_sum 0).tsum_eq,
    simpa only [coe_mk, â†quotient_add_group.coe_zero, periodic.lift_coe, zsmul_one, comp_apply,
      coe_add_right, zero_add] using this },
  { congr' 1 with n : 1,
    rw [â†real.fourier_coeff_tsum_comp_add h_norm n, fourier_eval_zero, smul_eq_mul, mul_one],
    refl },
end

section rpow_decay

variables {E : Type*} [normed_add_comm_group E]

/-- If `f` is `O(x ^ (-b))` at infinity, then so is the function
`Î» x, â€–f.restrict (Icc (x + R) (x + S))â€–` for any fixed `R` and `S`. -/
lemma is_O_norm_Icc_restrict_at_top {f : C(â„, E)} {b : â„} (hb : 0 < b)
  (hf : is_O at_top f (Î» x : â„, |x| ^ (-b))) (R S : â„) :
  is_O at_top (Î» x : â„, â€–f.restrict (Icc (x + R) (x + S))â€–) (Î» x : â„, |x| ^ (-b)) :=
begin
  -- First establish an explicit estimate on decay of inverse powers.
  -- This is logically independent of the rest of the proof, but of no mathematical interest in
  -- itself, so it is proved using `async` rather than being formulated as a separate lemma.
  have claim : âˆ€ (x : â„), max 0 (-2 * R) < x â†’
    âˆ€ (y : â„), x + R â‰¤ y â†’ y ^ (-b) â‰¤ (1 / 2) ^ (-b) * x ^ (-b),
  { intros x hx y hy,
    rw max_lt_iff at hx,
    have hxR : 0 < x + R,
    { rcases le_or_lt 0 R with h|h,
      { exact add_pos_of_pos_of_nonneg hx.1 h },
      { rw [â†sub_lt_iff_lt_add, zero_sub],
        refine lt_trans _ hx.2,
        rwa [neg_mul, neg_lt_neg_iff, two_mul, add_lt_iff_neg_left] } },
    have hy' : 0 < y, from hxR.trans_le hy,
    have : y ^ (-b) â‰¤ (x + R) ^ (-b),
    { rw [rpow_neg hy'.le, rpow_neg hxR.le,
      inv_le_inv (rpow_pos_of_pos hy' _) (rpow_pos_of_pos hxR _)],
    exact rpow_le_rpow hxR.le hy hb.le },
    refine this.trans _,
    rw [â†mul_rpow one_half_pos.le hx.1.le, rpow_neg (mul_pos one_half_pos hx.1).le,
      rpow_neg hxR.le],
    refine inv_le_inv_of_le (rpow_pos_of_pos (mul_pos one_half_pos hx.1) _) _,
    exact rpow_le_rpow (mul_pos one_half_pos hx.1).le (by linarith) hb.le },
  -- Now the main proof.
  obtain âŸ¨c, hc, hc'âŸ© := hf.exists_pos,
  simp only [is_O, is_O_with, eventually_at_top] at hc' âŠ¢,
  obtain âŸ¨d, hdâŸ© := hc',
  refine âŸ¨c * (1 / 2) ^ (-b), âŸ¨max (1 + max 0 (-2 * R)) (d - R), Î» x hx, _âŸ©âŸ©,
  rw [ge_iff_le, max_le_iff] at hx,
  have hx' : max 0 (-2 * R) < x, by linarith,
  rw max_lt_iff at hx',
  rw [norm_norm, continuous_map.norm_le _
    (mul_nonneg (mul_nonneg hc.le $ rpow_nonneg_of_nonneg one_half_pos.le _) (norm_nonneg _))],
  refine Î» y, (hd y.1 (by linarith [hx.1, y.2.1])).trans _,
  have A : âˆ€ (x : â„), 0 â‰¤ |x| ^ (-b), from Î» x, by positivity,
  rwa [mul_assoc, mul_le_mul_left hc, norm_of_nonneg (A _), norm_of_nonneg (A _)],
  convert claim x (by linarith only [hx.1]) y.1 y.2.1,
  { apply abs_of_nonneg, linarith [y.2.1] },
  { exact abs_of_pos hx'.1 },
end

lemma is_O_norm_Icc_restrict_at_bot {f : C(â„, E)} {b : â„} (hb : 0 < b)
  (hf : is_O at_bot f (Î» x : â„, |x| ^ (-b))) (R S : â„) :
  is_O at_bot (Î» x : â„, â€–f.restrict (Icc (x + R) (x + S))â€–) (Î» x : â„, |x| ^ (-b)) :=
begin
  have h1 : is_O at_top (f.comp (continuous_map.mk _ continuous_neg)) (Î» x : â„, |x| ^ (-b)),
  { convert hf.comp_tendsto tendsto_neg_at_top_at_bot,
    ext1 x, simp only [function.comp_app, abs_neg] },
  have h2 := (is_O_norm_Icc_restrict_at_top hb h1 (-S) (-R)).comp_tendsto tendsto_neg_at_bot_at_top,
  have : ((Î» (x : â„), |x| ^ -b) âˆ˜ has_neg.neg) = (Î» (x : â„), |x| ^ -b),
  { ext1 x, simp only [function.comp_app, abs_neg] },
  rw this at h2,
  refine (is_O_of_le _ (Î» x, _)).trans h2, -- equality holds, but less work to prove `â‰¤` alone
  rw [norm_norm, function.comp_app, norm_norm, continuous_map.norm_le _ (norm_nonneg _)],
  rintro âŸ¨x, hxâŸ©,
  rw [continuous_map.restrict_apply_mk],
  refine (le_of_eq _).trans (continuous_map.norm_coe_le_norm _ âŸ¨-x, _âŸ©),
  { exact âŸ¨by linarith [hx.2], by linarith [hx.1]âŸ© },
  { rw [continuous_map.restrict_apply_mk, continuous_map.comp_apply, continuous_map.coe_mk,
      neg_neg] }
end

lemma is_O_norm_restrict_cocompact (f : C(â„, E)) {b : â„} (hb : 0 < b)
  (hf : is_O (cocompact â„) f (Î» x : â„, |x| ^ (-b))) (K : compacts â„) :
  is_O (cocompact â„) (Î» x, â€–(f.comp (continuous_map.add_right x)).restrict Kâ€–) (Î» x, |x| ^ (-b)) :=
begin
  obtain âŸ¨r, hrâŸ© := K.is_compact.bounded.subset_ball 0,
  rw [closed_ball_eq_Icc, zero_add, zero_sub] at hr,
  have : âˆ€ (x : â„), â€–(f.comp (continuous_map.add_right x)).restrict Kâ€– â‰¤
    â€–f.restrict (Icc (x - r) (x + r))â€–,
  { intro x,
    rw continuous_map.norm_le _ (norm_nonneg _),
    rintro âŸ¨y, hyâŸ©,
    refine (le_of_eq _).trans (continuous_map.norm_coe_le_norm _ âŸ¨y + x, _âŸ©),
    exact âŸ¨by linarith [(hr hy).1], by linarith [(hr hy).2]âŸ©,
    simp_rw [continuous_map.restrict_apply, continuous_map.comp_apply,
      continuous_map.coe_add_right, subtype.coe_mk] },
  simp_rw [cocompact_eq, is_O_sup] at hf âŠ¢,
  split,
  { refine (is_O_of_le at_bot _).trans (is_O_norm_Icc_restrict_at_bot hb hf.1 (-r) r),
    simp_rw norm_norm, exact this },
  { refine (is_O_of_le at_top _).trans (is_O_norm_Icc_restrict_at_top hb hf.2 (-r) r),
    simp_rw norm_norm, exact this },
end


/-- **Poisson's summation formula**, assuming that `f` decays as
`|x| ^ (-b)` for some `1 < b` and its Fourier transform is summable. -/
lemma real.tsum_eq_tsum_fourier_integral_of_rpow_decay_of_summable {f : â„ â†’ â„‚} (hc : continuous f)
  {b : â„} (hb : 1 < b) (hf : is_O (cocompact â„) f (Î» x : â„, |x| ^ (-b)))
  (hFf : summable (Î» n : â„¤, ğ“• f n)) :
  âˆ‘' (n : â„¤), f n = âˆ‘' (n : â„¤), ğ“• f n :=
real.tsum_eq_tsum_fourier_integral
  (Î» K, summable_of_is_O (real.summable_abs_int_rpow hb)
    ((is_O_norm_restrict_cocompact (continuous_map.mk _ hc)
    (zero_lt_one.trans hb) hf K).comp_tendsto int.tendsto_coe_cofinite)) hFf

/-- **Poisson's summation formula**, assuming that both `f` and its Fourier transform decay as
`|x| ^ (-b)` for some `1 < b`. (This is the one-dimensional case of Corollary VII.2.6 of Stein and
Weiss, *Introduction to Fourier analysis on Euclidean spaces*.) -/
lemma real.tsum_eq_tsum_fourier_integral_of_rpow_decay {f : â„ â†’ â„‚} (hc : continuous f)
  {b : â„} (hb : 1 < b) (hf : is_O (cocompact â„) f (Î» x : â„, |x| ^ (-b)))
  (hFf : is_O (cocompact â„) (ğ“• f) (Î» x : â„, |x| ^ (-b))) :
  âˆ‘' (n : â„¤), f n = âˆ‘' (n : â„¤), ğ“• f n :=
real.tsum_eq_tsum_fourier_integral_of_rpow_decay_of_summable hc hb hf
  (summable_of_is_O (real.summable_abs_int_rpow hb) (hFf.comp_tendsto int.tendsto_coe_cofinite))

end rpow_decay

section schwartz

/-- **Poisson's summation formula** for Schwartz functions. -/
lemma schwartz_map.tsum_eq_tsum_fourier_integral
  (f g : schwartz_map â„ â„‚) (hfg : ğ“• f = g) :
  âˆ‘' (n : â„¤), f n = âˆ‘' (n : â„¤), g n :=
begin
  -- We know that Schwartz functions are `O(â€–x ^ (-b)â€–)` for *every* `b`; for this argument we take
  -- `b = 2` and work with that.
  simp_rw â†hfg,
  exact real.tsum_eq_tsum_fourier_integral_of_rpow_decay f.continuous one_lt_two
    (f.is_O_cocompact_rpow (-2)) (by simpa only [hfg] using g.is_O_cocompact_rpow (-2))
end

end schwartz
