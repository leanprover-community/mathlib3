/-
Copyright (c) 2022 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import measure_theory.function.continuous_map_dense
import measure_theory.integral.integral_eq_improper
import measure_theory.group.integration
import topology.continuous_function.zero_at_infty
import analysis.fourier.fourier_transform

/-!
# The Riemann-Lebesgue Lemma

In this file we prove a weak form of the Riemann-Lebesgue lemma, stating that for any
compactly-supported continuous function `f` on `â„` (valued in some complete normed space `E`), the
integral

`âˆ« (x : â„), exp (â†‘(t * x) * I) â€¢ f x`

tends to zero as `t â†’ âˆ`. (The actual lemma is that this holds for all `LÂ¹` functions `f`, which
follows from the result proved here together with the fact that continuous, compactly-supported
functions are dense in `LÂ¹(â„)`, which will be proved in a future iteration.)

## Main results

- `tendsto_integral_mul_exp_at_top_of_continuous_compact_support`: the Riemann-Lebesgue lemma for
  continuous compactly-supported functions on `â„`.
-/

open measure_theory filter complex set
open_locale filter topology real ennreal

variables {E : Type*} [normed_add_comm_group E] [normed_space â„‚ E] {f : â„ â†’ E}

section continuous_compact_support


/-- The integrand in the Riemann-Lebesgue lemma is integrable. -/
lemma fourier_integrand_integrable (hf : integrable f) (t : â„) :
  integrable (Î» x:â„, exp (â†‘(t * x) * I) â€¢ f x) :=
begin
  rw â†integrable_norm_iff,
  simp_rw [norm_smul, norm_exp_of_real_mul_I, one_mul],
  exacts [hf.norm, (continuous.ae_strongly_measurable (by continuity)).smul hf.1],
end

variable [complete_space E]

/-- Shifting `f` by `Ï€ / t` negates the integral in the Riemann-Lebesgue lemma. -/
lemma fourier_integral_half_period_translate {t : â„} (ht : t â‰  0) :
  âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f (x + Ï€ / t) = -âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f x :=
begin
  have : (Î» x:â„, exp (â†‘(t * x) * I) â€¢ f (x + Ï€ / t)) =
    (Î» x:â„, (Î» y:â„, -exp (â†‘(t * y) * I) â€¢ f y) (x + Ï€ / t)),
  { ext1 x, dsimp only,
    rw [of_real_mul, of_real_mul, of_real_add, mul_add, add_mul, exp_add, â†neg_mul],
    replace ht := complex.of_real_ne_zero.mpr ht,
    have : â†‘t * â†‘(Ï€ / t) * I = Ï€ * I, by { field_simp, ring },
    rw [this, exp_pi_mul_I], ring_nf, },
  rw [this, integral_add_right_eq_self],
  simp_rw [neg_smul, integral_neg],
end

/-- Rewrite the Riemann-Lebesgue integral in a form that allows us to use uniform continuity. -/
lemma fourier_integral_eq_half_sub_half_period_translate
  {t : â„} (ht : t â‰  0) (hf : integrable f) :
  âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f x =
  (1 / (2 : â„‚)) â€¢ âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ (f x - f (x + Ï€ / t)) :=
begin
  simp_rw [smul_sub],
  rw [integral_sub, fourier_integral_half_period_translate ht, sub_eq_add_neg, neg_neg,
    â†two_smul â„‚ _, â†@smul_assoc _ _ _ _ _ _ (is_scalar_tower.left â„‚), smul_eq_mul],
  norm_num,
  exacts [fourier_integrand_integrable hf t,
    fourier_integrand_integrable (hf.comp_add_right (Ï€ / t)) t],
end

/-- Riemann-Lebesgue Lemma for continuous and compactly-supported functions: the integral
`âˆ« x, exp (t * x * I) â€¢ f x` tends to 0 as `t` gets large.  -/
lemma tendsto_integral_mul_exp_at_top_of_continuous_compact_support
  (hf1 : continuous f) (hf2 : has_compact_support f) :
  tendsto (Î» t:â„, âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f x) at_top (ğ“ 0) :=
begin
  simp_rw [normed_add_comm_group.tendsto_nhds_zero, eventually_at_top, ge_iff_le],
  intros Îµ hÎµ,
  -- Extract an explicit candidate bound on `t` from uniform continuity.
  obtain âŸ¨R, hR1, hR2âŸ© := hf2.exists_pos_le_norm,
  obtain âŸ¨Î´, hÎ´1, hÎ´2âŸ© := metric.uniform_continuous_iff.mp
    (hf2.uniform_continuous_of_continuous hf1) (Îµ / (1 + 2 * R)) (div_pos hÎµ (by positivity)),
  refine âŸ¨max Ï€ (1 + Ï€ / Î´), Î» t ht, _âŸ©,
  have tpos : 0 < t := lt_of_lt_of_le real.pi_pos ((le_max_left _ _).trans ht),
  -- Rewrite integral in terms of `f x - f (x + Ï€ / t)`.
  rw fourier_integral_eq_half_sub_half_period_translate (lt_of_lt_of_le
    (lt_max_of_lt_left real.pi_pos) ht).ne' (hf1.integrable_of_has_compact_support hf2),
  rw [norm_smul, norm_eq_abs, â†complex.of_real_one, â†of_real_bit0, â†of_real_div,
    complex.abs_of_nonneg one_half_pos.le],
  have : Îµ = (1 / 2) * (2 * Îµ), by { field_simp, ring, },
  rw [this, mul_lt_mul_left (one_half_pos : (0:â„) < 1/2)],
  have : â€–âˆ« (x : â„), exp (â†‘(t * x) * I) â€¢ (f x - f (x + Ï€ / t))â€– â‰¤ âˆ« (x : â„),
    â€–exp (â†‘(t * x) * I) â€¢ (f x - f (x + Ï€ / t))â€–, from norm_integral_le_integral_norm _,
  refine lt_of_le_of_lt this _,
  simp_rw [norm_smul, norm_exp_of_real_mul_I, one_mul],
  -- Show integral can be taken over `[-(R + 1), R] âŠ‚ â„`.
  let A := Icc (-(R + 1)) R,
  have int_Icc : âˆ« (x : â„), â€–f x - f (x + Ï€ / t)â€– = âˆ« x in A, â€–f x - f (x + Ï€ / t)â€–,
  { refine (set_integral_eq_integral_of_forall_compl_eq_zero (Î» x hx, _)).symm,
    rw [mem_Icc, not_and_distrib, not_le, not_le, lt_neg] at hx,
    suffices : (f x = 0 âˆ§ f (x + Ï€ / t) = 0), by { rw [this.1, this.2, sub_zero, norm_zero], },
    have tp : 0 < t := real.pi_pos.trans_le ((le_max_left _ _).trans ht),
    refine âŸ¨hR2 x $ le_abs.mpr _, hR2 _ $ le_abs.mpr _âŸ©,
    { cases hx,
      { exact or.inr ((le_add_of_nonneg_right zero_le_one).trans hx.le) },
      { exact or.inl hx.le } },
    { cases hx,
      { refine or.inr _,
        rw [neg_add, â†sub_eq_add_neg, le_sub_iff_add_le],
        refine le_trans (add_le_add_left _ R) hx.le,
        exact (div_le_one tp).mpr ((le_max_left _ _).trans ht) },
      { exact or.inl (hx.trans $ lt_add_of_pos_right _ $ div_pos real.pi_pos tp).le } } },
  rw int_Icc,
  -- Bound integral using fact that â€–f x - f (x + Ï€ / t)â€– is small.
  have bdA : âˆ€ x : â„, (x âˆˆ A) â†’ â€– â€–f x - f (x + Ï€ / t) â€– â€– â‰¤ Îµ / (1 + 2 * R),
  { simp_rw norm_norm,
    refine (Î» x _, le_of_lt _),
    simp_rw dist_eq_norm at hÎ´2,
    apply hÎ´2,
    rw [sub_add_cancel', real.norm_eq_abs, abs_neg, abs_of_pos (div_pos real.pi_pos tpos),
      div_lt_iff tpos, mul_comm, â†div_lt_iff hÎ´1],
    linarith [(le_max_right Ï€ (1 + Ï€ / Î´)).trans ht] },
  have bdA2 := norm_set_integral_le_of_norm_le_const (measure_Icc_lt_top : volume A < âˆ) bdA _,
  swap, { apply continuous.ae_strongly_measurable,
    exact (continuous_norm.comp $ continuous.sub hf1 $ continuous.comp hf1 $
    continuous_id'.add continuous_const) },
  have : â€– _ â€– = âˆ« (x : â„) in A, â€–f x - f (x + Ï€ / t)â€– :=
    real.norm_of_nonneg (set_integral_nonneg measurable_set_Icc (Î» x hx, norm_nonneg _)),
  rw this at bdA2,
  refine lt_of_le_of_lt bdA2 _,
  rw [real.volume_Icc, (by ring : R - (-(R + 1)) = 1 + 2 * R)],
  have hh : 0 < 1 + 2 * R, by positivity,
  rw [ennreal.to_real_of_real hh.le, div_mul_cancel _ hh.ne', two_mul],
  exact lt_add_of_pos_left _ hÎµ,
end

lemma tendsto_integral_mul_exp_at_bot_of_continuous_compact_support
  (hf1 : continuous f) (hf2 : has_compact_support f) :
  tendsto (Î» t:â„, âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f x) at_bot (ğ“ 0) :=
begin
  have hg2 : has_compact_support (f âˆ˜ has_neg.neg),
    by simpa only [neg_one_smul] using hf2.comp_smul (neg_ne_zero.mpr $ one_ne_zero' â„),
  convert (tendsto_integral_mul_exp_at_top_of_continuous_compact_support (hf1.comp continuous_neg)
    hg2).comp tendsto_neg_at_bot_at_top,
  ext1 t,
  simp_rw [function.comp_app, neg_mul, â†mul_neg],
  rw â†integral_neg_eq_self,
end

lemma zero_at_infty_integral_mul_exp_of_continuous_compact_support
  (hf1 : continuous f) (hf2 : has_compact_support f) :
  tendsto (Î» t:â„, âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f x) (cocompact â„) (ğ“ 0) :=
begin
  rw [real.cocompact_eq, tendsto_sup],
  exact âŸ¨tendsto_integral_mul_exp_at_bot_of_continuous_compact_support hf1 hf2,
    tendsto_integral_mul_exp_at_top_of_continuous_compact_support hf1 hf2âŸ©
end

open_locale fourier_transform

/-- Riemann-Lebesgue lemma for continuous compactly-supported functions: the Fourier transform
tends to 0 at infinity. -/
lemma real.fourier_integral_zero_at_infty_of_continuous_compact_support
  (hc : continuous f) (hs : has_compact_support f) :
  tendsto (ğ“• f) (cocompact â„) (ğ“ 0) :=
begin
  refine ((zero_at_infty_integral_mul_exp_of_continuous_compact_support hc hs).comp
    (tendsto_cocompact_mul_leftâ‚€
    (mul_ne_zero (neg_ne_zero.mpr two_ne_zero) real.pi_pos.ne'))).congr (Î» w, _),
  rw [real.fourier_integral_eq_integral_exp_smul, function.comp_app],
  congr' 1 with x:1,
  ring_nf,
end

end continuous_compact_support

section L1

variable [complete_space E]

/-- Any continuous integrable function is `LÂ¹` approximated by compactly-supported continuous
functions. This lemma is private since it is a stepping stone towards the more general result
`cts_cpct_support_dense` proved below. -/
private lemma compact_support_dense_aux1
  {f : â„ â†’ E} (hfc : continuous f) (hfi : integrable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (g : â„ â†’ E), (has_compact_support g) âˆ§ (continuous g) âˆ§ (âˆ« (x : â„), â€–f x - g xâ€– < Îµ) :=
begin
  have := interval_integral_tendsto_integral hfi.norm tendsto_neg_at_top_at_bot tendsto_id,
  rw metric.tendsto_nhds at this,
  obtain âŸ¨R, hR1, hR2âŸ© := ((this Îµ hÎµ).and (eventually_gt_at_top 0)).exists,
  rw dist_eq_norm at hR1,
  dsimp at hR1,
  rw [interval_integral.integral_of_le (neg_lt_self hR2).le,
    â†integral_indicator (measurable_set_Ioc : measurable_set (Ioc (-R) R)),
    â†integral_sub _ hfi.norm] at hR1,
  swap,
  { rw integrable_indicator_iff (measurable_set_Ioc : measurable_set (Ioc (-R) R)),
    exact hfi.norm.integrable_on },
  rw [abs_of_nonpos, â†integral_neg] at hR1,
  simp_rw neg_sub at hR1,
  swap, { apply integral_nonpos, intro x,
    dsimp, rw [â†neg_nonneg, neg_sub],
    rw indicator,
    split_ifs, { simp, }, { simp }, },
  -- Now hR1 tells us that a compactly-supported, but possibly not continuous, function is L1-close
  -- to f. So we now extract a continuous approximation to that.
  have a1 : is_closed (Ici (R + 1) âˆª Iic (-(R+1))) := is_closed_Ici.union is_closed_Iic,
  have a2 : is_closed (Icc (-R) R) := is_closed_Icc,
  have a3 : disjoint  (Ici (R + 1) âˆª Iic (-(R+1))) (Icc (-R) R),
  { rw disjoint_iff_forall_ne,
    intros x hx y hy, rw mem_Icc at hy,
    cases hx, { rw mem_Ici at hx,linarith }, { rw mem_Iic at hx,linarith } },
  haveI : normal_space â„ := normal_space_of_t3_second_countable â„,
  obtain âŸ¨t, ht1, ht2, ht3âŸ© := exists_continuous_zero_one_of_closed a1 a2 a3,
  refine âŸ¨(Î» x, t x â€¢ f x), _, (continuous_map.continuous t).smul hfc, _âŸ©,
  { apply has_compact_support.smul_right,
    rw â†exists_compact_iff_has_compact_support,
    refine âŸ¨Icc (-(R+1)) (R + 1), is_compact_Icc, (Î» x hx, _)âŸ©,
    rw [mem_Icc, not_and_distrib, not_le, not_le] at hx,
    apply ht1,
    rw [mem_union, mem_Ici, mem_Iic],
    cases hx, { right, linarith, }, { left, linarith },  },
  refine lt_of_le_of_lt _ hR1,
  refine integral_mono_of_nonneg (ae_of_all _ (Î» a, norm_nonneg _)) _ (ae_of_all _ _),
  { have : (Î» (a : â„), â€–f aâ€– - (Ioc (-R) R).indicator (Î» (x : â„), â€–f xâ€–) a)
     = (Ioc (-R) R)á¶œ.indicator (Î» (x : â„), â€–f xâ€–), by { rw indicator_compl, ext1 x, simp,  },
    rw [this, integrable_indicator_iff],
    exact ((integrable_norm_iff hfi.1).mpr hfi).integrable_on,
    exact measurable_set_Ioc.compl,},
  intro a,
  rw indicator, split_ifs,
  { suffices : t a = 1, { rw this, simp },
    apply ht2, exact mem_of_mem_of_subset h Ioc_subset_Icc_self, },
  { specialize ht3 a,
    have : f a - t a â€¢ f a = (1 - t a) â€¢ f a, by rw [sub_smul, one_smul],
    rw [this, norm_smul, sub_zero],
    apply mul_le_of_le_one_left (norm_nonneg _),
    rw real.norm_eq_abs, rw _root_.abs_of_nonneg,
    linarith [ht3.1], linarith [ht3.2] },
end

/-- Any integrable function is `LÂ¹` approximated by continuous integrable functions.
This lemma is private, since it is a stepping stone towards the more general result
`cts_cpct_support_dense` proved below. -/
private lemma compact_support_dense_aux2 {f : â„ â†’ E} (hf : integrable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (g : â„ â†’ E), (integrable g) âˆ§ (continuous g) âˆ§ (âˆ« (x : â„), â€–f x - g xâ€– < Îµ) :=
begin
  haveI : normal_space â„ := normal_space_of_t3_second_countable â„,
  have := Lp.bounded_continuous_function_dense E ennreal.one_ne_top (volume : measure â„),
  let f_Lp := mem_â„’p.to_Lp f (mem_â„’p_one_iff_integrable.mpr hf),
  have f_closure : f_Lp âˆˆ (Lp.bounded_continuous_function E 1 volume).topological_closure,
  { rw this, tauto, },
  obtain âŸ¨b, hb1, hb2âŸ© := metric.mem_closure_iff.mp f_closure Îµ hÎµ,
  obtain âŸ¨b0, hb0âŸ© := Lp.mem_bounded_continuous_function_iff.mp hb1,
  -- We should be done here, but b0 is not quite b, and f_Lp is not quite f.
  have b_ae_eq : b0 =áµ[volume] b,
  { have t2 := continuous_map.coe_fn_to_ae_eq_fun volume b0.1,
    rw hb0 at t2,
    refine ae_eq_trans (ae_eq_refl _) t2.symm },
  have f_ae_eq : f_Lp =áµ[volume] f := mem_â„’p.coe_fn_to_Lp _,
  have fb_ae_eq : ((f_Lp - b) : â†¥(Lp E 1 volume)) =áµ[volume] (f - b0),
  { have := Lp.coe_fn_sub f_Lp b,
    refine eventually.mp ((this.and f_ae_eq).and b_ae_eq) (eventually_of_forall _),
    intros x hx,
    rw [hx.1.1, pi.sub_apply, pi.sub_apply, hx.1.2, â†hx.2] },
  refine âŸ¨b0, (integrable_congr b_ae_eq).mpr (L1.integrable_coe_fn b), b0.continuous, _âŸ©,
  { rw dist_eq_norm at hb2,
    rw L1.norm_eq_integral_norm at hb2,
    convert hb2 using 1,
    apply integral_congr_ae,
    refine eventually.mp fb_ae_eq (eventually_of_forall _),
    dsimp,
    intros x hx,
    rw hx }
end

/-- An integrable function on `â„` can be approximated arbitrarily closely in `LÂ¹` norm by continuous
and compactly-supported functions. -/
lemma cts_cpct_support_dense {f : â„ â†’ E} (hf : integrable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (g : â„ â†’ E), (continuous g) âˆ§ (has_compact_support g) âˆ§ (âˆ« (x : â„), â€–f x - g xâ€– < Îµ) :=
begin
  obtain âŸ¨j, hj1, hj2, hj3âŸ© := compact_support_dense_aux2 hf (by linarith : 0 < Îµ / 2),
  obtain âŸ¨g, hg1, hg2, hg3âŸ© := compact_support_dense_aux1 hj2 hj1 (by linarith : 0 < Îµ / 2),
  refine âŸ¨g, hg2, hg1, _âŸ©,
  rw (by ring : Îµ = Îµ/2 + Îµ/2),
  refine lt_of_le_of_lt _ (add_lt_add hj3 hg3),
  have hg4 : integrable g := hg2.integrable_of_has_compact_support hg1,
  rw â†integral_add,
  refine integral_mono_of_nonneg (eventually_of_forall (Î» x, norm_nonneg _)) _ _,
  { exact integrable.add (hf.sub hj1).norm (hj1.sub hg4).norm, },
  { refine eventually_of_forall (Î» x, _),
    convert le_trans (le_refl _) (norm_add_le _ _),
    rw [add_sub, sub_add_cancel], },
  exacts [(hf.sub hj1).norm, (hj1.sub hg4).norm],
end

/- If `f` and `g` are close in `LÂ¹` norm, then their Fourier transforms are close in sup norm. -/
lemma fourier_L1_cts {f g : â„ â†’ E} (hf : integrable f) (hg : integrable g) {Îµ : â„}
  (hfg : âˆ« (x : â„), â€–f x - g xâ€– < Îµ) (t : â„) :
  â€–(âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f x) - (âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ g x)â€– < Îµ :=
begin
  rw â†integral_sub (fourier_integrand_integrable hf _) (fourier_integrand_integrable hg _),
  refine lt_of_le_of_lt (norm_integral_le_integral_norm _) _,
  refine lt_of_le_of_lt (integral_mono _ (hf.sub hg).norm _) hfg,
  { exact ((fourier_integrand_integrable hf _).sub (fourier_integrand_integrable hg _)).norm },
  intro x, convert le_refl _, dsimp,
  rw [â†smul_sub, norm_smul, norm_eq_abs, abs_exp_of_real_mul_I, one_mul],
end

/-- Riemann-Lebesgue lemma for integrable functions. -/
theorem tendsto_integral_mul_exp_at_top {f : â„ â†’ E} (hfi : integrable f) :
  tendsto (Î» t:â„, âˆ« x:â„, exp (â†‘(t * x) * I) â€¢ f x) at_top (ğ“ 0) :=
metric.tendsto_nhds.mpr $ Î» Îµ hÎµ, begin
  obtain âŸ¨g, hg1, hg2, hg3âŸ© := cts_cpct_support_dense hfi (by linarith : 0 < Îµ/2),
  refine ((metric.tendsto_nhds.mp (tendsto_integral_mul_exp_at_top_of_continuous_compact_support
    hg1 hg2)) _ (by linarith : 0 < Îµ/2)).mp (eventually_of_forall (Î» t hI, _)),
  rw dist_eq_norm at hI âŠ¢,
  have := add_lt_add (fourier_L1_cts hfi (hg1.integrable_of_has_compact_support hg2) hg3 t) hI,
  rw (by ring : Îµ/2 + Îµ/2 = Îµ) at this,
  refine ((le_of_eq _).trans (norm_add_le _ _)).trans_lt this,
  simp only [sub_zero, sub_add_cancel],
end

end L1
