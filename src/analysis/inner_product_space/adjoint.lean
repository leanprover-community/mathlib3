/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/

import analysis.inner_product_space.dual

/-!
# Adjoint of operators on Hilbert spaces

Given an operator `A : E â†’L[ğ•œ] F`, where `E` and `F` are Hilbert spaces, its adjoint
`adjoint A : F â†’L[ğ•œ] E` is the unique operator such that `âŸªx, A yâŸ« = âŸªadjoint A x, yâŸ«` for all
`x` and `y`.

## Implementation notes

* The adjoint is defined as a conjugate-linear isometric equivalence between `E â†’L[ğ•œ] F` and
  `F â†’L[ğ•œ] E`. The bare function `adjoint'` is only an intermediate definition and is not meant
  to be used outside this file.

## Tags

adjoint

-/

noncomputable theory
open inner_product_space continuous_linear_map
open_locale complex_conjugate

variables {ğ•œ E F G : Type*} [is_R_or_C ğ•œ]
variables [inner_product_space ğ•œ E] [inner_product_space ğ•œ F] [inner_product_space ğ•œ G]
variables [complete_space E] [complete_space G]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

namespace continuous_linear_map

/-- The adjoint, as a bare function. This is only meant as an auxiliary definition for
the main definition `adjoint`, where this is bundled as a conjugate-linear isometric
equivalence. -/
@[simps] def adjoint' (A : E â†’L[ğ•œ] F) : F â†’L[ğ•œ] E :=
((to_dual ğ•œ E).symm : (normed_space.dual ğ•œ E) â†’Lâ‹†[ğ•œ] E).comp (to_sesq_formâ‚— A)

lemma adjoint'_inner_left {A : E â†’L[ğ•œ] F} {x : E} {y : F} : âŸªadjoint' A y, xâŸ« = âŸªy, A xâŸ« :=
by { simp only [adjoint'_apply, to_dual_symm_apply], refl }

lemma adjoint'_inner_right {A : E â†’L[ğ•œ] F} {x : E} {y : F} : âŸªx, adjoint' A yâŸ« = âŸªA x, yâŸ« :=
by rw [â†inner_conj_sym, adjoint'_inner_left, inner_conj_sym]

variables [complete_space F]

lemma adjoint'_adjoint'_apply (A : E â†’L[ğ•œ] F) : adjoint' (adjoint' A) = A :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  rw [adjoint'_inner_right, adjoint'_inner_left],
end

lemma adjoint'_norm {A : E â†’L[ğ•œ] F} : âˆ¥adjoint' Aâˆ¥ = âˆ¥Aâˆ¥ :=
begin
  refine le_antisymm _ _,
  { refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint'_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le },
  { nth_rewrite_lhs 0 [â†adjoint'_adjoint'_apply A],
    refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint'_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le }
end

/-- The adjoint of a bounded operator from Hilbert space E to Hilbert space F. -/
def adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] (F â†’L[ğ•œ] E) :=
linear_isometry_equiv.of_surjective
{ to_fun := adjoint',
  map_add' := Î» A B, by simp only [adjoint', linear_map.map_add, comp_add],
  map_smul' := Î» r A, by simp only [adjoint', linear_map.map_smulâ‚›â‚—, ring_hom.id_apply,
                                    comp_smulâ‚›â‚—],
  norm_map' := Î» A, adjoint'_norm }
(Î» A, âŸ¨adjoint' A, adjoint'_adjoint'_apply AâŸ©)

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_left {A : E â†’L[ğ•œ] F} {x : E} {y : F} : âŸªadjoint A y, xâŸ« = âŸªy, A xâŸ« :=
adjoint'_inner_left

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_right {A : E â†’L[ğ•œ] F} {x : E} {y : F} : âŸªx, adjoint A yâŸ« = âŸªA x, yâŸ« :=
adjoint'_inner_right

/-- The adjoint is involutive -/
@[simp] lemma adjoint_adjoint_apply {A : E â†’L[ğ•œ] F} : adjoint (adjoint A) = A :=
adjoint'_adjoint'_apply A

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp] lemma adjoint_comp {A : F â†’L[ğ•œ] G} {B : E â†’L[ğ•œ] F} :
  adjoint (A âˆ˜L B) = (adjoint B) âˆ˜L (adjoint A) :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  simp only [adjoint_inner_right, continuous_linear_map.coe_comp', function.comp_app],
end

/-- `E â†’L[ğ•œ] E` is a star algebra with the adjoint as the star operation. -/
instance : has_star (E â†’L[ğ•œ] E) := âŸ¨adjointâŸ©
instance : has_involutive_star (E â†’L[ğ•œ] E) := âŸ¨Î» _, adjoint_adjoint_applyâŸ©
instance : star_monoid (E â†’L[ğ•œ] E) := âŸ¨Î» _ _, adjoint_compâŸ©
instance : star_ring (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_add adjointâŸ©
instance : star_module ğ•œ (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_smulâ‚›â‚— adjointâŸ©

end continuous_linear_map
