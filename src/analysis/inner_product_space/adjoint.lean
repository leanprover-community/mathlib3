/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/

import analysis.inner_product_space.dual

/-!
# Adjoint of operators on Hilbert spaces

Given an operator `A : E â†’L[ğ•œ] F`, where `E` and `F` are Hilbert spaces, its adjoint
`adjoint A : F â†’L[ğ•œ] E` is the unique operator such that `âŸªx, A yâŸ« = âŸªadjoint A x, yâŸ«` for all
`x` and `y`.

We then use this to put a Câ‹†-algebra structure on `E â†’L[ğ•œ] E` with the adjoint as the star
operation.

## Implementation notes

* The adjoint is defined as a conjugate-linear isometric equivalence between `E â†’L[ğ•œ] F` and
  `F â†’L[ğ•œ] E`. The bare function `adjoint'` is only an intermediate definition and is not meant
  to be used outside this file.

## Tags

adjoint

-/

noncomputable theory
open inner_product_space continuous_linear_map is_R_or_C
open_locale complex_conjugate

variables {ğ•œ E F G : Type*} [is_R_or_C ğ•œ]
variables [inner_product_space ğ•œ E] [inner_product_space ğ•œ F] [inner_product_space ğ•œ G]
variables [complete_space E] [complete_space G]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

namespace continuous_linear_map

/-- The adjoint, as a continuous conjugate-linear map.  This is only meant as an auxiliary
definition for the main definition `adjoint`, where this is bundled as a conjugate-linear isometric
equivalence. -/
def adjoint' : (E â†’L[ğ•œ] F) â†’Lâ‹†[ğ•œ] (F â†’L[ğ•œ] E) :=
(continuous_linear_map.compSL _ _ _ _ _ ((to_dual ğ•œ E).symm : normed_space.dual ğ•œ E â†’Lâ‹†[ğ•œ] E)).comp
  (to_sesq_form : (E â†’L[ğ•œ] F) â†’L[ğ•œ] F â†’Lâ‹†[ğ•œ] normed_space.dual ğ•œ E)

@[simp] lemma adjoint'_apply (A : E â†’L[ğ•œ] F) (x : F) :
  adjoint' A x = ((to_dual ğ•œ E).symm : (normed_space.dual ğ•œ E) â†’ E) ((to_sesq_form A) x) := rfl

lemma adjoint'_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªadjoint' A y, xâŸ« = âŸªy, A xâŸ« :=
by { simp only [adjoint'_apply, to_dual_symm_apply, to_sesq_form_apply_coe, coe_comp',
                innerSL_apply_coe]}

lemma adjoint'_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªx, adjoint' A yâŸ« = âŸªA x, yâŸ« :=
by rw [â†inner_conj_sym, adjoint'_inner_left, inner_conj_sym]

variables [complete_space F]

lemma adjoint'_adjoint' (A : E â†’L[ğ•œ] F) : adjoint' (adjoint' A) = A :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  rw [adjoint'_inner_right, adjoint'_inner_left],
end

lemma adjoint'_norm (A : E â†’L[ğ•œ] F) : âˆ¥adjoint' Aâˆ¥ = âˆ¥Aâˆ¥ :=
begin
  refine le_antisymm _ _,
  { refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint'_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le },
  { nth_rewrite_lhs 0 [â†adjoint'_adjoint' A],
    refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint'_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le }
end

/-- The adjoint of a bounded operator from Hilbert space E to Hilbert space F. -/
def adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] (F â†’L[ğ•œ] E) :=
linear_isometry_equiv.of_surjective
{ norm_map' := adjoint'_norm,
  ..adjoint' }
(Î» A, âŸ¨adjoint' A, adjoint'_adjoint' AâŸ©)

local postfix `â€ `:1000 := adjoint

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªAâ€  y, xâŸ« = âŸªy, A xâŸ« :=
adjoint'_inner_left A x y

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªx, Aâ€  yâŸ« = âŸªA x, yâŸ« :=
adjoint'_inner_right A x y

/-- The adjoint is involutive -/
@[simp] lemma adjoint_adjoint (A : E â†’L[ğ•œ] F) : Aâ€ â€  = A :=
adjoint'_adjoint' A

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp] lemma adjoint_comp (A : F â†’L[ğ•œ] G) (B : E â†’L[ğ•œ] F) : (A âˆ˜L B)â€  = Bâ€  âˆ˜L Aâ€  :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  simp only [adjoint_inner_right, continuous_linear_map.coe_comp', function.comp_app],
end

/-- `E â†’L[ğ•œ] E` is a Câ‹†-algebra with the adjoint as the star operation. -/
instance : has_star (E â†’L[ğ•œ] E) := âŸ¨adjointâŸ©
instance : has_involutive_star (E â†’L[ğ•œ] E) := âŸ¨adjoint_adjointâŸ©
instance : star_monoid (E â†’L[ğ•œ] E) := âŸ¨adjoint_compâŸ©
instance : star_ring (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_add adjointâŸ©
instance : star_module ğ•œ (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_smulâ‚›â‚— adjointâŸ©

lemma star_eq_adjoint (A : E â†’L[ğ•œ] E) : star A = Aâ€  := rfl

instance : cstar_ring (E â†’L[ğ•œ] E) :=
âŸ¨begin
  intros A,
  rw [star_eq_adjoint],
  refine le_antisymm _ _,
  { calc âˆ¥Aâ€  * Aâˆ¥ â‰¤ âˆ¥Aâ€ âˆ¥ * âˆ¥Aâˆ¥      : op_norm_comp_le _ _
              ... = âˆ¥Aâˆ¥ * âˆ¥Aâˆ¥       : by rw [linear_isometry_equiv.norm_map] },
  { rw [â†sq, â†real.sqrt_le_sqrt_iff (norm_nonneg _), real.sqrt_sq (norm_nonneg _)],
    refine op_norm_le_bound _ (real.sqrt_nonneg _) (Î» x, _),
    have := calc
      re âŸªAâ€  (A x), xâŸ« â‰¤ abs âŸªAâ€  (A x), xâŸ«    : re_le_abs _
                  ...  â‰¤ âˆ¥Aâ€  (A x)âˆ¥ * âˆ¥xâˆ¥     : abs_inner_le_norm _ _
                  ...  â‰¤ âˆ¥Aâ€  * Aâˆ¥ * âˆ¥xâˆ¥ * âˆ¥xâˆ¥
                    : begin
                        refine mul_le_mul_of_nonneg_right _ (norm_nonneg _),
                        rw [â†comp_apply],
                        exact le_op_norm _ _,
                      end,
    calc âˆ¥A xâˆ¥ = real.sqrt (re âŸªA x, A xâŸ«)          : norm_eq_sqrt_inner _
          ...  = real.sqrt (re âŸªAâ€  (A x), xâŸ«)       : by rw [â†adjoint_inner_left]
          ...  â‰¤ real.sqrt (âˆ¥Aâ€  * Aâˆ¥ * âˆ¥xâˆ¥ * âˆ¥xâˆ¥)   : real.sqrt_le_sqrt this
          ...  = real.sqrt (âˆ¥Aâ€  * Aâˆ¥) * âˆ¥xâˆ¥
            : by rw [mul_assoc, real.sqrt_mul (norm_nonneg _), real.sqrt_mul_self (norm_nonneg _)] }
endâŸ©

end continuous_linear_map
