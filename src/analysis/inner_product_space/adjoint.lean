/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/

import analysis.inner_product_space.dual
import analysis.inner_product_space.pi_L2

/-!
# Adjoint of operators on Hilbert spaces

Given an operator `A : E â†’L[ğ•œ] F`, where `E` and `F` are Hilbert spaces, its adjoint
`adjoint A : F â†’L[ğ•œ] E` is the unique operator such that `âŸªx, A yâŸ« = âŸªadjoint A x, yâŸ«` for all
`x` and `y`.

We then use this to put a Câ‹†-algebra structure on `E â†’L[ğ•œ] E` with the adjoint as the star
operation.

This construction is used to define an adjoint for linear maps (i.e. not continuous) between
finite dimensional spaces.

## Main definitions

* `continuous_linear_map.adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] (F â†’L[ğ•œ] E)`: the adjoint of a continuous
  linear map, bundled as a conjugate-linear isometric equivalence.
* `linear_map.adjoint : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—â‹†[ğ•œ] (F â†’â‚—[ğ•œ] E)`: the adjoint of a linear map between
  finite-dimensional spaces, this time only as a conjugate-linear equivalence, since there is no
  norm defined on these maps.

## Implementation notes

* The continuous conjugate-linear version `adjoint_aux` is only an intermediate
  definition and is not meant to be used outside this file.

## Tags

adjoint

-/

noncomputable theory
open is_R_or_C
open_locale complex_conjugate

variables {ğ•œ E F G : Type*} [is_R_or_C ğ•œ]
variables [inner_product_space ğ•œ E] [inner_product_space ğ•œ F] [inner_product_space ğ•œ G]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

namespace inner_product_space

/-! ### Self-adjoint operators -/

/-- A (not necessarily bounded) operator on an inner product space is self-adjoint, if for all
`x`, `y`, we have `âŸªT x, yâŸ« = âŸªx, T yâŸ«`. -/
def is_self_adjoint (T : E â†’â‚—[ğ•œ] E) : Prop := âˆ€ x y, âŸªT x, yâŸ« = âŸªx, T yâŸ«

section real

variables {E' : Type*} [inner_product_space â„ E']

-- Todo: Generalize this to `is_R_or_C`.
/-- An operator `T` on a `â„`-inner product space is self-adjoint if and only if it is
`bilin_form.is_self_adjoint` with respect to the bilinear form given by the inner product. -/
lemma is_self_adjoint_iff_bilin_form (T : E' â†’â‚—[â„] E') :
  is_self_adjoint T â†” bilin_form_of_real_inner.is_self_adjoint T :=
by simp [is_self_adjoint, bilin_form.is_self_adjoint, bilin_form.is_adjoint_pair]

end real

lemma is_self_adjoint.conj_inner_sym {T : E â†’â‚—[ğ•œ] E} (hT : is_self_adjoint T) (x y : E) :
  conj âŸªT x, yâŸ« = âŸªT y, xâŸ« :=
by rw [hT x y, inner_conj_sym]

@[simp] lemma is_self_adjoint.apply_clm {T : E â†’L[ğ•œ] E} (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E))
  (x y : E) :
  âŸªT x, yâŸ« = âŸªx, T yâŸ« :=
hT x y

lemma is_self_adjoint_zero : is_self_adjoint (0 : E â†’â‚—[ğ•œ] E) :=
Î» x y, (inner_zero_right : âŸªx, 0âŸ« = 0).symm â–¸ (inner_zero_left : âŸª0, yâŸ« = 0)

lemma is_self_adjoint_id : is_self_adjoint (linear_map.id : E â†’â‚—[ğ•œ] E) :=
Î» x y, rfl

lemma is_self_adjoint.add {T S : E â†’â‚—[ğ•œ] E} (hT : is_self_adjoint T)
  (hS : is_self_adjoint S) : is_self_adjoint (T + S) :=
begin
  intros x y,
  rw [linear_map.add_apply, inner_add_left, hT x y, hS x y, â† inner_add_right],
  refl
end

/-- The orthogonal projection is self-adjoint. -/
lemma orthogonal_projection_is_self_adjoint (U : submodule ğ•œ E)
  [complete_space U] :
  is_self_adjoint (U.subtypeL âˆ˜L orthogonal_projection U : E â†’â‚—[ğ•œ] E):=
inner_orthogonal_projection_left_eq_right U

/-- The **Hellinger--Toeplitz theorem**: if a symmetric operator is defined everywhere, then
  it is automatically continuous. -/
lemma is_self_adjoint.continuous [complete_space E] {T : E â†’â‚—[ğ•œ] E} (hT : is_self_adjoint T) :
  continuous T :=
begin
  -- We prove it by using the closed graph theorem
  refine T.continuous_of_seq_closed_graph (Î» u x y hu hTu, _),
  rw [â†sub_eq_zero, â†inner_self_eq_zero],
  have hlhs : âˆ€ k : â„•, âŸªT (u k) - T x, y - T xâŸ« = âŸªu k - x, T (y - T x)âŸ« :=
  by { intro k, rw [â†T.map_sub, hT] },
  refine tendsto_nhds_unique ((hTu.sub_const _).inner tendsto_const_nhds) _,
  simp_rw hlhs,
  rw â†@inner_zero_left ğ•œ E _ _ (T (y - T x)),
  refine filter.tendsto.inner _ tendsto_const_nhds,
  rw â†sub_self x,
  exact hu.sub_const _,
end

/-- The **Hellinger--Toeplitz theorem**: Construct a self-adjoint operator from an everywhere
  defined symmetric operator.-/
def is_self_adjoint.clm [complete_space E] {T : E â†’â‚—[ğ•œ] E}
  (hT : is_self_adjoint T) : E â†’L[ğ•œ] E :=
âŸ¨T, hT.continuousâŸ©

lemma is_self_adjoint.clm_apply [complete_space E] {T : E â†’â‚—[ğ•œ] E}
  (hT : is_self_adjoint T) {x : E} : hT.clm x = T x := rfl

/-- For a self-adjoint operator `T`, the function `Î» x, âŸªT x, xâŸ«` is real-valued. -/
@[simp] lemma is_self_adjoint.coe_re_apply_inner_self_apply
  {T : E â†’L[ğ•œ] E} (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) (x : E) :
  (T.re_apply_inner_self x : ğ•œ) = âŸªT x, xâŸ« :=
begin
  suffices : âˆƒ r : â„, âŸªT x, xâŸ« = r,
  { obtain âŸ¨r, hrâŸ© := this,
    simp [hr, T.re_apply_inner_self_apply] },
  rw â† eq_conj_iff_real,
  exact hT.conj_inner_sym x x
end

/-- If a self-adjoint operator preserves a submodule, its restriction to that submodule is
self-adjoint. -/
lemma is_self_adjoint.restrict_invariant {T : E â†’â‚—[ğ•œ] E} (hT : is_self_adjoint T)
  {V : submodule ğ•œ E} (hV : âˆ€ v âˆˆ V, T v âˆˆ V) :
  is_self_adjoint (T.restrict hV) :=
Î» v w, hT v w

section complex

variables {V : Type*}
  [inner_product_space â„‚ V]

/-- A linear operator on a complex inner product space is self-adjoint precisely when
`âŸªT v, vâŸ«_â„‚` is real for all v.-/
lemma is_self_adjoint_iff_inner_map_self_real (T : V â†’â‚—[â„‚] V):
  is_self_adjoint T â†” âˆ€ (v : V), conj âŸªT v, vâŸ«_â„‚ = âŸªT v, vâŸ«_â„‚ :=
begin
  split,
  { intros hT v,
    apply is_self_adjoint.conj_inner_sym hT },
  { intros h x y,
    nth_rewrite 1 â† inner_conj_sym,
    nth_rewrite 1 inner_map_polarization,
    simp only [star_ring_end_apply, star_div', star_sub, star_add, star_mul],
    simp only [â† star_ring_end_apply],
    rw [h (x + y), h (x - y), h (x + complex.I â€¢ y), h (x - complex.I â€¢ y)],
    simp only [complex.conj_I],
    rw inner_map_polarization',
    norm_num,
    ring },
end

end complex

end inner_product_space

/-! ### Adjoint operator -/

open inner_product_space

namespace continuous_linear_map

variables [complete_space E] [complete_space G]

/-- The adjoint, as a continuous conjugate-linear map.  This is only meant as an auxiliary
definition for the main definition `adjoint`, where this is bundled as a conjugate-linear isometric
equivalence. -/
def adjoint_aux : (E â†’L[ğ•œ] F) â†’Lâ‹†[ğ•œ] (F â†’L[ğ•œ] E) :=
(continuous_linear_map.compSL _ _ _ _ _ ((to_dual ğ•œ E).symm : normed_space.dual ğ•œ E â†’Lâ‹†[ğ•œ] E)).comp
  (to_sesq_form : (E â†’L[ğ•œ] F) â†’L[ğ•œ] F â†’Lâ‹†[ğ•œ] normed_space.dual ğ•œ E)

@[simp] lemma adjoint_aux_apply (A : E â†’L[ğ•œ] F) (x : F) :
  adjoint_aux A x = ((to_dual ğ•œ E).symm : (normed_space.dual ğ•œ E) â†’ E) ((to_sesq_form A) x) := rfl

lemma adjoint_aux_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªadjoint_aux A y, xâŸ« = âŸªy, A xâŸ« :=
by { simp only [adjoint_aux_apply, to_dual_symm_apply, to_sesq_form_apply_coe, coe_comp',
                innerSL_apply_coe]}

lemma adjoint_aux_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªx, adjoint_aux A yâŸ« = âŸªA x, yâŸ« :=
by rw [â†inner_conj_sym, adjoint_aux_inner_left, inner_conj_sym]

variables [complete_space F]

lemma adjoint_aux_adjoint_aux (A : E â†’L[ğ•œ] F) : adjoint_aux (adjoint_aux A) = A :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  rw [adjoint_aux_inner_right, adjoint_aux_inner_left],
end

@[simp] lemma adjoint_aux_norm (A : E â†’L[ğ•œ] F) : âˆ¥adjoint_aux Aâˆ¥ = âˆ¥Aâˆ¥ :=
begin
  refine le_antisymm _ _,
  { refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint_aux_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le },
  { nth_rewrite_lhs 0 [â†adjoint_aux_adjoint_aux A],
    refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint_aux_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le }
end

/-- The adjoint of a bounded operator from Hilbert space E to Hilbert space F. -/
def adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] (F â†’L[ğ•œ] E) :=
linear_isometry_equiv.of_surjective
{ norm_map' := adjoint_aux_norm,
  ..adjoint_aux }
(Î» A, âŸ¨adjoint_aux A, adjoint_aux_adjoint_aux AâŸ©)

localized "postfix `â€ `:1000 := continuous_linear_map.adjoint" in inner_product

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªAâ€  y, xâŸ« = âŸªy, A xâŸ« :=
adjoint_aux_inner_left A x y

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªx, Aâ€  yâŸ« = âŸªA x, yâŸ« :=
adjoint_aux_inner_right A x y

/-- The adjoint is involutive -/
@[simp] lemma adjoint_adjoint (A : E â†’L[ğ•œ] F) : Aâ€ â€  = A :=
adjoint_aux_adjoint_aux A

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp] lemma adjoint_comp (A : F â†’L[ğ•œ] G) (B : E â†’L[ğ•œ] F) : (A âˆ˜L B)â€  = Bâ€  âˆ˜L Aâ€  :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  simp only [adjoint_inner_right, continuous_linear_map.coe_comp', function.comp_app],
end

lemma apply_norm_sq_eq_inner_adjoint_left (A : E â†’L[ğ•œ] E) (x : E) : âˆ¥A xâˆ¥^2 = re âŸª(Aâ€  * A) x, xâŸ« :=
have h : âŸª(Aâ€  * A) x, xâŸ« = âŸªA x, A xâŸ« := by { rw [â†adjoint_inner_left], refl },
by rw [h, â†inner_self_eq_norm_sq _]

lemma apply_norm_eq_sqrt_inner_adjoint_left (A : E â†’L[ğ•œ] E) (x : E) :
  âˆ¥A xâˆ¥ = real.sqrt (re âŸª(Aâ€  * A) x, xâŸ«) :=
by rw [â†apply_norm_sq_eq_inner_adjoint_left, real.sqrt_sq (norm_nonneg _)]

lemma apply_norm_sq_eq_inner_adjoint_right (A : E â†’L[ğ•œ] E) (x : E) : âˆ¥A xâˆ¥^2 = re âŸªx, (Aâ€  * A) xâŸ« :=
have h : âŸªx, (Aâ€  * A) xâŸ« = âŸªA x, A xâŸ« := by { rw [â†adjoint_inner_right], refl },
by rw [h, â†inner_self_eq_norm_sq _]

lemma apply_norm_eq_sqrt_inner_adjoint_right (A : E â†’L[ğ•œ] E) (x : E) :
  âˆ¥A xâˆ¥ = real.sqrt (re âŸªx, (Aâ€  * A) xâŸ«) :=
by rw [â†apply_norm_sq_eq_inner_adjoint_right, real.sqrt_sq (norm_nonneg _)]

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all `x` and `y`. -/
lemma eq_adjoint_iff (A : E â†’L[ğ•œ] F) (B : F â†’L[ğ•œ] E) :
  A = Bâ€  â†” (âˆ€ x y, âŸªA x, yâŸ« = âŸªx, B yâŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  ext x,
  exact ext_inner_right ğ•œ (Î» y, by simp only [adjoint_inner_left, h x y])
end

@[simp] lemma is_self_adjoint_iff_adjoint_eq (A : E â†’L[ğ•œ] E) :
  is_self_adjoint (A : E â†’â‚—[ğ•œ] E) â†” Aâ€  = A :=
by simp_rw [is_self_adjoint, coe_coe, â† eq_adjoint_iff, eq_comm]

lemma _root_.inner_product_space.is_self_adjoint.adjoint_eq {A : E â†’L[ğ•œ] E}
  (hA : is_self_adjoint (A : E â†’â‚—[ğ•œ] E)) : Aâ€  = A :=
by rwa is_self_adjoint_iff_adjoint_eq at hA

lemma _root_.inner_product_space.is_self_adjoint.conj_adjoint {T : E â†’L[ğ•œ] E}
  (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) (S : E â†’L[ğ•œ] F) :
  is_self_adjoint (S âˆ˜L T âˆ˜L Sâ€  : F â†’â‚—[ğ•œ] F) :=
begin
  intros x y,
  rw [coe_coe, comp_apply, comp_apply, â† adjoint_inner_right, â† coe_coe, hT, coe_coe,
      adjoint_inner_left],
  refl
end

lemma _root_.inner_product_space.is_self_adjoint.adjoint_conj {T : E â†’L[ğ•œ] E}
  (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) (S : F â†’L[ğ•œ] E) :
  is_self_adjoint (Sâ€  âˆ˜L T âˆ˜L S : F â†’â‚—[ğ•œ] F) :=
begin
  convert hT.conj_adjoint (Sâ€ ),
  rw adjoint_adjoint
end

lemma _root_.inner_product_space.is_self_adjoint.conj_orthogonal_projection {T : E â†’L[ğ•œ] E}
  (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) (U : submodule ğ•œ E) [complete_space U] :
  is_self_adjoint (U.subtypeL âˆ˜L orthogonal_projection U âˆ˜L T âˆ˜L U.subtypeL âˆ˜L
    orthogonal_projection U : E â†’â‚—[ğ•œ] E) :=
begin
  have := hT.conj_adjoint (U.subtypeL âˆ˜L orthogonal_projection U),
  rwa (orthogonal_projection_is_self_adjoint U).adjoint_eq at this
end

lemma _root_.submodule.adjoint_subtypeL (U : submodule ğ•œ E)
  [complete_space U] :
  (U.subtypeL)â€  = orthogonal_projection U :=
begin
  symmetry,
  rw eq_adjoint_iff,
  intros x u,
  rw [U.coe_inner, inner_orthogonal_projection_left_eq_right,
      orthogonal_projection_mem_subspace_eq_self],
  refl
end

lemma _root_.submodule.adjoint_orthogonal_projection (U : submodule ğ•œ E)
  [complete_space U] :
  (orthogonal_projection U : E â†’L[ğ•œ] U)â€  = U.subtypeL :=
by rw [â† U.adjoint_subtypeL, adjoint_adjoint]

/-- `E â†’L[ğ•œ] E` is a star algebra with the adjoint as the star operation. -/
instance : has_star (E â†’L[ğ•œ] E) := âŸ¨adjointâŸ©
instance : has_involutive_star (E â†’L[ğ•œ] E) := âŸ¨adjoint_adjointâŸ©
instance : star_semigroup (E â†’L[ğ•œ] E) := âŸ¨adjoint_compâŸ©
instance : star_ring (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_add adjointâŸ©
instance : star_module ğ•œ (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_smulâ‚›â‚— adjointâŸ©

lemma star_eq_adjoint (A : E â†’L[ğ•œ] E) : star A = Aâ€  := rfl

instance : cstar_ring (E â†’L[ğ•œ] E) :=
âŸ¨begin
  intros A,
  rw [star_eq_adjoint],
  refine le_antisymm _ _,
  { calc âˆ¥Aâ€  * Aâˆ¥ â‰¤ âˆ¥Aâ€ âˆ¥ * âˆ¥Aâˆ¥      : op_norm_comp_le _ _
              ... = âˆ¥Aâˆ¥ * âˆ¥Aâˆ¥       : by rw [linear_isometry_equiv.norm_map] },
  { rw [â†sq, â†real.sqrt_le_sqrt_iff (norm_nonneg _), real.sqrt_sq (norm_nonneg _)],
    refine op_norm_le_bound _ (real.sqrt_nonneg _) (Î» x, _),
    have := calc
      re âŸª(Aâ€  * A) x, xâŸ« â‰¤ âˆ¥(Aâ€  * A) xâˆ¥ * âˆ¥xâˆ¥     : re_inner_le_norm _ _
                    ...  â‰¤ âˆ¥Aâ€  * Aâˆ¥ * âˆ¥xâˆ¥ * âˆ¥xâˆ¥   : mul_le_mul_of_nonneg_right
                                                    (le_op_norm _ _) (norm_nonneg _),
    calc âˆ¥A xâˆ¥ = real.sqrt (re âŸª(Aâ€  * A) x, xâŸ«)     : by rw [apply_norm_eq_sqrt_inner_adjoint_left]
          ...  â‰¤ real.sqrt (âˆ¥Aâ€  * Aâˆ¥ * âˆ¥xâˆ¥ * âˆ¥xâˆ¥)   : real.sqrt_le_sqrt this
          ...  = real.sqrt (âˆ¥Aâ€  * Aâˆ¥) * âˆ¥xâˆ¥
            : by rw [mul_assoc, real.sqrt_mul (norm_nonneg _), real.sqrt_mul_self (norm_nonneg _)] }
endâŸ©

section real

variables {E' : Type*} {F' : Type*} [inner_product_space â„ E'] [inner_product_space â„ F']
variables [complete_space E'] [complete_space F']

-- Todo: Generalize this to `is_R_or_C`.
lemma is_adjoint_pair_inner (A : E' â†’L[â„] F') :
  linear_map.is_adjoint_pair (sesq_form_of_inner : E' â†’â‚—[â„] E' â†’â‚—[â„] â„)
  (sesq_form_of_inner : F' â†’â‚—[â„] F' â†’â‚—[â„] â„) A (Aâ€ ) :=
Î» x y, by simp only [sesq_form_of_inner_apply_apply, adjoint_inner_left, to_linear_map_eq_coe,
  coe_coe]

end real

end continuous_linear_map

namespace linear_map

variables [finite_dimensional ğ•œ E] [finite_dimensional ğ•œ F] [finite_dimensional ğ•œ G]
local attribute [instance, priority 20] finite_dimensional.complete

/-- The adjoint of an operator from the finite-dimensional inner product space E to the finite-
dimensional inner product space F. -/
def adjoint : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—â‹†[ğ•œ] (F â†’â‚—[ğ•œ] E) :=
((linear_map.to_continuous_linear_map : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—[ğ•œ] (E â†’L[ğ•œ] F)).trans
  continuous_linear_map.adjoint.to_linear_equiv).trans
  linear_map.to_continuous_linear_map.symm

lemma adjoint_to_continuous_linear_map (A : E â†’â‚—[ğ•œ] F) :
  A.adjoint.to_continuous_linear_map = A.to_continuous_linear_map.adjoint := rfl

lemma adjoint_eq_to_clm_adjoint (A : E â†’â‚—[ğ•œ] F) :
  A.adjoint = A.to_continuous_linear_map.adjoint := rfl

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_left (A : E â†’â‚—[ğ•œ] F) (x : E) (y : F) : âŸªadjoint A y, xâŸ« = âŸªy, A xâŸ« :=
begin
  rw [â†coe_to_continuous_linear_map A, adjoint_eq_to_clm_adjoint],
  exact continuous_linear_map.adjoint_inner_left _ x y,
end

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_right (A : E â†’â‚—[ğ•œ] F) (x : E) (y : F) : âŸªx, adjoint A yâŸ« = âŸªA x, yâŸ« :=
begin
  rw [â†coe_to_continuous_linear_map A, adjoint_eq_to_clm_adjoint],
  exact continuous_linear_map.adjoint_inner_right _ x y,
end

/-- The adjoint is involutive -/
@[simp] lemma adjoint_adjoint (A : E â†’â‚—[ğ•œ] F) : A.adjoint.adjoint = A :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  rw [adjoint_inner_right, adjoint_inner_left],
end

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp] lemma adjoint_comp (A : F â†’â‚—[ğ•œ] G) (B : E â†’â‚—[ğ•œ] F) :
  (A âˆ˜â‚— B).adjoint = B.adjoint âˆ˜â‚— A.adjoint :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  simp only [adjoint_inner_right, linear_map.coe_comp, function.comp_app],
end

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all `x` and `y`. -/
lemma eq_adjoint_iff (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ x y, âŸªA x, yâŸ« = âŸªx, B yâŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  ext x,
  exact ext_inner_right ğ•œ (Î» y, by simp only [adjoint_inner_left, h x y])
end

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all basis vectors `x` and `y`. -/
lemma eq_adjoint_iff_basis {Î¹â‚ : Type*} {Î¹â‚‚ : Type*} (bâ‚ : basis Î¹â‚ ğ•œ E) (bâ‚‚ : basis Î¹â‚‚ ğ•œ F)
  (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ (iâ‚ : Î¹â‚) (iâ‚‚ : Î¹â‚‚), âŸªA (bâ‚ iâ‚), bâ‚‚ iâ‚‚âŸ« = âŸªbâ‚ iâ‚, B (bâ‚‚ iâ‚‚)âŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  refine basis.ext bâ‚ (Î» iâ‚, _),
  exact ext_inner_right_basis bâ‚‚ (Î» iâ‚‚, by simp only [adjoint_inner_left, h iâ‚ iâ‚‚]),
end

lemma eq_adjoint_iff_basis_left {Î¹ : Type*} (b : basis Î¹ ğ•œ E) (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ i y, âŸªA (b i), yâŸ« = âŸªb i, B yâŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, basis.ext b (Î» i, _)âŸ©,
  exact ext_inner_right ğ•œ (Î» y, by simp only [h i, adjoint_inner_left]),
end

lemma eq_adjoint_iff_basis_right {Î¹ : Type*} (b : basis Î¹ ğ•œ F) (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ i x, âŸªA x, b iâŸ« = âŸªx, B (b i)âŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  ext x,
  refine ext_inner_right_basis b (Î» i, by simp only [h i, adjoint_inner_left]),
end

lemma is_self_adjoint_iff_eq_adjoint (A : E â†’â‚—[ğ•œ] E) :
  is_self_adjoint A â†” A = A.adjoint :=
by rw [is_self_adjoint, â† linear_map.eq_adjoint_iff]

/-- `E â†’â‚—[ğ•œ] E` is a star algebra with the adjoint as the star operation. -/
instance : has_star (E â†’â‚—[ğ•œ] E) := âŸ¨adjointâŸ©
instance : has_involutive_star (E â†’â‚—[ğ•œ] E) := âŸ¨adjoint_adjointâŸ©
instance : star_semigroup (E â†’â‚—[ğ•œ] E) := âŸ¨adjoint_compâŸ©
instance : star_ring (E â†’â‚—[ğ•œ] E) := âŸ¨linear_equiv.map_add adjointâŸ©
instance : star_module ğ•œ (E â†’â‚—[ğ•œ] E) := âŸ¨linear_equiv.map_smulâ‚›â‚— adjointâŸ©

lemma star_eq_adjoint (A : E â†’â‚—[ğ•œ] E) : star A = A.adjoint := rfl

section real

variables {E' : Type*} {F' : Type*} [inner_product_space â„ E'] [inner_product_space â„ F']
variables [finite_dimensional â„ E'] [finite_dimensional â„ F']

-- Todo: Generalize this to `is_R_or_C`.
lemma is_adjoint_pair_inner (A : E' â†’â‚—[â„] F') :
  is_adjoint_pair (sesq_form_of_inner : E' â†’â‚—[â„] E' â†’â‚—[â„] â„)
  (sesq_form_of_inner : F' â†’â‚—[â„] F' â†’â‚—[â„] â„) A A.adjoint :=
Î» x y, by simp only [sesq_form_of_inner_apply_apply, adjoint_inner_left]

end real

/-- The Gram operator Tâ€ T is self-adjoint. -/
lemma is_self_adjoint_adjoint_mul_self (T : E â†’â‚—[ğ•œ] E) : is_self_adjoint (T.adjoint * T) :=
Î» x y, by simp only [linear_map.mul_apply, linear_map.adjoint_inner_left,
  linear_map.adjoint_inner_right]

/-- The Gram operator Tâ€ T is a positive operator. -/
lemma re_inner_adjoint_mul_self_nonneg (T : E â†’â‚—[ğ•œ] E) (x : E) :
  0 â‰¤ re âŸª x, (T.adjoint * T) x âŸ« := by {simp only [linear_map.mul_apply,
  linear_map.adjoint_inner_right, inner_self_eq_norm_sq_to_K], norm_cast, exact sq_nonneg _}

@[simp] lemma im_inner_adjoint_mul_self_eq_zero (T : E â†’â‚—[ğ•œ] E) (x : E) :
  im âŸª x, linear_map.adjoint T (T x) âŸ« = 0 := by {simp only [linear_map.mul_apply,
    linear_map.adjoint_inner_right, inner_self_eq_norm_sq_to_K], norm_cast}

end linear_map

namespace matrix
variables {m n : Type*} [fintype m] [decidable_eq m] [fintype n] [decidable_eq n]
open_locale complex_conjugate

/-- The adjoint of the linear map associated to a matrix is the linear map associated to the
conjugate transpose of that matrix. -/
lemma conj_transpose_eq_adjoint (A : matrix m n ğ•œ) :
  to_lin' A.conj_transpose =
  @linear_map.adjoint _ (euclidean_space ğ•œ n) (euclidean_space ğ•œ m) _ _ _ _ _ (to_lin' A) :=
begin
  rw @linear_map.eq_adjoint_iff _ (euclidean_space ğ•œ m) (euclidean_space ğ•œ n),
  intros x y,
  convert dot_product_assoc (conj âˆ˜ (id x : m â†’ ğ•œ)) y A using 1,
  simp [dot_product, mul_vec, ring_hom.map_sum,  â† star_ring_end_apply, mul_comm],
end

end matrix
