/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/

import analysis.inner_product_space.dual
import analysis.inner_product_space.pi_L2

/-!
# Adjoint of operators on Hilbert spaces

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Given an operator `A : E â†’L[ğ•œ] F`, where `E` and `F` are Hilbert spaces, its adjoint
`adjoint A : F â†’L[ğ•œ] E` is the unique operator such that `âŸªx, A yâŸ« = âŸªadjoint A x, yâŸ«` for all
`x` and `y`.

We then use this to put a Câ‹†-algebra structure on `E â†’L[ğ•œ] E` with the adjoint as the star
operation.

This construction is used to define an adjoint for linear maps (i.e. not continuous) between
finite dimensional spaces.

## Main definitions

* `continuous_linear_map.adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] (F â†’L[ğ•œ] E)`: the adjoint of a continuous
  linear map, bundled as a conjugate-linear isometric equivalence.
* `linear_map.adjoint : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—â‹†[ğ•œ] (F â†’â‚—[ğ•œ] E)`: the adjoint of a linear map between
  finite-dimensional spaces, this time only as a conjugate-linear equivalence, since there is no
  norm defined on these maps.

## Implementation notes

* The continuous conjugate-linear version `adjoint_aux` is only an intermediate
  definition and is not meant to be used outside this file.

## Tags

adjoint

-/

noncomputable theory
open is_R_or_C
open_locale complex_conjugate

variables {ğ•œ E F G : Type*} [is_R_or_C ğ•œ]
variables [normed_add_comm_group E] [normed_add_comm_group F] [normed_add_comm_group G]
variables [inner_product_space ğ•œ E] [inner_product_space ğ•œ F] [inner_product_space ğ•œ G]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

/-! ### Adjoint operator -/

open inner_product_space

namespace continuous_linear_map

variables [complete_space E] [complete_space G]

/-- The adjoint, as a continuous conjugate-linear map.  This is only meant as an auxiliary
definition for the main definition `adjoint`, where this is bundled as a conjugate-linear isometric
equivalence. -/
def adjoint_aux : (E â†’L[ğ•œ] F) â†’Lâ‹†[ğ•œ] (F â†’L[ğ•œ] E) :=
(continuous_linear_map.compSL _ _ _ _ _ ((to_dual ğ•œ E).symm : normed_space.dual ğ•œ E â†’Lâ‹†[ğ•œ] E)).comp
  (to_sesq_form : (E â†’L[ğ•œ] F) â†’L[ğ•œ] F â†’Lâ‹†[ğ•œ] normed_space.dual ğ•œ E)

@[simp] lemma adjoint_aux_apply (A : E â†’L[ğ•œ] F) (x : F) :
  adjoint_aux A x = ((to_dual ğ•œ E).symm : (normed_space.dual ğ•œ E) â†’ E) ((to_sesq_form A) x) := rfl

lemma adjoint_aux_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªadjoint_aux A y, xâŸ« = âŸªy, A xâŸ« :=
by { simp only [adjoint_aux_apply, to_dual_symm_apply, to_sesq_form_apply_coe, coe_comp',
                innerSL_apply_coe]}

lemma adjoint_aux_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªx, adjoint_aux A yâŸ« = âŸªA x, yâŸ« :=
by rw [â†inner_conj_symm, adjoint_aux_inner_left, inner_conj_symm]

variables [complete_space F]

lemma adjoint_aux_adjoint_aux (A : E â†’L[ğ•œ] F) : adjoint_aux (adjoint_aux A) = A :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  rw [adjoint_aux_inner_right, adjoint_aux_inner_left],
end

@[simp] lemma adjoint_aux_norm (A : E â†’L[ğ•œ] F) : â€–adjoint_aux Aâ€– = â€–Aâ€– :=
begin
  refine le_antisymm _ _,
  { refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint_aux_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le },
  { nth_rewrite_lhs 0 [â†adjoint_aux_adjoint_aux A],
    refine continuous_linear_map.op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [adjoint_aux_apply, linear_isometry_equiv.norm_map],
    exact to_sesq_form_apply_norm_le }
end

/-- The adjoint of a bounded operator from Hilbert space E to Hilbert space F. -/
def adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] (F â†’L[ğ•œ] E) :=
linear_isometry_equiv.of_surjective
{ norm_map' := adjoint_aux_norm,
  ..adjoint_aux }
(Î» A, âŸ¨adjoint_aux A, adjoint_aux_adjoint_aux AâŸ©)

localized "postfix (name := adjoint) `â€ `:1000 := continuous_linear_map.adjoint" in inner_product

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªAâ€  y, xâŸ« = âŸªy, A xâŸ« :=
adjoint_aux_inner_left A x y

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªx, Aâ€  yâŸ« = âŸªA x, yâŸ« :=
adjoint_aux_inner_right A x y

/-- The adjoint is involutive -/
@[simp] lemma adjoint_adjoint (A : E â†’L[ğ•œ] F) : Aâ€ â€  = A :=
adjoint_aux_adjoint_aux A

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp] lemma adjoint_comp (A : F â†’L[ğ•œ] G) (B : E â†’L[ğ•œ] F) : (A âˆ˜L B)â€  = Bâ€  âˆ˜L Aâ€  :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  simp only [adjoint_inner_right, continuous_linear_map.coe_comp', function.comp_app],
end

lemma apply_norm_sq_eq_inner_adjoint_left (A : E â†’L[ğ•œ] E) (x : E) : â€–A xâ€–^2 = re âŸª(Aâ€  * A) x, xâŸ« :=
have h : âŸª(Aâ€  * A) x, xâŸ« = âŸªA x, A xâŸ« := by { rw [â†adjoint_inner_left], refl },
by rw [h, â†inner_self_eq_norm_sq _]

lemma apply_norm_eq_sqrt_inner_adjoint_left (A : E â†’L[ğ•œ] E) (x : E) :
  â€–A xâ€– = real.sqrt (re âŸª(Aâ€  * A) x, xâŸ«) :=
by rw [â†apply_norm_sq_eq_inner_adjoint_left, real.sqrt_sq (norm_nonneg _)]

lemma apply_norm_sq_eq_inner_adjoint_right (A : E â†’L[ğ•œ] E) (x : E) : â€–A xâ€–^2 = re âŸªx, (Aâ€  * A) xâŸ« :=
have h : âŸªx, (Aâ€  * A) xâŸ« = âŸªA x, A xâŸ« := by { rw [â†adjoint_inner_right], refl },
by rw [h, â†inner_self_eq_norm_sq _]

lemma apply_norm_eq_sqrt_inner_adjoint_right (A : E â†’L[ğ•œ] E) (x : E) :
  â€–A xâ€– = real.sqrt (re âŸªx, (Aâ€  * A) xâŸ«) :=
by rw [â†apply_norm_sq_eq_inner_adjoint_right, real.sqrt_sq (norm_nonneg _)]

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all `x` and `y`. -/
lemma eq_adjoint_iff (A : E â†’L[ğ•œ] F) (B : F â†’L[ğ•œ] E) :
  A = Bâ€  â†” (âˆ€ x y, âŸªA x, yâŸ« = âŸªx, B yâŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  ext x,
  exact ext_inner_right ğ•œ (Î» y, by simp only [adjoint_inner_left, h x y])
end

@[simp] lemma adjoint_id : (continuous_linear_map.id ğ•œ E).adjoint = continuous_linear_map.id ğ•œ E :=
begin
  refine eq.symm _,
  rw eq_adjoint_iff,
  simp,
end

lemma _root_.submodule.adjoint_subtypeL (U : submodule ğ•œ E)
  [complete_space U] :
  (U.subtypeL)â€  = orthogonal_projection U :=
begin
  symmetry,
  rw eq_adjoint_iff,
  intros x u,
  rw [U.coe_inner, inner_orthogonal_projection_left_eq_right,
      orthogonal_projection_mem_subspace_eq_self],
  refl
end

lemma _root_.submodule.adjoint_orthogonal_projection (U : submodule ğ•œ E)
  [complete_space U] :
  (orthogonal_projection U : E â†’L[ğ•œ] U)â€  = U.subtypeL :=
by rw [â† U.adjoint_subtypeL, adjoint_adjoint]

/-- `E â†’L[ğ•œ] E` is a star algebra with the adjoint as the star operation. -/
instance : has_star (E â†’L[ğ•œ] E) := âŸ¨adjointâŸ©
instance : has_involutive_star (E â†’L[ğ•œ] E) := âŸ¨adjoint_adjointâŸ©
instance : star_semigroup (E â†’L[ğ•œ] E) := âŸ¨adjoint_compâŸ©
instance : star_ring (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_add adjointâŸ©
instance : star_module ğ•œ (E â†’L[ğ•œ] E) := âŸ¨linear_isometry_equiv.map_smulâ‚›â‚— adjointâŸ©

lemma star_eq_adjoint (A : E â†’L[ğ•œ] E) : star A = Aâ€  := rfl

/-- A continuous linear operator is self-adjoint iff it is equal to its adjoint. -/
lemma is_self_adjoint_iff' {A : E â†’L[ğ•œ] E} : is_self_adjoint A â†” A.adjoint = A := iff.rfl

instance : cstar_ring (E â†’L[ğ•œ] E) :=
âŸ¨begin
  intros A,
  rw [star_eq_adjoint],
  refine le_antisymm _ _,
  { calc â€–Aâ€  * Aâ€– â‰¤ â€–Aâ€ â€– * â€–Aâ€–      : op_norm_comp_le _ _
              ... = â€–Aâ€– * â€–Aâ€–       : by rw [linear_isometry_equiv.norm_map] },
  { rw [â†sq, â†real.sqrt_le_sqrt_iff (norm_nonneg _), real.sqrt_sq (norm_nonneg _)],
    refine op_norm_le_bound _ (real.sqrt_nonneg _) (Î» x, _),
    have := calc
      re âŸª(Aâ€  * A) x, xâŸ« â‰¤ â€–(Aâ€  * A) xâ€– * â€–xâ€–     : re_inner_le_norm _ _
                    ...  â‰¤ â€–Aâ€  * Aâ€– * â€–xâ€– * â€–xâ€–   : mul_le_mul_of_nonneg_right
                                                    (le_op_norm _ _) (norm_nonneg _),
    calc â€–A xâ€– = real.sqrt (re âŸª(Aâ€  * A) x, xâŸ«)     : by rw [apply_norm_eq_sqrt_inner_adjoint_left]
          ...  â‰¤ real.sqrt (â€–Aâ€  * Aâ€– * â€–xâ€– * â€–xâ€–)   : real.sqrt_le_sqrt this
          ...  = real.sqrt (â€–Aâ€  * Aâ€–) * â€–xâ€–
            : by rw [mul_assoc, real.sqrt_mul (norm_nonneg _), real.sqrt_mul_self (norm_nonneg _)] }
endâŸ©

section real

variables {E' : Type*} {F' : Type*}
variables [normed_add_comm_group E'] [normed_add_comm_group F']
variables [inner_product_space â„ E'] [inner_product_space â„ F']
variables [complete_space E'] [complete_space F']

-- Todo: Generalize this to `is_R_or_C`.
lemma is_adjoint_pair_inner (A : E' â†’L[â„] F') :
  linear_map.is_adjoint_pair (sesq_form_of_inner : E' â†’â‚—[â„] E' â†’â‚—[â„] â„)
  (sesq_form_of_inner : F' â†’â‚—[â„] F' â†’â‚—[â„] â„) A (Aâ€ ) :=
Î» x y, by simp only [sesq_form_of_inner_apply_apply, adjoint_inner_left, to_linear_map_eq_coe,
  coe_coe]

end real

end continuous_linear_map

/-! ### Self-adjoint operators -/

namespace is_self_adjoint

open continuous_linear_map

variables [complete_space E] [complete_space F]

lemma adjoint_eq {A : E â†’L[ğ•œ] E} (hA : is_self_adjoint A) : A.adjoint = A := hA

/-- Every self-adjoint operator on an inner product space is symmetric. -/
lemma is_symmetric {A : E â†’L[ğ•œ] E} (hA : is_self_adjoint A) :
  (A : E â†’â‚—[ğ•œ] E).is_symmetric :=
Î» x y, by rw_mod_cast [â†A.adjoint_inner_right, hA.adjoint_eq]

/-- Conjugating preserves self-adjointness -/
lemma conj_adjoint {T : E â†’L[ğ•œ] E} (hT : is_self_adjoint T) (S : E â†’L[ğ•œ] F) :
  is_self_adjoint (S âˆ˜L T âˆ˜L S.adjoint) :=
begin
  rw is_self_adjoint_iff' at âŠ¢ hT,
  simp only [hT, adjoint_comp, adjoint_adjoint],
  exact continuous_linear_map.comp_assoc _ _ _,
end

/-- Conjugating preserves self-adjointness -/
lemma adjoint_conj {T : E â†’L[ğ•œ] E} (hT : is_self_adjoint T) (S : F â†’L[ğ•œ] E) :
  is_self_adjoint (S.adjoint âˆ˜L T âˆ˜L S) :=
begin
  rw is_self_adjoint_iff' at âŠ¢ hT,
  simp only [hT, adjoint_comp, adjoint_adjoint],
  exact continuous_linear_map.comp_assoc _ _ _,
end

lemma _root_.continuous_linear_map.is_self_adjoint_iff_is_symmetric {A : E â†’L[ğ•œ] E} :
  is_self_adjoint A â†” (A : E â†’â‚—[ğ•œ] E).is_symmetric :=
âŸ¨Î» hA, hA.is_symmetric, Î» hA, ext $ Î» x, ext_inner_right ğ•œ $
  Î» y, (A.adjoint_inner_left y x).symm â–¸ (hA x y).symmâŸ©

lemma _root_.linear_map.is_symmetric.is_self_adjoint {A : E â†’L[ğ•œ] E}
  (hA : (A : E â†’â‚—[ğ•œ] E).is_symmetric) : is_self_adjoint A :=
by rwa â†continuous_linear_map.is_self_adjoint_iff_is_symmetric at hA

/-- The orthogonal projection is self-adjoint. -/
lemma _root_.orthogonal_projection_is_self_adjoint (U : submodule ğ•œ E)
  [complete_space U] :
  is_self_adjoint (U.subtypeL âˆ˜L orthogonal_projection U) :=
(orthogonal_projection_is_symmetric U).is_self_adjoint

lemma conj_orthogonal_projection {T : E â†’L[ğ•œ] E}
  (hT : is_self_adjoint T) (U : submodule ğ•œ E) [complete_space U] :
  is_self_adjoint (U.subtypeL âˆ˜L orthogonal_projection U âˆ˜L T âˆ˜L U.subtypeL âˆ˜L
    orthogonal_projection U) :=
begin
  rw â†continuous_linear_map.comp_assoc,
  nth_rewrite 0 â†(orthogonal_projection_is_self_adjoint U).adjoint_eq,
  refine hT.adjoint_conj _,
end

end is_self_adjoint

namespace linear_map

variables [complete_space E]
variables {T : E â†’â‚—[ğ•œ] E}

/-- The **Hellinger--Toeplitz theorem**: Construct a self-adjoint operator from an everywhere
  defined symmetric operator.-/
def is_symmetric.to_self_adjoint (hT : is_symmetric T) : self_adjoint (E â†’L[ğ•œ] E) :=
âŸ¨âŸ¨T, hT.continuousâŸ©, continuous_linear_map.is_self_adjoint_iff_is_symmetric.mpr hTâŸ©

lemma is_symmetric.coe_to_self_adjoint (hT : is_symmetric T) :
  (hT.to_self_adjoint : E â†’â‚—[ğ•œ] E) = T := rfl

lemma is_symmetric.to_self_adjoint_apply (hT : is_symmetric T) {x : E} :
  hT.to_self_adjoint x = T x := rfl

end linear_map

namespace linear_map

variables [finite_dimensional ğ•œ E] [finite_dimensional ğ•œ F] [finite_dimensional ğ•œ G]
local attribute [instance, priority 20] finite_dimensional.complete

/-- The adjoint of an operator from the finite-dimensional inner product space E to the finite-
dimensional inner product space F. -/
def adjoint : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—â‹†[ğ•œ] (F â†’â‚—[ğ•œ] E) :=
((linear_map.to_continuous_linear_map : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—[ğ•œ] (E â†’L[ğ•œ] F)).trans
  continuous_linear_map.adjoint.to_linear_equiv).trans
  linear_map.to_continuous_linear_map.symm

lemma adjoint_to_continuous_linear_map (A : E â†’â‚—[ğ•œ] F) :
  A.adjoint.to_continuous_linear_map = A.to_continuous_linear_map.adjoint := rfl

lemma adjoint_eq_to_clm_adjoint (A : E â†’â‚—[ğ•œ] F) :
  A.adjoint = A.to_continuous_linear_map.adjoint := rfl

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_left (A : E â†’â‚—[ğ•œ] F) (x : E) (y : F) : âŸªadjoint A y, xâŸ« = âŸªy, A xâŸ« :=
begin
  rw [â†coe_to_continuous_linear_map A, adjoint_eq_to_clm_adjoint],
  exact continuous_linear_map.adjoint_inner_left _ x y,
end

/-- The fundamental property of the adjoint. -/
lemma adjoint_inner_right (A : E â†’â‚—[ğ•œ] F) (x : E) (y : F) : âŸªx, adjoint A yâŸ« = âŸªA x, yâŸ« :=
begin
  rw [â†coe_to_continuous_linear_map A, adjoint_eq_to_clm_adjoint],
  exact continuous_linear_map.adjoint_inner_right _ x y,
end

/-- The adjoint is involutive -/
@[simp] lemma adjoint_adjoint (A : E â†’â‚—[ğ•œ] F) : A.adjoint.adjoint = A :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  rw [adjoint_inner_right, adjoint_inner_left],
end

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp] lemma adjoint_comp (A : F â†’â‚—[ğ•œ] G) (B : E â†’â‚—[ğ•œ] F) :
  (A âˆ˜â‚— B).adjoint = B.adjoint âˆ˜â‚— A.adjoint :=
begin
  ext v,
  refine ext_inner_left ğ•œ (Î» w, _),
  simp only [adjoint_inner_right, linear_map.coe_comp, function.comp_app],
end

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all `x` and `y`. -/
lemma eq_adjoint_iff (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ x y, âŸªA x, yâŸ« = âŸªx, B yâŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  ext x,
  exact ext_inner_right ğ•œ (Î» y, by simp only [adjoint_inner_left, h x y])
end

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all basis vectors `x` and `y`. -/
lemma eq_adjoint_iff_basis {Î¹â‚ : Type*} {Î¹â‚‚ : Type*} (bâ‚ : basis Î¹â‚ ğ•œ E) (bâ‚‚ : basis Î¹â‚‚ ğ•œ F)
  (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ (iâ‚ : Î¹â‚) (iâ‚‚ : Î¹â‚‚), âŸªA (bâ‚ iâ‚), bâ‚‚ iâ‚‚âŸ« = âŸªbâ‚ iâ‚, B (bâ‚‚ iâ‚‚)âŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  refine basis.ext bâ‚ (Î» iâ‚, _),
  exact ext_inner_right_basis bâ‚‚ (Î» iâ‚‚, by simp only [adjoint_inner_left, h iâ‚ iâ‚‚]),
end

lemma eq_adjoint_iff_basis_left {Î¹ : Type*} (b : basis Î¹ ğ•œ E) (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ i y, âŸªA (b i), yâŸ« = âŸªb i, B yâŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, basis.ext b (Î» i, _)âŸ©,
  exact ext_inner_right ğ•œ (Î» y, by simp only [h i, adjoint_inner_left]),
end

lemma eq_adjoint_iff_basis_right {Î¹ : Type*} (b : basis Î¹ ğ•œ F) (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
  A = B.adjoint â†” (âˆ€ i x, âŸªA x, b iâŸ« = âŸªx, B (b i)âŸ«) :=
begin
  refine âŸ¨Î» h x y, by rw [h, adjoint_inner_left], Î» h, _âŸ©,
  ext x,
  refine ext_inner_right_basis b (Î» i, by simp only [h i, adjoint_inner_left]),
end

/-- `E â†’â‚—[ğ•œ] E` is a star algebra with the adjoint as the star operation. -/
instance : has_star (E â†’â‚—[ğ•œ] E) := âŸ¨adjointâŸ©
instance : has_involutive_star (E â†’â‚—[ğ•œ] E) := âŸ¨adjoint_adjointâŸ©
instance : star_semigroup (E â†’â‚—[ğ•œ] E) := âŸ¨adjoint_compâŸ©
instance : star_ring (E â†’â‚—[ğ•œ] E) := âŸ¨linear_equiv.map_add adjointâŸ©
instance : star_module ğ•œ (E â†’â‚—[ğ•œ] E) := âŸ¨linear_equiv.map_smulâ‚›â‚— adjointâŸ©

lemma star_eq_adjoint (A : E â†’â‚—[ğ•œ] E) : star A = A.adjoint := rfl

/-- A continuous linear operator is self-adjoint iff it is equal to its adjoint. -/
lemma is_self_adjoint_iff' {A : E â†’â‚—[ğ•œ] E} : is_self_adjoint A â†” A.adjoint = A := iff.rfl

lemma is_symmetric_iff_is_self_adjoint (A : E â†’â‚—[ğ•œ] E) :
  is_symmetric A â†” is_self_adjoint A :=
by { rw [is_self_adjoint_iff', is_symmetric, â† linear_map.eq_adjoint_iff], exact eq_comm }

section real

variables {E' : Type*} {F' : Type*}
variables [normed_add_comm_group E'] [normed_add_comm_group F']
variables [inner_product_space â„ E'] [inner_product_space â„ F']
variables [finite_dimensional â„ E'] [finite_dimensional â„ F']

-- Todo: Generalize this to `is_R_or_C`.
lemma is_adjoint_pair_inner (A : E' â†’â‚—[â„] F') :
  is_adjoint_pair (sesq_form_of_inner : E' â†’â‚—[â„] E' â†’â‚—[â„] â„)
  (sesq_form_of_inner : F' â†’â‚—[â„] F' â†’â‚—[â„] â„) A A.adjoint :=
Î» x y, by simp only [sesq_form_of_inner_apply_apply, adjoint_inner_left]

end real

/-- The Gram operator Tâ€ T is symmetric. -/
lemma is_symmetric_adjoint_mul_self (T : E â†’â‚—[ğ•œ] E) : is_symmetric (T.adjoint * T) :=
Î» x y, by simp only [mul_apply, adjoint_inner_left, adjoint_inner_right]

/-- The Gram operator Tâ€ T is a positive operator. -/
lemma re_inner_adjoint_mul_self_nonneg (T : E â†’â‚—[ğ•œ] E) (x : E) :
  0 â‰¤ re âŸª x, (T.adjoint * T) x âŸ« := by {simp only [mul_apply, adjoint_inner_right,
    inner_self_eq_norm_sq_to_K], norm_cast, exact sq_nonneg _}

@[simp] lemma im_inner_adjoint_mul_self_eq_zero (T : E â†’â‚—[ğ•œ] E) (x : E) :
  im âŸª x, linear_map.adjoint T (T x) âŸ« = 0 := by {simp only [mul_apply,
    adjoint_inner_right, inner_self_eq_norm_sq_to_K], norm_cast}

end linear_map

namespace matrix
variables {m n : Type*} [fintype m] [decidable_eq m] [fintype n] [decidable_eq n]
open_locale complex_conjugate

/-- The adjoint of the linear map associated to a matrix is the linear map associated to the
conjugate transpose of that matrix. -/
lemma to_euclidean_lin_conj_transpose_eq_adjoint (A : matrix m n ğ•œ) :
  A.conj_transpose.to_euclidean_lin = A.to_euclidean_lin.adjoint :=
begin
  rw linear_map.eq_adjoint_iff,
  intros x y,
  simp_rw [euclidean_space.inner_eq_star_dot_product, pi_Lp_equiv_to_euclidean_lin,
    to_lin'_apply, star_mul_vec, conj_transpose_conj_transpose, dot_product_mul_vec],
end

end matrix
