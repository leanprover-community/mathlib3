/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
import algebra.direct_sum.module
import analysis.complex.basic
import analysis.convex.uniform
import analysis.normed_space.completion
import analysis.normed_space.bounded_linear_maps
import linear_algebra.bilinear_form

/-!
# Inner product space

This file defines inner product spaces and proves the basic properties.  We do not formally
define Hilbert spaces, but they can be obtained using the set of assumptions
`[normed_add_comm_group E] [inner_product_space ğ•œ E] [complete_space E]`.

An inner product space is a vector space endowed with an inner product. It generalizes the notion of
dot product in `â„^n` and provides the means of defining the length of a vector and the angle between
two vectors. In particular vectors `x` and `y` are orthogonal if their inner product equals zero.
We define both the real and complex cases at the same time using the `is_R_or_C` typeclass.

This file proves general results on inner product spaces. For the specific construction of an inner
product structure on `n â†’ ğ•œ` for `ğ•œ = â„` or `â„‚`, see `euclidean_space` in
`analysis.inner_product_space.pi_L2`.

## Main results

- We define the class `inner_product_space ğ•œ E` extending `normed_space ğ•œ E` with a number of basic
  properties, most notably the Cauchy-Schwarz inequality. Here `ğ•œ` is understood to be either `â„`
  or `â„‚`, through the `is_R_or_C` typeclass.
- We show that the inner product is continuous, `continuous_inner`, and bundle it as the
  the continuous sesquilinear map `innerSL` (see also `innerâ‚›â‚—` for the non-continuous version).
- We define `orthonormal`, a predicate on a function `v : Î¹ â†’ E`, and prove the existence of a
  maximal orthonormal set, `exists_maximal_orthonormal`.  Bessel's inequality,
  `orthonormal.tsum_inner_products_le`, states that given an orthonormal set `v` and a vector `x`,
  the sum of the norm-squares of the inner products `âŸªv i, xâŸ«` is no more than the norm-square of
  `x`. For the existence of orthonormal bases, Hilbert bases, etc., see the file
  `analysis.inner_product_space.projection`.

## Notation

We globally denote the real and complex inner products by `âŸªÂ·, Â·âŸ«_â„` and `âŸªÂ·, Â·âŸ«_â„‚` respectively.
We also provide two notation namespaces: `real_inner_product_space`, `complex_inner_product_space`,
which respectively introduce the plain notation `âŸªÂ·, Â·âŸ«` for the real and complex inner product.

## Implementation notes

We choose the convention that inner products are conjugate linear in the first argument and linear
in the second.

## Tags

inner product space, Hilbert space, norm

## References
*  [ClÃ©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
*  [ClÃ©ment & Martin, *A Coq formal proof of the Laxâ€“Milgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/

noncomputable theory

open is_R_or_C real filter
open_locale big_operators topology complex_conjugate

variables {ğ•œ E F : Type*} [is_R_or_C ğ•œ]

/-- Syntactic typeclass for types endowed with an inner product -/
class has_inner (ğ•œ E : Type*) := (inner : E â†’ E â†’ ğ•œ)

export has_inner (inner)

notation `âŸª`x`, `y`âŸ«_â„` := @inner â„ _ _ x y
notation `âŸª`x`, `y`âŸ«_â„‚` := @inner â„‚ _ _ x y

section notations

localized "notation (name := inner.real)
  `âŸª`x`, `y`âŸ«` := @inner â„ _ _ x y" in real_inner_product_space
localized "notation (name := inner.complex)
  `âŸª`x`, `y`âŸ«` := @inner â„‚ _ _ x y" in complex_inner_product_space

end notations

/--
An inner product space is a vector space with an additional operation called inner product.
The norm could be derived from the inner product, instead we require the existence of a norm and
the fact that `â€–xâ€–^2 = re âŸªx, xâŸ«` to be able to put instances on `ğ•‚` or product
spaces.

To construct a norm from an inner product, see `inner_product_space.of_core`.
-/
class inner_product_space (ğ•œ : Type*) (E : Type*) [is_R_or_C ğ•œ] [normed_add_comm_group E]
  extends normed_space ğ•œ E, has_inner ğ•œ E :=
(norm_sq_eq_inner : âˆ€ (x : E), â€–xâ€–^2 = re (inner x x))
(conj_symm : âˆ€ x y, conj (inner y x) = inner x y)
(add_left  : âˆ€ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : âˆ€ x y r, inner (r â€¢ x) y = (conj r) * inner x y)

/-!
### Constructing a normed space structure from an inner product

In the definition of an inner product space, we require the existence of a norm, which is equal
(but maybe not defeq) to the square root of the scalar product. This makes it possible to put
an inner product space structure on spaces with a preexisting norm (for instance `â„`), with good
properties. However, sometimes, one would like to define the norm starting only from a well-behaved
scalar product. This is what we implement in this paragraph, starting from a structure
`inner_product_space.core` stating that we have a nice scalar product.

Our goal here is not to develop a whole theory with all the supporting API, as this will be done
below for `inner_product_space`. Instead, we implement the bare minimum to go as directly as
possible to the construction of the norm and the proof of the triangular inequality.

Warning: Do not use this `core` structure if the space you are interested in already has a norm
instance defined on it, otherwise this will create a second non-defeq norm instance!
-/

/-- A structure requiring that a scalar product is positive definite and symmetric, from which one
can construct an `inner_product_space` instance in `inner_product_space.of_core`. -/
@[nolint has_nonempty_instance]
structure inner_product_space.core (ğ•œ : Type*) (F : Type*)
  [is_R_or_C ğ•œ] [add_comm_group F] [module ğ•œ F] extends has_inner ğ•œ F :=
(conj_symm : âˆ€ x y, conj (inner y x) = inner x y)
(nonneg_re : âˆ€ x, 0 â‰¤ re (inner x x))
(definite  : âˆ€ x, inner x x = 0 â†’ x = 0)
(add_left  : âˆ€ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : âˆ€ x y r, inner (r â€¢ x) y = (conj r) * inner x y)

/- We set `inner_product_space.core` to be a class as we will use it as such in the construction
of the normed space structure that it produces. However, all the instances we will use will be
local to this proof. -/
attribute [class] inner_product_space.core

/-- Define `inner_product_space.core` from `inner_product_space`. Defined to reuse lemmas about
`inner_product_space.core` for `inner_product_space`s. Note that the `has_norm` instance provided by
`inner_product_space.core.has_norm` is propositionally but not definitionally equal to the original
norm. -/
def inner_product_space.to_core [normed_add_comm_group E] [c : inner_product_space ğ•œ E] :
  inner_product_space.core ğ•œ E :=
{ nonneg_re := Î» x, by { rw [â† inner_product_space.norm_sq_eq_inner], apply sq_nonneg },
  definite := Î» x hx, norm_eq_zero.1 $ pow_eq_zero $
    by rw [inner_product_space.norm_sq_eq_inner x, hx, map_zero],
  .. c }

namespace inner_product_space.core

variables [add_comm_group F] [module ğ•œ F] [c : inner_product_space.core ğ•œ F]
include c

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ F _ x y
local notation `norm_sqK` := @is_R_or_C.norm_sq ğ•œ _
local notation `reK` := @is_R_or_C.re ğ•œ _
local notation `ext_iff` := @is_R_or_C.ext_iff ğ•œ _
local postfix `â€ `:90 := star_ring_end _

/-- Inner product defined by the `inner_product_space.core` structure. We can't reuse
`inner_product_space.core.to_has_inner` because it takes `inner_product_space.core` as an explicit
argument. -/
def to_has_inner' : has_inner ğ•œ F := c.to_has_inner
local attribute [instance] to_has_inner'

/-- The norm squared function for `inner_product_space.core` structure. -/
def norm_sq (x : F) := reK âŸªx, xâŸ«

local notation `norm_sqF` := @norm_sq ğ•œ F _ _ _ _

lemma inner_conj_symm (x y : F) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« := c.conj_symm x y

lemma inner_self_nonneg {x : F} : 0 â‰¤ re âŸªx, xâŸ« := c.nonneg_re _

lemma inner_self_im (x : F) : im âŸªx, xâŸ« = 0 :=
by rw [â† @of_real_inj ğ•œ, im_eq_conj_sub]; simp [inner_conj_symm]

lemma inner_add_left (x y z : F) : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
c.add_left _ _ _

lemma inner_add_right (x y z : F) : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« :=
by rw [â†inner_conj_symm, inner_add_left, ring_hom.map_add]; simp only [inner_conj_symm]

lemma coe_norm_sq_eq_inner_self (x : F) : (norm_sqF x : ğ•œ) = âŸªx, xâŸ« :=
begin
  rw ext_iff,
  exact âŸ¨by simp only [of_real_re]; refl, by simp only [inner_self_im, of_real_im]âŸ©
end

lemma inner_re_symm (x y : F) : re âŸªx, yâŸ« = re âŸªy, xâŸ« :=
by rw [â†inner_conj_symm, conj_re]

lemma inner_im_symm (x y : F) : im âŸªx, yâŸ« = -im âŸªy, xâŸ« :=
by rw [â†inner_conj_symm, conj_im]

lemma inner_smul_left (x y : F) {r : ğ•œ} : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
c.smul_left _ _ _

lemma inner_smul_right (x y : F) {r : ğ•œ} : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« :=
by rw [â†inner_conj_symm, inner_smul_left]; simp only [conj_conj, inner_conj_symm, ring_hom.map_mul]

lemma inner_zero_left (x : F) : âŸª0, xâŸ« = 0 :=
by rw [â†zero_smul ğ•œ (0 : F), inner_smul_left]; simp only [zero_mul, ring_hom.map_zero]

lemma inner_zero_right (x : F) : âŸªx, 0âŸ« = 0 :=
by rw [â†inner_conj_symm, inner_zero_left]; simp only [ring_hom.map_zero]

lemma inner_self_eq_zero {x : F} : âŸªx, xâŸ« = 0 â†” x = 0 :=
âŸ¨c.definite _, by { rintro rfl, exact inner_zero_left _ }âŸ©

lemma norm_sq_eq_zero {x : F} : norm_sqF x = 0 â†” x = 0 :=
iff.trans (by simp only [norm_sq, ext_iff, map_zero, inner_self_im, eq_self_iff_true, and_true])
  (@inner_self_eq_zero ğ•œ _ _ _ _ _ x)

lemma inner_self_ne_zero {x : F} : âŸªx, xâŸ« â‰  0 â†” x â‰  0 :=
inner_self_eq_zero.not

lemma inner_self_re_to_K (x : F) : (re âŸªx, xâŸ« : ğ•œ) = âŸªx, xâŸ« :=
by norm_num [ext_iff, inner_self_im]

lemma norm_inner_symm (x y : F) : â€–âŸªx, yâŸ«â€– = â€–âŸªy, xâŸ«â€– :=
by rw [â†inner_conj_symm, norm_conj]

lemma inner_neg_left (x y : F) : âŸª-x, yâŸ« = -âŸªx, yâŸ« :=
by { rw [â† neg_one_smul ğ•œ x, inner_smul_left], simp }

lemma inner_neg_right (x y : F) : âŸªx, -yâŸ« = -âŸªx, yâŸ« :=
by rw [â†inner_conj_symm, inner_neg_left]; simp only [ring_hom.map_neg, inner_conj_symm]

lemma inner_sub_left (x y z : F) : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_left, inner_neg_left] }

lemma inner_sub_right (x y z : F) : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_right, inner_neg_right] }

lemma inner_mul_symm_re_eq_norm (x y : F) : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = â€–âŸªx, yâŸ« * âŸªy, xâŸ«â€– :=
by { rw [â†inner_conj_symm, mul_comm], exact re_eq_norm_of_mul_conj (inner y x), }

/-- Expand `inner (x + y) (x + y)` -/
lemma inner_add_add_self (x y : F) : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_add_left, inner_add_right]; ring

/- Expand `inner (x - y) (x - y)` -/
lemma inner_sub_sub_self (x y : F) : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_sub_left, inner_sub_right]; ring

/-- An auxiliary equality useful to prove the **Cauchyâ€“Schwarz inequality**: the square of the norm
of `âŸªx, yâŸ« â€¢ x - âŸªx, xâŸ« â€¢ y` is equal to `â€–xâ€– ^ 2 * (â€–xâ€– ^ 2 * â€–yâ€– ^ 2 - â€–âŸªx, yâŸ«â€– ^ 2)`. We use
`inner_product_space.of_core.norm_sq x` etc (defeq to `is_R_or_C.re âŸªx, xâŸ«`) instead of `â€–xâ€– ^ 2`
etc to avoid extra rewrites when applying it to an `inner_product_space`. -/
theorem cauchy_schwarz_aux (x y : F) :
  norm_sqF (âŸªx, yâŸ« â€¢ x - âŸªx, xâŸ« â€¢ y) =
    norm_sqF x * (norm_sqF x * norm_sqF y - â€–âŸªx, yâŸ«â€– ^ 2) :=
begin
  rw [â† @of_real_inj ğ•œ, coe_norm_sq_eq_inner_self],
  simp only [inner_sub_sub_self, inner_smul_left, inner_smul_right, conj_of_real, mul_sub,
    â† coe_norm_sq_eq_inner_self x, â† coe_norm_sq_eq_inner_self y],
  rw [â† mul_assoc, mul_conj, is_R_or_C.conj_mul, norm_sq_eq_def',
    mul_left_comm, â† inner_conj_symm y, mul_conj, norm_sq_eq_def'],
  push_cast,
  ring
end

/--
**Cauchyâ€“Schwarz inequality**.
We need this for the `core` structure to prove the triangle inequality below when
showing the core is a normed group.
-/
lemma inner_mul_inner_self_le (x y : F) : â€–âŸªx, yâŸ«â€– * â€–âŸªy, xâŸ«â€– â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« :=
begin
  rcases eq_or_ne x 0 with (rfl | hx),
  { simp only [inner_zero_left, map_zero, zero_mul, norm_zero] },
  { have hx' : 0 < norm_sqF x := inner_self_nonneg.lt_of_ne' (mt norm_sq_eq_zero.1 hx),
    rw [â† sub_nonneg, â† mul_nonneg_iff_right_nonneg_of_pos hx', â† norm_sq, â† norm_sq,
      norm_inner_symm y, â† sq, â† cauchy_schwarz_aux],
    exact inner_self_nonneg }
end

/-- Norm constructed from a `inner_product_space.core` structure, defined to be the square root
of the scalar product. -/
def to_has_norm : has_norm F :=
{ norm := Î» x, sqrt (re âŸªx, xâŸ«) }

local attribute [instance] to_has_norm

lemma norm_eq_sqrt_inner (x : F) : â€–xâ€– = sqrt (re âŸªx, xâŸ«) := rfl

lemma inner_self_eq_norm_mul_norm (x : F) : re âŸªx, xâŸ« = â€–xâ€– * â€–xâ€– :=
by rw [norm_eq_sqrt_inner, â†sqrt_mul inner_self_nonneg (re âŸªx, xâŸ«),
  sqrt_mul_self inner_self_nonneg]

lemma sqrt_norm_sq_eq_norm (x : F) : sqrt (norm_sqF x) = â€–xâ€– := rfl

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma norm_inner_le_norm (x y : F) : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– :=
nonneg_le_nonneg_of_sq_le_sq (mul_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) $
  calc â€–âŸªx, yâŸ«â€– * â€–âŸªx, yâŸ«â€– = â€–âŸªx, yâŸ«â€– * â€–âŸªy, xâŸ«â€– : by rw [norm_inner_symm]
  ... â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« : inner_mul_inner_self_le x y
  ... = â€–xâ€– * â€–yâ€– * (â€–xâ€– * â€–yâ€–) : by simp only [inner_self_eq_norm_mul_norm]; ring

/-- Normed group structure constructed from an `inner_product_space.core` structure -/
def to_normed_add_comm_group : normed_add_comm_group F :=
add_group_norm.to_normed_add_comm_group
{ to_fun := Î» x, sqrt (re âŸªx, xâŸ«),
  map_zero' := by simp only [sqrt_zero, inner_zero_right, map_zero],
  neg' := Î» x, by simp only [inner_neg_left, neg_neg, inner_neg_right],
  add_le' := Î» x y, begin
    have hâ‚ : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– := norm_inner_le_norm _ _,
    have hâ‚‚ : re âŸªx, yâŸ« â‰¤ â€–âŸªx, yâŸ«â€– := re_le_norm _,
    have hâ‚ƒ : re âŸªx, yâŸ« â‰¤ â€–xâ€– * â€–yâ€– := hâ‚‚.trans hâ‚,
    have hâ‚„ : re âŸªy, xâŸ« â‰¤ â€–xâ€– * â€–yâ€– := by rwa [â†inner_conj_symm, conj_re],
    have : â€–x + yâ€– * â€–x + yâ€– â‰¤ (â€–xâ€– + â€–yâ€–) * (â€–xâ€– + â€–yâ€–),
    { simp only [â†inner_self_eq_norm_mul_norm, inner_add_add_self, mul_add, mul_comm, map_add],
      linarith },
    exact nonneg_le_nonneg_of_sq_le_sq (add_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) this,
  end,
  eq_zero_of_map_eq_zero' := Î» x hx, norm_sq_eq_zero.1 $ (sqrt_eq_zero inner_self_nonneg).1 hx }

local attribute [instance] to_normed_add_comm_group

/-- Normed space structure constructed from a `inner_product_space.core` structure -/
def to_normed_space : normed_space ğ•œ F :=
{ norm_smul_le := assume r x,
  begin
    rw [norm_eq_sqrt_inner, inner_smul_left, inner_smul_right, â†mul_assoc],
    rw [is_R_or_C.conj_mul, of_real_mul_re, sqrt_mul, â† coe_norm_sq_eq_inner_self, of_real_re],
    { simp [sqrt_norm_sq_eq_norm, is_R_or_C.sqrt_norm_sq_eq_norm] },
    { exact norm_sq_nonneg r }
  end }

end inner_product_space.core

section
local attribute [instance] inner_product_space.core.to_normed_add_comm_group

/-- Given a `inner_product_space.core` structure on a space, one can use it to turn
the space into an inner product space. The `normed_add_comm_group` structure is expected
to already be defined with `inner_product_space.of_core.to_normed_add_comm_group`. -/
def inner_product_space.of_core [add_comm_group F] [module ğ•œ F]
  (c : inner_product_space.core ğ•œ F) : inner_product_space ğ•œ F :=
begin
  letI : normed_space ğ•œ F := @inner_product_space.core.to_normed_space ğ•œ F _ _ _ c,
  exact { norm_sq_eq_inner := Î» x,
    begin
      have hâ‚ : â€–xâ€–^2 = (sqrt (re (c.inner x x))) ^ 2 := rfl,
      have hâ‚‚ : 0 â‰¤ re (c.inner x x) := inner_product_space.core.inner_self_nonneg,
      simp [hâ‚, sq_sqrt, hâ‚‚],
    end,
    ..c }
end

end

/-! ### Properties of inner product spaces -/

variables [normed_add_comm_group E] [inner_product_space ğ•œ E]
variables [normed_add_comm_group F] [inner_product_space â„ F]
variables [dec_E : decidable_eq E]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y
local notation `IK` := @is_R_or_C.I ğ•œ _
local postfix `â€ `:90 := star_ring_end _

export inner_product_space (norm_sq_eq_inner)

section basic_properties

@[simp] lemma inner_conj_symm (x y : E) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« := inner_product_space.conj_symm _ _
lemma real_inner_comm (x y : F) : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := @inner_conj_symm â„ _ _ _ _ x y

lemma inner_eq_zero_symm {x y : E} : âŸªx, yâŸ« = 0 â†” âŸªy, xâŸ« = 0 :=
by { rw [â† inner_conj_symm], exact star_eq_zero }

@[simp] lemma inner_self_im (x : E) : im âŸªx, xâŸ« = 0 :=
by rw [â† @of_real_inj ğ•œ, im_eq_conj_sub]; simp

lemma inner_add_left (x y z : E) : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
inner_product_space.add_left _ _ _

lemma inner_add_right (x y z : E) : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« :=
by { rw [â†inner_conj_symm, inner_add_left, ring_hom.map_add], simp only [inner_conj_symm] }

lemma inner_re_symm (x y : E) : re âŸªx, yâŸ« = re âŸªy, xâŸ« :=
by rw [â†inner_conj_symm, conj_re]

lemma inner_im_symm (x y : E) : im âŸªx, yâŸ« = -im âŸªy, xâŸ« :=
by rw [â†inner_conj_symm, conj_im]

lemma inner_smul_left (x y : E) (r : ğ•œ) : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
inner_product_space.smul_left _ _ _
lemma real_inner_smul_left (x y : F) (r : â„) : âŸªr â€¢ x, yâŸ«_â„ = r * âŸªx, yâŸ«_â„ := inner_smul_left _ _ _

lemma inner_smul_real_left (x y : E) (r : â„) : âŸª(r : ğ•œ) â€¢ x, yâŸ« = r â€¢ âŸªx, yâŸ« :=
by { rw [inner_smul_left, conj_of_real, algebra.smul_def], refl }

lemma inner_smul_right (x y : E) (r : ğ•œ) : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« :=
by rw [â†inner_conj_symm, inner_smul_left, ring_hom.map_mul, conj_conj, inner_conj_symm]
lemma real_inner_smul_right (x y : F) (r : â„) : âŸªx, r â€¢ yâŸ«_â„ = r * âŸªx, yâŸ«_â„ :=
inner_smul_right _ _ _

lemma inner_smul_real_right (x y : E) (r : â„) : âŸªx, (r : ğ•œ) â€¢ yâŸ« = r â€¢ âŸªx, yâŸ« :=
by { rw [inner_smul_right, algebra.smul_def], refl }

/-- The inner product as a sesquilinear form.

Note that in the case `ğ•œ = â„` this is a bilinear form. -/
@[simps]
def sesq_form_of_inner : E â†’â‚—[ğ•œ] E â†’â‚—â‹†[ğ•œ] ğ•œ :=
linear_map.mkâ‚‚'â‚›â‚— (ring_hom.id ğ•œ) (star_ring_end _)
  (Î» x y, âŸªy, xâŸ«)
  (Î» x y z, inner_add_right _ _ _)
  (Î» r x y, inner_smul_right _ _ _)
  (Î» x y z, inner_add_left _ _ _)
  (Î» r x y, inner_smul_left _ _ _)

/-- The real inner product as a bilinear form. -/
@[simps]
def bilin_form_of_real_inner : bilin_form â„ F :=
{ bilin := inner,
  bilin_add_left := inner_add_left,
  bilin_smul_left := Î» a x y, inner_smul_left _ _ _,
  bilin_add_right := inner_add_right,
  bilin_smul_right := Î» a x y, inner_smul_right _ _ _ }

/-- An inner product with a sum on the left. -/
lemma sum_inner {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ E) (x : E) :
  âŸªâˆ‘ i in s, f i, xâŸ« = âˆ‘ i in s, âŸªf i, xâŸ« := (sesq_form_of_inner x).map_sum

/-- An inner product with a sum on the right. -/
lemma inner_sum {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ E) (x : E) :
  âŸªx, âˆ‘ i in s, f iâŸ« = âˆ‘ i in s, âŸªx, f iâŸ« := (linear_map.flip sesq_form_of_inner x).map_sum

/-- An inner product with a sum on the left, `finsupp` version. -/
lemma finsupp.sum_inner {Î¹ : Type*} (l : Î¹ â†’â‚€ ğ•œ) (v : Î¹ â†’ E) (x : E) :
  âŸªl.sum (Î» (i : Î¹) (a : ğ•œ), a â€¢ v i), xâŸ«
  = l.sum (Î» (i : Î¹) (a : ğ•œ), (conj a) â€¢ âŸªv i, xâŸ«) :=
by { convert sum_inner l.support (Î» a, l a â€¢ v a) x,
     simp only [inner_smul_left, finsupp.sum, smul_eq_mul] }

/-- An inner product with a sum on the right, `finsupp` version. -/
lemma finsupp.inner_sum {Î¹ : Type*} (l : Î¹ â†’â‚€ ğ•œ) (v : Î¹ â†’ E) (x : E) :
  âŸªx, l.sum (Î» (i : Î¹) (a : ğ•œ), a â€¢ v i)âŸ« = l.sum (Î» (i : Î¹) (a : ğ•œ), a â€¢ âŸªx, v iâŸ«) :=
by { convert inner_sum l.support (Î» a, l a â€¢ v a) x,
     simp only [inner_smul_right, finsupp.sum, smul_eq_mul] }

lemma dfinsupp.sum_inner {Î¹ : Type*} [dec : decidable_eq Î¹] {Î± : Î¹ â†’ Type*}
  [Î  i, add_zero_class (Î± i)] [Î  i (x : Î± i), decidable (x â‰  0)]
  (f : Î  i, Î± i â†’ E) (l : Î â‚€ i, Î± i) (x : E) :
  âŸªl.sum f, xâŸ« = l.sum (Î» i a, âŸªf i a, xâŸ«) :=
by simp only [dfinsupp.sum, sum_inner, smul_eq_mul] {contextual := tt}

lemma dfinsupp.inner_sum {Î¹ : Type*} [dec : decidable_eq Î¹] {Î± : Î¹ â†’ Type*}
  [Î  i, add_zero_class (Î± i)] [Î  i (x : Î± i), decidable (x â‰  0)]
  (f : Î  i, Î± i â†’ E) (l : Î â‚€ i, Î± i) (x : E) :
  âŸªx, l.sum fâŸ« = l.sum (Î» i a, âŸªx, f i aâŸ«) :=
by simp only [dfinsupp.sum, inner_sum, smul_eq_mul] {contextual := tt}

@[simp] lemma inner_zero_left (x : E) : âŸª0, xâŸ« = 0 :=
by rw [â† zero_smul ğ•œ (0:E), inner_smul_left, ring_hom.map_zero, zero_mul]

lemma inner_re_zero_left (x : E) : re âŸª0, xâŸ« = 0 :=
by simp only [inner_zero_left, add_monoid_hom.map_zero]

@[simp] lemma inner_zero_right (x : E) : âŸªx, 0âŸ« = 0 :=
by rw [â†inner_conj_symm, inner_zero_left, ring_hom.map_zero]

lemma inner_re_zero_right (x : E) : re âŸªx, 0âŸ« = 0 :=
by simp only [inner_zero_right, add_monoid_hom.map_zero]

lemma inner_self_nonneg {x : E} : 0 â‰¤ re âŸªx, xâŸ« :=
inner_product_space.to_core.nonneg_re x

lemma real_inner_self_nonneg {x : F} : 0 â‰¤ âŸªx, xâŸ«_â„ := @inner_self_nonneg â„ F _ _ _ x

@[simp] lemma inner_self_re_to_K (x : E) : (re âŸªx, xâŸ« : ğ•œ) = âŸªx, xâŸ« :=
((is_R_or_C.is_real_tfae (âŸªx, xâŸ« : ğ•œ)).out 2 3).2 (inner_self_im _)

lemma inner_self_eq_norm_sq_to_K (x : E) : âŸªx, xâŸ« = (â€–xâ€– ^ 2 : ğ•œ) :=
by rw [â† inner_self_re_to_K, â† norm_sq_eq_inner, of_real_pow]

lemma inner_self_re_eq_norm (x : E) : re âŸªx, xâŸ« = â€–âŸªx, xâŸ«â€– :=
begin
  conv_rhs { rw [â†inner_self_re_to_K] },
  symmetry,
  exact norm_of_nonneg inner_self_nonneg,
end

lemma inner_self_norm_to_K (x : E) : (â€–âŸªx, xâŸ«â€– : ğ•œ) = âŸªx, xâŸ« :=
by { rw [â†inner_self_re_eq_norm], exact inner_self_re_to_K _ }

lemma real_inner_self_abs (x : F) : |âŸªx, xâŸ«_â„| = âŸªx, xâŸ«_â„ :=
@inner_self_norm_to_K â„ F _ _ _ x

@[simp] lemma inner_self_eq_zero {x : E} : âŸªx, xâŸ« = 0 â†” x = 0 :=
by rw [inner_self_eq_norm_sq_to_K, sq_eq_zero_iff, of_real_eq_zero, norm_eq_zero]

lemma inner_self_ne_zero {x : E} : âŸªx, xâŸ« â‰  0 â†” x â‰  0 :=
inner_self_eq_zero.not

@[simp] lemma inner_self_nonpos {x : E} : re âŸªx, xâŸ« â‰¤ 0 â†” x = 0 :=
by rw [â† norm_sq_eq_inner, (sq_nonneg _).le_iff_eq, sq_eq_zero_iff, norm_eq_zero]

lemma real_inner_self_nonpos {x : F} : âŸªx, xâŸ«_â„ â‰¤ 0 â†” x = 0 :=
@inner_self_nonpos â„ F _ _ _ x

lemma norm_inner_symm (x y : E) : â€–âŸªx, yâŸ«â€– = â€–âŸªy, xâŸ«â€– :=
by rw [â†inner_conj_symm, norm_conj]

@[simp] lemma inner_neg_left (x y : E) : âŸª-x, yâŸ« = -âŸªx, yâŸ« :=
by { rw [â† neg_one_smul ğ•œ x, inner_smul_left], simp }

@[simp] lemma inner_neg_right (x y : E) : âŸªx, -yâŸ« = -âŸªx, yâŸ« :=
by rw [â†inner_conj_symm, inner_neg_left]; simp only [ring_hom.map_neg, inner_conj_symm]

lemma inner_neg_neg (x y : E) : âŸª-x, -yâŸ« = âŸªx, yâŸ« := by simp

@[simp] lemma inner_self_conj (x : E) : âŸªx, xâŸ«â€  = âŸªx, xâŸ« :=
by rw [is_R_or_C.ext_iff]; exact âŸ¨by rw [conj_re], by rw [conj_im, inner_self_im, neg_zero]âŸ©

lemma inner_sub_left (x y z : E) : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_left] }

lemma inner_sub_right (x y z : E) : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_right] }

lemma inner_mul_symm_re_eq_norm (x y : E) : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = â€–âŸªx, yâŸ« * âŸªy, xâŸ«â€– :=
by { rw [â†inner_conj_symm, mul_comm], exact re_eq_norm_of_mul_conj (inner y x), }

/-- Expand `âŸªx + y, x + yâŸ«` -/
lemma inner_add_add_self (x y : E) : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_add_left, inner_add_right]; ring

/-- Expand `âŸªx + y, x + yâŸ«_â„` -/
lemma real_inner_add_add_self (x y : F) : âŸªx + y, x + yâŸ«_â„ = âŸªx, xâŸ«_â„ + 2 * âŸªx, yâŸ«_â„ + âŸªy, yâŸ«_â„ :=
begin
  have : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := by rw [â†inner_conj_symm]; refl,
  simp only [inner_add_add_self, this, add_left_inj],
  ring,
end

/- Expand `âŸªx - y, x - yâŸ«` -/
lemma inner_sub_sub_self (x y : E) : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_sub_left, inner_sub_right]; ring

/-- Expand `âŸªx - y, x - yâŸ«_â„` -/
lemma real_inner_sub_sub_self (x y : F) : âŸªx - y, x - yâŸ«_â„ = âŸªx, xâŸ«_â„ - 2 * âŸªx, yâŸ«_â„ + âŸªy, yâŸ«_â„ :=
begin
  have : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := by rw [â†inner_conj_symm]; refl,
  simp only [inner_sub_sub_self, this, add_left_inj],
  ring,
end

variable (ğ•œ)
include ğ•œ

lemma ext_inner_left {x y : E} (h : âˆ€ v, âŸªv, xâŸ« = âŸªv, yâŸ«) : x = y :=
by rw [â†sub_eq_zero, â†@inner_self_eq_zero ğ•œ, inner_sub_right, sub_eq_zero, h (x - y)]

lemma ext_inner_right {x y : E} (h : âˆ€ v, âŸªx, vâŸ« = âŸªy, vâŸ«) : x = y :=
by rw [â†sub_eq_zero, â†@inner_self_eq_zero ğ•œ, inner_sub_left, sub_eq_zero, h (x - y)]

omit ğ•œ
variable {ğ•œ}

/-- Parallelogram law -/
lemma parallelogram_law {x y : E} :
  âŸªx + y, x + yâŸ« + âŸªx - y, x - yâŸ« = 2 * (âŸªx, xâŸ« + âŸªy, yâŸ«) :=
by simp [inner_add_add_self, inner_sub_sub_self, two_mul, sub_eq_add_neg, add_comm, add_left_comm]

/-- **Cauchyâ€“Schwarz inequality**. -/
lemma inner_mul_inner_self_le (x y : E) : â€–âŸªx, yâŸ«â€– * â€–âŸªy, xâŸ«â€– â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« :=
begin
  letI c : inner_product_space.core ğ•œ E := inner_product_space.to_core,
  exact inner_product_space.core.inner_mul_inner_self_le x y
end

/-- Cauchyâ€“Schwarz inequality for real inner products. -/
lemma real_inner_mul_inner_self_le (x y : F) : âŸªx, yâŸ«_â„ * âŸªx, yâŸ«_â„ â‰¤ âŸªx, xâŸ«_â„ * âŸªy, yâŸ«_â„ :=
calc âŸªx, yâŸ«_â„ * âŸªx, yâŸ«_â„ â‰¤ â€–âŸªx, yâŸ«_â„â€– * â€–âŸªy, xâŸ«_â„â€– :
  by { rw [real_inner_comm y, â† norm_mul], exact le_abs_self _ }
... â‰¤ âŸªx, xâŸ«_â„ * âŸªy, yâŸ«_â„ : @inner_mul_inner_self_le â„ _ _ _ _ x y

/-- A family of vectors is linearly independent if they are nonzero
and orthogonal. -/
lemma linear_independent_of_ne_zero_of_inner_eq_zero {Î¹ : Type*} {v : Î¹ â†’ E}
  (hz : âˆ€ i, v i â‰  0) (ho : âˆ€ i j, i â‰  j â†’ âŸªv i, v jâŸ« = 0) : linear_independent ğ•œ v :=
begin
  rw linear_independent_iff',
  intros s g hg i hi,
  have h' : g i * inner (v i) (v i) = inner (v i) (âˆ‘ j in s, g j â€¢ v j),
  { rw inner_sum,
    symmetry,
    convert finset.sum_eq_single i _ _,
    { rw inner_smul_right },
    { intros j hj hji,
      rw [inner_smul_right, ho i j hji.symm, mul_zero] },
    { exact Î» h, false.elim (h hi) } },
  simpa [hg, hz] using h'
end

end basic_properties

section orthonormal_sets
variables {Î¹ : Type*} [dec_Î¹ : decidable_eq Î¹] (ğ•œ)

include ğ•œ

/-- An orthonormal set of vectors in an `inner_product_space` -/
def orthonormal (v : Î¹ â†’ E) : Prop :=
(âˆ€ i, â€–v iâ€– = 1) âˆ§ (âˆ€ {i j}, i â‰  j â†’ âŸªv i, v jâŸ« = 0)

omit ğ•œ

variables {ğ•œ}

include dec_Î¹
/-- `if ... then ... else` characterization of an indexed set of vectors being orthonormal.  (Inner
product equals Kronecker delta.) -/
lemma orthonormal_iff_ite {v : Î¹ â†’ E} :
  orthonormal ğ•œ v â†” âˆ€ i j, âŸªv i, v jâŸ« = if i = j then (1:ğ•œ) else (0:ğ•œ) :=
begin
  split,
  { intros hv i j,
    split_ifs,
    { simp [h, inner_self_eq_norm_sq_to_K, hv.1] },
    { exact hv.2 h } },
  { intros h,
    split,
    { intros i,
      have h' : â€–v iâ€– ^ 2 = 1 ^ 2 := by simp [@norm_sq_eq_inner ğ•œ, h i i],
      have hâ‚ : 0 â‰¤ â€–v iâ€– := norm_nonneg _,
      have hâ‚‚ : (0:â„) â‰¤ 1 := zero_le_one,
      rwa sq_eq_sq hâ‚ hâ‚‚ at h' },
    { intros i j hij,
      simpa [hij] using h i j } }
end
omit dec_Î¹

include dec_E
/-- `if ... then ... else` characterization of a set of vectors being orthonormal.  (Inner product
equals Kronecker delta.) -/
theorem orthonormal_subtype_iff_ite {s : set E} :
  orthonormal ğ•œ (coe : s â†’ E) â†”
  (âˆ€ v âˆˆ s, âˆ€ w âˆˆ s, âŸªv, wâŸ« = if v = w then 1 else 0) :=
begin
  rw orthonormal_iff_ite,
  split,
  { intros h v hv w hw,
    convert h âŸ¨v, hvâŸ© âŸ¨w, hwâŸ© using 1,
    simp },
  { rintros h âŸ¨v, hvâŸ© âŸ¨w, hwâŸ©,
    convert h v hv w hw using 1,
    simp }
end
omit dec_E

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
lemma orthonormal.inner_right_finsupp {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (l : Î¹ â†’â‚€ ğ•œ) (i : Î¹) :
  âŸªv i, finsupp.total Î¹ E ğ•œ v lâŸ« = l i :=
by classical; simp [finsupp.total_apply, finsupp.inner_sum, orthonormal_iff_ite.mp hv]

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
lemma orthonormal.inner_right_sum
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ) {s : finset Î¹} {i : Î¹} (hi : i âˆˆ s) :
  âŸªv i, âˆ‘ i in s, (l i) â€¢ (v i)âŸ« = l i :=
by classical; simp [inner_sum, inner_smul_right, orthonormal_iff_ite.mp hv, hi]

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
lemma orthonormal.inner_right_fintype [fintype Î¹]
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ) (i : Î¹) :
  âŸªv i, âˆ‘ i : Î¹, (l i) â€¢ (v i)âŸ« = l i :=
hv.inner_right_sum l (finset.mem_univ _)

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
lemma orthonormal.inner_left_finsupp {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (l : Î¹ â†’â‚€ ğ•œ) (i : Î¹) :
  âŸªfinsupp.total Î¹ E ğ•œ v l, v iâŸ« = conj (l i) :=
by rw [â† inner_conj_symm, hv.inner_right_finsupp]

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
lemma orthonormal.inner_left_sum
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ) {s : finset Î¹} {i : Î¹} (hi : i âˆˆ s) :
  âŸªâˆ‘ i in s, (l i) â€¢ (v i), v iâŸ« = conj (l i) :=
by classical; simp only
[sum_inner, inner_smul_left, orthonormal_iff_ite.mp hv, hi, mul_boole, finset.sum_ite_eq', if_true]

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
lemma orthonormal.inner_left_fintype [fintype Î¹]
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ) (i : Î¹) :
  âŸªâˆ‘ i : Î¹, (l i) â€¢ (v i), v iâŸ« = conj (l i) :=
hv.inner_left_sum l (finset.mem_univ _)

/-- The inner product of two linear combinations of a set of orthonormal vectors, expressed as
a sum over the first `finsupp`. -/
lemma orthonormal.inner_finsupp_eq_sum_left
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (lâ‚ lâ‚‚ : Î¹ â†’â‚€ ğ•œ) :
  âŸªfinsupp.total Î¹ E ğ•œ v lâ‚, finsupp.total Î¹ E ğ•œ v lâ‚‚âŸ« = lâ‚.sum (Î» i y, conj y * lâ‚‚ i) :=
by simp only [lâ‚.total_apply _, finsupp.sum_inner, hv.inner_right_finsupp, smul_eq_mul]

/-- The inner product of two linear combinations of a set of orthonormal vectors, expressed as
a sum over the second `finsupp`. -/
lemma orthonormal.inner_finsupp_eq_sum_right
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (lâ‚ lâ‚‚ : Î¹ â†’â‚€ ğ•œ) :
  âŸªfinsupp.total Î¹ E ğ•œ v lâ‚, finsupp.total Î¹ E ğ•œ v lâ‚‚âŸ« = lâ‚‚.sum (Î» i y, conj (lâ‚ i) * y) :=
by simp only [lâ‚‚.total_apply _, finsupp.inner_sum, hv.inner_left_finsupp, mul_comm, smul_eq_mul]

/-- The inner product of two linear combinations of a set of orthonormal vectors, expressed as
a sum. -/
lemma orthonormal.inner_sum
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (lâ‚ lâ‚‚ : Î¹ â†’ ğ•œ) (s : finset Î¹) :
  âŸªâˆ‘ i in s, lâ‚ i â€¢ v i, âˆ‘ i in s, lâ‚‚ i â€¢ v iâŸ« = âˆ‘ i in s, conj (lâ‚ i) * lâ‚‚ i :=
begin
  simp_rw [sum_inner, inner_smul_left],
  refine finset.sum_congr rfl (Î» i hi, _),
  rw hv.inner_right_sum lâ‚‚ hi
end

/--
The double sum of weighted inner products of pairs of vectors from an orthonormal sequence is the
sum of the weights.
-/
lemma orthonormal.inner_left_right_finset {s : finset Î¹}  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v)
  {a : Î¹ â†’ Î¹ â†’ ğ•œ} : âˆ‘ i in s, âˆ‘ j in s, (a i j) â€¢ âŸªv j, v iâŸ« = âˆ‘ k in s, a k k :=
by classical; simp [orthonormal_iff_ite.mp hv, finset.sum_ite_of_true]

/-- An orthonormal set is linearly independent. -/
lemma orthonormal.linear_independent {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) :
  linear_independent ğ•œ v :=
begin
  rw linear_independent_iff,
  intros l hl,
  ext i,
  have key : âŸªv i, finsupp.total Î¹ E ğ•œ v lâŸ« = âŸªv i, 0âŸ« := by rw hl,
  simpa only [hv.inner_right_finsupp, inner_zero_right] using key
end

/-- A subfamily of an orthonormal family (i.e., a composition with an injective map) is an
orthonormal family. -/
lemma orthonormal.comp
  {Î¹' : Type*} {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (f : Î¹' â†’ Î¹) (hf : function.injective f) :
  orthonormal ğ•œ (v âˆ˜ f) :=
begin
  classical,
  rw orthonormal_iff_ite at âŠ¢ hv,
  intros i j,
  convert hv (f i) (f j) using 1,
  simp [hf.eq_iff]
end

/-- An injective family `v : Î¹ â†’ E` is orthonormal if and only if `coe : (range v) â†’ E` is
orthonormal. -/
lemma orthonormal_subtype_range {v : Î¹ â†’ E} (hv : function.injective v) :
  orthonormal ğ•œ (coe : set.range v â†’ E) â†” orthonormal ğ•œ v :=
begin
  let f : Î¹ â‰ƒ set.range v := equiv.of_injective v hv,
  refine âŸ¨Î» h, h.comp f f.injective, Î» h, _âŸ©,
  rw â† equiv.self_comp_of_injective_symm hv,
  exact h.comp f.symm f.symm.injective,
end

/-- If `v : Î¹ â†’ E` is an orthonormal family, then `coe : (range v) â†’ E` is an orthonormal
family. -/
lemma orthonormal.to_subtype_range {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) :
  orthonormal ğ•œ (coe : set.range v â†’ E) :=
(orthonormal_subtype_range hv.linear_independent.injective).2 hv

/-- A linear combination of some subset of an orthonormal set is orthogonal to other members of the
set. -/
lemma orthonormal.inner_finsupp_eq_zero
  {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) {s : set Î¹} {i : Î¹} (hi : i âˆ‰ s) {l : Î¹ â†’â‚€ ğ•œ}
  (hl : l âˆˆ finsupp.supported ğ•œ ğ•œ s) :
  âŸªfinsupp.total Î¹ E ğ•œ v l, v iâŸ« = 0 :=
begin
  rw finsupp.mem_supported' at hl,
  simp only [hv.inner_left_finsupp, hl i hi, map_zero],
end

/-- Given an orthonormal family, a second family of vectors is orthonormal if every vector equals
the corresponding vector in the original family or its negation. -/
lemma orthonormal.orthonormal_of_forall_eq_or_eq_neg {v w : Î¹ â†’ E} (hv : orthonormal ğ•œ v)
  (hw : âˆ€ i, w i = v i âˆ¨ w i = -(v i)) : orthonormal ğ•œ w :=
begin
  classical,
  rw orthonormal_iff_ite at *,
  intros i j,
  cases hw i with hi hi; cases hw j with hj hj; split_ifs with h;
  simpa only [hi, hj, h, inner_neg_right, inner_neg_left,
              neg_neg, eq_self_iff_true, neg_eq_zero] using hv i j
end

/- The material that follows, culminating in the existence of a maximal orthonormal subset, is
adapted from the corresponding development of the theory of linearly independents sets.  See
`exists_linear_independent` in particular. -/

variables (ğ•œ E)
lemma orthonormal_empty : orthonormal ğ•œ (Î» x, x : (âˆ… : set E) â†’ E) :=
by classical; simp [orthonormal_subtype_iff_ite]
variables {ğ•œ E}

lemma orthonormal_Union_of_directed
  {Î· : Type*} {s : Î· â†’ set E} (hs : directed (âŠ†) s) (h : âˆ€ i, orthonormal ğ•œ (Î» x, x : s i â†’ E)) :
  orthonormal ğ•œ (Î» x, x : (â‹ƒ i, s i) â†’ E) :=
begin
  classical,
  rw orthonormal_subtype_iff_ite,
  rintros x âŸ¨_, âŸ¨i, rflâŸ©, hxiâŸ© y âŸ¨_, âŸ¨j, rflâŸ©, hyjâŸ©,
  obtain âŸ¨k, hik, hjkâŸ© := hs i j,
  have h_orth : orthonormal ğ•œ (Î» x, x : (s k) â†’ E) := h k,
  rw orthonormal_subtype_iff_ite at h_orth,
  exact h_orth x (hik hxi) y (hjk hyj)
end

lemma orthonormal_sUnion_of_directed
  {s : set (set E)} (hs : directed_on (âŠ†) s)
  (h : âˆ€ a âˆˆ s, orthonormal ğ•œ (Î» x, x : (a : set E) â†’ E)) :
  orthonormal ğ•œ (Î» x, x : (â‹ƒâ‚€ s) â†’ E) :=
by rw set.sUnion_eq_Union; exact orthonormal_Union_of_directed hs.directed_coe (by simpa using h)

/-- Given an orthonormal set `v` of vectors in `E`, there exists a maximal orthonormal set
containing it. -/
lemma exists_maximal_orthonormal {s : set E} (hs : orthonormal ğ•œ (coe : s â†’ E)) :
  âˆƒ w âŠ‡ s, orthonormal ğ•œ (coe : w â†’ E) âˆ§ âˆ€ u âŠ‡ w, orthonormal ğ•œ (coe : u â†’ E) â†’ u = w :=
begin
  obtain âŸ¨b, bi, sb, hâŸ© := zorn_subset_nonempty {b | orthonormal ğ•œ (coe : b â†’ E)} _ _ hs,
  { refine âŸ¨b, sb, bi, _âŸ©,
    exact Î» u hus hu, h u hu hus },
  { refine Î» c hc cc c0, âŸ¨â‹ƒâ‚€ c, _, _âŸ©,
    { exact orthonormal_sUnion_of_directed cc.directed_on (Î» x xc, hc xc) },
    { exact Î» _, set.subset_sUnion_of_mem } }
end

lemma orthonormal.ne_zero {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (i : Î¹) : v i â‰  0 :=
begin
  have : â€–v iâ€– â‰  0,
  { rw hv.1 i,
    norm_num },
  simpa using this
end

open finite_dimensional

/-- A family of orthonormal vectors with the correct cardinality forms a basis. -/
def basis_of_orthonormal_of_card_eq_finrank [fintype Î¹] [nonempty Î¹] {v : Î¹ â†’ E}
  (hv : orthonormal ğ•œ v) (card_eq : fintype.card Î¹ = finrank ğ•œ E) :
  basis Î¹ ğ•œ E :=
basis_of_linear_independent_of_card_eq_finrank hv.linear_independent card_eq

@[simp] lemma coe_basis_of_orthonormal_of_card_eq_finrank [fintype Î¹] [nonempty Î¹] {v : Î¹ â†’ E}
  (hv : orthonormal ğ•œ v) (card_eq : fintype.card Î¹ = finrank ğ•œ E) :
  (basis_of_orthonormal_of_card_eq_finrank hv card_eq : Î¹ â†’ E) = v :=
coe_basis_of_linear_independent_of_card_eq_finrank _ _

end orthonormal_sets

section norm

lemma norm_eq_sqrt_inner (x : E) : â€–xâ€– = sqrt (re âŸªx, xâŸ«) :=
calc â€–xâ€– = sqrt (â€–xâ€– ^ 2) : (sqrt_sq (norm_nonneg _)).symm
... = sqrt (re âŸªx, xâŸ«) : congr_arg _ (norm_sq_eq_inner _)

lemma norm_eq_sqrt_real_inner (x : F) : â€–xâ€– = sqrt âŸªx, xâŸ«_â„ :=
@norm_eq_sqrt_inner â„ _ _ _ _ x

lemma inner_self_eq_norm_mul_norm (x : E) : re âŸªx, xâŸ« = â€–xâ€– * â€–xâ€– :=
by rw [@norm_eq_sqrt_inner ğ•œ, â†sqrt_mul inner_self_nonneg (re âŸªx, xâŸ«),
  sqrt_mul_self inner_self_nonneg]

lemma inner_self_eq_norm_sq (x : E) : re âŸªx, xâŸ« = â€–xâ€–^2 :=
by rw [pow_two, inner_self_eq_norm_mul_norm]

lemma real_inner_self_eq_norm_mul_norm (x : F) : âŸªx, xâŸ«_â„ = â€–xâ€– * â€–xâ€– :=
by { have h := @inner_self_eq_norm_mul_norm â„ F _ _ _ x, simpa using h }

lemma real_inner_self_eq_norm_sq (x : F) : âŸªx, xâŸ«_â„ = â€–xâ€–^2 :=
by rw [pow_two, real_inner_self_eq_norm_mul_norm]

variables (ğ•œ)
/-- Expand the square -/
lemma norm_add_sq (x y : E) : â€–x + yâ€–^2 = â€–xâ€–^2 + 2 * (re âŸªx, yâŸ«) + â€–yâ€–^2 :=
begin
  repeat {rw [sq, â†@inner_self_eq_norm_mul_norm ğ•œ]},
  rw [inner_add_add_self, two_mul],
  simp only [add_assoc, add_left_inj, add_right_inj, add_monoid_hom.map_add],
  rw [â†inner_conj_symm, conj_re],
end

alias norm_add_sq â† norm_add_pow_two

/-- Expand the square -/
lemma norm_add_sq_real (x y : F) : â€–x + yâ€–^2 = â€–xâ€–^2 + 2 * âŸªx, yâŸ«_â„ + â€–yâ€–^2 :=
by { have h := @norm_add_sq â„ _ _ _ _ x y, simpa using h }

alias norm_add_sq_real â† norm_add_pow_two_real

/-- Expand the square -/
lemma norm_add_mul_self (x y : E) : â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + 2 * (re âŸªx, yâŸ«) + â€–yâ€– * â€–yâ€– :=
by { repeat {rw [â† sq]}, exact norm_add_sq _ _ }

/-- Expand the square -/
lemma norm_add_mul_self_real (x y : F) : â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + 2 * âŸªx, yâŸ«_â„ + â€–yâ€– * â€–yâ€– :=
by { have h := @norm_add_mul_self â„ _ _ _ _ x y, simpa using h }

/-- Expand the square -/
lemma norm_sub_sq (x y : E) : â€–x - yâ€–^2 = â€–xâ€–^2 - 2 * (re âŸªx, yâŸ«) + â€–yâ€–^2 :=
by rw [sub_eq_add_neg, @norm_add_sq ğ•œ _ _ _ _ x (-y), norm_neg, inner_neg_right, map_neg, mul_neg,
  sub_eq_add_neg]

alias norm_sub_sq â† norm_sub_pow_two

/-- Expand the square -/
lemma norm_sub_sq_real (x y : F) : â€–x - yâ€–^2 = â€–xâ€–^2 - 2 * âŸªx, yâŸ«_â„ + â€–yâ€–^2 :=
@norm_sub_sq â„ _ _ _ _ _ _

alias norm_sub_sq_real â† norm_sub_pow_two_real

/-- Expand the square -/
lemma norm_sub_mul_self (x y : E) : â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– - 2 * re âŸªx, yâŸ« + â€–yâ€– * â€–yâ€– :=
by { repeat {rw [â† sq]}, exact norm_sub_sq _ _ }

/-- Expand the square -/
lemma norm_sub_mul_self_real (x y : F) : â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– - 2 * âŸªx, yâŸ«_â„ + â€–yâ€– * â€–yâ€– :=
by { have h := @norm_sub_mul_self â„ _ _ _ _ x y, simpa using h }

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma norm_inner_le_norm (x y : E) : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– :=
begin
  rw [norm_eq_sqrt_inner x, norm_eq_sqrt_inner y],
  letI : inner_product_space.core ğ•œ E := inner_product_space.to_core,
  exact inner_product_space.core.norm_inner_le_norm x y
end

lemma nnnorm_inner_le_nnnorm (x y : E) : â€–âŸªx, yâŸ«â€–â‚Š â‰¤ â€–xâ€–â‚Š * â€–yâ€–â‚Š :=
norm_inner_le_norm x y

lemma re_inner_le_norm (x y : E) : re âŸªx, yâŸ« â‰¤ â€–xâ€– * â€–yâ€– :=
le_trans (re_le_norm (inner x y)) (norm_inner_le_norm x y)

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma abs_real_inner_le_norm (x y : F) : |âŸªx, yâŸ«_â„| â‰¤ â€–xâ€– * â€–yâ€– :=
(real.norm_eq_abs _).ge.trans (norm_inner_le_norm x y)

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma real_inner_le_norm (x y : F) : âŸªx, yâŸ«_â„ â‰¤ â€–xâ€– * â€–yâ€– :=
le_trans (le_abs_self _) (abs_real_inner_le_norm _ _)

include ğ•œ
variables (ğ•œ)
lemma parallelogram_law_with_norm (x y : E) :
  â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–) :=
begin
  simp only [â† @inner_self_eq_norm_mul_norm ğ•œ],
  rw [â† re.map_add, parallelogram_law, two_mul, two_mul],
  simp only [re.map_add],
end

lemma parallelogram_law_with_nnnorm (x y : E) :
  â€–x + yâ€–â‚Š * â€–x + yâ€–â‚Š + â€–x - yâ€–â‚Š * â€–x - yâ€–â‚Š = 2 * (â€–xâ€–â‚Š * â€–xâ€–â‚Š + â€–yâ€–â‚Š * â€–yâ€–â‚Š) :=
subtype.ext $ parallelogram_law_with_norm ğ•œ x y
variables {ğ•œ}
omit ğ•œ

/-- Polarization identity: The real part of the  inner product, in terms of the norm. -/
lemma re_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two (x y : E) :
  re âŸªx, yâŸ« = (â€–x + yâ€– * â€–x + yâ€– - â€–xâ€– * â€–xâ€– - â€–yâ€– * â€–yâ€–) / 2 :=
by { rw @norm_add_mul_self ğ•œ, ring }

/-- Polarization identity: The real part of the  inner product, in terms of the norm. -/
lemma re_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two (x y : E) :
  re âŸªx, yâŸ« = (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– - â€–x - yâ€– * â€–x - yâ€–) / 2 :=
by { rw [@norm_sub_mul_self ğ•œ], ring }

/-- Polarization identity: The real part of the  inner product, in terms of the norm. -/
lemma re_inner_eq_norm_add_mul_self_sub_norm_sub_mul_self_div_four (x y : E) :
  re âŸªx, yâŸ« = (â€–x + yâ€– * â€–x + yâ€– - â€–x - yâ€– * â€–x - yâ€–) / 4 :=
by { rw [@norm_add_mul_self ğ•œ, @norm_sub_mul_self ğ•œ], ring }

/-- Polarization identity: The imaginary part of the inner product, in terms of the norm. -/
lemma im_inner_eq_norm_sub_I_smul_mul_self_sub_norm_add_I_smul_mul_self_div_four (x y : E) :
  im âŸªx, yâŸ« = (â€–x - IK â€¢ yâ€– * â€–x - IK â€¢ yâ€– - â€–x + IK â€¢ yâ€– * â€–x + IK â€¢ yâ€–) / 4 :=
by { simp only [@norm_add_mul_self ğ•œ, @norm_sub_mul_self ğ•œ, inner_smul_right, I_mul_re], ring }

/-- Polarization identity: The inner product, in terms of the norm. -/
lemma inner_eq_sum_norm_sq_div_four (x y : E) :
  âŸªx, yâŸ« = (â€–x + yâ€– ^ 2 - â€–x - yâ€– ^ 2 + (â€–x - IK â€¢ yâ€– ^ 2 - â€–x + IK â€¢ yâ€– ^ 2) * IK) / 4 :=
begin
  rw [â† re_add_im âŸªx, yâŸ«, re_inner_eq_norm_add_mul_self_sub_norm_sub_mul_self_div_four,
    im_inner_eq_norm_sub_I_smul_mul_self_sub_norm_add_I_smul_mul_self_div_four],
  push_cast,
  simp only [sq, â† mul_div_right_comm, â† add_div]
end

/-- Formula for the distance between the images of two nonzero points under an inversion with center
zero. See also `euclidean_geometry.dist_inversion_inversion` for inversions around a general
point. -/
lemma dist_div_norm_sq_smul {x y : F} (hx : x â‰  0) (hy : y â‰  0) (R : â„) :
  dist ((R / â€–xâ€–) ^ 2 â€¢ x) ((R / â€–yâ€–) ^ 2 â€¢ y) = (R ^ 2 / (â€–xâ€– * â€–yâ€–)) * dist x y :=
have hx' : â€–xâ€– â‰  0, from norm_ne_zero_iff.2 hx,
have hy' : â€–yâ€– â‰  0, from norm_ne_zero_iff.2 hy,
calc dist ((R / â€–xâ€–) ^ 2 â€¢ x) ((R / â€–yâ€–) ^ 2 â€¢ y)
    = sqrt (â€–(R / â€–xâ€–) ^ 2 â€¢ x - (R / â€–yâ€–) ^ 2 â€¢ yâ€–^2) :
  by rw [dist_eq_norm, sqrt_sq (norm_nonneg _)]
... = sqrt ((R ^ 2 / (â€–xâ€– * â€–yâ€–)) ^ 2 * â€–x - yâ€– ^ 2) :
  congr_arg sqrt $ by { field_simp [sq, norm_sub_mul_self_real, norm_smul, real_inner_smul_left,
    inner_smul_right, real.norm_of_nonneg (mul_self_nonneg _)], ring }
... = (R ^ 2 / (â€–xâ€– * â€–yâ€–)) * dist x y :
  by rw [sqrt_mul (sq_nonneg _), sqrt_sq (norm_nonneg _),
    sqrt_sq (div_nonneg (sq_nonneg _) (mul_nonneg (norm_nonneg _) (norm_nonneg _))), dist_eq_norm]

@[priority 100] -- See note [lower instance priority]
instance inner_product_space.to_uniform_convex_space : uniform_convex_space F :=
âŸ¨Î» Îµ hÎµ, begin
  refine âŸ¨2 - sqrt (4 - Îµ^2), sub_pos_of_lt $ (sqrt_lt' zero_lt_two).2 _, Î» x hx y hy hxy, _âŸ©,
  { norm_num,
    exact pow_pos hÎµ _ },
  rw sub_sub_cancel,
  refine le_sqrt_of_sq_le _,
  rw [sq, eq_sub_iff_add_eq.2 (parallelogram_law_with_norm â„ x y), â†sq (â€–x - yâ€–), hx, hy],
  norm_num,
  exact pow_le_pow_of_le_left hÎµ.le hxy _,
endâŸ©

section complex

variables {V : Type*} [normed_add_comm_group V] [inner_product_space â„‚ V]

/--
A complex polarization identity, with a linear map
-/
lemma inner_map_polarization (T : V â†’â‚—[â„‚] V) (x y : V):
  âŸª T y, x âŸ«_â„‚ = (âŸªT (x + y) , x + yâŸ«_â„‚ - âŸªT (x - y) , x - yâŸ«_â„‚ +
    complex.I * âŸªT (x + complex.I â€¢ y) , x + complex.I â€¢ yâŸ«_â„‚ -
    complex.I * âŸªT (x - complex.I â€¢ y), x - complex.I â€¢ y âŸ«_â„‚) / 4 :=
begin
  simp only [map_add, map_sub, inner_add_left, inner_add_right, linear_map.map_smul,
             inner_smul_left, inner_smul_right, complex.conj_I, â†pow_two, complex.I_sq,
             inner_sub_left, inner_sub_right, mul_add, â†mul_assoc, mul_neg, neg_neg,
             sub_neg_eq_add, one_mul, neg_one_mul, mul_sub, sub_sub],
  ring,
end

lemma inner_map_polarization' (T : V â†’â‚—[â„‚] V) (x y : V):
  âŸª T x, y âŸ«_â„‚ = (âŸªT (x + y) , x + yâŸ«_â„‚ - âŸªT (x - y) , x - yâŸ«_â„‚ -
    complex.I * âŸªT (x + complex.I â€¢ y) , x + complex.I â€¢ yâŸ«_â„‚ +
    complex.I * âŸªT (x - complex.I â€¢ y), x - complex.I â€¢ y âŸ«_â„‚) / 4 :=
begin
  simp only [map_add, map_sub, inner_add_left, inner_add_right, linear_map.map_smul,
             inner_smul_left, inner_smul_right, complex.conj_I, â†pow_two, complex.I_sq,
             inner_sub_left, inner_sub_right, mul_add, â†mul_assoc, mul_neg, neg_neg,
             sub_neg_eq_add, one_mul, neg_one_mul, mul_sub, sub_sub],
  ring,
end

/--
A linear map `T` is zero, if and only if the identity `âŸªT x, xâŸ«_â„‚ = 0` holds for all `x`.
-/
lemma inner_map_self_eq_zero (T : V â†’â‚—[â„‚] V) :
  (âˆ€ (x : V), âŸªT x, xâŸ«_â„‚ = 0) â†” T = 0 :=
begin
  split,
  { intro hT,
    ext x,
    simp only [linear_map.zero_apply, â† @inner_self_eq_zero â„‚, inner_map_polarization, hT],
    norm_num },
  { rintro rfl x,
    simp only [linear_map.zero_apply, inner_zero_left] }
end

/--
Two linear maps `S` and `T` are equal, if and only if the identity `âŸªS x, xâŸ«_â„‚ = âŸªT x, xâŸ«_â„‚` holds
for all `x`.
-/
lemma ext_inner_map (S T : V â†’â‚—[â„‚] V) :
  (âˆ€ (x : V), âŸªS x, xâŸ«_â„‚ = âŸªT x, xâŸ«_â„‚) â†” S = T :=
begin
  rw [â†sub_eq_zero, â†inner_map_self_eq_zero],
  refine forall_congr (Î» x, _),
  rw [linear_map.sub_apply, inner_sub_left, sub_eq_zero],
end

end complex

section

variables {Î¹ : Type*} {Î¹' : Type*} {Î¹'' : Type*}
variables {E' : Type*} [normed_add_comm_group E'] [inner_product_space ğ•œ E']
variables {E'' : Type*} [normed_add_comm_group E''] [inner_product_space ğ•œ E'']

/-- A linear isometry preserves the inner product. -/
@[simp] lemma linear_isometry.inner_map_map (f : E â†’â‚—áµ¢[ğ•œ] E') (x y : E) : âŸªf x, f yâŸ« = âŸªx, yâŸ« :=
by simp [inner_eq_sum_norm_sq_div_four, â† f.norm_map]

/-- A linear isometric equivalence preserves the inner product. -/
@[simp] lemma linear_isometry_equiv.inner_map_map (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (x y : E) :
  âŸªf x, f yâŸ« = âŸªx, yâŸ« :=
f.to_linear_isometry.inner_map_map x y

/-- A linear map that preserves the inner product is a linear isometry. -/
def linear_map.isometry_of_inner (f : E â†’â‚—[ğ•œ] E') (h : âˆ€ x y, âŸªf x, f yâŸ« = âŸªx, yâŸ«) : E â†’â‚—áµ¢[ğ•œ] E' :=
âŸ¨f, Î» x, by simp only [@norm_eq_sqrt_inner ğ•œ, h]âŸ©

@[simp] lemma linear_map.coe_isometry_of_inner (f : E â†’â‚—[ğ•œ] E') (h) :
  â‡‘(f.isometry_of_inner h) = f := rfl

@[simp] lemma linear_map.isometry_of_inner_to_linear_map (f : E â†’â‚—[ğ•œ] E') (h) :
  (f.isometry_of_inner h).to_linear_map = f := rfl

/-- A linear equivalence that preserves the inner product is a linear isometric equivalence. -/
def linear_equiv.isometry_of_inner (f : E â‰ƒâ‚—[ğ•œ] E') (h : âˆ€ x y, âŸªf x, f yâŸ« = âŸªx, yâŸ«) :
  E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
âŸ¨f, ((f : E â†’â‚—[ğ•œ] E').isometry_of_inner h).norm_mapâŸ©

@[simp] lemma linear_equiv.coe_isometry_of_inner (f : E â‰ƒâ‚—[ğ•œ] E') (h) :
  â‡‘(f.isometry_of_inner h) = f := rfl

@[simp] lemma linear_equiv.isometry_of_inner_to_linear_equiv (f : E â‰ƒâ‚—[ğ•œ] E') (h) :
  (f.isometry_of_inner h).to_linear_equiv = f := rfl

/-- A linear isometry preserves the property of being orthonormal. -/
lemma linear_isometry.orthonormal_comp_iff {v : Î¹ â†’ E} (f : E â†’â‚—áµ¢[ğ•œ] E') :
  orthonormal ğ•œ (f âˆ˜ v) â†” orthonormal ğ•œ v :=
begin
  classical,
  simp_rw [orthonormal_iff_ite, linear_isometry.inner_map_map]
end

/-- A linear isometry preserves the property of being orthonormal. -/
lemma orthonormal.comp_linear_isometry {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (f : E â†’â‚—áµ¢[ğ•œ] E') :
  orthonormal ğ•œ (f âˆ˜ v) :=
by rwa f.orthonormal_comp_iff

/-- A linear isometric equivalence preserves the property of being orthonormal. -/
lemma orthonormal.comp_linear_isometry_equiv {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') :
  orthonormal ğ•œ (f âˆ˜ v) :=
hv.comp_linear_isometry f.to_linear_isometry

/-- A linear isometric equivalence, applied with `basis.map`, preserves the property of being
orthonormal. -/
lemma orthonormal.map_linear_isometry_equiv {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v)
  (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') : orthonormal ğ•œ (v.map f.to_linear_equiv) :=
hv.comp_linear_isometry_equiv f

/-- A linear map that sends an orthonormal basis to orthonormal vectors is a linear isometry. -/
def linear_map.isometry_of_orthonormal (f : E â†’â‚—[ğ•œ] E') {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v)
  (hf : orthonormal ğ•œ (f âˆ˜ v)) : E â†’â‚—áµ¢[ğ•œ] E' :=
f.isometry_of_inner $ Î» x y, by rw [â†v.total_repr x, â†v.total_repr y, finsupp.apply_total,
                                    finsupp.apply_total, hv.inner_finsupp_eq_sum_left,
                                    hf.inner_finsupp_eq_sum_left]

@[simp] lemma linear_map.coe_isometry_of_orthonormal (f : E â†’â‚—[ğ•œ] E') {v : basis Î¹ ğ•œ E}
  (hv : orthonormal ğ•œ v) (hf : orthonormal ğ•œ (f âˆ˜ v)) :
  â‡‘(f.isometry_of_orthonormal hv hf) = f :=
rfl

@[simp] lemma linear_map.isometry_of_orthonormal_to_linear_map (f : E â†’â‚—[ğ•œ] E') {v : basis Î¹ ğ•œ E}
  (hv : orthonormal ğ•œ v) (hf : orthonormal ğ•œ (f âˆ˜ v)) :
  (f.isometry_of_orthonormal hv hf).to_linear_map = f :=
rfl

/-- A linear equivalence that sends an orthonormal basis to orthonormal vectors is a linear
isometric equivalence. -/
def linear_equiv.isometry_of_orthonormal (f : E â‰ƒâ‚—[ğ•œ] E') {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v)
  (hf : orthonormal ğ•œ (f âˆ˜ v)) : E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
f.isometry_of_inner $ Î» x y, begin
  rw â†linear_equiv.coe_coe at hf,
  rw [â†v.total_repr x, â†v.total_repr y, â†linear_equiv.coe_coe, finsupp.apply_total,
      finsupp.apply_total, hv.inner_finsupp_eq_sum_left, hf.inner_finsupp_eq_sum_left]
end

@[simp] lemma linear_equiv.coe_isometry_of_orthonormal (f : E â‰ƒâ‚—[ğ•œ] E') {v : basis Î¹ ğ•œ E}
  (hv : orthonormal ğ•œ v) (hf : orthonormal ğ•œ (f âˆ˜ v)) :
  â‡‘(f.isometry_of_orthonormal hv hf) = f :=
rfl

@[simp] lemma linear_equiv.isometry_of_orthonormal_to_linear_equiv (f : E â‰ƒâ‚—[ğ•œ] E')
  {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v) (hf : orthonormal ğ•œ (f âˆ˜ v)) :
  (f.isometry_of_orthonormal hv hf).to_linear_equiv = f :=
rfl

/-- A linear isometric equivalence that sends an orthonormal basis to a given orthonormal basis. -/
def orthonormal.equiv {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v) {v' : basis Î¹' ğ•œ E'}
  (hv' : orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') : E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
(v.equiv v' e).isometry_of_orthonormal hv begin
  have h : (v.equiv v' e) âˆ˜ v = v' âˆ˜ e,
  { ext i,
    simp },
  rw h,
  exact hv'.comp _ e.injective
end

@[simp] lemma orthonormal.equiv_to_linear_equiv {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v)
  {v' : basis Î¹' ğ•œ E'} (hv' : orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') :
  (hv.equiv hv' e).to_linear_equiv = v.equiv v' e :=
rfl

@[simp] lemma orthonormal.equiv_apply {Î¹' : Type*} {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v)
  {v' : basis Î¹' ğ•œ E'} (hv' : orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') (i : Î¹) :
  hv.equiv hv' e (v i) = v' (e i) :=
basis.equiv_apply _ _ _ _

@[simp] lemma orthonormal.equiv_refl {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v) :
  hv.equiv hv (equiv.refl Î¹) = linear_isometry_equiv.refl ğ•œ E :=
v.ext_linear_isometry_equiv $ Î» i,
  by simp only [orthonormal.equiv_apply, equiv.coe_refl, id.def, linear_isometry_equiv.coe_refl]

@[simp] lemma orthonormal.equiv_symm {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v) {v' : basis Î¹' ğ•œ E'}
  (hv' : orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') : (hv.equiv hv' e).symm = hv'.equiv hv e.symm :=
v'.ext_linear_isometry_equiv $ Î» i, (hv.equiv hv' e).injective $
  by simp only [linear_isometry_equiv.apply_symm_apply, orthonormal.equiv_apply, e.apply_symm_apply]

@[simp] lemma orthonormal.equiv_trans {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v) {v' : basis Î¹' ğ•œ E'}
  (hv' : orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') {v'' : basis Î¹'' ğ•œ E''} (hv'' : orthonormal ğ•œ v'')
  (e' : Î¹' â‰ƒ Î¹'') : (hv.equiv hv' e).trans (hv'.equiv hv'' e') = hv.equiv hv'' (e.trans e') :=
v.ext_linear_isometry_equiv $ Î» i,
  by simp only [linear_isometry_equiv.trans_apply, orthonormal.equiv_apply, e.coe_trans]

lemma orthonormal.map_equiv {v : basis Î¹ ğ•œ E} (hv : orthonormal ğ•œ v) {v' : basis Î¹' ğ•œ E'}
  (hv' : orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') :
  v.map ((hv.equiv hv' e).to_linear_equiv) = v'.reindex e.symm :=
v.map_equiv _ _

end

/-- Polarization identity: The real inner product, in terms of the norm. -/
lemma real_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two (x y : F) :
  âŸªx, yâŸ«_â„ = (â€–x + yâ€– * â€–x + yâ€– - â€–xâ€– * â€–xâ€– - â€–yâ€– * â€–yâ€–) / 2 :=
re_to_real.symm.trans $
  re_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two x y

/-- Polarization identity: The real inner product, in terms of the norm. -/
lemma real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two (x y : F) :
  âŸªx, yâŸ«_â„ = (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– - â€–x - yâ€– * â€–x - yâ€–) / 2 :=
re_to_real.symm.trans $
  re_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two x y

/-- Pythagorean theorem, if-and-only-if vector inner product form. -/
lemma norm_add_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero (x y : F) :
  â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– â†” âŸªx, yâŸ«_â„ = 0 :=
begin
  rw [@norm_add_mul_self â„, add_right_cancel_iff, add_right_eq_self, mul_eq_zero],
  norm_num
end

/-- Pythagorean theorem, if-and-if vector inner product form using square roots. -/
lemma norm_add_eq_sqrt_iff_real_inner_eq_zero {x y : F} :
  â€–x + yâ€– = sqrt (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–) â†” âŸªx, yâŸ«_â„ = 0 :=
by rw [â†norm_add_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero, eq_comm,
  sqrt_eq_iff_mul_self_eq (add_nonneg (mul_self_nonneg _) (mul_self_nonneg _)) (norm_nonneg _)]

/-- Pythagorean theorem, vector inner product form. -/
lemma norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (x y : E) (h : âŸªx, yâŸ« = 0) :
  â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– :=
begin
  rw [@norm_add_mul_self ğ•œ, add_right_cancel_iff, add_right_eq_self, mul_eq_zero],
  apply or.inr,
  simp only [h, zero_re'],
end

/-- Pythagorean theorem, vector inner product form. -/
lemma norm_add_sq_eq_norm_sq_add_norm_sq_real {x y : F} (h : âŸªx, yâŸ«_â„ = 0) :
  â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– :=
(norm_add_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero x y).2 h

/-- Pythagorean theorem, subtracting vectors, if-and-only-if vector
inner product form. -/
lemma norm_sub_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero (x y : F) :
  â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– â†” âŸªx, yâŸ«_â„ = 0 :=
begin
  rw [@norm_sub_mul_self â„, add_right_cancel_iff, sub_eq_add_neg, add_right_eq_self, neg_eq_zero,
      mul_eq_zero],
  norm_num
end

/-- Pythagorean theorem, subtracting vectors, if-and-if vector inner product form using square
roots. -/
lemma norm_sub_eq_sqrt_iff_real_inner_eq_zero {x y : F} :
  â€–x - yâ€– = sqrt (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–) â†” âŸªx, yâŸ«_â„ = 0 :=
by rw [â†norm_sub_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero, eq_comm,
  sqrt_eq_iff_mul_self_eq (add_nonneg (mul_self_nonneg _) (mul_self_nonneg _)) (norm_nonneg _)]

/-- Pythagorean theorem, subtracting vectors, vector inner product
form. -/
lemma norm_sub_sq_eq_norm_sq_add_norm_sq_real {x y : F} (h : âŸªx, yâŸ«_â„ = 0) :
  â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– :=
(norm_sub_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero x y).2 h

/-- The sum and difference of two vectors are orthogonal if and only
if they have the same norm. -/
lemma real_inner_add_sub_eq_zero_iff (x y : F) : âŸªx + y, x - yâŸ«_â„ = 0 â†” â€–xâ€– = â€–yâ€– :=
begin
  conv_rhs { rw â†mul_self_inj_of_nonneg (norm_nonneg _) (norm_nonneg _) },
  simp only [â†@inner_self_eq_norm_mul_norm â„, inner_add_left, inner_sub_right,
            real_inner_comm y x, sub_eq_zero, re_to_real],
  split,
  { intro h,
    rw [add_comm] at h,
    linarith },
  { intro h,
    linarith }
end

/-- Given two orthogonal vectors, their sum and difference have equal norms. -/
lemma norm_sub_eq_norm_add {v w : E} (h : âŸªv, wâŸ« = 0) : â€–w - vâ€– = â€–w + vâ€– :=
begin
  rw â†mul_self_inj_of_nonneg (norm_nonneg _) (norm_nonneg _),
  simp only [h, â†@inner_self_eq_norm_mul_norm ğ•œ, sub_neg_eq_add, sub_zero, map_sub, zero_re',
    zero_sub,
    add_zero, map_add, inner_add_right, inner_sub_left, inner_sub_right, inner_re_symm, zero_add]
end

/-- The real inner product of two vectors, divided by the product of their
norms, has absolute value at most 1. -/
lemma abs_real_inner_div_norm_mul_norm_le_one (x y : F) : |âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–)| â‰¤ 1 :=
begin
  rw [abs_div, abs_mul, abs_norm, abs_norm],
  exact div_le_one_of_le (abs_real_inner_le_norm x y) (by positivity)
end

/-- The inner product of a vector with a multiple of itself. -/
lemma real_inner_smul_self_left (x : F) (r : â„) : âŸªr â€¢ x, xâŸ«_â„ = r * (â€–xâ€– * â€–xâ€–) :=
by rw [real_inner_smul_left, â†real_inner_self_eq_norm_mul_norm]

/-- The inner product of a vector with a multiple of itself. -/
lemma real_inner_smul_self_right (x : F) (r : â„) : âŸªx, r â€¢ xâŸ«_â„ = r * (â€–xâ€– * â€–xâ€–) :=
by rw [inner_smul_right, â†real_inner_self_eq_norm_mul_norm]

/-- The inner product of a nonzero vector with a nonzero multiple of
itself, divided by the product of their norms, has absolute value
1. -/
lemma norm_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul
  {x : E} {r : ğ•œ} (hx : x â‰  0) (hr : r â‰  0) : â€–âŸªx, r â€¢ xâŸ«â€– / (â€–xâ€– * â€–r â€¢ xâ€–) = 1 :=
begin
  have hx' : â€–xâ€– â‰  0 := by simp [hx],
  have hr' : â€–râ€– â‰  0 := by simp [hr],
  rw [inner_smul_right, norm_mul, â† inner_self_re_eq_norm, inner_self_eq_norm_mul_norm, norm_smul],
  rw [â† mul_assoc, â† div_div, mul_div_cancel _ hx',
     â† div_div, mul_comm, mul_div_cancel _ hr', div_self hx'],
end

/-- The inner product of a nonzero vector with a nonzero multiple of
itself, divided by the product of their norms, has absolute value
1. -/
lemma abs_real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul
  {x : F} {r : â„} (hx : x â‰  0) (hr : r â‰  0) : |âŸªx, r â€¢ xâŸ«_â„| / (â€–xâ€– * â€–r â€¢ xâ€–) = 1 :=
norm_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul hx hr

/-- The inner product of a nonzero vector with a positive multiple of
itself, divided by the product of their norms, has value 1. -/
lemma real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul
  {x : F} {r : â„} (hx : x â‰  0) (hr : 0 < r) : âŸªx, r â€¢ xâŸ«_â„ / (â€–xâ€– * â€–r â€¢ xâ€–) = 1 :=
begin
  rw [real_inner_smul_self_right, norm_smul, real.norm_eq_abs, â†mul_assoc â€–xâ€–, mul_comm _ (|r|),
      mul_assoc, abs_of_nonneg hr.le, div_self],
  exact mul_ne_zero hr.ne' (mul_self_ne_zero.2 (norm_ne_zero_iff.2 hx))
end

/-- The inner product of a nonzero vector with a negative multiple of
itself, divided by the product of their norms, has value -1. -/
lemma real_inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul
  {x : F} {r : â„} (hx : x â‰  0) (hr : r < 0) : âŸªx, r â€¢ xâŸ«_â„ / (â€–xâ€– * â€–r â€¢ xâ€–) = -1 :=
begin
  rw [real_inner_smul_self_right, norm_smul, real.norm_eq_abs, â†mul_assoc â€–xâ€–, mul_comm _ (|r|),
      mul_assoc, abs_of_neg hr, neg_mul, div_neg_eq_neg_div, div_self],
  exact mul_ne_zero hr.ne (mul_self_ne_zero.2 (norm_ne_zero_iff.2 hx))
end

lemma norm_inner_eq_norm_tfae (x y : E) :
  tfae [â€–âŸªx, yâŸ«â€– = â€–xâ€– * â€–yâ€–,
    x = 0 âˆ¨ y = (âŸªx, yâŸ« / âŸªx, xâŸ«) â€¢ x,
    x = 0 âˆ¨ âˆƒ r : ğ•œ, y = r â€¢ x,
    x = 0 âˆ¨ y âˆˆ ğ•œ âˆ™ x] :=
begin
  tfae_have : 1 â†’ 2,
  { refine Î» h, or_iff_not_imp_left.2 (Î» hxâ‚€, _),
    have : â€–xâ€– ^ 2 â‰  0 := pow_ne_zero _ (norm_ne_zero_iff.2 hxâ‚€),
    rw [â† sq_eq_sq (norm_nonneg _) (mul_nonneg (norm_nonneg _) (norm_nonneg _)),
      mul_pow, â† mul_right_inj' this, eq_comm, â† sub_eq_zero, â† mul_sub] at h,
    simp only [@norm_sq_eq_inner ğ•œ] at h,
    letI : inner_product_space.core ğ•œ E := inner_product_space.to_core,
    erw [â† inner_product_space.core.cauchy_schwarz_aux,
      inner_product_space.core.norm_sq_eq_zero, sub_eq_zero] at h,
    rw [div_eq_inv_mul, mul_smul, h, inv_smul_smulâ‚€],
    rwa [inner_self_ne_zero] },
  tfae_have : 2 â†’ 3, from Î» h, h.imp_right (Î» h', âŸ¨_, h'âŸ©),
  tfae_have : 3 â†’ 1,
  { rintro (rfl | âŸ¨r, rflâŸ©); simp [inner_smul_right, norm_smul, inner_self_eq_norm_sq_to_K,
      inner_self_eq_norm_mul_norm, sq, mul_left_comm] },
  tfae_have : 3 â†” 4, by simp only [submodule.mem_span_singleton, eq_comm],
  tfae_finish
end

/--
If the inner product of two vectors is equal to the product of their norms, then the two vectors
are multiples of each other. One form of the equality case for Cauchy-Schwarz.
Compare `inner_eq_norm_mul_iff`, which takes the stronger hypothesis `âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`. -/
lemma norm_inner_eq_norm_iff {x y : E} (hxâ‚€ : x â‰  0) (hyâ‚€ : y â‰  0) :
  â€–âŸªx, yâŸ«â€– = â€–xâ€– * â€–yâ€– â†” âˆƒ (r : ğ•œ), r â‰  0 âˆ§ y = r â€¢ x :=
calc â€–âŸªx, yâŸ«â€– = â€–xâ€– * â€–yâ€– â†” x = 0 âˆ¨ âˆƒ r : ğ•œ, y = r â€¢ x :
  (@norm_inner_eq_norm_tfae ğ•œ _ _ _ _ x y).out 0 2
... â†” âˆƒ r : ğ•œ, y = r â€¢ x : or_iff_right hxâ‚€
... â†” âˆƒ r : ğ•œ, r â‰  0 âˆ§ y = r â€¢ x :
  âŸ¨Î» âŸ¨r, hâŸ©, âŸ¨r, Î» hrâ‚€, hyâ‚€ $ h.symm â–¸ smul_eq_zero.2 $ or.inl hrâ‚€, hâŸ©, Î» âŸ¨r, hrâ‚€, hâŸ©, âŸ¨r, hâŸ©âŸ©

/-- The inner product of two vectors, divided by the product of their
norms, has absolute value 1 if and only if they are nonzero and one is
a multiple of the other. One form of equality case for Cauchy-Schwarz. -/
lemma norm_inner_div_norm_mul_norm_eq_one_iff (x y : E) :
  â€–(âŸªx, yâŸ« / (â€–xâ€– * â€–yâ€–))â€– = 1 â†” (x â‰  0 âˆ§ âˆƒ (r : ğ•œ), r â‰  0 âˆ§ y = r â€¢ x) :=
begin
  split,
  { intro h,
    have hxâ‚€ : x â‰  0 := Î» hâ‚€, by simpa [hâ‚€] using h,
    have hyâ‚€ : y â‰  0 := Î» hâ‚€, by simpa [hâ‚€] using h,
    refine âŸ¨hxâ‚€, (norm_inner_eq_norm_iff hxâ‚€ hyâ‚€).1 $ eq_of_div_eq_one _âŸ©,
    simpa using h },
  { rintro âŸ¨hx, âŸ¨r, âŸ¨hr, rflâŸ©âŸ©âŸ©,
    simp only [norm_div, norm_mul, norm_of_real, abs_norm],
    exact norm_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul hx hr }
end

/-- The inner product of two vectors, divided by the product of their
norms, has absolute value 1 if and only if they are nonzero and one is
a multiple of the other. One form of equality case for Cauchy-Schwarz. -/
lemma abs_real_inner_div_norm_mul_norm_eq_one_iff (x y : F) :
  |âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–)| = 1 â†” (x â‰  0 âˆ§ âˆƒ (r : â„), r â‰  0 âˆ§ y = r â€¢ x) :=
@norm_inner_div_norm_mul_norm_eq_one_iff â„ F _ _ _ x y

lemma inner_eq_norm_mul_iff_div {x y : E} (hâ‚€ : x â‰  0) :
  âŸªx, yâŸ« = (â€–xâ€– : ğ•œ) * â€–yâ€– â†” (â€–yâ€– / â€–xâ€– : ğ•œ) â€¢ x = y :=
begin
  have hâ‚€' := hâ‚€,
  rw [â† norm_ne_zero_iff, ne.def, â† @of_real_eq_zero ğ•œ] at hâ‚€',
  split; intro h,
  { have : x = 0 âˆ¨ y = (âŸªx, yâŸ« / âŸªx, xâŸ« : ğ•œ) â€¢ x :=
      ((@norm_inner_eq_norm_tfae ğ•œ _ _ _ _ x y).out 0 1).1 (by simp [h]),
    rw [this.resolve_left hâ‚€, h],
    simp [norm_smul, inner_self_norm_to_K, hâ‚€'] },
  { conv_lhs { rw [â† h, inner_smul_right, inner_self_eq_norm_sq_to_K] },
    field_simp [sq, mul_left_comm] }
end

/-- If the inner product of two vectors is equal to the product of their norms (i.e.,
`âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`), then the two vectors are nonnegative real multiples of each other. One form
of the equality case for Cauchy-Schwarz.
Compare `norm_inner_eq_norm_iff`, which takes the weaker hypothesis `abs âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`. -/
lemma inner_eq_norm_mul_iff {x y : E} :
  âŸªx, yâŸ« = (â€–xâ€– : ğ•œ) * â€–yâ€– â†” (â€–yâ€– : ğ•œ) â€¢ x = (â€–xâ€– : ğ•œ) â€¢ y :=
begin
  rcases eq_or_ne x 0 with (rfl | hâ‚€),
  { simp },
  { rw [inner_eq_norm_mul_iff_div hâ‚€, div_eq_inv_mul, mul_smul, inv_smul_eq_iffâ‚€],
    rwa [ne.def, of_real_eq_zero, norm_eq_zero] },
end

/-- If the inner product of two vectors is equal to the product of their norms (i.e.,
`âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`), then the two vectors are nonnegative real multiples of each other. One form
of the equality case for Cauchy-Schwarz.
Compare `norm_inner_eq_norm_iff`, which takes the weaker hypothesis `abs âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`. -/
lemma inner_eq_norm_mul_iff_real {x y : F} : âŸªx, yâŸ«_â„ = â€–xâ€– * â€–yâ€– â†” â€–yâ€– â€¢ x = â€–xâ€– â€¢ y :=
inner_eq_norm_mul_iff

/-- The inner product of two vectors, divided by the product of their
norms, has value 1 if and only if they are nonzero and one is
a positive multiple of the other. -/
lemma real_inner_div_norm_mul_norm_eq_one_iff (x y : F) :
  âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–) = 1 â†” (x â‰  0 âˆ§ âˆƒ (r : â„), 0 < r âˆ§ y = r â€¢ x) :=
begin
  split,
  { intro h,
    have hxâ‚€ : x â‰  0 := Î» hâ‚€, by simpa [hâ‚€] using h,
    have hyâ‚€ : y â‰  0 := Î» hâ‚€, by simpa [hâ‚€] using h,
    refine âŸ¨hxâ‚€, â€–yâ€– / â€–xâ€–, div_pos (norm_pos_iff.2 hyâ‚€) (norm_pos_iff.2 hxâ‚€), _âŸ©,
    exact ((inner_eq_norm_mul_iff_div hxâ‚€).1 (eq_of_div_eq_one h)).symm },
  { rintro âŸ¨hx, âŸ¨r, âŸ¨hr, rflâŸ©âŸ©âŸ©,
    exact real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul hx hr }
end

/-- The inner product of two vectors, divided by the product of their
norms, has value -1 if and only if they are nonzero and one is
a negative multiple of the other. -/
lemma real_inner_div_norm_mul_norm_eq_neg_one_iff (x y : F) :
  âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–) = -1 â†” (x â‰  0 âˆ§ âˆƒ (r : â„), r < 0 âˆ§ y = r â€¢ x) :=
begin
  rw [â† neg_eq_iff_eq_neg, â† neg_div, â† inner_neg_right, â† norm_neg y,
    real_inner_div_norm_mul_norm_eq_one_iff, (@neg_surjective â„ _).exists],
  refine iff.rfl.and (exists_congr $ Î» r, _),
  rw [neg_pos, neg_smul, neg_inj]
end

/-- If the inner product of two unit vectors is `1`, then the two vectors are equal. One form of
the equality case for Cauchy-Schwarz. -/
lemma inner_eq_one_iff_of_norm_one {x y : E} (hx : â€–xâ€– = 1) (hy : â€–yâ€– = 1) :
  âŸªx, yâŸ« = 1 â†” x = y :=
by { convert inner_eq_norm_mul_iff using 2; simp [hx, hy] }

lemma inner_lt_norm_mul_iff_real {x y : F} :
  âŸªx, yâŸ«_â„ < â€–xâ€– * â€–yâ€– â†” â€–yâ€– â€¢ x â‰  â€–xâ€– â€¢ y :=
calc âŸªx, yâŸ«_â„ < â€–xâ€– * â€–yâ€–
    â†” âŸªx, yâŸ«_â„ â‰  â€–xâ€– * â€–yâ€– : âŸ¨ne_of_lt, lt_of_le_of_ne (real_inner_le_norm _ _)âŸ©
... â†” â€–yâ€– â€¢ x â‰  â€–xâ€– â€¢ y : not_congr inner_eq_norm_mul_iff_real

/-- If the inner product of two unit vectors is strictly less than `1`, then the two vectors are
distinct. One form of the equality case for Cauchy-Schwarz. -/
lemma inner_lt_one_iff_real_of_norm_one {x y : F} (hx : â€–xâ€– = 1) (hy : â€–yâ€– = 1) :
  âŸªx, yâŸ«_â„ < 1 â†” x â‰  y :=
by { convert inner_lt_norm_mul_iff_real; simp [hx, hy] }

/-- The inner product of two weighted sums, where the weights in each
sum add to 0, in terms of the norms of pairwise differences. -/
lemma inner_sum_smul_sum_smul_of_sum_eq_zero {Î¹â‚ : Type*} {sâ‚ : finset Î¹â‚} {wâ‚ : Î¹â‚ â†’ â„}
    (vâ‚ : Î¹â‚ â†’ F) (hâ‚ : âˆ‘ i in sâ‚, wâ‚ i = 0) {Î¹â‚‚ : Type*} {sâ‚‚ : finset Î¹â‚‚} {wâ‚‚ : Î¹â‚‚ â†’ â„}
    (vâ‚‚ : Î¹â‚‚ â†’ F) (hâ‚‚ : âˆ‘ i in sâ‚‚, wâ‚‚ i = 0) :
  âŸª(âˆ‘ iâ‚ in sâ‚, wâ‚ iâ‚ â€¢ vâ‚ iâ‚), (âˆ‘ iâ‚‚ in sâ‚‚, wâ‚‚ iâ‚‚ â€¢ vâ‚‚ iâ‚‚)âŸ«_â„ =
    (-âˆ‘ iâ‚ in sâ‚, âˆ‘ iâ‚‚ in sâ‚‚, wâ‚ iâ‚ * wâ‚‚ iâ‚‚ * (â€–vâ‚ iâ‚ - vâ‚‚ iâ‚‚â€– * â€–vâ‚ iâ‚ - vâ‚‚ iâ‚‚â€–)) / 2 :=
by simp_rw [sum_inner, inner_sum, real_inner_smul_left, real_inner_smul_right,
            real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two,
            â†div_sub_div_same, â†div_add_div_same, mul_sub_left_distrib, left_distrib,
            finset.sum_sub_distrib, finset.sum_add_distrib, â†finset.mul_sum, â†finset.sum_mul,
            hâ‚, hâ‚‚, zero_mul, mul_zero, finset.sum_const_zero, zero_add, zero_sub, finset.mul_sum,
            neg_div, finset.sum_div, mul_div_assoc, mul_assoc]

variables (ğ•œ)

/-- The inner product as a sesquilinear map. -/
def innerâ‚›â‚— : E â†’â‚—â‹†[ğ•œ] E â†’â‚—[ğ•œ] ğ•œ :=
linear_map.mkâ‚‚'â‚›â‚— _ _ (Î» v w, âŸªv, wâŸ«) inner_add_left (Î» _ _ _, inner_smul_left _ _ _)
  inner_add_right (Î» _ _ _, inner_smul_right _ _ _)

@[simp] lemma innerâ‚›â‚—_apply_coe (v : E) : â‡‘(innerâ‚›â‚— ğ•œ v) = Î» w, âŸªv, wâŸ« := rfl

@[simp] lemma innerâ‚›â‚—_apply (v w : E) : innerâ‚›â‚— ğ•œ v w = âŸªv, wâŸ« := rfl

/-- The inner product as a continuous sesquilinear map. Note that `to_dual_map` (resp. `to_dual`)
in `inner_product_space.dual` is a version of this given as a linear isometry (resp. linear
isometric equivalence). -/
def innerSL : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ :=
linear_map.mk_continuousâ‚‚ (innerâ‚›â‚— ğ•œ) 1
(Î» x y, by simp only [norm_inner_le_norm, one_mul, innerâ‚›â‚—_apply])

@[simp] lemma innerSL_apply_coe (v : E) : â‡‘(innerSL ğ•œ v) = Î» w, âŸªv, wâŸ« := rfl

@[simp] lemma innerSL_apply (v w : E) : innerSL ğ•œ v w = âŸªv, wâŸ« := rfl

/-- `innerSL` is an isometry. Note that the associated `linear_isometry` is defined in
`inner_product_space.dual` as `to_dual_map`.  -/
@[simp] lemma innerSL_apply_norm (x : E) : â€–innerSL ğ•œ xâ€– = â€–xâ€– :=
begin
  refine le_antisymm ((innerSL ğ•œ x).op_norm_le_bound (norm_nonneg _)
    (Î» y, norm_inner_le_norm _ _)) _,
  rcases eq_or_ne x 0 with (rfl | h),
  { simp },
  { refine (mul_le_mul_right (norm_pos_iff.2 h)).mp _,
    calc â€–xâ€– * â€–xâ€– = â€–(âŸªx, xâŸ« : ğ•œ)â€– : by rw [â† sq, inner_self_eq_norm_sq_to_K, norm_pow,
      norm_of_real, abs_norm]
    ... â‰¤ â€–innerSL ğ•œ xâ€– * â€–xâ€– : (innerSL ğ•œ x).le_op_norm _ }
end

/-- The inner product as a continuous sesquilinear map, with the two arguments flipped. -/
def innerSL_flip : E â†’L[ğ•œ] E â†’Lâ‹†[ğ•œ] ğ•œ :=
@continuous_linear_map.flipâ‚—áµ¢' ğ•œ ğ•œ ğ•œ E E ğ•œ _ _ _ _ _ _ _ _ _ (ring_hom.id ğ•œ) (star_ring_end ğ•œ) _ _
  (innerSL ğ•œ)

@[simp] lemma innerSL_flip_apply (x y : E) : innerSL_flip ğ•œ x y = âŸªy, xâŸ« := rfl

variables {ğ•œ}

namespace continuous_linear_map

variables  {E' : Type*} [normed_add_comm_group E'] [inner_product_space ğ•œ E']

/-- Given `f : E â†’L[ğ•œ] E'`, construct the continuous sesquilinear form `Î» x y, âŸªx, A yâŸ«`, given
as a continuous linear map. -/
def to_sesq_form : (E â†’L[ğ•œ] E') â†’L[ğ•œ] E' â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ :=
â†‘((continuous_linear_map.flipâ‚—áµ¢' E E' ğ•œ
  (star_ring_end ğ•œ) (ring_hom.id ğ•œ)).to_continuous_linear_equiv) âˆ˜L
(continuous_linear_map.compSL E E' (E' â†’Lâ‹†[ğ•œ] ğ•œ) (ring_hom.id ğ•œ) (ring_hom.id ğ•œ) (innerSL_flip ğ•œ))

@[simp] lemma to_sesq_form_apply_coe (f : E â†’L[ğ•œ] E') (x : E') :
  to_sesq_form f x = (innerSL ğ•œ x).comp f := rfl

lemma to_sesq_form_apply_norm_le {f : E â†’L[ğ•œ] E'} {v : E'} : â€–to_sesq_form f vâ€– â‰¤ â€–fâ€– * â€–vâ€– :=
begin
  refine op_norm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _,
  intro x,
  have hâ‚ : â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := le_op_norm _ _,
  have hâ‚‚ := @norm_inner_le_norm ğ•œ E' _ _ _ v (f x),
  calc â€–âŸªv, f xâŸ«â€– â‰¤ â€–vâ€– * â€–f xâ€–       :  hâ‚‚
              ... â‰¤ â€–vâ€– * (â€–fâ€– * â€–xâ€–)  : mul_le_mul_of_nonneg_left hâ‚ (norm_nonneg v)
              ... = â€–fâ€– * â€–vâ€– * â€–xâ€–    : by ring,
end

end continuous_linear_map

/-- When an inner product space `E` over `ğ•œ` is considered as a real normed space, its inner
product satisfies `is_bounded_bilinear_map`.

In order to state these results, we need a `normed_space â„ E` instance. We will later establish
such an instance by restriction-of-scalars, `inner_product_space.is_R_or_C_to_real ğ•œ E`, but this
instance may be not definitionally equal to some other â€œnaturalâ€ instance. So, we assume
`[normed_space â„ E]`.
-/
lemma is_bounded_bilinear_map_inner [normed_space â„ E] :
  is_bounded_bilinear_map â„ (Î» p : E Ã— E, âŸªp.1, p.2âŸ«) :=
{ add_left := inner_add_left,
  smul_left := Î» r x y,
    by simp only [â† algebra_map_smul ğ•œ r x, algebra_map_eq_of_real, inner_smul_real_left],
  add_right := inner_add_right,
  smul_right := Î» r x y,
    by simp only [â† algebra_map_smul ğ•œ r y, algebra_map_eq_of_real, inner_smul_real_right],
  bound := âŸ¨1, zero_lt_one, Î» x y,
    by { rw [one_mul], exact norm_inner_le_norm x y, }âŸ© }

end norm

section bessels_inequality

variables {Î¹: Type*} (x : E) {v : Î¹ â†’ E}

/-- Bessel's inequality for finite sums. -/
lemma orthonormal.sum_inner_products_le {s : finset Î¹} (hv : orthonormal ğ•œ v) :
  âˆ‘ i in s, â€–âŸªv i, xâŸ«â€– ^ 2 â‰¤ â€–xâ€– ^ 2 :=
begin
  have hâ‚‚ : âˆ‘ i in s, âˆ‘ j in s, âŸªv i, xâŸ« * âŸªx, v jâŸ« * âŸªv j, v iâŸ«
    = (âˆ‘ k in s, (âŸªv k, xâŸ« * âŸªx, v kâŸ«) : ğ•œ),
  { exact hv.inner_left_right_finset },
  have hâ‚ƒ : âˆ€ z : ğ•œ, re (z * conj (z)) = â€–zâ€– ^ 2,
  { intro z,
    simp only [mul_conj, norm_sq_eq_def'],
    norm_cast, },
  suffices hbf: â€–x -  âˆ‘ i in s, âŸªv i, xâŸ« â€¢ (v i)â€– ^ 2 = â€–xâ€– ^ 2 - âˆ‘ i in s, â€–âŸªv i, xâŸ«â€– ^ 2,
  { rw [â†sub_nonneg, â†hbf],
    simp only [norm_nonneg, pow_nonneg], },
  rw [@norm_sub_sq ğ•œ, sub_add],
  simp only [@inner_product_space.norm_sq_eq_inner ğ•œ, inner_sum],
  simp only [sum_inner, two_mul, inner_smul_right, inner_conj_symm, â†mul_assoc, hâ‚‚, â†hâ‚ƒ,
  inner_conj_symm, add_monoid_hom.map_sum, finset.mul_sum, â†finset.sum_sub_distrib, inner_smul_left,
  add_sub_cancel'],
end

/-- Bessel's inequality. -/
lemma orthonormal.tsum_inner_products_le (hv : orthonormal ğ•œ v) :
  âˆ‘' i, â€–âŸªv i, xâŸ«â€– ^ 2 â‰¤ â€–xâ€– ^ 2 :=
begin
  refine tsum_le_of_sum_le' _ (Î» s, hv.sum_inner_products_le x),
  simp only [norm_nonneg, pow_nonneg]
end

/-- The sum defined in Bessel's inequality is summable. -/
lemma orthonormal.inner_products_summable (hv : orthonormal ğ•œ v) : summable (Î» i, â€–âŸªv i, xâŸ«â€– ^ 2) :=
begin
  use â¨† s : finset Î¹, âˆ‘ i in s, â€–âŸªv i, xâŸ«â€– ^ 2,
  apply has_sum_of_is_lub_of_nonneg,
  { intro b,
    simp only [norm_nonneg, pow_nonneg], },
  { refine is_lub_csupr _,
    use â€–xâ€– ^ 2,
    rintro y âŸ¨s, rflâŸ©,
    exact hv.sum_inner_products_le x }
end

end bessels_inequality

/-- A field `ğ•œ` satisfying `is_R_or_C` is itself a `ğ•œ`-inner product space. -/
instance is_R_or_C.inner_product_space : inner_product_space ğ•œ ğ•œ :=
{ inner := Î» x y, conj x * y,
  norm_sq_eq_inner := Î» x,
    by { unfold inner, rw [mul_comm, mul_conj, of_real_re, norm_sq_eq_def'] },
  conj_symm := Î» x y, by simp only [mul_comm, map_mul, star_ring_end_self_apply],
  add_left := Î» x y z, by simp only [add_mul, map_add],
  smul_left := Î» x y z, by simp only [mul_assoc, smul_eq_mul, map_mul] }

@[simp] lemma is_R_or_C.inner_apply (x y : ğ•œ) : âŸªx, yâŸ« = (conj x) * y := rfl

/-! ### Inner product space structure on subspaces -/

/-- Induced inner product on a submodule. -/
instance submodule.inner_product_space (W : submodule ğ•œ E) : inner_product_space ğ•œ W :=
{ inner             := Î» x y, âŸª(x:E), (y:E)âŸ«,
  conj_symm         := Î» _ _, inner_conj_symm _ _,
  norm_sq_eq_inner  := Î» x, norm_sq_eq_inner (x : E),
  add_left          := Î» _ _ _, inner_add_left _ _ _,
  smul_left         := Î» _ _ _, inner_smul_left _ _ _,
  ..submodule.normed_space W }

/-- The inner product on submodules is the same as on the ambient space. -/
@[simp] lemma submodule.coe_inner (W : submodule ğ•œ E) (x y : W) : âŸªx, yâŸ« = âŸª(x:E), â†‘yâŸ« := rfl

lemma orthonormal.cod_restrict {Î¹ : Type*} {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v)
  (s : submodule ğ•œ E) (hvs : âˆ€ i, v i âˆˆ s) :
  @orthonormal ğ•œ s _ _ _ Î¹ (set.cod_restrict v s hvs) :=
s.subtypeâ‚—áµ¢.orthonormal_comp_iff.mp hv

lemma orthonormal_span {Î¹ : Type*} {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) :
  @orthonormal ğ•œ (submodule.span ğ•œ (set.range v)) _ _ _ Î¹
    (Î» i : Î¹, âŸ¨v i, submodule.subset_span (set.mem_range_self i)âŸ©) :=
hv.cod_restrict (submodule.span ğ•œ (set.range v))
  (Î» i, submodule.subset_span (set.mem_range_self i))

/-! ### Families of mutually-orthogonal subspaces of an inner product space -/

section orthogonal_family
variables {Î¹ : Type*} [dec_Î¹ : decidable_eq Î¹] (ğ•œ)
open_locale direct_sum

/-- An indexed family of mutually-orthogonal subspaces of an inner product space `E`.

The simple way to express this concept would be as a condition on `V : Î¹ â†’ submodule ğ•œ E`.  We
We instead implement it as a condition on a family of inner product spaces each equipped with an
isometric embedding into `E`, thus making it a property of morphisms rather than subobjects.
The connection to the subobject spelling is shown in `orthogonal_family_iff_pairwise`.

This definition is less lightweight, but allows for better definitional properties when the inner
product space structure on each of the submodules is important -- for example, when considering
their Hilbert sum (`pi_lp V 2`).  For example, given an orthonormal set of vectors `v : Î¹ â†’ E`,
we have an associated orthogonal family of one-dimensional subspaces of `E`, which it is convenient
to be able to discuss using `Î¹ â†’ ğ•œ` rather than `Î  i : Î¹, span ğ•œ (v i)`. -/
def orthogonal_family (G : Î¹ â†’ Type*)
  [Î  i, normed_add_comm_group (G i)] [Î  i, inner_product_space ğ•œ (G i)] (V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E) :
  Prop :=
âˆ€ â¦ƒi jâ¦„, i â‰  j â†’ âˆ€ v : G i, âˆ€ w : G j, âŸªV i v, V j wâŸ« = 0

variables {ğ•œ} {G : Î¹ â†’ Type*}
  [Î  i, normed_add_comm_group (G i)] [Î  i, inner_product_space ğ•œ (G i)] {V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E}
  (hV : orthogonal_family ğ•œ G V) [dec_V : Î  i (x : G i), decidable (x â‰  0)]

lemma orthonormal.orthogonal_family {v : Î¹ â†’ E} (hv : orthonormal ğ•œ v) :
  orthogonal_family ğ•œ (Î» i : Î¹, ğ•œ)
    (Î» i, linear_isometry.to_span_singleton ğ•œ E (hv.1 i)) :=
Î» i j hij a b, by simp [inner_smul_left, inner_smul_right, hv.2 hij]

include hV dec_Î¹
lemma orthogonal_family.eq_ite {i j : Î¹} (v : G i) (w : G j) :
  âŸªV i v, V j wâŸ« = ite (i = j) âŸªV i v, V j wâŸ« 0 :=
begin
  split_ifs,
  { refl },
  { exact hV h v w }
end

include dec_V
lemma orthogonal_family.inner_right_dfinsupp (l : â¨ i, G i) (i : Î¹) (v : G i) :
  âŸªV i v, l.sum (Î» j, V j)âŸ« = âŸªv, l iâŸ« :=
calc âŸªV i v, l.sum (Î» j, V j)âŸ«
    = l.sum (Î» j, Î» w, âŸªV i v, V j wâŸ«) : dfinsupp.inner_sum (Î» j, V j) l (V i v)
... = l.sum (Î» j, Î» w, ite (i=j) âŸªV i v, V j wâŸ« 0) :
  congr_arg l.sum $ funext $ Î» j, funext $ hV.eq_ite v
... = âŸªv, l iâŸ« :
begin
  simp only [dfinsupp.sum, submodule.coe_inner, finset.sum_ite_eq, ite_eq_left_iff,
    dfinsupp.mem_support_to_fun],
  split_ifs with h h,
  { simp only [linear_isometry.inner_map_map] },
  { simp only [of_not_not h, inner_zero_right] },
end
omit dec_Î¹ dec_V

lemma orthogonal_family.inner_right_fintype [fintype Î¹] (l : Î  i, G i) (i : Î¹) (v : G i) :
  âŸªV i v, âˆ‘ j : Î¹, V j (l j)âŸ« = âŸªv, l iâŸ« :=
by classical;
calc âŸªV i v, âˆ‘ j : Î¹, V j (l j)âŸ«
    = âˆ‘ j : Î¹, âŸªV i v, V j (l j)âŸ«: by rw inner_sum
... = âˆ‘ j, ite (i = j) âŸªV i v, V j (l j)âŸ« 0 :
  congr_arg (finset.sum finset.univ) $ funext $ Î» j, (hV.eq_ite v (l j))
... = âŸªv, l iâŸ« : by simp only [finset.sum_ite_eq, finset.mem_univ, (V i).inner_map_map, if_true]

lemma orthogonal_family.inner_sum (lâ‚ lâ‚‚ : Î  i, G i) (s : finset Î¹) :
  âŸªâˆ‘ i in s, V i (lâ‚ i), âˆ‘ j in s, V j (lâ‚‚ j)âŸ« = âˆ‘ i in s, âŸªlâ‚ i, lâ‚‚ iâŸ« :=
by classical;
calc âŸªâˆ‘ i in s, V i (lâ‚ i), âˆ‘ j in s, V j (lâ‚‚ j)âŸ«
    = âˆ‘ j in s, âˆ‘ i in s, âŸªV i (lâ‚ i), V j (lâ‚‚ j)âŸ« : by simp only [sum_inner, inner_sum]
... = âˆ‘ j in s, âˆ‘ i in s, ite (i = j) âŸªV i (lâ‚ i), V j (lâ‚‚ j)âŸ« 0 :
begin
  congr' with i,
  congr' with j,
  apply hV.eq_ite,
end
... = âˆ‘ i in s, âŸªlâ‚ i, lâ‚‚ iâŸ« : by simp only [finset.sum_ite_of_true,
  finset.sum_ite_eq', linear_isometry.inner_map_map, imp_self, implies_true_iff]

lemma orthogonal_family.norm_sum (l : Î  i, G i) (s : finset Î¹) :
  â€–âˆ‘ i in s, V i (l i)â€– ^ 2 = âˆ‘ i in s, â€–l iâ€– ^ 2 :=
begin
  have : (â€–âˆ‘ i in s, V i (l i)â€– ^ 2 : ğ•œ) = âˆ‘ i in s, â€–l iâ€– ^ 2,
  { simp only [â† inner_self_eq_norm_sq_to_K, hV.inner_sum] },
  exact_mod_cast this,
end

/-- The composition of an orthogonal family of subspaces with an injective function is also an
orthogonal family. -/
lemma orthogonal_family.comp {Î³ : Type*} {f : Î³ â†’ Î¹} (hf : function.injective f) :
  orthogonal_family ğ•œ (Î» g, G (f g)) (Î» g, V (f g)) :=
Î» i j hij v w, hV (hf.ne hij) v w

lemma orthogonal_family.orthonormal_sigma_orthonormal {Î± : Î¹ â†’ Type*} {v_family : Î  i, (Î± i) â†’ G i}
  (hv_family : âˆ€ i, orthonormal ğ•œ (v_family i)) :
  orthonormal ğ•œ (Î» a : Î£ i, Î± i, V a.1 (v_family a.1 a.2)) :=
begin
  split,
  { rintros âŸ¨i, vâŸ©,
    simpa only [linear_isometry.norm_map] using (hv_family i).left v },
  rintros âŸ¨i, vâŸ© âŸ¨j, wâŸ© hvw,
  by_cases hij : i = j,
  { subst hij,
    have : v â‰  w := Î» h, by { subst h, exact hvw rfl },
    simpa only [linear_isometry.inner_map_map] using (hv_family i).2 this },
  { exact hV hij (v_family i v) (v_family j w) }
end

include dec_Î¹
lemma orthogonal_family.norm_sq_diff_sum (f : Î  i, G i) (sâ‚ sâ‚‚ : finset Î¹) :
  â€–âˆ‘ i in sâ‚, V i (f i) - âˆ‘ i in sâ‚‚, V i (f i)â€– ^ 2
  = âˆ‘ i in sâ‚ \ sâ‚‚, â€–f iâ€– ^ 2 + âˆ‘ i in sâ‚‚ \ sâ‚, â€–f iâ€– ^ 2 :=
begin
  rw [â† finset.sum_sdiff_sub_sum_sdiff, sub_eq_add_neg, â† finset.sum_neg_distrib],
  let F : Î  i, G i := Î» i, if i âˆˆ sâ‚ then f i else - (f i),
  have hFâ‚ : âˆ€ i âˆˆ sâ‚ \ sâ‚‚, F i = f i := Î» i hi, if_pos (finset.sdiff_subset _ _ hi),
  have hFâ‚‚ : âˆ€ i âˆˆ sâ‚‚ \ sâ‚, F i = - f i := Î» i hi, if_neg (finset.mem_sdiff.mp hi).2,
  have hF : âˆ€ i, â€–F iâ€– = â€–f iâ€–,
  { intros i,
    dsimp only [F],
    split_ifs;
    simp only [eq_self_iff_true, norm_neg], },
  have : â€–âˆ‘ i in sâ‚ \ sâ‚‚, V i (F i) + âˆ‘ i in sâ‚‚ \ sâ‚, V i (F i)â€– ^ 2 =
    âˆ‘ i in sâ‚ \ sâ‚‚, â€–F iâ€– ^ 2 + âˆ‘ i in sâ‚‚ \ sâ‚, â€–F iâ€– ^ 2,
  { have hs : disjoint (sâ‚ \ sâ‚‚) (sâ‚‚ \ sâ‚) := disjoint_sdiff_sdiff,
    simpa only [finset.sum_union hs] using hV.norm_sum F (sâ‚ \ sâ‚‚ âˆª sâ‚‚ \ sâ‚) },
  convert this using 4,
  { refine finset.sum_congr rfl (Î» i hi, _),
    simp only [hFâ‚ i hi] },
  { refine finset.sum_congr rfl (Î» i hi, _),
    simp only [hFâ‚‚ i hi, linear_isometry.map_neg] },
  { simp only [hF] },
  { simp only [hF] },
end

omit dec_Î¹

/-- A family `f` of mutually-orthogonal elements of `E` is summable, if and only if
`(Î» i, â€–f iâ€– ^ 2)` is summable. -/
lemma orthogonal_family.summable_iff_norm_sq_summable [complete_space E] (f : Î  i, G i) :
  summable (Î» i, V i (f i)) â†” summable (Î» i, â€–f iâ€– ^ 2) :=
begin
  classical,
  simp only [summable_iff_cauchy_seq_finset, normed_add_comm_group.cauchy_seq_iff,
    real.norm_eq_abs],
  split,
  { intros hf Îµ hÎµ,
    obtain âŸ¨a, HâŸ© := hf _ (sqrt_pos.mpr hÎµ),
    use a,
    intros sâ‚ hsâ‚ sâ‚‚ hsâ‚‚,
    rw â† finset.sum_sdiff_sub_sum_sdiff,
    refine (abs_sub _ _).trans_lt _,
    have : âˆ€ i, 0 â‰¤ â€–f iâ€– ^ 2 := Î» i : Î¹, sq_nonneg _,
    simp only [finset.abs_sum_of_nonneg' this],
    have : âˆ‘ i in sâ‚ \ sâ‚‚, â€–f iâ€– ^ 2 + âˆ‘ i in sâ‚‚ \ sâ‚, â€–f iâ€– ^ 2 < (sqrt Îµ) ^ 2,
    { rw [â† hV.norm_sq_diff_sum, sq_lt_sq, abs_of_nonneg (sqrt_nonneg _),
        abs_of_nonneg (norm_nonneg _)],
      exact H sâ‚ hsâ‚ sâ‚‚ hsâ‚‚ },
    have hÎ· := sq_sqrt (le_of_lt hÎµ),
    linarith },
  { intros hf Îµ hÎµ,
    have hÎµ' : 0 < Îµ ^ 2 / 2 := half_pos (sq_pos_of_pos hÎµ),
    obtain âŸ¨a, HâŸ© := hf _ hÎµ',
    use a,
    intros sâ‚ hsâ‚ sâ‚‚ hsâ‚‚,
    refine (abs_lt_of_sq_lt_sq' _ (le_of_lt hÎµ)).2,
    have has : a â‰¤ sâ‚ âŠ“ sâ‚‚ := le_inf hsâ‚ hsâ‚‚,
    rw hV.norm_sq_diff_sum,
    have Hsâ‚ : âˆ‘ (x : Î¹) in sâ‚ \ sâ‚‚, â€–f xâ€– ^ 2 < Îµ ^ 2 / 2,
    { convert H _ hsâ‚ _ has,
      have : sâ‚ âŠ“ sâ‚‚ âŠ† sâ‚ := finset.inter_subset_left _ _,
      rw [â† finset.sum_sdiff this, add_tsub_cancel_right, finset.abs_sum_of_nonneg'],
      { simp },
      { exact Î» i, sq_nonneg _ } },
    have Hsâ‚‚ : âˆ‘ (x : Î¹) in sâ‚‚ \ sâ‚, â€–f xâ€– ^ 2 < Îµ ^ 2 /2,
    { convert H _ hsâ‚‚ _ has,
      have : sâ‚ âŠ“ sâ‚‚ âŠ† sâ‚‚ := finset.inter_subset_right _ _,
      rw [â† finset.sum_sdiff this, add_tsub_cancel_right, finset.abs_sum_of_nonneg'],
      { simp },
      { exact Î» i, sq_nonneg _ } },
    linarith },
end

omit hV

/-- An orthogonal family forms an independent family of subspaces; that is, any collection of
elements each from a different subspace in the family is linearly independent. In particular, the
pairwise intersections of elements of the family are 0. -/
lemma orthogonal_family.independent {V : Î¹ â†’ submodule ğ•œ E}
  (hV : orthogonal_family ğ•œ (Î» i, V i) (Î» i, (V i).subtypeâ‚—áµ¢)) :
  complete_lattice.independent V :=
begin
  classical,
  apply complete_lattice.independent_of_dfinsupp_lsum_injective,
  rw [â† @linear_map.ker_eq_bot _ _ _ _ _ _ (direct_sum.add_comm_group (Î» i, V i)),
    submodule.eq_bot_iff],
  intros v hv,
  rw linear_map.mem_ker at hv,
  ext i,
  suffices : âŸª(v i : E), v iâŸ« = 0,
  { simpa only [inner_self_eq_zero] using this },
  calc âŸª(v i : E), v iâŸ« = âŸª(v i : E), dfinsupp.lsum â„• (Î» i, (V i).subtype) vâŸ« :
    by simpa only [dfinsupp.sum_add_hom_apply, dfinsupp.lsum_apply_apply]
      using (hV.inner_right_dfinsupp v i (v i)).symm
  ... = 0 : by simp only [hv, inner_zero_right],
end

include dec_Î¹
lemma direct_sum.is_internal.collected_basis_orthonormal {V : Î¹ â†’ submodule ğ•œ E}
  (hV : orthogonal_family ğ•œ (Î» i, V i) (Î» i, (V i).subtypeâ‚—áµ¢))
  (hV_sum : direct_sum.is_internal (Î» i, V i))
  {Î± : Î¹ â†’ Type*}
  {v_family : Î  i, basis (Î± i) ğ•œ (V i)} (hv_family : âˆ€ i, orthonormal ğ•œ (v_family i)) :
  orthonormal ğ•œ (hV_sum.collected_basis v_family) :=
by simpa only [hV_sum.collected_basis_coe] using hV.orthonormal_sigma_orthonormal hv_family

end orthogonal_family

section is_R_or_C_to_real

variables {G : Type*}

variables (ğ•œ E)
include ğ•œ

/-- A general inner product implies a real inner product. This is not registered as an instance
since it creates problems with the case `ğ•œ = â„`. -/
def has_inner.is_R_or_C_to_real : has_inner â„ E :=
{ inner := Î» x y, re âŸªx, yâŸ« }

/-- A general inner product space structure implies a real inner product structure. This is not
registered as an instance since it creates problems with the case `ğ•œ = â„`, but in can be used in a
proof to obtain a real inner product space structure from a given `ğ•œ`-inner product space
structure. -/
def inner_product_space.is_R_or_C_to_real : inner_product_space â„ E :=
{ norm_sq_eq_inner := norm_sq_eq_inner,
  conj_symm := Î» x y, inner_re_symm _ _,
  add_left := Î» x y z, by
  { change re âŸªx + y, zâŸ« = re âŸªx, zâŸ« + re âŸªy, zâŸ«,
    simp only [inner_add_left, map_add] },
  smul_left := Î» x y r, by
  { change re âŸª(r : ğ•œ) â€¢ x, yâŸ« = r * re âŸªx, yâŸ«,
    simp only [inner_smul_left, conj_of_real, of_real_mul_re] },
  ..has_inner.is_R_or_C_to_real ğ•œ E,
  ..normed_space.restrict_scalars â„ ğ•œ E }

variable {E}

lemma real_inner_eq_re_inner (x y : E) :
  @has_inner.inner â„ E (has_inner.is_R_or_C_to_real ğ•œ E) x y = re âŸªx, yâŸ« := rfl

lemma real_inner_I_smul_self (x : E) :
  @has_inner.inner â„ E (has_inner.is_R_or_C_to_real ğ•œ E) x ((I : ğ•œ) â€¢ x) = 0 :=
by simp [real_inner_eq_re_inner, inner_smul_right]

omit ğ•œ

/-- A complex inner product implies a real inner product -/
instance inner_product_space.complex_to_real
  [normed_add_comm_group G] [inner_product_space â„‚ G] : inner_product_space â„ G :=
inner_product_space.is_R_or_C_to_real â„‚ G

@[simp] protected lemma complex.inner (w z : â„‚) : âŸªw, zâŸ«_â„ = (conj w * z).re := rfl

/-- The inner product on an inner product space of dimension 2 can be evaluated in terms
of a complex-number representation of the space. -/
lemma inner_map_complex [normed_add_comm_group G] [inner_product_space â„ G]
  (f : G â‰ƒâ‚—áµ¢[â„] â„‚) (x y : G) :
  âŸªx, yâŸ«_â„ = (conj (f x) * f y).re :=
by rw [â† complex.inner, f.inner_map_map]

end is_R_or_C_to_real

section continuous

/-!
### Continuity of the inner product
-/

lemma continuous_inner : continuous (Î» p : E Ã— E, âŸªp.1, p.2âŸ«) :=
begin
  letI : inner_product_space â„ E := inner_product_space.is_R_or_C_to_real ğ•œ E,
  exact is_bounded_bilinear_map_inner.continuous
end

variables {Î± : Type*}

lemma filter.tendsto.inner {f g : Î± â†’ E} {l : filter Î±} {x y : E} (hf : tendsto f l (ğ“ x))
  (hg : tendsto g l (ğ“ y)) :
  tendsto (Î» t, âŸªf t, g tâŸ«) l (ğ“ âŸªx, yâŸ«) :=
(continuous_inner.tendsto _).comp (hf.prod_mk_nhds hg)

variables [topological_space Î±] {f g : Î± â†’ E} {x : Î±} {s : set Î±}

include ğ•œ

lemma continuous_within_at.inner (hf : continuous_within_at f s x)
  (hg : continuous_within_at g s x) :
  continuous_within_at (Î» t, âŸªf t, g tâŸ«) s x :=
hf.inner hg

lemma continuous_at.inner (hf : continuous_at f x) (hg : continuous_at g x) :
  continuous_at (Î» t, âŸªf t, g tâŸ«) x :=
hf.inner hg

lemma continuous_on.inner (hf : continuous_on f s) (hg : continuous_on g s) :
  continuous_on (Î» t, âŸªf t, g tâŸ«) s :=
Î» x hx, (hf x hx).inner (hg x hx)

@[continuity]
lemma continuous.inner (hf : continuous f) (hg : continuous g) : continuous (Î» t, âŸªf t, g tâŸ«) :=
continuous_iff_continuous_at.2 $ Î» x, hf.continuous_at.inner hg.continuous_at

end continuous

section re_apply_inner_self

/-- Extract a real bilinear form from an operator `T`, by taking the pairing `Î» x, re âŸªT x, xâŸ«`. -/
def continuous_linear_map.re_apply_inner_self (T : E â†’L[ğ•œ] E) (x : E) : â„ := re âŸªT x, xâŸ«

lemma continuous_linear_map.re_apply_inner_self_apply (T : E â†’L[ğ•œ] E) (x : E) :
  T.re_apply_inner_self x = re âŸªT x, xâŸ« :=
rfl

lemma continuous_linear_map.re_apply_inner_self_continuous (T : E â†’L[ğ•œ] E) :
  continuous T.re_apply_inner_self :=
re_clm.continuous.comp $ T.continuous.inner continuous_id

lemma continuous_linear_map.re_apply_inner_self_smul (T : E â†’L[ğ•œ] E) (x : E) {c : ğ•œ} :
  T.re_apply_inner_self (c â€¢ x) = â€–câ€– ^ 2 * T.re_apply_inner_self x :=
by simp only [continuous_linear_map.map_smul, continuous_linear_map.re_apply_inner_self_apply,
  inner_smul_left, inner_smul_right, â† mul_assoc, mul_conj, norm_sq_eq_def', â† smul_re,
  algebra.smul_def (â€–câ€– ^ 2) âŸªT x, xâŸ«, algebra_map_eq_of_real]

end re_apply_inner_self

namespace uniform_space.completion

open uniform_space function

instance {ğ•œ' E' : Type*} [topological_space ğ•œ'] [uniform_space E'] [has_inner ğ•œ' E'] :
  has_inner ğ•œ' (completion E') :=
{ inner := curry $ (dense_inducing_coe.prod dense_inducing_coe).extend (uncurry inner) }

@[simp] lemma inner_coe (a b : E) :
  inner (a : completion E) (b : completion E) = (inner a b : ğ•œ) :=
(dense_inducing_coe.prod dense_inducing_coe).extend_eq
  (continuous_inner : continuous (uncurry inner : E Ã— E â†’ ğ•œ)) (a, b)

protected lemma continuous_inner :
  continuous (uncurry inner : completion E Ã— completion E â†’ ğ•œ) :=
begin
  let inner' : E â†’+ E â†’+ ğ•œ :=
  { to_fun := Î» x, (innerâ‚›â‚— ğ•œ x).to_add_monoid_hom,
    map_zero' := by ext x; exact inner_zero_left _,
    map_add' := Î» x y, by ext z; exact inner_add_left _ _ _ },
  have : continuous (Î» p : E Ã— E, inner' p.1 p.2) := continuous_inner,
  rw [completion.has_inner, uncurry_curry _],
  change continuous (((dense_inducing_to_compl E).prod (dense_inducing_to_compl E)).extend
    (Î» p : E Ã— E, inner' p.1 p.2)),
  exact (dense_inducing_to_compl E).extend_Z_bilin (dense_inducing_to_compl E) this,
end

protected lemma continuous.inner {Î± : Type*} [topological_space Î±]
  {f g : Î± â†’ completion E} (hf : continuous f) (hg : continuous g) :
  continuous (Î» x : Î±, inner (f x) (g x) : Î± â†’ ğ•œ) :=
uniform_space.completion.continuous_inner.comp (hf.prod_mk hg : _)

instance : inner_product_space ğ•œ (completion E) :=
{ norm_sq_eq_inner := Î» x, completion.induction_on x
    (is_closed_eq
      (continuous_norm.pow 2)
      (continuous_re.comp (continuous.inner continuous_id' continuous_id')))
    (Î» a, by simp only [norm_coe, inner_coe, inner_self_eq_norm_sq]),
  conj_symm := Î» x y, completion.induction_onâ‚‚ x y
    (is_closed_eq
      (continuous_conj.comp (continuous.inner continuous_snd continuous_fst))
      (continuous.inner continuous_fst continuous_snd))
    (Î» a b, by simp only [inner_coe, inner_conj_symm]),
  add_left := Î» x y z, completion.induction_onâ‚ƒ x y z
    (is_closed_eq
      (continuous.inner (continuous_fst.add (continuous_fst.comp continuous_snd))
        (continuous_snd.comp continuous_snd))
      ((continuous.inner continuous_fst (continuous_snd.comp continuous_snd)).add
        (continuous.inner (continuous_fst.comp continuous_snd)
          (continuous_snd.comp continuous_snd))))
    (Î» a b c, by simp only [â† coe_add, inner_coe, inner_add_left]),
  smul_left := Î» x y c, completion.induction_onâ‚‚ x y
    (is_closed_eq
      (continuous.inner (continuous_fst.const_smul c) continuous_snd)
      ((continuous_mul_left _).comp (continuous.inner continuous_fst continuous_snd)))
    (Î» a b, by simp only [â† coe_smul c a, inner_coe, inner_smul_left]) }

end uniform_space.completion
