/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.inner_product_space.basic
import analysis.special_functions.sqrt

/-!
# Derivative of the inner product

In this file we prove that the inner product and square of the norm in an inner space are
infinitely `â„`-smooth. In order to state these results, we need a `normed_space â„ E`
instance. Though we can deduce this structure from `inner_product_space ğ•œ E`, this instance may be
not definitionally equal to some other â€œnaturalâ€ instance. So, we assume `[normed_space â„ E]` and
`[is_scalar_tower â„ ğ•œ E]`. In both interesting cases `ğ•œ = â„` and `ğ•œ = â„‚` we have these instances.

Currently, the continuity of the inner product is also proved in this file, as a consequence of the
differentiability; however (TODO) this ought to be re-proved directly and moved to
`analysis.inner_product_space.basic`.

-/

noncomputable theory

open is_R_or_C real filter
open_locale big_operators classical topological_space

variables {ğ•œ E F : Type*} [is_R_or_C ğ•œ]
variables [inner_product_space ğ•œ E] [inner_product_space â„ F]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

section deriv

variables [normed_space â„ E] [is_scalar_tower â„ ğ•œ E]

lemma is_bounded_bilinear_map_inner : is_bounded_bilinear_map â„ (Î» p : E Ã— E, âŸªp.1, p.2âŸ«) :=
{ add_left := Î» _ _ _, inner_add_left,
  smul_left := Î» r x y,
    by simp only [â† algebra_map_smul ğ•œ r x, algebra_map_eq_of_real, inner_smul_real_left],
  add_right := Î» _ _ _, inner_add_right,
  smul_right := Î» r x y,
    by simp only [â† algebra_map_smul ğ•œ r y, algebra_map_eq_of_real, inner_smul_real_right],
  bound := âŸ¨1, zero_lt_one, Î» x y,
    by { rw [one_mul], exact norm_inner_le_norm x y, }âŸ© }

/-- Derivative of the inner product. -/
def fderiv_inner_clm (p : E Ã— E) : E Ã— E â†’L[â„] ğ•œ := is_bounded_bilinear_map_inner.deriv p

@[simp] lemma fderiv_inner_clm_apply (p x : E Ã— E) :
  fderiv_inner_clm  p x = âŸªp.1, x.2âŸ« + âŸªx.1, p.2âŸ« := rfl

lemma times_cont_diff_inner {n} : times_cont_diff â„ n (Î» p : E Ã— E, âŸªp.1, p.2âŸ«) :=
is_bounded_bilinear_map_inner.times_cont_diff

lemma times_cont_diff_at_inner {p : E Ã— E} {n} :
  times_cont_diff_at â„ n (Î» p : E Ã— E, âŸªp.1, p.2âŸ«) p :=
times_cont_diff_inner.times_cont_diff_at

lemma differentiable_inner : differentiable â„ (Î» p : E Ã— E, âŸªp.1, p.2âŸ«) :=
is_bounded_bilinear_map_inner.differentiable_at

variables {G : Type*} [normed_group G] [normed_space â„ G]
  {f g : G â†’ E} {f' g' : G â†’L[â„] E} {s : set G} {x : G} {n : with_top â„•}

include ğ•œ

lemma times_cont_diff_within_at.inner (hf : times_cont_diff_within_at â„ n f s x)
  (hg : times_cont_diff_within_at â„ n g s x) :
  times_cont_diff_within_at â„ n (Î» x, âŸªf x, g xâŸ«) s x :=
times_cont_diff_at_inner.comp_times_cont_diff_within_at x (hf.prod hg)

lemma times_cont_diff_at.inner (hf : times_cont_diff_at â„ n f x)
  (hg : times_cont_diff_at â„ n g x) :
  times_cont_diff_at â„ n (Î» x, âŸªf x, g xâŸ«) x :=
hf.inner hg

lemma times_cont_diff_on.inner (hf : times_cont_diff_on â„ n f s) (hg : times_cont_diff_on â„ n g s) :
  times_cont_diff_on â„ n (Î» x, âŸªf x, g xâŸ«) s :=
Î» x hx, (hf x hx).inner (hg x hx)

lemma times_cont_diff.inner (hf : times_cont_diff â„ n f) (hg : times_cont_diff â„ n g) :
  times_cont_diff â„ n (Î» x, âŸªf x, g xâŸ«) :=
times_cont_diff_inner.comp (hf.prod hg)

lemma has_fderiv_within_at.inner (hf : has_fderiv_within_at f f' s x)
  (hg : has_fderiv_within_at g g' s x) :
  has_fderiv_within_at (Î» t, âŸªf t, g tâŸ«) ((fderiv_inner_clm (f x, g x)).comp $ f'.prod g') s x :=
(is_bounded_bilinear_map_inner.has_fderiv_at (f x, g x)).comp_has_fderiv_within_at x (hf.prod hg)

lemma has_fderiv_at.inner (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) :
  has_fderiv_at (Î» t, âŸªf t, g tâŸ«) ((fderiv_inner_clm (f x, g x)).comp $ f'.prod g') x :=
(is_bounded_bilinear_map_inner.has_fderiv_at (f x, g x)).comp x (hf.prod hg)

lemma has_deriv_within_at.inner {f g : â„ â†’ E} {f' g' : E} {s : set â„} {x : â„}
  (hf : has_deriv_within_at f f' s x) (hg : has_deriv_within_at g g' s x) :
  has_deriv_within_at (Î» t, âŸªf t, g tâŸ«) (âŸªf x, g'âŸ« + âŸªf', g xâŸ«) s x :=
by simpa using (hf.has_fderiv_within_at.inner hg.has_fderiv_within_at).has_deriv_within_at

lemma has_deriv_at.inner {f g : â„ â†’ E} {f' g' : E} {x : â„} :
  has_deriv_at f f' x â†’  has_deriv_at g g' x â†’
  has_deriv_at (Î» t, âŸªf t, g tâŸ«) (âŸªf x, g'âŸ« + âŸªf', g xâŸ«) x :=
by simpa only [â† has_deriv_within_at_univ] using has_deriv_within_at.inner

lemma differentiable_within_at.inner (hf : differentiable_within_at â„ f s x)
  (hg : differentiable_within_at â„ g s x) :
  differentiable_within_at â„ (Î» x, âŸªf x, g xâŸ«) s x :=
((differentiable_inner _).has_fderiv_at.comp_has_fderiv_within_at x
  (hf.prod hg).has_fderiv_within_at).differentiable_within_at

lemma differentiable_at.inner (hf : differentiable_at â„ f x) (hg : differentiable_at â„ g x) :
  differentiable_at â„ (Î» x, âŸªf x, g xâŸ«) x :=
(differentiable_inner _).comp x (hf.prod hg)

lemma differentiable_on.inner (hf : differentiable_on â„ f s) (hg : differentiable_on â„ g s) :
  differentiable_on â„ (Î» x, âŸªf x, g xâŸ«) s :=
Î» x hx, (hf x hx).inner (hg x hx)

lemma differentiable.inner (hf : differentiable â„ f) (hg : differentiable â„ g) :
  differentiable â„ (Î» x, âŸªf x, g xâŸ«) :=
Î» x, (hf x).inner (hg x)

lemma fderiv_inner_apply (hf : differentiable_at â„ f x) (hg : differentiable_at â„ g x) (y : G) :
  fderiv â„ (Î» t, âŸªf t, g tâŸ«) x y = âŸªf x, fderiv â„ g x yâŸ« + âŸªfderiv â„ f x y, g xâŸ« :=
by { rw [(hf.has_fderiv_at.inner hg.has_fderiv_at).fderiv], refl }

lemma deriv_inner_apply {f g : â„ â†’ E} {x : â„} (hf : differentiable_at â„ f x)
  (hg : differentiable_at â„ g x) :
  deriv (Î» t, âŸªf t, g tâŸ«) x = âŸªf x, deriv g xâŸ« + âŸªderiv f x, g xâŸ« :=
(hf.has_deriv_at.inner hg.has_deriv_at).deriv

lemma times_cont_diff_norm_sq : times_cont_diff â„ n (Î» x : E, âˆ¥xâˆ¥ ^ 2) :=
begin
  simp only [sq, â† inner_self_eq_norm_sq],
  exact (re_clm : ğ•œ â†’L[â„] â„).times_cont_diff.comp (times_cont_diff_id.inner times_cont_diff_id)
end

lemma times_cont_diff.norm_sq (hf : times_cont_diff â„ n f) :
  times_cont_diff â„ n (Î» x, âˆ¥f xâˆ¥ ^ 2) :=
times_cont_diff_norm_sq.comp hf

lemma times_cont_diff_within_at.norm_sq (hf : times_cont_diff_within_at â„ n f s x) :
  times_cont_diff_within_at â„ n (Î» y, âˆ¥f yâˆ¥ ^ 2) s x :=
times_cont_diff_norm_sq.times_cont_diff_at.comp_times_cont_diff_within_at x hf

lemma times_cont_diff_at.norm_sq (hf : times_cont_diff_at â„ n f x) :
  times_cont_diff_at â„ n (Î» y, âˆ¥f yâˆ¥ ^ 2) x :=
hf.norm_sq

lemma times_cont_diff_at_norm {x : E} (hx : x â‰  0) : times_cont_diff_at â„ n norm x :=
have âˆ¥id xâˆ¥ ^ 2 â‰  0, from pow_ne_zero _ (norm_pos_iff.2 hx).ne',
by simpa only [id, sqrt_sq, norm_nonneg] using times_cont_diff_at_id.norm_sq.sqrt this

lemma times_cont_diff_at.norm (hf : times_cont_diff_at â„ n f x) (h0 : f x â‰  0) :
  times_cont_diff_at â„ n (Î» y, âˆ¥f yâˆ¥) x :=
(times_cont_diff_at_norm h0).comp x hf

lemma times_cont_diff_at.dist (hf : times_cont_diff_at â„ n f x) (hg : times_cont_diff_at â„ n g x)
  (hne : f x â‰  g x) :
  times_cont_diff_at â„ n (Î» y, dist (f y) (g y)) x :=
by { simp only [dist_eq_norm], exact (hf.sub hg).norm (sub_ne_zero.2 hne) }

lemma times_cont_diff_within_at.norm (hf : times_cont_diff_within_at â„ n f s x) (h0 : f x â‰  0) :
  times_cont_diff_within_at â„ n (Î» y, âˆ¥f yâˆ¥) s x :=
(times_cont_diff_at_norm h0).comp_times_cont_diff_within_at x hf

lemma times_cont_diff_within_at.dist (hf : times_cont_diff_within_at â„ n f s x)
  (hg : times_cont_diff_within_at â„ n g s x) (hne : f x â‰  g x) :
  times_cont_diff_within_at â„ n (Î» y, dist (f y) (g y)) s x :=
by { simp only [dist_eq_norm], exact (hf.sub hg).norm (sub_ne_zero.2 hne) }

lemma times_cont_diff_on.norm_sq (hf : times_cont_diff_on â„ n f s) :
  times_cont_diff_on â„ n (Î» y, âˆ¥f yâˆ¥ ^ 2) s :=
(Î» x hx, (hf x hx).norm_sq)

lemma times_cont_diff_on.norm (hf : times_cont_diff_on â„ n f s) (h0 : âˆ€ x âˆˆ s, f x â‰  0) :
  times_cont_diff_on â„ n (Î» y, âˆ¥f yâˆ¥) s :=
Î» x hx, (hf x hx).norm (h0 x hx)

lemma times_cont_diff_on.dist (hf : times_cont_diff_on â„ n f s)
  (hg : times_cont_diff_on â„ n g s) (hne : âˆ€ x âˆˆ s, f x â‰  g x) :
  times_cont_diff_on â„ n (Î» y, dist (f y) (g y)) s :=
Î» x hx, (hf x hx).dist (hg x hx) (hne x hx)

lemma times_cont_diff.norm (hf : times_cont_diff â„ n f) (h0 : âˆ€ x, f x â‰  0) :
  times_cont_diff â„ n (Î» y, âˆ¥f yâˆ¥) :=
times_cont_diff_iff_times_cont_diff_at.2 $ Î» x, hf.times_cont_diff_at.norm (h0 x)

lemma times_cont_diff.dist (hf : times_cont_diff â„ n f) (hg : times_cont_diff â„ n g)
  (hne : âˆ€ x, f x â‰  g x) :
  times_cont_diff â„ n (Î» y, dist (f y) (g y)) :=
times_cont_diff_iff_times_cont_diff_at.2 $
  Î» x, hf.times_cont_diff_at.dist hg.times_cont_diff_at (hne x)

lemma differentiable_at.norm_sq (hf : differentiable_at â„ f x) :
  differentiable_at â„ (Î» y, âˆ¥f yâˆ¥ ^ 2) x :=
(times_cont_diff_at_id.norm_sq.differentiable_at le_rfl).comp x hf

lemma differentiable_at.norm (hf : differentiable_at â„ f x) (h0 : f x â‰  0) :
  differentiable_at â„ (Î» y, âˆ¥f yâˆ¥) x :=
((times_cont_diff_at_norm h0).differentiable_at le_rfl).comp x hf

lemma differentiable_at.dist (hf : differentiable_at â„ f x) (hg : differentiable_at â„ g x)
  (hne : f x â‰  g x) :
  differentiable_at â„ (Î» y, dist (f y) (g y)) x :=
by { simp only [dist_eq_norm], exact (hf.sub hg).norm (sub_ne_zero.2 hne) }

lemma differentiable.norm_sq (hf : differentiable â„ f) : differentiable â„ (Î» y, âˆ¥f yâˆ¥ ^ 2) :=
Î» x, (hf x).norm_sq

lemma differentiable.norm (hf : differentiable â„ f) (h0 : âˆ€ x, f x â‰  0) :
  differentiable â„ (Î» y, âˆ¥f yâˆ¥) :=
Î» x, (hf x).norm (h0 x)

lemma differentiable.dist (hf : differentiable â„ f) (hg : differentiable â„ g)
  (hne : âˆ€ x, f x â‰  g x) :
  differentiable â„ (Î» y, dist (f y) (g y)) :=
Î» x, (hf x).dist (hg x) (hne x)

lemma differentiable_within_at.norm_sq (hf : differentiable_within_at â„ f s x) :
  differentiable_within_at â„ (Î» y, âˆ¥f yâˆ¥ ^ 2) s x :=
(times_cont_diff_at_id.norm_sq.differentiable_at le_rfl).comp_differentiable_within_at x hf

lemma differentiable_within_at.norm (hf : differentiable_within_at â„ f s x) (h0 : f x â‰  0) :
  differentiable_within_at â„ (Î» y, âˆ¥f yâˆ¥) s x :=
((times_cont_diff_at_id.norm h0).differentiable_at le_rfl).comp_differentiable_within_at x hf

lemma differentiable_within_at.dist (hf : differentiable_within_at â„ f s x)
  (hg : differentiable_within_at â„ g s x) (hne : f x â‰  g x) :
  differentiable_within_at â„ (Î» y, dist (f y) (g y)) s x :=
by { simp only [dist_eq_norm], exact (hf.sub hg).norm (sub_ne_zero.2 hne) }

lemma differentiable_on.norm_sq (hf : differentiable_on â„ f s) :
  differentiable_on â„ (Î» y, âˆ¥f yâˆ¥ ^ 2) s :=
Î» x hx, (hf x hx).norm_sq

lemma differentiable_on.norm (hf : differentiable_on â„ f s) (h0 : âˆ€ x âˆˆ s, f x â‰  0) :
  differentiable_on â„ (Î» y, âˆ¥f yâˆ¥) s :=
Î» x hx, (hf x hx).norm (h0 x hx)

lemma differentiable_on.dist (hf : differentiable_on â„ f s) (hg : differentiable_on â„ g s)
  (hne : âˆ€ x âˆˆ s, f x â‰  g x) :
  differentiable_on â„ (Î» y, dist (f y) (g y)) s :=
Î» x hx, (hf x hx).dist (hg x hx) (hne x hx)

end deriv

section continuous

/-!
### Continuity of the inner product

Since the inner product is `â„`-smooth, it is continuous. We do not need a `[normed_space â„ E]`
structure to *state* this fact and its corollaries, so we introduce them in the proof instead.
-/

lemma continuous_inner : continuous (Î» p : E Ã— E, âŸªp.1, p.2âŸ«) :=
begin
  letI : inner_product_space â„ E := inner_product_space.is_R_or_C_to_real ğ•œ E,
  letI : is_scalar_tower â„ ğ•œ E := restrict_scalars.is_scalar_tower _ _ _,
  exact differentiable_inner.continuous
end

variables {Î± : Type*}

lemma filter.tendsto.inner {f g : Î± â†’ E} {l : filter Î±} {x y : E} (hf : tendsto f l (ğ“ x))
  (hg : tendsto g l (ğ“ y)) :
  tendsto (Î» t, âŸªf t, g tâŸ«) l (ğ“ âŸªx, yâŸ«) :=
(continuous_inner.tendsto _).comp (hf.prod_mk_nhds hg)

variables [topological_space Î±] {f g : Î± â†’ E} {x : Î±} {s : set Î±}

include ğ•œ

lemma continuous_within_at.inner (hf : continuous_within_at f s x)
  (hg : continuous_within_at g s x) :
  continuous_within_at (Î» t, âŸªf t, g tâŸ«) s x :=
hf.inner hg

lemma continuous_at.inner (hf : continuous_at f x) (hg : continuous_at g x) :
  continuous_at (Î» t, âŸªf t, g tâŸ«) x :=
hf.inner hg

lemma continuous_on.inner (hf : continuous_on f s) (hg : continuous_on g s) :
  continuous_on (Î» t, âŸªf t, g tâŸ«) s :=
Î» x hx, (hf x hx).inner (hg x hx)

lemma continuous.inner (hf : continuous f) (hg : continuous g) : continuous (Î» t, âŸªf t, g tâŸ«) :=
continuous_iff_continuous_at.2 $ Î» x, hf.continuous_at.inner hg.continuous_at

end continuous
