/-
Copyright (c) 2020 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/
import analysis.inner_product_space.projection
import analysis.normed_space.dual

/-!
# The FrÃ©chet-Riesz representation theorem

We consider inner product spaces, with base field over `â„` (the corresponding results for `â„‚`
will require the definition of conjugate-linear maps). We define `to_dual_map`, a linear isometric
embedding of `E` into its dual, which maps an element `x` of the space to `Î» y, âŸªx, yâŸ«`. We also
define `to_dual'` as the function taking taking a vector to its dual for a base field `ğ•œ`
with `[is_R_or_C ğ•œ]`; this is a function and not a linear map.

Finally, under the hypothesis of completeness (i.e., for Hilbert spaces), we upgrade this to
`to_dual`, a linear isometric *equivalence* of `E` onto its dual; that is, we establish the
surjectivity of `to_dual'`.  This is the FrÃ©chet-Riesz representation theorem: every element of the
dual of a Hilbert space `E` has the form `Î» u, âŸªx, uâŸ«` for some `x : E`.

## References

* [M. Einsiedler and T. Ward, *Functional Analysis, Spectral Theory, and Applications*]
  [EinsiedlerWard2017]

## Tags

dual, FrÃ©chet-Riesz
-/

noncomputable theory
open_locale classical
universes u v

namespace inner_product_space
open is_R_or_C continuous_linear_map

section is_R_or_C

variables (ğ•œ : Type*)
variables {E : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ E]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y
local postfix `â€ `:90 := star_ring_aut

/--
Given some `x` in an inner product space, we can define its dual as the continuous linear map
`Î» y, âŸªx, yâŸ«`. Consider using `to_dual` or `to_dual_map` instead in the real case.
-/
def to_dual' : E â†’+ normed_space.dual ğ•œ E :=
{ to_fun := Î» x, linear_map.mk_continuous
  { to_fun := Î» y, âŸªx, yâŸ«,
    map_add' := Î» _ _, inner_add_right,
    map_smul' := Î» _ _, inner_smul_right }
  âˆ¥xâˆ¥
  (Î» y, by { rw [is_R_or_C.norm_eq_abs], exact abs_inner_le_norm _ _ }),
  map_zero' := by { ext z, simp },
  map_add' := Î» x y, by { ext z, simp [inner_add_left] } }

@[simp] lemma to_dual'_apply {x y : E} : to_dual' ğ•œ x y = âŸªx, yâŸ« := rfl

/-- In an inner product space, the norm of the dual of a vector `x` is `âˆ¥xâˆ¥` -/
@[simp] lemma norm_to_dual'_apply (x : E) : âˆ¥to_dual' ğ•œ xâˆ¥ = âˆ¥xâˆ¥ :=
begin
  refine le_antisymm _ _,
  { exact linear_map.mk_continuous_norm_le _ (norm_nonneg _) _ },
  { cases eq_or_lt_of_le (norm_nonneg x) with h h,
    { have : x = 0 := norm_eq_zero.mp (eq.symm h),
      simp [this] },
    { refine (mul_le_mul_right h).mp _,
      calc âˆ¥xâˆ¥ * âˆ¥xâˆ¥ = âˆ¥xâˆ¥ ^ 2 : by ring
      ... = re âŸªx, xâŸ« : norm_sq_eq_inner _
      ... â‰¤ abs âŸªx, xâŸ« : re_le_abs _
      ... = âˆ¥to_dual' ğ•œ x xâˆ¥ : by simp [norm_eq_abs]
      ... â‰¤ âˆ¥to_dual' ğ•œ xâˆ¥ * âˆ¥xâˆ¥ : le_op_norm (to_dual' ğ•œ x) x } }
end

variables (E)

lemma to_dual'_isometry : isometry (@to_dual' ğ•œ E _ _) :=
add_monoid_hom.isometry_of_norm _ (norm_to_dual'_apply ğ•œ)

/--
FrÃ©chet-Riesz representation: any `â„“` in the dual of a Hilbert space `E` is of the form
`Î» u, âŸªy, uâŸ«` for some `y : E`, i.e. `to_dual'` is surjective.
-/
lemma to_dual'_surjective [complete_space E] : function.surjective (@to_dual' ğ•œ E _ _) :=
begin
  intros â„“,
  set Y := ker â„“ with hY,
  by_cases htriv : Y = âŠ¤,
  { have hâ„“ : â„“ = 0,
    { have h' := linear_map.ker_eq_top.mp htriv,
      rw [â†coe_zero] at h',
      apply coe_injective,
      exact h' },
    exact âŸ¨0, by simp [hâ„“]âŸ© },
  { have Ycomplete := is_complete_ker â„“,
    rw [â† submodule.orthogonal_eq_bot_iff Ycomplete, â†hY] at htriv,
    change Yá—® â‰  âŠ¥ at htriv,
    rw [submodule.ne_bot_iff] at htriv,
    obtain âŸ¨z : E, hz : z âˆˆ Yá—®, z_ne_0 : z â‰  0âŸ© := htriv,
    refine âŸ¨((â„“ z)â€  / âŸªz, zâŸ«) â€¢ z, _âŸ©,
    ext x,
    have hâ‚ : (â„“ z) â€¢ x - (â„“ x) â€¢ z âˆˆ Y,
    { rw [mem_ker, map_sub, map_smul, map_smul, algebra.id.smul_eq_mul, algebra.id.smul_eq_mul,
          mul_comm],
      exact sub_self (â„“ x * â„“ z) },
    have hâ‚‚ : (â„“ z) * âŸªz, xâŸ« = (â„“ x) * âŸªz, zâŸ«,
    { have hâ‚ƒ := calc
        0    = âŸªz, (â„“ z) â€¢ x - (â„“ x) â€¢ zâŸ«       : by { rw [(Y.mem_orthogonal' z).mp hz], exact hâ‚ }
         ... = âŸªz, (â„“ z) â€¢ xâŸ« - âŸªz, (â„“ x) â€¢ zâŸ«  : by rw [inner_sub_right]
         ... = (â„“ z) * âŸªz, xâŸ« - (â„“ x) * âŸªz, zâŸ«  : by simp [inner_smul_right],
      exact sub_eq_zero.mp (eq.symm hâ‚ƒ) },
    have hâ‚„ := calc
      âŸª((â„“ z)â€  / âŸªz, zâŸ«) â€¢ z, xâŸ« = (â„“ z) / âŸªz, zâŸ« * âŸªz, xâŸ«
            : by simp [inner_smul_left, ring_equiv.map_div, conj_conj]
                            ... = (â„“ z) * âŸªz, xâŸ« / âŸªz, zâŸ«
            : by rw [â†div_mul_eq_mul_div]
                            ... = (â„“ x) * âŸªz, zâŸ« / âŸªz, zâŸ«
            : by rw [hâ‚‚]
                            ... = â„“ x
            : begin
                have : âŸªz, zâŸ« â‰  0,
                { change z = 0 â†’ false at z_ne_0,
                  rwa â†inner_self_eq_zero at z_ne_0 },
                field_simp [this]
              end,
    exact hâ‚„ }
end

end is_R_or_C

section real

variables (F : Type*) [inner_product_space â„ F]

/-- In a real inner product space `F`, the function that takes a vector `x` in `F` to its dual
`Î» y, âŸªx, yâŸ«` is an isometric linear embedding. If the space is complete (i.e. is a Hilbert space),
consider using `to_dual` instead. -/
-- TODO extend to `is_R_or_C` (requires a definition of conjugate linear maps)
def to_dual_map : F â†’â‚—áµ¢[â„] (normed_space.dual â„ F) :=
{ to_fun := to_dual' â„,
  map_add' := Î» x y, by { ext, simp [inner_add_left] },
  map_smul' := Î» c x, by { ext, simp [inner_smul_left] },
  norm_map' := norm_to_dual'_apply â„ }

variables {F}

@[simp] lemma to_dual_map_apply {x y : F} : to_dual_map F x y = âŸªx, yâŸ«_â„ := rfl

variables (F) [complete_space F]

/--
FrÃ©chet-Riesz representation: If `F` is a Hilbert space, the function that takes a vector in `F` to
its dual is an isometric linear equivalence.  -/
def to_dual : F â‰ƒâ‚—áµ¢[â„] (normed_space.dual â„ F) :=
linear_isometry_equiv.of_surjective (to_dual_map F) (to_dual'_surjective â„ F)

variables {F}

@[simp] lemma to_dual_apply {x y : F} : to_dual F x y = âŸªx, yâŸ«_â„ := rfl

end real

end inner_product_space
