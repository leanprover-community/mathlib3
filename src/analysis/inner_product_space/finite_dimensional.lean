/-
Copyright (c) 2023 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import linear_algebra.invariant_submodule
import analysis.inner_product_space.adjoint
import analysis.inner_product_space.spectrum

/-!
# Finite-dimensional inner product spaces

In this file, we prove some results in finite-dimensional inner product spaces.

## Notation

This file uses the local notation `P _` for `orthogonal_projection _`
and `â†¥P _` for the extended orthogonal projection `orthogonal_projection' _`.

We let `V` be an inner product space over `ğ•œ`.
-/

variables {ğ•œ V : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ V]

local notation `P` := orthogonal_projection
local notation `â†¥P` := orthogonal_projection'

-- the extended orthogonal projection is an invariant subspace
lemma submodule.invariant_orthogonal_projection' (U : submodule ğ•œ V) [complete_space U] :
  U.invariant_under (â†¥P U) := Î» x hx, set_like.coe_mem (P U x : U)

/-- if `U` is `T` invariant, then `(P U).comp T.comp (P U) = T.comp (P U)`
where `P U` is `orthogonal_projection U` -/
lemma submodule.invariant_under_imp_ortho_proj_comp_T_comp_ortho_proj_eq_T_comp_ortho_proj
  (U : submodule ğ•œ V) [complete_space U] (T : V â†’â‚—[ğ•œ] V)
  (h : U.invariant_under T) (x : V) : â†‘(P U (T â†‘(P U x))) = T â†‘(P U x) :=
by rw [orthogonal_projection_eq_linear_proj' x,
       â† U.proj_comp_self_comp_proj_eq_of_invariant_under _ _ _ h _,
       orthogonal_projection_mem_subspace_eq_self]

/-- if `(P U).comp T.comp (P U) = T.comp (P U)`, then `U` is `T` invariant,
where `P U` is `orthogonal_projection U` -/
lemma submodule.ortho_proj_comp_T_comp_ortho_proj_eq_T_comp_ortho_proj_imp_invariant
  (U : submodule ğ•œ V) [complete_space U] (T : V â†’â‚—[ğ•œ] V)
  (h : âˆ€ x : V, â†‘(P U (T â†‘(P U x))) = T â†‘(P U x)) : U.invariant_under T :=
by { simp_rw [orthogonal_projection_eq_linear_proj'] at h,
     exact submodule.invariant_under_of_proj_comp_self_comp_proj_eq _ _ _ T h, }

lemma submodule.invariant_under_iff_ortho_proj_comp_T_comp_ortho_proj_eq_T_comp_ortho_proj
  (U : submodule ğ•œ V) [complete_space U] (T : V â†’â‚—[ğ•œ] V) :
  U.invariant_under T â†” âˆ€ x : V, â†‘(P U (T â†‘(P U x))) = T â†‘(P U x) :=
âŸ¨Î» h, submodule.invariant_under_imp_ortho_proj_comp_T_comp_ortho_proj_eq_T_comp_ortho_proj _ _ h,
 Î» h, submodule.ortho_proj_comp_T_comp_ortho_proj_eq_T_comp_ortho_proj_imp_invariant _ _ hâŸ©

/-- `U,Uá—®` are `T` invariant if and only if `commute (P U) T`,
where `P U` is `orthogonal_projection U` -/
lemma submodule.invariant_under_and_ortho_invariant_iff_ortho_proj_and_T_commute
  (U : submodule ğ•œ V) [complete_space U] (T : V â†’â‚—[ğ•œ] V) :
  (U.invariant_under T âˆ§ Uá—®.invariant_under T) â†” commute â†‘(â†¥P U) T :=
by rw [orthogonal_projection'_eq_linear_proj,
       U.compl_invariant_under_iff_linear_proj_and_T_commute]

noncomputable instance linear_map.is_invertible_of_invertible_continuous_linear_map
  (T : V â†’L[ğ•œ] V) [invertible T] : invertible (T : V â†’â‚—[ğ•œ] V) :=
begin
  have : âˆ€ S T : V â†’L[ğ•œ] V, â†‘S * (T : V â†’â‚—[ğ•œ] V) = â†‘(S*T) := Î» S T, rfl,
  use T.inverse; rw [this],
  rw continuous_linear_map.inv_mul_self, refl,
  rw continuous_linear_map.mul_inv_self, refl,
end

lemma linear_map.is_inv_of_eq_inverse_continuous_linear_map (T : V â†’L[ğ•œ] V) [invertible T] :
  â…Ÿ(T : V â†’â‚—[ğ•œ] V) = T.inverse := rfl

/-- `commute (P U) T` if and only if `Tâ»Â¹.comp (P U).comp T = P U`,
where `P U` is `orthogonal_projection U` -/
lemma ortho_proj_and_T_commute_iff_Tinv_comp_ortho_proj_comp_T_eq_ortho_proj
  (U : submodule ğ•œ V) [complete_space U] (T : V â†’L[ğ•œ] V) [invertible T] :
  commute (â†¥P U) T â†” T.inverse.comp ((â†¥P U).comp T) = â†¥P U :=
begin
  simp_rw [commute, semiconj_by, continuous_linear_map.ext_iff,
           continuous_linear_map.mul_apply, continuous_linear_map.comp_apply,
           â† continuous_linear_map.coe_coe,
           orthogonal_projection'_eq_linear_proj,
           â† linear_map.mul_apply, â† linear_map.ext_iff],
  rw [â† semiconj_by, â† commute,
      submodule.commutes_with_linear_proj_iff_linear_proj_eq U Uá—® _ _],
  refl,
end

/-- `Tâ»Â¹ * (P U) * T = P U` if and only if `T(U) = U` and `T(Uá—®) = Uá—®`,
where `P U` is `orthogonal_projection U` -/
theorem T_inv_P_U_T_eq_P_U_iff_image_T_of_U_eq_U_and_image_T_of_U_ortho_eq_U_ortho
  [finite_dimensional ğ•œ V] (U : submodule ğ•œ V) (T : V â†’L[ğ•œ] V) [invertible T] :
  T.inverse.comp ((â†¥P U).comp T) = â†¥P U â†” T '' U = U âˆ§ T '' Uá—® = Uá—® :=
by simp_rw [continuous_linear_map.ext_iff, continuous_linear_map.comp_apply,
            â† continuous_linear_map.coe_coe _, orthogonal_projection'_eq_linear_proj',
            â† linear_map.comp_apply, â† linear_map.ext_iff,
            â† linear_map.is_inv_of_eq_inverse_continuous_linear_map,
            submodule.inv_linear_proj_comp_map_eq_linear_proj_iff_images_eq]

/-- `U` is `T` invariant if and only if `Uá—®` is `T.adjoint` invariant -/
theorem submodule.invariant_under_iff_ortho_adjoint_invariant
  [finite_dimensional ğ•œ V] (U : submodule ğ•œ V) (T : V â†’â‚—[ğ•œ] V) :
  submodule.invariant_under U T â†” submodule.invariant_under Uá—® T.adjoint :=
begin
  suffices : âˆ€ U : submodule ğ•œ V, âˆ€ T : V â†’â‚—[ğ•œ] V,
   submodule.invariant_under U T â†’ submodule.invariant_under Uá—® T.adjoint,
  { refine âŸ¨this U T, _âŸ©,
    intro h,
    rw [â† linear_map.adjoint_adjoint T, â† submodule.orthogonal_orthogonal U],
    apply this,
    exact h, },
  clear U T,
  simp only [ submodule.invariant_under_iff, set_like.mem_coe,
              set.image_subset_iff, set.subset_def, set.mem_image,
              forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚ ],
  intros U T h x hx y hy,
  rw linear_map.adjoint_inner_right,
  apply (submodule.mem_orthogonal U x).mp hx,
  apply h y hy,
end

/-- `T` is self adjoint implies
`U` is `T` invariant if and only if `Uá—®` is `T` invariant -/
lemma is_self_adjoint.submodule_invariant_iff_ortho_submodule_invariant
  [finite_dimensional ğ•œ V] (U : submodule ğ•œ V) (T : V â†’â‚—[ğ•œ] V) (h : is_self_adjoint T) :
  submodule.invariant_under U T â†” submodule.invariant_under Uá—® T :=
by rw [ submodule.invariant_under_iff_ortho_adjoint_invariant,
        linear_map.is_self_adjoint_iff'.mp h ]

/-- `T.ker = (T.adjoint.range)á—®` -/
lemma ker_is_ortho_adjoint_range {W : Type*} [finite_dimensional ğ•œ V]
  [inner_product_space ğ•œ W] [finite_dimensional ğ•œ W] (T : V â†’â‚—[ğ•œ] W) :
  T.ker = (T.adjoint.range)á—® :=
begin
  ext,
  simp only [linear_map.mem_ker, submodule.mem_orthogonal,
             linear_map.mem_range, forall_exists_index,
             forall_apply_eq_imp_iff', linear_map.adjoint_inner_left],
  exact âŸ¨ Î» h, by simp only [h, inner_zero_right, forall_const],
          Î» h, inner_self_eq_zero.mp (h (T x))âŸ©,
end
lemma continuous_linear_map.ker_is_eq_ortho_adjoint_range {W : Type*} [inner_product_space ğ•œ W]
  [complete_space V] [complete_space W] (T : V â†’L[ğ•œ] W) :
  T.ker = (T.adjoint.range)á—® :=
begin
  ext,
  simp_rw [submodule.mem_orthogonal, linear_map.mem_range,
           linear_map.mem_ker, continuous_linear_map.to_linear_map_eq_coe,
           continuous_linear_map.coe_coe, forall_exists_index, forall_apply_eq_imp_iff',
           continuous_linear_map.adjoint_inner_left],
  exact âŸ¨ Î» h, by simp_rw [h, inner_zero_right, forall_const],
          Î» h, inner_self_eq_zero.mp (h _)âŸ©,
end

/-- given any idempotent operator `T âˆˆ L(V)`, then `is_compl T.ker T.range`,
in other words, there exists unique `v âˆˆ T.ker` and `w âˆˆ T.range` such that `x = v + w` -/
lemma linear_map.is_idempotent.is_compl_range_ker {V R : Type*} [ring R] [add_comm_group V]
  [module R V] (T : V â†’â‚—[R] V) (h : is_idempotent_elem T) :
  is_compl T.ker T.range :=
begin
 split,
   { rw disjoint_iff,
     ext,
     simp only [submodule.mem_bot, submodule.mem_inf, linear_map.mem_ker,
                linear_map.mem_range, continuous_linear_map.to_linear_map_eq_coe,
                continuous_linear_map.coe_coe],
     split,
       { intro h',
         cases h'.2 with y hy,
         rw [â† hy, â† is_idempotent_elem.eq h, linear_map.mul_apply, hy],
         exact h'.1, },
       { intro h',
         rw [h', map_zero],
         simp only [eq_self_iff_true, true_and],
         use x,
         simp only [h', map_zero, eq_self_iff_true], }, },
    { suffices : âˆ€ x : V, âˆƒ v : T.ker, âˆƒ w : T.range, x = v + w,
        { rw [codisjoint_iff, â† submodule.add_eq_sup],
          ext,
          rcases this x with âŸ¨v,w,hvwâŸ©,
          simp only [submodule.mem_top, iff_true, hvw],
          apply submodule.add_mem_sup (set_like.coe_mem v) (set_like.coe_mem w), },
      intro x,
      use (x-(T x)), rw [linear_map.mem_ker, map_sub,
                         â† linear_map.mul_apply, is_idempotent_elem.eq h, sub_self],
      use (T x), rw [linear_map.mem_range]; simp only [exists_apply_eq_apply],
      simp only [submodule.coe_mk, sub_add_cancel], }
end

/-- idempotent `T` is self-adjoint if and only if `(T.ker)á—® = T.range` -/
theorem linear_map.is_idempotent_is_self_adjoint_iff_ker_is_ortho_to_range
  [inner_product_space â„‚ V] [finite_dimensional â„‚ V] (T : V â†’â‚—[â„‚] V) (h : is_idempotent_elem T) :
  is_self_adjoint T â†” (T.ker)á—® = T.range :=
begin
  rw linear_map.is_self_adjoint_iff',
  split,
    { intros l, rw [ker_is_ortho_adjoint_range, submodule.orthogonal_orthogonal],
      revert l, exact congr_arg linear_map.range, },
    { intro h1, apply eq_of_sub_eq_zero,
      simp only [â† inner_map_self_eq_zero],
      intro x,
      obtain âŸ¨v, w, hvw, huniqueâŸ© :=
        submodule.exists_unique_add_of_is_compl
        (linear_map.is_idempotent.is_compl_range_ker T h) x,
      simp only [linear_map.sub_apply, inner_sub_left, linear_map.adjoint_inner_left],
      cases (set_like.coe_mem w) with y hy,
      rw [â† hvw, map_add, linear_map.mem_ker.mp (set_like.coe_mem v),
          â† hy, â† linear_map.mul_apply, is_idempotent_elem.eq h, zero_add, hy, inner_add_left,
          inner_add_right, â† inner_conj_sym â†‘w â†‘v, (submodule.mem_orthogonal T.ker â†‘w).mp
            (by { rw h1, exact set_like.coe_mem w }) v (set_like.coe_mem v),
          map_zero, zero_add, sub_self], },
end

/-- `U` and `W` are mutually orthogonal if and only if `(P U).comp (P W) = 0`,
where `P U` is `orthogonal_projection U` -/
lemma ortho_spaces_iff_ortho_proj_comp_ortho_proj_eq_0 (U W : submodule ğ•œ V)
  [complete_space U] [complete_space W] :
  (âˆ€ x y, x âˆˆ U âˆ§ y âˆˆ W â†’ @inner ğ•œ _ _ x y = 0) â†” (â†¥P U).comp (â†¥P W) = 0 :=
begin
  split,
  { intros h,
    ext v,
    rw [continuous_linear_map.comp_apply, continuous_linear_map.zero_apply,
        â† inner_self_eq_zero, orthogonal_projection'_apply, orthogonal_projection'_apply,
        â† inner_orthogonal_projection_left_eq_right,
        orthogonal_projection_mem_subspace_eq_self],
    apply h, simp only [submodule.coe_mem, and_self], },
  { intros h x y hxy,
    rw [â† orthogonal_projection_eq_self_iff.mpr hxy.1,
        â† orthogonal_projection_eq_self_iff.mpr hxy.2,
        inner_orthogonal_projection_left_eq_right,
        â† orthogonal_projection'_apply, â† orthogonal_projection'_apply,
        â† continuous_linear_map.comp_apply, h,
        continuous_linear_map.zero_apply, inner_zero_right], }
end

section is_star_normal
open linear_map

/-- linear map `is_star_normal` if and only if it commutes with its adjoint -/
lemma linear_map.is_star_normal_iff_adjoint [finite_dimensional ğ•œ V] (T : V â†’â‚—[ğ•œ] V) :
  is_star_normal T â†” commute T T.adjoint :=
by { rw commute.symm_iff, exact âŸ¨Î» hT, hT.star_comm_self, is_star_normal.mkâŸ©, }

lemma continuous_linear_map.is_star_normal_iff_adjoint [complete_space V] (T : V â†’L[ğ•œ] V) :
  is_star_normal T â†” commute T T.adjoint :=
by { rw commute.symm_iff, exact âŸ¨Î» hT, hT.star_comm_self, is_star_normal.mkâŸ©, }

/-- `T` is normal if and only if `âˆ€ v, â€–T vâ€– = â€–T.adjoint vâ€–` -/
lemma linear_map.is_star_normal.norm_eq_adjoint [inner_product_space â„‚ V]
  [finite_dimensional â„‚ V] (T : V â†’â‚—[â„‚] V) :
  is_star_normal T â†” âˆ€ v : V, â€–T vâ€– = â€–T.adjoint vâ€– :=
begin
  rw [T.is_star_normal_iff_adjoint, commute, semiconj_by, â† sub_eq_zero],
  simp_rw [â† inner_map_self_eq_zero, sub_apply, inner_sub_left, mul_apply,
           adjoint_inner_left, inner_self_eq_norm_sq_to_K],
  simp_rw [â† adjoint_inner_right T, inner_self_eq_norm_sq_to_K, sub_eq_zero,
           â† sq_eq_sq (norm_nonneg _) (norm_nonneg _)],
  norm_cast,
  simp_rw eq_comm,
end

lemma continuous_linear_map.is_star_normal.norm_eq_adjoint [inner_product_space â„‚ V]
  [complete_space V] (T : V â†’L[â„‚] V) :
  is_star_normal T â†” âˆ€ v : V, â€–T vâ€– = â€–T.adjoint vâ€– :=
begin
  rw [T.is_star_normal_iff_adjoint, commute, semiconj_by, â† sub_eq_zero],
  simp_rw [continuous_linear_map.ext_iff, â† continuous_linear_map.coe_coe,
           continuous_linear_map.coe_sub, â† linear_map.ext_iff,
           continuous_linear_map.coe_zero, â† inner_map_self_eq_zero,
           sub_apply, inner_sub_left, continuous_linear_map.coe_coe,
           continuous_linear_map.mul_apply, continuous_linear_map.adjoint_inner_left,
           inner_self_eq_norm_sq_to_K, â† continuous_linear_map.adjoint_inner_right T,
           inner_self_eq_norm_sq_to_K, sub_eq_zero,
           â† sq_eq_sq (norm_nonneg _) (norm_nonneg _), eq_comm],
  norm_cast,
end

/-- if `T` is normal, then `T.ker = T.adjoint.ker` -/
lemma linear_map.is_star_normal.ker_eq_ker_adjoint [inner_product_space â„‚ V]
  [finite_dimensional â„‚ V] (T : V â†’â‚—[â„‚] V) (h : is_star_normal T) : T.ker = T.adjoint.ker :=
by ext; rw [mem_ker, mem_ker, â† norm_eq_zero, iff.comm,
            â† norm_eq_zero, â† (linear_map.is_star_normal.norm_eq_adjoint T).mp h]
/-- if `T` is normal, then `T.range = T.adjoint.range` -/
lemma linear_map.is_star_normal.range_eq_range_adjoint [inner_product_space â„‚ V]
  [finite_dimensional â„‚ V] (T : V â†’â‚—[â„‚] V) (h : is_star_normal T) : T.range = T.adjoint.range :=
by rw [â† submodule.orthogonal_orthogonal T.adjoint.range, â† ker_is_ortho_adjoint_range,
       linear_map.is_star_normal.ker_eq_ker_adjoint T h,
       ker_is_ortho_adjoint_range, adjoint_adjoint,
       submodule.orthogonal_orthogonal]

lemma continuous_linear_map.is_star_normal.ker_eq_ker_adjoint [inner_product_space â„‚ V]
  [complete_space V] (T : V â†’L[â„‚] V) (h : is_star_normal T) : T.ker = T.adjoint.ker :=
by { ext, simp_rw [mem_ker, continuous_linear_map.to_linear_map_eq_coe,
                   continuous_linear_map.coe_coe],
          rw [â† norm_eq_zero, iff.comm, â† norm_eq_zero,
              â† (continuous_linear_map.is_star_normal.norm_eq_adjoint T).mp h], }

lemma continuous_linear_map.is_idempotent_elem.to_linear_map {E R : Type*} [ring R]
  [add_comm_monoid E] [module R E] [topological_space E] (T : E â†’L[R] E) :
  is_idempotent_elem T â†” is_idempotent_elem T.to_linear_map :=
begin
  simp_rw [continuous_linear_map.to_linear_map_eq_coe, is_idempotent_elem,
           continuous_linear_map.ext_iff, linear_map.ext_iff, continuous_linear_map.coe_coe],
  refl,
end

theorem continuous_linear_map.is_idempotent.is_self_adjoint_iff_ker_is_ortho_to_range
  [inner_product_space â„‚ V] [complete_space V] (T : V â†’L[â„‚] V) (h : is_idempotent_elem T) :
  is_self_adjoint T â†” T.ker = (T.range)á—® :=
begin
  split,
  { intros l, rw [â† continuous_linear_map.adjoint_adjoint T,
                  â† continuous_linear_map.ker_is_eq_ortho_adjoint_range,
                  continuous_linear_map.adjoint_adjoint],
    exact continuous_linear_map.is_star_normal.ker_eq_ker_adjoint T (l.is_star_normal), },
  { intro h1,
    rw continuous_linear_map.is_self_adjoint_iff',
    apply eq_of_sub_eq_zero,
    simp_rw [continuous_linear_map.ext_iff, â† continuous_linear_map.coe_coe,
             continuous_linear_map.coe_sub, â† ext_iff,
             continuous_linear_map.coe_zero, â† inner_map_self_eq_zero],
    intro x,
    rw [continuous_linear_map.is_idempotent_elem.to_linear_map,
        continuous_linear_map.to_linear_map_eq_coe] at h,
    obtain âŸ¨v, w, hvw, huniqueâŸ© :=
      submodule.exists_unique_add_of_is_compl
        (linear_map.is_idempotent.is_compl_range_ker â†‘T h) x,
    simp only [linear_map.sub_apply, inner_sub_left, linear_map.adjoint_inner_left],
    cases (set_like.coe_mem w) with y hy,
    simp_rw [continuous_linear_map.coe_coe, continuous_linear_map.adjoint_inner_left,
             â† continuous_linear_map.coe_coe,
             â† hvw, map_add, linear_map.mem_ker.mp (set_like.coe_mem v),
             â† hy, â† linear_map.mul_apply, is_idempotent_elem.eq h, zero_add, hy, inner_add_left,
             inner_add_right, â† inner_conj_sym â†‘w â†‘v,
             (submodule.mem_orthogonal T.ker â†‘w).mp
               (by { rw h1, intros y hy, rw inner_eq_zero_sym, exact hy w (set_like.coe_mem w), })
                 v (set_like.coe_mem v),
             map_zero, zero_add, sub_self], },
end

open_locale complex_conjugate
open module.End
/-- if `T` is normal, then `âˆ€ x : V, x âˆˆ eigenspace T Î¼ â†” x âˆˆ eigenspace T.adjoint (conj Î¼)` -/
lemma linear_map.is_star_normal.eigenvec_in_eigenspace_iff_eigenvec_in_adjoint_conj_eigenspace
  [inner_product_space â„‚ V] [finite_dimensional â„‚ V] (T : V â†’â‚—[â„‚] V) (h : is_star_normal T)
  (Î¼ : â„‚) : âˆ€ x : V, x âˆˆ eigenspace T Î¼ â†” x âˆˆ eigenspace T.adjoint (conj Î¼) :=
begin
  suffices : âˆ€ T : V â†’â‚—[â„‚] V, is_star_normal T â†’
    âˆ€ Î¼ : â„‚, âˆ€ v : V, v âˆˆ eigenspace T Î¼ â†’ v âˆˆ eigenspace T.adjoint (conj Î¼),
  { intro v, refine âŸ¨this T h Î¼ v, _âŸ©,
    intro hv, rw [â† adjoint_adjoint T, â† is_R_or_C.conj_conj Î¼],
    apply this _ _ _ _ hv, exact is_star_normal_star_self, },
  clear h Î¼ T,
  intros T h Î¼ v hv,
  have t1 : (T - Î¼â€¢1) v = 0,
  { rw [sub_apply, smul_apply, one_apply, sub_eq_zero],
    exact mem_eigenspace_iff.mp hv, },
  suffices : (T.adjoint - (conj Î¼)â€¢1) v = 0,
  { rw [mem_eigenspace_iff, â† sub_eq_zero],
    rw [sub_apply, smul_apply, one_apply] at this, exact this, },
  rw â† norm_eq_zero,
  have nh : is_star_normal (T-Î¼â€¢1),
  { apply is_star_normal.mk,
    rw [star_sub, star_smul, is_R_or_C.star_def, star_one, commute, semiconj_by],
    simp only [sub_mul, mul_sub, commute.eq h.star_comm_self],
    simp only [smul_one_mul, smul_smul, mul_smul_comm, mul_one],
    rw [mul_comm, sub_sub_sub_comm], },
  have : (T-Î¼â€¢1).adjoint = T.adjoint - (conj Î¼)â€¢1 :=
  by simp only [â† star_eq_adjoint, star_sub, star_smul, is_R_or_C.star_def, star_one],
  rw [â† this, â† (linear_map.is_star_normal.norm_eq_adjoint (T-Î¼â€¢1)).mp nh, t1, norm_zero],
end
end is_star_normal

/-- `T` is injective if and only if `T.adjoint` is surjective  -/
lemma linear_map.injective_iff_adjoint_surjective
  {W : Type*} [inner_product_space ğ•œ W] [finite_dimensional ğ•œ W]
  [finite_dimensional ğ•œ V] (T : V â†’â‚—[ğ•œ] W) :
  function.injective T â†” function.surjective T.adjoint :=
by rw [â† linear_map.ker_eq_bot, â† linear_map.range_eq_top,
       ker_is_ortho_adjoint_range, submodule.orthogonal_eq_bot_iff]
