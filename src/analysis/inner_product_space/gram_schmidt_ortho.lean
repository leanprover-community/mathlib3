/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao, Kevin Buzzard, Alexander Bentkamp
-/

import analysis.inner_product_space.projection

/-!
# Gram-Schmidt Orthogonalization and Orthonormalization

In this file we introduce Gram-Schmidt Orthogonalization and Orthonormalization.

The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span.

## Main results

- `gram_schmidt` : the Gram-Schmidt process
- `gram_schmidt_orthogonal` :
  `gram_schmidt` produces an orthogonal system of vectors.
- `span_gram_schmidt` :
  `gram_schmidt` preserves span of vectors.
- `gram_schmidt_ne_zero` :
  If the first `n` input vectors of `gram_schmidt` are linearly independent,
  then the first `n` output vectors are non-zero.
- `gram_schmidt_normed` :
  the normalized `gram_schmidt` (i.e each vector in `gram_schmidt_normed` has unit length.)
- `gram_schmidt_orthornormal` :
  `gram_schmidt_normed` produces an orthornormal system of vectors.

## TODO
  Construct a version with an orthonormal basis from Gram-Schmidt process.
-/

open_locale big_operators

variables (ğ•œ : Type*) {E : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ E]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

/-- The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span. -/
noncomputable def gram_schmidt (f : â„• â†’ E) : â„• â†’ E
| n := f n - âˆ‘ i : fin n, orthogonal_projection (ğ•œ âˆ™ gram_schmidt i) (f n)
using_well_founded {dec_tac := `[exact i.prop]}

noncomputable def gram_schmidt_fin {m : â„•} (f : fin m â†’ E) : fin m â†’ E :=
  Î» i, have hm : fact (0 < m), from âŸ¨lt_of_le_of_lt (nat.zero_le _) i.2âŸ©,
    gram_schmidt ğ•œ (Î» j, f (@fin.of_nat' _ hm j)) i

/-- `gram_schmidt_def` turns the sum over `fin n` into a sum over `â„•`. -/
lemma gram_schmidt_def (f : â„• â†’ E) (n : â„•) :
  gram_schmidt ğ•œ f n = f n - âˆ‘ i in finset.range n,
    orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f n) :=
begin
  rw gram_schmidt,
  congr' 1,
  exact fin.sum_univ_eq_sum_range (Î» i,
    (orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f n) : E)) n,
end

-- TODO: move
lemma fin.of_nat'_coe {m : â„•} (n : fin m) :
  @fin.of_nat' _ âŸ¨lt_of_le_of_lt (nat.zero_le _) n.2âŸ© n = n :=
begin
  haveI hm : fact (0 < m), from âŸ¨lt_of_le_of_lt (nat.zero_le _) n.2âŸ©,
  ext,
  rw [fin.coe_of_nat_eq_mod', nat.mod_eq_of_lt],
  exact n.2,
end

-- TODO: move
lemma fin.cast_lt_cast_lt {m n : â„•} (i : fin n) (hm : i.val < m) (hn : i.val < n) :
  (i.cast_lt hm).cast_lt hn = i :=
by simp [fin.cast_lt]

-- TODO: move
lemma sum_fin_range_eq_sum_range {M : Type*} [add_comm_monoid M] (n : â„•) (f : â„• â†’ M) :
âˆ‘ i in finset.fin_range n, f i = âˆ‘ i in finset.range n, f i :=
begin
  apply finset.sum_bij (Î» (i : fin n) ih, i.val),
  exact Î» i ih, finset.mem_range.2 i.2,
  { intros, rw fin.coe_eq_val },
  exact Î» _ _ _ _, (fin.eq_iff_veq _ _).2,
  exact Î» i hi, âŸ¨âŸ¨i, finset.mem_range.1 hiâŸ©, finset.mem_fin_range _, rflâŸ©
end

lemma gram_schmidt_fin_def {m : â„•} (f : fin m â†’ E) (n : fin m) :
  gram_schmidt_fin ğ•œ f n = f n - âˆ‘ i in finset.fin_range n,
    orthogonal_projection (ğ•œ âˆ™ gram_schmidt_fin ğ•œ f (i.cast_lt (lt_trans i.2 n.2))) (f n) :=
begin
  haveI hm : fact (0 < m), from âŸ¨lt_of_le_of_lt (nat.zero_le _) n.2âŸ©,
  convert gram_schmidt_def ğ•œ (Î» (j : â„•), f (fin.of_nat' j)) n using 2,
  { rw [fin.of_nat'_coe] },
  { rw [â†sum_fin_range_eq_sum_range, fin.of_nat'_coe], refl }
end

lemma gram_schmidt_def' (f : â„• â†’ E) (n : â„•):
  f n = gram_schmidt ğ•œ f n + âˆ‘ i in finset.range n,
    orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f n) :=
by simp only [gram_schmidt_def, sub_add_cancel]

lemma gram_schmidt_fin_def' {m : â„•} (f : fin m â†’ E) (n : fin m) :
  f n = gram_schmidt_fin ğ•œ f n + âˆ‘ i in finset.fin_range n,
    orthogonal_projection (ğ•œ âˆ™ gram_schmidt_fin ğ•œ f (i.cast_lt (lt_trans i.2 n.2))) (f n) :=
by simp only [gram_schmidt_fin_def, sub_add_cancel]

@[simp] lemma gram_schmidt_zero (f : â„• â†’ E) :
  gram_schmidt ğ•œ f 0 = f 0 :=
by simp only [gram_schmidt, fintype.univ_of_is_empty, finset.sum_empty, sub_zero]

@[simp] lemma gram_schmidt_fin_zero {m : â„•} (f : fin m.succ â†’ E) :
  gram_schmidt_fin ğ•œ f 0 = f 0 :=
by { simp [gram_schmidt_fin, gram_schmidt_zero], refl }

/-- **Gram-Schmidt Orthogonalisation**:
`gram_schmidt` produces an orthogonal system of vectors. -/
theorem gram_schmidt_orthogonal (f : â„• â†’ E) {a b : â„•} (hâ‚€ : a â‰  b) :
  âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0 :=
begin
  suffices : âˆ€ a b : â„•, a < b â†’ âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0,
  { cases hâ‚€.lt_or_lt with ha hb,
    { exact this _ _ ha, },
    { rw inner_eq_zero_sym,
      exact this _ _ hb, }, },
  clear hâ‚€ a b,
  intros a b hâ‚€,
  induction b using nat.strong_induction_on with b ih generalizing a,
  simp only [gram_schmidt_def ğ•œ f b, inner_sub_right, inner_sum,
    orthogonal_projection_singleton, inner_smul_right],
  rw finset.sum_eq_single_of_mem a (finset.mem_range.mpr hâ‚€),
  { by_cases h : gram_schmidt ğ•œ f a = 0,
    { simp only [h, inner_zero_left, zero_div, zero_mul, sub_zero], },
    { rw [â† inner_self_eq_norm_sq_to_K, div_mul_cancel, sub_self],
      rwa [ne.def, inner_self_eq_zero], }, },
  simp_intros i hi hia only [finset.mem_range],
  simp only [mul_eq_zero, div_eq_zero_iff, inner_self_eq_zero],
  right,
  cases hia.lt_or_lt with hiaâ‚ hiaâ‚‚,
  { rw inner_eq_zero_sym,
    exact ih a hâ‚€ i hiaâ‚, },
  { exact ih i hi a hiaâ‚‚, },
end

/-- This is another version of `gram_schmidt_orthogonal` using `pairwise` instead. -/
theorem gram_schmidt_pairwise_orthogonal (f : â„• â†’ E) :
  pairwise (Î» a b, âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0) :=
@gram_schmidt_orthogonal ğ•œ _ _ _ f

theorem gram_schmidt_fin_orthogonal {m : â„•} (f : fin m â†’ E) {a b : fin m} (hâ‚€ : a â‰  b) :
  âŸªgram_schmidt_fin ğ•œ f a, gram_schmidt_fin ğ•œ f bâŸ« = 0 :=
gram_schmidt_orthogonal ğ•œ _ (Î» h, hâ‚€ ((fin.ext_iff _ _).2 h))

theorem gram_schmidt_fin_pairwise_orthogonal {m : â„•} (f : fin m â†’ E) :
  pairwise (Î» a b, âŸªgram_schmidt_fin ğ•œ f a, gram_schmidt_fin ğ•œ f bâŸ« = 0) :=
@gram_schmidt_fin_orthogonal ğ•œ _ _ _ _ f

open submodule set order

/-- `gram_schmidt` preserves span of vectors. -/
lemma span_gram_schmidt (f : â„• â†’ E) (c : â„•) :
  span ğ•œ (gram_schmidt ğ•œ f '' Iio c) = span ğ•œ (f '' Iio c) :=
begin
  induction c with c hc,
  { simp only [Iio, not_lt_zero', set_of_false, image_empty], },
  have hâ‚€ : âˆ€ b, b âˆˆ finset.range c â†’ gram_schmidt ğ•œ f b âˆˆ span ğ•œ (f '' Iio c),
  { simp_intros b hb only [finset.mem_range, nat.succ_eq_add_one],
    rw â† hc,
    refine subset_span _,
    simp only [mem_image, mem_Iio],
    refine âŸ¨b, by linarith, by reflâŸ©, },
  rw [â† nat.succ_eq_succ, Iio_succ_eq_insert],
  simp only [span_insert, image_insert_eq, hc],
  apply le_antisymm,
  { simp only [nat.succ_eq_succ,gram_schmidt_def ğ•œ f c, orthogonal_projection_singleton,
      sup_le_iff, span_singleton_le_iff_mem, le_sup_right, and_true],
    apply submodule.sub_mem _ _ _,
    { exact mem_sup_left (mem_span_singleton_self (f c)), },
    { exact submodule.sum_mem _ (Î» b hb, mem_sup_right (smul_mem _ _ (hâ‚€ b hb))), }, },
  { rw [gram_schmidt_def' ğ•œ f c],
    simp only [orthogonal_projection_singleton,
      sup_le_iff, span_singleton_le_iff_mem, le_sup_right, and_true],
    apply submodule.add_mem _ _ _,
    { exact mem_sup_left (mem_span_singleton_self (gram_schmidt ğ•œ f c)), },
    { exact submodule.sum_mem _ (Î» b hb, mem_sup_right (smul_mem _ _ (hâ‚€ b hb))), }, },
end

lemma fin.image_of_nat' (m : â„•) [h : fact (0 < m)] :
  (fin.of_nat' '' Iio m) = (set.univ : set (fin m)) :=
eq_univ_of_forall (Î» i, (mem_image _ _ _).2 âŸ¨i, mem_Iio.2 i.2, fin.of_nat'_coe _âŸ©)

lemma fin.range_coe (m : â„•) : range (Î» (i : fin m), (i : â„•)) = Iio m :=
by simp [Iio]

lemma span_gram_schmidt_fin {m : â„•} (f : fin m â†’ E) (c : â„•) :
  span ğ•œ (range (gram_schmidt_fin ğ•œ f)) = span ğ•œ (range f) :=
begin
  cases m,
  { simp only [matrix.range_empty] },
  { haveI : fact (0 < m.succ) := âŸ¨nat.zero_lt_succ mâŸ©,
    rw [gram_schmidt_fin, range_comp (gram_schmidt ğ•œ (Î» (j : â„•), f (fin.of_nat' j))) coe,
      fin.range_coe, â†image_univ, span_gram_schmidt ğ•œ (Î» i, f (fin.of_nat' i)) m.succ,
      image_comp f (Î» (x : â„•), fin.of_nat' x), fin.image_of_nat', image_univ] }
end

/-- If the input of the first `n + 1` vectors of `gram_schmidt` are linearly independent,
then the output of the first `n + 1` vectors are non-zero. -/
lemma gram_schmidt_ne_zero_aux (f : â„• â†’ E) (n : â„•)
  (hâ‚€ : linear_independent ğ•œ (f âˆ˜ (coe : fin n â†’ â„•))) :
    âˆ€ i (h : i < n), gram_schmidt ğ•œ f i â‰  0 :=
begin
  induction n with n hn,
  { intros, linarith },
  { intros i hi hâ‚,
    rw nat.succ_eq_add_one at hi,
    have hâ‚‚ := gram_schmidt_def' ğ•œ f i,
    simp only [nat.succ_eq_add_one, hâ‚, orthogonal_projection_singleton, zero_add] at hâ‚‚,
    have hâ‚ƒ : f i âˆˆ span ğ•œ (f '' Iio i),
    { rw [hâ‚‚, â† span_gram_schmidt ğ•œ f i],
      apply submodule.sum_mem _ _,
      simp_intros a ha only [finset.mem_range],
      apply submodule.smul_mem _ _ _,
      refine subset_span _,
      simp only [mem_image, mem_Iio],
      exact âŸ¨a, by linarith, by reflâŸ©, },
    change linear_independent ğ•œ (f âˆ˜ (coe : fin (n + 1) â†’ â„•)) at hâ‚€,
    have hâ‚„ : (i : fin (n + 1)) âˆ‰ (coe : fin (n + 1) â†’ â„•) â»Â¹' (Iio i),
    { simp only [mem_preimage, mem_Iio, not_le],
      rw [fin.coe_coe_of_lt, not_lt],
      exact hi },
    apply linear_independent.not_mem_span_image hâ‚€ hâ‚„,
    rw [image_comp, image_preimage_eq_inter_range],
    simp only [function.comp_app, subtype.range_coe_subtype],
    convert hâ‚ƒ,
    { exact fin.coe_coe_of_lt hi, },
    { simp only [inter_eq_left_iff_subset, Iio, set_of_subset_set_of],
      exact (Î» a ha, by linarith), }, },
end

/-- If the input of `gram_schmidt` is linearly independent, then the output is non-zero. -/
lemma gram_schmidt_ne_zero (f : â„• â†’ E) (hâ‚€ : linear_independent ğ•œ f) (n : â„•) :
  gram_schmidt ğ•œ f n â‰  0 :=
gram_schmidt_ne_zero_aux ğ•œ f (n + 1) (linear_independent.comp hâ‚€ _ (fin.coe_injective)) n (lt_succ n)

lemma gram_schmidt_fin_ne_zero {m : â„•} (f : fin m â†’ E)
  (hâ‚€ : linear_independent ğ•œ f) :
    âˆ€ i, gram_schmidt_fin ğ•œ f i â‰  0 :=
begin
  intro i,
  haveI : fact (0 < m) := âŸ¨lt_of_le_of_lt (nat.zero_le _) i.2âŸ©,
  have : linear_independent ğ•œ ((Î» (j : fin m), f (fin.of_nat' â†‘j))),
    by simpa only [fin.of_nat'_coe],
  exact gram_schmidt_ne_zero_aux ğ•œ (Î» (j : â„•), f (fin.of_nat' j)) m this i.1 i.2,
end

/-- the normalized `gram_schmidt`
(i.e each vector in `gram_schmidt_normed` has unit length.) -/
noncomputable def gram_schmidt_normed (f : â„• â†’ E) (n : â„•) : E :=
(âˆ¥gram_schmidt ğ•œ f nâˆ¥ : ğ•œ)â»Â¹ â€¢ (gram_schmidt ğ•œ f n)

noncomputable def gram_schmidt_normed_fin {m : â„•} (f : fin m â†’ E) (n : fin m) : E :=
  have hm : fact (0 < m), from âŸ¨lt_of_le_of_lt (nat.zero_le _) n.2âŸ©,
  gram_schmidt_normed ğ•œ (Î» i, f (@fin.of_nat' m hm i)) n

lemma gram_schmidt_normed_fin_def {m : â„•} (f : fin m â†’ E) (n : fin m) :
  gram_schmidt_normed_fin ğ•œ f n = (âˆ¥gram_schmidt_fin ğ•œ f nâˆ¥ : ğ•œ)â»Â¹ â€¢ (gram_schmidt_fin ğ•œ f n) :=
rfl

lemma gram_schmidt_normed_unit_length (f : â„• â†’ E) (n : â„•)
  (hâ‚€ : linear_independent ğ•œ f) :
    âˆ¥gram_schmidt_normed ğ•œ f nâˆ¥ = 1 :=
by simp only [gram_schmidt_ne_zero ğ•œ f hâ‚€,
  gram_schmidt_normed, norm_smul_inv_norm, ne.def, not_false_iff]

lemma gram_schmidt_normed_fin_unit_length {m : â„•} (f : fin m â†’ E) (n : fin m)
  (hâ‚€ : linear_independent ğ•œ f):
    âˆ¥gram_schmidt_normed_fin ğ•œ f nâˆ¥ = 1 :=
by simp only [gram_schmidt_fin_ne_zero ğ•œ f hâ‚€,
  gram_schmidt_normed_fin_def, norm_smul_inv_norm, ne.def, not_false_iff]

/-- **Gram-Schmidt Orthonormalization**:
`gram_schmidt_normed` produces an orthornormal system of vectors. -/
theorem gram_schmidt_orthonormal (f : â„• â†’ E) (hâ‚€ : linear_independent ğ•œ f) :
  orthonormal ğ•œ (gram_schmidt_normed ğ•œ f) :=
begin
  unfold orthonormal,
  split,
  { simp only [gram_schmidt_normed_unit_length, hâ‚€, forall_const], },
  { intros i j hij,
    simp only [gram_schmidt_normed, inner_smul_left, inner_smul_right, is_R_or_C.conj_inv,
      is_R_or_C.conj_of_real, mul_eq_zero, inv_eq_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero],
    repeat { right },
    exact gram_schmidt_orthogonal ğ•œ f hij, },
end

theorem gram_schmidt_fin_orthonormal {m : â„•} (f : fin m â†’ E) (hâ‚€ : linear_independent ğ•œ f) :
  orthonormal ğ•œ (gram_schmidt_normed_fin ğ•œ f) :=
begin
  unfold orthonormal,
  split,
  { intro i, simp only [gram_schmidt_normed_fin_unit_length, hâ‚€, forall_const] },
  { intros i j hij,
    simp only [gram_schmidt_normed_fin_def, inner_smul_left, inner_smul_right, is_R_or_C.conj_inv,
      is_R_or_C.conj_of_real, mul_eq_zero, inv_eq_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero],
    repeat { right },
    exact gram_schmidt_fin_orthogonal ğ•œ f hij, },
end
