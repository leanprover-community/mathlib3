/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao, Kevin Buzzard
-/

import analysis.inner_product_space.projection

/-!
# Gram-Schmidt Orthogonalization and Orthonormalization

In this file we introduce Gram-Schmidt Orthogonalization and Orthonormalization.

The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span.

## Main results

- `gram_schmidt` : Gram-Schmidt process
- `gram_schmidt_orthogonal` :
  the proof that `gram_schmidt` produces an orthogonal system of vectors
- `span_gram_schmidt` :
  Gram-Schmidt process preserves span of vectors
- `gram_schmidt_ne_zero` :
  If the input of first n vectors of gram_schmidt are linearly independent
  , then output of first n vectors are non-zero
- `gram_schmidt_normed` :
  Normalized "Gram-Schmidt" (i.e each vector in this system has unit length)
- `gram_schmidt_orthornormal` :
  the proof that `gram_schmidt_normed` produces an orthornormal system of vectors

## TODO
  Construct a version with an orthonormal basis from the Gram-Schmidt process.
-/

open_locale big_operators

variables (ğ•œ : Type*) {E : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ E]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

/-- Gram-Schmidt process -/
noncomputable def gram_schmidt (f : â„• â†’ E) : â„• â†’ E
| n := f n - âˆ‘ i : fin n, orthogonal_projection (ğ•œ âˆ™ gram_schmidt i) (f n)
using_well_founded {dec_tac := `[exact i.prop]}

/-- 'gram_schmidt_def' turns the sum over `fin n` into a sum over `â„•`. -/
lemma gram_schmidt_def (f : â„• â†’ E) (n : â„•) :
  gram_schmidt ğ•œ f n = f n - âˆ‘ i in finset.range n,
    orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f n) :=
begin
  rw gram_schmidt,
  congr' 1,
  exact fin.sum_univ_eq_sum_range (Î» i,
    (orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f n) : E)) n,
end

@[simp] lemma gram_schmidt_zero (f : â„• â†’ E) :
  gram_schmidt ğ•œ f 0 = f 0 :=
by simp only [gram_schmidt, fintype.univ_of_is_empty, finset.sum_empty, sub_zero]

/-- **Gram-Schmidt Orthogonalisation**
Gram-Schmidt process produces an orthogonal system of vectors. -/
theorem gram_schmidt_orthogonal (f : â„• â†’ E) (a b : â„•) (hâ‚€ : a â‰  b) :
  âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0 :=
begin
  suffices : âˆ€ a b : â„•, a < b â†’ âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0,
  { cases hâ‚€.lt_or_lt with ha hb,
    { exact this _ _ ha, },
    { rw inner_eq_zero_sym,
      exact this _ _ hb, }, },
  clear hâ‚€ a b,
  intros a b hâ‚€,
  obtain âŸ¨c, hbcâŸ© : âˆƒ c, b â‰¤ c := âŸ¨b, le_rflâŸ©,
  induction c using nat.strong_induction_on with c hc generalizing a b,
  rw le_iff_lt_or_eq at hbc,
  rcases hbc with (hbc | rfl),
  { exact hc b hbc a b hâ‚€ le_rfl, },
  simp only [gram_schmidt_def ğ•œ f b, inner_sub_right, inner_sum,
    orthogonal_projection_singleton, inner_smul_right],
  rw finset.sum_eq_single_of_mem a (finset.mem_range.mpr hâ‚€),
  { by_cases h : gram_schmidt ğ•œ f a = 0,
    { simp only [h, inner_zero_left, zero_div, zero_mul, sub_zero], },
    { rw [â† inner_self_eq_norm_sq_to_K, div_mul_cancel, sub_self],
      rwa [ne.def, inner_self_eq_zero], }, },
  intros i hi hia,
  simp only [mul_eq_zero, div_eq_zero_iff, inner_self_eq_zero],
  right,
  rw finset.mem_range at hi,
  cases hia.lt_or_lt with hiaâ‚ hiaâ‚‚,
  { rw inner_eq_zero_sym,
    exact hc a hâ‚€ i a hiaâ‚ le_rfl, },
  { exact hc i hi a i hiaâ‚‚ le_rfl, },
end

open submodule set

/-- Gram-Schmidt process preserves span -/
lemma span_gram_schmidt (f : â„• â†’ E) (c : â„•) :
  span ğ•œ (gram_schmidt ğ•œ f '' Iic c) = span ğ•œ (f '' Iic c) :=
begin
  induction c with c hc,
  { simp only [Iic, gram_schmidt_zero, le_zero_iff,
      set_of_eq_eq_singleton, image_singleton], },
  have h : Iic c.succ = insert c.succ (Iic c),
  { ext,
    simp only [Iic, mem_set_of_eq, mem_insert_iff,
      nat.lt_succ_iff, le_iff_lt_or_eq, or_comm], },
  have hâ‚€ : âˆ€ b, b âˆˆ finset.range c.succ â†’ gram_schmidt ğ•œ f b âˆˆ span ğ•œ (f '' Iic c),
  { intros b hb,
    rw [finset.mem_range, nat.succ_eq_add_one] at hb,
    have hbâ‚ : b â‰¤ c := by linarith,
    have hbâ‚‚ : gram_schmidt ğ•œ f b âˆˆ span ğ•œ (gram_schmidt ğ•œ f '' Iic c),
      { have hâ‚€ : gram_schmidt ğ•œ f b âˆˆ gram_schmidt ğ•œ f '' Iic c,
        { simp only [mem_image, mem_Iic],
          refine âŸ¨b, hbâ‚, by reflâŸ©, },
        have hâ‚ : gram_schmidt ğ•œ f '' Iic c â‰¤ span ğ•œ (gram_schmidt ğ•œ f '' Iic c) := subset_span,
        exact hâ‚ hâ‚€, },
    rwa hc at hbâ‚‚, },
  simp only [h, span_insert, image_insert_eq, hc],
  apply le_antisymm,
  { rw gram_schmidt_def,
    simp only [orthogonal_projection_singleton,
      sup_le_iff, span_singleton_le_iff_mem, le_sup_right, and_true],
    apply sub_mem _ _ _,
    { apply mem_sup_left,
      exact mem_span_singleton_self (f c.succ), },
    { apply sum_mem _ _,
      intros b hb,
      apply mem_sup_right,
      apply smul_mem _ _ _,
      specialize hâ‚€ b hb,
      exact hâ‚€, }, },
  { simp only [sup_le_iff, span_singleton_le_iff_mem, le_sup_right, and_true],
    have hcâ‚ : f c.succ = gram_schmidt ğ•œ f c.succ + âˆ‘ i in finset.range c.succ,
      orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f c.succ)
        := by simp only [gram_schmidt_def, sub_add_cancel],
    rw hcâ‚, clear hcâ‚,
    simp only [orthogonal_projection_singleton],
    apply add_mem _ _ _,
    { apply mem_sup_left,
      exact mem_span_singleton_self (gram_schmidt ğ•œ f c.succ), },
    { apply sum_mem _ _,
      intros b hb,
      apply mem_sup_right,
      apply smul_mem _ _ _,
      specialize hâ‚€ b hb,
      exact hâ‚€, }, },
end

/-- If the input of first n + 1 vectors of gram_schmidt are linearly independent
,then output of first n + 1 vectors are non-zero -/
lemma gram_schmidt_ne_zero (f : â„• â†’ E) (n : â„•)
  (hâ‚€ : linear_independent ğ•œ (f âˆ˜ (coe : fin n.succ â†’ â„•))) :
    gram_schmidt ğ•œ f n â‰  0 :=
begin
  induction n with n hn,
  { simp only [gram_schmidt_zero, ne.def],
    have h : f 0 = (f âˆ˜ (coe : fin 1 â†’ â„•)) 0 := by simp only [function.comp_app, fin.coe_zero],
    rw h,
    exact linear_independent.ne_zero 0 hâ‚€, },
  { by_contra hâ‚,
    rw nat.succ_eq_add_one at hn hâ‚€ hâ‚,
    have hâ‚‚ : f (n + 1) = gram_schmidt ğ•œ f (n + 1) + âˆ‘ i in finset.range (n + 1),
      orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f (n + 1))
        := by simp only [gram_schmidt_def, sub_add_cancel],
    simp only [hâ‚, orthogonal_projection_singleton, zero_add] at hâ‚‚,
    have hâ‚ƒ : âˆ‘ (x : â„•) in finset.range (n + 1),
      ((âŸªgram_schmidt ğ•œ f x, f (n + 1)âŸ« / âˆ¥gram_schmidt ğ•œ f xâˆ¥ ^ 2) : ğ•œ)
        â€¢ gram_schmidt ğ•œ f x âˆˆ span ğ•œ (gram_schmidt ğ•œ f '' Iic n),
    { apply sum_mem _ _,
      intros a ha,
      apply smul_mem _ _ _,
      have haâ‚ : gram_schmidt ğ•œ f a âˆˆ gram_schmidt ğ•œ f '' Iic n,
      { simp only [mem_image, mem_Iic],
        rw finset.mem_range at ha,
        refine âŸ¨a, by linarith, by reflâŸ©, },
      have haâ‚‚ : gram_schmidt ğ•œ f '' Iic n
        âŠ† span ğ•œ (gram_schmidt ğ•œ f '' Iic n) := subset_span,
      exact haâ‚‚ haâ‚, },
    rw [span_gram_schmidt ğ•œ f n, â† hâ‚‚] at hâ‚ƒ,
    change linear_independent ğ•œ (f âˆ˜ (coe : fin (n + 2) â†’ â„•)) at hâ‚€,
    have hâ‚„ : ((n + 1) : fin (n + 2)) âˆ‰ (coe : fin (n + 2) â†’ â„•) â»Â¹' (Iic n),
    { simp only [mem_preimage, mem_Iic, not_le],
      norm_cast,
      have h : n + 1 < n + 2 := by linarith,
      rw fin.coe_coe_of_lt h,
      linarith, },
    apply linear_independent.not_mem_span_image hâ‚€ hâ‚„,
    rw [image_comp, image_preimage_eq_inter_range],
    simp only [function.comp_app, subtype.range_coe_subtype],
    convert hâ‚ƒ,
    { norm_cast,
      apply fin.coe_coe_of_lt,
      linarith, },
    { rw inter_eq_left_iff_subset,
      simp only [Iic, set_of_subset_set_of],
      intros a ha,
      linarith, }, },
end

/-- If the input of gram_schmidt is linearly independent, then output is non-zero -/
lemma gram_schmidt_ne_zero' (f : â„• â†’ E) (hâ‚€ : linear_independent ğ•œ f) (n : â„•) :
  gram_schmidt ğ•œ f n â‰  0 :=
begin
  apply gram_schmidt_ne_zero ğ•œ f n,
  apply linear_independent.comp,
  { exact hâ‚€, },
  { exact fin.coe_injective, },
end

/-- Normalized Gram-Schmidt process
(i.e each vector in 'gram_schmidt_normed` has unit length) -/
noncomputable def gram_schmidt_normed (f : â„• â†’ E) (n : â„•) : E :=
(âˆ¥gram_schmidt ğ•œ f nâˆ¥ : ğ•œ)â»Â¹ â€¢ (gram_schmidt ğ•œ f n)

lemma gram_schmidt_normed_unit_length (f : â„• â†’ E) (n : â„•)
  (hâ‚€ : linear_independent ğ•œ (f âˆ˜ (coe : fin n.succ â†’ â„•))) :
    âˆ¥gram_schmidt_normed ğ•œ f nâˆ¥ = 1 :=
by simp only [gram_schmidt_ne_zero ğ•œ f n hâ‚€,
  gram_schmidt_normed, norm_smul_inv_norm, ne.def, not_false_iff]

lemma gram_schmidt_normed_unit_length' (f : â„• â†’ E) (n : â„•)
  (hâ‚€ : linear_independent ğ•œ f) :
    âˆ¥gram_schmidt_normed ğ•œ f nâˆ¥ = 1 :=
by simp only [gram_schmidt_ne_zero' ğ•œ f hâ‚€,
  gram_schmidt_normed, norm_smul_inv_norm, ne.def, not_false_iff]

/-- **Gram-Schmidt Orthonormalization**
Normalized Gram-Schmidt process produces an orthornormal system of vectors. -/
theorem gram_schmidt_orthonormal (f : â„• â†’ E) (hâ‚€ : linear_independent ğ•œ f) :
  orthonormal ğ•œ (gram_schmidt_normed ğ•œ f) :=
begin
  simp only [orthonormal],
  split,
  { simp only [gram_schmidt_normed_unit_length', hâ‚€, forall_const], },
  { intros i j hij,
    simp only [gram_schmidt_normed, inner_smul_left, inner_smul_right, is_R_or_C.conj_inv,
      is_R_or_C.conj_of_real, mul_eq_zero, inv_eq_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero],
    repeat { right },
    exact gram_schmidt_orthogonal ğ•œ f i j hij, },
end
