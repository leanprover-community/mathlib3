/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao, Kevin Buzzard, Alexander Bentkamp
-/

import analysis.inner_product_space.projection
import order.well_founded_set
import analysis.inner_product_space.pi_L2

/-!
# Gram-Schmidt Orthogonalization and Orthonormalization

In this file we introduce Gram-Schmidt Orthogonalization and Orthonormalization.

The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span.

## Main results

- `gram_schmidt` : the Gram-Schmidt process
- `gram_schmidt_orthogonal` :
  `gram_schmidt` produces an orthogonal system of vectors.
- `span_gram_schmidt` :
  `gram_schmidt` preserves span of vectors.
- `gram_schmidt_ne_zero` :
  If the input vectors of `gram_schmidt` are linearly independent,
  then the output vectors are non-zero.
- `gram_schmidt_basis` :
  The basis produced by the Gram-Schmidt process when given a basis as input.
- `gram_schmidt_normed` :
  the normalized `gram_schmidt` (i.e each vector in `gram_schmidt_normed` has unit length.)
- `gram_schmidt_orthornormal` :
  `gram_schmidt_normed` produces an orthornormal system of vectors.

## TODO
  Construct a version with an orthonormal basis from Gram-Schmidt process.
-/

open_locale big_operators
open finset

variables (ğ•œ : Type*) {E : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ E]
variables {Î¹ : Type*} [linear_order Î¹] [locally_finite_order_bot Î¹] [well_founded_lt Î¹]

local attribute [instance] is_well_order.to_has_well_founded

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

/-- The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span. -/
noncomputable def gram_schmidt (f : Î¹ â†’ E) : Î¹ â†’ E
| n := f n - âˆ‘ i : Iio n, orthogonal_projection (ğ•œ âˆ™ gram_schmidt i) (f n)
using_well_founded { dec_tac := `[exact mem_Iio.1 i.2] }

/-- This lemma uses `âˆ‘ i in` instead of `âˆ‘ i :`.-/
lemma gram_schmidt_def (f : Î¹ â†’ E) (n : Î¹):
  gram_schmidt ğ•œ f n = f n - âˆ‘ i in Iio n,
    orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f n) :=
by { rw [â†sum_attach, attach_eq_univ, gram_schmidt], refl }

lemma gram_schmidt_def' (f : Î¹ â†’ E) (n : Î¹):
  f n = gram_schmidt ğ•œ f n + âˆ‘ i in Iio n,
    orthogonal_projection (ğ•œ âˆ™ gram_schmidt ğ•œ f i) (f n) :=
by rw [gram_schmidt_def, sub_add_cancel]

@[simp] lemma gram_schmidt_zero {Î¹ : Type*} [linear_order Î¹] [locally_finite_order Î¹]
  [order_bot Î¹] [well_founded_lt Î¹] (f : Î¹ â†’ E) : gram_schmidt ğ•œ f âŠ¥ = f âŠ¥ :=
by rw [gram_schmidt_def, Iio_eq_Ico, finset.Ico_self, finset.sum_empty, sub_zero]

/-- **Gram-Schmidt Orthogonalisation**:
`gram_schmidt` produces an orthogonal system of vectors. -/
theorem gram_schmidt_orthogonal (f : Î¹ â†’ E) {a b : Î¹} (hâ‚€ : a â‰  b) :
  âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0 :=
begin
  suffices : âˆ€ a b : Î¹, a < b â†’ âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0,
  { cases hâ‚€.lt_or_lt with ha hb,
    { exact this _ _ ha, },
    { rw inner_eq_zero_sym,
      exact this _ _ hb, }, },
  clear hâ‚€ a b,
  intros a b hâ‚€,
  revert a,
  apply well_founded.induction (@is_well_order.wf Î¹ (<) _) b,
  intros b ih a hâ‚€,
  simp only [gram_schmidt_def ğ•œ f b, inner_sub_right, inner_sum,
    orthogonal_projection_singleton, inner_smul_right],
  rw finset.sum_eq_single_of_mem a (finset.mem_Iio.mpr hâ‚€),
  { by_cases h : gram_schmidt ğ•œ f a = 0,
    { simp only [h, inner_zero_left, zero_div, zero_mul, sub_zero], },
    { rw [â† inner_self_eq_norm_sq_to_K, div_mul_cancel, sub_self],
      rwa [ne.def, inner_self_eq_zero], }, },
  simp_intros i hi hia only [finset.mem_range],
  simp only [mul_eq_zero, div_eq_zero_iff, inner_self_eq_zero],
  right,
  cases hia.lt_or_lt with hiaâ‚ hiaâ‚‚,
  { rw inner_eq_zero_sym,
    exact ih a hâ‚€ i hiaâ‚ },
  { exact ih i (mem_Iio.1 hi) a hiaâ‚‚ }
end

/-- This is another version of `gram_schmidt_orthogonal` using `pairwise` instead. -/
theorem gram_schmidt_pairwise_orthogonal (f : Î¹ â†’ E) :
  pairwise (Î» a b, âŸªgram_schmidt ğ•œ f a, gram_schmidt ğ•œ f bâŸ« = 0) :=
Î» a b, gram_schmidt_orthogonal ğ•œ f

open submodule set order

lemma mem_span_gram_schmidt (f : Î¹ â†’ E) {i j : Î¹} (hij : i â‰¤ j) :
  f i âˆˆ span ğ•œ (gram_schmidt ğ•œ f '' Iic j) :=
begin
  rw [gram_schmidt_def' ğ•œ f i],
  simp_rw orthogonal_projection_singleton,
  exact submodule.add_mem _ (subset_span $ mem_image_of_mem _ hij)
    (submodule.sum_mem _ $ Î» k hk, smul_mem (span ğ•œ (gram_schmidt ğ•œ f '' Iic j)) _ $
    subset_span $ mem_image_of_mem (gram_schmidt ğ•œ f) $ (finset.mem_Iio.1 hk).le.trans hij),
end

lemma gram_schmidt_mem_span (f : Î¹ â†’ E) :
  âˆ€ {j i}, i â‰¤ j â†’ gram_schmidt ğ•œ f i âˆˆ span ğ•œ (f '' Iic j)
| j := Î» i hij,
begin
  rw [gram_schmidt_def ğ•œ f i],
  simp_rw orthogonal_projection_singleton,
  refine submodule.sub_mem _ (subset_span (mem_image_of_mem _ hij))
    (submodule.sum_mem _ $ Î» k hk, _),
  let hkj : k < j := (finset.mem_Iio.1 hk).trans_le hij,
  exact smul_mem _ _ (span_mono (image_subset f $ Iic_subset_Iic.2 hkj.le) $
    gram_schmidt_mem_span le_rfl),
end
using_well_founded { dec_tac := `[assumption] }

lemma span_gram_schmidt_Iic (f : Î¹ â†’ E) (c : Î¹) :
  span ğ•œ (gram_schmidt ğ•œ f '' Iic c) = span ğ•œ (f '' Iic c) :=
span_eq_span (set.image_subset_iff.2 $ Î» i, gram_schmidt_mem_span _ _) $
  set.image_subset_iff.2 $ Î» i, mem_span_gram_schmidt _ _

lemma span_gram_schmidt_Iio (f : Î¹ â†’ E) (c : Î¹) :
  span ğ•œ (gram_schmidt ğ•œ f '' Iio c) = span ğ•œ (f '' Iio c) :=
span_eq_span
  (set.image_subset_iff.2 $ Î» i hi, span_mono (image_subset _ $ Iic_subset_Iio.2 hi) $
    gram_schmidt_mem_span _ _ le_rfl) $
  set.image_subset_iff.2 $ Î» i hi, span_mono (image_subset _ $ Iic_subset_Iio.2 hi) $
    mem_span_gram_schmidt _ _ le_rfl

/-- `gram_schmidt` preserves span of vectors. -/
lemma span_gram_schmidt (f : Î¹ â†’ E) : span ğ•œ (range (gram_schmidt ğ•œ f)) = span ğ•œ (range f) :=
span_eq_span (range_subset_iff.2 $ Î» i, span_mono (image_subset_range _ _) $
  gram_schmidt_mem_span _ _ le_rfl) $
  range_subset_iff.2 $ Î» i, span_mono (image_subset_range _ _) $ mem_span_gram_schmidt _ _ le_rfl

lemma gram_schmidt_ne_zero_coe
    (f : Î¹ â†’ E) (n : Î¹) (hâ‚€ : linear_independent ğ•œ (f âˆ˜ (coe : set.Iic n â†’ Î¹))) :
  gram_schmidt ğ•œ f n â‰  0 :=
begin
  by_contra h,
  have hâ‚ : f n âˆˆ span ğ•œ (f '' Iio n),
  { rw [â† span_gram_schmidt_Iio ğ•œ f n, gram_schmidt_def' _ f, h, zero_add],
    apply submodule.sum_mem _ _,
    simp_intros a ha only [finset.mem_Ico],
    simp only [set.mem_image, set.mem_Iio, orthogonal_projection_singleton],
    apply submodule.smul_mem _ _ _,
    rw finset.mem_Iio at ha,
    refine subset_span âŸ¨a, ha, by reflâŸ© },
  have hâ‚‚ : (f âˆ˜ (coe : set.Iic n â†’ Î¹)) âŸ¨n, le_refl nâŸ©
    âˆˆ span ğ•œ (f âˆ˜ (coe : set.Iic n â†’ Î¹) '' Iio âŸ¨n, le_refl nâŸ©),
  { rw [image_comp],
    convert hâ‚ using 3,
    ext i,
    simpa using @le_of_lt _ _ i n },
  apply linear_independent.not_mem_span_image hâ‚€ _ hâ‚‚,
  simp only [set.mem_Iio, lt_self_iff_false, not_false_iff]
end

/-- If the input vectors of `gram_schmidt` are linearly independent,
then the output vectors are non-zero. -/
lemma gram_schmidt_ne_zero (f : Î¹ â†’ E) (n : Î¹) (hâ‚€ : linear_independent ğ•œ f) :
  gram_schmidt ğ•œ f n â‰  0 :=
gram_schmidt_ne_zero_coe _ _ _ (linear_independent.comp hâ‚€ _ subtype.coe_injective)

/-- `gram_schmidt` produces a triangular matrix of vectors when given a basis. -/
lemma gram_schmidt_triangular {i j : Î¹} (hij : i < j) (b : basis Î¹ ğ•œ E) :
  b.repr (gram_schmidt ğ•œ b i) j = 0 :=
begin
  have : gram_schmidt ğ•œ b i âˆˆ span ğ•œ (gram_schmidt ğ•œ b '' set.Iio j),
    from subset_span ((set.mem_image _ _ _).2 âŸ¨i, hij, rflâŸ©),
  have : gram_schmidt ğ•œ b i âˆˆ span ğ•œ (b '' set.Iio j),
    by rwa [â† span_gram_schmidt_Iio ğ•œ b j],
  have : â†‘(((b.repr) (gram_schmidt ğ•œ b i)).support) âŠ† set.Iio j,
    from basis.repr_support_subset_of_mem_span b (set.Iio j) this,
  exact (finsupp.mem_supported' _ _).1
    ((finsupp.mem_supported ğ•œ _).2 this) j (not_mem_Iio.2 (le_refl j)),
end

/-- `gram_schmidt` produces linearly independent vectors when given linearly independent vectors. -/
lemma gram_schmidt_linear_independent (f : Î¹ â†’ E) (hâ‚€ : linear_independent ğ•œ f) :
  linear_independent ğ•œ (gram_schmidt ğ•œ f) :=
linear_independent_of_ne_zero_of_inner_eq_zero
    (Î» i, gram_schmidt_ne_zero _ _ _ hâ‚€) (Î» i j, gram_schmidt_orthogonal ğ•œ f)

/-- When given a basis, `gram_schmidt` produces a basis. -/
noncomputable def gram_schmidt_basis (b : basis Î¹ ğ•œ E) : basis Î¹ ğ•œ E :=
basis.mk
  (gram_schmidt_linear_independent ğ•œ b b.linear_independent)
  ((span_gram_schmidt ğ•œ b).trans b.span_eq)

lemma coe_gram_schmidt_basis (b : basis Î¹ ğ•œ E) :
  (gram_schmidt_basis ğ•œ b : Î¹ â†’ E) = gram_schmidt ğ•œ b := basis.coe_mk _ _

/-- the normalized `gram_schmidt`
(i.e each vector in `gram_schmidt_normed` has unit length.) -/
noncomputable def gram_schmidt_normed (f : Î¹ â†’ E) (n : Î¹) : E :=
(âˆ¥gram_schmidt ğ•œ f nâˆ¥ : ğ•œ)â»Â¹ â€¢ (gram_schmidt ğ•œ f n)

lemma gram_schmidt_normed_unit_length_coe
    (f : Î¹ â†’ E) (n : Î¹) (hâ‚€ : linear_independent ğ•œ (f âˆ˜ (coe : set.Iic n â†’ Î¹))) :
  âˆ¥gram_schmidt_normed ğ•œ f nâˆ¥ = 1 :=
by simp only [gram_schmidt_ne_zero_coe ğ•œ f n hâ‚€,
  gram_schmidt_normed, norm_smul_inv_norm, ne.def, not_false_iff]

lemma gram_schmidt_normed_unit_length (f : Î¹ â†’ E) (n : Î¹) (hâ‚€ : linear_independent ğ•œ f) :
  âˆ¥gram_schmidt_normed ğ•œ f nâˆ¥ = 1 :=
gram_schmidt_normed_unit_length_coe _ _ _ (linear_independent.comp hâ‚€ _ subtype.coe_injective)

/-- **Gram-Schmidt Orthonormalization**:
`gram_schmidt_normed` produces an orthornormal system of vectors. -/
theorem gram_schmidt_orthonormal (f : Î¹ â†’ E) (hâ‚€ : linear_independent ğ•œ f) :
  orthonormal ğ•œ (gram_schmidt_normed ğ•œ f) :=
begin
  unfold orthonormal,
  split,
  { simp only [gram_schmidt_normed_unit_length, hâ‚€, eq_self_iff_true, implies_true_iff], },
  { intros i j hij,
    simp only [gram_schmidt_normed, inner_smul_left, inner_smul_right, is_R_or_C.conj_inv,
      is_R_or_C.conj_of_real, mul_eq_zero, inv_eq_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero],
    repeat { right },
    exact gram_schmidt_orthogonal ğ•œ f hij }
end

lemma span_gram_schmidt_normed (f : Î¹ â†’ E) (s : set Î¹) :
  span ğ•œ (gram_schmidt_normed ğ•œ f '' s) = span ğ•œ (gram_schmidt ğ•œ f '' s) :=
begin
  refine span_eq_span (set.image_subset_iff.2 $ Î» i hi, smul_mem _ _ $ subset_span $
    mem_image_of_mem _ hi)
    (set.image_subset_iff.2 $ Î» i hi, span_mono (image_subset _ $ singleton_subset_set_iff.2 hi) _),
  simp only [coe_singleton, set.image_singleton],
  by_cases h : gram_schmidt ğ•œ f i = 0,
  { simp [h] },
  { refine mem_span_singleton.2 âŸ¨âˆ¥gram_schmidt ğ•œ f iâˆ¥, smul_inv_smulâ‚€ _ _âŸ©,
    exact_mod_cast (norm_ne_zero_iff.2 h) }
end

lemma span_gram_schmidt_normed_range (f : Î¹ â†’ E) :
  span ğ•œ (range (gram_schmidt_normed ğ•œ f)) = span ğ•œ (range (gram_schmidt ğ•œ f)) :=
by simpa only [image_univ.symm] using span_gram_schmidt_normed ğ•œ f univ

/-- When given a basis, `gram_schmidt_normed` produces an orthonormal basis. -/
noncomputable def gram_schmidt_orthonormal_basis [fintype Î¹] (b : basis Î¹ ğ•œ E) :
  orthonormal_basis Î¹ ğ•œ E :=
orthonormal_basis.mk
  (gram_schmidt_orthonormal ğ•œ b b.linear_independent)
  (((span_gram_schmidt_normed_range ğ•œ b).trans (span_gram_schmidt ğ•œ b)).trans b.span_eq)
