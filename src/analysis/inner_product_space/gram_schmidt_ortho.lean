/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao, Kevin Buzzard, Alexander Bentkamp
-/

import analysis.inner_product_space.projection

/-!
# Gram-Schmidt Orthogonalization and Orthonormalization

In this file we introduce Gram-Schmidt Orthogonalization and Orthonormalization.

The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span.

## Main results

We prove all results for an infinite set of vectors, indexed by `‚Ñï`,
and for a finite set of vectors, indexed by `fin m`.

- `gram_schmidt`/`gram_schmidt_fin` : The Gram-Schmidt process.
- `gram_schmidt_orthogonal`/`gram_schmidt_fin_orthogonal` :
  The Gram-Schmidt process produces an orthogonal system of vectors.
- `span_gram_schmidt`/`span_gram_schmidt_fin` :
  The Gram-Schmidt process produces preserves span of vectors.
- `gram_schmidt_ne_zero`/`gram_schmidt_fin_ne_zero` :
  If the input vectors of the Gram-Schmidt process are linearly independent,
  then the output vectors are non-zero.
- `gram_schmidt_normed`/`gram_schmidt_normed_fin` :
  The normalized Gram-Schmidt process
  (i.e each vector in `gram_schmidt_normed`/`gram_schmidt_normed_fin` has unit length).
- `gram_schmidt_orthornormal`/`gram_schmidt_fin_orthonormal` :
  The normalized Gram-Schmidt process produces an orthornormal system of vectors.

## TODO
  Construct a version with an orthonormal basis from Gram-Schmidt process.
-/

open_locale big_operators

variables (ùïú : Type*) {E : Type*} [is_R_or_C ùïú] [inner_product_space ùïú E]

local notation `‚ü™`x`, `y`‚ü´` := @inner ùïú _ _ x y

section move

-- TODO: move
lemma fin.of_nat'_coe {m : ‚Ñï} (n : fin m) :
  @fin.of_nat' _ ‚ü®lt_of_le_of_lt (nat.zero_le _) n.2‚ü© n = n :=
begin
  haveI hm : fact (0 < m), from ‚ü®lt_of_le_of_lt (nat.zero_le _) n.2‚ü©,
  ext,
  rw [fin.coe_of_nat_eq_mod', nat.mod_eq_of_lt],
  exact n.2,
end

-- TODO: move
lemma fin.cast_lt_cast_lt {m n : ‚Ñï} (i : fin n) (hm : i.val < m) (hn : i.val < n) :
  (i.cast_lt hm).cast_lt hn = i :=
by simp [fin.cast_lt]

-- TODO: move
lemma sum_fin_range_eq_sum_range {M : Type*} [add_comm_monoid M] (n : ‚Ñï) (f : ‚Ñï ‚Üí M) :
‚àë i in finset.fin_range n, f i = ‚àë i in finset.range n, f i :=
begin
  apply finset.sum_bij (Œª (i : fin n) ih, i.val),
  exact Œª i ih, finset.mem_range.2 i.2,
  { intros, rw fin.coe_eq_val },
  exact Œª _ _ _ _, (fin.eq_iff_veq _ _).2,
  exact Œª i hi, ‚ü®‚ü®i, finset.mem_range.1 hi‚ü©, finset.mem_fin_range _, rfl‚ü©
end

open submodule set order

-- TODO: move
lemma fin.image_of_nat' (m : ‚Ñï) [h : fact (0 < m)] :
  (fin.of_nat' '' Iio m) = (set.univ : set (fin m)) :=
eq_univ_of_forall (Œª i, (mem_image _ _ _).2 ‚ü®i, mem_Iio.2 i.2, fin.of_nat'_coe _‚ü©)

-- TODO: move
lemma fin.range_coe (m : ‚Ñï) : range (Œª (i : fin m), (i : ‚Ñï)) = Iio m :=
by simp [Iio]

end move

section nat

/-- The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span. (Infinite version) -/
noncomputable def gram_schmidt (f : ‚Ñï ‚Üí E) : ‚Ñï ‚Üí E
| n := f n - ‚àë i : fin n, orthogonal_projection (ùïú ‚àô gram_schmidt i) (f n)
using_well_founded {dec_tac := `[exact i.prop]}

/-- `gram_schmidt_def` turns the sum over `fin n` into a sum over `‚Ñï`. -/
lemma gram_schmidt_def (f : ‚Ñï ‚Üí E) (n : ‚Ñï) :
  gram_schmidt ùïú f n = f n - ‚àë i in finset.range n,
    orthogonal_projection (ùïú ‚àô gram_schmidt ùïú f i) (f n) :=
begin
  rw gram_schmidt,
  congr' 1,
  exact fin.sum_univ_eq_sum_range (Œª i,
    (orthogonal_projection (ùïú ‚àô gram_schmidt ùïú f i) (f n) : E)) n,
end

lemma gram_schmidt_def' (f : ‚Ñï ‚Üí E) (n : ‚Ñï):
  f n = gram_schmidt ùïú f n + ‚àë i in finset.range n,
    orthogonal_projection (ùïú ‚àô gram_schmidt ùïú f i) (f n) :=
by simp only [gram_schmidt_def, sub_add_cancel]

@[simp] lemma gram_schmidt_zero (f : ‚Ñï ‚Üí E) :
  gram_schmidt ùïú f 0 = f 0 :=
by simp only [gram_schmidt, fintype.univ_of_is_empty, finset.sum_empty, sub_zero]

/-- **Gram-Schmidt Orthogonalisation** (Infinite version):
`gram_schmidt` produces an orthogonal system of vectors. -/
theorem gram_schmidt_orthogonal (f : ‚Ñï ‚Üí E) {a b : ‚Ñï} (h‚ÇÄ : a ‚â† b) :
  ‚ü™gram_schmidt ùïú f a, gram_schmidt ùïú f b‚ü´ = 0 :=
begin
  suffices : ‚àÄ a b : ‚Ñï, a < b ‚Üí ‚ü™gram_schmidt ùïú f a, gram_schmidt ùïú f b‚ü´ = 0,
  { cases h‚ÇÄ.lt_or_lt with ha hb,
    { exact this _ _ ha, },
    { rw inner_eq_zero_sym,
      exact this _ _ hb, }, },
  clear h‚ÇÄ a b,
  intros a b h‚ÇÄ,
  induction b using nat.strong_induction_on with b ih generalizing a,
  simp only [gram_schmidt_def ùïú f b, inner_sub_right, inner_sum,
    orthogonal_projection_singleton, inner_smul_right],
  rw finset.sum_eq_single_of_mem a (finset.mem_range.mpr h‚ÇÄ),
  { by_cases h : gram_schmidt ùïú f a = 0,
    { simp only [h, inner_zero_left, zero_div, zero_mul, sub_zero], },
    { rw [‚Üê inner_self_eq_norm_sq_to_K, div_mul_cancel, sub_self],
      rwa [ne.def, inner_self_eq_zero], }, },
  simp_intros i hi hia only [finset.mem_range],
  simp only [mul_eq_zero, div_eq_zero_iff, inner_self_eq_zero],
  right,
  cases hia.lt_or_lt with hia‚ÇÅ hia‚ÇÇ,
  { rw inner_eq_zero_sym,
    exact ih a h‚ÇÄ i hia‚ÇÅ, },
  { exact ih i hi a hia‚ÇÇ, },
end

/-- This is another version of `gram_schmidt_orthogonal` using `pairwise` instead. -/
theorem gram_schmidt_pairwise_orthogonal (f : ‚Ñï ‚Üí E) :
  pairwise (Œª a b, ‚ü™gram_schmidt ùïú f a, gram_schmidt ùïú f b‚ü´ = 0) :=
@gram_schmidt_orthogonal ùïú _ _ _ f

open submodule set order

/-- `gram_schmidt` preserves span of vectors. -/
lemma span_gram_schmidt (f : ‚Ñï ‚Üí E) (c : ‚Ñï) :
  span ùïú (gram_schmidt ùïú f '' Iio c) = span ùïú (f '' Iio c) :=
begin
  induction c with c hc,
  { simp only [Iio, not_lt_zero', set_of_false, image_empty], },
  have h‚ÇÄ : ‚àÄ b, b ‚àà finset.range c ‚Üí gram_schmidt ùïú f b ‚àà span ùïú (f '' Iio c),
  { simp_intros b hb only [finset.mem_range, nat.succ_eq_add_one],
    rw ‚Üê hc,
    refine subset_span _,
    simp only [mem_image, mem_Iio],
    refine ‚ü®b, by linarith, by refl‚ü©, },
  rw [‚Üê nat.succ_eq_succ, Iio_succ_eq_insert],
  simp only [span_insert, image_insert_eq, hc],
  apply le_antisymm,
  { simp only [nat.succ_eq_succ,gram_schmidt_def ùïú f c, orthogonal_projection_singleton,
      sup_le_iff, span_singleton_le_iff_mem, le_sup_right, and_true],
    apply submodule.sub_mem _ _ _,
    { exact mem_sup_left (mem_span_singleton_self (f c)), },
    { exact submodule.sum_mem _ (Œª b hb, mem_sup_right (smul_mem _ _ (h‚ÇÄ b hb))), }, },
  { rw [gram_schmidt_def' ùïú f c],
    simp only [orthogonal_projection_singleton,
      sup_le_iff, span_singleton_le_iff_mem, le_sup_right, and_true],
    apply submodule.add_mem _ _ _,
    { exact mem_sup_left (mem_span_singleton_self (gram_schmidt ùïú f c)), },
    { exact submodule.sum_mem _ (Œª b hb, mem_sup_right (smul_mem _ _ (h‚ÇÄ b hb))), }, },
end

/-- If the input of the first `n` vectors of `gram_schmidt` are linearly independent,
then the output of the first `n` vectors are non-zero. -/
lemma gram_schmidt_ne_zero_aux (f : ‚Ñï ‚Üí E) (n : ‚Ñï)
  (h‚ÇÄ : linear_independent ùïú (f ‚àò (coe : fin n ‚Üí ‚Ñï))) :
    ‚àÄ i (h : i < n), gram_schmidt ùïú f i ‚â† 0 :=
begin
  induction n with n hn,
  { intros, linarith },
  { intros i hi h‚ÇÅ,
    rw nat.succ_eq_add_one at hi,
    have h‚ÇÇ := gram_schmidt_def' ùïú f i,
    simp only [nat.succ_eq_add_one, h‚ÇÅ, orthogonal_projection_singleton, zero_add] at h‚ÇÇ,
    have h‚ÇÉ : f i ‚àà span ùïú (f '' Iio i),
    { rw [h‚ÇÇ, ‚Üê span_gram_schmidt ùïú f i],
      apply submodule.sum_mem _ _,
      simp_intros a ha only [finset.mem_range],
      apply submodule.smul_mem _ _ _,
      refine subset_span _,
      simp only [mem_image, mem_Iio],
      exact ‚ü®a, by linarith, by refl‚ü©, },
    change linear_independent ùïú (f ‚àò (coe : fin (n + 1) ‚Üí ‚Ñï)) at h‚ÇÄ,
    have h‚ÇÑ : (i : fin (n + 1)) ‚àâ (coe : fin (n + 1) ‚Üí ‚Ñï) ‚Åª¬π' (Iio i),
    { simp only [mem_preimage, mem_Iio, not_le],
      rw [fin.coe_coe_of_lt, not_lt],
      exact hi },
    apply linear_independent.not_mem_span_image h‚ÇÄ h‚ÇÑ,
    rw [image_comp, image_preimage_eq_inter_range],
    simp only [function.comp_app, subtype.range_coe_subtype],
    convert h‚ÇÉ,
    { exact fin.coe_coe_of_lt hi, },
    { simp only [inter_eq_left_iff_subset, Iio, set_of_subset_set_of],
      exact (Œª a ha, by linarith), }, },
end

/-- If the input of `gram_schmidt` is linearly independent, then the output is non-zero. -/
lemma gram_schmidt_ne_zero (f : ‚Ñï ‚Üí E) (h‚ÇÄ : linear_independent ùïú f) (n : ‚Ñï) :
  gram_schmidt ùïú f n ‚â† 0 :=
gram_schmidt_ne_zero_aux ùïú f (n + 1) (linear_independent.comp h‚ÇÄ _ (fin.coe_injective)) n (lt_succ n)

/-- The normalized `gram_schmidt` (Infinite version).
Each vector in `gram_schmidt_normed` has unit length. -/
noncomputable def gram_schmidt_normed (f : ‚Ñï ‚Üí E) (n : ‚Ñï) : E :=
(‚à•gram_schmidt ùïú f n‚à• : ùïú)‚Åª¬π ‚Ä¢ (gram_schmidt ùïú f n)

lemma gram_schmidt_normed_unit_length (f : ‚Ñï ‚Üí E) (n : ‚Ñï)
  (h‚ÇÄ : linear_independent ùïú f) :
    ‚à•gram_schmidt_normed ùïú f n‚à• = 1 :=
by simp only [gram_schmidt_ne_zero ùïú f h‚ÇÄ,
  gram_schmidt_normed, norm_smul_inv_norm, ne.def, not_false_iff]

/-- **Gram-Schmidt Orthonormalization** (Infinite version):
`gram_schmidt_normed` produces an orthornormal system of vectors. -/
theorem gram_schmidt_orthonormal (f : ‚Ñï ‚Üí E) (h‚ÇÄ : linear_independent ùïú f) :
  orthonormal ùïú (gram_schmidt_normed ùïú f) :=
begin
  unfold orthonormal,
  split,
  { simp only [gram_schmidt_normed_unit_length, h‚ÇÄ, forall_const], },
  { intros i j hij,
    simp only [gram_schmidt_normed, inner_smul_left, inner_smul_right, is_R_or_C.conj_inv,
      is_R_or_C.conj_of_real, mul_eq_zero, inv_eq_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero],
    repeat { right },
    exact gram_schmidt_orthogonal ùïú f hij, },
end

end nat

section fin

/-- The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span. (Finite version) -/
noncomputable def gram_schmidt_fin {m : ‚Ñï} (f : fin m ‚Üí E) : fin m ‚Üí E :=
  Œª i, have hm : fact (0 < m), from ‚ü®lt_of_le_of_lt (nat.zero_le _) i.2‚ü©,
    gram_schmidt ùïú (Œª j, f (@fin.of_nat' _ hm j)) i

lemma gram_schmidt_fin_def {m : ‚Ñï} (f : fin m ‚Üí E) (n : fin m) :
  gram_schmidt_fin ùïú f n = f n - ‚àë i in finset.fin_range n,
    orthogonal_projection (ùïú ‚àô gram_schmidt_fin ùïú f (i.cast_lt (lt_trans i.2 n.2))) (f n) :=
begin
  haveI hm : fact (0 < m), from ‚ü®lt_of_le_of_lt (nat.zero_le _) n.2‚ü©,
  convert gram_schmidt_def ùïú (Œª (j : ‚Ñï), f (fin.of_nat' j)) n using 2,
  { rw [fin.of_nat'_coe] },
  { rw [‚Üêsum_fin_range_eq_sum_range, fin.of_nat'_coe], refl }
end

lemma gram_schmidt_fin_def' {m : ‚Ñï} (f : fin m ‚Üí E) (n : fin m) :
  f n = gram_schmidt_fin ùïú f n + ‚àë i in finset.fin_range n,
    orthogonal_projection (ùïú ‚àô gram_schmidt_fin ùïú f (i.cast_lt (lt_trans i.2 n.2))) (f n) :=
by simp only [gram_schmidt_fin_def, sub_add_cancel]

@[simp] lemma gram_schmidt_fin_zero {m : ‚Ñï} (f : fin m.succ ‚Üí E) :
  gram_schmidt_fin ùïú f 0 = f 0 :=
by { simp [gram_schmidt_fin, gram_schmidt_zero], refl }

/-- **Gram-Schmidt Orthogonalisation** (Finite version):
`gram_schmidt_fin` produces an orthogonal system of vectors. -/
theorem gram_schmidt_fin_orthogonal {m : ‚Ñï} (f : fin m ‚Üí E) {a b : fin m} (h‚ÇÄ : a ‚â† b) :
  ‚ü™gram_schmidt_fin ùïú f a, gram_schmidt_fin ùïú f b‚ü´ = 0 :=
gram_schmidt_orthogonal ùïú _ (Œª h, h‚ÇÄ ((fin.ext_iff _ _).2 h))

/-- This is another version of `gram_schmidt_fin_orthogonal` using `pairwise` instead. -/
theorem gram_schmidt_fin_pairwise_orthogonal {m : ‚Ñï} (f : fin m ‚Üí E) :
  pairwise (Œª a b, ‚ü™gram_schmidt_fin ùïú f a, gram_schmidt_fin ùïú f b‚ü´ = 0) :=
@gram_schmidt_fin_orthogonal ùïú _ _ _ _ f

open submodule set order

/-- `gram_schmidt_fin` preserves span of vectors. -/
lemma span_gram_schmidt_fin {m : ‚Ñï} (f : fin m ‚Üí E) (c : ‚Ñï) :
  span ùïú (range (gram_schmidt_fin ùïú f)) = span ùïú (range f) :=
begin
  cases m,
  { simp only [matrix.range_empty] },
  { haveI : fact (0 < m.succ) := ‚ü®nat.zero_lt_succ m‚ü©,
    rw [gram_schmidt_fin, range_comp (gram_schmidt ùïú (Œª (j : ‚Ñï), f (fin.of_nat' j))) coe,
      fin.range_coe, ‚Üêimage_univ, span_gram_schmidt ùïú (Œª i, f (fin.of_nat' i)) m.succ,
      image_comp f (Œª (x : ‚Ñï), fin.of_nat' x), fin.image_of_nat', image_univ] }
end

/-- If the input of `gram_schmidt_fin` is linearly independent, then the output is non-zero. -/
lemma gram_schmidt_fin_ne_zero {m : ‚Ñï} (f : fin m ‚Üí E)
  (h‚ÇÄ : linear_independent ùïú f) :
    ‚àÄ i, gram_schmidt_fin ùïú f i ‚â† 0 :=
begin
  intro i,
  haveI : fact (0 < m) := ‚ü®lt_of_le_of_lt (nat.zero_le _) i.2‚ü©,
  have : linear_independent ùïú ((Œª (j : fin m), f (fin.of_nat' ‚Üëj))),
    by simpa only [fin.of_nat'_coe],
  exact gram_schmidt_ne_zero_aux ùïú (Œª (j : ‚Ñï), f (fin.of_nat' j)) m this i.1 i.2,
end

/-- The normalized `gram_schmidt` (Finite version).
Each vector in `gram_schmidt_normed_fin` has unit length. -/
noncomputable def gram_schmidt_normed_fin {m : ‚Ñï} (f : fin m ‚Üí E) (n : fin m) : E :=
  have hm : fact (0 < m), from ‚ü®lt_of_le_of_lt (nat.zero_le _) n.2‚ü©,
  gram_schmidt_normed ùïú (Œª i, f (@fin.of_nat' m hm i)) n

lemma gram_schmidt_normed_fin_def {m : ‚Ñï} (f : fin m ‚Üí E) (n : fin m) :
  gram_schmidt_normed_fin ùïú f n = (‚à•gram_schmidt_fin ùïú f n‚à• : ùïú)‚Åª¬π ‚Ä¢ (gram_schmidt_fin ùïú f n) :=
rfl

lemma gram_schmidt_normed_fin_unit_length {m : ‚Ñï} (f : fin m ‚Üí E) (n : fin m)
  (h‚ÇÄ : linear_independent ùïú f):
    ‚à•gram_schmidt_normed_fin ùïú f n‚à• = 1 :=
by simp only [gram_schmidt_fin_ne_zero ùïú f h‚ÇÄ,
  gram_schmidt_normed_fin_def, norm_smul_inv_norm, ne.def, not_false_iff]

/-- **Gram-Schmidt Orthonormalization** (Finite version):
`gram_schmidt_normed_fin` produces an orthornormal system of vectors. -/
theorem gram_schmidt_fin_orthonormal {m : ‚Ñï} (f : fin m ‚Üí E) (h‚ÇÄ : linear_independent ùïú f) :
  orthonormal ùïú (gram_schmidt_normed_fin ùïú f) :=
begin
  unfold orthonormal,
  split,
  { intro i, simp only [gram_schmidt_normed_fin_unit_length, h‚ÇÄ, forall_const] },
  { intros i j hij,
    simp only [gram_schmidt_normed_fin_def, inner_smul_left, inner_smul_right, is_R_or_C.conj_inv,
      is_R_or_C.conj_of_real, mul_eq_zero, inv_eq_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero],
    repeat { right },
    exact gram_schmidt_fin_orthogonal ùïú f hij, },
end

end fin
