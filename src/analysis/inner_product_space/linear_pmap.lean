/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/

import analysis.inner_product_space.adjoint
import topology.algebra.module.linear_pmap
import topology.algebra.module.basic

/-!

# Partially defined linear operators on Hilbert spaces

We will develop the basics of the theory of unbounded operators on Hilbert spaces.

## Main definitions

* `linear_pmap.is_formal_adjoint`: An operator `T` is a formal adjoint of `S` if for all `x` in the
domain of `T` and `y` in the domain of `S`, we have that `âŸªT x, yâŸ« = âŸªx, S yâŸ«`.
* `linear_pmap.adjoint`: The adjoint of a map `E â†’â‚—.[ğ•œ] F` as a map `F â†’â‚—.[ğ•œ] E`.

## Main statements

* `linear_pmap.adjoint_is_formal_adjoint`: The adjoint is a formal adjoint
* `linear_pmap.is_formal_adjoint.le_adjoint`: Every formal adjoint is contained in the adjoint
* `continuous_linear_map.to_pmap_adjoint_eq_adjoint_to_pmap_of_dense`: The adjoint on
`continuous_linear_map` and `linear_pmap` coincide.

## Notation

* For `T : E â†’â‚—.[ğ•œ] F` the adjoint can be written as `Tâ€ `.
This notation is localized in `linear_pmap`.

## Implementation notes

We use the junk value pattern to define the adjoint for all `linear_pmap`s. In the case that
`T : E â†’â‚—.[ğ•œ] F` is not densely defined the adjoint `Tâ€ ` is the zero map from `T.adjoint_domain` to
`E`.

## References

* [J. Weidmann, *Linear Operators in Hilbert Spaces*][weidmann_linear]

## Tags

Unbounded operators, closed operators
-/


noncomputable theory

open is_R_or_C
open_locale complex_conjugate classical

variables {ğ•œ E F G : Type*} [is_R_or_C ğ•œ]
variables [normed_add_comm_group E] [inner_product_space ğ•œ E]
variables [normed_add_comm_group F] [inner_product_space ğ•œ F]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

namespace linear_pmap

/-- An operator `T` is a formal adjoint of `S` if for all `x` in the domain of `T` and `y` in the
domain of `S`, we have that `âŸªT x, yâŸ« = âŸªx, S yâŸ«`. -/
def is_formal_adjoint (T : E â†’â‚—.[ğ•œ] F) (S : F â†’â‚—.[ğ•œ] E) : Prop :=
âˆ€ (x : T.domain) (y : S.domain), âŸªT x, yâŸ« = âŸª(x : E), S yâŸ«

variables {T : E â†’â‚—.[ğ•œ] F} {S : F â†’â‚—.[ğ•œ] E}

@[protected] lemma is_formal_adjoint.symm (h : T.is_formal_adjoint S) : S.is_formal_adjoint T :=
Î» y _, by rw [â†inner_conj_symm, â†inner_conj_symm (y : F), h]

variables (T)

/-- The domain of the adjoint operator.

This definition is needed to construct the adjoint operator and the preferred version to use is
`T.adjoint.domain` instead of `T.adjoint_domain`. -/
def adjoint_domain : submodule ğ•œ F :=
{ carrier := {y | continuous ((innerâ‚›â‚— ğ•œ y).comp T.to_fun)},
  zero_mem' := by { rw [set.mem_set_of_eq, linear_map.map_zero, linear_map.zero_comp],
      exact continuous_zero },
  add_mem' := Î» x y hx hy, by { rw [set.mem_set_of_eq, linear_map.map_add] at *, exact hx.add hy },
  smul_mem' := Î» a x hx, by { rw [set.mem_set_of_eq, linear_map.map_smulâ‚›â‚—] at *,
    exact hx.const_smul (conj a) } }

/-- The operator `Î» x, âŸªy, T xâŸ«` considered as a continuous linear operator from `T.adjoint_domain`
to `ğ•œ`. -/
def adjoint_domain_mk_clm (y : T.adjoint_domain) : T.domain â†’L[ğ•œ] ğ•œ :=
âŸ¨(innerâ‚›â‚— ğ•œ (y : F)).comp T.to_fun, y.propâŸ©

lemma adjoint_domain_mk_clm_apply (y : T.adjoint_domain) (x : T.domain) :
  adjoint_domain_mk_clm T y x = âŸª(y : F), T xâŸ« := rfl

variable {T}
variable (hT : dense (T.domain : set E))

include hT

/-- The unique continuous extension of the operator `adjoint_domain_mk_clm` to `E`. -/
def adjoint_domain_mk_clm_extend (y : T.adjoint_domain) :
  E â†’L[ğ•œ] ğ•œ :=
(T.adjoint_domain_mk_clm y).extend (submodule.subtypeL T.domain)
  hT.dense_range_coe uniform_embedding_subtype_coe.to_uniform_inducing

@[simp] lemma adjoint_domain_mk_clm_extend_apply (y : T.adjoint_domain) (x : T.domain) :
  adjoint_domain_mk_clm_extend hT y (x : E) = âŸª(y : F), T xâŸ« :=
continuous_linear_map.extend_eq _ _ _ _ _

variables [complete_space E]

lemma exists_unique_adjoint_elem (y : T.adjoint_domain) : âˆƒ! (w : E),
  âˆ€ (x : T.domain), âŸªw, xâŸ« = âŸª(y : F), T xâŸ« :=
exists_unique_of_exists_of_unique
  -- For the existence we use the FrÃ©chet-Riesz representation theorem and extend
  -- the map that is only defined on `T.domain` to `E`:
  âŸ¨(inner_product_space.to_dual ğ•œ E).symm (adjoint_domain_mk_clm_extend hT y),
    -- Implementation note: this is true `by simp`
    by simp only [inner_product_space.to_dual_symm_apply, adjoint_domain_mk_clm_extend_apply hT,
      eq_self_iff_true, forall_const]âŸ©
  -- The uniqueness follows directly from the fact that `T.domain` is dense in `E`.
  (Î» _ _ hyâ‚ hyâ‚‚, hT.eq_of_inner_left (Î» v, (hyâ‚ v).trans (hyâ‚‚ v).symm))

/-- The image of the adjoint operator.

This is an auxiliary definition needed to define the adjoint operator as a `linear_pmap`. -/
def adjoint_elem (y : T.adjoint_domain) : E :=
(exists_unique_adjoint_elem hT y).exists.some

lemma adjoint_elem_spec (y : T.adjoint_domain) (x : T.domain) :
  âŸªadjoint_elem hT y, xâŸ« = âŸª(y : F), T xâŸ« :=
(exists_unique_adjoint_elem hT y).exists.some_spec _

/-- The adjoint as a linear map from its domain to `E`.

This is an auxiliary definition needed to define the adjoint operator as a `linear_pmap` without
the assumption that `T.domain` is dense. -/
def adjoint_aux : T.adjoint_domain â†’â‚—[ğ•œ] E :=
{ to_fun := adjoint_elem hT,
  map_add' := Î» _ _, hT.eq_of_inner_left $ Î» _,
    by simp only [inner_add_left, adjoint_elem_spec, submodule.coe_add],
  map_smul' := Î» _ _, hT.eq_of_inner_left $ Î» _,
    by simp only [inner_smul_left, adjoint_elem_spec, submodule.coe_smul_of_tower,
      ring_hom.id_apply] }

omit hT

variable (T)

/-- The adjoint operator as a partially defined linear operator. -/
def adjoint : F â†’â‚—.[ğ•œ] E :=
{ domain := T.adjoint_domain,
  to_fun := if hT : dense (T.domain : set E) then adjoint_aux hT else 0 }

localized "postfix (name := adjoint) `â€ `:1100 := linear_pmap.adjoint" in linear_pmap

lemma mem_adjoint_domain_iff (y : F) :
  y âˆˆ Tâ€ .domain â†” continuous ((innerâ‚›â‚— ğ•œ y).comp T.to_fun) := iff.rfl

variable {T}

lemma mem_adjoint_domain_of_exists (y : F) (h : âˆƒ w : E, âˆ€ (x : T.domain), âŸªw, xâŸ« = âŸªy, T xâŸ«) :
  y âˆˆ Tâ€ .domain :=
begin
  cases h with w hw,
  rw T.mem_adjoint_domain_iff,
  have : continuous ((innerSL ğ•œ w).comp T.domain.subtypeL) := by continuity,
  convert this using 1,
  exact funext (Î» x, (hw x).symm),
end

lemma adjoint_apply_of_not_dense (hT : Â¬ dense (T.domain : set E)) (y : Tâ€ .domain) : Tâ€  y = 0 :=
begin
  change (if hT : dense (T.domain : set E) then adjoint_aux hT else 0) y = _,
  simp only [hT, not_false_iff, dif_neg, linear_map.zero_apply],
end

include hT

lemma adjoint_apply_of_dense (y : Tâ€ .domain) : Tâ€  y = adjoint_elem hT y :=
begin
  change (if hT : dense (T.domain : set E) then adjoint_aux hT else 0) y = _,
  simp only [hT, adjoint_aux, dif_pos, linear_map.coe_mk],
end

/-- The fundamental property of the adjoint. -/
lemma adjoint_is_formal_adjoint : Tâ€ .is_formal_adjoint T :=
begin
  intros x y,
  rw adjoint_apply_of_dense hT,
  exact adjoint_elem_spec hT x y,
end

/-- The adjoint is maximal in the sense that it contains every formal adjoint. -/
lemma is_formal_adjoint.le_adjoint (h : T.is_formal_adjoint S) : S â‰¤ Tâ€  :=
-- Trivially, every `x : S.domain` is in `T.adjoint.domain`
âŸ¨Î» x hx, mem_adjoint_domain_of_exists _ âŸ¨S âŸ¨x, hxâŸ©, h.symm âŸ¨x, hxâŸ©âŸ©,
  -- Equality on `S.domain` follows from equality
  -- `âŸªv, S xâŸ« = âŸªv, T.adjoint yâŸ«` for all `v : T.domain`:
  Î» _ _ hxy, hT.eq_of_inner_right (Î» _, by rw [â† h, hxy, â† (adjoint_is_formal_adjoint hT).symm])âŸ©

end linear_pmap

namespace continuous_linear_map

variables [complete_space E] [complete_space F]
variables (A : E â†’L[ğ•œ] F) {p : submodule ğ•œ E}

/-- Restricting `A` to a dense submodule and taking the `linear_pmap.adjoint` is the same
as taking the `continuous_linear_map.adjoint` interpreted as a `linear_pmap`. -/
lemma to_pmap_adjoint_eq_adjoint_to_pmap_of_dense (hp : dense (p : set E)) :
  (A.to_pmap p).adjoint = A.adjoint.to_pmap âŠ¤ :=
begin
  have hp' : dense ((A.to_pmap p).domain : set E) := hp,
  ext,
  { simp only [to_linear_map_eq_coe, linear_map.to_pmap_domain, submodule.mem_top, iff_true,
      linear_pmap.mem_adjoint_domain_iff, linear_map.coe_comp, innerâ‚›â‚—_apply_coe],
    exact ((innerSL ğ•œ x).comp $ A.comp $ submodule.subtypeL _).cont },
  intros x y hxy,
  refine hp'.eq_of_inner_left (Î» v, _),
  simp only [linear_pmap.adjoint_is_formal_adjoint hp' x v, adjoint_inner_left, hxy,
    linear_map.to_pmap_apply, to_linear_map_eq_coe, coe_coe],
end

end continuous_linear_map
