/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/

import topology.algebra.algebra
import analysis.inner_product_space.basic

/-!
# Inner product space derived from a norm

This file defines an `inner_product_space` instance from a norm that respects the
parallellogram identity. The parallelogram identity is a way to express the inner product of `x` and
`y` in terms of the norms of `x`, `y`, `x + y`, `x - y`.

## Main results

- `inner_product_space.of_norm`: a normed space whose norm respects the parallellogram identity,
  can be seen as an inner product space.

## TODO

Move upstream to `analysis.inner_product_space.basic`.

## References

- [Jordan, P. and von Neumann, J., *On inner products in linear, metric spaces*][Jordan1935]
- https://math.stackexchange.com/questions/21792/norms-induced-by-inner-products-and-the-parallelogram-law
- https://math.dartmouth.edu/archive/m113w10/public_html/jordan-vneumann-thm.pdf

## Tags

inner product space, Hilbert space, norm
-/

open is_R_or_C
open_locale complex_conjugate

variables {ğ•œ : Type*} [is_R_or_C ğ•œ] (E : Type*) [normed_add_comm_group E]

/-- Predicate for the parallelogram identity to hold in a normed group. This is a scalar-less
version of `inner_product_space`. If you have an `inner_product_spaceable` assumption, you can
locally upgrade that to `inner_product_space ğ•œ E` using `casesI nonempty_inner_product_space ğ•œ E`.
-/
class inner_product_spaceable : Prop :=
(parallelogram_identity :
  âˆ€ x y : E, â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–))

variables (ğ•œ) {E}

lemma inner_product_space.to_inner_product_spaceable [inner_product_space ğ•œ E] :
  inner_product_spaceable E :=
âŸ¨parallelogram_law_with_norm ğ•œâŸ©

@[priority 100] -- See note [lower instance priority]
instance inner_product_space.to_inner_product_spaceable_of_real [inner_product_space â„ E] :
  inner_product_spaceable E :=
âŸ¨parallelogram_law_with_norm â„âŸ©

variables [normed_space ğ•œ E]

local notation `ğ“š` := algebra_map â„ ğ•œ

/-- Auxiliary definition of the inner product derived from the norm. -/
private noncomputable def inner_ (x y : E) : ğ•œ :=
4â»Â¹ * ((ğ“š â€–x + yâ€–) * (ğ“š â€–x + yâ€–) - (ğ“š â€–x - yâ€–) * (ğ“š â€–x - yâ€–)
          + (I:ğ•œ) * (ğ“š â€–(I:ğ•œ) â€¢ x + yâ€–) * (ğ“š â€–(I:ğ•œ) â€¢ x + yâ€–)
          - (I:ğ•œ) * (ğ“š â€–(I:ğ•œ) â€¢ x - yâ€–) * (ğ“š â€–(I:ğ•œ) â€¢ x - yâ€–))

namespace inner_product_spaceable

variables {ğ•œ} (E)

/-- Auxiliary definition for the `add_left` property -/
private def inner_prop (r : ğ•œ) : Prop := âˆ€ x y : E, inner_ ğ•œ (r â€¢ x) y = conj r * inner_ ğ•œ x y

variables {E}

lemma inner_prop_neg_one : inner_prop E ((-1 : â„¤) : ğ•œ) :=
begin
  intros x y,
  simp only [inner_, neg_mul_eq_neg_mul, one_mul, int.cast_one, one_smul, ring_hom.map_one,
    map_neg, int.cast_neg, neg_smul, neg_one_mul],
  rw neg_mul_comm,
  congr' 1,
  have hâ‚ : â€–-x - yâ€– = â€–x + yâ€–,
  { rw [â†neg_add', norm_neg], },
  have hâ‚‚ : â€–-x + yâ€– = â€–x - yâ€–,
  { rw [â†neg_sub, norm_neg, sub_eq_neg_add], },
  have hâ‚ƒ : â€–(I : ğ•œ) â€¢ (-x) + yâ€– = â€–(I : ğ•œ) â€¢ x - yâ€–,
  { rw [â†neg_sub, norm_neg, sub_eq_neg_add, â†smul_neg], },
  have hâ‚„ : â€–(I : ğ•œ) â€¢ (-x) - yâ€– = â€–(I : ğ•œ) â€¢ x + yâ€–,
  { rw [smul_neg, â†neg_add', norm_neg] },
  rw [hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„],
  ring,
end

lemma continuous.inner_ {f g : â„ â†’ E} (hf : continuous f) (hg : continuous g) :
  continuous (Î» x, inner_ ğ•œ (f x) (g x)) :=
by { unfold inner_, continuity }

lemma inner_.norm_sq (x : E) : â€–xâ€– ^ 2 = re (inner_ ğ•œ x x) :=
begin
  simp only [inner_],
  have hâ‚ : norm_sq (4 : ğ•œ) = 16,
  { have : ((4 : â„) : ğ•œ) = (4 : ğ•œ),
    { simp only [of_real_one, of_real_bit0] },
    rw [â†this, norm_sq_eq_def', is_R_or_C.norm_eq_abs,
      is_R_or_C.abs_of_nonneg (by norm_num : (0 : â„) â‰¤ 4)],
    norm_num },
  have hâ‚‚ : â€–x + xâ€– = 2 * â€–xâ€–,
  { rw [â†two_smul ğ•œ, norm_smul, is_R_or_C.norm_eq_abs, is_R_or_C.abs_two] },
  simp only [inner, hâ‚, hâ‚‚, one_im, bit0_zero, add_zero, norm_zero, I_re, of_real_im, map_add,
    bit0_im, zero_div, zero_mul, add_monoid_hom.map_neg, of_real_re, map_sub, sub_zero, inv_re,
    one_re, inv_im, bit0_re, mul_re, mul_zero, sub_self, neg_zero, algebra_map_eq_of_real],
  ring,
end

lemma inner_.conj_symm (x y : E) : conj (inner_ ğ•œ y x) = inner_ ğ•œ x y :=
begin
  simp only [inner_],
  have h4 : conj (4â»Â¹ : ğ•œ) = 4â»Â¹,
  { rw [is_R_or_C.conj_inv, â†of_real_one, â†of_real_bit0, â†of_real_bit0, conj_of_real] },
  rw [map_mul, h4],
  congr' 1,
  simp only [map_sub, map_add, algebra_map_eq_of_real, â†of_real_mul, conj_of_real, map_mul, conj_I],
  rw [add_comm y x, norm_sub_rev],
  by_cases hI : (I : ğ•œ) = 0,
  { simp only [hI, neg_zero, zero_mul] },
  have hâ‚ : â€–(I : ğ•œ) â€¢ y - xâ€– = â€–(I : ğ•œ) â€¢ x + yâ€–,
  { transitivity â€–(I : ğ•œ) â€¢ ((I : ğ•œ) â€¢ y - x)â€–,
    { rw [norm_smul, norm_I_of_nonzero hI, one_mul] },
    { rw [smul_sub, smul_smul, I_mul_I_of_nonzero hI, neg_one_smul, â†neg_add', add_comm,
        norm_neg] } },
  have hâ‚‚ : â€–(I : ğ•œ) â€¢ y + xâ€– = â€–(I : ğ•œ) â€¢ x - yâ€–,
  { transitivity â€–(I : ğ•œ) â€¢ ((I : ğ•œ) â€¢ y + x)â€–,
    { rw [norm_smul, norm_I_of_nonzero hI, one_mul] },
    { rw [smul_add, smul_smul, I_mul_I_of_nonzero hI, neg_one_smul, â†neg_add_eq_sub]  }},
  rw [hâ‚, hâ‚‚, â†sub_add_eq_add_sub],
  simp only [neg_mul, sub_eq_add_neg, neg_neg],
end

variables [inner_product_spaceable E]

private lemma add_left_aux1 (x y z : E) :
  â€–x + y + zâ€– * â€–x + y + zâ€– =
    (â€–2 â€¢ x + yâ€– * â€–2 â€¢ x + yâ€– + â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€–) / 2 - â€–x - zâ€– * â€–x - zâ€– :=
begin
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm],
  convert parallelogram_identity (x + y + z) (x - z) using 4; { rw two_smul, abel }
end

private lemma add_left_aux2 (x y z : E) :
  â€–x + y - zâ€– * â€–x + y - zâ€– =
    (â€–2 â€¢ x + yâ€– * â€–2 â€¢ x + yâ€– + â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€–) / 2 - â€–x + zâ€– * â€–x + zâ€– :=
begin
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm],
  have hâ‚€ := parallelogram_identity (x + y - z) (x + z),
  convert hâ‚€ using 4; { rw two_smul, abel }
end

private lemma add_left_aux2' (x y z : E) :
  â€–x + y + zâ€– * â€–x + y + zâ€– - â€–x + y - zâ€– * â€–x + y - zâ€– =
    â€–x + zâ€– * â€–x + zâ€– - â€–x - zâ€– * â€–x - zâ€– +
        (â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€– - â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€–) / 2 :=
by { rw [add_left_aux1 , add_left_aux2], ring }

private lemma add_left_aux3 (y z : E) :
  â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€– = 2 * (â€–y + zâ€– * â€–y + zâ€– + â€–zâ€– * â€–zâ€–) - â€–yâ€– * â€–yâ€– :=
begin
  apply eq_sub_of_add_eq,
  convert parallelogram_identity (y + z) z using 4; { try { rw two_smul }, abel }
end

private lemma add_left_aux4 (y z : E) :
  â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€– = 2 * (â€–y - zâ€– * â€–y - zâ€– + â€–zâ€– * â€–zâ€–) - â€–yâ€– * â€–yâ€– :=
begin
  apply eq_sub_of_add_eq',
  have hâ‚€ := parallelogram_identity (y - z) z,
  convert hâ‚€ using 4; { try { rw two_smul }, abel }
end

private lemma add_left_aux4' (y z : E) :
  (â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€– - â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€–) / 2 =
    (â€–y + zâ€– * â€–y + zâ€–) - (â€–y - zâ€– * â€–y - zâ€–) :=
by { rw [add_left_aux3 , add_left_aux4], ring }

private lemma add_left_aux5 (x y z : E) :
  â€–(I : ğ•œ) â€¢ (x + y) + zâ€– * â€–(I : ğ•œ) â€¢ (x + y) + zâ€– =
    (â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– * â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– +
      â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€–) / 2 - â€–(I : ğ•œ) â€¢ x - zâ€– * â€–(I : ğ•œ) â€¢ x - zâ€– :=
begin
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm],
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ (x + y) + z) ((I : ğ•œ) â€¢ x - z),
  convert hâ‚€ using 4; { try { simp only [two_smul, smul_add] }, abel }
end

private lemma add_left_aux6 (x y z : E) :
  â€–(I : ğ•œ) â€¢ (x + y) - zâ€– * â€–(I : ğ•œ) â€¢ (x + y) - zâ€– =
    (â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– * â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– +
      â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€–) / 2 -
        â€–(I : ğ•œ) â€¢ x + zâ€– * â€–(I : ğ•œ) â€¢ x + zâ€– :=
begin
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm],
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ (x + y) - z) ((I : ğ•œ) â€¢ x + z),
  convert hâ‚€ using 4; { try { simp only [two_smul, smul_add] }, abel }
end

private lemma add_left_aux7 (y z : E) :
  â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– =
    2 * (â€–(I : ğ•œ) â€¢ y + zâ€– * â€–(I : ğ•œ) â€¢ y + zâ€– + â€–zâ€– * â€–zâ€–) - â€–(I : ğ•œ) â€¢ yâ€– * â€–(I : ğ•œ) â€¢ yâ€– :=
begin
  apply eq_sub_of_add_eq,
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ y + z) z,
  convert hâ‚€ using 4; { try { simp only [two_smul, smul_add] }, abel }
end

private lemma add_left_aux8 (y z : E) :
  â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– =
    2 * (â€–(I : ğ•œ) â€¢ y - zâ€– * â€–(I : ğ•œ) â€¢ y - zâ€– + â€–zâ€– * â€–zâ€–) - â€–(I : ğ•œ) â€¢ yâ€– * â€–(I : ğ•œ) â€¢ yâ€– :=
begin
  apply eq_sub_of_add_eq',
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ y - z) z,
  convert hâ‚€ using 4; { try { simp only [two_smul, smul_add] }, abel }
end

lemma add_left (x y z : E) : inner_ ğ•œ (x + y) z = inner_ ğ•œ x z + inner_ ğ•œ y z :=
begin
  simp only [inner_, â†mul_add],
  congr,
  simp only [mul_assoc, â†map_mul, add_sub_assoc, â†mul_sub, â†map_sub],
  rw add_add_add_comm,
  simp only [â†map_add, â†mul_add],
  congr,
  { rw [â†add_sub_assoc, add_left_aux2', add_left_aux4'] },
  { rw [add_left_aux5, add_left_aux6, add_left_aux7, add_left_aux8],
    simp only [map_sub, map_mul, map_add, div_eq_mul_inv],
    ring }
end

lemma nat (n : â„•) (x y : E) : inner_ ğ•œ ((n : ğ•œ) â€¢ x) y = (n : ğ•œ) * inner_ ğ•œ x y :=
begin
  induction n with n ih,
  { simp only [inner_, nat.nat_zero_eq_zero, zero_sub, nat.cast_zero, zero_mul, eq_self_iff_true,
      zero_smul, zero_add, mul_zero, sub_self, norm_neg, smul_zero], },
  { simp only [nat.cast_succ, add_smul, one_smul],
    rw [add_left, ih, add_mul, one_mul] }
end

private lemma nat_prop (r : â„•) : inner_prop E (r : ğ•œ) :=
Î» x y, by { simp only [map_nat_cast], exact nat r x y }

private lemma int_prop (n : â„¤) : inner_prop E (n : ğ•œ) :=
begin
  intros x y,
  rw â†n.sign_mul_nat_abs,
  simp only [int.cast_coe_nat, map_nat_cast, map_int_cast, int.cast_mul, map_mul, mul_smul],
  obtain hn | rfl | hn := lt_trichotomy n 0,
  { rw [int.sign_eq_neg_one_of_neg hn, inner_prop_neg_one ((n.nat_abs : ğ•œ) â€¢ x), nat],
    simp only [map_neg, neg_mul, one_mul, mul_eq_mul_left_iff, true_or,
      int.nat_abs_eq_zero, eq_self_iff_true, int.cast_one, map_one, neg_inj, nat.cast_eq_zero,
      int.cast_neg] },
  { simp only [inner_, int.cast_zero, zero_sub, nat.cast_zero, zero_mul, eq_self_iff_true,
      int.sign_zero, zero_smul, zero_add, mul_zero, smul_zero, sub_self, norm_neg,
      int.nat_abs_zero] },
  { rw int.sign_eq_one_of_pos hn,
    simp only [one_mul, mul_eq_mul_left_iff, true_or, int.nat_abs_eq_zero, eq_self_iff_true,
      int.cast_one, one_smul, nat.cast_eq_zero, nat] }
end

private lemma rat_prop (r : â„š) : inner_prop E (r : ğ•œ) :=
begin
  intros x y,
  have : (r.denom : ğ•œ) â‰  0,
  { haveI : char_zero ğ•œ := is_R_or_C.char_zero_R_or_C,
    exact_mod_cast r.pos.ne' },
  rw [â†r.num_div_denom, â†mul_right_inj' this, â†nat r.denom _ y, smul_smul, rat.cast_div],
  simp only [map_nat_cast, rat.cast_coe_nat, map_int_cast, rat.cast_coe_int, map_divâ‚€],
  rw [â†mul_assoc, mul_div_cancel' _ this, int_prop _ x, map_int_cast],
end

private lemma real_prop (r : â„) : inner_prop E (r : ğ•œ) :=
begin
  intros x y,
  revert r,
  rw â†function.funext_iff,
  refine rat.dense_embedding_coe_real.dense.equalizer _ _ (funext $ Î» X, _),
  { exact (continuous_of_real.smul continuous_const).inner_ continuous_const },
  { exact (continuous_conj.comp continuous_of_real).mul continuous_const },
  { simp only [function.comp_app, is_R_or_C.of_real_rat_cast, rat_prop _ _] }
end

private lemma I_prop : inner_prop E (I : ğ•œ) :=
begin
  by_cases hI : (I : ğ•œ) = 0,
  { rw [hI, â†nat.cast_zero], exact nat_prop _  },
  intros x y,
  have hI' : (-I : ğ•œ) * I = 1,
  { rw [â†inv_I, inv_mul_cancel hI], },
  rw [conj_I, inner_, inner_, mul_left_comm],
  congr' 1,
  rw [smul_smul, I_mul_I_of_nonzero hI, neg_one_smul],
  rw [mul_sub, mul_add, mul_sub,
    mul_assoc I (ğ“š â€–I â€¢ x - yâ€–), â†mul_assoc (-I) I, hI', one_mul,
    mul_assoc I (ğ“š â€–I â€¢ x + yâ€–), â†mul_assoc (-I) I, hI', one_mul],
  have hâ‚ : â€–-x - yâ€– = â€–x + yâ€–,
  { rw [â†neg_add', norm_neg], },
  have hâ‚‚ : â€–-x + yâ€– = â€–x - yâ€–,
  { rw [â†neg_sub, norm_neg, sub_eq_neg_add], },
  rw [hâ‚, hâ‚‚],
  simp only [sub_eq_add_neg, mul_assoc],
  rw [â†neg_mul_eq_neg_mul, â†neg_mul_eq_neg_mul],
  abel
end

lemma inner_prop (r : ğ•œ) : inner_prop E r :=
begin
  intros x y,
  rw [â†re_add_im r, add_smul, add_left, real_prop _ x, â†smul_smul, real_prop _ _ y, I_prop,
    map_add, map_mul, conj_of_real, conj_of_real, conj_I],
  ring,
end

end inner_product_spaceable

open inner_product_spaceable

/-- **FrÃ©chetâ€“von Neumannâ€“Jordan Theorem**. A normed space `E` whose norm satisfies the
parallelogram identity can be given a compatible inner product. -/
noncomputable def inner_product_space.of_norm
  (h : âˆ€ x y : E, â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–)) :
  inner_product_space ğ•œ E :=
begin
  haveI : inner_product_spaceable E := âŸ¨hâŸ©,
  exact
  { inner := inner_ ğ•œ,
    norm_sq_eq_inner := inner_.norm_sq,
    conj_symm := inner_.conj_symm,
    add_left := add_left,
    smul_left := Î» _ _ _, inner_prop _ _ _ }
end

variables (ğ•œ E) [inner_product_spaceable E]

/-- **FrÃ©chetâ€“von Neumannâ€“Jordan Theorem**. A normed space `E` whose norm satisfies the
parallelogram identity can be given a compatible inner product. Do
`casesI nonempty_inner_product_space ğ•œ E` to locally upgrade `inner_product_spaceable E` to
`inner_product_space ğ•œ E`. -/
lemma nonempty_inner_product_space : nonempty (inner_product_space ğ•œ E) :=
âŸ¨{ inner := inner_ ğ•œ,
  norm_sq_eq_inner := inner_.norm_sq,
  conj_symm := inner_.conj_symm,
  add_left := add_left,
  smul_left := Î» _ _ _, inner_prop _ _ _ }âŸ©

variables {ğ•œ E} [normed_space â„ E]

-- TODO: Replace `inner_product_space.to_uniform_convex_space`
@[priority 100] -- See note [lower instance priority]
instance inner_product_spaceable.to_uniform_convex_space : uniform_convex_space E :=
by { casesI nonempty_inner_product_space â„ E, apply_instance }
