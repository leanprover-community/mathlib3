/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
import algebra.direct_sum.module
import analysis.complex.basic
import analysis.convex.uniform
import analysis.normed_space.completion
import analysis.normed_space.bounded_linear_maps
import linear_algebra.bilinear_form

import analysis.inner_product_space.basic

/-!
# Orthogonal complements of submodules

In this file, the `orthogonal` complement of a submodule `K` is defined, and basic API established.
Some of the more subtle results about the orthogonal complement are delayed to
`analysis.inner_product_space.projection`.

See also `bilin_form.orthogonal` for orthogonality with respect to a general bilinear form.

## Notation

The orthogonal complement of a submodule `K` is denoted by `Ká—®`.

The proposition that two submodules are orthogonal, `submodule.is_ortho`, is denoted by `U âŸ‚ V`.
Note this is not the same unicode symbol as `âŠ¥` (`has_bot`).
-/

variables {ğ•œ E F : Type*} [is_R_or_C ğ•œ]
variables [normed_add_comm_group E] [inner_product_space ğ•œ E]
variables [normed_add_comm_group F] [inner_product_space ğ•œ F]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

namespace submodule

variables (K : submodule ğ•œ E)

/-- The subspace of vectors orthogonal to a given subspace. -/
def orthogonal : submodule ğ•œ E :=
{ carrier := {v | âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0},
  zero_mem' := Î» _ _, inner_zero_right _,
  add_mem' := Î» x y hx hy u hu, by rw [inner_add_right, hx u hu, hy u hu, add_zero],
  smul_mem' := Î» c x hx u hu, by rw [inner_smul_right, hx u hu, mul_zero] }

notation K`á—®`:1200 := orthogonal K

/-- When a vector is in `Ká—®`. -/
lemma mem_orthogonal (v : E) : v âˆˆ Ká—® â†” âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0 := iff.rfl

/-- When a vector is in `Ká—®`, with the inner product the
other way round. -/
lemma mem_orthogonal' (v : E) : v âˆˆ Ká—® â†” âˆ€ u âˆˆ K, âŸªv, uâŸ« = 0 :=
by simp_rw [mem_orthogonal, inner_eq_zero_symm]

variables {K}

/-- A vector in `K` is orthogonal to one in `Ká—®`. -/
lemma inner_right_of_mem_orthogonal {u v : E} (hu : u âˆˆ K) (hv : v âˆˆ Ká—®) : âŸªu, vâŸ« = 0 :=
(K.mem_orthogonal v).1 hv u hu

/-- A vector in `Ká—®` is orthogonal to one in `K`. -/
lemma inner_left_of_mem_orthogonal {u v : E} (hu : u âˆˆ K) (hv : v âˆˆ Ká—®) : âŸªv, uâŸ« = 0 :=
by rw [inner_eq_zero_symm]; exact inner_right_of_mem_orthogonal hu hv

/-- A vector is in `(ğ•œ âˆ™ u)á—®` iff it is orthogonal to `u`. -/
lemma mem_orthogonal_singleton_iff_inner_right {u v : E} : v âˆˆ (ğ•œ âˆ™ u)á—® â†” âŸªu, vâŸ« = 0 :=
begin
  refine âŸ¨inner_right_of_mem_orthogonal (mem_span_singleton_self u), _âŸ©,
  intros hv w hw,
  rw mem_span_singleton at hw,
  obtain âŸ¨c, rflâŸ© := hw,
  simp [inner_smul_left, hv],
end

/-- A vector in `(ğ•œ âˆ™ u)á—®` is orthogonal to `u`. -/
lemma mem_orthogonal_singleton_iff_inner_left {u v : E} : v âˆˆ (ğ•œ âˆ™ u)á—® â†” âŸªv, uâŸ« = 0 :=
by rw [mem_orthogonal_singleton_iff_inner_right, inner_eq_zero_symm]

lemma sub_mem_orthogonal_of_inner_left {x y : E}
  (h : âˆ€ (v : K), âŸªx, vâŸ« = âŸªy, vâŸ«) : x - y âˆˆ Ká—® :=
begin
  rw mem_orthogonal',
  intros u hu,
  rw [inner_sub_left, sub_eq_zero],
  exact h âŸ¨u, huâŸ©,
end

lemma sub_mem_orthogonal_of_inner_right {x y : E}
  (h : âˆ€ (v : K), âŸª(v : E), xâŸ« = âŸª(v : E), yâŸ«) : x - y âˆˆ Ká—® :=
begin
  intros u hu,
  rw [inner_sub_right, sub_eq_zero],
  exact h âŸ¨u, huâŸ©,
end

variables (K)

/-- `K` and `Ká—®` have trivial intersection. -/
lemma inf_orthogonal_eq_bot : K âŠ“ Ká—® = âŠ¥ :=
begin
  rw eq_bot_iff,
  intros x,
  rw mem_inf,
  exact Î» âŸ¨hx, hoâŸ©, inner_self_eq_zero.1 (ho x hx)
end

/-- `K` and `Ká—®` have trivial intersection. -/
lemma orthogonal_disjoint : disjoint K Ká—® :=
by simp [disjoint_iff, K.inf_orthogonal_eq_bot]

/-- `Ká—®` can be characterized as the intersection of the kernels of the operations of
inner product with each of the elements of `K`. -/
lemma orthogonal_eq_inter : Ká—® = â¨… v : K, linear_map.ker (innerSL ğ•œ (v : E)) :=
begin
  apply le_antisymm,
  { rw le_infi_iff,
    rintros âŸ¨v, hvâŸ© w hw,
    simpa using hw _ hv },
  { intros v hv w hw,
    simp only [mem_infi] at hv,
    exact hv âŸ¨w, hwâŸ© }
end

/-- The orthogonal complement of any submodule `K` is closed. -/
lemma is_closed_orthogonal : is_closed (Ká—® : set E) :=
begin
  rw orthogonal_eq_inter K,
  have := Î» v : K, continuous_linear_map.is_closed_ker (innerSL ğ•œ (v : E)),
  convert is_closed_Inter this,
  simp only [infi_coe],
end

/-- In a complete space, the orthogonal complement of any submodule `K` is complete. -/
instance [complete_space E] : complete_space Ká—® := K.is_closed_orthogonal.complete_space_coe

variables (ğ•œ E)

/-- `orthogonal` gives a `galois_connection` between
`submodule ğ•œ E` and its `order_dual`. -/
lemma orthogonal_gc :
  @galois_connection (submodule ğ•œ E) (submodule ğ•œ E)áµ’áµˆ _ _
    orthogonal orthogonal :=
Î» Kâ‚ Kâ‚‚, âŸ¨Î» h v hv u hu, inner_left_of_mem_orthogonal hv (h hu),
          Î» h v hv u hu, inner_left_of_mem_orthogonal hv (h hu)âŸ©

variables {ğ•œ E}

/-- `orthogonal` reverses the `â‰¤` ordering of two
subspaces. -/
lemma orthogonal_le {Kâ‚ Kâ‚‚ : submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚) : Kâ‚‚á—® â‰¤ Kâ‚á—® :=
(orthogonal_gc ğ•œ E).monotone_l h

/-- `orthogonal.orthogonal` preserves the `â‰¤` ordering of two
subspaces. -/
lemma orthogonal_orthogonal_monotone {Kâ‚ Kâ‚‚ : submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚) :
  Kâ‚á—®á—® â‰¤ Kâ‚‚á—®á—® :=
orthogonal_le (orthogonal_le h)

/-- `K` is contained in `Ká—®á—®`. -/
lemma le_orthogonal_orthogonal : K â‰¤ Ká—®á—® := (orthogonal_gc ğ•œ E).le_u_l _

/-- The inf of two orthogonal subspaces equals the subspace orthogonal
to the sup. -/
lemma inf_orthogonal (Kâ‚ Kâ‚‚ : submodule ğ•œ E) : Kâ‚á—® âŠ“ Kâ‚‚á—® = (Kâ‚ âŠ” Kâ‚‚)á—® :=
(orthogonal_gc ğ•œ E).l_sup.symm

/-- The inf of an indexed family of orthogonal subspaces equals the
subspace orthogonal to the sup. -/
lemma infi_orthogonal {Î¹ : Type*} (K : Î¹ â†’ submodule ğ•œ E) : (â¨… i, (K i)á—®) = (supr K)á—® :=
(orthogonal_gc ğ•œ E).l_supr.symm

/-- The inf of a set of orthogonal subspaces equals the subspace orthogonal to the sup. -/
lemma Inf_orthogonal (s : set $ submodule ğ•œ E) : (â¨… K âˆˆ s, Ká—®) = (Sup s)á—® :=
(orthogonal_gc ğ•œ E).l_Sup.symm

@[simp] lemma top_orthogonal_eq_bot : (âŠ¤ : submodule ğ•œ E)á—® = âŠ¥ :=
begin
  ext,
  rw [mem_bot, mem_orthogonal],
  exact âŸ¨Î» h, inner_self_eq_zero.mp (h x mem_top), by { rintro rfl, simp }âŸ©
end

@[simp] lemma bot_orthogonal_eq_top : (âŠ¥ : submodule ğ•œ E)á—® = âŠ¤ :=
begin
  rw [â† top_orthogonal_eq_bot, eq_top_iff],
  exact le_orthogonal_orthogonal âŠ¤
end

@[simp] lemma orthogonal_eq_top_iff : Ká—® = âŠ¤ â†” K = âŠ¥ :=
begin
  refine âŸ¨_, by { rintro rfl, exact bot_orthogonal_eq_top }âŸ©,
  intro h,
  have : K âŠ“ Ká—® = âŠ¥ := K.orthogonal_disjoint.eq_bot,
  rwa [h, inf_comm, top_inf_eq] at this
end

lemma orthogonal_family_self :
  orthogonal_family ğ•œ (Î» b, â†¥(cond b K Ká—®)) (Î» b, (cond b K Ká—®).subtypeâ‚—áµ¢)
| tt tt := absurd rfl
| tt ff := Î» _ x y, inner_right_of_mem_orthogonal x.prop y.prop
| ff tt := Î» _ x y, inner_left_of_mem_orthogonal y.prop x.prop
| ff ff := absurd rfl

end submodule

@[simp]
lemma bilin_form_of_real_inner_orthogonal {E} [normed_add_comm_group E] [inner_product_space â„ E]
  (K : submodule â„ E) :
  bilin_form_of_real_inner.orthogonal K = Ká—® := rfl

/-!
### Orthogonality of submodules

In this section we define `submodule.is_ortho U V`, with notation `U âŸ‚ V`.

The API roughly matches that of `disjoint`.
-/
namespace submodule

/-- The proposition that two submodules are orthogonal. Has notation `U âŸ‚ V`. -/
def is_ortho (U V : submodule ğ•œ E) : Prop :=
U â‰¤ Vá—®

infix ` âŸ‚ `:50 := submodule.is_ortho

lemma is_ortho.le {U V : submodule ğ•œ E} (h : U âŸ‚ V) : U â‰¤ Vá—® := h

lemma is_ortho_iff_le {U V : submodule ğ•œ E} : U âŸ‚ V â†” U â‰¤ Vá—® := iff.rfl

@[symm]
lemma is_ortho.symm {U V : submodule ğ•œ E} (h : U âŸ‚ V) : V âŸ‚ U :=
(le_orthogonal_orthogonal _).trans (orthogonal_le h)
lemma is_ortho_comm {U V : submodule ğ•œ E} : U âŸ‚ V â†” V âŸ‚ U := âŸ¨is_ortho.symm, is_ortho.symmâŸ©
lemma symmetric_is_ortho : symmetric (is_ortho : submodule ğ•œ E â†’ submodule ğ•œ E â†’ Prop) :=
Î» _ _, is_ortho.symm

lemma is_ortho.inner_eq {U V : submodule ğ•œ E} (h : U âŸ‚ V) {u v : E} (hu : u âˆˆ U) (hv : v âˆˆ V) :
  âŸªu, vâŸ« = 0 :=
h.symm hv _ hu

lemma is_ortho_iff_inner_eq {U V : submodule ğ•œ E} : U âŸ‚ V â†” âˆ€ (u âˆˆ U) (v âˆˆ V), âŸªu, vâŸ« = 0 :=
forallâ‚„_congr $ Î» u hu v hv, inner_eq_zero_symm

@[simp] lemma is_ortho_bot_left {V : submodule ğ•œ E} : âŠ¥ âŸ‚ V := bot_le
@[simp] lemma is_ortho_bot_right {U : submodule ğ•œ E} : U âŸ‚ âŠ¥ := is_ortho_bot_left.symm

lemma is_ortho.mono_left {Uâ‚ Uâ‚‚ V : submodule ğ•œ E} (hU : Uâ‚‚ â‰¤ Uâ‚) (h : Uâ‚ âŸ‚ V) : Uâ‚‚ âŸ‚ V :=
hU.trans h

lemma is_ortho.mono_right {U Vâ‚ Vâ‚‚ : submodule ğ•œ E} (hV : Vâ‚‚ â‰¤ Vâ‚) (h : U âŸ‚ Vâ‚) : U âŸ‚ Vâ‚‚ :=
(h.symm.mono_left hV).symm

lemma is_ortho.mono {Uâ‚ Vâ‚ Uâ‚‚ Vâ‚‚ : submodule ğ•œ E} (hU : Uâ‚‚ â‰¤ Uâ‚) (hV : Vâ‚‚ â‰¤ Vâ‚) (h : Uâ‚ âŸ‚ Vâ‚) :
  Uâ‚‚ âŸ‚ Vâ‚‚ :=
(h.mono_right hV).mono_left hU

@[simp]
lemma is_ortho_self {U : submodule ğ•œ E} : U âŸ‚ U â†” U = âŠ¥ :=
âŸ¨Î» h, eq_bot_iff.mpr $ Î» x hx, inner_self_eq_zero.mp (h hx x hx), Î» h, h.symm â–¸ is_ortho_bot_leftâŸ©

@[simp] lemma self_is_ortho_orthogonal (U : submodule ğ•œ E) : U âŸ‚ Uá—® :=
le_orthogonal_orthogonal _

@[simp] lemma orthogonal_is_ortho_self (U : submodule ğ•œ E) : Uá—® âŸ‚ U :=
(self_is_ortho_orthogonal U).symm

@[simp]
lemma is_ortho_top_right {U : submodule ğ•œ E} : U âŸ‚ âŠ¤ â†” U = âŠ¥ :=
âŸ¨Î» h, eq_bot_iff.mpr $ Î» x hx, inner_self_eq_zero.mp (h hx _ mem_top),
  Î» h, h.symm â–¸ is_ortho_bot_leftâŸ©

@[simp]
lemma is_ortho_top_left {V : submodule ğ•œ E} : âŠ¤ âŸ‚ V â†” V = âŠ¥ :=
is_ortho_comm.trans is_ortho_top_right

/-- Orthogonal submodules are disjoint. -/
lemma is_ortho.disjoint {U V : submodule ğ•œ E} (h : U âŸ‚ V) : disjoint U V :=
(submodule.orthogonal_disjoint _).mono_right h.symm

@[simp] lemma is_ortho_sup_left {Uâ‚ Uâ‚‚ V : submodule ğ•œ E} : Uâ‚ âŠ” Uâ‚‚ âŸ‚ V â†” Uâ‚ âŸ‚ V âˆ§ Uâ‚‚ âŸ‚ V :=
sup_le_iff

@[simp] lemma is_ortho_sup_right {U Vâ‚ Vâ‚‚ : submodule ğ•œ E} : U âŸ‚ Vâ‚ âŠ” Vâ‚‚ â†” U âŸ‚ Vâ‚ âˆ§ U âŸ‚ Vâ‚‚ :=
is_ortho_comm.trans $ is_ortho_sup_left.trans $ is_ortho_comm.and is_ortho_comm

@[simp] lemma is_ortho_Sup_left {U : set (submodule ğ•œ E)} {V : submodule ğ•œ E} :
  Sup U âŸ‚ V â†” âˆ€ Uáµ¢ âˆˆ U, Uáµ¢ âŸ‚ V :=
Sup_le_iff

@[simp] lemma is_ortho_Sup_right {U : submodule ğ•œ E} {V : set (submodule ğ•œ E)} :
  U âŸ‚ Sup V â†” âˆ€ Váµ¢ âˆˆ V, U âŸ‚ Váµ¢ :=
is_ortho_comm.trans $ is_ortho_Sup_left.trans $ by simp_rw is_ortho_comm

@[simp] lemma is_ortho_supr_left {Î¹ : Sort*} {U : Î¹ â†’ submodule ğ•œ E} {V : submodule ğ•œ E} :
  supr U âŸ‚ V â†” âˆ€ i, U i âŸ‚ V :=
supr_le_iff

@[simp] lemma is_ortho_supr_right {Î¹ : Sort*} {U : submodule ğ•œ E} {V : Î¹ â†’ submodule ğ•œ E} :
  U âŸ‚ supr V â†” âˆ€ i, U âŸ‚ V i :=
is_ortho_comm.trans $ is_ortho_supr_left.trans $ by simp_rw is_ortho_comm

@[simp] lemma is_ortho_span {s t : set E} : span ğ•œ s âŸ‚ span ğ•œ t â†” âˆ€ (u âˆˆ s) (v âˆˆ t), âŸªu, vâŸ« = 0 :=
begin
  simp_rw [span_eq_supr_of_singleton_spans s, span_eq_supr_of_singleton_spans t,
    is_ortho_supr_left, is_ortho_supr_right, is_ortho_iff_le, span_le, set.subset_def,
    set_like.mem_coe, mem_orthogonal_singleton_iff_inner_left, set.mem_singleton_iff, forall_eq],
end

lemma is_ortho.map (f : E â†’â‚—áµ¢[ğ•œ] F) {U V : submodule ğ•œ E} (h : U âŸ‚ V) : U.map f âŸ‚ V.map f :=
begin
  rw is_ortho_iff_inner_eq at *,
  simp_rw [mem_map, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚,
    linear_isometry.inner_map_map],
  exact h,
end

lemma is_ortho.comap (f : E â†’â‚—áµ¢[ğ•œ] F) {U V : submodule ğ•œ F} (h : U âŸ‚ V) : U.comap f âŸ‚ V.comap f :=
begin
  rw is_ortho_iff_inner_eq at *,
  simp_rw [mem_comap, â†f.inner_map_map],
  intros u hu v hv,
  exact h _ hu _ hv,
end

@[simp] lemma is_ortho.map_iff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) {U V : submodule ğ•œ E} : U.map f âŸ‚ V.map f â†” U âŸ‚ V :=
âŸ¨Î» h, begin
  have hf : âˆ€ p : submodule ğ•œ E, (p.map f).comap f.to_linear_isometry = p :=
    comap_map_eq_of_injective f.injective,
  simpa only [hf] using h.comap f.to_linear_isometry,
end, is_ortho.map f.to_linear_isometryâŸ©

@[simp] lemma is_ortho.comap_iff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) {U V : submodule ğ•œ F} :
  U.comap f âŸ‚ V.comap f â†” U âŸ‚ V :=
âŸ¨Î» h, begin
  have hf : âˆ€ p : submodule ğ•œ F, (p.comap f).map f.to_linear_isometry = p :=
    map_comap_eq_of_surjective f.surjective,
  simpa only [hf] using h.map f.to_linear_isometry,
end, is_ortho.comap f.to_linear_isometryâŸ©

end submodule

/-- Two submodules in an orthogonal family with different indices are orthogonal. -/
lemma orthogonal_family.is_ortho {Î¹} {V : Î¹ â†’ submodule ğ•œ E}
  (hV : orthogonal_family ğ•œ (Î» i, V i) (Î» i, (V i).subtypeâ‚—áµ¢)) {i j : Î¹} (hij : i â‰  j) :
  V i âŸ‚ V j :=
Î» x hx y hy, hV hij.symm âŸ¨y, hyâŸ© âŸ¨x, hxâŸ©
