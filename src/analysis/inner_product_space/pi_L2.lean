/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, SÃ©bastien GouÃ«zel, Heather Macbeth
-/
import analysis.inner_product_space.projection
import linear_algebra.finite_dimensional
import analysis.normed_space.pi_Lp

/-!
# `LÂ²` inner product space structure on finite products of inner product spaces

The `LÂ²` norm on a finite product of inner product spaces is compatible with an inner product
$$
\langle x, y\rangle = \sum \langle x_i, y_i \rangle.
$$
This is recorded in this file as an inner product space instance on `pi_Lp 2`.

This file develops the notion of a finite dimensional Hilbert space over `ğ•œ = â„‚, â„`, referred to as
`E`. We define an `orthonormal_basis ğ•œ Î¹ E` as a linear isometric equivalence
between `E` and `euclidean_space ğ•œ Î¹`. Then `std_orthonormal_basis` shows that such an equivalence
always exists if `E` is finite dimensional. We provide language for converting between a basis
that is orthonormal and an orthonormal basis (e.g. `basis.to_orthonormal_basis`). We show that
orthonormal bases for each summand in a direct sum of spaces can be combined into an orthonormal
basis for the the whole sum in `direct_sum.submodule_is_internal.subordinate_orthonormal_basis`. In
the last section, various properties of matrices are explored.

## Main definitions

- `euclidean_space ğ•œ n`: defined to be `pi_Lp 2 (n â†’ ğ•œ)` for any `fintype n`, i.e., the space
  from functions to `n` to `ğ•œ` with the `LÂ²` norm. We register several instances on it (notably
  that it is a finite-dimensional inner product space).

- `orthonormal_basis ğ•œ Î¹`: defined to be an isometry to Euclidean space from a given
  finite-dimensional innner product space, `E â‰ƒâ‚—áµ¢[ğ•œ] euclidean_space ğ•œ Î¹`.

- `basis.to_orthonormal_basis`: constructs an `orthonormal_basis` for a finite-dimensional
  Euclidean space from a `basis` which is `orthonormal`.

- `orthonormal.exists_orthonormal_basis_extension`: provides an existential result of an
  `orthonormal_basis` extending a given orthonormal set

- `exists_orthonormal_basis`: provides an orthonormal basis on a finite dimensional vector space

- `std_orthonormal_basis`: provides an arbitrarily-chosen `orthonormal_basis` of a given finite
  dimensional inner product space

For consequences in infinite dimension (Hilbert bases, etc.), see the file
`analysis.inner_product_space.l2_space`.

-/

open real set filter is_R_or_C submodule
open_locale big_operators uniformity topological_space nnreal ennreal complex_conjugate direct_sum

noncomputable theory

variables {Î¹ : Type*} {Î¹' : Type*}
variables {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [inner_product_space ğ•œ E]
variables {E' : Type*} [inner_product_space ğ•œ E']
variables {F : Type*} [inner_product_space â„ F]
variables {F' : Type*} [inner_product_space â„ F']
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

/-
 If `Î¹` is a finite type and each space `f i`, `i : Î¹`, is an inner product space,
then `Î  i, f i` is an inner product space as well. Since `Î  i, f i` is endowed with the sup norm,
we use instead `pi_Lp 2 f` for the product space, which is endowed with the `L^2` norm.
-/
instance pi_Lp.inner_product_space {Î¹ : Type*} [fintype Î¹] (f : Î¹ â†’ Type*)
  [Î  i, inner_product_space ğ•œ (f i)] : inner_product_space ğ•œ (pi_Lp 2 f) :=
{ inner := Î» x y, âˆ‘ i, inner (x i) (y i),
  norm_sq_eq_inner :=
  begin
    intro x,
    have hâ‚‚ : 0 â‰¤ âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„) :=
      finset.sum_nonneg (Î» j hj, rpow_nonneg_of_nonneg (norm_nonneg (x j)) 2),
    simp only [norm, add_monoid_hom.map_sum, â† norm_sq_eq_inner, one_div],
    rw [â† rpow_nat_cast ((âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„)) ^ (2 : â„)â»Â¹) 2, â† rpow_mul hâ‚‚],
    norm_num,
  end,
  conj_sym :=
  begin
    intros x y,
    unfold inner,
    rw ring_hom.map_sum,
    apply finset.sum_congr rfl,
    rintros z -,
    apply inner_conj_sym,
  end,
  add_left := Î» x y z,
    show âˆ‘ i, inner (x i + y i) (z i) = âˆ‘ i, inner (x i) (z i) + âˆ‘ i, inner (y i) (z i),
    by simp only [inner_add_left, finset.sum_add_distrib],
  smul_left := Î» x y r,
    show âˆ‘ (i : Î¹), inner (r â€¢ x i) (y i) = (conj r) * âˆ‘ i, inner (x i) (y i),
    by simp only [finset.mul_sum, inner_smul_left] }

@[simp] lemma pi_Lp.inner_apply {Î¹ : Type*} [fintype Î¹] {f : Î¹ â†’ Type*}
  [Î  i, inner_product_space ğ•œ (f i)] (x y : pi_Lp 2 f) :
  âŸªx, yâŸ« = âˆ‘ i, âŸªx i, y iâŸ« :=
rfl

/-- The standard real/complex Euclidean space, functions on a finite type. For an `n`-dimensional
space use `euclidean_space ğ•œ (fin n)`. -/
@[reducible, nolint unused_arguments]
def euclidean_space (ğ•œ : Type*) [is_R_or_C ğ•œ]
  (n : Type*) [fintype n] : Type* := pi_Lp 2 (Î» (i : n), ğ•œ)

lemma euclidean_space.norm_eq {ğ•œ : Type*} [is_R_or_C ğ•œ] {n : Type*} [fintype n]
  (x : euclidean_space ğ•œ n) : âˆ¥xâˆ¥ = real.sqrt (âˆ‘ i, âˆ¥x iâˆ¥ ^ 2) :=
pi_Lp.norm_eq_of_L2 x

lemma euclidean_space.nnnorm_eq {ğ•œ : Type*} [is_R_or_C ğ•œ] {n : Type*} [fintype n]
  (x : euclidean_space ğ•œ n) : âˆ¥xâˆ¥â‚Š = nnreal.sqrt (âˆ‘ i, âˆ¥x iâˆ¥â‚Š ^ 2) :=
pi_Lp.nnnorm_eq_of_L2 x

lemma euclidean_space.dist_eq {ğ•œ : Type*} [is_R_or_C ğ•œ] {n : Type*} [fintype n]
  (x y : euclidean_space ğ•œ n) : dist x y = (âˆ‘ i, dist (x i) (y i) ^ 2).sqrt :=
(pi_Lp.dist_eq_of_L2 x y : _)

lemma euclidean_space.nndist_eq {ğ•œ : Type*} [is_R_or_C ğ•œ] {n : Type*} [fintype n]
  (x y : euclidean_space ğ•œ n) : nndist x y = (âˆ‘ i, nndist (x i) (y i) ^ 2).sqrt :=
(pi_Lp.nndist_eq_of_L2 x y : _)

lemma euclidean_space.edist_eq {ğ•œ : Type*} [is_R_or_C ğ•œ] {n : Type*} [fintype n]
  (x y : euclidean_space ğ•œ n) : edist x y = (âˆ‘ i, edist (x i) (y i) ^ 2) ^ (1 / 2 : â„) :=
(pi_Lp.edist_eq_of_L2 x y : _)

variables [fintype Î¹]

section
local attribute [reducible] pi_Lp

instance : finite_dimensional ğ•œ (euclidean_space ğ•œ Î¹) := by apply_instance
instance : inner_product_space ğ•œ (euclidean_space ğ•œ Î¹) := by apply_instance

@[simp] lemma finrank_euclidean_space :
  finite_dimensional.finrank ğ•œ (euclidean_space ğ•œ Î¹) = fintype.card Î¹ := by simp

lemma finrank_euclidean_space_fin {n : â„•} :
  finite_dimensional.finrank ğ•œ (euclidean_space ğ•œ (fin n)) = n := by simp

lemma euclidean_space.inner_eq_star_dot_product (x y : euclidean_space ğ•œ Î¹) :
  âŸªx, yâŸ« = matrix.dot_product (star $ pi_Lp.equiv _ _ x) (pi_Lp.equiv _ _ y) := rfl

/-- A finite, mutually orthogonal family of subspaces of `E`, which span `E`, induce an isometry
from `E` to `pi_Lp 2` of the subspaces equipped with the `L2` inner product. -/
def direct_sum.is_internal.isometry_L2_of_orthogonal_family
  [decidable_eq Î¹] {V : Î¹ â†’ submodule ğ•œ E} (hV : direct_sum.is_internal V)
  (hV' : @orthogonal_family ğ•œ _ _ _ _ (Î» i, V i) _ (Î» i, (V i).subtypeâ‚—áµ¢)) :
  E â‰ƒâ‚—áµ¢[ğ•œ] pi_Lp 2 (Î» i, V i) :=
begin
  let eâ‚ := direct_sum.linear_equiv_fun_on_fintype ğ•œ Î¹ (Î» i, V i),
  let eâ‚‚ := linear_equiv.of_bijective (direct_sum.coe_linear_map V) hV.injective hV.surjective,
  refine (eâ‚‚.symm.trans eâ‚).isometry_of_inner _,
  suffices : âˆ€ v w, âŸªv, wâŸ« = âŸªeâ‚‚ (eâ‚.symm v), eâ‚‚ (eâ‚.symm w)âŸ«,
  { intros vâ‚€ wâ‚€,
    convert this (eâ‚ (eâ‚‚.symm vâ‚€)) (eâ‚ (eâ‚‚.symm wâ‚€));
    simp only [linear_equiv.symm_apply_apply, linear_equiv.apply_symm_apply] },
  intros v w,
  transitivity âŸª(âˆ‘ i, (V i).subtypeâ‚—áµ¢ (v i)), âˆ‘ i, (V i).subtypeâ‚—áµ¢ (w i)âŸ«,
  { simp only [sum_inner, hV'.inner_right_fintype, pi_Lp.inner_apply] },
  { congr; simp }
end

@[simp] lemma direct_sum.is_internal.isometry_L2_of_orthogonal_family_symm_apply
  [decidable_eq Î¹] {V : Î¹ â†’ submodule ğ•œ E} (hV : direct_sum.is_internal V)
  (hV' : @orthogonal_family ğ•œ _ _ _ _ (Î» i, V i) _ (Î» i, (V i).subtypeâ‚—áµ¢))
  (w : pi_Lp 2 (Î» i, V i)) :
  (hV.isometry_L2_of_orthogonal_family hV').symm w = âˆ‘ i, (w i : E) :=
begin
  classical,
  let eâ‚ := direct_sum.linear_equiv_fun_on_fintype ğ•œ Î¹ (Î» i, V i),
  let eâ‚‚ := linear_equiv.of_bijective (direct_sum.coe_linear_map V) hV.injective hV.surjective,
  suffices : âˆ€ v : â¨ i, V i, eâ‚‚ v = âˆ‘ i, eâ‚ v i,
  { exact this (eâ‚.symm w) },
  intros v,
  simp [eâ‚‚, direct_sum.coe_linear_map, direct_sum.to_module, dfinsupp.sum_add_hom_apply]
end

end

variables (Î¹ ğ•œ)

-- TODO : This should be generalized to `pi_Lp` with finite dimensional factors.
/-- `pi_Lp.linear_equiv` upgraded to a continuous linear map between `euclidean_space ğ•œ Î¹`
and `Î¹ â†’ ğ•œ`. -/
@[simps] def euclidean_space.equiv :
  euclidean_space ğ•œ Î¹ â‰ƒL[ğ•œ] (Î¹ â†’ ğ•œ) :=
(pi_Lp.linear_equiv 2 ğ•œ (Î» i : Î¹, ğ•œ)).to_continuous_linear_equiv

variables {Î¹ ğ•œ}

-- TODO : This should be generalized to `pi_Lp`.
/-- The projection on the `i`-th coordinate of `euclidean_space ğ•œ Î¹`, as a linear map. -/
@[simps] def euclidean_space.projâ‚— (i : Î¹) :
  euclidean_space ğ•œ Î¹ â†’â‚—[ğ•œ] ğ•œ :=
(linear_map.proj i).comp (pi_Lp.linear_equiv 2 ğ•œ (Î» i : Î¹, ğ•œ) : euclidean_space ğ•œ Î¹ â†’â‚—[ğ•œ] Î¹ â†’ ğ•œ)

-- TODO : This should be generalized to `pi_Lp`.
/-- The projection on the `i`-th coordinate of `euclidean_space ğ•œ Î¹`,
as a continuous linear map. -/
@[simps] def euclidean_space.proj (i : Î¹) :
  euclidean_space ğ•œ Î¹ â†’L[ğ•œ] ğ•œ :=
âŸ¨euclidean_space.projâ‚— i, continuous_apply iâŸ©

-- TODO : This should be generalized to `pi_Lp`.
/-- The vector given in euclidean space by being `1 : ğ•œ` at coordinate `i : Î¹` and `0 : ğ•œ` at
all other coordinates. -/
def euclidean_space.single [decidable_eq Î¹] (i : Î¹) (a : ğ•œ) :
  euclidean_space ğ•œ Î¹ :=
(pi_Lp.equiv _ _).symm (pi.single i a)

@[simp] lemma pi_Lp.equiv_single [decidable_eq Î¹] (i : Î¹) (a : ğ•œ) :
  pi_Lp.equiv _ _ (euclidean_space.single i a) = pi.single i a := rfl

@[simp] lemma pi_Lp.equiv_symm_single [decidable_eq Î¹] (i : Î¹) (a : ğ•œ) :
  (pi_Lp.equiv _ _).symm (pi.single i a) = euclidean_space.single i a := rfl

@[simp] theorem euclidean_space.single_apply [decidable_eq Î¹] (i : Î¹) (a : ğ•œ) (j : Î¹) :
  (euclidean_space.single i a) j = ite (j = i) a 0 :=
by { rw [euclidean_space.single, pi_Lp.equiv_symm_apply, â† pi.single_apply i a j] }

lemma euclidean_space.inner_single_left [decidable_eq Î¹] (i : Î¹) (a : ğ•œ) (v : euclidean_space ğ•œ Î¹) :
  âŸªeuclidean_space.single i (a : ğ•œ), vâŸ« = conj a * (v i) :=
by simp [apply_ite conj]

lemma euclidean_space.inner_single_right [decidable_eq Î¹] (i : Î¹) (a : ğ•œ)
  (v : euclidean_space ğ•œ Î¹) :
  âŸªv, euclidean_space.single i (a : ğ•œ)âŸ« =  a * conj (v i) :=
by simp [apply_ite conj, mul_comm]

lemma euclidean_space.pi_Lp_congr_left_single [decidable_eq Î¹] {Î¹' : Type*} [fintype Î¹']
  [decidable_eq Î¹'] (e : Î¹' â‰ƒ Î¹) (i' : Î¹') :
  linear_isometry_equiv.pi_Lp_congr_left 2 ğ•œ ğ•œ e (euclidean_space.single i' (1:ğ•œ)) =
    euclidean_space.single (e i') (1:ğ•œ) :=
begin
  ext i,
  simpa using if_congr e.symm_apply_eq rfl rfl
end

variables (Î¹ ğ•œ E)

/-- An orthonormal basis on E is an identification of `E` with its dimensional-matching
`euclidean_space ğ•œ Î¹`. -/
structure orthonormal_basis := of_repr :: (repr : E â‰ƒâ‚—áµ¢[ğ•œ] euclidean_space ğ•œ Î¹)

variables {Î¹ ğ•œ E}

namespace orthonormal_basis

instance : inhabited (orthonormal_basis Î¹ ğ•œ (euclidean_space ğ•œ Î¹)) :=
âŸ¨of_repr (linear_isometry_equiv.refl ğ•œ (euclidean_space ğ•œ Î¹))âŸ©

/-- `b i` is the `i`th basis vector. -/
instance : has_coe_to_fun (orthonormal_basis Î¹ ğ•œ E) (Î» _, Î¹ â†’ E) :=
{ coe := Î» b i, by classical; exact b.repr.symm (euclidean_space.single i (1 : ğ•œ)) }

@[simp] lemma coe_of_repr [decidable_eq Î¹] (e : E â‰ƒâ‚—áµ¢[ğ•œ] euclidean_space ğ•œ Î¹) :
  â‡‘(orthonormal_basis.of_repr e) = Î» i, e.symm (euclidean_space.single i (1 : ğ•œ)) :=
begin
  rw coe_fn,
  unfold has_coe_to_fun.coe,
  funext,
  congr,
  simp only [eq_iff_true_of_subsingleton],
end

@[simp] protected lemma repr_symm_single [decidable_eq Î¹] (b : orthonormal_basis Î¹ ğ•œ E) (i : Î¹) :
  b.repr.symm (euclidean_space.single i (1:ğ•œ)) = b i :=
by { classical, congr, simp, }

@[simp] protected lemma repr_self [decidable_eq Î¹] (b : orthonormal_basis Î¹ ğ•œ E) (i : Î¹) :
  b.repr (b i) = euclidean_space.single i (1:ğ•œ) :=
by rw [â† b.repr_symm_single i, linear_isometry_equiv.apply_symm_apply]

protected lemma repr_apply_apply (b : orthonormal_basis Î¹ ğ•œ E) (v : E) (i : Î¹) :
  b.repr v i = âŸªb i, vâŸ« :=
begin
  classical,
  rw [â† b.repr.inner_map_map (b i) v, b.repr_self i, euclidean_space.inner_single_left],
  simp only [one_mul, eq_self_iff_true, map_one],
end

@[simp]
protected lemma orthonormal (b : orthonormal_basis Î¹ ğ•œ E) : orthonormal ğ•œ b :=
begin
  classical,
  rw orthonormal_iff_ite,
  intros i j,
  rw [â† b.repr.inner_map_map (b i) (b j), b.repr_self i, b.repr_self j,
    euclidean_space.inner_single_left, euclidean_space.single_apply, map_one, one_mul],
end

/-- The `basis Î¹ ğ•œ E` underlying the `orthonormal_basis` --/
protected def to_basis (b : orthonormal_basis Î¹ ğ•œ E) : basis Î¹ ğ•œ E :=
basis.of_equiv_fun b.repr.to_linear_equiv

@[simp] protected lemma coe_to_basis (b : orthonormal_basis Î¹ ğ•œ E) :
  (â‡‘b.to_basis : Î¹ â†’ E) = â‡‘b :=
begin
  change â‡‘(basis.of_equiv_fun b.repr.to_linear_equiv) = b,
  ext j,
  rw basis.coe_of_equiv_fun,
  congr,
end

@[simp] protected lemma coe_to_basis_repr (b : orthonormal_basis Î¹ ğ•œ E) :
  b.to_basis.equiv_fun = b.repr.to_linear_equiv :=
begin
  change (basis.of_equiv_fun b.repr.to_linear_equiv).equiv_fun = b.repr.to_linear_equiv,
  ext x j,
  simp only [basis.of_equiv_fun_repr_apply, linear_isometry_equiv.coe_to_linear_equiv,
    basis.equiv_fun_apply],
end

@[simp] protected lemma coe_to_basis_repr_apply (b : orthonormal_basis Î¹ ğ•œ E) (x : E) (i : Î¹) :
  b.to_basis.repr x i = b.repr x i :=
by {rw [â† basis.equiv_fun_apply, orthonormal_basis.coe_to_basis_repr,
      linear_isometry_equiv.coe_to_linear_equiv]}

protected lemma sum_repr (b : orthonormal_basis Î¹ ğ•œ E) (x : E) :
  âˆ‘ i, b.repr x i â€¢ b i = x :=
by { simp_rw [â† b.coe_to_basis_repr_apply, â† b.coe_to_basis], exact b.to_basis.sum_repr x }

protected lemma sum_repr_symm (b : orthonormal_basis Î¹ ğ•œ E) (v : euclidean_space ğ•œ Î¹) :
  âˆ‘ i , v i â€¢ b i = (b.repr.symm v) :=
by { simpa using (b.to_basis.equiv_fun_symm_apply v).symm }

protected lemma sum_inner_mul_inner (b : orthonormal_basis Î¹ ğ•œ E) (x y : E) :
  âˆ‘ i, âŸªx, b iâŸ« * âŸªb i, yâŸ« = âŸªx, yâŸ« :=
begin
  have := congr_arg (@innerSL ğ•œ _ _ _ x) (b.sum_repr y),
  rw map_sum at this,
  convert this,
  ext i,
  rw [smul_hom_class.map_smul, b.repr_apply_apply, mul_comm],
  refl,
end

/-- Mapping an orthonormal basis along a `linear_isometry_equiv`. -/
protected def map {G : Type*} [inner_product_space ğ•œ G] (b : orthonormal_basis Î¹ ğ•œ E)
  (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) :
  orthonormal_basis Î¹ ğ•œ G :=
{ repr := L.symm.trans b.repr }

@[simp] protected lemma map_apply {G : Type*} [inner_product_space ğ•œ G]
  (b : orthonormal_basis Î¹ ğ•œ E) (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) (i : Î¹) :
  b.map L i = L (b i) := rfl

/-- A basis that is orthonormal is an orthonormal basis. -/
def _root_.basis.to_orthonormal_basis (v : basis Î¹ ğ•œ E) (hv : orthonormal ğ•œ v) :
  orthonormal_basis Î¹ ğ•œ E :=
orthonormal_basis.of_repr $
linear_equiv.isometry_of_inner v.equiv_fun
begin
  intros x y,
  let p : euclidean_space ğ•œ Î¹ := v.equiv_fun x,
  let q : euclidean_space ğ•œ Î¹ := v.equiv_fun y,
  have key : âŸªp, qâŸ« = âŸªâˆ‘ i, p i â€¢ v i, âˆ‘ i, q i â€¢ v iâŸ«,
  { simp [sum_inner, inner_smul_left, hv.inner_right_fintype] },
  convert key,
  { rw [â† v.equiv_fun.symm_apply_apply x, v.equiv_fun_symm_apply] },
  { rw [â† v.equiv_fun.symm_apply_apply y, v.equiv_fun_symm_apply] }
end

@[simp] lemma _root_.basis.coe_to_orthonormal_basis_repr (v : basis Î¹ ğ•œ E) (hv : orthonormal ğ•œ v) :
  ((v.to_orthonormal_basis hv).repr : E â†’ euclidean_space ğ•œ Î¹) = v.equiv_fun :=
rfl

@[simp] lemma _root_.basis.coe_to_orthonormal_basis_repr_symm
  (v : basis Î¹ ğ•œ E) (hv : orthonormal ğ•œ v) :
  ((v.to_orthonormal_basis hv).repr.symm : euclidean_space ğ•œ Î¹ â†’ E) = v.equiv_fun.symm :=
rfl

@[simp] lemma _root_.basis.to_basis_to_orthonormal_basis (v : basis Î¹ ğ•œ E) (hv : orthonormal ğ•œ v) :
  (v.to_orthonormal_basis hv).to_basis = v :=
by simp [basis.to_orthonormal_basis, orthonormal_basis.to_basis]

@[simp] lemma _root_.basis.coe_to_orthonormal_basis (v : basis Î¹ ğ•œ E) (hv : orthonormal ğ•œ v) :
  (v.to_orthonormal_basis hv : Î¹ â†’ E) = (v : Î¹ â†’ E) :=
calc (v.to_orthonormal_basis hv : Î¹ â†’ E) = ((v.to_orthonormal_basis hv).to_basis : Î¹ â†’ E) :
  by { classical, rw orthonormal_basis.coe_to_basis }
... = (v : Î¹ â†’ E) : by simp

variable {v : Î¹ â†’ E}

/-- A finite orthonormal set that spans is an orthonormal basis -/
protected def mk (hon : orthonormal ğ•œ v) (hsp: submodule.span ğ•œ (set.range v) = âŠ¤):
  orthonormal_basis Î¹ ğ•œ E :=
(basis.mk (orthonormal.linear_independent hon) hsp).to_orthonormal_basis (by rwa basis.coe_mk)

@[simp]
protected lemma coe_mk (hon : orthonormal ğ•œ v) (hsp: submodule.span ğ•œ (set.range v) = âŠ¤) :
  â‡‘(orthonormal_basis.mk hon hsp) = v :=
by classical; rw [orthonormal_basis.mk, _root_.basis.coe_to_orthonormal_basis, basis.coe_mk]

/-- Any finite subset of a orthonormal family is an `orthonormal_basis` for its span. -/
protected def span {v' : Î¹' â†’ E} (h : orthonormal ğ•œ v') (s : finset Î¹') :
  orthonormal_basis s ğ•œ (span ğ•œ (s.image v' : set E)) :=
let
  eâ‚€' : basis s ğ•œ _ := basis.span (h.linear_independent.comp (coe : s â†’ Î¹') subtype.coe_injective),
  eâ‚€ : orthonormal_basis s ğ•œ _ := orthonormal_basis.mk
    begin
      convert orthonormal_span (h.comp (coe : s â†’ Î¹') subtype.coe_injective),
      ext,
      simp [eâ‚€', basis.span_apply],
    end eâ‚€'.span_eq,
  Ï† : span ğ•œ (s.image v' : set E) â‰ƒâ‚—áµ¢[ğ•œ] span ğ•œ (range (v' âˆ˜ (coe : s â†’ Î¹'))) :=
    linear_isometry_equiv.of_eq _ _
    begin
      rw [finset.coe_image, image_eq_range],
      refl
    end
in
eâ‚€.map Ï†.symm

@[simp] protected lemma span_apply {v' : Î¹' â†’ E} (h : orthonormal ğ•œ v') (s : finset Î¹') (i : s) :
  (orthonormal_basis.span h s i : E) = v' i :=
by simp only [orthonormal_basis.span, basis.span_apply, linear_isometry_equiv.of_eq_symm,
              orthonormal_basis.map_apply, orthonormal_basis.coe_mk,
              linear_isometry_equiv.coe_of_eq_apply]

open submodule

/-- A finite orthonormal family of vectors whose span has trivial orthogonal complement is an
orthonormal basis. -/
protected def mk_of_orthogonal_eq_bot (hon : orthonormal ğ•œ v) (hsp : (span ğ•œ (set.range v))á—® = âŠ¥) :
  orthonormal_basis Î¹ ğ•œ E :=
orthonormal_basis.mk hon
begin
  haveI : finite_dimensional ğ•œ (span ğ•œ (range v)) :=
    finite_dimensional.span_of_finite ğ•œ (finite_range v),
  haveI : complete_space (span ğ•œ (range v)) := finite_dimensional.complete ğ•œ _,
  rwa orthogonal_eq_bot_iff at hsp,
end

@[simp] protected lemma coe_of_orthogonal_eq_bot_mk (hon : orthonormal ğ•œ v)
  (hsp : (span ğ•œ (set.range v))á—® = âŠ¥) :
  â‡‘(orthonormal_basis.mk_of_orthogonal_eq_bot hon hsp) = v :=
orthonormal_basis.coe_mk hon _

variables [fintype Î¹']

/-- `b.reindex (e : Î¹ â‰ƒ Î¹')` is an `orthonormal_basis` indexed by `Î¹'` -/
def reindex (b : orthonormal_basis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') : orthonormal_basis Î¹' ğ•œ E :=
orthonormal_basis.of_repr (b.repr.trans (linear_isometry_equiv.pi_Lp_congr_left 2 ğ•œ ğ•œ e))

protected lemma reindex_apply (b : orthonormal_basis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') (i' : Î¹') :
  (b.reindex e) i' = b (e.symm i') :=
begin
  classical,
  dsimp [reindex, orthonormal_basis.has_coe_to_fun],
  rw coe_of_repr,
  dsimp,
  rw [â† b.repr_symm_single, linear_isometry_equiv.pi_Lp_congr_left_symm,
    euclidean_space.pi_Lp_congr_left_single],
end

@[simp] protected lemma coe_reindex (b : orthonormal_basis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') :
  â‡‘(b.reindex e) = â‡‘b âˆ˜ â‡‘(e.symm) :=
funext (b.reindex_apply e)

@[simp] protected lemma reindex_repr
  (b : orthonormal_basis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') (x : E) (i' : Î¹') :
  ((b.reindex e).repr x) i' = (b.repr x) (e.symm i') :=
by { classical,
  rw [orthonormal_basis.repr_apply_apply, b.repr_apply_apply, orthonormal_basis.coe_reindex] }

end orthonormal_basis

/-- If `f : E â‰ƒâ‚—áµ¢[ğ•œ] E'` is a linear isometry of inner product spaces then an orthonormal basis `v`
of `E` determines a linear isometry `e : E' â‰ƒâ‚—áµ¢[ğ•œ] euclidean_space ğ•œ Î¹`. This result states that
`e` may be obtained either by transporting `v` to `E'` or by composing with the linear isometry
`E â‰ƒâ‚—áµ¢[ğ•œ] euclidean_space ğ•œ Î¹` provided by `v`. -/
@[simp] lemma basis.map_isometry_euclidean_of_orthonormal (v : basis Î¹ ğ•œ E) (hv : orthonormal ğ•œ v)
  (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') :
  ((v.map f.to_linear_equiv).to_orthonormal_basis (hv.map_linear_isometry_equiv f)).repr =
    f.symm.trans (v.to_orthonormal_basis hv).repr :=
linear_isometry_equiv.to_linear_equiv_injective $ v.map_equiv_fun _

/-- `â„‚` is isometric to `â„Â²` with the Euclidean inner product. -/
def complex.isometry_euclidean : â„‚ â‰ƒâ‚—áµ¢[â„] (euclidean_space â„ (fin 2)) :=
(complex.basis_one_I.to_orthonormal_basis
begin
  rw orthonormal_iff_ite,
  intros i, fin_cases i;
  intros j; fin_cases j;
  simp [real_inner_eq_re_inner]
end).repr

@[simp] lemma complex.isometry_euclidean_symm_apply (x : euclidean_space â„ (fin 2)) :
  complex.isometry_euclidean.symm x = (x 0) + (x 1) * I :=
begin
  convert complex.basis_one_I.equiv_fun_symm_apply x,
  { simpa },
  { simp },
end

lemma complex.isometry_euclidean_proj_eq_self (z : â„‚) :
  â†‘(complex.isometry_euclidean z 0) + â†‘(complex.isometry_euclidean z 1) * (I : â„‚) = z :=
by rw [â† complex.isometry_euclidean_symm_apply (complex.isometry_euclidean z),
  complex.isometry_euclidean.symm_apply_apply z]

@[simp] lemma complex.isometry_euclidean_apply_zero (z : â„‚) :
  complex.isometry_euclidean z 0 = z.re :=
by { conv_rhs { rw â† complex.isometry_euclidean_proj_eq_self z }, simp }

@[simp] lemma complex.isometry_euclidean_apply_one (z : â„‚) :
  complex.isometry_euclidean z 1 = z.im :=
by { conv_rhs { rw â† complex.isometry_euclidean_proj_eq_self z }, simp }

/-- The isometry between `â„‚` and a two-dimensional real inner product space given by a basis. -/
def complex.isometry_of_orthonormal {v : basis (fin 2) â„ F} (hv : orthonormal â„ v) : â„‚ â‰ƒâ‚—áµ¢[â„] F :=
complex.isometry_euclidean.trans (v.to_orthonormal_basis hv).repr.symm

@[simp] lemma complex.map_isometry_of_orthonormal {v : basis (fin 2) â„ F} (hv : orthonormal â„ v)
  (f : F â‰ƒâ‚—áµ¢[â„] F') :
  complex.isometry_of_orthonormal (hv.map_linear_isometry_equiv f) =
    (complex.isometry_of_orthonormal hv).trans f :=
by simp [complex.isometry_of_orthonormal, linear_isometry_equiv.trans_assoc]

lemma complex.isometry_of_orthonormal_symm_apply
  {v : basis (fin 2) â„ F} (hv : orthonormal â„ v) (f : F) :
  (complex.isometry_of_orthonormal hv).symm f = (v.coord 0 f : â„‚) + (v.coord 1 f : â„‚) * I :=
by simp [complex.isometry_of_orthonormal]

lemma complex.isometry_of_orthonormal_apply
  {v : basis (fin 2) â„ F} (hv : orthonormal â„ v) (z : â„‚) :
  complex.isometry_of_orthonormal hv z = z.re â€¢ v 0 + z.im â€¢ v 1 :=
by simp [complex.isometry_of_orthonormal, (dec_trivial : (finset.univ : finset (fin 2)) = {0, 1})]

open finite_dimensional

/-! ### Existence of orthonormal basis, etc. -/

section finite_dimensional

variables {v : set E}

variables {A : Î¹ â†’ submodule ğ•œ E}

/-- Given an internal direct sum decomposition of a module `M`, and an orthonormal basis for each
of the components of the direct sum, the disjoint union of these orthonormal bases is an
orthonormal basis for `M`. -/
noncomputable def direct_sum.is_internal.collected_orthonormal_basis
  (hV : @orthogonal_family ğ•œ _ _ _ _ (Î» i, A i) _ (Î» i, (A i).subtypeâ‚—áµ¢))
  [decidable_eq Î¹] (hV_sum : direct_sum.is_internal (Î» i, A i)) {Î± : Î¹ â†’ Type*}
  [Î  i, fintype (Î± i)] (v_family : Î  i, orthonormal_basis (Î± i) ğ•œ (A i)) :
  orthonormal_basis (Î£ i, Î± i) ğ•œ E :=
(hV_sum.collected_basis (Î» i, (v_family i).to_basis)).to_orthonormal_basis $
by simpa using hV.orthonormal_sigma_orthonormal
  (show (âˆ€ i, orthonormal ğ•œ (v_family i).to_basis), by simp)

lemma direct_sum.is_internal.collected_orthonormal_basis_mem [decidable_eq Î¹]
  (h : direct_sum.is_internal A) {Î± : Î¹ â†’ Type*}
  [Î  i, fintype (Î± i)] (hV : @orthogonal_family ğ•œ _ _ _ _ (Î» i, A i) _ (Î» i, (A i).subtypeâ‚—áµ¢))
  (v : Î  i, orthonormal_basis (Î± i) ğ•œ (A i)) (a : Î£ i, Î± i) :
  h.collected_orthonormal_basis hV v a âˆˆ A a.1 :=
by simp [direct_sum.is_internal.collected_orthonormal_basis]

variables [finite_dimensional ğ•œ E]

/-- In a finite-dimensional `inner_product_space`, any orthonormal subset can be extended to an
orthonormal basis. -/
lemma _root_.orthonormal.exists_orthonormal_basis_extension (hv : orthonormal ğ•œ (coe : v â†’ E)) :
  âˆƒ {u : finset E} (b : orthonormal_basis u ğ•œ E), v âŠ† u âˆ§ â‡‘b = coe :=
begin
  obtain âŸ¨uâ‚€, huâ‚€s, huâ‚€, huâ‚€_maxâŸ© := exists_maximal_orthonormal hv,
  rw maximal_orthonormal_iff_orthogonal_complement_eq_bot huâ‚€ at huâ‚€_max,
  have huâ‚€_finite : uâ‚€.finite := huâ‚€.linear_independent.finite,
  let u : finset E := huâ‚€_finite.to_finset,
  let fu : â†¥u â‰ƒ â†¥uâ‚€ := equiv.cast (congr_arg coe_sort huâ‚€_finite.coe_to_finset),
  have hfu : (coe : u â†’ E) = (coe : uâ‚€ â†’ E) âˆ˜ fu := by { ext, simp },
  have hu : orthonormal ğ•œ (coe : u â†’ E) := by simpa [hfu] using huâ‚€.comp _ fu.injective,
  refine âŸ¨u, orthonormal_basis.mk_of_orthogonal_eq_bot hu _, _, _âŸ©,
  { simpa using huâ‚€_max },
  { simpa using huâ‚€s },
  { simp },
end

variables (ğ•œ E)

/-- A finite-dimensional inner product space admits an orthonormal basis. -/
lemma _root_.exists_orthonormal_basis :
  âˆƒ (w : finset E) (b : orthonormal_basis w ğ•œ E), â‡‘b = (coe : w â†’ E) :=
let âŸ¨w, hw, hw', hw''âŸ© := (orthonormal_empty ğ•œ E).exists_orthonormal_basis_extension in
âŸ¨w, hw, hw''âŸ©

/-- Index for an arbitrary orthonormal basis on a finite-dimensional `inner_product_space`. -/
def orthonormal_basis_index : finset E :=
classical.some (exists_orthonormal_basis ğ•œ E)

/-- A finite-dimensional `inner_product_space` has an orthonormal basis. -/
def std_orthonormal_basis : orthonormal_basis (orthonormal_basis_index ğ•œ E) ğ•œ E :=
classical.some (classical.some_spec (exists_orthonormal_basis ğ•œ E))

@[simp] lemma coe_std_orthonormal_basis : â‡‘(std_orthonormal_basis ğ•œ E) = coe :=
classical.some_spec (classical.some_spec (exists_orthonormal_basis ğ•œ E))

variables {ğ•œ E}

/-- An `n`-dimensional `inner_product_space` has an orthonormal basis indexed by `fin n`. -/
def fin_std_orthonormal_basis {n : â„•} (hn : finrank ğ•œ E = n) : orthonormal_basis (fin n) ğ•œ E :=
have h : fintype.card (orthonormal_basis_index ğ•œ E) = n,
by rw [â† finrank_eq_card_basis (std_orthonormal_basis ğ•œ E).to_basis, hn],
(std_orthonormal_basis ğ•œ E).reindex (fintype.equiv_fin_of_card_eq h)

section subordinate_orthonormal_basis
open direct_sum
variables {n : â„•} (hn : finrank ğ•œ E = n) [decidable_eq Î¹]
  {V : Î¹ â†’ submodule ğ•œ E} (hV : is_internal V)

/-- Exhibit a bijection between `fin n` and the index set of a certain basis of an `n`-dimensional
inner product space `E`.  This should not be accessed directly, but only via the subsequent API. -/
@[irreducible] def direct_sum.is_internal.sigma_orthonormal_basis_index_equiv
  (hV' : @orthogonal_family ğ•œ _ _ _ _ (Î» i, V i) _ (Î» i, (V i).subtypeâ‚—áµ¢)) :
  (Î£ i, orthonormal_basis_index ğ•œ (V i)) â‰ƒ fin n :=
let b := hV.collected_orthonormal_basis hV' (Î» i, (std_orthonormal_basis ğ•œ (V i))) in
fintype.equiv_fin_of_card_eq $ (finite_dimensional.finrank_eq_card_basis b.to_basis).symm.trans hn

/-- An `n`-dimensional `inner_product_space` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `fin n` and subordinate to that direct sum. -/
@[irreducible] def direct_sum.is_internal.subordinate_orthonormal_basis
  (hV' : @orthogonal_family ğ•œ _ _ _ _ (Î» i, V i) _ (Î» i, (V i).subtypeâ‚—áµ¢)) :
  orthonormal_basis (fin n) ğ•œ E :=
((hV.collected_orthonormal_basis hV' (Î» i, (std_orthonormal_basis ğ•œ (V i)))).reindex
  (hV.sigma_orthonormal_basis_index_equiv hn hV'))

/-- An `n`-dimensional `inner_product_space` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `fin n` and subordinate to that direct sum. This function
provides the mapping by which it is subordinate. -/
def direct_sum.is_internal.subordinate_orthonormal_basis_index
  (a : fin n) (hV' : @orthogonal_family ğ•œ _ _ _ _ (Î» i, V i) _ (Î» i, (V i).subtypeâ‚—áµ¢)) : Î¹ :=
((hV.sigma_orthonormal_basis_index_equiv hn hV').symm a).1

/-- The basis constructed in `orthogonal_family.subordinate_orthonormal_basis` is subordinate to
the `orthogonal_family` in question. -/
lemma direct_sum.is_internal.subordinate_orthonormal_basis_subordinate
  (a : fin n) (hV' : @orthogonal_family ğ•œ _ _ _ _ (Î» i, V i) _ (Î» i, (V i).subtypeâ‚—áµ¢)) :
  (hV.subordinate_orthonormal_basis hn hV' a) âˆˆ
  V (hV.subordinate_orthonormal_basis_index hn a hV') :=
by simpa only [direct_sum.is_internal.subordinate_orthonormal_basis,
  orthonormal_basis.coe_reindex]
  using hV.collected_orthonormal_basis_mem hV' (Î» i, (std_orthonormal_basis ğ•œ (V i)))
    ((hV.sigma_orthonormal_basis_index_equiv hn hV').symm a)

attribute [irreducible] direct_sum.is_internal.subordinate_orthonormal_basis_index

end subordinate_orthonormal_basis

end finite_dimensional

local attribute [instance] fact_finite_dimensional_of_finrank_eq_succ

/-- Given a natural number `n` one less than the `finrank` of a finite-dimensional inner product
space, there exists an isometry from the orthogonal complement of a nonzero singleton to
`euclidean_space ğ•œ (fin n)`. -/
def orthonormal_basis.from_orthogonal_span_singleton
  (n : â„•) [fact (finrank ğ•œ E = n + 1)] {v : E} (hv : v â‰  0) :
  orthonormal_basis (fin n) ğ•œ (ğ•œ âˆ™ v)á—® :=
(fin_std_orthonormal_basis (finrank_orthogonal_span_singleton hv))

section linear_isometry

variables {V : Type*} [inner_product_space ğ•œ V] [finite_dimensional ğ•œ V]

variables {S : submodule ğ•œ V} {L : S â†’â‚—áµ¢[ğ•œ] V}

open finite_dimensional

/-- Let `S` be a subspace of a finite-dimensional complex inner product space `V`.  A linear
isometry mapping `S` into `V` can be extended to a full isometry of `V`.

TODO:  The case when `S` is a finite-dimensional subspace of an infinite-dimensional `V`.-/
noncomputable def linear_isometry.extend (L : S â†’â‚—áµ¢[ğ•œ] V): V â†’â‚—áµ¢[ğ•œ] V :=
begin
  -- Build an isometry from Sá—® to L(S)á—® through euclidean_space
  let d := finrank ğ•œ Sá—®,
  have dim_S_perp : finrank ğ•œ Sá—® = d := rfl,
  let LS := L.to_linear_map.range,
  have E : Sá—® â‰ƒâ‚—áµ¢[ğ•œ] LSá—®,
  { have dim_LS_perp : finrank ğ•œ LSá—® = d,
    calc  finrank ğ•œ LSá—® = finrank ğ•œ V - finrank ğ•œ LS : by simp only
        [â† LS.finrank_add_finrank_orthogonal, add_tsub_cancel_left]
      ...               = finrank ğ•œ V - finrank ğ•œ S : by simp only
        [linear_map.finrank_range_of_inj L.injective]
      ...               = finrank ğ•œ Sá—® : by simp only
        [â† S.finrank_add_finrank_orthogonal, add_tsub_cancel_left]
      ...               = d : dim_S_perp,
    let BS := (fin_std_orthonormal_basis dim_S_perp),
    let BLS := (fin_std_orthonormal_basis dim_LS_perp),
    exact BS.repr.trans BLS.repr.symm },
  let L3 := (LS)á—®.subtypeâ‚—áµ¢.comp E.to_linear_isometry,
  -- Project onto S and Sá—®
  haveI : complete_space S := finite_dimensional.complete ğ•œ S,
  haveI : complete_space V := finite_dimensional.complete ğ•œ V,
  let p1 := (orthogonal_projection S).to_linear_map,
  let p2 := (orthogonal_projection Sá—®).to_linear_map,
  -- Build a linear map from the isometries on S and Sá—®
  let M := L.to_linear_map.comp p1 + L3.to_linear_map.comp p2,
  -- Prove that M is an isometry
  have M_norm_map : âˆ€ (x : V), âˆ¥M xâˆ¥ = âˆ¥xâˆ¥,
  { intro x,
    -- Apply M to the orthogonal decomposition of x
    have Mx_decomp : M x = L (p1 x) + L3 (p2 x),
    { simp only [linear_map.add_apply, linear_map.comp_apply, linear_map.comp_apply,
      linear_isometry.coe_to_linear_map]},
    -- Mx_decomp is the orthogonal decomposition of M x
    have Mx_orth : âŸª L (p1 x), L3 (p2 x) âŸ« = 0,
    { have Lp1x : L (p1 x) âˆˆ L.to_linear_map.range := L.to_linear_map.mem_range_self (p1 x),
      have Lp2x : L3 (p2 x) âˆˆ (L.to_linear_map.range)á—®,
      { simp only [L3, linear_isometry.coe_comp, function.comp_app, submodule.coe_subtypeâ‚—áµ¢,
          â† submodule.range_subtype (LSá—®)],
        apply linear_map.mem_range_self},
      apply submodule.inner_right_of_mem_orthogonal Lp1x Lp2x},
    -- Apply the Pythagorean theorem and simplify
    rw [â† sq_eq_sq (norm_nonneg _) (norm_nonneg _), norm_sq_eq_add_norm_sq_projection x S],
    simp only [sq, Mx_decomp],
    rw norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (L (p1 x)) (L3 (p2 x)) Mx_orth,
    simp only [linear_isometry.norm_map, p1, p2, continuous_linear_map.to_linear_map_eq_coe,
      add_left_inj, mul_eq_mul_left_iff, norm_eq_zero, true_or, eq_self_iff_true,
      continuous_linear_map.coe_coe, submodule.coe_norm, submodule.coe_eq_zero] },
  exact { to_linear_map := M, norm_map' := M_norm_map },
end

lemma linear_isometry.extend_apply (L : S â†’â‚—áµ¢[ğ•œ] V) (s : S):
  L.extend s = L s :=
begin
  haveI : complete_space S := finite_dimensional.complete ğ•œ S,
  simp only [linear_isometry.extend, continuous_linear_map.to_linear_map_eq_coe,
    â†linear_isometry.coe_to_linear_map],
  simp only [add_right_eq_self, linear_isometry.coe_to_linear_map,
    linear_isometry_equiv.coe_to_linear_isometry, linear_isometry.coe_comp, function.comp_app,
    orthogonal_projection_mem_subspace_eq_self, linear_map.coe_comp, continuous_linear_map.coe_coe,
    submodule.coe_subtype, linear_map.add_apply, submodule.coe_eq_zero,
    linear_isometry_equiv.map_eq_zero_iff, submodule.coe_subtypeâ‚—áµ¢,
    orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero,
    submodule.orthogonal_orthogonal, submodule.coe_mem],
end

end linear_isometry

section matrix

open_locale matrix

variables {n m : â„•}

local notation `âŸª`x`, `y`âŸ«â‚˜` := @inner ğ•œ (euclidean_space ğ•œ (fin m)) _ x y
local notation `âŸª`x`, `y`âŸ«â‚™` := @inner ğ•œ (euclidean_space ğ•œ (fin n)) _ x y

/-- The inner product of a row of A and a row of B is an entry of B â¬ Aá´´. -/
lemma inner_matrix_row_row (A B : matrix (fin n) (fin m) ğ•œ) (i j : (fin n)) :
  âŸªA i, B jâŸ«â‚˜ = (B â¬ Aá´´) j i := by {simp only [inner, matrix.mul_apply, star_ring_end_apply,
    matrix.conj_transpose_apply,mul_comm]}

/-- The inner product of a column of A and a column of B is an entry of Aá´´ â¬ B -/
lemma inner_matrix_col_col (A B : matrix (fin n) (fin m) ğ•œ) (i j : (fin m)) :
  âŸªAáµ€ i, Báµ€ jâŸ«â‚™ = (Aá´´ â¬ B) i j := rfl

end matrix
