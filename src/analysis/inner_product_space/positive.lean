/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.inner_product_space.adjoint
import analysis.inner_product_space.spectrum

/-!
# Positive operators

In this file we define positive operators in a Hilbert space. We follow Bourbaki's choice
of requiring self adjointness in the definition.

## Main definitions

for linear maps:
* `is_positive` : a linear map is positive if it is symmetric and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`

for continuous linear maps:
* `is_positive` : a continuous linear map is positive if it is self adjoint and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`

## Main statements

for linear maps:
* `linear_map.is_positive.conj_adjoint` : if `T : E â†’â‚—[ğ•œ] E` and `E` is a finite-dimensional space,
  then for any `S : E â†’â‚—[ğ•œ] F`, we have `S.comp (T.comp S.adjoint)` is also positive.

for continuous linear maps:
* `continuous_linear_map.is_positive.conj_adjoint` : if `T : E â†’L[ğ•œ] E` is positive,
  then for any `S : E â†’L[ğ•œ] F`, `S âˆ˜L T âˆ˜L Sâ€ ` is also positive.
* `continuous_linear_map.is_positive_iff_complex` : in a ***complex*** hilbert space,
  checking that `âŸªT x, xâŸ«` is a nonnegative real number for all `x` suffices to prove that
  `T` is positive

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

Positive operator
-/
open inner_product_space is_R_or_C
open_locale inner_product complex_conjugate

variables {ğ•œ E F : Type*} [is_R_or_C ğ•œ]
  [normed_add_comm_group E] [normed_add_comm_group F]
  [inner_product_space ğ•œ E] [inner_product_space ğ•œ F]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

namespace linear_map

/-- `T` is (semi-definite) **positive** if `T` is symmetric
and `âˆ€ x : V, 0 â‰¤ re âŸªx, T xâŸ«` -/
def is_positive (T : E â†’â‚—[ğ•œ] E) : Prop :=
T.is_symmetric âˆ§ âˆ€ x : E, 0 â‰¤ re âŸªx, T xâŸ«

lemma is_positive_zero : (0 : E â†’â‚—[ğ•œ] E).is_positive :=
begin
  refine âŸ¨is_symmetric_zero, Î» x, _âŸ©,
  simp_rw [zero_apply, inner_re_zero_right],
end

lemma is_positive_one : (1 : E â†’â‚—[ğ•œ] E).is_positive :=
âŸ¨is_symmetric_id, Î» x, inner_self_nonnegâŸ©

lemma is_positive.add {S T : E â†’â‚—[ğ•œ] E} (hS : S.is_positive) (hT : T.is_positive) :
  (S + T).is_positive :=
begin
  refine âŸ¨is_symmetric.add hS.1 hT.1, Î» x, _âŸ©,
  rw [add_apply, inner_add_right, map_add],
  exact add_nonneg (hS.2 _) (hT.2 _),
end

lemma is_positive.inner_nonneg_left {T : E â†’â‚—[ğ•œ] E} (hT : is_positive T) (x : E) :
  0 â‰¤ re âŸªT x, xâŸ« :=
by { rw inner_re_symm, exact hT.2 x, }

lemma is_positive.inner_nonneg_right {T : E â†’â‚—[ğ•œ] E} (hT : is_positive T) (x : E) :
  0 â‰¤ re âŸªx, T xâŸ« :=
hT.2 x

/-- a linear projection onto `U` along its complement `V` is positive if
and only if `U` and `V` are orthogonal -/
lemma linear_proj_is_positive_iff {U V : submodule ğ•œ E} (hUV : is_compl U V) :
  (U.subtype.comp (U.linear_proj_of_is_compl V hUV)).is_positive â†” U âŸ‚ V :=
begin
  split,
  { intros h u hu v hv,
    let a : U := âŸ¨u, huâŸ©,
    let b : V := âŸ¨v, hvâŸ©,
    have hau : u = a := rfl,
    have hbv : v = b := rfl,
    rw [hau, â† submodule.linear_proj_of_is_compl_apply_left hUV a,
        â† submodule.subtype_apply _, â† comp_apply, â† h.1 _ _,
        comp_apply, hbv, submodule.linear_proj_of_is_compl_apply_right hUV b,
        map_zero, inner_zero_left], },
  { intro h,
    have : (U.subtype.comp (U.linear_proj_of_is_compl V hUV)).is_symmetric,
    { intros x y,
      nth_rewrite 0 â† submodule.linear_proj_add_linear_proj_of_is_compl_eq_self hUV y,
      nth_rewrite 1 â† submodule.linear_proj_add_linear_proj_of_is_compl_eq_self hUV x,
      simp_rw [inner_add_right, inner_add_left, comp_apply, submodule.subtype_apply _,
        â† submodule.coe_inner, submodule.is_ortho_iff_inner_eq.mp h _
          (submodule.coe_mem _) _ (submodule.coe_mem _),
        submodule.is_ortho_iff_inner_eq.mp h.symm _
          (submodule.coe_mem _) _ (submodule.coe_mem _)], },
    refine âŸ¨this, _âŸ©,
    intros x,
    rw [comp_apply, submodule.subtype_apply, â† submodule.linear_proj_of_is_compl_idempotent,
        â† submodule.subtype_apply, â† comp_apply, â† this _ ((U.linear_proj_of_is_compl V hUV) x)],
    exact inner_self_nonneg, },
end

/-- set over `ğ•œ` is **non-negative** if all its elements are real and non-negative -/
def set.is_nonneg (A : set ğ•œ) : Prop :=
âˆ€ x : ğ•œ, x âˆˆ A â†’ â†‘(re x) = x âˆ§ 0 â‰¤ re x

section finite_dimensional

local notation `e` := is_symmetric.eigenvector_basis
local notation `Î±` := is_symmetric.eigenvalues
local notation `âˆš` := real.sqrt

variables {n : â„•} [finite_dimensional ğ•œ E] (T : E â†’â‚—[ğ•œ] E)

/-- the spectrum of a positive linear map is non-negative -/
lemma is_positive.nonneg_spectrum (h : T.is_positive) :
  (spectrum ğ•œ T).is_nonneg :=
begin
  cases h with hT h,
  intros Î¼ hÎ¼,
  simp_rw [â† module.End.has_eigenvalue_iff_mem_spectrum] at hÎ¼,
  have : â†‘(re Î¼) = Î¼,
  { simp_rw [â† eq_conj_iff_re],
    exact is_symmetric.conj_eigenvalue_eq_self hT hÎ¼, },
  rw â† this at hÎ¼,
  exact âŸ¨this, eigenvalue_nonneg_of_nonneg hÎ¼ hâŸ©,
end

open_locale big_operators
/-- given a symmetric linear map with a non-negative spectrum,
we can write `T x = âˆ‘ i, âˆšÎ± i â€¢ âˆšÎ± i â€¢ âŸªe i, xâŸ«` for any `x âˆˆ E`,
where `Î± i` are the eigenvalues of `T` and `e i` are the respective eigenvectors
that form an eigenbasis (`is_symmetric.eigenvector_basis`) -/
lemma sq_mul_sq_eq_self_of_is_symmetric_and_nonneg_spectrum
  [decidable_eq ğ•œ] (hn : finite_dimensional.finrank ğ•œ E = n) (hT : T.is_symmetric)
  (hT1 : (spectrum ğ•œ T).is_nonneg) (v : E) :
  T v = âˆ‘ i, ((âˆš (Î± hT hn i) â€¢ âˆš (Î± hT hn i)) : ğ•œ) â€¢ âŸªe hT hn i, vâŸ« â€¢ e hT hn i :=
begin
  have : âˆ€ i : fin n, 0 â‰¤ Î± hT hn i := Î» i,
  by { specialize hT1 (hT.eigenvalues hn i),
       simp only [of_real_re, eq_self_iff_true, true_and] at hT1,
       apply hT1 (module.End.mem_spectrum_of_has_eigenvalue
         (is_symmetric.has_eigenvalue_eigenvalues hT hn i)), },
  calc T v = âˆ‘ i, âŸªe hT hn i, vâŸ« â€¢ T (e hT hn i) : _
       ... = âˆ‘ i, ((âˆš (Î± hT hn i) â€¢ âˆš (Î± hT hn i)) : ğ•œ) â€¢ âŸªe hT hn i, vâŸ« â€¢ (e hT hn i) : _,
  simp_rw [â† orthonormal_basis.repr_apply_apply, â† map_smul_of_tower, â† linear_map.map_sum,
    orthonormal_basis.sum_repr (e hT hn) v, is_symmetric.apply_eigenvector_basis,
    smul_smul, of_real_smul, â† of_real_mul, â† real.sqrt_mul (this _),
    real.sqrt_mul_self (this _), mul_comm],
end

/-- given a symmetric linear map `T` and a real number `r`,
we can define a linear map `S` such that `S = T ^ r` -/
noncomputable def rpow [decidable_eq ğ•œ] (hn : finite_dimensional.finrank ğ•œ E = n)
  (hT : T.is_symmetric) (r : â„) : E â†’â‚—[ğ•œ] E :=
{ to_fun := Î» v, âˆ‘ i : fin n, ((((Î± hT hn i : â„) ^ r : â„)) : ğ•œ) â€¢ âŸªe hT hn i, vâŸ« â€¢ e hT hn i,
  map_add' := Î» x y, by simp_rw [inner_add_right, add_smul, smul_add, finset.sum_add_distrib],
  map_smul' := Î» r x, by simp_rw [inner_smul_right, â† smul_smul, finset.smul_sum,
                           ring_hom.id_apply, smul_smul, â† mul_assoc, mul_comm] }

lemma rpow_apply [decidable_eq ğ•œ] (hn : finite_dimensional.finrank ğ•œ E = n)
  (hT : T.is_symmetric) (r : â„) (v : E) :
  T.rpow hn hT r v = âˆ‘ i : fin n, (((Î± hT hn i : â„) ^ r : â„) : ğ•œ) â€¢ âŸªe hT hn i, vâŸ« â€¢ e hT hn i :=
rfl

/-- the square root of a symmetric linear map can then directly be defined with `re_pow` -/
noncomputable def sqrt [decidable_eq ğ•œ] (hn : finite_dimensional.finrank ğ•œ E = n)
  (h : T.is_symmetric) :
  E â†’â‚—[ğ•œ] E := T.rpow hn h (1 / 2 : â„)

/-- the square root of a symmetric linear map `T`
is written as `T x = âˆ‘ i, âˆš (Î± i) â€¢ âŸªe i, xâŸ« â€¢ e i` for any `x âˆˆ E`,
where `Î± i` are the eigenvalues of `T` and `e i` are the respective eigenvectors
that form an eigenbasis (`is_symmetric.eigenvector_basis`) -/
lemma sqrt_apply (hn : finite_dimensional.finrank ğ•œ E = n) [decidable_eq ğ•œ]
  (hT : T.is_symmetric) (x : E) :
  T.sqrt hn hT x = âˆ‘ i, (âˆš (Î± hT hn i) : ğ•œ) â€¢ âŸªe hT hn i, xâŸ« â€¢ e hT hn i :=
by { simp_rw [real.sqrt_eq_rpow _], refl }

/-- given a symmetric linear map `T` with a non-negative spectrum,
the square root of `T` composed with itself equals itself, i.e., `T.sqrt ^ 2 = T`  -/
lemma sqrt_sq_eq_self_of_is_symmetric_and_nonneg_spectrum
  [decidable_eq ğ•œ] (hn : finite_dimensional.finrank ğ•œ E = n) (hT : T.is_symmetric)
  (hT1 : (spectrum ğ•œ T).is_nonneg) :
  (T.sqrt hn hT) ^ 2 = T :=
by simp_rw [pow_two, mul_eq_comp, linear_map.ext_iff, comp_apply, sqrt_apply,
     inner_sum, inner_smul_real_right, smul_smul, inner_smul_right,
     â† orthonormal_basis.repr_apply_apply, orthonormal_basis.repr_self,
     euclidean_space.single_apply, mul_boole, smul_ite, smul_zero,
     finset.sum_ite_eq, finset.mem_univ, if_true, algebra.mul_smul_comm,
     sq_mul_sq_eq_self_of_is_symmetric_and_nonneg_spectrum T hn hT hT1,
     orthonormal_basis.repr_apply_apply, â† smul_eq_mul, â† smul_assoc,
     eq_self_iff_true, forall_const]

/-- given a symmetric linear map `T`, we have that its root is positive -/
lemma is_symmetric.sqrt_is_positive
  [decidable_eq ğ•œ] (hn : finite_dimensional.finrank ğ•œ E = n) (hT : T.is_symmetric) :
  (T.sqrt hn hT).is_positive :=
begin
  have : (T.sqrt hn hT).is_symmetric,
  { intros x y,
    simp_rw [sqrt_apply T hn hT, inner_sum, sum_inner, smul_smul, inner_smul_right,
      inner_smul_left],
    have : âˆ€ i : fin n, conj ((âˆš (Î± hT hn i)) : ğ•œ) = ((âˆš (Î± hT hn i)) : ğ•œ) := Î» i,
    by simp_rw [eq_conj_iff_re, of_real_re],
    simp_rw  [mul_assoc, map_mul, this, inner_conj_symm,
      mul_comm âŸªe hT hn _, yâŸ«, â† mul_assoc], },
  refine âŸ¨this, _âŸ©,
  intro x,
  simp_rw [sqrt_apply _ hn hT, inner_sum, add_monoid_hom.map_sum, inner_smul_right],
  apply finset.sum_nonneg',
  intros i,
  simp_rw [â† inner_conj_symm x, â† orthonormal_basis.repr_apply_apply,
    mul_conj, â† of_real_mul, of_real_re],
  exact mul_nonneg (real.sqrt_nonneg _) (norm_sq_nonneg _),
end

/-- `T` is positive if and only if `T` is symmetric
(which is automatic from the definition of positivity)
and has a non-negative spectrum -/
lemma is_positive_iff_is_symmetric_and_nonneg_spectrum
  (hn : finite_dimensional.finrank ğ•œ E = n) :
  T.is_positive â†” T.is_symmetric âˆ§ (spectrum ğ•œ T).is_nonneg :=
begin
  classical,
  refine âŸ¨Î» h, âŸ¨h.1, Î» Î¼ hÎ¼, is_positive.nonneg_spectrum T h Î¼ hÎ¼âŸ©,
          Î» h, âŸ¨h.1, _âŸ©âŸ©,
  intros x,
  rw [â† sqrt_sq_eq_self_of_is_symmetric_and_nonneg_spectrum T hn h.1 h.2,
    pow_two, mul_apply, â† adjoint_inner_left, is_self_adjoint_iff'.mp
      ((is_symmetric_iff_is_self_adjoint _).mp (is_symmetric.sqrt_is_positive T hn h.1).1)],
  exact inner_self_nonneg,
end

/-- `T` is positive if and only if there exists a
linear map `S` such that `T = S.adjoint * S` -/
lemma is_positive_iff_exists_adjoint_mul_self
  (hn : finite_dimensional.finrank ğ•œ E = n) :
  T.is_positive â†” âˆƒ S : E â†’â‚—[ğ•œ] E, T = S.adjoint * S :=
begin
  classical,
   split,
  { rw [is_positive_iff_is_symmetric_and_nonneg_spectrum T hn],
    rintro âŸ¨hT, hT1âŸ©,
    use T.sqrt hn hT,
    rw [is_self_adjoint_iff'.mp
          ((is_symmetric_iff_is_self_adjoint _).mp (is_symmetric.sqrt_is_positive T hn hT).1),
      â† pow_two],
    exact (sqrt_sq_eq_self_of_is_symmetric_and_nonneg_spectrum T hn hT hT1).symm, },
  { intros h,
    rcases h with âŸ¨S, rflâŸ©,
    refine âŸ¨is_symmetric_adjoint_mul_self S, _âŸ©,
    intro x,
    simp_rw [mul_apply, adjoint_inner_right],
    exact inner_self_nonneg, },
end

section complex

/-- for spaces `V` over `â„‚`, it suffices to define positivity with
`0 â‰¤ âŸªv, T vâŸ«_â„‚` for all `v âˆˆ V` -/
lemma complex_is_positive {V : Type*} [normed_add_comm_group V]
  [inner_product_space â„‚ V] (T : V â†’â‚—[â„‚] V) :
  T.is_positive â†” âˆ€ v : V, â†‘(re âŸªv, T vâŸ«_â„‚) = âŸªv, T vâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªv, T vâŸ«_â„‚ :=
by simp_rw [is_positive, is_symmetric_iff_inner_map_self_real, inner_conj_symm,
     â† eq_conj_iff_re, inner_conj_symm, â† forall_and_distrib, and_comm, eq_comm]

end complex

lemma is_positive.conj_adjoint [finite_dimensional ğ•œ F]
  (T : E â†’â‚—[ğ•œ] E) (S : E â†’â‚—[ğ•œ] F) (h : T.is_positive) :
  (S.comp (T.comp S.adjoint)).is_positive :=
begin
  split,
  intros u v,
  simp_rw [comp_apply, â† adjoint_inner_left _ (T _), â† adjoint_inner_right _ (T _)],
  exact h.1 _ _,
  intros v,
  simp_rw [comp_apply, â† adjoint_inner_left _ (T _)],
  exact h.2 _,
end

lemma is_positive.adjoint_conj [finite_dimensional ğ•œ F]
  (T : E â†’â‚—[ğ•œ] E) (S : F â†’â‚—[ğ•œ] E) (h : T.is_positive) :
  (S.adjoint.comp (T.comp S)).is_positive :=
begin
  split,
  intros u v,
  simp_rw [comp_apply, adjoint_inner_left, adjoint_inner_right],
  exact h.1 _ _,
  intros v,
  simp_rw [comp_apply, adjoint_inner_right],
  exact h.2 _,
end

variable (hn : finite_dimensional.finrank ğ•œ E = n)
local notation `âˆšTâ‹†`T := (T.adjoint.comp T).sqrt hn (is_symmetric_adjoint_mul_self T)

/-- we have `(T.adjoint.comp T).sqrt` is positive, given any linear map `T` -/
lemma sqrt_adjoint_self_is_positive [decidable_eq ğ•œ] (T : E â†’â‚—[ğ•œ] E) : (âˆšTâ‹†T).is_positive :=
is_symmetric.sqrt_is_positive _ hn (is_symmetric_adjoint_mul_self T)

/-- given any linear map `T` and `x âˆˆ E` we have
`â€–(T.adjoint.comp T).sqrt xâ€– = â€–T xâ€–` -/
lemma norm_of_sqrt_adjoint_mul_self_eq [decidable_eq ğ•œ] (T : E â†’â‚—[ğ•œ] E) (x : E) :
  â€–(âˆšTâ‹†T) xâ€– = â€–T xâ€– :=
begin
  simp_rw [â† sq_eq_sq (norm_nonneg _) (norm_nonneg _), â† @inner_self_eq_norm_sq ğ•œ,
    â† adjoint_inner_left, is_self_adjoint_iff'.mp
      ((is_symmetric_iff_is_self_adjoint _).mp (sqrt_adjoint_self_is_positive hn T).1),
    â† mul_eq_comp, â† mul_apply, â† pow_two, mul_eq_comp],
 congr,
 apply sqrt_sq_eq_self_of_is_symmetric_and_nonneg_spectrum,
 apply is_positive.nonneg_spectrum _ âŸ¨is_symmetric_adjoint_mul_self T, _âŸ©,
 intros x,
 simp_rw [mul_apply, adjoint_inner_right],
 exact inner_self_nonneg,
end

end finite_dimensional

end linear_map


namespace continuous_linear_map

open continuous_linear_map

variables [complete_space E] [complete_space F]

/-- A continuous linear endomorphism `T` of a Hilbert space is **positive** if it is self adjoint
  and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
def is_positive (T : E â†’L[ğ•œ] E) : Prop :=
  is_self_adjoint T âˆ§ âˆ€ x, 0 â‰¤ T.re_apply_inner_self x

lemma is_positive.to_linear_map (T : E â†’L[ğ•œ] E) :
  T.to_linear_map.is_positive â†” T.is_positive :=
by simp_rw [to_linear_map_eq_coe, linear_map.is_positive, continuous_linear_map.coe_coe,
     is_positive, is_self_adjoint_iff_is_symmetric, re_apply_inner_self_apply T, inner_re_symm]

lemma is_positive.is_self_adjoint {T : E â†’L[ğ•œ] E} (hT : is_positive T) :
  is_self_adjoint T :=
hT.1

lemma is_positive.inner_nonneg_left {T : E â†’L[ğ•œ] E} (hT : is_positive T) (x : E) :
  0 â‰¤ re âŸªT x, xâŸ« :=
hT.2 x

lemma is_positive.inner_nonneg_right {T : E â†’L[ğ•œ] E} (hT : is_positive T) (x : E) :
  0 â‰¤ re âŸªx, T xâŸ« :=
by rw inner_re_symm; exact hT.inner_nonneg_left x

lemma is_positive_zero : is_positive (0 : E â†’L[ğ•œ] E) :=
begin
  refine âŸ¨is_self_adjoint_zero _, Î» x, _âŸ©,
  change 0 â‰¤ re âŸª_, _âŸ«,
  rw [zero_apply, inner_zero_left, zero_hom_class.map_zero]
end

lemma is_positive_one : is_positive (1 : E â†’L[ğ•œ] E) :=
âŸ¨is_self_adjoint_one _, Î» x, inner_self_nonnegâŸ©

lemma is_positive.add {T S : E â†’L[ğ•œ] E} (hT : T.is_positive)
  (hS : S.is_positive) : (T + S).is_positive :=
begin
  refine âŸ¨hT.is_self_adjoint.add hS.is_self_adjoint, Î» x, _âŸ©,
  rw [re_apply_inner_self, add_apply, inner_add_left, map_add],
  exact add_nonneg (hT.inner_nonneg_left x) (hS.inner_nonneg_left x)
end

lemma is_positive.conj_adjoint {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) (S : E â†’L[ğ•œ] F) : (S âˆ˜L T âˆ˜L Sâ€ ).is_positive :=
begin
  refine âŸ¨hT.is_self_adjoint.conj_adjoint S, Î» x, _âŸ©,
  rw [re_apply_inner_self, comp_apply, â† adjoint_inner_right],
  exact hT.inner_nonneg_left _
end

lemma is_positive.adjoint_conj {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) (S : F â†’L[ğ•œ] E) : (Sâ€  âˆ˜L T âˆ˜L S).is_positive :=
begin
  convert hT.conj_adjoint (Sâ€ ),
  rw adjoint_adjoint
end

lemma is_positive.conj_orthogonal_projection (U : submodule ğ•œ E) {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) [complete_space U] :
  (U.subtypeL âˆ˜L orthogonal_projection U âˆ˜L T âˆ˜L U.subtypeL âˆ˜L
    orthogonal_projection U).is_positive :=
begin
  have := hT.conj_adjoint (U.subtypeL âˆ˜L orthogonal_projection U),
  rwa (orthogonal_projection_is_self_adjoint U).adjoint_eq at this
end

lemma is_positive.orthogonal_projection_comp {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) (U : submodule ğ•œ E) [complete_space U] :
  (orthogonal_projection U âˆ˜L T âˆ˜L U.subtypeL).is_positive :=
begin
  have := hT.conj_adjoint (orthogonal_projection U : E â†’L[ğ•œ] U),
  rwa [U.adjoint_orthogonal_projection] at this,
end

section complex

variables {E' : Type*} [normed_add_comm_group E'] [inner_product_space â„‚ E'] [complete_space E']

lemma is_positive_iff_complex (T : E' â†’L[â„‚] E') :
  is_positive T â†” âˆ€ x, (re âŸªT x, xâŸ«_â„‚ : â„‚) = âŸªT x, xâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªT x, xâŸ«_â„‚ :=
begin
  simp_rw [is_positive, forall_and_distrib, is_self_adjoint_iff_is_symmetric,
    linear_map.is_symmetric_iff_inner_map_self_real, eq_conj_iff_re],
  refl
end

end complex

end continuous_linear_map
