/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.inner_product_space.adjoint
import analysis.inner_product_space.spectrum

/-!
# Positive operators

In this file we define positive operators in a Hilbert space. We follow Bourbaki's choice
of requiring self adjointness in the definition.

## Main definitions

* `is_positive` : a continuous linear map is positive if it is self adjoint and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`

## Main statements

* `continuous_linear_map.is_positive.conj_adjoint` : if `T : E â†’L[ğ•œ] E` is positive,
  then for any `S : E â†’L[ğ•œ] F`, `S âˆ˜L T âˆ˜L Sâ€ ` is also positive.
* `continuous_linear_map.is_positive_iff_complex` : in a ***complex*** hilbert space,
  checking that `âŸªT x, xâŸ«` is a nonnegative real number for all `x` suffices to prove that
  `T` is positive

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

Positive operator
-/

namespace continuous_linear_map

open inner_product_space is_R_or_C continuous_linear_map
open_locale inner_product complex_conjugate


variables {ğ•œ E F : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ E] [inner_product_space ğ•œ F]
  [complete_space E] [complete_space F]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

/-- A continuous linear endomorphism `T` of a Hilbert space is **positive** if it is self adjoint
  and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
def is_positive (T : E â†’L[ğ•œ] E) : Prop :=
  is_self_adjoint T âˆ§ âˆ€ x, 0 â‰¤ T.re_apply_inner_self x

lemma is_positive.is_self_adjoint {T : E â†’L[ğ•œ] E} (hT : is_positive T) :
  is_self_adjoint T :=
hT.1

lemma is_positive.inner_nonneg_left {T : E â†’L[ğ•œ] E} (hT : is_positive T) (x : E) :
  0 â‰¤ re âŸªT x, xâŸ« :=
hT.2 x

lemma is_positive.inner_nonneg_right {T : E â†’L[ğ•œ] E} (hT : is_positive T) (x : E) :
  0 â‰¤ re âŸªx, T xâŸ« :=
by rw inner_re_symm; exact hT.inner_nonneg_left x

lemma is_positive_zero : is_positive (0 : E â†’L[ğ•œ] E) :=
begin
  refine âŸ¨is_self_adjoint_zero _, Î» x, _âŸ©,
  change 0 â‰¤ re âŸª_, _âŸ«,
  rw [zero_apply, inner_zero_left, zero_hom_class.map_zero]
end

lemma is_positive_one : is_positive (1 : E â†’L[ğ•œ] E) :=
âŸ¨is_self_adjoint_one _, Î» x, inner_self_nonnegâŸ©

lemma is_positive.add {T S : E â†’L[ğ•œ] E} (hT : T.is_positive)
  (hS : S.is_positive) : (T + S).is_positive :=
begin
  refine âŸ¨hT.is_self_adjoint.add hS.is_self_adjoint, Î» x, _âŸ©,
  rw [re_apply_inner_self, add_apply, inner_add_left, map_add],
  exact add_nonneg (hT.inner_nonneg_left x) (hS.inner_nonneg_left x)
end

lemma is_positive.conj_adjoint {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) (S : E â†’L[ğ•œ] F) : (S âˆ˜L T âˆ˜L Sâ€ ).is_positive :=
begin
  refine âŸ¨hT.is_self_adjoint.conj_adjoint S, Î» x, _âŸ©,
  rw [re_apply_inner_self, comp_apply, â† adjoint_inner_right],
  exact hT.inner_nonneg_left _
end

lemma is_positive.adjoint_conj {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) (S : F â†’L[ğ•œ] E) : (Sâ€  âˆ˜L T âˆ˜L S).is_positive :=
begin
  convert hT.conj_adjoint (Sâ€ ),
  rw adjoint_adjoint
end

lemma is_positive.conj_orthogonal_projection (U : submodule ğ•œ E) {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) [complete_space U] :
  (U.subtypeL âˆ˜L orthogonal_projection U âˆ˜L T âˆ˜L U.subtypeL âˆ˜L
    orthogonal_projection U).is_positive :=
begin
  have := hT.conj_adjoint (U.subtypeL âˆ˜L orthogonal_projection U),
  rwa (orthogonal_projection_is_self_adjoint U).adjoint_eq at this
end

lemma is_positive.orthogonal_projection_comp {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive) (U : submodule ğ•œ E) [complete_space U] :
  (orthogonal_projection U âˆ˜L T âˆ˜L U.subtypeL).is_positive :=
begin
  have := hT.conj_adjoint (orthogonal_projection U : E â†’L[ğ•œ] U),
  rwa [U.adjoint_orthogonal_projection] at this,
end

section complex

variables {E' : Type*} [inner_product_space â„‚ E'] [complete_space E']

lemma is_positive_iff_complex (T : E' â†’L[â„‚] E') :
  is_positive T â†” âˆ€ x, (re âŸªT x, xâŸ«_â„‚ : â„‚) = âŸªT x, xâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªT x, xâŸ«_â„‚ :=
begin
  simp_rw [is_positive, forall_and_distrib, is_self_adjoint_iff_is_symmetric,
    linear_map.is_symmetric_iff_inner_map_self_real, eq_conj_iff_re],
  refl
end

end complex

end continuous_linear_map

namespace linear_map

open linear_map

variables {V : Type*} [inner_product_space â„‚ V]

local notation `e` := is_symmetric.eigenvector_basis
open_locale big_operators

/-- `T` is (semi-definite) positive if `âˆ€ x : V, âŸªx, T xâŸ«_â„‚ â‰¥ 0` -/
def is_positive (T : V â†’â‚—[â„‚] V) :
  Prop := âˆ€ x : V, 0 â‰¤ âŸªx, T xâŸ«_â„‚.re âˆ§ (âŸªx, T xâŸ«_â„‚.re : â„‚) = âŸªx, T xâŸ«_â„‚

lemma is_self_adjoint_iff_real_inner [finite_dimensional â„‚ V] (T : V â†’â‚—[â„‚] V) :
  is_self_adjoint T â†” âˆ€ x, (âŸªx, T xâŸ«_â„‚.re : â„‚) = âŸªx, T xâŸ«_â„‚ :=
begin
  simp_rw [â† is_symmetric_iff_is_self_adjoint, is_symmetric_iff_inner_map_self_real,
           inner_conj_sym, â† is_R_or_C.re_eq_complex_re, â† is_R_or_C.eq_conj_iff_re,
           inner_conj_sym],
  exact âŸ¨Î» h x, (h x).symm, Î» h x, (h x).symmâŸ©,
end

/-- if `T.is_positive`, then `T.is_self_adjoint` and all its eigenvalues are non-negative -/
lemma is_positive.self_adjoint_and_nonneg_spectrum [finite_dimensional â„‚ V]
  (T : V â†’â‚—[â„‚] V) (h : T.is_positive) :
  is_self_adjoint T âˆ§ âˆ€ Î¼ : â„‚, Î¼ âˆˆ spectrum â„‚ T â†’ Î¼ = Î¼.re âˆ§ 0 â‰¤ Î¼.re :=
begin
  have frs : is_self_adjoint T,
  { rw linear_map.is_self_adjoint_iff',
    symmetry,
    rw [â† sub_eq_zero, â† inner_map_self_eq_zero],
    intro x,
    cases h x,
    have := complex.re_add_im (inner x (T x)),
    rw [â† right, complex.of_real_re, complex.of_real_im] at this,
    rw [linear_map.sub_apply, inner_sub_left, â† inner_conj_sym, linear_map.adjoint_inner_left,
        â† right, is_R_or_C.conj_of_real, sub_self], },
   refine âŸ¨frs,_âŸ©,
   intros Î¼ hÎ¼,
   rw â† module.End.has_eigenvalue_iff_mem_spectrum at hÎ¼,
   have realeigen := (complex.eq_conj_iff_re.mp
     (linear_map.is_symmetric.conj_eigenvalue_eq_self
       ((linear_map.is_symmetric_iff_is_self_adjoint T).mpr frs) hÎ¼)).symm,
   refine âŸ¨realeigen, _âŸ©,
   have hÎ± : âˆƒ Î± : â„, Î± = Î¼.re := by use Î¼.re,
   cases hÎ± with Î± hÎ±,
   rw â† hÎ±,
   rw [realeigen, â† hÎ±] at hÎ¼,
   exact eigenvalue_nonneg_of_nonneg hÎ¼ (Î» x, ge_iff_le.mp (h x).1),
end

variables [finite_dimensional â„‚ V] {n : â„•} (hn : finite_dimensional.finrank â„‚ V = n)
variables (T : V â†’â‚—[â„‚] V)

lemma of_pos_eq_sqrt_sqrt (hT : T.is_symmetric)
  (hT1 : âˆ€ Î¼ : â„‚, Î¼ âˆˆ spectrum â„‚ T â†’ Î¼ = â†‘Î¼.re âˆ§ 0 â‰¤ Î¼.re) (v : V) :
  T v = âˆ‘ (i : (fin n)), real.sqrt (hT.eigenvalues hn i) â€¢ real.sqrt (hT.eigenvalues hn i)
   â€¢ âŸª(e hT hn) i, vâŸ«_â„‚ â€¢ (e hT hn) i :=
begin
  have : âˆ€ i : fin n, 0 â‰¤ hT.eigenvalues hn i := Î» i,
  by { specialize hT1 (hT.eigenvalues hn i),
      simp only [complex.of_real_re, eq_self_iff_true, true_and] at hT1,
      apply hT1 (module.End.mem_spectrum_of_has_eigenvalue
        (is_symmetric.has_eigenvalue_eigenvalues hT hn i)), },
  calc T v = âˆ‘ (i : (fin n)), âŸª(e hT hn) i, vâŸ«_â„‚ â€¢ T ((e hT hn) i) :
  by simp_rw [â† orthonormal_basis.repr_apply_apply, â† map_smul_of_tower, â† linear_map.map_sum,
                orthonormal_basis.sum_repr (is_symmetric.eigenvector_basis hT hn) v]
       ... = âˆ‘ (i : (fin n)),
        real.sqrt (hT.eigenvalues hn i) â€¢ real.sqrt (hT.eigenvalues hn i) â€¢
         âŸª(e hT hn) i, vâŸ«_â„‚ â€¢ (e hT hn) i :
  by simp_rw [is_symmetric.apply_eigenvector_basis, smul_smul, â† real.sqrt_mul (this _),
              real.sqrt_mul_self (this _), mul_comm, â† smul_smul, complex.coe_smul],
end

include hn
/-- Let `e = hT.eigenvector_basis hn` so that we have `T (e i) = Î± i â€¢ e i` for each `i`.
Then when `T.is_symmetric` and all its eigenvalues are nonnegative,
we can define `T.sqrt` by `e i â†¦ âˆšÎ± i â€¢ e i`. -/
noncomputable def sqrt (hT : T.is_symmetric) : V â†’â‚—[â„‚] V :=
{ to_fun := Î» v, âˆ‘ (i : (fin n)),
             real.sqrt (hT.eigenvalues hn i) â€¢ âŸª(hT.eigenvector_basis hn) i, vâŸ«_â„‚
              â€¢ (hT.eigenvector_basis hn) i,
  map_add' := Î» x y, by simp_rw [inner_add_right, add_smul, smul_add, finset.sum_add_distrib],
  map_smul' := Î» r x, by simp_rw [inner_smul_right, â† smul_smul, finset.smul_sum,
                                  ring_hom.id_apply, â† complex.coe_smul, smul_smul,
                                  â† mul_assoc, mul_comm] }

lemma sqrt_eq (hT : T.is_symmetric) (v : V) : (T.sqrt hn hT) v = âˆ‘ (i : (fin n)),
  real.sqrt (hT.eigenvalues hn i) â€¢ âŸª(hT.eigenvector_basis hn) i, vâŸ«_â„‚
   â€¢ (hT.eigenvector_basis hn) i := rfl

/-- `T.sqrt ^ 2 = T` and `T.sqrt.is_positive` -/
lemma sqrt_sq_eq_linear_map_and_is_positive (hT : T.is_symmetric)
  (hT1 : âˆ€ Î¼ : â„‚, Î¼ âˆˆ spectrum â„‚ T â†’ Î¼ = â†‘Î¼.re âˆ§ 0 â‰¤ Î¼.re) :
  (T.sqrt hn hT)^2 = T âˆ§ (T.sqrt hn hT).is_positive :=
begin
  rw [pow_two, mul_eq_comp],
  split,
  { ext v,
    simp only [comp_apply, linear_map.sqrt_eq, inner_sum, inner_smul_real_right],
    simp only [â† complex.coe_smul, smul_smul, inner_smul_right],
    simp only [â† orthonormal_basis.repr_apply_apply, orthonormal_basis.repr_self,
               euclidean_space.single_apply, mul_boole, finset.sum_ite_eq,
               finset.mem_univ, if_true, â† smul_smul, complex.coe_smul],
    symmetry,
    simp only [orthonormal_basis.repr_apply_apply],
    exact linear_map.of_pos_eq_sqrt_sqrt hn T hT hT1 v, },
  { intro,
    split,
    { simp_rw [linear_map.sqrt_eq, inner_sum, â† complex.coe_smul, smul_smul, inner_smul_right,
               complex.re_sum, mul_assoc, mul_comm, â† complex.real_smul, â† inner_conj_sym x,
               â† complex.norm_sq_eq_conj_mul_self, complex.smul_re, complex.of_real_re,
               smul_eq_mul],
      apply finset.sum_nonneg',
      intros i,
      specialize hT1 (hT.eigenvalues hn i),
      simp only [complex.of_real_re, eq_self_iff_true, true_and] at hT1,
      simp_rw [mul_nonneg_iff, real.sqrt_nonneg, complex.norm_sq_nonneg, and_self, true_or], },
    { suffices : âˆ€ x, (star_ring_end â„‚) âŸªx, (T.sqrt hn hT) xâŸ«_â„‚ = âŸªx, (T.sqrt hn hT) xâŸ«_â„‚,
      { rw [â† is_R_or_C.re_eq_complex_re, â† is_R_or_C.eq_conj_iff_re],
        exact this x, },
      intro x,
      simp_rw [inner_conj_sym, linear_map.sqrt_eq, sum_inner, inner_sum, â† complex.coe_smul,
               smul_smul, inner_smul_left, inner_smul_right, map_mul, is_R_or_C.conj_of_real,
               inner_conj_sym, mul_assoc, mul_comm âŸª_, xâŸ«_â„‚], }, },
end

/-- `T.is_positive` if and only if `T.is_self_adjoint` and all its eigenvalues are nonnegative. -/
theorem is_positive_iff_self_adjoint_and_nonneg_eigenvalues :
  T.is_positive â†” is_self_adjoint T âˆ§ (âˆ€ Î¼ : â„‚, Î¼ âˆˆ spectrum â„‚ T â†’ Î¼ = â†‘Î¼.re âˆ§ 0 â‰¤ Î¼.re) :=
begin
  split,
  { intro h, exact linear_map.is_positive.self_adjoint_and_nonneg_spectrum T h, },
  { intro h,
    have hT : T.is_symmetric := (is_symmetric_iff_is_self_adjoint T).mpr h.1,
    rw [â† (linear_map.sqrt_sq_eq_linear_map_and_is_positive hn T hT h.2).1, pow_two],
    have : (T.sqrt hn hT) * (T.sqrt hn hT) = (T.sqrt hn hT).adjoint * (T.sqrt hn hT) :=
    by rw is_self_adjoint_iff'.mp (linear_map.is_positive.self_adjoint_and_nonneg_spectrum _
     (linear_map.sqrt_sq_eq_linear_map_and_is_positive hn T hT h.2).2).1,
    rw this, clear this,
    intro,
    simp_rw [mul_apply, adjoint_inner_right, inner_self_eq_norm_sq_to_K],
    norm_cast, refine âŸ¨sq_nonneg â€–(linear_map.sqrt hn T hT) xâ€–, rflâŸ©, },
end

/-- every positive linear map can be written as `S.adjoint * S` for some linear map `S` -/
lemma is_positive_iff_exists_linear_map_mul_adjoint :
  T.is_positive â†” âˆƒ S : V â†’â‚—[â„‚] V, T = S.adjoint * S :=
begin
  split,
  { rw [linear_map.is_positive_iff_self_adjoint_and_nonneg_eigenvalues hn,
        â† is_symmetric_iff_is_self_adjoint],
    rintro âŸ¨hT, hT1âŸ©,
    use T.sqrt hn hT,
    rw [is_self_adjoint_iff'.mp (linear_map.is_positive.self_adjoint_and_nonneg_spectrum _
         (linear_map.sqrt_sq_eq_linear_map_and_is_positive hn T hT hT1).2).1,
        â† pow_two, (linear_map.sqrt_sq_eq_linear_map_and_is_positive hn T hT hT1).1],  },
  { intros h x,
    cases h with S hS,
    simp_rw [hS, mul_apply, adjoint_inner_right, inner_self_eq_norm_sq_to_K],
    norm_cast,
    refine âŸ¨sq_nonneg _, rflâŸ©, },
end

end linear_map

section finite_dimensional

variables (V : Type*) [inner_product_space â„‚ V] [finite_dimensional â„‚ V] (T : V â†’L[â„‚] V)

open linear_map
lemma self_adjoint_clm_iff_self_adjoint_lm :
  is_self_adjoint T â†” is_self_adjoint T.to_linear_map :=
begin
  simp_rw [continuous_linear_map.to_linear_map_eq_coe, is_self_adjoint_iff',
           continuous_linear_map.is_self_adjoint_iff', continuous_linear_map.ext_iff,
           linear_map.ext_iff, continuous_linear_map.coe_coe, adjoint_eq_to_clm_adjoint],
  split,
  { intros h x, rw â† h x, refl, },
  { intros h x, rw â† h x, refl, },
end

lemma is_positive_clm_iff_is_positive_lm :
  T.is_positive â†” linear_map.is_positive T.to_linear_map :=
begin
  simp_rw [linear_map.is_positive, continuous_linear_map.is_positive,
           self_adjoint_clm_iff_self_adjoint_lm, linear_map.is_self_adjoint_iff_real_inner,
           continuous_linear_map.re_apply_inner_self_apply, inner_re_symm,
           is_R_or_C.re_eq_complex_re, continuous_linear_map.to_linear_map_eq_coe,
           continuous_linear_map.coe_coe, and.comm],
  refine âŸ¨Î» h x, âŸ¨h.1 x, h.2 xâŸ©, Î» h, âŸ¨Î» x, (h x).1, Î» x, (h x).2âŸ©âŸ©,
end

end finite_dimensional
