/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, FrÃ©dÃ©ric Dupuis
-/
import analysis.inner_product_space.calculus
import analysis.inner_product_space.dual
import analysis.inner_product_space.adjoint
import analysis.calculus.lagrange_multipliers
import linear_algebra.eigenspace

/-!
# The Rayleigh quotient

The Rayleigh quotient of a self-adjoint operator `T` on an inner product space `E` is the function
`Î» x, âŸªT x, xâŸ« / â€–xâ€– ^ 2`.

The main results of this file are `is_self_adjoint.has_eigenvector_of_is_max_on` and
`is_self_adjoint.has_eigenvector_of_is_min_on`, which state that if `E` is complete, and if the
Rayleigh quotient attains its global maximum/minimum over some sphere at the point `xâ‚€`, then `xâ‚€`
is an eigenvector of `T`, and the `supr`/`infi` of `Î» x, âŸªT x, xâŸ« / â€–xâ€– ^ 2` is the corresponding
eigenvalue.

The corollaries `is_self_adjoint.has_eigenvalue_supr_of_finite_dimensional` and
`is_self_adjoint.has_eigenvalue_supr_of_finite_dimensional` state that if `E` is finite-dimensional
and nontrivial, then `T` has some (nonzero) eigenvectors with eigenvalue the `supr`/`infi` of
`Î» x, âŸªT x, xâŸ« / â€–xâ€– ^ 2`.

## TODO

A slightly more elaborate corollary is that if `E` is complete and `T` is a compact operator, then
`T` has some (nonzero) eigenvector with eigenvalue either `â¨† x, âŸªT x, xâŸ« / â€–xâ€– ^ 2` or
`â¨… x, âŸªT x, xâŸ« / â€–xâ€– ^ 2` (not necessarily both).

-/

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [inner_product_space ğ•œ E]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y
open_locale nnreal

open module.End metric

namespace continuous_linear_map
variables (T : E â†’L[ğ•œ] E)
local notation `rayleigh_quotient` := Î» x : E, T.re_apply_inner_self x / â€–(x:E)â€– ^ 2

lemma rayleigh_smul (x : E) {c : ğ•œ} (hc : c â‰  0) :
  rayleigh_quotient (c â€¢ x) = rayleigh_quotient x :=
begin
  by_cases hx : x = 0,
  { simp [hx] },
  have : â€–câ€– â‰  0 := by simp [hc],
  have : â€–xâ€– â‰  0 := by simp [hx],
  field_simp [norm_smul, T.re_apply_inner_self_smul],
  ring
end

lemma image_rayleigh_eq_image_rayleigh_sphere {r : â„} (hr : 0 < r) :
  rayleigh_quotient '' {0}á¶œ = rayleigh_quotient '' (sphere 0 r) :=
begin
  ext a,
  split,
  { rintros âŸ¨x, (hx : x â‰  0), hxTâŸ©,
    have : â€–xâ€– â‰  0 := by simp [hx],
    let c : ğ•œ := â†‘â€–xâ€–â»Â¹ * r,
    have : c â‰  0 := by simp [c, hx, hr.ne'],
    refine âŸ¨c â€¢ x, _, _âŸ©,
    { field_simp [norm_smul, is_R_or_C.norm_eq_abs, abs_of_nonneg hr.le] },
    { rw T.rayleigh_smul x this,
      exact hxT } },
  { rintros âŸ¨x, hx, hxTâŸ©,
    exact âŸ¨x, ne_zero_of_mem_sphere hr.ne' âŸ¨x, hxâŸ©, hxTâŸ© },
end

lemma supr_rayleigh_eq_supr_rayleigh_sphere {r : â„} (hr : 0 < r) :
  (â¨† x : {x : E // x â‰  0}, rayleigh_quotient x) = â¨† x : sphere (0:E) r, rayleigh_quotient x :=
show (â¨† x : ({0} : set E)á¶œ, rayleigh_quotient x) = _,
by simp only [â†@Sup_image' _ _ _ _ rayleigh_quotient, T.image_rayleigh_eq_image_rayleigh_sphere hr]

lemma infi_rayleigh_eq_infi_rayleigh_sphere {r : â„} (hr : 0 < r) :
  (â¨… x : {x : E // x â‰  0}, rayleigh_quotient x) = â¨… x : sphere (0:E) r, rayleigh_quotient x :=
show (â¨… x : ({0} : set E)á¶œ, rayleigh_quotient x) = _,
by simp only [â†@Inf_image' _ _ _ _ rayleigh_quotient, T.image_rayleigh_eq_image_rayleigh_sphere hr]

end continuous_linear_map

namespace is_self_adjoint

section real
variables {F : Type*} [inner_product_space â„ F]

lemma _root_.linear_map.is_symmetric.has_strict_fderiv_at_re_apply_inner_self
  {T : F â†’L[â„] F} (hT : (T : F â†’â‚—[â„] F).is_symmetric) (xâ‚€ : F) :
  has_strict_fderiv_at T.re_apply_inner_self (_root_.bit0 (innerSL (T xâ‚€) : F â†’L[â„] â„)) xâ‚€ :=
begin
  convert T.has_strict_fderiv_at.inner (has_strict_fderiv_at_id xâ‚€),
  ext y,
  simp_rw [_root_.bit0, continuous_linear_map.comp_apply, continuous_linear_map.add_apply,
    innerSL_apply, fderiv_inner_clm_apply, id.def, continuous_linear_map.prod_apply,
    continuous_linear_map.id_apply, hT.apply_clm xâ‚€ y, real_inner_comm _ xâ‚€],
end

variables [complete_space F] {T : F â†’L[â„] F}
local notation `rayleigh_quotient` := Î» x : F, T.re_apply_inner_self x / â€–(x:F)â€– ^ 2

lemma linearly_dependent_of_is_local_extr_on (hT : is_self_adjoint T)
  {xâ‚€ : F} (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:F) â€–xâ‚€â€–) xâ‚€) :
  âˆƒ a b : â„, (a, b) â‰  0 âˆ§ a â€¢ xâ‚€ + b â€¢ T xâ‚€ = 0 :=
begin
  have H : is_local_extr_on T.re_apply_inner_self {x : F | â€–xâ€– ^ 2 = â€–xâ‚€â€– ^ 2} xâ‚€,
  { convert hextr,
    ext x,
    simp [dist_eq_norm] },
  -- find Lagrange multipliers for the function `T.re_apply_inner_self` and the
  -- hypersurface-defining function `Î» x, â€–xâ€– ^ 2`
  obtain âŸ¨a, b, hâ‚, hâ‚‚âŸ© := is_local_extr_on.exists_multipliers_of_has_strict_fderiv_at_1d H
    (has_strict_fderiv_at_norm_sq xâ‚€) (hT.is_symmetric.has_strict_fderiv_at_re_apply_inner_self xâ‚€),
  refine âŸ¨a, b, hâ‚, _âŸ©,
  apply (inner_product_space.to_dual_map â„ F).injective,
  simp only [linear_isometry.map_add, linear_isometry.map_smul, linear_isometry.map_zero],
  change a â€¢ innerSL xâ‚€ + b â€¢ innerSL (T xâ‚€) = 0,
  apply smul_right_injective (F â†’L[â„] â„) (two_ne_zero : (2:â„) â‰  0),
  simpa only [_root_.bit0, add_smul, smul_add, one_smul, add_zero] using hâ‚‚
end

lemma eq_smul_self_of_is_local_extr_on_real (hT : is_self_adjoint T)
  {xâ‚€ : F} (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:F) â€–xâ‚€â€–) xâ‚€) :
  T xâ‚€ = (rayleigh_quotient xâ‚€) â€¢ xâ‚€ :=
begin
  obtain âŸ¨a, b, hâ‚, hâ‚‚âŸ© := hT.linearly_dependent_of_is_local_extr_on hextr,
  by_cases hxâ‚€ : xâ‚€ = 0,
  { simp [hxâ‚€] },
  by_cases hb : b = 0,
  { have : a â‰  0 := by simpa [hb] using hâ‚,
    refine absurd _ hxâ‚€,
    apply smul_right_injective F this,
    simpa [hb] using hâ‚‚ },
  let c : â„ := - bâ»Â¹ * a,
  have hc : T xâ‚€ = c â€¢ xâ‚€,
  { have : b * (bâ»Â¹ * a) = a := by field_simp [mul_comm],
    apply smul_right_injective F hb,
    simp [c, eq_neg_of_add_eq_zero_left hâ‚‚, â† mul_smul, this] },
  convert hc,
  have : â€–xâ‚€â€– â‰  0 := by simp [hxâ‚€],
  field_simp,
  simpa [inner_smul_left, real_inner_self_eq_norm_mul_norm, sq] using congr_arg (Î» x, âŸªx, xâ‚€âŸ«_â„) hc,
end

end real

section complete_space
variables [complete_space E] {T : E â†’L[ğ•œ] E}
local notation `rayleigh_quotient` := Î» x : E, T.re_apply_inner_self x / â€–(x:E)â€– ^ 2

lemma eq_smul_self_of_is_local_extr_on (hT : is_self_adjoint T) {xâ‚€ : E}
  (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:E) â€–xâ‚€â€–) xâ‚€) :
  T xâ‚€ = (â†‘(rayleigh_quotient xâ‚€) : ğ•œ) â€¢ xâ‚€ :=
begin
  letI := inner_product_space.is_R_or_C_to_real ğ•œ E,
  let hSA := hT.is_symmetric.restrict_scalars.to_self_adjoint.prop,
  exact hSA.eq_smul_self_of_is_local_extr_on_real hextr,
end

/-- For a self-adjoint operator `T`, a local extremum of the Rayleigh quotient of `T` on a sphere
centred at the origin is an eigenvector of `T`. -/
lemma has_eigenvector_of_is_local_extr_on (hT : is_self_adjoint T) {xâ‚€ : E}
  (hxâ‚€ : xâ‚€ â‰  0) (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:E) â€–xâ‚€â€–) xâ‚€) :
  has_eigenvector (T : E â†’â‚—[ğ•œ] E) â†‘(rayleigh_quotient xâ‚€) xâ‚€ :=
begin
  refine âŸ¨_, hxâ‚€âŸ©,
  rw module.End.mem_eigenspace_iff,
  exact hT.eq_smul_self_of_is_local_extr_on hextr
end

/-- For a self-adjoint operator `T`, a maximum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global supremum of the Rayleigh
quotient. -/
lemma has_eigenvector_of_is_max_on (hT : is_self_adjoint T) {xâ‚€ : E}
  (hxâ‚€ : xâ‚€ â‰  0) (hextr : is_max_on T.re_apply_inner_self (sphere (0:E) â€–xâ‚€â€–) xâ‚€) :
  has_eigenvector (T : E â†’â‚—[ğ•œ] E) â†‘(â¨† x : {x : E // x â‰  0}, rayleigh_quotient x) xâ‚€ :=
begin
  convert hT.has_eigenvector_of_is_local_extr_on hxâ‚€ (or.inr hextr.localize),
  have hxâ‚€' : 0 < â€–xâ‚€â€– := by simp [hxâ‚€],
  have hxâ‚€'' : xâ‚€ âˆˆ sphere (0:E) (â€–xâ‚€â€–) := by simp,
  rw T.supr_rayleigh_eq_supr_rayleigh_sphere hxâ‚€',
  refine is_max_on.supr_eq hxâ‚€'' _,
  intros x hx,
  dsimp,
  have : â€–xâ€– = â€–xâ‚€â€– := by simpa using hx,
  rw this,
  exact div_le_div_of_le (sq_nonneg â€–xâ‚€â€–) (hextr hx)
end

/-- For a self-adjoint operator `T`, a minimum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global infimum of the Rayleigh
quotient. -/
lemma has_eigenvector_of_is_min_on (hT : is_self_adjoint T) {xâ‚€ : E}
  (hxâ‚€ : xâ‚€ â‰  0) (hextr : is_min_on T.re_apply_inner_self (sphere (0:E) â€–xâ‚€â€–) xâ‚€) :
  has_eigenvector (T : E â†’â‚—[ğ•œ] E) â†‘(â¨… x : {x : E // x â‰  0}, rayleigh_quotient x) xâ‚€ :=
begin
  convert hT.has_eigenvector_of_is_local_extr_on hxâ‚€ (or.inl hextr.localize),
  have hxâ‚€' : 0 < â€–xâ‚€â€– := by simp [hxâ‚€],
  have hxâ‚€'' : xâ‚€ âˆˆ sphere (0:E) (â€–xâ‚€â€–) := by simp,
  rw T.infi_rayleigh_eq_infi_rayleigh_sphere hxâ‚€',
  refine is_min_on.infi_eq hxâ‚€'' _,
  intros x hx,
  dsimp,
  have : â€–xâ€– = â€–xâ‚€â€– := by simpa using hx,
  rw this,
  exact div_le_div_of_le (sq_nonneg â€–xâ‚€â€–) (hextr hx)
end

end complete_space

end is_self_adjoint

section finite_dimensional
variables [finite_dimensional ğ•œ E] [_i : nontrivial E] {T : E â†’â‚—[ğ•œ] E}

namespace linear_map

namespace is_symmetric

include _i

/-- The supremum of the Rayleigh quotient of a symmetric operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
lemma has_eigenvalue_supr_of_finite_dimensional (hT : T.is_symmetric) :
  has_eigenvalue T â†‘(â¨† x : {x : E // x â‰  0}, is_R_or_C.re âŸªT x, xâŸ« / â€–(x:E)â€– ^ 2) :=
begin
  haveI := finite_dimensional.proper_is_R_or_C ğ•œ E,
  let T' := hT.to_self_adjoint,
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0,
  have Hâ‚ : is_compact (sphere (0:E) â€–xâ€–) := is_compact_sphere _ _,
  have Hâ‚‚ : (sphere (0:E) â€–xâ€–).nonempty := âŸ¨x, by simpâŸ©,
  -- key point: in finite dimension, a continuous function on the sphere has a max
  obtain âŸ¨xâ‚€, hxâ‚€', hTxâ‚€âŸ© :=
    Hâ‚.exists_forall_ge Hâ‚‚ T'.val.re_apply_inner_self_continuous.continuous_on,
  have hxâ‚€ : â€–xâ‚€â€– = â€–xâ€– := by simpa using hxâ‚€',
  have : is_max_on T'.val.re_apply_inner_self (sphere 0 â€–xâ‚€â€–) xâ‚€,
  { simpa only [â† hxâ‚€] using hTxâ‚€ },
  have hxâ‚€_ne : xâ‚€ â‰  0,
  { have : â€–xâ‚€â€– â‰  0 := by simp only [hxâ‚€, norm_eq_zero, hx, ne.def, not_false_iff],
    simpa [â† norm_eq_zero, ne.def] },
  exact has_eigenvalue_of_has_eigenvector (T'.prop.has_eigenvector_of_is_max_on hxâ‚€_ne this)
end

/-- The infimum of the Rayleigh quotient of a symmetric operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
lemma has_eigenvalue_infi_of_finite_dimensional (hT : T.is_symmetric) :
  has_eigenvalue T â†‘(â¨… x : {x : E // x â‰  0}, is_R_or_C.re âŸªT x, xâŸ« / â€–(x:E)â€– ^ 2) :=
begin
  haveI := finite_dimensional.proper_is_R_or_C ğ•œ E,
  let T' := hT.to_self_adjoint,
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0,
  have Hâ‚ : is_compact (sphere (0:E) â€–xâ€–) := is_compact_sphere _ _,
  have Hâ‚‚ : (sphere (0:E) â€–xâ€–).nonempty := âŸ¨x, by simpâŸ©,
  -- key point: in finite dimension, a continuous function on the sphere has a min
  obtain âŸ¨xâ‚€, hxâ‚€', hTxâ‚€âŸ© :=
    Hâ‚.exists_forall_le Hâ‚‚ T'.val.re_apply_inner_self_continuous.continuous_on,
  have hxâ‚€ : â€–xâ‚€â€– = â€–xâ€– := by simpa using hxâ‚€',
  have : is_min_on T'.val.re_apply_inner_self (sphere 0 â€–xâ‚€â€–) xâ‚€,
  { simpa only [â† hxâ‚€] using hTxâ‚€ },
  have hxâ‚€_ne : xâ‚€ â‰  0,
  { have : â€–xâ‚€â€– â‰  0 := by simp only [hxâ‚€, norm_eq_zero, hx, ne.def, not_false_iff],
    simpa [â† norm_eq_zero, ne.def] },
  exact has_eigenvalue_of_has_eigenvector (T'.prop.has_eigenvector_of_is_min_on hxâ‚€_ne this)
end

omit _i

lemma subsingleton_of_no_eigenvalue_finite_dimensional
  (hT : T.is_symmetric) (hT' : âˆ€ Î¼ : ğ•œ, module.End.eigenspace (T : E â†’â‚—[ğ•œ] E) Î¼ = âŠ¥) :
  subsingleton E :=
(subsingleton_or_nontrivial E).resolve_right
  (Î» h, by exactI absurd (hT' _) hT.has_eigenvalue_supr_of_finite_dimensional)

end is_symmetric

end linear_map

end finite_dimensional
