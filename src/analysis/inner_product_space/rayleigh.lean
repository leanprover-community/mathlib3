/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import analysis.inner_product_space.calculus
import analysis.inner_product_space.dual
import analysis.calculus.lagrange_multipliers
import linear_algebra.eigenspace

/-!
# The Rayleigh quotient

-/
variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [inner_product_space ğ•œ E]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

open module.End metric

section real
variables {F : Type*} [inner_product_space â„ F] [complete_space F] {T : F â†’L[â„] F}

-- move this
lemma has_strict_fderiv_at_norm_sq (x : F) :
  has_strict_fderiv_at (Î» x, âˆ¥xâˆ¥ ^ 2) (bit0 (inner_right x)) x :=
begin
  convert (has_strict_fderiv_at_id x).inner (has_strict_fderiv_at_id x),
  { ext y,
    simp [norm_sq_eq_inner] },
  { ext y,
    simp [bit0, real_inner_comm] },
end

lemma self_adjoint.linearly_dependent_of_is_local_extr_on (hT : self_adjoint (T : F â†’â‚—[â„] F))
  {xâ‚€ : F} (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:F) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  âˆƒ a b : â„, (a, b) â‰  0 âˆ§ a â€¢ xâ‚€ + b â€¢ T xâ‚€ = 0 :=
begin
  let Ï†' : F â†’L[â„] â„ := bit0 (inner_right (T xâ‚€)),
  have hÏ†' : has_strict_fderiv_at T.re_apply_inner_self Ï†' xâ‚€,
  { convert T.has_strict_fderiv_at.inner (has_strict_fderiv_at_id xâ‚€),
    ext y,
    simp only [Ï†', add_left_inj, bit0, continuous_linear_map.add_apply,
      continuous_linear_map.coe_comp', continuous_linear_map.coe_id',
      continuous_linear_map.prod_apply, eq_self_iff_true, fderiv_inner_clm_apply,
      function.comp_app, id.def, inner_right_apply, hT.apply_clm xâ‚€ y, real_inner_comm xâ‚€] },
  obtain âŸ¨Î›, b, hâ‚, hâ‚‚âŸ© := is_local_extr_on.exists_linear_map_of_has_strict_fderiv_at _
    (has_strict_fderiv_at_norm_sq xâ‚€) hÏ†',
  refine âŸ¨Î› 1, b, _, _âŸ©,
  { contrapose! hâ‚,
    simp only [prod.mk_eq_zero] at âŠ¢ hâ‚,
    refine âŸ¨linear_map.ext (Î» x, _), hâ‚.2âŸ©,
    simpa [hâ‚.1] using Î›.map_smul x 1 },
  { apply (inner_product_space.to_dual_map F).injective,
    ext x,
    have : (Î› âŸªxâ‚€, xâŸ«_â„ + Î› âŸªxâ‚€, xâŸ«_â„) + (b * âŸªT xâ‚€, xâŸ«_â„ + b * âŸªT xâ‚€, xâŸ«_â„) = 0,
    { simpa only [Ï†', bit0, inner_right_apply, algebra.id.smul_eq_mul,
      continuous_linear_map.add_apply, linear_map.map_add, mul_add] using hâ‚‚ x },
    simp only [continuous_linear_map.zero_apply, algebra.id.smul_eq_mul,
      inner_product_space.to_dual_map_apply, linear_isometry.map_add, linear_isometry.map_smul,
      continuous_linear_map.add_apply, pi.smul_apply, linear_isometry.map_zero,
      continuous_linear_map.coe_smul'],
    have : Î› âŸªxâ‚€, xâŸ«_â„ = âŸªxâ‚€, xâŸ«_â„ * Î› 1,
    { simpa only [mul_one, algebra.id.smul_eq_mul] using Î›.map_smul âŸªxâ‚€, xâŸ«_â„ (1:â„) },
    linarith },
  convert hextr,
  ext x,
  simp [dist_eq_norm]
end

lemma self_adjoint.eq_smul_self_of_is_local_extr_on_real (hT : self_adjoint (T : F â†’â‚—[â„] F))
  {xâ‚€ : F} (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:F) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  T xâ‚€ = (T.re_apply_inner_self xâ‚€ / âˆ¥xâ‚€âˆ¥ ^ 2) â€¢ xâ‚€ :=
begin
  obtain âŸ¨a, b, hâ‚, hâ‚‚âŸ© := hT.linearly_dependent_of_is_local_extr_on hextr,
  by_cases hxâ‚€ : xâ‚€ = 0,
  { simp [hxâ‚€] },
  by_cases hb : b = 0,
  { have : a â‰  0 := by simpa [hb] using hâ‚,
    refine absurd _ hxâ‚€,
    apply smul_right_injective F this,
    simpa [hb] using hâ‚‚ },
  let c : â„ := - bâ»Â¹ * a,
  have hc : T xâ‚€ = c â€¢ xâ‚€,
  { have : b * (bâ»Â¹ * a) = a := by field_simp [mul_comm],
    apply smul_right_injective F hb,
    simp [c, â† neg_eq_of_add_eq_zero hâ‚‚, â† mul_smul, this] },
  convert hc,
  have : âˆ¥xâ‚€âˆ¥ â‰  0 := by simp [hxâ‚€],
  field_simp,
  simpa [inner_smul_left, real_inner_self_eq_norm_sq, sq] using congr_arg (Î» x, âŸªx, xâ‚€âŸ«_â„) hc,
end

end real

section complete_space
variables [complete_space E] {T : E â†’L[ğ•œ] E}

lemma self_adjoint.eq_smul_self_of_is_local_extr_on (hT : self_adjoint (T : E â†’â‚—[ğ•œ] E)) {xâ‚€ : E}
  (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:E) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  T xâ‚€ = (â†‘(T.re_apply_inner_self xâ‚€ / âˆ¥xâ‚€âˆ¥ ^ 2) : ğ•œ) â€¢ xâ‚€ :=
begin
  letI := inner_product_space.is_R_or_C_to_real ğ•œ E,
  letI : is_scalar_tower â„ ğ•œ E := restrict_scalars.is_scalar_tower _ _ _,
  let S : E â†’L[â„] E :=
    @continuous_linear_map.restrict_scalars ğ•œ E E _ _ _ _ _ _ _ â„ _ _ _ _ T,
  have hSA : self_adjoint (S : E â†’â‚—[â„] E) := Î» x y, by
  { have := hT x y,
    simp only [continuous_linear_map.coe_coe] at this,
    simp only [real_inner_eq_re_inner, this, continuous_linear_map.coe_restrict_scalars,
      continuous_linear_map.coe_coe, linear_map.coe_restrict_scalars_eq_coe] },
  exact self_adjoint.eq_smul_self_of_is_local_extr_on_real hSA hextr,
end

/-- For a self-adjoint operator `T`, a local extremum of the Rayleigh quotient of `T` on a sphere
centred at the origin is an eigenvector of `T`. -/
lemma self_adjoint.has_eigenvector_of_is_local_extr_on (hT : self_adjoint (T : E â†’â‚—[ğ•œ] E)) {xâ‚€ : E}
  (hxâ‚€ : xâ‚€ â‰  0) (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:E) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  has_eigenvector (T : E â†’â‚—[ğ•œ] E) â†‘(T.re_apply_inner_self xâ‚€ / âˆ¥xâ‚€âˆ¥ ^ 2) xâ‚€ :=
begin
  refine âŸ¨_, hxâ‚€âŸ©,
  rw module.End.mem_eigenspace_iff,
  exact hT.eq_smul_self_of_is_local_extr_on hextr
end

end complete_space

section finite_dimensional
variables [finite_dimensional ğ•œ E] [nontrivial E]

/-- A self-adjoint operator on a nontrivial finite-dimensional vector space has a real eigenvalue.
-/
lemma self_adjoint.has_eigenvalue_of_finite_dimensional {T : E â†’â‚—[ğ•œ] E} (hT : self_adjoint T) :
  âˆƒ c : â„, has_eigenvalue T c :=
begin
  let T' : E â†’L[ğ•œ] E := T.to_continuous_linear_map,
  have hT' : self_adjoint (T' : E â†’â‚—[ğ•œ] E) := hT,
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0,
  have Hâ‚ : is_compact (sphere (0:E) âˆ¥xâˆ¥) := is_compact_sphere _ _,
  have Hâ‚‚ : (sphere (0:E) âˆ¥xâˆ¥).nonempty := âŸ¨x, by simpâŸ©,
  obtain âŸ¨xâ‚€, hxâ‚€', hTxâ‚€âŸ© :=
    Hâ‚.exists_forall_ge Hâ‚‚ T'.re_apply_inner_self_continuous.continuous_on,
  have hxâ‚€ : âˆ¥xâ‚€âˆ¥ = âˆ¥xâˆ¥ := by simpa using hxâ‚€',
  have : is_local_max_on T'.re_apply_inner_self (sphere 0 âˆ¥xâ‚€âˆ¥) xâ‚€,
  { simp only [â† hxâ‚€] at hTxâ‚€,
    exact is_max_on.localize hTxâ‚€ },
  have hxâ‚€_ne : xâ‚€ â‰  0,
  { have : âˆ¥xâ‚€âˆ¥ â‰  0 := by simp only [hxâ‚€, norm_eq_zero, hx, ne.def, not_false_iff],
    simpa [â† norm_eq_zero, ne.def] },
  use (T'.re_apply_inner_self xâ‚€) / âˆ¥xâ‚€âˆ¥ ^ 2,
  exact has_eigenvalue_of_has_eigenvector
    (hT'.has_eigenvector_of_is_local_extr_on hxâ‚€_ne (or.inr this))
end

end finite_dimensional
