/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, FrÃ©dÃ©ric Dupuis
-/
import analysis.inner_product_space.calculus
import analysis.inner_product_space.dual
import analysis.inner_product_space.adjoint
import analysis.calculus.lagrange_multipliers
import analysis.normed_space.compact_map
import linear_algebra.eigenspace
import algebra.star.self_adjoint
import analysis.normed_space.pointwise

/-!
# The Rayleigh quotient

The Rayleigh quotient of a self-adjoint operator `T` on an inner product space `E` is the function
`Î» x, âŸªT x, xâŸ« / âˆ¥xâˆ¥ ^ 2`.

The main results of this file are `is_self_adjoint.has_eigenvector_of_is_max_on` and
`is_self_adjoint.has_eigenvector_of_is_min_on`, which state that if `E` is complete, and if the
Rayleigh quotient attains its global maximum/minimum over some sphere at the point `xâ‚€`, then `xâ‚€`
is an eigenvector of `T`, and the `supr`/`infi` of `Î» x, âŸªT x, xâŸ« / âˆ¥xâˆ¥ ^ 2` is the corresponding
eigenvalue.

The corollaries `is_self_adjoint.has_eigenvalue_supr_of_finite_dimensional` and
`is_self_adjoint.has_eigenvalue_supr_of_finite_dimensional` state that if `E` is finite-dimensional
and nontrivial, then `T` has some (nonzero) eigenvectors with eigenvalue the `supr`/`infi` of
`Î» x, âŸªT x, xâŸ« / âˆ¥xâˆ¥ ^ 2`.

## TODO

A slightly more elaborate corollary is that if `E` is complete and `T` is a compact operator, then
`T` has some (nonzero) eigenvector with eigenvalue either `â¨† x, âŸªT x, xâŸ« / âˆ¥xâˆ¥ ^ 2` or
`â¨… x, âŸªT x, xâŸ« / âˆ¥xâˆ¥ ^ 2` (not necessarily both).

-/

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
variables {E : Type*} [inner_product_space ğ•œ E]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y
open_locale nnreal complex_conjugate topological_space filter

open module.End metric

namespace continuous_linear_map
variables (T : E â†’L[ğ•œ] E)
local notation `rayleigh_quotient` := Î» x : E, T.re_apply_inner_self x / âˆ¥(x:E)âˆ¥ ^ 2
local notation `rayleigh_quotient_sphere` :=
Î» x : sphere (0:E) 1, T.re_apply_inner_self x / âˆ¥(x:E)âˆ¥ ^ 2

lemma rayleigh_smul (x : E) {c : ğ•œ} (hc : c â‰  0) :
  rayleigh_quotient (c â€¢ x) = rayleigh_quotient x :=
begin
  by_cases hx : x = 0,
  { simp [hx] },
  have : âˆ¥câˆ¥ â‰  0 := by simp [hc],
  have : âˆ¥xâˆ¥ â‰  0 := by simp [hx],
  field_simp [norm_smul, T.re_apply_inner_self_smul],
  ring
end

lemma image_rayleigh_eq_image_rayleigh_sphere {r : â„} (hr : 0 < r) :
  rayleigh_quotient '' {0}á¶œ = rayleigh_quotient '' (sphere 0 r) :=
begin
  ext a,
  split,
  { rintros âŸ¨x, (hx : x â‰  0), hxTâŸ©,
    have : âˆ¥xâˆ¥ â‰  0 := by simp [hx],
    let c : ğ•œ := â†‘âˆ¥xâˆ¥â»Â¹ * r,
    have : c â‰  0 := by simp [c, hx, hr.ne'],
    refine âŸ¨c â€¢ x, _, _âŸ©,
    { field_simp [norm_smul, is_R_or_C.norm_eq_abs, abs_of_nonneg hr.le] },
    { rw T.rayleigh_smul x this,
      exact hxT } },
  { rintros âŸ¨x, hx, hxTâŸ©,
    exact âŸ¨x, ne_zero_of_mem_sphere hr.ne' âŸ¨x, hxâŸ©, hxTâŸ© },
end

lemma supr_rayleigh_eq_supr_rayleigh_sphere {r : â„} (hr : 0 < r) :
  (â¨† x : {x : E // x â‰  0}, rayleigh_quotient x) = â¨† x : sphere (0:E) r, rayleigh_quotient x :=
show (â¨† x : ({0} : set E)á¶œ, rayleigh_quotient x) = _,
by simp only [@csupr_set _ _ _ _ rayleigh_quotient, T.image_rayleigh_eq_image_rayleigh_sphere hr]

lemma infi_rayleigh_eq_infi_rayleigh_sphere {r : â„} (hr : 0 < r) :
  (â¨… x : {x : E // x â‰  0}, rayleigh_quotient x) = â¨… x : sphere (0:E) r, rayleigh_quotient x :=
show (â¨… x : ({0} : set E)á¶œ, rayleigh_quotient x) = _,
by simp only [@cinfi_set _ _ _ _ rayleigh_quotient, T.image_rayleigh_eq_image_rayleigh_sphere hr]

lemma rayleigh_le_norm (x : E) : rayleigh_quotient x â‰¤ âˆ¥Tâˆ¥ :=
begin
  by_cases hx : x = 0,
  { simp only [hx, div_zero, nat.one_ne_zero, norm_zero, ne.def, norm_nonneg, not_false_iff,
               bit0_eq_zero, zero_pow'] },
  have h : T.re_apply_inner_self x â‰¤ âˆ¥T xâˆ¥ * âˆ¥xâˆ¥ := re_inner_le_norm (T x) x,
  refine (div_le_iff _).mpr _,
  refine pow_two_pos_of_ne_zero _ (ne_of_gt (norm_pos_iff.mpr hx)),
  calc _ â‰¤ âˆ¥T xâˆ¥ * âˆ¥xâˆ¥       : h
      ... â‰¤ âˆ¥Tâˆ¥ * âˆ¥xâˆ¥ * âˆ¥xâˆ¥  : mul_le_mul_of_nonneg_right (le_op_norm _ _) (norm_nonneg _)
      ... = âˆ¥Tâˆ¥ * âˆ¥xâˆ¥ ^ 2    : by rw [mul_assoc, pow_two],
end

lemma neg_rayleigh_le_norm (x : E) : -rayleigh_quotient x â‰¤ âˆ¥Tâˆ¥ :=
begin
  have h := rayleigh_le_norm (-T) x,
  rw [norm_neg] at h,
  simp only [re_apply_inner_self, neg_apply, inner_neg_left, add_monoid_hom.map_neg] at h,
  simp only [re_apply_inner_self, â†neg_div, h],
end

lemma supr_rayleigh_le_norm : (â¨† x, rayleigh_quotient x) â‰¤ âˆ¥Tâˆ¥ :=
csupr_le (Î» x, rayleigh_le_norm T x)

lemma supr_neg_rayleigh_le_norm : (â¨† x, -rayleigh_quotient x) â‰¤ âˆ¥Tâˆ¥ :=
csupr_le (Î» x, neg_rayleigh_le_norm T x)

lemma rayleigh_sphere_eq (x : sphere (0:E) 1) : rayleigh_quotient x = is_R_or_C.re âŸªT x, xâŸ« :=
by { simp_rw [norm_eq_of_mem_sphere x, one_pow 2, div_one _], refl }

-- moveme
include ğ•œ
lemma sphere_nonempty [nontrivial E] {r : â„} (hr : 0 â‰¤ r) : nonempty (sphere (0:E) r) :=
begin
  letI : inner_product_space â„ E := inner_product_space.is_R_or_C_to_real ğ•œ E,
  exact (sphere (0:E) r).nonempty_coe_sort.mpr (normed_space.sphere_nonempty.mpr hr),
end
omit ğ•œ

lemma supr_rayleigh_sphere_le_norm [nontrivial E] {r : â„} (hr : 0 â‰¤ r) :
  (â¨† x : sphere (0:E) r, rayleigh_quotient x) â‰¤ âˆ¥Tâˆ¥ :=
begin
  haveI : nonempty (sphere (0:E) r) := sphere_nonempty hr,
  exact csupr_le (Î» x, rayleigh_le_norm T x),
end

lemma supr_neg_rayleigh_sphere_le_norm [nontrivial E] {r : â„} (hr : 0 â‰¤ r) :
  (â¨† x : sphere (0:E) r, -rayleigh_quotient x) â‰¤ âˆ¥Tâˆ¥ :=
begin
  haveI : nonempty (sphere (0:E) r) := sphere_nonempty hr,
  exact csupr_le (Î» x, neg_rayleigh_le_norm T x),
end


lemma rayleigh_bdd_above : bdd_above (set.range rayleigh_quotient) :=
begin
  refine set.nonempty_def.mpr âŸ¨âˆ¥Tâˆ¥, mem_upper_bounds.mpr (Î» x hx, _)âŸ©,
  rw [set.mem_range] at hx,
  rcases hx with âŸ¨y, hyâŸ©,
  rw [â†hy],
  exact rayleigh_le_norm T y
end

lemma rayleigh_bdd_above_sphere : bdd_above (set.range rayleigh_quotient_sphere) :=
begin
  refine set.nonempty_def.mpr âŸ¨âˆ¥Tâˆ¥, mem_upper_bounds.mpr (Î» x hx, _)âŸ©,
  rw [set.mem_range] at hx,
  rcases hx with âŸ¨y, hyâŸ©,
  rw [â†hy],
  exact rayleigh_le_norm T y
end

lemma neg_rayleigh_bdd_above : bdd_above (set.range (-rayleigh_quotient)) :=
begin
  refine set.nonempty_def.mpr âŸ¨âˆ¥Tâˆ¥, mem_upper_bounds.mpr (Î» x hx, _)âŸ©,
  rw [set.mem_range] at hx,
  rcases hx with âŸ¨y, hyâŸ©,
  rw [â†hy],
  exact neg_rayleigh_le_norm T y
end

lemma supr_rayleigh_nonneg : (0 : â„) â‰¤ (â¨† x, rayleigh_quotient x) :=
le_csupr_of_le (rayleigh_bdd_above T) 0 (by simp)

lemma supr_neg_rayleigh_nonneg : (0 : â„) â‰¤ (â¨† x, (-rayleigh_quotient) x) :=
le_csupr_of_le (neg_rayleigh_bdd_above T) 0 (by simp)

-- move this
lemma _root_.is_lub_sup_of_is_lub_is_lub {Î± : Type*} {Î¹ : Sort*} [semilattice_sup Î±] {f g : Î¹ â†’ Î±}
  {A B : Î±} (hf : is_lub (set.range f) A) (hg : is_lub (set.range g) B) :
  is_lub (set.range (Î» i, (f i âŠ” g i))) (A âŠ” B) :=
begin
  split,
  { rintros _ âŸ¨i, rflâŸ©,
    exact sup_le_sup (hf.1 âŸ¨i, rflâŸ©) (hg.1 âŸ¨i, rflâŸ©) },
  intros C hC,
  have hfC : C âˆˆ upper_bounds (set.range f),
  { rintros _ âŸ¨i, rflâŸ©,
    exact le_sup_left.trans (hC âŸ¨i, rflâŸ©) },
  have hgC : C âˆˆ upper_bounds (set.range g),
  { rintros _ âŸ¨i, rflâŸ©,
    exact le_sup_right.trans (hC âŸ¨i, rflâŸ©) },
  exact sup_le (hf.2 hfC) (hg.2 hgC),
end

-- move this
lemma _root_.csupr_sup_eq {Î± : Type*} {Î¹ : Sort*} [nonempty Î¹] [conditionally_complete_lattice Î±]
  {f g : Î¹ â†’ Î±} (hf : bdd_above (set.range f)) (hg : bdd_above (set.range g)) :
  (â¨† (x : Î¹), f x âŠ” g x) = (â¨† (x : Î¹), f x) âŠ” â¨† (x : Î¹), g x :=
by rw (is_lub_sup_of_is_lub_is_lub (is_lub_csupr hf) (is_lub_csupr hg)).csupr_eq

-- move this
lemma _root_.real.csupr_sup_eq {Î¹ : Sort*} {f g : Î¹ â†’ â„} (hf : bdd_above (set.range f))
  (hg : bdd_above (set.range g)) :
  (â¨† (x : Î¹), f x âŠ” g x) = (â¨† (x : Î¹), f x) âŠ” â¨† (x : Î¹), g x :=
begin
  cases is_empty_or_nonempty Î¹; resetI,
  { simp [real.csupr_empty] },
  exact csupr_sup_eq hf hg,
end

lemma supr_abs_rayleigh_eq_sup_supr :
  (â¨† x : sphere (0:E) 1, |rayleigh_quotient x|) =
    max (â¨† x : sphere (0:E) 1, rayleigh_quotient x)
        (â¨† x : sphere (0:E) 1, -rayleigh_quotient x) :=
begin
  refine real.csupr_sup_eq T.rayleigh_bdd_above_sphere _,
  convert (-T).rayleigh_bdd_above_sphere,
  ext x,
  simp [re_apply_inner_self],
end

lemma abs_rayleigh_sphere_bdd_above :
  bdd_above (set.range (Î» x : sphere (0:E) 1, |rayleigh_quotient x|)) :=
begin
  refine set.nonempty_def.mpr âŸ¨âˆ¥Tâˆ¥, mem_upper_bounds.mpr (Î» x hx, _)âŸ©,
  rw [set.mem_range] at hx,
  rcases hx with âŸ¨y, hyâŸ©,
  rw [â†hy],
  simp [re_apply_inner_self_apply],
  calc |is_R_or_C.re (âŸªT y, yâŸ«)| â‰¤ is_R_or_C.abs (âŸªT y, yâŸ«)   : is_R_or_C.abs_re_le_abs _
                            ... â‰¤ âˆ¥T yâˆ¥ * âˆ¥(y : E)âˆ¥                 : abs_inner_le_norm _ _
                            ... = âˆ¥T yâˆ¥   : by simp [norm_eq_of_mem_sphere y]
                            ... â‰¤ âˆ¥Tâˆ¥ * âˆ¥(y : E)âˆ¥  : le_op_norm T y
                            ... = âˆ¥Tâˆ¥     : by simp [norm_eq_of_mem_sphere y]
end

lemma supr_abs_rayleigh_sphere_nonneg [nontrivial E] :
  (0 : â„) â‰¤ (â¨† x : sphere (0:E) 1, |rayleigh_quotient x|) :=
begin
  haveI : nonempty (sphere (0:E) 1) := sphere_nonempty zero_le_one,
  let xs : (sphere (0:E) 1) := nonempty.some (by apply_instance),
  calc (0 : â„) â‰¤ |rayleigh_quotient xs|     : abs_nonneg (rayleigh_quotient xs)
           ... â‰¤ (â¨† x : sphere (0:E) 1, |rayleigh_quotient x|)   :
             le_csupr_of_le T.abs_rayleigh_sphere_bdd_above xs (le_refl _),
end

lemma re_apply_inner_self_eq_rayleigh_mul_norm_sq (x : E) :
  T.re_apply_inner_self x = (rayleigh_quotient x) * âˆ¥xâˆ¥ ^ 2 :=
begin
  by_cases h : âˆ¥xâˆ¥ = 0,
  { rw [norm_eq_zero] at h,
    simp [re_apply_inner_self, h] },
  change âˆ¥xâˆ¥ â‰  0 at h,
  field_simp [re_apply_inner_self],
end

lemma re_apply_inner_self_le_supr_rayleigh_mul_norm_sq (x : E) :
  T.re_apply_inner_self x â‰¤ (â¨† z : {x : E // x â‰  0}, rayleigh_quotient z) * âˆ¥xâˆ¥ ^ 2 :=
begin
  by_cases hx : x = 0,
  { simp [hx, re_apply_inner_self] },
  rw [re_apply_inner_self_eq_rayleigh_mul_norm_sq T x],
  refine mul_le_mul_of_nonneg_right _ (pow_nonneg (norm_nonneg _) 2),
  refine @le_csupr _ {x : E // x â‰  0} _ (Î» z, rayleigh_quotient z) _ âŸ¨x, hxâŸ©,
  obtain âŸ¨C, hCâŸ© := rayleigh_bdd_above T,
  use C,
  rintros _ âŸ¨y, rflâŸ©,
  exact hC âŸ¨y, rflâŸ©,
end

lemma re_apply_inner_self_le_max_supr_rayleigh_mul_norm_sq (x : E) :
  T.re_apply_inner_self x â‰¤
    max (â¨† z : {x : E // x â‰  0}, rayleigh_quotient z)
      (â¨† z : {x : E // x â‰  0}, (-rayleigh_quotient) z) * âˆ¥xâˆ¥ ^ 2 :=
le_trans (re_apply_inner_self_le_supr_rayleigh_mul_norm_sq T x) $
  mul_le_mul_of_nonneg_right (le_max_left _ _) (sq_nonneg âˆ¥xâˆ¥)

lemma neg_re_apply_inner_self_le_supr_rayleigh_mul_norm_sq (x : E) :
  -T.re_apply_inner_self x â‰¤ (â¨† z, (-rayleigh_quotient) z) * âˆ¥xâˆ¥ ^ 2 :=
begin
  rw [re_apply_inner_self_eq_rayleigh_mul_norm_sq T x],
  simp only [neg_mul_eq_neg_mul],
  refine mul_le_mul_of_nonneg_right _ (pow_nonneg (norm_nonneg _) 2),
  exact le_csupr (neg_rayleigh_bdd_above T) _,
end

-- the tricky point in general is that for this to be true, the default value taken by the `cSup`
-- function must be 0
lemma _root_.real.csupr_neg {Î¹ : Type*} (f : Î¹ â†’ â„) : (â¨† i, - f i) = - â¨… i, f i :=
begin
  unfold supr,
  unfold infi,
  rw real.Inf_def,
  rw neg_neg,
  congr,
  ext i,
  split,
  { rintros âŸ¨a, rflâŸ©,
    use a,
    simp },
  rw set.mem_neg,
  { rintros âŸ¨a, haâŸ©,
    apply_fun has_neg.neg at ha,
    use a,
    simp [ha] },
end

lemma re_apply_inner_self_le_supr_abs_rayleigh_mul_norm_sq (x : E) :
  T.re_apply_inner_self x â‰¤ (â¨† z : sphere (0:E) 1, |rayleigh_quotient z|) * âˆ¥xâˆ¥ ^ 2 :=
begin
  by_cases hx : x = 0,
  { simp [hx, re_apply_inner_self], },
  simp only [supr_abs_rayleigh_eq_sup_supr],
  have := T.re_apply_inner_self_le_max_supr_rayleigh_mul_norm_sq x,
  simp at this,
  refine le_trans this _,
  refine mul_le_mul_of_nonneg_right _ (sq_nonneg _),
  apply max_le_max,
  { rw â† supr_rayleigh_eq_supr_rayleigh_sphere,
    simp },
  { rw [real.csupr_neg, real.csupr_neg, neg_le_neg_iff, â† infi_rayleigh_eq_infi_rayleigh_sphere],
    simp },
end

lemma neg_re_apply_inner_self_le_supr_abs_rayleigh_mul_norm_sq (x : E) :
  -T.re_apply_inner_self x â‰¤ (â¨† z : sphere (0:E) 1, |rayleigh_quotient z|) * âˆ¥xâˆ¥ ^ 2 :=
begin
  convert (-T).re_apply_inner_self_le_supr_abs_rayleigh_mul_norm_sq x,
  { simp [re_apply_inner_self] },
  ext x,
  simp [re_apply_inner_self],
end

lemma neg_re_apply_inner_self_le_max_supr_rayleigh_mul_norm_sq (x : E) :
  -T.re_apply_inner_self x â‰¤
    max (â¨† z, rayleigh_quotient z) (â¨† z, (-rayleigh_quotient) z) * âˆ¥xâˆ¥ ^ 2 :=
le_trans (neg_re_apply_inner_self_le_supr_rayleigh_mul_norm_sq T x) $
  mul_le_mul_of_nonneg_right (le_max_right _ _) (sq_nonneg âˆ¥xâˆ¥)

lemma _root_.is_R_or_C.of_real_mul_inv_re (r : â„) (z : ğ•œ) :
  is_R_or_C.re ((r : ğ•œ)â»Â¹ * z) = râ»Â¹ * is_R_or_C.re z :=
by rw [â†is_R_or_C.of_real_inv, is_R_or_C.of_real_mul_re]

lemma _root_.is_R_or_C.of_real_mul_conj_re (r : â„) (z : ğ•œ) :
  is_R_or_C.re ((conj (r : ğ•œ)) * z) = r * is_R_or_C.re z :=
by simp only [is_R_or_C.conj_of_real, is_R_or_C.mul_re, zero_mul, is_R_or_C.of_real_re, sub_zero,
  is_R_or_C.of_real_im]

lemma _root_.is_R_or_C.of_real_mul_conj_inv_re (r : â„) (z : ğ•œ) :
  is_R_or_C.re ((conj (râ»Â¹ : ğ•œ)) * z) = râ»Â¹ * is_R_or_C.re z :=
by rw [â†is_R_or_C.of_real_inv, is_R_or_C.of_real_mul_conj_re]


end continuous_linear_map

namespace inner_product_space
namespace is_self_adjoint

section general
open continuous_linear_map

variables {T : E â†’L[ğ•œ] E}
local notation `rayleigh_quotient` := Î» x : E, T.re_apply_inner_self x / âˆ¥(x:E)âˆ¥ ^ 2

lemma norm_eq_supr_abs_rayleigh_sphere (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) :
  âˆ¥Tâˆ¥ = (â¨† x : sphere (0:E) 1, |rayleigh_quotient x|) :=
begin
  rcases subsingleton_or_nontrivial E with h_trivE|h_nontrivE,
  { haveI := h_trivE,
    haveI : is_empty (sphere (0:E) 1) := sphere_is_empty_of_subsingleton one_ne_zero,
    rw [real.csupr_empty, op_norm_subsingleton] },
  haveI := h_nontrivE,
  haveI : nonempty (sphere (0:E) 1) := sphere_nonempty zero_le_one,
  refine op_norm_eq_of_bounds _ (Î» x, _) _,
  { exact T.supr_abs_rayleigh_sphere_nonneg },
  { by_cases h_ntriv : T x = 0,
    { simp only [h_ntriv, norm_zero],
      refine mul_nonneg _ (norm_nonneg _),
      exact T.supr_abs_rayleigh_sphere_nonneg },
    set L := real.sqrt (âˆ¥T xâˆ¥ / âˆ¥xâˆ¥) with hL,
    set rT := â¨† z : sphere (0:E) 1, |rayleigh_quotient z|,
    set xâ‚ := (L : ğ•œ) â€¢ x + (Lâ»Â¹ : ğ•œ) â€¢ (T x) with hxâ‚,
    set xâ‚‚ := (L : ğ•œ) â€¢ x - (Lâ»Â¹ : ğ•œ) â€¢ (T x) with hxâ‚‚,
    change T x â‰  0 at h_ntriv,
    have hTx_ntriv : âˆ¥T xâˆ¥ â‰  0 := Î» H, by { rw [norm_eq_zero] at H, exact h_ntriv H },
    have hx_ntriv : âˆ¥xâˆ¥ â‰  0,
    { intro H,
      rw [norm_eq_zero] at H,
      rw [H] at h_ntriv,
      exact h_ntriv (map_zero _) },
    have hL_nonneg : 0 â‰¤ L := real.sqrt_nonneg _,
    have hLinv_nonneg : 0 â‰¤ Lâ»Â¹ := inv_nonneg.mpr hL_nonneg,
    have hL_sq : L^2 = âˆ¥T xâˆ¥ / âˆ¥xâˆ¥ := real.sq_sqrt (div_nonneg (norm_nonneg _) (norm_nonneg _)),
    have hLinv_sq_nonneg : 0 â‰¤ âˆ¥xâˆ¥ / âˆ¥T xâˆ¥ := div_nonneg (norm_nonneg _) (norm_nonneg _),
    have hL_sq_nonneg : 0 â‰¤ âˆ¥T xâˆ¥ / âˆ¥xâˆ¥ := div_nonneg (norm_nonneg _) (norm_nonneg _),
    have hLinv_sq : (Lâ»Â¹)^2 = âˆ¥xâˆ¥ / âˆ¥T xâˆ¥,
    { simp only [â†real.sqrt_inv, inv_div],
      rw [real.sq_sqrt hLinv_sq_nonneg] },
    have hL_ntriv : L â‰  0,
    { intro H,
      rw [real.sqrt_eq_zero hL_sq_nonneg, div_eq_zero_iff] at H,
      exact or.elim H hTx_ntriv hx_ntriv },
    have hL_mulâ‚ : L * Lâ»Â¹ = 1 := mul_inv_cancel hL_ntriv,
    have hL_mulâ‚‚ : Lâ»Â¹ * L = 1 := (mul_comm L Lâ»Â¹) â–¸ hL_mulâ‚,
    have gizmo : âŸªT (T x), xâŸ« = âŸªT x, T xâŸ« := hT _ _,
    have hâ‚ : T.re_apply_inner_self xâ‚ - T.re_apply_inner_self xâ‚‚ = 4 * âˆ¥T xâˆ¥ ^ 2,
    { simp only [hxâ‚, hxâ‚‚, re_apply_inner_self_apply, inner_add_left, inner_add_right,
        inner_smul_left, inner_smul_right, â†inner_self_eq_norm_sq, inner_sub_left, inner_sub_right,
        hL_mulâ‚, hL_mulâ‚‚, is_R_or_C.of_real_mul_re, is_R_or_C.re.map_add, is_R_or_C.re.map_sub,
        is_R_or_C.of_real_mul_inv_re, continuous_linear_map.map_add, continuous_linear_map.map_sub,
        continuous_linear_map.map_smul, is_R_or_C.of_real_mul_conj_re,
        is_R_or_C.of_real_mul_conj_inv_re, gizmo],
      ring_nf,
      field_simp },
    have hâ‚„ : T.re_apply_inner_self xâ‚ â‰¤ rT * âˆ¥xâ‚âˆ¥^2 :=
      re_apply_inner_self_le_supr_abs_rayleigh_mul_norm_sq _ _,
    have hâ‚… : -T.re_apply_inner_self xâ‚‚ â‰¤ rT * âˆ¥xâ‚‚âˆ¥^2 :=
      neg_re_apply_inner_self_le_supr_abs_rayleigh_mul_norm_sq _ _,
    have hâ‚† := calc
      4 * âˆ¥T xâˆ¥^2 â‰¤ rT * âˆ¥xâ‚âˆ¥^2 + rT * âˆ¥xâ‚‚âˆ¥^2          :
        by { rw [â†hâ‚, sub_eq_add_neg], exact add_le_add hâ‚„ hâ‚… }
             ...  = rT * (âˆ¥xâ‚âˆ¥ * âˆ¥xâ‚âˆ¥) + rT * (âˆ¥xâ‚‚âˆ¥ * âˆ¥xâ‚‚âˆ¥)      : by simp only [pow_two]
             ...  = rT * (âˆ¥xâ‚âˆ¥ * âˆ¥xâ‚âˆ¥ + âˆ¥xâ‚‚âˆ¥ * âˆ¥xâ‚‚âˆ¥)     : by ring
             ...  = rT * (2 * (L^2 * âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + (Lâ»Â¹)^2 * âˆ¥T xâˆ¥ * âˆ¥T xâˆ¥)) :
              begin
                simp only [parallelogram_law_with_norm, norm_smul, is_R_or_C.norm_of_real,
                           real.norm_of_nonneg hL_nonneg, real.norm_of_nonneg hLinv_nonneg,
                           â†is_R_or_C.of_real_inv],
                ring
              end
             ...  = 4 * rT * âˆ¥T xâˆ¥ * âˆ¥xâˆ¥  :
              begin
                simp only [hL_sq, hLinv_sq],
                field_simp,
                ring
              end,
    have hâ‚‡ : 0 < 4 * âˆ¥T xâˆ¥ := mul_pos (by norm_num) (norm_pos_iff.mpr h_ntriv),
    rw [â†mul_le_mul_left hâ‚‡],
    calc 4 * âˆ¥T xâˆ¥ * âˆ¥T xâˆ¥ = 4 * âˆ¥T xâˆ¥ ^ 2          : by rw [mul_assoc, â†pow_two]
                      ... â‰¤ 4 * rT * âˆ¥T xâˆ¥ * âˆ¥xâˆ¥   : hâ‚†
                      ... = _                      : by ring },
  { intros N hN h,
    refine csupr_le (Î» x, _),
    have hx : âˆ¥(x : E)âˆ¥ = 1 := norm_eq_of_mem_sphere x,
    simp [hx, re_apply_inner_self],
    calc _ â‰¤ is_R_or_C.abs (âŸªT x, xâŸ«)       : is_R_or_C.abs_re_le_abs _
       ... â‰¤ âˆ¥T xâˆ¥ * âˆ¥(x:E)âˆ¥      : abs_inner_le_norm _ _
       ... = âˆ¥T xâˆ¥            : by simp [hx]
       ... â‰¤ N * âˆ¥(x:E)âˆ¥     : h x
       ... = N               : by simp [hx] }
end

end general

section real
variables {F : Type*} [inner_product_space â„ F]

lemma has_strict_fderiv_at_re_apply_inner_self
  {T : F â†’L[â„] F} (hT : is_self_adjoint (T : F â†’â‚—[â„] F)) (xâ‚€ : F) :
  has_strict_fderiv_at T.re_apply_inner_self (bit0 (innerSL (T xâ‚€))) xâ‚€ :=
begin
  convert T.has_strict_fderiv_at.inner (has_strict_fderiv_at_id xâ‚€),
  ext y,
  simp [bit0, hT.apply_clm xâ‚€ y, real_inner_comm xâ‚€]
end

variables [complete_space F] {T : F â†’L[â„] F}
local notation `rayleigh_quotient` := Î» x : F, T.re_apply_inner_self x / âˆ¥(x:F)âˆ¥ ^ 2

lemma linearly_dependent_of_is_local_extr_on (hT : is_self_adjoint (T : F â†’â‚—[â„] F))
  {xâ‚€ : F} (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:F) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  âˆƒ a b : â„, (a, b) â‰  0 âˆ§ a â€¢ xâ‚€ + b â€¢ T xâ‚€ = 0 :=
begin
  have H : is_local_extr_on T.re_apply_inner_self {x : F | âˆ¥xâˆ¥ ^ 2 = âˆ¥xâ‚€âˆ¥ ^ 2} xâ‚€,
  { convert hextr,
    ext x,
    simp [dist_eq_norm] },
  -- find Lagrange multipliers for the function `T.re_apply_inner_self` and the
  -- hypersurface-defining function `Î» x, âˆ¥xâˆ¥ ^ 2`
  obtain âŸ¨a, b, hâ‚, hâ‚‚âŸ© := is_local_extr_on.exists_multipliers_of_has_strict_fderiv_at_1d H
    (has_strict_fderiv_at_norm_sq xâ‚€) (hT.has_strict_fderiv_at_re_apply_inner_self xâ‚€),
  refine âŸ¨a, b, hâ‚, _âŸ©,
  apply (inner_product_space.to_dual_map â„ F).injective,
  simp only [linear_isometry.map_add, linear_isometry.map_smul, linear_isometry.map_zero],
  change a â€¢ innerSL xâ‚€ + b â€¢ innerSL (T xâ‚€) = 0,
  apply smul_right_injective (F â†’L[â„] â„) (two_ne_zero : (2:â„) â‰  0),
  simpa only [bit0, add_smul, smul_add, one_smul, add_zero] using hâ‚‚
end

lemma eq_smul_self_of_is_local_extr_on_real (hT : is_self_adjoint (T : F â†’â‚—[â„] F))
  {xâ‚€ : F} (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:F) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  T xâ‚€ = (rayleigh_quotient xâ‚€) â€¢ xâ‚€ :=
begin
  obtain âŸ¨a, b, hâ‚, hâ‚‚âŸ© := hT.linearly_dependent_of_is_local_extr_on hextr,
  by_cases hxâ‚€ : xâ‚€ = 0,
  { simp [hxâ‚€] },
  by_cases hb : b = 0,
  { have : a â‰  0 := by simpa [hb] using hâ‚,
    refine absurd _ hxâ‚€,
    apply smul_right_injective F this,
    simpa [hb] using hâ‚‚ },
  let c : â„ := - bâ»Â¹ * a,
  have hc : T xâ‚€ = c â€¢ xâ‚€,
  { have : b * (bâ»Â¹ * a) = a := by field_simp [mul_comm],
    apply smul_right_injective F hb,
    simp [c, â† neg_eq_of_add_eq_zero hâ‚‚, â† mul_smul, this] },
  convert hc,
  have : âˆ¥xâ‚€âˆ¥ â‰  0 := by simp [hxâ‚€],
  field_simp,
  simpa [inner_smul_left, real_inner_self_eq_norm_mul_norm, sq] using congr_arg (Î» x, âŸªx, xâ‚€âŸ«_â„) hc,
end

end real

section complete_space
variables [complete_space E] {T : E â†’L[ğ•œ] E}
local notation `rayleigh_quotient` := Î» x : E, T.re_apply_inner_self x / âˆ¥(x:E)âˆ¥ ^ 2

lemma eq_smul_self_of_is_local_extr_on (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) {xâ‚€ : E}
  (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:E) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  T xâ‚€ = (â†‘(rayleigh_quotient xâ‚€) : ğ•œ) â€¢ xâ‚€ :=
begin
  letI := inner_product_space.is_R_or_C_to_real ğ•œ E,
  let S : E â†’L[â„] E :=
    @continuous_linear_map.restrict_scalars ğ•œ E E _ _ _ _ _ _ _ â„ _ _ _ _ T,
  have hSA : is_self_adjoint (S : E â†’â‚—[â„] E) := Î» x y, by
  { have := hT x y,
    simp only [continuous_linear_map.coe_coe] at this,
    simp only [real_inner_eq_re_inner, this, continuous_linear_map.coe_restrict_scalars,
      continuous_linear_map.coe_coe, linear_map.coe_restrict_scalars_eq_coe] },
  exact eq_smul_self_of_is_local_extr_on_real hSA hextr,
end

/-- For a self-adjoint operator `T`, a local extremum of the Rayleigh quotient of `T` on a sphere
centred at the origin is an eigenvector of `T`. -/
lemma has_eigenvector_of_is_local_extr_on (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) {xâ‚€ : E}
  (hxâ‚€ : xâ‚€ â‰  0) (hextr : is_local_extr_on T.re_apply_inner_self (sphere (0:E) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  has_eigenvector (T : E â†’â‚—[ğ•œ] E) â†‘(rayleigh_quotient xâ‚€) xâ‚€ :=
begin
  refine âŸ¨_, hxâ‚€âŸ©,
  rw module.End.mem_eigenspace_iff,
  exact hT.eq_smul_self_of_is_local_extr_on hextr
end

/-- For a self-adjoint operator `T`, a maximum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global supremum of the Rayleigh
quotient. -/
lemma has_eigenvector_of_is_max_on (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) {xâ‚€ : E}
  (hxâ‚€ : xâ‚€ â‰  0) (hextr : is_max_on T.re_apply_inner_self (sphere (0:E) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  has_eigenvector (T : E â†’â‚—[ğ•œ] E) â†‘(â¨† x : {x : E // x â‰  0}, rayleigh_quotient x) xâ‚€ :=
begin
  convert hT.has_eigenvector_of_is_local_extr_on hxâ‚€ (or.inr hextr.localize),
  have hxâ‚€' : 0 < âˆ¥xâ‚€âˆ¥ := by simp [hxâ‚€],
  have hxâ‚€'' : xâ‚€ âˆˆ sphere (0:E) (âˆ¥xâ‚€âˆ¥) := by simp,
  rw T.supr_rayleigh_eq_supr_rayleigh_sphere hxâ‚€',
  refine is_max_on.supr_eq hxâ‚€'' _,
  intros x hx,
  dsimp,
  have : âˆ¥xâˆ¥ = âˆ¥xâ‚€âˆ¥ := by simpa using hx,
  rw this,
  exact div_le_div_of_le (sq_nonneg âˆ¥xâ‚€âˆ¥) (hextr hx)
end

/-- For a self-adjoint operator `T`, a minimum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global infimum of the Rayleigh
quotient. -/
lemma has_eigenvector_of_is_min_on (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E)) {xâ‚€ : E}
  (hxâ‚€ : xâ‚€ â‰  0) (hextr : is_min_on T.re_apply_inner_self (sphere (0:E) âˆ¥xâ‚€âˆ¥) xâ‚€) :
  has_eigenvector (T : E â†’â‚—[ğ•œ] E) â†‘(â¨… x : {x : E // x â‰  0}, rayleigh_quotient x) xâ‚€ :=
begin
  convert hT.has_eigenvector_of_is_local_extr_on hxâ‚€ (or.inl hextr.localize),
  have hxâ‚€' : 0 < âˆ¥xâ‚€âˆ¥ := by simp [hxâ‚€],
  have hxâ‚€'' : xâ‚€ âˆˆ sphere (0:E) (âˆ¥xâ‚€âˆ¥) := by simp,
  rw T.infi_rayleigh_eq_infi_rayleigh_sphere hxâ‚€',
  refine is_min_on.infi_eq hxâ‚€'' _,
  intros x hx,
  dsimp,
  have : âˆ¥xâˆ¥ = âˆ¥xâ‚€âˆ¥ := by simpa using hx,
  rw this,
  exact div_le_div_of_le (sq_nonneg âˆ¥xâ‚€âˆ¥) (hextr hx)
end

end complete_space

section compact
variables {T : E â†’L[ğ•œ] E}

local notation `rayleigh_quotient` := Î» x : E, T.re_apply_inner_self x / âˆ¥(x:E)âˆ¥ ^ 2
local notation `rayleigh_quotient_sphere` :=
Î» x : sphere (0:E) 1, T.re_apply_inner_self x / âˆ¥(x:E)âˆ¥ ^ 2
local notation `u_sph` := sphere (0:E) 1
lemma exists_eigenvalue_of_compact_aux [nontrivial E]
  (hT_cpct : compact_map T) (h_pos_case : âˆ¥Tâˆ¥ = (â¨† x : sphere (0:E) 1, rayleigh_quotient x)) :
  âˆƒ c, has_eigenvalue (T : E â†’â‚—[ğ•œ] E) c :=
begin
  haveI : nonempty (sphere (0:E) 1) := continuous_linear_map.sphere_nonempty zero_le_one,
  by_cases h_triv : T = 0,
  { rcases exists_ne (0 : E) with âŸ¨w, hwâŸ©,
    refine âŸ¨0, has_eigenvalue_of_has_eigenvector âŸ¨_, hwâŸ©âŸ©,
    simp only [mem_eigenspace_iff, h_triv, zero_smul, continuous_linear_map.to_linear_map_eq_coe,
      continuous_linear_map.coe_zero, linear_map.zero_apply] },
  { change T â‰  0 at h_triv,
    have nT_ne_zero : âˆ¥Tâˆ¥ â‰  0 := norm_ne_zero_iff.mpr h_triv,
    refine âŸ¨âˆ¥Tâˆ¥, _âŸ©,
    set lâ‚ : filter u_sph :=
      filter.comap rayleigh_quotient_sphere (ğ“[set.range rayleigh_quotient_sphere] âˆ¥Tâˆ¥),
    set lâ‚‚ : filter E := lâ‚.map (Î» x : u_sph, T x),
    have h_bdd_range : bdd_above (set.range rayleigh_quotient_sphere) :=
      T.rayleigh_bdd_above_sphere,
    have h_range_nonempty : (set.range rayleigh_quotient_sphere).nonempty,
    { exact set.range_nonempty _ },
    have h_ne_bot : (ğ“[set.range rayleigh_quotient_sphere] âˆ¥Tâˆ¥).ne_bot,
    { simp_rw [h_pos_case],
      exact is_lub.nhds_within_ne_bot (is_lub_csupr h_bdd_range) h_range_nonempty },
    have hlâ‚‚ : lâ‚‚ â‰¤ ğ“Ÿ (closure (T '' u_sph)) := by
    { refine le_trans _ (filter.monotone_principal subset_closure),
      have : T '' u_sph = set.range (Î» x : u_sph, T x),
      { ext, simp only [exists_prop, set.mem_range, set.mem_image, set_coe.exists, subtype.coe_mk]},
      rw [this, filter.map_le_iff_le_comap, filter.comap_principal],
      simp only [le_top, filter.principal_univ, set.preimage_range] },
    haveI : lâ‚.ne_bot := filter.ne_bot.comap_of_range_mem h_ne_bot self_mem_nhds_within,
    -- The image of T on the sphere is compact since T is a compact operator
    have h_img_cpct : is_compact (closure (T '' u_sph)) := hT_cpct _ bounded_sphere,
    -- lâ‚‚ is guaranteed to have a cluster point z for some vector z by compactness of T
    have hlâ‚‚' := h_img_cpct hlâ‚‚,
    rcases hlâ‚‚' with âŸ¨z, âŸ¨hzâ‚, hzâ‚‚âŸ©âŸ©,
    set lâ‚sub : filter u_sph := lâ‚ âŠ“ (ğ“ z âŠ“ lâ‚‚).comap (Î» x, T x),
    haveI : lâ‚sub.ne_bot,
    { simp only [â†filter.map_ne_bot_iff (Î» x : u_sph, T x), lâ‚sub, filter.push_pull, â†lâ‚‚, inf_comm],
      simp_rw [â†inf_assoc, inf_idem],
      rw [inf_comm],
      exact hzâ‚‚.ne_bot },
    have h_premainâ‚‚ : lâ‚sub.tendsto (Î» y, is_R_or_C.re (âŸªT y, yâŸ«)) (ğ“ âˆ¥Tâˆ¥),
    { simp_rw [â†T.rayleigh_sphere_eq],
      calc lâ‚sub.map rayleigh_quotient_sphere
            â‰¤ lâ‚.map rayleigh_quotient_sphere
            : filter.map_mono inf_le_left
        ... = (ğ“[set.range rayleigh_quotient_sphere] (âˆ¥Tâˆ¥)) âŠ“ ğ“Ÿ (set.range rayleigh_quotient_sphere)
            : filter.map_comap _ _
        ... â‰¤ (ğ“[set.range rayleigh_quotient_sphere] (âˆ¥Tâˆ¥))
            : inf_le_left
        ... â‰¤ ğ“ (âˆ¥Tâˆ¥)   : nhds_within_le_nhds },
    have h_premain : lâ‚sub.tendsto (Î» y, T y) (ğ“ z),
    { refine filter.tendsto.mono_left _ inf_le_right,
      simp only [filter.tendsto, filter.map_comap, inf_assoc, inf_le_left] },
    have h_main : lâ‚sub.tendsto (Î» y : u_sph, (âˆ¥Tâˆ¥ : ğ•œ) â€¢ (y : E)) (ğ“ z),
    { refine tendsto_of_tendsto_of_dist h_premain _,
      simp only [dist_eq_norm],
      have hâ‚â‚‚ : (Î» x : u_sph, âˆ¥T x - (âˆ¥Tâˆ¥ : ğ•œ) â€¢ xâˆ¥) =
        (Î» x : u_sph, real.sqrt (âˆ¥T x - (âˆ¥Tâˆ¥ : ğ•œ) â€¢ xâˆ¥ ^ 2)),
      { simp_rw [real.sqrt_sq (norm_nonneg _)] },
      rw [hâ‚â‚‚, â†real.sqrt_zero],
      refine filter.tendsto.sqrt _,
      -- Main calculation from Einsiedler-Ward
      have h_squeeze : âˆ€ y : u_sph,
        âˆ¥T y - (âˆ¥Tâˆ¥ : ğ•œ) â€¢ yâˆ¥ ^ 2 â‰¤ 2 * âˆ¥Tâˆ¥^2 - 2 * âˆ¥Tâˆ¥ * is_R_or_C.re (âŸªT y, yâŸ«),
      { intros y,
        calc âˆ¥T y - (âˆ¥Tâˆ¥ : ğ•œ) â€¢ yâˆ¥ ^ 2 =
               âˆ¥T yâˆ¥^2 - 2 * âˆ¥Tâˆ¥ * is_R_or_C.re (âŸªT y, yâŸ«) + âˆ¥(âˆ¥Tâˆ¥ : ğ•œ) â€¢ (y : E)âˆ¥^2
                  : by { simp_rw [norm_sub_sq, norm_smul, inner_smul_right,
                                  is_R_or_C.of_real_mul_re], ring }
           ... â‰¤ âˆ¥T yâˆ¥^2 - 2 * âˆ¥Tâˆ¥ * is_R_or_C.re (âŸªT y, yâŸ«) + âˆ¥Tâˆ¥ ^ 2
                  : by { refine add_le_add_left _ _, simp [norm_smul] }
           ... â‰¤ âˆ¥Tâˆ¥^2 - 2 * âˆ¥Tâˆ¥ * is_R_or_C.re (âŸªT y, yâŸ«) + âˆ¥Tâˆ¥ ^ 2
                  : begin
                      refine add_le_add_right _ _,
                      refine sub_le_sub_right _ _,
                      refine pow_le_pow_of_le_left (norm_nonneg _) _ 2,
                      calc âˆ¥T yâˆ¥ â‰¤ âˆ¥Tâˆ¥ * âˆ¥(y : E)âˆ¥    : T.le_op_norm _
                           ...   = âˆ¥Tâˆ¥                : by rw [norm_eq_of_mem_sphere y, mul_one]
                    end
           ... â‰¤ _
                  : by ring_nf },
      refine squeeze_zero (Î» y, pow_two_nonneg _) h_squeeze _,
      have h_bs : 2 * âˆ¥Tâˆ¥^2 - 2 * âˆ¥Tâˆ¥^2 = 0 := by ring,
      rw [â†h_bs],
      refine filter.tendsto.const_sub _ _,
      rw [pow_two, â†mul_assoc],
      refine filter.tendsto.const_mul _ h_premainâ‚‚ },
    have hz_norm : âˆ¥zâˆ¥ = âˆ¥Tâˆ¥,
    { have := h_main.norm,
      have h_smul : (Î» y : u_sph, âˆ¥(âˆ¥Tâˆ¥ : ğ•œ) â€¢ (y : E)âˆ¥) = Î» y, âˆ¥Tâˆ¥,
      { ext, simp [norm_smul] },
      simp [h_smul] at this,
      refine eq.symm _,
      refine tendsto_nhds_unique tendsto_const_nhds this },
    have z_ne_zero : z â‰  0,
    { rintro hz_zero,
      rw [hz_zero, norm_zero] at hz_norm,
      exact nT_ne_zero hz_norm.symm },
    let zs : u_sph := âŸ¨(âˆ¥zâˆ¥â»Â¹ : ğ•œ) â€¢ z, by rw [mem_sphere, dist_eq_norm, sub_zero,
                                              norm_smul_inv_norm z_ne_zero]âŸ©,
    have hâ‚‚ : (âˆ¥Tâˆ¥ : ğ•œ) â€¢ (zs : E) = z,
    { have : (âˆ¥zâˆ¥ : ğ•œ) â‰  0 := by { norm_cast, exact norm_ne_zero_iff.mpr z_ne_zero },
      simp only [â†hz_norm, smul_smul, mul_inv_cancel this, one_smul, subtype.coe_mk] },
    have hâ‚ƒ : (zs : E) â‰  0 := ne_zero_of_mem_unit_sphere zs,
    have hzs : (âˆ¥Tâˆ¥â»Â¹ : ğ•œ) â€¢ z = zs,
    { simp only [hz_norm, subtype.coe_mk]},
    have hâ‚„ : lâ‚sub â‰¤ ğ“ zs,
    { have h_main' := filter.tendsto.const_smul h_main (âˆ¥Tâˆ¥â»Â¹ : ğ•œ),
      have a_ne_zero' : (âˆ¥Tâˆ¥ : ğ•œ) â‰  0 := by simp [nT_ne_zero],
      simp only [smul_smul, inv_mul_cancel a_ne_zero', filter.tendsto_iff_comap, hzs, one_smul]
        at h_main',
      convert h_main',
      exact nhds_subtype_eq_comap },
    refine has_eigenvalue_of_has_eigenvector âŸ¨_, hâ‚ƒâŸ©,
    rw [mem_eigenspace_iff, hâ‚‚],
    refine tendsto_nhds_unique _ h_premain,
    refine filter.tendsto.mono_left _ hâ‚„,
    have Tsph_cont : continuous (Î» x : u_sph, T x) :=
      continuous.comp T.continuous continuous_subtype_coe,
    exact Tsph_cont.tendsto _ }
end

-- move this
lemma _root_.inner_product_space.is_self_adjoint.neg {ğ•œ : Type u_1} {E : Type u_2} [is_R_or_C ğ•œ]
  [inner_product_space ğ•œ E] {T : E â†’â‚—[ğ•œ] E} (hT : is_self_adjoint T) :
  is_self_adjoint (-T) :=
begin
  intros x y,
  simpa [inner_neg_left, inner_neg_right] using congr_arg (Î» a, -a) (hT x y),
end

lemma exists_eigenvalue_of_compact [nontrivial E] (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E))
  (hT_cpct : compact_map T) : âˆƒ c, has_eigenvalue (T : E â†’â‚—[ğ•œ] E) c :=
begin
  have Hâ‚ := hT.norm_eq_supr_abs_rayleigh_sphere,
  rw T.supr_abs_rayleigh_eq_sup_supr at Hâ‚,
  rcases max_cases (â¨† (x : â†¥(sphere (0:E) 1)), (Î» (x : E), T.re_apply_inner_self x / âˆ¥xâˆ¥ ^ 2) x)
                    (â¨† (x : â†¥(sphere (0:E) 1)), -(Î» (x : E), T.re_apply_inner_self x / âˆ¥xâˆ¥ ^ 2) x)
                    with âŸ¨hâ‚, hâ‚‚âŸ© | âŸ¨hâ‚, hâ‚‚âŸ©,
  { rw hâ‚ at Hâ‚,
    exact exists_eigenvalue_of_compact_aux hT_cpct Hâ‚ },
  { rw hâ‚ at Hâ‚,
    have : is_self_adjoint (â†‘(-T) : E â†’â‚—[ğ•œ] E) := hT.neg,
    obtain âŸ¨c, hcâŸ© := @exists_eigenvalue_of_compact_aux _ _ _ _ (-T) _ hT_cpct.neg _,
    { use -c,
      rw has_eigenvalue at hc âŠ¢,
      convert hc using 1,
      ext x,
      simp [mem_eigenspace_iff, neg_eq_iff_add_eq_zero, eq_neg_iff_add_eq_zero] },
    convert Hâ‚ using 1,
    { simp },
    congr,
    ext x,
    simp [continuous_linear_map.re_apply_inner_self] },
end

lemma subsingleton_of_no_eigenvalue_of_compact (hT : is_self_adjoint (T : E â†’â‚—[ğ•œ] E))
  (hT_cpct : compact_map T) (hT' : âˆ€ Î¼ : ğ•œ, module.End.eigenspace (T : E â†’â‚—[ğ•œ] E) Î¼ = âŠ¥) :
  subsingleton E :=
(subsingleton_or_nontrivial E).resolve_right
  (Î» h, by exactI absurd (hT' _) (hT.exists_eigenvalue_of_compact hT_cpct).some_spec)

end compact


section finite_dimensional
variables [finite_dimensional ğ•œ E] [_i : nontrivial E] {T : E â†’â‚—[ğ•œ] E}

include _i

/-- The supremum of the Rayleigh quotient of a self-adjoint operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
lemma has_eigenvalue_supr_of_finite_dimensional (hT : is_self_adjoint T) :
  has_eigenvalue T â†‘(â¨† x : {x : E // x â‰  0}, is_R_or_C.re âŸªT x, xâŸ« / âˆ¥(x:E)âˆ¥ ^ 2) :=
begin
  haveI := finite_dimensional.proper_is_R_or_C ğ•œ E,
  let T' : E â†’L[ğ•œ] E := T.to_continuous_linear_map,
  have hT' : is_self_adjoint (T' : E â†’â‚—[ğ•œ] E) := hT,
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0,
  have Hâ‚ : is_compact (sphere (0:E) âˆ¥xâˆ¥) := is_compact_sphere _ _,
  have Hâ‚‚ : (sphere (0:E) âˆ¥xâˆ¥).nonempty := âŸ¨x, by simpâŸ©,
  -- key point: in finite dimension, a continuous function on the sphere has a max
  obtain âŸ¨xâ‚€, hxâ‚€', hTxâ‚€âŸ© :=
    Hâ‚.exists_forall_ge Hâ‚‚ T'.re_apply_inner_self_continuous.continuous_on,
  have hxâ‚€ : âˆ¥xâ‚€âˆ¥ = âˆ¥xâˆ¥ := by simpa using hxâ‚€',
  have : is_max_on T'.re_apply_inner_self (sphere 0 âˆ¥xâ‚€âˆ¥) xâ‚€,
  { simpa only [â† hxâ‚€] using hTxâ‚€ },
  have hxâ‚€_ne : xâ‚€ â‰  0,
  { have : âˆ¥xâ‚€âˆ¥ â‰  0 := by simp only [hxâ‚€, norm_eq_zero, hx, ne.def, not_false_iff],
    simpa [â† norm_eq_zero, ne.def] },
  exact has_eigenvalue_of_has_eigenvector (hT'.has_eigenvector_of_is_max_on hxâ‚€_ne this)
end

/-- The infimum of the Rayleigh quotient of a self-adjoint operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
lemma has_eigenvalue_infi_of_finite_dimensional (hT : is_self_adjoint T) :
  has_eigenvalue T â†‘(â¨… x : {x : E // x â‰  0}, is_R_or_C.re âŸªT x, xâŸ« / âˆ¥(x:E)âˆ¥ ^ 2) :=
begin
  haveI := finite_dimensional.proper_is_R_or_C ğ•œ E,
  let T' : E â†’L[ğ•œ] E := T.to_continuous_linear_map,
  have hT' : is_self_adjoint (T' : E â†’â‚—[ğ•œ] E) := hT,
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0,
  have Hâ‚ : is_compact (sphere (0:E) âˆ¥xâˆ¥) := is_compact_sphere _ _,
  have Hâ‚‚ : (sphere (0:E) âˆ¥xâˆ¥).nonempty := âŸ¨x, by simpâŸ©,
  -- key point: in finite dimension, a continuous function on the sphere has a min
  obtain âŸ¨xâ‚€, hxâ‚€', hTxâ‚€âŸ© :=
    Hâ‚.exists_forall_le Hâ‚‚ T'.re_apply_inner_self_continuous.continuous_on,
  have hxâ‚€ : âˆ¥xâ‚€âˆ¥ = âˆ¥xâˆ¥ := by simpa using hxâ‚€',
  have : is_min_on T'.re_apply_inner_self (sphere 0 âˆ¥xâ‚€âˆ¥) xâ‚€,
  { simpa only [â† hxâ‚€] using hTxâ‚€ },
  have hxâ‚€_ne : xâ‚€ â‰  0,
  { have : âˆ¥xâ‚€âˆ¥ â‰  0 := by simp only [hxâ‚€, norm_eq_zero, hx, ne.def, not_false_iff],
    simpa [â† norm_eq_zero, ne.def] },
  exact has_eigenvalue_of_has_eigenvector (hT'.has_eigenvector_of_is_min_on hxâ‚€_ne this)
end

omit _i

lemma subsingleton_of_no_eigenvalue_finite_dimensional
  (hT : is_self_adjoint T) (hT' : âˆ€ Î¼ : ğ•œ, module.End.eigenspace (T : E â†’â‚—[ğ•œ] E) Î¼ = âŠ¥) :
  subsingleton E :=
(subsingleton_or_nontrivial E).resolve_right
  (Î» h, by exactI absurd (hT' _) hT.has_eigenvalue_supr_of_finite_dimensional)

end finite_dimensional

end is_self_adjoint
end inner_product_space
