/-
Copyright (c) 2022 Shing Tak Lam. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Shing Tak Lam
-/

import analysis.inner_product_space.projection
import analysis.inner_product_space.positive

/-!
# Reproducing Kernel Hilbert Space

A Hilbert space `H` is called a reproducing kernel Hilbert space (RKHS) if it is a vector subspace
of `X â†’ V` where `V` is a Hilbert space, and the evaluation map `H â†’ V` at each `X` is continuous.

## Main definitions

* `rkhs ğ•œ X V H` - `H` is a RKHS, where `ğ•œ` is the scalar field, `X` is the domain, `V` is the
  Hilbert space.
* `rkhs.kernel` - the kernel of a RKHS, as a continuous linear map `V â†’L[ğ•œ] V`
* `rkhs.eval'` - For `V = ğ•œ`, by the Riesz representation theorem, the evaluation map can be
  represented as the inner product with a point in `H`, which we call the `eval'`
* `rkhs.scalar_kernel` - For `V = ğ•œ`, we can represent the kernel of `H` as an inner product with a
  point in `H`, which we call the `scalar_kernel`
* `rkhs.eval'_span` - The span of the `eval'`s as a subspace of `H`.

## Main statements

* `rkhs.eval'_span_topological_closure_eq_top` - the topological closude of the span of the
  `eval's` is the whole space `H`.
* `rkhs.tendsto_nhds_apply` - Convergence in `H` implies pointwise convergence.

## Notes

Due to the statement of the Riesz representation theorem in mathlib, the order of `x` and `y` in
the definition of the `scalar_kernel` and the representation of it as an inner product of
`eval'`s is reversed. I opted to keep the statement simpler, but this leaves the order
reversed.

## Tags

Reproducing kernel Hilbert space, RKHS
-/

noncomputable theory

open_locale inner_product topological_space
open inner_product_space filter

variables (ğ•œ : Type*) [is_R_or_C ğ•œ]
variables (X : Type*)
variables (V : Type*) [inner_product_space ğ•œ V]

/--
A reproducing kernel Hilbert space (RKHS) is a vector subspace of `X â†’ V` where evaluation at a
point is continuous.
-/
class rkhs (H : Type*) [inner_product_space ğ•œ H] extends fun_like H X (Î» _, V) :=
(add_apply' : âˆ€ {f g : H} {x : X}, (f + g) x = f x + g x)
(smul_apply' : âˆ€ {c : ğ•œ} {f : H} {x : X}, (c â€¢ f) x = c â€¢ f x)
(continuous_eval' : âˆ€ (x : X), continuous (Î» (f : H), f x))

attribute [nolint dangerous_instance] rkhs.to_fun_like

namespace rkhs

variables {ğ•œ X V}
variables {H : Type*} [inner_product_space ğ•œ H] [hHrkhs : rkhs ğ•œ X V H]

include hHrkhs

@[simp]
lemma add_apply (f g : H) (x : X) : (f + g) x = f x + g x := rkhs.add_apply'

@[simp]
lemma smul_apply (c : ğ•œ) (f : H) (x : X) : (c â€¢ f) x = c â€¢ f x := rkhs.smul_apply'

@[simp]
lemma zero_apply (x : X) : (0 : H) x = 0 := by rw [â†zero_smul ğ•œ (0 : H), smul_apply, zero_smul]

section defs

variables (ğ•œ V H)

/--
The embedding of an element `f : H` of an RKHS into `X â†’ V`
-/
def to_pi : H â†’â‚—[ğ•œ] (X â†’ V) :=
{ to_fun := Î» f, f,
  map_add' := Î» f g, funext $ add_apply f g,
  map_smul' := Î» c f, funext $ smul_apply c f }

/--
Evaluation at a point `x` as a bundled continuous linear map `H â†’L[ğ•œ] V`
-/
def eval (x : X) : H â†’L[ğ•œ] V :=
{ to_fun := Î» f, f x,
  map_add' := Î» f g, add_apply f g x,
  map_smul' := Î» c f, smul_apply c f x,
  cont := continuous_eval' x }

end defs

@[simp]
lemma to_pi_apply (f : H) (x : X) : to_pi ğ•œ V H f x = f x := rfl

@[simp]
lemma eval_apply (f : H) (x : X) : eval ğ•œ V H x f = f x := rfl

lemma to_pi_inj : function.injective (to_pi ğ•œ V H : H â†’ (X â†’ V)) :=
Î» f g h, fun_like.coe_injective $ funext $ congr_fun h

section complete_space

variables (ğ•œ V H) [complete_space V] [complete_space H]

/--
The kernel of an RKHS, as a continuous linear function `V â†’L[ğ•œ] V`
-/
def kernel (x y : X) : V â†’L[ğ•œ] V := (eval ğ•œ V H x).comp $ (eval ğ•œ V H y)â€ 

variables {ğ•œ V H}

lemma kernel_def (x y : X) : kernel ğ•œ V H x y = (eval ğ•œ V H x).comp ((eval ğ•œ V H y)â€ ) := rfl

end complete_space

/-!
## Scalar functionals

In this section, we focus on the case `V = ğ•œ`.
-/

section scalar

omit hHrkhs

section defs

variables (ğ•œ X)
variables (Hâ‚ : Type*) [inner_product_space ğ•œ Hâ‚] [complete_space Hâ‚] [hHâ‚rkhs : rkhs ğ•œ X ğ•œ Hâ‚]

include hHâ‚rkhs

/--
Evaluation at a point `x`, represented as a point `eval' x` in the RKHS `Hâ‚` by the Riesz
representation theorem.
-/
def eval' (x : X) : Hâ‚ := (to_dual ğ•œ Hâ‚).symm (eval ğ•œ ğ•œ Hâ‚ x)

/--
The kernel of an RKHS, represented as a scalar value.
-/
def scalar_kernel (x y : X) : ğ•œ := (to_dual ğ•œ ğ•œ).symm (kernel ğ•œ ğ•œ Hâ‚ x y)

/--
The span of all points of the form `eval' x` as a subspace of `Hâ‚`.
-/
def eval'_span : submodule ğ•œ Hâ‚ := submodule.span ğ•œ $ set.range (eval' ğ•œ X Hâ‚)

end defs

variables {Hâ‚ : Type*} [inner_product_space ğ•œ Hâ‚] [complete_space Hâ‚] [hHâ‚rkhs : rkhs ğ•œ X ğ•œ Hâ‚]
include hHâ‚rkhs

lemma eval'_def (x : X) : eval' ğ•œ X Hâ‚ x = (to_dual ğ•œ Hâ‚).symm (eval ğ•œ ğ•œ Hâ‚ x) := rfl

lemma inner_eval' (f : Hâ‚) (x : X) : inner (eval' ğ•œ X Hâ‚ x) f = f x :=
by rw [eval'_def, to_dual_symm_apply, eval_apply]

lemma eval'_eq_eval_adjoint (x : X) : eval' ğ•œ X Hâ‚ x = (eval ğ•œ ğ•œ Hâ‚ x)â€  1 :=
begin
  apply (to_dual ğ•œ Hâ‚).injective,
  ext f,
  simp [continuous_linear_map.adjoint_apply, eval'_def],
end

lemma scalar_kernel_def (x y : X) :
  scalar_kernel ğ•œ X Hâ‚ x y = (to_dual ğ•œ ğ•œ).symm (kernel ğ•œ ğ•œ Hâ‚ x y) := rfl

lemma scalar_kernel_eq_inner (x y : X) :
  scalar_kernel ğ•œ X Hâ‚ x y = inner (eval' ğ•œ X Hâ‚ y) (eval' ğ•œ X Hâ‚ x) :=
begin
  apply (to_dual ğ•œ ğ•œ).injective,
  rw [scalar_kernel_def, linear_isometry_equiv.apply_symm_apply, kernel_def],
  ext,
  rw [continuous_linear_map.comp_apply, â†eval'_eq_eval_adjoint],
  simp only [eval_apply, to_dual_apply, is_R_or_C.inner_apply, inner_conj_sym, mul_one],
  rw inner_eval',
end

lemma kernel_conj_symm {x y : X} :
  scalar_kernel ğ•œ X Hâ‚ x y = star_ring_end ğ•œ (scalar_kernel ğ•œ X Hâ‚ y x) :=
by rw [scalar_kernel_eq_inner, scalar_kernel_eq_inner, inner_conj_sym]

lemma norm_eval'_eq_kernel {x : X} :
  âˆ¥ eval' ğ•œ X Hâ‚ x âˆ¥^2 = is_R_or_C.re (scalar_kernel ğ•œ X Hâ‚ x x) :=
by rw [â†inner_self_eq_norm_sq, scalar_kernel_eq_inner]

lemma norm_eval_eq_kernel {x : X} :
  âˆ¥ eval ğ•œ ğ•œ Hâ‚ x âˆ¥^2 = is_R_or_C.re (scalar_kernel ğ•œ X Hâ‚ x x) :=
by rw [â†norm_eval'_eq_kernel, eval', linear_isometry_equiv.norm_map]

/--
The span of the `eval'`s is dense in H
-/
lemma eval'_span_topological_closure_eq_top :
  (eval'_span ğ•œ X Hâ‚).topological_closure = âŠ¤ :=
begin
  rw [submodule.topological_closure_eq_top_iff, submodule.eq_bot_iff],
  intros f hf,
  apply fun_like.coe_injective,
  funext x,
  specialize hf (eval' ğ•œ X Hâ‚ x) _,
  rw [eval'_span],
  apply submodule.subset_span,
  use x,
  rw inner_eval' at hf,
  rw [hf, zero_apply],
end

/--
If `f n` tendsto `F` in `H`, then `f n` tendsto `F` pointwise.
-/
lemma tendsto_nhds_apply (f : â„• â†’ Hâ‚) (F : Hâ‚) (hf : tendsto f at_top (ğ“ F))
  (x : X) : tendsto (Î» n, f n x) at_top (ğ“ (F x)) :=
begin
  -- two cases, depending on whether `f x = 0` for all `f` in `H`.
  by_cases hker : eval' ğ•œ X Hâ‚ x = 0,
  { convert tendsto_const_nhds,
    funext n,
    rw [â†inner_eval', â†inner_eval', hker, inner_zero_left, inner_zero_left] },
  { rw [metric.tendsto_nhds] at âŠ¢ hf,
    intros Îµ hÎµ,
    let Îµ' := Îµ / âˆ¥ eval' ğ•œ X Hâ‚ x âˆ¥,
    have hÎµ' : 0 < Îµ',
    { apply div_pos hÎµ,
      rw norm_pos_iff,
      exact hker },
    specialize hf Îµ' hÎµ',
    rw filter.eventually_at_top at âŠ¢ hf,
    rcases hf with âŸ¨N, hNâŸ©,
    use N,
    intros n hn,
    specialize hN n hn,
    rw [dist_eq_norm, â†inner_eval', â†inner_eval', â†inner_sub_right],
    refine lt_of_le_of_lt (norm_inner_le_norm _ _) _,
    rw dist_eq_norm at hN,
    rw [mul_comm, â†lt_div_iff],
    { exact hN },
    { rw norm_pos_iff,
      exact hker } }
end

end scalar

end rkhs
