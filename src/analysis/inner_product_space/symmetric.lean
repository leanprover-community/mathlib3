/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
import analysis.inner_product_space.basic
import analysis.normed_space.banach
import linear_algebra.sesquilinear_form

/-!
# Symmetric linear maps in an inner product space

This file defines and proves basic theorems about symmetric **not necessarily bounded** operators
on an inner product space, i.e linear maps `T : E â†’ E` such that `âˆ€ x y, âŸªT x, yâŸ« = âŸªx, T yâŸ«`.

In comparison to `is_self_adjoint`, this definition works for non-continuous linear maps, and
doesn't rely on the definition of the adjoint, which allows it to be stated in non-complete space.

## Main definitions

* `linear_map.is_symmetric`: a (not necessarily bounded) operator on an inner product space is
symmetric, if for all `x`, `y`, we have `âŸªT x, yâŸ« = âŸªx, T yâŸ«`

## Main statements

* `is_symmetric.continuous`: if a symmetric operator is defined on a complete space, then
  it is automatically continuous.

## Tags

self-adjoint, symmetric
-/

open is_R_or_C
open_locale complex_conjugate

variables {ğ•œ E E' F G : Type*} [is_R_or_C ğ•œ]
variables [inner_product_space ğ•œ E] [inner_product_space ğ•œ F] [inner_product_space ğ•œ G]
variables [inner_product_space â„ E']
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

namespace linear_map

/-! ### Symmetric operators -/

/-- A (not necessarily bounded) operator on an inner product space is symmetric, if for all
`x`, `y`, we have `âŸªT x, yâŸ« = âŸªx, T yâŸ«`. -/
def is_symmetric (T : E â†’â‚—[ğ•œ] E) : Prop := âˆ€ x y, âŸªT x, yâŸ« = âŸªx, T yâŸ«

section real

variables

/-- An operator `T` on an inner product space is symmetric if and only if it is
`linear_map.is_self_adjoint` with respect to the sesquilinear form given by the inner product. -/
lemma is_symmetric_iff_sesq_form (T : E â†’â‚—[ğ•œ] E) :
  T.is_symmetric â†”
  @linear_map.is_self_adjoint ğ•œ E _ _ _ (star_ring_end ğ•œ) sesq_form_of_inner T :=
âŸ¨Î» h x y, (h y x).symm, Î» h x y, (h y x).symmâŸ©

end real

lemma is_symmetric.conj_inner_sym {T : E â†’â‚—[ğ•œ] E} (hT : is_symmetric T) (x y : E) :
  conj âŸªT x, yâŸ« = âŸªT y, xâŸ« :=
by rw [hT x y, inner_conj_sym]

@[simp] lemma is_symmetric.apply_clm {T : E â†’L[ğ•œ] E} (hT : is_symmetric (T : E â†’â‚—[ğ•œ] E))
  (x y : E) : âŸªT x, yâŸ« = âŸªx, T yâŸ« :=
hT x y

lemma is_symmetric_zero : (0 : E â†’â‚—[ğ•œ] E).is_symmetric :=
Î» x y, (inner_zero_right : âŸªx, 0âŸ« = 0).symm â–¸ (inner_zero_left : âŸª0, yâŸ« = 0)

lemma is_symmetric_id : (linear_map.id : E â†’â‚—[ğ•œ] E).is_symmetric :=
Î» x y, rfl

lemma is_symmetric.add {T S : E â†’â‚—[ğ•œ] E} (hT : T.is_symmetric) (hS : S.is_symmetric) :
  (T + S).is_symmetric :=
begin
  intros x y,
  rw [linear_map.add_apply, inner_add_left, hT x y, hS x y, â† inner_add_right],
  refl
end

/-- The **Hellinger--Toeplitz theorem**: if a symmetric operator is defined on a complete space,
  then it is automatically continuous. -/
lemma is_symmetric.continuous [complete_space E] {T : E â†’â‚—[ğ•œ] E} (hT : is_symmetric T) :
  continuous T :=
begin
  -- We prove it by using the closed graph theorem
  refine T.continuous_of_seq_closed_graph (Î» u x y hu hTu, _),
  rw [â†sub_eq_zero, â†inner_self_eq_zero],
  have hlhs : âˆ€ k : â„•, âŸªT (u k) - T x, y - T xâŸ« = âŸªu k - x, T (y - T x)âŸ« :=
  by { intro k, rw [â†T.map_sub, hT] },
  refine tendsto_nhds_unique ((hTu.sub_const _).inner tendsto_const_nhds) _,
  simp_rw hlhs,
  rw â†@inner_zero_left ğ•œ E _ _ (T (y - T x)),
  refine filter.tendsto.inner _ tendsto_const_nhds,
  rw â†sub_self x,
  exact hu.sub_const _,
end

/-- For a symmetric operator `T`, the function `Î» x, âŸªT x, xâŸ«` is real-valued. -/
@[simp] lemma is_symmetric.coe_re_apply_inner_self_apply
  {T : E â†’L[ğ•œ] E} (hT : is_symmetric (T : E â†’â‚—[ğ•œ] E)) (x : E) :
  (T.re_apply_inner_self x : ğ•œ) = âŸªT x, xâŸ« :=
begin
  rsuffices âŸ¨r, hrâŸ© : âˆƒ r : â„, âŸªT x, xâŸ« = r,
  { simp [hr, T.re_apply_inner_self_apply] },
  rw â† eq_conj_iff_real,
  exact hT.conj_inner_sym x x
end

/-- If a symmetric operator preserves a submodule, its restriction to that submodule is
symmetric. -/
lemma is_symmetric.restrict_invariant {T : E â†’â‚—[ğ•œ] E} (hT : is_symmetric T)
  {V : submodule ğ•œ E} (hV : âˆ€ v âˆˆ V, T v âˆˆ V) :
  is_symmetric (T.restrict hV) :=
Î» v w, hT v w

lemma is_symmetric.restrict_scalars {T : E â†’â‚—[ğ•œ] E} (hT : T.is_symmetric) :
  @linear_map.is_symmetric â„ E _ (inner_product_space.is_R_or_C_to_real ğ•œ E)
  (@linear_map.restrict_scalars â„ ğ•œ _ _ _ _ _ _
    (inner_product_space.is_R_or_C_to_real ğ•œ E).to_module
    (inner_product_space.is_R_or_C_to_real ğ•œ E).to_module _ _ _ T) :=
Î» x y, by simp [hT x y, real_inner_eq_re_inner, linear_map.coe_restrict_scalars_eq_coe]

section complex

variables {V : Type*}
  [inner_product_space â„‚ V]

/-- A linear operator on a complex inner product space is symmetric precisely when
`âŸªT v, vâŸ«_â„‚` is real for all v.-/
lemma is_symmetric_iff_inner_map_self_real (T : V â†’â‚—[â„‚] V):
  is_symmetric T â†” âˆ€ (v : V), conj âŸªT v, vâŸ«_â„‚ = âŸªT v, vâŸ«_â„‚ :=
begin
  split,
  { intros hT v,
    apply is_symmetric.conj_inner_sym hT },
  { intros h x y,
    nth_rewrite 1 â† inner_conj_sym,
    nth_rewrite 1 inner_map_polarization,
    simp only [star_ring_end_apply, star_div', star_sub, star_add, star_mul],
    simp only [â† star_ring_end_apply],
    rw [h (x + y), h (x - y), h (x + complex.I â€¢ y), h (x - complex.I â€¢ y)],
    simp only [complex.conj_I],
    rw inner_map_polarization',
    norm_num,
    ring },
end

end complex

end linear_map
