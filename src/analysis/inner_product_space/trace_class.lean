/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.inner_product_space.l2_space
import analysis.inner_product_space.positive
import linear_algebra.trace

/-!
# Trace-class operators

## Main definitions

* `foo_bar`

## Main statements

* `foo_bar_unique`

## Notation



## Implementation details



## References

* [F. Bar, *Quuxes*][bibkey]

## Tags

Foobars, barfoos
-/

open linear_map filter submodule set inner_product_space is_R_or_C
open_locale topological_space classical big_operators ennreal nnreal inner_product

abbreviation findim_subspace (R E : Type*) [division_ring R] [add_comm_group E] [module R E] :=
{U : submodule R E // finite_dimensional R U}

instance {R E : Type*} [division_ring R] [add_comm_group E]
  [module R E] : semilattice_sup (findim_subspace R E) :=
subtype.semilattice_sup (Î» U V, by introsI hU hV; apply_instance)

instance {R E : Type*} [division_ring R] [add_comm_group E]
  [module R E] : nonempty (findim_subspace R E) :=
âŸ¨âŸ¨âŠ¥, finite_dimensional_bot _ _âŸ©âŸ©

lemma findim_subspace.finite_dimensional {R E : Type*} [division_ring R] [add_comm_group E]
  [module R E] (U : findim_subspace R E) : finite_dimensional R (U : submodule R E) := U.2

local attribute [instance] findim_subspace.finite_dimensional

namespace continuous_linear_map

variables {ğ•œ E F : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ E] [inner_product_space ğ•œ F]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y

--noncomputable def _root_.genial_filter : filter (findim_subspace ğ•œ E) :=
--â¨… x : E, filter.comap
--  (Î» U : findim_subspace ğ•œ E, (orthogonal_projection (U : submodule ğ•œ E) x : E))
--  (ğ“ x)
--
--lemma _root_.tendsto_genial_filter {Î¹ : Type*} {l : filter Î¹} {U : Î¹ â†’ findim_subspace ğ•œ E}

section trace_along

private noncomputable def conj_proj (T : E â†’L[ğ•œ] E) (U : submodule ğ•œ E)
  [complete_space U] : E â†’L[ğ•œ] E :=
(U.subtypeL âˆ˜L orthogonal_projection U âˆ˜L T âˆ˜L U.subtypeL âˆ˜L orthogonal_projection U)

private lemma conj_proj_apply (T : E â†’L[ğ•œ] E) (U : submodule ğ•œ E)
  [complete_space U] (x : E) :
  conj_proj T U x = orthogonal_projection U (T (orthogonal_projection U x)) :=
rfl

noncomputable def trace_along (U : submodule ğ•œ E) [finite_dimensional ğ•œ U] :
  (E â†’L[ğ•œ] E) â†’â‚—[ğ•œ] ğ•œ :=
linear_map.trace ğ•œ U âˆ˜â‚— (coe_lm ğ•œ) âˆ˜â‚—
  (compL ğ•œ U E U (orthogonal_projection U) : (U â†’L[ğ•œ] E) â†’â‚—[ğ•œ] (U â†’L[ğ•œ] U)) âˆ˜â‚—
  ((compL ğ•œ U E E).flip U.subtypeL : (E â†’L[ğ•œ] E) â†’â‚—[ğ•œ] (U â†’L[ğ•œ] E))

@[simp] lemma trace_along_apply (U : submodule ğ•œ E) [finite_dimensional ğ•œ U] (T : E â†’L[ğ•œ] E) :
  trace_along U T = linear_map.trace ğ•œ U (dom_restrict ((orthogonal_projection U).comp T) U) :=
rfl

lemma trace_along_eq_of_orthonormal_basis {Î¹ : Type*} [fintype Î¹]
  {U : submodule ğ•œ E} [finite_dimensional ğ•œ U] (T : E â†’L[ğ•œ] E)
  (e : orthonormal_basis Î¹ ğ•œ (U : submodule ğ•œ E)) :
  trace_along U T = âˆ‘ i, âŸª(e i : E), T (e i)âŸ« :=
begin
  rw [trace_along_apply, trace_eq_sum_of_basis ğ•œ e.to_basis],
  congr,
  ext i,
  rw [basis.coord_apply, e.coe_to_basis_repr_apply, e.coe_to_basis, e.repr_apply_apply,
      coe_inner, dom_restrict_apply, coe_coe, comp_apply,
      â† inner_orthogonal_projection_left_eq_right U,
      orthogonal_projection_eq_self_iff.mpr (subtype.coe_prop $ e i)]
end

lemma has_sum_trace_along_of_hilbert_basis [complete_space E] {Î¹ : Type*}
  (U : submodule ğ•œ E) [finite_dimensional ğ•œ U] (T : E â†’L[ğ•œ] E)
  (e : hilbert_basis Î¹ ğ•œ E) :
  has_sum (Î» i, âŸª(e i : E), (conj_proj T U) (e i)âŸ«) (trace_along U T) :=
begin
  let f := std_orthonormal_basis ğ•œ U,
  rw trace_along_eq_of_orthonormal_basis T f,
  have : âˆ€ j, has_sum (Î» i, âŸª(conj_proj (Tâ€ ) U) (f j : E), e iâŸ« * âŸªe i, f jâŸ«)
    âŸª(conj_proj (Tâ€ ) U) (f j : E), f jâŸ« :=
    Î» j, e.has_sum_inner_mul_inner _ _,
  convert has_sum_sum (Î» j (_ : j âˆˆ finset.univ), this j),
  { ext i,
    rw [conj_proj_apply, â† inner_orthogonal_projection_left_eq_right],
    nth_rewrite 0 â† orthogonal_projection_mem_subspace_eq_self (orthogonal_projection U (e i)),
    rw [inner_orthogonal_projection_left_eq_right, â† coe_inner, â† f.sum_inner_mul_inner],
    congrm âˆ‘ j, _,
    rw [coe_inner, coe_inner, inner_orthogonal_projection_left_eq_right,
        orthogonal_projection_mem_subspace_eq_self, â† inner_orthogonal_projection_left_eq_right,
        â† T.adjoint_inner_left, â† inner_orthogonal_projection_left_eq_right, mul_comm],
    refl },
  { ext j,
    change _ = âŸªorthogonal_projection U (Tâ€  (orthogonal_projection U $ f j)), _âŸ«,
    rw [coe_inner, inner_orthogonal_projection_left_eq_right, T.adjoint_inner_left,
        orthogonal_projection_mem_subspace_eq_self] }
end

lemma trace_along_span_eq_of_orthonormal [complete_space E] {Î¹ : Type*} (T : E â†’L[ğ•œ] E) {e : Î¹ â†’ E}
  (he : orthonormal ğ•œ e) (s : finset Î¹) :
  trace_along (span ğ•œ (s.image e : set E)) T = âˆ‘ i in s, âŸª(e i : E), T (e i)âŸ« :=
begin
  let e'' := orthonormal_basis.span he s,
  simp_rw [T.trace_along_eq_of_orthonormal_basis e'', orthonormal_basis.span_apply,
            s.sum_coe_sort (Î» i, âŸªe i, T (e i)âŸ«)]
end

lemma trace_along_tendsto_of_pointwise [complete_space E] (U : submodule ğ•œ E)
  [finite_dimensional ğ•œ U] {Î¹ : Type*} {Î¦ : Î¹ â†’ E â†’L[ğ•œ] E} {Ï† : E â†’L[ğ•œ] E} {l : filter Î¹}
  (h : âˆ€ x, tendsto (Î» i, âŸªx, Î¦ i xâŸ«) l (ğ“ $ âŸªx, Ï† xâŸ«)) :
  tendsto (Î» i, trace_along U (Î¦ i)) l (ğ“ $ trace_along U Ï†) :=
begin
  let f := std_orthonormal_basis ğ•œ U,
  simp_rw [trace_along_eq_of_orthonormal_basis _ f],
  exact tendsto_finset_sum _ (Î» j _, h _)
end

end trace_along

section positive

lemma is_positive.trace_along_eq_re [complete_space E] {T : E â†’L[ğ•œ] E} (hT : T.is_positive)
  (U : submodule ğ•œ E) [finite_dimensional ğ•œ U] : trace_along U T = re (trace_along U T) :=
begin
  let e := std_orthonormal_basis ğ•œ U,
  rw [trace_along_eq_of_orthonormal_basis _ e, _root_.map_sum, of_real_sum],
  congr,
  ext i,
  rw [â† coe_coe, â† hT.1],
  exact (hT.1.coe_re_apply_inner_self_apply (e i)).symm
end

lemma is_positive.trace_along_nonneg [complete_space E] {T : E â†’L[ğ•œ] E} (hT : T.is_positive)
  (U : submodule ğ•œ E) [finite_dimensional ğ•œ U] : 0 â‰¤ re (trace_along U T) :=
begin
  let e := std_orthonormal_basis ğ•œ U,
  rw [trace_along_eq_of_orthonormal_basis _ e, _root_.map_sum],
  refine finset.sum_nonneg (Î» i _, _),
  rw [â† coe_coe, â† hT.1],
  exact hT.2 (e i)
end

lemma is_positive.trace_along_conj_proj_le [complete_space E] {T : E â†’L[ğ•œ] E} (hT : T.is_positive)
  (U V : submodule ğ•œ E) [finite_dimensional ğ•œ U] [finite_dimensional ğ•œ V] :
    re (trace_along U (conj_proj T V)) â‰¤
    re (trace_along V T) :=
begin
  have := U.is_hilbert_sum_orthogonal,
  let e := is_hilbert_sum.collected_hilbert_basis this
    (Î» b, std_hilbert_basis ğ•œ ((cond b U Uá—® : submodule ğ•œ E) : Type*)),
  have keyâ‚ := re_clm.has_sum ((conj_proj T V).has_sum_trace_along_of_hilbert_basis U e),
  have keyâ‚‚ := re_clm.has_sum (T.has_sum_trace_along_of_hilbert_basis V e),
  refine has_sum_le (Î» i, _) keyâ‚ keyâ‚‚,
  simp only [conj_proj, comp_apply, coe_subtypeL', subtype_apply, subtype.coe_mk],
  rcases i with âŸ¨b, iâŸ©,
  cases b,
  { rw [â† inner_orthogonal_projection_left_eq_right,
        is_hilbert_sum.coe_collected_hilbert_basis_mk,
        orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero,
        submodule.coe_zero, inner_zero_left, _root_.map_zero],
    { exact (hT.conj_orthogonal_projection V).inner_nonneg_right _ },
    { exact submodule.coe_mem _ } },
  { rw [â† inner_orthogonal_projection_left_eq_right,
        is_hilbert_sum.coe_collected_hilbert_basis_mk,
        orthogonal_projection_eq_self_iff.mpr],
    exact submodule.coe_mem _ }
end

lemma is_positive.monotone_trace_along [complete_space E] {T : E â†’L[ğ•œ] E} (hT : T.is_positive)
  {U V : submodule ğ•œ E} [finite_dimensional ğ•œ U] [finite_dimensional ğ•œ V] (hUV : U â‰¤ V):
    re (trace_along U T) â‰¤
    re (trace_along V T) :=
begin
  convert hT.trace_along_conj_proj_le U V using 2,
  let e := std_orthonormal_basis ğ•œ U,
  rw [trace_along_eq_of_orthonormal_basis _ e, trace_along_eq_of_orthonormal_basis _ e],
  congrm âˆ‘ i, _,
  rw [conj_proj_apply, â† inner_orthogonal_projection_left_eq_right,
      orthogonal_projection_eq_self_iff.mpr (hUV $ submodule.coe_mem _)]
end

noncomputable def is_positive.trace_along_ennreal [complete_space E] (U : submodule ğ•œ E)
  [finite_dimensional ğ•œ U] {T : E â†’L[ğ•œ] E} (hT : T.is_positive) : â„â‰¥0âˆ :=
@coe â„â‰¥0 â„â‰¥0âˆ _ âŸ¨re $ trace_along U T, hT.trace_along_nonneg UâŸ©

lemma is_positive.trace_along_ennreal_conj_proj_le [complete_space E] {T : E â†’L[ğ•œ] E}
  (hT : T.is_positive)
  (U V : submodule ğ•œ E) [finite_dimensional ğ•œ U] [finite_dimensional ğ•œ V] :
    (hT.conj_orthogonal_projection V).trace_along_ennreal U â‰¤
    hT.trace_along_ennreal V :=
begin
  rw [is_positive.trace_along_ennreal, is_positive.trace_along_ennreal, ennreal.coe_le_coe],
  exact hT.trace_along_conj_proj_le _ _
end

noncomputable def is_positive.trace [complete_space E] {T : E â†’L[ğ•œ] E} (hT : T.is_positive) :
  â„â‰¥0âˆ :=
â¨† (U : findim_subspace ğ•œ E), hT.trace_along_ennreal (U : submodule ğ•œ E)

lemma is_positive.trace_eq_supr_of_monotone [complete_space E]
  {T : E â†’L[ğ•œ] E} (hT : T.is_positive) {Ï„ : Type*} [nonempty Ï„] [semilattice_sup Ï„]
  (U : Ï„ â†’ submodule ğ•œ E) [âˆ€ t, finite_dimensional ğ•œ (U t)] (hU : monotone U)
  (hU' : âŠ¤ â‰¤ (â¨† t, U t).topological_closure) :
  hT.trace = â¨† t, hT.trace_along_ennreal (U t) :=
begin
  haveI : âˆ€ t, complete_space (U t) := Î» t, infer_instance,
  refine le_antisymm _ _,
  { refine supr_le (Î» V, _),
    haveI : finite_dimensional ğ•œ (V : submodule ğ•œ E) := V.finite_dimensional,
    suffices : tendsto
      (Î» t, (hT.conj_orthogonal_projection (U t)).trace_along_ennreal V) at_top
      (ğ“ $ hT.trace_along_ennreal V),
    from le_of_tendsto' this
      (Î» t, le_trans (hT.trace_along_ennreal_conj_proj_le _ _) $ le_supr _ t),
    simp_rw [is_positive.trace_along_ennreal, â† ennreal.of_real_eq_coe_nnreal],
    refine ennreal.tendsto_of_real (((continuous_re.tendsto _).comp $
      trace_along_tendsto_of_pointwise _ $ Î» x, _)),
    simp_rw [comp_apply, subtypeL_apply, â† inner_orthogonal_projection_left_eq_right],
    refine tendsto.inner _ ((T.cont.tendsto _).comp _);
    exact orthogonal_projection_tendsto_self _ _ hU _ hU' },
  { exact supr_mono' (Î» t, âŸ¨âŸ¨U t, infer_instanceâŸ©, le_rflâŸ©) }
end

lemma is_positive.has_sum_trace {Î¹ : Type*} [complete_space E] (e : hilbert_basis Î¹ ğ•œ E)
  {T : E â†’L[ğ•œ] E} (hT : T.is_positive) :
  has_sum (Î» i : Î¹, ennreal.of_real (re âŸªe i, T (e i)âŸ«)) hT.trace :=
begin
  have fact : âˆ€ J : finset Î¹, âˆ€ i âˆˆ J, 0 â‰¤ re âŸªe i, T (e i)âŸ« :=
    Î» J i _, hT.inner_nonneg_right (e i),
  rw [ennreal.summable.has_sum_iff, ennreal.tsum_eq_supr_sum,
      hT.trace_eq_supr_of_monotone _ e.partial_span_mono e.partial_span_dense.ge],
  congrm â¨† J, _,
  unfold hilbert_basis.partial_span,
  rw [is_positive.trace_along_ennreal, â† ennreal.of_real_eq_coe_nnreal,
      T.trace_along_span_eq_of_orthonormal e.orthonormal J,
      _root_.map_sum, ennreal.of_real_sum_of_nonneg (fact J)]
end

lemma is_positive.trace_lt_top_iff_summable {Î¹ : Type*} [complete_space E] (e : hilbert_basis Î¹ ğ•œ E)
  {T : E â†’L[ğ•œ] E} (hT : T.is_positive) :
  hT.trace < âŠ¤ â†” summable (Î» i, âŸªe i, T (e i)âŸ«) :=
begin
  rw [lt_top_iff_ne_top, â† (hT.has_sum_trace e).tsum_eq, â† ennreal.tsum_coe_ne_top_iff_summable],
end

end positive

end continuous_linear_map

section

end
