/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import analysis.locally_convex.balanced_core_hull
import analysis.locally_convex.with_seminorms
import analysis.convex.gauge

/-!
# Absolutely convex sets

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A set is called absolutely convex or disked if it is convex and balanced.
The importance of absolutely convex sets comes from the fact that every locally convex
topological vector space has a basis consisting of absolutely convex sets.

## Main definitions

* `gauge_seminorm_family`: the seminorm family induced by all open absolutely convex neighborhoods
of zero.

## Main statements

* `with_gauge_seminorm_family`: the topology of a locally convex space is induced by the family
`gauge_seminorm_family`.

## Todo

* Define the disked hull

## Tags

disks, convex, balanced
-/


open normed_field set
open_locale big_operators nnreal pointwise topology

variables {ğ•œ E F G Î¹ : Type*}

section nontrivially_normed_field

variables (ğ•œ E) {s : set E}

variables [nontrivially_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [module â„ E] [smul_comm_class â„ ğ•œ E]
variables [topological_space E] [locally_convex_space â„ E] [has_continuous_smul ğ•œ E]

lemma nhds_basis_abs_convex : (ğ“ (0 : E)).has_basis
  (Î» (s : set E), s âˆˆ ğ“ (0 : E) âˆ§ balanced ğ•œ s âˆ§ convex â„ s) id :=
begin
  refine (locally_convex_space.convex_basis_zero â„ E).to_has_basis (Î» s hs, _)
    (Î» s hs, âŸ¨s, âŸ¨hs.1, hs.2.2âŸ©, rfl.subsetâŸ©),
  refine âŸ¨convex_hull â„ (balanced_core ğ•œ s), _, convex_hull_min (balanced_core_subset s) hs.2âŸ©,
  refine âŸ¨filter.mem_of_superset (balanced_core_mem_nhds_zero hs.1) (subset_convex_hull â„ _), _âŸ©,
  refine âŸ¨balanced_convex_hull_of_balanced (balanced_core_balanced s), _âŸ©,
  exact convex_convex_hull â„ (balanced_core ğ•œ s),
end

variables [has_continuous_smul â„ E] [topological_add_group E]

lemma nhds_basis_abs_convex_open : (ğ“ (0 : E)).has_basis
  (Î» (s : set E), (0 : E) âˆˆ s âˆ§ is_open s âˆ§ balanced ğ•œ s âˆ§ convex â„ s) id :=
begin
  refine (nhds_basis_abs_convex ğ•œ E).to_has_basis _ _,
  { rintros s âŸ¨hs_nhds, hs_balanced, hs_convexâŸ©,
    refine âŸ¨interior s, _, interior_subsetâŸ©,
    exact âŸ¨mem_interior_iff_mem_nhds.mpr hs_nhds, is_open_interior,
      hs_balanced.interior (mem_interior_iff_mem_nhds.mpr hs_nhds), hs_convex.interiorâŸ© },
  rintros s âŸ¨hs_zero, hs_open, hs_balanced, hs_convexâŸ©,
  exact âŸ¨s, âŸ¨hs_open.mem_nhds hs_zero, hs_balanced, hs_convexâŸ©, rfl.subsetâŸ©,
end

end nontrivially_normed_field

section absolutely_convex_sets

variables [topological_space E] [add_comm_monoid E] [has_zero E] [semi_normed_ring ğ•œ]
variables [has_smul ğ•œ E] [has_smul â„ E]

variables (ğ•œ E)

/-- The type of absolutely convex open sets. -/
def abs_convex_open_sets :=
{ s : set E // (0 : E) âˆˆ s âˆ§ is_open s âˆ§ balanced ğ•œ s âˆ§ convex â„ s }

instance abs_convex_open_sets.has_coe : has_coe (abs_convex_open_sets ğ•œ E) (set E) := âŸ¨subtype.valâŸ©

namespace abs_convex_open_sets

variables {ğ•œ E}

lemma coe_zero_mem (s : abs_convex_open_sets ğ•œ E) : (0 : E) âˆˆ (s : set E) := s.2.1

lemma coe_is_open (s : abs_convex_open_sets ğ•œ E) : is_open (s : set E) := s.2.2.1

lemma coe_nhds (s : abs_convex_open_sets ğ•œ E) : (s : set E) âˆˆ ğ“ (0 : E) :=
s.coe_is_open.mem_nhds s.coe_zero_mem

lemma coe_balanced (s : abs_convex_open_sets ğ•œ E) : balanced ğ•œ (s : set E) := s.2.2.2.1

lemma coe_convex (s : abs_convex_open_sets ğ•œ E) : convex â„ (s : set E) := s.2.2.2.2

end abs_convex_open_sets

instance : nonempty (abs_convex_open_sets ğ•œ E) :=
begin
  rw â†exists_true_iff_nonempty,
  dunfold abs_convex_open_sets,
  rw subtype.exists,
  exact âŸ¨set.univ, âŸ¨mem_univ 0, is_open_univ, balanced_univ, convex_univâŸ©, trivialâŸ©,
end

end absolutely_convex_sets

variables [is_R_or_C ğ•œ]
variables [add_comm_group E] [topological_space E]
variables [module ğ•œ E] [module â„ E] [is_scalar_tower â„ ğ•œ E]
variables [has_continuous_smul â„ E]

variables (ğ•œ E)

/-- The family of seminorms defined by the gauges of absolute convex open sets. -/
noncomputable
def gauge_seminorm_family : seminorm_family ğ•œ E (abs_convex_open_sets ğ•œ E) :=
Î» s, gauge_seminorm s.coe_balanced s.coe_convex (absorbent_nhds_zero s.coe_nhds)

variables {ğ•œ E}

lemma gauge_seminorm_family_ball (s : abs_convex_open_sets ğ•œ E) :
  (gauge_seminorm_family ğ•œ E s).ball 0 1 = (s : set E) :=
begin
  dunfold gauge_seminorm_family,
  rw seminorm.ball_zero_eq,
  simp_rw gauge_seminorm_to_fun,
  exact gauge_lt_one_eq_self_of_open s.coe_convex s.coe_zero_mem s.coe_is_open,
end

variables [topological_add_group E] [has_continuous_smul ğ•œ E]
variables [smul_comm_class â„ ğ•œ E] [locally_convex_space â„ E]

/-- The topology of a locally convex space is induced by the gauge seminorm family. -/
lemma with_gauge_seminorm_family : with_seminorms (gauge_seminorm_family ğ•œ E) :=
begin
  refine seminorm_family.with_seminorms_of_has_basis _ _,
  refine (nhds_basis_abs_convex_open ğ•œ E).to_has_basis (Î» s hs, _) (Î» s hs, _),
  { refine âŸ¨s, âŸ¨_, rfl.subsetâŸ©âŸ©,
    convert (gauge_seminorm_family _ _).basis_sets_singleton_mem âŸ¨s, hsâŸ© one_pos,
    rw [gauge_seminorm_family_ball, subtype.coe_mk] },
  refine âŸ¨s, âŸ¨_, rfl.subsetâŸ©âŸ©,
  rw seminorm_family.basis_sets_iff at hs,
  rcases hs with âŸ¨t, r, hr, rflâŸ©,
  rw [seminorm.ball_finset_sup_eq_Inter _ _ _ hr],
  -- We have to show that the intersection contains zero, is open, balanced, and convex
  refine âŸ¨mem_Interâ‚‚.mpr (Î» _ _, by simp [seminorm.mem_ball_zero, hr]),
    is_open_bInter (to_finite _) (Î» S _, _),
    balanced_Interâ‚‚ (Î» _ _, seminorm.balanced_ball_zero _ _),
    convex_Interâ‚‚ (Î» _ _, seminorm.convex_ball _ _ _)âŸ©,
  -- The only nontrivial part is to show that the ball is open
  have hr' : r = â€–(r : ğ•œ)â€– * 1 := by simp [abs_of_pos hr],
  have hr'' : (r : ğ•œ) â‰  0 := by simp [hr.ne'],
  rw [hr', â† seminorm.smul_ball_zero hr'', gauge_seminorm_family_ball],
  exact S.coe_is_open.smulâ‚€ hr''
end
