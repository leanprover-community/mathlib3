/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import analysis.locally_convex.basic

/-!
# Balanced Core and Balanced Hull

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

## Main definitions

* `balanced_core`: The largest balanced subset of a set `s`.
* `balanced_hull`: The smallest balanced superset of a set `s`.

## Main statements

* `balanced_core_eq_Inter`: Characterization of the balanced core as an intersection over subsets.
* `nhds_basis_closed_balanced`: The closed balanced sets form a basis of the neighborhood filter.

## Implementation details

The balanced core and hull are implemented differently: for the core we take the obvious definition
of the union over all balanced sets that are contained in `s`, whereas for the hull, we take the
union over `r â€¢ s`, for `r` the scalars with `â€–râ€– â‰¤ 1`. We show that `balanced_hull` has the
defining properties of a hull in `balanced.hull_minimal` and `subset_balanced_hull`.
For the core we need slightly stronger assumptions to obtain a characterization as an intersection,
this is `balanced_core_eq_Inter`.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

balanced
-/


open set
open_locale pointwise topology filter


variables {ğ•œ E Î¹ : Type*}

section balanced_hull

section semi_normed_ring
variables [semi_normed_ring ğ•œ]

section has_smul
variables (ğ•œ) [has_smul ğ•œ E] {s t : set E} {x : E}

/-- The largest balanced subset of `s`.-/
def balanced_core (s : set E) := â‹ƒâ‚€ {t : set E | balanced ğ•œ t âˆ§ t âŠ† s}

/-- Helper definition to prove `balanced_core_eq_Inter`-/
def balanced_core_aux (s : set E) := â‹‚ (r : ğ•œ) (hr : 1 â‰¤ â€–râ€–), r â€¢ s

/-- The smallest balanced superset of `s`.-/
def balanced_hull (s : set E) := â‹ƒ (r : ğ•œ) (hr : â€–râ€– â‰¤ 1), r â€¢ s

variables {ğ•œ}

lemma balanced_core_subset (s : set E) : balanced_core ğ•œ s âŠ† s := sUnion_subset $ Î» t ht, ht.2

lemma balanced_core_empty : balanced_core ğ•œ (âˆ… : set E) = âˆ… :=
eq_empty_of_subset_empty (balanced_core_subset _)

lemma mem_balanced_core_iff : x âˆˆ balanced_core ğ•œ s â†” âˆƒ t, balanced ğ•œ t âˆ§ t âŠ† s âˆ§ x âˆˆ t :=
by simp_rw [balanced_core, mem_sUnion, mem_set_of_eq, exists_prop, and_assoc]

lemma smul_balanced_core_subset (s : set E) {a : ğ•œ} (ha : â€–aâ€– â‰¤ 1) :
  a â€¢ balanced_core ğ•œ s âŠ† balanced_core ğ•œ s :=
begin
  rintro x âŸ¨y, hy, rflâŸ©,
  rw mem_balanced_core_iff at hy,
  rcases hy with âŸ¨t, ht1, ht2, hyâŸ©,
  exact âŸ¨t, âŸ¨ht1, ht2âŸ©, ht1 a ha (smul_mem_smul_set hy)âŸ©,
end

lemma balanced_core_balanced (s : set E) : balanced ğ•œ (balanced_core ğ•œ s) :=
Î» _, smul_balanced_core_subset s

/-- The balanced core of `t` is maximal in the sense that it contains any balanced subset
`s` of `t`.-/
lemma balanced.subset_core_of_subset (hs : balanced ğ•œ s) (h : s âŠ† t) : s âŠ† balanced_core ğ•œ t :=
subset_sUnion_of_mem âŸ¨hs, hâŸ©

lemma mem_balanced_core_aux_iff : x âˆˆ balanced_core_aux ğ•œ s â†” âˆ€ r : ğ•œ, 1 â‰¤ â€–râ€– â†’ x âˆˆ r â€¢ s :=
mem_Interâ‚‚

lemma mem_balanced_hull_iff : x âˆˆ balanced_hull ğ•œ s â†” âˆƒ (r : ğ•œ) (hr : â€–râ€– â‰¤ 1), x âˆˆ r â€¢ s :=
mem_Unionâ‚‚

/-- The balanced hull of `s` is minimal in the sense that it is contained in any balanced superset
`t` of `s`. -/
lemma balanced.hull_subset_of_subset (ht : balanced ğ•œ t) (h : s âŠ† t) : balanced_hull ğ•œ s âŠ† t :=
Î» x hx, by { obtain âŸ¨r, hr, y, hy, rflâŸ© := mem_balanced_hull_iff.1 hx, exact ht.smul_mem hr (h hy) }

end has_smul

section module
variables [add_comm_group E] [module ğ•œ E] {s : set E}

lemma balanced_core_zero_mem (hs : (0 : E) âˆˆ s) : (0 : E) âˆˆ balanced_core ğ•œ s :=
mem_balanced_core_iff.2 âŸ¨0, balanced_zero, zero_subset.2 hs, zero_mem_zeroâŸ©

lemma balanced_core_nonempty_iff : (balanced_core ğ•œ s).nonempty â†” (0 : E) âˆˆ s :=
âŸ¨Î» h, zero_subset.1 $ (zero_smul_set h).superset.trans $ (balanced_core_balanced s (0 : ğ•œ) $
  norm_zero.trans_le zero_le_one).trans $ balanced_core_subset _,
    Î» h, âŸ¨0, balanced_core_zero_mem hâŸ©âŸ©

variables (ğ•œ)

lemma subset_balanced_hull [norm_one_class ğ•œ] {s : set E} : s âŠ† balanced_hull ğ•œ s :=
Î» _ hx, mem_balanced_hull_iff.2 âŸ¨1, norm_one.le, _, hx, one_smul _ _âŸ©

variables {ğ•œ}

lemma balanced_hull.balanced (s : set E) : balanced ğ•œ (balanced_hull ğ•œ s) :=
begin
  intros a ha,
  simp_rw [balanced_hull, smul_set_Unionâ‚‚, subset_def, mem_Unionâ‚‚],
  rintro x âŸ¨r, hr, hxâŸ©,
  rw â†smul_assoc at hx,
  exact âŸ¨a â€¢ r, (semi_normed_ring.norm_mul _ _).trans (mul_le_one ha (norm_nonneg r) hr), hxâŸ©,
end

end module
end semi_normed_ring

section normed_field

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] {s t : set E}

@[simp] lemma balanced_core_aux_empty : balanced_core_aux ğ•œ (âˆ… : set E) = âˆ… :=
begin
  simp_rw [balanced_core_aux, Interâ‚‚_eq_empty_iff, smul_set_empty],
  exact Î» _, âŸ¨1, norm_one.ge, not_mem_empty _âŸ©,
end

lemma balanced_core_aux_subset (s : set E) : balanced_core_aux ğ•œ s âŠ† s :=
Î» x hx, by simpa only [one_smul] using mem_balanced_core_aux_iff.1 hx 1 norm_one.ge

lemma balanced_core_aux_balanced (h0 : (0 : E) âˆˆ balanced_core_aux ğ•œ s):
  balanced ğ•œ (balanced_core_aux ğ•œ s) :=
begin
  rintro a ha x âŸ¨y, hy, rflâŸ©,
  obtain rfl | h := eq_or_ne a 0,
  { rwa zero_smul },
  rw mem_balanced_core_aux_iff at âŠ¢ hy,
  intros r hr,
  have h'' : 1 â‰¤ â€–aâ»Â¹ â€¢ râ€–,
  { rw [norm_smul, norm_inv],
    exact one_le_mul_of_one_le_of_one_le (one_le_inv (norm_pos_iff.mpr h) ha) hr },
  have h' := hy (aâ»Â¹ â€¢ r) h'',
  rwa [smul_assoc, mem_inv_smul_set_iffâ‚€ h] at h',
end

lemma balanced_core_aux_maximal (h : t âŠ† s) (ht : balanced ğ•œ t) : t âŠ† balanced_core_aux ğ•œ s :=
begin
  refine Î» x hx, mem_balanced_core_aux_iff.2 (Î» r hr, _),
  rw mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.mp $ zero_lt_one.trans_le hr),
  refine h (ht.smul_mem _ hx),
  rw norm_inv,
  exact inv_le_one hr,
end

lemma balanced_core_subset_balanced_core_aux : balanced_core ğ•œ s âŠ† balanced_core_aux ğ•œ s :=
balanced_core_aux_maximal (balanced_core_subset s) (balanced_core_balanced s)

lemma balanced_core_eq_Inter (hs : (0 : E) âˆˆ s) :
  balanced_core ğ•œ s = â‹‚ (r : ğ•œ) (hr : 1 â‰¤ â€–râ€–), r â€¢ s :=
begin
  refine balanced_core_subset_balanced_core_aux.antisymm  _,
  refine (balanced_core_aux_balanced _).subset_core_of_subset (balanced_core_aux_subset s),
  exact balanced_core_subset_balanced_core_aux (balanced_core_zero_mem hs),
end

lemma subset_balanced_core (ht : (0 : E) âˆˆ t) (hst : âˆ€ (a : ğ•œ) (ha : â€–aâ€– â‰¤ 1), a â€¢ s âŠ† t) :
  s âŠ† balanced_core ğ•œ t :=
begin
  rw balanced_core_eq_Inter ht,
  refine subset_Interâ‚‚ (Î» a ha, _),
  rw â†smul_inv_smulâ‚€ (norm_pos_iff.mp $ zero_lt_one.trans_le ha) s,
  refine smul_set_mono (hst _ _),
  rw [norm_inv],
  exact inv_le_one ha,
end

end normed_field

end balanced_hull

/-! ### Topological properties -/

section topology

variables [nontrivially_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] [topological_space E]
  [has_continuous_smul ğ•œ E] {U : set E}

protected lemma is_closed.balanced_core (hU : is_closed U) : is_closed (balanced_core ğ•œ U) :=
begin
  by_cases h : (0 : E) âˆˆ U,
  { rw balanced_core_eq_Inter h,
    refine is_closed_Inter (Î» a, _),
    refine is_closed_Inter (Î» ha, _),
    have ha' := lt_of_lt_of_le zero_lt_one ha,
    rw norm_pos_iff at ha',
    refine is_closed_map_smul_of_ne_zero ha' U hU },
  convert is_closed_empty,
  contrapose! h,
  exact balanced_core_nonempty_iff.mp (set.nonempty_iff_ne_empty.2 h),
end

lemma balanced_core_mem_nhds_zero (hU : U âˆˆ ğ“ (0 : E)) : balanced_core ğ•œ U âˆˆ ğ“ (0 : E) :=
begin
  -- Getting neighborhoods of the origin for `0 : ğ•œ` and `0 : E`
  obtain âŸ¨r, V, hr, hV, hrVUâŸ© : âˆƒ (r : â„) (V : set E), 0 < r âˆ§ V âˆˆ ğ“ (0 : E) âˆ§
    âˆ€ (c : ğ•œ) (y : E), â€–câ€– < r â†’ y âˆˆ V â†’ c â€¢ y âˆˆ U,
  { have h : filter.tendsto (Î» (x : ğ•œ Ã— E), x.fst â€¢ x.snd) (ğ“ (0,0)) (ğ“ 0),
      from continuous_smul.tendsto' (0, 0) _ (smul_zero _),
    simpa only [â† prod.exists', â† prod.forall', â† and_imp, â† and.assoc, exists_prop]
      using h.basis_left (normed_add_comm_group.nhds_zero_basis_norm_lt.prod_nhds
        ((ğ“ _).basis_sets)) U hU },
  rcases normed_field.exists_norm_lt ğ•œ hr with âŸ¨y, hyâ‚€, hyrâŸ©,
  rw [norm_pos_iff] at hyâ‚€,
  have : y â€¢ V âˆˆ ğ“ (0 : E) := (set_smul_mem_nhds_zero_iff hyâ‚€).mpr hV,
  -- It remains to show that `y â€¢ V âŠ† balanced_core ğ•œ U`
  refine filter.mem_of_superset this (subset_balanced_core (mem_of_mem_nhds hU) $ Î» a ha, _),
  rw [smul_smul],
  rintro _ âŸ¨z, hz, rflâŸ©,
  refine hrVU _ _ _ hz,
  rw [norm_mul, â† one_mul r],
  exact mul_lt_mul' ha hyr (norm_nonneg y) one_pos
end

variables (ğ•œ E)

lemma nhds_basis_balanced : (ğ“ (0 : E)).has_basis
  (Î» (s : set E), s âˆˆ ğ“ (0 : E) âˆ§ balanced ğ•œ s) id :=
filter.has_basis_self.mpr
  (Î» s hs, âŸ¨balanced_core ğ•œ s, balanced_core_mem_nhds_zero hs,
            balanced_core_balanced s, balanced_core_subset sâŸ©)

lemma nhds_basis_closed_balanced [regular_space E] : (ğ“ (0 : E)).has_basis
  (Î» (s : set E), s âˆˆ ğ“ (0 : E) âˆ§ is_closed s âˆ§ balanced ğ•œ s) id :=
begin
  refine (closed_nhds_basis 0).to_has_basis (Î» s hs, _) (Î» s hs, âŸ¨s, âŸ¨hs.1, hs.2.1âŸ©, rfl.subsetâŸ©),
  refine âŸ¨balanced_core ğ•œ s, âŸ¨balanced_core_mem_nhds_zero hs.1, _âŸ©, balanced_core_subset sâŸ©,
  exact âŸ¨hs.2.balanced_core, balanced_core_balanced sâŸ©
end

end topology
