/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Bhavik Mehta, YaÃ«l Dillies
-/
import analysis.normed_space.basic

/-!
# Local convexity

This file defines absorbent and balanced sets.

An absorbent set is one that "surrounds" the origin. The idea is made precise by requiring that any
point belongs to all large enough scalings of the set. This is the vector world analog of a
topological neighborhood of the origin.

A balanced set is one that is everywhere around the origin. This means that `a â€¢ s âŠ† s` for all `a`
of norm less than `1`.

## Main declarations

For a module over a normed ring:
* `absorbs`: A set `s` absorbs a set `t` if all large scalings of `s` contain `t`.
* `absorbent`: A set `s` is absorbent if every point eventually belongs to all large scalings of
  `s`.
* `balanced`: A set `s` is balanced if `a â€¢ s âŠ† s` for all `a` of norm less than `1`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

absorbent, balanced, locally convex, LCTVS
-/

open set
open_locale pointwise topological_space

variables {ğ•œ ğ• E : Type*}

section semi_normed_ring
variables [semi_normed_ring ğ•œ]

section has_scalar
variables (ğ•œ) [has_scalar ğ•œ E]

/-- A set `A` absorbs another set `B` if `B` is contained in all scalings of `A` by elements of
sufficiently large norm. -/
def absorbs (A B : set E) := âˆƒ r, 0 < r âˆ§ âˆ€ a : ğ•œ, r â‰¤ âˆ¥aâˆ¥ â†’ B âŠ† a â€¢ A

/-- A set is absorbent if it absorbs every singleton. -/
def absorbent (A : set E) := âˆ€ x, âˆƒ r, 0 < r âˆ§ âˆ€ a : ğ•œ, r â‰¤ âˆ¥aâˆ¥ â†’ x âˆˆ a â€¢ A

/-- A set `A` is balanced if `a â€¢ A` is contained in `A` whenever `a` has norm at most `1`. -/
def balanced (A : set E) := âˆ€ a : ğ•œ, âˆ¥aâˆ¥ â‰¤ 1 â†’ a â€¢ A âŠ† A

variables {ğ•œ} {A B : set E}

lemma balanced_univ : balanced ğ•œ (univ : set E) := Î» a ha, subset_univ _

lemma balanced.union (hA : balanced ğ•œ A) (hB : balanced ğ•œ B) : balanced ğ•œ (A âˆª B) :=
begin
  intros a ha t ht,
  rw smul_set_union at ht,
  exact ht.imp (Î» x, hA _ ha x) (Î» x, hB _ ha x),
end

end has_scalar

section add_comm_group
variables [add_comm_group E] [module ğ•œ E] {s t u v A B : set E}

lemma balanced.inter (hA : balanced ğ•œ A) (hB : balanced ğ•œ B) : balanced ğ•œ (A âˆ© B) :=
begin
  rintro a ha _ âŸ¨x, âŸ¨hxâ‚, hxâ‚‚âŸ©, rflâŸ©,
  exact âŸ¨hA _ ha âŸ¨_, hxâ‚, rflâŸ©, hB _ ha âŸ¨_, hxâ‚‚, rflâŸ©âŸ©,
end

lemma balanced.add (hAâ‚ : balanced ğ•œ A) (hAâ‚‚ : balanced ğ•œ B) : balanced ğ•œ (A + B) :=
begin
  rintro a ha _ âŸ¨_, âŸ¨x, y, hx, hy, rflâŸ©, rflâŸ©,
  rw smul_add,
  exact add_mem_add (hAâ‚ _ ha âŸ¨_, hx, rflâŸ©) (hAâ‚‚ _ ha âŸ¨_, hy, rflâŸ©),
end

lemma absorbs.mono (hs : absorbs ğ•œ s u) (hst : s âŠ† t) (hvu : v âŠ† u) : absorbs ğ•œ t v :=
let âŸ¨r, hr, hâŸ© := hs in âŸ¨r, hr, Î» a ha, hvu.trans $ (h _ ha).trans $ smul_set_mono hstâŸ©

lemma absorbs.mono_left (hs : absorbs ğ•œ s u) (h : s âŠ† t) : absorbs ğ•œ t u := hs.mono h subset.rfl
lemma absorbs.mono_right (hs : absorbs ğ•œ s u) (h : v âŠ† u) : absorbs ğ•œ s v := hs.mono subset.rfl h

lemma absorbs.union (hu : absorbs ğ•œ s u) (hv : absorbs ğ•œ s v) : absorbs ğ•œ s (u âˆª v) :=
begin
  obtain âŸ¨a, ha, huâŸ© := hu,
  obtain âŸ¨b, hb, hvâŸ© := hv,
  exact âŸ¨max a b, lt_max_of_lt_left ha,
    Î» c hc, union_subset (hu _ $ le_of_max_le_left hc) (hv _ $ le_of_max_le_right hc)âŸ©,
end

@[simp] lemma absorbs_union : absorbs ğ•œ s (u âˆª v) â†” absorbs ğ•œ s u âˆ§ absorbs ğ•œ s v :=
âŸ¨Î» h, âŸ¨h.mono_right $ subset_union_left _ _, h.mono_right $ subset_union_right _ _âŸ©,
  Î» h, h.1.union h.2âŸ©

lemma absorbent.subset (hA : absorbent ğ•œ A) (hAB : A âŠ† B) : absorbent ğ•œ B :=
begin
  refine forall_imp (Î» x, _) hA,
  exact Exists.imp (Î» r, and.imp_right $ forallâ‚‚_imp $ Î» a ha hx, set.smul_set_mono hAB hx),
end

lemma absorbent_iff_forall_absorbs_singleton : absorbent ğ•œ A â†” âˆ€ x, absorbs ğ•œ A {x} :=
by simp_rw [absorbs, absorbent, singleton_subset_iff]

lemma absorbent.absorbs (hs : absorbent ğ•œ s) {x : E} : absorbs ğ•œ s {x} :=
absorbent_iff_forall_absorbs_singleton.1 hs _

lemma absorbent_iff_nonneg_lt : absorbent ğ•œ A â†” âˆ€ x, âˆƒ r, 0 â‰¤ r âˆ§ âˆ€ â¦ƒa : ğ•œâ¦„, r < âˆ¥aâˆ¥ â†’ x âˆˆ a â€¢ A :=
forall_congr $ Î» x, âŸ¨Î» âŸ¨r, hr, hxâŸ©, âŸ¨r, hr.le, Î» a ha, hx a ha.leâŸ©, Î» âŸ¨r, hr, hxâŸ©,
  âŸ¨r + 1, add_pos_of_nonneg_of_pos hr zero_lt_one,
    Î» a ha, hx ((lt_add_of_pos_right r zero_lt_one).trans_le ha)âŸ©âŸ©

end add_comm_group
end semi_normed_ring

section normed_comm_ring
variables [normed_comm_ring ğ•œ] [add_comm_monoid E] [module ğ•œ E] {A B : set E} (a : ğ•œ)

lemma balanced.smul (hA : balanced ğ•œ A) : balanced ğ•œ (a â€¢ A) :=
begin
  rintro b hb _ âŸ¨_, âŸ¨x, hx, rflâŸ©, rflâŸ©,
  exact âŸ¨b â€¢ x, hA _ hb âŸ¨_, hx, rflâŸ©, smul_comm _ _ _âŸ©,
end

end normed_comm_ring

section normed_field
variables [normed_field ğ•œ] [normed_ring ğ•] [normed_space ğ•œ ğ•] [add_comm_group E] [module ğ•œ E]
  [smul_with_zero ğ• E] [is_scalar_tower ğ•œ ğ• E] {s t u v A B : set E} {a b : ğ•œ}

/-- Scalar multiplication (by possibly different types) of a balanced set is monotone. -/
lemma balanced.smul_mono (hs : balanced ğ• s) {a : ğ•} {b : ğ•œ} (h : âˆ¥aâˆ¥ â‰¤ âˆ¥bâˆ¥) : a â€¢ s âŠ† b â€¢ s :=
begin
  obtain rfl | hb := eq_or_ne b 0,
  { rw norm_zero at h,
    rw norm_eq_zero.1 (h.antisymm $ norm_nonneg _),
    obtain rfl | h := s.eq_empty_or_nonempty,
    { simp_rw [smul_set_empty] },
    { simp_rw [zero_smul_set h] } },
  rintro _ âŸ¨x, hx, rflâŸ©,
  refine âŸ¨bâ»Â¹ â€¢ a â€¢ x, _, smul_inv_smulâ‚€ hb _âŸ©,
  rw â†smul_assoc,
  refine hs _ _ (smul_mem_smul_set hx),
  rw [norm_smul, norm_inv, â†div_eq_inv_mul],
  exact div_le_one_of_le h (norm_nonneg _),
end

/-- A balanced set absorbs itself. -/
lemma balanced.absorbs_self (hA : balanced ğ•œ A) : absorbs ğ•œ A A :=
begin
  refine âŸ¨1, zero_lt_one, Î» a ha x hx, _âŸ©,
  rw mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.1 $ zero_lt_one.trans_le ha),
  refine hA aâ»Â¹ _ (smul_mem_smul_set hx),
  rw norm_inv,
  exact inv_le_one ha,
end

lemma balanced.subset_smul (hA : balanced ğ•œ A) (ha : 1 â‰¤ âˆ¥aâˆ¥) : A âŠ† a â€¢ A :=
begin
  refine (subset_set_smul_iffâ‚€ _).2 (hA (aâ»Â¹) _),
  { rintro rfl,
    rw norm_zero at ha,
    exact zero_lt_one.not_le ha },
  { rw norm_inv,
    exact inv_le_one ha }
end

lemma balanced.smul_eq (hA : balanced ğ•œ A) (ha : âˆ¥aâˆ¥ = 1) : a â€¢ A = A :=
(hA _ ha.le).antisymm $ hA.subset_smul ha.ge

lemma absorbs.inter (hs : absorbs ğ•œ s u) (ht : absorbs ğ•œ t u) : absorbs ğ•œ (s âˆ© t) u :=
begin
  obtain âŸ¨a, ha, hsâŸ© := hs,
  obtain âŸ¨b, hb, htâŸ© := ht,
  have h : 0 < max a b := lt_max_of_lt_left ha,
  refine âŸ¨max a b, lt_max_of_lt_left ha, Î» c hc, _âŸ©,
  rw smul_set_interâ‚€ (norm_pos_iff.1 $ h.trans_le hc),
  exact subset_inter (hs _ $ le_of_max_le_left hc) (ht _ $ le_of_max_le_right hc),
end

@[simp] lemma absorbs_inter : absorbs ğ•œ (s âˆ© t) u â†” absorbs ğ•œ s u âˆ§ absorbs ğ•œ t u :=
âŸ¨Î» h, âŸ¨h.mono_left $ inter_subset_left _ _, h.mono_left $ inter_subset_right _ _âŸ©,
  Î» h, h.1.inter h.2âŸ©

lemma absorbent_univ : absorbent ğ•œ (univ : set E) :=
begin
  refine Î» x, âŸ¨1, zero_lt_one, Î» a ha, _âŸ©,
  rw smul_set_univâ‚€ (norm_pos_iff.1 $ zero_lt_one.trans_le ha),
  exact trivial,
end

variables [topological_space E] [has_continuous_smul ğ•œ E]

/-- Every neighbourhood of the origin is absorbent. -/
lemma absorbent_nhds_zero (hA : A âˆˆ ğ“ (0 : E)) : absorbent ğ•œ A :=
begin
  intro x,
  obtain âŸ¨w, hwâ‚, hwâ‚‚, hwâ‚ƒâŸ© := mem_nhds_iff.mp hA,
  have hc : continuous (Î» t : ğ•œ, t â€¢ x) := continuous_id.smul continuous_const,
  obtain âŸ¨r, hrâ‚, hrâ‚‚âŸ© := metric.is_open_iff.mp (hwâ‚‚.preimage hc) 0
    (by rwa [mem_preimage, zero_smul]),
  have hrâ‚ƒ := inv_pos.mpr (half_pos hrâ‚),
  refine âŸ¨(r / 2)â»Â¹, hrâ‚ƒ, Î» a haâ‚, _âŸ©,
  have haâ‚‚ : 0 < âˆ¥aâˆ¥ := hrâ‚ƒ.trans_le haâ‚,
  refine (mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.mp haâ‚‚) _ _).2 (hwâ‚ $ hrâ‚‚ _),
  rw [metric.mem_ball, dist_zero_right, norm_inv],
  calc âˆ¥aâˆ¥â»Â¹ â‰¤ r/2 : (inv_le (half_pos hrâ‚) haâ‚‚).mp haâ‚
  ...       < r : half_lt_self hrâ‚,
end

/-- The union of `{0}` with the interior of a balanced set is balanced. -/
lemma balanced_zero_union_interior (hA : balanced ğ•œ A) : balanced ğ•œ ((0 : set E) âˆª interior A) :=
begin
  intros a ha,
  obtain rfl | h := eq_or_ne a 0,
  { rw zero_smul_set,
    exacts [subset_union_left _ _, âŸ¨0, or.inl rflâŸ©] },
  { rw [â†image_smul, image_union],
    apply union_subset_union,
    { rw [image_zero, smul_zero],
      refl },
    { calc a â€¢ interior A âŠ† interior (a â€¢ A) : (is_open_map_smulâ‚€ h).image_interior_subset A
                      ... âŠ† interior A       : interior_mono (hA _ ha) } }
end

/-- The interior of a balanced set is balanced if it contains the origin. -/
lemma balanced.interior (hA : balanced ğ•œ A) (h : (0 : E) âˆˆ interior A) : balanced ğ•œ (interior A) :=
begin
  rw â†union_eq_self_of_subset_left (singleton_subset_iff.2 h),
  exact balanced_zero_union_interior hA,
end

lemma balanced.closure (hA : balanced ğ•œ A) : balanced ğ•œ (closure A) :=
Î» a ha,
  (image_closure_subset_closure_image $ continuous_id.const_smul _).trans $ closure_mono $ hA _ ha

end normed_field

section nondiscrete_normed_field
variables [nondiscrete_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] {s : set E}

lemma absorbs_zero_iff : absorbs ğ•œ s 0 â†” (0 : E) âˆˆ s :=
begin
  refine âŸ¨_, Î» h, âŸ¨1, zero_lt_one, Î» a _, zero_subset.2 $ zero_mem_smul_set hâŸ©âŸ©,
  rintro âŸ¨r, hr, hâŸ©,
  obtain âŸ¨a, haâŸ© := normed_space.exists_lt_norm ğ•œ ğ•œ r,
  have := h _ ha.le,
  rwa [zero_subset, zero_mem_smul_set_iff] at this,
  exact norm_ne_zero_iff.1 (hr.trans ha).ne',
end

lemma absorbent.zero_mem (hs : absorbent ğ•œ s) : (0 : E) âˆˆ s :=
absorbs_zero_iff.1 $ absorbent_iff_forall_absorbs_singleton.1 hs _

end nondiscrete_normed_field
