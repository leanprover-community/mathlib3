/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Bhavik Mehta, YaÃ«l Dillies
-/
import analysis.convex.basic
import analysis.convex.hull
import analysis.normed_space.basic

/-!
# Local convexity

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines absorbent and balanced sets.

An absorbent set is one that "surrounds" the origin. The idea is made precise by requiring that any
point belongs to all large enough scalings of the set. This is the vector world analog of a
topological neighborhood of the origin.

A balanced set is one that is everywhere around the origin. This means that `a â€¢ s âŠ† s` for all `a`
of norm less than `1`.

## Main declarations

For a module over a normed ring:
* `absorbs`: A set `s` absorbs a set `t` if all large scalings of `s` contain `t`.
* `absorbent`: A set `s` is absorbent if every point eventually belongs to all large scalings of
  `s`.
* `balanced`: A set `s` is balanced if `a â€¢ s âŠ† s` for all `a` of norm less than `1`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

absorbent, balanced, locally convex, LCTVS
-/

open set
open_locale pointwise topology

variables {ğ•œ ğ• E  : Type*} {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*}

section semi_normed_ring
variables [semi_normed_ring ğ•œ]

section has_smul
variables (ğ•œ) [has_smul ğ•œ E]

/-- A set `A` absorbs another set `B` if `B` is contained in all scalings of `A` by elements of
sufficiently large norm. -/
def absorbs (A B : set E) := âˆƒ r, 0 < r âˆ§ âˆ€ a : ğ•œ, r â‰¤ â€–aâ€– â†’ B âŠ† a â€¢ A

variables {ğ•œ} {s t u v A B : set E}

@[simp] lemma absorbs_empty {s : set E}: absorbs ğ•œ s (âˆ… : set E) :=
âŸ¨1, one_pos, Î» a ha, set.empty_subset _âŸ©

lemma absorbs.mono (hs : absorbs ğ•œ s u) (hst : s âŠ† t) (hvu : v âŠ† u) : absorbs ğ•œ t v :=
let âŸ¨r, hr, hâŸ© := hs in âŸ¨r, hr, Î» a ha, hvu.trans $ (h _ ha).trans $ smul_set_mono hstâŸ©

lemma absorbs.mono_left (hs : absorbs ğ•œ s u) (h : s âŠ† t) : absorbs ğ•œ t u := hs.mono h subset.rfl
lemma absorbs.mono_right (hs : absorbs ğ•œ s u) (h : v âŠ† u) : absorbs ğ•œ s v := hs.mono subset.rfl h

lemma absorbs.union (hu : absorbs ğ•œ s u) (hv : absorbs ğ•œ s v) : absorbs ğ•œ s (u âˆª v) :=
begin
  obtain âŸ¨a, ha, huâŸ© := hu,
  obtain âŸ¨b, hb, hvâŸ© := hv,
  exact âŸ¨max a b, lt_max_of_lt_left ha,
    Î» c hc, union_subset (hu _ $ le_of_max_le_left hc) (hv _ $ le_of_max_le_right hc)âŸ©,
end

@[simp] lemma absorbs_union : absorbs ğ•œ s (u âˆª v) â†” absorbs ğ•œ s u âˆ§ absorbs ğ•œ s v :=
âŸ¨Î» h, âŸ¨h.mono_right $ subset_union_left _ _, h.mono_right $ subset_union_right _ _âŸ©,
  Î» h, h.1.union h.2âŸ©

lemma absorbs_Union_finset {Î¹ : Type*} {t : finset Î¹} {f : Î¹ â†’ set E} :
  absorbs ğ•œ s (â‹ƒ i âˆˆ t, f i) â†” âˆ€ i âˆˆ t, absorbs ğ•œ s (f i) :=
begin
  classical,
  induction t using finset.induction_on with i t ht hi,
  { simp only [finset.not_mem_empty, set.Union_false, set.Union_empty, absorbs_empty,
      is_empty.forall_iff, implies_true_iff] },
  rw [finset.set_bUnion_insert, absorbs_union, hi],
  split; intro h,
  { refine Î» _ hi', (finset.mem_insert.mp hi').elim _ (h.2 _),
    exact (Î» hi'', by { rw hi'', exact h.1 }) },
  exact âŸ¨h i (finset.mem_insert_self i t), Î» i' hi', h i' (finset.mem_insert_of_mem hi')âŸ©,
end

lemma set.finite.absorbs_Union {Î¹ : Type*} {s : set E} {t : set Î¹} {f : Î¹ â†’ set E} (hi : t.finite) :
  absorbs ğ•œ s (â‹ƒ i âˆˆ t, f i) â†” âˆ€ i âˆˆ t, absorbs ğ•œ s (f i) :=
begin
  lift t to finset Î¹ using hi,
  simp only [finset.mem_coe],
  exact absorbs_Union_finset,
end

variables (ğ•œ)

/-- A set is absorbent if it absorbs every singleton. -/
def absorbent (A : set E) := âˆ€ x, âˆƒ r, 0 < r âˆ§ âˆ€ a : ğ•œ, r â‰¤ â€–aâ€– â†’ x âˆˆ a â€¢ A

variables {ğ•œ}

lemma absorbent.subset (hA : absorbent ğ•œ A) (hAB : A âŠ† B) : absorbent ğ•œ B :=
begin
  refine forall_imp (Î» x, _) hA,
  exact Exists.imp (Î» r, and.imp_right $ forallâ‚‚_imp $ Î» a ha hx, set.smul_set_mono hAB hx),
end

lemma absorbent_iff_forall_absorbs_singleton : absorbent ğ•œ A â†” âˆ€ x, absorbs ğ•œ A {x} :=
by simp_rw [absorbs, absorbent, singleton_subset_iff]

lemma absorbent.absorbs (hs : absorbent ğ•œ s) {x : E} : absorbs ğ•œ s {x} :=
absorbent_iff_forall_absorbs_singleton.1 hs _

lemma absorbent_iff_nonneg_lt : absorbent ğ•œ A â†” âˆ€ x, âˆƒ r, 0 â‰¤ r âˆ§ âˆ€ â¦ƒa : ğ•œâ¦„, r < â€–aâ€– â†’ x âˆˆ a â€¢ A :=
forall_congr $ Î» x, âŸ¨Î» âŸ¨r, hr, hxâŸ©, âŸ¨r, hr.le, Î» a ha, hx a ha.leâŸ©, Î» âŸ¨r, hr, hxâŸ©,
  âŸ¨r + 1, add_pos_of_nonneg_of_pos hr zero_lt_one,
    Î» a ha, hx ((lt_add_of_pos_right r zero_lt_one).trans_le ha)âŸ©âŸ©

lemma absorbent.absorbs_finite {s : set E} (hs : absorbent ğ•œ s) {v : set E} (hv : v.finite) :
  absorbs ğ•œ s v :=
begin
  rw â†set.bUnion_of_singleton v,
  exact hv.absorbs_Union.mpr (Î» _ _, hs.absorbs),
end

variables (ğ•œ)

/-- A set `A` is balanced if `a â€¢ A` is contained in `A` whenever `a` has norm at most `1`. -/
def balanced (A : set E) := âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ A âŠ† A

variables {ğ•œ}

lemma balanced_iff_smul_mem : balanced ğ•œ s â†” âˆ€ â¦ƒa : ğ•œâ¦„, â€–aâ€– â‰¤ 1 â†’ âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ a â€¢ x âˆˆ s :=
forallâ‚‚_congr $ Î» a ha, smul_set_subset_iff

alias balanced_iff_smul_mem â†” balanced.smul_mem _

@[simp] lemma balanced_empty : balanced ğ•œ (âˆ… : set E) :=
Î» _ _, by { rw smul_set_empty }

@[simp] lemma balanced_univ : balanced ğ•œ (univ : set E) := Î» a ha, subset_univ _

lemma balanced.union (hA : balanced ğ•œ A) (hB : balanced ğ•œ B) : balanced ğ•œ (A âˆª B) :=
Î» a ha, smul_set_union.subset.trans $ union_subset_union (hA _ ha) $ hB _ ha

lemma balanced.inter (hA : balanced ğ•œ A) (hB : balanced ğ•œ B) : balanced ğ•œ (A âˆ© B) :=
Î» a ha, smul_set_inter_subset.trans $ inter_subset_inter (hA _ ha) $ hB _ ha

lemma balanced_Union {f : Î¹ â†’ set E} (h : âˆ€ i, balanced ğ•œ (f i)) : balanced ğ•œ (â‹ƒ i, f i) :=
Î» a ha, (smul_set_Union _ _).subset.trans $ Union_mono $ Î» _, h _ _ ha

lemma balanced_Unionâ‚‚ {f : Î  i, Îº i â†’ set E} (h : âˆ€ i j, balanced ğ•œ (f i j)) :
  balanced ğ•œ (â‹ƒ i j, f i j) :=
balanced_Union $ Î» _, balanced_Union $ h _

lemma balanced_Inter {f : Î¹ â†’ set E} (h : âˆ€ i, balanced ğ•œ (f i)) : balanced ğ•œ (â‹‚ i, f i) :=
Î» a ha, (smul_set_Inter_subset _ _).trans $ Inter_mono $ Î» _, h _ _ ha

lemma balanced_Interâ‚‚ {f : Î  i, Îº i â†’ set E} (h : âˆ€ i j, balanced ğ•œ (f i j)) :
  balanced ğ•œ (â‹‚ i j, f i j) :=
balanced_Inter $ Î» _, balanced_Inter $ h _

variables [has_smul ğ• E] [smul_comm_class ğ•œ ğ• E]

lemma balanced.smul (a : ğ•) (hs : balanced ğ•œ s) : balanced ğ•œ (a â€¢ s) :=
Î» b hb, (smul_comm _ _ _).subset.trans $ smul_set_mono $ hs _ hb

end has_smul

section module
variables [add_comm_group E] [module ğ•œ E] {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ : set E}

lemma absorbs.neg : absorbs ğ•œ s t â†’ absorbs ğ•œ (-s) (-t) :=
Exists.imp $ Î» r, and.imp_right $ forallâ‚‚_imp $ Î» _ _ h,
  (neg_subset_neg.2 h).trans (smul_set_neg _ _).superset

lemma balanced.neg : balanced ğ•œ s â†’ balanced ğ•œ (-s) :=
forallâ‚‚_imp $ Î» _ _ h, (smul_set_neg _ _).subset.trans $ neg_subset_neg.2 h

lemma absorbs.add : absorbs ğ•œ sâ‚ tâ‚ â†’ absorbs ğ•œ sâ‚‚ tâ‚‚ â†’ absorbs ğ•œ (sâ‚ + sâ‚‚) (tâ‚ + tâ‚‚) :=
Î» âŸ¨râ‚, hrâ‚, hâ‚âŸ© âŸ¨râ‚‚, hrâ‚‚, hâ‚‚âŸ©, âŸ¨max râ‚ râ‚‚, lt_max_of_lt_left hrâ‚, Î» a ha, (add_subset_add
  (hâ‚ _ $ le_of_max_le_left ha) $ hâ‚‚ _ $ le_of_max_le_right ha).trans (smul_add _ _ _).supersetâŸ©

lemma balanced.add (hs : balanced ğ•œ s) (ht : balanced ğ•œ t) : balanced ğ•œ (s + t) :=
Î» a ha, (smul_add _ _ _).subset.trans $ add_subset_add (hs _ ha) $ ht _ ha

lemma absorbs.sub (hâ‚ : absorbs ğ•œ sâ‚ tâ‚) (hâ‚‚ : absorbs ğ•œ sâ‚‚ tâ‚‚) : absorbs ğ•œ (sâ‚ - sâ‚‚) (tâ‚ - tâ‚‚) :=
by { simp_rw sub_eq_add_neg, exact hâ‚.add hâ‚‚.neg }

lemma balanced.sub (hs : balanced ğ•œ s) (ht : balanced ğ•œ t) : balanced ğ•œ (s - t) :=
by { simp_rw sub_eq_add_neg, exact hs.add ht.neg }

lemma balanced_zero : balanced ğ•œ (0 : set E) := Î» a ha, (smul_zero _).subset

end module
end semi_normed_ring

section normed_field
variables [normed_field ğ•œ] [normed_ring ğ•] [normed_space ğ•œ ğ•] [add_comm_group E] [module ğ•œ E]
  [smul_with_zero ğ• E] [is_scalar_tower ğ•œ ğ• E] {s t u v A B : set E} {x : E} {a b : ğ•œ}

/-- Scalar multiplication (by possibly different types) of a balanced set is monotone. -/
lemma balanced.smul_mono (hs : balanced ğ• s) {a : ğ•} {b : ğ•œ} (h : â€–aâ€– â‰¤ â€–bâ€–) : a â€¢ s âŠ† b â€¢ s :=
begin
  obtain rfl | hb := eq_or_ne b 0,
  { rw norm_zero at h,
    rw norm_eq_zero.1 (h.antisymm $ norm_nonneg _),
    obtain rfl | h := s.eq_empty_or_nonempty,
    { simp_rw [smul_set_empty] },
    { simp_rw [zero_smul_set h] } },
  rintro _ âŸ¨x, hx, rflâŸ©,
  refine âŸ¨bâ»Â¹ â€¢ a â€¢ x, _, smul_inv_smulâ‚€ hb _âŸ©,
  rw â†smul_assoc,
  refine hs _ _ (smul_mem_smul_set hx),
  rw [norm_smul, norm_inv, â†div_eq_inv_mul],
  exact div_le_one_of_le h (norm_nonneg _),
end

/-- A balanced set absorbs itself. -/
lemma balanced.absorbs_self (hA : balanced ğ•œ A) : absorbs ğ•œ A A :=
begin
  refine âŸ¨1, zero_lt_one, Î» a ha x hx, _âŸ©,
  rw mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.1 $ zero_lt_one.trans_le ha),
  refine hA aâ»Â¹ _ (smul_mem_smul_set hx),
  rw norm_inv,
  exact inv_le_one ha,
end

lemma balanced.subset_smul (hA : balanced ğ•œ A) (ha : 1 â‰¤ â€–aâ€–) : A âŠ† a â€¢ A :=
begin
  refine (subset_set_smul_iffâ‚€ _).2 (hA (aâ»Â¹) _),
  { rintro rfl,
    rw norm_zero at ha,
    exact zero_lt_one.not_le ha },
  { rw norm_inv,
    exact inv_le_one ha }
end

lemma balanced.smul_eq (hA : balanced ğ•œ A) (ha : â€–aâ€– = 1) : a â€¢ A = A :=
(hA _ ha.le).antisymm $ hA.subset_smul ha.ge

lemma balanced.mem_smul_iff (hs : balanced ğ•œ s) (h : â€–aâ€– = â€–bâ€–) : a â€¢ x âˆˆ s â†” b â€¢ x âˆˆ s :=
begin
  obtain rfl | hb := eq_or_ne b 0,
  { rw [norm_zero, norm_eq_zero] at h,
    rw h },
  have ha : a â‰  0 := norm_ne_zero_iff.1 (ne_of_eq_of_ne h $ norm_ne_zero_iff.2 hb),
  split; intro h'; [rw â†inv_mul_cancel_rightâ‚€ ha b, rw â†inv_mul_cancel_rightâ‚€ hb a];
  { rw [â†smul_eq_mul, smul_assoc],
    refine hs.smul_mem _ h',
    simp [â†h, ha] }
end

lemma balanced.neg_mem_iff (hs : balanced ğ•œ s) : -x âˆˆ s â†” x âˆˆ s :=
by convert hs.mem_smul_iff (norm_neg 1); simp only [neg_smul, one_smul]

lemma absorbs.inter (hs : absorbs ğ•œ s u) (ht : absorbs ğ•œ t u) : absorbs ğ•œ (s âˆ© t) u :=
begin
  obtain âŸ¨a, ha, hsâŸ© := hs,
  obtain âŸ¨b, hb, htâŸ© := ht,
  have h : 0 < max a b := lt_max_of_lt_left ha,
  refine âŸ¨max a b, lt_max_of_lt_left ha, Î» c hc, _âŸ©,
  rw smul_set_interâ‚€ (norm_pos_iff.1 $ h.trans_le hc),
  exact subset_inter (hs _ $ le_of_max_le_left hc) (ht _ $ le_of_max_le_right hc),
end

@[simp] lemma absorbs_inter : absorbs ğ•œ (s âˆ© t) u â†” absorbs ğ•œ s u âˆ§ absorbs ğ•œ t u :=
âŸ¨Î» h, âŸ¨h.mono_left $ inter_subset_left _ _, h.mono_left $ inter_subset_right _ _âŸ©,
  Î» h, h.1.inter h.2âŸ©

lemma absorbent_univ : absorbent ğ•œ (univ : set E) :=
begin
  refine Î» x, âŸ¨1, zero_lt_one, Î» a ha, _âŸ©,
  rw smul_set_univâ‚€ (norm_pos_iff.1 $ zero_lt_one.trans_le ha),
  exact trivial,
end

variables [topological_space E] [has_continuous_smul ğ•œ E]

/-- Every neighbourhood of the origin is absorbent. -/
lemma absorbent_nhds_zero (hA : A âˆˆ ğ“ (0 : E)) : absorbent ğ•œ A :=
begin
  intro x,
  obtain âŸ¨w, hwâ‚, hwâ‚‚, hwâ‚ƒâŸ© := mem_nhds_iff.mp hA,
  have hc : continuous (Î» t : ğ•œ, t â€¢ x) := continuous_id.smul continuous_const,
  obtain âŸ¨r, hrâ‚, hrâ‚‚âŸ© := metric.is_open_iff.mp (hwâ‚‚.preimage hc) 0
    (by rwa [mem_preimage, zero_smul]),
  have hrâ‚ƒ := inv_pos.mpr (half_pos hrâ‚),
  refine âŸ¨(r / 2)â»Â¹, hrâ‚ƒ, Î» a haâ‚, _âŸ©,
  have haâ‚‚ : 0 < â€–aâ€– := hrâ‚ƒ.trans_le haâ‚,
  refine (mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.mp haâ‚‚) _ _).2 (hwâ‚ $ hrâ‚‚ _),
  rw [metric.mem_ball, dist_zero_right, norm_inv],
  calc â€–aâ€–â»Â¹ â‰¤ r/2 : (inv_le (half_pos hrâ‚) haâ‚‚).mp haâ‚
  ...       < r : half_lt_self hrâ‚,
end

/-- The union of `{0}` with the interior of a balanced set is balanced. -/
lemma balanced_zero_union_interior (hA : balanced ğ•œ A) : balanced ğ•œ ((0 : set E) âˆª interior A) :=
begin
  intros a ha,
  obtain rfl | h := eq_or_ne a 0,
  { rw zero_smul_set,
    exacts [subset_union_left _ _, âŸ¨0, or.inl rflâŸ©] },
  { rw [â†image_smul, image_union],
    apply union_subset_union,
    { rw [image_zero, smul_zero],
      refl },
    { calc a â€¢ interior A âŠ† interior (a â€¢ A) : (is_open_map_smulâ‚€ h).image_interior_subset A
                      ... âŠ† interior A       : interior_mono (hA _ ha) } }
end

/-- The interior of a balanced set is balanced if it contains the origin. -/
lemma balanced.interior (hA : balanced ğ•œ A) (h : (0 : E) âˆˆ interior A) : balanced ğ•œ (interior A) :=
begin
  rw â†union_eq_self_of_subset_left (singleton_subset_iff.2 h),
  exact balanced_zero_union_interior hA,
end

lemma balanced.closure (hA : balanced ğ•œ A) : balanced ğ•œ (closure A) :=
Î» a ha,
  (image_closure_subset_closure_image $ continuous_id.const_smul _).trans $ closure_mono $ hA _ ha

end normed_field

section nontrivially_normed_field
variables [nontrivially_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] {s : set E}

lemma absorbs_zero_iff : absorbs ğ•œ s 0 â†” (0 : E) âˆˆ s :=
begin
  refine âŸ¨_, Î» h, âŸ¨1, zero_lt_one, Î» a _, zero_subset.2 $ zero_mem_smul_set hâŸ©âŸ©,
  rintro âŸ¨r, hr, hâŸ©,
  obtain âŸ¨a, haâŸ© := normed_space.exists_lt_norm ğ•œ ğ•œ r,
  have := h _ ha.le,
  rwa [zero_subset, zero_mem_smul_set_iff] at this,
  exact norm_ne_zero_iff.1 (hr.trans ha).ne',
end

lemma absorbent.zero_mem (hs : absorbent ğ•œ s) : (0 : E) âˆˆ s :=
absorbs_zero_iff.1 $ absorbent_iff_forall_absorbs_singleton.1 hs _

variables [module â„ E] [smul_comm_class â„ ğ•œ E]

lemma balanced_convex_hull_of_balanced (hs : balanced ğ•œ s) : balanced ğ•œ (convex_hull â„ s) :=
begin
  suffices : convex â„ {x | âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ x âˆˆ convex_hull â„ s},
  { rw balanced_iff_smul_mem at hs âŠ¢,
    refine Î» a ha x hx, convex_hull_min _ this hx a ha,
    exact Î» y hy a ha, subset_convex_hull â„ s (hs ha hy) },
  intros x hx y hy u v hu hv huv a ha,
  simp only [smul_add, â† smul_comm],
  exact convex_convex_hull â„ s (hx a ha) (hy a ha) hu hv huv
end

end nontrivially_normed_field

section real
variables [add_comm_group E] [module â„ E] {s : set E}

lemma balanced_iff_neg_mem (hs : convex â„ s) : balanced â„ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ -x âˆˆ s :=
begin
  refine âŸ¨Î» h x, h.neg_mem_iff.2, Î» h a ha, smul_set_subset_iff.2 $ Î» x hx, _âŸ©,
  rw [real.norm_eq_abs, abs_le] at ha,
  rw [show a = -((1 - a) / 2) + (a - -1)/2, by ring, add_smul, neg_smul, â†smul_neg],
  exact hs (h hx) hx (div_nonneg (sub_nonneg_of_le ha.2) zero_le_two)
    (div_nonneg (sub_nonneg_of_le ha.1) zero_le_two) (by ring),
end

end real
