/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import analysis.locally_convex.basic
import analysis.locally_convex.balanced_core_hull
import analysis.seminorm
import topology.bornology.basic
import topology.algebra.uniform_group
import topology.uniform_space.cauchy

/-!
# Von Neumann Boundedness

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines natural or von Neumann bounded sets and proves elementary properties.

## Main declarations

* `bornology.is_vonN_bounded`: A set `s` is von Neumann-bounded if every neighborhood of zero
absorbs `s`.
* `bornology.vonN_bornology`: The bornology made of the von Neumann-bounded sets.

## Main results

* `bornology.is_vonN_bounded.of_topological_space_le`: A coarser topology admits more
von Neumann-bounded sets.
* `bornology.is_vonN_bounded.image`: A continuous linear image of a bounded set is bounded.
* `bornology.is_vonN_bounded_iff_smul_tendsto_zero`: Given any sequence `Îµ` of scalars which tends
  to `ğ“[â‰ ] 0`, we have that a set `S` is bounded if and only if for any sequence `x : â„• â†’ S`,
  `Îµ â€¢ x` tends to 0. This shows that bounded sets are completely determined by sequences, which is
  the key fact for proving that sequential continuity implies continuity for linear maps defined on
  a bornological space

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

-/

variables {ğ•œ ğ•œ' E E' F Î¹ : Type*}

open set filter
open_locale topology pointwise

namespace bornology

section semi_normed_ring

section has_zero

variables (ğ•œ)
variables [semi_normed_ring ğ•œ] [has_smul ğ•œ E] [has_zero E]
variables [topological_space E]

/-- A set `s` is von Neumann bounded if every neighborhood of 0 absorbs `s`. -/
def is_vonN_bounded (s : set E) : Prop := âˆ€ â¦ƒVâ¦„, V âˆˆ ğ“ (0 : E) â†’ absorbs ğ•œ V s

variables (E)

@[simp] lemma is_vonN_bounded_empty : is_vonN_bounded ğ•œ (âˆ… : set E) :=
Î» _ _, absorbs_empty

variables {ğ•œ E}

lemma is_vonN_bounded_iff (s : set E) : is_vonN_bounded ğ•œ s â†” âˆ€ V âˆˆ ğ“ (0 : E), absorbs ğ•œ V s :=
iff.rfl

lemma _root_.filter.has_basis.is_vonN_bounded_basis_iff {q : Î¹ â†’ Prop} {s : Î¹ â†’ set E} {A : set E}
  (h : (ğ“ (0 : E)).has_basis q s) :
  is_vonN_bounded ğ•œ A â†” âˆ€ i (hi : q i), absorbs ğ•œ (s i) A :=
begin
  refine âŸ¨Î» hA i hi, hA (h.mem_of_mem hi), Î» hA V hV, _âŸ©,
  rcases h.mem_iff.mp hV with âŸ¨i, hi, hVâŸ©,
  exact (hA i hi).mono_left hV,
end

/-- Subsets of bounded sets are bounded. -/
lemma is_vonN_bounded.subset {sâ‚ sâ‚‚ : set E} (h : sâ‚ âŠ† sâ‚‚) (hsâ‚‚ : is_vonN_bounded ğ•œ sâ‚‚) :
  is_vonN_bounded ğ•œ sâ‚ :=
Î» V hV, (hsâ‚‚ hV).mono_right h

/-- The union of two bounded sets is bounded. -/
lemma is_vonN_bounded.union {sâ‚ sâ‚‚ : set E} (hsâ‚ : is_vonN_bounded ğ•œ sâ‚)
  (hsâ‚‚ : is_vonN_bounded ğ•œ sâ‚‚) :
  is_vonN_bounded ğ•œ (sâ‚ âˆª sâ‚‚) :=
Î» V hV, (hsâ‚ hV).union (hsâ‚‚ hV)

end has_zero

end semi_normed_ring

section multiple_topologies

variables [semi_normed_ring ğ•œ] [add_comm_group E] [module ğ•œ E]

/-- If a topology `t'` is coarser than `t`, then any set `s` that is bounded with respect to
`t` is bounded with respect to `t'`. -/
lemma is_vonN_bounded.of_topological_space_le {t t' : topological_space E} (h : t â‰¤ t') {s : set E}
  (hs : @is_vonN_bounded ğ•œ E _ _ _ t s) : @is_vonN_bounded ğ•œ E _ _ _ t' s :=
Î» V hV, hs $ (le_iff_nhds t t').mp h 0 hV

end multiple_topologies

section image

variables {ğ•œâ‚ ğ•œâ‚‚ : Type*} [normed_division_ring ğ•œâ‚] [normed_division_ring ğ•œâ‚‚]
  [add_comm_group E] [module ğ•œâ‚ E] [add_comm_group F] [module ğ•œâ‚‚ F]
  [topological_space E] [topological_space F]

/-- A continuous linear image of a bounded set is bounded. -/
lemma is_vonN_bounded.image {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚} [ring_hom_surjective Ïƒ] [ring_hom_isometric Ïƒ]
  {s : set E} (hs : is_vonN_bounded ğ•œâ‚ s) (f : E â†’SL[Ïƒ] F) :
  is_vonN_bounded ğ•œâ‚‚ (f '' s) :=
begin
  let Ïƒ' := ring_equiv.of_bijective Ïƒ âŸ¨Ïƒ.injective, Ïƒ.is_surjectiveâŸ©,
  have Ïƒ_iso : isometry Ïƒ := add_monoid_hom_class.isometry_of_norm Ïƒ
    (Î» x, ring_hom_isometric.is_iso),
  have Ïƒ'_symm_iso : isometry Ïƒ'.symm := Ïƒ_iso.right_inv Ïƒ'.right_inv,
  have f_tendsto_zero := f.continuous.tendsto 0,
  rw map_zero at f_tendsto_zero,
  intros V hV,
  rcases hs (f_tendsto_zero hV) with âŸ¨r, hrpos, hrâŸ©,
  refine âŸ¨r, hrpos, Î» a ha, _âŸ©,
  rw â† Ïƒ'.apply_symm_apply a,
  have hanz : a â‰  0 := norm_pos_iff.mp (hrpos.trans_le ha),
  have : Ïƒ'.symm a â‰  0 := (map_ne_zero Ïƒ'.symm.to_ring_hom).mpr hanz,
  change _ âŠ† Ïƒ _ â€¢ _,
  rw [set.image_subset_iff, preimage_smul_setâ‚›â‚— _ _ _ f this.is_unit],
  refine hr (Ïƒ'.symm a) _,
  rwa Ïƒ'_symm_iso.norm_map_of_map_zero (map_zero _)
end

end image

section sequence

variables {ğ• : Type*} [normed_field ğ•œ] [nontrivially_normed_field ğ•] [add_comm_group E] [module ğ•œ E]
  [module ğ• E] [topological_space E] [has_continuous_smul ğ• E]

lemma is_vonN_bounded.smul_tendsto_zero {S : set E} {Îµ : Î¹ â†’ ğ•œ} {x : Î¹ â†’ E} {l : filter Î¹}
  (hS : is_vonN_bounded ğ•œ S) (hxS : âˆ€á¶  n in l, x n âˆˆ S) (hÎµ : tendsto Îµ l (ğ“ 0)) :
  tendsto (Îµ â€¢ x) l (ğ“ 0) :=
begin
  rw tendsto_def at *,
  intros V hV,
  rcases hS hV with âŸ¨r, r_pos, hrSâŸ©,
  filter_upwards [hxS, hÎµ _ (metric.ball_mem_nhds 0 $ inv_pos.mpr r_pos)] with n hnS hnr,
  by_cases this : Îµ n = 0,
  { simp [this, mem_of_mem_nhds hV] },
  { rw [mem_preimage, mem_ball_zero_iff, lt_inv (norm_pos_iff.mpr this) r_pos, â† norm_inv] at hnr,
    rw [mem_preimage, pi.smul_apply', â† set.mem_inv_smul_set_iffâ‚€ this],
    exact hrS _ (hnr.le) hnS },
end

lemma is_vonN_bounded_of_smul_tendsto_zero {Îµ : Î¹ â†’ ğ•} {l : filter Î¹} [l.ne_bot]
  (hÎµ : âˆ€á¶  n in l, Îµ n â‰  0) {S : set E}
  (H : âˆ€ x : Î¹ â†’ E, (âˆ€ n, x n âˆˆ S) â†’ tendsto (Îµ â€¢ x) l (ğ“ 0)) :
  is_vonN_bounded ğ• S :=
begin
  rw (nhds_basis_balanced ğ• E).is_vonN_bounded_basis_iff,
  by_contra' H',
  rcases H' with âŸ¨V, âŸ¨hV, hVbâŸ©, hVSâŸ©,
  have : âˆ€á¶  n in l, âˆƒ x : S, (Îµ n) â€¢ (x : E) âˆ‰ V,
  { filter_upwards [hÎµ] with n hn,
    rw absorbs at hVS,
    push_neg at hVS,
    rcases hVS _ (norm_pos_iff.mpr $ inv_ne_zero hn) with âŸ¨a, haÎµ, haSâŸ©,
    rcases set.not_subset.mp haS with âŸ¨x, hxS, hxâŸ©,
    refine âŸ¨âŸ¨x, hxSâŸ©, Î» hnx, _âŸ©,
    rw â† set.mem_inv_smul_set_iffâ‚€ hn at hnx,
    exact hx (hVb.smul_mono haÎµ hnx) },
  rcases this.choice with âŸ¨x, hxâŸ©,
  refine filter.frequently_false l (filter.eventually.frequently _),
  filter_upwards [hx, (H (coe âˆ˜ x) (Î» n, (x n).2)).eventually (eventually_mem_set.mpr hV)]
    using Î» n, id
end

/-- Given any sequence `Îµ` of scalars which tends to `ğ“[â‰ ] 0`, we have that a set `S` is bounded
  if and only if for any sequence `x : â„• â†’ S`, `Îµ â€¢ x` tends to 0. This actually works for any
  indexing type `Î¹`, but in the special case `Î¹ = â„•` we get the important fact that convergent
  sequences fully characterize bounded sets. -/
lemma is_vonN_bounded_iff_smul_tendsto_zero {Îµ : Î¹ â†’ ğ•} {l : filter Î¹} [l.ne_bot]
  (hÎµ : tendsto Îµ l (ğ“[â‰ ] 0)) {S : set E} :
  is_vonN_bounded ğ• S â†” âˆ€ x : Î¹ â†’ E, (âˆ€ n, x n âˆˆ S) â†’ tendsto (Îµ â€¢ x) l (ğ“ 0) :=
âŸ¨Î» hS x hxS, hS.smul_tendsto_zero (eventually_of_forall hxS) (le_trans hÎµ nhds_within_le_nhds),
  is_vonN_bounded_of_smul_tendsto_zero (hÎµ self_mem_nhds_within)âŸ©

end sequence

section normed_field

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [topological_space E] [has_continuous_smul ğ•œ E]

/-- Singletons are bounded. -/
lemma is_vonN_bounded_singleton (x : E) : is_vonN_bounded ğ•œ ({x} : set E) :=
Î» V hV, (absorbent_nhds_zero hV).absorbs

/-- The union of all bounded set is the whole space. -/
lemma is_vonN_bounded_covers : â‹ƒâ‚€ (set_of (is_vonN_bounded ğ•œ)) = (set.univ : set E) :=
set.eq_univ_iff_forall.mpr (Î» x, set.mem_sUnion.mpr
  âŸ¨{x}, is_vonN_bounded_singleton _, set.mem_singleton _âŸ©)

variables (ğ•œ E)

/-- The von Neumann bornology defined by the von Neumann bounded sets.

Note that this is not registered as an instance, in order to avoid diamonds with the
metric bornology.-/
@[reducible] -- See note [reducible non-instances]
def vonN_bornology : bornology E :=
bornology.of_bounded (set_of (is_vonN_bounded ğ•œ)) (is_vonN_bounded_empty ğ•œ E)
  (Î» _ hs _ ht, hs.subset ht) (Î» _ hs _, hs.union) is_vonN_bounded_singleton

variables {E}

@[simp] lemma is_bounded_iff_is_vonN_bounded {s : set E} :
  @is_bounded _ (vonN_bornology ğ•œ E) s â†” is_vonN_bounded ğ•œ s :=
is_bounded_of_bounded_iff _

end normed_field

end bornology

section uniform_add_group

variables (ğ•œ) [nontrivially_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [uniform_space E] [uniform_add_group E] [has_continuous_smul ğ•œ E]

lemma totally_bounded.is_vonN_bounded {s : set E} (hs : totally_bounded s) :
  bornology.is_vonN_bounded ğ•œ s :=
begin
  rw totally_bounded_iff_subset_finite_Union_nhds_zero at hs,
  intros U hU,
  have h : filter.tendsto (Î» (x : E Ã— E), x.fst + x.snd) (ğ“ (0,0)) (ğ“ ((0 : E) + (0 : E))) :=
    tendsto_add,
  rw add_zero at h,
  have h' := (nhds_basis_balanced ğ•œ E).prod (nhds_basis_balanced ğ•œ E),
  simp_rw [â†nhds_prod_eq, id.def] at h',
  rcases h.basis_left h' U hU with âŸ¨x, hx, h''âŸ©,
  rcases hs x.snd hx.2.1 with âŸ¨t, ht, hsâŸ©,
  refine absorbs.mono_right _ hs,
  rw ht.absorbs_Union,
  have hx_fstsnd : x.fst + x.snd âŠ† U,
  { intros z hz,
    rcases set.mem_add.mp hz with âŸ¨z1, z2, hz1, hz2, hzâŸ©,
    have hz' : (z1, z2) âˆˆ x.fst Ã—Ë¢ x.snd := âŸ¨hz1, hz2âŸ©,
    simpa only [hz] using h'' hz' },
  refine Î» y hy, absorbs.mono_left _ hx_fstsnd,
  rw [â†set.singleton_vadd, vadd_eq_add],
  exact (absorbent_nhds_zero hx.1.1).absorbs.add hx.2.2.absorbs_self,
end

end uniform_add_group

section vonN_bornology_eq_metric

variables (ğ•œ E) [nontrivially_normed_field ğ•œ] [seminormed_add_comm_group E] [normed_space ğ•œ E]

namespace normed_space

lemma is_vonN_bounded_ball (r : â„) :
  bornology.is_vonN_bounded ğ•œ (metric.ball (0 : E) r) :=
begin
  rw [metric.nhds_basis_ball.is_vonN_bounded_basis_iff, â† ball_norm_seminorm ğ•œ E],
  exact Î» Îµ hÎµ, (norm_seminorm ğ•œ E).ball_zero_absorbs_ball_zero hÎµ
end

lemma is_vonN_bounded_closed_ball (r : â„) :
  bornology.is_vonN_bounded ğ•œ (metric.closed_ball (0 : E) r) :=
(is_vonN_bounded_ball ğ•œ E (r+1)).subset (metric.closed_ball_subset_ball $ by linarith)

lemma is_vonN_bounded_iff (s : set E) :
  bornology.is_vonN_bounded ğ•œ s â†” bornology.is_bounded s :=
begin
  rw [â† metric.bounded_iff_is_bounded, metric.bounded_iff_subset_ball (0 : E)],
  split,
  { intros h,
    rcases h (metric.ball_mem_nhds 0 zero_lt_one) with âŸ¨Ï, hÏ, hÏballâŸ©,
    rcases normed_field.exists_lt_norm ğ•œ Ï with âŸ¨a, haâŸ©,
    specialize hÏball a ha.le,
    rw [â† ball_norm_seminorm ğ•œ E, seminorm.smul_ball_zero (norm_pos_iff.1 $ hÏ.trans ha),
        ball_norm_seminorm, mul_one] at hÏball,
    exact âŸ¨â€–aâ€–, hÏball.trans metric.ball_subset_closed_ballâŸ© },
  { exact Î» âŸ¨C, hCâŸ©, (is_vonN_bounded_closed_ball ğ•œ E C).subset hC }
end

lemma is_vonN_bounded_iff' (s : set E) :
  bornology.is_vonN_bounded ğ•œ s â†” âˆƒ r : â„, âˆ€ (x : E) (hx : x âˆˆ s), â€–xâ€– â‰¤ r :=
by rw [normed_space.is_vonN_bounded_iff, â†metric.bounded_iff_is_bounded, bounded_iff_forall_norm_le]

lemma image_is_vonN_bounded_iff (f : E' â†’ E) (s : set E') :
  bornology.is_vonN_bounded ğ•œ (f '' s) â†” âˆƒ r : â„, âˆ€ (x : E') (hx : x âˆˆ s), â€–f xâ€– â‰¤ r :=
by simp_rw [is_vonN_bounded_iff', set.ball_image_iff]

/-- In a normed space, the von Neumann bornology (`bornology.vonN_bornology`) is equal to the
metric bornology. -/
lemma vonN_bornology_eq : bornology.vonN_bornology ğ•œ E = pseudo_metric_space.to_bornology :=
begin
  rw bornology.ext_iff_is_bounded,
  intro s,
  rw bornology.is_bounded_iff_is_vonN_bounded,
  exact is_vonN_bounded_iff ğ•œ E s
end

variable (ğ•œ)

lemma is_bounded_iff_subset_smul_ball {s : set E} :
  bornology.is_bounded s â†” âˆƒ a : ğ•œ, s âŠ† a â€¢ metric.ball 0 1 :=
begin
  rw â† is_vonN_bounded_iff ğ•œ,
  split,
  { intros h,
    rcases h (metric.ball_mem_nhds 0 zero_lt_one) with âŸ¨Ï, hÏ, hÏballâŸ©,
    rcases normed_field.exists_lt_norm ğ•œ Ï with âŸ¨a, haâŸ©,
    exact âŸ¨a, hÏball a ha.leâŸ© },
  { rintros âŸ¨a, haâŸ©,
    exact ((is_vonN_bounded_ball ğ•œ E 1).image (a â€¢ 1 : E â†’L[ğ•œ] E)).subset ha }
end

lemma is_bounded_iff_subset_smul_closed_ball {s : set E} :
  bornology.is_bounded s â†” âˆƒ a : ğ•œ, s âŠ† a â€¢ metric.closed_ball 0 1 :=
begin
  split,
  { rw is_bounded_iff_subset_smul_ball ğ•œ,
    exact exists_imp_exists
      (Î» a ha, ha.trans $ set.smul_set_mono $ metric.ball_subset_closed_ball) },
  { rw â† is_vonN_bounded_iff ğ•œ,
    rintros âŸ¨a, haâŸ©,
    exact ((is_vonN_bounded_closed_ball ğ•œ E 1).image (a â€¢ 1 : E â†’L[ğ•œ] E)).subset ha }
end

end normed_space

end vonN_bornology_eq_metric
