/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import analysis.locally_convex.bounded
import data.is_R_or_C.basic

/-!
# Continuity and Von Neumann boundedness

This files proves that for `E` and `F` two topological vector spaces over `â„` or `â„‚`,
if `E` is first countable, then every locally bounded linear map `E â†’â‚›â‚—[Ïƒ] F` is continuous
(this is `linear_map.continuous_of_locally_bounded`).

We keep this file separate from `analysis/locally_convex/bounded` in order not to import
`analysis/normed_space/is_R_or_C` there, because defining the strong topology on the space of
continuous linear maps will require importing `analysis/locally_convex/bounded` in
`analysis/normed_space/operator_norm`.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

-/

open topological_space bornology filter
open_locale topology pointwise

variables {ğ•œ ğ•œ' E F : Type*}
variables [add_comm_group E] [uniform_space E] [uniform_add_group E]
variables [add_comm_group F] [uniform_space F]

section nontrivially_normed_field

variables [uniform_add_group F]
variables [nontrivially_normed_field ğ•œ] [module ğ•œ E] [module ğ•œ F] [has_continuous_smul ğ•œ E]

/-- Construct a continuous linear map from a linear map `f : E â†’â‚—[ğ•œ] F` and the existence of a
neighborhood of zero that gets mapped into a bounded set in `F`. -/
def linear_map.clm_of_exists_bounded_image (f : E â†’â‚—[ğ•œ] F)
  (h : âˆƒ (V : set E) (hV : V âˆˆ ğ“ (0 : E)), bornology.is_vonN_bounded ğ•œ (f '' V)) : E â†’L[ğ•œ] F :=
âŸ¨f, begin
  -- It suffices to show that `f` is continuous at `0`.
  refine continuous_of_continuous_at_zero f _,
  rw [continuous_at_def, f.map_zero],
  intros U hU,
  -- Continuity means that `U âˆˆ ğ“ 0` implies that `f â»Â¹' U âˆˆ ğ“ 0`.
  rcases h with âŸ¨V, hV, hâŸ©,
  rcases h hU with âŸ¨r, hr, hâŸ©,
  rcases normed_field.exists_lt_norm ğ•œ r with âŸ¨x, hxâŸ©,
  specialize h x hx.le,
  -- After unfolding all the definitions, we know that `f '' V âŠ† x â€¢ U`. We use this to show the
  -- inclusion `xâ»Â¹ â€¢ V âŠ† fâ»Â¹' U`.
  have x_ne := norm_pos_iff.mp (hr.trans hx),
  have : xâ»Â¹ â€¢ V âŠ† fâ»Â¹' U :=
  calc xâ»Â¹ â€¢ V âŠ†  xâ»Â¹ â€¢ (fâ»Â¹' (f '' V)) : set.smul_set_mono (set.subset_preimage_image â‡‘f V)
  ... âŠ† xâ»Â¹ â€¢ (fâ»Â¹' (x â€¢ U)) : set.smul_set_mono (set.preimage_mono h)
  ... = fâ»Â¹' (xâ»Â¹ â€¢ (x â€¢ U)) :
      by ext; simp only [set.mem_inv_smul_set_iffâ‚€ x_ne, set.mem_preimage, linear_map.map_smul]
  ... âŠ† fâ»Â¹' U : by rw inv_smul_smulâ‚€ x_ne _,
  -- Using this inclusion, it suffices to show that `xâ»Â¹ â€¢ V` is in `ğ“ 0`, which is trivial.
  refine mem_of_superset _ this,
  convert set_smul_mem_nhds_smul hV (inv_ne_zero x_ne),
  exact (smul_zero _).symm,
endâŸ©

lemma linear_map.clm_of_exists_bounded_image_coe {f : E â†’â‚—[ğ•œ] F}
  {h : âˆƒ (V : set E) (hV : V âˆˆ ğ“ (0 : E)), bornology.is_vonN_bounded ğ•œ (f '' V)} :
  (f.clm_of_exists_bounded_image h : E â†’â‚—[ğ•œ] F) = f := rfl

@[simp] lemma linear_map.clm_of_exists_bounded_image_apply {f : E â†’â‚—[ğ•œ] F}
  {h : âˆƒ (V : set E) (hV : V âˆˆ ğ“ (0 : E)), bornology.is_vonN_bounded ğ•œ (f '' V)} {x : E} :
  f.clm_of_exists_bounded_image h x = f x := rfl

end nontrivially_normed_field

section is_R_or_C

open topological_space bornology

variables [first_countable_topology E]
variables [is_R_or_C ğ•œ] [module ğ•œ E] [has_continuous_smul ğ•œ E]
variables [is_R_or_C ğ•œ'] [module ğ•œ' F] [has_continuous_smul ğ•œ' F]
variables {Ïƒ : ğ•œ â†’+* ğ•œ'}

lemma linear_map.continuous_at_zero_of_locally_bounded (f : E â†’â‚›â‚—[Ïƒ] F)
  (hf : âˆ€ (s : set E) (hs : is_vonN_bounded ğ•œ s), is_vonN_bounded ğ•œ' (f '' s)) :
  continuous_at f 0 :=
begin
  -- Assume that f is not continuous at 0
  by_contradiction,
  -- We use a decreasing balanced basis for 0 : E and a balanced basis for 0 : F
  -- and reformulate non-continuity in terms of these bases
  rcases (nhds_basis_balanced ğ•œ E).exists_antitone_subbasis with âŸ¨b, bE1, bEâŸ©,
  simp only [id.def] at bE,
  have bE' : (ğ“ (0 : E)).has_basis (Î» (x : â„•), x â‰  0) (Î» n : â„•, (n : ğ•œ)â»Â¹ â€¢ b n) :=
  begin
    refine bE.1.to_has_basis _ _,
    { intros n _,
      use n+1,
      simp only [ne.def, nat.succ_ne_zero, not_false_iff, nat.cast_add, nat.cast_one, true_and],
      -- `b (n + 1) âŠ† b n` follows from `antitone`.
      have h : b (n + 1) âŠ† b n := bE.2 (by simp),
      refine subset_trans _ h,
      rintros y âŸ¨x, hx, hyâŸ©,
      -- Since `b (n + 1)` is balanced `(n+1)â»Â¹ b (n + 1) âŠ† b (n + 1)`
      rw â†hy,
      refine (bE1 (n+1)).2.smul_mem  _ hx,
      have h' : 0 < (n : â„) + 1 := n.cast_add_one_pos,
      rw [norm_inv, â†nat.cast_one, â†nat.cast_add, is_R_or_C.norm_eq_abs, is_R_or_C.abs_cast_nat,
        nat.cast_add, nat.cast_one, inv_le h' zero_lt_one],
      norm_cast,
      simp, },
    intros n hn,
    -- The converse direction follows from continuity of the scalar multiplication
    have hcont : continuous_at (Î» (x : E), (n : ğ•œ) â€¢ x) 0 :=
    (continuous_const_smul (n : ğ•œ)).continuous_at,
    simp only [continuous_at, map_zero, smul_zero] at hcont,
    rw bE.1.tendsto_left_iff at hcont,
    rcases hcont (b n) (bE1 n).1 with âŸ¨i, _, hiâŸ©,
    refine âŸ¨i, trivial, Î» x hx, âŸ¨(n : ğ•œ) â€¢ x, hi hx, _âŸ©âŸ©,
    simp [â†mul_smul, hn],
  end,
  rw [continuous_at, map_zero, bE'.tendsto_iff (nhds_basis_balanced ğ•œ' F)] at h,
  push_neg at h,
  rcases h with âŸ¨V, âŸ¨hV, hV'âŸ©, hâŸ©,
  simp only [id.def, forall_true_left] at h,
  -- There exists `u : â„• â†’ E` such that for all `n : â„•` we have `u n âˆˆ nâ»Â¹ â€¢ b n` and `f (u n) âˆ‰ V`
  choose! u hu hu' using h,
  -- The sequence `(Î» n, n â€¢ u n)` converges to `0`
  have h_tendsto : tendsto (Î» n : â„•, (n : ğ•œ) â€¢ u n) at_top (ğ“ (0 : E)) :=
  begin
    apply bE.tendsto,
    intros n,
    by_cases h : n = 0,
    { rw [h, nat.cast_zero, zero_smul],
      refine mem_of_mem_nhds (bE.1.mem_of_mem $ by triv) },
    rcases hu n h with âŸ¨y, hy, hu1âŸ©,
    convert hy,
    rw [â†hu1, â†mul_smul],
    simp only [h, mul_inv_cancel, ne.def, nat.cast_eq_zero, not_false_iff, one_smul],
  end,
  -- The image `(Î» n, n â€¢ u n)` is von Neumann bounded:
  have h_bounded : is_vonN_bounded ğ•œ (set.range (Î» n : â„•, (n : ğ•œ) â€¢ u n)) :=
  h_tendsto.cauchy_seq.totally_bounded_range.is_vonN_bounded ğ•œ,
  -- Since `range u` is bounded it absorbs `V`
  rcases hf _ h_bounded hV with âŸ¨r, hr, h'âŸ©,
  cases exists_nat_gt r with n hn,
  -- We now find a contradiction between `f (u n) âˆ‰ V` and the absorbing property
  have h1 : r â‰¤ â€–(n : ğ•œ')â€– :=
  by { rw [is_R_or_C.norm_eq_abs, is_R_or_C.abs_cast_nat], exact hn.le },
  have hn' : 0 < â€–(n : ğ•œ')â€– := lt_of_lt_of_le hr h1,
  rw [norm_pos_iff, ne.def, nat.cast_eq_zero] at hn',
  have h'' : f (u n) âˆˆ V :=
  begin
    simp only [set.image_subset_iff] at h',
    specialize h' (n : ğ•œ') h1 (set.mem_range_self n),
    simp only [set.mem_preimage, linear_map.map_smulâ‚›â‚—, map_nat_cast] at h',
    rcases h' with âŸ¨y, hy, h'âŸ©,
    apply_fun (Î» y : F, (n : ğ•œ')â»Â¹ â€¢ y) at h',
    simp only [hn', inv_smul_smulâ‚€, ne.def, nat.cast_eq_zero, not_false_iff] at h',
    rwa â†h',
  end,
  exact hu' n hn' h'',
end

/-- If `E` is first countable, then every locally bounded linear map `E â†’â‚›â‚—[Ïƒ] F` is continuous. -/
lemma linear_map.continuous_of_locally_bounded [uniform_add_group F] (f : E â†’â‚›â‚—[Ïƒ] F)
  (hf : âˆ€ (s : set E) (hs : is_vonN_bounded ğ•œ s), is_vonN_bounded ğ•œ' (f '' s)) :
  continuous f :=
(uniform_continuous_of_continuous_at_zero f $ f.continuous_at_zero_of_locally_bounded hf).continuous

end is_R_or_C
