/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, Anatole Dedecker
-/

import analysis.seminorm
import analysis.locally_convex.bounded
import topology.algebra.filter_basis
import topology.algebra.module.locally_convex

/-!
# Topology induced by a family of seminorms

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

## Main definitions

* `seminorm_family.basis_sets`: The set of open seminorm balls for a family of seminorms.
* `seminorm_family.module_filter_basis`: A module filter basis formed by the open balls.
* `seminorm.is_bounded`: A linear map `f : E â†’â‚—[ğ•œ] F` is bounded iff every seminorm in `F` can be
bounded by a finite number of seminorms in `E`.

## Main statements

* `with_seminorms.to_locally_convex_space`: A space equipped with a family of seminorms is locally
convex.
* `with_seminorms.first_countable`: A space is first countable if it's topology is induced by a
countable family of seminorms.

## Continuity of semilinear maps

If `E` and `F` are topological vector space with the topology induced by a family of seminorms, then
we have a direct method to prove that a linear map is continuous:
* `seminorm.continuous_from_bounded`: A bounded linear map `f : E â†’â‚—[ğ•œ] F` is continuous.

If the topology of a space `E` is induced by a family of seminorms, then we can characterize von
Neumann boundedness in terms of that seminorm family. Together with
`linear_map.continuous_of_locally_bounded` this gives general criterion for continuity.

* `with_seminorms.is_vonN_bounded_iff_finset_seminorm_bounded`
* `with_seminorms.is_vonN_bounded_iff_seminorm_bounded`
* `with_seminorms.image_is_vonN_bounded_iff_finset_seminorm_bounded`
* `with_seminorms.image_is_vonN_bounded_iff_seminorm_bounded`

## Tags

seminorm, locally convex
-/

open normed_field set seminorm topological_space
open_locale big_operators nnreal pointwise topology

variables {ğ•œ ğ•œâ‚‚ ğ• ğ•â‚‚ E F G Î¹ Î¹' : Type*}

section filter_basis

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]

variables (ğ•œ E Î¹)

/-- An abbreviation for indexed families of seminorms. This is mainly to allow for dot-notation. -/
abbreviation seminorm_family := Î¹ â†’ seminorm ğ•œ E

variables {ğ•œ E Î¹}

namespace seminorm_family

/-- The sets of a filter basis for the neighborhood filter of 0. -/
def basis_sets (p : seminorm_family ğ•œ E Î¹) : set (set E) :=
â‹ƒ (s : finset Î¹) r (hr : 0 < r), singleton $ ball (s.sup p) (0 : E) r

variables (p : seminorm_family ğ•œ E Î¹)

lemma basis_sets_iff {U : set E} :
  U âˆˆ p.basis_sets â†” âˆƒ (i : finset Î¹) r (hr : 0 < r), U = ball (i.sup p) 0 r :=
by simp only [basis_sets, mem_Union, mem_singleton_iff]

lemma basis_sets_mem (i : finset Î¹) {r : â„} (hr : 0 < r) :
  (i.sup p).ball 0 r âˆˆ p.basis_sets :=
(basis_sets_iff _).mpr âŸ¨i,_,hr,rflâŸ©

lemma basis_sets_singleton_mem (i : Î¹) {r : â„} (hr : 0 < r) :
  (p i).ball 0 r âˆˆ p.basis_sets :=
(basis_sets_iff _).mpr âŸ¨{i},_,hr, by rw finset.sup_singletonâŸ©

lemma basis_sets_nonempty [nonempty Î¹] : p.basis_sets.nonempty :=
begin
  let i := classical.arbitrary Î¹,
  refine set.nonempty_def.mpr âŸ¨(p i).ball 0 1, _âŸ©,
  exact p.basis_sets_singleton_mem i zero_lt_one,
end

lemma basis_sets_intersect
  (U V : set E) (hU : U âˆˆ p.basis_sets) (hV : V âˆˆ p.basis_sets) :
  âˆƒ (z : set E) (H : z âˆˆ p.basis_sets), z âŠ† U âˆ© V :=
begin
  classical,
  rcases p.basis_sets_iff.mp hU with âŸ¨s, râ‚, hrâ‚, hUâŸ©,
  rcases p.basis_sets_iff.mp hV with âŸ¨t, râ‚‚, hrâ‚‚, hVâŸ©,
  use ((s âˆª t).sup p).ball 0 (min râ‚ râ‚‚),
  refine âŸ¨p.basis_sets_mem (s âˆª t) (lt_min_iff.mpr âŸ¨hrâ‚, hrâ‚‚âŸ©), _âŸ©,
  rw [hU, hV, ball_finset_sup_eq_Inter _ _ _ (lt_min_iff.mpr âŸ¨hrâ‚, hrâ‚‚âŸ©),
    ball_finset_sup_eq_Inter _ _ _ hrâ‚, ball_finset_sup_eq_Inter _ _ _ hrâ‚‚],
  exact set.subset_inter
    (set.Interâ‚‚_mono' $ Î» i hi, âŸ¨i, finset.subset_union_left _ _ hi, ball_mono $ min_le_left _ _âŸ©)
    (set.Interâ‚‚_mono' $ Î» i hi, âŸ¨i, finset.subset_union_right _ _ hi, ball_mono $
    min_le_right _ _âŸ©),
end

lemma basis_sets_zero (U) (hU : U âˆˆ p.basis_sets) :
  (0 : E) âˆˆ U :=
begin
  rcases p.basis_sets_iff.mp hU with âŸ¨Î¹', r, hr, hUâŸ©,
  rw [hU, mem_ball_zero, map_zero],
  exact hr,
end

lemma basis_sets_add (U) (hU : U âˆˆ p.basis_sets) :
  âˆƒ (V : set E) (H : V âˆˆ p.basis_sets), V + V âŠ† U :=
begin
  rcases p.basis_sets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©,
  use (s.sup p).ball 0 (r/2),
  refine âŸ¨p.basis_sets_mem s (div_pos hr zero_lt_two), _âŸ©,
  refine set.subset.trans (ball_add_ball_subset (s.sup p) (r/2) (r/2) 0 0) _,
  rw [hU, add_zero, add_halves'],
end

lemma basis_sets_neg (U) (hU' : U âˆˆ p.basis_sets) :
  âˆƒ (V : set E) (H : V âˆˆ p.basis_sets), V âŠ† (Î» (x : E), -x) â»Â¹' U :=
begin
  rcases p.basis_sets_iff.mp hU' with âŸ¨s, r, hr, hUâŸ©,
  rw [hU, neg_preimage, neg_ball (s.sup p), neg_zero],
  exact âŸ¨U, hU', eq.subset hUâŸ©,
end

/-- The `add_group_filter_basis` induced by the filter basis `seminorm_basis_zero`. -/
protected def add_group_filter_basis [nonempty Î¹] : add_group_filter_basis E :=
add_group_filter_basis_of_comm p.basis_sets p.basis_sets_nonempty
  p.basis_sets_intersect p.basis_sets_zero p.basis_sets_add p.basis_sets_neg

lemma basis_sets_smul_right (v : E) (U : set E)
  (hU : U âˆˆ p.basis_sets) : âˆ€á¶  (x : ğ•œ) in ğ“ 0, x â€¢ v âˆˆ U :=
begin
  rcases p.basis_sets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©,
  rw [hU, filter.eventually_iff],
  simp_rw [(s.sup p).mem_ball_zero, map_smul_eq_mul],
  by_cases h : 0 < (s.sup p) v,
  { simp_rw (lt_div_iff h).symm,
    rw â†_root_.ball_zero_eq,
    exact metric.ball_mem_nhds 0 (div_pos hr h) },
  simp_rw [le_antisymm (not_lt.mp h) (map_nonneg _ v), mul_zero, hr],
  exact is_open.mem_nhds is_open_univ (mem_univ 0),
end

variables [nonempty Î¹]

lemma basis_sets_smul (U) (hU : U âˆˆ p.basis_sets) :
  âˆƒ (V : set ğ•œ) (H : V âˆˆ ğ“ (0 : ğ•œ)) (W : set E)
  (H : W âˆˆ p.add_group_filter_basis.sets), V â€¢ W âŠ† U :=
begin
  rcases p.basis_sets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©,
  refine âŸ¨metric.ball 0 r.sqrt, metric.ball_mem_nhds 0 (real.sqrt_pos.mpr hr), _âŸ©,
  refine âŸ¨(s.sup p).ball 0 r.sqrt, p.basis_sets_mem s (real.sqrt_pos.mpr hr), _âŸ©,
  refine set.subset.trans (ball_smul_ball (s.sup p) r.sqrt r.sqrt) _,
  rw [hU, real.mul_self_sqrt (le_of_lt hr)],
end

lemma basis_sets_smul_left (x : ğ•œ) (U : set E)
  (hU : U âˆˆ p.basis_sets) : âˆƒ (V : set E)
  (H : V âˆˆ p.add_group_filter_basis.sets), V âŠ† (Î» (y : E), x â€¢ y) â»Â¹' U :=
begin
  rcases p.basis_sets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©,
  rw hU,
  by_cases h : x â‰  0,
  { rw [(s.sup p).smul_ball_preimage 0 r x h, smul_zero],
    use (s.sup p).ball 0 (r / â€–xâ€–),
    exact âŸ¨p.basis_sets_mem s (div_pos hr (norm_pos_iff.mpr h)), subset.rflâŸ© },
  refine âŸ¨(s.sup p).ball 0 r, p.basis_sets_mem s hr, _âŸ©,
  simp only [not_ne_iff.mp h, subset_def, mem_ball_zero, hr, mem_univ, map_zero,
    implies_true_iff, preimage_const_of_mem, zero_smul],
end

/-- The `module_filter_basis` induced by the filter basis `seminorm_basis_zero`. -/
protected def module_filter_basis : module_filter_basis ğ•œ E :=
{ to_add_group_filter_basis := p.add_group_filter_basis,
  smul' := p.basis_sets_smul,
  smul_left' := p.basis_sets_smul_left,
  smul_right' := p.basis_sets_smul_right }

lemma filter_eq_infi (p : seminorm_family ğ•œ E Î¹) :
  p.module_filter_basis.to_filter_basis.filter = â¨… i, (ğ“ 0).comap (p i) :=
begin
  refine le_antisymm (le_infi $ Î» i, _) _,
  { rw p.module_filter_basis.to_filter_basis.has_basis.le_basis_iff
      (metric.nhds_basis_ball.comap _),
    intros Îµ hÎµ,
    refine âŸ¨(p i).ball 0 Îµ, _, _âŸ©,
    { rw â† (finset.sup_singleton : _ = p i),
      exact p.basis_sets_mem {i} hÎµ, },
    { rw [id, (p i).ball_zero_eq_preimage_ball] } },
  { rw p.module_filter_basis.to_filter_basis.has_basis.ge_iff,
    rintros U (hU : U âˆˆ p.basis_sets),
    rcases p.basis_sets_iff.mp hU with âŸ¨s, r, hr, rflâŸ©,
    rw [id, seminorm.ball_finset_sup_eq_Inter _ _ _ hr, s.Inter_mem_sets],
    exact Î» i hi, filter.mem_infi_of_mem i âŸ¨metric.ball 0 r, metric.ball_mem_nhds 0 hr,
      eq.subset ((p i).ball_zero_eq_preimage_ball).symmâŸ©, },
end

end seminorm_family

end filter_basis

section bounded

namespace seminorm

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [normed_field ğ•œâ‚‚] [add_comm_group F] [module ğ•œâ‚‚ F]
variables {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚]

-- Todo: This should be phrased entirely in terms of the von Neumann bornology.

/-- The proposition that a linear map is bounded between spaces with families of seminorms. -/
def is_bounded (p : Î¹ â†’ seminorm ğ•œ E) (q : Î¹' â†’ seminorm ğ•œâ‚‚ F) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) : Prop :=
  âˆ€ i, âˆƒ s : finset Î¹, âˆƒ C : â„â‰¥0, (q i).comp f â‰¤ C â€¢ s.sup p

lemma is_bounded_const (Î¹' : Type*) [nonempty Î¹']
  {p : Î¹ â†’ seminorm ğ•œ E} {q : seminorm ğ•œâ‚‚ F} (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
  is_bounded p (Î» _ : Î¹', q) f â†” âˆƒ (s : finset Î¹) C : â„â‰¥0, q.comp f â‰¤ C â€¢ s.sup p :=
by simp only [is_bounded, forall_const]

lemma const_is_bounded (Î¹ : Type*) [nonempty Î¹]
  {p : seminorm ğ•œ E} {q : Î¹' â†’ seminorm ğ•œâ‚‚ F} (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
  is_bounded (Î» _ : Î¹, p) q f â†” âˆ€ i, âˆƒ C : â„â‰¥0, (q i).comp f â‰¤ C â€¢ p :=
begin
  split; intros h i,
  { rcases h i with âŸ¨s, C, hâŸ©,
    exact âŸ¨C, le_trans h (smul_le_smul (finset.sup_le (Î» _ _, le_rfl)) le_rfl)âŸ© },
  use [{classical.arbitrary Î¹}],
  simp only [h, finset.sup_singleton],
end

lemma is_bounded_sup {p : Î¹ â†’ seminorm ğ•œ E} {q : Î¹' â†’ seminorm ğ•œâ‚‚ F}
  {f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F} (hf : is_bounded p q f) (s' : finset Î¹') :
  âˆƒ (C : â„â‰¥0) (s : finset Î¹), (s'.sup q).comp f â‰¤ C â€¢ (s.sup p) :=
begin
  classical,
  obtain rfl | hs' := s'.eq_empty_or_nonempty,
  { exact âŸ¨1, âˆ…, by simp [seminorm.bot_eq_zero]âŸ© },
  choose fâ‚› fC hf using hf,
  use [s'.card â€¢ s'.sup fC, finset.bUnion s' fâ‚›],
  have hs : âˆ€ i : Î¹', i âˆˆ s' â†’ (q i).comp f â‰¤ s'.sup fC â€¢ ((finset.bUnion s' fâ‚›).sup p) :=
  begin
    intros i hi,
    refine (hf i).trans (smul_le_smul _ (finset.le_sup hi)),
    exact finset.sup_mono (finset.subset_bUnion_of_mem fâ‚› hi),
  end,
  refine (comp_mono f (finset_sup_le_sum q s')).trans _,
  simp_rw [â†pullback_apply, add_monoid_hom.map_sum, pullback_apply],
  refine (finset.sum_le_sum hs).trans _,
  rw [finset.sum_const, smul_assoc],
  exact le_rfl,
end

end seminorm

end bounded

section topology

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] [nonempty Î¹]

/-- The proposition that the topology of `E` is induced by a family of seminorms `p`. -/
structure with_seminorms (p : seminorm_family ğ•œ E Î¹) [t : topological_space E] : Prop :=
(topology_eq_with_seminorms : t = p.module_filter_basis.topology)

lemma with_seminorms.with_seminorms_eq {p : seminorm_family ğ•œ E Î¹} [t : topological_space E]
  (hp : with_seminorms p) : t = p.module_filter_basis.topology := hp.1

variables [topological_space E]
variables {p : seminorm_family ğ•œ E Î¹}

lemma with_seminorms.topological_add_group (hp : with_seminorms p) : topological_add_group E :=
begin
  rw hp.with_seminorms_eq,
  exact add_group_filter_basis.is_topological_add_group _
end

lemma with_seminorms.has_basis (hp : with_seminorms p) : (ğ“ (0 : E)).has_basis
  (Î» (s : set E), s âˆˆ p.basis_sets) id :=
begin
  rw (congr_fun (congr_arg (@nhds E) hp.1) 0),
  exact add_group_filter_basis.nhds_zero_has_basis _,
end

lemma with_seminorms.has_basis_zero_ball (hp : with_seminorms p) : (ğ“ (0 : E)).has_basis
  (Î» sr : finset Î¹ Ã— â„, 0 < sr.2) (Î» sr, (sr.1.sup p).ball 0 sr.2) :=
begin
  refine âŸ¨Î» V, _âŸ©,
  simp only [hp.has_basis.mem_iff, seminorm_family.basis_sets_iff, prod.exists],
  split,
  { rintros âŸ¨-, âŸ¨s, r, hr, rflâŸ©, hVâŸ©,
    exact âŸ¨s, r, hr, hVâŸ© },
  { rintros âŸ¨s, r, hr, hVâŸ©,
    exact âŸ¨_, âŸ¨s, r, hr, rflâŸ©, hVâŸ© }
end

lemma with_seminorms.has_basis_ball (hp : with_seminorms p) {x : E} : (ğ“ (x : E)).has_basis
  (Î» sr : finset Î¹ Ã— â„, 0 < sr.2) (Î» sr, (sr.1.sup p).ball x sr.2) :=
begin
  haveI : topological_add_group E := hp.topological_add_group,
  rw [â† map_add_left_nhds_zero],
  convert (hp.has_basis_zero_ball.map ((+) x)),
  ext sr : 1,
  have : (sr.fst.sup p).ball (x +áµ¥ 0) sr.snd = x +áµ¥ (sr.fst.sup p).ball 0 sr.snd
    := eq.symm (seminorm.vadd_ball (sr.fst.sup p)),
  rwa [vadd_eq_add, add_zero] at this,
end

/-- The `x`-neighbourhoods of a space whose topology is induced by a family of seminorms
are exactly the sets which contain seminorm balls around `x`.-/
lemma with_seminorms.mem_nhds_iff (hp : with_seminorms p) (x : E) (U : set E) :
  U âˆˆ nhds x â†” âˆƒ (s : finset Î¹) (r > 0), (s.sup p).ball x r âŠ† U :=
by rw [hp.has_basis_ball.mem_iff, prod.exists]

/-- The open sets of a space whose topology is induced by a family of seminorms
are exactly the sets which contain seminorm balls around all of their points.-/
lemma with_seminorms.is_open_iff_mem_balls (hp : with_seminorms p) (U : set E) :
  is_open U â†” âˆ€ x âˆˆ U, âˆƒ (s : finset Î¹) (r > 0), (s.sup p).ball x r âŠ† U :=
by simp_rw [â†with_seminorms.mem_nhds_iff hp _ U, is_open_iff_mem_nhds]

/- Note that through the following lemmas, one also immediately has that separating families
of seminorms induce Tâ‚‚ and Tâ‚ƒ topologies by `topological_add_group.t2_space`
and `topological_add_group.t3_space` -/
/-- A separating family of seminorms induces a Tâ‚ topology. -/
lemma with_seminorms.t1_of_separating (hp : with_seminorms p) (h : âˆ€ x â‰  0, âˆƒ i, p i x â‰  0) :
  t1_space E :=
begin
  haveI := hp.topological_add_group,
  refine topological_add_group.t1_space _ _,
  rw [â† is_open_compl_iff, hp.is_open_iff_mem_balls],
  rintros x (hx : x â‰  0),
  cases h x hx with i pi_nonzero,
  refine âŸ¨{i}, p i x, by positivity, subset_compl_singleton_iff.mpr _âŸ©,
  rw [finset.sup_singleton, mem_ball, zero_sub, map_neg_eq_map, not_lt]
end

/-- A family of seminorms inducing a Tâ‚ topology is separating. -/
lemma with_seminorms.separating_of_t1 [t1_space E] (hp : with_seminorms p) (x : E) (hx : x â‰  0) :
  âˆƒ i, p i x â‰  0 :=
begin
  have := ((t1_space_tfae E).out 0 9).mp infer_instance,
  by_contra' h,
  refine hx (this _),
  rw hp.has_basis_zero_ball.specializes_iff,
  rintros âŸ¨s, râŸ© (hr : 0 < r),
  simp only [ball_finset_sup_eq_Inter _ _ _ hr, mem_Interâ‚‚, mem_ball_zero, h, hr, forall_true_iff],
end

/-- A family of seminorms is separating iff it induces a Tâ‚ topology. -/
lemma with_seminorms.separating_iff_t1 (hp : with_seminorms p) :
  (âˆ€ x â‰  0, âˆƒ i, p i x â‰  0) â†” t1_space E :=
begin
  refine âŸ¨with_seminorms.t1_of_separating hp, _âŸ©,
  introI,
  exact with_seminorms.separating_of_t1 hp,
end

end topology

section tendsto

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] [nonempty Î¹] [topological_space E]
variables {p : seminorm_family ğ•œ E Î¹}

/-- Convergence along filters for `with_seminorms`.

Variant with `finset.sup`. -/
lemma with_seminorms.tendsto_nhds' (hp : with_seminorms p) (u : F â†’ E) {f : filter F} (yâ‚€ : E) :
  filter.tendsto u f (ğ“ yâ‚€) â†” âˆ€ (s : finset Î¹) Îµ, 0 < Îµ â†’ âˆ€á¶  x in f, s.sup p (u x - yâ‚€) < Îµ :=
by simp [hp.has_basis_ball.tendsto_right_iff]

/-- Convergence along filters for `with_seminorms`. -/
lemma with_seminorms.tendsto_nhds (hp : with_seminorms p) (u : F â†’ E) {f : filter F} (yâ‚€ : E) :
  filter.tendsto u f (ğ“ yâ‚€) â†” âˆ€ i Îµ, 0 < Îµ â†’ âˆ€á¶  x in f, p i (u x - yâ‚€) < Îµ :=
begin
  rw hp.tendsto_nhds' u yâ‚€,
  exact âŸ¨Î» h i, by simpa only [finset.sup_singleton] using h {i},
    Î» h s Îµ hÎµ, (s.eventually_all.2 $ Î» i _, h i Îµ hÎµ).mono (Î» _, finset_sup_apply_lt hÎµ)âŸ©,
end

variables [semilattice_sup F] [nonempty F]

/-- Limit `â†’ âˆ` for `with_seminorms`. -/
lemma with_seminorms.tendsto_nhds_at_top (hp : with_seminorms p) (u : F â†’ E) (yâ‚€ : E) :
  filter.tendsto u filter.at_top (ğ“ yâ‚€) â†” âˆ€ i Îµ, 0 < Îµ â†’ âˆƒ xâ‚€, âˆ€ x, xâ‚€ â‰¤ x â†’ p i (u x - yâ‚€) < Îµ :=
begin
  rw hp.tendsto_nhds u yâ‚€,
  exact forallâ‚ƒ_congr (Î» _ _ _, filter.eventually_at_top),
end

end tendsto

section topological_add_group

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [t : topological_space E] [topological_add_group E]
variables [nonempty Î¹]

include t

lemma seminorm_family.with_seminorms_of_nhds (p : seminorm_family ğ•œ E Î¹)
  (h : ğ“ (0 : E) = p.module_filter_basis.to_filter_basis.filter) :
  with_seminorms p :=
begin
  refine âŸ¨topological_add_group.ext infer_instance
    (p.add_group_filter_basis.is_topological_add_group) _âŸ©,
  rw add_group_filter_basis.nhds_zero_eq,
  exact h,
end

lemma seminorm_family.with_seminorms_of_has_basis (p : seminorm_family ğ•œ E Î¹)
  (h : (ğ“ (0 : E)).has_basis (Î» (s : set E), s âˆˆ p.basis_sets) id) :
  with_seminorms p :=
p.with_seminorms_of_nhds $ filter.has_basis.eq_of_same_basis h
  p.add_group_filter_basis.to_filter_basis.has_basis

lemma seminorm_family.with_seminorms_iff_nhds_eq_infi (p : seminorm_family ğ•œ E Î¹) :
  with_seminorms p â†” (ğ“ 0 : filter E) = â¨… i, (ğ“ 0).comap (p i) :=
begin
  rw â† p.filter_eq_infi,
  refine âŸ¨Î» h, _, p.with_seminorms_of_nhdsâŸ©,
  rw h.topology_eq_with_seminorms,
  exact add_group_filter_basis.nhds_zero_eq _,
end

lemma with_seminorms.continuous_seminorm [nontrivially_normed_field ğ•]
  [module ğ• E] [has_continuous_const_smul ğ• E] {p : seminorm_family ğ• E Î¹} (hp : with_seminorms p)
  (i : Î¹) : continuous (p i) :=
begin
  refine seminorm.continuous one_pos _,
  rw [p.with_seminorms_iff_nhds_eq_infi.mp hp, ball_zero_eq_preimage_ball],
  exact filter.mem_infi_of_mem i (filter.preimage_mem_comap $ metric.ball_mem_nhds _ one_pos)
end

/-- The topology induced by a family of seminorms is exactly the infimum of the ones induced by
each seminorm individually. We express this as a characterization of `with_seminorms p`. -/
lemma seminorm_family.with_seminorms_iff_topological_space_eq_infi (p : seminorm_family ğ•œ E Î¹) :
  with_seminorms p â†” t = â¨… i, (p i).to_add_group_seminorm.to_seminormed_add_comm_group
    .to_uniform_space.to_topological_space :=
begin
  rw [p.with_seminorms_iff_nhds_eq_infi, topological_add_group.ext_iff infer_instance
        (topological_add_group_infi $ Î» i, infer_instance), nhds_infi],
  congrm (_ = â¨… i, _),
  exact @comap_norm_nhds_zero _ (p i).to_add_group_seminorm.to_seminormed_add_group,
  all_goals {apply_instance}
end

omit t

/-- The uniform structure induced by a family of seminorms is exactly the infimum of the ones
induced by each seminorm individually. We express this as a characterization of
`with_seminorms p`. -/
lemma seminorm_family.with_seminorms_iff_uniform_space_eq_infi [u : uniform_space E]
  [uniform_add_group E] (p : seminorm_family ğ•œ E Î¹) :
  with_seminorms p â†” u = â¨… i, (p i).to_add_group_seminorm.to_seminormed_add_comm_group
    .to_uniform_space :=
begin
  rw [p.with_seminorms_iff_nhds_eq_infi, uniform_add_group.ext_iff infer_instance
        (uniform_add_group_infi $ Î» i, infer_instance), to_topological_space_infi, nhds_infi],
  congrm (_ = â¨… i, _),
  exact @comap_norm_nhds_zero _ (p i).to_add_group_seminorm.to_seminormed_add_group,
  all_goals {apply_instance}
end

end topological_add_group

section normed_space

/-- The topology of a `normed_space ğ•œ E` is induced by the seminorm `norm_seminorm ğ•œ E`. -/
lemma norm_with_seminorms (ğ•œ E) [normed_field ğ•œ] [seminormed_add_comm_group E] [normed_space ğ•œ E] :
  with_seminorms (Î» (_ : fin 1), norm_seminorm ğ•œ E) :=
begin
  let p : seminorm_family ğ•œ E (fin 1) := Î» _, norm_seminorm ğ•œ E,
  refine âŸ¨seminormed_add_comm_group.to_topological_add_group.ext
    p.add_group_filter_basis.is_topological_add_group _âŸ©,
  refine filter.has_basis.eq_of_same_basis metric.nhds_basis_ball _,
  rw â†ball_norm_seminorm ğ•œ E,
  refine filter.has_basis.to_has_basis p.add_group_filter_basis.nhds_zero_has_basis _
    (Î» r hr, âŸ¨(norm_seminorm ğ•œ E).ball 0 r, p.basis_sets_singleton_mem 0 hr, rfl.subsetâŸ©),
  rintros U (hU : U âˆˆ p.basis_sets),
  rcases p.basis_sets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©,
  use [r, hr],
  rw [hU, id.def],
  by_cases h : s.nonempty,
  { rw finset.sup_const h },
  rw [finset.not_nonempty_iff_eq_empty.mp h, finset.sup_empty, ball_bot _ hr],
  exact set.subset_univ _,
end

end normed_space

section nontrivially_normed_field

variables [nontrivially_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] [nonempty Î¹]
variables {p : seminorm_family ğ•œ E Î¹}
variables [topological_space E]

lemma with_seminorms.is_vonN_bounded_iff_finset_seminorm_bounded {s : set E}
  (hp : with_seminorms p) :
  bornology.is_vonN_bounded ğ•œ s â†” âˆ€ I : finset Î¹, âˆƒ r (hr : 0 < r), âˆ€ (x âˆˆ s), I.sup p x < r :=
begin
  rw (hp.has_basis).is_vonN_bounded_basis_iff,
  split,
  { intros h I,
    simp only [id.def] at h,
    specialize h ((I.sup p).ball 0 1) (p.basis_sets_mem I zero_lt_one),
    rcases h with âŸ¨r, hr, hâŸ©,
    cases normed_field.exists_lt_norm ğ•œ r with a ha,
    specialize h a (le_of_lt ha),
    rw [seminorm.smul_ball_zero (norm_pos_iff.1 $ hr.trans ha), mul_one] at h,
    refine âŸ¨â€–aâ€–, lt_trans hr ha, _âŸ©,
    intros x hx,
    specialize h hx,
    exact (finset.sup I p).mem_ball_zero.mp h },
  intros h s' hs',
  rcases p.basis_sets_iff.mp hs' with âŸ¨I, r, hr, hs'âŸ©,
  rw [id.def, hs'],
  rcases h I with âŸ¨r', hr', h'âŸ©,
  simp_rw â†(I.sup p).mem_ball_zero at h',
  refine absorbs.mono_right _ h',
  exact (finset.sup I p).ball_zero_absorbs_ball_zero hr,
end

lemma with_seminorms.image_is_vonN_bounded_iff_finset_seminorm_bounded (f : G â†’ E) {s : set G}
  (hp : with_seminorms p) : bornology.is_vonN_bounded ğ•œ (f '' s) â†”
  âˆ€ I : finset Î¹, âˆƒ r (hr : 0 < r), âˆ€ (x âˆˆ s), I.sup p (f x) < r :=
by simp_rw [hp.is_vonN_bounded_iff_finset_seminorm_bounded, set.ball_image_iff]

lemma with_seminorms.is_vonN_bounded_iff_seminorm_bounded {s : set E} (hp : with_seminorms p) :
  bornology.is_vonN_bounded ğ•œ s â†” âˆ€ i : Î¹, âˆƒ r (hr : 0 < r), âˆ€ (x âˆˆ s), p i x < r :=
begin
  rw hp.is_vonN_bounded_iff_finset_seminorm_bounded,
  split,
  { intros hI i,
    convert hI {i},
    rw [finset.sup_singleton] },
  intros hi I,
  by_cases hI : I.nonempty,
  { choose r hr h using hi,
    have h' : 0 < I.sup' hI r :=
    by { rcases hI.bex with âŸ¨i, hiâŸ©, exact lt_of_lt_of_le (hr i) (finset.le_sup' r hi) },
    refine âŸ¨I.sup' hI r, h', Î» x hx, finset_sup_apply_lt h' (Î» i hi, _)âŸ©,
    refine lt_of_lt_of_le (h i x hx) _,
    simp only [finset.le_sup'_iff, exists_prop],
    exact âŸ¨i, hi, (eq.refl _).leâŸ© },
  simp only [finset.not_nonempty_iff_eq_empty.mp hI, finset.sup_empty, coe_bot, pi.zero_apply,
    exists_prop],
  exact âŸ¨1, zero_lt_one, Î» _ _, zero_lt_oneâŸ©,
end

lemma with_seminorms.image_is_vonN_bounded_iff_seminorm_bounded (f : G â†’ E) {s : set G}
  (hp : with_seminorms p) :
  bornology.is_vonN_bounded ğ•œ (f '' s) â†” âˆ€ i : Î¹, âˆƒ r (hr : 0 < r), âˆ€ (x âˆˆ s), p i (f x) < r :=
by simp_rw [hp.is_vonN_bounded_iff_seminorm_bounded, set.ball_image_iff]

end nontrivially_normed_field
section continuous_bounded

namespace seminorm

variables [nontrivially_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [normed_field ğ•] [module ğ• E]
variables [nontrivially_normed_field ğ•œâ‚‚] [add_comm_group F] [module ğ•œâ‚‚ F]
variables [normed_field ğ•â‚‚] [module ğ•â‚‚ F]
variables {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚]
variables {Ï„â‚â‚‚ : ğ• â†’+* ğ•â‚‚} [ring_hom_isometric Ï„â‚â‚‚]
variables [nonempty Î¹] [nonempty Î¹']

lemma continuous_of_continuous_comp {q : seminorm_family ğ•â‚‚ F Î¹'}
  [topological_space E] [topological_add_group E]
  [topological_space F] [topological_add_group F] (hq : with_seminorms q)
  (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F) (hf : âˆ€ i, continuous ((q i).comp f)) : continuous f :=
begin
  refine continuous_of_continuous_at_zero f _,
  simp_rw [continuous_at, f.map_zero, q.with_seminorms_iff_nhds_eq_infi.mp hq, filter.tendsto_infi,
            filter.tendsto_comap_iff],
  intros i,
  convert (hf i).continuous_at,
  exact (map_zero _).symm
end

lemma continuous_iff_continuous_comp
  {q : seminorm_family ğ•œâ‚‚ F Î¹'} [topological_space E] [topological_add_group E]
  [topological_space F] [topological_add_group F] [has_continuous_const_smul ğ•œâ‚‚ F]
  (hq : with_seminorms q) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
  continuous f â†” âˆ€ i, continuous ((q i).comp f) :=
âŸ¨Î» h i, continuous.comp (hq.continuous_seminorm i) h, continuous_of_continuous_comp hq fâŸ©

lemma continuous_from_bounded {p : seminorm_family ğ• E Î¹} {q : seminorm_family ğ•â‚‚ F Î¹'}
  [topological_space E] [topological_add_group E] (hp : with_seminorms p)
  [topological_space F] [topological_add_group F] (hq : with_seminorms q)
  (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F) (hf : seminorm.is_bounded p q f) : continuous f :=
begin
  refine continuous_of_continuous_comp hq _ (Î» i, seminorm.continuous_of_continuous_at_zero _),
  rw [metric.continuous_at_iff', map_zero],
  intros r hr,
  rcases hf i with âŸ¨sâ‚, C, hfâŸ©,
  have hC' : 0 < C + 1 := by positivity,
  rw hp.has_basis.eventually_iff,
  refine âŸ¨(sâ‚.sup p).ball 0 (r/(C + 1)), p.basis_sets_mem _ (by positivity), _âŸ©,
  simp_rw [ â†metric.mem_ball, â†mem_preimage, â†ball_zero_eq_preimage_ball],
  refine subset.trans _ (ball_antitone hf),
  norm_cast,
  rw â† ball_smul (sâ‚.sup p) hC',
  refine ball_antitone (smul_le_smul le_rfl _),
  simp only [le_add_iff_nonneg_right, zero_le'],
end

lemma cont_with_seminorms_normed_space (F) [seminormed_add_comm_group F] [normed_space ğ•â‚‚ F]
  [uniform_space E] [uniform_add_group E]
  {p : Î¹ â†’ seminorm ğ• E} (hp : with_seminorms p) (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F)
  (hf : âˆƒ (s : finset Î¹) C : â„â‰¥0, (norm_seminorm ğ•â‚‚ F).comp f â‰¤ C â€¢ s.sup p) :
  continuous f :=
begin
  rw â†seminorm.is_bounded_const (fin 1) at hf,
  exact continuous_from_bounded hp (norm_with_seminorms ğ•â‚‚ F) f hf,
end

lemma cont_normed_space_to_with_seminorms (E) [seminormed_add_comm_group E] [normed_space ğ• E]
  [uniform_space F] [uniform_add_group F]
  {q : Î¹ â†’ seminorm ğ•â‚‚ F} (hq : with_seminorms q) (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F)
  (hf : âˆ€ i : Î¹, âˆƒ C : â„â‰¥0, (q i).comp f â‰¤ C â€¢ (norm_seminorm ğ• E)) : continuous f :=
begin
  rw â†seminorm.const_is_bounded (fin 1) at hf,
  exact continuous_from_bounded (norm_with_seminorms ğ• E) hq f hf,
end

end seminorm

end continuous_bounded

section locally_convex_space

open locally_convex_space

variables [nonempty Î¹] [normed_field ğ•œ] [normed_space â„ ğ•œ]
  [add_comm_group E] [module ğ•œ E] [module â„ E] [is_scalar_tower â„ ğ•œ E] [topological_space E]
  [topological_add_group E]

lemma with_seminorms.to_locally_convex_space {p : seminorm_family ğ•œ E Î¹} (hp : with_seminorms p) :
  locally_convex_space â„ E :=
begin
  apply of_basis_zero â„ E id (Î» s, s âˆˆ p.basis_sets),
  { rw [hp.1, add_group_filter_basis.nhds_eq _, add_group_filter_basis.N_zero],
    exact filter_basis.has_basis _ },
  { intros s hs,
    change s âˆˆ set.Union _ at hs,
    simp_rw [set.mem_Union, set.mem_singleton_iff] at hs,
    rcases hs with âŸ¨I, r, hr, rflâŸ©,
    exact convex_ball _ _ _ }
end

end locally_convex_space

section normed_space

variables (ğ•œ) [normed_field ğ•œ] [normed_space â„ ğ•œ] [seminormed_add_comm_group E]

/-- Not an instance since `ğ•œ` can't be inferred. See `normed_space.to_locally_convex_space` for a
slightly weaker instance version. -/
lemma normed_space.to_locally_convex_space' [normed_space ğ•œ E] [module â„ E]
  [is_scalar_tower â„ ğ•œ E] : locally_convex_space â„ E :=
(norm_with_seminorms ğ•œ E).to_locally_convex_space

/-- See `normed_space.to_locally_convex_space'` for a slightly stronger version which is not an
instance. -/
instance normed_space.to_locally_convex_space [normed_space â„ E] :
  locally_convex_space â„ E :=
normed_space.to_locally_convex_space' â„

end normed_space

section topological_constructions

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]
variables [normed_field ğ•œâ‚‚] [add_comm_group F] [module ğ•œâ‚‚ F]
variables {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚]

/-- The family of seminorms obtained by composing each seminorm by a linear map. -/
def seminorm_family.comp (q : seminorm_family ğ•œâ‚‚ F Î¹) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
  seminorm_family ğ•œ E Î¹ :=
Î» i, (q i).comp f

lemma seminorm_family.comp_apply (q : seminorm_family ğ•œâ‚‚ F Î¹) (i : Î¹) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
  q.comp f i = (q i).comp f :=
rfl

lemma seminorm_family.finset_sup_comp (q : seminorm_family ğ•œâ‚‚ F Î¹) (s : finset Î¹)
  (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) : (s.sup q).comp f = s.sup (q.comp f) :=
begin
  ext x,
  rw [seminorm.comp_apply, seminorm.finset_sup_apply, seminorm.finset_sup_apply],
  refl
end

variables [topological_space F] [topological_add_group F]

lemma linear_map.with_seminorms_induced [hÎ¹ : nonempty Î¹] {q : seminorm_family ğ•œâ‚‚ F Î¹}
  (hq : with_seminorms q) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
  @with_seminorms ğ•œ E Î¹ _ _ _ _ (q.comp f) (induced f infer_instance) :=
begin
  letI : topological_space E := induced f infer_instance,
  letI : topological_add_group E := topological_add_group_induced f,
  rw [(q.comp f).with_seminorms_iff_nhds_eq_infi, nhds_induced, map_zero,
      q.with_seminorms_iff_nhds_eq_infi.mp hq, filter.comap_infi],
  refine infi_congr (Î» i, _),
  exact filter.comap_comap
end

lemma inducing.with_seminorms [hÎ¹ : nonempty Î¹] {q : seminorm_family ğ•œâ‚‚ F Î¹}
  (hq : with_seminorms q) [topological_space E] {f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F} (hf : inducing f) :
  with_seminorms (q.comp f) :=
begin
  rw hf.induced,
  exact f.with_seminorms_induced hq
end

end topological_constructions

section topological_properties

variables [nontrivially_normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] [nonempty Î¹] [countable Î¹]
variables {p : seminorm_family ğ•œ E Î¹}
variables [uniform_space E] [uniform_add_group E]

/-- If the topology of a space is induced by a countable family of seminorms, then the topology
is first countable. -/
lemma with_seminorms.first_countable (hp : with_seminorms p) :
  topological_space.first_countable_topology E :=
begin
  haveI : (ğ“ (0 : E)).is_countably_generated,
  { rw p.with_seminorms_iff_nhds_eq_infi.mp hp,
    exact filter.infi.is_countably_generated _ },
  haveI : (uniformity E).is_countably_generated := uniform_add_group.uniformity_countably_generated,
  exact uniform_space.first_countable_topology E,
end

end topological_properties
