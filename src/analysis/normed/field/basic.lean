/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl
-/
import algebra.algebra.subalgebra.basic
import analysis.normed.group.basic
import topology.instances.ennreal

/-!
# Normed fields

In this file we define (semi)normed rings and fields. We also prove some theorems about these
definitions.
-/

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Type*}

open filter metric
open_locale topology big_operators nnreal ennreal uniformity pointwise

/-- A non-unital seminormed ring is a not-necessarily-unital ring
endowed with a seminorm which satisfies the inequality `â€–x yâ€– â‰¤ â€–xâ€– â€–yâ€–`. -/
class non_unital_semi_normed_ring (Î± : Type*)
  extends has_norm Î±, non_unital_ring Î±, pseudo_metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul : âˆ€ a b, norm (a * b) â‰¤ norm a * norm b)

/-- A seminormed ring is a ring endowed with a seminorm which satisfies the inequality
`â€–x yâ€– â‰¤ â€–xâ€– â€–yâ€–`. -/
class semi_normed_ring (Î± : Type*) extends has_norm Î±, ring Î±, pseudo_metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul : âˆ€ a b, norm (a * b) â‰¤ norm a * norm b)

/-- A seminormed ring is a non-unital seminormed ring. -/
@[priority 100] -- see Note [lower instance priority]
instance semi_normed_ring.to_non_unital_semi_normed_ring [Î² : semi_normed_ring Î±] :
  non_unital_semi_normed_ring Î± :=
{ ..Î² }

/-- A non-unital normed ring is a not-necessarily-unital ring
endowed with a norm which satisfies the inequality `â€–x yâ€– â‰¤ â€–xâ€– â€–yâ€–`. -/
class non_unital_normed_ring (Î± : Type*) extends has_norm Î±, non_unital_ring Î±, metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul : âˆ€ a b, norm (a * b) â‰¤ norm a * norm b)

/-- A non-unital normed ring is a non-unital seminormed ring. -/
@[priority 100] -- see Note [lower instance priority]
instance non_unital_normed_ring.to_non_unital_semi_normed_ring [Î² : non_unital_normed_ring Î±] :
  non_unital_semi_normed_ring Î± :=
{ ..Î² }

/-- A normed ring is a ring endowed with a norm which satisfies the inequality `â€–x yâ€– â‰¤ â€–xâ€– â€–yâ€–`. -/
class normed_ring (Î± : Type*) extends has_norm Î±, ring Î±, metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul : âˆ€ a b, norm (a * b) â‰¤ norm a * norm b)

/-- A normed division ring is a division ring endowed with a seminorm which satisfies the equality
`â€–x yâ€– = â€–xâ€– â€–yâ€–`. -/
class normed_division_ring (Î± : Type*) extends has_norm Î±, division_ring Î±, metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul' : âˆ€ a b, norm (a * b) = norm a * norm b)

/-- A normed division ring is a normed ring. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_division_ring.to_normed_ring [Î² : normed_division_ring Î±] : normed_ring Î± :=
{ norm_mul := Î» a b, (normed_division_ring.norm_mul' a b).le,
  ..Î² }

/-- A normed ring is a seminormed ring. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_ring.to_semi_normed_ring [Î² : normed_ring Î±] : semi_normed_ring Î± :=
{ ..Î² }

/-- A normed ring is a non-unital normed ring. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_ring.to_non_unital_normed_ring [Î² : normed_ring Î±] : non_unital_normed_ring Î± :=
{ ..Î² }

/-- A seminormed commutative ring is a commutative ring endowed with a seminorm which satisfies
the inequality `â€–x yâ€– â‰¤ â€–xâ€– â€–yâ€–`. -/
class semi_normed_comm_ring (Î± : Type*) extends semi_normed_ring Î± :=
(mul_comm : âˆ€ x y : Î±, x * y = y * x)

/-- A normed commutative ring is a commutative ring endowed with a norm which satisfies
the inequality `â€–x yâ€– â‰¤ â€–xâ€– â€–yâ€–`. -/
class normed_comm_ring (Î± : Type*) extends normed_ring Î± :=
(mul_comm : âˆ€ x y : Î±, x * y = y * x)

/-- A normed commutative ring is a seminormed commutative ring. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_comm_ring.to_semi_normed_comm_ring [Î² : normed_comm_ring Î±] :
  semi_normed_comm_ring Î± := { ..Î² }

instance : normed_comm_ring punit :=
{ norm_mul := Î» _ _, by simp,
  ..punit.normed_add_comm_group,
  ..punit.comm_ring, }

/-- A mixin class with the axiom `â€–1â€– = 1`. Many `normed_ring`s and all `normed_field`s satisfy this
axiom. -/
class norm_one_class (Î± : Type*) [has_norm Î±] [has_one Î±] : Prop :=
(norm_one : â€–(1:Î±)â€– = 1)

export norm_one_class (norm_one)

attribute [simp] norm_one

@[simp] lemma nnnorm_one [seminormed_add_comm_group Î±] [has_one Î±] [norm_one_class Î±] :
  â€–(1 : Î±)â€–â‚Š = 1 :=
nnreal.eq norm_one

lemma norm_one_class.nontrivial (Î± : Type*) [seminormed_add_comm_group Î±] [has_one Î±]
  [norm_one_class Î±] :
  nontrivial Î± :=
nontrivial_of_ne 0 1 $ ne_of_apply_ne norm $ by simp

@[priority 100] -- see Note [lower instance priority]
instance semi_normed_comm_ring.to_comm_ring [Î² : semi_normed_comm_ring Î±] : comm_ring Î± := { ..Î² }

@[priority 100] -- see Note [lower instance priority]
instance non_unital_normed_ring.to_normed_add_comm_group [Î² : non_unital_normed_ring Î±] :
  normed_add_comm_group Î± :=
{ ..Î² }

@[priority 100] -- see Note [lower instance priority]
instance non_unital_semi_normed_ring.to_seminormed_add_comm_group [non_unital_semi_normed_ring Î±] :
  seminormed_add_comm_group Î± := { ..â€¹non_unital_semi_normed_ring Î±â€º }

instance [seminormed_add_comm_group Î±] [has_one Î±] [norm_one_class Î±] : norm_one_class (ulift Î±) :=
âŸ¨by simp [ulift.norm_def]âŸ©

instance prod.norm_one_class [seminormed_add_comm_group Î±] [has_one Î±] [norm_one_class Î±]
  [seminormed_add_comm_group Î²] [has_one Î²] [norm_one_class Î²] :
  norm_one_class (Î± Ã— Î²) :=
âŸ¨by simp [prod.norm_def]âŸ©

instance pi.norm_one_class {Î¹ : Type*} {Î± : Î¹ â†’ Type*} [nonempty Î¹] [fintype Î¹]
  [Î  i, seminormed_add_comm_group (Î± i)] [Î  i, has_one (Î± i)] [âˆ€ i, norm_one_class (Î± i)] :
  norm_one_class (Î  i, Î± i) :=
âŸ¨by simp [pi.norm_def, finset.sup_const finset.univ_nonempty]âŸ©

instance mul_opposite.norm_one_class [seminormed_add_comm_group Î±] [has_one Î±] [norm_one_class Î±] :
  norm_one_class Î±áµáµ’áµ– :=
âŸ¨@norm_one Î± _ _ _âŸ©

section non_unital_semi_normed_ring
variables [non_unital_semi_normed_ring Î±]

lemma norm_mul_le (a b : Î±) : (â€–a*bâ€–) â‰¤ (â€–aâ€–) * (â€–bâ€–) :=
non_unital_semi_normed_ring.norm_mul _ _

lemma nnnorm_mul_le (a b : Î±) : â€–a * bâ€–â‚Š â‰¤ â€–aâ€–â‚Š * â€–bâ€–â‚Š :=
by simpa only [â†norm_to_nnreal, â†real.to_nnreal_mul (norm_nonneg _)]
  using real.to_nnreal_mono (norm_mul_le _ _)

lemma one_le_norm_one (Î²) [normed_ring Î²] [nontrivial Î²] : 1 â‰¤ â€–(1 : Î²)â€– :=
(le_mul_iff_one_le_left $ norm_pos_iff.mpr (one_ne_zero : (1 : Î²) â‰  0)).mp
  (by simpa only [mul_one] using norm_mul_le (1 : Î²) 1)

lemma one_le_nnnorm_one (Î²) [normed_ring Î²] [nontrivial Î²] : 1 â‰¤ â€–(1 : Î²)â€–â‚Š :=
one_le_norm_one Î²

lemma filter.tendsto.zero_mul_is_bounded_under_le {f g : Î¹ â†’ Î±} {l : filter Î¹}
  (hf : tendsto f l (ğ“ 0)) (hg : is_bounded_under (â‰¤) l (norm âˆ˜ g)) :
  tendsto (Î» x, f x * g x) l (ğ“ 0) :=
hf.op_zero_is_bounded_under_le hg (*) norm_mul_le

lemma filter.is_bounded_under_le.mul_tendsto_zero {f g : Î¹ â†’ Î±} {l : filter Î¹}
  (hf : is_bounded_under (â‰¤) l (norm âˆ˜ f)) (hg : tendsto g l (ğ“ 0)) :
  tendsto (Î» x, f x * g x) l (ğ“ 0) :=
hg.op_zero_is_bounded_under_le hf (flip (*)) (Î» x y, ((norm_mul_le y x).trans_eq (mul_comm _ _)))

/-- In a seminormed ring, the left-multiplication `add_monoid_hom` is bounded. -/
lemma mul_left_bound (x : Î±) :
  âˆ€ (y:Î±), â€–add_monoid_hom.mul_left x yâ€– â‰¤ â€–xâ€– * â€–yâ€– :=
norm_mul_le x

/-- In a seminormed ring, the right-multiplication `add_monoid_hom` is bounded. -/
lemma mul_right_bound (x : Î±) :
  âˆ€ (y:Î±), â€–add_monoid_hom.mul_right x yâ€– â‰¤ â€–xâ€– * â€–yâ€– :=
Î» y, by {rw mul_comm, convert norm_mul_le y x}

instance : non_unital_semi_normed_ring (ulift Î±) :=
{ norm_mul := Î» x y, (norm_mul_le x.down y.down : _),
  .. ulift.seminormed_add_comm_group }

/-- Non-unital seminormed ring structure on the product of two non-unital seminormed rings,
  using the sup norm. -/
instance prod.non_unital_semi_normed_ring [non_unital_semi_normed_ring Î²] :
  non_unital_semi_normed_ring (Î± Ã— Î²) :=
{ norm_mul := assume x y,
  calc
    â€–x * yâ€– = â€–(x.1*y.1, x.2*y.2)â€– : rfl
        ... = (max â€–x.1*y.1â€–  â€–x.2*y.2â€–) : rfl
        ... â‰¤ (max (â€–x.1â€–*â€–y.1â€–) (â€–x.2â€–*â€–y.2â€–)) :
          max_le_max (norm_mul_le (x.1) (y.1)) (norm_mul_le (x.2) (y.2))
        ... = (max (â€–x.1â€–*â€–y.1â€–) (â€–y.2â€–*â€–x.2â€–)) : by simp[mul_comm]
        ... â‰¤ (max (â€–x.1â€–) (â€–x.2â€–)) * (max (â€–y.2â€–) (â€–y.1â€–)) :
          by apply max_mul_mul_le_max_mul_max; simp [norm_nonneg]
        ... = (max (â€–x.1â€–) (â€–x.2â€–)) * (max (â€–y.1â€–) (â€–y.2â€–)) : by simp [max_comm]
        ... = (â€–xâ€–*â€–yâ€–) : rfl,
  ..prod.seminormed_add_comm_group }

/-- Non-unital seminormed ring structure on the product of finitely many non-unital seminormed
rings, using the sup norm. -/
instance pi.non_unital_semi_normed_ring {Ï€ : Î¹ â†’ Type*} [fintype Î¹]
  [Î  i, non_unital_semi_normed_ring (Ï€ i)] :
  non_unital_semi_normed_ring (Î  i, Ï€ i) :=
{ norm_mul := Î» x y, nnreal.coe_mono $
    calc  finset.univ.sup (Î» i, â€–x i * y iâ€–â‚Š)
        â‰¤ finset.univ.sup ((Î» i, â€–x iâ€–â‚Š) * (Î» i, â€–y iâ€–â‚Š)) :
            finset.sup_mono_fun $ Î» b hb, norm_mul_le _ _
    ... â‰¤ finset.univ.sup (Î» i, â€–x iâ€–â‚Š) * finset.univ.sup (Î» i, â€–y iâ€–â‚Š) :
            finset.sup_mul_le_mul_sup_of_nonneg _ (Î» i _, zero_le _) (Î» i _, zero_le _),
  ..pi.seminormed_add_comm_group }

instance mul_opposite.non_unital_semi_normed_ring : non_unital_semi_normed_ring Î±áµáµ’áµ– :=
{ norm_mul := mul_opposite.rec $ Î» x, mul_opposite.rec $ Î» y,
    (norm_mul_le y x).trans_eq (mul_comm _ _),
  ..mul_opposite.seminormed_add_comm_group }

end non_unital_semi_normed_ring

section semi_normed_ring

variables [semi_normed_ring Î±]

/-- A subalgebra of a seminormed ring is also a seminormed ring, with the restriction of the norm.

See note [implicit instance arguments]. -/
instance subalgebra.semi_normed_ring {ğ•œ : Type*} {_ : comm_ring ğ•œ}
  {E : Type*} [semi_normed_ring E] {_ : algebra ğ•œ E} (s : subalgebra ğ•œ E) : semi_normed_ring s :=
{ norm_mul := Î» a b, norm_mul_le a.1 b.1,
  ..s.to_submodule.seminormed_add_comm_group }

/-- A subalgebra of a normed ring is also a normed ring, with the restriction of the norm.

See note [implicit instance arguments]. -/
instance subalgebra.normed_ring {ğ•œ : Type*} {_ : comm_ring ğ•œ}
  {E : Type*} [normed_ring E] {_ : algebra ğ•œ E} (s : subalgebra ğ•œ E) : normed_ring s :=
{ ..s.semi_normed_ring }

lemma nat.norm_cast_le : âˆ€ n : â„•, â€–(n : Î±)â€– â‰¤ n * â€–(1 : Î±)â€–
| 0 := by simp
| (n + 1) := by { rw [n.cast_succ, n.cast_succ, add_mul, one_mul],
                  exact norm_add_le_of_le (nat.norm_cast_le n) le_rfl }

lemma list.norm_prod_le' : âˆ€ {l : list Î±}, l â‰  [] â†’ â€–l.prodâ€– â‰¤ (l.map norm).prod
| [] h := (h rfl).elim
| [a] _ := by simp
| (a :: b :: l) _ :=
  begin
    rw [list.map_cons, list.prod_cons, @list.prod_cons _ _ _ â€–aâ€–],
    refine le_trans (norm_mul_le _ _) (mul_le_mul_of_nonneg_left _ (norm_nonneg _)),
    exact list.norm_prod_le' (list.cons_ne_nil b l)
  end

lemma list.nnnorm_prod_le' {l : list Î±} (hl : l â‰  []) : â€–l.prodâ€–â‚Š â‰¤ (l.map nnnorm).prod :=
(list.norm_prod_le' hl).trans_eq $ by simp [nnreal.coe_list_prod, list.map_map]

lemma list.norm_prod_le [norm_one_class Î±] : âˆ€ l : list Î±, â€–l.prodâ€– â‰¤ (l.map norm).prod
| [] := by simp
| (a::l) := list.norm_prod_le' (list.cons_ne_nil a l)

lemma list.nnnorm_prod_le [norm_one_class Î±] (l : list Î±) : â€–l.prodâ€–â‚Š â‰¤ (l.map nnnorm).prod :=
l.norm_prod_le.trans_eq $ by simp [nnreal.coe_list_prod, list.map_map]

lemma finset.norm_prod_le' {Î± : Type*} [normed_comm_ring Î±] (s : finset Î¹) (hs : s.nonempty)
  (f : Î¹ â†’ Î±) :
  â€–âˆ i in s, f iâ€– â‰¤ âˆ i in s, â€–f iâ€– :=
begin
  rcases s with âŸ¨âŸ¨lâŸ©, hlâŸ©,
  have : l.map f â‰  [], by simpa using hs,
  simpa using list.norm_prod_le' this
end

lemma finset.nnnorm_prod_le' {Î± : Type*} [normed_comm_ring Î±] (s : finset Î¹) (hs : s.nonempty)
  (f : Î¹ â†’ Î±) :
  â€–âˆ i in s, f iâ€–â‚Š â‰¤ âˆ i in s, â€–f iâ€–â‚Š :=
(s.norm_prod_le' hs f).trans_eq $ by simp [nnreal.coe_prod]

lemma finset.norm_prod_le {Î± : Type*} [normed_comm_ring Î±] [norm_one_class Î±] (s : finset Î¹)
  (f : Î¹ â†’ Î±) :
  â€–âˆ i in s, f iâ€– â‰¤ âˆ i in s, â€–f iâ€– :=
begin
  rcases s with âŸ¨âŸ¨lâŸ©, hlâŸ©,
  simpa using (l.map f).norm_prod_le
end

lemma finset.nnnorm_prod_le {Î± : Type*} [normed_comm_ring Î±] [norm_one_class Î±] (s : finset Î¹)
  (f : Î¹ â†’ Î±) :
  â€–âˆ i in s, f iâ€–â‚Š â‰¤ âˆ i in s, â€–f iâ€–â‚Š :=
(s.norm_prod_le f).trans_eq $ by simp [nnreal.coe_prod]

/-- If `Î±` is a seminormed ring, then `â€–a ^ nâ€–â‚Š â‰¤ â€–aâ€–â‚Š ^ n` for `n > 0`.
See also `nnnorm_pow_le`. -/
lemma nnnorm_pow_le' (a : Î±) : âˆ€ {n : â„•}, 0 < n â†’ â€–a ^ nâ€–â‚Š â‰¤ â€–aâ€–â‚Š ^ n
| 1 h := by simp only [pow_one]
| (n + 2) h := by simpa only [pow_succ _ (n + 1)] using
    le_trans (nnnorm_mul_le _ _) (mul_le_mul_left' (nnnorm_pow_le' n.succ_pos) _)

/-- If `Î±` is a seminormed ring with `â€–1â€–â‚Š = 1`, then `â€–a ^ nâ€–â‚Š â‰¤ â€–aâ€–â‚Š ^ n`.
See also `nnnorm_pow_le'`.-/
lemma nnnorm_pow_le [norm_one_class Î±] (a : Î±) (n : â„•) : â€–a ^ nâ€–â‚Š â‰¤ â€–aâ€–â‚Š ^ n :=
nat.rec_on n (by simp only [pow_zero, nnnorm_one]) (Î» k hk, nnnorm_pow_le' a k.succ_pos)

/-- If `Î±` is a seminormed ring, then `â€–a ^ nâ€– â‰¤ â€–aâ€– ^ n` for `n > 0`. See also `norm_pow_le`. -/
lemma norm_pow_le' (a : Î±) {n : â„•} (h : 0 < n) : â€–a ^ nâ€– â‰¤ â€–aâ€– ^ n :=
by simpa only [nnreal.coe_pow, coe_nnnorm] using nnreal.coe_mono (nnnorm_pow_le' a h)

/-- If `Î±` is a seminormed ring with `â€–1â€– = 1`, then `â€–a ^ nâ€– â‰¤ â€–aâ€– ^ n`. See also `norm_pow_le'`.-/
lemma norm_pow_le [norm_one_class Î±] (a : Î±) (n : â„•) : â€–a ^ nâ€– â‰¤ â€–aâ€– ^ n :=
nat.rec_on n (by simp only [pow_zero, norm_one]) (Î» n hn, norm_pow_le' a n.succ_pos)

lemma eventually_norm_pow_le (a : Î±) : âˆ€á¶  (n:â„•) in at_top, â€–a ^ nâ€– â‰¤ â€–aâ€– ^ n :=
eventually_at_top.mpr âŸ¨1, Î» b h, norm_pow_le' a (nat.succ_le_iff.mp h)âŸ©

instance : semi_normed_ring (ulift Î±) :=
{ .. ulift.non_unital_semi_normed_ring,
  .. ulift.seminormed_add_comm_group }

/-- Seminormed ring structure on the product of two seminormed rings,
  using the sup norm. -/
instance prod.semi_normed_ring [semi_normed_ring Î²] :
  semi_normed_ring (Î± Ã— Î²) :=
{ ..prod.non_unital_semi_normed_ring,
  ..prod.seminormed_add_comm_group, }

/-- Seminormed ring structure on the product of finitely many seminormed rings,
  using the sup norm. -/
instance pi.semi_normed_ring {Ï€ : Î¹ â†’ Type*} [fintype Î¹]
  [Î  i, semi_normed_ring (Ï€ i)] :
  semi_normed_ring (Î  i, Ï€ i) :=
{ ..pi.non_unital_semi_normed_ring,
  ..pi.seminormed_add_comm_group, }

instance mul_opposite.semi_normed_ring : semi_normed_ring Î±áµáµ’áµ– :=
{ ..mul_opposite.non_unital_semi_normed_ring,
  ..mul_opposite.seminormed_add_comm_group }

end semi_normed_ring

section non_unital_normed_ring
variables [non_unital_normed_ring Î±]

instance : non_unital_normed_ring (ulift Î±) :=
{ .. ulift.non_unital_semi_normed_ring,
  .. ulift.seminormed_add_comm_group }

/-- Non-unital normed ring structure on the product of two non-unital normed rings,
using the sup norm. -/
instance prod.non_unital_normed_ring [non_unital_normed_ring Î²] :
  non_unital_normed_ring (Î± Ã— Î²) :=
{ norm_mul := norm_mul_le,
  ..prod.seminormed_add_comm_group }

/-- Normed ring structure on the product of finitely many non-unital normed rings, using the sup
norm. -/
instance pi.non_unital_normed_ring {Ï€ : Î¹ â†’ Type*} [fintype Î¹] [Î  i, non_unital_normed_ring (Ï€ i)] :
  non_unital_normed_ring (Î  i, Ï€ i) :=
{ norm_mul := norm_mul_le,
  ..pi.normed_add_comm_group }

instance mul_opposite.non_unital_normed_ring : non_unital_normed_ring Î±áµáµ’áµ– :=
{ norm_mul := norm_mul_le,
  ..mul_opposite.normed_add_comm_group }

end non_unital_normed_ring

section normed_ring

variables [normed_ring Î±]

lemma units.norm_pos [nontrivial Î±] (x : Î±Ë£) : 0 < â€–(x:Î±)â€– :=
norm_pos_iff.mpr (units.ne_zero x)

lemma units.nnnorm_pos [nontrivial Î±] (x : Î±Ë£) : 0 < â€–(x:Î±)â€–â‚Š :=
x.norm_pos

instance : normed_ring (ulift Î±) :=
{ .. ulift.semi_normed_ring,
  .. ulift.normed_add_comm_group }

/-- Normed ring structure on the product of two normed rings, using the sup norm. -/
instance prod.normed_ring [normed_ring Î²] : normed_ring (Î± Ã— Î²) :=
{ norm_mul := norm_mul_le,
  ..prod.normed_add_comm_group }

/-- Normed ring structure on the product of finitely many normed rings, using the sup norm. -/
instance pi.normed_ring {Ï€ : Î¹ â†’ Type*} [fintype Î¹] [Î  i, normed_ring (Ï€ i)] :
  normed_ring (Î  i, Ï€ i) :=
{ norm_mul := norm_mul_le,
  ..pi.normed_add_comm_group }

instance mul_opposite.normed_ring : normed_ring Î±áµáµ’áµ– :=
{ norm_mul := norm_mul_le,
  ..mul_opposite.normed_add_comm_group }

end normed_ring

@[priority 100] -- see Note [lower instance priority]
instance semi_normed_ring_top_monoid [non_unital_semi_normed_ring Î±] : has_continuous_mul Î± :=
âŸ¨ continuous_iff_continuous_at.2 $ Î» x, tendsto_iff_norm_tendsto_zero.2 $
    begin
      have : âˆ€ e : Î± Ã— Î±, â€–e.1 * e.2 - x.1 * x.2â€– â‰¤ â€–e.1â€– * â€–e.2 - x.2â€– + â€–e.1 - x.1â€– * â€–x.2â€–,
      { intro e,
        calc â€–e.1 * e.2 - x.1 * x.2â€– â‰¤ â€–e.1 * (e.2 - x.2) + (e.1 - x.1) * x.2â€– :
          by rw [mul_sub, sub_mul, sub_add_sub_cancel]
        ... â‰¤ â€–e.1â€– * â€–e.2 - x.2â€– + â€–e.1 - x.1â€– * â€–x.2â€– :
          norm_add_le_of_le (norm_mul_le _ _) (norm_mul_le _ _) },
      refine squeeze_zero (Î» e, norm_nonneg _) this _,
      convert ((continuous_fst.tendsto x).norm.mul ((continuous_snd.tendsto x).sub
        tendsto_const_nhds).norm).add
        (((continuous_fst.tendsto x).sub tendsto_const_nhds).norm.mul _),
      show tendsto _ _ _, from tendsto_const_nhds,
      simp
    end âŸ©

/-- A seminormed ring is a topological ring. -/
@[priority 100] -- see Note [lower instance priority]
instance semi_normed_top_ring [non_unital_semi_normed_ring Î±] : topological_ring Î± := { }

section normed_division_ring

variables [normed_division_ring Î±]

@[simp] lemma norm_mul (a b : Î±) : â€–a * bâ€– = â€–aâ€– * â€–bâ€– :=
normed_division_ring.norm_mul' a b

@[priority 900]
instance normed_division_ring.to_norm_one_class : norm_one_class Î± :=
âŸ¨mul_left_cancelâ‚€ (mt norm_eq_zero.1 (one_ne_zero' Î±)) $
  by rw [â† norm_mul, mul_one, mul_one]âŸ©

instance is_absolute_value_norm : is_absolute_value (norm : Î± â†’ â„) :=
{ abv_nonneg := norm_nonneg,
  abv_eq_zero := Î» _, norm_eq_zero,
  abv_add := norm_add_le,
  abv_mul := norm_mul }

@[simp] lemma nnnorm_mul (a b : Î±) : â€–a * bâ€–â‚Š = â€–aâ€–â‚Š * â€–bâ€–â‚Š :=
nnreal.eq $ norm_mul a b

/-- `norm` as a `monoid_with_zero_hom`. -/
@[simps] def norm_hom : Î± â†’*â‚€ â„ := âŸ¨norm, norm_zero, norm_one, norm_mulâŸ©

/-- `nnnorm` as a `monoid_with_zero_hom`. -/
@[simps] def nnnorm_hom : Î± â†’*â‚€ â„â‰¥0 := âŸ¨nnnorm, nnnorm_zero, nnnorm_one, nnnorm_mulâŸ©

@[simp] lemma norm_pow (a : Î±) : âˆ€ (n : â„•), â€–a ^ nâ€– = â€–aâ€– ^ n :=
(norm_hom.to_monoid_hom : Î± â†’* â„).map_pow a

@[simp] lemma nnnorm_pow (a : Î±) (n : â„•) : â€–a ^ nâ€–â‚Š = â€–aâ€–â‚Š ^ n :=
(nnnorm_hom.to_monoid_hom : Î± â†’* â„â‰¥0).map_pow a n

protected lemma list.norm_prod (l : list Î±) : â€–l.prodâ€– = (l.map norm).prod :=
(norm_hom.to_monoid_hom : Î± â†’* â„).map_list_prod _

protected lemma list.nnnorm_prod (l : list Î±) : â€–l.prodâ€–â‚Š = (l.map nnnorm).prod :=
(nnnorm_hom.to_monoid_hom : Î± â†’* â„â‰¥0).map_list_prod _

@[simp] lemma norm_div (a b : Î±) : â€–a / bâ€– = â€–aâ€– / â€–bâ€– := map_divâ‚€ (norm_hom : Î± â†’*â‚€ â„) a b

@[simp] lemma nnnorm_div (a b : Î±) : â€–a / bâ€–â‚Š = â€–aâ€–â‚Š / â€–bâ€–â‚Š := map_divâ‚€ (nnnorm_hom : Î± â†’*â‚€ â„â‰¥0) a b

@[simp] lemma norm_inv (a : Î±) : â€–aâ»Â¹â€– = â€–aâ€–â»Â¹ := map_invâ‚€ (norm_hom : Î± â†’*â‚€ â„) a

@[simp] lemma nnnorm_inv (a : Î±) : â€–aâ»Â¹â€–â‚Š = â€–aâ€–â‚Šâ»Â¹ :=
nnreal.eq $ by simp

@[simp] lemma norm_zpow : âˆ€ (a : Î±) (n : â„¤), â€–a^nâ€– = â€–aâ€–^n := map_zpowâ‚€ (norm_hom : Î± â†’*â‚€ â„)

@[simp] lemma nnnorm_zpow : âˆ€ (a : Î±) (n : â„¤), â€–a ^ nâ€–â‚Š = â€–aâ€–â‚Š ^ n :=
map_zpowâ‚€ (nnnorm_hom : Î± â†’*â‚€ â„â‰¥0)

lemma dist_inv_invâ‚€ {z w : Î±} (hz : z â‰  0) (hw : w â‰  0) :
  dist zâ»Â¹ wâ»Â¹ = (dist z w) / (â€–zâ€– * â€–wâ€–) :=
by rw [dist_eq_norm, inv_sub_inv' hz hw, norm_mul, norm_mul, norm_inv, norm_inv, mul_comm â€–zâ€–â»Â¹,
  mul_assoc, dist_eq_norm', div_eq_mul_inv, mul_inv]

lemma nndist_inv_invâ‚€ {z w : Î±} (hz : z â‰  0) (hw : w â‰  0) :
  nndist zâ»Â¹ wâ»Â¹ = (nndist z w) / (â€–zâ€–â‚Š * â€–wâ€–â‚Š) :=
by { rw â† nnreal.coe_eq, simp [-nnreal.coe_eq, dist_inv_invâ‚€ hz hw], }

/-- Multiplication on the left by a nonzero element of a normed division ring tends to infinity at
infinity. TODO: use `bornology.cobounded` instead of `filter.comap has_norm.norm filter.at_top`. -/
lemma filter.tendsto_mul_left_cobounded {a : Î±} (ha : a â‰  0) :
  tendsto ((*) a) (comap norm at_top) (comap norm at_top) :=
by simpa only [tendsto_comap_iff, (âˆ˜), norm_mul]
  using tendsto_const_nhds.mul_at_top (norm_pos_iff.2 ha) tendsto_comap

/-- Multiplication on the right by a nonzero element of a normed division ring tends to infinity at
infinity. TODO: use `bornology.cobounded` instead of `filter.comap has_norm.norm filter.at_top`. -/
lemma filter.tendsto_mul_right_cobounded {a : Î±} (ha : a â‰  0) :
  tendsto (Î» x, x * a) (comap norm at_top) (comap norm at_top) :=
by simpa only [tendsto_comap_iff, (âˆ˜), norm_mul]
  using tendsto_comap.at_top_mul (norm_pos_iff.2 ha) tendsto_const_nhds

@[priority 100] -- see Note [lower instance priority]
instance normed_division_ring.to_has_continuous_invâ‚€ : has_continuous_invâ‚€ Î± :=
begin
  refine âŸ¨Î» r r0, tendsto_iff_norm_tendsto_zero.2 _âŸ©,
  have r0' : 0 < â€–râ€– := norm_pos_iff.2 r0,
  rcases exists_between r0' with âŸ¨Îµ, Îµ0, ÎµrâŸ©,
  have : âˆ€á¶  e in ğ“ r, â€–eâ»Â¹ - râ»Â¹â€– â‰¤ â€–r - eâ€– / â€–râ€– / Îµ,
  { filter_upwards [(is_open_lt continuous_const continuous_norm).eventually_mem Îµr] with e he,
    have e0 : e â‰  0 := norm_pos_iff.1 (Îµ0.trans he),
    calc â€–eâ»Â¹ - râ»Â¹â€– = â€–râ€–â»Â¹ * â€–r - eâ€– * â€–eâ€–â»Â¹ : by
      { rw [â†norm_inv, â†norm_inv, â†norm_mul, â†norm_mul, mul_sub, sub_mul, mul_assoc _ e,
          inv_mul_cancel r0, mul_inv_cancel e0, one_mul, mul_one] }
    ...              = â€–r - eâ€– / â€–râ€– / â€–eâ€– : by field_simp [mul_comm]
    ... â‰¤ â€–r - eâ€– / â€–râ€– / Îµ :
      div_le_div_of_le_left (div_nonneg (norm_nonneg _) (norm_nonneg _)) Îµ0 he.le },
  refine squeeze_zero' (eventually_of_forall $ Î» _, norm_nonneg _) this _,
  refine (((continuous_const.sub continuous_id).norm.div_const _).div_const _).tendsto' _ _ _,
  simp,
end

/-- A normed division ring is a topological division ring. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_division_ring.to_topological_division_ring : topological_division_ring Î± :=
{ }

lemma norm_map_one_of_pow_eq_one [monoid Î²] (Ï† : Î² â†’* Î±) {x : Î²} {k : â„•+}
  (h : x ^ (k : â„•) = 1) :
  â€–Ï† xâ€– = 1 :=
begin
  rw [â† pow_left_inj, â† norm_pow, â† map_pow, h, map_one, norm_one, one_pow],
  exacts [norm_nonneg _, zero_le_one, k.pos],
end

lemma norm_one_of_pow_eq_one {x : Î±} {k : â„•+} (h : x ^ (k : â„•) = 1) :
  â€–xâ€– = 1 :=
norm_map_one_of_pow_eq_one (monoid_hom.id Î±) h

end normed_division_ring

/-- A normed field is a field with a norm satisfying â€–x yâ€– = â€–xâ€– â€–yâ€–. -/
class normed_field (Î± : Type*) extends has_norm Î±, field Î±, metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul' : âˆ€ a b, norm (a * b) = norm a * norm b)

/-- A nontrivially normed field is a normed field in which there is an element of norm different
from `0` and `1`. This makes it possible to bring any element arbitrarily close to `0` by
multiplication by the powers of any element, and thus to relate algebra and topology. -/
class nontrivially_normed_field (Î± : Type*) extends normed_field Î± :=
(non_trivial : âˆƒ x : Î±, 1 < â€–xâ€–)

/-- A densely normed field is a normed field for which the image of the norm is dense in `â„â‰¥0`,
which means it is also nontrivially normed. However, not all nontrivally normed fields are densely
normed; in particular, the `padic`s exhibit this fact. -/
class densely_normed_field (Î± : Type*) extends normed_field Î± :=
(lt_norm_lt : âˆ€ x y : â„, 0 â‰¤ x â†’ x < y â†’ âˆƒ a : Î±, x < â€–aâ€– âˆ§ â€–aâ€– < y)

section normed_field

/-- A densely normed field is always a nontrivially normed field.
See note [lower instance priority]. -/
@[priority 100]
instance densely_normed_field.to_nontrivially_normed_field [densely_normed_field Î±] :
  nontrivially_normed_field Î± :=
{ non_trivial := let âŸ¨a, h, _âŸ© := densely_normed_field.lt_norm_lt 1 2 zero_le_one one_lt_two in
    âŸ¨a, hâŸ© }

variables [normed_field Î±]

@[priority 100] -- see Note [lower instance priority]
instance normed_field.to_normed_division_ring : normed_division_ring Î± :=
{ ..â€¹normed_field Î±â€º }

@[priority 100] -- see Note [lower instance priority]
instance normed_field.to_normed_comm_ring : normed_comm_ring Î± :=
{ norm_mul := Î» a b, (norm_mul a b).le, ..â€¹normed_field Î±â€º }

@[simp] lemma norm_prod (s : finset Î²) (f : Î² â†’ Î±) :
  â€–âˆ b in s, f bâ€– = âˆ b in s, â€–f bâ€– :=
(norm_hom.to_monoid_hom : Î± â†’* â„).map_prod f s

@[simp] lemma nnnorm_prod (s : finset Î²) (f : Î² â†’ Î±) :
  â€–âˆ b in s, f bâ€–â‚Š = âˆ b in s, â€–f bâ€–â‚Š :=
(nnnorm_hom.to_monoid_hom : Î± â†’* â„â‰¥0).map_prod f s

end normed_field

namespace normed_field

section nontrivially

variables (Î±) [nontrivially_normed_field Î±]

lemma exists_one_lt_norm : âˆƒx : Î±, 1 < â€–xâ€– := â€¹nontrivially_normed_field Î±â€º.non_trivial

lemma exists_lt_norm (r : â„) : âˆƒ x : Î±, r < â€–xâ€– :=
let âŸ¨w, hwâŸ© := exists_one_lt_norm Î± in
let âŸ¨n, hnâŸ© := pow_unbounded_of_one_lt r hw in
âŸ¨w^n, by rwa norm_powâŸ©

lemma exists_norm_lt {r : â„} (hr : 0 < r) : âˆƒ x : Î±, 0 < â€–xâ€– âˆ§ â€–xâ€– < r :=
let âŸ¨w, hwâŸ© := exists_lt_norm Î± râ»Â¹ in
âŸ¨wâ»Â¹, by rwa [â† set.mem_Ioo, norm_inv, â† set.mem_inv, set.inv_Ioo_0_left hr]âŸ©

lemma exists_norm_lt_one : âˆƒx : Î±, 0 < â€–xâ€– âˆ§ â€–xâ€– < 1 :=
exists_norm_lt Î± one_pos

variable {Î±}

@[instance]
lemma punctured_nhds_ne_bot (x : Î±) : ne_bot (ğ“[â‰ ] x) :=
begin
  rw [â† mem_closure_iff_nhds_within_ne_bot, metric.mem_closure_iff],
  rintros Îµ Îµ0,
  rcases exists_norm_lt Î± Îµ0 with âŸ¨b, hb0, hbÎµâŸ©,
  refine âŸ¨x + b, mt (set.mem_singleton_iff.trans add_right_eq_self).1 $ norm_pos_iff.1 hb0, _âŸ©,
  rwa [dist_comm, dist_eq_norm, add_sub_cancel'],
end

@[instance]
lemma nhds_within_is_unit_ne_bot : ne_bot (ğ“[{x : Î± | is_unit x}] 0) :=
by simpa only [is_unit_iff_ne_zero] using punctured_nhds_ne_bot (0:Î±)

end nontrivially

section densely

variables (Î±) [densely_normed_field Î±]

lemma exists_lt_norm_lt {râ‚ râ‚‚ : â„} (hâ‚€ : 0 â‰¤ râ‚) (h : râ‚ < râ‚‚) : âˆƒ x : Î±, râ‚ < â€–xâ€– âˆ§ â€–xâ€– < râ‚‚ :=
densely_normed_field.lt_norm_lt râ‚ râ‚‚ hâ‚€ h

lemma exists_lt_nnnorm_lt {râ‚ râ‚‚ : â„â‰¥0} (h : râ‚ < râ‚‚) : âˆƒ x : Î±, râ‚ < â€–xâ€–â‚Š âˆ§ â€–xâ€–â‚Š < râ‚‚ :=
by exact_mod_cast exists_lt_norm_lt Î± râ‚.prop h

instance densely_ordered_range_norm : densely_ordered (set.range (norm : Î± â†’ â„)) :=
{ dense :=
  begin
    rintro âŸ¨-, x, rflâŸ© âŸ¨-, y, rflâŸ© hxy,
    exact let âŸ¨z, hâŸ© := exists_lt_norm_lt Î± (norm_nonneg _) hxy in âŸ¨âŸ¨â€–zâ€–, z, rflâŸ©, hâŸ©,
  end }

instance densely_ordered_range_nnnorm : densely_ordered (set.range (nnnorm : Î± â†’ â„â‰¥0)) :=
{ dense :=
  begin
    rintro âŸ¨-, x, rflâŸ© âŸ¨-, y, rflâŸ© hxy,
    exact let âŸ¨z, hâŸ© := exists_lt_nnnorm_lt Î± hxy in âŸ¨âŸ¨â€–zâ€–â‚Š, z, rflâŸ©, hâŸ©,
  end }

lemma dense_range_nnnorm : dense_range (nnnorm : Î± â†’ â„â‰¥0) :=
dense_of_exists_between $ Î» _ _ hr, let âŸ¨x, hâŸ© := exists_lt_nnnorm_lt Î± hr in âŸ¨â€–xâ€–â‚Š, âŸ¨x, rflâŸ©, hâŸ©

end densely

end normed_field

instance : normed_comm_ring â„ :=
{ norm_mul := Î» x y, (abs_mul x y).le,
  .. real.normed_add_comm_group,
  .. real.comm_ring }

noncomputable instance : normed_field â„ :=
{ norm_mul' := abs_mul,
  .. real.normed_add_comm_group }

noncomputable instance : densely_normed_field â„ :=
{ lt_norm_lt := Î» _ _ hâ‚€ hr, let âŸ¨x, hâŸ© := exists_between hr in
    âŸ¨x, by rwa [real.norm_eq_abs, abs_of_nonneg (hâ‚€.trans h.1.le)]âŸ© }

namespace real

lemma to_nnreal_mul_nnnorm {x : â„} (y : â„) (hx : 0 â‰¤ x) : x.to_nnreal * â€–yâ€–â‚Š = â€–x * yâ€–â‚Š :=
by simp [real.to_nnreal_of_nonneg, nnnorm, norm_of_nonneg, hx]

lemma nnnorm_mul_to_nnreal (x : â„) {y : â„} (hy : 0 â‰¤ y) : â€–xâ€–â‚Š * y.to_nnreal = â€–x * yâ€–â‚Š :=
by simp [real.to_nnreal_of_nonneg, nnnorm, norm_of_nonneg, hy]

end real

namespace nnreal

open_locale nnreal

@[simp] lemma norm_eq (x : â„â‰¥0) : â€–(x : â„)â€– = x :=
by rw [real.norm_eq_abs, x.abs_eq]

@[simp] lemma nnnorm_eq (x : â„â‰¥0) : â€–(x : â„)â€–â‚Š = x :=
nnreal.eq $ real.norm_of_nonneg x.2

end nnreal

@[simp] lemma norm_norm [seminormed_add_comm_group Î±] (x : Î±) : â€–â€–xâ€–â€– = â€–xâ€– :=
real.norm_of_nonneg (norm_nonneg _)

@[simp] lemma nnnorm_norm [seminormed_add_comm_group Î±] (a : Î±) : â€–â€–aâ€–â€–â‚Š = â€–aâ€–â‚Š :=
by simpa [real.nnnorm_of_nonneg (norm_nonneg a)]

/-- A restatement of `metric_space.tendsto_at_top` in terms of the norm. -/
lemma normed_add_comm_group.tendsto_at_top [nonempty Î±] [semilattice_sup Î±] {Î² : Type*}
  [seminormed_add_comm_group Î²] {f : Î± â†’ Î²} {b : Î²} :
  tendsto f at_top (ğ“ b) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ â€–f n - bâ€– < Îµ :=
(at_top_basis.tendsto_iff metric.nhds_basis_ball).trans (by simp [dist_eq_norm])

/--
A variant of `normed_add_comm_group.tendsto_at_top` that
uses `âˆƒ N, âˆ€ n > N, ...` rather than `âˆƒ N, âˆ€ n â‰¥ N, ...`
-/
lemma normed_add_comm_group.tendsto_at_top' [nonempty Î±] [semilattice_sup Î±] [no_max_order Î±]
  {Î² : Type*} [seminormed_add_comm_group Î²]
  {f : Î± â†’ Î²} {b : Î²} :
  tendsto f at_top (ğ“ b) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N < n â†’ â€–f n - bâ€– < Îµ :=
(at_top_basis_Ioi.tendsto_iff metric.nhds_basis_ball).trans (by simp [dist_eq_norm])

instance : normed_comm_ring â„¤ :=
{ norm_mul := Î» m n, le_of_eq $ by simp only [norm, int.cast_mul, abs_mul],
  mul_comm := mul_comm,
  .. int.normed_add_comm_group }

instance : norm_one_class â„¤ :=
âŸ¨by simp [â† int.norm_cast_real]âŸ©

instance : normed_field â„š :=
{ norm_mul' := Î» râ‚ râ‚‚, by simp only [norm, rat.cast_mul, abs_mul],
  .. rat.normed_add_comm_group }

instance : densely_normed_field â„š :=
{ lt_norm_lt := Î» râ‚ râ‚‚ hâ‚€ hr, let âŸ¨q, hâŸ© := exists_rat_btwn hr in
    âŸ¨q, by { unfold norm, rwa abs_of_pos (hâ‚€.trans_lt h.1) } âŸ© }

section ring_hom_isometric

variables {Râ‚ : Type*} {Râ‚‚ : Type*} {Râ‚ƒ : Type*}

/-- This class states that a ring homomorphism is isometric. This is a sufficient assumption
for a continuous semilinear map to be bounded and this is the main use for this typeclass. -/
class ring_hom_isometric [semiring Râ‚] [semiring Râ‚‚] [has_norm Râ‚] [has_norm Râ‚‚]
  (Ïƒ : Râ‚ â†’+* Râ‚‚) : Prop :=
(is_iso : âˆ€ {x : Râ‚}, â€–Ïƒ xâ€– = â€–xâ€–)

attribute [simp] ring_hom_isometric.is_iso

variables [semi_normed_ring Râ‚] [semi_normed_ring Râ‚‚] [semi_normed_ring Râ‚ƒ]

instance ring_hom_isometric.ids : ring_hom_isometric (ring_hom.id Râ‚) :=
âŸ¨Î» x, rflâŸ©

end ring_hom_isometric

/-! ### Induced normed structures -/

section induced

variables {F : Type*} (R S : Type*)

/-- A non-unital ring homomorphism from an `non_unital_ring` to a `non_unital_semi_normed_ring`
induces a `non_unital_semi_normed_ring` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def non_unital_semi_normed_ring.induced [non_unital_ring R] [non_unital_semi_normed_ring S]
  [non_unital_ring_hom_class F R S] (f : F) : non_unital_semi_normed_ring R :=
{ norm_mul := Î» x y, by { unfold norm, exact (map_mul f x y).symm â–¸ norm_mul_le (f x) (f y) },
  .. seminormed_add_comm_group.induced R S f }

/-- An injective non-unital ring homomorphism from an `non_unital_ring` to a
`non_unital_normed_ring` induces a `non_unital_normed_ring` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def non_unital_normed_ring.induced [non_unital_ring R] [non_unital_normed_ring S]
  [non_unital_ring_hom_class F R S] (f : F) (hf : function.injective f) :
  non_unital_normed_ring R :=
{ .. non_unital_semi_normed_ring.induced R S f,
  .. normed_add_comm_group.induced R S f hf }

/-- A non-unital ring homomorphism from an `ring` to a `semi_normed_ring` induces a
`semi_normed_ring` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def semi_normed_ring.induced [ring R] [semi_normed_ring S] [non_unital_ring_hom_class F R S]
  (f : F) : semi_normed_ring R :=
{ .. non_unital_semi_normed_ring.induced R S f,
  .. seminormed_add_comm_group.induced R S f }

/-- An injective non-unital ring homomorphism from an `ring` to a `normed_ring` induces a
`normed_ring` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def normed_ring.induced [ring R] [normed_ring S] [non_unital_ring_hom_class F R S] (f : F)
  (hf : function.injective f) : normed_ring R :=
{ .. non_unital_semi_normed_ring.induced R S f,
  .. normed_add_comm_group.induced R S f hf }

/-- A non-unital ring homomorphism from a `comm_ring` to a `semi_normed_ring` induces a
`semi_normed_comm_ring` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def semi_normed_comm_ring.induced [comm_ring R] [semi_normed_ring S]
  [non_unital_ring_hom_class F R S] (f : F) : semi_normed_comm_ring R :=
{ mul_comm := mul_comm,
  .. non_unital_semi_normed_ring.induced R S f,
  .. seminormed_add_comm_group.induced R S f }

/-- An injective non-unital ring homomorphism from an `comm_ring` to a `normed_ring` induces a
`normed_comm_ring` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def normed_comm_ring.induced [comm_ring R] [normed_ring S] [non_unital_ring_hom_class F R S] (f : F)
  (hf : function.injective f) : normed_comm_ring R :=
{ .. semi_normed_comm_ring.induced R S f,
  .. normed_add_comm_group.induced R S f hf }

/-- An injective non-unital ring homomorphism from an `division_ring` to a `normed_ring` induces a
`normed_division_ring` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def normed_division_ring.induced [division_ring R] [normed_division_ring S]
  [non_unital_ring_hom_class F R S] (f : F) (hf : function.injective f) : normed_division_ring R :=
{ norm_mul' := Î» x y, by { unfold norm, exact (map_mul f x y).symm â–¸ norm_mul (f x) (f y) },
  .. normed_add_comm_group.induced R S f hf }

/-- An injective non-unital ring homomorphism from an `field` to a `normed_ring` induces a
`normed_field` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def normed_field.induced [field R] [normed_field S]
  [non_unital_ring_hom_class F R S] (f : F) (hf : function.injective f) : normed_field R :=
{ .. normed_division_ring.induced R S f hf }

/-- A ring homomorphism from a `ring R` to a `semi_normed_ring S` which induces the norm structure
`semi_normed_ring.induced` makes `R` satisfy `â€–(1 : R)â€– = 1` whenever `â€–(1 : S)â€– = 1`. -/
lemma norm_one_class.induced {F : Type*} (R S : Type*) [ring R] [semi_normed_ring S]
  [norm_one_class S] [ring_hom_class F R S] (f : F) :
  @norm_one_class R (semi_normed_ring.induced R S f).to_has_norm _ :=
{ norm_one := (congr_arg norm (map_one f)).trans norm_one }

end induced

namespace subring_class

variables {S R : Type*} [set_like S R]

instance to_semi_normed_ring [semi_normed_ring R] [subring_class S R] (s : S) :
  semi_normed_ring s :=
semi_normed_ring.induced s R (subring_class.subtype s)

instance to_normed_ring [normed_ring R] [subring_class S R] (s : S) :
  normed_ring s :=
normed_ring.induced s R (subring_class.subtype s) subtype.val_injective

instance to_semi_normed_comm_ring [semi_normed_comm_ring R] [h : subring_class S R] (s : S) :
  semi_normed_comm_ring s :=
{ mul_comm := mul_comm, .. subring_class.to_semi_normed_ring s }

instance to_normed_comm_ring [normed_comm_ring R] [subring_class S R] (s : S) :
  normed_comm_ring s :=
{ mul_comm := mul_comm, .. subring_class.to_normed_ring s }

end subring_class

-- Guard again import creep.
assert_not_exists restrict_scalars
