/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth
-/
import analysis.normed.field.basic
import analysis.normed.group.ball_sphere

/-!
# Algebraic structures on unit balls and spheres

In this file we define algebraic structures (`semigroup`, `comm_semigroup`, `monoid`, `comm_monoid`,
`group`, `comm_group`) on `metric.ball (0 : ğ•œ) 1`, `metric.closed_ball (0 : ğ•œ) 1`, and
`metric.sphere (0 : ğ•œ) 1`. In each case we use the weakest possible typeclass assumption on `ğ•œ`,
from `non_unital_semi_normed_ring` to `normed_field`.
-/

open set metric

variables {ğ•œ : Type*}

/-- Unit ball in a non unital semi normed ring as a bundled `subsemigroup`. -/
def subsemigroup.unit_ball (ğ•œ : Type*) [non_unital_semi_normed_ring ğ•œ] :
  subsemigroup ğ•œ :=
{ carrier := ball (0 : ğ•œ) 1,
  mul_mem' := Î» x y hx hy,
    begin
      rw [mem_ball_zero_iff] at *,
      exact (norm_mul_le x y).trans_lt (mul_lt_one_of_nonneg_of_lt_one_left (norm_nonneg _)
        hx hy.le)
    end }

instance [non_unital_semi_normed_ring ğ•œ] : semigroup (ball (0 : ğ•œ) 1) :=
mul_mem_class.to_semigroup (subsemigroup.unit_ball ğ•œ)

instance [non_unital_semi_normed_ring ğ•œ] : has_continuous_mul (ball (0 : ğ•œ) 1) :=
âŸ¨continuous_subtype_mk _ $ continuous.mul (continuous_subtype_val.comp continuous_fst)
  (continuous_subtype_val.comp continuous_snd)âŸ©

instance [semi_normed_comm_ring ğ•œ] : comm_semigroup (ball (0 : ğ•œ) 1) :=
mul_mem_class.to_comm_semigroup (subsemigroup.unit_ball ğ•œ)

instance [non_unital_semi_normed_ring ğ•œ] : has_distrib_neg (ball (0 : ğ•œ) 1) :=
subtype.coe_injective.has_distrib_neg (coe : ball (0 : ğ•œ) 1 â†’ ğ•œ) (Î» _, rfl) (Î» _ _, rfl)

/-- Closed unit ball in a non unital semi normed ring as a bundled `subsemigroup`. -/
def subsemigroup.unit_closed_ball (ğ•œ : Type*) [non_unital_semi_normed_ring ğ•œ] :
  subsemigroup ğ•œ :=
{ carrier := closed_ball 0 1,
  mul_mem' := Î» x y hx hy,
    begin
      rw [mem_closed_ball_zero_iff] at *,
      exact (norm_mul_le x y).trans (mul_le_one hx (norm_nonneg _) hy)
    end }

instance [non_unital_semi_normed_ring ğ•œ] : semigroup (closed_ball (0 : ğ•œ) 1) :=
mul_mem_class.to_semigroup (subsemigroup.unit_closed_ball ğ•œ)

instance [non_unital_semi_normed_ring ğ•œ] : has_distrib_neg (closed_ball (0 : ğ•œ) 1) :=
subtype.coe_injective.has_distrib_neg (coe : closed_ball (0 : ğ•œ) 1 â†’ ğ•œ) (Î» _, rfl) (Î» _ _, rfl)

instance [non_unital_semi_normed_ring ğ•œ] : has_continuous_mul (closed_ball (0 : ğ•œ) 1) :=
âŸ¨continuous_subtype_mk _ $ continuous.mul (continuous_subtype_val.comp continuous_fst)
  (continuous_subtype_val.comp continuous_snd)âŸ©

/-- Closed unit ball in a semi normed ring as a bundled `submonoid`. -/
def submonoid.unit_closed_ball (ğ•œ : Type*) [semi_normed_ring ğ•œ] [norm_one_class ğ•œ] :
  submonoid ğ•œ :=
{ carrier := closed_ball 0 1,
  one_mem' := mem_closed_ball_zero_iff.2 norm_one.le,
  .. subsemigroup.unit_closed_ball ğ•œ }

instance [semi_normed_ring ğ•œ] [norm_one_class ğ•œ] : monoid (closed_ball (0 : ğ•œ) 1) :=
submonoid_class.to_monoid (submonoid.unit_closed_ball ğ•œ)

instance [semi_normed_comm_ring ğ•œ] [norm_one_class ğ•œ] : comm_monoid (closed_ball (0 : ğ•œ) 1) :=
submonoid_class.to_comm_monoid (submonoid.unit_closed_ball ğ•œ)

/-- Unit sphere in a normed division ring as a bundled `submonoid`. -/
def submonoid.unit_sphere (ğ•œ : Type*) [normed_division_ring ğ•œ] : submonoid ğ•œ :=
{ carrier := sphere (0 : ğ•œ) 1,
  mul_mem' := Î» x y hx hy, by { rw [mem_sphere_zero_iff_norm] at *, simp * },
  one_mem' := mem_sphere_zero_iff_norm.2 norm_one }

instance [normed_division_ring ğ•œ] : group (sphere (0 : ğ•œ) 1) :=
{ inv := Î» x, âŸ¨xâ»Â¹, mem_sphere_zero_iff_norm.2 $
    by rw [norm_inv, mem_sphere_zero_iff_norm.1 x.coe_prop, inv_one]âŸ©,
  mul_left_inv := Î» x, subtype.coe_injective $ inv_mul_cancel $ ne_zero_of_mem_unit_sphere x,
  .. submonoid_class.to_monoid (submonoid.unit_sphere ğ•œ) }

instance [normed_division_ring ğ•œ] : has_distrib_neg (sphere (0 : ğ•œ) 1) :=
subtype.coe_injective.has_distrib_neg (coe : sphere (0 : ğ•œ) 1 â†’ ğ•œ) (Î» _, rfl) (Î» _ _, rfl)

/-- Monoid homomorphism from the unit sphere to the group of units. -/
def unit_sphere_to_units (ğ•œ : Type*) [normed_division_ring ğ•œ] : sphere (0 : ğ•œ) 1 â†’* units ğ•œ :=
units.lift_right (submonoid.unit_sphere ğ•œ).subtype (Î» x, units.mk0 x $ ne_zero_of_mem_unit_sphere _)
  (Î» x, rfl)

instance [normed_division_ring ğ•œ] : topological_group (sphere (0 : ğ•œ) 1) :=
{ continuous_mul := continuous_subtype_mk _ $ (continuous_subtype_val.comp continuous_fst).mul
    (continuous_subtype_val.comp continuous_snd),
  continuous_inv := continuous_subtype_mk _ $
    continuous_subtype_coe.invâ‚€ ne_zero_of_mem_unit_sphere }

instance [normed_field ğ•œ] : comm_group (sphere (0 : ğ•œ) 1) :=
{ .. metric.sphere.group,
  .. submonoid_class.to_comm_monoid (submonoid.unit_sphere ğ•œ) }
