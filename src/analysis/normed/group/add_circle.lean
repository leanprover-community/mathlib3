/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import algebra.order.floor
import algebra.order.to_interval_mod
import analysis.normed.group.quotient

/-!
# The additive circle

We define the additive circle `add_circle p` as the quotient `ğ•œ â§¸ (â„¤ âˆ™ p)` for some period `p : ğ•œ`.
Note that when `ğ•œ = â„`, this is naturally a normed group; for example if `p = 1` then:
`âˆ¥(x : add_circle 1)âˆ¥ = |x - round x|` for any `x : â„` (see `unit_add_circle.norm_eq`).

See also `circle` and `real.angle`.

## Main definitions:

 * `add_circle`: the additive circle `ğ•œ â§¸ (â„¤ âˆ™ p)` for some period `p : ğ•œ`
 * `unit_add_circle`: the special case `â„ â§¸ â„¤`
 * `add_circle.equiv_add_circle`: the rescaling equivalence `add_circle p â‰ƒ+ add_circle q`
 * `add_circle.equiv_Ico`: the natural equivalence `add_circle p â‰ƒ Ico 0 p`
 * `add_circle.norm_eq`: a characterisation of the norm on `add_circle p`

## Implementation notes:

Although the most important case is `ğ•œ = â„` we wish to support other types of scalars, such as
the rational circle `add_circle (1 : â„š)`, and so we set things up more generally.

## TODO

 * Link with periodicity
 * Measure space structure
 * Lie group structure
 * Exponential equivalence to `circle`
 * The fact `inner_product_geometry.angle (real.cos Î¸) (real.sin Î¸) = âˆ¥(Î¸ : real.angle)âˆ¥`

-/

noncomputable theory

open set int (hiding mem_zmultiples_iff) add_subgroup

variables {ğ•œ : Type*}

/-- The "additive circle": `ğ•œ â§¸ (â„¤ âˆ™ p)`. See also `circle` and `real.angle`. -/
@[derive [add_comm_group, topological_space, topological_add_group, inhabited, has_coe_t ğ•œ],
  nolint unused_arguments]
def add_circle [linear_ordered_add_comm_group ğ•œ] [topological_space ğ•œ] [order_topology ğ•œ] (p : ğ•œ) :=
ğ•œ â§¸ zmultiples p

namespace add_circle

section linear_ordered_field

variables [linear_ordered_field ğ•œ] [topological_space ğ•œ] [order_topology ğ•œ] (p q : ğ•œ)

@[continuity, nolint unused_arguments] protected lemma continuous_mk' :
  continuous (quotient_add_group.mk' (zmultiples p) : ğ•œ â†’ add_circle p) :=
continuous_coinduced_rng

/-- An auxiliary definition used only for constructing `add_circle.equiv_add_circle`. -/
private def equiv_add_circle_aux (hp : p â‰  0) : add_circle p â†’+ add_circle q :=
quotient_add_group.lift _
  ((quotient_add_group.mk' (zmultiples q)).comp $ add_monoid_hom.mul_right (pâ»Â¹ * q))
  (Î» x h, by obtain âŸ¨z, rflâŸ© := mem_zmultiples_iff.1 h; simp [hp, mul_assoc (z : ğ•œ), â† mul_assoc p])

/-- The rescaling equivalence between additive circles with different periods. -/
def equiv_add_circle (hp : p â‰  0) (hq : q â‰  0) : add_circle p â‰ƒ+ add_circle q :=
{ to_fun := equiv_add_circle_aux p q hp,
  inv_fun := equiv_add_circle_aux q p hq,
  left_inv := by { rintros âŸ¨xâŸ©, show quotient_add_group.mk _ = _, congr, field_simp [hp, hq], },
  right_inv := by { rintros âŸ¨xâŸ©, show quotient_add_group.mk _ = _, congr, field_simp [hp, hq], },
  .. equiv_add_circle_aux p q hp }

@[simp] lemma equiv_add_circle_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
  equiv_add_circle p q hp hq (x : ğ•œ) = (x * (pâ»Â¹ * q) : ğ•œ) :=
rfl

@[simp] lemma equiv_add_circle_symm_apply_mk (hp : p â‰  0) (hq : q â‰  0) (x : ğ•œ) :
  (equiv_add_circle p q hp hq).symm (x : ğ•œ) = (x * (qâ»Â¹ * p) : ğ•œ) :=
rfl

variables [floor_ring ğ•œ]

/-- The natural equivalence between `add_circle p` and the half-open interval `[0, p)`. -/
def equiv_Ico (hp : 0 < p) : add_circle p â‰ƒ Ico 0 p :=
{ inv_fun := quotient_add_group.mk' _ âˆ˜ coe,
  to_fun := Î» x, âŸ¨(to_Ico_mod_periodic 0 hp).lift x, quot.induction_on x $ to_Ico_mod_mem_Ico' hpâŸ©,
  right_inv := by { rintros âŸ¨x, hxâŸ©, ext, simp [to_Ico_mod_eq_self, hx.1, hx.2], },
  left_inv :=
  begin
    rintros âŸ¨xâŸ©,
    change quotient_add_group.mk (to_Ico_mod 0 hp x) = quotient_add_group.mk x,
    rw [quotient_add_group.eq', neg_add_eq_sub, self_sub_to_Ico_mod, zsmul_eq_mul],
    apply int_cast_mul_mem_zmultiples,
  end }

@[simp] lemma coe_equiv_Ico_mk_apply (hp : 0 < p) (x : ğ•œ) :
  (equiv_Ico p hp $ quotient_add_group.mk x : ğ•œ) = fract (x / p) * p :=
to_Ico_mod_eq_fract_mul hp x

@[continuity] lemma continuous_equiv_Ico_symm (hp : 0 < p) : continuous (equiv_Ico p hp).symm :=
continuous_coinduced_rng.comp continuous_induced_dom

/-- The image of the closed interval `[0, p]` under the quotient map `ğ•œ â†’ add_circle p` is the
entire space. -/
@[simp] lemma coe_image_Icc_eq (hp : 0 < p) :
  (coe : ğ•œ â†’ add_circle p) '' (Icc 0 p) = univ :=
begin
  refine eq_univ_iff_forall.mpr (Î» x, _),
  let y := equiv_Ico p hp x,
  exact âŸ¨y, âŸ¨y.2.1, y.2.2.leâŸ©, (equiv_Ico p hp).symm_apply_apply xâŸ©,
end

end linear_ordered_field

section real

variables (p : â„)

instance : normed_add_comm_group (add_circle p) := add_subgroup.normed_add_comm_group_quotient _

lemma compact_space (hp : 0 < p) : compact_space $ add_circle p :=
begin
  rw [â† is_compact_univ_iff, â† coe_image_Icc_eq p hp],
  exact is_compact_Icc.image (add_circle.continuous_mk' p),
end

@[simp] lemma norm_coe_mul (x : â„) (t : â„) :
  âˆ¥(â†‘(t * x) : add_circle (t * p))âˆ¥ = |t| * âˆ¥(x : add_circle p)âˆ¥ :=
begin
  have aux : âˆ€ {a b c : â„}, a âˆˆ zmultiples b â†’ c * a âˆˆ zmultiples (c * b) := Î» a b c h, by
  { simp only [mem_zmultiples_iff] at âŠ¢ h,
    obtain âŸ¨n, rflâŸ© := h,
    exact âŸ¨n, (mul_smul_comm n c b).symmâŸ©, },
  rcases eq_or_ne t 0 with rfl | ht, { simp, },
  have ht' : |t| â‰  0 := (not_congr abs_eq_zero).mpr ht,
  simp only [quotient_norm_eq, real.norm_eq_abs],
  conv_rhs { rw [â† smul_eq_mul, â† real.Inf_smul_of_nonneg (abs_nonneg t)], },
  simp only [quotient_add_group.mk'_apply, quotient_add_group.eq_iff_sub_mem],
  congr' 1,
  ext z,
  rw mem_smul_set_iff_inv_smul_memâ‚€ ht',
  show (âˆƒ y, y - t * x âˆˆ zmultiples (t * p) âˆ§ |y| = z) â†” âˆƒw, w - x âˆˆ zmultiples p âˆ§ |w| = |t|â»Â¹ * z,
  split,
  { rintros âŸ¨y, hy, rflâŸ©,
    refine âŸ¨tâ»Â¹ * y, _, by rw [abs_mul, abs_inv]âŸ©,
    rw [â† inv_mul_cancel_leftâ‚€ ht x, â† inv_mul_cancel_leftâ‚€ ht p, â† mul_sub],
    exact aux hy, },
  { rintros âŸ¨w, hw, hw'âŸ©,
    refine âŸ¨t * w, _, by rw [â† (eq_inv_mul_iff_mul_eqâ‚€ ht').mp hw', abs_mul]âŸ©,
    rw â† mul_sub,
    exact aux hw, },
end

@[simp] lemma norm_eq_of_zero {x : â„} : âˆ¥(x : add_circle (0 : â„))âˆ¥ = |x| :=
begin
  suffices : {y : â„ | (y : add_circle (0 : â„)) = (x : add_circle (0 : â„)) } = { x },
  { rw [quotient_norm_eq, this, image_singleton, real.norm_eq_abs, cInf_singleton], },
  ext y,
  simp [quotient_add_group.eq_iff_sub_mem, mem_zmultiples_iff, sub_eq_zero],
end

lemma norm_eq {x : â„} : âˆ¥(x : add_circle p)âˆ¥ = |x - round (pâ»Â¹ * x) * p| :=
begin
  suffices : âˆ€ (x : â„), âˆ¥(x : add_circle (1 : â„))âˆ¥ = |x - round x|,
  { rcases eq_or_ne p 0 with rfl | hp, { simp, },
    intros,
    have hx := norm_coe_mul p x pâ»Â¹,
    rw [abs_inv, eq_inv_mul_iff_mul_eqâ‚€ ((not_congr abs_eq_zero).mpr hp)] at hx,
    rw [â† hx, inv_mul_cancel hp, this, â† abs_mul, mul_sub, mul_inv_cancel_leftâ‚€ hp, mul_comm p], },
  clear x p,
  intros,
  rw [quotient_norm_eq, abs_sub_round_eq_min],
  have hâ‚ : bdd_below (abs '' {m : â„ | (m : add_circle (1 : â„)) = x}) :=
    âŸ¨0, by simp [mem_lower_bounds]âŸ©,
  have hâ‚‚ : (abs '' {m : â„ | (m : add_circle (1 : â„)) = x}).nonempty := âŸ¨|x|, âŸ¨x, rfl, rflâŸ©âŸ©,
  apply le_antisymm,
  { simp only [le_min_iff, real.norm_eq_abs, cInf_le_iff hâ‚ hâ‚‚],
    intros b h,
    refine âŸ¨mem_lower_bounds.1 h _ âŸ¨fract x, _, abs_fractâŸ©,
            mem_lower_bounds.1 h _ âŸ¨fract x - 1, _, by rw [abs_sub_comm, abs_one_sub_fract]âŸ©âŸ©,
    { simp only [mem_set_of_eq, fract, sub_eq_self, quotient_add_group.coe_sub,
        quotient_add_group.eq_zero_iff, int_cast_mem_zmultiples_one], },
    { simp only [mem_set_of_eq, fract, sub_eq_self, quotient_add_group.coe_sub,
        quotient_add_group.eq_zero_iff, int_cast_mem_zmultiples_one, sub_sub,
        (by norm_cast : (âŒŠxâŒ‹ : â„) + 1 = (â†‘(âŒŠxâŒ‹ + 1) : â„))], }, },
  { simp only [quotient_add_group.mk'_apply, real.norm_eq_abs, le_cInf_iff hâ‚ hâ‚‚],
    rintros b' âŸ¨b, hb, rflâŸ©,
    simp only [mem_set_of_eq, quotient_add_group.eq_iff_sub_mem, mem_zmultiples_iff,
      smul_one_eq_coe] at hb,
    obtain âŸ¨z, hzâŸ© := hb,
    rw [(by { rw hz, abel, } : x = b - z), fract_sub_int, â† abs_sub_round_eq_min],
    exact abs_sub_round_le_abs_self _, },
end

end real

end add_circle

/-- The unit circle `â„ â§¸ â„¤`. -/
abbreviation unit_add_circle := add_circle (1 : â„)

namespace unit_add_circle

instance : compact_space unit_add_circle := add_circle.compact_space _ zero_lt_one

lemma norm_eq {x : â„} : âˆ¥(x : unit_add_circle)âˆ¥ = |x - round x| := by simp [add_circle.norm_eq]

end unit_add_circle
