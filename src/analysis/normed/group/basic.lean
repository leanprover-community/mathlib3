/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
import order.liminf_limsup
import topology.algebra.uniform_group
import topology.metric_space.algebra
import topology.metric_space.isometry
import topology.sequences

/-!
# Normed (semi)groups

In this file we define four classes:

* `has_norm`, `has_nnnorm`: auxiliary classes endowing a type `Œ±` with a function `norm : Œ± ‚Üí ‚Ñù`
  (notation: `‚à•x‚à•`) and `nnnorm : Œ± ‚Üí ‚Ñù‚â•0` (notation: `‚à•x‚à•‚Çä`), respectively;
* `semi_normed_group`: a seminormed group is an additive group with a norm and a pseudo metric space
  structures that agree with each other: `‚àÄ x y, dist x y = ‚à•x - y‚à•`;
* `normed_group`: a normed group is an additive group with a norm and a metric space structures that
  agree with each other: `‚àÄ x y, dist x y = ‚à•x - y‚à•`.

We also prove basic properties of (semi)normed groups and provide some instances.

## Tags

normed group
-/

variables {Œ± Œπ E F : Type*}

open filter metric
open_locale topological_space big_operators nnreal ennreal uniformity pointwise

/-- Auxiliary class, endowing a type `E` with a function `norm : E ‚Üí ‚Ñù`. This class is designed to
be extended in more interesting classes specifying the properties of the norm. -/
class has_norm (E : Type*) := (norm : E ‚Üí ‚Ñù)

export has_norm (norm)

notation `‚à•` e `‚à•` := norm e

/-- A seminormed group is an additive group endowed with a norm for which `dist x y = ‚à•x - y‚à•`
defines a pseudometric space structure. -/
class semi_normed_group (E : Type*) extends has_norm E, add_comm_group E, pseudo_metric_space E :=
(dist_eq : ‚àÄ x y : E, dist x y = norm (x - y))

/-- A normed group is an additive group endowed with a norm for which `dist x y = ‚à•x - y‚à•` defines
a metric space structure. -/
class normed_group (E : Type*) extends has_norm E, add_comm_group E, metric_space E :=
(dist_eq : ‚àÄ x y : E, dist x y = norm (x - y))

/-- A normed group is a seminormed group. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_group.to_semi_normed_group [h : normed_group E] : semi_normed_group E :=
{ .. h }

/-- Construct a seminormed group from a translation invariant pseudodistance. -/
def semi_normed_group.of_add_dist [has_norm E] [add_comm_group E] [pseudo_metric_space E]
  (H1 : ‚àÄ x : E, ‚à•x‚à• = dist x 0)
  (H2 : ‚àÄ x y z : E, dist x y ‚â§ dist (x + z) (y + z)) : semi_normed_group E :=
{ dist_eq := Œª x y, begin
    rw H1, apply le_antisymm,
    { rw [sub_eq_add_neg, ‚Üê add_right_neg y], apply H2 },
    { have := H2 (x - y) 0 y, rwa [sub_add_cancel, zero_add] at this }
  end }

/-- Construct a seminormed group from a translation invariant pseudodistance -/
def semi_normed_group.of_add_dist' [has_norm E] [add_comm_group E] [pseudo_metric_space E]
  (H1 : ‚àÄ x : E, ‚à•x‚à• = dist x 0)
  (H2 : ‚àÄ x y z : E, dist (x + z) (y + z) ‚â§ dist x y) : semi_normed_group E :=
{ dist_eq := Œª x y, begin
    rw H1, apply le_antisymm,
    { have := H2 (x - y) 0 y, rwa [sub_add_cancel, zero_add] at this },
    { rw [sub_eq_add_neg, ‚Üê add_right_neg y], apply H2 }
  end }

/-- A seminormed group can be built from a seminorm that satisfies algebraic properties. This is
formalised in this structure. -/
structure semi_normed_group.core (E : Type*) [add_comm_group E] [has_norm E] : Prop :=
(norm_zero : ‚à•(0 : E)‚à• = 0)
(triangle : ‚àÄ x y : E, ‚à•x + y‚à• ‚â§ ‚à•x‚à• + ‚à•y‚à•)
(norm_neg : ‚àÄ x : E, ‚à•-x‚à• = ‚à•x‚à•)

/-- Constructing a seminormed group from core properties of a seminorm, i.e., registering the
pseudodistance and the pseudometric space structure from the seminorm properties. Note that in most
cases this instance creates bad definitional equalities (e.g., it does not take into account
a possibly existing `uniform_space` instance on `E`). -/
def semi_normed_group.of_core (E : Type*) [add_comm_group E] [has_norm E]
  (C : semi_normed_group.core E) : semi_normed_group E :=
{ dist := Œª x y, ‚à•x - y‚à•,
  dist_eq := assume x y, by refl,
  dist_self := assume x, by simp [C.norm_zero],
  dist_triangle := assume x y z,
    calc ‚à•x - z‚à• = ‚à•x - y + (y - z)‚à• : by rw sub_add_sub_cancel
            ... ‚â§ ‚à•x - y‚à• + ‚à•y - z‚à•  : C.triangle _ _,
  dist_comm := assume x y,
    calc ‚à•x - y‚à• = ‚à• -(y - x)‚à• : by simp
             ... = ‚à•y - x‚à• : by { rw [C.norm_neg] } }

instance : normed_group punit :=
{ norm := function.const _ 0,
  dist_eq := Œª _ _, rfl, }

@[simp] lemma punit.norm_eq_zero (r : punit) : ‚à•r‚à• = 0 := rfl

noncomputable instance : normed_group ‚Ñù :=
{ norm := Œª x, |x|,
  dist_eq := assume x y, rfl }

lemma real.norm_eq_abs (r : ‚Ñù) : ‚à•r‚à• = |r| := rfl

section semi_normed_group
variables [semi_normed_group E] [semi_normed_group F]

lemma dist_eq_norm (g h : E) : dist g h = ‚à•g - h‚à• :=
semi_normed_group.dist_eq _ _

lemma dist_eq_norm' (g h : E) : dist g h = ‚à•h - g‚à• :=
by rw [dist_comm, dist_eq_norm]

@[simp] lemma dist_zero_right (g : E) : dist g 0 = ‚à•g‚à• :=
by rw [dist_eq_norm, sub_zero]

@[simp] lemma dist_zero_left : dist (0 : E) = norm :=
funext $ Œª g, by rw [dist_comm, dist_zero_right]

lemma tendsto_norm_cocompact_at_top [proper_space E] :
  tendsto norm (cocompact E) at_top :=
by simpa only [dist_zero_right] using tendsto_dist_right_cocompact_at_top (0 : E)

lemma norm_sub_rev (g h : E) : ‚à•g - h‚à• = ‚à•h - g‚à• :=
by simpa only [dist_eq_norm] using dist_comm g h

@[simp] lemma norm_neg (g : E) : ‚à•-g‚à• = ‚à•g‚à• :=
by simpa using norm_sub_rev 0 g

@[simp] lemma dist_add_left (g h‚ÇÅ h‚ÇÇ : E) : dist (g + h‚ÇÅ) (g + h‚ÇÇ) = dist h‚ÇÅ h‚ÇÇ :=
by simp [dist_eq_norm]

@[simp] lemma dist_add_right (g‚ÇÅ g‚ÇÇ h : E) : dist (g‚ÇÅ + h) (g‚ÇÇ + h) = dist g‚ÇÅ g‚ÇÇ :=
by simp [dist_eq_norm]

@[simp] lemma dist_neg_neg (g h : E) : dist (-g) (-h) = dist g h :=
by simp only [dist_eq_norm, neg_sub_neg, norm_sub_rev]

@[simp] lemma dist_sub_left (g h‚ÇÅ h‚ÇÇ : E) : dist (g - h‚ÇÅ) (g - h‚ÇÇ) = dist h‚ÇÅ h‚ÇÇ :=
by simp only [sub_eq_add_neg, dist_add_left, dist_neg_neg]

@[simp] lemma dist_sub_right (g‚ÇÅ g‚ÇÇ h : E) : dist (g‚ÇÅ - h) (g‚ÇÇ - h) = dist g‚ÇÅ g‚ÇÇ :=
by simpa only [sub_eq_add_neg] using dist_add_right _ _ _

/-- **Triangle inequality** for the norm. -/
lemma norm_add_le (g h : E) : ‚à•g + h‚à• ‚â§ ‚à•g‚à• + ‚à•h‚à• :=
by simpa [dist_eq_norm] using dist_triangle g 0 (-h)

lemma norm_add_le_of_le {g‚ÇÅ g‚ÇÇ : E} {n‚ÇÅ n‚ÇÇ : ‚Ñù} (H‚ÇÅ : ‚à•g‚ÇÅ‚à• ‚â§ n‚ÇÅ) (H‚ÇÇ : ‚à•g‚ÇÇ‚à• ‚â§ n‚ÇÇ) :
  ‚à•g‚ÇÅ + g‚ÇÇ‚à• ‚â§ n‚ÇÅ + n‚ÇÇ :=
le_trans (norm_add_le g‚ÇÅ g‚ÇÇ) (add_le_add H‚ÇÅ H‚ÇÇ)

lemma dist_add_add_le (g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ : E) :
  dist (g‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + h‚ÇÇ) ‚â§ dist g‚ÇÅ h‚ÇÅ + dist g‚ÇÇ h‚ÇÇ :=
by simpa only [dist_add_left, dist_add_right] using dist_triangle (g‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + h‚ÇÇ)

lemma dist_add_add_le_of_le {g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ : E} {d‚ÇÅ d‚ÇÇ : ‚Ñù}
  (H‚ÇÅ : dist g‚ÇÅ h‚ÇÅ ‚â§ d‚ÇÅ) (H‚ÇÇ : dist g‚ÇÇ h‚ÇÇ ‚â§ d‚ÇÇ) :
  dist (g‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + h‚ÇÇ) ‚â§ d‚ÇÅ + d‚ÇÇ :=
le_trans (dist_add_add_le g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ) (add_le_add H‚ÇÅ H‚ÇÇ)

lemma dist_sub_sub_le (g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ : E) :
  dist (g‚ÇÅ - g‚ÇÇ) (h‚ÇÅ - h‚ÇÇ) ‚â§ dist g‚ÇÅ h‚ÇÅ + dist g‚ÇÇ h‚ÇÇ :=
by simpa only [sub_eq_add_neg, dist_neg_neg] using dist_add_add_le g‚ÇÅ (-g‚ÇÇ) h‚ÇÅ (-h‚ÇÇ)

lemma dist_sub_sub_le_of_le {g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ : E} {d‚ÇÅ d‚ÇÇ : ‚Ñù}
  (H‚ÇÅ : dist g‚ÇÅ h‚ÇÅ ‚â§ d‚ÇÅ) (H‚ÇÇ : dist g‚ÇÇ h‚ÇÇ ‚â§ d‚ÇÇ) :
  dist (g‚ÇÅ - g‚ÇÇ) (h‚ÇÅ - h‚ÇÇ) ‚â§ d‚ÇÅ + d‚ÇÇ :=
le_trans (dist_sub_sub_le g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ) (add_le_add H‚ÇÅ H‚ÇÇ)

lemma abs_dist_sub_le_dist_add_add (g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ : E) :
  |dist g‚ÇÅ h‚ÇÅ - dist g‚ÇÇ h‚ÇÇ| ‚â§ dist (g‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + h‚ÇÇ) :=
by simpa only [dist_add_left, dist_add_right, dist_comm h‚ÇÇ]
  using abs_dist_sub_le (g‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + h‚ÇÇ) (h‚ÇÅ + g‚ÇÇ)

@[simp] lemma norm_nonneg (g : E) : 0 ‚â§ ‚à•g‚à• :=
by { rw[‚Üêdist_zero_right], exact dist_nonneg }

@[simp] lemma norm_zero : ‚à•(0 : E)‚à• = 0 :=  by rw [‚Üê dist_zero_right, dist_self]

@[nontriviality] lemma norm_of_subsingleton [subsingleton E] (x : E) : ‚à•x‚à• = 0 :=
by rw [subsingleton.elim x 0, norm_zero]

lemma norm_sum_le (s : finset Œπ) (f : Œπ ‚Üí E) : ‚à•‚àë i in s, f i‚à• ‚â§ ‚àë i in s, ‚à•f i‚à• :=
s.le_sum_of_subadditive norm norm_zero norm_add_le f

lemma norm_sum_le_of_le (s : finset Œπ) {f : Œπ ‚Üí E} {n : Œπ ‚Üí ‚Ñù} (h : ‚àÄ b ‚àà s, ‚à•f b‚à• ‚â§ n b) :
  ‚à•‚àë b in s, f b‚à• ‚â§ ‚àë b in s, n b :=
le_trans (norm_sum_le s f) (finset.sum_le_sum h)

lemma dist_sum_sum_le_of_le (s : finset Œπ) {f g : Œπ ‚Üí E} {d : Œπ ‚Üí ‚Ñù}
  (h : ‚àÄ b ‚àà s, dist (f b) (g b) ‚â§ d b) :
  dist (‚àë b in s, f b) (‚àë b in s, g b) ‚â§ ‚àë b in s, d b :=
begin
  simp only [dist_eq_norm, ‚Üê finset.sum_sub_distrib] at *,
  exact norm_sum_le_of_le s h
end

lemma dist_sum_sum_le (s : finset Œπ) (f g : Œπ ‚Üí E) :
  dist (‚àë b in s, f b) (‚àë b in s, g b) ‚â§ ‚àë b in s, dist (f b) (g b) :=
dist_sum_sum_le_of_le s (Œª _ _, le_rfl)

lemma norm_sub_le (g h : E) : ‚à•g - h‚à• ‚â§ ‚à•g‚à• + ‚à•h‚à• :=
by simpa [dist_eq_norm] using dist_triangle g 0 h

lemma norm_sub_le_of_le {g‚ÇÅ g‚ÇÇ : E} {n‚ÇÅ n‚ÇÇ : ‚Ñù} (H‚ÇÅ : ‚à•g‚ÇÅ‚à• ‚â§ n‚ÇÅ) (H‚ÇÇ : ‚à•g‚ÇÇ‚à• ‚â§ n‚ÇÇ) :
  ‚à•g‚ÇÅ - g‚ÇÇ‚à• ‚â§ n‚ÇÅ + n‚ÇÇ :=
le_trans (norm_sub_le g‚ÇÅ g‚ÇÇ) (add_le_add H‚ÇÅ H‚ÇÇ)

lemma dist_le_norm_add_norm (g h : E) : dist g h ‚â§ ‚à•g‚à• + ‚à•h‚à• :=
by { rw dist_eq_norm, apply norm_sub_le }

lemma abs_norm_sub_norm_le (g h : E) : |‚à•g‚à• - ‚à•h‚à•| ‚â§ ‚à•g - h‚à• :=
by simpa [dist_eq_norm] using abs_dist_sub_le g h 0

lemma norm_sub_norm_le (g h : E) : ‚à•g‚à• - ‚à•h‚à• ‚â§ ‚à•g - h‚à• :=
le_trans (le_abs_self _) (abs_norm_sub_norm_le g h)

lemma dist_norm_norm_le (g h : E) : dist ‚à•g‚à• ‚à•h‚à• ‚â§ ‚à•g - h‚à• :=
abs_norm_sub_norm_le g h

lemma norm_le_insert (u v : E) : ‚à•v‚à• ‚â§ ‚à•u‚à• + ‚à•u - v‚à• :=
calc ‚à•v‚à• = ‚à•u - (u - v)‚à• : by abel
... ‚â§ ‚à•u‚à• + ‚à•u - v‚à• : norm_sub_le u _

lemma norm_le_insert' (u v : E) : ‚à•u‚à• ‚â§ ‚à•v‚à• + ‚à•u - v‚à• :=
by { rw norm_sub_rev, exact norm_le_insert v u }

lemma norm_le_add_norm_add (u v : E) :
  ‚à•u‚à• ‚â§ ‚à•u + v‚à• + ‚à•v‚à• :=
calc ‚à•u‚à• = ‚à•u + v - v‚à• : by rw add_sub_cancel
... ‚â§ ‚à•u + v‚à• + ‚à•v‚à• : norm_sub_le _ _

lemma ball_zero_eq (Œµ : ‚Ñù) : ball (0 : E) Œµ = {x | ‚à•x‚à• < Œµ} :=
set.ext $ assume a, by simp

lemma mem_ball_iff_norm {g h : E} {r : ‚Ñù} :
  h ‚àà ball g r ‚Üî ‚à•h - g‚à• < r :=
by rw [mem_ball, dist_eq_norm]

lemma add_mem_ball_iff_norm {g h : E} {r : ‚Ñù} :
  g + h ‚àà ball g r ‚Üî ‚à•h‚à• < r :=
by rw [mem_ball_iff_norm, add_sub_cancel']

lemma mem_ball_iff_norm' {g h : E} {r : ‚Ñù} :
  h ‚àà ball g r ‚Üî ‚à•g - h‚à• < r :=
by rw [mem_ball', dist_eq_norm]

@[simp] lemma mem_ball_zero_iff {Œµ : ‚Ñù} {x : E} : x ‚àà ball (0 : E) Œµ ‚Üî ‚à•x‚à• < Œµ :=
by rw [mem_ball, dist_zero_right]

lemma mem_closed_ball_iff_norm {g h : E} {r : ‚Ñù} :
  h ‚àà closed_ball g r ‚Üî ‚à•h - g‚à• ‚â§ r :=
by rw [mem_closed_ball, dist_eq_norm]

@[simp] lemma mem_closed_ball_zero_iff {Œµ : ‚Ñù} {x : E} : x ‚àà closed_ball (0 : E) Œµ ‚Üî ‚à•x‚à• ‚â§ Œµ :=
by rw [mem_closed_ball, dist_zero_right]

lemma add_mem_closed_ball_iff_norm {g h : E} {r : ‚Ñù} :
  g + h ‚àà closed_ball g r ‚Üî ‚à•h‚à• ‚â§ r :=
by rw [mem_closed_ball_iff_norm, add_sub_cancel']

lemma mem_closed_ball_iff_norm' {g h : E} {r : ‚Ñù} :
  h ‚àà closed_ball g r ‚Üî ‚à•g - h‚à• ‚â§ r :=
by rw [mem_closed_ball', dist_eq_norm]

lemma norm_le_of_mem_closed_ball {g h : E} {r : ‚Ñù} (H : h ‚àà closed_ball g r) :
  ‚à•h‚à• ‚â§ ‚à•g‚à• + r :=
calc
  ‚à•h‚à• = ‚à•g + (h - g)‚à• : by rw [add_sub_cancel'_right]
  ... ‚â§ ‚à•g‚à• + ‚à•h - g‚à•  : norm_add_le _ _
  ... ‚â§ ‚à•g‚à• + r : by { apply add_le_add_left, rw ‚Üê dist_eq_norm, exact H }

lemma norm_le_norm_add_const_of_dist_le {a b : E} {c : ‚Ñù} (h : dist a b ‚â§ c) :
  ‚à•a‚à• ‚â§ ‚à•b‚à• + c :=
norm_le_of_mem_closed_ball h

lemma norm_lt_of_mem_ball {g h : E} {r : ‚Ñù} (H : h ‚àà ball g r) :
  ‚à•h‚à• < ‚à•g‚à• + r :=
calc
  ‚à•h‚à• = ‚à•g + (h - g)‚à• : by rw [add_sub_cancel'_right]
  ... ‚â§ ‚à•g‚à• + ‚à•h - g‚à•  : norm_add_le _ _
  ... < ‚à•g‚à• + r : by { apply add_lt_add_left, rw ‚Üê dist_eq_norm, exact H }

lemma norm_lt_norm_add_const_of_dist_lt {a b : E} {c : ‚Ñù} (h : dist a b < c) :
  ‚à•a‚à• < ‚à•b‚à• + c :=
norm_lt_of_mem_ball h

lemma bounded_iff_forall_norm_le {s : set E} : bounded s ‚Üî ‚àÉ C, ‚àÄ x ‚àà s, ‚à•x‚à• ‚â§ C :=
by simpa only [set.subset_def, mem_closed_ball_iff_norm, sub_zero]
  using bounded_iff_subset_ball (0 : E)

@[simp] lemma preimage_add_ball (x y : E) (r : ‚Ñù) : ((+) y) ‚Åª¬π' (ball x r) = ball (x - y) r :=
begin
  ext z,
  simp only [dist_eq_norm, set.mem_preimage, mem_ball],
  abel
end

@[simp] lemma preimage_add_closed_ball (x y : E) (r : ‚Ñù) :
  ((+) y) ‚Åª¬π' (closed_ball x r) = closed_ball (x - y) r :=
begin
  ext z,
  simp only [dist_eq_norm, set.mem_preimage, mem_closed_ball],
  abel
end

@[simp] lemma mem_sphere_iff_norm (v w : E) (r : ‚Ñù) : w ‚àà sphere v r ‚Üî ‚à•w - v‚à• = r :=
by simp [dist_eq_norm]

@[simp] lemma mem_sphere_zero_iff_norm {w : E} {r : ‚Ñù} : w ‚àà sphere (0:E) r ‚Üî ‚à•w‚à• = r :=
by simp [dist_eq_norm]

@[simp] lemma norm_eq_of_mem_sphere {r : ‚Ñù} (x : sphere (0:E) r) : ‚à•(x:E)‚à• = r :=
mem_sphere_zero_iff_norm.mp x.2

lemma preimage_add_sphere (x y : E) (r : ‚Ñù) :
  ((+) y) ‚Åª¬π' (sphere x r) = sphere (x - y) r :=
begin
  ext z,
  simp only [set.mem_preimage, mem_sphere_iff_norm],
  abel
end

lemma ne_zero_of_norm_pos {g : E} : 0 < ‚à• g ‚à• ‚Üí g ‚â† 0 :=
begin
  intros hpos hzero,
  rw [hzero, norm_zero] at hpos,
  exact lt_irrefl 0 hpos,
end

lemma nonzero_of_mem_sphere {r : ‚Ñù} (hr : 0 < r) (x : sphere (0:E) r) : (x:E) ‚â† 0 :=
begin
  refine ne_zero_of_norm_pos _,
  rwa norm_eq_of_mem_sphere x,
end

lemma nonzero_of_mem_unit_sphere (x : sphere (0:E) 1) : (x:E) ‚â† 0 :=
by { apply nonzero_of_mem_sphere, norm_num }

/-- We equip the sphere, in a seminormed group, with a formal operation of negation, namely the
antipodal map. -/
instance {r : ‚Ñù} : has_neg (sphere (0:E) r) :=
{ neg := Œª w, ‚ü®-‚Üëw, by simp‚ü© }

@[simp] lemma coe_neg_sphere {r : ‚Ñù} (v : sphere (0:E) r) :
  (((-v) : sphere _ _) : E) = - (v:E) :=
rfl

namespace isometric
-- TODO This material is superseded by similar constructions such as
-- `affine_isometry_equiv.const_vadd`; deduplicate

/-- Addition `y ‚Ü¶ y + x` as an `isometry`. -/
protected def add_right (x : E) : E ‚âÉ·µ¢ E :=
{ isometry_to_fun := isometry_emetric_iff_metric.2 $ Œª y z, dist_add_right _ _ _,
  .. equiv.add_right x }

@[simp] lemma add_right_to_equiv (x : E) :
  (isometric.add_right x).to_equiv = equiv.add_right x := rfl

@[simp] lemma coe_add_right (x : E) : (isometric.add_right x : E ‚Üí E) = Œª y, y + x := rfl

lemma add_right_apply (x y : E) : (isometric.add_right x : E ‚Üí E) y = y + x := rfl

@[simp] lemma add_right_symm (x : E) :
  (isometric.add_right x).symm = isometric.add_right (-x) :=
ext $ Œª y, rfl

/-- Addition `y ‚Ü¶ x + y` as an `isometry`. -/
protected def add_left (x : E) : E ‚âÉ·µ¢ E :=
{ isometry_to_fun := isometry_emetric_iff_metric.2 $ Œª y z, dist_add_left _ _ _,
  to_equiv := equiv.add_left x }

@[simp] lemma add_left_to_equiv (x : E) :
  (isometric.add_left x).to_equiv = equiv.add_left x := rfl

@[simp] lemma coe_add_left (x : E) : ‚áë(isometric.add_left x) = (+) x := rfl

@[simp] lemma add_left_symm (x : E) :
  (isometric.add_left x).symm = isometric.add_left (-x) :=
ext $ Œª y, rfl

variable (E)

/-- Negation `x ‚Ü¶ -x` as an `isometry`. -/
protected def neg : E ‚âÉ·µ¢ E :=
{ isometry_to_fun := isometry_emetric_iff_metric.2 $ Œª x y, dist_neg_neg _ _,
  to_equiv := equiv.neg E }

variable {E}

@[simp] lemma neg_symm : (isometric.neg E).symm = isometric.neg E := rfl

@[simp] lemma neg_to_equiv : (isometric.neg E).to_equiv = equiv.neg E := rfl

@[simp] lemma coe_neg : ‚áë(isometric.neg E) = has_neg.neg := rfl

end isometric

theorem normed_group.tendsto_nhds_zero {f : Œ± ‚Üí E} {l : filter Œ±} :
  tendsto f l (ùìù 0) ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in l, ‚à• f x ‚à• < Œµ :=
metric.tendsto_nhds.trans $ by simp only [dist_zero_right]

lemma normed_group.tendsto_nhds_nhds {f : E ‚Üí F} {x : E} {y : F} :
  tendsto f (ùìù x) (ùìù y) ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x', ‚à•x' - x‚à• < Œ¥ ‚Üí ‚à•f x' - y‚à• < Œµ :=
by simp_rw [metric.tendsto_nhds_nhds, dist_eq_norm]

lemma normed_group.cauchy_seq_iff [nonempty Œ±] [semilattice_sup Œ±] {u : Œ± ‚Üí E} :
  cauchy_seq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ m n, N ‚â§ m ‚Üí N ‚â§ n ‚Üí ‚à•u m - u n‚à• < Œµ :=
by simp [metric.cauchy_seq_iff, dist_eq_norm]

lemma normed_group.uniformity_basis_dist :
  (ùì§ E).has_basis (Œª (Œµ : ‚Ñù), 0 < Œµ) (Œª Œµ, {p : E √ó E | ‚à•p.fst - p.snd‚à• < Œµ}) :=
begin
  convert metric.uniformity_basis_dist,
  simp [dist_eq_norm]
end

open finset

/-- A homomorphism `f` of seminormed groups is Lipschitz, if there exists a constant `C` such that
for all `x`, one has `‚à•f x‚à• ‚â§ C * ‚à•x‚à•`. The analogous condition for a linear map of
(semi)normed spaces is in `normed_space.operator_norm`. -/
lemma add_monoid_hom.lipschitz_of_bound (f : E ‚Üí+ F) (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  lipschitz_with (real.to_nnreal C) f :=
lipschitz_with.of_dist_le' $ Œª x y, by simpa only [dist_eq_norm, f.map_sub] using h (x - y)

lemma lipschitz_on_with_iff_norm_sub_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} {s : set E} :
  lipschitz_on_with C f s ‚Üî  ‚àÄ (x ‚àà s) (y ‚àà s), ‚à•f x - f y‚à• ‚â§ C * ‚à•x - y‚à• :=
by simp only [lipschitz_on_with_iff_dist_le_mul, dist_eq_norm]

lemma lipschitz_on_with.norm_sub_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} {s : set E} (h : lipschitz_on_with C f s)
  {x y : E} (x_in : x ‚àà s) (y_in : y ‚àà s) : ‚à•f x - f y‚à• ‚â§ C * ‚à•x - y‚à• :=
lipschitz_on_with_iff_norm_sub_le.mp h x x_in y y_in

lemma lipschitz_on_with.norm_sub_le_of_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} {s : set E}
  (h : lipschitz_on_with C f s){x y : E} (x_in : x ‚àà s) (y_in : y ‚àà s) {d : ‚Ñù} (hd : ‚à•x - y‚à• ‚â§ d) :
  ‚à•f x - f y‚à• ‚â§ C * d :=
(h.norm_sub_le x_in y_in).trans $ mul_le_mul_of_nonneg_left hd C.2

lemma lipschitz_with_iff_norm_sub_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} :
  lipschitz_with C f ‚Üî ‚àÄ x y, ‚à•f x - f y‚à• ‚â§ C * ‚à•x - y‚à• :=
by simp only [lipschitz_with_iff_dist_le_mul, dist_eq_norm]

alias lipschitz_with_iff_norm_sub_le ‚Üî lipschitz_with.norm_sub_le _

lemma lipschitz_with.norm_sub_le_of_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} (h : lipschitz_with C f)
  {x y : E} {d : ‚Ñù} (hd : ‚à•x - y‚à• ‚â§ d) :
  ‚à•f x - f y‚à• ‚â§ C * d :=
(h.norm_sub_le x y).trans $ mul_le_mul_of_nonneg_left hd C.2

/-- A homomorphism `f` of seminormed groups is continuous, if there exists a constant `C` such that
for all `x`, one has `‚à•f x‚à• ‚â§ C * ‚à•x‚à•`.
The analogous condition for a linear map of normed spaces is in `normed_space.operator_norm`. -/
lemma add_monoid_hom.continuous_of_bound (f : E ‚Üí+ F) (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  continuous f :=
(f.lipschitz_of_bound C h).continuous

lemma is_compact.exists_bound_of_continuous_on [topological_space Œ±]
  {s : set Œ±} (hs : is_compact s) {f : Œ± ‚Üí E} (hf : continuous_on f s) :
  ‚àÉ C, ‚àÄ x ‚àà s, ‚à•f x‚à• ‚â§ C :=
begin
  have : bounded (f '' s) := (hs.image_of_continuous_on hf).bounded,
  rcases bounded_iff_forall_norm_le.1 this with ‚ü®C, hC‚ü©,
  exact ‚ü®C, Œª x hx, hC _ (set.mem_image_of_mem _ hx)‚ü©,
end

lemma add_monoid_hom.isometry_iff_norm (f : E ‚Üí+ F) : isometry f ‚Üî ‚àÄ x, ‚à•f x‚à• = ‚à•x‚à• :=
begin
  simp only [isometry_emetric_iff_metric, dist_eq_norm, ‚Üê f.map_sub],
  refine ‚ü®Œª h x, _, Œª h x y, h _‚ü©,
  simpa using h x 0
end

lemma add_monoid_hom.isometry_of_norm (f : E ‚Üí+ F) (hf : ‚àÄ x, ‚à•f x‚à• = ‚à•x‚à•) : isometry f :=
f.isometry_iff_norm.2 hf

lemma controlled_sum_of_mem_closure {s : add_subgroup E} {g : E}
  (hg : g ‚àà closure (s : set E)) {b : ‚Ñï ‚Üí ‚Ñù} (b_pos : ‚àÄ n, 0 < b n) :
  ‚àÉ v : ‚Ñï ‚Üí E,
    tendsto (Œª n, ‚àë i in range (n+1), v i) at_top (ùìù g) ‚àß
    (‚àÄ n, v n ‚àà s) ‚àß
    ‚à•v 0 - g‚à• < b 0 ‚àß
    ‚àÄ n > 0, ‚à•v n‚à• < b n :=
begin
  obtain ‚ü®u : ‚Ñï ‚Üí E, u_in : ‚àÄ n, u n ‚àà s, lim_u : tendsto u at_top (ùìù g)‚ü© :=
    mem_closure_iff_seq_limit.mp hg,
  obtain ‚ü®n‚ÇÄ, hn‚ÇÄ‚ü© : ‚àÉ n‚ÇÄ, ‚àÄ n ‚â• n‚ÇÄ, ‚à•u n - g‚à• < b 0,
  { have : {x | ‚à•x - g‚à• < b 0} ‚àà ùìù g,
    { simp_rw ‚Üê dist_eq_norm,
      exact metric.ball_mem_nhds _ (b_pos _) },
    exact filter.tendsto_at_top'.mp lim_u _ this },
  set z : ‚Ñï ‚Üí E := Œª n, u (n + n‚ÇÄ),
  have lim_z : tendsto z at_top (ùìù g) := lim_u.comp (tendsto_add_at_top_nat n‚ÇÄ),
  have mem_ùì§ : ‚àÄ n, {p : E √ó E | ‚à•p.1 - p.2‚à• < b (n + 1)} ‚àà ùì§ E :=
  Œª n, by simpa [‚Üê dist_eq_norm] using metric.dist_mem_uniformity (b_pos $ n+1),
  obtain ‚ü®œÜ : ‚Ñï ‚Üí ‚Ñï, œÜ_extr : strict_mono œÜ,
          hœÜ : ‚àÄ n, ‚à•z (œÜ $ n + 1) - z (œÜ n)‚à• < b (n + 1)‚ü© :=
    lim_z.cauchy_seq.subseq_mem mem_ùì§,
  set w : ‚Ñï ‚Üí E := z ‚àò œÜ,
  have hw : tendsto w at_top (ùìù g),
    from lim_z.comp œÜ_extr.tendsto_at_top,
  set v : ‚Ñï ‚Üí E := Œª i, if i = 0 then w 0 else w i - w (i - 1),
  refine ‚ü®v, tendsto.congr (finset.eq_sum_range_sub' w) hw , _,
          hn‚ÇÄ _ (n‚ÇÄ.le_add_left _), _‚ü©,
  { rintro ‚ü®‚ü©,
    { change w 0 ‚àà s,
      apply u_in },
    { apply s.sub_mem ; apply u_in }, },
  { intros l hl,
    obtain ‚ü®k, rfl‚ü© : ‚àÉ k, l = k+1, exact nat.exists_eq_succ_of_ne_zero (ne_of_gt hl),
    apply hœÜ },
end

lemma controlled_sum_of_mem_closure_range {j : E ‚Üí+ F} {h : F}
  (Hh : h ‚àà (closure $ (j.range : set F))) {b : ‚Ñï ‚Üí ‚Ñù} (b_pos : ‚àÄ n, 0 < b n) :
  ‚àÉ g : ‚Ñï ‚Üí E,
    tendsto (Œª n, ‚àë i in range (n+1), j (g i)) at_top (ùìù h) ‚àß
    ‚à•j (g 0) - h‚à• < b 0 ‚àß
    ‚àÄ n > 0, ‚à•j (g n)‚à• < b n :=
begin
  rcases controlled_sum_of_mem_closure Hh b_pos with ‚ü®v, sum_v, v_in, hv‚ÇÄ, hv_pos‚ü©,
  choose g hg using v_in,
  change ‚àÄ (n : ‚Ñï), j (g n) = v n at hg,
  refine ‚ü®g, by simpa [‚Üê hg] using sum_v, by simpa [hg 0] using hv‚ÇÄ, Œª n hn,
          by simpa [hg] using hv_pos n hn‚ü©
end

section nnnorm

/-- Auxiliary class, endowing a type `Œ±` with a function `nnnorm : Œ± ‚Üí ‚Ñù‚â•0`. -/
class has_nnnorm (E : Type*) := (nnnorm : E ‚Üí ‚Ñù‚â•0)

export has_nnnorm (nnnorm)

notation `‚à•`e`‚à•‚Çä` := nnnorm e

@[priority 100] -- see Note [lower instance priority]
instance semi_normed_group.to_has_nnnorm : has_nnnorm E := ‚ü®Œª a, ‚ü®norm a, norm_nonneg a‚ü©‚ü©

@[simp, norm_cast] lemma coe_nnnorm (a : E) : (‚à•a‚à•‚Çä : ‚Ñù) = norm a := rfl

lemma norm_to_nnreal {a : E} : ‚à•a‚à•.to_nnreal = ‚à•a‚à•‚Çä :=
@real.to_nnreal_coe ‚à•a‚à•‚Çä

lemma nndist_eq_nnnorm (a b : E) : nndist a b = ‚à•a - b‚à•‚Çä := nnreal.eq $ dist_eq_norm _ _

@[simp] lemma nnnorm_zero : ‚à•(0 : E)‚à•‚Çä = 0 :=
nnreal.eq norm_zero

lemma nnnorm_add_le (g h : E) : ‚à•g + h‚à•‚Çä ‚â§ ‚à•g‚à•‚Çä + ‚à•h‚à•‚Çä :=
nnreal.coe_le_coe.1 $ norm_add_le g h

@[simp] lemma nnnorm_neg (g : E) : ‚à•-g‚à•‚Çä = ‚à•g‚à•‚Çä :=
nnreal.eq $ norm_neg g

lemma nndist_nnnorm_nnnorm_le (g h : E) : nndist ‚à•g‚à•‚Çä ‚à•h‚à•‚Çä ‚â§ ‚à•g - h‚à•‚Çä :=
nnreal.coe_le_coe.1 $ dist_norm_norm_le g h

lemma of_real_norm_eq_coe_nnnorm (x : E) : ennreal.of_real ‚à•x‚à• = (‚à•x‚à•‚Çä : ‚Ñù‚â•0‚àû) :=
ennreal.of_real_eq_coe_nnreal _

lemma edist_eq_coe_nnnorm_sub (x y : E) : edist x y = (‚à•x - y‚à•‚Çä : ‚Ñù‚â•0‚àû) :=
by rw [edist_dist, dist_eq_norm, of_real_norm_eq_coe_nnnorm]

lemma edist_eq_coe_nnnorm (x : E) : edist x 0 = (‚à•x‚à•‚Çä : ‚Ñù‚â•0‚àû) :=
by rw [edist_eq_coe_nnnorm_sub, _root_.sub_zero]

lemma mem_emetric_ball_zero_iff {x : E} {r : ‚Ñù‚â•0‚àû} : x ‚àà emetric.ball (0 : E) r ‚Üî ‚Üë‚à•x‚à•‚Çä < r :=
by rw [emetric.mem_ball, edist_eq_coe_nnnorm]

lemma nndist_add_add_le (g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ : E) :
  nndist (g‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + h‚ÇÇ) ‚â§ nndist g‚ÇÅ h‚ÇÅ + nndist g‚ÇÇ h‚ÇÇ :=
nnreal.coe_le_coe.1 $ dist_add_add_le g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ

lemma edist_add_add_le (g‚ÇÅ g‚ÇÇ h‚ÇÅ h‚ÇÇ : E) :
  edist (g‚ÇÅ + g‚ÇÇ) (h‚ÇÅ + h‚ÇÇ) ‚â§ edist g‚ÇÅ h‚ÇÅ + edist g‚ÇÇ h‚ÇÇ :=
by { simp only [edist_nndist], norm_cast, apply nndist_add_add_le }

lemma nnnorm_sum_le (s : finset Œπ) (f : Œπ ‚Üí E) :
  ‚à•‚àë a in s, f a‚à•‚Çä ‚â§ ‚àë a in s, ‚à•f a‚à•‚Çä :=
s.le_sum_of_subadditive nnnorm nnnorm_zero nnnorm_add_le f

lemma add_monoid_hom.lipschitz_of_bound_nnnorm (f : E ‚Üí+ F) (C : ‚Ñù‚â•0) (h : ‚àÄ x, ‚à•f x‚à•‚Çä ‚â§ C * ‚à•x‚à•‚Çä) :
  lipschitz_with C f :=
@real.to_nnreal_coe C ‚ñ∏ f.lipschitz_of_bound C h

end nnnorm

namespace lipschitz_with

variables [pseudo_emetric_space Œ±] {K Kf Kg : ‚Ñù‚â•0} {f g : Œ± ‚Üí E}

lemma neg (hf : lipschitz_with K f) : lipschitz_with K (Œª x, -f x) :=
Œª x y, by simpa only [edist_dist, dist_neg_neg] using hf x y

lemma add (hf : lipschitz_with Kf f) (hg : lipschitz_with Kg g) :
  lipschitz_with (Kf + Kg) (Œª x, f x + g x) :=
Œª x y,
calc edist (f x + g x) (f y + g y) ‚â§ edist (f x) (f y) + edist (g x) (g y) :
  edist_add_add_le _ _ _ _
... ‚â§ Kf * edist x y + Kg * edist x y :
  add_le_add (hf x y) (hg x y)
... = (Kf + Kg) * edist x y :
  (add_mul _ _ _).symm

lemma sub (hf : lipschitz_with Kf f) (hg : lipschitz_with Kg g) :
  lipschitz_with (Kf + Kg) (Œª x, f x - g x) :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

end lipschitz_with

namespace antilipschitz_with

variables [pseudo_emetric_space Œ±] {K Kf Kg : ‚Ñù‚â•0} {f g : Œ± ‚Üí E}

lemma add_lipschitz_with (hf : antilipschitz_with Kf f) (hg : lipschitz_with Kg g)
  (hK : Kg < Kf‚Åª¬π) : antilipschitz_with (Kf‚Åª¬π - Kg)‚Åª¬π (Œª x, f x + g x) :=
begin
  letI : pseudo_metric_space Œ± := pseudo_emetric_space.to_pseudo_metric_space hf.edist_ne_top,
  refine antilipschitz_with.of_le_mul_dist (Œª x y, _),
  rw [nnreal.coe_inv, ‚Üê div_eq_inv_mul],
  rw le_div_iff (nnreal.coe_pos.2 $ tsub_pos_iff_lt.2 hK),
  rw [mul_comm, nnreal.coe_sub hK.le, sub_mul],
  calc ‚ÜëKf‚Åª¬π * dist x y - Kg * dist x y ‚â§ dist (f x) (f y) - dist (g x) (g y) :
    sub_le_sub (hf.mul_le_dist x y) (hg.dist_le_mul x y)
  ... ‚â§ _ : le_trans (le_abs_self _) (abs_dist_sub_le_dist_add_add _ _ _ _)
end

lemma add_sub_lipschitz_with (hf : antilipschitz_with Kf f) (hg : lipschitz_with Kg (g - f))
  (hK : Kg < Kf‚Åª¬π) : antilipschitz_with (Kf‚Åª¬π - Kg)‚Åª¬π g :=
by simpa only [pi.sub_apply, add_sub_cancel'_right] using hf.add_lipschitz_with hg hK

end antilipschitz_with

/-- A group homomorphism from an `add_comm_group` to a `semi_normed_group` induces a
`semi_normed_group` structure on the domain.

See note [reducible non-instances] -/
@[reducible]
def semi_normed_group.induced {E} [add_comm_group E] (f : E ‚Üí+ F) : semi_normed_group E :=
{ norm    := Œª x, ‚à•f x‚à•,
  dist_eq := Œª x y, by simpa only [add_monoid_hom.map_sub, ‚Üê dist_eq_norm],
  .. pseudo_metric_space.induced f semi_normed_group.to_pseudo_metric_space, }

/-- A subgroup of a seminormed group is also a seminormed group,
with the restriction of the norm. -/
instance add_subgroup.semi_normed_group (s : add_subgroup E) : semi_normed_group s :=
semi_normed_group.induced s.subtype

/-- If `x` is an element of a subgroup `s` of a seminormed group `E`, its norm in `s` is equal to
its norm in `E`. -/
@[simp] lemma coe_norm_subgroup {E : Type*} [semi_normed_group E] {s : add_subgroup E} (x : s) :
  ‚à•x‚à• = ‚à•(x:E)‚à• :=
rfl

/-- A submodule of a seminormed group is also a seminormed group, with the restriction of the norm.

See note [implicit instance arguments]. -/
instance submodule.semi_normed_group {ùïú : Type*} {_ : ring ùïú}
  {E : Type*} [semi_normed_group E] {_ : module ùïú E} (s : submodule ùïú E) : semi_normed_group s :=
{ norm := Œªx, norm (x : E),
  dist_eq := Œªx y, dist_eq_norm (x : E) (y : E) }

/-- If `x` is an element of a submodule `s` of a normed group `E`, its norm in `E` is equal to its
norm in `s`.

See note [implicit instance arguments]. -/
@[simp, norm_cast] lemma submodule.norm_coe {ùïú : Type*} {_ : ring ùïú}
  {E : Type*} [semi_normed_group E] {_ : module ùïú E} {s : submodule ùïú E} (x : s) :
  ‚à•(x : E)‚à• = ‚à•x‚à• :=
rfl

@[simp] lemma submodule.norm_mk {ùïú : Type*} {_ : ring ùïú}
  {E : Type*} [semi_normed_group E] {_ : module ùïú E} {s : submodule ùïú E} (x : E) (hx : x ‚àà s) :
  ‚à•(‚ü®x, hx‚ü© : s)‚à• = ‚à•x‚à• :=
rfl

/-- seminormed group instance on the product of two seminormed groups, using the sup norm. -/
noncomputable instance prod.semi_normed_group : semi_normed_group (E √ó F) :=
{ norm := Œªx, max ‚à•x.1‚à• ‚à•x.2‚à•,
  dist_eq := assume (x y : E √ó F),
    show max (dist x.1 y.1) (dist x.2 y.2) = (max ‚à•(x - y).1‚à• ‚à•(x - y).2‚à•), by simp [dist_eq_norm] }

lemma prod.semi_norm_def (x : E √ó F) : ‚à•x‚à• = (max ‚à•x.1‚à• ‚à•x.2‚à•) := rfl

lemma prod.nnsemi_norm_def (x : E √ó F) : ‚à•x‚à•‚Çä = max (‚à•x.1‚à•‚Çä) (‚à•x.2‚à•‚Çä) :=
by { have := x.semi_norm_def, simp only [‚Üê coe_nnnorm] at this, exact_mod_cast this }

lemma semi_norm_fst_le (x : E √ó F) : ‚à•x.1‚à• ‚â§ ‚à•x‚à• :=
le_max_left _ _

lemma semi_norm_snd_le (x : E √ó F) : ‚à•x.2‚à• ‚â§ ‚à•x‚à• :=
le_max_right _ _

lemma semi_norm_prod_le_iff {x : E √ó F} {r : ‚Ñù} :
  ‚à•x‚à• ‚â§ r ‚Üî ‚à•x.1‚à• ‚â§ r ‚àß ‚à•x.2‚à• ‚â§ r :=
max_le_iff

/-- seminormed group instance on the product of finitely many seminormed groups,
using the sup norm. -/
noncomputable instance pi.semi_normed_group {œÄ : Œπ ‚Üí Type*} [fintype Œπ]
  [Œ† i, semi_normed_group (œÄ i)] : semi_normed_group (Œ† i, œÄ i) :=
{ norm := Œªf, ((finset.sup finset.univ (Œª b, ‚à•f b‚à•‚Çä) : ‚Ñù‚â•0) : ‚Ñù),
  dist_eq := assume x y,
    congr_arg (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) $ congr_arg (finset.sup finset.univ) $ funext $ assume a,
    show nndist (x a) (y a) = ‚à•x a - y a‚à•‚Çä, from nndist_eq_nnnorm _ _ }

/-- The seminorm of an element in a product space is `‚â§ r` if and only if the norm of each
component is. -/
lemma pi_semi_norm_le_iff {œÄ : Œπ ‚Üí Type*} [fintype Œπ] [‚àÄi, semi_normed_group (œÄ i)] {r : ‚Ñù}
  (hr : 0 ‚â§ r) {x : Œ†i, œÄ i} : ‚à•x‚à• ‚â§ r ‚Üî ‚àÄi, ‚à•x i‚à• ‚â§ r :=
by simp only [‚Üê dist_zero_right, dist_pi_le_iff hr, pi.zero_apply]

/-- The seminorm of an element in a product space is `< r` if and only if the norm of each
component is. -/
lemma pi_semi_norm_lt_iff {œÄ : Œπ ‚Üí Type*} [fintype Œπ] [‚àÄi, semi_normed_group (œÄ i)] {r : ‚Ñù}
  (hr : 0 < r) {x : Œ†i, œÄ i} : ‚à•x‚à• < r ‚Üî ‚àÄi, ‚à•x i‚à• < r :=
by simp only [‚Üê dist_zero_right, dist_pi_lt_iff hr, pi.zero_apply]

lemma semi_norm_le_pi_norm {œÄ : Œπ ‚Üí Type*} [fintype Œπ] [‚àÄi, semi_normed_group (œÄ i)] (x : Œ†i, œÄ i)
  (i : Œπ) : ‚à•x i‚à• ‚â§ ‚à•x‚à• :=
(pi_semi_norm_le_iff (norm_nonneg x)).1 (le_refl _) i

@[simp] lemma pi_semi_norm_const [nonempty Œπ] [fintype Œπ] (a : E) : ‚à•(Œª i : Œπ, a)‚à• = ‚à•a‚à• :=
by simpa only [‚Üê dist_zero_right] using dist_pi_const a 0

@[simp] lemma pi_nnsemi_norm_const [nonempty Œπ] [fintype Œπ] (a : E) :
  ‚à•(Œª i : Œπ, a)‚à•‚Çä = ‚à•a‚à•‚Çä :=
nnreal.eq $ pi_semi_norm_const a

lemma tendsto_iff_norm_tendsto_zero {f : Œ± ‚Üí E} {a : filter Œ±} {b : E} :
  tendsto f a (ùìù b) ‚Üî tendsto (Œª e, ‚à•f e - b‚à•) a (ùìù 0) :=
by { convert tendsto_iff_dist_tendsto_zero, simp [dist_eq_norm] }

lemma is_bounded_under_of_tendsto {l : filter Œ±} {f : Œ± ‚Üí E} {c : E}
  (h : filter.tendsto f l (ùìù c)) : is_bounded_under (‚â§) l (Œª x, ‚à•f x‚à•) :=
‚ü®‚à•c‚à• + 1, @tendsto.eventually Œ± E f _ _ (Œª k, ‚à•k‚à• ‚â§ ‚à•c‚à• + 1) h (filter.eventually_iff_exists_mem.mpr
  ‚ü®metric.closed_ball c 1, metric.closed_ball_mem_nhds c zero_lt_one,
    Œª y hy, norm_le_norm_add_const_of_dist_le hy‚ü©)‚ü©

lemma tendsto_zero_iff_norm_tendsto_zero {f : Œ± ‚Üí E} {a : filter Œ±} :
  tendsto f a (ùìù 0) ‚Üî tendsto (Œª e, ‚à•f e‚à•) a (ùìù 0) :=
by { rw [tendsto_iff_norm_tendsto_zero], simp only [sub_zero] }

/-- Special case of the sandwich theorem: if the norm of `f` is eventually bounded by a real
function `g` which tends to `0`, then `f` tends to `0`.
In this pair of lemmas (`squeeze_zero_norm'` and `squeeze_zero_norm`), following a convention of
similar lemmas in `topology.metric_space.basic` and `topology.algebra.ordered`, the `'` version is
phrased using "eventually" and the non-`'` version is phrased absolutely. -/
lemma squeeze_zero_norm' {f : Œ± ‚Üí E} {g : Œ± ‚Üí ‚Ñù} {t‚ÇÄ : filter Œ±}
  (h : ‚àÄ·∂† n in t‚ÇÄ, ‚à•f n‚à• ‚â§ g n)
  (h' : tendsto g t‚ÇÄ (ùìù 0)) : tendsto f t‚ÇÄ (ùìù 0) :=
tendsto_zero_iff_norm_tendsto_zero.mpr
  (squeeze_zero' (eventually_of_forall (Œª n, norm_nonneg _)) h h')

/-- Special case of the sandwich theorem: if the norm of `f` is bounded by a real function `g` which
tends to `0`, then `f` tends to `0`.  -/
lemma squeeze_zero_norm {f : Œ± ‚Üí E} {g : Œ± ‚Üí ‚Ñù} {t‚ÇÄ : filter Œ±}
  (h : ‚àÄ n, ‚à•f n‚à• ‚â§ g n) (h' : tendsto g t‚ÇÄ (ùìù 0)) :
  tendsto f t‚ÇÄ (ùìù 0) :=
squeeze_zero_norm' (eventually_of_forall h) h'

lemma tendsto_norm_sub_self (x : E) : tendsto (Œª g : E, ‚à•g - x‚à•) (ùìù x) (ùìù 0) :=
by simpa [dist_eq_norm] using tendsto_id.dist (tendsto_const_nhds : tendsto (Œª g, (x:E)) (ùìù x) _)

lemma tendsto_norm {x : E} : tendsto (Œªg : E, ‚à•g‚à•) (ùìù x) (ùìù ‚à•x‚à•) :=
by simpa using tendsto_id.dist (tendsto_const_nhds : tendsto (Œª g, (0:E)) _ _)

lemma tendsto_norm_zero : tendsto (Œªg : E, ‚à•g‚à•) (ùìù 0) (ùìù 0) :=
by simpa using tendsto_norm_sub_self (0:E)

@[continuity]
lemma continuous_norm : continuous (Œªg:E, ‚à•g‚à•) :=
by simpa using continuous_id.dist (continuous_const : continuous (Œª g, (0:E)))

@[continuity]
lemma continuous_nnnorm : continuous (Œª (a : E), ‚à•a‚à•‚Çä) :=
continuous_subtype_mk _ continuous_norm

lemma lipschitz_with_one_norm : lipschitz_with 1 (norm : E ‚Üí ‚Ñù) :=
by simpa only [dist_zero_left] using lipschitz_with.dist_right (0 : E)

lemma uniform_continuous_norm : uniform_continuous (norm : E ‚Üí ‚Ñù) :=
lipschitz_with_one_norm.uniform_continuous

lemma uniform_continuous_nnnorm : uniform_continuous (Œª (a : E), ‚à•a‚à•‚Çä) :=
uniform_continuous_subtype_mk uniform_continuous_norm _

section

variables {l : filter Œ±} {f : Œ± ‚Üí E} {a : E}

lemma filter.tendsto.norm (h : tendsto f l (ùìù a)) : tendsto (Œª x, ‚à•f x‚à•) l (ùìù ‚à•a‚à•) :=
tendsto_norm.comp h

lemma filter.tendsto.nnnorm (h : tendsto f l (ùìù a)) :
  tendsto (Œª x, ‚à•f x‚à•‚Çä) l (ùìù (‚à•a‚à•‚Çä)) :=
tendsto.comp continuous_nnnorm.continuous_at h

end

section

variables [topological_space Œ±] {f : Œ± ‚Üí E} {s : set Œ±} {a : Œ±} {b : E}

lemma continuous.norm (h : continuous f) : continuous (Œª x, ‚à•f x‚à•) := continuous_norm.comp h

lemma continuous.nnnorm (h : continuous f) : continuous (Œª x, ‚à•f x‚à•‚Çä) :=
continuous_nnnorm.comp h

lemma continuous_at.norm (h : continuous_at f a) : continuous_at (Œª x, ‚à•f x‚à•) a := h.norm

lemma continuous_at.nnnorm (h : continuous_at f a) : continuous_at (Œª x, ‚à•f x‚à•‚Çä) a := h.nnnorm

lemma continuous_within_at.norm (h : continuous_within_at f s a) :
  continuous_within_at (Œª x, ‚à•f x‚à•) s a :=
h.norm

lemma continuous_within_at.nnnorm (h : continuous_within_at f s a) :
  continuous_within_at (Œª x, ‚à•f x‚à•‚Çä) s a :=
h.nnnorm

lemma continuous_on.norm (h : continuous_on f s) : continuous_on (Œª x, ‚à•f x‚à•) s :=
Œª x hx, (h x hx).norm

lemma continuous_on.nnnorm (h : continuous_on f s) : continuous_on (Œª x, ‚à•f x‚à•‚Çä) s :=
Œª x hx, (h x hx).nnnorm

end

/-- If `‚à•y‚à•‚Üí‚àû`, then we can assume `y‚â†x` for any fixed `x`. -/
lemma eventually_ne_of_tendsto_norm_at_top {l : filter Œ±} {f : Œ± ‚Üí E}
  (h : tendsto (Œª y, ‚à•f y‚à•) l at_top) (x : E) :
  ‚àÄ·∂† y in l, f y ‚â† x :=
begin
  have : ‚àÄ·∂† y in l, 1 + ‚à•x‚à• ‚â§ ‚à•f y‚à• := h (mem_at_top (1 + ‚à•x‚à•)),
  refine this.mono (Œª y hy hxy, _),
  subst x,
  exact not_le_of_lt zero_lt_one (add_le_iff_nonpos_left.1 hy)
end

@[priority 100] -- see Note [lower instance priority]
instance semi_normed_group.has_lipschitz_add : has_lipschitz_add E :=
{ lipschitz_add := ‚ü®2, lipschitz_with.prod_fst.add lipschitz_with.prod_snd‚ü© }

/-- A seminormed group is a uniform additive group, i.e., addition and subtraction are uniformly
continuous. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_uniform_group : uniform_add_group E :=
‚ü®(lipschitz_with.prod_fst.sub lipschitz_with.prod_snd).uniform_continuous‚ü©

@[priority 100] -- see Note [lower instance priority]
instance normed_top_group : topological_add_group E :=
by apply_instance -- short-circuit type class inference

lemma nat.norm_cast_le [has_one E] : ‚àÄ n : ‚Ñï, ‚à•(n : E)‚à• ‚â§ n * ‚à•(1 : E)‚à•
| 0 := by simp
| (n + 1) := by { rw [n.cast_succ, n.cast_succ, add_mul, one_mul],
                  exact norm_add_le_of_le (nat.norm_cast_le n) le_rfl }

lemma semi_normed_group.mem_closure_iff {s : set E} {x : E} :
  x ‚àà closure s ‚Üî ‚àÄ Œµ > 0, ‚àÉ y ‚àà s, ‚à•x - y‚à• < Œµ :=
by simp [metric.mem_closure_iff, dist_eq_norm]

lemma norm_le_zero_iff' [separated_space E] {g : E} :
  ‚à•g‚à• ‚â§ 0 ‚Üî g = 0 :=
begin
  letI : normed_group E := { to_metric_space := of_t2_pseudo_metric_space ‚Äπ_‚Ä∫,
    .. ‚Äπsemi_normed_group E‚Ä∫ },
  rw [‚Üê dist_zero_right], exact dist_le_zero
end

lemma norm_eq_zero_iff' [separated_space E] {g : E} : ‚à•g‚à• = 0 ‚Üî g = 0 :=
(norm_nonneg g).le_iff_eq.symm.trans norm_le_zero_iff'

lemma norm_pos_iff' [separated_space E] {g : E} : 0 < ‚à•g‚à• ‚Üî g ‚â† 0 :=
by rw [‚Üê not_le, norm_le_zero_iff']

lemma cauchy_seq_sum_of_eventually_eq {u v : ‚Ñï ‚Üí E} {N : ‚Ñï} (huv : ‚àÄ n ‚â• N, u n = v n)
  (hv : cauchy_seq (Œª n, ‚àë k in range (n+1), v k)) : cauchy_seq (Œª n, ‚àë k in range (n + 1), u k) :=
begin
  let d : ‚Ñï ‚Üí E := Œª n, ‚àë k in range (n + 1), (u k - v k),
  rw show (Œª n, ‚àë k in range (n + 1), u k) = d + (Œª n, ‚àë k in range (n + 1), v k),
    by { ext n, simp [d] },
  have : ‚àÄ n ‚â• N, d n = d N,
  { intros n hn,
    dsimp [d],
    rw eventually_constant_sum _ hn,
    intros m hm,
    simp [huv m hm] },
  exact (tendsto_at_top_of_eventually_const this).cauchy_seq.add hv
end

end semi_normed_group

section normed_group

/-- Construct a normed group from a translation invariant distance -/
def normed_group.of_add_dist [has_norm E] [add_comm_group E] [metric_space E]
  (H1 : ‚àÄ x : E, ‚à•x‚à• = dist x 0)
  (H2 : ‚àÄ x y z : E, dist x y ‚â§ dist (x + z) (y + z)) : normed_group E :=
{ dist_eq := Œª x y, begin
    rw H1, apply le_antisymm,
    { rw [sub_eq_add_neg, ‚Üê add_right_neg y], apply H2 },
    { have := H2 (x-y) 0 y, rwa [sub_add_cancel, zero_add] at this }
  end }

/-- A normed group can be built from a norm that satisfies algebraic properties. This is
formalised in this structure. -/
structure normed_group.core (E : Type*) [add_comm_group E] [has_norm E] : Prop :=
(norm_eq_zero_iff : ‚àÄ x : E, ‚à•x‚à• = 0 ‚Üî x = 0)
(triangle : ‚àÄ x y : E, ‚à•x + y‚à• ‚â§ ‚à•x‚à• + ‚à•y‚à•)
(norm_neg : ‚àÄ x : E, ‚à•-x‚à• = ‚à•x‚à•)

/-- The `semi_normed_group.core` induced by a `normed_group.core`. -/
lemma normed_group.core.to_semi_normed_group.core {E : Type*} [add_comm_group E] [has_norm E]
  (C : normed_group.core E) : semi_normed_group.core E :=
{ norm_zero := (C.norm_eq_zero_iff 0).2 rfl,
  triangle := C.triangle,
  norm_neg := C.norm_neg }

/-- Constructing a normed group from core properties of a norm, i.e., registering the distance and
the metric space structure from the norm properties. -/
def normed_group.of_core (E : Type*) [add_comm_group E] [has_norm E]
  (C : normed_group.core E) : normed_group E :=
{ eq_of_dist_eq_zero := Œª x y h,
  begin
    rw [dist_eq_norm] at h,
    exact sub_eq_zero.mp ((C.norm_eq_zero_iff _).1 h)
  end
  ..semi_normed_group.of_core E (normed_group.core.to_semi_normed_group.core C) }

variables [normed_group E] [normed_group F]

@[simp] lemma norm_eq_zero {g : E} : ‚à•g‚à• = 0 ‚Üî g = 0 := norm_eq_zero_iff'

@[simp] lemma norm_pos_iff {g : E} : 0 < ‚à• g ‚à• ‚Üî g ‚â† 0 := norm_pos_iff'

@[simp] lemma norm_le_zero_iff {g : E} : ‚à•g‚à• ‚â§ 0 ‚Üî g = 0 := norm_le_zero_iff'

lemma norm_sub_eq_zero_iff {u v : E} : ‚à•u - v‚à• = 0 ‚Üî u = v :=
by rw [norm_eq_zero, sub_eq_zero]

lemma eq_of_norm_sub_le_zero {g h : E} (a : ‚à•g - h‚à• ‚â§ 0) : g = h :=
by rwa [‚Üê sub_eq_zero, ‚Üê norm_le_zero_iff]

lemma eq_of_norm_sub_eq_zero {u v : E} (h : ‚à•u - v‚à• = 0) : u = v :=
norm_sub_eq_zero_iff.1 h

@[simp] lemma nnnorm_eq_zero {a : E} : ‚à•a‚à•‚Çä = 0 ‚Üî a = 0 :=
by rw [‚Üê nnreal.coe_eq_zero, coe_nnnorm, norm_eq_zero]

/-- An injective group homomorphism from an `add_comm_group` to a `normed_group` induces a
`normed_group` structure on the domain.

See note [reducible non-instances]. -/
@[reducible]
def normed_group.induced {E} [add_comm_group E]
  (f : E ‚Üí+ F) (h : function.injective f) : normed_group E :=
{ .. semi_normed_group.induced f,
  .. metric_space.induced f h normed_group.to_metric_space, }

/-- A subgroup of a normed group is also a normed group, with the restriction of the norm. -/
instance add_subgroup.normed_group (s : add_subgroup E) : normed_group s :=
normed_group.induced s.subtype subtype.coe_injective

/-- A submodule of a normed group is also a normed group, with the restriction of the norm.

See note [implicit instance arguments]. -/
instance submodule.normed_group {ùïú : Type*} {_ : ring ùïú}
  {E : Type*} [normed_group E] {_ : module ùïú E} (s : submodule ùïú E) : normed_group s :=
{ ..submodule.semi_normed_group s }

/-- normed group instance on the product of two normed groups, using the sup norm. -/
noncomputable instance prod.normed_group : normed_group (E √ó F) := { ..prod.semi_normed_group }

lemma prod.norm_def (x : E √ó F) : ‚à•x‚à• = (max ‚à•x.1‚à• ‚à•x.2‚à•) := rfl

lemma prod.nnnorm_def (x : E √ó F) : ‚à•x‚à•‚Çä = max (‚à•x.1‚à•‚Çä) (‚à•x.2‚à•‚Çä) :=
by { have := x.norm_def, simp only [‚Üê coe_nnnorm] at this, exact_mod_cast this }

lemma norm_fst_le (x : E √ó F) : ‚à•x.1‚à• ‚â§ ‚à•x‚à• :=
le_max_left _ _

lemma norm_snd_le (x : E √ó F) : ‚à•x.2‚à• ‚â§ ‚à•x‚à• :=
le_max_right _ _

lemma norm_prod_le_iff {x : E √ó F} {r : ‚Ñù} :
  ‚à•x‚à• ‚â§ r ‚Üî ‚à•x.1‚à• ‚â§ r ‚àß ‚à•x.2‚à• ‚â§ r :=
max_le_iff

/-- normed group instance on the product of finitely many normed groups, using the sup norm. -/
noncomputable instance pi.normed_group {œÄ : Œπ ‚Üí Type*} [fintype Œπ] [‚àÄi, normed_group (œÄ i)] :
  normed_group (Œ†i, œÄ i) := { ..pi.semi_normed_group }

/-- The norm of an element in a product space is `‚â§ r` if and only if the norm of each
component is. -/
lemma pi_norm_le_iff {œÄ : Œπ ‚Üí Type*} [fintype Œπ] [‚àÄi, normed_group (œÄ i)] {r : ‚Ñù} (hr : 0 ‚â§ r)
  {x : Œ†i, œÄ i} : ‚à•x‚à• ‚â§ r ‚Üî ‚àÄi, ‚à•x i‚à• ‚â§ r :=
by simp only [‚Üê dist_zero_right, dist_pi_le_iff hr, pi.zero_apply]

/-- The norm of an element in a product space is `< r` if and only if the norm of each
component is. -/
lemma pi_norm_lt_iff {œÄ : Œπ ‚Üí Type*} [fintype Œπ] [‚àÄi, normed_group (œÄ i)] {r : ‚Ñù} (hr : 0 < r)
  {x : Œ†i, œÄ i} : ‚à•x‚à• < r ‚Üî ‚àÄi, ‚à•x i‚à• < r :=
by simp only [‚Üê dist_zero_right, dist_pi_lt_iff hr, pi.zero_apply]

lemma norm_le_pi_norm {œÄ : Œπ ‚Üí Type*} [fintype Œπ] [‚àÄi, normed_group (œÄ i)] (x : Œ†i, œÄ i) (i : Œπ) :
  ‚à•x i‚à• ‚â§ ‚à•x‚à• :=
(pi_norm_le_iff (norm_nonneg x)).1 (le_refl _) i

@[simp] lemma pi_norm_const [nonempty Œπ] [fintype Œπ] (a : E) : ‚à•(Œª i : Œπ, a)‚à• = ‚à•a‚à• :=
by simpa only [‚Üê dist_zero_right] using dist_pi_const a 0

@[simp] lemma pi_nnnorm_const [nonempty Œπ] [fintype Œπ] (a : E) :
  ‚à•(Œª i : Œπ, a)‚à•‚Çä = ‚à•a‚à•‚Çä :=
nnreal.eq $ pi_norm_const a

lemma tendsto_norm_nhds_within_zero : tendsto (norm : E ‚Üí ‚Ñù) (ùìù[‚â†] 0) (ùìù[>] 0) :=
(continuous_norm.tendsto' (0 : E) 0 norm_zero).inf $ tendsto_principal_principal.2 $
  Œª x, norm_pos_iff.2

end normed_group
