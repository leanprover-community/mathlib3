/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Heather Macbeth, Johannes HÃ¶lzl, Yury Kudryashov
-/
import algebra.big_operators.intervals
import analysis.normed.group.basic
import topology.instances.nnreal

/-!
# Infinite sums in (semi)normed groups

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In a complete (semi)normed group,

- `summable_iff_vanishing_norm`: a series `âˆ‘' i, f i` is summable if and only if for any `Îµ > 0`,
  there exists a finite set `s` such that the sum `âˆ‘ i in t, f i` over any finite set `t` disjoint
  with `s` has norm less than `Îµ`;

- `summable_of_norm_bounded`, `summable_of_norm_bounded_eventually`: if `â€–f iâ€–` is bounded above by
  a summable series `âˆ‘' i, g i`, then `âˆ‘' i, f i` is summable as well; the same is true if the
  inequality hold only off some finite set.

- `tsum_of_norm_bounded`, `has_sum.norm_le_of_bounded`: if `â€–f iâ€– â‰¤ g i`, where `âˆ‘' i, g i` is a
  summable series, then `â€–âˆ‘' i, f iâ€– â‰¤ âˆ‘' i, g i`.

## Tags

infinite series, absolute convergence, normed group
-/

open_locale classical big_operators topology nnreal
open finset filter metric

variables {Î¹ Î± E F : Type*} [seminormed_add_comm_group E] [seminormed_add_comm_group F]

lemma cauchy_seq_finset_iff_vanishing_norm {f : Î¹ â†’ E} :
  cauchy_seq (Î» s : finset Î¹, âˆ‘ i in s, f i) â†”
    âˆ€Îµ > (0 : â„), âˆƒs:finset Î¹, âˆ€t, disjoint t s â†’ â€– âˆ‘ i in t, f i â€– < Îµ :=
begin
  rw [cauchy_seq_finset_iff_vanishing, nhds_basis_ball.forall_iff],
  { simp only [ball_zero_eq, set.mem_set_of_eq] },
  { rintros s t hst âŸ¨s', hs'âŸ©,
    exact âŸ¨s', Î» t' ht', hst $ hs' _ ht'âŸ© }
end

lemma summable_iff_vanishing_norm [complete_space E] {f : Î¹ â†’ E} :
  summable f â†” âˆ€Îµ > (0 : â„), âˆƒs:finset Î¹, âˆ€t, disjoint t s â†’ â€– âˆ‘ i in t, f i â€– < Îµ :=
by rw [summable_iff_cauchy_seq_finset, cauchy_seq_finset_iff_vanishing_norm]

lemma cauchy_seq_finset_of_norm_bounded_eventually {f : Î¹ â†’ E} {g : Î¹ â†’ â„} (hg : summable g)
  (h : âˆ€á¶  i in cofinite, â€–f iâ€– â‰¤ g i) : cauchy_seq (Î» s, âˆ‘ i in s, f i) :=
begin
  refine cauchy_seq_finset_iff_vanishing_norm.2 (Î» Îµ hÎµ, _),
  rcases summable_iff_vanishing_norm.1 hg Îµ hÎµ with âŸ¨s, hsâŸ©,
  refine âŸ¨s âˆª h.to_finset, Î» t ht, _âŸ©,
  have : âˆ€ i âˆˆ t, â€–f iâ€– â‰¤ g i,
  { intros i hi,
    simp only [disjoint_left, mem_union, not_or_distrib, h.mem_to_finset, set.mem_compl_iff,
      not_not] at ht,
    exact (ht hi).2 },
  calc â€–âˆ‘ i in t, f iâ€– â‰¤ âˆ‘ i in t, g i    : norm_sum_le_of_le _ this
                    ... â‰¤ â€–âˆ‘ i in t, g iâ€– : le_abs_self _
                    ... < Îµ               : hs _ (ht.mono_right le_sup_left),
end

lemma cauchy_seq_finset_of_norm_bounded {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : summable g)
  (h : âˆ€i, â€–f iâ€– â‰¤ g i) : cauchy_seq (Î» s : finset Î¹, âˆ‘ i in s, f i) :=
cauchy_seq_finset_of_norm_bounded_eventually hg $ eventually_of_forall h

/-- A version of the **direct comparison test** for conditionally convergent series.
See `cauchy_seq_finset_of_norm_bounded` for the same statement about absolutely convergent ones. -/
lemma cauchy_seq_range_of_norm_bounded {f : â„• â†’ E} (g : â„• â†’ â„)
  (hg : cauchy_seq (Î» n, âˆ‘ i in range n, g i)) (hf : âˆ€ i, â€–f iâ€– â‰¤ g i) :
  cauchy_seq (Î» n, âˆ‘ i in range n, f i) :=
begin
  refine metric.cauchy_seq_iff'.2 (Î» Îµ hÎµ, _),
  refine (metric.cauchy_seq_iff'.1 hg Îµ hÎµ).imp (Î» N hg n hn, _),
  specialize hg n hn,
  rw [dist_eq_norm, â†sum_Ico_eq_sub _ hn] at âŠ¢ hg,
  calc  â€–âˆ‘ k in Ico N n, f kâ€–
      â‰¤  âˆ‘ k in _, â€–f kâ€– : norm_sum_le _ _
  ... â‰¤  âˆ‘ k in _, g k   : sum_le_sum (Î» x _, hf x)
  ... â‰¤ â€–âˆ‘ k in _, g kâ€–  : le_abs_self _
  ... <  Îµ               : hg
end

lemma cauchy_seq_finset_of_summable_norm {f : Î¹ â†’ E} (hf : summable (Î»a, â€–f aâ€–)) :
  cauchy_seq (Î» s : finset Î¹, âˆ‘ a in s, f a) :=
cauchy_seq_finset_of_norm_bounded _ hf (assume i, le_rfl)

/-- If a function `f` is summable in norm, and along some sequence of finsets exhausting the space
its sum is converging to a limit `a`, then this holds along all finsets, i.e., `f` is summable
with sum `a`. -/
lemma has_sum_of_subseq_of_summable {f : Î¹ â†’ E} (hf : summable (Î»a, â€–f aâ€–))
  {s : Î± â†’ finset Î¹} {p : filter Î±} [ne_bot p]
  (hs : tendsto s p at_top) {a : E} (ha : tendsto (Î» b, âˆ‘ i in s b, f i) p (ğ“ a)) :
  has_sum f a :=
tendsto_nhds_of_cauchy_seq_of_subseq (cauchy_seq_finset_of_summable_norm hf) hs ha

lemma has_sum_iff_tendsto_nat_of_summable_norm {f : â„• â†’ E} {a : E} (hf : summable (Î»i, â€–f iâ€–)) :
  has_sum f a â†” tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
âŸ¨Î» h, h.tendsto_sum_nat,
Î» h, has_sum_of_subseq_of_summable hf tendsto_finset_range hâŸ©

/-- The direct comparison test for series:  if the norm of `f` is bounded by a real function `g`
which is summable, then `f` is summable. -/
lemma summable_of_norm_bounded
  [complete_space E] {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : summable g) (h : âˆ€i, â€–f iâ€– â‰¤ g i) :
  summable f :=
by { rw summable_iff_cauchy_seq_finset, exact cauchy_seq_finset_of_norm_bounded g hg h }

lemma has_sum.norm_le_of_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„} {a : E} {b : â„}
  (hf : has_sum f a) (hg : has_sum g b) (h : âˆ€ i, â€–f iâ€– â‰¤ g i) :
  â€–aâ€– â‰¤ b :=
le_of_tendsto_of_tendsto' hf.norm hg $ Î» s, norm_sum_le_of_le _ $ Î» i hi, h i

/-- Quantitative result associated to the direct comparison test for series:  If `âˆ‘' i, g i` is
summable, and for all `i`, `â€–f iâ€– â‰¤ g i`, then `â€–âˆ‘' i, f iâ€– â‰¤ âˆ‘' i, g i`. Note that we do not
assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete space. -/
lemma tsum_of_norm_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„} {a : â„} (hg : has_sum g a)
  (h : âˆ€ i, â€–f iâ€– â‰¤ g i) :
  â€–âˆ‘' i : Î¹, f iâ€– â‰¤ a :=
begin
  by_cases hf : summable f,
  { exact hf.has_sum.norm_le_of_bounded hg h },
  { rw [tsum_eq_zero_of_not_summable hf, norm_zero],
    exact ge_of_tendsto' hg (Î» s, sum_nonneg $ Î» i hi, (norm_nonneg _).trans (h i)) }
end

/-- If `âˆ‘' i, â€–f iâ€–` is summable, then `â€–âˆ‘' i, f iâ€– â‰¤ (âˆ‘' i, â€–f iâ€–)`. Note that we do not assume
that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete space. -/
lemma norm_tsum_le_tsum_norm {f : Î¹ â†’ E} (hf : summable (Î»i, â€–f iâ€–)) :
  â€–âˆ‘' i, f iâ€– â‰¤ âˆ‘' i, â€–f iâ€– :=
tsum_of_norm_bounded hf.has_sum $ Î» i, le_rfl

/-- Quantitative result associated to the direct comparison test for series: If `âˆ‘' i, g i` is
summable, and for all `i`, `â€–f iâ€–â‚Š â‰¤ g i`, then `â€–âˆ‘' i, f iâ€–â‚Š â‰¤ âˆ‘' i, g i`. Note that we
do not assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete
space. -/
lemma tsum_of_nnnorm_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„â‰¥0} {a : â„â‰¥0} (hg : has_sum g a)
  (h : âˆ€ i, â€–f iâ€–â‚Š â‰¤ g i) :
  â€–âˆ‘' i : Î¹, f iâ€–â‚Š â‰¤ a :=
begin
  simp only [â† nnreal.coe_le_coe, â† nnreal.has_sum_coe, coe_nnnorm] at *,
  exact tsum_of_norm_bounded hg h
end

/-- If `âˆ‘' i, â€–f iâ€–â‚Š` is summable, then `â€–âˆ‘' i, f iâ€–â‚Š â‰¤ âˆ‘' i, â€–f iâ€–â‚Š`. Note that
we do not assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete
space. -/
lemma nnnorm_tsum_le {f : Î¹ â†’ E} (hf : summable (Î»i, â€–f iâ€–â‚Š)) :
  â€–âˆ‘' i, f iâ€–â‚Š â‰¤ âˆ‘' i, â€–f iâ€–â‚Š :=
tsum_of_nnnorm_bounded hf.has_sum (Î» i, le_rfl)

variable [complete_space E]

/-- Variant of the direct comparison test for series:  if the norm of `f` is eventually bounded by a
real function `g` which is summable, then `f` is summable. -/
lemma summable_of_norm_bounded_eventually {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : summable g)
  (h : âˆ€á¶  i in cofinite, â€–f iâ€– â‰¤ g i) : summable f :=
summable_iff_cauchy_seq_finset.2 $ cauchy_seq_finset_of_norm_bounded_eventually hg h

lemma summable_of_nnnorm_bounded {f : Î¹ â†’ E} (g : Î¹ â†’ â„â‰¥0) (hg : summable g)
  (h : âˆ€i, â€–f iâ€–â‚Š â‰¤ g i) : summable f :=
summable_of_norm_bounded (Î» i, (g i : â„)) (nnreal.summable_coe.2 hg) (Î» i, by exact_mod_cast h i)

lemma summable_of_summable_norm {f : Î¹ â†’ E} (hf : summable (Î»a, â€–f aâ€–)) : summable f :=
summable_of_norm_bounded _ hf (assume i, le_rfl)

lemma summable_of_summable_nnnorm {f : Î¹ â†’ E} (hf : summable (Î» a, â€–f aâ€–â‚Š)) : summable f :=
summable_of_nnnorm_bounded _ hf (assume i, le_rfl)
