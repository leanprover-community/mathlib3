/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Heather Macbeth, Johannes HÃ¶lzl, Yury Kudryashov
-/
import analysis.normed.group.basic
import topology.instances.nnreal

/-!
# Infinite sums in (semi)normed groups

In a complete (semi)normed group,

- `summable_iff_vanishing_norm`: a series `âˆ‘' i, f i` is summable if and only if for any `Îµ > 0`,
  there exists a finite set `s` such that the sum `âˆ‘ i in t, f i` over any finite set `t` disjoint
  with `s` has norm less than `Îµ`;

- `summable_of_norm_bounded`, `summable_of_norm_bounded_eventually`: if `âˆ¥f iâˆ¥` is bounded above by
  a summable series `âˆ‘' i, g i`, then `âˆ‘' i, f i` is summable as well; the same is true if the
  inequality hold only off some finite set.

- `tsum_of_norm_bounded`, `has_sum.norm_le_of_bounded`: if `âˆ¥f iâˆ¥ â‰¤ g i`, where `âˆ‘' i, g i` is a
  summable series, then `âˆ¥âˆ‘' i, f iâˆ¥ â‰¤ âˆ‘' i, g i`.

## Tags

infinite series, absolute convergence, normed group
-/

open_locale classical big_operators topological_space nnreal
open finset filter metric

variables {Î¹ Î± E F : Type*} [semi_normed_group E] [semi_normed_group F]

lemma cauchy_seq_finset_iff_vanishing_norm {f : Î¹ â†’ E} :
  cauchy_seq (Î» s : finset Î¹, âˆ‘ i in s, f i) â†”
    âˆ€Îµ > (0 : â„), âˆƒs:finset Î¹, âˆ€t, disjoint t s â†’ âˆ¥ âˆ‘ i in t, f i âˆ¥ < Îµ :=
begin
  rw [cauchy_seq_finset_iff_vanishing, nhds_basis_ball.forall_iff],
  { simp only [ball_zero_eq, set.mem_set_of_eq] },
  { rintros s t hst âŸ¨s', hs'âŸ©,
    exact âŸ¨s', Î» t' ht', hst $ hs' _ ht'âŸ© }
end

lemma summable_iff_vanishing_norm [complete_space E] {f : Î¹ â†’ E} :
  summable f â†” âˆ€Îµ > (0 : â„), âˆƒs:finset Î¹, âˆ€t, disjoint t s â†’ âˆ¥ âˆ‘ i in t, f i âˆ¥ < Îµ :=
by rw [summable_iff_cauchy_seq_finset, cauchy_seq_finset_iff_vanishing_norm]

lemma cauchy_seq_finset_of_norm_bounded_eventually {f : Î¹ â†’ E} {g : Î¹ â†’ â„} (hg : summable g)
  (h : âˆ€á¶  i in cofinite, âˆ¥f iâˆ¥ â‰¤ g i) : cauchy_seq (Î» s, âˆ‘ i in s, f i) :=
begin
  refine cauchy_seq_finset_iff_vanishing_norm.2 (Î» Îµ hÎµ, _),
  rcases summable_iff_vanishing_norm.1 hg Îµ hÎµ with âŸ¨s, hsâŸ©,
  refine âŸ¨s âˆª h.to_finset, Î» t ht, _âŸ©,
  have : âˆ€ i âˆˆ t, âˆ¥f iâˆ¥ â‰¤ g i,
  { intros i hi,
    simp only [disjoint_left, mem_union, not_or_distrib, h.mem_to_finset, set.mem_compl_iff,
      not_not] at ht,
    exact (ht hi).2 },
  calc âˆ¥âˆ‘ i in t, f iâˆ¥ â‰¤ âˆ‘ i in t, g i    : norm_sum_le_of_le _ this
                    ... â‰¤ âˆ¥âˆ‘ i in t, g iâˆ¥ : le_abs_self _
                    ... < Îµ               : hs _ (ht.mono_right le_sup_left),
end

lemma cauchy_seq_finset_of_norm_bounded {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : summable g)
  (h : âˆ€i, âˆ¥f iâˆ¥ â‰¤ g i) : cauchy_seq (Î» s : finset Î¹, âˆ‘ i in s, f i) :=
cauchy_seq_finset_of_norm_bounded_eventually hg $ eventually_of_forall h

lemma cauchy_seq_finset_of_summable_norm {f : Î¹ â†’ E} (hf : summable (Î»a, âˆ¥f aâˆ¥)) :
  cauchy_seq (Î» s : finset Î¹, âˆ‘ a in s, f a) :=
cauchy_seq_finset_of_norm_bounded _ hf (assume i, le_refl _)

/-- If a function `f` is summable in norm, and along some sequence of finsets exhausting the space
its sum is converging to a limit `a`, then this holds along all finsets, i.e., `f` is summable
with sum `a`. -/
lemma has_sum_of_subseq_of_summable {f : Î¹ â†’ E} (hf : summable (Î»a, âˆ¥f aâˆ¥))
  {s : Î± â†’ finset Î¹} {p : filter Î±} [ne_bot p]
  (hs : tendsto s p at_top) {a : E} (ha : tendsto (Î» b, âˆ‘ i in s b, f i) p (ğ“ a)) :
  has_sum f a :=
tendsto_nhds_of_cauchy_seq_of_subseq (cauchy_seq_finset_of_summable_norm hf) hs ha

lemma has_sum_iff_tendsto_nat_of_summable_norm {f : â„• â†’ E} {a : E} (hf : summable (Î»i, âˆ¥f iâˆ¥)) :
  has_sum f a â†” tendsto (Î»n:â„•, âˆ‘ i in range n, f i) at_top (ğ“ a) :=
âŸ¨Î» h, h.tendsto_sum_nat,
Î» h, has_sum_of_subseq_of_summable hf tendsto_finset_range hâŸ©

/-- The direct comparison test for series:  if the norm of `f` is bounded by a real function `g`
which is summable, then `f` is summable. -/
lemma summable_of_norm_bounded
  [complete_space E] {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : summable g) (h : âˆ€i, âˆ¥f iâˆ¥ â‰¤ g i) :
  summable f :=
by { rw summable_iff_cauchy_seq_finset, exact cauchy_seq_finset_of_norm_bounded g hg h }

lemma has_sum.norm_le_of_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„} {a : E} {b : â„}
  (hf : has_sum f a) (hg : has_sum g b) (h : âˆ€ i, âˆ¥f iâˆ¥ â‰¤ g i) :
  âˆ¥aâˆ¥ â‰¤ b :=
le_of_tendsto_of_tendsto' hf.norm hg $ Î» s, norm_sum_le_of_le _ $ Î» i hi, h i

/-- Quantitative result associated to the direct comparison test for series:  If `âˆ‘' i, g i` is
summable, and for all `i`, `âˆ¥f iâˆ¥ â‰¤ g i`, then `âˆ¥âˆ‘' i, f iâˆ¥ â‰¤ âˆ‘' i, g i`. Note that we do not
assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete space. -/
lemma tsum_of_norm_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„} {a : â„} (hg : has_sum g a)
  (h : âˆ€ i, âˆ¥f iâˆ¥ â‰¤ g i) :
  âˆ¥âˆ‘' i : Î¹, f iâˆ¥ â‰¤ a :=
begin
  by_cases hf : summable f,
  { exact hf.has_sum.norm_le_of_bounded hg h },
  { rw [tsum_eq_zero_of_not_summable hf, norm_zero],
    exact ge_of_tendsto' hg (Î» s, sum_nonneg $ Î» i hi, (norm_nonneg _).trans (h i)) }
end

/-- If `âˆ‘' i, âˆ¥f iâˆ¥` is summable, then `âˆ¥âˆ‘' i, f iâˆ¥ â‰¤ (âˆ‘' i, âˆ¥f iâˆ¥)`. Note that we do not assume
that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete space. -/
lemma norm_tsum_le_tsum_norm {f : Î¹ â†’ E} (hf : summable (Î»i, âˆ¥f iâˆ¥)) :
  âˆ¥âˆ‘' i, f iâˆ¥ â‰¤ âˆ‘' i, âˆ¥f iâˆ¥ :=
tsum_of_norm_bounded hf.has_sum $ Î» i, le_rfl

/-- Quantitative result associated to the direct comparison test for series: If `âˆ‘' i, g i` is
summable, and for all `i`, `âˆ¥f iâˆ¥â‚Š â‰¤ g i`, then `âˆ¥âˆ‘' i, f iâˆ¥â‚Š â‰¤ âˆ‘' i, g i`. Note that we
do not assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete
space. -/
lemma tsum_of_nnnorm_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„â‰¥0} {a : â„â‰¥0} (hg : has_sum g a)
  (h : âˆ€ i, âˆ¥f iâˆ¥â‚Š â‰¤ g i) :
  âˆ¥âˆ‘' i : Î¹, f iâˆ¥â‚Š â‰¤ a :=
begin
  simp only [â† nnreal.coe_le_coe, â† nnreal.has_sum_coe, coe_nnnorm] at *,
  exact tsum_of_norm_bounded hg h
end

/-- If `âˆ‘' i, âˆ¥f iâˆ¥â‚Š` is summable, then `âˆ¥âˆ‘' i, f iâˆ¥â‚Š â‰¤ âˆ‘' i, âˆ¥f iâˆ¥â‚Š`. Note that
we do not assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete
space. -/
lemma nnnorm_tsum_le {f : Î¹ â†’ E} (hf : summable (Î»i, âˆ¥f iâˆ¥â‚Š)) :
  âˆ¥âˆ‘' i, f iâˆ¥â‚Š â‰¤ âˆ‘' i, âˆ¥f iâˆ¥â‚Š :=
tsum_of_nnnorm_bounded hf.has_sum (Î» i, le_rfl)

variable [complete_space E]

/-- Variant of the direct comparison test for series:  if the norm of `f` is eventually bounded by a
real function `g` which is summable, then `f` is summable. -/
lemma summable_of_norm_bounded_eventually {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : summable g)
  (h : âˆ€á¶  i in cofinite, âˆ¥f iâˆ¥ â‰¤ g i) : summable f :=
summable_iff_cauchy_seq_finset.2 $ cauchy_seq_finset_of_norm_bounded_eventually hg h

lemma summable_of_nnnorm_bounded {f : Î¹ â†’ E} (g : Î¹ â†’ â„â‰¥0) (hg : summable g)
  (h : âˆ€i, âˆ¥f iâˆ¥â‚Š â‰¤ g i) : summable f :=
summable_of_norm_bounded (Î» i, (g i : â„)) (nnreal.summable_coe.2 hg) (Î» i, by exact_mod_cast h i)

lemma summable_of_summable_norm {f : Î¹ â†’ E} (hf : summable (Î»a, âˆ¥f aâˆ¥)) : summable f :=
summable_of_norm_bounded _ hf (assume i, le_refl _)

lemma summable_of_summable_nnnorm {f : Î¹ â†’ E} (hf : summable (Î» a, âˆ¥f aâˆ¥â‚Š)) : summable f :=
summable_of_nnnorm_bounded _ hf (assume i, le_refl _)
