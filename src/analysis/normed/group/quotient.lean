/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Riccardo Brasca
-/
import analysis.normed_space.basic
import analysis.normed.group.hom
import ring_theory.ideal.quotient_operations

/-!
# Quotients of seminormed groups

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

For any `seminormed_add_comm_group M` and any `S : add_subgroup M`, we provide a
`seminormed_add_comm_group`, the group quotient `M ‚ß∏ S`.
If `S` is closed, we provide `normed_add_comm_group (M ‚ß∏ S)` (regardless of whether `M` itself is
separated). The two main properties of these structures are the underlying topology is the quotient
topology and the projection is a normed group homomorphism which is norm non-increasing
(better, it has operator norm exactly one unless `S` is dense in `M`). The corresponding
universal property is that every normed group hom defined on `M` which vanishes on `S` descends
to a normed group hom defined on `M ‚ß∏ S`.

This file also introduces a predicate `is_quotient` characterizing normed group homs that
are isomorphic to the canonical projection onto a normed group quotient.

In addition, this file also provides normed structures for quotients of modules by submodules, and
of (commutative) rings by ideals. The `seminormed_add_comm_group` and `normed_add_comm_group`
instances described above are transferred directly, but we also define instances of `normed_space`,
`semi_normed_comm_ring`, `normed_comm_ring` and `normed_algebra` under appropriate type class
assumptions on the original space. Moreover, while `quotient_add_group.complete_space` works
out-of-the-box for quotients of `normed_add_comm_group`s by `add_subgroup`s, we need to transfer
this instance in `submodule.quotient.complete_space` so that it applies to these other quotients.

## Main definitions


We use `M` and `N` to denote seminormed groups and `S : add_subgroup M`.
All the following definitions are in the `add_subgroup` namespace. Hence we can access
`add_subgroup.normed_mk S` as `S.normed_mk`.

* `seminormed_add_comm_group_quotient` : The seminormed group structure on the quotient by
    an additive subgroup. This is an instance so there is no need to explictly use it.

* `normed_add_comm_group_quotient` : The normed group structure on the quotient by
    a closed additive subgroup. This is an instance so there is no need to explictly use it.

* `normed_mk S` : the normed group hom from `M` to `M ‚ß∏ S`.

* `lift S f hf`: implements the universal property of `M ‚ß∏ S`. Here
    `(f : normed_add_group_hom M N)`, `(hf : ‚àÄ s ‚àà S, f s = 0)` and
    `lift S f hf : normed_add_group_hom (M ‚ß∏ S) N`.

* `is_quotient`: given `f : normed_add_group_hom M N`, `is_quotient f` means `N` is isomorphic
    to a quotient of `M` by a subgroup, with projection `f`. Technically it asserts `f` is
    surjective and the norm of `f x` is the infimum of the norms of `x + m` for `m` in `f.ker`.

## Main results

* `norm_normed_mk` : the operator norm of the projection is `1` if the subspace is not dense.

* `is_quotient.norm_lift`: Provided `f : normed_hom M N` satisfies `is_quotient f`, for every
     `n : N` and positive `Œµ`, there exists `m` such that `f m = n ‚àß ‚Äñm‚Äñ < ‚Äñn‚Äñ + Œµ`.


## Implementation details

For any `seminormed_add_comm_group M` and any `S : add_subgroup M` we define a norm on `M ‚ß∏ S` by
`‚Äñx‚Äñ = Inf (norm '' {m | mk' S m = x})`. This formula is really an implementation detail, it
shouldn't be needed outside of this file setting up the theory.

Since `M ‚ß∏ S` is automatically a topological space (as any quotient of a topological space),
one needs to be careful while defining the `seminormed_add_comm_group` instance to avoid having two
different topologies on this quotient. This is not purely a technological issue.
Mathematically there is something to prove. The main point is proved in the auxiliary lemma
`quotient_nhd_basis` that has no use beyond this verification and states that zero in the quotient
admits as basis of neighborhoods in the quotient topology the sets `{x | ‚Äñx‚Äñ < Œµ}` for positive `Œµ`.

Once this mathematical point it settled, we have two topologies that are propositionaly equal. This
is not good enough for the type class system. As usual we ensure *definitional* equality
using forgetful inheritance, see Note [forgetful inheritance]. A (semi)-normed group structure
includes a uniform space structure which includes a topological space structure, together
with propositional fields asserting compatibility conditions.
The usual way to define a `seminormed_add_comm_group` is to let Lean build a uniform space structure
using the provided norm, and then trivially build a proof that the norm and uniform structure are
compatible. Here the uniform structure is provided using `topological_add_group.to_uniform_space`
which uses the topological structure and the group structure to build the uniform structure. This
uniform structure induces the correct topological structure by construction, but the fact that it
is compatible with the norm is not obvious; this is where the mathematical content explained in
the previous paragraph kicks in.

-/

noncomputable theory


open quotient_add_group metric set
open_locale topology nnreal

variables {M N : Type*} [seminormed_add_comm_group M] [seminormed_add_comm_group N]

/-- The definition of the norm on the quotient by an additive subgroup. -/
noncomputable
instance norm_on_quotient (S : add_subgroup M) : has_norm (M ‚ß∏ S) :=
{ norm := Œª x, Inf (norm '' {m | mk' S m = x}) }

lemma add_subgroup.quotient_norm_eq {S : add_subgroup M} (x : M ‚ß∏ S) :
  ‚Äñx‚Äñ = Inf (norm '' {m : M | (m : M ‚ß∏ S) = x}) :=
rfl

lemma image_norm_nonempty {S : add_subgroup M} :
  ‚àÄ x : M ‚ß∏ S, (norm '' {m | mk' S m = x}).nonempty :=
begin
  rintro ‚ü®m‚ü©,
  rw set.nonempty_image_iff,
  use m,
  change mk' S m = _,
  refl
end

lemma bdd_below_image_norm (s : set M) : bdd_below (norm '' s) :=
begin
  use 0,
  rintro _ ‚ü®x, hx, rfl‚ü©,
  apply norm_nonneg
end

/-- The norm on the quotient satisfies `‚Äñ-x‚Äñ = ‚Äñx‚Äñ`. -/
lemma quotient_norm_neg {S : add_subgroup M} (x : M ‚ß∏ S) : ‚Äñ-x‚Äñ = ‚Äñx‚Äñ :=
begin
  suffices : norm '' {m | mk' S m = x} = norm '' {m | mk' S m = -x},
    by simp only [this, norm],
  ext r,
  split,
  { rintros ‚ü®m, rfl : mk' S m = x, rfl‚ü©,
    rw ‚Üê norm_neg,
    exact ‚ü®-m, by simp only [(mk' S).map_neg, set.mem_set_of_eq], rfl‚ü© },
  { rintros ‚ü®m, hm : mk' S m = -x, rfl‚ü©,
    exact ‚ü®-m, by simpa using neg_eq_iff_eq_neg.mpr ((mk'_apply _ _).symm.trans hm)‚ü© }
end

lemma quotient_norm_sub_rev {S : add_subgroup M} (x y : M ‚ß∏ S) : ‚Äñx - y‚Äñ = ‚Äñy - x‚Äñ :=
by rw [show x - y = -(y - x), by abel, quotient_norm_neg]

/-- The norm of the projection is smaller or equal to the norm of the original element. -/
lemma quotient_norm_mk_le (S : add_subgroup M) (m : M) :
  ‚Äñmk' S m‚Äñ ‚â§ ‚Äñm‚Äñ :=
begin
  apply cInf_le,
  use 0,
  { rintros _ ‚ü®n, h, rfl‚ü©,
    apply norm_nonneg },
  { apply set.mem_image_of_mem,
    rw set.mem_set_of_eq }
end

/-- The norm of the projection is smaller or equal to the norm of the original element. -/
lemma quotient_norm_mk_le' (S : add_subgroup M) (m : M) :
  ‚Äñ(m : M ‚ß∏ S)‚Äñ ‚â§ ‚Äñm‚Äñ := quotient_norm_mk_le S m

/-- The norm of the image under the natural morphism to the quotient. -/
lemma quotient_norm_mk_eq (S : add_subgroup M) (m : M) :
  ‚Äñmk' S m‚Äñ = Inf ((Œª x, ‚Äñm + x‚Äñ) '' S) :=
begin
  change Inf _ = _,
  congr' 1,
  ext r,
  simp_rw [coe_mk', eq_iff_sub_mem],
  split,
  { rintros ‚ü®y, h, rfl‚ü©,
    use [y - m, h],
    simp },
  { rintros ‚ü®y, h, rfl‚ü©,
    use m + y,
    simpa using h },
end

/-- The quotient norm is nonnegative. -/
lemma quotient_norm_nonneg (S : add_subgroup M) : ‚àÄ x : M ‚ß∏ S, 0 ‚â§ ‚Äñx‚Äñ :=
begin
  rintros ‚ü®m‚ü©,
  change 0 ‚â§ ‚Äñmk' S m‚Äñ,
  apply le_cInf (image_norm_nonempty _),
  rintros _ ‚ü®n, h, rfl‚ü©,
  apply norm_nonneg
end

/-- The quotient norm is nonnegative. -/
lemma norm_mk_nonneg (S : add_subgroup M) (m : M) : 0 ‚â§ ‚Äñmk' S m‚Äñ :=
quotient_norm_nonneg S _

/-- The norm of the image of `m : M` in the quotient by `S` is zero if and only if `m` belongs
to the closure of `S`. -/
lemma quotient_norm_eq_zero_iff (S : add_subgroup M) (m : M) :
  ‚Äñmk' S m‚Äñ = 0 ‚Üî m ‚àà closure (S : set M) :=
begin
  have : 0 ‚â§ ‚Äñmk' S m‚Äñ := norm_mk_nonneg S m,
  rw [‚Üê this.le_iff_eq, quotient_norm_mk_eq, real.Inf_le_iff],
  simp_rw [zero_add],
  { calc (‚àÄ Œµ > (0 : ‚Ñù), ‚àÉ r ‚àà (Œª x, ‚Äñm + x‚Äñ) '' (S : set M), r < Œµ) ‚Üî
        (‚àÄ Œµ > 0, (‚àÉ x ‚àà S, ‚Äñm + x‚Äñ < Œµ)) : by simp [set.bex_image_iff]
     ... ‚Üî ‚àÄ Œµ > 0, (‚àÉ x ‚àà S, ‚Äñm + -x‚Äñ < Œµ) : _
     ... ‚Üî ‚àÄ Œµ > 0, (‚àÉ x ‚àà S, x ‚àà metric.ball m Œµ) : by simp [dist_eq_norm, ‚Üê sub_eq_add_neg,
                                                              norm_sub_rev]
     ... ‚Üî m ‚àà closure ‚ÜëS : by simp [metric.mem_closure_iff, dist_comm],
    refine forall‚ÇÇ_congr (Œª Œµ Œµ_pos, _),
    rw [‚Üê S.exists_neg_mem_iff_exists_mem],
    simp },
  { use 0,
    rintro _ ‚ü®x, x_in, rfl‚ü©,
    apply norm_nonneg },
  rw set.nonempty_image_iff,
  use [0, S.zero_mem]
end

/-- For any `x : M ‚ß∏ S` and any `0 < Œµ`, there is `m : M` such that `mk' S m = x`
and `‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ`. -/
lemma norm_mk_lt {S : add_subgroup M} (x : M ‚ß∏ S) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
  ‚àÉ (m : M), mk' S m = x ‚àß ‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ :=
begin
  obtain ‚ü®_, ‚ü®m : M, H : mk' S m = x, rfl‚ü©, hnorm : ‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ‚ü© :=
    real.lt_Inf_add_pos (image_norm_nonempty x) hŒµ,
  subst H,
  exact ‚ü®m, rfl, hnorm‚ü©,
end

/-- For any `m : M` and any `0 < Œµ`, there is `s ‚àà S` such that `‚Äñm + s‚Äñ < ‚Äñmk' S m‚Äñ + Œµ`. -/
lemma norm_mk_lt' (S : add_subgroup M) (m : M) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
  ‚àÉ s ‚àà S, ‚Äñm + s‚Äñ < ‚Äñmk' S m‚Äñ + Œµ :=
begin
  obtain ‚ü®n : M, hn : mk' S n = mk' S m, hn' : ‚Äñn‚Äñ < ‚Äñmk' S m‚Äñ + Œµ‚ü© :=
    norm_mk_lt (quotient_add_group.mk' S m) hŒµ,
  erw [eq_comm, quotient_add_group.eq] at hn,
  use [- m + n, hn],
  rwa [add_neg_cancel_left]
end

/-- The quotient norm satisfies the triangle inequality. -/
lemma quotient_norm_add_le (S : add_subgroup M) (x y : M ‚ß∏ S) : ‚Äñx + y‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy‚Äñ :=
begin
  refine le_of_forall_pos_le_add (Œª Œµ hŒµ, _),
  replace hŒµ := half_pos hŒµ,
  obtain ‚ü®m, rfl, hm : ‚Äñm‚Äñ < ‚Äñmk' S m‚Äñ + Œµ / 2‚ü© := norm_mk_lt x hŒµ,
  obtain ‚ü®n, rfl, hn : ‚Äñn‚Äñ < ‚Äñmk' S n‚Äñ + Œµ / 2‚ü© := norm_mk_lt y hŒµ,
  calc ‚Äñmk' S m + mk' S n‚Äñ = ‚Äñmk' S (m +  n)‚Äñ : by rw (mk' S).map_add
  ... ‚â§ ‚Äñm + n‚Äñ : quotient_norm_mk_le S (m + n)
  ... ‚â§ ‚Äñm‚Äñ + ‚Äñn‚Äñ : norm_add_le _ _
  ... ‚â§ ‚Äñmk' S m‚Äñ + ‚Äñmk' S n‚Äñ + Œµ : by linarith
end

/-- The quotient norm of `0` is `0`. -/
lemma norm_mk_zero (S : add_subgroup M) : ‚Äñ(0 : M ‚ß∏ S)‚Äñ = 0 :=
begin
  erw quotient_norm_eq_zero_iff,
  exact subset_closure S.zero_mem
end

/-- If `(m : M)` has norm equal to `0` in `M ‚ß∏ S` for a closed subgroup `S` of `M`, then
`m ‚àà S`. -/
lemma norm_zero_eq_zero (S : add_subgroup M) (hS : is_closed (S : set M)) (m : M)
  (h : ‚Äñmk' S m‚Äñ = 0) : m ‚àà S :=
by rwa [quotient_norm_eq_zero_iff, hS.closure_eq] at h

lemma quotient_nhd_basis (S : add_subgroup M) :
  (ùìù (0 : M ‚ß∏ S)).has_basis (Œª Œµ : ‚Ñù, 0 < Œµ) (Œª Œµ, {x | ‚Äñx‚Äñ < Œµ}) :=
‚ü®begin
  intros U,
  split,
  { intros U_in,
    rw ‚Üê (mk' S).map_zero at U_in,
    have := preimage_nhds_coinduced U_in,
    rcases metric.mem_nhds_iff.mp this with ‚ü®Œµ, Œµ_pos, H‚ü©,
    use [Œµ/2, half_pos Œµ_pos],
    intros x x_in,
    dsimp at x_in,
    rcases norm_mk_lt x (half_pos Œµ_pos) with ‚ü®y, rfl, ry‚ü©,
    apply H,
    rw ball_zero_eq,
    dsimp,
    linarith },
  { rintros ‚ü®Œµ, Œµ_pos, h‚ü©,
    have : (mk' S) '' (ball (0 : M) Œµ) ‚äÜ {x | ‚Äñx‚Äñ < Œµ},
    { rintros _ ‚ü®x, x_in, rfl‚ü©,
      rw mem_ball_zero_iff at x_in,
      exact lt_of_le_of_lt (quotient_norm_mk_le S x) x_in },
    apply filter.mem_of_superset _ (set.subset.trans this h),
    clear h U this,
    apply is_open.mem_nhds,
    { change is_open ((mk' S) ‚Åª¬π' _),
      erw quotient_add_group.preimage_image_coe,
      apply is_open_Union,
      rintros ‚ü®s, s_in‚ü©,
      exact (continuous_add_right s).is_open_preimage _ is_open_ball },
    { exact ‚ü®(0 : M), mem_ball_self Œµ_pos, (mk' S).map_zero‚ü© } },
end‚ü©

/-- The seminormed group structure on the quotient by an additive subgroup. -/
noncomputable
instance add_subgroup.seminormed_add_comm_group_quotient (S : add_subgroup M) :
  seminormed_add_comm_group (M ‚ß∏ S) :=
{ dist               := Œª x y, ‚Äñx - y‚Äñ,
  dist_self          := Œª x, by simp only [norm_mk_zero, sub_self],
  dist_comm          := quotient_norm_sub_rev,
  dist_triangle      := Œª x y z,
  begin
    unfold dist,
    have : x - z = (x - y) + (y - z) := by abel,
    rw this,
    exact quotient_norm_add_le S (x - y) (y - z)
  end,
  dist_eq := Œª x y, rfl,
  to_uniform_space   := topological_add_group.to_uniform_space (M ‚ß∏ S),
  uniformity_dist    :=
  begin
    rw uniformity_eq_comap_nhds_zero',
    have := (quotient_nhd_basis S).comap (Œª (p : (M ‚ß∏ S) √ó M ‚ß∏ S), p.2 - p.1),
    apply this.eq_of_same_basis,
    have : ‚àÄ Œµ : ‚Ñù, (Œª (p : (M ‚ß∏ S) √ó M ‚ß∏ S), p.snd - p.fst) ‚Åª¬π' {x | ‚Äñx‚Äñ < Œµ} =
      {p : (M ‚ß∏ S) √ó M ‚ß∏ S | ‚Äñp.fst - p.snd‚Äñ < Œµ},
    { intro Œµ,
      ext x,
      dsimp,
      rw quotient_norm_sub_rev },
    rw funext this,
    refine filter.has_basis_binfi_principal _ set.nonempty_Ioi,
    rintros Œµ (Œµ_pos : 0 < Œµ) Œ∑ (Œ∑_pos : 0 < Œ∑),
    refine ‚ü®min Œµ Œ∑, lt_min Œµ_pos Œ∑_pos, _, _‚ü©,
    { suffices : ‚àÄ (a b : M ‚ß∏ S), ‚Äña - b‚Äñ < Œµ ‚Üí ‚Äña - b‚Äñ < Œ∑ ‚Üí ‚Äña - b‚Äñ < Œµ, by simpa,
      exact Œª a b h h', h },
    { simp }
  end }

-- This is a sanity check left here on purpose to ensure that potential refactors won't destroy
-- this important property.
example (S : add_subgroup M) : (quotient.topological_space : topological_space $ M ‚ß∏ S) =
S.seminormed_add_comm_group_quotient.to_uniform_space.to_topological_space :=
rfl

/-- The quotient in the category of normed groups. -/
noncomputable
instance add_subgroup.normed_add_comm_group_quotient (S : add_subgroup M) [is_closed (S : set M)] :
  normed_add_comm_group (M ‚ß∏ S) :=
{ eq_of_dist_eq_zero :=
  begin
    rintros ‚ü®m‚ü© ‚ü®m'‚ü© (h : ‚Äñmk' S m - mk' S m'‚Äñ = 0),
    erw [‚Üê (mk' S).map_sub, quotient_norm_eq_zero_iff, ‚Äπis_closed _‚Ä∫.closure_eq,
         ‚Üê quotient_add_group.eq_iff_sub_mem] at h,
    exact h
  end,
  .. add_subgroup.seminormed_add_comm_group_quotient S }

-- This is a sanity check left here on purpose to ensure that potential refactors won't destroy
-- this important property.
example (S : add_subgroup M) [is_closed (S : set M)] :
  S.seminormed_add_comm_group_quotient = normed_add_comm_group.to_seminormed_add_comm_group := rfl


namespace add_subgroup

open normed_add_group_hom

/-- The morphism from a seminormed group to the quotient by a subgroup. -/
noncomputable
def normed_mk (S : add_subgroup M) : normed_add_group_hom M (M ‚ß∏ S) :=
{ bound' := ‚ü®1, Œª m, by simpa [one_mul] using quotient_norm_mk_le  _ m‚ü©,
  .. quotient_add_group.mk' S }

/-- `S.normed_mk` agrees with `quotient_add_group.mk' S`. -/
@[simp]
lemma normed_mk.apply (S : add_subgroup M) (m : M) : normed_mk S m = quotient_add_group.mk' S m :=
rfl

/-- `S.normed_mk` is surjective. -/
lemma surjective_normed_mk (S : add_subgroup M) : function.surjective (normed_mk S) :=
surjective_quot_mk _

/-- The kernel of `S.normed_mk` is `S`. -/
lemma ker_normed_mk (S : add_subgroup M) : S.normed_mk.ker = S :=
quotient_add_group.ker_mk  _

/-- The operator norm of the projection is at most `1`. -/
lemma norm_normed_mk_le (S : add_subgroup M) : ‚ÄñS.normed_mk‚Äñ ‚â§ 1 :=
normed_add_group_hom.op_norm_le_bound _ zero_le_one (Œª m, by simp [quotient_norm_mk_le'])

/-- The operator norm of the projection is `1` if the subspace is not dense. -/
lemma norm_normed_mk (S : add_subgroup M) (h : (S.topological_closure : set M) ‚â† univ) :
  ‚ÄñS.normed_mk‚Äñ = 1 :=
begin
  obtain ‚ü®x, hx‚ü© := set.nonempty_compl.2 h,
  let y := S.normed_mk x,
  have hy : ‚Äñy‚Äñ ‚â† 0,
  { intro h0,
    exact set.not_mem_of_mem_compl hx ((quotient_norm_eq_zero_iff S x).1 h0) },
  refine le_antisymm (norm_normed_mk_le S) (le_of_forall_pos_le_add (Œª Œµ hŒµ, _)),
  suffices : 1 ‚â§ ‚ÄñS.normed_mk‚Äñ + min Œµ ((1 : ‚Ñù)/2),
  { exact le_add_of_le_add_left this (min_le_left Œµ ((1 : ‚Ñù)/2)) },
  have hŒ¥ := sub_pos.mpr (lt_of_le_of_lt (min_le_right Œµ ((1 : ‚Ñù)/2)) one_half_lt_one),
  have hŒ¥pos : 0 < min Œµ ((1 : ‚Ñù)/2) := lt_min hŒµ one_half_pos,
  have hŒ¥norm := mul_pos (div_pos hŒ¥pos hŒ¥) (lt_of_le_of_ne (norm_nonneg y) hy.symm),
  obtain ‚ü®m, hm, hlt‚ü© := norm_mk_lt y hŒ¥norm,
  have hrw : ‚Äñy‚Äñ + min Œµ (1 / 2) / (1 - min Œµ (1 / 2)) * ‚Äñy‚Äñ =
    ‚Äñy‚Äñ * (1 + min Œµ (1 / 2) / (1 - min Œµ (1 / 2))) := by ring,
  rw [hrw] at hlt,
  have hm0 : ‚Äñm‚Äñ ‚â† 0,
  { intro h0,
    have hnorm := quotient_norm_mk_le S m,
    rw [h0, hm] at hnorm,
    replace hnorm := le_antisymm hnorm (norm_nonneg _),
    simpa [hnorm] using hy },
  replace hlt := (div_lt_div_right (lt_of_le_of_ne (norm_nonneg m) hm0.symm)).2 hlt,
  simp only [hm0, div_self, ne.def, not_false_iff] at hlt,
  have hrw‚ÇÅ : ‚Äñy‚Äñ * (1 + min Œµ (1 / 2) / (1 - min Œµ (1 / 2))) / ‚Äñm‚Äñ =
    (‚Äñy‚Äñ / ‚Äñm‚Äñ) * (1 + min Œµ (1 / 2) / (1 - min Œµ (1 / 2))) := by ring,
  rw [hrw‚ÇÅ] at hlt,
  replace hlt := (inv_pos_lt_iff_one_lt_mul (lt_trans (div_pos hŒ¥pos hŒ¥) (lt_one_add _))).2 hlt,
  suffices : ‚ÄñS.normed_mk‚Äñ ‚â• 1 - min Œµ (1 / 2),
  { exact sub_le_iff_le_add.mp this },
  calc ‚ÄñS.normed_mk‚Äñ ‚â• ‚Äñ(S.normed_mk) m‚Äñ / ‚Äñm‚Äñ : ratio_le_op_norm S.normed_mk m
  ...  = ‚Äñy‚Äñ / ‚Äñm‚Äñ : by rw [normed_mk.apply, hm]
  ... ‚â• (1 + min Œµ (1 / 2) / (1 - min Œµ (1 / 2)))‚Åª¬π : le_of_lt hlt
  ... = 1 - min Œµ (1 / 2) : by field_simp [(ne_of_lt hŒ¥).symm]
end

/-- The operator norm of the projection is `0` if the subspace is dense. -/
lemma norm_trivial_quotient_mk (S : add_subgroup M)
  (h : (S.topological_closure : set M) = set.univ) : ‚ÄñS.normed_mk‚Äñ = 0 :=
begin
  refine le_antisymm (op_norm_le_bound _ le_rfl (Œª x, _)) (norm_nonneg _),
  have hker : x ‚àà (S.normed_mk).ker.topological_closure,
  { rw [S.ker_normed_mk],
    exact set.mem_of_eq_of_mem h trivial },
  rw [ker_normed_mk] at hker,
  simp only [(quotient_norm_eq_zero_iff S x).mpr hker, normed_mk.apply, zero_mul],
end

end add_subgroup

namespace normed_add_group_hom

/-- `is_quotient f`, for `f : M ‚ü∂ N` means that `N` is isomorphic to the quotient of `M`
by the kernel of `f`. -/
structure is_quotient (f : normed_add_group_hom M N) : Prop :=
(surjective : function.surjective f)
(norm : ‚àÄ x, ‚Äñf x‚Äñ = Inf ((Œª m, ‚Äñx + m‚Äñ) '' f.ker))

/-- Given  `f : normed_add_group_hom M N` such that `f s = 0` for all `s ‚àà S`, where,
`S : add_subgroup M` is closed, the induced morphism `normed_add_group_hom (M ‚ß∏ S) N`. -/
noncomputable
def lift {N : Type*} [seminormed_add_comm_group N] (S : add_subgroup M)
  (f : normed_add_group_hom M N) (hf : ‚àÄ s ‚àà S, f s = 0) :
  normed_add_group_hom (M ‚ß∏ S) N :=
{ bound' :=
  begin
    obtain ‚ü®c : ‚Ñù, hcpos : (0 : ‚Ñù) < c, hc : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ c * ‚Äñx‚Äñ‚ü© := f.bound,
    refine ‚ü®c, Œª mbar, le_of_forall_pos_le_add (Œª Œµ hŒµ, _)‚ü©,
    obtain ‚ü®m : M, rfl : mk' S m = mbar, hmnorm : ‚Äñm‚Äñ < ‚Äñmk' S m‚Äñ + Œµ/c‚ü© :=
      norm_mk_lt mbar (div_pos hŒµ hcpos),
    calc ‚Äñf m‚Äñ ‚â§ c * ‚Äñm‚Äñ : hc m
    ... ‚â§ c*(‚Äñmk' S m‚Äñ + Œµ/c) : ((mul_lt_mul_left hcpos).mpr hmnorm).le
    ... = c * ‚Äñmk' S m‚Äñ + Œµ : by rw [mul_add, mul_div_cancel' _ hcpos.ne.symm]
  end,
  .. quotient_add_group.lift S f.to_add_monoid_hom hf }

lemma lift_mk {N : Type*} [seminormed_add_comm_group N] (S : add_subgroup M)
  (f : normed_add_group_hom M N) (hf : ‚àÄ s ‚àà S, f s = 0) (m : M) :
  lift S f hf (S.normed_mk m) = f m := rfl

lemma lift_unique {N : Type*} [seminormed_add_comm_group N] (S : add_subgroup M)
  (f : normed_add_group_hom M N) (hf : ‚àÄ s ‚àà S, f s = 0)
  (g : normed_add_group_hom (M ‚ß∏ S) N) :
  g.comp (S.normed_mk) = f ‚Üí g = lift S f hf :=
begin
  intro h,
  ext,
  rcases add_subgroup.surjective_normed_mk _ x with ‚ü®x,rfl‚ü©,
  change (g.comp (S.normed_mk) x) = _,
  simpa only [h]
end

/-- `S.normed_mk` satisfies `is_quotient`. -/
lemma is_quotient_quotient (S : add_subgroup M) : is_quotient (S.normed_mk) :=
‚ü®S.surjective_normed_mk, Œª m, by simpa [S.ker_normed_mk] using quotient_norm_mk_eq _ m‚ü©

lemma is_quotient.norm_lift {f : normed_add_group_hom M N} (hquot : is_quotient f) {Œµ : ‚Ñù}
  (hŒµ : 0 < Œµ) (n : N) : ‚àÉ (m : M), f m = n ‚àß ‚Äñm‚Äñ < ‚Äñn‚Äñ + Œµ :=
begin
  obtain ‚ü®m, rfl‚ü© := hquot.surjective n,
  have nonemp : ((Œª m', ‚Äñm + m'‚Äñ) '' f.ker).nonempty,
  { rw set.nonempty_image_iff,
    exact ‚ü®0, f.ker.zero_mem‚ü© },
  rcases real.lt_Inf_add_pos nonemp hŒµ with
    ‚ü®_, ‚ü®‚ü®x, hx, rfl‚ü©, H : ‚Äñm + x‚Äñ < Inf ((Œª (m' : M), ‚Äñm + m'‚Äñ) '' f.ker) + Œµ‚ü©‚ü©,
  exact ‚ü®m+x, by rw [map_add,(normed_add_group_hom.mem_ker f x).mp hx, add_zero],
               by rwa hquot.norm‚ü©,
end

lemma is_quotient.norm_le {f : normed_add_group_hom M N} (hquot : is_quotient f) (m : M) :
  ‚Äñf m‚Äñ ‚â§ ‚Äñm‚Äñ :=
begin
  rw hquot.norm,
  apply cInf_le,
  { use 0,
    rintros _ ‚ü®m', hm', rfl‚ü©,
    apply norm_nonneg },
  { exact ‚ü®0, f.ker.zero_mem, by simp‚ü© }
end

lemma lift_norm_le {N : Type*} [seminormed_add_comm_group N] (S : add_subgroup M)
  (f : normed_add_group_hom M N) (hf : ‚àÄ s ‚àà S, f s = 0)
  {c : ‚Ñù‚â•0} (fb : ‚Äñf‚Äñ ‚â§ c) :
  ‚Äñlift S f hf‚Äñ ‚â§ c :=
begin
  apply op_norm_le_bound _ c.coe_nonneg,
  intros x,
  by_cases hc : c = 0,
  { simp only [hc, nnreal.coe_zero, zero_mul] at fb ‚ä¢,
    obtain ‚ü®x, rfl‚ü© := surjective_quot_mk _ x,
    show ‚Äñf x‚Äñ ‚â§ 0,
    calc ‚Äñf x‚Äñ ‚â§ 0 * ‚Äñx‚Äñ : f.le_of_op_norm_le fb x
          ... = 0 : zero_mul _ },
  { replace hc : 0 < c := pos_iff_ne_zero.mpr hc,
    apply le_of_forall_pos_le_add,
    intros Œµ hŒµ,
    have aux : 0 < (Œµ / c) := div_pos hŒµ hc,
    obtain ‚ü®x, rfl, Hx‚ü© : ‚àÉ x', S.normed_mk x' = x ‚àß ‚Äñx'‚Äñ < ‚Äñx‚Äñ + (Œµ / c) :=
      (is_quotient_quotient _).norm_lift aux _,
    rw lift_mk,
    calc ‚Äñf x‚Äñ ‚â§ c * ‚Äñx‚Äñ : f.le_of_op_norm_le fb x
          ... ‚â§ c * (‚ÄñS.normed_mk x‚Äñ + Œµ / c) : (mul_le_mul_left _).mpr Hx.le
          ... = c * _ + Œµ : _,
    { exact_mod_cast hc },
    { rw [mul_add, mul_div_cancel'], exact_mod_cast hc.ne' } },
end

lemma lift_norm_noninc {N : Type*} [seminormed_add_comm_group N] (S : add_subgroup M)
  (f : normed_add_group_hom M N) (hf : ‚àÄ s ‚àà S, f s = 0)
  (fb : f.norm_noninc) :
  (lift S f hf).norm_noninc :=
Œª x,
begin
  have fb' : ‚Äñf‚Äñ ‚â§ (1 : ‚Ñù‚â•0) := norm_noninc.norm_noninc_iff_norm_le_one.mp fb,
  simpa using le_of_op_norm_le _ (f.lift_norm_le _ _ fb') _,
end

end normed_add_group_hom

/-!
### Submodules and ideals

In what follows, the norm structures created above for quotients of (semi)`normed_add_comm_group`s
by `add_subgroup`s are transferred via definitional equality to quotients of modules by submodules,
and of rings by ideals, thereby preserving the definitional equality for the topological group and
uniform structures worked for above. Completeness is also transferred via this definitional
equality.

In addition, instances are constructed for `normed_space`, `semi_normed_comm_ring`,
`normed_comm_ring` and `normed_algebra` under the appropriate hypotheses. Currently, we do not
have quotients of rings by two-sided ideals, hence the commutativity hypotheses are required.
 -/

section submodule

variables {R : Type*} [ring R] [module R M] (S : submodule R M)

instance submodule.quotient.seminormed_add_comm_group :
  seminormed_add_comm_group (M ‚ß∏ S) :=
add_subgroup.seminormed_add_comm_group_quotient S.to_add_subgroup

instance submodule.quotient.normed_add_comm_group [hS : is_closed (S : set M)] :
  normed_add_comm_group (M ‚ß∏ S) :=
@add_subgroup.normed_add_comm_group_quotient _ _ S.to_add_subgroup hS

instance submodule.quotient.complete_space [complete_space M] : complete_space (M ‚ß∏ S) :=
quotient_add_group.complete_space M S.to_add_subgroup

/-- For any `x : M ‚ß∏ S` and any `0 < Œµ`, there is `m : M` such that `submodule.quotient.mk m = x`
and `‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ`. -/
lemma submodule.quotient.norm_mk_lt {S : submodule R M} (x : M ‚ß∏ S) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
  ‚àÉ m : M, submodule.quotient.mk m = x ‚àß ‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ :=
norm_mk_lt x hŒµ

lemma submodule.quotient.norm_mk_le (m : M) :
  ‚Äñ(submodule.quotient.mk m : M ‚ß∏ S)‚Äñ ‚â§ ‚Äñm‚Äñ :=
quotient_norm_mk_le S.to_add_subgroup m

instance submodule.quotient.normed_space (ùïú : Type*) [normed_field ùïú] [normed_space ùïú M]
  [has_smul ùïú R] [is_scalar_tower ùïú R M] : normed_space ùïú (M ‚ß∏ S) :=
{ norm_smul_le := Œª k x, le_of_forall_pos_le_add $ Œª Œµ hŒµ,
  begin
    have := (nhds_basis_ball.tendsto_iff nhds_basis_ball).mp
      ((@real.uniform_continuous_const_mul (‚Äñk‚Äñ)).continuous.tendsto (‚Äñx‚Äñ)) Œµ hŒµ,
    simp only [mem_ball, exists_prop, dist, abs_sub_lt_iff] at this,
    rcases this with ‚ü®Œ¥, hŒ¥, h‚ü©,
    obtain ‚ü®a, rfl, ha‚ü© := submodule.quotient.norm_mk_lt x hŒ¥,
    specialize h (‚Äña‚Äñ) (‚ü®by linarith, by linarith [submodule.quotient.norm_mk_le S a]‚ü©),
    calc _ ‚â§ ‚Äñk‚Äñ * ‚Äña‚Äñ : (quotient_norm_mk_le S.to_add_subgroup (k ‚Ä¢ a)).trans_eq (norm_smul k a)
    ...    ‚â§ _ : (sub_lt_iff_lt_add'.mp h.1).le
  end,
  .. submodule.quotient.module' S, }

end submodule

section ideal

variables {R : Type*} [semi_normed_comm_ring R] (I : ideal R)

lemma ideal.quotient.norm_mk_lt {I : ideal R} (x : R ‚ß∏ I) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
  ‚àÉ r : R, ideal.quotient.mk I r = x ‚àß ‚Äñr‚Äñ < ‚Äñx‚Äñ + Œµ :=
norm_mk_lt x hŒµ

lemma ideal.quotient.norm_mk_le (r : R) :
  ‚Äñideal.quotient.mk I r‚Äñ ‚â§ ‚Äñr‚Äñ :=
quotient_norm_mk_le I.to_add_subgroup r

instance ideal.quotient.semi_normed_comm_ring : semi_normed_comm_ring (R ‚ß∏ I) :=
{ mul_comm := mul_comm,
  norm_mul := Œª x y, le_of_forall_pos_le_add $ Œª Œµ hŒµ,
  begin
    have := ((nhds_basis_ball.prod_nhds nhds_basis_ball).tendsto_iff nhds_basis_ball).mp
      (real.continuous_mul.tendsto (‚Äñx‚Äñ, ‚Äñy‚Äñ)) Œµ hŒµ,
    simp only [set.mem_prod, mem_ball, and_imp, prod.forall, exists_prop, prod.exists] at this,
    rcases this with ‚ü®Œµ‚ÇÅ, Œµ‚ÇÇ, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, h‚ü©,
    obtain ‚ü®‚ü®a, rfl, ha‚ü©, ‚ü®b, rfl, hb‚ü©‚ü© :=
      ‚ü®ideal.quotient.norm_mk_lt x h‚ÇÅ, ideal.quotient.norm_mk_lt y h‚ÇÇ‚ü©,
    simp only [dist, abs_sub_lt_iff] at h,
    specialize h (‚Äña‚Äñ) (‚Äñb‚Äñ) (‚ü®by linarith, by linarith [ideal.quotient.norm_mk_le I a]‚ü©)
      (‚ü®by linarith, by linarith [ideal.quotient.norm_mk_le I b]‚ü©),
    calc _ ‚â§ ‚Äña‚Äñ * ‚Äñb‚Äñ : (ideal.quotient.norm_mk_le I (a * b)).trans (norm_mul_le a b)
    ...    ‚â§ _        : (sub_lt_iff_lt_add'.mp h.1).le
  end,
  .. submodule.quotient.seminormed_add_comm_group I }

instance ideal.quotient.normed_comm_ring [is_closed (I : set R)] :
  normed_comm_ring (R ‚ß∏ I) :=
{ .. ideal.quotient.semi_normed_comm_ring I,
  .. submodule.quotient.normed_add_comm_group I }

variables (ùïú : Type*) [normed_field ùïú]

instance ideal.quotient.normed_algebra [normed_algebra ùïú R] :
  normed_algebra ùïú (R ‚ß∏ I) :=
{ .. submodule.quotient.normed_space I ùïú,
  .. ideal.quotient.algebra ùïú }

end ideal
