/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl
-/
import analysis.normed.group.infinite_sum
import topology.algebra.module.basic
import topology.instances.ennreal
import topology.instances.rat

/-!
# Normed fields

In this file we define (semi)normed rings and fields. We also prove some theorems about these
definitions.
-/

variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Type*}

noncomputable theory
open filter metric
open_locale topological_space big_operators nnreal ennreal uniformity pointwise

section semi_normed_ring

/-- A seminormed ring is a ring endowed with a seminorm which satisfies the inequality
`âˆ¥x yâˆ¥ â‰¤ âˆ¥xâˆ¥ âˆ¥yâˆ¥`. -/
class semi_normed_ring (Î± : Type*) extends has_norm Î±, ring Î±, pseudo_metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul : âˆ€ a b, norm (a * b) â‰¤ norm a * norm b)

/-- A normed ring is a ring endowed with a norm which satisfies the inequality `âˆ¥x yâˆ¥ â‰¤ âˆ¥xâˆ¥ âˆ¥yâˆ¥`. -/
class normed_ring (Î± : Type*) extends has_norm Î±, ring Î±, metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul : âˆ€ a b, norm (a * b) â‰¤ norm a * norm b)

/-- A normed ring is a seminormed ring. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_ring.to_semi_normed_ring [Î² : normed_ring Î±] : semi_normed_ring Î± :=
{ ..Î² }

/-- A seminormed commutative ring is a commutative ring endowed with a seminorm which satisfies
the inequality `âˆ¥x yâˆ¥ â‰¤ âˆ¥xâˆ¥ âˆ¥yâˆ¥`. -/
class semi_normed_comm_ring (Î± : Type*) extends semi_normed_ring Î± :=
(mul_comm : âˆ€ x y : Î±, x * y = y * x)

/-- A normed commutative ring is a commutative ring endowed with a norm which satisfies
the inequality `âˆ¥x yâˆ¥ â‰¤ âˆ¥xâˆ¥ âˆ¥yâˆ¥`. -/
class normed_comm_ring (Î± : Type*) extends normed_ring Î± :=
(mul_comm : âˆ€ x y : Î±, x * y = y * x)

/-- A normed commutative ring is a seminormed commutative ring. -/
@[priority 100] -- see Note [lower instance priority]
instance normed_comm_ring.to_semi_normed_comm_ring [Î² : normed_comm_ring Î±] :
  semi_normed_comm_ring Î± := { ..Î² }

instance : normed_comm_ring punit :=
{ norm_mul := Î» _ _, by simp,
  ..punit.normed_group,
  ..punit.comm_ring, }

/-- A mixin class with the axiom `âˆ¥1âˆ¥ = 1`. Many `normed_ring`s and all `normed_field`s satisfy this
axiom. -/
class norm_one_class (Î± : Type*) [has_norm Î±] [has_one Î±] : Prop :=
(norm_one : âˆ¥(1:Î±)âˆ¥ = 1)

export norm_one_class (norm_one)

attribute [simp] norm_one

@[simp] lemma nnnorm_one [semi_normed_group Î±] [has_one Î±] [norm_one_class Î±] : âˆ¥(1 : Î±)âˆ¥â‚Š = 1 :=
nnreal.eq norm_one

@[priority 100] -- see Note [lower instance priority]
instance semi_normed_comm_ring.to_comm_ring [Î² : semi_normed_comm_ring Î±] : comm_ring Î± := { ..Î² }

@[priority 100] -- see Note [lower instance priority]
instance normed_ring.to_normed_group [Î² : normed_ring Î±] : normed_group Î± := { ..Î² }

@[priority 100] -- see Note [lower instance priority]
instance semi_normed_ring.to_semi_normed_group [Î² : semi_normed_ring Î±] :
  semi_normed_group Î± := { ..Î² }

instance prod.norm_one_class [semi_normed_group Î±] [has_one Î±] [norm_one_class Î±]
  [semi_normed_group Î²] [has_one Î²] [norm_one_class Î²] :
  norm_one_class (Î± Ã— Î²) :=
âŸ¨by simp [prod.norm_def]âŸ©

variables [semi_normed_ring Î±]

lemma norm_mul_le (a b : Î±) : (âˆ¥a*bâˆ¥) â‰¤ (âˆ¥aâˆ¥) * (âˆ¥bâˆ¥) :=
semi_normed_ring.norm_mul _ _

lemma nnnorm_mul_le (a b : Î±) : âˆ¥a * bâˆ¥â‚Š â‰¤ âˆ¥aâˆ¥â‚Š * âˆ¥bâˆ¥â‚Š :=
by simpa only [â†norm_to_nnreal, â†real.to_nnreal_mul (norm_nonneg _)]
  using real.to_nnreal_mono (norm_mul_le _ _)

/-- A subalgebra of a seminormed ring is also a seminormed ring, with the restriction of the norm.

See note [implicit instance arguments]. -/
instance subalgebra.semi_normed_ring {ğ•œ : Type*} {_ : comm_ring ğ•œ}
  {E : Type*} [semi_normed_ring E] {_ : algebra ğ•œ E} (s : subalgebra ğ•œ E) : semi_normed_ring s :=
{ norm_mul := Î» a b, norm_mul_le a.1 b.1,
  ..s.to_submodule.semi_normed_group }

/-- A subalgebra of a normed ring is also a normed ring, with the restriction of the norm.

See note [implicit instance arguments]. -/
instance subalgebra.normed_ring {ğ•œ : Type*} {_ : comm_ring ğ•œ}
  {E : Type*} [normed_ring E] {_ : algebra ğ•œ E} (s : subalgebra ğ•œ E) : normed_ring s :=
{ ..s.semi_normed_ring }

lemma list.norm_prod_le' : âˆ€ {l : list Î±}, l â‰  [] â†’ âˆ¥l.prodâˆ¥ â‰¤ (l.map norm).prod
| [] h := (h rfl).elim
| [a] _ := by simp
| (a :: b :: l) _ :=
  begin
    rw [list.map_cons, list.prod_cons, @list.prod_cons _ _ _ âˆ¥aâˆ¥],
    refine le_trans (norm_mul_le _ _) (mul_le_mul_of_nonneg_left _ (norm_nonneg _)),
    exact list.norm_prod_le' (list.cons_ne_nil b l)
  end

lemma list.norm_prod_le [norm_one_class Î±] : âˆ€ l : list Î±, âˆ¥l.prodâˆ¥ â‰¤ (l.map norm).prod
| [] := by simp
| (a::l) := list.norm_prod_le' (list.cons_ne_nil a l)

lemma finset.norm_prod_le' {Î± : Type*} [normed_comm_ring Î±] (s : finset Î¹) (hs : s.nonempty)
  (f : Î¹ â†’ Î±) :
  âˆ¥âˆ i in s, f iâˆ¥ â‰¤ âˆ i in s, âˆ¥f iâˆ¥ :=
begin
  rcases s with âŸ¨âŸ¨lâŸ©, hlâŸ©,
  have : l.map f â‰  [], by simpa using hs,
  simpa using list.norm_prod_le' this
end

lemma finset.norm_prod_le {Î± : Type*} [normed_comm_ring Î±] [norm_one_class Î±] (s : finset Î¹)
  (f : Î¹ â†’ Î±) :
  âˆ¥âˆ i in s, f iâˆ¥ â‰¤ âˆ i in s, âˆ¥f iâˆ¥ :=
begin
  rcases s with âŸ¨âŸ¨lâŸ©, hlâŸ©,
  simpa using (l.map f).norm_prod_le
end

/-- If `Î±` is a seminormed ring, then `âˆ¥a ^ nâˆ¥â‚Š â‰¤ âˆ¥aâˆ¥â‚Š ^ n` for `n > 0`.
See also `nnnorm_pow_le`. -/
lemma nnnorm_pow_le' (a : Î±) : âˆ€ {n : â„•}, 0 < n â†’ âˆ¥a ^ nâˆ¥â‚Š â‰¤ âˆ¥aâˆ¥â‚Š ^ n
| 1 h := by simp only [pow_one]
| (n + 2) h := by simpa only [pow_succ _ (n + 1)] using
    le_trans (nnnorm_mul_le _ _) (mul_le_mul_left' (nnnorm_pow_le' n.succ_pos) _)

/-- If `Î±` is a seminormed ring with `âˆ¥1âˆ¥â‚Š = 1`, then `âˆ¥a ^ nâˆ¥â‚Š â‰¤ âˆ¥aâˆ¥â‚Š ^ n`.
See also `nnnorm_pow_le'`.-/
lemma nnnorm_pow_le [norm_one_class Î±] (a : Î±) (n : â„•) : âˆ¥a ^ nâˆ¥â‚Š â‰¤ âˆ¥aâˆ¥â‚Š ^ n :=
nat.rec_on n (by simp only [pow_zero, nnnorm_one]) (Î» k hk, nnnorm_pow_le' a k.succ_pos)

/-- If `Î±` is a seminormed ring, then `âˆ¥a ^ nâˆ¥ â‰¤ âˆ¥aâˆ¥ ^ n` for `n > 0`. See also `norm_pow_le`. -/
lemma norm_pow_le' (a : Î±) {n : â„•} (h : 0 < n) : âˆ¥a ^ nâˆ¥ â‰¤ âˆ¥aâˆ¥ ^ n :=
by simpa only [nnreal.coe_pow, coe_nnnorm] using nnreal.coe_mono (nnnorm_pow_le' a h)

/-- If `Î±` is a seminormed ring with `âˆ¥1âˆ¥ = 1`, then `âˆ¥a ^ nâˆ¥ â‰¤ âˆ¥aâˆ¥ ^ n`. See also `norm_pow_le'`.-/
lemma norm_pow_le [norm_one_class Î±] (a : Î±) (n : â„•) : âˆ¥a ^ nâˆ¥ â‰¤ âˆ¥aâˆ¥ ^ n :=
nat.rec_on n (by simp only [pow_zero, norm_one]) (Î» n hn, norm_pow_le' a n.succ_pos)

lemma eventually_norm_pow_le (a : Î±) : âˆ€á¶  (n:â„•) in at_top, âˆ¥a ^ nâˆ¥ â‰¤ âˆ¥aâˆ¥ ^ n :=
eventually_at_top.mpr âŸ¨1, Î» b h, norm_pow_le' a (nat.succ_le_iff.mp h)âŸ©

/-- In a seminormed ring, the left-multiplication `add_monoid_hom` is bounded. -/
lemma mul_left_bound (x : Î±) :
  âˆ€ (y:Î±), âˆ¥add_monoid_hom.mul_left x yâˆ¥ â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ :=
norm_mul_le x

/-- In a seminormed ring, the right-multiplication `add_monoid_hom` is bounded. -/
lemma mul_right_bound (x : Î±) :
  âˆ€ (y:Î±), âˆ¥add_monoid_hom.mul_right x yâˆ¥ â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ :=
Î» y, by {rw mul_comm, convert norm_mul_le y x}

/-- Seminormed ring structure on the product of two seminormed rings, using the sup norm. -/
instance prod.semi_normed_ring [semi_normed_ring Î²] : semi_normed_ring (Î± Ã— Î²) :=
{ norm_mul := assume x y,
  calc
    âˆ¥x * yâˆ¥ = âˆ¥(x.1*y.1, x.2*y.2)âˆ¥ : rfl
        ... = (max âˆ¥x.1*y.1âˆ¥  âˆ¥x.2*y.2âˆ¥) : rfl
        ... â‰¤ (max (âˆ¥x.1âˆ¥*âˆ¥y.1âˆ¥) (âˆ¥x.2âˆ¥*âˆ¥y.2âˆ¥)) :
          max_le_max (norm_mul_le (x.1) (y.1)) (norm_mul_le (x.2) (y.2))
        ... = (max (âˆ¥x.1âˆ¥*âˆ¥y.1âˆ¥) (âˆ¥y.2âˆ¥*âˆ¥x.2âˆ¥)) : by simp[mul_comm]
        ... â‰¤ (max (âˆ¥x.1âˆ¥) (âˆ¥x.2âˆ¥)) * (max (âˆ¥y.2âˆ¥) (âˆ¥y.1âˆ¥)) :
          by apply max_mul_mul_le_max_mul_max; simp [norm_nonneg]
        ... = (max (âˆ¥x.1âˆ¥) (âˆ¥x.2âˆ¥)) * (max (âˆ¥y.1âˆ¥) (âˆ¥y.2âˆ¥)) : by simp [max_comm]
        ... = (âˆ¥xâˆ¥*âˆ¥yâˆ¥) : rfl,
  ..prod.semi_normed_group }

/-- Seminormed group instance (using sup norm of sup norm) for matrices over a seminormed ring. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
def matrix.semi_normed_group {n m : Type*} [fintype n] [fintype m] :
  semi_normed_group (matrix n m Î±) :=
pi.semi_normed_group

local attribute [instance] matrix.semi_normed_group

lemma norm_matrix_le_iff {n m : Type*} [fintype n] [fintype m] {r : â„} (hr : 0 â‰¤ r)
  {A : matrix n m Î±} :
  âˆ¥Aâˆ¥ â‰¤ r â†” âˆ€ i j, âˆ¥A i jâˆ¥ â‰¤ r :=
by simp [pi_norm_le_iff hr]

lemma norm_matrix_lt_iff {n m : Type*} [fintype n] [fintype m] {r : â„} (hr : 0 < r)
  {A : matrix n m Î±} :
  âˆ¥Aâˆ¥ < r â†” âˆ€ i j, âˆ¥A i jâˆ¥ < r :=
by simp [pi_norm_lt_iff hr]

end semi_normed_ring

section normed_ring

variables [normed_ring Î±]

lemma units.norm_pos [nontrivial Î±] (x : Î±Ë£) : 0 < âˆ¥(x:Î±)âˆ¥ :=
norm_pos_iff.mpr (units.ne_zero x)

/-- Normed ring structure on the product of two normed rings, using the sup norm. -/
instance prod.normed_ring [normed_ring Î²] : normed_ring (Î± Ã— Î²) :=
{ norm_mul := norm_mul_le,
  ..prod.semi_normed_group }

/-- Normed group instance (using sup norm of sup norm) for matrices over a normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
def matrix.normed_group {n m : Type*} [fintype n] [fintype m] : normed_group (matrix n m Î±) :=
pi.normed_group

end normed_ring

@[priority 100] -- see Note [lower instance priority]
instance semi_normed_ring_top_monoid [semi_normed_ring Î±] : has_continuous_mul Î± :=
âŸ¨ continuous_iff_continuous_at.2 $ Î» x, tendsto_iff_norm_tendsto_zero.2 $
    begin
      have : âˆ€ e : Î± Ã— Î±, âˆ¥e.1 * e.2 - x.1 * x.2âˆ¥ â‰¤ âˆ¥e.1âˆ¥ * âˆ¥e.2 - x.2âˆ¥ + âˆ¥e.1 - x.1âˆ¥ * âˆ¥x.2âˆ¥,
      { intro e,
        calc âˆ¥e.1 * e.2 - x.1 * x.2âˆ¥ â‰¤ âˆ¥e.1 * (e.2 - x.2) + (e.1 - x.1) * x.2âˆ¥ :
          by rw [mul_sub, sub_mul, sub_add_sub_cancel]
        ... â‰¤ âˆ¥e.1âˆ¥ * âˆ¥e.2 - x.2âˆ¥ + âˆ¥e.1 - x.1âˆ¥ * âˆ¥x.2âˆ¥ :
          norm_add_le_of_le (norm_mul_le _ _) (norm_mul_le _ _) },
      refine squeeze_zero (Î» e, norm_nonneg _) this _,
      convert ((continuous_fst.tendsto x).norm.mul ((continuous_snd.tendsto x).sub
        tendsto_const_nhds).norm).add
        (((continuous_fst.tendsto x).sub tendsto_const_nhds).norm.mul _),
      show tendsto _ _ _, from tendsto_const_nhds,
      simp
    end âŸ©

/-- A seminormed ring is a topological ring. -/
@[priority 100] -- see Note [lower instance priority]
instance semi_normed_top_ring [semi_normed_ring Î±] : topological_ring Î± := { }

/-- A normed field is a field with a norm satisfying âˆ¥x yâˆ¥ = âˆ¥xâˆ¥ âˆ¥yâˆ¥. -/
class normed_field (Î± : Type*) extends has_norm Î±, field Î±, metric_space Î± :=
(dist_eq : âˆ€ x y, dist x y = norm (x - y))
(norm_mul' : âˆ€ a b, norm (a * b) = norm a * norm b)

/-- A nondiscrete normed field is a normed field in which there is an element of norm different from
`0` and `1`. This makes it possible to bring any element arbitrarily close to `0` by multiplication
by the powers of any element, and thus to relate algebra and topology. -/
class nondiscrete_normed_field (Î± : Type*) extends normed_field Î± :=
(non_trivial : âˆƒx:Î±, 1<âˆ¥xâˆ¥)

section normed_field

variables [normed_field Î±]

@[simp] lemma norm_mul (a b : Î±) : âˆ¥a * bâˆ¥ = âˆ¥aâˆ¥ * âˆ¥bâˆ¥ :=
normed_field.norm_mul' a b

@[priority 100] -- see Note [lower instance priority]
instance normed_field.to_normed_comm_ring : normed_comm_ring Î± :=
{ norm_mul := Î» a b, (norm_mul a b).le, ..â€¹normed_field Î±â€º }

@[priority 900]
instance normed_field.to_norm_one_class : norm_one_class Î± :=
âŸ¨mul_left_cancelâ‚€ (mt norm_eq_zero.1 (@one_ne_zero Î± _ _)) $
  by rw [â† norm_mul, mul_one, mul_one]âŸ©

export norm_one_class (norm_one)

@[simp] lemma nnnorm_mul (a b : Î±) : âˆ¥a * bâˆ¥â‚Š = âˆ¥aâˆ¥â‚Š * âˆ¥bâˆ¥â‚Š :=
nnreal.eq $ norm_mul a b

/-- `norm` as a `monoid_with_zero_hom`. -/
@[simps] def norm_hom : Î± â†’*â‚€ â„ := âŸ¨norm, norm_zero, norm_one, norm_mulâŸ©

/-- `nnnorm` as a `monoid_with_zero_hom`. -/
@[simps] def nnnorm_hom : Î± â†’*â‚€ â„â‰¥0 := âŸ¨nnnorm, nnnorm_zero, nnnorm_one, nnnorm_mulâŸ©

@[simp] lemma norm_pow (a : Î±) : âˆ€ (n : â„•), âˆ¥a ^ nâˆ¥ = âˆ¥aâˆ¥ ^ n :=
(norm_hom.to_monoid_hom : Î± â†’* â„).map_pow a

@[simp] lemma nnnorm_pow (a : Î±) (n : â„•) : âˆ¥a ^ nâˆ¥â‚Š = âˆ¥aâˆ¥â‚Š ^ n :=
(nnnorm_hom.to_monoid_hom : Î± â†’* â„â‰¥0).map_pow a n

@[simp] lemma norm_prod (s : finset Î²) (f : Î² â†’ Î±) :
  âˆ¥âˆ b in s, f bâˆ¥ = âˆ b in s, âˆ¥f bâˆ¥ :=
(norm_hom.to_monoid_hom : Î± â†’* â„).map_prod f s

@[simp] lemma nnnorm_prod (s : finset Î²) (f : Î² â†’ Î±) :
  âˆ¥âˆ b in s, f bâˆ¥â‚Š = âˆ b in s, âˆ¥f bâˆ¥â‚Š :=
(nnnorm_hom.to_monoid_hom : Î± â†’* â„â‰¥0).map_prod f s

@[simp] lemma norm_div (a b : Î±) : âˆ¥a / bâˆ¥ = âˆ¥aâˆ¥ / âˆ¥bâˆ¥ := (norm_hom : Î± â†’*â‚€ â„).map_div a b

@[simp] lemma nnnorm_div (a b : Î±) : âˆ¥a / bâˆ¥â‚Š = âˆ¥aâˆ¥â‚Š / âˆ¥bâˆ¥â‚Š := (nnnorm_hom : Î± â†’*â‚€ â„â‰¥0).map_div a b

@[simp] lemma norm_inv (a : Î±) : âˆ¥aâ»Â¹âˆ¥ = âˆ¥aâˆ¥â»Â¹ := (norm_hom : Î± â†’*â‚€ â„).map_inv a

@[simp] lemma nnnorm_inv (a : Î±) : âˆ¥aâ»Â¹âˆ¥â‚Š = âˆ¥aâˆ¥â‚Šâ»Â¹ :=
nnreal.eq $ by simp

@[simp] lemma norm_zpow : âˆ€ (a : Î±) (n : â„¤), âˆ¥a^nâˆ¥ = âˆ¥aâˆ¥^n := (norm_hom : Î± â†’*â‚€ â„).map_zpow

@[simp] lemma nnnorm_zpow : âˆ€ (a : Î±) (n : â„¤), âˆ¥a ^ nâˆ¥â‚Š = âˆ¥aâˆ¥â‚Š ^ n :=
(nnnorm_hom : Î± â†’*â‚€ â„â‰¥0).map_zpow

@[priority 100] -- see Note [lower instance priority]
instance normed_field.has_continuous_invâ‚€ : has_continuous_invâ‚€ Î± :=
begin
  refine âŸ¨Î» r r0, tendsto_iff_norm_tendsto_zero.2 _âŸ©,
  have r0' : 0 < âˆ¥râˆ¥ := norm_pos_iff.2 r0,
  rcases exists_between r0' with âŸ¨Îµ, Îµ0, ÎµrâŸ©,
  have : âˆ€á¶  e in ğ“ r, âˆ¥eâ»Â¹ - râ»Â¹âˆ¥ â‰¤ âˆ¥r - eâˆ¥ / âˆ¥râˆ¥ / Îµ,
  { filter_upwards [(is_open_lt continuous_const continuous_norm).eventually_mem Îµr] with e he,
    have e0 : e â‰  0 := norm_pos_iff.1 (Îµ0.trans he),
    calc âˆ¥eâ»Â¹ - râ»Â¹âˆ¥ = âˆ¥r - eâˆ¥ / âˆ¥râˆ¥ / âˆ¥eâˆ¥ : by field_simp [mul_comm]
    ... â‰¤ âˆ¥r - eâˆ¥ / âˆ¥râˆ¥ / Îµ :
      div_le_div_of_le_left (div_nonneg (norm_nonneg _) (norm_nonneg _)) Îµ0 he.le },
  refine squeeze_zero' (eventually_of_forall $ Î» _, norm_nonneg _) this _,
  refine (continuous_const.sub continuous_id).norm.div_const.div_const.tendsto' _ _ _,
  simp
end

end normed_field

namespace normed_field

variables (Î±) [nondiscrete_normed_field Î±]

lemma exists_one_lt_norm : âˆƒx : Î±, 1 < âˆ¥xâˆ¥ := â€¹nondiscrete_normed_field Î±â€º.non_trivial

lemma exists_norm_lt_one : âˆƒx : Î±, 0 < âˆ¥xâˆ¥ âˆ§ âˆ¥xâˆ¥ < 1 :=
begin
  rcases exists_one_lt_norm Î± with âŸ¨y, hyâŸ©,
  refine âŸ¨yâ»Â¹, _, _âŸ©,
  { simp only [inv_eq_zero, ne.def, norm_pos_iff],
    rintro rfl,
    rw norm_zero at hy,
    exact lt_asymm zero_lt_one hy },
  { simp [inv_lt_one hy] }
end

lemma exists_lt_norm (r : â„) : âˆƒ x : Î±, r < âˆ¥xâˆ¥ :=
let âŸ¨w, hwâŸ© := exists_one_lt_norm Î± in
let âŸ¨n, hnâŸ© := pow_unbounded_of_one_lt r hw in
âŸ¨w^n, by rwa norm_powâŸ©

lemma exists_norm_lt {r : â„} (hr : 0 < r) : âˆƒ x : Î±, 0 < âˆ¥xâˆ¥ âˆ§ âˆ¥xâˆ¥ < r :=
let âŸ¨w, hwâŸ© := exists_one_lt_norm Î± in
let âŸ¨n, hle, hltâŸ© := exists_mem_Ioc_zpow hr hw in
âŸ¨w^n, by { rw norm_zpow; exact zpow_pos_of_pos (lt_trans zero_lt_one hw) _},
by rwa norm_zpowâŸ©

variable {Î±}

@[instance]
lemma punctured_nhds_ne_bot (x : Î±) : ne_bot (ğ“[â‰ ] x) :=
begin
  rw [â† mem_closure_iff_nhds_within_ne_bot, metric.mem_closure_iff],
  rintros Îµ Îµ0,
  rcases exists_norm_lt Î± Îµ0 with âŸ¨b, hb0, hbÎµâŸ©,
  refine âŸ¨x + b, mt (set.mem_singleton_iff.trans add_right_eq_self).1 $ norm_pos_iff.1 hb0, _âŸ©,
  rwa [dist_comm, dist_eq_norm, add_sub_cancel'],
end

@[instance]
lemma nhds_within_is_unit_ne_bot : ne_bot (ğ“[{x : Î± | is_unit x}] 0) :=
by simpa only [is_unit_iff_ne_zero] using punctured_nhds_ne_bot (0:Î±)

end normed_field

instance : normed_field â„ :=
{ norm_mul' := abs_mul,
  .. real.normed_group }

instance : nondiscrete_normed_field â„ :=
{ non_trivial := âŸ¨2, by { unfold norm, rw abs_of_nonneg; norm_num }âŸ© }

namespace real

lemma norm_of_nonneg {x : â„} (hx : 0 â‰¤ x) : âˆ¥xâˆ¥ = x :=
abs_of_nonneg hx

lemma norm_of_nonpos {x : â„} (hx : x â‰¤ 0) : âˆ¥xâˆ¥ = -x :=
abs_of_nonpos hx

@[simp] lemma norm_coe_nat (n : â„•) : âˆ¥(n : â„)âˆ¥ = n := abs_of_nonneg n.cast_nonneg

@[simp] lemma nnnorm_coe_nat (n : â„•) : âˆ¥(n : â„)âˆ¥â‚Š = n := nnreal.eq $ by simp

@[simp] lemma norm_two : âˆ¥(2 : â„)âˆ¥ = 2 := abs_of_pos (@zero_lt_two â„ _ _)

@[simp] lemma nnnorm_two : âˆ¥(2 : â„)âˆ¥â‚Š = 2 := nnreal.eq $ by simp

lemma nnnorm_of_nonneg {x : â„} (hx : 0 â‰¤ x) : âˆ¥xâˆ¥â‚Š = âŸ¨x, hxâŸ© :=
nnreal.eq $ norm_of_nonneg hx

lemma ennnorm_eq_of_real {x : â„} (hx : 0 â‰¤ x) : (âˆ¥xâˆ¥â‚Š : â„â‰¥0âˆ) = ennreal.of_real x :=
by { rw [â† of_real_norm_eq_coe_nnnorm, norm_of_nonneg hx] }

lemma of_real_le_ennnorm (x : â„) : ennreal.of_real x â‰¤ âˆ¥xâˆ¥â‚Š :=
begin
  by_cases hx : 0 â‰¤ x,
  { rw real.ennnorm_eq_of_real hx, refl' },
  { rw [ennreal.of_real_eq_zero.2 (le_of_lt (not_le.1 hx))],
    exact bot_le }
end

/-- If `E` is a nontrivial topological module over `â„`, then `E` has no isolated points.
This is a particular case of `module.punctured_nhds_ne_bot`. -/
instance punctured_nhds_module_ne_bot
  {E : Type*} [add_comm_group E] [topological_space E] [has_continuous_add E] [nontrivial E]
  [module â„ E] [has_continuous_smul â„ E] (x : E) :
  ne_bot (ğ“[â‰ ] x) :=
module.punctured_nhds_ne_bot â„ E x

end real

namespace nnreal

open_locale nnreal

@[simp] lemma norm_eq (x : â„â‰¥0) : âˆ¥(x : â„)âˆ¥ = x :=
by rw [real.norm_eq_abs, x.abs_eq]

@[simp] lemma nnnorm_eq (x : â„â‰¥0) : âˆ¥(x : â„)âˆ¥â‚Š = x :=
nnreal.eq $ real.norm_of_nonneg x.2

end nnreal

@[simp] lemma norm_norm [semi_normed_group Î±] (x : Î±) : âˆ¥âˆ¥xâˆ¥âˆ¥ = âˆ¥xâˆ¥ :=
real.norm_of_nonneg (norm_nonneg _)

@[simp] lemma nnnorm_norm [semi_normed_group Î±] (a : Î±) : âˆ¥âˆ¥aâˆ¥âˆ¥â‚Š = âˆ¥aâˆ¥â‚Š :=
by simpa [real.nnnorm_of_nonneg (norm_nonneg a)]

/-- A restatement of `metric_space.tendsto_at_top` in terms of the norm. -/
lemma normed_group.tendsto_at_top [nonempty Î±] [semilattice_sup Î±] {Î² : Type*} [semi_normed_group Î²]
  {f : Î± â†’ Î²} {b : Î²} :
  tendsto f at_top (ğ“ b) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N â‰¤ n â†’ âˆ¥f n - bâˆ¥ < Îµ :=
(at_top_basis.tendsto_iff metric.nhds_basis_ball).trans (by simp [dist_eq_norm])

/--
A variant of `normed_group.tendsto_at_top` that
uses `âˆƒ N, âˆ€ n > N, ...` rather than `âˆƒ N, âˆ€ n â‰¥ N, ...`
-/
lemma normed_group.tendsto_at_top' [nonempty Î±] [semilattice_sup Î±] [no_max_order Î±]
  {Î² : Type*} [semi_normed_group Î²]
  {f : Î± â†’ Î²} {b : Î²} :
  tendsto f at_top (ğ“ b) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ N, âˆ€ n, N < n â†’ âˆ¥f n - bâˆ¥ < Îµ :=
(at_top_basis_Ioi.tendsto_iff metric.nhds_basis_ball).trans (by simp [dist_eq_norm])

instance : normed_comm_ring â„¤ :=
{ norm := Î» n, âˆ¥(n : â„)âˆ¥,
  norm_mul := Î» m n, le_of_eq $ by simp only [norm, int.cast_mul, abs_mul],
  dist_eq := Î» m n, by simp only [int.dist_eq, norm, int.cast_sub],
  mul_comm := mul_comm }

@[norm_cast] lemma int.norm_cast_real (m : â„¤) : âˆ¥(m : â„)âˆ¥ = âˆ¥mâˆ¥ := rfl

lemma int.norm_eq_abs (n : â„¤) : âˆ¥nâˆ¥ = |n| := rfl

lemma nnreal.coe_nat_abs (n : â„¤) : (n.nat_abs : â„â‰¥0) = âˆ¥nâˆ¥â‚Š :=
nnreal.eq $ calc ((n.nat_abs : â„â‰¥0) : â„)
               = (n.nat_abs : â„¤) : by simp only [int.cast_coe_nat, nnreal.coe_nat_cast]
           ... = |n|           : by simp only [â† int.abs_eq_nat_abs, int.cast_abs]
           ... = âˆ¥nâˆ¥              : rfl

instance : norm_one_class â„¤ :=
âŸ¨by simp [â† int.norm_cast_real]âŸ©

instance : normed_field â„š :=
{ norm := Î» r, âˆ¥(r : â„)âˆ¥,
  norm_mul' := Î» râ‚ râ‚‚, by simp only [norm, rat.cast_mul, abs_mul],
  dist_eq := Î» râ‚ râ‚‚, by simp only [rat.dist_eq, norm, rat.cast_sub] }

instance : nondiscrete_normed_field â„š :=
{ non_trivial := âŸ¨2, by { unfold norm, rw abs_of_nonneg; norm_num }âŸ© }

@[norm_cast, simp] lemma rat.norm_cast_real (r : â„š) : âˆ¥(r : â„)âˆ¥ = âˆ¥râˆ¥ := rfl

@[norm_cast, simp] lemma int.norm_cast_rat (m : â„¤) : âˆ¥(m : â„š)âˆ¥ = âˆ¥mâˆ¥ :=
by rw [â† rat.norm_cast_real, â† int.norm_cast_real]; congr' 1; norm_cast

-- Now that we've installed the norm on `â„¤`,
-- we can state some lemmas about `nsmul` and `zsmul`.
section
variables [semi_normed_group Î±]

lemma norm_nsmul_le (n : â„•) (a : Î±) : âˆ¥n â€¢ aâˆ¥ â‰¤ n * âˆ¥aâˆ¥ :=
begin
  induction n with n ih,
  { simp only [norm_zero, nat.cast_zero, zero_mul, zero_smul] },
  simp only [nat.succ_eq_add_one, add_smul, add_mul, one_mul, nat.cast_add,
    nat.cast_one, one_nsmul],
  exact norm_add_le_of_le ih le_rfl
end

lemma norm_zsmul_le (n : â„¤) (a : Î±) : âˆ¥n â€¢ aâˆ¥ â‰¤ âˆ¥nâˆ¥ * âˆ¥aâˆ¥ :=
begin
  induction n with n n,
  { simp only [int.of_nat_eq_coe, coe_nat_zsmul],
    convert norm_nsmul_le n a,
    exact nat.abs_cast n },
  { simp only [int.neg_succ_of_nat_coe, neg_smul, norm_neg, coe_nat_zsmul],
    convert norm_nsmul_le n.succ a,
    exact nat.abs_cast n.succ, }
end

lemma nnnorm_nsmul_le (n : â„•) (a : Î±) : âˆ¥n â€¢ aâˆ¥â‚Š â‰¤ n * âˆ¥aâˆ¥â‚Š :=
by simpa only [â†nnreal.coe_le_coe, nnreal.coe_mul, nnreal.coe_nat_cast]
  using norm_nsmul_le n a

lemma nnnorm_zsmul_le (n : â„¤) (a : Î±) : âˆ¥n â€¢ aâˆ¥â‚Š â‰¤ âˆ¥nâˆ¥â‚Š * âˆ¥aâˆ¥â‚Š :=
by simpa only [â†nnreal.coe_le_coe, nnreal.coe_mul] using norm_zsmul_le n a

end

section cauchy_product

/-! ## Multiplying two infinite sums in a normed ring

In this section, we prove various results about `(âˆ‘' x : Î¹, f x) * (âˆ‘' y : Î¹', g y)` in a normed
ring. There are similar results proven in `topology/algebra/infinite_sum` (e.g `tsum_mul_tsum`),
but in a normed ring we get summability results which aren't true in general.

We first establish results about arbitrary index types, `Î²` and `Î³`, and then we specialize to
`Î² = Î³ = â„•` to prove the Cauchy product formula
(see `tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm`).

### Arbitrary index types
-/

variables {Î¹' : Type*} [normed_ring Î±]

open finset
open_locale classical

lemma summable.mul_of_nonneg {f : Î¹ â†’ â„} {g : Î¹' â†’ â„}
  (hf : summable f) (hg : summable g) (hf' : 0 â‰¤ f) (hg' : 0 â‰¤ g) :
  summable (Î» (x : Î¹ Ã— Î¹'), f x.1 * g x.2) :=
let âŸ¨s, hfâŸ© := hf in
let âŸ¨t, hgâŸ© := hg in
suffices this : âˆ€ u : finset (Î¹ Ã— Î¹'), âˆ‘ x in u, f x.1 * g x.2 â‰¤ s*t,
  from summable_of_sum_le (Î» x, mul_nonneg (hf' _) (hg' _)) this,
assume u,
calc  âˆ‘ x in u, f x.1 * g x.2
    â‰¤ âˆ‘ x in (u.image prod.fst).product (u.image prod.snd), f x.1 * g x.2 :
      sum_mono_set_of_nonneg (Î» x, mul_nonneg (hf' _) (hg' _)) subset_product
... = âˆ‘ x in u.image prod.fst, âˆ‘ y in u.image prod.snd, f x * g y : sum_product
... = âˆ‘ x in u.image prod.fst, f x * âˆ‘ y in u.image prod.snd, g y :
      sum_congr rfl (Î» x _, mul_sum.symm)
... â‰¤ âˆ‘ x in u.image prod.fst, f x * t :
      sum_le_sum
        (Î» x _, mul_le_mul_of_nonneg_left (sum_le_has_sum _ (Î» _ _, hg' _) hg) (hf' _))
... = (âˆ‘ x in u.image prod.fst, f x) * t : sum_mul.symm
... â‰¤ s * t :
      mul_le_mul_of_nonneg_right (sum_le_has_sum _ (Î» _ _, hf' _) hf) (hg.nonneg $ Î» _, hg' _)

lemma summable.mul_norm {f : Î¹ â†’ Î±} {g : Î¹' â†’ Î±}
  (hf : summable (Î» x, âˆ¥f xâˆ¥)) (hg : summable (Î» x, âˆ¥g xâˆ¥)) :
  summable (Î» (x : Î¹ Ã— Î¹'), âˆ¥f x.1 * g x.2âˆ¥) :=
summable_of_nonneg_of_le (Î» x, norm_nonneg (f x.1 * g x.2)) (Î» x, norm_mul_le (f x.1) (g x.2))
  (hf.mul_of_nonneg hg (Î» x, norm_nonneg $ f x) (Î» x, norm_nonneg $ g x) : _)

lemma summable_mul_of_summable_norm [complete_space Î±] {f : Î¹ â†’ Î±} {g : Î¹' â†’ Î±}
  (hf : summable (Î» x, âˆ¥f xâˆ¥)) (hg : summable (Î» x, âˆ¥g xâˆ¥)) :
  summable (Î» (x : Î¹ Ã— Î¹'), f x.1 * g x.2) :=
summable_of_summable_norm (hf.mul_norm hg)

/-- Product of two infinites sums indexed by arbitrary types.
    See also `tsum_mul_tsum` if `f` and `g` are *not* absolutely summable. -/
lemma tsum_mul_tsum_of_summable_norm [complete_space Î±] {f : Î¹ â†’ Î±} {g : Î¹' â†’ Î±}
  (hf : summable (Î» x, âˆ¥f xâˆ¥)) (hg : summable (Î» x, âˆ¥g xâˆ¥)) :
  (âˆ‘' x, f x) * (âˆ‘' y, g y) = (âˆ‘' z : Î¹ Ã— Î¹', f z.1 * g z.2) :=
tsum_mul_tsum (summable_of_summable_norm hf) (summable_of_summable_norm hg)
  (summable_mul_of_summable_norm hf hg)

/-! ### `â„•`-indexed families (Cauchy product)

We prove two versions of the Cauchy product formula. The first one is
`tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm`, where the `n`-th term is a sum over
`finset.range (n+1)` involving `nat` substraction.
In order to avoid `nat` substraction, we also provide
`tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm`,
where the `n`-th term is a sum over all pairs `(k, l)` such that `k+l=n`, which corresponds to the
`finset` `finset.nat.antidiagonal n`. -/

section nat

open finset.nat

lemma summable_norm_sum_mul_antidiagonal_of_summable_norm {f g : â„• â†’ Î±}
  (hf : summable (Î» x, âˆ¥f xâˆ¥)) (hg : summable (Î» x, âˆ¥g xâˆ¥)) :
  summable (Î» n, âˆ¥âˆ‘ kl in antidiagonal n, f kl.1 * g kl.2âˆ¥) :=
begin
  have := summable_sum_mul_antidiagonal_of_summable_mul
    (summable.mul_of_nonneg hf hg (Î» _, norm_nonneg _) (Î» _, norm_nonneg _)),
  refine summable_of_nonneg_of_le (Î» _, norm_nonneg _) _ this,
  intros n,
  calc  âˆ¥âˆ‘ kl in antidiagonal n, f kl.1 * g kl.2âˆ¥
      â‰¤ âˆ‘ kl in antidiagonal n, âˆ¥f kl.1 * g kl.2âˆ¥ : norm_sum_le _ _
  ... â‰¤ âˆ‘ kl in antidiagonal n, âˆ¥f kl.1âˆ¥ * âˆ¥g kl.2âˆ¥ : sum_le_sum (Î» i _, norm_mul_le _ _)
end

/-- The Cauchy product formula for the product of two infinite sums indexed by `â„•`,
    expressed by summing on `finset.nat.antidiagonal`.
    See also `tsum_mul_tsum_eq_tsum_sum_antidiagonal` if `f` and `g` are
    *not* absolutely summable. -/
lemma tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm [complete_space Î±] {f g : â„• â†’ Î±}
  (hf : summable (Î» x, âˆ¥f xâˆ¥)) (hg : summable (Î» x, âˆ¥g xâˆ¥)) :
  (âˆ‘' n, f n) * (âˆ‘' n, g n) = âˆ‘' n, âˆ‘ kl in antidiagonal n, f kl.1 * g kl.2 :=
tsum_mul_tsum_eq_tsum_sum_antidiagonal (summable_of_summable_norm hf) (summable_of_summable_norm hg)
  (summable_mul_of_summable_norm hf hg)

lemma summable_norm_sum_mul_range_of_summable_norm {f g : â„• â†’ Î±}
  (hf : summable (Î» x, âˆ¥f xâˆ¥)) (hg : summable (Î» x, âˆ¥g xâˆ¥)) :
  summable (Î» n, âˆ¥âˆ‘ k in range (n+1), f k * g (n - k)âˆ¥) :=
begin
  simp_rw â† sum_antidiagonal_eq_sum_range_succ (Î» k l, f k * g l),
  exact summable_norm_sum_mul_antidiagonal_of_summable_norm hf hg
end

/-- The Cauchy product formula for the product of two infinite sums indexed by `â„•`,
    expressed by summing on `finset.range`.
    See also `tsum_mul_tsum_eq_tsum_sum_range` if `f` and `g` are
    *not* absolutely summable. -/
lemma tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm [complete_space Î±] {f g : â„• â†’ Î±}
  (hf : summable (Î» x, âˆ¥f xâˆ¥)) (hg : summable (Î» x, âˆ¥g xâˆ¥)) :
  (âˆ‘' n, f n) * (âˆ‘' n, g n) = âˆ‘' n, âˆ‘ k in range (n+1), f k * g (n - k) :=
begin
  simp_rw â† sum_antidiagonal_eq_sum_range_succ (Î» k l, f k * g l),
  exact tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm hf hg
end

end nat

end cauchy_product

section ring_hom_isometric

variables {Râ‚ : Type*} {Râ‚‚ : Type*} {Râ‚ƒ : Type*}

/-- This class states that a ring homomorphism is isometric. This is a sufficient assumption
for a continuous semilinear map to be bounded and this is the main use for this typeclass. -/
class ring_hom_isometric [semiring Râ‚] [semiring Râ‚‚] [has_norm Râ‚] [has_norm Râ‚‚]
  (Ïƒ : Râ‚ â†’+* Râ‚‚) : Prop :=
(is_iso : âˆ€ {x : Râ‚}, âˆ¥Ïƒ xâˆ¥ = âˆ¥xâˆ¥)

attribute [simp] ring_hom_isometric.is_iso

variables [semi_normed_ring Râ‚] [semi_normed_ring Râ‚‚] [semi_normed_ring Râ‚ƒ]

instance ring_hom_isometric.ids : ring_hom_isometric (ring_hom.id Râ‚) :=
âŸ¨Î» x, rflâŸ©

end ring_hom_isometric
