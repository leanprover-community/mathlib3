/-
Copyright (c) 2022 Christopher Hoskin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christopher Hoskin
-/
import analysis.normed_space.basic

/-!
# M-structure

A continuous projection P on a normed space X is said to be an L-projection if, for all `x` in `X`,
$$
âˆ¥xâˆ¥ = âˆ¥P xâˆ¥ + âˆ¥(1-P) xâˆ¥.
$$
The range of an L-projection is said to be an L-summand of X.

A continuous projection P on a normed space X is said to be an M-projection if, for all `x` in `X`,
$$
âˆ¥xâˆ¥ = max(âˆ¥P xâˆ¥,âˆ¥(1-P) xâˆ¥).
$$
The range of an M-projection is said to be an M-summand of X.

The L-projections and M-projections form Boolean algebras. When X is a Banach space, the Boolean
algebra of L-projections is complete.

Let `X` be a normed space with dual `X^*`. A closed subspace `M` of `X` is said to be an M-ideal if
the topological annihilator `M^âˆ˜` is an L-summand of `X^*`.

M-ideal, M-summands and L-summands were introduced by Alfsen and Effros in [alfseneffros1972] to
study the structure of general Banach spaces. When `A` is a JB*-triple, the M-ideals of `A` are
exactly the norm-closed ideals of `A`. When `A` is a JBW*-triple with predual `X`, the M-summands of
`A` are exactly the weak*-closed ideals, and their pre-duals can be identified with the L-summands
of `X`. In the special case when `A` is a C*-algebra, the M-ideals are exactly the norm-closed
two-sided ideals of `A`, when `A` is also a W*-algebra the M-summands are exactly the weak*-closed
two-sided ideals of `A`.

## Implementation notes

The approach to showing that the L-projections form a Boolean algebra is inspired by
`measure_theory.measurable_space`.

## References

* [Behrends, M-structure and the Banach-Stone Theorem][behrends1979]
* [Harmand, Werner, Werner, M-ideals in Banach spaces and Banach algebras][harmandwernerwerner1993]

## Tags

M-summand, M-projection, L-summand, L-projection, M-ideal, M-structure

-/

variables {M : Type*} [monoid M]

/--
A continuous linear map `P` on a normed space `X` is said to be a projection if it is idempotent.
-/
def is_projection (x : M) : Prop := x^2 = x

lemma projection_def {P : M} (h : is_projection P) : P^2 = P := by exact h

namespace is_projection

lemma mul_of_commute {P Q : M} (h : commute P Q) (hâ‚ : is_projection P) (hâ‚‚ : is_projection Q) :
  is_projection (P*Q)  :=
begin
  unfold is_projection,
  unfold is_projection at hâ‚,
  unfold is_projection at hâ‚‚,
  unfold commute at h,
  unfold semiconj_by at h,
  rw [sq, mul_assoc, â† mul_assoc Q, â†h, mul_assoc P, â† sq, hâ‚‚, â† mul_assoc, â† sq, hâ‚],
end

variables {R : Type*} [ring R]

lemma complement {P: R} (h: is_projection P) : is_projection (1-P) :=
begin
  unfold is_projection,
  unfold is_projection at h,
  rw sq at h,
  rw [sq, mul_sub_left_distrib, mul_one, sub_mul, one_mul, h, sub_self, sub_zero],
end


lemma complement_iff {P: R} : is_projection P â†” is_projection (1-P) :=
âŸ¨ is_projection.complement ,
begin
  intros h,
  rw â† sub_sub_cancel 1 P,
  apply is_projection.complement h,
end âŸ©

instance : has_compl (subtype (is_projection  : R â†’ Prop)) :=
âŸ¨Î» P, âŸ¨1-P, P.prop.complementâŸ©âŸ©

end is_projection

variables {X : Type*} [normed_group X]

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ X]

/--
A projection on a normed space `X` is said to be an L-projection if, for all `x` in `X`,
$$
âˆ¥xâˆ¥ = âˆ¥P xâˆ¥ + âˆ¥(1-P) xâˆ¥.
$$
-/
def is_Lprojection (P : X â†’L[ğ•œ] X) : Prop := is_projection P âˆ§ âˆ€ (x : X), âˆ¥xâˆ¥ = âˆ¥P xâˆ¥ + âˆ¥(1-P) xâˆ¥

/--
A projection on a normed space `X` is said to be an M-projection if, for all `x` in `X`,
$$
âˆ¥xâˆ¥ = max(âˆ¥P xâˆ¥, âˆ¥(1-P) xâˆ¥).
$$
-/
def is_Mprojection (P: X â†’L[ğ•œ] X) : Prop :=
  is_projection P âˆ§ âˆ€ (x : X), âˆ¥xâˆ¥ = (max âˆ¥P xâˆ¥  âˆ¥(1-P) xâˆ¥)

namespace is_Lprojection

lemma Lcomplement {P: X â†’L[ğ•œ] X} (h: is_Lprojection P) :  is_Lprojection (1-P) :=
âŸ¨is_projection.complement_iff.mp h.1, Î» x, by { rw [add_comm, sub_sub_cancel], exact h.2 x }âŸ©

lemma Lcomplement_iff (P: X â†’L[ğ•œ] X) : is_Lprojection P â†” is_Lprojection (1-P) := âŸ¨
  Lcomplement,
  begin
    intros h,
    rw â† sub_sub_cancel 1 P,
    apply Lcomplement h,
  end âŸ©

lemma PQ_eq_QPQ (P Q : X â†’L[ğ•œ] X) (hâ‚: is_Lprojection P) (hâ‚‚: is_Lprojection Q) :
  P * Q = Q * P * Q :=
begin
  ext,
  rw â† norm_sub_eq_zero_iff,
  have e1 : âˆ¥Q xâˆ¥ â‰¥ âˆ¥Q xâˆ¥ + 2 â€¢ âˆ¥ (P * Q) x - (Q * P * Q) xâˆ¥ :=
  calc âˆ¥Q xâˆ¥ = âˆ¥P (Q x)âˆ¥ + âˆ¥(1 - P) (Q x)âˆ¥ : by rw hâ‚.right
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + âˆ¥(1 - P) (Q x)âˆ¥ : by rw hâ‚‚.right
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q ((1 - P) (Q x))âˆ¥ + âˆ¥(1-Q) ((1 - P) (Q x))âˆ¥) :
    by rw hâ‚‚.right ((1 - P) (Q x))
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q (Q x - P (Q x))âˆ¥ + âˆ¥(1-Q) ((1 - P) (Q x))âˆ¥) : rfl
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q (Q x) - Q (P (Q x))âˆ¥ + âˆ¥(1-Q) ((1 - P) (Q x))âˆ¥) :
    by rw map_sub
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥(Q * Q) x - Q (P (Q x))âˆ¥ + âˆ¥(1-Q) ((1 - P) (Q x))âˆ¥) :
    rfl
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q x - Q (P (Q x))âˆ¥ + âˆ¥(1-Q) ((1 - P) (Q x))âˆ¥) :
    by rw [â† sq, projection_def hâ‚‚.left]
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q x - Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (Q x - P (Q x))âˆ¥) : rfl
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q x - Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (Q x) - (1-Q) (P (Q x))âˆ¥)
    : by rw map_sub
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q x - Q (P (Q x))âˆ¥
    + âˆ¥((1-Q) * Q) x - (1-Q) (P (Q x))âˆ¥) : rfl
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q x - Q (P (Q x))âˆ¥ + âˆ¥0 - (1-Q) (P (Q x))âˆ¥) :
    by {rw [sub_mul, â† sq, projection_def hâ‚‚.left, one_mul, sub_self ], exact rfl }
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥ + (âˆ¥Q x - Q (P (Q x))âˆ¥ + âˆ¥(1-Q) (P (Q x))âˆ¥) :
    by rw [zero_sub, norm_neg]
  ... = âˆ¥Q (P (Q x))âˆ¥ + âˆ¥Q x - Q (P (Q x))âˆ¥ + 2â€¢âˆ¥(1-Q) (P (Q x))âˆ¥  : by abel
  ... â‰¥ âˆ¥Q xâˆ¥ + 2 â€¢ âˆ¥ (P * Q) x - (Q * P * Q) xâˆ¥ :
    by exact add_le_add_right (norm_le_insert' (Q x) (Q (P (Q x)))) (2â€¢âˆ¥(1-Q) (P (Q x))âˆ¥),
  rw ge at e1,
  nth_rewrite_rhs 0 â† add_zero (âˆ¥Q xâˆ¥) at e1,
  rw [add_le_add_iff_left, two_smul,  â† two_mul]  at e1,
  rw le_antisymm_iff,
  split,
  { rw â† mul_zero (2:â„) at e1,
    rw mul_le_mul_left at e1, exact e1, norm_num, },
  { apply norm_nonneg, }
end

lemma QP_eq_QPQ (P Q : X â†’L[ğ•œ] X) (hâ‚: is_Lprojection P) (hâ‚‚: is_Lprojection Q) : Q * P = Q * P * Q
  :=
begin
  have e1: P * (1 - Q) = P * (1 - Q) - (Q * P - Q * P * Q) :=
  calc P * (1 - Q) = (1 - Q) * P * (1 - Q) : by rw PQ_eq_QPQ P (1 - Q) hâ‚ hâ‚‚.Lcomplement
  ... = 1 * (P * (1 - Q)) - Q * (P * (1 - Q)) : by {rw mul_assoc, rw sub_mul,}
  ... = P * (1 - Q) - Q * (P * (1 - Q)) : by rw one_mul
  ... = P * (1 - Q) - Q * (P - P * Q) : by rw [mul_sub, mul_one]
  ... = P * (1 - Q) - (Q * P - Q * P * Q) : by rw [mul_sub Q, mul_assoc],
  rw [eq_sub_iff_add_eq, add_right_eq_self, sub_eq_zero] at e1,
  exact e1,
end

lemma Lproj_commute {P Q: X â†’L[ğ•œ] X} (hâ‚: is_Lprojection P) (hâ‚‚ : is_Lprojection Q) : commute P Q :=
begin
  unfold commute,
  unfold semiconj_by,
  rw PQ_eq_QPQ P Q hâ‚ hâ‚‚,
  nth_rewrite_rhs 0 QP_eq_QPQ P Q hâ‚ hâ‚‚,
end

lemma mul {P Q : X â†’L[ğ•œ] X} (hâ‚ : is_Lprojection P) (hâ‚‚ : is_Lprojection Q) :
  is_Lprojection (P*Q) :=
begin
  unfold is_Lprojection,
  split,
  { apply is_projection.mul_of_commute (Lproj_commute hâ‚ hâ‚‚) hâ‚.left hâ‚‚.left, },
  { intro x,
    rw le_antisymm_iff,
    split,
    -- rw map_sub, apply norm_add_le,
    { calc âˆ¥ x âˆ¥ = âˆ¥(P * Q) x + (x - (P * Q) x)âˆ¥ : by abel
      ... â‰¤ âˆ¥(P * Q) xâˆ¥ + âˆ¥ x - (P * Q) x âˆ¥ : by apply norm_add_le
      ... = âˆ¥(P * Q) xâˆ¥ + âˆ¥(1 - P * Q) xâˆ¥ : rfl },
    { calc âˆ¥xâˆ¥ = âˆ¥Q xâˆ¥ + âˆ¥(1-Q) xâˆ¥ : by rw hâ‚‚.right x
      ... = âˆ¥P(Q x)âˆ¥ + âˆ¥(1-P)(Q x)âˆ¥ + âˆ¥(1-Q) xâˆ¥ : by rw hâ‚.right (Q x)
      ... = âˆ¥P(Q x)âˆ¥ + âˆ¥Q x - P (Q x)âˆ¥ + âˆ¥x - Q xâˆ¥ : rfl
      ... = âˆ¥P(Q x)âˆ¥ + (âˆ¥Q x - P (Q x)âˆ¥ + âˆ¥x - Q xâˆ¥) : by rw add_assoc
      ... â‰¥ âˆ¥P(Q x)âˆ¥ + âˆ¥(Q x - P (Q x)) + (x - Q x)âˆ¥ :
        by apply (add_le_add_iff_left (âˆ¥P(Q x)âˆ¥)).mpr (norm_add_le (Q x - P (Q x)) (x - Q x))
      ... = âˆ¥P(Q x)âˆ¥ + âˆ¥x - P (Q x)âˆ¥ : by rw sub_add_sub_cancel'
      ... = âˆ¥(P * Q) xâˆ¥ + âˆ¥(1 - P * Q) xâˆ¥ : rfl }, }
end

lemma join {P Q: X â†’L[ğ•œ] X} (hâ‚ : is_Lprojection P) (hâ‚‚ : is_Lprojection Q) :
  is_Lprojection (P + Q - P * Q) :=
begin
  have e1:  1 - (1 - P) * (1 - Q) = P + Q - P * Q :=
  calc 1 - (1 - P) * (1 - Q) = 1 -(1 - Q - P * (1 - Q)) : by rw [sub_mul, one_mul]
  ... = Q + P * (1 - Q) : by rw [sub_sub, sub_sub_self]
  ... = P + Q - P * Q : by rw [mul_sub, mul_one, add_sub, add_comm],
  rw â† e1,
  rw â† is_Lprojection.Lcomplement_iff,
  apply is_Lprojection.mul,
  apply is_Lprojection.Lcomplement hâ‚,
  apply is_Lprojection.Lcomplement hâ‚‚,
end

instance : has_compl { f : X â†’L[ğ•œ] X // is_Lprojection f } :=
âŸ¨Î» P, âŸ¨1-P, P.prop.LcomplementâŸ©âŸ©

@[simp] lemma coe_compl (P : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :
  â†‘(Pá¶œ) = (1:X â†’L[ğ•œ] X) - â†‘P := rfl

instance : has_inf (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :=
âŸ¨Î» P Q, âŸ¨P * Q, P.prop.mul Q.propâŸ© âŸ©

@[simp] lemma coe_inf (P Q : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :
  â†‘(P âŠ“ Q) = ((â†‘P : (X â†’L[ğ•œ] X)) * â†‘Q) := rfl

instance : has_sup (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :=
âŸ¨Î» P Q, âŸ¨P + Q - P * Q, P.prop.join Q.propâŸ© âŸ©

@[simp] lemma coe_sup (P Q : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :
  â†‘(P âŠ” Q) = ((â†‘P:X â†’L[ğ•œ] X) + â†‘Q - â†‘P * â†‘Q) := rfl

instance : has_sdiff (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :=
âŸ¨Î» P Q, âŸ¨P * (1-Q), by exact is_Lprojection.mul P.prop (is_Lprojection.Lcomplement Q.prop) âŸ©âŸ©

@[simp] lemma coe_sdiff (P Q : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :
  â†‘(P \ Q) = (â†‘P:X â†’L[ğ•œ] X) * (1-â†‘Q) := rfl

instance : partial_order (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :=
{ le := Î» P Q, (â†‘P:X â†’L[ğ•œ] X) = â†‘(P âŠ“ Q),
  le_refl := Î» P, begin
    simp only [coe_inf],
    rw [â† sq, projection_def],
    exact P.prop.left,
  end,
  le_trans := Î» P Q R, begin
    intros hâ‚ hâ‚‚,
    simp only [coe_inf],
    have eâ‚: â†‘P = â†‘P * â†‘Q := hâ‚,
    have eâ‚‚: â†‘Q = â†‘Q * â†‘R := hâ‚‚,
    have eâ‚ƒ: (â†‘P:X â†’L[ğ•œ] X) = â†‘P * â†‘R := begin
      nth_rewrite_rhs 0 eâ‚,
      rw [mul_assoc, â† eâ‚‚, â† eâ‚],
    end,
    apply eâ‚ƒ,
  end,
  le_antisymm := Î» P Q, begin
    intros hâ‚ hâ‚‚,
    have eâ‚: â†‘P = â†‘P * â†‘Q := hâ‚,
    have eâ‚‚: â†‘Q = â†‘Q * â†‘P := hâ‚‚,
    have eâ‚ƒ: (â†‘P:X â†’L[ğ•œ] X) = â†‘Q := by rw [eâ‚, commute.eq (Lproj_commute P.prop Q.prop), â† eâ‚‚],
    apply subtype.eq eâ‚ƒ,
  end, }

instance : has_zero (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop))  :=
âŸ¨âŸ¨0, begin
  unfold is_Lprojection,
  split,
  { unfold is_projection,
    rw [sq, zero_mul], },
  { intro, simp only [continuous_linear_map.zero_apply, norm_zero, sub_zero,
      continuous_linear_map.one_apply, zero_add], },
endâŸ©âŸ©

@[simp] lemma coe_zero : â†‘(0 : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) = (0 : X â†’L[ğ•œ] X) :=
rfl

instance : has_one (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop))  :=
âŸ¨âŸ¨1, begin
  rw â† sub_zero (1:X â†’L[ğ•œ] X),
  apply is_Lprojection.Lcomplement,
  apply (0 : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)).prop,
endâŸ©âŸ©

@[simp] lemma coe_one : â†‘(1 : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) = (1 : X â†’L[ğ•œ] X) :=
rfl

instance : bounded_order (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :=
{ top := 1,
  le_top := Î» P, begin
    have e: (â†‘P:X â†’L[ğ•œ] X) = â†‘P *  1 := by rw mul_one,
    apply e,
  end,
  bot := 0,
  bot_le := Î» P, show 0 â‰¤ P, from zero_mul P, }

@[simp] lemma coe_bot : â†‘(bounded_order.bot : subtype (is_Lprojection  :
  (X â†’L[ğ•œ] X) â†’ Prop)) = (0: X â†’L[ğ•œ] X) := rfl

@[simp] lemma coe_top : â†‘(bounded_order.top : subtype (is_Lprojection  :
  (X â†’L[ğ•œ] X) â†’ Prop)) = (1: X â†’L[ğ•œ] X) := rfl

lemma compl_mul_left {P : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)} {Q: X â†’L[ğ•œ] X} :
  Q - â†‘P * Q = â†‘Pá¶œ * Q := by rw [coe_compl, sub_mul, one_mul]

lemma compl_orthog {P : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)} :
  (â†‘P: X â†’L[ğ•œ] X) * (â†‘ Pá¶œ) = 0 :=
by rw [coe_compl, mul_sub, â† sq, mul_one, projection_def P.prop.left, sub_self]

lemma distrib_lattice_lemma {P Q R : subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)} :
  ((â†‘P:X â†’L[ğ•œ] X) + â†‘Pá¶œ * R) * (â†‘P + â†‘Q * â†‘R * â†‘Pá¶œ) = (â†‘P + â†‘Q * â†‘R * â†‘Pá¶œ) :=
by rw [add_mul, mul_add, mul_add, mul_assoc â†‘Pá¶œ â†‘R (â†‘Q * â†‘R * â†‘Pá¶œ), â† mul_assoc â†‘R (â†‘Q*â†‘R)  â†‘Pá¶œ,
    â† coe_inf Q, commute.eq (Lproj_commute Pá¶œ.prop R.prop),
    commute.eq (Lproj_commute (QâŠ“R).prop Pá¶œ.prop), commute.eq (Lproj_commute R.prop (QâŠ“R).prop),
    coe_inf Q, mul_assoc â†‘Q, â† mul_assoc, mul_assoc â†‘R, commute.eq (Lproj_commute Pá¶œ.prop P.prop),
    compl_orthog, zero_mul, mul_zero, zero_add, add_zero, â† mul_assoc, â† sq, â† sq,
    projection_def P.prop.left, projection_def R.prop.left, â† coe_inf Q, mul_assoc,
    commute.eq (Lproj_commute (QâŠ“R).prop Pá¶œ.prop), â† mul_assoc, â† sq, projection_def Pá¶œ.prop.left]

instance : distrib_lattice (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :=
{ le_sup_left := Î» P Q, begin
    have e: â†‘P = â†‘P * â†‘(P âŠ” Q) := by rw [coe_sup, â† add_sub, mul_add, mul_sub, â† mul_assoc, â† sq,
      projection_def P.prop.left, sub_self, add_zero],
    apply e,
  end,
  le_sup_right := Î» P Q, begin
    have e: (â†‘Q: X â†’L[ğ•œ] X) = â†‘Q * â†‘(P âŠ” Q) :=
    begin
      rw [coe_sup, â† add_sub, mul_add, mul_sub, commute.eq (Lproj_commute P.prop Q.prop),
        â† mul_assoc, â† sq, projection_def Q.prop.left],
      abel,
    end,
    apply e,
  end,
  sup_le := Î» P Q R, begin
    intros hâ‚ hâ‚‚,
    have eâ‚: â†‘P = â†‘P * â†‘R := hâ‚,
    have eâ‚‚: â†‘Q = â†‘Q * â†‘R := hâ‚‚,
    have e:  â†‘(P âŠ” Q) = â†‘(P âŠ” Q) * â†‘R := by rw [coe_sup, â† add_sub, add_mul, sub_mul, mul_assoc,
       â† eâ‚‚, â† eâ‚],
    apply e,
  end,
  inf_le_left := Î» P Q, begin
    have e: â†‘(P âŠ“ Q) = â†‘(P âŠ“ Q) * â†‘P := by rw [coe_inf, mul_assoc,
      commute.eq (Lproj_commute Q.prop P.prop), â† mul_assoc, â† sq, (projection_def P.prop.left)],
    apply e,
  end,
  inf_le_right := Î» P Q, begin
    have e: â†‘(P âŠ“ Q) = â†‘(P âŠ“ Q) * â†‘Q := by
      rw [coe_inf, mul_assoc,  â† sq, (projection_def Q.prop.left)],
    apply e,
  end,
  le_inf := Î» P Q R, begin
    intros hâ‚ hâ‚‚,
    have eâ‚: â†‘P = â†‘P * â†‘Q := hâ‚,
    have e: â†‘P =  â†‘P * â†‘(Q âŠ“ R) := begin
      rw [coe_inf, â† mul_assoc, â† eâ‚],
      apply hâ‚‚,
    end,
    apply e,
  end,
  le_sup_inf := Î» P Q R, begin
    have eâ‚: â†‘((P âŠ” Q) âŠ“ (P âŠ” R)) = â†‘P + â†‘Q * â†‘R * â†‘Pá¶œ := by rw [coe_inf, coe_sup, coe_sup,
      â† add_sub, â† add_sub, compl_mul_left, compl_mul_left, add_mul,
      mul_add, commute.eq (Lproj_commute Pá¶œ.prop Q.prop), mul_add, â† mul_assoc, mul_assoc â†‘Q,
      commute.eq (Lproj_commute Pá¶œ.prop P.prop), compl_orthog, zero_mul, mul_zero, zero_add,
      add_zero, â† mul_assoc, mul_assoc â†‘Q, â†sq, â† sq, projection_def P.prop.left,
      projection_def Pá¶œ.prop.left, mul_assoc, commute.eq (Lproj_commute Pá¶œ.prop R.prop),
      â† mul_assoc],
    have eâ‚‚ : â†‘((P âŠ” Q) âŠ“ (P âŠ” R)) * â†‘(P âŠ” Q âŠ“ R) = â†‘P + â†‘Q * â†‘R * â†‘Pá¶œ := by rw [coe_inf, coe_sup,
      coe_sup, coe_sup, â† add_sub, â† add_sub, â† add_sub, compl_mul_left, compl_mul_left,
      compl_mul_left, commute.eq (Lproj_commute Pá¶œ.prop (QâŠ“R).prop), coe_inf, mul_assoc,
      distrib_lattice_lemma, commute.eq (Lproj_commute Q.prop R.prop), distrib_lattice_lemma],
    have e: â†‘((P âŠ” Q) âŠ“ (P âŠ” R)) = â†‘((P âŠ” Q) âŠ“ (P âŠ” R)) * â†‘(P âŠ” Q âŠ“ R) := by rw [eâ‚‚, eâ‚],
    apply e,
  end,
  .. is_Lprojection.subtype.has_inf,
  .. is_Lprojection.subtype.has_sup,
  .. is_Lprojection.subtype.partial_order }

instance : boolean_algebra (subtype (is_Lprojection  : (X â†’L[ğ•œ] X) â†’ Prop)) :=
{ sup_inf_sdiff := Î» P Q, begin
    apply subtype.eq,
    simp only [subtype.val_eq_coe, coe_sup, coe_inf, coe_sdiff],
    rw [mul_assoc, â† mul_assoc â†‘Q, commute.eq (Lproj_commute Q.prop P.prop), mul_assoc â†‘P â†‘Q,
      â† coe_compl, compl_orthog, mul_zero, mul_zero, sub_zero, â† mul_add, coe_compl,
      add_sub_cancel'_right, mul_one],
  end,
  inf_inf_sdiff := Î» P Q, begin
    apply subtype.eq,
    simp only [subtype.val_eq_coe, coe_inf, coe_sdiff, coe_bot],
    rw [mul_assoc, â† mul_assoc â†‘Q, commute.eq (Lproj_commute Q.prop P.prop), â† coe_compl, mul_assoc,
      compl_orthog, mul_zero, mul_zero],
  end,
  inf_compl_le_bot := Î» P, begin
    apply eq.le,
    apply subtype.eq,
    simp only [subtype.val_eq_coe, coe_inf, coe_compl, coe_bot],
    rw [â† coe_compl, compl_orthog],
  end,
  top_le_sup_compl := Î» P, begin
    apply eq.le,
    apply subtype.eq,
    simp only [subtype.val_eq_coe, coe_top, coe_sup, coe_compl, add_sub_cancel'_right],
    rw [â† coe_compl, compl_orthog, sub_zero],
  end,
  sdiff_eq := Î» P Q, begin
    apply subtype.eq,
    simp only [subtype.val_eq_coe, coe_inf, coe_sdiff, coe_compl],
  end,
  .. is_Lprojection.subtype.has_compl,
  .. is_Lprojection.subtype.has_sdiff,
  .. is_Lprojection.subtype.bounded_order,
  .. is_Lprojection.subtype.distrib_lattice }

end is_Lprojection
