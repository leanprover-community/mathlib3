/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, Yury Kudryashov
-/
import analysis.normed_space.basic
import linear_algebra.affine_space.midpoint
import topology.instances.real_vector_space

/-!
# Torsors of additive normed group actions.

This file defines torsors of additive normed group actions, with a
metric space structure.  The motivating case is Euclidean affine
spaces.

-/

noncomputable theory
open_locale nnreal topological_space
open filter

/-- A `semi_normed_add_torsor V P` is a torsor of an additive seminormed group
action by a `semi_normed_group V` on points `P`. We bundle the pseudometric space
structure and require the distance to be the same as results from the
norm (which in fact implies the distance yields a pseudometric space, but
bundling just the distance and using an instance for the pseudometric space
results in type class problems). -/
class semi_normed_add_torsor (V : out_param $ Type*) (P : Type*)
  [out_param $ semi_normed_group V] [pseudo_metric_space P]
  extends add_torsor V P :=
(dist_eq_norm' : ‚àÄ (x y : P), dist x y = ‚à•(x -·µ• y : V)‚à•)

/-- A `normed_add_torsor V P` is a torsor of an additive normed group
action by a `normed_group V` on points `P`. We bundle the metric space
structure and require the distance to be the same as results from the
norm (which in fact implies the distance yields a metric space, but
bundling just the distance and using an instance for the metric space
results in type class problems). -/
class normed_add_torsor (V : out_param $ Type*) (P : Type*)
  [out_param $ normed_group V] [metric_space P]
  extends add_torsor V P :=
(dist_eq_norm' : ‚àÄ (x y : P), dist x y = ‚à•(x -·µ• y : V)‚à•)

/-- A `normed_add_torsor` is a `semi_normed_add_torsor`. -/
@[priority 100]
instance normed_add_torsor.to_semi_normed_add_torsor {V P : Type*} [normed_group V] [metric_space P]
  [Œ≤ : normed_add_torsor V P] : semi_normed_add_torsor V P := { ..Œ≤ }

variables {Œ± V P : Type*} [semi_normed_group V] [pseudo_metric_space P] [semi_normed_add_torsor V P]
variables {W Q : Type*} [normed_group W] [metric_space Q] [normed_add_torsor W Q]

/-- A `semi_normed_group` is a `semi_normed_add_torsor` over itself. -/
@[priority 100]
instance semi_normed_group.normed_add_torsor : semi_normed_add_torsor V V :=
{ dist_eq_norm' := dist_eq_norm }

/-- A `normed_group` is a `normed_add_torsor` over itself. -/
@[priority 100]
instance normed_group.normed_add_torsor : normed_add_torsor W W :=
{ dist_eq_norm' := dist_eq_norm }

include V

section

variables (V W)

/-- The distance equals the norm of subtracting two points. In this
lemma, it is necessary to have `V` as an explicit argument; otherwise
`rw dist_eq_norm_vsub` sometimes doesn't work. -/
lemma dist_eq_norm_vsub (x y : P) :
  dist x y = ‚à•(x -·µ• y)‚à• :=
semi_normed_add_torsor.dist_eq_norm' x y

end

@[simp] lemma dist_vadd_cancel_left (v : V) (x y : P) :
  dist (v +·µ• x) (v +·µ• y) = dist x y :=
by rw [dist_eq_norm_vsub V, dist_eq_norm_vsub V, vadd_vsub_vadd_cancel_left]

@[simp] lemma dist_vadd_cancel_right (v‚ÇÅ v‚ÇÇ : V) (x : P) :
  dist (v‚ÇÅ +·µ• x) (v‚ÇÇ +·µ• x) = dist v‚ÇÅ v‚ÇÇ :=
by rw [dist_eq_norm_vsub V, dist_eq_norm, vadd_vsub_vadd_cancel_right]

@[simp] lemma dist_vadd_left (v : V) (x : P) : dist (v +·µ• x) x = ‚à•v‚à• :=
by simp [dist_eq_norm_vsub V _ x]

@[simp] lemma dist_vadd_right (v : V) (x : P) : dist x (v +·µ• x) = ‚à•v‚à• :=
by rw [dist_comm, dist_vadd_left]

@[simp] lemma dist_vsub_cancel_left (x y z : P) : dist (x -·µ• y) (x -·µ• z) = dist y z :=
by rw [dist_eq_norm, vsub_sub_vsub_cancel_left, dist_comm, dist_eq_norm_vsub V]

@[simp] lemma dist_vsub_cancel_right (x y z : P) : dist (x -·µ• z) (y -·µ• z) = dist x y :=
by rw [dist_eq_norm, vsub_sub_vsub_cancel_right, dist_eq_norm_vsub V]

lemma dist_vadd_vadd_le (v v' : V) (p p' : P) :
  dist (v +·µ• p) (v' +·µ• p') ‚â§ dist v v' + dist p p' :=
by simpa using dist_triangle (v +·µ• p) (v' +·µ• p) (v' +·µ• p')

lemma dist_vsub_vsub_le (p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : P) :
  dist (p‚ÇÅ -·µ• p‚ÇÇ) (p‚ÇÉ -·µ• p‚ÇÑ) ‚â§ dist p‚ÇÅ p‚ÇÉ + dist p‚ÇÇ p‚ÇÑ :=
by { rw [dist_eq_norm, vsub_sub_vsub_comm, dist_eq_norm_vsub V, dist_eq_norm_vsub V],
 exact norm_sub_le _ _ }

lemma nndist_vadd_vadd_le (v v' : V) (p p' : P) :
  nndist (v +·µ• p) (v' +·µ• p') ‚â§ nndist v v' + nndist p p' :=
by simp only [‚Üê nnreal.coe_le_coe, nnreal.coe_add, ‚Üê dist_nndist, dist_vadd_vadd_le]

lemma nndist_vsub_vsub_le (p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : P) :
  nndist (p‚ÇÅ -·µ• p‚ÇÇ) (p‚ÇÉ -·µ• p‚ÇÑ) ‚â§ nndist p‚ÇÅ p‚ÇÉ + nndist p‚ÇÇ p‚ÇÑ :=
by simp only [‚Üê nnreal.coe_le_coe, nnreal.coe_add, ‚Üê dist_nndist, dist_vsub_vsub_le]

lemma edist_vadd_vadd_le (v v' : V) (p p' : P) :
  edist (v +·µ• p) (v' +·µ• p') ‚â§ edist v v' + edist p p' :=
by { simp only [edist_nndist], apply_mod_cast nndist_vadd_vadd_le }

lemma edist_vsub_vsub_le (p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : P) :
  edist (p‚ÇÅ -·µ• p‚ÇÇ) (p‚ÇÉ -·µ• p‚ÇÑ) ‚â§ edist p‚ÇÅ p‚ÇÉ + edist p‚ÇÇ p‚ÇÑ :=
by { simp only [edist_nndist], apply_mod_cast nndist_vsub_vsub_le }

omit V

/-- The pseudodistance defines a pseudometric space structure on the torsor. This
is not an instance because it depends on `V` to define a `metric_space
P`. -/
def pseudo_metric_space_of_normed_group_of_add_torsor (V P : Type*) [semi_normed_group V]
  [add_torsor V P] : pseudo_metric_space P :=
{ dist := Œª x y, ‚à•(x -·µ• y : V)‚à•,
  dist_self := Œª x, by simp,
  dist_comm := Œª x y, by simp only [‚Üêneg_vsub_eq_vsub_rev y x, norm_neg],
  dist_triangle := begin
    intros x y z,
    change ‚à•x -·µ• z‚à• ‚â§ ‚à•x -·µ• y‚à• + ‚à•y -·µ• z‚à•,
    rw ‚Üêvsub_add_vsub_cancel,
    apply norm_add_le
  end }

/-- The distance defines a metric space structure on the torsor. This
is not an instance because it depends on `V` to define a `metric_space
P`. -/
def metric_space_of_normed_group_of_add_torsor (V P : Type*) [normed_group V] [add_torsor V P] :
  metric_space P :=
{ dist := Œª x y, ‚à•(x -·µ• y : V)‚à•,
  dist_self := Œª x, by simp,
  eq_of_dist_eq_zero := Œª x y h, by simpa using h,
  dist_comm := Œª x y, by simp only [‚Üêneg_vsub_eq_vsub_rev y x, norm_neg],
  dist_triangle := begin
    intros x y z,
    change ‚à•x -·µ• z‚à• ‚â§ ‚à•x -·µ• y‚à• + ‚à•y -·µ• z‚à•,
    rw ‚Üêvsub_add_vsub_cancel,
    apply norm_add_le
  end }

include V

lemma lipschitz_with.vadd [pseudo_emetric_space Œ±] {f : Œ± ‚Üí V} {g : Œ± ‚Üí P} {Kf Kg : ‚Ñù‚â•0}
  (hf : lipschitz_with Kf f) (hg : lipschitz_with Kg g) :
  lipschitz_with (Kf + Kg) (f +·µ• g) :=
Œª x y,
calc edist (f x +·µ• g x) (f y +·µ• g y) ‚â§ edist (f x) (f y) + edist (g x) (g y) :
  edist_vadd_vadd_le _ _ _ _
... ‚â§ Kf * edist x y + Kg * edist x y :
  add_le_add (hf x y) (hg x y)
... = (Kf + Kg) * edist x y :
  (add_mul _ _ _).symm

lemma lipschitz_with.vsub [pseudo_emetric_space Œ±] {f g : Œ± ‚Üí P} {Kf Kg : ‚Ñù‚â•0}
  (hf : lipschitz_with Kf f) (hg : lipschitz_with Kg g) :
  lipschitz_with (Kf + Kg) (f -·µ• g) :=
Œª x y,
calc edist (f x -·µ• g x) (f y -·µ• g y) ‚â§ edist (f x) (f y) + edist (g x) (g y) :
  edist_vsub_vsub_le _ _ _ _
... ‚â§ Kf * edist x y + Kg * edist x y :
  add_le_add (hf x y) (hg x y)
... = (Kf + Kg) * edist x y :
  (add_mul _ _ _).symm

lemma uniform_continuous_vadd : uniform_continuous (Œª x : V √ó P, x.1 +·µ• x.2) :=
(lipschitz_with.prod_fst.vadd lipschitz_with.prod_snd).uniform_continuous

lemma uniform_continuous_vsub : uniform_continuous (Œª x : P √ó P, x.1 -·µ• x.2) :=
(lipschitz_with.prod_fst.vsub lipschitz_with.prod_snd).uniform_continuous

lemma continuous_vadd : continuous (Œª x : V √ó P, x.1 +·µ• x.2) :=
uniform_continuous_vadd.continuous

lemma continuous_vsub : continuous (Œª x : P √ó P, x.1 -·µ• x.2) :=
uniform_continuous_vsub.continuous

lemma filter.tendsto.vadd {l : filter Œ±} {f : Œ± ‚Üí V} {g : Œ± ‚Üí P} {v : V} {p : P}
  (hf : tendsto f l (ùìù v)) (hg : tendsto g l (ùìù p)) :
  tendsto (f +·µ• g) l (ùìù (v +·µ• p)) :=
(continuous_vadd.tendsto (v, p)).comp (hf.prod_mk_nhds hg)

lemma filter.tendsto.vsub {l : filter Œ±} {f g : Œ± ‚Üí P} {x y : P}
  (hf : tendsto f l (ùìù x)) (hg : tendsto g l (ùìù y)) :
  tendsto (f -·µ• g) l (ùìù (x -·µ• y)) :=
(continuous_vsub.tendsto (x, y)).comp (hf.prod_mk_nhds hg)

section

variables [topological_space Œ±]

lemma continuous.vadd {f : Œ± ‚Üí V} {g : Œ± ‚Üí P} (hf : continuous f) (hg : continuous g) :
  continuous (f +·µ• g) :=
continuous_vadd.comp (hf.prod_mk hg)

lemma continuous.vsub {f g : Œ± ‚Üí P} (hf : continuous f) (hg : continuous g) :
  continuous (f -·µ• g) :=
continuous_vsub.comp (hf.prod_mk hg : _)

lemma continuous_at.vadd {f : Œ± ‚Üí V} {g : Œ± ‚Üí P} {x : Œ±} (hf : continuous_at f x)
  (hg : continuous_at g x) :
  continuous_at (f +·µ• g) x :=
hf.vadd hg

lemma continuous_at.vsub {f g : Œ± ‚Üí P}  {x : Œ±} (hf : continuous_at f x) (hg : continuous_at g x) :
  continuous_at (f -·µ• g) x :=
hf.vsub hg

lemma continuous_within_at.vadd {f : Œ± ‚Üí V} {g : Œ± ‚Üí P} {x : Œ±} {s : set Œ±}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g s x) :
  continuous_within_at (f +·µ• g) s x :=
hf.vadd hg

lemma continuous_within_at.vsub {f g : Œ± ‚Üí P} {x : Œ±} {s : set Œ±}
  (hf : continuous_within_at f s x) (hg : continuous_within_at g s x) :
  continuous_within_at (f -·µ• g) s x :=
hf.vsub hg

end

section

variables {R : Type*} [ring R] [topological_space R] [module R V] [has_continuous_smul R V]

lemma filter.tendsto.line_map {l : filter Œ±} {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí P} {g : Œ± ‚Üí R} {p‚ÇÅ p‚ÇÇ : P} {c : R}
  (h‚ÇÅ : tendsto f‚ÇÅ l (ùìù p‚ÇÅ)) (h‚ÇÇ : tendsto f‚ÇÇ l (ùìù p‚ÇÇ)) (hg : tendsto g l (ùìù c)) :
  tendsto (Œª x, affine_map.line_map (f‚ÇÅ x) (f‚ÇÇ x) (g x)) l (ùìù $ affine_map.line_map p‚ÇÅ p‚ÇÇ c) :=
(hg.smul (h‚ÇÇ.vsub h‚ÇÅ)).vadd h‚ÇÅ

lemma filter.tendsto.midpoint [invertible (2:R)] {l : filter Œ±} {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí P} {p‚ÇÅ p‚ÇÇ : P}
  (h‚ÇÅ : tendsto f‚ÇÅ l (ùìù p‚ÇÅ)) (h‚ÇÇ : tendsto f‚ÇÇ l (ùìù p‚ÇÇ)) :
  tendsto (Œª x, midpoint R (f‚ÇÅ x) (f‚ÇÇ x)) l (ùìù $ midpoint R p‚ÇÅ p‚ÇÇ) :=
h‚ÇÅ.line_map h‚ÇÇ tendsto_const_nhds

end

section normed_space

variables {ùïú : Type*} [normed_field ùïú] [semi_normed_space ùïú V]

open affine_map

@[simp] lemma dist_center_homothety (p‚ÇÅ p‚ÇÇ : P) (c : ùïú) :
  dist p‚ÇÅ (homothety p‚ÇÅ c p‚ÇÇ) = ‚à•c‚à• * dist p‚ÇÅ p‚ÇÇ :=
by simp [homothety_def, norm_smul, ‚Üê dist_eq_norm_vsub, dist_comm]

@[simp] lemma dist_homothety_center (p‚ÇÅ p‚ÇÇ : P) (c : ùïú) :
  dist (homothety p‚ÇÅ c p‚ÇÇ) p‚ÇÅ = ‚à•c‚à• * dist p‚ÇÅ p‚ÇÇ :=
by rw [dist_comm, dist_center_homothety]

@[simp] lemma dist_homothety_self (p‚ÇÅ p‚ÇÇ : P) (c : ùïú) :
  dist (homothety p‚ÇÅ c p‚ÇÇ) p‚ÇÇ = ‚à•1 - c‚à• * dist p‚ÇÅ p‚ÇÇ :=
by rw [homothety_eq_line_map, ‚Üê line_map_apply_one_sub, ‚Üê homothety_eq_line_map,
  dist_homothety_center, dist_comm]

@[simp] lemma dist_self_homothety (p‚ÇÅ p‚ÇÇ : P) (c : ùïú) :
  dist p‚ÇÇ (homothety p‚ÇÅ c p‚ÇÇ) = ‚à•1 - c‚à• * dist p‚ÇÅ p‚ÇÇ :=
by rw [dist_comm, dist_homothety_self]

variables [invertible (2:ùïú)]

@[simp] lemma dist_left_midpoint (p‚ÇÅ p‚ÇÇ : P) :
  dist p‚ÇÅ (midpoint ùïú p‚ÇÅ p‚ÇÇ) = ‚à•(2:ùïú)‚à•‚Åª¬π * dist p‚ÇÅ p‚ÇÇ :=
by rw [midpoint, ‚Üê homothety_eq_line_map, dist_center_homothety, inv_of_eq_inv,
  ‚Üê normed_field.norm_inv]

@[simp] lemma dist_midpoint_left (p‚ÇÅ p‚ÇÇ : P) :
  dist (midpoint ùïú p‚ÇÅ p‚ÇÇ) p‚ÇÅ = ‚à•(2:ùïú)‚à•‚Åª¬π * dist p‚ÇÅ p‚ÇÇ :=
by rw [dist_comm, dist_left_midpoint]

@[simp] lemma dist_midpoint_right (p‚ÇÅ p‚ÇÇ : P) :
  dist (midpoint ùïú p‚ÇÅ p‚ÇÇ) p‚ÇÇ = ‚à•(2:ùïú)‚à•‚Åª¬π * dist p‚ÇÅ p‚ÇÇ :=
by rw [midpoint_comm, dist_midpoint_left, dist_comm]

@[simp] lemma dist_right_midpoint (p‚ÇÅ p‚ÇÇ : P) :
  dist p‚ÇÇ (midpoint ùïú p‚ÇÅ p‚ÇÇ) = ‚à•(2:ùïú)‚à•‚Åª¬π * dist p‚ÇÅ p‚ÇÇ :=
by rw [dist_comm, dist_midpoint_right]

lemma dist_midpoint_midpoint_le' (p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : P) :
  dist (midpoint ùïú p‚ÇÅ p‚ÇÇ) (midpoint ùïú p‚ÇÉ p‚ÇÑ) ‚â§ (dist p‚ÇÅ p‚ÇÉ + dist p‚ÇÇ p‚ÇÑ) / ‚à•(2 : ùïú)‚à• :=
begin
  rw [dist_eq_norm_vsub V, dist_eq_norm_vsub V, dist_eq_norm_vsub V, midpoint_vsub_midpoint];
    try { apply_instance },
  rw [midpoint_eq_smul_add, norm_smul, inv_of_eq_inv, normed_field.norm_inv, ‚Üê div_eq_inv_mul],
  exact div_le_div_of_le_of_nonneg (norm_add_le _ _) (norm_nonneg _),
end

end normed_space

variables [semi_normed_space ‚Ñù V] [normed_space ‚Ñù W]

lemma dist_midpoint_midpoint_le (p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : V) :
  dist (midpoint ‚Ñù p‚ÇÅ p‚ÇÇ) (midpoint ‚Ñù p‚ÇÉ p‚ÇÑ) ‚â§ (dist p‚ÇÅ p‚ÇÉ + dist p‚ÇÇ p‚ÇÑ) / 2 :=
by simpa using dist_midpoint_midpoint_le' p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ

include W

/-- A continuous map between two normed affine spaces is an affine map provided that
it sends midpoints to midpoints. -/
def affine_map.of_map_midpoint (f : P ‚Üí Q)
  (h : ‚àÄ x y, f (midpoint ‚Ñù x y) = midpoint ‚Ñù (f x) (f y))
  (hfc : continuous f) :
  P ‚Üí·µÉ[‚Ñù] Q :=
affine_map.mk' f
  ‚Üë((add_monoid_hom.of_map_midpoint ‚Ñù ‚Ñù
    ((affine_equiv.vadd_const ‚Ñù (f $ classical.arbitrary P)).symm ‚àò f ‚àò
      (affine_equiv.vadd_const ‚Ñù (classical.arbitrary P))) (by simp)
      (Œª x y, by simp [h])).to_real_linear_map $ by apply_rules [continuous.vadd, continuous.vsub,
        continuous_const, hfc.comp, continuous_id])
  (classical.arbitrary P)
  (Œª p, by simp)
