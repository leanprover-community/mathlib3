/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import analysis.normed_space.banach
import analysis.normed_space.finite_dimension
import analysis.convex.combination
import linear_algebra.affine_space.barycentric_coords
import linear_algebra.affine_space.finite_dimensional

/-!
# Bases in normed affine spaces.

This file contains results about bases in normed affine spaces.

## Main definitions:

 * `continuous_barycentric_coord`
 * `is_open_map_barycentric_coord`
 * `interior_convex_hull_aff_basis`
 * `exists_subset_affine_independent_span_eq_top_of_open`
 * `interior_convex_hull_nonempty_iff_aff_span_eq_top`
-/

section barycentric

variables {Î¹ ğ•œ E P : Type*} [nondiscrete_normed_field ğ•œ] [complete_space ğ•œ]
variables [normed_group E] [normed_space ğ•œ E] [finite_dimensional ğ•œ E]
variables [metric_space P] [normed_add_torsor E P]
variables {p : Î¹ â†’ P} (h_ind : affine_independent ğ•œ p) (h_tot : affine_span ğ•œ (set.range p) = âŠ¤)

@[continuity]
lemma continuous_barycentric_coord (i : Î¹) : continuous (barycentric_coord h_ind h_tot i) :=
affine_map.continuous_of_finite_dimensional _

local attribute [instance] finite_dimensional.complete

lemma is_open_map_barycentric_coord [nontrivial Î¹] (i : Î¹) :
  is_open_map (barycentric_coord h_ind h_tot i) :=
open_mapping_affine
  (continuous_barycentric_coord h_ind h_tot i)
  (surjective_barycentric_coord h_ind h_tot i)

end barycentric

open set

/-- Given a finite-dimensional normed real vector space, the interior of the convex hull of an
affine basis is the set of points whose barycentric coordinates are strictly positive with respect
to this basis.

TODO Restate this result for affine spaces (instead of vector spaces) once the definition of
convexity is generalised to this setting. -/
lemma interior_convex_hull_aff_basis {Î¹ E : Type*} [fintype Î¹] [normed_group E] [normed_space â„ E]
  {p : Î¹ â†’ E} (h_ind : affine_independent â„ p) (h_tot : affine_span â„ (range p) = âŠ¤) :
  interior (convex_hull â„ (range p)) = { x | âˆ€ i, 0 < barycentric_coord h_ind h_tot i x } :=
begin
  cases subsingleton_or_nontrivial Î¹ with h h,
  { -- The zero-dimensional case.
    haveI := h,
    suffices : range p = univ, { simp [this], },
    refine affine_subspace.eq_univ_of_subsingleton_span_eq_top _ h_tot,
    rw â† image_univ,
    exact subsingleton.image subsingleton_of_subsingleton p, },
  { -- The positive-dimensional case.
    haveI : finite_dimensional â„ E,
    { classical,
      obtain âŸ¨iâŸ© := (infer_instance : nonempty Î¹),
      have b := basis_of_aff_ind_span_eq_top h_ind h_tot i,
      exact finite_dimensional.of_fintype_basis b, },
    have : convex_hull â„ (range p) = â‹‚ i, (barycentric_coord h_ind h_tot i)â»Â¹' Ici 0,
    { rw convex_hull_affine_basis_eq_nonneg_barycentric h_ind h_tot, ext, simp, },
    ext,
    simp only [this, interior_Inter_of_fintype, â† is_open_map.preimage_interior_eq_interior_preimage
      (continuous_barycentric_coord h_ind h_tot _) (is_open_map_barycentric_coord h_ind h_tot _),
      interior_Ici, mem_Inter, mem_set_of_eq, mem_Ioi, mem_preimage], },
end

variables {V P : Type*} [normed_group V] [normed_space â„ V] [metric_space P] [normed_add_torsor V P]
include V

open affine_map

/-- Given a set `s` of affine-independent points belonging to an open set `u`, we may extend `s` to
an affine basis, all of whose elements belong to `u`. -/
lemma exists_subset_affine_independent_span_eq_top_of_open {s u : set P} (hu : is_open u)
  (hsu : s âŠ† u) (hne : s.nonempty) (h : affine_independent â„ (coe : s â†’ P)) :
  âˆƒ t : set P, s âŠ† t âˆ§ t âŠ† u âˆ§ affine_independent â„ (coe : t â†’ P) âˆ§ affine_span â„ t = âŠ¤ :=
begin
  obtain âŸ¨q, hqâŸ© := hne,
  obtain âŸ¨Îµ, hÎµ, hÎµuâŸ© := metric.is_open_iff.mp hu q (hsu hq),
  obtain âŸ¨t, htâ‚, htâ‚‚, htâ‚ƒâŸ© := exists_subset_affine_independent_affine_span_eq_top h,
  let f : P â†’ P := Î» y, line_map q y (Îµ / 2 / (dist y q)),
  have hf : âˆ€ y, f y âˆˆ u,
  { intros y,
    apply hÎµu,
    simp only [metric.mem_ball, f, line_map_apply, dist_vadd_left, norm_smul, real.norm_eq_abs,
      dist_eq_norm_vsub V y q],
    cases eq_or_ne (âˆ¥y -áµ¥ qâˆ¥) 0 with hyq hyq, { rwa [hyq, mul_zero], },
    rw [â† norm_pos_iff, norm_norm] at hyq,
    calc abs (Îµ / 2 / âˆ¥y -áµ¥ qâˆ¥) * âˆ¥y -áµ¥ qâˆ¥
          = Îµ / 2 / âˆ¥y -áµ¥ qâˆ¥ * âˆ¥y -áµ¥ qâˆ¥ : by rw [abs_div, abs_of_pos (half_pos hÎµ), abs_of_pos hyq]
      ... = Îµ / 2 : div_mul_cancel _ (ne_of_gt hyq)
      ... < Îµ : half_lt_self hÎµ, },
  have hÎµyq : âˆ€ (y âˆ‰ s), Îµ / 2 / dist y q â‰  0,
  { simp only [ne.def, div_eq_zero_iff, or_false, dist_eq_zero, bit0_eq_zero, one_ne_zero,
      not_or_distrib, ne_of_gt hÎµ, true_and, not_false_iff],
    finish, },
  classical,
  let w : t â†’ units â„ := Î» p, if hp : (p : P) âˆˆ s then 1 else units.mk0 _ (hÎµyq â†‘p hp),
  refine âŸ¨set.range (Î» (p : t), line_map q p (w p : â„)), _, _, _, _âŸ©,
  { intros p hp, use âŸ¨p, htâ‚ hpâŸ©, simp [w, hp], },
  { intros y hy,
    simp only [set.mem_range, set_coe.exists, subtype.coe_mk] at hy,
    obtain âŸ¨p, hp, hyqâŸ© := hy,
    by_cases hps : p âˆˆ s;
    simp only [w, hps, line_map_apply_one, units.coe_mk0, dif_neg, dif_pos, not_false_iff,
      units.coe_one, subtype.coe_mk] at hyq;
    rw â† hyq;
    [exact hsu hps, exact hf p], },
  { exact (htâ‚‚.units_line_map âŸ¨q, htâ‚ hqâŸ© w).range, },
  { rw [affine_span_eq_affine_span_line_map_units (htâ‚ hq) w, htâ‚ƒ], },
end

lemma interior_convex_hull_nonempty_iff_aff_span_eq_top [finite_dimensional â„ V] {s : set V} :
  (interior (convex_hull â„ s)).nonempty â†” affine_span â„ s = âŠ¤ :=
begin
  split,
  { rintros âŸ¨x, hxâŸ©,
    obtain âŸ¨u, huâ‚, huâ‚‚, huâ‚ƒâŸ© := mem_interior.mp hx,
    let t : set V := {x},
    obtain âŸ¨b, hbâ‚, hbâ‚‚, hbâ‚ƒ, hbâ‚„âŸ© := exists_subset_affine_independent_span_eq_top_of_open huâ‚‚
      (singleton_subset_iff.mpr huâ‚ƒ) (singleton_nonempty x)
      (affine_independent_of_subsingleton â„ (coe : t â†’ V)),
    rw [eq_top_iff, â† hbâ‚„, â† affine_span_convex_hull s],
    mono,
    exact hbâ‚‚.trans huâ‚, },
  { intros h,
    obtain âŸ¨t, hts, h_tot, h_indâŸ© := exists_affine_independent â„ V s,
    suffices : (interior (convex_hull â„ (range (coe : t â†’ V)))).nonempty,
    { rw [subtype.range_coe_subtype, set_of_mem_eq] at this,
      apply nonempty.mono _ this, 
      mono* },
    haveI : fintype t := fintype_of_fin_dim_affine_independent â„ h_ind,
    use finset.centroid â„ (finset.univ : finset t) (coe : t â†’ V),
    rw [h, â† @set_of_mem_eq V t, â† subtype.range_coe_subtype] at h_tot,
    rw interior_convex_hull_aff_basis h_ind h_tot,
    have htne : (finset.univ : finset t).nonempty,
    { simpa [finset.univ_nonempty_iff] using
        affine_subspace.nonempty_of_affine_span_eq_top â„ V V h_tot, },
    simp [finset.centroid_def,
      barycentric_coord_apply_combination_of_mem h_ind h_tot (finset.mem_univ _)
      (finset.sum_centroid_weights_eq_one_of_nonempty â„ (finset.univ : finset t) htne),
      finset.centroid_weights_apply, nat.cast_pos, inv_pos, finset.card_pos.mpr htne], },
end
