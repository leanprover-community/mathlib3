/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import analysis.normed_space.finite_dimension
import analysis.calculus.affine_map
import analysis.convex.combination
import linear_algebra.affine_space.finite_dimensional

/-!
# Bases in normed affine spaces.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains results about bases in normed affine spaces.

## Main definitions:

 * `continuous_barycentric_coord`
 * `is_open_map_barycentric_coord`
 * `affine_basis.interior_convex_hull`
 * `exists_subset_affine_independent_span_eq_top_of_open`
 * `interior_convex_hull_nonempty_iff_affine_span_eq_top`
-/

section barycentric

variables {Î¹ ğ•œ E P : Type*} [nontrivially_normed_field ğ•œ] [complete_space ğ•œ]
variables [normed_add_comm_group E] [normed_space ğ•œ E]
variables [metric_space P] [normed_add_torsor E P]

include E

lemma is_open_map_barycentric_coord [nontrivial Î¹] (b : affine_basis Î¹ ğ•œ P) (i : Î¹) :
  is_open_map (b.coord i) :=
affine_map.is_open_map_linear_iff.mp $ (b.coord i).linear.is_open_map_of_finite_dimensional $
  (b.coord i).linear_surjective_iff.mpr (b.surjective_coord i)

variables [finite_dimensional ğ•œ E] (b : affine_basis Î¹ ğ•œ P)

@[continuity]
lemma continuous_barycentric_coord (i : Î¹) : continuous (b.coord i) :=
(b.coord i).continuous_of_finite_dimensional

lemma smooth_barycentric_coord (b : affine_basis Î¹ ğ•œ E) (i : Î¹) : cont_diff ğ•œ âŠ¤ (b.coord i) :=
(âŸ¨b.coord i, continuous_barycentric_coord b iâŸ© : E â†’A[ğ•œ] ğ•œ).cont_diff

end barycentric

open set

/-- Given a finite-dimensional normed real vector space, the interior of the convex hull of an
affine basis is the set of points whose barycentric coordinates are strictly positive with respect
to this basis.

TODO Restate this result for affine spaces (instead of vector spaces) once the definition of
convexity is generalised to this setting. -/
lemma affine_basis.interior_convex_hull {Î¹ E : Type*} [finite Î¹] [normed_add_comm_group E]
  [normed_space â„ E] (b : affine_basis Î¹ â„ E) :
  interior (convex_hull â„ (range b)) = {x | âˆ€ i, 0 < b.coord i x} :=
begin
  casesI subsingleton_or_nontrivial Î¹,
  { -- The zero-dimensional case.
    have : range b = univ,
      from affine_subspace.eq_univ_of_subsingleton_span_eq_top (subsingleton_range _) b.tot,
    simp [this] },
  { -- The positive-dimensional case.
    haveI : finite_dimensional â„ E := b.finite_dimensional,
    have : convex_hull â„ (range b) = â‹‚ i, (b.coord i)â»Â¹' Ici 0,
    { rw [b.convex_hull_eq_nonneg_coord, set_of_forall], refl },
    ext,
    simp only [this, interior_Inter, â† is_open_map.preimage_interior_eq_interior_preimage
      (is_open_map_barycentric_coord b _) (continuous_barycentric_coord b _),
      interior_Ici, mem_Inter, mem_set_of_eq, mem_Ioi, mem_preimage], },
end

variables {V P : Type*} [normed_add_comm_group V] [normed_space â„ V] [metric_space P]
  [normed_add_torsor V P]
include V

open affine_map

/-- Given a set `s` of affine-independent points belonging to an open set `u`, we may extend `s` to
an affine basis, all of whose elements belong to `u`. -/
lemma is_open.exists_between_affine_independent_span_eq_top {s u : set P} (hu : is_open u)
  (hsu : s âŠ† u) (hne : s.nonempty) (h : affine_independent â„ (coe : s â†’ P)) :
  âˆƒ t : set P, s âŠ† t âˆ§ t âŠ† u âˆ§ affine_independent â„ (coe : t â†’ P) âˆ§ affine_span â„ t = âŠ¤ :=
begin
  obtain âŸ¨q, hqâŸ© := hne,
  obtain âŸ¨Îµ, Îµ0, hÎµuâŸ© := metric.nhds_basis_closed_ball.mem_iff.1 (hu.mem_nhds $ hsu hq),
  obtain âŸ¨t, htâ‚, htâ‚‚, htâ‚ƒâŸ© := exists_subset_affine_independent_affine_span_eq_top h,
  let f : P â†’ P := Î» y, line_map q y (Îµ / dist y q),
  have hf : âˆ€ y, f y âˆˆ u,
  { refine Î» y, hÎµu _,
    simp only [f],
    rw [metric.mem_closed_ball, line_map_apply, dist_vadd_left, norm_smul, real.norm_eq_abs,
      dist_eq_norm_vsub V y q, abs_div, abs_of_pos Îµ0, abs_of_nonneg (norm_nonneg _), div_mul_comm],
    exact mul_le_of_le_one_left Îµ0.le (div_self_le_one _) },
  have hÎµyq : âˆ€ y âˆ‰ s, Îµ / dist y q â‰  0,
    from Î» y hy, div_ne_zero Îµ0.ne' (dist_ne_zero.2 (ne_of_mem_of_not_mem hq hy).symm),
  classical,
  let w : t â†’ â„Ë£ := Î» p, if hp : (p : P) âˆˆ s then 1 else units.mk0 _ (hÎµyq â†‘p hp),
  refine âŸ¨set.range (Î» (p : t), line_map q p (w p : â„)), _, _, _, _âŸ©,
  { intros p hp, use âŸ¨p, htâ‚ hpâŸ©, simp [w, hp], },
  { rintros y âŸ¨âŸ¨p, hpâŸ©, rflâŸ©,
    by_cases hps : p âˆˆ s; simp only [w, hps, line_map_apply_one, units.coe_mk0, dif_neg, dif_pos,
      not_false_iff, units.coe_one, subtype.coe_mk];
      [exact hsu hps, exact hf p], },
  { exact (htâ‚‚.units_line_map âŸ¨q, htâ‚ hqâŸ© w).range, },
  { rw [affine_span_eq_affine_span_line_map_units (htâ‚ hq) w, htâ‚ƒ], },
end

lemma is_open.exists_subset_affine_independent_span_eq_top {u : set P} (hu : is_open u)
  (hne : u.nonempty) :
  âˆƒ s âŠ† u, affine_independent â„ (coe : s â†’ P) âˆ§ affine_span â„ s = âŠ¤ :=
begin
  rcases hne with âŸ¨x, hxâŸ©,
  rcases hu.exists_between_affine_independent_span_eq_top (singleton_subset_iff.mpr hx)
    (singleton_nonempty _) (affine_independent_of_subsingleton _ _) with âŸ¨s, -, hsu, hsâŸ©,
  exact âŸ¨s, hsu, hsâŸ©
end

/-- The affine span of a nonempty open set is `âŠ¤`. -/
lemma is_open.affine_span_eq_top {u : set P} (hu : is_open u) (hne : u.nonempty) :
  affine_span â„ u = âŠ¤ :=
let âŸ¨s, hsu, hs, hs'âŸ© := hu.exists_subset_affine_independent_span_eq_top hne
in top_unique $ hs' â–¸ affine_span_mono _ hsu

lemma affine_span_eq_top_of_nonempty_interior {s : set V}
  (hs : (interior $ convex_hull â„ s).nonempty) :
  affine_span â„ s = âŠ¤ :=
top_unique $ is_open_interior.affine_span_eq_top hs â–¸
  (affine_span_mono _ interior_subset).trans_eq (affine_span_convex_hull _)

lemma affine_basis.centroid_mem_interior_convex_hull {Î¹} [fintype Î¹] (b : affine_basis Î¹ â„ V) :
  finset.univ.centroid â„ b âˆˆ interior (convex_hull â„ (range b)) :=
begin
  haveI := b.nonempty,
  simp only [b.interior_convex_hull, mem_set_of_eq, b.coord_apply_centroid (finset.mem_univ _),
    inv_pos, nat.cast_pos, finset.card_pos, finset.univ_nonempty, forall_true_iff]
end

lemma interior_convex_hull_nonempty_iff_affine_span_eq_top [finite_dimensional â„ V] {s : set V} :
  (interior (convex_hull â„ s)).nonempty â†” affine_span â„ s = âŠ¤ :=
begin
  refine âŸ¨affine_span_eq_top_of_nonempty_interior, Î» h, _âŸ©,
  obtain âŸ¨t, hts, b, hbâŸ© := affine_basis.exists_affine_subbasis h,
  suffices : (interior (convex_hull â„ (range b))).nonempty,
  { rw [hb, subtype.range_coe_subtype, set_of_mem_eq] at this,
    refine this.mono _,
    mono* },
  lift t to finset V using b.finite_set,
  exact âŸ¨_, b.centroid_mem_interior_convex_hullâŸ©
end

lemma convex.interior_nonempty_iff_affine_span_eq_top [finite_dimensional â„ V] {s : set V}
  (hs : convex â„ s) : (interior s).nonempty â†” affine_span â„ s = âŠ¤ :=
by rw [â† interior_convex_hull_nonempty_iff_affine_span_eq_top, hs.convex_hull_eq]
