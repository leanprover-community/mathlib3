/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import analysis.normed_space.add_torsor
import analysis.normed_space.linear_isometry
import linear_algebra.affine_space.affine_subspace

/-!
# Affine isometries

In this file we define `affine_isometry ğ•œ P Pâ‚‚` to be an affine isometric embedding of normed
add-torsors `P` into `Pâ‚‚` over normed `ğ•œ`-spaces and `affine_isometry_equiv` to be an affine
isometric equivalence between `P` and `Pâ‚‚`.

We also prove basic lemmas and provide convenience constructors.  The choice of these lemmas and
constructors is closely modelled on those for the `linear_isometry` and `affine_map` theories.

Since many elementary properties don't require `âˆ¥xâˆ¥ = 0 â†’ x = 0` we initially set up the theory for
`semi_normed_add_torsor` and specialize to `normed_add_torsor` only when needed.

## Notation

We introduce the notation `P â†’áµƒâ±[ğ•œ] Pâ‚‚` for `affine_isometry ğ•œ P Pâ‚‚`, and `P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚` for
`affine_isometry_equiv ğ•œ P Pâ‚‚`.  In contrast with the notation `â†’â‚—áµ¢` for linear isometries, `â‰ƒáµ¢`
for isometric equivalences, etc., the "i" here is a superscript.  This is for aesthetic reasons to
match the superscript "a" (note that in mathlib `â†’áµƒ` is an affine map, since `â†’â‚` has been taken by
algebra-homomorphisms.)

-/
open function set

variables (ğ•œ : Type*) {V Vâ‚ Vâ‚‚ Vâ‚ƒ Vâ‚„ : Type*} {Pâ‚ : Type*} (P Pâ‚‚ : Type*) {Pâ‚ƒ Pâ‚„ : Type*}
    [normed_field ğ•œ]
  [semi_normed_group V] [normed_group Vâ‚] [semi_normed_group Vâ‚‚] [semi_normed_group Vâ‚ƒ]
    [semi_normed_group Vâ‚„]
  [semi_normed_space ğ•œ V] [normed_space ğ•œ Vâ‚] [semi_normed_space ğ•œ Vâ‚‚] [semi_normed_space ğ•œ Vâ‚ƒ]
    [semi_normed_space ğ•œ Vâ‚„]
  [pseudo_metric_space P] [metric_space Pâ‚] [pseudo_metric_space Pâ‚‚] [pseudo_metric_space Pâ‚ƒ]
    [pseudo_metric_space Pâ‚„]
  [semi_normed_add_torsor V P] [normed_add_torsor Vâ‚ Pâ‚] [semi_normed_add_torsor Vâ‚‚ Pâ‚‚]
    [semi_normed_add_torsor Vâ‚ƒ Pâ‚ƒ] [semi_normed_add_torsor Vâ‚„ Pâ‚„]

include V Vâ‚‚

/-- An `ğ•œ`-affine isometric embedding of one normed add-torsor over a normed `ğ•œ`-space into
another. -/
structure affine_isometry extends P â†’áµƒ[ğ•œ] Pâ‚‚ :=
(norm_map : âˆ€ x : V, âˆ¥linear xâˆ¥ = âˆ¥xâˆ¥)

omit V Vâ‚‚
variables {ğ•œ P Pâ‚‚}

-- `â†’áµƒáµ¢` would be more consistent with the linear isometry notation, but it is uglier
notation P ` â†’áµƒâ±[`:25 ğ•œ:25 `] `:0 Pâ‚‚:0 := affine_isometry ğ•œ P Pâ‚‚

namespace affine_isometry
variables (f : P â†’áµƒâ±[ğ•œ] Pâ‚‚)

/-- The underlying linear map of an affine isometry is in fact a linear isometry. -/
protected def linear_isometry : V â†’â‚—áµ¢[ğ•œ] Vâ‚‚ :=
{ norm_map' := f.norm_map,
  .. f.linear }

@[simp] lemma linear_eq_linear_isometry : f.linear = f.linear_isometry.to_linear_map :=
by { ext, refl }

include V Vâ‚‚
instance : has_coe_to_fun (P â†’áµƒâ±[ğ•œ] Pâ‚‚) := âŸ¨_, Î» f, f.to_funâŸ©
omit V Vâ‚‚

@[simp] lemma coe_to_affine_map : â‡‘f.to_affine_map = f := rfl

include V Vâ‚‚
lemma to_affine_map_injective : injective (to_affine_map : (P â†’áµƒâ±[ğ•œ] Pâ‚‚) â†’ (P â†’áµƒ[ğ•œ] Pâ‚‚))
| âŸ¨f, _âŸ© âŸ¨g, _âŸ© rfl := rfl

lemma coe_fn_injective : @injective (P â†’áµƒâ±[ğ•œ] Pâ‚‚) (P â†’ Pâ‚‚) coe_fn :=
affine_map.coe_fn_injective.comp to_affine_map_injective

@[ext] lemma ext {f g : P â†’áµƒâ±[ğ•œ] Pâ‚‚} (h : âˆ€ x, f x = g x) : f = g :=
coe_fn_injective $ funext h
omit V Vâ‚‚

end affine_isometry

namespace linear_isometry
variables (f : V â†’â‚—áµ¢[ğ•œ] Vâ‚‚)

/-- Reinterpret a linear isometry as an affine isometry. -/
def to_affine_isometry : V â†’áµƒâ±[ğ•œ] Vâ‚‚ :=
{ norm_map := f.norm_map,
  .. f.to_linear_map.to_affine_map }

@[simp] lemma coe_to_affine_isometry : â‡‘(f.to_affine_isometry : V â†’áµƒâ±[ğ•œ] Vâ‚‚) = f := rfl

@[simp] lemma to_affine_isometry_linear_isometry : f.to_affine_isometry.linear_isometry = f :=
by { ext, refl }

-- somewhat arbitrary choice of simp direction
@[simp] lemma to_affine_isometry_to_affine_map :
  f.to_affine_isometry.to_affine_map = f.to_linear_map.to_affine_map :=
rfl

end linear_isometry

namespace affine_isometry

/-- We use `fâ‚` when we need the domain to be a `normed_space`. -/
variables (f : P â†’áµƒâ±[ğ•œ] Pâ‚‚) (fâ‚ : Pâ‚ â†’áµƒâ±[ğ•œ] Pâ‚‚)

@[simp] lemma map_vadd (p : P) (v : V) : f (v +áµ¥ p) = f.linear_isometry v +áµ¥ f p :=
f.to_affine_map.map_vadd p v

@[simp] lemma map_vsub (p1 p2 : P) : f.linear_isometry (p1 -áµ¥ p2) = f p1 -áµ¥ f p2 :=
f.to_affine_map.linear_map_vsub p1 p2

@[simp] lemma dist_map (x y : P) : dist (f x) (f y) = dist x y :=
by rw [dist_eq_norm_vsub Vâ‚‚, dist_eq_norm_vsub V, â† map_vsub, f.linear_isometry.norm_map]

@[simp] lemma nndist_map (x y : P) : nndist (f x) (f y) = nndist x y := by simp [nndist_dist]

@[simp] lemma edist_map (x y : P) : edist (f x) (f y) = edist x y := by simp [edist_dist]

protected lemma isometry : isometry f := f.edist_map

protected lemma injective : injective fâ‚ := fâ‚.isometry.injective

@[simp] lemma map_eq_iff {x y : Pâ‚} : fâ‚ x = fâ‚ y â†” x = y := fâ‚.injective.eq_iff

lemma map_ne {x y : Pâ‚} (h : x â‰  y) : fâ‚ x â‰  fâ‚ y := fâ‚.injective.ne h

protected lemma lipschitz : lipschitz_with 1 f := f.isometry.lipschitz

protected lemma antilipschitz : antilipschitz_with 1 f := f.isometry.antilipschitz

@[continuity] protected lemma continuous : continuous f := f.isometry.continuous

lemma ediam_image (s : set P) : emetric.diam (f '' s) = emetric.diam s :=
f.isometry.ediam_image s

lemma ediam_range : emetric.diam (range f) = emetric.diam (univ : set P) :=
f.isometry.ediam_range

lemma diam_image (s : set P) : metric.diam (f '' s) = metric.diam s :=
f.isometry.diam_image s

lemma diam_range : metric.diam (range f) = metric.diam (univ : set P) :=
f.isometry.diam_range

@[simp] lemma comp_continuous_iff {Î± : Type*} [topological_space Î±] {g : Î± â†’ P} :
  continuous (f âˆ˜ g) â†” continuous g :=
f.isometry.comp_continuous_iff

include V
/-- The identity affine isometry. -/
def id : P â†’áµƒâ±[ğ•œ] P := âŸ¨affine_map.id ğ•œ P, Î» x, rflâŸ©

@[simp] lemma coe_id : â‡‘(id : P â†’áµƒâ±[ğ•œ] P) = id := rfl

@[simp] lemma id_apply (x : P) : (affine_isometry.id : P â†’áµƒâ±[ğ•œ] P) x = x := rfl

@[simp] lemma id_to_affine_map : (id.to_affine_map : P â†’áµƒ[ğ•œ] P) = affine_map.id ğ•œ P := rfl

instance : inhabited (P â†’áµƒâ±[ğ•œ] P) := âŸ¨idâŸ©

include Vâ‚‚ Vâ‚ƒ
/-- Composition of affine isometries. -/
def comp (g : Pâ‚‚ â†’áµƒâ±[ğ•œ] Pâ‚ƒ) (f : P â†’áµƒâ±[ğ•œ] Pâ‚‚) : P â†’áµƒâ±[ğ•œ] Pâ‚ƒ :=
âŸ¨g.to_affine_map.comp f.to_affine_map, Î» x, (g.norm_map _).trans (f.norm_map _)âŸ©

@[simp] lemma coe_comp (g : Pâ‚‚ â†’áµƒâ±[ğ•œ] Pâ‚ƒ) (f : P â†’áµƒâ±[ğ•œ] Pâ‚‚) :
  â‡‘(g.comp f) = g âˆ˜ f :=
rfl

omit V Vâ‚‚ Vâ‚ƒ

@[simp] lemma id_comp : (id : Pâ‚‚ â†’áµƒâ±[ğ•œ] Pâ‚‚).comp f = f := ext $ Î» x, rfl

@[simp] lemma comp_id : f.comp id = f := ext $ Î» x, rfl

include V Vâ‚‚ Vâ‚ƒ Vâ‚„
lemma comp_assoc (f : Pâ‚ƒ â†’áµƒâ±[ğ•œ] Pâ‚„) (g : Pâ‚‚ â†’áµƒâ±[ğ•œ] Pâ‚ƒ) (h : P â†’áµƒâ±[ğ•œ] Pâ‚‚) :
  (f.comp g).comp h = f.comp (g.comp h) :=
rfl
omit Vâ‚‚ Vâ‚ƒ Vâ‚„

instance : monoid (P â†’áµƒâ±[ğ•œ] P) :=
{ one := id,
  mul := comp,
  mul_assoc := comp_assoc,
  one_mul := id_comp,
  mul_one := comp_id }

@[simp] lemma coe_one : â‡‘(1 : P â†’áµƒâ±[ğ•œ] P) = id := rfl
@[simp] lemma coe_mul (f g : P â†’áµƒâ±[ğ•œ] P) : â‡‘(f * g) = f âˆ˜ g := rfl

end affine_isometry

-- remark: by analogy with the `linear_isometry` file from which this is adapted, there should
-- follow here a section defining an "inclusion" affine isometry from `p : affine_subspace ğ•œ P`
-- into `P`; we omit this for now

variables (ğ•œ P Pâ‚‚)
include V Vâ‚‚

/-- A affine isometric equivalence between two normed vector spaces. -/
structure affine_isometry_equiv extends P â‰ƒáµƒ[ğ•œ] Pâ‚‚ :=
(norm_map : âˆ€ x, âˆ¥linear xâˆ¥ = âˆ¥xâˆ¥)

variables {ğ•œ P Pâ‚‚}
omit V Vâ‚‚

-- `â‰ƒáµƒáµ¢` would be more consistent with the linear isometry equiv notation, but it is uglier
notation P ` â‰ƒáµƒâ±[`:25 ğ•œ:25 `] `:0 Pâ‚‚:0 := affine_isometry_equiv ğ•œ P Pâ‚‚

namespace affine_isometry_equiv

variables (e : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚)

/-- The underlying linear equiv of an affine isometry equiv is in fact a linear isometry equiv. -/
protected def linear_isometry_equiv : V â‰ƒâ‚—áµ¢[ğ•œ] Vâ‚‚ :=
{ norm_map' := e.norm_map,
  .. e.linear }

@[simp] lemma linear_eq_linear_isometry : e.linear = e.linear_isometry_equiv.to_linear_equiv :=
by { ext, refl }

include V Vâ‚‚
instance : has_coe_to_fun (P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) := âŸ¨_, Î» f, f.to_funâŸ©

@[simp] lemma coe_mk (e : P â‰ƒáµƒ[ğ•œ] Pâ‚‚) (he : âˆ€ x, âˆ¥e.linear xâˆ¥ = âˆ¥xâˆ¥) :
  â‡‘(mk e he) = e :=
rfl

@[simp] lemma coe_to_affine_equiv (e : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) : â‡‘e.to_affine_equiv = e := rfl

lemma to_affine_equiv_injective : injective (to_affine_equiv : (P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) â†’ (P â‰ƒáµƒ[ğ•œ] Pâ‚‚))
| âŸ¨e, _âŸ© âŸ¨_, _âŸ© rfl := rfl

@[ext] lemma ext {e e' : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚} (h : âˆ€ x, e x = e' x) : e = e' :=
to_affine_equiv_injective $ affine_equiv.ext h
omit V Vâ‚‚

/-- Reinterpret a `affine_isometry_equiv` as a `affine_isometry`. -/
def to_affine_isometry : P â†’áµƒâ±[ğ•œ] Pâ‚‚ := âŸ¨e.1.to_affine_map, e.2âŸ©

@[simp] lemma coe_to_affine_isometry : â‡‘e.to_affine_isometry = e := rfl

/-- Construct an affine isometry equivalence by verifying the relation between the map and its
linear part at one base point. Namely, this function takes a map `e : Pâ‚ â†’ Pâ‚‚`, a linear isometry
equivalence `e' : Vâ‚ â‰ƒáµ¢â‚—[k] Vâ‚‚`, and a point `p` such that for any other point `p'` we have
`e p' = e' (p' -áµ¥ p) +áµ¥ e p`. -/
def mk' (e : Pâ‚ â†’ Pâ‚‚) (e' : Vâ‚ â‰ƒâ‚—áµ¢[ğ•œ] Vâ‚‚) (p : Pâ‚) (h : âˆ€ p' : Pâ‚, e p' = e' (p' -áµ¥ p) +áµ¥ e p) :
  Pâ‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚‚ :=
{ norm_map := e'.norm_map,
  .. affine_equiv.mk' e e'.to_linear_equiv p h }

@[simp] lemma coe_mk' (e : Pâ‚ â†’ Pâ‚‚) (e' : Vâ‚ â‰ƒâ‚—áµ¢[ğ•œ] Vâ‚‚) (p h) : â‡‘(mk' e e' p h) = e := rfl
@[simp] lemma linear_isometry_equiv_mk' (e : Pâ‚ â†’ Pâ‚‚) (e' : Vâ‚ â‰ƒâ‚—áµ¢[ğ•œ] Vâ‚‚) (p h) :
  (mk' e e' p h).linear_isometry_equiv = e' := by { ext, refl }

end affine_isometry_equiv

namespace linear_isometry_equiv
variables (e : V â‰ƒâ‚—áµ¢[ğ•œ] Vâ‚‚)

/-- Reinterpret a linear isometry equiv as an affine isometry equiv. -/
def to_affine_isometry_equiv  : V â‰ƒáµƒâ±[ğ•œ] Vâ‚‚ :=
{ norm_map := e.norm_map,
  .. e.to_linear_equiv.to_affine_equiv }

@[simp] lemma coe_to_affine_isometry_equiv : â‡‘(e.to_affine_isometry_equiv : V â‰ƒáµƒâ±[ğ•œ] Vâ‚‚) = e := rfl

@[simp] lemma to_affine_isometry_equiv_linear_isometry_equiv :
  e.to_affine_isometry_equiv.linear_isometry_equiv = e :=
by { ext, refl }

-- somewhat arbitrary choice of simp direction
@[simp] lemma to_affine_isometry_equiv_to_affine_equiv :
  e.to_affine_isometry_equiv.to_affine_equiv = e.to_linear_equiv.to_affine_equiv :=
rfl

-- somewhat arbitrary choice of simp direction
@[simp] lemma to_affine_isometry_equiv_to_affine_isometry :
  e.to_affine_isometry_equiv.to_affine_isometry = e.to_linear_isometry.to_affine_isometry :=
rfl

end linear_isometry_equiv

namespace affine_isometry_equiv
variables (e : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚)

protected lemma isometry : isometry e := e.to_affine_isometry.isometry

/-- Reinterpret a `affine_isometry_equiv` as an `isometric`. -/
def to_isometric : P â‰ƒáµ¢ Pâ‚‚ := âŸ¨e.to_affine_equiv.to_equiv, e.isometryâŸ©

@[simp] lemma coe_to_isometric : â‡‘e.to_isometric = e := rfl

include V Vâ‚‚
lemma range_eq_univ (e : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) : set.range e = set.univ :=
by { rw â† coe_to_isometric, exact isometric.range_eq_univ _, }
omit V Vâ‚‚

/-- Reinterpret a `affine_isometry_equiv` as an `homeomorph`. -/
def to_homeomorph : P â‰ƒâ‚œ Pâ‚‚ := e.to_isometric.to_homeomorph

@[simp] lemma coe_to_homeomorph : â‡‘e.to_homeomorph = e := rfl

protected lemma continuous : continuous e := e.isometry.continuous
protected lemma continuous_at {x} : continuous_at e x := e.continuous.continuous_at
protected lemma continuous_on {s} : continuous_on e s := e.continuous.continuous_on

protected lemma continuous_within_at {s x} : continuous_within_at e s x :=
e.continuous.continuous_within_at

variables (ğ•œ P)
include V

/-- Identity map as a `affine_isometry_equiv`. -/
def refl : P â‰ƒáµƒâ±[ğ•œ] P := âŸ¨affine_equiv.refl ğ•œ P, Î» x, rflâŸ©

variables {ğ•œ P}

instance : inhabited (P â‰ƒáµƒâ±[ğ•œ] P) := âŸ¨refl ğ•œ PâŸ©

@[simp] lemma coe_refl : â‡‘(refl ğ•œ P) = id := rfl
@[simp] lemma to_affine_equiv_refl : (refl ğ•œ P).to_affine_equiv = affine_equiv.refl ğ•œ P := rfl
@[simp] lemma to_isometric_refl : (refl ğ•œ P).to_isometric = isometric.refl P := rfl
@[simp] lemma to_homeomorph_refl : (refl ğ•œ P).to_homeomorph = homeomorph.refl P := rfl
omit V

/-- The inverse `affine_isometry_equiv`. -/
def symm : Pâ‚‚ â‰ƒáµƒâ±[ğ•œ] P :=
{ norm_map := e.linear_isometry_equiv.symm.norm_map,
  .. e.to_affine_equiv.symm }

@[simp] lemma apply_symm_apply (x : Pâ‚‚) : e (e.symm x) = x := e.to_affine_equiv.apply_symm_apply x
@[simp] lemma symm_apply_apply (x : P) : e.symm (e x) = x := e.to_affine_equiv.symm_apply_apply x
@[simp] lemma symm_symm : e.symm.symm = e := ext $ Î» x, rfl

@[simp] lemma to_affine_equiv_symm : e.to_affine_equiv.symm = e.symm.to_affine_equiv := rfl
@[simp] lemma to_isometric_symm : e.to_isometric.symm = e.symm.to_isometric := rfl
@[simp] lemma to_homeomorph_symm : e.to_homeomorph.symm = e.symm.to_homeomorph := rfl

include Vâ‚ƒ
/-- Composition of `affine_isometry_equiv`s as a `affine_isometry_equiv`. -/
def trans (e' : Pâ‚‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚ƒ) : P â‰ƒáµƒâ±[ğ•œ] Pâ‚ƒ :=
âŸ¨e.to_affine_equiv.trans e'.to_affine_equiv, Î» x, (e'.norm_map _).trans (e.norm_map _)âŸ©

include V Vâ‚‚
@[simp] lemma coe_trans (eâ‚ : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) (eâ‚‚ : Pâ‚‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚ƒ) : â‡‘(eâ‚.trans eâ‚‚) = eâ‚‚ âˆ˜ eâ‚ := rfl
omit V Vâ‚‚ Vâ‚ƒ

@[simp] lemma trans_refl : e.trans (refl ğ•œ Pâ‚‚) = e := ext $ Î» x, rfl
@[simp] lemma refl_trans : (refl ğ•œ P).trans e = e := ext $ Î» x, rfl
@[simp] lemma trans_symm : e.trans e.symm = refl ğ•œ P := ext e.symm_apply_apply
@[simp] lemma symm_trans : e.symm.trans e = refl ğ•œ Pâ‚‚ := ext e.apply_symm_apply

include V Vâ‚‚ Vâ‚ƒ
@[simp] lemma coe_symm_trans (eâ‚ : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) (eâ‚‚ : Pâ‚‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚ƒ) :
  â‡‘(eâ‚.trans eâ‚‚).symm = eâ‚.symm âˆ˜ eâ‚‚.symm :=
rfl

include Vâ‚„
lemma trans_assoc (ePPâ‚‚ : P â‰ƒáµƒâ±[ğ•œ] Pâ‚‚) (ePâ‚‚G : Pâ‚‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚ƒ) (eGG' : Pâ‚ƒ â‰ƒáµƒâ±[ğ•œ] Pâ‚„) :
  ePPâ‚‚.trans (ePâ‚‚G.trans eGG') = (ePPâ‚‚.trans ePâ‚‚G).trans eGG' :=
rfl
omit Vâ‚‚ Vâ‚ƒ Vâ‚„

/-- The group of affine isometries of a `normed_add_torsor`, `P`. -/
instance : group (P â‰ƒáµƒâ±[ğ•œ] P) :=
{ mul := Î» eâ‚ eâ‚‚, eâ‚‚.trans eâ‚,
  one := refl _ _,
  inv := symm,
  one_mul := trans_refl,
  mul_one := refl_trans,
  mul_assoc := Î» _ _ _, trans_assoc _ _ _,
  mul_left_inv := trans_symm }

@[simp] lemma coe_one : â‡‘(1 : P â‰ƒáµƒâ±[ğ•œ] P) = id := rfl
@[simp] lemma coe_mul (e e' : P â‰ƒáµƒâ±[ğ•œ] P) : â‡‘(e * e') = e âˆ˜ e' := rfl
@[simp] lemma coe_inv (e : P â‰ƒáµƒâ±[ğ•œ] P) : â‡‘(eâ»Â¹) = e.symm := rfl

omit V

@[simp] lemma map_vadd (p : P) (v : V) : e (v +áµ¥ p) = e.linear_isometry_equiv v +áµ¥ e p :=
e.to_affine_isometry.map_vadd p v

@[simp] lemma map_vsub (p1 p2 : P) : e.linear_isometry_equiv (p1 -áµ¥ p2) = e p1 -áµ¥ e p2 :=
e.to_affine_isometry.map_vsub p1 p2

@[simp] lemma dist_map (x y : P) : dist (e x) (e y) = dist x y :=
e.to_affine_isometry.dist_map x y

@[simp] lemma edist_map (x y : P) : edist (e x) (e y) = edist x y :=
e.to_affine_isometry.edist_map x y

protected lemma bijective : bijective e := e.1.bijective
protected lemma injective : injective e := e.1.injective
protected lemma surjective : surjective e := e.1.surjective

@[simp] lemma map_eq_iff {x y : P} : e x = e y â†” x = y := e.injective.eq_iff

lemma map_ne {x y : P} (h : x â‰  y) : e x â‰  e y := e.injective.ne h

protected lemma lipschitz : lipschitz_with 1 e := e.isometry.lipschitz

protected lemma antilipschitz : antilipschitz_with 1 e := e.isometry.antilipschitz

@[simp] lemma ediam_image (s : set P) : emetric.diam (e '' s) = emetric.diam s :=
e.isometry.ediam_image s

@[simp] lemma diam_image (s : set P) : metric.diam (e '' s) = metric.diam s :=
e.isometry.diam_image s

variables {Î± : Type*} [topological_space Î±]

@[simp] lemma comp_continuous_on_iff {f : Î± â†’ P} {s : set Î±} :
  continuous_on (e âˆ˜ f) s â†” continuous_on f s :=
e.isometry.comp_continuous_on_iff

@[simp] lemma comp_continuous_iff {f : Î± â†’ P} :
  continuous (e âˆ˜ f) â†” continuous f :=
e.isometry.comp_continuous_iff

section constructions

variables (ğ•œ)
/-- The map `v â†¦ v +áµ¥ p` as an affine isometric equivalence between `V` and `P`. -/
def vadd_const (p : P) : V â‰ƒáµƒâ±[ğ•œ] P :=
{ norm_map := Î» x, rfl,
  .. affine_equiv.vadd_const ğ•œ p }
variables {ğ•œ}

include V
@[simp] lemma coe_vadd_const (p : P) : â‡‘(vadd_const ğ•œ p) = Î» v, v +áµ¥ p := rfl

@[simp] lemma coe_vadd_const_symm (p : P) : â‡‘(vadd_const ğ•œ p).symm = Î» p', p' -áµ¥ p :=
rfl

@[simp] lemma vadd_const_to_affine_equiv (p : P) :
  (vadd_const ğ•œ p).to_affine_equiv = affine_equiv.vadd_const ğ•œ p :=
rfl
omit V

variables (ğ•œ)
/-- `p' â†¦ p -áµ¥ p'` as an affine isometric equivalence. -/
def const_vsub (p : P) : P â‰ƒáµƒâ±[ğ•œ] V :=
{ norm_map := norm_neg,
  .. affine_equiv.const_vsub ğ•œ p }
variables {ğ•œ}

include V
@[simp] lemma coe_const_vsub (p : P) : â‡‘(const_vsub ğ•œ p) = (-áµ¥) p := rfl

@[simp] lemma symm_const_vsub (p : P) :
  (const_vsub ğ•œ p).symm
  = (linear_isometry_equiv.neg ğ•œ).to_affine_isometry_equiv.trans (vadd_const ğ•œ p) :=
by { ext, refl }
omit V

variables (ğ•œ P)
/-- Translation by `v` (that is, the map `p â†¦ v +áµ¥ p`) as an affine isometric automorphism of `P`.
-/
def const_vadd (v : V) : P â‰ƒáµƒâ±[ğ•œ] P :=
{ norm_map := Î» x, rfl,
  .. affine_equiv.const_vadd ğ•œ P v }
variables {ğ•œ P}

@[simp] lemma coe_const_vadd (v : V) : â‡‘(const_vadd ğ•œ P v : P â‰ƒáµƒâ±[ğ•œ] P) = (+áµ¥) v := rfl

@[simp] lemma const_vadd_zero : const_vadd ğ•œ P (0:V) = refl ğ•œ P := ext $ zero_vadd V

include ğ•œ V
/-- The map `g` from `V` to `Vâ‚‚` corresponding to a map `f` from `P` to `Pâ‚‚`, at a base point `p`,
is an isometry if `f` is one. -/
lemma vadd_vsub {f : P â†’ Pâ‚‚} (hf : isometry f) {p : P} {g : V â†’ Vâ‚‚}
  (hg : âˆ€ v, g v = f (v +áµ¥ p) -áµ¥ f p) : isometry g :=
begin
  convert (vadd_const ğ•œ (f p)).symm.isometry.comp (hf.comp (vadd_const ğ•œ p).isometry),
  exact funext hg
end
omit ğ•œ

variables (ğ•œ)
/-- Point reflection in `x` as an affine isometric automorphism. -/
def point_reflection (x : P) : P â‰ƒáµƒâ±[ğ•œ] P := (const_vsub ğ•œ x).trans (vadd_const ğ•œ x)
variables {ğ•œ}

lemma point_reflection_apply (x y : P) : (point_reflection ğ•œ x) y = x -áµ¥ y +áµ¥ x := rfl

@[simp] lemma point_reflection_to_affine_equiv (x : P) :
  (point_reflection ğ•œ x).to_affine_equiv = affine_equiv.point_reflection ğ•œ x := rfl

@[simp] lemma point_reflection_self (x : P) : point_reflection ğ•œ x x = x :=
affine_equiv.point_reflection_self ğ•œ x

lemma point_reflection_involutive (x : P) : function.involutive (point_reflection ğ•œ x) :=
equiv.point_reflection_involutive x

@[simp] lemma point_reflection_symm (x : P) : (point_reflection ğ•œ x).symm = point_reflection ğ•œ x :=
to_affine_equiv_injective $ affine_equiv.point_reflection_symm ğ•œ x

@[simp] lemma dist_point_reflection_fixed (x y : P) :
  dist (point_reflection ğ•œ x y) x = dist y x :=
by rw [â† (point_reflection ğ•œ x).dist_map y x, point_reflection_self]

lemma dist_point_reflection_self' (x y : P) :
  dist (point_reflection ğ•œ x y) y = âˆ¥bit0 (x -áµ¥ y)âˆ¥ :=
by rw [point_reflection_apply, dist_eq_norm_vsub V, vadd_vsub_assoc, bit0]

lemma dist_point_reflection_self (x y : P) :
  dist (point_reflection ğ•œ x y) y = âˆ¥(2:ğ•œ)âˆ¥ * dist x y :=
by rw [dist_point_reflection_self', â† two_smul' ğ•œ (x -áµ¥ y), norm_smul, â† dist_eq_norm_vsub V]

lemma point_reflection_fixed_iff [invertible (2:ğ•œ)] {x y : P} :
  point_reflection ğ•œ x y = y â†” y = x :=
affine_equiv.point_reflection_fixed_iff_of_module ğ•œ

variables [semi_normed_space â„ V]

lemma dist_point_reflection_self_real (x y : P) :
  dist (point_reflection â„ x y) y = 2 * dist x y :=
by { rw [dist_point_reflection_self, real.norm_two] }

@[simp] lemma point_reflection_midpoint_left (x y : P) :
  point_reflection â„ (midpoint â„ x y) x = y :=
affine_equiv.point_reflection_midpoint_left x y

@[simp] lemma point_reflection_midpoint_right (x y : P) :
  point_reflection â„ (midpoint â„ x y) y = x :=
affine_equiv.point_reflection_midpoint_right x y

end constructions

end affine_isometry_equiv

namespace affine_isometry

open finite_dimensional affine_map

variables [finite_dimensional ğ•œ Vâ‚] [finite_dimensional ğ•œ Vâ‚‚]

/-- A affine isometry between finite dimensional spaces of equal dimension can be upgraded
    to an affine isometry equivalence. -/
noncomputable def to_affine_isometry_equiv [inhabited Pâ‚]
  (li : Pâ‚ â†’áµƒâ±[ğ•œ] Pâ‚‚) (h : finrank ğ•œ Vâ‚ = finrank ğ•œ Vâ‚‚) : Pâ‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚‚ :=
affine_isometry_equiv.mk' li (li.linear_isometry.to_linear_isometry_equiv h) (arbitrary Pâ‚)
  (Î» p, by simp)

@[simp] lemma coe_to_affine_isometry_equiv [inhabited Pâ‚]
  (li : Pâ‚ â†’áµƒâ±[ğ•œ] Pâ‚‚) (h : finrank ğ•œ Vâ‚ = finrank ğ•œ Vâ‚‚) :
  (li.to_affine_isometry_equiv h : Pâ‚ â†’ Pâ‚‚) = li := rfl

@[simp] lemma to_affine_isometry_equiv_apply [inhabited Pâ‚]
  (li : Pâ‚ â†’áµƒâ±[ğ•œ] Pâ‚‚) (h : finrank ğ•œ Vâ‚ = finrank ğ•œ Vâ‚‚) (x : Pâ‚) :
  (li.to_affine_isometry_equiv h) x = li x := rfl

end affine_isometry

include V Vâ‚‚
/-- If `f` is an affine map, then its linear part is continuous iff `f` is continuous. -/
lemma affine_map.continuous_linear_iff {f : P â†’áµƒ[ğ•œ] Pâ‚‚} :
  continuous f.linear â†” continuous f :=
begin
  inhabit P,
  have : (f.linear : V â†’ Vâ‚‚) =
    (affine_isometry_equiv.vadd_const ğ•œ $ f $ default P).to_homeomorph.symm âˆ˜ f âˆ˜
      (affine_isometry_equiv.vadd_const ğ•œ $ default P).to_homeomorph,
  { ext v, simp },
  rw this,
  simp only [homeomorph.comp_continuous_iff, homeomorph.comp_continuous_iff'],
end
