/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel

Banach spaces, i.e., complete vector spaces.

This file contains the Banach open mapping theorem, i.e., the fact that a bijective
bounded linear map between Banach spaces has a bounded inverse.
-/

import topology.metric_space.baire analysis.normed_space.bounded_linear_maps

open function metric set filter finset
open_locale classical

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [complete_space E] [normed_space ğ•œ E]
{F : Type*} [normed_group F] [complete_space F] [normed_space ğ•œ F]
{f : E â†’ F}
include ğ•œ

set_option class.instance_max_depth 70

/-- The Banach open mapping theorem: if a bounded linear map between Banach spaces is onto, then
any point has a preimage with controlled norm. -/
theorem exists_preimage_norm_le (hf : is_bounded_linear_map ğ•œ f) (surj : surjective f) :
  âˆƒC, 0 < C âˆ§ âˆ€y, âˆƒx, f x = y âˆ§ âˆ¥xâˆ¥ â‰¤ C * âˆ¥yâˆ¥ :=
begin
  have lin := hf.to_is_linear_map,
  haveI : nonempty F := âŸ¨0âŸ©,
  /- First step of the proof (using completeness of `F`): by Baire's theorem, there exists a
  ball in E whose image closure has nonempty interior. Rescaling everything, it follows that
  any `y âˆˆ F` is arbitrarily well approached by images of elements of norm at
  most `C * âˆ¥yâˆ¥`. For further use, we will only need such an element whose image
  is within distance âˆ¥yâˆ¥/2 of y, to apply an iterative process. -/
  have A : (â‹ƒn:â„•, closure (f '' (ball 0 n))) = univ,
  { refine subset.antisymm (subset_univ _) (Î»y hy, _),
    rcases surj y with âŸ¨x, hxâŸ©,
    rcases exists_nat_gt (âˆ¥xâˆ¥) with âŸ¨n, hnâŸ©,
    refine mem_Union.2 âŸ¨n, subset_closure _âŸ©,
    refine (mem_image _ _ _).2 âŸ¨x, âŸ¨_, hxâŸ©âŸ©,
    rwa [mem_ball, dist_eq_norm, sub_zero] },
  have : âˆƒ(n:â„•) y Îµ, 0 < Îµ âˆ§ ball y Îµ âŠ† closure (f '' (ball 0 n)) :=
    nonempty_interior_of_Union_of_closed (Î»n, is_closed_closure) A,
  have : âˆƒC, 0 â‰¤ C âˆ§ âˆ€y, âˆƒx, dist (f x) y â‰¤ (1/2) * âˆ¥yâˆ¥ âˆ§ âˆ¥xâˆ¥ â‰¤ C * âˆ¥yâˆ¥,
  { rcases this with âŸ¨n, a, Îµ, âŸ¨Îµpos, HâŸ©âŸ©,
    rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
    refine âŸ¨(Îµ/2)â»Â¹ * âˆ¥câˆ¥ * 2 * n, _, Î»y, _âŸ©,
    { refine mul_nonneg (mul_nonneg (mul_nonneg _ (norm_nonneg _)) (by norm_num)) _,
      refine inv_nonneg.2 (div_nonneg' (le_of_lt Îµpos) (by norm_num)),
      exact nat.cast_nonneg n },
    { by_cases hy : y = 0,
      { use 0, simp [hy, lin.map_zero] },
      { rcases rescale_to_shell hc (half_pos Îµpos) hy with âŸ¨d, hd, ydle, leyd, dinvâŸ©,
        let Î´ := âˆ¥dâˆ¥ * âˆ¥yâˆ¥/4,
        have Î´pos : 0 < Î´ :=
          div_pos (mul_pos ((norm_pos_iff _).2 hd) ((norm_pos_iff _).2 hy)) (by norm_num),
        have : a + d â€¢ y âˆˆ ball a Îµ,
          by simp [dist_eq_norm, lt_of_le_of_lt ydle (half_lt_self Îµpos)],
        rcases mem_closure_iff'.1 (H this) _ Î´pos with âŸ¨zâ‚, zâ‚im, hâ‚âŸ©,
        rcases (mem_image _ _ _).1 zâ‚im with âŸ¨xâ‚, hxâ‚, xzâ‚âŸ©,
        rw â† xzâ‚ at hâ‚,
        rw [mem_ball, dist_eq_norm, sub_zero] at hxâ‚,
        have : a âˆˆ ball a Îµ, by { simp, exact Îµpos },
        rcases mem_closure_iff'.1 (H this) _ Î´pos with âŸ¨zâ‚‚, zâ‚‚im, hâ‚‚âŸ©,
        rcases (mem_image _ _ _).1 zâ‚‚im with âŸ¨xâ‚‚, hxâ‚‚, xzâ‚‚âŸ©,
        rw â† xzâ‚‚ at hâ‚‚,
        rw [mem_ball, dist_eq_norm, sub_zero] at hxâ‚‚,
        let x := xâ‚ - xâ‚‚,
        have I : âˆ¥f x - d â€¢ yâˆ¥ â‰¤ 2 * Î´ := calc
          âˆ¥f x - d â€¢ yâˆ¥ = âˆ¥f xâ‚ - (a + d â€¢ y) - (f xâ‚‚ - a)âˆ¥ :
            by { congr' 1, simp only [x, lin.map_sub], abel }
          ... â‰¤ âˆ¥f xâ‚ - (a + d â€¢ y)âˆ¥ + âˆ¥f xâ‚‚ - aâˆ¥ :
            norm_triangle_sub
          ... â‰¤ Î´ + Î´ : begin
              apply add_le_add,
              { rw [â† dist_eq_norm, dist_comm], exact le_of_lt hâ‚ },
              { rw [â† dist_eq_norm, dist_comm], exact le_of_lt hâ‚‚ }
            end
          ... = 2 * Î´ : (two_mul _).symm,
        have J : âˆ¥f (dâ»Â¹ â€¢ x) - yâˆ¥ â‰¤ 1/2 * âˆ¥yâˆ¥ := calc
          âˆ¥f (dâ»Â¹ â€¢ x) - yâˆ¥ = âˆ¥dâ»Â¹ â€¢ f x - (dâ»Â¹ * d) â€¢ yâˆ¥ :
            by rwa [lin.smul, inv_mul_cancel, one_smul]
          ... = âˆ¥dâ»Â¹ â€¢ (f x - d â€¢ y)âˆ¥ : by rw [mul_smul, smul_sub]
          ... = âˆ¥dâˆ¥â»Â¹ * âˆ¥f x - d â€¢ yâˆ¥ : by rw [norm_smul, normed_field.norm_inv]
          ... â‰¤ âˆ¥dâˆ¥â»Â¹ * (2 * Î´) : begin
              apply mul_le_mul_of_nonneg_left I,
              rw inv_nonneg,
              exact norm_nonneg _
            end
          ... = (âˆ¥dâˆ¥â»Â¹ * âˆ¥dâˆ¥) * âˆ¥yâˆ¥ /2 : by { simp only [Î´], ring }
          ... = âˆ¥yâˆ¥/2 : by { rw [inv_mul_cancel, one_mul],  simp [norm_eq_zero, hd] }
          ... = (1/2) * âˆ¥yâˆ¥ : by ring,
        rw â† dist_eq_norm at J,
        have ğ•œ : âˆ¥dâ»Â¹ â€¢ xâˆ¥ â‰¤ (Îµ / 2)â»Â¹ * âˆ¥câˆ¥ * 2 * â†‘n * âˆ¥yâˆ¥ := calc
          âˆ¥dâ»Â¹ â€¢ xâˆ¥ = âˆ¥dâˆ¥â»Â¹ * âˆ¥xâ‚ - xâ‚‚âˆ¥ : by rw [norm_smul, normed_field.norm_inv]
          ... â‰¤ ((Îµ / 2)â»Â¹ * âˆ¥câˆ¥ * âˆ¥yâˆ¥) * (n + n) : begin
              refine mul_le_mul dinv _ (norm_nonneg _) _,
              { exact le_trans (norm_triangle_sub) (add_le_add (le_of_lt hxâ‚) (le_of_lt hxâ‚‚)) },
              { apply mul_nonneg (mul_nonneg _ (norm_nonneg _)) (norm_nonneg _),
                exact inv_nonneg.2 (le_of_lt (half_pos Îµpos)) }
            end
          ... = (Îµ / 2)â»Â¹ * âˆ¥câˆ¥ * 2 * â†‘n * âˆ¥yâˆ¥ : by ring,
        exact âŸ¨dâ»Â¹ â€¢ x, J, ğ•œâŸ© } } },
  rcases this with âŸ¨C, C0, hCâŸ©,
  /- Second step of the proof: starting from `y`, we want an exact preimage of `y`. Let `g y` be
  the approximate preimage of `y` given by the first step, and `h y = y - f(g y)` the part that
  has no preimage yet. We will iterate this process, taking the approximate preimage of `h y`,
  leaving only `h^2 y` without preimage yet, and so on. Let `u n` be the approximate preimage
  of `h^n y`. Then `u` is a converging series, and by design the sum of the series is a
  preimage of `y`. This uses completeness of `E`. -/
  choose g hg using hC,
  let h := Î»y, y - f (g y),
  have hle : âˆ€y, âˆ¥h yâˆ¥ â‰¤ (1/2) * âˆ¥yâˆ¥,
  { assume y,
    rw [â† dist_eq_norm, dist_comm],
    exact (hg y).1 },
  refine âŸ¨2 * C + 1, by linarith, Î»y, _âŸ©,
  have hnle : âˆ€n:â„•, âˆ¥(h^[n]) yâˆ¥ â‰¤ (1/2)^n * âˆ¥yâˆ¥,
  { assume n,
    induction n with n IH,
    { simp only [one_div_eq_inv, nat.nat_zero_eq_zero, one_mul, nat.iterate_zero, pow_zero] },
    { rw [nat.iterate_succ'],
      apply le_trans (hle _) _,
      rw [pow_succ, mul_assoc],
      apply mul_le_mul_of_nonneg_left IH,
      norm_num } },
  let u := Î»n, g((h^[n]) y),
  have ule : âˆ€n, âˆ¥u nâˆ¥ â‰¤ (1/2)^n * (C * âˆ¥yâˆ¥),
  { assume n,
    apply le_trans (hg _).2 _,
    calc C * âˆ¥(h^[n]) yâˆ¥ â‰¤ C * ((1/2)^n * âˆ¥yâˆ¥) : mul_le_mul_of_nonneg_left (hnle n) C0
         ... = (1 / 2) ^ n * (C * âˆ¥yâˆ¥) : by ring },
  have sNu : summable (Î»n, âˆ¥u nâˆ¥),
  { refine summable_of_nonneg_of_le (Î»n, norm_nonneg _) ule _,
    exact summable_mul_right _ (summable_geometric (by norm_num) (by norm_num)) },
  have su : summable u := summable_of_summable_norm sNu,
  let x := tsum u,
  have x_ineq : âˆ¥xâˆ¥ â‰¤ (2 * C + 1) * âˆ¥yâˆ¥ := calc
    âˆ¥xâˆ¥ â‰¤ (âˆ‘n, âˆ¥u nâˆ¥) : norm_tsum_le_tsum_norm sNu
    ... â‰¤ (âˆ‘n, (1/2)^n * (C * âˆ¥yâˆ¥)) :
      tsum_le_tsum ule sNu (summable_mul_right _ summable_geometric_two)
    ... = (âˆ‘n, (1/2)^n) * (C * âˆ¥yâˆ¥) : by { rw tsum_mul_right, exact summable_geometric_two }
    ... = 2 * (C * âˆ¥yâˆ¥) : by rw tsum_geometric_two
    ... = 2 * C * âˆ¥yâˆ¥ + 0 : by rw [add_zero, mul_assoc]
    ... â‰¤ 2 * C * âˆ¥yâˆ¥ + âˆ¥yâˆ¥ : add_le_add (le_refl _) (norm_nonneg _)
    ... = (2 * C + 1) * âˆ¥yâˆ¥ : by ring,
  have fsumeq : âˆ€n:â„•, f((range n).sum u) = y - (h^[n]) y,
  { assume n,
    induction n with n IH,
    { simp [lin.map_zero] },
    { rw [sum_range_succ, lin.add, IH, nat.iterate_succ'],
      simp [u, h] } },
  have : tendsto (Î»n, (range n).sum u) at_top (nhds x) :=
    tendsto_sum_nat_of_has_sum (has_sum_tsum su),
  have Lâ‚ : tendsto (Î»n, f((range n).sum u)) at_top (nhds (f x)) :=
    tendsto.comp (hf.continuous.tendsto _) this,
  simp only [fsumeq] at Lâ‚,
  have Lâ‚‚ : tendsto (Î»n, y - (h^[n]) y) at_top (nhds (y - 0)),
  { refine tendsto_sub tendsto_const_nhds _,
    rw tendsto_iff_norm_tendsto_zero,
    simp only [sub_zero],
    refine squeeze_zero (Î»_, norm_nonneg _) hnle _,
    have : 0 = 0 * âˆ¥yâˆ¥, by rw zero_mul,
    rw this,
    refine tendsto_mul _ tendsto_const_nhds,
    exact tendsto_pow_at_top_nhds_0_of_lt_1 (by norm_num) (by norm_num) },
  have feq : f x = y - 0,
  { apply tendsto_nhds_unique _ Lâ‚ Lâ‚‚,
    simp },
  rw sub_zero at feq,
  exact âŸ¨x, feq, x_ineqâŸ©
end

/-- The Banach open mapping theorem: a surjective bounded linear map between Banach spaces is open. -/
theorem open_mapping (hf : is_bounded_linear_map ğ•œ f) (surj : surjective f) : is_open_map f :=
begin
  assume s hs,
  have lin := hf.to_is_linear_map,
  rcases exists_preimage_norm_le hf surj with âŸ¨C, Cpos, hCâŸ©,
  refine is_open_iff.2 (Î»y yfs, _),
  rcases mem_image_iff_bex.1 yfs with âŸ¨x, xs, fxyâŸ©,
  rcases is_open_iff.1 hs x xs with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  refine âŸ¨Îµ/C, div_pos Îµpos Cpos, Î»z hz, _âŸ©,
  rcases hC (z-y) with âŸ¨w, wim, wnormâŸ©,
  have : f (x + w) = z, by { rw [lin.add, wim, fxy], abel },
  rw â† this,
  have : x + w âˆˆ ball x Îµ := calc
    dist (x+w) x = âˆ¥wâˆ¥ : by { rw dist_eq_norm, simp }
    ... â‰¤ C * âˆ¥z - yâˆ¥ : wnorm
    ... < C * (Îµ/C) : begin
        apply mul_lt_mul_of_pos_left _ Cpos,
        rwa [mem_ball, dist_eq_norm] at hz,
      end
    ... = Îµ : mul_div_cancel' _ (ne_of_gt Cpos),
  exact set.mem_image_of_mem _ (hÎµ this)
end

/-- If a bounded linear map is a bijection, then its inverse is also a bounded linear map. -/
theorem linear_equiv.is_bounded_inv (e : linear_equiv ğ•œ E F) (h : is_bounded_linear_map ğ•œ e.to_fun) :
  is_bounded_linear_map ğ•œ e.inv_fun :=
{ bound := begin
    have : surjective e.to_fun := (equiv.bijective e.to_equiv).2,
    rcases exists_preimage_norm_le h this with âŸ¨M, Mpos, hMâŸ©,
    refine âŸ¨M, Mpos, Î»y, _âŸ©,
    rcases hM y with âŸ¨x, hx, xnormâŸ©,
    have : x = e.inv_fun y, by { rw â† hx, exact (e.symm_apply_apply _).symm },
    rwa â† this
  end,
  ..e.symm }
