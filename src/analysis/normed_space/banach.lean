/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.metric_space.baire
import analysis.normed_space.operator_norm
import analysis.normed_space.affine_isometry

/-!
# Banach open mapping theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains the Banach open mapping theorem, i.e., the fact that a bijective
bounded linear map between Banach spaces has a bounded inverse.
-/

open function metric set filter finset linear_map (range ker)
open_locale classical topology big_operators nnreal

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
(f : E â†’L[ğ•œ] F)
include ğ•œ

namespace continuous_linear_map

/-- A (possibly nonlinear) right inverse to a continuous linear map, which doesn't have to be
linear itself but which satisfies a bound `â€–inverse xâ€– â‰¤ C * â€–xâ€–`. A surjective continuous linear
map doesn't always have a continuous linear right inverse, but it always has a nonlinear inverse
in this sense, by Banach's open mapping theorem. -/
structure nonlinear_right_inverse :=
(to_fun : F â†’ E)
(nnnorm : â„â‰¥0)
(bound' : âˆ€ y, â€–to_fun yâ€– â‰¤ nnnorm * â€–yâ€–)
(right_inv' : âˆ€ y, f (to_fun y) = y)

instance : has_coe_to_fun (nonlinear_right_inverse f) (Î» _, F â†’ E) := âŸ¨Î» fsymm, fsymm.to_funâŸ©

@[simp] lemma nonlinear_right_inverse.right_inv {f : E â†’L[ğ•œ] F} (fsymm : nonlinear_right_inverse f)
  (y : F) : f (fsymm y) = y :=
fsymm.right_inv' y

lemma nonlinear_right_inverse.bound {f : E â†’L[ğ•œ] F} (fsymm : nonlinear_right_inverse f) (y : F) :
  â€–fsymm yâ€– â‰¤ fsymm.nnnorm * â€–yâ€– :=
fsymm.bound' y

end continuous_linear_map

/-- Given a continuous linear equivalence, the inverse is in particular an instance of
`nonlinear_right_inverse` (which turns out to be linear). -/
noncomputable def continuous_linear_equiv.to_nonlinear_right_inverse (f : E â‰ƒL[ğ•œ] F) :
  continuous_linear_map.nonlinear_right_inverse (f : E â†’L[ğ•œ] F) :=
{ to_fun := f.inv_fun,
  nnnorm := â€–(f.symm : F â†’L[ğ•œ] E)â€–â‚Š,
  bound' := Î» y, continuous_linear_map.le_op_norm (f.symm : F â†’L[ğ•œ] E) _,
  right_inv' := f.apply_symm_apply }

noncomputable instance (f : E â‰ƒL[ğ•œ] F) :
  inhabited (continuous_linear_map.nonlinear_right_inverse (f : E â†’L[ğ•œ] F)) :=
âŸ¨f.to_nonlinear_right_inverseâŸ©

/-! ### Proof of the Banach open mapping theorem -/

variable [complete_space F]

namespace continuous_linear_map
/--
First step of the proof of the Banach open mapping theorem (using completeness of `F`):
by Baire's theorem, there exists a ball in `E` whose image closure has nonempty interior.
Rescaling everything, it follows that any `y âˆˆ F` is arbitrarily well approached by
images of elements of norm at most `C * â€–yâ€–`.
For further use, we will only need such an element whose image
is within distance `â€–yâ€–/2` of `y`, to apply an iterative process. -/
lemma exists_approx_preimage_norm_le (surj : surjective f) :
  âˆƒC â‰¥ 0, âˆ€y, âˆƒx, dist (f x) y â‰¤ 1/2 * â€–yâ€– âˆ§ â€–xâ€– â‰¤ C * â€–yâ€– :=
begin
  have A : (â‹ƒn:â„•, closure (f '' (ball 0 n))) = univ,
  { refine subset.antisymm (subset_univ _) (Î»y hy, _),
    rcases surj y with âŸ¨x, hxâŸ©,
    rcases exists_nat_gt (â€–xâ€–) with âŸ¨n, hnâŸ©,
    refine mem_Union.2 âŸ¨n, subset_closure _âŸ©,
    refine (mem_image _ _ _).2 âŸ¨x, âŸ¨_, hxâŸ©âŸ©,
    rwa [mem_ball, dist_eq_norm, sub_zero] },
  have : âˆƒ (n : â„•) x, x âˆˆ interior (closure (f '' (ball 0 n))) :=
    nonempty_interior_of_Union_of_closed (Î»n, is_closed_closure) A,
  simp only [mem_interior_iff_mem_nhds, metric.mem_nhds_iff] at this,
  rcases this with âŸ¨n, a, Îµ, âŸ¨Îµpos, HâŸ©âŸ©,
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  refine âŸ¨(Îµ/2)â»Â¹ * â€–câ€– * 2 * n, _, Î»y, _âŸ©,
  { refine mul_nonneg (mul_nonneg (mul_nonneg _ (norm_nonneg _)) (by norm_num)) _,
    exacts [inv_nonneg.2 (div_nonneg (le_of_lt Îµpos) (by norm_num)), n.cast_nonneg] },
  { by_cases hy : y = 0,
    { use 0, simp [hy] },
    { rcases rescale_to_shell hc (half_pos Îµpos) hy with âŸ¨d, hd, ydlt, leyd, dinvâŸ©,
      let Î´ := â€–dâ€– * â€–yâ€–/4,
      have Î´pos : 0 < Î´ :=
        div_pos (mul_pos (norm_pos_iff.2 hd) (norm_pos_iff.2 hy)) (by norm_num),
      have : a + d â€¢ y âˆˆ ball a Îµ,
        by simp [dist_eq_norm, lt_of_le_of_lt ydlt.le (half_lt_self Îµpos)],
      rcases metric.mem_closure_iff.1 (H this) _ Î´pos with âŸ¨zâ‚, zâ‚im, hâ‚âŸ©,
      rcases (mem_image _ _ _).1 zâ‚im with âŸ¨xâ‚, hxâ‚, xzâ‚âŸ©,
      rw â† xzâ‚ at hâ‚,
      rw [mem_ball, dist_eq_norm, sub_zero] at hxâ‚,
      have : a âˆˆ ball a Îµ, by { simp, exact Îµpos },
      rcases metric.mem_closure_iff.1 (H this) _ Î´pos with âŸ¨zâ‚‚, zâ‚‚im, hâ‚‚âŸ©,
      rcases (mem_image _ _ _).1 zâ‚‚im with âŸ¨xâ‚‚, hxâ‚‚, xzâ‚‚âŸ©,
      rw â† xzâ‚‚ at hâ‚‚,
      rw [mem_ball, dist_eq_norm, sub_zero] at hxâ‚‚,
      let x := xâ‚ - xâ‚‚,
      have I : â€–f x - d â€¢ yâ€– â‰¤ 2 * Î´ := calc
        â€–f x - d â€¢ yâ€– = â€–f xâ‚ - (a + d â€¢ y) - (f xâ‚‚ - a)â€– :
          by { congr' 1, simp only [x, f.map_sub], abel }
        ... â‰¤ â€–f xâ‚ - (a + d â€¢ y)â€– + â€–f xâ‚‚ - aâ€– :
          norm_sub_le _ _
        ... â‰¤ Î´ + Î´ : begin
            apply add_le_add,
            { rw [â† dist_eq_norm, dist_comm], exact le_of_lt hâ‚ },
            { rw [â† dist_eq_norm, dist_comm], exact le_of_lt hâ‚‚ }
          end
        ... = 2 * Î´ : (two_mul _).symm,
      have J : â€–f (dâ»Â¹ â€¢ x) - yâ€– â‰¤ 1/2 * â€–yâ€– := calc
        â€–f (dâ»Â¹ â€¢ x) - yâ€– = â€–dâ»Â¹ â€¢ f x - (dâ»Â¹ * d) â€¢ yâ€– :
          by rwa [f.map_smul _, inv_mul_cancel, one_smul]
        ... = â€–dâ»Â¹ â€¢ (f x - d â€¢ y)â€– : by rw [mul_smul, smul_sub]
        ... = â€–dâ€–â»Â¹ * â€–f x - d â€¢ yâ€– : by rw [norm_smul, norm_inv]
        ... â‰¤ â€–dâ€–â»Â¹ * (2 * Î´) : begin
            apply mul_le_mul_of_nonneg_left I,
            rw inv_nonneg,
            exact norm_nonneg _
          end
        ... = (â€–dâ€–â»Â¹ * â€–dâ€–) * â€–yâ€– /2 : by { simp only [Î´], ring }
        ... = â€–yâ€–/2 : by { rw [inv_mul_cancel, one_mul],  simp [norm_eq_zero, hd] }
        ... = (1/2) * â€–yâ€– : by ring,
      rw â† dist_eq_norm at J,
      have K : â€–dâ»Â¹ â€¢ xâ€– â‰¤ (Îµ / 2)â»Â¹ * â€–câ€– * 2 * â†‘n * â€–yâ€– := calc
        â€–dâ»Â¹ â€¢ xâ€– = â€–dâ€–â»Â¹ * â€–xâ‚ - xâ‚‚â€– : by rw [norm_smul, norm_inv]
        ... â‰¤ ((Îµ / 2)â»Â¹ * â€–câ€– * â€–yâ€–) * (n + n) : begin
            refine mul_le_mul dinv _ (norm_nonneg _) _,
            { exact le_trans (norm_sub_le _ _) (add_le_add (le_of_lt hxâ‚) (le_of_lt hxâ‚‚)) },
            { apply mul_nonneg (mul_nonneg _ (norm_nonneg _)) (norm_nonneg _),
              exact inv_nonneg.2 (le_of_lt (half_pos Îµpos)) }
          end
        ... = (Îµ / 2)â»Â¹ * â€–câ€– * 2 * â†‘n * â€–yâ€– : by ring,
      exact âŸ¨dâ»Â¹ â€¢ x, J, KâŸ© } },
end

variable [complete_space E]

/-- The Banach open mapping theorem: if a bounded linear map between Banach spaces is onto, then
any point has a preimage with controlled norm. -/
theorem exists_preimage_norm_le (surj : surjective f) :
  âˆƒC > 0, âˆ€y, âˆƒx, f x = y âˆ§ â€–xâ€– â‰¤ C * â€–yâ€– :=
begin
  obtain âŸ¨C, C0, hCâŸ© := exists_approx_preimage_norm_le f surj,
  /- Second step of the proof: starting from `y`, we want an exact preimage of `y`. Let `g y` be
  the approximate preimage of `y` given by the first step, and `h y = y - f(g y)` the part that
  has no preimage yet. We will iterate this process, taking the approximate preimage of `h y`,
  leaving only `h^2 y` without preimage yet, and so on. Let `u n` be the approximate preimage
  of `h^n y`. Then `u` is a converging series, and by design the sum of the series is a
  preimage of `y`. This uses completeness of `E`. -/
  choose g hg using hC,
  let h := Î»y, y - f (g y),
  have hle : âˆ€y, â€–h yâ€– â‰¤ (1/2) * â€–yâ€–,
  { assume y,
    rw [â† dist_eq_norm, dist_comm],
    exact (hg y).1 },
  refine âŸ¨2 * C + 1, by linarith, Î»y, _âŸ©,
  have hnle : âˆ€n:â„•, â€–(h^[n]) yâ€– â‰¤ (1/2)^n * â€–yâ€–,
  { assume n,
    induction n with n IH,
    { simp only [one_div, nat.nat_zero_eq_zero, one_mul, iterate_zero_apply,
        pow_zero] },
    { rw [iterate_succ'],
      apply le_trans (hle _) _,
      rw [pow_succ, mul_assoc],
      apply mul_le_mul_of_nonneg_left IH,
      norm_num } },
  let u := Î»n, g((h^[n]) y),
  have ule : âˆ€n, â€–u nâ€– â‰¤ (1/2)^n * (C * â€–yâ€–),
  { assume n,
    apply le_trans (hg _).2 _,
    calc C * â€–(h^[n]) yâ€– â‰¤ C * ((1/2)^n * â€–yâ€–) : mul_le_mul_of_nonneg_left (hnle n) C0
         ... = (1 / 2) ^ n * (C * â€–yâ€–) : by ring },
  have sNu : summable (Î»n, â€–u nâ€–),
  { refine summable_of_nonneg_of_le (Î»n, norm_nonneg _) ule _,
    exact summable.mul_right _ (summable_geometric_of_lt_1 (by norm_num) (by norm_num)) },
  have su : summable u := summable_of_summable_norm sNu,
  let x := tsum u,
  have x_ineq : â€–xâ€– â‰¤ (2 * C + 1) * â€–yâ€– := calc
    â€–xâ€– â‰¤ âˆ‘'n, â€–u nâ€– : norm_tsum_le_tsum_norm sNu
    ... â‰¤ âˆ‘'n, (1/2)^n * (C * â€–yâ€–) :
      tsum_le_tsum ule sNu (summable.mul_right _ summable_geometric_two)
    ... = (âˆ‘'n, (1/2)^n) * (C * â€–yâ€–) : tsum_mul_right
    ... = 2 * C * â€–yâ€– : by rw [tsum_geometric_two, mul_assoc]
    ... â‰¤ 2 * C * â€–yâ€– + â€–yâ€– : le_add_of_nonneg_right (norm_nonneg y)
    ... = (2 * C + 1) * â€–yâ€– : by ring,
  have fsumeq : âˆ€n:â„•, f (âˆ‘ i in finset.range n, u i) = y - (h^[n]) y,
  { assume n,
    induction n with n IH,
    { simp [f.map_zero] },
    { rw [sum_range_succ, f.map_add, IH, iterate_succ', sub_add] } },
  have : tendsto (Î»n, âˆ‘ i in finset.range n, u i) at_top (ğ“ x) :=
    su.has_sum.tendsto_sum_nat,
  have Lâ‚ : tendsto (Î»n, f (âˆ‘ i in finset.range n, u i)) at_top (ğ“ (f x)) :=
    (f.continuous.tendsto _).comp this,
  simp only [fsumeq] at Lâ‚,
  have Lâ‚‚ : tendsto (Î»n, y - (h^[n]) y) at_top (ğ“ (y - 0)),
  { refine tendsto_const_nhds.sub _,
    rw tendsto_iff_norm_tendsto_zero,
    simp only [sub_zero],
    refine squeeze_zero (Î»_, norm_nonneg _) hnle _,
    rw [â† zero_mul â€–yâ€–],
    refine (tendsto_pow_at_top_nhds_0_of_lt_1 _ _).mul tendsto_const_nhds; norm_num },
  have feq : f x = y - 0 := tendsto_nhds_unique Lâ‚ Lâ‚‚,
  rw sub_zero at feq,
  exact âŸ¨x, feq, x_ineqâŸ©
end

/-- The Banach open mapping theorem: a surjective bounded linear map between Banach spaces is
open. -/
protected theorem is_open_map (surj : surjective f) : is_open_map f :=
begin
  assume s hs,
  rcases exists_preimage_norm_le f surj with âŸ¨C, Cpos, hCâŸ©,
  refine is_open_iff.2 (Î»y yfs, _),
  rcases mem_image_iff_bex.1 yfs with âŸ¨x, xs, fxyâŸ©,
  rcases is_open_iff.1 hs x xs with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  refine âŸ¨Îµ/C, div_pos Îµpos Cpos, Î»z hz, _âŸ©,
  rcases hC (z-y) with âŸ¨w, wim, wnormâŸ©,
  have : f (x + w) = z, by { rw [f.map_add, wim, fxy, add_sub_cancel'_right] },
  rw â† this,
  have : x + w âˆˆ ball x Îµ := calc
    dist (x+w) x = â€–wâ€– : by { rw dist_eq_norm, simp }
    ... â‰¤ C * â€–z - yâ€– : wnorm
    ... < C * (Îµ/C) : begin
        apply mul_lt_mul_of_pos_left _ Cpos,
        rwa [mem_ball, dist_eq_norm] at hz,
      end
    ... = Îµ : mul_div_cancel' _ (ne_of_gt Cpos),
  exact set.mem_image_of_mem _ (hÎµ this)
end

protected theorem quotient_map (surj : surjective f) : quotient_map f :=
(f.is_open_map surj).to_quotient_map f.continuous surj

lemma _root_.affine_map.is_open_map {P Q : Type*}
  [metric_space P] [normed_add_torsor E P] [metric_space Q] [normed_add_torsor F Q]
  (f : P â†’áµƒ[ğ•œ] Q) (hf : continuous f) (surj : surjective f) :
  is_open_map f :=
affine_map.is_open_map_linear_iff.mp $ continuous_linear_map.is_open_map
  { cont := affine_map.continuous_linear_iff.mpr hf, .. f.linear }
  (f.linear_surjective_iff.mpr surj)

/-! ### Applications of the Banach open mapping theorem -/

lemma interior_preimage (hsurj : surjective f) (s : set F) :
  interior (f â»Â¹' s) = f â»Â¹' (interior s) :=
((f.is_open_map hsurj).preimage_interior_eq_interior_preimage f.continuous s).symm

lemma closure_preimage (hsurj : surjective f) (s : set F) :
  closure (f â»Â¹' s) = f â»Â¹' (closure s) :=
((f.is_open_map hsurj).preimage_closure_eq_closure_preimage f.continuous s).symm

lemma frontier_preimage (hsurj : surjective f) (s : set F) :
  frontier (f â»Â¹' s) = f â»Â¹' (frontier s) :=
((f.is_open_map hsurj).preimage_frontier_eq_frontier_preimage f.continuous s).symm

lemma exists_nonlinear_right_inverse_of_surjective (f : E â†’L[ğ•œ] F)
  (hsurj : linear_map.range f = âŠ¤) : âˆƒ (fsymm : nonlinear_right_inverse f), 0 < fsymm.nnnorm :=
begin
  choose C hC fsymm h using exists_preimage_norm_le _ (linear_map.range_eq_top.mp hsurj),
  use { to_fun := fsymm,
        nnnorm := âŸ¨C, hC.lt.leâŸ©,
        bound' := Î» y, (h y).2,
        right_inv' := Î» y, (h y).1 },
  exact hC
end

/-- A surjective continuous linear map between Banach spaces admits a (possibly nonlinear)
controlled right inverse. In general, it is not possible to ensure that such a right inverse
is linear (take for instance the map from `E` to `E/F` where `F` is a closed subspace of `E`
without a closed complement. Then it doesn't have a continuous linear right inverse.) -/
@[irreducible] noncomputable def nonlinear_right_inverse_of_surjective
  (f : E â†’L[ğ•œ] F) (hsurj : linear_map.range f = âŠ¤) : nonlinear_right_inverse f :=
classical.some (exists_nonlinear_right_inverse_of_surjective f hsurj)

lemma nonlinear_right_inverse_of_surjective_nnnorm_pos (f : E â†’L[ğ•œ] F)
  (hsurj : linear_map.range f = âŠ¤) : 0 < (nonlinear_right_inverse_of_surjective f hsurj).nnnorm :=
begin
  rw nonlinear_right_inverse_of_surjective,
  exact classical.some_spec (exists_nonlinear_right_inverse_of_surjective f hsurj)
end

end continuous_linear_map

namespace linear_equiv

variables [complete_space E]

/-- If a bounded linear map is a bijection, then its inverse is also a bounded linear map. -/
@[continuity]
theorem continuous_symm (e : E â‰ƒâ‚—[ğ•œ] F) (h : continuous e) :
  continuous e.symm :=
begin
  rw continuous_def,
  intros s hs,
  rw [â† e.image_eq_preimage],
  rw [â† e.coe_coe] at h âŠ¢,
  exact continuous_linear_map.is_open_map âŸ¨â†‘e, hâŸ© e.surjective s hs
end

/-- Associating to a linear equivalence between Banach spaces a continuous linear equivalence when
the direct map is continuous, thanks to the Banach open mapping theorem that ensures that the
inverse map is also continuous. -/
def to_continuous_linear_equiv_of_continuous (e : E â‰ƒâ‚—[ğ•œ] F) (h : continuous e) :
  E â‰ƒL[ğ•œ] F :=
{ continuous_to_fun := h,
  continuous_inv_fun := e.continuous_symm h,
  ..e }

@[simp] lemma coe_fn_to_continuous_linear_equiv_of_continuous (e : E â‰ƒâ‚—[ğ•œ] F) (h : continuous e) :
  â‡‘(e.to_continuous_linear_equiv_of_continuous h) = e := rfl

@[simp] lemma coe_fn_to_continuous_linear_equiv_of_continuous_symm (e : E â‰ƒâ‚—[ğ•œ] F)
  (h : continuous e) :
  â‡‘(e.to_continuous_linear_equiv_of_continuous h).symm = e.symm := rfl

end linear_equiv

namespace continuous_linear_equiv

variables [complete_space E]

/-- Convert a bijective continuous linear map `f : E â†’L[ğ•œ] F` from a Banach space to a normed space
to a continuous linear equivalence. -/
noncomputable def of_bijective (f : E â†’L[ğ•œ] F) (hinj : ker f = âŠ¥)
  (hsurj : linear_map.range f = âŠ¤) : E â‰ƒL[ğ•œ] F :=
(linear_equiv.of_bijective â†‘f âŸ¨linear_map.ker_eq_bot.mp hinj, linear_map.range_eq_top.mp hsurjâŸ©)
.to_continuous_linear_equiv_of_continuous f.continuous

@[simp] lemma coe_fn_of_bijective (f : E â†’L[ğ•œ] F) (hinj : ker f = âŠ¥)
  (hsurj : linear_map.range f = âŠ¤) : â‡‘(of_bijective f hinj hsurj) = f := rfl

lemma coe_of_bijective (f : E â†’L[ğ•œ] F) (hinj : ker f = âŠ¥) (hsurj : linear_map.range f = âŠ¤) :
  â†‘(of_bijective f hinj hsurj) = f := by { ext, refl }

@[simp] lemma of_bijective_symm_apply_apply (f : E â†’L[ğ•œ] F) (hinj : ker f = âŠ¥)
  (hsurj : linear_map.range f = âŠ¤) (x : E) :
  (of_bijective f hinj hsurj).symm (f x) = x :=
(of_bijective f hinj hsurj).symm_apply_apply x

@[simp] lemma of_bijective_apply_symm_apply (f : E â†’L[ğ•œ] F) (hinj : ker f = âŠ¥)
  (hsurj : linear_map.range f = âŠ¤) (y : F) :
  f ((of_bijective f hinj hsurj).symm y) = y :=
(of_bijective f hinj hsurj).apply_symm_apply y

end continuous_linear_equiv

namespace continuous_linear_map

variables [complete_space E]

/-- Intermediate definition used to show
`continuous_linear_map.closed_complemented_range_of_is_compl_of_ker_eq_bot`.

This is `f.coprod G.subtypeL` as an `continuous_linear_equiv`. -/
noncomputable def coprod_subtypeL_equiv_of_is_compl
  (f : E â†’L[ğ•œ] F) {G : submodule ğ•œ F}
  (h : is_compl (linear_map.range f) G) [complete_space G] (hker : ker f = âŠ¥) : (E Ã— G) â‰ƒL[ğ•œ] F :=
continuous_linear_equiv.of_bijective (f.coprod G.subtypeL)
  (begin
    rw ker_coprod_of_disjoint_range,
    { rw [hker, submodule.ker_subtypeL, submodule.prod_bot] },
    { rw submodule.range_subtypeL,
      exact h.disjoint }
  end)
  (by simp only [range_coprod, h.sup_eq_top, submodule.range_subtypeL])

lemma range_eq_map_coprod_subtypeL_equiv_of_is_compl
  (f : E â†’L[ğ•œ] F) {G : submodule ğ•œ F}
  (h : is_compl (linear_map.range f) G) [complete_space G] (hker : ker f = âŠ¥) :
    linear_map.range f = ((âŠ¤ : submodule ğ•œ E).prod (âŠ¥ : submodule ğ•œ G)).map
      (f.coprod_subtypeL_equiv_of_is_compl h hker : E Ã— G â†’â‚—[ğ•œ] F) :=
begin
  rw [coprod_subtypeL_equiv_of_is_compl, _root_.coe_coe, continuous_linear_equiv.coe_of_bijective,
         coe_coprod, linear_map.coprod_map_prod, submodule.map_bot, sup_bot_eq, submodule.map_top],
  refl
end

/- TODO: remove the assumption `f.ker = âŠ¥` in the next lemma, by using the map induced by `f` on
`E / f.ker`, once we have quotient normed spaces. -/
lemma closed_complemented_range_of_is_compl_of_ker_eq_bot (f : E â†’L[ğ•œ] F) (G : submodule ğ•œ F)
  (h : is_compl (linear_map.range f) G) (hG : is_closed (G : set F)) (hker : ker f = âŠ¥) :
  is_closed (linear_map.range f : set F) :=
begin
  haveI : complete_space G := hG.complete_space_coe,
  let g := coprod_subtypeL_equiv_of_is_compl f h hker,
  rw congr_arg coe (range_eq_map_coprod_subtypeL_equiv_of_is_compl f h hker ),
  apply g.to_homeomorph.is_closed_image.2,
  exact is_closed_univ.prod is_closed_singleton,
end

end continuous_linear_map

section closed_graph_thm

variables [complete_space E] (g : E â†’â‚—[ğ•œ] F)

/-- The **closed graph theorem** : a linear map between two Banach spaces whose graph is closed
is continuous. -/
theorem linear_map.continuous_of_is_closed_graph (hg : is_closed (g.graph : set $ E Ã— F)) :
  continuous g :=
begin
  letI : complete_space g.graph := complete_space_coe_iff_is_complete.mpr hg.is_complete,
  let Ï†â‚€ : E â†’â‚—[ğ•œ] E Ã— F := linear_map.id.prod g,
  have : function.left_inverse prod.fst Ï†â‚€ := Î» x, rfl,
  let Ï† : E â‰ƒâ‚—[ğ•œ] g.graph :=
    (linear_equiv.of_left_inverse this).trans
    (linear_equiv.of_eq _ _ g.graph_eq_range_prod.symm),
  let Ïˆ : g.graph â‰ƒL[ğ•œ] E := Ï†.symm.to_continuous_linear_equiv_of_continuous
    continuous_subtype_coe.fst,
  exact (continuous_subtype_coe.comp Ïˆ.symm.continuous).snd
end

/-- A useful form of the **closed graph theorem** : let `f` be a linear map between two Banach
spaces. To show that `f` is continuous, it suffices to show that for any convergent sequence
`uâ‚™ âŸ¶ x`, if `f(uâ‚™) âŸ¶ y` then `y = f(x)`. -/
theorem linear_map.continuous_of_seq_closed_graph
  (hg : âˆ€ (u : â„• â†’ E) x y, tendsto u at_top (ğ“ x) â†’ tendsto (g âˆ˜ u) at_top (ğ“ y) â†’ y = g x) :
  continuous g :=
begin
  refine g.continuous_of_is_closed_graph (is_seq_closed.is_closed _),
  rintros Ï† âŸ¨x, yâŸ© hÏ†g hÏ†,
  refine hg (prod.fst âˆ˜ Ï†) x y ((continuous_fst.tendsto _).comp hÏ†) _,
  have : g âˆ˜ prod.fst âˆ˜ Ï† = prod.snd âˆ˜ Ï†,
  { ext n,
    exact (hÏ†g n).symm },
  rw this,
  exact (continuous_snd.tendsto _).comp hÏ†
end

variable {g}

namespace continuous_linear_map

/-- Upgrade a `linear_map` to a `continuous_linear_map` using the **closed graph theorem**. -/
def of_is_closed_graph (hg : is_closed (g.graph : set $ E Ã— F)) :
  E â†’L[ğ•œ] F :=
{ to_linear_map := g,
  cont := g.continuous_of_is_closed_graph hg }

@[simp] lemma coe_fn_of_is_closed_graph (hg : is_closed (g.graph : set $ E Ã— F)) :
  â‡‘(continuous_linear_map.of_is_closed_graph hg) = g := rfl

lemma coe_of_is_closed_graph (hg : is_closed (g.graph : set $ E Ã— F)) :
  â†‘(continuous_linear_map.of_is_closed_graph hg) = g := by { ext, refl }

/-- Upgrade a `linear_map` to a `continuous_linear_map` using a variation on the
**closed graph theorem**. -/
def of_seq_closed_graph
  (hg : âˆ€ (u : â„• â†’ E) x y, tendsto u at_top (ğ“ x) â†’ tendsto (g âˆ˜ u) at_top (ğ“ y) â†’ y = g x) :
  E â†’L[ğ•œ] F :=
{ to_linear_map := g,
  cont := g.continuous_of_seq_closed_graph hg }

@[simp] lemma coe_fn_of_seq_closed_graph
  (hg : âˆ€ (u : â„• â†’ E) x y, tendsto u at_top (ğ“ x) â†’ tendsto (g âˆ˜ u) at_top (ğ“ y) â†’ y = g x) :
  â‡‘(continuous_linear_map.of_seq_closed_graph hg) = g := rfl

lemma coe_of_seq_closed_graph
  (hg : âˆ€ (u : â„• â†’ E) x y, tendsto u at_top (ğ“ x) â†’ tendsto (g âˆ˜ u) at_top (ğ“ y) â†’ y = g x) :
  â†‘(continuous_linear_map.of_seq_closed_graph hg) = g := by { ext, refl }

end continuous_linear_map

end closed_graph_thm
