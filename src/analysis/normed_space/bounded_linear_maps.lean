/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl
-/
import analysis.normed_space.multilinear
import analysis.normed_space.units
import analysis.asymptotics.asymptotics

/-!
# Bounded linear maps

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines a class stating that a map between normed vector spaces is (bi)linear and
continuous.
Instead of asking for continuity, the definition takes the equivalent condition (because the space
is normed) that `â€–f xâ€–` is bounded by a multiple of `â€–xâ€–`. Hence the "bounded" in the name refers to
`â€–f xâ€–/â€–xâ€–` rather than `â€–f xâ€–` itself.

## Main definitions

* `is_bounded_linear_map`: Class stating that a map `f : E â†’ F` is linear and has `â€–f xâ€–` bounded
  by a multiple of `â€–xâ€–`.
* `is_bounded_bilinear_map`: Class stating that a map `f : E Ã— F â†’ G` is bilinear and continuous,
  but through the simpler to provide statement that `â€–f (x, y)â€–` is bounded by a multiple of
  `â€–xâ€– * â€–yâ€–`
* `is_bounded_bilinear_map.linear_deriv`: Derivative of a continuous bilinear map as a linear map.
* `is_bounded_bilinear_map.deriv`: Derivative of a continuous bilinear map as a continuous linear
  map. The proof that it is indeed the derivative is `is_bounded_bilinear_map.has_fderiv_at` in
  `analysis.calculus.fderiv`.

## Main theorems

* `is_bounded_bilinear_map.continuous`: A bounded bilinear map is continuous.
* `continuous_linear_equiv.is_open`: The continuous linear equivalences are an open subset of the
  set of continuous linear maps between a pair of Banach spaces.  Placed in this file because its
  proof uses `is_bounded_bilinear_map.continuous`.

## Notes

The main use of this file is `is_bounded_bilinear_map`. The file `analysis.normed_space.multilinear`
already expounds the theory of multilinear maps, but the `2`-variables case is sufficiently simpler
to currently deserve its own treatment.

`is_bounded_linear_map` is effectively an unbundled version of `continuous_linear_map` (defined
in `topology.algebra.module.basic`, theory over normed spaces developed in
`analysis.normed_space.operator_norm`), albeit the name disparity. A bundled
`continuous_linear_map` is to be preferred over a `is_bounded_linear_map` hypothesis. Historical
artifact, really.
-/

noncomputable theory
open_locale big_operators topology

open filter (tendsto) metric continuous_linear_map

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
          {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
          {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
          {G : Type*} [normed_add_comm_group G] [normed_space ğ•œ G]

/-- A function `f` satisfies `is_bounded_linear_map ğ•œ f` if it is linear and satisfies the
inequality `â€–f xâ€– â‰¤ M * â€–xâ€–` for some positive constant `M`. -/
structure is_bounded_linear_map (ğ•œ : Type*) [normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F] (f : E â†’ F)
  extends is_linear_map ğ•œ f : Prop :=
(bound : âˆƒ M, 0 < M âˆ§ âˆ€ x : E, â€–f xâ€– â‰¤ M * â€–xâ€–)

lemma is_linear_map.with_bound
  {f : E â†’ F} (hf : is_linear_map ğ•œ f) (M : â„) (h : âˆ€ x : E, â€–f xâ€– â‰¤ M * â€–xâ€–) :
  is_bounded_linear_map ğ•œ f :=
âŸ¨ hf, classical.by_cases
  (assume : M â‰¤ 0, âŸ¨1, zero_lt_one, Î» x,
    (h x).trans $ mul_le_mul_of_nonneg_right (this.trans zero_le_one) (norm_nonneg x)âŸ©)
  (assume : Â¬ M â‰¤ 0, âŸ¨M, lt_of_not_ge this, hâŸ©)âŸ©

/-- A continuous linear map satisfies `is_bounded_linear_map` -/
lemma continuous_linear_map.is_bounded_linear_map (f : E â†’L[ğ•œ] F) : is_bounded_linear_map ğ•œ f :=
{ bound := f.bound,
  ..f.to_linear_map.is_linear }

namespace is_bounded_linear_map

/-- Construct a linear map from a function `f` satisfying `is_bounded_linear_map ğ•œ f`. -/
def to_linear_map (f : E â†’ F) (h : is_bounded_linear_map ğ•œ f) : E â†’â‚—[ğ•œ] F :=
(is_linear_map.mk' _ h.to_is_linear_map)

/-- Construct a continuous linear map from is_bounded_linear_map -/
def to_continuous_linear_map {f : E â†’ F} (hf : is_bounded_linear_map ğ•œ f) : E â†’L[ğ•œ] F :=
{ cont := let âŸ¨C, Cpos, hCâŸ© :=
    hf.bound in add_monoid_hom_class.continuous_of_bound (to_linear_map f hf) C hC,
  ..to_linear_map f hf}

lemma zero : is_bounded_linear_map ğ•œ (Î» (x:E), (0:F)) :=
(0 : E â†’â‚—[ğ•œ] F).is_linear.with_bound 0 $ by simp [le_refl]

lemma id : is_bounded_linear_map ğ•œ (Î» (x:E), x) :=
linear_map.id.is_linear.with_bound 1 $ by simp [le_refl]

lemma fst : is_bounded_linear_map ğ•œ (Î» x : E Ã— F, x.1) :=
begin
  refine (linear_map.fst ğ•œ E F).is_linear.with_bound 1 (Î» x, _),
  rw one_mul,
  exact le_max_left _ _
end

lemma snd : is_bounded_linear_map ğ•œ (Î» x : E Ã— F, x.2) :=
begin
  refine (linear_map.snd ğ•œ E F).is_linear.with_bound 1 (Î» x, _),
  rw one_mul,
  exact le_max_right _ _
end

variables {f g : E â†’ F}

lemma smul (c : ğ•œ) (hf : is_bounded_linear_map ğ•œ f) :
  is_bounded_linear_map ğ•œ (c â€¢ f) :=
let âŸ¨hlf, M, hMp, hMâŸ© := hf in
(c â€¢ hlf.mk' f).is_linear.with_bound (â€–câ€– * M) $ Î» x,
  calc â€–c â€¢ f xâ€– = â€–câ€– * â€–f xâ€– : norm_smul c (f x)
  ... â‰¤ â€–câ€– * (M * â€–xâ€–)        : mul_le_mul_of_nonneg_left (hM _) (norm_nonneg _)
  ... = (â€–câ€– * M) * â€–xâ€–        : (mul_assoc _ _ _).symm

lemma neg (hf : is_bounded_linear_map ğ•œ f) :
  is_bounded_linear_map ğ•œ (Î» e, -f e) :=
begin
  rw show (Î» e, -f e) = (Î» e, (-1 : ğ•œ) â€¢ f e), { funext, simp },
  exact smul (-1) hf
end

lemma add (hf : is_bounded_linear_map ğ•œ f) (hg : is_bounded_linear_map ğ•œ g) :
  is_bounded_linear_map ğ•œ (Î» e, f e + g e) :=
let âŸ¨hlf, Mf, hMfp, hMfâŸ© := hf in
let âŸ¨hlg, Mg, hMgp, hMgâŸ© := hg in
(hlf.mk' _ + hlg.mk' _).is_linear.with_bound (Mf + Mg) $ Î» x,
  calc â€–f x + g xâ€– â‰¤ Mf * â€–xâ€– + Mg * â€–xâ€– : norm_add_le_of_le (hMf x) (hMg x)
               ... â‰¤ (Mf + Mg) * â€–xâ€–     : by rw add_mul

lemma sub (hf : is_bounded_linear_map ğ•œ f) (hg : is_bounded_linear_map ğ•œ g) :
  is_bounded_linear_map ğ•œ (Î» e, f e - g e) :=
by simpa [sub_eq_add_neg] using add hf (neg hg)

lemma comp {g : F â†’ G}
  (hg : is_bounded_linear_map ğ•œ g) (hf : is_bounded_linear_map ğ•œ f) :
  is_bounded_linear_map ğ•œ (g âˆ˜ f) :=
(hg.to_continuous_linear_map.comp hf.to_continuous_linear_map).is_bounded_linear_map

protected lemma tendsto (x : E) (hf : is_bounded_linear_map ğ•œ f) :
  tendsto f (ğ“ x) (ğ“ (f x)) :=
let âŸ¨hf, M, hMp, hMâŸ© := hf in
tendsto_iff_norm_tendsto_zero.2 $
  squeeze_zero (Î» e, norm_nonneg _)
    (Î» e,
      calc â€–f e - f xâ€– = â€–hf.mk' f (e - x)â€– : by rw (hf.mk' _).map_sub e x; refl
                   ... â‰¤ M * â€–e - xâ€–        : hM (e - x))
    (suffices tendsto (Î» (e : E), M * â€–e - xâ€–) (ğ“ x) (ğ“ (M * 0)), by simpa,
      tendsto_const_nhds.mul (tendsto_norm_sub_self _))

lemma continuous (hf : is_bounded_linear_map ğ•œ f) : continuous f :=
continuous_iff_continuous_at.2 $ Î» _, hf.tendsto _

lemma lim_zero_bounded_linear_map (hf : is_bounded_linear_map ğ•œ f) :
  tendsto f (ğ“ 0) (ğ“ 0) :=
(hf.1.mk' _).map_zero â–¸ continuous_iff_continuous_at.1 hf.continuous 0

section
open asymptotics filter

theorem is_O_id {f : E â†’ F} (h : is_bounded_linear_map ğ•œ f) (l : filter E) :
  f =O[l] (Î» x, x) :=
let âŸ¨M, hMp, hMâŸ© := h.bound in is_O.of_bound _ (mem_of_superset univ_mem (Î» x _, hM x))

theorem is_O_comp {E : Type*} {g : F â†’ G} (hg : is_bounded_linear_map ğ•œ g)
  {f : E â†’ F} (l : filter E) : (Î» x', g (f x')) =O[l] f :=
(hg.is_O_id âŠ¤).comp_tendsto le_top

theorem is_O_sub {f : E â†’ F} (h : is_bounded_linear_map ğ•œ f)
  (l : filter E) (x : E) : (Î» x', f (x' - x)) =O[l] (Î» x', x' - x) :=
is_O_comp h l

end

end is_bounded_linear_map

section
variables {Î¹ : Type*} [fintype Î¹]

/-- Taking the cartesian product of two continuous multilinear maps
is a bounded linear operation. -/
lemma is_bounded_linear_map_prod_multilinear
  {E : Î¹ â†’ Type*} [âˆ€ i, normed_add_comm_group (E i)] [âˆ€ i, normed_space ğ•œ (E i)] :
  is_bounded_linear_map ğ•œ
  (Î» p : (continuous_multilinear_map ğ•œ E F) Ã— (continuous_multilinear_map ğ•œ E G), p.1.prod p.2) :=
{ map_add := Î» pâ‚ pâ‚‚, by { ext1 m, refl },
  map_smul := Î» c p, by { ext1 m, refl },
  bound := âŸ¨1, zero_lt_one, Î» p, begin
    rw one_mul,
    apply continuous_multilinear_map.op_norm_le_bound _ (norm_nonneg _) (Î» m, _),
    rw [continuous_multilinear_map.prod_apply, norm_prod_le_iff],
    split,
    { exact (p.1.le_op_norm m).trans
        (mul_le_mul_of_nonneg_right (norm_fst_le p) (finset.prod_nonneg (Î» i hi, norm_nonneg _))) },
    { exact (p.2.le_op_norm m).trans
        (mul_le_mul_of_nonneg_right (norm_snd_le p) (finset.prod_nonneg (Î» i hi, norm_nonneg _))) },
  endâŸ© }

/-- Given a fixed continuous linear map `g`, associating to a continuous multilinear map `f` the
continuous multilinear map `f (g mâ‚, ..., g mâ‚™)` is a bounded linear operation. -/
lemma is_bounded_linear_map_continuous_multilinear_map_comp_linear (g : G â†’L[ğ•œ] E) :
  is_bounded_linear_map ğ•œ (Î» f : continuous_multilinear_map ğ•œ (Î» (i : Î¹), E) F,
    f.comp_continuous_linear_map (Î» _, g)) :=
begin
  refine is_linear_map.with_bound âŸ¨Î» fâ‚ fâ‚‚, by { ext m, refl }, Î» c f, by { ext m, refl }âŸ©
    (â€–gâ€– ^ (fintype.card Î¹)) (Î» f, _),
  apply continuous_multilinear_map.op_norm_le_bound _ _ (Î» m, _),
  { apply_rules [mul_nonneg, pow_nonneg, norm_nonneg] },
  calc â€–f (g âˆ˜ m)â€– â‰¤
    â€–fâ€– * âˆ i, â€–g (m i)â€– : f.le_op_norm _
    ... â‰¤ â€–fâ€– * âˆ i, (â€–gâ€– * â€–m iâ€–) : begin
      apply mul_le_mul_of_nonneg_left _ (norm_nonneg _),
      exact finset.prod_le_prod (Î» i hi, norm_nonneg _) (Î» i hi, g.le_op_norm _)
    end
    ... = â€–gâ€– ^ fintype.card Î¹ * â€–fâ€– * âˆ i, â€–m iâ€– :
      by { simp [finset.prod_mul_distrib, finset.card_univ], ring }
end

end

section bilinear_map

namespace continuous_linear_map

/-! We prove some computation rules for continuous (semi-)bilinear maps in their first argument.
  If `f` is a continuuous bilinear map, to use the corresponding rules for the second argument, use
  `(f _).map_add` and similar.

We have to assume that `F` and `G` are normed spaces in this section, to use
`continuous_linear_map.to_normed_add_comm_group`, but we don't need to assume this for the first
argument of `f`.
-/

variables {R : Type*}
variables {ğ•œâ‚‚ ğ•œ' : Type*} [nontrivially_normed_field ğ•œ'] [nontrivially_normed_field ğ•œâ‚‚]
variables {M : Type*} [topological_space M]
variables {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
variables {G' : Type*} [normed_add_comm_group G'] [normed_space ğ•œâ‚‚ G'] [normed_space ğ•œ' G']
variables [smul_comm_class ğ•œâ‚‚ ğ•œ' G']

section semiring
variables [semiring R] [add_comm_monoid M] [module R M] {Ïâ‚â‚‚ : R â†’+* ğ•œ'}

lemma map_addâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (x x' : M) (y : F) :
  f (x + x') y = f x y + f x' y :=
by rw [f.map_add, add_apply]

lemma map_zeroâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (y : F) : f 0 y = 0 :=
by rw [f.map_zero, zero_apply]

lemma map_smulâ‚›â‚—â‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (c : R) (x : M) (y : F) :
  f (c â€¢ x) y = Ïâ‚â‚‚ c â€¢ f x y :=
by rw [f.map_smulâ‚›â‚—, smul_apply]
end semiring

section ring

variables [ring R] [add_comm_group M] [module R M] {Ïâ‚â‚‚ : R â†’+* ğ•œ'}

lemma map_subâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (x x' : M) (y : F) :
  f (x - x') y = f x y - f x' y :=
by rw [f.map_sub, sub_apply]

lemma map_negâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (x : M) (y : F) : f (- x) y = - f x y :=
by rw [f.map_neg, neg_apply]

end ring

lemma map_smulâ‚‚ (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (c : ğ•œ) (x : E) (y : F) : f (c â€¢ x) y = c â€¢ f x y :=
by rw [f.map_smul, smul_apply]

end continuous_linear_map

variable (ğ•œ)

/-- A map `f : E Ã— F â†’ G` satisfies `is_bounded_bilinear_map ğ•œ f` if it is bilinear and
continuous. -/
structure is_bounded_bilinear_map (f : E Ã— F â†’ G) : Prop :=
(add_left   : âˆ€ (xâ‚ xâ‚‚ : E) (y : F), f (xâ‚ + xâ‚‚, y) = f (xâ‚, y) + f (xâ‚‚, y))
(smul_left  : âˆ€ (c : ğ•œ) (x : E) (y : F), f (c â€¢ x, y) = c â€¢ f (x, y))
(add_right  : âˆ€ (x : E) (yâ‚ yâ‚‚ : F), f (x, yâ‚ + yâ‚‚) = f (x, yâ‚) + f (x, yâ‚‚))
(smul_right : âˆ€ (c : ğ•œ) (x : E) (y : F), f (x, c â€¢ y) = c â€¢ f (x,y))
(bound      : âˆƒ C > 0, âˆ€ (x : E) (y : F), â€–f (x, y)â€– â‰¤ C * â€–xâ€– * â€–yâ€–)

variable {ğ•œ}
variable {f : E Ã— F â†’ G}

lemma continuous_linear_map.is_bounded_bilinear_map (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) :
  is_bounded_bilinear_map ğ•œ (Î» x : E Ã— F, f x.1 x.2) :=
{ add_left := f.map_addâ‚‚,
  smul_left := f.map_smulâ‚‚,
  add_right := Î» x, (f x).map_add,
  smul_right := Î» c x, (f x).map_smul c,
  bound := âŸ¨max â€–fâ€– 1, zero_lt_one.trans_le (le_max_right _ _),
    Î» x y, (f.le_op_normâ‚‚ x y).trans $
      by apply_rules [mul_le_mul_of_nonneg_right, norm_nonneg, le_max_left]âŸ© }

protected lemma is_bounded_bilinear_map.is_O (h : is_bounded_bilinear_map ğ•œ f) :
  f =O[âŠ¤] (Î» p : E Ã— F, â€–p.1â€– * â€–p.2â€–) :=
let âŸ¨C, Cpos, hCâŸ© := h.bound in asymptotics.is_O.of_bound _ $
filter.eventually_of_forall $ Î» âŸ¨x, yâŸ©, by simpa [mul_assoc] using hC x y

lemma is_bounded_bilinear_map.is_O_comp {Î± : Type*} (H : is_bounded_bilinear_map ğ•œ f)
  {g : Î± â†’ E} {h : Î± â†’ F} {l : filter Î±} :
  (Î» x, f (g x, h x)) =O[l] (Î» x, â€–g xâ€– * â€–h xâ€–) :=
H.is_O.comp_tendsto le_top

protected lemma is_bounded_bilinear_map.is_O' (h : is_bounded_bilinear_map ğ•œ f) :
  f =O[âŠ¤] (Î» p : E Ã— F, â€–pâ€– * â€–pâ€–) :=
h.is_O.trans $ (@asymptotics.is_O_fst_prod' _ E F _ _ _ _).norm_norm.mul
  (@asymptotics.is_O_snd_prod' _ E F _ _ _ _).norm_norm

lemma is_bounded_bilinear_map.map_sub_left (h : is_bounded_bilinear_map ğ•œ f) {x y : E} {z : F} :
  f (x - y, z) = f (x, z) - f(y, z) :=
calc f (x - y, z) = f (x + (-1 : ğ•œ) â€¢ y, z) : by simp [sub_eq_add_neg]
... = f (x, z) + (-1 : ğ•œ) â€¢ f (y, z) : by simp only [h.add_left, h.smul_left]
... = f (x, z) - f (y, z) : by simp [sub_eq_add_neg]

lemma is_bounded_bilinear_map.map_sub_right (h : is_bounded_bilinear_map ğ•œ f) {x : E} {y z : F} :
  f (x, y - z) = f (x, y) - f (x, z) :=
calc f (x, y - z) = f (x, y + (-1 : ğ•œ) â€¢ z) : by simp [sub_eq_add_neg]
... = f (x, y) + (-1 : ğ•œ) â€¢ f (x, z) : by simp only [h.add_right, h.smul_right]
... = f (x, y) - f (x, z) : by simp [sub_eq_add_neg]

/-- Useful to use together with `continuous.compâ‚‚`. -/
lemma is_bounded_bilinear_map.continuous (h : is_bounded_bilinear_map ğ•œ f) :
  continuous f :=
begin
  have one_ne : (1:â„) â‰  0 := by simp,
  obtain âŸ¨C, (Cpos : 0 < C), hCâŸ© := h.bound,
  rw continuous_iff_continuous_at,
  intros x,
  have H : âˆ€ (a:E) (b:F), â€–f (a, b)â€– â‰¤ C * â€–â€–aâ€– * â€–bâ€–â€–,
  { intros a b,
    simpa [mul_assoc] using hC a b },
  have hâ‚ : (Î» e : E Ã— F, f (e.1 - x.1, e.2)) =o[ğ“ x] (Î» e, (1:â„)),
  { refine (asymptotics.is_O_of_le' (ğ“ x) (Î» e, H (e.1 - x.1) e.2)).trans_is_o _,
    rw asymptotics.is_o_const_iff one_ne,
    convert ((continuous_fst.sub continuous_const).norm.mul continuous_snd.norm).continuous_at,
    { simp },
    apply_instance },
  have hâ‚‚ : (Î» e : E Ã— F, f (x.1, e.2 - x.2)) =o[ğ“ x] (Î» e, (1:â„)),
  { refine (asymptotics.is_O_of_le' (ğ“ x) (Î» e, H x.1 (e.2 - x.2))).trans_is_o _,
    rw asymptotics.is_o_const_iff one_ne,
    convert (continuous_const.mul (continuous_snd.sub continuous_const).norm).continuous_at,
    { simp },
    apply_instance },
  have := hâ‚.add hâ‚‚,
  rw asymptotics.is_o_const_iff one_ne at this,
  change tendsto _ _ _,
  convert this.add_const (f x),
  { ext e,
    simp [h.map_sub_left, h.map_sub_right], },
  { simp }
end

lemma is_bounded_bilinear_map.continuous_left (h : is_bounded_bilinear_map ğ•œ f) {eâ‚‚ : F} :
  continuous (Î»eâ‚, f (eâ‚, eâ‚‚)) :=
h.continuous.comp (continuous_id.prod_mk continuous_const)

lemma is_bounded_bilinear_map.continuous_right (h : is_bounded_bilinear_map ğ•œ f) {eâ‚ : E} :
  continuous (Î»eâ‚‚, f (eâ‚, eâ‚‚)) :=
h.continuous.comp (continuous_const.prod_mk continuous_id)

/-- Useful to use together with `continuous.compâ‚‚`. -/
lemma continuous_linear_map.continuousâ‚‚ (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) :
  continuous (function.uncurry (Î» x y, f x y)) :=
f.is_bounded_bilinear_map.continuous

lemma is_bounded_bilinear_map.is_bounded_linear_map_left (h : is_bounded_bilinear_map ğ•œ f) (y : F) :
  is_bounded_linear_map ğ•œ (Î» x, f (x, y)) :=
{ map_add  := Î» x x', h.add_left _ _ _,
  map_smul := Î» c x, h.smul_left _ _ _,
  bound    := begin
    rcases h.bound with âŸ¨C, C_pos, hCâŸ©,
    refine âŸ¨C * (â€–yâ€– + 1), mul_pos C_pos (lt_of_lt_of_le (zero_lt_one) (by simp)), Î» x, _âŸ©,
    have : â€–yâ€– â‰¤ â€–yâ€– + 1, by simp [zero_le_one],
    calc â€–f (x, y)â€– â‰¤ C * â€–xâ€– * â€–yâ€– : hC x y
    ... â‰¤ C * â€–xâ€– * (â€–yâ€– + 1) :
      by apply_rules [norm_nonneg, mul_le_mul_of_nonneg_left, le_of_lt C_pos, mul_nonneg]
    ... = C * (â€–yâ€– + 1) * â€–xâ€– : by ring
  end }

lemma is_bounded_bilinear_map.is_bounded_linear_map_right
  (h : is_bounded_bilinear_map ğ•œ f) (x : E) :
  is_bounded_linear_map ğ•œ (Î» y, f (x, y)) :=
{ map_add  := Î» y y', h.add_right _ _ _,
  map_smul := Î» c y, h.smul_right _ _ _,
  bound    := begin
    rcases h.bound with âŸ¨C, C_pos, hCâŸ©,
    refine âŸ¨C * (â€–xâ€– + 1), mul_pos C_pos (lt_of_lt_of_le (zero_lt_one) (by simp)), Î» y, _âŸ©,
    have : â€–xâ€– â‰¤ â€–xâ€– + 1, by simp [zero_le_one],
    calc â€–f (x, y)â€– â‰¤ C * â€–xâ€– * â€–yâ€– : hC x y
    ... â‰¤ C * (â€–xâ€– + 1) * â€–yâ€– :
      by apply_rules [mul_le_mul_of_nonneg_right, norm_nonneg, mul_le_mul_of_nonneg_left,
                      le_of_lt C_pos]
  end }

lemma is_bounded_bilinear_map_smul {ğ•œ' : Type*} [normed_field ğ•œ']
  [normed_algebra ğ•œ ğ•œ'] {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E] [normed_space ğ•œ' E]
  [is_scalar_tower ğ•œ ğ•œ' E] :
  is_bounded_bilinear_map ğ•œ (Î» (p : ğ•œ' Ã— E), p.1 â€¢ p.2) :=
(lsmul ğ•œ ğ•œ' : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E).is_bounded_bilinear_map

lemma is_bounded_bilinear_map_mul :
  is_bounded_bilinear_map ğ•œ (Î» (p : ğ•œ Ã— ğ•œ), p.1 * p.2) :=
by simp_rw â† smul_eq_mul; exact is_bounded_bilinear_map_smul

lemma is_bounded_bilinear_map_comp :
  is_bounded_bilinear_map ğ•œ (Î» (p : (F â†’L[ğ•œ] G) Ã— (E â†’L[ğ•œ] F)), p.1.comp p.2) :=
(compL ğ•œ E F G).is_bounded_bilinear_map

lemma continuous_linear_map.is_bounded_linear_map_comp_left (g : F â†’L[ğ•œ] G) :
  is_bounded_linear_map ğ•œ (Î» (f : E â†’L[ğ•œ] F), continuous_linear_map.comp g f) :=
is_bounded_bilinear_map_comp.is_bounded_linear_map_right _

lemma continuous_linear_map.is_bounded_linear_map_comp_right (f : E â†’L[ğ•œ] F) :
  is_bounded_linear_map ğ•œ (Î» (g : F â†’L[ğ•œ] G), continuous_linear_map.comp g f) :=
is_bounded_bilinear_map_comp.is_bounded_linear_map_left _

lemma is_bounded_bilinear_map_apply :
  is_bounded_bilinear_map ğ•œ (Î» p : (E â†’L[ğ•œ] F) Ã— E, p.1 p.2) :=
(continuous_linear_map.flip (apply ğ•œ F : E â†’L[ğ•œ] (E â†’L[ğ•œ] F) â†’L[ğ•œ] F)).is_bounded_bilinear_map

/-- The function `continuous_linear_map.smul_right`, associating to a continuous linear map
`f : E â†’ ğ•œ` and a scalar `c : F` the tensor product `f âŠ— c` as a continuous linear map from `E` to
`F`, is a bounded bilinear map. -/
lemma is_bounded_bilinear_map_smul_right :
  is_bounded_bilinear_map ğ•œ
    (Î» p, (continuous_linear_map.smul_right : (E â†’L[ğ•œ] ğ•œ) â†’ F â†’ (E â†’L[ğ•œ] F)) p.1 p.2) :=
(smul_rightL ğ•œ E F).is_bounded_bilinear_map

/-- The composition of a continuous linear map with a continuous multilinear map is a bounded
bilinear operation. -/
lemma is_bounded_bilinear_map_comp_multilinear {Î¹ : Type*} {E : Î¹ â†’ Type*}
  [fintype Î¹] [âˆ€ i, normed_add_comm_group (E i)] [âˆ€ i, normed_space ğ•œ (E i)] :
  is_bounded_bilinear_map ğ•œ (Î» p : (F â†’L[ğ•œ] G) Ã— (continuous_multilinear_map ğ•œ E F),
    p.1.comp_continuous_multilinear_map p.2) :=
(comp_continuous_multilinear_mapL ğ•œ E F G).is_bounded_bilinear_map

/-- Definition of the derivative of a bilinear map `f`, given at a point `p` by
`q â†¦ f(p.1, q.2) + f(q.1, p.2)` as in the standard formula for the derivative of a product.
We define this function here as a linear map `E Ã— F â†’â‚—[ğ•œ] G`, then `is_bounded_bilinear_map.deriv`
strengthens it to a continuous linear map `E Ã— F â†’L[ğ•œ] G`.
``. -/
def is_bounded_bilinear_map.linear_deriv (h : is_bounded_bilinear_map ğ•œ f) (p : E Ã— F) :
  E Ã— F â†’â‚—[ğ•œ] G :=
{ to_fun := Î» q, f (p.1, q.2) + f (q.1, p.2),
  map_add' := Î» qâ‚ qâ‚‚, begin
    change f (p.1, qâ‚.2 + qâ‚‚.2) + f (qâ‚.1 + qâ‚‚.1, p.2) =
      f (p.1, qâ‚.2) + f (qâ‚.1, p.2) + (f (p.1, qâ‚‚.2) + f (qâ‚‚.1, p.2)),
    simp [h.add_left, h.add_right], abel
  end,
  map_smul' := Î» c q, begin
    change f (p.1, c â€¢ q.2) + f (c â€¢ q.1, p.2) = c â€¢ (f (p.1, q.2) + f (q.1, p.2)),
    simp [h.smul_left, h.smul_right, smul_add]
  end }

/-- The derivative of a bounded bilinear map at a point `p : E Ã— F`, as a continuous linear map
from `E Ã— F` to `G`. The statement that this is indeed the derivative of `f` is
`is_bounded_bilinear_map.has_fderiv_at` in `analysis.calculus.fderiv`. -/
def is_bounded_bilinear_map.deriv (h : is_bounded_bilinear_map ğ•œ f) (p : E Ã— F) : E Ã— F â†’L[ğ•œ] G :=
(h.linear_deriv p).mk_continuous_of_exists_bound $ begin
  rcases h.bound with âŸ¨C, Cpos, hCâŸ©,
  refine âŸ¨C * â€–p.1â€– + C * â€–p.2â€–, Î» q, _âŸ©,
  calc â€–f (p.1, q.2) + f (q.1, p.2)â€–
    â‰¤ C * â€–p.1â€– * â€–q.2â€– + C * â€–q.1â€– * â€–p.2â€– : norm_add_le_of_le (hC _ _) (hC _ _)
  ... â‰¤ C * â€–p.1â€– * â€–qâ€– + C * â€–qâ€– * â€–p.2â€– : begin
      apply add_le_add,
      exact mul_le_mul_of_nonneg_left
        (le_max_right _ _) (mul_nonneg (le_of_lt Cpos) (norm_nonneg _)),
      apply mul_le_mul_of_nonneg_right _ (norm_nonneg _),
      exact mul_le_mul_of_nonneg_left (le_max_left _ _) (le_of_lt Cpos),
  end
  ... = (C * â€–p.1â€– + C * â€–p.2â€–) * â€–qâ€– : by ring
end

@[simp] lemma is_bounded_bilinear_map_deriv_coe (h : is_bounded_bilinear_map ğ•œ f) (p q : E Ã— F) :
  h.deriv p q = f (p.1, q.2) + f (q.1, p.2) := rfl

variables (ğ•œ)

/-- The function `continuous_linear_map.mul_left_right : ğ•œ' Ã— ğ•œ' â†’ (ğ•œ' â†’L[ğ•œ] ğ•œ')` is a bounded
bilinear map. -/
lemma continuous_linear_map.mul_left_right_is_bounded_bilinear
  (ğ•œ' : Type*) [normed_ring ğ•œ'] [normed_algebra ğ•œ ğ•œ'] :
  is_bounded_bilinear_map ğ•œ (Î» p : ğ•œ' Ã— ğ•œ', continuous_linear_map.mul_left_right ğ•œ ğ•œ' p.1 p.2) :=
(continuous_linear_map.mul_left_right ğ•œ ğ•œ').is_bounded_bilinear_map

variables {ğ•œ}

/-- Given a bounded bilinear map `f`, the map associating to a point `p` the derivative of `f` at
`p` is itself a bounded linear map. -/
lemma is_bounded_bilinear_map.is_bounded_linear_map_deriv (h : is_bounded_bilinear_map ğ•œ f) :
  is_bounded_linear_map ğ•œ (Î» p : E Ã— F, h.deriv p) :=
begin
  rcases h.bound with âŸ¨C, Cpos : 0 < C, hCâŸ©,
  refine is_linear_map.with_bound âŸ¨Î» pâ‚ pâ‚‚, _, Î» c p, _âŸ© (C + C) (Î» p, _),
  { ext; simp only [h.add_left, h.add_right, coe_comp', function.comp_app, inl_apply,
    is_bounded_bilinear_map_deriv_coe, prod.fst_add, prod.snd_add, add_apply]; abel },
  { ext; simp only [h.smul_left, h.smul_right, smul_add, coe_comp', function.comp_app,
    is_bounded_bilinear_map_deriv_coe, prod.smul_fst, prod.smul_snd, coe_smul', pi.smul_apply] },
  { refine continuous_linear_map.op_norm_le_bound _
      (mul_nonneg (add_nonneg Cpos.le Cpos.le) (norm_nonneg _)) (Î» q, _),
    calc â€–f (p.1, q.2) + f (q.1, p.2)â€–
      â‰¤ C * â€–p.1â€– * â€–q.2â€– + C * â€–q.1â€– * â€–p.2â€– : norm_add_le_of_le (hC _ _) (hC _ _)
    ... â‰¤ C * â€–pâ€– * â€–qâ€– + C * â€–qâ€– * â€–pâ€– : by apply_rules [add_le_add, mul_le_mul, norm_nonneg,
      Cpos.le, le_refl, le_max_left, le_max_right, mul_nonneg]
    ... = (C + C) * â€–pâ€– * â€–qâ€– : by ring },
end

end bilinear_map

lemma continuous.clm_comp {X} [topological_space X] {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F}
  (hg : continuous g) (hf : continuous f) :
  continuous (Î» x, (g x).comp (f x)) :=
(compL ğ•œ E F G).continuousâ‚‚.compâ‚‚ hg hf

lemma continuous_on.clm_comp {X} [topological_space X] {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F}
  {s : set X} (hg : continuous_on g s) (hf : continuous_on f s) :
  continuous_on (Î» x, (g x).comp (f x)) s :=
(compL ğ•œ E F G).continuousâ‚‚.comp_continuous_on (hg.prod hf)

namespace continuous_linear_equiv

open set

/-!
### The set of continuous linear equivalences between two Banach spaces is open

In this section we establish that the set of continuous linear equivalences between two Banach
spaces is an open subset of the space of linear maps between them.
-/

protected lemma is_open [complete_space E] : is_open (range (coe : (E â‰ƒL[ğ•œ] F) â†’ (E â†’L[ğ•œ] F))) :=
begin
  rw [is_open_iff_mem_nhds, forall_range_iff],
  refine Î» e, is_open.mem_nhds _ (mem_range_self _),
  let O : (E â†’L[ğ•œ] F) â†’ (E â†’L[ğ•œ] E) := Î» f, (e.symm : F â†’L[ğ•œ] E).comp f,
  have h_O : continuous O := is_bounded_bilinear_map_comp.continuous_right,
  convert show is_open (O â»Â¹' {x | is_unit x}), from units.is_open.preimage h_O using 1,
  ext f',
  split,
  { rintros âŸ¨e', rflâŸ©,
    exact âŸ¨(e'.trans e.symm).to_unit, rflâŸ© },
  { rintros âŸ¨w, hwâŸ©,
    use (units_equiv ğ•œ E w).trans e,
    ext x,
    simp [coe_fn_coe_base' w, hw] }
end

protected lemma nhds [complete_space E] (e : E â‰ƒL[ğ•œ] F) :
  (range (coe : (E â‰ƒL[ğ•œ] F) â†’ (E â†’L[ğ•œ] F))) âˆˆ ğ“ (e : E â†’L[ğ•œ] F) :=
is_open.mem_nhds continuous_linear_equiv.is_open (by simp)

end continuous_linear_equiv
