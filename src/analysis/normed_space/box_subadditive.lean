/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury G. Kudryashov
-/
import data.real.ennreal
import topology.metric_space.basic
import linear_algebra.affine_space.ordered
import analysis.normed_space.add_torsor
import analysis.specific_limits
import analysis.asymptotics

/-!
# Sub/sup-additive functions on boxes

Let `s` be a set in `Î¹ â†’ â„`. A subbox of `s` is a product of closed intervals which is included
in `s`.

A function `f : (Î¹ â†’ â„) â†’ (Î¹ â†’ â„) â†’ M` is called
`box_subadditive_on`/`box_additive_on`/`box_supadditive_on` a set `s` if for any `l u : Î¹ â†’ â„`, `l â‰¤
u`, `Icc l u âŠ† s` and any hyperplane `x i = c`, `l i â‰¤ c â‰¤ u i`, the sum of the values of `f` on the
two subboxes `[l, u] âˆ© {x | x i â‰¤ c}` and `[l, u] âˆ© {x | c â‰¤ x i}` is greater than or
equal/equal/less than or equal to its value on `[l, u]`.

The main result of this file is theorem `box_subadditive_on.eq_zero_of_forall_is_o_prod`. It says
that `f l u = 0` provided that `f` is `box_subadditive_on` the interval `[l, u]`, `l â‰¤ u`, and
for any `p âˆˆ [l, u]` we have `f l' u' = o(volume [l', u'])` as both `l'` tends to `p` along
`[l, p]`, `u'` tends to `p` along `[p, u]`, and the subbox `[l', u']` is homothetic to `[l, u]`.
-/

variables {Î¹ Î± Î² M : Type*}

open set (univ pi Icc Ioi) function finset (hiding univ pi) filter
open_locale big_operators topological_space nnreal filter

/-!
### Definitions and basic properties

In this section we define `box_subadditive_on`, `box_additive_on`, and `box_supadditive_on`, and
prove some basic properties.
-/

/-- A function `f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M` is called `box_subadditive_on` a set `s : set (Î¹ â†’ Î±)`
if for any rectangular box `[l, u] âŠ† s` and a hyperplane `x i = c`, `l i â‰¤ c â‰¤ u i`,
we have `f' I â‰¤ f' (I âˆ© {x | x i â‰¤ c}) + f' (I âˆ© {x | c â‰¤ x i})`, where `I = [l, u]`, and
`f' [a, b]` means `f a b`. -/
def box_subadditive_on [decidable_eq Î¹] [preorder Î±] [ordered_add_comm_monoid M]
  (f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M) (s : set (Î¹ â†’ Î±)) :=
âˆ€ â¦ƒl uâ¦„ (hsub : Icc l u âŠ† s) â¦ƒm : Î¹ â†’ Î±â¦„ (hm : m âˆˆ Icc l u) i,
  f l u â‰¤ f l (update u i (m i)) + f (update l i (m i)) u

/-- A function `f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M` is called `box_additive_on` a set `s : set (Î¹ â†’ Î±)`
if for any rectangular box `[l, u] âŠ† s` and a hyperplane `x i = c`, `l i â‰¤ c â‰¤ u i`,
we have `f' (I âˆ© {x | x i â‰¤ c}) + f' (I âˆ© {x | c â‰¤ x i}) = f' I`, where `I = [l, u]`, and
`f' [a, b]` means `f a b`. -/
def box_additive_on [decidable_eq Î¹] [preorder Î±] [has_add M] (f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M)
  (s : set (Î¹ â†’ Î±)) :=
âˆ€ â¦ƒl uâ¦„ (hsub : Icc l u âŠ† s) â¦ƒm : Î¹ â†’ Î±â¦„ (hm : m âˆˆ Icc l u) i,
  f l (update u i (m i)) + f (update l i (m i)) u = f l u

/-- A function `f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M` is called `box_supadditive_on` a set `s : set (Î¹ â†’ Î±)`
if for any rectangular box `[l, u] âŠ† s` and a hyperplane `x i = c`, `l i â‰¤ c â‰¤ u i`,
we have `f' (I âˆ© {x | x i â‰¤ c}) + f' (I âˆ© {x | c â‰¤ x i}) â‰¤ f' I`, where `I = [l, u]`, and
`f' [a, b]` means `f a b`. -/
def box_supadditive_on [decidable_eq Î¹] [preorder Î±] [ordered_add_comm_monoid M]
  (f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M) (s : set (Î¹ â†’ Î±)) :=
âˆ€ â¦ƒl uâ¦„ (hsub : Icc l u âŠ† s) â¦ƒm : Î¹ â†’ Î±â¦„ (hm : m âˆˆ Icc l u) i,
  f l (update u i (m i)) + f (update l i (m i)) u â‰¤ f l u

namespace box_subadditive_on

variables [decidable_eq Î¹] [preorder Î±] [ordered_add_comm_monoid M] {s : set (Î¹ â†’ Î±)}
  {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M} {l m u : Î¹ â†’ Î±}

protected lemma mono (h : box_subadditive_on f s) {t} (ht : t âŠ† s) : box_subadditive_on f t :=
Î» l u hsub, h (set.subset.trans hsub ht)

lemma le_sum_finset_subboxes (h : box_subadditive_on f s) (hsub : Icc l u âŠ† s)
  (hm : m âˆˆ Icc l u) (t : finset Î¹) :
  f l u â‰¤ âˆ‘ t' in t.powerset, f (t'.piecewise m l) ((t \ t').piecewise m u) :=
begin
  induction t using finset.induction_on with j t hj iht, { simp },
  simp only [sum_powerset_insert hj, piecewise_insert, â† sum_add_distrib],
  refine iht.trans (sum_le_sum $ Î» t' ht', _),
  rw [mem_powerset] at ht',
  rw [insert_sdiff_of_not_mem _ (mt (@ht' _) hj), piecewise_insert,
    insert_sdiff_insert, sdiff_insert_of_not_mem hj],
  refine h (set.subset.trans (set.Icc_subset_Icc _ _) hsub) âŸ¨_, _âŸ© _;
    apply_rules [le_piecewise_of_le_of_le, piecewise_le_of_le_of_le, hm.1, hm.2, hm.1.trans hm.2];
    refl'
end

variables [fintype Î¹]

/-- Take a rectangular box `[lo, hi]` in `Î¹ â†’ Î±` and a point `mid âˆˆ [lo, hi]`. The hyperplanes `x i
= mid i` split the box `[lo, hi]` into `2^n` subboxes, where `n = card Î¹`.  If `f` is subadditive on
subboxes, then its value on `[lo, hi]` is less than or equal to the sum of its values on these `2^n`
subboxes. -/
lemma le_sum_subboxes (h : box_subadditive_on f s) (hsub : Icc l u âŠ† s) {m} (hm : m âˆˆ Icc l u) :
  f l u â‰¤ âˆ‘ t : finset Î¹, f (t.piecewise m l) (t.piecewise u m) :=
begin
  convert h.le_sum_finset_subboxes hsub hm finset.univ,
  ext t,
  rw [â† compl_eq_univ_sdiff, piecewise_compl]
end
/-
 TODO: why does `by simpa only [â† compl_eq_univ_sdiff, piecewise_compl]
    using h.le_sum_finset_subboxes hsub hm finset.univ`
 times out?
-/

end box_subadditive_on

namespace box_additive_on

variables {G : Type*} [decidable_eq Î¹] [preorder Î±] {s : set (Î¹ â†’ Î±)}

protected lemma congr [has_add G] {f g : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ G}
  (hf : box_additive_on f s) (hfg : âˆ€ â¦ƒl uâ¦„, l â‰¤ u â†’ Icc l u âŠ† s â†’ f l u = g l u) :
  box_additive_on g s :=
begin
  refine Î» l u hsub m hm i, _,
  have hle : l â‰¤ u := hm.1.trans hm.2,
  rw [â† hfg (hm.1.trans hm.2), â† hfg, â† hfg, hf hsub hm];
    apply_rules [le_update_iff.2, update_le_iff.2, hsub, trans (set.Icc_subset_Icc _ _) hsub,
      and.intro, hm.1, hm.2, hle, le_refl l, le_refl u]; intros; apply_rules [le_refl, hle]
end

protected lemma mono [has_add G] {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ G}
  (h : box_additive_on f s) {t} (ht : t âŠ† s) : box_additive_on f t :=
Î» l u hsub, h (set.subset.trans hsub ht)

lemma abs_of_nonneg [linear_ordered_add_comm_group G] {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ G}
  (h : box_additive_on f s) (hâ‚€ : âˆ€ {l u}, l â‰¤ u â†’ Icc l u âŠ† s â†’ 0 â‰¤ f l u) :
  box_additive_on (Î» x y, abs (f x y)) s :=
h.congr $ Î» l u hle hsub, (abs_of_nonneg $ hâ‚€ hle hsub).symm

lemma eq_zero_of_eq [add_left_cancel_monoid M] {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M}
  (h : box_additive_on f s) {l u i} (hle : l â‰¤ u) (hsub : Icc l u âŠ† s) (hi : l i = u i) :
  f l u = 0 :=
begin
  have := h hsub (set.left_mem_Icc.2 hle) i,
  rwa [update_eq_self, hi, update_eq_self, add_eq_left_iff] at this
end

protected lemma add [add_comm_semigroup M] {f g : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M}
  (hf : box_additive_on f s) (hg : box_additive_on g s) :
  box_additive_on (f + g) s :=
Î» l u h m hm i, by simp [hf h hm i, hg h hm i, add_add_add_comm _ (g _ _)]

protected lemma neg [add_comm_group G] {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ G} (hf : box_additive_on f s) :
  box_additive_on (-f) s :=
Î» l u h m hm i, by simp [â† hf h hm i, add_comm]

protected lemma sub [add_comm_group G] {f g : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ G}
  (hf : box_additive_on f s) (hg : box_additive_on g s) :
  box_additive_on (f - g) s :=
by simp only [sub_eq_add_neg, hf.add hg.neg]

protected lemma prod [fintype Î¹] {R} [comm_semiring R] (f : Î± â†’ Î± â†’ R)
  (hf : âˆ€ â¦ƒx y zâ¦„, x â‰¤ y â†’ y â‰¤ z â†’ f x y + f y z = f x z) :
  box_additive_on (Î» x y, âˆ i : Î¹, f (x i) (y i)) s :=
begin
  intros l u h m hm i,
  have := function.apply_update (Î» j, f (l j)) u i (m i),
  have := function.apply_update (Î» j y, f y (u j)) l i (m i),
  simp only at *,
  simp only [*, prod_update_of_mem, mem_univ, â† add_mul],
  rw [â† prod_mul_prod_compl {i}, prod_singleton, compl_eq_univ_sdiff, hf (hm.1 i) (hm.2 i)]
end

protected lemma box_subadditive_on [ordered_add_comm_monoid M] {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M}
  (hf : box_additive_on f s) : box_subadditive_on f s :=
Î» l u h m hm i, (hf h hm i).ge

protected lemma box_supadditive_on [ordered_add_comm_monoid M] {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M}
  (hf : box_additive_on f s) : box_supadditive_on f s :=
Î» l u h m hm i, (hf h hm i).le

lemma norm_subadditive_on {E : Type*} [normed_group E] {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ E}
  (hf : box_additive_on f s) : box_subadditive_on (Î» x y, âˆ¥f x yâˆ¥) s :=
Î» l u h m hm i, by simp only [â† hf h hm i, norm_add_le]

end box_additive_on

namespace box_supadditive_on

variables [decidable_eq Î¹] [preorder Î±] [ordered_add_comm_monoid M] {s : set (Î¹ â†’ Î±)}
  {l m u : Î¹ â†’ Î±} {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M}

protected lemma order_dual (hf : box_supadditive_on f s) :
  @box_subadditive_on Î¹ Î± (order_dual M) _ _ _ f s :=
hf

protected lemma mono (h : box_supadditive_on f s) {t} (ht : t âŠ† s) : box_supadditive_on f t :=
Î» l u hsub, h (set.subset.trans hsub ht)

lemma sum_finset_subboxes_le (h : box_supadditive_on f s) (hsub : Icc l u âŠ† s) (hm : m âˆˆ Icc l u)
  (t : finset Î¹) :
  âˆ‘ t' in t.powerset, f (t'.piecewise m l) ((t \ t').piecewise m u) â‰¤ f l u :=
h.order_dual.le_sum_finset_subboxes hsub hm t

variables [fintype Î¹]

/-- Take a rectangular box `[lo, hi]` in `Î¹ â†’ Î±` and a point `mid âˆˆ [lo, hi]`. The hyperplanes `x i
= mid i` split the box `[lo, hi]` into `2^n` subboxes, where `n = card Î¹`.  If `f` is supadditive on
subboxes, then its value on `[lo, hi]` is greater than or equal to the sum of its values on these
`2^n` subboxes. -/
lemma sum_subboxes_le (h : box_supadditive_on f s) (hsub : Icc l u âŠ† s) (hm : m âˆˆ Icc l u) :
  âˆ‘ t : finset Î¹, f (t.piecewise m l) (t.piecewise u m) â‰¤ f l u :=
h.order_dual.le_sum_subboxes hsub hm

end box_supadditive_on

section coe

variables {N : Type*} [decidable_eq Î¹] [preorder Î±] {s : set (Î¹ â†’ Î±)}

lemma box_subsupadditive_coe_helper [add_monoid M] [add_monoid N] {c : M â†’ N} (rM : M â†’ M â†’ Prop)
  (rN : N â†’ N â†’ Prop) (hr : âˆ€ x y, rN (c x) (c y) â†” rM x y)
  (hadd : âˆ€ x y, c (x + y) = c x + c y) {f : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ M} :
  (âˆ€ â¦ƒl u : Î¹ â†’ Î±â¦„ (h : Icc l u âŠ† s) â¦ƒm : Î¹ â†’ Î±â¦„ (hm : m âˆˆ Icc l u) i, rN (c $ f l u) $
    (c $ f l (update u i (m i))) + (c $ f (update l i (m i)) u)) â†”
  (âˆ€ â¦ƒl u : Î¹ â†’ Î±â¦„ (h : Icc l u âŠ† s) â¦ƒm : Î¹ â†’ Î±â¦„ (hm : m âˆˆ Icc l u) i, rM (f l u) $
    (f l (update u i (m i))) + (f (update l i (m i)) u)) :=
by simp only [â† hadd, hr]

variables {f g : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ â„â‰¥0}

@[simp, norm_cast]
lemma box_subadditive_on.coe_ennreal :
  box_subadditive_on (Î» x y, (f x y : ennreal)) s â†” box_subadditive_on f s :=
box_subsupadditive_coe_helper (â‰¤) (â‰¤) (Î» _ _, ennreal.coe_le_coe) (Î» _ _, ennreal.coe_add)

@[simp, norm_cast]
lemma box_additive_on.coe_ennreal :
  box_additive_on (Î» l r, (f l r : ennreal)) s â†” box_additive_on f s :=
box_subsupadditive_coe_helper (flip (=)) (flip (=)) (Î» _ _, ennreal.coe_eq_coe)
  (Î» _ _, ennreal.coe_add)

@[simp, norm_cast]
lemma box_supadditive_on.coe_ennreal :
  box_supadditive_on (Î» l r, (f l r : ennreal)) s â†” box_supadditive_on f s :=
box_subsupadditive_coe_helper (â‰¥) (â‰¥) (Î» _ _, ennreal.coe_le_coe) (Î» _ _, ennreal.coe_add)

@[simp, norm_cast]
lemma box_subadditive_on.coe_nnreal :
  box_subadditive_on (Î» x y, (f x y : â„)) s â†” box_subadditive_on f s :=
box_subsupadditive_coe_helper (â‰¤) (â‰¤) (Î» _ _, nnreal.coe_le_coe) nnreal.coe_add

@[simp, norm_cast]
lemma box_additive_on.coe_nnreal :
  box_additive_on (Î» l r, (f l r : â„)) s â†” box_additive_on f s :=
box_subsupadditive_coe_helper (flip (=)) (flip (=)) (Î» _ _, nnreal.coe_eq) nnreal.coe_add

@[simp, norm_cast]
lemma box_supadditive_on.coe_nnreal :
  box_supadditive_on (Î» l r, (f l r : â„)) s â†” box_supadditive_on f s :=
box_subsupadditive_coe_helper (â‰¥) (â‰¥) (Î» _ _, nnreal.coe_le_coe) nnreal.coe_add

end coe

/-!
### Examples of `box_additive`, `box_subadditive, and `box_supadditive` functions
-/

section

lemma box_additive_on_prod_sub [decidable_eq Î¹] [fintype Î¹] (s : set (Î¹ â†’ â„)) :
  box_additive_on (Î» l r, âˆ i, (r i - l i)) s :=
box_additive_on.prod (Î» x y : â„, y - x) $ Î» x y z _ _, sub_add_sub_cancel' _ _ _

lemma box_additive_on_prod_dist [decidable_eq Î¹] [fintype Î¹] (s : set (Î¹ â†’ â„)) :
  box_additive_on (Î» l r, âˆ i, dist (l i) (r i)) s :=
by simpa only [real.dist_eq, abs_prod, abs_sub]
  using (box_additive_on_prod_sub s).abs_of_nonneg
    (Î» l u h _, prod_nonneg (Î» i _, sub_nonneg.2 (h _)))

lemma box_additive_on_prod_nndist [decidable_eq Î¹] [fintype Î¹] (s : set (Î¹ â†’ â„)) :
  box_additive_on (Î» l r, âˆ i, nndist (l i) (r i)) s :=
by simpa only [â† box_additive_on.coe_nnreal, nnreal.coe_prod, coe_nndist]
  using box_additive_on_prod_dist s

lemma box_additive_on_prod_edist [decidable_eq Î¹] [fintype Î¹] (s : set (Î¹ â†’ â„)) :
  box_additive_on (Î» l r, âˆ i, edist (l i) (r i)) s :=
by simpa only [edist_nndist, â† ennreal.coe_finset_prod, box_additive_on.coe_ennreal]
  using box_additive_on_prod_nndist s

end

namespace box_subadditive_on

section preorder

variables [decidable_eq Î¹] [fintype Î¹] [preorder Î±]
  {s : set (Î¹ â†’ Î±)} {f g : (Î¹ â†’ Î±) â†’ (Î¹ â†’ Î±) â†’ ennreal} {l m u : Î¹ â†’ Î±}

lemma exists_subbox_mul_lt_of_mul_lt (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (hsub : Icc l u âŠ† s) (hm : m âˆˆ Icc l u) {c : ennreal} (hlt : c * g l u < f l u) :
  âˆƒ t : finset Î¹,
    c * g (t.piecewise m l) (t.piecewise u m) < f (t.piecewise m l) (t.piecewise u m) :=
begin
  contrapose! hlt,
  calc f l u â‰¤ âˆ‘ t : finset Î¹, f (t.piecewise m l) (t.piecewise u m) : hf.le_sum_subboxes hsub hm
  ... â‰¤ âˆ‘ t : finset Î¹, c * g (t.piecewise m l) (t.piecewise u m) : sum_le_sum (Î» t _, hlt t)
  ... = c * âˆ‘ t : finset Î¹, g (t.piecewise m l) (t.piecewise u m) : mul_sum.symm
  ... â‰¤ c * g l u : canonically_ordered_semiring.mul_le_mul_left' (hg.sum_subboxes_le hsub hm) c
end

end preorder

variables [decidable_eq Î¹] [fintype Î¹]

noncomputable theory

variables {s : set (Î¹ â†’ â„)} {l u : Î¹ â†’ â„}

section ennreal

variables {f g : (Î¹ â†’ â„) â†’ (Î¹ â†’ â„) â†’ ennreal} {c : ennreal}

/-- An auxiliary type for the proof of `box_subadditive_on.eq_zero_of_forall_eventually_le_mul`. -/
@[nolint has_inhabited_instance]
structure subbox_mul_lt (s : set (Î¹ â†’ â„)) (f g : (Î¹ â†’ â„) â†’ (Î¹ â†’ â„) â†’ ennreal) (c : ennreal) :=
(left right : Î¹ â†’ â„)
(le : left â‰¤ right)
(sub : Icc left right âŠ† s)
(mul_lt : c * g left right < f left right)

lemma subbox_mul_lt.midpoint_mem (I : subbox_mul_lt s f g c) :
  midpoint â„ I.left I.right âˆˆ Icc I.left I.right :=
âŸ¨left_le_midpoint.2 I.le, midpoint_le_right.2 I.leâŸ©

/-- An auxiliary definition for `box_subadditive_on.eq_zero_of_forall_eventually_le_mul`. -/
def next (hf : box_subadditive_on f s) (hg : box_supadditive_on g s) (I : subbox_mul_lt s f g c) :
  {I' : subbox_mul_lt s f g c // I.left â‰¤ I'.left âˆ§ I'.right â‰¤ I.right âˆ§
    âˆ€ i, I'.right i - I'.left i = (I.right i - I.left i) / 2} :=
begin
  obtain âŸ¨t, htâŸ© := classical.indefinite_description _
    (hf.exists_subbox_mul_lt_of_mul_lt hg I.sub I.midpoint_mem I.mul_lt),
  have hl : I.left â‰¤ t.piecewise (midpoint â„ I.left I.right) I.left :=
    t.le_piecewise_of_le_of_le I.midpoint_mem.1 le_rfl,
  have hr : t.piecewise I.right (midpoint â„ I.left I.right) â‰¤ I.right :=
    t.piecewise_le_of_le_of_le le_rfl I.midpoint_mem.2,
  refine âŸ¨âŸ¨_, _, _, set.subset.trans (set.Icc_subset_Icc hl hr) I.sub, htâŸ©, hl, hr, Î» i, _âŸ©,
  { exact t.piecewise_le_piecewise I.midpoint_mem.2 I.midpoint_mem.1 },
  { by_cases hi : i âˆˆ t; simp [hi, div_eq_inv_mul] }
end

/-- An auxiliary definition for `box_subadditive_on.eq_zero_of_forall_eventually_le_mul`:
a decreasing sequence of subboxes `[l n, u n]` such that `c * g (l n) (u n) < f (l u) (u n)`. -/
def seq (hf : box_subadditive_on f s) (hg : box_supadditive_on g s) (Iâ‚€ : subbox_mul_lt s f g c)
  (n : â„•) : subbox_mul_lt s f g c :=
(Î» I, hf.next hg I)^[n] Iâ‚€

/-- An auxiliary definition for `box_subadditive_on.eq_zero_of_forall_eventually_le_mul`:
the limit point of the sequence `box_subadditive_on.seq hf hg I hI`. -/
def fix (hf : box_subadditive_on f s) (hg : box_supadditive_on g s) (I : subbox_mul_lt s f g c) :
  Î¹ â†’ â„ :=
â¨† n, (seq hf hg I n).left

@[simp] lemma seq_zero (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  seq hf hg I 0 = I := rfl

@[simp] lemma seq_succ (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) (n : â„•) :
  seq hf hg I (n + 1) = next hf hg (seq hf hg I n) :=
iterate_succ_apply' _ _ _

@[simp] lemma seq_right_sub_left_apply (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) (n : â„•) (i : Î¹) :
  (seq hf hg I n).right i - (seq hf hg I n).left i = (I.right i - I.left i) / 2 ^ n :=
begin
  induction n with n ihn, { simp },
  rw [seq_succ, (next hf hg _).coe_prop.2.2, ihn, pow_succ', div_div_eq_div_mul]
end

@[simp] lemma seq_right_sub_left (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) (n : â„•) :
  (seq hf hg I n).right - (seq hf hg I n).left = ((1 / 2) ^ n : â„) â€¢ (I.right - I.left) :=
funext $ Î» i, (seq_right_sub_left_apply hf hg I n i).trans $
  by simp only [div_eq_inv_mul, pi.smul_apply, pi.sub_apply, smul_eq_mul, mul_one, inv_pow']

lemma dist_seq_left_right (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) (n : â„•) :
  dist (seq hf hg I n).left (seq hf hg I n).right = dist I.left I.right / 2 ^ n :=
by rw [dist_comm, dist_eq_norm, seq_right_sub_left, norm_smul, â† dist_eq_norm, dist_comm, mul_comm,
  one_div, inv_pow', normed_field.norm_inv, normed_field.norm_pow, real.norm_eq_abs,
  abs_of_pos (@zero_lt_two â„ _ _), div_eq_mul_inv]

lemma seq_left_mono (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  monotone (Î» n, (seq hf hg I n).left) :=
monotone_of_monotone_nat $ Î» n, by simp only [seq_succ, (next _ _ _).coe_prop.1]

lemma seq_right_mono (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  âˆ€ â¦ƒm nâ¦„, m â‰¤ n â†’ (seq hf hg I n).right â‰¤ (seq hf hg I m).right :=
begin
  refine @monotone_of_monotone_nat (order_dual (Î¹ â†’ â„)) _ _ (Î» n, _),
  rw seq_succ,
  exact (next _ _ _).coe_prop.2.1
end

lemma tendsto_dist_seq_left_right (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  tendsto (Î» n, dist (seq hf hg I n).left (seq hf hg I n).right) at_top (ğ“ 0) :=
begin
  simp only [dist_seq_left_right],
  exact tendsto_const_nhds.div_at_top (tendsto_pow_at_top_at_top_of_one_lt one_lt_two)
end

lemma fix_mem_Inter_seq (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c):
  fix hf hg I âˆˆ â‹‚ n, Icc (seq hf hg I n).left (seq hf hg I n).right :=
csupr_mem_Inter_Icc_of_mono_incr_of_mono_decr (seq_left_mono hf hg I) (seq_right_mono hf hg I) $
  Î» n, (seq hf hg I n).le

lemma fix_mem_seq (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) (n : â„•) :
  fix hf hg I âˆˆ Icc (seq hf hg I n).left (seq hf hg I n).right :=
have _ := set.mem_Inter.1 (fix_mem_Inter_seq hf hg I) n, this

lemma fix_mem (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  fix hf hg I âˆˆ Icc I.left I.right :=
fix_mem_seq hf hg I 0

lemma fix_mem_set (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  fix hf hg I âˆˆ s :=
I.sub $ fix_mem hf hg I

lemma tendsto_left_nhds_fix (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  tendsto (Î» n, (seq hf hg I n).left) at_top (ğ“ (fix hf hg I)) :=
begin
  refine (tendsto_iff_dist_tendsto_zero.2 $
    squeeze_zero (Î» _, dist_nonneg) (Î» n, _) (tendsto_dist_seq_left_right hf hg I)),
  refine (dist_pi_le_iff dist_nonneg).2 (Î» i, le_trans _ (dist_le_pi_dist _ _ i)),
  exact real.dist_left_le_of_mem_interval (set.Icc_subset_interval $
    âŸ¨(fix_mem_seq hf hg I _).1 _, (fix_mem_seq hf hg I _).2 _âŸ©)
end

lemma tendsto_left_nhds_within_fix (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  tendsto (Î» n, (seq hf hg I n).left) at_top (ğ“[Icc I.left (fix hf hg I)] (fix hf hg I)) :=
tendsto_inf.2 âŸ¨tendsto_left_nhds_fix hf hg I, tendsto_principal.2 $ eventually_of_forall $
  Î» n, âŸ¨seq_left_mono hf hg I (zero_le n), (fix_mem_seq hf hg I n).1âŸ©âŸ©

lemma tendsto_right_nhds_fix (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  tendsto (Î» n, (seq hf hg I n).right) at_top (ğ“ (fix hf hg I)) :=
(tendsto_left_nhds_fix hf hg I).congr_dist (tendsto_dist_seq_left_right hf hg I)

lemma tendsto_right_nhds_within_fix (hf : box_subadditive_on f s) (hg : box_supadditive_on g s)
  (I : subbox_mul_lt s f g c) :
  tendsto (Î» n, (seq hf hg I n).right) at_top (ğ“[Icc (fix hf hg I) I.right] (fix hf hg I)) :=
tendsto_inf.2 âŸ¨tendsto_right_nhds_fix hf hg I, tendsto_principal.2 $ eventually_of_forall $
  Î» n, âŸ¨(fix_mem_seq hf hg I n).2, seq_right_mono hf hg I (zero_le n)âŸ©âŸ©

lemma le_mul_of_forall_eventually_le_mul (hle : l â‰¤ u)
  (hf : box_subadditive_on f (Icc l u)) (hg : box_supadditive_on g (Icc l u))
  (Hc : âˆ€ (b âˆˆ Icc l u),
    âˆ€á¶  (p : ((Î¹ â†’ â„) Ã— (Î¹ â†’ â„)) Ã— â„) in ğ“[(Icc l b)] b Ã—á¶  ğ“[Icc b u] b Ã—á¶  ğ“[Ioi 0] (0:â„),
    (p.1.2 - p.1.1 = p.2 â€¢ (u - l)) â†’ f (p.1.1) p.1.2 â‰¤ c * g p.1.1 p.1.2) :
  f l u â‰¤ c * g l u :=
begin
  contrapose! Hc,
  set I : subbox_mul_lt (Icc l u) f g c := âŸ¨l, u, hle, refl _, HcâŸ©,
  refine âŸ¨_, fix_mem hf hg I, Î» H, _âŸ©,
  have : tendsto (Î» n : â„•, (1 / 2 : â„) ^ n) at_top (ğ“[Ioi 0] 0),
    from tendsto_pow_at_top_nhds_within_0_of_lt_1 one_half_pos one_half_lt_one,
  obtain âŸ¨n, hnâŸ© := ((((tendsto_left_nhds_within_fix hf hg I).prod_mk
    (tendsto_right_nhds_within_fix hf hg I)).prod_mk this).eventually H).exists,
  exact (hn (seq_right_sub_left hf hg I n)).not_lt (seq hf hg I n).mul_lt
end

/-- Let `Icc l u` (a.k.a. `[l, u]`) be a non-trivial interval in a finite-dimensional space
`Î¹ â†’ â„`. Suppose that `f` is an `ennreal`-valued function such that `box_subadditive_on f [l, u]`
and for any `p âˆˆ [l, u]` we have `f l' u' = o(volume [l', u'])` as `l'` tends to `p` along `[l, p]`,
`u'` tends to `p` along `[p, u]`, and the subbox `[l', u']` is homothetic to `[l, u]`.
Then `f l u = 0`. -/
lemma eq_zero_of_forall_eventually_le_mul (hle : l â‰¤ u) (hf : box_subadditive_on f (Icc l u))
  (hg : box_supadditive_on g (Icc l u)) (h_inf : g l u â‰  âŠ¤)
  (Hc : âˆ€ (b âˆˆ Icc l u) (c : â„â‰¥0), 0 < c â†’
    âˆ€á¶  (p : ((Î¹ â†’ â„) Ã— (Î¹ â†’ â„)) Ã— â„) in ğ“[Icc l b] b Ã—á¶  ğ“[Icc b u] b Ã—á¶  ğ“[Ioi 0] 0,
    (p.1.2 - p.1.1 = p.2 â€¢ (u - l)) â†’ f p.1.1 p.1.2 â‰¤ (c : â„â‰¥0) * g p.1.1 p.1.2) :
  f l u = 0 :=
begin
  by_contra h0,
  rcases ennreal.exists_nnreal_pos_mul_lt h_inf h0 with âŸ¨c, cpos, hcâŸ©,
  exact hc.not_le (le_mul_of_forall_eventually_le_mul hle hf hg $ Î» b hb, Hc b hb c cpos)
end

end ennreal

section normed_group

variables {E F : Type*} [normed_group E] [normed_group F]
  {f : (Î¹ â†’ â„) â†’ (Î¹ â†’ â„) â†’ E} {g : (Î¹ â†’ â„) â†’ (Î¹ â†’ â„) â†’ F}

open asymptotics function

lemma eq_zero_of_forall_is_o (hle : l â‰¤ u) (hf : box_subadditive_on (Î» x y, âˆ¥f x yâˆ¥) (Icc l u))
  (hg : box_supadditive_on (Î» x y, âˆ¥g x yâˆ¥) (Icc l u))
  (Hc : âˆ€ (b âˆˆ Icc l u), is_o (Î» p : _ Ã— â„, uncurry f p.1) (Î» p, uncurry g p.1)
    ((ğ“[Icc l b] b Ã—á¶  ğ“[Icc b u] b Ã—á¶  ğ“[Ioi 0] 0) âŠ“ ğ“Ÿ {p | p.1.2 - p.1.1 = p.2 â€¢ (u - l)}))
  : f l u = 0 :=
begin
  simp only [â† coe_nnnorm, coe_nnreal, â† coe_ennreal] at hf,
  simp only [â† coe_nnnorm, box_supadditive_on.coe_nnreal,
    â† box_supadditive_on.coe_ennreal] at hg,
  rw [â† nnnorm_eq_zero, â† ennreal.coe_eq_zero],
  refine eq_zero_of_forall_eventually_le_mul hle hf hg ennreal.coe_ne_top _,
  intros b hb c hc,
  simpa [â† coe_nnnorm, uncurry, â† nnreal.coe_mul, â† ennreal.coe_mul, eventually_inf_principal]
    using (Hc b hb).def hc
end

/-- Let `Icc l u` (a.k.a. `[l, u]`) be a non-trivial box in a finite-dimensional space
`Î¹ â†’ â„`. Suppose that `box_subadditive_on f [l, u]` and for any `p âˆˆ [l, u]` we have
`f l' u' = o(volume [l', u'])` as `l'` tends to `p` along `[l, p]`, `u'` tends to `p`
along `[p, u]`, and the subbox `[l', u']` is homothetic to `[l, u]`. Then `f l u = 0`. -/
lemma eq_zero_of_forall_is_o_prod (hle : l â‰¤ u)
  (hf : box_subadditive_on (Î» x y, âˆ¥f x yâˆ¥) (Icc l u))
  (Hc : âˆ€ (b âˆˆ Icc l u), is_o (Î» p : _ Ã— â„, uncurry f p.1) (Î» p, âˆ i, (p.1.2 i - p.1.1 i))
    ((ğ“[Icc l b] b Ã—á¶  ğ“[Icc b u] b Ã—á¶  ğ“[Ioi 0] 0) âŠ“ ğ“Ÿ {p | p.1.2 - p.1.1 = p.2 â€¢ (u - l)})) :
  f l u = 0 :=
begin
  have : box_supadditive_on (Î» l r, âˆ¥âˆ (i : Î¹), dist (l i) (r i)âˆ¥) (Icc l u) :=
    ((box_additive_on_prod_dist (Icc l u)).abs_of_nonneg
      (Î» _ _ _ _, prod_nonneg $ Î» _ _, dist_nonneg)).box_supadditive_on,
  refine eq_zero_of_forall_is_o hle hf this _,
  simpa only [dist_eq_norm', â† normed_field.norm_prod, uncurry, is_o_norm_right]
end

end normed_group

end box_subadditive_on
