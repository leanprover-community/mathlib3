/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.normed_space.operator_norm
import analysis.locally_convex.bounded

/-!
# Compact operators

In this file we define compact linear operators between two topological vector spaces (TVS).

## Main definitions

* `is_compact_operator` : predicate for compact operators

## Main statements

* `is_compact_operator_iff_is_compact_closure_image_closed_ball` : the usual characterization of
  compact operators from a normed space to a T2 TVS.
* `is_compact_operator.comp_clm` : precomposing a compact operator by a continuous linear map gives
  a compact operator
* `is_compact_operator.clm_comp` : postcomposing a compact operator by a continuous linear map
  gives a compact operator
* `is_compact_operator.continuous` : compact operators are automatically continuous
* `is_closed_set_of_is_compact_operator` : the set of compact operators is closed for the operator
  norm

## Implementation details

We define `is_compact_operator` as a predicate, because the space of compact operators inherits all
of its structure from the space of continuous linear maps (e.g we want to have the usual operator
norm on compact operators).

The two natural options then would be to make it a predicate over linear maps or continuous linear
maps. Instead we define it as a predicate over bare functions, although it really only makes sense
for linear functions, because Lean is really good at finding coercions to bare functions (whereas
coercing from continuous linear maps to linear maps often needs type ascriptions).

## References

* Bourbaki, *Spectral Theory*, chapters 3 to 5, to be published (2022)

## Tags

Compact operator
-/

open function set filter bornology metric

open_locale pointwise big_operators topology

/-- A compact operator between two topological vector spaces. This definition is usually
given as "there exists a neighborhood of zero whose image is contained in a compact set",
but we choose a definition which involves fewer existential quantifiers and replaces images
with preimages.

We prove the equivalence in `is_compact_operator_iff_exists_mem_nhds_image_subset_compact`. -/
def is_compact_operator {Mâ‚ Mâ‚‚ : Type*} [has_zero Mâ‚] [topological_space Mâ‚]
  [topological_space Mâ‚‚] (f : Mâ‚ â†’ Mâ‚‚) : Prop :=
âˆƒ K, is_compact K âˆ§ f â»Â¹' K âˆˆ (ğ“ 0 : filter Mâ‚)

lemma is_compact_operator_zero {Mâ‚ Mâ‚‚ : Type*} [has_zero Mâ‚] [topological_space Mâ‚]
  [topological_space Mâ‚‚] [has_zero Mâ‚‚] : is_compact_operator (0 : Mâ‚ â†’ Mâ‚‚) :=
âŸ¨{0}, is_compact_singleton, mem_of_superset univ_mem (Î» x _, rfl)âŸ©

section characterizations

section

variables {Râ‚ Râ‚‚ : Type*} [semiring Râ‚] [semiring Râ‚‚] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚} {Mâ‚ Mâ‚‚ : Type*}
  [topological_space Mâ‚] [add_comm_monoid Mâ‚] [topological_space Mâ‚‚]

lemma is_compact_operator_iff_exists_mem_nhds_image_subset_compact (f : Mâ‚ â†’ Mâ‚‚) :
  is_compact_operator f â†” âˆƒ V âˆˆ (ğ“ 0 : filter Mâ‚), âˆƒ (K : set Mâ‚‚), is_compact K âˆ§ f '' V âŠ† K :=
âŸ¨Î» âŸ¨K, hK, hKfâŸ©, âŸ¨f â»Â¹' K, hKf, K, hK, image_preimage_subset _ _âŸ©,
  Î» âŸ¨V, hV, K, hK, hVKâŸ©, âŸ¨K, hK, mem_of_superset hV (image_subset_iff.mp hVK)âŸ©âŸ©

lemma is_compact_operator_iff_exists_mem_nhds_is_compact_closure_image [t2_space Mâ‚‚]
  (f : Mâ‚ â†’ Mâ‚‚) :
  is_compact_operator f â†” âˆƒ V âˆˆ (ğ“ 0 : filter Mâ‚), is_compact (closure $ f '' V) :=
begin
  rw is_compact_operator_iff_exists_mem_nhds_image_subset_compact,
  exact âŸ¨Î» âŸ¨V, hV, K, hK, hKVâŸ©, âŸ¨V, hV, is_compact_closure_of_subset_compact hK hKVâŸ©,
    Î» âŸ¨V, hV, hVcâŸ©, âŸ¨V, hV, closure (f '' V), hVc, subset_closureâŸ©âŸ©,
end

end

section bounded

variables {ğ•œâ‚ ğ•œâ‚‚ : Type*} [nontrivially_normed_field ğ•œâ‚] [semi_normed_ring ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚}
  {Mâ‚ Mâ‚‚ : Type*} [topological_space Mâ‚] [add_comm_monoid Mâ‚] [topological_space Mâ‚‚]
  [add_comm_monoid Mâ‚‚] [module ğ•œâ‚ Mâ‚] [module ğ•œâ‚‚ Mâ‚‚] [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]

lemma is_compact_operator.image_subset_compact_of_vonN_bounded {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚}
  (hf : is_compact_operator f) {S : set Mâ‚} (hS : is_vonN_bounded ğ•œâ‚ S) :
  âˆƒ (K : set Mâ‚‚), is_compact K âˆ§ f '' S âŠ† K :=
let âŸ¨K, hK, hKfâŸ© := hf,
    âŸ¨r, hr, hrSâŸ© := hS hKf,
    âŸ¨c, hcâŸ© := normed_field.exists_lt_norm ğ•œâ‚ r,
    this := ne_zero_of_norm_ne_zero (hr.trans hc).ne.symm in
âŸ¨Ïƒâ‚â‚‚ c â€¢ K, hK.image $ continuous_id.const_smul (Ïƒâ‚â‚‚ c),
  by rw [image_subset_iff, preimage_smul_setâ‚›â‚— _ _ _ f this.is_unit]; exact hrS c hc.leâŸ©

lemma is_compact_operator.is_compact_closure_image_of_vonN_bounded [t2_space Mâ‚‚]
  {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : is_compact_operator f) {S : set Mâ‚}
  (hS : is_vonN_bounded ğ•œâ‚ S) : is_compact (closure $ f '' S) :=
let âŸ¨K, hK, hKfâŸ© := hf.image_subset_compact_of_vonN_bounded hS in
is_compact_closure_of_subset_compact hK hKf

end bounded

section normed_space

variables {ğ•œâ‚ ğ•œâ‚‚ : Type*} [nontrivially_normed_field ğ•œâ‚] [semi_normed_ring ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚}
  {Mâ‚ Mâ‚‚ Mâ‚ƒ : Type*} [seminormed_add_comm_group Mâ‚] [topological_space Mâ‚‚]
  [add_comm_monoid Mâ‚‚] [normed_space ğ•œâ‚ Mâ‚] [module ğ•œâ‚‚ Mâ‚‚]

lemma is_compact_operator.image_subset_compact_of_bounded [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : is_compact_operator f) {S : set Mâ‚} (hS : metric.bounded S) :
  âˆƒ (K : set Mâ‚‚), is_compact K âˆ§ f '' S âŠ† K :=
hf.image_subset_compact_of_vonN_bounded
(by rwa [normed_space.is_vonN_bounded_iff, â† metric.bounded_iff_is_bounded])

lemma is_compact_operator.is_compact_closure_image_of_bounded [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  [t2_space Mâ‚‚] {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : is_compact_operator f) {S : set Mâ‚}
  (hS : metric.bounded S) : is_compact (closure $ f '' S) :=
hf.is_compact_closure_image_of_vonN_bounded
(by rwa [normed_space.is_vonN_bounded_iff, â† metric.bounded_iff_is_bounded])

lemma is_compact_operator.image_ball_subset_compact [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : is_compact_operator f) (r : â„) :
  âˆƒ (K : set Mâ‚‚), is_compact K âˆ§ f '' metric.ball 0 r âŠ† K :=
hf.image_subset_compact_of_vonN_bounded (normed_space.is_vonN_bounded_ball ğ•œâ‚ Mâ‚ r)

lemma is_compact_operator.image_closed_ball_subset_compact [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : is_compact_operator f) (r : â„) :
  âˆƒ (K : set Mâ‚‚), is_compact K âˆ§ f '' metric.closed_ball 0 r âŠ† K :=
hf.image_subset_compact_of_vonN_bounded (normed_space.is_vonN_bounded_closed_ball ğ•œâ‚ Mâ‚ r)

lemma is_compact_operator.is_compact_closure_image_ball [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  [t2_space Mâ‚‚] {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : is_compact_operator f) (r : â„) :
  is_compact (closure $ f '' metric.ball 0 r) :=
hf.is_compact_closure_image_of_vonN_bounded (normed_space.is_vonN_bounded_ball ğ•œâ‚ Mâ‚ r)

lemma is_compact_operator.is_compact_closure_image_closed_ball [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  [t2_space Mâ‚‚] {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : is_compact_operator f) (r : â„) :
  is_compact (closure $ f '' metric.closed_ball 0 r) :=
hf.is_compact_closure_image_of_vonN_bounded (normed_space.is_vonN_bounded_closed_ball ğ•œâ‚ Mâ‚ r)

lemma is_compact_operator_iff_image_ball_subset_compact [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  (f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚) {r : â„} (hr : 0 < r) :
  is_compact_operator f â†” âˆƒ (K : set Mâ‚‚), is_compact K âˆ§ f '' metric.ball 0 r âŠ† K :=
âŸ¨Î» hf, hf.image_ball_subset_compact r,
  Î» âŸ¨K, hK, hKrâŸ©, (is_compact_operator_iff_exists_mem_nhds_image_subset_compact f).mpr
  âŸ¨metric.ball 0 r, ball_mem_nhds _ hr, K, hK, hKrâŸ©âŸ©

lemma is_compact_operator_iff_image_closed_ball_subset_compact [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  (f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚) {r : â„} (hr : 0 < r) :
  is_compact_operator f â†” âˆƒ (K : set Mâ‚‚), is_compact K âˆ§ f '' metric.closed_ball 0 r âŠ† K :=
âŸ¨Î» hf, hf.image_closed_ball_subset_compact r,
  Î» âŸ¨K, hK, hKrâŸ©, (is_compact_operator_iff_exists_mem_nhds_image_subset_compact f).mpr
  âŸ¨metric.closed_ball 0 r, closed_ball_mem_nhds _ hr, K, hK, hKrâŸ©âŸ©

lemma is_compact_operator_iff_is_compact_closure_image_ball [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚]
  [t2_space Mâ‚‚] (f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚) {r : â„} (hr : 0 < r) :
  is_compact_operator f â†” is_compact (closure $ f '' metric.ball 0 r) :=
âŸ¨Î» hf, hf.is_compact_closure_image_ball r,
  Î» hf, (is_compact_operator_iff_exists_mem_nhds_is_compact_closure_image f).mpr
  âŸ¨metric.ball 0 r, ball_mem_nhds _ hr, hfâŸ©âŸ©

lemma is_compact_operator_iff_is_compact_closure_image_closed_ball
  [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚] [t2_space Mâ‚‚] (f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚) {r : â„} (hr : 0 < r) :
  is_compact_operator f â†” is_compact (closure $ f '' metric.closed_ball 0 r) :=
âŸ¨Î» hf, hf.is_compact_closure_image_closed_ball r,
  Î» hf, (is_compact_operator_iff_exists_mem_nhds_is_compact_closure_image f).mpr
  âŸ¨metric.closed_ball 0 r, closed_ball_mem_nhds _ hr, hfâŸ©âŸ©

end normed_space

end characterizations

section operations

variables {Râ‚ Râ‚‚ Râ‚ƒ Râ‚„ : Type*} [semiring Râ‚] [semiring Râ‚‚] [comm_semiring Râ‚ƒ] [comm_semiring Râ‚„]
  {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚} {Ïƒâ‚â‚„ : Râ‚ â†’+* Râ‚„} {Ïƒâ‚ƒâ‚„ : Râ‚ƒ â†’+* Râ‚„} {Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„ : Type*}
  [topological_space Mâ‚] [add_comm_monoid Mâ‚] [topological_space Mâ‚‚] [add_comm_monoid Mâ‚‚]
  [topological_space Mâ‚ƒ] [add_comm_group Mâ‚ƒ] [topological_space Mâ‚„] [add_comm_group Mâ‚„]

lemma is_compact_operator.smul {S : Type*} [monoid S] [distrib_mul_action S Mâ‚‚]
  [has_continuous_const_smul S Mâ‚‚] {f : Mâ‚ â†’ Mâ‚‚}
  (hf : is_compact_operator f) (c : S) :
  is_compact_operator (c â€¢ f) :=
let âŸ¨K, hK, hKfâŸ© := hf in âŸ¨c â€¢ K, hK.image $ continuous_id.const_smul c,
  mem_of_superset hKf (Î» x hx, smul_mem_smul_set hx)âŸ©

lemma is_compact_operator.add [has_continuous_add Mâ‚‚] {f g : Mâ‚ â†’ Mâ‚‚}
  (hf : is_compact_operator f) (hg : is_compact_operator g) :
  is_compact_operator (f + g) :=
let âŸ¨A, hA, hAfâŸ© := hf, âŸ¨B, hB, hBgâŸ© := hg in
âŸ¨A + B, hA.add hB, mem_of_superset (inter_mem hAf hBg) (Î» x âŸ¨hxA, hxBâŸ©, set.add_mem_add hxA hxB)âŸ©

lemma is_compact_operator.neg [has_continuous_neg Mâ‚„] {f : Mâ‚ â†’ Mâ‚„}
  (hf : is_compact_operator f) : is_compact_operator (-f) :=
let âŸ¨K, hK, hKfâŸ© := hf in
âŸ¨-K, hK.neg, mem_of_superset hKf $ Î» x (hx : f x âˆˆ K), set.neg_mem_neg.mpr hxâŸ©

lemma is_compact_operator.sub [topological_add_group Mâ‚„] {f g : Mâ‚ â†’ Mâ‚„}
  (hf : is_compact_operator f) (hg : is_compact_operator g) : is_compact_operator (f - g) :=
by rw sub_eq_add_neg; exact hf.add hg.neg

variables (Ïƒâ‚â‚„ Mâ‚ Mâ‚„)

/-- The submodule of compact continuous linear maps. -/
def compact_operator [module Râ‚ Mâ‚] [module Râ‚„ Mâ‚„] [has_continuous_const_smul Râ‚„ Mâ‚„]
  [topological_add_group Mâ‚„] :
  submodule Râ‚„ (Mâ‚ â†’SL[Ïƒâ‚â‚„] Mâ‚„) :=
{ carrier := {f | is_compact_operator f},
  add_mem' := Î» f g hf hg, hf.add hg,
  zero_mem' := is_compact_operator_zero,
  smul_mem' := Î» c f hf, hf.smul c }

end operations

section comp

variables {Râ‚ Râ‚‚ Râ‚ƒ : Type*} [semiring Râ‚] [semiring Râ‚‚] [semiring Râ‚ƒ] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚}
  {Ïƒâ‚‚â‚ƒ : Râ‚‚ â†’+* Râ‚ƒ} {Mâ‚ Mâ‚‚ Mâ‚ƒ : Type*} [topological_space Mâ‚] [topological_space Mâ‚‚]
  [topological_space Mâ‚ƒ] [add_comm_monoid Mâ‚] [module Râ‚ Mâ‚]

lemma is_compact_operator.comp_clm [add_comm_monoid Mâ‚‚] [module Râ‚‚ Mâ‚‚] {f : Mâ‚‚ â†’ Mâ‚ƒ}
  (hf : is_compact_operator f) (g : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚) :
  is_compact_operator (f âˆ˜ g) :=
begin
  have := g.continuous.tendsto 0,
  rw map_zero at this,
  rcases hf with âŸ¨K, hK, hKfâŸ©,
  exact âŸ¨K, hK, this hKfâŸ©
end

lemma is_compact_operator.continuous_comp {f : Mâ‚ â†’ Mâ‚‚} (hf : is_compact_operator f) {g : Mâ‚‚ â†’ Mâ‚ƒ}
  (hg : continuous g) :
  is_compact_operator (g âˆ˜ f) :=
begin
  rcases hf with âŸ¨K, hK, hKfâŸ©,
  refine âŸ¨g '' K, hK.image hg, mem_of_superset hKf _âŸ©,
  nth_rewrite 1 preimage_comp,
  exact preimage_mono (subset_preimage_image _ _)
end

lemma is_compact_operator.clm_comp [add_comm_monoid Mâ‚‚] [module Râ‚‚ Mâ‚‚] [add_comm_monoid Mâ‚ƒ]
  [module Râ‚ƒ Mâ‚ƒ] {f : Mâ‚ â†’ Mâ‚‚} (hf : is_compact_operator f) (g : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ) :
  is_compact_operator (g âˆ˜ f) :=
hf.continuous_comp g.continuous

end comp

section cod_restrict

variables {Râ‚ Râ‚‚ : Type*} [semiring Râ‚] [semiring Râ‚‚] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚}
  {Mâ‚ Mâ‚‚ : Type*} [topological_space Mâ‚] [topological_space Mâ‚‚]
  [add_comm_monoid Mâ‚] [add_comm_monoid Mâ‚‚] [module Râ‚ Mâ‚] [module Râ‚‚ Mâ‚‚]

lemma is_compact_operator.cod_restrict {f : Mâ‚ â†’ Mâ‚‚} (hf : is_compact_operator f)
  {V : submodule Râ‚‚ Mâ‚‚} (hV : âˆ€ x, f x âˆˆ V) (h_closed : is_closed (V : set Mâ‚‚)):
  is_compact_operator (set.cod_restrict f V hV) :=
let âŸ¨K, hK, hKfâŸ© := hf in
âŸ¨coe â»Â¹' K, (closed_embedding_subtype_coe h_closed).is_compact_preimage hK, hKfâŸ©

end cod_restrict

section restrict

variables {Râ‚ Râ‚‚ Râ‚ƒ : Type*} [semiring Râ‚] [semiring Râ‚‚] [semiring Râ‚ƒ] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚}
  {Ïƒâ‚‚â‚ƒ : Râ‚‚ â†’+* Râ‚ƒ} {Mâ‚ Mâ‚‚ Mâ‚ƒ : Type*} [topological_space Mâ‚] [uniform_space Mâ‚‚]
  [topological_space Mâ‚ƒ] [add_comm_monoid Mâ‚] [add_comm_monoid Mâ‚‚] [add_comm_monoid Mâ‚ƒ]
  [module Râ‚ Mâ‚] [module Râ‚‚ Mâ‚‚] [module Râ‚ƒ Mâ‚ƒ]

/-- If a compact operator preserves a closed submodule, its restriction to that submodule is
compact.

Note that, following mathlib's convention in linear algebra, `restrict` designates the restriction
of an endomorphism `f : E â†’â‚— E` to an endomorphism `f' : â†¥V â†’â‚— â†¥V`. To prove that the restriction
`f' : â†¥U â†’â‚›â‚— â†¥V` of a compact operator `f : E â†’â‚›â‚— F` is compact, apply
`is_compact_operator.cod_restrict` to `f âˆ˜ U.subtypeL`, which is compact by
`is_compact_operator.comp_clm`. -/
lemma is_compact_operator.restrict {f : Mâ‚ â†’â‚—[Râ‚] Mâ‚} (hf : is_compact_operator f)
  {V : submodule Râ‚ Mâ‚} (hV : âˆ€ v âˆˆ V, f v âˆˆ V) (h_closed : is_closed (V : set Mâ‚)):
  is_compact_operator (f.restrict hV) :=
(hf.comp_clm V.subtypeL).cod_restrict (set_like.forall.2 hV) h_closed

/-- If a compact operator preserves a complete submodule, its restriction to that submodule is
compact.

Note that, following mathlib's convention in linear algebra, `restrict` designates the restriction
of an endomorphism `f : E â†’â‚— E` to an endomorphism `f' : â†¥V â†’â‚— â†¥V`. To prove that the restriction
`f' : â†¥U â†’â‚›â‚— â†¥V` of a compact operator `f : E â†’â‚›â‚— F` is compact, apply
`is_compact_operator.cod_restrict` to `f âˆ˜ U.subtypeL`, which is compact by
`is_compact_operator.comp_clm`. -/
lemma is_compact_operator.restrict' [separated_space Mâ‚‚] {f : Mâ‚‚ â†’â‚—[Râ‚‚] Mâ‚‚}
  (hf : is_compact_operator f) {V : submodule Râ‚‚ Mâ‚‚} (hV : âˆ€ v âˆˆ V, f v âˆˆ V)
  [hcomplete : complete_space V] : is_compact_operator (f.restrict hV) :=
hf.restrict hV (complete_space_coe_iff_is_complete.mp hcomplete).is_closed

end restrict

section continuous

variables {ğ•œâ‚ ğ•œâ‚‚ : Type*} [nontrivially_normed_field ğ•œâ‚] [nontrivially_normed_field ğ•œâ‚‚]
  {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚] {Mâ‚ Mâ‚‚ : Type*} [topological_space Mâ‚]
  [add_comm_group Mâ‚] [topological_space Mâ‚‚] [add_comm_group Mâ‚‚] [module ğ•œâ‚ Mâ‚] [module ğ•œâ‚‚ Mâ‚‚]
  [topological_add_group Mâ‚] [has_continuous_const_smul ğ•œâ‚ Mâ‚]
  [topological_add_group Mâ‚‚] [has_continuous_smul ğ•œâ‚‚ Mâ‚‚]

@[continuity] lemma is_compact_operator.continuous {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚}
  (hf : is_compact_operator f) : continuous f :=
begin
  letI : uniform_space Mâ‚‚ := topological_add_group.to_uniform_space _,
  haveI : uniform_add_group Mâ‚‚ := topological_add_comm_group_is_uniform,
  -- Since `f` is linear, we only need to show that it is continuous at zero.
  -- Let `U` be a neighborhood of `0` in `Mâ‚‚`.
  refine continuous_of_continuous_at_zero f (Î» U hU, _),
  rw map_zero at hU,
  -- The compactness of `f` gives us a compact set `K : set Mâ‚‚` such that `f â»Â¹' K` is a
  -- neighborhood of `0` in `Mâ‚`.
  rcases hf with âŸ¨K, hK, hKfâŸ©,
  -- But any compact set is totally bounded, hence Von-Neumann bounded. Thus, `K` absorbs `U`.
  -- This gives `r > 0` such that `âˆ€ a : ğ•œâ‚‚, r â‰¤ â€–aâ€– â†’ K âŠ† a â€¢ U`.
  rcases hK.totally_bounded.is_vonN_bounded ğ•œâ‚‚ hU with âŸ¨r, hr, hrUâŸ©,
  -- Choose `c : ğ•œâ‚‚` with `r < â€–câ€–`.
  rcases normed_field.exists_lt_norm ğ•œâ‚ r with âŸ¨c, hcâŸ©,
  have hcnz : c â‰  0 := ne_zero_of_norm_ne_zero (hr.trans hc).ne.symm,
  -- We have `f â»Â¹' ((Ïƒâ‚â‚‚ câ»Â¹) â€¢ K) = câ»Â¹ â€¢ f â»Â¹' K âˆˆ ğ“ 0`. Thus, showing that
  -- `(Ïƒâ‚â‚‚ câ»Â¹) â€¢ K âŠ† U` is enough to deduce that `f â»Â¹' U âˆˆ ğ“ 0`.
  suffices : (Ïƒâ‚â‚‚ $ câ»Â¹) â€¢ K âŠ† U,
  { refine mem_of_superset _ this,
    have : is_unit câ»Â¹ := hcnz.is_unit.inv,
    rwa [mem_map, preimage_smul_setâ‚›â‚— _ _ _ f this, set_smul_mem_nhds_zero_iff (inv_ne_zero hcnz)],
    apply_instance },
  -- Since `Ïƒâ‚â‚‚ câ»Â¹` = `(Ïƒâ‚â‚‚ c)â»Â¹`, we have to prove that `K âŠ† Ïƒâ‚â‚‚ c â€¢ U`.
  rw [map_invâ‚€, â† subset_set_smul_iffâ‚€ ((map_ne_zero Ïƒâ‚â‚‚).mpr hcnz)],
  -- But `Ïƒâ‚â‚‚` is isometric, so `â€–Ïƒâ‚â‚‚ câ€– = â€–câ€– > r`, which concludes the argument since
  -- `âˆ€ a : ğ•œâ‚‚, r â‰¤ â€–aâ€– â†’ K âŠ† a â€¢ U`.
  refine hrU (Ïƒâ‚â‚‚ c) _,
  rw ring_hom_isometric.is_iso,
  exact hc.le
end

/-- Upgrade a compact `linear_map` to a `continuous_linear_map`. -/
def continuous_linear_map.mk_of_is_compact_operator {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚}
  (hf : is_compact_operator f) : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚ :=
âŸ¨f, hf.continuousâŸ©

@[simp] lemma continuous_linear_map.mk_of_is_compact_operator_to_linear_map {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚}
  (hf : is_compact_operator f) :
  (continuous_linear_map.mk_of_is_compact_operator hf : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚) = f :=
rfl

@[simp] lemma continuous_linear_map.coe_mk_of_is_compact_operator {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚}
  (hf : is_compact_operator f) :
  (continuous_linear_map.mk_of_is_compact_operator hf : Mâ‚ â†’ Mâ‚‚) = f :=
rfl

lemma continuous_linear_map.mk_of_is_compact_operator_mem_compact_operator {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚}
  (hf : is_compact_operator f) :
  continuous_linear_map.mk_of_is_compact_operator hf âˆˆ compact_operator Ïƒâ‚â‚‚ Mâ‚ Mâ‚‚ :=
hf

end continuous

/-- The set of compact operators from a normed space to a complete topological vector space is
closed. -/
lemma is_closed_set_of_is_compact_operator {ğ•œâ‚ ğ•œâ‚‚ : Type*} [nontrivially_normed_field ğ•œâ‚]
  [normed_field ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚} {Mâ‚ Mâ‚‚ : Type*} [seminormed_add_comm_group Mâ‚]
  [add_comm_group Mâ‚‚] [normed_space ğ•œâ‚ Mâ‚] [module ğ•œâ‚‚ Mâ‚‚] [uniform_space Mâ‚‚] [uniform_add_group Mâ‚‚]
  [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚] [t2_space Mâ‚‚] [complete_space Mâ‚‚] :
  is_closed {f : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚ | is_compact_operator f} :=
begin
  refine is_closed_of_closure_subset _,
  rintros u hu,
  rw [mem_closure_iff_nhds_zero] at hu,
  suffices : totally_bounded (u '' metric.closed_ball 0 1),
  { change is_compact_operator (u : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚),
    rw is_compact_operator_iff_is_compact_closure_image_closed_ball (u : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚)
      zero_lt_one,
    exact is_compact_of_totally_bounded_is_closed this.closure is_closed_closure },
  rw totally_bounded_iff_subset_finite_Union_nhds_zero,
  intros U hU,
  rcases exists_nhds_zero_half hU with âŸ¨V, hV, hVUâŸ©,
  let SV : set Mâ‚ Ã— set Mâ‚‚ := âŸ¨closed_ball 0 1, -VâŸ©,
  rcases hu {f | âˆ€ x âˆˆ SV.1, f x âˆˆ SV.2} (continuous_linear_map.has_basis_nhds_zero.mem_of_mem
    âŸ¨normed_space.is_vonN_bounded_closed_ball _ _ _, neg_mem_nhds_zero Mâ‚‚ hVâŸ©) with âŸ¨v, hv, huvâŸ©,
  rcases totally_bounded_iff_subset_finite_Union_nhds_zero.mp
    (hv.is_compact_closure_image_closed_ball 1).totally_bounded V hV with âŸ¨T, hT, hTvâŸ©,
  have hTv : v '' closed_ball 0 1 âŠ† _ := subset_closure.trans hTv,
  refine âŸ¨T, hT, _âŸ©,
  rw [image_subset_iff, preimage_Unionâ‚‚] at âŠ¢ hTv,
  intros x hx,
  specialize hTv hx,
  rw [mem_Unionâ‚‚] at âŠ¢ hTv,
  rcases hTv with âŸ¨t, ht, htxâŸ©,
  refine âŸ¨t, ht, _âŸ©,
  rw [mem_preimage, mem_vadd_set_iff_neg_vadd_mem, vadd_eq_add, neg_add_eq_sub] at âŠ¢ htx,
  convert hVU _ htx _ (huv x hx) using 1,
  rw [continuous_linear_map.sub_apply],
  abel
end

lemma compact_operator_topological_closure {ğ•œâ‚ ğ•œâ‚‚ : Type*} [nontrivially_normed_field ğ•œâ‚]
  [normed_field ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚} {Mâ‚ Mâ‚‚ : Type*}
  [seminormed_add_comm_group Mâ‚] [add_comm_group Mâ‚‚] [normed_space ğ•œâ‚ Mâ‚] [module ğ•œâ‚‚ Mâ‚‚]
  [uniform_space Mâ‚‚] [uniform_add_group Mâ‚‚] [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚] [t2_space Mâ‚‚]
  [complete_space Mâ‚‚] [has_continuous_smul ğ•œâ‚‚ (Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚)] :
  (compact_operator Ïƒâ‚â‚‚ Mâ‚ Mâ‚‚).topological_closure = compact_operator Ïƒâ‚â‚‚ Mâ‚ Mâ‚‚ :=
set_like.ext' (is_closed_set_of_is_compact_operator.closure_eq)

lemma is_compact_operator_of_tendsto {Î¹ ğ•œâ‚ ğ•œâ‚‚ : Type*} [nontrivially_normed_field ğ•œâ‚]
  [normed_field ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚} {Mâ‚ Mâ‚‚ : Type*}
  [seminormed_add_comm_group Mâ‚] [add_comm_group Mâ‚‚] [normed_space ğ•œâ‚ Mâ‚] [module ğ•œâ‚‚ Mâ‚‚]
  [uniform_space Mâ‚‚] [uniform_add_group Mâ‚‚] [has_continuous_const_smul ğ•œâ‚‚ Mâ‚‚] [t2_space Mâ‚‚]
  [complete_space Mâ‚‚] {l : filter Î¹} [l.ne_bot] {F : Î¹ â†’ Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚} {f : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚}
  (hf : tendsto F l (ğ“ f)) (hF : âˆ€á¶  i in l, is_compact_operator (F i)) :
  is_compact_operator f :=
is_closed_set_of_is_compact_operator.mem_of_tendsto hf hF
