/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury Kudryashov
-/
import analysis.normed_space.banach
import analysis.normed_space.finite_dimension

/-!
# Complemented subspaces of normed vector spaces

A submodule `p` of a topological module `E` over `R` is called *complemented* if there exists
a continuous linear projection `f : E â†’â‚—[R] p`, `âˆ€ x : p, f x = x`. We prove that for
a closed subspace of a normed space this condition is equivalent to existence of a closed
subspace `q` such that `p âŠ“ q = âŠ¥`, `p âŠ” q = âŠ¤`. We also prove that a subspace of finite codimension
is always a complemented subspace.

## Tags

complemented subspace, normed vector space
-/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {F : Type*} [normed_group F] [normed_space ğ•œ F]

noncomputable theory

namespace continuous_linear_map

variables [complete_space ğ•œ]

lemma ker_closed_complemented_of_finite_dimensional_range (f : E â†’L[ğ•œ] F)
  [finite_dimensional ğ•œ f.range] :
  f.ker.closed_complemented :=
begin
  set f' : E â†’L[ğ•œ] f.range := f.cod_restrict _ (f : E â†’â‚—[ğ•œ] F).mem_range_self,
  rcases f'.exists_right_inverse_of_surjective (f : E â†’â‚—[ğ•œ] F).range_range_restrict with âŸ¨g, hgâŸ©,
  simpa only [ker_cod_restrict] using f'.closed_complemented_ker_of_right_inverse g (ext_iff.1 hg)
end

end continuous_linear_map

namespace subspace

variables [complete_space E] (p q : subspace ğ•œ E)

open continuous_linear_map (subtype_val)

/-- If `q` is a closed complement of a closed subspace `p`, then `p Ã— q` is continuously
isomorphic to `E`. -/
def prod_equiv_of_closed_compl (h : is_compl p q) (hp : is_closed (p : set E))
  (hq : is_closed (q : set E)) : (p Ã— q) â‰ƒL[ğ•œ] E :=
begin
  haveI := hp.complete_space_coe, haveI := hq.complete_space_coe,
  refine (p.prod_equiv_of_is_compl q h).to_continuous_linear_equiv_of_continuous _,
  exact ((subtype_val p).coprod (subtype_val q)).continuous
end

/-- Projection to a closed submodule along a closed complement. -/
def linear_proj_of_closed_compl (h : is_compl p q) (hp : is_closed (p : set E))
  (hq : is_closed (q : set E)) :
  E â†’L[ğ•œ] p :=
(continuous_linear_map.fst ğ•œ p q).comp $ (prod_equiv_of_closed_compl p q h hp hq).symm

variables {p q}

@[simp] lemma coe_prod_equiv_of_closed_compl (h : is_compl p q) (hp : is_closed (p : set E))
  (hq : is_closed (q : set E)) :
  â‡‘(p.prod_equiv_of_closed_compl q h hp hq) = p.prod_equiv_of_is_compl q h := rfl

@[simp] lemma coe_prod_equiv_of_closed_compl_symm (h : is_compl p q) (hp : is_closed (p : set E))
  (hq : is_closed (q : set E)) :
  â‡‘(p.prod_equiv_of_closed_compl q h hp hq).symm = (p.prod_equiv_of_is_compl q h).symm := rfl

@[simp] lemma coe_continuous_linear_proj_of_closed_compl (h : is_compl p q)
  (hp : is_closed (p : set E)) (hq : is_closed (q : set E)) :
  (p.linear_proj_of_closed_compl q h hp hq : E â†’â‚—[ğ•œ] p) = p.linear_proj_of_is_compl q h := rfl

@[simp] lemma coe_continuous_linear_proj_of_closed_compl' (h : is_compl p q)
  (hp : is_closed (p : set E)) (hq : is_closed (q : set E)) :
  â‡‘(p.linear_proj_of_closed_compl q h hp hq) = p.linear_proj_of_is_compl q h := rfl

lemma closed_complemented_of_closed_compl (h : is_compl p q) (hp : is_closed (p : set E))
  (hq : is_closed (q : set E)) : p.closed_complemented :=
âŸ¨p.linear_proj_of_closed_compl q h hp hq, submodule.linear_proj_of_is_compl_apply_left hâŸ©

lemma closed_complemented_iff_has_closed_compl : p.closed_complemented â†”
  is_closed (p : set E) âˆ§ âˆƒ (q : subspace ğ•œ E) (hq : is_closed (q : set E)), is_compl p q :=
âŸ¨Î» h, âŸ¨h.is_closed, h.has_closed_complementâŸ©,
  Î» âŸ¨hp, âŸ¨q, hq, hpqâŸ©âŸ©, closed_complemented_of_closed_compl hpq hp hqâŸ©

lemma closed_complemented_of_quotient_finite_dimensional [complete_space ğ•œ]
  [finite_dimensional ğ•œ p.quotient] (hp : is_closed (p : set E)) :
  p.closed_complemented :=
begin
  obtain âŸ¨q, hqâŸ© : âˆƒ q, is_compl p q := p.exists_is_compl,
  haveI : finite_dimensional ğ•œ q := (p.quotient_equiv_of_is_compl q hq).finite_dimensional,
  exact closed_complemented_of_closed_compl hq hp q.closed_of_finite_dimensional
end

end subspace

namespace continuous_linear_map

/-- If `f : E â†’ F` is a surjective continuous linear map between two Banach spaces
and `g : E â†’ ker f` is a continuous linear projection onto `ker f`, then `x â†¦ (g x, f x)`
defines a continuous linear equivalence between `E` and `F Ã— ker f`. -/
def equiv_prod_of_proj_ker_of_surjective [complete_space E] [complete_space F]
  {f : E â†’L[ğ•œ] F} (g : E â†’L[ğ•œ] f.ker) (hg : âˆ€ x : f.ker, g x = x) (hf : f.range = âŠ¤) :
  E â‰ƒL[ğ•œ] (F Ã— f.ker) :=
linear_equiv.to_continuous_linear_equiv_of_continuous
  ((g : E â†’â‚—[ğ•œ] f.ker).equiv_prod_of_proj_ker_of_surjective hg hf)
  (f.continuous.prod_mk g.continuous)

@[simp] lemma equiv_prod_of_proj_ker_of_surjective_symm_apply [complete_space E] [complete_space F]
  {f : E â†’L[ğ•œ] F} (g : E â†’L[ğ•œ] f.ker) (hg : âˆ€ x : f.ker, g x = x) (hf : f.range = âŠ¤) (x : E) :
  equiv_prod_of_proj_ker_of_surjective g hg hf x = (f x, g x) := rfl

end continuous_linear_map
