/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.normed.group.completion
import analysis.normed_space.operator_norm
import topology.algebra.uniform_ring

/-!
# Normed space structure on the completion of a normed space

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

If `E` is a normed space over `ğ•œ`, then so is `uniform_space.completion E`. In this file we provide
necessary instances and define `uniform_space.completion.to_complâ‚—áµ¢` - coercion
`E â†’ uniform_space.completion E` as a bundled linear isometry.

We also show that if `A` is a normed algebra over `ğ•œ`, then so is `uniform_space.completion A`.

TODO: Generalise the results here from the concrete `completion` to any `abstract_completion`.
-/

noncomputable theory

namespace uniform_space
namespace completion

variables (ğ•œ E : Type*) [normed_field ğ•œ] [normed_add_comm_group E] [normed_space ğ•œ E]

@[priority 100]
instance normed_space.to_has_uniform_continuous_const_smul :
  has_uniform_continuous_const_smul ğ•œ E :=
âŸ¨Î» c, (lipschitz_with_smul c).uniform_continuousâŸ©

instance : normed_space ğ•œ (completion E) :=
{ smul := (â€¢),
  norm_smul_le := Î» c x, induction_on x
    (is_closed_le (continuous_const_smul _).norm (continuous_const.mul continuous_norm)) $
    Î» y, by simp only [â† coe_smul, norm_coe, norm_smul],
  .. completion.module }

variables {ğ•œ E}

/-- Embedding of a normed space to its completion as a linear isometry. -/
def to_complâ‚—áµ¢ : E â†’â‚—áµ¢[ğ•œ] completion E :=
{ to_fun := coe,
  map_smul' := coe_smul,
  norm_map' := norm_coe,
  .. to_compl }

@[simp] lemma coe_to_complâ‚—áµ¢ : â‡‘(to_complâ‚—áµ¢ : E â†’â‚—áµ¢[ğ•œ] completion E) = coe := rfl

/-- Embedding of a normed space to its completion as a continuous linear map. -/
def to_complL : E â†’L[ğ•œ] completion E :=
to_complâ‚—áµ¢.to_continuous_linear_map

@[simp] lemma coe_to_complL : â‡‘(to_complL : E â†’L[ğ•œ] completion E) = coe := rfl

@[simp] lemma norm_to_complL {ğ•œ E : Type*} [nontrivially_normed_field ğ•œ] [normed_add_comm_group E]
  [normed_space ğ•œ E] [nontrivial E] : â€–(to_complL : E â†’L[ğ•œ] completion E)â€– = 1 :=
(to_complâ‚—áµ¢ : E â†’â‚—áµ¢[ğ•œ] completion E).norm_to_continuous_linear_map

section algebra

variables (ğ•œ) (A : Type*)

instance [semi_normed_ring A] : normed_ring (completion A) :=
{ dist_eq := Î» x y,
  begin
    apply completion.induction_onâ‚‚ x y; clear x y,
    { refine is_closed_eq (completion.uniform_continuous_extensionâ‚‚ _).continuous _,
      exact continuous.comp completion.continuous_extension continuous_sub },
    { intros x y,
      rw [â† completion.coe_sub, norm_coe, completion.dist_eq, dist_eq_norm] }
  end,
  norm_mul := Î» x y,
  begin
    apply completion.induction_onâ‚‚ x y; clear x y,
    { exact is_closed_le (continuous.comp (continuous_norm) continuous_mul) (continuous.comp
        real.continuous_mul (continuous.prod_map continuous_norm continuous_norm)) },
    { intros x y,
      simp only [â† coe_mul, norm_coe], exact norm_mul_le x y, }
  end,
  ..completion.ring,
  ..completion.metric_space }

instance [semi_normed_comm_ring A] [normed_algebra ğ•œ A] [has_uniform_continuous_const_smul ğ•œ A] :
  normed_algebra ğ•œ (completion A) :=
{ norm_smul_le := Î» r x,
  begin
    apply completion.induction_on x; clear x,
    { exact is_closed_le (continuous.comp (continuous_norm) (continuous_const_smul r))
      (continuous.comp (continuous_mul_left _) continuous_norm), },
    { intros x,
      simp only [â† coe_smul, norm_coe], exact norm_smul_le r x }
  end,
  ..completion.algebra A ğ•œ}

end algebra

end completion
end uniform_space
