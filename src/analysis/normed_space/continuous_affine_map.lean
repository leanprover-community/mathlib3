/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import topology.algebra.continuous_affine_map
import analysis.normed_space.add_torsor
import analysis.normed_space.affine_isometry
import analysis.normed_space.operator_norm

/-!
# Continuous affine maps between normed spaces.

This file develops the theory of continuous affine maps between affine spaces modelled on normed
spaces.

In the particular case that the affine spaces are just normed vector spaces `V`, `W`, we define a
norm on the space of continuous affine maps by defining the norm of `f : V â†’A[ğ•œ] W` to be
`âˆ¥fâˆ¥ = max âˆ¥f 0âˆ¥ âˆ¥f.cont_linearâˆ¥`. This is chosen so that we have a linear isometry:
`(V â†’A[ğ•œ] W) â‰ƒâ‚—áµ¢[ğ•œ] W Ã— (V â†’L[ğ•œ] W)`.

The abstract picture is that for an affine space `P` modelled on a vector space `V`, together with
a vector space `W`, there is an exact sequence of `ğ•œ`-modules: `0 â†’ C â†’ A â†’ L â†’ 0` where `C`, `A`
are the spaces of constant and affine maps `P â†’ W` and `L` is the space of linear maps `V â†’ W`.

Any choice of a base point in `P` corresponds to a splitting of this sequence so in particular if we
take `P = V`, using `0 : V` as the base point provides a splitting, and we prove this is an
isometric decomposition.

On the other hand, choosing a base point breaks the affine invariance so the norm fails to be
submultiplicative: for a composition of maps, we have only `âˆ¥f.comp gâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥ + âˆ¥f 0âˆ¥`.

## Main definitions:

 * `continuous_affine_map.cont_linear`
 * `continuous_affine_map.has_norm`
 * `continuous_affine_map.norm_comp_le`
 * `continuous_affine_map.to_const_prod_continuous_linear_map`

-/

namespace continuous_affine_map

variables {ğ•œ R V W Wâ‚‚ P Q Qâ‚‚ : Type*}
variables [normed_group V] [metric_space P] [normed_add_torsor V P]
variables [normed_group W] [metric_space Q] [normed_add_torsor W Q]
variables [normed_group Wâ‚‚] [metric_space Qâ‚‚] [normed_add_torsor Wâ‚‚ Qâ‚‚]
variables [normed_field R] [normed_space R V] [normed_space R W] [normed_space R Wâ‚‚]
variables [nondiscrete_normed_field ğ•œ] [normed_space ğ•œ V] [normed_space ğ•œ W] [normed_space ğ•œ Wâ‚‚]

include V W

/-- The linear map underlying a continuous affine map is continuous. -/
def cont_linear (f : P â†’A[R] Q) : V â†’L[R] W :=
{ to_fun := f.linear,
  cont   := by { rw affine_map.continuous_linear_iff, exact f.cont, },
  .. f.linear, }

@[simp] lemma coe_cont_linear (f : P â†’A[R] Q) :
  (f.cont_linear : V â†’ W) = f.linear :=
rfl

@[simp] lemma coe_cont_linear_eq_linear (f : P â†’A[R] Q) :
  (f.cont_linear : V â†’â‚—[R] W) = (f : P â†’áµƒ[R] Q).linear :=
by { ext, refl, }

@[simp] lemma coe_mk_const_linear_eq_linear (f : P â†’áµƒ[R] Q) (h) :
  ((âŸ¨f, hâŸ© : P â†’A[R] Q).cont_linear : V â†’ W) = f.linear :=
rfl

lemma coe_linear_eq_coe_cont_linear (f : P â†’A[R] Q) :
  ((f : P â†’áµƒ[R] Q).linear : V â†’ W) = (â‡‘f.cont_linear : V â†’ W) :=
rfl

include Wâ‚‚

@[simp] lemma comp_cont_linear (f : P â†’A[R] Q) (g : Q â†’A[R] Qâ‚‚) :
  (g.comp f).cont_linear = g.cont_linear.comp f.cont_linear :=
rfl

omit Wâ‚‚

@[simp] lemma map_vadd (f : P â†’A[R] Q) (p : P) (v : V) :
  f (v +áµ¥ p) = f.cont_linear v +áµ¥ f p :=
f.map_vadd' p v

@[simp] lemma cont_linear_map_vsub (f : P â†’A[R] Q) (pâ‚ pâ‚‚ : P) :
  f.cont_linear (pâ‚ -áµ¥ pâ‚‚) = f pâ‚ -áµ¥ f pâ‚‚ :=
f.to_affine_map.linear_map_vsub pâ‚ pâ‚‚

@[simp] lemma const_cont_linear (q : Q) : (const R P q).cont_linear = 0 := rfl

lemma cont_linear_eq_zero_iff_exists_const (f : P â†’A[R] Q) :
  f.cont_linear = 0 â†” âˆƒ q, f = const R P q :=
begin
  have hâ‚ : f.cont_linear = 0 â†” (f : P â†’áµƒ[R] Q).linear = 0,
  { refine âŸ¨Î» h, _, Î» h, _âŸ©;
    ext,
    { rw [â† coe_cont_linear_eq_linear, h], refl, },
    { rw [â† coe_linear_eq_coe_cont_linear, h], refl, }, },
  have hâ‚‚ : âˆ€ (q : Q), f = const R P q â†” (f : P â†’áµƒ[R] Q) = affine_map.const R P q,
  { intros q,
    refine âŸ¨Î» h, _, Î» h, _âŸ©;
    ext,
    { rw h, refl, },
    { rw [â† coe_to_affine_map, h], refl, }, },
  simp_rw [hâ‚, hâ‚‚],
  exact (f : P â†’áµƒ[R] Q).linear_eq_zero_iff_exists_const,
end

@[simp] lemma to_affine_map_cont_linear (f : V â†’L[R] W) :
  f.to_continuous_affine_map.cont_linear = f :=
by { ext, refl, }

@[simp] lemma zero_cont_linear :
  (0 : P â†’A[R] W).cont_linear = 0 :=
rfl

@[simp] lemma add_cont_linear (f g : P â†’A[R] W) :
  (f + g).cont_linear = f.cont_linear + g.cont_linear :=
rfl

@[simp] lemma sub_cont_linear (f g : P â†’A[R] W) :
  (f - g).cont_linear = f.cont_linear - g.cont_linear :=
rfl

@[simp] lemma neg_cont_linear (f : P â†’A[R] W) :
  (-f).cont_linear = -f.cont_linear :=
rfl

@[simp] lemma smul_cont_linear (t : R) (f : P â†’A[R] W) :
  (t â€¢ f).cont_linear = t â€¢ f.cont_linear :=
rfl

lemma decomp (f : V â†’A[R] W) :
  (f : V â†’ W) = f.cont_linear + function.const V (f 0) :=
begin
  rcases f with âŸ¨f, hâŸ©,
  rw [coe_mk_const_linear_eq_linear, coe_mk, f.decomp, pi.add_apply, linear_map.map_zero, zero_add],
end

section normed_space_structure

variables (f : V â†’A[ğ•œ] W)

/-- Note that unlike the operator norm for linear maps, this norm is _not_ submultiplicative:
we do _not_ necessarily have `âˆ¥f.comp gâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥`. See `norm_comp_le` for what we can say. -/
noncomputable instance has_norm : has_norm (V â†’A[ğ•œ] W) := âŸ¨Î» f, max âˆ¥f 0âˆ¥ âˆ¥f.cont_linearâˆ¥âŸ©

lemma norm_def : âˆ¥fâˆ¥ = (max âˆ¥f 0âˆ¥ âˆ¥f.cont_linearâˆ¥) := rfl

lemma norm_cont_linear_le : âˆ¥f.cont_linearâˆ¥ â‰¤ âˆ¥fâˆ¥ := le_max_right _ _

lemma norm_image_zero_le : âˆ¥f 0âˆ¥ â‰¤ âˆ¥fâˆ¥ := le_max_left _ _

@[simp] lemma norm_eq (h : f 0 = 0) : âˆ¥fâˆ¥ = âˆ¥f.cont_linearâˆ¥ :=
calc âˆ¥fâˆ¥ = (max âˆ¥f 0âˆ¥ âˆ¥f.cont_linearâˆ¥) : by rw norm_def
    ... = (max 0 âˆ¥f.cont_linearâˆ¥) : by rw [h, norm_zero]
    ... = âˆ¥f.cont_linearâˆ¥ : max_eq_right (norm_nonneg _)

noncomputable instance : normed_group (V â†’A[ğ•œ] W) :=
normed_group.of_core _
{ norm_eq_zero_iff := Î» f,
    begin
      rw norm_def,
      refine âŸ¨Î» hâ‚€, _, by { rintros rfl, simp, }âŸ©,
      rcases max_eq_iff.mp hâ‚€ with âŸ¨hâ‚, hâ‚‚âŸ© | âŸ¨hâ‚, hâ‚‚âŸ©;
      rw hâ‚ at hâ‚‚,
      { rw [norm_le_zero_iff, cont_linear_eq_zero_iff_exists_const] at hâ‚‚,
        obtain âŸ¨q, rflâŸ© := hâ‚‚,
        simp only [function.const_apply, coe_const, norm_eq_zero] at hâ‚,
        rw hâ‚,
        refl, },
      { rw [norm_eq_zero_iff', cont_linear_eq_zero_iff_exists_const] at hâ‚,
        obtain âŸ¨q, rflâŸ© := hâ‚,
        simp only [function.const_apply, coe_const, norm_le_zero_iff] at hâ‚‚,
        rw hâ‚‚,
        refl, },
    end,
  triangle := Î» f g,
    begin
      simp only [norm_def, pi.add_apply, add_cont_linear, coe_add, max_le_iff],
      exact âŸ¨(norm_add_le _ _).trans (add_le_add (le_max_left _ _) (le_max_left _ _)),
             (norm_add_le _ _).trans (add_le_add (le_max_right _ _) (le_max_right _ _))âŸ©,
    end,
  norm_neg := Î» f, by simp [norm_def], }

instance : normed_space ğ•œ (V â†’A[ğ•œ] W) :=
{ norm_smul_le := Î» t f, by simp only [norm_def, smul_cont_linear, coe_smul, pi.smul_apply,
    norm_smul, â† mul_max_of_nonneg _ _ (norm_nonneg t)], }

lemma norm_comp_le (g : Wâ‚‚ â†’A[ğ•œ] V) :
  âˆ¥f.comp gâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥ + âˆ¥f 0âˆ¥ :=
begin
  rw [norm_def, max_le_iff],
  split,
  { calc âˆ¥f.comp g 0âˆ¥ = âˆ¥f (g 0)âˆ¥ : by simp
                 ... = âˆ¥f.cont_linear (g 0) + f 0âˆ¥ : by { rw f.decomp, simp, }
                 ... â‰¤ âˆ¥f.cont_linearâˆ¥ * âˆ¥g 0âˆ¥ + âˆ¥f 0âˆ¥ :
                          (norm_add_le _ _).trans (add_le_add_right (f.cont_linear.le_op_norm _) _)
                 ... â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥ + âˆ¥f 0âˆ¥ :
                          add_le_add_right (mul_le_mul f.norm_cont_linear_le g.norm_image_zero_le
                          (norm_nonneg _) (norm_nonneg _)) _, },
  { calc âˆ¥(f.comp g).cont_linearâˆ¥ â‰¤ âˆ¥f.cont_linearâˆ¥ * âˆ¥g.cont_linearâˆ¥ :
                                      (g.comp_cont_linear f).symm â–¸ f.cont_linear.op_norm_comp_le _
                             ... â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥ :
                                      mul_le_mul f.norm_cont_linear_le g.norm_cont_linear_le
                                      (norm_nonneg _) (norm_nonneg _)
                             ... â‰¤ âˆ¥fâˆ¥ * âˆ¥gâˆ¥ + âˆ¥f 0âˆ¥ :
                                      by { rw le_add_iff_nonneg_right, apply norm_nonneg, }, },
end

variables (ğ•œ V W)

/-- The space of affine maps between two normed spaces is linearly isometric to the product of the
codomain with the space of linear maps, by taking the value of the affine map at `(0 : V)` and the
linear part. -/
def to_const_prod_continuous_linear_map : (V â†’A[ğ•œ] W) â‰ƒâ‚—áµ¢[ğ•œ] W Ã— (V â†’L[ğ•œ] W) :=
{ to_fun    := Î» f, âŸ¨f 0, f.cont_linearâŸ©,
  inv_fun   := Î» p, p.2.to_continuous_affine_map + const ğ•œ V p.1,
  left_inv  := Î» f, by { ext, rw f.decomp, simp, },
  right_inv := by { rintros âŸ¨v, fâŸ©, ext; simp, },
  map_add'  := by simp,
  map_smul' := by simp,
  norm_map' := Î» f, by simp [prod.norm_def, norm_def], }

@[simp] lemma to_const_prod_continuous_linear_map_fst (f : V â†’A[ğ•œ] W) :
  (to_const_prod_continuous_linear_map ğ•œ V W f).fst = f 0 :=
rfl

@[simp] lemma to_const_prod_continuous_linear_map_snd (f : V â†’A[ğ•œ] W) :
  (to_const_prod_continuous_linear_map ğ•œ V W f).snd = f.cont_linear :=
rfl

end normed_space_structure

end continuous_affine_map
