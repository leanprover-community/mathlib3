/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, FrÃ©dÃ©ric Dupuis
-/
import analysis.inner_product_space.projection
import analysis.normed_space.bounded_linear_maps
import analysis.normed_space.hahn_banach

/-!
# The topological dual of a normed space

In this file we define the topological dual of a normed space, and the bounded linear map from
a normed space into its double dual.

We also prove that, for base field `ğ•œ` with `[is_R_or_C ğ•œ]`, this map is an isometry.

We then consider inner product spaces, with base field over `â„` (the corresponding results for `â„‚`
will require the definition of conjugate-linear maps). We define `to_dual_map`, a continuous linear
map from `E` to its dual, which maps an element `x` of the space to `Î» y, âŸªx, yâŸ«`. We check
(`to_dual_map_isometry`) that this map is an isometry onto its image, and particular is injective.
We also define `to_dual'` as the function taking taking a vector to its dual for a base field `ğ•œ`
with `[is_R_or_C ğ•œ]`; this is a function and not a linear map.

Finally, under the hypothesis of completeness (i.e., for Hilbert spaces), we prove the FrÃ©chet-Riesz
representation (`to_dual_map_eq_top`), which states the surjectivity: every element of the dual
of a Hilbert space `E` has the form `Î» u, âŸªx, uâŸ«` for some `x : E`.  This permits the map
`to_dual_map` to be upgraded to an (isometric) continuous linear equivalence, `to_dual`, between a
Hilbert space and its dual.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `semi_normed_space` and we specialize to `normed_space` when needed.

## References

* [M. Einsiedler and T. Ward, *Functional Analysis, Spectral Theory, and Applications*]
  [EinsiedlerWard2017]

## Tags

dual, FrÃ©chet-Riesz
-/

noncomputable theory
open_locale classical
universes u v

namespace normed_space

section general
variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
variables (E : Type*) [semi_normed_group E] [semi_normed_space ğ•œ E]
variables (F : Type*) [normed_group F] [normed_space ğ•œ F]

/-- The topological dual of a seminormed space `E`. -/
@[derive [inhabited, has_coe_to_fun, semi_normed_group, semi_normed_space ğ•œ]] def dual := E â†’L[ğ•œ] ğ•œ

instance : normed_group (dual ğ•œ F) := continuous_linear_map.to_normed_group

instance : normed_space ğ•œ (dual ğ•œ F) := continuous_linear_map.to_normed_space

/-- The inclusion of a normed space in its double (topological) dual, considered
   as a bounded linear map. -/
def inclusion_in_double_dual : E â†’L[ğ•œ] (dual ğ•œ (dual ğ•œ E)) :=
continuous_linear_map.apply ğ•œ ğ•œ

@[simp] lemma dual_def (x : E) (f : dual ğ•œ E) : inclusion_in_double_dual ğ•œ E x f = f x := rfl

lemma inclusion_in_double_dual_norm_eq :
  âˆ¥inclusion_in_double_dual ğ•œ Eâˆ¥ = âˆ¥(continuous_linear_map.id ğ•œ (dual ğ•œ E))âˆ¥ :=
continuous_linear_map.op_norm_flip _

lemma inclusion_in_double_dual_norm_le : âˆ¥inclusion_in_double_dual ğ•œ Eâˆ¥ â‰¤ 1 :=
by { rw inclusion_in_double_dual_norm_eq, exact continuous_linear_map.norm_id_le }

lemma double_dual_bound (x : E) : âˆ¥(inclusion_in_double_dual ğ•œ E) xâˆ¥ â‰¤ âˆ¥xâˆ¥ :=
by simpa using continuous_linear_map.le_of_op_norm_le _ (inclusion_in_double_dual_norm_le ğ•œ E) x

end general

section bidual_isometry

variables (ğ•œ : Type v) [is_R_or_C ğ•œ]
  {E : Type u} [normed_group E] [normed_space ğ•œ E]

/-- If one controls the norm of every `f x`, then one controls the norm of `x`.
    Compare `continuous_linear_map.op_norm_le_bound`. -/
lemma norm_le_dual_bound (x : E) {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ (f : dual ğ•œ E), âˆ¥f xâˆ¥ â‰¤ M * âˆ¥fâˆ¥) :
  âˆ¥xâˆ¥ â‰¤ M :=
begin
  classical,
  by_cases h : x = 0,
  { simp only [h, hMp, norm_zero] },
  { obtain âŸ¨f, hfâŸ© : âˆƒ g : E â†’L[ğ•œ] ğ•œ, _ := exists_dual_vector ğ•œ x h,
    calc âˆ¥xâˆ¥ = âˆ¥norm' ğ•œ xâˆ¥ : (norm_norm' _ _ _).symm
    ... = âˆ¥f xâˆ¥ : by rw hf.2
    ... â‰¤ M * âˆ¥fâˆ¥ : hM f
    ... = M : by rw [hf.1, mul_one] }
end

lemma eq_zero_of_forall_dual_eq_zero {x : E} (h : âˆ€ f : dual ğ•œ E, f x = (0 : ğ•œ)) : x = 0 :=
norm_eq_zero.mp (le_antisymm (norm_le_dual_bound ğ•œ x le_rfl (Î» f, by simp [h f])) (norm_nonneg _))

lemma eq_zero_iff_forall_dual_eq_zero (x : E) : x = 0 â†” âˆ€ g : dual ğ•œ E, g x = 0 :=
âŸ¨Î» hx, by simp [hx], Î» h, eq_zero_of_forall_dual_eq_zero ğ•œ hâŸ©

lemma eq_iff_forall_dual_eq {x y : E} :
  x = y â†” âˆ€ g : dual ğ•œ E, g x = g y :=
begin
  rw [â† sub_eq_zero, eq_zero_iff_forall_dual_eq_zero ğ•œ (x - y)],
  simp [sub_eq_zero],
end

/-- The inclusion of a normed space in its double dual is an isometry onto its image.-/
lemma inclusion_in_double_dual_isometry (x : E) : âˆ¥inclusion_in_double_dual ğ•œ E xâˆ¥ = âˆ¥xâˆ¥ :=
begin
  apply le_antisymm,
  { exact double_dual_bound ğ•œ E x },
  { rw continuous_linear_map.norm_def,
    apply le_cInf continuous_linear_map.bounds_nonempty,
    rintros c âŸ¨hc1, hc2âŸ©,
    exact norm_le_dual_bound ğ•œ x hc1 hc2 },
end

end bidual_isometry

end normed_space

namespace inner_product_space
open is_R_or_C continuous_linear_map

section is_R_or_C

variables (ğ•œ : Type*)
variables {E : Type*} [is_R_or_C ğ•œ] [inner_product_space ğ•œ E]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y
local postfix `â€ `:90 := @is_R_or_C.conj ğ•œ _

/--
Given some `x` in an inner product space, we can define its dual as the continuous linear map
`Î» y, âŸªx, yâŸ«`. Consider using `to_dual` or `to_dual_map` instead in the real case.
-/
def to_dual' : E â†’+ normed_space.dual ğ•œ E :=
{ to_fun := Î» x, linear_map.mk_continuous
  { to_fun := Î» y, âŸªx, yâŸ«,
    map_add' := Î» _ _, inner_add_right,
    map_smul' := Î» _ _, inner_smul_right }
  âˆ¥xâˆ¥
  (Î» y, by { rw [is_R_or_C.norm_eq_abs], exact abs_inner_le_norm _ _ }),
  map_zero' := by { ext z, simp },
  map_add' := Î» x y, by { ext z, simp [inner_add_left] } }

@[simp] lemma to_dual'_apply {x y : E} : to_dual' ğ•œ x y = âŸªx, yâŸ« := rfl

/-- In an inner product space, the norm of the dual of a vector `x` is `âˆ¥xâˆ¥` -/
@[simp] lemma norm_to_dual'_apply (x : E) : âˆ¥to_dual' ğ•œ xâˆ¥ = âˆ¥xâˆ¥ :=
begin
  refine le_antisymm _ _,
  { exact linear_map.mk_continuous_norm_le _ (norm_nonneg _) _ },
  { cases eq_or_lt_of_le (norm_nonneg x) with h h,
    { have : x = 0 := norm_eq_zero.mp (eq.symm h),
      simp [this] },
    { refine (mul_le_mul_right h).mp _,
      calc âˆ¥xâˆ¥ * âˆ¥xâˆ¥ = âˆ¥xâˆ¥ ^ 2 : by ring
      ... = re âŸªx, xâŸ« : norm_sq_eq_inner _
      ... â‰¤ abs âŸªx, xâŸ« : re_le_abs _
      ... = âˆ¥to_dual' ğ•œ x xâˆ¥ : by simp [norm_eq_abs]
      ... â‰¤ âˆ¥to_dual' ğ•œ xâˆ¥ * âˆ¥xâˆ¥ : le_op_norm (to_dual' ğ•œ x) x } }
end

variables (E)

lemma to_dual'_isometry : isometry (@to_dual' ğ•œ E _ _) :=
add_monoid_hom.isometry_of_norm _ (norm_to_dual'_apply ğ•œ)

/--
FrÃ©chet-Riesz representation: any `â„“` in the dual of a Hilbert space `E` is of the form
`Î» u, âŸªy, uâŸ«` for some `y : E`, i.e. `to_dual'` is surjective.
-/
lemma to_dual'_surjective [complete_space E] : function.surjective (@to_dual' ğ•œ E _ _) :=
begin
  intros â„“,
  set Y := ker â„“ with hY,
  by_cases htriv : Y = âŠ¤,
  { have hâ„“ : â„“ = 0,
    { have h' := linear_map.ker_eq_top.mp htriv,
      rw [â†coe_zero] at h',
      apply coe_injective,
      exact h' },
    exact âŸ¨0, by simp [hâ„“]âŸ© },
  { have Ycomplete := is_complete_ker â„“,
    rw [â† submodule.orthogonal_eq_bot_iff Ycomplete, â†hY] at htriv,
    change Yá—® â‰  âŠ¥ at htriv,
    rw [submodule.ne_bot_iff] at htriv,
    obtain âŸ¨z : E, hz : z âˆˆ Yá—®, z_ne_0 : z â‰  0âŸ© := htriv,
    refine âŸ¨((â„“ z)â€  / âŸªz, zâŸ«) â€¢ z, _âŸ©,
    ext x,
    have hâ‚ : (â„“ z) â€¢ x - (â„“ x) â€¢ z âˆˆ Y,
    { rw [mem_ker, map_sub, map_smul, map_smul, algebra.id.smul_eq_mul, algebra.id.smul_eq_mul,
          mul_comm],
      exact sub_self (â„“ x * â„“ z) },
    have hâ‚‚ : (â„“ z) * âŸªz, xâŸ« = (â„“ x) * âŸªz, zâŸ«,
    { have hâ‚ƒ := calc
        0    = âŸªz, (â„“ z) â€¢ x - (â„“ x) â€¢ zâŸ«       : by { rw [(Y.mem_orthogonal' z).mp hz], exact hâ‚ }
         ... = âŸªz, (â„“ z) â€¢ xâŸ« - âŸªz, (â„“ x) â€¢ zâŸ«  : by rw [inner_sub_right]
         ... = (â„“ z) * âŸªz, xâŸ« - (â„“ x) * âŸªz, zâŸ«  : by simp [inner_smul_right],
      exact sub_eq_zero.mp (eq.symm hâ‚ƒ) },
    have hâ‚„ := calc
      âŸª((â„“ z)â€  / âŸªz, zâŸ«) â€¢ z, xâŸ« = (â„“ z) / âŸªz, zâŸ« * âŸªz, xâŸ«
            : by simp [inner_smul_left, conj_div, conj_conj]
                            ... = (â„“ z) * âŸªz, xâŸ« / âŸªz, zâŸ«
            : by rw [â†div_mul_eq_mul_div]
                            ... = (â„“ x) * âŸªz, zâŸ« / âŸªz, zâŸ«
            : by rw [hâ‚‚]
                            ... = â„“ x
            : begin
                have : âŸªz, zâŸ« â‰  0,
                { change z = 0 â†’ false at z_ne_0,
                  rwa â†inner_self_eq_zero at z_ne_0 },
                field_simp [this]
              end,
    exact hâ‚„ }
end

end is_R_or_C

section real

variables {F : Type*} [inner_product_space â„ F]

/-- In a real inner product space `F`, the function that takes a vector `x` in `F` to its dual
`Î» y, âŸªx, yâŸ«` is a continuous linear map. If the space is complete (i.e. is a Hilbert space),
consider using `to_dual` instead. -/
-- TODO extend to `is_R_or_C` (requires a definition of conjugate linear maps)
def to_dual_map : F â†’L[â„] (normed_space.dual â„ F) :=
linear_map.mk_continuous
  { to_fun := to_dual' â„,
    map_add' := Î» x y, by { ext, simp [inner_add_left] },
    map_smul' := Î» c x, by { ext, simp [inner_smul_left] } }
  1
  (Î» x, by simp only [norm_to_dual'_apply, one_mul, linear_map.coe_mk])

@[simp] lemma to_dual_map_apply {x y : F} : to_dual_map x y = âŸªx, yâŸ«_â„ := rfl

/-- In an inner product space, the norm of the dual of a vector `x` is `âˆ¥xâˆ¥` -/
@[simp] lemma norm_to_dual_map_apply (x : F) : âˆ¥to_dual_map xâˆ¥ = âˆ¥xâˆ¥ := norm_to_dual'_apply _ _

lemma to_dual_map_isometry : isometry (@to_dual_map F _) :=
add_monoid_hom.isometry_of_norm _ norm_to_dual_map_apply

lemma to_dual_map_injective : function.injective (@to_dual_map F _) :=
(@to_dual_map_isometry F _).injective

@[simp] lemma ker_to_dual_map : (@to_dual_map F _).ker = âŠ¥ :=
linear_map.ker_eq_bot.mpr to_dual_map_injective

@[simp] lemma to_dual_map_eq_iff_eq {x y : F} : to_dual_map x = to_dual_map y â†” x = y :=
((linear_map.ker_eq_bot).mp (@ker_to_dual_map F _)).eq_iff

variables [complete_space F]

/--
FrÃ©chet-Riesz representation: any `â„“` in the dual of a real Hilbert space `F` is of the form
`Î» u, âŸªy, uâŸ«` for some `y` in `F`.  See `inner_product_space.to_dual` for the continuous linear
equivalence thus induced.
-/
-- TODO extend to `is_R_or_C` (requires a definition of conjugate linear maps)
lemma range_to_dual_map : (@to_dual_map F _).range = âŠ¤ :=
linear_map.range_eq_top.mpr (to_dual'_surjective â„ F)

/--
FrÃ©chet-Riesz representation: If `F` is a Hilbert space, the function that takes a vector in `F` to
its dual is a continuous linear equivalence.  -/
def to_dual : F â‰ƒL[â„] (normed_space.dual â„ F) :=
continuous_linear_equiv.of_isometry to_dual_map.to_linear_map to_dual_map_isometry range_to_dual_map

/--
FrÃ©chet-Riesz representation: If `F` is a Hilbert space, the function that takes a vector in `F` to
its dual is an isometry.  -/
def isometric.to_dual : F â‰ƒáµ¢ normed_space.dual â„ F :=
{ to_equiv := to_dual.to_linear_equiv.to_equiv,
  isometry_to_fun := to_dual'_isometry â„ F}

@[simp] lemma to_dual_apply {x y : F} : to_dual x y = âŸªx, yâŸ«_â„ := rfl

@[simp] lemma to_dual_eq_iff_eq {x y : F} : to_dual x = to_dual y â†” x = y :=
(@to_dual F _ _).injective.eq_iff

lemma to_dual_eq_iff_eq' {x x' : F} : (âˆ€ y : F, âŸªx, yâŸ«_â„ = âŸªx', yâŸ«_â„) â†” x = x' :=
begin
  split,
  { intros h,
    have : to_dual x = to_dual x' â†’ x = x' := to_dual_eq_iff_eq.mp,
    apply this,
    simp_rw [â†to_dual_apply] at h,
    ext z,
    exact h z },
  { rintros rfl y,
    refl }
end

@[simp] lemma norm_to_dual_apply (x : F) : âˆ¥to_dual xâˆ¥ = âˆ¥xâˆ¥ := norm_to_dual_map_apply x

/-- In a Hilbert space, the norm of a vector in the dual space is the norm of its corresponding
primal vector. -/
lemma norm_to_dual_symm_apply (â„“ : normed_space.dual â„ F) : âˆ¥to_dual.symm â„“âˆ¥ = âˆ¥â„“âˆ¥ :=
begin
  have : â„“ = to_dual (to_dual.symm â„“) := by simp only [continuous_linear_equiv.apply_symm_apply],
  conv_rhs { rw [this] },
  refine eq.symm (norm_to_dual_apply _),
end

end real

end inner_product_space
