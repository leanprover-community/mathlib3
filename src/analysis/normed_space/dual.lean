/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import analysis.normed_space.hahn_banach
import analysis.seminorm
import analysis.normed_space.is_R_or_C

/-!
# The topological dual of a normed space

In this file we define the topological dual `normed_space.dual` of a normed space, and the
continuous linear map `normed_space.inclusion_in_double_dual` from a normed space into its double
dual.

For base field `ğ•œ = â„` or `ğ•œ = â„‚`, this map is actually an isometric embedding; we provide a
version `normed_space.inclusion_in_double_dual_li` of the map which is of type a bundled linear
isometric embedding, `E â†’â‚—áµ¢[ğ•œ] (dual ğ•œ (dual ğ•œ E))`.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `semi_normed_space` and we specialize to `normed_space` when needed.

## Main definitions

* `inclusion_in_double_dual` and `inclusion_in_double_dual_li` are the inclusion of a normed space
  in its double dual, considered as a bounded linear map and as a linear isometry, respectively.
* `polar ğ•œ s` is the subset of `dual ğ•œ E` consisting of those functionals `x'` for which
  `âˆ¥x' zâˆ¥ â‰¤ 1` for every `z âˆˆ s`.

## Tags

dual
-/

noncomputable theory
open_locale classical
universes u v

namespace normed_space

section general
variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
variables (E : Type*) [semi_normed_group E] [semi_normed_space ğ•œ E]
variables (F : Type*) [normed_group F] [normed_space ğ•œ F]

/-- The topological dual of a seminormed space `E`. -/
@[derive [inhabited, semi_normed_group, semi_normed_space ğ•œ]] def dual := E â†’L[ğ•œ] ğ•œ

instance : has_coe_to_fun (dual ğ•œ E) (Î» _, E â†’ ğ•œ) := continuous_linear_map.to_fun

instance : normed_group (dual ğ•œ F) := continuous_linear_map.to_normed_group

instance : normed_space ğ•œ (dual ğ•œ F) := continuous_linear_map.to_normed_space

instance [finite_dimensional ğ•œ E] : finite_dimensional ğ•œ (dual ğ•œ E) :=
continuous_linear_map.finite_dimensional

/-- The inclusion of a normed space in its double (topological) dual, considered
   as a bounded linear map. -/
def inclusion_in_double_dual : E â†’L[ğ•œ] (dual ğ•œ (dual ğ•œ E)) :=
continuous_linear_map.apply ğ•œ ğ•œ

@[simp] lemma dual_def (x : E) (f : dual ğ•œ E) : inclusion_in_double_dual ğ•œ E x f = f x := rfl

lemma inclusion_in_double_dual_norm_eq :
  âˆ¥inclusion_in_double_dual ğ•œ Eâˆ¥ = âˆ¥(continuous_linear_map.id ğ•œ (dual ğ•œ E))âˆ¥ :=
continuous_linear_map.op_norm_flip _

lemma inclusion_in_double_dual_norm_le : âˆ¥inclusion_in_double_dual ğ•œ Eâˆ¥ â‰¤ 1 :=
by { rw inclusion_in_double_dual_norm_eq, exact continuous_linear_map.norm_id_le }

lemma double_dual_bound (x : E) : âˆ¥(inclusion_in_double_dual ğ•œ E) xâˆ¥ â‰¤ âˆ¥xâˆ¥ :=
by simpa using continuous_linear_map.le_of_op_norm_le _ (inclusion_in_double_dual_norm_le ğ•œ E) x

end general

section bidual_isometry

variables (ğ•œ : Type v) [is_R_or_C ğ•œ]
  {E : Type u} [normed_group E] [normed_space ğ•œ E]

/-- If one controls the norm of every `f x`, then one controls the norm of `x`.
    Compare `continuous_linear_map.op_norm_le_bound`. -/
lemma norm_le_dual_bound (x : E) {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ (f : dual ğ•œ E), âˆ¥f xâˆ¥ â‰¤ M * âˆ¥fâˆ¥) :
  âˆ¥xâˆ¥ â‰¤ M :=
begin
  classical,
  by_cases h : x = 0,
  { simp only [h, hMp, norm_zero] },
  { obtain âŸ¨f, hfâŸ© : âˆƒ g : E â†’L[ğ•œ] ğ•œ, _ := exists_dual_vector ğ•œ x h,
    calc âˆ¥xâˆ¥ = âˆ¥norm' ğ•œ xâˆ¥ : (norm_norm' _ _ _).symm
    ... = âˆ¥f xâˆ¥ : by rw hf.2
    ... â‰¤ M * âˆ¥fâˆ¥ : hM f
    ... = M : by rw [hf.1, mul_one] }
end

lemma eq_zero_of_forall_dual_eq_zero {x : E} (h : âˆ€ f : dual ğ•œ E, f x = (0 : ğ•œ)) : x = 0 :=
norm_eq_zero.mp (le_antisymm (norm_le_dual_bound ğ•œ x le_rfl (Î» f, by simp [h f])) (norm_nonneg _))

lemma eq_zero_iff_forall_dual_eq_zero (x : E) : x = 0 â†” âˆ€ g : dual ğ•œ E, g x = 0 :=
âŸ¨Î» hx, by simp [hx], Î» h, eq_zero_of_forall_dual_eq_zero ğ•œ hâŸ©

lemma eq_iff_forall_dual_eq {x y : E} :
  x = y â†” âˆ€ g : dual ğ•œ E, g x = g y :=
begin
  rw [â† sub_eq_zero, eq_zero_iff_forall_dual_eq_zero ğ•œ (x - y)],
  simp [sub_eq_zero],
end

/-- The inclusion of a normed space in its double dual is an isometry onto its image.-/
def inclusion_in_double_dual_li : E â†’â‚—áµ¢[ğ•œ] (dual ğ•œ (dual ğ•œ E)) :=
{ norm_map' := begin
    intros x,
    apply le_antisymm,
    { exact double_dual_bound ğ•œ E x },
    rw continuous_linear_map.norm_def,
    apply le_cInf continuous_linear_map.bounds_nonempty,
    rintros c âŸ¨hc1, hc2âŸ©,
    exact norm_le_dual_bound ğ•œ x hc1 hc2
  end,
  .. inclusion_in_double_dual ğ•œ E }

end bidual_isometry

end normed_space

section polar_sets

open metric set normed_space

/-- Given a subset `s` in a normed space `E` (over a field `ğ•œ`), the polar
`polar ğ•œ s` is the subset of `dual ğ•œ E` consisting of those functionals which
evaluate to something of norm at most one at all points `z âˆˆ s`. -/
def polar (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] (s : set E) : set (dual ğ•œ E) :=
{x' : dual ğ•œ E | âˆ€ z âˆˆ s, âˆ¥ x' z âˆ¥ â‰¤ 1 }

namespace polar

open metric set normed_space
open_locale topological_space

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]

@[simp] lemma zero_mem (s : set E) :
  (0 : dual ğ•œ E) âˆˆ polar ğ•œ s :=
Î» _ _, by simp only [zero_le_one, continuous_linear_map.zero_apply, norm_zero]

lemma eq_Inter (s : set E) :
  polar ğ•œ s = â‹‚ z âˆˆ s, {x' : dual ğ•œ E | âˆ¥ x' z âˆ¥ â‰¤ 1 } :=
by { dunfold polar, ext, simp only [mem_bInter_iff, mem_set_of_eq], }

lemma of_empty : polar ğ•œ (âˆ… : set E) = univ :=
by { simp only [polar, forall_false_left, mem_empty_eq, forall_const, set_of_true], }

/-- If `x'` is a dual element such that the norms `âˆ¥x' zâˆ¥` are bounded for `z âˆˆ s`, then a
small scalar multiple of `x'` is in `polar ğ•œ s`. -/
lemma smul_mem {s : set E} {x' : dual ğ•œ E} {c : ğ•œ}
  (hc : âˆ€ z, z âˆˆ s â†’ âˆ¥ x' z âˆ¥ â‰¤ âˆ¥câˆ¥) : (câ»Â¹ â€¢ x') âˆˆ polar ğ•œ s :=
begin
  by_cases c_zero : c = 0,
  { rw c_zero,
    dunfold polar,
    simp only [zero_le_one, continuous_linear_map.zero_apply, norm_zero,
               mem_set_of_eq, implies_true_iff, inv_zero, zero_smul], },
  have eq : âˆ€ z, âˆ¥ câ»Â¹ â€¢ (x' z) âˆ¥ = âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ x' z âˆ¥ := Î» z, norm_smul câ»Â¹ _,
  have le : âˆ€ z, z âˆˆ s â†’ âˆ¥ câ»Â¹ â€¢ (x' z) âˆ¥ â‰¤ âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ c âˆ¥,
  { intros z hzs,
    rw eq z,
    apply mul_le_mul (le_of_eq rfl) (hc z hzs) (norm_nonneg _) (norm_nonneg _), },
  have cancel : âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ c âˆ¥ = 1,
  by simp only [c_zero, norm_eq_zero, ne.def, not_false_iff,
                inv_mul_cancel, normed_field.norm_inv],
  rwa cancel at le,
end

/-- The `polar` of closed unit ball in a normed space `E` is the closed unit ball of the dual. -/
lemma of_closed_unit_ball
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E] :
  polar ğ•œ (closed_ball (0 : E) 1) = {x' : dual ğ•œ E | âˆ¥ x' âˆ¥ â‰¤ 1 } :=
begin
  ext x',
  simp only [mem_closed_ball, mem_set_of_eq, dist_zero_right],
  split,
  { intros h,
    apply continuous_linear_map.op_norm_le_of_ball zero_lt_one zero_le_one,
    intros z hz,
    have key := linear_map.bound_of_ball_bound zero_lt_one 1 x'.to_linear_map h z,
    simp only [continuous_linear_map.to_linear_map_eq_coe,
               continuous_linear_map.coe_coe, div_one] at key,
    exact key, },
  { intros h z hz,
    simp only [mem_closed_ball, dist_zero_right] at hz,
    apply (continuous_linear_map.unit_le_op_norm x' z hz).trans h, },
end

/-- If `s` is a neighborhood of the origin in a normed space `E`, then at any point `z : E`
there exists a bound for the norms of the values `x' z` of the elements `x' âˆˆ polar ğ•œ s` of the
polar of `s`. -/
lemma eval_bounded_of_nbhd_zero (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) (z : E) :
  âˆƒ (r : â„), âˆ€ (x' : dual ğ•œ E), x' âˆˆ polar ğ•œ s â†’ âˆ¥ x' z âˆ¥ â‰¤ r :=
begin
  have s_absnt : absorbent ğ•œ s := absorbent_nhds_zero s_nhd,
  rcases s_absnt z with âŸ¨c, âŸ¨c_pos, hcâŸ©âŸ©,
  cases normed_field.exists_lt_norm ğ•œ c with a ha,
  specialize hc a ha.le,
  have a_norm_pos : 0 < âˆ¥ a âˆ¥ := lt_trans c_pos ha,
  have a_ne_zero : a â‰  0 := norm_pos_iff.mp a_norm_pos,
  have w_in_s : aâ»Â¹ â€¢ z âˆˆ s,
  { rcases hc with âŸ¨ w , âŸ¨hws, hawâŸ©âŸ©,
    rwa [â† haw, â† mul_smul, inv_mul_cancel a_ne_zero, one_smul], },
  use âˆ¥aâˆ¥,
  intros x' hx',
  specialize hx' _ w_in_s,
  simp only [algebra.id.smul_eq_mul, normed_field.norm_mul,
             continuous_linear_map.map_smul, normed_field.norm_inv] at hx',
  have key := mul_le_mul (@rfl _ âˆ¥ a âˆ¥).ge hx' _ (norm_nonneg a),
  rwa [mul_one, â† mul_assoc, mul_inv_cancel (ne_of_gt a_norm_pos), one_mul] at key,
  apply mul_nonneg _ (norm_nonneg _),
  simp only [inv_nonneg, norm_nonneg],
end

/-- If `s` is a neighborhood of the origin in a normed space `E`, then there exists a
function `r : E â†’ â„` such that for all elements `x' âˆˆ polar ğ•œ s` one has `âˆ¥x' zâˆ¥ â‰¤ r(z)`. -/
lemma finite_values_of_nbhd_zero {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  âˆƒ (r : E â†’ â„), âˆ€ (x' : dual ğ•œ E) (z : E), x' âˆˆ polar ğ•œ s â†’ âˆ¥ x' z âˆ¥ â‰¤ r z :=
begin
  cases classical.axiom_of_choice (eval_bounded_of_nbhd_zero ğ•œ s_nhd) with r hr,
  use r,
  intros x' z,
  exact hr z x',
end

/-- Given a neighborhood `s` of the origin in a normed space `E` over `â„` or `â„‚`, the dual norms
of all elements of the polar `polar ğ•œ s` are bounded by a constant. -/
lemma bounded_of_nbhd_zero {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  âˆƒ (c : â„), âˆ€ (x' : dual ğ•œ E), x' âˆˆ polar ğ•œ s â†’ âˆ¥ x' âˆ¥ â‰¤ c :=
begin
  rcases metric.mem_nhds_iff.mp s_nhd with âŸ¨r, âŸ¨r_pos, r_ballâŸ©âŸ©,
  have half_r_pos : 0 < r / 2 := by linarith,
  use 2 / r,
  intros x' hx',
  have key := continuous_linear_map.op_norm_bound_of_ball_bound half_r_pos 1 x',
  simp only [one_div_div] at key,
  apply key,
  intros z hz,
  have z_mem_ball : z âˆˆ ball (0 : E) r,
  { simp only [mem_ball_zero_iff],
    simp only [mem_closed_ball, dist_zero_right] at hz,
    linarith, },
  exact hx' z (r_ball z_mem_ball),
end

/-- Given a neighborhood `s` of the origin in a normed space `E`, for any `z : E` it
is possible to choose a real number `r` such that for any functional `x' âˆˆ polar ğ•œ s` in
the polar of `s`, the value at `z` satisfies the norm bound `âˆ¥x' zâˆ¥ â‰¤ r`. Such an `r`
is given by `bounds_fun _ z`. -/
def bounds_fun (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) : E â†’ â„ :=
classical.some (classical.axiom_of_choice (eval_bounded_of_nbhd_zero ğ•œ s_nhd))

lemma bounds_fun_spec (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) (x' : dual ğ•œ E) (z : E) :
  x' âˆˆ polar ğ•œ s â†’ âˆ¥ x' z âˆ¥ â‰¤ bounds_fun ğ•œ s_nhd z :=
classical.some_spec
  (classical.axiom_of_choice (eval_bounded_of_nbhd_zero ğ•œ s_nhd)) z x'

end polar

end polar_sets
