/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import analysis.normed_space.hahn_banach
import analysis.normed_space.is_R_or_C
import analysis.locally_convex.polar

/-!
# The topological dual of a normed space

In this file we define the topological dual `normed_space.dual` of a normed space, and the
continuous linear map `normed_space.inclusion_in_double_dual` from a normed space into its double
dual.

For base field `ğ•œ = â„` or `ğ•œ = â„‚`, this map is actually an isometric embedding; we provide a
version `normed_space.inclusion_in_double_dual_li` of the map which is of type a bundled linear
isometric embedding, `E â†’â‚—áµ¢[ğ•œ] (dual ğ•œ (dual ğ•œ E))`.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `semi_normed_group` and we specialize to `normed_group` when needed.

## Main definitions

* `inclusion_in_double_dual` and `inclusion_in_double_dual_li` are the inclusion of a normed space
  in its double dual, considered as a bounded linear map and as a linear isometry, respectively.
* `polar ğ•œ s` is the subset of `dual ğ•œ E` consisting of those functionals `x'` for which
  `âˆ¥x' zâˆ¥ â‰¤ 1` for every `z âˆˆ s`.

## Tags

dual
-/

noncomputable theory
open_locale classical topological_space
universes u v

namespace normed_space

section general
variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
variables (E : Type*) [semi_normed_group E] [normed_space ğ•œ E]
variables (F : Type*) [normed_group F] [normed_space ğ•œ F]

/-- The topological dual of a seminormed space `E`. -/
@[derive [inhabited, semi_normed_group, normed_space ğ•œ]] def dual := E â†’L[ğ•œ] ğ•œ

instance : add_monoid_hom_class (dual ğ•œ E) E ğ•œ := continuous_linear_map.add_monoid_hom_class

instance : has_coe_to_fun (dual ğ•œ E) (Î» _, E â†’ ğ•œ) := continuous_linear_map.to_fun

instance : normed_group (dual ğ•œ F) := continuous_linear_map.to_normed_group

instance [finite_dimensional ğ•œ E] : finite_dimensional ğ•œ (dual ğ•œ E) :=
continuous_linear_map.finite_dimensional

/-- The inclusion of a normed space in its double (topological) dual, considered
   as a bounded linear map. -/
def inclusion_in_double_dual : E â†’L[ğ•œ] (dual ğ•œ (dual ğ•œ E)) :=
continuous_linear_map.apply ğ•œ ğ•œ

@[simp] lemma dual_def (x : E) (f : dual ğ•œ E) : inclusion_in_double_dual ğ•œ E x f = f x := rfl

lemma inclusion_in_double_dual_norm_eq :
  âˆ¥inclusion_in_double_dual ğ•œ Eâˆ¥ = âˆ¥(continuous_linear_map.id ğ•œ (dual ğ•œ E))âˆ¥ :=
continuous_linear_map.op_norm_flip _

lemma inclusion_in_double_dual_norm_le : âˆ¥inclusion_in_double_dual ğ•œ Eâˆ¥ â‰¤ 1 :=
by { rw inclusion_in_double_dual_norm_eq, exact continuous_linear_map.norm_id_le }

lemma double_dual_bound (x : E) : âˆ¥(inclusion_in_double_dual ğ•œ E) xâˆ¥ â‰¤ âˆ¥xâˆ¥ :=
by simpa using continuous_linear_map.le_of_op_norm_le _ (inclusion_in_double_dual_norm_le ğ•œ E) x

/-- The dual pairing as a bilinear form. -/
def dual_pairing : (dual ğ•œ E) â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] ğ•œ := continuous_linear_map.coe_lm ğ•œ

@[simp] lemma dual_pairing_apply {v : dual ğ•œ E} {x : E} : dual_pairing ğ•œ E v x = v x := rfl

lemma dual_pairing_separating_left : (dual_pairing ğ•œ E).separating_left :=
begin
  rw [linear_map.separating_left_iff_ker_eq_bot, linear_map.ker_eq_bot],
  exact continuous_linear_map.coe_injective,
end

end general

section bidual_isometry

variables (ğ•œ : Type v) [is_R_or_C ğ•œ]
  {E : Type u} [normed_group E] [normed_space ğ•œ E]

/-- If one controls the norm of every `f x`, then one controls the norm of `x`.
    Compare `continuous_linear_map.op_norm_le_bound`. -/
lemma norm_le_dual_bound (x : E) {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ (f : dual ğ•œ E), âˆ¥f xâˆ¥ â‰¤ M * âˆ¥fâˆ¥) :
  âˆ¥xâˆ¥ â‰¤ M :=
begin
  classical,
  by_cases h : x = 0,
  { simp only [h, hMp, norm_zero] },
  { obtain âŸ¨f, hfâ‚, hfxâŸ© : âˆƒ f : E â†’L[ğ•œ] ğ•œ, âˆ¥fâˆ¥ = 1 âˆ§ f x = âˆ¥xâˆ¥ := exists_dual_vector ğ•œ x h,
    calc âˆ¥xâˆ¥ = âˆ¥(âˆ¥xâˆ¥ : ğ•œ)âˆ¥ : is_R_or_C.norm_coe_norm.symm
    ... = âˆ¥f xâˆ¥ : by rw hfx
    ... â‰¤ M * âˆ¥fâˆ¥ : hM f
    ... = M : by rw [hfâ‚, mul_one] }
end

lemma eq_zero_of_forall_dual_eq_zero {x : E} (h : âˆ€ f : dual ğ•œ E, f x = (0 : ğ•œ)) : x = 0 :=
norm_le_zero_iff.mp (norm_le_dual_bound ğ•œ x le_rfl (Î» f, by simp [h f]))

lemma eq_zero_iff_forall_dual_eq_zero (x : E) : x = 0 â†” âˆ€ g : dual ğ•œ E, g x = 0 :=
âŸ¨Î» hx, by simp [hx], Î» h, eq_zero_of_forall_dual_eq_zero ğ•œ hâŸ©

lemma eq_iff_forall_dual_eq {x y : E} :
  x = y â†” âˆ€ g : dual ğ•œ E, g x = g y :=
begin
  rw [â† sub_eq_zero, eq_zero_iff_forall_dual_eq_zero ğ•œ (x - y)],
  simp [sub_eq_zero],
end

/-- The inclusion of a normed space in its double dual is an isometry onto its image.-/
def inclusion_in_double_dual_li : E â†’â‚—áµ¢[ğ•œ] (dual ğ•œ (dual ğ•œ E)) :=
{ norm_map' := begin
    intros x,
    apply le_antisymm,
    { exact double_dual_bound ğ•œ E x },
    rw continuous_linear_map.norm_def,
    refine le_cInf continuous_linear_map.bounds_nonempty _,
    rintros c âŸ¨hc1, hc2âŸ©,
    exact norm_le_dual_bound ğ•œ x hc1 hc2
  end,
  .. inclusion_in_double_dual ğ•œ E }

end bidual_isometry

section polar_sets

open metric set normed_space

/-- Given a subset `s` in a normed space `E` (over a field `ğ•œ`), the polar
`polar ğ•œ s` is the subset of `dual ğ•œ E` consisting of those functionals which
evaluate to something of norm at most one at all points `z âˆˆ s`. -/
def polar (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  {E : Type*} [semi_normed_group E] [normed_space ğ•œ E] : set E â†’ set (dual ğ•œ E) :=
(dual_pairing ğ•œ E).flip.polar

variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [semi_normed_group E] [normed_space ğ•œ E]

lemma mem_polar_iff {x' : dual ğ•œ E} (s : set E) : x' âˆˆ polar ğ•œ s â†” âˆ€ z âˆˆ s, âˆ¥x' zâˆ¥ â‰¤ 1 := iff.rfl

@[simp] lemma polar_univ : polar ğ•œ (univ : set E) = {(0 : dual ğ•œ E)} :=
(dual_pairing ğ•œ E).flip.polar_univ
  (linear_map.flip_separating_right.mpr (dual_pairing_separating_left ğ•œ E))

lemma is_closed_polar (s : set E) : is_closed (polar ğ•œ s) :=
begin
  dunfold normed_space.polar,
  simp only [linear_map.polar_eq_Inter, linear_map.flip_apply],
  refine is_closed_bInter (Î» z hz, _),
  exact is_closed_Iic.preimage (continuous_linear_map.apply ğ•œ ğ•œ z).continuous.norm
end

@[simp] lemma polar_closure (s : set E) : polar ğ•œ (closure s) = polar ğ•œ s :=
((dual_pairing ğ•œ E).flip.polar_antitone subset_closure).antisymm $
  (dual_pairing ğ•œ E).flip.polar_gc.l_le $
  closure_minimal ((dual_pairing ğ•œ E).flip.polar_gc.le_u_l s) $
  by simpa [linear_map.flip_flip]
    using (is_closed_polar _ _).preimage (inclusion_in_double_dual ğ•œ E).continuous

variables {ğ•œ}

/-- If `x'` is a dual element such that the norms `âˆ¥x' zâˆ¥` are bounded for `z âˆˆ s`, then a
small scalar multiple of `x'` is in `polar ğ•œ s`. -/
lemma smul_mem_polar {s : set E} {x' : dual ğ•œ E} {c : ğ•œ}
  (hc : âˆ€ z, z âˆˆ s â†’ âˆ¥ x' z âˆ¥ â‰¤ âˆ¥câˆ¥) : câ»Â¹ â€¢ x' âˆˆ polar ğ•œ s :=
begin
  by_cases c_zero : c = 0, { simp only [c_zero, inv_zero, zero_smul],
    exact (dual_pairing ğ•œ E).flip.zero_mem_polar _ },
  have eq : âˆ€ z, âˆ¥ câ»Â¹ â€¢ (x' z) âˆ¥ = âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ x' z âˆ¥ := Î» z, norm_smul câ»Â¹ _,
  have le : âˆ€ z, z âˆˆ s â†’ âˆ¥ câ»Â¹ â€¢ (x' z) âˆ¥ â‰¤ âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ c âˆ¥,
  { intros z hzs,
    rw eq z,
    apply mul_le_mul (le_of_eq rfl) (hc z hzs) (norm_nonneg _) (norm_nonneg _), },
  have cancel : âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ c âˆ¥ = 1,
  by simp only [c_zero, norm_eq_zero, ne.def, not_false_iff,
                inv_mul_cancel, norm_inv],
  rwa cancel at le,
end

lemma polar_ball_subset_closed_ball_div {c : ğ•œ} (hc : 1 < âˆ¥câˆ¥) {r : â„} (hr : 0 < r) :
  polar ğ•œ (ball (0 : E) r) âŠ† closed_ball (0 : dual ğ•œ E) (âˆ¥câˆ¥ / r) :=
begin
  intros x' hx',
  rw mem_polar_iff at hx',
  simp only [polar, mem_set_of_eq, mem_closed_ball_zero_iff, mem_ball_zero_iff] at *,
  have hcr : 0 < âˆ¥câˆ¥ / r, from div_pos (zero_lt_one.trans hc) hr,
  refine continuous_linear_map.op_norm_le_of_shell hr hcr.le hc (Î» x hâ‚ hâ‚‚, _),
  calc âˆ¥x' xâˆ¥ â‰¤ 1 : hx' _ hâ‚‚
  ... â‰¤ (âˆ¥câˆ¥ / r) * âˆ¥xâˆ¥ : (inv_pos_le_iff_one_le_mul' hcr).1 (by rwa inv_div)
end

variables (ğ•œ)

lemma closed_ball_inv_subset_polar_closed_ball {r : â„} :
  closed_ball (0 : dual ğ•œ E) râ»Â¹ âŠ† polar ğ•œ (closed_ball (0 : E) r) :=
Î» x' hx' x hx,
calc âˆ¥x' xâˆ¥ â‰¤ âˆ¥x'âˆ¥ * âˆ¥xâˆ¥ : x'.le_op_norm x
... â‰¤ râ»Â¹ * r :
  mul_le_mul (mem_closed_ball_zero_iff.1 hx') (mem_closed_ball_zero_iff.1 hx)
    (norm_nonneg _) (dist_nonneg.trans hx')
... = r / r : inv_mul_eq_div _ _
... â‰¤ 1 : div_self_le_one r

/-- The `polar` of closed ball in a normed space `E` is the closed ball of the dual with
inverse radius. -/
lemma polar_closed_ball
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E] {r : â„} (hr : 0 < r) :
  polar ğ•œ (closed_ball (0 : E) r) = closed_ball (0 : dual ğ•œ E) râ»Â¹ :=
begin
  refine subset.antisymm _ (closed_ball_inv_subset_polar_closed_ball _),
  intros x' h,
  simp only [mem_closed_ball_zero_iff],
  refine continuous_linear_map.op_norm_le_of_ball hr (inv_nonneg.mpr hr.le) (Î» z hz, _),
  simpa only [one_div] using linear_map.bound_of_ball_bound' hr 1 x'.to_linear_map h z
end

/-- Given a neighborhood `s` of the origin in a normed space `E`, the dual norms
of all elements of the polar `polar ğ•œ s` are bounded by a constant. -/
lemma bounded_polar_of_mem_nhds_zero {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  bounded (polar ğ•œ s) :=
begin
  obtain âŸ¨a, haâŸ© : âˆƒ a : ğ•œ, 1 < âˆ¥aâˆ¥ := normed_field.exists_one_lt_norm ğ•œ,
  obtain âŸ¨r, r_pos, r_ballâŸ© : âˆƒ (r : â„) (hr : 0 < r), ball 0 r âŠ† s :=
    metric.mem_nhds_iff.1 s_nhd,
  exact bounded_closed_ball.mono (((dual_pairing ğ•œ E).flip.polar_antitone r_ball).trans $
    polar_ball_subset_closed_ball_div ha r_pos)
end

end polar_sets

end normed_space
