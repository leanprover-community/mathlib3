/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Eric Wieser
-/
import analysis.specific_limits.basic
import analysis.analytic.basic
import analysis.complex.basic
import data.nat.choose.cast
import data.finset.noncomm_prod

/-!
# Exponential in a Banach algebra

In this file, we define `exp ğ•‚ : ğ”¸ â†’ ğ”¸`, the exponential map in a topological algebra `ğ”¸` over a
field `ğ•‚`.

While for most interesting results we need `ğ”¸` to be normed algebra, we do not require this in the
definition in order to make `exp` independent of a particular choice of norm. The definition also
does not require that `ğ”¸` be complete, but we need to assume it for most results.

We then prove some basic results, but we avoid importing derivatives here to minimize dependencies.
Results involving derivatives and comparisons with `real.exp` and `complex.exp` can be found in
`analysis/special_functions/exponential`.

## Main results

We prove most result for an arbitrary field `ğ•‚`, and then specialize to `ğ•‚ = â„` or `ğ•‚ = â„‚`.

### General case

- `exp_add_of_commute_of_mem_ball` : if `ğ•‚` has characteristic zero, then given two commuting
  elements `x` and `y` in the disk of convergence, we have
  `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
- `exp_add_of_mem_ball` : if `ğ•‚` has characteristic zero and `ğ”¸` is commutative, then given two
  elements `x` and `y` in the disk of convergence, we have
  `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
- `exp_neg_of_mem_ball` : if `ğ•‚` has characteristic zero and `ğ”¸` is a division ring, then given an
  element `x` in the disk of convergence, we have `exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹`.

### `ğ•‚ = â„` or `ğ•‚ = â„‚`

- `exp_series_radius_eq_top` : the `formal_multilinear_series` defining `exp ğ•‚` has infinite
  radius of convergence
- `exp_add_of_commute` : given two commuting elements `x` and `y`, we have
  `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
- `exp_add` : if `ğ”¸` is commutative, then we have `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
  for any `x` and `y`
- `exp_neg` : if `ğ”¸` is a division ring, then we have `exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹`.
- `exp_sum_of_commute` : the analogous result to `exp_add_of_commute` for `finset.sum`.
- `exp_sum` : the analogous result to `exp_add` for `finset.sum`.
- `exp_nsmul` : repeated addition in the domain corresponds to repeated multiplication in the
  codomain.
- `exp_zsmul` : repeated addition in the domain corresponds to repeated multiplication in the
  codomain.

### Other useful compatibility results

- `exp_eq_exp` : if `ğ”¸` is a normed algebra over two fields `ğ•‚` and `ğ•‚'`, then `exp ğ•‚ = exp ğ•‚' ğ”¸`

-/

open filter is_R_or_C continuous_multilinear_map normed_field asymptotics
open_locale nat topological_space big_operators ennreal

section topological_algebra

variables (ğ•‚ ğ”¸ : Type*) [field ğ•‚] [ring ğ”¸] [algebra ğ•‚ ğ”¸] [topological_space ğ”¸]
  [topological_ring ğ”¸]

/-- `exp_series ğ•‚ ğ”¸` is the `formal_multilinear_series` whose `n`-th term is the map
`(xáµ¢) : ğ”¸â¿ â†¦ (1/n! : ğ•‚) â€¢ âˆ xáµ¢`. Its sum is the exponential map `exp ğ•‚ : ğ”¸ â†’ ğ”¸`. -/
def exp_series : formal_multilinear_series ğ•‚ ğ”¸ ğ”¸ :=
Î» n, (n!â»Â¹ : ğ•‚) â€¢ continuous_multilinear_map.mk_pi_algebra_fin ğ•‚ n ğ”¸

variables {ğ”¸}

/-- `exp ğ•‚ : ğ”¸ â†’ ğ”¸` is the exponential map determined by the action of `ğ•‚` on `ğ”¸`.
It is defined as the sum of the `formal_multilinear_series` `exp_series ğ•‚ ğ”¸`.

Note that when `ğ”¸ = matrix n n ğ•‚`, this is the **Matrix Exponential**; see
[`analysis.normed_space.matrix_exponential`](../matrix_exponential) for lemmas specific to that
case. -/
noncomputable def exp (x : ğ”¸) : ğ”¸ := (exp_series ğ•‚ ğ”¸).sum x

variables {ğ•‚}

lemma exp_series_apply_eq (x : ğ”¸) (n : â„•) : exp_series ğ•‚ ğ”¸ n (Î» _, x) = (n!â»Â¹ : ğ•‚) â€¢ x^n :=
by simp [exp_series]

lemma exp_series_apply_eq' (x : ğ”¸) :
  (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) = (Î» n, (n!â»Â¹ : ğ•‚) â€¢ x^n) :=
funext (exp_series_apply_eq x)

lemma exp_series_sum_eq (x : ğ”¸) : (exp_series ğ•‚ ğ”¸).sum x = âˆ‘' (n : â„•), (n!â»Â¹ : ğ•‚) â€¢ x^n :=
tsum_congr (Î» n, exp_series_apply_eq x n)

lemma exp_eq_tsum : exp ğ•‚ = (Î» x : ğ”¸, âˆ‘' (n : â„•), (n!â»Â¹ : ğ•‚) â€¢ x^n) :=
funext exp_series_sum_eq

@[simp] lemma exp_zero [t2_space ğ”¸] : exp ğ•‚ (0 : ğ”¸) = 1 :=
begin
  suffices : (Î» x : ğ”¸, âˆ‘' (n : â„•), (n!â»Â¹ : ğ•‚) â€¢ x^n) 0 = âˆ‘' (n : â„•), if n = 0 then 1 else 0,
  { have key : âˆ€ n âˆ‰ ({0} : finset â„•), (if n = 0 then (1 : ğ”¸) else 0) = 0,
      from Î» n hn, if_neg (finset.not_mem_singleton.mp hn),
    rw [exp_eq_tsum, this, tsum_eq_sum key, finset.sum_singleton],
    simp },
  refine tsum_congr (Î» n, _),
  split_ifs with h h;
  simp [h]
end

@[simp] lemma exp_op [t2_space ğ”¸] (x : ğ”¸) :
  exp ğ•‚ (mul_opposite.op x) = mul_opposite.op (exp ğ•‚ x) :=
by simp_rw [exp, exp_series_sum_eq, â†mul_opposite.op_pow, â†mul_opposite.op_smul, tsum_op]

@[simp] lemma exp_unop [t2_space ğ”¸] (x : ğ”¸áµáµ’áµ–) :
  exp ğ•‚ (mul_opposite.unop x) = mul_opposite.unop (exp ğ•‚ x) :=
by simp_rw [exp, exp_series_sum_eq, â†mul_opposite.unop_pow, â†mul_opposite.unop_smul, tsum_unop]

lemma star_exp [t2_space ğ”¸] [star_ring ğ”¸] [has_continuous_star ğ”¸] (x : ğ”¸) :
  star (exp ğ•‚ x) = exp ğ•‚ (star x) :=
by simp_rw [exp_eq_tsum, â†star_pow, â†star_inv_nat_cast_smul, â†tsum_star]

variables (ğ•‚)

lemma commute.exp_right [t2_space ğ”¸] {x y : ğ”¸} (h : commute x y) : commute x (exp ğ•‚ y) :=
begin
  rw exp_eq_tsum,
  exact commute.tsum_right x (Î» n, (h.pow_right n).smul_right _),
end

lemma commute.exp_left [t2_space ğ”¸] {x y : ğ”¸} (h : commute x y) : commute (exp ğ•‚ x) y :=
(h.symm.exp_right ğ•‚).symm

lemma commute.exp [t2_space ğ”¸] {x y : ğ”¸} (h : commute x y) : commute (exp ğ•‚ x) (exp ğ•‚ y) :=
(h.exp_left _).exp_right _

end topological_algebra

section topological_division_algebra
variables {ğ•‚ ğ”¸ : Type*} [field ğ•‚] [division_ring ğ”¸] [algebra ğ•‚ ğ”¸] [topological_space ğ”¸]
  [topological_ring ğ”¸]

lemma exp_series_apply_eq_div (x : ğ”¸) (n : â„•) : exp_series ğ•‚ ğ”¸ n (Î» _, x) = x^n / n! :=
by rw [div_eq_mul_inv, â†(nat.cast_commute n! (x ^ n)).inv_leftâ‚€.eq, â†smul_eq_mul,
    exp_series_apply_eq, inv_nat_cast_smul_eq _ _ _ _]

lemma exp_series_apply_eq_div' (x : ğ”¸) : (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) = (Î» n, x^n / n!) :=
funext (exp_series_apply_eq_div x)

lemma exp_series_sum_eq_div (x : ğ”¸) : (exp_series ğ•‚ ğ”¸).sum x = âˆ‘' (n : â„•), x^n / n! :=
tsum_congr (exp_series_apply_eq_div x)

lemma exp_eq_tsum_div : exp ğ•‚ = (Î» x : ğ”¸, âˆ‘' (n : â„•), x^n / n!) :=
funext exp_series_sum_eq_div

end topological_division_algebra

section normed

section any_field_any_algebra

variables {ğ•‚ ğ”¸ ğ”¹ : Type*} [nondiscrete_normed_field ğ•‚]
variables [normed_ring ğ”¸] [normed_ring ğ”¹] [normed_algebra ğ•‚ ğ”¸] [normed_algebra ğ•‚ ğ”¹]

lemma norm_exp_series_summable_of_mem_ball (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, âˆ¥exp_series ğ•‚ ğ”¸ n (Î» _, x)âˆ¥) :=
(exp_series ğ•‚ ğ”¸).summable_norm_apply hx

lemma norm_exp_series_summable_of_mem_ball' (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, âˆ¥(n!â»Â¹ : ğ•‚) â€¢ x^nâˆ¥) :=
begin
  change summable (norm âˆ˜ _),
  rw â† exp_series_apply_eq',
  exact norm_exp_series_summable_of_mem_ball x hx
end

section complete_algebra

variables [complete_space ğ”¸]

lemma exp_series_summable_of_mem_ball (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) :=
summable_of_summable_norm (norm_exp_series_summable_of_mem_ball x hx)

lemma exp_series_summable_of_mem_ball' (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, (n!â»Â¹ : ğ•‚) â€¢ x^n) :=
summable_of_summable_norm (norm_exp_series_summable_of_mem_ball' x hx)

lemma exp_series_has_sum_exp_of_mem_ball (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_sum (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) (exp ğ•‚ x) :=
formal_multilinear_series.has_sum (exp_series ğ•‚ ğ”¸) hx

lemma exp_series_has_sum_exp_of_mem_ball' (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_sum (Î» n, (n!â»Â¹ : ğ•‚) â€¢ x^n) (exp ğ•‚ x):=
begin
  rw â† exp_series_apply_eq',
  exact exp_series_has_sum_exp_of_mem_ball x hx
end

lemma has_fpower_series_on_ball_exp_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_fpower_series_on_ball (exp ğ•‚) (exp_series ğ•‚ ğ”¸) 0 (exp_series ğ•‚ ğ”¸).radius :=
(exp_series ğ•‚ ğ”¸).has_fpower_series_on_ball h

lemma has_fpower_series_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_fpower_series_at (exp ğ•‚) (exp_series ğ•‚ ğ”¸) 0 :=
(has_fpower_series_on_ball_exp_of_radius_pos h).has_fpower_series_at

lemma continuous_on_exp :
  continuous_on (exp ğ•‚ : ğ”¸ â†’ ğ”¸) (emetric.ball 0 (exp_series ğ•‚ ğ”¸).radius) :=
formal_multilinear_series.continuous_on

lemma analytic_at_exp_of_mem_ball (x : ğ”¸) (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  analytic_at ğ•‚ (exp ğ•‚) x:=
begin
  by_cases h : (exp_series ğ•‚ ğ”¸).radius = 0,
  { rw h at hx, exact (ennreal.not_lt_zero hx).elim },
  { have h := pos_iff_ne_zero.mpr h,
    exact (has_fpower_series_on_ball_exp_of_radius_pos h).analytic_at_of_mem hx }
end

/-- In a Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero, if `x` and `y` are
in the disk of convergence and commute, then `exp ğ•‚ (x + y) = (exp ğ•‚ x) * (exp ğ•‚ y)`. -/
lemma exp_add_of_commute_of_mem_ball [char_zero ğ•‚]
  {x y : ğ”¸} (hxy : commute x y) (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius)
  (hy : y âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  exp ğ•‚ (x + y) = (exp ğ•‚ x) * (exp ğ•‚ y) :=
begin
  rw [exp_eq_tsum, tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm
        (norm_exp_series_summable_of_mem_ball' x hx) (norm_exp_series_summable_of_mem_ball' y hy)],
  dsimp only,
  conv_lhs {congr, funext, rw [hxy.add_pow' _, finset.smul_sum]},
  refine tsum_congr (Î» n, finset.sum_congr rfl $ Î» kl hkl, _),
  rw [nsmul_eq_smul_cast ğ•‚, smul_smul, smul_mul_smul, â† (finset.nat.mem_antidiagonal.mp hkl),
      nat.cast_add_choose, (finset.nat.mem_antidiagonal.mp hkl)],
  congr' 1,
  have : (n! : ğ•‚) â‰  0 := nat.cast_ne_zero.mpr n.factorial_ne_zero,
  field_simp [this]
end

/-- `exp ğ•‚ x` has explicit two-sided inverse `exp ğ•‚ (-x)`. -/
noncomputable def invertible_exp_of_mem_ball [char_zero ğ•‚] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) : invertible (exp ğ•‚ x) :=
{ inv_of := exp ğ•‚ (-x),
  inv_of_mul_self := begin
    have hnx : -x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius,
    { rw [emetric.mem_ball, â†neg_zero, edist_neg_neg],
      exact hx },
    rw [â†exp_add_of_commute_of_mem_ball (commute.neg_left $ commute.refl x) hnx hx, neg_add_self,
      exp_zero],
  end,
  mul_inv_of_self := begin
    have hnx : -x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius,
    { rw [emetric.mem_ball, â†neg_zero, edist_neg_neg],
      exact hx },
    rw [â†exp_add_of_commute_of_mem_ball (commute.neg_right $ commute.refl x) hx hnx, add_neg_self,
      exp_zero],
  end }

lemma is_unit_exp_of_mem_ball [char_zero ğ•‚] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) : is_unit (exp ğ•‚ x) :=
@is_unit_of_invertible _ _ _ (invertible_exp_of_mem_ball hx)

lemma inv_of_exp_of_mem_ball [char_zero ğ•‚] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) [invertible (exp ğ•‚ x)] :
  â…Ÿ(exp ğ•‚ x) = exp ğ•‚ (-x) :=
by { letI := invertible_exp_of_mem_ball hx, convert (rfl : â…Ÿ(exp ğ•‚ x) = _) }

/-- Any continuous ring homomorphism commutes with `exp`. -/
lemma map_exp_of_mem_ball {F} [ring_hom_class F ğ”¸ ğ”¹] (f : F) (hf : continuous f) (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  f (exp ğ•‚ x) = exp ğ•‚ (f x) :=
begin
  rw [exp_eq_tsum, exp_eq_tsum],
  refine ((exp_series_summable_of_mem_ball' _ hx).has_sum.map f hf).tsum_eq.symm.trans _,
  dsimp only [function.comp],
  simp_rw [one_div, map_inv_nat_cast_smul f ğ•‚ ğ•‚, map_pow],
end

end complete_algebra

lemma algebra_map_exp_comm_of_mem_ball [complete_space ğ•‚] (x : ğ•‚)
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) :
  algebra_map ğ•‚ ğ”¸ (exp ğ•‚ x) = exp ğ•‚ (algebra_map ğ•‚ ğ”¸ x) :=
map_exp_of_mem_ball _ (algebra_map_clm _ _).continuous _ hx

end any_field_any_algebra

section any_field_division_algebra

variables {ğ•‚ ğ”¸ : Type*} [nondiscrete_normed_field ğ•‚] [normed_division_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]

variables (ğ•‚)

lemma norm_exp_series_div_summable_of_mem_ball (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, âˆ¥x^n / n!âˆ¥) :=
begin
  change summable (norm âˆ˜ _),
  rw â† exp_series_apply_eq_div' x,
  exact norm_exp_series_summable_of_mem_ball x hx
end

lemma exp_series_div_summable_of_mem_ball [complete_space ğ”¸] (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) : summable (Î» n, x^n / n!) :=
summable_of_summable_norm (norm_exp_series_div_summable_of_mem_ball ğ•‚ x hx)

lemma exp_series_div_has_sum_exp_of_mem_ball [complete_space ğ”¸] (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) : has_sum (Î» n, x^n / n!) (exp ğ•‚ x) :=
begin
  rw â† exp_series_apply_eq_div' x,
  exact exp_series_has_sum_exp_of_mem_ball x hx
end

variables {ğ•‚}

lemma exp_neg_of_mem_ball [char_zero ğ•‚] [complete_space ğ”¸] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹ :=
begin
  letI := invertible_exp_of_mem_ball hx,
  exact inv_of_eq_inv (exp ğ•‚ x),
end

end any_field_division_algebra


section any_field_comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [nondiscrete_normed_field ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- In a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero,
`exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)` for all `x`, `y` in the disk of convergence. -/
lemma exp_add_of_mem_ball [char_zero ğ•‚] {x y : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius)
  (hy : y âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  exp ğ•‚ (x + y) = (exp ğ•‚ x) * (exp ğ•‚ y) :=
exp_add_of_commute_of_mem_ball (commute.all x y) hx hy

end any_field_comm_algebra

section is_R_or_C

section any_algebra

variables (ğ•‚ ğ”¸ ğ”¹ : Type*) [is_R_or_C ğ•‚] [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
variables [normed_ring ğ”¹] [normed_algebra ğ•‚ ğ”¹]

/-- In a normed algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, the series defining the exponential map
has an infinite radius of convergence. -/
lemma exp_series_radius_eq_top : (exp_series ğ•‚ ğ”¸).radius = âˆ :=
begin
  refine (exp_series ğ•‚ ğ”¸).radius_eq_top_of_summable_norm (Î» r, _),
  refine summable_of_norm_bounded_eventually _ (real.summable_pow_div_factorial r) _,
  filter_upwards [eventually_cofinite_ne 0] with n hn,
  rw [norm_mul, norm_norm (exp_series ğ•‚ ğ”¸ n), exp_series, norm_smul, norm_inv, norm_pow,
      nnreal.norm_eq, norm_eq_abs, abs_cast_nat, mul_comm, â†mul_assoc, â†div_eq_mul_inv],
  have : âˆ¥continuous_multilinear_map.mk_pi_algebra_fin ğ•‚ n ğ”¸âˆ¥ â‰¤ 1 :=
    norm_mk_pi_algebra_fin_le_of_pos (nat.pos_of_ne_zero hn),
  exact mul_le_of_le_one_right (div_nonneg (pow_nonneg r.coe_nonneg n) n!.cast_nonneg) this
end

lemma exp_series_radius_pos : 0 < (exp_series ğ•‚ ğ”¸).radius :=
begin
  rw exp_series_radius_eq_top,
  exact with_top.zero_lt_top
end

variables {ğ•‚ ğ”¸ ğ”¹}

lemma norm_exp_series_summable (x : ğ”¸) : summable (Î» n, âˆ¥exp_series ğ•‚ ğ”¸ n (Î» _, x)âˆ¥) :=
norm_exp_series_summable_of_mem_ball x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

lemma norm_exp_series_summable' (x : ğ”¸) : summable (Î» n, âˆ¥(n!â»Â¹ : ğ•‚) â€¢ x^nâˆ¥) :=
norm_exp_series_summable_of_mem_ball' x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

section complete_algebra

variables [complete_space ğ”¸]

lemma exp_series_summable (x : ğ”¸) : summable (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) :=
summable_of_summable_norm (norm_exp_series_summable x)

lemma exp_series_summable' (x : ğ”¸) : summable (Î» n, (n!â»Â¹ : ğ•‚) â€¢ x^n) :=
summable_of_summable_norm (norm_exp_series_summable' x)

lemma exp_series_has_sum_exp (x : ğ”¸) : has_sum (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) (exp ğ•‚ x) :=
exp_series_has_sum_exp_of_mem_ball x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

lemma exp_series_has_sum_exp' (x : ğ”¸) : has_sum (Î» n, (n!â»Â¹ : ğ•‚) â€¢ x^n) (exp ğ•‚ x):=
exp_series_has_sum_exp_of_mem_ball' x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

lemma exp_has_fpower_series_on_ball :
  has_fpower_series_on_ball (exp ğ•‚) (exp_series ğ•‚ ğ”¸) 0 âˆ :=
exp_series_radius_eq_top ğ•‚ ğ”¸ â–¸
  has_fpower_series_on_ball_exp_of_radius_pos (exp_series_radius_pos _ _)

lemma exp_has_fpower_series_at_zero :
  has_fpower_series_at (exp ğ•‚) (exp_series ğ•‚ ğ”¸) 0 :=
exp_has_fpower_series_on_ball.has_fpower_series_at

lemma exp_continuous : continuous (exp ğ•‚ : ğ”¸ â†’ ğ”¸) :=
begin
  rw [continuous_iff_continuous_on_univ, â† metric.eball_top_eq_univ (0 : ğ”¸),
      â† exp_series_radius_eq_top ğ•‚ ğ”¸],
  exact continuous_on_exp
end

lemma exp_analytic (x : ğ”¸) :
  analytic_at ğ•‚ (exp ğ•‚) x :=
analytic_at_exp_of_mem_ball x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

/-- In a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, if `x` and `y` commute, then
`exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`. -/
lemma exp_add_of_commute
  {x y : ğ”¸} (hxy : commute x y) :
  exp ğ•‚ (x + y) = (exp ğ•‚ x) * (exp ğ•‚ y) :=
exp_add_of_commute_of_mem_ball hxy ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
  ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

section
variables (ğ•‚)

/-- `exp ğ•‚ x` has explicit two-sided inverse `exp ğ•‚ (-x)`. -/
noncomputable def invertible_exp (x : ğ”¸) : invertible (exp ğ•‚ x) :=
invertible_exp_of_mem_ball $ (exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

lemma is_unit_exp (x : ğ”¸) : is_unit (exp ğ•‚ x) :=
is_unit_exp_of_mem_ball $ (exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

lemma inv_of_exp (x : ğ”¸) [invertible (exp ğ•‚ x)] :
  â…Ÿ(exp ğ•‚ x) = exp ğ•‚ (-x) :=
inv_of_exp_of_mem_ball $ (exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

lemma ring.inverse_exp (x : ğ”¸) : ring.inverse (exp ğ•‚ x) = exp ğ•‚ (-x) :=
begin
  letI := invertible_exp ğ•‚ x,
  exact ring.inverse_invertible _,
end

end

/-- In a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, if a family of elements `f i` mutually
commute then `exp ğ•‚ (âˆ‘ i, f i) = âˆ i, exp ğ•‚ (f i)`. -/
lemma exp_sum_of_commute {Î¹} (s : finset Î¹) (f : Î¹ â†’ ğ”¸)
  (h : âˆ€ (i âˆˆ s) (j âˆˆ s), commute (f i) (f j)) :
  exp ğ•‚ (âˆ‘ i in s, f i) = s.noncomm_prod (Î» i, exp ğ•‚ (f i))
    (Î» i hi j hj, (h i hi j hj).exp ğ•‚) :=
begin
  classical,
  induction s using finset.induction_on with a s ha ih,
  { simp },
  rw [finset.noncomm_prod_insert_of_not_mem _ _ _ _ ha, finset.sum_insert ha,
      exp_add_of_commute, ih],
  refine commute.sum_right _ _ _ _,
  intros i hi,
  exact h _ (finset.mem_insert_self _ _) _ (finset.mem_insert_of_mem hi),
end

lemma exp_nsmul (n : â„•) (x : ğ”¸) :
  exp ğ•‚ (n â€¢ x) = exp ğ•‚ x ^ n :=
begin
  induction n with n ih,
  { rw [zero_smul, pow_zero, exp_zero], },
  { rw [succ_nsmul, pow_succ, exp_add_of_commute ((commute.refl x).smul_right n), ih] }
end

variables (ğ•‚)

/-- Any continuous ring homomorphism commutes with `exp`. -/
lemma map_exp {F} [ring_hom_class F ğ”¸ ğ”¹] (f : F) (hf : continuous f) (x : ğ”¸) :
  f (exp ğ•‚ x) = exp ğ•‚ (f x) :=
map_exp_of_mem_ball f hf x $ (exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

lemma exp_smul {G} [monoid G] [mul_semiring_action G ğ”¸] [has_continuous_const_smul G ğ”¸]
  (g : G) (x : ğ”¸) :
  exp ğ•‚ (g â€¢ x) = g â€¢ exp ğ•‚ x :=
(map_exp ğ•‚ (mul_semiring_action.to_ring_hom G ğ”¸ g) (continuous_const_smul _) x).symm

lemma exp_units_conj (y : ğ”¸Ë£) (x : ğ”¸)  :
  exp ğ•‚ (y * x * â†‘(yâ»Â¹) : ğ”¸) = y * exp ğ•‚ x * â†‘(yâ»Â¹) :=
exp_smul _ (conj_act.to_conj_act y) x

lemma exp_units_conj' (y : ğ”¸Ë£) (x : ğ”¸)  :
  exp ğ•‚ (â†‘(yâ»Â¹) * x * y) = â†‘(yâ»Â¹) * exp ğ•‚ x * y :=
exp_units_conj _ _ _

@[simp] lemma prod.fst_exp [complete_space ğ”¹] (x : ğ”¸ Ã— ğ”¹) : (exp ğ•‚ x).fst = exp ğ•‚ x.fst :=
map_exp _ (ring_hom.fst ğ”¸ ğ”¹) continuous_fst x

@[simp] lemma prod.snd_exp [complete_space ğ”¹] (x : ğ”¸ Ã— ğ”¹) : (exp ğ•‚ x).snd = exp ğ•‚ x.snd :=
map_exp _ (ring_hom.snd ğ”¸ ğ”¹) continuous_snd x

@[simp] lemma pi.exp_apply {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [fintype Î¹]
  [Î  i, normed_ring (ğ”¸ i)] [Î  i, normed_algebra ğ•‚ (ğ”¸ i)] [Î  i, complete_space (ğ”¸ i)]
  (x : Î  i, ğ”¸ i) (i : Î¹) :
  exp ğ•‚ x i = exp ğ•‚ (x i) :=
begin
  -- Lean struggles to infer this instance due to it wanting `[Î  i, semi_normed_ring (ğ”¸ i)]`
  letI : normed_algebra ğ•‚ (Î  i, ğ”¸ i) := pi.normed_algebra _,
  exact map_exp _ (pi.eval_ring_hom ğ”¸ i) (continuous_apply _) x
end

lemma pi.exp_def {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [fintype Î¹]
  [Î  i, normed_ring (ğ”¸ i)] [Î  i, normed_algebra ğ•‚ (ğ”¸ i)] [Î  i, complete_space (ğ”¸ i)]
  (x : Î  i, ğ”¸ i) :
  exp ğ•‚ x = Î» i, exp ğ•‚ (x i) :=
funext $ pi.exp_apply ğ•‚ x

lemma function.update_exp {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [fintype Î¹] [decidable_eq Î¹]
  [Î  i, normed_ring (ğ”¸ i)] [Î  i, normed_algebra ğ•‚ (ğ”¸ i)] [Î  i, complete_space (ğ”¸ i)]
  (x : Î  i, ğ”¸ i) (j : Î¹) (xj : ğ”¸ j) :
  function.update (exp ğ•‚ x) j (exp ğ•‚ xj) = exp ğ•‚ (function.update x j xj) :=
begin
  ext i,
  simp_rw [pi.exp_def],
  exact (function.apply_update (Î» i, exp ğ•‚) x j xj i).symm,
end

end complete_algebra

lemma algebra_map_exp_comm (x : ğ•‚) :
  algebra_map ğ•‚ ğ”¸ (exp ğ•‚ x) = exp ğ•‚ (algebra_map ğ•‚ ğ”¸ x) :=
algebra_map_exp_comm_of_mem_ball x $
  (exp_series_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _

end any_algebra

section division_algebra

variables {ğ•‚ ğ”¸ : Type*} [is_R_or_C ğ•‚] [normed_division_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]

variables (ğ•‚)

lemma norm_exp_series_div_summable (x : ğ”¸) : summable (Î» n, âˆ¥x^n / n!âˆ¥) :=
norm_exp_series_div_summable_of_mem_ball ğ•‚ x
  ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

variables [complete_space ğ”¸]

lemma exp_series_div_summable (x : ğ”¸) : summable (Î» n, x^n / n!) :=
summable_of_summable_norm (norm_exp_series_div_summable ğ•‚ x)

lemma exp_series_div_has_sum_exp (x : ğ”¸) : has_sum (Î» n, x^n / n!) (exp ğ•‚ x):=
exp_series_div_has_sum_exp_of_mem_ball ğ•‚ x
  ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

variables {ğ•‚}

lemma exp_neg (x : ğ”¸) : exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹ :=
exp_neg_of_mem_ball $ (exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

lemma exp_zsmul (z : â„¤) (x : ğ”¸) : exp ğ•‚ (z â€¢ x) = (exp ğ•‚ x) ^ z :=
begin
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_coe_or_neg,
  { rw [zpow_coe_nat, coe_nat_zsmul, exp_nsmul] },
  { rw [zpow_negâ‚€, zpow_coe_nat, neg_smul, exp_neg, coe_nat_zsmul, exp_nsmul] },
end

lemma exp_conj (y : ğ”¸) (x : ğ”¸) (hy : y â‰  0) :
  exp ğ•‚ (y * x * yâ»Â¹) = y * exp ğ•‚ x * yâ»Â¹ :=
exp_units_conj _ (units.mk0 y hy) x

lemma exp_conj' (y : ğ”¸) (x : ğ”¸)  (hy : y â‰  0) :
  exp ğ•‚ (yâ»Â¹ * x * y) = yâ»Â¹ * exp ğ•‚ x * y :=
exp_units_conj' _ (units.mk0 y hy) x

end division_algebra

section comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [is_R_or_C ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸] [complete_space ğ”¸]

/-- In a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`,
`exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`. -/
lemma exp_add {x y : ğ”¸} : exp ğ•‚ (x + y) = (exp ğ•‚ x) * (exp ğ•‚ y) :=
exp_add_of_mem_ball ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
  ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

/-- A version of `exp_sum_of_commute` for a commutative Banach-algebra. -/
lemma exp_sum {Î¹} (s : finset Î¹) (f : Î¹ â†’ ğ”¸) :
  exp ğ•‚ (âˆ‘ i in s, f i) = âˆ i in s, exp ğ•‚ (f i) :=
begin
  rw [exp_sum_of_commute, finset.noncomm_prod_eq_prod],
  exact Î» i hi j hj, commute.all _ _,
end

end comm_algebra

end is_R_or_C

end normed

section scalar_tower

variables (ğ•‚ ğ•‚' ğ”¸ : Type*) [field ğ•‚] [field ğ•‚'] [ring ğ”¸] [algebra ğ•‚ ğ”¸] [algebra ğ•‚' ğ”¸]
  [topological_space ğ”¸] [topological_ring ğ”¸]

/-- If a normed ring `ğ”¸` is a normed algebra over two fields, then they define the same
`exp_series` on `ğ”¸`. -/
lemma exp_series_eq_exp_series (n : â„•) (x : ğ”¸) :
  (exp_series ğ•‚ ğ”¸ n (Î» _, x)) = (exp_series ğ•‚' ğ”¸ n (Î» _, x)) :=
by rw [exp_series_apply_eq, exp_series_apply_eq, inv_nat_cast_smul_eq ğ•‚ ğ•‚']

/-- If a normed ring `ğ”¸` is a normed algebra over two fields, then they define the same
exponential function on `ğ”¸`. -/
lemma exp_eq_exp : (exp ğ•‚ : ğ”¸ â†’ ğ”¸) = exp ğ•‚' :=
begin
  ext,
  rw [exp, exp],
  refine tsum_congr (Î» n, _),
  rw exp_series_eq_exp_series ğ•‚ ğ•‚' ğ”¸ n x
end

lemma exp_â„_â„‚_eq_exp_â„‚_â„‚ : (exp â„ : â„‚ â†’ â„‚) = exp â„‚ :=
exp_eq_exp â„ â„‚ â„‚

end scalar_tower
