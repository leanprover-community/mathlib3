/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.specific_limits
import analysis.analytic.basic
import analysis.complex.basic
import data.nat.choose.cast

/-!
# Exponential in a Banach algebra

In this file, we define `exp ğ•‚ ğ”¸`, the exponential map in a normed algebra `ğ”¸` over a nondiscrete
normed field `ğ•‚`. Although the definition doesn't require `ğ”¸` to be complete, we need to assume it
for most results.

We then prove some basic results, but we avoid importing derivatives here to minimize dependencies.
Results involving derivatives and comparisons with `real.exp` and `complex.exp` can be found in
`analysis/special_functions/exponential`.

## Main results

We prove most result for an arbitrary field `ğ•‚`, and then specialize to `ğ•‚ = â„` or `ğ•‚ = â„‚`.

### General case

- `exp_add_of_commute_of_lt_radius` : if `ğ•‚` has characteristic zero, then given two commuting
  elements `x` and `y` in the disk of convergence, we have
  `exp ğ•‚ ğ”¸ (x+y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)`
- `exp_add_of_lt_radius` : if `ğ•‚` has characteristic zero and `ğ”¸` is commutative, then given two
  elements `x` and `y` in the disk of convergence, we have
  `exp ğ•‚ ğ”¸ (x+y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)`

### `ğ•‚ = â„` or `ğ•‚ = â„‚`

- `exp_series_radius_eq_top` : the `formal_multilinear_series` defining `exp ğ•‚ ğ”¸` has infinite
  radius of convergence
- `exp_add_of_commute` : given two commuting elements `x` and `y`, we have
  `exp ğ•‚ ğ”¸ (x+y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)`
- `exp_add` : if `ğ”¸` is commutative, then we have `exp ğ•‚ ğ”¸ (x+y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)`
  for any `x` and `y`

### Other useful compatibility results

- `exp_eq_exp` : if `ğ”¸` is a normed algebra over two fields `ğ•‚` and `ğ•‚'`, then `exp ğ•‚ ğ”¸ = exp ğ•‚' ğ”¸`

-/

open filter is_R_or_C continuous_multilinear_map normed_field asymptotics
open_locale nat topological_space big_operators ennreal

section any_field_any_algebra

variables (ğ•‚ ğ”¸ : Type*) [nondiscrete_normed_field ğ•‚] [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]

/-- In a Banach algebra `ğ”¸` over a normed field `ğ•‚`, `exp_series ğ•‚ ğ”¸` is the
`formal_multilinear_series` whose `n`-th term is the map `(xáµ¢) : ğ”¸â¿ â†¦ (1/n! : ğ•‚) â€¢ âˆ xáµ¢`.
Its sum is the exponential map `exp ğ•‚ ğ”¸ : ğ”¸ â†’ ğ”¸`. -/
def exp_series : formal_multilinear_series ğ•‚ ğ”¸ ğ”¸ :=
  Î» n, (1/n! : ğ•‚) â€¢ continuous_multilinear_map.mk_pi_algebra_fin ğ•‚ n ğ”¸

/-- In a Banach algebra `ğ”¸` over a normed field `ğ•‚`, `exp ğ•‚ ğ”¸ : ğ”¸ â†’ ğ”¸` is the exponential map
determined by the action of `ğ•‚` on `ğ”¸`.
It is defined as the sum of the `formal_multilinear_series` `exp_series ğ•‚ ğ”¸`. -/
noncomputable def exp (x : ğ”¸) : ğ”¸ := (exp_series ğ•‚ ğ”¸).sum x

variables {ğ•‚ ğ”¸}

lemma exp_series_apply_eq (x : ğ”¸) (n : â„•) : exp_series ğ•‚ ğ”¸ n (Î» _, x) = (1 / n! : ğ•‚) â€¢ x^n :=
by simp [exp_series]

lemma exp_series_apply_eq' (x : ğ”¸) :
  (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) = (Î» n, (1 / n! : ğ•‚) â€¢ x^n) :=
funext (exp_series_apply_eq x)

lemma exp_series_apply_eq_field (x : ğ•‚) (n : â„•) : exp_series ğ•‚ ğ•‚ n (Î» _, x) = x^n / n! :=
begin
  rw [div_eq_inv_mul, â†smul_eq_mul, inv_eq_one_div],
  exact exp_series_apply_eq x n,
end

lemma exp_series_apply_eq_field' (x : ğ•‚) : (Î» n, exp_series ğ•‚ ğ•‚ n (Î» _, x)) = (Î» n, x^n / n!) :=
funext (exp_series_apply_eq_field x)

lemma exp_series_sum_eq (x : ğ”¸) : (exp_series ğ•‚ ğ”¸).sum x = âˆ‘' (n : â„•), (1 / n! : ğ•‚) â€¢ x^n :=
tsum_congr (Î» n, exp_series_apply_eq x n)

lemma exp_series_sum_eq_field (x : ğ•‚) : (exp_series ğ•‚ ğ•‚).sum x = âˆ‘' (n : â„•), x^n / n! :=
tsum_congr (Î» n, exp_series_apply_eq_field x n)

lemma exp_eq_tsum : exp ğ•‚ ğ”¸ = (Î» x : ğ”¸, âˆ‘' (n : â„•), (1 / n! : ğ•‚) â€¢ x^n) :=
funext exp_series_sum_eq

lemma exp_eq_tsum_field : exp ğ•‚ ğ•‚ = (Î» x : ğ•‚, âˆ‘' (n : â„•), x^n / n!) :=
funext exp_series_sum_eq_field

lemma exp_zero : exp ğ•‚ ğ”¸ 0 = 1 :=
begin
  suffices : (Î» x : ğ”¸, âˆ‘' (n : â„•), (1 / n! : ğ•‚) â€¢ x^n) 0 = âˆ‘' (n : â„•), if n = 0 then 1 else 0,
  { have key : âˆ€ n âˆ‰ ({0} : finset â„•), (if n = 0 then (1 : ğ”¸) else 0) = 0,
      from Î» n hn, if_neg (finset.not_mem_singleton.mp hn),
    rw [exp_eq_tsum, this, tsum_eq_sum key, finset.sum_singleton],
    simp },
  refine tsum_congr (Î» n, _),
  split_ifs with h h;
  simp [h]
end

lemma norm_exp_series_summable_of_mem_ball (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, âˆ¥exp_series ğ•‚ ğ”¸ n (Î» _, x)âˆ¥) :=
(exp_series ğ•‚ ğ”¸).summable_norm_apply hx

lemma norm_exp_series_summable_of_mem_ball' (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, âˆ¥(1 / n! : ğ•‚) â€¢ x^nâˆ¥) :=
begin
  change summable (norm âˆ˜ _),
  rw â† exp_series_apply_eq',
  exact norm_exp_series_summable_of_mem_ball x hx
end

lemma norm_exp_series_field_summable_of_mem_ball (x : ğ•‚)
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) :
  summable (Î» n, âˆ¥x^n / n!âˆ¥) :=
begin
  change summable (norm âˆ˜ _),
  rw â† exp_series_apply_eq_field',
  exact norm_exp_series_summable_of_mem_ball x hx
end

section complete_algebra

variables [complete_space ğ”¸]

lemma exp_series_summable_of_mem_ball (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) :=
summable_of_summable_norm (norm_exp_series_summable_of_mem_ball x hx)

lemma exp_series_summable_of_mem_ball' (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  summable (Î» n, (1 / n! : ğ•‚) â€¢ x^n) :=
summable_of_summable_norm (norm_exp_series_summable_of_mem_ball' x hx)

lemma exp_series_field_summable_of_mem_ball [complete_space ğ•‚] (x : ğ•‚)
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) : summable (Î» n, x^n / n!) :=
summable_of_summable_norm (norm_exp_series_field_summable_of_mem_ball x hx)

lemma exp_series_has_sum_exp_of_mem_ball (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_sum (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) (exp ğ•‚ ğ”¸ x) :=
formal_multilinear_series.has_sum (exp_series ğ•‚ ğ”¸) hx

lemma exp_series_has_sum_exp_of_mem_ball' (x : ğ”¸)
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_sum (Î» n, (1 / n! : ğ•‚) â€¢ x^n) (exp ğ•‚ ğ”¸ x):=
begin
  rw â† exp_series_apply_eq',
  exact exp_series_has_sum_exp_of_mem_ball x hx
end

lemma exp_series_field_has_sum_exp_of_mem_ball [complete_space ğ•‚] (x : ğ•‚)
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) : has_sum (Î» n, x^n / n!) (exp ğ•‚ ğ•‚ x) :=
begin
  rw â† exp_series_apply_eq_field',
  exact exp_series_has_sum_exp_of_mem_ball x hx
end

lemma has_fpower_series_on_ball_exp_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_fpower_series_on_ball (exp ğ•‚ ğ”¸) (exp_series ğ•‚ ğ”¸) 0 (exp_series ğ•‚ ğ”¸).radius :=
(exp_series ğ•‚ ğ”¸).has_fpower_series_on_ball h

lemma has_fpower_series_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_fpower_series_at (exp ğ•‚ ğ”¸) (exp_series ğ•‚ ğ”¸) 0 :=
(has_fpower_series_on_ball_exp_of_radius_pos h).has_fpower_series_at

lemma continuous_on_exp :
  continuous_on (exp ğ•‚ ğ”¸) (emetric.ball 0 (exp_series ğ•‚ ğ”¸).radius) :=
formal_multilinear_series.continuous_on

lemma analytic_at_exp_of_mem_ball (x : ğ”¸) (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  analytic_at ğ•‚ (exp ğ•‚ ğ”¸) x:=
begin
  by_cases h : (exp_series ğ•‚ ğ”¸).radius = 0,
  { rw h at hx, exact (ennreal.not_lt_zero hx).elim },
  { have h := pos_iff_ne_zero.mpr h,
    exact (has_fpower_series_on_ball_exp_of_radius_pos h).analytic_at_of_mem hx }
end

/-- In a Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero, if `x` and `y` are
in the disk of convergence and commute, then `exp ğ•‚ ğ”¸ (x + y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)`. -/
lemma exp_add_of_commute_of_mem_ball [char_zero ğ•‚]
  {x y : ğ”¸} (hxy : commute x y) (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius)
  (hy : y âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  exp ğ•‚ ğ”¸ (x + y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y) :=
begin
  rw [exp_eq_tsum, tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm
        (norm_exp_series_summable_of_mem_ball' x hx) (norm_exp_series_summable_of_mem_ball' y hy)],
  dsimp only,
  conv_lhs {congr, funext, rw [hxy.add_pow' _, finset.smul_sum]},
  refine tsum_congr (Î» n, finset.sum_congr rfl $ Î» kl hkl, _),
  rw [nsmul_eq_smul_cast ğ•‚, smul_smul, smul_mul_smul, â† (finset.nat.mem_antidiagonal.mp hkl),
      nat.cast_add_choose, (finset.nat.mem_antidiagonal.mp hkl)],
  congr' 1,
  have : (n! : ğ•‚) â‰  0 := nat.cast_ne_zero.mpr n.factorial_ne_zero,
  field_simp [this]
end

end complete_algebra

end any_field_any_algebra

section any_field_comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [nondiscrete_normed_field ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- In a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero,
`exp ğ•‚ ğ”¸ (x+y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)` for all `x`, `y` in the disk of convergence. -/
lemma exp_add_of_mem_ball [char_zero ğ•‚] {x y : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius)
  (hy : y âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  exp ğ•‚ ğ”¸ (x + y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y) :=
exp_add_of_commute_of_mem_ball (commute.all x y) hx hy

end any_field_comm_algebra

section is_R_or_C

section any_algebra

variables (ğ•‚ ğ”¸ : Type*) [is_R_or_C ğ•‚] [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]

/-- In a normed algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, the series defining the exponential map
has an infinite radius of convergence. -/
lemma exp_series_radius_eq_top : (exp_series ğ•‚ ğ”¸).radius = âˆ :=
begin
  refine (exp_series ğ•‚ ğ”¸).radius_eq_top_of_summable_norm (Î» r, _),
  refine summable_of_norm_bounded_eventually _ (real.summable_pow_div_factorial r) _,
  filter_upwards [eventually_cofinite_ne 0],
  intros n hn,
  rw [norm_mul, norm_norm (exp_series ğ•‚ ğ”¸ n), exp_series, norm_smul, norm_div, norm_one, norm_pow,
      nnreal.norm_eq, norm_eq_abs, abs_cast_nat, mul_comm, â†mul_assoc, â†mul_div_assoc, mul_one],
  have : âˆ¥continuous_multilinear_map.mk_pi_algebra_fin ğ•‚ n ğ”¸âˆ¥ â‰¤ 1 :=
    norm_mk_pi_algebra_fin_le_of_pos (nat.pos_of_ne_zero hn),
  exact mul_le_of_le_one_right (div_nonneg (pow_nonneg r.coe_nonneg n) n!.cast_nonneg) this
end

lemma exp_series_radius_pos : 0 < (exp_series ğ•‚ ğ”¸).radius :=
begin
  rw exp_series_radius_eq_top,
  exact with_top.zero_lt_top
end

variables {ğ•‚ ğ”¸}

section complete_algebra

lemma norm_exp_series_summable (x : ğ”¸) : summable (Î» n, âˆ¥exp_series ğ•‚ ğ”¸ n (Î» _, x)âˆ¥) :=
norm_exp_series_summable_of_mem_ball x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

lemma norm_exp_series_summable' (x : ğ”¸) : summable (Î» n, âˆ¥(1 / n! : ğ•‚) â€¢ x^nâˆ¥) :=
norm_exp_series_summable_of_mem_ball' x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

lemma norm_exp_series_field_summable (x : ğ•‚) : summable (Î» n, âˆ¥x^n / n!âˆ¥) :=
norm_exp_series_field_summable_of_mem_ball x
  ((exp_series_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _)

variables [complete_space ğ”¸]

lemma exp_series_summable (x : ğ”¸) : summable (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) :=
summable_of_summable_norm (norm_exp_series_summable x)

lemma exp_series_summable' (x : ğ”¸) : summable (Î» n, (1 / n! : ğ•‚) â€¢ x^n) :=
summable_of_summable_norm (norm_exp_series_summable' x)

lemma exp_series_field_summable (x : ğ•‚) : summable (Î» n, x^n / n!) :=
summable_of_summable_norm (norm_exp_series_field_summable x)

lemma exp_series_has_sum_exp (x : ğ”¸) : has_sum (Î» n, exp_series ğ•‚ ğ”¸ n (Î» _, x)) (exp ğ•‚ ğ”¸ x) :=
exp_series_has_sum_exp_of_mem_ball x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

lemma exp_series_has_sum_exp' (x : ğ”¸) : has_sum (Î» n, (1 / n! : ğ•‚) â€¢ x^n) (exp ğ•‚ ğ”¸ x):=
exp_series_has_sum_exp_of_mem_ball' x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

lemma exp_series_field_has_sum_exp (x : ğ•‚) : has_sum (Î» n, x^n / n!) (exp ğ•‚ ğ•‚ x):=
exp_series_field_has_sum_exp_of_mem_ball x ((exp_series_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _)

lemma exp_has_fpower_series_on_ball :
  has_fpower_series_on_ball (exp ğ•‚ ğ”¸) (exp_series ğ•‚ ğ”¸) 0 âˆ :=
exp_series_radius_eq_top ğ•‚ ğ”¸ â–¸
  has_fpower_series_on_ball_exp_of_radius_pos (exp_series_radius_pos _ _)

lemma exp_has_fpower_series_at_zero :
  has_fpower_series_at (exp ğ•‚ ğ”¸) (exp_series ğ•‚ ğ”¸) 0 :=
exp_has_fpower_series_on_ball.has_fpower_series_at

lemma exp_continuous :
  continuous (exp ğ•‚ ğ”¸) :=
begin
  rw [continuous_iff_continuous_on_univ, â† metric.eball_top_eq_univ (0 : ğ”¸),
      â† exp_series_radius_eq_top ğ•‚ ğ”¸],
  exact continuous_on_exp
end

lemma exp_analytic (x : ğ”¸) :
  analytic_at ğ•‚ (exp ğ•‚ ğ”¸) x :=
analytic_at_exp_of_mem_ball x ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

end complete_algebra

local attribute [instance] char_zero_R_or_C

/-- In a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, if `x` and `y` commute, then
`exp ğ•‚ ğ”¸ (x+y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)`. -/
lemma exp_add_of_commute [complete_space ğ”¸]
  {x y : ğ”¸} (hxy : commute x y) :
  exp ğ•‚ ğ”¸ (x + y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y) :=
exp_add_of_commute_of_mem_ball hxy ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
  ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

end any_algebra

section comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [is_R_or_C ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸] [complete_space ğ”¸]

local attribute [instance] char_zero_R_or_C

/-- In a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`,
`exp ğ•‚ ğ”¸ (x+y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y)`. -/
lemma exp_add {x y : ğ”¸} : exp ğ•‚ ğ”¸ (x + y) = (exp ğ•‚ ğ”¸ x) * (exp ğ•‚ ğ”¸ y) :=
exp_add_of_mem_ball ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
  ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

end comm_algebra

end is_R_or_C

section scalar_tower

variables (ğ•‚ ğ•‚' ğ”¸ : Type*) [nondiscrete_normed_field ğ•‚] [nondiscrete_normed_field ğ•‚']
  [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸] [normed_algebra ğ•‚' ğ”¸]

/-- If a normed ring `ğ”¸` is a normed algebra over two fields, then they define the same
`exp_series` on `ğ”¸`. -/
lemma exp_series_eq_exp_series (n : â„•) (x : ğ”¸) :
  (exp_series ğ•‚ ğ”¸ n (Î» _, x)) = (exp_series ğ•‚' ğ”¸ n (Î» _, x)) :=
by rw [exp_series, exp_series,
       smul_apply, mk_pi_algebra_fin_apply, list.of_fn_const, list.prod_repeat,
       smul_apply, mk_pi_algebra_fin_apply, list.of_fn_const, list.prod_repeat,
       one_div, one_div, inv_nat_cast_smul_eq ğ•‚ ğ•‚']

/-- If a normed ring `ğ”¸` is a normed algebra over two fields, then they define the same
exponential function on `ğ”¸`. -/
lemma exp_eq_exp : exp ğ•‚ ğ”¸ = exp ğ•‚' ğ”¸ :=
begin
  ext,
  rw [exp, exp],
  refine tsum_congr (Î» n, _),
  rw exp_series_eq_exp_series ğ•‚ ğ•‚' ğ”¸ n x
end

lemma exp_â„_â„‚_eq_exp_â„‚_â„‚ : exp â„ â„‚ = exp â„‚ â„‚ :=
exp_eq_exp â„ â„‚ â„‚

end scalar_tower
