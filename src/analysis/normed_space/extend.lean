/-
Copyright (c) 2020 Ruben Van de Velde. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ruben Van de Velde
-/

import algebra.algebra.restrict_scalars
import data.complex.is_R_or_C

/-!
# Extending a continuous `â„`-linear map to a continuous `ğ•œ`-linear map

In this file we provide a way to extend a continuous `â„`-linear map to a continuous `ğ•œ`-linear map
in a way that bounds the norm by the norm of the original map, when `ğ•œ` is either `â„` (the
extension is trivial) or `â„‚`. We formulate the extension uniformly, by assuming `is_R_or_C ğ•œ`.

We motivate the form of the extension as follows. Note that `fc : F â†’â‚—[ğ•œ] ğ•œ` is determined fully by
`Re fc`: for all `x : F`, `fc (I â€¢ x) = I * fc x`, so `Im (fc x) = -Re (fc (I â€¢ x))`. Therefore,
given an `fr : F â†’â‚—[â„] â„`, we define `fc x = fr x - fr (I â€¢ x) * I`.

## Main definitions

* `linear_map.extend_to_ğ•œ`
* `continuous_linear_map.extend_to_ğ•œ`

## Implementation details

For convenience, the main definitions above operate in terms of `restrict_scalars â„ ğ•œ F`.
Alternate forms which operate on `[is_scalar_tower â„ ğ•œ F]` instead are provided with a primed name.

-/

open is_R_or_C

variables {ğ•œ : Type*} [is_R_or_C ğ•œ] {F : Type*} [semi_normed_group F] [semi_normed_space ğ•œ F]
local notation `absğ•œ` := @is_R_or_C.abs ğ•œ _

/-- Extend `fr : F â†’â‚—[â„] â„` to `F â†’â‚—[ğ•œ] ğ•œ` in a way that will also be continuous and have its norm
bounded by `âˆ¥frâˆ¥` if `fr` is continuous. -/
noncomputable def linear_map.extend_to_ğ•œ'
  [module â„ F] [is_scalar_tower â„ ğ•œ F] (fr : F â†’â‚—[â„] â„) : F â†’â‚—[ğ•œ] ğ•œ :=
begin
  let fc : F â†’ ğ•œ := Î» x, (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x)),
  have add : âˆ€ x y : F, fc (x + y) = fc x + fc y,
  { assume x y,
    simp only [fc],
    unfold_coes,
    simp only [smul_add, ring_hom.map_add, ring_hom.to_fun_eq_coe, linear_map.to_fun_eq_coe,
               linear_map.map_add],
    rw mul_add,
    abel, },
  have A : âˆ€ (c : â„) (x : F), (fr ((c : ğ•œ) â€¢ x) : ğ•œ) = (c : ğ•œ) * (fr x : ğ•œ),
  { assume c x,
    rw [â† of_real_mul],
    congr' 1,
    rw [is_R_or_C.of_real_alg, smul_assoc, fr.map_smul, algebra.id.smul_eq_mul, one_smul] },
  have smul_â„ : âˆ€ (c : â„) (x : F), fc ((c : ğ•œ) â€¢ x) = (c : ğ•œ) * fc x,
  { assume c x,
    simp only [fc, A],
    rw A c x,
    rw [smul_smul, mul_comm I (c : ğ•œ), â† smul_smul, A, mul_sub],
    ring },
  have smul_I : âˆ€ x : F, fc ((I : ğ•œ) â€¢ x) = (I : ğ•œ) * fc x,
  { assume x,
    simp only [fc],
    cases @I_mul_I_ax ğ•œ _ with h h, { simp [h] },
    rw [mul_sub, â† mul_assoc, smul_smul, h],
    simp only [neg_mul_eq_neg_mul_symm, linear_map.map_neg, one_mul, one_smul,
      mul_neg_eq_neg_mul_symm, of_real_neg, neg_smul, sub_neg_eq_add, add_comm] },
  have smul_ğ•œ : âˆ€ (c : ğ•œ) (x : F), fc (c â€¢ x) = c â€¢ fc x,
  { assume c x,
    rw [â† re_add_im c, add_smul, add_smul, add, smul_â„, â† smul_smul, smul_â„, smul_I, â† mul_assoc],
    refl },
  exact { to_fun := fc, map_add' := add, map_smul' := smul_ğ•œ }
end

lemma linear_map.extend_to_ğ•œ'_apply [module â„ F] [is_scalar_tower â„ ğ•œ F]
  (fr : F â†’â‚—[â„] â„) (x : F) :
  fr.extend_to_ğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x) := rfl

/-- The norm of the extension is bounded by `âˆ¥frâˆ¥`. -/
lemma norm_bound [semi_normed_space â„ F] [is_scalar_tower â„ ğ•œ F] (fr : F â†’L[â„] â„) (x : F) :
  âˆ¥(fr.to_linear_map.extend_to_ğ•œ' x : ğ•œ)âˆ¥ â‰¤ âˆ¥frâˆ¥ * âˆ¥xâˆ¥ :=
begin
  let lm : F â†’â‚—[ğ•œ] ğ•œ := fr.to_linear_map.extend_to_ğ•œ',
  -- We aim to find a `t : ğ•œ` such that
  -- * `lm (t â€¢ x) = fr (t â€¢ x)` (so `lm (t â€¢ x) = t * lm x âˆˆ â„`)
  -- * `âˆ¥lm xâˆ¥ = âˆ¥lm (t â€¢ x)âˆ¥` (so `t.abs` must be 1)
  -- If `lm x â‰  0`, `(lm x)â»Â¹` satisfies the first requirement, and after normalizing, it
  -- satisfies the second.
  -- (If `lm x = 0`, the goal is trivial.)
  classical,
  by_cases h : lm x = 0,
  { rw [h, norm_zero],
    apply mul_nonneg; exact norm_nonneg _ },
  let fx := (lm x)â»Â¹,
  let t := fx / (absğ•œ fx : ğ•œ),
  have ht : absğ•œ t = 1, by field_simp [abs_of_real, of_real_inv, is_R_or_C.abs_inv,
    is_R_or_C.abs_div, is_R_or_C.abs_abs, h],
  have h1 : (fr (t â€¢ x) : ğ•œ) = lm (t â€¢ x),
  { apply ext,
    { simp only [lm, of_real_re, linear_map.extend_to_ğ•œ'_apply, mul_re, I_re, of_real_im, zero_mul,
        add_monoid_hom.map_sub, sub_zero, mul_zero],
      refl },
    { symmetry,
      calc im (lm (t â€¢ x))
          = im (t * lm x) : by rw [lm.map_smul, smul_eq_mul]
      ... = im ((lm x)â»Â¹ / (absğ•œ (lm x)â»Â¹) * lm x) : rfl
      ... = im (1 / (absğ•œ (lm x)â»Â¹ : ğ•œ)) : by rw [div_mul_eq_mul_div, inv_mul_cancel h]
      ... = 0 : by rw [â† of_real_one, â† of_real_div, of_real_im]
      ... = im (fr (t â€¢ x) : ğ•œ) : by rw [of_real_im] } },
  calc âˆ¥lm xâˆ¥ = absğ•œ t * âˆ¥lm xâˆ¥ : by rw [ht, one_mul]
  ... = âˆ¥t * lm xâˆ¥ : by rw [â† norm_eq_abs, normed_field.norm_mul]
  ... = âˆ¥lm (t â€¢ x)âˆ¥ : by rw [â†smul_eq_mul, lm.map_smul]
  ... = âˆ¥(fr (t â€¢ x) : ğ•œ)âˆ¥ : by rw h1
  ... = âˆ¥fr (t â€¢ x)âˆ¥ : by rw [norm_eq_abs, abs_of_real, norm_eq_abs, abs_to_real]
  ... â‰¤ âˆ¥frâˆ¥ * âˆ¥t â€¢ xâˆ¥ : continuous_linear_map.le_op_norm _ _
  ... = âˆ¥frâˆ¥ * (âˆ¥tâˆ¥ * âˆ¥xâˆ¥) : by rw norm_smul
  ... â‰¤ âˆ¥frâˆ¥ * âˆ¥xâˆ¥ : by rw [norm_eq_abs, ht, one_mul]
end

/-- Extend `fr : F â†’L[â„] â„` to `F â†’L[ğ•œ] ğ•œ`. -/
noncomputable def continuous_linear_map.extend_to_ğ•œ' [semi_normed_space â„ F] [is_scalar_tower â„ ğ•œ F]
  (fr : F â†’L[â„] â„) :
  F â†’L[ğ•œ] ğ•œ :=
linear_map.mk_continuous _ (âˆ¥frâˆ¥) (norm_bound _)

lemma continuous_linear_map.extend_to_ğ•œ'_apply [semi_normed_space â„ F] [is_scalar_tower â„ ğ•œ F]
  (fr : F â†’L[â„] â„) (x : F) :
  fr.extend_to_ğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x) := rfl

/-- Extend `fr : restrict_scalars â„ ğ•œ F â†’â‚—[â„] â„` to `F â†’â‚—[ğ•œ] ğ•œ`. -/
noncomputable def linear_map.extend_to_ğ•œ (fr : (restrict_scalars â„ ğ•œ F) â†’â‚—[â„] â„) : F â†’â‚—[ğ•œ] ğ•œ :=
fr.extend_to_ğ•œ'

lemma linear_map.extend_to_ğ•œ_apply (fr : (restrict_scalars â„ ğ•œ F) â†’â‚—[â„] â„) (x : F) :
  fr.extend_to_ğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x) := rfl

/-- Extend `fr : restrict_scalars â„ ğ•œ F â†’L[â„] â„` to `F â†’L[ğ•œ] ğ•œ`. -/
noncomputable def continuous_linear_map.extend_to_ğ•œ (fr : (restrict_scalars â„ ğ•œ F) â†’L[â„] â„) :
  F â†’L[ğ•œ] ğ•œ :=
fr.extend_to_ğ•œ'

lemma continuous_linear_map.extend_to_ğ•œ_apply (fr : (restrict_scalars â„ ğ•œ F) â†’L[â„] â„) (x : F) :
  fr.extend_to_ğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x) := rfl
