/-
Copyright (c) 2020 Ruben Van de Velde. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ruben Van de Velde
-/

import analysis.normed_space.operator_norm
import algebra.algebra.restrict_scalars
import data.is_R_or_C.basic

/-!
# Extending a continuous `â„`-linear map to a continuous `ğ•œ`-linear map

In this file we provide a way to extend a continuous `â„`-linear map to a continuous `ğ•œ`-linear map
in a way that bounds the norm by the norm of the original map, when `ğ•œ` is either `â„` (the
extension is trivial) or `â„‚`. We formulate the extension uniformly, by assuming `is_R_or_C ğ•œ`.

We motivate the form of the extension as follows. Note that `fc : F â†’â‚—[ğ•œ] ğ•œ` is determined fully by
`Re fc`: for all `x : F`, `fc (I â€¢ x) = I * fc x`, so `Im (fc x) = -Re (fc (I â€¢ x))`. Therefore,
given an `fr : F â†’â‚—[â„] â„`, we define `fc x = fr x - fr (I â€¢ x) * I`.

## Main definitions

* `linear_map.extend_to_ğ•œ`
* `continuous_linear_map.extend_to_ğ•œ`

## Implementation details

For convenience, the main definitions above operate in terms of `restrict_scalars â„ ğ•œ F`.
Alternate forms which operate on `[is_scalar_tower â„ ğ•œ F]` instead are provided with a primed name.

-/

open is_R_or_C
open_locale complex_conjugate

variables {ğ•œ : Type*} [is_R_or_C ğ•œ] {F : Type*} [seminormed_add_comm_group F] [normed_space ğ•œ F]

namespace linear_map

variables [module â„ F] [is_scalar_tower â„ ğ•œ F]

/-- Extend `fr : F â†’â‚—[â„] â„` to `F â†’â‚—[ğ•œ] ğ•œ` in a way that will also be continuous and have its norm
bounded by `â€–frâ€–` if `fr` is continuous. -/
noncomputable def extend_to_ğ•œ' (fr : F â†’â‚—[â„] â„) : F â†’â‚—[ğ•œ] ğ•œ :=
begin
  let fc : F â†’ ğ•œ := Î» x, (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x)),
  have add : âˆ€ x y : F, fc (x + y) = fc x + fc y,
  { assume x y,
    simp only [fc],
    simp only [smul_add, linear_map.map_add, of_real_add],
    rw mul_add,
    abel, },
  have A : âˆ€ (c : â„) (x : F), (fr ((c : ğ•œ) â€¢ x) : ğ•œ) = (c : ğ•œ) * (fr x : ğ•œ),
  { assume c x,
    rw [â† of_real_mul],
    congr' 1,
    rw [is_R_or_C.of_real_alg, smul_assoc, fr.map_smul, algebra.id.smul_eq_mul, one_smul] },
  have smul_â„ : âˆ€ (c : â„) (x : F), fc ((c : ğ•œ) â€¢ x) = (c : ğ•œ) * fc x,
  { assume c x,
    simp only [fc, A],
    rw A c x,
    rw [smul_smul, mul_comm I (c : ğ•œ), â† smul_smul, A, mul_sub],
    ring },
  have smul_I : âˆ€ x : F, fc ((I : ğ•œ) â€¢ x) = (I : ğ•œ) * fc x,
  { assume x,
    simp only [fc],
    cases @I_mul_I_ax ğ•œ _ with h h, { simp [h] },
    rw [mul_sub, â† mul_assoc, smul_smul, h],
    simp only [neg_mul, linear_map.map_neg, one_mul, one_smul,
      mul_neg, of_real_neg, neg_smul, sub_neg_eq_add, add_comm] },
  have smul_ğ•œ : âˆ€ (c : ğ•œ) (x : F), fc (c â€¢ x) = c â€¢ fc x,
  { assume c x,
    rw [â† re_add_im c, add_smul, add_smul, add, smul_â„, â† smul_smul, smul_â„, smul_I, â† mul_assoc],
    refl },
  exact { to_fun := fc, map_add' := add, map_smul' := smul_ğ•œ }
end

lemma extend_to_ğ•œ'_apply (fr : F â†’â‚—[â„] â„) (x : F) :
  fr.extend_to_ğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x) := rfl

@[simp] lemma extend_to_ğ•œ'_apply_re (fr : F â†’â‚—[â„] â„) (x : F) : re (fr.extend_to_ğ•œ' x : ğ•œ) = fr x :=
by simp only [extend_to_ğ•œ'_apply, map_sub, zero_mul, mul_zero, sub_zero] with is_R_or_C_simps

lemma norm_extend_to_ğ•œ'_apply_sq (f : F â†’â‚—[â„] â„) (x : F) :
  â€–(f.extend_to_ğ•œ' x : ğ•œ)â€– ^ 2 = f (conj (f.extend_to_ğ•œ' x : ğ•œ) â€¢ x) :=
calc â€–(f.extend_to_ğ•œ' x : ğ•œ)â€– ^ 2 = re (conj (f.extend_to_ğ•œ' x) * f.extend_to_ğ•œ' x : ğ•œ) :
  by rw [is_R_or_C.conj_mul, norm_sq_eq_def', of_real_re]
... = f (conj (f.extend_to_ğ•œ' x : ğ•œ) â€¢ x) :
  by rw [â† smul_eq_mul, â† map_smul, extend_to_ğ•œ'_apply_re]

end linear_map

namespace continuous_linear_map

variables [normed_space â„ F] [is_scalar_tower â„ ğ•œ F]

/-- The norm of the extension is bounded by `â€–frâ€–`. -/
lemma norm_extend_to_ğ•œ'_bound (fr : F â†’L[â„] â„) (x : F) :
  â€–(fr.to_linear_map.extend_to_ğ•œ' x : ğ•œ)â€– â‰¤ â€–frâ€– * â€–xâ€– :=
begin
  set lm : F â†’â‚—[ğ•œ] ğ•œ := fr.to_linear_map.extend_to_ğ•œ',
  classical,
  by_cases h : lm x = 0,
  { rw [h, norm_zero],
    apply mul_nonneg; exact norm_nonneg _ },
  rw [â† mul_le_mul_left (norm_pos_iff.2 h), â† sq],
  calc â€–lm xâ€– ^ 2 = fr (conj (lm x : ğ•œ) â€¢ x) : fr.to_linear_map.norm_extend_to_ğ•œ'_apply_sq x
  ... â‰¤ â€–fr (conj (lm x : ğ•œ) â€¢ x)â€– : le_abs_self _
  ... â‰¤ â€–frâ€– * â€–conj (lm x : ğ•œ) â€¢ xâ€– : le_op_norm _ _
  ... = â€–(lm x : ğ•œ)â€– * (â€–frâ€– * â€–xâ€–) : by rw [norm_smul, norm_conj, mul_left_comm]
end

/-- Extend `fr : F â†’L[â„] â„` to `F â†’L[ğ•œ] ğ•œ`. -/
noncomputable def extend_to_ğ•œ' (fr : F â†’L[â„] â„) : F â†’L[ğ•œ] ğ•œ :=
linear_map.mk_continuous _ (â€–frâ€–) fr.norm_extend_to_ğ•œ'_bound

lemma extend_to_ğ•œ'_apply (fr : F â†’L[â„] â„) (x : F) :
  fr.extend_to_ğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x) := rfl

@[simp] lemma norm_extend_to_ğ•œ' (fr : F â†’L[â„] â„) : â€–(fr.extend_to_ğ•œ' : F â†’L[ğ•œ] ğ•œ)â€– = â€–frâ€– :=
le_antisymm (linear_map.mk_continuous_norm_le _ (norm_nonneg _) _) $
  op_norm_le_bound _ (norm_nonneg _) $ Î» x,
    calc â€–fr xâ€– = â€–re (fr.extend_to_ğ•œ' x : ğ•œ)â€– : congr_arg norm (fr.extend_to_ğ•œ'_apply_re x).symm
    ... â‰¤ â€–(fr.extend_to_ğ•œ' x : ğ•œ)â€– : (abs_re_le_abs _).trans_eq (norm_eq_abs _).symm
    ... â‰¤ â€–(fr.extend_to_ğ•œ' : F â†’L[ğ•œ] ğ•œ)â€– * â€–xâ€– : le_op_norm _ _

end continuous_linear_map

/-- Extend `fr : restrict_scalars â„ ğ•œ F â†’â‚—[â„] â„` to `F â†’â‚—[ğ•œ] ğ•œ`. -/
noncomputable def linear_map.extend_to_ğ•œ (fr : (restrict_scalars â„ ğ•œ F) â†’â‚—[â„] â„) : F â†’â‚—[ğ•œ] ğ•œ :=
fr.extend_to_ğ•œ'

lemma linear_map.extend_to_ğ•œ_apply (fr : (restrict_scalars â„ ğ•œ F) â†’â‚—[â„] â„) (x : F) :
  fr.extend_to_ğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x : _) := rfl

/-- Extend `fr : restrict_scalars â„ ğ•œ F â†’L[â„] â„` to `F â†’L[ğ•œ] ğ•œ`. -/
noncomputable def continuous_linear_map.extend_to_ğ•œ (fr : (restrict_scalars â„ ğ•œ F) â†’L[â„] â„) :
  F â†’L[ğ•œ] ğ•œ :=
fr.extend_to_ğ•œ'

lemma continuous_linear_map.extend_to_ğ•œ_apply (fr : (restrict_scalars â„ ğ•œ F) â†’L[â„] â„) (x : F) :
  fr.extend_to_ğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x : _) := rfl

@[simp] lemma continuous_linear_map.norm_extend_to_ğ•œ (fr : (restrict_scalars â„ ğ•œ F) â†’L[â„] â„) :
  â€–fr.extend_to_ğ•œâ€– = â€–frâ€– :=
fr.norm_extend_to_ğ•œ'
