/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.normed_space.operator_norm
import topology.bases
import linear_algebra.finite_dimensional
import tactic.omega

/-!
# Finite dimensional normed spaces over complete fields

Over a complete nondiscrete field, in finite dimension, all norms are equivalent and all linear maps
are continuous. Moreover, a finite-dimensional subspace is always complete and closed.

## Main results:

* `linear_map.continuous_of_finite_dimensional` : a linear map on a finite-dimensional space over a
  complete field is continuous.
* `finite_dimensional.complete` : a finite-dimensional space over a complete field is complete. This
  is not registered as an instance, as the field would be an unknown metavariable in typeclass
  resolution.
* `submodule.closed_of_finite_dimensional` : a finite-dimensional subspace over a complete field is
  closed
* `finite_dimensional.proper` : a finite-dimensional space over a proper field is proper. This
  is not registered as an instance, as the field would be an unknown metavariable in typeclass
  resolution. It is however registered as an instance for `ğ•œ = â„` and `ğ•œ = â„‚`. As properness
  implies completeness, there is no need to also register `finite_dimensional.complete` on `â„` or
  `â„‚`.

## Implementation notes

The fact that all norms are equivalent is not written explicitly, as it would mean having two norms
on a single space, which is not the way type classes work. However, if one has a
finite-dimensional vector space `E` with a norm, and a copy `E'` of this type with another norm,
then the identities from `E` to `E'` and from `E'`to `E` are continuous thanks to
`linear_map.continuous_of_finite_dimensional`. This gives the desired norm equivalence.
-/

universes u v w x

open set finite_dimensional topological_space
open_locale classical big_operators

noncomputable theory

/-- A linear map on `Î¹ â†’ ğ•œ` (where `Î¹` is a fintype) is continuous -/
lemma linear_map.continuous_on_pi {Î¹ : Type w} [fintype Î¹] {ğ•œ : Type u} [normed_field ğ•œ]
  {E : Type v}  [add_comm_group E] [vector_space ğ•œ E] [topological_space E]
  [topological_add_group E] [topological_vector_space ğ•œ E] (f : (Î¹ â†’ ğ•œ) â†’â‚—[ğ•œ] E) : continuous f :=
begin
  -- for the proof, write `f` in the standard basis, and use that each coordinate is a continuous
  -- function.
  have : (f : (Î¹ â†’ ğ•œ) â†’ E) =
         (Î»x, âˆ‘ i : Î¹, x i â€¢ (f (Î»j, if i = j then 1 else 0))),
    by { ext x, exact f.pi_apply_eq_sum_univ x },
  rw this,
  refine continuous_finset_sum _ (Î»i hi, _),
  exact (continuous_apply i).smul continuous_const
end

section complete_field

variables {ğ•œ : Type u} [nondiscrete_normed_field ğ•œ]
{E : Type v} [normed_group E] [normed_space ğ•œ E]
{F : Type w} [normed_group F] [normed_space ğ•œ F]
{F' : Type x} [add_comm_group F'] [vector_space ğ•œ F'] [topological_space F']
[topological_add_group F'] [topological_vector_space ğ•œ F']
[complete_space ğ•œ]


/-- In finite dimension over a complete field, the canonical identification (in terms of a basis)
with `ğ•œ^n` together with its sup norm is continuous. This is the nontrivial part in the fact that
all norms are equivalent in finite dimension.

This statement is superceded by the fact that every linear map on a finite-dimensional space is
continuous, in `linear_map.continuous_of_finite_dimensional`. -/
lemma continuous_equiv_fun_basis {Î¹ : Type v} [fintype Î¹] (Î¾ : Î¹ â†’ E) (hÎ¾ : is_basis ğ•œ Î¾) :
  continuous hÎ¾.equiv_fun :=
begin
  unfreezingI { induction hn : fintype.card Î¹ with n IH generalizing Î¹ E },
  { apply linear_map.continuous_of_bound _ 0 (Î»x, _),
    have : hÎ¾.equiv_fun x = 0,
      by { ext i, exact (fintype.card_eq_zero_iff.1 hn i).elim },
    change âˆ¥hÎ¾.equiv_fun xâˆ¥ â‰¤ 0 * âˆ¥xâˆ¥,
    rw this,
    simp [norm_nonneg] },
  { haveI : finite_dimensional ğ•œ E := of_finite_basis hÎ¾,
    -- first step: thanks to the inductive assumption, any n-dimensional subspace is equivalent
    -- to a standard space of dimension n, hence it is complete and therefore closed.
    have Hâ‚ : âˆ€s : submodule ğ•œ E, findim ğ•œ s = n â†’ is_closed (s : set E),
    { assume s s_dim,
      rcases exists_is_basis_finite ğ•œ s with âŸ¨b, b_basis, b_finiteâŸ©,
      letI : fintype b := finite.fintype b_finite,
      have U : uniform_embedding b_basis.equiv_fun.symm.to_equiv,
      { have : fintype.card b = n,
          by { rw â† s_dim, exact (findim_eq_card_basis b_basis).symm },
        have : continuous b_basis.equiv_fun := IH (subtype.val : b â†’ s) b_basis this,
        exact b_basis.equiv_fun.symm.uniform_embedding (linear_map.continuous_on_pi _) this },
      have : is_complete (s : set E),
        from complete_space_coe_iff_is_complete.1 ((complete_space_congr U).1 (by apply_instance)),
      exact this.is_closed },
    -- second step: any linear form is continuous, as its kernel is closed by the first step
    have Hâ‚‚ : âˆ€f : E â†’â‚—[ğ•œ] ğ•œ, continuous f,
    { assume f,
      have : findim ğ•œ f.ker = n âˆ¨ findim ğ•œ f.ker = n.succ,
      { have Z := f.findim_range_add_findim_ker,
        rw [findim_eq_card_basis hÎ¾, hn] at Z,
        have : findim ğ•œ f.range = 0 âˆ¨ findim ğ•œ f.range = 1,
        { have I : âˆ€(k : â„•), k â‰¤ 1 â†” k = 0 âˆ¨ k = 1, by omega manual,
          have : findim ğ•œ f.range â‰¤ findim ğ•œ ğ•œ := submodule.findim_le _,
          rwa [findim_of_field, I] at this },
        cases this,
        { rw this at Z,
          right,
          simpa using Z },
        { left,
          rw [this, add_comm, nat.add_one] at Z,
          exact nat.succ.inj Z } },
      have : is_closed (f.ker : set E),
      { cases this,
        { exact Hâ‚ _ this },
        { have : f.ker = âŠ¤,
            by { apply eq_top_of_findim_eq, rw [findim_eq_card_basis hÎ¾, hn, this] },
          simp [this] } },
      exact linear_map.continuous_iff_is_closed_ker.2 this },
    -- third step: applying the continuity to the linear form corresponding to a coefficient in the
    -- basis decomposition, deduce that all such coefficients are controlled in terms of the norm
    have : âˆ€i:Î¹, âˆƒC, 0 â‰¤ C âˆ§ âˆ€(x:E), âˆ¥hÎ¾.equiv_fun x iâˆ¥ â‰¤ C * âˆ¥xâˆ¥,
    { assume i,
      let f : E â†’â‚—[ğ•œ] ğ•œ := (linear_map.proj i).comp hÎ¾.equiv_fun,
      let f' : E â†’L[ğ•œ] ğ•œ := { cont := Hâ‚‚ f, ..f },
      exact âŸ¨âˆ¥f'âˆ¥, norm_nonneg _, Î»x, continuous_linear_map.le_op_norm f' xâŸ© },
    -- fourth step: combine the bound on each coefficient to get a global bound and the continuity
    choose C0 hC0 using this,
    let C := âˆ‘ i, C0 i,
    have C_nonneg : 0 â‰¤ C := finset.sum_nonneg (Î»i hi, (hC0 i).1),
    have C0_le : âˆ€i, C0 i â‰¤ C :=
      Î»i, finset.single_le_sum (Î»j hj, (hC0 j).1) (finset.mem_univ _),
    apply linear_map.continuous_of_bound _ C (Î»x, _),
    rw pi_norm_le_iff,
    { exact Î»i, le_trans ((hC0 i).2 x) (mul_le_mul_of_nonneg_right (C0_le i) (norm_nonneg _)) },
    { exact mul_nonneg C_nonneg (norm_nonneg _) } }
end

/-- Any linear map on a finite dimensional space over a complete field is continuous. -/
theorem linear_map.continuous_of_finite_dimensional [finite_dimensional ğ•œ E] (f : E â†’â‚—[ğ•œ] F') :
  continuous f :=
begin
  -- for the proof, go to a model vector space `b â†’ ğ•œ` thanks to `continuous_equiv_fun_basis`, and
  -- argue that all linear maps there are continuous.
  rcases exists_is_basis_finite ğ•œ E with âŸ¨b, b_basis, b_finiteâŸ©,
  letI : fintype b := finite.fintype b_finite,
  have A : continuous b_basis.equiv_fun :=
    continuous_equiv_fun_basis _ b_basis,
  have B : continuous (f.comp (b_basis.equiv_fun.symm : (b â†’ ğ•œ) â†’â‚—[ğ•œ] E)) :=
    linear_map.continuous_on_pi _,
  have : continuous ((f.comp (b_basis.equiv_fun.symm : (b â†’ ğ•œ) â†’â‚—[ğ•œ] E))
                      âˆ˜ b_basis.equiv_fun) := B.comp A,
  convert this,
  ext x,
  dsimp,
  rw linear_equiv.symm_apply_apply
end

/-- The continuous linear map induced by a linear map on a finite dimensional space -/
def linear_map.to_continuous_linear_map [finite_dimensional ğ•œ E] (f : E â†’â‚—[ğ•œ] F') : E â†’L[ğ•œ] F' :=
{ cont := f.continuous_of_finite_dimensional, ..f }

/-- The continuous linear equivalence induced by a linear equivalence on a finite dimensional space. -/
def linear_equiv.to_continuous_linear_equiv [finite_dimensional ğ•œ E] (e : E â‰ƒâ‚—[ğ•œ] F) : E â‰ƒL[ğ•œ] F :=
{ continuous_to_fun := e.to_linear_map.continuous_of_finite_dimensional,
  continuous_inv_fun := begin
    haveI : finite_dimensional ğ•œ F := e.finite_dimensional,
    exact e.symm.to_linear_map.continuous_of_finite_dimensional
  end,
  ..e }

variables {Î¹ : Type*} [fintype Î¹]

/-- Construct a continuous linear map given the value at a finite basis. -/
def is_basis.constrL {v : Î¹ â†’ E} (hv : is_basis ğ•œ v) (f : Î¹ â†’ F) :
  E â†’L[ğ•œ] F :=
âŸ¨hv.constr f, begin
  haveI : finite_dimensional ğ•œ E := finite_dimensional.of_finite_basis hv,
  exact (hv.constr f).continuous_of_finite_dimensional,
endâŸ©

@[simp, norm_cast] lemma is_basis.coe_constrL {v : Î¹ â†’ E} (hv : is_basis ğ•œ v) (f : Î¹ â†’ F) :
  (hv.constrL f : E â†’â‚—[ğ•œ] F) = hv.constr f := rfl

/-- The continuous linear equivalence between a vector space over `ğ•œ` with a finite basis and
functions from its basis indexing type to `ğ•œ`. -/
def is_basis.equiv_funL {v : Î¹ â†’ E} (hv : is_basis ğ•œ v) : E â‰ƒL[ğ•œ] (Î¹ â†’ ğ•œ) :=
{ continuous_to_fun := begin
    haveI : finite_dimensional ğ•œ E := finite_dimensional.of_finite_basis hv,
    apply linear_map.continuous_of_finite_dimensional,
  end,
  continuous_inv_fun := begin
    change continuous hv.equiv_fun.symm.to_fun,
    apply linear_map.continuous_of_finite_dimensional,
  end,
  ..hv.equiv_fun }


@[simp] lemma is_basis.constrL_apply {v : Î¹ â†’ E} (hv : is_basis ğ•œ v) (f : Î¹ â†’ F) (e : E) :
  (hv.constrL f) e = âˆ‘ i, (hv.equiv_fun e i) â€¢ f i :=
hv.constr_apply_fintype _ _

@[simp] lemma is_basis.constrL_basis {v : Î¹ â†’ E} (hv : is_basis ğ•œ v) (f : Î¹ â†’ F) (i : Î¹) :
  (hv.constrL f) (v i) = f i :=
constr_basis _

lemma is_basis.sup_norm_le_norm {v : Î¹ â†’ E} (hv : is_basis ğ•œ v) :
  âˆƒ C > (0 : â„), âˆ€ e : E, âˆ‘ i, âˆ¥hv.equiv_fun e iâˆ¥ â‰¤ C * âˆ¥eâˆ¥ :=
begin
  set Ï† := hv.equiv_funL.to_continuous_linear_map,
  set C := âˆ¥Ï†âˆ¥ * (fintype.card Î¹),
  use [max C 1, lt_of_lt_of_le (zero_lt_one) (le_max_right C 1)],
  intros e,
  calc âˆ‘ i, âˆ¥Ï† e iâˆ¥ â‰¤ âˆ‘ i : Î¹, âˆ¥Ï† eâˆ¥ : by { apply finset.sum_le_sum,
                                           exact Î» i hi, norm_le_pi_norm (Ï† e) i }
  ... = âˆ¥Ï† eâˆ¥*(fintype.card Î¹) : by simpa only [mul_comm, finset.sum_const, nsmul_eq_mul]
  ... â‰¤ âˆ¥Ï†âˆ¥ * âˆ¥eâˆ¥ * (fintype.card Î¹) : mul_le_mul_of_nonneg_right (Ï†.le_op_norm e)
                                                                 (fintype.card Î¹).cast_nonneg
  ... = âˆ¥Ï†âˆ¥ * (fintype.card Î¹) * âˆ¥eâˆ¥ : by ring
  ... â‰¤ max C 1 * âˆ¥eâˆ¥ :  mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _)
end

lemma is_basis.op_norm_le  {Î¹ : Type*} [fintype Î¹] {v : Î¹ â†’ E} (hv : is_basis ğ•œ v) :
  âˆƒ C > (0 : â„), âˆ€ {u : E â†’L[ğ•œ] F} {M : â„}, 0 â‰¤ M â†’ (âˆ€ i, âˆ¥u (v i)âˆ¥ â‰¤ M) â†’ âˆ¥uâˆ¥ â‰¤ C*M :=
begin
  obtain âŸ¨C, C_pos, hCâŸ© : âˆƒ C > (0 : â„), âˆ€ (e : E), âˆ‘ i, âˆ¥hv.equiv_fun e iâˆ¥ â‰¤ C * âˆ¥eâˆ¥,
    from hv.sup_norm_le_norm,
  use [C, C_pos],
  intros u M hM hu,
  apply u.op_norm_le_bound (mul_nonneg (le_of_lt C_pos) hM),
  intros e,
  calc
  âˆ¥u eâˆ¥ = âˆ¥u (âˆ‘ i, hv.equiv_fun e i â€¢ v i)âˆ¥ :  by conv_lhs { rw â† hv.equiv_fun_total e }
  ... = âˆ¥âˆ‘ i, (hv.equiv_fun e i) â€¢ (u $ v i)âˆ¥ :  by simp [u.map_sum, linear_map.map_smul]
  ... â‰¤ âˆ‘ i, âˆ¥(hv.equiv_fun e i) â€¢ (u $ v i)âˆ¥ : norm_sum_le _ _
  ... = âˆ‘ i, âˆ¥hv.equiv_fun e iâˆ¥ * âˆ¥u (v i)âˆ¥ : by simp only [norm_smul]
  ... â‰¤ âˆ‘ i, âˆ¥hv.equiv_fun e iâˆ¥ * M : finset.sum_le_sum (Î» i hi,
                                                  mul_le_mul_of_nonneg_left (hu i) (norm_nonneg _))
  ... = (âˆ‘ i, âˆ¥hv.equiv_fun e iâˆ¥) * M : finset.sum_mul.symm
  ... â‰¤ C * âˆ¥eâˆ¥ * M : mul_le_mul_of_nonneg_right (hC e) hM
  ... = C * M * âˆ¥eâˆ¥ : by ring
end

instance [finite_dimensional ğ•œ E] [second_countable_topology F] :
  second_countable_topology (E â†’L[ğ•œ] F) :=
begin
  set d := finite_dimensional.findim ğ•œ E,
  suffices :
    âˆ€ Îµ > (0 : â„), âˆƒ n : (E â†’L[ğ•œ] F) â†’ fin d â†’ â„•, âˆ€ (f g : E â†’L[ğ•œ] F), n f = n g â†’ dist f g â‰¤ Îµ,
  from metric.second_countable_of_countable_discretization
    (Î» Îµ Îµ_pos, âŸ¨fin d â†’ â„•, by apply_instance, this Îµ Îµ_posâŸ©),
  intros Îµ Îµ_pos,
  obtain âŸ¨u : â„• â†’ F, hu : closure (range u) = univâŸ© := exists_dense_seq F,
  obtain âŸ¨v : fin d â†’ E, hv : is_basis ğ•œ vâŸ© := finite_dimensional.fin_basis ğ•œ E,
  obtain âŸ¨C : â„, C_pos : 0 < C,
          hC : âˆ€ {Ï† : E â†’L[ğ•œ] F} {M : â„}, 0 â‰¤ M â†’ (âˆ€ i, âˆ¥Ï† (v i)âˆ¥ â‰¤ M) â†’ âˆ¥Ï†âˆ¥ â‰¤ C * MâŸ© := hv.op_norm_le,
  have h_2C : 0 < 2*C := mul_pos zero_lt_two C_pos,
  have hÎµ2C : 0 < Îµ/(2*C) := div_pos Îµ_pos h_2C,
  have : âˆ€ Ï† : E â†’L[ğ•œ] F, âˆƒ n : fin d â†’ â„•, âˆ¥Ï† - (hv.constrL $ u âˆ˜ n)âˆ¥ â‰¤ Îµ/2,
  { intros Ï†,
    have : âˆ€ i, âˆƒ n, âˆ¥Ï† (v i) - u nâˆ¥ â‰¤ Îµ/(2*C),
    { simp only [norm_sub_rev],
      intro i,
      have : Ï† (v i) âˆˆ closure (range u), by simp [hu],
      obtain âŸ¨n, hnâŸ© : âˆƒ n, âˆ¥u n - Ï† (v i)âˆ¥ < Îµ / (2 * C),
      { rw mem_closure_iff_nhds_basis metric.nhds_basis_ball at this,
        specialize this (Îµ/(2*C)) hÎµ2C,
        simpa [dist_eq_norm] },
      exact âŸ¨n, le_of_lt hnâŸ© },
    choose n hn using this,
    use n,
    replace hn : âˆ€ i : fin d, âˆ¥(Ï† - (hv.constrL $ u âˆ˜ n)) (v i)âˆ¥ â‰¤ Îµ / (2 * C), by simp [hn],
    have : C * (Îµ / (2 * C)) = Îµ/2,
    { rw [eq_div_iff (two_ne_zero : (2 : â„) â‰  0), mul_comm, â† mul_assoc,
          mul_div_cancel' _ (ne_of_gt h_2C)] },
    specialize hC (le_of_lt hÎµ2C) hn,
    rwa this at hC },
  choose n hn using this,
  set Î¦ := Î» Ï† : E â†’L[ğ•œ] F, (hv.constrL $ u âˆ˜ (n Ï†)),
  change âˆ€ z, dist z (Î¦ z) â‰¤ Îµ/2 at hn,
  use n,
  intros x y hxy,
  calc dist x y â‰¤ dist x (Î¦ x) + dist (Î¦ x) y : dist_triangle _ _ _
  ... = dist x (Î¦ x) + dist y (Î¦ y) : by simp [Î¦, hxy, dist_comm]
  ... â‰¤ Îµ : by linarith [hn x, hn y]
end

/-- Any finite-dimensional vector space over a complete field is complete.
We do not register this as an instance to avoid an instance loop when trying to prove the
completeness of `ğ•œ`, and the search for `ğ•œ` as an unknown metavariable. Declare the instance
explicitly when needed. -/
variables (ğ•œ E)
lemma finite_dimensional.complete [finite_dimensional ğ•œ E] : complete_space E :=
begin
  rcases exists_is_basis_finite ğ•œ E with âŸ¨b, b_basis, b_finiteâŸ©,
  letI : fintype b := finite.fintype b_finite,
  have : uniform_embedding b_basis.equiv_fun.symm :=
    linear_equiv.uniform_embedding _ (linear_map.continuous_of_finite_dimensional _)
    (linear_map.continuous_of_finite_dimensional _),
  change uniform_embedding b_basis.equiv_fun.symm.to_equiv at this,
  exact (complete_space_congr this).1 (by apply_instance)
end

variables {ğ•œ E}
/-- A finite-dimensional subspace is complete. -/
lemma submodule.complete_of_finite_dimensional (s : submodule ğ•œ E) [finite_dimensional ğ•œ s] :
  is_complete (s : set E) :=
complete_space_coe_iff_is_complete.1 (finite_dimensional.complete ğ•œ s)

/-- A finite-dimensional subspace is closed. -/
lemma submodule.closed_of_finite_dimensional (s : submodule ğ•œ E) [finite_dimensional ğ•œ s] :
  is_closed (s : set E) :=
s.complete_of_finite_dimensional.is_closed

lemma continuous_linear_map.exists_right_inverse_of_surjective [finite_dimensional ğ•œ F]
  (f : E â†’L[ğ•œ] F) (hf : f.range = âŠ¤) :
  âˆƒ g : F â†’L[ğ•œ] E, f.comp g = continuous_linear_map.id ğ•œ F :=
let âŸ¨g, hgâŸ© := (f : E â†’â‚—[ğ•œ] F).exists_right_inverse_of_surjective hf in
âŸ¨g.to_continuous_linear_map, continuous_linear_map.ext $ linear_map.ext_iff.1 hgâŸ©

lemma closed_embedding_smul_left {c : E} (hc : c â‰  0) : closed_embedding (Î» x : ğ•œ, x â€¢ c) :=
begin
  haveI : finite_dimensional ğ•œ (submodule.span ğ•œ {c}) :=
    finite_dimensional.span_of_finite ğ•œ (finite_singleton c),
  have m1 : closed_embedding (coe : submodule.span ğ•œ {c} â†’ E) :=
  (submodule.span ğ•œ {c}).closed_of_finite_dimensional.closed_embedding_subtype_coe,
  have m2 : closed_embedding
    (linear_equiv.to_span_nonzero_singleton ğ•œ E c hc : ğ•œ â†’ submodule.span ğ•œ {c}) :=
  (continuous_linear_equiv.to_span_nonzero_singleton ğ•œ c hc).to_homeomorph.closed_embedding,
  exact m1.comp m2
end

/- `smul` is a closed map in the first argument. -/
lemma is_closed_map_smul_left (c : E) : is_closed_map (Î» x : ğ•œ, x â€¢ c) :=
begin
  by_cases hc : c = 0,
  { simp_rw [hc, smul_zero], exact is_closed_map_const },
  { exact (closed_embedding_smul_left hc).is_closed_map }
end

end complete_field

section proper_field
variables (ğ•œ : Type u) [nondiscrete_normed_field ğ•œ]
(E : Type v) [normed_group E] [normed_space ğ•œ E] [proper_space ğ•œ]

/-- Any finite-dimensional vector space over a proper field is proper.
We do not register this as an instance to avoid an instance loop when trying to prove the
properness of `ğ•œ`, and the search for `ğ•œ` as an unknown metavariable. Declare the instance
explicitly when needed. -/
lemma finite_dimensional.proper [finite_dimensional ğ•œ E] : proper_space E :=
begin
  rcases exists_is_basis_finite ğ•œ E with âŸ¨b, b_basis, b_finiteâŸ©,
  letI : fintype b := finite.fintype b_finite,
  let e := b_basis.equiv_fun,
  let f : E â†’L[ğ•œ] (b â†’ ğ•œ) :=
    { cont := linear_map.continuous_of_finite_dimensional _, ..e.to_linear_map },
  refine metric.proper_image_of_proper e.symm
    (linear_map.continuous_of_finite_dimensional _) _ (âˆ¥fâˆ¥)  (Î»x y, _),
  { exact equiv.range_eq_univ e.symm.to_equiv },
  { have A : e (e.symm x) = x := linear_equiv.apply_symm_apply _ _,
    have B : e (e.symm y) = y := linear_equiv.apply_symm_apply _ _,
    conv_lhs { rw [â† A, â† B] },
    change dist (f (e.symm x)) (f (e.symm y)) â‰¤ âˆ¥fâˆ¥ * dist (e.symm x) (e.symm y),
    unfreezingI { exact f.lipschitz.dist_le_mul _ _ } }
end

end proper_field

/- Over the real numbers, we can register the previous statement as an instance as it will not
cause problems in instance resolution since the properness of `â„` is already known. -/
instance finite_dimensional.proper_real
  (E : Type u) [normed_group E] [normed_space â„ E] [finite_dimensional â„ E] : proper_space E :=
finite_dimensional.proper â„ E

attribute [instance, priority 900] finite_dimensional.proper_real
