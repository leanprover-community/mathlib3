/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.normed_space.affine_isometry
import analysis.normed_space.operator_norm
import analysis.asymptotics.asymptotic_equivalent
import linear_algebra.finite_dimensional

/-!
# Finite dimensional normed spaces over complete fields

Over a complete nondiscrete field, in finite dimension, all norms are equivalent and all linear maps
are continuous. Moreover, a finite-dimensional subspace is always complete and closed.

## Main results:

* `linear_map.continuous_of_finite_dimensional` : a linear map on a finite-dimensional space over a
  complete field is continuous.
* `finite_dimensional.complete` : a finite-dimensional space over a complete field is complete. This
  is not registered as an instance, as the field would be an unknown metavariable in typeclass
  resolution.
* `submodule.closed_of_finite_dimensional` : a finite-dimensional subspace over a complete field is
  closed
* `finite_dimensional.proper` : a finite-dimensional space over a proper field is proper. This
  is not registered as an instance, as the field would be an unknown metavariable in typeclass
  resolution. It is however registered as an instance for `ğ•œ = â„` and `ğ•œ = â„‚`. As properness
  implies completeness, there is no need to also register `finite_dimensional.complete` on `â„` or
  `â„‚`.

## Implementation notes

The fact that all norms are equivalent is not written explicitly, as it would mean having two norms
on a single space, which is not the way type classes work. However, if one has a
finite-dimensional vector space `E` with a norm, and a copy `E'` of this type with another norm,
then the identities from `E` to `E'` and from `E'`to `E` are continuous thanks to
`linear_map.continuous_of_finite_dimensional`. This gives the desired norm equivalence.
-/

universes u v w x

open set finite_dimensional topological_space filter asymptotics
open_locale classical big_operators filter topological_space asymptotics

noncomputable theory

/-- A linear map on `Î¹ â†’ ğ•œ` (where `Î¹` is a fintype) is continuous -/
lemma linear_map.continuous_on_pi {Î¹ : Type w} [fintype Î¹] {ğ•œ : Type u} [normed_field ğ•œ]
  {E : Type v}  [add_comm_group E] [module ğ•œ E] [topological_space E]
  [topological_add_group E] [has_continuous_smul ğ•œ E] (f : (Î¹ â†’ ğ•œ) â†’â‚—[ğ•œ] E) : continuous f :=
begin
  -- for the proof, write `f` in the standard basis, and use that each coordinate is a continuous
  -- function.
  have : (f : (Î¹ â†’ ğ•œ) â†’ E) =
         (Î»x, âˆ‘ i : Î¹, x i â€¢ (f (Î»j, if i = j then 1 else 0))),
    by { ext x, exact f.pi_apply_eq_sum_univ x },
  rw this,
  refine continuous_finset_sum _ (Î»i hi, _),
  exact (continuous_apply i).smul continuous_const
end

section complete_field

variables {ğ•œ : Type u} [nondiscrete_normed_field ğ•œ]
{E : Type v} [normed_group E] [normed_space ğ•œ E]
{F : Type w} [normed_group F] [normed_space ğ•œ F]
{F' : Type x} [add_comm_group F'] [module ğ•œ F'] [topological_space F']
[topological_add_group F'] [has_continuous_smul ğ•œ F']
[complete_space ğ•œ]

/-- In finite dimension over a complete field, the canonical identification (in terms of a basis)
with `ğ•œ^n` together with its sup norm is continuous. This is the nontrivial part in the fact that
all norms are equivalent in finite dimension.

This statement is superceded by the fact that every linear map on a finite-dimensional space is
continuous, in `linear_map.continuous_of_finite_dimensional`. -/
lemma continuous_equiv_fun_basis {Î¹ : Type v} [fintype Î¹] (Î¾ : basis Î¹ ğ•œ E) :
  continuous Î¾.equiv_fun :=
begin
  unfreezingI { induction hn : fintype.card Î¹ with n IH generalizing Î¹ E },
  { apply linear_map.continuous_of_bound _ 0 (Î»x, _),
    have : Î¾.equiv_fun x = 0,
      by { ext i, exact (fintype.card_eq_zero_iff.1 hn).elim i },
    change âˆ¥Î¾.equiv_fun xâˆ¥ â‰¤ 0 * âˆ¥xâˆ¥,
    rw this,
    simp [norm_nonneg] },
  { haveI : finite_dimensional ğ•œ E := of_fintype_basis Î¾,
    -- first step: thanks to the inductive assumption, any n-dimensional subspace is equivalent
    -- to a standard space of dimension n, hence it is complete and therefore closed.
    have Hâ‚ : âˆ€s : submodule ğ•œ E, finrank ğ•œ s = n â†’ is_closed (s : set E),
    { assume s s_dim,
      let b := basis.of_vector_space ğ•œ s,
      have U : uniform_embedding b.equiv_fun.symm.to_equiv,
      { have : fintype.card (basis.of_vector_space_index ğ•œ s) = n,
          by { rw â† s_dim, exact (finrank_eq_card_basis b).symm },
        have : continuous b.equiv_fun := IH b this,
        exact b.equiv_fun.symm.uniform_embedding (linear_map.continuous_on_pi _) this },
      have : is_complete (s : set E),
        from complete_space_coe_iff_is_complete.1 ((complete_space_congr U).1 (by apply_instance)),
      exact this.is_closed },
    -- second step: any linear form is continuous, as its kernel is closed by the first step
    have Hâ‚‚ : âˆ€f : E â†’â‚—[ğ•œ] ğ•œ, continuous f,
    { assume f,
      have : finrank ğ•œ f.ker = n âˆ¨ finrank ğ•œ f.ker = n.succ,
      { have Z := f.finrank_range_add_finrank_ker,
        rw [finrank_eq_card_basis Î¾, hn] at Z,
        by_cases H : finrank ğ•œ f.range = 0,
        { right,
          rw H at Z,
          simpa using Z },
        { left,
          have : finrank ğ•œ f.range = 1,
          { refine le_antisymm _ (zero_lt_iff.mpr H),
            simpa [finrank_self] using f.range.finrank_le },
          rw [this, add_comm, nat.add_one] at Z,
          exact nat.succ.inj Z } },
      have : is_closed (f.ker : set E),
      { cases this,
        { exact Hâ‚ _ this },
        { have : f.ker = âŠ¤,
            by { apply eq_top_of_finrank_eq, rw [finrank_eq_card_basis Î¾, hn, this] },
          simp [this] } },
      exact linear_map.continuous_iff_is_closed_ker.2 this },
    -- third step: applying the continuity to the linear form corresponding to a coefficient in the
    -- basis decomposition, deduce that all such coefficients are controlled in terms of the norm
    have : âˆ€i:Î¹, âˆƒC, 0 â‰¤ C âˆ§ âˆ€(x:E), âˆ¥Î¾.equiv_fun x iâˆ¥ â‰¤ C * âˆ¥xâˆ¥,
    { assume i,
      let f : E â†’â‚—[ğ•œ] ğ•œ := (linear_map.proj i) âˆ˜â‚— â†‘Î¾.equiv_fun,
      let f' : E â†’L[ğ•œ] ğ•œ := { cont := Hâ‚‚ f, ..f },
      exact âŸ¨âˆ¥f'âˆ¥, norm_nonneg _, Î»x, continuous_linear_map.le_op_norm f' xâŸ© },
    -- fourth step: combine the bound on each coefficient to get a global bound and the continuity
    choose C0 hC0 using this,
    let C := âˆ‘ i, C0 i,
    have C_nonneg : 0 â‰¤ C := finset.sum_nonneg (Î»i hi, (hC0 i).1),
    have C0_le : âˆ€i, C0 i â‰¤ C :=
      Î»i, finset.single_le_sum (Î»j hj, (hC0 j).1) (finset.mem_univ _),
    apply linear_map.continuous_of_bound _ C (Î»x, _),
    rw pi_semi_norm_le_iff,
    { exact Î»i, le_trans ((hC0 i).2 x) (mul_le_mul_of_nonneg_right (C0_le i) (norm_nonneg _)) },
    { exact mul_nonneg C_nonneg (norm_nonneg _) } }
end

/-- Any linear map on a finite dimensional space over a complete field is continuous. -/
theorem linear_map.continuous_of_finite_dimensional [finite_dimensional ğ•œ E] (f : E â†’â‚—[ğ•œ] F') :
  continuous f :=
begin
  -- for the proof, go to a model vector space `b â†’ ğ•œ` thanks to `continuous_equiv_fun_basis`, and
  -- argue that all linear maps there are continuous.
  let b := basis.of_vector_space ğ•œ E,
  have A : continuous b.equiv_fun :=
    continuous_equiv_fun_basis b,
  have B : continuous (f.comp (b.equiv_fun.symm : (basis.of_vector_space_index ğ•œ E â†’ ğ•œ) â†’â‚—[ğ•œ] E)) :=
    linear_map.continuous_on_pi _,
  have : continuous ((f.comp (b.equiv_fun.symm : (basis.of_vector_space_index ğ•œ E â†’ ğ•œ) â†’â‚—[ğ•œ] E))
                      âˆ˜ b.equiv_fun) := B.comp A,
  convert this,
  ext x,
  dsimp,
  rw [basis.equiv_fun_symm_apply, basis.sum_repr]
end

theorem affine_map.continuous_of_finite_dimensional {PE PF : Type*}
  [metric_space PE] [normed_add_torsor E PE] [metric_space PF] [normed_add_torsor F PF]
  [finite_dimensional ğ•œ E] (f : PE â†’áµƒ[ğ•œ] PF) : continuous f :=
affine_map.continuous_linear_iff.1 f.linear.continuous_of_finite_dimensional

namespace linear_map

variables [finite_dimensional ğ•œ E]

/-- The continuous linear map induced by a linear map on a finite dimensional space -/
def to_continuous_linear_map : (E â†’â‚—[ğ•œ] F') â‰ƒâ‚—[ğ•œ] E â†’L[ğ•œ] F' :=
{ to_fun := Î» f, âŸ¨f, f.continuous_of_finite_dimensionalâŸ©,
  inv_fun := coe,
  map_add' := Î» f g, rfl,
  map_smul' := Î» c f, rfl,
  left_inv := Î» f, rfl,
  right_inv := Î» f, continuous_linear_map.coe_injective rfl }

@[simp] lemma coe_to_continuous_linear_map' (f : E â†’â‚—[ğ•œ] F') :
  â‡‘f.to_continuous_linear_map = f := rfl

@[simp] lemma coe_to_continuous_linear_map (f : E â†’â‚—[ğ•œ] F') :
  (f.to_continuous_linear_map : E â†’â‚—[ğ•œ] F') = f := rfl

@[simp] lemma coe_to_continuous_linear_map_symm :
  â‡‘(to_continuous_linear_map : (E â†’â‚—[ğ•œ] F') â‰ƒâ‚—[ğ•œ] E â†’L[ğ•œ] F').symm = coe := rfl

end linear_map

/-- The continuous linear equivalence induced by a linear equivalence on a finite dimensional
space. -/
def linear_equiv.to_continuous_linear_equiv [finite_dimensional ğ•œ E] (e : E â‰ƒâ‚—[ğ•œ] F) : E â‰ƒL[ğ•œ] F :=
{ continuous_to_fun := e.to_linear_map.continuous_of_finite_dimensional,
  continuous_inv_fun := begin
    haveI : finite_dimensional ğ•œ F := e.finite_dimensional,
    exact e.symm.to_linear_map.continuous_of_finite_dimensional
  end,
  ..e }

lemma linear_map.exists_antilipschitz_with [finite_dimensional ğ•œ E] (f : E â†’â‚—[ğ•œ] F)
  (hf : f.ker = âŠ¥) : âˆƒ K > 0, antilipschitz_with K f :=
begin
  cases subsingleton_or_nontrivial E; resetI,
  { exact âŸ¨1, zero_lt_one, antilipschitz_with.of_subsingletonâŸ© },
  { let e : E â‰ƒL[ğ•œ] f.range := (linear_equiv.of_injective f hf).to_continuous_linear_equiv,
    exact âŸ¨_, e.nnnorm_symm_pos, e.antilipschitzâŸ© }
end

protected lemma linear_independent.eventually {Î¹} [fintype Î¹] {f : Î¹ â†’ E}
  (hf : linear_independent ğ•œ f) : âˆ€á¶  g in ğ“ f, linear_independent ğ•œ g :=
begin
  simp only [fintype.linear_independent_iff'] at hf âŠ¢,
  rcases linear_map.exists_antilipschitz_with _ hf with âŸ¨K, K0, hKâŸ©,
  have : tendsto (Î» g : Î¹ â†’ E, âˆ‘ i, âˆ¥g i - f iâˆ¥) (ğ“ f) (ğ“ $ âˆ‘ i, âˆ¥f i - f iâˆ¥),
    from tendsto_finset_sum _ (Î» i hi, tendsto.norm $
      ((continuous_apply i).tendsto _).sub tendsto_const_nhds),
  simp only [sub_self, norm_zero, finset.sum_const_zero] at this,
  refine (this.eventually (gt_mem_nhds $ inv_pos.2 K0)).mono (Î» g hg, _),
  replace hg : âˆ‘ i, nnnorm (g i - f i) < Kâ»Â¹, by { rw â† nnreal.coe_lt_coe, push_cast, exact hg },
  rw linear_map.ker_eq_bot,
  refine (hK.add_sub_lipschitz_with (lipschitz_with.of_dist_le_mul $ Î» v u, _) hg).injective,
  simp only [dist_eq_norm, linear_map.lsum_apply, pi.sub_apply, linear_map.sum_apply,
    linear_map.comp_apply, linear_map.proj_apply, linear_map.smul_right_apply, linear_map.id_apply,
    â† finset.sum_sub_distrib, â† smul_sub, â† sub_smul, nnreal.coe_sum, coe_nnnorm, finset.sum_mul],
  refine norm_sum_le_of_le _ (Î» i _, _),
  rw [norm_smul, mul_comm],
  exact mul_le_mul_of_nonneg_left (norm_le_pi_norm (v - u) i) (norm_nonneg _)
end

lemma is_open_set_of_linear_independent {Î¹ : Type*} [fintype Î¹] :
  is_open {f : Î¹ â†’ E | linear_independent ğ•œ f} :=
is_open_iff_mem_nhds.2 $ Î» f, linear_independent.eventually

lemma is_open_set_of_nat_le_rank (n : â„•) : is_open {f : E â†’L[ğ•œ] F | â†‘n â‰¤ rank (f : E â†’â‚—[ğ•œ] F)} :=
begin
  simp only [le_rank_iff_exists_linear_independent_finset, set_of_exists, â† exists_prop],
  refine is_open_bUnion (Î» t ht, _),
  have : continuous (Î» f : E â†’L[ğ•œ] F, (Î» x : (t : set E), f x)),
    from continuous_pi (Î» x, (continuous_linear_map.apply ğ•œ F (x : E)).continuous),
  exact is_open_set_of_linear_independent.preimage this
end

/-- Two finite-dimensional normed spaces are continuously linearly equivalent if they have the same
(finite) dimension. -/
theorem finite_dimensional.nonempty_continuous_linear_equiv_of_finrank_eq
  [finite_dimensional ğ•œ E] [finite_dimensional ğ•œ F] (cond : finrank ğ•œ E = finrank ğ•œ F) :
  nonempty (E â‰ƒL[ğ•œ] F) :=
(nonempty_linear_equiv_of_finrank_eq cond).map linear_equiv.to_continuous_linear_equiv

/-- Two finite-dimensional normed spaces are continuously linearly equivalent if and only if they
have the same (finite) dimension. -/
theorem finite_dimensional.nonempty_continuous_linear_equiv_iff_finrank_eq
  [finite_dimensional ğ•œ E] [finite_dimensional ğ•œ F] :
   nonempty (E â‰ƒL[ğ•œ] F) â†” finrank ğ•œ E = finrank ğ•œ F :=
âŸ¨ Î» âŸ¨hâŸ©, h.to_linear_equiv.finrank_eq,
  Î» h, finite_dimensional.nonempty_continuous_linear_equiv_of_finrank_eq h âŸ©

/-- A continuous linear equivalence between two finite-dimensional normed spaces of the same
(finite) dimension. -/
def continuous_linear_equiv.of_finrank_eq [finite_dimensional ğ•œ E] [finite_dimensional ğ•œ F]
  (cond : finrank ğ•œ E = finrank ğ•œ F) :
  E â‰ƒL[ğ•œ] F :=
(linear_equiv.of_finrank_eq E F cond).to_continuous_linear_equiv

variables {Î¹ : Type*} [fintype Î¹]

/-- Construct a continuous linear map given the value at a finite basis. -/
def basis.constrL (v : basis Î¹ ğ•œ E) (f : Î¹ â†’ F) :
  E â†’L[ğ•œ] F :=
by haveI : finite_dimensional ğ•œ E := finite_dimensional.of_fintype_basis v;
  exact (v.constr ğ•œ f).to_continuous_linear_map

@[simp, norm_cast] lemma basis.coe_constrL (v : basis Î¹ ğ•œ E) (f : Î¹ â†’ F) :
  (v.constrL f : E â†’â‚—[ğ•œ] F) = v.constr ğ•œ f := rfl

/-- The continuous linear equivalence between a vector space over `ğ•œ` with a finite basis and
functions from its basis indexing type to `ğ•œ`. -/
def basis.equiv_funL (v : basis Î¹ ğ•œ E) : E â‰ƒL[ğ•œ] (Î¹ â†’ ğ•œ) :=
{ continuous_to_fun := begin
    haveI : finite_dimensional ğ•œ E := finite_dimensional.of_fintype_basis v,
    apply linear_map.continuous_of_finite_dimensional,
  end,
  continuous_inv_fun := begin
    change continuous v.equiv_fun.symm.to_fun,
    apply linear_map.continuous_of_finite_dimensional,
  end,
  ..v.equiv_fun }


@[simp] lemma basis.constrL_apply (v : basis Î¹ ğ•œ E) (f : Î¹ â†’ F) (e : E) :
  (v.constrL f) e = âˆ‘ i, (v.equiv_fun e i) â€¢ f i :=
v.constr_apply_fintype ğ•œ _ _

@[simp] lemma basis.constrL_basis (v : basis Î¹ ğ•œ E) (f : Î¹ â†’ F) (i : Î¹) :
  (v.constrL f) (v i) = f i :=
v.constr_basis ğ•œ _ _

lemma basis.sup_norm_le_norm (v : basis Î¹ ğ•œ E) :
  âˆƒ C > (0 : â„), âˆ€ e : E, âˆ‘ i, âˆ¥v.equiv_fun e iâˆ¥ â‰¤ C * âˆ¥eâˆ¥ :=
begin
  set Ï† := v.equiv_funL.to_continuous_linear_map,
  set C := âˆ¥Ï†âˆ¥ * (fintype.card Î¹),
  use [max C 1, lt_of_lt_of_le (zero_lt_one) (le_max_right C 1)],
  intros e,
  calc âˆ‘ i, âˆ¥Ï† e iâˆ¥ â‰¤ âˆ‘ i : Î¹, âˆ¥Ï† eâˆ¥ : by { apply finset.sum_le_sum,
                                           exact Î» i hi, norm_le_pi_norm (Ï† e) i }
  ... = âˆ¥Ï† eâˆ¥*(fintype.card Î¹) : by simpa only [mul_comm, finset.sum_const, nsmul_eq_mul]
  ... â‰¤ âˆ¥Ï†âˆ¥ * âˆ¥eâˆ¥ * (fintype.card Î¹) : mul_le_mul_of_nonneg_right (Ï†.le_op_norm e)
                                                                 (fintype.card Î¹).cast_nonneg
  ... = âˆ¥Ï†âˆ¥ * (fintype.card Î¹) * âˆ¥eâˆ¥ : by ring
  ... â‰¤ max C 1 * âˆ¥eâˆ¥ :  mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _)
end

lemma basis.op_norm_le  {Î¹ : Type*} [fintype Î¹] (v : basis Î¹ ğ•œ E) :
  âˆƒ C > (0 : â„), âˆ€ {u : E â†’L[ğ•œ] F} {M : â„}, 0 â‰¤ M â†’ (âˆ€ i, âˆ¥u (v i)âˆ¥ â‰¤ M) â†’ âˆ¥uâˆ¥ â‰¤ C*M :=
begin
  obtain âŸ¨C, C_pos, hCâŸ© : âˆƒ C > (0 : â„), âˆ€ (e : E), âˆ‘ i, âˆ¥v.equiv_fun e iâˆ¥ â‰¤ C * âˆ¥eâˆ¥,
    from v.sup_norm_le_norm,
  use [C, C_pos],
  intros u M hM hu,
  apply u.op_norm_le_bound (mul_nonneg (le_of_lt C_pos) hM),
  intros e,
  calc
  âˆ¥u eâˆ¥ = âˆ¥u (âˆ‘ i, v.equiv_fun e i â€¢ v i)âˆ¥ :   by rw [v.sum_equiv_fun]
  ... = âˆ¥âˆ‘ i, (v.equiv_fun e i) â€¢ (u $ v i)âˆ¥ : by simp [u.map_sum, linear_map.map_smul]
  ... â‰¤ âˆ‘ i, âˆ¥(v.equiv_fun e i) â€¢ (u $ v i)âˆ¥ : norm_sum_le _ _
  ... = âˆ‘ i, âˆ¥v.equiv_fun e iâˆ¥ * âˆ¥u (v i)âˆ¥ :   by simp only [norm_smul]
  ... â‰¤ âˆ‘ i, âˆ¥v.equiv_fun e iâˆ¥ * M : finset.sum_le_sum (Î» i hi,
                                                  mul_le_mul_of_nonneg_left (hu i) (norm_nonneg _))
  ... = (âˆ‘ i, âˆ¥v.equiv_fun e iâˆ¥) * M : finset.sum_mul.symm
  ... â‰¤ C * âˆ¥eâˆ¥ * M : mul_le_mul_of_nonneg_right (hC e) hM
  ... = C * M * âˆ¥eâˆ¥ : by ring
end

instance [finite_dimensional ğ•œ E] [second_countable_topology F] :
  second_countable_topology (E â†’L[ğ•œ] F) :=
begin
  set d := finite_dimensional.finrank ğ•œ E,
  suffices :
    âˆ€ Îµ > (0 : â„), âˆƒ n : (E â†’L[ğ•œ] F) â†’ fin d â†’ â„•, âˆ€ (f g : E â†’L[ğ•œ] F), n f = n g â†’ dist f g â‰¤ Îµ,
  from metric.second_countable_of_countable_discretization
    (Î» Îµ Îµ_pos, âŸ¨fin d â†’ â„•, by apply_instance, this Îµ Îµ_posâŸ©),
  intros Îµ Îµ_pos,
  obtain âŸ¨u : â„• â†’ F, hu : dense_range uâŸ© := exists_dense_seq F,
  let v := finite_dimensional.fin_basis ğ•œ E,
  obtain âŸ¨C : â„, C_pos : 0 < C,
          hC : âˆ€ {Ï† : E â†’L[ğ•œ] F} {M : â„}, 0 â‰¤ M â†’ (âˆ€ i, âˆ¥Ï† (v i)âˆ¥ â‰¤ M) â†’ âˆ¥Ï†âˆ¥ â‰¤ C * MâŸ© :=
    v.op_norm_le,
  have h_2C : 0 < 2*C := mul_pos zero_lt_two C_pos,
  have hÎµ2C : 0 < Îµ/(2*C) := div_pos Îµ_pos h_2C,
  have : âˆ€ Ï† : E â†’L[ğ•œ] F, âˆƒ n : fin d â†’ â„•, âˆ¥Ï† - (v.constrL $ u âˆ˜ n)âˆ¥ â‰¤ Îµ/2,
  { intros Ï†,
    have : âˆ€ i, âˆƒ n, âˆ¥Ï† (v i) - u nâˆ¥ â‰¤ Îµ/(2*C),
    { simp only [norm_sub_rev],
      intro i,
      have : Ï† (v i) âˆˆ closure (range u) := hu _,
      obtain âŸ¨n, hnâŸ© : âˆƒ n, âˆ¥u n - Ï† (v i)âˆ¥ < Îµ / (2 * C),
      { rw mem_closure_iff_nhds_basis metric.nhds_basis_ball at this,
        specialize this (Îµ/(2*C)) hÎµ2C,
        simpa [dist_eq_norm] },
      exact âŸ¨n, le_of_lt hnâŸ© },
    choose n hn using this,
    use n,
    replace hn : âˆ€ i : fin d, âˆ¥(Ï† - (v.constrL $ u âˆ˜ n)) (v i)âˆ¥ â‰¤ Îµ / (2 * C), by simp [hn],
    have : C * (Îµ / (2 * C)) = Îµ/2,
    { rw [eq_div_iff (two_ne_zero : (2 : â„) â‰  0), mul_comm, â† mul_assoc,
          mul_div_cancel' _ (ne_of_gt h_2C)] },
    specialize hC (le_of_lt hÎµ2C) hn,
    rwa this at hC },
  choose n hn using this,
  set Î¦ := Î» Ï† : E â†’L[ğ•œ] F, (v.constrL $ u âˆ˜ (n Ï†)),
  change âˆ€ z, dist z (Î¦ z) â‰¤ Îµ/2 at hn,
  use n,
  intros x y hxy,
  calc dist x y â‰¤ dist x (Î¦ x) + dist (Î¦ x) y : dist_triangle _ _ _
  ... = dist x (Î¦ x) + dist y (Î¦ y) : by simp [Î¦, hxy, dist_comm]
  ... â‰¤ Îµ : by linarith [hn x, hn y]
end

/-- Any finite-dimensional vector space over a complete field is complete.
We do not register this as an instance to avoid an instance loop when trying to prove the
completeness of `ğ•œ`, and the search for `ğ•œ` as an unknown metavariable. Declare the instance
explicitly when needed. -/
variables (ğ•œ E)
lemma finite_dimensional.complete [finite_dimensional ğ•œ E] : complete_space E :=
begin
  set e := continuous_linear_equiv.of_finrank_eq (@finrank_fin_fun ğ•œ _ (finrank ğ•œ E)).symm,
  have : uniform_embedding e.to_linear_equiv.to_equiv.symm := e.symm.uniform_embedding,
  exact (complete_space_congr this).1 (by apply_instance)
end

variables {ğ•œ E}
/-- A finite-dimensional subspace is complete. -/
lemma submodule.complete_of_finite_dimensional (s : submodule ğ•œ E) [finite_dimensional ğ•œ s] :
  is_complete (s : set E) :=
complete_space_coe_iff_is_complete.1 (finite_dimensional.complete ğ•œ s)

/-- A finite-dimensional subspace is closed. -/
lemma submodule.closed_of_finite_dimensional (s : submodule ğ•œ E) [finite_dimensional ğ•œ s] :
  is_closed (s : set E) :=
s.complete_of_finite_dimensional.is_closed

lemma continuous_linear_map.exists_right_inverse_of_surjective [finite_dimensional ğ•œ F]
  (f : E â†’L[ğ•œ] F) (hf : f.range = âŠ¤) :
  âˆƒ g : F â†’L[ğ•œ] E, f.comp g = continuous_linear_map.id ğ•œ F :=
let âŸ¨g, hgâŸ© := (f : E â†’â‚—[ğ•œ] F).exists_right_inverse_of_surjective hf in
âŸ¨g.to_continuous_linear_map, continuous_linear_map.ext $ linear_map.ext_iff.1 hgâŸ©

lemma closed_embedding_smul_left {c : E} (hc : c â‰  0) : closed_embedding (Î» x : ğ•œ, x â€¢ c) :=
begin
  haveI : finite_dimensional ğ•œ (submodule.span ğ•œ {c}) :=
    finite_dimensional.span_of_finite ğ•œ (finite_singleton c),
  have m1 : closed_embedding (coe : submodule.span ğ•œ {c} â†’ E) :=
  (submodule.span ğ•œ {c}).closed_of_finite_dimensional.closed_embedding_subtype_coe,
  have m2 : closed_embedding
    (linear_equiv.to_span_nonzero_singleton ğ•œ E c hc : ğ•œ â†’ submodule.span ğ•œ {c}) :=
  (continuous_linear_equiv.to_span_nonzero_singleton ğ•œ c hc).to_homeomorph.closed_embedding,
  exact m1.comp m2
end

/- `smul` is a closed map in the first argument. -/
lemma is_closed_map_smul_left (c : E) : is_closed_map (Î» x : ğ•œ, x â€¢ c) :=
begin
  by_cases hc : c = 0,
  { simp_rw [hc, smul_zero], exact is_closed_map_const },
  { exact (closed_embedding_smul_left hc).is_closed_map }
end

end complete_field

section proper_field
variables (ğ•œ : Type u) [nondiscrete_normed_field ğ•œ]
(E : Type v) [normed_group E] [normed_space ğ•œ E] [proper_space ğ•œ]

/-- Any finite-dimensional vector space over a proper field is proper.
We do not register this as an instance to avoid an instance loop when trying to prove the
properness of `ğ•œ`, and the search for `ğ•œ` as an unknown metavariable. Declare the instance
explicitly when needed. -/
lemma finite_dimensional.proper [finite_dimensional ğ•œ E] : proper_space E :=
begin
  set e := continuous_linear_equiv.of_finrank_eq (@finrank_fin_fun ğ•œ _ (finrank ğ•œ E)).symm,
  exact e.symm.antilipschitz.proper_space e.symm.continuous e.symm.surjective
end

end proper_field

/- Over the real numbers, we can register the previous statement as an instance as it will not
cause problems in instance resolution since the properness of `â„` is already known. -/
instance finite_dimensional.proper_real
  (E : Type u) [normed_group E] [normed_space â„ E] [finite_dimensional â„ E] : proper_space E :=
finite_dimensional.proper â„ E

attribute [instance, priority 900] finite_dimensional.proper_real

/-- In a finite dimensional vector space over `â„`, the series `âˆ‘ x, âˆ¥f xâˆ¥` is unconditionally
summable if and only if the series `âˆ‘ x, f x` is unconditionally summable. One implication holds in
any complete normed space, while the other holds only in finite dimensional spaces. -/
lemma summable_norm_iff {Î± E : Type*} [normed_group E] [normed_space â„ E] [finite_dimensional â„ E]
  {f : Î± â†’ E} : summable (Î» x, âˆ¥f xâˆ¥) â†” summable f :=
begin
  refine âŸ¨summable_of_summable_norm, Î» hf, _âŸ©,
  -- First we use a finite basis to reduce the problem to the case `E = fin N â†’ â„`
  suffices : âˆ€ {N : â„•} {g : Î± â†’ fin N â†’ â„}, summable g â†’ summable (Î» x, âˆ¥g xâˆ¥),
  { obtain v := fin_basis â„ E,
    set e := v.equiv_funL,
    have : summable (Î» x, âˆ¥e (f x)âˆ¥) := this (e.summable.2 hf),
    refine summable_of_norm_bounded _ (this.mul_left
      â†‘(nnnorm (e.symm : (fin (finrank â„ E) â†’ â„) â†’L[â„] E))) (Î» i, _),
    simpa using (e.symm : (fin (finrank â„ E) â†’ â„) â†’L[â„] E).le_op_norm (e $ f i) },
  unfreezingI { clear_dependent E },
  -- Now we deal with `g : Î± â†’ fin N â†’ â„`
  intros N g hg,
  have : âˆ€ i, summable (Î» x, âˆ¥g x iâˆ¥) := Î» i, (pi.summable.1 hg i).abs,
  refine summable_of_norm_bounded _ (summable_sum (Î» i (hi : i âˆˆ finset.univ), this i)) (Î» x, _),
  rw [norm_norm, pi_norm_le_iff],
  { refine Î» i, finset.single_le_sum (Î» i hi, _) (finset.mem_univ i),
    exact norm_nonneg (g x i) },
  { exact finset.sum_nonneg (Î» _ _, norm_nonneg _) }
end

lemma summable_of_is_O' {Î¹ E F : Type*} [normed_group E] [complete_space E] [normed_group F]
  [normed_space â„ F] [finite_dimensional â„ F] {f : Î¹ â†’ E} {g : Î¹ â†’ F}
  (hg : summable g) (h : is_O f g cofinite) : summable f :=
summable_of_is_O (summable_norm_iff.mpr hg) h.norm_right

lemma summable_of_is_O_nat' {E F : Type*} [normed_group E] [complete_space E] [normed_group F]
  [normed_space â„ F] [finite_dimensional â„ F] {f : â„• â†’ E} {g : â„• â†’ F}
  (hg : summable g) (h : is_O f g at_top) : summable f :=
summable_of_is_O_nat (summable_norm_iff.mpr hg) h.norm_right

lemma summable_of_is_equivalent {Î¹ E : Type*} [normed_group E] [normed_space â„ E]
  [finite_dimensional â„ E] {f : Î¹ â†’ E} {g : Î¹ â†’ E}
  (hg : summable g) (h : f ~[cofinite] g) : summable f :=
hg.trans_sub (summable_of_is_O' hg h.is_o.is_O)

lemma summable_of_is_equivalent_nat {E : Type*} [normed_group E] [normed_space â„ E]
  [finite_dimensional â„ E] {f : â„• â†’ E} {g : â„• â†’ E}
  (hg : summable g) (h : f ~[at_top] g) : summable f :=
hg.trans_sub (summable_of_is_O_nat' hg h.is_o.is_O)

lemma is_equivalent.summable_iff {Î¹ E : Type*} [normed_group E] [normed_space â„ E]
  [finite_dimensional â„ E] {f : Î¹ â†’ E} {g : Î¹ â†’ E}
  (h : f ~[cofinite] g) : summable f â†” summable g :=
âŸ¨Î» hf, summable_of_is_equivalent hf h.symm, Î» hg, summable_of_is_equivalent hg hâŸ©

lemma is_equivalent.summable_iff_nat {E : Type*} [normed_group E] [normed_space â„ E]
  [finite_dimensional â„ E] {f : â„• â†’ E} {g : â„• â†’ E}
  (h : f ~[at_top] g) : summable f â†” summable g :=
âŸ¨Î» hf, summable_of_is_equivalent_nat hf h.symm, Î» hg, summable_of_is_equivalent_nat hg hâŸ©
