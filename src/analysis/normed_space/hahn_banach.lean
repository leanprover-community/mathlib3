/-
Copyright (c) 2020 Yury Kudryashov All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth
-/
import analysis.normed_space.operator_norm
import analysis.normed_space.extend
import analysis.convex.cone
import analysis.convex.topology
import analysis.specific_limits
import data.complex.is_R_or_C

/-!
# Hahn-Banach theorem

In this file we prove a version of Hahn-Banach theorem for continuous linear
functions on normed spaces over `â„` and `â„‚`.

In order to state and prove its corollaries uniformly, we prove the statements for a field `ğ•œ`
satisfying `is_R_or_C ğ•œ`.

In this setting, `exists_dual_vector` states that, for any nonzero `x`, there exists a continuous
linear form `g` of norm `1` with `g x = âˆ¥xâˆ¥` (where the norm has to be interpreted as an element
of `ğ•œ`).

-/

universes u v

/--
The norm of `x` as an element of `ğ•œ` (a normed algebra over `â„`). This is needed in particular to
state equalities of the form `g x = norm' ğ•œ x` when `g` is a linear function.

For the concrete cases of `â„` and `â„‚`, this is just `âˆ¥xâˆ¥` and `â†‘âˆ¥xâˆ¥`, respectively.
-/
noncomputable def norm' (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ]
  {E : Type*} [normed_group E] (x : E) : ğ•œ :=
algebra_map â„ ğ•œ âˆ¥xâˆ¥

lemma norm'_def (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ]
  {E : Type*} [normed_group E] (x : E) :
  norm' ğ•œ x = (algebra_map â„ ğ•œ âˆ¥xâˆ¥) := rfl

lemma norm_norm'
  (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ]
  (A : Type*) [normed_group A]
  (x : A) : âˆ¥norm' ğ•œ xâˆ¥ = âˆ¥xâˆ¥ :=
by rw [norm'_def, norm_algebra_map_eq, norm_norm]

namespace real
variables {E : Type*} [normed_group E] [normed_space â„ E]

/-- Hahn-Banach theorem for continuous linear functions over `â„`. -/
theorem exists_extension_norm_eq (p : subspace â„ E) (f : p â†’L[â„] â„) :
  âˆƒ g : E â†’L[â„] â„, (âˆ€ x : p, g x = f x) âˆ§ âˆ¥gâˆ¥ = âˆ¥fâˆ¥ :=
begin
  rcases exists_extension_of_le_sublinear âŸ¨p, fâŸ© (Î» x, âˆ¥fâˆ¥ * âˆ¥xâˆ¥)
    (Î» c hc x, by simp only [norm_smul c x, real.norm_eq_abs, abs_of_pos hc, mul_left_comm])
    (Î» x y, _) (Î» x, le_trans (le_abs_self _) (f.le_op_norm _))
    with âŸ¨g, g_eq, g_leâŸ©,
  set g' := g.mk_continuous (âˆ¥fâˆ¥)
    (Î» x, abs_le.2 âŸ¨neg_le.1 $ g.map_neg x â–¸ norm_neg x â–¸ g_le (-x), g_le xâŸ©),
  { refine âŸ¨g', g_eq, _âŸ©,
    { apply le_antisymm (g.mk_continuous_norm_le (norm_nonneg f) _),
      refine f.op_norm_le_bound (norm_nonneg _) (Î» x, _),
      dsimp at g_eq,
      rw â† g_eq,
      apply g'.le_op_norm } },
  { simp only [â† mul_add],
    exact mul_le_mul_of_nonneg_left (norm_add_le x y) (norm_nonneg f) }
end

end real

section is_R_or_C
open is_R_or_C

variables {ğ•œ : Type*} [is_R_or_C ğ•œ] {F : Type*} [normed_group F] [normed_space ğ•œ F]

/-- Hahn-Banach theorem for continuous linear functions over `ğ•œ` satisyfing `is_R_or_C ğ•œ`. -/
theorem exists_extension_norm_eq (p : subspace ğ•œ F) (f : p â†’L[ğ•œ] ğ•œ) :
  âˆƒ g : F â†’L[ğ•œ] ğ•œ, (âˆ€ x : p, g x = f x) âˆ§ âˆ¥gâˆ¥ = âˆ¥fâˆ¥ :=
begin
  letI : module â„ F := restrict_scalars.semimodule â„ ğ•œ F,
  letI : is_scalar_tower â„ ğ•œ F := restrict_scalars.is_scalar_tower _ _ _,
  letI : normed_space â„ F := normed_space.restrict_scalars _ ğ•œ _,
  -- Let `fr: p â†’L[â„] â„` be the real part of `f`.
  let fr := re_clm.comp (f.restrict_scalars â„),
  have fr_apply : âˆ€ x, fr x = re (f x), by { assume x, refl },
  -- Use the real version to get a norm-preserving extension of `fr`, which
  -- we'll call `g : F â†’L[â„] â„`.
  rcases real.exists_extension_norm_eq (p.restrict_scalars â„) fr with âŸ¨g, âŸ¨hextends, hnormeqâŸ©âŸ©,
  -- Now `g` can be extended to the `F â†’L[ğ•œ] ğ•œ` we need.
  use g.extend_to_ğ•œ,
  -- It is an extension of `f`.
  have h : âˆ€ x : p, g.extend_to_ğ•œ x = f x,
  { assume x,
    rw [continuous_linear_map.extend_to_ğ•œ_apply, â†submodule.coe_smul, hextends, hextends],
    have : (fr x : ğ•œ) - I * â†‘(fr (I â€¢ x)) = (re (f x) : ğ•œ) - (I : ğ•œ) * (re (f ((I : ğ•œ) â€¢ x))),
      by refl,
    rw this,
    apply ext,
    { simp only [add_zero, algebra.id.smul_eq_mul, I_re, of_real_im, add_monoid_hom.map_add,
        zero_sub, I_im', zero_mul, of_real_re, eq_self_iff_true, sub_zero, mul_neg_eq_neg_mul_symm,
        of_real_neg, mul_re, mul_zero, sub_neg_eq_add, continuous_linear_map.map_smul] },
    { simp only [algebra.id.smul_eq_mul, I_re, of_real_im, add_monoid_hom.map_add, zero_sub, I_im',
        zero_mul, of_real_re, mul_neg_eq_neg_mul_symm, mul_im, zero_add, of_real_neg, mul_re,
        sub_neg_eq_add, continuous_linear_map.map_smul] } },
  refine âŸ¨h, _âŸ©,
  -- And we derive the equality of the norms by bounding on both sides.
  refine le_antisymm _ _,
  { calc âˆ¥g.extend_to_ğ•œâˆ¥
        â‰¤ âˆ¥gâˆ¥ : g.extend_to_ğ•œ.op_norm_le_bound g.op_norm_nonneg (norm_bound _)
    ... = âˆ¥frâˆ¥ : hnormeq
    ... â‰¤ âˆ¥re_clmâˆ¥ * âˆ¥fâˆ¥ : continuous_linear_map.op_norm_comp_le _ _
    ... = âˆ¥fâˆ¥ : by rw [re_clm_norm, one_mul] },
  { exact f.op_norm_le_bound g.extend_to_ğ•œ.op_norm_nonneg (Î» x, h x â–¸ g.extend_to_ğ•œ.le_op_norm x) },
end

end is_R_or_C

section dual_vector
variables {ğ•œ : Type v} [is_R_or_C ğ•œ]
variables {E : Type u} [normed_group E] [normed_space ğ•œ E]

open continuous_linear_equiv submodule
open_locale classical

lemma coord_norm' (x : E) (h : x â‰  0) :
  âˆ¥norm' ğ•œ x â€¢ coord ğ•œ x hâˆ¥ = 1 :=
by rw [norm_smul, norm_norm', coord_norm, mul_inv_cancel (mt norm_eq_zero.mp h)]

/-- Corollary of Hahn-Banach.  Given a nonzero element `x` of a normed space, there exists an
    element of the dual space, of norm `1`, whose value on `x` is `âˆ¥xâˆ¥`. -/
theorem exists_dual_vector (x : E) (h : x â‰  0) :
  âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g x = norm' ğ•œ x :=
begin
  let p : submodule ğ•œ E := ğ•œ âˆ™ x,
  let f := norm' ğ•œ x â€¢ coord ğ•œ x h,
  obtain âŸ¨g, hgâŸ© := exists_extension_norm_eq p f,
  use g, split,
  { rw [hg.2, coord_norm'] },
  { calc g x = g (âŸ¨x, mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) : by rw coe_mk
    ... = (norm' ğ•œ x â€¢ coord ğ•œ x h) (âŸ¨x, mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) : by rw â† hg.1
    ... = norm' ğ•œ x : by simp }
end

/-- Variant of Hahn-Banach, eliminating the hypothesis that `x` be nonzero, and choosing
    the dual element arbitrarily when `x = 0`. -/
theorem exists_dual_vector' [nontrivial E] (x : E) :
  âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g x = norm' ğ•œ x :=
begin
  by_cases hx : x = 0,
  { obtain âŸ¨y, hyâŸ© := exists_ne (0 : E),
    obtain âŸ¨g, hgâŸ© : âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g y = norm' ğ•œ y := exists_dual_vector y hy,
    refine âŸ¨g, hg.left, _âŸ©,
    rw [norm'_def, hx, norm_zero, ring_hom.map_zero, continuous_linear_map.map_zero] },
  { exact exists_dual_vector x hx }
end

end dual_vector

section separating
open set

noncomputable theory
variables {E : Type*} [normed_group E] [normed_space â„ E]

lemma mem_sep_true {Î± : Type*} (s : set Î±) :
  {a âˆˆ s | true} = s :=
by { ext; simp }

lemma mem_sep_false {Î± : Type*} (s : set Î±) :
  {a âˆˆ s | false} = âˆ… :=
by { ext; simp }

lemma real.zero_le_Inf (S : set â„) (hS : âˆ€ x âˆˆ S, (0:â„) â‰¤ x) :
  0 â‰¤ Inf S :=
begin
  rcases S.eq_empty_or_nonempty with (rfl | hSâ‚‚),
  { simp [real.Inf_empty] },
  { apply real.lb_le_Inf S hSâ‚‚ hS }
end

lemma Inf_le_of_forall_lt (S : set â„) (hS : bdd_below S) (y : â„)
  (h : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ Î´, Î´ < Îµ âˆ§ y + Î´ âˆˆ S) :
  Inf S â‰¤ y :=
begin
  apply le_of_forall_pos_lt_add,
  intros Îµ hÎµ,
  obtain âŸ¨Î´, hÎ´â‚, hÎ´â‚‚âŸ© := h Îµ hÎµ,
  exact cInf_lt_of_lt hS hÎ´â‚‚ (add_lt_add_left hÎ´â‚ _),
end

@[simp]
lemma Inf_Ioi {Î± : Type*} (x : Î±) [conditionally_complete_lattice Î±] [no_top_order Î±]
  [densely_ordered Î±] :
  Inf (set.Ioi x) = x :=
cInf_intro nonempty_Ioi (Î» a ha, le_of_lt ha) (Î» w hw, by simpa using exists_between hw)

def gauge (K : set E) (x : E) :
  â„ :=
Inf {y âˆˆ set.Ioi 0 | yâ»Â¹ â€¢ x âˆˆ K}

lemma gauge_set_nonempty_of_absorbing {K : set E} (absorbing : âˆ€ x, âˆƒ (Î¸ : â„), 0 < Î¸ âˆ§ Î¸ â€¢ x âˆˆ K)
  {x : E} :
  {y âˆˆ set.Ioi (0:â„) | yâ»Â¹ â€¢ x âˆˆ K}.nonempty :=
let âŸ¨Î¸, hÎ¸â‚, hÎ¸â‚‚âŸ© := absorbing x in âŸ¨Î¸â»Â¹, inv_pos.2 hÎ¸â‚, by simpaâŸ©

lemma gauge_set_bdd_below {K : set E} {x : E} :
  bdd_below {y âˆˆ set.Ioi (0:â„) | yâ»Â¹ â€¢ x âˆˆ K} :=
âŸ¨0, Î» y hy, le_of_lt hy.1âŸ©

@[simp]
lemma gauge_zero {K : set E} :
  gauge K 0 = 0 :=
begin
  rw gauge,
  by_cases (0:E) âˆˆ K,
  { simp [h, mem_sep_true] },
  { simp [h, mem_sep_false, real.Inf_empty] }
end

lemma smul_mem_of_convex {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K) {x : E}
  {Î¸ : â„} (hÎ¸â‚ : 0 â‰¤ Î¸) (hÎ¸â‚‚ : Î¸ â‰¤ 1)
  (hx : x âˆˆ K) : Î¸ â€¢ x âˆˆ K :=
begin
  have := hK.segment_subset zero_mem hx,
  rw segment_eq_image at this,
  apply this âŸ¨_, âŸ¨â€¹0 â‰¤ Î¸â€º, â€¹_â€ºâŸ©, by simpâŸ©,
end

lemma zero_le_gauge {K : set E} (x : E) :
  0 â‰¤ gauge K x :=
real.zero_le_Inf _ (Î» x hx, le_of_lt hx.1)

lemma gauge_le_one_eq {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K)
  (absorbing : âˆ€ x, âˆƒ (Î¸ : â„), 0 < Î¸ âˆ§ Î¸ â€¢ x âˆˆ K) :
  {x | gauge K x â‰¤ 1} = â‹‚ (Î¸ âˆˆ set.Ioi (1:â„)), Î¸ â€¢ K :=
begin
  ext,
  simp only [set.mem_Ioi, set.mem_Inter, set.mem_set_of_eq],
  split,
  { intros h Î¸ hÎ¸,
    rw mem_smul_set_iff_inv_smul_mem (show Î¸ â‰  0, by linarith),
    rcases exists_lt_of_cInf_lt _ (lt_of_le_of_lt h hÎ¸) with âŸ¨Î´, âŸ¨hÎ´â‚, hÎ´â‚‚âŸ©, _âŸ©,
    { suffices : (Î¸â»Â¹ * Î´) â€¢ Î´â»Â¹ â€¢ x âˆˆ K,
      { rwa [smul_smul, mul_inv_cancel_right' â€¹0 < Î´â€º.ne'] at this },
      apply smul_mem_of_convex hK zero_mem _ _ hÎ´â‚‚,
      { refine mul_nonneg (inv_nonneg.2 (by linarith)) (le_of_lt hÎ´â‚), },
      { rw [inv_mul_le_iff (lt_trans â€¹0 < Î´â€º â€¹Î´ < Î¸â€º), mul_one],
        apply â€¹Î´ < Î¸â€º.le } },
    apply gauge_set_nonempty_of_absorbing absorbing },
  { intro h,
    apply Inf_le_of_forall_lt _ gauge_set_bdd_below,
    intros Îµ hÎµ,
    refine âŸ¨Îµ/2, by linarith, show 0 < 1 + Îµ/2, by linarith, _âŸ©,
    change _ âˆˆ _,
    rw â†mem_smul_set_iff_inv_smul_mem (show 1 + Îµ/2 â‰  0, by linarith),
    apply h,
    linarith }
end

lemma gauge_lt_one_eq {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K)
  (absorbing : âˆ€ x, âˆƒ (Î¸ : â„), 0 < Î¸ âˆ§ Î¸ â€¢ x âˆˆ K) :
  {x | gauge K x < 1} = â‹ƒ (Î¸ âˆˆ set.Ioo 0 (1:â„)), Î¸ â€¢ K :=
begin
  ext,
  simp only [exists_prop, set.mem_Union, set.mem_Ioi, set.mem_set_of_eq],
  split,
  { intro h,
    obtain âŸ¨Î¸, âŸ¨_, _âŸ©, _âŸ© := exists_lt_of_cInf_lt (gauge_set_nonempty_of_absorbing absorbing) h,
    refine âŸ¨Î¸, âŸ¨â€¹_â€º, â€¹Î¸ < 1â€ºâŸ©, by rwa mem_smul_set_iff_inv_smul_mem (ne_of_gt â€¹0 < Î¸â€º)âŸ© },
  { rintro âŸ¨Î¸, âŸ¨_, _âŸ©, _âŸ©,
    apply cInf_lt_of_lt gauge_set_bdd_below âŸ¨â€¹0 < Î¸â€º, _âŸ© â€¹Î¸ < 1â€º,
    change _ âˆˆ _,
    rwa â†mem_smul_set_iff_inv_smul_mem (ne_of_gt â€¹0 < Î¸â€º) }
end

lemma gauge_lt_one_subset_self {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K)
  (absorbing : âˆ€ x, âˆƒ (Î¸ : â„), 0 < Î¸ âˆ§ Î¸ â€¢ x âˆˆ K) :
  {x | gauge K x < 1} âŠ† K :=
begin
  rw gauge_lt_one_eq hK zero_mem absorbing,
  apply set.bUnion_subset,
  intros Î¸ hÎ¸,
  rintro _ âŸ¨y, hy, rflâŸ©,
  rw convex_iff_segment_subset at hK,
  simp_rw segment_eq_image at hK,
  apply hK zero_mem hy âŸ¨Î¸, Ioo_subset_Icc_self hÎ¸, _âŸ©,
  simp,
end

lemma gauge_le_one_convex {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K)
  (absorbing : âˆ€ x, âˆƒ (Î¸ : â„), 0 < Î¸ âˆ§ Î¸ â€¢ x âˆˆ K) :
  convex {x | gauge K x â‰¤ 1} :=
begin
  rw gauge_le_one_eq hK zero_mem absorbing,
  refine convex_Inter (Î» i, convex_Inter (Î» (hi : _ < _), convex.smul _ hK)),
end

-- lemma mem_inv_smul_set_iff [field Î±] [mul_action Î± Î²] {a : Î±} (ha : a â‰  0) (A : set Î²) (x : Î²) :
--   x âˆˆ aâ»Â¹ â€¢ A â†” a â€¢ x âˆˆ A :=
-- by simp only [â† image_smul, mem_image, inv_smul_eq_iff' ha, exists_eq_right]

-- lemma mem_smul_set_iff_inv_smul_mem [field Î±] [mul_action Î± Î²] {a : Î±} (ha : a â‰  0) (A : set Î²)
--   (x : Î²) : x âˆˆ a â€¢ A â†” aâ»Â¹ â€¢ x âˆˆ A :=
-- by rw [â† mem_inv_smul_set_iff $ inv_ne_zero ha, inv_inv'

lemma gauge_le_one_of_mem {K : set E} (x : E) (hx : x âˆˆ K) : gauge K x â‰¤ 1 :=
real.Inf_le _ âŸ¨0, Î» y hy, le_of_lt hy.1âŸ© âŸ¨by norm_num, by simpaâŸ©

lemma gauge_le_of_mem {K : set E} (x : E) {Î¸ : â„} (hÎ¸ : 0 < Î¸) (hx : Î¸â»Â¹ â€¢ x âˆˆ K) : gauge K x â‰¤ Î¸ :=
cInf_le gauge_set_bdd_below âŸ¨hÎ¸, hxâŸ©

lemma convex_open_zero_mem_is_absorbing {C : set E} (zero_mem : (0:E) âˆˆ C) (hC : convex C)
  (hCâ‚‚ : is_open C) :
  âˆ€ (x : E), âˆƒ (Î¸:â„), 0 < Î¸ âˆ§ Î¸ â€¢ x âˆˆ C :=
begin
  intro x,
  let F : â„ â†’ E := Î» t, t â€¢ x,
  have : continuous F,
  { continuity },
  let C' := preimage F C,
  have : is_open C' := this.is_open_preimage _ hCâ‚‚,
  have zero_mem : (0:â„) âˆˆ C',
  { change _ â€¢ _ âˆˆ C,
    simpa },
  rw metric.is_open_iff at this,
  obtain âŸ¨Îµ, hÎµâ‚, hÎµâ‚‚âŸ© := this 0 zero_mem,
  refine âŸ¨_, half_pos hÎµâ‚, _âŸ©,
  change Îµ / 2 âˆˆ C',
  apply hÎµâ‚‚,
  simp only [metric.mem_ball, real.dist_0_eq_abs, abs_of_pos (half_pos hÎµâ‚), half_lt_self hÎµâ‚],
end

lemma gauge_lt_one_eq_self_of_open {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K)
  (hCâ‚‚ : is_open K) :
  {x | gauge K x < 1} = K :=
begin
  apply set.subset.antisymm,
  { apply gauge_lt_one_subset_self hK â€¹_â€º (convex_open_zero_mem_is_absorbing zero_mem hK hCâ‚‚) },
  intros x hx,
  let F : â„ â†’ E := Î» t, t â€¢ x,
  have : continuous F,
  { continuity },
  let K' := preimage F K,
  have : is_open K' := this.is_open_preimage _ hCâ‚‚,
  have one_mem : (1:â„) âˆˆ K',
  { change _ â€¢ _ âˆˆ K,
    simpa },
  obtain âŸ¨Îµ, _, hÎµâ‚‚âŸ© := (metric.nhds_basis_closed_ball.1 _).1 (is_open_iff_mem_nhds.1 this 1 â€¹_â€º),
  rw closed_ball_Icc at hÎµâ‚‚,
  have : (1 + Îµ)â»Â¹ < 1,
  { rw inv_lt_one_iff,
    right,
    linarith },
  refine cInf_lt_of_lt gauge_set_bdd_below âŸ¨_, _âŸ© â€¹(1 + Îµ)â»Â¹ < 1â€º,
  { change (0:â„) < _,
    rw inv_pos,
    linarith },
  change _ âˆˆ _,
  rw inv_inv',
  change _ âˆˆ K',
  apply hÎµâ‚‚,
  simp;
  linarith
end

lemma gauge_lt_one_of_mem_of_open {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K)
  (hKâ‚‚ : is_open K) (x : E) (hx : x âˆˆ K) :
  gauge K x < 1 :=
by rwa â†gauge_lt_one_eq_self_of_open hK zero_mem hKâ‚‚ at hx

lemma one_le_gauge_of_not_mem {K : set E} (hK : convex K) (zero_mem : (0:E) âˆˆ K)
  (hKâ‚‚ : is_open K) (x : E) (hx : x âˆ‰ K) :
  1 â‰¤ gauge K x :=
begin
  rw â†gauge_lt_one_eq_self_of_open hK zero_mem hKâ‚‚ at hx,
  exact le_of_not_lt hx
end


lemma Inf_smul (K : set â„) {Î¸ : â„} (hÎ¸ : 0 â‰¤ Î¸) :
  Î¸ * Inf K = Inf (Î¸ â€¢ K) :=
begin
  cases K.eq_empty_or_nonempty,
  { subst h,
    simp [real.Inf_empty] },
  by_cases hâ‚ : bdd_below K,
  { have : monotone (Î» x, (Î¸:â„) * x),
    { exact monotone_mul_left_of_nonneg hÎ¸ },
    have z := map_cInf_of_continuous_at_of_monotone (continuous_mul_left Î¸).continuous_at
                  (monotone_mul_left_of_nonneg hÎ¸) â€¹_â€º â€¹_â€º,
    dsimp at z,
    rw [z, â†set.image_smul],
    refl },
  { rw [real.Inf_of_not_bdd_below hâ‚, mul_zero],
    rcases eq_or_lt_of_le hÎ¸ with (rfl | hÎ¸),
    { rw zero_smul_set h,
      have : (0 : set â„) = {0},
      { ext, simp },
      rw this,
      simp only [cInf_singleton] },
    { rw real.Inf_of_not_bdd_below,
      rintro âŸ¨t, htâŸ©,
      apply hâ‚,
      refine âŸ¨t / Î¸, Î» z hz, _âŸ©,
      rw div_le_iff hÎ¸,
      apply ht,
      rw mul_comm,
      exact âŸ¨_, hz, smul_eq_mul _âŸ© } },
end

lemma gauge_neg {K : set E} (balanced : âˆ€ x âˆˆ K, -x âˆˆ K) (x : E) :
  gauge K (-x) = gauge K x :=
begin
  have : âˆ€ x, -x âˆˆ K â†” x âˆˆ K := Î» x, âŸ¨Î» h, by simpa using balanced _ h, balanced xâŸ©,
  change Inf _ = Inf _,
  simp_rw [smul_neg, this],
end

lemma gauge_mul_nonneg {K : set E}
  {Î¸ : â„} (hÎ¸ : 0 â‰¤ Î¸) (x : E) :
gauge K (Î¸ â€¢ x) = Î¸ * gauge K x :=
begin
  rcases eq_or_lt_of_le hÎ¸ with (rfl | hÎ¸'),
  { simp },
  change Inf _ = _ * Inf _,
  rw Inf_smul _ â€¹0 â‰¤ Î¸â€º,
  congr' 1,
  ext Î²,
  simp only [set.mem_smul_set, set.mem_sep_eq, smul_eq_mul, set.mem_Ioi],
  split,
  { rintro âŸ¨hÎ²â‚, hÎ²â‚‚âŸ©,
    refine âŸ¨Î² * Î¸â»Â¹, âŸ¨mul_pos â€¹0 < Î²â€º (inv_pos.2 â€¹0 < Î¸â€º), _âŸ©, _âŸ©,
    rwa [mul_inv', inv_inv', mul_smul],
    rw [mul_left_comm, mul_inv_cancel (ne_of_gt â€¹0 < Î¸â€º), mul_one] },
  { rintro âŸ¨Î², âŸ¨_, _âŸ©, rflâŸ©,
    refine âŸ¨mul_pos â€¹0 < Î¸â€º â€¹0 < Î²â€º, _âŸ©,
    rwa [mul_inv_rev', â†mul_smul, mul_assoc, inv_mul_cancel (ne_of_gt â€¹0 < Î¸â€º), mul_one] }
end

lemma gauge_homogeneous {K : set E} (balanced : âˆ€ x âˆˆ K, -x âˆˆ K)
  (Î¸ : â„) (x : E) :
  gauge K (Î¸ â€¢ x) = abs Î¸ * gauge K x :=
begin
  rw â†gauge_mul_nonneg (abs_nonneg Î¸),
  cases le_total 0 Î¸,
  { rw abs_of_nonneg h },
  { rw [abs_of_nonpos h, neg_smul, gauge_neg balanced] }
end

lemma gauge_subadditive {K : set E} (hK : convex K)
  (absorbing : âˆ€ x, âˆƒ (Î¸ : â„), 0 < Î¸ âˆ§ Î¸ â€¢ x âˆˆ K) (x y : E) :
  gauge K (x + y) â‰¤ gauge K x + gauge K y :=
begin
  apply le_of_forall_pos_lt_add,
  intros Îµ hÎµ,
  obtain âŸ¨a, âŸ¨haâ‚ : _ < _, haâ‚‚âŸ©, haâ‚ƒ : _ < gauge _ _ + _âŸ© :=
    exists_lt_of_cInf_lt (gauge_set_nonempty_of_absorbing absorbing)
      (lt_add_of_pos_right (gauge K x) (half_pos hÎµ)),
  obtain âŸ¨b, âŸ¨hbâ‚ : _ < _, hbâ‚‚âŸ©, hbâ‚ƒ : _ < gauge _ _ + _âŸ© :=
    exists_lt_of_cInf_lt (gauge_set_nonempty_of_absorbing absorbing)
      (lt_add_of_pos_right (gauge K y) (half_pos hÎµ)),
  suffices : gauge K (x + y) â‰¤ a + b,
  { linarith },
  rw convex_iff_div at hK,
  have := hK haâ‚‚ hbâ‚‚ (le_of_lt haâ‚) (le_of_lt hbâ‚) (by linarith),
  rw [smul_smul, smul_smul, mul_comm_div', mul_comm_div', â†mul_div_assoc, â†mul_div_assoc,
    mul_inv_cancel (ne_of_gt haâ‚), mul_inv_cancel (ne_of_gt hbâ‚), â†smul_add] at this,
  apply gauge_le_of_mem,
  { linarith },
  simpa,
end

theorem zorn_reverse_subset {Î± : Type u} (S : set (set Î±))
  (h : âˆ€c âŠ† S, zorn.chain (âŠ†) c â†’ âˆƒlb âˆˆ S, âˆ€ s âˆˆ c, lb âŠ† s) :
  âˆƒ m âˆˆ S, âˆ€a âˆˆ S, a âŠ† m â†’ a = m :=
begin
  let rev : S â†’ S â†’ Prop := Î» X Y, Y.1 âŠ† X.1,
  have hS : âˆ€ (c : set S), zorn.chain rev c â†’ âˆƒ ub, âˆ€ a âˆˆ c, rev a ub,
  { intros c hc,
    obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := h (coe '' c) (by simp)
      (by { rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© ne,
            apply (hc _ hx _ hy (Î» t, ne (congr_arg coe t))).symm }),
    exact âŸ¨âŸ¨_, htâ‚âŸ©, Î» a ha, htâ‚‚ a âŸ¨_, ha, rflâŸ©âŸ© },
  obtain âŸ¨m, hmâ‚âŸ© := zorn.exists_maximal_of_chains_bounded hS _,
  { refine âŸ¨m, m.prop, Î» a ha haâ‚‚, set.subset.antisymm haâ‚‚ (hmâ‚ âŸ¨a, haâŸ© haâ‚‚)âŸ© },
  intros x y z xy yz,
  apply set.subset.trans yz xy
end

lemma continuous_linear_map_of_continuous_at_zero {E F : Type*} [normed_group E] [normed_space â„ E]
  [normed_group F] [normed_space â„ F] (f : E â†’â‚—[â„] F) (hf : continuous_at f (0:E)) :
  continuous f :=
begin
  have : filter.tendsto f (nhds 0) (nhds 0), by simpa using hf.tendsto,
  exact (uniform_continuous_of_tendsto_zero this).continuous,
end

lemma continuous_at_of_exists_open {E : Type*} [normed_group E] [normed_space â„ E]
  (f : E â†’â‚—[â„] â„) (hf : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ (U : set E), (0:E) âˆˆ U âˆ§ is_open U âˆ§ âˆ€ x âˆˆ U, âˆ¥f xâˆ¥ < Îµ) :
  continuous_at f (0:E) :=
begin
  intros U hU,
  rw metric.nhds_basis_ball.1 at hU,
  rcases hU with âŸ¨Îµ, hÎµâ‚, hÎµâ‚‚âŸ©,
  simp only [linear_map.map_zero] at hÎµâ‚‚,
  simp only [filter.mem_map],
  obtain âŸ¨V, hVâ‚, hVâ‚‚, hVâ‚ƒâŸ© := hf Îµ hÎµâ‚,
  rw mem_nhds_sets_iff,
  refine âŸ¨V, Î» x hx, hÎµâ‚‚ _, hVâ‚‚, hVâ‚âŸ©,
  simp only [metric.mem_ball, dist_zero_right],
  apply hVâ‚ƒ _ hx,
end

/--
Given a set `C` which is a convex neighbourhood of `0` and a point `xâ‚€` outside of it, there is a
continuous linear functional `f` which sends `xâ‚€` to 1 and all of `C` to values strictly below 1.
-/
lemma separate_convex_open_set {C : set E} (zero_mem : (0:E) âˆˆ C) (hC : convex C) (hCâ‚‚ : is_open C)
  (xâ‚€ : E) (hxâ‚€ : xâ‚€ âˆ‰ C) :
âˆƒ (f : E â†’L[â„] â„), f xâ‚€ = 1 âˆ§ âˆ€ x âˆˆ C, f x < 1 :=
begin
  let f : linear_pmap â„ E â„ :=
    linear_pmap.mk_span_singleton xâ‚€ 1 (ne_of_mem_of_not_mem zero_mem hxâ‚€).symm,
  have : f âŸ¨(1:â„) â€¢ xâ‚€, by { dsimp, rw submodule.mem_span_singleton, refine âŸ¨1, rflâŸ© }âŸ© = 1,
  { change linear_pmap.mk_span_singleton _ _ _ _ = _,
    rw linear_pmap.mk_span_singleton_apply,
    simp },
  rcases exists_extension_of_le_sublinear f (gauge C) _ _ _ with âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ©,
  { refine âŸ¨âŸ¨Ï†, _âŸ©, _, _âŸ©,
    { apply continuous_linear_map_of_continuous_at_zero,
      apply continuous_at_of_exists_open,
      intros Îµ hÎµ,
      refine âŸ¨(Îµ â€¢ C) âˆ© (-Îµ â€¢ C), âŸ¨_, _âŸ©, _, _âŸ©,
      { rw mem_smul_set,
        refine âŸ¨0, zero_mem, by simpâŸ© },
      { rw mem_smul_set,
        refine âŸ¨0, zero_mem, by simpâŸ© },
      { apply is_open_inter,
        { exact is_open_map_smul' hÎµ.ne' _ hCâ‚‚ },
        { exact is_open_map_smul' (by linarith) _ hCâ‚‚ } },
      { rintro x âŸ¨hxâ‚, hxâ‚‚âŸ©,
        rw [real.norm_eq_abs, abs_lt],
        split,
        { rw [neg_lt, â†linear_map.map_neg],
          apply (hÏ†â‚‚ _).trans_lt,
          have : -Îµâ»Â¹ â€¢ x âˆˆ C,
          { obtain âŸ¨y, _, rflâŸ© := hxâ‚‚,
            simpa [smul_smul, hÎµ.ne'] },
          have := gauge_lt_one_of_mem_of_open hC zero_mem hCâ‚‚ (-Îµâ»Â¹ â€¢ x) â€¹_ âˆˆ Câ€º,
          simpa [â†smul_neg, gauge_mul_nonneg (inv_nonneg.2 hÎµ.le), inv_mul_lt_iff hÎµ] using this },
        { have : Îµâ»Â¹ â€¢ x âˆˆ C,
          { rwa â†mem_smul_set_iff_inv_smul_mem hÎµ.ne' },
          have := gauge_lt_one_of_mem_of_open hC zero_mem hCâ‚‚ (Îµâ»Â¹ â€¢ x) â€¹_â€º,
          rw [gauge_mul_nonneg (inv_nonneg.2 hÎµ.le), inv_mul_lt_iff hÎµ, mul_one] at this,
          apply (hÏ†â‚‚ _).trans_lt â€¹_â€º } } },
    { dsimp,
      rw [â†â€¹f âŸ¨_, _âŸ© = 1â€º, â†hÏ†â‚],
      simp, },
    { intros x hx,
      apply (hÏ†â‚‚ x).trans_lt (gauge_lt_one_of_mem_of_open hC zero_mem hCâ‚‚ _ hx) } },
  { intros c hc x,
    apply gauge_mul_nonneg (le_of_lt hc) },
  { intros x y,
    apply gauge_subadditive hC (convex_open_zero_mem_is_absorbing zero_mem hC hCâ‚‚) },
  { rintro âŸ¨x, hxâŸ©,
    obtain âŸ¨y, rflâŸ© := submodule.mem_span_singleton.1 hx,
    rw linear_pmap.mk_span_singleton_apply,
    simp only [mul_one, algebra.id.smul_eq_mul, submodule.coe_mk],
    cases lt_or_le 0 y,
    { rw [gauge_mul_nonneg (le_of_lt h), le_mul_iff_one_le_right h],
      apply one_le_gauge_of_not_mem hC â€¹_â€º â€¹_â€º _ hxâ‚€ },
    apply â€¹y â‰¤ 0â€º.trans (zero_le_gauge _) }
end

/-- A nonzero continuous linear functional is open. -/
lemma nonzero_linear_map_is_open_map (f : E â†’L[â„] â„) (hf : f â‰  0) :
  is_open_map f :=
begin
  have : âˆƒ xâ‚€, f xâ‚€ â‰  0,
  { by_contra h,
    push_neg at h,
    apply hf,
    ext,
    simp [h] },
  obtain âŸ¨xâ‚, hxâ‚âŸ© : âˆƒ xâ‚, f xâ‚ = 1,
  { rcases this with âŸ¨xâ‚€, hxâ‚€âŸ©,
    refine âŸ¨(f xâ‚€)â»Â¹ â€¢ xâ‚€, _âŸ©,
    simp [hxâ‚€] },
  intros A hA,
  rw is_open_iff_mem_nhds,
  rintro _ âŸ¨a, ha, rflâŸ©,
  let g : â„ â†’ E := Î» x, a + x â€¢ xâ‚,
  have := (show continuous g, by continuity).is_open_preimage _ â€¹is_open Aâ€º,
  rw is_open_iff_mem_nhds at this,
  specialize this 0 _,
  { change a + _ â€¢ _ âˆˆ A,
    simpa },
  rw metric.nhds_basis_ball.1 at this,
  rcases this with âŸ¨Îµ, hÎµâ‚, hÎµâ‚‚âŸ©,
  rw metric.nhds_basis_ball.1,
  refine âŸ¨Îµ, hÎµâ‚, _âŸ©,
  intros x hx,
  simp only [metric.mem_ball, real.dist_eq] at hx,
  have : x - f a âˆˆ g â»Â¹' A,
  { apply hÎµâ‚‚,
    rwa [metric.mem_ball, real.dist_0_eq_abs] },
  refine âŸ¨_, this, _âŸ©,
  simp [hxâ‚],
end

/--
A version of the Hahn-Banach theorem: given disjoint convex subsets `A,B` where `A` is open, there
is a continuous linear functional which separates them.
-/
theorem geometric_hahn_banach_open {A B : set E}
  (hAâ‚ : convex A) (hAâ‚‚ : is_open A)
  (hB : convex B)
  (disj : disjoint A B) :
  âˆƒ (f : E â†’L[â„] â„) (s : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ (âˆ€ b âˆˆ B, s â‰¤ f b) :=
begin
  rcases A.eq_empty_or_nonempty with (rfl | âŸ¨aâ‚€, haâ‚€âŸ©),
  { refine âŸ¨0, 0, by simp, Î» b hb, by simpâŸ© },
  rcases B.eq_empty_or_nonempty with (rfl | âŸ¨bâ‚€, hbâ‚€âŸ©),
  { refine âŸ¨0, 1, Î» a ha, by norm_num, by simpâŸ© },
  let xâ‚€ := bâ‚€ - aâ‚€,
  let C := {xâ‚€} + A + -B,
  have : (0:E) âˆˆ C,
  { refine âŸ¨_ + aâ‚€, -bâ‚€, add_mem_add rfl â€¹_â€º, neg_mem_neg.2 â€¹_â€º, _âŸ©,
    simp },
  have : is_open C := hAâ‚‚.add_left.add_right,
  have : convex C := ((convex_singleton _).add hAâ‚).add â€¹convex Bâ€º.neg_preimage,
  have : xâ‚€ âˆ‰ C,
  { intro hxâ‚€,
    simp only [mem_add, mem_singleton_iff, mem_neg, exists_eq_left, exists_exists_and_eq_and,
      exists_and_distrib_left, add_assoc xâ‚€, add_right_eq_self] at hxâ‚€,
    obtain âŸ¨a, ha, b, hb, _âŸ© := hxâ‚€,
    apply disj âŸ¨ha, _âŸ©,
    convert hb,
    rwa â†add_eq_zero_iff_eq_neg },
  obtain âŸ¨f, hfâ‚, hfâ‚‚âŸ© := separate_convex_open_set â€¹0 âˆˆ Câ€º â€¹_â€º â€¹_â€º _ â€¹xâ‚€ âˆ‰ Câ€º,
  have : f bâ‚€ = f aâ‚€ + 1,
  { simp [â†hfâ‚] },
  have forall_lt : âˆ€ (a âˆˆ A) (b âˆˆ B), f a < f b,
  { intros a ha b hb,
    have := hfâ‚‚ (xâ‚€ + a + -b) (add_mem_add (add_mem_add rfl ha) (neg_mem_neg.2 hb)),
    simp [â€¹f bâ‚€ = _â€º] at this,
    linarith },
  have A_le_Inf : âˆ€ a âˆˆ A, f a â‰¤ Inf (f '' B),
  { intros a ha,
    apply le_cInf âŸ¨f bâ‚€, _âŸ©,
    { rintro _ âŸ¨b', _, rflâŸ©,
      apply (forall_lt _ â€¹a âˆˆ _â€º _ â€¹b' âˆˆ _â€º).le },
    { apply mem_image_of_mem _ â€¹bâ‚€ âˆˆ Bâ€º } },
  refine âŸ¨f, Inf (f '' B), _, _âŸ©,
  { intros a ha,
    apply lt_of_le_of_ne,
    { apply A_le_Inf _ ha },
    intro same,
    let g : â„ â†’ E := Î» x, a + x â€¢ xâ‚€,
    have := (show continuous g, by continuity).is_open_preimage _ â€¹is_open Aâ€º,
    rw is_open_iff_mem_nhds at this,
    specialize this 0 _,
    { change a + _ â€¢ _ âˆˆ A,
      simpa },
    rw metric.nhds_basis_closed_ball.1 at this,
    rcases this with âŸ¨Îµ, hÎµâ‚, hÎµâ‚‚âŸ©,
    have : Îµ âˆˆ metric.closed_ball (0:â„) Îµ,
    { simp [real.norm_eq_abs, abs_of_pos hÎµâ‚], },
    have : f (_ + _) â‰¤ _ := A_le_Inf _ (hÎµâ‚‚ â€¹Îµ âˆˆ _â€º),
    rw [f.map_add] at this,
    simp only [algebra.id.smul_eq_mul, continuous_linear_map.map_smul, hfâ‚, â†same, mul_one] at this,
    linarith },
  { intros b hb,
    apply cInf_le âŸ¨f aâ‚€, _âŸ© (mem_image_of_mem _ hb),
    rintro _ âŸ¨b', hb', rflâŸ©,
    apply (forall_lt _ haâ‚€ _ hb').le },
end

theorem geometric_hahn_banach_open_point {A : set E} {x : E}
  (hAâ‚ : convex A) (hAâ‚‚ : is_open A)
  (disj : x âˆ‰ A) :
  âˆƒ (f : E â†’L[â„] â„), (âˆ€ a âˆˆ A, f a < f x) :=
let âŸ¨f, s, hA, hxâŸ© := geometric_hahn_banach_open hAâ‚ hAâ‚‚ (convex_singleton x)
  (disjoint_singleton_right.2 disj)
  in âŸ¨f, Î» a ha, lt_of_lt_of_le (hA a ha) (hx x (mem_singleton _))âŸ©

theorem geometric_hahn_banach_point_open {x : E} {B : set E}
  (hBâ‚ : convex B) (hBâ‚‚ : is_open B)
  (disj : x âˆ‰ B) :
  âˆƒ (f : E â†’L[â„] â„), (âˆ€ b âˆˆ B, f x < f b) :=
let âŸ¨f, hfâŸ© := geometric_hahn_banach_open_point hBâ‚ hBâ‚‚ disj in âŸ¨-f, by simpaâŸ©

theorem geometric_hahn_banach_open_open {A B : set E}
  (hAâ‚ : convex A) (hAâ‚‚ : is_open A)
  (hBâ‚ : convex B) (hBâ‚ƒ : is_open B)
  (disj : disjoint A B) :
âˆƒ (f : E â†’L[â„] â„) (s : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ (âˆ€ b âˆˆ B, s < f b) :=
begin
  rcases A.eq_empty_or_nonempty with (rfl | âŸ¨aâ‚€, haâ‚€âŸ©),
  { refine âŸ¨0, -1, by simp, Î» b hb, by norm_numâŸ© },
  rcases B.eq_empty_or_nonempty with (rfl | âŸ¨bâ‚€, hbâ‚€âŸ©),
  { refine âŸ¨0, 1, Î» a ha, by norm_num, by simpâŸ© },
  obtain âŸ¨f, s, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open hAâ‚ hAâ‚‚ hBâ‚ disj,
  have : f â‰  0,
  { rintro rfl,
    have := hfâ‚ _ haâ‚€,
    simp only [continuous_linear_map.zero_apply] at this,
    have := hfâ‚‚ _ hbâ‚€,
    simp only [continuous_linear_map.zero_apply] at this,
    linarith },
  have : is_open_map f := nonzero_linear_map_is_open_map _ â€¹f â‰  0â€º,
  refine âŸ¨f, s, hfâ‚, _âŸ©,
  suffices : f '' B âŠ† Ioi s,
  { intros b hb,
    apply this âŸ¨b, â€¹_â€º, rflâŸ© },
  rw â†interior_Ici,
  apply interior_maximal,
  { rintro _ âŸ¨_, _, rflâŸ©,
    refine hfâ‚‚ _ â€¹_â€º },
  apply â€¹is_open_map fâ€º _ hBâ‚ƒ,
end

open filter
open_locale topological_space

/--
If `A,B` are disjoint convex sets, `A` is compact and `B` is closed then we can find open disjoint
convex subsets containing them.
-/
-- TODO: This proof uses the normed space structure of `E`, but it could work for locally convex
-- topological vector spaces: instead of taking the balls around 0 with radius 1/n, we could show
-- there must be some convex neighbourhood `W` of 0 which make `A + W` and `B + W` disjoint?
theorem closed_compact_separate {A B : set E}
  (hAâ‚ : convex A) (hAâ‚‚ : is_compact A) (hBâ‚ : convex B) (hBâ‚ƒ : is_closed B) (disj : disjoint A B) :
  âˆƒ U V, is_open U âˆ§ is_open V âˆ§ convex U âˆ§ convex V âˆ§ A âŠ† U âˆ§ B âŠ† V âˆ§ disjoint U V :=
begin
  have : âˆƒ (n : â„•), disjoint (A + metric.ball 0 (n+1)â»Â¹) (B + metric.ball 0 (n+1)â»Â¹),
  { by_contra h,
    push_neg at h,
    simp only [not_disjoint_iff, set.mem_add, metric.mem_ball, dist_zero_right,
      â†exists_and_distrib_left, â†exists_and_distrib_right, and_assoc] at h,
    choose z f f' g g' hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚… hâ‚† using h,
    obtain âŸ¨w, hw, Ï†, hÏ†â‚, hÏ†â‚‚ : tendsto (f âˆ˜ _) _ _âŸ© := hAâ‚‚.tendsto_subseq hâ‚,
    have : tendsto (g âˆ˜ Ï†) at_top (ğ“ w),
    { have : tendsto (f - g) at_top (ğ“ 0),
      { suffices : âˆ€ n, âˆ¥(f - g) nâˆ¥ â‰¤ 2 * (n+1)â»Â¹,
        { apply squeeze_zero_norm this,
          rw â†mul_zero (2:â„),
          apply tendsto.const_mul (2:â„),
          simp_rw inv_eq_one_div,
          apply tendsto_one_div_add_at_top_nhds_0_nat },
        intro n,
        simp only [pi.sub_apply],
        have : f n - g n = g' n - f' n,
        { rw [sub_eq_iff_eq_add', â†add_sub_assoc, hâ‚†, â†hâ‚ƒ, add_sub_cancel] },
        rw this,
        apply le_trans (norm_sub_le _ _) _,
        rw two_mul,
        apply add_le_add (hâ‚… n).le (hâ‚‚ n).le },
      have : tendsto (f âˆ˜ Ï† - g âˆ˜ Ï†) at_top (ğ“ 0),
      { have : f âˆ˜ Ï† - g âˆ˜ Ï† = (f - g) âˆ˜ Ï†,
        { ext,
          simp },
        rw this,
        apply tendsto.comp â€¹tendsto (f - g) at_top _â€º (strict_mono_tendsto_at_top hÏ†â‚) },
      simpa using tendsto.sub hÏ†â‚‚ â€¹tendsto (f âˆ˜ Ï† - g âˆ˜ Ï†) at_top _â€º },
    have := mem_of_is_closed_sequential â€¹is_closed Bâ€º (Î» n, hâ‚„ (Ï† n)) this,
    apply disj âŸ¨hw, â€¹w âˆˆ Bâ€ºâŸ© },
  obtain âŸ¨n, hnâŸ© := this,
  refine âŸ¨_, _, _, _, hAâ‚.add _, hBâ‚.add _, _, _, hnâŸ©,
  { exact metric.is_open_ball.add_left },
  { exact metric.is_open_ball.add_left },
  { exact convex_ball 0 _ },
  { exact convex_ball 0 _ },
  { suffices : A + {0} âŠ† A + metric.ball (0:E) (n+1)â»Â¹,
    { simpa },
    apply add_subset_add (set.subset.refl _),
    simp only [metric.mem_ball, norm_zero, dist_zero_left, singleton_subset_iff, inv_pos],
    norm_cast,
    simp },
  { suffices : B + {0} âŠ† B + metric.ball (0:E) (n+1)â»Â¹,
    { simpa },
    apply add_subset_add (set.subset.refl _),
    simp only [metric.mem_ball, norm_zero, dist_zero_left, singleton_subset_iff, inv_pos],
    norm_cast,
    simp },
end

/--
A version of the Hahn-Banach theorem: given disjoint convex subsets `A,B` where `A` is compact,
and `B` is closed, there is a continuous linear functional which strongly separates them.
-/
theorem geometric_hahn_banach_compact_closed {A B : set E}
  (hAâ‚ : convex A) (hAâ‚‚ : is_compact A)
  (hBâ‚ : convex B) (hBâ‚‚ : is_closed B)
  (disj : disjoint A B) :
  âˆƒ (f : E â†’L[â„] â„) (s t : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ s < t âˆ§ (âˆ€ b âˆˆ B, t < f b) :=
begin
  rcases A.eq_empty_or_nonempty with (rfl | hA),
  { refine âŸ¨0, -2, -1, by simp, by norm_num, Î» b hb, by norm_numâŸ© },
  rcases B.eq_empty_or_nonempty with (h | hB),
  { rw h,
    exact âŸ¨0, 1, 2, Î» a ha, by norm_num, by norm_num, by simpâŸ© },
  obtain âŸ¨U, V, hU, hV, hUâ‚, hVâ‚, AU, BV, disj'âŸ© := closed_compact_separate hAâ‚ hAâ‚‚ hBâ‚ hBâ‚‚ disj,
  obtain âŸ¨f, s, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open_open hUâ‚ hU hVâ‚ hV disj',
  obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := hAâ‚‚.exists_forall_ge hA f.continuous.continuous_on,
  have : Sup (f '' A) = f x,
  { apply le_antisymm,
    { apply cSup_le (hA.image f) (by simpa) },
    refine le_cSup âŸ¨f x, _âŸ© âŸ¨_, hxâ‚, rflâŸ©,
    simpa [upper_bounds] },
  have : f x < s,
  { apply hfâ‚,
    apply AU,
    apply hxâ‚ },
  exact âŸ¨f, (f x + s)/2, s, Î» a ha, by linarith [hxâ‚‚ a ha], by linarith, Î» b hb, hfâ‚‚ b (BV hb)âŸ©,
end

/--
A version of the Hahn-Banach theorem: given disjoint convex subsets `A,B` where `A` is closed,
and `B` is compact, there is a continuous linear functional which strongly separates them.
-/
theorem geometric_hahn_banach_closed_compact {A B : set E}
  (hAâ‚ : convex A) (hAâ‚‚ : is_closed A)
  (hBâ‚ : convex B) (hBâ‚‚ : is_compact B)
  (disj : disjoint A B) :
  âˆƒ (f : E â†’L[â„] â„) (s t : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ s < t âˆ§ (âˆ€ b âˆˆ B, t < f b) :=
let âŸ¨f, s, t, hs, st, htâŸ© := geometric_hahn_banach_compact_closed hBâ‚ hBâ‚‚ hAâ‚ hAâ‚‚ disj.symm in
âŸ¨-f, -t, -s, by simpa using ht, by simpa using st, by simpa using hsâŸ©

theorem geometric_hahn_banach_point_closed {x : E} {B : set E}
  (hBâ‚ : convex B) (hBâ‚‚ : is_closed B)
  (disj : x âˆ‰ B) :
  âˆƒ (f : E â†’L[â„] â„) (s : â„), f x < s âˆ§ (âˆ€ b âˆˆ B, s < f b) :=
let âŸ¨f, s, t, ha, hst, hbâŸ© := geometric_hahn_banach_compact_closed (convex_singleton x)
  compact_singleton hBâ‚ hBâ‚‚ (disjoint_singleton_left.2 disj)
  in âŸ¨f, t, lt_trans (ha x (mem_singleton _)) hst, hbâŸ©

theorem geometric_hahn_banach_closed_point {A : set E} {x : E}
  (hAâ‚ : convex A) (hAâ‚‚ : is_closed A)
  (disj : x âˆ‰ A) :
  âˆƒ (f : E â†’L[â„] â„) (s : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ s < f x :=
let âŸ¨f, s, t, ha, hst, hbâŸ© := geometric_hahn_banach_closed_compact hAâ‚ hAâ‚‚ (convex_singleton x)
  compact_singleton (disjoint_singleton_right.2 disj)
  in âŸ¨f, s, ha, lt_trans hst (hb x (mem_singleton _))âŸ©

theorem geometric_hahn_banach_point_point {x y : E} (hxy : x â‰  y) :
  âˆƒ (f : E â†’L[â„] â„), f x < f y :=
begin
  have : disjoint ({x} : set E) {y},
  { simp [hxy.symm] },
  obtain âŸ¨f, s, t, hs, st, htâŸ© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) compact_singleton (convex_singleton y)
      is_closed_singleton this,
  refine âŸ¨f, by linarith [hs x rfl, ht y rfl]âŸ©,
end

end separating
