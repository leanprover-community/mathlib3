/-
Copyright (c) 2020 Yury Kudryashov All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth
-/
import analysis.convex.cone
import analysis.normed_space.extend

/-!
# Hahn-Banach theorem

In this file we prove a version of Hahn-Banach theorem for continuous linear
functions on normed spaces over `â„` and `â„‚`.

In order to state and prove its corollaries uniformly, we prove the statements for a field `ğ•œ`
satisfying `is_R_or_C ğ•œ`.

In this setting, `exists_dual_vector` states that, for any nonzero `x`, there exists a continuous
linear form `g` of norm `1` with `g x = âˆ¥xâˆ¥` (where the norm has to be interpreted as an element
of `ğ•œ`).

-/

universes u v

/--
The norm of `x` as an element of `ğ•œ` (a normed algebra over `â„`). This is needed in particular to
state equalities of the form `g x = norm' ğ•œ x` when `g` is a linear function.

For the concrete cases of `â„` and `â„‚`, this is just `âˆ¥xâˆ¥` and `â†‘âˆ¥xâˆ¥`, respectively.
-/
noncomputable def norm' (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [semi_normed_algebra â„ ğ•œ]
  {E : Type*} [semi_normed_group E] (x : E) : ğ•œ :=
algebra_map â„ ğ•œ âˆ¥xâˆ¥

lemma norm'_def (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [semi_normed_algebra â„ ğ•œ]
  {E : Type*} [semi_normed_group E] (x : E) :
  norm' ğ•œ x = (algebra_map â„ ğ•œ âˆ¥xâˆ¥) := rfl

lemma norm_norm'
  (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [semi_normed_algebra â„ ğ•œ]
  (A : Type*) [semi_normed_group A]
  (x : A) : âˆ¥norm' ğ•œ xâˆ¥ = âˆ¥xâˆ¥ :=
by rw [norm'_def, norm_algebra_map_eq, norm_norm]

@[simp] lemma norm'_eq_zero_iff
  (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [semi_normed_algebra â„ ğ•œ]
  (A : Type*) [normed_group A] (x : A) :
  norm' ğ•œ x = 0 â†” x = 0 :=
by simp [norm', â† norm_eq_zero, norm_algebra_map_eq]

namespace real
variables {E : Type*} [semi_normed_group E] [semi_normed_space â„ E]

/-- Hahn-Banach theorem for continuous linear functions over `â„`. -/
theorem exists_extension_norm_eq (p : subspace â„ E) (f : p â†’L[â„] â„) :
  âˆƒ g : E â†’L[â„] â„, (âˆ€ x : p, g x = f x) âˆ§ âˆ¥gâˆ¥ = âˆ¥fâˆ¥ :=
begin
  rcases exists_extension_of_le_sublinear âŸ¨p, fâŸ© (Î» x, âˆ¥fâˆ¥ * âˆ¥xâˆ¥)
    (Î» c hc x, by simp only [norm_smul c x, real.norm_eq_abs, abs_of_pos hc, mul_left_comm])
    (Î» x y, _) (Î» x, le_trans (le_abs_self _) (f.le_op_norm _))
    with âŸ¨g, g_eq, g_leâŸ©,
  set g' := g.mk_continuous (âˆ¥fâˆ¥)
    (Î» x, abs_le.2 âŸ¨neg_le.1 $ g.map_neg x â–¸ norm_neg x â–¸ g_le (-x), g_le xâŸ©),
  { refine âŸ¨g', g_eq, _âŸ©,
    { apply le_antisymm (g.mk_continuous_norm_le (norm_nonneg f) _),
      refine f.op_norm_le_bound (norm_nonneg _) (Î» x, _),
      dsimp at g_eq,
      rw â† g_eq,
      apply g'.le_op_norm } },
  { simp only [â† mul_add],
    exact mul_le_mul_of_nonneg_left (norm_add_le x y) (norm_nonneg f) }
end

end real

section is_R_or_C
open is_R_or_C

variables {ğ•œ : Type*} [is_R_or_C ğ•œ] {F : Type*} [semi_normed_group F] [semi_normed_space ğ•œ F]

/-- Hahn-Banach theorem for continuous linear functions over `ğ•œ` satisyfing `is_R_or_C ğ•œ`. -/
theorem exists_extension_norm_eq (p : subspace ğ•œ F) (f : p â†’L[ğ•œ] ğ•œ) :
  âˆƒ g : F â†’L[ğ•œ] ğ•œ, (âˆ€ x : p, g x = f x) âˆ§ âˆ¥gâˆ¥ = âˆ¥fâˆ¥ :=
begin
  letI : module â„ F := restrict_scalars.module â„ ğ•œ F,
  letI : is_scalar_tower â„ ğ•œ F := restrict_scalars.is_scalar_tower _ _ _,
  letI : semi_normed_space â„ F := semi_normed_space.restrict_scalars _ ğ•œ _,
  -- Let `fr: p â†’L[â„] â„` be the real part of `f`.
  let fr := re_clm.comp (f.restrict_scalars â„),
  have fr_apply : âˆ€ x, fr x = re (f x), by { assume x, refl },
  -- Use the real version to get a norm-preserving extension of `fr`, which
  -- we'll call `g : F â†’L[â„] â„`.
  rcases real.exists_extension_norm_eq (p.restrict_scalars â„) fr with âŸ¨g, âŸ¨hextends, hnormeqâŸ©âŸ©,
  -- Now `g` can be extended to the `F â†’L[ğ•œ] ğ•œ` we need.
  refine âŸ¨g.extend_to_ğ•œ, _âŸ©,
  -- It is an extension of `f`.
  have h : âˆ€ x : p, g.extend_to_ğ•œ x = f x,
  { assume x,
    rw [continuous_linear_map.extend_to_ğ•œ_apply, â†submodule.coe_smul, hextends, hextends],
    have : (fr x : ğ•œ) - I * â†‘(fr (I â€¢ x)) = (re (f x) : ğ•œ) - (I : ğ•œ) * (re (f ((I : ğ•œ) â€¢ x))),
      by refl,
    rw this,
    apply ext,
    { simp only [add_zero, algebra.id.smul_eq_mul, I_re, of_real_im, add_monoid_hom.map_add,
        zero_sub, I_im', zero_mul, of_real_re, eq_self_iff_true, sub_zero, mul_neg_eq_neg_mul_symm,
        of_real_neg, mul_re, mul_zero, sub_neg_eq_add, continuous_linear_map.map_smul] },
    { simp only [algebra.id.smul_eq_mul, I_re, of_real_im, add_monoid_hom.map_add, zero_sub, I_im',
        zero_mul, of_real_re, mul_neg_eq_neg_mul_symm, mul_im, zero_add, of_real_neg, mul_re,
        sub_neg_eq_add, continuous_linear_map.map_smul] } },
  -- And we derive the equality of the norms by bounding on both sides.
  refine âŸ¨h, le_antisymm _ _âŸ©,
  { calc âˆ¥g.extend_to_ğ•œâˆ¥
        â‰¤ âˆ¥gâˆ¥ : g.extend_to_ğ•œ.op_norm_le_bound g.op_norm_nonneg (norm_bound _)
    ... = âˆ¥frâˆ¥ : hnormeq
    ... â‰¤ âˆ¥re_clmâˆ¥ * âˆ¥fâˆ¥ : continuous_linear_map.op_norm_comp_le _ _
    ... = âˆ¥fâˆ¥ : by rw [re_clm_norm, one_mul] },
  { exact f.op_norm_le_bound g.extend_to_ğ•œ.op_norm_nonneg (Î» x, h x â–¸ g.extend_to_ğ•œ.le_op_norm x) }
end

end is_R_or_C

section dual_vector
variables (ğ•œ : Type v) [is_R_or_C ğ•œ]
variables {E : Type u} [normed_group E] [normed_space ğ•œ E]

open continuous_linear_equiv submodule
open_locale classical

lemma coord_norm' (x : E) (h : x â‰  0) : âˆ¥norm' ğ•œ x â€¢ coord ğ•œ x hâˆ¥ = 1 :=
by rw [norm_smul, norm_norm', coord_norm, mul_inv_cancel (mt norm_eq_zero.mp h)]

/-- Corollary of Hahn-Banach.  Given a nonzero element `x` of a normed space, there exists an
    element of the dual space, of norm `1`, whose value on `x` is `âˆ¥xâˆ¥`. -/
theorem exists_dual_vector (x : E) (h : x â‰  0) : âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g x = norm' ğ•œ x :=
begin
  let p : submodule ğ•œ E := ğ•œ âˆ™ x,
  let f := norm' ğ•œ x â€¢ coord ğ•œ x h,
  obtain âŸ¨g, hgâŸ© := exists_extension_norm_eq p f,
  refine âŸ¨g, _, _âŸ©,
  { rw [hg.2, coord_norm'] },
  { calc g x = g (âŸ¨x, mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) : by rw coe_mk
    ... = (norm' ğ•œ x â€¢ coord ğ•œ x h) (âŸ¨x, mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) : by rw â† hg.1
    ... = norm' ğ•œ x : by simp }
end

/-- Variant of Hahn-Banach, eliminating the hypothesis that `x` be nonzero, and choosing
    the dual element arbitrarily when `x = 0`. -/
theorem exists_dual_vector' [nontrivial E] (x : E) :
  âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g x = norm' ğ•œ x :=
begin
  by_cases hx : x = 0,
  { obtain âŸ¨y, hyâŸ© := exists_ne (0 : E),
    obtain âŸ¨g, hgâŸ© : âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g y = norm' ğ•œ y := exists_dual_vector ğ•œ y hy,
    refine âŸ¨g, hg.left, _âŸ©,
    rw [norm'_def, hx, norm_zero, ring_hom.map_zero, continuous_linear_map.map_zero] },
  { exact exists_dual_vector ğ•œ x hx }
end

/-- Variant of Hahn-Banach, eliminating the hypothesis that `x` be nonzero, but only ensuring that
    the dual element has norm at most `1` (this can not be improved for the trivial
    vector space). -/
theorem exists_dual_vector'' (x : E) :
  âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ â‰¤ 1 âˆ§ g x = norm' ğ•œ x :=
begin
  by_cases hx : x = 0,
  { refine âŸ¨0, by simp, _âŸ©,
    symmetry,
    simp [hx], },
  { rcases exists_dual_vector ğ•œ x hx with âŸ¨g, g_norm, g_eqâŸ©,
    exact âŸ¨g, g_norm.le, g_eqâŸ© }
end

end dual_vector
