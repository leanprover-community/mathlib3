/-
Copyright (c) 2020 Yury Kudryashov All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth
-/
import analysis.normed_space.operator_norm
import analysis.normed_space.extend
import analysis.convex.cone

/-!
# Hahn-Banach theorem

In this file we prove a version of Hahn-Banach theorem for continuous linear
functions on normed spaces over `â„` and `â„‚`.

In order to state and prove its corollaries uniformly, we introduce a typeclass
`has_exists_extension_norm_eq` for a field, requiring that a strong version of the
Hahn-Banach theorem holds over this field, and provide instances for `â„` and `â„‚`.

In this setting, `exists_dual_vector` states that, for any nonzero `x`, there exists a continuous
linear form `g` of norm `1` with `g x = âˆ¥xâˆ¥` (where the norm has to be interpreted as an element
of `ğ•œ`).

-/

universes u v

/--
A field where the Hahn-Banach theorem for continuous linear functions holds. This allows stating
theorems that depend on it uniformly over such fields.

In particular, this is satisfied by `â„` and `â„‚`.
-/
class has_exists_extension_norm_eq (ğ•œ : Type v) [nondiscrete_normed_field ğ•œ] : Prop :=
(exists_extension_norm_eq :
  âˆ€ (E : Type u)
  [normed_group E] [normed_space ğ•œ E]
  (p : subspace ğ•œ E)
  (f : p â†’L[ğ•œ] ğ•œ),
  âˆƒ g : E â†’L[ğ•œ] ğ•œ, (âˆ€ x : p, g x = f x) âˆ§ âˆ¥gâˆ¥ = âˆ¥fâˆ¥)

/--
The norm of `x` as an element of `ğ•œ` (a normed algebra over `â„`). This is needed in particular to
state equalities of the form `g x = norm' ğ•œ x` when `g` is a linear function.

For the concrete cases of `â„` and `â„‚`, this is just `âˆ¥xâˆ¥` and `â†‘âˆ¥xâˆ¥`, respectively.
-/
noncomputable def norm' (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ]
  {E : Type*} [normed_group E] (x : E) : ğ•œ :=
algebra_map â„ ğ•œ âˆ¥xâˆ¥

lemma norm'_def (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ]
  {E : Type*} [normed_group E] (x : E) :
  norm' ğ•œ x = (algebra_map â„ ğ•œ âˆ¥xâˆ¥) := rfl

lemma norm_norm'
  (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ]
  (A : Type*) [normed_group A]
  (x : A) : âˆ¥norm' ğ•œ xâˆ¥ = âˆ¥xâˆ¥ :=
by rw [norm'_def, norm_algebra_map_eq, norm_norm]

section real
variables {E : Type*} [normed_group E] [normed_space â„ E]

/-- Hahn-Banach theorem for continuous linear functions over `â„`. -/
theorem exists_extension_norm_eq (p : subspace â„ E) (f : p â†’L[â„] â„) :
  âˆƒ g : E â†’L[â„] â„, (âˆ€ x : p, g x = f x) âˆ§ âˆ¥gâˆ¥ = âˆ¥fâˆ¥ :=
begin
  rcases exists_extension_of_le_sublinear âŸ¨p, fâŸ© (Î» x, âˆ¥fâˆ¥ * âˆ¥xâˆ¥)
    (Î» c hc x, by simp only [norm_smul c x, real.norm_eq_abs, abs_of_pos hc, mul_left_comm])
    (Î» x y, _) (Î» x, le_trans (le_abs_self _) (f.le_op_norm _))
    with âŸ¨g, g_eq, g_leâŸ©,
  set g' := g.mk_continuous (âˆ¥fâˆ¥)
    (Î» x, abs_le.2 âŸ¨neg_le.1 $ g.map_neg x â–¸ norm_neg x â–¸ g_le (-x), g_le xâŸ©),
  { refine âŸ¨g', g_eq, _âŸ©,
    { apply le_antisymm (g.mk_continuous_norm_le (norm_nonneg f) _),
      refine f.op_norm_le_bound (norm_nonneg _) (Î» x, _),
      dsimp at g_eq,
      rw â† g_eq,
      apply g'.le_op_norm } },
  { simp only [â† mul_add],
    exact mul_le_mul_of_nonneg_left (norm_add_le x y) (norm_nonneg f) }
end

instance real_has_exists_extension_norm_eq : has_exists_extension_norm_eq â„ :=
âŸ¨by { intros, apply exists_extension_norm_eq }âŸ©

end real

section complex
variables {F : Type*} [normed_group F] [normed_space â„‚ F]

-- Inlining the following two definitions causes a type mismatch between
-- subspace â„ (semimodule.restrict_scalars â„ â„‚ F) and subspace â„‚ F.
/-- Restrict a `â„‚`-subspace to an `â„`-subspace. -/
noncomputable def restrict_scalars (p : subspace â„‚ F) : subspace â„ F := p.restrict_scalars â„

private lemma apply_real (p : subspace â„‚ F) (f' : p â†’L[â„] â„) :
  âˆƒ g : F â†’L[â„] â„, (âˆ€ x : restrict_scalars p, g x = f' x) âˆ§ âˆ¥gâˆ¥ = âˆ¥f'âˆ¥ :=
  exists_extension_norm_eq (p.restrict_scalars â„) f'

open complex

/-- Hahn-Banach theorem for continuous linear functions over `â„‚`. -/
theorem complex.exists_extension_norm_eq (p : subspace â„‚ F) (f : p â†’L[â„‚] â„‚) :
  âˆƒ g : F â†’L[â„‚] â„‚, (âˆ€ x : p, g x = f x) âˆ§ âˆ¥gâˆ¥ = âˆ¥fâˆ¥ :=
begin
  -- Let `fr: p â†’L[â„] â„` be the real part of `f`.
  let fr := continuous_linear_map.re.comp (f.restrict_scalars â„),
  have fr_apply : âˆ€ x, fr x = (f x).re := Î» x, rfl,

  -- Use the real version to get a norm-preserving extension of `fr`, which we'll call `g: F â†’L[â„] â„`.
  rcases apply_real p fr with âŸ¨g, âŸ¨hextends, hnormeqâŸ©âŸ©,

  -- Now `g` can be extended to the `F â†’L[â„‚] â„‚` we need.
  use g.extend_to_â„‚,

  -- It is an extension of `f`.
  have h : âˆ€ x : p, g.extend_to_â„‚ x = f x,
  { intros,
    change (âŸ¨g x, -g ((I â€¢ x) : p)âŸ© : â„‚) = f x,
    ext; dsimp only; rw [hextends, fr_apply],
    rw [continuous_linear_map.map_smul, algebra.id.smul_eq_mul, mul_re, I_re, I_im],
    ring },

  refine âŸ¨h, _âŸ©,

  -- And we derive the equality of the norms by bounding on both sides.
  refine le_antisymm _ _,
  { calc âˆ¥g.extend_to_â„‚âˆ¥
        â‰¤ âˆ¥gâˆ¥ : g.extend_to_â„‚.op_norm_le_bound g.op_norm_nonneg (norm_bound _)
    ... = âˆ¥frâˆ¥ : hnormeq
    ... â‰¤ âˆ¥continuous_linear_map.reâˆ¥ * âˆ¥fâˆ¥ : continuous_linear_map.op_norm_comp_le _ _
    ... = âˆ¥fâˆ¥ : by rw [complex.continuous_linear_map.re_norm, one_mul] },

  { exact f.op_norm_le_bound g.extend_to_â„‚.op_norm_nonneg (Î» x, h x â–¸ g.extend_to_â„‚.le_op_norm x) },
end

instance complex_has_exists_extension_norm_eq : has_exists_extension_norm_eq â„‚ :=
âŸ¨by { intros, apply complex.exists_extension_norm_eq }âŸ©

end complex

section dual_vector
variables {ğ•œ : Type v} [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ]
variables {E : Type u} [normed_group E] [normed_space ğ•œ E]

open continuous_linear_equiv
open_locale classical

lemma coord_norm' (x : E) (h : x â‰  0) : âˆ¥norm' ğ•œ x â€¢ coord ğ•œ x hâˆ¥ = 1 :=
by rw [norm_smul, norm_norm', coord_norm, mul_inv_cancel (mt norm_eq_zero.mp h)]

variables [has_exists_extension_norm_eq.{u} ğ•œ]
open submodule

/-- Corollary of Hahn-Banach.  Given a nonzero element `x` of a normed space, there exists an
    element of the dual space, of norm `1`, whose value on `x` is `âˆ¥xâˆ¥`. -/
theorem exists_dual_vector (x : E) (h : x â‰  0) : âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g x = norm' ğ•œ x :=
begin
  let p : submodule ğ•œ E := span ğ•œ {x},
  let f := norm' ğ•œ x â€¢ coord ğ•œ x h,
  obtain âŸ¨g, hgâŸ© := has_exists_extension_norm_eq.exists_extension_norm_eq E p f,
  use g, split,
  { rw [hg.2, coord_norm'] },
  { calc g x = g (âŸ¨x, mem_span_singleton_self xâŸ© : span ğ•œ {x}) : by rw coe_mk
    ... = (norm' ğ•œ x â€¢ coord ğ•œ x h) (âŸ¨x, mem_span_singleton_self xâŸ© : span ğ•œ {x}) : by rw â† hg.1
    ... = norm' ğ•œ x : by simp [coord_self] }
end

/-- Variant of the above theorem, eliminating the hypothesis that `x` be nonzero, and choosing
    the dual element arbitrarily when `x = 0`. -/
theorem exists_dual_vector' [nontrivial E] (x : E) :
  âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g x = norm' ğ•œ x :=
begin
  by_cases hx : x = 0,
  { obtain âŸ¨y, hyâŸ© := exists_ne (0 : E),
    obtain âŸ¨g, hgâŸ© : âˆƒ g : E â†’L[ğ•œ] ğ•œ, âˆ¥gâˆ¥ = 1 âˆ§ g y = norm' ğ•œ y := exists_dual_vector y hy,
    refine âŸ¨g, hg.left, _âŸ©,
    rw [norm'_def, hx, norm_zero, ring_hom.map_zero, continuous_linear_map.map_zero] },
  { exact exists_dual_vector x hx }
end

end dual_vector
