/-
Copyright (c) 2022 Bhavik Mehta All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Ya√´l Dillies
-/
import analysis.convex.cone
import analysis.convex.topology
import analysis.normed.group.pointwise
import analysis.seminorm
import tactic.by_contra

/-!
# Separation Hahn-Banach theorem

In this file we prove the geometric Hahn-Banach theorem. For any two disjoint convex sets, there
exists a continuous linear functional separating them, geometrically meaning that we can intercalate
a plane between them.

We provide many variations to stricten the result under more assumptions on the convex sets:
* `geometric_hahn_banach_open`: One set is open. Weak separation.
* `geometric_hahn_banach_open_point`, `geometric_hahn_banach_point_open`: One set is open, the
  other is a singleton. Weak separation.
* `geometric_hahn_banach_open_open`: Both sets are open. Semistrict separation.
* `geometric_hahn_banach_compact_closed`, `geometric_hahn_banach_closed_compact`: One set is closed,
  the other one is compact. Strict separation.
* `geometric_hahn_banach_point_closed`, `geometric_hahn_banach_closed_point`: One set is closed, the
  other one is a singleton. Strict separation.
* `geometric_hahn_banach_point_point`: Both sets are singletons. Strict separation.

## TODO

* Eidelheit's theorem
* `convex ‚Ñù s ‚Üí interior (closure s) ‚äÜ s`
-/

open filter function metric set
open_locale pointwise topological_space

variables {ùïú E : Type*}

/-- Given a set `s` which is a convex neighbourhood of `0` and a point `x‚ÇÄ` outside of it, there is
a continuous linear functional `f` separating `x‚ÇÄ` and `s`, in the sense that it sends `x‚ÇÄ` to 1 and
all of `s` to values strictly below `1`. -/
lemma separate_convex_open_set {s : set E} (hs‚ÇÄ : (0 : E) ‚àà s) (hs‚ÇÅ : convex ‚Ñù s)
  (hs‚ÇÇ : is_open s) {x‚ÇÄ : E} (hx‚ÇÄ : x‚ÇÄ ‚àâ s) :
  ‚àÉ f : E ‚ÜíL[‚Ñù] ‚Ñù, f x‚ÇÄ = 1 ‚àß ‚àÄ x ‚àà s, f x < 1 :=
begin
  let f : linear_pmap ‚Ñù E ‚Ñù :=
    linear_pmap.mk_span_singleton x‚ÇÄ 1 (ne_of_mem_of_not_mem hs‚ÇÄ hx‚ÇÄ).symm,
  obtain ‚ü®r, hr, hrs‚ü© := metric.mem_nhds_iff.1
    (inter_mem (hs‚ÇÇ.mem_nhds hs‚ÇÄ) $ hs‚ÇÇ.neg.mem_nhds $ by rwa [mem_neg, neg_zero]),
  obtain ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ‚ü© := exists_extension_of_le_sublinear f (gauge s)
    (Œª c hc, gauge_smul_of_nonneg hc.le)
    (gauge_add_le hs‚ÇÅ $ absorbent_nhds_zero $ hs‚ÇÇ.mem_nhds hs‚ÇÄ) _,
  { refine ‚ü®œÜ.mk_continuous (r‚Åª¬π) $ Œª x, _, _, _‚ü©,
    { rw [real.norm_eq_abs, abs_le, neg_le, ‚Üêlinear_map.map_neg],
      nth_rewrite 0 ‚Üênorm_neg x,
      suffices : ‚àÄ x, œÜ x ‚â§ r‚Åª¬π * ‚à•x‚à•,
      { exact ‚ü®this _, this _‚ü© },
      refine Œª x, (hœÜ‚ÇÇ _).trans _,
      rw [‚Üêdiv_eq_inv_mul, ‚Üêgauge_ball hr],
      exact gauge_mono (absorbent_ball_zero hr) (hrs.trans $ inter_subset_left _ _) x },
    { dsimp,
      rw [‚Üêsubmodule.coe_mk x‚ÇÄ (submodule.mem_span_singleton_self _), hœÜ‚ÇÅ,
        linear_pmap.mk_span_singleton_apply_self] },
    { exact Œª x hx, (hœÜ‚ÇÇ x).trans_lt (gauge_lt_one_of_mem_of_open hs‚ÇÅ hs‚ÇÄ hs‚ÇÇ hx) } },
  { rintro ‚ü®x, hx‚ü©,
    obtain ‚ü®y, rfl‚ü© := submodule.mem_span_singleton.1 hx,
    rw linear_pmap.mk_span_singleton_apply,
    simp only [mul_one, algebra.id.smul_eq_mul, submodule.coe_mk],
    obtain h | h := le_or_lt y 0,
    { exact h.trans (gauge_nonneg _) },
    { rw [gauge_smul_of_nonneg h.le, smul_eq_mul, le_mul_iff_one_le_right h],
      exact one_le_gauge_of_not_mem (hs‚ÇÅ.star_convex hs‚ÇÄ)
        ((absorbent_ball_zero hr).subset $ hrs.trans $ inter_subset_left _ _).absorbs hx‚ÇÄ,
      apply_instance } }
end

variables [normed_group E] [normed_space ‚Ñù E] {s t : set E} {x : E}

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `s`, `t` where `s` is open,
there is a continuous linear functional which separates them. -/
theorem geometric_hahn_banach_open (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÇ : is_open s) (ht : convex ‚Ñù t)
  (disj : disjoint s t) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (u : ‚Ñù), (‚àÄ a ‚àà s, f a < u) ‚àß ‚àÄ b ‚àà t, u ‚â§ f b :=
begin
  obtain rfl | ‚ü®a‚ÇÄ, ha‚ÇÄ‚ü© := s.eq_empty_or_nonempty,
  { exact ‚ü®0, 0, by simp, Œª b hb, by simp‚ü© },
  obtain rfl | ‚ü®b‚ÇÄ, hb‚ÇÄ‚ü© := t.eq_empty_or_nonempty,
  { exact ‚ü®0, 1, Œª a ha, by norm_num, by simp‚ü© },
  let x‚ÇÄ := b‚ÇÄ - a‚ÇÄ,
  let C := {x‚ÇÄ} + s + -t,
  have : (0:E) ‚àà C := ‚ü®_ + a‚ÇÄ, -b‚ÇÄ, add_mem_add rfl ha‚ÇÄ, neg_mem_neg.2 hb‚ÇÄ, by simp‚ü©,
  have : convex ‚Ñù C := ((convex_singleton _).add hs‚ÇÅ).add ht.neg_preimage,
  have : x‚ÇÄ ‚àâ C,
  { intro hx‚ÇÄ,
    simp only [mem_add, mem_singleton_iff, mem_neg, exists_eq_left, exists_exists_and_eq_and,
      exists_and_distrib_left, add_assoc x‚ÇÄ, add_right_eq_self] at hx‚ÇÄ,
    obtain ‚ü®a, ha, b, hb, hab‚ü© := hx‚ÇÄ,
    rw ‚Üêeq_neg_of_add_eq_zero hab at hb,
    exact disj ‚ü®ha, hb‚ü© },
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := separate_convex_open_set ‚Äπ0 ‚àà C‚Ä∫ ‚Äπ_‚Ä∫ hs‚ÇÇ.add_left.add_right ‚Äπx‚ÇÄ ‚àâ C‚Ä∫,
  have : f b‚ÇÄ = f a‚ÇÄ + 1,
  { simp [‚Üêhf‚ÇÅ] },
  have forall_lt : ‚àÄ (a ‚àà s) (b ‚àà t), f a < f b,
  { intros a ha b hb,
    have := hf‚ÇÇ (x‚ÇÄ + a + -b) (add_mem_add (add_mem_add rfl ha) (neg_mem_neg.2 hb)),
    simp only [f.map_neg, f.map_add, f.map_sub, hf‚ÇÅ] at this,
    linarith },
  refine ‚ü®f, Inf (f '' t), _, _‚ü©,
  { suffices : f '' s ‚äÜ Iio (Inf (f '' t)),
    { intros a ha,
      apply this ‚ü®_, ha, rfl‚ü© },
    rw ‚Üêinterior_Iic,
    apply interior_maximal,
    { rintro _ ‚ü®a, ha, rfl‚ü©,
      apply le_cInf ‚ü®f b‚ÇÄ, _‚ü©,
      { rintro _ ‚ü®b', hb, rfl‚ü©,
        exact (forall_lt _ ha _ hb).le },
      { exact mem_image_of_mem _ hb‚ÇÄ } },
    refine f.is_open_map _ _ hs‚ÇÇ,
    rintro rfl,
    simpa using hf‚ÇÅ },
  { intros b hb,
    apply cInf_le ‚ü®f a‚ÇÄ, _‚ü© (mem_image_of_mem _ hb),
    rintro _ ‚ü®b', hb', rfl‚ü©,
    exact (forall_lt _ ha‚ÇÄ _ hb').le },
end

theorem geometric_hahn_banach_open_point (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÇ : is_open s) (disj : x ‚àâ s) :
  ‚àÉ f : E ‚ÜíL[‚Ñù] ‚Ñù, ‚àÄ a ‚àà s, f a < f x :=
let ‚ü®f, s, hs, hx‚ü© := geometric_hahn_banach_open hs‚ÇÅ hs‚ÇÇ (convex_singleton x)
  (disjoint_singleton_right.2 disj)
  in ‚ü®f, Œª a ha, lt_of_lt_of_le (hs a ha) (hx x (mem_singleton _))‚ü©

theorem geometric_hahn_banach_point_open (ht‚ÇÅ : convex ‚Ñù t) (ht‚ÇÇ : is_open t) (disj : x ‚àâ t) :
  ‚àÉ f : E ‚ÜíL[‚Ñù] ‚Ñù, ‚àÄ b ‚àà t, f x < f b :=
let ‚ü®f, hf‚ü© := geometric_hahn_banach_open_point ht‚ÇÅ ht‚ÇÇ disj in ‚ü®-f, by simpa‚ü©

theorem geometric_hahn_banach_open_open (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÇ : is_open s) (ht‚ÇÅ : convex ‚Ñù t)
  (ht‚ÇÉ : is_open t) (disj : disjoint s t) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (u : ‚Ñù), (‚àÄ a ‚àà s, f a < u) ‚àß ‚àÄ b ‚àà t, u < f b :=
begin
  obtain (rfl | ‚ü®a‚ÇÄ, ha‚ÇÄ‚ü©) := s.eq_empty_or_nonempty,
  { exact ‚ü®0, -1, by simp, Œª b hb, by norm_num‚ü© },
  obtain (rfl | ‚ü®b‚ÇÄ, hb‚ÇÄ‚ü©) := t.eq_empty_or_nonempty,
  { exact ‚ü®0, 1, Œª a ha, by norm_num, by simp‚ü© },
  obtain ‚ü®f, s, hf‚ÇÅ, hf‚ÇÇ‚ü© := geometric_hahn_banach_open hs‚ÇÅ hs‚ÇÇ ht‚ÇÅ disj,
  have : f ‚â† 0,
  { rintro rfl,
    exact (hf‚ÇÅ _ ha‚ÇÄ).not_le (hf‚ÇÇ _ hb‚ÇÄ) },
  have : is_open_map f := f.is_open_map this,
  refine ‚ü®f, s, hf‚ÇÅ, _‚ü©,
  suffices : f '' t ‚äÜ Ioi s,
  { exact Œª b hb, this ‚ü®b, ‚Äπ_‚Ä∫, rfl‚ü© },
  rw ‚Üêinterior_Ici,
  refine interior_maximal _ (this _ ht‚ÇÉ),
  rintro _ ‚ü®_, _, rfl‚ü©,
  exact hf‚ÇÇ _ ‚Äπ_‚Ä∫,
end

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `s`, `t` where `s` is compact
and `t` is closed, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_compact_closed {s t : set E} (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÇ : is_compact s)
  (ht‚ÇÅ : convex ‚Ñù t) (ht‚ÇÇ : is_closed t) (disj : disjoint s t) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (u v : ‚Ñù), (‚àÄ a ‚àà s, f a < u) ‚àß u < v ‚àß ‚àÄ b ‚àà t, v < f b :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { exact ‚ü®0, -2, -1, by simp, by norm_num, Œª b hb, by norm_num‚ü© },
  tactic.unfreeze_local_instances,
  obtain rfl | ht := t.eq_empty_or_nonempty,
  { exact ‚ü®0, 1, 2, Œª a ha, by norm_num, by norm_num, by simp‚ü© },
  obtain ‚ü®U, V, hU, hV, hU‚ÇÅ, hV‚ÇÅ, sU, tV, disj'‚ü© :=
    exists_disjoint_open_convexes hs‚ÇÅ hs‚ÇÇ ht‚ÇÅ ht‚ÇÇ disj,
  obtain ‚ü®f, u, hf‚ÇÅ, hf‚ÇÇ‚ü© := geometric_hahn_banach_open_open hU‚ÇÅ hU hV‚ÇÅ hV disj',
  obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := hs‚ÇÇ.exists_forall_ge hs f.continuous.continuous_on,
  have : Sup (f '' s) = f x,
  { apply le_antisymm (cSup_le (hs.image f) (by simpa)),
    exact le_cSup ‚ü®f x, by simpa [upper_bounds]‚ü© ‚ü®_, hx‚ÇÅ, rfl‚ü© },
  have : f x < u := hf‚ÇÅ x (sU hx‚ÇÅ),
  exact ‚ü®f, (f x + u)/2, u, Œª a ha, by linarith [hx‚ÇÇ a ha], by linarith, Œª b hb, hf‚ÇÇ b (tV hb)‚ü©,
end

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `s`, `t` where `s` is closed,
and `t` is compact, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_closed_compact (hs‚ÇÅ : convex ‚Ñù s) (hs‚ÇÇ : is_closed s)
  (ht‚ÇÅ : convex ‚Ñù t) (ht‚ÇÇ : is_compact t) (disj : disjoint s t) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (u v : ‚Ñù), (‚àÄ a ‚àà s, f a < u) ‚àß u < v ‚àß ‚àÄ b ‚àà t, v < f b :=
let ‚ü®f, s, t, hs, st, ht‚ü© := geometric_hahn_banach_compact_closed ht‚ÇÅ ht‚ÇÇ hs‚ÇÅ hs‚ÇÇ disj.symm in
‚ü®-f, -t, -s, by simpa using ht, by simpa using st, by simpa using hs‚ü©

theorem geometric_hahn_banach_point_closed (ht‚ÇÅ : convex ‚Ñù t) (ht‚ÇÇ : is_closed t) (disj : x ‚àâ t) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (u : ‚Ñù), f x < u ‚àß ‚àÄ b ‚àà t, u < f b :=
let ‚ü®f, u, v, ha, hst, hb‚ü© := geometric_hahn_banach_compact_closed (convex_singleton x)
  is_compact_singleton ht‚ÇÅ ht‚ÇÇ (disjoint_singleton_left.2 disj)
  in ‚ü®f, v, lt_trans (ha x (mem_singleton _)) hst, hb‚ü©

theorem geometric_hahn_banach_closed_point {s : set E} {x : E} (hs‚ÇÅ : convex ‚Ñù s)
  (hs‚ÇÇ : is_closed s) (disj : x ‚àâ s) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (u : ‚Ñù), (‚àÄ a ‚àà s, f a < u) ‚àß u < f x :=
let ‚ü®f, s, t, ha, hst, hb‚ü© := geometric_hahn_banach_closed_compact hs‚ÇÅ hs‚ÇÇ (convex_singleton x)
  is_compact_singleton (disjoint_singleton_right.2 disj)
  in ‚ü®f, s, ha, lt_trans hst (hb x (mem_singleton _))‚ü©

theorem geometric_hahn_banach_point_point {x y : E} (hxy : x ‚â† y) : ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), f x < f y :=
begin
  obtain ‚ü®f, s, t, hs, st, ht‚ü© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) is_compact_singleton
      (convex_singleton y) is_closed_singleton (disjoint_singleton.2 hxy),
  exact ‚ü®f, by linarith [hs x rfl, ht y rfl]‚ü©,
end
