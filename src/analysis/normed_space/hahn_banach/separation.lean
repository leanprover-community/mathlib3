/-
Copyright (c) 2022 Bhavik Mehta All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, YaÃ«l Dillies
-/
import analysis.convex.cone
import analysis.convex.topology
import analysis.seminorm
import tactic.by_contra

/-!
# Separation Hahn-Banach theorem

In this file we prove the geometric Hahn-Banach theorem. For any two disjoint convex sets, there
exists a continuous linear functional separating them, geometrically meaning that we can intercalate
a plane between them.

We provide many variations to stricten the result under more assumptions on the convex sets:
* `geometric_hahn_banach_open`: One set is open. Weak separation.
* `geometric_hahn_banach_open_point`, `geometric_hahn_banach_point_open`: One set is open, the
  other is a singleton. Weak separation.
* `geometric_hahn_banach_open_open`: Both sets are open. Semistrict separation.
* `geometric_hahn_banach_compact_closed`, `geometric_hahn_banach_closed_compact`: One set is closed,
  the other one is compact. Strict separation.
* `geometric_hahn_banach_point_closed`, `geometric_hahn_banach_closed_point`: One set is closed, the
  other one is a singleton. Strict separation.
* `geometric_hahn_banach_point_point`: Both sets are singletons. Strict separation.
-/

open function set
open_locale pointwise

variables {ğ•œ E : Type*}

section
open filter
open_locale topological_space

lemma linear_map.exists_ne_zero {Râ‚ Râ‚‚ : Type*} [semiring Râ‚] [semiring Râ‚‚] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚}
  {Mâ‚ : Type*} [add_comm_monoid Mâ‚] {Mâ‚‚ : Type*} [add_comm_monoid Mâ‚‚] [module Râ‚ Mâ‚] [module Râ‚‚ Mâ‚‚]
  {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : f â‰  0) :
  âˆƒ x, f x â‰  0 :=
begin
  by_contra' h,
  exact hf (linear_map.ext h),
end

lemma continuous_linear_map.exists_ne_zero {Râ‚ Râ‚‚ : Type*} [semiring Râ‚]
  [semiring Râ‚‚] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚} {Mâ‚ : Type*} [topological_space Mâ‚] [add_comm_monoid Mâ‚]
  {Mâ‚‚ : Type*} [topological_space Mâ‚‚] [add_comm_monoid Mâ‚‚] [module Râ‚ Mâ‚] [module Râ‚‚ Mâ‚‚]
  {f : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚} (hf : f â‰  0) :
  âˆƒ x, f x â‰  0 :=
begin
  by_contra' h,
  exact hf (continuous_linear_map.ext h),
end

lemma nhds_le_map_nhds [topological_space ğ•œ] [topological_space E] {f : E â†’ ğ•œ} {g : ğ•œ â†’ E} {a : E}
  (hg : continuous_at g (f a)) (hcomp : f âˆ˜ g = id) (hgfa : g (f a) = a) :
  ğ“ (f a) â‰¤ map f (ğ“ a) :=
calc ğ“ (f a) = ((ğ“ (f a)).map g).map f : by rw [map_map, hcomp, map_id]
  ... â‰¤ (ğ“ $ g (f a)).map f             : map_mono hg
  ... = (ğ“ a).map f                     : by rw hgfa

lemma linear_map.nhds_le_map_nhds [topological_space ğ•œ] [division_ring ğ•œ] [topological_ring ğ•œ]
  [add_comm_group E] [topological_space E] [topological_add_group E] [module ğ•œ E]
  [has_continuous_smul ğ•œ E] {f : E â†’â‚—[ğ•œ] ğ•œ} (hf : f â‰  0) (a : E) :
  ğ“ (f a) â‰¤ map f (ğ“ a) :=
begin
  obtain âŸ¨xâ‚€, hxâ‚€âŸ© := linear_map.exists_ne_zero hf,
  let g : ğ•œ â†’ E := Î» x, a + (x - f a) â€¢ (f xâ‚€)â»Â¹ â€¢ xâ‚€,
  have hg : continuous g, by continuity,
  have hcomp : f âˆ˜ g = id, by { ext, simp [hxâ‚€] },
  have hgfa : g (f a) = a, by simp [hxâ‚€],
  exact nhds_le_map_nhds hg.continuous_at hcomp hgfa,
end

/-- A nonzero continuous linear functional is open. -/
lemma continuous_linear_map.is_open_map [topological_space ğ•œ] [division_ring ğ•œ]
  [topological_ring ğ•œ] [add_comm_group E] [topological_space E] [topological_add_group E]
  [module ğ•œ E] [has_continuous_smul ğ•œ E] (f : E â†’L[ğ•œ] ğ•œ) (hf : f â‰  0) :
  is_open_map f :=
begin
  refine is_open_map.of_nhds_le (Î» a, _),
  obtain âŸ¨xâ‚€, hxâ‚€âŸ© := continuous_linear_map.exists_ne_zero hf,
  let g : ğ•œ â†’ E := Î» x, a + (x - f a) â€¢ (f xâ‚€)â»Â¹ â€¢ xâ‚€,
  have hg : continuous g, by continuity,
  have hcomp : f âˆ˜ g = id, by { ext, simp [hxâ‚€] },
  have hgfa : g (f a) = a, by simp [hxâ‚€],
  exact nhds_le_map_nhds hg.continuous_at hcomp hgfa,
end

variables [normed_group E]

/-- If `A`, `B` are disjoint sets, `A` is compact and `B` is closed then we can surround them while
keeping them disjoint. -/
-- TODO: This proof uses the normed group structure of `E`, but it could work for locally convex
-- topological vector spaces: instead of taking the balls around 0 with radius 1/n, we could show
-- there must be some convex neighbourhood `W` of 0 which make `A + W` and `B + W` disjoint?
theorem exists_disjoint_add_ball {A B : set E} (hA : is_compact A) (hB : is_closed B)
  (disj : disjoint A B) :
  âˆƒ Îµ : â„, 0 < Îµ âˆ§ disjoint (A + metric.ball 0 Îµ) (B + metric.ball 0 Îµ) :=
begin
  obtain âŸ¨u, -, u_pos, u_limâŸ© := exists_seq_strict_anti_tendsto (0 : â„),
  suffices h : âˆƒ (n : â„•), disjoint (A + metric.ball 0 (u n)) (B + metric.ball 0 (u n)),
  { obtain âŸ¨n, hnâŸ© := h,
    exact âŸ¨u n, u_pos n, hnâŸ© },
  by_contra' h,
  simp only [not_disjoint_iff, set.mem_add, metric.mem_ball, dist_zero_right,
    â† exists_and_distrib_left, â† exists_and_distrib_right, and_assoc] at h,
  choose z f f' g g' hâ‚ hâ‚‚ hâ‚ƒ hâ‚„ hâ‚… hâ‚† using h,
  obtain âŸ¨w, hw, Ï†, hÏ†â‚, hÏ†â‚‚ : tendsto (f âˆ˜ _) _ _âŸ© := hA.tendsto_subseq hâ‚,
  refine disj âŸ¨hw, mem_of_is_closed_sequential hB (Î» n, hâ‚„ (Ï† n)) _âŸ©,
  suffices hfg : tendsto (f - g) at_top (ğ“ 0),
  { simpa only [sub_sub_cancel, sub_zero, comp_app, pi.sub_apply]
      using hÏ†â‚‚.sub (hfg.comp hÏ†â‚.tendsto_at_top) },
  suffices : âˆ€ n, âˆ¥(f - g) nâˆ¥ â‰¤ 2 * u n,
  { apply squeeze_zero_norm this,
    rw â†mul_zero (2 : â„),
    exact u_lim.const_mul (2:â„) },
  intro n,
  have : f n - g n = g' n - f' n,
  { rw [sub_eq_iff_eq_add', â†add_sub_assoc, hâ‚†, â†hâ‚ƒ, add_sub_cancel] },
  rw [pi.sub_apply, this, two_mul],
  exact (norm_sub_le _ _).trans (add_le_add (hâ‚… n).le (hâ‚‚ n).le),
end

variables [normed_space â„ E]

/-- Given a set `C` which is a convex neighbourhood of `0` and a point `xâ‚€` outside of it, there is
a continuous linear functional `f` separating `x0` and `C`, in the sense that it sends `xâ‚€` to 1 and
all of `C` to values strictly below `1`. -/
lemma separate_convex_open_set {C : set E} (zero_mem : (0:E) âˆˆ C) (hC : convex â„ C)
  (hCâ‚‚ : is_open C) {xâ‚€ : E} (hxâ‚€ : xâ‚€ âˆ‰ C) :
  âˆƒ (f : E â†’L[â„] â„), f xâ‚€ = 1 âˆ§ âˆ€ x âˆˆ C, f x < 1 :=
begin
  let f : linear_pmap â„ E â„ :=
    linear_pmap.mk_span_singleton xâ‚€ 1 (ne_of_mem_of_not_mem zero_mem hxâ‚€).symm,
  have hfxâ‚€ : f âŸ¨(1:â„) â€¢ xâ‚€, by { dsimp, rw submodule.mem_span_singleton, exact âŸ¨1, rflâŸ© }âŸ© = 1,
  { simp_rw [linear_pmap.mk_span_singleton_apply, one_smul] },
  obtain âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ© := exists_extension_of_le_sublinear f (gauge C) _ _ _,
  { refine âŸ¨âŸ¨Ï†, (Ï†.to_add_monoid_hom.uniform_continuous_of_continuous_at_zero _).continuousâŸ©, _, _âŸ©,
    { change tendsto _ _ _,
      rw (nhds_basis_opens (0:E)).tendsto_iff metric.nhds_basis_ball,
      refine Î» Îµ hÎµ, âŸ¨(Îµ â€¢ C) âˆ© (-Îµ â€¢ C), âŸ¨âŸ¨_, _âŸ©, _âŸ©, _âŸ©,
      { exact mem_smul_set.mpr âŸ¨0, zero_mem, smul_zero _âŸ© },
      { exact mem_smul_set.mpr âŸ¨0, zero_mem, smul_zero _âŸ© },
      { exact (is_open_map_smulâ‚€ hÎµ.ne' _ hCâ‚‚).inter
          (is_open_map_smulâ‚€ (neg_ne_zero.mpr hÎµ.ne.symm) _ hCâ‚‚) },
      rintro x âŸ¨hxâ‚, hxâ‚‚âŸ©,
      have : âˆ¥Ï† xâˆ¥ < Îµ,
      { rw [real.norm_eq_abs, abs_lt, neg_lt, â†linear_map.map_neg],
        split; apply (hÏ†â‚‚ _).trans_lt,
        { refine gauge_lt_of_mem_smul (-x) Îµ hÎµ zero_mem hC hCâ‚‚ _,
          rw [mem_smul_set_iff_inv_smul_memâ‚€ hÎµ.ne', smul_neg],
          rwa [mem_smul_set_iff_inv_smul_memâ‚€ (neg_ne_zero.mpr hÎµ.ne'), inv_neg, neg_smul] at hxâ‚‚ },
        { exact gauge_lt_of_mem_smul x Îµ hÎµ zero_mem hC hCâ‚‚ hxâ‚ } },
      simp [this] },
    { dsimp,
      have : xâ‚€ âˆˆ f.domain := submodule.mem_span_singleton_self _,
      rw [â†submodule.coe_mk xâ‚€ this, hÏ†â‚, â† hfxâ‚€],
      congr,
      rw one_smul },
    { exact Î» x hx, (hÏ†â‚‚ x).trans_lt (gauge_lt_one_of_mem_of_open hC zero_mem hCâ‚‚ _ hx) } },
  { simp_rw â† smul_eq_mul,
    exact Î» c hc x, gauge_smul_of_nonneg hc.le x },
  { exact gauge_add_le hC (absorbent_nhds_zero (hCâ‚‚.mem_nhds zero_mem)) },
  { rintro âŸ¨x, hxâŸ©,
    obtain âŸ¨y, rflâŸ© := submodule.mem_span_singleton.1 hx,
    rw linear_pmap.mk_span_singleton_apply,
    simp only [mul_one, algebra.id.smul_eq_mul, submodule.coe_mk],
    obtain h | h := le_or_lt y 0,
    { exact h.trans (gauge_nonneg _) },
    { rw [gauge_smul_of_nonneg h.le, smul_eq_mul, le_mul_iff_one_le_right h],
      exact one_le_gauge_of_not_mem hC zero_mem hCâ‚‚ hxâ‚€,
      apply_instance } }
end

end

variables [normed_group E] [normed_space â„ E]

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `A`, `B` where `A` is open,
there is a continuous linear functional which separates them. -/
theorem geometric_hahn_banach_open {A B : set E}
  (hAâ‚ : convex â„ A) (hAâ‚‚ : is_open A) (hB : convex â„ B) (disj : disjoint A B) :
  âˆƒ (f : E â†’L[â„] â„) (s : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ (âˆ€ b âˆˆ B, s â‰¤ f b) :=
begin
  rcases A.eq_empty_or_nonempty with (rfl | âŸ¨aâ‚€, haâ‚€âŸ©),
  { exact âŸ¨0, 0, by simp, Î» b hb, by simpâŸ© },
  rcases B.eq_empty_or_nonempty with (rfl | âŸ¨bâ‚€, hbâ‚€âŸ©),
  { exact âŸ¨0, 1, Î» a ha, by norm_num, by simpâŸ© },
  let xâ‚€ := bâ‚€ - aâ‚€,
  let C := {xâ‚€} + A + -B,
  have : (0:E) âˆˆ C := âŸ¨_ + aâ‚€, -bâ‚€, add_mem_add rfl haâ‚€, neg_mem_neg.2 hbâ‚€, by simpâŸ©,
  have : convex â„ C := ((convex_singleton _).add hAâ‚).add hB.neg_preimage,
  have : xâ‚€ âˆ‰ C,
  { intro hxâ‚€,
    simp only [mem_add, mem_singleton_iff, mem_neg, exists_eq_left, exists_exists_and_eq_and,
      exists_and_distrib_left, add_assoc xâ‚€, add_right_eq_self] at hxâ‚€,
    obtain âŸ¨a, ha, b, hb, habâŸ© := hxâ‚€,
    rw â†eq_neg_of_add_eq_zero hab at hb,
    exact disj âŸ¨ha, hbâŸ© },
  obtain âŸ¨f, hfâ‚, hfâ‚‚âŸ© := separate_convex_open_set â€¹0 âˆˆ Câ€º â€¹_â€º hAâ‚‚.add_left.add_right â€¹xâ‚€ âˆ‰ Câ€º,
  have : f bâ‚€ = f aâ‚€ + 1,
  { simp [â†hfâ‚] },
  have forall_lt : âˆ€ (a âˆˆ A) (b âˆˆ B), f a < f b,
  { intros a ha b hb,
    have := hfâ‚‚ (xâ‚€ + a + -b) (add_mem_add (add_mem_add rfl ha) (neg_mem_neg.2 hb)),
    simp only [f.map_neg, f.map_add, f.map_sub, hfâ‚] at this,
    linarith },
  refine âŸ¨f, Inf (f '' B), _, _âŸ©,
  { suffices : f '' A âŠ† Iio (Inf (f '' B)),
    { intros a ha,
      apply this âŸ¨_, ha, rflâŸ© },
    rw â†interior_Iic,
    apply interior_maximal,
    { rintro _ âŸ¨a, ha, rflâŸ©,
      apply le_cInf âŸ¨f bâ‚€, _âŸ©,
      { rintro _ âŸ¨b', hb, rflâŸ©,
        exact (forall_lt _ ha _ hb).le },
      { exact mem_image_of_mem _ hbâ‚€ } },
    refine f.is_open_map _ _ hAâ‚‚,
    rintro rfl,
    simpa using hfâ‚ },
  { intros b hb,
    apply cInf_le âŸ¨f aâ‚€, _âŸ© (mem_image_of_mem _ hb),
    rintro _ âŸ¨b', hb', rflâŸ©,
    exact (forall_lt _ haâ‚€ _ hb').le },
end

theorem geometric_hahn_banach_open_point {A : set E} {x : E} (hAâ‚ : convex â„ A) (hAâ‚‚ : is_open A)
  (disj : x âˆ‰ A) :
  âˆƒ (f : E â†’L[â„] â„), (âˆ€ a âˆˆ A, f a < f x) :=
let âŸ¨f, s, hA, hxâŸ© := geometric_hahn_banach_open hAâ‚ hAâ‚‚ (convex_singleton x)
  (disjoint_singleton_right.2 disj)
  in âŸ¨f, Î» a ha, lt_of_lt_of_le (hA a ha) (hx x (mem_singleton _))âŸ©

theorem geometric_hahn_banach_point_open {x : E} {B : set E} (hBâ‚ : convex â„ B) (hBâ‚‚ : is_open B)
  (disj : x âˆ‰ B) :
  âˆƒ (f : E â†’L[â„] â„), (âˆ€ b âˆˆ B, f x < f b) :=
let âŸ¨f, hfâŸ© := geometric_hahn_banach_open_point hBâ‚ hBâ‚‚ disj in âŸ¨-f, by simpaâŸ©

theorem geometric_hahn_banach_open_open {A B : set E} (hAâ‚ : convex â„ A) (hAâ‚‚ : is_open A)
  (hBâ‚ : convex â„ B) (hBâ‚ƒ : is_open B) (disj : disjoint A B) :
  âˆƒ (f : E â†’L[â„] â„) (s : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ (âˆ€ b âˆˆ B, s < f b) :=
begin
  obtain (rfl | âŸ¨aâ‚€, haâ‚€âŸ©) := A.eq_empty_or_nonempty,
  { exact âŸ¨0, -1, by simp, Î» b hb, by norm_numâŸ© },
  obtain (rfl | âŸ¨bâ‚€, hbâ‚€âŸ©) := B.eq_empty_or_nonempty,
  { exact âŸ¨0, 1, Î» a ha, by norm_num, by simpâŸ© },
  obtain âŸ¨f, s, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open hAâ‚ hAâ‚‚ hBâ‚ disj,
  have : f â‰  0,
  { rintro rfl,
    exact (hfâ‚ _ haâ‚€).not_le (hfâ‚‚ _ hbâ‚€) },
  have : is_open_map f := f.is_open_map this,
  refine âŸ¨f, s, hfâ‚, _âŸ©,
  suffices : f '' B âŠ† Ioi s,
  { exact Î» b hb, this âŸ¨b, â€¹_â€º, rflâŸ© },
  rw â†interior_Ici,
  refine interior_maximal _ (this _ hBâ‚ƒ),
  rintro _ âŸ¨_, _, rflâŸ©,
  exact hfâ‚‚ _ â€¹_â€º,
end

open filter
open_locale topological_space

/-- If `A`, `B` are disjoint convex sets, `A` is compact and `B` is closed then we can find open
disjoint convex sets containing them. -/
-- TODO: This proof uses the normed space structure of `E`, but it could work for locally convex
-- topological vector spaces: instead of taking the balls around 0 with radius 1/n, we could show
-- there must be some convex neighbourhood `W` of 0 which make `A + W` and `B + W` disjoint?
theorem closed_compact_separate {A B : set E} (hAâ‚ : convex â„ A) (hAâ‚‚ : is_compact A)
  (hBâ‚ : convex â„ B) (hBâ‚‚ : is_closed B) (disj : disjoint A B) :
  âˆƒ U V, is_open U âˆ§ is_open V âˆ§ convex â„ U âˆ§ convex â„ V âˆ§ A âŠ† U âˆ§ B âŠ† V âˆ§ disjoint U V :=
let âŸ¨Îµ, hÎµ, hABâŸ© := exists_disjoint_add_ball hAâ‚‚ hBâ‚‚ disj in
  âŸ¨_, _, metric.is_open_ball.add_left, metric.is_open_ball.add_left,
    hAâ‚.add (convex_ball 0 _), hBâ‚.add (convex_ball 0 _),
    subset_add_left A (metric.mem_ball_self hÎµ), subset_add_left B (metric.mem_ball_self hÎµ), hABâŸ©

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `A`, `B` where `A` is compact
and `B` is closed, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_compact_closed {A B : set E} (hAâ‚ : convex â„ A) (hAâ‚‚ : is_compact A)
  (hBâ‚ : convex â„ B) (hBâ‚‚ : is_closed B) (disj : disjoint A B) :
  âˆƒ (f : E â†’L[â„] â„) (s t : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ s < t âˆ§ (âˆ€ b âˆˆ B, t < f b) :=
begin
  obtain rfl | hA := A.eq_empty_or_nonempty,
  { exact âŸ¨0, -2, -1, by simp, by norm_num, Î» b hb, by norm_numâŸ© },
  tactic.unfreeze_local_instances,
  obtain rfl | hB := B.eq_empty_or_nonempty,
  { exact âŸ¨0, 1, 2, Î» a ha, by norm_num, by norm_num, by simpâŸ© },
  obtain âŸ¨U, V, hU, hV, hUâ‚, hVâ‚, AU, BV, disj'âŸ© := closed_compact_separate hAâ‚ hAâ‚‚ hBâ‚ hBâ‚‚ disj,
  obtain âŸ¨f, s, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open_open hUâ‚ hU hVâ‚ hV disj',
  obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := hAâ‚‚.exists_forall_ge hA f.continuous.continuous_on,
  have : Sup (f '' A) = f x,
  { apply le_antisymm (cSup_le (hA.image f) (by simpa)),
    exact le_cSup âŸ¨f x, by simpa [upper_bounds]âŸ© âŸ¨_, hxâ‚, rflâŸ© },
  have : f x < s := hfâ‚ x (AU hxâ‚),
  exact âŸ¨f, (f x + s)/2, s, Î» a ha, by linarith [hxâ‚‚ a ha], by linarith, Î» b hb, hfâ‚‚ b (BV hb)âŸ©,
end

/-- A version of the Hahn-Banach theorem: given disjoint convex subsets `A,B` where `A` is closed,
and `B` is compact, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_closed_compact {A B : set E} (hAâ‚ : convex â„ A) (hAâ‚‚ : is_closed A)
  (hBâ‚ : convex â„ B) (hBâ‚‚ : is_compact B) (disj : disjoint A B) :
  âˆƒ (f : E â†’L[â„] â„) (s t : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ s < t âˆ§ (âˆ€ b âˆˆ B, t < f b) :=
let âŸ¨f, s, t, hs, st, htâŸ© := geometric_hahn_banach_compact_closed hBâ‚ hBâ‚‚ hAâ‚ hAâ‚‚ disj.symm in
âŸ¨-f, -t, -s, by simpa using ht, by simpa using st, by simpa using hsâŸ©

theorem geometric_hahn_banach_point_closed {x : E} {B : set E} (hBâ‚ : convex â„ B)
  (hBâ‚‚ : is_closed B) (disj : x âˆ‰ B) :
  âˆƒ (f : E â†’L[â„] â„) (s : â„), f x < s âˆ§ (âˆ€ b âˆˆ B, s < f b) :=
let âŸ¨f, s, t, ha, hst, hbâŸ© := geometric_hahn_banach_compact_closed (convex_singleton x)
  is_compact_singleton hBâ‚ hBâ‚‚ (disjoint_singleton_left.2 disj)
  in âŸ¨f, t, lt_trans (ha x (mem_singleton _)) hst, hbâŸ©

theorem geometric_hahn_banach_closed_point {A : set E} {x : E} (hAâ‚ : convex â„ A)
  (hAâ‚‚ : is_closed A) (disj : x âˆ‰ A) :
  âˆƒ (f : E â†’L[â„] â„) (s : â„), (âˆ€ a âˆˆ A, f a < s) âˆ§ s < f x :=
let âŸ¨f, s, t, ha, hst, hbâŸ© := geometric_hahn_banach_closed_compact hAâ‚ hAâ‚‚ (convex_singleton x)
  is_compact_singleton (disjoint_singleton_right.2 disj)
  in âŸ¨f, s, ha, lt_trans hst (hb x (mem_singleton _))âŸ©

theorem geometric_hahn_banach_point_point {x y : E} (hxy : x â‰  y) : âˆƒ (f : E â†’L[â„] â„), f x < f y :=
begin
  obtain âŸ¨f, s, t, hs, st, htâŸ© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) is_compact_singleton
      (convex_singleton y) is_closed_singleton (disjoint_singleton.2 hxy),
  exact âŸ¨f, by linarith [hs x rfl, ht y rfl]âŸ©,
end
