/-
Copyright (c) 2022 Bhavik Mehta All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Ya√´l Dillies
-/
import analysis.convex.cone
import analysis.convex.topology
import analysis.normed.group.pointwise
import analysis.seminorm
import tactic.by_contra

/-!
# Separation Hahn-Banach theorem

In this file we prove the geometric Hahn-Banach theorem. For any two disjoint convex sets, there
exists a continuous linear functional separating them, geometrically meaning that we can intercalate
a plane between them.

We provide many variations to stricten the result under more assumptions on the convex sets:
* `geometric_hahn_banach_open`: One set is open. Weak separation.
* `geometric_hahn_banach_open_point`, `geometric_hahn_banach_point_open`: One set is open, the
  other is a singleton. Weak separation.
* `geometric_hahn_banach_open_open`: Both sets are open. Semistrict separation.
* `geometric_hahn_banach_compact_closed`, `geometric_hahn_banach_closed_compact`: One set is closed,
  the other one is compact. Strict separation.
* `geometric_hahn_banach_point_closed`, `geometric_hahn_banach_closed_point`: One set is closed, the
  other one is a singleton. Strict separation.
* `geometric_hahn_banach_point_point`: Both sets are singletons. Strict separation.
-/

open function set
open_locale pointwise

variables {ùïú E : Type*}

namespace set
variables {Œ± : Type*}

@[to_additive]
lemma bUnion_mul {Œπ : Sort*} [has_mul Œ±] (f : Œπ ‚Üí set Œ±) (s : set Œπ) (t : set Œ±) :
  (‚ãÉ i ‚àà s, f i) * t = ‚ãÉ i ‚àà s, (f i * t) :=
by simp_rw [Union_mul]

@[to_additive]
lemma mul_bUnion {Œπ : Sort*} [has_mul Œ±] (f : Œπ ‚Üí set Œ±) (s : set Œπ) (t : set Œ±) :
  t * (‚ãÉ i ‚àà s, f i) = ‚ãÉ i ‚àà s, (t * f i) :=
by simp_rw [mul_Union]

end set

open set

namespace metric
section
variables [pseudo_metric_space E]

open filter
open_locale topological_space

lemma exists_disjoint_thickenings {s t : set E} (hs : is_compact s) (ht : is_closed t)
  (hst : disjoint s t) :
  ‚àÉ Œµ, 0 < Œµ ‚àß disjoint (thickening Œµ s) (thickening Œµ t) :=
begin
  obtain ‚ü®u, -, u_pos, u_lim‚ü© := exists_seq_strict_anti_tendsto (0 : ‚Ñù),
  suffices h : ‚àÉ (n : ‚Ñï), disjoint (thickening (u n) s) (thickening (u n) t),
  { obtain ‚ü®n, hn‚ü© := h,
    exact ‚ü®u n, u_pos n, hn‚ü© },
  by_contra' h,
  -- have := hs.exists_forall_le,
  simp only [not_disjoint_iff, mem_thickening_iff,
    ‚Üê exists_and_distrib_left, ‚Üê exists_and_distrib_right, and_assoc] at h,
  choose x y hy z hz hxy hxz using h,
  obtain ‚ü®w, hw, œÜ, hœÜ, hyœÜ : tendsto (y ‚àò _) _ _‚ü© := hs.tendsto_subseq hy,
  have h : ‚àÄ n, dist (y n) (z n) ‚â§ u n + u n,
    from Œª n, (dist_triangle_left _ _ _).trans (add_le_add (hxy n).le (hxz n).le),
  refine hst ‚ü®hw, mem_of_is_closed_sequential ht (Œª n, hz (œÜ n)) $ (tendsto_iff_of_dist _).1 hyœÜ‚ü©,
  refine squeeze_zero (Œª _, dist_nonneg) (Œª n, h _) _,
  simp_rw ‚Üêtwo_mul,
  rw ‚Üêmul_zero (2 : ‚Ñù),
  exact (u_lim.const_mul (2 : ‚Ñù)).comp hœÜ.tendsto_at_top,
end

end

variables [semi_normed_group E]

@[simp] lemma add_ball (s : set E) (Œµ : ‚Ñù) : s + ball 0 Œµ = thickening Œµ s :=
begin
  rw thickening_eq_bUnion_ball,
  convert bUnion_add _ s (ball (0 : E) Œµ),
  exact s.bUnion_of_singleton.symm,
  ext x y,
  simp_rw [singleton_add_ball, add_zero],
end

@[simp] lemma ball_add (s : set E) (Œµ : ‚Ñù) : ball 0 Œµ + s = thickening Œµ s :=
by rw [add_comm, add_ball]


variables (ùïú E) [normed_field ùïú] [semi_normed_space ùïú E] {r : ‚Ñù}

/-- The norm of a seminormed group as a seminorm. -/
def _root_.norm_seminorm : seminorm ùïú E :=
{ to_fun := norm,
  smul' := norm_smul,
  triangle' := norm_add_le }

@[simp] lemma _root_.coe_norm_seminorm : ‚áë(norm_seminorm ùïú E) = norm := rfl

@[simp] lemma _root_.ball_norm_seminorm : (norm_seminorm ùïú E).ball = ball :=
by { ext x r y, simp only [seminorm.mem_ball, mem_ball, coe_norm_seminorm, dist_eq_norm] }

variables {ùïú E}

/-- Balls at the origin are absorbent. -/
lemma absorbent_ball_zero (hr : 0 < r) : absorbent ùïú (ball (0 : E) r) :=
by { convert (norm_seminorm _ _).absorbent_ball_zero hr, rw ball_norm_seminorm }

/-- Balls containing the origin are absorbent. -/
lemma absorbent_ball {x : E} (hx : ‚à•x‚à• < r) : absorbent ùïú (ball x r) :=
by { convert (norm_seminorm _ _).absorbent_ball hx, rw ball_norm_seminorm }

end metric

open metric

lemma convex.thickening [normed_group E] [normed_space ‚Ñù E] {s : set E} (hs : convex ‚Ñù s) (Œµ : ‚Ñù) :
  convex ‚Ñù (thickening Œµ s) :=
by { rw ‚Üêadd_ball, exact hs.add (convex_ball 0 _) }

lemma convex.cthickening [normed_group E] [normed_space ‚Ñù E] {s : set E} (hs : convex ‚Ñù s) (Œµ : ‚Ñù) :
  convex ‚Ñù (cthickening Œµ s) :=
begin
  obtain hŒµ | hŒµ := le_total 0 Œµ,
  { rw cthickening_eq_Inter_thickening hŒµ,
    exact convex_bInter (Œª _ _, hs.thickening _) },
  { rw cthickening_of_nonpos hŒµ,
    exact hs.closure }
end

section
open filter
open_locale topological_space

lemma linear_map.exists_ne_zero {R‚ÇÅ R‚ÇÇ : Type*} [semiring R‚ÇÅ] [semiring R‚ÇÇ] {œÉ‚ÇÅ‚ÇÇ : R‚ÇÅ ‚Üí+* R‚ÇÇ}
  {M‚ÇÅ : Type*} [add_comm_monoid M‚ÇÅ] {M‚ÇÇ : Type*} [add_comm_monoid M‚ÇÇ] [module R‚ÇÅ M‚ÇÅ] [module R‚ÇÇ M‚ÇÇ]
  {f : M‚ÇÅ ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ} (hf : f ‚â† 0) :
  ‚àÉ x, f x ‚â† 0 :=
by { by_contra' h, exact hf (linear_map.ext h) }

lemma continuous_linear_map.exists_ne_zero {R‚ÇÅ R‚ÇÇ : Type*} [semiring R‚ÇÅ]
  [semiring R‚ÇÇ] {œÉ‚ÇÅ‚ÇÇ : R‚ÇÅ ‚Üí+* R‚ÇÇ} {M‚ÇÅ : Type*} [topological_space M‚ÇÅ] [add_comm_monoid M‚ÇÅ]
  {M‚ÇÇ : Type*} [topological_space M‚ÇÇ] [add_comm_monoid M‚ÇÇ] [module R‚ÇÅ M‚ÇÅ] [module R‚ÇÇ M‚ÇÇ]
  {f : M‚ÇÅ ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] M‚ÇÇ} (hf : f ‚â† 0) :
  ‚àÉ x, f x ‚â† 0 :=
by { by_contra' h, exact hf (continuous_linear_map.ext h) }

lemma nhds_le_map_nhds [topological_space ùïú] [topological_space E] {f : E ‚Üí ùïú} {g : ùïú ‚Üí E} {a : E}
  (hg : continuous_at g (f a)) (hcomp : f ‚àò g = id) (hgfa : g (f a) = a) :
  ùìù (f a) ‚â§ map f (ùìù a) :=
calc ùìù (f a) = ((ùìù (f a)).map g).map f : by rw [map_map, hcomp, map_id]
  ... ‚â§ (ùìù $ g (f a)).map f             : map_mono hg
  ... = (ùìù a).map f                     : by rw hgfa

lemma linear_map.nhds_le_map_nhds [topological_space ùïú] [division_ring ùïú] [topological_ring ùïú]
  [add_comm_group E] [topological_space E] [topological_add_group E] [module ùïú E]
  [has_continuous_smul ùïú E] {f : E ‚Üí‚Çó[ùïú] ùïú} (hf : f ‚â† 0) (a : E) :
  ùìù (f a) ‚â§ map f (ùìù a) :=
begin
  obtain ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü© := linear_map.exists_ne_zero hf,
  let g : ùïú ‚Üí E := Œª x, a + (x - f a) ‚Ä¢ (f x‚ÇÄ)‚Åª¬π ‚Ä¢ x‚ÇÄ,
  have hg : continuous g, by continuity,
  have hcomp : f ‚àò g = id, by { ext, simp [hx‚ÇÄ] },
  have hgfa : g (f a) = a, by simp [hx‚ÇÄ],
  exact nhds_le_map_nhds hg.continuous_at hcomp hgfa,
end

/-- A nonzero continuous linear functional is open. -/
lemma continuous_linear_map.is_open_map [topological_space ùïú] [division_ring ùïú]
  [topological_ring ùïú] [add_comm_group E] [topological_space E] [topological_add_group E]
  [module ùïú E] [has_continuous_smul ùïú E] (f : E ‚ÜíL[ùïú] ùïú) (hf : f ‚â† 0) :
  is_open_map f :=
begin
  refine is_open_map.of_nhds_le (Œª a, _),
  obtain ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü© := continuous_linear_map.exists_ne_zero hf,
  let g : ùïú ‚Üí E := Œª x, a + (x - f a) ‚Ä¢ (f x‚ÇÄ)‚Åª¬π ‚Ä¢ x‚ÇÄ,
  have hg : continuous g, by continuity,
  have hcomp : f ‚àò g = id, by { ext, simp [hx‚ÇÄ] },
  have hgfa : g (f a) = a, by simp [hx‚ÇÄ],
  exact nhds_le_map_nhds hg.continuous_at hcomp hgfa,
end

variables [normed_group E] [normed_space ‚Ñù E]

lemma seminorm.exists_extension (f : linear_pmap ‚Ñù E ‚Ñù) (p : seminorm ‚Ñù E)
  (hf : ‚àÄ x : f.domain, f x ‚â§ p x) :
  ‚àÉ g : E ‚Üí‚Çó[‚Ñù] ‚Ñù, (‚àÄ x : f.domain, g x = f x) ‚àß ‚àÄ x, g x ‚â§ p x :=
exists_extension_of_le_sublinear f p (Œª c hc x, by rw [p.smul, real.norm_of_nonneg hc.le])
  p.triangle hf

/-- Given a set `C` which is a convex neighbourhood of `0` and a point `x‚ÇÄ` outside of it, there is
a continuous linear functional `f` separating `x‚ÇÄ` and `C`, in the sense that it sends `x‚ÇÄ` to 1 and
all of `C` to values strictly below `1`. -/
lemma separate_convex_open_set {C : set E} (hC‚ÇÄ : (0:E) ‚àà C) (hC‚ÇÅ : convex ‚Ñù C)
  (hC‚ÇÇ : is_open C) {x‚ÇÄ : E} (hx‚ÇÄ : x‚ÇÄ ‚àâ C) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), f x‚ÇÄ = 1 ‚àß ‚àÄ x ‚àà C, f x < 1 :=
begin
  let f : linear_pmap ‚Ñù E ‚Ñù :=
    linear_pmap.mk_span_singleton x‚ÇÄ 1 (ne_of_mem_of_not_mem hC‚ÇÄ hx‚ÇÄ).symm,
  have hfx‚ÇÄ : f ‚ü®(1:‚Ñù) ‚Ä¢ x‚ÇÄ, by { dsimp, rw submodule.mem_span_singleton, exact ‚ü®1, rfl‚ü© }‚ü© = 1,
  { simp_rw [linear_pmap.mk_span_singleton_apply, one_smul] },
  have : C ‚à© (-C) ‚àà ùìù (0:E),
    from inter_mem (hC‚ÇÇ.mem_nhds hC‚ÇÄ) (hC‚ÇÇ.neg.mem_nhds $ by rwa [mem_neg, neg_zero]),
  obtain ‚ü®r, hr, hrC‚ü© := metric.mem_nhds_iff.1 this,
  have hC‚ÇÄ' : ‚àÄ x, x ‚àà C ‚à© -C ‚Üí -x ‚àà C ‚à© -C := Œª x h,
    by rwa [‚Üêmem_neg, inter_neg, set.neg_neg, inter_comm],
  have hC‚ÇÅ' : convex ‚Ñù (C ‚à© -C) := hC‚ÇÅ.inter hC‚ÇÅ.neg,
  obtain ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ‚ü© := (gauge_seminorm hC‚ÇÄ' hC‚ÇÅ' $
    (absorbent_ball_zero hr).subset hrC).exists_extension f _,
  { refine ‚ü®œÜ.mk_continuous (r‚Åª¬π) _, _, _‚ü©,
    { intros x,
      rw [real.norm_eq_abs, abs_le, neg_le, ‚Üêlinear_map.map_neg],
      split; apply (hœÜ‚ÇÇ _).trans _,
      { sorry },
      { sorry } },
    { dsimp,
      have : x‚ÇÄ ‚àà f.domain := submodule.mem_span_singleton_self _,
      rw [‚Üêsubmodule.coe_mk x‚ÇÄ this, hœÜ‚ÇÅ, ‚Üêhfx‚ÇÄ],
      congr,
      rw one_smul },
    { exact Œª x hx, (hœÜ‚ÇÇ x).trans_lt (gauge_lt_one_of_mem_of_open hC zero_mem hC‚ÇÇ _ hx) } },
  { rintro ‚ü®x, hx‚ü©,
    obtain ‚ü®y, rfl‚ü© := submodule.mem_span_singleton.1 hx,
    rw linear_pmap.mk_span_singleton_apply,
    simp only [mul_one, algebra.id.smul_eq_mul, submodule.coe_mk],
    obtain h | h := le_or_lt y 0,
    { exact h.trans (gauge_nonneg _) },
    { rw [seminorm.smul, real.norm_of_nonneg h.le, le_mul_iff_one_le_right h],
      exact one_le_gauge_of_not_mem hC‚ÇÅ' hC‚ÇÄ' hC‚ÇÇ hx‚ÇÄ,
      apply_instance } }
end

/-- Given a set `C` which is a convex neighbourhood of `0` and a point `x‚ÇÄ` outside of it, there is
a continuous linear functional `f` separating `x‚ÇÄ` and `C`, in the sense that it sends `x‚ÇÄ` to 1 and
all of `C` to values strictly below `1`. -/
lemma separate_convex_open_set' {C : set E} (zero_mem : (0:E) ‚àà C) (hC : convex ‚Ñù C)
  (hC‚ÇÇ : is_open C) {x‚ÇÄ : E} (hx‚ÇÄ : x‚ÇÄ ‚àâ C) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), f x‚ÇÄ = 1 ‚àß ‚àÄ x ‚àà C, f x < 1 :=
begin
  let f : linear_pmap ‚Ñù E ‚Ñù :=
    linear_pmap.mk_span_singleton x‚ÇÄ 1 (ne_of_mem_of_not_mem zero_mem hx‚ÇÄ).symm,
  have hfx‚ÇÄ : f ‚ü®(1:‚Ñù) ‚Ä¢ x‚ÇÄ, by { dsimp, rw submodule.mem_span_singleton, exact ‚ü®1, rfl‚ü© }‚ü© = 1,
  { simp_rw [linear_pmap.mk_span_singleton_apply, one_smul] },
  obtain ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ‚ü© := exists_extension_of_le_sublinear f (gauge C) _ _ _,
  { refine ‚ü®‚ü®œÜ, (œÜ.to_add_monoid_hom.uniform_continuous_of_continuous_at_zero _).continuous‚ü©, _, _‚ü©,
    { change tendsto _ _ _,
      rw (nhds_basis_opens (0:E)).tendsto_iff nhds_basis_ball,
      refine Œª Œµ hŒµ, ‚ü®(Œµ ‚Ä¢ C) ‚à© (-Œµ ‚Ä¢ C), ‚ü®‚ü®_, _‚ü©, _‚ü©, _‚ü©,
      { exact mem_smul_set.mpr ‚ü®0, zero_mem, smul_zero _‚ü© },
      { exact mem_smul_set.mpr ‚ü®0, zero_mem, smul_zero _‚ü© },
      { exact (is_open_map_smul‚ÇÄ hŒµ.ne' _ hC‚ÇÇ).inter
          (is_open_map_smul‚ÇÄ (neg_ne_zero.mpr hŒµ.ne.symm) _ hC‚ÇÇ) },
      rintro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©,
      have : ‚à•œÜ x‚à• < Œµ,
      { rw [real.norm_eq_abs, abs_lt, neg_lt, ‚Üêlinear_map.map_neg],
        split; apply (hœÜ‚ÇÇ _).trans_lt,
        { refine gauge_lt_of_mem_smul (-x) Œµ hŒµ zero_mem hC hC‚ÇÇ _,
          rw [mem_smul_set_iff_inv_smul_mem‚ÇÄ hŒµ.ne', smul_neg],
          rwa [mem_smul_set_iff_inv_smul_mem‚ÇÄ (neg_ne_zero.mpr hŒµ.ne'), inv_neg, neg_smul] at hx‚ÇÇ },
        { exact gauge_lt_of_mem_smul x Œµ hŒµ zero_mem hC hC‚ÇÇ hx‚ÇÅ } },
      simp [this] },
    { dsimp,
      have : x‚ÇÄ ‚àà f.domain := submodule.mem_span_singleton_self _,
      rw [‚Üêsubmodule.coe_mk x‚ÇÄ this, hœÜ‚ÇÅ, ‚Üê hfx‚ÇÄ],
      congr,
      rw one_smul },
    { exact Œª x hx, (hœÜ‚ÇÇ x).trans_lt (gauge_lt_one_of_mem_of_open hC zero_mem hC‚ÇÇ _ hx) } },
  { simp_rw ‚Üê smul_eq_mul,
    exact Œª c hc x, gauge_smul_of_nonneg hc.le x },
  { exact gauge_add_le hC (absorbent_nhds_zero (hC‚ÇÇ.mem_nhds zero_mem)) },
  { rintro ‚ü®x, hx‚ü©,
    obtain ‚ü®y, rfl‚ü© := submodule.mem_span_singleton.1 hx,
    rw linear_pmap.mk_span_singleton_apply,
    simp only [mul_one, algebra.id.smul_eq_mul, submodule.coe_mk],
    obtain h | h := le_or_lt y 0,
    { exact h.trans (gauge_nonneg _) },
    { rw [gauge_smul_of_nonneg h.le, smul_eq_mul, le_mul_iff_one_le_right h],
      exact one_le_gauge_of_not_mem hC zero_mem hC‚ÇÇ hx‚ÇÄ,
      apply_instance } }
end

end

variables [normed_group E] [normed_space ‚Ñù E]

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `A`, `B` where `A` is open,
there is a continuous linear functional which separates them. -/
theorem geometric_hahn_banach_open {A B : set E}
  (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_open A) (hB : convex ‚Ñù B) (disj : disjoint A B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß (‚àÄ b ‚àà B, s ‚â§ f b) :=
begin
  rcases A.eq_empty_or_nonempty with (rfl | ‚ü®a‚ÇÄ, ha‚ÇÄ‚ü©),
  { exact ‚ü®0, 0, by simp, Œª b hb, by simp‚ü© },
  rcases B.eq_empty_or_nonempty with (rfl | ‚ü®b‚ÇÄ, hb‚ÇÄ‚ü©),
  { exact ‚ü®0, 1, Œª a ha, by norm_num, by simp‚ü© },
  let x‚ÇÄ := b‚ÇÄ - a‚ÇÄ,
  let C := {x‚ÇÄ} + A + -B,
  have : (0:E) ‚àà C := ‚ü®_ + a‚ÇÄ, -b‚ÇÄ, add_mem_add rfl ha‚ÇÄ, neg_mem_neg.2 hb‚ÇÄ, by simp‚ü©,
  have : convex ‚Ñù C := ((convex_singleton _).add hA‚ÇÅ).add hB.neg_preimage,
  have : x‚ÇÄ ‚àâ C,
  { intro hx‚ÇÄ,
    simp only [mem_add, mem_singleton_iff, mem_neg, exists_eq_left, exists_exists_and_eq_and,
      exists_and_distrib_left, add_assoc x‚ÇÄ, add_right_eq_self] at hx‚ÇÄ,
    obtain ‚ü®a, ha, b, hb, hab‚ü© := hx‚ÇÄ,
    rw ‚Üêeq_neg_of_add_eq_zero hab at hb,
    exact disj ‚ü®ha, hb‚ü© },
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := separate_convex_open_set ‚Äπ0 ‚àà C‚Ä∫ ‚Äπ_‚Ä∫ hA‚ÇÇ.add_left.add_right ‚Äπx‚ÇÄ ‚àâ C‚Ä∫,
  have : f b‚ÇÄ = f a‚ÇÄ + 1,
  { simp [‚Üêhf‚ÇÅ] },
  have forall_lt : ‚àÄ (a ‚àà A) (b ‚àà B), f a < f b,
  { intros a ha b hb,
    have := hf‚ÇÇ (x‚ÇÄ + a + -b) (add_mem_add (add_mem_add rfl ha) (neg_mem_neg.2 hb)),
    simp only [f.map_neg, f.map_add, f.map_sub, hf‚ÇÅ] at this,
    linarith },
  refine ‚ü®f, Inf (f '' B), _, _‚ü©,
  { suffices : f '' A ‚äÜ Iio (Inf (f '' B)),
    { intros a ha,
      apply this ‚ü®_, ha, rfl‚ü© },
    rw ‚Üêinterior_Iic,
    apply interior_maximal,
    { rintro _ ‚ü®a, ha, rfl‚ü©,
      apply le_cInf ‚ü®f b‚ÇÄ, _‚ü©,
      { rintro _ ‚ü®b', hb, rfl‚ü©,
        exact (forall_lt _ ha _ hb).le },
      { exact mem_image_of_mem _ hb‚ÇÄ } },
    refine f.is_open_map _ _ hA‚ÇÇ,
    rintro rfl,
    simpa using hf‚ÇÅ },
  { intros b hb,
    apply cInf_le ‚ü®f a‚ÇÄ, _‚ü© (mem_image_of_mem _ hb),
    rintro _ ‚ü®b', hb', rfl‚ü©,
    exact (forall_lt _ ha‚ÇÄ _ hb').le },
end

theorem geometric_hahn_banach_open_point {A : set E} {x : E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_open A)
  (disj : x ‚àâ A) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), (‚àÄ a ‚àà A, f a < f x) :=
let ‚ü®f, s, hA, hx‚ü© := geometric_hahn_banach_open hA‚ÇÅ hA‚ÇÇ (convex_singleton x)
  (disjoint_singleton_right.2 disj)
  in ‚ü®f, Œª a ha, lt_of_lt_of_le (hA a ha) (hx x (mem_singleton _))‚ü©

theorem geometric_hahn_banach_point_open {x : E} {B : set E} (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÇ : is_open B)
  (disj : x ‚àâ B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), (‚àÄ b ‚àà B, f x < f b) :=
let ‚ü®f, hf‚ü© := geometric_hahn_banach_open_point hB‚ÇÅ hB‚ÇÇ disj in ‚ü®-f, by simpa‚ü©

theorem geometric_hahn_banach_open_open {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_open A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÉ : is_open B) (disj : disjoint A B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß (‚àÄ b ‚àà B, s < f b) :=
begin
  obtain (rfl | ‚ü®a‚ÇÄ, ha‚ÇÄ‚ü©) := A.eq_empty_or_nonempty,
  { exact ‚ü®0, -1, by simp, Œª b hb, by norm_num‚ü© },
  obtain (rfl | ‚ü®b‚ÇÄ, hb‚ÇÄ‚ü©) := B.eq_empty_or_nonempty,
  { exact ‚ü®0, 1, Œª a ha, by norm_num, by simp‚ü© },
  obtain ‚ü®f, s, hf‚ÇÅ, hf‚ÇÇ‚ü© := geometric_hahn_banach_open hA‚ÇÅ hA‚ÇÇ hB‚ÇÅ disj,
  have : f ‚â† 0,
  { rintro rfl,
    exact (hf‚ÇÅ _ ha‚ÇÄ).not_le (hf‚ÇÇ _ hb‚ÇÄ) },
  have : is_open_map f := f.is_open_map this,
  refine ‚ü®f, s, hf‚ÇÅ, _‚ü©,
  suffices : f '' B ‚äÜ Ioi s,
  { exact Œª b hb, this ‚ü®b, ‚Äπ_‚Ä∫, rfl‚ü© },
  rw ‚Üêinterior_Ici,
  refine interior_maximal _ (this _ hB‚ÇÉ),
  rintro _ ‚ü®_, _, rfl‚ü©,
  exact hf‚ÇÇ _ ‚Äπ_‚Ä∫,
end

open filter
open_locale topological_space

/-- If `A`, `B` are disjoint convex sets, `A` is compact and `B` is closed then we can find open
disjoint convex sets containing them. -/
-- TODO: This proof uses the normed space structure of `E`, but it could work for locally convex
-- topological vector spaces: instead of taking the balls around 0 with radius 1/n, we could show
-- there must be some convex neighbourhood `W` of 0 which make `A + W` and `B + W` disjoint?
theorem closed_compact_separate {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_compact A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÇ : is_closed B) (disj : disjoint A B) :
  ‚àÉ U V, is_open U ‚àß is_open V ‚àß convex ‚Ñù U ‚àß convex ‚Ñù V ‚àß A ‚äÜ U ‚àß B ‚äÜ V ‚àß disjoint U V :=
let ‚ü®Œµ, hŒµ, hAB‚ü© := exists_disjoint_thickenings hA‚ÇÇ hB‚ÇÇ disj in
  ‚ü®_, _, is_open_thickening, is_open_thickening, hA‚ÇÅ.thickening _, hB‚ÇÅ.thickening _,
    self_subset_thickening hŒµ _, self_subset_thickening hŒµ _, hAB‚ü©

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `A`, `B` where `A` is compact
and `B` is closed, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_compact_closed {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_compact A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÇ : is_closed B) (disj : disjoint A B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s t : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß s < t ‚àß (‚àÄ b ‚àà B, t < f b) :=
begin
  obtain rfl | hA := A.eq_empty_or_nonempty,
  { exact ‚ü®0, -2, -1, by simp, by norm_num, Œª b hb, by norm_num‚ü© },
  tactic.unfreeze_local_instances,
  obtain rfl | hB := B.eq_empty_or_nonempty,
  { exact ‚ü®0, 1, 2, Œª a ha, by norm_num, by norm_num, by simp‚ü© },
  obtain ‚ü®U, V, hU, hV, hU‚ÇÅ, hV‚ÇÅ, AU, BV, disj'‚ü© := closed_compact_separate hA‚ÇÅ hA‚ÇÇ hB‚ÇÅ hB‚ÇÇ disj,
  obtain ‚ü®f, s, hf‚ÇÅ, hf‚ÇÇ‚ü© := geometric_hahn_banach_open_open hU‚ÇÅ hU hV‚ÇÅ hV disj',
  obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := hA‚ÇÇ.exists_forall_ge hA f.continuous.continuous_on,
  have : Sup (f '' A) = f x,
  { apply le_antisymm (cSup_le (hA.image f) (by simpa)),
    exact le_cSup ‚ü®f x, by simpa [upper_bounds]‚ü© ‚ü®_, hx‚ÇÅ, rfl‚ü© },
  have : f x < s := hf‚ÇÅ x (AU hx‚ÇÅ),
  exact ‚ü®f, (f x + s)/2, s, Œª a ha, by linarith [hx‚ÇÇ a ha], by linarith, Œª b hb, hf‚ÇÇ b (BV hb)‚ü©,
end

/-- A version of the Hahn-Banach theorem: given disjoint convex subsets `A,B` where `A` is closed,
and `B` is compact, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_closed_compact {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_closed A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÇ : is_compact B) (disj : disjoint A B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s t : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß s < t ‚àß (‚àÄ b ‚àà B, t < f b) :=
let ‚ü®f, s, t, hs, st, ht‚ü© := geometric_hahn_banach_compact_closed hB‚ÇÅ hB‚ÇÇ hA‚ÇÅ hA‚ÇÇ disj.symm in
‚ü®-f, -t, -s, by simpa using ht, by simpa using st, by simpa using hs‚ü©

theorem geometric_hahn_banach_point_closed {x : E} {B : set E} (hB‚ÇÅ : convex ‚Ñù B)
  (hB‚ÇÇ : is_closed B) (disj : x ‚àâ B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), f x < s ‚àß (‚àÄ b ‚àà B, s < f b) :=
let ‚ü®f, s, t, ha, hst, hb‚ü© := geometric_hahn_banach_compact_closed (convex_singleton x)
  is_compact_singleton hB‚ÇÅ hB‚ÇÇ (disjoint_singleton_left.2 disj)
  in ‚ü®f, t, lt_trans (ha x (mem_singleton _)) hst, hb‚ü©

theorem geometric_hahn_banach_closed_point {A : set E} {x : E} (hA‚ÇÅ : convex ‚Ñù A)
  (hA‚ÇÇ : is_closed A) (disj : x ‚àâ A) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß s < f x :=
let ‚ü®f, s, t, ha, hst, hb‚ü© := geometric_hahn_banach_closed_compact hA‚ÇÅ hA‚ÇÇ (convex_singleton x)
  is_compact_singleton (disjoint_singleton_right.2 disj)
  in ‚ü®f, s, ha, lt_trans hst (hb x (mem_singleton _))‚ü©

theorem geometric_hahn_banach_point_point {x y : E} (hxy : x ‚â† y) : ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), f x < f y :=
begin
  obtain ‚ü®f, s, t, hs, st, ht‚ü© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) is_compact_singleton
      (convex_singleton y) is_closed_singleton (disjoint_singleton.2 hxy),
  exact ‚ü®f, by linarith [hs x rfl, ht y rfl]‚ü©,
end
