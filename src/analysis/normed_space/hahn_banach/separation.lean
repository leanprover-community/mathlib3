/-
Copyright (c) 2022 Bhavik Mehta All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, YaÃ«l Dillies
-/
import analysis.convex.cone
import analysis.convex.topology
import analysis.normed.group.pointwise
import analysis.seminorm
import tactic.by_contra

/-!
# Separation Hahn-Banach theorem

In this file we prove the geometric Hahn-Banach theorem. For any two disjoint convex sets, there
exists a continuous linear functional separating them, geometrically meaning that we can intercalate
a plane between them.

We provide many variations to stricten the result under more assumptions on the convex sets:
* `geometric_hahn_banach_open`: One set is open. Weak separation.
* `geometric_hahn_banach_open_point`, `geometric_hahn_banach_point_open`: One set is open, the
  other is a singleton. Weak separation.
* `geometric_hahn_banach_open_open`: Both sets are open. Semistrict separation.
* `geometric_hahn_banach_compact_closed`, `geometric_hahn_banach_closed_compact`: One set is closed,
  the other one is compact. Strict separation.
* `geometric_hahn_banach_point_closed`, `geometric_hahn_banach_closed_point`: One set is closed, the
  other one is a singleton. Strict separation.
* `geometric_hahn_banach_point_point`: Both sets are singletons. Strict separation.

## TODO

* Eidelheit's theorem
* `0 < Î´ â†’ convex â„ s â†’ interior (cthickening Î´ s) = thickening Î´ s`
-/

open function metric set
open_locale pointwise

variables {ð•œ E : Type*}

section
open filter
open_locale topological_space

lemma linear_map.exists_ne_zero {Râ‚ Râ‚‚ : Type*} [semiring Râ‚] [semiring Râ‚‚] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚}
  {Mâ‚ : Type*} [add_comm_monoid Mâ‚] {Mâ‚‚ : Type*} [add_comm_monoid Mâ‚‚] [module Râ‚ Mâ‚] [module Râ‚‚ Mâ‚‚]
  {f : Mâ‚ â†’â‚›â‚—[Ïƒâ‚â‚‚] Mâ‚‚} (hf : f â‰  0) :
  âˆƒ x, f x â‰  0 :=
by { by_contra' h, exact hf (linear_map.ext h) }

lemma continuous_linear_map.exists_ne_zero {Râ‚ Râ‚‚ : Type*} [semiring Râ‚]
  [semiring Râ‚‚] {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚} {Mâ‚ : Type*} [topological_space Mâ‚] [add_comm_monoid Mâ‚]
  {Mâ‚‚ : Type*} [topological_space Mâ‚‚] [add_comm_monoid Mâ‚‚] [module Râ‚ Mâ‚] [module Râ‚‚ Mâ‚‚]
  {f : Mâ‚ â†’SL[Ïƒâ‚â‚‚] Mâ‚‚} (hf : f â‰  0) :
  âˆƒ x, f x â‰  0 :=
by { by_contra' h, exact hf (continuous_linear_map.ext h) }

lemma nhds_le_map_nhds [topological_space ð•œ] [topological_space E] {f : E â†’ ð•œ} {g : ð•œ â†’ E} {a : E}
  (hg : continuous_at g (f a)) (hcomp : f âˆ˜ g = id) (hgfa : g (f a) = a) :
  ð“ (f a) â‰¤ map f (ð“ a) :=
calc ð“ (f a) = ((ð“ (f a)).map g).map f : by rw [map_map, hcomp, map_id]
  ... â‰¤ (ð“ $ g (f a)).map f             : map_mono hg
  ... = (ð“ a).map f                     : by rw hgfa

lemma linear_map.nhds_le_map_nhds [topological_space ð•œ] [division_ring ð•œ] [topological_ring ð•œ]
  [add_comm_group E] [topological_space E] [topological_add_group E] [module ð•œ E]
  [has_continuous_smul ð•œ E] {f : E â†’â‚—[ð•œ] ð•œ} (hf : f â‰  0) (a : E) :
  ð“ (f a) â‰¤ map f (ð“ a) :=
begin
  obtain âŸ¨xâ‚€, hxâ‚€âŸ© := linear_map.exists_ne_zero hf,
  let g : ð•œ â†’ E := Î» x, a + (x - f a) â€¢ (f xâ‚€)â»Â¹ â€¢ xâ‚€,
  have hg : continuous g, by continuity,
  have hcomp : f âˆ˜ g = id, by { ext, simp [hxâ‚€] },
  have hgfa : g (f a) = a, by simp [hxâ‚€],
  exact nhds_le_map_nhds hg.continuous_at hcomp hgfa,
end

/-- A nonzero continuous linear functional is open. -/
lemma continuous_linear_map.is_open_map [topological_space ð•œ] [division_ring ð•œ]
  [topological_ring ð•œ] [add_comm_group E] [topological_space E] [topological_add_group E]
  [module ð•œ E] [has_continuous_smul ð•œ E] (f : E â†’L[ð•œ] ð•œ) (hf : f â‰  0) :
  is_open_map f :=
begin
  refine is_open_map.of_nhds_le (Î» a, _),
  obtain âŸ¨xâ‚€, hxâ‚€âŸ© := continuous_linear_map.exists_ne_zero hf,
  let g : ð•œ â†’ E := Î» x, a + (x - f a) â€¢ (f xâ‚€)â»Â¹ â€¢ xâ‚€,
  have hg : continuous g, by continuity,
  have hcomp : f âˆ˜ g = id, by { ext, simp [hxâ‚€] },
  have hgfa : g (f a) = a, by simp [hxâ‚€],
  exact nhds_le_map_nhds hg.continuous_at hcomp hgfa,
end

variables [normed_group E] [normed_space â„ E]

lemma seminorm.exists_extension (f : linear_pmap â„ E â„) (p : seminorm â„ E)
  (hf : âˆ€ x : f.domain, f x â‰¤ p x) :
  âˆƒ g : E â†’â‚—[â„] â„, (âˆ€ x : f.domain, g x = f x) âˆ§ âˆ€ x, g x â‰¤ p x :=
exists_extension_of_le_sublinear f p (Î» c hc x, by rw [p.smul, real.norm_of_nonneg hc.le])
  p.triangle hf

/-- Given a set `s` which is a convex neighbourhood of `0` and a point `xâ‚€` outside of it, there is
a continuous linear functional `f` separating `xâ‚€` and `s`, in the sense that it sends `xâ‚€` to 1 and
all of `s` to values strictly below `1`. -/
lemma separate_convex_open_set {s : set E} (hsâ‚€ : (0 : E) âˆˆ s) (hsâ‚ : convex â„ s)
  (hsâ‚‚ : is_open s) {xâ‚€ : E} (hxâ‚€ : xâ‚€ âˆ‰ s) :
  âˆƒ f : E â†’L[â„] â„, f xâ‚€ = 1 âˆ§ âˆ€ x âˆˆ s, f x < 1 :=
begin
  let f : linear_pmap â„ E â„ :=
    linear_pmap.mk_span_singleton xâ‚€ 1 (ne_of_mem_of_not_mem hsâ‚€ hxâ‚€).symm,
  obtain âŸ¨r, hr, hrsâŸ© := metric.mem_nhds_iff.1
    (inter_mem (hsâ‚‚.mem_nhds hsâ‚€) $ hsâ‚‚.neg.mem_nhds $ by rwa [mem_neg, neg_zero]),
  obtain âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ© := exists_extension_of_le_sublinear f (gauge s)
    (Î» c hc, gauge_smul_of_nonneg hc.le)
    (gauge_add_le hsâ‚ $ absorbent_nhds_zero $ hsâ‚‚.mem_nhds hsâ‚€) _,
  { refine âŸ¨Ï†.mk_continuous (râ»Â¹) $ Î» x, _, _, _âŸ©,
    { rw [real.norm_eq_abs, abs_le, neg_le, â†linear_map.map_neg],
      nth_rewrite 0 â†norm_neg x,
      suffices : âˆ€ x, Ï† x â‰¤ râ»Â¹ * âˆ¥xâˆ¥,
      { exact âŸ¨this _, this _âŸ© },
      refine Î» x, (hÏ†â‚‚ _).trans _,
      rw [â†div_eq_inv_mul, â†gauge_ball hr],
      exact gauge_mono (absorbent_ball_zero hr) (hrs.trans $ inter_subset_left _ _) x },
    { dsimp,
      rw [â†submodule.coe_mk xâ‚€ (submodule.mem_span_singleton_self _), hÏ†â‚,
        linear_pmap.mk_span_singleton_apply_self] },
    { exact Î» x hx, (hÏ†â‚‚ x).trans_lt (gauge_lt_one_of_mem_of_open hsâ‚ hsâ‚€ hsâ‚‚ hx) } },
  { rintro âŸ¨x, hxâŸ©,
    obtain âŸ¨y, rflâŸ© := submodule.mem_span_singleton.1 hx,
    rw linear_pmap.mk_span_singleton_apply,
    simp only [mul_one, algebra.id.smul_eq_mul, submodule.coe_mk],
    obtain h | h := le_or_lt y 0,
    { exact h.trans (gauge_nonneg _) },
    { rw [gauge_smul_of_nonneg h.le, smul_eq_mul, le_mul_iff_one_le_right h],
      exact one_le_gauge_of_not_mem (hsâ‚.star_convex hsâ‚€)
        ((absorbent_ball_zero hr).subset $ hrs.trans $ inter_subset_left _ _).absorbs hxâ‚€,
      apply_instance } }
end

end

variables [normed_group E] [normed_space â„ E] {s t : set E} {x : E}

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `s`, `t` where `s` is open,
there is a continuous linear functional which separates them. -/
theorem geometric_hahn_banach_open (hsâ‚ : convex â„ s) (hsâ‚‚ : is_open s) (ht : convex â„ t)
  (disj : disjoint s t) :
  âˆƒ (f : E â†’L[â„] â„) (u : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ âˆ€ b âˆˆ t, u â‰¤ f b :=
begin
  obtain rfl | âŸ¨aâ‚€, haâ‚€âŸ© := s.eq_empty_or_nonempty,
  { exact âŸ¨0, 0, by simp, Î» b hb, by simpâŸ© },
  obtain rfl | âŸ¨bâ‚€, hbâ‚€âŸ© := t.eq_empty_or_nonempty,
  { exact âŸ¨0, 1, Î» a ha, by norm_num, by simpâŸ© },
  let xâ‚€ := bâ‚€ - aâ‚€,
  let C := {xâ‚€} + s + -t,
  have : (0:E) âˆˆ C := âŸ¨_ + aâ‚€, -bâ‚€, add_mem_add rfl haâ‚€, neg_mem_neg.2 hbâ‚€, by simpâŸ©,
  have : convex â„ C := ((convex_singleton _).add hsâ‚).add ht.neg_preimage,
  have : xâ‚€ âˆ‰ C,
  { intro hxâ‚€,
    simp only [mem_add, mem_singleton_iff, mem_neg, exists_eq_left, exists_exists_and_eq_and,
      exists_and_distrib_left, add_assoc xâ‚€, add_right_eq_self] at hxâ‚€,
    obtain âŸ¨a, ha, b, hb, habâŸ© := hxâ‚€,
    rw â†eq_neg_of_add_eq_zero hab at hb,
    exact disj âŸ¨ha, hbâŸ© },
  obtain âŸ¨f, hfâ‚, hfâ‚‚âŸ© := separate_convex_open_set â€¹0 âˆˆ Câ€º â€¹_â€º hsâ‚‚.add_left.add_right â€¹xâ‚€ âˆ‰ Câ€º,
  have : f bâ‚€ = f aâ‚€ + 1,
  { simp [â†hfâ‚] },
  have forall_lt : âˆ€ (a âˆˆ s) (b âˆˆ t), f a < f b,
  { intros a ha b hb,
    have := hfâ‚‚ (xâ‚€ + a + -b) (add_mem_add (add_mem_add rfl ha) (neg_mem_neg.2 hb)),
    simp only [f.map_neg, f.map_add, f.map_sub, hfâ‚] at this,
    linarith },
  refine âŸ¨f, Inf (f '' t), _, _âŸ©,
  { suffices : f '' s âŠ† Iio (Inf (f '' t)),
    { intros a ha,
      apply this âŸ¨_, ha, rflâŸ© },
    rw â†interior_Iic,
    apply interior_maximal,
    { rintro _ âŸ¨a, ha, rflâŸ©,
      apply le_cInf âŸ¨f bâ‚€, _âŸ©,
      { rintro _ âŸ¨b', hb, rflâŸ©,
        exact (forall_lt _ ha _ hb).le },
      { exact mem_image_of_mem _ hbâ‚€ } },
    refine f.is_open_map _ _ hsâ‚‚,
    rintro rfl,
    simpa using hfâ‚ },
  { intros b hb,
    apply cInf_le âŸ¨f aâ‚€, _âŸ© (mem_image_of_mem _ hb),
    rintro _ âŸ¨b', hb', rflâŸ©,
    exact (forall_lt _ haâ‚€ _ hb').le },
end

theorem geometric_hahn_banach_open_point (hsâ‚ : convex â„ s) (hsâ‚‚ : is_open s) (disj : x âˆ‰ s) :
  âˆƒ f : E â†’L[â„] â„, âˆ€ a âˆˆ s, f a < f x :=
let âŸ¨f, s, hs, hxâŸ© := geometric_hahn_banach_open hsâ‚ hsâ‚‚ (convex_singleton x)
  (disjoint_singleton_right.2 disj)
  in âŸ¨f, Î» a ha, lt_of_lt_of_le (hs a ha) (hx x (mem_singleton _))âŸ©

theorem geometric_hahn_banach_point_open (htâ‚ : convex â„ t) (htâ‚‚ : is_open t) (disj : x âˆ‰ t) :
  âˆƒ f : E â†’L[â„] â„, âˆ€ b âˆˆ t, f x < f b :=
let âŸ¨f, hfâŸ© := geometric_hahn_banach_open_point htâ‚ htâ‚‚ disj in âŸ¨-f, by simpaâŸ©

theorem geometric_hahn_banach_open_open (hsâ‚ : convex â„ s) (hsâ‚‚ : is_open s) (htâ‚ : convex â„ t)
  (htâ‚ƒ : is_open t) (disj : disjoint s t) :
  âˆƒ (f : E â†’L[â„] â„) (u : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ âˆ€ b âˆˆ t, u < f b :=
begin
  obtain (rfl | âŸ¨aâ‚€, haâ‚€âŸ©) := s.eq_empty_or_nonempty,
  { exact âŸ¨0, -1, by simp, Î» b hb, by norm_numâŸ© },
  obtain (rfl | âŸ¨bâ‚€, hbâ‚€âŸ©) := t.eq_empty_or_nonempty,
  { exact âŸ¨0, 1, Î» a ha, by norm_num, by simpâŸ© },
  obtain âŸ¨f, s, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open hsâ‚ hsâ‚‚ htâ‚ disj,
  have : f â‰  0,
  { rintro rfl,
    exact (hfâ‚ _ haâ‚€).not_le (hfâ‚‚ _ hbâ‚€) },
  have : is_open_map f := f.is_open_map this,
  refine âŸ¨f, s, hfâ‚, _âŸ©,
  suffices : f '' t âŠ† Ioi s,
  { exact Î» b hb, this âŸ¨b, â€¹_â€º, rflâŸ© },
  rw â†interior_Ici,
  refine interior_maximal _ (this _ htâ‚ƒ),
  rintro _ âŸ¨_, _, rflâŸ©,
  exact hfâ‚‚ _ â€¹_â€º,
end

open filter
open_locale topological_space

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `s`, `t` where `s` is compact
and `t` is closed, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_compact_closed {s t : set E} (hsâ‚ : convex â„ s) (hsâ‚‚ : is_compact s)
  (htâ‚ : convex â„ t) (htâ‚‚ : is_closed t) (disj : disjoint s t) :
  âˆƒ (f : E â†’L[â„] â„) (u v : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ u < v âˆ§ âˆ€ b âˆˆ t, v < f b :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { exact âŸ¨0, -2, -1, by simp, by norm_num, Î» b hb, by norm_numâŸ© },
  tactic.unfreeze_local_instances,
  obtain rfl | ht := t.eq_empty_or_nonempty,
  { exact âŸ¨0, 1, 2, Î» a ha, by norm_num, by norm_num, by simpâŸ© },
  obtain âŸ¨U, V, hU, hV, hUâ‚, hVâ‚, sU, tV, disj'âŸ© :=
    exists_disjoint_open_convexes hsâ‚ hsâ‚‚ htâ‚ htâ‚‚ disj,
  obtain âŸ¨f, u, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open_open hUâ‚ hU hVâ‚ hV disj',
  obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := hsâ‚‚.exists_forall_ge hs f.continuous.continuous_on,
  have : Sup (f '' s) = f x,
  { apply le_antisymm (cSup_le (hs.image f) (by simpa)),
    exact le_cSup âŸ¨f x, by simpa [upper_bounds]âŸ© âŸ¨_, hxâ‚, rflâŸ© },
  have : f x < u := hfâ‚ x (sU hxâ‚),
  exact âŸ¨f, (f x + u)/2, u, Î» a ha, by linarith [hxâ‚‚ a ha], by linarith, Î» b hb, hfâ‚‚ b (tV hb)âŸ©,
end

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `s`, `t` where `s` is closed,
and `t` is compact, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_closed_compact (hsâ‚ : convex â„ s) (hsâ‚‚ : is_closed s)
  (htâ‚ : convex â„ t) (htâ‚‚ : is_compact t) (disj : disjoint s t) :
  âˆƒ (f : E â†’L[â„] â„) (u v : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ u < v âˆ§ âˆ€ b âˆˆ t, v < f b :=
let âŸ¨f, s, t, hs, st, htâŸ© := geometric_hahn_banach_compact_closed htâ‚ htâ‚‚ hsâ‚ hsâ‚‚ disj.symm in
âŸ¨-f, -t, -s, by simpa using ht, by simpa using st, by simpa using hsâŸ©

theorem geometric_hahn_banach_point_closed (htâ‚ : convex â„ t) (htâ‚‚ : is_closed t) (disj : x âˆ‰ t) :
  âˆƒ (f : E â†’L[â„] â„) (u : â„), f x < u âˆ§ âˆ€ b âˆˆ t, u < f b :=
let âŸ¨f, u, v, ha, hst, hbâŸ© := geometric_hahn_banach_compact_closed (convex_singleton x)
  is_compact_singleton htâ‚ htâ‚‚ (disjoint_singleton_left.2 disj)
  in âŸ¨f, v, lt_trans (ha x (mem_singleton _)) hst, hbâŸ©

theorem geometric_hahn_banach_closed_point {s : set E} {x : E} (hsâ‚ : convex â„ s)
  (hsâ‚‚ : is_closed s) (disj : x âˆ‰ s) :
  âˆƒ (f : E â†’L[â„] â„) (u : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ u < f x :=
let âŸ¨f, s, t, ha, hst, hbâŸ© := geometric_hahn_banach_closed_compact hsâ‚ hsâ‚‚ (convex_singleton x)
  is_compact_singleton (disjoint_singleton_right.2 disj)
  in âŸ¨f, s, ha, lt_trans hst (hb x (mem_singleton _))âŸ©

theorem geometric_hahn_banach_point_point {x y : E} (hxy : x â‰  y) : âˆƒ (f : E â†’L[â„] â„), f x < f y :=
begin
  obtain âŸ¨f, s, t, hs, st, htâŸ© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) is_compact_singleton
      (convex_singleton y) is_closed_singleton (disjoint_singleton.2 hxy),
  exact âŸ¨f, by linarith [hs x rfl, ht y rfl]âŸ©,
end
