/-
Copyright (c) 2021 Bhavik Mehta All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Ya√´l Dillies
-/
import analysis.convex.cone
import analysis.convex.topology
import analysis.seminorm

/-!
# Separation Hahn-Banach theorem

In this file we prove the geometric Hahn-Banach theorem. For any two disjoint convex sets, there
exists a continuous linear functional separating them, geometrically meaning that we can intercalate
a plane between them.

We provide many variations to stricten the result under more assumptions on the convex sets:
* `geometric_hahn_banach_open`: One set is open. Weak separation.
* `geometric_hahn_banach_open_point`, `geometric_hahn_banach_point_open`: One set is open, the
  other is a singleton. Weak separation.
* `geometric_hahn_banach_open_open`: Both sets are open. Semistrict separation.
* `geometric_hahn_banach_compact_closed`, `geometric_hahn_banach_closed_compact`: One set is closed,
  the other one is compact. Strict separation.
* `geometric_hahn_banach_point_closed`, `geometric_hahn_banach_closed_point`: One set is closed, the
  other one is a singleton. Strict separation.
* `geometric_hahn_banach_point_point`: Both sets are singletons. Strict separation.
-/

open set
open_locale pointwise

variables {E : Type*} [normed_group E] [normed_space ‚Ñù E]

lemma continuous_at_of_exists_open (f : E ‚Üí‚Çó[‚Ñù] ‚Ñù)
  (hf : ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ (U : set E), (0:E) ‚àà U ‚àß is_open U ‚àß ‚àÄ x ‚àà U, ‚à•f x‚à• < Œµ) :
  continuous_at f (0:E) :=
begin
  intros U hU,
  rw metric.nhds_basis_ball.1 at hU,
  rcases hU with ‚ü®Œµ, hŒµ‚ÇÅ, hŒµ‚ÇÇ‚ü©,
  simp only [linear_map.map_zero] at hŒµ‚ÇÇ,
  simp only [filter.mem_map],
  obtain ‚ü®V, hV‚ÇÅ, hV‚ÇÇ, hV‚ÇÉ‚ü© := hf Œµ hŒµ‚ÇÅ,
  rw mem_nhds_iff,
  refine ‚ü®V, Œª x hx, hŒµ‚ÇÇ _, hV‚ÇÇ, hV‚ÇÅ‚ü©,
  simp only [metric.mem_ball, dist_zero_right],
  apply hV‚ÇÉ _ hx,
end

/-- Given a set `C` which is a convex neighbourhood of `0` and a point `x‚ÇÄ` outside of it, there is
a continuous linear functional `f` separating `x‚ÇÄ` and `C`, in the sense that it sends `x‚ÇÄ` to 1 and
all of `C` to values strictly below `1`. -/
lemma separate_convex_open_set {C : set E} (zero_mem : (0:E) ‚àà C) (hC : convex ‚Ñù C)
  (hC‚ÇÇ : is_open C) {x‚ÇÄ : E} (hx‚ÇÄ : x‚ÇÄ ‚àâ C) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), f x‚ÇÄ = 1 ‚àß ‚àÄ x ‚àà C, f x < 1 :=
begin
  let f : linear_pmap ‚Ñù E ‚Ñù :=
    linear_pmap.mk_span_singleton x‚ÇÄ 1 (ne_of_mem_of_not_mem zero_mem hx‚ÇÄ).symm,
  have : f ‚ü®(1:‚Ñù) ‚Ä¢ x‚ÇÄ, by { dsimp, rw submodule.mem_span_singleton, refine ‚ü®1, rfl‚ü© }‚ü© = 1,
  { change linear_pmap.mk_span_singleton _ _ _ _ = _,
    rw [linear_pmap.mk_span_singleton_apply, one_smul] },
  rcases exists_extension_of_le_sublinear f (gauge C) _ _ _ with ‚ü®œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ‚ü©,
  { refine ‚ü®‚ü®œÜ, (œÜ.to_add_monoid_hom.uniform_continuous_of_continuous_at_zero _).continuous‚ü©, _, _‚ü©,
    { refine continuous_at_of_exists_open _ (Œª Œµ hŒµ, ‚ü®(Œµ ‚Ä¢ C) ‚à© (-Œµ ‚Ä¢ C), ‚ü®_, _‚ü©, _, _‚ü©),
      { rw mem_smul_set,
        exact ‚ü®0, zero_mem, by rw smul_zero‚ü© },
      { rw mem_smul_set,
        exact ‚ü®0, zero_mem, by rw smul_zero‚ü© },
      { apply is_open.inter (is_open_map_smul‚ÇÄ hŒµ.ne' _ hC‚ÇÇ),
        { exact is_open_map_smul‚ÇÄ (by linarith) _ hC‚ÇÇ } },
      rintro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©,
      rw [real.norm_eq_abs, abs_lt],
      split,
      { rw [neg_lt, ‚Üêlinear_map.map_neg],
        apply (hœÜ‚ÇÇ _).trans_lt,
        have : -Œµ‚Åª¬π ‚Ä¢ x ‚àà C,
        { obtain ‚ü®y, _, rfl‚ü© := hx‚ÇÇ,
          simpa [smul_smul, hŒµ.ne'] },
        have := gauge_lt_one_of_mem_of_open hC zero_mem hC‚ÇÇ (-Œµ‚Åª¬π ‚Ä¢ x) ‚Äπ_ ‚àà C‚Ä∫,
        rwa [neg_smul, ‚Üêsmul_neg, gauge_smul_of_nonneg (inv_nonneg.2 hŒµ.le), smul_eq_mul,
          inv_mul_lt_iff hŒµ, mul_one] at this,
        apply_instance },
      { have : Œµ‚Åª¬π ‚Ä¢ x ‚àà C,
        { rwa ‚Üêmem_smul_set_iff_inv_smul_mem‚ÇÄ hŒµ.ne' },
        have := gauge_lt_one_of_mem_of_open hC zero_mem hC‚ÇÇ (Œµ‚Åª¬π ‚Ä¢ x) ‚Äπ_‚Ä∫,
        rw [gauge_smul_of_nonneg (inv_nonneg.2 hŒµ.le), smul_eq_mul, inv_mul_lt_iff hŒµ, mul_one]
          at this,
        exact (hœÜ‚ÇÇ _).trans_lt ‚Äπ_‚Ä∫,
        apply_instance } },
    { dsimp,
      have : x‚ÇÄ ‚àà f.domain := submodule.mem_span_singleton_self _,
      rw [‚Üêsubmodule.coe_mk x‚ÇÄ this, hœÜ‚ÇÅ],
      convert linear_pmap.mk_span_singleton_apply x‚ÇÄ (1 : ‚Ñù) _ (1 : ‚Ñù) _; rw one_smul,
      exact this },
    { exact Œª x hx, (hœÜ‚ÇÇ x).trans_lt (gauge_lt_one_of_mem_of_open hC zero_mem hC‚ÇÇ _ hx) } },
  { rintro c hc x,
    rw [gauge_smul_of_nonneg (le_of_lt hc), smul_eq_mul],
    apply_instance },
  { exact gauge_add_le hC (absorbent_nhds_zero (hC‚ÇÇ.mem_nhds zero_mem)) },
  { rintro ‚ü®x, hx‚ü©,
    obtain ‚ü®y, rfl‚ü© := submodule.mem_span_singleton.1 hx,
    rw linear_pmap.mk_span_singleton_apply,
    simp only [mul_one, algebra.id.smul_eq_mul, submodule.coe_mk],
    cases lt_or_le 0 y with h h,
    { rw [gauge_smul_of_nonneg h.le, smul_eq_mul, le_mul_iff_one_le_right h],
      exact one_le_gauge_of_not_mem hC zero_mem hC‚ÇÇ hx‚ÇÄ,
      apply_instance },
    exact h.trans (gauge_nonneg _) }
end

/-- A nonzero continuous linear functional is open. -/
lemma nonzero_linear_map_is_open_map {E : Type*} [add_comm_group E] [topological_space E]
  [topological_add_group E] [module ‚Ñù E] [has_continuous_smul ‚Ñù E] (f : E ‚ÜíL[‚Ñù] ‚Ñù) (hf : f ‚â† 0) :
  is_open_map f :=
begin
  obtain ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü© : ‚àÉ x‚ÇÄ, f x‚ÇÄ ‚â† 0,
  { by_contra h,
    push_neg at h,
    exact hf (continuous_linear_map.ext (Œª x, by simp [h]) )},
  intros A hA,
  rw is_open_iff_mem_nhds,
  rintro _ ‚ü®a, ha, rfl‚ü©,
  let g : ‚Ñù ‚Üí E := Œª x, a + (x - f a) ‚Ä¢ (f x‚ÇÄ)‚Åª¬π ‚Ä¢ x‚ÇÄ,
  have := (show continuous g, by continuity).is_open_preimage _ ‚Äπis_open A‚Ä∫,
  rw is_open_iff_mem_nhds at this,
  exact filter.sets_of_superset _ (this (f a) (by simpa [set.mem_preimage, g]))
    (Œª x hx, ‚ü®_, hx, by simp [hx‚ÇÄ]‚ü©),
end

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `A`, `B` where `A` is open,
there is a continuous linear functional which separates them. -/
theorem geometric_hahn_banach_open {A B : set E}
  (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_open A) (hB : convex ‚Ñù B) (disj : disjoint A B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß (‚àÄ b ‚àà B, s ‚â§ f b) :=
begin
  rcases A.eq_empty_or_nonempty with (rfl | ‚ü®a‚ÇÄ, ha‚ÇÄ‚ü©),
  { exact ‚ü®0, 0, by simp, Œª b hb, by simp‚ü© },
  rcases B.eq_empty_or_nonempty with (rfl | ‚ü®b‚ÇÄ, hb‚ÇÄ‚ü©),
  { exact ‚ü®0, 1, Œª a ha, by norm_num, by simp‚ü© },
  let x‚ÇÄ := b‚ÇÄ - a‚ÇÄ,
  let C := {x‚ÇÄ} + A + -B,
  have : (0:E) ‚àà C := ‚ü®_ + a‚ÇÄ, -b‚ÇÄ, add_mem_add rfl ha‚ÇÄ, neg_mem_neg.2 hb‚ÇÄ, by simp‚ü©,
  have : convex ‚Ñù C := ((convex_singleton _).add hA‚ÇÅ).add hB.neg_preimage,
  have : x‚ÇÄ ‚àâ C,
  { intro hx‚ÇÄ,
    simp only [mem_add, mem_singleton_iff, mem_neg, exists_eq_left, exists_exists_and_eq_and,
      exists_and_distrib_left, add_assoc x‚ÇÄ, add_right_eq_self] at hx‚ÇÄ,
    obtain ‚ü®a, ha, b, hb, hab‚ü© := hx‚ÇÄ,
    rw ‚Üêeq_neg_of_add_eq_zero hab at hb,
    exact disj ‚ü®ha, hb‚ü© },
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := separate_convex_open_set ‚Äπ0 ‚àà C‚Ä∫ ‚Äπ_‚Ä∫ hA‚ÇÇ.add_left.add_right ‚Äπx‚ÇÄ ‚àâ C‚Ä∫,
  have : f b‚ÇÄ = f a‚ÇÄ + 1,
  { simp [‚Üêhf‚ÇÅ] },
  have forall_lt : ‚àÄ (a ‚àà A) (b ‚àà B), f a < f b,
  { intros a ha b hb,
    have := hf‚ÇÇ (x‚ÇÄ + a + -b) (add_mem_add (add_mem_add rfl ha) (neg_mem_neg.2 hb)),
    simp only [f.map_neg, f.map_add, f.map_sub, hf‚ÇÅ] at this,
    linarith },
  refine ‚ü®f, Inf (f '' B), _, _‚ü©,
  { suffices : f '' A ‚äÜ Iio (Inf (f '' B)),
    { intros a ha,
      apply this ‚ü®_, ha, rfl‚ü© },
    rw ‚Üêinterior_Iic,
    apply interior_maximal,
    { rintro _ ‚ü®a, ha, rfl‚ü©,
      apply le_cInf ‚ü®f b‚ÇÄ, _‚ü©,
      { rintro _ ‚ü®b', hb, rfl‚ü©,
        exact (forall_lt _ ha _ hb).le },
      { exact mem_image_of_mem _ hb‚ÇÄ } },
    apply nonzero_linear_map_is_open_map _ _ _ hA‚ÇÇ,
    rintro rfl,
    simpa using hf‚ÇÅ },
  { intros b hb,
    apply cInf_le ‚ü®f a‚ÇÄ, _‚ü© (mem_image_of_mem _ hb),
    rintro _ ‚ü®b', hb', rfl‚ü©,
    exact (forall_lt _ ha‚ÇÄ _ hb').le },
end

theorem geometric_hahn_banach_open_point {A : set E} {x : E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_open A)
  (disj : x ‚àâ A) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), (‚àÄ a ‚àà A, f a < f x) :=
let ‚ü®f, s, hA, hx‚ü© := geometric_hahn_banach_open hA‚ÇÅ hA‚ÇÇ (convex_singleton x)
  (disjoint_singleton_right.2 disj)
  in ‚ü®f, Œª a ha, lt_of_lt_of_le (hA a ha) (hx x (mem_singleton _))‚ü©

theorem geometric_hahn_banach_point_open {x : E} {B : set E} (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÇ : is_open B)
  (disj : x ‚àâ B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), (‚àÄ b ‚àà B, f x < f b) :=
let ‚ü®f, hf‚ü© := geometric_hahn_banach_open_point hB‚ÇÅ hB‚ÇÇ disj in ‚ü®-f, by simpa‚ü©

theorem geometric_hahn_banach_open_open {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_open A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÉ : is_open B) (disj : disjoint A B) :
‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß (‚àÄ b ‚àà B, s < f b) :=
begin
  obtain (rfl | ‚ü®a‚ÇÄ, ha‚ÇÄ‚ü©) := A.eq_empty_or_nonempty,
  { exact ‚ü®0, -1, by simp, Œª b hb, by norm_num‚ü© },
  obtain (rfl | ‚ü®b‚ÇÄ, hb‚ÇÄ‚ü©) := B.eq_empty_or_nonempty,
  { exact ‚ü®0, 1, Œª a ha, by norm_num, by simp‚ü© },
  obtain ‚ü®f, s, hf‚ÇÅ, hf‚ÇÇ‚ü© := geometric_hahn_banach_open hA‚ÇÅ hA‚ÇÇ hB‚ÇÅ disj,
  have : f ‚â† 0,
  { rintro rfl,
    have := hf‚ÇÅ _ ha‚ÇÄ,
    simp only [continuous_linear_map.zero_apply] at this,
    have := hf‚ÇÇ _ hb‚ÇÄ,
    simp only [continuous_linear_map.zero_apply] at this,
    linarith },
  have : is_open_map f := nonzero_linear_map_is_open_map _ this,
  refine ‚ü®f, s, hf‚ÇÅ, _‚ü©,
  suffices : f '' B ‚äÜ Ioi s,
  { exact Œª b hb, this ‚ü®b, ‚Äπ_‚Ä∫, rfl‚ü© },
  rw ‚Üêinterior_Ici,
  refine interior_maximal _ (this _ hB‚ÇÉ),
  rintro _ ‚ü®_, _, rfl‚ü©,
  exact hf‚ÇÇ _ ‚Äπ_‚Ä∫,
end

open filter
open_locale topological_space

/-- If `A`, `B` are disjoint convex sets, `A` is compact and `B` is closed then we can find open
disjoint convex sets containing them. -/
-- TODO: This proof uses the normed space structure of `E`, but it could work for locally convex
-- topological vector spaces: instead of taking the balls around 0 with radius 1/n, we could show
-- there must be some convex neighbourhood `W` of 0 which make `A + W` and `B + W` disjoint?
theorem closed_compact_separate {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_compact A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÉ : is_closed B) (disj : disjoint A B) :
  ‚àÉ U V, is_open U ‚àß is_open V ‚àß convex ‚Ñù U ‚àß convex ‚Ñù V ‚àß A ‚äÜ U ‚àß B ‚äÜ V ‚àß disjoint U V :=
begin
  have : ‚àÉ (n : ‚Ñï), disjoint (A + metric.ball 0 (n+1)‚Åª¬π) (B + metric.ball 0 (n+1)‚Åª¬π),
  { by_contra h,
    push_neg at h,
    simp only [not_disjoint_iff, set.mem_add, metric.mem_ball, dist_zero_right,
      ‚Üêexists_and_distrib_left, ‚Üêexists_and_distrib_right, and_assoc] at h,
    choose z f f' g g' h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ h‚ÇÖ h‚ÇÜ using h,
    obtain ‚ü®w, hw, œÜ, hœÜ‚ÇÅ, hœÜ‚ÇÇ : tendsto (f ‚àò _) _ _‚ü© := hA‚ÇÇ.tendsto_subseq h‚ÇÅ,
    have : tendsto (g ‚àò œÜ) at_top (ùìù w),
    { have : tendsto (f - g) at_top (ùìù 0),
      { suffices : ‚àÄ n, ‚à•(f - g) n‚à• ‚â§ 2 * (n+1)‚Åª¬π,
        { apply squeeze_zero_norm this,
          rw ‚Üêmul_zero (2:‚Ñù),
          apply tendsto.const_mul (2:‚Ñù),
          simp_rw inv_eq_one_div,
          apply tendsto_one_div_add_at_top_nhds_0_nat },
        intro n,
        simp only [pi.sub_apply],
        have : f n - g n = g' n - f' n,
        { rw [sub_eq_iff_eq_add', ‚Üêadd_sub_assoc, h‚ÇÜ, ‚Üêh‚ÇÉ, add_sub_cancel] },
        rw this,
        apply le_trans (norm_sub_le _ _) _,
        rw two_mul,
        apply add_le_add (h‚ÇÖ n).le (h‚ÇÇ n).le },
      have : tendsto (f ‚àò œÜ - g ‚àò œÜ) at_top (ùìù 0),
      { have : f ‚àò œÜ - g ‚àò œÜ = (f - g) ‚àò œÜ,
        { ext,
          simp },
        rw this,
        apply tendsto.comp ‚Äπtendsto (f - g) at_top _‚Ä∫ hœÜ‚ÇÅ.tendsto_at_top },
      simpa using tendsto.sub hœÜ‚ÇÇ ‚Äπtendsto (f ‚àò œÜ - g ‚àò œÜ) at_top _‚Ä∫ },
    have := mem_of_is_closed_sequential ‚Äπis_closed B‚Ä∫ (Œª n, h‚ÇÑ (œÜ n)) this,
    apply disj ‚ü®hw, ‚Äπw ‚àà B‚Ä∫‚ü© },
  obtain ‚ü®n, hn‚ü© := this,
  refine ‚ü®_, _, _, _, hA‚ÇÅ.add _, hB‚ÇÅ.add _, _, _, hn‚ü©,
  { exact metric.is_open_ball.add_left },
  { exact metric.is_open_ball.add_left },
  { exact convex_ball 0 _ },
  { exact convex_ball 0 _ },
  { suffices : A + {0} ‚äÜ A + metric.ball (0:E) (n+1)‚Åª¬π,
    { simpa },
    apply add_subset_add (set.subset.refl _),
    simp only [metric.mem_ball, norm_zero, dist_zero_left, singleton_subset_iff, inv_pos],
    norm_cast,
    simp },
  { suffices : B + {0} ‚äÜ B + metric.ball (0:E) (n+1)‚Åª¬π,
    { simpa },
    apply add_subset_add (set.subset.refl _),
    simp only [metric.mem_ball, norm_zero, dist_zero_left, singleton_subset_iff, inv_pos],
    norm_cast,
    simp },
end

/-- A version of the Hahn-Banach theorem: given disjoint convex sets `A`, `B` where `A` is compact
and `B` is closed, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_compact_closed {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_compact A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÇ : is_closed B) (disj : disjoint A B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s t : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß s < t ‚àß (‚àÄ b ‚àà B, t < f b) :=
begin
  rcases A.eq_empty_or_nonempty with (rfl | hA),
  { refine ‚ü®0, -2, -1, by simp, by norm_num, Œª b hb, by norm_num‚ü© },
  rcases B.eq_empty_or_nonempty with (h | hB),
  { rw h,
    exact ‚ü®0, 1, 2, Œª a ha, by norm_num, by norm_num, by simp‚ü© },
  obtain ‚ü®U, V, hU, hV, hU‚ÇÅ, hV‚ÇÅ, AU, BV, disj'‚ü© := closed_compact_separate hA‚ÇÅ hA‚ÇÇ hB‚ÇÅ hB‚ÇÇ disj,
  obtain ‚ü®f, s, hf‚ÇÅ, hf‚ÇÇ‚ü© := geometric_hahn_banach_open_open hU‚ÇÅ hU hV‚ÇÅ hV disj',
  obtain ‚ü®x, hx‚ÇÅ, hx‚ÇÇ‚ü© := hA‚ÇÇ.exists_forall_ge hA f.continuous.continuous_on,
  have : Sup (f '' A) = f x,
  { apply le_antisymm (cSup_le (hA.image f) (by simpa)),
    refine le_cSup ‚ü®f x, by simpa [upper_bounds]‚ü© ‚ü®_, hx‚ÇÅ, rfl‚ü© },
  have : f x < s := hf‚ÇÅ x (AU hx‚ÇÅ),
  exact ‚ü®f, (f x + s)/2, s, Œª a ha, by linarith [hx‚ÇÇ a ha], by linarith, Œª b hb, hf‚ÇÇ b (BV hb)‚ü©,
end

/--
A version of the Hahn-Banach theorem: given disjoint convex subsets `A,B` where `A` is closed,
and `B` is compact, there is a continuous linear functional which strongly separates them.
-/
theorem geometric_hahn_banach_closed_compact {A B : set E} (hA‚ÇÅ : convex ‚Ñù A) (hA‚ÇÇ : is_closed A)
  (hB‚ÇÅ : convex ‚Ñù B) (hB‚ÇÇ : is_compact B) (disj : disjoint A B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s t : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß s < t ‚àß (‚àÄ b ‚àà B, t < f b) :=
let ‚ü®f, s, t, hs, st, ht‚ü© := geometric_hahn_banach_compact_closed hB‚ÇÅ hB‚ÇÇ hA‚ÇÅ hA‚ÇÇ disj.symm in
‚ü®-f, -t, -s, by simpa using ht, by simpa using st, by simpa using hs‚ü©

theorem geometric_hahn_banach_point_closed {x : E} {B : set E} (hB‚ÇÅ : convex ‚Ñù B)
  (hB‚ÇÇ : is_closed B) (disj : x ‚àâ B) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), f x < s ‚àß (‚àÄ b ‚àà B, s < f b) :=
let ‚ü®f, s, t, ha, hst, hb‚ü© := geometric_hahn_banach_compact_closed (convex_singleton x)
  is_compact_singleton hB‚ÇÅ hB‚ÇÇ (disjoint_singleton_left.2 disj)
  in ‚ü®f, t, lt_trans (ha x (mem_singleton _)) hst, hb‚ü©

theorem geometric_hahn_banach_closed_point {A : set E} {x : E} (hA‚ÇÅ : convex ‚Ñù A)
  (hA‚ÇÇ : is_closed A) (disj : x ‚àâ A) :
  ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù) (s : ‚Ñù), (‚àÄ a ‚àà A, f a < s) ‚àß s < f x :=
let ‚ü®f, s, t, ha, hst, hb‚ü© := geometric_hahn_banach_closed_compact hA‚ÇÅ hA‚ÇÇ (convex_singleton x)
  is_compact_singleton (disjoint_singleton_right.2 disj)
  in ‚ü®f, s, ha, lt_trans hst (hb x (mem_singleton _))‚ü©

theorem geometric_hahn_banach_point_point {x y : E} (hxy : x ‚â† y) : ‚àÉ (f : E ‚ÜíL[‚Ñù] ‚Ñù), f x < f y :=
begin
  obtain ‚ü®f, s, t, hs, st, ht‚ü© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) is_compact_singleton
      (convex_singleton y) is_closed_singleton (disjoint_singleton.2 hxy),
  exact ‚ü®f, by linarith [hs x rfl, ht y rfl]‚ü©,
end
