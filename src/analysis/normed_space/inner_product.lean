/-
Copyright (c) 2020 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import algebra.quadratic_discriminant
import analysis.special_functions.pow
import topology.metric_space.pi_Lp
import data.complex.is_R_or_C

/-!
# Inner Product Space

This file defines inner product spaces and proves its basic properties.

An inner product space is a vector space endowed with an inner product `âŸªÂ·, Â·âŸ«` which
satisfies `âŸªx, yâŸ« = conj âŸªy, xâŸ«`.

## Main results

- We define the class `inner_product_space` with a number of basic properties, most
  notably the Cauchy-Schwarz inequality.
- We show that if `f i` is an inner product space for each `i`, then so is `Î  i, f i`
- We define `euclidean_space K n` to be `n â†’ K` for any `fintype n`, and show that
  this an inner product space.

## Notation

We locally denote the inner product by `âŸªÂ·, Â·âŸ«`.

## Implementation notes

We define both the real and complex cases at the same time using the `is_R_of_C` typeclass.

## Tags

inner product space, norm
-/

noncomputable theory

namespace is_R_or_C

open is_R_or_C real set -- vector_space finite_dimensional submodule module
open_locale big_operators
open_locale topological_space

variables {Î± : Type*} {F : Type*} {G : Type*}
variables {K : Type*} [nondiscrete_normed_field K] [algebra â„ K] [is_R_or_C K]
local notation `ğ“š` := @is_R_or_C.of_real K _ _ _


class has_inner (K Î± : Type*) := (inner : Î± â†’ Î± â†’ K)

export has_inner (inner)

section prio

set_option default_priority 100 -- see Note [default priority]
-- see Note[vector space definition] for why we extend `semimodule`.
/--
An inner product space is a vector space with an additional operation called inner product.
The norm could be derived from the inner product, instead we require the existence of a norm and
the fact that `âˆ¥xâˆ¥^2 = re âŸªx, xâŸ«` to be able to put instances on `K` or product
spaces.

To construct a norm from an inner product, see `inner_product_space.of_core`.
-/
class inner_product_space (K : Type*) (Î± : Type*)
  [nondiscrete_normed_field K] [algebra â„ K] [is_R_or_C K]
  extends normed_group Î±, normed_space K Î±, has_inner K Î± :=
(norm_sq_eq_inner : âˆ€ (x : Î±), âˆ¥xâˆ¥^2 = re (inner x x))
(conj_sym  : âˆ€ x y, conj (inner y x) = inner x y)
(nonneg_im : âˆ€ x, im (inner x x) = 0)
(add_left  : âˆ€ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : âˆ€ x y r, inner (r â€¢ x) y = (conj r) * inner x y)

end prio

/-!
### Constructing a normed space structure from a scalar product

In the definition of an inner product space, we require the existence of a norm, which is equal
(but maybe not defeq) to the square root of the scalar product. This makes it possible to put
an inner product space structure on spaces with a preexisting norm (for instance `â„`), with good
properties. However, sometimes, one would like to define the norm starting only from a well-behaved
scalar product. This is what we implement in this paragraph, starting from a structure
`inner_product_space.core` stating that we have a nice scalar product.

Our goal here is not to develop a whole theory with all the supporting API, as this will be done
below for `inner_product_space`. Instead, we implement the bare minimum to go as directly as
possible to the construction of the norm and the proof of the triangular inequality.
-/

/-- A structure requiring that a scalar product is positive definite and symmetric, from which one
can construct an `inner_product_space` instance in `inner_product_space.of_core`. -/
@[nolint has_inhabited_instance]
structure inner_product_space.core
  (K : Type*) (F : Type*)
  [nondiscrete_normed_field K] [algebra â„ K] [is_R_or_C K]
  [add_comm_group F] [semimodule K F] :=
(inner     : F â†’ F â†’ K)
(conj_sym  : âˆ€ x y, conj (inner y x) = inner x y)
(nonneg_im : âˆ€ x, im (inner x x) = 0)
(nonneg_re : âˆ€ x, re (inner x x) â‰¥ 0)
(definite  : âˆ€ x, inner x x = 0 â†’ x = 0)
(add_left  : âˆ€ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : âˆ€ x y r, inner (r â€¢ x) y = (conj r) * inner x y)

/- We set `inner_product_space.core` to be a class as we will use it as such in the construction
of the normed space structure that it produces. However, all the instances we will use will be
local to this proof. -/
attribute [class] inner_product_space.core

namespace inner_product_space.of_core

variables [add_comm_group F] [semimodule K F] [c : inner_product_space.core K F]
include c

local notation `âŸª`x`, `y`âŸ«` := @inner K F _ x y
local notation `ğ“š` := @is_R_or_C.of_real K _ _ _
local notation `norm_sqK` := @is_R_or_C.norm_sq K _ _ _
local notation `reK` := @is_R_or_C.re K _ _ _
local notation `ext_iff` := @is_R_or_C.ext_iff K _ _ _
local postfix `â€ `:90 := @is_R_or_C.conj K _ _ _

/-- Inner product defined by the `inner_product_space.core` structure. -/
def to_has_inner : has_inner K F := { inner := c.inner }
local attribute [instance] to_has_inner

/-- The norm squared function for inner product spaces. -/
def norm_sq (x : F) := reK âŸªx, xâŸ«

local notation `norm_sqF` := @norm_sq F K _ _ _ _ _ _

@[simp] lemma inner_conj_sym (x y : F) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« := c.conj_sym x y

lemma inner_self_nonneg {x : F} : 0 â‰¤ re âŸªx, xâŸ« := c.nonneg_re _

lemma inner_self_nonneg_im {x : F} : im âŸªx, xâŸ« = 0 := c.nonneg_im _

lemma inner_self_im_zero {x : F} : im âŸªx, xâŸ« = 0 := c.nonneg_im _

lemma inner_add_left {x y z : F} : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
c.add_left _ _ _

lemma inner_add_right {x y z : F} : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« :=
by rw [â†inner_conj_sym, inner_add_left, ring_hom.map_add]; simp only [inner_conj_sym]

lemma inner_norm_sq_eq_inner_self_re (x : F) : norm_sqF x = re âŸªx, xâŸ« := rfl

lemma inner_norm_sq_eq_inner_self (x : F) : ğ“š (norm_sqF x) = âŸªx, xâŸ« :=
begin
  rw ext_iff,
  exact âŸ¨by simp only [of_real_re]; refl, by simp only [inner_self_nonneg_im, of_real_im]âŸ©
end

lemma inner_re_symm {x y : F} : re âŸªx, yâŸ« = re âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_re]

lemma inner_im_symm {x y : F} : im âŸªx, yâŸ« = -im âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_im]

lemma inner_smul_left {x y : F} {r : K} : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
c.smul_left _ _ _

lemma inner_smul_right {x y : F} {r : K} : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_smul_left]; simp only [conj_conj, inner_conj_sym, ring_hom.map_mul]

@[simp] lemma inner_zero_left {x : F} : âŸª0, xâŸ« = 0 :=
by rw [â†zero_smul K (0 : F), inner_smul_left]; simp only [zero_mul, ring_hom.map_zero]

lemma inner_re_zero_left {x : F} : re âŸª0, xâŸ« = 0 := by simp

@[simp] lemma inner_zero_right {x : F} : âŸªx, 0âŸ« = 0 :=
by rw [â†inner_conj_sym, inner_zero_left]; simp only [ring_hom.map_zero]

lemma inner_re_zero_right {x : F} : re âŸªx, 0âŸ« = 0 :=
by simp only [inner_zero_right, add_monoid_hom.map_zero]

@[simp] lemma inner_self_eq_zero {x : F} : âŸªx, xâŸ« = 0 â†” x = 0 :=
iff.intro (c.definite _) (by { rintro rfl, exact inner_zero_left })

@[simp] lemma inner_self_nonpos {x : F} : re âŸªx, xâŸ« â‰¤ 0 â†” x = 0 :=
begin
  split,
  { intro h,
    apply (@inner_self_eq_zero F K _ _ _ _ _ _ x).mp,
    have Hâ‚ : re âŸªx, xâŸ« â‰¥ 0, exact inner_self_nonneg,
    have Hâ‚‚ : re âŸªx, xâŸ« = 0, exact le_antisymm h Hâ‚,
    rw [ext_iff],
    split,
    { simp only [Hâ‚‚, add_monoid_hom.map_zero] },
    { simp [inner_self_im_zero] } },
  { intro h,
    simp only [h, inner_zero_left, add_monoid_hom.map_zero] }
end

lemma inner_symm_re {x y : F} : re âŸªx, yâŸ« = re âŸªy, xâŸ« := by rw [â†inner_conj_sym, conj_re]

@[simp] lemma inner_self_re_to_K {x : F} : ğ“š (re âŸªx, xâŸ«) = âŸªx, xâŸ« :=
by norm_num [ext_iff, inner_self_nonneg_im]

lemma inner_self_re_abs {x : F} : re âŸªx, xâŸ« = abs âŸªx, xâŸ« :=
begin
  have H : âŸªx, xâŸ« = ğ“š (re âŸªx, xâŸ«) + ğ“š (im âŸªx, xâŸ«) * I,
  { rw re_add_im, },
  rw [H, is_add_hom.map_add re (ğ“š (re âŸªx, xâŸ«)) ((ğ“š (im âŸªx, xâŸ«)) * I)],
  rw [mul_re, I_re, mul_zero, I_im, zero_sub, tactic.ring.add_neg_eq_sub],
  rw [of_real_re, of_real_im, sub_zero, inner_self_nonneg_im],
  simp only [abs_of_real, add_zero, of_real_zero, zero_mul],
  exact (_root_.abs_of_nonneg inner_self_nonneg).symm,
end

lemma inner_self_abs_to_K {x : F} : ğ“š (abs âŸªx, xâŸ«) = âŸªx, xâŸ« :=
  by { rw[â†inner_self_re_abs], exact inner_self_re_to_K }

lemma inner_abs_conj_sym {x y : F} : abs âŸªx, yâŸ« = abs âŸªy, xâŸ« :=
  by rw [â†inner_conj_sym, abs_conj]

@[simp] lemma inner_neg_left {x y : F} : âŸª-x, yâŸ« = -âŸªx, yâŸ« :=
by { rw [â† neg_one_smul K x, inner_smul_left], simp }

@[simp] lemma inner_neg_right {x y : F} : âŸªx, -yâŸ« = -âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_neg_left]; simp only [ring_hom.map_neg, inner_conj_sym]

lemma inner_neg_neg {x y : F} : âŸª-x, -yâŸ« = âŸªx, yâŸ« := by simp

@[simp] lemma inner_self_conj {x : F} : âŸªx, xâŸ«â€  = âŸªx, xâŸ« :=
by rw [ext_iff]; exact âŸ¨by rw [conj_re], by rw [conj_im, inner_self_im_zero, neg_zero]âŸ©

lemma inner_sub_left {x y z : F} : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_left] }

lemma inner_sub_right {x y z : F} : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_right] }

lemma inner_mul_conj_re_abs {x y : F} : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = abs (âŸªx, yâŸ« * âŸªy, xâŸ«) :=
by { rw[â†inner_conj_sym, mul_comm], exact re_eq_abs_of_mul_conj (inner y x), }

/-- Expand `inner (x + y) (x + y)` -/
lemma inner_add_add_self {x y : F} : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_add_left, inner_add_right]; ring

/- Expand `inner (x - y) (x - y)` -/
lemma inner_sub_sub_self {x y : F} : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_sub_left, inner_sub_right]; ring

/- Parallelogram law -/
lemma parallelogram_law {x y : F} :
  âŸªx + y, x + yâŸ« + âŸªx - y, x - yâŸ« = 2 * (âŸªx, xâŸ« + âŸªy, yâŸ«) :=
by simp [inner_add_add_self, inner_sub_sub_self, two_mul, sub_eq_add_neg, add_comm, add_left_comm]

/--
Cauchyâ€“Schwarz inequality. This proof follows "Proof 2" on Wikipedia.
We need this for the `core` structure to prove the triangle inequality below when
showing the core is a normed group.
-/
lemma inner_mul_inner_self_le (x y : F) : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« :=
begin
  by_cases hy : y = 0,
  { rw [hy], simp only [abs_zero, inner_zero_left, mul_zero, add_monoid_hom.map_zero] },
  { change y â‰  0 at hy,
    have hy' : âŸªy, yâŸ« â‰  0 := Î» h, by rw [inner_self_eq_zero] at h; exact hy h,
    set T := âŸªy, xâŸ« / âŸªy, yâŸ« with hT,
    have hâ‚ : re âŸªy, xâŸ« = re âŸªx, yâŸ« := inner_re_symm,
    have hâ‚‚ : im âŸªy, xâŸ« = -im âŸªx, yâŸ« := inner_im_symm,
    have hâ‚ƒ : âŸªy, xâŸ« * âŸªx, yâŸ« * âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = âŸªy, xâŸ« * âŸªx, yâŸ« / âŸªy, yâŸ«,
    { rw [mul_div_assoc],
      have : âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = 1 / âŸªy, yâŸ« :=
        by rw [div_mul_eq_div_mul_one_div, div_self hy', one_mul],
      rw [this, div_eq_mul_inv, one_mul, â†div_eq_mul_inv] },
    have hâ‚„ : âŸªy, yâŸ« = ğ“š (re âŸªy, yâŸ«) := by simp only [inner_self_re_to_K],
    have hâ‚… : re âŸªy, yâŸ« > 0,
    { refine lt_of_le_of_ne inner_self_nonneg _,
      intro H,
      apply hy',
      rw ext_iff,
      exact âŸ¨by simp [H],by simp [inner_self_nonneg_im]âŸ© },
    have hâ‚† : re âŸªy, yâŸ« â‰  0 := ne_of_gt hâ‚…,
    have hmain := calc
      0   â‰¤ re âŸªx - T â€¢ y, x - T â€¢ yâŸ«
                  : inner_self_nonneg
      ... = re âŸªx, xâŸ« - re âŸªT â€¢ y, xâŸ« - re âŸªx, T â€¢ yâŸ« + re âŸªT â€¢ y, T â€¢ yâŸ«
                  : by simp [inner_sub_sub_self, inner_smul_left, inner_smul_right, hâ‚, hâ‚‚]
      ... = re âŸªx, xâŸ« - re (Tâ€  * âŸªy, xâŸ«) - re (T * âŸªx, yâŸ«) + re (T * Tâ€  * âŸªy, yâŸ«)
                  : by simp [inner_smul_left, inner_smul_right, mul_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« / âŸªy, yâŸ« * âŸªy, xâŸ«)
                  : by field_simp [-mul_re, hT, conj_div, hâ‚, hâ‚ƒ]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / âŸªy, yâŸ«)
                  : by rw [div_mul_eq_mul_div_comm, â†mul_div_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / ğ“š (re âŸªy, yâŸ«))
                  : by conv_lhs { rw [hâ‚„] }
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [div_re_of_real]
      ... = re âŸªx, xâŸ« - abs (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [inner_mul_conj_re_abs]
      ... = re âŸªx, xâŸ« - abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ«
                  : by rw abs_mul,
    have hmain' : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ« â‰¤ re âŸªx, xâŸ« := by linarith,
    have := (mul_le_mul_right hâ‚…).mpr hmain',
    rwa [div_mul_cancel (abs âŸªx, yâŸ« * abs âŸªy, xâŸ«) hâ‚†] at this }
end

/-- Norm constructed from a `inner_product_space.core` structure, defined to be the square root
of the scalar product. -/
def to_has_norm : has_norm F :=
{ norm := Î» x, sqrt (re âŸªx, xâŸ«) }

local attribute [instance] to_has_norm


lemma norm_eq_sqrt_inner (x : F) : âˆ¥xâˆ¥ = sqrt (re âŸªx, xâŸ«) := rfl

lemma inner_self_eq_norm_square (x : F) : re âŸªx, xâŸ« = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ :=
  by rw[norm_eq_sqrt_inner, â†sqrt_mul inner_self_nonneg (re âŸªx, xâŸ«),
        sqrt_mul_self inner_self_nonneg]

lemma sqrt_norm_sq_eq_norm {x : F} : sqrt (norm_sqF x) = âˆ¥xâˆ¥ := rfl

/-- Expand the square -/
lemma norm_add_pow_two {x y : F} : âˆ¥x + yâˆ¥^2 = âˆ¥xâˆ¥^2 + 2 * (re âŸªx, yâŸ«) + âˆ¥yâˆ¥^2 :=
begin
  repeat {rw [pow_two, â†inner_self_eq_norm_square]},
  rw[inner_add_add_self, two_mul],
  simp only [add_assoc, add_left_inj, add_right_inj, add_monoid_hom.map_add],
  rw [â†inner_conj_sym, conj_re],
end

/-- Same lemma as above but in a different form -/
lemma norm_add_mul_self {x y : F} : âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + 2 * (re âŸªx, yâŸ«) + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ :=
    by { repeat {rw [â† pow_two]}, exact norm_add_pow_two }

/-- Expand the square -/
lemma norm_sub_pow_two {x y : F} : âˆ¥x - yâˆ¥^2 = âˆ¥xâˆ¥^2 - 2 * (re âŸªx, yâŸ«) + âˆ¥yâˆ¥^2 :=
begin
repeat {rw [pow_two, â†inner_self_eq_norm_square]},
rw[inner_sub_sub_self],
calc
  re (âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ«)
      = re âŸªx, xâŸ« - re âŸªx, yâŸ« - re âŸªy, xâŸ« + re âŸªy, yâŸ«  : by simp
  ... = -re âŸªy, xâŸ« - re âŸªx, yâŸ« + re âŸªx, xâŸ« + re âŸªy, yâŸ«  : by ring
  ... = -re (âŸªx, yâŸ«â€ ) - re âŸªx, yâŸ« + re âŸªx, xâŸ« + re âŸªy, yâŸ« : by rw[inner_conj_sym]
  ... = -re âŸªx, yâŸ« - re âŸªx, yâŸ« + re âŸªx, xâŸ« + re âŸªy, yâŸ« : by rw[conj_re]
  ... = re âŸªx, xâŸ« - 2*re âŸªx, yâŸ« + re âŸªy, yâŸ« : by ring
end

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma abs_inner_le_norm (x y : F) : abs âŸªx, yâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ :=
nonneg_le_nonneg_of_squares_le (mul_nonneg (sqrt_nonneg _) (sqrt_nonneg _))
begin
  have H : âˆ¥xâˆ¥ * âˆ¥yâˆ¥ * (âˆ¥xâˆ¥ * âˆ¥yâˆ¥) = re âŸªy, yâŸ« * re âŸªx, xâŸ«,
  { simp only [inner_self_eq_norm_square], ring, },
  rw H,
  conv
  begin
    to_lhs, congr, rw[inner_abs_conj_sym],
  end,
  exact inner_mul_inner_self_le y x,
end

lemma parallelogram_law_with_norm {x y : F} :
  âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ + âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥ = 2 * (âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥) :=
begin
  simp only [(inner_self_eq_norm_square _).symm],
  rw[â†add_monoid_hom.map_add, parallelogram_law, two_mul, two_mul],
  simp only [add_monoid_hom.map_add],
end

/-- Normed group structure constructed from an `inner_product_space.core` structure -/
def to_normed_group : normed_group F :=
normed_group.of_core F
{ norm_eq_zero_iff := assume x,
  begin
    split,
    { intro H,
      change sqrt (re âŸªx, xâŸ«) = 0 at H,
      rw [sqrt_eq_zero inner_self_nonneg] at H,
      apply (inner_self_eq_zero : âŸªx, xâŸ« = 0 â†” x = 0).mp,
      rw ext_iff,
      exact âŸ¨by simp [H], by simp [inner_self_im_zero]âŸ© },
    { rintro rfl,
      change sqrt (re âŸª0, 0âŸ«) = 0,
      simp only [sqrt_zero, inner_zero_right, add_monoid_hom.map_zero] }
  end,
  triangle := assume x y,
  begin
    have hâ‚ : abs âŸªx, yâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ := abs_inner_le_norm _ _,
    have hâ‚‚ : re âŸªx, yâŸ« â‰¤ abs âŸªx, yâŸ« := re_le_abs _,
    have hâ‚ƒ : re âŸªx, yâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ := by linarith,
    have hâ‚„ : re âŸªy, xâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ := by rwa [â†inner_conj_sym, conj_re],
    have : âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ â‰¤ (âˆ¥xâˆ¥ + âˆ¥yâˆ¥) * (âˆ¥xâˆ¥ + âˆ¥yâˆ¥),
    { simp [â†inner_self_eq_norm_square, inner_add_add_self, add_mul, mul_add, mul_comm],
      linarith },
    exact nonneg_le_nonneg_of_squares_le (add_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) this
  end,
  norm_neg := Î» x, by simp only [norm, inner_neg_left, neg_neg, inner_neg_right]
}

local attribute [instance] to_normed_group

/-- Normed space structure constructed from a `inner_product_space.core` structure -/
def to_normed_space : normed_space K F :=
{ norm_smul_le := assume r x,
  begin
    rw [norm_eq_sqrt_inner, inner_smul_left, inner_smul_right, â†mul_assoc],
    rw [conj_mul_eq_norm_sq_left, of_real_mul_re, sqrt_mul, â†inner_norm_sq_eq_inner_self, of_real_re],
    { simp [sqrt_norm_sq_eq_norm, is_R_or_C.sqrt_norm_sq_eq_norm] },
    { exact norm_sq_nonneg r }
  end }

end inner_product_space.of_core

/-- Given a `inner_product_space.core` structure on a space, one can use it to turn
the space into an inner product space, constructing the norm out of the inner product -/
def inner_product_space.of_core [add_comm_group F] [semimodule K F]
  (c : inner_product_space.core K F) : inner_product_space K F :=
begin
  letI : normed_group F := @inner_product_space.of_core.to_normed_group F K _ _ _ _ _ c,
  letI : normed_space K F := @inner_product_space.of_core.to_normed_space F K _ _ _ _ _ c,
  exact { norm_sq_eq_inner := Î» x,
    begin
      have hâ‚ : âˆ¥xâˆ¥^2 = (sqrt (re (c.inner x x))) ^ 2 := rfl,
      have hâ‚‚ : 0 â‰¤ re (c.inner x x) := inner_product_space.of_core.inner_self_nonneg,
      simp [hâ‚, sqr_sqrt, hâ‚‚],
    end,
    ..c }
end

/-! ### Properties of complex inner product spaces -/

variables [inner_product_space K Î±]
local notation `âŸª`x`, `y`âŸ«` := @inner K Î± _ x y
local postfix `â€ `:90 := @is_R_or_C.conj K _ _ _

export inner_product_space (norm_sq_eq_inner)

section basic_properties

lemma inner_conj_sym (x y : Î±) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« := inner_product_space.conj_sym _ _

lemma inner_self_nonneg_im {x : Î±} : im âŸªx, xâŸ« = 0 := inner_product_space.nonneg_im _

lemma inner_self_im_zero {x : Î±} : im âŸªx, xâŸ« = 0 := inner_product_space.nonneg_im _

lemma inner_add_left {x y z : Î±} : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
inner_product_space.add_left _ _ _

lemma inner_add_right {x y z : Î±} : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« :=
begin
  rw [â†inner_conj_sym, inner_add_left, ring_hom.map_add],
  conv_rhs { rw â†inner_conj_sym, conv { congr, skip, rw â†inner_conj_sym } }
end

lemma inner_re_symm {x y : Î±} : re âŸªx, yâŸ« = re âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_re]

lemma inner_im_symm {x y : Î±} : im âŸªx, yâŸ« = -im âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_im]

lemma inner_smul_left {x y : Î±} {r : K} : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
inner_product_space.smul_left _ _ _

lemma inner_smul_right {x y : Î±} {r : K} : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_smul_left, ring_hom.map_mul, conj_conj, inner_conj_sym]

@[simp] lemma inner_zero_left {x : Î±} : âŸª0, xâŸ« = 0 :=
by rw [â† zero_smul K (0:Î±), inner_smul_left, ring_hom.map_zero, zero_mul]

lemma inner_re_zero_left {x : Î±} : re âŸª0, xâŸ« = 0 :=
by simp only [inner_zero_left, add_monoid_hom.map_zero]

@[simp] lemma inner_zero_right {x : Î±} : âŸªx, 0âŸ« = 0 :=
by rw [â†inner_conj_sym, inner_zero_left, ring_hom.map_zero]

lemma inner_re_zero_right {x : Î±} : re âŸªx, 0âŸ« = 0 :=
by simp only [inner_zero_right, add_monoid_hom.map_zero]

lemma inner_self_nonneg {x : Î±} : 0 â‰¤ re âŸªx, xâŸ« :=
by rw [â†norm_sq_eq_inner]; exact pow_nonneg (norm_nonneg x) 2

@[simp] lemma inner_self_eq_zero {x : Î±} : âŸªx, xâŸ« = 0 â†” x = 0 :=
begin
  split,
  { intro h,
    have hâ‚ : re âŸªx, xâŸ« = 0 := by rw ext_iff at h; simp [h.1],
    rw [â†norm_sq_eq_inner x] at hâ‚,
    rw [â†norm_eq_zero],
    exact pow_eq_zero hâ‚ },
  { rintro rfl,
    exact inner_zero_left }
end

@[simp] lemma inner_self_nonpos {x : Î±} : re âŸªx, xâŸ« â‰¤ 0 â†” x = 0 :=
begin
  split,
  { intro h,
    rw â†inner_self_eq_zero,
    have Hâ‚ : re âŸªx, xâŸ« â‰¥ 0, exact inner_self_nonneg,
    have Hâ‚‚ : re âŸªx, xâŸ« = 0, exact le_antisymm h Hâ‚,
    rw ext_iff,
    exact âŸ¨by simp [Hâ‚‚], by simp [inner_self_nonneg_im]âŸ© },
  { rintro rfl,
    simp only [inner_zero_left, add_monoid_hom.map_zero] }
end

@[simp] lemma inner_self_re_to_K {x : Î±} : ğ“š (re âŸªx, xâŸ«) = âŸªx, xâŸ« :=
by rw ext_iff; exact âŸ¨by simp, by simp [inner_self_nonneg_im]âŸ©

lemma inner_self_re_abs {x : Î±} : re âŸªx, xâŸ« = abs âŸªx, xâŸ« :=
begin
  have H : âŸªx, xâŸ« = ğ“š (re âŸªx, xâŸ«) + ğ“š (im âŸªx, xâŸ«) * I,
  { rw re_add_im, },
  rw [H, is_add_hom.map_add re (ğ“š (re âŸªx, xâŸ«)) ((ğ“š (im âŸªx, xâŸ«)) * I)],
  rw [mul_re, I_re, mul_zero, I_im, zero_sub, tactic.ring.add_neg_eq_sub],
  rw [of_real_re, of_real_im, sub_zero, inner_self_nonneg_im],
  simp only [abs_of_real, add_zero, of_real_zero, zero_mul],
  exact (_root_.abs_of_nonneg inner_self_nonneg).symm,
end

lemma inner_self_abs_to_K {x : Î±} : ğ“š (abs âŸªx, xâŸ«) = âŸªx, xâŸ« :=
by { rw[â†inner_self_re_abs], exact inner_self_re_to_K }

lemma inner_abs_conj_sym {x y : Î±} : abs âŸªx, yâŸ« = abs âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, abs_conj]

@[simp] lemma inner_neg_left {x y : Î±} : âŸª-x, yâŸ« = -âŸªx, yâŸ« :=
by { rw [â† neg_one_smul K x, inner_smul_left], simp }

@[simp] lemma inner_neg_right {x y : Î±} : âŸªx, -yâŸ« = -âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_neg_left]; simp only [ring_hom.map_neg, inner_conj_sym]

lemma inner_neg_neg {x y : Î±} : âŸª-x, -yâŸ« = âŸªx, yâŸ« := by simp

@[simp] lemma inner_self_conj {x : Î±} : âŸªx, xâŸ«â€  = âŸªx, xâŸ« :=
by rw [ext_iff]; exact âŸ¨by rw [conj_re], by rw [conj_im, inner_self_im_zero, neg_zero]âŸ©

lemma inner_sub_left {x y z : Î±} : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_left] }

lemma inner_sub_right {x y z : Î±} : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_right] }

lemma inner_mul_conj_re_abs {x y : Î±} : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = abs (âŸªx, yâŸ« * âŸªy, xâŸ«) :=
by { rw[â†inner_conj_sym, mul_comm], exact re_eq_abs_of_mul_conj (inner y x), }

/-- Expand `inner (x + y) (x + y)` -/
lemma inner_add_add_self {x y : Î±} : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_add_left, inner_add_right]; ring

/- Expand `inner (x - y) (x - y)` -/
lemma inner_sub_sub_self {x y : Î±} : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_sub_left, inner_sub_right]; ring

/-- Parallelogram law -/
lemma parallelogram_law {x y : Î±} :
  âŸªx + y, x + yâŸ« + âŸªx - y, x - yâŸ« = 2 * (âŸªx, xâŸ« + âŸªy, yâŸ«) :=
by simp [inner_add_add_self, inner_sub_sub_self, two_mul, sub_eq_add_neg, add_comm, add_left_comm]

/-- Cauchyâ€“Schwarz inequality. This proof follows "Proof 2" on Wikipedia. -/
lemma inner_mul_inner_self_le (x y : Î±) : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« :=
begin
  by_cases hy : y = 0,
  { rw [hy], simp only [abs_zero, inner_zero_left, mul_zero, add_monoid_hom.map_zero] },
  { change y â‰  0 at hy,
    have hy' : âŸªy, yâŸ« â‰  0 := Î» h, by rw [inner_self_eq_zero] at h; exact hy h,
    set T := âŸªy, xâŸ« / âŸªy, yâŸ« with hT,
    have hâ‚ : re âŸªy, xâŸ« = re âŸªx, yâŸ« := inner_re_symm,
    have hâ‚‚ : im âŸªy, xâŸ« = -im âŸªx, yâŸ« := inner_im_symm,
    have hâ‚ƒ : âŸªy, xâŸ« * âŸªx, yâŸ« * âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = âŸªy, xâŸ« * âŸªx, yâŸ« / âŸªy, yâŸ«,
    { rw [mul_div_assoc],
      have : âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = 1 / âŸªy, yâŸ« :=
        by rw [div_mul_eq_div_mul_one_div, div_self hy', one_mul],
      rw [this, div_eq_mul_inv, one_mul, â†div_eq_mul_inv] },
    have hâ‚„ : âŸªy, yâŸ« = ğ“š (re âŸªy, yâŸ«) := by simp,
    have hâ‚… : re âŸªy, yâŸ« > 0,
    { refine lt_of_le_of_ne inner_self_nonneg _,
      intro H,
      apply hy',
      rw ext_iff,
      exact âŸ¨by simp [H],by simp [inner_self_nonneg_im]âŸ© },
    have hâ‚† : re âŸªy, yâŸ« â‰  0 := ne_of_gt hâ‚…,
    have hmain := calc
      0   â‰¤ re âŸªx - T â€¢ y, x - T â€¢ yâŸ«
                  : inner_self_nonneg
      ... = re âŸªx, xâŸ« - re âŸªT â€¢ y, xâŸ« - re âŸªx, T â€¢ yâŸ« + re âŸªT â€¢ y, T â€¢ yâŸ«
                  : by simp [inner_sub_sub_self, inner_smul_left, inner_smul_right, hâ‚, hâ‚‚]
      ... = re âŸªx, xâŸ« - re (Tâ€  * âŸªy, xâŸ«) - re (T * âŸªx, yâŸ«) + re (T * Tâ€  * âŸªy, yâŸ«)
                  : by simp [inner_smul_left, inner_smul_right, mul_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« / âŸªy, yâŸ« * âŸªy, xâŸ«)
                  : by field_simp [-mul_re, hT, conj_div, hâ‚, hâ‚ƒ, inner_conj_sym]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / âŸªy, yâŸ«)
                  : by rw [div_mul_eq_mul_div_comm, â†mul_div_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / ğ“š (re âŸªy, yâŸ«))
                  : by conv_lhs { rw [hâ‚„] }
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [div_re_of_real]
      ... = re âŸªx, xâŸ« - abs (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [inner_mul_conj_re_abs]
      ... = re âŸªx, xâŸ« - abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ«
                  : by rw abs_mul,
    have hmain' : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ« â‰¤ re âŸªx, xâŸ« := by linarith,
    have := (mul_le_mul_right hâ‚…).mpr hmain',
    rwa [div_mul_cancel (abs âŸªx, yâŸ« * abs âŸªy, xâŸ«) hâ‚†] at this }
end

end basic_properties


/-! ### Inner product space structure on product spaces -/

/-
 If `Î¹` is a finite type and each space `f i`, `i : Î¹`, is an inner product space,
then `Î  i, f i` is an inner product space as well. This is not an instance to avoid conflict
with the default instance for the norm on `Î  i, f i`.
-/
instance pi_Lp.inner_product_space {Î¹ : Type*} [fintype Î¹] (f : Î¹ â†’ Type*)
  [Î  i, inner_product_space K (f i)] : inner_product_space K (pi_Lp 2 one_le_two f) :=
{ inner := Î» x y, âˆ‘ i, inner (x i) (y i),
  norm_sq_eq_inner :=
  begin
    intro x,
    have hâ‚ : âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„•) = âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„),
    { apply finset.sum_congr rfl,
      intros j hj,
      simp [â†rpow_nat_cast] },
    have hâ‚‚ : 0 â‰¤ âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„),
    { rw [â†hâ‚],
      exact finset.sum_nonneg (Î» (j : Î¹) (hj : j âˆˆ finset.univ), pow_nonneg (norm_nonneg (x j)) 2) },
    simp [norm, add_monoid_hom.map_sum, â†norm_sq_eq_inner],
    rw [â†rpow_nat_cast ((âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„)) ^ (2 : â„)â»Â¹) 2],
    rw [â†rpow_mul hâ‚‚],
    norm_num [hâ‚],
  end,
  conj_sym :=
  begin
    intros x y,
    unfold inner,
    rw [â†finset.sum_hom finset.univ conj],
    apply finset.sum_congr rfl,
    rintros z -,
    apply inner_conj_sym,
    apply_instance
  end,
  nonneg_im :=
  begin
    intro x,
    unfold inner,
    rw[â†finset.sum_hom finset.univ im],
    apply finset.sum_eq_zero,
    rintros z -,
    exact inner_self_nonneg_im,
    apply_instance
  end,
  add_left := Î» x y z,
    show âˆ‘ i, inner (x i + y i) (z i) = âˆ‘ i, inner (x i) (z i) + âˆ‘ i, inner (y i) (z i),
    by simp only [inner_add_left, finset.sum_add_distrib],
  smul_left := Î» x y r,
    show âˆ‘ (i : Î¹), inner (r â€¢ x i) (y i) = (conj r) * âˆ‘ i, inner (x i) (y i),
    by simp only [finset.mul_sum, inner_smul_left]
}

/-- A field `K` satisfying `is_R_or_C` is itself a `K`-inner product space. -/
instance is_R_or_C.inner_product_space : inner_product_space K K :=
{ inner := (Î» x y, (conj x) * y),
  norm_sq_eq_inner := Î» x, by unfold inner; rw [mul_comm, mul_conj, of_real_re, norm_sq, norm_sq_eq_def],
  conj_sym := Î» x y, by simp [mul_comm],
  nonneg_im := Î» x, by rw[mul_im, conj_re, conj_im]; ring,
  add_left := Î» x y z, by simp [inner, add_mul],
  smul_left := Î» x y z, by simp [inner, mul_assoc] }


/-- The standard real/complex Euclidean space, functions on a finite type. For an `n`-dimensional space
use `euclidean_space K (fin n)`.  -/
@[reducible, nolint unused_arguments]
def euclidean_space (K : Type*) [nondiscrete_normed_field K] [algebra â„ K] [is_R_or_C K]
  (n : Type*) [fintype n] : Type* := pi_Lp 2 one_le_two (Î» (i : n), K)

section pi_Lp
local attribute [reducible] pi_Lp
variables {Î¹ : Type*} [fintype Î¹]

instance : finite_dimensional K (euclidean_space K Î¹) := by apply_instance

@[simp] lemma findim_euclidean_space :
  finite_dimensional.findim K (euclidean_space K Î¹) = fintype.card Î¹ := by simp

lemma findim_euclidean_space_fin {n : â„•} :
  finite_dimensional.findim K (euclidean_space K (fin n)) = n := by simp

end pi_Lp

end is_R_or_C
