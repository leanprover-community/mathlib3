/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/

import linear_algebra.bilinear_form
import linear_algebra.sesquilinear_form
import analysis.special_functions.pow
import topology.metric_space.pi_Lp
import data.complex.is_R_or_C

/-!
# Inner Product Space

This file defines inner product spaces and proves its basic properties.

An inner product space is a vector space endowed with an inner product. It generalizes the notion of
dot product in `â„^n` and provides the means of defining the length of a vector and the angle between
two vectors. In particular vectors `x` and `y` are orthogonal if their inner product equals zero.
We define both the real and complex cases at the same time using the `is_R_or_C` typeclass.


## Main results

- We define the class `inner_product_space ğ•œ E` extending `normed_space ğ•œ E` with a number of basic
  properties, most notably the Cauchy-Schwarz inequality. Here `ğ•œ` is understood to be either `â„`
  or `â„‚`, through the `is_R_or_C` typeclass.
- We show that if `f i` is an inner product space for each `i`, then so is `Î  i, f i`
- We define `euclidean_space ğ•œ n` to be `n â†’ ğ•œ` for any `fintype n`, and show that
  this an inner product space.
- Existence of orthogonal projection onto nonempty complete subspace:
  Let `u` be a point in an inner product space, and let `K` be a nonempty complete subspace.
  Then there exists a unique `v` in `K` that minimizes the distance `âˆ¥u - vâˆ¥` to `u`.
  The point `v` is usually called the orthogonal projection of `u` onto `K`.

## Notation

We globally denote the real and complex inner products by `âŸªÂ·, Â·âŸ«_â„` and `âŸªÂ·, Â·âŸ«_â„‚` respectively.
We also provide two notation namespaces: `real_inner_product_space`, `complex_inner_product_space`,
which respectively introduce the plain notation `âŸªÂ·, Â·âŸ«` for the the real and complex inner product.

## Implementation notes

We choose the convention that inner products are conjugate linear in the first argument and linear
in the second.

## TODO

- Fix the section on the existence of minimizers and orthogonal projections to make sure that it
  also applies in the complex case.

## Tags

inner product space, norm

## References
*  [ClÃ©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
*  [ClÃ©ment & Martin, *A Coq formal proof of the Laxâ€“Milgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/

noncomputable theory

open is_R_or_C real
open_locale big_operators classical

variables {ğ•œ E F : Type*} [is_R_or_C ğ•œ]
local notation `ğ“š` := @is_R_or_C.of_real ğ•œ _

/-- Syntactic typeclass for types endowed with an inner product -/
class has_inner (ğ•œ E : Type*) := (inner : E â†’ E â†’ ğ•œ)

export has_inner (inner)

notation `âŸª`x`, `y`âŸ«_â„` := @inner â„ _ _ x y
notation `âŸª`x`, `y`âŸ«_â„‚` := @inner â„‚ _ _ x y

section notations

localized "notation `âŸª`x`, `y`âŸ«` := @inner â„ _ _ x y" in real_inner_product_space
localized "notation `âŸª`x`, `y`âŸ«` := @inner â„‚ _ _ x y" in complex_inner_product_space

end notations

/--
An inner product space is a vector space with an additional operation called inner product.
The norm could be derived from the inner product, instead we require the existence of a norm and
the fact that `âˆ¥xâˆ¥^2 = re âŸªx, xâŸ«` to be able to put instances on `ğ•‚` or product
spaces.

To construct a norm from an inner product, see `inner_product_space.of_core`.
-/
class inner_product_space (ğ•œ : Type*) (E : Type*) [is_R_or_C ğ•œ]
  extends normed_group E, normed_space ğ•œ E, has_inner ğ•œ E :=
(norm_sq_eq_inner : âˆ€ (x : E), âˆ¥xâˆ¥^2 = re (inner x x))
(conj_sym  : âˆ€ x y, conj (inner y x) = inner x y)
(nonneg_im : âˆ€ x, im (inner x x) = 0)
(add_left  : âˆ€ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : âˆ€ x y r, inner (r â€¢ x) y = (conj r) * inner x y)

/- This instance generates the type-class problem `inner_product_space ?m E` when looking for
   `normed_group E`. However, since `?m` can only ever be `â„` or `â„‚`, this should not cause
   problems. -/
attribute [nolint dangerous_instance] inner_product_space.to_normed_group

/-!
### Constructing a normed space structure from an inner product

In the definition of an inner product space, we require the existence of a norm, which is equal
(but maybe not defeq) to the square root of the scalar product. This makes it possible to put
an inner product space structure on spaces with a preexisting norm (for instance `â„`), with good
properties. However, sometimes, one would like to define the norm starting only from a well-behaved
scalar product. This is what we implement in this paragraph, starting from a structure
`inner_product_space.core` stating that we have a nice scalar product.

Our goal here is not to develop a whole theory with all the supporting API, as this will be done
below for `inner_product_space`. Instead, we implement the bare minimum to go as directly as
possible to the construction of the norm and the proof of the triangular inequality.
-/

/-- A structure requiring that a scalar product is positive definite and symmetric, from which one
can construct an `inner_product_space` instance in `inner_product_space.of_core`. -/
@[nolint has_inhabited_instance]
structure inner_product_space.core
  (ğ•œ : Type*) (F : Type*)
  [is_R_or_C ğ•œ] [add_comm_group F] [semimodule ğ•œ F] :=
(inner     : F â†’ F â†’ ğ•œ)
(conj_sym  : âˆ€ x y, conj (inner y x) = inner x y)
(nonneg_im : âˆ€ x, im (inner x x) = 0)
(nonneg_re : âˆ€ x, re (inner x x) â‰¥ 0)
(definite  : âˆ€ x, inner x x = 0 â†’ x = 0)
(add_left  : âˆ€ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : âˆ€ x y r, inner (r â€¢ x) y = (conj r) * inner x y)

/- We set `inner_product_space.core` to be a class as we will use it as such in the construction
of the normed space structure that it produces. However, all the instances we will use will be
local to this proof. -/
attribute [class] inner_product_space.core

namespace inner_product_space.of_core

variables [add_comm_group F] [semimodule ğ•œ F] [c : inner_product_space.core ğ•œ F]
include c

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ F _ x y
local notation `ğ“š` := @is_R_or_C.of_real ğ•œ _
local notation `norm_sqK` := @is_R_or_C.norm_sq ğ•œ _
local notation `reK` := @is_R_or_C.re ğ•œ _
local notation `ext_iff` := @is_R_or_C.ext_iff ğ•œ _
local postfix `â€ `:90 := @is_R_or_C.conj ğ•œ _

/-- Inner product defined by the `inner_product_space.core` structure. -/
def to_has_inner : has_inner ğ•œ F := { inner := c.inner }
local attribute [instance] to_has_inner

/-- The norm squared function for `inner_product_space.core` structure. -/
def norm_sq (x : F) := reK âŸªx, xâŸ«

local notation `norm_sqF` := @norm_sq ğ•œ F _ _ _ _

lemma inner_conj_sym (x y : F) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« := c.conj_sym x y

lemma inner_self_nonneg {x : F} : 0 â‰¤ re âŸªx, xâŸ« := c.nonneg_re _

lemma inner_self_nonneg_im {x : F} : im âŸªx, xâŸ« = 0 := c.nonneg_im _

lemma inner_self_im_zero {x : F} : im âŸªx, xâŸ« = 0 := c.nonneg_im _

lemma inner_add_left {x y z : F} : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
c.add_left _ _ _

lemma inner_add_right {x y z : F} : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« :=
by rw [â†inner_conj_sym, inner_add_left, ring_hom.map_add]; simp only [inner_conj_sym]

lemma inner_norm_sq_eq_inner_self (x : F) : ğ“š (norm_sqF x) = âŸªx, xâŸ« :=
begin
  rw ext_iff,
  exact âŸ¨by simp only [of_real_re]; refl, by simp only [inner_self_nonneg_im, of_real_im]âŸ©
end

lemma inner_re_symm {x y : F} : re âŸªx, yâŸ« = re âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_re]

lemma inner_im_symm {x y : F} : im âŸªx, yâŸ« = -im âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_im]

lemma inner_smul_left {x y : F} {r : ğ•œ} : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
c.smul_left _ _ _

lemma inner_smul_right {x y : F} {r : ğ•œ} : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_smul_left]; simp only [conj_conj, inner_conj_sym, ring_hom.map_mul]

lemma inner_zero_left {x : F} : âŸª0, xâŸ« = 0 :=
by rw [â†zero_smul ğ•œ (0 : F), inner_smul_left]; simp only [zero_mul, ring_hom.map_zero]

lemma inner_zero_right {x : F} : âŸªx, 0âŸ« = 0 :=
by rw [â†inner_conj_sym, inner_zero_left]; simp only [ring_hom.map_zero]

lemma inner_self_eq_zero {x : F} : âŸªx, xâŸ« = 0 â†” x = 0 :=
iff.intro (c.definite _) (by { rintro rfl, exact inner_zero_left })

lemma inner_self_re_to_K {x : F} : ğ“š (re âŸªx, xâŸ«) = âŸªx, xâŸ« :=
by norm_num [ext_iff, inner_self_nonneg_im]

lemma inner_abs_conj_sym {x y : F} : abs âŸªx, yâŸ« = abs âŸªy, xâŸ« :=
  by rw [â†inner_conj_sym, abs_conj]

lemma inner_neg_left {x y : F} : âŸª-x, yâŸ« = -âŸªx, yâŸ« :=
by { rw [â† neg_one_smul ğ•œ x, inner_smul_left], simp }

lemma inner_neg_right {x y : F} : âŸªx, -yâŸ« = -âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_neg_left]; simp only [ring_hom.map_neg, inner_conj_sym]

lemma inner_sub_left {x y z : F} : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_left, inner_neg_left] }

lemma inner_sub_right {x y z : F} : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_right, inner_neg_right] }

lemma inner_mul_conj_re_abs {x y : F} : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = abs (âŸªx, yâŸ« * âŸªy, xâŸ«) :=
by { rw[â†inner_conj_sym, mul_comm], exact re_eq_abs_of_mul_conj (inner y x), }

/-- Expand `inner (x + y) (x + y)` -/
lemma inner_add_add_self {x y : F} : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_add_left, inner_add_right]; ring

/- Expand `inner (x - y) (x - y)` -/
lemma inner_sub_sub_self {x y : F} : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_sub_left, inner_sub_right]; ring

/--
Cauchyâ€“Schwarz inequality. This proof follows "Proof 2" on Wikipedia.
We need this for the `core` structure to prove the triangle inequality below when
showing the core is a normed group.
-/
lemma inner_mul_inner_self_le (x y : F) : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« :=
begin
  by_cases hy : y = 0,
  { rw [hy], simp only [is_R_or_C.abs_zero, inner_zero_left, mul_zero, add_monoid_hom.map_zero] },
  { change y â‰  0 at hy,
    have hy' : âŸªy, yâŸ« â‰  0 := Î» h, by rw [inner_self_eq_zero] at h; exact hy h,
    set T := âŸªy, xâŸ« / âŸªy, yâŸ« with hT,
    have hâ‚ : re âŸªy, xâŸ« = re âŸªx, yâŸ« := inner_re_symm,
    have hâ‚‚ : im âŸªy, xâŸ« = -im âŸªx, yâŸ« := inner_im_symm,
    have hâ‚ƒ : âŸªy, xâŸ« * âŸªx, yâŸ« * âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = âŸªy, xâŸ« * âŸªx, yâŸ« / âŸªy, yâŸ«,
    { rw [mul_div_assoc],
      have : âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = 1 / âŸªy, yâŸ« :=
        by rw [div_mul_eq_div_mul_one_div, div_self hy', one_mul],
      rw [this, div_eq_mul_inv, one_mul, â†div_eq_mul_inv] },
    have hâ‚„ : âŸªy, yâŸ« = ğ“š (re âŸªy, yâŸ«) := by simp only [inner_self_re_to_K],
    have hâ‚… : re âŸªy, yâŸ« > 0,
    { refine lt_of_le_of_ne inner_self_nonneg _,
      intro H,
      apply hy',
      rw ext_iff,
      exact âŸ¨by simp [H],by simp [inner_self_nonneg_im]âŸ© },
    have hâ‚† : re âŸªy, yâŸ« â‰  0 := ne_of_gt hâ‚…,
    have hmain := calc
      0   â‰¤ re âŸªx - T â€¢ y, x - T â€¢ yâŸ«
                  : inner_self_nonneg
      ... = re âŸªx, xâŸ« - re âŸªT â€¢ y, xâŸ« - re âŸªx, T â€¢ yâŸ« + re âŸªT â€¢ y, T â€¢ yâŸ«
                  : by simp [inner_sub_sub_self, inner_smul_left, inner_smul_right, hâ‚, hâ‚‚]
      ... = re âŸªx, xâŸ« - re (Tâ€  * âŸªy, xâŸ«) - re (T * âŸªx, yâŸ«) + re (T * Tâ€  * âŸªy, yâŸ«)
                  : by simp [inner_smul_left, inner_smul_right, mul_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« / âŸªy, yâŸ« * âŸªy, xâŸ«)
                  : by field_simp [-mul_re, inner_conj_sym, hT, conj_div, hâ‚, hâ‚ƒ]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / âŸªy, yâŸ«)
                  : by rw [div_mul_eq_mul_div_comm, â†mul_div_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / ğ“š (re âŸªy, yâŸ«))
                  : by conv_lhs { rw [hâ‚„] }
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [div_re_of_real]
      ... = re âŸªx, xâŸ« - abs (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [inner_mul_conj_re_abs]
      ... = re âŸªx, xâŸ« - abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ«
                  : by rw is_R_or_C.abs_mul,
    have hmain' : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ« â‰¤ re âŸªx, xâŸ« := by linarith,
    have := (mul_le_mul_right hâ‚…).mpr hmain',
    rwa [div_mul_cancel (abs âŸªx, yâŸ« * abs âŸªy, xâŸ«) hâ‚†] at this }
end

/-- Norm constructed from a `inner_product_space.core` structure, defined to be the square root
of the scalar product. -/
def to_has_norm : has_norm F :=
{ norm := Î» x, sqrt (re âŸªx, xâŸ«) }

local attribute [instance] to_has_norm

lemma norm_eq_sqrt_inner (x : F) : âˆ¥xâˆ¥ = sqrt (re âŸªx, xâŸ«) := rfl

lemma inner_self_eq_norm_square (x : F) : re âŸªx, xâŸ« = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ :=
by rw[norm_eq_sqrt_inner, â†sqrt_mul inner_self_nonneg (re âŸªx, xâŸ«),
  sqrt_mul_self inner_self_nonneg]

lemma sqrt_norm_sq_eq_norm {x : F} : sqrt (norm_sqF x) = âˆ¥xâˆ¥ := rfl

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma abs_inner_le_norm (x y : F) : abs âŸªx, yâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ :=
nonneg_le_nonneg_of_squares_le (mul_nonneg (sqrt_nonneg _) (sqrt_nonneg _))
begin
  have H : âˆ¥xâˆ¥ * âˆ¥yâˆ¥ * (âˆ¥xâˆ¥ * âˆ¥yâˆ¥) = re âŸªy, yâŸ« * re âŸªx, xâŸ«,
  { simp only [inner_self_eq_norm_square], ring, },
  rw H,
  conv
  begin
    to_lhs, congr, rw[inner_abs_conj_sym],
  end,
  exact inner_mul_inner_self_le y x,
end

/-- Normed group structure constructed from an `inner_product_space.core` structure -/
def to_normed_group : normed_group F :=
normed_group.of_core F
{ norm_eq_zero_iff := assume x,
  begin
    split,
    { intro H,
      change sqrt (re âŸªx, xâŸ«) = 0 at H,
      rw [sqrt_eq_zero inner_self_nonneg] at H,
      apply (inner_self_eq_zero : âŸªx, xâŸ« = 0 â†” x = 0).mp,
      rw ext_iff,
      exact âŸ¨by simp [H], by simp [inner_self_im_zero]âŸ© },
    { rintro rfl,
      change sqrt (re âŸª0, 0âŸ«) = 0,
      simp only [sqrt_zero, inner_zero_right, add_monoid_hom.map_zero] }
  end,
  triangle := assume x y,
  begin
    have hâ‚ : abs âŸªx, yâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ := abs_inner_le_norm _ _,
    have hâ‚‚ : re âŸªx, yâŸ« â‰¤ abs âŸªx, yâŸ« := re_le_abs _,
    have hâ‚ƒ : re âŸªx, yâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ := by linarith,
    have hâ‚„ : re âŸªy, xâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ := by rwa [â†inner_conj_sym, conj_re],
    have : âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ â‰¤ (âˆ¥xâˆ¥ + âˆ¥yâˆ¥) * (âˆ¥xâˆ¥ + âˆ¥yâˆ¥),
    { simp [â†inner_self_eq_norm_square, inner_add_add_self, add_mul, mul_add, mul_comm],
      linarith },
    exact nonneg_le_nonneg_of_squares_le (add_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) this
  end,
  norm_neg := Î» x, by simp only [norm, inner_neg_left, neg_neg, inner_neg_right] }

local attribute [instance] to_normed_group

/-- Normed space structure constructed from a `inner_product_space.core` structure -/
def to_normed_space : normed_space ğ•œ F :=
{ norm_smul_le := assume r x,
  begin
    rw [norm_eq_sqrt_inner, inner_smul_left, inner_smul_right, â†mul_assoc],
    rw [conj_mul_eq_norm_sq_left, of_real_mul_re, sqrt_mul, â†inner_norm_sq_eq_inner_self, of_real_re],
    { simp [sqrt_norm_sq_eq_norm, is_R_or_C.sqrt_norm_sq_eq_norm] },
    { exact norm_sq_nonneg r }
  end }

end inner_product_space.of_core

/-- Given a `inner_product_space.core` structure on a space, one can use it to turn
the space into an inner product space, constructing the norm out of the inner product -/
def inner_product_space.of_core [add_comm_group F] [semimodule ğ•œ F]
  (c : inner_product_space.core ğ•œ F) : inner_product_space ğ•œ F :=
begin
  letI : normed_group F := @inner_product_space.of_core.to_normed_group ğ•œ F _ _ _ c,
  letI : normed_space ğ•œ F := @inner_product_space.of_core.to_normed_space ğ•œ F _ _ _ c,
  exact { norm_sq_eq_inner := Î» x,
    begin
      have hâ‚ : âˆ¥xâˆ¥^2 = (sqrt (re (c.inner x x))) ^ 2 := rfl,
      have hâ‚‚ : 0 â‰¤ re (c.inner x x) := inner_product_space.of_core.inner_self_nonneg,
      simp [hâ‚, sqr_sqrt, hâ‚‚],
    end,
    ..c }
end

/-! ### Properties of inner product spaces -/

variables [inner_product_space ğ•œ E] [inner_product_space â„ F]
local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ _ _ x y
local notation `âŸª`x`, `y`âŸ«_â„` := @inner â„ _ _ x y
local notation `âŸª`x`, `y`âŸ«_â„‚` := @inner â„‚ _ _ x y
local notation `IK` := @is_R_or_C.I ğ•œ _
local notation `absR` := _root_.abs
local postfix `â€ `:90 := @is_R_or_C.conj ğ•œ _
local postfix `â‹†`:90 := complex.conj

export inner_product_space (norm_sq_eq_inner)

section basic_properties

lemma inner_conj_sym (x y : E) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« := inner_product_space.conj_sym _ _
lemma real_inner_comm (x y : F) : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := inner_conj_sym x y

lemma inner_eq_zero_sym {x y : E} : âŸªx, yâŸ« = 0 â†” âŸªy, xâŸ« = 0 :=
âŸ¨Î» h, by simp [â†inner_conj_sym, h], Î» h, by simp [â†inner_conj_sym, h]âŸ©

lemma inner_self_nonneg_im {x : E} : im âŸªx, xâŸ« = 0 := inner_product_space.nonneg_im _

lemma inner_self_im_zero {x : E} : im âŸªx, xâŸ« = 0 := inner_product_space.nonneg_im _

lemma inner_add_left {x y z : E} : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
inner_product_space.add_left _ _ _

lemma inner_add_right {x y z : E} : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« :=
begin
  rw [â†inner_conj_sym, inner_add_left, ring_hom.map_add],
  conv_rhs { rw â†inner_conj_sym, conv { congr, skip, rw â†inner_conj_sym } }
end

lemma inner_re_symm {x y : E} : re âŸªx, yâŸ« = re âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_re]

lemma inner_im_symm {x y : E} : im âŸªx, yâŸ« = -im âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, conj_im]

lemma inner_smul_left {x y : E} {r : ğ•œ} : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
inner_product_space.smul_left _ _ _
lemma real_inner_smul_left {x y : F} {r : â„} : âŸªr â€¢ x, yâŸ«_â„ = r * âŸªx, yâŸ«_â„ := inner_smul_left

lemma inner_smul_right {x y : E} {r : ğ•œ} : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_smul_left, ring_hom.map_mul, conj_conj, inner_conj_sym]
lemma real_inner_smul_right {x y : F} {r : â„} : âŸªx, r â€¢ yâŸ«_â„ = r * âŸªx, yâŸ«_â„ := inner_smul_right

/-- The inner product as a sesquilinear form. -/
def sesq_form_of_inner : sesq_form ğ•œ E conj_to_ring_equiv :=
{ sesq := Î» x y, âŸªy, xâŸ«,    -- Note that sesquilinear forms are linear in the first argument
  sesq_add_left := Î» x y z, inner_add_right,
  sesq_add_right := Î» x y z, inner_add_left,
  sesq_smul_left := Î» r x y, inner_smul_right,
  sesq_smul_right := Î» r x y, inner_smul_left }

/-- The real inner product as a bilinear form. -/
def bilin_form_of_real_inner : bilin_form â„ F :=
{ bilin := inner,
  bilin_add_left := Î» x y z, inner_add_left,
  bilin_smul_left := Î» a x y, inner_smul_left,
  bilin_add_right := Î» x y z, inner_add_right,
  bilin_smul_right := Î» a x y, inner_smul_right }

/-- An inner product with a sum on the left. -/
lemma sum_inner {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ E) (x : E) :
  âŸªâˆ‘ i in s, f i, xâŸ« = âˆ‘ i in s, âŸªf i, xâŸ« :=
sesq_form.map_sum_right (sesq_form_of_inner) _ _ _

/-- An inner product with a sum on the right. -/
lemma inner_sum {Î¹ : Type*} (s : finset Î¹) (f : Î¹ â†’ E) (x : E) :
  âŸªx, âˆ‘ i in s, f iâŸ« = âˆ‘ i in s, âŸªx, f iâŸ« :=
sesq_form.map_sum_left (sesq_form_of_inner) _ _ _

@[simp] lemma inner_zero_left {x : E} : âŸª0, xâŸ« = 0 :=
by rw [â† zero_smul ğ•œ (0:E), inner_smul_left, ring_hom.map_zero, zero_mul]

lemma inner_re_zero_left {x : E} : re âŸª0, xâŸ« = 0 :=
by simp only [inner_zero_left, add_monoid_hom.map_zero]

@[simp] lemma inner_zero_right {x : E} : âŸªx, 0âŸ« = 0 :=
by rw [â†inner_conj_sym, inner_zero_left, ring_hom.map_zero]

lemma inner_re_zero_right {x : E} : re âŸªx, 0âŸ« = 0 :=
by simp only [inner_zero_right, add_monoid_hom.map_zero]

lemma inner_self_nonneg {x : E} : 0 â‰¤ re âŸªx, xâŸ« :=
by rw [â†norm_sq_eq_inner]; exact pow_nonneg (norm_nonneg x) 2
lemma real_inner_self_nonneg {x : F} : 0 â‰¤ âŸªx, xâŸ«_â„ := @inner_self_nonneg â„ F _ _ x

@[simp] lemma inner_self_eq_zero {x : E} : âŸªx, xâŸ« = 0 â†” x = 0 :=
begin
  split,
  { intro h,
    have hâ‚ : re âŸªx, xâŸ« = 0 := by rw is_R_or_C.ext_iff at h; simp [h.1],
    rw [â†norm_sq_eq_inner x] at hâ‚,
    rw [â†norm_eq_zero],
    exact pow_eq_zero hâ‚ },
  { rintro rfl,
    exact inner_zero_left }
end

@[simp] lemma inner_self_nonpos {x : E} : re âŸªx, xâŸ« â‰¤ 0 â†” x = 0 :=
begin
  split,
  { intro h,
    rw â†inner_self_eq_zero,
    have Hâ‚ : re âŸªx, xâŸ« â‰¥ 0, exact inner_self_nonneg,
    have Hâ‚‚ : re âŸªx, xâŸ« = 0, exact le_antisymm h Hâ‚,
    rw is_R_or_C.ext_iff,
    exact âŸ¨by simp [Hâ‚‚], by simp [inner_self_nonneg_im]âŸ© },
  { rintro rfl,
    simp only [inner_zero_left, add_monoid_hom.map_zero] }
end

lemma real_inner_self_nonpos {x : F} : âŸªx, xâŸ«_â„ â‰¤ 0 â†” x = 0 :=
by { have h := @inner_self_nonpos â„ F _ _ x, simpa using h }

@[simp] lemma inner_self_re_to_K {x : E} : ğ“š (re âŸªx, xâŸ«) = âŸªx, xâŸ« :=
by rw is_R_or_C.ext_iff; exact âŸ¨by simp, by simp [inner_self_nonneg_im]âŸ©

lemma inner_self_re_abs {x : E} : re âŸªx, xâŸ« = abs âŸªx, xâŸ« :=
begin
  have H : âŸªx, xâŸ« = ğ“š (re âŸªx, xâŸ«) + ğ“š (im âŸªx, xâŸ«) * I,
  { rw re_add_im, },
  rw [H, is_add_hom.map_add re (ğ“š (re âŸªx, xâŸ«)) ((ğ“š (im âŸªx, xâŸ«)) * I)],
  rw [mul_re, I_re, mul_zero, I_im, zero_sub, tactic.ring.add_neg_eq_sub],
  rw [of_real_re, of_real_im, sub_zero, inner_self_nonneg_im],
  simp only [abs_of_real, add_zero, of_real_zero, zero_mul],
  exact (_root_.abs_of_nonneg inner_self_nonneg).symm,
end

lemma inner_self_abs_to_K {x : E} : ğ“š (abs âŸªx, xâŸ«) = âŸªx, xâŸ« :=
by { rw[â†inner_self_re_abs], exact inner_self_re_to_K }

lemma real_inner_self_abs {x : F} : absR âŸªx, xâŸ«_â„ = âŸªx, xâŸ«_â„ :=
by { have h := @inner_self_abs_to_K â„ F _ _ x, simpa using h }

lemma inner_abs_conj_sym {x y : E} : abs âŸªx, yâŸ« = abs âŸªy, xâŸ« :=
by rw [â†inner_conj_sym, abs_conj]

@[simp] lemma inner_neg_left {x y : E} : âŸª-x, yâŸ« = -âŸªx, yâŸ« :=
by { rw [â† neg_one_smul ğ•œ x, inner_smul_left], simp }

@[simp] lemma inner_neg_right {x y : E} : âŸªx, -yâŸ« = -âŸªx, yâŸ« :=
by rw [â†inner_conj_sym, inner_neg_left]; simp only [ring_hom.map_neg, inner_conj_sym]

lemma inner_neg_neg {x y : E} : âŸª-x, -yâŸ« = âŸªx, yâŸ« := by simp

@[simp] lemma inner_self_conj {x : E} : âŸªx, xâŸ«â€  = âŸªx, xâŸ« :=
by rw [is_R_or_C.ext_iff]; exact âŸ¨by rw [conj_re], by rw [conj_im, inner_self_im_zero, neg_zero]âŸ©

lemma inner_sub_left {x y z : E} : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_left] }

lemma inner_sub_right {x y z : E} : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« :=
by { simp [sub_eq_add_neg, inner_add_right] }

lemma inner_mul_conj_re_abs {x y : E} : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = abs (âŸªx, yâŸ« * âŸªy, xâŸ«) :=
by { rw[â†inner_conj_sym, mul_comm], exact re_eq_abs_of_mul_conj (inner y x), }

/-- Expand `âŸªx + y, x + yâŸ«` -/
lemma inner_add_add_self {x y : E} : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_add_left, inner_add_right]; ring

/-- Expand `âŸªx + y, x + yâŸ«_â„` -/
lemma real_inner_add_add_self {x y : F} : âŸªx + y, x + yâŸ«_â„ = âŸªx, xâŸ«_â„ + 2 * âŸªx, yâŸ«_â„ + âŸªy, yâŸ«_â„ :=
begin
  have : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := by rw [â†inner_conj_sym]; refl,
  simp [inner_add_add_self, this],
  ring,
end

/- Expand `âŸªx - y, x - yâŸ«` -/
lemma inner_sub_sub_self {x y : E} : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« :=
by simp only [inner_sub_left, inner_sub_right]; ring

/-- Expand `âŸªx - y, x - yâŸ«_â„` -/
lemma real_inner_sub_sub_self {x y : F} : âŸªx - y, x - yâŸ«_â„ = âŸªx, xâŸ«_â„ - 2 * âŸªx, yâŸ«_â„ + âŸªy, yâŸ«_â„ :=
begin
  have : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := by rw [â†inner_conj_sym]; refl,
  simp [inner_sub_sub_self, this],
  ring,
end

/-- Parallelogram law -/
lemma parallelogram_law {x y : E} :
  âŸªx + y, x + yâŸ« + âŸªx - y, x - yâŸ« = 2 * (âŸªx, xâŸ« + âŸªy, yâŸ«) :=
by simp [inner_add_add_self, inner_sub_sub_self, two_mul, sub_eq_add_neg, add_comm, add_left_comm]

/-- Cauchyâ€“Schwarz inequality. This proof follows "Proof 2" on Wikipedia. -/
lemma inner_mul_inner_self_le (x y : E) : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« :=
begin
  by_cases hy : y = 0,
  { rw [hy], simp only [is_R_or_C.abs_zero, inner_zero_left, mul_zero, add_monoid_hom.map_zero] },
  { change y â‰  0 at hy,
    have hy' : âŸªy, yâŸ« â‰  0 := Î» h, by rw [inner_self_eq_zero] at h; exact hy h,
    set T := âŸªy, xâŸ« / âŸªy, yâŸ« with hT,
    have hâ‚ : re âŸªy, xâŸ« = re âŸªx, yâŸ« := inner_re_symm,
    have hâ‚‚ : im âŸªy, xâŸ« = -im âŸªx, yâŸ« := inner_im_symm,
    have hâ‚ƒ : âŸªy, xâŸ« * âŸªx, yâŸ« * âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = âŸªy, xâŸ« * âŸªx, yâŸ« / âŸªy, yâŸ«,
    { rw [mul_div_assoc],
      have : âŸªy, yâŸ« / (âŸªy, yâŸ« * âŸªy, yâŸ«) = 1 / âŸªy, yâŸ« :=
        by rw [div_mul_eq_div_mul_one_div, div_self hy', one_mul],
      rw [this, div_eq_mul_inv, one_mul, â†div_eq_mul_inv] },
    have hâ‚„ : âŸªy, yâŸ« = ğ“š (re âŸªy, yâŸ«) := by simp,
    have hâ‚… : re âŸªy, yâŸ« > 0,
    { refine lt_of_le_of_ne inner_self_nonneg _,
      intro H,
      apply hy',
      rw is_R_or_C.ext_iff,
      exact âŸ¨by simp [H],by simp [inner_self_nonneg_im]âŸ© },
    have hâ‚† : re âŸªy, yâŸ« â‰  0 := ne_of_gt hâ‚…,
    have hmain := calc
      0   â‰¤ re âŸªx - T â€¢ y, x - T â€¢ yâŸ«
                  : inner_self_nonneg
      ... = re âŸªx, xâŸ« - re âŸªT â€¢ y, xâŸ« - re âŸªx, T â€¢ yâŸ« + re âŸªT â€¢ y, T â€¢ yâŸ«
                  : by simp [inner_sub_sub_self, inner_smul_left, inner_smul_right, hâ‚, hâ‚‚]
      ... = re âŸªx, xâŸ« - re (Tâ€  * âŸªy, xâŸ«) - re (T * âŸªx, yâŸ«) + re (T * Tâ€  * âŸªy, yâŸ«)
                  : by simp [inner_smul_left, inner_smul_right, mul_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« / âŸªy, yâŸ« * âŸªy, xâŸ«)
                  : by field_simp [-mul_re, hT, conj_div, hâ‚, hâ‚ƒ, inner_conj_sym]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / âŸªy, yâŸ«)
                  : by rw [div_mul_eq_mul_div_comm, â†mul_div_assoc]
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ« / ğ“š (re âŸªy, yâŸ«))
                  : by conv_lhs { rw [hâ‚„] }
      ... = re âŸªx, xâŸ« - re (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [div_re_of_real]
      ... = re âŸªx, xâŸ« - abs (âŸªx, yâŸ« * âŸªy, xâŸ«) / re âŸªy, yâŸ«
                  : by rw [inner_mul_conj_re_abs]
      ... = re âŸªx, xâŸ« - abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ«
                  : by rw is_R_or_C.abs_mul,
    have hmain' : abs âŸªx, yâŸ« * abs âŸªy, xâŸ« / re âŸªy, yâŸ« â‰¤ re âŸªx, xâŸ« := by linarith,
    have := (mul_le_mul_right hâ‚…).mpr hmain',
    rwa [div_mul_cancel (abs âŸªx, yâŸ« * abs âŸªy, xâŸ«) hâ‚†] at this }
end

/-- Cauchyâ€“Schwarz inequality for real inner products. -/
lemma real_inner_mul_inner_self_le (x y : F) : âŸªx, yâŸ«_â„ * âŸªx, yâŸ«_â„ â‰¤ âŸªx, xâŸ«_â„ * âŸªy, yâŸ«_â„ :=
begin
  have hâ‚ : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := by rw [â†inner_conj_sym]; refl,
  have hâ‚‚ := @inner_mul_inner_self_le â„ F _ _ x y,
  dsimp at hâ‚‚,
  have hâ‚ƒ := abs_mul_abs_self âŸªx, yâŸ«_â„,
  rw [hâ‚] at hâ‚‚,
  simpa [hâ‚ƒ] using hâ‚‚,
end

/-- A family of vectors is linearly independent if they are nonzero
and orthogonal. -/
lemma linear_independent_of_ne_zero_of_inner_eq_zero {Î¹ : Type*} {v : Î¹ â†’ E}
  (hz : âˆ€ i, v i â‰  0) (ho : âˆ€ i j, i â‰  j â†’ âŸªv i, v jâŸ« = 0) : linear_independent ğ•œ v :=
begin
  rw linear_independent_iff',
  intros s g hg i hi,
  have h' : g i * inner (v i) (v i) = inner (v i) (âˆ‘ j in s, g j â€¢ v j),
  { rw inner_sum,
    symmetry,
    convert finset.sum_eq_single i _ _,
    { rw inner_smul_right },
    { intros j hj hji,
      rw [inner_smul_right, ho i j hji.symm, mul_zero] },
    { exact Î» h, false.elim (h hi) } },
  simpa [hg, hz] using h'
end

end basic_properties

section norm

lemma norm_eq_sqrt_inner (x : E) : âˆ¥xâˆ¥ = sqrt (re âŸªx, xâŸ«) :=
begin
  have hâ‚ : âˆ¥xâˆ¥^2 = re âŸªx, xâŸ« := norm_sq_eq_inner x,
  have hâ‚‚ := congr_arg sqrt hâ‚,
  simpa using hâ‚‚,
end

lemma norm_eq_sqrt_real_inner (x : F) : âˆ¥xâˆ¥ = sqrt âŸªx, xâŸ«_â„ :=
by { have h := @norm_eq_sqrt_inner â„ F _ _ x, simpa using h }

lemma inner_self_eq_norm_square (x : E) : re âŸªx, xâŸ« = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ :=
by rw[norm_eq_sqrt_inner, â†sqrt_mul inner_self_nonneg (re âŸªx, xâŸ«),
  sqrt_mul_self inner_self_nonneg]

lemma real_inner_self_eq_norm_square (x : F) : âŸªx, xâŸ«_â„ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ :=
by { have h := @inner_self_eq_norm_square â„ F _ _ x, simpa using h }


/-- Expand the square -/
lemma norm_add_pow_two {x y : E} : âˆ¥x + yâˆ¥^2 = âˆ¥xâˆ¥^2 + 2 * (re âŸªx, yâŸ«) + âˆ¥yâˆ¥^2 :=
begin
  repeat {rw [pow_two, â†inner_self_eq_norm_square]},
  rw[inner_add_add_self, two_mul],
  simp only [add_assoc, add_left_inj, add_right_inj, add_monoid_hom.map_add],
  rw [â†inner_conj_sym, conj_re],
end

/-- Expand the square -/
lemma norm_add_pow_two_real {x y : F} : âˆ¥x + yâˆ¥^2 = âˆ¥xâˆ¥^2 + 2 * âŸªx, yâŸ«_â„ + âˆ¥yâˆ¥^2 :=
by { have h := @norm_add_pow_two â„ F _ _, simpa using h }

/-- Expand the square -/
lemma norm_add_mul_self {x y : E} : âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + 2 * (re âŸªx, yâŸ«) + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ :=
by { repeat {rw [â† pow_two]}, exact norm_add_pow_two }

/-- Expand the square -/
lemma norm_add_mul_self_real {x y : F} : âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + 2 * âŸªx, yâŸ«_â„ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ :=
by { have h := @norm_add_mul_self â„ F _ _, simpa using h }

/-- Expand the square -/
lemma norm_sub_pow_two {x y : E} : âˆ¥x - yâˆ¥^2 = âˆ¥xâˆ¥^2 - 2 * (re âŸªx, yâŸ«) + âˆ¥yâˆ¥^2 :=
begin
  repeat {rw [pow_two, â†inner_self_eq_norm_square]},
  rw[inner_sub_sub_self],
  calc
    re (âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ«)
        = re âŸªx, xâŸ« - re âŸªx, yâŸ« - re âŸªy, xâŸ« + re âŸªy, yâŸ«  : by simp
    ... = -re âŸªy, xâŸ« - re âŸªx, yâŸ« + re âŸªx, xâŸ« + re âŸªy, yâŸ«  : by ring
    ... = -re (âŸªx, yâŸ«â€ ) - re âŸªx, yâŸ« + re âŸªx, xâŸ« + re âŸªy, yâŸ« : by rw[inner_conj_sym]
    ... = -re âŸªx, yâŸ« - re âŸªx, yâŸ« + re âŸªx, xâŸ« + re âŸªy, yâŸ« : by rw[conj_re]
    ... = re âŸªx, xâŸ« - 2*re âŸªx, yâŸ« + re âŸªy, yâŸ« : by ring
end

/-- Expand the square -/
lemma norm_sub_pow_two_real {x y : F} : âˆ¥x - yâˆ¥^2 = âˆ¥xâˆ¥^2 - 2 * âŸªx, yâŸ«_â„ + âˆ¥yâˆ¥^2 :=
by { have h := @norm_sub_pow_two â„ F _ _, simpa using h }

/-- Expand the square -/
lemma norm_sub_mul_self {x y : E} : âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ - 2 * re âŸªx, yâŸ« + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ :=
by { repeat {rw [â† pow_two]}, exact norm_sub_pow_two }

/-- Expand the square -/
lemma norm_sub_mul_self_real {x y : F} : âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ - 2 * âŸªx, yâŸ«_â„ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ :=
by { have h := @norm_sub_mul_self â„ F _ _, simpa using h }

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma abs_inner_le_norm (x y : E) : abs âŸªx, yâŸ« â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ :=
nonneg_le_nonneg_of_squares_le (mul_nonneg (norm_nonneg _) (norm_nonneg _))
begin
  have : âˆ¥xâˆ¥ * âˆ¥yâˆ¥ * (âˆ¥xâˆ¥ * âˆ¥yâˆ¥) = (re âŸªx, xâŸ«) * (re âŸªy, yâŸ«),
    simp only [inner_self_eq_norm_square], ring,
  rw this,
  conv_lhs { congr, skip, rw [inner_abs_conj_sym] },
  exact inner_mul_inner_self_le _ _
end

/-- Cauchyâ€“Schwarz inequality with norm -/
lemma abs_real_inner_le_norm (x y : F) : absR âŸªx, yâŸ«_â„ â‰¤ âˆ¥xâˆ¥ * âˆ¥yâˆ¥ :=
by { have h := @abs_inner_le_norm â„ F _ _ x y, simpa using h }

include ğ•œ
lemma parallelogram_law_with_norm {x y : E} :
  âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ + âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥ = 2 * (âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥) :=
begin
  simp only [(inner_self_eq_norm_square _).symm],
  rw[â†add_monoid_hom.map_add, parallelogram_law, two_mul, two_mul],
  simp only [add_monoid_hom.map_add],
end
omit ğ•œ

lemma parallelogram_law_with_norm_real {x y : F} :
  âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ + âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥ = 2 * (âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥) :=
by { have h := @parallelogram_law_with_norm â„ F _ _ x y, simpa using h }

/-- Polarization identity: The real inner product, in terms of the norm. -/
lemma real_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two (x y : F) :
  âŸªx, yâŸ«_â„ = (âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ - âˆ¥xâˆ¥ * âˆ¥xâˆ¥ - âˆ¥yâˆ¥ * âˆ¥yâˆ¥) / 2 :=
by rw norm_add_mul_self; ring

/-- Polarization identity: The real inner product, in terms of the norm. -/
lemma real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two (x y : F) :
  âŸªx, yâŸ«_â„ = (âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ - âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥) / 2 :=
by rw norm_sub_mul_self; ring

/-- Pythagorean theorem, if-and-only-if vector inner product form. -/
lemma norm_add_square_eq_norm_square_add_norm_square_iff_real_inner_eq_zero (x y : F) :
  âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ â†” âŸªx, yâŸ«_â„ = 0 :=
begin
  rw [norm_add_mul_self, add_right_cancel_iff, add_right_eq_self, mul_eq_zero],
  norm_num
end

/-- Pythagorean theorem, vector inner product form. -/
lemma norm_add_square_eq_norm_square_add_norm_square_real {x y : F} (h : âŸªx, yâŸ«_â„ = 0) :
  âˆ¥x + yâˆ¥ * âˆ¥x + yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ :=
(norm_add_square_eq_norm_square_add_norm_square_iff_real_inner_eq_zero x y).2 h

/-- Pythagorean theorem, subtracting vectors, if-and-only-if vector
inner product form. -/
lemma norm_sub_square_eq_norm_square_add_norm_square_iff_real_inner_eq_zero (x y : F) :
  âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ â†” âŸªx, yâŸ«_â„ = 0 :=
begin
  rw [norm_sub_mul_self, add_right_cancel_iff, sub_eq_add_neg, add_right_eq_self, neg_eq_zero,
      mul_eq_zero],
  norm_num
end

/-- Pythagorean theorem, subtracting vectors, vector inner product
form. -/
lemma norm_sub_square_eq_norm_square_add_norm_square_real {x y : F} (h : âŸªx, yâŸ«_â„ = 0) :
  âˆ¥x - yâˆ¥ * âˆ¥x - yâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ + âˆ¥yâˆ¥ * âˆ¥yâˆ¥ :=
(norm_sub_square_eq_norm_square_add_norm_square_iff_real_inner_eq_zero x y).2 h

/-- The sum and difference of two vectors are orthogonal if and only
if they have the same norm. -/
lemma real_inner_add_sub_eq_zero_iff (x y : F) : âŸªx + y, x - yâŸ«_â„ = 0 â†” âˆ¥xâˆ¥ = âˆ¥yâˆ¥ :=
begin
  conv_rhs { rw â†mul_self_inj_of_nonneg (norm_nonneg _) (norm_nonneg _) },
  simp only [â†inner_self_eq_norm_square, inner_add_left, inner_sub_right,
            real_inner_comm y x, sub_eq_zero, re_to_real],
  split,
  { intro h,
    rw [add_comm] at h,
    linarith },
  { intro h,
    linarith }
end

/-- The real inner product of two vectors, divided by the product of their
norms, has absolute value at most 1. -/
lemma abs_real_inner_div_norm_mul_norm_le_one (x y : F) : absR (âŸªx, yâŸ«_â„ / (âˆ¥xâˆ¥ * âˆ¥yâˆ¥)) â‰¤ 1 :=
begin
  rw _root_.abs_div,
  by_cases h : 0 = absR (âˆ¥xâˆ¥ * âˆ¥yâˆ¥),
  { rw [â†h, div_zero],
    norm_num },
  { change 0 â‰  absR (âˆ¥xâˆ¥ * âˆ¥yâˆ¥) at h,
    rw div_le_iff' (lt_of_le_of_ne (ge_iff_le.mp (_root_.abs_nonneg (âˆ¥xâˆ¥ * âˆ¥yâˆ¥))) h),
    convert abs_real_inner_le_norm x y using 1,
    rw [_root_.abs_mul, _root_.abs_of_nonneg (norm_nonneg x), _root_.abs_of_nonneg (norm_nonneg y), mul_one] }
end

/-- The inner product of a vector with a multiple of itself. -/
lemma real_inner_smul_self_left (x : F) (r : â„) : âŸªr â€¢ x, xâŸ«_â„ = r * (âˆ¥xâˆ¥ * âˆ¥xâˆ¥) :=
by rw [real_inner_smul_left, â†real_inner_self_eq_norm_square]

/-- The inner product of a vector with a multiple of itself. -/
lemma real_inner_smul_self_right (x : F) (r : â„) : âŸªx, r â€¢ xâŸ«_â„ = r * (âˆ¥xâˆ¥ * âˆ¥xâˆ¥) :=
by rw [inner_smul_right, â†real_inner_self_eq_norm_square]

/-- The inner product of a nonzero vector with a nonzero multiple of
itself, divided by the product of their norms, has absolute value
1. -/
lemma abs_real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul
  {x : F} {r : â„} (hx : x â‰  0) (hr : r â‰  0) : absR âŸªx, r â€¢ xâŸ«_â„ / (âˆ¥xâˆ¥ * âˆ¥r â€¢ xâˆ¥) = 1 :=
begin
  simp [real_inner_smul_self_right, norm_smul, _root_.abs_mul, norm_eq_abs],
  conv_lhs { congr, rw [â†mul_assoc, mul_comm] },
  apply div_self,
  intro h,
  rcases (mul_eq_zero.mp h) with hâ‚|hâ‚‚,
  { exact hx (norm_eq_zero.mp hâ‚) },
  { rcases (mul_eq_zero.mp hâ‚‚) with hâ‚‚'|hâ‚‚'',
    { rw [_root_.abs_eq_zero] at hâ‚‚',
      exact hr hâ‚‚' },
    { exact hx (norm_eq_zero.mp hâ‚‚'') } }
end

/-- The inner product of a nonzero vector with a positive multiple of
itself, divided by the product of their norms, has value 1. -/
lemma real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul
  {x : F} {r : â„} (hx : x â‰  0) (hr : 0 < r) : âŸªx, r â€¢ xâŸ«_â„ / (âˆ¥xâˆ¥ * âˆ¥r â€¢ xâˆ¥) = 1 :=
begin
  rw [real_inner_smul_self_right, norm_smul, norm_eq_abs, â†mul_assoc âˆ¥xâˆ¥, mul_comm _ (absR r),
      mul_assoc, _root_.abs_of_nonneg (le_of_lt hr), div_self],
  exact mul_ne_zero (ne_of_gt hr)
    (Î» h, hx (norm_eq_zero.1 (eq_zero_of_mul_self_eq_zero h)))
end

/-- The inner product of a nonzero vector with a negative multiple of
itself, divided by the product of their norms, has value -1. -/
lemma real_inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul
  {x : F} {r : â„} (hx : x â‰  0) (hr : r < 0) : âŸªx, r â€¢ xâŸ«_â„ / (âˆ¥xâˆ¥ * âˆ¥r â€¢ xâˆ¥) = -1 :=
begin
  rw [real_inner_smul_self_right, norm_smul, real.norm_eq_abs, â†mul_assoc âˆ¥xâˆ¥, mul_comm _ (absR r),
      mul_assoc, abs_of_neg hr, â†neg_mul_eq_neg_mul, div_neg_eq_neg_div, div_self],
  exact mul_ne_zero (ne_of_lt hr)
    (Î» h, hx (norm_eq_zero.1 (eq_zero_of_mul_self_eq_zero h)))
end

/-- The inner product of two vectors, divided by the product of their
norms, has absolute value 1 if and only if they are nonzero and one is
a multiple of the other. One form of equality case for Cauchy-Schwarz. -/
lemma abs_real_inner_div_norm_mul_norm_eq_one_iff (x y : F) :
  absR (âŸªx, yâŸ«_â„ / (âˆ¥xâˆ¥ * âˆ¥yâˆ¥)) = 1 â†” (x â‰  0 âˆ§ âˆƒ (r : â„), r â‰  0 âˆ§ y = r â€¢ x) :=
begin
  split,
  { intro h,
    have hx0 : x â‰  0,
    { intro hx0,
      rw [hx0, inner_zero_left, zero_div] at h,
      norm_num at h,
      exact h },
    refine and.intro hx0 _,
    set r := inner x y / (âˆ¥xâˆ¥ * âˆ¥xâˆ¥) with hr,
    use r,
    set t := y - r â€¢ x with ht,
    have ht0 : âŸªx, tâŸ«_â„ = 0,
    { rw [ht, inner_sub_right, inner_smul_right, hr, â†real_inner_self_eq_norm_square,
          div_mul_cancel _ (Î» h, hx0 (inner_self_eq_zero.1 h)), sub_self] },
    rw [â†sub_add_cancel y (r â€¢ x), â†ht, inner_add_right, ht0, zero_add, inner_smul_right,
        real_inner_self_eq_norm_square, â†mul_assoc, mul_comm,
        mul_div_mul_left _ _ (Î» h, hx0 (norm_eq_zero.1 h)), _root_.abs_div, _root_.abs_mul,
        _root_.abs_of_nonneg (norm_nonneg _), _root_.abs_of_nonneg (norm_nonneg _), â†real.norm_eq_abs,
        â†norm_smul] at h,
    have hr0 : r â‰  0,
    { intro hr0,
      rw [hr0, zero_smul, norm_zero, zero_div] at h,
      norm_num at h },
    refine and.intro hr0 _,
    have h2 : âˆ¥r â€¢ xâˆ¥ ^ 2 = âˆ¥t + r â€¢ xâˆ¥ ^ 2,
    { rw [eq_of_div_eq_one h] },
    rw [pow_two, pow_two, â†real_inner_self_eq_norm_square, â†real_inner_self_eq_norm_square,
        inner_add_add_self] at h2,
    conv_rhs at h2 {
      congr,
      congr,
      skip,
      rw [real_inner_smul_left, ht0, mul_zero]
    },
    symmetry' at h2,
    have hâ‚ : âŸªt, r â€¢ xâŸ«_â„ = 0 := by rw [inner_smul_right, real_inner_comm, ht0, mul_zero],
    rw [add_zero, hâ‚, add_left_eq_self, add_zero, inner_self_eq_zero] at h2,
    rw h2 at ht,
    exact eq_of_sub_eq_zero ht.symm },
  { intro h,
    rcases h with âŸ¨hx, âŸ¨r, âŸ¨hr, hyâŸ©âŸ©âŸ©,
    rw hy,
    rw [_root_.abs_div, _root_.abs_mul, abs_norm_eq_norm, abs_norm_eq_norm],
    exact abs_real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul hx hr }
end

/-- The inner product of two vectors, divided by the product of their
norms, has value 1 if and only if they are nonzero and one is
a positive multiple of the other. -/
lemma real_inner_div_norm_mul_norm_eq_one_iff (x y : F) :
  âŸªx, yâŸ«_â„ / (âˆ¥xâˆ¥ * âˆ¥yâˆ¥) = 1 â†” (x â‰  0 âˆ§ âˆƒ (r : â„), 0 < r âˆ§ y = r â€¢ x) :=
begin
  split,
  { intro h,
    have ha := h,
    apply_fun absR at ha,
    norm_num at ha,
    rcases (abs_real_inner_div_norm_mul_norm_eq_one_iff x y).1 ha with âŸ¨hx, âŸ¨r, âŸ¨hr, hyâŸ©âŸ©âŸ©,
    use [hx, r],
    refine and.intro _ hy,
    by_contradiction hrneg,
    rw hy at h,
    rw real_inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul hx
      (lt_of_le_of_ne (le_of_not_lt hrneg) hr) at h,
    norm_num at h },
  { intro h,
    rcases h with âŸ¨hx, âŸ¨r, âŸ¨hr, hyâŸ©âŸ©âŸ©,
    rw hy,
    exact real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul hx hr }
end

/-- The inner product of two vectors, divided by the product of their
norms, has value -1 if and only if they are nonzero and one is
a negative multiple of the other. -/
lemma real_inner_div_norm_mul_norm_eq_neg_one_iff (x y : F) :
  âŸªx, yâŸ«_â„ / (âˆ¥xâˆ¥ * âˆ¥yâˆ¥) = -1 â†” (x â‰  0 âˆ§ âˆƒ (r : â„), r < 0 âˆ§ y = r â€¢ x) :=
begin
  split,
  { intro h,
    have ha := h,
    apply_fun absR at ha,
    norm_num at ha,
    rcases (abs_real_inner_div_norm_mul_norm_eq_one_iff x y).1 ha with âŸ¨hx, âŸ¨r, âŸ¨hr, hyâŸ©âŸ©âŸ©,
    use [hx, r],
    refine and.intro _ hy,
    by_contradiction hrpos,
    rw hy at h,
    rw real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul hx
      (lt_of_le_of_ne (le_of_not_lt hrpos) hr.symm) at h,
    norm_num at h },
  { intro h,
    rcases h with âŸ¨hx, âŸ¨r, âŸ¨hr, hyâŸ©âŸ©âŸ©,
    rw hy,
    exact real_inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul hx hr }
end

/-- The inner product of two weighted sums, where the weights in each
sum add to 0, in terms of the norms of pairwise differences. -/
lemma inner_sum_smul_sum_smul_of_sum_eq_zero {Î¹â‚ : Type*} {sâ‚ : finset Î¹â‚} {wâ‚ : Î¹â‚ â†’ â„}
    (vâ‚ : Î¹â‚ â†’ F) (hâ‚ : âˆ‘ i in sâ‚, wâ‚ i = 0) {Î¹â‚‚ : Type*} {sâ‚‚ : finset Î¹â‚‚} {wâ‚‚ : Î¹â‚‚ â†’ â„}
    (vâ‚‚ : Î¹â‚‚ â†’ F) (hâ‚‚ : âˆ‘ i in sâ‚‚, wâ‚‚ i = 0) :
  âŸª(âˆ‘ iâ‚ in sâ‚, wâ‚ iâ‚ â€¢ vâ‚ iâ‚), (âˆ‘ iâ‚‚ in sâ‚‚, wâ‚‚ iâ‚‚ â€¢ vâ‚‚ iâ‚‚)âŸ«_â„ =
    (-âˆ‘ iâ‚ in sâ‚, âˆ‘ iâ‚‚ in sâ‚‚, wâ‚ iâ‚ * wâ‚‚ iâ‚‚ * (âˆ¥vâ‚ iâ‚ - vâ‚‚ iâ‚‚âˆ¥ * âˆ¥vâ‚ iâ‚ - vâ‚‚ iâ‚‚âˆ¥)) / 2 :=
by simp_rw [sum_inner, inner_sum, real_inner_smul_left, real_inner_smul_right,
            real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two,
            â†div_sub_div_same, â†div_add_div_same, mul_sub_left_distrib, left_distrib,
            finset.sum_sub_distrib, finset.sum_add_distrib, â†finset.mul_sum, â†finset.sum_mul,
            hâ‚, hâ‚‚, zero_mul, mul_zero, finset.sum_const_zero, zero_add, zero_sub, finset.mul_sum,
            neg_div, finset.sum_div, mul_div_assoc, mul_assoc]

end norm

/-! ### Inner product space structure on product spaces -/

/-
 If `Î¹` is a finite type and each space `f i`, `i : Î¹`, is an inner product space,
then `Î  i, f i` is an inner product space as well. Since `Î  i, f i` is endowed with the sup norm,
we use instead `pi_Lp 2 one_le_two f` for the product space, which is endowed with the `L^2` norm.
-/
instance pi_Lp.inner_product_space {Î¹ : Type*} [fintype Î¹] (f : Î¹ â†’ Type*)
  [Î  i, inner_product_space ğ•œ (f i)] : inner_product_space ğ•œ (pi_Lp 2 one_le_two f) :=
{ inner := Î» x y, âˆ‘ i, inner (x i) (y i),
  norm_sq_eq_inner :=
  begin
    intro x,
    have hâ‚ : âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„•) = âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„),
    { apply finset.sum_congr rfl,
      intros j hj,
      simp [â†rpow_nat_cast] },
    have hâ‚‚ : 0 â‰¤ âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„),
    { rw [â†hâ‚],
      exact finset.sum_nonneg (Î» (j : Î¹) (hj : j âˆˆ finset.univ), pow_nonneg (norm_nonneg (x j)) 2) },
    simp [norm, add_monoid_hom.map_sum, â†norm_sq_eq_inner],
    rw [â†rpow_nat_cast ((âˆ‘ (i : Î¹), âˆ¥x iâˆ¥ ^ (2 : â„)) ^ (2 : â„)â»Â¹) 2],
    rw [â†rpow_mul hâ‚‚],
    norm_num [hâ‚],
  end,
  conj_sym :=
  begin
    intros x y,
    unfold inner,
    rw [â†finset.sum_hom finset.univ conj],
    apply finset.sum_congr rfl,
    rintros z -,
    apply inner_conj_sym,
    apply_instance
  end,
  nonneg_im :=
  begin
    intro x,
    unfold inner,
    rw[â†finset.sum_hom finset.univ im],
    apply finset.sum_eq_zero,
    rintros z -,
    exact inner_self_nonneg_im,
    apply_instance
  end,
  add_left := Î» x y z,
    show âˆ‘ i, inner (x i + y i) (z i) = âˆ‘ i, inner (x i) (z i) + âˆ‘ i, inner (y i) (z i),
    by simp only [inner_add_left, finset.sum_add_distrib],
  smul_left := Î» x y r,
    show âˆ‘ (i : Î¹), inner (r â€¢ x i) (y i) = (conj r) * âˆ‘ i, inner (x i) (y i),
    by simp only [finset.mul_sum, inner_smul_left]
}

/-- A field `ğ•œ` satisfying `is_R_or_C` is itself a `ğ•œ`-inner product space. -/
instance is_R_or_C.inner_product_space : inner_product_space ğ•œ ğ•œ :=
{ inner := (Î» x y, (conj x) * y),
  norm_sq_eq_inner := Î» x, by unfold inner; rw [mul_comm, mul_conj, of_real_re, norm_sq, norm_sq_eq_def],
  conj_sym := Î» x y, by simp [mul_comm],
  nonneg_im := Î» x, by rw[mul_im, conj_re, conj_im]; ring,
  add_left := Î» x y z, by simp [inner, add_mul],
  smul_left := Î» x y z, by simp [inner, mul_assoc] }


/-- The standard real/complex Euclidean space, functions on a finite type. For an `n`-dimensional space
use `euclidean_space ğ•œ (fin n)`.  -/
@[reducible, nolint unused_arguments]
def euclidean_space (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ] [normed_algebra â„ ğ•œ] [is_R_or_C ğ•œ]
  (n : Type*) [fintype n] : Type* := pi_Lp 2 one_le_two (Î» (i : n), ğ•œ)

section is_R_or_C_to_real

variables {G : Type*}

variables (ğ•œ)
include ğ•œ

/-- A general inner product implies a real inner product. This is not registered as an instance
since it creates problems with the case `ğ•œ = â„`. -/
def has_inner.is_R_or_C_to_real : has_inner â„ E :=
{ inner := Î» x y, re âŸªx, yâŸ« }

lemma real_inner_eq_re_inner (x y : E) :
  @has_inner.inner â„ E (has_inner.is_R_or_C_to_real ğ•œ) x y = re âŸªx, yâŸ« := rfl

/-- A general inner product space structure implies a real inner product structure. This is not
registered as an instance since it creates problems with the case `ğ•œ = â„`, but in can be used in a
proof to obtain a real inner product space structure from a given `ğ•œ`-inner product space
structure. -/
def inner_product_space.is_R_or_C_to_real : inner_product_space â„ E :=
{ norm_sq_eq_inner := norm_sq_eq_inner,
  conj_sym := Î» x y, inner_re_symm,
  nonneg_im := Î» x, rfl,
  add_left := Î» x y z, by { change re âŸªx + y, zâŸ« = re âŸªx, zâŸ« + re âŸªy, zâŸ«, simp [inner_add_left] },
  smul_left :=
  begin
    intros x y r,
    change re âŸª(algebra_map â„ ğ•œ r) â€¢ x, yâŸ« = r * re âŸªx, yâŸ«,
    have : algebra_map â„ ğ•œ r = r â€¢ (1 : ğ•œ) := by simp [algebra_map, algebra.smul_def'],
    simp [this, inner_smul_left, smul_coe_mul_ax],
  end,
  ..has_inner.is_R_or_C_to_real ğ•œ,
  ..normed_space.restrict_scalars' â„ ğ•œ E }

omit ğ•œ

/-- A complex inner product implies a real inner product -/
instance inner_product_space.complex_to_real [inner_product_space â„‚ G] : inner_product_space â„ G :=
inner_product_space.is_R_or_C_to_real â„‚

end is_R_or_C_to_real

section pi_Lp
local attribute [reducible] pi_Lp
variables {Î¹ : Type*} [fintype Î¹]

instance : finite_dimensional ğ•œ (euclidean_space ğ•œ Î¹) := by apply_instance

@[simp] lemma findim_euclidean_space :
  finite_dimensional.findim ğ•œ (euclidean_space ğ•œ Î¹) = fintype.card Î¹ := by simp

lemma findim_euclidean_space_fin {n : â„•} :
  finite_dimensional.findim ğ•œ (euclidean_space ğ•œ (fin n)) = n := by simp

end pi_Lp


/-! ### Orthogonal projection in inner product spaces -/

section orthogonal

open filter

/--
Existence of minimizers
Let `u` be a point in a real inner product space, and let `K` be a nonempty complete convex subset.
Then there exists a (unique) `v` in `K` that minimizes the distance `âˆ¥u - vâˆ¥` to `u`.
 -/
-- FIXME this monolithic proof causes a deterministic timeout with `-T50000`
-- It should be broken in a sequence of more manageable pieces,
-- perhaps with individual statements for the three steps below.
theorem exists_norm_eq_infi_of_complete_convex {K : set F} (ne : K.nonempty) (hâ‚ : is_complete K)
  (hâ‚‚ : convex K) : âˆ€ u : F, âˆƒ v âˆˆ K, âˆ¥u - vâˆ¥ = â¨… w : K, âˆ¥u - wâˆ¥ := assume u,
begin
  let Î´ := â¨… w : K, âˆ¥u - wâˆ¥,
  letI : nonempty K := ne.to_subtype,
  have zero_le_Î´ : 0 â‰¤ Î´ := le_cinfi (Î» _, norm_nonneg _),
  have Î´_le : âˆ€ w : K, Î´ â‰¤ âˆ¥u - wâˆ¥,
    from cinfi_le âŸ¨0, set.forall_range_iff.2 $ Î» _, norm_nonneg _âŸ©,
  have Î´_le' : âˆ€ w âˆˆ K, Î´ â‰¤ âˆ¥u - wâˆ¥ := assume w hw, Î´_le âŸ¨w, hwâŸ©,
  -- Step 1: since `Î´` is the infimum, can find a sequence `w : â„• â†’ K` in `K`
  -- such that `âˆ¥u - w nâˆ¥ < Î´ + 1 / (n + 1)` (which implies `âˆ¥u - w nâˆ¥ --> Î´`);
  -- maybe this should be a separate lemma
  have exists_seq : âˆƒ w : â„• â†’ K, âˆ€ n, âˆ¥u - w nâˆ¥ < Î´ + 1 / (n + 1),
  { have hÎ´ : âˆ€n:â„•, Î´ < Î´ + 1 / (n + 1), from
      Î» n, lt_add_of_le_of_pos (le_refl _) nat.one_div_pos_of_nat,
    have h := Î» n, exists_lt_of_cinfi_lt (hÎ´ n),
    let w : â„• â†’ K := Î» n, classical.some (h n),
    exact âŸ¨w, Î» n, classical.some_spec (h n)âŸ© },
  rcases exists_seq with âŸ¨w, hwâŸ©,
  have norm_tendsto : tendsto (Î» n, âˆ¥u - w nâˆ¥) at_top (nhds Î´),
  { have h : tendsto (Î» n:â„•, Î´) at_top (nhds Î´) := tendsto_const_nhds,
    have h' : tendsto (Î» n:â„•, Î´ + 1 / (n + 1)) at_top (nhds Î´),
    { convert h.add tendsto_one_div_add_at_top_nhds_0_nat, simp only [add_zero] },
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le h h'
      (Î» x, Î´_le _) (Î» x, le_of_lt (hw _)) },
  -- Step 2: Prove that the sequence `w : â„• â†’ K` is a Cauchy sequence
  have seq_is_cauchy : cauchy_seq (Î» n, ((w n):F)),
  { rw cauchy_seq_iff_le_tendsto_0, -- splits into three goals
    let b := Î» n:â„•, (8 * Î´ * (1/(n+1)) + 4 * (1/(n+1)) * (1/(n+1))),
    use (Î»n, sqrt (b n)),
    split,
    -- first goal :  `âˆ€ (n : â„•), 0 â‰¤ sqrt (b n)`
    assume n, exact sqrt_nonneg _,
    split,
    -- second goal : `âˆ€ (n m N : â„•), N â‰¤ n â†’ N â‰¤ m â†’ dist â†‘(w n) â†‘(w m) â‰¤ sqrt (b N)`
    assume p q N hp hq,
    let wp := ((w p):F), let wq := ((w q):F),
    let a := u - wq, let b := u - wp,
    let half := 1 / (2:â„), let div := 1 / ((N:â„) + 1),
    have : 4 * âˆ¥u - half â€¢ (wq + wp)âˆ¥ * âˆ¥u - half â€¢ (wq + wp)âˆ¥ + âˆ¥wp - wqâˆ¥ * âˆ¥wp - wqâˆ¥ =
      2 * (âˆ¥aâˆ¥ * âˆ¥aâˆ¥ + âˆ¥bâˆ¥ * âˆ¥bâˆ¥) :=
    calc
      4 * âˆ¥u - halfâ€¢(wq + wp)âˆ¥ * âˆ¥u - halfâ€¢(wq + wp)âˆ¥ + âˆ¥wp - wqâˆ¥ * âˆ¥wp - wqâˆ¥
          = (2*âˆ¥u - halfâ€¢(wq + wp)âˆ¥) * (2 * âˆ¥u - halfâ€¢(wq + wp)âˆ¥) + âˆ¥wp-wqâˆ¥*âˆ¥wp-wqâˆ¥ : by ring
      ... = (absR ((2:â„)) * âˆ¥u - halfâ€¢(wq + wp)âˆ¥) * (absR ((2:â„)) * âˆ¥u - halfâ€¢(wq+wp)âˆ¥) + âˆ¥wp-wqâˆ¥*âˆ¥wp-wqâˆ¥ :
      by { rw _root_.abs_of_nonneg, exact add_nonneg zero_le_one zero_le_one }
      ... = âˆ¥(2:â„) â€¢ (u - half â€¢ (wq + wp))âˆ¥ * âˆ¥(2:â„) â€¢ (u - half â€¢ (wq + wp))âˆ¥ + âˆ¥wp-wqâˆ¥ * âˆ¥wp-wqâˆ¥ :
      by simp [norm_smul]
      ... = âˆ¥a + bâˆ¥ * âˆ¥a + bâˆ¥ + âˆ¥a - bâˆ¥ * âˆ¥a - bâˆ¥ :
      begin
        rw [smul_sub, smul_smul, mul_one_div_cancel (_root_.two_ne_zero : (2 : â„) â‰  0),
            â† one_add_one_eq_two, add_smul],
        simp only [one_smul],
        have eqâ‚ : wp - wq = a - b := calc
            wp - wq = (u - wq) - (u - wp)    : by rw [sub_sub_assoc_swap, add_sub_assoc, sub_add_sub_cancel']
                ... = a - b                  : rfl,
        have eqâ‚‚ : u + u - (wq + wp) = a + b, show u + u - (wq + wp) = (u - wq) + (u - wp), abel,
        rw [eqâ‚, eqâ‚‚],
      end
      ... = 2 * (âˆ¥aâˆ¥ * âˆ¥aâˆ¥ + âˆ¥bâˆ¥ * âˆ¥bâˆ¥) : parallelogram_law_with_norm,
    have eq : Î´ â‰¤ âˆ¥u - half â€¢ (wq + wp)âˆ¥,
    { rw smul_add,
      apply Î´_le', apply hâ‚‚,
        repeat {exact subtype.mem _},
        repeat {exact le_of_lt one_half_pos},
        exact add_halves 1 },
    have eqâ‚ : 4 * Î´ * Î´ â‰¤ 4 * âˆ¥u - half â€¢ (wq + wp)âˆ¥ * âˆ¥u - half â€¢ (wq + wp)âˆ¥,
    {  mono, mono, norm_num, apply mul_nonneg, norm_num, exact norm_nonneg _ },
    have eqâ‚‚ : âˆ¥aâˆ¥ * âˆ¥aâˆ¥ â‰¤ (Î´ + div) * (Î´ + div) :=
      mul_self_le_mul_self (norm_nonneg _)
        (le_trans (le_of_lt $ hw q) (add_le_add_left (nat.one_div_le_one_div hq) _)),
    have eqâ‚‚' : âˆ¥bâˆ¥ * âˆ¥bâˆ¥ â‰¤ (Î´ + div) * (Î´ + div) :=
      mul_self_le_mul_self (norm_nonneg _)
        (le_trans (le_of_lt $ hw p) (add_le_add_left (nat.one_div_le_one_div hp) _)),
    rw dist_eq_norm,
    apply nonneg_le_nonneg_of_squares_le, { exact sqrt_nonneg _ },
    rw mul_self_sqrt,
    exact calc
      âˆ¥wp - wqâˆ¥ * âˆ¥wp - wqâˆ¥ = 2 * (âˆ¥aâˆ¥*âˆ¥aâˆ¥ + âˆ¥bâˆ¥*âˆ¥bâˆ¥) - 4 * âˆ¥u - half â€¢ (wq+wp)âˆ¥ * âˆ¥u - half â€¢ (wq+wp)âˆ¥ :
        by { rw â† this, simp }
      ... â‰¤ 2 * (âˆ¥aâˆ¥ * âˆ¥aâˆ¥ + âˆ¥bâˆ¥ * âˆ¥bâˆ¥) - 4 * Î´ * Î´ : sub_le_sub_left eqâ‚ _
      ... â‰¤ 2 * ((Î´ + div) * (Î´ + div) + (Î´ + div) * (Î´ + div)) - 4 * Î´ * Î´ :
        sub_le_sub_right (mul_le_mul_of_nonneg_left (add_le_add eqâ‚‚ eqâ‚‚') (by norm_num)) _
      ... = 8 * Î´ * div + 4 * div * div : by ring,
    exact add_nonneg (mul_nonneg (mul_nonneg (by norm_num) zero_le_Î´) (le_of_lt nat.one_div_pos_of_nat))
      (mul_nonneg (mul_nonneg (by norm_num) (le_of_lt nat.one_div_pos_of_nat)) (le_of_lt nat.one_div_pos_of_nat)),
    -- third goal : `tendsto (Î» (n : â„•), sqrt (b n)) at_top (ğ“ 0)`
    apply tendsto.comp,
    { convert continuous_sqrt.continuous_at, exact sqrt_zero.symm },
    have eqâ‚ : tendsto (Î» (n : â„•), 8 * Î´ * (1 / (n + 1))) at_top (nhds (0:â„)),
    { convert (@tendsto_const_nhds _ _ _ (8 * Î´) _).mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    have : tendsto (Î» (n : â„•), (4:â„) * (1 / (n + 1))) at_top (nhds (0:â„)),
    { convert (@tendsto_const_nhds _ _ _ (4:â„) _).mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    have eqâ‚‚ : tendsto (Î» (n : â„•), (4:â„) * (1 / (n + 1)) * (1 / (n + 1))) at_top (nhds (0:â„)),
    { convert this.mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    convert eqâ‚.add eqâ‚‚, simp only [add_zero] },
  -- Step 3: By completeness of `K`, let `w : â„• â†’ K` converge to some `v : K`.
  -- Prove that it satisfies all requirements.
  rcases cauchy_seq_tendsto_of_is_complete hâ‚ (Î» n, _) seq_is_cauchy with âŸ¨v, hv, w_tendstoâŸ©,
  use v, use hv,
  have h_cont : continuous (Î» v, âˆ¥u - vâˆ¥) :=
    continuous.comp continuous_norm (continuous.sub continuous_const continuous_id),
  have : tendsto (Î» n, âˆ¥u - w nâˆ¥) at_top (nhds âˆ¥u - vâˆ¥),
    convert (tendsto.comp h_cont.continuous_at w_tendsto),
  exact tendsto_nhds_unique this norm_tendsto,
  exact subtype.mem _
end

/-- Characterization of minimizers for the projection on a convex set in a real inner product
space. -/
theorem norm_eq_infi_iff_real_inner_le_zero {K : set F} (h : convex K) {u : F} {v : F}
  (hv : v âˆˆ K) : âˆ¥u - vâˆ¥ = (â¨… w : K, âˆ¥u - wâˆ¥) â†” âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0 :=
iff.intro
begin
  assume eq w hw,
  let Î´ := â¨… w : K, âˆ¥u - wâˆ¥, let p := âŸªu - v, w - vâŸ«_â„, let q := âˆ¥w - vâˆ¥^2,
  letI : nonempty K := âŸ¨âŸ¨v, hvâŸ©âŸ©,
  have zero_le_Î´ : 0 â‰¤ Î´,
    apply le_cinfi, intro, exact norm_nonneg _,
  have Î´_le : âˆ€ w : K, Î´ â‰¤ âˆ¥u - wâˆ¥,
    assume w, apply cinfi_le, use (0:â„), rintros _ âŸ¨_, rflâŸ©, exact norm_nonneg _,
  have Î´_le' : âˆ€ w âˆˆ K, Î´ â‰¤ âˆ¥u - wâˆ¥ := assume w hw, Î´_le âŸ¨w, hwâŸ©,
  have : âˆ€Î¸:â„, 0 < Î¸ â†’ Î¸ â‰¤ 1 â†’ 2 * p â‰¤ Î¸ * q,
    assume Î¸ hÎ¸â‚ hÎ¸â‚‚,
    have : âˆ¥u - vâˆ¥^2 â‰¤ âˆ¥u - vâˆ¥^2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸*Î¸*âˆ¥w - vâˆ¥^2 :=
    calc
      âˆ¥u - vâˆ¥^2 â‰¤ âˆ¥u - (Î¸â€¢w + (1-Î¸)â€¢v)âˆ¥^2 :
      begin
        simp only [pow_two], apply mul_self_le_mul_self (norm_nonneg _),
        rw [eq], apply Î´_le',
        apply h hw hv,
        exacts [le_of_lt hÎ¸â‚, sub_nonneg.2 hÎ¸â‚‚, add_sub_cancel'_right _ _],
      end
      ... = âˆ¥(u - v) - Î¸ â€¢ (w - v)âˆ¥^2 :
      begin
        have : u - (Î¸â€¢w + (1-Î¸)â€¢v) = (u - v) - Î¸ â€¢ (w - v),
        { rw [smul_sub, sub_smul, one_smul],
          simp only [sub_eq_add_neg, add_comm, add_left_comm, add_assoc, neg_add_rev] },
        rw this
      end
      ... = âˆ¥u - vâˆ¥^2 - 2 * Î¸ * inner (u - v) (w - v) + Î¸*Î¸*âˆ¥w - vâˆ¥^2 :
      begin
        rw [norm_sub_pow_two, inner_smul_right, norm_smul],
        simp only [pow_two],
        show âˆ¥u-vâˆ¥*âˆ¥u-vâˆ¥-2*(Î¸*inner(u-v)(w-v))+absR (Î¸)*âˆ¥w-vâˆ¥*(absR (Î¸)*âˆ¥w-vâˆ¥)=
                âˆ¥u-vâˆ¥*âˆ¥u-vâˆ¥-2*Î¸*inner(u-v)(w-v)+Î¸*Î¸*(âˆ¥w-vâˆ¥*âˆ¥w-vâˆ¥),
        rw abs_of_pos hÎ¸â‚, ring
      end,
    have eqâ‚ : âˆ¥u-vâˆ¥^2-2*Î¸*inner(u-v)(w-v)+Î¸*Î¸*âˆ¥w-vâˆ¥^2=âˆ¥u-vâˆ¥^2+(Î¸*Î¸*âˆ¥w-vâˆ¥^2-2*Î¸*inner(u-v)(w-v)), abel,
    rw [eqâ‚, le_add_iff_nonneg_right] at this,
    have eqâ‚‚ : Î¸*Î¸*âˆ¥w-vâˆ¥^2-2*Î¸*inner(u-v)(w-v)=Î¸*(Î¸*âˆ¥w-vâˆ¥^2-2*inner(u-v)(w-v)), ring,
    rw eqâ‚‚ at this,
    have := le_of_sub_nonneg (nonneg_of_mul_nonneg_left this hÎ¸â‚),
    exact this,
  by_cases hq : q = 0,
  { rw hq at this,
    have : p â‰¤ 0,
      have := this (1:â„) (by norm_num) (by norm_num),
      linarith,
    exact this },
  { have q_pos : 0 < q,
      apply lt_of_le_of_ne, exact pow_two_nonneg _, intro h, exact hq h.symm,
    by_contradiction hp, rw not_le at hp,
    let Î¸ := min (1:â„) (p / q),
    have eqâ‚ : Î¸*q â‰¤ p := calc
      Î¸*q â‰¤ (p/q) * q : mul_le_mul_of_nonneg_right (min_le_right _ _) (pow_two_nonneg _)
      ... = p : div_mul_cancel _ hq,
    have : 2 * p â‰¤ p := calc
      2 * p â‰¤ Î¸*q : by { refine this Î¸ (lt_min (by norm_num) (div_pos hp q_pos)) (by norm_num) }
      ... â‰¤ p : eqâ‚,
    linarith }
end
begin
  assume h,
  letI : nonempty K := âŸ¨âŸ¨v, hvâŸ©âŸ©,
  apply le_antisymm,
  { apply le_cinfi, assume w,
    apply nonneg_le_nonneg_of_squares_le (norm_nonneg _),
    have := h w w.2,
    exact calc
      âˆ¥u - vâˆ¥ * âˆ¥u - vâˆ¥ â‰¤ âˆ¥u - vâˆ¥ * âˆ¥u - vâˆ¥ - 2 * inner (u - v) ((w:F) - v) : by linarith
      ... â‰¤ âˆ¥u - vâˆ¥^2 - 2 * inner (u - v) ((w:F) - v) + âˆ¥(w:F) - vâˆ¥^2 :
        by { rw pow_two, refine le_add_of_nonneg_right _, exact pow_two_nonneg _ }
      ... = âˆ¥(u - v) - (w - v)âˆ¥^2 : norm_sub_pow_two.symm
      ... = âˆ¥u - wâˆ¥ * âˆ¥u - wâˆ¥ :
        by { have : (u - v) - (w - v) = u - w, abel, rw [this, pow_two] } },
  { show (â¨… (w : K), âˆ¥u - wâˆ¥) â‰¤ (Î»w:K, âˆ¥u - wâˆ¥) âŸ¨v, hvâŸ©,
      apply cinfi_le, use 0, rintros y âŸ¨z, rflâŸ©, exact norm_nonneg _ }
end

/--
Existence of projections on complete subspaces.
Let `u` be a point in an inner product space, and let `K` be a nonempty complete subspace.
Then there exists a (unique) `v` in `K` that minimizes the distance `âˆ¥u - vâˆ¥` to `u`.
This point `v` is usually called the orthogonal projection of `u` onto `K`.
-/
theorem exists_norm_eq_infi_of_complete_subspace (K : subspace ğ•œ E)
  (h : is_complete (â†‘K : set E)) : âˆ€ u : E, âˆƒ v âˆˆ K, âˆ¥u - vâˆ¥ = â¨… w : (K : set E), âˆ¥u - wâˆ¥ :=
begin
  letI : inner_product_space â„ E := inner_product_space.is_R_or_C_to_real ğ•œ,
  let K' : subspace â„ E := K.restrict_scalars â„,
  exact exists_norm_eq_infi_of_complete_convex âŸ¨0, K'.zero_memâŸ© h K'.convex
end

/--
Characterization of minimizers in the projection on a subspace, in the real case.
Let `u` be a point in a real inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `âˆ¥u - vâˆ¥` over points in `K` if and only if
for all `w âˆˆ K`, `âŸªu - v, wâŸ« = 0` (i.e., `u - v` is orthogonal to the subspace `K`).
This is superceded by `norm_eq_infi_iff_inner_eq_zero` that gives the same conclusion over
any `is_R_or_C` field.
-/
theorem norm_eq_infi_iff_real_inner_eq_zero (K : subspace â„ F) {u : F} {v : F}
  (hv : v âˆˆ K) : âˆ¥u - vâˆ¥ = (â¨… w : (â†‘K : set F), âˆ¥u - wâˆ¥) â†” âˆ€ w âˆˆ K, âŸªu - v, wâŸ«_â„ = 0 :=
iff.intro
begin
  assume h,
  have h : âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0,
  { rwa [norm_eq_infi_iff_real_inner_le_zero] at h, exacts [K.convex, hv] },
  assume w hw,
  have le : âŸªu - v, wâŸ«_â„ â‰¤ 0,
    let w' := w + v,
    have : w' âˆˆ K := submodule.add_mem _ hw hv,
    have hâ‚ := h w' this,
    have hâ‚‚ : w' - v = w, simp only [add_neg_cancel_right, sub_eq_add_neg],
    rw hâ‚‚ at hâ‚, exact hâ‚,
  have ge : âŸªu - v, wâŸ«_â„ â‰¥ 0,
    let w'' := -w + v,
    have : w'' âˆˆ K := submodule.add_mem _ (submodule.neg_mem _ hw) hv,
    have hâ‚ := h w'' this,
    have hâ‚‚ : w'' - v = -w, simp only [neg_inj, add_neg_cancel_right, sub_eq_add_neg],
    rw [hâ‚‚, inner_neg_right] at hâ‚,
    linarith,
    exact le_antisymm le ge
end
begin
  assume h,
  have : âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0,
    assume w hw,
    let w' := w - v,
    have : w' âˆˆ K := submodule.sub_mem _ hw hv,
    have hâ‚ := h w' this,
    exact le_of_eq hâ‚,
  rwa norm_eq_infi_iff_real_inner_le_zero,
  exacts [submodule.convex _, hv]
end

/--
Characterization of minimizers in the projection on a subspace.
Let `u` be a point in an inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `âˆ¥u - vâˆ¥` over points in `K` if and only if
for all `w âˆˆ K`, `âŸªu - v, wâŸ« = 0` (i.e., `u - v` is orthogonal to the subspace `K`)
-/
theorem norm_eq_infi_iff_inner_eq_zero (K : subspace ğ•œ E) {u : E} {v : E}
  (hv : v âˆˆ K) : âˆ¥u - vâˆ¥ = (â¨… w : (â†‘K : set E), âˆ¥u - wâˆ¥) â†” âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0 :=
begin
  letI : inner_product_space â„ E := inner_product_space.is_R_or_C_to_real ğ•œ,
  let K' : subspace â„ E := K.restrict_scalars â„,
  split,
  { assume H,
    have A : âˆ€ w âˆˆ K, re âŸªu - v, wâŸ« = 0 := (norm_eq_infi_iff_real_inner_eq_zero K' hv).1 H,
    assume w hw,
    apply ext,
    { simp [A w hw] },
    { symmetry, calc
      im (0 : ğ•œ) = 0 : im.map_zero
      ... = re âŸªu - v, (-I) â€¢ wâŸ« : (A _ (K.smul_mem (-I) hw)).symm
      ... = re ((-I) * âŸªu - v, wâŸ«) : by rw inner_smul_right
      ... = im âŸªu - v, wâŸ« : by simp } },
  { assume H,
    have : âˆ€ w âˆˆ K', âŸªu - v, wâŸ«_â„ = 0,
    { assume w hw,
      rw [real_inner_eq_re_inner, H w hw],
      exact zero_re' },
    exact (norm_eq_infi_iff_real_inner_eq_zero K' hv).2 this }
end

/-- The orthogonal projection onto a complete subspace, as an
unbundled function.  This definition is only intended for use in
setting up the bundled version `orthogonal_projection` and should not
be used once that is defined. -/
def orthogonal_projection_fn {K : subspace ğ•œ E} (h : is_complete (K : set E)) (v : E) :=
(exists_norm_eq_infi_of_complete_subspace K h v).some

/-- The unbundled orthogonal projection is in the given subspace.
This lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
lemma orthogonal_projection_fn_mem {K : submodule ğ•œ E} (h : is_complete (K : set E)) (v : E) :
  orthogonal_projection_fn h v âˆˆ K :=
(exists_norm_eq_infi_of_complete_subspace K h v).some_spec.some

/-- The characterization of the unbundled orthogonal projection.  This
lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
lemma orthogonal_projection_fn_inner_eq_zero {K : submodule ğ•œ E} (h : is_complete (K : set E))
  (v : E) : âˆ€ w âˆˆ K, âŸªv - orthogonal_projection_fn h v, wâŸ« = 0 :=
begin
  rw â†norm_eq_infi_iff_inner_eq_zero K (orthogonal_projection_fn_mem h v),
  exact (exists_norm_eq_infi_of_complete_subspace K h v).some_spec.some_spec
end

/-- The unbundled orthogonal projection is the unique point in `K`
with the orthogonality property.  This lemma is only intended for use
in setting up the bundled version and should not be used once that is
defined. -/
lemma eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero {K : submodule ğ•œ E}
  (h : is_complete (K : set E)) {u v : E} (hvm : v âˆˆ K) (hvo : âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0) :
  v = orthogonal_projection_fn h u :=
begin
  rw [â†sub_eq_zero, â†inner_self_eq_zero],
  have hvs : v - orthogonal_projection_fn h u âˆˆ K :=
    submodule.sub_mem K hvm (orthogonal_projection_fn_mem h u),
  have huo : âŸªu - orthogonal_projection_fn h u, v - orthogonal_projection_fn h uâŸ« = 0 :=
    orthogonal_projection_fn_inner_eq_zero h u _ hvs,
  have huv : âŸªu - v, v - orthogonal_projection_fn h uâŸ« = 0 := hvo _ hvs,
  have houv : âŸª(u - orthogonal_projection_fn h u) - (u - v), v - orthogonal_projection_fn h uâŸ« = 0,
  { rw [inner_sub_left, huo, huv, sub_zero] },
  rwa sub_sub_sub_cancel_left at houv
end

/-- The orthogonal projection onto a complete subspace.  For most
purposes, `orthogonal_projection`, which removes the `is_complete`
hypothesis and is the identity map when the subspace is not complete,
should be used instead. -/
def orthogonal_projection_of_complete {K : submodule ğ•œ E} (h : is_complete (K : set E)) :
  linear_map ğ•œ E E :=
{ to_fun := orthogonal_projection_fn h,
  map_add' := Î» x y, begin
    have hm : orthogonal_projection_fn h x + orthogonal_projection_fn h y âˆˆ K :=
      submodule.add_mem K (orthogonal_projection_fn_mem h x) (orthogonal_projection_fn_mem h y),
    have ho :
      âˆ€ w âˆˆ K, âŸªx + y - (orthogonal_projection_fn h x + orthogonal_projection_fn h y), wâŸ« = 0,
    { intros w hw,
      rw [add_sub_comm, inner_add_left, orthogonal_projection_fn_inner_eq_zero h _ w hw,
          orthogonal_projection_fn_inner_eq_zero h _ w hw, add_zero] },
    rw eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero h hm ho
  end,
  map_smul' := Î» c x, begin
    have hm : c â€¢ orthogonal_projection_fn h x âˆˆ K :=
      submodule.smul_mem K _ (orthogonal_projection_fn_mem h x),
    have ho : âˆ€ w âˆˆ K, âŸªc â€¢ x - c â€¢ orthogonal_projection_fn h x, wâŸ« = 0,
    { intros w hw,
      rw [â†smul_sub, inner_smul_left, orthogonal_projection_fn_inner_eq_zero h _ w hw, mul_zero] },
    rw eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero h hm ho
  end }

/-- The orthogonal projection onto a subspace, which is expected to be
complete.  If the subspace is not complete, this uses the identity map
instead. -/
def orthogonal_projection (K : submodule ğ•œ E) : linear_map ğ•œ E E :=
if h : is_complete (K : set E) then orthogonal_projection_of_complete h else linear_map.id

/-- The definition of `orthogonal_projection` using `if`. -/
lemma orthogonal_projection_def (K : submodule ğ•œ E) :
  orthogonal_projection K =
    if h : is_complete (K : set E) then orthogonal_projection_of_complete h else linear_map.id :=
rfl

@[simp]
lemma orthogonal_projection_fn_eq {K : submodule ğ•œ E} (h : is_complete (K : set E)) (v : E) :
  orthogonal_projection_fn h v = orthogonal_projection K v :=
by { rw [orthogonal_projection_def, dif_pos h], refl }

/-- The orthogonal projection is in the given subspace. -/
lemma orthogonal_projection_mem {K : submodule ğ•œ E} (h : is_complete (K : set E)) (v : E) :
  orthogonal_projection K v âˆˆ K :=
begin
  rw â†orthogonal_projection_fn_eq h,
  exact orthogonal_projection_fn_mem h v
end

/-- The characterization of the orthogonal projection.  -/
@[simp]
lemma orthogonal_projection_inner_eq_zero (K : submodule ğ•œ E) (v : E) :
  âˆ€ w âˆˆ K, âŸªv - orthogonal_projection K v, wâŸ« = 0 :=
begin
  simp_rw orthogonal_projection_def,
  split_ifs,
  { exact orthogonal_projection_fn_inner_eq_zero h v },
  { simp },
end

/-- The orthogonal projection is the unique point in `K` with the
orthogonality property. -/
lemma eq_orthogonal_projection_of_mem_of_inner_eq_zero {K : submodule ğ•œ E}
  (h : is_complete (K : set E)) {u v : E} (hvm : v âˆˆ K) (hvo : âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0) :
  v = orthogonal_projection K u :=
begin
  rw â†orthogonal_projection_fn_eq h,
  exact eq_orthogonal_projection_fn_of_mem_of_inner_eq_zero h hvm hvo
end

/-- The subspace of vectors orthogonal to a given subspace. -/
def submodule.orthogonal (K : submodule ğ•œ E) : submodule ğ•œ E :=
{ carrier := {v | âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0},
  zero_mem' := Î» _ _, inner_zero_right,
  add_mem' := Î» x y hx hy u hu, by rw [inner_add_right, hx u hu, hy u hu, add_zero],
  smul_mem' := Î» c x hx u hu, by rw [inner_smul_right, hx u hu, mul_zero] }

/-- When a vector is in `K.orthogonal`. -/
lemma submodule.mem_orthogonal (K : submodule ğ•œ E) (v : E) :
  v âˆˆ K.orthogonal â†” âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0 :=
iff.rfl

/-- When a vector is in `K.orthogonal`, with the inner product the
other way round. -/
lemma submodule.mem_orthogonal' (K : submodule ğ•œ E) (v : E) :
  v âˆˆ K.orthogonal â†” âˆ€ u âˆˆ K, âŸªv, uâŸ« = 0 :=
by simp_rw [submodule.mem_orthogonal, inner_eq_zero_sym]

/-- A vector in `K` is orthogonal to one in `K.orthogonal`. -/
lemma submodule.inner_right_of_mem_orthogonal {u v : E} {K : submodule ğ•œ E} (hu : u âˆˆ K)
    (hv : v âˆˆ K.orthogonal) : âŸªu, vâŸ« = 0 :=
(K.mem_orthogonal v).1 hv u hu

/-- A vector in `K.orthogonal` is orthogonal to one in `K`. -/
lemma submodule.inner_left_of_mem_orthogonal {u v : E} {K : submodule ğ•œ E} (hu : u âˆˆ K)
    (hv : v âˆˆ K.orthogonal) : âŸªv, uâŸ« = 0 :=
by rw [inner_eq_zero_sym]; exact submodule.inner_right_of_mem_orthogonal hu hv

/-- `K` and `K.orthogonal` have trivial intersection. -/
lemma submodule.orthogonal_disjoint (K : submodule ğ•œ E) : disjoint K K.orthogonal :=
begin
  simp_rw [submodule.disjoint_def, submodule.mem_orthogonal],
  exact Î» x hx ho, inner_self_eq_zero.1 (ho x hx)
end

variables (ğ•œ E)

/-- `submodule.orthogonal` gives a `galois_connection` between
`submodule ğ•œ E` and its `order_dual`. -/
lemma submodule.orthogonal_gc :
  @galois_connection (submodule ğ•œ E) (order_dual $ submodule ğ•œ E) _ _
    submodule.orthogonal submodule.orthogonal :=
Î» Kâ‚ Kâ‚‚, âŸ¨Î» h v hv u hu, submodule.inner_left_of_mem_orthogonal hv (h hu),
          Î» h v hv u hu, submodule.inner_left_of_mem_orthogonal hv (h hu)âŸ©

variables {ğ•œ E}

/-- `submodule.orthogonal` reverses the `â‰¤` ordering of two
subspaces. -/
lemma submodule.orthogonal_le {Kâ‚ Kâ‚‚ : submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚) :
  Kâ‚‚.orthogonal â‰¤ Kâ‚.orthogonal :=
(submodule.orthogonal_gc ğ•œ E).monotone_l h


/-- `K` is contained in `K.orthogonal.orthogonal`. -/
lemma submodule.le_orthogonal_orthogonal (K : submodule ğ•œ E) : K â‰¤ K.orthogonal.orthogonal :=
(submodule.orthogonal_gc ğ•œ E).le_u_l _

/-- The inf of two orthogonal subspaces equals the subspace orthogonal
to the sup. -/
lemma submodule.inf_orthogonal (Kâ‚ Kâ‚‚ : submodule ğ•œ E) :
  Kâ‚.orthogonal âŠ“ Kâ‚‚.orthogonal = (Kâ‚ âŠ” Kâ‚‚).orthogonal :=
(submodule.orthogonal_gc ğ•œ E).l_sup.symm

/-- The inf of an indexed family of orthogonal subspaces equals the
subspace orthogonal to the sup. -/
lemma submodule.infi_orthogonal {Î¹ : Type*} (K : Î¹ â†’ submodule ğ•œ E) :
  (â¨… i, (K i).orthogonal) = (supr K).orthogonal :=
(submodule.orthogonal_gc ğ•œ E).l_supr.symm

/-- The inf of a set of orthogonal subspaces equals the subspace
orthogonal to the sup. -/
lemma submodule.Inf_orthogonal (s : set $ submodule ğ•œ E) :
  (â¨… K âˆˆ s, submodule.orthogonal K) = (Sup s).orthogonal :=
(submodule.orthogonal_gc ğ•œ E).l_Sup.symm

/-- If `Kâ‚` is complete and contained in `Kâ‚‚`, `Kâ‚` and `Kâ‚.orthogonal âŠ“ Kâ‚‚` span `Kâ‚‚`. -/
lemma submodule.sup_orthogonal_inf_of_is_complete {Kâ‚ Kâ‚‚ : submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚)
  (hc : is_complete (Kâ‚ : set E)) : Kâ‚ âŠ” (Kâ‚.orthogonal âŠ“ Kâ‚‚) = Kâ‚‚ :=
begin
  ext x,
  rw submodule.mem_sup,
  rcases exists_norm_eq_infi_of_complete_subspace Kâ‚ hc x with âŸ¨v, hv, hvmâŸ©,
  rw norm_eq_infi_iff_inner_eq_zero Kâ‚ hv at hvm,
  split,
  { rintro âŸ¨y, hy, z, hz, rflâŸ©,
    exact Kâ‚‚.add_mem (h hy) hz.2 },
  { exact Î» hx, âŸ¨v, hv, x - v, âŸ¨(Kâ‚.mem_orthogonal' _).2 hvm, Kâ‚‚.sub_mem hx (h hv)âŸ©,
                 add_sub_cancel'_right _ _âŸ© }
end

/-- If `K` is complete, `K` and `K.orthogonal` span the whole
space. -/
lemma submodule.sup_orthogonal_of_is_complete {K : submodule ğ•œ E} (h : is_complete (K : set E)) :
  K âŠ” K.orthogonal = âŠ¤ :=
begin
  convert submodule.sup_orthogonal_inf_of_is_complete (le_top : K â‰¤ âŠ¤) h,
  simp
end

/-- If `K` is complete, `K` and `K.orthogonal` are complements of each
other. -/
lemma submodule.is_compl_orthogonal_of_is_complete_real {K : submodule ğ•œ E}
    (h : is_complete (K : set E)) : is_compl K K.orthogonal :=
âŸ¨K.orthogonal_disjoint, le_of_eq (submodule.sup_orthogonal_of_is_complete h).symmâŸ©

open finite_dimensional

/-- Given a finite-dimensional subspace `Kâ‚‚`, and a subspace `Kâ‚`
containined in it, the dimensions of `Kâ‚` and the intersection of its
orthogonal subspace with `Kâ‚‚` add to that of `Kâ‚‚`. -/
lemma submodule.findim_add_inf_findim_orthogonal {Kâ‚ Kâ‚‚ : submodule ğ•œ E}
  [finite_dimensional ğ•œ Kâ‚‚] (h : Kâ‚ â‰¤ Kâ‚‚) :
  findim ğ•œ Kâ‚ + findim ğ•œ (Kâ‚.orthogonal âŠ“ Kâ‚‚ : submodule ğ•œ E) = findim ğ•œ Kâ‚‚ :=
begin
  haveI := submodule.finite_dimensional_of_le h,
  have hd := submodule.dim_sup_add_dim_inf_eq Kâ‚ (Kâ‚.orthogonal âŠ“ Kâ‚‚),
  rw [â†inf_assoc, (submodule.orthogonal_disjoint Kâ‚).eq_bot, bot_inf_eq, findim_bot,
      submodule.sup_orthogonal_inf_of_is_complete h
        (submodule.complete_of_finite_dimensional _)] at hd,
  rw add_zero at hd,
  exact hd.symm
end
end orthogonal
