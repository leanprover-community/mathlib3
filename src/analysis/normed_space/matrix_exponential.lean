/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/

import analysis.normed_space.exponential
import analysis.matrix
import topology.uniform_space.matrix

/-!
# Lemmas about the matrix exponential

In this file, we provide results about `exp` on `matrix`s over a topological or normed algebra.
The topological results are:

* `matrix.exp_transpose`
* `matrix.exp_diagonal`

Lemmas `exp_add_of_commute` require a canonical norm on the type, but for matrices there are
multiple sensible choices of norm, none of which are canonical. In an application where you choose
a particular norm using `local attribute [instance]`, then the usual lemmas about `exp` are fine.

In this file, we copy across the lemmas about a `exp` and instantiate a non-canonical norm in the
proof.

* `matrix.exp_add_of_commute`
* `matrix.exp_nsmul`

After this, we prove some additional results about matrix operations:

* `matrix.exp_block_diagonal`
* `matrix.exp_block_diagonal'`

-/
open_locale matrix

section hacks_for_pi_instance_search

/-- A special case of `pi.topological_ring` for when `R` is not dependently typed. -/
instance function.topological_ring (I : Type*) (R : Type*)
  [non_unital_ring R] [topological_space R] [topological_ring R] :
  topological_ring (I â†’ R) :=
pi.topological_ring

/-- A special case of `pi.has_continuous_const_smul` for when `M` is not dependently typed. -/
instance function.has_continuous_const_smul (I : Type*) (R : Type*) (M : Type*) [has_scalar R M]
  [topological_space M] [has_continuous_const_smul R M] :
  has_continuous_const_smul R (I â†’ M) :=
pi.has_continuous_const_smul

/-- A special case of `function.algebra` for when A is a ring not a semiring -/
instance function.algebra_ring (I : Type*) {R : Type*} (A : Type*) [comm_semiring R]
  [ring A] [algebra R A] : algebra R (I â†’ A) :=
pi.algebra _ _

/-- A special case of `pi.algebra` for when `f = Î» i, matrix (m i) (m i) A`. -/
instance pi.matrix_algebra (I R A : Type*) (m : I â†’ Type*)
  [comm_semiring R] [semiring A] [algebra R A]
  [Î  i, fintype (m i)] [Î  i, decidable_eq (m i)] :
  algebra R (Î  i, matrix (m i) (m i) A) :=
@pi.algebra I R (Î» i, matrix (m i) (m i) A) _ _ (Î» i, matrix.algebra)

/-- A special case of `pi.topological_ring` for when `f = Î» i, matrix (m i) (m i) A`. -/
instance pi.matrix_topological_ring (I A : Type*) (m : I â†’ Type*)
  [ring A] [topological_space A] [topological_ring A]
  [Î  i, fintype (m i)] :
  topological_ring (Î  i, matrix (m i) (m i) A) :=
@pi.topological_ring _ (Î» i, matrix (m i) (m i) A) _ _ (Î» i, matrix.topological_ring)

instance pi.matrix_has_continuous_const_smul (I R A : Type*) (m : I â†’ Type*)
  [topological_space A] [has_scalar R A] [has_continuous_const_smul R A] :
  has_continuous_const_smul R (Î  i, matrix (m i) (m i) A) :=
@pi.has_continuous_const_smul _ _ (Î» i, matrix (m i) (m i) A) _ _
  (Î» i, matrix.has_continuous_const_smul)

end hacks_for_pi_instance_search

variables (ğ•‚ : Type*) {m n : Type*} {n' : m â†’ Type*} {ğ”¸ : Type*}

namespace matrix

section topological

section ring
variables [fintype m] [decidable_eq m] [field ğ•‚]
  [ring ğ”¸] [topological_space ğ”¸] [topological_ring ğ”¸] [algebra ğ•‚ ğ”¸]
  [has_continuous_const_smul ğ•‚ ğ”¸] [t2_space ğ”¸]

lemma exp_diagonal (v : m â†’ ğ”¸) : exp ğ•‚ _ (diagonal v) = diagonal (exp ğ•‚ (m â†’ ğ”¸) v) :=
by simp_rw [exp_eq_tsum, diagonal_pow, â†diagonal_smul, â†diagonal_tsum]

/-! TODO: add a `has_continuous_star` typeclass so we can write
```lean
lemma exp_conj_transpose [star_ring ğ”¸] [has_continuous_star ğ”¸] (A : matrix m m ğ”¸) :
  exp ğ•‚ (matrix m m ğ”¸) Aá´´ = (exp ğ•‚ _ A)á´´ :=
by simp_rw [exp_eq_tsum, conj_transpose_tsum, conj_transpose_smul, conj_transpose_pow]
```
-/

end ring

section comm_ring
variables [fintype m] [decidable_eq m] [field ğ•‚]
  [comm_ring ğ”¸] [topological_space ğ”¸] [topological_ring ğ”¸] [algebra ğ•‚ ğ”¸]
  [has_continuous_const_smul ğ•‚ ğ”¸] [t2_space ğ”¸]

-- where's transpose_pow!?
lemma exp_transpose (A : matrix m m ğ”¸) : exp ğ•‚ (matrix m m ğ”¸) Aáµ€ = (exp ğ•‚ _ A)áµ€ :=
by simp_rw [exp_eq_tsum, transpose_tsum, transpose_smul, transpose_pow]

end comm_ring

end topological

section normed

variables [is_R_or_C ğ•‚]
  [fintype m] [decidable_eq m]
  [fintype n] [decidable_eq n]
  [Î  i, fintype (n' i)] [Î  i, decidable_eq (n' i)]
  [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸] [complete_space ğ”¸]

lemma exp_add_of_commute (A B : matrix m m ğ”¸) (h : commute A B) :
  exp ğ•‚ _ (A + B) = exp ğ•‚ _ A * exp ğ•‚ _ B :=
begin
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact exp_add_of_commute h,
end

lemma exp_nsmul (n : â„•) (A : matrix m m ğ”¸) :
  exp ğ•‚ _ (n â€¢ A) = exp ğ•‚ _ A ^ n :=
begin
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact exp_nsmul n A,
end

lemma exp_block_diagonal (v : m â†’ matrix n n ğ”¸) :
  exp ğ•‚ _ (block_diagonal v) = block_diagonal (exp ğ•‚ (m â†’ matrix n n ğ”¸) v) :=
begin
  -- pick the norm on the spaces of matrices
  letI : semi_normed_ring (matrix n n ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix n n ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix n n ğ”¸) := matrix.linfty_op_normed_algebra,
  letI : semi_normed_ring (matrix (n Ã— m) (n Ã— m) ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix (n Ã— m) (n Ã— m) ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix (n Ã— m) (n Ã— m) ğ”¸) := matrix.linfty_op_normed_algebra,
  -- help out lean which is bad at typeclass resolution on pi types
  letI : complete_space (m â†’ matrix n n ğ”¸) := by apply_instance,
  refine (map_exp ğ•‚ (block_diagonal_ring_hom n m ğ”¸) _ v).symm,
  exact continuous.matrix_block_diagonal continuous_id,
end

lemma exp_block_diagonal' (v : Î  i, matrix (n' i) (n' i) ğ”¸) :
  exp ğ•‚ _ (block_diagonal' v) = block_diagonal' (exp ğ•‚ (Î  i, matrix (n' i) (n' i) ğ”¸) v) :=
begin
  -- pick the norm on the spaces of matrices
  letI : Î  i : m, semi_normed_ring (matrix (n' i) (n' i) ğ”¸) :=
    Î» i, matrix.linfty_op_semi_normed_ring,
  letI : Î  i : m, normed_ring (matrix (n' i) (n' i) ğ”¸) := Î» i, matrix.linfty_op_normed_ring,
  letI : Î  i : m, normed_algebra ğ•‚ (matrix (n' i) (n' i) ğ”¸) :=
    Î» i, matrix.linfty_op_normed_algebra,
  letI : semi_normed_ring (matrix (Î£ i, n' i) (Î£ i, n' i) ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix (Î£ i, n' i) (Î£ i, n' i) ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix (Î£ i, n' i) (Î£ i, n' i) ğ”¸) := matrix.linfty_op_normed_algebra,
  -- help out lean which is bad at typeclass resolution on pi types
  letI : normed_algebra ğ•‚ (Î  (i : m), matrix (n' i) (n' i) ğ”¸) := by apply_instance,
  letI : complete_space (Î  i, matrix (n' i) (n' i) ğ”¸) := by apply_instance,
  refine (map_exp ğ•‚ (block_diagonal'_ring_hom n' ğ”¸) _ v).symm,
  exact continuous.matrix_block_diagonal' continuous_id,
end

end normed

end matrix
