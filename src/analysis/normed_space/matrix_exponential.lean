/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/

import analysis.normed_space.exponential
import analysis.matrix
import linear_algebra.matrix.zpow
import linear_algebra.matrix.hermitian
import linear_algebra.matrix.symmetric
import topology.uniform_space.matrix

/-!
# Lemmas about the matrix exponential

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file, we provide results about `exp` on `matrix`s over a topological or normed algebra.
Note that generic results over all topological spaces such as `exp_zero` can be used on matrices
without issue, so are not repeated here. The topological results specific to matrices are:

* `matrix.exp_transpose`
* `matrix.exp_conj_transpose`
* `matrix.exp_diagonal`
* `matrix.exp_block_diagonal`
* `matrix.exp_block_diagonal'`

Lemmas like `exp_add_of_commute` require a canonical norm on the type; while there are multiple
sensible choices for the norm of a `matrix` (`matrix.normed_add_comm_group`,
`matrix.frobenius_normed_add_comm_group`, `matrix.linfty_op_normed_add_comm_group`), none of them
are canonical. In an application where a particular norm is chosen using
`local attribute [instance]`, then the usual lemmas about `exp` are fine. When choosing a norm is
undesirable, the results in this file can be used.

In this file, we copy across the lemmas about `exp`, but hide the requirement for a norm inside the
proof.

* `matrix.exp_add_of_commute`
* `matrix.exp_sum_of_commute`
* `matrix.exp_nsmul`
* `matrix.is_unit_exp`
* `matrix.exp_units_conj`
* `matrix.exp_units_conj'`

Additionally, we prove some results about `matrix.has_inv` and `matrix.div_inv_monoid`, as the
results for general rings are instead stated about `ring.inverse`:

* `matrix.exp_neg`
* `matrix.exp_zsmul`
* `matrix.exp_conj`
* `matrix.exp_conj'`

## Implementation notes

This file runs into some sharp edges on typeclass search in lean 3, especially regarding pi types.
To work around this, we copy a handful of instances for when lean can't find them by itself.
Hopefully we will be able to remove these in Lean 4.

## TODO

* Show that `matrix.det (exp A) = exp (matrix.trace A)`

## References

* https://en.wikipedia.org/wiki/Matrix_exponential
-/
open_locale matrix big_operators

section hacks_for_pi_instance_search

/-- A special case of `pi.topological_ring` for when `R` is not dependently typed. -/
instance function.topological_ring (I : Type*) (R : Type*)
  [non_unital_ring R] [topological_space R] [topological_ring R] :
  topological_ring (I â†’ R) :=
pi.topological_ring

/-- A special case of `function.algebra` for when A is a `ring` not a `semiring` -/
instance function.algebra_ring (I : Type*) {R : Type*} (A : Type*) [comm_semiring R]
  [ring A] [algebra R A] : algebra R (I â†’ A) :=
pi.algebra _ _

/-- A special case of `pi.algebra` for when `f = Î» i, matrix (m i) (m i) A`. -/
instance pi.matrix_algebra (I R A : Type*) (m : I â†’ Type*)
  [comm_semiring R] [semiring A] [algebra R A]
  [Î  i, fintype (m i)] [Î  i, decidable_eq (m i)] :
  algebra R (Î  i, matrix (m i) (m i) A) :=
@pi.algebra I R (Î» i, matrix (m i) (m i) A) _ _ (Î» i, matrix.algebra)

/-- A special case of `pi.topological_ring` for when `f = Î» i, matrix (m i) (m i) A`. -/
instance pi.matrix_topological_ring (I A : Type*) (m : I â†’ Type*)
  [ring A] [topological_space A] [topological_ring A]
  [Î  i, fintype (m i)] :
  topological_ring (Î  i, matrix (m i) (m i) A) :=
@pi.topological_ring _ (Î» i, matrix (m i) (m i) A) _ _ (Î» i, matrix.topological_ring)

end hacks_for_pi_instance_search

variables (ğ•‚ : Type*) {m n p : Type*} {n' : m â†’ Type*} {ğ”¸ : Type*}

namespace matrix

section topological

section ring
variables [fintype m] [decidable_eq m] [fintype n] [decidable_eq n]
  [Î  i, fintype (n' i)] [Î  i, decidable_eq (n' i)]
  [field ğ•‚] [ring ğ”¸] [topological_space ğ”¸] [topological_ring ğ”¸] [algebra ğ•‚ ğ”¸] [algebra â„š ğ”¸]
  [t2_space ğ”¸]

lemma exp_diagonal (v : m â†’ ğ”¸) : exp (diagonal v) = diagonal (exp v) :=
by simp_rw [exp_eq_tsum, diagonal_pow, â†diagonal_smul, â†diagonal_tsum]

lemma exp_block_diagonal (v : m â†’ matrix n n ğ”¸) :
  exp (block_diagonal v) = block_diagonal (exp v) :=
by simp_rw [exp_eq_tsum, â†block_diagonal_pow, â†block_diagonal_smul, â†block_diagonal_tsum]

lemma exp_block_diagonal' (v : Î  i, matrix (n' i) (n' i) ğ”¸) :
  exp (block_diagonal' v) = block_diagonal' (exp v) :=
by simp_rw [exp_eq_tsum, â†block_diagonal'_pow, â†block_diagonal'_smul, â†block_diagonal'_tsum]

lemma exp_conj_transpose [star_ring ğ”¸] [has_continuous_star ğ”¸] (A : matrix m m ğ”¸) :
  exp Aá´´ = (exp A)á´´ :=
(star_exp A).symm

lemma is_hermitian.exp [star_ring ğ”¸] [has_continuous_star ğ”¸] {A : matrix m m ğ”¸}
  (h : A.is_hermitian) : (exp A).is_hermitian :=
(exp_conj_transpose _).symm.trans $ congr_arg _ h

end ring

section comm_ring
variables [fintype m] [decidable_eq m] [field ğ•‚]
  [comm_ring ğ”¸] [topological_space ğ”¸] [topological_ring ğ”¸] [algebra ğ•‚ ğ”¸] [algebra â„š ğ”¸] [t2_space ğ”¸]

lemma exp_transpose (A : matrix m m ğ”¸) : exp Aáµ€ = (exp A)áµ€ :=
by simp_rw [exp_eq_tsum, transpose_tsum, transpose_smul, transpose_pow]

lemma is_symm.exp {A : matrix m m ğ”¸} (h : A.is_symm) : (exp A).is_symm :=
(exp_transpose _).symm.trans $ congr_arg _ h

end comm_ring

end topological

section normed

variables [is_R_or_C ğ•‚]
  [fintype m] [decidable_eq m]
  [fintype n] [decidable_eq n]
  [Î  i, fintype (n' i)] [Î  i, decidable_eq (n' i)]
  [normed_ring ğ”¸] [algebra â„š ğ”¸] [normed_algebra ğ•‚ ğ”¸] [complete_space ğ”¸]

lemma exp_add_of_commute (A B : matrix m m ğ”¸) (h : commute A B) :
  exp (A + B) = exp A â¬ exp B :=
let ğ•‚ := ğ•‚ in
begin
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact exp_add_of_commute ğ•‚ h,
end

lemma exp_sum_of_commute {Î¹} (s : finset Î¹) (f : Î¹ â†’ matrix m m ğ”¸)
  (h : (s : set Î¹).pairwise $ Î» i j, commute (f i) (f j)) :
  exp (âˆ‘ i in s, f i) = s.noncomm_prod (Î» i, exp (f i))
    (Î» i hi j hj _, (h.of_refl hi hj).exp) :=
let ğ•‚ := ğ•‚ in
begin
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact exp_sum_of_commute ğ•‚ s f h,
end

lemma exp_nsmul (n : â„•) (A : matrix m m ğ”¸) :
  exp (n â€¢ A) = exp A ^ n :=
let ğ•‚ := ğ•‚ in
begin
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact exp_nsmul ğ•‚ n A,
end

lemma is_unit_exp (A : matrix m m ğ”¸) : is_unit (exp A) :=
let ğ•‚ := ğ•‚ in
begin
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact is_unit_exp ğ•‚ A,
end

lemma exp_units_conj (U : (matrix m m ğ”¸)Ë£) (A : matrix m m ğ”¸)  :
  exp (â†‘U â¬ A â¬ â†‘(Uâ»Â¹) : matrix m m ğ”¸) = â†‘U â¬ exp A â¬ â†‘(Uâ»Â¹) :=
let ğ•‚ := ğ•‚ in
begin
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact exp_units_conj ğ•‚ U A,
end

lemma exp_units_conj' (U : (matrix m m ğ”¸)Ë£) (A : matrix m m ğ”¸)  :
  exp (â†‘(Uâ»Â¹) â¬ A â¬ U : matrix m m ğ”¸) = â†‘(Uâ»Â¹) â¬ exp A â¬ U :=
exp_units_conj ğ•‚ Uâ»Â¹ A

end normed

section normed_comm

variables [is_R_or_C ğ•‚]
  [fintype m] [decidable_eq m]
  [fintype n] [decidable_eq n]
  [Î  i, fintype (n' i)] [Î  i, decidable_eq (n' i)]
  [normed_comm_ring ğ”¸] [algebra â„š ğ”¸] [normed_algebra ğ•‚ ğ”¸] [complete_space ğ”¸]

lemma exp_neg (A : matrix m m ğ”¸) : exp (-A) = (exp A)â»Â¹ :=
let ğ•‚ := ğ•‚ in
begin
  rw nonsing_inv_eq_ring_inverse,
  letI : semi_normed_ring (matrix m m ğ”¸) := matrix.linfty_op_semi_normed_ring,
  letI : normed_ring (matrix m m ğ”¸) := matrix.linfty_op_normed_ring,
  letI : normed_algebra ğ•‚ (matrix m m ğ”¸) := matrix.linfty_op_normed_algebra,
  exact (ring.inverse_exp ğ•‚ A).symm,
end

lemma exp_zsmul (z : â„¤) (A : matrix m m ğ”¸) : exp (z â€¢ A) = exp A ^ z :=
let ğ•‚ := ğ•‚ in
begin
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_coe_or_neg,
  { rw [zpow_coe_nat, coe_nat_zsmul, exp_nsmul ğ•‚]; apply_instance },
  { have : is_unit (exp A).det := (matrix.is_unit_iff_is_unit_det _).mp (is_unit_exp ğ•‚ _),
    rw [matrix.zpow_neg this, zpow_coe_nat, neg_smul, exp_neg ğ•‚, coe_nat_zsmul, exp_nsmul ğ•‚];
      apply_instance },
end

lemma exp_conj (U : matrix m m ğ”¸) (A : matrix m m ğ”¸) (hy : is_unit U) :
  exp (U â¬ A â¬ Uâ»Â¹) = U â¬ exp A â¬ Uâ»Â¹ :=
let ğ•‚ := ğ•‚ in
let âŸ¨u, huâŸ© := hy in hu â–¸ by simpa only [matrix.coe_units_inv] using exp_units_conj ğ•‚ u A

lemma exp_conj' (U : matrix m m ğ”¸) (A : matrix m m ğ”¸) (hy : is_unit U) :
  exp (Uâ»Â¹ â¬ A â¬ U) = Uâ»Â¹ â¬ exp A â¬ U :=
let ğ•‚ := ğ•‚ in
let âŸ¨u, huâŸ© := hy in hu â–¸ by simpa only [matrix.coe_units_inv] using exp_units_conj' ğ•‚ u A

end normed_comm

end matrix
