/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.normed_space.operator_norm
import topology.algebra.multilinear

/-!
# Operator norm on the space of continuous multilinear maps

When `f` is a continuous multilinear map in finitely many variables, we define its norm `âˆ¥fâˆ¥` as the
smallest number such that `âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥` for all `m`.

We show that it is indeed a norm, and prove its basic properties.

## Main results

Let `f` be a multilinear map in finitely many variables.
* `exists_bound_of_continuous` asserts that, if `f` is continuous, then there exists `C > 0`
  with `âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥` for all `m`.
* `continuous_of_bound`, conversely, asserts that this bound implies continuity.
* `mk_continuous` constructs the associated continuous multilinear map.

Let `f` be a continuous multilinear map in finitely many variables.
* `âˆ¥fâˆ¥` is its norm, i.e., the smallest number such that `âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥` for
  all `m`.
* `le_op_norm f m` asserts the fundamental inequality `âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥`.
* `norm_image_sub_le f mâ‚ mâ‚‚` gives a control of the difference `f mâ‚ - f mâ‚‚` in terms of
  `âˆ¥fâˆ¥` and `âˆ¥mâ‚ - mâ‚‚âˆ¥`.

We also register isomorphisms corresponding to currying or uncurrying variables, transforming a
continuous multilinear function `f` in `n+1` variables into a continuous linear function taking
values in continuous multilinear functions in `n` variables, and also into a continuous multilinear
function in `n` variables taking values in continuous linear functions. These operations are called
`f.curry_left` and `f.curry_right` respectively (with inverses `f.uncurry_left` and
`f.uncurry_right`). They induce continuous linear equivalences between spaces of
continuous multilinear functions in `n+1` variables and spaces of continuous linear functions into
continuous multilinear functions in `n` variables (resp. continuous multilinear functions in `n`
variables taking values in continuous linear functions), called respectively
`continuous_multilinear_curry_left_equiv` and `continuous_multilinear_curry_right_equiv`.

## Implementation notes

We mostly follow the API (and the proofs) of `operator_norm.lean`, with the additional complexity
that we should deal with multilinear maps in several variables. The currying/uncurrying
constructions are based on those in `multilinear.lean`.

From the mathematical point of view, all the results follow from the results on operator norm in
one variable, by applying them to one variable after the other through currying. However, this
is only well defined when there is an order on the variables (for instance on `fin n`) although
the final result is independent of the order. While everything could be done following this
approach, it turns out that direct proofs are easier and more efficient.
-/

noncomputable theory
open_locale classical big_operators nnreal
open finset metric

local attribute [instance, priority 1001]
add_comm_group.to_add_comm_monoid normed_group.to_add_comm_group normed_space.to_module

-- hack to speed up simp when dealing with complicated types
local attribute [-instance] unique.subsingleton pi.subsingleton

/-!
### Type variables

We use the following type variables in this file:

* `ğ•œ` : a `nondiscrete_normed_field`;
* `Î¹`, `Î¹'` : finite index types with decidable equality;
* `E`, `Eâ‚` : families of normed vector spaces over `ğ•œ` indexed by `i : Î¹`;
* `E'` : a family of normed vector spaces over `ğ•œ` indexed by `i' : Î¹'`;
* `Ei` : a family of normed vector spaces over `ğ•œ` indexed by `i : fin (nat.succ n)`;
* `G`, `G'` : normed vector spaces over `ğ•œ`.
-/

universes u v v' wE wEâ‚ wE' wEi wG wG'
variables {ğ•œ : Type u} {Î¹ : Type v} {Î¹' : Type v'} {n : â„•}
  {E : Î¹ â†’ Type wE} {Eâ‚ : Î¹ â†’ Type wEâ‚} {E' : Î¹' â†’ Type wE'} {Ei : fin n.succ â†’ Type wEi}
  {G : Type wG} {G' : Type wG'}
  [decidable_eq Î¹] [fintype Î¹] [decidable_eq Î¹'] [fintype Î¹'] [nondiscrete_normed_field ğ•œ]
  [Î  i, normed_group (E i)] [Î  i, normed_space ğ•œ (E i)]
  [Î  i, normed_group (Eâ‚ i)] [Î  i, normed_space ğ•œ (Eâ‚ i)]
  [Î  i, normed_group (E' i)] [Î  i, normed_space ğ•œ (E' i)]
  [Î  i, normed_group (Ei i)] [Î  i, normed_space ğ•œ (Ei i)]
  [normed_group G] [normed_space ğ•œ G] [normed_group G'] [normed_space ğ•œ G']

/-!
### Continuity properties of multilinear maps

We relate continuity of multilinear maps to the inequality `âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥`, in
both directions. Along the way, we prove useful bounds on the difference `âˆ¥f mâ‚ - f mâ‚‚âˆ¥`.
-/
namespace multilinear_map

variable (f : multilinear_map ğ•œ E G)

/-- If a multilinear map in finitely many variables on normed spaces satisfies the inequality
`âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥` on a shell `Îµ i / âˆ¥c iâˆ¥ < âˆ¥m iâˆ¥ < Îµ i` for some positive numbers `Îµ i`
and elements `c i : ğ•œ`, `1 < âˆ¥c iâˆ¥`, then it satisfies this inequality for all `m`. -/
lemma bound_of_shell {Îµ : Î¹ â†’ â„} {C : â„} (hÎµ : âˆ€ i, 0 < Îµ i) {c : Î¹ â†’ ğ•œ} (hc : âˆ€ i, 1 < âˆ¥c iâˆ¥)
  (hf : âˆ€ m : Î  i, E i, (âˆ€ i, Îµ i / âˆ¥c iâˆ¥ â‰¤ âˆ¥m iâˆ¥) â†’ (âˆ€ i, âˆ¥m iâˆ¥ < Îµ i) â†’ âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥)
  (m : Î  i, E i) : âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥ :=
begin
  rcases em (âˆƒ i, m i = 0) with âŸ¨i, hiâŸ©|hm; [skip, push_neg at hm],
  { simp [f.map_coord_zero i hi, prod_eq_zero (mem_univ i), hi] },
  choose Î´ hÎ´0 hÎ´m_lt hle_Î´m hÎ´inv using Î» i, rescale_to_shell (hc i) (hÎµ i) (hm i),
  have hÎ´0 : 0 < âˆ i, âˆ¥Î´ iâˆ¥, from prod_pos (Î» i _, norm_pos_iff.2 (hÎ´0 i)),
  simpa [map_smul_univ, norm_smul, prod_mul_distrib, mul_left_comm C, mul_le_mul_left hÎ´0]
    using hf (Î» i, Î´ i â€¢ m i) hle_Î´m hÎ´m_lt,
end

/-- If a multilinear map in finitely many variables on normed spaces is continuous, then it
satisfies the inequality `âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥`, for some `C` which can be chosen to be
positive. -/
theorem exists_bound_of_continuous (hf : continuous f) :
  âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) :=
begin
  casesI is_empty_or_nonempty Î¹,
  { refine âŸ¨âˆ¥f 0âˆ¥ + 1, add_pos_of_nonneg_of_pos (norm_nonneg _) zero_lt_one, Î» m, _âŸ©,
    obtain rfl : m = 0, from funext (is_empty.elim â€¹_â€º),
    simp [univ_eq_empty, zero_le_one] },
  obtain âŸ¨Îµ : â„, Îµ0 : 0 < Îµ, hÎµ : âˆ€ m : Î  i, E i, âˆ¥m - 0âˆ¥ < Îµ â†’ âˆ¥f m - f 0âˆ¥ < 1âŸ© :=
    normed_group.tendsto_nhds_nhds.1 (hf.tendsto 0) 1 zero_lt_one,
  simp only [sub_zero, f.map_zero] at hÎµ,
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  have : 0 < (âˆ¥câˆ¥ / Îµ) ^ fintype.card Î¹, from pow_pos (div_pos (zero_lt_one.trans hc) Îµ0) _,
  refine âŸ¨_, this, _âŸ©,
  refine f.bound_of_shell (Î» _, Îµ0) (Î» _, hc) (Î» m hcm hm, _),
  refine (hÎµ m ((pi_norm_lt_iff Îµ0).2 hm)).le.trans _,
  rw [â† div_le_iff' this, one_div, â† inv_pow', inv_div, fintype.card, â† prod_const],
  exact prod_le_prod (Î» _ _, div_nonneg Îµ0.le (norm_nonneg _)) (Î» i _, hcm i)
end

/-- If `f` satisfies a boundedness property around `0`, one can deduce a bound on `f mâ‚ - f mâ‚‚`
using the multilinearity. Here, we give a precise but hard to use version. See
`norm_image_sub_le_of_bound` for a less precise but more usable version. The bound reads
`âˆ¥f m - f m'âˆ¥ â‰¤
  C * âˆ¥m 1 - m' 1âˆ¥ * max âˆ¥m 2âˆ¥ âˆ¥m' 2âˆ¥ * max âˆ¥m 3âˆ¥ âˆ¥m' 3âˆ¥ * ... * max âˆ¥m nâˆ¥ âˆ¥m' nâˆ¥ + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`. -/
lemma norm_image_sub_le_of_bound' {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) (mâ‚ mâ‚‚ : Î i, E i) :
  âˆ¥f mâ‚ - f mâ‚‚âˆ¥ â‰¤
  C * âˆ‘ i, âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥ :=
begin
  have A : âˆ€(s : finset Î¹), âˆ¥f mâ‚ - f (s.piecewise mâ‚‚ mâ‚)âˆ¥
    â‰¤ C * âˆ‘ i in s, âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥,
  { refine finset.induction (by simp) _,
    assume i s his Hrec,
    have I : âˆ¥f (s.piecewise mâ‚‚ mâ‚) - f ((insert i s).piecewise mâ‚‚ mâ‚)âˆ¥
      â‰¤ C * âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥,
    { have A : ((insert i s).piecewise mâ‚‚ mâ‚)
            = function.update (s.piecewise mâ‚‚ mâ‚) i (mâ‚‚ i) := s.piecewise_insert _ _ _,
      have B : s.piecewise mâ‚‚ mâ‚ = function.update (s.piecewise mâ‚‚ mâ‚) i (mâ‚ i),
      { ext j,
        by_cases h : j = i,
        { rw h, simp [his] },
        { simp [h] } },
      rw [B, A, â† f.map_sub],
      apply le_trans (H _) (mul_le_mul_of_nonneg_left _ hC),
      refine prod_le_prod (Î»j hj, norm_nonneg _) (Î»j hj, _),
      by_cases h : j = i,
      { rw h, simp },
      { by_cases h' : j âˆˆ s;
        simp [h', h, le_refl] } },
    calc âˆ¥f mâ‚ - f ((insert i s).piecewise mâ‚‚ mâ‚)âˆ¥ â‰¤
      âˆ¥f mâ‚ - f (s.piecewise mâ‚‚ mâ‚)âˆ¥ + âˆ¥f (s.piecewise mâ‚‚ mâ‚) - f ((insert i s).piecewise mâ‚‚ mâ‚)âˆ¥ :
        by { rw [â† dist_eq_norm, â† dist_eq_norm, â† dist_eq_norm], exact dist_triangle _ _ _ }
      ... â‰¤ (C * âˆ‘ i in s, âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥)
            + C * âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥ :
        add_le_add Hrec I
      ... = C * âˆ‘ i in insert i s, âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥ :
        by simp [his, add_comm, left_distrib] },
  convert A univ,
  simp
end

/-- If `f` satisfies a boundedness property around `0`, one can deduce a bound on `f mâ‚ - f mâ‚‚`
using the multilinearity. Here, we give a usable but not very precise version. See
`norm_image_sub_le_of_bound'` for a more precise but less usable version. The bound is
`âˆ¥f m - f m'âˆ¥ â‰¤ C * card Î¹ * âˆ¥m - m'âˆ¥ * (max âˆ¥mâˆ¥ âˆ¥m'âˆ¥) ^ (card Î¹ - 1)`. -/
lemma norm_image_sub_le_of_bound {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) (mâ‚ mâ‚‚ : Î i, E i) :
  âˆ¥f mâ‚ - f mâ‚‚âˆ¥ â‰¤ C * (fintype.card Î¹) * (max âˆ¥mâ‚âˆ¥ âˆ¥mâ‚‚âˆ¥) ^ (fintype.card Î¹ - 1) * âˆ¥mâ‚ - mâ‚‚âˆ¥ :=
begin
  have A : âˆ€ (i : Î¹), âˆ j, (if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥)
    â‰¤ âˆ¥mâ‚ - mâ‚‚âˆ¥ * (max âˆ¥mâ‚âˆ¥ âˆ¥mâ‚‚âˆ¥) ^ (fintype.card Î¹ - 1),
  { assume i,
    calc âˆ j, (if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥)
    â‰¤ âˆ j : Î¹, function.update (Î» j, max âˆ¥mâ‚âˆ¥ âˆ¥mâ‚‚âˆ¥) i (âˆ¥mâ‚ - mâ‚‚âˆ¥) j :
      begin
        apply prod_le_prod,
        { assume j hj, by_cases h : j = i; simp [h, norm_nonneg] },
        { assume j hj,
          by_cases h : j = i,
          { rw h, simp, exact norm_le_pi_norm (mâ‚ - mâ‚‚) i },
          { simp [h, max_le_max, norm_le_pi_norm] } }
      end
    ... = âˆ¥mâ‚ - mâ‚‚âˆ¥ * (max âˆ¥mâ‚âˆ¥ âˆ¥mâ‚‚âˆ¥) ^ (fintype.card Î¹ - 1) :
      by { rw prod_update_of_mem (finset.mem_univ _), simp [card_univ_diff] } },
  calc
  âˆ¥f mâ‚ - f mâ‚‚âˆ¥
  â‰¤ C * âˆ‘ i, âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥ :
    f.norm_image_sub_le_of_bound' hC H mâ‚ mâ‚‚
  ... â‰¤ C * âˆ‘ i, âˆ¥mâ‚ - mâ‚‚âˆ¥ * (max âˆ¥mâ‚âˆ¥ âˆ¥mâ‚‚âˆ¥) ^ (fintype.card Î¹ - 1) :
    mul_le_mul_of_nonneg_left (sum_le_sum (Î»i hi, A i)) hC
  ... = C * (fintype.card Î¹) * (max âˆ¥mâ‚âˆ¥ âˆ¥mâ‚‚âˆ¥) ^ (fintype.card Î¹ - 1) * âˆ¥mâ‚ - mâ‚‚âˆ¥ :
    by { rw [sum_const, card_univ, nsmul_eq_mul], ring }
end

/-- If a multilinear map satisfies an inequality `âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥`, then it is
continuous. -/
theorem continuous_of_bound (C : â„) (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) :
  continuous f :=
begin
  let D := max C 1,
  have D_pos : 0 â‰¤ D := le_trans zero_le_one (le_max_right _ _),
  replace H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ D * âˆ i, âˆ¥m iâˆ¥,
  { assume m,
    apply le_trans (H m) (mul_le_mul_of_nonneg_right (le_max_left _ _) _),
    exact prod_nonneg (Î»(i : Î¹) hi, norm_nonneg (m i)) },
  refine continuous_iff_continuous_at.2 (Î»m, _),
  refine continuous_at_of_locally_lipschitz zero_lt_one
    (D * (fintype.card Î¹) * (âˆ¥mâˆ¥ + 1) ^ (fintype.card Î¹ - 1)) (Î»m' h', _),
  rw [dist_eq_norm, dist_eq_norm],
  have : 0 â‰¤ (max âˆ¥m'âˆ¥ âˆ¥mâˆ¥), by simp,
  have : (max âˆ¥m'âˆ¥ âˆ¥mâˆ¥) â‰¤ âˆ¥mâˆ¥ + 1,
    by simp [zero_le_one, norm_le_of_mem_closed_ball (le_of_lt h'), -add_comm],
  calc
    âˆ¥f m' - f mâˆ¥
    â‰¤ D * (fintype.card Î¹) * (max âˆ¥m'âˆ¥ âˆ¥mâˆ¥) ^ (fintype.card Î¹ - 1) * âˆ¥m' - mâˆ¥ :
      f.norm_image_sub_le_of_bound D_pos H m' m
    ... â‰¤ D * (fintype.card Î¹) * (âˆ¥mâˆ¥ + 1) ^ (fintype.card Î¹ - 1) * âˆ¥m' - mâˆ¥ :
      by apply_rules [mul_le_mul_of_nonneg_right, mul_le_mul_of_nonneg_left, mul_nonneg,
        norm_nonneg, nat.cast_nonneg, pow_le_pow_of_le_left]
end

/-- Constructing a continuous multilinear map from a multilinear map satisfying a boundedness
condition. -/
def mk_continuous (C : â„) (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) :
  continuous_multilinear_map ğ•œ E G :=
{ cont := f.continuous_of_bound C H, ..f }

@[simp] lemma coe_mk_continuous (C : â„) (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) :
  â‡‘(f.mk_continuous C H) = f :=
rfl

/-- Given a multilinear map in `n` variables, if one restricts it to `k` variables putting `z` on
the other coordinates, then the resulting restricted function satisfies an inequality
`âˆ¥f.restr vâˆ¥ â‰¤ C * âˆ¥zâˆ¥^(n-k) * Î  âˆ¥v iâˆ¥` if the original function satisfies `âˆ¥f vâˆ¥ â‰¤ C * Î  âˆ¥v iâˆ¥`. -/
lemma restr_norm_le {k n : â„•} (f : (multilinear_map ğ•œ (Î» i : fin n, G) G' : _))
  (s : finset (fin n)) (hk : s.card = k) (z : G) {C : â„}
  (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) (v : fin k â†’ G) :
  âˆ¥f.restr s hk z vâˆ¥ â‰¤ C * âˆ¥zâˆ¥ ^ (n - k) * âˆ i, âˆ¥v iâˆ¥ :=
begin
  rw [mul_right_comm, mul_assoc],
  convert H _ using 2,
  simp only [apply_dite norm, fintype.prod_dite, prod_const (âˆ¥zâˆ¥), finset.card_univ,
    fintype.card_of_subtype sá¶œ (Î» x, mem_compl), card_compl, fintype.card_fin, hk, mk_coe,
    â† (s.order_iso_of_fin hk).symm.bijective.prod_comp (Î» x, âˆ¥v xâˆ¥)],
  refl
end

end multilinear_map

/-!
### Continuous multilinear maps

We define the norm `âˆ¥fâˆ¥` of a continuous multilinear map `f` in finitely many variables as the
smallest number such that `âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥` for all `m`. We show that this
defines a normed space structure on `continuous_multilinear_map ğ•œ E G`.
-/
namespace continuous_multilinear_map

variables (c : ğ•œ) (f g : continuous_multilinear_map ğ•œ E G) (m : Î i, E i)

theorem bound : âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) :=
f.to_multilinear_map.exists_bound_of_continuous f.2

open real

/-- The operator norm of a continuous multilinear map is the inf of all its bounds. -/
def op_norm := Inf {c | 0 â‰¤ (c : â„) âˆ§ âˆ€ m, âˆ¥f mâˆ¥ â‰¤ c * âˆ i, âˆ¥m iâˆ¥}
instance has_op_norm : has_norm (continuous_multilinear_map ğ•œ E G) := âŸ¨op_normâŸ©

lemma norm_def : âˆ¥fâˆ¥ = Inf {c | 0 â‰¤ (c : â„) âˆ§ âˆ€ m, âˆ¥f mâˆ¥ â‰¤ c * âˆ i, âˆ¥m iâˆ¥} := rfl

-- So that invocations of `le_cInf` make sense: we show that the set of
-- bounds is nonempty and bounded below.
lemma bounds_nonempty {f : continuous_multilinear_map ğ•œ E G} :
  âˆƒ c, c âˆˆ {c | 0 â‰¤ c âˆ§ âˆ€ m, âˆ¥f mâˆ¥ â‰¤ c * âˆ i, âˆ¥m iâˆ¥} :=
let âŸ¨M, hMp, hMbâŸ© := f.bound in âŸ¨M, le_of_lt hMp, hMbâŸ©

lemma bounds_bdd_below {f : continuous_multilinear_map ğ•œ E G} :
  bdd_below {c | 0 â‰¤ c âˆ§ âˆ€ m, âˆ¥f mâˆ¥ â‰¤ c * âˆ i, âˆ¥m iâˆ¥} :=
âŸ¨0, Î» _ âŸ¨hn, _âŸ©, hnâŸ©

lemma op_norm_nonneg : 0 â‰¤ âˆ¥fâˆ¥ :=
le_cInf bounds_nonempty (Î» _ âŸ¨hx, _âŸ©, hx)

/-- The fundamental property of the operator norm of a continuous multilinear map:
`âˆ¥f mâˆ¥` is bounded by `âˆ¥fâˆ¥` times the product of the `âˆ¥m iâˆ¥`. -/
theorem le_op_norm : âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥ :=
begin
  have A : 0 â‰¤ âˆ i, âˆ¥m iâˆ¥ := prod_nonneg (Î»j hj, norm_nonneg _),
  cases A.eq_or_lt with h hlt,
  { rcases prod_eq_zero_iff.1 h.symm with âŸ¨i, _, hiâŸ©,
    rw norm_eq_zero at hi,
    have : f m = 0 := f.map_coord_zero i hi,
    rw [this, norm_zero],
    exact mul_nonneg (op_norm_nonneg f) A },
  { rw [â† div_le_iff hlt],
    apply le_cInf bounds_nonempty,
    rintro c âŸ¨_, hcâŸ©, rw [div_le_iff hlt], apply hc }
end

theorem le_of_op_norm_le {C : â„} (h : âˆ¥fâˆ¥ â‰¤ C) : âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥ :=
(f.le_op_norm m).trans $ mul_le_mul_of_nonneg_right h (prod_nonneg $ Î» i _, norm_nonneg (m i))

lemma ratio_le_op_norm : âˆ¥f mâˆ¥ / âˆ i, âˆ¥m iâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
div_le_of_nonneg_of_le_mul (prod_nonneg $ Î» i _, norm_nonneg _) (op_norm_nonneg _) (f.le_op_norm m)

/-- The image of the unit ball under a continuous multilinear map is bounded. -/
lemma unit_le_op_norm (h : âˆ¥mâˆ¥ â‰¤ 1) : âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
calc
  âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥ : f.le_op_norm m
  ... â‰¤ âˆ¥fâˆ¥ * âˆ i : Î¹, 1 :
    mul_le_mul_of_nonneg_left (prod_le_prod (Î»i hi, norm_nonneg _)
      (Î»i hi, le_trans (norm_le_pi_norm _ _) h)) (op_norm_nonneg f)
  ... = âˆ¥fâˆ¥ : by simp

/-- If one controls the norm of every `f x`, then one controls the norm of `f`. -/
lemma op_norm_le_bound {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ M * âˆ i, âˆ¥m iâˆ¥) :
  âˆ¥fâˆ¥ â‰¤ M :=
cInf_le bounds_bdd_below âŸ¨hMp, hMâŸ©

/-- The operator norm satisfies the triangle inequality. -/
theorem op_norm_add_le : âˆ¥f + gâˆ¥ â‰¤ âˆ¥fâˆ¥ + âˆ¥gâˆ¥ :=
cInf_le bounds_bdd_below
  âŸ¨add_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Î» x, by { rw add_mul,
    exact norm_add_le_of_le (le_op_norm _ _) (le_op_norm _ _) }âŸ©

/-- A continuous linear map is zero iff its norm vanishes. -/
theorem op_norm_zero_iff : âˆ¥fâˆ¥ = 0 â†” f = 0 :=
begin
  split,
  { assume h,
    ext m,
    simpa [h] using f.le_op_norm m },
  { rintro rfl,
    apply le_antisymm (op_norm_le_bound 0 le_rfl (Î»m, _)) (op_norm_nonneg _),
    simp }
end

variables {ğ•œ' : Type*} [nondiscrete_normed_field ğ•œ'] [normed_algebra ğ•œ' ğ•œ]
  [normed_space ğ•œ' G] [is_scalar_tower ğ•œ' ğ•œ G]

lemma op_norm_smul_le (c : ğ•œ') : âˆ¥c â€¢ fâˆ¥ â‰¤ âˆ¥câˆ¥ * âˆ¥fâˆ¥ :=
(c â€¢ f).op_norm_le_bound
  (mul_nonneg (norm_nonneg _) (op_norm_nonneg _))
  begin
    intro m,
    erw [norm_smul, mul_assoc],
    exact mul_le_mul_of_nonneg_left (le_op_norm _ _) (norm_nonneg _)
  end

lemma op_norm_neg : âˆ¥-fâˆ¥ = âˆ¥fâˆ¥ := by { rw norm_def, apply congr_arg, ext, simp }

/-- Continuous multilinear maps themselves form a normed space with respect to
    the operator norm. -/
instance to_normed_group : normed_group (continuous_multilinear_map ğ•œ E G) :=
normed_group.of_core _ âŸ¨op_norm_zero_iff, op_norm_add_le, op_norm_negâŸ©

instance to_normed_space : normed_space ğ•œ' (continuous_multilinear_map ğ•œ E G) :=
âŸ¨Î» c f, f.op_norm_smul_le câŸ©

theorem le_op_norm_mul_prod_of_le {b : Î¹ â†’ â„} (hm : âˆ€ i, âˆ¥m iâˆ¥ â‰¤ b i) : âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, b i :=
(f.le_op_norm m).trans $ mul_le_mul_of_nonneg_left
  (prod_le_prod (Î» _ _, norm_nonneg _) (Î» i _, hm i)) (norm_nonneg f)

theorem le_op_norm_mul_pow_card_of_le {b : â„} (hm : âˆ€ i, âˆ¥m iâˆ¥ â‰¤ b) :
  âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * b ^ fintype.card Î¹ :=
by simpa only [prod_const] using f.le_op_norm_mul_prod_of_le m hm

theorem le_op_norm_mul_pow_of_le {Ei : fin n â†’ Type*} [Î  i, normed_group (Ei i)]
  [Î  i, normed_space ğ•œ (Ei i)] (f : continuous_multilinear_map ğ•œ Ei G) (m : Î  i, Ei i)
  {b : â„} (hm : âˆ¥mâˆ¥ â‰¤ b) :
  âˆ¥f mâˆ¥ â‰¤ âˆ¥fâˆ¥ * b ^ n :=
by simpa only [fintype.card_fin]
  using f.le_op_norm_mul_pow_card_of_le m (Î» i, (norm_le_pi_norm m i).trans hm)

/-- The fundamental property of the operator norm of a continuous multilinear map:
`âˆ¥f mâˆ¥` is bounded by `âˆ¥fâˆ¥` times the product of the `âˆ¥m iâˆ¥`, `nnnorm` version. -/
theorem le_op_nnnorm : nnnorm (f m) â‰¤ nnnorm f * âˆ i, nnnorm (m i) :=
nnreal.coe_le_coe.1 $ by { push_cast, exact f.le_op_norm m }

theorem le_of_op_nnnorm_le {C : â„â‰¥0} (h : nnnorm f â‰¤ C) : nnnorm (f m) â‰¤ C * âˆ i, nnnorm (m i) :=
(f.le_op_nnnorm m).trans $ mul_le_mul' h le_rfl

lemma op_norm_prod (f : continuous_multilinear_map ğ•œ E G) (g : continuous_multilinear_map ğ•œ E G') :
  âˆ¥f.prod gâˆ¥ = max (âˆ¥fâˆ¥) (âˆ¥gâˆ¥) :=
le_antisymm
  (op_norm_le_bound _ (norm_nonneg (f, g)) (Î» m,
    have H : 0 â‰¤ âˆ i, âˆ¥m iâˆ¥, from prod_nonneg $ Î» _ _,  norm_nonneg _,
    by simpa only [prod_apply, prod.norm_def, max_mul_of_nonneg, H]
      using max_le_max (f.le_op_norm m) (g.le_op_norm m))) $
  max_le
    (f.op_norm_le_bound (norm_nonneg _) $ Î» m, (le_max_left _ _).trans ((f.prod g).le_op_norm _))
    (g.op_norm_le_bound (norm_nonneg _) $ Î» m, (le_max_right _ _).trans ((f.prod g).le_op_norm _))

lemma norm_pi {Î¹' : Type v'} [fintype Î¹'] {E' : Î¹' â†’ Type wE'} [Î  i', normed_group (E' i')]
  [Î  i', normed_space ğ•œ (E' i')] (f : Î  i', continuous_multilinear_map ğ•œ E (E' i')) :
  âˆ¥pi fâˆ¥ = âˆ¥fâˆ¥ :=
begin
  apply le_antisymm,
  { refine (op_norm_le_bound _ (norm_nonneg f) (Î» m, _)),
    dsimp,
    rw pi_norm_le_iff,
    exacts [Î» i, (f i).le_of_op_norm_le m (norm_le_pi_norm f i),
      mul_nonneg (norm_nonneg f) (prod_nonneg $ Î» _ _, norm_nonneg _)] },
  { refine (pi_norm_le_iff (norm_nonneg _)).2 (Î» i, _),
    refine (op_norm_le_bound _ (norm_nonneg _) (Î» m, _)),
    refine le_trans _ ((pi f).le_op_norm m),
    convert norm_le_pi_norm (Î» j, f j m) i }
end

section

variables (ğ•œ E E' G G')

/-- `continuous_multilinear_map.prod` as a `linear_isometry_equiv`. -/
def prodL :
  (continuous_multilinear_map ğ•œ E G) Ã— (continuous_multilinear_map ğ•œ E G') â‰ƒâ‚—áµ¢[ğ•œ]
    continuous_multilinear_map ğ•œ E (G Ã— G') :=
{ to_fun := Î» f, f.1.prod f.2,
  inv_fun := Î» f, ((continuous_linear_map.fst ğ•œ G G').comp_continuous_multilinear_map f,
    (continuous_linear_map.snd ğ•œ G G').comp_continuous_multilinear_map f),
  map_add' := Î» f g, rfl,
  map_smul' := Î» c f, rfl,
  left_inv := Î» f, by ext; refl,
  right_inv := Î» f, by ext; refl,
  norm_map' := Î» f, op_norm_prod f.1 f.2 }

/-- `continuous_multilinear_map.pi` as a `linear_isometry_equiv`. -/
def piâ‚—áµ¢ {Î¹' : Type v'} [fintype Î¹'] {E' : Î¹' â†’ Type wE'} [Î  i', normed_group (E' i')]
  [Î  i', normed_space ğ•œ (E' i')] :
  @linear_isometry_equiv ğ•œ (Î  i', continuous_multilinear_map ğ•œ E (E' i'))
    (continuous_multilinear_map ğ•œ E (Î  i, E' i)) _ _ _
      (@pi.module Î¹' _ ğ•œ _ _ (Î» i', infer_instance)) _ :=
{ to_linear_equiv :=
  -- note: `pi_linear_equiv` does not unify correctly here, presumably due to issues with dependent
  -- typeclass arguments.
  { map_add' := Î» f g, rfl,
    map_smul' := Î» c f, rfl,
    .. pi_equiv, },
  norm_map' := norm_pi }

end

section restrict_scalars

variables [Î  i, normed_space ğ•œ' (E i)] [âˆ€ i, is_scalar_tower ğ•œ' ğ•œ (E i)]

@[simp] lemma norm_restrict_scalars : âˆ¥f.restrict_scalars ğ•œ'âˆ¥ = âˆ¥fâˆ¥ :=
by simp only [norm_def, coe_restrict_scalars]

variable (ğ•œ')

/-- `continuous_multilinear_map.restrict_scalars` as a `continuous_multilinear_map`. -/
def restrict_scalars_linear :
  continuous_multilinear_map ğ•œ E G â†’L[ğ•œ'] continuous_multilinear_map ğ•œ' E G :=
linear_map.mk_continuous
{ to_fun := restrict_scalars ğ•œ',
  map_add' := Î» mâ‚ mâ‚‚, rfl,
  map_smul' := Î» c m, rfl } 1 $ Î» f, by simp

variable {ğ•œ'}

lemma continuous_restrict_scalars :
  continuous (restrict_scalars ğ•œ' : continuous_multilinear_map ğ•œ E G â†’
    continuous_multilinear_map ğ•œ' E G) :=
(restrict_scalars_linear ğ•œ').continuous

end restrict_scalars

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `âˆ¥fâˆ¥` and `âˆ¥mâ‚ - mâ‚‚âˆ¥`, precise version.
For a less precise but more usable version, see `norm_image_sub_le`. The bound reads
`âˆ¥f m - f m'âˆ¥ â‰¤
  âˆ¥fâˆ¥ * âˆ¥m 1 - m' 1âˆ¥ * max âˆ¥m 2âˆ¥ âˆ¥m' 2âˆ¥ * max âˆ¥m 3âˆ¥ âˆ¥m' 3âˆ¥ * ... * max âˆ¥m nâˆ¥ âˆ¥m' nâˆ¥ + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`.-/
lemma norm_image_sub_le' (mâ‚ mâ‚‚ : Î i, E i) :
  âˆ¥f mâ‚ - f mâ‚‚âˆ¥ â‰¤
  âˆ¥fâˆ¥ * âˆ‘ i, âˆ j, if j = i then âˆ¥mâ‚ i - mâ‚‚ iâˆ¥ else max âˆ¥mâ‚ jâˆ¥ âˆ¥mâ‚‚ jâˆ¥ :=
f.to_multilinear_map.norm_image_sub_le_of_bound' (norm_nonneg _) f.le_op_norm _ _

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `âˆ¥fâˆ¥` and `âˆ¥mâ‚ - mâ‚‚âˆ¥`, less precise
version. For a more precise but less usable version, see `norm_image_sub_le'`.
The bound is `âˆ¥f m - f m'âˆ¥ â‰¤ âˆ¥fâˆ¥ * card Î¹ * âˆ¥m - m'âˆ¥ * (max âˆ¥mâˆ¥ âˆ¥m'âˆ¥) ^ (card Î¹ - 1)`.-/
lemma norm_image_sub_le (mâ‚ mâ‚‚ : Î i, E i) :
  âˆ¥f mâ‚ - f mâ‚‚âˆ¥ â‰¤ âˆ¥fâˆ¥ * (fintype.card Î¹) * (max âˆ¥mâ‚âˆ¥ âˆ¥mâ‚‚âˆ¥) ^ (fintype.card Î¹ - 1) * âˆ¥mâ‚ - mâ‚‚âˆ¥ :=
f.to_multilinear_map.norm_image_sub_le_of_bound (norm_nonneg _) f.le_op_norm _ _

/-- Applying a multilinear map to a vector is continuous in both coordinates. -/
lemma continuous_eval :
  continuous (Î» p : continuous_multilinear_map ğ•œ E G Ã— Î  i, E i, p.1 p.2) :=
begin
  apply continuous_iff_continuous_at.2 (Î»p, _),
  apply continuous_at_of_locally_lipschitz zero_lt_one
    ((âˆ¥pâˆ¥ + 1) * (fintype.card Î¹) * (âˆ¥pâˆ¥ + 1) ^ (fintype.card Î¹ - 1) + âˆ i, âˆ¥p.2 iâˆ¥)
    (Î»q hq, _),
  have : 0 â‰¤ (max âˆ¥q.2âˆ¥ âˆ¥p.2âˆ¥), by simp,
  have : 0 â‰¤ âˆ¥pâˆ¥ + 1, by simp [le_trans zero_le_one],
  have A : âˆ¥qâˆ¥ â‰¤ âˆ¥pâˆ¥ + 1 := norm_le_of_mem_closed_ball (le_of_lt hq),
  have : (max âˆ¥q.2âˆ¥ âˆ¥p.2âˆ¥) â‰¤ âˆ¥pâˆ¥ + 1 :=
    le_trans (max_le_max (norm_snd_le q) (norm_snd_le p)) (by simp [A, -add_comm, zero_le_one]),
  have : âˆ€ (i : Î¹), i âˆˆ univ â†’ 0 â‰¤ âˆ¥p.2 iâˆ¥ := Î» i hi, norm_nonneg _,
  calc dist (q.1 q.2) (p.1 p.2)
    â‰¤ dist (q.1 q.2) (q.1 p.2) + dist (q.1 p.2) (p.1 p.2) : dist_triangle _ _ _
    ... = âˆ¥q.1 q.2 - q.1 p.2âˆ¥ + âˆ¥q.1 p.2 - p.1 p.2âˆ¥ : by rw [dist_eq_norm, dist_eq_norm]
    ... â‰¤ âˆ¥q.1âˆ¥ * (fintype.card Î¹) * (max âˆ¥q.2âˆ¥ âˆ¥p.2âˆ¥) ^ (fintype.card Î¹ - 1) * âˆ¥q.2 - p.2âˆ¥
          + âˆ¥q.1 - p.1âˆ¥ * âˆ i, âˆ¥p.2 iâˆ¥ :
      add_le_add (norm_image_sub_le _ _ _) ((q.1 - p.1).le_op_norm p.2)
    ... â‰¤ (âˆ¥pâˆ¥ + 1) * (fintype.card Î¹) * (âˆ¥pâˆ¥ + 1) ^ (fintype.card Î¹ - 1) * âˆ¥q - pâˆ¥
          + âˆ¥q - pâˆ¥ * âˆ i, âˆ¥p.2 iâˆ¥ :
      by apply_rules [add_le_add, mul_le_mul, le_refl, le_trans (norm_fst_le q) A, nat.cast_nonneg,
        mul_nonneg, pow_le_pow_of_le_left, pow_nonneg, norm_snd_le (q - p), norm_nonneg,
        norm_fst_le (q - p), prod_nonneg]
    ... = ((âˆ¥pâˆ¥ + 1) * (fintype.card Î¹) * (âˆ¥pâˆ¥ + 1) ^ (fintype.card Î¹ - 1)
              + (âˆ i, âˆ¥p.2 iâˆ¥)) * dist q p : by { rw dist_eq_norm, ring }
end

lemma continuous_eval_left (m : Î  i, E i) :
  continuous (Î» p : continuous_multilinear_map ğ•œ E G, p m) :=
continuous_eval.comp (continuous_id.prod_mk continuous_const)

lemma has_sum_eval
  {Î± : Type*} {p : Î± â†’ continuous_multilinear_map ğ•œ E G} {q : continuous_multilinear_map ğ•œ E G}
  (h : has_sum p q) (m : Î  i, E i) : has_sum (Î» a, p a m) (q m) :=
begin
  dsimp [has_sum] at h âŠ¢,
  convert ((continuous_eval_left m).tendsto _).comp h,
  ext s,
  simp
end

lemma tsum_eval {Î± : Type*} {p : Î± â†’ continuous_multilinear_map ğ•œ E G} (hp : summable p)
  (m : Î  i, E i) : (âˆ‘' a, p a) m = âˆ‘' a, p a m :=
(has_sum_eval hp.has_sum m).tsum_eq.symm

open_locale topological_space
open filter

/-- If the target space is complete, the space of continuous multilinear maps with its norm is also
complete. The proof is essentially the same as for the space of continuous linear maps (modulo the
addition of `finset.prod` where needed. The duplication could be avoided by deducing the linear
case from the multilinear case via a currying isomorphism. However, this would mess up imports,
and it is more satisfactory to have the simplest case as a standalone proof. -/
instance [complete_space G] : complete_space (continuous_multilinear_map ğ•œ E G) :=
begin
  have nonneg : âˆ€ (v : Î  i, E i), 0 â‰¤ âˆ i, âˆ¥v iâˆ¥ :=
    Î» v, finset.prod_nonneg (Î» i hi, norm_nonneg _),
  -- We show that every Cauchy sequence converges.
  refine metric.complete_of_cauchy_seq_tendsto (Î» f hf, _),
  -- We now expand out the definition of a Cauchy sequence,
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with âŸ¨b, b0, b_bound, b_limâŸ©,
  -- and establish that the evaluation at any point `v : Î  i, E i` is Cauchy.
  have cau : âˆ€ v, cauchy_seq (Î» n, f n v),
  { assume v,
    apply cauchy_seq_iff_le_tendsto_0.2 âŸ¨Î» n, b n * âˆ i, âˆ¥v iâˆ¥, Î» n, _, _, _âŸ©,
    { exact mul_nonneg (b0 n) (nonneg v) },
    { assume n m N hn hm,
      rw dist_eq_norm,
      apply le_trans ((f n - f m).le_op_norm v) _,
      exact mul_le_mul_of_nonneg_right (b_bound n m N hn hm) (nonneg v) },
    { simpa using b_lim.mul tendsto_const_nhds } },
  -- We assemble the limits points of those Cauchy sequences
  -- (which exist as `G` is complete)
  -- into a function which we call `F`.
  choose F hF using Î»v, cauchy_seq_tendsto_of_complete (cau v),
  -- Next, we show that this `F` is multilinear,
  let Fmult : multilinear_map ğ•œ E G :=
  { to_fun := F,
    map_add' := Î» v i x y, begin
      have A := hF (function.update v i (x + y)),
      have B := (hF (function.update v i x)).add (hF (function.update v i y)),
      simp at A B,
      exact tendsto_nhds_unique A B
    end,
    map_smul' := Î» v i c x, begin
      have A := hF (function.update v i (c â€¢ x)),
      have B := filter.tendsto.smul (@tendsto_const_nhds _ â„• _ c _) (hF (function.update v i x)),
      simp at A B,
      exact tendsto_nhds_unique A B
    end },
  -- and that `F` has norm at most `(b 0 + âˆ¥f 0âˆ¥)`.
  have Fnorm : âˆ€ v, âˆ¥F vâˆ¥ â‰¤ (b 0 + âˆ¥f 0âˆ¥) * âˆ i, âˆ¥v iâˆ¥,
  { assume v,
    have A : âˆ€ n, âˆ¥f n vâˆ¥ â‰¤ (b 0 + âˆ¥f 0âˆ¥) * âˆ i, âˆ¥v iâˆ¥,
    { assume n,
      apply le_trans ((f n).le_op_norm _) _,
      apply mul_le_mul_of_nonneg_right _ (nonneg v),
      calc âˆ¥f nâˆ¥ = âˆ¥(f n - f 0) + f 0âˆ¥ : by { congr' 1, abel }
      ... â‰¤ âˆ¥f n - f 0âˆ¥ + âˆ¥f 0âˆ¥ : norm_add_le _ _
      ... â‰¤ b 0 + âˆ¥f 0âˆ¥ : begin
        apply add_le_add_right,
        simpa [dist_eq_norm] using b_bound n 0 0 (zero_le _) (zero_le _)
      end },
    exact le_of_tendsto (hF v).norm (eventually_of_forall A) },
  -- Thus `F` is continuous, and we propose that as the limit point of our original Cauchy sequence.
  let Fcont := Fmult.mk_continuous _ Fnorm,
  use Fcont,
  -- Our last task is to establish convergence to `F` in norm.
  have : âˆ€ n, âˆ¥f n - Fcontâˆ¥ â‰¤ b n,
  { assume n,
    apply op_norm_le_bound _ (b0 n) (Î» v, _),
    have A : âˆ€á¶  m in at_top, âˆ¥(f n - f m) vâˆ¥ â‰¤ b n * âˆ i, âˆ¥v iâˆ¥,
    { refine eventually_at_top.2 âŸ¨n, Î» m hm, _âŸ©,
      apply le_trans ((f n - f m).le_op_norm _) _,
      exact mul_le_mul_of_nonneg_right (b_bound n m n (le_refl _) hm) (nonneg v) },
    have B : tendsto (Î» m, âˆ¥(f n - f m) vâˆ¥) at_top (ğ“ (âˆ¥(f n - Fcont) vâˆ¥)) :=
      tendsto.norm (tendsto_const_nhds.sub (hF v)),
    exact le_of_tendsto B A },
  erw tendsto_iff_norm_tendsto_zero,
  exact squeeze_zero (Î» n, norm_nonneg _) this b_lim,
end

end continuous_multilinear_map

/-- If a continuous multilinear map is constructed from a multilinear map via the constructor
`mk_continuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
lemma multilinear_map.mk_continuous_norm_le (f : multilinear_map ğ•œ E G) {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) : âˆ¥f.mk_continuous C Hâˆ¥ â‰¤ C :=
continuous_multilinear_map.op_norm_le_bound _ hC (Î»m, H m)

/-- If a continuous multilinear map is constructed from a multilinear map via the constructor
`mk_continuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
lemma multilinear_map.mk_continuous_norm_le' (f : multilinear_map ğ•œ E G) {C : â„}
  (H : âˆ€ m, âˆ¥f mâˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥) : âˆ¥f.mk_continuous C Hâˆ¥ â‰¤ max C 0 :=
continuous_multilinear_map.op_norm_le_bound _ (le_max_right _ _) $
  Î» m, (H m).trans $ mul_le_mul_of_nonneg_right (le_max_left _ _)
    (prod_nonneg $ Î» _ _, norm_nonneg _)

namespace continuous_multilinear_map

/-- Given a continuous multilinear map `f` on `n` variables (parameterized by `fin n`) and a subset
`s` of `k` of these variables, one gets a new continuous multilinear map on `fin k` by varying
these variables, and fixing the other ones equal to a given value `z`. It is denoted by
`f.restr s hk z`, where `hk` is a proof that the cardinality of `s` is `k`. The implicit
identification between `fin k` and `s` that we use is the canonical (increasing) bijection. -/
def restr {k n : â„•} (f : (G [Ã—n]â†’L[ğ•œ] G' : _))
  (s : finset (fin n)) (hk : s.card = k) (z : G) : G [Ã—k]â†’L[ğ•œ] G' :=
(f.to_multilinear_map.restr s hk z).mk_continuous
(âˆ¥fâˆ¥ * âˆ¥zâˆ¥^(n-k)) $ Î» v, multilinear_map.restr_norm_le _ _ _ _ f.le_op_norm _

lemma norm_restr {k n : â„•} (f : G [Ã—n]â†’L[ğ•œ] G') (s : finset (fin n)) (hk : s.card = k) (z : G) :
  âˆ¥f.restr s hk zâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥zâˆ¥ ^ (n - k) :=
begin
  apply multilinear_map.mk_continuous_norm_le,
  exact mul_nonneg (norm_nonneg _) (pow_nonneg (norm_nonneg _) _)
end

section

variables (ğ•œ Î¹) (A : Type*) [normed_comm_ring A] [normed_algebra ğ•œ A]

/-- The continuous multilinear map on `A^Î¹`, where `A` is a normed commutative algebra
over `ğ•œ`, associating to `m` the product of all the `m i`.

See also `continuous_multilinear_map.mk_pi_algebra_fin`. -/
protected def mk_pi_algebra : continuous_multilinear_map ğ•œ (Î» i : Î¹, A) A :=
multilinear_map.mk_continuous
  (multilinear_map.mk_pi_algebra ğ•œ Î¹ A) (if nonempty Î¹ then 1 else âˆ¥(1 : A)âˆ¥) $
  begin
    intro m,
    casesI is_empty_or_nonempty Î¹ with hÎ¹ hÎ¹,
    { simp [eq_empty_of_is_empty univ, not_nonempty_iff.2 hÎ¹] },
    { simp [norm_prod_le' univ univ_nonempty, hÎ¹] }
  end

variables {A ğ•œ Î¹}

@[simp] lemma mk_pi_algebra_apply (m : Î¹ â†’ A) :
  continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ A m = âˆ i, m i :=
rfl

lemma norm_mk_pi_algebra_le [nonempty Î¹] :
  âˆ¥continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâˆ¥ â‰¤ 1 :=
calc âˆ¥continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâˆ¥ â‰¤ if nonempty Î¹ then 1 else âˆ¥(1 : A)âˆ¥ :
  multilinear_map.mk_continuous_norm_le _ (by split_ifs; simp [zero_le_one]) _
... = _ : if_pos â€¹_â€º

lemma norm_mk_pi_algebra_of_empty [is_empty Î¹] :
  âˆ¥continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâˆ¥ = âˆ¥(1 : A)âˆ¥ :=
begin
  apply le_antisymm,
  calc âˆ¥continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâˆ¥ â‰¤ if nonempty Î¹ then 1 else âˆ¥(1 : A)âˆ¥ :
    multilinear_map.mk_continuous_norm_le _ (by split_ifs; simp [zero_le_one]) _
  ... = âˆ¥(1 : A)âˆ¥ : if_neg (not_nonempty_iff.mpr â€¹_â€º),
  convert ratio_le_op_norm _ (Î» _, (1 : A)),
  simp [eq_empty_of_is_empty (univ : finset Î¹)],
end

@[simp] lemma norm_mk_pi_algebra [norm_one_class A] :
  âˆ¥continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâˆ¥ = 1 :=
begin
  casesI is_empty_or_nonempty Î¹,
  { simp [norm_mk_pi_algebra_of_empty] },
  { refine le_antisymm norm_mk_pi_algebra_le _,
    convert ratio_le_op_norm _ (Î» _, 1); [skip, apply_instance],
    simp },
end

end

section

variables (ğ•œ n) (A : Type*) [normed_ring A] [normed_algebra ğ•œ A]

/-- The continuous multilinear map on `A^n`, where `A` is a normed algebra over `ğ•œ`, associating to
`m` the product of all the `m i`.

See also: `multilinear_map.mk_pi_algebra`. -/
protected def mk_pi_algebra_fin : continuous_multilinear_map ğ•œ (Î» i : fin n, A) A :=
multilinear_map.mk_continuous
  (multilinear_map.mk_pi_algebra_fin ğ•œ n A) (nat.cases_on n âˆ¥(1 : A)âˆ¥ (Î» _, 1)) $
  begin
    intro m,
    cases n,
    { simp },
    { have : @list.of_fn A n.succ m â‰  [] := by simp,
      simpa [â† fin.prod_of_fn] using list.norm_prod_le' this }
  end

variables {A ğ•œ n}

@[simp] lemma mk_pi_algebra_fin_apply (m : fin n â†’ A) :
  continuous_multilinear_map.mk_pi_algebra_fin ğ•œ n A m = (list.of_fn m).prod :=
rfl

lemma norm_mk_pi_algebra_fin_succ_le :
  âˆ¥continuous_multilinear_map.mk_pi_algebra_fin ğ•œ n.succ Aâˆ¥ â‰¤ 1 :=
multilinear_map.mk_continuous_norm_le _ zero_le_one _

lemma norm_mk_pi_algebra_fin_le_of_pos (hn : 0 < n) :
  âˆ¥continuous_multilinear_map.mk_pi_algebra_fin ğ•œ n Aâˆ¥ â‰¤ 1 :=
by cases n; [exact hn.false.elim, exact norm_mk_pi_algebra_fin_succ_le]

lemma norm_mk_pi_algebra_fin_zero :
  âˆ¥continuous_multilinear_map.mk_pi_algebra_fin ğ•œ 0 Aâˆ¥ = âˆ¥(1 : A)âˆ¥ :=
begin
  refine le_antisymm (multilinear_map.mk_continuous_norm_le _ (norm_nonneg _) _) _,
  convert ratio_le_op_norm _ (Î» _, 1); [simp, apply_instance]
end

@[simp] lemma norm_mk_pi_algebra_fin [norm_one_class A] :
  âˆ¥continuous_multilinear_map.mk_pi_algebra_fin ğ•œ n Aâˆ¥ = 1 :=
begin
  cases n,
  { simp [norm_mk_pi_algebra_fin_zero] },
  { refine le_antisymm norm_mk_pi_algebra_fin_succ_le _,
    convert ratio_le_op_norm _ (Î» _, 1); [skip, apply_instance],
    simp }
end

end

variables (ğ•œ Î¹)

/-- The canonical continuous multilinear map on `ğ•œ^Î¹`, associating to `m` the product of all the
`m i` (multiplied by a fixed reference element `z` in the target module) -/
protected def mk_pi_field (z : G) : continuous_multilinear_map ğ•œ (Î»(i : Î¹), ğ•œ) G :=
multilinear_map.mk_continuous
  (multilinear_map.mk_pi_ring ğ•œ Î¹ z) (âˆ¥zâˆ¥)
  (Î» m, by simp only [multilinear_map.mk_pi_ring_apply, norm_smul, normed_field.norm_prod,
    mul_comm])

variables {ğ•œ Î¹}

@[simp] lemma mk_pi_field_apply (z : G) (m : Î¹ â†’ ğ•œ) :
  (continuous_multilinear_map.mk_pi_field ğ•œ Î¹ z : (Î¹ â†’ ğ•œ) â†’ G) m = (âˆ i, m i) â€¢ z := rfl

lemma mk_pi_field_apply_one_eq_self (f : continuous_multilinear_map ğ•œ (Î»(i : Î¹), ğ•œ) G) :
  continuous_multilinear_map.mk_pi_field ğ•œ Î¹ (f (Î»i, 1)) = f :=
to_multilinear_map_inj f.to_multilinear_map.mk_pi_ring_apply_one_eq_self

variables (ğ•œ Î¹ G)

/-- Continuous multilinear maps on `ğ•œ^n` with values in `G` are in bijection with `G`, as such a
continuous multilinear map is completely determined by its value on the constant vector made of
ones. We register this bijection as a linear equivalence in
`continuous_multilinear_map.pi_field_equiv_aux`. The continuous linear equivalence is
`continuous_multilinear_map.pi_field_equiv`. -/
protected def pi_field_equiv_aux : G â‰ƒâ‚—[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : Î¹), ğ•œ) G) :=
{ to_fun    := Î» z, continuous_multilinear_map.mk_pi_field ğ•œ Î¹ z,
  inv_fun   := Î» f, f (Î»i, 1),
  map_add'  := Î» z z', by { ext m, simp [smul_add] },
  map_smul' := Î» c z, by { ext m, simp [smul_smul, mul_comm] },
  left_inv  := Î» z, by simp,
  right_inv := Î» f, f.mk_pi_field_apply_one_eq_self }

/-- Continuous multilinear maps on `ğ•œ^n` with values in `G` are in bijection with `G`, as such a
continuous multilinear map is completely determined by its value on the constant vector made of
ones. We register this bijection as a continuous linear equivalence in
`continuous_multilinear_map.pi_field_equiv`. -/
protected def pi_field_equiv : G â‰ƒL[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : Î¹), ğ•œ) G) :=
{ continuous_to_fun := begin
    refine (continuous_multilinear_map.pi_field_equiv_aux ğ•œ Î¹ G).to_linear_map.continuous_of_bound
      (1 : â„) (Î»z, _),
    rw one_mul,
    change âˆ¥continuous_multilinear_map.mk_pi_field ğ•œ Î¹ zâˆ¥ â‰¤ âˆ¥zâˆ¥,
    exact multilinear_map.mk_continuous_norm_le _ (norm_nonneg _) _
  end,
  continuous_inv_fun := begin
    refine
      (continuous_multilinear_map.pi_field_equiv_aux ğ•œ Î¹ G).symm.to_linear_map.continuous_of_bound
      (1 : â„) (Î»f, _),
    rw one_mul,
    change âˆ¥f (Î»i, 1)âˆ¥ â‰¤ âˆ¥fâˆ¥,
    apply @continuous_multilinear_map.unit_le_op_norm ğ•œ Î¹ (Î» (i : Î¹), ğ•œ) G _ _ _ _ _ _ _ f,
    simp [pi_norm_le_iff zero_le_one, le_refl]
  end,
  .. continuous_multilinear_map.pi_field_equiv_aux ğ•œ Î¹ G }

end continuous_multilinear_map

namespace continuous_linear_map

lemma norm_comp_continuous_multilinear_map_le (g : G â†’L[ğ•œ] G')
  (f : continuous_multilinear_map ğ•œ E G) :
  âˆ¥g.comp_continuous_multilinear_map fâˆ¥ â‰¤ âˆ¥gâˆ¥ * âˆ¥fâˆ¥ :=
continuous_multilinear_map.op_norm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) $ Î» m,
calc âˆ¥g (f m)âˆ¥ â‰¤ âˆ¥gâˆ¥ * (âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥) : g.le_op_norm_of_le $ f.le_op_norm _
           ... = _                        : (mul_assoc _ _ _).symm

/-- `continuous_linear_map.comp_continuous_multilinear_map` as a bundled continuous bilinear map. -/
def comp_continuous_multilinear_mapL :
  (G â†’L[ğ•œ] G') â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G' :=
linear_map.mk_continuousâ‚‚
  (linear_map.mkâ‚‚ ğ•œ comp_continuous_multilinear_map (Î» fâ‚ fâ‚‚ g, rfl) (Î» c f g, rfl)
    (Î» f gâ‚ gâ‚‚, by { ext1, apply f.map_add }) (Î» c f g, by { ext1, simp }))
  1 $ Î» f g, by { rw one_mul, exact f.norm_comp_continuous_multilinear_map_le g }

/-- Flip arguments in `f : G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G'` to get
`continuous_multilinear_map ğ•œ E (G â†’L[ğ•œ] G')` -/
def flip_multilinear (f : G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G') :
  continuous_multilinear_map ğ•œ E (G â†’L[ğ•œ] G') :=
multilinear_map.mk_continuous
  { to_fun := Î» m, linear_map.mk_continuous
      { to_fun := Î» x, f x m,
        map_add' := Î» x y, by simp only [map_add, continuous_multilinear_map.add_apply],
        map_smul' := Î» c x, by simp only [continuous_multilinear_map.smul_apply, map_smul]}
      (âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥) $ Î» x,
      by { rw mul_right_comm, exact (f x).le_of_op_norm_le _ (f.le_op_norm x) },
    map_add' := Î» m i x y,
      by { ext1, simp only [add_apply, continuous_multilinear_map.map_add, linear_map.coe_mk,
                            linear_map.mk_continuous_apply]},
    map_smul' := Î» m i c x,
      by { ext1, simp only [coe_smul', continuous_multilinear_map.map_smul, linear_map.coe_mk,
                            linear_map.mk_continuous_apply, pi.smul_apply]} }
  âˆ¥fâˆ¥ $ Î» m,
  linear_map.mk_continuous_norm_le _
    (mul_nonneg (norm_nonneg f) (prod_nonneg $ Î» i hi, norm_nonneg (m i))) _

end continuous_linear_map
open continuous_multilinear_map

namespace multilinear_map

/-- Given a map `f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G'` and an estimate
`H : âˆ€ x m, âˆ¥f x mâˆ¥ â‰¤ C * âˆ¥xâˆ¥ * âˆ i, âˆ¥m iâˆ¥`, construct a continuous linear
map from `G` to `continuous_multilinear_map ğ•œ E G'`.

In order to lift, e.g., a map `f : (multilinear_map ğ•œ E G) â†’â‚—[ğ•œ] multilinear_map ğ•œ E' G'`
to a map `(continuous_multilinear_map ğ•œ E G) â†’L[ğ•œ] continuous_multilinear_map ğ•œ E' G'`,
one can apply this construction to `f.comp continuous_multilinear_map.to_multilinear_map_linear`
which is a linear map from `continuous_multilinear_map ğ•œ E G` to `multilinear_map ğ•œ E' G'`. -/
def mk_continuous_linear (f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G') (C : â„)
  (H : âˆ€ x m, âˆ¥f x mâˆ¥ â‰¤ C * âˆ¥xâˆ¥ * âˆ i, âˆ¥m iâˆ¥) :
  G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G' :=
linear_map.mk_continuous
  { to_fun := Î» x, (f x).mk_continuous (C * âˆ¥xâˆ¥) $ H x,
    map_add' := Î» x y, by { ext1, simp },
    map_smul' := Î» c x, by { ext1, simp } }
  (max C 0) $ Î» x, ((f x).mk_continuous_norm_le' _).trans_eq $
    by rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]

lemma mk_continuous_linear_norm_le' (f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G') (C : â„)
  (H : âˆ€ x m, âˆ¥f x mâˆ¥ â‰¤ C * âˆ¥xâˆ¥ * âˆ i, âˆ¥m iâˆ¥) :
  âˆ¥mk_continuous_linear f C Hâˆ¥ â‰¤ max C 0 :=
begin
  dunfold mk_continuous_linear,
  exact linear_map.mk_continuous_norm_le _ (le_max_right _ _) _
end

lemma mk_continuous_linear_norm_le (f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G') {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ x m, âˆ¥f x mâˆ¥ â‰¤ C * âˆ¥xâˆ¥ * âˆ i, âˆ¥m iâˆ¥) :
  âˆ¥mk_continuous_linear f C Hâˆ¥ â‰¤ C :=
(mk_continuous_linear_norm_le' f C H).trans_eq (max_eq_left hC)

/-- Given a map `f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)` and an estimate
`H : âˆ€ m m', âˆ¥f m m'âˆ¥ â‰¤ C * âˆ i, âˆ¥m iâˆ¥ * âˆ i, âˆ¥m' iâˆ¥`, upgrade all `multilinear_map`s in the type to
`continuous_multilinear_map`s. -/
def mk_continuous_multilinear (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)) (C : â„)
  (H : âˆ€ mâ‚ mâ‚‚, âˆ¥f mâ‚ mâ‚‚âˆ¥ â‰¤ C * (âˆ i, âˆ¥mâ‚ iâˆ¥) * âˆ i, âˆ¥mâ‚‚ iâˆ¥) :
  continuous_multilinear_map ğ•œ E (continuous_multilinear_map ğ•œ E' G) :=
mk_continuous
  { to_fun := Î» m, mk_continuous (f m) (C * âˆ i, âˆ¥m iâˆ¥) $ H m,
    map_add' := Î» m i x y, by { ext1, simp },
    map_smul' := Î» m i c x, by { ext1, simp } }
  (max C 0) $ Î» m, ((f m).mk_continuous_norm_le' _).trans_eq $
    by { rw [max_mul_of_nonneg, zero_mul], exact prod_nonneg (Î» _ _, norm_nonneg _) }

@[simp] lemma mk_continuous_multilinear_apply (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G))
  {C : â„} (H : âˆ€ mâ‚ mâ‚‚, âˆ¥f mâ‚ mâ‚‚âˆ¥ â‰¤ C * (âˆ i, âˆ¥mâ‚ iâˆ¥) * âˆ i, âˆ¥mâ‚‚ iâˆ¥) (m : Î  i, E i) :
  â‡‘(mk_continuous_multilinear f C H m) = f m :=
rfl

lemma mk_continuous_multilinear_norm_le' (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)) (C : â„)
  (H : âˆ€ mâ‚ mâ‚‚, âˆ¥f mâ‚ mâ‚‚âˆ¥ â‰¤ C * (âˆ i, âˆ¥mâ‚ iâˆ¥) * âˆ i, âˆ¥mâ‚‚ iâˆ¥) :
  âˆ¥mk_continuous_multilinear f C Hâˆ¥ â‰¤ max C 0 :=
begin
  dunfold mk_continuous_multilinear,
  exact mk_continuous_norm_le _ (le_max_right _ _) _
end

lemma mk_continuous_multilinear_norm_le (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)) {C : â„}
  (hC : 0 â‰¤ C) (H : âˆ€ mâ‚ mâ‚‚, âˆ¥f mâ‚ mâ‚‚âˆ¥ â‰¤ C * (âˆ i, âˆ¥mâ‚ iâˆ¥) * âˆ i, âˆ¥mâ‚‚ iâˆ¥) :
  âˆ¥mk_continuous_multilinear f C Hâˆ¥ â‰¤ C :=
(mk_continuous_multilinear_norm_le' f C H).trans_eq (max_eq_left hC)

end multilinear_map

namespace continuous_multilinear_map

lemma norm_comp_continuous_linear_le (g : continuous_multilinear_map ğ•œ Eâ‚ G)
  (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  âˆ¥g.comp_continuous_linear_map fâˆ¥ â‰¤ âˆ¥gâˆ¥ * âˆ i, âˆ¥f iâˆ¥ :=
op_norm_le_bound _ (mul_nonneg (norm_nonneg _) $ prod_nonneg $ Î» i hi, norm_nonneg _) $ Î» m,
calc âˆ¥g (Î» i, f i (m i))âˆ¥ â‰¤ âˆ¥gâˆ¥ * âˆ i, âˆ¥f i (m i)âˆ¥ : g.le_op_norm _
... â‰¤ âˆ¥gâˆ¥ * âˆ i, (âˆ¥f iâˆ¥ * âˆ¥m iâˆ¥) :
  mul_le_mul_of_nonneg_left
    (prod_le_prod (Î» _ _, norm_nonneg _) (Î» i hi, (f i).le_op_norm (m i))) (norm_nonneg g)
... = (âˆ¥gâˆ¥ * âˆ i, âˆ¥f iâˆ¥) * âˆ i, âˆ¥m iâˆ¥ : by rw [prod_mul_distrib, mul_assoc]

/-- `continuous_multilinear_map.comp_continuous_linear_map` as a bundled continuous linear map.
This implementation fixes `f : Î  i, E i â†’L[ğ•œ] Eâ‚ i`.

TODO: Actually, the map is multilinear in `f` but an attempt to formalize this failed because of
issues with class instances. -/
def comp_continuous_linear_mapL (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  continuous_multilinear_map ğ•œ Eâ‚ G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G :=
linear_map.mk_continuous
  { to_fun := Î» g, g.comp_continuous_linear_map f,
    map_add' := Î» gâ‚ gâ‚‚, rfl,
    map_smul' := Î» c g, rfl }
  (âˆ i, âˆ¥f iâˆ¥) $ Î» g, (norm_comp_continuous_linear_le _ _).trans_eq (mul_comm _ _)

@[simp] lemma comp_continuous_linear_mapL_apply (g : continuous_multilinear_map ğ•œ Eâ‚ G)
  (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  comp_continuous_linear_mapL f g = g.comp_continuous_linear_map f :=
rfl

lemma norm_comp_continuous_linear_mapL_le (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  âˆ¥@comp_continuous_linear_mapL ğ•œ Î¹ E Eâ‚ G _ _ _ _ _ _ _ _ _ fâˆ¥ â‰¤ (âˆ i, âˆ¥f iâˆ¥) :=
linear_map.mk_continuous_norm_le _ (prod_nonneg $ Î» i _, norm_nonneg _) _

end continuous_multilinear_map

section currying
/-!
### Currying

We associate to a continuous multilinear map in `n+1` variables (i.e., based on `fin n.succ`) two
curried functions, named `f.curry_left` (which is a continuous linear map on `E 0` taking values
in continuous multilinear maps in `n` variables) and `f.curry_right` (which is a continuous
multilinear map in `n` variables taking values in continuous linear maps on `E (last n)`).
The inverse operations are called `uncurry_left` and `uncurry_right`.

We also register continuous linear equiv versions of these correspondences, in
`continuous_multilinear_curry_left_equiv` and `continuous_multilinear_curry_right_equiv`.
-/
open fin function

lemma continuous_linear_map.norm_map_tail_le
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) (m : Î i, Ei i) :
  âˆ¥f (m 0) (tail m)âˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥ :=
calc
  âˆ¥f (m 0) (tail m)âˆ¥ â‰¤ âˆ¥f (m 0)âˆ¥ * âˆ i, âˆ¥(tail m) iâˆ¥ : (f (m 0)).le_op_norm _
  ... â‰¤ (âˆ¥fâˆ¥ * âˆ¥m 0âˆ¥) * âˆ i, âˆ¥(tail m) iâˆ¥ :
    mul_le_mul_of_nonneg_right (f.le_op_norm _) (prod_nonneg (Î»i hi, norm_nonneg _))
  ... = âˆ¥fâˆ¥ * (âˆ¥m 0âˆ¥ * âˆ i, âˆ¥(tail m) iâˆ¥) : by ring
  ... = âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥ : by { rw prod_univ_succ, refl }

lemma continuous_multilinear_map.norm_map_init_le
  (f : continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G))
  (m : Î i, Ei i) :
  âˆ¥f (init m) (m (last n))âˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥ :=
calc
  âˆ¥f (init m) (m (last n))âˆ¥ â‰¤ âˆ¥f (init m)âˆ¥ * âˆ¥m (last n)âˆ¥ : (f (init m)).le_op_norm _
  ... â‰¤ (âˆ¥fâˆ¥ * (âˆ i, âˆ¥(init m) iâˆ¥)) * âˆ¥m (last n)âˆ¥ :
    mul_le_mul_of_nonneg_right (f.le_op_norm _) (norm_nonneg _)
  ... = âˆ¥fâˆ¥ * ((âˆ i, âˆ¥(init m) iâˆ¥) * âˆ¥m (last n)âˆ¥) : mul_assoc _ _ _
  ... = âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥ : by { rw prod_univ_cast_succ, refl }

lemma continuous_multilinear_map.norm_map_cons_le
  (f : continuous_multilinear_map ğ•œ Ei G) (x : Ei 0) (m : Î (i : fin n), Ei i.succ) :
  âˆ¥f (cons x m)âˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ * âˆ i, âˆ¥m iâˆ¥ :=
calc
  âˆ¥f (cons x m)âˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥cons x m iâˆ¥ : f.le_op_norm _
  ... = (âˆ¥fâˆ¥ * âˆ¥xâˆ¥) * âˆ i, âˆ¥m iâˆ¥ : by { rw prod_univ_succ, simp [mul_assoc] }

lemma continuous_multilinear_map.norm_map_snoc_le
  (f : continuous_multilinear_map ğ•œ Ei G) (m : Î (i : fin n), Ei i.cast_succ) (x : Ei (last n)) :
  âˆ¥f (snoc m x)âˆ¥ â‰¤ âˆ¥fâˆ¥ * (âˆ i, âˆ¥m iâˆ¥) * âˆ¥xâˆ¥ :=
calc
  âˆ¥f (snoc m x)âˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ i, âˆ¥snoc m x iâˆ¥ : f.le_op_norm _
  ... = âˆ¥fâˆ¥ * (âˆ i, âˆ¥m iâˆ¥) * âˆ¥xâˆ¥ : by { rw prod_univ_cast_succ, simp [mul_assoc] }

/-! #### Left currying -/

/-- Given a continuous linear map `f` from `E 0` to continuous multilinear maps on `n` variables,
construct the corresponding continuous multilinear map on `n+1` variables obtained by concatenating
the variables, given by `m â†¦ f (m 0) (tail m)`-/
def continuous_linear_map.uncurry_left
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) :
  continuous_multilinear_map ğ•œ Ei G :=
(@linear_map.uncurry_left ğ•œ n Ei G _ _ _ _ _
  (continuous_multilinear_map.to_multilinear_map_linear.comp f.to_linear_map)).mk_continuous
    (âˆ¥fâˆ¥) (Î»m, continuous_linear_map.norm_map_tail_le f m)

@[simp] lemma continuous_linear_map.uncurry_left_apply
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) (m : Î i, Ei i) :
  f.uncurry_left m = f (m 0) (tail m) := rfl

/-- Given a continuous multilinear map `f` in `n+1` variables, split the first variable to obtain
a continuous linear map into continuous multilinear maps in `n` variables, given by
`x â†¦ (m â†¦ f (cons x m))`. -/
def continuous_multilinear_map.curry_left
  (f : continuous_multilinear_map ğ•œ Ei G) :
  Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G) :=
linear_map.mk_continuous
{ -- define a linear map into `n` continuous multilinear maps from an `n+1` continuous multilinear
  -- map
  to_fun    := Î»x, (f.to_multilinear_map.curry_left x).mk_continuous
    (âˆ¥fâˆ¥ * âˆ¥xâˆ¥) (f.norm_map_cons_le x),
  map_add'  := Î»x y, by { ext m, exact f.cons_add m x y },
  map_smul' := Î»c x, by { ext m, exact f.cons_smul m c x } }
  -- then register its continuity thanks to its boundedness properties.
(âˆ¥fâˆ¥) (Î»x, multilinear_map.mk_continuous_norm_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _)

@[simp] lemma continuous_multilinear_map.curry_left_apply
  (f : continuous_multilinear_map ğ•œ Ei G) (x : Ei 0) (m : Î (i : fin n), Ei i.succ) :
  f.curry_left x m = f (cons x m) := rfl

@[simp] lemma continuous_linear_map.curry_uncurry_left
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) :
  f.uncurry_left.curry_left = f :=
begin
  ext m x,
  simp only [tail_cons, continuous_linear_map.uncurry_left_apply,
             continuous_multilinear_map.curry_left_apply],
  rw cons_zero
end

@[simp] lemma continuous_multilinear_map.uncurry_curry_left
  (f : continuous_multilinear_map ğ•œ Ei G) : f.curry_left.uncurry_left = f :=
continuous_multilinear_map.to_multilinear_map_inj $ f.to_multilinear_map.uncurry_curry_left

variables (ğ•œ Ei G)

/-- The space of continuous multilinear maps on `Î (i : fin (n+1)), E i` is canonically isomorphic to
the space of continuous linear maps from `E 0` to the space of continuous multilinear maps on
`Î (i : fin n), E i.succ `, by separating the first variable. We register this isomorphism in
`continuous_multilinear_curry_left_equiv ğ•œ E Eâ‚‚`. The algebraic version (without topology) is given
in `multilinear_curry_left_equiv ğ•œ E Eâ‚‚`.

The direct and inverse maps are given by `f.uncurry_left` and `f.curry_left`. Use these
unless you need the full framework of linear isometric equivs. -/
def continuous_multilinear_curry_left_equiv :
  (Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) â‰ƒâ‚—áµ¢[ğ•œ]
  (continuous_multilinear_map ğ•œ Ei G) :=
linear_isometry_equiv.of_bounds
  { to_fun    := continuous_linear_map.uncurry_left,
    map_add'  := Î»fâ‚ fâ‚‚, by { ext m, refl },
    map_smul' := Î»c f, by { ext m, refl },
    inv_fun   := continuous_multilinear_map.curry_left,
    left_inv  := continuous_linear_map.curry_uncurry_left,
    right_inv := continuous_multilinear_map.uncurry_curry_left }
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)
  (Î» f, linear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

variables {ğ•œ Ei G}

@[simp] lemma continuous_multilinear_curry_left_equiv_apply
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.succ) G)) (v : Î  i, Ei i) :
  continuous_multilinear_curry_left_equiv ğ•œ Ei G f v = f (v 0) (tail v) := rfl

@[simp] lemma continuous_multilinear_curry_left_equiv_symm_apply
  (f : continuous_multilinear_map ğ•œ Ei G) (x : Ei 0) (v : Î  i : fin n, Ei i.succ) :
  (continuous_multilinear_curry_left_equiv ğ•œ Ei G).symm f x v = f (cons x v) := rfl

@[simp] lemma continuous_multilinear_map.curry_left_norm
  (f : continuous_multilinear_map ğ•œ Ei G) : âˆ¥f.curry_leftâˆ¥ = âˆ¥fâˆ¥ :=
(continuous_multilinear_curry_left_equiv ğ•œ Ei G).symm.norm_map f

@[simp] lemma continuous_linear_map.uncurry_left_norm
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) :
  âˆ¥f.uncurry_leftâˆ¥ = âˆ¥fâˆ¥ :=
(continuous_multilinear_curry_left_equiv ğ•œ Ei G).norm_map f

/-! #### Right currying -/

/-- Given a continuous linear map `f` from continuous multilinear maps on `n` variables to
continuous linear maps on `E 0`, construct the corresponding continuous multilinear map on `n+1`
variables obtained by concatenating the variables, given by `m â†¦ f (init m) (m (last n))`. -/
def continuous_multilinear_map.uncurry_right
  (f : continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) :
  continuous_multilinear_map ğ•œ Ei G :=
let f' : multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’â‚—[ğ•œ] G) :=
{ to_fun    := Î» m, (f m).to_linear_map,
  map_add'  := Î» m i x y, by simp,
  map_smul' := Î» m i c x, by simp } in
(@multilinear_map.uncurry_right ğ•œ n Ei G _ _ _ _ _ f').mk_continuous
  (âˆ¥fâˆ¥) (Î»m, f.norm_map_init_le m)

@[simp] lemma continuous_multilinear_map.uncurry_right_apply
  (f : continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G))
  (m : Î i, Ei i) :
  f.uncurry_right m = f (init m) (m (last n)) := rfl

/-- Given a continuous multilinear map `f` in `n+1` variables, split the last variable to obtain
a continuous multilinear map in `n` variables into continuous linear maps, given by
`m â†¦ (x â†¦ f (snoc m x))`. -/
def continuous_multilinear_map.curry_right
  (f : continuous_multilinear_map ğ•œ Ei G) :
  continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G) :=
let f' : multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G) :=
{ to_fun    := Î»m, (f.to_multilinear_map.curry_right m).mk_continuous
    (âˆ¥fâˆ¥ * âˆ i, âˆ¥m iâˆ¥) $ Î»x, f.norm_map_snoc_le m x,
  map_add'  := Î» m i x y, by { simp, refl },
  map_smul' := Î» m i c x, by { simp, refl } } in
f'.mk_continuous (âˆ¥fâˆ¥) (Î»m, linear_map.mk_continuous_norm_le _
  (mul_nonneg (norm_nonneg _) (prod_nonneg (Î»j hj, norm_nonneg _))) _)

@[simp] lemma continuous_multilinear_map.curry_right_apply
  (f : continuous_multilinear_map ğ•œ Ei G) (m : Î  i : fin n, Ei i.cast_succ) (x : Ei (last n)) :
  f.curry_right m x = f (snoc m x) := rfl

@[simp] lemma continuous_multilinear_map.curry_uncurry_right
  (f : continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) :
  f.uncurry_right.curry_right = f :=
begin
  ext m x,
  simp only [snoc_last, continuous_multilinear_map.curry_right_apply,
             continuous_multilinear_map.uncurry_right_apply],
  rw init_snoc
end

@[simp] lemma continuous_multilinear_map.uncurry_curry_right
  (f : continuous_multilinear_map ğ•œ Ei G) : f.curry_right.uncurry_right = f :=
by { ext m, simp }

variables (ğ•œ Ei G)

/--
The space of continuous multilinear maps on `Î (i : fin (n+1)), Ei i` is canonically isomorphic to
the space of continuous multilinear maps on `Î (i : fin n), Ei i.cast_succ` with values in the space
of continuous linear maps on `Ei (last n)`, by separating the last variable. We register this
isomorphism as a continuous linear equiv in `continuous_multilinear_curry_right_equiv ğ•œ Ei G`.
The algebraic version (without topology) is given in `multilinear_curry_right_equiv ğ•œ Ei G`.

The direct and inverse maps are given by `f.uncurry_right` and `f.curry_right`. Use these
unless you need the full framework of linear isometric equivs.
-/
def continuous_multilinear_curry_right_equiv :
  (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) â‰ƒâ‚—áµ¢[ğ•œ]
  (continuous_multilinear_map ğ•œ Ei G) :=
linear_isometry_equiv.of_bounds
  { to_fun    := continuous_multilinear_map.uncurry_right,
    map_add'  := Î»fâ‚ fâ‚‚, by { ext m, refl },
    map_smul' := Î»c f, by { ext m, refl },
    inv_fun   := continuous_multilinear_map.curry_right,
    left_inv  := continuous_multilinear_map.curry_uncurry_right,
    right_inv := continuous_multilinear_map.uncurry_curry_right }
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

variables (n G')

/-- The space of continuous multilinear maps on `Î (i : fin (n+1)), G` is canonically isomorphic to
the space of continuous multilinear maps on `Î (i : fin n), G` with values in the space
of continuous linear maps on `G`, by separating the last variable. We register this
isomorphism as a continuous linear equiv in `continuous_multilinear_curry_right_equiv' ğ•œ n G G'`.
For a version allowing dependent types, see `continuous_multilinear_curry_right_equiv`. When there
are no dependent types, use the primed version as it helps Lean a lot for unification.

The direct and inverse maps are given by `f.uncurry_right` and `f.curry_right`. Use these
unless you need the full framework of linear isometric equivs. -/
def continuous_multilinear_curry_right_equiv' :
  (G [Ã—n]â†’L[ğ•œ] (G â†’L[ğ•œ] G')) â‰ƒâ‚—áµ¢[ğ•œ] (G [Ã—n.succ]â†’L[ğ•œ] G') :=
continuous_multilinear_curry_right_equiv ğ•œ (Î» (i : fin n.succ), G) G'

variables {n ğ•œ G Ei G'}

@[simp] lemma continuous_multilinear_curry_right_equiv_apply
  (f : (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)))
  (v : Î  i, Ei i) :
  (continuous_multilinear_curry_right_equiv ğ•œ Ei G) f v = f (init v) (v (last n)) := rfl

@[simp] lemma continuous_multilinear_curry_right_equiv_symm_apply
  (f : continuous_multilinear_map ğ•œ Ei G)
  (v : Î  (i : fin n), Ei i.cast_succ) (x : Ei (last n)) :
  (continuous_multilinear_curry_right_equiv ğ•œ Ei G).symm f v x = f (snoc v x) := rfl

@[simp] lemma continuous_multilinear_curry_right_equiv_apply'
  (f : G [Ã—n]â†’L[ğ•œ] (G â†’L[ğ•œ] G')) (v : Î  (i : fin n.succ), G) :
  continuous_multilinear_curry_right_equiv' ğ•œ n G G' f v = f (init v) (v (last n)) := rfl

@[simp] lemma continuous_multilinear_curry_right_equiv_symm_apply'
  (f : G [Ã—n.succ]â†’L[ğ•œ] G') (v : Î  (i : fin n), G) (x : G) :
  (continuous_multilinear_curry_right_equiv' ğ•œ n G G').symm f v x = f (snoc v x) := rfl

@[simp] lemma continuous_multilinear_map.curry_right_norm
  (f : continuous_multilinear_map ğ•œ Ei G) : âˆ¥f.curry_rightâˆ¥ = âˆ¥fâˆ¥ :=
(continuous_multilinear_curry_right_equiv ğ•œ Ei G).symm.norm_map f

@[simp] lemma continuous_multilinear_map.uncurry_right_norm
  (f : continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) :
  âˆ¥f.uncurry_rightâˆ¥ = âˆ¥fâˆ¥ :=
(continuous_multilinear_curry_right_equiv ğ•œ Ei G).norm_map f

/-!
#### Currying with `0` variables

The space of multilinear maps with `0` variables is trivial: such a multilinear map is just an
arbitrary constant (note that multilinear maps in `0` variables need not map `0` to `0`!).
Therefore, the space of continuous multilinear maps on `(fin 0) â†’ G` with values in `Eâ‚‚` is
isomorphic (and even isometric) to `Eâ‚‚`. As this is the zeroth step in the construction of iterated
derivatives, we register this isomorphism. -/

section
local attribute [instance] unique.subsingleton

variables {ğ•œ G G'}

/-- Associating to a continuous multilinear map in `0` variables the unique value it takes. -/
def continuous_multilinear_map.uncurry0
  (f : continuous_multilinear_map ğ•œ (Î» (i : fin 0), G) G') : G' := f 0

variables (ğ•œ G)
/-- Associating to an element `x` of a vector space `Eâ‚‚` the continuous multilinear map in `0`
variables taking the (unique) value `x` -/
def continuous_multilinear_map.curry0 (x : G') : G [Ã—0]â†’L[ğ•œ] G' :=
{ to_fun    := Î»m, x,
  map_add'  := Î» m i, fin.elim0 i,
  map_smul' := Î» m i, fin.elim0 i,
  cont      := continuous_const }

variable {G}
@[simp] lemma continuous_multilinear_map.curry0_apply (x : G') (m : (fin 0) â†’ G) :
  continuous_multilinear_map.curry0 ğ•œ G x m = x := rfl

variable {ğ•œ}
@[simp] lemma continuous_multilinear_map.uncurry0_apply (f : G [Ã—0]â†’L[ğ•œ] G') :
  f.uncurry0 = f 0 := rfl

@[simp] lemma continuous_multilinear_map.apply_zero_curry0 (f : G [Ã—0]â†’L[ğ•œ] G') {x : fin 0 â†’ G} :
  continuous_multilinear_map.curry0 ğ•œ G (f x) = f :=
by { ext m, simp [(subsingleton.elim _ _ : x = m)] }

lemma continuous_multilinear_map.uncurry0_curry0 (f : G [Ã—0]â†’L[ğ•œ] G') :
  continuous_multilinear_map.curry0 ğ•œ G (f.uncurry0) = f :=
by simp

variables (ğ•œ G)
@[simp] lemma continuous_multilinear_map.curry0_uncurry0 (x : G') :
  (continuous_multilinear_map.curry0 ğ•œ G x).uncurry0 = x := rfl

@[simp] lemma continuous_multilinear_map.curry0_norm (x : G')  :
  âˆ¥continuous_multilinear_map.curry0 ğ•œ G xâˆ¥ = âˆ¥xâˆ¥ :=
begin
  apply le_antisymm,
  { exact continuous_multilinear_map.op_norm_le_bound _ (norm_nonneg _) (Î»m, by simp) },
  { simpa using (continuous_multilinear_map.curry0 ğ•œ G x).le_op_norm 0 }
end

variables {ğ•œ G}
@[simp] lemma continuous_multilinear_map.fin0_apply_norm (f : G [Ã—0]â†’L[ğ•œ] G') {x : fin 0 â†’ G} :
  âˆ¥f xâˆ¥ = âˆ¥fâˆ¥ :=
begin
  have : x = 0 := subsingleton.elim _ _, subst this,
  refine le_antisymm (by simpa using f.le_op_norm 0) _,
  have : âˆ¥continuous_multilinear_map.curry0 ğ•œ G (f.uncurry0)âˆ¥ â‰¤ âˆ¥f.uncurry0âˆ¥ :=
    continuous_multilinear_map.op_norm_le_bound _ (norm_nonneg _) (Î»m,
      by simp [-continuous_multilinear_map.apply_zero_curry0]),
  simpa
end

lemma continuous_multilinear_map.uncurry0_norm (f : G [Ã—0]â†’L[ğ•œ] G') : âˆ¥f.uncurry0âˆ¥ = âˆ¥fâˆ¥ :=
by simp

variables (ğ•œ G G')
/-- The continuous linear isomorphism between elements of a normed space, and continuous multilinear
maps in `0` variables with values in this normed space.

The direct and inverse maps are `uncurry0` and `curry0`. Use these unless you need the full
framework of linear isometric equivs. -/
def continuous_multilinear_curry_fin0 : (G [Ã—0]â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ] G' :=
{ to_fun    := Î»f, continuous_multilinear_map.uncurry0 f,
  inv_fun   := Î»f, continuous_multilinear_map.curry0 ğ•œ G f,
  map_add'  := Î»f g, rfl,
  map_smul' := Î»c f, rfl,
  left_inv  := continuous_multilinear_map.uncurry0_curry0,
  right_inv := continuous_multilinear_map.curry0_uncurry0 ğ•œ G,
  norm_map' := continuous_multilinear_map.uncurry0_norm }

variables {ğ•œ G G'}

@[simp] lemma continuous_multilinear_curry_fin0_apply (f : G [Ã—0]â†’L[ğ•œ] G') :
  continuous_multilinear_curry_fin0 ğ•œ G G' f = f 0 := rfl

@[simp] lemma continuous_multilinear_curry_fin0_symm_apply (x : G') (v : (fin 0) â†’ G) :
  (continuous_multilinear_curry_fin0 ğ•œ G G').symm x v = x := rfl

end

/-! #### With 1 variable -/

variables (ğ•œ G G')

/-- Continuous multilinear maps from `G^1` to `G'` are isomorphic with continuous linear maps from
`G` to `G'`. -/
def continuous_multilinear_curry_fin1 : (G [Ã—1]â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ] (G â†’L[ğ•œ] G') :=
(continuous_multilinear_curry_right_equiv ğ•œ (Î» (i : fin 1), G) G').symm.trans
(continuous_multilinear_curry_fin0 ğ•œ G (G â†’L[ğ•œ] G'))

variables {ğ•œ G G'}

@[simp] lemma continuous_multilinear_curry_fin1_apply (f : G [Ã—1]â†’L[ğ•œ] G') (x : G) :
  continuous_multilinear_curry_fin1 ğ•œ G G' f x = f (fin.snoc 0 x) := rfl

@[simp] lemma continuous_multilinear_curry_fin1_symm_apply
  (f : G â†’L[ğ•œ] G') (v : (fin 1) â†’ G) :
  (continuous_multilinear_curry_fin1 ğ•œ G G').symm f v = f (v 0) := rfl

namespace continuous_multilinear_map

variables (ğ•œ G G')

/-- An equivalence of the index set defines a linear isometric equivalence between the spaces
of multilinear maps. -/
def dom_dom_congr (Ïƒ : Î¹ â‰ƒ Î¹') :
  continuous_multilinear_map ğ•œ (Î» _ : Î¹, G) G' â‰ƒâ‚—áµ¢[ğ•œ]
    continuous_multilinear_map ğ•œ (Î» _ : Î¹', G) G' :=
linear_isometry_equiv.of_bounds
  { to_fun := Î» f, (multilinear_map.dom_dom_congr Ïƒ f.to_multilinear_map).mk_continuous âˆ¥fâˆ¥ $
      Î» m, (f.le_op_norm (Î» i, m (Ïƒ i))).trans_eq $ by rw [â† Ïƒ.prod_comp],
    inv_fun := Î» f, (multilinear_map.dom_dom_congr Ïƒ.symm f.to_multilinear_map).mk_continuous âˆ¥fâˆ¥ $
      Î» m, (f.le_op_norm (Î» i, m (Ïƒ.symm i))).trans_eq $ by rw [â† Ïƒ.symm.prod_comp],
    left_inv := Î» f, ext $ Î» m, congr_arg f $ by simp only [Ïƒ.symm_apply_apply],
    right_inv := Î» f, ext $ Î» m, congr_arg f $ by simp only [Ïƒ.apply_symm_apply],
    map_add' := Î» f g, rfl,
    map_smul' := Î» c f, rfl }
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

variables {ğ•œ G G'}

section

variable [decidable_eq (Î¹ âŠ• Î¹')]

/-- A continuous multilinear map with variables indexed by `Î¹ âŠ• Î¹'` defines a continuous multilinear
map with variables indexed by `Î¹` taking values in the space of continuous multilinear maps with
variables indexed by `Î¹'`. -/
def curry_sum (f : continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G') :
  continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G') :=
multilinear_map.mk_continuous_multilinear (multilinear_map.curry_sum f.to_multilinear_map) (âˆ¥fâˆ¥) $
  Î» m m', by simpa [fintype.prod_sum_type, mul_assoc] using f.le_op_norm (sum.elim m m')

@[simp] lemma curry_sum_apply (f : continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G')
  (m : Î¹ â†’ G) (m' : Î¹' â†’ G) :
  f.curry_sum m m' = f (sum.elim m m') :=
rfl

/-- A continuous multilinear map with variables indexed by `Î¹` taking values in the space of
continuous multilinear maps with variables indexed by `Î¹'` defines a continuous multilinear map with
variables indexed by `Î¹ âŠ• Î¹'`. -/
def uncurry_sum
  (f : continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G')) :
  continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G' :=
multilinear_map.mk_continuous
  (to_multilinear_map_linear.comp_multilinear_map f.to_multilinear_map).uncurry_sum (âˆ¥fâˆ¥) $ Î» m,
  by simpa [fintype.prod_sum_type, mul_assoc]
    using (f (m âˆ˜ sum.inl)).le_of_op_norm_le (m âˆ˜ sum.inr) (f.le_op_norm _)

@[simp] lemma uncurry_sum_apply
  (f : continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G'))
  (m : Î¹ âŠ• Î¹' â†’ G) :
  f.uncurry_sum m = f (m âˆ˜ sum.inl) (m âˆ˜ sum.inr) :=
rfl

variables (ğ•œ Î¹ Î¹' G G')

/-- Linear isometric equivalence between the space of continuous multilinear maps with variables
indexed by `Î¹ âŠ• Î¹'` and the space of continuous multilinear maps with variables indexed by `Î¹`
taking values in the space of continuous multilinear maps with variables indexed by `Î¹'`.

The forward and inverse functions are `continuous_multilinear_map.curry_sum`
and `continuous_multilinear_map.uncurry_sum`. Use this definition only if you need
some properties of `linear_isometry_equiv`. -/
def curry_sum_equiv : continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G' â‰ƒâ‚—áµ¢[ğ•œ]
  continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G') :=
linear_isometry_equiv.of_bounds
  { to_fun := curry_sum,
    inv_fun := uncurry_sum,
    map_add' := Î» f g, by { ext, refl },
    map_smul' := Î» c f, by { ext, refl },
    left_inv := Î» f, by { ext m, exact congr_arg f (sum.elim_comp_inl_inr m) },
    right_inv := Î» f, by { ext mâ‚ mâ‚‚, change f _ _ = f _ _,
      rw [sum.elim_comp_inl, sum.elim_comp_inr] } }
  (Î» f, multilinear_map.mk_continuous_multilinear_norm_le _ (norm_nonneg f) _)
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

end

section

variables (ğ•œ G G') {k l : â„•} {s : finset (fin n)}

/-- If `s : finset (fin n)` is a finite set of cardinality `k` and its complement has cardinality
`l`, then the space of continuous multilinear maps `G [Ã—n]â†’L[ğ•œ] G'` of `n` variables is isomorphic
to the space of continuous multilinear maps `G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G'` of `k` variables taking
values in the space of continuous multilinear maps of `l` variables. -/
def curry_fin_finset {k l n : â„•} {s : finset (fin n)}
  (hk : s.card = k) (hl : sá¶œ.card = l) :
  (G [Ã—n]â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ] (G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') :=
(dom_dom_congr ğ•œ G G' (fin_sum_equiv_of_finset hk hl).symm).trans
  (curry_sum_equiv ğ•œ (fin k) (fin l) G G')

variables {ğ•œ G G'}

@[simp] lemma curry_fin_finset_apply (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—n]â†’L[ğ•œ] G') (mk : fin k â†’ G) (ml : fin l â†’ G) :
  curry_fin_finset ğ•œ G G' hk hl f mk ml =
    f (Î» i, sum.elim mk ml ((fin_sum_equiv_of_finset hk hl).symm i)) :=
rfl

@[simp] lemma curry_fin_finset_symm_apply (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') (m : fin n â†’ G) :
  (curry_fin_finset ğ•œ G G' hk hl).symm f m =
    f (Î» i, m $ fin_sum_equiv_of_finset hk hl (sum.inl i))
      (Î» i, m $ fin_sum_equiv_of_finset hk hl (sum.inr i)) :=
rfl

@[simp] lemma curry_fin_finset_symm_apply_piecewise_const (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') (x y : G) :
  (curry_fin_finset ğ•œ G G' hk hl).symm f (s.piecewise (Î» _, x) (Î» _, y)) = f (Î» _, x) (Î» _, y) :=
multilinear_map.curry_fin_finset_symm_apply_piecewise_const hk hl _ x y

@[simp] lemma curry_fin_finset_symm_apply_const (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') (x : G) :
  (curry_fin_finset ğ•œ G G' hk hl).symm f (Î» _, x) = f (Î» _, x) (Î» _, x) :=
rfl

@[simp] lemma curry_fin_finset_apply_const (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—n]â†’L[ğ•œ] G') (x y : G) :
  curry_fin_finset ğ•œ G G' hk hl f (Î» _, x) (Î» _, y) = f (s.piecewise (Î» _, x) (Î» _, y)) :=
begin
  refine (curry_fin_finset_symm_apply_piecewise_const hk hl _ _ _).symm.trans _, -- `rw` fails
  rw linear_isometry_equiv.symm_apply_apply
end

end

end continuous_multilinear_map

end currying
