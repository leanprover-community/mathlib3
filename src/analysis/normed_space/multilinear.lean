/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.normed_space.operator_norm
import topology.algebra.module.multilinear

/-!
# Operator norm on the space of continuous multilinear maps

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

When `f` is a continuous multilinear map in finitely many variables, we define its norm `â€–fâ€–` as the
smallest number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for all `m`.

We show that it is indeed a norm, and prove its basic properties.

## Main results

Let `f` be a multilinear map in finitely many variables.
* `exists_bound_of_continuous` asserts that, if `f` is continuous, then there exists `C > 0`
  with `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` for all `m`.
* `continuous_of_bound`, conversely, asserts that this bound implies continuity.
* `mk_continuous` constructs the associated continuous multilinear map.

Let `f` be a continuous multilinear map in finitely many variables.
* `â€–fâ€–` is its norm, i.e., the smallest number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for
  all `m`.
* `le_op_norm f m` asserts the fundamental inequality `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–`.
* `norm_image_sub_le f mâ‚ mâ‚‚` gives a control of the difference `f mâ‚ - f mâ‚‚` in terms of
  `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`.

We also register isomorphisms corresponding to currying or uncurrying variables, transforming a
continuous multilinear function `f` in `n+1` variables into a continuous linear function taking
values in continuous multilinear functions in `n` variables, and also into a continuous multilinear
function in `n` variables taking values in continuous linear functions. These operations are called
`f.curry_left` and `f.curry_right` respectively (with inverses `f.uncurry_left` and
`f.uncurry_right`). They induce continuous linear equivalences between spaces of
continuous multilinear functions in `n+1` variables and spaces of continuous linear functions into
continuous multilinear functions in `n` variables (resp. continuous multilinear functions in `n`
variables taking values in continuous linear functions), called respectively
`continuous_multilinear_curry_left_equiv` and `continuous_multilinear_curry_right_equiv`.

## Implementation notes

We mostly follow the API (and the proofs) of `operator_norm.lean`, with the additional complexity
that we should deal with multilinear maps in several variables. The currying/uncurrying
constructions are based on those in `multilinear.lean`.

From the mathematical point of view, all the results follow from the results on operator norm in
one variable, by applying them to one variable after the other through currying. However, this
is only well defined when there is an order on the variables (for instance on `fin n`) although
the final result is independent of the order. While everything could be done following this
approach, it turns out that direct proofs are easier and more efficient.
-/

noncomputable theory
open_locale big_operators nnreal
open finset metric

local attribute [instance, priority 1001]
add_comm_group.to_add_comm_monoid normed_add_comm_group.to_add_comm_group normed_space.to_module'

/-!
### Type variables

We use the following type variables in this file:

* `ğ•œ` : a `nontrivially_normed_field`;
* `Î¹`, `Î¹'` : finite index types with decidable equality;
* `E`, `Eâ‚` : families of normed vector spaces over `ğ•œ` indexed by `i : Î¹`;
* `E'` : a family of normed vector spaces over `ğ•œ` indexed by `i' : Î¹'`;
* `Ei` : a family of normed vector spaces over `ğ•œ` indexed by `i : fin (nat.succ n)`;
* `G`, `G'` : normed vector spaces over `ğ•œ`.
-/

universes u v v' wE wEâ‚ wE' wEi wG wG'
variables {ğ•œ : Type u} {Î¹ : Type v} {Î¹' : Type v'} {n : â„•}
  {E : Î¹ â†’ Type wE} {Eâ‚ : Î¹ â†’ Type wEâ‚} {E' : Î¹' â†’ Type wE'} {Ei : fin n.succ â†’ Type wEi}
  {G : Type wG} {G' : Type wG'}
  [fintype Î¹] [fintype Î¹'] [nontrivially_normed_field ğ•œ]
  [Î  i, normed_add_comm_group (E i)] [Î  i, normed_space ğ•œ (E i)]
  [Î  i, normed_add_comm_group (Eâ‚ i)] [Î  i, normed_space ğ•œ (Eâ‚ i)]
  [Î  i, normed_add_comm_group (E' i)] [Î  i, normed_space ğ•œ (E' i)]
  [Î  i, normed_add_comm_group (Ei i)] [Î  i, normed_space ğ•œ (Ei i)]
  [normed_add_comm_group G] [normed_space ğ•œ G] [normed_add_comm_group G'] [normed_space ğ•œ G']

/-!
### Continuity properties of multilinear maps

We relate continuity of multilinear maps to the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, in
both directions. Along the way, we prove useful bounds on the difference `â€–f mâ‚ - f mâ‚‚â€–`.
-/
namespace multilinear_map

variable (f : multilinear_map ğ•œ E G)

/-- If a multilinear map in finitely many variables on normed spaces satisfies the inequality
`â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` on a shell `Îµ i / â€–c iâ€– < â€–m iâ€– < Îµ i` for some positive numbers `Îµ i`
and elements `c i : ğ•œ`, `1 < â€–c iâ€–`, then it satisfies this inequality for all `m`. -/
lemma bound_of_shell {Îµ : Î¹ â†’ â„} {C : â„} (hÎµ : âˆ€ i, 0 < Îµ i) {c : Î¹ â†’ ğ•œ} (hc : âˆ€ i, 1 < â€–c iâ€–)
  (hf : âˆ€ m : Î  i, E i, (âˆ€ i, Îµ i / â€–c iâ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ i) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
  (m : Î  i, E i) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
begin
  rcases em (âˆƒ i, m i = 0) with âŸ¨i, hiâŸ©|hm; [skip, push_neg at hm],
  { simp [f.map_coord_zero i hi, prod_eq_zero (mem_univ i), hi] },
  choose Î´ hÎ´0 hÎ´m_lt hle_Î´m hÎ´inv using Î» i, rescale_to_shell (hc i) (hÎµ i) (hm i),
  have hÎ´0 : 0 < âˆ i, â€–Î´ iâ€–, from prod_pos (Î» i _, norm_pos_iff.2 (hÎ´0 i)),
  simpa [map_smul_univ, norm_smul, prod_mul_distrib, mul_left_comm C, mul_le_mul_left hÎ´0]
    using hf (Î» i, Î´ i â€¢ m i) hle_Î´m hÎ´m_lt,
end

/-- If a multilinear map in finitely many variables on normed spaces is continuous, then it
satisfies the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, for some `C` which can be chosen to be
positive. -/
theorem exists_bound_of_continuous (hf : continuous f) :
  âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :=
begin
  casesI is_empty_or_nonempty Î¹,
  { refine âŸ¨â€–f 0â€– + 1, add_pos_of_nonneg_of_pos (norm_nonneg _) zero_lt_one, Î» m, _âŸ©,
    obtain rfl : m = 0, from funext (is_empty.elim â€¹_â€º),
    simp [univ_eq_empty, zero_le_one] },
  obtain âŸ¨Îµ : â„, Îµ0 : 0 < Îµ, hÎµ : âˆ€ m : Î  i, E i, â€–m - 0â€– < Îµ â†’ â€–f m - f 0â€– < 1âŸ© :=
    normed_add_comm_group.tendsto_nhds_nhds.1 (hf.tendsto 0) 1 zero_lt_one,
  simp only [sub_zero, f.map_zero] at hÎµ,
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  have : 0 < (â€–câ€– / Îµ) ^ fintype.card Î¹, from pow_pos (div_pos (zero_lt_one.trans hc) Îµ0) _,
  refine âŸ¨_, this, _âŸ©,
  refine f.bound_of_shell (Î» _, Îµ0) (Î» _, hc) (Î» m hcm hm, _),
  refine (hÎµ m ((pi_norm_lt_iff Îµ0).2 hm)).le.trans _,
  rw [â† div_le_iff' this, one_div, â† inv_pow, inv_div, fintype.card, â† prod_const],
  exact prod_le_prod (Î» _ _, div_nonneg Îµ0.le (norm_nonneg _)) (Î» i _, hcm i)
end

/-- If `f` satisfies a boundedness property around `0`, one can deduce a bound on `f mâ‚ - f mâ‚‚`
using the multilinearity. Here, we give a precise but hard to use version. See
`norm_image_sub_le_of_bound` for a less precise but more usable version. The bound reads
`â€–f m - f m'â€– â‰¤
  C * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`. -/
lemma norm_image_sub_le_of_bound' [decidable_eq Î¹] {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (mâ‚ mâ‚‚ : Î i, E i) :
  â€–f mâ‚ - f mâ‚‚â€– â‰¤
  C * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
begin
  have A : âˆ€(s : finset Î¹), â€–f mâ‚ - f (s.piecewise mâ‚‚ mâ‚)â€–
    â‰¤ C * âˆ‘ i in s, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–,
  { refine finset.induction (by simp) _,
    assume i s his Hrec,
    have I : â€–f (s.piecewise mâ‚‚ mâ‚) - f ((insert i s).piecewise mâ‚‚ mâ‚)â€–
      â‰¤ C * âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–,
    { have A : ((insert i s).piecewise mâ‚‚ mâ‚)
            = function.update (s.piecewise mâ‚‚ mâ‚) i (mâ‚‚ i) := s.piecewise_insert _ _ _,
      have B : s.piecewise mâ‚‚ mâ‚ = function.update (s.piecewise mâ‚‚ mâ‚) i (mâ‚ i),
      { ext j,
        by_cases h : j = i,
        { rw h, simp [his] },
        { simp [h] } },
      rw [B, A, â† f.map_sub],
      apply le_trans (H _) (mul_le_mul_of_nonneg_left _ hC),
      refine prod_le_prod (Î»j hj, norm_nonneg _) (Î»j hj, _),
      by_cases h : j = i,
      { rw h, simp },
      { by_cases h' : j âˆˆ s;
        simp [h', h, le_refl] } },
    calc â€–f mâ‚ - f ((insert i s).piecewise mâ‚‚ mâ‚)â€– â‰¤
      â€–f mâ‚ - f (s.piecewise mâ‚‚ mâ‚)â€– + â€–f (s.piecewise mâ‚‚ mâ‚) - f ((insert i s).piecewise mâ‚‚ mâ‚)â€– :
        by { rw [â† dist_eq_norm, â† dist_eq_norm, â† dist_eq_norm], exact dist_triangle _ _ _ }
      ... â‰¤ (C * âˆ‘ i in s, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–)
            + C * âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :
        add_le_add Hrec I
      ... = C * âˆ‘ i in insert i s, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :
        by simp [his, add_comm, left_distrib] },
  convert A univ,
  simp
end

/-- If `f` satisfies a boundedness property around `0`, one can deduce a bound on `f mâ‚ - f mâ‚‚`
using the multilinearity. Here, we give a usable but not very precise version. See
`norm_image_sub_le_of_bound'` for a more precise but less usable version. The bound is
`â€–f m - f m'â€– â‰¤ C * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`. -/
lemma norm_image_sub_le_of_bound {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (mâ‚ mâ‚‚ : Î i, E i) :
  â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * (fintype.card Î¹) * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :=
begin
  letI := classical.dec_eq Î¹,
  have A : âˆ€ (i : Î¹), âˆ j, (if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–)
    â‰¤ â€–mâ‚ - mâ‚‚â€– * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (fintype.card Î¹ - 1),
  { assume i,
    calc âˆ j, (if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–)
    â‰¤ âˆ j : Î¹, function.update (Î» j, max â€–mâ‚â€– â€–mâ‚‚â€–) i (â€–mâ‚ - mâ‚‚â€–) j :
      begin
        apply prod_le_prod,
        { assume j hj, by_cases h : j = i; simp [h, norm_nonneg] },
        { assume j hj,
          by_cases h : j = i,
          { rw h, simp, exact norm_le_pi_norm (mâ‚ - mâ‚‚) i },
          { simp [h, max_le_max, norm_le_pi_norm (_ : Î  i, E i)] } }
      end
    ... = â€–mâ‚ - mâ‚‚â€– * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (fintype.card Î¹ - 1) :
      by { rw prod_update_of_mem (finset.mem_univ _), simp [card_univ_diff] } },
  calc
  â€–f mâ‚ - f mâ‚‚â€–
  â‰¤ C * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :
    f.norm_image_sub_le_of_bound' hC H mâ‚ mâ‚‚
  ... â‰¤ C * âˆ‘ i, â€–mâ‚ - mâ‚‚â€– * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (fintype.card Î¹ - 1) :
    mul_le_mul_of_nonneg_left (sum_le_sum (Î»i hi, A i)) hC
  ... = C * (fintype.card Î¹) * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :
    by { rw [sum_const, card_univ, nsmul_eq_mul], ring }
end

/-- If a multilinear map satisfies an inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, then it is
continuous. -/
theorem continuous_of_bound (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :
  continuous f :=
begin
  let D := max C 1,
  have D_pos : 0 â‰¤ D := le_trans zero_le_one (le_max_right _ _),
  replace H : âˆ€ m, â€–f mâ€– â‰¤ D * âˆ i, â€–m iâ€–,
  { assume m,
    apply le_trans (H m) (mul_le_mul_of_nonneg_right (le_max_left _ _) _),
    exact prod_nonneg (Î»(i : Î¹) hi, norm_nonneg (m i)) },
  refine continuous_iff_continuous_at.2 (Î»m, _),
  refine continuous_at_of_locally_lipschitz zero_lt_one
    (D * (fintype.card Î¹) * (â€–mâ€– + 1) ^ (fintype.card Î¹ - 1)) (Î»m' h', _),
  rw [dist_eq_norm, dist_eq_norm],
  have : 0 â‰¤ (max â€–m'â€– â€–mâ€–), by simp,
  have : (max â€–m'â€– â€–mâ€–) â‰¤ â€–mâ€– + 1,
    by simp [zero_le_one, norm_le_of_mem_closed_ball (le_of_lt h'), -add_comm],
  calc
    â€–f m' - f mâ€–
    â‰¤ D * (fintype.card Î¹) * (max â€–m'â€– â€–mâ€–) ^ (fintype.card Î¹ - 1) * â€–m' - mâ€– :
      f.norm_image_sub_le_of_bound D_pos H m' m
    ... â‰¤ D * (fintype.card Î¹) * (â€–mâ€– + 1) ^ (fintype.card Î¹ - 1) * â€–m' - mâ€– :
      by apply_rules [mul_le_mul_of_nonneg_right, mul_le_mul_of_nonneg_left, mul_nonneg,
        norm_nonneg, nat.cast_nonneg, pow_le_pow_of_le_left]
end

/-- Constructing a continuous multilinear map from a multilinear map satisfying a boundedness
condition. -/
def mk_continuous (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :
  continuous_multilinear_map ğ•œ E G :=
{ cont := f.continuous_of_bound C H, ..f }

@[simp] lemma coe_mk_continuous (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :
  â‡‘(f.mk_continuous C H) = f :=
rfl

/-- Given a multilinear map in `n` variables, if one restricts it to `k` variables putting `z` on
the other coordinates, then the resulting restricted function satisfies an inequality
`â€–f.restr vâ€– â‰¤ C * â€–zâ€–^(n-k) * Î  â€–v iâ€–` if the original function satisfies `â€–f vâ€– â‰¤ C * Î  â€–v iâ€–`. -/
lemma restr_norm_le {k n : â„•} (f : (multilinear_map ğ•œ (Î» i : fin n, G) G' : _))
  (s : finset (fin n)) (hk : s.card = k) (z : G) {C : â„}
  (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (v : fin k â†’ G) :
  â€–f.restr s hk z vâ€– â‰¤ C * â€–zâ€– ^ (n - k) * âˆ i, â€–v iâ€– :=
begin
  rw [mul_right_comm, mul_assoc],
  convert H _ using 2,
  simp only [apply_dite norm, fintype.prod_dite, prod_const (â€–zâ€–), finset.card_univ,
    fintype.card_of_subtype sá¶œ (Î» x, mem_compl), card_compl, fintype.card_fin, hk, mk_coe,
    â† (s.order_iso_of_fin hk).symm.bijective.prod_comp (Î» x, â€–v xâ€–)],
  refl
end

end multilinear_map

/-!
### Continuous multilinear maps

We define the norm `â€–fâ€–` of a continuous multilinear map `f` in finitely many variables as the
smallest number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for all `m`. We show that this
defines a normed space structure on `continuous_multilinear_map ğ•œ E G`.
-/
namespace continuous_multilinear_map

variables (c : ğ•œ) (f g : continuous_multilinear_map ğ•œ E G) (m : Î i, E i)

theorem bound : âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :=
f.to_multilinear_map.exists_bound_of_continuous f.2

open real

/-- The operator norm of a continuous multilinear map is the inf of all its bounds. -/
def op_norm := Inf {c | 0 â‰¤ (c : â„) âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–}
instance has_op_norm : has_norm (continuous_multilinear_map ğ•œ E G) := âŸ¨op_normâŸ©

/-- An alias of `continuous_multilinear_map.has_op_norm` with non-dependent types to help typeclass
search. -/
instance has_op_norm' : has_norm (continuous_multilinear_map ğ•œ (Î» (i : Î¹), G) G') :=
continuous_multilinear_map.has_op_norm

lemma norm_def : â€–fâ€– = Inf {c | 0 â‰¤ (c : â„) âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} := rfl

-- So that invocations of `le_cInf` make sense: we show that the set of
-- bounds is nonempty and bounded below.
lemma bounds_nonempty {f : continuous_multilinear_map ğ•œ E G} :
  âˆƒ c, c âˆˆ {c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} :=
let âŸ¨M, hMp, hMbâŸ© := f.bound in âŸ¨M, le_of_lt hMp, hMbâŸ©

lemma bounds_bdd_below {f : continuous_multilinear_map ğ•œ E G} :
  bdd_below {c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} :=
âŸ¨0, Î» _ âŸ¨hn, _âŸ©, hnâŸ©

lemma op_norm_nonneg : 0 â‰¤ â€–fâ€– :=
le_cInf bounds_nonempty (Î» _ âŸ¨hx, _âŸ©, hx)

/-- The fundamental property of the operator norm of a continuous multilinear map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`. -/
theorem le_op_norm : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€– :=
begin
  have A : 0 â‰¤ âˆ i, â€–m iâ€– := prod_nonneg (Î»j hj, norm_nonneg _),
  cases A.eq_or_lt with h hlt,
  { rcases prod_eq_zero_iff.1 h.symm with âŸ¨i, _, hiâŸ©,
    rw norm_eq_zero at hi,
    have : f m = 0 := f.map_coord_zero i hi,
    rw [this, norm_zero],
    exact mul_nonneg (op_norm_nonneg f) A },
  { rw [â† div_le_iff hlt],
    apply le_cInf bounds_nonempty,
    rintro c âŸ¨_, hcâŸ©, rw [div_le_iff hlt], apply hc }
end

theorem le_of_op_norm_le {C : â„} (h : â€–fâ€– â‰¤ C) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
(f.le_op_norm m).trans $ mul_le_mul_of_nonneg_right h (prod_nonneg $ Î» i _, norm_nonneg (m i))

lemma ratio_le_op_norm : â€–f mâ€– / âˆ i, â€–m iâ€– â‰¤ â€–fâ€– :=
div_le_of_nonneg_of_le_mul (prod_nonneg $ Î» i _, norm_nonneg _) (op_norm_nonneg _) (f.le_op_norm m)

/-- The image of the unit ball under a continuous multilinear map is bounded. -/
lemma unit_le_op_norm (h : â€–mâ€– â‰¤ 1) : â€–f mâ€– â‰¤ â€–fâ€– :=
calc
  â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€– : f.le_op_norm m
  ... â‰¤ â€–fâ€– * âˆ i : Î¹, 1 :
    mul_le_mul_of_nonneg_left (prod_le_prod (Î»i hi, norm_nonneg _)
      (Î»i hi, le_trans (norm_le_pi_norm (_ : Î  i, E i) _) h)) (op_norm_nonneg f)
  ... = â€–fâ€– : by simp

/-- If one controls the norm of every `f x`, then one controls the norm of `f`. -/
lemma op_norm_le_bound {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ m, â€–f mâ€– â‰¤ M * âˆ i, â€–m iâ€–) :
  â€–fâ€– â‰¤ M :=
cInf_le bounds_bdd_below âŸ¨hMp, hMâŸ©

/-- The operator norm satisfies the triangle inequality. -/
theorem op_norm_add_le : â€–f + gâ€– â‰¤ â€–fâ€– + â€–gâ€– :=
cInf_le bounds_bdd_below
  âŸ¨add_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Î» x, by { rw add_mul,
    exact norm_add_le_of_le (le_op_norm _ _) (le_op_norm _ _) }âŸ©

lemma op_norm_zero : â€–(0 : continuous_multilinear_map ğ•œ E G)â€– = 0 :=
(op_norm_nonneg _).antisymm' $ op_norm_le_bound 0 le_rfl $ Î» m, by simp

/-- A continuous linear map is zero iff its norm vanishes. -/
theorem op_norm_zero_iff : â€–fâ€– = 0 â†” f = 0 :=
âŸ¨Î» h, by { ext m, simpa [h] using f.le_op_norm m }, by { rintro rfl, exact op_norm_zero }âŸ©

section
variables {ğ•œ' : Type*} [normed_field ğ•œ'] [normed_space ğ•œ' G] [smul_comm_class ğ•œ ğ•œ' G]

lemma op_norm_smul_le (c : ğ•œ') : â€–c â€¢ fâ€– â‰¤ â€–câ€– * â€–fâ€– :=
(c â€¢ f).op_norm_le_bound
  (mul_nonneg (norm_nonneg _) (op_norm_nonneg _))
  begin
    intro m,
    erw [norm_smul, mul_assoc],
    exact mul_le_mul_of_nonneg_left (le_op_norm _ _) (norm_nonneg _)
  end

lemma op_norm_neg : â€–-fâ€– = â€–fâ€– := by { rw norm_def, apply congr_arg, ext, simp }

/-- Continuous multilinear maps themselves form a normed space with respect to
    the operator norm. -/
instance normed_add_comm_group : normed_add_comm_group (continuous_multilinear_map ğ•œ E G) :=
add_group_norm.to_normed_add_comm_group
{ to_fun := norm,
  map_zero' := op_norm_zero,
  neg' := op_norm_neg,
  add_le' := op_norm_add_le,
  eq_zero_of_map_eq_zero' := Î» f, f.op_norm_zero_iff.1 }

/-- An alias of `continuous_multilinear_map.normed_add_comm_group` with non-dependent types to help
typeclass search. -/
instance normed_add_comm_group' :
  normed_add_comm_group (continuous_multilinear_map ğ•œ (Î» i : Î¹, G) G') :=
continuous_multilinear_map.normed_add_comm_group

instance normed_space : normed_space ğ•œ' (continuous_multilinear_map ğ•œ E G) :=
âŸ¨Î» c f, f.op_norm_smul_le câŸ©

/-- An alias of `continuous_multilinear_map.normed_space` with non-dependent types to help typeclass
search. -/
instance normed_space' : normed_space ğ•œ' (continuous_multilinear_map ğ•œ (Î» i : Î¹, G') G) :=
continuous_multilinear_map.normed_space

theorem le_op_norm_mul_prod_of_le {b : Î¹ â†’ â„} (hm : âˆ€ i, â€–m iâ€– â‰¤ b i) : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, b i :=
(f.le_op_norm m).trans $ mul_le_mul_of_nonneg_left
  (prod_le_prod (Î» _ _, norm_nonneg _) (Î» i _, hm i)) (norm_nonneg f)

theorem le_op_norm_mul_pow_card_of_le {b : â„} (hm : âˆ€ i, â€–m iâ€– â‰¤ b) :
  â€–f mâ€– â‰¤ â€–fâ€– * b ^ fintype.card Î¹ :=
by simpa only [prod_const] using f.le_op_norm_mul_prod_of_le m hm

theorem le_op_norm_mul_pow_of_le {Ei : fin n â†’ Type*} [Î  i, normed_add_comm_group (Ei i)]
  [Î  i, normed_space ğ•œ (Ei i)] (f : continuous_multilinear_map ğ•œ Ei G) (m : Î  i, Ei i)
  {b : â„} (hm : â€–mâ€– â‰¤ b) :
  â€–f mâ€– â‰¤ â€–fâ€– * b ^ n :=
by simpa only [fintype.card_fin]
  using f.le_op_norm_mul_pow_card_of_le m (Î» i, (norm_le_pi_norm m i).trans hm)

/-- The fundamental property of the operator norm of a continuous multilinear map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`, `nnnorm` version. -/
theorem le_op_nnnorm : â€–f mâ€–â‚Š â‰¤ â€–fâ€–â‚Š * âˆ i, â€–m iâ€–â‚Š :=
nnreal.coe_le_coe.1 $ by { push_cast, exact f.le_op_norm m }

theorem le_of_op_nnnorm_le {C : â„â‰¥0} (h : â€–fâ€–â‚Š â‰¤ C) : â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š :=
(f.le_op_nnnorm m).trans $ mul_le_mul' h le_rfl

lemma op_norm_prod (f : continuous_multilinear_map ğ•œ E G) (g : continuous_multilinear_map ğ•œ E G') :
  â€–f.prod gâ€– = max (â€–fâ€–) (â€–gâ€–) :=
le_antisymm
  (op_norm_le_bound _ (norm_nonneg (f, g)) (Î» m,
    have H : 0 â‰¤ âˆ i, â€–m iâ€–, from prod_nonneg $ Î» _ _,  norm_nonneg _,
    by simpa only [prod_apply, prod.norm_def, max_mul_of_nonneg, H]
      using max_le_max (f.le_op_norm m) (g.le_op_norm m))) $
  max_le
    (f.op_norm_le_bound (norm_nonneg _) $ Î» m, (le_max_left _ _).trans ((f.prod g).le_op_norm _))
    (g.op_norm_le_bound (norm_nonneg _) $ Î» m, (le_max_right _ _).trans ((f.prod g).le_op_norm _))

lemma norm_pi {Î¹' : Type v'} [fintype Î¹'] {E' : Î¹' â†’ Type wE'} [Î  i', normed_add_comm_group (E' i')]
  [Î  i', normed_space ğ•œ (E' i')] (f : Î  i', continuous_multilinear_map ğ•œ E (E' i')) :
  â€–pi fâ€– = â€–fâ€– :=
begin
  apply le_antisymm,
  { refine (op_norm_le_bound _ (norm_nonneg f) (Î» m, _)),
    dsimp,
    rw pi_norm_le_iff_of_nonneg,
    exacts [Î» i, (f i).le_of_op_norm_le m (norm_le_pi_norm f i),
      mul_nonneg (norm_nonneg f) (prod_nonneg $ Î» _ _, norm_nonneg _)] },
  { refine (pi_norm_le_iff_of_nonneg (norm_nonneg _)).2 (Î» i, _),
    refine (op_norm_le_bound _ (norm_nonneg _) (Î» m, _)),
    refine le_trans _ ((pi f).le_op_norm m),
    convert norm_le_pi_norm (Î» j, f j m) i }
end

section
variables (ğ•œ G)

@[simp] lemma norm_of_subsingleton [subsingleton Î¹] [nontrivial G] (i' : Î¹) :
  â€–of_subsingleton ğ•œ G i'â€– = 1 :=
begin
  apply le_antisymm,
  { refine op_norm_le_bound _ zero_le_one (Î» m, _),
    rw [fintype.prod_subsingleton _ i', one_mul, of_subsingleton_apply] },
  { obtain âŸ¨g, hgâŸ© := exists_ne (0 : G),
    rw â†norm_ne_zero_iff at hg,
    have := (of_subsingleton ğ•œ G i').ratio_le_op_norm (Î» _, g),
    rwa [fintype.prod_subsingleton _ i', of_subsingleton_apply, div_self hg] at this },
end

@[simp] lemma nnnorm_of_subsingleton [subsingleton Î¹] [nontrivial G] (i' : Î¹) :
  â€–of_subsingleton ğ•œ G i'â€–â‚Š = 1 :=
nnreal.eq $ norm_of_subsingleton _ _ _

variables {G} (E)

@[simp] lemma norm_const_of_is_empty [is_empty Î¹] (x : G) : â€–const_of_is_empty ğ•œ E xâ€– = â€–xâ€– :=
begin
  apply le_antisymm,
  { refine op_norm_le_bound _ (norm_nonneg _) (Î» x, _),
    rw [fintype.prod_empty, mul_one, const_of_is_empty_apply], },
  { simpa using (const_of_is_empty ğ•œ E x).le_op_norm 0 }
end

@[simp] lemma nnnorm_const_of_is_empty [is_empty Î¹] (x : G) : â€–const_of_is_empty ğ•œ E xâ€–â‚Š = â€–xâ€–â‚Š :=
nnreal.eq $ norm_const_of_is_empty _ _ _

end

section

variables (ğ•œ E E' G G')

/-- `continuous_multilinear_map.prod` as a `linear_isometry_equiv`. -/
def prodL :
  (continuous_multilinear_map ğ•œ E G) Ã— (continuous_multilinear_map ğ•œ E G') â‰ƒâ‚—áµ¢[ğ•œ]
    continuous_multilinear_map ğ•œ E (G Ã— G') :=
{ to_fun := Î» f, f.1.prod f.2,
  inv_fun := Î» f, ((continuous_linear_map.fst ğ•œ G G').comp_continuous_multilinear_map f,
    (continuous_linear_map.snd ğ•œ G G').comp_continuous_multilinear_map f),
  map_add' := Î» f g, rfl,
  map_smul' := Î» c f, rfl,
  left_inv := Î» f, by ext; refl,
  right_inv := Î» f, by ext; refl,
  norm_map' := Î» f, op_norm_prod f.1 f.2 }

/-- `continuous_multilinear_map.pi` as a `linear_isometry_equiv`. -/
def piâ‚—áµ¢ {Î¹' : Type v'} [fintype Î¹'] {E' : Î¹' â†’ Type wE'} [Î  i', normed_add_comm_group (E' i')]
  [Î  i', normed_space ğ•œ (E' i')] :
  @linear_isometry_equiv ğ•œ ğ•œ _ _ (ring_hom.id ğ•œ) _ _ _
    (Î  i', continuous_multilinear_map ğ•œ E (E' i')) (continuous_multilinear_map ğ•œ E (Î  i, E' i)) _ _
      (@pi.module Î¹' _ ğ•œ _ _ (Î» i', infer_instance)) _ :=
{ to_linear_equiv :=
  -- note: `pi_linear_equiv` does not unify correctly here, presumably due to issues with dependent
  -- typeclass arguments.
  { map_add' := Î» f g, rfl,
    map_smul' := Î» c f, rfl,
    .. pi_equiv, },
  norm_map' := norm_pi }

end

end

section restrict_scalars

variables {ğ•œ' : Type*} [nontrivially_normed_field ğ•œ'] [normed_algebra ğ•œ' ğ•œ]
variables [normed_space ğ•œ' G] [is_scalar_tower ğ•œ' ğ•œ G]
variables [Î  i, normed_space ğ•œ' (E i)] [âˆ€ i, is_scalar_tower ğ•œ' ğ•œ (E i)]

@[simp] lemma norm_restrict_scalars : â€–f.restrict_scalars ğ•œ'â€– = â€–fâ€– :=
by simp only [norm_def, coe_restrict_scalars]

variable (ğ•œ')

/-- `continuous_multilinear_map.restrict_scalars` as a `continuous_multilinear_map`. -/
def restrict_scalars_linear :
  continuous_multilinear_map ğ•œ E G â†’L[ğ•œ'] continuous_multilinear_map ğ•œ' E G :=
linear_map.mk_continuous
{ to_fun := restrict_scalars ğ•œ',
  map_add' := Î» mâ‚ mâ‚‚, rfl,
  map_smul' := Î» c m, rfl } 1 $ Î» f, by simp

variable {ğ•œ'}

lemma continuous_restrict_scalars :
  continuous (restrict_scalars ğ•œ' : continuous_multilinear_map ğ•œ E G â†’
    continuous_multilinear_map ğ•œ' E G) :=
(restrict_scalars_linear ğ•œ').continuous

end restrict_scalars

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`, precise version.
For a less precise but more usable version, see `norm_image_sub_le`. The bound reads
`â€–f m - f m'â€– â‰¤
  â€–fâ€– * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`.-/
lemma norm_image_sub_le' [decidable_eq Î¹] (mâ‚ mâ‚‚ : Î i, E i) :
  â€–f mâ‚ - f mâ‚‚â€– â‰¤
  â€–fâ€– * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
f.to_multilinear_map.norm_image_sub_le_of_bound' (norm_nonneg _) f.le_op_norm _ _

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`, less precise
version. For a more precise but less usable version, see `norm_image_sub_le'`.
The bound is `â€–f m - f m'â€– â‰¤ â€–fâ€– * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`.-/
lemma norm_image_sub_le (mâ‚ mâ‚‚ : Î i, E i) :
  â€–f mâ‚ - f mâ‚‚â€– â‰¤ â€–fâ€– * (fintype.card Î¹) * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :=
f.to_multilinear_map.norm_image_sub_le_of_bound (norm_nonneg _) f.le_op_norm _ _

/-- Applying a multilinear map to a vector is continuous in both coordinates. -/
lemma continuous_eval :
  continuous (Î» p : continuous_multilinear_map ğ•œ E G Ã— Î  i, E i, p.1 p.2) :=
begin
  apply continuous_iff_continuous_at.2 (Î»p, _),
  apply continuous_at_of_locally_lipschitz zero_lt_one
    ((â€–pâ€– + 1) * (fintype.card Î¹) * (â€–pâ€– + 1) ^ (fintype.card Î¹ - 1) + âˆ i, â€–p.2 iâ€–)
    (Î»q hq, _),
  have : 0 â‰¤ (max â€–q.2â€– â€–p.2â€–), by simp,
  have : 0 â‰¤ â€–pâ€– + 1 := zero_le_one.trans ((le_add_iff_nonneg_left 1).2 $ norm_nonneg p),
  have A : â€–qâ€– â‰¤ â€–pâ€– + 1 := norm_le_of_mem_closed_ball hq.le,
  have : (max â€–q.2â€– â€–p.2â€–) â‰¤ â€–pâ€– + 1 :=
    (max_le_max (norm_snd_le q) (norm_snd_le p)).trans (by simp [A, -add_comm, zero_le_one]),
  have : âˆ€ (i : Î¹), i âˆˆ univ â†’ 0 â‰¤ â€–p.2 iâ€– := Î» i hi, norm_nonneg _,
  calc dist (q.1 q.2) (p.1 p.2)
    â‰¤ dist (q.1 q.2) (q.1 p.2) + dist (q.1 p.2) (p.1 p.2) : dist_triangle _ _ _
    ... = â€–q.1 q.2 - q.1 p.2â€– + â€–q.1 p.2 - p.1 p.2â€– : by rw [dist_eq_norm, dist_eq_norm]
    ... â‰¤ â€–q.1â€– * (fintype.card Î¹) * (max â€–q.2â€– â€–p.2â€–) ^ (fintype.card Î¹ - 1) * â€–q.2 - p.2â€–
          + â€–q.1 - p.1â€– * âˆ i, â€–p.2 iâ€– :
      add_le_add (norm_image_sub_le _ _ _) ((q.1 - p.1).le_op_norm p.2)
    ... â‰¤ (â€–pâ€– + 1) * (fintype.card Î¹) * (â€–pâ€– + 1) ^ (fintype.card Î¹ - 1) * â€–q - pâ€–
          + â€–q - pâ€– * âˆ i, â€–p.2 iâ€– :
      by apply_rules [add_le_add, mul_le_mul, le_refl, le_trans (norm_fst_le q) A, nat.cast_nonneg,
        mul_nonneg, pow_le_pow_of_le_left, pow_nonneg, norm_snd_le (q - p), norm_nonneg,
        norm_fst_le (q - p), prod_nonneg]
    ... = ((â€–pâ€– + 1) * (fintype.card Î¹) * (â€–pâ€– + 1) ^ (fintype.card Î¹ - 1)
              + (âˆ i, â€–p.2 iâ€–)) * dist q p : by { rw dist_eq_norm, ring }
end

lemma continuous_eval_left (m : Î  i, E i) :
  continuous (Î» p : continuous_multilinear_map ğ•œ E G, p m) :=
continuous_eval.comp (continuous_id.prod_mk continuous_const)

lemma has_sum_eval
  {Î± : Type*} {p : Î± â†’ continuous_multilinear_map ğ•œ E G} {q : continuous_multilinear_map ğ•œ E G}
  (h : has_sum p q) (m : Î  i, E i) : has_sum (Î» a, p a m) (q m) :=
begin
  dsimp [has_sum] at h âŠ¢,
  convert ((continuous_eval_left m).tendsto _).comp h,
  ext s,
  simp
end

lemma tsum_eval {Î± : Type*} {p : Î± â†’ continuous_multilinear_map ğ•œ E G} (hp : summable p)
  (m : Î  i, E i) : (âˆ‘' a, p a) m = âˆ‘' a, p a m :=
(has_sum_eval hp.has_sum m).tsum_eq.symm

open_locale topology
open filter

/-- If the target space is complete, the space of continuous multilinear maps with its norm is also
complete. The proof is essentially the same as for the space of continuous linear maps (modulo the
addition of `finset.prod` where needed. The duplication could be avoided by deducing the linear
case from the multilinear case via a currying isomorphism. However, this would mess up imports,
and it is more satisfactory to have the simplest case as a standalone proof. -/
instance [complete_space G] : complete_space (continuous_multilinear_map ğ•œ E G) :=
begin
  have nonneg : âˆ€ (v : Î  i, E i), 0 â‰¤ âˆ i, â€–v iâ€– :=
    Î» v, finset.prod_nonneg (Î» i hi, norm_nonneg _),
  -- We show that every Cauchy sequence converges.
  refine metric.complete_of_cauchy_seq_tendsto (Î» f hf, _),
  -- We now expand out the definition of a Cauchy sequence,
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with âŸ¨b, b0, b_bound, b_limâŸ©,
  -- and establish that the evaluation at any point `v : Î  i, E i` is Cauchy.
  have cau : âˆ€ v, cauchy_seq (Î» n, f n v),
  { assume v,
    apply cauchy_seq_iff_le_tendsto_0.2 âŸ¨Î» n, b n * âˆ i, â€–v iâ€–, Î» n, _, _, _âŸ©,
    { exact mul_nonneg (b0 n) (nonneg v) },
    { assume n m N hn hm,
      rw dist_eq_norm,
      apply le_trans ((f n - f m).le_op_norm v) _,
      exact mul_le_mul_of_nonneg_right (b_bound n m N hn hm) (nonneg v) },
    { simpa using b_lim.mul tendsto_const_nhds } },
  -- We assemble the limits points of those Cauchy sequences
  -- (which exist as `G` is complete)
  -- into a function which we call `F`.
  choose F hF using Î»v, cauchy_seq_tendsto_of_complete (cau v),
  -- Next, we show that this `F` is multilinear,
  let Fmult : multilinear_map ğ•œ E G :=
  { to_fun := F,
    map_add' := Î» _ v i x y, begin
      resetI,
      have A := hF (function.update v i (x + y)),
      have B := (hF (function.update v i x)).add (hF (function.update v i y)),
      simp at A B,
      exact tendsto_nhds_unique A B
    end,
    map_smul' := Î» _ v i c x, begin
      resetI,
      have A := hF (function.update v i (c â€¢ x)),
      have B := filter.tendsto.smul (@tendsto_const_nhds _ â„• _ c _) (hF (function.update v i x)),
      simp at A B,
      exact tendsto_nhds_unique A B
    end },
  -- and that `F` has norm at most `(b 0 + â€–f 0â€–)`.
  have Fnorm : âˆ€ v, â€–F vâ€– â‰¤ (b 0 + â€–f 0â€–) * âˆ i, â€–v iâ€–,
  { assume v,
    have A : âˆ€ n, â€–f n vâ€– â‰¤ (b 0 + â€–f 0â€–) * âˆ i, â€–v iâ€–,
    { assume n,
      apply le_trans ((f n).le_op_norm _) _,
      apply mul_le_mul_of_nonneg_right _ (nonneg v),
      calc â€–f nâ€– = â€–(f n - f 0) + f 0â€– : by { congr' 1, abel }
      ... â‰¤ â€–f n - f 0â€– + â€–f 0â€– : norm_add_le _ _
      ... â‰¤ b 0 + â€–f 0â€– : begin
        apply add_le_add_right,
        simpa [dist_eq_norm] using b_bound n 0 0 (zero_le _) (zero_le _)
      end },
    exact le_of_tendsto (hF v).norm (eventually_of_forall A) },
  -- Thus `F` is continuous, and we propose that as the limit point of our original Cauchy sequence.
  let Fcont := Fmult.mk_continuous _ Fnorm,
  use Fcont,
  -- Our last task is to establish convergence to `F` in norm.
  have : âˆ€ n, â€–f n - Fcontâ€– â‰¤ b n,
  { assume n,
    apply op_norm_le_bound _ (b0 n) (Î» v, _),
    have A : âˆ€á¶  m in at_top, â€–(f n - f m) vâ€– â‰¤ b n * âˆ i, â€–v iâ€–,
    { refine eventually_at_top.2 âŸ¨n, Î» m hm, _âŸ©,
      apply le_trans ((f n - f m).le_op_norm _) _,
      exact mul_le_mul_of_nonneg_right (b_bound n m n le_rfl hm) (nonneg v) },
    have B : tendsto (Î» m, â€–(f n - f m) vâ€–) at_top (ğ“ (â€–(f n - Fcont) vâ€–)) :=
      tendsto.norm (tendsto_const_nhds.sub (hF v)),
    exact le_of_tendsto B A },
  erw tendsto_iff_norm_tendsto_zero,
  exact squeeze_zero (Î» n, norm_nonneg _) this b_lim,
end

end continuous_multilinear_map

/-- If a continuous multilinear map is constructed from a multilinear map via the constructor
`mk_continuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
lemma multilinear_map.mk_continuous_norm_le (f : multilinear_map ğ•œ E G) {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mk_continuous C Hâ€– â‰¤ C :=
continuous_multilinear_map.op_norm_le_bound _ hC (Î»m, H m)

/-- If a continuous multilinear map is constructed from a multilinear map via the constructor
`mk_continuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
lemma multilinear_map.mk_continuous_norm_le' (f : multilinear_map ğ•œ E G) {C : â„}
  (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mk_continuous C Hâ€– â‰¤ max C 0 :=
continuous_multilinear_map.op_norm_le_bound _ (le_max_right _ _) $
  Î» m, (H m).trans $ mul_le_mul_of_nonneg_right (le_max_left _ _)
    (prod_nonneg $ Î» _ _, norm_nonneg _)

namespace continuous_multilinear_map

/-- Given a continuous multilinear map `f` on `n` variables (parameterized by `fin n`) and a subset
`s` of `k` of these variables, one gets a new continuous multilinear map on `fin k` by varying
these variables, and fixing the other ones equal to a given value `z`. It is denoted by
`f.restr s hk z`, where `hk` is a proof that the cardinality of `s` is `k`. The implicit
identification between `fin k` and `s` that we use is the canonical (increasing) bijection. -/
def restr {k n : â„•} (f : (G [Ã—n]â†’L[ğ•œ] G' : _)) (s : finset (fin n)) (hk : s.card = k) (z : G) :
  G [Ã—k]â†’L[ğ•œ] G' :=
(f.to_multilinear_map.restr s hk z).mk_continuous
(â€–fâ€– * â€–zâ€–^(n-k)) $ Î» v, multilinear_map.restr_norm_le _ _ _ _ f.le_op_norm _

lemma norm_restr {k n : â„•} (f : G [Ã—n]â†’L[ğ•œ] G') (s : finset (fin n)) (hk : s.card = k) (z : G) :
  â€–f.restr s hk zâ€– â‰¤ â€–fâ€– * â€–zâ€– ^ (n - k) :=
begin
  apply multilinear_map.mk_continuous_norm_le,
  exact mul_nonneg (norm_nonneg _) (pow_nonneg (norm_nonneg _) _)
end

section

variables {ğ•œ Î¹} {A : Type*} [normed_comm_ring A] [normed_algebra ğ•œ A]

@[simp]
lemma norm_mk_pi_algebra_le [nonempty Î¹] :
  â€–continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâ€– â‰¤ 1 :=
begin
  have := Î» f, @op_norm_le_bound ğ•œ Î¹ (Î» i, A) A _ _ _ _ _ _ f _ zero_le_one,
  refine this _ _,
  intros m,
  simp only [continuous_multilinear_map.mk_pi_algebra_apply, one_mul],
  exact norm_prod_le' _ univ_nonempty _,
end

lemma norm_mk_pi_algebra_of_empty [is_empty Î¹] :
  â€–continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâ€– = â€–(1 : A)â€– :=
begin
  apply le_antisymm,
  { have := Î» f, @op_norm_le_bound ğ•œ Î¹ (Î» i, A) A _ _ _ _ _ _ f _ (norm_nonneg (1 : A)),
    refine this _ _,
    simp, },
  { convert ratio_le_op_norm _ (Î» _, (1 : A)),
    simp [eq_empty_of_is_empty (univ : finset Î¹)], },
end

@[simp] lemma norm_mk_pi_algebra [norm_one_class A] :
  â€–continuous_multilinear_map.mk_pi_algebra ğ•œ Î¹ Aâ€– = 1 :=
begin
  casesI is_empty_or_nonempty Î¹,
  { simp [norm_mk_pi_algebra_of_empty] },
  { refine le_antisymm norm_mk_pi_algebra_le _,
    convert ratio_le_op_norm _ (Î» _, 1); [skip, apply_instance],
    simp },
end

end

section

variables {ğ•œ n} {A : Type*} [normed_ring A] [normed_algebra ğ•œ A]

lemma norm_mk_pi_algebra_fin_succ_le :
  â€–continuous_multilinear_map.mk_pi_algebra_fin ğ•œ n.succ Aâ€– â‰¤ 1 :=
begin
  have := Î» f, @op_norm_le_bound ğ•œ (fin n.succ) (Î» i, A) A _ _ _ _ _ _ f _ zero_le_one,
  refine this _ _,
  intros m,
  simp only [continuous_multilinear_map.mk_pi_algebra_fin_apply, one_mul, list.of_fn_eq_map,
    fin.prod_univ_def, multiset.coe_map, multiset.coe_prod],
  refine (list.norm_prod_le' _).trans_eq _,
  { rw [ne.def, list.map_eq_nil, list.fin_range_eq_nil],
    exact nat.succ_ne_zero _, },
  rw list.map_map,
end

lemma norm_mk_pi_algebra_fin_le_of_pos (hn : 0 < n) :
  â€–continuous_multilinear_map.mk_pi_algebra_fin ğ•œ n Aâ€– â‰¤ 1 :=
begin
  obtain âŸ¨n, rflâŸ© := nat.exists_eq_succ_of_ne_zero hn.ne',
  exact norm_mk_pi_algebra_fin_succ_le
end

lemma norm_mk_pi_algebra_fin_zero :
  â€–continuous_multilinear_map.mk_pi_algebra_fin ğ•œ 0 Aâ€– = â€–(1 : A)â€– :=
begin
  refine le_antisymm _ _,
  { have := Î» f, @op_norm_le_bound ğ•œ (fin 0) (Î» i, A) A _ _ _ _ _ _ f _ (norm_nonneg (1 : A)),
    refine this _ _,
    simp, },
  { convert ratio_le_op_norm _ (Î» _, (1 : A)),
    simp }
end

@[simp] lemma norm_mk_pi_algebra_fin [norm_one_class A] :
  â€–continuous_multilinear_map.mk_pi_algebra_fin ğ•œ n Aâ€– = 1 :=
begin
  cases n,
  { simp [norm_mk_pi_algebra_fin_zero] },
  { refine le_antisymm norm_mk_pi_algebra_fin_succ_le _,
    convert ratio_le_op_norm _ (Î» _, 1); [skip, apply_instance],
    simp }
end

end

variables (ğ•œ Î¹)

/-- The canonical continuous multilinear map on `ğ•œ^Î¹`, associating to `m` the product of all the
`m i` (multiplied by a fixed reference element `z` in the target module) -/
protected def mk_pi_field (z : G) : continuous_multilinear_map ğ•œ (Î»(i : Î¹), ğ•œ) G :=
multilinear_map.mk_continuous
  (multilinear_map.mk_pi_ring ğ•œ Î¹ z) (â€–zâ€–)
  (Î» m, by simp only [multilinear_map.mk_pi_ring_apply, norm_smul, norm_prod,
    mul_comm])

variables {ğ•œ Î¹}

@[simp] lemma mk_pi_field_apply (z : G) (m : Î¹ â†’ ğ•œ) :
  (continuous_multilinear_map.mk_pi_field ğ•œ Î¹ z : (Î¹ â†’ ğ•œ) â†’ G) m = (âˆ i, m i) â€¢ z := rfl

lemma mk_pi_field_apply_one_eq_self (f : continuous_multilinear_map ğ•œ (Î»(i : Î¹), ğ•œ) G) :
  continuous_multilinear_map.mk_pi_field ğ•œ Î¹ (f (Î»i, 1)) = f :=
to_multilinear_map_injective f.to_multilinear_map.mk_pi_ring_apply_one_eq_self

@[simp] lemma norm_mk_pi_field (z : G) : â€–continuous_multilinear_map.mk_pi_field ğ•œ Î¹ zâ€– = â€–zâ€– :=
(multilinear_map.mk_continuous_norm_le _ (norm_nonneg z) _).antisymm $
  by simpa using (continuous_multilinear_map.mk_pi_field ğ•œ Î¹ z).le_op_norm (Î» _, 1)

lemma mk_pi_field_eq_iff {zâ‚ zâ‚‚ : G} :
  continuous_multilinear_map.mk_pi_field ğ•œ Î¹ zâ‚ = continuous_multilinear_map.mk_pi_field ğ•œ Î¹ zâ‚‚ â†”
  zâ‚ = zâ‚‚ :=
begin
  rw [â† to_multilinear_map_injective.eq_iff],
  exact multilinear_map.mk_pi_ring_eq_iff
end

lemma mk_pi_field_zero :
  continuous_multilinear_map.mk_pi_field ğ•œ Î¹ (0 : G) = 0 :=
by ext; rw [mk_pi_field_apply, smul_zero, continuous_multilinear_map.zero_apply]

lemma mk_pi_field_eq_zero_iff (z : G) :
  continuous_multilinear_map.mk_pi_field ğ•œ Î¹ z = 0 â†” z = 0 :=
by rw [â† mk_pi_field_zero, mk_pi_field_eq_iff]

variables (ğ•œ Î¹ G)

/-- Continuous multilinear maps on `ğ•œ^n` with values in `G` are in bijection with `G`, as such a
continuous multilinear map is completely determined by its value on the constant vector made of
ones. We register this bijection as a linear isometry in
`continuous_multilinear_map.pi_field_equiv`. -/
protected def pi_field_equiv : G â‰ƒâ‚—áµ¢[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : Î¹), ğ•œ) G) :=
{ to_fun    := Î» z, continuous_multilinear_map.mk_pi_field ğ•œ Î¹ z,
  inv_fun   := Î» f, f (Î»i, 1),
  map_add'  := Î» z z', by { ext m, simp [smul_add] },
  map_smul' := Î» c z, by { ext m, simp [smul_smul, mul_comm] },
  left_inv  := Î» z, by simp,
  right_inv := Î» f, f.mk_pi_field_apply_one_eq_self,
  norm_map' := norm_mk_pi_field }

end continuous_multilinear_map

namespace continuous_linear_map

lemma norm_comp_continuous_multilinear_map_le (g : G â†’L[ğ•œ] G')
  (f : continuous_multilinear_map ğ•œ E G) :
  â€–g.comp_continuous_multilinear_map fâ€– â‰¤ â€–gâ€– * â€–fâ€– :=
continuous_multilinear_map.op_norm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) $ Î» m,
calc â€–g (f m)â€– â‰¤ â€–gâ€– * (â€–fâ€– * âˆ i, â€–m iâ€–) : g.le_op_norm_of_le $ f.le_op_norm _
           ... = _                        : (mul_assoc _ _ _).symm

variables (ğ•œ E G G')
/-- `continuous_linear_map.comp_continuous_multilinear_map` as a bundled continuous bilinear map. -/
def comp_continuous_multilinear_mapL :
  (G â†’L[ğ•œ] G') â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G' :=
linear_map.mk_continuousâ‚‚
  (linear_map.mkâ‚‚ ğ•œ comp_continuous_multilinear_map (Î» fâ‚ fâ‚‚ g, rfl) (Î» c f g, rfl)
    (Î» f gâ‚ gâ‚‚, by { ext1, apply f.map_add }) (Î» c f g, by { ext1, simp }))
  1 $ Î» f g, by { rw one_mul, exact f.norm_comp_continuous_multilinear_map_le g }

variables {ğ•œ G G'}

/-- `continuous_linear_map.comp_continuous_multilinear_map` as a bundled
continuous linear equiv. -/
def _root_.continuous_linear_equiv.comp_continuous_multilinear_mapL (g : G â‰ƒL[ğ•œ] G') :
  continuous_multilinear_map ğ•œ E G â‰ƒL[ğ•œ] continuous_multilinear_map ğ•œ E G' :=
{ inv_fun := comp_continuous_multilinear_mapL ğ•œ _ _ _ g.symm.to_continuous_linear_map,
  left_inv := begin
    assume f,
    ext1 m,
    simp only [comp_continuous_multilinear_mapL, continuous_linear_equiv.coe_def_rev,
      to_linear_map_eq_coe, linear_map.to_fun_eq_coe, coe_coe, linear_map.mk_continuousâ‚‚_apply,
      linear_map.mkâ‚‚_apply, comp_continuous_multilinear_map_coe, continuous_linear_equiv.coe_coe,
      function.comp_app, continuous_linear_equiv.symm_apply_apply],
  end,
  right_inv := begin
    assume f,
    ext1 m,
    simp only [comp_continuous_multilinear_mapL, continuous_linear_equiv.coe_def_rev,
      to_linear_map_eq_coe, linear_map.mk_continuousâ‚‚_apply, linear_map.mkâ‚‚_apply,
      linear_map.to_fun_eq_coe, coe_coe, comp_continuous_multilinear_map_coe,
      continuous_linear_equiv.coe_coe, function.comp_app, continuous_linear_equiv.apply_symm_apply],
  end,
  continuous_to_fun :=
    (comp_continuous_multilinear_mapL ğ•œ _ _ _ g.to_continuous_linear_map).continuous,
  continuous_inv_fun :=
    (comp_continuous_multilinear_mapL ğ•œ _ _ _ g.symm.to_continuous_linear_map).continuous,
  .. comp_continuous_multilinear_mapL ğ•œ _ _ _ g.to_continuous_linear_map }

@[simp] lemma _root_.continuous_linear_equiv.comp_continuous_multilinear_mapL_symm
  (g : G â‰ƒL[ğ•œ] G') :
  (g.comp_continuous_multilinear_mapL E).symm = g.symm.comp_continuous_multilinear_mapL E := rfl

variables {E}

@[simp] lemma _root_.continuous_linear_equiv.comp_continuous_multilinear_mapL_apply
  (g : G â‰ƒL[ğ•œ] G') (f : continuous_multilinear_map ğ•œ E G) :
  g.comp_continuous_multilinear_mapL E f = (g : G â†’L[ğ•œ] G').comp_continuous_multilinear_map f :=
rfl

/-- Flip arguments in `f : G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G'` to get
`continuous_multilinear_map ğ•œ E (G â†’L[ğ•œ] G')` -/
def flip_multilinear (f : G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G') :
  continuous_multilinear_map ğ•œ E (G â†’L[ğ•œ] G') :=
multilinear_map.mk_continuous
  { to_fun := Î» m, linear_map.mk_continuous
      { to_fun := Î» x, f x m,
        map_add' := Î» x y, by simp only [map_add, continuous_multilinear_map.add_apply],
        map_smul' := Î» c x, by simp only [continuous_multilinear_map.smul_apply, map_smul,
                                          ring_hom.id_apply] }
      (â€–fâ€– * âˆ i, â€–m iâ€–) $ Î» x,
      by { rw mul_right_comm, exact (f x).le_of_op_norm_le _ (f.le_op_norm x) },
    map_add' := Î» _ m i x y,
      by { ext1, simp only [add_apply, continuous_multilinear_map.map_add, linear_map.coe_mk,
                            linear_map.mk_continuous_apply]},
    map_smul' := Î» _ m i c x,
      by { ext1, simp only [coe_smul', continuous_multilinear_map.map_smul, linear_map.coe_mk,
                            linear_map.mk_continuous_apply, pi.smul_apply]} }
  â€–fâ€– $ Î» m,
  linear_map.mk_continuous_norm_le _
    (mul_nonneg (norm_nonneg f) (prod_nonneg $ Î» i hi, norm_nonneg (m i))) _

end continuous_linear_map

lemma linear_isometry.norm_comp_continuous_multilinear_map
  (g : G â†’â‚—áµ¢[ğ•œ] G') (f : continuous_multilinear_map ğ•œ E G) :
  â€–g.to_continuous_linear_map.comp_continuous_multilinear_map fâ€– = â€–fâ€– :=
by simp only [continuous_linear_map.comp_continuous_multilinear_map_coe,
    linear_isometry.coe_to_continuous_linear_map, linear_isometry.norm_map,
    continuous_multilinear_map.norm_def]

open continuous_multilinear_map

namespace multilinear_map

/-- Given a map `f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G'` and an estimate
`H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–`, construct a continuous linear
map from `G` to `continuous_multilinear_map ğ•œ E G'`.

In order to lift, e.g., a map `f : (multilinear_map ğ•œ E G) â†’â‚—[ğ•œ] multilinear_map ğ•œ E' G'`
to a map `(continuous_multilinear_map ğ•œ E G) â†’L[ğ•œ] continuous_multilinear_map ğ•œ E' G'`,
one can apply this construction to `f.comp continuous_multilinear_map.to_multilinear_map_linear`
which is a linear map from `continuous_multilinear_map ğ•œ E G` to `multilinear_map ğ•œ E' G'`. -/
def mk_continuous_linear (f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G') (C : â„)
  (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) :
  G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G' :=
linear_map.mk_continuous
  { to_fun := Î» x, (f x).mk_continuous (C * â€–xâ€–) $ H x,
    map_add' := Î» x y, by { ext1, simp only [_root_.map_add], refl },
    map_smul' := Î» c x, by { ext1, simp only [smul_hom_class.map_smul], refl } }
  (max C 0) $ Î» x, ((f x).mk_continuous_norm_le' _).trans_eq $
    by rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]

lemma mk_continuous_linear_norm_le' (f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G') (C : â„)
  (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) :
  â€–mk_continuous_linear f C Hâ€– â‰¤ max C 0 :=
begin
  dunfold mk_continuous_linear,
  exact linear_map.mk_continuous_norm_le _ (le_max_right _ _) _
end

lemma mk_continuous_linear_norm_le (f : G â†’â‚—[ğ•œ] multilinear_map ğ•œ E G') {C : â„} (hC : 0 â‰¤ C)
  (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) :
  â€–mk_continuous_linear f C Hâ€– â‰¤ C :=
(mk_continuous_linear_norm_le' f C H).trans_eq (max_eq_left hC)

/-- Given a map `f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)` and an estimate
`H : âˆ€ m m', â€–f m m'â€– â‰¤ C * âˆ i, â€–m iâ€– * âˆ i, â€–m' iâ€–`, upgrade all `multilinear_map`s in the type to
`continuous_multilinear_map`s. -/
def mk_continuous_multilinear (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)) (C : â„)
  (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ C * (âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
  continuous_multilinear_map ğ•œ E (continuous_multilinear_map ğ•œ E' G) :=
mk_continuous
  { to_fun := Î» m, mk_continuous (f m) (C * âˆ i, â€–m iâ€–) $ H m,
    map_add' := Î» _ m i x y, by { ext1, simp },
    map_smul' := Î» _ m i c x, by { ext1, simp } }
  (max C 0) $ Î» m, ((f m).mk_continuous_norm_le' _).trans_eq $
    by { rw [max_mul_of_nonneg, zero_mul], exact prod_nonneg (Î» _ _, norm_nonneg _) }

@[simp] lemma mk_continuous_multilinear_apply (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G))
  {C : â„} (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ C * (âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) (m : Î  i, E i) :
  â‡‘(mk_continuous_multilinear f C H m) = f m :=
rfl

lemma mk_continuous_multilinear_norm_le' (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)) (C : â„)
  (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ C * (âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
  â€–mk_continuous_multilinear f C Hâ€– â‰¤ max C 0 :=
begin
  dunfold mk_continuous_multilinear,
  exact mk_continuous_norm_le _ (le_max_right _ _) _
end

lemma mk_continuous_multilinear_norm_le (f : multilinear_map ğ•œ E (multilinear_map ğ•œ E' G)) {C : â„}
  (hC : 0 â‰¤ C) (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ C * (âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
  â€–mk_continuous_multilinear f C Hâ€– â‰¤ C :=
(mk_continuous_multilinear_norm_le' f C H).trans_eq (max_eq_left hC)

end multilinear_map

namespace continuous_multilinear_map

lemma norm_comp_continuous_linear_le (g : continuous_multilinear_map ğ•œ Eâ‚ G)
  (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  â€–g.comp_continuous_linear_map fâ€– â‰¤ â€–gâ€– * âˆ i, â€–f iâ€– :=
op_norm_le_bound _ (mul_nonneg (norm_nonneg _) $ prod_nonneg $ Î» i hi, norm_nonneg _) $ Î» m,
calc â€–g (Î» i, f i (m i))â€– â‰¤ â€–gâ€– * âˆ i, â€–f i (m i)â€– : g.le_op_norm _
... â‰¤ â€–gâ€– * âˆ i, (â€–f iâ€– * â€–m iâ€–) :
  mul_le_mul_of_nonneg_left
    (prod_le_prod (Î» _ _, norm_nonneg _) (Î» i hi, (f i).le_op_norm (m i))) (norm_nonneg g)
... = (â€–gâ€– * âˆ i, â€–f iâ€–) * âˆ i, â€–m iâ€– : by rw [prod_mul_distrib, mul_assoc]

lemma norm_comp_continuous_linear_isometry_le (g : continuous_multilinear_map ğ•œ Eâ‚ G)
  (f : Î  i, E i â†’â‚—áµ¢[ğ•œ] Eâ‚ i) :
  â€–g.comp_continuous_linear_map (Î» i, (f i).to_continuous_linear_map)â€– â‰¤ â€–gâ€– :=
begin
  apply op_norm_le_bound _ (norm_nonneg _) (Î» m, _),
  apply (g.le_op_norm _).trans _,
  simp only [continuous_linear_map.to_linear_map_eq_coe, continuous_linear_map.coe_coe,
    linear_isometry.coe_to_continuous_linear_map, linear_isometry.norm_map]
end

lemma norm_comp_continuous_linear_isometry_equiv (g : continuous_multilinear_map ğ•œ Eâ‚ G)
  (f : Î  i, E i â‰ƒâ‚—áµ¢[ğ•œ] Eâ‚ i) :
  â€–g.comp_continuous_linear_map (Î» i, (f i : E i â†’L[ğ•œ] Eâ‚ i))â€– = â€–gâ€– :=
begin
  apply le_antisymm (g.norm_comp_continuous_linear_isometry_le (Î» i, (f i).to_linear_isometry)),
  have : g = (g.comp_continuous_linear_map (Î» i, (f i : E i â†’L[ğ•œ] Eâ‚ i)))
    .comp_continuous_linear_map (Î» i, ((f i).symm : Eâ‚ i â†’L[ğ•œ] E i)),
  { ext1 m,
    simp only [comp_continuous_linear_map_apply, linear_isometry_equiv.coe_coe'',
      linear_isometry_equiv.apply_symm_apply] },
  conv_lhs { rw this },
  apply (g.comp_continuous_linear_map (Î» i, (f i : E i â†’L[ğ•œ] Eâ‚ i)))
    .norm_comp_continuous_linear_isometry_le (Î» i, (f i).symm.to_linear_isometry),
end

/-- `continuous_multilinear_map.comp_continuous_linear_map` as a bundled continuous linear map.
This implementation fixes `f : Î  i, E i â†’L[ğ•œ] Eâ‚ i`.

TODO: Actually, the map is multilinear in `f` but an attempt to formalize this failed because of
issues with class instances. -/
def comp_continuous_linear_mapL (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  continuous_multilinear_map ğ•œ Eâ‚ G â†’L[ğ•œ] continuous_multilinear_map ğ•œ E G :=
linear_map.mk_continuous
  { to_fun := Î» g, g.comp_continuous_linear_map f,
    map_add' := Î» gâ‚ gâ‚‚, rfl,
    map_smul' := Î» c g, rfl }
  (âˆ i, â€–f iâ€–) $ Î» g, (norm_comp_continuous_linear_le _ _).trans_eq (mul_comm _ _)

@[simp] lemma comp_continuous_linear_mapL_apply
  (g : continuous_multilinear_map ğ•œ Eâ‚ G) (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  comp_continuous_linear_mapL f g = g.comp_continuous_linear_map f :=
rfl

lemma norm_comp_continuous_linear_mapL_le (f : Î  i, E i â†’L[ğ•œ] Eâ‚ i) :
  â€–@comp_continuous_linear_mapL ğ•œ Î¹ E Eâ‚ G _ _ _ _ _ _ _ _ fâ€– â‰¤ (âˆ i, â€–f iâ€–) :=
linear_map.mk_continuous_norm_le _ (prod_nonneg $ Î» i _, norm_nonneg _) _

variable (G)

/-- `continuous_multilinear_map.comp_continuous_linear_map` as a bundled continuous linear equiv,
given `f : Î  i, E i â‰ƒL[ğ•œ] Eâ‚ i`. -/
def comp_continuous_linear_map_equivL (f : Î  i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
  continuous_multilinear_map ğ•œ Eâ‚ G â‰ƒL[ğ•œ] continuous_multilinear_map ğ•œ E G :=
{ inv_fun := comp_continuous_linear_mapL (Î» i, ((f i).symm : Eâ‚ i â†’L[ğ•œ] E i)),
  continuous_to_fun := (comp_continuous_linear_mapL (Î» i, (f i : E i â†’L[ğ•œ] Eâ‚ i))).continuous,
  continuous_inv_fun :=
    (comp_continuous_linear_mapL (Î» i, ((f i).symm : Eâ‚ i â†’L[ğ•œ] E i))).continuous,
  left_inv := begin
    assume g,
    ext1 m,
    simp only [continuous_linear_map.to_linear_map_eq_coe, linear_map.to_fun_eq_coe,
      continuous_linear_map.coe_coe, comp_continuous_linear_mapL_apply,
      comp_continuous_linear_map_apply, continuous_linear_equiv.coe_coe,
      continuous_linear_equiv.apply_symm_apply],
  end,
  right_inv := begin
    assume g,
    ext1 m,
    simp only [continuous_linear_map.to_linear_map_eq_coe, comp_continuous_linear_mapL_apply,
      linear_map.to_fun_eq_coe, continuous_linear_map.coe_coe, comp_continuous_linear_map_apply,
      continuous_linear_equiv.coe_coe, continuous_linear_equiv.symm_apply_apply],
  end,
  .. comp_continuous_linear_mapL (Î» i, (f i : E i â†’L[ğ•œ] Eâ‚ i)) }

@[simp] lemma comp_continuous_linear_map_equivL_symm (f : Î  i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
  (comp_continuous_linear_map_equivL G f).symm =
    comp_continuous_linear_map_equivL G (Î» (i : Î¹), (f i).symm) :=
rfl

variable {G}

@[simp] lemma comp_continuous_linear_map_equivL_apply
  (g : continuous_multilinear_map ğ•œ Eâ‚ G) (f : Î  i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
  comp_continuous_linear_map_equivL G f g =
    g.comp_continuous_linear_map (Î» i, (f i : E i â†’L[ğ•œ] Eâ‚ i)) := rfl

end continuous_multilinear_map

section smul

variables {R : Type*} [semiring R] [module R G] [smul_comm_class ğ•œ R G]
  [has_continuous_const_smul R G]

instance : has_continuous_const_smul R (continuous_multilinear_map ğ•œ E G) :=
âŸ¨Î» c, (continuous_linear_map.comp_continuous_multilinear_mapL ğ•œ _ G G
  (c â€¢ continuous_linear_map.id ğ•œ G)).2âŸ©

end smul

section currying
/-!
### Currying

We associate to a continuous multilinear map in `n+1` variables (i.e., based on `fin n.succ`) two
curried functions, named `f.curry_left` (which is a continuous linear map on `E 0` taking values
in continuous multilinear maps in `n` variables) and `f.curry_right` (which is a continuous
multilinear map in `n` variables taking values in continuous linear maps on `E (last n)`).
The inverse operations are called `uncurry_left` and `uncurry_right`.

We also register continuous linear equiv versions of these correspondences, in
`continuous_multilinear_curry_left_equiv` and `continuous_multilinear_curry_right_equiv`.
-/
open fin function

lemma continuous_linear_map.norm_map_tail_le
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) (m : Î i, Ei i) :
  â€–f (m 0) (tail m)â€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€– :=
calc
  â€–f (m 0) (tail m)â€– â‰¤ â€–f (m 0)â€– * âˆ i, â€–(tail m) iâ€– : (f (m 0)).le_op_norm _
  ... â‰¤ (â€–fâ€– * â€–m 0â€–) * âˆ i, â€–(tail m) iâ€– :
    mul_le_mul_of_nonneg_right (f.le_op_norm _) (prod_nonneg (Î»i hi, norm_nonneg _))
  ... = â€–fâ€– * (â€–m 0â€– * âˆ i, â€–(tail m) iâ€–) : by ring
  ... = â€–fâ€– * âˆ i, â€–m iâ€– : by { rw prod_univ_succ, refl }

lemma continuous_multilinear_map.norm_map_init_le
  (f : continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G))
  (m : Î i, Ei i) :
  â€–f (init m) (m (last n))â€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€– :=
calc
  â€–f (init m) (m (last n))â€– â‰¤ â€–f (init m)â€– * â€–m (last n)â€– : (f (init m)).le_op_norm _
  ... â‰¤ (â€–fâ€– * (âˆ i, â€–(init m) iâ€–)) * â€–m (last n)â€– :
    mul_le_mul_of_nonneg_right (f.le_op_norm _) (norm_nonneg _)
  ... = â€–fâ€– * ((âˆ i, â€–(init m) iâ€–) * â€–m (last n)â€–) : mul_assoc _ _ _
  ... = â€–fâ€– * âˆ i, â€–m iâ€– : by { rw prod_univ_cast_succ, refl }

lemma continuous_multilinear_map.norm_map_cons_le
  (f : continuous_multilinear_map ğ•œ Ei G) (x : Ei 0) (m : Î (i : fin n), Ei i.succ) :
  â€–f (cons x m)â€– â‰¤ â€–fâ€– * â€–xâ€– * âˆ i, â€–m iâ€– :=
calc
  â€–f (cons x m)â€– â‰¤ â€–fâ€– * âˆ i, â€–cons x m iâ€– : f.le_op_norm _
  ... = (â€–fâ€– * â€–xâ€–) * âˆ i, â€–m iâ€– : by { rw prod_univ_succ, simp [mul_assoc] }

lemma continuous_multilinear_map.norm_map_snoc_le
  (f : continuous_multilinear_map ğ•œ Ei G) (m : Î (i : fin n), Ei i.cast_succ) (x : Ei (last n)) :
  â€–f (snoc m x)â€– â‰¤ â€–fâ€– * (âˆ i, â€–m iâ€–) * â€–xâ€– :=
calc
  â€–f (snoc m x)â€– â‰¤ â€–fâ€– * âˆ i, â€–snoc m x iâ€– : f.le_op_norm _
  ... = â€–fâ€– * (âˆ i, â€–m iâ€–) * â€–xâ€– : by { rw prod_univ_cast_succ, simp [mul_assoc] }

/-! #### Left currying -/

/-- Given a continuous linear map `f` from `E 0` to continuous multilinear maps on `n` variables,
construct the corresponding continuous multilinear map on `n+1` variables obtained by concatenating
the variables, given by `m â†¦ f (m 0) (tail m)`-/
def continuous_linear_map.uncurry_left
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) :
  continuous_multilinear_map ğ•œ Ei G :=
(@linear_map.uncurry_left ğ•œ n Ei G _ _ _ _ _
  (continuous_multilinear_map.to_multilinear_map_linear.comp f.to_linear_map)).mk_continuous
    (â€–fâ€–) (Î»m, continuous_linear_map.norm_map_tail_le f m)

@[simp] lemma continuous_linear_map.uncurry_left_apply
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) (m : Î i, Ei i) :
  f.uncurry_left m = f (m 0) (tail m) := rfl

/-- Given a continuous multilinear map `f` in `n+1` variables, split the first variable to obtain
a continuous linear map into continuous multilinear maps in `n` variables, given by
`x â†¦ (m â†¦ f (cons x m))`. -/
def continuous_multilinear_map.curry_left
  (f : continuous_multilinear_map ğ•œ Ei G) :
  Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G) :=
linear_map.mk_continuous
{ -- define a linear map into `n` continuous multilinear maps from an `n+1` continuous multilinear
  -- map
  to_fun    := Î»x, (f.to_multilinear_map.curry_left x).mk_continuous
    (â€–fâ€– * â€–xâ€–) (f.norm_map_cons_le x),
  map_add'  := Î»x y, by { ext m, exact f.cons_add m x y },
  map_smul' := Î»c x, by { ext m, exact f.cons_smul m c x } }
  -- then register its continuity thanks to its boundedness properties.
(â€–fâ€–) (Î»x, multilinear_map.mk_continuous_norm_le _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) _)

@[simp] lemma continuous_multilinear_map.curry_left_apply
  (f : continuous_multilinear_map ğ•œ Ei G) (x : Ei 0) (m : Î (i : fin n), Ei i.succ) :
  f.curry_left x m = f (cons x m) := rfl

@[simp] lemma continuous_linear_map.curry_uncurry_left
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) :
  f.uncurry_left.curry_left = f :=
begin
  ext m x,
  simp only [tail_cons, continuous_linear_map.uncurry_left_apply,
             continuous_multilinear_map.curry_left_apply],
  rw cons_zero
end

@[simp] lemma continuous_multilinear_map.uncurry_curry_left
  (f : continuous_multilinear_map ğ•œ Ei G) : f.curry_left.uncurry_left = f :=
continuous_multilinear_map.to_multilinear_map_injective $ f.to_multilinear_map.uncurry_curry_left

variables (ğ•œ Ei G)

/-- The space of continuous multilinear maps on `Î (i : fin (n+1)), E i` is canonically isomorphic to
the space of continuous linear maps from `E 0` to the space of continuous multilinear maps on
`Î (i : fin n), E i.succ `, by separating the first variable. We register this isomorphism in
`continuous_multilinear_curry_left_equiv ğ•œ E Eâ‚‚`. The algebraic version (without topology) is given
in `multilinear_curry_left_equiv ğ•œ E Eâ‚‚`.

The direct and inverse maps are given by `f.uncurry_left` and `f.curry_left`. Use these
unless you need the full framework of linear isometric equivs. -/
def continuous_multilinear_curry_left_equiv :
  (Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) â‰ƒâ‚—áµ¢[ğ•œ]
  (continuous_multilinear_map ğ•œ Ei G) :=
linear_isometry_equiv.of_bounds
  { to_fun    := continuous_linear_map.uncurry_left,
    map_add'  := Î»fâ‚ fâ‚‚, by { ext m, refl },
    map_smul' := Î»c f, by { ext m, refl },
    inv_fun   := continuous_multilinear_map.curry_left,
    left_inv  := continuous_linear_map.curry_uncurry_left,
    right_inv := continuous_multilinear_map.uncurry_curry_left }
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)
  (Î» f, linear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

variables {ğ•œ Ei G}

@[simp] lemma continuous_multilinear_curry_left_equiv_apply
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.succ) G)) (v : Î  i, Ei i) :
  continuous_multilinear_curry_left_equiv ğ•œ Ei G f v = f (v 0) (tail v) := rfl

@[simp] lemma continuous_multilinear_curry_left_equiv_symm_apply
  (f : continuous_multilinear_map ğ•œ Ei G) (x : Ei 0) (v : Î  i : fin n, Ei i.succ) :
  (continuous_multilinear_curry_left_equiv ğ•œ Ei G).symm f x v = f (cons x v) := rfl

@[simp] lemma continuous_multilinear_map.curry_left_norm
  (f : continuous_multilinear_map ğ•œ Ei G) : â€–f.curry_leftâ€– = â€–fâ€– :=
(continuous_multilinear_curry_left_equiv ğ•œ Ei G).symm.norm_map f

@[simp] lemma continuous_linear_map.uncurry_left_norm
  (f : Ei 0 â†’L[ğ•œ] (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.succ) G)) :
  â€–f.uncurry_leftâ€– = â€–fâ€– :=
(continuous_multilinear_curry_left_equiv ğ•œ Ei G).norm_map f

/-! #### Right currying -/

/-- Given a continuous linear map `f` from continuous multilinear maps on `n` variables to
continuous linear maps on `E 0`, construct the corresponding continuous multilinear map on `n+1`
variables obtained by concatenating the variables, given by `m â†¦ f (init m) (m (last n))`. -/
def continuous_multilinear_map.uncurry_right
  (f : continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) :
  continuous_multilinear_map ğ•œ Ei G :=
let f' : multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’â‚—[ğ•œ] G) :=
{ to_fun    := Î» m, (f m).to_linear_map,
  map_add'  := Î» _ m i x y, by simp,
  map_smul' := Î» _ m i c x, by simp } in
(@multilinear_map.uncurry_right ğ•œ n Ei G _ _ _ _ _ f').mk_continuous
  (â€–fâ€–) (Î»m, f.norm_map_init_le m)

@[simp] lemma continuous_multilinear_map.uncurry_right_apply
  (f : continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G))
  (m : Î i, Ei i) :
  f.uncurry_right m = f (init m) (m (last n)) := rfl

/-- Given a continuous multilinear map `f` in `n+1` variables, split the last variable to obtain
a continuous multilinear map in `n` variables into continuous linear maps, given by
`m â†¦ (x â†¦ f (snoc m x))`. -/
def continuous_multilinear_map.curry_right
  (f : continuous_multilinear_map ğ•œ Ei G) :
  continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G) :=
let f' : multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G) :=
{ to_fun    := Î»m, (f.to_multilinear_map.curry_right m).mk_continuous
    (â€–fâ€– * âˆ i, â€–m iâ€–) $ Î»x, f.norm_map_snoc_le m x,
  map_add'  := Î» _ m i x y, by { simp, refl },
  map_smul' := Î» _ m i c x, by { simp, refl } } in
f'.mk_continuous (â€–fâ€–) (Î»m, linear_map.mk_continuous_norm_le _
  (mul_nonneg (norm_nonneg _) (prod_nonneg (Î»j hj, norm_nonneg _))) _)

@[simp] lemma continuous_multilinear_map.curry_right_apply
  (f : continuous_multilinear_map ğ•œ Ei G) (m : Î  i : fin n, Ei i.cast_succ) (x : Ei (last n)) :
  f.curry_right m x = f (snoc m x) := rfl

@[simp] lemma continuous_multilinear_map.curry_uncurry_right
  (f : continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) :
  f.uncurry_right.curry_right = f :=
begin
  ext m x,
  simp only [snoc_last, continuous_multilinear_map.curry_right_apply,
             continuous_multilinear_map.uncurry_right_apply],
  rw init_snoc
end

@[simp] lemma continuous_multilinear_map.uncurry_curry_right
  (f : continuous_multilinear_map ğ•œ Ei G) : f.curry_right.uncurry_right = f :=
by { ext m, simp }

variables (ğ•œ Ei G)

/--
The space of continuous multilinear maps on `Î (i : fin (n+1)), Ei i` is canonically isomorphic to
the space of continuous multilinear maps on `Î (i : fin n), Ei i.cast_succ` with values in the space
of continuous linear maps on `Ei (last n)`, by separating the last variable. We register this
isomorphism as a continuous linear equiv in `continuous_multilinear_curry_right_equiv ğ•œ Ei G`.
The algebraic version (without topology) is given in `multilinear_curry_right_equiv ğ•œ Ei G`.

The direct and inverse maps are given by `f.uncurry_right` and `f.curry_right`. Use these
unless you need the full framework of linear isometric equivs.
-/
def continuous_multilinear_curry_right_equiv :
  (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) â‰ƒâ‚—áµ¢[ğ•œ]
  (continuous_multilinear_map ğ•œ Ei G) :=
linear_isometry_equiv.of_bounds
  { to_fun    := continuous_multilinear_map.uncurry_right,
    map_add'  := Î»fâ‚ fâ‚‚, by { ext m, refl },
    map_smul' := Î»c f, by { ext m, refl },
    inv_fun   := continuous_multilinear_map.curry_right,
    left_inv  := continuous_multilinear_map.curry_uncurry_right,
    right_inv := continuous_multilinear_map.uncurry_curry_right }
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

variables (n G')

/-- The space of continuous multilinear maps on `Î (i : fin (n+1)), G` is canonically isomorphic to
the space of continuous multilinear maps on `Î (i : fin n), G` with values in the space
of continuous linear maps on `G`, by separating the last variable. We register this
isomorphism as a continuous linear equiv in `continuous_multilinear_curry_right_equiv' ğ•œ n G G'`.
For a version allowing dependent types, see `continuous_multilinear_curry_right_equiv`. When there
are no dependent types, use the primed version as it helps Lean a lot for unification.

The direct and inverse maps are given by `f.uncurry_right` and `f.curry_right`. Use these
unless you need the full framework of linear isometric equivs. -/
def continuous_multilinear_curry_right_equiv' :
  (G [Ã—n]â†’L[ğ•œ] (G â†’L[ğ•œ] G')) â‰ƒâ‚—áµ¢[ğ•œ] (G [Ã—n.succ]â†’L[ğ•œ] G') :=
continuous_multilinear_curry_right_equiv ğ•œ (Î» (i : fin n.succ), G) G'

variables {n ğ•œ G Ei G'}

@[simp] lemma continuous_multilinear_curry_right_equiv_apply
  (f : (continuous_multilinear_map ğ•œ (Î»(i : fin n), Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)))
  (v : Î  i, Ei i) :
  (continuous_multilinear_curry_right_equiv ğ•œ Ei G) f v = f (init v) (v (last n)) := rfl

@[simp] lemma continuous_multilinear_curry_right_equiv_symm_apply
  (f : continuous_multilinear_map ğ•œ Ei G)
  (v : Î  (i : fin n), Ei i.cast_succ) (x : Ei (last n)) :
  (continuous_multilinear_curry_right_equiv ğ•œ Ei G).symm f v x = f (snoc v x) := rfl

@[simp] lemma continuous_multilinear_curry_right_equiv_apply'
  (f : G [Ã—n]â†’L[ğ•œ] (G â†’L[ğ•œ] G')) (v : fin (n + 1) â†’ G) :
  continuous_multilinear_curry_right_equiv' ğ•œ n G G' f v = f (init v) (v (last n)) := rfl

@[simp] lemma continuous_multilinear_curry_right_equiv_symm_apply'
  (f : G [Ã—n.succ]â†’L[ğ•œ] G') (v : fin n â†’ G) (x : G) :
  (continuous_multilinear_curry_right_equiv' ğ•œ n G G').symm f v x = f (snoc v x) := rfl

@[simp] lemma continuous_multilinear_map.curry_right_norm
  (f : continuous_multilinear_map ğ•œ Ei G) : â€–f.curry_rightâ€– = â€–fâ€– :=
(continuous_multilinear_curry_right_equiv ğ•œ Ei G).symm.norm_map f

@[simp] lemma continuous_multilinear_map.uncurry_right_norm
  (f : continuous_multilinear_map ğ•œ (Î» i : fin n, Ei i.cast_succ) (Ei (last n) â†’L[ğ•œ] G)) :
  â€–f.uncurry_rightâ€– = â€–fâ€– :=
(continuous_multilinear_curry_right_equiv ğ•œ Ei G).norm_map f

/-!
#### Currying with `0` variables

The space of multilinear maps with `0` variables is trivial: such a multilinear map is just an
arbitrary constant (note that multilinear maps in `0` variables need not map `0` to `0`!).
Therefore, the space of continuous multilinear maps on `(fin 0) â†’ G` with values in `Eâ‚‚` is
isomorphic (and even isometric) to `Eâ‚‚`. As this is the zeroth step in the construction of iterated
derivatives, we register this isomorphism. -/

section

variables {ğ•œ G G'}

/-- Associating to a continuous multilinear map in `0` variables the unique value it takes. -/
def continuous_multilinear_map.uncurry0
  (f : continuous_multilinear_map ğ•œ (Î» (i : fin 0), G) G') : G' := f 0

variables (ğ•œ G)
/-- Associating to an element `x` of a vector space `Eâ‚‚` the continuous multilinear map in `0`
variables taking the (unique) value `x` -/
def continuous_multilinear_map.curry0 (x : G') : G [Ã—0]â†’L[ğ•œ] G' :=
continuous_multilinear_map.const_of_is_empty ğ•œ _ x

variable {G}
@[simp] lemma continuous_multilinear_map.curry0_apply (x : G') (m : (fin 0) â†’ G) :
  continuous_multilinear_map.curry0 ğ•œ G x m = x := rfl

variable {ğ•œ}
@[simp] lemma continuous_multilinear_map.uncurry0_apply (f : G [Ã—0]â†’L[ğ•œ] G') :
  f.uncurry0 = f 0 := rfl

@[simp] lemma continuous_multilinear_map.apply_zero_curry0 (f : G [Ã—0]â†’L[ğ•œ] G') {x : fin 0 â†’ G} :
  continuous_multilinear_map.curry0 ğ•œ G (f x) = f :=
by { ext m, simp [(subsingleton.elim _ _ : x = m)] }

lemma continuous_multilinear_map.uncurry0_curry0 (f : G [Ã—0]â†’L[ğ•œ] G') :
  continuous_multilinear_map.curry0 ğ•œ G (f.uncurry0) = f :=
by simp

variables (ğ•œ G)
@[simp] lemma continuous_multilinear_map.curry0_uncurry0 (x : G') :
  (continuous_multilinear_map.curry0 ğ•œ G x).uncurry0 = x := rfl

@[simp] lemma continuous_multilinear_map.curry0_norm (x : G')  :
  â€–continuous_multilinear_map.curry0 ğ•œ G xâ€– = â€–xâ€– :=
norm_const_of_is_empty _ _ _

variables {ğ•œ G}
@[simp] lemma continuous_multilinear_map.fin0_apply_norm (f : G [Ã—0]â†’L[ğ•œ] G') {x : fin 0 â†’ G} :
  â€–f xâ€– = â€–fâ€– :=
begin
  obtain rfl : x = 0 := subsingleton.elim _ _,
  refine le_antisymm (by simpa using f.le_op_norm 0) _,
  have : â€–continuous_multilinear_map.curry0 ğ•œ G (f.uncurry0)â€– â‰¤ â€–f.uncurry0â€– :=
    continuous_multilinear_map.op_norm_le_bound _ (norm_nonneg _) (Î»m,
      by simp [-continuous_multilinear_map.apply_zero_curry0]),
  simpa
end

lemma continuous_multilinear_map.uncurry0_norm (f : G [Ã—0]â†’L[ğ•œ] G') : â€–f.uncurry0â€– = â€–fâ€– :=
by simp

variables (ğ•œ G G')
/-- The continuous linear isomorphism between elements of a normed space, and continuous multilinear
maps in `0` variables with values in this normed space.

The direct and inverse maps are `uncurry0` and `curry0`. Use these unless you need the full
framework of linear isometric equivs. -/
def continuous_multilinear_curry_fin0 : (G [Ã—0]â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ] G' :=
{ to_fun    := Î»f, continuous_multilinear_map.uncurry0 f,
  inv_fun   := Î»f, continuous_multilinear_map.curry0 ğ•œ G f,
  map_add'  := Î»f g, rfl,
  map_smul' := Î»c f, rfl,
  left_inv  := continuous_multilinear_map.uncurry0_curry0,
  right_inv := continuous_multilinear_map.curry0_uncurry0 ğ•œ G,
  norm_map' := continuous_multilinear_map.uncurry0_norm }

variables {ğ•œ G G'}

@[simp] lemma continuous_multilinear_curry_fin0_apply (f : G [Ã—0]â†’L[ğ•œ] G') :
  continuous_multilinear_curry_fin0 ğ•œ G G' f = f 0 := rfl

@[simp] lemma continuous_multilinear_curry_fin0_symm_apply (x : G') (v : (fin 0) â†’ G) :
  (continuous_multilinear_curry_fin0 ğ•œ G G').symm x v = x := rfl

end

/-! #### With 1 variable -/

variables (ğ•œ G G')

/-- Continuous multilinear maps from `G^1` to `G'` are isomorphic with continuous linear maps from
`G` to `G'`. -/
def continuous_multilinear_curry_fin1 : (G [Ã—1]â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ] (G â†’L[ğ•œ] G') :=
(continuous_multilinear_curry_right_equiv ğ•œ (Î» (i : fin 1), G) G').symm.trans
(continuous_multilinear_curry_fin0 ğ•œ G (G â†’L[ğ•œ] G'))

variables {ğ•œ G G'}

@[simp] lemma continuous_multilinear_curry_fin1_apply (f : G [Ã—1]â†’L[ğ•œ] G') (x : G) :
  continuous_multilinear_curry_fin1 ğ•œ G G' f x = f (fin.snoc 0 x) := rfl

@[simp] lemma continuous_multilinear_curry_fin1_symm_apply
  (f : G â†’L[ğ•œ] G') (v : (fin 1) â†’ G) :
  (continuous_multilinear_curry_fin1 ğ•œ G G').symm f v = f (v 0) := rfl

namespace continuous_multilinear_map

variables (ğ•œ G G')

/-- An equivalence of the index set defines a linear isometric equivalence between the spaces
of multilinear maps. -/
def dom_dom_congr (Ïƒ : Î¹ â‰ƒ Î¹') :
  continuous_multilinear_map ğ•œ (Î» _ : Î¹, G) G' â‰ƒâ‚—áµ¢[ğ•œ]
    continuous_multilinear_map ğ•œ (Î» _ : Î¹', G) G' :=
linear_isometry_equiv.of_bounds
  { to_fun := Î» f, (multilinear_map.dom_dom_congr Ïƒ f.to_multilinear_map).mk_continuous â€–fâ€– $
      Î» m, (f.le_op_norm (Î» i, m (Ïƒ i))).trans_eq $ by rw [â† Ïƒ.prod_comp],
    inv_fun := Î» f, (multilinear_map.dom_dom_congr Ïƒ.symm f.to_multilinear_map).mk_continuous â€–fâ€– $
      Î» m, (f.le_op_norm (Î» i, m (Ïƒ.symm i))).trans_eq $ by rw [â† Ïƒ.symm.prod_comp],
    left_inv := Î» f, ext $ Î» m, congr_arg f $ by simp only [Ïƒ.symm_apply_apply],
    right_inv := Î» f, ext $ Î» m, congr_arg f $ by simp only [Ïƒ.apply_symm_apply],
    map_add' := Î» f g, rfl,
    map_smul' := Î» c f, rfl }
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

variables {ğ•œ G G'}

section

/-- A continuous multilinear map with variables indexed by `Î¹ âŠ• Î¹'` defines a continuous multilinear
map with variables indexed by `Î¹` taking values in the space of continuous multilinear maps with
variables indexed by `Î¹'`. -/
def curry_sum (f : continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G') :
  continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G') :=
multilinear_map.mk_continuous_multilinear (multilinear_map.curry_sum f.to_multilinear_map) (â€–fâ€–) $
  Î» m m', by simpa [fintype.prod_sum_type, mul_assoc] using f.le_op_norm (sum.elim m m')

@[simp] lemma curry_sum_apply (f : continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G')
  (m : Î¹ â†’ G) (m' : Î¹' â†’ G) :
  f.curry_sum m m' = f (sum.elim m m') :=
rfl

/-- A continuous multilinear map with variables indexed by `Î¹` taking values in the space of
continuous multilinear maps with variables indexed by `Î¹'` defines a continuous multilinear map with
variables indexed by `Î¹ âŠ• Î¹'`. -/
def uncurry_sum
  (f : continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G')) :
  continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G' :=
multilinear_map.mk_continuous
  (to_multilinear_map_linear.comp_multilinear_map f.to_multilinear_map).uncurry_sum (â€–fâ€–) $ Î» m,
  by simpa [fintype.prod_sum_type, mul_assoc]
    using (f (m âˆ˜ sum.inl)).le_of_op_norm_le (m âˆ˜ sum.inr) (f.le_op_norm _)

@[simp] lemma uncurry_sum_apply
  (f : continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G'))
  (m : Î¹ âŠ• Î¹' â†’ G) :
  f.uncurry_sum m = f (m âˆ˜ sum.inl) (m âˆ˜ sum.inr) :=
rfl

variables (ğ•œ Î¹ Î¹' G G')

/-- Linear isometric equivalence between the space of continuous multilinear maps with variables
indexed by `Î¹ âŠ• Î¹'` and the space of continuous multilinear maps with variables indexed by `Î¹`
taking values in the space of continuous multilinear maps with variables indexed by `Î¹'`.

The forward and inverse functions are `continuous_multilinear_map.curry_sum`
and `continuous_multilinear_map.uncurry_sum`. Use this definition only if you need
some properties of `linear_isometry_equiv`. -/
def curry_sum_equiv : continuous_multilinear_map ğ•œ (Î» x : Î¹ âŠ• Î¹', G) G' â‰ƒâ‚—áµ¢[ğ•œ]
  continuous_multilinear_map ğ•œ (Î» x : Î¹, G) (continuous_multilinear_map ğ•œ (Î» x : Î¹', G) G') :=
linear_isometry_equiv.of_bounds
  { to_fun := curry_sum,
    inv_fun := uncurry_sum,
    map_add' := Î» f g, by { ext, refl },
    map_smul' := Î» c f, by { ext, refl },
    left_inv := Î» f, by { ext m, exact congr_arg f (sum.elim_comp_inl_inr m) },
    right_inv := Î» f, by { ext mâ‚ mâ‚‚, change f _ _ = f _ _,
      rw [sum.elim_comp_inl, sum.elim_comp_inr] } }
  (Î» f, multilinear_map.mk_continuous_multilinear_norm_le _ (norm_nonneg f) _)
  (Î» f, multilinear_map.mk_continuous_norm_le _ (norm_nonneg f) _)

end

section

variables (ğ•œ G G') {k l : â„•} {s : finset (fin n)}

/-- If `s : finset (fin n)` is a finite set of cardinality `k` and its complement has cardinality
`l`, then the space of continuous multilinear maps `G [Ã—n]â†’L[ğ•œ] G'` of `n` variables is isomorphic
to the space of continuous multilinear maps `G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G'` of `k` variables taking
values in the space of continuous multilinear maps of `l` variables. -/
def curry_fin_finset {k l n : â„•} {s : finset (fin n)}
  (hk : s.card = k) (hl : sá¶œ.card = l) :
  (G [Ã—n]â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ] (G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') :=
(dom_dom_congr ğ•œ G G' (fin_sum_equiv_of_finset hk hl).symm).trans
  (curry_sum_equiv ğ•œ (fin k) (fin l) G G')

variables {ğ•œ G G'}

@[simp] lemma curry_fin_finset_apply (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—n]â†’L[ğ•œ] G') (mk : fin k â†’ G) (ml : fin l â†’ G) :
  curry_fin_finset ğ•œ G G' hk hl f mk ml =
    f (Î» i, sum.elim mk ml ((fin_sum_equiv_of_finset hk hl).symm i)) :=
rfl

@[simp] lemma curry_fin_finset_symm_apply (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') (m : fin n â†’ G) :
  (curry_fin_finset ğ•œ G G' hk hl).symm f m =
    f (Î» i, m $ fin_sum_equiv_of_finset hk hl (sum.inl i))
      (Î» i, m $ fin_sum_equiv_of_finset hk hl (sum.inr i)) :=
rfl

@[simp] lemma curry_fin_finset_symm_apply_piecewise_const (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') (x y : G) :
  (curry_fin_finset ğ•œ G G' hk hl).symm f (s.piecewise (Î» _, x) (Î» _, y)) = f (Î» _, x) (Î» _, y) :=
multilinear_map.curry_fin_finset_symm_apply_piecewise_const hk hl _ x y

@[simp] lemma curry_fin_finset_symm_apply_const (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—k]â†’L[ğ•œ] G [Ã—l]â†’L[ğ•œ] G') (x : G) :
  (curry_fin_finset ğ•œ G G' hk hl).symm f (Î» _, x) = f (Î» _, x) (Î» _, x) :=
rfl

@[simp] lemma curry_fin_finset_apply_const (hk : s.card = k) (hl : sá¶œ.card = l)
  (f : G [Ã—n]â†’L[ğ•œ] G') (x y : G) :
  curry_fin_finset ğ•œ G G' hk hl f (Î» _, x) (Î» _, y) = f (s.piecewise (Î» _, x) (Î» _, y)) :=
begin
  refine (curry_fin_finset_symm_apply_piecewise_const hk hl _ _ _).symm.trans _, -- `rw` fails
  rw linear_isometry_equiv.symm_apply_apply
end

end

end continuous_multilinear_map

end currying
