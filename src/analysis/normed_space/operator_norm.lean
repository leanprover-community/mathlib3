/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, S√©bastien Gou√´zel, Jean Lo
-/
import linear_algebra.finite_dimensional
import analysis.normed_space.riesz_lemma
import analysis.asymptotics

/-!
# Operator norm on the space of continuous linear maps

Define the operator norm on the space of continuous linear maps between normed spaces, and prove
its basic properties. In particular, show that this space is itself a normed space.
-/

noncomputable theory
open_locale classical


variables {ùïú : Type*} {E : Type*} {F : Type*} {G : Type*}
[normed_group E] [normed_group F] [normed_group G]

open metric continuous_linear_map

lemma exists_pos_bound_of_bound {f : E ‚Üí F} (M : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ M * ‚à•x‚à•) :
  ‚àÉ N, 0 < N ‚àß ‚àÄx, ‚à•f x‚à• ‚â§ N * ‚à•x‚à• :=
‚ü®max M 1, lt_of_lt_of_le zero_lt_one (le_max_right _ _), Œªx, calc
  ‚à•f x‚à• ‚â§ M * ‚à•x‚à• : h x
  ... ‚â§ max M 1 * ‚à•x‚à• : mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _) ‚ü©

section normed_field
/- Most statements in this file require the field to be non-discrete, as this is necessary
to deduce an inequality `‚à•f x‚à• ‚â§ C ‚à•x‚à•` from the continuity of f. However, the other direction always
holds. In this section, we just assume that `ùïú` is a normed field. In the remainder of the file,
it will be non-discrete. -/

variables [normed_field ùïú] [normed_space ùïú E] [normed_space ùïú F] (f : E ‚Üí‚Çó[ùïú] F)

lemma linear_map.lipschitz_of_bound (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  lipschitz_with (nnreal.of_real C) f :=
lipschitz_with.of_dist_le' $ Œª x y, by simpa only [dist_eq_norm, f.map_sub] using h (x - y)

theorem linear_map.antilipschitz_of_bound {K : nnreal} (h : ‚àÄ x, ‚à•x‚à• ‚â§ K * ‚à•f x‚à•) :
  antilipschitz_with K f :=
antilipschitz_with.of_le_mul_dist $
Œª x y, by simpa only [dist_eq_norm, f.map_sub] using h (x - y)

lemma linear_map.uniform_continuous_of_bound (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  uniform_continuous f :=
(f.lipschitz_of_bound C h).uniform_continuous

lemma linear_map.continuous_of_bound (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  continuous f :=
(f.lipschitz_of_bound C h).continuous

/-- Construct a continuous linear map from a linear map and a bound on this linear map.
The fact that the norm of the continuous linear map is then controlled is given in
`linear_map.mk_continuous_norm_le`. -/
def linear_map.mk_continuous (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) : E ‚ÜíL[ùïú] F :=
‚ü®f, linear_map.continuous_of_bound f C h‚ü©

/-- Reinterpret a linear map `ùïú ‚Üí‚Çó[ùïú] E` as a continuous linear map. This construction
is generalized to the case of any finite dimensional domain
in `linear_map.to_continuous_linear_map`. -/
def linear_map.to_continuous_linear_map‚ÇÅ (f : ùïú ‚Üí‚Çó[ùïú] E) : ùïú ‚ÜíL[ùïú] E :=
f.mk_continuous (‚à•f 1‚à•) $ Œª x, le_of_eq $
by { conv_lhs { rw ‚Üê mul_one x }, rw [‚Üê smul_eq_mul, f.map_smul, norm_smul, mul_comm] }

/-- Construct a continuous linear map from a linear map and the existence of a bound on this linear
map. If you have an explicit bound, use `linear_map.mk_continuous` instead, as a norm estimate will
follow automatically in `linear_map.mk_continuous_norm_le`. -/
def linear_map.mk_continuous_of_exists_bound (h : ‚àÉC, ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) : E ‚ÜíL[ùïú] F :=
‚ü®f, let ‚ü®C, hC‚ü© := h in linear_map.continuous_of_bound f C hC‚ü©

@[simp, norm_cast] lemma linear_map.mk_continuous_coe (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  ((f.mk_continuous C h) : E ‚Üí‚Çó[ùïú] F) = f := rfl

@[simp] lemma linear_map.mk_continuous_apply (C : ‚Ñù) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) (x : E) :
  f.mk_continuous C h x = f x := rfl

@[simp, norm_cast] lemma linear_map.mk_continuous_of_exists_bound_coe (h : ‚àÉC, ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  ((f.mk_continuous_of_exists_bound h) : E ‚Üí‚Çó[ùïú] F) = f := rfl

@[simp] lemma linear_map.mk_continuous_of_exists_bound_apply (h : ‚àÉC, ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) (x : E) :
  f.mk_continuous_of_exists_bound h x = f x := rfl

@[simp] lemma linear_map.to_continuous_linear_map‚ÇÅ_coe (f : ùïú ‚Üí‚Çó[ùïú] E) :
  (f.to_continuous_linear_map‚ÇÅ : ùïú ‚Üí‚Çó[ùïú] E) = f :=
rfl

@[simp] lemma linear_map.to_continuous_linear_map‚ÇÅ_apply (f : ùïú ‚Üí‚Çó[ùïú] E) (x) :
  f.to_continuous_linear_map‚ÇÅ x = f x :=
rfl

lemma linear_map.continuous_iff_is_closed_ker {f : E ‚Üí‚Çó[ùïú] ùïú} :
  continuous f ‚Üî is_closed (f.ker : set E) :=
begin
  -- the continuity of f obviously implies that its kernel is closed
  refine ‚ü®Œªh, (continuous_iff_is_closed.1 h) {0} (t1_space.t1 0), Œªh, _‚ü©,
  -- for the other direction, we assume that the kernel is closed
  by_cases hf : ‚àÄx, x ‚àà f.ker,
  { -- if `f = 0`, its continuity is obvious
    have : (f : E ‚Üí ùïú) = (Œªx, 0), by { ext x, simpa using hf x },
    rw this,
    exact continuous_const },
  { /- if `f` is not zero, we use an element `x‚ÇÄ ‚àâ ker f` such that `‚à•x‚ÇÄ‚à• ‚â§ 2 ‚à•x‚ÇÄ - y‚à•` for all
    `y ‚àà ker f`, given by Riesz's lemma, and prove that `2 ‚à•f x‚ÇÄ‚à• / ‚à•x‚ÇÄ‚à•` gives a bound on the
    operator norm of `f`. For this, start from an arbitrary `x` and note that
    `y = x‚ÇÄ - (f x‚ÇÄ / f x) x` belongs to the kernel of `f`. Applying the above inequality to `x‚ÇÄ`
    and `y` readily gives the conclusion. -/
    push_neg at hf,
    let r : ‚Ñù := (2 : ‚Ñù)‚Åª¬π,
    have : 0 ‚â§ r, by norm_num [r],
    have : r < 1, by norm_num [r],
    obtain ‚ü®x‚ÇÄ, x‚ÇÄker, h‚ÇÄ‚ü© : ‚àÉ (x‚ÇÄ : E), x‚ÇÄ ‚àâ f.ker ‚àß ‚àÄ y ‚àà linear_map.ker f, r * ‚à•x‚ÇÄ‚à• ‚â§ ‚à•x‚ÇÄ - y‚à•,
      from riesz_lemma h hf this,
    have : x‚ÇÄ ‚â† 0,
    { assume h,
      have : x‚ÇÄ ‚àà f.ker, by { rw h, exact (linear_map.ker f).zero_mem },
      exact x‚ÇÄker this },
    have rx‚ÇÄ_ne_zero : r * ‚à•x‚ÇÄ‚à• ‚â† 0, by { simp [norm_eq_zero, this], norm_num },
    have : ‚àÄx, ‚à•f x‚à• ‚â§ (((r * ‚à•x‚ÇÄ‚à•)‚Åª¬π) * ‚à•f x‚ÇÄ‚à•) * ‚à•x‚à•,
    { assume x,
      by_cases hx : f x = 0,
      { rw [hx, norm_zero],
        apply_rules [mul_nonneg, norm_nonneg, inv_nonneg.2] },
      { let y := x‚ÇÄ - (f x‚ÇÄ * (f x)‚Åª¬π ) ‚Ä¢ x,
        have fy_zero : f y = 0, by calc
          f y = f x‚ÇÄ - (f x‚ÇÄ * (f x)‚Åª¬π ) * f x : by simp [y]
          ... = 0 :
            by { rw [mul_assoc, inv_mul_cancel hx, mul_one, sub_eq_zero_of_eq], refl },
        have A : r * ‚à•x‚ÇÄ‚à• ‚â§ ‚à•f x‚ÇÄ‚à• * ‚à•f x‚à•‚Åª¬π * ‚à•x‚à•, from calc
          r * ‚à•x‚ÇÄ‚à• ‚â§ ‚à•x‚ÇÄ - y‚à• : h‚ÇÄ _ (linear_map.mem_ker.2 fy_zero)
          ... = ‚à•(f x‚ÇÄ * (f x)‚Åª¬π ) ‚Ä¢ x‚à• : by { dsimp [y], congr, abel }
          ... = ‚à•f x‚ÇÄ‚à• * ‚à•f x‚à•‚Åª¬π * ‚à•x‚à• :
            by rw [norm_smul, normed_field.norm_mul, normed_field.norm_inv],
        calc
          ‚à•f x‚à• = (r * ‚à•x‚ÇÄ‚à•)‚Åª¬π * (r * ‚à•x‚ÇÄ‚à•) * ‚à•f x‚à• : by rwa [inv_mul_cancel, one_mul]
          ... ‚â§ (r * ‚à•x‚ÇÄ‚à•)‚Åª¬π * (‚à•f x‚ÇÄ‚à• * ‚à•f x‚à•‚Åª¬π * ‚à•x‚à•) * ‚à•f x‚à• : begin
            apply mul_le_mul_of_nonneg_right (mul_le_mul_of_nonneg_left A _) (norm_nonneg _),
            exact inv_nonneg.2 (mul_nonneg (by norm_num) (norm_nonneg _))
          end
          ... = (‚à•f x‚à• ‚Åª¬π * ‚à•f x‚à•) * (((r * ‚à•x‚ÇÄ‚à•)‚Åª¬π) * ‚à•f x‚ÇÄ‚à•) * ‚à•x‚à• : by ring
          ... = (((r * ‚à•x‚ÇÄ‚à•)‚Åª¬π) * ‚à•f x‚ÇÄ‚à•) * ‚à•x‚à• :
            by { rw [inv_mul_cancel, one_mul], simp [norm_eq_zero, hx] } } },
    exact linear_map.continuous_of_bound f _ this }
end

end normed_field

variables [nondiscrete_normed_field ùïú] [normed_space ùïú E] [normed_space ùïú F] [normed_space ùïú G]
(c : ùïú) (f g : E ‚ÜíL[ùïú] F) (h : F ‚ÜíL[ùïú] G) (x y z : E)
include ùïú

/-- A continuous linear map between normed spaces is bounded when the field is nondiscrete.
The continuity ensures boundedness on a ball of some radius `Œ¥`. The nondiscreteness is then
used to rescale any element into an element of norm in `[Œ¥/C, Œ¥]`, whose image has a controlled norm.
The norm control for the original element follows by rescaling. -/
lemma linear_map.bound_of_continuous (f : E ‚Üí‚Çó[ùïú] F) (hf : continuous f) :
  ‚àÉ C, 0 < C ‚àß (‚àÄ x : E, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :=
begin
  have : continuous_at f 0 := continuous_iff_continuous_at.1 hf _,
  rcases metric.tendsto_nhds_nhds.1 this 1 zero_lt_one with ‚ü®Œµ, Œµ_pos, hŒµ‚ü©,
  let Œ¥ := Œµ/2,
  have Œ¥_pos : Œ¥ > 0 := half_pos Œµ_pos,
  have H : ‚àÄ{a}, ‚à•a‚à• ‚â§ Œ¥ ‚Üí ‚à•f a‚à• ‚â§ 1,
  { assume a ha,
    have : dist (f a) (f 0) ‚â§ 1,
    { apply le_of_lt (hŒµ _),
      rw [dist_eq_norm, sub_zero],
      exact lt_of_le_of_lt ha (half_lt_self Œµ_pos) },
    simpa using this },
  rcases normed_field.exists_one_lt_norm ùïú with ‚ü®c, hc‚ü©,
  refine ‚ü®Œ¥‚Åª¬π * ‚à•c‚à•, mul_pos (inv_pos.2 Œ¥_pos) (lt_trans zero_lt_one hc), (Œªx, _)‚ü©,
  by_cases h : x = 0,
  { simp only [h, norm_zero, mul_zero, linear_map.map_zero] },
  { rcases rescale_to_shell hc Œ¥_pos h with ‚ü®d, hd, dxle, ledx, dinv‚ü©,
    calc ‚à•f x‚à•
      = ‚à•f ((d‚Åª¬π * d) ‚Ä¢ x)‚à• : by rwa [inv_mul_cancel, one_smul]
      ... = ‚à•d‚à•‚Åª¬π * ‚à•f (d ‚Ä¢ x)‚à• :
        by rw [mul_smul, linear_map.map_smul, norm_smul, normed_field.norm_inv]
      ... ‚â§ ‚à•d‚à•‚Åª¬π * 1 :
        mul_le_mul_of_nonneg_left (H dxle) (by { rw ‚Üê normed_field.norm_inv, exact norm_nonneg _ })
      ... ‚â§ Œ¥‚Åª¬π * ‚à•c‚à• * ‚à•x‚à• : by { rw mul_one, exact dinv } }
end

namespace continuous_linear_map

theorem bound : ‚àÉ C, 0 < C ‚àß (‚àÄ x : E, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :=
f.to_linear_map.bound_of_continuous f.2

section
open asymptotics filter

theorem is_O_id (l : filter E) : is_O f (Œª x, x) l :=
let ‚ü®M, hMp, hM‚ü© := f.bound in is_O_of_le' l hM

theorem is_O_comp {Œ± : Type*} (g : F ‚ÜíL[ùïú] G) (f : Œ± ‚Üí F) (l : filter Œ±) :
  is_O (Œª x', g (f x')) f l :=
(g.is_O_id ‚ä§).comp_tendsto le_top

theorem is_O_sub (f : E ‚ÜíL[ùïú] F) (l : filter E) (x : E) :
  is_O (Œª x', f (x' - x)) (Œª x', x' - x) l :=
f.is_O_comp _ l

/-- A linear map which is a homothety is a continuous linear map.
    Since the field `ùïú` need not have `‚Ñù` as a subfield, this theorem is not directly deducible from
    the corresponding theorem about isometries plus a theorem about scalar multiplication.  Likewise
    for the other theorems about homotheties in this file.
 -/
def of_homothety (f : E ‚Üí‚Çó[ùïú] F) (a : ‚Ñù) (hf : ‚àÄx, ‚à•f x‚à• = a * ‚à•x‚à•) : E ‚ÜíL[ùïú] F :=
f.mk_continuous a (Œª x, le_of_eq (hf x))

variable (ùïú)

lemma to_span_singleton_homothety (x : E) (c : ùïú) : ‚à•linear_map.to_span_singleton ùïú E x c‚à• = ‚à•x‚à• * ‚à•c‚à• :=
by {rw mul_comm, exact norm_smul _ _}

/-- Given an element `x` of a normed space `E` over a field `ùïú`, the natural continuous
    linear map from `E` to the span of `x`.-/
def to_span_singleton (x : E) : ùïú ‚ÜíL[ùïú] E :=
of_homothety (linear_map.to_span_singleton ùïú E x) ‚à•x‚à• (to_span_singleton_homothety ùïú x)

end

section op_norm
open set real


/-- The operator norm of a continuous linear map is the inf of all its bounds. -/
def op_norm := Inf {c | 0 ‚â§ c ‚àß ‚àÄ x, ‚à•f x‚à• ‚â§ c * ‚à•x‚à•}
instance has_op_norm : has_norm (E ‚ÜíL[ùïú] F) := ‚ü®op_norm‚ü©

lemma norm_def : ‚à•f‚à• = Inf {c | 0 ‚â§ c ‚àß ‚àÄ x, ‚à•f x‚à• ‚â§ c * ‚à•x‚à•} := rfl

-- So that invocations of `real.Inf_le` make sense: we show that the set of
-- bounds is nonempty and bounded below.
lemma bounds_nonempty {f : E ‚ÜíL[ùïú] F} :
  ‚àÉ c, c ‚àà { c | 0 ‚â§ c ‚àß ‚àÄ x, ‚à•f x‚à• ‚â§ c * ‚à•x‚à• } :=
let ‚ü®M, hMp, hMb‚ü© := f.bound in ‚ü®M, le_of_lt hMp, hMb‚ü©

lemma bounds_bdd_below {f : E ‚ÜíL[ùïú] F} :
  bdd_below { c | 0 ‚â§ c ‚àß ‚àÄ x, ‚à•f x‚à• ‚â§ c * ‚à•x‚à• } :=
‚ü®0, Œª _ ‚ü®hn, _‚ü©, hn‚ü©

lemma op_norm_nonneg : 0 ‚â§ ‚à•f‚à• :=
lb_le_Inf _ bounds_nonempty (Œª _ ‚ü®hx, _‚ü©, hx)

/-- The fundamental property of the operator norm: `‚à•f x‚à• ‚â§ ‚à•f‚à• * ‚à•x‚à•`. -/
theorem le_op_norm : ‚à•f x‚à• ‚â§ ‚à•f‚à• * ‚à•x‚à• :=
classical.by_cases
  (Œª heq : x = 0, by { rw heq, simp })
  (Œª hne, have hlt : 0 < ‚à•x‚à•, from norm_pos_iff.2 hne,
    le_mul_of_div_le hlt ((le_Inf _ bounds_nonempty bounds_bdd_below).2
    (Œª c ‚ü®_, hc‚ü©, div_le_of_le_mul hlt (by { rw mul_comm, apply hc }))))

theorem le_op_norm_of_le {c : ‚Ñù} {x} (h : ‚à•x‚à• ‚â§ c) : ‚à•f x‚à• ‚â§ ‚à•f‚à• * c :=
le_trans (f.le_op_norm x) (mul_le_mul_of_nonneg_left h f.op_norm_nonneg)

/-- continuous linear maps are Lipschitz continuous. -/
theorem lipschitz : lipschitz_with ‚ü®‚à•f‚à•, op_norm_nonneg f‚ü© f :=
lipschitz_with.of_dist_le_mul $ Œª x y,
  by { rw [dist_eq_norm, dist_eq_norm, ‚Üêmap_sub], apply le_op_norm }

lemma ratio_le_op_norm : ‚à•f x‚à• / ‚à•x‚à• ‚â§ ‚à•f‚à• :=
(or.elim (lt_or_eq_of_le (norm_nonneg _))
  (Œª hlt, div_le_of_le_mul hlt (by { rw mul_comm, apply le_op_norm }))
  (Œª heq, by { rw [‚Üêheq, div_zero], apply op_norm_nonneg }))

/-- The image of the unit ball under a continuous linear map is bounded. -/
lemma unit_le_op_norm : ‚à•x‚à• ‚â§ 1 ‚Üí ‚à•f x‚à• ‚â§ ‚à•f‚à• :=
mul_one ‚à•f‚à• ‚ñ∏ f.le_op_norm_of_le

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma op_norm_le_bound {M : ‚Ñù} (hMp: 0 ‚â§ M) (hM : ‚àÄ x, ‚à•f x‚à• ‚â§ M * ‚à•x‚à•) :
  ‚à•f‚à• ‚â§ M :=
Inf_le _ bounds_bdd_below ‚ü®hMp, hM‚ü©

theorem op_norm_le_of_lipschitz {f : E ‚ÜíL[ùïú] F} {K : nnreal} (hf : lipschitz_with K f) :
  ‚à•f‚à• ‚â§ K :=
f.op_norm_le_bound K.2 $ Œª x, by simpa only [dist_zero_right, f.map_zero] using hf.dist_le_mul x 0

/-- The operator norm satisfies the triangle inequality. -/
theorem op_norm_add_le : ‚à•f + g‚à• ‚â§ ‚à•f‚à• + ‚à•g‚à• :=
show ‚à•f + g‚à• ‚â§ (coe : nnreal ‚Üí ‚Ñù) (‚ü®_, f.op_norm_nonneg‚ü© + ‚ü®_, g.op_norm_nonneg‚ü©),
from op_norm_le_of_lipschitz (f.lipschitz.add g.lipschitz)

/-- An operator is zero iff its norm vanishes. -/
theorem op_norm_zero_iff : ‚à•f‚à• = 0 ‚Üî f = 0 :=
iff.intro
  (Œª hn, continuous_linear_map.ext (Œª x, norm_le_zero_iff.1
    (calc _ ‚â§ ‚à•f‚à• * ‚à•x‚à• : le_op_norm _ _
     ...     = _ : by rw [hn, zero_mul])))
  (Œª hf, le_antisymm (Inf_le _ bounds_bdd_below
    ‚ü®ge_of_eq rfl, Œª _, le_of_eq (by { rw [zero_mul, hf], exact norm_zero })‚ü©)
    (op_norm_nonneg _))

@[simp] lemma norm_zero : ‚à•(0 : E ‚ÜíL[ùïú] F)‚à• = 0 :=
by rw op_norm_zero_iff

/-- The norm of the identity is at most `1`. It is in fact `1`, except when the space is trivial
where it is `0`. It means that one can not do better than an inequality in general. -/
lemma norm_id_le : ‚à•id ùïú E‚à• ‚â§ 1 :=
op_norm_le_bound _ zero_le_one (Œªx, by simp)

/-- If a space is non-trivial, then the norm of the identity equals `1`. -/
lemma norm_id [nontrivial E] : ‚à•id ùïú E‚à• = 1 :=
le_antisymm norm_id_le $ let ‚ü®x, hx‚ü© := exists_ne (0 : E) in
have _ := (id ùïú E).ratio_le_op_norm x,
by rwa [id_apply, div_self (ne_of_gt $ norm_pos_iff.2 hx)] at this

@[simp] lemma norm_id_field : ‚à•id ùïú ùïú‚à• = 1 :=
norm_id

@[simp] lemma norm_id_field' : ‚à•(1 : ùïú ‚ÜíL[ùïú] ùïú)‚à• = 1 :=
norm_id_field

lemma op_norm_smul_le : ‚à•c ‚Ä¢ f‚à• ‚â§ ‚à•c‚à• * ‚à•f‚à• :=
((c ‚Ä¢ f).op_norm_le_bound
  (mul_nonneg (norm_nonneg _) (op_norm_nonneg _)) (Œª _,
  begin
    erw [norm_smul, mul_assoc],
    exact mul_le_mul_of_nonneg_left (le_op_norm _ _) (norm_nonneg _)
  end))

lemma op_norm_neg : ‚à•-f‚à• = ‚à•f‚à• := by { rw norm_def, apply congr_arg, ext, simp }

/-- Continuous linear maps themselves form a normed space with respect to
    the operator norm. -/
instance to_normed_group : normed_group (E ‚ÜíL[ùïú] F) :=
normed_group.of_core _ ‚ü®op_norm_zero_iff, op_norm_add_le, op_norm_neg‚ü©

instance to_normed_space : normed_space ùïú (E ‚ÜíL[ùïú] F) :=
‚ü®op_norm_smul_le‚ü©

/-- The operator norm is submultiplicative. -/
lemma op_norm_comp_le (f : E ‚ÜíL[ùïú] F) : ‚à•h.comp f‚à• ‚â§ ‚à•h‚à• * ‚à•f‚à• :=
(Inf_le _ bounds_bdd_below
  ‚ü®mul_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Œª x,
    by { rw mul_assoc, exact h.le_op_norm_of_le (f.le_op_norm x) } ‚ü©)

/-- Continuous linear maps form a normed ring with respect to the operator norm. -/
instance to_normed_ring : normed_ring (E ‚ÜíL[ùïú] E) :=
{ norm_mul := op_norm_comp_le,
  .. continuous_linear_map.to_normed_group }

/-- For a nonzero normed space `E`, continuous linear endomorphisms form a normed algebra with
respect to the operator norm. -/
instance to_normed_algebra [nontrivial E] : normed_algebra ùïú (E ‚ÜíL[ùïú] E) :=
{ norm_algebra_map_eq := Œª c, show ‚à•c ‚Ä¢ id ùïú E‚à• = ‚à•c‚à•,
    by {rw [norm_smul, norm_id], simp},
  .. continuous_linear_map.algebra }

/-- A continuous linear map is automatically uniformly continuous. -/
protected theorem uniform_continuous : uniform_continuous f :=
f.lipschitz.uniform_continuous

variable {f}
/-- A continuous linear map is an isometry if and only if it preserves the norm. -/
lemma isometry_iff_norm_image_eq_norm :
  isometry f ‚Üî ‚àÄx, ‚à•f x‚à• = ‚à•x‚à• :=
begin
  rw isometry_emetric_iff_metric,
  split,
  { assume H x,
    have := H x 0,
    rwa [dist_eq_norm, dist_eq_norm, f.map_zero, sub_zero, sub_zero] at this },
  { assume H x y,
    rw [dist_eq_norm, dist_eq_norm, ‚Üê f.map_sub, H] }
end

lemma homothety_norm (hE : 0 < vector_space.dim ùïú E) (f : E ‚ÜíL[ùïú] F) {a : ‚Ñù} (ha : 0 ‚â§ a) (hf : ‚àÄx, ‚à•f x‚à• = a * ‚à•x‚à•) :
  ‚à•f‚à• = a :=
begin
  refine le_antisymm_iff.mpr ‚ü®_, _‚ü©,
  { exact continuous_linear_map.op_norm_le_bound f ha (Œª y, le_of_eq (hf y)) },
  { rw continuous_linear_map.norm_def,
    apply real.lb_le_Inf _ continuous_linear_map.bounds_nonempty,
    cases dim_pos_iff_exists_ne_zero.mp hE with x hx,
    intros c h, rw mem_set_of_eq at h,
    apply (mul_le_mul_right (norm_pos_iff.mpr hx)).mp,
    rw ‚Üê hf x, exact h.2 x }
end

lemma to_span_singleton_norm (x : E) : ‚à•to_span_singleton ùïú x‚à• = ‚à•x‚à• :=
begin
  refine homothety_norm _ _ (norm_nonneg x) (to_span_singleton_homothety ùïú x),
  rw dim_of_field, exact cardinal.zero_lt_one,
end

variable (f)

theorem uniform_embedding_of_bound {K : nnreal} (hf : ‚àÄ x, ‚à•x‚à• ‚â§ K * ‚à•f x‚à•) :
  uniform_embedding f :=
(f.to_linear_map.antilipschitz_of_bound hf).uniform_embedding f.uniform_continuous

/-- If a continuous linear map is a uniform embedding, then it is expands the distances
by a positive factor.-/
theorem antilipschitz_of_uniform_embedding (hf : uniform_embedding f) :
  ‚àÉ K, antilipschitz_with K f :=
begin
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ (Œµ : ‚Ñù) (H : Œµ > 0), ‚àÄ {x y : E}, dist (f x) (f y) < Œµ ‚Üí dist x y < 1, from
    (uniform_embedding_iff.1 hf).2.2 1 zero_lt_one,
  let Œ¥ := Œµ/2,
  have Œ¥_pos : Œ¥ > 0 := half_pos Œµpos,
  have H : ‚àÄ{x}, ‚à•f x‚à• ‚â§ Œ¥ ‚Üí ‚à•x‚à• ‚â§ 1,
  { assume x hx,
    have : dist x 0 ‚â§ 1,
    { apply le_of_lt,
      apply hŒµ,
      simp [dist_eq_norm],
      exact lt_of_le_of_lt hx (half_lt_self Œµpos) },
    simpa using this },
  rcases normed_field.exists_one_lt_norm ùïú with ‚ü®c, hc‚ü©,
  refine ‚ü®‚ü®Œ¥‚Åª¬π, _‚ü© * nnnorm c, f.to_linear_map.antilipschitz_of_bound $ Œªx, _‚ü©,
  exact inv_nonneg.2 (le_of_lt Œ¥_pos),
  by_cases hx : f x = 0,
  { have : f x = f 0, by { simp [hx] },
    have : x = 0 := (uniform_embedding_iff.1 hf).1 this,
    simp [this] },
  { rcases rescale_to_shell hc Œ¥_pos hx with ‚ü®d, hd, dxle, ledx, dinv‚ü©,
    have : ‚à•f (d ‚Ä¢ x)‚à• ‚â§ Œ¥, by simpa,
    have : ‚à•d ‚Ä¢ x‚à• ‚â§ 1 := H this,
    calc ‚à•x‚à• = ‚à•d‚à•‚Åª¬π * ‚à•d ‚Ä¢ x‚à• :
      by rwa [‚Üê normed_field.norm_inv, ‚Üê norm_smul, ‚Üê mul_smul, inv_mul_cancel, one_smul]
    ... ‚â§ ‚à•d‚à•‚Åª¬π * 1 :
      mul_le_mul_of_nonneg_left this (inv_nonneg.2 (norm_nonneg _))
    ... ‚â§ Œ¥‚Åª¬π * ‚à•c‚à• * ‚à•f x‚à• :
      by rwa [mul_one] }
end

section completeness

open_locale topological_space
open filter

/-- If the target space is complete, the space of continuous linear maps with its norm is also
complete. -/
instance [complete_space F] : complete_space (E ‚ÜíL[ùïú] F) :=
begin
  -- We show that every Cauchy sequence converges.
  refine metric.complete_of_cauchy_seq_tendsto (Œª f hf, _),
  -- We now expand out the definition of a Cauchy sequence,
  rcases cauchy_seq_iff_le_tendsto_0.1 hf with ‚ü®b, b0, b_bound, b_lim‚ü©, clear hf,
  -- and establish that the evaluation at any point `v : E` is Cauchy.
  have cau : ‚àÄ v, cauchy_seq (Œª n, f n v),
  { assume v,
    apply cauchy_seq_iff_le_tendsto_0.2 ‚ü®Œª n, b n * ‚à•v‚à•, Œª n, _, _, _‚ü©,
    { exact mul_nonneg (b0 n) (norm_nonneg _) },
    { assume n m N hn hm,
      rw dist_eq_norm,
      apply le_trans ((f n - f m).le_op_norm v) _,
      exact mul_le_mul_of_nonneg_right (b_bound n m N hn hm) (norm_nonneg v) },
    { simpa using b_lim.mul tendsto_const_nhds } },
  -- We assemble the limits points of those Cauchy sequences
  -- (which exist as `F` is complete)
  -- into a function which we call `G`.
  choose G hG using Œªv, cauchy_seq_tendsto_of_complete (cau v),
  -- Next, we show that this `G` is linear,
  let Glin : E ‚Üí‚Çó[ùïú] F :=
  { to_fun := G,
    map_add' := Œª v w, begin
      have A := hG (v + w),
      have B := (hG v).add (hG w),
      simp only [map_add] at A B,
      exact tendsto_nhds_unique A B,
    end,
    map_smul' := Œª c v, begin
      have A := hG (c ‚Ä¢ v),
      have B := filter.tendsto.smul (@tendsto_const_nhds _ ‚Ñï _ c _) (hG v),
      simp only [map_smul] at A B,
      exact tendsto_nhds_unique A B
    end },
  -- and that `G` has norm at most `(b 0 + ‚à•f 0‚à•)`.
  have Gnorm : ‚àÄ v, ‚à•G v‚à• ‚â§ (b 0 + ‚à•f 0‚à•) * ‚à•v‚à•,
  { assume v,
    have A : ‚àÄ n, ‚à•f n v‚à• ‚â§ (b 0 + ‚à•f 0‚à•) * ‚à•v‚à•,
    { assume n,
      apply le_trans ((f n).le_op_norm _) _,
      apply mul_le_mul_of_nonneg_right _ (norm_nonneg v),
      calc ‚à•f n‚à• = ‚à•(f n - f 0) + f 0‚à• : by { congr' 1, abel }
      ... ‚â§ ‚à•f n - f 0‚à• + ‚à•f 0‚à• : norm_add_le _ _
      ... ‚â§ b 0 + ‚à•f 0‚à• : begin
        apply add_le_add_right,
        simpa [dist_eq_norm] using b_bound n 0 0 (zero_le _) (zero_le _)
      end },
    exact le_of_tendsto (hG v).norm (eventually_of_forall A) },
  -- Thus `G` is continuous, and we propose that as the limit point of our original Cauchy sequence.
  let Gcont := Glin.mk_continuous _ Gnorm,
  use Gcont,
  -- Our last task is to establish convergence to `G` in norm.
  have : ‚àÄ n, ‚à•f n - Gcont‚à• ‚â§ b n,
  { assume n,
    apply op_norm_le_bound _ (b0 n) (Œª v, _),
    have A : ‚àÄ·∂† m in at_top, ‚à•(f n - f m) v‚à• ‚â§ b n * ‚à•v‚à•,
    { refine eventually_at_top.2 ‚ü®n, Œª m hm, _‚ü©,
      apply le_trans ((f n - f m).le_op_norm _) _,
      exact mul_le_mul_of_nonneg_right (b_bound n m n (le_refl _) hm) (norm_nonneg v) },
    have B : tendsto (Œª m, ‚à•(f n - f m) v‚à•) at_top (ùìù (‚à•(f n - Gcont) v‚à•)) :=
      tendsto.norm (tendsto_const_nhds.sub (hG v)),
    exact le_of_tendsto B A },
  erw tendsto_iff_norm_tendsto_zero,
  exact squeeze_zero (Œª n, norm_nonneg _) this b_lim,
end

end completeness

section uniformly_extend

variables [complete_space F] (e : E ‚ÜíL[ùïú] G) (h_dense : dense_range e)

section
variables (h_e : uniform_inducing e)

/-- Extension of a continuous linear map `f : E ‚ÜíL[ùïú] F`, with `E` a normed space and `F` a complete
    normed space, along a uniform and dense embedding `e : E ‚ÜíL[ùïú] G`.  -/
def extend : G ‚ÜíL[ùïú] F :=
/- extension of `f` is continuous -/
have cont : _ := (uniform_continuous_uniformly_extend h_e h_dense f.uniform_continuous).continuous,
/- extension of `f` agrees with `f` on the domain of the embedding `e` -/
have eq : _ := uniformly_extend_of_ind h_e h_dense f.uniform_continuous,
{ to_fun := (h_e.dense_inducing h_dense).extend f,
  map_add' :=
  begin
    refine h_dense.induction_on‚ÇÇ _ _,
    { exact is_closed_eq (cont.comp continuous_add)
        ((cont.comp continuous_fst).add (cont.comp continuous_snd)) },
    { assume x y, simp only [eq, ‚Üê e.map_add], exact f.map_add _ _  },
  end,
  map_smul' := Œªk,
  begin
    refine (Œª b, h_dense.induction_on b _ _),
    { exact is_closed_eq (cont.comp (continuous_const.smul continuous_id))
        ((continuous_const.smul continuous_id).comp cont) },
    { assume x, rw ‚Üê map_smul, simp only [eq], exact map_smul _ _ _  },
  end,
  cont := cont
}

lemma extend_unique (g : G ‚ÜíL[ùïú] F) (H : g.comp e = f) : extend f e h_dense h_e = g :=
continuous_linear_map.coe_injective' $
  uniformly_extend_unique h_e h_dense (continuous_linear_map.ext_iff.1 H) g.continuous

@[simp] lemma extend_zero : extend (0 : E ‚ÜíL[ùïú] F) e h_dense h_e = 0 :=
extend_unique _ _ _ _ _ (zero_comp _)

end

section
variables {N : nnreal} (h_e : ‚àÄx, ‚à•x‚à• ‚â§ N * ‚à•e x‚à•)

local notation `œà` := f.extend e h_dense (uniform_embedding_of_bound _ h_e).to_uniform_inducing

/-- If a dense embedding `e : E ‚ÜíL[ùïú] G` expands the norm by a constant factor `N‚Åª¬π`, then the norm
    of the extension of `f` along `e` is bounded by `N * ‚à•f‚à•`. -/
lemma op_norm_extend_le : ‚à•œà‚à• ‚â§ N * ‚à•f‚à• :=
begin
  have uni : uniform_inducing e := (uniform_embedding_of_bound _ h_e).to_uniform_inducing,
  have eq : ‚àÄx, œà (e x) = f x := uniformly_extend_of_ind uni h_dense f.uniform_continuous,
  by_cases N0 : 0 ‚â§ N,
  { refine op_norm_le_bound œà _ (is_closed_property h_dense (is_closed_le _ _) _),
    { exact mul_nonneg N0 (norm_nonneg _) },
    { exact continuous_norm.comp (cont œà) },
    { exact continuous_const.mul continuous_norm },
    { assume x,
      rw eq,
      calc ‚à•f x‚à• ‚â§ ‚à•f‚à• * ‚à•x‚à• : le_op_norm _ _
        ... ‚â§ ‚à•f‚à• * (N * ‚à•e x‚à•) : mul_le_mul_of_nonneg_left (h_e x) (norm_nonneg _)
        ... ‚â§ N * ‚à•f‚à• * ‚à•e x‚à• : by rw [mul_comm ‚ÜëN ‚à•f‚à•, mul_assoc] } },
  { have he : ‚àÄ x : E, x = 0,
    { assume x,
      have N0 : N ‚â§ 0 := le_of_lt (lt_of_not_ge N0),
      rw ‚Üê norm_le_zero_iff,
      exact le_trans (h_e x) (mul_nonpos_of_nonpos_of_nonneg N0 (norm_nonneg _)) },
    have hf : f = 0, { ext, simp only [he x, zero_apply, map_zero] },
    have hœà : œà = 0, { rw hf, apply extend_zero },
    rw [hœà, hf, norm_zero, norm_zero, mul_zero] }
end

end

end uniformly_extend

end op_norm

/-- The norm of the tensor product of a scalar linear map and of an element of a normed space
is the product of the norms. -/
@[simp] lemma smul_right_norm {c : E ‚ÜíL[ùïú] ùïú} {f : F} :
  ‚à•smul_right c f‚à• = ‚à•c‚à• * ‚à•f‚à• :=
begin
  refine le_antisymm _ _,
  { apply op_norm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) (Œªx, _),
    calc
     ‚à•(c x) ‚Ä¢ f‚à• = ‚à•c x‚à• * ‚à•f‚à• : norm_smul _ _
     ... ‚â§ (‚à•c‚à• * ‚à•x‚à•) * ‚à•f‚à• :
       mul_le_mul_of_nonneg_right (le_op_norm _ _) (norm_nonneg _)
     ... = ‚à•c‚à• * ‚à•f‚à• * ‚à•x‚à• : by ring },
  { by_cases h : ‚à•f‚à• = 0,
    { rw h, simp [norm_nonneg] },
    { have : 0 < ‚à•f‚à• := lt_of_le_of_ne (norm_nonneg _) (ne.symm h),
      rw ‚Üê le_div_iff this,
      apply op_norm_le_bound _ (div_nonneg (norm_nonneg _) this) (Œªx, _),
      rw [div_mul_eq_mul_div, le_div_iff this],
      calc ‚à•c x‚à• * ‚à•f‚à• = ‚à•c x ‚Ä¢ f‚à• : (norm_smul _ _).symm
      ... = ‚à•((smul_right c f) : E ‚Üí F) x‚à• : rfl
      ... ‚â§ ‚à•smul_right c f‚à• * ‚à•x‚à• : le_op_norm _ _ } },
end

/-- Left-multiplication in a normed algebra, considered as a continuous linear map. -/
def lmul_left (ùïú : Type*) (ùïú' : Type*) [normed_field ùïú] [normed_ring ùïú']
  [h : normed_algebra ùïú ùïú'] : ùïú' ‚Üí (ùïú' ‚ÜíL[ùïú] ùïú') :=
Œª x, (algebra.lmul_left ùïú ùïú' x).mk_continuous ‚à•x‚à•
(Œª y, by {rw algebra.lmul_left_apply, exact norm_mul_le x y})

@[simp] lemma lmul_left_apply {ùïú : Type*} (ùïú' : Type*) [normed_field ùïú]
  [normed_ring ùïú'] [h : normed_algebra ùïú ùïú'] (x y : ùïú') :
  lmul_left ùïú ùïú' x y = x * y := rfl

/-- Right-multiplication in a normed algebra, considered as a continuous linear map. -/
def lmul_right (ùïú : Type*) (ùïú' : Type*) [normed_field ùïú]
  [normed_ring ùïú'] [h : normed_algebra ùïú ùïú'] : ùïú' ‚Üí (ùïú' ‚ÜíL[ùïú] ùïú') :=
Œª x, (algebra.lmul_right ùïú ùïú' x).mk_continuous ‚à•x‚à•
(Œª y, by {rw [algebra.lmul_right_apply, mul_comm], exact norm_mul_le y x})

@[simp] lemma lmul_right_apply {ùïú : Type*} (ùïú' : Type*) [normed_field ùïú]
  [normed_ring ùïú'] [h : normed_algebra ùïú ùïú'] (x y : ùïú') :
  lmul_right ùïú ùïú' x y = y * x := rfl

section restrict_scalars

variable (ùïú)
variables {ùïú' : Type*} [normed_field ùïú'] [normed_algebra ùïú ùïú']
{E' : Type*} [normed_group E'] [normed_space ùïú' E']
{F' : Type*} [normed_group F'] [normed_space ùïú' F']

local attribute [instance, priority 500] normed_space.restrict_scalars

/-- `ùïú`-linear continuous function induced by a `ùïú'`-linear continuous function when `ùïú'` is a
normed algebra over `ùïú`. -/
def restrict_scalars (f : E' ‚ÜíL[ùïú'] F') : E' ‚ÜíL[ùïú] F' :=
{ cont := f.cont,
  ..linear_map.restrict_scalars ùïú (f.to_linear_map) }

@[simp, norm_cast] lemma restrict_scalars_coe_eq_coe (f : E' ‚ÜíL[ùïú'] F') :
  (f.restrict_scalars ùïú : E' ‚Üí‚Çó[ùïú] F') = (f : E' ‚Üí‚Çó[ùïú'] F').restrict_scalars ùïú := rfl

@[simp, norm_cast squash] lemma restrict_scalars_coe_eq_coe' (f : E' ‚ÜíL[ùïú'] F') :
  (f.restrict_scalars ùïú : E' ‚Üí F') = f := rfl

end restrict_scalars

end continuous_linear_map

variables {Œπ : Type*}

/-- Applying a continuous linear map commutes with taking an (infinite) sum. -/
lemma continuous_linear_map.has_sum {f : Œπ ‚Üí E} (œÜ : E ‚ÜíL[ùïú] F) {x : E} (hf : has_sum f x) :
  has_sum (Œª (b:Œπ), œÜ (f b)) (œÜ x) :=
begin
  unfold has_sum,
  convert œÜ.continuous.continuous_at.tendsto.comp hf,
  ext s, rw [function.comp_app, finset.sum_hom s œÜ],
end

lemma continuous_linear_map.has_sum_of_summable {f : Œπ ‚Üí E} (œÜ : E ‚ÜíL[ùïú] F) (hf : summable f) :
  has_sum (Œª (b:Œπ), œÜ (f b)) (œÜ (‚àë'b, f b)) :=
continuous_linear_map.has_sum œÜ hf.has_sum

namespace continuous_linear_equiv

variable (e : E ‚âÉL[ùïú] F)

protected lemma lipschitz : lipschitz_with (nnnorm (e : E ‚ÜíL[ùïú] F)) e :=
(e : E ‚ÜíL[ùïú] F).lipschitz

protected lemma antilipschitz : antilipschitz_with (nnnorm (e.symm : F ‚ÜíL[ùïú] E)) e :=
e.symm.lipschitz.to_right_inverse e.left_inv

theorem is_O_comp {Œ± : Type*} (f : Œ± ‚Üí E) (l : filter Œ±) :
  asymptotics.is_O (Œª x', e (f x')) f l :=
(e : E ‚ÜíL[ùïú] F).is_O_comp f l

theorem is_O_sub (l : filter E) (x : E) :
  asymptotics.is_O (Œª x', e (x' - x)) (Œª x', x' - x) l :=
(e : E ‚ÜíL[ùïú] F).is_O_sub l x

theorem is_O_comp_rev {Œ± : Type*} (f : Œ± ‚Üí E) (l : filter Œ±) :
  asymptotics.is_O f (Œª x', e (f x')) l :=
(e.symm.is_O_comp _ l).congr_left $ Œª _, e.symm_apply_apply _

theorem is_O_sub_rev (l : filter E) (x : E) :
  asymptotics.is_O (Œª x', x' - x) (Œª x', e (x' - x)) l :=
e.is_O_comp_rev _ _

/-- A continuous linear equiv is a uniform embedding. -/
lemma uniform_embedding : uniform_embedding e :=
e.antilipschitz.uniform_embedding e.lipschitz.uniform_continuous

lemma one_le_norm_mul_norm_symm [nontrivial E] :
  1 ‚â§ ‚à•(e : E ‚ÜíL[ùïú] F)‚à• * ‚à•(e.symm : F ‚ÜíL[ùïú] E)‚à• :=
begin
  rw [mul_comm],
  convert (e.symm : F ‚ÜíL[ùïú] E).op_norm_comp_le (e : E ‚ÜíL[ùïú] F),
  rw [e.coe_symm_comp_coe, continuous_linear_map.norm_id]
end

lemma norm_pos [nontrivial E] : 0 < ‚à•(e : E ‚ÜíL[ùïú] F)‚à• :=
pos_of_mul_pos_right (lt_of_lt_of_le zero_lt_one e.one_le_norm_mul_norm_symm) (norm_nonneg _)

lemma norm_symm_pos [nontrivial E] : 0 < ‚à•(e.symm : F ‚ÜíL[ùïú] E)‚à• :=
pos_of_mul_pos_left (lt_of_lt_of_le zero_lt_one e.one_le_norm_mul_norm_symm) (norm_nonneg _)

lemma subsingleton_or_norm_symm_pos : subsingleton E ‚à® 0 < ‚à•(e.symm : F ‚ÜíL[ùïú] E)‚à• :=
begin
  rcases subsingleton_or_nontrivial E with _i|_i; resetI,
  { left, apply_instance },
  { right, exact e.norm_symm_pos }
end

lemma subsingleton_or_nnnorm_symm_pos : subsingleton E ‚à® 0 < (nnnorm $ (e.symm : F ‚ÜíL[ùïú] E)) :=
subsingleton_or_norm_symm_pos e

lemma homothety_inverse (a : ‚Ñù) (ha : 0 < a) (f : E ‚âÉ‚Çó[ùïú] F) :
  (‚àÄ (x : E), ‚à•f x‚à• = a * ‚à•x‚à•) ‚Üí (‚àÄ (y : F), ‚à•f.symm y‚à• = a‚Åª¬π * ‚à•y‚à•) :=
begin
  intros hf y,
  calc ‚à•(f.symm) y‚à• = a‚Åª¬π * (a * ‚à• (f.symm) y‚à•) : _
  ... =  a‚Åª¬π * ‚à•f ((f.symm) y)‚à• : by rw hf
  ... = a‚Åª¬π * ‚à•y‚à• : by simp,
  rw [‚Üê mul_assoc, inv_mul_cancel (ne_of_lt ha).symm, one_mul],
end

variable (ùïú)

/-- A linear equivalence which is a homothety is a continuous linear equivalence. -/
def of_homothety (f : E ‚âÉ‚Çó[ùïú] F) (a : ‚Ñù) (ha : 0 < a) (hf : ‚àÄx, ‚à•f x‚à• = a * ‚à•x‚à•) : E ‚âÉL[ùïú] F :=
{ to_linear_equiv := f,
  continuous_to_fun := f.to_linear_map.continuous_of_bound a (Œª x, le_of_eq (hf x)),
  continuous_inv_fun := f.symm.to_linear_map.continuous_of_bound a‚Åª¬π
    (Œª x, le_of_eq (homothety_inverse a ha f hf x)) }

lemma to_span_nonzero_singleton_homothety (x : E) (h : x ‚â† 0) (c : ùïú) :
  ‚à•linear_equiv.to_span_nonzero_singleton ùïú E x h c‚à• = ‚à•x‚à• * ‚à•c‚à• :=
continuous_linear_map.to_span_singleton_homothety _ _ _

/-- Given a nonzero element `x` of a normed space `E` over a field `ùïú`, the natural
    continuous linear equivalence from `E` to the span of `x`.-/
def to_span_nonzero_singleton (x : E) (h : x ‚â† 0) : ùïú ‚âÉL[ùïú] (submodule.span ùïú ({x} : set E)) :=
of_homothety ùïú
  (linear_equiv.to_span_nonzero_singleton ùïú E x h)
  ‚à•x‚à•
  (norm_pos_iff.mpr h)
  (to_span_nonzero_singleton_homothety ùïú x h)

/-- Given a nonzero element `x` of a normed space `E` over a field `ùïú`, the natural continuous
    linear map from the span of `x` to `ùïú`.-/
abbreviation coord (x : E) (h : x ‚â† 0) : (submodule.span ùïú ({x} : set E)) ‚ÜíL[ùïú] ùïú :=
  (to_span_nonzero_singleton ùïú x h).symm

lemma coord_norm (x : E) (h : x ‚â† 0) : ‚à•coord ùïú x h‚à• = ‚à•x‚à•‚Åª¬π :=
begin
  have hx : 0 < ‚à•x‚à• := (norm_pos_iff.mpr h),
  refine continuous_linear_map.homothety_norm _ _ (le_of_lt (inv_pos.mpr hx)) _,
  { rw ‚Üê finite_dimensional.findim_eq_dim,
    rw ‚Üê linear_equiv.findim_eq (linear_equiv.to_span_nonzero_singleton ùïú E x h),
    rw finite_dimensional.findim_of_field,
    have : 0 = ((0:nat) : cardinal) := rfl,
    rw this, apply cardinal.nat_cast_lt.mpr, norm_num },
  { intros y,
    have : (coord ùïú x h) y = (to_span_nonzero_singleton ùïú x h).symm y := rfl,
    rw this, apply homothety_inverse, exact hx, exact to_span_nonzero_singleton_homothety ùïú x h, }
end

variable (E)

/-- The continuous linear equivalences from `E` to itself form a group under composition. -/
instance automorphism_group : group (E ‚âÉL[ùïú] E) :=
{ mul          := Œª f g, g.trans f,
  one          := continuous_linear_equiv.refl ùïú E,
  inv          := Œª f, f.symm,
  mul_assoc    := Œª f g h, by {ext, refl},
  mul_one      := Œª f, by {ext, refl},
  one_mul      := Œª f, by {ext, refl},
  mul_left_inv := Œª f, by {ext, exact f.left_inv x} }

variables {ùïú E}

/-- An invertible continuous linear map `f` determines a continuous equivalence from `E` to itself.
-/
def of_unit (f : units (E ‚ÜíL[ùïú] E)) : (E ‚âÉL[ùïú] E) :=
{ to_linear_equiv :=
  { to_fun    := f.val,
    map_add'  := by simp,
    map_smul' := by simp,
    inv_fun   := f.inv,
    left_inv  := Œª x, show (f.inv * f.val) x = x, by {rw f.inv_val, simp},
    right_inv := Œª x, show (f.val * f.inv) x = x, by {rw f.val_inv, simp}, },
  continuous_to_fun  := f.val.continuous,
  continuous_inv_fun := f.inv.continuous }

/-- A continuous equivalence from `E` to itself determines an invertible continuous linear map. -/
def to_unit (f : (E ‚âÉL[ùïú] E)) : units (E ‚ÜíL[ùïú] E) :=
{ val     := f,
  inv     := f.symm,
  val_inv := by {ext, simp},
  inv_val := by {ext, simp} }

variables (ùïú E)

/-- The units of the algebra of continuous `ùïú`-linear endomorphisms of `E` is multiplicatively
equivalent to the type of continuous linear equivalences between `E` and itself. -/
def units_equiv : units (E ‚ÜíL[ùïú] E) ‚âÉ* (E ‚âÉL[ùïú] E) :=
{ to_fun    := of_unit,
  inv_fun   := to_unit,
  left_inv  := Œª f, by {ext, refl},
  right_inv := Œª f, by {ext, refl},
  map_mul'  := Œª x y, by {ext, refl} }

@[simp] lemma units_equiv_to_continuous_linear_map
  (f : units (E ‚ÜíL[ùïú] E)) :
  (units_equiv ùïú E f : E ‚ÜíL[ùïú] E) = f := by {ext, refl}

end continuous_linear_equiv

lemma linear_equiv.uniform_embedding (e : E ‚âÉ‚Çó[ùïú] F) (h‚ÇÅ : continuous e) (h‚ÇÇ : continuous e.symm) :
  uniform_embedding e :=
continuous_linear_equiv.uniform_embedding
{ continuous_to_fun := h‚ÇÅ,
  continuous_inv_fun := h‚ÇÇ,
  .. e }

/-- If a continuous linear map is constructed from a linear map via the constructor `mk_continuous`,
then its norm is bounded by the bound given to the constructor if it is nonnegative. -/
lemma linear_map.mk_continuous_norm_le (f : E ‚Üí‚Çó[ùïú] F) {C : ‚Ñù} (hC : 0 ‚â§ C) (h : ‚àÄx, ‚à•f x‚à• ‚â§ C * ‚à•x‚à•) :
  ‚à•f.mk_continuous C h‚à• ‚â§ C :=
continuous_linear_map.op_norm_le_bound _ hC h
