/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import algebra.algebra.tower
import analysis.asymptotics.asymptotics
import analysis.normed_space.continuous_linear_map
import analysis.normed_space.linear_isometry
import topology.algebra.module.strong_topology

/-!
# Operator norm on the space of continuous linear maps

Define the operator norm on the space of continuous (semi)linear maps between normed spaces, and
prove its basic properties. In particular, show that this space is itself a normed space.

Since a lot of elementary properties don't require `â€–xâ€– = 0 â†’ x = 0` we start setting up the
theory for `seminormed_add_comm_group` and we specialize to `normed_add_comm_group` at the end.

Note that most of statements that apply to semilinear maps only hold when the ring homomorphism
is isometric, as expressed by the typeclass `[ring_hom_isometric Ïƒ]`.

-/

noncomputable theory
open_locale classical nnreal topology

-- the `â‚—` subscript variables are for special cases about linear (as opposed to semilinear) maps
variables {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ E Eâ‚— F Fâ‚— G Gâ‚— ğ“• : Type*}

section semi_normed

open metric continuous_linear_map

variables [seminormed_add_comm_group E] [seminormed_add_comm_group Eâ‚—] [seminormed_add_comm_group F]
  [seminormed_add_comm_group Fâ‚—] [seminormed_add_comm_group G] [seminormed_add_comm_group Gâ‚—]

variables [nontrivially_normed_field ğ•œ] [nontrivially_normed_field ğ•œâ‚‚]
  [nontrivially_normed_field ğ•œâ‚ƒ] [normed_space ğ•œ E] [normed_space ğ•œ Eâ‚—] [normed_space ğ•œâ‚‚ F]
  [normed_space ğ•œ Fâ‚—] [normed_space ğ•œâ‚ƒ G] [normed_space ğ•œ Gâ‚—]
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ}
  [ring_hom_comp_triple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]

/-- If `â€–xâ€– = 0` and `f` is continuous then `â€–f xâ€– = 0`. -/
lemma norm_image_of_norm_zero [semilinear_map_class ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
  (hf : continuous f) {x : E} (hx : â€–xâ€– = 0) : â€–f xâ€– = 0 :=
begin
  refine le_antisymm (le_of_forall_pos_le_add (Î» Îµ hÎµ, _)) (norm_nonneg (f x)),
  rcases normed_add_comm_group.tendsto_nhds_nhds.1 (hf.tendsto 0) Îµ hÎµ with âŸ¨Î´, Î´_pos, hÎ´âŸ©,
  replace hÎ´ := hÎ´ x,
  rw [sub_zero, hx] at hÎ´,
  replace hÎ´ := le_of_lt (hÎ´ Î´_pos),
  rw [map_zero, sub_zero] at hÎ´,
  rwa [zero_add]
end

section

variables [ring_hom_isometric Ïƒâ‚â‚‚] [ring_hom_isometric Ïƒâ‚‚â‚ƒ]

lemma semilinear_map_class.bound_of_shell_semi_normed [semilinear_map_class ğ“• Ïƒâ‚â‚‚ E F]
  (f : ğ“•) {Îµ C : â„} (Îµ_pos : 0 < Îµ) {c : ğ•œ} (hc : 1 < â€–câ€–)
  (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) {x : E} (hx : â€–xâ€– â‰  0) :
  â€–f xâ€– â‰¤ C * â€–xâ€– :=
begin
  rcases rescale_to_shell_semi_normed hc Îµ_pos hx with âŸ¨Î´, hÎ´, Î´xle, leÎ´x, Î´invâŸ©,
  have := hf (Î´ â€¢ x) leÎ´x Î´xle,
  simpa only [map_smulâ‚›â‚—, norm_smul, mul_left_comm C, mul_le_mul_left (norm_pos_iff.2 hÎ´),
              ring_hom_isometric.is_iso] using hf (Î´ â€¢ x) leÎ´x Î´xle
end

/-- A continuous linear map between seminormed spaces is bounded when the field is nontrivially
normed. The continuity ensures boundedness on a ball of some radius `Îµ`. The nontriviality of the
norm is then used to rescale any element into an element of norm in `[Îµ/C, Îµ]`, whose image has a
controlled norm. The norm control for the original element follows by rescaling. -/
lemma semilinear_map_class.bound_of_continuous [semilinear_map_class ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
  (hf : continuous f) : âˆƒ C, 0 < C âˆ§ (âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€–) :=
begin
  rcases normed_add_comm_group.tendsto_nhds_nhds.1 (hf.tendsto 0) 1 zero_lt_one with âŸ¨Îµ, Îµ_pos, hÎµâŸ©,
  simp only [sub_zero, map_zero] at hÎµ,
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  have : 0 < â€–câ€– / Îµ, from div_pos (zero_lt_one.trans hc) Îµ_pos,
  refine âŸ¨â€–câ€– / Îµ, this, Î» x, _âŸ©,
  by_cases hx : â€–xâ€– = 0,
  { rw [hx, mul_zero],
    exact le_of_eq (norm_image_of_norm_zero f hf hx) },
  refine semilinear_map_class.bound_of_shell_semi_normed f Îµ_pos hc (Î» x hle hlt, _) hx,
  refine (hÎµ _ hlt).le.trans _,
  rwa [â† div_le_iff' this, one_div_div]
end

end

namespace continuous_linear_map

theorem bound [ring_hom_isometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
  âˆƒ C, 0 < C âˆ§ (âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€–) :=
semilinear_map_class.bound_of_continuous f f.2

section
open filter

variables (ğ•œ E)
/-- Given a unit-length element `x` of a normed space `E` over a field `ğ•œ`, the natural linear
    isometry map from `ğ•œ` to `E` by taking multiples of `x`.-/
def _root_.linear_isometry.to_span_singleton {v : E} (hv : â€–vâ€– = 1) : ğ•œ â†’â‚—áµ¢[ğ•œ] E :=
{ norm_map' := Î» x, by simp [norm_smul, hv],
  .. linear_map.to_span_singleton ğ•œ E v }
variables {ğ•œ E}

@[simp] lemma _root_.linear_isometry.to_span_singleton_apply {v : E} (hv : â€–vâ€– = 1) (a : ğ•œ) :
  linear_isometry.to_span_singleton ğ•œ E hv a = a â€¢ v :=
rfl

@[simp] lemma _root_.linear_isometry.coe_to_span_singleton {v : E} (hv : â€–vâ€– = 1) :
  (linear_isometry.to_span_singleton ğ•œ E hv).to_linear_map = linear_map.to_span_singleton ğ•œ E v :=
rfl

end

section op_norm
open set real

/-- The operator norm of a continuous linear map is the inf of all its bounds. -/
def op_norm (f : E â†’SL[Ïƒâ‚â‚‚] F) := Inf {c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€–}
instance has_op_norm : has_norm (E â†’SL[Ïƒâ‚â‚‚] F) := âŸ¨op_normâŸ©

lemma norm_def (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€– = Inf {c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€–} := rfl

-- So that invocations of `le_cInf` make sense: we show that the set of
-- bounds is nonempty and bounded below.
lemma bounds_nonempty [ring_hom_isometric Ïƒâ‚â‚‚] {f : E â†’SL[Ïƒâ‚â‚‚] F} :
  âˆƒ c, c âˆˆ { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
let âŸ¨M, hMp, hMbâŸ© := f.bound in âŸ¨M, le_of_lt hMp, hMbâŸ©

lemma bounds_bdd_below {f : E â†’SL[Ïƒâ‚â‚‚] F} :
  bdd_below { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
âŸ¨0, Î» _ âŸ¨hn, _âŸ©, hnâŸ©

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma op_norm_le_bound (f : E â†’SL[Ïƒâ‚â‚‚] F) {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ x, â€–f xâ€– â‰¤ M * â€–xâ€–) :
  â€–fâ€– â‰¤ M :=
cInf_le bounds_bdd_below âŸ¨hMp, hMâŸ©

/-- If one controls the norm of every `A x`, `â€–xâ€– â‰  0`, then one controls the norm of `A`. -/
lemma op_norm_le_bound' (f : E â†’SL[Ïƒâ‚â‚‚] F) {M : â„} (hMp: 0 â‰¤ M)
  (hM : âˆ€ x, â€–xâ€– â‰  0 â†’ â€–f xâ€– â‰¤ M * â€–xâ€–) :
  â€–fâ€– â‰¤ M :=
op_norm_le_bound f hMp $ Î» x, (ne_or_eq (â€–xâ€–) 0).elim (hM x) $
  Î» h, by simp only [h, mul_zero, norm_image_of_norm_zero f f.2 h]

theorem op_norm_le_of_lipschitz {f : E â†’SL[Ïƒâ‚â‚‚] F} {K : â„â‰¥0} (hf : lipschitz_with K f) :
  â€–fâ€– â‰¤ K :=
f.op_norm_le_bound K.2 $ Î» x, by simpa only [dist_zero_right, f.map_zero] using hf.dist_le_mul x 0

lemma op_norm_eq_of_bounds {Ï† : E â†’SL[Ïƒâ‚â‚‚] F} {M : â„} (M_nonneg : 0 â‰¤ M)
  (h_above : âˆ€ x, â€–Ï† xâ€– â‰¤ M*â€–xâ€–) (h_below : âˆ€ N â‰¥ 0, (âˆ€ x, â€–Ï† xâ€– â‰¤ N*â€–xâ€–) â†’ M â‰¤ N) :
  â€–Ï†â€– = M :=
le_antisymm (Ï†.op_norm_le_bound M_nonneg h_above)
  ((le_cInf_iff continuous_linear_map.bounds_bdd_below âŸ¨M, M_nonneg, h_aboveâŸ©).mpr $
   Î» N âŸ¨N_nonneg, hNâŸ©, h_below N N_nonneg hN)

lemma op_norm_neg (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–-fâ€– = â€–fâ€– := by simp only [norm_def, neg_apply, norm_neg]

section

variables [ring_hom_isometric Ïƒâ‚â‚‚] [ring_hom_isometric Ïƒâ‚‚â‚ƒ]
  (f g : E â†’SL[Ïƒâ‚â‚‚] F) (h : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (x : E)

lemma op_norm_nonneg : 0 â‰¤ â€–fâ€– :=
le_cInf bounds_nonempty (Î» _ âŸ¨hx, _âŸ©, hx)

/-- The fundamental property of the operator norm: `â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€–`. -/
theorem le_op_norm : â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– :=
begin
  obtain âŸ¨C, Cpos, hCâŸ© := f.bound,
  replace hC := hC x,
  by_cases h : â€–xâ€– = 0,
  { rwa [h, mul_zero] at âŠ¢ hC },
  have hlt : 0 < â€–xâ€– := lt_of_le_of_ne (norm_nonneg x) (ne.symm h),
  exact  (div_le_iff hlt).mp (le_cInf bounds_nonempty (Î» c âŸ¨_, hcâŸ©,
    (div_le_iff hlt).mpr $ by { apply hc })),
end

theorem dist_le_op_norm (x y : E) : dist (f x) (f y) â‰¤ â€–fâ€– * dist x y :=
by simp_rw [dist_eq_norm, â† map_sub, f.le_op_norm]

theorem le_op_norm_of_le {c : â„} {x} (h : â€–xâ€– â‰¤ c) : â€–f xâ€– â‰¤ â€–fâ€– * c :=
le_trans (f.le_op_norm x) (mul_le_mul_of_nonneg_left h f.op_norm_nonneg)

theorem le_of_op_norm_le {c : â„} (h : â€–fâ€– â‰¤ c) (x : E) : â€–f xâ€– â‰¤ c * â€–xâ€– :=
(f.le_op_norm x).trans (mul_le_mul_of_nonneg_right h (norm_nonneg x))

lemma ratio_le_op_norm : â€–f xâ€– / â€–xâ€– â‰¤ â€–fâ€– :=
div_le_of_nonneg_of_le_mul (norm_nonneg _) f.op_norm_nonneg (le_op_norm _ _)

/-- The image of the unit ball under a continuous linear map is bounded. -/
lemma unit_le_op_norm : â€–xâ€– â‰¤ 1 â†’ â€–f xâ€– â‰¤ â€–fâ€– :=
mul_one â€–fâ€– â–¸ f.le_op_norm_of_le

lemma op_norm_le_of_shell {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C)
  {c : ğ•œ} (hc : 1 < â€–câ€–) (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) :
  â€–fâ€– â‰¤ C :=
f.op_norm_le_bound' hC $ Î» x hx, semilinear_map_class.bound_of_shell_semi_normed f Îµ_pos hc hf hx

lemma op_norm_le_of_ball {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ : â„} {C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C)
  (hf : âˆ€ x âˆˆ ball (0 : E) Îµ, â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C :=
begin
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  refine op_norm_le_of_shell Îµ_pos hC hc (Î» x _ hx, hf x _),
  rwa ball_zero_eq
end

lemma op_norm_le_of_nhds_zero {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„} (hC : 0 â‰¤ C)
  (hf : âˆ€á¶  x in ğ“ (0 : E), â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C :=
let âŸ¨Îµ, Îµ0, hÎµâŸ© := metric.eventually_nhds_iff_ball.1 hf in op_norm_le_of_ball Îµ0 hC hÎµ

lemma op_norm_le_of_shell' {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C)
  {c : ğ•œ} (hc : â€–câ€– < 1) (hf : âˆ€ x, Îµ * â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) :
  â€–fâ€– â‰¤ C :=
begin
  by_cases h0 : c = 0,
  { refine op_norm_le_of_ball Îµ_pos hC (Î» x hx, hf x _ _),
    { simp [h0] },
    { rwa ball_zero_eq at hx } },
  { rw [â† inv_inv c, norm_inv,
      inv_lt_one_iff_of_pos (norm_pos_iff.2 $ inv_ne_zero h0)] at hc,
    refine op_norm_le_of_shell Îµ_pos hC hc _,
    rwa [norm_inv, div_eq_mul_inv, inv_inv] }
end

/-- For a continuous real linear map `f`, if one controls the norm of every `f x`, `â€–xâ€– = 1`, then
one controls the norm of `f`. -/
lemma op_norm_le_of_unit_norm [normed_space â„ E] [normed_space â„ F] {f : E â†’L[â„] F} {C : â„}
  (hC : 0 â‰¤ C) (hf : âˆ€ x, â€–xâ€– = 1 â†’ â€–f xâ€– â‰¤ C) : â€–fâ€– â‰¤ C :=
begin
  refine op_norm_le_bound' f hC (Î» x hx, _),
  have Hâ‚ : â€–(â€–xâ€–â»Â¹ â€¢ x)â€– = 1, by rw [norm_smul, norm_inv, norm_norm, inv_mul_cancel hx],
  have Hâ‚‚ := hf _ Hâ‚,
  rwa [map_smul, norm_smul, norm_inv, norm_norm, â† div_eq_inv_mul, div_le_iff] at Hâ‚‚,
  exact (norm_nonneg x).lt_of_ne' hx
end

/-- The operator norm satisfies the triangle inequality. -/
theorem op_norm_add_le : â€–f + gâ€– â‰¤ â€–fâ€– + â€–gâ€– :=
(f + g).op_norm_le_bound (add_nonneg f.op_norm_nonneg g.op_norm_nonneg) $
  Î» x, (norm_add_le_of_le (f.le_op_norm x) (g.le_op_norm x)).trans_eq (add_mul _ _ _).symm

/-- The norm of the `0` operator is `0`. -/
theorem op_norm_zero : â€–(0 : E â†’SL[Ïƒâ‚â‚‚] F)â€– = 0 :=
le_antisymm (cInf_le bounds_bdd_below
    âŸ¨le_rfl, Î» _, le_of_eq (by { rw [zero_mul], exact norm_zero })âŸ©)
    (op_norm_nonneg _)

/-- The norm of the identity is at most `1`. It is in fact `1`, except when the space is trivial
where it is `0`. It means that one can not do better than an inequality in general. -/
lemma norm_id_le : â€–id ğ•œ Eâ€– â‰¤ 1 :=
op_norm_le_bound _ zero_le_one (Î»x, by simp)

/-- If there is an element with norm different from `0`, then the norm of the identity equals `1`.
(Since we are working with seminorms supposing that the space is non-trivial is not enough.) -/
lemma norm_id_of_nontrivial_seminorm (h : âˆƒ (x : E), â€–xâ€– â‰  0) : â€–id ğ•œ Eâ€– = 1 :=
le_antisymm norm_id_le $ let âŸ¨x, hxâŸ© := h in
have _ := (id ğ•œ E).ratio_le_op_norm x,
by rwa [id_apply, div_self hx] at this

lemma op_norm_smul_le {ğ•œ' : Type*} [normed_field ğ•œ'] [normed_space ğ•œ' F]
  [smul_comm_class ğ•œâ‚‚ ğ•œ' F] (c : ğ•œ') (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–c â€¢ fâ€– â‰¤ â€–câ€– * â€–fâ€– :=
((c â€¢ f).op_norm_le_bound
  (mul_nonneg (norm_nonneg _) (op_norm_nonneg _)) (Î» _,
  begin
    erw [norm_smul, mul_assoc],
    exact mul_le_mul_of_nonneg_left (le_op_norm _ _) (norm_nonneg _)
  end))

/-- Continuous linear maps themselves form a seminormed space with respect to
the operator norm. This is only a temporary definition because we want to replace the topology
with `continuous_linear_map.topological_space` to avoid diamond issues.
See Note [forgetful inheritance] -/
protected def tmp_seminormed_add_comm_group : seminormed_add_comm_group (E â†’SL[Ïƒâ‚â‚‚] F) :=
add_group_seminorm.to_seminormed_add_comm_group
{ to_fun := norm,
  map_zero' := op_norm_zero,
  add_le' := op_norm_add_le,
  neg' := op_norm_neg }

/-- The `pseudo_metric_space` structure on `E â†’SL[Ïƒâ‚â‚‚] F` coming from
`continuous_linear_map.tmp_seminormed_add_comm_group`.
See Note [forgetful inheritance] -/
protected def tmp_pseudo_metric_space : pseudo_metric_space (E â†’SL[Ïƒâ‚â‚‚] F) :=
continuous_linear_map.tmp_seminormed_add_comm_group.to_pseudo_metric_space

/-- The `uniform_space` structure on `E â†’SL[Ïƒâ‚â‚‚] F` coming from
`continuous_linear_map.tmp_seminormed_add_comm_group`.
See Note [forgetful inheritance] -/
protected def tmp_uniform_space : uniform_space (E â†’SL[Ïƒâ‚â‚‚] F) :=
continuous_linear_map.tmp_pseudo_metric_space.to_uniform_space

/-- The `topological_space` structure on `E â†’SL[Ïƒâ‚â‚‚] F` coming from
`continuous_linear_map.tmp_seminormed_add_comm_group`.
See Note [forgetful inheritance] -/
protected def tmp_topological_space : topological_space (E â†’SL[Ïƒâ‚â‚‚] F) :=
continuous_linear_map.tmp_uniform_space.to_topological_space

section tmp

local attribute [-instance] continuous_linear_map.topological_space
local attribute [-instance] continuous_linear_map.uniform_space
local attribute [instance] continuous_linear_map.tmp_seminormed_add_comm_group

protected lemma tmp_topological_add_group : topological_add_group (E â†’SL[Ïƒâ‚â‚‚] F) :=
infer_instance

protected lemma tmp_closed_ball_div_subset {a b : â„} (ha : 0 < a) (hb : 0 < b) :
  closed_ball (0 : E â†’SL[Ïƒâ‚â‚‚] F) (a / b) âŠ†
  {f | âˆ€ x âˆˆ closed_ball (0 : E) b, f x âˆˆ closed_ball (0 : F) a} :=
begin
  intros f hf x hx,
  rw mem_closed_ball_zero_iff at âŠ¢ hf hx,
  calc â€–f xâ€–
      â‰¤ â€–fâ€– * â€–xâ€– : le_op_norm _ _
  ... â‰¤ (a/b) * b : mul_le_mul hf hx (norm_nonneg _) (div_pos ha hb).le
  ... = a : div_mul_cancel a hb.ne.symm
end

end tmp

protected theorem tmp_topology_eq :
  (continuous_linear_map.tmp_topological_space : topological_space (E â†’SL[Ïƒâ‚â‚‚] F)) =
  continuous_linear_map.topological_space :=
begin
  refine continuous_linear_map.tmp_topological_add_group.ext infer_instance
    ((@metric.nhds_basis_closed_ball _ continuous_linear_map.tmp_pseudo_metric_space 0).ext
      (continuous_linear_map.has_basis_nhds_zero_of_basis metric.nhds_basis_closed_ball) _ _),
  { rcases normed_field.exists_norm_lt_one ğ•œ with âŸ¨c, hcâ‚€, hcâ‚âŸ©,
    refine Î» Îµ hÎµ, âŸ¨âŸ¨closed_ball 0 (1 / â€–câ€–), ÎµâŸ©,
      âŸ¨normed_space.is_vonN_bounded_closed_ball _ _ _, hÎµâŸ©, Î» f hf, _âŸ©,
    change âˆ€ x, _ at hf,
    simp_rw mem_closed_ball_zero_iff at hf,
    rw @mem_closed_ball_zero_iff _ seminormed_add_comm_group.to_seminormed_add_group,
    refine op_norm_le_of_shell' (div_pos one_pos hcâ‚€) hÎµ.le hcâ‚ (Î» x hxâ‚ hxc, _),
    rw div_mul_cancel 1 hcâ‚€.ne.symm at hxâ‚,
    exact (hf x hxc.le).trans (le_mul_of_one_le_right hÎµ.le hxâ‚) },
  { rintros âŸ¨S, ÎµâŸ© âŸ¨hS, hÎµâŸ©,
    rw [normed_space.is_vonN_bounded_iff, â† bounded_iff_is_bounded] at hS,
    rcases hS.subset_ball_lt 0 0 with âŸ¨Î´, hÎ´, hSÎ´âŸ©,
    exact âŸ¨Îµ/Î´, div_pos hÎµ hÎ´, (continuous_linear_map.tmp_closed_ball_div_subset hÎµ hÎ´).trans $
      Î» f hf x hx, hf x $ hSÎ´ hxâŸ© }
end

protected theorem tmp_uniform_space_eq :
  (continuous_linear_map.tmp_uniform_space : uniform_space (E â†’SL[Ïƒâ‚â‚‚] F)) =
  continuous_linear_map.uniform_space :=
begin
  rw [â† @uniform_add_group.to_uniform_space_eq _ continuous_linear_map.tmp_uniform_space,
      â† @uniform_add_group.to_uniform_space_eq _ continuous_linear_map.uniform_space],
  congr' 1,
  exact continuous_linear_map.tmp_topology_eq
end

instance to_pseudo_metric_space : pseudo_metric_space (E â†’SL[Ïƒâ‚â‚‚] F) :=
continuous_linear_map.tmp_pseudo_metric_space.replace_uniformity
  (congr_arg _ continuous_linear_map.tmp_uniform_space_eq.symm)

/-- Continuous linear maps themselves form a seminormed space with respect to
    the operator norm. -/
instance to_seminormed_add_comm_group : seminormed_add_comm_group (E â†’SL[Ïƒâ‚â‚‚] F) :=
{ dist_eq := continuous_linear_map.tmp_seminormed_add_comm_group.dist_eq }

lemma nnnorm_def (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€–â‚Š = Inf {c | âˆ€ x, â€–f xâ€–â‚Š â‰¤ c * â€–xâ€–â‚Š} :=
begin
  ext,
  rw [nnreal.coe_Inf, coe_nnnorm, norm_def, nnreal.coe_image],
  simp_rw [â† nnreal.coe_le_coe, nnreal.coe_mul, coe_nnnorm, mem_set_of_eq, subtype.coe_mk,
    exists_prop],
end

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma op_nnnorm_le_bound (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) :
  â€–fâ€–â‚Š â‰¤ M :=
op_norm_le_bound f (zero_le M) hM

/-- If one controls the norm of every `A x`, `â€–xâ€–â‚Š â‰  0`, then one controls the norm of `A`. -/
lemma op_nnnorm_le_bound' (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–xâ€–â‚Š â‰  0 â†’ â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) :
  â€–fâ€–â‚Š â‰¤ M :=
op_norm_le_bound' f (zero_le M) $ Î» x hx, hM x $ by rwa [â† nnreal.coe_ne_zero]

/-- For a continuous real linear map `f`, if one controls the norm of every `f x`, `â€–xâ€–â‚Š = 1`, then
one controls the norm of `f`. -/
lemma op_nnnorm_le_of_unit_nnnorm [normed_space â„ E] [normed_space â„ F] {f : E â†’L[â„] F} {C : â„â‰¥0}
  (hf : âˆ€ x, â€–xâ€–â‚Š = 1 â†’ â€–f xâ€–â‚Š â‰¤ C) : â€–fâ€–â‚Š â‰¤ C :=
op_norm_le_of_unit_norm C.coe_nonneg $ Î» x hx, hf x $ by rwa [â† nnreal.coe_eq_one]

theorem op_nnnorm_le_of_lipschitz {f : E â†’SL[Ïƒâ‚â‚‚] F} {K : â„â‰¥0} (hf : lipschitz_with K f) :
  â€–fâ€–â‚Š â‰¤ K :=
op_norm_le_of_lipschitz hf

lemma op_nnnorm_eq_of_bounds {Ï† : E â†’SL[Ïƒâ‚â‚‚] F} (M : â„â‰¥0)
  (h_above : âˆ€ x, â€–Ï† xâ€– â‰¤ M*â€–xâ€–) (h_below : âˆ€ N, (âˆ€ x, â€–Ï† xâ€–â‚Š â‰¤ N*â€–xâ€–â‚Š) â†’ M â‰¤ N) :
  â€–Ï†â€–â‚Š = M :=
subtype.ext $ op_norm_eq_of_bounds (zero_le M) h_above $ subtype.forall'.mpr h_below

instance to_normed_space {ğ•œ' : Type*} [normed_field ğ•œ'] [normed_space ğ•œ' F]
  [smul_comm_class ğ•œâ‚‚ ğ•œ' F] : normed_space ğ•œ' (E â†’SL[Ïƒâ‚â‚‚] F) :=
âŸ¨op_norm_smul_leâŸ©

include Ïƒâ‚â‚ƒ
/-- The operator norm is submultiplicative. -/
lemma op_norm_comp_le (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€– â‰¤ â€–hâ€– * â€–fâ€– :=
(cInf_le bounds_bdd_below
  âŸ¨mul_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Î» x,
    by { rw mul_assoc, exact h.le_op_norm_of_le (f.le_op_norm x) } âŸ©)

lemma op_nnnorm_comp_le [ring_hom_isometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€–â‚Š â‰¤ â€–hâ€–â‚Š * â€–fâ€–â‚Š :=
op_norm_comp_le h f
omit Ïƒâ‚â‚ƒ

/-- Continuous linear maps form a seminormed ring with respect to the operator norm. -/
instance to_semi_normed_ring : semi_normed_ring (E â†’L[ğ•œ] E) :=
{ norm_mul := Î» f g, op_norm_comp_le f g,
  .. continuous_linear_map.to_seminormed_add_comm_group, .. continuous_linear_map.ring }

/-- For a normed space `E`, continuous linear endomorphisms form a normed algebra with
respect to the operator norm. -/
instance to_normed_algebra : normed_algebra ğ•œ (E â†’L[ğ•œ] E) :=
{ .. continuous_linear_map.to_normed_space,
  .. continuous_linear_map.algebra }

end

end op_norm

end continuous_linear_map

namespace linear_map

/-- If a continuous linear map is constructed from a linear map via the constructor `mk_continuous`,
then its norm is bounded by the bound given to the constructor if it is nonnegative. -/
lemma mk_continuous_norm_le (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {C : â„} (hC : 0 â‰¤ C) (h : âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
  â€–f.mk_continuous C hâ€– â‰¤ C :=
continuous_linear_map.op_norm_le_bound _ hC h

/-- If a continuous linear map is constructed from a linear map via the constructor `mk_continuous`,
then its norm is bounded by the bound or zero if bound is negative. -/
lemma mk_continuous_norm_le' (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {C : â„} (h : âˆ€x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
  â€–f.mk_continuous C hâ€– â‰¤ max C 0 :=
continuous_linear_map.op_norm_le_bound _ (le_max_right _ _) $ Î» x, (h x).trans $
  mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg x)

variables [ring_hom_isometric Ïƒâ‚‚â‚ƒ]

/-- Create a bilinear map (represented as a map `E â†’L[ğ•œ] F â†’L[ğ•œ] G`) from the corresponding linear
map and a bound on the norm of the image. The linear map can be constructed using
`linear_map.mkâ‚‚`. -/
def mk_continuousâ‚‚ (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) (C : â„)
  (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) :
  E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G :=
linear_map.mk_continuous
  { to_fun := Î» x, (f x).mk_continuous (C * â€–xâ€–) (hC x),
    map_add' := Î» x y,
    begin
      ext z,
      rw [continuous_linear_map.add_apply, mk_continuous_apply, mk_continuous_apply,
          mk_continuous_apply, map_add, add_apply]
    end,
    map_smul' := Î» c x,
    begin
      ext z,
      rw [continuous_linear_map.smul_apply, mk_continuous_apply, mk_continuous_apply, map_smulâ‚›â‚—,
          smul_apply]
    end, }
  (max C 0) $ Î» x, (mk_continuous_norm_le' _ _).trans_eq $
    by rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]

@[simp] lemma mk_continuousâ‚‚_apply (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„}
  (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) (x : E) (y : F) :
  f.mk_continuousâ‚‚ C hC x y = f x y :=
rfl

lemma mk_continuousâ‚‚_norm_le' (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„}
  (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) :
  â€–f.mk_continuousâ‚‚ C hCâ€– â‰¤ max C 0 :=
mk_continuous_norm_le _ (le_max_iff.2 $ or.inr le_rfl) _

lemma mk_continuousâ‚‚_norm_le (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„} (h0 : 0 â‰¤ C)
  (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) :
  â€–f.mk_continuousâ‚‚ C hCâ€– â‰¤ C :=
(f.mk_continuousâ‚‚_norm_le' hC).trans_eq $ max_eq_left h0

end linear_map

end semi_normed

section normed

variables [normed_add_comm_group E] [normed_add_comm_group F] [normed_add_comm_group G]
  [normed_add_comm_group Fâ‚—]

open metric continuous_linear_map

section
variables [nontrivially_normed_field ğ•œ] [nontrivially_normed_field ğ•œâ‚‚]
  [nontrivially_normed_field ğ•œâ‚ƒ] [normed_space ğ•œ E] [normed_space ğ•œâ‚‚ F] [normed_space ğ•œâ‚ƒ G]
  [normed_space ğ•œ Fâ‚—] (c : ğ•œ)
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ}
  (f g : E â†’SL[Ïƒâ‚â‚‚] F) (x y z : E)

namespace continuous_linear_map

section op_norm
open set real

/-- An operator is zero iff its norm vanishes. -/
theorem op_norm_zero_iff [ring_hom_isometric Ïƒâ‚â‚‚] : â€–fâ€– = 0 â†” f = 0 :=
iff.intro
  (Î» hn, continuous_linear_map.ext (Î» x, norm_le_zero_iff.1
    (calc _ â‰¤ â€–fâ€– * â€–xâ€– : le_op_norm _ _
     ...     = _ : by rw [hn, zero_mul])))
  (by { rintro rfl, exact op_norm_zero })

/-- If a normed space is non-trivial, then the norm of the identity equals `1`. -/
@[simp] lemma norm_id [nontrivial E] : â€–id ğ•œ Eâ€– = 1 :=
begin
  refine norm_id_of_nontrivial_seminorm _,
  obtain âŸ¨x, hxâŸ© := exists_ne (0 : E),
  exact âŸ¨x, ne_of_gt (norm_pos_iff.2 hx)âŸ©,
end

instance norm_one_class [nontrivial E] : norm_one_class (E â†’L[ğ•œ] E) := âŸ¨norm_idâŸ©

/-- Continuous linear maps themselves form a normed space with respect to
    the operator norm. -/
instance to_normed_add_comm_group [ring_hom_isometric Ïƒâ‚â‚‚] : normed_add_comm_group (E â†’SL[Ïƒâ‚â‚‚] F) :=
normed_add_comm_group.of_separation (Î» f, (op_norm_zero_iff f).mp)

/-- Continuous linear maps form a normed ring with respect to the operator norm. -/
instance to_normed_ring : normed_ring (E â†’L[ğ•œ] E) :=
{ .. continuous_linear_map.to_normed_add_comm_group, .. continuous_linear_map.to_semi_normed_ring }

end op_norm

end continuous_linear_map

end

end normed
