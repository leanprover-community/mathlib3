/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo

Operator norm on the space of continuous linear maps

Define the operator norm on the space of continuous linear maps between normed spaces, and prove
its basic properties. In particular, show that this space is itself a normed space.
-/

import topology.metric_space.lipschitz analysis.normed_space.riesz_lemma
import analysis.asymptotics
noncomputable theory
open_locale classical

set_option class.instance_max_depth 70

variables {ğ•œ : Type*} {E : Type*} {F : Type*} {G : Type*}
[normed_group E] [normed_group F] [normed_group G]

open metric continuous_linear_map

lemma exists_pos_bound_of_bound {f : E â†’ F} (M : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ M * âˆ¥xâˆ¥) :
  âˆƒ N, 0 < N âˆ§ âˆ€x, âˆ¥f xâˆ¥ â‰¤ N * âˆ¥xâˆ¥ :=
âŸ¨max M 1, lt_of_lt_of_le zero_lt_one (le_max_right _ _), Î»x, calc
  âˆ¥f xâˆ¥ â‰¤ M * âˆ¥xâˆ¥ : h x
  ... â‰¤ max M 1 * âˆ¥xâˆ¥ : mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _) âŸ©

section normed_field
/- Most statements in this file require the field to be non-discrete, as this is necessary
to deduce an inequality `âˆ¥f xâˆ¥ â‰¤ C âˆ¥xâˆ¥` from the continuity of f. However, the other direction always
holds. In this section, we just assume that `ğ•œ` is a normed field. In the remainder of the file,
it will be non-discrete. -/

variables [normed_field ğ•œ] [normed_space ğ•œ E] [normed_space ğ•œ F] (f : E â†’â‚—[ğ•œ] F)

lemma linear_map.lipschitz_of_bound (C : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  lipschitz_with (nnreal.of_real C) f :=
lipschitz_with.of_dist_le $ Î» x y, by simpa [dist_eq_norm] using h (x - y)

lemma linear_map.uniform_continuous_of_bound (C : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  uniform_continuous f :=
(f.lipschitz_of_bound C h).to_uniform_continuous

lemma linear_map.continuous_of_bound (C : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  continuous f :=
(f.lipschitz_of_bound C h).to_continuous

/-- Construct a continuous linear map from a linear map and a bound on this linear map.
The fact that the norm of the continuous linear map is then controlled is given in
`linear_map.mk_continuous_norm_le`. -/
def linear_map.mk_continuous (C : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) : E â†’L[ğ•œ] F :=
âŸ¨f, linear_map.continuous_of_bound f C hâŸ©

/-- Construct a continuous linear map from a linear map and the existence of a bound on this linear
map. If you have an explicit bound, use `linear_map.mk_continuous` instead, as a norm estimate will
follow automatically in `linear_map.mk_continuous_norm_le`. -/
def linear_map.mk_continuous_of_exists_bound (h : âˆƒC, âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) : E â†’L[ğ•œ] F :=
âŸ¨f, let âŸ¨C, hCâŸ© := h in linear_map.continuous_of_bound f C hCâŸ©

@[simp, elim_cast] lemma linear_map.mk_continuous_coe (C : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  ((f.mk_continuous C h) : E â†’â‚—[ğ•œ] F) = f := rfl

@[simp] lemma linear_map.mk_continuous_apply (C : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) (x : E) :
  f.mk_continuous C h x = f x := rfl

@[simp, elim_cast] lemma linear_map.mk_continuous_of_exists_bound_coe (h : âˆƒC, âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  ((f.mk_continuous_of_exists_bound h) : E â†’â‚—[ğ•œ] F) = f := rfl

@[simp] lemma linear_map.mk_continuous_of_exists_bound_apply (h : âˆƒC, âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) (x : E) :
  f.mk_continuous_of_exists_bound h x = f x := rfl

lemma linear_map.continuous_iff_is_closed_ker {f : E â†’â‚—[ğ•œ] ğ•œ} :
  continuous f â†” is_closed (f.ker : set E) :=
begin
  -- the continuity of f obviously implies that its kernel is closed
  refine âŸ¨Î»h, (continuous_iff_is_closed.1 h) {0} (t1_space.t1 0), Î»h, _âŸ©,
  -- for the other direction, we assume that the kernel is closed
  by_cases hf : âˆ€x, x âˆˆ f.ker,
  { -- if `f = 0`, its continuity is obvious
    have : (f : E â†’ ğ•œ) = (Î»x, 0), by { ext x, simpa using hf x },
    rw this,
    exact continuous_const },
  { /- if `f` is not zero, we use an element `xâ‚€ âˆ‰ ker f` such that `âˆ¥xâ‚€âˆ¥ â‰¤ 2 âˆ¥xâ‚€ - yâˆ¥` for all
    `y âˆˆ ker f`, given by Riesz's lemma, and prove that `2 âˆ¥f xâ‚€âˆ¥ / âˆ¥xâ‚€âˆ¥` gives a bound on the
    operator norm of `f`. For this, start from an arbitrary `x` and note that
    `y = xâ‚€ - (f xâ‚€ / f x) x` belongs to the kernel of `f`. Applying the above inequality to `xâ‚€`
    and `y` readily gives the conclusion. -/
    push_neg at hf,
    let r : â„ := (2 : â„)â»Â¹,
    have : 0 â‰¤ r, by norm_num [r],
    have : r < 1, by norm_num [r],
    obtain âŸ¨xâ‚€, xâ‚€ker, hâ‚€âŸ© : âˆƒ (xâ‚€ : E), xâ‚€ âˆ‰ f.ker âˆ§ âˆ€ y âˆˆ linear_map.ker f, r * âˆ¥xâ‚€âˆ¥ â‰¤ âˆ¥xâ‚€ - yâˆ¥,
      from riesz_lemma h hf this,
    have : xâ‚€ â‰  0,
    { assume h,
      have : xâ‚€ âˆˆ f.ker, by { rw h, exact (linear_map.ker f).zero },
      exact xâ‚€ker this },
    have rxâ‚€_ne_zero : r * âˆ¥xâ‚€âˆ¥ â‰  0, by { simp [norm_eq_zero, this], norm_num },
    have : âˆ€x, âˆ¥f xâˆ¥ â‰¤ (((r * âˆ¥xâ‚€âˆ¥)â»Â¹) * âˆ¥f xâ‚€âˆ¥) * âˆ¥xâˆ¥,
    { assume x,
      by_cases hx : f x = 0,
      { rw [hx, norm_zero],
        apply_rules [mul_nonneg', norm_nonneg, inv_nonneg.2, norm_nonneg] },
      { let y := xâ‚€ - (f xâ‚€ * (f x)â»Â¹ ) â€¢ x,
        have fy_zero : f y = 0, by calc
          f y = f xâ‚€ - (f xâ‚€ * (f x)â»Â¹ ) * f x :
            by { dsimp [y], rw [f.map_add, f.map_neg, f.map_smul], refl }
          ... = 0 :
            by { rw [mul_assoc, inv_mul_cancel hx, mul_one, sub_eq_zero_of_eq], refl },
        have A : r * âˆ¥xâ‚€âˆ¥ â‰¤ âˆ¥f xâ‚€âˆ¥ * âˆ¥f xâˆ¥â»Â¹ * âˆ¥xâˆ¥, from calc
          r * âˆ¥xâ‚€âˆ¥ â‰¤ âˆ¥xâ‚€ - yâˆ¥ : hâ‚€ _ (linear_map.mem_ker.2 fy_zero)
          ... = âˆ¥(f xâ‚€ * (f x)â»Â¹ ) â€¢ xâˆ¥ : by { dsimp [y], congr, abel }
          ... = âˆ¥f xâ‚€âˆ¥ * âˆ¥f xâˆ¥â»Â¹ * âˆ¥xâˆ¥ :
            by rw [norm_smul, normed_field.norm_mul, normed_field.norm_inv],
        calc
          âˆ¥f xâˆ¥ = (r * âˆ¥xâ‚€âˆ¥)â»Â¹ * (r * âˆ¥xâ‚€âˆ¥) * âˆ¥f xâˆ¥ : by rwa [inv_mul_cancel, one_mul]
          ... â‰¤ (r * âˆ¥xâ‚€âˆ¥)â»Â¹ * (âˆ¥f xâ‚€âˆ¥ * âˆ¥f xâˆ¥â»Â¹ * âˆ¥xâˆ¥) * âˆ¥f xâˆ¥ : begin
            apply mul_le_mul_of_nonneg_right (mul_le_mul_of_nonneg_left A _) (norm_nonneg _),
            exact inv_nonneg.2 (mul_nonneg' (by norm_num) (norm_nonneg _))
          end
          ... = (âˆ¥f xâˆ¥ â»Â¹ * âˆ¥f xâˆ¥) * (((r * âˆ¥xâ‚€âˆ¥)â»Â¹) * âˆ¥f xâ‚€âˆ¥) * âˆ¥xâˆ¥ : by ring
          ... = (((r * âˆ¥xâ‚€âˆ¥)â»Â¹) * âˆ¥f xâ‚€âˆ¥) * âˆ¥xâˆ¥ :
            by { rw [inv_mul_cancel, one_mul], simp [norm_eq_zero, hx] } } },
    exact linear_map.continuous_of_bound f _ this }
end

end normed_field

variables [nondiscrete_normed_field ğ•œ] [normed_space ğ•œ E] [normed_space ğ•œ F] [normed_space ğ•œ G]
(c : ğ•œ) (f g : E â†’L[ğ•œ] F) (h : F â†’L[ğ•œ] G) (x y z : E)
include ğ•œ

/-- A continuous linear map between normed spaces is bounded when the field is nondiscrete.
The continuity ensures boundedness on a ball of some radius `Î´`. The nondiscreteness is then
used to rescale any element into an element of norm in `[Î´/C, Î´]`, whose image has a controlled norm.
The norm control for the original element follows by rescaling. -/
lemma linear_map.bound_of_continuous (f : E â†’â‚—[ğ•œ] F) (hf : continuous f) :
  âˆƒ C, 0 < C âˆ§ (âˆ€ x : E, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :=
begin
  have : continuous_at f 0 := continuous_iff_continuous_at.1 hf _,
  rcases metric.tendsto_nhds_nhds.1 this 1 zero_lt_one with âŸ¨Îµ, Îµ_pos, hÎµâŸ©,
  let Î´ := Îµ/2,
  have Î´_pos : Î´ > 0 := half_pos Îµ_pos,
  have H : âˆ€{a}, âˆ¥aâˆ¥ â‰¤ Î´ â†’ âˆ¥f aâˆ¥ â‰¤ 1,
  { assume a ha,
    have : dist (f a) (f 0) â‰¤ 1,
    { apply le_of_lt (hÎµ _),
      rw [dist_eq_norm, sub_zero],
      exact lt_of_le_of_lt ha (half_lt_self Îµ_pos) },
    simpa using this },
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  refine âŸ¨Î´â»Â¹ * âˆ¥câˆ¥, mul_pos (inv_pos Î´_pos) (lt_trans zero_lt_one hc), (Î»x, _)âŸ©,
  by_cases h : x = 0,
  { simp only [h, norm_zero, mul_zero, linear_map.map_zero] },
  { rcases rescale_to_shell hc Î´_pos h with âŸ¨d, hd, dxle, ledx, dinvâŸ©,
    calc âˆ¥f xâˆ¥
      = âˆ¥f ((dâ»Â¹ * d) â€¢ x)âˆ¥ : by rwa [inv_mul_cancel, one_smul]
      ... = âˆ¥dâˆ¥â»Â¹ * âˆ¥f (d â€¢ x)âˆ¥ :
        by rw [mul_smul, linear_map.map_smul, norm_smul, normed_field.norm_inv]
      ... â‰¤ âˆ¥dâˆ¥â»Â¹ * 1 :
        mul_le_mul_of_nonneg_left (H dxle) (by { rw â† normed_field.norm_inv, exact norm_nonneg _ })
      ... â‰¤ Î´â»Â¹ * âˆ¥câˆ¥ * âˆ¥xâˆ¥ : by { rw mul_one, exact dinv } }
end

namespace continuous_linear_map

theorem bound : âˆƒ C, 0 < C âˆ§ (âˆ€ x : E, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :=
f.to_linear_map.bound_of_continuous f.2

section
open asymptotics filter

theorem is_O_id (l : filter E) : is_O f (Î» x, x) l :=
let âŸ¨M, hMp, hMâŸ© := f.bound in is_O_of_le' l hM

theorem is_O_comp {E : Type*} (g : F â†’L[ğ•œ] G) (f : E â†’ F) (l : filter E) :
  is_O (Î» x', g (f x')) f l :=
(g.is_O_id âŠ¤).comp_tendsto lattice.le_top

theorem is_O_sub (f : E â†’L[ğ•œ] F) (l : filter E) (x : E) :
  is_O (Î» x', f (x' - x)) (Î» x', x' - x) l :=
f.is_O_comp _ l

end

section op_norm
open set real

set_option class.instance_max_depth 100

/-- The operator norm of a continuous linear map is the inf of all its bounds. -/
def op_norm := Inf { c | c â‰¥ 0 âˆ§ âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥xâˆ¥ }
instance has_op_norm : has_norm (E â†’L[ğ•œ] F) := âŸ¨op_normâŸ©

-- So that invocations of `real.Inf_le` make sense: we show that the set of
-- bounds is nonempty and bounded below.
lemma bounds_nonempty {f : E â†’L[ğ•œ] F} :
  âˆƒ c, c âˆˆ { c | 0 â‰¤ c âˆ§ âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥xâˆ¥ } :=
let âŸ¨M, hMp, hMbâŸ© := f.bound in âŸ¨M, le_of_lt hMp, hMbâŸ©

lemma bounds_bdd_below {f : E â†’L[ğ•œ] F} :
  bdd_below { c | 0 â‰¤ c âˆ§ âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥xâˆ¥ } :=
âŸ¨0, Î» _ âŸ¨hn, _âŸ©, hnâŸ©

lemma op_norm_nonneg : 0 â‰¤ âˆ¥fâˆ¥ :=
lb_le_Inf _ bounds_nonempty (Î» _ âŸ¨hx, _âŸ©, hx)

/-- The fundamental property of the operator norm: `âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥`. -/
theorem le_op_norm : âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ :=
classical.by_cases
  (Î» heq : x = 0, by { rw heq, simp })
  (Î» hne, have hlt : 0 < âˆ¥xâˆ¥, from (norm_pos_iff _).2 hne,
    le_mul_of_div_le hlt ((le_Inf _ bounds_nonempty bounds_bdd_below).2
    (Î» c âŸ¨_, hcâŸ©, div_le_of_le_mul hlt (by { rw mul_comm, apply hc }))))

lemma ratio_le_op_norm : âˆ¥f xâˆ¥ / âˆ¥xâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
(or.elim (lt_or_eq_of_le (norm_nonneg _))
  (Î» hlt, div_le_of_le_mul hlt (by { rw mul_comm, apply le_op_norm }))
  (Î» heq, by { rw [â†heq, div_zero], apply op_norm_nonneg }))

/-- The image of the unit ball under a continuous linear map is bounded. -/
lemma unit_le_op_norm : âˆ¥xâˆ¥ â‰¤ 1 â†’ âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
Î» hx, begin
  rw [â†(mul_one âˆ¥fâˆ¥)],
  calc _ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ : le_op_norm _ _
  ...    â‰¤ _ : mul_le_mul_of_nonneg_left hx (op_norm_nonneg _)
end

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma op_norm_le_bound {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ M * âˆ¥xâˆ¥) :
  âˆ¥fâˆ¥ â‰¤ M :=
Inf_le _ bounds_bdd_below âŸ¨hMp, hMâŸ©

/-- The operator norm satisfies the triangle inequality. -/
theorem op_norm_add_le : âˆ¥f + gâˆ¥ â‰¤ âˆ¥fâˆ¥ + âˆ¥gâˆ¥ :=
Inf_le _ bounds_bdd_below
  âŸ¨add_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Î» x, by { rw add_mul,
    exact norm_add_le_of_le (le_op_norm _ _) (le_op_norm _ _) }âŸ©

/-- An operator is zero iff its norm vanishes. -/
theorem op_norm_zero_iff : âˆ¥fâˆ¥ = 0 â†” f = 0 :=
iff.intro
  (Î» hn, continuous_linear_map.ext (Î» x, (norm_le_zero_iff _).1
    (calc _ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ : le_op_norm _ _
     ...     = _ : by rw [hn, zero_mul])))
  (Î» hf, le_antisymm (Inf_le _ bounds_bdd_below
    âŸ¨ge_of_eq rfl, Î» _, le_of_eq (by { rw [zero_mul, hf], exact norm_zero })âŸ©)
    (op_norm_nonneg _))

@[simp] lemma norm_zero : âˆ¥(0 : E â†’L[ğ•œ] F)âˆ¥ = 0 :=
by rw op_norm_zero_iff

/-- The norm of the identity is at most `1`. It is in fact `1`, except when the space is trivial
where it is `0`. It means that one can not do better than an inequality in general. -/
lemma norm_id : âˆ¥(id : E â†’L[ğ•œ] E)âˆ¥ â‰¤ 1 :=
op_norm_le_bound _ zero_le_one (Î»x, by simp)

/-- The operator norm is homogeneous. -/
lemma op_norm_smul : âˆ¥c â€¢ fâˆ¥ = âˆ¥câˆ¥ * âˆ¥fâˆ¥ :=
le_antisymm
  (Inf_le _ bounds_bdd_below
    âŸ¨mul_nonneg (norm_nonneg _) (op_norm_nonneg _), Î» _,
    begin
      erw [norm_smul, mul_assoc],
      exact mul_le_mul_of_nonneg_left (le_op_norm _ _) (norm_nonneg _)
    endâŸ©)
  (lb_le_Inf _ bounds_nonempty (Î» _ âŸ¨hn, hcâŸ©,
    (or.elim (lt_or_eq_of_le (norm_nonneg c))
      (Î» hlt,
        begin
          rw mul_comm,
          exact mul_le_of_le_div hlt (Inf_le _ bounds_bdd_below
          âŸ¨div_nonneg hn hlt, Î» _,
          (by { rw div_mul_eq_mul_div, exact le_div_of_mul_le hlt
          (by { rw [ mul_comm, â†norm_smul ], exact hc _ }) })âŸ©)
        end)
      (Î» heq, by { rw [â†heq, zero_mul], exact hn }))))

lemma op_norm_neg : âˆ¥-fâˆ¥ = âˆ¥fâˆ¥ := calc
  âˆ¥-fâˆ¥ = âˆ¥(-1:ğ•œ) â€¢ fâˆ¥ : by rw neg_one_smul
  ... = âˆ¥(-1:ğ•œ)âˆ¥ * âˆ¥fâˆ¥ : by rw op_norm_smul
  ... = âˆ¥fâˆ¥ : by simp

/-- Continuous linear maps themselves form a normed space with respect to
    the operator norm. -/
instance to_normed_group : normed_group (E â†’L[ğ•œ] F) :=
normed_group.of_core _ âŸ¨op_norm_zero_iff, op_norm_add_le, op_norm_negâŸ©

instance to_normed_space : normed_space ğ•œ (E â†’L[ğ•œ] F) :=
âŸ¨op_norm_smulâŸ©

/-- The operator norm is submultiplicative. -/
lemma op_norm_comp_le : âˆ¥comp h fâˆ¥ â‰¤ âˆ¥hâˆ¥ * âˆ¥fâˆ¥ :=
(Inf_le _ bounds_bdd_below
  âŸ¨mul_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Î» x,
  begin
    rw mul_assoc,
    calc _ â‰¤ âˆ¥hâˆ¥ * âˆ¥f xâˆ¥: le_op_norm _ _
    ... â‰¤ _ : mul_le_mul_of_nonneg_left
              (le_op_norm _ _) (op_norm_nonneg _)
  endâŸ©)

/-- continuous linear maps are Lipschitz continuous. -/
theorem lipschitz : lipschitz_with âŸ¨âˆ¥fâˆ¥, op_norm_nonneg fâŸ© f :=
Î» x y, by { rw [dist_eq_norm, dist_eq_norm, â†map_sub], apply le_op_norm }

/-- A continuous linear map is automatically uniformly continuous. -/
protected theorem uniform_continuous : uniform_continuous f :=
f.lipschitz.to_uniform_continuous

variable {f}
/-- A continuous linear map is an isometry if and only if it preserves the norm. -/
lemma isometry_iff_norm_image_eq_norm :
  isometry f â†” âˆ€x, âˆ¥f xâˆ¥ = âˆ¥xâˆ¥ :=
begin
  rw isometry_emetric_iff_metric,
  split,
  { assume H x,
    have := H x 0,
    rwa [dist_eq_norm, dist_eq_norm, f.map_zero, sub_zero, sub_zero] at this },
  { assume H x y,
    rw [dist_eq_norm, dist_eq_norm, â† f.map_sub, H] }
end

variable (f)
/-- A continuous linear map is a uniform embedding if it expands the norm by a constant factor. -/
theorem uniform_embedding_of_bound (C : â„) (hC : âˆ€x, âˆ¥xâˆ¥ â‰¤ C * âˆ¥f xâˆ¥) :
  uniform_embedding f :=
begin
  have Cpos : 0 < max C 1 := lt_of_lt_of_le zero_lt_one (le_max_right _ _),
  refine uniform_embedding_iff'.2 âŸ¨metric.uniform_continuous_iff.1 f.uniform_continuous,
                                    Î»Î´ Î´pos, âŸ¨Î´ / (max C 1), div_pos Î´pos Cpos, Î»x y hxy, _âŸ©âŸ©,
  calc dist x y = âˆ¥x - yâˆ¥ : by rw dist_eq_norm
  ... â‰¤ C * âˆ¥f (x - y)âˆ¥ : hC _
  ... = C * dist (f x) (f y) : by rw [f.map_sub, dist_eq_norm]
  ... â‰¤ max C 1 * dist (f x) (f y) :
    mul_le_mul_of_nonneg_right (le_max_left _ _) dist_nonneg
  ... < max C 1 * (Î´ / max C 1) : mul_lt_mul_of_pos_left hxy Cpos
  ... = Î´ : by { rw mul_comm, exact div_mul_cancel _ (ne_of_lt Cpos).symm }
end

/-- If a continuous linear map is a uniform embedding, then it expands the norm by a positive
factor.-/
theorem bound_of_uniform_embedding (hf : uniform_embedding f) :
  âˆƒ C : â„, 0 < C âˆ§ âˆ€x, âˆ¥xâˆ¥ â‰¤ C * âˆ¥f xâˆ¥ :=
begin
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ (Îµ : â„) (H : Îµ > 0), âˆ€ {x y : E}, dist (f x) (f y) < Îµ â†’ dist x y < 1, from
    (uniform_embedding_iff.1 hf).2.2 1 zero_lt_one,
  let Î´ := Îµ/2,
  have Î´_pos : Î´ > 0 := half_pos Îµpos,
  have H : âˆ€{x}, âˆ¥f xâˆ¥ â‰¤ Î´ â†’ âˆ¥xâˆ¥ â‰¤ 1,
  { assume x hx,
    have : dist x 0 â‰¤ 1,
    { apply le_of_lt,
      apply hÎµ,
      simp [dist_eq_norm],
      exact lt_of_le_of_lt hx (half_lt_self Îµpos) },
  simpa using this },
  rcases normed_field.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©,
  refine âŸ¨Î´â»Â¹ * âˆ¥câˆ¥, (mul_pos (inv_pos Î´_pos) ((lt_trans zero_lt_one hc))), (Î»x, _)âŸ©,
  by_cases hx : f x = 0,
  { have : f x = f 0, by { simp [hx] },
    have : x = 0 := (uniform_embedding_iff.1 hf).1 this,
    simp [this] },
  { rcases rescale_to_shell hc Î´_pos hx with âŸ¨d, hd, dxle, ledx, dinvâŸ©,
    have : âˆ¥f (d â€¢ x)âˆ¥ â‰¤ Î´, by simpa,
    have : âˆ¥d â€¢ xâˆ¥ â‰¤ 1 := H this,
    calc âˆ¥xâˆ¥ = âˆ¥dâˆ¥â»Â¹ * âˆ¥d â€¢ xâˆ¥ :
      by rwa [â† normed_field.norm_inv, â† norm_smul, â† mul_smul, inv_mul_cancel, one_smul]
    ... â‰¤ âˆ¥dâˆ¥â»Â¹ * 1 :
      mul_le_mul_of_nonneg_left this (inv_nonneg.2 (norm_nonneg _))
    ... â‰¤ Î´â»Â¹ * âˆ¥câˆ¥ * âˆ¥f xâˆ¥ :
      by rwa [mul_one] }
end

section uniformly_extend

variables [complete_space F] (e : E â†’L[ğ•œ] G) (h_dense : dense_range e)

section
variables (h_e : uniform_inducing e)

/-- Extension of a continuous linear map `f : E â†’L[ğ•œ] F`, with `E` a normed space and `F` a complete
    normed space, along a uniform and dense embedding `e : E â†’L[ğ•œ] G`.  -/
def extend : G â†’L[ğ•œ] F :=
/- extension of `f` is continuous -/
have cont : _ := (uniform_continuous_uniformly_extend h_e h_dense f.uniform_continuous).continuous,
/- extension of `f` agrees with `f` on the domain of the embedding `e` -/
have eq : _ := uniformly_extend_of_ind h_e h_dense f.uniform_continuous,
{ to_fun := (h_e.dense_inducing h_dense).extend f,
  add :=
  begin
    refine is_closed_property2 h_dense (is_closed_eq _ _) _,
    { exact cont.comp (continuous_fst.add continuous_snd) },
    { exact (cont.comp continuous_fst).add (cont.comp continuous_snd) },
    { assume x y, rw â† e.map_add, simp only [eq], exact f.map_add _ _  },
  end,
  smul := Î»k,
  begin
    refine is_closed_property h_dense (is_closed_eq _ _) _,
    { exact cont.comp (continuous_const.smul continuous_id)  },
    { exact (continuous_const.smul continuous_id).comp cont },
    { assume x, rw â† map_smul, simp only [eq], exact map_smul _ _ _  },
  end,
  cont := cont
}

@[simp] lemma extend_zero : extend (0 : E â†’L[ğ•œ] F) e h_dense h_e = 0 :=
begin
  apply ext,
  refine is_closed_property h_dense (is_closed_eq _ _) _,
  { exact (uniform_continuous_uniformly_extend h_e h_dense uniform_continuous_const).continuous },
  { simp only [zero_apply], exact continuous_const },
  { assume x, exact uniformly_extend_of_ind h_e h_dense uniform_continuous_const x }
end

end

section
variables {N : â„} (h_e : âˆ€x, âˆ¥xâˆ¥ â‰¤ N * âˆ¥e xâˆ¥)

local notation `Ïˆ` := f.extend e h_dense (uniform_embedding_of_bound _ _ h_e).to_uniform_inducing

/-- If a dense embedding `e : E â†’L[ğ•œ] G` expands the norm by a constant factor `Nâ»Â¹`, then the norm
    of the extension of `f` along `e` is bounded by `N * âˆ¥fâˆ¥`. -/
lemma op_norm_extend_le : âˆ¥Ïˆâˆ¥ â‰¤ N * âˆ¥fâˆ¥ :=
begin
  have uni : uniform_inducing e := (uniform_embedding_of_bound _ _ h_e).to_uniform_inducing,
  have eq : âˆ€x, Ïˆ (e x) = f x := uniformly_extend_of_ind uni h_dense f.uniform_continuous,
  by_cases N0 : 0 â‰¤ N,
  { refine op_norm_le_bound Ïˆ _ (is_closed_property h_dense (is_closed_le _ _) _),
    { exact mul_nonneg N0 (norm_nonneg _) },
    { exact continuous_norm.comp (cont Ïˆ) },
    { exact continuous_const.mul continuous_norm },
    { assume x,
      rw eq,
      calc âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ : le_op_norm _ _
        ... â‰¤ âˆ¥fâˆ¥ * (N * âˆ¥e xâˆ¥) : mul_le_mul_of_nonneg_left (h_e x) (norm_nonneg _)
        ... â‰¤ N * âˆ¥fâˆ¥ * âˆ¥e xâˆ¥ : by rw [mul_comm N âˆ¥fâˆ¥, mul_assoc] } },
  { have he : âˆ€ x : E, x = 0,
    { assume x,
      have N0 : N â‰¤ 0 := le_of_lt (lt_of_not_ge N0),
      rw â† norm_le_zero_iff,
      exact le_trans (h_e x) (mul_nonpos_of_nonpos_of_nonneg N0 (norm_nonneg _)) },
    have hf : f = 0, { ext, simp only [he x, zero_apply, map_zero] },
    have hÏˆ : Ïˆ = 0, { rw hf, apply extend_zero },
    rw [hÏˆ, hf, norm_zero, norm_zero, mul_zero] }
end

end

end uniformly_extend

end op_norm

/-- The norm of the tensor product of a scalar linear map and of an element of a normed space
is the product of the norms. -/
@[simp] lemma smul_right_norm {c : E â†’L[ğ•œ] ğ•œ} {f : F} :
  âˆ¥smul_right c fâˆ¥ = âˆ¥câˆ¥ * âˆ¥fâˆ¥ :=
begin
  refine le_antisymm _ _,
  { apply op_norm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) (Î»x, _),
    calc
     âˆ¥(c x) â€¢ fâˆ¥ = âˆ¥c xâˆ¥ * âˆ¥fâˆ¥ : norm_smul _ _
     ... â‰¤ (âˆ¥câˆ¥ * âˆ¥xâˆ¥) * âˆ¥fâˆ¥ :
       mul_le_mul_of_nonneg_right (le_op_norm _ _) (norm_nonneg _)
     ... = âˆ¥câˆ¥ * âˆ¥fâˆ¥ * âˆ¥xâˆ¥ : by ring },
  { by_cases h : âˆ¥fâˆ¥ = 0,
    { rw h, simp [norm_nonneg] },
    { have : 0 < âˆ¥fâˆ¥ := lt_of_le_of_ne (norm_nonneg _) (ne.symm h),
      rw â† le_div_iff this,
      apply op_norm_le_bound _ (div_nonneg (norm_nonneg _) this) (Î»x, _),
      rw [div_mul_eq_mul_div, le_div_iff this],
      calc âˆ¥c xâˆ¥ * âˆ¥fâˆ¥ = âˆ¥c x â€¢ fâˆ¥ : (norm_smul _ _).symm
      ... = âˆ¥((smul_right c f) : E â†’ F) xâˆ¥ : rfl
      ... â‰¤ âˆ¥smul_right c fâˆ¥ * âˆ¥xâˆ¥ : le_op_norm _ _ } },
end

section restrict_scalars

variable (ğ•œ)
variables {ğ•œ' : Type*} [normed_field ğ•œ'] [normed_algebra ğ•œ ğ•œ']
{E' : Type*} [normed_group E'] [normed_space ğ•œ' E']
{F' : Type*} [normed_group F'] [normed_space ğ•œ' F']

local attribute [instance, priority 500] normed_space.restrict_scalars

/-- `ğ•œ`-linear continuous function induced by a `ğ•œ'`-linear continuous function when `ğ•œ'` is a
normed algebra over `ğ•œ`. -/
def restrict_scalars (f : E' â†’L[ğ•œ'] F') : E' â†’L[ğ•œ] F' :=
{ cont := f.cont,
  ..linear_map.restrict_scalars ğ•œ (f.to_linear_map) }

@[simp, move_cast] lemma restrict_scalars_coe_eq_coe (f : E' â†’L[ğ•œ'] F') :
  (f.restrict_scalars ğ•œ : E' â†’â‚—[ğ•œ] F') = (f : E' â†’â‚—[ğ•œ'] F').restrict_scalars ğ•œ := rfl

@[simp, squash_cast] lemma restrict_scalars_coe_eq_coe' (f : E' â†’L[ğ•œ'] F') :
  (f.restrict_scalars ğ•œ : E' â†’ F') = f := rfl

end restrict_scalars

end continuous_linear_map

/-- If both directions in a linear equiv `e` are continuous, then `e` is a uniform embedding. -/
lemma linear_equiv.uniform_embedding (e : E â‰ƒâ‚—[ğ•œ] F) (hâ‚ : continuous e) (hâ‚‚ : continuous e.symm) :
  uniform_embedding e :=
begin
  rcases linear_map.bound_of_continuous e.symm.to_linear_map hâ‚‚ with âŸ¨C, Cpos, hCâŸ©,
  let f : E â†’L[ğ•œ] F := { cont := hâ‚, ..e },
  apply f.uniform_embedding_of_bound C (Î»x, _),
  have : e.symm (e x) = x := linear_equiv.symm_apply_apply _ _,
  conv_lhs { rw â† this },
  exact hC _
end

/-- If a continuous linear map is constructed from a linear map via the constructor `mk_continuous`,
then its norm is bounded by the bound given to the constructor if it is nonnegative. -/
lemma linear_map.mk_continuous_norm_le (f : E â†’â‚—[ğ•œ] F) {C : â„} (hC : 0 â‰¤ C) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  âˆ¥f.mk_continuous C hâˆ¥ â‰¤ C :=
continuous_linear_map.op_norm_le_bound _ hC h
