/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo

Operator norm on the space of continuous linear maps

Define the operator norm on the space of continuous linear maps between normed spaces, and prove
its basic properties. In particular, show that this space is itself a normed space.
-/

import topology.metric_space.lipschitz
import analysis.asymptotics
noncomputable theory
local attribute [instance] classical.prop_decidable

set_option class.instance_max_depth 70

variables {ğ•‚ : Type*} {E : Type*} {F : Type*} {G : Type*}
[nondiscrete_normed_field ğ•‚]
[normed_group E] [normed_space ğ•‚ E]
[normed_group F] [normed_space ğ•‚ F]
[normed_group G] [normed_space ğ•‚ G]
(c : ğ•‚) (f g : E â†’L[ğ•‚] F) (h : F â†’L[ğ•‚] G) (x y z : E)
include ğ•‚

open metric continuous_linear_map

lemma exists_pos_bound_of_bound {f : E â†’ F} (M : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ M * âˆ¥xâˆ¥) :
  âˆƒ N > 0, âˆ€x, âˆ¥f xâˆ¥ â‰¤ N * âˆ¥xâˆ¥ :=
âŸ¨max M 1, lt_of_lt_of_le zero_lt_one (le_max_right _ _), Î»x, calc
  âˆ¥f xâˆ¥ â‰¤ M * âˆ¥xâˆ¥ : h x
  ... â‰¤ max M 1 * âˆ¥xâˆ¥ : mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _) âŸ©

lemma linear_map.continuous_of_bound (f : E â†’â‚—[ğ•‚] F) (C : â„) (h : âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  continuous f :=
begin
  have : âˆ€ (x y : E), dist (f x) (f y) â‰¤ C * dist x y := Î»x y, calc
    dist (f x) (f y) = âˆ¥f x - f yâˆ¥ : by rw dist_eq_norm
    ... = âˆ¥f (x - y)âˆ¥ : by simp
    ... â‰¤ C * âˆ¥x - yâˆ¥ : h _
    ... = C * dist x y : by rw dist_eq_norm,
  exact continuous_of_lipschitz this
end

def linear_map.with_bound (f : E â†’â‚—[ğ•‚] F) (h : âˆƒC : â„, âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) : E â†’L[ğ•‚] F :=
âŸ¨f, let âŸ¨C, hCâŸ© := h in linear_map.continuous_of_bound f C hCâŸ©

@[simp, elim_cast] lemma linear_map_with_bound_coe (f : E â†’â‚—[ğ•‚] F) (h : âˆƒC : â„, âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) :
  ((f.with_bound h) : E â†’â‚—[ğ•‚] F) = f := rfl

@[simp] lemma linear_map_with_bound_apply (f : E â†’â‚—[ğ•‚] F) (h : âˆƒC : â„, âˆ€x, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥) (x : E) :
  f.with_bound h x = f x := rfl

namespace continuous_linear_map

/-- A continuous linear map between normed spaces is bounded when the field is nondiscrete.
The continuity ensures boundedness on a ball of some radius Î´. The nondiscreteness is then
used to rescale any element into an element of norm in [Î´/C, Î´], whose image has a controlled norm.
The norm control for the original element follows by rescaling. -/
theorem bound : âˆƒ C > 0, âˆ€ x : E, âˆ¥f xâˆ¥ â‰¤ C * âˆ¥xâˆ¥ :=
begin
  have : continuous_at f 0 := continuous_iff_continuous_at.1 f.2 _,
  rcases metric.tendsto_nhds_nhds.1 this 1 zero_lt_one with âŸ¨Îµ, Îµ_pos, hÎµâŸ©,
  let Î´ := Îµ/2,
  have Î´_pos : Î´ > 0 := half_pos Îµ_pos,
  have H : âˆ€{a}, âˆ¥aâˆ¥ â‰¤ Î´ â†’ âˆ¥f aâˆ¥ â‰¤ 1,
  { assume a ha,
    have : dist (f a) (f 0) â‰¤ 1,
    { apply le_of_lt (hÎµ _),
      rw [dist_eq_norm, sub_zero],
      exact lt_of_le_of_lt ha (half_lt_self Îµ_pos) },
    simpa using this },
  rcases exists_one_lt_norm ğ•‚ with âŸ¨c, hcâŸ©,
  refine âŸ¨Î´â»Â¹ * âˆ¥câˆ¥, mul_pos (inv_pos Î´_pos) (lt_trans zero_lt_one hc), (Î»x, _)âŸ©,
  by_cases h : x = 0,
  { simp only [h, norm_zero, mul_zero, continuous_linear_map.map_zero], },
  { rcases rescale_to_shell hc Î´_pos h with âŸ¨d, hd, dxle, ledx, dinvâŸ©,
    calc âˆ¥f xâˆ¥
      = âˆ¥f ((dâ»Â¹ * d) â€¢ x)âˆ¥ : by rwa [inv_mul_cancel, one_smul]
      ... = âˆ¥dâˆ¥â»Â¹ * âˆ¥f (d â€¢ x)âˆ¥ :
        by rw [mul_smul, map_smul, norm_smul, norm_inv]
      ... â‰¤ âˆ¥dâˆ¥â»Â¹ * 1 :
        mul_le_mul_of_nonneg_left (H dxle) (by { rw â† norm_inv, exact norm_nonneg _ })
      ... â‰¤ Î´â»Â¹ * âˆ¥câˆ¥ * âˆ¥xâˆ¥ : by { rw mul_one, exact dinv } }
end

section
open asymptotics filter

theorem is_O_id (l : filter E) : is_O f (Î» x, x) l :=
let âŸ¨M, hMp, hMâŸ© := f.bound in
âŸ¨M, hMp, mem_sets_of_superset univ_mem_sets (Î» x _, hM x)âŸ©

theorem is_O_comp (g : F â†’L[ğ•‚] G) (f : E â†’ F) (l : filter E) :
  is_O (Î» x', g (f x')) f l :=
((g.is_O_id âŠ¤).comp _).mono (map_le_iff_le_comap.mp lattice.le_top)

theorem is_O_sub (f : E â†’L[ğ•‚] F) (l : filter E) (x : E) :
  is_O (Î» x', f (x' - x)) (Î» x', x' - x) l :=
is_O_comp f _ l

end

section op_norm
open set real

set_option class.instance_max_depth 100

/-- The operator norm of a continuous linear map is the inf of all its bounds. -/
def op_norm := Inf { c | c â‰¥ 0 âˆ§ âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥xâˆ¥ }
instance has_op_norm : has_norm (E â†’L[ğ•‚] F) := âŸ¨op_normâŸ©

-- So that invocations of real.Inf_le mağ•‚e sense: we show that the set of
-- bounds is nonempty and bounded below.
lemma bounds_nonempty {f : E â†’L[ğ•‚] F} :
  âˆƒ c, c âˆˆ { c | c â‰¥ 0 âˆ§ âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥xâˆ¥ } :=
let âŸ¨M, hMp, hMbâŸ© := f.bound in âŸ¨M, le_of_lt hMp, hMbâŸ©

lemma bounds_bdd_below {f : E â†’L[ğ•‚] F} :
  bdd_below { c | c â‰¥ 0 âˆ§ âˆ€ x, âˆ¥f xâˆ¥ â‰¤ c * âˆ¥xâˆ¥ } :=
âŸ¨0, Î» _ âŸ¨hn, _âŸ©, hnâŸ©

lemma op_norm_nonneg : 0 â‰¤ âˆ¥fâˆ¥ :=
lb_le_Inf _ bounds_nonempty (Î» _ âŸ¨hx, _âŸ©, hx)

/-- The fundamental property of the operator norm: âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥. -/
theorem le_op_norm : âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ :=
classical.by_cases
  (Î» heq : x = 0, by { rw heq, simp })
  (Î» hne, have hlt : 0 < âˆ¥xâˆ¥, from (norm_pos_iff _).2 hne,
    le_mul_of_div_le hlt ((le_Inf _ bounds_nonempty bounds_bdd_below).2
    (Î» c âŸ¨_, hcâŸ©, div_le_of_le_mul hlt (by { rw mul_comm, apply hc }))))

lemma ratio_le_op_norm : âˆ¥f xâˆ¥ / âˆ¥xâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
(or.elim (lt_or_eq_of_le (norm_nonneg _))
  (Î» hlt, div_le_of_le_mul hlt (by { rw mul_comm, apply le_op_norm }))
  (Î» heq, by { rw [â†heq, div_zero], apply op_norm_nonneg }))

/-- The image of the unit ball under a continuous linear map is bounded. -/
lemma unit_le_op_norm : âˆ¥xâˆ¥ â‰¤ 1 â†’ âˆ¥f xâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
Î» hx, begin
  rw [â†(mul_one âˆ¥fâˆ¥)],
  calc _ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ : le_op_norm _ _
  ...    â‰¤ _ : mul_le_mul_of_nonneg_left hx (op_norm_nonneg _)
end

/-- If one controls the norm of every A x, then one controls the norm of A. -/
lemma op_norm_le_bound {M : â„} (hMp: 0 â‰¤ M) (hM : âˆ€ x, âˆ¥f xâˆ¥ â‰¤ M * âˆ¥xâˆ¥) :
  âˆ¥fâˆ¥ â‰¤ M :=
Inf_le _ bounds_bdd_below âŸ¨hMp, hMâŸ©

/-- The operator norm satisfies the triangle inequality. -/
theorem op_norm_triangle : âˆ¥f + gâˆ¥ â‰¤ âˆ¥fâˆ¥ + âˆ¥gâˆ¥ :=
Inf_le _ bounds_bdd_below
  âŸ¨add_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Î» x, by { rw add_mul,
    calc _ â‰¤ âˆ¥f xâˆ¥ + âˆ¥g xâˆ¥ : norm_triangle _ _
    ...    â‰¤ _             : add_le_add (le_op_norm _ _) (le_op_norm _ _) }âŸ©

/-- An operator is zero iff its norm vanishes. -/
theorem op_norm_zero_iff : âˆ¥fâˆ¥ = 0 â†” f = 0 :=
iff.intro
  (Î» hn, continuous_linear_map.ext (Î» x, (norm_le_zero_iff _).1
    (calc _ â‰¤ âˆ¥fâˆ¥ * âˆ¥xâˆ¥ : le_op_norm _ _
     ...     = _ : by rw [hn, zero_mul])))
  (Î» hf, le_antisymm (Inf_le _ bounds_bdd_below
    âŸ¨ge_of_eq rfl, Î» _, le_of_eq (by { rw [zero_mul, hf], exact norm_zero })âŸ©)
    (op_norm_nonneg _))

@[simp] lemma norm_zero : âˆ¥(0 : E â†’L[ğ•‚] F)âˆ¥ = 0 :=
by rw op_norm_zero_iff

/-- The norm of the identity is at most 1. It is in fact 1, except when the space is trivial where
it is 0. It means that one can not do better than an inequality in general. -/
lemma norm_id : âˆ¥(id : E â†’L[ğ•‚] E)âˆ¥ â‰¤ 1 :=
op_norm_le_bound _ zero_le_one (Î»x, by simp)

/-- The operator norm is homogeneous. -/
lemma op_norm_smul : âˆ¥c â€¢ fâˆ¥ = âˆ¥câˆ¥ * âˆ¥fâˆ¥ :=
le_antisymm
  (Inf_le _ bounds_bdd_below
    âŸ¨mul_nonneg (norm_nonneg _) (op_norm_nonneg _), Î» _,
    begin
      erw [norm_smul, mul_assoc],
      exact mul_le_mul_of_nonneg_left (le_op_norm _ _) (norm_nonneg _)
    endâŸ©)
  (lb_le_Inf _ bounds_nonempty (Î» _ âŸ¨hn, hcâŸ©,
    (or.elim (lt_or_eq_of_le (norm_nonneg c))
      (Î» hlt,
        begin
          rw mul_comm,
          exact mul_le_of_le_div hlt (Inf_le _ bounds_bdd_below
          âŸ¨div_nonneg hn hlt, Î» _,
          (by { rw div_mul_eq_mul_div, exact le_div_of_mul_le hlt
          (by { rw [ mul_comm, â†norm_smul ], exact hc _ }) })âŸ©)
        end)
      (Î» heq, by { rw [â†heq, zero_mul], exact hn }))))

lemma op_norm_neg : âˆ¥-fâˆ¥ = âˆ¥fâˆ¥ := calc
  âˆ¥-fâˆ¥ = âˆ¥(-1:ğ•‚) â€¢ fâˆ¥ : by rw neg_one_smul
  ... = âˆ¥(-1:ğ•‚)âˆ¥ * âˆ¥fâˆ¥ : by rw op_norm_smul
  ... = âˆ¥fâˆ¥ : by simp

/-- Continuous linear maps themselves form a normed space with respect to
    the operator norm. -/
instance to_normed_group : normed_group (E â†’L[ğ•‚] F) :=
normed_group.of_core _ âŸ¨op_norm_zero_iff, op_norm_triangle, op_norm_negâŸ©

/- The next instance should be found automatically, but it is not.
TODO: fix me -/
instance to_normed_group_prod : normed_group (E â†’L[ğ•‚] (F Ã— G)) :=
continuous_linear_map.to_normed_group

instance to_normed_space : normed_space ğ•‚ (E â†’L[ğ•‚] F) :=
âŸ¨op_norm_smulâŸ©

/-- The operator norm is submultiplicative. -/
lemma op_norm_comp_le : âˆ¥comp h fâˆ¥ â‰¤ âˆ¥hâˆ¥ * âˆ¥fâˆ¥ :=
(Inf_le _ bounds_bdd_below
  âŸ¨mul_nonneg (op_norm_nonneg _) (op_norm_nonneg _), Î» x,
  begin
    rw mul_assoc,
    calc _ â‰¤ âˆ¥hâˆ¥ * âˆ¥f xâˆ¥: le_op_norm _ _
    ... â‰¤ _ : mul_le_mul_of_nonneg_left
              (le_op_norm _ _) (op_norm_nonneg _)
  endâŸ©)

/-- continuous linear maps are Lipschitz continuous. -/
theorem lipschitz : lipschitz_with âˆ¥fâˆ¥ f :=
âŸ¨op_norm_nonneg _, Î» x y,
  by { rw [dist_eq_norm, dist_eq_norm, â†map_sub], apply le_op_norm }âŸ©

end op_norm

/-- The norm of the tensor product of a scalar linear map and of an element of a normed space
is the product of the norms. -/
@[simp] lemma scalar_prod_space_iso_norm {c : E â†’L[ğ•‚] ğ•‚} {f : F} :
  âˆ¥scalar_prod_space_iso c fâˆ¥ = âˆ¥câˆ¥ * âˆ¥fâˆ¥ :=
begin
  refine le_antisymm _ _,
  { apply op_norm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) (Î»x, _),
    calc
     âˆ¥(c x) â€¢ fâˆ¥ = âˆ¥c xâˆ¥ * âˆ¥fâˆ¥ : norm_smul _ _
     ... â‰¤ (âˆ¥câˆ¥ * âˆ¥xâˆ¥) * âˆ¥fâˆ¥ :
       mul_le_mul_of_nonneg_right (le_op_norm _ _) (norm_nonneg _)
     ... = âˆ¥câˆ¥ * âˆ¥fâˆ¥ * âˆ¥xâˆ¥ : by ring },
  { by_cases h : âˆ¥fâˆ¥ = 0,
    { rw h, simp [norm_nonneg] },
    { have : 0 < âˆ¥fâˆ¥ := lt_of_le_of_ne (norm_nonneg _) (ne.symm h),
      rw â† le_div_iff this,
      apply op_norm_le_bound _ (div_nonneg (norm_nonneg _) this) (Î»x, _),
      rw [div_mul_eq_mul_div, le_div_iff this],
      calc âˆ¥c xâˆ¥ * âˆ¥fâˆ¥ = âˆ¥c x â€¢ fâˆ¥ : (norm_smul _ _).symm
      ... = âˆ¥((scalar_prod_space_iso c f) : E â†’ F) xâˆ¥ : rfl
      ... â‰¤ âˆ¥scalar_prod_space_iso c fâˆ¥ * âˆ¥xâˆ¥ : le_op_norm _ _ } },
end

end continuous_linear_map
