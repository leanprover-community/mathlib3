/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.normed_space.basic
import algebra.ring.basic
import analysis.asymptotics

/-!
# Ordered normed spaces

In this file, we define classes for fields and groups that are both normed and ordered.
These are mostly useful to avoid diamonds during type class inference.
-/

open filter asymptotics set
open_locale topological_space

/-- A `normed_linear_ordered_group` is an additive group that is both a `normed_group` and
    a `linear_ordered_add_comm_group`. This class is necessary to avoid diamonds. -/
class normed_linear_ordered_group (Œ± : Type*)
extends linear_ordered_add_comm_group Œ±, has_norm Œ±, metric_space Œ± :=
(dist_eq : ‚àÄ x y, dist x y = norm (x - y))

@[priority 100] instance normed_linear_ordered_group.to_normed_group (Œ± : Type*)
  [normed_linear_ordered_group Œ±] : normed_group Œ± :=
‚ü®normed_linear_ordered_group.dist_eq‚ü©

/-- A `normed_linear_ordered_field` is a field that is both a `normed_field` and a
    `linear_ordered_field`. This class is necessary to avoid diamonds. -/
class normed_linear_ordered_field (Œ± : Type*)
extends linear_ordered_field Œ±, has_norm Œ±, metric_space Œ± :=
(dist_eq : ‚àÄ x y, dist x y = norm (x - y))
(norm_mul' : ‚àÄ a b, norm (a * b) = norm a * norm b)

@[priority 100] instance normed_linear_ordered_field.to_normed_field (Œ± : Type*)
  [normed_linear_ordered_field Œ±] : normed_field Œ± :=
{ dist_eq := normed_linear_ordered_field.dist_eq,
  norm_mul' := normed_linear_ordered_field.norm_mul' }

@[priority 100] instance normed_linear_ordered_field.to_normed_linear_ordered_group (Œ± : Type*)
[normed_linear_ordered_field Œ±] : normed_linear_ordered_group Œ± :=
‚ü®normed_linear_ordered_field.dist_eq‚ü©

noncomputable
instance : normed_linear_ordered_field ‚Ñö :=
‚ü®dist_eq_norm, normed_field.norm_mul‚ü©

noncomputable
instance : normed_linear_ordered_field ‚Ñù :=
‚ü®dist_eq_norm, normed_field.norm_mul‚ü©

variables {ùïú : Type*} [normed_linear_ordered_field ùïú]

lemma pow_div_pow_eventually_eq_at_top {p q : ‚Ñï} :
  (Œª x : ùïú, x^p / x^q) =·∂†[at_top] (Œª x, x^((p : ‚Ñ§) -q)) :=
begin
  apply ((eventually_gt_at_top (0 : ùïú)).mono (Œª x hx, _)),
  simp [fpow_sub hx.ne'],
end

lemma pow_div_pow_eventually_eq_at_bot {p q : ‚Ñï} :
  (Œª x : ùïú, x^p / x^q) =·∂†[at_bot] (Œª x, x^((p : ‚Ñ§) -q)) :=
begin
  apply ((eventually_lt_at_bot (0 : ùïú)).mono (Œª x hx, _)),
  simp [fpow_sub hx.ne'.symm],
end

lemma tendsto_fpow_at_top_at_top {n : ‚Ñ§} (hn : 0 < n) : tendsto (Œª x : ùïú, x^n) at_top at_top :=
begin
  lift n to ‚Ñï using hn.le,
  exact tendsto_pow_at_top (nat.succ_le_iff.mpr $int.coe_nat_pos.mp hn)
end

lemma tendsto_fpow_at_top_zero [order_topology ùïú] {n : ‚Ñ§} (hn : n < 0) :
  tendsto (Œª x : ùïú, x^n) at_top (ùìù 0) :=
begin
  have : 1 ‚â§ -n, by linarith,
  apply tendsto.congr (show ‚àÄ x : ùïú, x^-(-n) = x^n, by simp),
  lift -n to ‚Ñï using le_of_lt (neg_pos.mpr hn) with N,
  exact tendsto_pow_neg_at_top (by exact_mod_cast this)
end

lemma tendsto_pow_div_pow_at_top_at_top {p q : ‚Ñï} (hpq : q < p) :
  tendsto (Œª x : ùïú, x^p / x^q) at_top at_top :=
begin
  rw tendsto_congr' pow_div_pow_eventually_eq_at_top,
  apply tendsto_fpow_at_top_at_top,
  linarith
end

lemma tendsto_pow_div_pow_at_top_zero
  [order_topology ùïú] {p q : ‚Ñï} (hpq : p < q) :
  tendsto (Œª x : ùïú, x^p / x^q) at_top (ùìù 0) :=
begin
  rw tendsto_congr' pow_div_pow_eventually_eq_at_top,
  apply tendsto_fpow_at_top_zero,
  linarith
end

lemma asymptotics.is_o_pow_pow_at_top_of_lt
  [order_topology ùïú] {p q : ‚Ñï} (hpq : p < q) :
  is_o (Œª x : ùïú, x^p) (Œª x, x^q) at_top :=
begin
  refine (is_o_iff_tendsto' _).mpr (tendsto_pow_div_pow_at_top_zero hpq),
  exact (eventually_gt_at_top 0).mono (Œª x hx hxq, (pow_ne_zero q hx.ne' hxq).elim),
end

lemma asymptotics.is_O.trans_tendsto_norm_at_top {Œ± : Type*} {u v : Œ± ‚Üí ùïú} {l : filter Œ±}
  (huv : is_O u v l) (hu : tendsto (Œª x, ‚à•u x‚à•) l at_top) : tendsto (Œª x, ‚à•v x‚à•) l at_top :=
begin
  rcases huv.exists_pos with ‚ü®c, hc, hcuv‚ü©,
  rw is_O_with at hcuv,
  convert tendsto.at_top_div_const hc (tendsto_at_top_mono' l hcuv hu),
  ext x,
  rw mul_div_cancel_left _ hc.ne.symm,
end
