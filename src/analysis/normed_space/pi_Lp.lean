/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Jireh Loreaux
-/
import analysis.mean_inequalities
import data.fintype.order

/-!
# `L^p` distance on finite products of metric spaces
Given finitely many metric spaces, one can put the max distance on their product, but there is also
a whole family of natural distances, indexed by a parameter `p : ‚Ñù‚â•0‚àû`, that also induce
the product topology. We define them in this file. For `0 < p < ‚àû`, the distance on `Œ† i, Œ± i`
is given by
$$
d(x, y) = \left(\sum d(x_i, y_i)^p\right)^{1/p}.
$$,
whereas for `p = 0` it is the cardinality of the set ${ i | x_i ‚â† y_i}$. For `p = ‚àû` the distance
is the supremum of the distances.

We give instances of this construction for emetric spaces, metric spaces, normed groups and normed
spaces.

To avoid conflicting instances, all these are defined on a copy of the original Œ†-type, named
`pi_Lp p Œ±`. The assumpion `[fact (1 ‚â§ p)]` is required for the metric and normed space instances.

We ensure that the topology, bornology and uniform structure on `pi_Lp p Œ±` are (defeq to) the
product topology, product bornology and product uniformity, to be able to use freely continuity
statements for the coordinate functions, for instance.

## Implementation notes

We only deal with the `L^p` distance on a product of finitely many metric spaces, which may be
distinct. A closely related construction is `lp`, the `L^p` norm on a product of (possibly
infinitely many) normed spaces, where the norm is
$$
\left(\sum ‚Äñf (x)‚Äñ^p \right)^{1/p}.
$$
However, the topology induced by this construction is not the product topology, and some functions
have infinite `L^p` norm. These subtleties are not present in the case of finitely many metric
spaces, hence it is worth devoting a file to this specific case which is particularly well behaved.

Another related construction is `measure_theory.Lp`, the `L^p` norm on the space of functions from
a measure space to a normed space, where the norm is
$$
\left(\int ‚Äñf (x)‚Äñ^p dŒº\right)^{1/p}.
$$
This has all the same subtleties as `lp`, and the further subtlety that this only
defines a seminorm (as almost everywhere zero functions have zero `L^p` norm).
The construction `pi_Lp` corresponds to the special case of `measure_theory.Lp` in which the basis
is a finite space equipped with the counting measure.

To prove that the topology (and the uniform structure) on a finite product with the `L^p` distance
are the same as those coming from the `L^‚àû` distance, we could argue that the `L^p` and `L^‚àû` norms
are equivalent on `‚Ñù^n` for abstract (norm equivalence) reasons. Instead, we give a more explicit
(easy) proof which provides a comparison between these two norms with explicit constants.

We also set up the theory for `pseudo_emetric_space` and `pseudo_metric_space`.
-/

open real set filter is_R_or_C bornology
open_locale big_operators uniformity topology nnreal ennreal

noncomputable theory

/-- A copy of a Pi type, on which we will put the `L^p` distance. Since the Pi type itself is
already endowed with the `L^‚àû` distance, we need the type synonym to avoid confusing typeclass
resolution. Also, we let it depend on `p`, to get a whole family of type on which we can put
different distances. -/
@[nolint unused_arguments]
def pi_Lp (p : ‚Ñù‚â•0‚àû) {Œπ : Type*} (Œ± : Œπ ‚Üí Type*) : Type* := Œ† (i : Œπ), Œ± i

instance (p : ‚Ñù‚â•0‚àû) {Œπ : Type*} (Œ± : Œπ ‚Üí Type*) [Œ† i, inhabited (Œ± i)] : inhabited (pi_Lp p Œ±) :=
‚ü®Œª i, default‚ü©

namespace pi_Lp

variables (p : ‚Ñù‚â•0‚àû) (ùïú : Type*) {Œπ : Type*} (Œ± : Œπ ‚Üí Type*) (Œ≤ : Œπ ‚Üí Type*)

/-- Canonical bijection between `pi_Lp p Œ±` and the original Pi type. We introduce it to be able
to compare the `L^p` and `L^‚àû` distances through it. -/
protected def equiv : pi_Lp p Œ± ‚âÉ Œ† (i : Œπ), Œ± i :=
equiv.refl _

/-! Note that the unapplied versions of these lemmas are deliberately omitted, as they break
the use of the type synonym. -/

@[simp] lemma equiv_apply (x : pi_Lp p Œ±) (i : Œπ) : pi_Lp.equiv p Œ± x i = x i := rfl
@[simp] lemma equiv_symm_apply (x : Œ† i, Œ± i) (i : Œπ) : (pi_Lp.equiv p Œ±).symm x i = x i := rfl

section dist_norm
variables [fintype Œπ]

/-!
### Definition of `edist`, `dist` and `norm` on `pi_Lp`

In this section we define the `edist`, `dist` and `norm` functions on `pi_Lp p Œ±` without assuming
`[fact (1 ‚â§ p)]` or metric properties of the spaces `Œ± i`. This allows us to provide the rewrite
lemmas for each of three cases `p = 0`, `p = ‚àû` and `0 < p.to_real`.
-/

section edist

variables [Œ† i, has_edist (Œ≤ i)]
/-- Endowing the space `pi_Lp p Œ≤` with the `L^p` edistance. We register this instance
separate from `pi_Lp.pseudo_emetric` since the latter requires the type class hypothesis
`[fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future emetric-like structure on `pi_Lp p Œ≤` for `p < 1`
satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance : has_edist (pi_Lp p Œ≤) :=
{ edist := Œª f g, if hp : p = 0 then {i | f i ‚â† g i}.to_finite.to_finset.card
    else (if p = ‚àû then ‚®Ü i, edist (f i) (g i)
    else (‚àë i, (edist (f i) (g i) ^ p.to_real)) ^ (1/p.to_real)) }

variable {Œ≤}
lemma edist_eq_card (f g : pi_Lp 0 Œ≤) : edist f g = {i | f i ‚â† g i}.to_finite.to_finset.card :=
if_pos rfl

lemma edist_eq_sum {p : ‚Ñù‚â•0‚àû} (hp : 0 < p.to_real) (f g : pi_Lp p Œ≤) :
  edist f g = (‚àë i, edist (f i) (g i) ^ p.to_real) ^ (1/p.to_real) :=
let hp' := ennreal.to_real_pos_iff.mp hp in (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

lemma edist_eq_supr (f g : pi_Lp ‚àû Œ≤) : edist f g = ‚®Ü i, edist (f i) (g i) :=
by { dsimp [edist], exact if_neg ennreal.top_ne_zero }

end edist

section edist_prop

variables {Œ≤} [Œ† i, pseudo_emetric_space (Œ≤ i)]

/-- This holds independent of `p` and does not require `[fact (1 ‚â§ p)]`. We keep it separate
from `pi_Lp.pseudo_emetric_space` so it can be used also for `p < 1`. -/
protected lemma edist_self (f : pi_Lp p Œ≤) : edist f f = 0 :=
begin
  rcases p.trichotomy with (rfl | rfl | h),
  { simp [edist_eq_card], },
  { simp [edist_eq_supr], },
  { simp [edist_eq_sum h, ennreal.zero_rpow_of_pos h, ennreal.zero_rpow_of_pos (inv_pos.2 $ h)]}
end

/-- This holds independent of `p` and does not require `[fact (1 ‚â§ p)]`. We keep it separate
from `pi_Lp.pseudo_emetric_space` so it can be used also for `p < 1`. -/
protected lemma edist_comm (f g : pi_Lp p Œ≤) : edist f g = edist g f :=
begin
  rcases p.trichotomy with (rfl | rfl | h),
  { simp only [edist_eq_card, eq_comm, ne.def] },
  { simp only [edist_eq_supr, edist_comm] },
  { simp only [edist_eq_sum h, edist_comm] }
end

end edist_prop

section dist

variables [Œ† i, has_dist (Œ± i)]
/-- Endowing the space `pi_Lp p Œ≤` with the `L^p` distance. We register this instance
separate from `pi_Lp.pseudo_metric` since the latter requires the type class hypothesis
`[fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future metric-like structure on `pi_Lp p Œ≤` for `p < 1`
satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance : has_dist (pi_Lp p Œ±) :=
{ dist := Œª f g, if hp : p = 0 then {i | f i ‚â† g i}.to_finite.to_finset.card
    else (if p = ‚àû then ‚®Ü i, dist (f i) (g i)
    else (‚àë i, (dist (f i) (g i) ^ p.to_real)) ^ (1/p.to_real)) }

variable {Œ±}
lemma dist_eq_card (f g : pi_Lp 0 Œ±) : dist f g = {i | f i ‚â† g i}.to_finite.to_finset.card :=
if_pos rfl

lemma dist_eq_sum {p : ‚Ñù‚â•0‚àû} (hp : 0 < p.to_real) (f g : pi_Lp p Œ±) :
  dist f g = (‚àë i, dist (f i) (g i) ^ p.to_real) ^ (1/p.to_real) :=
let hp' := ennreal.to_real_pos_iff.mp hp in (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

lemma dist_eq_csupr (f g : pi_Lp ‚àû Œ±) : dist f g = ‚®Ü i, dist (f i) (g i) :=
by { dsimp [dist], exact if_neg ennreal.top_ne_zero }

end dist

section norm

variables [Œ† i, has_norm (Œ≤ i)] [Œ† i, has_zero (Œ≤ i)]

/-- Endowing the space `pi_Lp p Œ≤` with the `L^p` norm. We register this instance
separate from `pi_Lp.seminormed_add_comm_group` since the latter requires the type class hypothesis
`[fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future norm-like structure on `pi_Lp p Œ≤` for `p < 1`
satisfying a relaxed triangle inequality. These are called *quasi-norms*. -/
instance has_norm : has_norm (pi_Lp p Œ≤) :=
{ norm := Œª f, if hp : p = 0 then {i | f i ‚â† 0}.to_finite.to_finset.card
   else (if p = ‚àû then ‚®Ü i, ‚Äñf i‚Äñ else (‚àë i, ‚Äñf i‚Äñ ^ p.to_real) ^ (1 / p.to_real)) }

variables {p Œ≤}
lemma norm_eq_card (f : pi_Lp 0 Œ≤) : ‚Äñf‚Äñ = {i | f i ‚â† 0}.to_finite.to_finset.card :=
if_pos rfl

lemma norm_eq_csupr (f : pi_Lp ‚àû Œ≤) : ‚Äñf‚Äñ = ‚®Ü i, ‚Äñf i‚Äñ :=
by { dsimp [norm], exact if_neg ennreal.top_ne_zero }

lemma norm_eq_sum (hp : 0 < p.to_real) (f : pi_Lp p Œ≤) :
  ‚Äñf‚Äñ = (‚àë i, ‚Äñf i‚Äñ ^ p.to_real) ^ (1 / p.to_real) :=
let hp' := ennreal.to_real_pos_iff.mp hp in (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

end norm

end dist_norm

section aux
/-!
### The uniformity on finite `L^p` products is the product uniformity

In this section, we put the `L^p` edistance on `pi_Lp p Œ±`, and we check that the uniformity
coming from this edistance coincides with the product uniformity, by showing that the canonical
map to the Pi type (with the `L^‚àû` distance) is a uniform embedding, as it is both Lipschitz and
antiLipschitz.

We only register this emetric space structure as a temporary instance, as the true instance (to be
registered later) will have as uniformity exactly the product uniformity, instead of the one coming
from the edistance (which is equal to it, but not defeq). See Note [forgetful inheritance]
explaining why having definitionally the right uniformity is often important.
-/

variables [fact (1 ‚â§ p)] [Œ† i, pseudo_metric_space (Œ± i)] [Œ† i, pseudo_emetric_space (Œ≤ i)]
variables [fintype Œπ]

/-- Endowing the space `pi_Lp p Œ≤` with the `L^p` pseudoemetric structure. This definition is not
satisfactory, as it does not register the fact that the topology and the uniform structure coincide
with the product one. Therefore, we do not register it as an instance. Using this as a temporary
pseudoemetric space instance, we will show that the uniform structure is equal (but not defeq) to
the product one, and then register an instance in which we replace the uniform structure by the
product one using this pseudoemetric space and `pseudo_emetric_space.replace_uniformity`. -/
def pseudo_emetric_aux : pseudo_emetric_space (pi_Lp p Œ≤) :=
{ edist_self := pi_Lp.edist_self p,
  edist_comm := pi_Lp.edist_comm p,
  edist_triangle := Œª f g h,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | hp) },
    { simp only [edist_eq_supr],
      casesI is_empty_or_nonempty Œπ,
      { simp only [csupr_of_empty, ennreal.bot_eq_zero, add_zero, nonpos_iff_eq_zero] },
      exact supr_le (Œª i, (edist_triangle _ (g i) _).trans $
        add_le_add (le_supr _ i) (le_supr _ i))},
    { simp only [edist_eq_sum (zero_lt_one.trans_le hp)],
      calc (‚àë i, edist (f i) (h i) ^ p.to_real) ^ (1 / p.to_real) ‚â§
      (‚àë i, (edist (f i) (g i) + edist (g i) (h i)) ^ p.to_real) ^ (1 / p.to_real) :
      begin
        apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ zero_le_one.trans hp),
        refine finset.sum_le_sum (Œª i hi, _),
        exact ennreal.rpow_le_rpow (edist_triangle _ _ _) (zero_le_one.trans hp),
      end
      ... ‚â§ (‚àë i, edist (f i) (g i) ^ p.to_real) ^ (1 / p.to_real)
            + (‚àë i, edist (g i) (h i) ^ p.to_real) ^ (1 / p.to_real) : ennreal.Lp_add_le _ _ _ hp },
  end }

local attribute [instance] pi_Lp.pseudo_emetric_aux

/-- An auxiliary lemma used twice in the proof of `pi_Lp.pseudo_metric_aux` below. Not intended for
use outside this file. -/
lemma supr_edist_ne_top_aux {Œπ : Type*} [finite Œπ] {Œ± : Œπ ‚Üí Type*} [Œ† i, pseudo_metric_space (Œ± i)]
  (f g : pi_Lp ‚àû Œ±) : (‚®Ü i, edist (f i) (g i)) ‚â† ‚ä§ :=
begin
  casesI nonempty_fintype Œπ,
  obtain ‚ü®M, hM‚ü© := fintype.exists_le (Œª i, (‚ü®dist (f i) (g i), dist_nonneg‚ü© : ‚Ñù‚â•0)),
  refine ne_of_lt ((supr_le $ Œª i, _).trans_lt (@ennreal.coe_lt_top M)),
  simp only [edist, pseudo_metric_space.edist_dist, ennreal.of_real_eq_coe_nnreal dist_nonneg],
  exact_mod_cast hM i,
end

/-- Endowing the space `pi_Lp p Œ±` with the `L^p` pseudometric structure. This definition is not
satisfactory, as it does not register the fact that the topology, the uniform structure, and the
bornology coincide with the product ones. Therefore, we do not register it as an instance. Using
this as a temporary pseudoemetric space instance, we will show that the uniform structure is equal
(but not defeq) to the product one, and then register an instance in which we replace the uniform
structure and the bornology by the product ones using this pseudometric space,
`pseudo_metric_space.replace_uniformity`, and `pseudo_metric_space.replace_bornology`.

See note [reducible non-instances] -/
@[reducible] def pseudo_metric_aux : pseudo_metric_space (pi_Lp p Œ±) :=
pseudo_emetric_space.to_pseudo_metric_space_of_dist dist
  (Œª f g,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { exact supr_edist_ne_top_aux f g },
    { rw edist_eq_sum (zero_lt_one.trans_le h),
      exact ennreal.rpow_ne_top_of_nonneg (one_div_nonneg.2 (zero_le_one.trans h)) (ne_of_lt $
        (ennreal.sum_lt_top $ Œª i hi, ennreal.rpow_ne_top_of_nonneg (zero_le_one.trans h)
        (edist_ne_top _ _)))}
  end)
  (Œª f g,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { rw [edist_eq_supr, dist_eq_csupr],
      { casesI is_empty_or_nonempty Œπ,
        { simp only [real.csupr_empty, csupr_of_empty, ennreal.bot_eq_zero, ennreal.zero_to_real] },
        { refine le_antisymm (csupr_le $ Œª i, _) _,
          { rw [‚Üêennreal.of_real_le_iff_le_to_real (supr_edist_ne_top_aux f g),
              ‚Üêpseudo_metric_space.edist_dist],
            exact le_supr _ i, },
          { refine ennreal.to_real_le_of_le_of_real (real.Sup_nonneg _ _) (supr_le $ Œª i, _),
            { rintro - ‚ü®i, rfl‚ü©,
              exact dist_nonneg, },
            { unfold edist, rw pseudo_metric_space.edist_dist,
              exact ennreal.of_real_le_of_real (le_csupr (fintype.bdd_above_range _) i), } } } } },
    { have A : ‚àÄ i, edist (f i) (g i) ^ p.to_real ‚â† ‚ä§,
        from Œª i, ennreal.rpow_ne_top_of_nonneg (zero_le_one.trans h) (edist_ne_top _ _),
      simp only [edist_eq_sum (zero_lt_one.trans_le h), dist_edist, ennreal.to_real_rpow,
        dist_eq_sum (zero_lt_one.trans_le h), ‚Üê ennreal.to_real_sum (Œª i _, A i)] }
  end)

local attribute [instance] pi_Lp.pseudo_metric_aux

lemma lipschitz_with_equiv_aux : lipschitz_with 1 (pi_Lp.equiv p Œ≤) :=
begin
  intros x y,
  unfreezingI { rcases p.dichotomy with (rfl | h) },
  { simpa only [ennreal.coe_one, one_mul, edist_eq_supr, edist, finset.sup_le_iff,
      finset.mem_univ, forall_true_left] using le_supr (Œª i, edist (x i) (y i)), },
  { have cancel : p.to_real * (1/p.to_real) = 1 := mul_div_cancel' 1 (zero_lt_one.trans_le h).ne',
    rw edist_eq_sum (zero_lt_one.trans_le h),
    simp only [edist, forall_prop_of_true, one_mul, finset.mem_univ, finset.sup_le_iff,
      ennreal.coe_one],
    assume i,
    calc
    edist (x i) (y i) = (edist (x i) (y i) ^ p.to_real) ^ (1/p.to_real) :
      by simp [‚Üê ennreal.rpow_mul, cancel, -one_div]
    ... ‚â§ (‚àë i, edist (x i) (y i) ^ p.to_real) ^ (1 / p.to_real) :
    begin
      apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ (zero_le_one.trans h)),
      exact finset.single_le_sum (Œª i hi, (bot_le : (0 : ‚Ñù‚â•0‚àû) ‚â§ _)) (finset.mem_univ i)
    end }
end

lemma antilipschitz_with_equiv_aux :
  antilipschitz_with ((fintype.card Œπ : ‚Ñù‚â•0) ^ (1 / p).to_real) (pi_Lp.equiv p Œ≤) :=
begin
  intros x y,
  unfreezingI { rcases p.dichotomy with (rfl | h) },
  { simp only [edist_eq_supr, ennreal.div_top, ennreal.zero_to_real, nnreal.rpow_zero,
      ennreal.coe_one, one_mul, supr_le_iff],
    exact Œª i, finset.le_sup (finset.mem_univ i), },
  { have pos : 0 < p.to_real := zero_lt_one.trans_le h,
    have nonneg : 0 ‚â§ 1 / p.to_real := one_div_nonneg.2 (le_of_lt pos),
    have cancel : p.to_real * (1/p.to_real) = 1 := mul_div_cancel' 1 (ne_of_gt pos),
    rw [edist_eq_sum pos, ennreal.to_real_div 1 p],
    simp only [edist, ‚Üêone_div, ennreal.one_to_real],
    calc (‚àë i, edist (x i) (y i) ^ p.to_real) ^ (1 / p.to_real) ‚â§
    (‚àë i, edist (pi_Lp.equiv p Œ≤ x) (pi_Lp.equiv p Œ≤ y) ^ p.to_real) ^ (1 / p.to_real) :
    begin
      apply ennreal.rpow_le_rpow _ nonneg,
      apply finset.sum_le_sum (Œª i hi, _),
      apply ennreal.rpow_le_rpow _ (le_of_lt pos),
      exact finset.le_sup (finset.mem_univ i)
    end
    ... = (((fintype.card Œπ : ‚Ñù‚â•0)) ^ (1 / p.to_real) : ‚Ñù‚â•0) *
      edist (pi_Lp.equiv p Œ≤ x) (pi_Lp.equiv p Œ≤ y) :
    begin
      simp only [nsmul_eq_mul, finset.card_univ, ennreal.rpow_one, finset.sum_const,
        ennreal.mul_rpow_of_nonneg _ _ nonneg, ‚Üêennreal.rpow_mul, cancel],
      have : (fintype.card Œπ : ‚Ñù‚â•0‚àû) = (fintype.card Œπ : ‚Ñù‚â•0) :=
        (ennreal.coe_nat (fintype.card Œπ)).symm,
      rw [this, ennreal.coe_rpow_of_nonneg _ nonneg]
    end }
end

lemma aux_uniformity_eq :
  ùì§ (pi_Lp p Œ≤) = ùì§[Pi.uniform_space _] :=
begin
  have A : uniform_inducing (pi_Lp.equiv p Œ≤) :=
    (antilipschitz_with_equiv_aux p Œ≤).uniform_inducing
    (lipschitz_with_equiv_aux p Œ≤).uniform_continuous,
  have : (Œª (x : pi_Lp p Œ≤ √ó pi_Lp p Œ≤),
    ((pi_Lp.equiv p Œ≤) x.fst, (pi_Lp.equiv p Œ≤) x.snd)) = id,
    by ext i; refl,
  rw [‚Üê A.comap_uniformity, this, comap_id]
end

lemma aux_cobounded_eq :
  cobounded (pi_Lp p Œ±) = @cobounded _ pi.bornology :=
calc cobounded (pi_Lp p Œ±) = comap (pi_Lp.equiv p Œ±) (cobounded _) :
  le_antisymm (antilipschitz_with_equiv_aux p Œ±).tendsto_cobounded.le_comap
    (lipschitz_with_equiv_aux p Œ±).comap_cobounded_le
... = _ : comap_id

end aux

/-! ### Instances on finite `L^p` products -/

instance uniform_space [Œ† i, uniform_space (Œ≤ i)] : uniform_space (pi_Lp p Œ≤) :=
Pi.uniform_space _

lemma uniform_continuous_equiv [Œ† i, uniform_space (Œ≤ i)] :
  uniform_continuous (pi_Lp.equiv p Œ≤) :=
uniform_continuous_id

lemma uniform_continuous_equiv_symm [Œ† i, uniform_space (Œ≤ i)] :
  uniform_continuous (pi_Lp.equiv p Œ≤).symm :=
uniform_continuous_id

@[continuity]
lemma continuous_equiv [Œ† i, uniform_space (Œ≤ i)] : continuous (pi_Lp.equiv p Œ≤) :=
continuous_id

@[continuity]
lemma continuous_equiv_symm [Œ† i, uniform_space (Œ≤ i)] : continuous (pi_Lp.equiv p Œ≤).symm :=
continuous_id

variable [fintype Œπ]

instance bornology [Œ† i, bornology (Œ≤ i)] : bornology (pi_Lp p Œ≤) := pi.bornology

-- throughout the rest of the file, we assume `1 ‚â§ p`
variables [fact (1 ‚â§ p)]

/-- pseudoemetric space instance on the product of finitely many pseudoemetric spaces, using the
`L^p` pseudoedistance, and having as uniformity the product uniformity. -/
instance [Œ† i, pseudo_emetric_space (Œ≤ i)] : pseudo_emetric_space (pi_Lp p Œ≤) :=
(pseudo_emetric_aux p Œ≤).replace_uniformity (aux_uniformity_eq p Œ≤).symm

/-- emetric space instance on the product of finitely many emetric spaces, using the `L^p`
edistance, and having as uniformity the product uniformity. -/
instance [Œ† i, emetric_space (Œ± i)] : emetric_space (pi_Lp p Œ±) :=
@emetric_space.of_t0_pseudo_emetric_space (pi_Lp p Œ±) _ pi.t0_space

/-- pseudometric space instance on the product of finitely many psuedometric spaces, using the
`L^p` distance, and having as uniformity the product uniformity. -/
instance [Œ† i, pseudo_metric_space (Œ≤ i)] : pseudo_metric_space (pi_Lp p Œ≤) :=
((pseudo_metric_aux p Œ≤).replace_uniformity (aux_uniformity_eq p Œ≤).symm).replace_bornology $
  Œª s, filter.ext_iff.1 (aux_cobounded_eq p Œ≤).symm s·∂ú

/-- metric space instance on the product of finitely many metric spaces, using the `L^p` distance,
and having as uniformity the product uniformity. -/
instance [Œ† i, metric_space (Œ± i)] : metric_space (pi_Lp p Œ±) :=
metric_space.of_t0_pseudo_metric_space _

lemma nndist_eq_sum {p : ‚Ñù‚â•0‚àû} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [Œ† i, pseudo_metric_space (Œ≤ i)] (hp : p ‚â† ‚àû) (x y : pi_Lp p Œ≤) :
  nndist x y = (‚àë i : Œπ, nndist (x i) (y i) ^ p.to_real) ^ (1 / p.to_real) :=
subtype.ext $ by { push_cast, exact dist_eq_sum (p.to_real_pos_iff_ne_top.mpr hp) _ _ }

lemma nndist_eq_supr {Œ≤ : Œπ ‚Üí Type*} [Œ† i, pseudo_metric_space (Œ≤ i)] (x y : pi_Lp ‚àû Œ≤) :
  nndist x y = ‚®Ü i, nndist (x i) (y i) :=
subtype.ext $ by { push_cast, exact dist_eq_csupr _ _ }

lemma lipschitz_with_equiv [Œ† i, pseudo_emetric_space (Œ≤ i)] :
  lipschitz_with 1 (pi_Lp.equiv p Œ≤) :=
lipschitz_with_equiv_aux p Œ≤

lemma antilipschitz_with_equiv [Œ† i, pseudo_emetric_space (Œ≤ i)] :
  antilipschitz_with ((fintype.card Œπ : ‚Ñù‚â•0) ^ (1 / p).to_real) (pi_Lp.equiv p Œ≤) :=
antilipschitz_with_equiv_aux p Œ≤

lemma infty_equiv_isometry [Œ† i, pseudo_emetric_space (Œ≤ i)] :
  isometry (pi_Lp.equiv ‚àû Œ≤) :=
Œª x y, le_antisymm (by simpa only [ennreal.coe_one, one_mul] using lipschitz_with_equiv ‚àû Œ≤ x y)
  (by simpa only [ennreal.div_top, ennreal.zero_to_real, nnreal.rpow_zero, ennreal.coe_one, one_mul]
    using antilipschitz_with_equiv ‚àû Œ≤ x y)

variables (p Œ≤)
/-- seminormed group instance on the product of finitely many normed groups, using the `L^p`
norm. -/
instance seminormed_add_comm_group [Œ† i, seminormed_add_comm_group (Œ≤ i)] :
  seminormed_add_comm_group (pi_Lp p Œ≤) :=
{ dist_eq := Œª x y,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { simpa only [dist_eq_csupr, norm_eq_csupr, dist_eq_norm] },
    { have : p ‚â† ‚àû, { intros hp, rw [hp, ennreal.top_to_real] at h, linarith,} ,
      simpa only [dist_eq_sum (zero_lt_one.trans_le h), norm_eq_sum (zero_lt_one.trans_le h),
        dist_eq_norm], }
  end,
  .. pi.add_comm_group, }

/-- normed group instance on the product of finitely many normed groups, using the `L^p` norm. -/
instance normed_add_comm_group [Œ† i, normed_add_comm_group (Œ± i)] :
  normed_add_comm_group (pi_Lp p Œ±) :=
{ ..pi_Lp.seminormed_add_comm_group p Œ± }

lemma nnnorm_eq_sum {p : ‚Ñù‚â•0‚àû} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*} (hp : p ‚â† ‚àû)
  [Œ† i, seminormed_add_comm_group (Œ≤ i)] (f : pi_Lp p Œ≤) :
  ‚Äñf‚Äñ‚Çä = (‚àë i, ‚Äñf i‚Äñ‚Çä ^ p.to_real) ^ (1 / p.to_real) :=
by { ext, simp [nnreal.coe_sum, norm_eq_sum (p.to_real_pos_iff_ne_top.mpr hp)] }

lemma nnnorm_eq_csupr {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (f : pi_Lp ‚àû Œ≤) :
  ‚Äñf‚Äñ‚Çä = ‚®Ü i, ‚Äñf i‚Äñ‚Çä :=
by { ext, simp [nnreal.coe_supr, norm_eq_csupr] }

lemma norm_eq_of_nat {p : ‚Ñù‚â•0‚àû} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [Œ† i, seminormed_add_comm_group (Œ≤ i)] (n : ‚Ñï) (h : p = n) (f : pi_Lp p Œ≤) :
  ‚Äñf‚Äñ = (‚àë i, ‚Äñf i‚Äñ ^ n) ^ (1/(n : ‚Ñù)) :=
begin
  have := p.to_real_pos_iff_ne_top.mpr (ne_of_eq_of_ne h $ ennreal.nat_ne_top n),
  simp only [one_div, h, real.rpow_nat_cast, ennreal.to_real_nat, eq_self_iff_true,
    finset.sum_congr, norm_eq_sum this],
end

lemma norm_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x : pi_Lp 2 Œ≤) :
  ‚Äñx‚Äñ = sqrt (‚àë (i : Œπ), ‚Äñx i‚Äñ ^ 2) :=
by { convert norm_eq_of_nat 2 (by norm_cast) _, rw sqrt_eq_rpow, norm_cast }

lemma nnnorm_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x : pi_Lp 2 Œ≤) :
  ‚Äñx‚Äñ‚Çä = nnreal.sqrt (‚àë (i : Œπ), ‚Äñx i‚Äñ‚Çä ^ 2) :=
subtype.ext $ by { push_cast, exact norm_eq_of_L2 x }

lemma norm_sq_eq_of_L2 (Œ≤ : Œπ ‚Üí Type*) [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x : pi_Lp 2 Œ≤) :
  ‚Äñx‚Äñ ^ 2 = ‚àë (i : Œπ), ‚Äñx i‚Äñ ^ 2 :=
begin
  suffices : ‚Äñx‚Äñ‚Çä ^ 2 = ‚àë (i : Œπ), ‚Äñx i‚Äñ‚Çä ^ 2,
  { simpa only [nnreal.coe_sum] using congr_arg (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) this },
  rw [nnnorm_eq_of_L2, nnreal.sq_sqrt],
end

lemma dist_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x y : pi_Lp 2 Œ≤) :
  dist x y = (‚àë i, dist (x i) (y i) ^ 2).sqrt :=
by simp_rw [dist_eq_norm, norm_eq_of_L2, pi.sub_apply]

lemma nndist_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x y : pi_Lp 2 Œ≤) :
  nndist x y = (‚àë i, nndist (x i) (y i) ^ 2).sqrt :=
subtype.ext $ by { push_cast, exact dist_eq_of_L2 _ _ }

lemma edist_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x y : pi_Lp 2 Œ≤) :
  edist x y = (‚àë i, edist (x i) (y i) ^ 2) ^ (1 / 2 : ‚Ñù) :=
by simp [pi_Lp.edist_eq_sum]

variables [normed_field ùïú]

/-- The product of finitely many normed spaces is a normed space, with the `L^p` norm. -/
instance normed_space [Œ† i, seminormed_add_comm_group (Œ≤ i)]
  [Œ† i, normed_space ùïú (Œ≤ i)] : normed_space ùïú (pi_Lp p Œ≤) :=
{ norm_smul_le := Œª c f,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | hp) },
    { letI : module ùïú (pi_Lp ‚àû Œ≤) := pi.module Œπ Œ≤ ùïú,
      suffices : ‚Äñc ‚Ä¢ f‚Äñ‚Çä = ‚Äñc‚Äñ‚Çä * ‚Äñf‚Äñ‚Çä, { exact_mod_cast nnreal.coe_mono this.le },
      simpa only [nnnorm_eq_csupr, nnreal.mul_supr, ‚Üênnnorm_smul] },
    { have : p.to_real * (1 / p.to_real) = 1 := mul_div_cancel' 1 (zero_lt_one.trans_le hp).ne',
      simp only [norm_eq_sum (zero_lt_one.trans_le hp), norm_smul, mul_rpow, norm_nonneg,
        ‚Üêfinset.mul_sum, pi.smul_apply],
      rw [mul_rpow (rpow_nonneg_of_nonneg (norm_nonneg _) _), ‚Üê rpow_mul (norm_nonneg _),
        this, rpow_one],
      exact finset.sum_nonneg (Œª i hi, rpow_nonneg_of_nonneg (norm_nonneg _) _) },
  end,
  .. (pi.module Œπ Œ≤ ùïú) }

instance finite_dimensional [Œ† i, seminormed_add_comm_group (Œ≤ i)]
  [Œ† i, normed_space ùïú (Œ≤ i)] [I : ‚àÄ i, finite_dimensional ùïú (Œ≤ i)] :
  finite_dimensional ùïú (pi_Lp p Œ≤) :=
finite_dimensional.finite_dimensional_pi' _ _

/- Register simplification lemmas for the applications of `pi_Lp` elements, as the usual lemmas
for Pi types will not trigger. -/
variables {ùïú p Œ±} [Œ† i, seminormed_add_comm_group (Œ≤ i)] [Œ† i, normed_space ùïú (Œ≤ i)] (c : ùïú)
variables (x y : pi_Lp p Œ≤) (x' y' : Œ† i, Œ≤ i) (i : Œπ)

@[simp] lemma zero_apply : (0 : pi_Lp p Œ≤) i = 0 := rfl
@[simp] lemma add_apply : (x + y) i = x i + y i := rfl
@[simp] lemma sub_apply : (x - y) i = x i - y i := rfl
@[simp] lemma smul_apply : (c ‚Ä¢ x) i = c ‚Ä¢ x i := rfl
@[simp] lemma neg_apply : (-x) i = - (x i) := rfl

/-- The canonical map `pi_Lp.equiv` between `pi_Lp ‚àû Œ≤` and `Œ† i, Œ≤ i` as a linear isometric
equivalence. -/
def equiv‚Çó·µ¢ : pi_Lp ‚àû Œ≤ ‚âÉ‚Çó·µ¢[ùïú] Œ† i, Œ≤ i :=
{ map_add' := Œª f g, rfl,
  map_smul' := Œª c f, rfl,
  norm_map' := Œª f,
  begin
    suffices : finset.univ.sup (Œª i, ‚Äñf i‚Äñ‚Çä) = ‚®Ü i, ‚Äñf i‚Äñ‚Çä,
    { simpa only [nnreal.coe_supr] using congr_arg (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) this },
    refine antisymm (finset.sup_le (Œª i _, le_csupr (fintype.bdd_above_range (Œª i, ‚Äñf i‚Äñ‚Çä)) _)) _,
    casesI is_empty_or_nonempty Œπ,
    { simp only [csupr_of_empty, finset.univ_eq_empty, finset.sup_empty], },
    { exact csupr_le (Œª i, finset.le_sup (finset.mem_univ i)) },
  end,
  .. pi_Lp.equiv ‚àû Œ≤ }

variables {Œπ' : Type*}
variables [fintype Œπ']

variables (p ùïú) (E : Type*) [normed_add_comm_group E] [normed_space ùïú E]

/-- An equivalence of finite domains induces a linearly isometric equivalence of finitely supported
functions-/
def _root_.linear_isometry_equiv.pi_Lp_congr_left (e : Œπ ‚âÉ Œπ') :
  pi_Lp p (Œª i : Œπ, E) ‚âÉ‚Çó·µ¢[ùïú] pi_Lp p (Œª i : Œπ', E) :=
{ to_linear_equiv := linear_equiv.Pi_congr_left' ùïú (Œª i : Œπ, E) e,
  norm_map' := Œª x,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { simp_rw [norm_eq_csupr, linear_equiv.Pi_congr_left'_apply ùïú (Œª i : Œπ, E) e x _],
      exact e.symm.supr_congr (Œª i, rfl) },
    { simp only [norm_eq_sum (zero_lt_one.trans_le h)],
      simp_rw linear_equiv.Pi_congr_left'_apply ùïú (Œª i : Œπ, E) e x _,
      congr,
      exact (fintype.sum_equiv (e.symm) _ _ (Œª i, rfl)) }
  end, }

variables {p ùïú E}

@[simp] lemma _root_.linear_isometry_equiv.pi_Lp_congr_left_apply
  (e : Œπ ‚âÉ Œπ') (v : pi_Lp p (Œª i : Œπ, E)) :
  linear_isometry_equiv.pi_Lp_congr_left p ùïú E e v = equiv.Pi_congr_left' (Œª i : Œπ, E) e v :=
rfl

@[simp] lemma _root_.linear_isometry_equiv.pi_Lp_congr_left_symm (e : Œπ ‚âÉ Œπ') :
  (linear_isometry_equiv.pi_Lp_congr_left p ùïú E e).symm
    = (linear_isometry_equiv.pi_Lp_congr_left p ùïú E e.symm) :=
linear_isometry_equiv.ext $ Œª x, rfl

@[simp] lemma _root_.linear_isometry_equiv.pi_Lp_congr_left_single
  [decidable_eq Œπ] [decidable_eq Œπ'] (e : Œπ ‚âÉ Œπ') (i : Œπ) (v : E) :
  linear_isometry_equiv.pi_Lp_congr_left p ùïú E e (pi.single i v) = pi.single (e i) v :=
begin
  funext x,
  simp [linear_isometry_equiv.pi_Lp_congr_left, linear_equiv.Pi_congr_left', equiv.Pi_congr_left',
    pi.single, function.update, equiv.symm_apply_eq],
end

@[simp] lemma equiv_zero : pi_Lp.equiv p Œ≤ 0 = 0 := rfl
@[simp] lemma equiv_symm_zero : (pi_Lp.equiv p Œ≤).symm 0 = 0 := rfl

@[simp] lemma equiv_add :
  pi_Lp.equiv p Œ≤ (x + y) = pi_Lp.equiv p Œ≤ x + pi_Lp.equiv p Œ≤ y := rfl
@[simp] lemma equiv_symm_add :
  (pi_Lp.equiv p Œ≤).symm (x' + y') = (pi_Lp.equiv p Œ≤).symm x' + (pi_Lp.equiv p Œ≤).symm y' := rfl

@[simp] lemma equiv_sub : pi_Lp.equiv p Œ≤ (x - y) = pi_Lp.equiv p Œ≤ x - pi_Lp.equiv p Œ≤ y := rfl
@[simp] lemma equiv_symm_sub :
  (pi_Lp.equiv p Œ≤).symm (x' - y') = (pi_Lp.equiv p Œ≤).symm x' - (pi_Lp.equiv p Œ≤).symm y' := rfl

@[simp] lemma equiv_neg : pi_Lp.equiv p Œ≤ (-x) = -pi_Lp.equiv p Œ≤ x := rfl
@[simp] lemma equiv_symm_neg : (pi_Lp.equiv p Œ≤).symm (-x') = -(pi_Lp.equiv p Œ≤).symm x' := rfl

@[simp] lemma equiv_smul : pi_Lp.equiv p Œ≤ (c ‚Ä¢ x) = c ‚Ä¢ pi_Lp.equiv p Œ≤ x := rfl
@[simp] lemma equiv_symm_smul :
  (pi_Lp.equiv p Œ≤).symm (c ‚Ä¢ x') = c ‚Ä¢ (pi_Lp.equiv p Œ≤).symm x' := rfl

/-- When `p = ‚àû`, this lemma does not hold without the additional assumption `nonempty Œπ` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `‚Äñb‚Äñ‚Çä`. See
`pi_Lp.nnnorm_equiv_symm_const'` for a version which exchanges the hypothesis `p ‚â† ‚àû` for
`nonempty Œπ`. -/
lemma nnnorm_equiv_symm_const {Œ≤} [seminormed_add_comm_group Œ≤] (hp : p ‚â† ‚àû) (b : Œ≤) :
  ‚Äñ(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm (function.const _ b)‚Äñ‚Çä=
  fintype.card Œπ ^ (1 / p).to_real * ‚Äñb‚Äñ‚Çä :=
begin
  rcases p.dichotomy with (h | h),
  { exact false.elim (hp h) },
  { have ne_zero : p.to_real ‚â† 0 := (zero_lt_one.trans_le h).ne',
    simp_rw [nnnorm_eq_sum hp, equiv_symm_apply, function.const_apply, finset.sum_const,
      finset.card_univ, nsmul_eq_mul, nnreal.mul_rpow, ‚Üênnreal.rpow_mul, mul_one_div_cancel ne_zero,
      nnreal.rpow_one, ennreal.to_real_div, ennreal.one_to_real], },
end

/-- When `is_empty Œπ`, this lemma does not hold without the additional assumption `p ‚â† ‚àû` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `‚Äñb‚Äñ‚Çä`. See
`pi_Lp.nnnorm_equiv_symm_const` for a version which exchanges the hypothesis `nonempty Œπ`.
for `p ‚â† ‚àû`. -/
lemma nnnorm_equiv_symm_const' {Œ≤} [seminormed_add_comm_group Œ≤] [nonempty Œπ] (b : Œ≤) :
  ‚Äñ(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm (function.const _ b)‚Äñ‚Çä=
  fintype.card Œπ ^ (1 / p).to_real * ‚Äñb‚Äñ‚Çä :=
begin
  unfreezingI { rcases (em $ p = ‚àû) with (rfl | hp) },
  { simp only [equiv_symm_apply, ennreal.div_top, ennreal.zero_to_real, nnreal.rpow_zero, one_mul,
      nnnorm_eq_csupr, function.const_apply, csupr_const], },
  { exact nnnorm_equiv_symm_const hp b, },
end

/-- When `p = ‚àû`, this lemma does not hold without the additional assumption `nonempty Œπ` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `‚Äñb‚Äñ‚Çä`. See
`pi_Lp.norm_equiv_symm_const'` for a version which exchanges the hypothesis `p ‚â† ‚àû` for
`nonempty Œπ`. -/
lemma norm_equiv_symm_const {Œ≤} [seminormed_add_comm_group Œ≤] (hp : p ‚â† ‚àû) (b : Œ≤) :
  ‚Äñ(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm (function.const _ b)‚Äñ =
  fintype.card Œπ ^ (1 / p).to_real * ‚Äñb‚Äñ :=
(congr_arg coe $ nnnorm_equiv_symm_const hp b).trans $ by simp

/-- When `is_empty Œπ`, this lemma does not hold without the additional assumption `p ‚â† ‚àû` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `‚Äñb‚Äñ‚Çä`. See
`pi_Lp.norm_equiv_symm_const` for a version which exchanges the hypothesis `nonempty Œπ`.
for `p ‚â† ‚àû`. -/
lemma norm_equiv_symm_const' {Œ≤} [seminormed_add_comm_group Œ≤] [nonempty Œπ] (b : Œ≤) :
  ‚Äñ(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm (function.const _ b)‚Äñ =
  fintype.card Œπ ^ (1 / p).to_real * ‚Äñb‚Äñ :=
(congr_arg coe $ nnnorm_equiv_symm_const' b).trans $ by simp

lemma nnnorm_equiv_symm_one {Œ≤} [seminormed_add_comm_group Œ≤] (hp : p ‚â† ‚àû) [has_one Œ≤] :
  ‚Äñ(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm 1‚Äñ‚Çä = fintype.card Œπ ^ (1 / p).to_real * ‚Äñ(1 : Œ≤)‚Äñ‚Çä :=
(nnnorm_equiv_symm_const hp (1 : Œ≤)).trans rfl

lemma norm_equiv_symm_one {Œ≤} [seminormed_add_comm_group Œ≤] (hp : p ‚â† ‚àû) [has_one Œ≤] :
  ‚Äñ(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm 1‚Äñ = fintype.card Œπ ^ (1 / p).to_real * ‚Äñ(1 : Œ≤)‚Äñ :=
(norm_equiv_symm_const hp (1 : Œ≤)).trans rfl

variables (ùïú p)

/-- `pi_Lp.equiv` as a linear map. -/
@[simps {fully_applied := ff}]
protected def linear_equiv : pi_Lp p Œ≤ ‚âÉ‚Çó[ùïú] Œ† i, Œ≤ i :=
{ to_fun := pi_Lp.equiv _ _,
  inv_fun := (pi_Lp.equiv _ _).symm,
  ..linear_equiv.refl _ _}

section basis

variables (Œπ)

/-- A version of `pi.basis_fun` for `pi_Lp`. -/
def basis_fun : basis Œπ ùïú (pi_Lp p (Œª _, ùïú)) :=
basis.of_equiv_fun (pi_Lp.linear_equiv p ùïú (Œª _ : Œπ, ùïú))

@[simp] lemma basis_fun_apply [decidable_eq Œπ] (i) :
  basis_fun p ùïú Œπ i = (pi_Lp.equiv p _).symm (pi.single i 1) :=
by simp_rw [basis_fun, basis.coe_of_equiv_fun, pi_Lp.linear_equiv_symm_apply, pi.single]

@[simp] lemma basis_fun_repr (x : pi_Lp p (Œª i : Œπ, ùïú)) (i : Œπ) :
  (basis_fun p ùïú Œπ).repr x i = x i :=
rfl

lemma basis_fun_eq_pi_basis_fun :
  basis_fun p ùïú Œπ = (pi.basis_fun ùïú Œπ).map (pi_Lp.linear_equiv p ùïú (Œª _ : Œπ, ùïú)).symm :=
rfl

@[simp] lemma basis_fun_map :
  (basis_fun p ùïú Œπ).map (pi_Lp.linear_equiv p ùïú (Œª _ : Œπ, ùïú)) = pi.basis_fun ùïú Œπ :=
rfl

open_locale matrix

lemma basis_to_matrix_basis_fun_mul (b : basis Œπ ùïú (pi_Lp p (Œª i : Œπ, ùïú))) (A : matrix Œπ Œπ ùïú) :
  b.to_matrix (pi_Lp.basis_fun _ _ _) ‚¨ù A =
    matrix.of (Œª i j, b.repr ((pi_Lp.equiv _ _).symm (A·µÄ j)) i) :=
begin
  have := basis_to_matrix_basis_fun_mul (b.map (pi_Lp.linear_equiv _ ùïú _)) A,
  simp_rw [‚Üêpi_Lp.basis_fun_map p, basis.map_repr, linear_equiv.trans_apply,
    pi_Lp.linear_equiv_symm_apply, basis.to_matrix_map, function.comp, basis.map_apply,
    linear_equiv.symm_apply_apply] at this,
  exact this,
end

end basis

end pi_Lp
