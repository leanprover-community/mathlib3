/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import analysis.mean_inequalities
import analysis.normed_space.inner_product

/-!
# `L^p` distance on finite products of metric spaces
Given finitely many metric spaces, one can put the max distance on their product, but there is also
a whole family of natural distances, indexed by a real parameter `p ‚àà [1, ‚àû)`, that also induce
the product topology. We define them in this file. The distance on `Œ† i, Œ± i` is given by
$$
d(x, y) = \left(\sum d(x_i, y_i)^p\right)^{1/p}.
$$

We give instances of this construction for emetric spaces, metric spaces, normed groups and normed
spaces.

To avoid conflicting instances, all these are defined on a copy of the original Pi type, named
`pi_Lp p hp Œ±`, where `hp : 1 ‚â§ p`. This assumption is included in the definition of the type
to make sure that it is always available to typeclass inference to construct the instances.

We ensure that the topology and uniform structure on `pi_Lp p hp Œ±` are (defeq to) the product
topology and product uniformity, to be able to use freely continuity statements for the coordinate
functions, for instance.

In the specific case of the `L^2`-norm, we show that we get an inner product space.

We define `euclidean_space ùïú n` to be `pi_Lp 2 _ (n ‚Üí ùïú)` for any `fintype n`, i.e., the space
from functions to `n` to `ùïú` with the `L^2` norm, and register several instances on it
(notably that it is a finite-dimensional inner product space).


## Implementation notes

We only deal with the `L^p` distance on a product of finitely many metric spaces, which may be
distinct. A closely related construction is the `L^p` norm on the space of
functions from a measure space to a normed space, where the norm is
$$
\left(\int ‚à•f (x)‚à•^p dŒº\right)^{1/p}.
$$
However, the topology induced by this construction is not the product topology, this only
defines a seminorm (as almost everywhere zero functions have zero `L^p` norm), and some functions
have infinite `L^p` norm. All these subtleties are not present in the case of finitely many
metric spaces (which corresponds to the basis which is a finite space with the counting measure),
hence it is worth devoting a file to this specific case which is particularly well behaved.
The general case is not yet formalized in mathlib.

To prove that the topology (and the uniform structure) on a finite product with the `L^p` distance
are the same as those coming from the `L^‚àû` distance, we could argue that the `L^p` and `L^‚àû` norms
are equivalent on `‚Ñù^n` for abstract (norm equivalence) reasons. Instead, we give a more explicit
(easy) proof which provides a comparison between these two norms with explicit constants.

We also set up the theory for `pseudo_emetric_space` and `pseudo_metric_space`.
-/

open real set filter is_R_or_C
open_locale big_operators uniformity topological_space nnreal ennreal

noncomputable theory

variables {Œπ : Type*}

/-- A copy of a Pi type, on which we will put the `L^p` distance. Since the Pi type itself is
already endowed with the `L^‚àû` distance, we need the type synonym to avoid confusing typeclass
resolution. Also, we let it depend on `p`, to get a whole family of type on which we can put
different distances, and we provide the assumption `hp` in the definition, to make it available
to typeclass resolution when it looks for a distance on `pi_Lp p hp Œ±`. -/
@[nolint unused_arguments]
def pi_Lp {Œπ : Type*} (p : ‚Ñù) (hp : 1 ‚â§ p) (Œ± : Œπ ‚Üí Type*) : Type* := Œ† (i : Œπ), Œ± i

instance {Œπ : Type*} (p : ‚Ñù) (hp : 1 ‚â§ p) (Œ± : Œπ ‚Üí Type*) [‚àÄ i, inhabited (Œ± i)] :
  inhabited (pi_Lp p hp Œ±) :=
‚ü®Œª i, default (Œ± i)‚ü©

namespace pi_Lp

variables (p : ‚Ñù) (hp : 1 ‚â§ p) (Œ± : Œπ ‚Üí Type*) (Œ≤ : Œπ ‚Üí Type*)

/-- Canonical bijection between `pi_Lp p hp Œ±` and the original Pi type. We introduce it to be able
to compare the `L^p` and `L^‚àû` distances through it. -/
protected def equiv : pi_Lp p hp Œ± ‚âÉ Œ† (i : Œπ), Œ± i :=
equiv.refl _

section
/-!
### The uniformity on finite `L^p` products is the product uniformity

In this section, we put the `L^p` edistance on `pi_Lp p hp Œ±`, and we check that the uniformity
coming from this edistance coincides with the product uniformity, by showing that the canonical
map to the Pi type (with the `L^‚àû` distance) is a uniform embedding, as it is both Lipschitz and
antiLipschitz.

We only register this emetric space structure as a temporary instance, as the true instance (to be
registered later) will have as uniformity exactly the product uniformity, instead of the one coming
from the edistance (which is equal to it, but not defeq). See Note [forgetful inheritance]
explaining why having definitionally the right uniformity is often important.
-/

variables [‚àÄ i, emetric_space (Œ± i)] [‚àÄ i, pseudo_emetric_space (Œ≤ i)] [fintype Œπ]

/-- Endowing the space `pi_Lp p hp Œ≤` with the `L^p` pseudoedistance. This definition is not
satisfactory, as it does not register the fact that the topology and the uniform structure coincide
with the product one. Therefore, we do not register it as an instance. Using this as a temporary
pseudoemetric space instance, we will show that the uniform structure is equal (but not defeq) to
the product one, and then register an instance in which we replace the uniform structure by the
product one using this pseudoemetric space and `pseudo_emetric_space.replace_uniformity`. -/
def pseudo_emetric_aux : pseudo_emetric_space (pi_Lp p hp Œ≤) :=
have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp,
{ edist          := Œª f g, (‚àë (i : Œπ), (edist (f i) (g i)) ^ p) ^ (1/p),
  edist_self     := Œª f, by simp [edist, ennreal.zero_rpow_of_pos pos,
                                  ennreal.zero_rpow_of_pos (inv_pos.2 pos)],
  edist_comm     := Œª f g, by simp [edist, edist_comm],
  edist_triangle := Œª f g h, calc
    (‚àë (i : Œπ), edist (f i) (h i) ^ p) ^ (1 / p) ‚â§
    (‚àë (i : Œπ), (edist (f i) (g i) + edist (g i) (h i)) ^ p) ^ (1 / p) :
    begin
      apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ le_of_lt pos),
      refine finset.sum_le_sum (Œª i hi, _),
      exact ennreal.rpow_le_rpow (edist_triangle _ _ _) (le_trans zero_le_one hp)
    end
    ... ‚â§
    (‚àë (i : Œπ), edist (f i) (g i) ^ p) ^ (1 / p) + (‚àë (i : Œπ), edist (g i) (h i) ^ p) ^ (1 / p) :
      ennreal.Lp_add_le _ _ _ hp }

/-- Endowing the space `pi_Lp p hp Œ±` with the `L^p` edistance. This definition is not satisfactory,
as it does not register the fact that the topology and the uniform structure coincide with the
product one. Therefore, we do not register it as an instance. Using this as a temporary emetric
space instance, we will show that the uniform structure is equal (but not defeq) to the product one,
and then register an instance in which we replace the uniform structure by the product one using
this emetric space and `emetric_space.replace_uniformity`. -/
def emetric_aux : emetric_space (pi_Lp p hp Œ±) :=
{ eq_of_edist_eq_zero := Œª f g hfg,
  begin
    have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp,
    letI h := pseudo_emetric_aux p hp Œ±,
    have h : edist f g = (‚àë (i : Œπ), (edist (f i) (g i)) ^ p) ^ (1/p) := rfl,
    simp [h, ennreal.rpow_eq_zero_iff, pos, asymm pos, finset.sum_eq_zero_iff_of_nonneg] at hfg,
    exact funext hfg
  end,
  ..pseudo_emetric_aux p hp Œ± }

local attribute [instance] pi_Lp.emetric_aux pi_Lp.pseudo_emetric_aux

lemma lipschitz_with_equiv : lipschitz_with 1 (pi_Lp.equiv p hp Œ≤) :=
begin
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp,
  have cancel : p * (1/p) = 1 := mul_div_cancel' 1 (ne_of_gt pos),
  assume x y,
  simp only [edist, forall_prop_of_true, one_mul, finset.mem_univ, finset.sup_le_iff,
             ennreal.coe_one],
  assume i,
  calc
  edist (x i) (y i) = (edist (x i) (y i) ^ p) ^ (1/p) :
    by simp [‚Üê ennreal.rpow_mul, cancel, -one_div]
  ... ‚â§ (‚àë (i : Œπ), edist (x i) (y i) ^ p) ^ (1 / p) :
  begin
    apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ le_of_lt pos),
    exact finset.single_le_sum (Œª i hi, (bot_le : (0 : ‚Ñù‚â•0‚àû) ‚â§ _)) (finset.mem_univ i)
  end
end

lemma antilipschitz_with_equiv :
  antilipschitz_with ((fintype.card Œπ : ‚Ñù‚â•0) ^ (1/p)) (pi_Lp.equiv p hp Œ≤) :=
begin
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp,
  have nonneg : 0 ‚â§ 1 / p := one_div_nonneg.2 (le_of_lt pos),
  have cancel : p * (1/p) = 1 := mul_div_cancel' 1 (ne_of_gt pos),
  assume x y,
  simp [edist, -one_div],
  calc (‚àë (i : Œπ), edist (x i) (y i) ^ p) ^ (1 / p) ‚â§
  (‚àë (i : Œπ), edist (pi_Lp.equiv p hp Œ≤ x) (pi_Lp.equiv p hp Œ≤ y) ^ p) ^ (1 / p) :
  begin
    apply ennreal.rpow_le_rpow _ nonneg,
    apply finset.sum_le_sum (Œª i hi, _),
    apply ennreal.rpow_le_rpow _ (le_of_lt pos),
    exact finset.le_sup (finset.mem_univ i)
  end
  ... = (((fintype.card Œπ : ‚Ñù‚â•0)) ^ (1/p) : ‚Ñù‚â•0) *
    edist (pi_Lp.equiv p hp Œ≤ x) (pi_Lp.equiv p hp Œ≤ y) :
  begin
    simp only [nsmul_eq_mul, finset.card_univ, ennreal.rpow_one, finset.sum_const,
      ennreal.mul_rpow_of_nonneg _ _ nonneg, ‚Üêennreal.rpow_mul, cancel],
    have : (fintype.card Œπ : ‚Ñù‚â•0‚àû) = (fintype.card Œπ : ‚Ñù‚â•0) :=
      (ennreal.coe_nat (fintype.card Œπ)).symm,
    rw [this, ennreal.coe_rpow_of_nonneg _ nonneg]
  end
end

lemma aux_uniformity_eq :
  ùì§ (pi_Lp p hp Œ≤) = @uniformity _ (Pi.uniform_space _) :=
begin
  have A : uniform_inducing (pi_Lp.equiv p hp Œ≤) :=
    (antilipschitz_with_equiv p hp Œ≤).uniform_inducing
    (lipschitz_with_equiv p hp Œ≤).uniform_continuous,
  have : (Œª (x : pi_Lp p hp Œ≤ √ó pi_Lp p hp Œ≤),
    ((pi_Lp.equiv p hp Œ≤) x.fst, (pi_Lp.equiv p hp Œ≤) x.snd)) = id,
    by ext i; refl,
  rw [‚Üê A.comap_uniformity, this, comap_id]
end

end

/-! ### Instances on finite `L^p` products -/

instance uniform_space [‚àÄ i, uniform_space (Œ≤ i)] : uniform_space (pi_Lp p hp Œ≤) :=
Pi.uniform_space _

variable [fintype Œπ]

/-- pseudoemetric space instance on the product of finitely many pseudoemetric spaces, using the
`L^p` pseudoedistance, and having as uniformity the product uniformity. -/
instance [‚àÄ i, pseudo_emetric_space (Œ≤ i)] : pseudo_emetric_space (pi_Lp p hp Œ≤) :=
(pseudo_emetric_aux p hp Œ≤).replace_uniformity (aux_uniformity_eq p hp Œ≤).symm

/-- emetric space instance on the product of finitely many emetric spaces, using the `L^p`
edistance, and having as uniformity the product uniformity. -/
instance [‚àÄ i, emetric_space (Œ± i)] : emetric_space (pi_Lp p hp Œ±) :=
(emetric_aux p hp Œ±).replace_uniformity (aux_uniformity_eq p hp Œ±).symm

protected lemma edist {p : ‚Ñù} {hp : 1 ‚â§ p} {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄ i, pseudo_emetric_space (Œ≤ i)] (x y : pi_Lp p hp Œ≤) :
  edist x y = (‚àë (i : Œπ), (edist (x i) (y i)) ^ p) ^ (1/p) := rfl

/-- pseudometric space instance on the product of finitely many psuedometric spaces, using the
`L^p` distance, and having as uniformity the product uniformity. -/
instance [‚àÄ i, pseudo_metric_space (Œ≤ i)] : pseudo_metric_space (pi_Lp p hp Œ≤) :=
begin
  /- we construct the instance from the pseudo emetric space instance to avoid checking again that
  the uniformity is the same as the product uniformity, but we register nevertheless a nice formula
  for the distance -/
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp,
  refine pseudo_emetric_space.to_pseudo_metric_space_of_dist
    (Œªf g, (‚àë (i : Œπ), (dist (f i) (g i)) ^ p) ^ (1/p)) (Œª f g, _) (Œª f g, _),
  { simp [pi_Lp.edist, ennreal.rpow_eq_top_iff, asymm pos, pos,
          ennreal.sum_eq_top_iff, edist_ne_top] },
  { have A : ‚àÄ (i : Œπ), i ‚àà (finset.univ : finset Œπ) ‚Üí edist (f i) (g i) ^ p < ‚ä§ :=
      Œª i hi, by simp [lt_top_iff_ne_top, edist_ne_top, le_of_lt pos],
    simp [dist, -one_div, pi_Lp.edist, ‚Üê ennreal.to_real_rpow,
          ennreal.to_real_sum A, dist_edist] }
end

/-- metric space instance on the product of finitely many metric spaces, using the `L^p` distance,
and having as uniformity the product uniformity. -/
instance [‚àÄ i, metric_space (Œ± i)] : metric_space (pi_Lp p hp Œ±) :=
begin
  /- we construct the instance from the emetric space instance to avoid checking again that the
  uniformity is the same as the product uniformity, but we register nevertheless a nice formula
  for the distance -/
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp,
  refine emetric_space.to_metric_space_of_dist
    (Œªf g, (‚àë (i : Œπ), (dist (f i) (g i)) ^ p) ^ (1/p)) (Œª f g, _) (Œª f g, _),
  { simp [pi_Lp.edist, ennreal.rpow_eq_top_iff, asymm pos, pos,
          ennreal.sum_eq_top_iff, edist_ne_top] },
  { have A : ‚àÄ (i : Œπ), i ‚àà (finset.univ : finset Œπ) ‚Üí edist (f i) (g i) ^ p < ‚ä§ :=
      Œª i hi, by simp [lt_top_iff_ne_top, edist_ne_top, le_of_lt pos],
    simp [dist, -one_div, pi_Lp.edist, ‚Üê ennreal.to_real_rpow,
          ennreal.to_real_sum A, dist_edist] }
end

protected lemma dist {p : ‚Ñù} {hp : 1 ‚â§ p} {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄ i, pseudo_metric_space (Œ≤ i)] (x y : pi_Lp p hp Œ≤) :
  dist x y = (‚àë (i : Œπ), (dist (x i) (y i)) ^ p) ^ (1/p) := rfl

/-- seminormed group instance on the product of finitely many normed groups, using the `L^p`
norm. -/
instance semi_normed_group [‚àÄi, semi_normed_group (Œ≤ i)] : semi_normed_group (pi_Lp p hp Œ≤) :=
{ norm := Œªf, (‚àë (i : Œπ), norm (f i) ^ p) ^ (1/p),
  dist_eq := Œª x y, by { simp [pi_Lp.dist, dist_eq_norm, sub_eq_add_neg] },
  .. pi.add_comm_group }

/-- normed group instance on the product of finitely many normed groups, using the `L^p` norm. -/
instance normed_group [‚àÄi, normed_group (Œ± i)] : normed_group (pi_Lp p hp Œ±) :=
{ ..pi_Lp.semi_normed_group p hp Œ± }

lemma norm_eq {p : ‚Ñù} {hp : 1 ‚â§ p} {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄi, semi_normed_group (Œ≤ i)] (f : pi_Lp p hp Œ≤) :
  ‚à•f‚à• = (‚àë (i : Œπ), ‚à•f i‚à• ^ p) ^ (1/p) := rfl

lemma norm_eq_of_nat {p : ‚Ñù} {hp : 1 ‚â§ p} {Œ≤ : Œπ ‚Üí Type*}
  [‚àÄi, semi_normed_group (Œ≤ i)] (n : ‚Ñï) (h : p = n) (f : pi_Lp p hp Œ≤) :
  ‚à•f‚à• = (‚àë (i : Œπ), ‚à•f i‚à• ^ n) ^ (1/(n : ‚Ñù)) :=
by simp [norm_eq, h, real.sqrt_eq_rpow, ‚Üêreal.rpow_nat_cast]

variables (ùïú : Type*) [normed_field ùïú]

/-- The product of finitely many seminormed spaces is a seminormed space, with the `L^p` norm. -/
instance semi_normed_space [‚àÄi, semi_normed_group (Œ≤ i)] [‚àÄi, semi_normed_space ùïú (Œ≤ i)] :
  semi_normed_space ùïú (pi_Lp p hp Œ≤) :=
{ norm_smul_le :=
  begin
    assume c f,
    have : p * (1 / p) = 1 := mul_div_cancel' 1 (ne_of_gt (lt_of_lt_of_le zero_lt_one hp)),
    simp only [pi_Lp.norm_eq, norm_smul, mul_rpow, norm_nonneg, ‚Üêfinset.mul_sum, pi.smul_apply],
    rw [mul_rpow (rpow_nonneg_of_nonneg (norm_nonneg _) _), ‚Üê rpow_mul (norm_nonneg _),
        this, rpow_one],
    exact finset.sum_nonneg (Œª i hi, rpow_nonneg_of_nonneg (norm_nonneg _) _)
  end,
  .. pi.module Œπ Œ≤ ùïú }

/-- The product of finitely many normed spaces is a normed space, with the `L^p` norm. -/
instance normed_space [‚àÄi, normed_group (Œ± i)] [‚àÄi, normed_space ùïú (Œ± i)] :
  normed_space ùïú (pi_Lp p hp Œ±) :=
{ ..pi_Lp.semi_normed_space p hp Œ± ùïú }

/- Register simplification lemmas for the applications of `pi_Lp` elements, as the usual lemmas
for Pi types will not trigger. -/
variables {ùïú p hp Œ±}
[‚àÄi, semi_normed_group (Œ≤ i)] [‚àÄi, semi_normed_space ùïú (Œ≤ i)] (c : ùïú) (x y : pi_Lp p hp Œ≤) (i : Œπ)

@[simp] lemma add_apply : (x + y) i = x i + y i := rfl
@[simp] lemma sub_apply : (x - y) i = x i - y i := rfl
@[simp] lemma smul_apply : (c ‚Ä¢ x) i = c ‚Ä¢ x i := rfl
@[simp] lemma neg_apply : (-x) i = - (x i) := rfl

end pi_Lp

section

/-! ### Inner product space structure on product spaces -/

variables {ùïú : Type*} [is_R_or_C ùïú] {E : Type*} [inner_product_space ùïú E]
local notation `‚ü™`x`, `y`‚ü´` := @inner ùïú _ _ x y

/-
 If `Œπ` is a finite type and each space `f i`, `i : Œπ`, is an inner product space,
then `Œ† i, f i` is an inner product space as well. Since `Œ† i, f i` is endowed with the sup norm,
we use instead `pi_Lp 2 one_le_two f` for the product space, which is endowed with the `L^2` norm.
-/
instance pi_Lp.inner_product_space {Œπ : Type*} [fintype Œπ] (f : Œπ ‚Üí Type*)
  [Œ† i, inner_product_space ùïú (f i)] : inner_product_space ùïú (pi_Lp 2 one_le_two f) :=
{ inner := Œª x y, ‚àë i, inner (x i) (y i),
  norm_sq_eq_inner :=
  begin
    intro x,
    have h‚ÇÅ : ‚àë (i : Œπ), ‚à•x i‚à• ^ (2 : ‚Ñï) = ‚àë (i : Œπ), ‚à•x i‚à• ^ (2 : ‚Ñù),
    { apply finset.sum_congr rfl,
      intros j hj,
      simp [‚Üêrpow_nat_cast] },
    have h‚ÇÇ : 0 ‚â§ ‚àë (i : Œπ), ‚à•x i‚à• ^ (2 : ‚Ñù),
    { rw [‚Üêh‚ÇÅ],
      exact finset.sum_nonneg (Œª j (hj : j ‚àà finset.univ), pow_nonneg (norm_nonneg (x j)) 2) },
    simp [norm, add_monoid_hom.map_sum, ‚Üênorm_sq_eq_inner],
    rw [‚Üêrpow_nat_cast ((‚àë (i : Œπ), ‚à•x i‚à• ^ (2 : ‚Ñù)) ^ (2 : ‚Ñù)‚Åª¬π) 2],
    rw [‚Üêrpow_mul h‚ÇÇ],
    norm_num [h‚ÇÅ],
  end,
  conj_sym :=
  begin
    intros x y,
    unfold inner,
    rw conj.map_sum,
    apply finset.sum_congr rfl,
    rintros z -,
    apply inner_conj_sym,
  end,
  add_left := Œª x y z,
    show ‚àë i, inner (x i + y i) (z i) = ‚àë i, inner (x i) (z i) + ‚àë i, inner (y i) (z i),
    by simp only [inner_add_left, finset.sum_add_distrib],
  smul_left := Œª x y r,
    show ‚àë (i : Œπ), inner (r ‚Ä¢ x i) (y i) = (conj r) * ‚àë i, inner (x i) (y i),
    by simp only [finset.mul_sum, inner_smul_left] }

@[simp] lemma pi_Lp.inner_apply {Œπ : Type*} [fintype Œπ] {f : Œπ ‚Üí Type*}
  [Œ† i, inner_product_space ùïú (f i)] (x y : pi_Lp 2 one_le_two f) :
  ‚ü™x, y‚ü´ = ‚àë i, ‚ü™x i, y i‚ü´ :=
rfl

lemma pi_Lp.norm_eq_of_L2 {Œπ : Type*} [fintype Œπ] {f : Œπ ‚Üí Type*}
  [Œ† i, inner_product_space ùïú (f i)] (x : pi_Lp 2 one_le_two f) :
  ‚à•x‚à• = sqrt (‚àë (i : Œπ), ‚à•x i‚à• ^ 2) :=
by { rw [pi_Lp.norm_eq_of_nat 2]; simp [sqrt_eq_rpow] }


/-- The standard real/complex Euclidean space, functions on a finite type. For an `n`-dimensional
space use `euclidean_space ùïú (fin n)`. -/
@[reducible, nolint unused_arguments]
def euclidean_space (ùïú : Type*) [is_R_or_C ùïú]
  (n : Type*) [fintype n] : Type* := pi_Lp 2 one_le_two (Œª (i : n), ùïú)

lemma euclidean_space.norm_eq {ùïú : Type*} [is_R_or_C ùïú] {n : Type*} [fintype n]
  (x : euclidean_space ùïú n) : ‚à•x‚à• = real.sqrt (‚àë (i : n), ‚à•x i‚à• ^ 2) :=
pi_Lp.norm_eq_of_L2 x

section
local attribute [reducible] pi_Lp

variables [fintype Œπ]

instance : finite_dimensional ùïú (euclidean_space ùïú Œπ) := by apply_instance
instance : inner_product_space ùïú (euclidean_space ùïú Œπ) := by apply_instance

@[simp] lemma finrank_euclidean_space :
  finite_dimensional.finrank ùïú (euclidean_space ùïú Œπ) = fintype.card Œπ := by simp

lemma finrank_euclidean_space_fin {n : ‚Ñï} :
  finite_dimensional.finrank ùïú (euclidean_space ùïú (fin n)) = n := by simp

/-- An orthonormal basis on a fintype `Œπ` for an inner product space induces an isometry with
`euclidean_space ùïú Œπ`. -/
def basis.isometry_euclidean_of_orthonormal
  (v : basis Œπ ùïú E) (hv : orthonormal ùïú v) :
  E ‚âÉ‚Çó·µ¢[ùïú] (euclidean_space ùïú Œπ) :=
v.equiv_fun.isometry_of_inner
begin
  intros x y,
  let p : euclidean_space ùïú Œπ := v.equiv_fun x,
  let q : euclidean_space ùïú Œπ := v.equiv_fun y,
  have key : ‚ü™p, q‚ü´ = ‚ü™‚àë i, p i ‚Ä¢ v i, ‚àë i, q i ‚Ä¢ v i‚ü´,
  { simp [sum_inner, inner_smul_left, hv.inner_right_fintype] },
  convert key,
  { rw [‚Üê v.equiv_fun.symm_apply_apply x, v.equiv_fun_symm_apply] },
  { rw [‚Üê v.equiv_fun.symm_apply_apply y, v.equiv_fun_symm_apply] }
end

end

/-- `‚ÑÇ` is isometric to `‚Ñù¬≤` with the Euclidean inner product. -/
def complex.isometry_euclidean : ‚ÑÇ ‚âÉ‚Çó·µ¢[‚Ñù] (euclidean_space ‚Ñù (fin 2)) :=
complex.basis_one_I.isometry_euclidean_of_orthonormal
begin
  rw orthonormal_iff_ite,
  intros i, fin_cases i;
  intros j; fin_cases j;
  simp [real_inner_eq_re_inner]
end

@[simp] lemma complex.isometry_euclidean_symm_apply (x : euclidean_space ‚Ñù (fin 2)) :
  complex.isometry_euclidean.symm x = (x 0) + (x 1) * I :=
begin
  convert complex.basis_one_I.equiv_fun_symm_apply x,
  { simpa },
  { simp },
end

lemma complex.isometry_euclidean_proj_eq_self (z : ‚ÑÇ) :
  ‚Üë(complex.isometry_euclidean z 0) + ‚Üë(complex.isometry_euclidean z 1) * (I : ‚ÑÇ) = z :=
by rw [‚Üê complex.isometry_euclidean_symm_apply (complex.isometry_euclidean z),
  complex.isometry_euclidean.symm_apply_apply z]

@[simp] lemma complex.isometry_euclidean_apply_zero (z : ‚ÑÇ) :
  complex.isometry_euclidean z 0 = z.re :=
by { conv_rhs { rw ‚Üê complex.isometry_euclidean_proj_eq_self z }, simp }

@[simp] lemma complex.isometry_euclidean_apply_one (z : ‚ÑÇ) :
  complex.isometry_euclidean z 1 = z.im :=
by { conv_rhs { rw ‚Üê complex.isometry_euclidean_proj_eq_self z }, simp }

open finite_dimensional

/-- Given a natural number `n` equal to the `finrank` of a finite-dimensional inner product space,
there exists an isometry from the space to `euclidean_space ùïú (fin n)`. -/
def linear_isometry_equiv.of_inner_product_space
  [finite_dimensional ùïú E] {n : ‚Ñï} (hn : finrank ùïú E = n) :
  E ‚âÉ‚Çó·µ¢[ùïú] (euclidean_space ùïú (fin n)) :=
(fin_orthonormal_basis hn).isometry_euclidean_of_orthonormal (fin_orthonormal_basis_orthonormal hn)

local attribute [instance] finite_dimensional_of_finrank_eq_succ

/-- Given a natural number `n` one less than the `finrank` of a finite-dimensional inner product
space, there exists an isometry from the orthogonal complement of a nonzero singleton to
`euclidean_space ùïú (fin n)`. -/
def linear_isometry_equiv.from_orthogonal_span_singleton
  (n : ‚Ñï) [fact (finrank ùïú E = n + 1)] {v : E} (hv : v ‚â† 0) :
  (ùïú ‚àô v)·óÆ ‚âÉ‚Çó·µ¢[ùïú] (euclidean_space ùïú (fin n)) :=
linear_isometry_equiv.of_inner_product_space (finrank_orthogonal_span_singleton hv)

end
