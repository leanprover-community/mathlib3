/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import analysis.mean_inequalities
import data.fintype.order

/-!
# `L^p` distance on finite products of metric spaces
Given finitely many metric spaces, one can put the max distance on their product, but there is also
a whole family of natural distances, indexed by a real parameter `p ‚àà [1, ‚àû)`, that also induce
the product topology. We define them in this file. The distance on `Œ† i, Œ± i` is given by
$$
d(x, y) = \left(\sum d(x_i, y_i)^p\right)^{1/p}.
$$

We give instances of this construction for emetric spaces, metric spaces, normed groups and normed
spaces.

To avoid conflicting instances, all these are defined on a copy of the original Pi type, named
`pi_Lp p Œ±`. The assumpion `[fact (1 ‚â§ p)]` is required for the metric and normed space instances.

We ensure that the topology and uniform structure on `pi_Lp p Œ±` are (defeq to) the product
topology and product uniformity, to be able to use freely continuity statements for the coordinate
functions, for instance.

## Implementation notes

We only deal with the `L^p` distance on a product of finitely many metric spaces, which may be
distinct. A closely related construction is `lp`, the `L^p` norm on a product of (possibly
infinitely many) normed spaces, where the norm is
$$
\left(\sum ‚à•f (x)‚à•^p \right)^{1/p}.
$$
However, the topology induced by this construction is not the product topology, and some functions
have infinite `L^p` norm. These subtleties are not present in the case of finitely many metric
spaces, hence it is worth devoting a file to this specific case which is particularly well behaved.

Another related construction is `measure_theory.Lp`, the `L^p` norm on the space of functions from
a measure space to a normed space, where the norm is
$$
\left(\int ‚à•f (x)‚à•^p dŒº\right)^{1/p}.
$$
This has all the same subtleties as `lp`, and the further subtlety that this only
defines a seminorm (as almost everywhere zero functions have zero `L^p` norm).
The construction `pi_Lp` corresponds to the special case of `measure_theory.Lp` in which the basis
is a finite space equipped with the counting measure.

To prove that the topology (and the uniform structure) on a finite product with the `L^p` distance
are the same as those coming from the `L^‚àû` distance, we could argue that the `L^p` and `L^‚àû` norms
are equivalent on `‚Ñù^n` for abstract (norm equivalence) reasons. Instead, we give a more explicit
(easy) proof which provides a comparison between these two norms with explicit constants.

We also set up the theory for `pseudo_emetric_space` and `pseudo_metric_space`.
-/

open real set filter is_R_or_C bornology
open_locale big_operators uniformity topological_space nnreal ennreal

noncomputable theory

variables {Œπ : Type*}

-- this needs to go in `data.real.ennreal`
instance : zero_le_one_class ‚Ñù‚â•0‚àû := ‚ü®zero_le 1‚ü©

-- this should be a bit more general, see `fintype.sum_equiv`
lemma conditionally_complete_lattice.supr_equiv {Œπ Œπ' E : Type*} [conditionally_complete_lattice E]
  (e : Œπ ‚âÉ Œπ') {f : Œπ ‚Üí E} (hf :bdd_above (range f)) : (‚®Ü i, f (e.symm i)) = ‚®Ü i, f i :=
begin
  cases is_empty_or_nonempty Œπ; resetI,
  { haveI : is_empty Œπ' := (equiv.is_empty_congr e).mp h, simp only [supr, range_eq_empty] },
  { haveI : nonempty Œπ' := (equiv.nonempty_congr e).mp h,
    have hf' : bdd_above (range (Œª i, f (e.symm i))),
    { obtain ‚ü®M, hM‚ü© := hf, use M, rintros - ‚ü®i, rfl‚ü©, exact hM ‚ü®(e.symm i), rfl‚ü© },
    refine le_antisymm (csupr_le (Œª i, le_csupr hf (e.symm i))) (csupr_le (Œª i, _)),
    simpa only [equiv.symm_apply_apply] using le_csupr hf' (e i) },
end

lemma fintype.supr_equiv {Œπ Œπ' E : Type*} [fintype Œπ] [conditionally_complete_lattice E]
  (e : Œπ ‚âÉ Œπ') (f : Œπ ‚Üí E) : (‚®Ü i, f (e.symm i)) = ‚®Ü i, f i :=
conditionally_complete_lattice.supr_equiv e (fintype.bdd_above_range f)

/-- A copy of a Pi type, on which we will put the `L^p` distance. Since the Pi type itself is
already endowed with the `L^‚àû` distance, we need the type synonym to avoid confusing typeclass
resolution. Also, we let it depend on `p`, to get a whole family of type on which we can put
different distances. -/
@[nolint unused_arguments]
def pi_Lp {Œπ : Type*} (p : ‚Ñù‚â•0‚àû) (Œ± : Œπ ‚Üí Type*) : Type* := Œ† (i : Œπ), Œ± i

instance {Œπ : Type*} (p : ‚Ñù‚â•0‚àû) (Œ± : Œπ ‚Üí Type*) [Œ† i, inhabited (Œ± i)] : inhabited (pi_Lp p Œ±) :=
‚ü®Œª i, default‚ü©

instance fact_one_le_one_real : fact ((1 : ‚Ñù‚â•0‚àû) ‚â§ 1) := ‚ü®rfl.le‚ü©
instance fact_one_le_two_real : fact ((1 : ‚Ñù‚â•0‚àû) ‚â§ 2) := ‚ü®one_le_two‚ü©

namespace pi_Lp

variables (p : ‚Ñù‚â•0‚àû) [fact_one_le_p : fact (1 ‚â§ p)] (ùïú : Type*) (Œ± : Œπ ‚Üí Type*) (Œ≤ : Œπ ‚Üí Type*)

/-- Canonical bijection between `pi_Lp p Œ±` and the original Pi type. We introduce it to be able
to compare the `L^p` and `L^‚àû` distances through it. -/
protected def equiv : pi_Lp p Œ± ‚âÉ Œ† (i : Œπ), Œ± i :=
equiv.refl _

lemma equiv_apply (x : pi_Lp p Œ±) (i : Œπ) : pi_Lp.equiv p Œ± x i = x i := rfl
lemma equiv_symm_apply (x : Œ† i, Œ± i) (i : Œπ) : (pi_Lp.equiv p Œ±).symm x i = x i := rfl

@[simp] lemma equiv_apply' (x : pi_Lp p Œ±) : pi_Lp.equiv p Œ± x = x := rfl
@[simp] lemma equiv_symm_apply' (x : Œ† i, Œ± i) : (pi_Lp.equiv p Œ±).symm x = x := rfl

section
/-!
### The uniformity on finite `L^p` products is the product uniformity

In this section, we put the `L^p` edistance on `pi_Lp p Œ±`, and we check that the uniformity
coming from this edistance coincides with the product uniformity, by showing that the canonical
map to the Pi type (with the `L^‚àû` distance) is a uniform embedding, as it is both Lipschitz and
antiLipschitz.

We only register this emetric space structure as a temporary instance, as the true instance (to be
registered later) will have as uniformity exactly the product uniformity, instead of the one coming
from the edistance (which is equal to it, but not defeq). See Note [forgetful inheritance]
explaining why having definitionally the right uniformity is often important.
-/

variables [Œ† i, pseudo_metric_space (Œ± i)] [Œ† i, pseudo_emetric_space (Œ≤ i)] [fintype Œπ]

def edist_aux : has_edist (pi_Lp p Œ≤) :=
{ edist := Œª f g, if hp : p = 0 then by subst hp; exact {i | f i ‚â† g i}.to_finite.to_finset.card
    else (if p = ‚àû then ‚®Ü i, edist (f i) (g i)
    else (‚àë i, (edist (f i) (g i) ^ p.to_real)) ^ (1/p.to_real)) }

local attribute [instance] pi_Lp.edist_aux

variable {Œ≤}
lemma edist_aux_eq_card (f g : pi_Lp 0 Œ≤) : edist f g = {i | f i ‚â† g i}.to_finite.to_finset.card :=
dif_pos rfl

lemma edist_aux_eq_sum {p : ‚Ñù‚â•0‚àû} (hp : 0 < p.to_real) (f g : pi_Lp p Œ≤) :
  edist f g = (‚àë i, edist (f i) (g i) ^ p.to_real) ^ (1/p.to_real) :=
begin
  dsimp [edist],
  rw ennreal.to_real_pos_iff at hp,
  rw [dif_neg hp.1.ne', if_neg hp.2.ne],
end

lemma edist_aux_eq_supr (f g : pi_Lp ‚àû Œ≤) : edist f g = ‚®Ü i, edist (f i) (g i) :=
begin
  dsimp [edist],
  rw [dif_neg ennreal.top_ne_zero, if_pos rfl]
end

lemma edist_aux_self (f : pi_Lp p Œ≤) : edist f f = 0 :=
begin
  rcases p.trichotomy with (rfl | rfl | h),
  { simp [edist_aux_eq_card], },
  { simp [edist_aux_eq_supr], },
  { simp [edist_aux_eq_sum h, ennreal.zero_rpow_of_pos h, ennreal.zero_rpow_of_pos (inv_pos.2 $ h)]}
end

lemma edist_aux_comm (f g : pi_Lp p Œ≤) : edist f g = edist g f :=
begin
  rcases p.trichotomy with (rfl | rfl | h),
  { simp only [edist_aux_eq_card, eq_comm, ne.def] },
  { simp only [edist_aux_eq_supr, edist_comm] },
  { simp only [edist_aux_eq_sum h, edist_comm] }
end

include fact_one_le_p

lemma edist_aux_triangle (f g h : pi_Lp p Œ≤) : edist f h ‚â§ edist f g + edist g h :=
begin
  unfreezingI { rcases p.dichotomy with (rfl | hp) },
  { simp only [edist_aux_eq_supr],
    cases is_empty_or_nonempty Œπ; resetI,
    { simp only [csupr_of_empty, ennreal.bot_eq_zero, add_zero, nonpos_iff_eq_zero] },
    exact supr_le (Œª i, (edist_triangle _ (g i) _).trans $ add_le_add (le_supr _ i) (le_supr _ i))},
  { simp only [edist_aux_eq_sum (zero_lt_one.trans_le hp)],
    calc (‚àë i, edist (f i) (h i) ^ p.to_real) ^ (1 / p.to_real) ‚â§
    (‚àë i, (edist (f i) (g i) + edist (g i) (h i)) ^ p.to_real) ^ (1 / p.to_real) :
    begin
      apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ zero_le_one.trans hp),
      refine finset.sum_le_sum (Œª i hi, _),
      exact ennreal.rpow_le_rpow (edist_triangle _ _ _) (zero_le_one.trans hp),
    end
    ... ‚â§ (‚àë i, edist (f i) (g i) ^ p.to_real) ^ (1 / p.to_real)
          + (‚àë i, edist (g i) (h i) ^ p.to_real) ^ (1 / p.to_real) :
      ennreal.Lp_add_le _ _ _ hp } ,
end

variable (Œ≤)

/-- Endowing the space `pi_Lp p Œ≤` with the `L^p` pseudoedistance. This definition is not
satisfactory, as it does not register the fact that the topology and the uniform structure coincide
with the product one. Therefore, we do not register it as an instance. Using this as a temporary
pseudoemetric space instance, we will show that the uniform structure is equal (but not defeq) to
the product one, and then register an instance in which we replace the uniform structure by the
product one using this pseudoemetric space and `pseudo_emetric_space.replace_uniformity`. -/
def pseudo_emetric_aux : pseudo_emetric_space (pi_Lp p Œ≤) :=
{ edist_self := edist_aux_self p,
  edist_comm := edist_aux_comm p,
  edist_triangle := edist_aux_triangle p }

omit fact_one_le_p

local attribute [instance] pi_Lp.pseudo_emetric_aux

def dist_aux : has_dist (pi_Lp p Œ±) :=
{ dist := Œª f g, if hp : p = 0 then by subst hp; exact {i | f i ‚â† g i}.to_finite.to_finset.card
    else (if p = ‚àû then ‚®Ü i, dist (f i) (g i)
    else (‚àë i, (dist (f i) (g i) ^ p.to_real)) ^ (1/p.to_real)) }

local attribute [instance] pi_Lp.dist_aux

variable {Œ±}
lemma dist_aux_eq_card (f g : pi_Lp 0 Œ±) : dist f g = {i | f i ‚â† g i}.to_finite.to_finset.card :=
dif_pos rfl

lemma dist_aux_eq_sum {p : ‚Ñù‚â•0‚àû} (hp : 0 < p.to_real) (f g : pi_Lp p Œ±) :
  dist f g = (‚àë i, dist (f i) (g i) ^ p.to_real) ^ (1/p.to_real) :=
begin
  dsimp [dist],
  rw ennreal.to_real_pos_iff at hp,
  rw [dif_neg hp.1.ne', if_neg hp.2.ne],
end

lemma dist_aux_eq_csupr (f g : pi_Lp ‚àû Œ±) : dist f g = ‚®Ü i, dist (f i) (g i) :=
begin
  dsimp [dist],
  rw [dif_neg ennreal.top_ne_zero, if_pos rfl]
end

variable (Œ±)
include fact_one_le_p

example (f g : pi_Lp ‚àû Œ±) : (‚®Ü i, edist (f i) (g i)) ‚â† ‚ä§ :=
begin
  obtain ‚ü®M, hM‚ü© := fintype.exists_le (Œª i, (‚ü®dist (f i) (g i), dist_nonneg‚ü© : ‚Ñù‚â•0)),
  refine ne_of_lt ((supr_le $ Œª i, _).trans_lt (@ennreal.coe_lt_top M)),
  simp only [edist, pseudo_metric_space.edist_dist, ennreal.of_real_eq_coe_nnreal dist_nonneg],
  exact_mod_cast hM i,
end

/-- Endowing the space `pi_Lp p Œ±` with the `L^p` pseudodistance. This definition is not
satisfactory, as it does not register the fact that the topology, the uniform structure, and the
bornology coincide with the product ones. Therefore, we do not register it as an instance. Using
this as a temporary pseudoemetric space instance, we will show that the uniform structure is equal
(but not defeq) to the product one, and then register an instance in which we replace the uniform
structure and the bornology by the product ones using this pseudometric space,
`pseudo_metric_space.replace_uniformity`, and `pseudo_metric_space.replace_bornology`.

See note [reducible non-instances] -/
@[reducible] def pseudo_metric_aux : pseudo_metric_space (pi_Lp p Œ±) :=
have supr_ne_top : ‚àÄ (f g : pi_Lp ‚àû Œ±), (‚®Ü i, edist (f i) (g i)) ‚â† ‚ä§ := Œª f g,
begin
  obtain ‚ü®M, hM‚ü© := fintype.exists_le (Œª i, (‚ü®dist (f i) (g i), dist_nonneg‚ü© : ‚Ñù‚â•0)),
  refine ne_of_lt ((supr_le $ Œª i, _).trans_lt (@ennreal.coe_lt_top M)),
  simp only [edist, pseudo_metric_space.edist_dist, ennreal.of_real_eq_coe_nnreal dist_nonneg],
  exact_mod_cast hM i,
end,
pseudo_emetric_space.to_pseudo_metric_space_of_dist dist
  (Œª f g,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { exact supr_ne_top f g },
    { rw edist_aux_eq_sum (zero_lt_one.trans_le h),
      exact ennreal.rpow_ne_top_of_nonneg (one_div_nonneg.2 (zero_le_one.trans h)) (ne_of_lt $
        (ennreal.sum_lt_top $ Œª i hi, ennreal.rpow_ne_top_of_nonneg (zero_le_one.trans h)
        (edist_ne_top _ _)))}
  end)
  (Œª f g,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { rw [edist_aux_eq_supr, dist_aux_eq_csupr],
      { cases is_empty_or_nonempty Œπ; resetI,
        { simp only [real.csupr_empty, csupr_of_empty, ennreal.bot_eq_zero, ennreal.zero_to_real] },
        { refine le_antisymm (csupr_le $ Œª i, _) _,
          { rw [‚Üêennreal.of_real_le_iff_le_to_real (supr_ne_top f g), ‚Üêpseudo_metric_space.edist_dist],
            exact le_supr _ i, },
          { refine ennreal.to_real_le_of_le_of_real (real.Sup_nonneg _ _) (supr_le $ Œª i, _),
            { rintro - ‚ü®i, rfl‚ü©,
              exact dist_nonneg, },
            { unfold edist, rw pseudo_metric_space.edist_dist,
              exact ennreal.of_real_le_of_real (le_csupr (fintype.bdd_above_range _) i), } } } } },
    { have A : ‚àÄ i, edist (f i) (g i) ^ p.to_real ‚â† ‚ä§,
        from Œª i, ennreal.rpow_ne_top_of_nonneg (zero_le_one.trans h) (edist_ne_top _ _),
      simp only [edist_aux_eq_sum (zero_lt_one.trans_le h), dist_edist, ennreal.to_real_rpow,
        dist_aux_eq_sum (zero_lt_one.trans_le h), ‚Üê ennreal.to_real_sum (Œª i _, A i)] }
  end)

local attribute [instance] pi_Lp.pseudo_metric_aux

lemma lipschitz_with_equiv_aux : lipschitz_with 1 (pi_Lp.equiv p Œ≤) :=
begin
  intros f g,
  unfreezingI { rcases p.dichotomy with (rfl | h) },
  { simpa only [equiv_apply', ennreal.coe_one, one_mul, edist_aux_eq_supr, edist, finset.sup_le_iff,
      finset.mem_univ, forall_true_left] using le_supr (Œª i, edist (f i) (g i)), },
  { have cancel : p.to_real * (1/p.to_real) = 1 := mul_div_cancel' 1 (zero_lt_one.trans_le h).ne',
    rw edist_aux_eq_sum (zero_lt_one.trans_le h),
    simp only [edist, forall_prop_of_true, one_mul, finset.mem_univ, finset.sup_le_iff,
      ennreal.coe_one],
    assume i,
    calc
    edist (f i) (g i) = (edist (f i) (g i) ^ p.to_real) ^ (1/p.to_real) :
      by simp [‚Üê ennreal.rpow_mul, cancel, -one_div]
    ... ‚â§ (‚àë i, edist (f i) (g i) ^ p.to_real) ^ (1 / p.to_real) :
    begin
      apply ennreal.rpow_le_rpow _ (one_div_nonneg.2 $ (zero_le_one.trans h)),
      exact finset.single_le_sum (Œª i hi, (bot_le : (0 : ‚Ñù‚â•0‚àû) ‚â§ _)) (finset.mem_univ i)
    end }
end

lemma antilipschitz_with_equiv_aux :
  antilipschitz_with ((fintype.card Œπ : ‚Ñù‚â•0) ^ (1 / p).to_real) (pi_Lp.equiv p Œ≤) :=
begin
  intros f g,
  unfreezingI { rcases p.dichotomy with (rfl | h) },
  { simp only [edist_aux_eq_supr, ennreal.div_top, ennreal.zero_to_real, nnreal.rpow_zero,
      ennreal.coe_one, equiv_apply', one_mul, supr_le_iff],
    exact Œª i, finset.le_sup (finset.mem_univ i), },
  { have pos : 0 < p.to_real := zero_lt_one.trans_le h,
    have nonneg : 0 ‚â§ 1 / p.to_real := one_div_nonneg.2 (le_of_lt pos),
    have cancel : p.to_real * (1/p.to_real) = 1 := mul_div_cancel' 1 (ne_of_gt pos),
    rw [edist_aux_eq_sum pos, ennreal.to_real_div 1 p],
    simp only [edist, equiv_apply', ‚Üêone_div, ennreal.one_to_real],
    calc (‚àë i, edist (f i) (g i) ^ p.to_real) ^ (1 / p.to_real) ‚â§
    (‚àë i, edist (pi_Lp.equiv p Œ≤ f) (pi_Lp.equiv p Œ≤ g) ^ p.to_real) ^ (1 / p.to_real) :
    begin
      apply ennreal.rpow_le_rpow _ nonneg,
      apply finset.sum_le_sum (Œª i hi, _),
      apply ennreal.rpow_le_rpow _ (le_of_lt pos),
      exact finset.le_sup (finset.mem_univ i)
    end
    ... = (((fintype.card Œπ : ‚Ñù‚â•0)) ^ (1 / p.to_real) : ‚Ñù‚â•0) *
      edist (pi_Lp.equiv p Œ≤ f) (pi_Lp.equiv p Œ≤ g) :
    begin
      simp only [nsmul_eq_mul, finset.card_univ, ennreal.rpow_one, finset.sum_const,
        ennreal.mul_rpow_of_nonneg _ _ nonneg, ‚Üêennreal.rpow_mul, cancel],
      have : (fintype.card Œπ : ‚Ñù‚â•0‚àû) = (fintype.card Œπ : ‚Ñù‚â•0) :=
        (ennreal.coe_nat (fintype.card Œπ)).symm,
      rw [this, ennreal.coe_rpow_of_nonneg _ nonneg]
    end }
end

lemma aux_uniformity_eq :
  ùì§ (pi_Lp p Œ≤) = @uniformity _ (Pi.uniform_space _) :=
begin
  have A : uniform_inducing (pi_Lp.equiv p Œ≤) :=
    (antilipschitz_with_equiv_aux p Œ≤).uniform_inducing
    (lipschitz_with_equiv_aux p Œ≤).uniform_continuous,
  have : (Œª (x : pi_Lp p Œ≤ √ó pi_Lp p Œ≤),
    ((pi_Lp.equiv p Œ≤) x.fst, (pi_Lp.equiv p Œ≤) x.snd)) = id,
    by ext i; refl,
  rw [‚Üê A.comap_uniformity, this, comap_id]
end

lemma aux_cobounded_eq :
  cobounded (pi_Lp p Œ±) = @cobounded _ pi.bornology :=
calc cobounded (pi_Lp p Œ±) = comap (pi_Lp.equiv p Œ±) (cobounded _) :
  le_antisymm (antilipschitz_with_equiv_aux p Œ±).tendsto_cobounded.le_comap
    (lipschitz_with_equiv_aux p Œ±).comap_cobounded_le
... = _ : comap_id

end

/-! ### Instances on finite `L^p` products -/

instance uniform_space [Œ† i, uniform_space (Œ≤ i)] : uniform_space (pi_Lp p Œ≤) :=
Pi.uniform_space _

variable [fintype Œπ]

instance bornology [Œ† i, bornology (Œ≤ i)] : bornology (pi_Lp p Œ≤) := pi.bornology

include fact_one_le_p

/-- pseudoemetric space instance on the product of finitely many pseudoemetric spaces, using the
`L^p` pseudoedistance, and having as uniformity the product uniformity. -/
instance [Œ† i, pseudo_emetric_space (Œ≤ i)] : pseudo_emetric_space (pi_Lp p Œ≤) :=
(pseudo_emetric_aux p Œ≤).replace_uniformity (aux_uniformity_eq p Œ≤).symm

/-- emetric space instance on the product of finitely many emetric spaces, using the `L^p`
edistance, and having as uniformity the product uniformity. -/
instance [Œ† i, emetric_space (Œ± i)] : emetric_space (pi_Lp p Œ±) :=
@emetric.of_t0_pseudo_emetric_space (pi_Lp p Œ±) _ pi.t0_space

omit fact_one_le_p
variables {p Œ≤}
lemma edist_eq_supr [Œ† i, pseudo_emetric_space (Œ≤ i)] (x y : pi_Lp ‚àû Œ≤) :
  edist x y = ‚®Ü i, edist (x i) (y i) := rfl

include fact_one_le_p
lemma edist_eq_sum [Œ† i, pseudo_emetric_space (Œ≤ i)] (hp : 0 < p.to_real) (x y : pi_Lp p Œ≤) :
  edist x y = (‚àë i, edist (x i) (y i) ^ p.to_real) ^ (1 / p.to_real) := edist_aux_eq_sum hp x y
variables (p Œ≤)

/-- pseudometric space instance on the product of finitely many psuedometric spaces, using the
`L^p` distance, and having as uniformity the product uniformity. -/
instance [Œ† i, pseudo_metric_space (Œ≤ i)] : pseudo_metric_space (pi_Lp p Œ≤) :=
((pseudo_metric_aux p Œ≤).replace_uniformity (aux_uniformity_eq p Œ≤).symm).replace_bornology $
  Œª s, filter.ext_iff.1 (aux_cobounded_eq p Œ≤).symm s·∂ú

/-- metric space instance on the product of finitely many metric spaces, using the `L^p` distance,
and having as uniformity the product uniformity. -/
instance [Œ† i, metric_space (Œ± i)] : metric_space (pi_Lp p Œ±) := metric.of_t0_pseudo_metric_space _

omit fact_one_le_p
lemma dist_eq_sum {p : ‚Ñù‚â•0‚àû} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [Œ† i, pseudo_metric_space (Œ≤ i)] (hp : p ‚â† ‚àû) (x y : pi_Lp p Œ≤) :
  dist x y = (‚àë i : Œπ, dist (x i) (y i) ^ p.to_real) ^ (1 / p.to_real) :=
dist_aux_eq_sum (zero_lt_one.trans_le (p.dichotomy.resolve_left hp)) _ _

lemma dist_eq_csupr {Œ≤ : Œπ ‚Üí Type*} [Œ† i, pseudo_metric_space (Œ≤ i)] (x y : pi_Lp ‚àû Œ≤) :
  dist x y = ‚®Ü i, dist (x i) (y i) :=  rfl

lemma nndist_eq_sum {p : ‚Ñù‚â•0‚àû} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [Œ† i, pseudo_metric_space (Œ≤ i)] (hp : p ‚â† ‚àû) (x y : pi_Lp p Œ≤) :
  nndist x y = (‚àë i : Œπ, nndist (x i) (y i) ^ p.to_real) ^ (1 / p.to_real) :=
subtype.ext $ by { push_cast, exact dist_eq_sum hp _ _ }

lemma nndist_eq_supr {Œ≤ : Œπ ‚Üí Type*} [Œ† i, pseudo_metric_space (Œ≤ i)] (x y : pi_Lp ‚àû Œ≤) :
  nndist x y = ‚®Ü i, nndist (x i) (y i) :=
subtype.ext $ by { push_cast, exact dist_eq_csupr _ _ }

include fact_one_le_p

lemma lipschitz_with_equiv [Œ† i, pseudo_emetric_space (Œ≤ i)] :
  lipschitz_with 1 (pi_Lp.equiv p Œ≤) :=
lipschitz_with_equiv_aux p Œ≤

lemma antilipschitz_with_equiv [Œ† i, pseudo_emetric_space (Œ≤ i)] :
  antilipschitz_with ((fintype.card Œπ : ‚Ñù‚â•0) ^ (1 / p).to_real) (pi_Lp.equiv p Œ≤) :=
antilipschitz_with_equiv_aux p Œ≤

omit fact_one_le_p
lemma isometry_pi_Lp_equiv_infty [Œ† i, pseudo_emetric_space (Œ≤ i)] :
  isometry (pi_Lp.equiv ‚àû Œ≤) :=
Œª x y, le_antisymm (by simpa only [ennreal.coe_one, one_mul] using lipschitz_with_equiv ‚àû Œ≤ x y)
  (by simpa only [ennreal.div_top, ennreal.zero_to_real, nnreal.rpow_zero, ennreal.coe_one, one_mul]
    using antilipschitz_with_equiv ‚àû Œ≤ x y)

instance has_norm [Œ† i, has_norm (Œ≤ i)] [Œ† i, has_zero (Œ≤ i)] : has_norm (pi_Lp p Œ≤) :=
{ norm := Œª f, if hp : p = 0 then by subst hp; exact {i | f i ‚â† 0}.to_finite.to_finset.card
   else (if p = ‚àû then ‚®Ü i, ‚à•f i‚à• else (‚àë i, ‚à•f i‚à• ^ p.to_real) ^ (1 / p.to_real)) }

variables {p Œ≤}
lemma norm_eq_card [Œ† i, has_norm (Œ≤ i)] [Œ† i, has_zero (Œ≤ i)] (f : pi_Lp 0 Œ≤) :
  ‚à•f‚à• = {i | f i ‚â† 0}.to_finite.to_finset.card :=
dif_pos rfl

lemma norm_eq_csupr [Œ† i, has_norm (Œ≤ i)] [Œ† i, has_zero (Œ≤ i)] (f : pi_Lp ‚àû Œ≤) :
  ‚à•f‚à• = ‚®Ü i, ‚à•f i‚à• :=
begin
  dsimp [norm],
  rw [dif_neg ennreal.top_ne_zero, if_pos rfl]
end

lemma norm_eq_sum [Œ† i, has_norm (Œ≤ i)] [Œ† i, has_zero (Œ≤ i)] (hp : 0 < p.to_real) (f : pi_Lp p Œ≤) :
  ‚à•f‚à• = (‚àë i, ‚à•f i‚à• ^ p.to_real) ^ (1 / p.to_real) :=
begin
  dsimp [norm],
  rw ennreal.to_real_pos_iff at hp,
  rw [dif_neg hp.1.ne', if_neg hp.2.ne],
end

include fact_one_le_p

variables (p Œ≤)
/-- seminormed group instance on the product of finitely many normed groups, using the `L^p`
norm. -/
instance seminormed_add_comm_group [Œ† i, seminormed_add_comm_group (Œ≤ i)] :
  seminormed_add_comm_group (pi_Lp p Œ≤) :=
{ dist_eq := Œª x y,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { simpa only [dist_eq_csupr, norm_eq_csupr, dist_eq_norm] },
    { have : p ‚â† ‚àû, { intros hp, rw [hp, ennreal.top_to_real] at h, linarith,} ,
      simpa only [dist_eq_sum this, norm_eq_sum (zero_lt_one.trans_le h), dist_eq_norm], }
  end,
  .. pi.add_comm_group, }

/-- normed group instance on the product of finitely many normed groups, using the `L^p` norm. -/
instance normed_add_comm_group [Œ† i, normed_add_comm_group (Œ± i)] :
  normed_add_comm_group (pi_Lp p Œ±) :=
{ ..pi_Lp.seminormed_add_comm_group p Œ± }

omit fact_one_le_p

lemma nnnorm_eq_sum {p : ‚Ñù‚â•0‚àû} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*} (hp : p ‚â† ‚àû)
  [Œ† i, seminormed_add_comm_group (Œ≤ i)] (f : pi_Lp p Œ≤) :
  ‚à•f‚à•‚Çä = (‚àë i, ‚à•f i‚à•‚Çä ^ p.to_real) ^ (1 / p.to_real) :=
by { ext, simp [nnreal.coe_sum, norm_eq_sum (zero_lt_one.trans_le (p.dichotomy.resolve_left hp))] }

lemma nnnorm_eq_csupr {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (f : pi_Lp ‚àû Œ≤) :
  ‚à•f‚à•‚Çä = ‚®Ü i, ‚à•f i‚à•‚Çä :=
by { ext, simp [nnreal.coe_supr, norm_eq_csupr] }

lemma norm_eq_of_nat {p : ‚Ñù‚â•0‚àû} [fact (1 ‚â§ p)] {Œ≤ : Œπ ‚Üí Type*}
  [Œ† i, seminormed_add_comm_group (Œ≤ i)] (n : ‚Ñï) (h : p = n) (f : pi_Lp p Œ≤) :
  ‚à•f‚à• = (‚àë i, ‚à•f i‚à• ^ n) ^ (1/(n : ‚Ñù)) :=
begin
  have := zero_lt_one.trans_le (p.dichotomy.resolve_left $ ne_of_eq_of_ne h (ennreal.nat_ne_top n)),
  simp only [one_div, h, real.rpow_nat_cast, ennreal.to_real_nat, eq_self_iff_true,
    finset.sum_congr, norm_eq_sum this],
end

lemma norm_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x : pi_Lp 2 Œ≤) :
  ‚à•x‚à• = sqrt (‚àë (i : Œπ), ‚à•x i‚à• ^ 2) :=
by { convert norm_eq_of_nat 2 (by norm_cast) _, rw sqrt_eq_rpow, norm_cast }

lemma nnnorm_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x : pi_Lp 2 Œ≤) :
  ‚à•x‚à•‚Çä = nnreal.sqrt (‚àë (i : Œπ), ‚à•x i‚à•‚Çä ^ 2) :=
subtype.ext $ by { push_cast, exact norm_eq_of_L2 x }

lemma dist_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x y : pi_Lp 2 Œ≤) :
  dist x y = (‚àë i, dist (x i) (y i) ^ 2).sqrt :=
by simp_rw [dist_eq_norm, norm_eq_of_L2, pi.sub_apply]

lemma nndist_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x y : pi_Lp 2 Œ≤) :
  nndist x y = (‚àë i, nndist (x i) (y i) ^ 2).sqrt :=
subtype.ext $ by { push_cast, exact dist_eq_of_L2 _ _ }

lemma edist_eq_of_L2 {Œ≤ : Œπ ‚Üí Type*} [Œ† i, seminormed_add_comm_group (Œ≤ i)] (x y : pi_Lp 2 Œ≤) :
  edist x y = (‚àë i, edist (x i) (y i) ^ 2) ^ (1 / 2 : ‚Ñù) :=
by simp [pi_Lp.edist_eq_sum]

include fact_one_le_p

variables [normed_field ùïú]

-- this was necessary to get Lean to accept `‚à•c ‚Ä¢ f‚à•‚Çä` in the `normed_space` instance below
-- can we just do this with `letI`?
instance module [Œ† i, seminormed_add_comm_group (Œ≤ i)] [Œ† i, module ùïú (Œ≤ i)] :
  module ùïú (pi_Lp p Œ≤) := pi.module Œπ Œ≤ ùïú

/-- The product of finitely many normed spaces is a normed space, with the `L^p` norm. -/
instance normed_space [Œ† i, seminormed_add_comm_group (Œ≤ i)] [Œ† i, normed_space ùïú (Œ≤ i)] :
  normed_space ùïú (pi_Lp p Œ≤) :=
{ norm_smul_le := Œª c f,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | hp) },
    { suffices : ‚à•c ‚Ä¢ f‚à•‚Çä = ‚à•c‚à•‚Çä * ‚à•f‚à•‚Çä, { exact_mod_cast nnreal.coe_mono this.le },
      simpa only [nnnorm_eq_csupr, nnreal.mul_supr, ‚Üênnnorm_smul] },
    { have : p.to_real * (1 / p.to_real) = 1 := mul_div_cancel' 1 (zero_lt_one.trans_le hp).ne',
      simp only [norm_eq_sum (zero_lt_one.trans_le hp), norm_smul, mul_rpow, norm_nonneg,
        ‚Üêfinset.mul_sum, pi.smul_apply],
      rw [mul_rpow (rpow_nonneg_of_nonneg (norm_nonneg _) _), ‚Üê rpow_mul (norm_nonneg _),
        this, rpow_one],
      exact finset.sum_nonneg (Œª i hi, rpow_nonneg_of_nonneg (norm_nonneg _) _) },
  end, }

instance finite_dimensional [Œ† i, seminormed_add_comm_group (Œ≤ i)] [Œ† i, normed_space ùïú (Œ≤ i)]
  [I : ‚àÄ i, finite_dimensional ùïú (Œ≤ i)] :
  finite_dimensional ùïú (pi_Lp p Œ≤) :=
finite_dimensional.finite_dimensional_pi' _ _

/- Register simplification lemmas for the applications of `pi_Lp` elements, as the usual lemmas
for Pi types will not trigger. -/
variables {ùïú p Œ±} [Œ† i, seminormed_add_comm_group (Œ≤ i)] [Œ† i, normed_space ùïú (Œ≤ i)] (c : ùïú)
variables (x y : pi_Lp p Œ≤) (x' y' : Œ† i, Œ≤ i) (i : Œπ)

@[simp] lemma zero_apply : (0 : pi_Lp p Œ≤) i = 0 := rfl
@[simp] lemma add_apply : (x + y) i = x i + y i := rfl
@[simp] lemma sub_apply : (x - y) i = x i - y i := rfl
@[simp] lemma smul_apply : (c ‚Ä¢ x) i = c ‚Ä¢ x i := rfl
@[simp] lemma neg_apply : (-x) i = - (x i) := rfl

variables {Œπ' : Type*}
variables [fintype Œπ']

variables (p ùïú) (E : Type*) [normed_add_comm_group E] [normed_space ùïú E]

/-- An equivalence of finite domains induces a linearly isometric equivalence of finitely supported
functions-/
def _root_.linear_isometry_equiv.pi_Lp_congr_left (e : Œπ ‚âÉ Œπ') :
  pi_Lp p (Œª i : Œπ, E) ‚âÉ‚Çó·µ¢[ùïú] pi_Lp p (Œª i : Œπ', E) :=
{ to_linear_equiv := linear_equiv.Pi_congr_left' ùïú (Œª i : Œπ, E) e,
  norm_map' := Œª x,
  begin
    unfreezingI { rcases p.dichotomy with (rfl | h) },
    { simp only [norm_eq_csupr],
      simp_rw linear_equiv.Pi_congr_left'_apply ùïú (Œª i : Œπ, E) e x _,
      rw fintype.supr_equiv e (Œª i, ‚à•x i‚à•),  },
    { simp only [norm_eq_sum (zero_lt_one.trans_le h)],
      simp_rw linear_equiv.Pi_congr_left'_apply ùïú (Œª i : Œπ, E) e x _,
      congr,
      rw fintype.sum_equiv (e.symm),
      exact Œª i, rfl,}
  end, }

variables {p ùïú E}

@[simp] lemma _root_.linear_isometry_equiv.pi_Lp_congr_left_apply
  (e : Œπ ‚âÉ Œπ') (v : pi_Lp p (Œª i : Œπ, E)) :
  linear_isometry_equiv.pi_Lp_congr_left p ùïú E e v = equiv.Pi_congr_left' (Œª i : Œπ, E) e v :=
rfl

@[simp] lemma _root_.linear_isometry_equiv.pi_Lp_congr_left_symm (e : Œπ ‚âÉ Œπ') :
  (linear_isometry_equiv.pi_Lp_congr_left p ùïú E e).symm
    = (linear_isometry_equiv.pi_Lp_congr_left p ùïú E e.symm) :=
linear_isometry_equiv.ext $ Œª x, rfl

@[simp] lemma _root_.linear_isometry_equiv.pi_Lp_congr_left_single
  [decidable_eq Œπ] [decidable_eq Œπ'] (e : Œπ ‚âÉ Œπ') (i : Œπ) (v : E) :
  linear_isometry_equiv.pi_Lp_congr_left p ùïú E e (pi.single i v) = pi.single (e i) v :=
begin
  funext x,
  simp [linear_isometry_equiv.pi_Lp_congr_left, linear_equiv.Pi_congr_left', equiv.Pi_congr_left',
    pi.single, function.update, equiv.symm_apply_eq],
end

@[simp] lemma equiv_zero : pi_Lp.equiv p Œ≤ 0 = 0 := rfl
@[simp] lemma equiv_symm_zero : (pi_Lp.equiv p Œ≤).symm 0 = 0 := rfl

@[simp] lemma equiv_add :
  pi_Lp.equiv p Œ≤ (x + y) = pi_Lp.equiv p Œ≤ x + pi_Lp.equiv p Œ≤ y := rfl
@[simp] lemma equiv_symm_add :
  (pi_Lp.equiv p Œ≤).symm (x' + y') = (pi_Lp.equiv p Œ≤).symm x' + (pi_Lp.equiv p Œ≤).symm y' := rfl

@[simp] lemma equiv_sub : pi_Lp.equiv p Œ≤ (x - y) = pi_Lp.equiv p Œ≤ x - pi_Lp.equiv p Œ≤ y := rfl
@[simp] lemma equiv_symm_sub :
  (pi_Lp.equiv p Œ≤).symm (x' - y') = (pi_Lp.equiv p Œ≤).symm x' - (pi_Lp.equiv p Œ≤).symm y' := rfl

@[simp] lemma equiv_neg : pi_Lp.equiv p Œ≤ (-x) = -pi_Lp.equiv p Œ≤ x := rfl
@[simp] lemma equiv_symm_neg : (pi_Lp.equiv p Œ≤).symm (-x') = -(pi_Lp.equiv p Œ≤).symm x' := rfl

@[simp] lemma equiv_smul : pi_Lp.equiv p Œ≤ (c ‚Ä¢ x) = c ‚Ä¢ pi_Lp.equiv p Œ≤ x := rfl
@[simp] lemma equiv_symm_smul :
  (pi_Lp.equiv p Œ≤).symm (c ‚Ä¢ x') = c ‚Ä¢ (pi_Lp.equiv p Œ≤).symm x' := rfl

-- this needs the `nonempty Œπ` hypothesis for the `p = ‚àû` case, but not for `1 ‚â§ p < ‚àû`
-- one way to fix this would be to make the right-hand side an `ite`
lemma nnnorm_equiv_symm_const {Œ≤} [seminormed_add_comm_group Œ≤] [nonempty Œπ] (b : Œ≤) :
  ‚à•(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm (function.const _ b)‚à•‚Çä=
  fintype.card Œπ ^ (1 / p).to_real * ‚à•b‚à•‚Çä :=
begin
  unfreezingI { rcases p.dichotomy with (rfl | hp) },
  { simp only [equiv_symm_apply', ennreal.div_top, ennreal.zero_to_real, nnreal.rpow_zero, one_mul,
      nnnorm_eq_csupr, function.const_apply, csupr_const], },
  { have ne_zero : p.to_real ‚â† 0 := (zero_lt_one.trans_le hp).ne',
    have ne_top : p ‚â† ‚àû, { intros h, rw [h, ennreal.top_to_real] at hp, linarith,},
    simp_rw [nnnorm_eq_sum ne_top, equiv_symm_apply, function.const_apply, finset.sum_const,
      finset.card_univ, nsmul_eq_mul, nnreal.mul_rpow, ‚Üênnreal.rpow_mul, mul_one_div_cancel ne_zero,
      nnreal.rpow_one, ennreal.to_real_div, ennreal.one_to_real], },
end

lemma norm_equiv_symm_const {Œ≤} [seminormed_add_comm_group Œ≤] [nonempty Œπ] (b : Œ≤) :
  ‚à•(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm (function.const _ b)‚à• =
  fintype.card Œπ ^ (1 / p).to_real * ‚à•b‚à• :=
(congr_arg coe $ nnnorm_equiv_symm_const b).trans $ by simp

lemma nnnorm_equiv_symm_one {Œ≤} [seminormed_add_comm_group Œ≤] [nonempty Œπ] [has_one Œ≤] :
  ‚à•(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm 1‚à•‚Çä = fintype.card Œπ ^ (1 / p).to_real * ‚à•(1 : Œ≤)‚à•‚Çä :=
(nnnorm_equiv_symm_const (1 : Œ≤)).trans rfl

lemma norm_equiv_symm_one {Œ≤} [seminormed_add_comm_group Œ≤] [nonempty Œπ] [has_one Œ≤] :
  ‚à•(pi_Lp.equiv p (Œª _ : Œπ, Œ≤)).symm 1‚à• = fintype.card Œπ ^ (1 / p).to_real * ‚à•(1 : Œ≤)‚à• :=
(norm_equiv_symm_const (1 : Œ≤)).trans rfl

variables (ùïú p)

/-- `pi_Lp.equiv` as a linear map. -/
@[simps {fully_applied := ff}]
protected def linear_equiv : pi_Lp p Œ≤ ‚âÉ‚Çó[ùïú] Œ† i, Œ≤ i :=
{ to_fun := pi_Lp.equiv _ _,
  inv_fun := (pi_Lp.equiv _ _).symm,
  ..linear_equiv.refl _ _}

end pi_Lp
