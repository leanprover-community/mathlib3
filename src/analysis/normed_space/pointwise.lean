/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Ya√´l Dillies
-/
import analysis.normed.group.pointwise
import analysis.normed.group.add_torsor
import analysis.normed_space.basic
import topology.metric_space.hausdorff_distance

/-!
# Properties of pointwise scalar multiplication of sets in normed spaces.

We explore the relationships between scalar multiplication of sets in vector spaces, and the norm.
Notably, we express arbitrary balls as rescaling of other balls, and we show that the
multiplication of bounded sets remain bounded.
-/

section move
section
variables {Œπ' : Sort*} {Œπ Œ± : Type*} [complete_lattice Œ±]

open set

lemma supr_Union (s : Œπ' ‚Üí set Œπ) (f : Œπ ‚Üí Œ±) : (‚®Ü i ‚àà (‚ãÉ j, s j), f i) = ‚®Ü j (i ‚àà s j), f i :=
by { rw supr_comm, simp_rw [mem_Union, supr_exists] }

lemma infi_Union (s : Œπ' ‚Üí set Œπ) (f : Œπ ‚Üí Œ±) : (‚®Ö i ‚àà (‚ãÉ j, s j), f i) = ‚®Ö j (i ‚àà s j), f i :=
by { rw infi_comm, simp_rw [mem_Union, infi_exists] }

end

namespace ennreal
open_locale ennreal
variables {a b c : ‚Ñù‚â•0‚àû} {p q : ‚Ñù}

protected lemma eq_sub_of_add_eq (hb : b ‚â† ‚àû) : a + b = c ‚Üí a = c - b :=
(cancel_of_ne hb).eq_tsub_of_add_eq

lemma le_sub_of_add_le_left (ha : a ‚â† ‚àû) : a + b ‚â§ c ‚Üí b ‚â§ c - a :=
(cancel_of_ne ha).le_tsub_of_add_le_left

lemma le_sub_of_add_le_right (hb : b ‚â† ‚àû) : a + b ‚â§ c ‚Üí a ‚â§ c - b :=
(cancel_of_ne hb).le_tsub_of_add_le_right

lemma of_real_sub (p : ‚Ñù) (hq : 0 ‚â§ q) :
  ennreal.of_real (p - q) = ennreal.of_real p - ennreal.of_real q :=
begin
  obtain h | h := le_total p q,
  { rw [of_real_of_nonpos (sub_nonpos_of_le h), tsub_eq_zero_of_le (of_real_le_of_real h)] },
  refine ennreal.eq_sub_of_add_eq of_real_ne_top _,
  rw [‚Üêof_real_add (sub_nonneg_of_le h) hq, sub_add_cancel],
end

end ennreal

open ennreal

@[simp] lemma edist_lt_of_real {Œ± : Type*} [pseudo_metric_space Œ±] {x y : Œ±} {r : ‚Ñù} :
  edist x y < ennreal.of_real r ‚Üî dist x y < r :=
begin
  obtain h | h := le_total r 0,
  { rw of_real_of_nonpos h,
    exact iff_of_false not_lt_zero (h.trans dist_nonneg).not_lt },
  { rw [of_real_eq_coe_nnreal h, edist_lt_coe, ‚Üêdist_lt_coe, subtype.coe_mk] }
end

namespace metric
variables {Œ± : Type*} [pseudo_emetric_space Œ±] {s t : set Œ±} {Œ¥ : ‚Ñù}

open emetric set

@[simp] lemma thickening_closure : thickening Œ¥ (closure s) = thickening Œ¥ s :=
by simp_rw [thickening, inf_edist_closure]

variables {Œπ : Sort*}

@[simp] lemma inf_edist_Union (f : Œπ ‚Üí set Œ±) (x : Œ±) :
  inf_edist x (‚ãÉ i, f i) = ‚®Ö i, inf_edist x (f i) :=
infi_Union f _

@[simp] lemma thickening_union (Œ¥ : ‚Ñù) (s t : set Œ±) :
  thickening Œ¥ (s ‚à™ t) = thickening Œ¥ s ‚à™ thickening Œ¥ t :=
by simp_rw [thickening, inf_edist_union, inf_eq_min, min_lt_iff, set_of_or]

@[simp] lemma cthickening_union (Œ¥ : ‚Ñù) (s t : set Œ±) :
  cthickening Œ¥ (s ‚à™ t) = cthickening Œ¥ s ‚à™ cthickening Œ¥ t :=
by simp_rw [cthickening, inf_edist_union, inf_eq_min, min_le_iff, set_of_or]

@[simp] lemma thickening_Union (Œ¥ : ‚Ñù) (f : Œπ ‚Üí set Œ±) :
  thickening Œ¥ (‚ãÉ i, f i) = ‚ãÉ i, thickening Œ¥ (f i) :=
by simp_rw [thickening, inf_edist_Union, infi_lt_iff, set_of_exists]

end metric
end move

open metric set
open_locale pointwise topological_space

variables {ùïú E : Type*} [normed_field ùïú]

section semi_normed_group
variables [semi_normed_group E] [normed_space ùïú E]

theorem smul_ball {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) :
  c ‚Ä¢ ball x r = ball (c ‚Ä¢ x) (‚à•c‚à• * r) :=
begin
  ext y,
  rw mem_smul_set_iff_inv_smul_mem‚ÇÄ hc,
  conv_lhs { rw ‚Üêinv_smul_smul‚ÇÄ hc x },
  simp [‚Üê div_eq_inv_mul, div_lt_iff (norm_pos_iff.2 hc), mul_comm _ r, dist_smul],
end

lemma smul_unit_ball {c : ùïú} (hc : c ‚â† 0) : c ‚Ä¢ ball (0 : E) (1 : ‚Ñù) = ball (0 : E) (‚à•c‚à•) :=
by rw [smul_ball hc, smul_zero, mul_one]

theorem smul_sphere' {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) :
  c ‚Ä¢ sphere x r = sphere (c ‚Ä¢ x) (‚à•c‚à• * r) :=
begin
  ext y,
  rw mem_smul_set_iff_inv_smul_mem‚ÇÄ hc,
  conv_lhs { rw ‚Üêinv_smul_smul‚ÇÄ hc x },
  simp only [mem_sphere, dist_smul, norm_inv, ‚Üê div_eq_inv_mul,
    div_eq_iff (norm_pos_iff.2 hc).ne', mul_comm r],
end

theorem smul_closed_ball' {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) :
  c ‚Ä¢ closed_ball x r = closed_ball (c ‚Ä¢ x) (‚à•c‚à• * r) :=
by simp only [‚Üê ball_union_sphere, set.smul_set_union, smul_ball hc, smul_sphere' hc]

lemma metric.bounded.smul {s : set E} (hs : bounded s) (c : ùïú) :
  bounded (c ‚Ä¢ s) :=
begin
  obtain ‚ü®R, hR‚ü© : ‚àÉ (R : ‚Ñù), ‚àÄ x ‚àà s, ‚à•x‚à• ‚â§ R := hs.exists_norm_le,
  refine (bounded_iff_exists_norm_le).2 ‚ü®‚à•c‚à• * R, _‚ü©,
  assume z hz,
  obtain ‚ü®y, ys, rfl‚ü© : ‚àÉ (y : E), y ‚àà s ‚àß c ‚Ä¢ y = z := mem_smul_set.1 hz,
  calc ‚à•c ‚Ä¢ y‚à• = ‚à•c‚à• * ‚à•y‚à• : norm_smul _ _
  ... ‚â§ ‚à•c‚à• * R : mul_le_mul_of_nonneg_left (hR y ys) (norm_nonneg _)
end

/-- If `s` is a bounded set, then for small enough `r`, the set `{x} + r ‚Ä¢ s` is contained in any
fixed neighborhood of `x`. -/
lemma eventually_singleton_add_smul_subset
  {x : E} {s : set E} (hs : bounded s) {u : set E} (hu : u ‚àà ùìù x) :
  ‚àÄ·∂† r in ùìù (0 : ùïú), {x} + r ‚Ä¢ s ‚äÜ u :=
begin
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ (hŒµ : 0 < Œµ), closed_ball x Œµ ‚äÜ u :=
    nhds_basis_closed_ball.mem_iff.1 hu,
  obtain ‚ü®R, Rpos, hR‚ü© : ‚àÉ (R : ‚Ñù), 0 < R ‚àß s ‚äÜ closed_ball 0 R := hs.subset_ball_lt 0 0,
  have : metric.closed_ball (0 : ùïú) (Œµ / R) ‚àà ùìù (0 : ùïú) :=
    closed_ball_mem_nhds _ (div_pos Œµpos Rpos),
  filter_upwards [this] with r hr,
  simp only [image_add_left, singleton_add],
  assume y hy,
  obtain ‚ü®z, zs, hz‚ü© : ‚àÉ (z : E), z ‚àà s ‚àß r ‚Ä¢ z = -x + y, by simpa [mem_smul_set] using hy,
  have I : ‚à•r ‚Ä¢ z‚à• ‚â§ Œµ := calc
    ‚à•r ‚Ä¢ z‚à• = ‚à•r‚à• * ‚à•z‚à• : norm_smul _ _
    ... ‚â§ (Œµ / R) * R :
      mul_le_mul (mem_closed_ball_zero_iff.1 hr)
        (mem_closed_ball_zero_iff.1 (hR zs)) (norm_nonneg _) (div_pos Œµpos Rpos).le
    ... = Œµ : by field_simp [Rpos.ne'],
  have : y = x + r ‚Ä¢ z, by simp only [hz, add_neg_cancel_left],
  apply hŒµ,
  simpa only [this, dist_eq_norm, add_sub_cancel', mem_closed_ball] using I,
end

/-- Any ball is the image of a ball centered at the origin under a shift. -/
lemma vadd_ball_zero (x : E) (r : ‚Ñù) : x +·µ• ball 0 r = ball x r :=
by rw [vadd_ball, vadd_eq_add, add_zero]

/-- Any closed ball is the image of a closed ball centered at the origin under a shift. -/
lemma vadd_closed_ball_zero (x : E) (r : ‚Ñù) : x +·µ• closed_ball 0 r = closed_ball x r :=
by rw [vadd_closed_ball, vadd_eq_add, add_zero]

variables [normed_space ‚Ñù E] {x y z : E} {Œ¥ Œµ : ‚Ñù}

/-- In a real normed space, the image of the unit ball under scalar multiplication by a positive
constant `r` is the ball of radius `r`. -/
lemma smul_unit_ball_of_pos {r : ‚Ñù} (hr : 0 < r) : r ‚Ä¢ ball 0 1 = ball (0 : E) r :=
by rw [smul_unit_ball hr.ne', real.norm_of_nonneg hr.le]

-- This is also true for `‚Ñö`-normed spaces
lemma exists_dist_eq (x z : E) {a b : ‚Ñù} (ha : 0 ‚â§ a) (hb : 0 ‚â§ b) (hab : a + b = 1) :
  ‚àÉ y, dist x y = b * dist x z ‚àß dist y z = a * dist x z :=
begin
  use a ‚Ä¢ x + b ‚Ä¢ z,
  nth_rewrite 0 [‚Üêone_smul ‚Ñù x],
  nth_rewrite 3 [‚Üêone_smul ‚Ñù z],
  simp [dist_eq_norm, ‚Üêhab, add_smul, ‚Üêsmul_sub, norm_smul_of_nonneg, ha, hb],
end

lemma exists_dist_le_le (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 ‚â§ Œµ) (h : dist x z ‚â§ Œµ + Œ¥) :
  ‚àÉ y, dist x y ‚â§ Œ¥ ‚àß dist y z ‚â§ Œµ :=
begin
  obtain rfl | hŒµ' := hŒµ.eq_or_lt,
  { exact ‚ü®z, by rwa zero_add at h, (dist_self _).le‚ü© },
  have hŒµŒ¥ := add_pos_of_pos_of_nonneg hŒµ' hŒ¥,
  refine (exists_dist_eq x z (div_nonneg hŒµ $ add_nonneg hŒµ hŒ¥) (div_nonneg hŒ¥ $ add_nonneg hŒµ hŒ¥) $
    by rw [‚Üêadd_div, div_self hŒµŒ¥.ne']).imp (Œª y hy, _),
  rw [hy.1, hy.2, div_mul_comm', div_mul_comm' Œµ],
  rw ‚Üêdiv_le_one hŒµŒ¥ at h,
  exact ‚ü®mul_le_of_le_one_left hŒ¥ h, mul_le_of_le_one_left hŒµ h‚ü©,
end

-- This is also true for `‚Ñö`-normed spaces
lemma exists_dist_le_lt (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 < Œµ) (h : dist x z < Œµ + Œ¥) :
  ‚àÉ y, dist x y ‚â§ Œ¥ ‚àß dist y z < Œµ :=
begin
  refine (exists_dist_eq x z (div_nonneg hŒµ.le $ add_nonneg hŒµ.le hŒ¥) (div_nonneg hŒ¥ $ add_nonneg
    hŒµ.le hŒ¥) $ by rw [‚Üêadd_div, div_self (add_pos_of_pos_of_nonneg hŒµ hŒ¥).ne']).imp (Œª y hy, _),
  rw [hy.1, hy.2, div_mul_comm', div_mul_comm' Œµ],
  rw ‚Üêdiv_lt_one (add_pos_of_pos_of_nonneg hŒµ hŒ¥) at h,
  exact ‚ü®mul_le_of_le_one_left hŒ¥ h.le, mul_lt_of_lt_one_left hŒµ h‚ü©,
end

-- This is also true for `‚Ñö`-normed spaces
lemma exists_dist_lt_le (hŒ¥ : 0 < Œ¥) (hŒµ : 0 ‚â§ Œµ) (h : dist x z < Œµ + Œ¥) :
  ‚àÉ y, dist x y < Œ¥ ‚àß dist y z ‚â§ Œµ :=
begin
  obtain ‚ü®y, yz, xy‚ü© := exists_dist_le_lt hŒµ hŒ¥
    (show dist z x < Œ¥ + Œµ, by simpa only [dist_comm, add_comm] using h),
  exact ‚ü®y, by simp [dist_comm x y, dist_comm y z, *]‚ü©,
end

-- This is also true for `‚Ñö`-normed spaces
lemma exists_dist_lt_lt (hŒ¥ : 0 < Œ¥) (hŒµ : 0 < Œµ) (h : dist x z < Œµ + Œ¥) :
  ‚àÉ y, dist x y < Œ¥ ‚àß dist y z < Œµ :=
begin
  refine (exists_dist_eq x z (div_nonneg hŒµ.le $ add_nonneg hŒµ.le hŒ¥.le) (div_nonneg hŒ¥.le $
    add_nonneg hŒµ.le hŒ¥.le) $ by rw [‚Üêadd_div, div_self (add_pos hŒµ hŒ¥).ne']).imp (Œª y hy, _),
  rw [hy.1, hy.2, div_mul_comm', div_mul_comm' Œµ],
  rw ‚Üêdiv_lt_one (add_pos hŒµ hŒ¥) at h,
  exact ‚ü®mul_lt_of_lt_one_left hŒ¥ h, mul_lt_of_lt_one_left hŒµ h‚ü©,
end

-- This is also true for `‚Ñö`-normed spaces
lemma disjoint_ball_ball_iff (hŒ¥ : 0 < Œ¥) (hŒµ : 0 < Œµ) :
  disjoint (ball x Œ¥) (ball y Œµ) ‚Üî Œ¥ + Œµ ‚â§ dist x y :=
begin
  refine ‚ü®Œª h, le_of_not_lt $ Œª hxy, _, ball_disjoint_ball‚ü©,
  rw add_comm at hxy,
  obtain ‚ü®z, hxz, hzy‚ü© := exists_dist_lt_lt hŒ¥ hŒµ hxy,
  rw dist_comm at hxz,
  exact h ‚ü®hxz, hzy‚ü©,
end

-- This is also true for `‚Ñö`-normed spaces
lemma disjoint_ball_closed_ball_iff (hŒ¥ : 0 < Œ¥) (hŒµ : 0 ‚â§ Œµ) :
  disjoint (ball x Œ¥) (closed_ball y Œµ) ‚Üî Œ¥ + Œµ ‚â§ dist x y :=
begin
  refine ‚ü®Œª h, le_of_not_lt $ Œª hxy, _, ball_disjoint_closed_ball‚ü©,
  rw add_comm at hxy,
  obtain ‚ü®z, hxz, hzy‚ü© := exists_dist_lt_le hŒ¥ hŒµ hxy,
  rw dist_comm at hxz,
  exact h ‚ü®hxz, hzy‚ü©,
end

-- This is also true for `‚Ñö`-normed spaces
lemma disjoint_closed_ball_ball_iff (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 < Œµ) :
  disjoint (closed_ball x Œ¥) (ball y Œµ) ‚Üî Œ¥ + Œµ ‚â§ dist x y :=
by rw [disjoint.comm, disjoint_ball_closed_ball_iff hŒµ hŒ¥, add_comm, dist_comm]; apply_instance

lemma disjoint_closed_ball_closed_ball_iff (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 ‚â§ Œµ) :
  disjoint (closed_ball x Œ¥) (closed_ball y Œµ) ‚Üî Œ¥ + Œµ < dist x y :=
begin
  refine ‚ü®Œª h, lt_of_not_ge $ Œª hxy, _, closed_ball_disjoint_closed_ball‚ü©,
  rw add_comm at hxy,
  obtain ‚ü®z, hxz, hzy‚ü© := exists_dist_le_le hŒ¥ hŒµ hxy,
  rw dist_comm at hxz,
  exact h ‚ü®hxz, hzy‚ü©,
end

open emetric ennreal

@[simp] lemma inf_edist_thickening (hŒ¥ : 0 < Œ¥) (s : set E) (x : E) :
  inf_edist x (thickening Œ¥ s) = inf_edist x s - ennreal.of_real Œ¥ :=
begin
  obtain hs | hs := lt_or_le (inf_edist x s) (ennreal.of_real Œ¥),
  { rw [inf_edist_zero_of_mem, tsub_eq_zero_of_le hs.le], exact hs },
  refine (tsub_le_iff_right.2 inf_edist_le_inf_edist_thickening_add).antisymm' _,
  refine le_sub_of_add_le_right of_real_ne_top _,
  refine le_inf_edist.2 (Œª z hz, le_of_forall_lt' $ Œª r h, _),
  cases r,
  { exact add_lt_top.2 ‚ü®lt_top_iff_ne_top.2 $ inf_edist_ne_top ‚ü®z, self_subset_thickening hŒ¥ _ hz‚ü©,
      of_real_lt_top‚ü© },
  have hr : 0 < ‚Üër - Œ¥,
  { refine sub_pos_of_lt _,
    have := hs.trans_lt ((inf_edist_le_edist_of_mem hz).trans_lt h),
    rw [of_real_eq_coe_nnreal hŒ¥.le, some_eq_coe] at this,
    exact_mod_cast this },
  rw [some_eq_coe, edist_lt_coe, ‚Üêdist_lt_coe, ‚Üêadd_sub_cancel'_right Œ¥ (‚Üër)] at h,
  obtain ‚ü®y, hxy, hyz‚ü© := exists_dist_lt_lt hr hŒ¥ h,
  refine (ennreal.add_lt_add_right of_real_ne_top $ inf_edist_lt_iff.2
    ‚ü®_, (mem_thickening_iff _ _).2 ‚ü®_, hz, hyz‚ü©, edist_lt_of_real.2 hxy‚ü©).trans_le _,
  rw [‚Üêof_real_add hr.le hŒ¥.le, sub_add_cancel, of_real_coe_nnreal],
  exact le_rfl,
end

@[simp] lemma inf_edist_cthickening (Œ¥ : ‚Ñù) (s : set E) (x : E) :
  inf_edist x (cthickening Œ¥ s) = inf_edist x s - ennreal.of_real Œ¥ :=
begin
  obtain hŒ¥ | hŒ¥ := le_total Œ¥ 0,
  { rw [cthickening_of_nonpos hŒ¥, inf_edist_closure, of_real_of_nonpos hŒ¥, tsub_zero] },
  obtain hs | hs := le_or_lt (inf_edist x s) (ennreal.of_real Œ¥),
  { rw [inf_edist_zero_of_mem (mem_cthickening_iff.2 hs), tsub_eq_zero_of_le hs] },
  refine (tsub_le_iff_right.2 inf_edist_le_inf_edist_cthickening_add).antisymm' _,
  refine le_sub_of_add_le_right of_real_ne_top _,
  refine le_inf_edist.2 (Œª z hz, le_of_forall_lt' $ Œª r h, _),
  cases r,
  { exact add_lt_top.2 ‚ü®lt_top_iff_ne_top.2 $ inf_edist_ne_top ‚ü®z, self_subset_cthickening _ hz‚ü©,
      of_real_lt_top‚ü© },
  have hr : 0 < ‚Üër - Œ¥,
  { refine sub_pos_of_lt _,
    have := hs.trans ((inf_edist_le_edist_of_mem hz).trans_lt h),
    rw [of_real_eq_coe_nnreal hŒ¥, some_eq_coe] at this,
    exact_mod_cast this },
  rw [some_eq_coe, edist_lt_coe, ‚Üêdist_lt_coe, ‚Üêadd_sub_cancel'_right Œ¥ (‚Üër)] at h,
  obtain ‚ü®y, hxy, hyz‚ü© := exists_dist_lt_le hr hŒ¥ h,
  refine (ennreal.add_lt_add_right of_real_ne_top $ inf_edist_lt_iff.2
    ‚ü®_, mem_cthickening_of_dist_le _ _ _ _ hz hyz, edist_lt_of_real.2 hxy‚ü©).trans_le _,
  rw [‚Üêof_real_add hr.le hŒ¥, sub_add_cancel, of_real_coe_nnreal],
  exact le_rfl,
end

@[simp] lemma thickening_thickening (hŒµ : 0 < Œµ) (hŒ¥ : 0 < Œ¥) (s : set E) :
  thickening Œµ (thickening Œ¥ s) = thickening (Œµ + Œ¥) s :=
(thickening_thickening_subset _ _ _).antisymm $ Œª x, begin
  simp_rw mem_thickening_iff,
  rintro ‚ü®z, hz, hxz‚ü©,
  rw add_comm at hxz,
  obtain ‚ü®y, hxy, hyz‚ü© := exists_dist_lt_lt hŒµ hŒ¥ hxz,
  exact ‚ü®y, ‚ü®_, hz, hyz‚ü©, hxy‚ü©,
end

@[simp] lemma thickening_cthickening (hŒµ : 0 < Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (s : set E) :
  thickening Œµ (cthickening Œ¥ s) = thickening (Œµ + Œ¥) s :=
(thickening_cthickening_subset _ hŒ¥ _).antisymm $ Œª x, begin
  simp_rw mem_thickening_iff,
  rintro ‚ü®z, hz, hxz‚ü©,
  rw add_comm at hxz,
  obtain ‚ü®y, hxy, hyz‚ü© := exists_dist_lt_le hŒµ hŒ¥ hxz,
  exact ‚ü®y, mem_cthickening_of_dist_le _ _ _ _ hz hyz, hxy‚ü©,
end

@[simp] lemma cthickening_thickening (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 < Œ¥) (s : set E) :
  cthickening Œµ (thickening Œ¥ s) = cthickening (Œµ + Œ¥) s :=
(cthickening_thickening_subset hŒµ _ _).antisymm $ Œª x, begin
  simp_rw [mem_cthickening_iff, ennreal.of_real_add hŒµ hŒ¥.le, inf_edist_thickening hŒ¥],
  exact tsub_le_iff_right.2,
end

@[simp] lemma cthickening_cthickening (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (s : set E) :
  cthickening Œµ (cthickening Œ¥ s) = cthickening (Œµ + Œ¥) s :=
(cthickening_cthickening_subset hŒµ hŒ¥ _).antisymm $ Œª x, begin
  simp_rw [mem_cthickening_iff, ennreal.of_real_add hŒµ hŒ¥, inf_edist_cthickening],
  exact tsub_le_iff_right.2,
end

end semi_normed_group

section normed_group
variables [normed_group E] [normed_space ùïú E]

theorem smul_closed_ball (c : ùïú) (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  c ‚Ä¢ closed_ball x r = closed_ball (c ‚Ä¢ x) (‚à•c‚à• * r) :=
begin
  rcases eq_or_ne c 0 with rfl|hc,
  { simp [hr, zero_smul_set, set.singleton_zero, ‚Üê nonempty_closed_ball] },
  { exact smul_closed_ball' hc x r }
end

lemma smul_closed_unit_ball (c : ùïú) : c ‚Ä¢ closed_ball (0 : E) (1 : ‚Ñù) = closed_ball (0 : E) (‚à•c‚à•) :=
by rw [smul_closed_ball _ _ zero_le_one, smul_zero, mul_one]

variables [normed_space ‚Ñù E]

/-- In a real normed space, the image of the unit closed ball under multiplication by a nonnegative
number `r` is the closed ball of radius `r` with center at the origin. -/
lemma smul_closed_unit_ball_of_nonneg {r : ‚Ñù} (hr : 0 ‚â§ r) :
  r ‚Ä¢ closed_ball 0 1 = closed_ball (0 : E) r :=
by rw [smul_closed_unit_ball, real.norm_of_nonneg hr]

/-- In a nontrivial real normed space, a sphere is nonempty if and only if its radius is
nonnegative. -/
@[simp] lemma normed_space.sphere_nonempty [nontrivial E] {x : E} {r : ‚Ñù} :
  (sphere x r).nonempty ‚Üî 0 ‚â§ r :=
begin
  obtain ‚ü®y, hy‚ü© := exists_ne x,
  refine ‚ü®Œª h, nonempty_closed_ball.1 (h.mono sphere_subset_closed_ball), Œª hr,
    ‚ü®r ‚Ä¢ ‚à•y - x‚à•‚Åª¬π ‚Ä¢ (y - x) + x, _‚ü©‚ü©,
  have : ‚à•y - x‚à• ‚â† 0, by simpa [sub_eq_zero],
  simp [norm_smul, this, real.norm_of_nonneg hr],
end

lemma smul_sphere [nontrivial E] (c : ùïú) (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
  c ‚Ä¢ sphere x r = sphere (c ‚Ä¢ x) (‚à•c‚à• * r) :=
begin
  rcases eq_or_ne c 0 with rfl|hc,
  { simp [zero_smul_set, set.singleton_zero, hr] },
  { exact smul_sphere' hc x r }
end

/-- Any ball `metric.ball x r`, `0 < r` is the image of the unit ball under `Œª y, x + r ‚Ä¢ y`. -/
lemma affinity_unit_ball {r : ‚Ñù} (hr : 0 < r) (x : E) : x +·µ• r ‚Ä¢ ball 0 1 = ball x r :=
by rw [smul_unit_ball_of_pos hr, vadd_ball_zero]

/-- Any closed ball `metric.closed_ball x r`, `0 ‚â§ r` is the image of the unit closed ball under
`Œª y, x + r ‚Ä¢ y`. -/
lemma affinity_unit_closed_ball {r : ‚Ñù} (hr : 0 ‚â§ r) (x : E) :
  x +·µ• r ‚Ä¢ closed_ball 0 1 = closed_ball x r :=
by rw [smul_closed_unit_ball, real.norm_of_nonneg hr, vadd_closed_ball_zero]

end normed_group
