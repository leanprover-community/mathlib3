/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, YaÃ«l Dillies
-/
import analysis.normed.group.pointwise
import analysis.normed.group.add_torsor
import analysis.normed_space.basic
import topology.metric_space.hausdorff_distance

/-!
# Properties of pointwise scalar multiplication of sets in normed spaces.

We explore the relationships between scalar multiplication of sets in vector spaces, and the norm.
Notably, we express arbitrary balls as rescaling of other balls, and we show that the
multiplication of bounded sets remain bounded.
-/

open metric set
open_locale pointwise topological_space

variables {ğ•œ E : Type*} [normed_field ğ•œ]

section semi_normed_group
variables [semi_normed_group E] [normed_space ğ•œ E]

theorem smul_ball {c : ğ•œ} (hc : c â‰  0) (x : E) (r : â„) :
  c â€¢ ball x r = ball (c â€¢ x) (âˆ¥câˆ¥ * r) :=
begin
  ext y,
  rw mem_smul_set_iff_inv_smul_memâ‚€ hc,
  conv_lhs { rw â†inv_smul_smulâ‚€ hc x },
  simp [â† div_eq_inv_mul, div_lt_iff (norm_pos_iff.2 hc), mul_comm _ r, dist_smul],
end

lemma smul_unit_ball {c : ğ•œ} (hc : c â‰  0) : c â€¢ ball (0 : E) (1 : â„) = ball (0 : E) (âˆ¥câˆ¥) :=
by rw [smul_ball hc, smul_zero, mul_one]

theorem smul_sphere' {c : ğ•œ} (hc : c â‰  0) (x : E) (r : â„) :
  c â€¢ sphere x r = sphere (c â€¢ x) (âˆ¥câˆ¥ * r) :=
begin
  ext y,
  rw mem_smul_set_iff_inv_smul_memâ‚€ hc,
  conv_lhs { rw â†inv_smul_smulâ‚€ hc x },
  simp only [mem_sphere, dist_smul, norm_inv, â† div_eq_inv_mul,
    div_eq_iff (norm_pos_iff.2 hc).ne', mul_comm r],
end

theorem smul_closed_ball' {c : ğ•œ} (hc : c â‰  0) (x : E) (r : â„) :
  c â€¢ closed_ball x r = closed_ball (c â€¢ x) (âˆ¥câˆ¥ * r) :=
by simp only [â† ball_union_sphere, set.smul_set_union, smul_ball hc, smul_sphere' hc]

lemma metric.bounded.smul {s : set E} (hs : bounded s) (c : ğ•œ) :
  bounded (c â€¢ s) :=
begin
  obtain âŸ¨R, hRâŸ© : âˆƒ (R : â„), âˆ€ x âˆˆ s, âˆ¥xâˆ¥ â‰¤ R := hs.exists_norm_le,
  refine (bounded_iff_exists_norm_le).2 âŸ¨âˆ¥câˆ¥ * R, _âŸ©,
  assume z hz,
  obtain âŸ¨y, ys, rflâŸ© : âˆƒ (y : E), y âˆˆ s âˆ§ c â€¢ y = z := mem_smul_set.1 hz,
  calc âˆ¥c â€¢ yâˆ¥ = âˆ¥câˆ¥ * âˆ¥yâˆ¥ : norm_smul _ _
  ... â‰¤ âˆ¥câˆ¥ * R : mul_le_mul_of_nonneg_left (hR y ys) (norm_nonneg _)
end

/-- If `s` is a bounded set, then for small enough `r`, the set `{x} + r â€¢ s` is contained in any
fixed neighborhood of `x`. -/
lemma eventually_singleton_add_smul_subset
  {x : E} {s : set E} (hs : bounded s) {u : set E} (hu : u âˆˆ ğ“ x) :
  âˆ€á¶  r in ğ“ (0 : ğ•œ), {x} + r â€¢ s âŠ† u :=
begin
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ Îµ (hÎµ : 0 < Îµ), closed_ball x Îµ âŠ† u :=
    nhds_basis_closed_ball.mem_iff.1 hu,
  obtain âŸ¨R, Rpos, hRâŸ© : âˆƒ (R : â„), 0 < R âˆ§ s âŠ† closed_ball 0 R := hs.subset_ball_lt 0 0,
  have : metric.closed_ball (0 : ğ•œ) (Îµ / R) âˆˆ ğ“ (0 : ğ•œ) :=
    closed_ball_mem_nhds _ (div_pos Îµpos Rpos),
  filter_upwards [this] with r hr,
  simp only [image_add_left, singleton_add],
  assume y hy,
  obtain âŸ¨z, zs, hzâŸ© : âˆƒ (z : E), z âˆˆ s âˆ§ r â€¢ z = -x + y, by simpa [mem_smul_set] using hy,
  have I : âˆ¥r â€¢ zâˆ¥ â‰¤ Îµ := calc
    âˆ¥r â€¢ zâˆ¥ = âˆ¥râˆ¥ * âˆ¥zâˆ¥ : norm_smul _ _
    ... â‰¤ (Îµ / R) * R :
      mul_le_mul (mem_closed_ball_zero_iff.1 hr)
        (mem_closed_ball_zero_iff.1 (hR zs)) (norm_nonneg _) (div_pos Îµpos Rpos).le
    ... = Îµ : by field_simp [Rpos.ne'],
  have : y = x + r â€¢ z, by simp only [hz, add_neg_cancel_left],
  apply hÎµ,
  simpa only [this, dist_eq_norm, add_sub_cancel', mem_closed_ball] using I,
end

/-- Any ball is the image of a ball centered at the origin under a shift. -/
lemma vadd_ball_zero (x : E) (r : â„) : x +áµ¥ ball 0 r = ball x r :=
by rw [vadd_ball, vadd_eq_add, add_zero]

/-- Any closed ball is the image of a closed ball centered at the origin under a shift. -/
lemma vadd_closed_ball_zero (x : E) (r : â„) : x +áµ¥ closed_ball 0 r = closed_ball x r :=
by rw [vadd_closed_ball, vadd_eq_add, add_zero]

variables [normed_space â„ E] {x y z : E} {Î´ Îµ : â„}

/-- In a real normed space, the image of the unit ball under scalar multiplication by a positive
constant `r` is the ball of radius `r`. -/
lemma smul_unit_ball_of_pos {r : â„} (hr : 0 < r) : r â€¢ ball 0 1 = ball (0 : E) r :=
by rw [smul_unit_ball hr.ne', real.norm_of_nonneg hr.le]

-- This is also true for `â„š`-normed spaces
lemma exists_dist_eq (x z : E) {a b : â„} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
  âˆƒ y, dist x y = b * dist x z âˆ§ dist y z = a * dist x z :=
begin
  use a â€¢ x + b â€¢ z,
  nth_rewrite 0 [â†one_smul â„ x],
  nth_rewrite 3 [â†one_smul â„ z],
  simp [dist_eq_norm, â†hab, add_smul, â†smul_sub, norm_smul_of_nonneg, ha, hb],
end

lemma exists_dist_le_le (hÎ´ : 0 â‰¤ Î´) (hÎµ : 0 â‰¤ Îµ) (h : dist x z â‰¤ Îµ + Î´) :
  âˆƒ y, dist x y â‰¤ Î´ âˆ§ dist y z â‰¤ Îµ :=
begin
  obtain rfl | hÎµ' := hÎµ.eq_or_lt,
  { exact âŸ¨z, by rwa zero_add at h, (dist_self _).leâŸ© },
  have hÎµÎ´ := add_pos_of_pos_of_nonneg hÎµ' hÎ´,
  refine (exists_dist_eq x z (div_nonneg hÎµ $ add_nonneg hÎµ hÎ´) (div_nonneg hÎ´ $ add_nonneg hÎµ hÎ´) $
    by rw [â†add_div, div_self hÎµÎ´.ne']).imp (Î» y hy, _),
  rw [hy.1, hy.2, div_mul_comm', div_mul_comm' Îµ],
  rw â†div_le_one hÎµÎ´ at h,
  exact âŸ¨mul_le_of_le_one_left hÎ´ h, mul_le_of_le_one_left hÎµ hâŸ©,
end

-- This is also true for `â„š`-normed spaces
lemma exists_dist_le_lt (hÎ´ : 0 â‰¤ Î´) (hÎµ : 0 < Îµ) (h : dist x z < Îµ + Î´) :
  âˆƒ y, dist x y â‰¤ Î´ âˆ§ dist y z < Îµ :=
begin
  refine (exists_dist_eq x z (div_nonneg hÎµ.le $ add_nonneg hÎµ.le hÎ´) (div_nonneg hÎ´ $ add_nonneg
    hÎµ.le hÎ´) $ by rw [â†add_div, div_self (add_pos_of_pos_of_nonneg hÎµ hÎ´).ne']).imp (Î» y hy, _),
  rw [hy.1, hy.2, div_mul_comm', div_mul_comm' Îµ],
  rw â†div_lt_one (add_pos_of_pos_of_nonneg hÎµ hÎ´) at h,
  exact âŸ¨mul_le_of_le_one_left hÎ´ h.le, mul_lt_of_lt_one_left hÎµ hâŸ©,
end

-- This is also true for `â„š`-normed spaces
lemma exists_dist_lt_le (hÎ´ : 0 < Î´) (hÎµ : 0 â‰¤ Îµ) (h : dist x z < Îµ + Î´) :
  âˆƒ y, dist x y < Î´ âˆ§ dist y z â‰¤ Îµ :=
begin
  obtain âŸ¨y, yz, xyâŸ© := exists_dist_le_lt hÎµ hÎ´
    (show dist z x < Î´ + Îµ, by simpa only [dist_comm, add_comm] using h),
  exact âŸ¨y, by simp [dist_comm x y, dist_comm y z, *]âŸ©,
end

-- This is also true for `â„š`-normed spaces
lemma exists_dist_lt_lt (hÎ´ : 0 < Î´) (hÎµ : 0 < Îµ) (h : dist x z < Îµ + Î´) :
  âˆƒ y, dist x y < Î´ âˆ§ dist y z < Îµ :=
begin
  refine (exists_dist_eq x z (div_nonneg hÎµ.le $ add_nonneg hÎµ.le hÎ´.le) (div_nonneg hÎ´.le $
    add_nonneg hÎµ.le hÎ´.le) $ by rw [â†add_div, div_self (add_pos hÎµ hÎ´).ne']).imp (Î» y hy, _),
  rw [hy.1, hy.2, div_mul_comm', div_mul_comm' Îµ],
  rw â†div_lt_one (add_pos hÎµ hÎ´) at h,
  exact âŸ¨mul_lt_of_lt_one_left hÎ´ h, mul_lt_of_lt_one_left hÎµ hâŸ©,
end

-- This is also true for `â„š`-normed spaces
lemma disjoint_ball_ball_iff (hÎ´ : 0 < Î´) (hÎµ : 0 < Îµ) :
  disjoint (ball x Î´) (ball y Îµ) â†” Î´ + Îµ â‰¤ dist x y :=
begin
  refine âŸ¨Î» h, le_of_not_lt $ Î» hxy, _, ball_disjoint_ballâŸ©,
  rw add_comm at hxy,
  obtain âŸ¨z, hxz, hzyâŸ© := exists_dist_lt_lt hÎ´ hÎµ hxy,
  rw dist_comm at hxz,
  exact h âŸ¨hxz, hzyâŸ©,
end

-- This is also true for `â„š`-normed spaces
lemma disjoint_ball_closed_ball_iff (hÎ´ : 0 < Î´) (hÎµ : 0 â‰¤ Îµ) :
  disjoint (ball x Î´) (closed_ball y Îµ) â†” Î´ + Îµ â‰¤ dist x y :=
begin
  refine âŸ¨Î» h, le_of_not_lt $ Î» hxy, _, ball_disjoint_closed_ballâŸ©,
  rw add_comm at hxy,
  obtain âŸ¨z, hxz, hzyâŸ© := exists_dist_lt_le hÎ´ hÎµ hxy,
  rw dist_comm at hxz,
  exact h âŸ¨hxz, hzyâŸ©,
end

-- This is also true for `â„š`-normed spaces
lemma disjoint_closed_ball_ball_iff (hÎ´ : 0 â‰¤ Î´) (hÎµ : 0 < Îµ) :
  disjoint (closed_ball x Î´) (ball y Îµ) â†” Î´ + Îµ â‰¤ dist x y :=
by rw [disjoint.comm, disjoint_ball_closed_ball_iff hÎµ hÎ´, add_comm, dist_comm]; apply_instance

lemma disjoint_closed_ball_closed_ball_iff (hÎ´ : 0 â‰¤ Î´) (hÎµ : 0 â‰¤ Îµ) :
  disjoint (closed_ball x Î´) (closed_ball y Îµ) â†” Î´ + Îµ < dist x y :=
begin
  refine âŸ¨Î» h, lt_of_not_ge $ Î» hxy, _, closed_ball_disjoint_closed_ballâŸ©,
  rw add_comm at hxy,
  obtain âŸ¨z, hxz, hzyâŸ© := exists_dist_le_le hÎ´ hÎµ hxy,
  rw dist_comm at hxz,
  exact h âŸ¨hxz, hzyâŸ©,
end

end semi_normed_group

section normed_group
variables [normed_group E] [normed_space ğ•œ E]

theorem smul_closed_ball (c : ğ•œ) (x : E) {r : â„} (hr : 0 â‰¤ r) :
  c â€¢ closed_ball x r = closed_ball (c â€¢ x) (âˆ¥câˆ¥ * r) :=
begin
  rcases eq_or_ne c 0 with rfl|hc,
  { simp [hr, zero_smul_set, set.singleton_zero, â† nonempty_closed_ball] },
  { exact smul_closed_ball' hc x r }
end

lemma smul_closed_unit_ball (c : ğ•œ) : c â€¢ closed_ball (0 : E) (1 : â„) = closed_ball (0 : E) (âˆ¥câˆ¥) :=
by rw [smul_closed_ball _ _ zero_le_one, smul_zero, mul_one]

variables [normed_space â„ E]

/-- In a real normed space, the image of the unit closed ball under multiplication by a nonnegative
number `r` is the closed ball of radius `r` with center at the origin. -/
lemma smul_closed_unit_ball_of_nonneg {r : â„} (hr : 0 â‰¤ r) :
  r â€¢ closed_ball 0 1 = closed_ball (0 : E) r :=
by rw [smul_closed_unit_ball, real.norm_of_nonneg hr]

/-- In a nontrivial real normed space, a sphere is nonempty if and only if its radius is
nonnegative. -/
@[simp] lemma normed_space.sphere_nonempty [nontrivial E] {x : E} {r : â„} :
  (sphere x r).nonempty â†” 0 â‰¤ r :=
begin
  obtain âŸ¨y, hyâŸ© := exists_ne x,
  refine âŸ¨Î» h, nonempty_closed_ball.1 (h.mono sphere_subset_closed_ball), Î» hr,
    âŸ¨r â€¢ âˆ¥y - xâˆ¥â»Â¹ â€¢ (y - x) + x, _âŸ©âŸ©,
  have : âˆ¥y - xâˆ¥ â‰  0, by simpa [sub_eq_zero],
  simp [norm_smul, this, real.norm_of_nonneg hr],
end

lemma smul_sphere [nontrivial E] (c : ğ•œ) (x : E) {r : â„} (hr : 0 â‰¤ r) :
  c â€¢ sphere x r = sphere (c â€¢ x) (âˆ¥câˆ¥ * r) :=
begin
  rcases eq_or_ne c 0 with rfl|hc,
  { simp [zero_smul_set, set.singleton_zero, hr] },
  { exact smul_sphere' hc x r }
end

/-- Any ball `metric.ball x r`, `0 < r` is the image of the unit ball under `Î» y, x + r â€¢ y`. -/
lemma affinity_unit_ball {r : â„} (hr : 0 < r) (x : E) : x +áµ¥ r â€¢ ball 0 1 = ball x r :=
by rw [smul_unit_ball_of_pos hr, vadd_ball_zero]

/-- Any closed ball `metric.closed_ball x r`, `0 â‰¤ r` is the image of the unit closed ball under
`Î» y, x + r â€¢ y`. -/
lemma affinity_unit_closed_ball {r : â„} (hr : 0 â‰¤ r) (x : E) :
  x +áµ¥ r â€¢ closed_ball 0 1 = closed_ball x r :=
by rw [smul_closed_unit_ball, real.norm_of_nonneg hr, vadd_closed_ball_zero]

end normed_group
