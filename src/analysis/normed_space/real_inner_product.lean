/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, S√©bastien Gou√´zel
-/
import algebra.quadratic_discriminant
import linear_algebra.bilinear_form
import tactic.apply_fun
import tactic.monotonicity
import topology.metric_space.pi_Lp

/-!
# Inner Product Space

This file defines real inner product space and proves its basic properties.

An inner product space is a vector space endowed with an inner product. It generalizes the notion of
dot product in `‚Ñù^n` and provides the means of defining the length of a vector and the angle between
two vectors. In particular vectors `x` and `y` are orthogonal if their inner product equals zero.

## Main statements

Existence of orthogonal projection onto nonempty complete subspace:
Let `u` be a point in an inner product space, and let `K` be a nonempty complete subspace.
Then there exists a unique `v` in `K` that minimizes the distance `‚à•u - v‚à•` to `u`.
The point `v` is usually called the orthogonal projection of `u` onto `K`.

## Implementation notes

We decide to develop the theory of real inner product spaces and that of complex inner product
spaces separately.

## Tags

inner product space, norm, orthogonal projection

## References
*  [Cl√©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
*  [Cl√©ment & Martin, *A Coq formal proof of the Lax‚ÄìMilgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/

noncomputable theory

open real set
open_locale big_operators
open_locale topological_space

universes u v w

variables {Œ± : Type u} {F : Type v} {G : Type w}

/-- Syntactic typeclass for types endowed with an inner product -/
class has_inner (Œ± : Type*) := (inner : Œ± ‚Üí Œ± ‚Üí ‚Ñù)

export has_inner (inner)

section prio

set_option default_priority 100 -- see Note [default priority]

-- the norm is embedded in the inner product space structure
-- to avoid definitional equality issues. See Note [forgetful inheritance].

/--
An inner product space is a real vector space with an additional operation called inner product.
Inner product spaces over complex vector space will be defined in another file.
The norm could be derived from the inner product, instead we require the existence of a norm and
the fact that it is equal to `sqrt (inner x x)` to be able to put instances on `‚Ñù` or product
spaces.

To construct a norm from an inner product, see `inner_product_space.of_core`.
-/
class inner_product_space (Œ± : Type*) extends normed_group Œ±, normed_space ‚Ñù Œ±, has_inner Œ± :=
(norm_eq_sqrt_inner : ‚àÄ (x : Œ±), ‚à•x‚à• = sqrt (inner x x))
(comm      : ‚àÄ x y, inner x y = inner y x)
(add_left  : ‚àÄ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : ‚àÄ x y r, inner (r ‚Ä¢ x) y = r * inner x y)
end prio

/-!
### Constructing a normed space structure from a scalar product

In the definition of an inner product space, we require the existence of a norm, which is equal
(but maybe not defeq) to the square root of the scalar product. This makes it possible to put
an inner product space structure on spaces with a preexisting norm (for instance `‚Ñù`), with good
properties. However, sometimes, one would like to define the norm starting only from a well-behaved
scalar product. This is what we implement in this paragraph, starting from a structure
`inner_product_space.core` stating that we have a nice scalar product.

Our goal here is not to develop a whole theory with all the supporting API, as this will be done
below for `inner_product_space`. Instead, we implement the bare minimum to go as directly as
possible to the construction of the norm and the proof of the triangular inequality.
-/

/-- A structure requiring that a scalar product is positive definite and symmetric, from which one
can construct an `inner_product_space` instance in `inner_product_space.of_core`. -/
@[nolint has_inhabited_instance]
structure inner_product_space.core
  (F : Type*) [add_comm_group F] [semimodule ‚Ñù F] :=
(inner     : F ‚Üí F ‚Üí ‚Ñù)
(comm      : ‚àÄ x y, inner x y = inner y x)
(nonneg    : ‚àÄ x, 0 ‚â§ inner x x)
(definite  : ‚àÄ x, inner x x = 0 ‚Üí x = 0)
(add_left  : ‚àÄ x y z, inner (x + y) z = inner x z + inner y z)
(smul_left : ‚àÄ x y r, inner (r ‚Ä¢ x) y = r * inner x y)

/- We set `inner_product_space.core` to be a class as we will use it as such in the construction
of the normed space structure that it produces. However, all the instances we will use will be
local to this proof. -/
attribute [class] inner_product_space.core

namespace inner_product_space.of_core

variables [add_comm_group F] [semimodule ‚Ñù F] [c : inner_product_space.core F]
include c

/-- Inner product constructed from an `inner_product_space.core` structure -/
def to_has_inner : has_inner F :=
{ inner := c.inner }

local attribute [instance] to_has_inner

lemma inner_comm (x y : F) : inner x y = inner y x := c.comm x y

lemma inner_add_left (x y z : F) : inner (x + y) z = inner x z + inner y z :=
c.add_left _ _ _

lemma inner_add_right (x y z : F) : inner x (y + z) = inner x y + inner x z :=
by { rw [inner_comm, inner_add_left], simp [inner_comm] }

lemma inner_smul_left (x y : F) (r : ‚Ñù) : inner (r ‚Ä¢ x) y = r * inner x y :=
c.smul_left _ _ _

lemma inner_smul_right (x y : F) (r : ‚Ñù) : inner x (r ‚Ä¢ y) = r * inner x y :=
by { rw [inner_comm, inner_smul_left, inner_comm] }

/-- Norm constructed from an `inner_product_space.core` structure, defined to be the square root
of the scalar product. -/
def to_has_norm : has_norm F :=
{ norm := Œª x, sqrt (inner x x) }

local attribute [instance] to_has_norm

lemma norm_eq_sqrt_inner (x : F) : ‚à•x‚à• = sqrt (inner x x) := rfl

lemma inner_self_eq_norm_square (x : F) : inner x x = ‚à•x‚à• * ‚à•x‚à• :=
(mul_self_sqrt (c.nonneg _)).symm

/-- Expand `inner (x + y) (x + y)` -/
lemma inner_add_add_self (x y : F) : inner (x + y) (x + y) = inner x x + 2 * inner x y + inner y y :=
by simpa [inner_add_left, inner_add_right, two_mul, add_assoc] using inner_comm _ _

/-- Cauchy‚ÄìSchwarz inequality -/
lemma inner_mul_inner_self_le (x y : F) : inner x y * inner x y ‚â§ inner x x * inner y y :=
begin
  have : ‚àÄ t, 0 ‚â§ inner y y * t * t + 2 * inner x y * t + inner x x, from
    assume t, calc
      0 ‚â§ inner (x+t‚Ä¢y) (x+t‚Ä¢y) : c.nonneg _
      ... = inner y y * t * t + 2 * inner x y * t + inner x x :
        by { simp only [inner_add_add_self, inner_smul_right, inner_smul_left], ring },
  have := discriminant_le_zero this, rw discrim at this,
  have h : (2 * inner x y)^2 - 4 * inner y y * inner x x =
                      4 * (inner x y * inner x y - inner x x * inner y y) := by ring,
  rw h at this,
  linarith
end

/-- Cauchy‚ÄìSchwarz inequality with norm -/
lemma abs_inner_le_norm (x y : F) : abs (inner x y) ‚â§ ‚à•x‚à• * ‚à•y‚à• :=
nonneg_le_nonneg_of_squares_le (mul_nonneg (sqrt_nonneg _) (sqrt_nonneg _))
begin
  rw abs_mul_abs_self,
  have : ‚à•x‚à• * ‚à•y‚à• * (‚à•x‚à• * ‚à•y‚à•) = inner x x * inner y y,
    simp only [inner_self_eq_norm_square], ring,
  rw this,
  exact inner_mul_inner_self_le _ _
end

/-- Normed group structure constructed from an `inner_product_space.core` structure. -/
def to_normed_group : normed_group F :=
normed_group.of_core F
{ norm_eq_zero_iff := assume x,
    begin
      split,
      { assume h,
        rw [norm_eq_sqrt_inner,  sqrt_eq_zero] at h,
        { exact c.definite x h },
        { exact c.nonneg x } },
      { rintros rfl,
        have : inner ((0 : ‚Ñù) ‚Ä¢ (0 : F)) 0 = 0 * inner (0 : F) 0 := inner_smul_left _ _ _,
        simp at this,
        simp [norm, this] }
    end,
  norm_neg := assume x,
    begin
      have A : (- (1 : ‚Ñù)) ‚Ä¢ x = -x, by simp,
      rw [norm_eq_sqrt_inner, norm_eq_sqrt_inner, ‚Üê A, inner_smul_left, inner_smul_right],
      simp,
    end,
  triangle := assume x y,
  begin
    have := calc
      ‚à•x + y‚à• * ‚à•x + y‚à• = inner (x + y) (x + y) : by rw inner_self_eq_norm_square
      ... = inner x x + 2 * inner x y + inner y y : inner_add_add_self _ _
      ... ‚â§ inner x x + 2 * (‚à•x‚à• * ‚à•y‚à•) + inner y y :
        by linarith [abs_inner_le_norm x y, le_abs_self (inner x y)]
      ... = (‚à•x‚à• + ‚à•y‚à•) * (‚à•x‚à• + ‚à•y‚à•) : by { simp only [inner_self_eq_norm_square], ring },
    exact nonneg_le_nonneg_of_squares_le (add_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) this
  end }

local attribute [instance] to_normed_group

/-- Normed space structure constructed from an `inner_product_space.core` structure. -/
def to_normed_space : normed_space ‚Ñù F :=
{ norm_smul_le := assume r x, le_of_eq $
  begin
    have A : 0 ‚â§ ‚à•r‚à• * ‚à•x‚à• := mul_nonneg (abs_nonneg _) (sqrt_nonneg _),
    rw [norm_eq_sqrt_inner, sqrt_eq_iff_mul_self_eq _ A,
        inner_smul_left, inner_smul_right, inner_self_eq_norm_square],
    { calc
        abs(r) * ‚à•x‚à• * (abs(r) * ‚à•x‚à•) = (abs(r) * abs(r)) * (‚à•x‚à• * ‚à•x‚à•) : by ring
        ... = r * (r * (‚à•x‚à• * ‚à•x‚à•)) : by { rw abs_mul_abs_self, ring } },
    { exact c.nonneg _ }
  end }

end inner_product_space.of_core

/-- Given an `inner_product_space.core` structure on a space, one can use it to turn the space into
an inner product space, constructing the norm out of the inner product. -/
def inner_product_space.of_core [add_comm_group F] [semimodule ‚Ñù F]
  (c : inner_product_space.core F) : inner_product_space F :=
begin
  letI : normed_group F := @inner_product_space.of_core.to_normed_group F _ _ c,
  letI : normed_space ‚Ñù F := @inner_product_space.of_core.to_normed_space F _ _ c,
  exact { norm_eq_sqrt_inner := Œª x, rfl, .. c }
end

/-! ### Properties of inner product spaces -/

variables [inner_product_space Œ±]

export inner_product_space (norm_eq_sqrt_inner)

section basic_properties

lemma inner_comm (x y : Œ±) : inner x y = inner y x := inner_product_space.comm x y

lemma inner_add_left {x y z : Œ±} : inner (x + y) z = inner x z + inner y z :=
inner_product_space.add_left _ _ _

lemma inner_add_right {x y z : Œ±} : inner x (y + z) = inner x y + inner x z :=
by { rw [inner_comm, inner_add_left], simp [inner_comm] }

lemma inner_smul_left {x y : Œ±} {r : ‚Ñù} : inner (r ‚Ä¢ x) y = r * inner x y :=
inner_product_space.smul_left _ _ _

lemma inner_smul_right {x y : Œ±} {r : ‚Ñù} : inner x (r ‚Ä¢ y) = r * inner x y :=
by { rw [inner_comm, inner_smul_left, inner_comm] }

variables (Œ±)

/-- The inner product as a bilinear form. -/
def bilin_form_of_inner : bilin_form ‚Ñù Œ± :=
{ bilin := inner,
  bilin_add_left := Œª x y z, inner_add_left,
  bilin_smul_left := Œª a x y, inner_smul_left,
  bilin_add_right := Œª x y z, inner_add_right,
  bilin_smul_right := Œª a x y, inner_smul_right }

variables {Œ±}

/-- An inner product with a sum on the left. -/
lemma sum_inner {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí Œ±) (x : Œ±) :
  inner (‚àë i in s, f i) x = ‚àë i in s, inner (f i) x :=
bilin_form.map_sum_left (bilin_form_of_inner Œ±) _ _ _

/-- An inner product with a sum on the right. -/
lemma inner_sum {Œπ : Type*} (s : finset Œπ) (f : Œπ ‚Üí Œ±) (x : Œ±) :
  inner x (‚àë i in s, f i) = ‚àë i in s, inner x (f i) :=
bilin_form.map_sum_right (bilin_form_of_inner Œ±) _ _ _

@[simp] lemma inner_zero_left {x : Œ±} : inner 0 x = 0 :=
by { rw [‚Üê zero_smul ‚Ñù (0:Œ±), inner_smul_left, zero_mul] }

@[simp] lemma inner_zero_right {x : Œ±} : inner x 0 = 0 :=
by { rw [inner_comm, inner_zero_left] }

@[simp] lemma inner_self_eq_zero {x : Œ±} : inner x x = 0 ‚Üî x = 0 :=
‚ü®Œª h, by simpa [h] using norm_eq_sqrt_inner x, Œª h, by simp [h]‚ü©

lemma inner_self_nonneg {x : Œ±} : 0 ‚â§ inner x x :=
begin
  classical,
  by_cases h : x = 0,
  { simp [h] },
  { have : 0 < sqrt (inner x x),
    { rw ‚Üê norm_eq_sqrt_inner,
      exact norm_pos_iff.mpr h },
    exact le_of_lt (sqrt_pos.1 this) }
end

@[simp] lemma inner_self_nonpos {x : Œ±} : inner x x ‚â§ 0 ‚Üî x = 0 :=
‚ü®Œª h, inner_self_eq_zero.1 (le_antisymm h inner_self_nonneg),
  Œª h, h.symm ‚ñ∏ le_of_eq inner_zero_left‚ü©

@[simp] lemma inner_neg_left {x y : Œ±} : inner (-x) y = -inner x y :=
by { rw [‚Üê neg_one_smul ‚Ñù x, inner_smul_left], simp }

@[simp] lemma inner_neg_right {x y : Œ±} : inner x (-y) = -inner x y :=
by { rw [inner_comm, inner_neg_left, inner_comm] }

@[simp] lemma inner_neg_neg {x y : Œ±} : inner (-x) (-y) = inner x y := by simp

lemma inner_sub_left {x y z : Œ±} : inner (x - y) z = inner x z - inner y z :=
by { simp [sub_eq_add_neg, inner_add_left] }

lemma inner_sub_right {x y z : Œ±} : inner x (y - z) = inner x y - inner x z :=
by { simp [sub_eq_add_neg, inner_add_right] }

/-- Expand `inner (x + y) (x + y)` -/
lemma inner_add_add_self {x y : Œ±} : inner (x + y) (x + y) = inner x x + 2 * inner x y + inner y y :=
by simpa [inner_add_left, inner_add_right, two_mul, add_assoc] using inner_comm _ _

/-- Expand `inner (x - y) (x - y)` -/
lemma inner_sub_sub_self {x y : Œ±} : inner (x - y) (x - y) = inner x x - 2 * inner x y + inner y y :=
begin
  simp only [inner_sub_left, inner_sub_right, two_mul],
  simpa [sub_eq_add_neg, add_comm, add_left_comm] using inner_comm _ _
end

/-- Parallelogram law -/
lemma parallelogram_law {x y : Œ±} :
  inner (x + y) (x + y) + inner (x - y) (x - y) = 2 * (inner x x + inner y y) :=
by simp [inner_add_add_self, inner_sub_sub_self, two_mul, sub_eq_add_neg, add_comm, add_left_comm]

/-- Cauchy‚ÄìSchwarz inequality -/
lemma inner_mul_inner_self_le (x y : Œ±) : inner x y * inner x y ‚â§ inner x x * inner y y :=
begin
  have : ‚àÄ t, 0 ‚â§ inner y y * t * t + 2 * inner x y * t + inner x x, from
    assume t, calc
      0 ‚â§ inner (x+t‚Ä¢y) (x+t‚Ä¢y) : inner_self_nonneg
      ... = inner y y * t * t + 2 * inner x y * t + inner x x :
        by { simp only [inner_add_add_self, inner_smul_right, inner_smul_left], ring },
  have := discriminant_le_zero this, rw discrim at this,
  have h : (2 * inner x y)^2 - 4 * inner y y * inner x x =
                      4 * (inner x y * inner x y - inner x x * inner y y) := by ring,
  rw h at this,
  linarith
end

end basic_properties

section norm

lemma inner_self_eq_norm_square (x : Œ±) : inner x x = ‚à•x‚à• * ‚à•x‚à• :=
by { rw norm_eq_sqrt_inner, exact (mul_self_sqrt inner_self_nonneg).symm }

/-- Expand the square -/
lemma norm_add_pow_two {x y : Œ±} : ‚à•x + y‚à•^2 = ‚à•x‚à•^2 + 2 * inner x y + ‚à•y‚à•^2 :=
by { repeat {rw [pow_two, ‚Üê inner_self_eq_norm_square]}, exact inner_add_add_self }

/-- Same lemma as above but in a different form -/
lemma norm_add_mul_self {x y : Œ±} : ‚à•x + y‚à• * ‚à•x + y‚à• = ‚à•x‚à• * ‚à•x‚à• + 2 * inner x y + ‚à•y‚à• * ‚à•y‚à• :=
by { repeat {rw [‚Üê pow_two]}, exact norm_add_pow_two }

/-- Expand the square -/
lemma norm_sub_pow_two {x y : Œ±} : ‚à•x - y‚à•^2 = ‚à•x‚à•^2 - 2 * inner x y + ‚à•y‚à•^2 :=
by { repeat {rw [pow_two, ‚Üê inner_self_eq_norm_square]}, exact inner_sub_sub_self }

/-- Same lemma as above but in a different form -/
lemma norm_sub_mul_self {x y : Œ±} : ‚à•x - y‚à• * ‚à•x - y‚à• = ‚à•x‚à• * ‚à•x‚à• - 2 * inner x y + ‚à•y‚à• * ‚à•y‚à• :=
by { repeat {rw [‚Üê pow_two]}, exact norm_sub_pow_two }

/-- Cauchy‚ÄìSchwarz inequality with norm -/
lemma abs_inner_le_norm (x y : Œ±) : abs (inner x y) ‚â§ ‚à•x‚à• * ‚à•y‚à• :=
nonneg_le_nonneg_of_squares_le (mul_nonneg (norm_nonneg _) (norm_nonneg _))
begin
  rw abs_mul_abs_self,
  have : ‚à•x‚à• * ‚à•y‚à• * (‚à•x‚à• * ‚à•y‚à•) = inner x x * inner y y,
    simp only [inner_self_eq_norm_square], ring,
  rw this,
  exact inner_mul_inner_self_le _ _
end

lemma parallelogram_law_with_norm {x y : Œ±} :
  ‚à•x + y‚à• * ‚à•x + y‚à• + ‚à•x - y‚à• * ‚à•x - y‚à• = 2 * (‚à•x‚à• * ‚à•x‚à• + ‚à•y‚à• * ‚à•y‚à•) :=
by { simp only [(inner_self_eq_norm_square _).symm], exact parallelogram_law }

/-- The inner product, in terms of the norm. -/
lemma inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two (x y : Œ±) :
  inner x y = (‚à•x + y‚à• * ‚à•x + y‚à• - ‚à•x‚à• * ‚à•x‚à• - ‚à•y‚à• * ‚à•y‚à•) / 2 :=
begin
  rw norm_add_mul_self,
  ring
end

/-- The inner product, in terms of the norm. -/
lemma inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two (x y : Œ±) :
  inner x y = (‚à•x‚à• * ‚à•x‚à• + ‚à•y‚à• * ‚à•y‚à• - ‚à•x - y‚à• * ‚à•x - y‚à•) / 2 :=
begin
  rw norm_sub_mul_self,
  ring
end

/-- The inner product, in terms of the norm. -/
lemma inner_eq_norm_add_mul_self_sub_norm_sub_mul_self_div_four (x y : Œ±) :
  inner x y = (‚à•x + y‚à• * ‚à•x + y‚à• - ‚à•x - y‚à• * ‚à•x - y‚à•) / 4 :=
begin
  rw [norm_add_mul_self, norm_sub_mul_self],
  ring
end

/-- Pythagorean theorem, if-and-only-if vector inner product form. -/
lemma norm_add_square_eq_norm_square_add_norm_square_iff_inner_eq_zero (x y : Œ±) :
  ‚à•x + y‚à• * ‚à•x + y‚à• = ‚à•x‚à• * ‚à•x‚à• + ‚à•y‚à• * ‚à•y‚à• ‚Üî inner x y = 0 :=
begin
  rw [norm_add_mul_self, add_right_cancel_iff, add_right_eq_self, mul_eq_zero],
  norm_num
end

/-- Pythagorean theorem, vector inner product form. -/
lemma norm_add_square_eq_norm_square_add_norm_square {x y : Œ±} (h : inner x y = 0) :
  ‚à•x + y‚à• * ‚à•x + y‚à• = ‚à•x‚à• * ‚à•x‚à• + ‚à•y‚à• * ‚à•y‚à• :=
(norm_add_square_eq_norm_square_add_norm_square_iff_inner_eq_zero x y).2 h

/-- Pythagorean theorem, subtracting vectors, if-and-only-if vector
inner product form. -/
lemma norm_sub_square_eq_norm_square_add_norm_square_iff_inner_eq_zero (x y : Œ±) :
  ‚à•x - y‚à• * ‚à•x - y‚à• = ‚à•x‚à• * ‚à•x‚à• + ‚à•y‚à• * ‚à•y‚à• ‚Üî inner x y = 0 :=
begin
  rw [norm_sub_mul_self, add_right_cancel_iff, sub_eq_add_neg, add_right_eq_self, neg_eq_zero,
      mul_eq_zero],
  norm_num
end

/-- Pythagorean theorem, subtracting vectors, vector inner product
form. -/
lemma norm_sub_square_eq_norm_square_add_norm_square {x y : Œ±} (h : inner x y = 0) :
  ‚à•x - y‚à• * ‚à•x - y‚à• = ‚à•x‚à• * ‚à•x‚à• + ‚à•y‚à• * ‚à•y‚à• :=
(norm_sub_square_eq_norm_square_add_norm_square_iff_inner_eq_zero x y).2 h

/-- The inner product of two vectors, divided by the product of their
norms, has absolute value at most 1. -/
lemma abs_inner_div_norm_mul_norm_le_one (x y : Œ±) : abs (inner x y / (‚à•x‚à• * ‚à•y‚à•)) ‚â§ 1 :=
begin
  rw abs_div,
  by_cases h : 0 = abs (‚à•x‚à• * ‚à•y‚à•),
  { rw [‚Üêh, div_zero],
    norm_num },
  { apply div_le_of_le_mul (lt_of_le_of_ne (ge_iff_le.mp (abs_nonneg (‚à•x‚à• * ‚à•y‚à•))) h),
    convert abs_inner_le_norm x y using 1,
    rw [abs_mul, abs_of_nonneg (norm_nonneg x), abs_of_nonneg (norm_nonneg y), mul_one] }
end

/-- The inner product of a vector with a multiple of itself. -/
lemma inner_smul_self_left (x : Œ±) (r : ‚Ñù) : inner (r ‚Ä¢ x) x = r * (‚à•x‚à• * ‚à•x‚à•) :=
by rw [inner_smul_left, ‚Üêinner_self_eq_norm_square]

/-- The inner product of a vector with a multiple of itself. -/
lemma inner_smul_self_right (x : Œ±) (r : ‚Ñù) : inner x (r ‚Ä¢ x) = r * (‚à•x‚à• * ‚à•x‚à•) :=
by rw [inner_smul_right, ‚Üêinner_self_eq_norm_square]

/-- The inner product of a nonzero vector with a nonzero multiple of
itself, divided by the product of their norms, has absolute value
1. -/
lemma abs_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul
  {x : Œ±} {r : ‚Ñù} (hx : x ‚â† 0) (hr : r ‚â† 0) : abs (inner x (r ‚Ä¢ x) / (‚à•x‚à• * ‚à•r ‚Ä¢ x‚à•)) = 1 :=
begin
  rw [inner_smul_self_right, norm_smul, real.norm_eq_abs, ‚Üêmul_assoc ‚à•x‚à•, mul_comm _ (abs r),
      mul_assoc, abs_div, abs_mul r, abs_mul (abs r), abs_abs, div_self],
  exact mul_ne_zero (Œª h, hr (eq_zero_of_abs_eq_zero h))
    (Œª h, hx (norm_eq_zero.1 (eq_zero_of_mul_self_eq_zero (eq_zero_of_abs_eq_zero h))))
end

/-- The inner product of a nonzero vector with a positive multiple of
itself, divided by the product of their norms, has value 1. -/
lemma inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul
  {x : Œ±} {r : ‚Ñù} (hx : x ‚â† 0) (hr : 0 < r) : inner x (r ‚Ä¢ x) / (‚à•x‚à• * ‚à•r ‚Ä¢ x‚à•) = 1 :=
begin
  rw [inner_smul_self_right, norm_smul, real.norm_eq_abs, ‚Üêmul_assoc ‚à•x‚à•, mul_comm _ (abs r),
      mul_assoc, abs_of_nonneg (le_of_lt hr), div_self],
  exact mul_ne_zero (ne_of_gt hr)
    (Œª h, hx (norm_eq_zero.1 (eq_zero_of_mul_self_eq_zero h)))
end

/-- The inner product of a nonzero vector with a negative multiple of
itself, divided by the product of their norms, has value -1. -/
lemma inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul
  {x : Œ±} {r : ‚Ñù} (hx : x ‚â† 0) (hr : r < 0) : inner x (r ‚Ä¢ x) / (‚à•x‚à• * ‚à•r ‚Ä¢ x‚à•) = -1 :=
begin
  rw [inner_smul_self_right, norm_smul, real.norm_eq_abs, ‚Üêmul_assoc ‚à•x‚à•, mul_comm _ (abs r),
      mul_assoc, abs_of_neg hr, ‚Üêneg_mul_eq_neg_mul, div_neg_eq_neg_div, div_self],
  exact mul_ne_zero (ne_of_lt hr)
    (Œª h, hx (norm_eq_zero.1 (eq_zero_of_mul_self_eq_zero h)))
end

/-- The inner product of two vectors, divided by the product of their
norms, has absolute value 1 if and only if they are nonzero and one is
a multiple of the other. One form of equality case for Cauchy-Schwarz. -/
lemma abs_inner_div_norm_mul_norm_eq_one_iff (x y : Œ±) :
  abs (inner x y / (‚à•x‚à• * ‚à•y‚à•)) = 1 ‚Üî (x ‚â† 0 ‚àß ‚àÉ (r : ‚Ñù), r ‚â† 0 ‚àß y = r ‚Ä¢ x) :=
begin
  split,
  { intro h,
    have hx0 : x ‚â† 0,
    { intro hx0,
      rw [hx0, inner_zero_left, zero_div] at h,
      norm_num at h,
      exact h },
    refine and.intro hx0 _,
    set r := inner x y / (‚à•x‚à• * ‚à•x‚à•) with hr,
    use r,
    set t := y - r ‚Ä¢ x with ht,
    have ht0 : inner x t = 0,
    { rw [ht, inner_sub_right, inner_smul_right, hr, ‚Üêinner_self_eq_norm_square,
          div_mul_cancel _ (Œª h, hx0 (inner_self_eq_zero.1 h)), sub_self] },
    rw [‚Üêsub_add_cancel y (r ‚Ä¢ x), ‚Üêht, inner_add_right, ht0, zero_add, inner_smul_right,
        inner_self_eq_norm_square, ‚Üêmul_assoc, mul_comm,
        mul_div_mul_left _ _ (Œª h, hx0 (norm_eq_zero.1 h)), abs_div, abs_mul,
        abs_of_nonneg (norm_nonneg _), abs_of_nonneg (norm_nonneg _), ‚Üêreal.norm_eq_abs,
        ‚Üênorm_smul] at h,
    have hr0 : r ‚â† 0,
    { intro hr0,
      rw [hr0, zero_smul, norm_zero, zero_div] at h,
      norm_num at h },
    refine and.intro hr0 _,
    have h2 : ‚à•r ‚Ä¢ x‚à• ^ 2 = ‚à•t + r ‚Ä¢ x‚à• ^ 2,
    { congr' 1,
      refine eq_of_div_eq_one _ _ h,
      intro h0,
      rw [h0, div_zero] at h,
      norm_num at h },
    rw [pow_two, pow_two, ‚Üêinner_self_eq_norm_square, ‚Üêinner_self_eq_norm_square,
        inner_add_add_self] at h2,
    conv_rhs at h2 {
      congr,
      congr,
      skip,
      rw [inner_smul_right, inner_comm, ht0, mul_zero, mul_zero]
    },
    symmetry' at h2,
    rw [add_zero, add_left_eq_self, inner_self_eq_zero] at h2,
    rw h2 at ht,
    exact eq_of_sub_eq_zero ht.symm },
  { intro h,
    rcases h with ‚ü®hx, ‚ü®r, ‚ü®hr, hy‚ü©‚ü©‚ü©,
    rw hy,
    exact abs_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul hx hr }
end

/-- The inner product of two vectors, divided by the product of their
norms, has value 1 if and only if they are nonzero and one is
a positive multiple of the other. -/
lemma inner_div_norm_mul_norm_eq_one_iff (x y : Œ±) :
  inner x y / (‚à•x‚à• * ‚à•y‚à•) = 1 ‚Üî (x ‚â† 0 ‚àß ‚àÉ (r : ‚Ñù), 0 < r ‚àß y = r ‚Ä¢ x) :=
begin
  split,
  { intro h,
    have ha := h,
    apply_fun abs at ha,
    norm_num at ha,
    rcases (abs_inner_div_norm_mul_norm_eq_one_iff x y).1 ha with ‚ü®hx, ‚ü®r, ‚ü®hr, hy‚ü©‚ü©‚ü©,
    use [hx, r],
    refine and.intro _ hy,
    by_contradiction hrneg,
    rw hy at h,
    rw inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul hx
      (lt_of_le_of_ne' (le_of_not_lt hrneg) hr) at h,
    norm_num at h },
  { intro h,
    rcases h with ‚ü®hx, ‚ü®r, ‚ü®hr, hy‚ü©‚ü©‚ü©,
    rw hy,
    exact inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul hx hr }
end

/-- The inner product of two vectors, divided by the product of their
norms, has value -1 if and only if they are nonzero and one is
a negative multiple of the other. -/
lemma inner_div_norm_mul_norm_eq_neg_one_iff (x y : Œ±) :
  inner x y / (‚à•x‚à• * ‚à•y‚à•) = -1 ‚Üî (x ‚â† 0 ‚àß ‚àÉ (r : ‚Ñù), r < 0 ‚àß y = r ‚Ä¢ x) :=
begin
  split,
  { intro h,
    have ha := h,
    apply_fun abs at ha,
    norm_num at ha,
    rcases (abs_inner_div_norm_mul_norm_eq_one_iff x y).1 ha with ‚ü®hx, ‚ü®r, ‚ü®hr, hy‚ü©‚ü©‚ü©,
    use [hx, r],
    refine and.intro _ hy,
    by_contradiction hrpos,
    rw hy at h,
    rw inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul hx
      (lt_of_le_of_ne' (le_of_not_lt hrpos) hr.symm) at h,
    norm_num at h },
  { intro h,
    rcases h with ‚ü®hx, ‚ü®r, ‚ü®hr, hy‚ü©‚ü©‚ü©,
    rw hy,
    exact inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul hx hr }
end

/-- The inner product of two weighted sums, where the weights in each
sum add to 0, in terms of the norms of pairwise differences. -/
lemma inner_sum_smul_sum_smul_of_sum_eq_zero {Œπ‚ÇÅ : Type*} {s‚ÇÅ : finset Œπ‚ÇÅ} {w‚ÇÅ : Œπ‚ÇÅ ‚Üí ‚Ñù}
    (v‚ÇÅ : Œπ‚ÇÅ ‚Üí Œ±) (h‚ÇÅ : ‚àë i in s‚ÇÅ, w‚ÇÅ i = 0) {Œπ‚ÇÇ : Type*} {s‚ÇÇ : finset Œπ‚ÇÇ} {w‚ÇÇ : Œπ‚ÇÇ ‚Üí ‚Ñù}
    (v‚ÇÇ : Œπ‚ÇÇ ‚Üí Œ±) (h‚ÇÇ : ‚àë i in s‚ÇÇ, w‚ÇÇ i = 0) :
  inner (‚àë i‚ÇÅ in s‚ÇÅ, w‚ÇÅ i‚ÇÅ ‚Ä¢ v‚ÇÅ i‚ÇÅ) (‚àë i‚ÇÇ in s‚ÇÇ, w‚ÇÇ i‚ÇÇ ‚Ä¢ v‚ÇÇ i‚ÇÇ) =
    (-‚àë i‚ÇÅ in s‚ÇÅ, ‚àë i‚ÇÇ in s‚ÇÇ, w‚ÇÅ i‚ÇÅ * w‚ÇÇ i‚ÇÇ * (‚à•v‚ÇÅ i‚ÇÅ - v‚ÇÇ i‚ÇÇ‚à• * ‚à•v‚ÇÅ i‚ÇÅ - v‚ÇÇ i‚ÇÇ‚à•)) / 2 :=
by simp_rw [sum_inner, inner_sum, inner_smul_left, inner_smul_right,
            inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two,
            ‚Üêdiv_sub_div_same, ‚Üêdiv_add_div_same, mul_sub_left_distrib, left_distrib,
            finset.sum_sub_distrib, finset.sum_add_distrib, ‚Üêfinset.mul_sum, ‚Üêfinset.sum_mul,
            h‚ÇÅ, h‚ÇÇ, zero_mul, mul_zero, finset.sum_const_zero, zero_add, zero_sub, finset.mul_sum,
            neg_div, finset.sum_div, mul_div_assoc, mul_assoc]

end norm

/-! ### Inner product space structure on product spaces -/

-- TODO [Lean 3.15]: drop some of these `show`s
/-- If `Œπ` is a finite type and each space `f i`, `i : Œπ`, is an inner product space,
then `Œ† i, f i` is an inner product space as well. Since `Œ† i, f i` is endowed with the sup norm,
we use instead `pi_Lp 2 one_le_two f` for the product space, which is endowed with the `L^2` norm.
-/
instance pi_Lp.inner_product_space
  (Œπ : Type*) [fintype Œπ] (f : Œπ ‚Üí Type*) [Œ† i, inner_product_space (f i)] :
  inner_product_space (pi_Lp 2 one_le_two f) :=
{ inner := Œª x y, ‚àë i, inner (x i) (y i),
  norm_eq_sqrt_inner := begin
    assume x,
    have : (2 : ‚Ñù)‚Åª¬π * 2 = 1, by norm_num,
    simp [inner, pi_Lp.norm_eq, norm_eq_sqrt_inner, sqrt_eq_rpow, ‚Üê rpow_mul (inner_self_nonneg),
          this],
  end,
  comm := Œª x y, finset.sum_congr rfl $ Œª i hi, inner_comm (x i) (y i),
  add_left := Œª x y z,
    show ‚àë i, inner (x i + y i) (z i) = ‚àë i, inner (x i) (z i) + ‚àë i, inner (y i) (z i),
    by simp only [inner_add_left, finset.sum_add_distrib],
  smul_left := Œª x y r,
    show ‚àë (i : Œπ), inner (r ‚Ä¢ x i) (y i) = r * ‚àë i, inner (x i) (y i),
    by simp only [finset.mul_sum, inner_smul_left] }

/-- The type of real numbers is an inner product space. -/
instance real.inner_product_space : inner_product_space ‚Ñù :=
{ inner := (*),
  norm_eq_sqrt_inner := Œª x, by simp [inner, norm_eq_abs, sqrt_mul_self_eq_abs],
  comm := mul_comm,
  add_left := add_mul,
  smul_left := Œª _ _ _, mul_assoc _ _ _ }

/-- The standard Euclidean space, functions on a finite type. For an `n`-dimensional space
use `euclidean_space (fin n)`.  -/
@[reducible, nolint unused_arguments]
def euclidean_space (n : Type*) [fintype n] : Type* := pi_Lp 2 one_le_two (Œª (i : n), ‚Ñù)

section pi_Lp
local attribute [reducible] pi_Lp
variables (n : Type*) [fintype n]

instance : finite_dimensional ‚Ñù (euclidean_space n) :=
by apply_instance

@[simp] lemma findim_euclidean_space :
  finite_dimensional.findim ‚Ñù (euclidean_space n) = fintype.card n :=
by simp

lemma findim_euclidean_space_fin {n : ‚Ñï} :
  finite_dimensional.findim ‚Ñù (euclidean_space (fin n)) = n :=
by simp

end pi_Lp

/-! ### Orthogonal projection in inner product spaces -/
section orthogonal

open filter

/--
Existence of minimizers
Let `u` be a point in an inner product space, and let `K` be a nonempty complete convex subset.
Then there exists a unique `v` in `K` that minimizes the distance `‚à•u - v‚à•` to `u`.
 -/
-- FIXME this monolithic proof causes a deterministic timeout with `-T50000`
-- It should be broken in a sequence of more manageable pieces,
-- perhaps with individual statements for the three steps below.
theorem exists_norm_eq_infi_of_complete_convex {K : set Œ±} (ne : K.nonempty) (h‚ÇÅ : is_complete K)
  (h‚ÇÇ : convex K) : ‚àÄ u : Œ±, ‚àÉ v ‚àà K, ‚à•u - v‚à• = ‚®Ö w : K, ‚à•u - w‚à• := assume u,
begin
  let Œ¥ := ‚®Ö w : K, ‚à•u - w‚à•,
  letI : nonempty K := ne.to_subtype,
  have zero_le_Œ¥ : 0 ‚â§ Œ¥ := le_cinfi (Œª _, norm_nonneg _),
  have Œ¥_le : ‚àÄ w : K, Œ¥ ‚â§ ‚à•u - w‚à•,
    from cinfi_le ‚ü®0, forall_range_iff.2 $ Œª _, norm_nonneg _‚ü©,
  have Œ¥_le' : ‚àÄ w ‚àà K, Œ¥ ‚â§ ‚à•u - w‚à• := assume w hw, Œ¥_le ‚ü®w, hw‚ü©,
  -- Step 1: since `Œ¥` is the infimum, can find a sequence `w : ‚Ñï ‚Üí K` in `K`
  -- such that `‚à•u - w n‚à• < Œ¥ + 1 / (n + 1)` (which implies `‚à•u - w n‚à• --> Œ¥`);
  -- maybe this should be a separate lemma
  have exists_seq : ‚àÉ w : ‚Ñï ‚Üí K, ‚àÄ n, ‚à•u - w n‚à• < Œ¥ + 1 / (n + 1),
  { have hŒ¥ : ‚àÄn:‚Ñï, Œ¥ < Œ¥ + 1 / (n + 1), from
      Œª n, lt_add_of_le_of_pos (le_refl _) nat.one_div_pos_of_nat,
    have h := Œª n, exists_lt_of_cinfi_lt (hŒ¥ n),
    let w : ‚Ñï ‚Üí K := Œª n, classical.some (h n),
    exact ‚ü®w, Œª n, classical.some_spec (h n)‚ü© },
  rcases exists_seq with ‚ü®w, hw‚ü©,
  have norm_tendsto : tendsto (Œª n, ‚à•u - w n‚à•) at_top (ùìù Œ¥),
  { have h : tendsto (Œª n:‚Ñï, Œ¥) at_top (ùìù Œ¥) := tendsto_const_nhds,
    have h' : tendsto (Œª n:‚Ñï, Œ¥ + 1 / (n + 1)) at_top (ùìù Œ¥),
    { convert h.add tendsto_one_div_add_at_top_nhds_0_nat, simp only [add_zero] },
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le h h'
      (Œª x, Œ¥_le _) (Œª x, le_of_lt (hw _)) },
  -- Step 2: Prove that the sequence `w : ‚Ñï ‚Üí K` is a Cauchy sequence
  have seq_is_cauchy : cauchy_seq (Œª n, ((w n):Œ±)),
  { rw cauchy_seq_iff_le_tendsto_0, -- splits into three goals
    let b := Œª n:‚Ñï, (8 * Œ¥ * (1/(n+1)) + 4 * (1/(n+1)) * (1/(n+1))),
    use (Œªn, sqrt (b n)),
    split,
    -- first goal :  `‚àÄ (n : ‚Ñï), 0 ‚â§ sqrt (b n)`
    assume n, exact sqrt_nonneg _,
    split,
    -- second goal : `‚àÄ (n m N : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí dist ‚Üë(w n) ‚Üë(w m) ‚â§ sqrt (b N)`
    assume p q N hp hq,
    let wp := ((w p):Œ±), let wq := ((w q):Œ±),
    let a := u - wq, let b := u - wp,
    let half := 1 / (2:‚Ñù), let div := 1 / ((N:‚Ñù) + 1),
    have : 4 * ‚à•u - half ‚Ä¢ (wq + wp)‚à• * ‚à•u - half ‚Ä¢ (wq + wp)‚à• + ‚à•wp - wq‚à• * ‚à•wp - wq‚à• =
      2 * (‚à•a‚à• * ‚à•a‚à• + ‚à•b‚à• * ‚à•b‚à•) :=
    calc
      4 * ‚à•u - half‚Ä¢(wq + wp)‚à• * ‚à•u - half‚Ä¢(wq + wp)‚à• + ‚à•wp - wq‚à• * ‚à•wp - wq‚à•
          = (2*‚à•u - half‚Ä¢(wq + wp)‚à•) * (2 * ‚à•u - half‚Ä¢(wq + wp)‚à•) + ‚à•wp-wq‚à•*‚à•wp-wq‚à• : by ring
      ... = (abs((2:‚Ñù)) * ‚à•u - half‚Ä¢(wq + wp)‚à•) * (abs((2:‚Ñù)) * ‚à•u - half‚Ä¢(wq+wp)‚à•) + ‚à•wp-wq‚à•*‚à•wp-wq‚à• :
      by { rw abs_of_nonneg, exact add_nonneg zero_le_one zero_le_one }
      ... = ‚à•(2:‚Ñù) ‚Ä¢ (u - half ‚Ä¢ (wq + wp))‚à• * ‚à•(2:‚Ñù) ‚Ä¢ (u - half ‚Ä¢ (wq + wp))‚à• + ‚à•wp-wq‚à• * ‚à•wp-wq‚à• :
        by { rw [norm_smul], refl }
      ... = ‚à•a + b‚à• * ‚à•a + b‚à• + ‚à•a - b‚à• * ‚à•a - b‚à• :
      begin
        rw [smul_sub, smul_smul, mul_one_div_cancel (two_ne_zero : (2 : ‚Ñù) ‚â† 0),
            ‚Üê one_add_one_eq_two, add_smul],
        simp only [one_smul],
        have eq‚ÇÅ : wp - wq = a - b, show wp - wq = (u - wq) - (u - wp), abel,
        have eq‚ÇÇ : u + u - (wq + wp) = a + b, show u + u - (wq + wp) = (u - wq) + (u - wp), abel,
        rw [eq‚ÇÅ, eq‚ÇÇ],
      end
      ... = 2 * (‚à•a‚à• * ‚à•a‚à• + ‚à•b‚à• * ‚à•b‚à•) : parallelogram_law_with_norm,
    have eq : Œ¥ ‚â§ ‚à•u - half ‚Ä¢ (wq + wp)‚à•,
    { rw smul_add,
      apply Œ¥_le', apply h‚ÇÇ,
        repeat {exact subtype.mem _},
        repeat {exact le_of_lt one_half_pos},
        exact add_halves 1 },
    have eq‚ÇÅ : 4 * Œ¥ * Œ¥ ‚â§ 4 * ‚à•u - half ‚Ä¢ (wq + wp)‚à• * ‚à•u - half ‚Ä¢ (wq + wp)‚à•,
    {  mono, mono, norm_num, apply mul_nonneg, norm_num, exact norm_nonneg _ },
    have eq‚ÇÇ : ‚à•a‚à• * ‚à•a‚à• ‚â§ (Œ¥ + div) * (Œ¥ + div) :=
      mul_self_le_mul_self (norm_nonneg _)
        (le_trans (le_of_lt $ hw q) (add_le_add_left (nat.one_div_le_one_div hq) _)),
    have eq‚ÇÇ' : ‚à•b‚à• * ‚à•b‚à• ‚â§ (Œ¥ + div) * (Œ¥ + div) :=
      mul_self_le_mul_self (norm_nonneg _)
        (le_trans (le_of_lt $ hw p) (add_le_add_left (nat.one_div_le_one_div hp) _)),
    rw dist_eq_norm,
    apply nonneg_le_nonneg_of_squares_le, { exact sqrt_nonneg _ },
    rw mul_self_sqrt,
    exact calc
      ‚à•wp - wq‚à• * ‚à•wp - wq‚à• = 2 * (‚à•a‚à•*‚à•a‚à• + ‚à•b‚à•*‚à•b‚à•) - 4 * ‚à•u - half ‚Ä¢ (wq+wp)‚à• * ‚à•u - half ‚Ä¢ (wq+wp)‚à• :
        by { rw ‚Üê this, simp }
      ... ‚â§ 2 * (‚à•a‚à• * ‚à•a‚à• + ‚à•b‚à• * ‚à•b‚à•) - 4 * Œ¥ * Œ¥ : sub_le_sub_left eq‚ÇÅ _
      ... ‚â§ 2 * ((Œ¥ + div) * (Œ¥ + div) + (Œ¥ + div) * (Œ¥ + div)) - 4 * Œ¥ * Œ¥ :
        sub_le_sub_right (mul_le_mul_of_nonneg_left (add_le_add eq‚ÇÇ eq‚ÇÇ') (by norm_num)) _
      ... = 8 * Œ¥ * div + 4 * div * div : by ring,
    exact add_nonneg (mul_nonneg (mul_nonneg (by norm_num) zero_le_Œ¥) (le_of_lt nat.one_div_pos_of_nat))
      (mul_nonneg (mul_nonneg (by norm_num) (le_of_lt nat.one_div_pos_of_nat)) (le_of_lt nat.one_div_pos_of_nat)),
    -- third goal : `tendsto (Œª (n : ‚Ñï), sqrt (b n)) at_top (ùìù 0)`
    apply tendsto.comp,
    { convert continuous_sqrt.continuous_at, exact sqrt_zero.symm },
    have eq‚ÇÅ : tendsto (Œª (n : ‚Ñï), 8 * Œ¥ * (1 / (n + 1))) at_top (ùìù (0:‚Ñù)),
    { convert (@tendsto_const_nhds _ _ _ (8 * Œ¥) _).mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    have : tendsto (Œª (n : ‚Ñï), (4:‚Ñù) * (1 / (n + 1))) at_top (ùìù (0:‚Ñù)),
    { convert (@tendsto_const_nhds _ _ _ (4:‚Ñù) _).mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    have eq‚ÇÇ : tendsto (Œª (n : ‚Ñï), (4:‚Ñù) * (1 / (n + 1)) * (1 / (n + 1))) at_top (ùìù (0:‚Ñù)),
    { convert this.mul tendsto_one_div_add_at_top_nhds_0_nat,
      simp only [mul_zero] },
    convert eq‚ÇÅ.add eq‚ÇÇ, simp only [add_zero] },
  -- Step 3: By completeness of `K`, let `w : ‚Ñï ‚Üí K` converge to some `v : K`.
  -- Prove that it satisfies all requirements.
  rcases cauchy_seq_tendsto_of_is_complete h‚ÇÅ (Œª n, _) seq_is_cauchy with ‚ü®v, hv, w_tendsto‚ü©,
  use v, use hv,
  have h_cont : continuous (Œª v, ‚à•u - v‚à•) :=
    continuous.comp continuous_norm (continuous.sub continuous_const continuous_id),
  have : tendsto (Œª n, ‚à•u - w n‚à•) at_top (ùìù ‚à•u - v‚à•),
    convert (tendsto.comp h_cont.continuous_at w_tendsto),
  exact tendsto_nhds_unique at_top_ne_bot this norm_tendsto,
  exact subtype.mem _
end

/-- Characterization of minimizers in the above theorem -/
theorem norm_eq_infi_iff_inner_le_zero {K : set Œ±} (h : convex K) {u : Œ±} {v : Œ±}
  (hv : v ‚àà K) : ‚à•u - v‚à• = (‚®Ö w : K, ‚à•u - w‚à•) ‚Üî ‚àÄ w ‚àà K, inner (u - v) (w - v) ‚â§ 0 :=
iff.intro
begin
  assume eq w hw,
  let Œ¥ := ‚®Ö w : K, ‚à•u - w‚à•, let p := inner (u - v) (w - v), let q := ‚à•w - v‚à•^2,
  letI : nonempty K := ‚ü®‚ü®v, hv‚ü©‚ü©,
  have zero_le_Œ¥ : 0 ‚â§ Œ¥,
    apply le_cinfi, intro, exact norm_nonneg _,
  have Œ¥_le : ‚àÄ w : K, Œ¥ ‚â§ ‚à•u - w‚à•,
    assume w, apply cinfi_le, use (0:‚Ñù), rintros _ ‚ü®_, rfl‚ü©, exact norm_nonneg _,
  have Œ¥_le' : ‚àÄ w ‚àà K, Œ¥ ‚â§ ‚à•u - w‚à• := assume w hw, Œ¥_le ‚ü®w, hw‚ü©,
  have : ‚àÄŒ∏:‚Ñù, 0 < Œ∏ ‚Üí Œ∏ ‚â§ 1 ‚Üí 2 * p ‚â§ Œ∏ * q,
    assume Œ∏ hŒ∏‚ÇÅ hŒ∏‚ÇÇ,
    have : ‚à•u - v‚à•^2 ‚â§ ‚à•u - v‚à•^2 - 2 * Œ∏ * inner (u - v) (w - v) + Œ∏*Œ∏*‚à•w - v‚à•^2 :=
    calc
      ‚à•u - v‚à•^2 ‚â§ ‚à•u - (Œ∏‚Ä¢w + (1-Œ∏)‚Ä¢v)‚à•^2 :
      begin
        simp only [pow_two], apply mul_self_le_mul_self (norm_nonneg _),
        rw [eq], apply Œ¥_le',
        apply h hw hv,
        exacts [le_of_lt hŒ∏‚ÇÅ, sub_nonneg.2 hŒ∏‚ÇÇ, add_sub_cancel'_right _ _],
      end
      ... = ‚à•(u - v) - Œ∏ ‚Ä¢ (w - v)‚à•^2 :
      begin
        have : u - (Œ∏‚Ä¢w + (1-Œ∏)‚Ä¢v) = (u - v) - Œ∏ ‚Ä¢ (w - v),
        { rw [smul_sub, sub_smul, one_smul],
          simp only [sub_eq_add_neg, add_comm, add_left_comm, add_assoc, neg_add_rev] },
        rw this
      end
      ... = ‚à•u - v‚à•^2 - 2 * Œ∏ * inner (u - v) (w - v) + Œ∏*Œ∏*‚à•w - v‚à•^2 :
      begin
        rw [norm_sub_pow_two, inner_smul_right, norm_smul],
        simp only [pow_two],
        show ‚à•u-v‚à•*‚à•u-v‚à•-2*(Œ∏*inner(u-v)(w-v))+abs(Œ∏)*‚à•w-v‚à•*(abs(Œ∏)*‚à•w-v‚à•)=
                ‚à•u-v‚à•*‚à•u-v‚à•-2*Œ∏*inner(u-v)(w-v)+Œ∏*Œ∏*(‚à•w-v‚à•*‚à•w-v‚à•),
        rw abs_of_pos hŒ∏‚ÇÅ, ring
      end,
    have eq‚ÇÅ : ‚à•u-v‚à•^2-2*Œ∏*inner(u-v)(w-v)+Œ∏*Œ∏*‚à•w-v‚à•^2=‚à•u-v‚à•^2+(Œ∏*Œ∏*‚à•w-v‚à•^2-2*Œ∏*inner(u-v)(w-v)), abel,
    rw [eq‚ÇÅ, le_add_iff_nonneg_right] at this,
    have eq‚ÇÇ : Œ∏*Œ∏*‚à•w-v‚à•^2-2*Œ∏*inner(u-v)(w-v)=Œ∏*(Œ∏*‚à•w-v‚à•^2-2*inner(u-v)(w-v)), ring,
    rw eq‚ÇÇ at this,
    have := le_of_sub_nonneg (nonneg_of_mul_nonneg_left this hŒ∏‚ÇÅ),
    exact this,
  by_cases hq : q = 0,
  { rw hq at this,
    have : p ‚â§ 0,
      have := this (1:‚Ñù) (by norm_num) (by norm_num),
      linarith,
    exact this },
  { have q_pos : 0 < q,
      apply lt_of_le_of_ne, exact pow_two_nonneg _, intro h, exact hq h.symm,
    by_contradiction hp, rw not_le at hp,
    let Œ∏ := min (1:‚Ñù) (p / q),
    have eq‚ÇÅ : Œ∏*q ‚â§ p := calc
      Œ∏*q ‚â§ (p/q) * q : mul_le_mul_of_nonneg_right (min_le_right _ _) (pow_two_nonneg _)
      ... = p : div_mul_cancel _ hq,
    have : 2 * p ‚â§ p := calc
      2 * p ‚â§ Œ∏*q : by { refine this Œ∏ (lt_min (by norm_num) (div_pos hp q_pos)) (by norm_num) }
      ... ‚â§ p : eq‚ÇÅ,
    linarith }
end
begin
  assume h,
  letI : nonempty K := ‚ü®‚ü®v, hv‚ü©‚ü©,
  apply le_antisymm,
  { apply le_cinfi, assume w,
    apply nonneg_le_nonneg_of_squares_le (norm_nonneg _),
    have := h w w.2,
    exact calc
      ‚à•u - v‚à• * ‚à•u - v‚à• ‚â§ ‚à•u - v‚à• * ‚à•u - v‚à• - 2 * inner (u - v) ((w:Œ±) - v) : by linarith
      ... ‚â§ ‚à•u - v‚à•^2 - 2 * inner (u - v) ((w:Œ±) - v) + ‚à•(w:Œ±) - v‚à•^2 :
        by { rw pow_two, refine le_add_of_nonneg_right _, exact pow_two_nonneg _ }
      ... = ‚à•(u - v) - (w - v)‚à•^2 : norm_sub_pow_two.symm
      ... = ‚à•u - w‚à• * ‚à•u - w‚à• :
        by { have : (u - v) - (w - v) = u - w, abel, rw [this, pow_two] } },
  { show (‚®Ö (w : K), ‚à•u - w‚à•) ‚â§ (Œªw:K, ‚à•u - w‚à•) ‚ü®v, hv‚ü©,
      apply cinfi_le, use 0, rintros y ‚ü®z, rfl‚ü©, exact norm_nonneg _ }
end

/--
Existence of minimizers.
Let `u` be a point in an inner product space, and let `K` be a nonempty complete subspace.
Then there exists a unique `v` in `K` that minimizes the distance `‚à•u - v‚à•` to `u`.
This point `v` is usually called the orthogonal projection of `u` onto `K`.
-/
theorem exists_norm_eq_infi_of_complete_subspace (K : subspace ‚Ñù Œ±)
  (h : is_complete (‚ÜëK : set Œ±)) : ‚àÄ u : Œ±, ‚àÉ v ‚àà K, ‚à•u - v‚à• = ‚®Ö w : (‚ÜëK : set Œ±), ‚à•u - w‚à• :=
exists_norm_eq_infi_of_complete_convex ‚ü®0, K.zero_mem‚ü© h K.convex

/--
Characterization of minimizers in the above theorem.
Let `u` be a point in an inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `‚à•u - v‚à•` if and only if
for all `w ‚àà K`, `inner (u - v) w = 0` (i.e., `u - v` is orthogonal to the subspace `K`)
-/
theorem norm_eq_infi_iff_inner_eq_zero (K : subspace ‚Ñù Œ±) {u : Œ±} {v : Œ±}
  (hv : v ‚àà K) : ‚à•u - v‚à• = (‚®Ö w : (‚ÜëK : set Œ±), ‚à•u - w‚à•) ‚Üî ‚àÄ w ‚àà K, inner (u - v) w = 0 :=
iff.intro
begin
  assume h,
  have h : ‚àÄ w ‚àà K, inner (u - v) (w - v) ‚â§ 0,
  { rwa [norm_eq_infi_iff_inner_le_zero] at h, exacts [K.convex, hv] },
  assume w hw,
  have le : inner (u - v) w ‚â§ 0,
    let w' := w + v,
    have : w' ‚àà K := submodule.add_mem _ hw hv,
    have h‚ÇÅ := h w' this,
    have h‚ÇÇ : w' - v = w, simp only [add_neg_cancel_right, sub_eq_add_neg],
    rw h‚ÇÇ at h‚ÇÅ, exact h‚ÇÅ,
  have ge : inner (u - v) w ‚â• 0,
    let w'' := -w + v,
    have : w'' ‚àà K := submodule.add_mem _ (submodule.neg_mem _ hw) hv,
    have h‚ÇÅ := h w'' this,
    have h‚ÇÇ : w'' - v = -w, simp only [neg_inj', add_neg_cancel_right, sub_eq_add_neg],
    rw [h‚ÇÇ, inner_neg_right] at h‚ÇÅ,
    linarith,
    exact le_antisymm le ge
end
begin
  assume h,
  have : ‚àÄ w ‚àà K, inner (u - v) (w - v) ‚â§ 0,
    assume w hw,
    let w' := w - v,
    have : w' ‚àà K := submodule.sub_mem _ hw hv,
    have h‚ÇÅ := h w' this,
    exact le_of_eq h‚ÇÅ,
  rwa norm_eq_infi_iff_inner_le_zero,
  exacts [submodule.convex _, hv]
end

end orthogonal
