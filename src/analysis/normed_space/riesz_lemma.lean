/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Yury Kudryashov
-/
import analysis.normed_space.basic
import topology.metric_space.hausdorff_distance

/-!
# Applications of the Hausdorff distance in normed spaces

Riesz's lemma, stated for a normed space over a normed field: for any
closed proper subspace `F` of `E`, there is a nonzero `x` such that `‚à•x - F‚à•`
is at least `r * ‚à•x‚à•` for any `r < 1`. This is `riesz_lemma`.

In a nontrivially normed field (with an element `c` of norm `> 1`) and any `R > ‚à•c‚à•`, one can
guarantee `‚à•x‚à• ‚â§ R` and `‚à•x - y‚à• ‚â• 1` for any `y` in `F`. This is `riesz_lemma_of_norm_lt`.

A further lemma, `metric.closed_ball_inf_dist_compl_subset_closure`, finds a *closed* ball within
the closure of a set `s` of optimal distance from a point in `x` to the frontier of `s`.
-/

open set metric
open_locale topological_space

variables {ùïú : Type*} [normed_field ùïú]
variables {E : Type*} [normed_group E] [normed_space ùïú E]
variables {F : Type*} [semi_normed_group F] [normed_space ‚Ñù F]

/-- Riesz's lemma, which usually states that it is possible to find a
vector with norm 1 whose distance to a closed proper subspace is
arbitrarily close to 1. The statement here is in terms of multiples of
norms, since in general the existence of an element of norm exactly 1
is not guaranteed. For a variant giving an element with norm in `[1, R]`, see
`riesz_lemma_of_norm_lt`. -/
lemma riesz_lemma {F : subspace ùïú E} (hFc : is_closed (F : set E))
  (hF : ‚àÉ x : E, x ‚àâ F) {r : ‚Ñù} (hr : r < 1) :
  ‚àÉ x‚ÇÄ : E, x‚ÇÄ ‚àâ F ‚àß ‚àÄ y ‚àà F, r * ‚à•x‚ÇÄ‚à• ‚â§ ‚à•x‚ÇÄ - y‚à• :=
begin
  classical,
  obtain ‚ü®x, hx‚ü© : ‚àÉ x : E, x ‚àâ F := hF,
  let d := metric.inf_dist x F,
  have hFn : (F : set E).nonempty, from ‚ü®_, F.zero_mem‚ü©,
  have hdp : 0 < d,
    from lt_of_le_of_ne metric.inf_dist_nonneg (Œª heq, hx
    ((hFc.mem_iff_inf_dist_zero hFn).2 heq.symm)),
  let r' := max r 2‚Åª¬π,
  have hr' : r' < 1, by { simp [r', hr], norm_num },
  have hlt : 0 < r' := lt_of_lt_of_le (by norm_num) (le_max_right r 2‚Åª¬π),
  have hdlt : d < d / r', from (lt_div_iff hlt).mpr ((mul_lt_iff_lt_one_right hdp).2 hr'),
  obtain ‚ü®y‚ÇÄ, hy‚ÇÄF, hxy‚ÇÄ‚ü© : ‚àÉ y ‚àà F, dist x y < d / r' := (metric.inf_dist_lt_iff hFn).mp hdlt,
  have x_ne_y‚ÇÄ : x - y‚ÇÄ ‚àâ F,
  { by_contradiction h,
    have : (x - y‚ÇÄ) + y‚ÇÄ ‚àà F, from F.add_mem h hy‚ÇÄF,
    simp only [neg_add_cancel_right, sub_eq_add_neg] at this,
    exact hx this },
  refine ‚ü®x - y‚ÇÄ, x_ne_y‚ÇÄ, Œªy hy, le_of_lt _‚ü©,
  have hy‚ÇÄy : y‚ÇÄ + y ‚àà F, from F.add_mem hy‚ÇÄF hy,
  calc
    r * ‚à•x - y‚ÇÄ‚à• ‚â§ r' * ‚à•x - y‚ÇÄ‚à• : mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _)
    ... < d : by { rw ‚Üêdist_eq_norm, exact (lt_div_iff' hlt).1 hxy‚ÇÄ }
    ... ‚â§ dist x (y‚ÇÄ + y) : metric.inf_dist_le_dist_of_mem hy‚ÇÄy
    ... = ‚à•x - y‚ÇÄ - y‚à• : by { rw [sub_sub, dist_eq_norm] }
end

/--
A version of Riesz lemma: given a strict closed subspace `F`, one may find an element of norm `‚â§ R`
which is at distance  at least `1` of every element of `F`. Here, `R` is any given constant
strictly larger than the norm of an element of norm `> 1`. For a version without an `R`, see
`riesz_lemma`.

Since we are considering a general nontrivially normed field, there may be a gap in possible norms
(for instance no element of norm in `(1,2)`). Hence, we can not allow `R` arbitrarily close to `1`,
and require `R > ‚à•c‚à•` for some `c : ùïú` with norm `> 1`.
-/
lemma riesz_lemma_of_norm_lt
  {c : ùïú} (hc : 1 < ‚à•c‚à•) {R : ‚Ñù} (hR : ‚à•c‚à• < R)
  {F : subspace ùïú E} (hFc : is_closed (F : set E)) (hF : ‚àÉ x : E, x ‚àâ F) :
  ‚àÉ x‚ÇÄ : E, ‚à•x‚ÇÄ‚à• ‚â§ R ‚àß ‚àÄ y ‚àà F, 1 ‚â§ ‚à•x‚ÇÄ - y‚à• :=
begin
  have Rpos : 0 < R := (norm_nonneg _).trans_lt hR,
  have : ‚à•c‚à• / R < 1, by { rw div_lt_iff Rpos, simpa using hR },
  rcases riesz_lemma hFc hF this with ‚ü®x, xF, hx‚ü©,
  have x0 : x ‚â† 0 := Œª H, by simpa [H] using xF,
  obtain ‚ü®d, d0, dxlt, ledx, -‚ü© :
    ‚àÉ (d : ùïú), d ‚â† 0 ‚àß ‚à•d ‚Ä¢ x‚à• < R ‚àß R / ‚à•c‚à• ‚â§ ‚à•d ‚Ä¢ x‚à• ‚àß ‚à•d‚à•‚Åª¬π ‚â§ R‚Åª¬π * ‚à•c‚à• * ‚à•x‚à• :=
      rescale_to_shell hc Rpos x0,
  refine ‚ü®d ‚Ä¢ x, dxlt.le, Œª y hy, _‚ü©,
  set y' := d‚Åª¬π ‚Ä¢ y with hy',
  have y'F : y' ‚àà F, by simp [hy', submodule.smul_mem _ _ hy],
  have yy' : y = d ‚Ä¢ y', by simp [hy', smul_smul, mul_inv_cancel d0],
  calc 1 = (‚à•c‚à•/R) * (R/‚à•c‚à•) : by field_simp [Rpos.ne', (zero_lt_one.trans hc).ne']
  ... ‚â§ (‚à•c‚à•/R) * (‚à•d ‚Ä¢ x‚à•) :
    mul_le_mul_of_nonneg_left ledx (div_nonneg (norm_nonneg _) Rpos.le)
  ... = ‚à•d‚à• * (‚à•c‚à•/R * ‚à•x‚à•) : by { simp [norm_smul], ring }
  ... ‚â§ ‚à•d‚à• * ‚à•x - y'‚à• :
    mul_le_mul_of_nonneg_left (hx y' (by simp [hy', submodule.smul_mem _ _ hy])) (norm_nonneg _)
  ... = ‚à•d ‚Ä¢ x - y‚à• : by simp [yy', ‚Üê smul_sub, norm_smul],
end

lemma metric.closed_ball_inf_dist_compl_subset_closure {x : F} {s : set F} (hx : x ‚àà s) :
  closed_ball x (inf_dist x s·∂ú) ‚äÜ closure s :=
begin
  cases eq_or_ne (inf_dist x s·∂ú) 0 with h‚ÇÄ h‚ÇÄ,
  { rw [h‚ÇÄ, closed_ball_zero'],
    exact closure_mono (singleton_subset_iff.2 hx) },
  { rw ‚Üê closure_ball x h‚ÇÄ,
    exact closure_mono ball_inf_dist_compl_subset }
end
