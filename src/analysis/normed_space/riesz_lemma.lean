/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo
-/
import topology.metric_space.hausdorff_distance

/-!
# Riesz's lemma

Riesz's lemma, stated for a normed space over a normed field: for any
closed proper subspace `F` of `E`, there is a nonzero `x` such that `âˆ¥x - Fâˆ¥`
is at least `r * âˆ¥xâˆ¥` for any `r < 1`. This is `riesz_lemma`.

In a nondiscrete normed field (with an element `c` of norm `> 1`) and any `R > âˆ¥câˆ¥`, one can
guarantee `âˆ¥xâˆ¥ â‰¤ R` and `âˆ¥x - yâˆ¥ â‰¥ 1` for any `y` in `F`. This is `riesz_lemma_of_norm_lt`.
-/

variables {ğ•œ : Type*} [normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]

/-- Riesz's lemma, which usually states that it is possible to find a
vector with norm 1 whose distance to a closed proper subspace is
arbitrarily close to 1. The statement here is in terms of multiples of
norms, since in general the existence of an element of norm exactly 1
is not guaranteed. For a variant giving an element with norm in `[1, R]`, see
`riesz_lemma_of_norm_lt`. -/
lemma riesz_lemma {F : subspace ğ•œ E} (hFc : is_closed (F : set E))
  (hF : âˆƒ x : E, x âˆ‰ F) {r : â„} (hr : r < 1) :
  âˆƒ xâ‚€ : E, xâ‚€ âˆ‰ F âˆ§ âˆ€ y âˆˆ F, r * âˆ¥xâ‚€âˆ¥ â‰¤ âˆ¥xâ‚€ - yâˆ¥ :=
begin
  classical,
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x âˆ‰ F := hF,
  let d := metric.inf_dist x F,
  have hFn : (F : set E).nonempty, from âŸ¨_, F.zero_memâŸ©,
  have hdp : 0 < d,
    from lt_of_le_of_ne metric.inf_dist_nonneg (Î» heq, hx
    ((metric.mem_iff_inf_dist_zero_of_closed hFc hFn).2 heq.symm)),
  let r' := max r 2â»Â¹,
  have hr' : r' < 1, by { simp [r', hr], norm_num },
  have hlt : 0 < r' := lt_of_lt_of_le (by norm_num) (le_max_right r 2â»Â¹),
  have hdlt : d < d / r', from (lt_div_iff hlt).mpr ((mul_lt_iff_lt_one_right hdp).2 hr'),
  obtain âŸ¨yâ‚€, hyâ‚€F, hxyâ‚€âŸ© : âˆƒ y âˆˆ F, dist x y < d / r' :=
    metric.exists_dist_lt_of_inf_dist_lt hdlt hFn,
  have x_ne_yâ‚€ : x - yâ‚€ âˆ‰ F,
  { by_contradiction h,
    have : (x - yâ‚€) + yâ‚€ âˆˆ F, from F.add_mem h hyâ‚€F,
    simp only [neg_add_cancel_right, sub_eq_add_neg] at this,
    exact hx this },
  refine âŸ¨x - yâ‚€, x_ne_yâ‚€, Î»y hy, le_of_lt _âŸ©,
  have hyâ‚€y : yâ‚€ + y âˆˆ F, from F.add_mem hyâ‚€F hy,
  calc
    r * âˆ¥x - yâ‚€âˆ¥ â‰¤ r' * âˆ¥x - yâ‚€âˆ¥ : mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _)
    ... < d : by { rw â†dist_eq_norm, exact (lt_div_iff' hlt).1 hxyâ‚€ }
    ... â‰¤ dist x (yâ‚€ + y) : metric.inf_dist_le_dist_of_mem hyâ‚€y
    ... = âˆ¥x - yâ‚€ - yâˆ¥ : by { rw [sub_sub, dist_eq_norm] }
end

/--
A version of Riesz lemma: given a strict closed subspace `F`, one may find an element of norm `â‰¤ R`
which is at distance  at least `1` of every element of `F`. Here, `R` is any given constant
strictly larger than the norm of an element of norm `> 1`. For a version without an `R`, see
`riesz_lemma`.

Since we are considering a general nondiscrete normed field, there may be a gap in possible norms
(for instance no element of norm in `(1,2)`). Hence, we can not allow `R` arbitrarily close to `1`,
and require `R > âˆ¥câˆ¥` for some `c : ğ•œ` with norm `> 1`.
-/
lemma riesz_lemma_of_norm_lt
  {c : ğ•œ} (hc : 1 < âˆ¥câˆ¥) {R : â„} (hR : âˆ¥câˆ¥ < R)
  {F : subspace ğ•œ E} (hFc : is_closed (F : set E)) (hF : âˆƒ x : E, x âˆ‰ F) :
  âˆƒ xâ‚€ : E, âˆ¥xâ‚€âˆ¥ â‰¤ R âˆ§ âˆ€ y âˆˆ F, 1 â‰¤ âˆ¥xâ‚€ - yâˆ¥ :=
begin
  have Rpos : 0 < R := (norm_nonneg _).trans_lt hR,
  have : âˆ¥câˆ¥ / R < 1, by { rw div_lt_iff Rpos, simpa using hR },
  rcases riesz_lemma hFc hF this with âŸ¨x, xF, hxâŸ©,
  have x0 : x â‰  0 := Î» H, by simpa [H] using xF,
  obtain âŸ¨d, d0, dxlt, ledx, -âŸ© :
    âˆƒ (d : ğ•œ), d â‰  0 âˆ§ âˆ¥d â€¢ xâˆ¥ < R âˆ§ R / âˆ¥câˆ¥ â‰¤ âˆ¥d â€¢ xâˆ¥ âˆ§ âˆ¥dâˆ¥â»Â¹ â‰¤ Râ»Â¹ * âˆ¥câˆ¥ * âˆ¥xâˆ¥ :=
      rescale_to_shell hc Rpos x0,
  refine âŸ¨d â€¢ x, dxlt.le, Î» y hy, _âŸ©,
  set y' := dâ»Â¹ â€¢ y with hy',
  have y'F : y' âˆˆ F, by simp [hy', submodule.smul_mem _ _ hy],
  have yy' : y = d â€¢ y', by simp [hy', smul_smul, mul_inv_cancel d0],
  calc 1 = (âˆ¥câˆ¥/R) * (R/âˆ¥câˆ¥) : by field_simp [Rpos.ne', (zero_lt_one.trans hc).ne']
  ... â‰¤ (âˆ¥câˆ¥/R) * (âˆ¥d â€¢ xâˆ¥) :
    mul_le_mul_of_nonneg_left ledx (div_nonneg (norm_nonneg _) Rpos.le)
  ... = âˆ¥dâˆ¥ * (âˆ¥câˆ¥/R * âˆ¥xâˆ¥) : by { simp [norm_smul], ring }
  ... â‰¤ âˆ¥dâˆ¥ * âˆ¥x - y'âˆ¥ :
    mul_le_mul_of_nonneg_left (hx y' (by simp [hy', submodule.smul_mem _ _ hy])) (norm_nonneg _)
  ... = âˆ¥d â€¢ x - yâˆ¥ : by simp [yy', â† smul_sub, norm_smul],
end
