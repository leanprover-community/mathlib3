/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Yury Kudryashov
-/
import analysis.normed_space.basic
import topology.metric_space.hausdorff_distance

/-!
# Applications of the Hausdorff distance in normed spaces

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Riesz's lemma, stated for a normed space over a normed field: for any
closed proper subspace `F` of `E`, there is a nonzero `x` such that `â€–x - Fâ€–`
is at least `r * â€–xâ€–` for any `r < 1`. This is `riesz_lemma`.

In a nontrivially normed field (with an element `c` of norm `> 1`) and any `R > â€–câ€–`, one can
guarantee `â€–xâ€– â‰¤ R` and `â€–x - yâ€– â‰¥ 1` for any `y` in `F`. This is `riesz_lemma_of_norm_lt`.

A further lemma, `metric.closed_ball_inf_dist_compl_subset_closure`, finds a *closed* ball within
the closure of a set `s` of optimal distance from a point in `x` to the frontier of `s`.
-/

open set metric
open_locale topology

variables {ğ•œ : Type*} [normed_field ğ•œ]
variables {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
variables {F : Type*} [seminormed_add_comm_group F] [normed_space â„ F]

/-- Riesz's lemma, which usually states that it is possible to find a
vector with norm 1 whose distance to a closed proper subspace is
arbitrarily close to 1. The statement here is in terms of multiples of
norms, since in general the existence of an element of norm exactly 1
is not guaranteed. For a variant giving an element with norm in `[1, R]`, see
`riesz_lemma_of_norm_lt`. -/
lemma riesz_lemma {F : subspace ğ•œ E} (hFc : is_closed (F : set E))
  (hF : âˆƒ x : E, x âˆ‰ F) {r : â„} (hr : r < 1) :
  âˆƒ xâ‚€ : E, xâ‚€ âˆ‰ F âˆ§ âˆ€ y âˆˆ F, r * â€–xâ‚€â€– â‰¤ â€–xâ‚€ - yâ€– :=
begin
  classical,
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x âˆ‰ F := hF,
  let d := metric.inf_dist x F,
  have hFn : (F : set E).nonempty, from âŸ¨_, F.zero_memâŸ©,
  have hdp : 0 < d,
    from lt_of_le_of_ne metric.inf_dist_nonneg (Î» heq, hx
    ((hFc.mem_iff_inf_dist_zero hFn).2 heq.symm)),
  let r' := max r 2â»Â¹,
  have hr' : r' < 1, by { simp [r', hr], norm_num },
  have hlt : 0 < r' := lt_of_lt_of_le (by norm_num) (le_max_right r 2â»Â¹),
  have hdlt : d < d / r', from (lt_div_iff hlt).mpr ((mul_lt_iff_lt_one_right hdp).2 hr'),
  obtain âŸ¨yâ‚€, hyâ‚€F, hxyâ‚€âŸ© : âˆƒ y âˆˆ F, dist x y < d / r' := (metric.inf_dist_lt_iff hFn).mp hdlt,
  have x_ne_yâ‚€ : x - yâ‚€ âˆ‰ F,
  { by_contradiction h,
    have : (x - yâ‚€) + yâ‚€ âˆˆ F, from F.add_mem h hyâ‚€F,
    simp only [neg_add_cancel_right, sub_eq_add_neg] at this,
    exact hx this },
  refine âŸ¨x - yâ‚€, x_ne_yâ‚€, Î»y hy, le_of_lt _âŸ©,
  have hyâ‚€y : yâ‚€ + y âˆˆ F, from F.add_mem hyâ‚€F hy,
  calc
    r * â€–x - yâ‚€â€– â‰¤ r' * â€–x - yâ‚€â€– : mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg _)
    ... < d : by { rw â†dist_eq_norm, exact (lt_div_iff' hlt).1 hxyâ‚€ }
    ... â‰¤ dist x (yâ‚€ + y) : metric.inf_dist_le_dist_of_mem hyâ‚€y
    ... = â€–x - yâ‚€ - yâ€– : by { rw [sub_sub, dist_eq_norm] }
end

/--
A version of Riesz lemma: given a strict closed subspace `F`, one may find an element of norm `â‰¤ R`
which is at distance  at least `1` of every element of `F`. Here, `R` is any given constant
strictly larger than the norm of an element of norm `> 1`. For a version without an `R`, see
`riesz_lemma`.

Since we are considering a general nontrivially normed field, there may be a gap in possible norms
(for instance no element of norm in `(1,2)`). Hence, we can not allow `R` arbitrarily close to `1`,
and require `R > â€–câ€–` for some `c : ğ•œ` with norm `> 1`.
-/
lemma riesz_lemma_of_norm_lt
  {c : ğ•œ} (hc : 1 < â€–câ€–) {R : â„} (hR : â€–câ€– < R)
  {F : subspace ğ•œ E} (hFc : is_closed (F : set E)) (hF : âˆƒ x : E, x âˆ‰ F) :
  âˆƒ xâ‚€ : E, â€–xâ‚€â€– â‰¤ R âˆ§ âˆ€ y âˆˆ F, 1 â‰¤ â€–xâ‚€ - yâ€– :=
begin
  have Rpos : 0 < R := (norm_nonneg _).trans_lt hR,
  have : â€–câ€– / R < 1, by { rw div_lt_iff Rpos, simpa using hR },
  rcases riesz_lemma hFc hF this with âŸ¨x, xF, hxâŸ©,
  have x0 : x â‰  0 := Î» H, by simpa [H] using xF,
  obtain âŸ¨d, d0, dxlt, ledx, -âŸ© :
    âˆƒ (d : ğ•œ), d â‰  0 âˆ§ â€–d â€¢ xâ€– < R âˆ§ R / â€–câ€– â‰¤ â€–d â€¢ xâ€– âˆ§ â€–dâ€–â»Â¹ â‰¤ Râ»Â¹ * â€–câ€– * â€–xâ€– :=
      rescale_to_shell hc Rpos x0,
  refine âŸ¨d â€¢ x, dxlt.le, Î» y hy, _âŸ©,
  set y' := dâ»Â¹ â€¢ y with hy',
  have y'F : y' âˆˆ F, by simp [hy', submodule.smul_mem _ _ hy],
  have yy' : y = d â€¢ y', by simp [hy', smul_smul, mul_inv_cancel d0],
  calc 1 = (â€–câ€–/R) * (R/â€–câ€–) : by field_simp [Rpos.ne', (zero_lt_one.trans hc).ne']
  ... â‰¤ (â€–câ€–/R) * (â€–d â€¢ xâ€–) :
    mul_le_mul_of_nonneg_left ledx (div_nonneg (norm_nonneg _) Rpos.le)
  ... = â€–dâ€– * (â€–câ€–/R * â€–xâ€–) : by { simp [norm_smul], ring }
  ... â‰¤ â€–dâ€– * â€–x - y'â€– :
    mul_le_mul_of_nonneg_left (hx y' (by simp [hy', submodule.smul_mem _ _ hy])) (norm_nonneg _)
  ... = â€–d â€¢ x - yâ€– : by simp [yy', â† smul_sub, norm_smul],
end

lemma metric.closed_ball_inf_dist_compl_subset_closure {x : F} {s : set F} (hx : x âˆˆ s) :
  closed_ball x (inf_dist x sá¶œ) âŠ† closure s :=
begin
  cases eq_or_ne (inf_dist x sá¶œ) 0 with hâ‚€ hâ‚€,
  { rw [hâ‚€, closed_ball_zero'],
    exact closure_mono (singleton_subset_iff.2 hx) },
  { rw â† closure_ball x hâ‚€,
    exact closure_mono ball_inf_dist_compl_subset }
end
