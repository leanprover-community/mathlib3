/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import algebra.algebra.spectrum
import analysis.special_functions.pow
import analysis.complex.liouville
import analysis.complex.polynomial
import analysis.analytic.radius_liminf
import topology.algebra.module.character_space
import analysis.normed_space.exponential
/-!
# The spectrum of elements in a complete normed algebra

This file contains the basic theory for the resolvent and spectrum of a Banach algebra.

## Main definitions

* `spectral_radius : â„â‰¥0âˆ`: supremum of `â€–kâ€–â‚Š` for all `k âˆˆ spectrum ğ•œ a`
* `normed_ring.alg_equiv_complex_of_complete`: **Gelfand-Mazur theorem** For a complex
  Banach division algebra, the natural `algebra_map â„‚ A` is an algebra isomorphism whose inverse
  is given by selecting the (unique) element of `spectrum â„‚ a`

## Main statements

* `spectrum.is_open_resolvent_set`: the resolvent set is open.
* `spectrum.is_closed`: the spectrum is closed.
* `spectrum.subset_closed_ball_norm`: the spectrum is a subset of closed disk of radius
  equal to the norm.
* `spectrum.is_compact`: the spectrum is compact.
* `spectrum.spectral_radius_le_nnnorm`: the spectral radius is bounded above by the norm.
* `spectrum.has_deriv_at_resolvent`: the resolvent function is differentiable on the resolvent set.
* `spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius`: Gelfand's formula for the
  spectral radius in Banach algebras over `â„‚`.
* `spectrum.nonempty`: the spectrum of any element in a complex Banach algebra is nonempty.


## TODO

* compute all derivatives of `resolvent a`.

-/

open_locale ennreal nnreal

/-- The *spectral radius* is the supremum of the `nnnorm` (`â€–â¬â€–â‚Š`) of elements in the spectrum,
    coerced into an element of `â„â‰¥0âˆ`. Note that it is possible for `spectrum ğ•œ a = âˆ…`. In this
    case, `spectral_radius a = 0`.  It is also possible that `spectrum ğ•œ a` be unbounded (though
    not for Banach algebras, see `spectrum.is_bounded`, below).  In this case,
    `spectral_radius a = âˆ`. -/
noncomputable def spectral_radius (ğ•œ : Type*) {A : Type*} [normed_field ğ•œ] [ring A]
  [algebra ğ•œ A] (a : A) : â„â‰¥0âˆ :=
â¨† k âˆˆ spectrum ğ•œ a, â€–kâ€–â‚Š

variables {ğ•œ : Type*} {A : Type*}

namespace spectrum

section spectrum_compact

open filter

variables [normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A]

local notation `Ïƒ` := spectrum ğ•œ
local notation `Ï` := resolvent_set ğ•œ
local notation `â†‘â‚` := algebra_map ğ•œ A

@[simp] lemma spectral_radius.of_subsingleton [subsingleton A] (a : A) :
  spectral_radius ğ•œ a = 0 :=
by simp [spectral_radius]

@[simp] lemma spectral_radius_zero : spectral_radius ğ•œ (0 : A) = 0 :=
by { nontriviality A, simp [spectral_radius] }

lemma mem_resolvent_set_of_spectral_radius_lt {a : A} {k : ğ•œ} (h : spectral_radius ğ•œ a < â€–kâ€–â‚Š) :
  k âˆˆ Ï a :=
not_not.mp $ Î» hn, h.not_le $ le_suprâ‚‚ k hn

variable [complete_space A]

lemma is_open_resolvent_set (a : A) : is_open (Ï a) :=
units.is_open.preimage ((continuous_algebra_map ğ•œ A).sub continuous_const)

protected lemma is_closed (a : A) : is_closed (Ïƒ a) :=
(is_open_resolvent_set a).is_closed_compl

lemma mem_resolvent_set_of_norm_lt_mul {a : A} {k : ğ•œ} (h : â€–aâ€– * â€–(1 : A)â€– < â€–kâ€–) :
  k âˆˆ Ï a :=
begin
  rw [resolvent_set, set.mem_set_of_eq, algebra.algebra_map_eq_smul_one],
  nontriviality A,
  have hk : k â‰  0,
    from ne_zero_of_norm_ne_zero ((mul_nonneg (norm_nonneg _) (norm_nonneg _)).trans_lt h).ne',
  let ku := units.map (â†‘â‚).to_monoid_hom (units.mk0 k hk),
  rw [â†inv_inv (â€–(1 : A)â€–), mul_inv_lt_iff (inv_pos.2 $ norm_pos_iff.2 (one_ne_zero : (1 : A) â‰  0))]
    at h,
  have hku : â€–-aâ€– < â€–(â†‘kuâ»Â¹:A)â€–â»Â¹ := by simpa [ku, norm_algebra_map] using h,
  simpa [ku, sub_eq_add_neg, algebra.algebra_map_eq_smul_one] using (ku.add (-a) hku).is_unit,
end

lemma mem_resolvent_set_of_norm_lt [norm_one_class A] {a : A} {k : ğ•œ} (h : â€–aâ€– < â€–kâ€–) :
  k âˆˆ Ï a :=
mem_resolvent_set_of_norm_lt_mul (by rwa [norm_one, mul_one])

lemma norm_le_norm_mul_of_mem {a : A} {k : ğ•œ} (hk : k âˆˆ Ïƒ a) :
  â€–kâ€– â‰¤ â€–aâ€– * â€–(1 : A)â€– :=
le_of_not_lt $ mt mem_resolvent_set_of_norm_lt_mul hk

lemma norm_le_norm_of_mem [norm_one_class A] {a : A} {k : ğ•œ} (hk : k âˆˆ Ïƒ a) :
  â€–kâ€– â‰¤ â€–aâ€– :=
le_of_not_lt $ mt mem_resolvent_set_of_norm_lt hk

lemma subset_closed_ball_norm_mul (a : A) :
  Ïƒ a âŠ† metric.closed_ball (0 : ğ•œ) (â€–aâ€– * â€–(1 : A)â€–) :=
Î» k hk, by simp [norm_le_norm_mul_of_mem hk]

lemma subset_closed_ball_norm [norm_one_class A] (a : A) :
  Ïƒ a âŠ† metric.closed_ball (0 : ğ•œ) (â€–aâ€–) :=
Î» k hk, by simp [norm_le_norm_of_mem hk]

lemma is_bounded (a : A) : metric.bounded (Ïƒ a) :=
(metric.bounded_iff_subset_ball 0).mpr âŸ¨â€–aâ€– * â€–(1 : A)â€–, subset_closed_ball_norm_mul aâŸ©

protected theorem is_compact [proper_space ğ•œ] (a : A) : is_compact (Ïƒ a) :=
metric.is_compact_of_is_closed_bounded (spectrum.is_closed a) (is_bounded a)

theorem spectral_radius_le_nnnorm [norm_one_class A] (a : A) :
  spectral_radius ğ•œ a â‰¤ â€–aâ€–â‚Š :=
by { refine suprâ‚‚_le (Î» k hk, _), exact_mod_cast norm_le_norm_of_mem hk }

lemma exists_nnnorm_eq_spectral_radius_of_nonempty [proper_space ğ•œ] {a : A} (ha : (Ïƒ a).nonempty) :
  âˆƒ k âˆˆ Ïƒ a, (â€–kâ€–â‚Š : â„â‰¥0âˆ) = spectral_radius ğ•œ a :=
begin
  obtain âŸ¨k, hk, hâŸ© := (spectrum.is_compact a).exists_forall_ge ha continuous_nnnorm.continuous_on,
  exact âŸ¨k, hk, le_antisymm (le_suprâ‚‚ k hk) (suprâ‚‚_le $ by exact_mod_cast h)âŸ©,
end

lemma spectral_radius_lt_of_forall_lt_of_nonempty [proper_space ğ•œ] {a : A}
  (ha : (Ïƒ a).nonempty) {r : â„â‰¥0} (hr : âˆ€ k âˆˆ Ïƒ a, â€–kâ€–â‚Š < r) :
  spectral_radius ğ•œ a < r :=
Sup_image.symm.trans_lt $ ((spectrum.is_compact a).Sup_lt_iff_of_continuous ha
  (ennreal.continuous_coe.comp continuous_nnnorm).continuous_on (r : â„â‰¥0âˆ)).mpr
  (by exact_mod_cast hr)

open ennreal polynomial

variable (ğ•œ)
theorem spectral_radius_le_pow_nnnorm_pow_one_div (a : A) (n : â„•) :
  spectral_radius ğ•œ a â‰¤ (â€–a ^ (n + 1)â€–â‚Š) ^ (1 / (n + 1) : â„) * (â€–(1 : A)â€–â‚Š) ^ (1 / (n + 1) : â„) :=
begin
  refine suprâ‚‚_le (Î» k hk, _),
  /- apply easy direction of the spectral mapping theorem for polynomials -/
  have pow_mem : k ^ (n + 1) âˆˆ Ïƒ (a ^ (n + 1)),
    by simpa only [one_mul, algebra.algebra_map_eq_smul_one, one_smul, aeval_monomial, one_mul,
      eval_monomial] using subset_polynomial_aeval a (monomial (n + 1) (1 : ğ•œ)) âŸ¨k, hk, rflâŸ©,
  /- power of the norm is bounded by norm of the power -/
  have nnnorm_pow_le : (â†‘(â€–kâ€–â‚Š ^ (n + 1)) : â„â‰¥0âˆ) â‰¤ â€–a ^ (n + 1)â€–â‚Š * â€–(1 : A)â€–â‚Š,
    { simpa only [real.to_nnreal_mul (norm_nonneg _), norm_to_nnreal, nnnorm_pow k (n + 1),
        ennreal.coe_mul] using coe_mono (real.to_nnreal_mono (norm_le_norm_mul_of_mem pow_mem)) },
  /- take (n + 1)áµ—Ê° roots and clean up the left-hand side -/
  have hn : 0 < ((n + 1 : â„•) : â„), by exact_mod_cast nat.succ_pos',
  convert monotone_rpow_of_nonneg (one_div_pos.mpr hn).le nnnorm_pow_le,
  erw [coe_pow, â†rpow_nat_cast, â†rpow_mul, mul_one_div_cancel hn.ne', rpow_one],
  rw [nat.cast_succ, ennreal.coe_mul_rpow],
end

theorem spectral_radius_le_liminf_pow_nnnorm_pow_one_div (a : A) :
  spectral_radius ğ•œ a â‰¤ at_top.liminf (Î» n : â„•, (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„)) :=
begin
  refine ennreal.le_of_forall_lt_one_mul_le (Î» Îµ hÎµ, _),
  by_cases Îµ = 0,
  { simp only [h, zero_mul, zero_le'] },
  have hÎµ' : Îµâ»Â¹ â‰  âˆ,
    from Î» h', h (by simpa only [inv_inv, inv_top] using congr_arg (Î» (x : â„â‰¥0âˆ), xâ»Â¹) h'),
  simp only [ennreal.mul_le_iff_le_inv h (hÎµ.trans_le le_top).ne, mul_comm Îµâ»Â¹,
    liminf_eq_supr_infi_of_nat', ennreal.supr_mul, ennreal.infi_mul hÎµ'],
  rw [â†ennreal.inv_lt_inv, inv_one] at hÎµ,
  obtain âŸ¨N, hNâŸ© := eventually_at_top.mp
    (ennreal.eventually_pow_one_div_le (ennreal.coe_ne_top : â†‘â€–(1 : A)â€–â‚Š â‰  âˆ) hÎµ),
  refine (le_trans _ (le_supr _ (N + 1))),
  refine le_infi (Î» n, _),
  simp only [â†add_assoc],
  refine (spectral_radius_le_pow_nnnorm_pow_one_div ğ•œ a (n + N)).trans _,
  norm_cast,
  exact mul_le_mul_left' (hN (n + N + 1) (by linarith)) _,
end

end spectrum_compact

section resolvent

open filter asymptotics

variables [nontrivially_normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A] [complete_space A]

local notation `Ï` := resolvent_set ğ•œ
local notation `â†‘â‚` := algebra_map ğ•œ A

theorem has_deriv_at_resolvent {a : A} {k : ğ•œ} (hk : k âˆˆ Ï a) :
  has_deriv_at (resolvent a) (-(resolvent a k) ^ 2) k :=
begin
  have Hâ‚ : has_fderiv_at ring.inverse _ (â†‘â‚k - a) := has_fderiv_at_ring_inverse hk.unit,
  have Hâ‚‚ : has_deriv_at (Î» k, â†‘â‚k - a) 1 k,
  { simpa using (algebra.linear_map ğ•œ A).has_deriv_at.sub_const a },
  simpa [resolvent, sq, hk.unit_spec, â† ring.inverse_unit hk.unit] using Hâ‚.comp_has_deriv_at k Hâ‚‚,
end

/- TODO: Once there is sufficient API for bornology, we should get a nice filter / asymptotics
version of this, for example: `tendsto (resolvent a) (cobounded ğ•œ) (ğ“ 0)` or more specifically
`(resolvent a) =O[cobounded ğ•œ] (Î» z, zâ»Â¹)`. -/
lemma norm_resolvent_le_forall (a : A) :
  âˆ€ Îµ > 0, âˆƒ R > 0, âˆ€ z : ğ•œ, R â‰¤ â€–zâ€– â†’ â€–resolvent a zâ€– â‰¤ Îµ :=
begin
  obtain âŸ¨c, c_pos, hcâŸ© := (@normed_ring.inverse_one_sub_norm A _ _).exists_pos,
  rw [is_O_with_iff, eventually_iff, metric.mem_nhds_iff] at hc,
  rcases hc with âŸ¨Î´, Î´_pos, hÎ´âŸ©,
  simp only [cstar_ring.norm_one, mul_one] at hÎ´,
  intros Îµ hÎµ,
  have haâ‚ : 0 < â€–aâ€– + 1 := lt_of_le_of_lt (norm_nonneg a) (lt_add_one _),
  have min_pos : 0 < min (Î´ * (â€–aâ€– + 1)â»Â¹) (Îµ * câ»Â¹),
    from lt_min (mul_pos Î´_pos (inv_pos.mpr haâ‚)) (mul_pos hÎµ (inv_pos.mpr c_pos)),
  refine âŸ¨(min (Î´ * (â€–aâ€– + 1)â»Â¹) (Îµ * câ»Â¹))â»Â¹, inv_pos.mpr min_pos, (Î» z hz, _)âŸ©,
  have hnz : z â‰  0 := norm_pos_iff.mp (lt_of_lt_of_le (inv_pos.mpr min_pos) hz),
  replace hz := inv_le_of_inv_le min_pos hz,
  rcases (âŸ¨units.mk0 z hnz, units.coe_mk0 hnzâŸ© : is_unit z) with âŸ¨z, rflâŸ©,
  have lt_Î´ : â€–zâ»Â¹ â€¢ aâ€– < Î´,
  { rw [units.smul_def, norm_smul, units.coe_inv, norm_inv],
    calc â€–(z : ğ•œ)â€–â»Â¹ * â€–aâ€– â‰¤ Î´ * (â€–aâ€– + 1)â»Â¹ * â€–aâ€–
        : mul_le_mul_of_nonneg_right (hz.trans (min_le_left _ _)) (norm_nonneg _)
    ...                   < Î´
        : by { conv { rw mul_assoc, to_rhs, rw (mul_one Î´).symm },
               exact mul_lt_mul_of_pos_left
                 ((inv_mul_lt_iff haâ‚).mpr ((mul_one (â€–aâ€– + 1)).symm â–¸ (lt_add_one _))) Î´_pos } },
  rw [â†inv_smul_smul z (resolvent a (z : ğ•œ)), units_smul_resolvent_self, resolvent,
    algebra.algebra_map_eq_smul_one, one_smul, units.smul_def, norm_smul, units.coe_inv, norm_inv],
  calc _ â‰¤ Îµ * câ»Â¹ * c : mul_le_mul (hz.trans (min_le_right _ _)) (hÎ´ (mem_ball_zero_iff.mpr lt_Î´))
                           (norm_nonneg _) (mul_pos hÎµ (inv_pos.mpr c_pos)).le
  ...    = _           : inv_mul_cancel_rightâ‚€ c_pos.ne.symm Îµ,
end

end resolvent

section one_sub_smul

open continuous_multilinear_map ennreal formal_multilinear_series
open_locale nnreal ennreal

variables
[nontrivially_normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A]

variable (ğ•œ)
/-- In a Banach algebra `A` over a nontrivially normed field `ğ•œ`, for any `a : A` the
power series with coefficients `a ^ n` represents the function `(1 - z â€¢ a)â»Â¹` in a disk of
radius `â€–aâ€–â‚Šâ»Â¹`. -/
lemma has_fpower_series_on_ball_inverse_one_sub_smul [complete_space A] (a : A) :
  has_fpower_series_on_ball (Î» z : ğ•œ, ring.inverse (1 - z â€¢ a))
    (Î» n, continuous_multilinear_map.mk_pi_field ğ•œ (fin n) (a ^ n)) 0 (â€–aâ€–â‚Š)â»Â¹ :=
{ r_le :=
  begin
    refine le_of_forall_nnreal_lt (Î» r hr, le_radius_of_bound_nnreal _ (max 1 â€–(1 : A)â€–â‚Š) (Î» n, _)),
    rw [â†norm_to_nnreal, norm_mk_pi_field, norm_to_nnreal],
    cases n,
    { simp only [le_refl, mul_one, or_true, le_max_iff, pow_zero] },
    { refine le_trans (le_trans (mul_le_mul_right' (nnnorm_pow_le' a n.succ_pos) (r ^ n.succ)) _)
        (le_max_left _ _),
      { by_cases â€–aâ€–â‚Š = 0,
        { simp only [h, zero_mul, zero_le', pow_succ], },
        { rw [â†coe_inv h, coe_lt_coe, nnreal.lt_inv_iff_mul_lt h] at hr,
          simpa only [â†mul_pow, mul_comm] using pow_le_one' hr.le n.succ } } }
  end,
  r_pos := ennreal.inv_pos.mpr coe_ne_top,
  has_sum := Î» y hy,
  begin
    have norm_lt : â€–y â€¢ aâ€– < 1,
    { by_cases h : â€–aâ€–â‚Š = 0,
      { simp only [nnnorm_eq_zero.mp h, norm_zero, zero_lt_one, smul_zero] },
      { have nnnorm_lt : â€–yâ€–â‚Š < â€–aâ€–â‚Šâ»Â¹,
          by simpa only [â†coe_inv h, mem_ball_zero_iff, metric.emetric_ball_nnreal] using hy,
        rwa [â†coe_nnnorm, â†real.lt_to_nnreal_iff_coe_lt, real.to_nnreal_one, nnnorm_smul,
          â†nnreal.lt_inv_iff_mul_lt h] } },
    simpa [â†smul_pow, (normed_ring.summable_geometric_of_norm_lt_1 _ norm_lt).has_sum_iff]
      using (normed_ring.inverse_one_sub _ norm_lt).symm,
  end }

variable {ğ•œ}
lemma is_unit_one_sub_smul_of_lt_inv_radius {a : A} {z : ğ•œ} (h : â†‘â€–zâ€–â‚Š < (spectral_radius ğ•œ a)â»Â¹) :
  is_unit (1 - z â€¢ a) :=
begin
  by_cases hz : z = 0,
  { simp only [hz, is_unit_one, sub_zero, zero_smul] },
  { let u := units.mk0 z hz,
    suffices hu : is_unit (uâ»Â¹ â€¢ 1 - a),
    { rwa [is_unit.smul_sub_iff_sub_inv_smul, inv_inv u] at hu },
    { rw [units.smul_def, â†algebra.algebra_map_eq_smul_one, â†mem_resolvent_set_iff],
      refine mem_resolvent_set_of_spectral_radius_lt _,
      rwa [units.coe_inv, nnnorm_inv, coe_inv (nnnorm_ne_zero_iff.mpr
        (units.coe_mk0 hz â–¸ hz : (u : ğ•œ) â‰  0)), lt_inv_iff_lt_inv] } }
end

/-- In a Banach algebra `A` over `ğ•œ`, for `a : A` the function `Î» z, (1 - z â€¢ a)â»Â¹` is
differentiable on any closed ball centered at zero of radius `r < (spectral_radius ğ•œ a)â»Â¹`. -/
theorem differentiable_on_inverse_one_sub_smul [complete_space A] {a : A} {r : â„â‰¥0}
  (hr : (r : â„â‰¥0âˆ) < (spectral_radius ğ•œ a)â»Â¹) :
  differentiable_on ğ•œ (Î» z : ğ•œ, ring.inverse (1 - z â€¢ a)) (metric.closed_ball 0 r) :=
begin
  intros z z_mem,
  apply differentiable_at.differentiable_within_at,
  have hu : is_unit (1 - z â€¢ a),
  { refine is_unit_one_sub_smul_of_lt_inv_radius (lt_of_le_of_lt (coe_mono _) hr),
    simpa only [norm_to_nnreal, real.to_nnreal_coe]
      using real.to_nnreal_mono (mem_closed_ball_zero_iff.mp z_mem) },
  have Hâ‚ : differentiable ğ•œ (Î» w : ğ•œ, 1 - w â€¢ a) := (differentiable_id.smul_const a).const_sub 1,
  exact differentiable_at.comp z (differentiable_at_inverse hu.unit) (Hâ‚.differentiable_at),
end

end one_sub_smul

section gelfand_formula

open filter ennreal continuous_multilinear_map
open_locale topology

variables
[normed_ring A] [normed_algebra â„‚ A] [complete_space A]

/-- The `limsup` relationship for the spectral radius used to prove `spectrum.gelfand_formula`. -/
lemma limsup_pow_nnnorm_pow_one_div_le_spectral_radius (a : A) :
  limsup (Î» n : â„•, â†‘â€–a ^ nâ€–â‚Š ^ (1 / n : â„)) at_top â‰¤ spectral_radius â„‚ a :=
begin
  refine ennreal.inv_le_inv.mp (le_of_forall_pos_nnreal_lt (Î» r r_pos r_lt, _)),
  simp_rw [inv_limsup, â†one_div],
  let p : formal_multilinear_series â„‚ â„‚ A :=
    Î» n, continuous_multilinear_map.mk_pi_field â„‚ (fin n) (a ^ n),
  suffices h : (r : â„â‰¥0âˆ) â‰¤ p.radius,
  { convert h,
    simp only [p.radius_eq_liminf, â†norm_to_nnreal, norm_mk_pi_field],
    congr,
    ext n,
    rw [norm_to_nnreal, ennreal.coe_rpow_def (â€–a ^ nâ€–â‚Š) (1 / n : â„), if_neg],
    exact Î» ha, by linarith [ha.2, (one_div_nonneg.mpr n.cast_nonneg : 0 â‰¤ (1 / n : â„))], },
  { have Hâ‚ := (differentiable_on_inverse_one_sub_smul r_lt).has_fpower_series_on_ball r_pos,
    exact ((has_fpower_series_on_ball_inverse_one_sub_smul â„‚ a).exchange_radius Hâ‚).r_le, }
end

/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectral_radius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)` -/
theorem pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (a : A) :
  tendsto (Î» n : â„•, ((â€–a ^ nâ€–â‚Š ^ (1 / n : â„)) : â„â‰¥0âˆ)) at_top (ğ“ (spectral_radius â„‚ a)) :=
tendsto_of_le_liminf_of_limsup_le (spectral_radius_le_liminf_pow_nnnorm_pow_one_div â„‚ a)
  (limsup_pow_nnnorm_pow_one_div_le_spectral_radius a)

/- This is the same as `pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius` but for `norm`
instead of `nnnorm`. -/
/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectral_radius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)` -/
theorem pow_norm_pow_one_div_tendsto_nhds_spectral_radius (a : A) :
  tendsto (Î» n : â„•,  ennreal.of_real (â€–a ^ nâ€– ^ (1 / n : â„))) at_top (ğ“ (spectral_radius â„‚ a)) :=
begin
  convert pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius a,
  ext1,
  rw [â†of_real_rpow_of_nonneg (norm_nonneg _) _, â†coe_nnnorm, coe_nnreal_eq],
  exact one_div_nonneg.mpr (by exact_mod_cast zero_le _),
end

end gelfand_formula

section nonempty_spectrum

variables [normed_ring A] [normed_algebra â„‚ A] [complete_space A] [nontrivial A] (a : A)

/-- In a (nontrivial) complex Banach algebra, every element has nonempty spectrum. -/
protected theorem nonempty : (spectrum â„‚ a).nonempty :=
begin
  /- Suppose `Ïƒ a = âˆ…`, then resolvent set is `â„‚`, any `(z â€¢ 1 - a)` is a unit, and `resolvent`
  is differentiable on `â„‚`. -/
  rw set.nonempty_iff_ne_empty,
  by_contra h,
  have Hâ‚€ : resolvent_set â„‚ a = set.univ, by rwa [spectrum, set.compl_empty_iff] at h,
  have Hâ‚ : differentiable â„‚ (Î» z : â„‚, resolvent a z), from Î» z,
    (has_deriv_at_resolvent (Hâ‚€.symm â–¸ set.mem_univ z : z âˆˆ resolvent_set â„‚ a)).differentiable_at,
  /- The norm of the resolvent is small for all sufficently large `z`, and by compactness and
  continuity it is bounded on the complement of a large ball, thus uniformly bounded on `â„‚`.
  By Liouville's theorem `Î» z, resolvent a z` is constant -/
  have Hâ‚‚ := norm_resolvent_le_forall a,
  have Hâ‚ƒ : âˆ€ z : â„‚, resolvent a z = resolvent a (0 : â„‚),
  { refine Î» z, Hâ‚.apply_eq_apply_of_bounded (bounded_iff_forall_norm_le.mpr _) z 0,
    rcases Hâ‚‚ 1 zero_lt_one with âŸ¨R, R_pos, hRâŸ©,
    rcases (proper_space.is_compact_closed_ball (0 : â„‚) R).exists_bound_of_continuous_on
      Hâ‚.continuous.continuous_on with âŸ¨C, hCâŸ©,
    use max C 1,
    rintros _ âŸ¨w, rflâŸ©,
    refine or.elim (em (â€–wâ€– â‰¤ R)) (Î» hw, _) (Î» hw, _),
      { exact (hC w (mem_closed_ball_zero_iff.mpr hw)).trans (le_max_left _ _) },
      { exact (hR w (not_le.mp hw).le).trans (le_max_right _ _), }, },
  /- `resolvent a 0 = 0`, which is a contradition because it isn't a unit. -/
  have Hâ‚… : resolvent a (0 : â„‚) = 0,
  { refine norm_eq_zero.mp (le_antisymm (le_of_forall_pos_le_add (Î» Îµ hÎµ, _)) (norm_nonneg _)),
    rcases Hâ‚‚ Îµ hÎµ with âŸ¨R, R_pos, hRâŸ©,
    simpa only [Hâ‚ƒ R] using (zero_add Îµ).symm.subst
      (hR R (by exact_mod_cast (real.norm_of_nonneg R_pos.lt.le).symm.le)), },
  /- `not_is_unit_zero` is where we need `nontrivial A`, it is unavoidable. -/
  exact not_is_unit_zero (Hâ‚….subst (is_unit_resolvent.mp
    (mem_resolvent_set_iff.mp (Hâ‚€.symm â–¸ set.mem_univ 0)))),
end

/-- In a complex Banach algebra, the spectral radius is always attained by some element of the
spectrum. -/
lemma exists_nnnorm_eq_spectral_radius : âˆƒ z âˆˆ spectrum â„‚ a, (â€–zâ€–â‚Š : â„â‰¥0âˆ) = spectral_radius â„‚ a :=
exists_nnnorm_eq_spectral_radius_of_nonempty (spectrum.nonempty a)

/-- In a complex Banach algebra, if every element of the spectrum has norm strictly less than
`r : â„â‰¥0`, then the spectral radius is also strictly less than `r`. -/
lemma spectral_radius_lt_of_forall_lt {r : â„â‰¥0} (hr : âˆ€ z âˆˆ spectrum â„‚ a, â€–zâ€–â‚Š < r) :
  spectral_radius â„‚ a < r :=
spectral_radius_lt_of_forall_lt_of_nonempty (spectrum.nonempty a) hr

open_locale polynomial
open polynomial

/-- The **spectral mapping theorem** for polynomials in a Banach algebra over `â„‚`. -/
lemma map_polynomial_aeval (p : â„‚[X]) :
  spectrum â„‚ (aeval a p) = (Î» k, eval k p) '' (spectrum â„‚ a) :=
map_polynomial_aeval_of_nonempty a p (spectrum.nonempty a)

/-- A specialization of the spectral mapping theorem for polynomials in a Banach algebra over `â„‚`
to monic monomials. -/
protected lemma map_pow (n : â„•) : spectrum â„‚ (a ^ n) = (Î» x, x ^ n) '' (spectrum â„‚ a) :=
by simpa only [aeval_X_pow, eval_pow, eval_X] using map_polynomial_aeval a (X ^ n)

end nonempty_spectrum

section gelfand_mazur_isomorphism

variables [normed_ring A] [normed_algebra â„‚ A] (hA : âˆ€ {a : A}, is_unit a â†” a â‰  0)
include hA

local notation `Ïƒ` := spectrum â„‚

lemma algebra_map_eq_of_mem {a : A} {z : â„‚} (h : z âˆˆ Ïƒ a) : algebra_map â„‚ A z = a :=
by rwa [mem_iff, hA, not_not, sub_eq_zero] at h

/-- **Gelfand-Mazur theorem**: For a complex Banach division algebra, the natural `algebra_map â„‚ A`
is an algebra isomorphism whose inverse is given by selecting the (unique) element of
`spectrum â„‚ a`. In addition, `algebra_map_isometry` guarantees this map is an isometry.

Note: because `normed_division_ring` requires the field `norm_mul' : âˆ€ a b, â€–a * bâ€– = â€–aâ€– * â€–bâ€–`, we
don't use this type class and instead opt for a `normed_ring` in which the nonzero elements are
precisely the units. This allows for the application of this isomorphism in broader contexts, e.g.,
to the quotient of a complex Banach algebra by a maximal ideal. In the case when `A` is actually a
`normed_division_ring`, one may fill in the argument `hA` with the lemma `is_unit_iff_ne_zero`. -/
@[simps]
noncomputable def _root_.normed_ring.alg_equiv_complex_of_complete
  [complete_space A] : â„‚ â‰ƒâ‚[â„‚] A :=
let nt : nontrivial A := âŸ¨âŸ¨1, 0, hA.mp âŸ¨âŸ¨1, 1, mul_one _, mul_one _âŸ©, rflâŸ©âŸ©âŸ© in
{ to_fun := algebra_map â„‚ A,
  inv_fun := Î» a, (@spectrum.nonempty _ _ _ _ nt a).some,
  left_inv := Î» z, by simpa only [@scalar_eq _ _ _ _ _ nt _] using
    (@spectrum.nonempty _ _ _ _ nt $ algebra_map â„‚ A z).some_mem,
  right_inv := Î» a, algebra_map_eq_of_mem @hA (@spectrum.nonempty _ _ _ _ nt a).some_mem,
  ..algebra.of_id â„‚ A }

end gelfand_mazur_isomorphism

section exp_mapping

local notation `â†‘â‚` := algebra_map ğ•œ A

/-- For `ğ•œ = â„` or `ğ•œ = â„‚`, `exp ğ•œ` maps the spectrum of `a` into the spectrum of `exp ğ•œ a`. -/
theorem exp_mem_exp [is_R_or_C ğ•œ] [normed_ring A] [normed_algebra ğ•œ A] [complete_space A]
  (a : A) {z : ğ•œ} (hz : z âˆˆ spectrum ğ•œ a) : exp ğ•œ z âˆˆ spectrum ğ•œ (exp ğ•œ a) :=
begin
  have hexpmul : exp ğ•œ a = exp ğ•œ (a - â†‘â‚ z) * â†‘â‚ (exp ğ•œ z),
  { rw [algebra_map_exp_comm z, â†exp_add_of_commute (algebra.commutes z (a - â†‘â‚z)).symm,
      sub_add_cancel] },
  let b := âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚z) ^ n,
  have hb : summable (Î» n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚z) ^ n),
  { refine summable_of_norm_bounded_eventually _ (real.summable_pow_div_factorial â€–a - â†‘â‚zâ€–) _,
    filter_upwards [filter.eventually_cofinite_ne 0] with n hn,
    rw [norm_smul, mul_comm, norm_inv, is_R_or_C.norm_eq_abs, is_R_or_C.abs_cast_nat,
      â†div_eq_mul_inv],
    exact div_le_div (pow_nonneg (norm_nonneg _) n) (norm_pow_le' (a - â†‘â‚z) (zero_lt_iff.mpr hn))
      (by exact_mod_cast nat.factorial_pos n)
      (by exact_mod_cast nat.factorial_le (lt_add_one n).le) },
  have hâ‚€ : âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚z) ^ (n + 1) = (a - â†‘â‚z) * b,
    { simpa only [mul_smul_comm, pow_succ] using hb.tsum_mul_left (a - â†‘â‚z) },
  have hâ‚ : âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚z) ^ (n + 1) = b * (a - â†‘â‚z),
    { simpa only [pow_succ', algebra.smul_mul_assoc] using hb.tsum_mul_right (a - â†‘â‚z) },
  have hâ‚ƒ : exp ğ•œ (a - â†‘â‚z) = 1 + (a - â†‘â‚z) * b,
  { rw exp_eq_tsum,
    convert tsum_eq_zero_add (exp_series_summable' (a - â†‘â‚z)),
    simp only [nat.factorial_zero, nat.cast_one, inv_one, pow_zero, one_smul],
    exact hâ‚€.symm },
  rw [spectrum.mem_iff, is_unit.sub_iff, â†one_mul (â†‘â‚(exp ğ•œ z)), hexpmul, â†_root_.sub_mul,
    commute.is_unit_mul_iff (algebra.commutes (exp ğ•œ z) (exp ğ•œ (a - â†‘â‚z) - 1)).symm,
    sub_eq_iff_eq_add'.mpr hâ‚ƒ, commute.is_unit_mul_iff (hâ‚€ â–¸ hâ‚ : (a - â†‘â‚z) * b = b * (a - â†‘â‚z))],
  exact not_and_of_not_left _ (not_and_of_not_left _ ((not_iff_not.mpr is_unit.sub_iff).mp hz)),
end

end exp_mapping

end spectrum

namespace alg_hom

section normed_field
variables {F : Type*} [normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A] [complete_space A]
local notation `â†‘â‚` := algebra_map ğ•œ A

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). See note [lower instance priority] -/
@[priority 100]
instance [alg_hom_class F ğ•œ A ğ•œ] : continuous_linear_map_class F ğ•œ A ğ•œ :=
{ map_continuous := Î» Ï†, add_monoid_hom_class.continuous_of_bound Ï† â€–(1 : A)â€– $
    Î» a, (mul_comm â€–aâ€– â€–(1 : A)â€–) â–¸ spectrum.norm_le_norm_mul_of_mem (apply_mem_spectrum Ï† _),
  .. alg_hom_class.linear_map_class }

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). -/
def to_continuous_linear_map (Ï† : A â†’â‚[ğ•œ] ğ•œ) : A â†’L[ğ•œ] ğ•œ :=
{ cont := map_continuous Ï†, .. Ï†.to_linear_map }

@[simp] lemma coe_to_continuous_linear_map (Ï† : A â†’â‚[ğ•œ] ğ•œ) :
  â‡‘Ï†.to_continuous_linear_map = Ï† := rfl

lemma norm_apply_le_self_mul_norm_one [alg_hom_class F ğ•œ A ğ•œ] (f : F) (a : A) :
  â€–f aâ€– â‰¤ â€–aâ€– * â€–(1 : A)â€– :=
spectrum.norm_le_norm_mul_of_mem (apply_mem_spectrum f _)

lemma norm_apply_le_self [norm_one_class A] [alg_hom_class F ğ•œ A ğ•œ] (f : F) (a : A) : â€–f aâ€– â‰¤ â€–aâ€– :=
spectrum.norm_le_norm_of_mem (apply_mem_spectrum f _)

end normed_field

section nontrivially_normed_field
variables [nontrivially_normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A] [complete_space A]
local notation `â†‘â‚` := algebra_map ğ•œ A

@[simp] lemma to_continuous_linear_map_norm [norm_one_class A] (Ï† : A â†’â‚[ğ•œ] ğ•œ) :
  â€–Ï†.to_continuous_linear_mapâ€– = 1 :=
continuous_linear_map.op_norm_eq_of_bounds zero_le_one
  (Î» a, (one_mul â€–aâ€–).symm â–¸ spectrum.norm_le_norm_of_mem (apply_mem_spectrum Ï† _))
  (Î» _ _ h, by simpa only [coe_to_continuous_linear_map, map_one, norm_one, mul_one] using h 1)

end nontrivially_normed_field

end alg_hom

namespace weak_dual

namespace character_space

variables [nontrivially_normed_field ğ•œ] [normed_ring A] [complete_space A]
variables [normed_algebra ğ•œ A]

/-- The equivalence between characters and algebra homomorphisms into the base field. -/
def equiv_alg_hom : (character_space ğ•œ A) â‰ƒ (A â†’â‚[ğ•œ] ğ•œ)  :=
{ to_fun := to_alg_hom,
  inv_fun := Î» f,
  { val := f.to_continuous_linear_map,
    property := by { rw eq_set_map_one_map_mul, exact âŸ¨map_one f, map_mul fâŸ© } },
  left_inv := Î» f, subtype.ext $ continuous_linear_map.ext $ Î» x, rfl,
  right_inv := Î» f, alg_hom.ext $ Î» x, rfl }

@[simp] lemma equiv_alg_hom_coe (f : character_space ğ•œ A) : â‡‘(equiv_alg_hom f) = f := rfl

@[simp] lemma equiv_alg_hom_symm_coe  (f : A â†’â‚[ğ•œ] ğ•œ) : â‡‘(equiv_alg_hom.symm f) = f := rfl

end character_space

end weak_dual
