/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import algebra.algebra.spectrum
import analysis.special_functions.pow
import analysis.special_functions.exponential
import analysis.complex.liouville
import analysis.analytic.radius_liminf
import topology.algebra.module.character_space
/-!
# The spectrum of elements in a complete normed algebra

This file contains the basic theory for the resolvent and spectrum of a Banach algebra.

## Main definitions

* `spectral_radius : ‚Ñù‚â•0‚àû`: supremum of `‚à•k‚à•‚Çä` for all `k ‚àà spectrum ùïú a`
* `normed_ring.alg_equiv_complex_of_complete`: **Gelfand-Mazur theorem** For a complex
  Banach division algebra, the natural `algebra_map ‚ÑÇ A` is an algebra isomorphism whose inverse
  is given by selecting the (unique) element of `spectrum ‚ÑÇ a`

## Main statements

* `spectrum.is_open_resolvent_set`: the resolvent set is open.
* `spectrum.is_closed`: the spectrum is closed.
* `spectrum.subset_closed_ball_norm`: the spectrum is a subset of closed disk of radius
  equal to the norm.
* `spectrum.is_compact`: the spectrum is compact.
* `spectrum.spectral_radius_le_nnnorm`: the spectral radius is bounded above by the norm.
* `spectrum.has_deriv_at_resolvent`: the resolvent function is differentiable on the resolvent set.
* `spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius`: Gelfand's formula for the
  spectral radius in Banach algebras over `‚ÑÇ`.
* `spectrum.nonempty`: the spectrum of any element in a complex Banach algebra is nonempty.


## TODO

* compute all derivatives of `resolvent a`.

-/

open_locale ennreal

--prereqs

open filter

open_locale nnreal

lemma nnreal.eventually_pow_one_div_le (x : ‚Ñù‚â•0) {Œµ : ‚Ñù‚â•0} (hŒµ : 1 < Œµ) :
  ‚àÄ·∂† (n : ‚Ñï) in at_top, x ^ (1 / n : ‚Ñù) ‚â§ Œµ :=
begin
  obtain ‚ü®m, hm‚ü© := add_one_pow_unbounded_of_pos x (tsub_pos_of_lt hŒµ),
  rw [tsub_add_cancel_of_le hŒµ.le] at hm,
  refine eventually_at_top.2 ‚ü®m + 1, Œª n hn, _‚ü©,
  simpa only [nnreal.rpow_one_div_le_iff (nat.cast_pos.2 $ m.succ_pos.trans_le hn),
    nnreal.rpow_nat_cast] using hm.le.trans (pow_le_pow hŒµ.le (m.le_succ.trans hn)),
end

lemma ennreal.eventually_pow_one_div_le {x : ‚Ñù‚â•0‚àû} (hx : x ‚â† ‚àû) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : 1 < Œµ) :
  ‚àÄ·∂† (n : ‚Ñï) in at_top, x ^ (1 / n : ‚Ñù) ‚â§ Œµ :=
begin
  lift x to ‚Ñù‚â•0 using hx,
  by_cases Œµ = ‚àû,
  refine eventually_of_forall (Œª n, h.symm ‚ñ∏ le_top),
  lift Œµ to ‚Ñù‚â•0 using h,
  have := nnreal.eventually_pow_one_div_le x (by exact_mod_cast hŒµ : 1 < Œµ),
  refine this.congr (eventually_of_forall $ Œª n, _),
  rw [ennreal.coe_rpow_of_nonneg x (by positivity : 0 ‚â§ (1 / n : ‚Ñù)), ennreal.coe_le_coe],
end


--prereqs


/-- The *spectral radius* is the supremum of the `nnnorm` (`‚à•‚¨ù‚à•‚Çä`) of elements in the spectrum,
    coerced into an element of `‚Ñù‚â•0‚àû`. Note that it is possible for `spectrum ùïú a = ‚àÖ`. In this
    case, `spectral_radius a = 0`.  It is also possible that `spectrum ùïú a` be unbounded (though
    not for Banach algebras, see `spectrum.is_bounded`, below).  In this case,
    `spectral_radius a = ‚àû`. -/
noncomputable def spectral_radius (ùïú : Type*) {A : Type*} [normed_field ùïú] [ring A]
  [algebra ùïú A] (a : A) : ‚Ñù‚â•0‚àû :=
‚®Ü k ‚àà spectrum ùïú a, ‚à•k‚à•‚Çä

variables {ùïú : Type*} {A : Type*}

namespace spectrum

section spectrum_compact

variables [nontrivially_normed_field ùïú] [normed_ring A] [normed_algebra ùïú A]

local notation `œÉ` := spectrum ùïú
local notation `œÅ` := resolvent_set ùïú
local notation `‚Üë‚Çê` := algebra_map ùïú A

@[simp] lemma spectral_radius.of_subsingleton [subsingleton A] (a : A) :
  spectral_radius ùïú a = 0 :=
by simp [spectral_radius]

@[simp] lemma spectral_radius_zero : spectral_radius ùïú (0 : A) = 0 :=
by { nontriviality A, simp [spectral_radius] }

lemma mem_resolvent_set_of_spectral_radius_lt {a : A} {k : ùïú} (h : spectral_radius ùïú a < ‚à•k‚à•‚Çä) :
  k ‚àà œÅ a :=
not_not.mp $ Œª hn, h.not_le $ le_supr‚ÇÇ k hn

variable [complete_space A]

lemma is_open_resolvent_set (a : A) : is_open (œÅ a) :=
units.is_open.preimage ((continuous_algebra_map ùïú A).sub continuous_const)

protected lemma is_closed (a : A) : is_closed (œÉ a) :=
(is_open_resolvent_set a).is_closed_compl

lemma mem_resolvent_set_of_norm_lt_mul {a : A} {k : ùïú} (h : ‚à•a‚à• * ‚à•(1 : A)‚à• < ‚à•k‚à•) :
  k ‚àà œÅ a :=
begin
  rw [resolvent_set, set.mem_set_of_eq, algebra.algebra_map_eq_smul_one],
  nontriviality A,
  have hk : k ‚â† 0,
    from ne_zero_of_norm_ne_zero ((mul_nonneg (norm_nonneg _) (norm_nonneg _)).trans_lt h).ne',
  let ku := units.map (‚Üë‚Çê).to_monoid_hom (units.mk0 k hk),
  rw [‚Üêinv_inv (‚à•(1 : A)‚à•), mul_inv_lt_iff (inv_pos.2 $ norm_pos_iff.2 (one_ne_zero : (1 : A) ‚â† 0))]
    at h,
  have hku : ‚à•-a‚à• < ‚à•(‚Üëku‚Åª¬π:A)‚à•‚Åª¬π := by simpa [ku, norm_algebra_map] using h,
  simpa [ku, sub_eq_add_neg, algebra.algebra_map_eq_smul_one] using (ku.add (-a) hku).is_unit,
end

lemma mem_resolvent_set_of_norm_lt [norm_one_class A] {a : A} {k : ùïú} (h : ‚à•a‚à• < ‚à•k‚à•) :
  k ‚àà œÅ a :=
mem_resolvent_set_of_norm_lt_mul (by rwa [norm_one, mul_one])

lemma norm_le_norm_mul_of_mem {a : A} {k : ùïú} (hk : k ‚àà œÉ a) :
  ‚à•k‚à• ‚â§ ‚à•a‚à• * ‚à•(1 : A)‚à• :=
le_of_not_lt $ mt mem_resolvent_set_of_norm_lt_mul hk

lemma norm_le_norm_of_mem [norm_one_class A] {a : A} {k : ùïú} (hk : k ‚àà œÉ a) :
  ‚à•k‚à• ‚â§ ‚à•a‚à• :=
le_of_not_lt $ mt mem_resolvent_set_of_norm_lt hk

lemma subset_closed_ball_norm_mul (a : A) :
  œÉ a ‚äÜ metric.closed_ball (0 : ùïú) (‚à•a‚à• * ‚à•(1 : A)‚à•) :=
Œª k hk, by simp [norm_le_norm_mul_of_mem hk]

lemma subset_closed_ball_norm [norm_one_class A] (a : A) :
  œÉ a ‚äÜ metric.closed_ball (0 : ùïú) (‚à•a‚à•) :=
Œª k hk, by simp [norm_le_norm_of_mem hk]

lemma is_bounded (a : A) : metric.bounded (œÉ a) :=
(metric.bounded_iff_subset_ball 0).mpr ‚ü®‚à•a‚à• * ‚à•(1 : A)‚à•, subset_closed_ball_norm_mul a‚ü©

protected theorem is_compact [proper_space ùïú] (a : A) : is_compact (œÉ a) :=
metric.is_compact_of_is_closed_bounded (spectrum.is_closed a) (is_bounded a)

theorem spectral_radius_le_nnnorm [norm_one_class A] (a : A) :
  spectral_radius ùïú a ‚â§ ‚à•a‚à•‚Çä :=
by { refine supr‚ÇÇ_le (Œª k hk, _), exact_mod_cast norm_le_norm_of_mem hk }

open ennreal polynomial

variable (ùïú)
theorem spectral_radius_le_pow_nnnorm_pow_one_div (a : A) (n : ‚Ñï) :
  spectral_radius ùïú a ‚â§ (‚à•a ^ (n + 1)‚à•‚Çä) ^ (1 / (n + 1) : ‚Ñù) * (‚à•(1 : A)‚à•‚Çä) ^ (1 / (n + 1) : ‚Ñù) :=
begin
  refine supr‚ÇÇ_le (Œª k hk, _),
  /- apply easy direction of the spectral mapping theorem for polynomials -/
  have pow_mem : k ^ (n + 1) ‚àà œÉ (a ^ (n + 1)),
    by simpa only [one_mul, algebra.algebra_map_eq_smul_one, one_smul, aeval_monomial, one_mul,
      eval_monomial] using subset_polynomial_aeval a (monomial (n + 1) (1 : ùïú)) ‚ü®k, hk, rfl‚ü©,
  /- power of the norm is bounded by norm of the power -/
  have nnnorm_pow_le : (‚Üë(‚à•k‚à•‚Çä ^ (n + 1)) : ‚Ñù‚â•0‚àû) ‚â§ ‚à•a ^ (n + 1)‚à•‚Çä * ‚à•(1 : A)‚à•‚Çä,
    { simpa only [real.to_nnreal_mul (norm_nonneg _), norm_to_nnreal, nnnorm_pow k (n + 1),
        ennreal.coe_mul] using coe_mono (real.to_nnreal_mono (norm_le_norm_mul_of_mem pow_mem)) },
  /- take (n + 1)·µó ∞ roots and clean up the left-hand side -/
  have hn : 0 < ((n + 1 : ‚Ñï) : ‚Ñù), by exact_mod_cast nat.succ_pos',
  convert monotone_rpow_of_nonneg (one_div_pos.mpr hn).le nnnorm_pow_le,
  erw [coe_pow, ‚Üêrpow_nat_cast, ‚Üêrpow_mul, mul_one_div_cancel hn.ne', rpow_one],
  rw [nat.cast_succ, ennreal.coe_mul_rpow],
end

theorem spectral_radius_le_liminf_pow_nnnorm_pow_one_div (a : A) :
  spectral_radius ùïú a ‚â§ at_top.liminf (Œª n : ‚Ñï, (‚à•a ^ n‚à•‚Çä : ‚Ñù‚â•0‚àû) ^ (1 / n : ‚Ñù)) :=
begin
  refine ennreal.le_of_forall_lt_one_mul_le (Œª Œµ hŒµ, _),
  by_cases Œµ = 0,
  { simp only [h, zero_mul, zero_le'] },
  have hŒµ' : Œµ‚Åª¬π ‚â† ‚àû,
    from Œª h', h (by simpa only [inv_inv, inv_top] using congr_arg (Œª (x : ‚Ñù‚â•0‚àû), x‚Åª¬π) h'),
  simp only [ennreal.mul_le_iff_le_inv h (hŒµ.trans_le le_top).ne, mul_comm Œµ‚Åª¬π,
    liminf_eq_supr_infi_of_nat', ennreal.supr_mul, ennreal.infi_mul hŒµ'],
  rw [‚Üêennreal.inv_lt_inv, inv_one] at hŒµ,
  obtain ‚ü®N, hN‚ü© := eventually_at_top.mp (ennreal.eventually_pow_one_div_le (ennreal.coe_ne_top : ‚Üë‚à•(1 : A)‚à•‚Çä ‚â† ‚àû) hŒµ),
  refine (le_trans _ (le_supr _ (N + 1))),
  refine le_infi (Œª n, _),
  simp only [‚Üêadd_assoc],
  refine (spectral_radius_le_pow_nnnorm_pow_one_div ùïú a (n + N)).trans _,
  norm_cast,
  exact mul_le_mul_left' (hN (n + N + 1) (by linarith)) _,
end

end spectrum_compact

section resolvent

open filter asymptotics

variables [nontrivially_normed_field ùïú] [normed_ring A] [normed_algebra ùïú A] [complete_space A]

local notation `œÅ` := resolvent_set ùïú
local notation `‚Üë‚Çê` := algebra_map ùïú A

theorem has_deriv_at_resolvent {a : A} {k : ùïú} (hk : k ‚àà œÅ a) :
  has_deriv_at (resolvent a) (-(resolvent a k) ^ 2) k :=
begin
  have H‚ÇÅ : has_fderiv_at ring.inverse _ (‚Üë‚Çêk - a) := has_fderiv_at_ring_inverse hk.unit,
  have H‚ÇÇ : has_deriv_at (Œª k, ‚Üë‚Çêk - a) 1 k,
  { simpa using (algebra.linear_map ùïú A).has_deriv_at.sub_const a },
  simpa [resolvent, sq, hk.unit_spec, ‚Üê ring.inverse_unit hk.unit] using H‚ÇÅ.comp_has_deriv_at k H‚ÇÇ,
end

/- TODO: Once there is sufficient API for bornology, we should get a nice filter / asymptotics
version of this, for example: `tendsto (resolvent a) (cobounded ùïú) (ùìù 0)` or more specifically
`(resolvent a) =O[cobounded ùïú] (Œª z, z‚Åª¬π)`. -/
lemma norm_resolvent_le_forall (a : A) :
  ‚àÄ Œµ > 0, ‚àÉ R > 0, ‚àÄ z : ùïú, R ‚â§ ‚à•z‚à• ‚Üí ‚à•resolvent a z‚à• ‚â§ Œµ :=
begin
  obtain ‚ü®c, c_pos, hc‚ü© := (@normed_ring.inverse_one_sub_norm A _ _).exists_pos,
  rw [is_O_with_iff, eventually_iff, metric.mem_nhds_iff] at hc,
  rcases hc with ‚ü®Œ¥, Œ¥_pos, hŒ¥‚ü©,
  simp only [cstar_ring.norm_one, mul_one] at hŒ¥,
  intros Œµ hŒµ,
  have ha‚ÇÅ : 0 < ‚à•a‚à• + 1 := lt_of_le_of_lt (norm_nonneg a) (lt_add_one _),
  have min_pos : 0 < min (Œ¥ * (‚à•a‚à• + 1)‚Åª¬π) (Œµ * c‚Åª¬π),
    from lt_min (mul_pos Œ¥_pos (inv_pos.mpr ha‚ÇÅ)) (mul_pos hŒµ (inv_pos.mpr c_pos)),
  refine ‚ü®(min (Œ¥ * (‚à•a‚à• + 1)‚Åª¬π) (Œµ * c‚Åª¬π))‚Åª¬π, inv_pos.mpr min_pos, (Œª z hz, _)‚ü©,
  have hnz : z ‚â† 0 := norm_pos_iff.mp (lt_of_lt_of_le (inv_pos.mpr min_pos) hz),
  replace hz := inv_le_of_inv_le min_pos hz,
  rcases (‚ü®units.mk0 z hnz, units.coe_mk0 hnz‚ü© : is_unit z) with ‚ü®z, rfl‚ü©,
  have lt_Œ¥ : ‚à•z‚Åª¬π ‚Ä¢ a‚à• < Œ¥,
  { rw [units.smul_def, norm_smul, units.coe_inv, norm_inv],
    calc ‚à•(z : ùïú)‚à•‚Åª¬π * ‚à•a‚à• ‚â§ Œ¥ * (‚à•a‚à• + 1)‚Åª¬π * ‚à•a‚à•
        : mul_le_mul_of_nonneg_right (hz.trans (min_le_left _ _)) (norm_nonneg _)
    ...                   < Œ¥
        : by { conv { rw mul_assoc, to_rhs, rw (mul_one Œ¥).symm },
               exact mul_lt_mul_of_pos_left
                 ((inv_mul_lt_iff ha‚ÇÅ).mpr ((mul_one (‚à•a‚à• + 1)).symm ‚ñ∏ (lt_add_one _))) Œ¥_pos } },
  rw [‚Üêinv_smul_smul z (resolvent a (z : ùïú)), units_smul_resolvent_self, resolvent,
    algebra.algebra_map_eq_smul_one, one_smul, units.smul_def, norm_smul, units.coe_inv, norm_inv],
  calc _ ‚â§ Œµ * c‚Åª¬π * c : mul_le_mul (hz.trans (min_le_right _ _)) (hŒ¥ (mem_ball_zero_iff.mpr lt_Œ¥))
                           (norm_nonneg _) (mul_pos hŒµ (inv_pos.mpr c_pos)).le
  ...    = _           : inv_mul_cancel_right‚ÇÄ c_pos.ne.symm Œµ,
end

end resolvent

section one_sub_smul

open continuous_multilinear_map ennreal formal_multilinear_series
open_locale nnreal ennreal

variables
[nontrivially_normed_field ùïú] [normed_ring A] [normed_algebra ùïú A]

variable (ùïú)
/-- In a Banach algebra `A` over a nontrivially normed field `ùïú`, for any `a : A` the
power series with coefficients `a ^ n` represents the function `(1 - z ‚Ä¢ a)‚Åª¬π` in a disk of
radius `‚à•a‚à•‚Çä‚Åª¬π`. -/
lemma has_fpower_series_on_ball_inverse_one_sub_smul [complete_space A] (a : A) :
  has_fpower_series_on_ball (Œª z : ùïú, ring.inverse (1 - z ‚Ä¢ a))
    (Œª n, continuous_multilinear_map.mk_pi_field ùïú (fin n) (a ^ n)) 0 (‚à•a‚à•‚Çä)‚Åª¬π :=
{ r_le :=
  begin
    refine le_of_forall_nnreal_lt (Œª r hr, le_radius_of_bound_nnreal _ (max 1 ‚à•(1 : A)‚à•‚Çä) (Œª n, _)),
    rw [‚Üênorm_to_nnreal, norm_mk_pi_field, norm_to_nnreal],
    cases n,
    { simp only [le_refl, mul_one, or_true, le_max_iff, pow_zero] },
    { refine le_trans (le_trans (mul_le_mul_right' (nnnorm_pow_le' a n.succ_pos) (r ^ n.succ)) _)
        (le_max_left _ _),
      { by_cases ‚à•a‚à•‚Çä = 0,
        { simp only [h, zero_mul, zero_le', pow_succ], },
        { rw [‚Üêcoe_inv h, coe_lt_coe, nnreal.lt_inv_iff_mul_lt h] at hr,
          simpa only [‚Üêmul_pow, mul_comm] using pow_le_one' hr.le n.succ } } }
  end,
  r_pos := ennreal.inv_pos.mpr coe_ne_top,
  has_sum := Œª y hy,
  begin
    have norm_lt : ‚à•y ‚Ä¢ a‚à• < 1,
    { by_cases h : ‚à•a‚à•‚Çä = 0,
      { simp only [nnnorm_eq_zero.mp h, norm_zero, zero_lt_one, smul_zero] },
      { have nnnorm_lt : ‚à•y‚à•‚Çä < ‚à•a‚à•‚Çä‚Åª¬π,
          by simpa only [‚Üêcoe_inv h, mem_ball_zero_iff, metric.emetric_ball_nnreal] using hy,
        rwa [‚Üêcoe_nnnorm, ‚Üêreal.lt_to_nnreal_iff_coe_lt, real.to_nnreal_one, nnnorm_smul,
          ‚Üênnreal.lt_inv_iff_mul_lt h] } },
    simpa [‚Üêsmul_pow, (normed_ring.summable_geometric_of_norm_lt_1 _ norm_lt).has_sum_iff]
      using (normed_ring.inverse_one_sub _ norm_lt).symm,
  end }

variable {ùïú}
lemma is_unit_one_sub_smul_of_lt_inv_radius {a : A} {z : ùïú} (h : ‚Üë‚à•z‚à•‚Çä < (spectral_radius ùïú a)‚Åª¬π) :
  is_unit (1 - z ‚Ä¢ a) :=
begin
  by_cases hz : z = 0,
  { simp only [hz, is_unit_one, sub_zero, zero_smul] },
  { let u := units.mk0 z hz,
    suffices hu : is_unit (u‚Åª¬π ‚Ä¢ 1 - a),
    { rwa [is_unit.smul_sub_iff_sub_inv_smul, inv_inv u] at hu },
    { rw [units.smul_def, ‚Üêalgebra.algebra_map_eq_smul_one, ‚Üêmem_resolvent_set_iff],
      refine mem_resolvent_set_of_spectral_radius_lt _,
      rwa [units.coe_inv, nnnorm_inv, coe_inv (nnnorm_ne_zero_iff.mpr
        (units.coe_mk0 hz ‚ñ∏ hz : (u : ùïú) ‚â† 0)), lt_inv_iff_lt_inv] } }
end

/-- In a Banach algebra `A` over `ùïú`, for `a : A` the function `Œª z, (1 - z ‚Ä¢ a)‚Åª¬π` is
differentiable on any closed ball centered at zero of radius `r < (spectral_radius ùïú a)‚Åª¬π`. -/
theorem differentiable_on_inverse_one_sub_smul [complete_space A] {a : A} {r : ‚Ñù‚â•0}
  (hr : (r : ‚Ñù‚â•0‚àû) < (spectral_radius ùïú a)‚Åª¬π) :
  differentiable_on ùïú (Œª z : ùïú, ring.inverse (1 - z ‚Ä¢ a)) (metric.closed_ball 0 r) :=
begin
  intros z z_mem,
  apply differentiable_at.differentiable_within_at,
  have hu : is_unit (1 - z ‚Ä¢ a),
  { refine is_unit_one_sub_smul_of_lt_inv_radius (lt_of_le_of_lt (coe_mono _) hr),
    simpa only [norm_to_nnreal, real.to_nnreal_coe]
      using real.to_nnreal_mono (mem_closed_ball_zero_iff.mp z_mem) },
  have H‚ÇÅ : differentiable ùïú (Œª w : ùïú, 1 - w ‚Ä¢ a) := (differentiable_id.smul_const a).const_sub 1,
  exact differentiable_at.comp z (differentiable_at_inverse hu.unit) (H‚ÇÅ.differentiable_at),
end

end one_sub_smul

section gelfand_formula

open filter ennreal continuous_multilinear_map
open_locale topological_space

variables
[normed_ring A] [normed_algebra ‚ÑÇ A] [complete_space A]

/-- The `limsup` relationship for the spectral radius used to prove `spectrum.gelfand_formula`. -/
lemma limsup_pow_nnnorm_pow_one_div_le_spectral_radius (a : A) :
  limsup at_top (Œª n : ‚Ñï, ‚Üë‚à•a ^ n‚à•‚Çä ^ (1 / n : ‚Ñù)) ‚â§ spectral_radius ‚ÑÇ a :=
begin
  refine ennreal.inv_le_inv.mp (le_of_forall_pos_nnreal_lt (Œª r r_pos r_lt, _)),
  simp_rw [inv_limsup, ‚Üêone_div],
  let p : formal_multilinear_series ‚ÑÇ ‚ÑÇ A :=
    Œª n, continuous_multilinear_map.mk_pi_field ‚ÑÇ (fin n) (a ^ n),
  suffices h : (r : ‚Ñù‚â•0‚àû) ‚â§ p.radius,
  { convert h,
    simp only [p.radius_eq_liminf, ‚Üênorm_to_nnreal, norm_mk_pi_field],
    refine congr_arg _ (funext (Œª n, congr_arg _ _)),
    rw [norm_to_nnreal, ennreal.coe_rpow_def (‚à•a ^ n‚à•‚Çä) (1 / n : ‚Ñù), if_neg],
    exact Œª ha, by linarith [ha.2, (one_div_nonneg.mpr n.cast_nonneg : 0 ‚â§ (1 / n : ‚Ñù))], },
  { have H‚ÇÅ := (differentiable_on_inverse_one_sub_smul r_lt).has_fpower_series_on_ball r_pos,
    exact ((has_fpower_series_on_ball_inverse_one_sub_smul ‚ÑÇ a).exchange_radius H‚ÇÅ).r_le, }
end

/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectral_radius` of `a` is the limit of the sequence `‚à•a ^ n‚à•‚Çä ^ (1 / n)` -/
theorem pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (a : A) :
  tendsto (Œª n : ‚Ñï, ((‚à•a ^ n‚à•‚Çä ^ (1 / n : ‚Ñù)) : ‚Ñù‚â•0‚àû)) at_top (ùìù (spectral_radius ‚ÑÇ a)) :=
tendsto_of_le_liminf_of_limsup_le (spectral_radius_le_liminf_pow_nnnorm_pow_one_div ‚ÑÇ a)
  (limsup_pow_nnnorm_pow_one_div_le_spectral_radius a)

/- This is the same as `pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius` but for `norm`
instead of `nnnorm`. -/
/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectral_radius` of `a` is the limit of the sequence `‚à•a ^ n‚à•‚Çä ^ (1 / n)` -/
theorem pow_norm_pow_one_div_tendsto_nhds_spectral_radius (a : A) :
  tendsto (Œª n : ‚Ñï,  ennreal.of_real (‚à•a ^ n‚à• ^ (1 / n : ‚Ñù))) at_top (ùìù (spectral_radius ‚ÑÇ a)) :=
begin
  convert pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius a,
  ext1,
  rw [‚Üêof_real_rpow_of_nonneg (norm_nonneg _) _, ‚Üêcoe_nnnorm, coe_nnreal_eq],
  exact one_div_nonneg.mpr (by exact_mod_cast zero_le _),
end

end gelfand_formula

/-- In a (nontrivial) complex Banach algebra, every element has nonempty spectrum. -/
theorem nonempty {A : Type*} [normed_ring A] [normed_algebra ‚ÑÇ A] [complete_space A]
  [nontrivial A]
  (a : A) : (spectrum ‚ÑÇ a).nonempty :=
begin
  /- Suppose `œÉ a = ‚àÖ`, then resolvent set is `‚ÑÇ`, any `(z ‚Ä¢ 1 - a)` is a unit, and `resolvent`
  is differentiable on `‚ÑÇ`. -/
  rw ‚Üêset.ne_empty_iff_nonempty,
  by_contra h,
  have H‚ÇÄ : resolvent_set ‚ÑÇ a = set.univ, by rwa [spectrum, set.compl_empty_iff] at h,
  have H‚ÇÅ : differentiable ‚ÑÇ (Œª z : ‚ÑÇ, resolvent a z), from Œª z,
    (has_deriv_at_resolvent (H‚ÇÄ.symm ‚ñ∏ set.mem_univ z : z ‚àà resolvent_set ‚ÑÇ a)).differentiable_at,
  /- The norm of the resolvent is small for all sufficently large `z`, and by compactness and
  continuity it is bounded on the complement of a large ball, thus uniformly bounded on `‚ÑÇ`.
  By Liouville's theorem `Œª z, resolvent a z` is constant -/
  have H‚ÇÇ := norm_resolvent_le_forall a,
  have H‚ÇÉ : ‚àÄ z : ‚ÑÇ, resolvent a z = resolvent a (0 : ‚ÑÇ),
  { refine Œª z, H‚ÇÅ.apply_eq_apply_of_bounded (bounded_iff_exists_norm_le.mpr _) z 0,
    rcases H‚ÇÇ 1 zero_lt_one with ‚ü®R, R_pos, hR‚ü©,
    rcases (proper_space.is_compact_closed_ball (0 : ‚ÑÇ) R).exists_bound_of_continuous_on
      H‚ÇÅ.continuous.continuous_on with ‚ü®C, hC‚ü©,
    use max C 1,
    rintros _ ‚ü®w, rfl‚ü©,
    refine or.elim (em (‚à•w‚à• ‚â§ R)) (Œª hw, _) (Œª hw, _),
      { exact (hC w (mem_closed_ball_zero_iff.mpr hw)).trans (le_max_left _ _) },
      { exact (hR w (not_le.mp hw).le).trans (le_max_right _ _), }, },
  /- `resolvent a 0 = 0`, which is a contradition because it isn't a unit. -/
  have H‚ÇÖ : resolvent a (0 : ‚ÑÇ) = 0,
  { refine norm_eq_zero.mp (le_antisymm (le_of_forall_pos_le_add (Œª Œµ hŒµ, _)) (norm_nonneg _)),
    rcases H‚ÇÇ Œµ hŒµ with ‚ü®R, R_pos, hR‚ü©,
    simpa only [H‚ÇÉ R] using (zero_add Œµ).symm.subst
      (hR R (by exact_mod_cast (real.norm_of_nonneg R_pos.lt.le).symm.le)), },
  /- `not_is_unit_zero` is where we need `nontrivial A`, it is unavoidable. -/
  exact not_is_unit_zero (H‚ÇÖ.subst (is_unit_resolvent.mp
    (mem_resolvent_set_iff.mp (H‚ÇÄ.symm ‚ñ∏ set.mem_univ 0)))),
end

section gelfand_mazur_isomorphism

variables [normed_ring A] [normed_algebra ‚ÑÇ A] (hA : ‚àÄ {a : A}, is_unit a ‚Üî a ‚â† 0)
include hA

local notation `œÉ` := spectrum ‚ÑÇ

lemma algebra_map_eq_of_mem {a : A} {z : ‚ÑÇ} (h : z ‚àà œÉ a) : algebra_map ‚ÑÇ A z = a :=
by rwa [mem_iff, hA, not_not, sub_eq_zero] at h

/-- **Gelfand-Mazur theorem**: For a complex Banach division algebra, the natural `algebra_map ‚ÑÇ A`
is an algebra isomorphism whose inverse is given by selecting the (unique) element of
`spectrum ‚ÑÇ a`. In addition, `algebra_map_isometry` guarantees this map is an isometry.

Note: because `normed_division_ring` requires the field `norm_mul' : ‚àÄ a b, ‚à•a * b‚à• = ‚à•a‚à• * ‚à•b‚à•`, we
don't use this type class and instead opt for a `normed_ring` in which the nonzero elements are
precisely the units. This allows for the application of this isomorphism in broader contexts, e.g.,
to the quotient of a complex Banach algebra by a maximal ideal. In the case when `A` is actually a
`normed_division_ring`, one may fill in the argument `hA` with the lemma `is_unit_iff_ne_zero`. -/
@[simps]
noncomputable def _root_.normed_ring.alg_equiv_complex_of_complete
  [complete_space A] : ‚ÑÇ ‚âÉ‚Çê[‚ÑÇ] A :=
let nt : nontrivial A := ‚ü®‚ü®1, 0, hA.mp ‚ü®‚ü®1, 1, mul_one _, mul_one _‚ü©, rfl‚ü©‚ü©‚ü© in
{ to_fun := algebra_map ‚ÑÇ A,
  inv_fun := Œª a, (@spectrum.nonempty _ _ _ _ nt a).some,
  left_inv := Œª z, by simpa only [@scalar_eq _ _ _ _ _ nt _] using
    (@spectrum.nonempty _ _ _ _ nt $ algebra_map ‚ÑÇ A z).some_mem,
  right_inv := Œª a, algebra_map_eq_of_mem @hA (@spectrum.nonempty _ _ _ _ nt a).some_mem,
  ..algebra.of_id ‚ÑÇ A }

end gelfand_mazur_isomorphism

section exp_mapping

local notation `‚Üë‚Çê` := algebra_map ùïú A

/-- For `ùïú = ‚Ñù` or `ùïú = ‚ÑÇ`, `exp ùïú` maps the spectrum of `a` into the spectrum of `exp ùïú a`. -/
theorem exp_mem_exp [is_R_or_C ùïú] [normed_ring A] [normed_algebra ùïú A] [complete_space A]
  (a : A) {z : ùïú} (hz : z ‚àà spectrum ùïú a) : exp ùïú z ‚àà spectrum ùïú (exp ùïú a) :=
begin
  have hexpmul : exp ùïú a = exp ùïú (a - ‚Üë‚Çê z) * ‚Üë‚Çê (exp ùïú z),
  { rw [algebra_map_exp_comm z, ‚Üêexp_add_of_commute (algebra.commutes z (a - ‚Üë‚Çêz)).symm,
      sub_add_cancel] },
  let b := ‚àë' n : ‚Ñï, ((n + 1).factorial‚Åª¬π : ùïú) ‚Ä¢ (a - ‚Üë‚Çêz) ^ n,
  have hb : summable (Œª n : ‚Ñï, ((n + 1).factorial‚Åª¬π : ùïú) ‚Ä¢ (a - ‚Üë‚Çêz) ^ n),
  { refine summable_of_norm_bounded_eventually _ (real.summable_pow_div_factorial ‚à•a - ‚Üë‚Çêz‚à•) _,
    filter_upwards [filter.eventually_cofinite_ne 0] with n hn,
    rw [norm_smul, mul_comm, norm_inv, is_R_or_C.norm_eq_abs, is_R_or_C.abs_cast_nat,
      ‚Üêdiv_eq_mul_inv],
    exact div_le_div (pow_nonneg (norm_nonneg _) n) (norm_pow_le' (a - ‚Üë‚Çêz) (zero_lt_iff.mpr hn))
      (by exact_mod_cast nat.factorial_pos n)
      (by exact_mod_cast nat.factorial_le (lt_add_one n).le) },
  have h‚ÇÄ : ‚àë' n : ‚Ñï, ((n + 1).factorial‚Åª¬π : ùïú) ‚Ä¢ (a - ‚Üë‚Çêz) ^ (n + 1) = (a - ‚Üë‚Çêz) * b,
    { simpa only [mul_smul_comm, pow_succ] using hb.tsum_mul_left (a - ‚Üë‚Çêz) },
  have h‚ÇÅ : ‚àë' n : ‚Ñï, ((n + 1).factorial‚Åª¬π : ùïú) ‚Ä¢ (a - ‚Üë‚Çêz) ^ (n + 1) = b * (a - ‚Üë‚Çêz),
    { simpa only [pow_succ', algebra.smul_mul_assoc] using hb.tsum_mul_right (a - ‚Üë‚Çêz) },
  have h‚ÇÉ : exp ùïú (a - ‚Üë‚Çêz) = 1 + (a - ‚Üë‚Çêz) * b,
  { rw exp_eq_tsum,
    convert tsum_eq_zero_add (exp_series_summable' (a - ‚Üë‚Çêz)),
    simp only [nat.factorial_zero, nat.cast_one, inv_one, pow_zero, one_smul],
    exact h‚ÇÄ.symm },
  rw [spectrum.mem_iff, is_unit.sub_iff, ‚Üêone_mul (‚Üë‚Çê(exp ùïú z)), hexpmul, ‚Üê_root_.sub_mul,
    commute.is_unit_mul_iff (algebra.commutes (exp ùïú z) (exp ùïú (a - ‚Üë‚Çêz) - 1)).symm,
    sub_eq_iff_eq_add'.mpr h‚ÇÉ, commute.is_unit_mul_iff (h‚ÇÄ ‚ñ∏ h‚ÇÅ : (a - ‚Üë‚Çêz) * b = b * (a - ‚Üë‚Çêz))],
  exact not_and_of_not_left _ (not_and_of_not_left _ ((not_iff_not.mpr is_unit.sub_iff).mp hz)),
end

end exp_mapping

end spectrum

namespace alg_hom

section normed_field
variables [nontrivially_normed_field ùïú] [normed_ring A] [normed_algebra ùïú A] [complete_space A]
local notation `‚Üë‚Çê` := algebra_map ùïú A

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). -/
instance : continuous_linear_map_class (A ‚Üí‚Çê[ùïú] ùïú) ùïú A ùïú :=
{ map_continuous := Œª œÜ, add_monoid_hom_class.continuous_of_bound œÜ ‚à•(1 : A)‚à• $
    Œª a, (mul_comm ‚à•a‚à• ‚à•(1 : A)‚à•) ‚ñ∏ spectrum.norm_le_norm_mul_of_mem (apply_mem_spectrum œÜ _),
  .. alg_hom_class.linear_map_class }

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). -/
def to_continuous_linear_map (œÜ : A ‚Üí‚Çê[ùïú] ùïú) : A ‚ÜíL[ùïú] ùïú :=
{ cont := map_continuous œÜ, .. œÜ.to_linear_map }

@[simp] lemma coe_to_continuous_linear_map (œÜ : A ‚Üí‚Çê[ùïú] ùïú) :
  ‚áëœÜ.to_continuous_linear_map = œÜ := rfl

end normed_field

section nontrivially_normed_field
variables [nontrivially_normed_field ùïú] [normed_ring A] [normed_algebra ùïú A] [complete_space A]
local notation `‚Üë‚Çê` := algebra_map ùïú A

@[simp] lemma to_continuous_linear_map_norm [norm_one_class A] (œÜ : A ‚Üí‚Çê[ùïú] ùïú) :
  ‚à•œÜ.to_continuous_linear_map‚à• = 1 :=
continuous_linear_map.op_norm_eq_of_bounds zero_le_one
  (Œª a, (one_mul ‚à•a‚à•).symm ‚ñ∏ spectrum.norm_le_norm_of_mem (apply_mem_spectrum œÜ _))
  (Œª _ _ h, by simpa only [coe_to_continuous_linear_map, map_one, norm_one, mul_one] using h 1)

end nontrivially_normed_field

end alg_hom

namespace weak_dual

namespace character_space

variables [nontrivially_normed_field ùïú] [normed_ring A] [complete_space A]
variables [normed_algebra ùïú A]

/-- The equivalence between characters and algebra homomorphisms into the base field. -/
def equiv_alg_hom : (character_space ùïú A) ‚âÉ (A ‚Üí‚Çê[ùïú] ùïú)  :=
{ to_fun := to_alg_hom,
  inv_fun := Œª f,
  { val := f.to_continuous_linear_map,
    property := by { rw eq_set_map_one_map_mul, exact ‚ü®map_one f, map_mul f‚ü© } },
  left_inv := Œª f, subtype.ext $ continuous_linear_map.ext $ Œª x, rfl,
  right_inv := Œª f, alg_hom.ext $ Œª x, rfl }

@[simp] lemma equiv_alg_hom_coe (f : character_space ùïú A) : ‚áë(equiv_alg_hom f) = f := rfl

@[simp] lemma equiv_alg_hom_symm_coe  (f : A ‚Üí‚Çê[ùïú] ùïú) : ‚áë(equiv_alg_hom.symm f) = f := rfl

end character_space

end weak_dual
