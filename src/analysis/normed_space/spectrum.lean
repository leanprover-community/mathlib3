/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import algebra.algebra.spectrum
import analysis.special_functions.pow
import analysis.complex.liouville
import analysis.analytic.radius_liminf
/-!
# The spectrum of elements in a complete normed algebra

This file contains the basic theory for the resolvent and spectrum of a Banach algebra.

## Main definitions

* `spectral_radius : â„â‰¥0âˆ`: supremum of `âˆ¥kâˆ¥â‚Š` for all `k âˆˆ spectrum ğ•œ a`

## Main statements

* `spectrum.is_open_resolvent_set`: the resolvent set is open.
* `spectrum.is_closed`: the spectrum is closed.
* `spectrum.subset_closed_ball_norm`: the spectrum is a subset of closed disk of radius
  equal to the norm.
* `spectrum.is_compact`: the spectrum is compact.
* `spectrum.spectral_radius_le_nnnorm`: the spectral radius is bounded above by the norm.
* `spectrum.has_deriv_at_resolvent`: the resolvent function is differentiable on the resolvent set.
* `spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius`: Gelfand's formula for the
  spectral radius in Banach algebras over `â„‚`.
* `spectrum.nonempty`: the spectrum of any element in a complex Banach algebra is nonempty.


## TODO

* after we have Liouville's theorem, prove that the spectrum is nonempty when the
  scalar field is â„‚.
* compute all derivatives of `resolvent a`.

-/

open_locale ennreal

/-- The *spectral radius* is the supremum of the `nnnorm` (`âˆ¥â¬âˆ¥â‚Š`) of elements in the spectrum,
    coerced into an element of `â„â‰¥0âˆ`. Note that it is possible for `spectrum ğ•œ a = âˆ…`. In this
    case, `spectral_radius a = 0`.  It is also possible that `spectrum ğ•œ a` be unbounded (though
    not for Banach algebras, see `spectrum.is_bounded`, below).  In this case,
    `spectral_radius a = âˆ`. -/
noncomputable def spectral_radius (ğ•œ : Type*) {A : Type*} [normed_field ğ•œ] [ring A]
  [algebra ğ•œ A] (a : A) : â„â‰¥0âˆ :=
â¨† k âˆˆ spectrum ğ•œ a, âˆ¥kâˆ¥â‚Š

variables {ğ•œ : Type*} {A : Type*}

namespace spectrum

section spectrum_compact

variables [normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A]

local notation `Ïƒ` := spectrum ğ•œ
local notation `Ï` := resolvent_set ğ•œ
local notation `â†‘â‚` := algebra_map ğ•œ A

lemma mem_resolvent_set_of_spectral_radius_lt {a : A} {k : ğ•œ} (h : spectral_radius ğ•œ a < âˆ¥kâˆ¥â‚Š) :
  k âˆˆ Ï a :=
not_not.mp (Î» hn, (lt_self_iff_false _).mp (lt_of_le_of_lt (le_bsupr k hn) h))

variable [complete_space A]

lemma is_open_resolvent_set (a : A) : is_open (Ï a) :=
units.is_open.preimage ((algebra_map_isometry ğ•œ A).continuous.sub continuous_const)

lemma is_closed (a : A) : is_closed (Ïƒ a) :=
(is_open_resolvent_set a).is_closed_compl

lemma mem_resolvent_of_norm_lt {a : A} {k : ğ•œ} (h : âˆ¥aâˆ¥ < âˆ¥kâˆ¥) :
  k âˆˆ Ï a :=
begin
  rw [resolvent_set, set.mem_set_of_eq, algebra.algebra_map_eq_smul_one],
  have hk : k â‰  0 := ne_zero_of_norm_ne_zero (by linarith [norm_nonneg a]),
  let ku := units.map (â†‘â‚).to_monoid_hom (units.mk0 k hk),
  have hku : âˆ¥-aâˆ¥ < âˆ¥(â†‘kuâ»Â¹:A)âˆ¥â»Â¹ := by simpa [ku, algebra_map_isometry] using h,
  simpa [ku, sub_eq_add_neg, algebra.algebra_map_eq_smul_one] using (ku.add (-a) hku).is_unit,
end

lemma norm_le_norm_of_mem {a : A} {k : ğ•œ} (hk : k âˆˆ Ïƒ a) :
  âˆ¥kâˆ¥ â‰¤ âˆ¥aâˆ¥ :=
le_of_not_lt $ mt mem_resolvent_of_norm_lt hk

lemma subset_closed_ball_norm (a : A) :
  Ïƒ a âŠ† metric.closed_ball (0 : ğ•œ) (âˆ¥aâˆ¥) :=
Î» k hk, by simp [norm_le_norm_of_mem hk]

lemma is_bounded (a : A) : metric.bounded (Ïƒ a) :=
(metric.bounded_iff_subset_ball 0).mpr âŸ¨âˆ¥aâˆ¥, subset_closed_ball_norm aâŸ©

theorem is_compact [proper_space ğ•œ] (a : A) : is_compact (Ïƒ a) :=
metric.is_compact_of_is_closed_bounded (is_closed a) (is_bounded a)

theorem spectral_radius_le_nnnorm (a : A) :
  spectral_radius ğ•œ a â‰¤ âˆ¥aâˆ¥â‚Š :=
by { refine bsupr_le (Î» k hk, _), exact_mod_cast norm_le_norm_of_mem hk }

open ennreal polynomial

variable (ğ•œ)
theorem spectral_radius_le_pow_nnnorm_pow_one_div (a : A) (n : â„•) :
  spectral_radius ğ•œ a â‰¤ âˆ¥a ^ (n + 1)âˆ¥â‚Š ^ (1 / (n + 1) : â„) :=
begin
  refine bsupr_le (Î» k hk, _),
  /- apply easy direction of the spectral mapping theorem for polynomials -/
  have pow_mem : k ^ (n + 1) âˆˆ Ïƒ (a ^ (n + 1)),
    by simpa only [one_mul, algebra.algebra_map_eq_smul_one, one_smul, aeval_monomial, one_mul,
      eval_monomial] using subset_polynomial_aeval a (monomial (n + 1) (1 : ğ•œ)) âŸ¨k, hk, rflâŸ©,
  /- power of the norm is bounded by norm of the power -/
  have nnnorm_pow_le : (â†‘(âˆ¥kâˆ¥â‚Š ^ (n + 1)) : â„â‰¥0âˆ) â‰¤ â†‘âˆ¥a ^ (n + 1)âˆ¥â‚Š,
    by simpa only [norm_to_nnreal, nnnorm_pow k (n+1)]
      using coe_mono (real.to_nnreal_mono (norm_le_norm_of_mem pow_mem)),
  /- take (n + 1)áµ—Ê° roots and clean up the left-hand side -/
  have hn : 0 < ((n + 1) : â„), by exact_mod_cast nat.succ_pos',
  convert monotone_rpow_of_nonneg (one_div_pos.mpr hn).le nnnorm_pow_le,
  erw [coe_pow, â†rpow_nat_cast, â†rpow_mul, mul_one_div_cancel hn.ne', rpow_one],
end

end spectrum_compact

section resolvent

open filter asymptotics

variables [nondiscrete_normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A] [complete_space A]

local notation `Ï` := resolvent_set ğ•œ
local notation `â†‘â‚` := algebra_map ğ•œ A

theorem has_deriv_at_resolvent {a : A} {k : ğ•œ} (hk : k âˆˆ Ï a) :
  has_deriv_at (resolvent a) (-(resolvent a k) ^ 2) k :=
begin
  have Hâ‚ : has_fderiv_at ring.inverse _ (â†‘â‚k - a) := has_fderiv_at_ring_inverse hk.unit,
  have Hâ‚‚ : has_deriv_at (Î» k, â†‘â‚k - a) 1 k,
  { simpa using (algebra.linear_map ğ•œ A).has_deriv_at.sub_const a },
  simpa [resolvent, sq, hk.unit_spec, â† ring.inverse_unit hk.unit] using Hâ‚.comp_has_deriv_at k Hâ‚‚,
end

/- TODO: Once there is sufficient API for bornology, we should get a nice filter / asymptotics
version of this, for example: `tendsto (resolvent a) (cobounded ğ•œ) (ğ“ 0)` or more specifically
`is_O (resolvent a) (Î» z, zâ»Â¹) (cobounded ğ•œ)`. -/
lemma norm_resolvent_le_forall (a : A) :
  âˆ€ Îµ > 0, âˆƒ R > 0, âˆ€ z : ğ•œ, R â‰¤ âˆ¥zâˆ¥ â†’ âˆ¥resolvent a zâˆ¥ â‰¤ Îµ :=
begin
  obtain âŸ¨c, c_pos, hcâŸ© := (@normed_ring.inverse_one_sub_norm A _ _).exists_pos,
  rw [is_O_with_iff, eventually_iff, metric.mem_nhds_iff] at hc,
  rcases hc with âŸ¨Î´, Î´_pos, hÎ´âŸ©,
  simp only [cstar_ring.norm_one, mul_one] at hÎ´,
  intros Îµ hÎµ,
  have haâ‚ : 0 < âˆ¥aâˆ¥ + 1 := lt_of_le_of_lt (norm_nonneg a) (lt_add_one _),
  have min_pos : 0 < min (Î´ * (âˆ¥aâˆ¥ + 1)â»Â¹) (Îµ * câ»Â¹),
    from lt_min (mul_pos Î´_pos (inv_pos.mpr haâ‚)) (mul_pos hÎµ (inv_pos.mpr c_pos)),
  refine âŸ¨(min (Î´ * (âˆ¥aâˆ¥ + 1)â»Â¹) (Îµ * câ»Â¹))â»Â¹, inv_pos.mpr min_pos, (Î» z hz, _)âŸ©,
  have hnz : z â‰  0 := norm_pos_iff.mp (lt_of_lt_of_le (inv_pos.mpr min_pos) hz),
  replace hz := inv_le_of_inv_le min_pos hz,
  rcases (âŸ¨units.mk0 z hnz, units.coe_mk0 hnzâŸ© : is_unit z) with âŸ¨z, rflâŸ©,
  have lt_Î´ : âˆ¥zâ»Â¹ â€¢ aâˆ¥ < Î´,
  { rw [units.smul_def, norm_smul, units.coe_inv', norm_inv],
    calc âˆ¥(z : ğ•œ)âˆ¥â»Â¹ * âˆ¥aâˆ¥ â‰¤ Î´ * (âˆ¥aâˆ¥ + 1)â»Â¹ * âˆ¥aâˆ¥
        : mul_le_mul_of_nonneg_right (hz.trans (min_le_left _ _)) (norm_nonneg _)
    ...                   < Î´
        : by { conv { rw mul_assoc, to_rhs, rw (mul_one Î´).symm },
               exact mul_lt_mul_of_pos_left
                 ((inv_mul_lt_iff haâ‚).mpr ((mul_one (âˆ¥aâˆ¥ + 1)).symm â–¸ (lt_add_one _))) Î´_pos } },
  rw [â†inv_smul_smul z (resolvent a (z : ğ•œ)), units_smul_resolvent_self, resolvent,
    algebra.algebra_map_eq_smul_one, one_smul, units.smul_def, norm_smul, units.coe_inv', norm_inv],
  calc _ â‰¤ Îµ * câ»Â¹ * c : mul_le_mul (hz.trans (min_le_right _ _)) (hÎ´ (mem_ball_zero_iff.mpr lt_Î´))
                           (norm_nonneg _) (mul_pos hÎµ (inv_pos.mpr c_pos)).le
  ...    = _           : inv_mul_cancel_rightâ‚€ c_pos.ne.symm Îµ,
end

end resolvent

section one_sub_smul

open continuous_multilinear_map ennreal formal_multilinear_series
open_locale nnreal ennreal

variables
[nondiscrete_normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A]

variable (ğ•œ)
/-- In a Banach algebra `A` over a nondiscrete normed field `ğ•œ`, for any `a : A` the
power series with coefficients `a ^ n` represents the function `(1 - z â€¢ a)â»Â¹` in a disk of
radius `âˆ¥aâˆ¥â‚Šâ»Â¹`. -/
lemma has_fpower_series_on_ball_inverse_one_sub_smul [complete_space A] (a : A) :
  has_fpower_series_on_ball (Î» z : ğ•œ, ring.inverse (1 - z â€¢ a))
    (Î» n, continuous_multilinear_map.mk_pi_field ğ•œ (fin n) (a ^ n)) 0 (âˆ¥aâˆ¥â‚Š)â»Â¹ :=
{ r_le :=
  begin
    refine le_of_forall_nnreal_lt (Î» r hr, le_radius_of_bound_nnreal _ (max 1 âˆ¥(1 : A)âˆ¥â‚Š) (Î» n, _)),
    rw [â†norm_to_nnreal, norm_mk_pi_field, norm_to_nnreal],
    cases n,
    { simp only [le_refl, mul_one, or_true, le_max_iff, pow_zero] },
    { refine le_trans (le_trans (mul_le_mul_right' (nnnorm_pow_le' a n.succ_pos) (r ^ n.succ)) _)
        (le_max_left _ _),
      { by_cases âˆ¥aâˆ¥â‚Š = 0,
        { simp only [h, zero_mul, zero_le', pow_succ], },
        { rw [â†coe_inv h, coe_lt_coe, nnreal.lt_inv_iff_mul_lt h] at hr,
          simpa only [â†mul_pow, mul_comm] using pow_le_one' hr.le n.succ } } }
  end,
  r_pos := ennreal.inv_pos.mpr coe_ne_top,
  has_sum := Î» y hy,
  begin
    have norm_lt : âˆ¥y â€¢ aâˆ¥ < 1,
    { by_cases h : âˆ¥aâˆ¥â‚Š = 0,
      { simp only [nnnorm_eq_zero.mp h, norm_zero, zero_lt_one, smul_zero] },
      { have nnnorm_lt : âˆ¥yâˆ¥â‚Š < âˆ¥aâˆ¥â‚Šâ»Â¹,
          by simpa only [â†coe_inv h, mem_ball_zero_iff, metric.emetric_ball_nnreal] using hy,
        rwa [â†coe_nnnorm, â†real.lt_to_nnreal_iff_coe_lt, real.to_nnreal_one, nnnorm_smul,
          â†nnreal.lt_inv_iff_mul_lt h] } },
    simpa [â†smul_pow, (normed_ring.summable_geometric_of_norm_lt_1 _ norm_lt).has_sum_iff]
      using (normed_ring.inverse_one_sub _ norm_lt).symm,
  end }

variable {ğ•œ}
lemma is_unit_one_sub_smul_of_lt_inv_radius {a : A} {z : ğ•œ} (h : â†‘âˆ¥zâˆ¥â‚Š < (spectral_radius ğ•œ a)â»Â¹) :
  is_unit (1 - z â€¢ a) :=
begin
  by_cases hz : z = 0,
  { simp only [hz, is_unit_one, sub_zero, zero_smul] },
  { let u := units.mk0 z hz,
    suffices hu : is_unit (uâ»Â¹ â€¢ 1 - a),
    { rwa [is_unit.smul_sub_iff_sub_inv_smul, inv_inv u] at hu },
    { rw [units.smul_def, â†algebra.algebra_map_eq_smul_one, â†mem_resolvent_set_iff],
      refine mem_resolvent_set_of_spectral_radius_lt _,
      rwa [units.coe_inv', nnnorm_inv, coe_inv (nnnorm_ne_zero_iff.mpr
        (units.coe_mk0 hz â–¸ hz : (u : ğ•œ) â‰  0)), lt_inv_iff_lt_inv] } }
end

/-- In a Banach algebra `A` over `ğ•œ`, for `a : A` the function `Î» z, (1 - z â€¢ a)â»Â¹` is
differentiable on any closed ball centered at zero of radius `r < (spectral_radius ğ•œ a)â»Â¹`. -/
theorem differentiable_on_inverse_one_sub_smul [complete_space A] {a : A} {r : â„â‰¥0}
  (hr : (r : â„â‰¥0âˆ) < (spectral_radius ğ•œ a)â»Â¹) :
  differentiable_on ğ•œ (Î» z : ğ•œ, ring.inverse (1 - z â€¢ a)) (metric.closed_ball 0 r) :=
begin
  intros z z_mem,
  apply differentiable_at.differentiable_within_at,
  have hu : is_unit (1 - z â€¢ a),
  { refine is_unit_one_sub_smul_of_lt_inv_radius (lt_of_le_of_lt (coe_mono _) hr),
    simpa only [norm_to_nnreal, real.to_nnreal_coe]
      using real.to_nnreal_mono (mem_closed_ball_zero_iff.mp z_mem) },
  have Hâ‚ : differentiable ğ•œ (Î» w : ğ•œ, 1 - w â€¢ a) := (differentiable_id.smul_const a).const_sub 1,
  exact differentiable_at.comp z (differentiable_at_inverse hu.unit) (Hâ‚.differentiable_at),
end

end one_sub_smul

section gelfand_formula

open filter ennreal continuous_multilinear_map
open_locale topological_space

/- the assumption below that `A` be second countable is a technical limitation due to
the current implementation of Bochner integrals in mathlib. Once this is changed, we
will be able to remove that hypothesis. -/
variables
[normed_ring A] [normed_algebra â„‚ A] [complete_space A]
[measurable_space A] [borel_space A] [topological_space.second_countable_topology A]

/-- The `limsup` relationship for the spectral radius used to prove `spectrum.gelfand_formula`. -/
lemma limsup_pow_nnnorm_pow_one_div_le_spectral_radius (a : A) :
  limsup at_top (Î» n : â„•, â†‘âˆ¥a ^ nâˆ¥â‚Š ^ (1 / n : â„)) â‰¤ spectral_radius â„‚ a :=
begin
  refine ennreal.inv_le_inv.mp (le_of_forall_pos_nnreal_lt (Î» r r_pos r_lt, _)),
  simp_rw [inv_limsup, â†one_div],
  let p : formal_multilinear_series â„‚ â„‚ A :=
    Î» n, continuous_multilinear_map.mk_pi_field â„‚ (fin n) (a ^ n),
  suffices h : (r : â„â‰¥0âˆ) â‰¤ p.radius,
  { convert h,
    simp only [p.radius_eq_liminf, â†norm_to_nnreal, norm_mk_pi_field],
    refine congr_arg _ (funext (Î» n, congr_arg _ _)),
    rw [norm_to_nnreal, ennreal.coe_rpow_def (âˆ¥a ^ nâˆ¥â‚Š) (1 / n : â„), if_neg],
    exact Î» ha, by linarith [ha.2, (one_div_nonneg.mpr n.cast_nonneg : 0 â‰¤ (1 / n : â„))], },
  { have Hâ‚ := (differentiable_on_inverse_one_sub_smul r_lt).has_fpower_series_on_ball r_pos,
    exact ((has_fpower_series_on_ball_inverse_one_sub_smul â„‚ a).exchange_radius Hâ‚).r_le, }
end

/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectral_radius` of `a` is the limit of the sequence `âˆ¥a ^ nâˆ¥â‚Š ^ (1 / n)` -/
theorem pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (a : A) :
  tendsto (Î» n : â„•, ((âˆ¥a ^ nâˆ¥â‚Š ^ (1 / n : â„)) : â„â‰¥0âˆ)) at_top (ğ“ (spectral_radius â„‚ a)) :=
begin
  refine tendsto_of_le_liminf_of_limsup_le _ _ (by apply_auto_param) (by apply_auto_param),
  { rw [â†liminf_nat_add _ 1, liminf_eq_supr_infi_of_nat],
    refine le_trans _ (le_supr _ 0),
    exact le_binfi (Î» i hi, spectral_radius_le_pow_nnnorm_pow_one_div â„‚ a i) },
  { exact limsup_pow_nnnorm_pow_one_div_le_spectral_radius a },
end

/- This is the same as `pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius` but for `norm`
instead of `nnnorm`. -/
/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectral_radius` of `a` is the limit of the sequence `âˆ¥a ^ nâˆ¥â‚Š ^ (1 / n)` -/
theorem pow_norm_pow_one_div_tendsto_nhds_spectral_radius (a : A) :
  tendsto (Î» n : â„•,  ennreal.of_real (âˆ¥a ^ nâˆ¥ ^ (1 / n : â„))) at_top (ğ“ (spectral_radius â„‚ a)) :=
begin
  convert pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius a,
  ext1,
  rw [â†of_real_rpow_of_nonneg (norm_nonneg _) _, â†coe_nnnorm, coe_nnreal_eq],
  exact one_div_nonneg.mpr (by exact_mod_cast zero_le _),
end

end gelfand_formula

/-- In a (nontrivial) complex Banach algebra, every element has nonempty spectrum. -/
theorem nonempty {A : Type*} [normed_ring A] [normed_algebra â„‚ A] [complete_space A]
  [nontrivial A] [topological_space.second_countable_topology A]
  (a : A) : (spectrum â„‚ a).nonempty :=
begin
  /- Suppose `Ïƒ a = âˆ…`, then resolvent set is `â„‚`, any `(z â€¢ 1 - a)` is a unit, and `resolvent`
  is differentiable on `â„‚`. -/
  rw â†set.ne_empty_iff_nonempty,
  by_contra h,
  have Hâ‚€ : resolvent_set â„‚ a = set.univ, by rwa [spectrum, set.compl_empty_iff] at h,
  have Hâ‚ : differentiable â„‚ (Î» z : â„‚, resolvent a z), from Î» z,
    (has_deriv_at_resolvent (Hâ‚€.symm â–¸ set.mem_univ z : z âˆˆ resolvent_set â„‚ a)).differentiable_at,
  /- The norm of the resolvent is small for all sufficently large `z`, and by compactness and
  continuity it is bounded on the complement of a large ball, thus uniformly bounded on `â„‚`.
  By Liouville's theorem `Î» z, resolvent a z` is constant -/
  have Hâ‚‚ := norm_resolvent_le_forall a,
  have Hâ‚ƒ : âˆ€ z : â„‚, resolvent a z = resolvent a (0 : â„‚),
  { refine Î» z, Hâ‚.apply_eq_apply_of_bounded (bounded_iff_exists_norm_le.mpr _) z 0,
    rcases Hâ‚‚ 1 zero_lt_one with âŸ¨R, R_pos, hRâŸ©,
    rcases (proper_space.is_compact_closed_ball (0 : â„‚) R).exists_bound_of_continuous_on
      Hâ‚.continuous.continuous_on with âŸ¨C, hCâŸ©,
    use max C 1,
    rintros _ âŸ¨w, rflâŸ©,
    refine or.elim (em (âˆ¥wâˆ¥ â‰¤ R)) (Î» hw, _) (Î» hw, _),
      { exact (hC w (mem_closed_ball_zero_iff.mpr hw)).trans (le_max_left _ _) },
      { exact (hR w (not_le.mp hw).le).trans (le_max_right _ _), }, },
  /- `resolvent a 0 = 0`, which is a contradition because it isn't a unit. -/
  have Hâ‚… : resolvent a (0 : â„‚) = 0,
  { refine norm_eq_zero.mp (le_antisymm (le_of_forall_pos_le_add (Î» Îµ hÎµ, _)) (norm_nonneg _)),
    rcases Hâ‚‚ Îµ hÎµ with âŸ¨R, R_pos, hRâŸ©,
    simpa only [Hâ‚ƒ R] using (zero_add Îµ).symm.subst
      (hR R (by exact_mod_cast (real.norm_of_nonneg R_pos.lt.le).symm.le)), },
  /- `not_is_unit_zero` is where we need `nontrivial A`, it is unavoidable. -/
  exact not_is_unit_zero (Hâ‚….subst (is_unit_resolvent.mp
    (mem_resolvent_set_iff.mp (Hâ‚€.symm â–¸ set.mem_univ 0)))),
end

end spectrum

namespace alg_hom

section normed_field
variables [normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A] [complete_space A]
local notation `â†‘â‚` := algebra_map ğ•œ A

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). -/
@[simps] def to_continuous_linear_map (Ï† : A â†’â‚[ğ•œ] ğ•œ) : A â†’L[ğ•œ] ğ•œ :=
Ï†.to_linear_map.mk_continuous_of_exists_bound $
  âŸ¨1, Î» a, (one_mul âˆ¥aâˆ¥).symm â–¸ spectrum.norm_le_norm_of_mem (Ï†.apply_mem_spectrum _)âŸ©

lemma continuous (Ï† : A â†’â‚[ğ•œ] ğ•œ) : continuous Ï† := Ï†.to_continuous_linear_map.continuous

end normed_field

section nondiscrete_normed_field
variables [nondiscrete_normed_field ğ•œ] [normed_ring A] [normed_algebra ğ•œ A] [complete_space A]
local notation `â†‘â‚` := algebra_map ğ•œ A

@[simp] lemma to_continuous_linear_map_norm [norm_one_class A] (Ï† : A â†’â‚[ğ•œ] ğ•œ) :
  âˆ¥Ï†.to_continuous_linear_mapâˆ¥ = 1 :=
continuous_linear_map.op_norm_eq_of_bounds zero_le_one
  (Î» a, (one_mul âˆ¥aâˆ¥).symm â–¸ spectrum.norm_le_norm_of_mem (Ï†.apply_mem_spectrum _))
  (Î» _ _ h, by simpa only [to_continuous_linear_map_apply, mul_one, map_one, norm_one] using h 1)

end nondiscrete_normed_field

end alg_hom
