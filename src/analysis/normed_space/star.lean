/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import analysis.normed_space.linear_isometry

/-!
# Normed star rings and algebras

A normed star ring is a star ring endowed with a norm such that the star operation is isometric.

A Câ‹†-ring is a normed star ring that verifies the stronger condition `âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâˆ¥^2` for all `x`.
If a Câ‹†-ring is also a star algebra, then it is a Câ‹†-algebra.

To get a Câ‹†-algebra `E` over field `ğ•œ`, use
`[normed_field ğ•œ] [star_ring ğ•œ] [cstar_ring E] [normed_algebra ğ•œ E] [star_module ğ•œ E]`.

-/

local postfix `â‹†`:1000 := star

/-- A normed star ring is a star ring endowed with a norm such that `star` is isometric. -/
class normed_star_ring (E : Type*) [normed_ring E] [star_ring E] :=
(norm_star : âˆ€ {x : E}, âˆ¥xâ‹†âˆ¥ = âˆ¥xâˆ¥)

export normed_star_ring (norm_star)
attribute [simp] norm_star

/-- A C*-ring is a normed star ring that satifies the stronger condition `âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâˆ¥^2`
for every `x`. -/
class cstar_ring (E : Type*) extends normed_ring E, star_ring E :=
(norm_star_mul_self : âˆ€ {x : E}, âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥)

variables {ğ•œ E : Type*}

open cstar_ring

/-- In a C*-ring, star preserves the norm. -/
instance cstar_ring.to_normed_star_ring {E : Type*} [cstar_ring E] : normed_star_ring E :=
âŸ¨begin
  intro x,
  by_cases htriv : x = 0,
  { simp only [htriv, star_zero] },
  { change x â‰  0 at htriv,
    have hnt : 0 < âˆ¥xâˆ¥ := norm_pos_iff.mpr htriv,
    have hnt_star : 0 < âˆ¥xâ‹†âˆ¥ :=
      norm_pos_iff.mpr ((add_equiv.map_ne_zero_iff star_add_equiv).mpr htriv),
    have hâ‚ := calc
      âˆ¥xâˆ¥ * âˆ¥xâˆ¥ = âˆ¥xâ‹† * xâˆ¥        : norm_star_mul_self.symm
            ... â‰¤ âˆ¥xâ‹†âˆ¥ * âˆ¥xâˆ¥      : norm_mul_le _ _,
    have hâ‚‚ := calc
      âˆ¥xâ‹†âˆ¥ * âˆ¥xâ‹†âˆ¥ = âˆ¥x * xâ‹†âˆ¥      : by rw [â†norm_star_mul_self, star_star]
             ... â‰¤ âˆ¥xâˆ¥ * âˆ¥xâ‹†âˆ¥     : norm_mul_le _ _,
    exact le_antisymm (le_of_mul_le_mul_right hâ‚‚ hnt_star) (le_of_mul_le_mul_right hâ‚ hnt) },
endâŸ©

lemma cstar_ring.norm_self_mul_star [cstar_ring E] {x : E} : âˆ¥x * xâ‹†âˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ :=
by { nth_rewrite 0 [â†star_star x], simp only [norm_star_mul_self, norm_star] }

/-- `star` bundled as a linear isometric equivalence -/
noncomputable def starâ‚—áµ¢ [comm_semiring ğ•œ] [star_ring ğ•œ] [normed_ring E] [star_ring E]
  [normed_star_ring E] [module ğ•œ E] [star_module ğ•œ E] : E â‰ƒâ‚—áµ¢â‹†[ğ•œ] E :=
linear_isometry_equiv.of_surjective
{ to_fun := star,
  map_add' := star_add,
  map_smul' := star_smul,
  norm_map' := Î» x, norm_star }
(Î» x, âŸ¨xâ‹†, star_star xâŸ©)
