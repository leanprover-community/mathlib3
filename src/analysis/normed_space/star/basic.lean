/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import analysis.normed.group.hom
import analysis.normed_space.basic
import analysis.normed_space.linear_isometry
import algebra.star.self_adjoint
import algebra.star.unitary

/-!
# Normed star rings and algebras

A normed star group is a normed group with a compatible `star` which is isometric.

A Câ‹†-ring is a normed star group that is also a ring and that verifies the stronger
condition `âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâˆ¥^2` for all `x`.  If a Câ‹†-ring is also a star algebra, then it is a
Câ‹†-algebra.

To get a Câ‹†-algebra `E` over field `ğ•œ`, use
`[normed_field ğ•œ] [star_ring ğ•œ] [normed_ring E] [star_ring E] [cstar_ring E]
 [normed_algebra ğ•œ E] [star_module ğ•œ E]`.

## TODO

- Show that `âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâˆ¥^2` is equivalent to `âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâ‹†âˆ¥ * âˆ¥xâˆ¥`, which is used as the
  definition of C*-algebras in some sources (e.g. Wikipedia).

-/

open_locale topological_space

local postfix `â‹†`:std.prec.max_plus := star

/-- A normed star group is a normed group with a compatible `star` which is isometric. -/
class normed_star_group (E : Type*) [normed_group E] [star_add_monoid E] : Prop :=
(norm_star : âˆ€ {x : E}, âˆ¥xâ‹†âˆ¥ = âˆ¥xâˆ¥)

export normed_star_group (norm_star)
attribute [simp] norm_star

variables {ğ•œ E Î± : Type*}

section normed_star_group
variables [normed_group E] [star_add_monoid E] [normed_star_group E]

/-- The `star` map in a normed star group is a normed group homomorphism. -/
def star_normed_group_hom : normed_group_hom E E :=
{ bound' := âŸ¨1, Î» v, le_trans (norm_star.le) (one_mul _).symm.leâŸ©,
  .. star_add_equiv }

/-- The `star` map in a normed star group is an isometry -/
lemma star_isometry : isometry (star : E â†’ E) :=
star_add_equiv.to_add_monoid_hom.isometry_of_norm (Î» _, norm_star)

lemma continuous_star : continuous (star : E â†’ E) := star_isometry.continuous

lemma continuous_on_star {s : set E} : continuous_on star s := continuous_star.continuous_on

lemma continuous_at_star {x : E} : continuous_at star x := continuous_star.continuous_at

lemma continuous_within_at_star {s : set E} {x : E} : continuous_within_at star s x :=
continuous_star.continuous_within_at

lemma tendsto_star (x : E) : filter.tendsto star (ğ“ x) (ğ“ xâ‹†) := continuous_star.tendsto x

lemma filter.tendsto.star {f : Î± â†’ E} {l : filter Î±} {y : E} (h : filter.tendsto f l (ğ“ y)) :
  filter.tendsto (Î» x, (f x)â‹†) l (ğ“ yâ‹†) :=
(continuous_star.tendsto y).comp h

variables [topological_space Î±]

lemma continuous.star {f : Î± â†’ E} (hf : continuous f) : continuous (Î» y, star (f y)) :=
continuous_star.comp hf

lemma continuous_at.star {f : Î± â†’ E} {x : Î±} (hf : continuous_at f x) :
  continuous_at (Î» x, (f x)â‹†) x :=
continuous_at_star.comp hf

lemma continuous_on.star {f : Î± â†’ E} {s : set Î±} (hf : continuous_on f s) :
  continuous_on (Î» x, (f x)â‹†) s :=
continuous_star.comp_continuous_on hf

lemma continuous_within_at.star {f : Î± â†’ E} {s : set Î±} {x : Î±}
  (hf : continuous_within_at f s x) : continuous_within_at (Î» x, (f x)â‹†) s x := hf.star

end normed_star_group

instance ring_hom_isometric.star_ring_end [normed_comm_ring E] [star_ring E]
  [normed_star_group E] : ring_hom_isometric (star_ring_end E) :=
âŸ¨Î» _, norm_starâŸ©

/-- A C*-ring is a normed star ring that satifies the stronger condition `âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâˆ¥^2`
for every `x`. -/
class cstar_ring (E : Type*) [non_unital_normed_ring E] [star_ring E] : Prop :=
(norm_star_mul_self : âˆ€ {x : E}, âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥)

instance : cstar_ring â„ :=
{ norm_star_mul_self := Î» x, by simp only [star, id.def, norm_mul] }

namespace cstar_ring
section non_unital

variables [non_unital_normed_ring E] [star_ring E] [cstar_ring E]

/-- In a C*-ring, star preserves the norm. -/
@[priority 100] -- see Note [lower instance priority]
instance to_normed_star_group : normed_star_group E :=
âŸ¨begin
  intro x,
  by_cases htriv : x = 0,
  { simp only [htriv, star_zero] },
  { have hnt : 0 < âˆ¥xâˆ¥ := norm_pos_iff.mpr htriv,
    have hnt_star : 0 < âˆ¥xâ‹†âˆ¥ :=
      norm_pos_iff.mpr ((add_equiv.map_ne_zero_iff star_add_equiv).mpr htriv),
    have hâ‚ := calc
      âˆ¥xâˆ¥ * âˆ¥xâˆ¥ = âˆ¥xâ‹† * xâˆ¥        : norm_star_mul_self.symm
            ... â‰¤ âˆ¥xâ‹†âˆ¥ * âˆ¥xâˆ¥      : norm_mul_le _ _,
    have hâ‚‚ := calc
      âˆ¥xâ‹†âˆ¥ * âˆ¥xâ‹†âˆ¥ = âˆ¥x * xâ‹†âˆ¥      : by rw [â†norm_star_mul_self, star_star]
             ... â‰¤ âˆ¥xâˆ¥ * âˆ¥xâ‹†âˆ¥     : norm_mul_le _ _,
    exact le_antisymm (le_of_mul_le_mul_right hâ‚‚ hnt_star) (le_of_mul_le_mul_right hâ‚ hnt) },
endâŸ©

lemma norm_self_mul_star {x : E} : âˆ¥x * xâ‹†âˆ¥ = âˆ¥xâˆ¥ * âˆ¥xâˆ¥ :=
by { nth_rewrite 0 [â†star_star x], simp only [norm_star_mul_self, norm_star] }

lemma norm_star_mul_self' {x : E} : âˆ¥xâ‹† * xâˆ¥ = âˆ¥xâ‹†âˆ¥ * âˆ¥xâˆ¥ :=
by rw [norm_star_mul_self, norm_star]

lemma nnnorm_star_mul_self {x : E} : âˆ¥xâ‹† * xâˆ¥â‚Š = âˆ¥xâˆ¥â‚Š * âˆ¥xâˆ¥â‚Š :=
subtype.ext norm_star_mul_self

end non_unital

section unital
variables [normed_ring E] [star_ring E] [cstar_ring E]

@[simp] lemma norm_one [nontrivial E] : âˆ¥(1 : E)âˆ¥ = 1 :=
begin
  have : 0 < âˆ¥(1 : E)âˆ¥ := norm_pos_iff.mpr one_ne_zero,
  rw [â†mul_left_inj' this.ne', â†norm_star_mul_self, mul_one, star_one, one_mul],
end

@[priority 100] -- see Note [lower instance priority]
instance [nontrivial E] : norm_one_class E := âŸ¨norm_oneâŸ©

lemma norm_coe_unitary [nontrivial E] (U : unitary E) : âˆ¥(U : E)âˆ¥ = 1 :=
begin
  rw [â†sq_eq_sq (norm_nonneg _) zero_le_one, one_pow 2, sq, â†cstar_ring.norm_star_mul_self,
    unitary.coe_star_mul_self, cstar_ring.norm_one],
end

@[simp] lemma norm_of_mem_unitary [nontrivial E] {U : E} (hU : U âˆˆ unitary E) : âˆ¥Uâˆ¥ = 1 :=
norm_coe_unitary âŸ¨U, hUâŸ©

@[simp] lemma norm_coe_unitary_mul (U : unitary E) (A : E) : âˆ¥(U : E) * Aâˆ¥ = âˆ¥Aâˆ¥ :=
begin
  nontriviality E,
  refine le_antisymm _ _,
  { calc _  â‰¤ âˆ¥(U : E)âˆ¥ * âˆ¥Aâˆ¥     : norm_mul_le _ _
        ... = âˆ¥Aâˆ¥                 : by rw [norm_coe_unitary, one_mul] },
  { calc _ = âˆ¥(U : E)â‹† * U * Aâˆ¥         : by rw [unitary.coe_star_mul_self U, one_mul]
       ... â‰¤ âˆ¥(U : E)â‹†âˆ¥ * âˆ¥(U : E) * Aâˆ¥ : by { rw [mul_assoc], exact norm_mul_le _ _ }
       ... = âˆ¥(U : E) * Aâˆ¥              : by rw [norm_star, norm_coe_unitary, one_mul] },
end

@[simp] lemma norm_unitary_smul (U : unitary E) (A : E) : âˆ¥U â€¢ Aâˆ¥ = âˆ¥Aâˆ¥ :=
norm_coe_unitary_mul U A

lemma norm_mem_unitary_mul {U : E} (A : E) (hU : U âˆˆ unitary E) : âˆ¥U * Aâˆ¥ = âˆ¥Aâˆ¥ :=
norm_coe_unitary_mul âŸ¨U, hUâŸ© A

@[simp] lemma norm_mul_coe_unitary (A : E) (U : unitary E) : âˆ¥A * Uâˆ¥ = âˆ¥Aâˆ¥ :=
calc _ = âˆ¥((U : E)â‹† * Aâ‹†)â‹†âˆ¥ : by simp only [star_star, star_mul]
  ...  = âˆ¥(U : E)â‹† * Aâ‹†âˆ¥    : by rw [norm_star]
  ...  = âˆ¥Aâ‹†âˆ¥               : norm_mem_unitary_mul (star A) (unitary.star_mem U.prop)
  ...  = âˆ¥Aâˆ¥                : norm_star

lemma norm_mul_mem_unitary (A : E) {U : E} (hU : U âˆˆ unitary E) : âˆ¥A * Uâˆ¥ = âˆ¥Aâˆ¥ :=
norm_mul_coe_unitary A âŸ¨U, hUâŸ©

end unital
end cstar_ring

lemma nnnorm_pow_two_pow_of_self_adjoint [normed_ring E] [star_ring E] [cstar_ring E]
  {x : E} (hx : x âˆˆ self_adjoint E) (n : â„•) : âˆ¥x ^ 2 ^ nâˆ¥â‚Š = âˆ¥xâˆ¥â‚Š ^ (2 ^ n) :=
begin
  induction n with k hk,
  { simp only [pow_zero, pow_one] },
  { rw [pow_succ, pow_mul', sq],
    nth_rewrite 0 â†(self_adjoint.mem_iff.mp hx),
    rw [â†star_pow, cstar_ring.nnnorm_star_mul_self, â†sq, hk, pow_mul'] },
end

lemma self_adjoint.nnnorm_pow_two_pow [normed_ring E] [star_ring E] [cstar_ring E]
  (x : self_adjoint E) (n : â„•) : âˆ¥x ^ 2 ^ nâˆ¥â‚Š = âˆ¥xâˆ¥â‚Š ^ (2 ^ n) :=
nnnorm_pow_two_pow_of_self_adjoint x.property _

section starâ‚—áµ¢

variables [comm_semiring ğ•œ] [star_ring ğ•œ] [normed_ring E] [star_ring E] [normed_star_group E]
variables [module ğ•œ E] [star_module ğ•œ E]

variables (ğ•œ)
/-- `star` bundled as a linear isometric equivalence -/
def starâ‚—áµ¢ : E â‰ƒâ‚—áµ¢â‹†[ğ•œ] E :=
{ map_smul' := star_smul,
  norm_map' := Î» x, norm_star,
  .. star_add_equiv }

variables {ğ•œ}

@[simp] lemma coe_starâ‚—áµ¢ : (starâ‚—áµ¢ ğ•œ : E â†’ E) = star := rfl

lemma starâ‚—áµ¢_apply {x : E} : starâ‚—áµ¢ ğ•œ x = star x := rfl

end starâ‚—áµ¢
