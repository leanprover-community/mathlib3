/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import analysis.normed.group.hom
import analysis.normed_space.basic
import analysis.normed_space.linear_isometry
import analysis.normed_space.operator_norm
import algebra.star.self_adjoint
import algebra.star.unitary

/-!
# Normed star rings and algebras

A normed star group is a normed group with a compatible `star` which is isometric.

A Câ‹†-ring is a normed star group that is also a ring and that verifies the stronger
condition `â€–xâ‹† * xâ€– = â€–xâ€–^2` for all `x`.  If a Câ‹†-ring is also a star algebra, then it is a
Câ‹†-algebra.

To get a Câ‹†-algebra `E` over field `ğ•œ`, use
`[normed_field ğ•œ] [star_ring ğ•œ] [normed_ring E] [star_ring E] [cstar_ring E]
 [normed_algebra ğ•œ E] [star_module ğ•œ E]`.

## TODO

- Show that `â€–xâ‹† * xâ€– = â€–xâ€–^2` is equivalent to `â€–xâ‹† * xâ€– = â€–xâ‹†â€– * â€–xâ€–`, which is used as the
  definition of C*-algebras in some sources (e.g. Wikipedia).

-/

open_locale topological_space

local postfix `â‹†`:std.prec.max_plus := star

/-- A normed star group is a normed group with a compatible `star` which is isometric. -/
class normed_star_group (E : Type*) [seminormed_add_comm_group E] [star_add_monoid E] : Prop :=
(norm_star : âˆ€ x : E, â€–xâ‹†â€– = â€–xâ€–)

export normed_star_group (norm_star)
attribute [simp] norm_star

variables {ğ•œ E Î± : Type*}

section normed_star_group
variables [seminormed_add_comm_group E] [star_add_monoid E] [normed_star_group E]

@[simp] lemma nnnorm_star (x : E) : â€–star xâ€–â‚Š = â€–xâ€–â‚Š := subtype.ext $ norm_star _

/-- The `star` map in a normed star group is a normed group homomorphism. -/
def star_normed_add_group_hom : normed_add_group_hom E E :=
{ bound' := âŸ¨1, Î» v, le_trans (norm_star _).le (one_mul _).symm.leâŸ©,
  .. star_add_equiv }

/-- The `star` map in a normed star group is an isometry -/
lemma star_isometry : isometry (star : E â†’ E) :=
show isometry star_add_equiv,
by exact add_monoid_hom_class.isometry_of_norm star_add_equiv
    (show âˆ€ x, â€–xâ‹†â€– = â€–xâ€–, from norm_star)

@[priority 100]
instance normed_star_group.to_has_continuous_star : has_continuous_star E :=
âŸ¨star_isometry.continuousâŸ©

end normed_star_group

instance ring_hom_isometric.star_ring_end [normed_comm_ring E] [star_ring E]
  [normed_star_group E] : ring_hom_isometric (star_ring_end E) :=
âŸ¨norm_starâŸ©

/-- A C*-ring is a normed star ring that satifies the stronger condition `â€–xâ‹† * xâ€– = â€–xâ€–^2`
for every `x`. -/
class cstar_ring (E : Type*) [non_unital_normed_ring E] [star_ring E] : Prop :=
(norm_star_mul_self : âˆ€ {x : E}, â€–xâ‹† * xâ€– = â€–xâ€– * â€–xâ€–)

instance : cstar_ring â„ :=
{ norm_star_mul_self := Î» x, by simp only [star, id.def, norm_mul] }

namespace cstar_ring
section non_unital

variables [non_unital_normed_ring E] [star_ring E] [cstar_ring E]

/-- In a C*-ring, star preserves the norm. -/
@[priority 100] -- see Note [lower instance priority]
instance to_normed_star_group : normed_star_group E :=
âŸ¨begin
  intro x,
  by_cases htriv : x = 0,
  { simp only [htriv, star_zero] },
  { have hnt : 0 < â€–xâ€– := norm_pos_iff.mpr htriv,
    have hnt_star : 0 < â€–xâ‹†â€– :=
      norm_pos_iff.mpr ((add_equiv.map_ne_zero_iff star_add_equiv).mpr htriv),
    have hâ‚ := calc
      â€–xâ€– * â€–xâ€– = â€–xâ‹† * xâ€–        : norm_star_mul_self.symm
            ... â‰¤ â€–xâ‹†â€– * â€–xâ€–      : norm_mul_le _ _,
    have hâ‚‚ := calc
      â€–xâ‹†â€– * â€–xâ‹†â€– = â€–x * xâ‹†â€–      : by rw [â†norm_star_mul_self, star_star]
             ... â‰¤ â€–xâ€– * â€–xâ‹†â€–     : norm_mul_le _ _,
    exact le_antisymm (le_of_mul_le_mul_right hâ‚‚ hnt_star) (le_of_mul_le_mul_right hâ‚ hnt) },
endâŸ©

lemma norm_self_mul_star {x : E} : â€–x * xâ‹†â€– = â€–xâ€– * â€–xâ€– :=
by { nth_rewrite 0 [â†star_star x], simp only [norm_star_mul_self, norm_star] }

lemma norm_star_mul_self' {x : E} : â€–xâ‹† * xâ€– = â€–xâ‹†â€– * â€–xâ€– :=
by rw [norm_star_mul_self, norm_star]

lemma nnnorm_self_mul_star {x : E} : â€–x * star xâ€–â‚Š = â€–xâ€–â‚Š * â€–xâ€–â‚Š :=
subtype.ext norm_self_mul_star

lemma nnnorm_star_mul_self {x : E} : â€–xâ‹† * xâ€–â‚Š = â€–xâ€–â‚Š * â€–xâ€–â‚Š :=
subtype.ext norm_star_mul_self

@[simp]
lemma star_mul_self_eq_zero_iff (x : E) : star x * x = 0 â†” x = 0 :=
by { rw [â†norm_eq_zero, norm_star_mul_self], exact mul_self_eq_zero.trans norm_eq_zero }

lemma star_mul_self_ne_zero_iff (x : E) : star x * x â‰  0 â†” x â‰  0 :=
by simp only [ne.def, star_mul_self_eq_zero_iff]

@[simp]
lemma mul_star_self_eq_zero_iff (x : E) : x * star x = 0 â†” x = 0 :=
by simpa only [star_eq_zero, star_star] using @star_mul_self_eq_zero_iff _ _ _ _ (star x)

lemma mul_star_self_ne_zero_iff (x : E) : x * star x â‰  0 â†” x â‰  0 :=
by simp only [ne.def, mul_star_self_eq_zero_iff]

end non_unital

section prod_pi

variables {Î¹ Râ‚ Râ‚‚ : Type*} {R : Î¹ â†’ Type*}
variables [non_unital_normed_ring Râ‚] [star_ring Râ‚] [cstar_ring Râ‚]
variables [non_unital_normed_ring Râ‚‚] [star_ring Râ‚‚] [cstar_ring Râ‚‚]
variables [Î  i, non_unital_normed_ring (R i)] [Î  i, star_ring (R i)]

/-- This instance exists to short circuit type class resolution because of problems with
inference involving Î -types. -/
instance _root_.pi.star_ring' : star_ring (Î  i, R i) := infer_instance

variables [fintype Î¹] [Î  i, cstar_ring (R i)]

instance _root_.prod.cstar_ring : cstar_ring (Râ‚ Ã— Râ‚‚) :=
{ norm_star_mul_self := Î» x,
  begin
    unfold norm,
    simp only [prod.fst_mul, prod.fst_star, prod.snd_mul, prod.snd_star, norm_star_mul_self, â†sq],
    refine le_antisymm _ _,
    { refine max_le _ _;
      rw [sq_le_sq, abs_of_nonneg (norm_nonneg _)],
      exact (le_max_left _ _).trans (le_abs_self _),
      exact (le_max_right _ _).trans (le_abs_self _) },
    { rw le_sup_iff,
      rcases le_total (â€–x.fstâ€–) (â€–x.sndâ€–) with (h | h);
      simp [h] }
  end }

instance _root_.pi.cstar_ring : cstar_ring (Î  i, R i) :=
{ norm_star_mul_self := Î» x,
  begin
    simp only [norm, pi.mul_apply, pi.star_apply, nnnorm_star_mul_self, â†sq],
    norm_cast,
    exact (finset.comp_sup_eq_sup_comp_of_is_total (Î» x : nnreal, x ^ 2)
      (Î» x y h, by simpa only [sq] using mul_le_mul' h h) (by simp)).symm,
  end }

instance _root_.pi.cstar_ring' : cstar_ring (Î¹ â†’ Râ‚) := pi.cstar_ring

end prod_pi

section unital
variables [normed_ring E] [star_ring E] [cstar_ring E]

@[simp] lemma norm_one [nontrivial E] : â€–(1 : E)â€– = 1 :=
begin
  have : 0 < â€–(1 : E)â€– := norm_pos_iff.mpr one_ne_zero,
  rw [â†mul_left_inj' this.ne', â†norm_star_mul_self, mul_one, star_one, one_mul],
end

@[priority 100] -- see Note [lower instance priority]
instance [nontrivial E] : norm_one_class E := âŸ¨norm_oneâŸ©

lemma norm_coe_unitary [nontrivial E] (U : unitary E) : â€–(U : E)â€– = 1 :=
begin
  rw [â†sq_eq_sq (norm_nonneg _) zero_le_one, one_pow 2, sq, â†cstar_ring.norm_star_mul_self,
    unitary.coe_star_mul_self, cstar_ring.norm_one],
end

@[simp] lemma norm_of_mem_unitary [nontrivial E] {U : E} (hU : U âˆˆ unitary E) : â€–Uâ€– = 1 :=
norm_coe_unitary âŸ¨U, hUâŸ©

@[simp] lemma norm_coe_unitary_mul (U : unitary E) (A : E) : â€–(U : E) * Aâ€– = â€–Aâ€– :=
begin
  nontriviality E,
  refine le_antisymm _ _,
  { calc _  â‰¤ â€–(U : E)â€– * â€–Aâ€–     : norm_mul_le _ _
        ... = â€–Aâ€–                 : by rw [norm_coe_unitary, one_mul] },
  { calc _ = â€–(U : E)â‹† * U * Aâ€–         : by rw [unitary.coe_star_mul_self U, one_mul]
       ... â‰¤ â€–(U : E)â‹†â€– * â€–(U : E) * Aâ€– : by { rw [mul_assoc], exact norm_mul_le _ _ }
       ... = â€–(U : E) * Aâ€–              : by rw [norm_star, norm_coe_unitary, one_mul] },
end

@[simp] lemma norm_unitary_smul (U : unitary E) (A : E) : â€–U â€¢ Aâ€– = â€–Aâ€– :=
norm_coe_unitary_mul U A

lemma norm_mem_unitary_mul {U : E} (A : E) (hU : U âˆˆ unitary E) : â€–U * Aâ€– = â€–Aâ€– :=
norm_coe_unitary_mul âŸ¨U, hUâŸ© A

@[simp] lemma norm_mul_coe_unitary (A : E) (U : unitary E) : â€–A * Uâ€– = â€–Aâ€– :=
calc _ = â€–((U : E)â‹† * Aâ‹†)â‹†â€– : by simp only [star_star, star_mul]
  ...  = â€–(U : E)â‹† * Aâ‹†â€–    : by rw [norm_star]
  ...  = â€–Aâ‹†â€–               : norm_mem_unitary_mul (star A) (unitary.star_mem U.prop)
  ...  = â€–Aâ€–                : norm_star _

lemma norm_mul_mem_unitary (A : E) {U : E} (hU : U âˆˆ unitary E) : â€–A * Uâ€– = â€–Aâ€– :=
norm_mul_coe_unitary A âŸ¨U, hUâŸ©

end unital
end cstar_ring

lemma is_self_adjoint.nnnorm_pow_two_pow [normed_ring E] [star_ring E]
  [cstar_ring E] {x : E} (hx : is_self_adjoint x) (n : â„•) : â€–x ^ 2 ^ nâ€–â‚Š = â€–xâ€–â‚Š ^ (2 ^ n) :=
begin
  induction n with k hk,
  { simp only [pow_zero, pow_one] },
  { rw [pow_succ, pow_mul', sq],
    nth_rewrite 0 â†(self_adjoint.mem_iff.mp hx),
    rw [â†star_pow, cstar_ring.nnnorm_star_mul_self, â†sq, hk, pow_mul'] },
end

lemma self_adjoint.nnnorm_pow_two_pow [normed_ring E] [star_ring E] [cstar_ring E]
  (x : self_adjoint E) (n : â„•) : â€–x ^ 2 ^ nâ€–â‚Š = â€–xâ€–â‚Š ^ (2 ^ n) :=
x.prop.nnnorm_pow_two_pow _

section starâ‚—áµ¢

variables [comm_semiring ğ•œ] [star_ring ğ•œ]
variables [seminormed_add_comm_group E] [star_add_monoid E] [normed_star_group E]
variables [module ğ•œ E] [star_module ğ•œ E]

variables (ğ•œ)
/-- `star` bundled as a linear isometric equivalence -/
def starâ‚—áµ¢ : E â‰ƒâ‚—áµ¢â‹†[ğ•œ] E :=
{ map_smul' := star_smul,
  norm_map' := norm_star,
  .. star_add_equiv }

variables {ğ•œ}

@[simp] lemma coe_starâ‚—áµ¢ : (starâ‚—áµ¢ ğ•œ : E â†’ E) = star := rfl

lemma starâ‚—áµ¢_apply {x : E} : starâ‚—áµ¢ ğ•œ x = star x := rfl

end starâ‚—áµ¢

section mul

open continuous_linear_map

variables (ğ•œ) [densely_normed_field ğ•œ] [non_unital_normed_ring E] [star_ring E] [cstar_ring E]
variables [normed_space ğ•œ E] [is_scalar_tower ğ•œ E E] [smul_comm_class ğ•œ E E] (a : E)

/-- In a Câ‹†-algebra `E`, either unital or non-unital, multiplication on the left by `a : E` has
norm equal to the norm of `a`. -/
@[simp] lemma op_nnnorm_mul : â€–mul ğ•œ E aâ€–â‚Š = â€–aâ€–â‚Š :=
begin
  rw â†Sup_closed_unit_ball_eq_nnnorm,
  refine cSup_eq_of_forall_le_of_forall_lt_exists_gt _ _ (Î» r hr, _),
  { exact (metric.nonempty_closed_ball.mpr zero_le_one).image _ },
  { rintro - âŸ¨x, hx, rflâŸ©,
    exact ((mul ğ•œ E a).unit_le_op_norm x $ mem_closed_ball_zero_iff.mp hx).trans
      (op_norm_mul_apply_le ğ•œ E a) },
  { have ha : 0 < â€–aâ€–â‚Š := zero_le'.trans_lt hr,
    rw [â†inv_inv (â€–aâ€–â‚Š), nnreal.lt_inv_iff_mul_lt (inv_ne_zero ha.ne')] at hr,
    obtain âŸ¨k, hkâ‚, hkâ‚‚âŸ© := normed_field.exists_lt_nnnorm_lt ğ•œ (mul_lt_mul_of_pos_right hr $
      nnreal.inv_pos.2 ha),
    refine âŸ¨_, âŸ¨k â€¢ star a, _, rflâŸ©, _âŸ©,
    { simpa only [mem_closed_ball_zero_iff, norm_smul, one_mul, norm_star] using
        (nnreal.le_inv_iff_mul_le ha.ne').1 (one_mul â€–aâ€–â‚Šâ»Â¹ â–¸ hkâ‚‚.le : â€–kâ€–â‚Š â‰¤ â€–aâ€–â‚Šâ»Â¹) },
    { simp only [map_smul, nnnorm_smul, mul_apply', mul_smul_comm, cstar_ring.nnnorm_self_mul_star],
      rwa [â†nnreal.div_lt_iff (mul_pos ha ha).ne', div_eq_mul_inv, mul_inv, â†mul_assoc] } },
end

/-- In a Câ‹†-algebra `E`, either unital or non-unital, multiplication on the right by `a : E` has
norm eqaul to the norm of `a`. -/
@[simp] lemma op_nnnorm_mul_flip : â€–(mul ğ•œ E).flip aâ€–â‚Š = â€–aâ€–â‚Š :=
begin
  rw [â†Sup_unit_ball_eq_nnnorm, â†nnnorm_star, â†@op_nnnorm_mul ğ•œ E, â†Sup_unit_ball_eq_nnnorm],
  congr' 1,
  simp only [mul_apply', flip_apply],
  refine set.subset.antisymm _ _;
  rintro - âŸ¨b, hb, rflâŸ©;
  refine âŸ¨star b, by simpa only [norm_star, mem_ball_zero_iff] using hb, _âŸ©,
  { simp only [â†star_mul, nnnorm_star] },
  { simpa using (nnnorm_star (star b * a)).symm }
end

variables (E)

/-- In a Câ‹†-algebra `E`, either unital or non-unital, the left regular representation is an
isometry. -/
lemma mul_isometry : isometry (mul ğ•œ E) :=
add_monoid_hom_class.isometry_of_norm _ (Î» a, congr_arg coe $ op_nnnorm_mul ğ•œ a)

/-- In a Câ‹†-algebra `E`, either unital or non-unital, the right regular anti-representation is an
isometry. -/
lemma mul_flip_isometry : isometry (mul ğ•œ E).flip :=
add_monoid_hom_class.isometry_of_norm _ (Î» a, congr_arg coe $ op_nnnorm_mul_flip ğ•œ a)

end mul
