import analysis.normed_space.star.basic
import analysis.normed_space.operator_norm
import data.real.sqrt
import analysis.special_functions.pow

/-!
# Multiplier Algebra of a Câ‹†-algebra

Define the multiplier algebra of a Câ‹†-algebra as the algebra (over `ğ•œ`) of double centralizers,
for which we provide the localized notation `ğ“œ(ğ•œ, A)`.  A double centralizer is a pair of
continuous linear maps `L R : A â†’L[ğ•œ] A` satisfying the intertwining condition `R x * y = x * L y`.

There is a natural embedding `A â†’ ğ“œ(ğ•œ, A)` which sends `a : A` to the continuous linear maps
`L R : A â†’L[ğ•œ] A` given by left and right multiplication by `a`, and we provide this map as a
coercion.

The multiplier algebra corresponds to a non-commutative Stoneâ€“ÄŒech compactification in the sense
that when the algebra `A` is commutative, it can be identified with `Câ‚€(X, â„‚)` for some locally
compact Hausdorff space `X`, and in that case `ğ“œ(ğ•œ, A)` can be identified with `C(Î² X, â„‚)`.

## Implementation notes

## TODO

+ show that `ğ“œ(ğ•œ, A)` is a Câ‹†-ring
+ define a type synonym for `ğ“œ(ğ•œ, A)` which is equipped with the strict topology
+ after â‹†-algebra morphisms are implemented in mathlib, bundle the coercion `A â†’ ğ“œ(ğ•œ, A)`
+ show that the image of `A` in `ğ“œ(ğ•œ, A)` is an essential ideal
+ prove the universal property of `ğ“œ(ğ•œ, A)`
* Construct a double centralizer from a pair of maps `L : A â†’ A`, `R : A â†’ A` satisfying the
  centrality condition `âˆ€ x y, R x * y = x * L y`.
-/

noncomputable theory

open_locale nnreal ennreal
open nnreal continuous_linear_map

universes u v

variables (ğ•œ : Type u) (A : Type v)
  [nontrivially_normed_field ğ•œ]
  [non_unital_normed_ring A]
  [normed_space ğ•œ A] [smul_comm_class ğ•œ A A] [is_scalar_tower ğ•œ A A]

section prereqs

-- this should go in `analysis.normed_space.star_basic`
lemma _root_.cstar_ring.nnnorm_self_mul_star {E : Type*} [non_unital_normed_ring E] [star_ring E]
  [cstar_ring E] {x : E} : âˆ¥x * star xâˆ¥â‚Š = âˆ¥xâˆ¥â‚Š * âˆ¥xâˆ¥â‚Š :=
by simpa using @cstar_ring.nnnorm_star_mul_self _ _ _ _ (star x)

end prereqs

@[ext]
structure double_centralizer : Type v :=
(left : A â†’L[ğ•œ] A)
(right : A â†’L[ğ•œ] A)
(central : âˆ€ x y : A, right x * y = x * left y)

localized "notation `ğ“œ(` ğ•œ `, ` A `)` := double_centralizer ğ•œ A" in multiplier_algebra

/-!
### Normed space structure

Because the multiplier algebra is defined as the algebra of double centralizers, there is a natural
map `double_centralizer.prod_mk := Î» a, (a.left, a.right) : ğ“œ(ğ•œ, A) â†’ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)`.
We use this map to pull back the normed space structure from `(A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)` to
`ğ“œ(ğ•œ, A)`, which provides a definitional isometric embedding. Consequently, completeness of
`ğ“œ(ğ•œ, A)` is obtained by proving that the range of this map is closed.
-/

namespace double_centralizer

def prod_mk : ğ“œ(ğ•œ, A) â†’ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A) := Î» a, (a.left, a.right) -- (a.left, a.right)

@[simp] lemma prod_mk_def (a : ğ“œ(ğ•œ, A)) : prod_mk ğ•œ A a = (a.left, a.right) := rfl

variables {ğ•œ A}

lemma injective_prod_mk : function.injective (prod_mk ğ•œ A) :=
Î» a b h, ext a b (prod.ext_iff.mp h).1 (prod.ext_iff.mp h).2

lemma range_prod_mk : set.range (prod_mk ğ•œ A) = {lr | âˆ€ x y, lr.2 x * y = x * lr.1 y} :=
set.ext $ Î» x, âŸ¨by {rintro âŸ¨a, rflâŸ©, exact a.central}, Î» hx, âŸ¨âŸ¨x.1, x.2, hxâŸ©, by simpâŸ©âŸ©

instance : has_add ğ“œ(ğ•œ, A) :=
{ add := Î» a b,
  { left := a.left + b.left,
    right := a.right + b.right,
    central := Î» x y, by simp only [continuous_linear_map.add_apply, add_mul, mul_add, central] } }

instance : has_zero ğ“œ(ğ•œ, A) :=
{ zero :=
  { left := 0,
    right := 0,
    central := Î» x y, by simp only [continuous_linear_map.zero_apply, zero_mul, mul_zero] } }

instance : has_neg ğ“œ(ğ•œ, A) :=
{ neg := Î» a,
  { left := -(a.left),
    right := -(a.right),
    central := Î» x y, by simp only [continuous_linear_map.neg_apply, neg_mul,
                      mul_neg, neg_inj, central]}}

instance : has_sub ğ“œ(ğ•œ, A) :=
{ sub := Î» a b,
  { left := a.left - b.left,
    right := a.right - b.right,
    central := Î» x y, by simp only [continuous_linear_map.coe_sub', pi.sub_apply, sub_mul,
      mul_sub, central] } }

instance : has_smul ğ•œ ğ“œ(ğ•œ, A) :=
{ smul := Î» k a,
  { left := k â€¢ a.left,
    right := k â€¢ a.right,
    central := Î» x y , by simp only [continuous_linear_map.coe_smul', pi.smul_apply, central,
      mul_smul_comm, smul_mul_assoc] } }

@[simp] lemma add_left (a b : ğ“œ(ğ•œ, A)) : (a + b).left = a.left + b.left := rfl
@[simp] lemma add_right (a b : ğ“œ(ğ•œ, A)) : (a + b).right = a.right + b.right := rfl
@[simp] lemma zero_left : (0 : ğ“œ(ğ•œ, A)).left = 0 := rfl
@[simp] lemma zero_right : (0 : ğ“œ(ğ•œ, A)).right = 0 := rfl
@[simp] lemma neg_left (a : ğ“œ(ğ•œ, A)) : (-a).left = -a.left := rfl
@[simp] lemma neg_right (a : ğ“œ(ğ•œ, A)) : (-a).right = -a.right := rfl
@[simp] lemma sub_left (a b : ğ“œ(ğ•œ, A)) : (a - b).left = a.left - b.left := rfl
@[simp] lemma sub_right (a b : ğ“œ(ğ•œ, A)) : (a - b).right = a.right - b.right := rfl
@[simp] lemma smul_left (k : ğ•œ) (a : ğ“œ(ğ•œ, A)) : (k â€¢ a).left = k â€¢ a.left := rfl
@[simp] lemma smul_right (k : ğ•œ) (a : ğ“œ(ğ•œ, A)) : (k â€¢ a).right = k â€¢ a.right := rfl

-- this is easier than defining the instances of `has_smul` for `â„•` and `â„¤` and pulling the group
-- structure back along `double_centralizer.prod_mk`.
instance : add_comm_group ğ“œ(ğ•œ, A) :=
{ add := (+),
  add_assoc := Î» a b c, by {ext; exact add_assoc _ _ _},
  zero := 0,
  zero_add := Î» a, by {ext; exact zero_add _},
  add_zero := Î» a, by {ext; exact add_zero _},
  neg := Î» x, -x,
  sub := Î» x y,  x - y,
  sub_eq_add_neg := Î» a b, by {ext; exact sub_eq_add_neg _ _},
  add_left_neg := Î» a, by {ext; exact add_left_neg _},
  add_comm := Î» a b, by {ext; exact add_comm _ _}, }

def add_group_hom_prod_mk : ğ“œ(ğ•œ, A) â†’+ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A) :=
{ to_fun := prod_mk ğ•œ A,
  map_zero' := rfl,
  map_add' := Î» x y, rfl }

/-- The module structure is inherited as the pullback under the additive group monomoprhism
`double_centralizer.prod_mk : ğ“œ(ğ•œ, A) â†’+ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)` -/
instance : module ğ•œ ğ“œ(ğ•œ, A) :=
function.injective.module ğ•œ add_group_hom_prod_mk injective_prod_mk (Î» x y, rfl)

/-- The normed group structure is inherited as the pullback under the additive group monomoprhism
`double_centralizer.prod_mk : ğ“œ(ğ•œ, A) â†’+ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)` -/
instance : normed_add_comm_group ğ“œ(ğ•œ, A) :=
normed_add_comm_group.induced add_group_hom_prod_mk injective_prod_mk

instance : normed_space ğ•œ ğ“œ(ğ•œ, A) :=
{ norm_smul_le := Î» k a, show max (âˆ¥k â€¢ a.leftâˆ¥) (âˆ¥k â€¢ a.rightâˆ¥) â‰¤ âˆ¥kâˆ¥ * max (âˆ¥a.leftâˆ¥) (âˆ¥a.rightâˆ¥),
    by simp only [mul_max_of_nonneg _ _ (norm_nonneg k), norm_smul],
  .. double_centralizer.module }

lemma uniform_embedding_prod_mk : uniform_embedding (prod_mk ğ•œ A) :=
uniform_embedding_comap injective_prod_mk

instance [complete_space A] : complete_space ğ“œ(ğ•œ, A) :=
begin
  rw complete_space_iff_is_complete_range uniform_embedding_prod_mk.to_uniform_inducing,
  apply is_closed.is_complete,
  simp only [range_prod_mk, set.set_of_forall],
  refine is_closed_Inter (Î» x, is_closed_Inter $ Î» y, is_closed_eq _ _),
  { exact ((continuous_mul_right y).comp (continuous_linear_map.apply ğ•œ A x).continuous).comp
      continuous_snd },
  { exact ((continuous_mul_left x).comp (continuous_linear_map.apply ğ•œ A y).continuous).comp
      continuous_fst }
end

/-!
### Multiplicative structure
-/

instance : has_one ğ“œ(ğ•œ, A) :=
{ one :=
  { left := 1,
    right := 1,
    central := Î» x y, rfl } }

instance : has_mul ğ“œ(ğ•œ, A) :=
{ mul := Î» a b,
  { left := a.left.comp b.left,
    right := b.right.comp a.right,
    central := Î» x y, by simp only [continuous_linear_map.coe_comp', function.comp_app, central]}}

@[simp] lemma one_left : (1 : ğ“œ(ğ•œ, A)).left = 1 := rfl
@[simp] lemma one_right : (1 : ğ“œ(ğ•œ, A)).right = 1 := rfl
@[simp] lemma mul_left (a b : ğ“œ(ğ•œ, A)) : (a * b).left = a.left * b.left := rfl
@[simp] lemma mul_right (a b : ğ“œ(ğ•œ, A)) : (a * b).right = b.right * a.right := rfl

instance : ring ğ“œ(ğ•œ, A) :=
{ one := 1,
  mul := Î» x y, x * y,
  mul_assoc := Î» a b c, by {ext1; simp only [mul_left, mul_right, mul_assoc]},
  one_mul := Î» a, by {ext1; simp},
  mul_one := Î» a, by {ext1; simp},
  left_distrib := Î» a b c,
  begin
    ext1,
    { rw [mul_left, add_left, add_left],
      simp only [mul_add, mul_left] },
    { rw [mul_right, add_right, add_right],
      simp only [add_mul, mul_right] }
  end,
  right_distrib := Î» a b c,
  begin
    ext1,
    { rw [mul_left, add_left, add_left],
      simp only [add_mul, mul_left] },
    { rw [mul_right, add_right, add_right],
      simp only [mul_add, mul_right] },
  end,
  .. double_centralizer.add_comm_group }
/-!
### Star structure
-/

variables [star_ring ğ•œ] [star_ring A] [star_module ğ•œ A] [normed_star_group A]

instance : has_star ğ“œ(ğ•œ, A) :=
{ star := Î» a,
  { left := (((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A).comp a.right).comp
      ((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A),
    right := (((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A).comp a.left).comp
      ((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A),
    central := Î» x y, by simpa only [star_mul, star_star]
      using (congr_arg star (a.central (star y) (star x))).symm } }

@[simp] lemma star_left (a : ğ“œ(ğ•œ, A)) (b : A) : (star a).left b = star (a.right (star b)) := rfl
@[simp] lemma star_right (a : ğ“œ(ğ•œ, A)) (b : A) : (star a).right b = star (a.left (star b)) := rfl

instance : star_add_monoid ğ“œ(ğ•œ, A) :=
{ star_involutive := Î» x, by {ext; simp only [star_left, star_right, star_star]},
  star_add := Î» x y, by {ext; simp only [star_left, star_right, add_left, add_right,
    continuous_linear_map.add_apply, star_add]},
  .. double_centralizer.has_star }

instance : star_ring ğ“œ(ğ•œ, A) :=
{ star_mul := Î» a b, by {ext; simp only [star_left, star_right, mul_left, mul_right, star_star,
    continuous_linear_map.coe_mul, function.comp_app]},
  .. double_centralizer.star_add_monoid }

instance : star_module ğ•œ ğ“œ(ğ•œ, A) :=
{ star_smul := Î» k a, by {ext; exact star_smul _ _},
  .. double_centralizer.star_add_monoid }

/-!
### Norm structures
-/

noncomputable instance : normed_ring ğ“œ(ğ•œ, A) :=
{ norm_mul := Î» a b,
    begin
      refine max_le ((norm_mul_le _ _).trans _) ((norm_mul_le _ _).trans _),
      exact mul_le_mul (le_max_left _ _) (le_max_left _ _) (norm_nonneg _)
        ((norm_nonneg _).trans $ le_max_left _ _),
      exact mul_comm (âˆ¥a.rightâˆ¥) (âˆ¥b.rightâˆ¥) â–¸ mul_le_mul (le_max_right _ _) (le_max_right _ _)
        (norm_nonneg _) ((norm_nonneg _).trans $ le_max_right _ _),
    end,
  .. double_centralizer.ring,
  .. double_centralizer.normed_add_comm_group }

variables [cstar_ring A]

/-- For `a : ğ“œ(ğ•œ, A)`, the norms of `a.left` and `a.right` coincide, and hence these
also coincide with `âˆ¥aâˆ¥` which is `max (âˆ¥a.leftâˆ¥) (âˆ¥a.rightâˆ¥)`. -/
lemma norm_left_eq_right (a : ğ“œ(ğ•œ, A)) : âˆ¥a.leftâˆ¥ = âˆ¥a.rightâˆ¥ :=
begin
  -- a handy lemma for this proof
  have h0 : âˆ€ f : A â†’L[ğ•œ] A, âˆ€ C : â„â‰¥0, (âˆ€ b : A, âˆ¥f bâˆ¥â‚Š ^ 2 â‰¤ C * âˆ¥f bâˆ¥â‚Š * âˆ¥bâˆ¥â‚Š) â†’ âˆ¥fâˆ¥â‚Š â‰¤ C,
  { intros f C h,
    have h1 : âˆ€ b, C * âˆ¥f bâˆ¥â‚Š * âˆ¥bâˆ¥â‚Š â‰¤ C * âˆ¥fâˆ¥â‚Š * âˆ¥bâˆ¥â‚Š ^ 2,
    { intros b,
      convert mul_le_mul_right' (mul_le_mul_left' (f.le_op_nnnorm b) C) (âˆ¥bâˆ¥â‚Š) using 1,
      ring, },
    have := div_le_of_le_mul (f.op_nnnorm_le_bound _ (by simpa only [sqrt_sq, sqrt_mul]
      using (Î» b, sqrt_le_sqrt_iff.mpr ((h b).trans (h1 b))))),
    convert rpow_le_rpow this (by exact_mod_cast zero_le (2 : â„•) : 0 â‰¤ (2 : â„)),
    { simp only [rpow_two, div_pow, sq_sqrt], simp only [sq, mul_self_div_self] },
    { simp only [rpow_two, sq_sqrt] } },
  have h1 : âˆ€ b, âˆ¥ a.left b âˆ¥â‚Š ^ 2 â‰¤  âˆ¥ a.right âˆ¥â‚Š * âˆ¥ a.left b âˆ¥â‚Š * âˆ¥ b âˆ¥â‚Š,
  { intros b,
    calc âˆ¥ a.left b âˆ¥â‚Š ^ 2
        = âˆ¥ star (a.left b) * (a.left b) âˆ¥â‚Š
        : by simpa only [â†sq] using (cstar_ring.nnnorm_star_mul_self).symm
    ... â‰¤ âˆ¥ a.right (star (a.left b))âˆ¥â‚Š * âˆ¥ b âˆ¥â‚Š : a.central (star (a.left b)) b â–¸ nnnorm_mul_le _ _
    ... â‰¤ âˆ¥ a.right âˆ¥â‚Š * âˆ¥ a.left b âˆ¥â‚Š * âˆ¥ b âˆ¥â‚Š
        : nnnorm_star (a.left b) â–¸ mul_le_mul_right' (a.right.le_op_nnnorm _) _},
  have h2 : âˆ€ b, âˆ¥ a.right b âˆ¥â‚Š ^ 2 â‰¤  âˆ¥ a.left âˆ¥â‚Š * âˆ¥ a.right b âˆ¥â‚Š * âˆ¥ b âˆ¥â‚Š,
  { intros b,
    calc âˆ¥ a.right b âˆ¥â‚Š ^ 2
        = âˆ¥ a.right b * star (a.right b) âˆ¥â‚Š
        : by simpa only [â†sq] using (cstar_ring.nnnorm_self_mul_star).symm
    ... â‰¤ âˆ¥ b âˆ¥â‚Š * âˆ¥ a.left (star (a.right b))âˆ¥â‚Š
        : (a.central b (star (a.right b))).symm â–¸ nnnorm_mul_le _ _
    ... = âˆ¥ a.left (star (a.right b))âˆ¥â‚Š * âˆ¥bâˆ¥â‚Š : mul_comm _ _
    ... â‰¤ âˆ¥ a.left âˆ¥â‚Š * âˆ¥ a.right b âˆ¥â‚Š * âˆ¥ b âˆ¥â‚Š
        : nnnorm_star (a.right b) â–¸ mul_le_mul_right' (a.left.le_op_nnnorm _) _  },
  exact le_antisymm (h0 _ _ h1) (h0 _ _ h2),
end

@[simp] lemma norm_eq (a : ğ“œ(ğ•œ, A)) : âˆ¥aâˆ¥ = max (âˆ¥a.leftâˆ¥) (âˆ¥a.rightâˆ¥) := rfl
lemma norm_left (a : ğ“œ(ğ•œ, A)) : âˆ¥aâˆ¥ = âˆ¥a.leftâˆ¥ :=
by simp only [norm_eq, norm_left_eq_right, max_eq_right, eq_self_iff_true]
lemma norm_right (a : ğ“œ(ğ•œ, A)) : âˆ¥aâˆ¥ = âˆ¥a.rightâˆ¥ := by rw [norm_left, norm_left_eq_right]

/- I think we don't have the necessary type class to make this lemma true.
`nontrivially_normed_field ğ•œ` is too weak, but `is_R_or_C ğ•œ` is far too strong. What we
want is a type class for `ğ•œ` where we can say `Î» k : ğ•œ, âˆ¥kâˆ¥` has dense range in `â„`. -/
lemma normed_field.exists_nnnorm_lt_and_lt {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  (r : â„) (hr : 0 < r) : âˆƒ k : ğ•œ, 1 - r < âˆ¥kâˆ¥ âˆ§ âˆ¥kâˆ¥ < 1 :=
begin
  sorry
end

-- it would be nice if maybe we could get this for `â„â‰¥0` instead, but we go to `â„â‰¥0âˆ` because it
-- is a complete lattice and therefore `supr` is well-behaved.
lemma key_lemma {ğ•œ E : Type*} [nontrivially_normed_field ğ•œ] [non_unital_normed_ring E] [star_ring E]
  [cstar_ring E] [module ğ•œ E] [is_scalar_tower ğ•œ E E] [normed_space ğ•œ E] (a : E) :
  (âˆ¥aâˆ¥â‚Š : â„â‰¥0âˆ) = â¨† b (hb : âˆ¥bâˆ¥â‚Š â‰¤ 1), âˆ¥b * aâˆ¥â‚Š :=
begin
  refine le_antisymm _ (suprâ‚‚_le (Î» b hb, _)),
  { by_cases h : âˆ¥aâˆ¥â‚Š = 0,
    { rw h, exact_mod_cast zero_le _ },
    { refine ennreal.le_of_forall_pos_le_add (Î» Îµ hÎµ h_lt, _),
      rw ennreal.bsupr_add' (âŸ¨0, by simp only [nnnorm_zero, zero_le']âŸ© : âˆƒ x : E, âˆ¥xâˆ¥â‚Š â‰¤ 1),
      /- we now want to choose some `k : ğ•œ` such that `(1 + Îµ * âˆ¥aâˆ¥â‚Šâ»Â¹)â»Â¹ * âˆ¥aâˆ¥â‚Š < âˆ¥k'âˆ¥â‚Š < 1`, then
      we will apply `refine le_trans _ (le_suprâ‚‚ (kâ»Â¹ â€¢ (star a)) _)`; This is why we want that
      lemma above. -/
      sorry, } },
  { calc (âˆ¥b * aâˆ¥â‚Š : â„â‰¥0âˆ) â‰¤ âˆ¥bâˆ¥â‚Š * âˆ¥aâˆ¥â‚Š : by exact_mod_cast norm_mul_le _ _
    ...                    â‰¤ âˆ¥aâˆ¥â‚Š : by simpa using (ennreal.coe_mono $ mul_le_mul_right' hb _) }
end

instance : cstar_ring ğ“œ(ğ•œ, A) :=
{ norm_star_mul_self := sorry }

/-!
### Coercion from an algebra into its multiplier algebra
-/

noncomputable instance : has_coe_t A ğ“œ(ğ•œ, A) :=
{ coe := Î» a,
  { left := continuous_linear_map.lmul ğ•œ A a,
    right := continuous_linear_map.lmul_right ğ•œ A a,
    central := Î» x y, mul_assoc _ _ _ } }

@[simp, norm_cast]
lemma coe_left (a : A) : (a : ğ“œ(ğ•œ, A)).left = continuous_linear_map.lmul ğ•œ A a := rfl
@[simp, norm_cast]
lemma coe_right (a : A) : (a : ğ“œ(ğ•œ, A)).right = continuous_linear_map.lmul_right ğ•œ A a := rfl

-- TODO: make this into a `non_unital_star_alg_hom` once we have those
def non_unital_algebra_hom_coe : A â†’â‚™â‚[ğ•œ] ğ“œ(ğ•œ, A) :=
{ to_fun := Î» a, a,
  map_smul' := Î» k a, by {ext1; simp only [coe_left, coe_right, continuous_linear_map.map_smul,
    smul_left, smul_right]},
  map_zero' := by {ext1; simp only [coe_left, coe_right, map_zero, zero_left, zero_right]},
  map_add' := Î» a b, by {ext1; simp only [coe_left, coe_right, map_add, add_left, add_right]},
  map_mul' := Î» a b, by {ext; simp only [coe_left, coe_right, continuous_linear_map.lmul_apply,
    continuous_linear_map.lmul_right_apply, mul_left, mul_right, coe_mul, function.comp_app,
    mul_assoc]} }

end double_centralizer
