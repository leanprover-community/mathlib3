/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import analysis.normed_space.star.basic
import analysis.normed_space.spectrum
import analysis.normed_space.star.exponential
import analysis.special_functions.exponential
import algebra.star.star_alg_hom

/-! # Spectral properties in C‚ãÜ-algebras
In this file, we establish various propreties related to the spectrum of elements in C‚ãÜ-algebras.
-/

local postfix `‚ãÜ`:std.prec.max_plus := star

section

open_locale topological_space ennreal
open filter ennreal spectrum cstar_ring

section unitary_spectrum

variables
{ùïú : Type*} [normed_field ùïú]
{E : Type*} [normed_ring E] [star_ring E] [cstar_ring E]
[normed_algebra ùïú E] [complete_space E]

lemma unitary.spectrum_subset_circle (u : unitary E) :
  spectrum ùïú (u : E) ‚äÜ metric.sphere 0 1 :=
begin
  nontriviality E,
  refine Œª k hk, mem_sphere_zero_iff_norm.mpr (le_antisymm _ _),
  { simpa only [cstar_ring.norm_coe_unitary u] using norm_le_norm_of_mem hk },
  { rw ‚Üêunitary.coe_to_units_apply u at hk,
    have hnk := ne_zero_of_mem_of_unit hk,
    rw [‚Üêinv_inv (unitary.to_units u), ‚Üêspectrum.map_inv, set.mem_inv] at hk,
    have : ‚à•k‚à•‚Åª¬π ‚â§ ‚à•‚Üë((unitary.to_units u)‚Åª¬π)‚à•, simpa only [norm_inv] using norm_le_norm_of_mem hk,
    simpa using inv_le_of_inv_le (norm_pos_iff.mpr hnk) this }
end

lemma spectrum.subset_circle_of_unitary {u : E} (h : u ‚àà unitary E) :
  spectrum ùïú u ‚äÜ metric.sphere 0 1 :=
unitary.spectrum_subset_circle ‚ü®u, h‚ü©

end unitary_spectrum

section complex_scalars

open complex

variables {A : Type*}
[normed_ring A] [normed_algebra ‚ÑÇ A] [complete_space A] [star_ring A] [cstar_ring A]

local notation `‚Üë‚Çê` := algebra_map ‚ÑÇ A

lemma is_self_adjoint.spectral_radius_eq_nnnorm {a : A}
  (ha : is_self_adjoint a) :
  spectral_radius ‚ÑÇ a = ‚à•a‚à•‚Çä :=
begin
  have hconst : tendsto (Œª n : ‚Ñï, (‚à•a‚à•‚Çä : ‚Ñù‚â•0‚àû)) at_top _ := tendsto_const_nhds,
  refine tendsto_nhds_unique _ hconst,
  convert (spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (a : A)).comp
      (nat.tendsto_pow_at_top_at_top_of_one_lt one_lt_two),
  refine funext (Œª n, _),
  rw [function.comp_app, ha.nnnorm_pow_two_pow, ennreal.coe_pow, ‚Üêrpow_nat_cast,
    ‚Üêrpow_mul],
  simp,
end

lemma is_star_normal.spectral_radius_eq_nnnorm (a : A) [is_star_normal a] :
  spectral_radius ‚ÑÇ a = ‚à•a‚à•‚Çä :=
begin
  refine (ennreal.pow_strict_mono two_ne_zero).injective _,
  have heq : (Œª n : ‚Ñï, ((‚à•(a‚ãÜ * a) ^ n‚à•‚Çä ^ (1 / n : ‚Ñù)) : ‚Ñù‚â•0‚àû))
    = (Œª x, x ^ 2) ‚àò (Œª n : ‚Ñï, ((‚à•a ^ n‚à•‚Çä ^ (1 / n : ‚Ñù)) : ‚Ñù‚â•0‚àû)),
  { funext,
    rw [function.comp_apply, ‚Üêrpow_nat_cast, ‚Üêrpow_mul, mul_comm, rpow_mul, rpow_nat_cast,
      ‚Üêcoe_pow, sq, ‚Üênnnorm_star_mul_self, commute.mul_pow (star_comm_self' a), star_pow], },
  have h‚ÇÇ := ((ennreal.continuous_pow 2).tendsto (spectral_radius ‚ÑÇ a)).comp
    (spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius a),
  rw ‚Üêheq at h‚ÇÇ,
  convert tendsto_nhds_unique h‚ÇÇ (pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (a‚ãÜ * a)),
  rw [(is_self_adjoint.star_mul_self a).spectral_radius_eq_nnnorm, sq, nnnorm_star_mul_self,
    coe_mul],
end

/-- Any element of the spectrum of a selfadjoint is real. -/
theorem is_self_adjoint.mem_spectrum_eq_re [star_module ‚ÑÇ A] {a : A}
  (ha : is_self_adjoint a) {z : ‚ÑÇ} (hz : z ‚àà spectrum ‚ÑÇ a) : z = z.re :=
begin
  let Iu := units.mk0 I I_ne_zero,
  have : exp ‚ÑÇ (I ‚Ä¢ z) ‚àà spectrum ‚ÑÇ (exp ‚ÑÇ (I ‚Ä¢ a)),
    by simpa only [units.smul_def, units.coe_mk0]
      using spectrum.exp_mem_exp (Iu ‚Ä¢ a) (smul_mem_smul_iff.mpr hz),
  exact complex.ext (of_real_re _)
    (by simpa only [‚Üêcomplex.exp_eq_exp_‚ÑÇ, mem_sphere_zero_iff_norm, norm_eq_abs, abs_exp,
      real.exp_eq_one_iff, smul_eq_mul, I_mul, neg_eq_zero]
      using spectrum.subset_circle_of_unitary ha.exp_i_smul_unitary this),
end

/-- Any element of the spectrum of a selfadjoint is real. -/
theorem self_adjoint.mem_spectrum_eq_re [star_module ‚ÑÇ A]
  (a : self_adjoint A) {z : ‚ÑÇ} (hz : z ‚àà spectrum ‚ÑÇ (a : A)) : z = z.re :=
a.prop.mem_spectrum_eq_re hz

/-- The spectrum of a selfadjoint is real -/
theorem is_self_adjoint.coe_re_map_spectrum [star_module ‚ÑÇ A] {a : A}
  (ha : is_self_adjoint a) : spectrum ‚ÑÇ a = (coe ‚àò re '' (spectrum ‚ÑÇ a) : set ‚ÑÇ) :=
le_antisymm (Œª z hz, ‚ü®z, hz, (ha.mem_spectrum_eq_re hz).symm‚ü©) (Œª z, by
  { rintros ‚ü®z, hz, rfl‚ü©,
    simpa only [(ha.mem_spectrum_eq_re hz).symm, function.comp_app] using hz })

/-- The spectrum of a selfadjoint is real -/
theorem self_adjoint.coe_re_map_spectrum [star_module ‚ÑÇ A] (a : self_adjoint A) :
  spectrum ‚ÑÇ (a : A) = (coe ‚àò re '' (spectrum ‚ÑÇ (a : A)) : set ‚ÑÇ) :=
a.property.coe_re_map_spectrum

end complex_scalars

namespace star_alg_hom

variables {F A B : Type*}
[normed_ring A] [normed_algebra ‚ÑÇ A] [complete_space A] [star_ring A] [cstar_ring A]
[normed_ring B] [normed_algebra ‚ÑÇ B] [complete_space B] [star_ring B] [cstar_ring B]
[hF : star_alg_hom_class F ‚ÑÇ A B] (œÜ : F)
include hF

/-- A star algebra homomorphism of complex C‚ãÜ-algebras is norm contractive. -/
lemma nnnorm_apply_le (a : A) : ‚à•(œÜ a : B)‚à•‚Çä ‚â§ ‚à•a‚à•‚Çä :=
begin
  suffices : ‚àÄ s : A, is_self_adjoint s ‚Üí ‚à•œÜ s‚à•‚Çä ‚â§ ‚à•s‚à•‚Çä,
  { exact nonneg_le_nonneg_of_sq_le_sq zero_le'
      (by simpa only [nnnorm_star_mul_self, map_star, map_mul]
      using this _ (is_self_adjoint.star_mul_self a)) },
  { intros s hs,
    simpa only [hs.spectral_radius_eq_nnnorm, (hs.star_hom_apply œÜ).spectral_radius_eq_nnnorm,
      coe_le_coe] using (show spectral_radius ‚ÑÇ (œÜ s) ‚â§ spectral_radius ‚ÑÇ s,
      from supr_le_supr_of_subset (alg_hom.spectrum_apply_subset œÜ s)) }
end

/-- A star algebra homomorphism of complex C‚ãÜ-algebras is norm contractive. -/
lemma norm_apply_le (a : A) : ‚à•(œÜ a : B)‚à• ‚â§ ‚à•a‚à• := nnnorm_apply_le œÜ a

/-- Star algebra homomorphisms between C‚ãÜ-algebras are continuous linear maps.
See note [lower instance priority] -/
@[priority 100]
noncomputable instance : continuous_linear_map_class F ‚ÑÇ A B :=
{ map_continuous := Œª œÜ, add_monoid_hom_class.continuous_of_bound œÜ 1
    (by simpa only [one_mul] using nnnorm_apply_le œÜ),
  .. alg_hom_class.linear_map_class }

end star_alg_hom

end

namespace weak_dual

open continuous_map complex
open_locale complex_star_module

variables {F A : Type*} [normed_ring A] [normed_algebra ‚ÑÇ A] [complete_space A]
  [star_ring A] [cstar_ring A] [star_module ‚ÑÇ A] [hF : alg_hom_class F ‚ÑÇ A ‚ÑÇ]

include hF

/-- This instance is provided instead of `star_alg_hom_class` to avoid type class inference loops.
See note [lower instance priority] -/
@[priority 100]
noncomputable instance : star_hom_class F A ‚ÑÇ :=
{ coe := Œª œÜ, œÜ,
  coe_injective' := fun_like.coe_injective',
  map_star := Œª œÜ a,
  begin
    suffices hsa : ‚àÄ s : self_adjoint A, (œÜ s)‚ãÜ = œÜ s,
    { rw ‚Üêreal_part_add_I_smul_imaginary_part a,
      simp only [map_add, map_smul, star_add, star_smul, hsa, self_adjoint.star_coe_eq] },
    { intros s,
      have := alg_hom.apply_mem_spectrum œÜ (s : A),
      rw self_adjoint.coe_re_map_spectrum s at this,
      rcases this with ‚ü®‚ü®_, _‚ü©, _, heq‚ü©,
      rw [‚Üêheq, is_R_or_C.star_def, is_R_or_C.conj_of_real] }
  end }

/-- This is not an instance to avoid type class inference loops. See
`weak_dual.complex.star_hom_class`. -/
noncomputable def _root_.alg_hom_class.star_alg_hom_class : star_alg_hom_class F ‚ÑÇ A ‚ÑÇ :=
{ coe := Œª f, f,
  .. weak_dual.complex.star_hom_class,
  .. hF }

omit hF

namespace character_space

noncomputable instance : star_alg_hom_class (character_space ‚ÑÇ A) ‚ÑÇ A ‚ÑÇ :=
{ coe := Œª f, f,
  .. alg_hom_class.star_alg_hom_class }

end character_space

end weak_dual
