/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import analysis.normed_space.star.basic
import analysis.normed_space.spectrum
import algebra.star.module
import analysis.special_functions.exponential

/-! # Spectral properties in Câ‹†-algebras
In this file, we establish various propreties related to the spectrum of elements in Câ‹†-algebras.
-/

open_locale topological_space ennreal
open filter ennreal spectrum

local postfix `â‹†`:std.prec.max_plus := star

section unitary_spectrum

variables
{ğ•œ : Type*} [normed_field ğ•œ]
{E : Type*} [normed_ring E] [star_ring E] [cstar_ring E]
[normed_algebra ğ•œ E] [complete_space E] [nontrivial E]

lemma unitary.spectrum_subset_circle (u : unitary E) :
  spectrum ğ•œ (u : E) âŠ† metric.sphere 0 1 :=
begin
  refine Î» k hk, mem_sphere_zero_iff_norm.mpr (le_antisymm _ _),
  { simpa only [cstar_ring.norm_coe_unitary u] using norm_le_norm_of_mem hk },
  { rw â†unitary.coe_to_units_apply u at hk,
    have hnk := ne_zero_of_mem_of_unit hk,
    rw [â†inv_inv (unitary.to_units u), â†spectrum.map_inv, set.mem_inv] at hk,
    have : âˆ¥kâˆ¥â»Â¹ â‰¤ âˆ¥â†‘((unitary.to_units u)â»Â¹)âˆ¥, simpa only [norm_inv] using norm_le_norm_of_mem hk,
    simpa using inv_le_of_inv_le (norm_pos_iff.mpr hnk) this }
end

lemma spectrum.subset_circle_of_unitary {u : E} (h : u âˆˆ unitary E) :
  spectrum ğ•œ u âŠ† metric.sphere 0 1 :=
unitary.spectrum_subset_circle âŸ¨u, hâŸ©

end unitary_spectrum

section complex_scalars

variables {A : Type*}
[normed_ring A] [normed_algebra â„‚ A] [star_ring A] [cstar_ring A] [complete_space A]
[measurable_space A] [borel_space A] [topological_space.second_countable_topology A]

lemma spectral_radius_eq_nnnorm_of_self_adjoint {a : A} (ha : a âˆˆ self_adjoint A) :
  spectral_radius â„‚ a = âˆ¥aâˆ¥â‚Š :=
begin
  have hconst : tendsto (Î» n : â„•, (âˆ¥aâˆ¥â‚Š : â„â‰¥0âˆ)) at_top _ := tendsto_const_nhds,
  refine tendsto_nhds_unique _ hconst,
  convert (spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (a : A)).comp
      (nat.tendsto_pow_at_top_at_top_of_one_lt (by linarith : 1 < 2)),
  refine funext (Î» n, _),
  rw [function.comp_app, nnnorm_pow_two_pow_of_self_adjoint ha, ennreal.coe_pow, â†rpow_nat_cast,
    â†rpow_mul],
  simp,
end

lemma self_adjoint.coe_spectral_radius_eq_nnnorm (a : self_adjoint A) :
  spectral_radius â„‚ (a : A) = âˆ¥(a : A)âˆ¥â‚Š :=
spectral_radius_eq_nnnorm_of_self_adjoint a.property

end complex_scalars


/-- The inclusion of the base field in a algebra as a continuous linear map. -/
@[simps]
def algebra_map_clm (ğ•œ : Type*) (E : Type*) [normed_field ğ•œ] [semi_normed_ring E]
  [normed_algebra ğ•œ E] : ğ•œ â†’L[ğ•œ] E :=
{ to_fun := algebra_map ğ•œ E,
  map_add' := (algebra_map ğ•œ E).map_add,
  map_smul' := Î» r x, by rw [algebra.id.smul_eq_mul, map_mul, ring_hom.id_apply, algebra.smul_def],
  cont := (algebra_map_isometry ğ•œ E).continuous }

lemma algebra_map_clm_coe (ğ•œ : Type*) (E : Type*) [normed_field ğ•œ] [semi_normed_ring E]
  [normed_algebra ğ•œ E] : (algebra_map_clm ğ•œ E : ğ•œ â†’ E) = (algebra_map ğ•œ E : ğ•œ â†’ E) := rfl

lemma star_exp {ğ•œ A : Type*} [is_R_or_C ğ•œ] [normed_ring A] [normed_algebra ğ•œ A]
  [star_ring A] [cstar_ring A] [complete_space A]
  [star_module ğ•œ A] (a : A) : (exp ğ•œ A a)â‹† = exp ğ•œ A aâ‹† :=
begin
  rw exp_eq_tsum,
  have := continuous_linear_map.map_tsum
    (starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A).to_linear_isometry.to_continuous_linear_map
    (exp_series_summable' a),
  dsimp at this,
  convert this,
  funext,
  simp only [star_smul, star_pow, one_div, is_R_or_C.star_def, is_R_or_C.conj_inv, map_nat_cast],
end

lemma algebra_map_exp_comm (ğ•œ : Type*) (A : Type*) [is_R_or_C ğ•œ] [normed_ring A]
  [normed_algebra ğ•œ A] [complete_space A] (z : ğ•œ) :
  algebra_map ğ•œ A (exp ğ•œ ğ•œ z) = exp ğ•œ A (algebra_map ğ•œ A z) :=
begin
  rw [exp_eq_tsum, exp_eq_tsum, â†algebra_map_clm_coe,
    (algebra_map_clm ğ•œ A).map_tsum (exp_series_summable' z)],
  simp_rw [(algebra_map_clm ğ•œ A).map_smul, algebra_map_clm_coe, map_pow],
end

variables {A : Type*}
[normed_ring A] [normed_algebra â„‚ A] [star_ring A] [cstar_ring A] [complete_space A]
[star_module â„‚ A]

open complex

lemma self_adjoint.exp_i_smul_unitary {a : A} (ha : a âˆˆ self_adjoint A) :
  exp â„‚ A (I â€¢ a) âˆˆ unitary A :=
begin
  rw [unitary.mem_iff, star_exp],
  simp only [star_smul, is_R_or_C.star_def, self_adjoint.mem_iff.mp ha, conj_I, neg_smul],
  rw â†@exp_add_of_commute â„‚ A _ _ _ _ _ _ ((commute.refl (I â€¢ a)).neg_left),
  rw â†@exp_add_of_commute â„‚ A _ _ _ _ _ _ ((commute.refl (I â€¢ a)).neg_right),
  simpa only [add_right_neg, add_left_neg, and_self] using (exp_zero : exp â„‚ A 0 = 1),
end

/-- The map from the selfadjoint real subspace to the unitary group. This map only makes sense
over â„‚. -/
@[simps]
noncomputable def self_adjoint.exp_unitary (a : self_adjoint A) : unitary A :=
âŸ¨exp â„‚ A (I â€¢ a), self_adjoint.exp_i_smul_unitary (a.property)âŸ©

open self_adjoint

lemma commute.exp_unitary_add {a b : self_adjoint A} (h : commute (a : A) (b : A)) :
  exp_unitary (a + b) = exp_unitary a * exp_unitary b :=
begin
  ext,
  have hcomm : commute (I â€¢ (a : A)) (I â€¢ (b : A)),
  calc _ = _ : by simp only [h.eq, algebra.smul_mul_assoc, algebra.mul_smul_comm],
  simpa only [exp_unitary_coe, add_subgroup.coe_add, smul_add] using exp_add_of_commute hcomm,
end

lemma commute.exp_unitary {a b : self_adjoint A} (h : commute (a : A) (b : A)) :
  commute (exp_unitary a) (exp_unitary b) :=
calc (exp_unitary a) * (exp_unitary b) = (exp_unitary b) * (exp_unitary a)
  : by rw [â†h.exp_unitary_add, â†h.symm.exp_unitary_add, add_comm]


local notation `â†‘â‚` := algebra_map â„‚ A

set_option profiler true

/-- `exp â„‚ â„‚` maps the spectrum of `a` into the spectrum of `exp â„‚ A a`. -/
theorem spectrum.exp_mem (a : A) {z : â„‚} (hz : z âˆˆ spectrum â„‚ a) :
  exp â„‚ â„‚ z âˆˆ spectrum â„‚ (exp â„‚ A a) :=
begin
  have hexpmul : exp â„‚ A a = exp â„‚ A (a - â†‘â‚ z) * â†‘â‚ (exp â„‚ â„‚ z),
  { rw [algebra_map_exp_comm â„‚ A z, â†exp_add_of_commute (algebra.commutes z (a - â†‘â‚z)).symm,
      sub_add_cancel] },
  let b := âˆ‘' n : â„•, ((1 / (n + 1).factorial) : â„‚) â€¢ (a - â†‘â‚z) ^ n,
  have hb : summable (Î» n : â„•, ((1 / (n + 1).factorial) : â„‚) â€¢ (a - â†‘â‚z) ^ n),
  { refine summable_of_norm_bounded_eventually _ (real.summable_pow_div_factorial âˆ¥a - â†‘â‚zâˆ¥) _,
    filter_upwards [eventually_cofinite_ne 0] with n hn,
    field_simp [norm_smul],
    exact div_le_div (pow_nonneg (norm_nonneg _) n) (norm_pow_le' (a - â†‘â‚z) (zero_lt_iff.mpr hn))
      (by exact_mod_cast nat.factorial_pos n)
      (by exact_mod_cast nat.factorial_le (lt_add_one n).le) },
  have hâ‚€ : âˆ‘' n : â„•, ((1 / (n + 1).factorial) : â„‚) â€¢ (a - â†‘â‚z) ^ (n + 1) = (a - â†‘â‚z) * b,
    { simpa only [mul_smul_comm, pow_succ] using hb.tsum_mul_left (a - â†‘â‚z) },
  have hâ‚ : âˆ‘' n : â„•, ((1 / (n + 1).factorial) : â„‚) â€¢ (a - â†‘â‚z) ^ (n + 1) = b * (a - â†‘â‚z),
    { simpa only [pow_succ', algebra.smul_mul_assoc] using hb.tsum_mul_right (a - â†‘â‚z) },
  have hâ‚ƒ : exp â„‚ A (a - â†‘â‚z) = 1 + (a - â†‘â‚z) * b,
  { rw exp_eq_tsum,
    convert tsum_eq_zero_add (exp_series_summable' (a - â†‘â‚z)),
    simp only [nat.factorial_zero, nat.cast_one, _root_.div_one, pow_zero, one_smul],
    exact hâ‚€.symm },
  rw [spectrum.mem_iff, is_unit.sub_iff, â†one_mul (â†‘â‚(exp â„‚ â„‚ z)), hexpmul, â†_root_.sub_mul,
    commute.is_unit_mul_iff (algebra.commutes (exp â„‚ â„‚ z) (exp â„‚ A (a - â†‘â‚z) - 1)).symm,
    sub_eq_iff_eq_add'.mpr hâ‚ƒ, commute.is_unit_mul_iff (hâ‚€ â–¸ hâ‚ : (a - â†‘â‚z) * b = b * (a - â†‘â‚z))],
  exact not_and_of_not_left _ (not_and_of_not_left _ ((not_iff_not.mpr is_unit.sub_iff).mp hz)),
end

open_locale pointwise

theorem self_adjoint.mem_spectrum_eq_re [nontrivial A] {a : A} (ha : a âˆˆ self_adjoint A) {z : â„‚}
  (hz : z âˆˆ spectrum â„‚ a) : z = z.re :=
begin
  let Iu := units.mk0 I I_ne_zero,
  have : exp â„‚ â„‚ (I â€¢ z) âˆˆ spectrum â„‚ (exp â„‚ A (I â€¢ a)),
    by simpa only [units.smul_def, units.coe_mk0]
      using spectrum.exp_mem (Iu â€¢ a) (smul_mem_smul_iff.mpr hz),
  exact complex.ext (of_real_re _)
    (by simpa only [â†complex.exp_eq_exp_â„‚_â„‚, mem_sphere_zero_iff_norm, norm_eq_abs, abs_exp,
      real.exp_eq_one_iff, smul_eq_mul, I_mul, neg_eq_zero]
      using spectrum.subset_circle_of_unitary (exp_i_smul_unitary ha) this),
end

theorem self_adjoint.coe_re_map_spectrum [nontrivial A] {a : A} (ha : a âˆˆ self_adjoint A) :
  spectrum â„‚ a = (coe âˆ˜ re '' (spectrum â„‚ a) : set â„‚) :=
le_antisymm (Î» z hz, âŸ¨z, hz, (self_adjoint.mem_spectrum_eq_re ha hz).symmâŸ©) (Î» z,
  by { rintros âŸ¨z, hz, rflâŸ©,
       simpa only [(self_adjoint.mem_spectrum_eq_re ha hz).symm, function.comp_app] using hz })
