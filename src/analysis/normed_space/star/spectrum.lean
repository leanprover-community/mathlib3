/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import analysis.normed_space.star.basic
import analysis.normed_space.spectrum
import analysis.normed_space.star.exponential
import analysis.special_functions.exponential
import algebra.star.star_alg_hom

/-! # Spectral properties in Câ‹†-algebras
In this file, we establish various properties related to the spectrum of elements in Câ‹†-algebras.
-/

local postfix `â‹†`:std.prec.max_plus := star

section

open_locale topology ennreal
open filter ennreal spectrum cstar_ring

section unitary_spectrum

variables
{ğ•œ : Type*} [normed_field ğ•œ]
{E : Type*} [normed_ring E] [star_ring E] [cstar_ring E]
[normed_algebra ğ•œ E] [complete_space E]

lemma unitary.spectrum_subset_circle (u : unitary E) :
  spectrum ğ•œ (u : E) âŠ† metric.sphere 0 1 :=
begin
  nontriviality E,
  refine Î» k hk, mem_sphere_zero_iff_norm.mpr (le_antisymm _ _),
  { simpa only [cstar_ring.norm_coe_unitary u] using norm_le_norm_of_mem hk },
  { rw â†unitary.coe_to_units_apply u at hk,
    have hnk := ne_zero_of_mem_of_unit hk,
    rw [â†inv_inv (unitary.to_units u), â†spectrum.map_inv, set.mem_inv] at hk,
    have : â€–kâ€–â»Â¹ â‰¤ â€–â†‘((unitary.to_units u)â»Â¹)â€–, simpa only [norm_inv] using norm_le_norm_of_mem hk,
    simpa using inv_le_of_inv_le (norm_pos_iff.mpr hnk) this }
end

lemma spectrum.subset_circle_of_unitary {u : E} (h : u âˆˆ unitary E) :
  spectrum ğ•œ u âŠ† metric.sphere 0 1 :=
unitary.spectrum_subset_circle âŸ¨u, hâŸ©

end unitary_spectrum

section complex_scalars

open complex

variables {A : Type*}
[normed_ring A] [normed_algebra â„‚ A] [complete_space A] [star_ring A] [cstar_ring A]

local notation `â†‘â‚` := algebra_map â„‚ A

lemma is_self_adjoint.spectral_radius_eq_nnnorm {a : A}
  (ha : is_self_adjoint a) :
  spectral_radius â„‚ a = â€–aâ€–â‚Š :=
begin
  have hconst : tendsto (Î» n : â„•, (â€–aâ€–â‚Š : â„â‰¥0âˆ)) at_top _ := tendsto_const_nhds,
  refine tendsto_nhds_unique _ hconst,
  convert (spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (a : A)).comp
      (nat.tendsto_pow_at_top_at_top_of_one_lt one_lt_two),
  refine funext (Î» n, _),
  rw [function.comp_app, ha.nnnorm_pow_two_pow, ennreal.coe_pow, â†rpow_nat_cast,
    â†rpow_mul],
  simp,
end

lemma is_star_normal.spectral_radius_eq_nnnorm (a : A) [is_star_normal a] :
  spectral_radius â„‚ a = â€–aâ€–â‚Š :=
begin
  refine (ennreal.pow_strict_mono two_ne_zero).injective _,
  have heq : (Î» n : â„•, ((â€–(aâ‹† * a) ^ nâ€–â‚Š ^ (1 / n : â„)) : â„â‰¥0âˆ))
    = (Î» x, x ^ 2) âˆ˜ (Î» n : â„•, ((â€–a ^ nâ€–â‚Š ^ (1 / n : â„)) : â„â‰¥0âˆ)),
  { funext,
    rw [function.comp_apply, â†rpow_nat_cast, â†rpow_mul, mul_comm, rpow_mul, rpow_nat_cast,
      â†coe_pow, sq, â†nnnorm_star_mul_self, commute.mul_pow (star_comm_self' a), star_pow], },
  have hâ‚‚ := ((ennreal.continuous_pow 2).tendsto (spectral_radius â„‚ a)).comp
    (spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius a),
  rw â†heq at hâ‚‚,
  convert tendsto_nhds_unique hâ‚‚ (pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius (aâ‹† * a)),
  rw [(is_self_adjoint.star_mul_self a).spectral_radius_eq_nnnorm, sq, nnnorm_star_mul_self,
    coe_mul],
end

/-- Any element of the spectrum of a selfadjoint is real. -/
theorem is_self_adjoint.mem_spectrum_eq_re [star_module â„‚ A] {a : A}
  (ha : is_self_adjoint a) {z : â„‚} (hz : z âˆˆ spectrum â„‚ a) : z = z.re :=
begin
  have hu := exp_mem_unitary_of_mem_skew_adjoint â„‚ (ha.smul_mem_skew_adjoint conj_I),
  let Iu := units.mk0 I I_ne_zero,
  have : exp â„‚ (I â€¢ z) âˆˆ spectrum â„‚ (exp â„‚ (I â€¢ a)),
    by simpa only [units.smul_def, units.coe_mk0]
      using spectrum.exp_mem_exp (Iu â€¢ a) (smul_mem_smul_iff.mpr hz),
  exact complex.ext (of_real_re _)
    (by simpa only [â†complex.exp_eq_exp_â„‚, mem_sphere_zero_iff_norm, norm_eq_abs, abs_exp,
      real.exp_eq_one_iff, smul_eq_mul, I_mul, neg_eq_zero]
      using spectrum.subset_circle_of_unitary hu this),
end

/-- Any element of the spectrum of a selfadjoint is real. -/
theorem self_adjoint.mem_spectrum_eq_re [star_module â„‚ A]
  (a : self_adjoint A) {z : â„‚} (hz : z âˆˆ spectrum â„‚ (a : A)) : z = z.re :=
a.prop.mem_spectrum_eq_re hz

/-- The spectrum of a selfadjoint is real -/
theorem is_self_adjoint.coe_re_map_spectrum [star_module â„‚ A] {a : A}
  (ha : is_self_adjoint a) : spectrum â„‚ a = (coe âˆ˜ re '' (spectrum â„‚ a) : set â„‚) :=
le_antisymm (Î» z hz, âŸ¨z, hz, (ha.mem_spectrum_eq_re hz).symmâŸ©) (Î» z, by
  { rintros âŸ¨z, hz, rflâŸ©,
    simpa only [(ha.mem_spectrum_eq_re hz).symm, function.comp_app] using hz })

/-- The spectrum of a selfadjoint is real -/
theorem self_adjoint.coe_re_map_spectrum [star_module â„‚ A] (a : self_adjoint A) :
  spectrum â„‚ (a : A) = (coe âˆ˜ re '' (spectrum â„‚ (a : A)) : set â„‚) :=
a.property.coe_re_map_spectrum

end complex_scalars

namespace star_alg_hom

variables {F A B : Type*}
[normed_ring A] [normed_algebra â„‚ A] [complete_space A] [star_ring A] [cstar_ring A]
[normed_ring B] [normed_algebra â„‚ B] [complete_space B] [star_ring B] [cstar_ring B]
[hF : star_alg_hom_class F â„‚ A B] (Ï† : F)
include hF

/-- A star algebra homomorphism of complex Câ‹†-algebras is norm contractive. -/
lemma nnnorm_apply_le (a : A) : â€–(Ï† a : B)â€–â‚Š â‰¤ â€–aâ€–â‚Š :=
begin
  suffices : âˆ€ s : A, is_self_adjoint s â†’ â€–Ï† sâ€–â‚Š â‰¤ â€–sâ€–â‚Š,
  { exact nonneg_le_nonneg_of_sq_le_sq zero_le'
      (by simpa only [nnnorm_star_mul_self, map_star, map_mul]
      using this _ (is_self_adjoint.star_mul_self a)) },
  { intros s hs,
    simpa only [hs.spectral_radius_eq_nnnorm, (hs.star_hom_apply Ï†).spectral_radius_eq_nnnorm,
      coe_le_coe] using (show spectral_radius â„‚ (Ï† s) â‰¤ spectral_radius â„‚ s,
      from supr_le_supr_of_subset (alg_hom.spectrum_apply_subset Ï† s)) }
end

/-- A star algebra homomorphism of complex Câ‹†-algebras is norm contractive. -/
lemma norm_apply_le (a : A) : â€–(Ï† a : B)â€– â‰¤ â€–aâ€– := nnnorm_apply_le Ï† a

/-- Star algebra homomorphisms between Câ‹†-algebras are continuous linear maps.
See note [lower instance priority] -/
@[priority 100]
noncomputable instance : continuous_linear_map_class F â„‚ A B :=
{ map_continuous := Î» Ï†, add_monoid_hom_class.continuous_of_bound Ï† 1
    (by simpa only [one_mul] using nnnorm_apply_le Ï†),
  .. alg_hom_class.linear_map_class }

end star_alg_hom

end

namespace weak_dual

open continuous_map complex
open_locale complex_star_module

variables {F A : Type*} [normed_ring A] [normed_algebra â„‚ A] [complete_space A]
  [star_ring A] [cstar_ring A] [star_module â„‚ A] [hF : alg_hom_class F â„‚ A â„‚]

include hF

/-- This instance is provided instead of `star_alg_hom_class` to avoid type class inference loops.
See note [lower instance priority] -/
@[priority 100]
noncomputable instance : star_hom_class F A â„‚ :=
{ coe := Î» Ï†, Ï†,
  coe_injective' := fun_like.coe_injective',
  map_star := Î» Ï† a,
  begin
    suffices hsa : âˆ€ s : self_adjoint A, (Ï† s)â‹† = Ï† s,
    { rw â†real_part_add_I_smul_imaginary_part a,
      simp only [map_add, map_smul, star_add, star_smul, hsa, self_adjoint.star_coe_eq] },
    { intros s,
      have := alg_hom.apply_mem_spectrum Ï† (s : A),
      rw self_adjoint.coe_re_map_spectrum s at this,
      rcases this with âŸ¨âŸ¨_, _âŸ©, _, heqâŸ©,
      rw [â†heq, is_R_or_C.star_def, is_R_or_C.conj_of_real] }
  end }

/-- This is not an instance to avoid type class inference loops. See
`weak_dual.complex.star_hom_class`. -/
noncomputable def _root_.alg_hom_class.star_alg_hom_class : star_alg_hom_class F â„‚ A â„‚ :=
{ coe := Î» f, f,
  .. weak_dual.complex.star_hom_class,
  .. hF }

omit hF

namespace character_space

noncomputable instance : star_alg_hom_class (character_space â„‚ A) â„‚ A â„‚ :=
{ coe := Î» f, f,
  .. alg_hom_class.star_alg_hom_class }

end character_space

end weak_dual
