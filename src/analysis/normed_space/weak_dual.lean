/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import topology.algebra.weak_dual_topology
import analysis.normed_space.dual
import analysis.normed_space.operator_norm

/-!
# Weak dual of normed space

Let `E` be a normed space over a field `ğ•œ`. This file is concerned with properties of the weak-*
topology on the dual of `E`. By the dual, we mean either of the type synonyms
`normed_space.dual ğ•œ E` or `weak_dual ğ•œ E`, depending on whether it is viewed as equipped with its
usual operator norm topology or the weak-* topology.

It is shown that the canonical mapping `normed_space.dual ğ•œ E â†’ weak_dual ğ•œ E` is continuous, and
as a consequence the weak-* topology is coarser than the topology obtained from the operator norm
(dual norm).

In this file, we also establish the Banach-Alaoglu theorem about the compactness of closed balls
in the dual of `E` (as well as sets of somewhat more general form) with respect to the weak-*
topology.

## Main definitions

The main definitions concern the canonical mapping `dual ğ•œ E â†’ weak_dual ğ•œ E`.

* `normed_space.dual.to_weak_dual` and `weak_dual.to_normed_dual`: Linear equivalences from
  `dual ğ•œ E` to `weak_dual ğ•œ E` and in the converse direction.
* `normed_space.dual.continuous_linear_map_to_weak_dual`: A continuous linear mapping from
  `dual ğ•œ E` to `weak_dual ğ•œ E` (same as `normed_space.dual.to_weak_dual` but different bundled
  data).

## Main results

The first main result concerns the comparison of the operator norm topology on `dual ğ•œ E` and the
weak-* topology on (its type synonym) `weak_dual ğ•œ E`:
* `dual_norm_topology_le_weak_dual_topology`: The weak-* topology on the dual of a normed space is
  coarser (not necessarily strictly) than the operator norm topology.
* `weak_dual.is_compact_polar` (a version of the Banach-Alaoglu theorem): The polar set of a
  neighborhood of the origin in a normed space `E` over `ğ•œ` is compact in `weak_dual _ E`, if the
  nondiscrete normed field `ğ•œ` is proper as a topological space.
* `weak_dual.is_compact_closed_ball` (the most common special case of the Banach-Alaoglu theorem):
  Closed balls in the dual of a normed space `E` over `â„` or `â„‚` are compact in the weak-star
  topology.

TODOs:
* Add that in finite dimensions, the weak-* topology and the dual norm topology coincide.
* Add that in infinite dimensions, the weak-* topology is strictly coarser than the dual norm
  topology.
* Add metrizability of the dual unit ball (more generally weak-star compact subsets) of
  `weak_dual ğ•œ E` under the assumption of separability of `E`.
* Add the sequential Banach-Alaoglu theorem: the dual unit ball of a separable normed space `E`
  is sequentially compact in the weak-star topology. (Would follow from the metrizability above.)

## Notations

No new notation is introduced.

## Implementation notes

Weak-* topology is defined generally in the file `topology.algebra.weak_dual_topology`.

When `E` is a normed space, the duals `dual ğ•œ E` and `weak_dual ğ•œ E` are type synonyms with
different topology instances.

For the proof of Banach-Alaoglu theorem, the weak dual of `E` is embedded in the space of
functions `E â†’ ğ•œ` with the topology of pointwise convergence. The fact that this is an embedding
is `weak_dual.to_Pi_embedding`.

The polar set `polar ğ•œ s` of a subset `s` of `E` is originally defined as a subset of the dual
`dual ğ•œ E`. We care about properties of these w.r.t. weak-* topology, and for this purpose give
the definition `weak_dual.polar ğ•œ s` for the "same" subset viewed as a subset of `weak_dual ğ•œ E`
(a type synonym of the dual but with a different topology instance).

## References

* https://en.wikipedia.org/wiki/Weak_topology#Weak-*_topology
* https://en.wikipedia.org/wiki/Banach%E2%80%93Alaoglu_theorem

## Tags

weak-star, weak dual

-/

noncomputable theory
open filter
open_locale topological_space

section weak_star_topology_for_duals_of_normed_spaces
/-!
### Weak star topology on duals of normed spaces
In this section, we prove properties about the weak-* topology on duals of normed spaces.
We prove in particular that the canonical mapping `dual ğ•œ E â†’ weak_dual ğ•œ E` is continuous,
i.e., that the weak-* topology is coarser (not necessarily strictly) than the topology given
by the dual-norm (i.e. the operator-norm).
-/

open normed_space

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]

/-- For normed spaces `E`, there is a canonical map `dual ğ•œ E â†’ weak_dual ğ•œ E` (the "identity"
mapping). It is a linear equivalence. -/
def normed_space.dual.to_weak_dual : dual ğ•œ E â‰ƒâ‚—[ğ•œ] weak_dual ğ•œ E :=
linear_equiv.refl ğ•œ (E â†’L[ğ•œ] ğ•œ)

/-- For normed spaces `E`, there is a canonical map `weak_dual ğ•œ E â†’ dual ğ•œ E` (the "identity"
mapping). It is a linear equivalence. Here it is implemented as the inverse of the linear
equivalence `normed_space.dual.to_weak_dual` in the other direction. -/
def weak_dual.to_normed_dual : weak_dual ğ•œ E â‰ƒâ‚—[ğ•œ] dual ğ•œ E :=
normed_space.dual.to_weak_dual.symm

@[simp] lemma weak_dual.coe_to_fun_eq_normed_coe_to_fun (x' : dual ğ•œ E) :
  (x'.to_weak_dual : E â†’ ğ•œ) = x' := rfl

namespace normed_space.dual

@[simp] lemma to_weak_dual_eq_iff (x' y' : dual ğ•œ E) :
  x'.to_weak_dual = y'.to_weak_dual â†” x' = y' :=
to_weak_dual.injective.eq_iff

@[simp] lemma _root_.weak_dual.to_normed_dual_eq_iff (x' y' : weak_dual ğ•œ E) :
  x'.to_normed_dual = y'.to_normed_dual â†” x' = y' :=
weak_dual.to_normed_dual.injective.eq_iff

theorem to_weak_dual_continuous :
  continuous (Î» (x' : dual ğ•œ E), x'.to_weak_dual) :=
begin
  apply weak_dual.continuous_of_continuous_eval,
  intros z,
  exact (inclusion_in_double_dual ğ•œ E z).continuous,
end

/-- For a normed space `E`, according to `to_weak_dual_continuous` the "identity mapping"
`dual ğ•œ E â†’ weak_dual ğ•œ E` is continuous. This definition implements it as a continuous linear
map. -/
def continuous_linear_map_to_weak_dual : dual ğ•œ E â†’L[ğ•œ] weak_dual ğ•œ E :=
{ cont := to_weak_dual_continuous, .. to_weak_dual, }

/-- The weak-star topology is coarser than the dual-norm topology. -/
theorem dual_norm_topology_le_weak_dual_topology :
  (by apply_instance : topological_space (dual ğ•œ E)) â‰¤
    (by apply_instance : topological_space (weak_dual ğ•œ E)) :=
begin
  refine continuous.le_induced _,
  apply continuous_pi_iff.mpr,
  intros z,
  exact (inclusion_in_double_dual ğ•œ E z).continuous,
end

end normed_space.dual

namespace weak_dual

variables (ğ•œ)

/-- The polar set `polar ğ•œ s` of `s : set E` seen as a subset of the dual of `E` with the
weak-star topology is `weak_dual.polar ğ•œ s`. -/
def polar (s : set E) : set (weak_dual ğ•œ E) := to_normed_dual â»Â¹' (polar ğ•œ s)

end weak_dual

end weak_star_topology_for_duals_of_normed_spaces

section polar_sets_in_weak_dual

open metric set normed_space

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]

/-- The polar `polar ğ•œ s` of a set `s : E` is a closed subset when the weak star topology
is used, i.e., when `polar ğ•œ s` is interpreted as a subset of `weak_dual ğ•œ E`. -/
lemma weak_dual.is_closed_polar (s : set E) : is_closed (weak_dual.polar ğ•œ s) :=
begin
  rw [weak_dual.polar, polar_eq_Inter, preimage_bInter],
  apply is_closed_bInter,
  intros z hz,
  rw set.preimage_set_of_eq,
  have eq : {x' : weak_dual ğ•œ E | âˆ¥weak_dual.to_normed_dual x' zâˆ¥ â‰¤ 1}
    = (Î» (x' : weak_dual ğ•œ E), âˆ¥x' zâˆ¥)â»Â¹' (Iic 1) := by refl,
  rw eq,
  refine is_closed.preimage _ (is_closed_Iic),
  apply continuous.comp continuous_norm (weak_dual.eval_continuous _ _ z),
end

end polar_sets_in_weak_dual

section embedding_to_Pi

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]

open metric set normed_space

/-- The (weak) dual `weak_dual ğ•œ E` of a normed space `E` consists of bounded linear
functionals `E â†’ ğ•œ`. Such functionals can be interpreted as elements of the Cartesian
product `E â†’ ğ•œ` via the function `weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)`. -/
def _root_.weak_dual.to_Pi (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  (E : Type*) [topological_space E] [add_comm_group E] [module ğ•œ E] (x' : weak_dual ğ•œ E) :=
x'.to_fun

/-- The function `weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` is an embedding. -/
lemma weak_dual.to_Pi_embedding (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  (E : Type*) [topological_space E] [add_comm_group E] [module ğ•œ E] :
  embedding (weak_dual.to_Pi ğ•œ E) :=
{ induced := eq_of_nhds_eq_nhds (congr_fun rfl),
  inj := by { intros Ï†â‚ Ï†â‚‚ h, ext z, exact congr_fun h z, }, }

namespace weak_dual.to_Pi_embedding

/-- Elements of the closure of the range of the embedding
`weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` are linear. Here it is stated as the elements
respecting linear combinations. -/
lemma linear_of_mem_closure_range'
  (f : E â†’ ğ•œ) (hf : f âˆˆ closure (range (weak_dual.to_Pi ğ•œ E)))
  (câ‚ câ‚‚ : ğ•œ) (zâ‚ zâ‚‚ : E) : f (câ‚ â€¢ zâ‚ + câ‚‚ â€¢ zâ‚‚) = câ‚ â€¢ f(zâ‚) + câ‚‚ â€¢ f(zâ‚‚) :=
begin
  set Ï† : (E â†’ ğ•œ) â†’ ğ•œ := (Î» g, g (câ‚ â€¢ zâ‚ + câ‚‚ â€¢ zâ‚‚) - câ‚ â€¢ g(zâ‚) - câ‚‚ â€¢ g(zâ‚‚)) with hÏ†,
  have Ï†_cont : continuous Ï†,
  { apply continuous.sub,
    { apply continuous.sub,
      { exact continuous_apply (câ‚ â€¢ zâ‚ + câ‚‚ â€¢ zâ‚‚), },
      exact continuous.smul continuous_const (continuous_apply _), },
    exact continuous.smul continuous_const (continuous_apply _), },
  have sin_closed : is_closed ({0} : set ğ•œ) := t1_space.t1 0,
  have preim_cl : is_closed (Ï†â»Â¹' ({0} : set ğ•œ)) := is_closed.preimage Ï†_cont sin_closed,
  suffices : range (weak_dual.to_Pi ğ•œ E) âŠ† Ï†â»Â¹' ({0} : set ğ•œ),
  { have key := (is_closed.closure_subset_iff preim_cl).mpr this hf,
    exact sub_eq_iff_eq_add'.mp (sub_eq_zero.mp key), },
  intros g hg,
  cases hg with gâ‚€ hgâ‚€,
  simp only [algebra.id.smul_eq_mul, mem_singleton_iff, norm_eq_zero, mem_preimage],
  rw [hÏ†, â† hgâ‚€, weak_dual.to_Pi],
  dsimp,
  simp only [algebra.id.smul_eq_mul, continuous_linear_map.map_add, add_sub_cancel,
             sub_self, continuous_linear_map.map_smul, add_sub_cancel', sub_self],
end

/-- Elements of the closure of the range of the embedding
`weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` can be viewed as linear maps `E â†’ ğ•œ`. -/
def linear_of_mem_closure_range
  (f : (E â†’ ğ•œ)) (hf : f âˆˆ closure (range (weak_dual.to_Pi ğ•œ E))) :
  E â†’â‚—[ğ•œ] ğ•œ :=
{ to_fun := f,
  map_add' := begin
    intros zâ‚ zâ‚‚,
    have key := linear_of_mem_closure_range' f hf (1 : ğ•œ) (1 : ğ•œ) zâ‚ zâ‚‚,
    rwa [one_smul, one_smul, one_smul ğ•œ _, one_smul ğ•œ _] at key,
  end,
  map_smul' := begin
    intros c z,
    have key := linear_of_mem_closure_range' f hf c (0 : ğ•œ) z (0 : E),
    rwa [zero_smul, zero_smul, add_zero, add_zero] at key,
  end, }

lemma linear_of_mem_closure_range_apply
  (f : E â†’ ğ•œ) (hf : f âˆˆ closure (range (weak_dual.to_Pi ğ•œ E))) (z : E) :
  linear_of_mem_closure_range f hf z = f z := rfl

/-- Elements of the closure of the image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` of
a subset defined by a non-strict bound on the norm still satisfy the same bound. -/
lemma norm_eval_le_of_mem_closure_norm_eval_le
  (z : E) (c : â„) (f : E â†’ ğ•œ)
  (hf : f âˆˆ closure ((weak_dual.to_Pi ğ•œ E) '' {x' : weak_dual ğ•œ E | âˆ¥ x' z âˆ¥ â‰¤ c})) :
  âˆ¥ f z âˆ¥ â‰¤ c :=
begin
  suffices : âˆ€ (Îµ : â„), 0 < Îµ â†’ âˆ¥ f (z) âˆ¥ â‰¤ c + Îµ,
  { exact le_of_forall_pos_le_add this, },
  intros Îµ Îµ_pos,
  have nhd : {g : E â†’ ğ•œ | âˆ¥f z - g zâˆ¥ < Îµ} âˆˆ ğ“ f,
  { have nhd' := continuous_at_apply z f (ball_mem_nhds (f z) Îµ_pos),
    rwa [mem_map,
         (_ : (Î» (g : E â†’ ğ•œ), g z) â»Â¹' (ball (f z) Îµ) = {g : E â†’ ğ•œ | âˆ¥f z - g zâˆ¥ < Îµ})] at nhd',
    ext g,
    simp only [ball, dist_comm, mem_set_of_eq, preimage_set_of_eq],
    exact mem_ball_iff_norm, },
  cases mem_closure_iff_nhds.mp hf _ nhd with g hg,
  simp only [mem_image, mem_inter_eq, mem_set_of_eq] at hg,
  rcases hg with âŸ¨tri, âŸ¨y', âŸ¨at_z_le, eq_gâŸ©âŸ©âŸ©,
  have eq : weak_dual.to_Pi ğ•œ E y' z = y' z := rfl,
  rw [â† eq_g, eq] at tri,
  have key := norm_add_le_of_le tri.le at_z_le,
  rwa [sub_add_cancel, add_comm] at key,
end

/-- Elements of the closure of the image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` of
a polar `polar s` of a neighborhood `s` of the origin are continuous (linear) functions. -/
lemma continuous_of_mem_closure_polar
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) (Ï† : E â†’ ğ•œ)
  (hÏ† : Ï† âˆˆ closure ((weak_dual.to_Pi ğ•œ E) '' (weak_dual.polar ğ•œ s))) :
  continuous Ï† :=
begin
  cases polar_bounded_of_nhds_zero ğ•œ s_nhd with c hc,
  have c_nn : 0 â‰¤ c := le_trans (norm_nonneg _) (hc 0 (zero_mem_polar ğ•œ s)),
  have hÏ†' : Ï† âˆˆ closure (range (weak_dual.to_Pi ğ•œ E)),
  from closure_mono (image_subset_range _ _) hÏ†,
  set flin := linear_of_mem_closure_range Ï† hÏ†' with hflin,
  suffices : continuous flin,
  { assumption, },
  apply linear_map.continuous_of_bound flin c,
  intros z,
  set Î¸ := Î» (Ïˆ : E â†’ ğ•œ), âˆ¥ Ïˆ z âˆ¥ with hÎ¸,
  have Î¸_cont : continuous Î¸,
  { apply continuous.comp continuous_norm,
    exact continuous_apply z, },
  have sin_closed : is_closed (Icc (-c * âˆ¥zâˆ¥) (c * âˆ¥zâˆ¥) : set â„) := is_closed_Icc,
  have preim_cl := is_closed.preimage Î¸_cont sin_closed,
  suffices : (weak_dual.to_Pi ğ•œ E) '' (weak_dual.polar ğ•œ s) âŠ† Î¸â»Â¹' (Icc (-c * âˆ¥zâˆ¥) (c * âˆ¥zâˆ¥)),
  { exact ((is_closed.closure_subset_iff preim_cl).mpr this hÏ†).right, },
  intros Ïˆ hÏˆ,
  rcases hÏˆ with âŸ¨x', âŸ¨polar_x', Ïˆ_x'âŸ©âŸ©,
  rw â† Ïˆ_x',
  simp only [neg_mul_eq_neg_mul_symm, mem_preimage, mem_Icc, hÎ¸],
  split,
  { apply le_trans _ (norm_nonneg _),
    rw right.neg_nonpos_iff,
    exact mul_nonneg c_nn (norm_nonneg _), },
  apply le_trans (continuous_linear_map.le_op_norm x' z) _,
  apply mul_le_mul (hc x' polar_x') rfl.ge (norm_nonneg z) c_nn,
end

/-- The image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` of a polar `polar ğ•œ s` of a
neighborhood `s` of the origin is a closed set. -/
lemma is_closed_image_polar
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  is_closed ((weak_dual.to_Pi ğ•œ E) '' (weak_dual.polar ğ•œ s)) :=
begin
  apply is_closed_iff_cluster_pt.mpr,
  intros f hf,
  simp only [mem_image, mem_set_of_eq],
  have f_in_closure : f âˆˆ closure ((weak_dual.to_Pi ğ•œ E) '' (weak_dual.polar ğ•œ s)),
  from mem_closure_iff_cluster_pt.mpr hf,
  have f_in_closureâ‚€ : f âˆˆ closure (range (weak_dual.to_Pi ğ•œ E)),
  { apply closure_mono (image_subset_range _ _),
    exact mem_closure_iff_cluster_pt.mpr hf, },
  set f_lin := linear_of_mem_closure_range f f_in_closureâ‚€ with h_f_lin,
  have f_cont := continuous_of_mem_closure_polar s_nhd f f_in_closure,
  set Ï† : weak_dual ğ•œ E :=
    { to_fun := f,
      map_add' := begin
        intros zâ‚ zâ‚‚,
        have key := f_lin.map_add zâ‚ zâ‚‚,
        rw h_f_lin at key,
        repeat {rwa linear_of_mem_closure_range_apply
          f f_in_closureâ‚€ _ at key, },
      end,
      map_smul' := begin
        intros c z,
        have key := f_lin.map_smul c z,
        rw h_f_lin at key,
        repeat {rwa linear_of_mem_closure_range_apply
          f f_in_closureâ‚€ _ at key, },
      end,
      cont := f_cont, } with hÏ†,
  refine âŸ¨Ï†, âŸ¨_, rflâŸ©âŸ©,
  rw [weak_dual.polar, polar],
  intros z hz,
  apply norm_eval_le_of_mem_closure_norm_eval_le z 1 f,
  have ss : weak_dual.polar ğ•œ s âŠ† {x' : weak_dual ğ•œ E | âˆ¥x' zâˆ¥ â‰¤ 1},
  { intros x' hx',
    simp only [weak_dual.polar, polar, mem_set_of_eq, preimage_set_of_eq] at hx',
    exact hx' z hz, },
  apply closure_mono (image_subset _ ss),
  exact mem_closure_iff_cluster_pt.mpr hf,
end

/-- The image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ (E â†’ ğ•œ)` of the polar `polar s` of
a neighborhood `s` of the origin is compact if the field `ğ•œ` is a proper topological space. -/
lemma image_polar_compact [proper_space ğ•œ] {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  is_compact ((weak_dual.to_Pi ğ•œ E) '' (weak_dual.polar ğ•œ s)) :=
begin
  cases polar_bounded_of_nhds_zero ğ•œ s_nhd with c hc,
  have ss : (weak_dual.to_Pi ğ•œ E) '' (weak_dual.polar ğ•œ s) âŠ†
    (set.pi (univ : set E) (Î» z, (closed_ball (0 : ğ•œ) (c * âˆ¥ z âˆ¥)))),
  { intros f hf,
    simp only [mem_image, exists_exists_and_eq_and] at hf,
    rcases hf with âŸ¨x', hx', f_eqâŸ©,
    simp only [mem_closed_ball, dist_zero_right, mem_univ_pi],
    intros z,
    have bd : âˆ¥ x' z âˆ¥ â‰¤ c * âˆ¥ z âˆ¥,
    { apply (continuous_linear_map.le_op_norm x' z).trans
        (mul_le_mul (hc x' hx') (le_refl âˆ¥zâˆ¥) (norm_nonneg z) _),
      have c_nn := hc 0 (zero_mem_polar ğ•œ s),
      rwa norm_zero at c_nn, },
    have eq : x' z = f z := congr_fun f_eq z,
    rwa eq at bd, },
  apply compact_of_is_closed_subset _ _ ss,
  { apply is_compact_univ_pi,
    exact Î» z, proper_space.is_compact_closed_ball 0 _, },
  exact is_closed_image_polar s_nhd,
end

end weak_dual.to_Pi_embedding

/-- The Banach-Alaoglu theorem: the polar set of a neighborhood `s` of the origin in a
normed space `E` is a compact subset of `weak_dual ğ•œ E`. -/
theorem weak_dual.is_compact_polar [proper_space ğ•œ] {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  is_compact (weak_dual.polar ğ•œ s) :=
begin
  apply (weak_dual.to_Pi_embedding ğ•œ E).is_compact_iff_is_compact_image.mpr,
  exact weak_dual.to_Pi_embedding.image_polar_compact s_nhd,
end

/-- The Banach-Alaoglu theorem: closed balls of the dual of a normed space `E` over `â„` or `â„‚`
are compact in the weak-star topology. -/
theorem weak_dual.is_compact_closed_ball
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E] (r : â„) (hr : 0 < r) :
  is_compact (id (closed_ball 0 r : set (normed_space.dual ğ•œ E)) : set (weak_dual ğ•œ E)) :=
begin
  have as_polar := @polar_closed_ball ğ•œ _ E _ _ râ»Â¹ (inv_pos.mpr hr),
  simp only [one_div, inv_invâ‚€] at as_polar,
  rw â†as_polar,
  exact weak_dual.is_compact_polar (closed_ball_mem_nhds (0 : E) (inv_pos.mpr hr)),
end

end embedding_to_Pi
