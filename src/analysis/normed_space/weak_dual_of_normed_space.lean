/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤ and Heather Macbeth
-/
import tactic
import topology.algebra.weak_dual_topology
import analysis.normed_space.dual
import analysis.normed_space.operator_norm

noncomputable theory
open filter
open_locale topological_space

section weak_star_topology_for_duals_of_normed_spaces

/-!
### Weak star topology on duals of normed spaces
In this section, we prove properties about the weak-* topology on duals of normed spaces.
We prove in particular that the canonical mapping `dual ğ•œ E â†’ weak_dual ğ•œ E` is continuous,
i.e., that the weak-* topology is coarser (not necessarily strictly) than the topology given
by the dual-norm (i.e. the operator-norm).
-/

open normed_space

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]

/-- For normed spaces `E`, there is a canonical map `dual ğ•œ E â‰ƒâ‚—[ğ•œ] weak_dual ğ•œ E`. -/
def normed_space.dual.to_weak_dual : dual ğ•œ E â‰ƒâ‚—[ğ•œ] weak_dual ğ•œ E :=
linear_equiv.refl ğ•œ (E â†’L[ğ•œ] ğ•œ)

/-- For normed spaces `E`, there is a canonical map `weak_dual ğ•œ E â†’ dual ğ•œ E`. -/
def weak_dual.to_original_dual : weak_dual ğ•œ E â†’ dual ğ•œ E := id

@[simp] lemma weak_dual.coe_to_fun_eq_original_coe_to_fun (x' : dual ğ•œ E) :
  (x'.to_weak_dual : E â†’ ğ•œ) = x' := rfl

lemma to_weak_dual_injective :
  function.injective (Î» (x' : dual ğ•œ E), x'.to_weak_dual) := Î» _ _ , id

lemma to_original_dual_injective :
  function.injective (Î» (x' : weak_dual ğ•œ E), x'.to_original_dual) := Î» _ _ , id

-- Q: Are the following simp-lemmas useful?

@[simp] lemma to_weak_dual_to_original_dual (x' : weak_dual ğ•œ E) :
  (x'.to_original_dual).to_weak_dual = x' := rfl

@[simp] lemma to_original_dual_to_weak_dual (x' : dual ğ•œ E) :
  (x'.to_weak_dual).to_original_dual = x' := rfl

@[simp] lemma to_weak_dual_inj_iff (x' y' : dual ğ•œ E) :
  x'.to_weak_dual = y'.to_weak_dual â†” x' = y' := iff.rfl

@[simp] lemma to_original_dual_inj_iff (x' y' : weak_dual ğ•œ E) :
  x'.to_original_dual = y'.to_original_dual â†” x' = y' := iff.rfl

/-- The linear equivalence between `dual ğ•œ E` and `weak_dual ğ•œ E` for a normed space `E`. -/
def linequiv_to_weak_dual : dual ğ•œ E â‰ƒâ‚—[ğ•œ] weak_dual ğ•œ E :=
{ to_fun := (Î» (x' : dual ğ•œ E), x'.to_weak_dual),
  map_add' := by { intros x' y', refl, },
  map_smul' := by { intros c x', refl, },
  inv_fun := (Î» (x' : weak_dual ğ•œ E), x'.to_original_dual),
  left_inv := to_original_dual_to_weak_dual,
  right_inv := to_weak_dual_to_original_dual, }

@[simp]
lemma linequiv_to_weak_dual_apply (x' : dual ğ•œ E) :
  linequiv_to_weak_dual x' = x'.to_weak_dual := rfl

@[simp]
lemma equiv_to_weak_dual_symm_apply (x' : weak_dual ğ•œ E) :
  linequiv_to_weak_dual.symm x' = x'.to_original_dual := rfl

-- TODO: The only reason to separate this from `evaluate_dual_at` is to get access to the proofs
-- of `map_add'` and `map_smul'`. Surely there is a syntax to avoid this unnecessary intermediate
-- step... right?
def normed_space.evaluate_dual_at' (z : E) : (dual ğ•œ E) â†’â‚—[ğ•œ] ğ•œ :=
{ to_fun := (Î» (x' : dual ğ•œ E), x' z),
  map_add' := by simp only [forall_const, eq_self_iff_true, continuous_linear_map.add_apply],
  map_smul' := by simp only [forall_const, eq_self_iff_true, pi.smul_apply,
                             continuous_linear_map.coe_smul'], }

/-
TODO: Is there a way to make the following dot notation work?
(And the same for `evaluate_dual_at`?)

variables (w : E)
#check w
#check normed_space.evaluate_dual_at' w
#check w.evaluate_dual_at' -- fails
-/

def normed_space.evaluate_dual_at (z : E) : (dual ğ•œ E) â†’L[ğ•œ] ğ•œ :=
{ to_fun := (Î» (x' : dual ğ•œ E), x' z),
  map_add' := (normed_space.evaluate_dual_at' z).map_add,
  map_smul' := (normed_space.evaluate_dual_at' z).map_smul,
  cont := begin
    apply @continuous_of_linear_of_bound ğ•œ (dual ğ•œ E) ğ•œ _ infer_instance _ _ _
      (Î» (x' : dual ğ•œ E), x' z) (normed_space.evaluate_dual_at' z).map_add
      (normed_space.evaluate_dual_at' z).map_smul (âˆ¥ z âˆ¥),
    intros x',
    have key := continuous_linear_map.le_op_norm x' z,
    rwa mul_comm at key,
  end, }

theorem to_weak_dual_continuous :
  continuous (Î» (x' : dual ğ•œ E), x'.to_weak_dual) :=
begin
  apply continuous_induced_rng,
  apply continuous_pi_iff.mpr,
  intros z,
  exact (inclusion_in_double_dual ğ•œ E z).continuous,
end

def normed_space.dual.continuous_linear_map_to_weak_dual : dual ğ•œ E â†’L[ğ•œ] weak_dual ğ•œ E :=
{ to_fun := (Î» (x' : dual ğ•œ E), x'.to_weak_dual),
  map_add' := (@linequiv_to_weak_dual ğ•œ _ E _ _).map_add',
  map_smul' := (@linequiv_to_weak_dual ğ•œ _ E _ _).map_smul',
  cont := to_weak_dual_continuous, }

-- This is a relatively straightforward statement of the fact that the weak-star topology is
-- coarser than the dual-norm topology, without abusing definitional equality.
/-- The weak-star topology is coarser than the dual-norm topology: all weak-star open sets are
    norm-topology open. -/
lemma open_set_of_weak_dual_open_set (s : set (dual ğ•œ E))
  (s_weak_dual_open : is_open (linequiv_to_weak_dual '' s)) : is_open s :=
begin
  have eq : (linequiv_to_weak_dual)â»Â¹' (linequiv_to_weak_dual '' s) = s,
  { ext x',
    simp only [set.mem_preimage, linequiv_to_weak_dual_apply, set.mem_image, to_weak_dual_inj_iff,
               exists_eq_right], },
  rw â†eq,
  apply continuous_def.mp to_weak_dual_continuous _ s_weak_dual_open,
end

-- TODO: The proof below may be abusing definitional equality... And it looks like it needs golf.
private lemma linequiv_to_weak_dual_image (s : set (dual ğ•œ E)) :
  (linequiv_to_weak_dual '' s) = s :=
begin
  ext x',
  split,
  { intros hx',
    rcases hx' with âŸ¨y', hy', h_eqâŸ©,
    rwa â†h_eq, },
  { intros hx',
    use x',
    exact âŸ¨ hx', by refl âŸ©, },
end

-- TODO: The proof and even the statement below may be abusing definitional equality...
-- But I don't think this can be stated using `â‰¤` on topologies without such abuse.
/-- The weak-star topology is coarser than the dual-norm topology. -/
theorem dual_norm_topology_le_weak_dual_topology :
  (by apply_instance : topological_space (dual ğ•œ E)) â‰¤ weak_dual.topology ğ•œ E :=
begin
  intros U hU,
  apply open_set_of_weak_dual_open_set U,
  rwa linequiv_to_weak_dual_image,
end

end weak_star_topology_for_duals_of_normed_spaces
