import topology.algebra.ring.basic
import algebra.big_operators.basic
import order.filter.at_top_bot
import analysis.specific_limits.normed
import topology.metric_space.cau_seq_filter
import tactic

open_locale big_operators topology

universe u

/--
`partial_sum f n` is the sum `f 0 + f 1 + f 2 + ... + f (n - 1)`. Note that this does not include
the term `f n`.
-/
def partial_sum {R : Type u} [add_comm_monoid R] (f : â„• â†’ R) (n : â„•) :=
âˆ‘ i in finset.range n, f i

lemma partial_sum_zero (R : Type u) [add_comm_monoid R] (n : â„•) : partial_sum (Î» _ : â„•, 0) n = 0 :=
finset.sum_eq_zero (Î» _ _, rfl)

lemma partial_sum_next {R : Type u} [add_comm_monoid R] (f : â„• â†’ R) (n : â„•) :
  partial_sum f (n + 1) = f n + partial_sum f n :=
begin
  unfold partial_sum,
  rw finset.range_succ,
  apply finset.sum_insert,
  exact finset.not_mem_range_self
end

def series_sums_to {R : Type u} [add_comm_monoid R] [topological_space R] (f : â„• â†’ R) (a : R) :=
filter.tendsto (partial_sum f) filter.at_top (ğ“ a)

def series_converges {R : Type u} [add_comm_monoid R] [topological_space R] (f : â„• â†’ R) :=
âˆƒ a : R, series_sums_to f a

def series_converges_absolutely {R : Type u} [add_comm_monoid R] [topological_space R] [has_abs R] (f : â„• â†’ R) :=
series_converges (Î» x, |f x|)

lemma tail_limit {R : Type u} [topological_space R] (f : â„• â†’ R) (T : R) (h : filter.tendsto f filter.at_top (ğ“ T)) :
  filter.tendsto (Î» k, f (k + 1)) filter.at_top (ğ“ T) :=
begin
  rw filter.tendsto_def at h âŠ¢,
  intros s hs,
  specialize h s hs,
  rw filter.mem_at_top_sets at h âŠ¢,
  cases h with a h,
  use a,
  intros b hb,
  exact h (b + 1) (nat.le_succ_of_le hb)
end

theorem terms_tendsto_zero {R : Type u} [add_comm_group R] [topological_space R] [topological_add_group R]
  (a : â„• â†’ R) (h : series_converges a) : filter.tendsto a filter.at_top (ğ“ 0) :=
begin
  -- Since R is a topological group, it has a uniform space.
  -- Since R is abelian, it satisfies `uniform_add_group`
  letI : uniform_space R := topological_add_group.to_uniform_space R,
  haveI : uniform_add_group R := topological_add_comm_group_is_uniform,

  -- It suffices to show that for all neighborhoods `X` of 0, There exists an `N` such that for all
  -- `n â‰¥ N`, `a n âˆˆ X`.
  rw filter.tendsto_def,
  intros X hX,
  rw filter.mem_at_top_sets,

  -- Because `X âˆˆ ğ“ 0`, there exists an entourage `V` such that `V[0] âŠ† X`
  rcases uniform_space.mem_nhds_iff.mp hX with âŸ¨V, hVâ‚, hVâ‚‚âŸ©,

  let m := Î» (x : R Ã— R), x.snd - x.fst,

  -- By the definition of an entourage in a topological group, there exists a neighbourhood `t` of 0
  -- such that `m â»Â¹' t âŠ† V`
  rw uniformity_eq_comap_nhds_zero R at hVâ‚,
  rcases filter.mem_comap.mp hVâ‚ with âŸ¨t, htâ‚, htâ‚‚âŸ©,

  -- Note that `m â»Â¹' t` is itself an entourage.
  have hm : m â»Â¹' t âˆˆ uniformity R := begin
    rw uniformity_eq_comap_nhds_zero R,
    rw filter.mem_comap,
    use t,
    use htâ‚,
  end,

  -- Let `U` by a "half-size" entourage of `m â»Â¹' t` (so that `comp_rel U U âŠ† m â»Â¹' t`)
  -- Let `W` by the largest symmetric relation which is a subset of `U`. `W` is an entourage.
  obtain âŸ¨U, hUâ‚, hUâ‚‚âŸ© := comp_mem_uniformity_sets hm,
  let W := symmetrize_rel U,
  have hWâ‚ : W âˆˆ uniformity R := symmetrize_mem_uniformity hUâ‚,
  have hWâ‚‚ : symmetric_rel W := symmetric_symmetrize_rel U,
  have hWâ‚ƒ : W âŠ† U := symmetrize_rel_subset_self U,

  -- By hypothesis, the partial sums of `a` tend to some `T`. This means that given any
  -- neighbourhood of T, there exists an `N : â„•` such that for all `n â‰¥ N`, the nth partial sum lies
  -- within that neighbourhood. Because `W[T]` is a neighbourhood of `T`, we use this to find our
  -- desired `N`.
  cases h with T h,
  unfold series_sums_to at h,
  rw filter.tendsto_def at h,
  specialize h (uniform_space.ball T W) (uniform_space.ball_mem_nhds T hWâ‚),
  obtain âŸ¨N, hNâŸ© := filter.mem_at_top_sets.mp h,

  -- Using the `N` we just found, we need to show that for all `n â‰¥ N`, `a n âˆˆ X`. Since `V[0] âŠ† X`,
  -- it suffices to show that `a n âˆˆ V[0]`, or that `(0, a n) âˆˆ V`.
  use N,
  intros n hn,
  rw set.mem_preimage,
  apply hVâ‚‚,
  unfold uniform_space.ball,
  rw set.mem_preimage,

  -- Since `m â»Â¹ t âŠ† V`, it suffices to show that `(0, a n) âˆˆ m â»Â¹ t`, which is the same as showing
  -- that `m (0, a n) âˆˆ t`. This is the same as showing that `a n - 0 âˆˆ t`.
  apply htâ‚‚,
  rw set.mem_preimage,
  change a n - 0 âˆˆ t,

  -- Note that `a n - 0` is the same as the difference between the nth and (n+1)th partial sums
  -- (because of the way that partial sums are defined here).
  rw sub_zero,
  rw (show a n = partial_sum a (n + 1) - partial_sum a n, by simp [partial_sum_next a n]),

  -- Therefore, we need to show that `partial_sum a (n + 1) - partial_sum a n âˆˆ t`, which is the
  -- same as showing that `(partial_sum a n, partial_sum a (n + 1)) âˆˆ m â»Â¹ t`.
  change m (partial_sum a n, partial_sum a (n + 1)) âˆˆ t,
  rw â†set.mem_preimage,

  -- Using what we deduced earlier from the hypothesis, `(T, partial_sum a n) âˆˆ W` and
  -- `(T, partial_sum a (n + 1)) âˆˆ W` (since `n â‰¥ N` and `n + 1 â‰¥ N`)
  have hnâ‚ := set.mem_preimage.mp (hN n hn),
  have hnâ‚‚ := set.mem_preimage.mp (hN (n + 1) (by linarith)),
  unfold uniform_space.ball at hnâ‚ hnâ‚‚,
  rw set.mem_preimage at hnâ‚ hnâ‚‚,

  -- `W` is a symmetric relation so `(partial_sum a n, T) âˆˆ W`.
  rw symmetric_rel.mk_mem_comm hWâ‚‚ at hnâ‚,

  -- Since `W âŠ† U`, `(partial_sum a n, T) âˆˆ U` and `(T, partial_sum a (n + 1)) âˆˆ U`,
  replace hnâ‚ := hWâ‚ƒ hnâ‚,
  replace hnâ‚‚ := hWâ‚ƒ hnâ‚‚,

  -- Because `comp_rel U U âŠ† m â»Â¹' t`, `(partial_sum a n, partial_sum a (n + 1)) âˆˆ m â»Â¹ t`.
  show (partial_sum a n, partial_sum a (n + 1)) âˆˆ m â»Â¹' t, from hUâ‚‚ (mem_comp_rel.mpr âŸ¨T, âŸ¨hnâ‚, hnâ‚‚âŸ©âŸ©)
end

lemma partial_sums_le (a b : â„• â†’ â„) (h : âˆ€ n, a n â‰¤ b n) : âˆ€ n, partial_sum a n â‰¤ partial_sum b n :=
begin
  intro n,
  induction n with n hi,
  { unfold partial_sum,
    simp },
  calc partial_sum a (n + 1) = a n + partial_sum a n : partial_sum_next a n
    ... â‰¤ b n + partial_sum b n : add_le_add (h n) (hi)
    ... = partial_sum b (n + 1) : (partial_sum_next b n).symm
end

lemma cau_seq_of_le (a b : â„• â†’ â„) (h : âˆ€ n, 0 < a n âˆ§ a n < b n) (hb : series_converges b) : is_cau_seq abs a :=
begin
  cases hb with T hT,
  intros Îµ hÎµ,
  sorry
end

theorem summable_of_series_absolute_convergence_real {f : â„• â†’ â„}
  (h : series_converges_absolutely f) : summable f :=
summable_of_absolute_convergence_real h
