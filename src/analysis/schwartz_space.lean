/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/

import analysis.calculus.cont_diff
import analysis.calculus.iterated_deriv
import analysis.locally_convex.with_seminorms
import topology.algebra.uniform_filter_basis
import topology.continuous_function.bounded
import tactic.positivity
import analysis.special_functions.pow.real

/-!
# Schwartz space

This file defines the Schwartz space. Usually, the Schwartz space is defined as the set of smooth
functions $f : â„^n â†’ â„‚$ such that there exists $C_{Î±Î²} > 0$ with $$|x^Î± âˆ‚^Î² f(x)| < C_{Î±Î²}$$ for
all $x âˆˆ â„^n$ and for all multiindices $Î±, Î²$.
In mathlib, we use a slightly different approach and define define the Schwartz space as all
smooth functions `f : E â†’ F`, where `E` and `F` are real normed vector spaces such that for all
natural numbers `k` and `n` we have uniform bounds `â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€– < C`.
This approach completely avoids using partial derivatives as well as polynomials.
We construct the topology on the Schwartz space by a family of seminorms, which are the best
constants in the above estimates. The abstract theory of topological vector spaces developed in
`seminorm_family.module_filter_basis` and `with_seminorms.to_locally_convex_space` turns the
Schwartz space into a locally convex topological vector space.

## Main definitions

* `schwartz_map`: The Schwartz space is the space of smooth functions such that all derivatives
decay faster than any power of `â€–xâ€–`.
* `schwartz_map.seminorm`: The family of seminorms as described above
* `schwartz_map.fderiv_clm`: The differential as a continuous linear map
`ð“¢(E, F) â†’L[ð•œ] ð“¢(E, E â†’L[â„] F)`
* `schwartz_map.deriv_clm`: The one-dimensional derivative as a continuous linear map
`ð“¢(â„, F) â†’L[ð•œ] ð“¢(â„, F)`

## Main statements

* `schwartz_map.uniform_add_group` and `schwartz_map.locally_convex`: The Schwartz space is a
locally convex topological vector space.
* `schwartz_map.one_add_le_sup_seminorm_apply`: For a Schwartz function `f` there is a uniform bound
on `(1 + â€–xâ€–) ^ k * â€–iterated_fderiv â„ n f xâ€–`.

## Implementation details

The implementation of the seminorms is taken almost literally from `continuous_linear_map.op_norm`.

## Notation

* `ð“¢(E, F)`: The Schwartz space `schwartz_map E F` localized in `schwartz_space`

## Tags

Schwartz space, tempered distributions
-/

noncomputable theory

open_locale big_operators nat

variables {ð•œ ð•œ' D E F G : Type*}

variables [normed_add_comm_group E] [normed_space â„ E]
variables [normed_add_comm_group F] [normed_space â„ F]

variables (E F)

/-- A function is a Schwartz function if it is smooth and all derivatives decay faster than
  any power of `â€–xâ€–`. -/
structure schwartz_map :=
  (to_fun : E â†’ F)
  (smooth' : cont_diff â„ âŠ¤ to_fun)
  (decay' : âˆ€ (k n : â„•), âˆƒ (C : â„), âˆ€ x, â€–xâ€–^k * â€–iterated_fderiv â„ n to_fun xâ€– â‰¤ C)

localized "notation `ð“¢(` E `, ` F `)` := schwartz_map E F" in schwartz_space

variables {E F}

namespace schwartz_map

instance : has_coe ð“¢(E, F) (E â†’ F) := âŸ¨to_funâŸ©

instance fun_like : fun_like ð“¢(E, F) E (Î» _, F) :=
{ coe := Î» f, f.to_fun,
  coe_injective' := Î» f g h, by cases f; cases g; congr' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`. -/
instance : has_coe_to_fun ð“¢(E, F) (Î» _, E â†’ F) := âŸ¨Î» p, p.to_funâŸ©

/-- All derivatives of a Schwartz function are rapidly decaying. -/
lemma decay (f : ð“¢(E, F)) (k n : â„•) : âˆƒ (C : â„) (hC : 0 < C),
  âˆ€ x, â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€– â‰¤ C :=
begin
  rcases f.decay' k n with âŸ¨C, hCâŸ©,
  exact âŸ¨max C 1, by positivity, Î» x, (hC x).trans (le_max_left _ _)âŸ©,
end

/-- Every Schwartz function is smooth. -/
lemma smooth (f : ð“¢(E, F)) (n : â„•âˆž) : cont_diff â„ n f := f.smooth'.of_le le_top

/-- Every Schwartz function is continuous. -/
@[continuity, protected] lemma continuous (f : ð“¢(E, F)) : continuous f := (f.smooth 0).continuous

/-- Every Schwartz function is differentiable. -/
@[protected] lemma differentiable (f : ð“¢(E, F)) : differentiable â„ f :=
(f.smooth 1).differentiable rfl.le

/-- Every Schwartz function is differentiable at any point. -/
@[protected] lemma differentiable_at (f : ð“¢(E, F)) {x : E} : differentiable_at â„ f x :=
f.differentiable.differentiable_at

@[ext] lemma ext {f g : ð“¢(E, F)} (h : âˆ€ x, (f : E â†’ F) x = g x) : f = g := fun_like.ext f g h

section is_O

variables (f : ð“¢(E, F))

/-- Auxiliary lemma, used in proving the more general result `is_O_cocompact_zpow`. -/
lemma is_O_cocompact_zpow_neg_nat (k : â„•) :
  asymptotics.is_O (filter.cocompact E) f (Î» x, â€–xâ€– ^ (-k : â„¤)) :=
begin
  obtain âŸ¨d, hd, hd'âŸ© := f.decay k 0,
  simp_rw norm_iterated_fderiv_zero at hd',
  simp_rw [asymptotics.is_O, asymptotics.is_O_with],
  refine âŸ¨d, filter.eventually.filter_mono filter.cocompact_le_cofinite _âŸ©,
  refine (filter.eventually_cofinite_ne 0).mp (filter.eventually_of_forall (Î» x hx, _)),
  rwa [real.norm_of_nonneg (zpow_nonneg (norm_nonneg _) _), zpow_neg, â†div_eq_mul_inv, le_div_iff'],
  exacts [hd' x, zpow_pos_of_pos (norm_pos_iff.mpr hx) _],
end

lemma is_O_cocompact_rpow [proper_space E] (s : â„) :
  asymptotics.is_O (filter.cocompact E) f (Î» x, â€–xâ€– ^ s) :=
begin
  let k := âŒˆ-sâŒ‰â‚Š,
  have hk : -(k : â„) â‰¤ s, from neg_le.mp (nat.le_ceil (-s)),
  refine (is_O_cocompact_zpow_neg_nat f k).trans _,
  refine (_ : asymptotics.is_O filter.at_top
    (Î» x:â„, x ^ (-k : â„¤)) (Î» x:â„, x ^ s)).comp_tendsto tendsto_norm_cocompact_at_top,
  simp_rw [asymptotics.is_O, asymptotics.is_O_with],
  refine âŸ¨1, filter.eventually_of_mem (filter.eventually_ge_at_top 1) (Î» x hx, _)âŸ©,
  rw [one_mul, real.norm_of_nonneg (real.rpow_nonneg_of_nonneg (zero_le_one.trans hx) _),
    real.norm_of_nonneg (zpow_nonneg (zero_le_one.trans hx) _), â†real.rpow_int_cast, int.cast_neg,
    int.cast_coe_nat],
  exact real.rpow_le_rpow_of_exponent_le hx hk,
end

lemma is_O_cocompact_zpow [proper_space E] (k : â„¤) :
  asymptotics.is_O (filter.cocompact E) f (Î» x, â€–xâ€– ^ k) :=
by simpa only [real.rpow_int_cast] using is_O_cocompact_rpow f k

end is_O

section aux

lemma bounds_nonempty (k n : â„•) (f : ð“¢(E, F)) :
  âˆƒ (c : â„), c âˆˆ {c : â„ | 0 â‰¤ c âˆ§ âˆ€ (x : E), â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€– â‰¤ c} :=
let âŸ¨M, hMp, hMbâŸ© := f.decay k n in âŸ¨M, le_of_lt hMp, hMbâŸ©

lemma bounds_bdd_below (k n : â„•) (f : ð“¢(E, F)) :
  bdd_below {c | 0 â‰¤ c âˆ§ âˆ€ x, â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€– â‰¤ c} :=
âŸ¨0, Î» _ âŸ¨hn, _âŸ©, hnâŸ©

lemma decay_add_le_aux (k n : â„•) (f g : ð“¢(E, F)) (x : E) :
  â€–xâ€–^k * â€–iterated_fderiv â„ n (f+g) xâ€– â‰¤
  â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€–
  + â€–xâ€–^k * â€–iterated_fderiv â„ n g xâ€– :=
begin
  rw â†mul_add,
  refine mul_le_mul_of_nonneg_left _ (by positivity),
  convert norm_add_le _ _,
  exact iterated_fderiv_add_apply (f.smooth _) (g.smooth _),
end

lemma decay_neg_aux (k n : â„•) (f : ð“¢(E, F)) (x : E) :
  â€–xâ€– ^ k * â€–iterated_fderiv â„ n (-f) xâ€– = â€–xâ€– ^ k * â€–iterated_fderiv â„ n f xâ€– :=
begin
  nth_rewrite 3 â†norm_neg,
  congr,
  exact iterated_fderiv_neg_apply,
end

variables [normed_field ð•œ] [normed_space ð•œ F] [smul_comm_class â„ ð•œ F]

lemma decay_smul_aux (k n : â„•) (f : ð“¢(E, F)) (c : ð•œ) (x : E) :
  â€–xâ€– ^ k * â€–iterated_fderiv â„ n (c â€¢ f) xâ€– =
  â€–câ€– * â€–xâ€– ^ k * â€–iterated_fderiv â„ n f xâ€– :=
by rw [mul_comm (â€–câ€–), mul_assoc, iterated_fderiv_const_smul_apply (f.smooth _), norm_smul]

end aux

section seminorm_aux

/-- Helper definition for the seminorms of the Schwartz space. -/
@[protected]
def seminorm_aux (k n : â„•) (f : ð“¢(E, F)) : â„ :=
Inf {c | 0 â‰¤ c âˆ§ âˆ€ x, â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€– â‰¤ c}

lemma seminorm_aux_nonneg (k n : â„•) (f : ð“¢(E, F)) : 0 â‰¤ f.seminorm_aux k n :=
le_cInf (bounds_nonempty k n f) (Î» _ âŸ¨hx, _âŸ©, hx)

lemma le_seminorm_aux (k n : â„•) (f : ð“¢(E, F)) (x : E) :
  â€–xâ€– ^ k * â€–iterated_fderiv â„ n â‡‘f xâ€– â‰¤ f.seminorm_aux k n :=
le_cInf (bounds_nonempty k n f) (Î» y âŸ¨_, hâŸ©, h x)

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma seminorm_aux_le_bound (k n : â„•) (f : ð“¢(E, F)) {M : â„} (hMp: 0 â‰¤ M)
  (hM : âˆ€ x, â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€– â‰¤ M) :
  f.seminorm_aux k n â‰¤ M :=
cInf_le (bounds_bdd_below k n f) âŸ¨hMp, hMâŸ©

end seminorm_aux

/-! ### Algebraic properties -/

section smul

variables [normed_field ð•œ] [normed_space ð•œ F] [smul_comm_class â„ ð•œ F]
  [normed_field ð•œ'] [normed_space ð•œ' F] [smul_comm_class â„ ð•œ' F]

instance : has_smul ð•œ ð“¢(E, F) :=
âŸ¨Î» c f, { to_fun := c â€¢ f,
  smooth' := (f.smooth _).const_smul c,
  decay' := Î» k n, begin
    refine âŸ¨f.seminorm_aux k n * (â€–câ€–+1), Î» x, _âŸ©,
    have hc : 0 â‰¤ â€–câ€– := by positivity,
    refine le_trans _ ((mul_le_mul_of_nonneg_right (f.le_seminorm_aux k n x) hc).trans _),
    { apply eq.le,
      rw [mul_comm _ (â€–câ€–), â† mul_assoc],
      exact decay_smul_aux k n f c x },
    { apply mul_le_mul_of_nonneg_left _ (f.seminorm_aux_nonneg k n),
      linarith }
  end}âŸ©

@[simp] lemma smul_apply {f : ð“¢(E, F)} {c : ð•œ} {x : E} : (c â€¢ f) x = c â€¢ (f x) := rfl

instance
[has_smul ð•œ ð•œ'] [is_scalar_tower ð•œ ð•œ' F] : is_scalar_tower ð•œ ð•œ' ð“¢(E, F) :=
âŸ¨Î» a b f, ext $ Î» x, smul_assoc a b (f x)âŸ©

instance [smul_comm_class ð•œ ð•œ' F] : smul_comm_class ð•œ ð•œ' ð“¢(E, F) :=
âŸ¨Î» a b f, ext $ Î» x, smul_comm a b (f x)âŸ©

lemma seminorm_aux_smul_le (k n : â„•) (c : ð•œ) (f : ð“¢(E, F)) :
  (c â€¢ f).seminorm_aux k n â‰¤ â€–câ€– * f.seminorm_aux k n :=
begin
  refine (c â€¢ f).seminorm_aux_le_bound k n (mul_nonneg (norm_nonneg _) (seminorm_aux_nonneg _ _ _))
    (Î» x, (decay_smul_aux k n f c x).le.trans _),
  rw mul_assoc,
  exact mul_le_mul_of_nonneg_left (f.le_seminorm_aux k n x) (norm_nonneg _),
end

instance has_nsmul : has_smul â„• ð“¢(E, F) :=
âŸ¨Î» c f, { to_fun := c â€¢ f,
  smooth' := (f.smooth _).const_smul c,
  decay' := begin
    have : c â€¢ (f : E â†’ F) = (c : â„) â€¢ f,
    { ext x, simp only [pi.smul_apply, â† nsmul_eq_smul_cast] },
    simp only [this],
    exact ((c : â„) â€¢ f).decay',
  end}âŸ©

instance has_zsmul : has_smul â„¤ ð“¢(E, F) :=
âŸ¨Î» c f, { to_fun := c â€¢ f,
  smooth' := (f.smooth _).const_smul c,
  decay' := begin
    have : c â€¢ (f : E â†’ F) = (c : â„) â€¢ f,
    { ext x, simp only [pi.smul_apply, â† zsmul_eq_smul_cast] },
    simp only [this],
    exact ((c : â„) â€¢ f).decay',
  end}âŸ©

end smul

section zero

instance : has_zero ð“¢(E, F) :=
âŸ¨{ to_fun := Î» _, 0,
  smooth' := cont_diff_const,
  decay' := Î» _ _, âŸ¨1, Î» _, by simpâŸ© }âŸ©

instance : inhabited ð“¢(E, F) := âŸ¨0âŸ©

lemma coe_zero : â†‘(0 : ð“¢(E, F)) = (0 : E â†’ F) := rfl

@[simp] lemma coe_fn_zero : coe_fn (0 : ð“¢(E, F)) = (0 : E â†’ F) := rfl

@[simp] lemma zero_apply {x : E} : (0 : ð“¢(E, F)) x = 0 := rfl

lemma seminorm_aux_zero (k n : â„•) :
  (0 : ð“¢(E, F)).seminorm_aux k n = 0 :=
le_antisymm (seminorm_aux_le_bound k n _ rfl.le (Î» _, by simp [pi.zero_def]))
  (seminorm_aux_nonneg _ _ _)

end zero

section neg

instance : has_neg ð“¢(E, F) :=
âŸ¨Î» f, âŸ¨-f, (f.smooth _).neg, Î» k n,
  âŸ¨f.seminorm_aux k n, Î» x, (decay_neg_aux k n f x).le.trans (f.le_seminorm_aux k n x)âŸ©âŸ©âŸ©

end neg

section add

instance : has_add ð“¢(E, F) :=
âŸ¨Î» f g, âŸ¨f + g, (f.smooth _).add (g.smooth _), Î» k n,
  âŸ¨f.seminorm_aux k n + g.seminorm_aux k n, Î» x, (decay_add_le_aux k n f g x).trans
    (add_le_add (f.le_seminorm_aux k n x) (g.le_seminorm_aux k n x))âŸ©âŸ©âŸ©

@[simp] lemma add_apply {f g : ð“¢(E, F)} {x : E} : (f + g) x = f x + g x := rfl

lemma seminorm_aux_add_le (k n : â„•) (f g : ð“¢(E, F)) :
  (f + g).seminorm_aux k n â‰¤ f.seminorm_aux k n + g.seminorm_aux k n :=
(f + g).seminorm_aux_le_bound k n
  (add_nonneg (seminorm_aux_nonneg _ _ _) (seminorm_aux_nonneg _ _ _)) $
  Î» x, (decay_add_le_aux k n f g x).trans $
  add_le_add (f.le_seminorm_aux k n x) (g.le_seminorm_aux k n x)

end add

section sub

instance : has_sub ð“¢(E, F) :=
âŸ¨Î» f g, âŸ¨f - g, (f.smooth _).sub (g.smooth _),
  begin
    intros k n,
    refine âŸ¨f.seminorm_aux k n + g.seminorm_aux k n, Î» x, _âŸ©,
    refine le_trans _ (add_le_add (f.le_seminorm_aux k n x) (g.le_seminorm_aux k n x)),
    rw sub_eq_add_neg,
    rw â†decay_neg_aux k n g x,
    convert decay_add_le_aux k n f (-g) x,
    -- exact fails with deterministic timeout
  endâŸ© âŸ©

@[simp] lemma sub_apply {f g : ð“¢(E, F)} {x : E} : (f - g) x = f x - g x := rfl

end sub

section add_comm_group

instance : add_comm_group ð“¢(E, F) :=
fun_like.coe_injective.add_comm_group _ rfl (Î» _ _, rfl) (Î» _, rfl) (Î» _ _, rfl) (Î» _ _, rfl)
  (Î» _ _, rfl)

variables (E F)

/-- Coercion as an additive homomorphism. -/
def coe_hom : ð“¢(E, F) â†’+ (E â†’ F) :=
{ to_fun := Î» f, f, map_zero' := coe_zero, map_add' := Î» _ _, rfl }

variables {E F}

lemma coe_coe_hom : (coe_hom E F : ð“¢(E, F) â†’ (E â†’ F)) = coe_fn := rfl

lemma coe_hom_injective : function.injective (coe_hom E F) :=
by { rw coe_coe_hom, exact fun_like.coe_injective }

end add_comm_group

section module

variables [normed_field ð•œ] [normed_space ð•œ F] [smul_comm_class â„ ð•œ F]

instance : module ð•œ ð“¢(E, F) :=
coe_hom_injective.module ð•œ (coe_hom E F) (Î» _ _, rfl)

end module

section seminorms

/-! ### Seminorms on Schwartz space-/

variables [normed_field ð•œ] [normed_space ð•œ F] [smul_comm_class â„ ð•œ F]
variable (ð•œ)

/-- The seminorms of the Schwartz space given by the best constants in the definition of
`ð“¢(E, F)`. -/
@[protected]
def seminorm (k n : â„•) : seminorm ð•œ ð“¢(E, F) := seminorm.of_smul_le (seminorm_aux k n)
  (seminorm_aux_zero k n) (seminorm_aux_add_le k n) (seminorm_aux_smul_le k n)

/-- If one controls the seminorm for every `x`, then one controls the seminorm. -/
lemma seminorm_le_bound (k n : â„•) (f : ð“¢(E, F)) {M : â„} (hMp: 0 â‰¤ M)
  (hM : âˆ€ x, â€–xâ€–^k * â€–iterated_fderiv â„ n f xâ€– â‰¤ M) : seminorm ð•œ k n f â‰¤ M :=
f.seminorm_aux_le_bound k n hMp hM

/-- If one controls the seminorm for every `x`, then one controls the seminorm.

Variant for functions `ð“¢(â„, F)`. -/
lemma seminorm_le_bound' (k n : â„•) (f : ð“¢(â„, F)) {M : â„} (hMp: 0 â‰¤ M)
  (hM : âˆ€ x, |x|^k * â€–iterated_deriv n f xâ€– â‰¤ M) : seminorm ð•œ k n f â‰¤ M :=
begin
  refine seminorm_le_bound ð•œ k n f hMp _,
  simpa only [real.norm_eq_abs, norm_iterated_fderiv_eq_norm_iterated_deriv],
end

/-- The seminorm controls the Schwartz estimate for any fixed `x`. -/
lemma le_seminorm (k n : â„•) (f : ð“¢(E, F)) (x : E) :
  â€–xâ€– ^ k * â€–iterated_fderiv â„ n f xâ€– â‰¤ seminorm ð•œ k n f :=
f.le_seminorm_aux k n x

/-- The seminorm controls the Schwartz estimate for any fixed `x`.

Variant for functions `ð“¢(â„, F)`. -/
lemma le_seminorm' (k n : â„•) (f : ð“¢(â„, F)) (x : â„) :
  |x| ^ k * â€–iterated_deriv n f xâ€– â‰¤ seminorm ð•œ k n f :=
begin
  have := le_seminorm ð•œ k n f x,
  rwa [â† real.norm_eq_abs, â† norm_iterated_fderiv_eq_norm_iterated_deriv],
end

lemma norm_iterated_fderiv_le_seminorm (f : ð“¢(E, F)) (n : â„•) (xâ‚€ : E) :
  â€–iterated_fderiv â„ n f xâ‚€â€– â‰¤ (schwartz_map.seminorm ð•œ 0 n) f :=
begin
  have := schwartz_map.le_seminorm ð•œ 0 n f xâ‚€,
  rwa [pow_zero, one_mul] at this,
end

lemma norm_pow_mul_le_seminorm (f : ð“¢(E, F)) (k : â„•) (xâ‚€ : E) :
  â€–xâ‚€â€–^k * â€–f xâ‚€â€– â‰¤ (schwartz_map.seminorm ð•œ k 0) f :=
begin
  have := schwartz_map.le_seminorm ð•œ k 0 f xâ‚€,
  rwa norm_iterated_fderiv_zero at this,
end

lemma norm_le_seminorm (f : ð“¢(E, F)) (xâ‚€ : E) :
  â€–f xâ‚€â€– â‰¤ (schwartz_map.seminorm ð•œ 0 0) f :=
begin
  have := norm_pow_mul_le_seminorm ð•œ f 0 xâ‚€,
  rwa [pow_zero, one_mul] at this,
end

variables (ð•œ E F)

/-- The family of Schwartz seminorms. -/
def _root_.schwartz_seminorm_family : seminorm_family ð•œ ð“¢(E, F) (â„• Ã— â„•) :=
Î» m, seminorm ð•œ m.1 m.2

@[simp] lemma schwartz_seminorm_family_apply (n k : â„•) :
  schwartz_seminorm_family ð•œ E F (n,k) = schwartz_map.seminorm ð•œ n k := rfl

@[simp] lemma schwartz_seminorm_family_apply_zero :
  schwartz_seminorm_family ð•œ E F 0 = schwartz_map.seminorm ð•œ 0 0 := rfl

variables {ð•œ E F}

/-- A more convenient version of `le_sup_seminorm_apply`.

The set `finset.Iic m` is the set of all pairs `(k', n')` with `k' â‰¤ m.1` and `n' â‰¤ m.2`.
Note that the constant is far from optimal. -/
lemma one_add_le_sup_seminorm_apply {m : â„• Ã— â„•} {k n : â„•} (hk : k â‰¤ m.1) (hn : n â‰¤ m.2)
  (f : ð“¢(E, F)) (x : E) :
  (1 + â€–xâ€–) ^ k * â€–iterated_fderiv â„ n f xâ€–
    â‰¤ 2^m.1 * (finset.Iic m).sup (Î» m, seminorm ð•œ m.1 m.2) f :=
begin
  rw [add_comm, add_pow],
  simp only [one_pow, mul_one, finset.sum_congr, finset.sum_mul],
  norm_cast,
  rw â† nat.sum_range_choose m.1,
  push_cast,
  rw [finset.sum_mul],
  have hk' : finset.range (k + 1) âŠ† finset.range (m.1 + 1) :=
  by rwa [finset.range_subset, add_le_add_iff_right],
  refine le_trans (finset.sum_le_sum_of_subset_of_nonneg hk' (Î» _ _ _, by positivity)) _,
  refine finset.sum_le_sum (Î» i hi, _),
  rw [mul_comm (â€–xâ€–^i), mul_assoc],
  refine mul_le_mul _ _ (by positivity) (by positivity),
  { norm_cast,
    exact i.choose_le_choose hk },
  exact (le_seminorm ð•œ i n f x).trans (seminorm.le_def.1 (finset.le_sup_of_le
    (finset.mem_Iic.2 $ prod.mk_le_mk.2 âŸ¨finset.mem_range_succ_iff.mp hi, hnâŸ©) le_rfl) _),
end

end seminorms

section topology

/-! ### The topology on the Schwartz space-/

variables [normed_field ð•œ] [normed_space ð•œ F] [smul_comm_class â„ ð•œ F]
variables (ð•œ E F)

instance : topological_space ð“¢(E, F) :=
(schwartz_seminorm_family â„ E F).module_filter_basis.topology'

lemma _root_.schwartz_with_seminorms : with_seminorms (schwartz_seminorm_family ð•œ E F) :=
begin
  have A : with_seminorms (schwartz_seminorm_family â„ E F) := âŸ¨rflâŸ©,
  rw seminorm_family.with_seminorms_iff_nhds_eq_infi at âŠ¢ A,
  rw A,
  refl
end

variables {ð•œ E F}

instance : has_continuous_smul ð•œ ð“¢(E, F) :=
begin
  rw (schwartz_with_seminorms ð•œ E F).with_seminorms_eq,
  exact (schwartz_seminorm_family ð•œ E F).module_filter_basis.has_continuous_smul,
end

instance : topological_add_group ð“¢(E, F) :=
(schwartz_seminorm_family â„ E F).add_group_filter_basis.is_topological_add_group

instance : uniform_space ð“¢(E, F) :=
(schwartz_seminorm_family â„ E F).add_group_filter_basis.uniform_space

instance : uniform_add_group ð“¢(E, F) :=
(schwartz_seminorm_family â„ E F).add_group_filter_basis.uniform_add_group

instance : locally_convex_space â„ ð“¢(E, F) :=
(schwartz_with_seminorms â„ E F).to_locally_convex_space

instance : topological_space.first_countable_topology (ð“¢(E, F)) :=
(schwartz_with_seminorms â„ E F).first_countable

end topology

section clm

/-! ### Construction of continuous linear maps between Schwartz spaces -/

variables [normed_field ð•œ] [normed_field ð•œ']
variables [normed_add_comm_group D] [normed_space â„ D]
variables [normed_space ð•œ E] [smul_comm_class â„ ð•œ E]
variables [normed_add_comm_group G] [normed_space â„ G] [normed_space ð•œ' G] [smul_comm_class â„ ð•œ' G]
variables {Ïƒ : ð•œ â†’+* ð•œ'}

/-- Create a semilinear map between Schwartz spaces.

Note: This is a helper definition for `mk_clm`. -/
def mk_lm (A : (D â†’ E) â†’ (F â†’ G))
  (hadd : âˆ€ (f g : ð“¢(D, E)) x, A (f + g) x = A f x + A g x)
  (hsmul : âˆ€ (a : ð•œ) (f : ð“¢(D, E)) x, A (a â€¢ f) x = Ïƒ a â€¢ A f x)
  (hsmooth : âˆ€ (f : ð“¢(D, E)), cont_diff â„ âŠ¤ (A f))
  (hbound : âˆ€ (n : â„• Ã— â„•), âˆƒ (s : finset (â„• Ã— â„•)) (C : â„) (hC : 0 â‰¤ C), âˆ€ (f : ð“¢(D, E)) (x : F),
  â€–xâ€– ^ n.fst * â€–iterated_fderiv â„ n.snd (A f) xâ€– â‰¤ C * s.sup (schwartz_seminorm_family ð•œ D E) f) :
  ð“¢(D, E) â†’â‚›â‚—[Ïƒ] ð“¢(F, G) :=
{ to_fun := Î» f,
  { to_fun := A f,
    smooth' := hsmooth f,
    decay' :=
    begin
      intros k n,
      rcases hbound âŸ¨k, nâŸ© with âŸ¨s, C, hC, hâŸ©,
      exact âŸ¨C * (s.sup (schwartz_seminorm_family ð•œ D E)) f, h fâŸ©,
    end, },
  map_add' := Î» f g, ext (hadd f g),
  map_smul' := Î» a f, ext (hsmul a f), }

/-- Create a continuous semilinear map between Schwartz spaces.

For an example of using this definition, see `fderiv_clm`. -/
def mk_clm [ring_hom_isometric Ïƒ] (A : (D â†’ E) â†’ (F â†’ G))
  (hadd : âˆ€ (f g : ð“¢(D, E)) x, A (f + g) x = A f x + A g x)
  (hsmul : âˆ€ (a : ð•œ) (f : ð“¢(D, E)) x, A (a â€¢ f) x = Ïƒ a â€¢ A f x)
  (hsmooth : âˆ€ (f : ð“¢(D, E)), cont_diff â„ âŠ¤ (A f))
  (hbound : âˆ€ (n : â„• Ã— â„•), âˆƒ (s : finset (â„• Ã— â„•)) (C : â„) (hC : 0 â‰¤ C), âˆ€ (f : ð“¢(D, E)) (x : F),
  â€–xâ€– ^ n.fst * â€–iterated_fderiv â„ n.snd (A f) xâ€– â‰¤ C * s.sup (schwartz_seminorm_family ð•œ D E) f) :
  ð“¢(D, E) â†’SL[Ïƒ] ð“¢(F, G) :=
{ cont :=
  begin
    change continuous (mk_lm A hadd hsmul hsmooth hbound : ð“¢(D, E) â†’â‚›â‚—[Ïƒ] ð“¢(F, G)),
    refine seminorm.continuous_from_bounded (schwartz_with_seminorms ð•œ D E)
      (schwartz_with_seminorms ð•œ' F G) _ (Î» n, _),
    rcases hbound n with âŸ¨s, C, hC, hâŸ©,
    refine âŸ¨s, âŸ¨C, hCâŸ©, (Î» f, _)âŸ©,
    simp only [seminorm.comp_apply, seminorm.smul_apply, nnreal.smul_def, algebra.id.smul_eq_mul,
      subtype.coe_mk],
    exact (mk_lm A hadd hsmul hsmooth hbound f).seminorm_le_bound ð•œ' n.1 n.2 (by positivity) (h f),
  end,
  to_linear_map := mk_lm A hadd hsmul hsmooth hbound }

end clm

section multiplication

variables [normed_add_comm_group D] [normed_space â„ D]
variables [normed_add_comm_group G] [normed_space â„ G]

/-- Provided one can estimate each `iterated_fderiv â„ n` by `C * (1 + â€–xâ€–)^k`,
then one can find `C` and `k` independent of `n` such that the same estimate holds. -/
lemma norm_iterated_fderiv_le_uniform_aux {g : D â†’ F}
  (hg_growth : âˆ€ n : â„•, âˆƒ (k : â„•) (C : â„), âˆ€ (x : D), â€–iterated_fderiv â„ n g xâ€– â‰¤ C * (1 + â€–xâ€–)^k) :
  âˆ€ n : â„•, âˆƒ (k : â„•) (C : â„) (hC : 0 â‰¤ C), âˆ€ (N : â„•) (hN : N â‰¤ n) (x : D) ,
    â€–iterated_fderiv â„ N g xâ€– â‰¤ C * (1 + â€–xâ€–)^k :=
begin
  intro n,
  choose k C f using hg_growth,
  use (finset.range (n+1)).sup k,
  let C' := max (0 : â„) ((finset.range (n+1)).sup' (by simp) C),
  have hC' : 0 â‰¤ C' := by simp only [le_refl, finset.le_sup'_iff, true_or, le_max_iff],
  use [C', hC'],
  intros N hN x,
  rw â† finset.mem_range_succ_iff at hN,
  refine le_trans (f N x) (mul_le_mul _ _ (by positivity) hC'),
  { simp only [finset.le_sup'_iff, le_max_iff],
    right,
    exact âŸ¨N, hN, rfl.leâŸ© },
  refine pow_le_pow (by simp only [le_add_iff_nonneg_right, norm_nonneg]) _,
  exact finset.le_sup hN,
end

/-- The multiplication with a smooth function of temperate growth is a continuous linear map on
Schwartz space. -/
def mul_clm (B : E â†’L[â„] F â†’L[â„] G) {g : D â†’ F} (hg_smooth : cont_diff â„ âŠ¤ g)
  (hg_growth : âˆ€ n : â„•, âˆƒ (k : â„•) (C : â„), âˆ€ (x : D), â€–iterated_fderiv â„ n g xâ€– â‰¤ C * (1 + â€–xâ€–)^k) :
  ð“¢(D, E) â†’L[â„] ð“¢(D, G) :=
mk_clm (Î» f x, B (f x) (g x))
  (Î» _ _ _, by simp only [map_add, add_left_inj, pi.add_apply, eq_self_iff_true,
    continuous_linear_map.add_apply])
  (Î» _ _ _, by simp only [pi.smul_apply, continuous_linear_map.coe_smul',
    continuous_linear_map.map_smul, ring_hom.id_apply])
  (Î» f, B.is_bounded_bilinear_map.cont_diff.comp (f.smooth'.prod hg_smooth))
  (begin
    -- Porting note: rewrite this proof with `rel_congr`
    rintro âŸ¨k, nâŸ©,
    rcases norm_iterated_fderiv_le_uniform_aux hg_growth n with âŸ¨l, C, hC, hgrowth'âŸ©,
    use [finset.Iic (l+k,n), â€–Bâ€– * (n + 1) * n.choose (n / 2) * (C * 2^(l + k)), by positivity],
    intros f x,
    have hxk : 0 â‰¤ â€–xâ€–^k := by positivity,
    have hnorm_mul :=
    continuous_linear_map.norm_iterated_fderiv_le_of_bilinear B f.smooth' hg_smooth x le_top,
    refine le_trans (mul_le_mul_of_nonneg_left hnorm_mul hxk) _,
    rw [â† mul_assoc (â€–xâ€–^k), mul_comm (â€–xâ€–^k)],
    simp_rw [mul_assoc (â€–Bâ€–)],
    refine mul_le_mul_of_nonneg_left _ (by positivity),
    rw [finset.mul_sum],
    have : âˆ‘ (x_1 : â„•) in finset.range (n + 1), (1 : â„) = n + 1 := by simp,
    repeat { rw [mul_assoc ((n : â„) + 1)] },
    rw [â† this, finset.sum_mul],
    refine finset.sum_le_sum (Î» i hi, _),
    simp only [one_mul],
    rw [â† mul_assoc, mul_comm (â€–xâ€–^k), mul_assoc, mul_assoc, mul_assoc],
    refine mul_le_mul _ _ (by positivity) (by positivity),
    { norm_cast,
      exact i.choose_le_middle n },
    specialize hgrowth' (n - i) (by simp only [tsub_le_self]) x,
    rw [â† mul_assoc],
    refine le_trans (mul_le_mul_of_nonneg_left hgrowth' (by positivity)) _,
    rw [mul_comm _ (C * _), mul_assoc, mul_assoc C],
    refine mul_le_mul_of_nonneg_left _ hC,
    nth_rewrite 1 mul_comm,
    rw [â† mul_assoc],
    rw finset.mem_range_succ_iff at hi,
    change i â‰¤ (l + k, n).snd at hi,
    refine le_trans _ (one_add_le_sup_seminorm_apply le_rfl hi f x ),
    refine mul_le_mul_of_nonneg_right _ (norm_nonneg _),
    rw [pow_add],
    refine mul_le_mul_of_nonneg_left _ (by positivity),
    refine pow_le_pow_of_le_left (norm_nonneg _) _ _,
    simp only [zero_le_one, le_add_iff_nonneg_left],
  end)

end multiplication

section comp

variables [normed_add_comm_group D] [normed_space â„ D]
variables [normed_add_comm_group G] [normed_space â„ G]

/-- Composition with a smooth function of temperate growth on the right is a continuous linear
map on Schwartz space. -/
def comp_clm {g : D â†’ E} (hg_smooth : cont_diff â„ âŠ¤ g)
  (hg_growth : âˆ€ n : â„•, âˆƒ (k : â„•) (C : â„), âˆ€ x, â€–iterated_fderiv â„ n g xâ€– â‰¤ C * (1 + â€–xâ€–)^k)
  (hg_upper : âˆƒ (k : â„•) (C : â„) (hC : 1 â‰¤ C), âˆ€ x, 1 + â€–xâ€– â‰¤ C * (1 + â€–g xâ€–)^k ) :
  ð“¢(E, F) â†’L[â„] ð“¢(D, F) :=
mk_clm (Î» f x, (f (g x)))
  (Î» f f' x, by simp only [add_left_inj, pi.add_apply, eq_self_iff_true])
  (Î» a f x, by simp only [pi.smul_apply, ring_hom.id_apply])
  (Î» f, f.smooth'.comp hg_smooth)
  (begin
    rintros âŸ¨k, nâŸ©,
    rcases norm_iterated_fderiv_le_uniform_aux hg_growth n with âŸ¨l, C, hC, hgrowth'âŸ©,
    rcases hg_upper with âŸ¨kg, Cg, hCg, hg_upper'âŸ©,
    let k' := kg * (k + l * n),
    use [finset.Iic (k',n), Cg ^ (k + l * n) * ((C + 1) ^ n * n! * 2 ^ k'), by positivity],
    intros f x,
    let seminorm_f := ((finset.Iic (k',n)).sup (schwartz_seminorm_family â„ _ _)) f,
    have hg_upper'' : (1 + â€–xâ€–)^(k + l * n) â‰¤ Cg^(k + l*n) * (1 + â€–g xâ€–)^k' :=
    begin
      rw [pow_mul, â† mul_pow],
      exact pow_le_pow_of_le_left (by positivity) (hg_upper' x) _,
    end,
    have hbound : âˆ€ i, i â‰¤ n â†’ â€–iterated_fderiv â„ i f (g x)â€– â‰¤
      2 ^ k' * seminorm_f / ((1 + â€–g xâ€–) ^ k'):=
    begin
      intros i hi,
      have hpos : 0 < (1 + â€–g xâ€–) ^ k' := by positivity,
      rw le_div_iff' hpos,
      change i â‰¤ (k', n).snd at hi,
      exact one_add_le_sup_seminorm_apply le_rfl hi _ _,
    end,
    have hgrowth'' : âˆ€ (N : â„•) (hNâ‚ : 1 â‰¤ N) (hNâ‚‚ : N â‰¤ n),
      â€–iterated_fderiv â„ N g xâ€– â‰¤ ((C + 1) * (1 + â€–xâ€–)^l)^N :=
    begin
      intros N hNâ‚ hNâ‚‚,
      refine (hgrowth' N hNâ‚‚ x).trans _,
      rw [mul_pow],
      have hNâ‚' := (lt_of_lt_of_le zero_lt_one hNâ‚).ne.symm,
      refine mul_le_mul _ _ (by positivity) (by positivity),
      { exact le_trans (by simp [hC]) (le_self_pow (by simp [hC]) hNâ‚'), },
      { refine le_self_pow (one_le_pow_of_one_le _ l) hNâ‚',
      simp only [le_add_iff_nonneg_right, norm_nonneg] },
    end,
    have := norm_iterated_fderiv_comp_le f.smooth' hg_smooth le_top x hbound hgrowth'',
    have hxk : â€–xâ€–^k â‰¤ (1 + â€–xâ€–)^k :=
    pow_le_pow_of_le_left (norm_nonneg _) (by simp only [zero_le_one, le_add_iff_nonneg_left]) _,
    refine le_trans (mul_le_mul hxk this (by positivity) (by positivity)) _,
    have rearrange :
      (1 + â€–xâ€–) ^ k * (n! * (2 ^ k' * seminorm_f / (1 + â€–g xâ€–) ^ k') * ((C + 1) * (1 + â€–xâ€–) ^ l) ^ n) =
      ((1 + â€–xâ€–)^(k + l * n) / (1 + â€–g xâ€–) ^ k') * ((C + 1)^n * n! * 2^k' * seminorm_f) :=
    begin
      rw [mul_pow, pow_add, â† pow_mul],
      ring,
    end,
    rw rearrange,
    have hgxk' : 0 < (1 + â€–g xâ€–) ^ k' := by positivity,
    rw â† div_le_iff hgxk' at hg_upper'',
    have hpos : 0 â‰¤ (C + 1) ^ n * n! * 2 ^ k' * seminorm_f :=
    begin
      have : 0 â‰¤ seminorm_f := map_nonneg _ _,
      positivity,
    end,
    refine le_trans (mul_le_mul_of_nonneg_right hg_upper'' hpos) _,
    rw [â† mul_assoc],
  end)

end comp

section derivatives

/-! ### Derivatives of Schwartz functions -/

variables (ð•œ)
variables [is_R_or_C ð•œ] [normed_space ð•œ F] [smul_comm_class â„ ð•œ F]

/-- The FrÃ©chet derivative on Schwartz space as a continuous `ð•œ`-linear map. -/
def fderiv_clm : ð“¢(E, F) â†’L[ð•œ] ð“¢(E, E â†’L[â„] F) :=
mk_clm (fderiv â„)
  (Î» f g _, fderiv_add f.differentiable_at g.differentiable_at)
  (Î» a f _, fderiv_const_smul f.differentiable_at a)
  (Î» f, (cont_diff_top_iff_fderiv.mp f.smooth').2)
  (Î» âŸ¨k, nâŸ©, âŸ¨{âŸ¨k, n+1âŸ©}, 1, zero_le_one, Î» f x, by simpa only [schwartz_seminorm_family_apply,
    seminorm.comp_apply, finset.sup_singleton, one_smul, norm_iterated_fderiv_fderiv, one_mul]
      using f.le_seminorm ð•œ k (n+1) xâŸ©)

@[simp] lemma fderiv_clm_apply (f : ð“¢(E, F)) (x : E) : fderiv_clm ð•œ f x = fderiv â„ f x :=
rfl

/-- The 1-dimensional derivative on Schwartz space as a continuous `ð•œ`-linear map. -/
def deriv_clm : ð“¢(â„, F) â†’L[ð•œ] ð“¢(â„, F) :=
mk_clm (Î» f, deriv f)
  (Î» f g _, deriv_add f.differentiable_at g.differentiable_at)
  (Î» a f _, deriv_const_smul a f.differentiable_at)
  (Î» f, (cont_diff_top_iff_deriv.mp f.smooth').2)
  (Î» âŸ¨k, nâŸ©, âŸ¨{âŸ¨k, n+1âŸ©}, 1, zero_le_one, Î» f x, by simpa only [real.norm_eq_abs,
    finset.sup_singleton, schwartz_seminorm_family_apply, one_mul,
    norm_iterated_fderiv_eq_norm_iterated_deriv, â† iterated_deriv_succ']
    using f.le_seminorm' ð•œ k (n + 1) xâŸ©)

@[simp] lemma deriv_clm_apply (f : ð“¢(â„, F)) (x : â„) : deriv_clm ð•œ f x = deriv f x := rfl

end derivatives

section bounded_continuous_function

/-! ### Inclusion into the space of bounded continuous functions -/

open_locale bounded_continuous_function

/-- Schwartz functions as bounded continuous functions -/
def to_bounded_continuous_function (f : ð“¢(E, F)) : E â†’áµ‡ F :=
bounded_continuous_function.of_normed_add_comm_group f (schwartz_map.continuous f)
  (schwartz_map.seminorm â„ 0 0 f) (norm_le_seminorm â„ f)

@[simp] lemma to_bounded_continuous_function_apply (f : ð“¢(E, F)) (x : E) :
  f.to_bounded_continuous_function x = f x := rfl

/-- Schwartz functions as continuous functions -/
def to_continuous_map (f : ð“¢(E, F)) : C(E, F) :=
f.to_bounded_continuous_function.to_continuous_map

variables (ð•œ E F)
variables [is_R_or_C ð•œ] [normed_space ð•œ F] [smul_comm_class â„ ð•œ F]

/-- The inclusion map from Schwartz functions to bounded continuous functions as a linear map. -/
def to_bounded_continuous_function_lm : ð“¢(E, F) â†’â‚—[ð•œ] E â†’áµ‡ F :=
{ to_fun := Î» f, f.to_bounded_continuous_function,
  map_add' := Î» f g, by { ext, exact add_apply },
  map_smul' := Î» a f, by { ext, exact smul_apply } }

@[simp] lemma to_bounded_continuous_function_lm_apply (f : ð“¢(E, F)) (x : E) :
  to_bounded_continuous_function_lm ð•œ E F f x = f x := rfl

/-- The inclusion map from Schwartz functions to bounded continuous functions as a continuous linear
map. -/
def to_bounded_continuous_function_clm : ð“¢(E, F) â†’L[ð•œ] E â†’áµ‡ F :=
{ cont :=
  begin
    change continuous (to_bounded_continuous_function_lm ð•œ E F),
    refine seminorm.continuous_from_bounded (schwartz_with_seminorms ð•œ E F)
      (norm_with_seminorms ð•œ (E â†’áµ‡ F)) _ (Î» i, âŸ¨{0}, 1, Î» f, _âŸ©),
    rw [finset.sup_singleton, one_smul , seminorm.comp_apply, coe_norm_seminorm,
        schwartz_seminorm_family_apply_zero, bounded_continuous_function.norm_le (map_nonneg _ _)],
    intros x,
    exact norm_le_seminorm ð•œ _ _,
  end,
  .. to_bounded_continuous_function_lm ð•œ E F}

@[simp] lemma to_bounded_continuous_function_clm_apply (f : ð“¢(E, F)) (x : E) :
  to_bounded_continuous_function_clm ð•œ E F f x = f x := rfl

variables {E}

/-- The Dirac delta distribution -/
def delta (x : E) : ð“¢(E, F) â†’L[ð•œ] F :=
(bounded_continuous_function.eval_clm ð•œ x).comp (to_bounded_continuous_function_clm ð•œ E F)

@[simp] lemma delta_apply (xâ‚€ : E) (f : ð“¢(E, F)) : delta ð•œ F xâ‚€ f = f xâ‚€ := rfl

end bounded_continuous_function

end schwartz_map
