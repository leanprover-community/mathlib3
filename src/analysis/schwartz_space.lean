/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/

import analysis.calculus.cont_diff
import analysis.complex.basic
import analysis.locally_convex.with_seminorms
import topology.algebra.uniform_filter_basis
import tactic.positivity

/-!
# Schwartz space

This file defines the Schwartz space and the space of tempered distributions. Usually,
the Schwartz space is defined as the set of smooth functions $f : â„^n â†’ â„‚$ such that there exists
$C_{Î±Î²} > 0$ with $$|x^Î± âˆ‚^Î² f(x)| < C_{Î±Î²}$$ for all $x âˆˆ â„^n$ and for all multiindices $Î±, Î²$.
In mathlib, we use a slightly different approach and define define the Schwartz space as all
smooth functions `f : E â†’ F`, where `E` and `F` are real normed vector spaces such that for all
natural numbers `k` and `n` we have uniform bounds `âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥ < C`.
This approach completely avoids using partial derivatives as well as polynomials.
We construct the topology on the Schwartz space by a family of seminorms, which are the best
constants in the above estimates, which is by abstract theory from
`seminorm_family.module_filter_basis` and `seminorm_family.to_locally_convex_space` turns the
Schwartz space into a locally convex topological vector space.

## Main definitions

* `schwartz_map`: The Schwartz space is the space of smooth functions such that all derivatives
decay faster than any power of `âˆ¥xâˆ¥`.
* `schwartz_map.seminorm`: The family of seminorms as described above

## Main statements

* `schwartz_map.uniform_add_group` and `schwartz_map.locally_convex`: The Schwartz space is a
locally convex topological vector space.

## Implementation details

The implementation of the seminorms is taken almost literally from `continuous_linear_map.op_norm`.

## Notation

* `ð“¢(E, F)`: The Schwartz space `schwartz_map E F` localized in `schwartz_space`

## Tags

Schwartz space, tempered distributions
-/

noncomputable theory

variables {R R' ð•œ E F : Type*}

variables [normed_add_comm_group E] [normed_space â„ E]
variables [normed_add_comm_group F] [normed_space â„ F]

variables (E F)

/-- A function is a Schwartz function if it is smooth and all derivatives decay faster than
  any power of `âˆ¥xâˆ¥`. -/
structure schwartz_map :=
  (to_fun : E â†’ F)
  (smooth' : cont_diff â„ âŠ¤ to_fun)
  (decay' : âˆ€ (k n : â„•), âˆƒ (C : â„) (hC : 0 < C), âˆ€ x, âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n to_fun xâˆ¥ â‰¤ C)

localized "notation `ð“¢(` E `, ` F `)` := schwartz_map E F" in schwartz_space

variables {E F}

namespace schwartz_map

instance : has_coe ð“¢(E, F) (E â†’ F) := âŸ¨to_funâŸ©

instance fun_like : fun_like ð“¢(E, F) E (Î» _, F) :=
{ coe := Î» f, f.to_fun,
  coe_injective' := Î» f g h, by cases f; cases g; congr' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`. -/
instance : has_coe_to_fun ð“¢(E, F) (Î» _, E â†’ F) := âŸ¨Î» p, p.to_funâŸ©

/-- All derivatives of a Schwartz function are rapidly decaying. -/
lemma decay (f : ð“¢(E, F)) (k n : â„•) : âˆƒ (C : â„) (hC : 0 < C),
  âˆ€ x, âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥ â‰¤ C :=
f.decay' k n

/-- Every Schwartz function is smooth. -/
lemma smooth (f : ð“¢(E, F)) : cont_diff â„ âŠ¤ f := f.smooth'

@[ext] lemma ext {f g : ð“¢(E, F)} (h : âˆ€ x, (f : E â†’ F) x = g x) : f = g := fun_like.ext f g h

section aux

lemma bounds_nonempty (k n : â„•) (f : ð“¢(E, F)) :
  âˆƒ (c : â„), c âˆˆ {c : â„ | 0 â‰¤ c âˆ§ âˆ€ (x : E), âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥ â‰¤ c} :=
let âŸ¨M, hMp, hMbâŸ© := f.decay k n in âŸ¨M, le_of_lt hMp, hMbâŸ©

lemma bounds_bdd_below (k n : â„•) (f : ð“¢(E, F)) :
  bdd_below { c | 0 â‰¤ c âˆ§ âˆ€ x, âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥ â‰¤ c } :=
âŸ¨0, Î» _ âŸ¨hn, _âŸ©, hnâŸ©

lemma decay_add_le_aux (k n : â„•) (f g : ð“¢(E, F)) (x : E) :
  âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n (f+g) xâˆ¥ â‰¤
  âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥
  + âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n g xâˆ¥ :=
begin
  rw â†mul_add,
  refine mul_le_mul_of_nonneg_left _ (by positivity),
  convert norm_add_le _ _,
  refine iterated_fderiv_add_apply _ _,
  { exact f.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
  { exact g.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
end

variables [is_R_or_C ð•œ] [normed_space ð•œ F]
variables [semiring R] [module R ð•œ] [module R F] [smul_comm_class â„ R F]
variables [has_continuous_const_smul R F] [is_scalar_tower R ð•œ F]

lemma decay_smul_aux (k n : â„•) (f : ð“¢(E, F)) (c : R) (x : E) :
  âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n (c â€¢ f) xâˆ¥ =
  âˆ¥c â€¢ (1 : ð•œ)âˆ¥ * âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n f xâˆ¥ :=
begin
  nth_rewrite 2 mul_comm,
  rw mul_assoc,
  congr,
  rw iterated_fderiv_const_smul_apply ,
  { rw â†smul_one_smul ð•œ c,
    rw norm_smul,
    apply_instance },
  { exact f.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
end

lemma decay_neg_aux (k n : â„•) (f : ð“¢(E, F)) (x : E) :
  âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n (-f) xâˆ¥ = âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n f xâˆ¥ :=
begin
  nth_rewrite 3 â†norm_neg,
  congr,
  exact iterated_fderiv_neg_apply,
end

variables [normed_space â„‚ F]

lemma decay_smul_aux' (k n : â„•) (f : ð“¢(E, F)) (c : â„‚) (x : E) :
  âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n (c â€¢ f) xâˆ¥ =
  âˆ¥câˆ¥ * âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n f xâˆ¥ :=
begin
  nth_rewrite 2 mul_comm,
  rw mul_assoc,
  congr,
  rw iterated_fderiv_const_smul_apply ,
  { rw norm_smul },
  { exact f.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
end

end aux

section seminorm_aux

/-- Helper definition for the seminorms of the Schwartz space. -/
@[protected]
def seminorm_aux (k n : â„•) (f : ð“¢(E, F)) : â„ :=
Inf {c | 0 â‰¤ c âˆ§ âˆ€ x, âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥ â‰¤ c}

lemma seminorm_aux_nonneg (k n : â„•) (f : ð“¢(E, F)) : 0 â‰¤ f.seminorm_aux k n :=
le_cInf (bounds_nonempty k n f) (Î» _ âŸ¨hx, _âŸ©, hx)

lemma le_seminorm_aux (k n : â„•) (f : ð“¢(E, F)) (x : E) :
  âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n â‡‘f xâˆ¥ â‰¤ f.seminorm_aux k n :=
le_cInf (bounds_nonempty k n f) (Î» y âŸ¨_, hâŸ©, h x)

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma seminorm_aux_le_bound (k n : â„•) (f : ð“¢(E, F)) {M : â„} (hMp: 0 â‰¤ M)
  (hM : âˆ€ x, âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥ â‰¤ M) :
  f.seminorm_aux k n â‰¤ M :=
cInf_le (bounds_bdd_below k n f) âŸ¨hMp, hMâŸ©

end seminorm_aux

/-! ### Algebraic properties -/

section smul

variables [normed_space â„‚ F]
variables [semiring R] [module R â„‚] [module R F] [smul_comm_class â„ R F]
variables [has_continuous_const_smul R F] [is_scalar_tower R â„‚ F]
variables [semiring R'] [module R' â„‚] [module R' F] [smul_comm_class â„ R' F]
variables [has_continuous_const_smul R' F] [is_scalar_tower R' â„‚ F]

-- Note that we define the scalar multiplication only in the case that `F` is a vector space
-- over `â„‚`. The reason for this is that the type-system cannot infer instances if we were to
-- replace `â„‚` by `[is_R_or_C ð•œ]`. This is mathematically no problem, because the usual Schwartz
-- space is `ð“¢(E, â„‚)` and the space `ð“¢(E, â„)` is never used in mathematics.
instance : has_smul R ð“¢(E, F) :=
âŸ¨Î» c f, { to_fun := c â€¢ f,
  smooth' := f.smooth.const_smul c,
  decay' := Î» k n, begin
    rcases f.decay k n with âŸ¨C, hC, hfâŸ©,
    refine âŸ¨C * (âˆ¥c â€¢ (1 : â„‚)âˆ¥+1), by positivity, _âŸ©,
    intros x,
    specialize hf x,
    have hc : 0 â‰¤ âˆ¥c â€¢ (1 : â„‚)âˆ¥ := by positivity,
    refine le_trans _ ((mul_le_mul_of_nonneg_right hf hc).trans _),
    { refine eq.le _,
      nth_rewrite 1 mul_comm,
      rw â†mul_assoc,
      refine decay_smul_aux k n f c x },
    rw [mul_le_mul_left hC, le_add_iff_nonneg_right],
    exact zero_le_one,
  end}âŸ©

@[simp] lemma smul_apply {f : ð“¢(E, F)} {c : R} {x : E} : (c â€¢ f) x = c â€¢ (f x) := rfl

instance [has_smul R R'] [is_scalar_tower R R' F] : is_scalar_tower R R' ð“¢(E, F) :=
âŸ¨Î» a b f, ext $ Î» x, smul_assoc a b (f x)âŸ©

instance [smul_comm_class R R' F] : smul_comm_class R R' ð“¢(E, F) :=
âŸ¨Î» a b f, ext $ Î» x, smul_comm a b (f x)âŸ©

lemma seminorm_aux_smul_le (k n : â„•) (r : â„‚) (f : ð“¢(E, F)) :
  (r â€¢ f).seminorm_aux k n â‰¤ âˆ¥râˆ¥ * f.seminorm_aux k n :=
begin
  refine (r â€¢ f).seminorm_aux_le_bound k n (mul_nonneg (norm_nonneg _) (seminorm_aux_nonneg _ _ _))
    (Î» x, (decay_smul_aux' k n f r x).le.trans _),
  rw mul_assoc,
  exact mul_le_mul_of_nonneg_left (f.le_seminorm_aux k n x) (norm_nonneg _),
end

end smul

section zero

instance : has_zero ð“¢(E, F) :=
âŸ¨{ to_fun := Î» _, 0,
  smooth' := cont_diff_const,
  decay' := Î» _ _, âŸ¨1, zero_lt_one, Î» _, by simpâŸ© }âŸ©

instance : inhabited ð“¢(E, F) := âŸ¨0âŸ©

lemma coe_zero : â†‘(0 : ð“¢(E, F)) = (0 : E â†’ F) := rfl

@[simp] lemma coe_fn_zero : coe_fn (0 : ð“¢(E, F)) = (0 : E â†’ F) := rfl

@[simp] lemma zero_apply {x : E} : (0 : ð“¢(E, F)) x = 0 := rfl

lemma seminorm_aux_zero (k n : â„•) :
  (0 : ð“¢(E, F)).seminorm_aux k n = 0 :=
le_antisymm (seminorm_aux_le_bound k n _ rfl.le (Î» _, by simp [pi.zero_def]))
  (seminorm_aux_nonneg _ _ _)

end zero

section neg

instance : has_neg ð“¢(E, F) :=
âŸ¨Î» f, âŸ¨-f, f.smooth.neg,
  begin
    intros k n,
    rcases f.decay k n with âŸ¨C, hC, hfâŸ©,
    exact âŸ¨C, hC, Î» x, (decay_neg_aux k n f x).le.trans (hf x)âŸ©,
  endâŸ© âŸ©

end neg

section add

instance : has_add ð“¢(E, F) :=
âŸ¨Î» f g, âŸ¨f + g, f.smooth.add g.smooth,
  begin
    intros k n,
    rcases f.decay k n with âŸ¨Cf, hCf, hfâŸ©,
    rcases g.decay k n with âŸ¨Cg, hCg, hgâŸ©,
    refine âŸ¨Cf + Cg, by positivity, Î» x, _âŸ©,
    exact (decay_add_le_aux k n f g x).trans (add_le_add (hf x) (hg x)),
  endâŸ© âŸ©

@[simp] lemma add_apply {f g : ð“¢(E, F)} {x : E} : (f + g) x = f x + g x := rfl

lemma seminorm_aux_add_le (k n : â„•) (f g : ð“¢(E, F)) :
  (f + g).seminorm_aux k n â‰¤ f.seminorm_aux k n + g.seminorm_aux k n :=
(f + g).seminorm_aux_le_bound k n
  (add_nonneg (seminorm_aux_nonneg _ _ _) (seminorm_aux_nonneg _ _ _)) $
  Î» x, (decay_add_le_aux k n f g x).trans $
  add_le_add (f.le_seminorm_aux k n x) (g.le_seminorm_aux k n x)

end add

section sub

instance : has_sub ð“¢(E, F) :=
âŸ¨Î» f g, âŸ¨f - g, f.smooth.sub g.smooth,
  begin
    intros k n,
    rcases f.decay k n with âŸ¨Cf, hCf, hfâŸ©,
    rcases g.decay k n with âŸ¨Cg, hCg, hgâŸ©,
    refine âŸ¨Cf + Cg, by positivity, Î» x, _âŸ©,
    refine le_trans _ (add_le_add (hf x) (hg x)),
    rw sub_eq_add_neg,
    rw â†decay_neg_aux k n g x,
    convert decay_add_le_aux k n f (-g) x,
    -- exact fails with deterministic timeout
  endâŸ© âŸ©

@[simp] lemma sub_apply {f g : ð“¢(E, F)} {x : E} : (f - g) x = f x - g x := rfl

end sub

section add_comm_group

variables [normed_space â„‚ F]

instance : add_comm_group ð“¢(E, F) :=
fun_like.coe_injective.add_comm_group _ rfl (Î» _ _, rfl) (Î» _, rfl) (Î» _ _, rfl) (Î» _ _, rfl)
  (Î» _ _, rfl)

variables (E F)

/-- Coercion as an additive homomorphism. -/
def coe_hom : ð“¢(E, F) â†’+ (E â†’ F) :=
{ to_fun := Î» f, f, map_zero' := coe_zero, map_add' := Î» _ _, rfl }

variables {E F}

lemma coe_coe_hom : (coe_hom E F : ð“¢(E, F) â†’ (E â†’ F)) = coe_fn := rfl

lemma coe_hom_injective : function.injective (coe_hom E F) :=
by { rw coe_coe_hom, exact fun_like.coe_injective }

end add_comm_group

section module

variables [normed_space â„‚ F]
variables [semiring R] [module R â„‚] [module R F] [smul_comm_class â„ R F]
variables [has_continuous_const_smul R F] [is_scalar_tower R â„‚ F]

instance : module R ð“¢(E, F) :=
coe_hom_injective.module R (coe_hom E F) (Î» _ _, rfl)

end module

section seminorms

/-! ### Seminorms on Schwartz space-/

variables [normed_space â„‚ F]

/-- The seminorms of the Schwartz space given by the best constants in the definition of
`ð“¢(E, F)`. -/
@[protected]
def seminorm (k n : â„•) : seminorm â„‚ ð“¢(E, F) := seminorm.of_smul_le (seminorm_aux k n)
  (seminorm_aux_zero k n) (seminorm_aux_add_le k n) (seminorm_aux_smul_le k n)

/-- If one controls the seminorm for every `x`, then one controls the seminorm. -/
lemma seminorm_le_bound (k n : â„•) (f : ð“¢(E, F)) {M : â„} (hMp: 0 â‰¤ M)
  (hM : âˆ€ x, âˆ¥xâˆ¥^k * âˆ¥iterated_fderiv â„ n f xâˆ¥ â‰¤ M) : seminorm k n f â‰¤ M :=
f.seminorm_aux_le_bound k n hMp hM

/-- The seminorm controls the Schwartz estimate for any fixed `x`. -/
lemma le_seminorm (k n : â„•) (f : ð“¢(E, F)) (x : E) :
  âˆ¥xâˆ¥ ^ k * âˆ¥iterated_fderiv â„ n f xâˆ¥ â‰¤ seminorm k n f :=
f.le_seminorm_aux k n x

end seminorms

section topology

/-! ### The topology on the Schwartz space-/

variables (E F)
variables [normed_space â„‚ F]

/-- The family of Schwartz seminorms. -/
def _root_.schwartz_seminorm_family : seminorm_family â„‚ ð“¢(E, F) (â„• Ã— â„•) :=
Î» n, seminorm n.1 n.2

variables {E F}

instance : topological_space ð“¢(E, F) := (schwartz_seminorm_family E F).module_filter_basis.topology'

instance : has_continuous_smul â„‚ ð“¢(E, F) :=
(schwartz_seminorm_family E F).module_filter_basis.has_continuous_smul

instance : topological_add_group ð“¢(E, F) :=
(schwartz_seminorm_family E F).module_filter_basis.to_add_group_filter_basis
  .is_topological_add_group

instance : uniform_space ð“¢(E, F) :=
(schwartz_seminorm_family E F).module_filter_basis.to_add_group_filter_basis.uniform_space

instance : uniform_add_group ð“¢(E, F) :=
(schwartz_seminorm_family E F).module_filter_basis.to_add_group_filter_basis.uniform_add_group

variables (E F)

lemma _root_.schwartz_with_seminorms : with_seminorms (schwartz_seminorm_family E F) := âŸ¨rflâŸ©

variables {E F}

instance : locally_convex_space â„ ð“¢(E, F) :=
seminorm_family.to_locally_convex_space (schwartz_with_seminorms E F)

end topology

end schwartz_map
