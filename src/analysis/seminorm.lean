/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo
-/

import algebra.pointwise
import analysis.normed_space.basic

/-!
# Seminorms and Local Convexity

This file introduces the following notions, defined for a vector space
over a normed field:

- the subset properties of being `absorbent` and `balanced`,

- a `seminorm`, a function to the reals that is positive-semidefinite,
  absolutely homogeneous, and subadditive.

We prove related properties.

## TODO

Define and show equivalence of two notions of local convexity for a
topological vector space over â„ or â„‚: that it has a local base of
balanced convex absorbent sets, and that it carries the initial
topology induced by a family of seminorms.

## References
* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]
-/

/-!
### Subset Properties

Absorbent and balanced sets in a vector space over a
nondiscrete normed field.
-/

section

variables
(ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
{E : Type*} [add_comm_group E] [module ğ•œ E]

open set normed_field
open_locale topological_space pointwise

/-- A set `A` absorbs another set `B` if `B` is contained in scaling
`A` by elements of sufficiently large norms. -/
def absorbs (A B : set E) := âˆƒ r > 0, âˆ€ a : ğ•œ, r â‰¤ âˆ¥aâˆ¥ â†’ B âŠ† a â€¢ A

/-- A set is absorbent if it absorbs every singleton. -/
def absorbent (A : set E) := âˆ€ x, âˆƒ r > 0, âˆ€ a : ğ•œ, r â‰¤ âˆ¥aâˆ¥ â†’ x âˆˆ a â€¢ A

/-- A set `A` is balanced if `a â€¢ A` is contained in `A` whenever `a`
has norm no greater than one. -/
def balanced (A : set E) := âˆ€ a : ğ•œ, âˆ¥aâˆ¥ â‰¤ 1 â†’ a â€¢ A âŠ† A

variables {ğ•œ} (a : ğ•œ) {A : set E}

/-- A balanced set absorbs itself. -/
lemma balanced.absorbs_self (hA : balanced ğ•œ A) : absorbs ğ•œ A A :=
begin
  use [1, zero_lt_one],
  intros a ha x hx,
  rw mem_smul_set_iff_inv_smul_memâ‚€,
  { apply hA aâ»Â¹,
    { rw norm_inv, exact inv_le_one ha },
    { rw mem_smul_set, use [x, hx] }},
  { rw â†norm_pos_iff, calc 0 < 1 : zero_lt_one ... â‰¤ âˆ¥aâˆ¥ : ha, }
end

lemma balanced.univ : balanced ğ•œ (univ : set E) :=
Î» a ha, subset_univ _

lemma balanced.union {Aâ‚ Aâ‚‚ : set E} (hAâ‚ : balanced ğ•œ Aâ‚) (hAâ‚‚ : balanced ğ•œ Aâ‚‚) :
  balanced ğ•œ (Aâ‚ âˆª Aâ‚‚) :=
begin
  intros a ha t ht,
  rw [smul_set_union] at ht,
  exact ht.imp (Î» x, hAâ‚ _ ha x) (Î» x, hAâ‚‚ _ ha x),
end

lemma balanced.inter {Aâ‚ Aâ‚‚ : set E} (hAâ‚ : balanced ğ•œ Aâ‚) (hAâ‚‚ : balanced ğ•œ Aâ‚‚) :
  balanced ğ•œ (Aâ‚ âˆ© Aâ‚‚) :=
begin
  rintro a ha _ âŸ¨x, âŸ¨hxâ‚, hxâ‚‚âŸ©, rflâŸ©,
  exact âŸ¨hAâ‚ _ ha âŸ¨_, hxâ‚, rflâŸ©, hAâ‚‚ _ ha âŸ¨_, hxâ‚‚, rflâŸ©âŸ©,
end

lemma balanced.add {Aâ‚ Aâ‚‚ : set E} (hAâ‚ : balanced ğ•œ Aâ‚) (hAâ‚‚ : balanced ğ•œ Aâ‚‚) :
  balanced ğ•œ (Aâ‚ + Aâ‚‚) :=
begin
  rintro a ha _ âŸ¨_, âŸ¨x, y, hx, hy, rflâŸ©, rflâŸ©,
  rw smul_add,
  exact âŸ¨_, _, hAâ‚ _ ha âŸ¨_, hx, rflâŸ©, hAâ‚‚ _ ha âŸ¨_, hy, rflâŸ©, rflâŸ©,
end

lemma balanced.smul (hA : balanced ğ•œ A) : balanced ğ•œ (a â€¢ A) :=
begin
  rintro b hb _ âŸ¨_, âŸ¨x, hx, rflâŸ©, rflâŸ©,
  exact âŸ¨b â€¢ x, hA _ hb âŸ¨_, hx, rflâŸ©, smul_comm _ _ _âŸ©,
end

lemma absorbent_iff_forall_absorbs_singleton :
  absorbent ğ•œ A â†” âˆ€ x, absorbs ğ•œ A {x} :=
by simp [absorbs, absorbent]

/-!
Properties of balanced and absorbing sets in a topological vector space:
-/
variables [topological_space E] [has_continuous_smul ğ•œ E]

/-- Every neighbourhood of the origin is absorbent. -/
lemma absorbent_nhds_zero (hA : A âˆˆ ğ“ (0 : E)) : absorbent ğ•œ A :=
begin
  intro x,
  rcases mem_nhds_iff.mp hA with âŸ¨w, hwâ‚, hwâ‚‚, hwâ‚ƒâŸ©,
  have hc : continuous (Î» t : ğ•œ, t â€¢ x), from continuous_id.smul continuous_const,
  rcases metric.is_open_iff.mp (hwâ‚‚.preimage hc) 0 (by rwa [mem_preimage, zero_smul])
    with âŸ¨r, hrâ‚, hrâ‚‚âŸ©,
  have hrâ‚ƒ, from inv_pos.mpr (half_pos hrâ‚),
  use [(r/2)â»Â¹, hrâ‚ƒ],
  intros a haâ‚,
  have haâ‚‚ : 0 < âˆ¥aâˆ¥ := hrâ‚ƒ.trans_le haâ‚,
  have haâ‚ƒ : a â»Â¹ â€¢ x âˆˆ w,
  { apply hrâ‚‚,
    rw [metric.mem_ball, dist_zero_right, norm_inv],
    calc âˆ¥aâˆ¥â»Â¹ â‰¤ r/2 : (inv_le (half_pos hrâ‚) haâ‚‚).mp haâ‚
    ...       < r : half_lt_self hrâ‚ },
  rw [mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.mp haâ‚‚)],
  exact hwâ‚ haâ‚ƒ,
end

/-- The union of `{0}` with the interior of a balanced set
    is balanced. -/
lemma balanced_zero_union_interior (hA : balanced ğ•œ A) :
  balanced ğ•œ ({(0 : E)} âˆª interior A) :=
begin
  intros a ha, by_cases a = 0,
  { rw [h, zero_smul_set],
    exacts [subset_union_left _ _, âŸ¨0, or.inl rflâŸ©] },
  { rw [â†image_smul, image_union],
    apply union_subset_union,
    { rw [image_singleton, smul_zero] },
    { calc a â€¢ interior A âŠ† interior (a â€¢ A) : (is_open_map_smulâ‚€ h).image_interior_subset A
                      ... âŠ† interior A       : interior_mono (hA _ ha) } }
end

/-- The interior of a balanced set is balanced if it contains the origin. -/
lemma balanced.interior (hA : balanced ğ•œ A) (h : (0 : E) âˆˆ interior A) :
  balanced ğ•œ (interior A) :=
begin
  rw â†singleton_subset_iff at h,
  rw [â†union_eq_self_of_subset_left h],
  exact balanced_zero_union_interior hA,
end

/-- The closure of a balanced set is balanced. -/
lemma balanced.closure (hA : balanced ğ•œ A) : balanced ğ•œ (closure A) :=
assume a ha,
calc _ âŠ† closure (a â€¢ A) : image_closure_subset_closure_image (continuous_id.const_smul _)
...    âŠ† _ : closure_mono (hA _ ha)

end

/-!
### Seminorms
-/

/-- A seminorm on a vector space over a normed field is a function to
the reals that is positive semidefinite, positive homogeneous, and
subadditive. -/
structure seminorm (ğ•œ : Type*) (E : Type*)
  [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] :=
(to_fun    : E â†’ â„)
(smul'     : âˆ€ (a : ğ•œ) (x : E), to_fun (a â€¢ x) = âˆ¥aâˆ¥ * to_fun x)
(triangle' : âˆ€ x y : E, to_fun (x + y) â‰¤ to_fun x + to_fun y)

variables
{ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [add_comm_group E] [module ğ•œ E]

instance : inhabited (seminorm ğ•œ E) :=
âŸ¨{ to_fun     := Î» _, 0,
   smul'     := Î» _ _, (mul_zero _).symm,
   triangle' := Î» x y, by rw add_zero }âŸ©

instance : has_coe_to_fun (seminorm ğ•œ E) (Î» _, E â†’ â„) := âŸ¨Î» p, p.to_funâŸ©

namespace seminorm

variables (p : seminorm ğ•œ E) (c : ğ•œ) (x y : E) (r : â„)

protected lemma smul : p (c â€¢ x) = âˆ¥câˆ¥ * p x := p.smul' _ _
protected lemma triangle : p (x + y) â‰¤ p x + p y := p.triangle' _ _

@[simp]
protected lemma zero : p 0 = 0 :=
calc p 0 = p ((0 : ğ•œ) â€¢ 0) : by rw zero_smul
...      = 0 : by rw [p.smul, norm_zero, zero_mul]

@[simp]
protected lemma neg : p (-x) = p x :=
calc p (-x) = p ((-1 : ğ•œ) â€¢ x) : by rw neg_one_smul
...         = p x : by rw [p.smul, norm_neg, norm_one, one_mul]

lemma nonneg : 0 â‰¤ p x :=
have h: 0 â‰¤ 2 * p x, from
calc 0 = p (x + (- x)) : by rw [add_neg_self, p.zero]
...    â‰¤ p x + p (-x)  : p.triangle _ _
...    = 2 * p x : by rw [p.neg, two_mul],
nonneg_of_mul_nonneg_left h zero_lt_two

lemma sub_rev : p (x - y) = p (y - x) :=
by rw [â†neg_sub, p.neg]

/-- The ball of radius `r` at `x` with respect to seminorm `p`
    is the set of elements `y` with `p (y - x) < `r`. -/
def ball (p : seminorm ğ•œ E) (x : E) (r : â„) := { y : E | p (y - x) < r }

lemma mem_ball : y âˆˆ ball p x r â†” p (y - x) < r :=
iff.rfl

lemma mem_ball_zero : y âˆˆ ball p 0 r â†” p y < r :=
by rw [mem_ball, sub_zero]

lemma ball_zero_eq : ball p 0 r = { y : E | p y < r } :=
set.ext $ Î» x,by { rw mem_ball_zero, exact iff.rfl }

/-- Seminorm-balls at the origin are balanced. -/
lemma balanced_ball_zero : balanced ğ•œ (ball p 0 r) :=
begin
  rintro a ha x âŸ¨y, hy, hxâŸ©,
  rw [mem_ball_zero, â†hx, p.smul],
  calc _ â‰¤ p y : mul_le_of_le_one_left (p.nonneg _) ha
  ...    < r   : by rwa mem_ball_zero at hy,
end

-- TODO: convexity and absorbent/balanced sets in vector spaces over â„

end seminorm

-- TODO: the minkowski functional, topology induced by family of
-- seminorms, local convexity.
