/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, YaÃ«l Dillies, Moritz Doll
-/
import data.real.pointwise
import analysis.convex.function
import analysis.locally_convex.basic
import analysis.normed.group.add_torsor

/-!
# Seminorms

This file defines seminorms.

A seminorm is a function to the reals which is positive-semidefinite, absolutely homogeneous, and
subadditive. They are closely related to convex sets and a topological vector space is locally
convex if and only if its topology is induced by a family of seminorms.

## Main declarations

For a module over a normed ring:
* `seminorm`: A function to the reals that is positive-semidefinite, absolutely homogeneous, and
  subadditive.
* `norm_seminorm ğ•œ E`: The norm on `E` as a seminorm.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

seminorm, locally convex, LCTVS
-/

set_option old_structure_cmd true

open normed_field set
open_locale big_operators nnreal pointwise topology

variables {R R' ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ ğ• E Eâ‚‚ Eâ‚ƒ F G Î¹ : Type*}

/-- A seminorm on a module over a normed ring is a function to the reals that is positive
semidefinite, positive homogeneous, and subadditive. -/
structure seminorm (ğ•œ : Type*) (E : Type*) [semi_normed_ring ğ•œ] [add_group E] [has_smul ğ•œ E]
  extends add_group_seminorm E :=
(smul' : âˆ€ (a : ğ•œ) (x : E), to_fun (a â€¢ x) = â€–aâ€– * to_fun x)

attribute [nolint doc_blame] seminorm.to_add_group_seminorm

/-- `seminorm_class F ğ•œ E` states that `F` is a type of seminorms on the `ğ•œ`-module E.

You should extend this class when you extend `seminorm`. -/
class seminorm_class (F : Type*) (ğ•œ E : out_param $ Type*) [semi_normed_ring ğ•œ] [add_group E]
  [has_smul ğ•œ E] extends add_group_seminorm_class F E â„ :=
(map_smul_eq_mul (f : F) (a : ğ•œ) (x : E) : f (a â€¢ x) = â€–aâ€– * f x)

export seminorm_class (map_smul_eq_mul)

-- `ğ•œ` is an `out_param`, so this is a false positive.
attribute [nolint dangerous_instance] seminorm_class.to_add_group_seminorm_class

section of

/-- Alternative constructor for a `seminorm` on an `add_comm_group E` that is a module over a
`semi_norm_ring ğ•œ`. -/
def seminorm.of [semi_normed_ring ğ•œ] [add_comm_group E] [module ğ•œ E] (f : E â†’ â„)
  (add_le : âˆ€ (x y : E), f (x + y) â‰¤ f x + f y)
  (smul : âˆ€ (a : ğ•œ) (x : E), f (a â€¢ x) = â€–aâ€– * f x) : seminorm ğ•œ E :=
{ to_fun    := f,
  map_zero' := by rw [â†zero_smul ğ•œ (0 : E), smul, norm_zero, zero_mul],
  add_le'   := add_le,
  smul'     := smul,
  neg'      := Î» x, by rw [â†neg_one_smul ğ•œ, smul, norm_neg, â† smul, one_smul] }

/-- Alternative constructor for a `seminorm` over a normed field `ğ•œ` that only assumes `f 0 = 0`
and an inequality for the scalar multiplication. -/
def seminorm.of_smul_le [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] (f : E â†’ â„)
  (map_zero : f 0 = 0) (add_le : âˆ€ x y, f (x + y) â‰¤ f x + f y)
  (smul_le : âˆ€ (r : ğ•œ) x, f (r â€¢ x) â‰¤ â€–râ€– * f x) : seminorm ğ•œ E :=
seminorm.of f add_le
  (Î» r x, begin
    refine le_antisymm (smul_le r x) _,
    by_cases r = 0,
    { simp [h, map_zero] },
    rw â†mul_le_mul_left (inv_pos.mpr (norm_pos_iff.mpr h)),
    rw inv_mul_cancel_leftâ‚€ (norm_ne_zero_iff.mpr h),
    specialize smul_le râ»Â¹ (r â€¢ x),
    rw norm_inv at smul_le,
    convert smul_le,
    simp [h],
  end)

end of

namespace seminorm

section semi_normed_ring
variables [semi_normed_ring ğ•œ]

section add_group
variables [add_group E]

section has_smul
variables [has_smul ğ•œ E]

instance seminorm_class : seminorm_class (seminorm ğ•œ E) ğ•œ E :=
{ coe := Î» f, f.to_fun,
  coe_injective' := Î» f g h, by cases f; cases g; congr',
  map_zero := Î» f, f.map_zero',
  map_add_le_add := Î» f, f.add_le',
  map_neg_eq_map := Î» f, f.neg',
  map_smul_eq_mul := Î» f, f.smul' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`. -/
instance : has_coe_to_fun (seminorm ğ•œ E) (Î» _, E â†’ â„) := fun_like.has_coe_to_fun

@[ext] lemma ext {p q : seminorm ğ•œ E} (h : âˆ€ x, (p : E â†’ â„) x = q x) : p = q := fun_like.ext p q h

instance : has_zero (seminorm ğ•œ E) :=
âŸ¨{ smul' := Î» _ _, (mul_zero _).symm,
  ..add_group_seminorm.has_zero.zero }âŸ©

@[simp] lemma coe_zero : â‡‘(0 : seminorm ğ•œ E) = 0 := rfl

@[simp] lemma zero_apply (x : E) : (0 : seminorm ğ•œ E) x = 0 := rfl

instance : inhabited (seminorm ğ•œ E) := âŸ¨0âŸ©

variables (p : seminorm ğ•œ E) (c : ğ•œ) (x y : E) (r : â„)

/-- Any action on `â„` which factors through `â„â‰¥0` applies to a seminorm. -/
instance [has_smul R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  has_smul R (seminorm ğ•œ E) :=
{ smul := Î» r p,
  { to_fun  := Î» x, r â€¢ p x,
    smul' := Î» _ _, begin
      simp only [â†smul_one_smul â„â‰¥0 r (_ : â„), nnreal.smul_def, smul_eq_mul],
      rw [map_smul_eq_mul, mul_left_comm],
    end,
    ..(r â€¢ p.to_add_group_seminorm) }}

instance [has_smul R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  [has_smul R' â„] [has_smul R' â„â‰¥0] [is_scalar_tower R' â„â‰¥0 â„]
  [has_smul R R'] [is_scalar_tower R R' â„] :
  is_scalar_tower R R' (seminorm ğ•œ E) :=
{ smul_assoc := Î» r a p, ext $ Î» x, smul_assoc r a (p x) }

lemma coe_smul [has_smul R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p : seminorm ğ•œ E) : â‡‘(r â€¢ p) = r â€¢ p := rfl

@[simp] lemma smul_apply [has_smul R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p : seminorm ğ•œ E) (x : E) : (r â€¢ p) x = r â€¢ p x := rfl

instance : has_add (seminorm ğ•œ E) :=
{ add := Î» p q,
  { to_fun    := Î» x, p x + q x,
    smul'     := Î» a x, by simp only [map_smul_eq_mul, map_smul_eq_mul, mul_add],
    ..(p.to_add_group_seminorm + q.to_add_group_seminorm) }}

lemma coe_add (p q : seminorm ğ•œ E) : â‡‘(p + q) = p + q := rfl

@[simp] lemma add_apply (p q : seminorm ğ•œ E) (x : E) : (p + q) x = p x + q x := rfl

instance : add_monoid (seminorm ğ•œ E) :=
fun_like.coe_injective.add_monoid _ rfl coe_add (Î» p n, coe_smul n p)

instance : ordered_cancel_add_comm_monoid (seminorm ğ•œ E) :=
fun_like.coe_injective.ordered_cancel_add_comm_monoid _ rfl coe_add (Î» p n, coe_smul n p)

instance [monoid R] [mul_action R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  mul_action R (seminorm ğ•œ E) :=
fun_like.coe_injective.mul_action _ coe_smul

variables (ğ•œ E)

/-- `coe_fn` as an `add_monoid_hom`. Helper definition for showing that `seminorm ğ•œ E` is
a module. -/
@[simps]
def coe_fn_add_monoid_hom : add_monoid_hom (seminorm ğ•œ E) (E â†’ â„) := âŸ¨coe_fn, coe_zero, coe_addâŸ©

lemma coe_fn_add_monoid_hom_injective : function.injective (coe_fn_add_monoid_hom ğ•œ E) :=
show @function.injective (seminorm ğ•œ E) (E â†’ â„) coe_fn, from fun_like.coe_injective

variables {ğ•œ E}

instance [monoid R] [distrib_mul_action R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  distrib_mul_action R (seminorm ğ•œ E) :=
(coe_fn_add_monoid_hom_injective ğ•œ E).distrib_mul_action _ coe_smul

instance [semiring R] [module R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  module R (seminorm ğ•œ E) :=
(coe_fn_add_monoid_hom_injective ğ•œ E).module R _ coe_smul

instance : has_sup (seminorm ğ•œ E) :=
{ sup := Î» p q,
  { to_fun  := p âŠ” q,
    smul' := Î» x v, (congr_arg2 max (map_smul_eq_mul p x v) (map_smul_eq_mul q x v)).trans $
      (mul_max_of_nonneg _ _ $ norm_nonneg x).symm,
    ..(p.to_add_group_seminorm âŠ” q.to_add_group_seminorm) } }

@[simp] lemma coe_sup (p q : seminorm ğ•œ E) : â‡‘(p âŠ” q) = p âŠ” q := rfl
lemma sup_apply (p q : seminorm ğ•œ E) (x : E) : (p âŠ” q) x = p x âŠ” q x := rfl

lemma smul_sup [has_smul R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p q : seminorm ğ•œ E) :
  r â€¢ (p âŠ” q) = r â€¢ p âŠ” r â€¢ q :=
have real.smul_max : âˆ€ x y : â„, r â€¢ max x y = max (r â€¢ x) (r â€¢ y),
from Î» x y, by simpa only [â†smul_eq_mul, â†nnreal.smul_def, smul_one_smul â„â‰¥0 r (_ : â„)]
                     using mul_max_of_nonneg x y (r â€¢ 1 : â„â‰¥0).prop,
ext $ Î» x, real.smul_max _ _

instance : partial_order (seminorm ğ•œ E) :=
  partial_order.lift _ fun_like.coe_injective

lemma le_def (p q : seminorm ğ•œ E) : p â‰¤ q â†” (p : E â†’ â„) â‰¤ q := iff.rfl
lemma lt_def (p q : seminorm ğ•œ E) : p < q â†” (p : E â†’ â„) < q := iff.rfl

instance : semilattice_sup (seminorm ğ•œ E) :=
function.injective.semilattice_sup _ fun_like.coe_injective coe_sup

end has_smul

end add_group

section module
variables [semi_normed_ring ğ•œâ‚‚] [semi_normed_ring ğ•œâ‚ƒ]
variables {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚]
variables {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} [ring_hom_isometric Ïƒâ‚‚â‚ƒ]
variables {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ} [ring_hom_isometric Ïƒâ‚â‚ƒ]
variables [add_comm_group E] [add_comm_group Eâ‚‚] [add_comm_group Eâ‚ƒ]
variables [add_comm_group F] [add_comm_group G]
variables [module ğ•œ E] [module ğ•œâ‚‚ Eâ‚‚] [module ğ•œâ‚ƒ Eâ‚ƒ] [module ğ•œ F] [module ğ•œ G]
variables [has_smul R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]

/-- Composition of a seminorm with a linear map is a seminorm. -/
def comp (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) : seminorm ğ•œ E :=
{ to_fun    := Î» x, p (f x),
  smul'     := Î» _ _, by rw [map_smulâ‚›â‚—, map_smul_eq_mul, ring_hom_isometric.is_iso],
  ..(p.to_add_group_seminorm.comp f.to_add_monoid_hom) }

lemma coe_comp (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) : â‡‘(p.comp f) = p âˆ˜ f := rfl

@[simp] lemma comp_apply (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) (x : E) :
  (p.comp f) x = p (f x) := rfl

@[simp] lemma comp_id (p : seminorm ğ•œ E) : p.comp linear_map.id = p :=
ext $ Î» _, rfl

@[simp] lemma comp_zero (p : seminorm ğ•œâ‚‚ Eâ‚‚) : p.comp (0 : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) = 0 :=
ext $ Î» _, map_zero p

@[simp] lemma zero_comp (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) : (0 : seminorm ğ•œâ‚‚ Eâ‚‚).comp f = 0 :=
ext $ Î» _, rfl

lemma comp_comp [ring_hom_comp_triple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ] (p : seminorm ğ•œâ‚ƒ Eâ‚ƒ)
  (g : Eâ‚‚ â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] Eâ‚ƒ) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) :
  p.comp (g.comp f) = (p.comp g).comp f :=
ext $ Î» _, rfl

lemma add_comp (p q : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) : (p + q).comp f = p.comp f + q.comp f :=
ext $ Î» _, rfl

lemma comp_add_le (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f g : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) :
  p.comp (f + g) â‰¤ p.comp f + p.comp g :=
Î» _, map_add_le_add p _ _

lemma smul_comp (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) (c : R) :
  (c â€¢ p).comp f = c â€¢ (p.comp f) :=
ext $ Î» _, rfl

lemma comp_mono {p q : seminorm ğ•œâ‚‚ Eâ‚‚} (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) (hp : p â‰¤ q) :
  p.comp f â‰¤ q.comp f := Î» _, hp _

/-- The composition as an `add_monoid_hom`. -/
@[simps] def pullback (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) : seminorm ğ•œâ‚‚ Eâ‚‚ â†’+ seminorm ğ•œ E :=
âŸ¨Î» p, p.comp f, zero_comp f, Î» p q, add_comp p q fâŸ©

instance : order_bot (seminorm ğ•œ E) := âŸ¨0, map_nonnegâŸ©

@[simp] lemma coe_bot : â‡‘(âŠ¥ : seminorm ğ•œ E) = 0 := rfl

lemma bot_eq_zero : (âŠ¥ : seminorm ğ•œ E) = 0 := rfl

lemma smul_le_smul {p q : seminorm ğ•œ E} {a b : â„â‰¥0} (hpq : p â‰¤ q) (hab : a â‰¤ b) :
  a â€¢ p â‰¤ b â€¢ q :=
begin
  simp_rw [le_def, pi.le_def, coe_smul],
  intros x,
  simp_rw [pi.smul_apply, nnreal.smul_def, smul_eq_mul],
  exact mul_le_mul hab (hpq x) (map_nonneg p x) (nnreal.coe_nonneg b),
end

lemma finset_sup_apply (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) :
  s.sup p x = â†‘(s.sup (Î» i, âŸ¨p i x, map_nonneg (p i) xâŸ©) : â„â‰¥0) :=
begin
  induction s using finset.cons_induction_on with a s ha ih,
  { rw [finset.sup_empty, finset.sup_empty, coe_bot, _root_.bot_eq_zero, pi.zero_apply,
        nonneg.coe_zero] },
  { rw [finset.sup_cons, finset.sup_cons, coe_sup, sup_eq_max, pi.sup_apply, sup_eq_max,
        nnreal.coe_max, subtype.coe_mk, ih] }
end

lemma finset_sup_le_sum (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) : s.sup p â‰¤ âˆ‘ i in s, p i :=
begin
  classical,
  refine finset.sup_le_iff.mpr _,
  intros i hi,
  rw [finset.sum_eq_sum_diff_singleton_add hi, le_add_iff_nonneg_left],
  exact bot_le,
end

lemma finset_sup_apply_le {p : Î¹ â†’ seminorm ğ•œ E} {s : finset Î¹} {x : E} {a : â„} (ha : 0 â‰¤ a)
  (h : âˆ€ i, i âˆˆ s â†’ p i x â‰¤ a) : s.sup p x â‰¤ a :=
begin
  lift a to â„â‰¥0 using ha,
  rw [finset_sup_apply, nnreal.coe_le_coe],
  exact finset.sup_le h,
end

lemma finset_sup_apply_lt {p : Î¹ â†’ seminorm ğ•œ E} {s : finset Î¹} {x : E} {a : â„} (ha : 0 < a)
  (h : âˆ€ i, i âˆˆ s â†’ p i x < a) : s.sup p x < a :=
begin
  lift a to â„â‰¥0 using ha.le,
  rw [finset_sup_apply, nnreal.coe_lt_coe, finset.sup_lt_iff],
  { exact h },
  { exact nnreal.coe_pos.mpr ha },
end

lemma norm_sub_map_le_sub (p : seminorm ğ•œ E) (x y : E) : â€–p x - p yâ€– â‰¤ p (x - y) :=
abs_sub_map_le_sub p x y

end module
end semi_normed_ring

section semi_normed_comm_ring
variables [semi_normed_ring ğ•œ] [semi_normed_comm_ring ğ•œâ‚‚]
variables {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚]
variables [add_comm_group E] [add_comm_group Eâ‚‚] [module ğ•œ E] [module ğ•œâ‚‚ Eâ‚‚]

lemma comp_smul (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) (c : ğ•œâ‚‚) :
  p.comp (c â€¢ f) = â€–câ€–â‚Š â€¢ p.comp f :=
ext $ Î» _, by rw [comp_apply, smul_apply, linear_map.smul_apply, map_smul_eq_mul, nnreal.smul_def,
  coe_nnnorm, smul_eq_mul, comp_apply]

lemma comp_smul_apply (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) (c : ğ•œâ‚‚) (x : E) :
  p.comp (c â€¢ f) x = â€–câ€– * p (f x) := map_smul_eq_mul p _ _

end semi_normed_comm_ring

section normed_field
variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] {p q : seminorm ğ•œ E} {x : E}

/-- Auxiliary lemma to show that the infimum of seminorms is well-defined. -/
lemma bdd_below_range_add : bdd_below (range $ Î» u, p u + q (x - u)) :=
âŸ¨0, by { rintro _ âŸ¨x, rflâŸ©, dsimp, positivity }âŸ©

noncomputable instance : has_inf (seminorm ğ•œ E) :=
{ inf := Î» p q,
  { to_fun  := Î» x, â¨… u : E, p u + q (x-u),
    smul' :=
    begin
      intros a x,
      obtain rfl | ha := eq_or_ne a 0,
      { rw [norm_zero, zero_mul, zero_smul],
        refine cinfi_eq_of_forall_ge_of_forall_gt_exists_lt (Î» i, by positivity)
          (Î» x hx, âŸ¨0, by rwa [map_zero, sub_zero, map_zero, add_zero]âŸ©) },
      simp_rw [real.mul_infi_of_nonneg (norm_nonneg a), mul_add, â†map_smul_eq_mul p,
        â†map_smul_eq_mul q, smul_sub],
      refine function.surjective.infi_congr ((â€¢) aâ»Â¹ : E â†’ E) (Î» u, âŸ¨a â€¢ u, inv_smul_smulâ‚€ ha uâŸ©)
        (Î» u, _),
      rw smul_inv_smulâ‚€ ha
    end,
    ..(p.to_add_group_seminorm âŠ“ q.to_add_group_seminorm) }}

@[simp] lemma inf_apply (p q : seminorm ğ•œ E) (x : E) : (p âŠ“ q) x = â¨… u : E, p u + q (x-u) := rfl

noncomputable instance : lattice (seminorm ğ•œ E) :=
{ inf := (âŠ“),
  inf_le_left := Î» p q x, cinfi_le_of_le bdd_below_range_add x $
    by simp only [sub_self, map_zero, add_zero],
  inf_le_right := Î» p q x, cinfi_le_of_le bdd_below_range_add 0 $
    by simp only [sub_self, map_zero, zero_add, sub_zero],
  le_inf := Î» a b c hab hac x,
    le_cinfi $ Î» u, (le_map_add_map_sub a _ _).trans $ add_le_add (hab _) (hac _),
  ..seminorm.semilattice_sup }

lemma smul_inf [has_smul R â„] [has_smul R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p q : seminorm ğ•œ E) :
  r â€¢ (p âŠ“ q) = r â€¢ p âŠ“ r â€¢ q :=
begin
  ext,
  simp_rw [smul_apply, inf_apply, smul_apply, â†smul_one_smul â„â‰¥0 r (_ : â„), nnreal.smul_def,
    smul_eq_mul, real.mul_infi_of_nonneg (subtype.prop _), mul_add],
end

section classical

open_locale classical

/-- We define the supremum of an arbitrary subset of `seminorm ğ•œ E` as follows:
* if `s` is `bdd_above` *as a set of functions `E â†’ â„`* (that is, if `s` is pointwise bounded
above), we take the pointwise supremum of all elements of `s`, and we prove that it is indeed a
seminorm.
* otherwise, we take the zero seminorm `âŠ¥`.

There are two things worth mentionning here:
* First, it is not trivial at first that `s` being bounded above *by a function* implies
being bounded above *as a seminorm*. We show this in `seminorm.bdd_above_iff` by using
that the `Sup s` as defined here is then a bounding seminorm for `s`. So it is important to make
the case disjunction on `bdd_above (coe_fn '' s : set (E â†’ â„))` and not `bdd_above s`.
* Since the pointwise `Sup` already gives `0` at points where a family of functions is
not bounded above, one could hope that just using the pointwise `Sup` would work here, without the
need for an additional case disjunction. As discussed on Zulip, this doesn't work because this can
give a function which does *not* satisfy the seminorm axioms (typically sub-additivity).
-/
noncomputable instance : has_Sup (seminorm ğ•œ E) :=
{ Sup := Î» s, if h : bdd_above (coe_fn '' s : set (E â†’ â„)) then
  { to_fun := â¨† p : s, ((p : seminorm ğ•œ E) : E â†’ â„),
    map_zero' :=
    begin
      rw [supr_apply, â† @real.csupr_const_zero s],
      congrm â¨† i, _,
      exact map_zero i.1
    end,
    add_le' := Î» x y,
    begin
      rcases h with âŸ¨q, hqâŸ©,
      obtain rfl | h := s.eq_empty_or_nonempty,
      { simp [real.csupr_empty] },
      haveI : nonempty â†¥s := h.coe_sort,
      simp only [supr_apply],
      refine csupr_le (Î» i, ((i : seminorm ğ•œ E).add_le' x y).trans $
        add_le_add (le_csupr âŸ¨q x, _âŸ© i) (le_csupr âŸ¨q y, _âŸ© i));
      rw [mem_upper_bounds, forall_range_iff];
      exact Î» j, hq (mem_image_of_mem _ j.2) _,
    end,
    neg' := Î» x,
    begin
      simp only [supr_apply],
      congrm â¨† i, _,
      exact i.1.neg' _
    end,
    smul' := Î» a x,
    begin
      simp only [supr_apply],
      rw [â† smul_eq_mul, real.smul_supr_of_nonneg (norm_nonneg a) (Î» i : s, (i : seminorm ğ•œ E) x)],
      congrm â¨† i, _,
      exact i.1.smul' a x
    end }
  else âŠ¥ }

protected lemma coe_Sup_eq' {s : set $ seminorm ğ•œ E} (hs : bdd_above (coe_fn '' s : set (E â†’ â„))) :
  coe_fn (Sup s) = â¨† p : s, p :=
congr_arg _ (dif_pos hs)

protected lemma bdd_above_iff {s : set $ seminorm ğ•œ E} :
  bdd_above s â†” bdd_above (coe_fn '' s : set (E â†’ â„)) :=
âŸ¨Î» âŸ¨q, hqâŸ©, âŸ¨q, ball_image_of_ball $ Î» p hp, hq hpâŸ©,
  Î» H, âŸ¨Sup s, Î» p hp x,
  begin
    rw [seminorm.coe_Sup_eq' H, supr_apply],
    rcases H with âŸ¨q, hqâŸ©,
    exact le_csupr âŸ¨q x, forall_range_iff.mpr $ Î» i : s, hq (mem_image_of_mem _ i.2) xâŸ© âŸ¨p, hpâŸ©
  end âŸ©âŸ©

protected lemma coe_Sup_eq {s : set $ seminorm ğ•œ E} (hs : bdd_above s) :
  coe_fn (Sup s) = â¨† p : s, p :=
seminorm.coe_Sup_eq' (seminorm.bdd_above_iff.mp hs)

protected lemma coe_supr_eq {Î¹ : Type*} {p : Î¹ â†’ seminorm ğ•œ E} (hp : bdd_above (range p)) :
  coe_fn (â¨† i, p i) = â¨† i, p i :=
by rw [â† Sup_range, seminorm.coe_Sup_eq hp]; exact supr_range' (coe_fn : seminorm ğ•œ E â†’ E â†’ â„) p

private lemma seminorm.is_lub_Sup (s : set (seminorm ğ•œ E)) (hsâ‚ : bdd_above s) (hsâ‚‚ : s.nonempty) :
  is_lub s (Sup s) :=
begin
  refine âŸ¨Î» p hp x, _, Î» p hp x, _âŸ©;
  haveI : nonempty â†¥s := hsâ‚‚.coe_sort;
  rw [seminorm.coe_Sup_eq hsâ‚, supr_apply],
  { rcases hsâ‚ with âŸ¨q, hqâŸ©,
    exact le_csupr âŸ¨q x, forall_range_iff.mpr $ Î» i : s, hq i.2 xâŸ© âŸ¨p, hpâŸ© },
  { exact csupr_le (Î» q, hp q.2 x) }
end

/-- `seminorm ğ•œ E` is a conditionally complete lattice.

Note that, while `inf`, `sup` and `Sup` have good definitional properties (corresponding to
`seminorm.has_inf`, `seminorm.has_sup` and `seminorm.has_Sup` respectively), `Inf s` is just
defined as the supremum of the lower bounds of `s`, which is not really useful in practice. If you
need to use `Inf` on seminorms, then you should probably provide a more workable definition first,
but this is unlikely to happen so we keep the "bad" definition for now. -/
noncomputable instance : conditionally_complete_lattice (seminorm ğ•œ E) :=
conditionally_complete_lattice_of_lattice_of_Sup (seminorm ğ•œ E) seminorm.is_lub_Sup

end classical

end normed_field

/-! ### Seminorm ball -/

section semi_normed_ring
variables [semi_normed_ring ğ•œ]

section add_comm_group
variables [add_comm_group E]

section has_smul
variables [has_smul ğ•œ E] (p : seminorm ğ•œ E)

/-- The ball of radius `r` at `x` with respect to seminorm `p` is the set of elements `y` with
`p (y - x) < r`. -/
def ball (x : E) (r : â„) := { y : E | p (y - x) < r }

/-- The closed ball of radius `r` at `x` with respect to seminorm `p` is the set of elements `y`
with `p (y - x) â‰¤ r`. -/
def closed_ball (x : E) (r : â„) := { y : E | p (y - x) â‰¤ r }

variables {x y : E} {r : â„}

@[simp] lemma mem_ball : y âˆˆ ball p x r â†” p (y - x) < r := iff.rfl
@[simp] lemma mem_closed_ball : y âˆˆ closed_ball p x r â†” p (y - x) â‰¤ r := iff.rfl

lemma mem_ball_self (hr : 0 < r) : x âˆˆ ball p x r := by simp [hr]
lemma mem_closed_ball_self (hr : 0 â‰¤ r) : x âˆˆ closed_ball p x r := by simp [hr]

lemma mem_ball_zero : y âˆˆ ball p 0 r â†” p y < r := by rw [mem_ball, sub_zero]
lemma mem_closed_ball_zero : y âˆˆ closed_ball p 0 r â†” p y â‰¤ r := by rw [mem_closed_ball, sub_zero]

lemma ball_zero_eq : ball p 0 r = { y : E | p y < r } := set.ext $ Î» x, p.mem_ball_zero
lemma closed_ball_zero_eq : closed_ball p 0 r = { y : E | p y â‰¤ r } :=
set.ext $ Î» x, p.mem_closed_ball_zero

lemma ball_subset_closed_ball (x r) : ball p x r âŠ† closed_ball p x r := Î» y (hy : _ < _), hy.le

lemma closed_ball_eq_bInter_ball (x r) : closed_ball p x r = â‹‚ Ï > r, ball p x Ï :=
by ext y; simp_rw [mem_closed_ball, mem_Interâ‚‚, mem_ball, â† forall_lt_iff_le']

@[simp] lemma ball_zero' (x : E) (hr : 0 < r) : ball (0 : seminorm ğ•œ E) x r = set.univ :=
begin
  rw [set.eq_univ_iff_forall, ball],
  simp [hr],
end

@[simp] lemma closed_ball_zero' (x : E) (hr : 0 < r) :
  closed_ball (0 : seminorm ğ•œ E) x r = set.univ :=
eq_univ_of_subset (ball_subset_closed_ball _ _ _) (ball_zero' x hr)

lemma ball_smul (p : seminorm ğ•œ E) {c : nnreal} (hc : 0 < c) (r : â„) (x : E) :
  (c â€¢ p).ball x r = p.ball x (r / c) :=
by { ext, rw [mem_ball, mem_ball, smul_apply, nnreal.smul_def, smul_eq_mul, mul_comm,
  lt_div_iff (nnreal.coe_pos.mpr hc)] }

lemma closed_ball_smul (p : seminorm ğ•œ E) {c : nnreal} (hc : 0 < c) (r : â„) (x : E) :
  (c â€¢ p).closed_ball x r = p.closed_ball x (r / c) :=
by { ext, rw [mem_closed_ball, mem_closed_ball, smul_apply, nnreal.smul_def, smul_eq_mul, mul_comm,
  le_div_iff (nnreal.coe_pos.mpr hc)] }

lemma ball_sup (p : seminorm ğ•œ E) (q : seminorm ğ•œ E) (e : E) (r : â„) :
  ball (p âŠ” q) e r = ball p e r âˆ© ball q e r :=
by simp_rw [ball, â†set.set_of_and, coe_sup, pi.sup_apply, sup_lt_iff]

lemma closed_ball_sup (p : seminorm ğ•œ E) (q : seminorm ğ•œ E) (e : E) (r : â„) :
  closed_ball (p âŠ” q) e r = closed_ball p e r âˆ© closed_ball q e r :=
by simp_rw [closed_ball, â†set.set_of_and, coe_sup, pi.sup_apply, sup_le_iff]

lemma ball_finset_sup' (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (H : s.nonempty) (e : E) (r : â„) :
  ball (s.sup' H p) e r = s.inf' H (Î» i, ball (p i) e r) :=
begin
  induction H using finset.nonempty.cons_induction with a a s ha hs ih,
  { classical, simp },
  { rw [finset.sup'_cons hs, finset.inf'_cons hs, ball_sup, inf_eq_inter, ih] },
end

lemma closed_ball_finset_sup' (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (H : s.nonempty) (e : E)
  (r : â„) : closed_ball (s.sup' H p) e r = s.inf' H (Î» i, closed_ball (p i) e r) :=
begin
  induction H using finset.nonempty.cons_induction with a a s ha hs ih,
  { classical, simp },
  { rw [finset.sup'_cons hs, finset.inf'_cons hs, closed_ball_sup, inf_eq_inter, ih] },
end

lemma ball_mono {p : seminorm ğ•œ E} {râ‚ râ‚‚ : â„} (h : râ‚ â‰¤ râ‚‚) : p.ball x râ‚ âŠ† p.ball x râ‚‚ :=
Î» _ (hx : _ < _), hx.trans_le h

lemma closed_ball_mono {p : seminorm ğ•œ E} {râ‚ râ‚‚ : â„} (h : râ‚ â‰¤ râ‚‚) :
  p.closed_ball x râ‚ âŠ† p.closed_ball x râ‚‚ :=
Î» _ (hx : _ â‰¤ _), hx.trans h

lemma ball_antitone {p q : seminorm ğ•œ E} (h : q â‰¤ p) : p.ball x r âŠ† q.ball x r :=
Î» _, (h _).trans_lt

lemma closed_ball_antitone {p q : seminorm ğ•œ E} (h : q â‰¤ p) :
  p.closed_ball x r âŠ† q.closed_ball x r :=
Î» _, (h _).trans

lemma ball_add_ball_subset (p : seminorm ğ•œ E) (râ‚ râ‚‚ : â„) (xâ‚ xâ‚‚ : E):
  p.ball (xâ‚ : E) râ‚ + p.ball (xâ‚‚ : E) râ‚‚ âŠ† p.ball (xâ‚ + xâ‚‚) (râ‚ + râ‚‚) :=
begin
  rintros x âŸ¨yâ‚, yâ‚‚, hyâ‚, hyâ‚‚, rflâŸ©,
  rw [mem_ball, add_sub_add_comm],
  exact (map_add_le_add p _ _).trans_lt (add_lt_add hyâ‚ hyâ‚‚),
end

lemma closed_ball_add_closed_ball_subset (p : seminorm ğ•œ E) (râ‚ râ‚‚ : â„) (xâ‚ xâ‚‚ : E) :
  p.closed_ball (xâ‚ : E) râ‚ + p.closed_ball (xâ‚‚ : E) râ‚‚ âŠ† p.closed_ball (xâ‚ + xâ‚‚) (râ‚ + râ‚‚) :=
begin
  rintros x âŸ¨yâ‚, yâ‚‚, hyâ‚, hyâ‚‚, rflâŸ©,
  rw [mem_closed_ball, add_sub_add_comm],
  exact (map_add_le_add p _ _).trans (add_le_add hyâ‚ hyâ‚‚)
end

lemma sub_mem_ball (p : seminorm ğ•œ E) (xâ‚ xâ‚‚ y : E) (r : â„) :
  xâ‚ - xâ‚‚ âˆˆ p.ball y r â†” xâ‚ âˆˆ p.ball (xâ‚‚ + y) r :=
by simp_rw [mem_ball, sub_sub]

/-- The image of a ball under addition with a singleton is another ball. -/
lemma vadd_ball (p : seminorm ğ•œ E) :
  x +áµ¥ p.ball y r = p.ball (x +áµ¥ y) r :=
begin
  letI := add_group_seminorm.to_seminormed_add_comm_group p.to_add_group_seminorm,
  exact metric.vadd_ball x y r,
end

/-- The image of a closed ball under addition with a singleton is another closed ball. -/
lemma vadd_closed_ball (p : seminorm ğ•œ E) :
  x +áµ¥ p.closed_ball y r = p.closed_ball (x +áµ¥ y) r :=
begin
  letI := add_group_seminorm.to_seminormed_add_comm_group p.to_add_group_seminorm,
  exact metric.vadd_closed_ball x y r,
end

end has_smul

section module

variables [module ğ•œ E]
variables [semi_normed_ring ğ•œâ‚‚] [add_comm_group Eâ‚‚] [module ğ•œâ‚‚ Eâ‚‚]
variables {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚]

lemma ball_comp (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) (x : E) (r : â„) :
  (p.comp f).ball x r = f â»Â¹' (p.ball (f x) r) :=
begin
  ext,
  simp_rw [ball, mem_preimage, comp_apply, set.mem_set_of_eq, map_sub],
end

lemma closed_ball_comp (p : seminorm ğ•œâ‚‚ Eâ‚‚) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] Eâ‚‚) (x : E) (r : â„) :
  (p.comp f).closed_ball x r = f â»Â¹' (p.closed_ball (f x) r) :=
begin
  ext,
  simp_rw [closed_ball, mem_preimage, comp_apply, set.mem_set_of_eq, map_sub],
end

variables (p : seminorm ğ•œ E)

lemma preimage_metric_ball {r : â„} :
  p â»Â¹' (metric.ball 0 r) = {x | p x < r} :=
begin
  ext x,
  simp only [mem_set_of, mem_preimage, mem_ball_zero_iff, real.norm_of_nonneg (map_nonneg p _)]
end

lemma preimage_metric_closed_ball {r : â„} :
  p â»Â¹' (metric.closed_ball 0 r) = {x | p x â‰¤ r} :=
begin
  ext x,
  simp only [mem_set_of, mem_preimage, mem_closed_ball_zero_iff,
    real.norm_of_nonneg (map_nonneg p _)]
end

lemma ball_zero_eq_preimage_ball {r : â„} :
  p.ball 0 r = p â»Â¹' (metric.ball 0 r) :=
by rw [ball_zero_eq, preimage_metric_ball]

lemma closed_ball_zero_eq_preimage_closed_ball {r : â„} :
  p.closed_ball 0 r = p â»Â¹' (metric.closed_ball 0 r) :=
by rw [closed_ball_zero_eq, preimage_metric_closed_ball]

@[simp] lemma ball_bot {r : â„} (x : E) (hr : 0 < r) :
  ball (âŠ¥ : seminorm ğ•œ E) x r = set.univ :=
ball_zero' x hr

@[simp] lemma closed_ball_bot {r : â„} (x : E) (hr : 0 < r) :
  closed_ball (âŠ¥ : seminorm ğ•œ E) x r = set.univ :=
closed_ball_zero' x hr

/-- Seminorm-balls at the origin are balanced. -/
lemma balanced_ball_zero (r : â„) : balanced ğ•œ (ball p 0 r) :=
begin
  rintro a ha x âŸ¨y, hy, hxâŸ©,
  rw [mem_ball_zero, â†hx, map_smul_eq_mul],
  calc _ â‰¤ p y : mul_le_of_le_one_left (map_nonneg p _) ha
  ...    < r   : by rwa mem_ball_zero at hy,
end

/-- Closed seminorm-balls at the origin are balanced. -/
lemma balanced_closed_ball_zero (r : â„) : balanced ğ•œ (closed_ball p 0 r) :=
begin
  rintro a ha x âŸ¨y, hy, hxâŸ©,
  rw [mem_closed_ball_zero, â†hx, map_smul_eq_mul],
  calc _ â‰¤ p y : mul_le_of_le_one_left (map_nonneg p _) ha
  ...    â‰¤ r   : by rwa mem_closed_ball_zero at hy
end

lemma ball_finset_sup_eq_Inter (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) {r : â„} (hr : 0 < r) :
  ball (s.sup p) x r = â‹‚ (i âˆˆ s), ball (p i) x r :=
begin
  lift r to nnreal using hr.le,
  simp_rw [ball, Inter_set_of, finset_sup_apply, nnreal.coe_lt_coe,
    finset.sup_lt_iff (show âŠ¥ < r, from hr), â†nnreal.coe_lt_coe, subtype.coe_mk],
end

lemma closed_ball_finset_sup_eq_Inter (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) {r : â„}
  (hr : 0 â‰¤ r) : closed_ball (s.sup p) x r = â‹‚ (i âˆˆ s), closed_ball (p i) x r :=
begin
  lift r to nnreal using hr,
  simp_rw [closed_ball, Inter_set_of, finset_sup_apply, nnreal.coe_le_coe,
    finset.sup_le_iff, â†nnreal.coe_le_coe, subtype.coe_mk]
end

lemma ball_finset_sup (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) {r : â„} (hr : 0 < r) :
  ball (s.sup p) x r = s.inf (Î» i, ball (p i) x r) :=
begin
  rw finset.inf_eq_infi,
  exact ball_finset_sup_eq_Inter _ _ _ hr,
end

lemma closed_ball_finset_sup (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) {r : â„} (hr : 0 â‰¤ r) :
  closed_ball (s.sup p) x r = s.inf (Î» i, closed_ball (p i) x r) :=
begin
  rw finset.inf_eq_infi,
  exact closed_ball_finset_sup_eq_Inter _ _ _ hr,
end

lemma ball_smul_ball (p : seminorm ğ•œ E) (râ‚ râ‚‚ : â„) :
  metric.ball (0 : ğ•œ) râ‚ â€¢ p.ball 0 râ‚‚ âŠ† p.ball 0 (râ‚ * râ‚‚) :=
begin
  rw set.subset_def,
  intros x hx,
  rw set.mem_smul at hx,
  rcases hx with âŸ¨a, y, ha, hy, hxâŸ©,
  rw [â†hx, mem_ball_zero, map_smul_eq_mul],
  exact mul_lt_mul'' (mem_ball_zero_iff.mp ha) (p.mem_ball_zero.mp hy) (norm_nonneg a)
    (map_nonneg p y),
end

lemma closed_ball_smul_closed_ball (p : seminorm ğ•œ E) (râ‚ râ‚‚ : â„) :
  metric.closed_ball (0 : ğ•œ) râ‚ â€¢ p.closed_ball 0 râ‚‚ âŠ† p.closed_ball 0 (râ‚ * râ‚‚) :=
begin
  rw set.subset_def,
  intros x hx,
  rw set.mem_smul at hx,
  rcases hx with âŸ¨a, y, ha, hy, hxâŸ©,
  rw [â†hx, mem_closed_ball_zero, map_smul_eq_mul],
  rw mem_closed_ball_zero_iff at ha,
  exact mul_le_mul ha (p.mem_closed_ball_zero.mp hy) (map_nonneg _ y) ((norm_nonneg a).trans ha)
end

@[simp] lemma ball_eq_emptyset (p : seminorm ğ•œ E) {x : E} {r : â„} (hr : r â‰¤ 0) : p.ball x r = âˆ… :=
begin
  ext,
  rw [seminorm.mem_ball, set.mem_empty_iff_false, iff_false, not_lt],
  exact hr.trans (map_nonneg p _),
end

@[simp] lemma closed_ball_eq_emptyset (p : seminorm ğ•œ E) {x : E} {r : â„} (hr : r < 0) :
  p.closed_ball x r = âˆ… :=
begin
  ext,
  rw [seminorm.mem_closed_ball, set.mem_empty_iff_false, iff_false, not_le],
  exact hr.trans_le (map_nonneg _ _),
end

end module
end add_comm_group
end semi_normed_ring

section normed_field
variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] (p : seminorm ğ•œ E) {A B : set E}
  {a : ğ•œ} {r : â„} {x : E}

lemma ball_norm_mul_subset {p : seminorm ğ•œ E} {k : ğ•œ} {r : â„} :
  p.ball 0 (â€–kâ€– * r) âŠ† k â€¢ p.ball 0 r :=
begin
  rcases eq_or_ne k 0 with (rfl | hk),
  { rw [norm_zero, zero_mul, ball_eq_emptyset  _ le_rfl],
    exact empty_subset _ },
  { intro x,
    rw [set.mem_smul_set, seminorm.mem_ball_zero],
    refine Î» hx, âŸ¨kâ»Â¹ â€¢ x, _, _âŸ©,
    { rwa [seminorm.mem_ball_zero, map_smul_eq_mul, norm_inv,
      â†(mul_lt_mul_left $ norm_pos_iff.mpr hk), â†mul_assoc, â†(div_eq_mul_inv â€–kâ€– â€–kâ€–),
      div_self (ne_of_gt $ norm_pos_iff.mpr hk), one_mul] },
    rw [â†smul_assoc, smul_eq_mul, â†div_eq_mul_inv, div_self hk, one_smul] }
end

lemma smul_ball_zero {p : seminorm ğ•œ E} {k : ğ•œ} {r : â„} (hk : k â‰  0) :
  k â€¢ p.ball 0 r = p.ball 0 (â€–kâ€– * r) :=
begin
  ext,
  rw [mem_smul_set_iff_inv_smul_memâ‚€ hk, p.mem_ball_zero, p.mem_ball_zero, map_smul_eq_mul,
    norm_inv, â† div_eq_inv_mul, div_lt_iff (norm_pos_iff.2 hk), mul_comm]
end

lemma smul_closed_ball_subset {p : seminorm ğ•œ E} {k : ğ•œ} {r : â„} :
  k â€¢ p.closed_ball 0 r âŠ† p.closed_ball 0 (â€–kâ€– * r) :=
begin
  rintros x âŸ¨y, hy, hâŸ©,
  rw [seminorm.mem_closed_ball_zero, â†h, map_smul_eq_mul],
  rw seminorm.mem_closed_ball_zero at hy,
  exact mul_le_mul_of_nonneg_left hy (norm_nonneg _)
end

lemma smul_closed_ball_zero {p : seminorm ğ•œ E} {k : ğ•œ} {r : â„} (hk : 0 < â€–kâ€–) :
  k â€¢ p.closed_ball 0 r = p.closed_ball 0 (â€–kâ€– * r) :=
begin
  refine subset_antisymm smul_closed_ball_subset _,
  intro x,
  rw [set.mem_smul_set, seminorm.mem_closed_ball_zero],
  refine Î» hx, âŸ¨kâ»Â¹ â€¢ x, _, _âŸ©,
  { rwa [seminorm.mem_closed_ball_zero, map_smul_eq_mul, norm_inv, â†(mul_le_mul_left hk),
      â†mul_assoc, â†(div_eq_mul_inv â€–kâ€– â€–kâ€–), div_self (ne_of_gt hk), one_mul] },
  rw [â†smul_assoc, smul_eq_mul, â†div_eq_mul_inv, div_self (norm_pos_iff.mp hk), one_smul],
end

lemma ball_zero_absorbs_ball_zero (p : seminorm ğ•œ E) {râ‚ râ‚‚ : â„} (hrâ‚ : 0 < râ‚) :
  absorbs ğ•œ (p.ball 0 râ‚) (p.ball 0 râ‚‚) :=
begin
  rcases exists_pos_lt_mul hrâ‚ râ‚‚ with âŸ¨r, hrâ‚€, hrâŸ©,
  refine âŸ¨r, hrâ‚€, Î» a ha x hx, _âŸ©,
  rw [smul_ball_zero (norm_pos_iff.1 $ hrâ‚€.trans_le ha), p.mem_ball_zero],
  rw p.mem_ball_zero at hx,
  exact hx.trans (hr.trans_le $ mul_le_mul_of_nonneg_right ha hrâ‚.le)
end

/-- Seminorm-balls at the origin are absorbent. -/
protected lemma absorbent_ball_zero (hr : 0 < r) : absorbent ğ•œ (ball p (0 : E) r) :=
absorbent_iff_forall_absorbs_singleton.2 $ Î» x, (p.ball_zero_absorbs_ball_zero hr).mono_right $
  singleton_subset_iff.2 $ p.mem_ball_zero.2 $ lt_add_one _

/-- Closed seminorm-balls at the origin are absorbent. -/
protected lemma absorbent_closed_ball_zero (hr : 0 < r) : absorbent ğ•œ (closed_ball p (0 : E) r) :=
(p.absorbent_ball_zero hr).subset (p.ball_subset_closed_ball _ _)

/-- Seminorm-balls containing the origin are absorbent. -/
protected lemma absorbent_ball (hpr : p x < r) : absorbent ğ•œ (ball p x r) :=
begin
  refine (p.absorbent_ball_zero $ sub_pos.2 hpr).subset (Î» y hy, _),
  rw p.mem_ball_zero at hy,
  exact p.mem_ball.2 ((map_sub_le_add p _ _).trans_lt $ add_lt_of_lt_sub_right hy),
end

/-- Seminorm-balls containing the origin are absorbent. -/
protected lemma absorbent_closed_ball (hpr : p x < r) : absorbent ğ•œ (closed_ball p x r) :=
begin
  refine (p.absorbent_closed_ball_zero $ sub_pos.2 hpr).subset (Î» y hy, _),
  rw p.mem_closed_ball_zero at hy,
  exact p.mem_closed_ball.2 ((map_sub_le_add p _ _).trans $ add_le_of_le_sub_right hy),
end

lemma symmetric_ball_zero (r : â„) (hx : x âˆˆ ball p 0 r) : -x âˆˆ ball p 0 r :=
balanced_ball_zero p r (-1) (by rw [norm_neg, norm_one]) âŸ¨x, hx, by rw [neg_smul, one_smul]âŸ©

@[simp]
lemma neg_ball (p : seminorm ğ•œ E) (r : â„) (x : E) :
  -ball p x r = ball p (-x) r :=
by { ext, rw [mem_neg, mem_ball, mem_ball, â†neg_add', sub_neg_eq_add, map_neg_eq_map] }

@[simp]
lemma smul_ball_preimage (p : seminorm ğ•œ E) (y : E) (r : â„) (a : ğ•œ) (ha : a â‰  0) :
  ((â€¢) a) â»Â¹' p.ball y r = p.ball (aâ»Â¹ â€¢ y) (r / â€–aâ€–) :=
set.ext $ Î» _, by rw [mem_preimage, mem_ball, mem_ball,
  lt_div_iff (norm_pos_iff.mpr ha), mul_comm, â†map_smul_eq_mul p, smul_sub, smul_inv_smulâ‚€ ha]

end normed_field

section convex
variables [normed_field ğ•œ] [add_comm_group E] [normed_space â„ ğ•œ] [module ğ•œ E]

section has_smul
variables [has_smul â„ E] [is_scalar_tower â„ ğ•œ E] (p : seminorm ğ•œ E)

/-- A seminorm is convex. Also see `convex_on_norm`. -/
protected lemma convex_on : convex_on â„ univ p :=
begin
  refine âŸ¨convex_univ, Î» x _ y _ a b ha hb hab, _âŸ©,
  calc p (a â€¢ x + b â€¢ y) â‰¤ p (a â€¢ x) + p (b â€¢ y) : map_add_le_add p _ _
    ... = â€–a â€¢ (1 : ğ•œ)â€– * p x + â€–b â€¢ (1 : ğ•œ)â€– * p y
        : by rw [â†map_smul_eq_mul p, â†map_smul_eq_mul p, smul_one_smul, smul_one_smul]
    ... = a * p x + b * p y
        : by rw [norm_smul, norm_smul, norm_one, mul_one, mul_one, real.norm_of_nonneg ha,
            real.norm_of_nonneg hb],
end

end has_smul

section module
variables [module â„ E] [is_scalar_tower â„ ğ•œ E] (p : seminorm ğ•œ E) (x : E) (r : â„)

/-- Seminorm-balls are convex. -/
lemma convex_ball : convex â„ (ball p x r) :=
begin
  convert (p.convex_on.translate_left (-x)).convex_lt r,
  ext y,
  rw [preimage_univ, sep_univ, p.mem_ball, sub_eq_add_neg],
  refl,
end

/-- Closed seminorm-balls are convex. -/
lemma convex_closed_ball : convex â„ (closed_ball p x r) :=
begin
  rw closed_ball_eq_bInter_ball,
  exact convex_Interâ‚‚ (Î» _ _, convex_ball _ _ _)
end

end module
end convex

section restrict_scalars

variables (ğ•œ) {ğ•œ' : Type*} [normed_field ğ•œ] [semi_normed_ring ğ•œ'] [normed_algebra ğ•œ ğ•œ']
  [norm_one_class ğ•œ'] [add_comm_group E] [module ğ•œ' E] [has_smul ğ•œ E] [is_scalar_tower ğ•œ ğ•œ' E]

/-- Reinterpret a seminorm over a field `ğ•œ'` as a seminorm over a smaller field `ğ•œ`. This will
typically be used with `is_R_or_C ğ•œ'` and `ğ•œ = â„`. -/
protected def restrict_scalars (p : seminorm ğ•œ' E) :
  seminorm ğ•œ E :=
{ smul' := Î» a x, by rw [â† smul_one_smul ğ•œ' a x, p.smul', norm_smul, norm_one, mul_one],
  ..p }

@[simp] lemma coe_restrict_scalars (p : seminorm ğ•œ' E) :
  (p.restrict_scalars ğ•œ : E â†’ â„) = p :=
rfl

@[simp] lemma restrict_scalars_ball (p : seminorm ğ•œ' E) :
  (p.restrict_scalars ğ•œ).ball = p.ball :=
rfl

@[simp] lemma restrict_scalars_closed_ball (p : seminorm ğ•œ' E) :
  (p.restrict_scalars ğ•œ).closed_ball = p.closed_ball :=
rfl

end restrict_scalars

/-! ### Continuity criterions for seminorms -/

section continuity

variables [nontrivially_normed_field ğ•œ] [semi_normed_ring ğ•] [add_comm_group E] [module ğ•œ E]
variables [module ğ• E]

lemma continuous_at_zero' [topological_space E] [has_continuous_const_smul ğ•œ E] {p : seminorm ğ•œ E}
  {r : â„} (hr : 0 < r) (hp : p.closed_ball 0 r âˆˆ (ğ“ 0 : filter E)) :
  continuous_at p 0 :=
begin
  refine metric.nhds_basis_closed_ball.tendsto_right_iff.mpr _,
  intros Îµ hÎµ,
  rw map_zero,
  suffices : p.closed_ball 0 Îµ âˆˆ (ğ“ 0 : filter E),
  { rwa seminorm.closed_ball_zero_eq_preimage_closed_ball at this },
  rcases exists_norm_lt ğ•œ (div_pos hÎµ hr) with âŸ¨k, hk0, hkÎµâŸ©,
  have hk0' := norm_pos_iff.mp hk0,
  have := (set_smul_mem_nhds_zero_iff hk0').mpr hp,
  refine filter.mem_of_superset this (smul_set_subset_iff.mpr $ Î» x hx, _),
  rw [mem_closed_ball_zero, map_smul_eq_mul, â† div_mul_cancel Îµ hr.ne.symm],
  exact mul_le_mul hkÎµ.le (p.mem_closed_ball_zero.mp hx) (map_nonneg _ _) (div_nonneg hÎµ.le hr.le)
end

lemma continuous_at_zero [topological_space E] [has_continuous_const_smul ğ•œ E] {p : seminorm ğ•œ E}
  {r : â„} (hr : 0 < r) (hp : p.ball 0 r âˆˆ (ğ“ 0 : filter E)) :
  continuous_at p 0 :=
continuous_at_zero' hr (filter.mem_of_superset hp $ p.ball_subset_closed_ball _ _)

protected lemma uniform_continuous_of_continuous_at_zero [uniform_space E] [uniform_add_group E]
  {p : seminorm ğ• E} (hp : continuous_at p 0) :
  uniform_continuous p :=
begin
  have hp : filter.tendsto p (ğ“ 0) (ğ“ 0) := map_zero p â–¸ hp,
  rw [uniform_continuous, uniformity_eq_comap_nhds_zero_swapped,
      metric.uniformity_eq_comap_nhds_zero, filter.tendsto_comap_iff],
  exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds
    (hp.comp filter.tendsto_comap) (Î» xy, dist_nonneg) (Î» xy, p.norm_sub_map_le_sub _ _)
end

protected lemma continuous_of_continuous_at_zero [topological_space E] [topological_add_group E]
  {p : seminorm ğ• E} (hp : continuous_at p 0) :
  continuous p :=
begin
  letI := topological_add_group.to_uniform_space E,
  haveI : uniform_add_group E := topological_add_comm_group_is_uniform,
  exact (seminorm.uniform_continuous_of_continuous_at_zero hp).continuous
end

protected lemma uniform_continuous [uniform_space E] [uniform_add_group E]
  [has_continuous_const_smul ğ•œ E] {p : seminorm ğ•œ E} {r : â„} (hr : 0 < r)
  (hp : p.ball 0 r âˆˆ (ğ“ 0 : filter E)) : uniform_continuous p :=
seminorm.uniform_continuous_of_continuous_at_zero (continuous_at_zero hr hp)

protected lemma uniform_continuous' [uniform_space E] [uniform_add_group E]
  [has_continuous_const_smul ğ•œ E] {p : seminorm ğ•œ E} {r : â„} (hr : 0 < r)
  (hp : p.closed_ball 0 r âˆˆ (ğ“ 0 : filter E)) : uniform_continuous p :=
seminorm.uniform_continuous_of_continuous_at_zero (continuous_at_zero' hr hp)

protected lemma continuous [topological_space E] [topological_add_group E]
  [has_continuous_const_smul ğ•œ E] {p : seminorm ğ•œ E} {r : â„} (hr : 0 < r)
  (hp : p.ball 0 r âˆˆ (ğ“ 0 : filter E)) : continuous p :=
seminorm.continuous_of_continuous_at_zero (continuous_at_zero hr hp)

protected lemma continuous' [topological_space E] [topological_add_group E]
  [has_continuous_const_smul ğ•œ E] {p : seminorm ğ•œ E} {r : â„} (hr : 0 < r)
  (hp : p.closed_ball 0 r âˆˆ (ğ“ 0 : filter E)) : continuous p :=
seminorm.continuous_of_continuous_at_zero (continuous_at_zero' hr hp)

lemma continuous_of_le [topological_space E] [topological_add_group E]
  [has_continuous_const_smul ğ•œ E] {p q : seminorm ğ•œ E} (hq : continuous q) (hpq : p â‰¤ q) :
  continuous p :=
begin
  refine seminorm.continuous one_pos (filter.mem_of_superset
    (is_open.mem_nhds _ $ q.mem_ball_self zero_lt_one) (ball_antitone hpq)),
  rw ball_zero_eq,
  exact is_open_lt hq continuous_const
end

end continuity

end seminorm

/-! ### The norm as a seminorm -/

section norm_seminorm
variables (ğ•œ) (E) [normed_field ğ•œ] [seminormed_add_comm_group E] [normed_space ğ•œ E] {r : â„}

/-- The norm of a seminormed group as a seminorm. -/
def norm_seminorm : seminorm ğ•œ E :=
{ smul' := norm_smul,
  ..(norm_add_group_seminorm E)}

@[simp] lemma coe_norm_seminorm : â‡‘(norm_seminorm ğ•œ E) = norm := rfl

@[simp] lemma ball_norm_seminorm : (norm_seminorm ğ•œ E).ball = metric.ball :=
by { ext x r y, simp only [seminorm.mem_ball, metric.mem_ball, coe_norm_seminorm, dist_eq_norm] }

variables {ğ•œ E} {x : E}

/-- Balls at the origin are absorbent. -/
lemma absorbent_ball_zero (hr : 0 < r) : absorbent ğ•œ (metric.ball (0 : E) r) :=
by { rw â†ball_norm_seminorm ğ•œ, exact (norm_seminorm _ _).absorbent_ball_zero hr }

/-- Balls containing the origin are absorbent. -/
lemma absorbent_ball (hx : â€–xâ€– < r) : absorbent ğ•œ (metric.ball x r) :=
by { rw â†ball_norm_seminorm ğ•œ, exact (norm_seminorm _ _).absorbent_ball hx }

/-- Balls at the origin are balanced. -/
lemma balanced_ball_zero : balanced ğ•œ (metric.ball (0 : E) r) :=
by { rw â†ball_norm_seminorm ğ•œ, exact (norm_seminorm _ _).balanced_ball_zero r }

end norm_seminorm

-- Guard against import creep.
assert_not_exists balanced_core
