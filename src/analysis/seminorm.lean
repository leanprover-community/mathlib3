/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Bhavik Mehta, Ya√´l Dillies, Moritz Doll
-/
import analysis.convex.function
import analysis.normed_space.ordered
import data.real.pointwise
import data.real.sqrt
import topology.algebra.filter_basis
import topology.algebra.module.locally_convex

/-!
# Seminorms and Local Convexity

This file defines absorbent sets, balanced sets and seminorms.

An absorbent set is one that "surrounds" the origin. The idea is made precise by requiring that any
point belongs to all large enough scalings of the set. This is the vector world analog of a
topological neighborhood of the origin.

A balanced set is one that is everywhere around the origin. This means that `a ‚Ä¢ s ‚äÜ s` for all `a`
of norm less than `1`.

A seminorm is a function to the reals which is positive-semidefinite, absolutely homogeneous, and
subadditive. They are closely related to convex sets and a topological vector space is locally
convex if and only if its topology is induced by a family of seminorms.

## Main declarations

For a vector space over a normed field:
* `absorbent`: A set `s` is absorbent if every point eventually belongs to all large scalings of
  `s`.
* `balanced`: A set `s` is balanced if `a ‚Ä¢ s ‚äÜ s` for all `a` of norm less than `1`.
* `seminorm`: A function to the reals that is positive-semidefinite, absolutely homogeneous, and
  subadditive.
* `norm_seminorm ùïú E`: The norm on `E` as a seminorm.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## TODO

Define and show equivalence of two notions of local convexity for a
topological vector space over ‚Ñù or ‚ÑÇ: that it has a local base of
balanced convex absorbent sets, and that it carries the initial
topology induced by a family of seminorms.

Prove the properties of balanced and absorbent sets of a real vector space.

## Tags

absorbent, balanced, seminorm, locally convex, LCTVS
-/

open normed_field set
open_locale big_operators nnreal pointwise topological_space

variables {R R' ùïú ùïù E F G Œπ Œπ' : Type*}

/-!
### Set Properties

Absorbent and balanced sets in a vector space over a normed field.
-/

section semi_normed_ring
variables [semi_normed_ring ùïú]

section has_scalar
variables (ùïú) [has_scalar ùïú E]

/-- A set `A` absorbs another set `B` if `B` is contained in all scalings of
`A` by elements of sufficiently large norms. -/
def absorbs (A B : set E) := ‚àÉ r, 0 < r ‚àß ‚àÄ a : ùïú, r ‚â§ ‚à•a‚à• ‚Üí B ‚äÜ a ‚Ä¢ A

/-- A set is absorbent if it absorbs every singleton. -/
def absorbent (A : set E) := ‚àÄ x, ‚àÉ r, 0 < r ‚àß ‚àÄ a : ùïú, r ‚â§ ‚à•a‚à• ‚Üí x ‚àà a ‚Ä¢ A

/-- A set `A` is balanced if `a ‚Ä¢ A` is contained in `A` whenever `a`
has norm less than or equal to one. -/
def balanced (A : set E) := ‚àÄ a : ùïú, ‚à•a‚à• ‚â§ 1 ‚Üí a ‚Ä¢ A ‚äÜ A

variables {ùïú} {A B : set E}

lemma balanced_univ : balanced ùïú (univ : set E) := Œª a ha, subset_univ _

lemma balanced.union (hA : balanced ùïú A) (hB : balanced ùïú B) : balanced ùïú (A ‚à™ B) :=
begin
  intros a ha t ht,
  rw [smul_set_union] at ht,
  exact ht.imp (Œª x, hA _ ha x) (Œª x, hB _ ha x),
end

end has_scalar

section add_comm_group
variables [add_comm_group E] [module ùïú E] {s t u v A B : set E}

lemma balanced.inter (hA : balanced ùïú A) (hB : balanced ùïú B) : balanced ùïú (A ‚à© B) :=
begin
  rintro a ha _ ‚ü®x, ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©, rfl‚ü©,
  exact ‚ü®hA _ ha ‚ü®_, hx‚ÇÅ, rfl‚ü©, hB _ ha ‚ü®_, hx‚ÇÇ, rfl‚ü©‚ü©,
end

lemma balanced.add (hA‚ÇÅ : balanced ùïú A) (hA‚ÇÇ : balanced ùïú B) : balanced ùïú (A + B) :=
begin
  rintro a ha _ ‚ü®_, ‚ü®x, y, hx, hy, rfl‚ü©, rfl‚ü©,
  rw smul_add,
  exact ‚ü®_, _, hA‚ÇÅ _ ha ‚ü®_, hx, rfl‚ü©, hA‚ÇÇ _ ha ‚ü®_, hy, rfl‚ü©, rfl‚ü©,
end

lemma absorbs.mono (hs : absorbs ùïú s u) (hst : s ‚äÜ t) (hvu : v ‚äÜ u) : absorbs ùïú t v :=
let ‚ü®r, hr, h‚ü© := hs in ‚ü®r, hr, Œª a ha, hvu.trans $ (h _ ha).trans $ smul_set_mono hst‚ü©

lemma absorbs.mono_left (hs : absorbs ùïú s u) (h : s ‚äÜ t) : absorbs ùïú t u := hs.mono h subset.rfl
lemma absorbs.mono_right (hs : absorbs ùïú s u) (h : v ‚äÜ u) : absorbs ùïú s v := hs.mono subset.rfl h

lemma absorbs.union (hu : absorbs ùïú s u) (hv : absorbs ùïú s v) : absorbs ùïú s (u ‚à™ v) :=
begin
  obtain ‚ü®a, ha, hu‚ü© := hu,
  obtain ‚ü®b, hb, hv‚ü© := hv,
  exact ‚ü®max a b, lt_max_of_lt_left ha,
    Œª c hc, union_subset (hu _ $ le_of_max_le_left hc) (hv _ $ le_of_max_le_right hc)‚ü©,
end

@[simp] lemma absorbs_union : absorbs ùïú s (u ‚à™ v) ‚Üî absorbs ùïú s u ‚àß absorbs ùïú s v :=
‚ü®Œª h, ‚ü®h.mono_right $ subset_union_left _ _, h.mono_right $ subset_union_right _ _‚ü©,
  Œª h, h.1.union h.2‚ü©

lemma absorbent.subset (hA : absorbent ùïú A) (hAB : A ‚äÜ B) : absorbent ùïú B :=
begin
  rintro x,
  obtain ‚ü®r, hr, hx‚ü© := hA x,
  exact ‚ü®r, hr, Œª a ha, set.smul_set_mono hAB $ hx a ha‚ü©,
end

lemma absorbent_iff_forall_absorbs_singleton : absorbent ùïú A ‚Üî ‚àÄ x, absorbs ùïú A {x} :=
by simp_rw [absorbs, absorbent, singleton_subset_iff]

lemma absorbent.absorbs (hs : absorbent ùïú s) {x : E} : absorbs ùïú s {x} :=
absorbent_iff_forall_absorbs_singleton.1 hs _

lemma absorbent_iff_nonneg_lt : absorbent ùïú A ‚Üî ‚àÄ x, ‚àÉ r, 0 ‚â§ r ‚àß ‚àÄ a : ùïú, r < ‚à•a‚à• ‚Üí x ‚àà a ‚Ä¢ A :=
begin
  split,
  { rintro hA x,
    obtain ‚ü®r, hr, hx‚ü© := hA x,
    exact ‚ü®r, hr.le, Œª a ha, hx a ha.le‚ü© },
  { rintro hA x,
    obtain ‚ü®r, hr, hx‚ü© := hA x,
    exact ‚ü®r + 1, add_pos_of_nonneg_of_pos hr zero_lt_one,
      Œª a ha, hx a ((lt_add_of_pos_right r zero_lt_one).trans_le ha)‚ü© }
end

end add_comm_group
end semi_normed_ring

section normed_comm_ring
variables [normed_comm_ring ùïú] [add_comm_monoid E] [module ùïú E] {A B : set E} (a : ùïú)

lemma balanced.smul (hA : balanced ùïú A) : balanced ùïú (a ‚Ä¢ A) :=
begin
  rintro b hb _ ‚ü®_, ‚ü®x, hx, rfl‚ü©, rfl‚ü©,
  exact ‚ü®b ‚Ä¢ x, hA _ hb ‚ü®_, hx, rfl‚ü©, smul_comm _ _ _‚ü©,
end

end normed_comm_ring

section normed_field
variables [normed_field ùïú] [normed_ring ùïù] [normed_space ùïú ùïù] [add_comm_group E] [module ùïú E]
  [smul_with_zero ùïù E] [is_scalar_tower ùïú ùïù E] {s t u v A B : set E} {a b : ùïú}

/-- Scalar multiplication (by possibly different types) of a balanced set is monotone. -/
lemma balanced.smul_mono (hs : balanced ùïù s) {a : ùïù} {b : ùïú} (h : ‚à•a‚à• ‚â§ ‚à•b‚à•) : a ‚Ä¢ s ‚äÜ b ‚Ä¢ s :=
begin
  obtain rfl | hb := eq_or_ne b 0,
  { rw norm_zero at h,
    rw norm_eq_zero.1 (h.antisymm $ norm_nonneg _),
    obtain rfl | h := s.eq_empty_or_nonempty,
    { simp_rw [smul_set_empty] },
    { simp_rw [zero_smul_set h] } },
  rintro _ ‚ü®x, hx, rfl‚ü©,
  refine ‚ü®b‚Åª¬π ‚Ä¢ a ‚Ä¢ x, _, smul_inv_smul‚ÇÄ hb _‚ü©,
  rw ‚Üêsmul_assoc,
  refine hs _ _ (smul_mem_smul_set hx),
  rw [norm_smul, norm_inv, ‚Üêdiv_eq_inv_mul],
  exact div_le_one_of_le h (norm_nonneg _),
end

/-- A balanced set absorbs itself. -/
lemma balanced.absorbs_self (hA : balanced ùïú A) : absorbs ùïú A A :=
begin
  use [1, zero_lt_one],
  intros a ha x hx,
  rw mem_smul_set_iff_inv_smul_mem‚ÇÄ,
  { apply hA a‚Åª¬π,
    { rw norm_inv, exact inv_le_one ha },
    { rw mem_smul_set, use [x, hx] }},
  { rw ‚Üênorm_pos_iff, calc 0 < 1 : zero_lt_one ... ‚â§ ‚à•a‚à• : ha, }
end

lemma balanced.subset_smul (hA : balanced ùïú A) (ha : 1 ‚â§ ‚à•a‚à•) : A ‚äÜ a ‚Ä¢ A :=
begin
  refine (subset_set_smul_iff‚ÇÄ _).2 (hA (a‚Åª¬π) _),
  { rintro rfl,
    rw norm_zero at ha,
    exact zero_lt_one.not_le ha },
  { rw norm_inv,
    exact inv_le_one ha }
end

lemma balanced.smul_eq (hA : balanced ùïú A) (ha : ‚à•a‚à• = 1) : a ‚Ä¢ A = A :=
(hA _ ha.le).antisymm $ hA.subset_smul ha.ge

lemma absorbs.inter (hs : absorbs ùïú s u) (ht : absorbs ùïú t u) : absorbs ùïú (s ‚à© t) u :=
begin
  obtain ‚ü®a, ha, hs‚ü© := hs,
  obtain ‚ü®b, hb, ht‚ü© := ht,
  have h : 0 < max a b := lt_max_of_lt_left ha,
  refine ‚ü®max a b, lt_max_of_lt_left ha, Œª c hc, _‚ü©,
  rw smul_set_inter‚ÇÄ (norm_pos_iff.1 $ h.trans_le hc),
  exact subset_inter (hs _ $ le_of_max_le_left hc) (ht _ $ le_of_max_le_right hc),
end

@[simp] lemma absorbs_inter : absorbs ùïú (s ‚à© t) u ‚Üî absorbs ùïú s u ‚àß absorbs ùïú t u :=
‚ü®Œª h, ‚ü®h.mono_left $ inter_subset_left _ _, h.mono_left $ inter_subset_right _ _‚ü©,
  Œª h, h.1.inter h.2‚ü©

lemma absorbent_univ : absorbent ùïú (univ : set E) :=
begin
  refine Œª x, ‚ü®1, zero_lt_one, Œª a ha, _‚ü©,
  rw smul_set_univ‚ÇÄ (norm_pos_iff.1 $ zero_lt_one.trans_le ha),
  exact trivial,
end

/-! #### Topological vector space -/

variables [topological_space E] [has_continuous_smul ùïú E]

/-- Every neighbourhood of the origin is absorbent. -/
lemma absorbent_nhds_zero (hA : A ‚àà ùìù (0 : E)) : absorbent ùïú A :=
begin
  intro x,
  rcases mem_nhds_iff.mp hA with ‚ü®w, hw‚ÇÅ, hw‚ÇÇ, hw‚ÇÉ‚ü©,
  have hc : continuous (Œª t : ùïú, t ‚Ä¢ x), from continuous_id.smul continuous_const,
  rcases metric.is_open_iff.mp (hw‚ÇÇ.preimage hc) 0 (by rwa [mem_preimage, zero_smul])
    with ‚ü®r, hr‚ÇÅ, hr‚ÇÇ‚ü©,
  have hr‚ÇÉ, from inv_pos.mpr (half_pos hr‚ÇÅ),
  use [(r/2)‚Åª¬π, hr‚ÇÉ],
  intros a ha‚ÇÅ,
  have ha‚ÇÇ : 0 < ‚à•a‚à• := hr‚ÇÉ.trans_le ha‚ÇÅ,
  rw [mem_smul_set_iff_inv_smul_mem‚ÇÄ (norm_pos_iff.mp ha‚ÇÇ)],
  refine hw‚ÇÅ (hr‚ÇÇ _),
  rw [metric.mem_ball, dist_zero_right, norm_inv],
  calc ‚à•a‚à•‚Åª¬π ‚â§ r/2 : (inv_le (half_pos hr‚ÇÅ) ha‚ÇÇ).mp ha‚ÇÅ
  ...       < r : half_lt_self hr‚ÇÅ,
end

/-- The union of `{0}` with the interior of a balanced set is balanced. -/
lemma balanced_zero_union_interior (hA : balanced ùïú A) : balanced ùïú ((0 : set E) ‚à™ interior A) :=
begin
  intros a ha, by_cases a = 0,
  { rw [h, zero_smul_set],
    exacts [subset_union_left _ _, ‚ü®0, or.inl rfl‚ü©] },
  { rw [‚Üêimage_smul, image_union],
    apply union_subset_union,
    { rw [image_zero, smul_zero],
      refl },
    { calc a ‚Ä¢ interior A ‚äÜ interior (a ‚Ä¢ A) : (is_open_map_smul‚ÇÄ h).image_interior_subset A
                      ... ‚äÜ interior A       : interior_mono (hA _ ha) } }
end

/-- The interior of a balanced set is balanced if it contains the origin. -/
lemma balanced.interior (hA : balanced ùïú A) (h : (0 : E) ‚àà interior A) :
  balanced ùïú (interior A) :=
begin
  rw ‚Üêsingleton_subset_iff at h,
  rw [‚Üêunion_eq_self_of_subset_left h],
  exact balanced_zero_union_interior hA,
end

/-- The closure of a balanced set is balanced. -/
lemma balanced.closure (hA : balanced ùïú A) : balanced ùïú (closure A) :=
assume a ha,
calc _ ‚äÜ closure (a ‚Ä¢ A) : image_closure_subset_closure_image (continuous_id.const_smul _)
...    ‚äÜ _ : closure_mono (hA _ ha)

end normed_field

section nondiscrete_normed_field
variables [nondiscrete_normed_field ùïú] [add_comm_group E] [module ùïú E] {s : set E}

lemma absorbs_zero_iff : absorbs ùïú s 0 ‚Üî (0 : E) ‚àà s :=
begin
  refine ‚ü®_, Œª h, ‚ü®1, zero_lt_one, Œª a _, zero_subset.2 $ zero_mem_smul_set h‚ü©‚ü©,
  rintro ‚ü®r, hr, h‚ü©,
  obtain ‚ü®a, ha‚ü© := normed_space.exists_lt_norm ùïú ùïú r,
  have := h _ ha.le,
  rwa [zero_subset, zero_mem_smul_set_iff] at this,
  exact norm_ne_zero_iff.1 (hr.trans ha).ne',
end

lemma absorbent.zero_mem (hs : absorbent ùïú s) : (0 : E) ‚àà s :=
absorbs_zero_iff.1 $ absorbent_iff_forall_absorbs_singleton.1 hs _

end nondiscrete_normed_field

/-!
### Seminorms
-/

/-- A seminorm on a vector space over a normed field is a function to
the reals that is positive semidefinite, positive homogeneous, and
subadditive. -/
structure seminorm (ùïú : Type*) (E : Type*) [semi_normed_ring ùïú] [add_monoid E] [has_scalar ùïú E] :=
(to_fun    : E ‚Üí ‚Ñù)
(smul'     : ‚àÄ (a : ùïú) (x : E), to_fun (a ‚Ä¢ x) = ‚à•a‚à• * to_fun x)
(triangle' : ‚àÄ x y : E, to_fun (x + y) ‚â§ to_fun x + to_fun y)

namespace seminorm

section semi_normed_ring
variables [semi_normed_ring ùïú]

section add_monoid
variables [add_monoid E]

section has_scalar
variables [has_scalar ùïú E]

instance fun_like : fun_like (seminorm ùïú E) E (Œª _, ‚Ñù) :=
{ coe := seminorm.to_fun, coe_injective' := Œª f g h, by cases f; cases g; congr' }

/-- Helper instance for when there's too many metavariables to apply `to_fun.to_coe_fn`. -/
instance : has_coe_to_fun (seminorm ùïú E) (Œª _, E ‚Üí ‚Ñù) := ‚ü®Œª p, p.to_fun‚ü©

@[ext] lemma ext {p q : seminorm ùïú E} (h : ‚àÄ x, (p : E ‚Üí ‚Ñù) x = q x) : p = q := fun_like.ext p q h

instance : has_zero (seminorm ùïú E) :=
‚ü®{ to_fun    := 0,
  smul'     := Œª _ _, (mul_zero _).symm,
  triangle' := Œª _ _, eq.ge (zero_add _) }‚ü©

@[simp] lemma coe_zero : ‚áë(0 : seminorm ùïú E) = 0 := rfl

@[simp] lemma zero_apply (x : E) : (0 : seminorm ùïú E) x = 0 := rfl

instance : inhabited (seminorm ùïú E) := ‚ü®0‚ü©

variables (p : seminorm ùïú E) (c : ùïú) (x y : E) (r : ‚Ñù)

protected lemma smul : p (c ‚Ä¢ x) = ‚à•c‚à• * p x := p.smul' _ _
protected lemma triangle : p (x + y) ‚â§ p x + p y := p.triangle' _ _

/-- Any action on `‚Ñù` which factors through `‚Ñù‚â•0` applies to a seminorm. -/
instance [has_scalar R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  has_scalar R (seminorm ùïú E) :=
{ smul := Œª r p,
  { to_fun := Œª x, r ‚Ä¢ p x,
    smul' := Œª _ _, begin
      simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul],
      rw [p.smul, mul_left_comm],
    end,
    triangle' := Œª _ _, begin
      simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul],
      exact (mul_le_mul_of_nonneg_left (p.triangle _ _) (nnreal.coe_nonneg _)).trans_eq
        (mul_add _ _ _),
    end } }

instance [has_scalar R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  [has_scalar R' ‚Ñù] [has_scalar R' ‚Ñù‚â•0] [is_scalar_tower R' ‚Ñù‚â•0 ‚Ñù]
  [has_scalar R R'] [is_scalar_tower R R' ‚Ñù] :
  is_scalar_tower R R' (seminorm ùïú E) :=
{ smul_assoc := Œª r a p, ext $ Œª x, smul_assoc r a (p x) }

lemma coe_smul [has_scalar R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p : seminorm ùïú E) : ‚áë(r ‚Ä¢ p) = r ‚Ä¢ p := rfl

@[simp] lemma smul_apply [has_scalar R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p : seminorm ùïú E) (x : E) : (r ‚Ä¢ p) x = r ‚Ä¢ p x := rfl

instance : has_add (seminorm ùïú E) :=
{ add := Œª p q,
  { to_fun := Œª x, p x + q x,
    smul' := Œª a x, by rw [p.smul, q.smul, mul_add],
    triangle' := Œª _ _, has_le.le.trans_eq (add_le_add (p.triangle _ _) (q.triangle _ _))
      (add_add_add_comm _ _ _ _) } }

lemma coe_add (p q : seminorm ùïú E) : ‚áë(p + q) = p + q := rfl

@[simp] lemma add_apply (p q : seminorm ùïú E) (x : E) : (p + q) x = p x + q x := rfl

instance : add_monoid (seminorm ùïú E) :=
fun_like.coe_injective.add_monoid_smul _ rfl coe_add (Œª p n, coe_smul n p)

instance : ordered_cancel_add_comm_monoid (seminorm ùïú E) :=
{ nsmul := (‚Ä¢),  -- to avoid introducing a diamond
  ..seminorm.add_monoid,
  ..(fun_like.coe_injective.ordered_cancel_add_comm_monoid _ rfl coe_add
      : ordered_cancel_add_comm_monoid (seminorm ùïú E)) }

instance [monoid R] [mul_action R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  mul_action R (seminorm ùïú E) :=
fun_like.coe_injective.mul_action _ coe_smul

variables (ùïú E)

/-- `coe_fn` as an `add_monoid_hom`. Helper definition for showing that `seminorm ùïú E` is
a module. -/
@[simps]
def coe_fn_add_monoid_hom : add_monoid_hom (seminorm ùïú E) (E ‚Üí ‚Ñù) := ‚ü®coe_fn, coe_zero, coe_add‚ü©

lemma coe_fn_add_monoid_hom_injective : function.injective (coe_fn_add_monoid_hom ùïú E) :=
show @function.injective (seminorm ùïú E) (E ‚Üí ‚Ñù) coe_fn, from fun_like.coe_injective

variables {ùïú E}

instance [monoid R] [distrib_mul_action R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  distrib_mul_action R (seminorm ùïú E) :=
(coe_fn_add_monoid_hom_injective ùïú E).distrib_mul_action _ coe_smul

instance [semiring R] [module R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  module R (seminorm ùïú E) :=
(coe_fn_add_monoid_hom_injective ùïú E).module R _ coe_smul

-- TODO: define `has_Sup` too, from the skeleton at
-- https://github.com/leanprover-community/mathlib/pull/11329#issuecomment-1008915345
noncomputable instance : has_sup (seminorm ùïú E) :=
{ sup := Œª p q,
  { to_fun := p ‚äî q,
    triangle' := Œª x y, sup_le
      ((p.triangle x y).trans $ add_le_add le_sup_left le_sup_left)
      ((q.triangle x y).trans $ add_le_add le_sup_right le_sup_right),
    smul' := Œª x v, (congr_arg2 max (p.smul x v) (q.smul x v)).trans $
      (mul_max_of_nonneg _ _ $ norm_nonneg x).symm } }

@[simp] lemma coe_sup (p q : seminorm ùïú E) : ‚áë(p ‚äî q) = p ‚äî q := rfl
lemma sup_apply (p q : seminorm ùïú E) (x : E) : (p ‚äî q) x = p x ‚äî q x := rfl

lemma smul_sup [has_scalar R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p q : seminorm ùïú E) :
  r ‚Ä¢ (p ‚äî q) = r ‚Ä¢ p ‚äî r ‚Ä¢ q :=
have real.smul_max : ‚àÄ x y : ‚Ñù, r ‚Ä¢ max x y = max (r ‚Ä¢ x) (r ‚Ä¢ y),
from Œª x y, by simpa only [‚Üêsmul_eq_mul, ‚Üênnreal.smul_def, smul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù)]
                     using mul_max_of_nonneg x y (r ‚Ä¢ 1 : ‚Ñù‚â•0).prop,
ext $ Œª x, real.smul_max _ _

instance : partial_order (seminorm ùïú E) :=
  partial_order.lift _ fun_like.coe_injective

lemma le_def (p q : seminorm ùïú E) : p ‚â§ q ‚Üî (p : E ‚Üí ‚Ñù) ‚â§ q := iff.rfl
lemma lt_def (p q : seminorm ùïú E) : p < q ‚Üî (p : E ‚Üí ‚Ñù) < q := iff.rfl

noncomputable instance : semilattice_sup (seminorm ùïú E) :=
function.injective.semilattice_sup _ fun_like.coe_injective coe_sup

end has_scalar

section smul_with_zero
variables [smul_with_zero ùïú E] (p : seminorm ùïú E)

@[simp]
protected lemma zero : p 0 = 0 :=
calc p 0 = p ((0 : ùïú) ‚Ä¢ 0) : by rw zero_smul
...      = 0 : by rw [p.smul, norm_zero, zero_mul]

end smul_with_zero
end add_monoid

section module
variables [add_comm_group E] [add_comm_group F] [add_comm_group G]
variables [module ùïú E] [module ùïú F] [module ùïú G]
variables [has_scalar R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]

/-- Composition of a seminorm with a linear map is a seminorm. -/
def comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) : seminorm ùïú E :=
{ to_fun := Œª x, p(f x),
  smul' := Œª _ _, (congr_arg p (f.map_smul _ _)).trans (p.smul _ _),
  triangle' := Œª _ _, eq.trans_le (congr_arg p (f.map_add _ _)) (p.triangle _ _) }

lemma coe_comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) : ‚áë(p.comp f) = p ‚àò f := rfl

@[simp] lemma comp_apply (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (x : E) : (p.comp f) x = p (f x) := rfl

@[simp] lemma comp_id (p : seminorm ùïú E) : p.comp linear_map.id = p :=
ext $ Œª _, rfl

@[simp] lemma comp_zero (p : seminorm ùïú F) : p.comp (0 : E ‚Üí‚Çó[ùïú] F) = 0 :=
ext $ Œª _, seminorm.zero _

@[simp] lemma zero_comp (f : E ‚Üí‚Çó[ùïú] F) : (0 : seminorm ùïú F).comp f = 0 :=
ext $ Œª _, rfl

lemma comp_comp (p : seminorm ùïú G) (g : F ‚Üí‚Çó[ùïú] G) (f : E ‚Üí‚Çó[ùïú] F) :
  p.comp (g.comp f) = (p.comp g).comp f :=
ext $ Œª _, rfl

lemma add_comp (p q : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) : (p + q).comp f = p.comp f + q.comp f :=
ext $ Œª _, rfl

lemma comp_triangle (p : seminorm ùïú F) (f g : E ‚Üí‚Çó[ùïú] F) : p.comp (f + g) ‚â§ p.comp f + p.comp g :=
Œª _, p.triangle _ _

lemma smul_comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (c : R) : (c ‚Ä¢ p).comp f = c ‚Ä¢ (p.comp f) :=
ext $ Œª _, rfl

lemma comp_mono {p : seminorm ùïú F} {q : seminorm ùïú F} (f : E ‚Üí‚Çó[ùïú] F) (hp : p ‚â§ q) :
  p.comp f ‚â§ q.comp f := Œª _, hp _

/-- The composition as an `add_monoid_hom`. -/
@[simps] def pullback (f : E ‚Üí‚Çó[ùïú] F) : add_monoid_hom (seminorm ùïú F) (seminorm ùïú E) :=
‚ü®Œª p, p.comp f, zero_comp f, Œª p q, add_comp p q f‚ü©

section norm_one_class
variables [norm_one_class ùïú] (p : seminorm ùïú E) (x y : E) (r : ‚Ñù)

@[simp]
protected lemma neg : p (-x) = p x :=
calc p (-x) = p ((-1 : ùïú) ‚Ä¢ x) : by rw neg_one_smul
...         = p x : by rw [p.smul, norm_neg, norm_one, one_mul]

protected lemma sub_le : p (x - y) ‚â§ p x + p y :=
calc
  p (x - y)
      = p (x + -y) : by rw sub_eq_add_neg
  ... ‚â§ p x + p (-y) : p.triangle x (-y)
  ... = p x + p y : by rw p.neg

lemma nonneg : 0 ‚â§ p x :=
have h: 0 ‚â§ 2 * p x, from
calc 0 = p (x + (- x)) : by rw [add_neg_self, p.zero]
...    ‚â§ p x + p (-x)  : p.triangle _ _
...    = 2 * p x : by rw [p.neg, two_mul],
nonneg_of_mul_nonneg_left h zero_lt_two

lemma sub_rev : p (x - y) = p (y - x) := by rw [‚Üêneg_sub, p.neg]

/-- The direct path from 0 to y is shorter than the path with x "inserted" in between. -/
lemma le_insert : p y ‚â§ p x + p (x - y) :=
calc p y = p (x - (x - y)) : by rw sub_sub_cancel
... ‚â§ p x + p (x - y) : p.sub_le _ _

/-- The direct path from 0 to x is shorter than the path with y "inserted" in between. -/
lemma le_insert' : p x ‚â§ p y + p (x - y) := by { rw sub_rev, exact le_insert _ _ _ }

instance : order_bot (seminorm ùïú E) := ‚ü®0, nonneg‚ü©

@[simp] lemma coe_bot : ‚áë(‚ä• : seminorm ùïú E) = 0 := rfl

lemma bot_eq_zero : (‚ä• : seminorm ùïú E) = 0 := rfl

lemma smul_le_smul {p q : seminorm ùïú E} {a b : ‚Ñù‚â•0} (hpq : p ‚â§ q) (hab : a ‚â§ b) :
  a ‚Ä¢ p ‚â§ b ‚Ä¢ q :=
begin
  simp_rw [le_def, pi.le_def, coe_smul],
  intros x,
  simp_rw [pi.smul_apply, nnreal.smul_def, smul_eq_mul],
  exact mul_le_mul hab (hpq x) (nonneg p x) (nnreal.coe_nonneg b),
end

lemma finset_sup_apply (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (x : E) :
  s.sup p x = ‚Üë(s.sup (Œª i, ‚ü®p i x, nonneg (p i) x‚ü©) : ‚Ñù‚â•0) :=
begin
  induction s using finset.cons_induction_on with a s ha ih,
  { rw [finset.sup_empty, finset.sup_empty, coe_bot, _root_.bot_eq_zero, pi.zero_apply,
        nonneg.coe_zero] },
  { rw [finset.sup_cons, finset.sup_cons, coe_sup, sup_eq_max, pi.sup_apply, sup_eq_max,
        nnreal.coe_max, subtype.coe_mk, ih] }
end

lemma finset_sup_le_sum (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) : s.sup p ‚â§ ‚àë i in s, p i :=
begin
  classical,
  refine finset.sup_le_iff.mpr _,
  intros i hi,
  rw [finset.sum_eq_sum_diff_singleton_add hi, le_add_iff_nonneg_left],
  exact bot_le,
end

end norm_one_class
end module
end semi_normed_ring

section semi_normed_comm_ring
variables [semi_normed_comm_ring ùïú] [add_comm_group E] [add_comm_group F] [module ùïú E] [module ùïú F]

lemma comp_smul (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (c : ùïú) :
  p.comp (c ‚Ä¢ f) = ‚à•c‚à•‚Çä ‚Ä¢ p.comp f :=
ext $ Œª _, by rw [comp_apply, smul_apply, linear_map.smul_apply, p.smul, nnreal.smul_def,
  coe_nnnorm, smul_eq_mul, comp_apply]

lemma comp_smul_apply (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (c : ùïú) (x : E) :
  p.comp (c ‚Ä¢ f) x = ‚à•c‚à• * p (f x) := p.smul _ _

end semi_normed_comm_ring

section normed_field
variables [normed_field ùïú] [add_comm_group E] [module ùïú E]

private lemma bdd_below_range_add (x : E) (p q : seminorm ùïú E) :
  bdd_below (range (Œª (u : E), p u + q (x - u))) :=
by { use 0, rintro _ ‚ü®x, rfl‚ü©, exact add_nonneg (p.nonneg _) (q.nonneg _) }

noncomputable instance : has_inf (seminorm ùïú E) :=
{ inf := Œª p q,
  { to_fun := Œª x, ‚®Ö u : E, p u + q (x-u),
    triangle' := Œª x y, begin
      refine le_cinfi_add_cinfi (Œª u v, _),
      apply cinfi_le_of_le (bdd_below_range_add _ _ _) (v+u), dsimp only,
      convert add_le_add (p.triangle v u) (q.triangle (y-v) (x-u)) using 1,
      { rw show x + y - (v + u) = y - v + (x - u), by abel },
      { abel },
    end,
    smul' := Œª a x, begin
      obtain rfl | ha := eq_or_ne a 0,
      { simp_rw [norm_zero, zero_mul, zero_smul, zero_sub, seminorm.neg],
        refine cinfi_eq_of_forall_ge_of_forall_gt_exists_lt
          (Œª i, add_nonneg (p.nonneg _) (q.nonneg _))
          (Œª x hx, ‚ü®0, by rwa [p.zero, q.zero, add_zero]‚ü©) },
      simp_rw [real.mul_infi_of_nonneg (norm_nonneg a), mul_add, ‚Üêp.smul, ‚Üêq.smul, smul_sub],
      refine infi_congr ((‚Ä¢) a‚Åª¬π : E ‚Üí E) (Œª u, ‚ü®a ‚Ä¢ u, inv_smul_smul‚ÇÄ ha u‚ü©) (Œª u, _),
      rw smul_inv_smul‚ÇÄ ha,
    end } }

@[simp] lemma inf_apply (p q : seminorm ùïú E) (x : E) : (p ‚äì q) x = ‚®Ö u : E, p u + q (x-u) := rfl

noncomputable instance : lattice (seminorm ùïú E) :=
{ inf := (‚äì),
  inf_le_left := Œª p q x, begin
    apply cinfi_le_of_le (bdd_below_range_add _ _ _) x,
    simp only [sub_self, seminorm.zero, add_zero],
  end,
  inf_le_right := Œª p q x, begin
    apply cinfi_le_of_le (bdd_below_range_add _ _ _) (0:E),
    simp only [sub_self, seminorm.zero, zero_add, sub_zero],
  end,
  le_inf := Œª a b c hab hac x,
    le_cinfi $ Œª u, le_trans (a.le_insert' _ _) (add_le_add (hab _) (hac _)),
  ..seminorm.semilattice_sup }

lemma smul_inf [has_scalar R ‚Ñù] [has_scalar R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p q : seminorm ùïú E) :
  r ‚Ä¢ (p ‚äì q) = r ‚Ä¢ p ‚äì r ‚Ä¢ q :=
begin
  ext,
  simp_rw [smul_apply, inf_apply, smul_apply, ‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def,
    smul_eq_mul, real.mul_infi_of_nonneg (subtype.prop _), mul_add],
end

end normed_field

/-! ### Seminorm ball -/

section semi_normed_ring
variables [semi_normed_ring ùïú]

section add_comm_group
variables [add_comm_group E]

section has_scalar
variables [has_scalar ùïú E] (p : seminorm ùïú E)

/-- The ball of radius `r` at `x` with respect to seminorm `p` is the set of elements `y` with
`p (y - x) < `r`. -/
def ball (x : E) (r : ‚Ñù) := { y : E | p (y - x) < r }

variables {x y : E} {r : ‚Ñù}

@[simp] lemma mem_ball : y ‚àà ball p x r ‚Üî p (y - x) < r := iff.rfl

lemma mem_ball_zero : y ‚àà ball p 0 r ‚Üî p y < r := by rw [mem_ball, sub_zero]

lemma ball_zero_eq : ball p 0 r = { y : E | p y < r } := set.ext $ Œª x, p.mem_ball_zero

@[simp] lemma ball_zero' (x : E) (hr : 0 < r) : ball (0 : seminorm ùïú E) x r = set.univ :=
begin
  rw [set.eq_univ_iff_forall, ball],
  simp [hr],
end

lemma ball_smul (p : seminorm ùïú E) {c : nnreal} (hc : 0 < c) (r : ‚Ñù) (x : E) :
  (c ‚Ä¢ p).ball x r = p.ball x (r / c) :=
by { ext, rw [mem_ball, mem_ball, smul_apply, nnreal.smul_def, smul_eq_mul, mul_comm,
  lt_div_iff (nnreal.coe_pos.mpr hc)] }

lemma ball_sup (p : seminorm ùïú E) (q : seminorm ùïú E) (e : E) (r : ‚Ñù) :
  ball (p ‚äî q) e r = ball p e r ‚à© ball q e r :=
by simp_rw [ball, ‚Üêset.set_of_and, coe_sup, pi.sup_apply, sup_lt_iff]

lemma ball_finset_sup' (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (H : s.nonempty) (e : E) (r : ‚Ñù) :
  ball (s.sup' H p) e r = s.inf' H (Œª i, ball (p i) e r) :=
begin
  induction H using finset.nonempty.cons_induction with a a s ha hs ih,
  { classical, simp },
  { rw [finset.sup'_cons hs, finset.inf'_cons hs, ball_sup, inf_eq_inter, ih] },
end

lemma ball_mono {p : seminorm ùïú E} {r‚ÇÅ r‚ÇÇ : ‚Ñù} (h : r‚ÇÅ ‚â§ r‚ÇÇ) : p.ball x r‚ÇÅ ‚äÜ p.ball x r‚ÇÇ :=
Œª _ (hx : _ < _), hx.trans_le h

lemma ball_antitone {p q : seminorm ùïú E} (h : q ‚â§ p) : p.ball x r ‚äÜ q.ball x r :=
Œª _, (h _).trans_lt

lemma ball_add_ball_subset (p : seminorm ùïú E) (r‚ÇÅ r‚ÇÇ : ‚Ñù) (x‚ÇÅ x‚ÇÇ : E):
  p.ball (x‚ÇÅ : E) r‚ÇÅ + p.ball (x‚ÇÇ : E) r‚ÇÇ ‚äÜ p.ball (x‚ÇÅ + x‚ÇÇ) (r‚ÇÅ + r‚ÇÇ) :=
begin
  rintros x ‚ü®y‚ÇÅ, y‚ÇÇ, hy‚ÇÅ, hy‚ÇÇ, rfl‚ü©,
  rw [mem_ball, add_sub_comm],
  exact (p.triangle _ _).trans_lt (add_lt_add hy‚ÇÅ hy‚ÇÇ),
end

end has_scalar

section module

variables [module ùïú E]
variables [add_comm_group F] [module ùïú F]

lemma ball_comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (x : E) (r : ‚Ñù) :
  (p.comp f).ball x r = f ‚Åª¬π' (p.ball (f x) r) :=
begin
  ext,
  simp_rw [ball, mem_preimage, comp_apply, set.mem_set_of_eq, map_sub],
end

section norm_one_class
variables [norm_one_class ùïú] (p : seminorm ùïú E)

@[simp] lemma ball_bot {r : ‚Ñù} (x : E) (hr : 0 < r) : ball (‚ä• : seminorm ùïú E) x r = set.univ :=
ball_zero' x hr

/-- Seminorm-balls at the origin are balanced. -/
lemma balanced_ball_zero (r : ‚Ñù) : balanced ùïú (ball p 0 r) :=
begin
  rintro a ha x ‚ü®y, hy, hx‚ü©,
  rw [mem_ball_zero, ‚Üêhx, p.smul],
  calc _ ‚â§ p y : mul_le_of_le_one_left (p.nonneg _) ha
  ...    < r   : by rwa mem_ball_zero at hy,
end

lemma ball_finset_sup_eq_Inter (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (x : E) {r : ‚Ñù} (hr : 0 < r) :
  ball (s.sup p) x r = ‚ãÇ (i ‚àà s), ball (p i) x r :=
begin
  lift r to nnreal using hr.le,
  simp_rw [ball, Inter_set_of, finset_sup_apply, nnreal.coe_lt_coe,
    finset.sup_lt_iff (show ‚ä• < r, from hr), ‚Üênnreal.coe_lt_coe, subtype.coe_mk],
end

lemma ball_finset_sup (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (x : E) {r : ‚Ñù}
  (hr : 0 < r) : ball (s.sup p) x r = s.inf (Œª i, ball (p i) x r) :=
begin
  rw finset.inf_eq_infi,
  exact ball_finset_sup_eq_Inter _ _ _ hr,
end

lemma ball_smul_ball (p : seminorm ùïú E) (r‚ÇÅ r‚ÇÇ : ‚Ñù) :
  metric.ball (0 : ùïú) r‚ÇÅ ‚Ä¢ p.ball 0 r‚ÇÇ ‚äÜ p.ball 0 (r‚ÇÅ * r‚ÇÇ) :=
begin
  rw set.subset_def,
  intros x hx,
  rw set.mem_smul at hx,
  rcases hx with ‚ü®a, y, ha, hy, hx‚ü©,
  rw [‚Üêhx, mem_ball_zero, seminorm.smul],
  exact mul_lt_mul'' (mem_ball_zero_iff.mp ha) (p.mem_ball_zero.mp hy) (norm_nonneg a) (p.nonneg y),
end

end norm_one_class
end module
end add_comm_group
end semi_normed_ring

section normed_field
variables [normed_field ùïú] [add_comm_group E] [module ùïú E] (p : seminorm ùïú E) {A B : set E}
  {a : ùïú} {r : ‚Ñù} {x : E}

/-- Seminorm-balls at the origin are absorbent. -/
protected lemma absorbent_ball_zero (hr : 0 < r) : absorbent ùïú (ball p (0 : E) r) :=
begin
  rw absorbent_iff_nonneg_lt,
  rintro x,
  have hxr : 0 ‚â§ p x/r := div_nonneg (p.nonneg _) hr.le,
  refine ‚ü®p x/r, hxr, Œª a ha, _‚ü©,
  have ha‚ÇÄ : 0 < ‚à•a‚à• := hxr.trans_lt ha,
  refine ‚ü®a‚Åª¬π ‚Ä¢ x, _, smul_inv_smul‚ÇÄ (norm_pos_iff.1 ha‚ÇÄ) x‚ü©,
  rwa [mem_ball_zero, p.smul, norm_inv, inv_mul_lt_iff ha‚ÇÄ, ‚Üêdiv_lt_iff hr],
end

/-- Seminorm-balls containing the origin are absorbent. -/
protected lemma absorbent_ball (hpr : p x < r) : absorbent ùïú (ball p x r) :=
begin
  refine (p.absorbent_ball_zero $ sub_pos.2 hpr).subset (Œª y hy, _),
  rw p.mem_ball_zero at hy,
  exact p.mem_ball.2 ((p.sub_le _ _).trans_lt $ add_lt_of_lt_sub_right hy),
end

lemma symmetric_ball_zero (r : ‚Ñù) (hx : x ‚àà ball p 0 r) : -x ‚àà ball p 0 r :=
balanced_ball_zero p r (-1) (by rw [norm_neg, norm_one]) ‚ü®x, hx, by rw [neg_smul, one_smul]‚ü©

@[simp]
lemma neg_ball (p : seminorm ùïú E) (r : ‚Ñù) (x : E) :
  -ball p x r = ball p (-x) r :=
by { ext, rw [mem_neg, mem_ball, mem_ball, ‚Üêneg_add', sub_neg_eq_add, p.neg], }

@[simp]
lemma smul_ball_preimage (p : seminorm ùïú E) (y : E) (r : ‚Ñù) (a : ùïú) (ha : a ‚â† 0) :
  ((‚Ä¢) a) ‚Åª¬π' p.ball y r = p.ball (a‚Åª¬π ‚Ä¢ y) (r / ‚à•a‚à•) :=
set.ext $ Œª _, by rw [mem_preimage, mem_ball, mem_ball,
  lt_div_iff (norm_pos_iff.mpr ha), mul_comm, ‚Üêp.smul, smul_sub, smul_inv_smul‚ÇÄ ha]

end normed_field

section normed_linear_ordered_field
variables [normed_linear_ordered_field ùïú] [add_comm_group E] [normed_space ‚Ñù ùïú] [module ùïú E]

section has_scalar
variables [has_scalar ‚Ñù E] [is_scalar_tower ‚Ñù ùïú E] (p : seminorm ùïú E)

/-- A seminorm is convex. Also see `convex_on_norm`. -/
protected lemma convex_on : convex_on ‚Ñù univ p :=
begin
  refine ‚ü®convex_univ, Œª x y _ _ a b ha hb hab, _‚ü©,
  calc p (a ‚Ä¢ x + b ‚Ä¢ y) ‚â§ p (a ‚Ä¢ x) + p (b ‚Ä¢ y) : p.triangle _ _
    ... = ‚à•a ‚Ä¢ (1 : ùïú)‚à• * p x + ‚à•b ‚Ä¢ (1 : ùïú)‚à• * p y
        : by rw [‚Üêp.smul, ‚Üêp.smul, smul_one_smul, smul_one_smul]
    ... = a * p x + b * p y
        : by rw [norm_smul, norm_smul, norm_one, mul_one, mul_one, real.norm_of_nonneg ha,
            real.norm_of_nonneg hb],
end

end has_scalar

section module
variables [module ‚Ñù E] [is_scalar_tower ‚Ñù ùïú E] (p : seminorm ùïú E) (x : E) (r : ‚Ñù)

/-- Seminorm-balls are convex. -/
lemma convex_ball : convex ‚Ñù (ball p x r) :=
begin
  convert (p.convex_on.translate_left (-x)).convex_lt r,
  ext y,
  rw [preimage_univ, sep_univ, p.mem_ball, sub_eq_add_neg],
  refl,
end

end module
end normed_linear_ordered_field

-- TODO: convexity and absorbent/balanced sets in vector spaces over ‚Ñù

end seminorm

/-! ### The norm as a seminorm -/

section norm_seminorm
variables (ùïú E) [normed_field ùïú] [semi_normed_group E] [normed_space ùïú E] {r : ‚Ñù}

/-- The norm of a seminormed group as a seminorm. -/
def norm_seminorm : seminorm ùïú E := ‚ü®norm, norm_smul, norm_add_le‚ü©

@[simp] lemma coe_norm_seminorm : ‚áë(norm_seminorm ùïú E) = norm := rfl

@[simp] lemma ball_norm_seminorm : (norm_seminorm ùïú E).ball = metric.ball :=
by { ext x r y, simp only [seminorm.mem_ball, metric.mem_ball, coe_norm_seminorm, dist_eq_norm] }

variables {ùïú E} {x : E}

/-- Balls at the origin are absorbent. -/
lemma absorbent_ball_zero (hr : 0 < r) : absorbent ùïú (metric.ball (0 : E) r) :=
by { rw ‚Üêball_norm_seminorm ùïú, exact (norm_seminorm _ _).absorbent_ball_zero hr }

/-- Balls containing the origin are absorbent. -/
lemma absorbent_ball (hx : ‚à•x‚à• < r) : absorbent ùïú (metric.ball x r) :=
by { rw ‚Üêball_norm_seminorm ùïú, exact (norm_seminorm _ _).absorbent_ball hx }

/-- Balls at the origin are balanced. -/
lemma balanced_ball_zero [norm_one_class ùïú] : balanced ùïú (metric.ball (0 : E) r) :=
by { rw ‚Üêball_norm_seminorm ùïú, exact (norm_seminorm _ _).balanced_ball_zero r }

end norm_seminorm

/-! ### Topology induced by a family of seminorms -/

namespace seminorm

section filter_basis

variables [normed_field ùïú] [add_comm_group E] [module ùïú E]

/-- A filter basis for the neighborhood filter of 0. -/
def seminorm_basis_zero (p : Œπ ‚Üí seminorm ùïú E) : set (set E) :=
‚ãÉ (s : finset Œπ) r (hr : 0 < r), singleton $ ball (s.sup p) (0 : E) r

lemma seminorm_basis_zero_iff (p : Œπ ‚Üí seminorm ùïú E) (U : set E) :
  U ‚àà seminorm_basis_zero p ‚Üî ‚àÉ (i : finset Œπ) r (hr : 0 < r), U = ball (i.sup p) 0 r :=
by simp only [seminorm_basis_zero, mem_Union, mem_singleton_iff]

lemma seminorm_basis_zero_mem (p : Œπ ‚Üí seminorm ùïú E) (i : finset Œπ) {r : ‚Ñù} (hr : 0 < r) :
  (i.sup p).ball 0 r ‚àà seminorm_basis_zero p :=
(seminorm_basis_zero_iff _ _).mpr ‚ü®i,_,hr,rfl‚ü©

lemma seminorm_basis_zero_singleton_mem (p : Œπ ‚Üí seminorm ùïú E) (i : Œπ) {r : ‚Ñù} (hr : 0 < r) :
  (p i).ball 0 r ‚àà seminorm_basis_zero p :=
(seminorm_basis_zero_iff _ _).mpr ‚ü®{i},_,hr, by rw finset.sup_singleton‚ü©

lemma seminorm_basis_zero_nonempty (p : Œπ ‚Üí seminorm ùïú E) [nonempty Œπ] :
  (seminorm_basis_zero p).nonempty :=
begin
  let i := classical.arbitrary Œπ,
  refine set.nonempty_def.mpr ‚ü®ball (p i) 0 1, _‚ü©,
  exact seminorm_basis_zero_singleton_mem _ i zero_lt_one,
end

lemma seminorm_basis_zero_intersect (p : Œπ ‚Üí seminorm ùïú E)
  (U V : set E) (hU : U ‚àà seminorm_basis_zero p) (hV : V ‚àà seminorm_basis_zero p) :
  ‚àÉ (z : set E) (H : z ‚àà (seminorm_basis_zero p)), z ‚äÜ U ‚à© V :=
begin
  classical,
  rcases (seminorm_basis_zero_iff p U).mp hU with ‚ü®s, r‚ÇÅ, hr‚ÇÅ, hU‚ü©,
  rcases (seminorm_basis_zero_iff p V).mp hV with ‚ü®t, r‚ÇÇ, hr‚ÇÇ, hV‚ü©,
  use ((s ‚à™ t).sup p).ball 0 (min r‚ÇÅ r‚ÇÇ),
  refine ‚ü®seminorm_basis_zero_mem p (s ‚à™ t) (lt_min_iff.mpr ‚ü®hr‚ÇÅ, hr‚ÇÇ‚ü©), _‚ü©,
  rw [hU, hV, ball_finset_sup_eq_Inter _ _ _ (lt_min_iff.mpr ‚ü®hr‚ÇÅ, hr‚ÇÇ‚ü©),
    ball_finset_sup_eq_Inter _ _ _ hr‚ÇÅ, ball_finset_sup_eq_Inter _ _ _ hr‚ÇÇ],
  exact set.subset_inter
    (set.Inter‚ÇÇ_mono' $ Œª i hi, ‚ü®i, finset.subset_union_left _ _ hi, ball_mono $ min_le_left _ _‚ü©)
    (set.Inter‚ÇÇ_mono' $ Œª i hi, ‚ü®i, finset.subset_union_right _ _ hi, ball_mono $
    min_le_right _ _‚ü©),
end

lemma seminorm_basis_zero_zero (p : Œπ ‚Üí seminorm ùïú E) (U) (hU : U ‚àà seminorm_basis_zero p) :
  (0 : E) ‚àà U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with ‚ü®Œπ', r, hr, hU‚ü©,
  rw [hU, mem_ball_zero, (Œπ'.sup p).zero],
  exact hr,
end

lemma seminorm_basis_zero_add (p : Œπ ‚Üí seminorm ùïú E) (U) (hU : U ‚àà seminorm_basis_zero p) :
  ‚àÉ (V : set E) (H : V ‚àà (seminorm_basis_zero p)), V + V ‚äÜ U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with ‚ü®s, r, hr, hU‚ü©,
  use (s.sup p).ball 0 (r/2),
  refine ‚ü®seminorm_basis_zero_mem p s (div_pos hr zero_lt_two), _‚ü©,
  refine set.subset.trans (ball_add_ball_subset (s.sup p) (r/2) (r/2) 0 0) _,
  rw [hU, add_zero, add_halves'],
end

lemma seminorm_basis_zero_neg (p : Œπ ‚Üí seminorm ùïú E) (U) (hU' : U ‚àà seminorm_basis_zero p) :
  ‚àÉ (V : set E) (H : V ‚àà (seminorm_basis_zero p)), V ‚äÜ (Œª (x : E), -x) ‚Åª¬π' U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU' with ‚ü®s, r, hr, hU‚ü©,
  rw [hU, neg_preimage, neg_ball (s.sup p), neg_zero],
  exact ‚ü®U, hU', eq.subset hU‚ü©,
end

/-- The `add_group_filter_basis` induced by the filter basis `seminorm_basis_zero`. -/
def seminorm_add_group_filter_basis [nonempty Œπ]
  (p : Œπ ‚Üí seminorm ùïú E) : add_group_filter_basis E :=
add_group_filter_basis_of_comm (seminorm_basis_zero p)
  (seminorm_basis_zero_nonempty p)
  (seminorm_basis_zero_intersect p)
  (seminorm_basis_zero_zero p)
  (seminorm_basis_zero_add p)
  (seminorm_basis_zero_neg p)

lemma seminorm_basis_zero_smul_right (p : Œπ ‚Üí seminorm ùïú E) (v : E) (U : set E)
  (hU : U ‚àà seminorm_basis_zero p) : ‚àÄ·∂† (x : ùïú) in ùìù 0, x ‚Ä¢ v ‚àà U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with ‚ü®s, r, hr, hU‚ü©,
  rw [hU, filter.eventually_iff],
  simp_rw [(s.sup p).mem_ball_zero, (s.sup p).smul],
  by_cases h : 0 < (s.sup p) v,
  { simp_rw (lt_div_iff h).symm,
    rw ‚Üê_root_.ball_zero_eq,
    exact metric.ball_mem_nhds 0 (div_pos hr h) },
  simp_rw [le_antisymm (not_lt.mp h) ((s.sup p).nonneg v), mul_zero, hr],
  exact is_open.mem_nhds is_open_univ (mem_univ 0),
end

variables [nonempty Œπ]

lemma seminorm_basis_zero_smul (p : Œπ ‚Üí seminorm ùïú E) (U) (hU : U ‚àà seminorm_basis_zero p) :
  ‚àÉ (V : set ùïú) (H : V ‚àà ùìù (0 : ùïú)) (W : set E)
  (H : W ‚àà (seminorm_add_group_filter_basis p).sets), V ‚Ä¢ W ‚äÜ U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with ‚ü®s, r, hr, hU‚ü©,
  refine ‚ü®metric.ball 0 r.sqrt, metric.ball_mem_nhds 0 (real.sqrt_pos.mpr hr), _‚ü©,
  refine ‚ü®(s.sup p).ball 0 r.sqrt, seminorm_basis_zero_mem p s (real.sqrt_pos.mpr hr), _‚ü©,
  refine set.subset.trans (ball_smul_ball (s.sup p) r.sqrt r.sqrt) _,
  rw [hU, real.mul_self_sqrt (le_of_lt hr)],
end

lemma seminorm_basis_zero_smul_left (p : Œπ ‚Üí seminorm ùïú E) (x : ùïú) (U : set E)
  (hU : U ‚àà seminorm_basis_zero p) : ‚àÉ (V : set E)
  (H : V ‚àà (seminorm_add_group_filter_basis p).sets), V ‚äÜ (Œª (y : E), x ‚Ä¢ y) ‚Åª¬π' U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with ‚ü®s, r, hr, hU‚ü©,
  rw hU,
  by_cases h : x ‚â† 0,
  { rw [(s.sup p).smul_ball_preimage 0 r x h, smul_zero],
    use (s.sup p).ball 0 (r / ‚à•x‚à•),
    exact ‚ü®seminorm_basis_zero_mem p s (div_pos hr (norm_pos_iff.mpr h)), subset.rfl‚ü© },
  refine ‚ü®(s.sup p).ball 0 r, seminorm_basis_zero_mem p s hr, _‚ü©,
  simp only [not_ne_iff.mp h, subset_def, mem_ball_zero, hr, mem_univ, seminorm.zero,
    implies_true_iff, preimage_const_of_mem, zero_smul],
end

/-- The `module_filter_basis` induced by the filter basis `seminorm_basis_zero`. -/
def seminorm_module_filter_basis (p : Œπ ‚Üí seminorm ùïú E) : module_filter_basis ùïú E :=
{ to_add_group_filter_basis := seminorm_add_group_filter_basis p,
  smul' := seminorm_basis_zero_smul p,
  smul_left' := seminorm_basis_zero_smul_left p,
  smul_right' := seminorm_basis_zero_smul_right p }

end filter_basis

section bounded

variables [normed_field ùïú] [add_comm_group E] [module ùïú E] [add_comm_group F] [module ùïú F]

/-- The proposition that a linear map is bounded between spaces with families of seminorms. -/
def is_bounded (p : Œπ ‚Üí seminorm ùïú E) (q : Œπ' ‚Üí seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) : Prop :=
  ‚àÄ i, ‚àÉ s : finset Œπ, ‚àÉ C : ‚Ñù‚â•0, C ‚â† 0 ‚àß (q i).comp f ‚â§ C ‚Ä¢ s.sup p

lemma is_bounded_const (Œπ' : Type*) [nonempty Œπ']
  {p : Œπ ‚Üí seminorm ùïú E} {q : seminorm ùïú F} (f : E ‚Üí‚Çó[ùïú] F) :
  is_bounded p (Œª _ : Œπ', q) f ‚Üî ‚àÉ (s : finset Œπ) C : ‚Ñù‚â•0, C ‚â† 0 ‚àß q.comp f ‚â§ C ‚Ä¢ s.sup p :=
by simp only [is_bounded, forall_const]

lemma const_is_bounded (Œπ : Type*) [nonempty Œπ]
  {p : seminorm ùïú E} {q : Œπ' ‚Üí seminorm ùïú F} (f : E ‚Üí‚Çó[ùïú] F) :
  is_bounded (Œª _ : Œπ, p) q f ‚Üî ‚àÄ i, ‚àÉ C : ‚Ñù‚â•0, C ‚â† 0 ‚àß (q i).comp f ‚â§ C ‚Ä¢ p :=
begin
  dunfold is_bounded,
  split,
  { intros h i,
    rcases h i with ‚ü®s, C, hC, h‚ü©,
    exact ‚ü®C, hC, le_trans h (smul_le_smul (finset.sup_le (Œª _ _, le_rfl)) le_rfl)‚ü© },
  intros h i,
  use [{classical.arbitrary Œπ}],
  simp only [h, finset.sup_singleton],
end

lemma is_bounded_sup {p : Œπ ‚Üí seminorm ùïú E} {q : Œπ' ‚Üí seminorm ùïú F}
  {f : E ‚Üí‚Çó[ùïú] F} (hf : is_bounded p q f) (s' : finset Œπ') :
  ‚àÉ (C : ‚Ñù‚â•0) (s : finset Œπ), 0 < C ‚àß (s'.sup q).comp f ‚â§ C ‚Ä¢ (s.sup p) :=
begin
  classical,
  by_cases hs' : ¬¨s'.nonempty,
  { refine ‚ü®1, ‚àÖ, zero_lt_one, _‚ü©,
    rw [finset.not_nonempty_iff_eq_empty.mp hs', finset.sup_empty, bot_eq_zero, zero_comp],
    exact seminorm.nonneg _ },
  rw not_not at hs',
  choose f‚Çõ fC hf using hf,
  use [s'.card ‚Ä¢ s'.sup fC, finset.bUnion s' f‚Çõ],
  split,
  { refine nsmul_pos _ (ne_of_gt (finset.nonempty.card_pos hs')),
    cases finset.nonempty.bex hs' with j hj,
    exact lt_of_lt_of_le (zero_lt_iff.mpr (and.elim_left (hf j))) (finset.le_sup hj) },
  have hs : ‚àÄ i : Œπ', i ‚àà s' ‚Üí (q i).comp f ‚â§ s'.sup fC ‚Ä¢ ((finset.bUnion s' f‚Çõ).sup p) :=
  begin
    intros i hi,
    refine le_trans (and.elim_right (hf i)) (smul_le_smul _ (finset.le_sup hi)),
    exact finset.sup_mono (finset.subset_bUnion_of_mem f‚Çõ hi),
  end,
  refine le_trans (comp_mono f (finset_sup_le_sum q s')) _,
  simp_rw [‚Üêpullback_apply, add_monoid_hom.map_sum, pullback_apply], --improve this
  refine le_trans (finset.sum_le_sum hs) _,
  rw [finset.sum_const, smul_assoc],
  exact le_rfl,
end

end bounded

section topology

variables [normed_field ùïú] [add_comm_group E] [module ùïú E] [add_comm_group F] [module ùïú F]
variables [nonempty Œπ] [nonempty Œπ']

/-- The proposition that the topology of `E` is induced by a family of seminorms `p`. -/
class with_seminorms (p : Œπ ‚Üí seminorm ùïú E) [t : topological_space E] : Prop :=
(topology_eq_with_seminorms : t = (seminorm_module_filter_basis p).topology)

lemma with_seminorms_eq (p : Œπ ‚Üí seminorm ùïú E) [t : topological_space E] [with_seminorms p] :
  t = ((seminorm_module_filter_basis p).topology) := with_seminorms.topology_eq_with_seminorms

/-- The topology of a `normed_space ùïú E` is induced by the seminorm `norm_seminorm ùïú E`. -/
instance norm_with_seminorms (ùïú E) [normed_field ùïú] [semi_normed_group E] [normed_space ùïú E] :
  with_seminorms (Œª (_ : fin 1), norm_seminorm ùïú E) :=
begin
  let p := Œª _ : fin 1, norm_seminorm ùïú E,
  refine ‚ü®topological_add_group.ext normed_top_group
    ((seminorm_add_group_filter_basis _).is_topological_add_group) _‚ü©,
  refine filter.has_basis.eq_of_same_basis metric.nhds_basis_ball _,
  rw ‚Üêball_norm_seminorm ùïú E,
  refine filter.has_basis.to_has_basis (seminorm_add_group_filter_basis p).nhds_zero_has_basis _
    (Œª r hr, ‚ü®(norm_seminorm ùïú E).ball 0 r, seminorm_basis_zero_singleton_mem p 0 hr, rfl.subset‚ü©),
  rintros U (hU : U ‚àà seminorm_basis_zero p),
  rcases (seminorm_basis_zero_iff p U).mp hU with ‚ü®s, r, hr, hU‚ü©,
  use [r, hr],
  rw [hU, id.def],
  by_cases h : s.nonempty,
  { rw finset.sup_const h },
  rw [finset.not_nonempty_iff_eq_empty.mp h, finset.sup_empty, ball_bot _ hr],
  exact set.subset_univ _,
end

lemma continuous_from_bounded (p : Œπ ‚Üí seminorm ùïú E) (q : Œπ' ‚Üí seminorm ùïú F)
  [uniform_space E] [uniform_add_group E] [with_seminorms p]
  [uniform_space F] [uniform_add_group F] [with_seminorms q]
  (f : E ‚Üí‚Çó[ùïú] F) (hf : is_bounded p q f) : continuous f :=
begin
  refine uniform_continuous.continuous _,
  refine add_monoid_hom.uniform_continuous_of_continuous_at_zero f.to_add_monoid_hom _,
  rw [f.to_add_monoid_hom_coe, continuous_at_def, f.map_zero, with_seminorms_eq p],
  intros U hU,
  rw [with_seminorms_eq q, add_group_filter_basis.nhds_zero_eq, filter_basis.mem_filter_iff] at hU,
  rcases hU with ‚ü®V, hV : V ‚àà seminorm_basis_zero q, hU‚ü©,
  rcases (seminorm_basis_zero_iff q V).mp hV with ‚ü®s‚ÇÇ, r, hr, hV‚ü©,
  rw hV at hU,
  rw [(seminorm_add_group_filter_basis p).nhds_zero_eq, filter_basis.mem_filter_iff],
  rcases (is_bounded_sup hf s‚ÇÇ) with ‚ü®C, s‚ÇÅ, hC, hf‚ü©,
  refine ‚ü®(s‚ÇÅ.sup p).ball 0 (r/C),
    seminorm_basis_zero_mem p _ (div_pos hr (nnreal.coe_pos.mpr hC)), _‚ü©,
  refine subset.trans _ (preimage_mono hU),
  simp_rw [‚Üêlinear_map.map_zero f, ‚Üêball_comp],
  refine subset.trans _ (ball_antitone hf),
  rw ball_smul (s‚ÇÅ.sup p) hC,
end

lemma cont_with_seminorms_normed_space (F) [semi_normed_group F] [normed_space ùïú F]
  [uniform_space E] [uniform_add_group E]
  (p : Œπ ‚Üí seminorm ùïú E) [with_seminorms p] (f : E ‚Üí‚Çó[ùïú] F)
  (hf : ‚àÉ (s : finset Œπ) C : ‚Ñù‚â•0, C ‚â† 0 ‚àß (norm_seminorm ùïú F).comp f ‚â§ C ‚Ä¢ s.sup p) :
  continuous f :=
begin
  rw ‚Üêis_bounded_const (fin 1) at hf,
  exact continuous_from_bounded p (Œª _ : fin 1, norm_seminorm ùïú F) f hf,
end

lemma cont_normed_space_to_with_seminorms (E) [semi_normed_group E] [normed_space ùïú E]
  [uniform_space F] [uniform_add_group F]
  (q : Œπ ‚Üí seminorm ùïú F) [with_seminorms q] (f : E ‚Üí‚Çó[ùïú] F)
  (hf : ‚àÄ i : Œπ, ‚àÉ C : ‚Ñù‚â•0, C ‚â† 0 ‚àß (q i).comp f ‚â§ C ‚Ä¢ (norm_seminorm ùïú E)) : continuous f :=
begin
  rw ‚Üêconst_is_bounded (fin 1) at hf,
  exact continuous_from_bounded (Œª _ : fin 1, norm_seminorm ùïú E) q f hf,
end

end topology

section locally_convex_space

open locally_convex_space

variables [nonempty Œπ] [normed_linear_ordered_field ùïú] [normed_space ‚Ñù ùïú]
  [add_comm_group E] [module ùïú E] [module ‚Ñù E] [is_scalar_tower ‚Ñù ùïú E] [topological_space E]
  [topological_add_group E]

lemma with_seminorms.to_locally_convex_space (p : Œπ ‚Üí seminorm ùïú E) [with_seminorms p] :
  locally_convex_space ‚Ñù E :=
begin
  apply of_basis_zero ‚Ñù E id (Œª s, s ‚àà seminorm_basis_zero p),
  { rw [with_seminorms_eq p, add_group_filter_basis.nhds_eq _, add_group_filter_basis.N_zero],
    exact filter_basis.has_basis _ },
  { intros s hs,
    change s ‚àà set.Union _ at hs,
    simp_rw [set.mem_Union, set.mem_singleton_iff] at hs,
    rcases hs with ‚ü®I, r, hr, rfl‚ü©,
    exact convex_ball _ _ _ }
end

end locally_convex_space

end seminorm

section normed_space

variables (ùïú) [normed_linear_ordered_field ùïú] [normed_space ‚Ñù ùïú] [semi_normed_group E]

/-- Not an instance since `ùïú` can't be inferred. See `normed_space.to_locally_convex_space` for a
slightly weaker instance version. -/
lemma normed_space.to_locally_convex_space' [normed_space ùïú E] [module ‚Ñù E]
  [is_scalar_tower ‚Ñù ùïú E] : locally_convex_space ‚Ñù E :=
seminorm.with_seminorms.to_locally_convex_space (Œª _ : fin 1, norm_seminorm ùïú E)

/-- See `normed_space.to_locally_convex_space'` for a slightly stronger version which is not an
instance. -/
instance normed_space.to_locally_convex_space [normed_space ‚Ñù E] :
  locally_convex_space ‚Ñù E :=
normed_space.to_locally_convex_space' ‚Ñù

end normed_space
