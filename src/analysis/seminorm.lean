/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Ya√´l Dillies, Moritz Doll
-/
import analysis.locally_convex.basic
import data.real.pointwise
import data.real.sqrt
import topology.algebra.filter_basis
import topology.algebra.module.locally_convex

/-!
# Seminorms

This file defines seminorms.

A seminorm is a function to the reals which is positive-semidefinite, absolutely homogeneous, and
subadditive. They are closely related to convex sets and a topological vector space is locally
convex if and only if its topology is induced by a family of seminorms.

## Main declarations

* `add_group_seminorm`: A function `f` from an additive group `G` to the reals that preserves zero,
  takes nonnegative values, is subadditive and such that `f (-x) = f x` for all `x`.
* `group_seminorm`: A function `f` from a group `G` to the reals that sends one to zero, takes
  nonnegative values, is submultiplicative and such that `f x‚Åª¬π = f x` for all `x`.

For a module over a normed ring:
* `seminorm`: A function to the reals that is positive-semidefinite, absolutely homogeneous, and
  subadditive.
* `norm_seminorm ùïú E`: The norm on `E` as a seminorm.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

seminorm, locally convex, LCTVS
-/

set_option old_structure_cmd true

open normed_field set
open_locale big_operators nnreal pointwise topological_space

variables {R R' ùïú E F G Œπ : Type*}

/-- A seminorm on an additive group `G` is a function `f : G ‚Üí ‚Ñù` that preserves zero, takes
nonnegative values, is subadditive and such that `f (-x) = f x` for all `x ‚àà G`. -/
structure add_group_seminorm (G : Type*) [add_group G]
  extends zero_hom G ‚Ñù :=
(nonneg' : ‚àÄ r, 0 ‚â§ to_fun r)
(add_le' : ‚àÄ r s, to_fun (r + s) ‚â§ to_fun r + to_fun s)
(neg' : ‚àÄ r, to_fun (- r) = to_fun r)

/-- A seminorm on a group `G` is a function `f : G ‚Üí ‚Ñù` that preserves zero, takes nonnegative
values, is submultiplicative and such that `f x‚Åª¬π = f x` for all `x`. -/
@[to_additive] structure group_seminorm (G : Type*) [group G] :=
(to_fun : G ‚Üí ‚Ñù)
(map_one' : to_fun 1 = 0)
(nonneg' : ‚àÄ x, 0 ‚â§ to_fun x)
(mul_le' : ‚àÄ x y, to_fun (x * y) ‚â§ to_fun x + to_fun y)
(inv' : ‚àÄ x, to_fun x‚Åª¬π = to_fun x)

attribute [nolint doc_blame] add_group_seminorm.to_zero_hom

namespace group_seminorm
section group
variables [group E] [group F] [group G]

@[to_additive] instance fun_like : fun_like (group_seminorm E) E (Œª _, ‚Ñù) :=
{ coe := Œª f, f.to_fun,
  coe_injective' := Œª f g h, by cases f; cases g; congr' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`. -/
@[to_additive "Helper instance for when there's too many metavariables to apply
`fun_like.has_coe_to_fun`. "]
instance : has_coe_to_fun (group_seminorm E) (Œª _, E ‚Üí ‚Ñù) := ‚ü®to_fun‚ü©

@[ext, to_additive] lemma ext {p q : group_seminorm E} : (‚àÄ x, (p : E ‚Üí ‚Ñù) x = q x) ‚Üí p = q :=
fun_like.ext p q

variables (p q : group_seminorm E) (x y : E) (r : ‚Ñù)

@[to_additive] protected lemma nonneg : 0 ‚â§ p x := p.nonneg' _
@[simp, to_additive] protected lemma map_one : p 1 = 0 := p.map_one'
@[to_additive] protected lemma mul_le : p (x * y) ‚â§ p x + p y := p.mul_le' _ _
@[simp, to_additive] protected lemma inv : p x‚Åª¬π = p x := p.inv' _

@[to_additive] protected lemma div_le  : p (x / y) ‚â§ p x + p y :=
by { rw [div_eq_mul_inv, ‚Üêp.inv y], exact p.mul_le _ _ }

@[to_additive] lemma div_rev : p (x / y) = p (y / x) := by rw [‚Üêinv_div, p.inv]

@[to_additive] instance : partial_order (group_seminorm E) :=
partial_order.lift _ fun_like.coe_injective

@[to_additive] lemma le_def : p ‚â§ q ‚Üî (p : E ‚Üí ‚Ñù) ‚â§ q := iff.rfl
@[to_additive] lemma lt_def : p < q ‚Üî (p : E ‚Üí ‚Ñù) < q := iff.rfl

variables {p q}

@[simp, to_additive] lemma coe_le_coe : (p : E ‚Üí ‚Ñù) ‚â§ q ‚Üî p ‚â§ q := iff.rfl
@[simp, to_additive] lemma coe_lt_coe : (p : E ‚Üí ‚Ñù) < q ‚Üî p < q := iff.rfl

variables (p q) (f : F ‚Üí* E)

@[to_additive] instance : has_zero (group_seminorm E) :=
‚ü®{ to_fun   := 0,
  nonneg'   := Œª r, le_rfl,
  map_one' := pi.zero_apply _,
  mul_le'   := Œª _ _, (zero_add _).ge,
  inv'      := Œª x, rfl}‚ü©

@[simp, to_additive] lemma coe_zero : ‚áë(0 : group_seminorm E) = 0 := rfl
@[simp, to_additive] lemma zero_apply (x : E) : (0 : group_seminorm E) x = 0 := rfl

@[to_additive] instance : inhabited (group_seminorm E) := ‚ü®0‚ü©

@[to_additive] instance : has_add (group_seminorm E) :=
{ add := Œª p q,
  { to_fun    := Œª x, p x + q x,
    nonneg'   := Œª x, add_nonneg (p.nonneg _) (q.nonneg _),
    map_one' := by rw [p.map_one, q.map_one, zero_add],
    mul_le'   := Œª _ _, (add_le_add (p.mul_le _ _) $ q.mul_le _ _).trans_eq $
      add_add_add_comm _ _ _ _,
    inv' := Œª x, by rw [p.inv, q.inv] }}

@[simp, to_additive] lemma coe_add : ‚áë(p + q) = p + q := rfl
@[simp, to_additive] lemma add_apply (x : E) : (p + q) x = p x + q x := rfl

-- TODO: define `has_Sup` too, from the skeleton at
-- https://github.com/leanprover-community/mathlib/pull/11329#issuecomment-1008915345
@[to_additive] noncomputable instance : has_sup (group_seminorm E) :=
{ sup := Œª p q,
  { to_fun  := p ‚äî q,
    nonneg' := Œª x, le_sup_of_le_left $ p.nonneg _,
    map_one' := by rw [pi.sup_apply, ‚Üêp.map_one, sup_eq_left, p.map_one, q.map_one],
    mul_le' := Œª x y, sup_le
      ((p.mul_le x y).trans $ add_le_add le_sup_left le_sup_left)
      ((q.mul_le x y).trans $ add_le_add le_sup_right le_sup_right),
    inv' := Œª x, by rw [pi.sup_apply, pi.sup_apply, p.inv, q.inv] }}

@[simp, to_additive] lemma coe_sup : ‚áë(p ‚äî q) = p ‚äî q := rfl
@[simp, to_additive] lemma sup_apply (x : E) : (p ‚äî q) x = p x ‚äî q x := rfl

@[to_additive] noncomputable instance : semilattice_sup (group_seminorm E) :=
fun_like.coe_injective.semilattice_sup _ coe_sup

/-- Composition of a group seminorm with a monoid homomorphism as a group seminorm. -/
@[to_additive "Composition of an additive group seminorm with an additive monoid homomorphism as an
additive group seminorm."]
def comp (p : group_seminorm E) (f : F ‚Üí* E) : group_seminorm F :=
{ to_fun   := Œª x, p (f x),
  nonneg'  := Œª x, p.nonneg _,
  map_one' := by rw [f.map_one, p.map_one],
  mul_le'  := Œª _ _, (congr_arg p $ f.map_mul _ _).trans_le $ p.mul_le _ _,
  inv'     := Œª x, by rw [map_inv, p.inv] }

@[simp, to_additive] lemma coe_comp : ‚áë(p.comp f) = p ‚àò f := rfl
@[simp, to_additive] lemma comp_apply (x : F) : (p.comp f) x = p (f x) := rfl
@[simp, to_additive] lemma comp_id : p.comp (monoid_hom.id _) = p := ext $ Œª _, rfl
@[simp, to_additive] lemma comp_zero : p.comp (1 : F ‚Üí* E) = 0 := ext $ Œª _, p.map_one
@[simp, to_additive] lemma zero_comp : (0 : group_seminorm E).comp f = 0 := ext $ Œª _, rfl

@[to_additive] lemma comp_assoc (g : F ‚Üí* E) (f : G ‚Üí* F) : p.comp (g.comp f) = (p.comp g).comp f :=
ext $ Œª _, rfl

@[to_additive] lemma add_comp (f : F ‚Üí* E) : (p + q).comp f = p.comp f + q.comp f := ext $ Œª _, rfl

variables {p q}

@[to_additive] lemma comp_mono (hp : p ‚â§ q) : p.comp f ‚â§ q.comp f := Œª _, hp _

end group

section comm_group
variables [comm_group E] [comm_group F] (p q : group_seminorm E) (x y : E)

/-- The direct path from `1` to `y` is shorter than the path with `x` "inserted" in between. -/
@[to_additive "The direct path from `0` to `y` is shorter than the path with `x` \"inserted\" in
between."]
lemma le_insert : p y ‚â§ p x + p (x / y) :=
calc p y = p (x / (x / y)) : by rw div_div_cancel
     ... ‚â§ p x + p (x / y) : p.div_le  _ _

/-- The direct path from 0 to x is shorter than the path with y "inserted" in between. -/
@[to_additive "The direct path from 0 to x is shorter than the path with y \"inserted\" in
between."]
lemma le_insert' : p x ‚â§ p y + p (x / y) := by { rw div_rev, exact le_insert _ _ _ }

@[to_additive] lemma comp_mul_le (f g : F ‚Üí* E) : p.comp (f * g) ‚â§ p.comp f + p.comp g :=
Œª _, p.mul_le _ _

@[to_additive] private lemma mul_bdd_below_range_add {p q : group_seminorm E} {x : E} :
  bdd_below (range (Œª y, p y + q (x / y))) :=
‚ü®0, by { rintro _ ‚ü®x, rfl‚ü©, exact add_nonneg (p.nonneg _) (q.nonneg _) }‚ü©

@[to_additive] noncomputable instance : has_inf (group_seminorm E) :=
{ inf := Œª p q,
  { to_fun    := Œª x, ‚®Ö y, p y + q (x / y),
    nonneg'   := Œª x, le_cinfi $ Œª x, add_nonneg (p.nonneg _) (q.nonneg _),
    map_one' := cinfi_eq_of_forall_ge_of_forall_gt_exists_lt
        (Œª x, add_nonneg (p.nonneg _) (q.nonneg _))
        (Œª r hr, ‚ü®1, by rwa [div_one, p.map_one, q.map_one, add_zero]‚ü©),
    mul_le'   := Œª x y, le_cinfi_add_cinfi $ Œª u v, begin
      apply cinfi_le_of_le mul_bdd_below_range_add (u * v),
      dsimp,
      rw [mul_div_mul_comm, add_add_add_comm],
      exact add_le_add (p.mul_le _ _) (q.mul_le _ _),
    end,
    inv' := Œª x, (inv_surjective.infi_comp _).symm.trans $ by simp_rw [p.inv, ‚Üêinv_div', q.inv] }}

@[simp, to_additive] lemma inf_apply : (p ‚äì q) x = ‚®Ö y, p y + q (x / y) := rfl

@[to_additive] noncomputable instance : lattice (group_seminorm E) :=
{ inf := (‚äì),
  inf_le_left := Œª p q x, cinfi_le_of_le mul_bdd_below_range_add x $
    by rw [div_self', q.map_one, add_zero],
  inf_le_right := Œª p q x, cinfi_le_of_le mul_bdd_below_range_add (1 : E) $
    by simp only [div_one, p.map_one, zero_add],
  le_inf := Œª a b c hb hc x, le_cinfi $ Œª u, (a.le_insert' _ _).trans $ add_le_add (hb _) (hc _),
  ..group_seminorm.semilattice_sup }

end comm_group
end group_seminorm

namespace add_group_seminorm
variables [add_group E] (p : add_group_seminorm E) (x y : E) (r : ‚Ñù)

instance zero_hom_class : zero_hom_class (add_group_seminorm E) E ‚Ñù :=
{ coe := Œª f, f.to_fun,
  coe_injective' := Œª f g h, by cases f; cases g; congr',
  map_zero := Œª f, f.map_zero' }

/- TODO: All the following ought to be automated using `to_additive`. The problem is that it doesn't
see that `has_smul R ‚Ñù` should be fixed because `‚Ñù` is fixed. -/

/-- Any action on `‚Ñù` which factors through `‚Ñù‚â•0` applies to an `add_group_seminorm`. -/
instance [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  has_smul R (add_group_seminorm E) :=
{ smul := Œª r p,
  { to_fun := Œª x, r ‚Ä¢ p x,
    nonneg' := Œª x, begin
      simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul],
      exact mul_nonneg (nnreal.coe_nonneg _) (p.nonneg _)
    end,
    map_zero' := by simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul,
      p.map_zero, mul_zero],
    add_le' := Œª _ _, begin
      simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul],
      exact (mul_le_mul_of_nonneg_left (p.add_le _ _) (nnreal.coe_nonneg _)).trans_eq
        (mul_add _ _ _),
    end,
    neg' := Œª x, by rw p.neg }}

instance [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  [has_smul R' ‚Ñù] [has_smul R' ‚Ñù‚â•0] [is_scalar_tower R' ‚Ñù‚â•0 ‚Ñù]
  [has_smul R R'] [is_scalar_tower R R' ‚Ñù] :
  is_scalar_tower R R' (add_group_seminorm E) :=
{ smul_assoc := Œª r a p, ext $ Œª x, smul_assoc r a (p x) }

@[simp] lemma coe_smul [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p : add_group_seminorm E) : ‚áë(r ‚Ä¢ p) = r ‚Ä¢ p := rfl

@[simp] lemma smul_apply [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p : add_group_seminorm E) (x : E) : (r ‚Ä¢ p) x = r ‚Ä¢ p x := rfl

lemma smul_sup [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p q : add_group_seminorm E) :
  r ‚Ä¢ (p ‚äî q) = r ‚Ä¢ p ‚äî r ‚Ä¢ q :=
have real.smul_max : ‚àÄ x y : ‚Ñù, r ‚Ä¢ max x y = max (r ‚Ä¢ x) (r ‚Ä¢ y),
from Œª x y, by simpa only [‚Üêsmul_eq_mul, ‚Üênnreal.smul_def, smul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù)]
                     using mul_max_of_nonneg x y (r ‚Ä¢ 1 : ‚Ñù‚â•0).prop,
ext $ Œª x, real.smul_max _ _

end add_group_seminorm

namespace group_seminorm
variables [group E] [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]

/-- Any action on `‚Ñù` which factors through `‚Ñù‚â•0` applies to an `add_group_seminorm`. -/
@[to_additive add_group_seminorm.has_smul] instance : has_smul R (group_seminorm E) :=
 ‚ü®Œª r p,
  { to_fun := Œª x, r ‚Ä¢ p x,
    nonneg' := Œª x, begin
      simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul],
      exact mul_nonneg (nnreal.coe_nonneg _) (p.nonneg _)
    end,
    map_one' := by simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul,
      p.map_one, mul_zero],
    mul_le' := Œª _ _, begin
      simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul],
      exact (mul_le_mul_of_nonneg_left (p.mul_le _ _) $ nnreal.coe_nonneg _).trans_eq
        (mul_add _ _ _),
    end,
    inv' := Œª x, by rw p.inv }‚ü©

@[to_additive]
instance [has_smul R' ‚Ñù] [has_smul R' ‚Ñù‚â•0] [is_scalar_tower R' ‚Ñù‚â•0 ‚Ñù] [has_smul R R']
  [is_scalar_tower R R' ‚Ñù] : is_scalar_tower R R' (group_seminorm E) :=
‚ü®Œª r a p, ext $ Œª x, smul_assoc r a $ p x‚ü©

@[simp, to_additive add_group_seminorm.coe_smul]
lemma coe_smul (r : R) (p : group_seminorm E) : ‚áë(r ‚Ä¢ p) = r ‚Ä¢ p := rfl

@[simp, to_additive add_group_seminorm.smul_apply]
lemma smul_apply (r : R) (p : group_seminorm E) (x : E) : (r ‚Ä¢ p) x = r ‚Ä¢ p x := rfl

@[to_additive add_group_seminorm.smul_sup]
lemma smul_sup (r : R) (p q : group_seminorm E) : r ‚Ä¢ (p ‚äî q) = r ‚Ä¢ p ‚äî r ‚Ä¢ q :=
have real.smul_max : ‚àÄ x y : ‚Ñù, r ‚Ä¢ max x y = max (r ‚Ä¢ x) (r ‚Ä¢ y),
from Œª x y, by simpa only [‚Üêsmul_eq_mul, ‚Üênnreal.smul_def, smul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù)]
                     using mul_max_of_nonneg x y (r ‚Ä¢ 1 : ‚Ñù‚â•0).prop,
ext $ Œª x, real.smul_max _ _

end group_seminorm

/-- A seminorm on a module over a normed ring is a function to the reals that is positive
semidefinite, positive homogeneous, and subadditive. -/
structure seminorm (ùïú : Type*) (E : Type*) [semi_normed_ring ùïú] [add_group E] [has_smul ùïú E]
  extends add_group_seminorm E :=
(smul' : ‚àÄ (a : ùïú) (x : E), to_fun (a ‚Ä¢ x) = ‚à•a‚à• * to_fun x)

attribute [nolint doc_blame] seminorm.to_add_group_seminorm

private lemma map_zero.of_smul {ùïú : Type*} {E : Type*} [semi_normed_ring ùïú] [add_group E]
  [smul_with_zero ùïú E] {f : E ‚Üí ‚Ñù} (smul : ‚àÄ (a : ùïú) (x : E), f (a ‚Ä¢ x) = ‚à•a‚à• * f x) : f 0 = 0 :=
calc f 0 = f ((0 : ùïú) ‚Ä¢ 0) : by rw zero_smul
     ... = 0 : by rw [smul, norm_zero, zero_mul]

private lemma neg.of_smul {ùïú : Type*} {E : Type*} [semi_normed_ring ùïú] [add_comm_group E]
  [module ùïú E] {f : E ‚Üí ‚Ñù} (smul : ‚àÄ (a : ùïú) (x : E), f (a ‚Ä¢ x) = ‚à•a‚à• * f x) (x : E) :
  f (-x) = f x :=
by rw [‚Üêneg_one_smul ùïú, smul, norm_neg, ‚Üê smul, one_smul]

private lemma nonneg.of {ùïú : Type*} {E : Type*} [semi_normed_ring ùïú] [add_comm_group E] [module ùïú E]
  {f : E ‚Üí ‚Ñù} (add_le : ‚àÄ (x y : E), f (x + y) ‚â§ f x + f y)
  (smul : ‚àÄ (a : ùïú) (x : E), f (a ‚Ä¢ x) = ‚à•a‚à• * f x) (x : E) : 0 ‚â§ f x :=
have h: 0 ‚â§ 2 * f x, from
calc 0 = f (x + (- x)) : by rw [add_neg_self, map_zero.of_smul smul]
...    ‚â§ f x + f (-x)  : add_le _ _
...    = 2 * f x : by rw [neg.of_smul smul, two_mul],
nonneg_of_mul_nonneg_right h zero_lt_two

/-- Alternative constructor for a `seminorm` on an `add_comm_group E` that is a module over a
`semi_norm_ring ùïú`. -/
def seminorm.of {ùïú : Type*} {E : Type*} [semi_normed_ring ùïú] [add_comm_group E] [module ùïú E]
  (f : E ‚Üí ‚Ñù) (add_le : ‚àÄ (x y : E), f (x + y) ‚â§ f x + f y)
  (smul : ‚àÄ (a : ùïú) (x : E), f (a ‚Ä¢ x) = ‚à•a‚à• * f x) : seminorm ùïú E :=
{ to_fun    := f,
  map_zero' := map_zero.of_smul smul,
  nonneg'   := nonneg.of add_le smul,
  add_le'   := add_le,
  smul'     := smul,
  neg'      := neg.of_smul smul }

namespace seminorm

section semi_normed_ring
variables [semi_normed_ring ùïú]

section add_group
variables [add_group E]

section has_smul
variables [has_smul ùïú E]

instance zero_hom_class : zero_hom_class (seminorm ùïú E) E ‚Ñù :=
{ coe := Œª f, f.to_fun,
  coe_injective' := Œª f g h, by cases f; cases g; congr',
  map_zero := Œª f, f.map_zero' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`. -/
instance : has_coe_to_fun (seminorm ùïú E) (Œª _, E ‚Üí ‚Ñù) := ‚ü®Œª p, p.to_fun‚ü©

@[ext] lemma ext {p q : seminorm ùïú E} (h : ‚àÄ x, (p : E ‚Üí ‚Ñù) x = q x) : p = q := fun_like.ext p q h

instance : has_zero (seminorm ùïú E) :=
‚ü®{ smul' := Œª _ _, (mul_zero _).symm,
  ..add_group_seminorm.has_zero.zero }‚ü©

@[simp] lemma coe_zero : ‚áë(0 : seminorm ùïú E) = 0 := rfl

@[simp] lemma zero_apply (x : E) : (0 : seminorm ùïú E) x = 0 := rfl

instance : inhabited (seminorm ùïú E) := ‚ü®0‚ü©

variables (p : seminorm ùïú E) (c : ùïú) (x y : E) (r : ‚Ñù)

protected lemma nonneg : 0 ‚â§ p x := p.nonneg' _
protected lemma map_zero : p 0 = 0 := p.map_zero'
protected lemma smul : p (c ‚Ä¢ x) = ‚à•c‚à• * p x := p.smul' _ _
protected lemma add_le : p (x + y) ‚â§ p x + p y := p.add_le' _ _

/-- Any action on `‚Ñù` which factors through `‚Ñù‚â•0` applies to a seminorm. -/
instance [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  has_smul R (seminorm ùïú E) :=
{ smul := Œª r p,
  { to_fun  := Œª x, r ‚Ä¢ p x,
    smul' := Œª _ _, begin
      simp only [‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def, smul_eq_mul],
      rw [p.smul, mul_left_comm],
    end,
    ..(r ‚Ä¢ p.to_add_group_seminorm) }}

instance [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  [has_smul R' ‚Ñù] [has_smul R' ‚Ñù‚â•0] [is_scalar_tower R' ‚Ñù‚â•0 ‚Ñù]
  [has_smul R R'] [is_scalar_tower R R' ‚Ñù] :
  is_scalar_tower R R' (seminorm ùïú E) :=
{ smul_assoc := Œª r a p, ext $ Œª x, smul_assoc r a (p x) }

lemma coe_smul [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p : seminorm ùïú E) : ‚áë(r ‚Ä¢ p) = r ‚Ä¢ p := rfl

@[simp] lemma smul_apply [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p : seminorm ùïú E) (x : E) : (r ‚Ä¢ p) x = r ‚Ä¢ p x := rfl

instance : has_add (seminorm ùïú E) :=
{ add := Œª p q,
  { to_fun    := Œª x, p x + q x,
    smul'     := Œª a x, by simp only [p.smul, q.smul, mul_add],
    ..(p.to_add_group_seminorm + q.to_add_group_seminorm) }}

lemma coe_add (p q : seminorm ùïú E) : ‚áë(p + q) = p + q := rfl

@[simp] lemma add_apply (p q : seminorm ùïú E) (x : E) : (p + q) x = p x + q x := rfl

instance : add_monoid (seminorm ùïú E) :=
fun_like.coe_injective.add_monoid _ rfl coe_add (Œª p n, coe_smul n p)

instance : ordered_cancel_add_comm_monoid (seminorm ùïú E) :=
fun_like.coe_injective.ordered_cancel_add_comm_monoid _ rfl coe_add (Œª p n, coe_smul n p)

instance [monoid R] [mul_action R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  mul_action R (seminorm ùïú E) :=
fun_like.coe_injective.mul_action _ coe_smul

variables (ùïú E)

/-- `coe_fn` as an `add_monoid_hom`. Helper definition for showing that `seminorm ùïú E` is
a module. -/
@[simps]
def coe_fn_add_monoid_hom : add_monoid_hom (seminorm ùïú E) (E ‚Üí ‚Ñù) := ‚ü®coe_fn, coe_zero, coe_add‚ü©

lemma coe_fn_add_monoid_hom_injective : function.injective (coe_fn_add_monoid_hom ùïú E) :=
show @function.injective (seminorm ùïú E) (E ‚Üí ‚Ñù) coe_fn, from fun_like.coe_injective

variables {ùïú E}

instance [monoid R] [distrib_mul_action R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  distrib_mul_action R (seminorm ùïú E) :=
(coe_fn_add_monoid_hom_injective ùïú E).distrib_mul_action _ coe_smul

instance [semiring R] [module R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù] :
  module R (seminorm ùïú E) :=
(coe_fn_add_monoid_hom_injective ùïú E).module R _ coe_smul

-- TODO: define `has_Sup` too, from the skeleton at
-- https://github.com/leanprover-community/mathlib/pull/11329#issuecomment-1008915345
noncomputable instance : has_sup (seminorm ùïú E) :=
{ sup := Œª p q,
  { to_fun  := p ‚äî q,
    smul' := Œª x v, (congr_arg2 max (p.smul x v) (q.smul x v)).trans $
      (mul_max_of_nonneg _ _ $ norm_nonneg x).symm,
    ..(p.to_add_group_seminorm ‚äî q.to_add_group_seminorm) } }

@[simp] lemma coe_sup (p q : seminorm ùïú E) : ‚áë(p ‚äî q) = p ‚äî q := rfl
lemma sup_apply (p q : seminorm ùïú E) (x : E) : (p ‚äî q) x = p x ‚äî q x := rfl

lemma smul_sup [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p q : seminorm ùïú E) :
  r ‚Ä¢ (p ‚äî q) = r ‚Ä¢ p ‚äî r ‚Ä¢ q :=
have real.smul_max : ‚àÄ x y : ‚Ñù, r ‚Ä¢ max x y = max (r ‚Ä¢ x) (r ‚Ä¢ y),
from Œª x y, by simpa only [‚Üêsmul_eq_mul, ‚Üênnreal.smul_def, smul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù)]
                     using mul_max_of_nonneg x y (r ‚Ä¢ 1 : ‚Ñù‚â•0).prop,
ext $ Œª x, real.smul_max _ _

instance : partial_order (seminorm ùïú E) :=
  partial_order.lift _ fun_like.coe_injective

lemma le_def (p q : seminorm ùïú E) : p ‚â§ q ‚Üî (p : E ‚Üí ‚Ñù) ‚â§ q := iff.rfl
lemma lt_def (p q : seminorm ùïú E) : p < q ‚Üî (p : E ‚Üí ‚Ñù) < q := iff.rfl

noncomputable instance : semilattice_sup (seminorm ùïú E) :=
function.injective.semilattice_sup _ fun_like.coe_injective coe_sup

end has_smul

end add_group

section module
variables [add_comm_group E] [add_comm_group F] [add_comm_group G]
variables [module ùïú E] [module ùïú F] [module ùïú G]
variables [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]

/-- Composition of a seminorm with a linear map is a seminorm. -/
def comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) : seminorm ùïú E :=
{ to_fun    := Œª x, p (f x),
  smul'     := Œª _ _, (congr_arg p (f.map_smul _ _)).trans (p.smul _ _),
  ..(p.to_add_group_seminorm.comp f.to_add_monoid_hom) }

lemma coe_comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) : ‚áë(p.comp f) = p ‚àò f := rfl

@[simp] lemma comp_apply (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (x : E) : (p.comp f) x = p (f x) := rfl

@[simp] lemma comp_id (p : seminorm ùïú E) : p.comp linear_map.id = p :=
ext $ Œª _, rfl

@[simp] lemma comp_zero (p : seminorm ùïú F) : p.comp (0 : E ‚Üí‚Çó[ùïú] F) = 0 :=
ext $ Œª _, map_zero p

@[simp] lemma zero_comp (f : E ‚Üí‚Çó[ùïú] F) : (0 : seminorm ùïú F).comp f = 0 :=
ext $ Œª _, rfl

lemma comp_comp (p : seminorm ùïú G) (g : F ‚Üí‚Çó[ùïú] G) (f : E ‚Üí‚Çó[ùïú] F) :
  p.comp (g.comp f) = (p.comp g).comp f :=
ext $ Œª _, rfl

lemma add_comp (p q : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) : (p + q).comp f = p.comp f + q.comp f :=
ext $ Œª _, rfl

lemma comp_add_le (p : seminorm ùïú F) (f g : E ‚Üí‚Çó[ùïú] F) : p.comp (f + g) ‚â§ p.comp f + p.comp g :=
Œª _, p.add_le _ _

lemma smul_comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (c : R) : (c ‚Ä¢ p).comp f = c ‚Ä¢ (p.comp f) :=
ext $ Œª _, rfl

lemma comp_mono {p : seminorm ùïú F} {q : seminorm ùïú F} (f : E ‚Üí‚Çó[ùïú] F) (hp : p ‚â§ q) :
  p.comp f ‚â§ q.comp f := Œª _, hp _

/-- The composition as an `add_monoid_hom`. -/
@[simps] def pullback (f : E ‚Üí‚Çó[ùïú] F) : add_monoid_hom (seminorm ùïú F) (seminorm ùïú E) :=
‚ü®Œª p, p.comp f, zero_comp f, Œª p q, add_comp p q f‚ü©

section
variables (p : seminorm ùïú E)

@[simp]
protected lemma neg (x : E) : p (-x) = p x :=
by rw [‚Üêneg_one_smul ùïú, seminorm.smul, norm_neg, ‚Üêseminorm.smul, one_smul]

protected lemma sub_le (x y : E) : p (x - y) ‚â§ p x + p y :=
calc
  p (x - y)
      = p (x + -y) : by rw sub_eq_add_neg
  ... ‚â§ p x + p (-y) : p.add_le x (-y)
  ... = p x + p y : by rw p.neg

lemma sub_rev (x y : E) : p (x - y) = p (y - x) := by rw [‚Üêneg_sub, p.neg]

/-- The direct path from 0 to y is shorter than the path with x "inserted" in between. -/
lemma le_insert (x y : E) : p y ‚â§ p x + p (x - y) :=
calc p y = p (x - (x - y)) : by rw sub_sub_cancel
... ‚â§ p x + p (x - y) : p.sub_le _ _

/-- The direct path from 0 to x is shorter than the path with y "inserted" in between. -/
lemma le_insert' (x y : E) : p x ‚â§ p y + p (x - y) := by { rw sub_rev, exact le_insert _ _ _ }

end

instance : order_bot (seminorm ùïú E) := ‚ü®0, seminorm.nonneg‚ü©

@[simp] lemma coe_bot : ‚áë(‚ä• : seminorm ùïú E) = 0 := rfl

lemma bot_eq_zero : (‚ä• : seminorm ùïú E) = 0 := rfl

lemma smul_le_smul {p q : seminorm ùïú E} {a b : ‚Ñù‚â•0} (hpq : p ‚â§ q) (hab : a ‚â§ b) :
  a ‚Ä¢ p ‚â§ b ‚Ä¢ q :=
begin
  simp_rw [le_def, pi.le_def, coe_smul],
  intros x,
  simp_rw [pi.smul_apply, nnreal.smul_def, smul_eq_mul],
  exact mul_le_mul hab (hpq x) (p.nonneg x) (nnreal.coe_nonneg b),
end

lemma finset_sup_apply (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (x : E) :
  s.sup p x = ‚Üë(s.sup (Œª i, ‚ü®p i x, (p i).nonneg x‚ü©) : ‚Ñù‚â•0) :=
begin
  induction s using finset.cons_induction_on with a s ha ih,
  { rw [finset.sup_empty, finset.sup_empty, coe_bot, _root_.bot_eq_zero, pi.zero_apply,
        nonneg.coe_zero] },
  { rw [finset.sup_cons, finset.sup_cons, coe_sup, sup_eq_max, pi.sup_apply, sup_eq_max,
        nnreal.coe_max, subtype.coe_mk, ih] }
end

lemma finset_sup_le_sum (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) : s.sup p ‚â§ ‚àë i in s, p i :=
begin
  classical,
  refine finset.sup_le_iff.mpr _,
  intros i hi,
  rw [finset.sum_eq_sum_diff_singleton_add hi, le_add_iff_nonneg_left],
  exact bot_le,
end

lemma finset_sup_apply_le {p : Œπ ‚Üí seminorm ùïú E} {s : finset Œπ} {x : E} {a : ‚Ñù} (ha : 0 ‚â§ a)
  (h : ‚àÄ i, i ‚àà s ‚Üí p i x ‚â§ a) : s.sup p x ‚â§ a :=
begin
  lift a to ‚Ñù‚â•0 using ha,
  rw [finset_sup_apply, nnreal.coe_le_coe],
  exact finset.sup_le h,
end

lemma finset_sup_apply_lt {p : Œπ ‚Üí seminorm ùïú E} {s : finset Œπ} {x : E} {a : ‚Ñù} (ha : 0 < a)
  (h : ‚àÄ i, i ‚àà s ‚Üí p i x < a) : s.sup p x < a :=
begin
  lift a to ‚Ñù‚â•0 using ha.le,
  rw [finset_sup_apply, nnreal.coe_lt_coe, finset.sup_lt_iff],
  { exact h },
  { exact nnreal.coe_pos.mpr ha },
end

end module
end semi_normed_ring

section semi_normed_comm_ring
variables [semi_normed_comm_ring ùïú] [add_comm_group E] [add_comm_group F] [module ùïú E] [module ùïú F]

lemma comp_smul (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (c : ùïú) :
  p.comp (c ‚Ä¢ f) = ‚à•c‚à•‚Çä ‚Ä¢ p.comp f :=
ext $ Œª _, by rw [comp_apply, smul_apply, linear_map.smul_apply, p.smul, nnreal.smul_def,
  coe_nnnorm, smul_eq_mul, comp_apply]

lemma comp_smul_apply (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (c : ùïú) (x : E) :
  p.comp (c ‚Ä¢ f) x = ‚à•c‚à• * p (f x) := p.smul _ _

end semi_normed_comm_ring

section normed_field
variables [normed_field ùïú] [add_comm_group E] [module ùïú E]

private lemma bdd_below_range_add (x : E) (p q : seminorm ùïú E) :
  bdd_below (range (Œª (u : E), p u + q (x - u))) :=
by { use 0, rintro _ ‚ü®x, rfl‚ü©, exact add_nonneg (p.nonneg _) (q.nonneg _) }

noncomputable instance : has_inf (seminorm ùïú E) :=
{ inf := Œª p q,
  { to_fun  := Œª x, ‚®Ö u : E, p u + q (x-u),
    smul' :=
    begin
      intros a x,
      obtain rfl | ha := eq_or_ne a 0,
      { rw [norm_zero, zero_mul, zero_smul],
        refine cinfi_eq_of_forall_ge_of_forall_gt_exists_lt
          (Œª i, add_nonneg (p.nonneg _) (q.nonneg _))
          (Œª x hx, ‚ü®0, by rwa [map_zero, sub_zero, map_zero, add_zero]‚ü©) },
      simp_rw [real.mul_infi_of_nonneg (norm_nonneg a), mul_add, ‚Üêp.smul, ‚Üêq.smul, smul_sub],
      refine function.surjective.infi_congr ((‚Ä¢) a‚Åª¬π : E ‚Üí E) (Œª u, ‚ü®a ‚Ä¢ u, inv_smul_smul‚ÇÄ ha u‚ü©)
        (Œª u, _),
      rw smul_inv_smul‚ÇÄ ha
    end,
    ..(p.to_add_group_seminorm ‚äì q.to_add_group_seminorm) }}

@[simp] lemma inf_apply (p q : seminorm ùïú E) (x : E) : (p ‚äì q) x = ‚®Ö u : E, p u + q (x-u) := rfl

noncomputable instance : lattice (seminorm ùïú E) :=
{ inf := (‚äì),
  inf_le_left := Œª p q x, begin
    apply cinfi_le_of_le (bdd_below_range_add _ _ _) x,
    simp only [sub_self, map_zero, add_zero],
  end,
  inf_le_right := Œª p q x, begin
    apply cinfi_le_of_le (bdd_below_range_add _ _ _) (0:E),
    simp only [sub_self, map_zero, zero_add, sub_zero],
  end,
  le_inf := Œª a b c hab hac x,
    le_cinfi $ Œª u, le_trans (a.le_insert' _ _) (add_le_add (hab _) (hac _)),
  ..seminorm.semilattice_sup }

lemma smul_inf [has_smul R ‚Ñù] [has_smul R ‚Ñù‚â•0] [is_scalar_tower R ‚Ñù‚â•0 ‚Ñù]
  (r : R) (p q : seminorm ùïú E) :
  r ‚Ä¢ (p ‚äì q) = r ‚Ä¢ p ‚äì r ‚Ä¢ q :=
begin
  ext,
  simp_rw [smul_apply, inf_apply, smul_apply, ‚Üêsmul_one_smul ‚Ñù‚â•0 r (_ : ‚Ñù), nnreal.smul_def,
    smul_eq_mul, real.mul_infi_of_nonneg (subtype.prop _), mul_add],
end

end normed_field

/-! ### Seminorm ball -/

section semi_normed_ring
variables [semi_normed_ring ùïú]

section add_comm_group
variables [add_comm_group E]

section has_smul
variables [has_smul ùïú E] (p : seminorm ùïú E)

/-- The ball of radius `r` at `x` with respect to seminorm `p` is the set of elements `y` with
`p (y - x) < `r`. -/
def ball (x : E) (r : ‚Ñù) := { y : E | p (y - x) < r }

variables {x y : E} {r : ‚Ñù}

@[simp] lemma mem_ball : y ‚àà ball p x r ‚Üî p (y - x) < r := iff.rfl

lemma mem_ball_zero : y ‚àà ball p 0 r ‚Üî p y < r := by rw [mem_ball, sub_zero]

lemma ball_zero_eq : ball p 0 r = { y : E | p y < r } := set.ext $ Œª x, p.mem_ball_zero

@[simp] lemma ball_zero' (x : E) (hr : 0 < r) : ball (0 : seminorm ùïú E) x r = set.univ :=
begin
  rw [set.eq_univ_iff_forall, ball],
  simp [hr],
end

lemma ball_smul (p : seminorm ùïú E) {c : nnreal} (hc : 0 < c) (r : ‚Ñù) (x : E) :
  (c ‚Ä¢ p).ball x r = p.ball x (r / c) :=
by { ext, rw [mem_ball, mem_ball, smul_apply, nnreal.smul_def, smul_eq_mul, mul_comm,
  lt_div_iff (nnreal.coe_pos.mpr hc)] }

lemma ball_sup (p : seminorm ùïú E) (q : seminorm ùïú E) (e : E) (r : ‚Ñù) :
  ball (p ‚äî q) e r = ball p e r ‚à© ball q e r :=
by simp_rw [ball, ‚Üêset.set_of_and, coe_sup, pi.sup_apply, sup_lt_iff]

lemma ball_finset_sup' (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (H : s.nonempty) (e : E) (r : ‚Ñù) :
  ball (s.sup' H p) e r = s.inf' H (Œª i, ball (p i) e r) :=
begin
  induction H using finset.nonempty.cons_induction with a a s ha hs ih,
  { classical, simp },
  { rw [finset.sup'_cons hs, finset.inf'_cons hs, ball_sup, inf_eq_inter, ih] },
end

lemma ball_mono {p : seminorm ùïú E} {r‚ÇÅ r‚ÇÇ : ‚Ñù} (h : r‚ÇÅ ‚â§ r‚ÇÇ) : p.ball x r‚ÇÅ ‚äÜ p.ball x r‚ÇÇ :=
Œª _ (hx : _ < _), hx.trans_le h

lemma ball_antitone {p q : seminorm ùïú E} (h : q ‚â§ p) : p.ball x r ‚äÜ q.ball x r :=
Œª _, (h _).trans_lt

lemma ball_add_ball_subset (p : seminorm ùïú E) (r‚ÇÅ r‚ÇÇ : ‚Ñù) (x‚ÇÅ x‚ÇÇ : E):
  p.ball (x‚ÇÅ : E) r‚ÇÅ + p.ball (x‚ÇÇ : E) r‚ÇÇ ‚äÜ p.ball (x‚ÇÅ + x‚ÇÇ) (r‚ÇÅ + r‚ÇÇ) :=
begin
  rintros x ‚ü®y‚ÇÅ, y‚ÇÇ, hy‚ÇÅ, hy‚ÇÇ, rfl‚ü©,
  rw [mem_ball, add_sub_add_comm],
  exact (p.add_le _ _).trans_lt (add_lt_add hy‚ÇÅ hy‚ÇÇ),
end

end has_smul

section module

variables [module ùïú E]
variables [add_comm_group F] [module ùïú F]

lemma ball_comp (p : seminorm ùïú F) (f : E ‚Üí‚Çó[ùïú] F) (x : E) (r : ‚Ñù) :
  (p.comp f).ball x r = f ‚Åª¬π' (p.ball (f x) r) :=
begin
  ext,
  simp_rw [ball, mem_preimage, comp_apply, set.mem_set_of_eq, map_sub],
end

variables (p : seminorm ùïú E)

lemma ball_zero_eq_preimage_ball {r : ‚Ñù} :
  p.ball 0 r = p ‚Åª¬π' (metric.ball 0 r) :=
begin
  ext x,
  simp only [mem_ball, sub_zero, mem_preimage, mem_ball_zero_iff],
  rw real.norm_of_nonneg,
  exact p.nonneg _,
end

@[simp] lemma ball_bot {r : ‚Ñù} (x : E) (hr : 0 < r) :
  ball (‚ä• : seminorm ùïú E) x r = set.univ :=
ball_zero' x hr

/-- Seminorm-balls at the origin are balanced. -/
lemma balanced_ball_zero (r : ‚Ñù) : balanced ùïú (ball p 0 r) :=
begin
  rintro a ha x ‚ü®y, hy, hx‚ü©,
  rw [mem_ball_zero, ‚Üêhx, p.smul],
  calc _ ‚â§ p y : mul_le_of_le_one_left (p.nonneg _) ha
  ...    < r   : by rwa mem_ball_zero at hy,
end

lemma ball_finset_sup_eq_Inter (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (x : E) {r : ‚Ñù} (hr : 0 < r) :
  ball (s.sup p) x r = ‚ãÇ (i ‚àà s), ball (p i) x r :=
begin
  lift r to nnreal using hr.le,
  simp_rw [ball, Inter_set_of, finset_sup_apply, nnreal.coe_lt_coe,
    finset.sup_lt_iff (show ‚ä• < r, from hr), ‚Üênnreal.coe_lt_coe, subtype.coe_mk],
end

lemma ball_finset_sup (p : Œπ ‚Üí seminorm ùïú E) (s : finset Œπ) (x : E) {r : ‚Ñù} (hr : 0 < r) :
  ball (s.sup p) x r = s.inf (Œª i, ball (p i) x r) :=
begin
  rw finset.inf_eq_infi,
  exact ball_finset_sup_eq_Inter _ _ _ hr,
end

lemma ball_smul_ball (p : seminorm ùïú E) (r‚ÇÅ r‚ÇÇ : ‚Ñù) :
  metric.ball (0 : ùïú) r‚ÇÅ ‚Ä¢ p.ball 0 r‚ÇÇ ‚äÜ p.ball 0 (r‚ÇÅ * r‚ÇÇ) :=
begin
  rw set.subset_def,
  intros x hx,
  rw set.mem_smul at hx,
  rcases hx with ‚ü®a, y, ha, hy, hx‚ü©,
  rw [‚Üêhx, mem_ball_zero, seminorm.smul],
  exact mul_lt_mul'' (mem_ball_zero_iff.mp ha) (p.mem_ball_zero.mp hy) (norm_nonneg a) (p.nonneg y),
end

@[simp] lemma ball_eq_emptyset (p : seminorm ùïú E) {x : E} {r : ‚Ñù} (hr : r ‚â§ 0) : p.ball x r = ‚àÖ :=
begin
  ext,
  rw [seminorm.mem_ball, set.mem_empty_eq, iff_false, not_lt],
  exact hr.trans (p.nonneg _),
end

end module
end add_comm_group
end semi_normed_ring

section normed_field
variables [normed_field ùïú] [add_comm_group E] [module ùïú E] (p : seminorm ùïú E) {A B : set E}
  {a : ùïú} {r : ‚Ñù} {x : E}

lemma smul_ball_zero {p : seminorm ùïú E} {k : ùïú} {r : ‚Ñù} (hk : 0 < ‚à•k‚à•) :
  k ‚Ä¢ p.ball 0 r = p.ball 0 (‚à•k‚à• * r) :=
begin
  ext,
  rw [set.mem_smul_set, seminorm.mem_ball_zero],
  split; intro h,
  { rcases h with ‚ü®y, hy, h‚ü©,
    rw [‚Üêh, seminorm.smul],
    rw seminorm.mem_ball_zero at hy,
    exact (mul_lt_mul_left hk).mpr hy },
  refine ‚ü®k‚Åª¬π ‚Ä¢ x, _, _‚ü©,
  { rw [seminorm.mem_ball_zero, seminorm.smul, norm_inv, ‚Üê(mul_lt_mul_left hk),
      ‚Üêmul_assoc, ‚Üê(div_eq_mul_inv ‚à•k‚à• ‚à•k‚à•), div_self (ne_of_gt hk), one_mul],
    exact h},
  rw [‚Üêsmul_assoc, smul_eq_mul, ‚Üêdiv_eq_mul_inv, div_self (norm_pos_iff.mp hk), one_smul],
end

lemma ball_zero_absorbs_ball_zero (p : seminorm ùïú E) {r‚ÇÅ r‚ÇÇ : ‚Ñù} (hr‚ÇÅ : 0 < r‚ÇÅ) :
  absorbs ùïú (p.ball 0 r‚ÇÅ) (p.ball 0 r‚ÇÇ) :=
begin
  by_cases hr‚ÇÇ : r‚ÇÇ ‚â§ 0,
  { rw ball_eq_emptyset p hr‚ÇÇ, exact absorbs_empty },
  rw [not_le] at hr‚ÇÇ,
  rcases exists_between hr‚ÇÅ with ‚ü®r, hr, hr'‚ü©,
  refine ‚ü®r‚ÇÇ/r, div_pos hr‚ÇÇ hr, _‚ü©,
  simp_rw set.subset_def,
  intros a ha x hx,
  have ha' : 0 < ‚à•a‚à• := lt_of_lt_of_le (div_pos hr‚ÇÇ hr) ha,
  rw [smul_ball_zero ha', p.mem_ball_zero],
  rw p.mem_ball_zero at hx,
  rw div_le_iff hr at ha,
  exact hx.trans (lt_of_le_of_lt ha ((mul_lt_mul_left ha').mpr hr')),
end

/-- Seminorm-balls at the origin are absorbent. -/
protected lemma absorbent_ball_zero (hr : 0 < r) : absorbent ùïú (ball p (0 : E) r) :=
begin
  rw absorbent_iff_nonneg_lt,
  rintro x,
  have hxr : 0 ‚â§ p x/r := div_nonneg (p.nonneg _) hr.le,
  refine ‚ü®p x/r, hxr, Œª a ha, _‚ü©,
  have ha‚ÇÄ : 0 < ‚à•a‚à• := hxr.trans_lt ha,
  refine ‚ü®a‚Åª¬π ‚Ä¢ x, _, smul_inv_smul‚ÇÄ (norm_pos_iff.1 ha‚ÇÄ) x‚ü©,
  rwa [mem_ball_zero, p.smul, norm_inv, inv_mul_lt_iff ha‚ÇÄ, ‚Üêdiv_lt_iff hr],
end

/-- Seminorm-balls containing the origin are absorbent. -/
protected lemma absorbent_ball (hpr : p x < r) : absorbent ùïú (ball p x r) :=
begin
  refine (p.absorbent_ball_zero $ sub_pos.2 hpr).subset (Œª y hy, _),
  rw p.mem_ball_zero at hy,
  exact p.mem_ball.2 ((p.sub_le _ _).trans_lt $ add_lt_of_lt_sub_right hy),
end

lemma symmetric_ball_zero (r : ‚Ñù) (hx : x ‚àà ball p 0 r) : -x ‚àà ball p 0 r :=
balanced_ball_zero p r (-1) (by rw [norm_neg, norm_one]) ‚ü®x, hx, by rw [neg_smul, one_smul]‚ü©

@[simp]
lemma neg_ball (p : seminorm ùïú E) (r : ‚Ñù) (x : E) :
  -ball p x r = ball p (-x) r :=
by { ext, rw [mem_neg, mem_ball, mem_ball, ‚Üêneg_add', sub_neg_eq_add, p.neg], }

@[simp]
lemma smul_ball_preimage (p : seminorm ùïú E) (y : E) (r : ‚Ñù) (a : ùïú) (ha : a ‚â† 0) :
  ((‚Ä¢) a) ‚Åª¬π' p.ball y r = p.ball (a‚Åª¬π ‚Ä¢ y) (r / ‚à•a‚à•) :=
set.ext $ Œª _, by rw [mem_preimage, mem_ball, mem_ball,
  lt_div_iff (norm_pos_iff.mpr ha), mul_comm, ‚Üêp.smul, smul_sub, smul_inv_smul‚ÇÄ ha]

end normed_field

section convex
variables [normed_field ùïú] [add_comm_group E] [normed_space ‚Ñù ùïú] [module ùïú E]

section has_smul
variables [has_smul ‚Ñù E] [is_scalar_tower ‚Ñù ùïú E] (p : seminorm ùïú E)

/-- A seminorm is convex. Also see `convex_on_norm`. -/
protected lemma convex_on : convex_on ‚Ñù univ p :=
begin
  refine ‚ü®convex_univ, Œª x y _ _ a b ha hb hab, _‚ü©,
  calc p (a ‚Ä¢ x + b ‚Ä¢ y) ‚â§ p (a ‚Ä¢ x) + p (b ‚Ä¢ y) : p.add_le _ _
    ... = ‚à•a ‚Ä¢ (1 : ùïú)‚à• * p x + ‚à•b ‚Ä¢ (1 : ùïú)‚à• * p y
        : by rw [‚Üêp.smul, ‚Üêp.smul, smul_one_smul, smul_one_smul]
    ... = a * p x + b * p y
        : by rw [norm_smul, norm_smul, norm_one, mul_one, mul_one, real.norm_of_nonneg ha,
            real.norm_of_nonneg hb],
end

end has_smul

section module
variables [module ‚Ñù E] [is_scalar_tower ‚Ñù ùïú E] (p : seminorm ùïú E) (x : E) (r : ‚Ñù)

/-- Seminorm-balls are convex. -/
lemma convex_ball : convex ‚Ñù (ball p x r) :=
begin
  convert (p.convex_on.translate_left (-x)).convex_lt r,
  ext y,
  rw [preimage_univ, sep_univ, p.mem_ball, sub_eq_add_neg],
  refl,
end

end module
end convex
end seminorm

/-! ### The norm as a seminorm -/

section norm_seminorm
variables (ùïú) (E) [normed_field ùïú] [semi_normed_group E] [normed_space ùïú E] {r : ‚Ñù}

/-- The norm of a seminormed group as an add_monoid seminorm. -/
def norm_add_group_seminorm : add_group_seminorm E :=
‚ü®norm, norm_zero, norm_nonneg, norm_add_le, norm_neg‚ü©

@[simp] lemma coe_norm_add_group_seminorm : ‚áë(norm_add_group_seminorm E) = norm := rfl

/-- The norm of a seminormed group as a seminorm. -/
def norm_seminorm : seminorm ùïú E :=
{ smul' := norm_smul,
  ..(norm_add_group_seminorm E)}

@[simp] lemma coe_norm_seminorm : ‚áë(norm_seminorm ùïú E) = norm := rfl

@[simp] lemma ball_norm_seminorm : (norm_seminorm ùïú E).ball = metric.ball :=
by { ext x r y, simp only [seminorm.mem_ball, metric.mem_ball, coe_norm_seminorm, dist_eq_norm] }

variables {ùïú E} {x : E}

/-- Balls at the origin are absorbent. -/
lemma absorbent_ball_zero (hr : 0 < r) : absorbent ùïú (metric.ball (0 : E) r) :=
by { rw ‚Üêball_norm_seminorm ùïú, exact (norm_seminorm _ _).absorbent_ball_zero hr }

/-- Balls containing the origin are absorbent. -/
lemma absorbent_ball (hx : ‚à•x‚à• < r) : absorbent ùïú (metric.ball x r) :=
by { rw ‚Üêball_norm_seminorm ùïú, exact (norm_seminorm _ _).absorbent_ball hx }

/-- Balls at the origin are balanced. -/
lemma balanced_ball_zero : balanced ùïú (metric.ball (0 : E) r) :=
by { rw ‚Üêball_norm_seminorm ùïú, exact (norm_seminorm _ _).balanced_ball_zero r }

end norm_seminorm
