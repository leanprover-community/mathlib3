/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, YaÃ«l Dillies, Moritz Doll
-/
import analysis.locally_convex.basic
import data.real.pointwise
import data.real.sqrt
import topology.algebra.filter_basis
import topology.algebra.module.locally_convex

/-!
# Seminorms

This file defines seminorms.

A seminorm is a function to the reals which is positive-semidefinite, absolutely homogeneous, and
subadditive. They are closely related to convex sets and a topological vector space is locally
convex if and only if its topology is induced by a family of seminorms.

## Main declarations

For a module over a normed ring:
* `seminorm`: A function to the reals that is positive-semidefinite, absolutely homogeneous, and
  subadditive.
* `norm_seminorm ğ•œ E`: The norm on `E` as a seminorm.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## TODO

Define and show equivalence of two notions of local convexity for a
topological vector space over â„ or â„‚: that it has a local base of
balanced convex absorbent sets, and that it carries the initial
topology induced by a family of seminorms.

## Tags

seminorm, locally convex, LCTVS
-/

open normed_field set
open_locale big_operators nnreal pointwise topological_space

variables {R R' ğ•œ E F G Î¹ Î¹' : Type*}

/-- A seminorm on a module over a normed ring is a function to the reals that is positive
semidefinite, positive homogeneous, and subadditive. -/
structure seminorm (ğ•œ : Type*) (E : Type*) [semi_normed_ring ğ•œ] [add_monoid E] [has_scalar ğ•œ E] :=
(to_fun    : E â†’ â„)
(smul'     : âˆ€ (a : ğ•œ) (x : E), to_fun (a â€¢ x) = âˆ¥aâˆ¥ * to_fun x)
(triangle' : âˆ€ x y : E, to_fun (x + y) â‰¤ to_fun x + to_fun y)

namespace seminorm

section semi_normed_ring
variables [semi_normed_ring ğ•œ]

section add_monoid
variables [add_monoid E]

section has_scalar
variables [has_scalar ğ•œ E]

instance fun_like : fun_like (seminorm ğ•œ E) E (Î» _, â„) :=
{ coe := seminorm.to_fun, coe_injective' := Î» f g h, by cases f; cases g; congr' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`. -/
instance : has_coe_to_fun (seminorm ğ•œ E) (Î» _, E â†’ â„) := âŸ¨Î» p, p.to_funâŸ©

@[ext] lemma ext {p q : seminorm ğ•œ E} (h : âˆ€ x, (p : E â†’ â„) x = q x) : p = q := fun_like.ext p q h

instance : has_zero (seminorm ğ•œ E) :=
âŸ¨{ to_fun    := 0,
  smul'     := Î» _ _, (mul_zero _).symm,
  triangle' := Î» _ _, eq.ge (zero_add _) }âŸ©

@[simp] lemma coe_zero : â‡‘(0 : seminorm ğ•œ E) = 0 := rfl

@[simp] lemma zero_apply (x : E) : (0 : seminorm ğ•œ E) x = 0 := rfl

instance : inhabited (seminorm ğ•œ E) := âŸ¨0âŸ©

variables (p : seminorm ğ•œ E) (c : ğ•œ) (x y : E) (r : â„)

protected lemma smul : p (c â€¢ x) = âˆ¥câˆ¥ * p x := p.smul' _ _
protected lemma triangle : p (x + y) â‰¤ p x + p y := p.triangle' _ _

/-- Any action on `â„` which factors through `â„â‰¥0` applies to a seminorm. -/
instance [has_scalar R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  has_scalar R (seminorm ğ•œ E) :=
{ smul := Î» r p,
  { to_fun := Î» x, r â€¢ p x,
    smul' := Î» _ _, begin
      simp only [â†smul_one_smul â„â‰¥0 r (_ : â„), nnreal.smul_def, smul_eq_mul],
      rw [p.smul, mul_left_comm],
    end,
    triangle' := Î» _ _, begin
      simp only [â†smul_one_smul â„â‰¥0 r (_ : â„), nnreal.smul_def, smul_eq_mul],
      exact (mul_le_mul_of_nonneg_left (p.triangle _ _) (nnreal.coe_nonneg _)).trans_eq
        (mul_add _ _ _),
    end } }

instance [has_scalar R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  [has_scalar R' â„] [has_scalar R' â„â‰¥0] [is_scalar_tower R' â„â‰¥0 â„]
  [has_scalar R R'] [is_scalar_tower R R' â„] :
  is_scalar_tower R R' (seminorm ğ•œ E) :=
{ smul_assoc := Î» r a p, ext $ Î» x, smul_assoc r a (p x) }

lemma coe_smul [has_scalar R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p : seminorm ğ•œ E) : â‡‘(r â€¢ p) = r â€¢ p := rfl

@[simp] lemma smul_apply [has_scalar R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p : seminorm ğ•œ E) (x : E) : (r â€¢ p) x = r â€¢ p x := rfl

instance : has_add (seminorm ğ•œ E) :=
{ add := Î» p q,
  { to_fun := Î» x, p x + q x,
    smul' := Î» a x, by rw [p.smul, q.smul, mul_add],
    triangle' := Î» _ _, has_le.le.trans_eq (add_le_add (p.triangle _ _) (q.triangle _ _))
      (add_add_add_comm _ _ _ _) } }

lemma coe_add (p q : seminorm ğ•œ E) : â‡‘(p + q) = p + q := rfl

@[simp] lemma add_apply (p q : seminorm ğ•œ E) (x : E) : (p + q) x = p x + q x := rfl

instance : add_monoid (seminorm ğ•œ E) :=
fun_like.coe_injective.add_monoid _ rfl coe_add (Î» p n, coe_smul n p)

instance : ordered_cancel_add_comm_monoid (seminorm ğ•œ E) :=
fun_like.coe_injective.ordered_cancel_add_comm_monoid _ rfl coe_add (Î» p n, coe_smul n p)

instance [monoid R] [mul_action R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  mul_action R (seminorm ğ•œ E) :=
fun_like.coe_injective.mul_action _ coe_smul

variables (ğ•œ E)

/-- `coe_fn` as an `add_monoid_hom`. Helper definition for showing that `seminorm ğ•œ E` is
a module. -/
@[simps]
def coe_fn_add_monoid_hom : add_monoid_hom (seminorm ğ•œ E) (E â†’ â„) := âŸ¨coe_fn, coe_zero, coe_addâŸ©

lemma coe_fn_add_monoid_hom_injective : function.injective (coe_fn_add_monoid_hom ğ•œ E) :=
show @function.injective (seminorm ğ•œ E) (E â†’ â„) coe_fn, from fun_like.coe_injective

variables {ğ•œ E}

instance [monoid R] [distrib_mul_action R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  distrib_mul_action R (seminorm ğ•œ E) :=
(coe_fn_add_monoid_hom_injective ğ•œ E).distrib_mul_action _ coe_smul

instance [semiring R] [module R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„] :
  module R (seminorm ğ•œ E) :=
(coe_fn_add_monoid_hom_injective ğ•œ E).module R _ coe_smul

-- TODO: define `has_Sup` too, from the skeleton at
-- https://github.com/leanprover-community/mathlib/pull/11329#issuecomment-1008915345
noncomputable instance : has_sup (seminorm ğ•œ E) :=
{ sup := Î» p q,
  { to_fun := p âŠ” q,
    triangle' := Î» x y, sup_le
      ((p.triangle x y).trans $ add_le_add le_sup_left le_sup_left)
      ((q.triangle x y).trans $ add_le_add le_sup_right le_sup_right),
    smul' := Î» x v, (congr_arg2 max (p.smul x v) (q.smul x v)).trans $
      (mul_max_of_nonneg _ _ $ norm_nonneg x).symm } }

@[simp] lemma coe_sup (p q : seminorm ğ•œ E) : â‡‘(p âŠ” q) = p âŠ” q := rfl
lemma sup_apply (p q : seminorm ğ•œ E) (x : E) : (p âŠ” q) x = p x âŠ” q x := rfl

lemma smul_sup [has_scalar R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p q : seminorm ğ•œ E) :
  r â€¢ (p âŠ” q) = r â€¢ p âŠ” r â€¢ q :=
have real.smul_max : âˆ€ x y : â„, r â€¢ max x y = max (r â€¢ x) (r â€¢ y),
from Î» x y, by simpa only [â†smul_eq_mul, â†nnreal.smul_def, smul_one_smul â„â‰¥0 r (_ : â„)]
                     using mul_max_of_nonneg x y (r â€¢ 1 : â„â‰¥0).prop,
ext $ Î» x, real.smul_max _ _

instance : partial_order (seminorm ğ•œ E) :=
  partial_order.lift _ fun_like.coe_injective

lemma le_def (p q : seminorm ğ•œ E) : p â‰¤ q â†” (p : E â†’ â„) â‰¤ q := iff.rfl
lemma lt_def (p q : seminorm ğ•œ E) : p < q â†” (p : E â†’ â„) < q := iff.rfl

noncomputable instance : semilattice_sup (seminorm ğ•œ E) :=
function.injective.semilattice_sup _ fun_like.coe_injective coe_sup

end has_scalar

section smul_with_zero
variables [smul_with_zero ğ•œ E] (p : seminorm ğ•œ E)

@[simp]
protected lemma zero : p 0 = 0 :=
calc p 0 = p ((0 : ğ•œ) â€¢ 0) : by rw zero_smul
...      = 0 : by rw [p.smul, norm_zero, zero_mul]

end smul_with_zero
end add_monoid

section module
variables [add_comm_group E] [add_comm_group F] [add_comm_group G]
variables [module ğ•œ E] [module ğ•œ F] [module ğ•œ G]
variables [has_scalar R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]

/-- Composition of a seminorm with a linear map is a seminorm. -/
def comp (p : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) : seminorm ğ•œ E :=
{ to_fun := Î» x, p(f x),
  smul' := Î» _ _, (congr_arg p (f.map_smul _ _)).trans (p.smul _ _),
  triangle' := Î» _ _, eq.trans_le (congr_arg p (f.map_add _ _)) (p.triangle _ _) }

lemma coe_comp (p : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) : â‡‘(p.comp f) = p âˆ˜ f := rfl

@[simp] lemma comp_apply (p : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) (x : E) : (p.comp f) x = p (f x) := rfl

@[simp] lemma comp_id (p : seminorm ğ•œ E) : p.comp linear_map.id = p :=
ext $ Î» _, rfl

@[simp] lemma comp_zero (p : seminorm ğ•œ F) : p.comp (0 : E â†’â‚—[ğ•œ] F) = 0 :=
ext $ Î» _, seminorm.zero _

@[simp] lemma zero_comp (f : E â†’â‚—[ğ•œ] F) : (0 : seminorm ğ•œ F).comp f = 0 :=
ext $ Î» _, rfl

lemma comp_comp (p : seminorm ğ•œ G) (g : F â†’â‚—[ğ•œ] G) (f : E â†’â‚—[ğ•œ] F) :
  p.comp (g.comp f) = (p.comp g).comp f :=
ext $ Î» _, rfl

lemma add_comp (p q : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) : (p + q).comp f = p.comp f + q.comp f :=
ext $ Î» _, rfl

lemma comp_triangle (p : seminorm ğ•œ F) (f g : E â†’â‚—[ğ•œ] F) : p.comp (f + g) â‰¤ p.comp f + p.comp g :=
Î» _, p.triangle _ _

lemma smul_comp (p : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) (c : R) : (c â€¢ p).comp f = c â€¢ (p.comp f) :=
ext $ Î» _, rfl

lemma comp_mono {p : seminorm ğ•œ F} {q : seminorm ğ•œ F} (f : E â†’â‚—[ğ•œ] F) (hp : p â‰¤ q) :
  p.comp f â‰¤ q.comp f := Î» _, hp _

/-- The composition as an `add_monoid_hom`. -/
@[simps] def pullback (f : E â†’â‚—[ğ•œ] F) : add_monoid_hom (seminorm ğ•œ F) (seminorm ğ•œ E) :=
âŸ¨Î» p, p.comp f, zero_comp f, Î» p q, add_comp p q fâŸ©

section norm_one_class
variables [norm_one_class ğ•œ] (p : seminorm ğ•œ E) (x y : E) (r : â„)

@[simp]
protected lemma neg : p (-x) = p x :=
calc p (-x) = p ((-1 : ğ•œ) â€¢ x) : by rw neg_one_smul
...         = p x : by rw [p.smul, norm_neg, norm_one, one_mul]

protected lemma sub_le : p (x - y) â‰¤ p x + p y :=
calc
  p (x - y)
      = p (x + -y) : by rw sub_eq_add_neg
  ... â‰¤ p x + p (-y) : p.triangle x (-y)
  ... = p x + p y : by rw p.neg

lemma nonneg : 0 â‰¤ p x :=
have h: 0 â‰¤ 2 * p x, from
calc 0 = p (x + (- x)) : by rw [add_neg_self, p.zero]
...    â‰¤ p x + p (-x)  : p.triangle _ _
...    = 2 * p x : by rw [p.neg, two_mul],
nonneg_of_mul_nonneg_left h zero_lt_two

lemma sub_rev : p (x - y) = p (y - x) := by rw [â†neg_sub, p.neg]

/-- The direct path from 0 to y is shorter than the path with x "inserted" in between. -/
lemma le_insert : p y â‰¤ p x + p (x - y) :=
calc p y = p (x - (x - y)) : by rw sub_sub_cancel
... â‰¤ p x + p (x - y) : p.sub_le _ _

/-- The direct path from 0 to x is shorter than the path with y "inserted" in between. -/
lemma le_insert' : p x â‰¤ p y + p (x - y) := by { rw sub_rev, exact le_insert _ _ _ }

instance : order_bot (seminorm ğ•œ E) := âŸ¨0, nonnegâŸ©

@[simp] lemma coe_bot : â‡‘(âŠ¥ : seminorm ğ•œ E) = 0 := rfl

lemma bot_eq_zero : (âŠ¥ : seminorm ğ•œ E) = 0 := rfl

lemma smul_le_smul {p q : seminorm ğ•œ E} {a b : â„â‰¥0} (hpq : p â‰¤ q) (hab : a â‰¤ b) :
  a â€¢ p â‰¤ b â€¢ q :=
begin
  simp_rw [le_def, pi.le_def, coe_smul],
  intros x,
  simp_rw [pi.smul_apply, nnreal.smul_def, smul_eq_mul],
  exact mul_le_mul hab (hpq x) (nonneg p x) (nnreal.coe_nonneg b),
end

lemma finset_sup_apply (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) :
  s.sup p x = â†‘(s.sup (Î» i, âŸ¨p i x, nonneg (p i) xâŸ©) : â„â‰¥0) :=
begin
  induction s using finset.cons_induction_on with a s ha ih,
  { rw [finset.sup_empty, finset.sup_empty, coe_bot, _root_.bot_eq_zero, pi.zero_apply,
        nonneg.coe_zero] },
  { rw [finset.sup_cons, finset.sup_cons, coe_sup, sup_eq_max, pi.sup_apply, sup_eq_max,
        nnreal.coe_max, subtype.coe_mk, ih] }
end

lemma finset_sup_le_sum (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) : s.sup p â‰¤ âˆ‘ i in s, p i :=
begin
  classical,
  refine finset.sup_le_iff.mpr _,
  intros i hi,
  rw [finset.sum_eq_sum_diff_singleton_add hi, le_add_iff_nonneg_left],
  exact bot_le,
end

end norm_one_class
end module
end semi_normed_ring

section semi_normed_comm_ring
variables [semi_normed_comm_ring ğ•œ] [add_comm_group E] [add_comm_group F] [module ğ•œ E] [module ğ•œ F]

lemma comp_smul (p : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) (c : ğ•œ) :
  p.comp (c â€¢ f) = âˆ¥câˆ¥â‚Š â€¢ p.comp f :=
ext $ Î» _, by rw [comp_apply, smul_apply, linear_map.smul_apply, p.smul, nnreal.smul_def,
  coe_nnnorm, smul_eq_mul, comp_apply]

lemma comp_smul_apply (p : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) (c : ğ•œ) (x : E) :
  p.comp (c â€¢ f) x = âˆ¥câˆ¥ * p (f x) := p.smul _ _

end semi_normed_comm_ring

section normed_field
variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]

private lemma bdd_below_range_add (x : E) (p q : seminorm ğ•œ E) :
  bdd_below (range (Î» (u : E), p u + q (x - u))) :=
by { use 0, rintro _ âŸ¨x, rflâŸ©, exact add_nonneg (p.nonneg _) (q.nonneg _) }

noncomputable instance : has_inf (seminorm ğ•œ E) :=
{ inf := Î» p q,
  { to_fun := Î» x, â¨… u : E, p u + q (x-u),
    triangle' := Î» x y, begin
      refine le_cinfi_add_cinfi (Î» u v, _),
      apply cinfi_le_of_le (bdd_below_range_add _ _ _) (v+u), dsimp only,
      convert add_le_add (p.triangle v u) (q.triangle (y-v) (x-u)) using 1,
      { rw show x + y - (v + u) = y - v + (x - u), by abel },
      { abel },
    end,
    smul' := Î» a x, begin
      obtain rfl | ha := eq_or_ne a 0,
      { simp_rw [norm_zero, zero_mul, zero_smul, zero_sub, seminorm.neg],
        refine cinfi_eq_of_forall_ge_of_forall_gt_exists_lt
          (Î» i, add_nonneg (p.nonneg _) (q.nonneg _))
          (Î» x hx, âŸ¨0, by rwa [p.zero, q.zero, add_zero]âŸ©) },
      simp_rw [real.mul_infi_of_nonneg (norm_nonneg a), mul_add, â†p.smul, â†q.smul, smul_sub],
      refine infi_congr ((â€¢) aâ»Â¹ : E â†’ E) (Î» u, âŸ¨a â€¢ u, inv_smul_smulâ‚€ ha uâŸ©) (Î» u, _),
      rw smul_inv_smulâ‚€ ha,
    end } }

@[simp] lemma inf_apply (p q : seminorm ğ•œ E) (x : E) : (p âŠ“ q) x = â¨… u : E, p u + q (x-u) := rfl

noncomputable instance : lattice (seminorm ğ•œ E) :=
{ inf := (âŠ“),
  inf_le_left := Î» p q x, begin
    apply cinfi_le_of_le (bdd_below_range_add _ _ _) x,
    simp only [sub_self, seminorm.zero, add_zero],
  end,
  inf_le_right := Î» p q x, begin
    apply cinfi_le_of_le (bdd_below_range_add _ _ _) (0:E),
    simp only [sub_self, seminorm.zero, zero_add, sub_zero],
  end,
  le_inf := Î» a b c hab hac x,
    le_cinfi $ Î» u, le_trans (a.le_insert' _ _) (add_le_add (hab _) (hac _)),
  ..seminorm.semilattice_sup }

lemma smul_inf [has_scalar R â„] [has_scalar R â„â‰¥0] [is_scalar_tower R â„â‰¥0 â„]
  (r : R) (p q : seminorm ğ•œ E) :
  r â€¢ (p âŠ“ q) = r â€¢ p âŠ“ r â€¢ q :=
begin
  ext,
  simp_rw [smul_apply, inf_apply, smul_apply, â†smul_one_smul â„â‰¥0 r (_ : â„), nnreal.smul_def,
    smul_eq_mul, real.mul_infi_of_nonneg (subtype.prop _), mul_add],
end

end normed_field

/-! ### Seminorm ball -/

section semi_normed_ring
variables [semi_normed_ring ğ•œ]

section add_comm_group
variables [add_comm_group E]

section has_scalar
variables [has_scalar ğ•œ E] (p : seminorm ğ•œ E)

/-- The ball of radius `r` at `x` with respect to seminorm `p` is the set of elements `y` with
`p (y - x) < `r`. -/
def ball (x : E) (r : â„) := { y : E | p (y - x) < r }

variables {x y : E} {r : â„}

@[simp] lemma mem_ball : y âˆˆ ball p x r â†” p (y - x) < r := iff.rfl

lemma mem_ball_zero : y âˆˆ ball p 0 r â†” p y < r := by rw [mem_ball, sub_zero]

lemma ball_zero_eq : ball p 0 r = { y : E | p y < r } := set.ext $ Î» x, p.mem_ball_zero

@[simp] lemma ball_zero' (x : E) (hr : 0 < r) : ball (0 : seminorm ğ•œ E) x r = set.univ :=
begin
  rw [set.eq_univ_iff_forall, ball],
  simp [hr],
end

lemma ball_smul (p : seminorm ğ•œ E) {c : nnreal} (hc : 0 < c) (r : â„) (x : E) :
  (c â€¢ p).ball x r = p.ball x (r / c) :=
by { ext, rw [mem_ball, mem_ball, smul_apply, nnreal.smul_def, smul_eq_mul, mul_comm,
  lt_div_iff (nnreal.coe_pos.mpr hc)] }

lemma ball_sup (p : seminorm ğ•œ E) (q : seminorm ğ•œ E) (e : E) (r : â„) :
  ball (p âŠ” q) e r = ball p e r âˆ© ball q e r :=
by simp_rw [ball, â†set.set_of_and, coe_sup, pi.sup_apply, sup_lt_iff]

lemma ball_finset_sup' (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (H : s.nonempty) (e : E) (r : â„) :
  ball (s.sup' H p) e r = s.inf' H (Î» i, ball (p i) e r) :=
begin
  induction H using finset.nonempty.cons_induction with a a s ha hs ih,
  { classical, simp },
  { rw [finset.sup'_cons hs, finset.inf'_cons hs, ball_sup, inf_eq_inter, ih] },
end

lemma ball_mono {p : seminorm ğ•œ E} {râ‚ râ‚‚ : â„} (h : râ‚ â‰¤ râ‚‚) : p.ball x râ‚ âŠ† p.ball x râ‚‚ :=
Î» _ (hx : _ < _), hx.trans_le h

lemma ball_antitone {p q : seminorm ğ•œ E} (h : q â‰¤ p) : p.ball x r âŠ† q.ball x r :=
Î» _, (h _).trans_lt

lemma ball_add_ball_subset (p : seminorm ğ•œ E) (râ‚ râ‚‚ : â„) (xâ‚ xâ‚‚ : E):
  p.ball (xâ‚ : E) râ‚ + p.ball (xâ‚‚ : E) râ‚‚ âŠ† p.ball (xâ‚ + xâ‚‚) (râ‚ + râ‚‚) :=
begin
  rintros x âŸ¨yâ‚, yâ‚‚, hyâ‚, hyâ‚‚, rflâŸ©,
  rw [mem_ball, add_sub_comm],
  exact (p.triangle _ _).trans_lt (add_lt_add hyâ‚ hyâ‚‚),
end

end has_scalar

section module

variables [module ğ•œ E]
variables [add_comm_group F] [module ğ•œ F]

lemma ball_comp (p : seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) (x : E) (r : â„) :
  (p.comp f).ball x r = f â»Â¹' (p.ball (f x) r) :=
begin
  ext,
  simp_rw [ball, mem_preimage, comp_apply, set.mem_set_of_eq, map_sub],
end

section norm_one_class
variables [norm_one_class ğ•œ] (p : seminorm ğ•œ E)

@[simp] lemma ball_bot {r : â„} (x : E) (hr : 0 < r) : ball (âŠ¥ : seminorm ğ•œ E) x r = set.univ :=
ball_zero' x hr

/-- Seminorm-balls at the origin are balanced. -/
lemma balanced_ball_zero (r : â„) : balanced ğ•œ (ball p 0 r) :=
begin
  rintro a ha x âŸ¨y, hy, hxâŸ©,
  rw [mem_ball_zero, â†hx, p.smul],
  calc _ â‰¤ p y : mul_le_of_le_one_left (p.nonneg _) ha
  ...    < r   : by rwa mem_ball_zero at hy,
end

lemma ball_finset_sup_eq_Inter (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) {r : â„} (hr : 0 < r) :
  ball (s.sup p) x r = â‹‚ (i âˆˆ s), ball (p i) x r :=
begin
  lift r to nnreal using hr.le,
  simp_rw [ball, Inter_set_of, finset_sup_apply, nnreal.coe_lt_coe,
    finset.sup_lt_iff (show âŠ¥ < r, from hr), â†nnreal.coe_lt_coe, subtype.coe_mk],
end

lemma ball_finset_sup (p : Î¹ â†’ seminorm ğ•œ E) (s : finset Î¹) (x : E) {r : â„}
  (hr : 0 < r) : ball (s.sup p) x r = s.inf (Î» i, ball (p i) x r) :=
begin
  rw finset.inf_eq_infi,
  exact ball_finset_sup_eq_Inter _ _ _ hr,
end

lemma ball_smul_ball (p : seminorm ğ•œ E) (râ‚ râ‚‚ : â„) :
  metric.ball (0 : ğ•œ) râ‚ â€¢ p.ball 0 râ‚‚ âŠ† p.ball 0 (râ‚ * râ‚‚) :=
begin
  rw set.subset_def,
  intros x hx,
  rw set.mem_smul at hx,
  rcases hx with âŸ¨a, y, ha, hy, hxâŸ©,
  rw [â†hx, mem_ball_zero, seminorm.smul],
  exact mul_lt_mul'' (mem_ball_zero_iff.mp ha) (p.mem_ball_zero.mp hy) (norm_nonneg a) (p.nonneg y),
end

end norm_one_class
end module
end add_comm_group
end semi_normed_ring

section normed_field
variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] (p : seminorm ğ•œ E) {A B : set E}
  {a : ğ•œ} {r : â„} {x : E}

/-- Seminorm-balls at the origin are absorbent. -/
protected lemma absorbent_ball_zero (hr : 0 < r) : absorbent ğ•œ (ball p (0 : E) r) :=
begin
  rw absorbent_iff_nonneg_lt,
  rintro x,
  have hxr : 0 â‰¤ p x/r := div_nonneg (p.nonneg _) hr.le,
  refine âŸ¨p x/r, hxr, Î» a ha, _âŸ©,
  have haâ‚€ : 0 < âˆ¥aâˆ¥ := hxr.trans_lt ha,
  refine âŸ¨aâ»Â¹ â€¢ x, _, smul_inv_smulâ‚€ (norm_pos_iff.1 haâ‚€) xâŸ©,
  rwa [mem_ball_zero, p.smul, norm_inv, inv_mul_lt_iff haâ‚€, â†div_lt_iff hr],
end

/-- Seminorm-balls containing the origin are absorbent. -/
protected lemma absorbent_ball (hpr : p x < r) : absorbent ğ•œ (ball p x r) :=
begin
  refine (p.absorbent_ball_zero $ sub_pos.2 hpr).subset (Î» y hy, _),
  rw p.mem_ball_zero at hy,
  exact p.mem_ball.2 ((p.sub_le _ _).trans_lt $ add_lt_of_lt_sub_right hy),
end

lemma symmetric_ball_zero (r : â„) (hx : x âˆˆ ball p 0 r) : -x âˆˆ ball p 0 r :=
balanced_ball_zero p r (-1) (by rw [norm_neg, norm_one]) âŸ¨x, hx, by rw [neg_smul, one_smul]âŸ©

@[simp]
lemma neg_ball (p : seminorm ğ•œ E) (r : â„) (x : E) :
  -ball p x r = ball p (-x) r :=
by { ext, rw [mem_neg, mem_ball, mem_ball, â†neg_add', sub_neg_eq_add, p.neg], }

@[simp]
lemma smul_ball_preimage (p : seminorm ğ•œ E) (y : E) (r : â„) (a : ğ•œ) (ha : a â‰  0) :
  ((â€¢) a) â»Â¹' p.ball y r = p.ball (aâ»Â¹ â€¢ y) (r / âˆ¥aâˆ¥) :=
set.ext $ Î» _, by rw [mem_preimage, mem_ball, mem_ball,
  lt_div_iff (norm_pos_iff.mpr ha), mul_comm, â†p.smul, smul_sub, smul_inv_smulâ‚€ ha]

end normed_field

section normed_linear_ordered_field
variables [normed_linear_ordered_field ğ•œ] [add_comm_group E] [normed_space â„ ğ•œ] [module ğ•œ E]

section has_scalar
variables [has_scalar â„ E] [is_scalar_tower â„ ğ•œ E] (p : seminorm ğ•œ E)

/-- A seminorm is convex. Also see `convex_on_norm`. -/
protected lemma convex_on : convex_on â„ univ p :=
begin
  refine âŸ¨convex_univ, Î» x y _ _ a b ha hb hab, _âŸ©,
  calc p (a â€¢ x + b â€¢ y) â‰¤ p (a â€¢ x) + p (b â€¢ y) : p.triangle _ _
    ... = âˆ¥a â€¢ (1 : ğ•œ)âˆ¥ * p x + âˆ¥b â€¢ (1 : ğ•œ)âˆ¥ * p y
        : by rw [â†p.smul, â†p.smul, smul_one_smul, smul_one_smul]
    ... = a * p x + b * p y
        : by rw [norm_smul, norm_smul, norm_one, mul_one, mul_one, real.norm_of_nonneg ha,
            real.norm_of_nonneg hb],
end

end has_scalar

section module
variables [module â„ E] [is_scalar_tower â„ ğ•œ E] (p : seminorm ğ•œ E) (x : E) (r : â„)

/-- Seminorm-balls are convex. -/
lemma convex_ball : convex â„ (ball p x r) :=
begin
  convert (p.convex_on.translate_left (-x)).convex_lt r,
  ext y,
  rw [preimage_univ, sep_univ, p.mem_ball, sub_eq_add_neg],
  refl,
end

end module
end normed_linear_ordered_field
end seminorm

/-! ### The norm as a seminorm -/

section norm_seminorm
variables (ğ•œ E) [normed_field ğ•œ] [semi_normed_group E] [normed_space ğ•œ E] {r : â„}

/-- The norm of a seminormed group as a seminorm. -/
def norm_seminorm : seminorm ğ•œ E := âŸ¨norm, norm_smul, norm_add_leâŸ©

@[simp] lemma coe_norm_seminorm : â‡‘(norm_seminorm ğ•œ E) = norm := rfl

@[simp] lemma ball_norm_seminorm : (norm_seminorm ğ•œ E).ball = metric.ball :=
by { ext x r y, simp only [seminorm.mem_ball, metric.mem_ball, coe_norm_seminorm, dist_eq_norm] }

variables {ğ•œ E} {x : E}

/-- Balls at the origin are absorbent. -/
lemma absorbent_ball_zero (hr : 0 < r) : absorbent ğ•œ (metric.ball (0 : E) r) :=
by { rw â†ball_norm_seminorm ğ•œ, exact (norm_seminorm _ _).absorbent_ball_zero hr }

/-- Balls containing the origin are absorbent. -/
lemma absorbent_ball (hx : âˆ¥xâˆ¥ < r) : absorbent ğ•œ (metric.ball x r) :=
by { rw â†ball_norm_seminorm ğ•œ, exact (norm_seminorm _ _).absorbent_ball hx }

/-- Balls at the origin are balanced. -/
lemma balanced_ball_zero [norm_one_class ğ•œ] : balanced ğ•œ (metric.ball (0 : E) r) :=
by { rw â†ball_norm_seminorm ğ•œ, exact (norm_seminorm _ _).balanced_ball_zero r }

end norm_seminorm

/-! ### Topology induced by a family of seminorms -/

namespace seminorm

section filter_basis

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E]

/-- A filter basis for the neighborhood filter of 0. -/
def seminorm_basis_zero (p : Î¹ â†’ seminorm ğ•œ E) : set (set E) :=
â‹ƒ (s : finset Î¹) r (hr : 0 < r), singleton $ ball (s.sup p) (0 : E) r

lemma seminorm_basis_zero_iff (p : Î¹ â†’ seminorm ğ•œ E) (U : set E) :
  U âˆˆ seminorm_basis_zero p â†” âˆƒ (i : finset Î¹) r (hr : 0 < r), U = ball (i.sup p) 0 r :=
by simp only [seminorm_basis_zero, mem_Union, mem_singleton_iff]

lemma seminorm_basis_zero_mem (p : Î¹ â†’ seminorm ğ•œ E) (i : finset Î¹) {r : â„} (hr : 0 < r) :
  (i.sup p).ball 0 r âˆˆ seminorm_basis_zero p :=
(seminorm_basis_zero_iff _ _).mpr âŸ¨i,_,hr,rflâŸ©

lemma seminorm_basis_zero_singleton_mem (p : Î¹ â†’ seminorm ğ•œ E) (i : Î¹) {r : â„} (hr : 0 < r) :
  (p i).ball 0 r âˆˆ seminorm_basis_zero p :=
(seminorm_basis_zero_iff _ _).mpr âŸ¨{i},_,hr, by rw finset.sup_singletonâŸ©

lemma seminorm_basis_zero_nonempty (p : Î¹ â†’ seminorm ğ•œ E) [nonempty Î¹] :
  (seminorm_basis_zero p).nonempty :=
begin
  let i := classical.arbitrary Î¹,
  refine set.nonempty_def.mpr âŸ¨ball (p i) 0 1, _âŸ©,
  exact seminorm_basis_zero_singleton_mem _ i zero_lt_one,
end

lemma seminorm_basis_zero_intersect (p : Î¹ â†’ seminorm ğ•œ E)
  (U V : set E) (hU : U âˆˆ seminorm_basis_zero p) (hV : V âˆˆ seminorm_basis_zero p) :
  âˆƒ (z : set E) (H : z âˆˆ (seminorm_basis_zero p)), z âŠ† U âˆ© V :=
begin
  classical,
  rcases (seminorm_basis_zero_iff p U).mp hU with âŸ¨s, râ‚, hrâ‚, hUâŸ©,
  rcases (seminorm_basis_zero_iff p V).mp hV with âŸ¨t, râ‚‚, hrâ‚‚, hVâŸ©,
  use ((s âˆª t).sup p).ball 0 (min râ‚ râ‚‚),
  refine âŸ¨seminorm_basis_zero_mem p (s âˆª t) (lt_min_iff.mpr âŸ¨hrâ‚, hrâ‚‚âŸ©), _âŸ©,
  rw [hU, hV, ball_finset_sup_eq_Inter _ _ _ (lt_min_iff.mpr âŸ¨hrâ‚, hrâ‚‚âŸ©),
    ball_finset_sup_eq_Inter _ _ _ hrâ‚, ball_finset_sup_eq_Inter _ _ _ hrâ‚‚],
  exact set.subset_inter
    (set.Interâ‚‚_mono' $ Î» i hi, âŸ¨i, finset.subset_union_left _ _ hi, ball_mono $ min_le_left _ _âŸ©)
    (set.Interâ‚‚_mono' $ Î» i hi, âŸ¨i, finset.subset_union_right _ _ hi, ball_mono $
    min_le_right _ _âŸ©),
end

lemma seminorm_basis_zero_zero (p : Î¹ â†’ seminorm ğ•œ E) (U) (hU : U âˆˆ seminorm_basis_zero p) :
  (0 : E) âˆˆ U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with âŸ¨Î¹', r, hr, hUâŸ©,
  rw [hU, mem_ball_zero, (Î¹'.sup p).zero],
  exact hr,
end

lemma seminorm_basis_zero_add (p : Î¹ â†’ seminorm ğ•œ E) (U) (hU : U âˆˆ seminorm_basis_zero p) :
  âˆƒ (V : set E) (H : V âˆˆ (seminorm_basis_zero p)), V + V âŠ† U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with âŸ¨s, r, hr, hUâŸ©,
  use (s.sup p).ball 0 (r/2),
  refine âŸ¨seminorm_basis_zero_mem p s (div_pos hr zero_lt_two), _âŸ©,
  refine set.subset.trans (ball_add_ball_subset (s.sup p) (r/2) (r/2) 0 0) _,
  rw [hU, add_zero, add_halves'],
end

lemma seminorm_basis_zero_neg (p : Î¹ â†’ seminorm ğ•œ E) (U) (hU' : U âˆˆ seminorm_basis_zero p) :
  âˆƒ (V : set E) (H : V âˆˆ (seminorm_basis_zero p)), V âŠ† (Î» (x : E), -x) â»Â¹' U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU' with âŸ¨s, r, hr, hUâŸ©,
  rw [hU, neg_preimage, neg_ball (s.sup p), neg_zero],
  exact âŸ¨U, hU', eq.subset hUâŸ©,
end

/-- The `add_group_filter_basis` induced by the filter basis `seminorm_basis_zero`. -/
def seminorm_add_group_filter_basis [nonempty Î¹]
  (p : Î¹ â†’ seminorm ğ•œ E) : add_group_filter_basis E :=
add_group_filter_basis_of_comm (seminorm_basis_zero p)
  (seminorm_basis_zero_nonempty p)
  (seminorm_basis_zero_intersect p)
  (seminorm_basis_zero_zero p)
  (seminorm_basis_zero_add p)
  (seminorm_basis_zero_neg p)

lemma seminorm_basis_zero_smul_right (p : Î¹ â†’ seminorm ğ•œ E) (v : E) (U : set E)
  (hU : U âˆˆ seminorm_basis_zero p) : âˆ€á¶  (x : ğ•œ) in ğ“ 0, x â€¢ v âˆˆ U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with âŸ¨s, r, hr, hUâŸ©,
  rw [hU, filter.eventually_iff],
  simp_rw [(s.sup p).mem_ball_zero, (s.sup p).smul],
  by_cases h : 0 < (s.sup p) v,
  { simp_rw (lt_div_iff h).symm,
    rw â†_root_.ball_zero_eq,
    exact metric.ball_mem_nhds 0 (div_pos hr h) },
  simp_rw [le_antisymm (not_lt.mp h) ((s.sup p).nonneg v), mul_zero, hr],
  exact is_open.mem_nhds is_open_univ (mem_univ 0),
end

variables [nonempty Î¹]

lemma seminorm_basis_zero_smul (p : Î¹ â†’ seminorm ğ•œ E) (U) (hU : U âˆˆ seminorm_basis_zero p) :
  âˆƒ (V : set ğ•œ) (H : V âˆˆ ğ“ (0 : ğ•œ)) (W : set E)
  (H : W âˆˆ (seminorm_add_group_filter_basis p).sets), V â€¢ W âŠ† U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with âŸ¨s, r, hr, hUâŸ©,
  refine âŸ¨metric.ball 0 r.sqrt, metric.ball_mem_nhds 0 (real.sqrt_pos.mpr hr), _âŸ©,
  refine âŸ¨(s.sup p).ball 0 r.sqrt, seminorm_basis_zero_mem p s (real.sqrt_pos.mpr hr), _âŸ©,
  refine set.subset.trans (ball_smul_ball (s.sup p) r.sqrt r.sqrt) _,
  rw [hU, real.mul_self_sqrt (le_of_lt hr)],
end

lemma seminorm_basis_zero_smul_left (p : Î¹ â†’ seminorm ğ•œ E) (x : ğ•œ) (U : set E)
  (hU : U âˆˆ seminorm_basis_zero p) : âˆƒ (V : set E)
  (H : V âˆˆ (seminorm_add_group_filter_basis p).sets), V âŠ† (Î» (y : E), x â€¢ y) â»Â¹' U :=
begin
  rcases (seminorm_basis_zero_iff p U).mp hU with âŸ¨s, r, hr, hUâŸ©,
  rw hU,
  by_cases h : x â‰  0,
  { rw [(s.sup p).smul_ball_preimage 0 r x h, smul_zero],
    use (s.sup p).ball 0 (r / âˆ¥xâˆ¥),
    exact âŸ¨seminorm_basis_zero_mem p s (div_pos hr (norm_pos_iff.mpr h)), subset.rflâŸ© },
  refine âŸ¨(s.sup p).ball 0 r, seminorm_basis_zero_mem p s hr, _âŸ©,
  simp only [not_ne_iff.mp h, subset_def, mem_ball_zero, hr, mem_univ, seminorm.zero,
    implies_true_iff, preimage_const_of_mem, zero_smul],
end

/-- The `module_filter_basis` induced by the filter basis `seminorm_basis_zero`. -/
def seminorm_module_filter_basis (p : Î¹ â†’ seminorm ğ•œ E) : module_filter_basis ğ•œ E :=
{ to_add_group_filter_basis := seminorm_add_group_filter_basis p,
  smul' := seminorm_basis_zero_smul p,
  smul_left' := seminorm_basis_zero_smul_left p,
  smul_right' := seminorm_basis_zero_smul_right p }

end filter_basis

section bounded

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] [add_comm_group F] [module ğ•œ F]

/-- The proposition that a linear map is bounded between spaces with families of seminorms. -/
def is_bounded (p : Î¹ â†’ seminorm ğ•œ E) (q : Î¹' â†’ seminorm ğ•œ F) (f : E â†’â‚—[ğ•œ] F) : Prop :=
  âˆ€ i, âˆƒ s : finset Î¹, âˆƒ C : â„â‰¥0, C â‰  0 âˆ§ (q i).comp f â‰¤ C â€¢ s.sup p

lemma is_bounded_const (Î¹' : Type*) [nonempty Î¹']
  {p : Î¹ â†’ seminorm ğ•œ E} {q : seminorm ğ•œ F} (f : E â†’â‚—[ğ•œ] F) :
  is_bounded p (Î» _ : Î¹', q) f â†” âˆƒ (s : finset Î¹) C : â„â‰¥0, C â‰  0 âˆ§ q.comp f â‰¤ C â€¢ s.sup p :=
by simp only [is_bounded, forall_const]

lemma const_is_bounded (Î¹ : Type*) [nonempty Î¹]
  {p : seminorm ğ•œ E} {q : Î¹' â†’ seminorm ğ•œ F} (f : E â†’â‚—[ğ•œ] F) :
  is_bounded (Î» _ : Î¹, p) q f â†” âˆ€ i, âˆƒ C : â„â‰¥0, C â‰  0 âˆ§ (q i).comp f â‰¤ C â€¢ p :=
begin
  dunfold is_bounded,
  split,
  { intros h i,
    rcases h i with âŸ¨s, C, hC, hâŸ©,
    exact âŸ¨C, hC, le_trans h (smul_le_smul (finset.sup_le (Î» _ _, le_rfl)) le_rfl)âŸ© },
  intros h i,
  use [{classical.arbitrary Î¹}],
  simp only [h, finset.sup_singleton],
end

lemma is_bounded_sup {p : Î¹ â†’ seminorm ğ•œ E} {q : Î¹' â†’ seminorm ğ•œ F}
  {f : E â†’â‚—[ğ•œ] F} (hf : is_bounded p q f) (s' : finset Î¹') :
  âˆƒ (C : â„â‰¥0) (s : finset Î¹), 0 < C âˆ§ (s'.sup q).comp f â‰¤ C â€¢ (s.sup p) :=
begin
  classical,
  by_cases hs' : Â¬s'.nonempty,
  { refine âŸ¨1, âˆ…, zero_lt_one, _âŸ©,
    rw [finset.not_nonempty_iff_eq_empty.mp hs', finset.sup_empty, bot_eq_zero, zero_comp],
    exact seminorm.nonneg _ },
  rw not_not at hs',
  choose fâ‚› fC hf using hf,
  use [s'.card â€¢ s'.sup fC, finset.bUnion s' fâ‚›],
  split,
  { refine nsmul_pos _ (ne_of_gt (finset.nonempty.card_pos hs')),
    cases finset.nonempty.bex hs' with j hj,
    exact lt_of_lt_of_le (zero_lt_iff.mpr (and.elim_left (hf j))) (finset.le_sup hj) },
  have hs : âˆ€ i : Î¹', i âˆˆ s' â†’ (q i).comp f â‰¤ s'.sup fC â€¢ ((finset.bUnion s' fâ‚›).sup p) :=
  begin
    intros i hi,
    refine le_trans (and.elim_right (hf i)) (smul_le_smul _ (finset.le_sup hi)),
    exact finset.sup_mono (finset.subset_bUnion_of_mem fâ‚› hi),
  end,
  refine le_trans (comp_mono f (finset_sup_le_sum q s')) _,
  simp_rw [â†pullback_apply, add_monoid_hom.map_sum, pullback_apply], --improve this
  refine le_trans (finset.sum_le_sum hs) _,
  rw [finset.sum_const, smul_assoc],
  exact le_rfl,
end

end bounded

section topology

variables [normed_field ğ•œ] [add_comm_group E] [module ğ•œ E] [add_comm_group F] [module ğ•œ F]
variables [nonempty Î¹] [nonempty Î¹']

/-- The proposition that the topology of `E` is induced by a family of seminorms `p`. -/
class with_seminorms (p : Î¹ â†’ seminorm ğ•œ E) [t : topological_space E] : Prop :=
(topology_eq_with_seminorms : t = (seminorm_module_filter_basis p).topology)

lemma with_seminorms_eq (p : Î¹ â†’ seminorm ğ•œ E) [t : topological_space E] [with_seminorms p] :
  t = ((seminorm_module_filter_basis p).topology) := with_seminorms.topology_eq_with_seminorms

/-- The topology of a `normed_space ğ•œ E` is induced by the seminorm `norm_seminorm ğ•œ E`. -/
instance norm_with_seminorms (ğ•œ E) [normed_field ğ•œ] [semi_normed_group E] [normed_space ğ•œ E] :
  with_seminorms (Î» (_ : fin 1), norm_seminorm ğ•œ E) :=
begin
  let p := Î» _ : fin 1, norm_seminorm ğ•œ E,
  refine âŸ¨topological_add_group.ext normed_top_group
    ((seminorm_add_group_filter_basis _).is_topological_add_group) _âŸ©,
  refine filter.has_basis.eq_of_same_basis metric.nhds_basis_ball _,
  rw â†ball_norm_seminorm ğ•œ E,
  refine filter.has_basis.to_has_basis (seminorm_add_group_filter_basis p).nhds_zero_has_basis _
    (Î» r hr, âŸ¨(norm_seminorm ğ•œ E).ball 0 r, seminorm_basis_zero_singleton_mem p 0 hr, rfl.subsetâŸ©),
  rintros U (hU : U âˆˆ seminorm_basis_zero p),
  rcases (seminorm_basis_zero_iff p U).mp hU with âŸ¨s, r, hr, hUâŸ©,
  use [r, hr],
  rw [hU, id.def],
  by_cases h : s.nonempty,
  { rw finset.sup_const h },
  rw [finset.not_nonempty_iff_eq_empty.mp h, finset.sup_empty, ball_bot _ hr],
  exact set.subset_univ _,
end

lemma continuous_from_bounded (p : Î¹ â†’ seminorm ğ•œ E) (q : Î¹' â†’ seminorm ğ•œ F)
  [uniform_space E] [uniform_add_group E] [with_seminorms p]
  [uniform_space F] [uniform_add_group F] [with_seminorms q]
  (f : E â†’â‚—[ğ•œ] F) (hf : is_bounded p q f) : continuous f :=
begin
  refine uniform_continuous.continuous _,
  refine add_monoid_hom.uniform_continuous_of_continuous_at_zero f.to_add_monoid_hom _,
  rw [f.to_add_monoid_hom_coe, continuous_at_def, f.map_zero, with_seminorms_eq p],
  intros U hU,
  rw [with_seminorms_eq q, add_group_filter_basis.nhds_zero_eq, filter_basis.mem_filter_iff] at hU,
  rcases hU with âŸ¨V, hV : V âˆˆ seminorm_basis_zero q, hUâŸ©,
  rcases (seminorm_basis_zero_iff q V).mp hV with âŸ¨sâ‚‚, r, hr, hVâŸ©,
  rw hV at hU,
  rw [(seminorm_add_group_filter_basis p).nhds_zero_eq, filter_basis.mem_filter_iff],
  rcases (is_bounded_sup hf sâ‚‚) with âŸ¨C, sâ‚, hC, hfâŸ©,
  refine âŸ¨(sâ‚.sup p).ball 0 (r/C),
    seminorm_basis_zero_mem p _ (div_pos hr (nnreal.coe_pos.mpr hC)), _âŸ©,
  refine subset.trans _ (preimage_mono hU),
  simp_rw [â†linear_map.map_zero f, â†ball_comp],
  refine subset.trans _ (ball_antitone hf),
  rw ball_smul (sâ‚.sup p) hC,
end

lemma cont_with_seminorms_normed_space (F) [semi_normed_group F] [normed_space ğ•œ F]
  [uniform_space E] [uniform_add_group E]
  (p : Î¹ â†’ seminorm ğ•œ E) [with_seminorms p] (f : E â†’â‚—[ğ•œ] F)
  (hf : âˆƒ (s : finset Î¹) C : â„â‰¥0, C â‰  0 âˆ§ (norm_seminorm ğ•œ F).comp f â‰¤ C â€¢ s.sup p) :
  continuous f :=
begin
  rw â†is_bounded_const (fin 1) at hf,
  exact continuous_from_bounded p (Î» _ : fin 1, norm_seminorm ğ•œ F) f hf,
end

lemma cont_normed_space_to_with_seminorms (E) [semi_normed_group E] [normed_space ğ•œ E]
  [uniform_space F] [uniform_add_group F]
  (q : Î¹ â†’ seminorm ğ•œ F) [with_seminorms q] (f : E â†’â‚—[ğ•œ] F)
  (hf : âˆ€ i : Î¹, âˆƒ C : â„â‰¥0, C â‰  0 âˆ§ (q i).comp f â‰¤ C â€¢ (norm_seminorm ğ•œ E)) : continuous f :=
begin
  rw â†const_is_bounded (fin 1) at hf,
  exact continuous_from_bounded (Î» _ : fin 1, norm_seminorm ğ•œ E) q f hf,
end

end topology

section locally_convex_space

open locally_convex_space

variables [nonempty Î¹] [normed_linear_ordered_field ğ•œ] [normed_space â„ ğ•œ]
  [add_comm_group E] [module ğ•œ E] [module â„ E] [is_scalar_tower â„ ğ•œ E] [topological_space E]
  [topological_add_group E]

lemma with_seminorms.to_locally_convex_space (p : Î¹ â†’ seminorm ğ•œ E) [with_seminorms p] :
  locally_convex_space â„ E :=
begin
  apply of_basis_zero â„ E id (Î» s, s âˆˆ seminorm_basis_zero p),
  { rw [with_seminorms_eq p, add_group_filter_basis.nhds_eq _, add_group_filter_basis.N_zero],
    exact filter_basis.has_basis _ },
  { intros s hs,
    change s âˆˆ set.Union _ at hs,
    simp_rw [set.mem_Union, set.mem_singleton_iff] at hs,
    rcases hs with âŸ¨I, r, hr, rflâŸ©,
    exact convex_ball _ _ _ }
end

end locally_convex_space

end seminorm

section normed_space

variables (ğ•œ) [normed_linear_ordered_field ğ•œ] [normed_space â„ ğ•œ] [semi_normed_group E]

/-- Not an instance since `ğ•œ` can't be inferred. See `normed_space.to_locally_convex_space` for a
slightly weaker instance version. -/
lemma normed_space.to_locally_convex_space' [normed_space ğ•œ E] [module â„ E]
  [is_scalar_tower â„ ğ•œ E] : locally_convex_space â„ E :=
seminorm.with_seminorms.to_locally_convex_space (Î» _ : fin 1, norm_seminorm ğ•œ E)

/-- See `normed_space.to_locally_convex_space'` for a slightly stronger version which is not an
instance. -/
instance normed_space.to_locally_convex_space [normed_space â„ E] :
  locally_convex_space â„ E :=
normed_space.to_locally_convex_space' â„

end normed_space
