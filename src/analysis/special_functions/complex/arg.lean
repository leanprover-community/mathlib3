/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import analysis.special_functions.trigonometric.inverse

/-!
# The argument of a complex number.

We define `arg : ‚ÑÇ ‚Üí ‚Ñù`, returing a real number in the range (-œÄ, œÄ],
such that for `x ‚â† 0`, `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
while `arg 0` defaults to `0`
-/

noncomputable theory

namespace complex

open_locale real topological_space
open filter

/-- `arg` returns values in the range (-œÄ, œÄ], such that for `x ‚â† 0`,
  `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
  `arg 0` defaults to `0` -/
noncomputable def arg (x : ‚ÑÇ) : ‚Ñù :=
if 0 ‚â§ x.re
then real.arcsin (x.im / x.abs)
else if 0 ‚â§ x.im
then real.arcsin ((-x).im / x.abs) + œÄ
else real.arcsin ((-x).im / x.abs) - œÄ

lemma arg_le_pi (x : ‚ÑÇ) : arg x ‚â§ œÄ :=
if hx‚ÇÅ : 0 ‚â§ x.re
then by rw [arg, if_pos hx‚ÇÅ];
  exact le_trans (real.arcsin_le_pi_div_two _) (le_of_lt (half_lt_self real.pi_pos))
else
  if hx‚ÇÇ : 0 ‚â§ x.im
  then by rw [arg, if_neg hx‚ÇÅ, if_pos hx‚ÇÇ, ‚Üê le_sub_iff_add_le, sub_self, real.arcsin_nonpos,
    neg_im, neg_div, neg_nonpos];
        exact div_nonneg hx‚ÇÇ (abs_nonneg _)
  else by rw [arg, if_neg hx‚ÇÅ, if_neg hx‚ÇÇ];
      exact sub_le_iff_le_add.2 (le_trans (real.arcsin_le_pi_div_two _)
        (by linarith [real.pi_pos]))

lemma neg_pi_lt_arg (x : ‚ÑÇ) : -œÄ < arg x :=
if hx‚ÇÅ : 0 ‚â§ x.re
then by rw [arg, if_pos hx‚ÇÅ];
  exact lt_of_lt_of_le (neg_lt_neg (half_lt_self real.pi_pos)) (real.neg_pi_div_two_le_arcsin _)
else
  have hx : x ‚â† 0, from Œª h, by simpa [h, lt_irrefl] using hx‚ÇÅ,
  if hx‚ÇÇ : 0 ‚â§ x.im
  then by { rw [arg, if_neg hx‚ÇÅ, if_pos hx‚ÇÇ, ‚Üê sub_lt_iff_lt_add'],
    refine lt_of_lt_of_le _ real.pi_pos.le,
    rw [neg_im, sub_lt_iff_lt_add', add_zero, neg_lt, neg_div, real.arcsin_neg, neg_neg],
    exact (real.arcsin_le_pi_div_two _).trans_lt (half_lt_self real.pi_pos) }
  else by rw [arg, if_neg hx‚ÇÅ, if_neg hx‚ÇÇ, lt_sub_iff_add_lt, neg_add_self, real.arcsin_pos,
    neg_im];
      exact div_pos (neg_pos.2 (lt_of_not_ge hx‚ÇÇ)) (abs_pos.2 hx)

lemma arg_eq_arg_neg_add_pi_of_im_nonneg_of_re_neg {x : ‚ÑÇ} (hxr : x.re < 0) (hxi : 0 ‚â§ x.im) :
  arg x = arg (-x) + œÄ :=
have 0 ‚â§ (-x).re, from le_of_lt $ by simpa [neg_pos],
by rw [arg, arg, if_neg (not_le.2 hxr), if_pos this, if_pos hxi, abs_neg]

lemma arg_eq_arg_neg_sub_pi_of_im_neg_of_re_neg {x : ‚ÑÇ} (hxr : x.re < 0) (hxi : x.im < 0) :
  arg x = arg (-x) - œÄ :=
have 0 ‚â§ (-x).re, from le_of_lt $ by simpa [neg_pos],
by rw [arg, arg, if_neg (not_le.2 hxr), if_neg (not_le.2 hxi), if_pos this, abs_neg]

@[simp] lemma arg_zero : arg 0 = 0 :=
by simp [arg, le_refl]

@[simp] lemma arg_one : arg 1 = 0 :=
by simp [arg, zero_le_one]

@[simp] lemma arg_neg_one : arg (-1) = œÄ :=
by simp [arg, le_refl, not_le.2 (@zero_lt_one ‚Ñù _ _)]

@[simp] lemma arg_I : arg I = œÄ / 2 :=
by simp [arg, le_refl]

@[simp] lemma arg_neg_I : arg (-I) = -(œÄ / 2) :=
by simp [arg, le_refl]

lemma sin_arg (x : ‚ÑÇ) : real.sin (arg x) = x.im / x.abs :=
by unfold arg; split_ifs;
  simp [sub_eq_add_neg, arg, real.sin_arcsin (abs_le.1 (abs_im_div_abs_le_one x)).1
    (abs_le.1 (abs_im_div_abs_le_one x)).2, real.sin_add, neg_div, real.arcsin_neg,
    real.sin_neg]

private lemma cos_arg_of_re_nonneg {x : ‚ÑÇ} (hx : x ‚â† 0) (hxr : 0 ‚â§ x.re) :
  real.cos (arg x) = x.re / x.abs :=
have 0 ‚â§ 1 - (x.im / abs x) ^ 2,
  from sub_nonneg.2 $ by rw [sq, ‚Üê _root_.abs_mul_self, _root_.abs_mul, ‚Üê sq];
  exact pow_le_one _ (_root_.abs_nonneg _) (abs_im_div_abs_le_one _),
by rw [eq_div_iff_mul_eq (mt abs_eq_zero.1 hx), ‚Üê real.mul_self_sqrt (abs_nonneg x),
    arg, if_pos hxr, real.cos_arcsin (abs_le.1 (abs_im_div_abs_le_one x)).1
    (abs_le.1 (abs_im_div_abs_le_one x)).2, ‚Üê real.sqrt_mul (abs_nonneg _), ‚Üê real.sqrt_mul this,
    sub_mul, div_pow, ‚Üê sq, div_mul_cancel _ (pow_ne_zero 2 (mt abs_eq_zero.1 hx)),
    one_mul, sq, mul_self_abs, norm_sq_apply, sq, add_sub_cancel, real.sqrt_mul_self hxr]

lemma cos_arg {x : ‚ÑÇ} (hx : x ‚â† 0) : real.cos (arg x) = x.re / x.abs :=
if hxr : 0 ‚â§ x.re then cos_arg_of_re_nonneg hx hxr
else
  have 0 ‚â§ (-x).re, from le_of_lt $ by simpa [neg_pos] using hxr,
  if hxi : 0 ‚â§ x.im
  then have 0 ‚â§ (-x).re, from le_of_lt $ by simpa [neg_pos] using hxr,
    by rw [arg_eq_arg_neg_add_pi_of_im_nonneg_of_re_neg (not_le.1 hxr) hxi, real.cos_add_pi,
        cos_arg_of_re_nonneg (neg_ne_zero.2 hx) this];
      simp [neg_div]
  else by rw [arg_eq_arg_neg_sub_pi_of_im_neg_of_re_neg (not_le.1 hxr) (not_le.1 hxi)];
    simp [sub_eq_add_neg, real.cos_add, neg_div, cos_arg_of_re_nonneg (neg_ne_zero.2 hx) this]

lemma tan_arg {x : ‚ÑÇ} : real.tan (arg x) = x.im / x.re :=
begin
  by_cases h : x = 0,
  { simp only [h, zero_div, complex.zero_im, complex.arg_zero, real.tan_zero, complex.zero_re] },
  rw [real.tan_eq_sin_div_cos, sin_arg, cos_arg h,
      div_div_div_cancel_right _ (mt abs_eq_zero.1 h)]
end

lemma arg_cos_add_sin_mul_I {x : ‚Ñù} (hx‚ÇÅ : -œÄ < x) (hx‚ÇÇ : x ‚â§ œÄ) :
  arg (cos x + sin x * I) = x :=
if hx‚ÇÉ : -(œÄ / 2) ‚â§ x ‚àß x ‚â§ œÄ / 2
then
  have hx‚ÇÑ : 0 ‚â§ (cos x + sin x * I).re,
    by simp; exact real.cos_nonneg_of_mem_Icc hx‚ÇÉ,
  by rw [arg, if_pos hx‚ÇÑ];
    simp [abs_cos_add_sin_mul_I, sin_of_real_re, real.arcsin_sin hx‚ÇÉ.1 hx‚ÇÉ.2]
else if hx‚ÇÑ : x < -(œÄ / 2)
then
  have hx‚ÇÖ : ¬¨0 ‚â§ (cos x + sin x * I).re :=
    suffices ¬¨ 0 ‚â§ real.cos x, by simpa,
    not_le.2 $ by rw ‚Üê real.cos_neg;
      apply real.cos_neg_of_pi_div_two_lt_of_lt; linarith,
  have hx‚ÇÜ : ¬¨0 ‚â§ (cos ‚Üëx + sin ‚Üëx * I).im :=
    suffices real.sin x < 0, by simpa,
    by apply real.sin_neg_of_neg_of_neg_pi_lt; linarith,
  suffices -œÄ + -real.arcsin (real.sin x) = x,
    by rw [arg, if_neg hx‚ÇÖ, if_neg hx‚ÇÜ];
    simpa [sub_eq_add_neg, add_comm, abs_cos_add_sin_mul_I, sin_of_real_re],
  by rw [‚Üê real.arcsin_neg, ‚Üê real.sin_add_pi, real.arcsin_sin]; try {simp [add_left_comm]};
    linarith
else
  have hx‚ÇÖ : œÄ / 2 < x, by cases not_and_distrib.1 hx‚ÇÉ; linarith,
  have hx‚ÇÜ : ¬¨0 ‚â§ (cos x + sin x * I).re :=
    suffices ¬¨0 ‚â§ real.cos x, by simpa,
    not_le.2 $ by apply real.cos_neg_of_pi_div_two_lt_of_lt; linarith,
  have hx‚Çá : 0 ‚â§ (cos x + sin x * I).im :=
    suffices 0 ‚â§ real.sin x, by simpa,
    by apply real.sin_nonneg_of_nonneg_of_le_pi; linarith,
  suffices œÄ - real.arcsin (real.sin x) = x,
    by rw [arg, if_neg hx‚ÇÜ, if_pos hx‚Çá];
      simpa [sub_eq_add_neg, add_comm, abs_cos_add_sin_mul_I, sin_of_real_re],
  by rw [‚Üê real.sin_pi_sub, real.arcsin_sin]; simp [sub_eq_add_neg]; linarith

lemma arg_eq_arg_iff {x y : ‚ÑÇ} (hx : x ‚â† 0) (hy : y ‚â† 0) :
  arg x = arg y ‚Üî (abs y / abs x : ‚ÑÇ) * x = y :=
have hax : abs x ‚â† 0, from (mt abs_eq_zero.1 hx),
have hay : abs y ‚â† 0, from (mt abs_eq_zero.1 hy),
‚ü®Œª h,
  begin
    have hcos := congr_arg real.cos h,
    rw [cos_arg hx, cos_arg hy, div_eq_div_iff hax hay] at hcos,
    have hsin := congr_arg real.sin h,
    rw [sin_arg, sin_arg, div_eq_div_iff hax hay] at hsin,
    apply complex.ext,
    { rw [mul_re, ‚Üê of_real_div, of_real_re, of_real_im, zero_mul, sub_zero, mul_comm,
        ‚Üê mul_div_assoc, hcos, mul_div_cancel _ hax] },
    { rw [mul_im, ‚Üê of_real_div, of_real_re, of_real_im, zero_mul, add_zero,
        mul_comm, ‚Üê mul_div_assoc, hsin, mul_div_cancel _ hax] }
  end,
Œª h,
  have hre : abs (y / x) * x.re = y.re,
    by rw ‚Üê of_real_div at h;
      simpa [-of_real_div, -is_R_or_C.of_real_div] using congr_arg re h,
  have hre' : abs (x / y) * y.re = x.re,
    by rw [‚Üê hre, abs_div, abs_div, ‚Üê mul_assoc, div_mul_div,
      mul_comm (abs _), div_self (mul_ne_zero hay hax), one_mul],
  have him : abs (y / x) * x.im = y.im,
    by rw ‚Üê of_real_div at h;
      simpa [-of_real_div, -is_R_or_C.of_real_div] using congr_arg im h,
  have him' : abs (x / y) * y.im = x.im,
    by rw [‚Üê him, abs_div, abs_div, ‚Üê mul_assoc, div_mul_div,
      mul_comm (abs _), div_self (mul_ne_zero hay hax), one_mul],
  have hxya : x.im / abs x = y.im / abs y,
    by rw [‚Üê him, abs_div, mul_comm, ‚Üê mul_div_comm, mul_div_cancel_left _ hay],
  have hnxya : (-x).im / abs x = (-y).im / abs y,
    by rw [neg_im, neg_im, neg_div, neg_div, hxya],
  if hxr : 0 ‚â§ x.re
  then
    have hyr : 0 ‚â§ y.re, from hre ‚ñ∏ mul_nonneg (abs_nonneg _) hxr,
    by simp [arg, *] at *
  else
    have hyr : ¬¨ 0 ‚â§ y.re, from Œª hyr, hxr $ hre' ‚ñ∏ mul_nonneg (abs_nonneg _) hyr,
    if hxi : 0 ‚â§ x.im
    then
      have hyi : 0 ‚â§ y.im, from him ‚ñ∏ mul_nonneg (abs_nonneg _) hxi,
      by simp [arg, *] at *
    else
      have hyi : ¬¨ 0 ‚â§ y.im, from Œª hyi, hxi $ him' ‚ñ∏ mul_nonneg (abs_nonneg _) hyi,
      by simp [arg, *] at *‚ü©

lemma arg_real_mul (x : ‚ÑÇ) {r : ‚Ñù} (hr : 0 < r) : arg (r * x) = arg x :=
if hx : x = 0 then by simp [hx]
else (arg_eq_arg_iff (mul_ne_zero (of_real_ne_zero.2 (ne_of_lt hr).symm) hx) hx).2 $
  by rw [abs_mul, abs_of_nonneg (le_of_lt hr), ‚Üê mul_assoc,
    of_real_mul, mul_comm (r : ‚ÑÇ), ‚Üê div_div_eq_div_mul,
    div_mul_cancel _ (of_real_ne_zero.2 (ne_of_lt hr).symm),
    div_self (of_real_ne_zero.2 (mt abs_eq_zero.1 hx)), one_mul]

lemma ext_abs_arg {x y : ‚ÑÇ} (h‚ÇÅ : x.abs = y.abs) (h‚ÇÇ : x.arg = y.arg) : x = y :=
if hy : y = 0 then by simp * at *
else have hx : x ‚â† 0, from Œª hx, by simp [*, eq_comm] at *,
  by rwa [arg_eq_arg_iff hx hy, h‚ÇÅ, div_self (of_real_ne_zero.2 (mt abs_eq_zero.1 hy)), one_mul]
    at h‚ÇÇ

lemma arg_of_real_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) : arg x = 0 :=
by simp [arg, hx]

lemma arg_eq_pi_iff {z : ‚ÑÇ} : arg z = œÄ ‚Üî z.re < 0 ‚àß z.im = 0 :=
begin
  by_cases h‚ÇÄ : z = 0, { simp [h‚ÇÄ, lt_irrefl, real.pi_ne_zero.symm] },
  have h‚ÇÄ' : (abs z : ‚ÑÇ) ‚â† 0, by simpa,
  rw [‚Üê arg_neg_one, arg_eq_arg_iff h‚ÇÄ (neg_ne_zero.2 one_ne_zero), abs_neg, abs_one,
    of_real_one, one_div, ‚Üê div_eq_inv_mul, div_eq_iff_mul_eq h‚ÇÄ', neg_one_mul,
    ext_iff, neg_im, of_real_im, neg_zero, @eq_comm _ z.im, and.congr_left_iff],
  rcases z with ‚ü®x, y‚ü©, simp only,
  rintro rfl,
  simp only [‚Üê of_real_def, of_real_eq_zero] at *,
  simp [‚Üê ne.le_iff_lt h‚ÇÄ, @neg_eq_iff_neg_eq _ _ _ x, @eq_comm _ (-x)]
end

lemma arg_of_real_of_neg {x : ‚Ñù} (hx : x < 0) : arg x = œÄ :=
arg_eq_pi_iff.2 ‚ü®hx, rfl‚ü©

lemma tendsto_arg_nhds_within_im_neg_of_re_neg_of_im_zero
  {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
  tendsto arg (ùìù[{z : ‚ÑÇ | z.im < 0}] z) (ùìù (-œÄ)) :=
begin
  suffices H :
    tendsto (Œª x : ‚ÑÇ, real.arcsin ((-x).im / x.abs) - œÄ) (ùìù[{z : ‚ÑÇ | z.im < 0}] z) (ùìù (-œÄ)),
  { refine H.congr' _,
    have : ‚àÄ·∂† x : ‚ÑÇ in ùìù z, x.re < 0, from continuous_re.tendsto z (gt_mem_nhds hre),
    filter_upwards [self_mem_nhds_within, mem_nhds_within_of_mem_nhds this],
    intros w him hre,
    rw [arg, if_neg hre.not_le, if_neg him.not_le] },
  convert (real.continuous_at_arcsin.comp_continuous_within_at
    ((continuous_im.continuous_at.comp_continuous_within_at continuous_within_at_neg).div
      continuous_abs.continuous_within_at _)).sub tendsto_const_nhds,
  { simp [him] },
  { lift z to ‚Ñù using him, simpa using hre.ne }
end

lemma continuous_within_at_arg_of_re_neg_of_im_zero
  {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
  continuous_within_at arg {z : ‚ÑÇ | 0 ‚â§ z.im} z :=
begin
  have : arg =·∂†[ùìù[{z : ‚ÑÇ | 0 ‚â§ z.im}] z] Œª x, real.arcsin ((-x).im / x.abs) + œÄ,
  { have : ‚àÄ·∂† x : ‚ÑÇ in ùìù z, x.re < 0, from continuous_re.tendsto z (gt_mem_nhds hre),
    filter_upwards [self_mem_nhds_within, mem_nhds_within_of_mem_nhds this],
    intros w him hre,
    rw [arg, if_neg hre.not_le, if_pos him] },
  refine continuous_within_at.congr_of_eventually_eq _ this _,
  { refine (real.continuous_at_arcsin.comp_continuous_within_at
      ((continuous_im.continuous_at.comp_continuous_within_at continuous_within_at_neg).div
        continuous_abs.continuous_within_at _)).add tendsto_const_nhds,
    lift z to ‚Ñù using him, simpa using hre.ne },
  { rw [arg, if_neg hre.not_le, if_pos him.ge] }
end

lemma tendsto_arg_nhds_within_im_nonneg_of_re_neg_of_im_zero
  {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
  tendsto arg (ùìù[{z : ‚ÑÇ | 0 ‚â§ z.im}] z) (ùìù œÄ) :=
by simpa only [arg_eq_pi_iff.2 ‚ü®hre, him‚ü©]
  using (continuous_within_at_arg_of_re_neg_of_im_zero hre him).tendsto

end complex
