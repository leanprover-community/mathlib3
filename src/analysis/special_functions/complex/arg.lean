/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import analysis.special_functions.trigonometric.inverse

/-!
# The argument of a complex number.

We define `arg : ‚ÑÇ ‚Üí ‚Ñù`, returing a real number in the range (-œÄ, œÄ],
such that for `x ‚â† 0`, `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
while `arg 0` defaults to `0`
-/

noncomputable theory

namespace complex

open_locale real topological_space
open filter set

/-- `arg` returns values in the range (-œÄ, œÄ], such that for `x ‚â† 0`,
  `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
  `arg 0` defaults to `0` -/
noncomputable def arg (x : ‚ÑÇ) : ‚Ñù :=
if 0 ‚â§ x.re
then real.arcsin (x.im / x.abs)
else if 0 ‚â§ x.im
then real.arcsin ((-x).im / x.abs) + œÄ
else real.arcsin ((-x).im / x.abs) - œÄ

lemma sin_arg (x : ‚ÑÇ) : real.sin (arg x) = x.im / x.abs :=
by unfold arg; split_ifs;
  simp [sub_eq_add_neg, arg, real.sin_arcsin (abs_le.1 (abs_im_div_abs_le_one x)).1
    (abs_le.1 (abs_im_div_abs_le_one x)).2, real.sin_add, neg_div, real.arcsin_neg,
    real.sin_neg]

lemma cos_arg {x : ‚ÑÇ} (hx : x ‚â† 0) : real.cos (arg x) = x.re / x.abs :=
begin
  have habs : 0 < abs x := abs_pos.2 hx,
  have him : |im x / abs x| ‚â§ 1,
  { rw [_root_.abs_div, abs_abs], exact div_le_one_of_le x.abs_im_le_abs x.abs_nonneg },
  rw abs_le at him,
  rw arg, split_ifs with h‚ÇÅ h‚ÇÇ h‚ÇÇ,
  { rw [real.cos_arcsin]; field_simp [real.sqrt_sq, habs.le, *] },
  { rw [real.cos_add_pi, real.cos_arcsin],
    { field_simp [real.sqrt_div (sq_nonneg _), real.sqrt_sq_eq_abs,
        _root_.abs_of_neg (not_le.1 h‚ÇÅ), *] },
    { simpa [neg_div] using him.2 },
    { simpa [neg_div, neg_le] using him.1 } },
  { rw [real.cos_sub_pi, real.cos_arcsin],
    { field_simp [real.sqrt_div (sq_nonneg _), real.sqrt_sq_eq_abs,
        _root_.abs_of_neg (not_le.1 h‚ÇÅ), *] },
    { simpa [neg_div] using him.2 },
    { simpa [neg_div, neg_le] using him.1 } }
end

@[simp] lemma abs_mul_exp_arg_mul_I (x : ‚ÑÇ) : ‚Üë(abs x) * exp (arg x * I) = x :=
begin
  rcases eq_or_ne x 0 with (rfl|hx),
  { simp },
  { have : abs x ‚â† 0 := abs_ne_zero.2 hx,
    ext; field_simp [sin_arg, cos_arg hx, this, mul_comm (abs x)] }
end

@[simp] lemma abs_mul_cos_add_sin_mul_I (x : ‚ÑÇ) :
  (abs x * (cos (arg x) + sin (arg x) * I) : ‚ÑÇ) = x :=
by rw [‚Üê exp_mul_I, abs_mul_exp_arg_mul_I]

@[simp] lemma range_exp_mul_I : range (Œª x : ‚Ñù, exp (x * I)) = metric.sphere 0 1 :=
begin
  simp only [metric.sphere, dist_eq, sub_zero],
  refine (range_subset_iff.2 $ Œª x, _).antisymm (Œª z (hz : abs z = 1), _),
  { exact abs_exp_of_real_mul_I _ },
  { refine ‚ü®arg z, _‚ü©,
    calc exp (arg z * I) = abs z * exp (arg z * I) : by rw [hz, of_real_one, one_mul]
    ... = z : abs_mul_exp_arg_mul_I z }
end

lemma arg_mul_cos_add_sin_mul_I {r : ‚Ñù} (hr : 0 < r) {Œ∏ : ‚Ñù} (hŒ∏ : Œ∏ ‚àà Ioc (-œÄ) œÄ) :
  arg (r * (cos Œ∏ + sin Œ∏ * I)) = Œ∏ :=
begin
  have hœÄ := real.pi_pos,
  simp only [arg, abs_mul, abs_cos_add_sin_mul_I, abs_of_nonneg hr.le, mul_one],
  simp only [of_real_mul_re, of_real_mul_im, neg_im, ‚Üê of_real_cos, ‚Üê of_real_sin,
    ‚Üê mk_eq_add_mul_I, neg_div, mul_div_cancel_left _ hr.ne',
    mul_nonneg_iff_right_nonneg_of_pos hr],
  by_cases h‚ÇÅ : Œ∏ ‚àà Icc (-(œÄ / 2)) (œÄ / 2),
  { rw if_pos, exacts [real.arcsin_sin' h‚ÇÅ, real.cos_nonneg_of_mem_Icc h‚ÇÅ] },
  { rw [mem_Icc, not_and_distrib, not_le, not_le] at h‚ÇÅ, cases h‚ÇÅ,
    { replace hŒ∏ := hŒ∏.1,
      have hcos : real.cos Œ∏ < 0,
      { rw [‚Üê neg_pos, ‚Üê real.cos_add_pi], refine real.cos_pos_of_mem_Ioo ‚ü®_, _‚ü©; linarith },
      have hsin : real.sin Œ∏ < 0 := real.sin_neg_of_neg_of_neg_pi_lt (by linarith) hŒ∏,
      rw [if_neg, if_neg, ‚Üê real.sin_add_pi, real.arcsin_sin, add_sub_cancel];
        [linarith, linarith, exact hsin.not_le, exact hcos.not_le] },
    { replace hŒ∏ := hŒ∏.2,
      have hcos : real.cos Œ∏ < 0 := real.cos_neg_of_pi_div_two_lt_of_lt h‚ÇÅ (by linarith),
      have hsin : 0 ‚â§ real.sin Œ∏ := real.sin_nonneg_of_mem_Icc ‚ü®by linarith, hŒ∏‚ü©,
      rw [if_neg, if_pos, ‚Üê real.sin_sub_pi, real.arcsin_sin, sub_add_cancel];
        [linarith, linarith, exact hsin, exact hcos.not_le] } }
end

lemma arg_cos_add_sin_mul_I {Œ∏ : ‚Ñù} (hŒ∏ : Œ∏ ‚àà Ioc (-œÄ) œÄ) :
  arg (cos Œ∏ + sin Œ∏ * I) = Œ∏ :=
by rw [‚Üê one_mul (_ + _), ‚Üê of_real_one, arg_mul_cos_add_sin_mul_I zero_lt_one hŒ∏]

@[simp] lemma arg_zero : arg 0 = 0 := by simp [arg, le_refl]

lemma ext_abs_arg {x y : ‚ÑÇ} (h‚ÇÅ : x.abs = y.abs) (h‚ÇÇ : x.arg = y.arg) : x = y :=
by rw [‚Üê abs_mul_exp_arg_mul_I x, ‚Üê abs_mul_exp_arg_mul_I y, h‚ÇÅ, h‚ÇÇ]

lemma ext_abs_arg_iff {x y : ‚ÑÇ} : x = y ‚Üî abs x = abs y ‚àß arg x = arg y :=
‚ü®Œª h, h ‚ñ∏ ‚ü®rfl, rfl‚ü©, and_imp.2 ext_abs_arg‚ü©

lemma arg_mem_Ioc (z : ‚ÑÇ) : arg z ‚àà Ioc (-œÄ) œÄ :=
begin
  have hœÄ : 0 < œÄ := real.pi_pos,
  rcases eq_or_ne z 0 with (rfl|hz), simp [hœÄ, hœÄ.le],
  rcases exists_unique_add_zsmul_mem_Ioc real.two_pi_pos (arg z) (-œÄ) with ‚ü®N, hN, -‚ü©,
  rw [two_mul, neg_add_cancel_left, ‚Üê two_mul, zsmul_eq_mul] at hN,
  rw [‚Üê abs_mul_cos_add_sin_mul_I z, ‚Üê cos_add_int_mul_two_pi _ N,
    ‚Üê sin_add_int_mul_two_pi _ N],
  simp only [‚Üê of_real_one, ‚Üê of_real_bit0, ‚Üê of_real_mul, ‚Üê of_real_add, ‚Üê of_real_int_cast],
  rwa [arg_mul_cos_add_sin_mul_I (abs_pos.2 hz) hN]
end

@[simp] lemma range_arg : range arg = Ioc (-œÄ) œÄ :=
(range_subset_iff.2 arg_mem_Ioc).antisymm (Œª x hx, ‚ü®_, arg_cos_add_sin_mul_I hx‚ü©)

lemma arg_le_pi (x : ‚ÑÇ) : arg x ‚â§ œÄ :=
(arg_mem_Ioc x).2

lemma neg_pi_lt_arg (x : ‚ÑÇ) : -œÄ < arg x :=
(arg_mem_Ioc x).1

@[simp] lemma arg_nonneg_iff {z : ‚ÑÇ} : 0 ‚â§ arg z ‚Üî 0 ‚â§ z.im :=
begin
  rcases eq_or_ne z 0 with (rfl|h‚ÇÄ), { simp },
  calc 0 ‚â§ arg z ‚Üî 0 ‚â§ real.sin (arg z) :
    ‚ü®Œª h, real.sin_nonneg_of_mem_Icc ‚ü®h, arg_le_pi z‚ü©,
      by { contrapose!, intro h, exact real.sin_neg_of_neg_of_neg_pi_lt h (neg_pi_lt_arg _) }‚ü©
  ... ‚Üî _ : by rw [sin_arg, le_div_iff (abs_pos.2 h‚ÇÄ), zero_mul]
end

@[simp] lemma arg_neg_iff {z : ‚ÑÇ} : arg z < 0 ‚Üî z.im < 0 :=
lt_iff_lt_of_le_iff_le arg_nonneg_iff

lemma arg_real_mul (x : ‚ÑÇ) {r : ‚Ñù} (hr : 0 < r) : arg (r * x) = arg x :=
begin
  rcases eq_or_ne x 0 with (rfl|hx), { rw mul_zero },
  conv_lhs { rw [‚Üê abs_mul_cos_add_sin_mul_I x, ‚Üê mul_assoc, ‚Üê of_real_mul,
    arg_mul_cos_add_sin_mul_I (mul_pos hr (abs_pos.2 hx)) x.arg_mem_Ioc] }
end

lemma arg_eq_arg_iff {x y : ‚ÑÇ} (hx : x ‚â† 0) (hy : y ‚â† 0) :
  arg x = arg y ‚Üî (abs y / abs x : ‚ÑÇ) * x = y :=
begin
  simp only [ext_abs_arg_iff, abs_mul, abs_div, abs_of_real, abs_abs,
    div_mul_cancel _ (abs_ne_zero.2 hx), eq_self_iff_true, true_and],
  rw [‚Üê of_real_div, arg_real_mul],
  exact div_pos (abs_pos.2 hy) (abs_pos.2 hx)
end

lemma arg_eq_arg_neg_add_pi_of_im_nonneg_of_re_neg {x : ‚ÑÇ} (hxr : x.re < 0) (hxi : 0 ‚â§ x.im) :
  arg x = arg (-x) + œÄ :=
have 0 ‚â§ (-x).re, from le_of_lt $ by simpa [neg_pos],
by rw [arg, arg, if_neg (not_le.2 hxr), if_pos this, if_pos hxi, abs_neg]

lemma arg_eq_arg_neg_sub_pi_of_im_neg_of_re_neg {x : ‚ÑÇ} (hxr : x.re < 0) (hxi : x.im < 0) :
  arg x = arg (-x) - œÄ :=
have 0 ‚â§ (-x).re, from le_of_lt $ by simpa [neg_pos],
by rw [arg, arg, if_neg (not_le.2 hxr), if_neg (not_le.2 hxi), if_pos this, abs_neg]

@[simp] lemma arg_one : arg 1 = 0 :=
by simp [arg, zero_le_one]

@[simp] lemma arg_neg_one : arg (-1) = œÄ :=
by simp [arg, le_refl, not_le.2 (@zero_lt_one ‚Ñù _ _)]

@[simp] lemma arg_I : arg I = œÄ / 2 :=
by simp [arg, le_refl]

@[simp] lemma arg_neg_I : arg (-I) = -(œÄ / 2) :=
by simp [arg, le_refl]

@[simp] lemma tan_arg (x : ‚ÑÇ) : real.tan (arg x) = x.im / x.re :=
begin
  by_cases h : x = 0,
  { simp only [h, zero_div, complex.zero_im, complex.arg_zero, real.tan_zero, complex.zero_re] },
  rw [real.tan_eq_sin_div_cos, sin_arg, cos_arg h,
      div_div_div_cancel_right _ (abs_ne_zero.2 h)]
end

lemma arg_of_real_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) : arg x = 0 :=
by simp [arg, hx]

lemma arg_eq_pi_iff {z : ‚ÑÇ} : arg z = œÄ ‚Üî z.re < 0 ‚àß z.im = 0 :=
begin
  by_cases h‚ÇÄ : z = 0, { simp [h‚ÇÄ, lt_irrefl, real.pi_ne_zero.symm] },
  split,
  { intro h, rw [‚Üê abs_mul_cos_add_sin_mul_I z, h], simp [h‚ÇÄ] },
  { cases z with x y, rintro ‚ü®h : x < 0, rfl : y = 0‚ü©,
    rw [‚Üê arg_neg_one, ‚Üê arg_real_mul (-1) (neg_pos.2 h)], simp [‚Üê of_real_def] }
end

lemma arg_of_real_of_neg {x : ‚Ñù} (hx : x < 0) : arg x = œÄ :=
arg_eq_pi_iff.2 ‚ü®hx, rfl‚ü©

lemma arg_eq_pi_div_two_iff {z : ‚ÑÇ} : arg z = œÄ / 2 ‚Üî z.re = 0 ‚àß 0 < z.im :=
begin
  by_cases h‚ÇÄ : z = 0, { simp [h‚ÇÄ, lt_irrefl, real.pi_div_two_pos.ne] },
  split,
  { intro h, rw [‚Üê abs_mul_cos_add_sin_mul_I z, h], simp [h‚ÇÄ] },
  { cases z with x y, rintro ‚ü®rfl : x = 0, hy : 0 < y‚ü©,
    rw [‚Üê arg_I, ‚Üê arg_real_mul I hy, of_real_mul', I_re, I_im, mul_zero, mul_one] }
end

lemma arg_eq_neg_pi_div_two_iff {z : ‚ÑÇ} : arg z = - (œÄ / 2) ‚Üî z.re = 0 ‚àß z.im < 0 :=
begin
  by_cases h‚ÇÄ : z = 0, { simp [h‚ÇÄ, lt_irrefl, real.pi_ne_zero] },
  split,
  { intro h, rw [‚Üê abs_mul_cos_add_sin_mul_I z, h], simp [h‚ÇÄ] },
  { cases z with x y, rintro ‚ü®rfl : x = 0, hy : y < 0‚ü©,
    rw [‚Üê arg_neg_I, ‚Üê arg_real_mul (-I) (neg_pos.2 hy), mk_eq_add_mul_I],
    simp }
end

lemma arg_of_re_nonneg {x : ‚ÑÇ} (hx : 0 ‚â§ x.re) : arg x = real.arcsin (x.im / x.abs) :=
if_pos hx

lemma arg_of_re_neg_of_im_nonneg {x : ‚ÑÇ} (hx_re : x.re < 0) (hx_im : 0 ‚â§ x.im) :
  arg x = real.arcsin ((-x).im / x.abs) + œÄ :=
by simp only [arg, hx_re.not_le, hx_im, if_true, if_false]

lemma arg_of_re_neg_of_im_neg {x : ‚ÑÇ} (hx_re : x.re < 0) (hx_im : x.im < 0) :
  arg x = real.arcsin ((-x).im / x.abs) - œÄ :=
by simp only [arg, hx_re.not_le, hx_im.not_le, if_false]

lemma arg_of_im_nonneg_of_ne_zero {z : ‚ÑÇ} (h‚ÇÅ : 0 ‚â§ z.im) (h‚ÇÇ : z ‚â† 0) :
  arg z = real.arccos (z.re / abs z) :=
by rw [‚Üê cos_arg h‚ÇÇ, real.arccos_cos (arg_nonneg_iff.2 h‚ÇÅ) (arg_le_pi _)]

lemma arg_of_im_pos {z : ‚ÑÇ} (hz : 0 < z.im) : arg z = real.arccos (z.re / abs z) :=
arg_of_im_nonneg_of_ne_zero hz.le (Œª h, hz.ne' $ h.symm ‚ñ∏ rfl)

lemma arg_of_im_neg {z : ‚ÑÇ} (hz : z.im < 0) : arg z = -real.arccos (z.re / abs z) :=
begin
  have h‚ÇÄ : z ‚â† 0, from mt (congr_arg im) hz.ne,
  rw [‚Üê cos_arg h‚ÇÄ, ‚Üê real.cos_neg, real.arccos_cos, neg_neg],
  exacts [neg_nonneg.2 (arg_neg_iff.2 hz).le, neg_le.2 (neg_pi_lt_arg z).le]
end

section continuity

variables {x z : ‚ÑÇ}

lemma arg_eq_nhds_of_re_pos (hx : 0 < x.re) : arg =·∂†[ùìù x] Œª x, real.arcsin (x.im / x.abs) :=
((continuous_re.tendsto _).eventually (lt_mem_nhds hx)).mono $ Œª y hy, arg_of_re_nonneg hy.le

lemma arg_eq_nhds_of_re_neg_of_im_pos (hx_re : x.re < 0) (hx_im : 0 < x.im) :
  arg =·∂†[ùìù x] Œª x, real.arcsin ((-x).im / x.abs) + œÄ :=
begin
  suffices h_forall_nhds : ‚àÄ·∂† (y : ‚ÑÇ) in (ùìù x), y.re < 0 ‚àß 0 < y.im,
    from h_forall_nhds.mono (Œª y hy, arg_of_re_neg_of_im_nonneg hy.1 hy.2.le),
  refine is_open.eventually_mem _ (‚ü®hx_re, hx_im‚ü© : x.re < 0 ‚àß 0 < x.im),
  exact is_open.and (is_open_lt continuous_re continuous_zero)
    (is_open_lt continuous_zero continuous_im),
end

lemma arg_eq_nhds_of_re_neg_of_im_neg (hx_re : x.re < 0) (hx_im : x.im < 0) :
  arg =·∂†[ùìù x] Œª x, real.arcsin ((-x).im / x.abs) - œÄ :=
begin
  suffices h_forall_nhds : ‚àÄ·∂† (y : ‚ÑÇ) in (ùìù x), y.re < 0 ‚àß y.im < 0,
    from h_forall_nhds.mono (Œª y hy, arg_of_re_neg_of_im_neg hy.1 hy.2),
  refine is_open.eventually_mem _ (‚ü®hx_re, hx_im‚ü© : x.re < 0 ‚àß x.im < 0),
  exact is_open.and (is_open_lt continuous_re continuous_zero)
    (is_open_lt continuous_im continuous_zero),
end

lemma arg_eq_nhds_of_im_pos (hz : 0 < im z) :
  arg =·∂†[ùìù z] Œª x, real.arccos (x.re / abs x) :=
((continuous_im.tendsto _).eventually (lt_mem_nhds hz)).mono $ Œª x, arg_of_im_pos

lemma arg_eq_nhds_of_im_neg (hz : im z < 0) :
  arg =·∂†[ùìù z] Œª x, -real.arccos (x.re / abs x) :=
((continuous_im.tendsto _).eventually (gt_mem_nhds hz)).mono $ Œª x, arg_of_im_neg

lemma continuous_at_arg (h : 0 < x.re ‚à® x.im ‚â† 0) : continuous_at arg x :=
begin
  have h‚ÇÄ : abs x ‚â† 0, { rw abs_ne_zero, rintro rfl, simpa using h },
  rw [‚Üê lt_or_lt_iff_ne] at h,
  rcases h with (hx_re|hx_im|hx_im),
  exacts [(real.continuous_at_arcsin.comp (continuous_im.continuous_at.div
    continuous_abs.continuous_at h‚ÇÄ)).congr (arg_eq_nhds_of_re_pos hx_re).symm,
    (real.continuous_arccos.continuous_at.comp (continuous_re.continuous_at.div
      continuous_abs.continuous_at h‚ÇÄ)).neg.congr (arg_eq_nhds_of_im_neg hx_im).symm,
    (real.continuous_arccos.continuous_at.comp (continuous_re.continuous_at.div
      continuous_abs.continuous_at h‚ÇÄ)).congr (arg_eq_nhds_of_im_pos hx_im).symm]
end

lemma tendsto_arg_nhds_within_im_neg_of_re_neg_of_im_zero
  {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
  tendsto arg (ùìù[{z : ‚ÑÇ | z.im < 0}] z) (ùìù (-œÄ)) :=
begin
  suffices H :
    tendsto (Œª x : ‚ÑÇ, real.arcsin ((-x).im / x.abs) - œÄ) (ùìù[{z : ‚ÑÇ | z.im < 0}] z) (ùìù (-œÄ)),
  { refine H.congr' _,
    have : ‚àÄ·∂† x : ‚ÑÇ in ùìù z, x.re < 0, from continuous_re.tendsto z (gt_mem_nhds hre),
    filter_upwards [self_mem_nhds_within, mem_nhds_within_of_mem_nhds this],
    intros w him hre,
    rw [arg, if_neg hre.not_le, if_neg him.not_le] },
  convert (real.continuous_at_arcsin.comp_continuous_within_at
    ((continuous_im.continuous_at.comp_continuous_within_at continuous_within_at_neg).div
      continuous_abs.continuous_within_at _)).sub tendsto_const_nhds,
  { simp [him] },
  { lift z to ‚Ñù using him, simpa using hre.ne }
end

lemma continuous_within_at_arg_of_re_neg_of_im_zero
  {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
  continuous_within_at arg {z : ‚ÑÇ | 0 ‚â§ z.im} z :=
begin
  have : arg =·∂†[ùìù[{z : ‚ÑÇ | 0 ‚â§ z.im}] z] Œª x, real.arcsin ((-x).im / x.abs) + œÄ,
  { have : ‚àÄ·∂† x : ‚ÑÇ in ùìù z, x.re < 0, from continuous_re.tendsto z (gt_mem_nhds hre),
    filter_upwards [self_mem_nhds_within, mem_nhds_within_of_mem_nhds this],
    intros w him hre,
    rw [arg, if_neg hre.not_le, if_pos him] },
  refine continuous_within_at.congr_of_eventually_eq _ this _,
  { refine (real.continuous_at_arcsin.comp_continuous_within_at
      ((continuous_im.continuous_at.comp_continuous_within_at continuous_within_at_neg).div
        continuous_abs.continuous_within_at _)).add tendsto_const_nhds,
    lift z to ‚Ñù using him, simpa using hre.ne },
  { rw [arg, if_neg hre.not_le, if_pos him.ge] }
end

lemma tendsto_arg_nhds_within_im_nonneg_of_re_neg_of_im_zero
  {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
  tendsto arg (ùìù[{z : ‚ÑÇ | 0 ‚â§ z.im}] z) (ùìù œÄ) :=
by simpa only [arg_eq_pi_iff.2 ‚ü®hre, him‚ü©]
  using (continuous_within_at_arg_of_re_neg_of_im_zero hre him).tendsto

end continuity

end complex
