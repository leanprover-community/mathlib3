/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne, Benjamin Davidson
-/
import analysis.special_functions.complex.arg

/-!
# The complex `log` function

Basic properties, relationship with `exp`, and differentiability.
-/

noncomputable theory

namespace complex

open set filter

open_locale real topological_space

/-- Inverse of the `exp` function. Returns values such that `(log x).im > - Ï€` and `(log x).im â‰¤ Ï€`.
  `log 0 = 0`-/
@[pp_nodot] noncomputable def log (x : â„‚) : â„‚ := x.abs.log + arg x * I

lemma log_re (x : â„‚) : x.log.re = x.abs.log := by simp [log]

lemma log_im (x : â„‚) : x.log.im = x.arg := by simp [log]

lemma neg_pi_lt_log_im (x : â„‚) : -Ï€ < (log x).im := by simp only [log_im, neg_pi_lt_arg]
lemma log_im_le_pi (x : â„‚) : (log x).im â‰¤ Ï€ := by simp only [log_im, arg_le_pi]

lemma exp_log {x : â„‚} (hx : x â‰  0) : exp (log x) = x :=
by rw [log, exp_add_mul_I, â† of_real_sin, sin_arg, â† of_real_cos, cos_arg hx,
  â† of_real_exp, real.exp_log (abs_pos.2 hx), mul_add, of_real_div, of_real_div,
  mul_div_cancel' _ (of_real_ne_zero.2 (mt abs_eq_zero.1 hx)), â† mul_assoc,
  mul_div_cancel' _ (of_real_ne_zero.2 (mt abs_eq_zero.1 hx)), re_add_im]

@[simp] lemma range_exp : range exp = {0}á¶œ :=
set.ext $ Î» x, âŸ¨by { rintro âŸ¨x, rflâŸ©, exact exp_ne_zero x }, Î» hx, âŸ¨log x, exp_log hxâŸ©âŸ©

lemma exp_inj_of_neg_pi_lt_of_le_pi {x y : â„‚} (hxâ‚ : -Ï€ < x.im) (hxâ‚‚ : x.im â‰¤ Ï€)
  (hyâ‚ : - Ï€ < y.im) (hyâ‚‚ : y.im â‰¤ Ï€) (hxy : exp x = exp y) : x = y :=
by rw [exp_eq_exp_re_mul_sin_add_cos, exp_eq_exp_re_mul_sin_add_cos y] at hxy;
  exact complex.ext
    (real.exp_injective $
      by simpa [abs_mul, abs_cos_add_sin_mul_I] using congr_arg complex.abs hxy)
    (by simpa [(of_real_exp _).symm, - of_real_exp, arg_real_mul _ (real.exp_pos _),
      arg_cos_add_sin_mul_I hxâ‚ hxâ‚‚, arg_cos_add_sin_mul_I hyâ‚ hyâ‚‚] using congr_arg arg hxy)

lemma log_exp {x : â„‚} (hxâ‚ : -Ï€ < x.im) (hxâ‚‚: x.im â‰¤ Ï€) : log (exp x) = x :=
exp_inj_of_neg_pi_lt_of_le_pi
  (by rw log_im; exact neg_pi_lt_arg _)
  (by rw log_im; exact arg_le_pi _)
  hxâ‚ hxâ‚‚ (by rw [exp_log (exp_ne_zero _)])

lemma of_real_log {x : â„} (hx : 0 â‰¤ x) : (x.log : â„‚) = log x :=
complex.ext
  (by rw [log_re, of_real_re, abs_of_nonneg hx])
  (by rw [of_real_im, log_im, arg_of_real_of_nonneg hx])

lemma log_of_real_re (x : â„) : (log (x : â„‚)).re = real.log x := by simp [log_re]

@[simp] lemma log_zero : log 0 = 0 := by simp [log]

@[simp] lemma log_one : log 1 = 0 := by simp [log]

lemma log_neg_one : log (-1) = Ï€ * I := by simp [log]

lemma log_I : log I = Ï€ / 2 * I := by simp [log]

lemma log_neg_I : log (-I) = -(Ï€ / 2) * I := by simp [log]

lemma two_pi_I_ne_zero : (2 * Ï€ * I : â„‚) â‰  0 :=
by norm_num [real.pi_ne_zero, I_ne_zero]

lemma exp_eq_one_iff {x : â„‚} : exp x = 1 â†” âˆƒ n : â„¤, x = n * ((2 * Ï€) * I) :=
have real.exp (x.re) * real.cos (x.im) = 1 â†’ real.cos x.im â‰  -1,
  from Î» hâ‚ hâ‚‚, begin
    rw [hâ‚‚, mul_neg_eq_neg_mul_symm, mul_one, neg_eq_iff_neg_eq] at hâ‚,
    have := real.exp_pos x.re,
    rw â† hâ‚ at this,
    exact absurd this (by norm_num)
  end,
calc exp x = 1 â†” (exp x).re = 1 âˆ§ (exp x).im = 0 : by simp [complex.ext_iff]
  ... â†” real.cos x.im = 1 âˆ§ real.sin x.im = 0 âˆ§ x.re = 0 :
    begin
      rw exp_eq_exp_re_mul_sin_add_cos,
      simp [complex.ext_iff, cos_of_real_re, sin_of_real_re, exp_of_real_re,
        real.exp_ne_zero],
      split; finish [real.sin_eq_zero_iff_cos_eq]
    end
  ... â†” (âˆƒ n : â„¤, â†‘n * (2 * Ï€) = x.im) âˆ§ (âˆƒ n : â„¤, â†‘n * Ï€ = x.im) âˆ§ x.re = 0 :
    by rw [real.sin_eq_zero_iff, real.cos_eq_one_iff]
  ... â†” âˆƒ n : â„¤, x = n * ((2 * Ï€) * I) :
    âŸ¨Î» âŸ¨âŸ¨n, hnâŸ©, âŸ¨m, hmâŸ©, hâŸ©, âŸ¨n, by simp [complex.ext_iff, hn.symm, h]âŸ©,
      Î» âŸ¨n, hnâŸ©, âŸ¨âŸ¨n, by simp [hn]âŸ©, âŸ¨2 * n, by simp [hn, mul_comm, mul_assoc, mul_left_comm]âŸ©,
        by simp [hn]âŸ©âŸ©

lemma exp_eq_exp_iff_exp_sub_eq_one {x y : â„‚} : exp x = exp y â†” exp (x - y) = 1 :=
by rw [exp_sub, div_eq_one_iff_eq (exp_ne_zero _)]

lemma exp_eq_exp_iff_exists_int {x y : â„‚} : exp x = exp y â†” âˆƒ n : â„¤, x = y + n * ((2 * Ï€) * I) :=
by simp only [exp_eq_exp_iff_exp_sub_eq_one, exp_eq_one_iff, sub_eq_iff_eq_add']

@[simp] lemma countable_preimage_exp {s : set â„‚} : countable (exp â»Â¹' s) â†” countable s :=
begin
  refine âŸ¨Î» hs, _, Î» hs, _âŸ©,
  { refine ((hs.image exp).insert 0).mono _,
    rw [image_preimage_eq_inter_range, range_exp, â† diff_eq, â† union_singleton, diff_union_self],
    exact subset_union_left _ _ },
  { rw â† bUnion_preimage_singleton,
    refine hs.bUnion (Î» z hz, _),
    rcases em (âˆƒ w, exp w = z) with âŸ¨w, rflâŸ©|hne,
    { simp only [preimage, mem_singleton_iff, exp_eq_exp_iff_exists_int, set_of_exists],
      exact countable_Union (Î» m, countable_singleton _) },
    { push_neg at hne, simp [preimage, hne] } }
end

alias countable_preimage_exp â†” _ set.countable.preimage_cexp

/-- `complex.exp` as a `local_homeomorph` with `source = {z | -Ï€ < im z < Ï€}` and
`target = {z | 0 < re z} âˆª {z | im z â‰  0}`. This definition is used to prove that `complex.log`
is complex differentiable at all points but the negative real semi-axis. -/
def exp_local_homeomorph : local_homeomorph â„‚ â„‚ :=
local_homeomorph.of_continuous_open
{ to_fun := exp,
  inv_fun := log,
  source := {z : â„‚ | z.im âˆˆ Ioo (- Ï€) Ï€},
  target := {z : â„‚ | 0 < z.re} âˆª {z : â„‚ | z.im â‰  0},
  map_source' :=
    begin
      rintro âŸ¨x, yâŸ© âŸ¨hâ‚ : -Ï€ < y, hâ‚‚ : y < Ï€âŸ©,
      refine (not_or_of_imp $ Î» hz, _).symm,
      obtain rfl : y = 0,
      { rw exp_im at hz,
        simpa [(real.exp_pos _).ne', real.sin_eq_zero_iff_of_lt_of_lt hâ‚ hâ‚‚] using hz },
      rw [mem_set_of_eq, â† of_real_def, exp_of_real_re],
      exact real.exp_pos x
    end,
  map_target' := Î» z h,
    suffices 0 â‰¤ z.re âˆ¨ z.im â‰  0,
      by simpa [log_im, neg_pi_lt_arg, (arg_le_pi _).lt_iff_ne, arg_eq_pi_iff, not_and_distrib],
    h.imp (Î» h, le_of_lt h) id,
  left_inv' := Î» x hx, log_exp hx.1 (le_of_lt hx.2),
  right_inv' := Î» x hx, exp_log $ by { rintro rfl, simpa [lt_irrefl] using hx } }
continuous_exp.continuous_on is_open_map_exp (is_open_Ioo.preimage continuous_im)

lemma has_strict_deriv_at_log {x : â„‚} (h : 0 < x.re âˆ¨ x.im â‰  0) :
  has_strict_deriv_at log xâ»Â¹ x :=
have h0 :  x â‰  0, by { rintro rfl, simpa [lt_irrefl] using h },
exp_local_homeomorph.has_strict_deriv_at_symm h h0 $
  by simpa [exp_log h0] using has_strict_deriv_at_exp (log x)

lemma has_strict_fderiv_at_log_real {x : â„‚} (h : 0 < x.re âˆ¨ x.im â‰  0) :
  has_strict_fderiv_at log (xâ»Â¹ â€¢ (1 : â„‚ â†’L[â„] â„‚)) x :=
(has_strict_deriv_at_log h).complex_to_real_fderiv

lemma times_cont_diff_at_log {x : â„‚} (h : 0 < x.re âˆ¨ x.im â‰  0) {n : with_top â„•} :
  times_cont_diff_at â„‚ n log x :=
exp_local_homeomorph.times_cont_diff_at_symm_deriv (exp_ne_zero $ log x) h
  (has_deriv_at_exp _) times_cont_diff_exp.times_cont_diff_at

lemma tendsto_log_nhds_within_im_neg_of_re_neg_of_im_zero
  {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
  tendsto log (ð“[{z : â„‚ | z.im < 0}] z) (ð“ $ real.log (abs z) - Ï€ * I) :=
begin
  have := (continuous_of_real.continuous_at.comp_continuous_within_at
    (continuous_abs.continuous_within_at.log _)).tendsto.add
    (((continuous_of_real.tendsto _).comp $
    tendsto_arg_nhds_within_im_neg_of_re_neg_of_im_zero hre him).mul tendsto_const_nhds),
  convert this,
  { simp [sub_eq_add_neg] },
  { lift z to â„ using him, simpa using hre.ne }
end

lemma continuous_within_at_log_of_re_neg_of_im_zero
  {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
  continuous_within_at log {z : â„‚ | 0 â‰¤ z.im} z :=
begin
  have := (continuous_of_real.continuous_at.comp_continuous_within_at
    (continuous_abs.continuous_within_at.log _)).tendsto.add
    ((continuous_of_real.continuous_at.comp_continuous_within_at $
    continuous_within_at_arg_of_re_neg_of_im_zero hre him).mul tendsto_const_nhds),
  convert this,
  { lift z to â„ using him, simpa using hre.ne }
end

lemma tendsto_log_nhds_within_im_nonneg_of_re_neg_of_im_zero
  {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
  tendsto log (ð“[{z : â„‚ | 0 â‰¤ z.im}] z) (ð“ $ real.log (abs z) + Ï€ * I) :=
by simpa only [log, arg_eq_pi_iff.2 âŸ¨hre, himâŸ©]
  using (continuous_within_at_log_of_re_neg_of_im_zero hre him).tendsto

end complex

section log_deriv

open complex filter
open_locale topological_space

variables {Î± : Type*}

lemma filter.tendsto.clog {l : filter Î±} {f : Î± â†’ â„‚} {x : â„‚} (h : tendsto f l (ð“ x))
  (hx : 0 < x.re âˆ¨ x.im â‰  0) :
  tendsto (Î» t, log (f t)) l (ð“ $ log x) :=
(has_strict_deriv_at_log hx).continuous_at.tendsto.comp h

variables [topological_space Î±]

lemma continuous_at.clog {f : Î± â†’ â„‚} {x : Î±} (hâ‚ : continuous_at f x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous_at (Î» t, log (f t)) x :=
hâ‚.clog hâ‚‚

lemma continuous_within_at.clog {f : Î± â†’ â„‚} {s : set Î±} {x : Î±} (hâ‚ : continuous_within_at f s x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous_within_at (Î» t, log (f t)) s x :=
hâ‚.clog hâ‚‚

lemma continuous_on.clog {f : Î± â†’ â„‚} {s : set Î±} (hâ‚ : continuous_on f s)
  (hâ‚‚ : âˆ€ x âˆˆ s, 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous_on (Î» t, log (f t)) s :=
Î» x hx, (hâ‚ x hx).clog (hâ‚‚ x hx)

lemma continuous.clog {f : Î± â†’ â„‚} (hâ‚ : continuous f) (hâ‚‚ : âˆ€ x, 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous (Î» t, log (f t)) :=
continuous_iff_continuous_at.2 $ Î» x, hâ‚.continuous_at.clog (hâ‚‚ x)

variables {E : Type*} [normed_group E] [normed_space â„‚ E]

lemma has_strict_fderiv_at.clog {f : E â†’ â„‚} {f' : E â†’L[â„‚] â„‚} {x : E}
  (hâ‚ : has_strict_fderiv_at f f' x) (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_strict_fderiv_at (Î» t, log (f t)) ((f x)â»Â¹ â€¢ f') x :=
(has_strict_deriv_at_log hâ‚‚).comp_has_strict_fderiv_at x hâ‚

lemma has_strict_deriv_at.clog {f : â„‚ â†’ â„‚} {f' x : â„‚} (hâ‚ : has_strict_deriv_at f f' x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_strict_deriv_at (Î» t, log (f t)) (f' / f x) x :=
by { rw div_eq_inv_mul, exact (has_strict_deriv_at_log hâ‚‚).comp x hâ‚ }

lemma has_strict_deriv_at.clog_real {f : â„ â†’ â„‚} {x : â„} {f' : â„‚} (hâ‚ : has_strict_deriv_at f f' x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_strict_deriv_at (Î» t, log (f t)) (f' / f x) x :=
by simpa only [div_eq_inv_mul]
  using (has_strict_fderiv_at_log_real hâ‚‚).comp_has_strict_deriv_at x hâ‚

lemma has_fderiv_at.clog {f : E â†’ â„‚} {f' : E â†’L[â„‚] â„‚} {x : E}
  (hâ‚ : has_fderiv_at f f' x) (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_fderiv_at (Î» t, log (f t)) ((f x)â»Â¹ â€¢ f') x :=
(has_strict_deriv_at_log hâ‚‚).has_deriv_at.comp_has_fderiv_at x hâ‚

lemma has_deriv_at.clog {f : â„‚ â†’ â„‚} {f' x : â„‚} (hâ‚ : has_deriv_at f f' x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_deriv_at (Î» t, log (f t)) (f' / f x) x :=
by { rw div_eq_inv_mul, exact (has_strict_deriv_at_log hâ‚‚).has_deriv_at.comp x hâ‚ }

lemma has_deriv_at.clog_real {f : â„ â†’ â„‚} {x : â„} {f' : â„‚} (hâ‚ : has_deriv_at f f' x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_deriv_at (Î» t, log (f t)) (f' / f x) x :=
by simpa only [div_eq_inv_mul]
  using (has_strict_fderiv_at_log_real hâ‚‚).has_fderiv_at.comp_has_deriv_at x hâ‚

lemma differentiable_at.clog {f : E â†’ â„‚} {x : E} (hâ‚ : differentiable_at â„‚ f x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  differentiable_at â„‚ (Î» t, log (f t)) x :=
(hâ‚.has_fderiv_at.clog hâ‚‚).differentiable_at

lemma has_fderiv_within_at.clog {f : E â†’ â„‚} {f' : E â†’L[â„‚] â„‚} {s : set E} {x : E}
  (hâ‚ : has_fderiv_within_at f f' s x) (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_fderiv_within_at (Î» t, log (f t)) ((f x)â»Â¹ â€¢ f') s x :=
(has_strict_deriv_at_log hâ‚‚).has_deriv_at.comp_has_fderiv_within_at x hâ‚

lemma has_deriv_within_at.clog {f : â„‚ â†’ â„‚} {f' x : â„‚} {s : set â„‚}
  (hâ‚ : has_deriv_within_at f f' s x) (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_deriv_within_at (Î» t, log (f t)) (f' / f x) s x :=
by { rw div_eq_inv_mul,
     exact (has_strict_deriv_at_log hâ‚‚).has_deriv_at.comp_has_deriv_within_at x hâ‚ }

lemma has_deriv_within_at.clog_real {f : â„ â†’ â„‚} {s : set â„} {x : â„} {f' : â„‚}
  (hâ‚ : has_deriv_within_at f f' s x) (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  has_deriv_within_at (Î» t, log (f t)) (f' / f x) s x :=
by simpa only [div_eq_inv_mul]
  using (has_strict_fderiv_at_log_real hâ‚‚).has_fderiv_at.comp_has_deriv_within_at x hâ‚

lemma differentiable_within_at.clog {f : E â†’ â„‚} {s : set E} {x : E}
  (hâ‚ : differentiable_within_at â„‚ f s x) (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  differentiable_within_at â„‚ (Î» t, log (f t)) s x :=
(hâ‚.has_fderiv_within_at.clog hâ‚‚).differentiable_within_at

lemma differentiable_on.clog {f : E â†’ â„‚} {s : set E}
  (hâ‚ : differentiable_on â„‚ f s) (hâ‚‚ : âˆ€ x âˆˆ s, 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  differentiable_on â„‚ (Î» t, log (f t)) s :=
Î» x hx, (hâ‚ x hx).clog (hâ‚‚ x hx)

lemma differentiable.clog {f : E â†’ â„‚} (hâ‚ : differentiable â„‚ f)
  (hâ‚‚ : âˆ€ x, 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  differentiable â„‚ (Î» t, log (f t)) :=
Î» x, (hâ‚ x).clog (hâ‚‚ x)

end log_deriv
