/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne, Benjamin Davidson
-/
import analysis.special_functions.complex.arg
import analysis.special_functions.log.basic

/-!
# The complex `log` function

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Basic properties, relationship with `exp`.
-/

noncomputable theory

namespace complex

open set filter

open_locale real topology complex_conjugate

/-- Inverse of the `exp` function. Returns values such that `(log x).im > - Ï€` and `(log x).im â‰¤ Ï€`.
  `log 0 = 0`-/
@[pp_nodot] noncomputable def log (x : â„‚) : â„‚ := x.abs.log + arg x * I

lemma log_re (x : â„‚) : x.log.re = x.abs.log := by simp [log]

lemma log_im (x : â„‚) : x.log.im = x.arg := by simp [log]

lemma neg_pi_lt_log_im (x : â„‚) : -Ï€ < (log x).im := by simp only [log_im, neg_pi_lt_arg]
lemma log_im_le_pi (x : â„‚) : (log x).im â‰¤ Ï€ := by simp only [log_im, arg_le_pi]

lemma exp_log {x : â„‚} (hx : x â‰  0) : exp (log x) = x :=
by rw [log, exp_add_mul_I, â† of_real_sin, sin_arg, â† of_real_cos, cos_arg hx,
  â† of_real_exp, real.exp_log (abs.pos hx), mul_add, of_real_div, of_real_div,
  mul_div_cancel' _ (of_real_ne_zero.2 $ abs.ne_zero hx), â† mul_assoc,
  mul_div_cancel' _ (of_real_ne_zero.2 $ abs.ne_zero hx), re_add_im]

@[simp] lemma range_exp : range exp = {0}á¶œ :=
set.ext $ Î» x, âŸ¨by { rintro âŸ¨x, rflâŸ©, exact exp_ne_zero x }, Î» hx, âŸ¨log x, exp_log hxâŸ©âŸ©

lemma log_exp {x : â„‚} (hxâ‚ : -Ï€ < x.im) (hxâ‚‚: x.im â‰¤ Ï€) : log (exp x) = x :=
by rw [log, abs_exp, real.log_exp, exp_eq_exp_re_mul_sin_add_cos, â† of_real_exp,
  arg_mul_cos_add_sin_mul_I (real.exp_pos _) âŸ¨hxâ‚, hxâ‚‚âŸ©, re_add_im]

lemma exp_inj_of_neg_pi_lt_of_le_pi {x y : â„‚} (hxâ‚ : -Ï€ < x.im) (hxâ‚‚ : x.im â‰¤ Ï€)
  (hyâ‚ : - Ï€ < y.im) (hyâ‚‚ : y.im â‰¤ Ï€) (hxy : exp x = exp y) : x = y :=
by rw [â† log_exp hxâ‚ hxâ‚‚, â† log_exp hyâ‚ hyâ‚‚, hxy]

lemma of_real_log {x : â„} (hx : 0 â‰¤ x) : (x.log : â„‚) = log x :=
complex.ext
  (by rw [log_re, of_real_re, abs_of_nonneg hx])
  (by rw [of_real_im, log_im, arg_of_real_of_nonneg hx])

lemma log_of_real_re (x : â„) : (log (x : â„‚)).re = real.log x := by simp [log_re]

lemma log_of_real_mul {r : â„} (hr : 0 < r) {x : â„‚} (hx : x â‰  0) :
  log (r * x) = real.log r + log x :=
begin
  replace hx := complex.abs.ne_zero_iff.mpr hx,
  simp_rw [log, map_mul, abs_of_real, arg_real_mul _ hr, abs_of_pos hr, real.log_mul hr.ne' hx,
    of_real_add, add_assoc],
end

lemma log_mul_of_real (r : â„) (hr : 0 < r) (x : â„‚) (hx : x â‰  0) :
  log (x * r) = real.log r + log x :=
by rw [mul_comm, log_of_real_mul hr hx, add_comm]

@[simp] lemma log_zero : log 0 = 0 := by simp [log]

@[simp] lemma log_one : log 1 = 0 := by simp [log]

lemma log_neg_one : log (-1) = Ï€ * I := by simp [log]

lemma log_I : log I = Ï€ / 2 * I := by simp [log]

lemma log_neg_I : log (-I) = -(Ï€ / 2) * I := by simp [log]

lemma log_conj_eq_ite (x : â„‚) :
  log (conj x) = if x.arg = Ï€ then log x else conj (log x) :=
begin
  simp_rw [log, abs_conj, arg_conj, map_add, map_mul, conj_of_real],
  split_ifs with hx,
  { rw hx },
  simp_rw [of_real_neg, conj_I, mul_neg, neg_mul]
end

lemma log_conj (x : â„‚) (h : x.arg â‰  Ï€) : log (conj x) = conj (log x) :=
by rw [log_conj_eq_ite, if_neg h]

lemma log_inv_eq_ite (x : â„‚) : log (xâ»Â¹) = if x.arg = Ï€ then -conj (log x) else -log x :=
begin
  by_cases hx : x = 0,
  { simp [hx] },
  rw [inv_def, log_mul_of_real, real.log_inv, of_real_neg, â†sub_eq_neg_add, log_conj_eq_ite],
  { simp_rw [log, map_add, map_mul, conj_of_real, conj_I, norm_sq_eq_abs, real.log_pow,
      nat.cast_two, of_real_mul, of_real_bit0, of_real_one, neg_add, mul_neg, two_mul, neg_neg],
    split_ifs,
    { rw [add_sub_right_comm, sub_add_cancel'] },
    { rw [add_sub_right_comm, sub_add_cancel'] } },
  { rwa [inv_pos, complex.norm_sq_pos] },
  { rwa map_ne_zero },
end

lemma log_inv (x : â„‚) (hx : x.arg â‰  Ï€) : log (xâ»Â¹) = -log x :=
by rw [log_inv_eq_ite, if_neg hx]

lemma two_pi_I_ne_zero : (2 * Ï€ * I : â„‚) â‰  0 :=
by norm_num [real.pi_ne_zero, I_ne_zero]

lemma exp_eq_one_iff {x : â„‚} : exp x = 1 â†” âˆƒ n : â„¤, x = n * ((2 * Ï€) * I) :=
begin
  split,
  { intro h,
    rcases exists_unique_add_zsmul_mem_Ioc real.two_pi_pos x.im (-Ï€) with âŸ¨n, hn, -âŸ©,
    use -n,
    rw [int.cast_neg, neg_mul, eq_neg_iff_add_eq_zero],
    have : (x + n * (2 * Ï€ * I)).im âˆˆ Ioc (-Ï€) Ï€, by simpa [two_mul, mul_add] using hn,
    rw [â† log_exp this.1 this.2, exp_periodic.int_mul n, h, log_one] },
  { rintro âŸ¨n, rflâŸ©, exact (exp_periodic.int_mul n).eq.trans exp_zero }
end

lemma exp_eq_exp_iff_exp_sub_eq_one {x y : â„‚} : exp x = exp y â†” exp (x - y) = 1 :=
by rw [exp_sub, div_eq_one_iff_eq (exp_ne_zero _)]

lemma exp_eq_exp_iff_exists_int {x y : â„‚} : exp x = exp y â†” âˆƒ n : â„¤, x = y + n * ((2 * Ï€) * I) :=
by simp only [exp_eq_exp_iff_exp_sub_eq_one, exp_eq_one_iff, sub_eq_iff_eq_add']

@[simp] lemma countable_preimage_exp {s : set â„‚} : (exp â»Â¹' s).countable â†” s.countable :=
begin
  refine âŸ¨Î» hs, _, Î» hs, _âŸ©,
  { refine ((hs.image exp).insert 0).mono _,
    rw [image_preimage_eq_inter_range, range_exp, â† diff_eq, â† union_singleton, diff_union_self],
    exact subset_union_left _ _ },
  { rw â† bUnion_preimage_singleton,
    refine hs.bUnion (Î» z hz, _),
    rcases em (âˆƒ w, exp w = z) with âŸ¨w, rflâŸ©|hne,
    { simp only [preimage, mem_singleton_iff, exp_eq_exp_iff_exists_int, set_of_exists],
      exact countable_Union (Î» m, countable_singleton _) },
    { push_neg at hne, simp [preimage, hne] } }
end

alias countable_preimage_exp â†” _ _root_.set.countable.preimage_cexp

lemma tendsto_log_nhds_within_im_neg_of_re_neg_of_im_zero
  {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
  tendsto log (ð“[{z : â„‚ | z.im < 0}] z) (ð“ $ real.log (abs z) - Ï€ * I) :=
begin
  have := (continuous_of_real.continuous_at.comp_continuous_within_at
    (continuous_abs.continuous_within_at.log _)).tendsto.add
    (((continuous_of_real.tendsto _).comp $
    tendsto_arg_nhds_within_im_neg_of_re_neg_of_im_zero hre him).mul tendsto_const_nhds),
  convert this,
  { simp [sub_eq_add_neg] },
  { lift z to â„ using him, simpa using hre.ne }
end

lemma continuous_within_at_log_of_re_neg_of_im_zero
  {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
  continuous_within_at log {z : â„‚ | 0 â‰¤ z.im} z :=
begin
  have := (continuous_of_real.continuous_at.comp_continuous_within_at
    (continuous_abs.continuous_within_at.log _)).tendsto.add
    ((continuous_of_real.continuous_at.comp_continuous_within_at $
    continuous_within_at_arg_of_re_neg_of_im_zero hre him).mul tendsto_const_nhds),
  convert this,
  { lift z to â„ using him, simpa using hre.ne }
end

lemma tendsto_log_nhds_within_im_nonneg_of_re_neg_of_im_zero
  {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
  tendsto log (ð“[{z : â„‚ | 0 â‰¤ z.im}] z) (ð“ $ real.log (abs z) + Ï€ * I) :=
by simpa only [log, arg_eq_pi_iff.2 âŸ¨hre, himâŸ©]
  using (continuous_within_at_log_of_re_neg_of_im_zero hre him).tendsto

@[simp] lemma map_exp_comap_re_at_bot : map exp (comap re at_bot) = ð“[â‰ ] 0 :=
by rw [â† comap_exp_nhds_zero, map_comap, range_exp, nhds_within]

@[simp] lemma map_exp_comap_re_at_top : map exp (comap re at_top) = comap abs at_top :=
begin
  rw [â† comap_exp_comap_abs_at_top, map_comap, range_exp, inf_eq_left, le_principal_iff],
  exact eventually_ne_of_tendsto_norm_at_top tendsto_comap 0
end

end complex

section log_deriv

open complex filter
open_locale topology

variables {Î± : Type*}

lemma continuous_at_clog {x : â„‚} (h : 0 < x.re âˆ¨ x.im â‰  0) :
  continuous_at log x :=
begin
  refine continuous_at.add _ _,
  { refine continuous_of_real.continuous_at.comp _,
    refine (real.continuous_at_log _).comp complex.continuous_abs.continuous_at,
    rw complex.abs.ne_zero_iff,
    rintro rfl,
    simpa using h },
  { have h_cont_mul : continuous (Î» x : â„‚, x * I), from continuous_id'.mul continuous_const,
    refine h_cont_mul.continuous_at.comp (continuous_of_real.continuous_at.comp _),
    exact continuous_at_arg h, },
end

lemma filter.tendsto.clog {l : filter Î±} {f : Î± â†’ â„‚} {x : â„‚} (h : tendsto f l (ð“ x))
  (hx : 0 < x.re âˆ¨ x.im â‰  0) :
  tendsto (Î» t, log (f t)) l (ð“ $ log x) :=
(continuous_at_clog hx).tendsto.comp h

variables [topological_space Î±]

lemma continuous_at.clog {f : Î± â†’ â„‚} {x : Î±} (hâ‚ : continuous_at f x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous_at (Î» t, log (f t)) x :=
hâ‚.clog hâ‚‚

lemma continuous_within_at.clog {f : Î± â†’ â„‚} {s : set Î±} {x : Î±} (hâ‚ : continuous_within_at f s x)
  (hâ‚‚ : 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous_within_at (Î» t, log (f t)) s x :=
hâ‚.clog hâ‚‚

lemma continuous_on.clog {f : Î± â†’ â„‚} {s : set Î±} (hâ‚ : continuous_on f s)
  (hâ‚‚ : âˆ€ x âˆˆ s, 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous_on (Î» t, log (f t)) s :=
Î» x hx, (hâ‚ x hx).clog (hâ‚‚ x hx)

lemma continuous.clog {f : Î± â†’ â„‚} (hâ‚ : continuous f) (hâ‚‚ : âˆ€ x, 0 < (f x).re âˆ¨ (f x).im â‰  0) :
  continuous (Î» t, log (f t)) :=
continuous_iff_continuous_at.2 $ Î» x, hâ‚.continuous_at.clog (hâ‚‚ x)

end log_deriv
