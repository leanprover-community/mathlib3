/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne
-/
import analysis.complex.basic
import data.complex.exponential

/-!
# Complex and real exponential

In this file we prove continuity of `complex.exp` and `real.exp`. We also prove a few facts about
limits of `real.exp` at infinity.

## Tags

exp
-/

noncomputable theory

open finset filter metric asymptotics set function
open_locale classical topological_space

namespace complex

variables {z y x : ‚Ñù}

lemma exp_bound_sq (x z : ‚ÑÇ) (hz : ‚à•z‚à• ‚â§ 1) :
  ‚à•exp (x + z) - exp x - z ‚Ä¢ exp x‚à• ‚â§ ‚à•exp x‚à• * ‚à•z‚à• ^ 2 :=
calc ‚à•exp (x + z) - exp x - z * exp x‚à•
    = ‚à•exp x * (exp z - 1 - z)‚à• : by { congr, rw [exp_add], ring }
... = ‚à•exp x‚à• * ‚à•exp z - 1 - z‚à• : norm_mul _ _
... ‚â§ ‚à•exp x‚à• * ‚à•z‚à•^2 : mul_le_mul_of_nonneg_left (abs_exp_sub_one_sub_id_le hz) (norm_nonneg _)

lemma locally_lipschitz_exp {r : ‚Ñù} (hr_nonneg : 0 ‚â§ r) (hr_le : r ‚â§ 1) (x y : ‚ÑÇ)
  (hyx : ‚à•y - x‚à• < r) :
  ‚à•exp y - exp x‚à• ‚â§ (1 + r) * ‚à•exp x‚à• * ‚à•y - x‚à• :=
begin
  have hy_eq : y = x + (y - x), by abel,
  have hyx_sq_le : ‚à•y - x‚à• ^ 2 ‚â§ r * ‚à•y - x‚à•,
  { rw pow_two,
    exact mul_le_mul hyx.le le_rfl (norm_nonneg _) hr_nonneg, },
  have h_sq : ‚àÄ z, ‚à•z‚à• ‚â§ 1 ‚Üí ‚à•exp (x + z) - exp x‚à• ‚â§ ‚à•z‚à• * ‚à•exp x‚à• + ‚à•exp x‚à• * ‚à•z‚à• ^ 2,
  { intros z hz,
    have : ‚à•exp (x + z) - exp x - z ‚Ä¢ exp x‚à• ‚â§ ‚à•exp x‚à• * ‚à•z‚à• ^ 2, from exp_bound_sq x z hz,
    rw [‚Üê sub_le_iff_le_add',  ‚Üê norm_smul z],
    exact (norm_sub_norm_le _ _).trans this, },
  calc ‚à•exp y - exp x‚à• = ‚à•exp (x + (y - x)) - exp x‚à• : by nth_rewrite 0 hy_eq
  ... ‚â§ ‚à•y - x‚à• * ‚à•exp x‚à• + ‚à•exp x‚à• * ‚à•y - x‚à• ^ 2 : h_sq (y - x) (hyx.le.trans hr_le)
  ... ‚â§ ‚à•y - x‚à• * ‚à•exp x‚à• + ‚à•exp x‚à• * (r * ‚à•y - x‚à•) :
    add_le_add_left (mul_le_mul le_rfl hyx_sq_le (sq_nonneg _) (norm_nonneg _)) _
  ... = (1 + r) * ‚à•exp x‚à• * ‚à•y - x‚à• : by ring,
end

@[continuity] lemma continuous_exp : continuous exp :=
continuous_iff_continuous_at.mpr $
  Œª x, continuous_at_of_locally_lipschitz zero_lt_one (2 * ‚à•exp x‚à•)
    (locally_lipschitz_exp zero_le_one le_rfl x)

lemma continuous_on_exp {s : set ‚ÑÇ} : continuous_on exp s :=
continuous_exp.continuous_on

end complex

section complex_continuous_exp_comp

variable {Œ± : Type*}

open complex

lemma filter.tendsto.cexp {l : filter Œ±} {f : Œ± ‚Üí ‚ÑÇ} {z : ‚ÑÇ} (hf : tendsto f l (ùìù z)) :
  tendsto (Œª x, exp (f x)) l (ùìù (exp z)) :=
(continuous_exp.tendsto _).comp hf

variables [topological_space Œ±] {f : Œ± ‚Üí ‚ÑÇ} {s : set Œ±} {x : Œ±}

lemma continuous_within_at.cexp (h : continuous_within_at f s x) :
  continuous_within_at (Œª y, exp (f y)) s x :=
h.cexp

lemma continuous_at.cexp (h : continuous_at f x) : continuous_at (Œª y, exp (f y)) x :=
h.cexp

lemma continuous_on.cexp (h : continuous_on f s) : continuous_on (Œª y, exp (f y)) s :=
Œª x hx, (h x hx).cexp

lemma continuous.cexp (h : continuous f) : continuous (Œª y, exp (f y)) :=
continuous_iff_continuous_at.2 $ Œª x, h.continuous_at.cexp

end complex_continuous_exp_comp

namespace real

@[continuity] lemma continuous_exp : continuous exp :=
complex.continuous_re.comp complex.continuous_of_real.cexp

lemma continuous_on_exp {s : set ‚Ñù} : continuous_on exp s :=
continuous_exp.continuous_on

end real

section real_continuous_exp_comp

variable {Œ± : Type*}

open real

lemma filter.tendsto.exp {l : filter Œ±} {f : Œ± ‚Üí ‚Ñù} {z : ‚Ñù} (hf : tendsto f l (ùìù z)) :
  tendsto (Œª x, exp (f x)) l (ùìù (exp z)) :=
(continuous_exp.tendsto _).comp hf

variables [topological_space Œ±] {f : Œ± ‚Üí ‚Ñù} {s : set Œ±} {x : Œ±}

lemma continuous_within_at.exp (h : continuous_within_at f s x) :
  continuous_within_at (Œª y, exp (f y)) s x :=
h.exp

lemma continuous_at.exp (h : continuous_at f x) : continuous_at (Œª y, exp (f y)) x :=
h.exp

lemma continuous_on.exp (h : continuous_on f s) : continuous_on (Œª y, exp (f y)) s :=
Œª x hx, (h x hx).exp

lemma continuous.exp (h : continuous f) : continuous (Œª y, exp (f y)) :=
continuous_iff_continuous_at.2 $ Œª x, h.continuous_at.exp

end real_continuous_exp_comp

namespace real

variables {x y z : ‚Ñù}

/-- The real exponential function tends to `+‚àû` at `+‚àû`. -/
lemma tendsto_exp_at_top : tendsto exp at_top at_top :=
begin
  have A : tendsto (Œªx:‚Ñù, x + 1) at_top at_top :=
    tendsto_at_top_add_const_right at_top 1 tendsto_id,
  have B : ‚àÄ·∂† x in at_top, x + 1 ‚â§ exp x :=
    eventually_at_top.2 ‚ü®0, Œªx hx, add_one_le_exp x‚ü©,
  exact tendsto_at_top_mono' at_top B A
end

/-- The real exponential function tends to `0` at `-‚àû` or, equivalently, `exp(-x)` tends to `0`
at `+‚àû` -/
lemma tendsto_exp_neg_at_top_nhds_0 : tendsto (Œªx, exp (-x)) at_top (ùìù 0) :=
(tendsto_inv_at_top_zero.comp tendsto_exp_at_top).congr (Œªx, (exp_neg x).symm)

/-- The real exponential function tends to `1` at `0`. -/
lemma tendsto_exp_nhds_0_nhds_1 : tendsto exp (ùìù 0) (ùìù 1) :=
by { convert continuous_exp.tendsto 0, simp }

lemma tendsto_exp_at_bot : tendsto exp at_bot (ùìù 0) :=
(tendsto_exp_neg_at_top_nhds_0.comp tendsto_neg_at_bot_at_top).congr $
  Œª x, congr_arg exp $ neg_neg x

lemma tendsto_exp_at_bot_nhds_within : tendsto exp at_bot (ùìù[>] 0) :=
tendsto_inf.2 ‚ü®tendsto_exp_at_bot, tendsto_principal.2 $ eventually_of_forall exp_pos‚ü©

/-- The function `exp(x)/x^n` tends to `+‚àû` at `+‚àû`, for any natural number `n` -/
lemma tendsto_exp_div_pow_at_top (n : ‚Ñï) : tendsto (Œªx, exp x / x^n) at_top at_top :=
begin
  refine (at_top_basis_Ioi.tendsto_iff (at_top_basis' 1)).2 (Œª C hC‚ÇÅ, _),
  have hC‚ÇÄ : 0 < C, from zero_lt_one.trans_le hC‚ÇÅ,
  have : 0 < (exp 1 * C)‚Åª¬π := inv_pos.2 (mul_pos (exp_pos _) hC‚ÇÄ),
  obtain ‚ü®N, hN‚ü© : ‚àÉ N, ‚àÄ k ‚â• N, (‚Üëk ^ n : ‚Ñù) / exp 1 ^ k < (exp 1 * C)‚Åª¬π :=
    eventually_at_top.1 ((tendsto_pow_const_div_const_pow_of_one_lt n
      (one_lt_exp_iff.2 zero_lt_one)).eventually (gt_mem_nhds this)),
  simp only [‚Üê exp_nat_mul, mul_one, div_lt_iff, exp_pos, ‚Üê div_eq_inv_mul] at hN,
  refine ‚ü®N, trivial, Œª x hx, _‚ü©, rw set.mem_Ioi at hx,
  have hx‚ÇÄ : 0 < x, from N.cast_nonneg.trans_lt hx,
  rw [set.mem_Ici, le_div_iff (pow_pos hx‚ÇÄ _), ‚Üê le_div_iff' hC‚ÇÄ],
  calc x ^ n ‚â§ ‚åàx‚åâ‚Çä ^ n : pow_le_pow_of_le_left hx‚ÇÄ.le (nat.le_ceil _) _
  ... ‚â§ exp ‚åàx‚åâ‚Çä / (exp 1 * C) : (hN _ (nat.lt_ceil.2 hx).le).le
  ... ‚â§ exp (x + 1) / (exp 1 * C) : div_le_div_of_le (mul_pos (exp_pos _) hC‚ÇÄ).le
    (exp_le_exp.2 $ (nat.ceil_lt_add_one hx‚ÇÄ.le).le)
  ... = exp x / C : by rw [add_comm, exp_add, mul_div_mul_left _ _ (exp_pos _).ne']
end

/-- The function `x^n * exp(-x)` tends to `0` at `+‚àû`, for any natural number `n`. -/
lemma tendsto_pow_mul_exp_neg_at_top_nhds_0 (n : ‚Ñï) : tendsto (Œªx, x^n * exp (-x)) at_top (ùìù 0) :=
(tendsto_inv_at_top_zero.comp (tendsto_exp_div_pow_at_top n)).congr $ Œªx,
  by rw [comp_app, inv_eq_one_div, div_div_eq_mul_div, one_mul, div_eq_mul_inv, exp_neg]

/-- The function `(b * exp x + c) / (x ^ n)` tends to `+‚àû` at `+‚àû`, for any natural number
`n` and any real numbers `b` and `c` such that `b` is positive. -/
lemma tendsto_mul_exp_add_div_pow_at_top (b c : ‚Ñù) (n : ‚Ñï) (hb : 0 < b) :
  tendsto (Œª x, (b * exp x + c) / x ^ n) at_top at_top :=
begin
  rcases n.eq_zero_or_pos with rfl | hn,
  { simp only [pow_zero, div_one],
    exact (tendsto_exp_at_top.const_mul_at_top hb).at_top_add tendsto_const_nhds },
  refine tendsto.congr' (eventually_eq_of_mem (Ioi_mem_at_top 0) _)
    (((tendsto_exp_div_pow_at_top n).const_mul_at_top hb).at_top_add
      ((tendsto_pow_neg_at_top hn).mul (@tendsto_const_nhds _ _ _ c _))),
  intros x hx,
  simp only [zpow_neg x n],
  ring,
end

/-- The function `(x ^ n) / (b * exp x + c)` tends to `0` at `+‚àû`, for any natural number
`n` and any real numbers `b` and `c` such that `b` is nonzero. -/
lemma tendsto_div_pow_mul_exp_add_at_top (b c : ‚Ñù) (n : ‚Ñï) (hb : 0 ‚â† b) :
  tendsto (Œª x, x ^ n / (b * exp x + c)) at_top (ùìù 0) :=
begin
  have H : ‚àÄ d e, 0 < d ‚Üí tendsto (Œª (x:‚Ñù), x^n / (d * (exp x) + e)) at_top (ùìù 0),
  { intros b' c' h,
    convert (tendsto_mul_exp_add_div_pow_at_top b' c' n h).inv_tendsto_at_top ,
    ext x,
    simpa only [pi.inv_apply] using (inv_div _ _).symm },
  cases lt_or_gt_of_ne hb,
  { exact H b c h },
  { convert (H (-b) (-c) (neg_pos.mpr h)).neg,
    { ext x,
      field_simp,
      rw [‚Üê neg_add (b * exp x) c, neg_div_neg_eq] },
    { exact neg_zero.symm } },
end

/-- `real.exp` as an order isomorphism between `‚Ñù` and `(0, +‚àû)`. -/
def exp_order_iso : ‚Ñù ‚âÉo Ioi (0 : ‚Ñù) :=
strict_mono.order_iso_of_surjective _ (exp_strict_mono.cod_restrict exp_pos) $
  (continuous_subtype_mk _ continuous_exp).surjective
    (by simp only [tendsto_Ioi_at_top, subtype.coe_mk, tendsto_exp_at_top])
    (by simp [tendsto_exp_at_bot_nhds_within])

@[simp] lemma coe_exp_order_iso_apply (x : ‚Ñù) : (exp_order_iso x : ‚Ñù) = exp x := rfl

@[simp] lemma coe_comp_exp_order_iso : coe ‚àò exp_order_iso = exp := rfl

@[simp] lemma range_exp : range exp = Ioi 0 :=
by rw [‚Üê coe_comp_exp_order_iso, range_comp, exp_order_iso.range_eq, image_univ, subtype.range_coe]

@[simp] lemma map_exp_at_top : map exp at_top = at_top :=
by rw [‚Üê coe_comp_exp_order_iso, ‚Üê filter.map_map, order_iso.map_at_top, map_coe_Ioi_at_top]

@[simp] lemma comap_exp_at_top : comap exp at_top = at_top :=
by rw [‚Üê map_exp_at_top, comap_map exp_injective, map_exp_at_top]

@[simp] lemma tendsto_exp_comp_at_top {Œ± : Type*} {l : filter Œ±} {f : Œ± ‚Üí ‚Ñù} :
  tendsto (Œª x, exp (f x)) l at_top ‚Üî tendsto f l at_top :=
by rw [‚Üê tendsto_comap_iff, comap_exp_at_top]

lemma tendsto_comp_exp_at_top {Œ± : Type*} {l : filter Œ±} {f : ‚Ñù ‚Üí Œ±} :
  tendsto (Œª x, f (exp x)) at_top l ‚Üî tendsto f at_top l :=
by rw [‚Üê tendsto_map'_iff, map_exp_at_top]

@[simp] lemma map_exp_at_bot : map exp at_bot = ùìù[>] 0 :=
by rw [‚Üê coe_comp_exp_order_iso, ‚Üê filter.map_map, exp_order_iso.map_at_bot, ‚Üê map_coe_Ioi_at_bot]

lemma comap_exp_nhds_within_Ioi_zero : comap exp (ùìù[>] 0) = at_bot :=
by rw [‚Üê map_exp_at_bot, comap_map exp_injective]

lemma tendsto_comp_exp_at_bot {Œ± : Type*} {l : filter Œ±} {f : ‚Ñù ‚Üí Œ±} :
  tendsto (Œª x, f (exp x)) at_bot l ‚Üî tendsto f (ùìù[>] 0) l :=
by rw [‚Üê map_exp_at_bot, tendsto_map'_iff]

lemma is_o_pow_exp_at_top {n : ‚Ñï} : is_o (Œª x, x^n) real.exp at_top :=
by simpa [is_o_iff_tendsto (Œª x hx, ((exp_pos x).ne' hx).elim)]
  using tendsto_div_pow_mul_exp_add_at_top 1 0 n zero_ne_one

end real

namespace complex

/-- `complex.abs (complex.exp z) ‚Üí ‚àû` as `complex.re z ‚Üí ‚àû`. TODO: use `bornology.cobounded`. -/
lemma tendsto_exp_comap_re_at_top : tendsto exp (comap re at_top) (comap abs at_top) :=
by simpa only [tendsto_comap_iff, (‚àò), abs_exp] using real.tendsto_exp_at_top.comp tendsto_comap

/-- `complex.exp z ‚Üí 0` as `complex.re z ‚Üí -‚àû`.-/
lemma tendsto_exp_comap_re_at_bot : tendsto exp (comap re at_bot) (ùìù 0) :=
tendsto_zero_iff_norm_tendsto_zero.2 $
  by simpa only [norm_eq_abs, abs_exp] using real.tendsto_exp_at_bot.comp tendsto_comap

lemma tendsto_exp_comap_re_at_bot_nhds_within : tendsto exp (comap re at_bot) (ùìù[‚â†] 0) :=
tendsto_inf.2 ‚ü®tendsto_exp_comap_re_at_bot,
  tendsto_principal.2 $ eventually_of_forall $ exp_ne_zero‚ü©

end complex
