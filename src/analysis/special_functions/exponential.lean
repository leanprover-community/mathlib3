/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Eric Wieser
-/
import analysis.normed_space.exponential
import analysis.calculus.fderiv_analytic
import topology.metric_space.cau_seq_filter

/-!
# Calculus results on exponential in a Banach algebra

In this file, we prove basic properties about the derivative of the exponential map `exp ğ•‚`
in a Banach algebra `ğ”¸` over a field `ğ•‚`. We keep them separate from the main file
`analysis/normed_space/exponential` in order to minimize dependencies.

## Main results

We prove most result for an arbitrary field `ğ•‚`, and then specialize to `ğ•‚ = â„` or `ğ•‚ = â„‚`.

### General case

- `has_strict_fderiv_at_exp_zero_of_radius_pos` : `exp ğ•‚` has strict FrÃ©chet-derivative
  `1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero
  (see also `has_strict_deriv_at_exp_zero_of_radius_pos` for the case `ğ”¸ = ğ•‚`)
- `has_strict_fderiv_at_exp_of_lt_radius` : if `ğ•‚` has characteristic zero and `ğ”¸` is commutative,
  then given a point `x` in the disk of convergence, `exp ğ•‚` has strict FrÃ©chet-derivative
  `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at x (see also `has_strict_deriv_at_exp_of_lt_radius` for the case
  `ğ”¸ = ğ•‚`)
- `has_strict_fderiv_at_exp_smul_const_of_mem_ball`: `u â†¦ exp ğ•‚ (u â€¢ x)`, where `x` belongs to a
  ring that may not be commutative, has strict FrÃ©chet-derivative
  `exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x` at `t` if `t â€¢ x` is in the radius of convergence.

### `ğ•‚ = â„` or `ğ•‚ = â„‚`

- `has_strict_fderiv_at_exp_zero` : `exp ğ•‚` has strict FrÃ©chet-derivative `1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero
  (see also `has_strict_deriv_at_exp_zero` for the case `ğ”¸ = ğ•‚`)
- `has_strict_fderiv_at_exp` : if `ğ”¸` is commutative, then given any point `x`, `exp ğ•‚` has strict
  FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at x (see also `has_strict_deriv_at_exp` for the
  case `ğ”¸ = ğ•‚`)
- `has_strict_fderiv_at_exp_smul_const`: `u â†¦ exp ğ•‚ (u â€¢ x)`, where `x` belongs to a
  ring that may not be commutative, has strict FrÃ©chet-derivative
  `exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x` at `t`.

### Compatibilty with `real.exp` and `complex.exp`

- `complex.exp_eq_exp_â„‚` : `complex.exp = exp â„‚ â„‚`
- `real.exp_eq_exp_â„` : `real.exp = exp â„ â„`

-/

open filter is_R_or_C continuous_multilinear_map normed_field asymptotics
open_locale nat topology big_operators ennreal

section any_field_any_algebra

variables {ğ•‚ ğ”¸ : Type*} [nontrivially_normed_field ğ•‚] [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential in a Banach-algebra `ğ”¸` over a normed field `ğ•‚` has strict FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_strict_fderiv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_strict_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
begin
  convert (has_fpower_series_at_exp_zero_of_radius_pos h).has_strict_fderiv_at,
  ext x,
  change x = exp_series ğ•‚ ğ”¸ 1 (Î» _, x),
  simp [exp_series_apply_eq]
end

/-- The exponential in a Banach-algebra `ğ”¸` over a normed field `ğ•‚` has FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_fderiv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
(has_strict_fderiv_at_exp_zero_of_radius_pos h).has_fderiv_at

end any_field_any_algebra

section any_field_comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [nontrivially_normed_field ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of
characteristic zero has FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x` in the
disk of convergence. -/
lemma has_fderiv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
begin
  have hpos : 0 < (exp_series ğ•‚ ğ”¸).radius := (zero_le _).trans_lt hx,
  rw has_fderiv_at_iff_is_o_nhds_zero,
  suffices : (Î» h, exp ğ•‚ x * (exp ğ•‚ (0 + h) - exp ğ•‚ 0 - continuous_linear_map.id ğ•‚ ğ”¸ h))
    =á¶ [ğ“ 0] (Î» h, exp ğ•‚ (x + h) - exp ğ•‚ x - exp ğ•‚ x â€¢ continuous_linear_map.id ğ•‚ ğ”¸ h),
  { refine (is_o.const_mul_left _ _).congr' this (eventually_eq.refl _ _),
    rw â† has_fderiv_at_iff_is_o_nhds_zero,
    exact has_fderiv_at_exp_zero_of_radius_pos hpos },
  have : âˆ€á¶  h in ğ“ (0 : ğ”¸), h âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius :=
    emetric.ball_mem_nhds _ hpos,
  filter_upwards [this] with _ hh,
  rw [exp_add_of_mem_ball hx hh, exp_zero, zero_add, continuous_linear_map.id_apply, smul_eq_mul],
  ring
end

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of
characteristic zero has strict FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x` in
the disk of convergence. -/
lemma has_strict_fderiv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_strict_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
let âŸ¨p, hpâŸ© := analytic_at_exp_of_mem_ball x hx in
hp.has_fderiv_at.unique (has_fderiv_at_exp_of_mem_ball hx) â–¸ hp.has_strict_fderiv_at

end any_field_comm_algebra

section deriv

variables {ğ•‚ : Type*} [nontrivially_normed_field ğ•‚] [complete_space ğ•‚]

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has strict derivative
`exp ğ•‚ x` at any point `x` in the disk of convergence. -/
lemma has_strict_deriv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ•‚}
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) :
  has_strict_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
by simpa using (has_strict_fderiv_at_exp_of_mem_ball hx).has_strict_deriv_at

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has derivative
`exp ğ•‚ x` at any point `x` in the disk of convergence. -/
lemma has_deriv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ•‚}
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) :
  has_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
(has_strict_deriv_at_exp_of_mem_ball hx).has_deriv_at

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has strict derivative
`1` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_strict_deriv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ•‚).radius) :
  has_strict_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
(has_strict_fderiv_at_exp_zero_of_radius_pos h).has_strict_deriv_at

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has derivative
`1` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_deriv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ•‚).radius) :
  has_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
(has_strict_deriv_at_exp_zero_of_radius_pos h).has_deriv_at

end deriv

section is_R_or_C_any_algebra

variables {ğ•‚ ğ”¸ : Type*} [is_R_or_C ğ•‚] [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential in a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero. -/
lemma has_strict_fderiv_at_exp_zero :
  has_strict_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
has_strict_fderiv_at_exp_zero_of_radius_pos (exp_series_radius_pos ğ•‚ ğ”¸)

/-- The exponential in a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero. -/
lemma has_fderiv_at_exp_zero :
  has_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
has_strict_fderiv_at_exp_zero.has_fderiv_at

end is_R_or_C_any_algebra

section is_R_or_C_comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [is_R_or_C ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict
FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x`. -/
lemma has_strict_fderiv_at_exp {x : ğ”¸} :
  has_strict_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
has_strict_fderiv_at_exp_of_mem_ball ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has
FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x`. -/
lemma has_fderiv_at_exp {x : ğ”¸} :
  has_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
has_strict_fderiv_at_exp.has_fderiv_at

end is_R_or_C_comm_algebra

section deriv_R_or_C

variables {ğ•‚ : Type*} [is_R_or_C ğ•‚]

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict derivative `exp ğ•‚ x` at any point
`x`. -/
lemma has_strict_deriv_at_exp {x : ğ•‚} : has_strict_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
has_strict_deriv_at_exp_of_mem_ball ((exp_series_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _)

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has derivative `exp ğ•‚ x` at any point `x`. -/
lemma has_deriv_at_exp {x : ğ•‚} : has_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
has_strict_deriv_at_exp.has_deriv_at

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict derivative `1` at zero. -/
lemma has_strict_deriv_at_exp_zero : has_strict_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
has_strict_deriv_at_exp_zero_of_radius_pos (exp_series_radius_pos ğ•‚ ğ•‚)

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has derivative `1` at zero. -/
lemma has_deriv_at_exp_zero :
  has_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
has_strict_deriv_at_exp_zero.has_deriv_at

end deriv_R_or_C

lemma complex.exp_eq_exp_â„‚ : complex.exp = exp â„‚ :=
begin
  refine funext (Î» x, _),
  rw [complex.exp, exp_eq_tsum_div],
  exact tendsto_nhds_unique x.exp'.tendsto_limit
    (exp_series_div_summable â„ x).has_sum.tendsto_sum_nat
end

lemma real.exp_eq_exp_â„ : real.exp = exp â„ :=
by { ext x, exact_mod_cast congr_fun complex.exp_eq_exp_â„‚ x }

/-! ### Derivative of $\exp (ux)$ by $u$

Note that since for `x : ğ”¹` we have `normed_ring ğ”¹` not `normed_comm_ring ğ”¹`, we cannot deduce
these results from `has_fderiv_at_exp_of_mem_ball`.

We could deduce them from the more general non-commutive case,
$$\frac{d}{dt}e^{x(t)} = \int_0^1 e^{sx(t)} \left(\frac{d}{dt}e^{x(t)}\right) e^{(1-s)x(t)} ds$$
but this is harder to prove, and typically is shown by going via these results first.

TODO: prove this result too!
-/

section exp_smul
variables {ğ•‚ ğ”¸ ğ”¹ : Type*}
variables (ğ•‚)

open_locale topology
open asymptotics filter

section mem_ball
variables [nontrivially_normed_field ğ•‚] [char_zero ğ•‚]
variables [normed_comm_ring ğ”¸] [normed_ring ğ”¹]
variables [normed_algebra ğ•‚ ğ”¸] [normed_algebra ğ•‚ ğ”¹] [algebra ğ”¸ ğ”¹] [has_continuous_smul ğ”¸ ğ”¹]
variables [is_scalar_tower ğ•‚ ğ”¸ ğ”¹]
variables [complete_space ğ”¹]

lemma has_fderiv_at_exp_smul_const_of_mem_ball
  (x : ğ”¹) (t : ğ”¸) (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x) t :=
begin
  -- TODO: prove this via `has_fderiv_at_exp_of_mem_ball` using the commutative ring
  -- `algebra.elemental_algebra ğ”¸ x`. See leanprover-community/mathlib#19062 for discussion.
  have hpos : 0 < (exp_series ğ•‚ ğ”¹).radius := (zero_le _).trans_lt htx,
  rw has_fderiv_at_iff_is_o_nhds_zero,
  suffices :
    (Î» h, exp ğ•‚ (t â€¢ x) * (exp ğ•‚ ((0 + h) â€¢ x) - exp ğ•‚ ((0 : ğ”¸) â€¢ x)
      - ((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x) h))
    =á¶ [ğ“ 0] (Î» h, exp ğ•‚ ((t + h) â€¢ x) - exp ğ•‚ (t â€¢ x)
      - (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x) h),
  { refine (is_o.const_mul_left _ _).congr' this (eventually_eq.refl _ _),
    rw â† @has_fderiv_at_iff_is_o_nhds_zero _ _ _ _ _ _ _ _
      (Î» u, exp ğ•‚ (u â€¢ x)) ((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x) 0,
    have : has_fderiv_at (exp ğ•‚) (1 : ğ”¹ â†’L[ğ•‚] ğ”¹) ((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x 0),
    { rw [continuous_linear_map.smul_right_apply, continuous_linear_map.one_apply, zero_smul],
      exact has_fderiv_at_exp_zero_of_radius_pos hpos },
    exact this.comp 0 ((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x).has_fderiv_at },
  have : tendsto (Î» h : ğ”¸, h â€¢ x) (ğ“ 0) (ğ“ 0),
  { rw â† zero_smul ğ”¸ x,
    exact tendsto_id.smul_const x },
  have : âˆ€á¶  h in ğ“ (0 : ğ”¸), h â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius :=
    this.eventually (emetric.ball_mem_nhds _ hpos),
  filter_upwards [this],
  intros h hh,
  have : commute (t â€¢ x) (h â€¢ x) := ((commute.refl x).smul_left t).smul_right h,
  rw [add_smul t h, exp_add_of_commute_of_mem_ball this htx hh, zero_add, zero_smul, exp_zero,
      continuous_linear_map.smul_right_apply, continuous_linear_map.one_apply,
      continuous_linear_map.smul_apply, continuous_linear_map.smul_right_apply,
      continuous_linear_map.one_apply, smul_eq_mul, mul_sub_left_distrib, mul_sub_left_distrib,
      mul_one],
end

lemma has_fderiv_at_exp_smul_const_of_mem_ball'
  (x : ğ”¹) (t : ğ”¸) (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x).smul_right (exp ğ•‚ (t â€¢ x))) t :=
begin
  convert has_fderiv_at_exp_smul_const_of_mem_ball ğ•‚ _ _ htx using 1,
  ext t',
  show commute (t' â€¢ x) (exp ğ•‚ (t â€¢ x)),
  exact (((commute.refl x).smul_left t').smul_right t).exp_right ğ•‚,
end

lemma has_strict_fderiv_at_exp_smul_const_of_mem_ball (t : ğ”¸) (x : ğ”¹)
  (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_strict_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x) t :=
let âŸ¨p, hpâŸ© := analytic_at_exp_of_mem_ball (t â€¢ x) htx in
have derivâ‚ : has_strict_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x)) _ t,
  from hp.has_strict_fderiv_at.comp t
    ((continuous_linear_map.id ğ•‚ ğ”¸).smul_right x).has_strict_fderiv_at,
have derivâ‚‚ : has_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x)) _ t,
  from has_fderiv_at_exp_smul_const_of_mem_ball ğ•‚ x t htx,
(derivâ‚.has_fderiv_at.unique derivâ‚‚) â–¸ derivâ‚

lemma has_strict_fderiv_at_exp_smul_const_of_mem_ball' (t : ğ”¸) (x : ğ”¹)
  (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_strict_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x).smul_right (exp ğ•‚ (t â€¢ x))) t :=
let âŸ¨p, hpâŸ© := analytic_at_exp_of_mem_ball (t â€¢ x) htx in
begin
  convert has_strict_fderiv_at_exp_smul_const_of_mem_ball ğ•‚ _ _ htx using 1,
  ext t',
  show commute (t' â€¢ x) (exp ğ•‚ (t â€¢ x)),
  exact (((commute.refl x).smul_left t').smul_right t).exp_right ğ•‚,
end

variables {ğ•‚}

lemma has_strict_deriv_at_exp_smul_const_of_mem_ball (t : ğ•‚) (x : ğ”¹)
  (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_strict_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t :=
by simpa using (has_strict_fderiv_at_exp_smul_const_of_mem_ball ğ•‚ t x htx).has_strict_deriv_at


lemma has_strict_deriv_at_exp_smul_const_of_mem_ball' (t : ğ•‚) (x : ğ”¹)
  (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_strict_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t :=
by simpa using (has_strict_fderiv_at_exp_smul_const_of_mem_ball' ğ•‚ t x htx).has_strict_deriv_at

lemma has_deriv_at_exp_smul_const_of_mem_ball (t : ğ•‚) (x : ğ”¹)
  (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t :=
(has_strict_deriv_at_exp_smul_const_of_mem_ball t x htx).has_deriv_at

lemma has_deriv_at_exp_smul_const_of_mem_ball' (t : ğ•‚) (x : ğ”¹)
  (htx : t â€¢ x âˆˆ emetric.ball (0 : ğ”¹) (exp_series ğ•‚ ğ”¹).radius) :
  has_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t :=
(has_strict_deriv_at_exp_smul_const_of_mem_ball' t x htx).has_deriv_at

end mem_ball

section is_R_or_C
variables [is_R_or_C ğ•‚]
variables [normed_comm_ring ğ”¸] [normed_ring ğ”¹]
variables [normed_algebra ğ•‚ ğ”¸] [normed_algebra ğ•‚ ğ”¹] [algebra ğ”¸ ğ”¹] [has_continuous_smul ğ”¸ ğ”¹]
variables [is_scalar_tower ğ•‚ ğ”¸ ğ”¹]
variables [complete_space ğ”¹]

variables (ğ•‚)

lemma has_fderiv_at_exp_smul_const (x : ğ”¹) (t : ğ”¸) :
  has_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x) t :=
has_fderiv_at_exp_smul_const_of_mem_ball ğ•‚ _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

lemma has_fderiv_at_exp_smul_const' (x : ğ”¹) (t : ğ”¸) :
  has_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x).smul_right (exp ğ•‚ (t â€¢ x))) t :=
has_fderiv_at_exp_smul_const_of_mem_ball' ğ•‚ _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

lemma has_strict_fderiv_at_exp_smul_const (t : ğ”¸) (x : ğ”¹) :
  has_strict_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x) t :=
has_strict_fderiv_at_exp_smul_const_of_mem_ball ğ•‚ _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

lemma has_strict_fderiv_at_exp_smul_const' (t : ğ”¸) (x : ğ”¹) :
  has_strict_fderiv_at (Î» u : ğ”¸, exp ğ•‚ (u â€¢ x))
    (((1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smul_right x).smul_right (exp ğ•‚ (t â€¢ x))) t :=
has_strict_fderiv_at_exp_smul_const_of_mem_ball' ğ•‚ _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

variables {ğ•‚}

lemma has_strict_deriv_at_exp_smul_const (t : ğ•‚) (x : ğ”¹) :
  has_strict_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t :=
has_strict_deriv_at_exp_smul_const_of_mem_ball _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

lemma has_strict_deriv_at_exp_smul_const' (t : ğ•‚) (x : ğ”¹) :
  has_strict_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t :=
has_strict_deriv_at_exp_smul_const_of_mem_ball' _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

lemma has_deriv_at_exp_smul_const (t : ğ•‚) (x : ğ”¹) :
  has_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t :=
has_deriv_at_exp_smul_const_of_mem_ball _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

lemma has_deriv_at_exp_smul_const' (t : ğ•‚) (x : ğ”¹) :
  has_deriv_at (Î» u : ğ•‚, exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t :=
has_deriv_at_exp_smul_const_of_mem_ball' _ _ $
  (exp_series_radius_eq_top ğ•‚ ğ”¹).symm â–¸ edist_lt_top _ _

end is_R_or_C

end exp_smul
