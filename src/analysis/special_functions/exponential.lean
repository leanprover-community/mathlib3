/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import analysis.normed_space.exponential
import analysis.calculus.fderiv_analytic
import data.complex.exponential
import topology.metric_space.cau_seq_filter

/-!
# Calculus results on exponential in a Banach algebra

In this file, we prove basic properties about the derivative of the exponential map `exp ğ•‚`
in a Banach algebra `ğ”¸` over a field `ğ•‚`. We keep them separate from the main file
`analysis/normed_space/exponential` in order to minimize dependencies.

## Main results

We prove most result for an arbitrary field `ğ•‚`, and then specialize to `ğ•‚ = â„` or `ğ•‚ = â„‚`.

### General case

- `has_strict_fderiv_at_exp_zero_of_radius_pos` : `exp ğ•‚` has strict FrÃ©chet-derivative
  `1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero
  (see also `has_strict_deriv_at_exp_zero_of_radius_pos` for the case `ğ”¸ = ğ•‚`)
- `has_strict_fderiv_at_exp_of_lt_radius` : if `ğ•‚` has characteristic zero and `ğ”¸` is commutative,
  then given a point `x` in the disk of convergence, `exp ğ•‚` as strict FrÃ©chet-derivative
  `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at x (see also `has_strict_deriv_at_exp_of_lt_radius` for the case
  `ğ”¸ = ğ•‚`)

### `ğ•‚ = â„` or `ğ•‚ = â„‚`

- `has_strict_fderiv_at_exp_zero` : `exp ğ•‚` has strict FrÃ©chet-derivative `1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero
  (see also `has_strict_deriv_at_exp_zero` for the case `ğ”¸ = ğ•‚`)
- `has_strict_fderiv_at_exp` : if `ğ”¸` is commutative, then given any point `x`, `exp ğ•‚` as strict
  FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at x (see also `has_strict_deriv_at_exp` for the
  case `ğ”¸ = ğ•‚`)

### Compatibilty with `real.exp` and `complex.exp`

- `complex.exp_eq_exp_â„‚` : `complex.exp = exp â„‚ â„‚`
- `real.exp_eq_exp_â„` : `real.exp = exp â„ â„`

-/

open filter is_R_or_C continuous_multilinear_map normed_field asymptotics
open_locale nat topology big_operators ennreal

section any_field_any_algebra

variables {ğ•‚ ğ”¸ : Type*} [nontrivially_normed_field ğ•‚] [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential in a Banach-algebra `ğ”¸` over a normed field `ğ•‚` has strict FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_strict_fderiv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_strict_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
begin
  convert (has_fpower_series_at_exp_zero_of_radius_pos h).has_strict_fderiv_at,
  ext x,
  change x = exp_series ğ•‚ ğ”¸ 1 (Î» _, x),
  simp [exp_series_apply_eq]
end

/-- The exponential in a Banach-algebra `ğ”¸` over a normed field `ğ•‚` has FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_fderiv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ”¸).radius) :
  has_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
(has_strict_fderiv_at_exp_zero_of_radius_pos h).has_fderiv_at

end any_field_any_algebra

section any_field_comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [nontrivially_normed_field ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of
characteristic zero has FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x` in the
disk of convergence. -/
lemma has_fderiv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
begin
  have hpos : 0 < (exp_series ğ•‚ ğ”¸).radius := (zero_le _).trans_lt hx,
  rw has_fderiv_at_iff_is_o_nhds_zero,
  suffices : (Î» h, exp ğ•‚ x * (exp ğ•‚ (0 + h) - exp ğ•‚ 0 - continuous_linear_map.id ğ•‚ ğ”¸ h))
    =á¶ [ğ“ 0] (Î» h, exp ğ•‚ (x + h) - exp ğ•‚ x - exp ğ•‚ x â€¢ continuous_linear_map.id ğ•‚ ğ”¸ h),
  { refine (is_o.const_mul_left _ _).congr' this (eventually_eq.refl _ _),
    rw â† has_fderiv_at_iff_is_o_nhds_zero,
    exact has_fderiv_at_exp_zero_of_radius_pos hpos },
  have : âˆ€á¶  h in ğ“ (0 : ğ”¸), h âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius :=
    emetric.ball_mem_nhds _ hpos,
  filter_upwards [this] with _ hh,
  rw [exp_add_of_mem_ball hx hh, exp_zero, zero_add, continuous_linear_map.id_apply, smul_eq_mul],
  ring
end

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of
characteristic zero has strict FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x` in
the disk of convergence. -/
lemma has_strict_fderiv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ”¸}
  (hx : x âˆˆ emetric.ball (0 : ğ”¸) (exp_series ğ•‚ ğ”¸).radius) :
  has_strict_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
let âŸ¨p, hpâŸ© := analytic_at_exp_of_mem_ball x hx in
hp.has_fderiv_at.unique (has_fderiv_at_exp_of_mem_ball hx) â–¸ hp.has_strict_fderiv_at

end any_field_comm_algebra

section deriv

variables {ğ•‚ : Type*} [nontrivially_normed_field ğ•‚] [complete_space ğ•‚]

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has strict derivative
`exp ğ•‚ x` at any point `x` in the disk of convergence. -/
lemma has_strict_deriv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ•‚}
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) :
  has_strict_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
by simpa using (has_strict_fderiv_at_exp_of_mem_ball hx).has_strict_deriv_at

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has derivative
`exp ğ•‚ x` at any point `x` in the disk of convergence. -/
lemma has_deriv_at_exp_of_mem_ball [char_zero ğ•‚] {x : ğ•‚}
  (hx : x âˆˆ emetric.ball (0 : ğ•‚) (exp_series ğ•‚ ğ•‚).radius) :
  has_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
(has_strict_deriv_at_exp_of_mem_ball hx).has_deriv_at

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has strict derivative
`1` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_strict_deriv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ•‚).radius) :
  has_strict_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
(has_strict_fderiv_at_exp_zero_of_radius_pos h).has_strict_deriv_at

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has derivative
`1` at zero, as long as it converges on a neighborhood of zero. -/
lemma has_deriv_at_exp_zero_of_radius_pos (h : 0 < (exp_series ğ•‚ ğ•‚).radius) :
  has_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
(has_strict_deriv_at_exp_zero_of_radius_pos h).has_deriv_at

end deriv

section is_R_or_C_any_algebra

variables {ğ•‚ ğ”¸ : Type*} [is_R_or_C ğ•‚] [normed_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential in a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero. -/
lemma has_strict_fderiv_at_exp_zero :
  has_strict_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
has_strict_fderiv_at_exp_zero_of_radius_pos (exp_series_radius_pos ğ•‚ ğ”¸)

/-- The exponential in a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has FrÃ©chet-derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero. -/
lemma has_fderiv_at_exp_zero :
  has_fderiv_at (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
has_strict_fderiv_at_exp_zero.has_fderiv_at

end is_R_or_C_any_algebra

section is_R_or_C_comm_algebra

variables {ğ•‚ ğ”¸ : Type*} [is_R_or_C ğ•‚] [normed_comm_ring ğ”¸] [normed_algebra ğ•‚ ğ”¸]
  [complete_space ğ”¸]

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict
FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x`. -/
lemma has_strict_fderiv_at_exp {x : ğ”¸} :
  has_strict_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
has_strict_fderiv_at_exp_of_mem_ball ((exp_series_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

/-- The exponential map in a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has
FrÃ©chet-derivative `exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x`. -/
lemma has_fderiv_at_exp {x : ğ”¸} :
  has_fderiv_at (exp ğ•‚) (exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸) x :=
has_strict_fderiv_at_exp.has_fderiv_at

end is_R_or_C_comm_algebra

section deriv_R_or_C

variables {ğ•‚ : Type*} [is_R_or_C ğ•‚]

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict derivative `exp ğ•‚ x` at any point
`x`. -/
lemma has_strict_deriv_at_exp {x : ğ•‚} : has_strict_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
has_strict_deriv_at_exp_of_mem_ball ((exp_series_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _)

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has derivative `exp ğ•‚ x` at any point `x`. -/
lemma has_deriv_at_exp {x : ğ•‚} : has_deriv_at (exp ğ•‚) (exp ğ•‚ x) x :=
has_strict_deriv_at_exp.has_deriv_at

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict derivative `1` at zero. -/
lemma has_strict_deriv_at_exp_zero : has_strict_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
has_strict_deriv_at_exp_zero_of_radius_pos (exp_series_radius_pos ğ•‚ ğ•‚)

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has derivative `1` at zero. -/
lemma has_deriv_at_exp_zero :
  has_deriv_at (exp ğ•‚) (1 : ğ•‚) 0 :=
has_strict_deriv_at_exp_zero.has_deriv_at

end deriv_R_or_C

lemma complex.exp_eq_exp_â„‚ : complex.exp = exp â„‚ :=
begin
  refine funext (Î» x, _),
  rw [complex.exp, exp_eq_tsum_div],
  exact tendsto_nhds_unique x.exp'.tendsto_limit
    (exp_series_div_summable â„ x).has_sum.tendsto_sum_nat
end

lemma real.exp_eq_exp_â„ : real.exp = exp â„ :=
by { ext x, exact_mod_cast congr_fun complex.exp_eq_exp_â„‚ x }

/-! ### Series expansions of `cos` and `sin` for `â„` and `â„‚` -/

/-- The equivalence between `a` and `(a / n, a % n)` for nonzero `n`. -/
@[simps]
def nat.div_mod_equiv (n : â„•) [ne_zero n] : â„• â‰ƒ â„• Ã— fin n :=
{ to_fun := Î» a, (a / n, â†‘a),
  inv_fun := Î» p, p.1 * n + p.2,
  left_inv := Î» a, nat.div_add_mod' _ _,
  right_inv := Î» p, begin
    refine prod.ext _ (fin.ext $ nat.mul_add_mod_of_lt p.2.is_lt),
    dsimp only,
    rw [add_comm, nat.add_mul_div_right _ _ (ne_zero.pos n), nat.div_eq_zero p.2.is_lt, zero_add],
  end }

section sin_cos

lemma complex.has_sum_cos' (z : â„‚) :
  has_sum (Î» n : â„•, (z * complex.I) ^ (2 * n) / â†‘(2 * n)!) (complex.cos z) :=
begin
  rw [complex.cos, complex.exp_eq_exp_â„‚],
  have := ((exp_series_div_has_sum_exp â„‚ (z * complex.I)).add
          (exp_series_div_has_sum_exp â„‚ (-z * complex.I))).div_const 2,
  replace := ((nat.div_mod_equiv 2)).symm.has_sum_iff.mpr this,
  dsimp [function.comp] at this,
  simp_rw [â†mul_comm 2 _] at this,
  refine this.prod_fiberwise (Î» k, _),
  dsimp only,
  convert has_sum_fintype (_ : fin 2 â†’ â„‚) using 1,
  rw fin.sum_univ_two,
  simp_rw [fin.coe_zero, fin.coe_one, add_zero, pow_succ', pow_mul,
    mul_pow, neg_sq, â†two_mul, neg_mul, mul_neg, neg_div, add_right_neg, zero_div, add_zero,
    mul_div_cancel_left _ (two_ne_zero : (2 : â„‚) â‰  0)],
end

lemma complex.has_sum_sin' (z : â„‚) :
  has_sum (Î» n : â„•, (z * complex.I) ^ (2 * n + 1) / â†‘(2 * n + 1)! / complex.I) (complex.sin z) :=
begin
  rw [complex.sin, complex.exp_eq_exp_â„‚],
  have := (((exp_series_div_has_sum_exp â„‚ (-z * complex.I)).sub
          (exp_series_div_has_sum_exp â„‚ (z * complex.I))).mul_right complex.I).div_const 2,
  replace := ((nat.div_mod_equiv 2)).symm.has_sum_iff.mpr this,
  dsimp [function.comp] at this,
  simp_rw [â†mul_comm 2 _] at this,
  refine this.prod_fiberwise (Î» k, _),
  dsimp only,
  convert has_sum_fintype (_ : fin 2 â†’ â„‚) using 1,
  rw fin.sum_univ_two,
  simp_rw [fin.coe_zero, fin.coe_one, add_zero, pow_succ', pow_mul,
    mul_pow, neg_sq, sub_self, zero_mul, zero_div, zero_add,
    neg_mul, mul_neg, neg_div, â† neg_add', â†two_mul, neg_mul, neg_div, mul_assoc,
    mul_div_cancel_left _ (two_ne_zero : (2 : â„‚) â‰  0), complex.div_I],
end

lemma complex.has_sum_cos (z : â„‚) :
  has_sum (Î» n : â„•, ((-1) ^ n) * z ^ (2 * n) / â†‘(2 * n)!) (complex.cos z) :=
begin
  convert complex.has_sum_cos' z using 1,
  simp_rw [mul_pow, pow_mul, complex.I_sq, mul_comm]
end

lemma complex.has_sum_sin (z : â„‚) :
  has_sum (Î» n : â„•, ((-1) ^ n) * z ^ (2 * n + 1) / â†‘(2 * n + 1)!) (complex.sin z) :=
begin
  convert complex.has_sum_sin' z using 1,
  simp_rw [mul_pow, pow_succ', pow_mul, complex.I_sq, â†mul_assoc,
    mul_div_assoc, div_right_comm, div_self complex.I_ne_zero, mul_comm _ ((-1 : â„‚)^_), mul_one_div,
    mul_div_assoc, mul_assoc]
end

lemma complex.cos_eq_tsum' (z : â„‚) :
  complex.cos z = âˆ‘' n : â„•, (z * complex.I) ^ (2 * n) / â†‘(2 * n)! :=
(complex.has_sum_cos' z).tsum_eq.symm

lemma complex.sin_eq_tsum' (z : â„‚) :
  complex.sin z = âˆ‘' n : â„•, (z * complex.I) ^ (2 * n + 1) / â†‘(2 * n + 1)! / complex.I :=
(complex.has_sum_sin' z).tsum_eq.symm

lemma complex.cos_eq_tsum (z : â„‚) :
  complex.cos z = âˆ‘' n : â„•, ((-1) ^ n) * z ^ (2 * n) / â†‘(2 * n)! :=
(complex.has_sum_cos z).tsum_eq.symm

lemma complex.sin_eq_tsum (z : â„‚) :
  complex.sin z = âˆ‘' n : â„•, ((-1) ^ n) * z ^ (2 * n + 1) / â†‘(2 * n + 1)! :=
(complex.has_sum_sin z).tsum_eq.symm

lemma real.has_sum_cos (r : â„) :
  has_sum (Î» n : â„•, ((-1) ^ n) * r ^ (2 * n) / â†‘(2 * n)!) (real.cos r) :=
by exact_mod_cast complex.has_sum_cos r

lemma real.has_sum_sin (r : â„) :
  has_sum (Î» n : â„•, ((-1) ^ n) * r ^ (2 * n + 1) / â†‘(2 * n + 1)!) (real.sin r) :=
by exact_mod_cast complex.has_sum_sin r

lemma real.cos_eq_tsum (r : â„) :
  real.cos r = âˆ‘' n : â„•, ((-1) ^ n) * r ^ (2 * n) / â†‘(2 * n)! :=
(real.has_sum_cos r).tsum_eq.symm

lemma real.sin_eq_tsum (r : â„) :
  real.sin r = âˆ‘' n : â„•, ((-1) ^ n) * r ^ (2 * n + 1) / â†‘(2 * n + 1)! :=
(real.has_sum_sin r).tsum_eq.symm

end sin_cos
