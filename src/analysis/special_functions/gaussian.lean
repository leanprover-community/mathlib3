/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/

import analysis.special_functions.gamma
import analysis.special_functions.polar_coord
import analysis.convex.complex
import analysis.normed.group.basic
import analysis.complex.cauchy_integral
import measure_theory.group.integration

/-!
# Gaussian integral

We prove various versions of the formula for the Gaussian integral:
* `integral_gaussian`: for real `b` we have `‚à´ x:‚Ñù, exp (-b * x^2) = sqrt (œÄ / b)`.
* `integral_gaussian_complex`: for complex `b` with `0 < re b` we have
  `‚à´ x:‚Ñù, exp (-b * x^2) = (œÄ / b) ^ (1 / 2)`.
* `integral_gaussian_Ioi` and `integral_gaussian_complex_Ioi`: variants for integrals over `Ioi 0`.
* `complex.Gamma_one_half_eq`: the formula `Œì (1 / 2) = ‚àöœÄ`.

We also prove, more generally, that the Fourier transform of the Gaussian
is another Gaussian:

* `integral_cexp_neg_mul_sq_add_const`: for all complex `b` and `c` with `0 < re b` we have
  `‚à´ (x : ‚Ñù), exp (-b * (x + c) ^ 2) = (œÄ / b) ^ (1 / 2)`.
* `fourier_transform_gaussian`: for all complex `b` and `t` with `0 < re b`, we have
  `‚à´ x:‚Ñù, exp (I * t * x) * exp (-b * x^2) = (œÄ / b) ^ (1 / 2) * exp (-t ^ 2 / (4 * b))`.
* `fourier_transform_gaussian_pi`: a variant with `b` and `t` scaled to give a more symmetric
  statement, `‚à´ x:‚Ñù, exp (2 * œÄ * I * t * x) * exp (-œÄ * b * x^2) =
  (1 / b) ^ (1 / 2) * exp (-œÄ * (1 / b) * t ^ 2)`.
-/

noncomputable theory

open real set measure_theory filter asymptotics
open_locale real topology

open complex (hiding exp continuous_exp abs_of_nonneg)
notation `cexp` := complex.exp

lemma exp_neg_mul_sq_is_o_exp_neg {b : ‚Ñù} (hb : 0 < b) :
  (Œª x:‚Ñù, exp (-b * x^2)) =o[at_top] (Œª x:‚Ñù, exp (-x)) :=
begin
  have A : (Œª (x : ‚Ñù), -x - -b * x ^ 2) = (Œª x, x * (b * x + (- 1))), by { ext x, ring },
  rw [is_o_exp_comp_exp_comp, A],
  apply tendsto.at_top_mul_at_top tendsto_id,
  apply tendsto_at_top_add_const_right at_top (-1 : ‚Ñù),
  exact tendsto.const_mul_at_top hb tendsto_id,
end

lemma rpow_mul_exp_neg_mul_sq_is_o_exp_neg {b : ‚Ñù} (hb : 0 < b) (s : ‚Ñù) :
  (Œª x:‚Ñù, x ^ s * exp (-b * x^2)) =o[at_top] (Œª x:‚Ñù, exp (-(1/2) * x)) :=
begin
  apply ((is_O_refl (Œª x:‚Ñù, x ^ s) at_top).mul_is_o (exp_neg_mul_sq_is_o_exp_neg hb)).trans,
  convert Gamma_integrand_is_o s,
  simp_rw [mul_comm],
end

lemma integrable_on_rpow_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) {s : ‚Ñù} (hs : -1 < s) :
  integrable_on (Œª x:‚Ñù, x ^ s * exp (-b * x^2)) (Ioi 0) :=
begin
  rw [‚Üê Ioc_union_Ioi_eq_Ioi (zero_le_one : (0 : ‚Ñù) ‚â§ 1), integrable_on_union],
  split,
  { rw [‚Üêintegrable_on_Icc_iff_integrable_on_Ioc],
    refine integrable_on.mul_continuous_on _ _ is_compact_Icc,
    { refine (interval_integrable_iff_integrable_Icc_of_le zero_le_one).mp _,
      exact interval_integral.interval_integrable_rpow' hs },
    { exact (continuous_exp.comp (continuous_const.mul (continuous_pow 2))).continuous_on } },
  { have B : (0 : ‚Ñù) < 1/2, by norm_num,
    apply integrable_of_is_O_exp_neg B _ (is_o.is_O (rpow_mul_exp_neg_mul_sq_is_o_exp_neg hb _)),
    assume x hx,
    have N : x ‚â† 0, { refine (zero_lt_one.trans_le _).ne', exact hx },
    apply ((continuous_at_rpow_const _ _ (or.inl N)).mul _).continuous_within_at,
    exact (continuous_exp.comp (continuous_const.mul (continuous_pow 2))).continuous_at },
end

lemma integrable_rpow_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) {s : ‚Ñù} (hs : -1 < s) :
  integrable (Œª x:‚Ñù, x ^ s * exp (-b * x^2)) :=
begin
  rw [‚Üê integrable_on_univ, ‚Üê @Iio_union_Ici _ _ (0 : ‚Ñù), integrable_on_union,
      integrable_on_Ici_iff_integrable_on_Ioi],
  refine ‚ü®_, integrable_on_rpow_mul_exp_neg_mul_sq hb hs‚ü©,
  rw ‚Üê (measure.measure_preserving_neg (volume : measure ‚Ñù)).integrable_on_comp_preimage
    ((homeomorph.neg ‚Ñù).to_measurable_equiv.measurable_embedding),
  simp only [function.comp, neg_sq, neg_preimage, preimage_neg_Iio, neg_neg, neg_zero],
  apply integrable.mono' (integrable_on_rpow_mul_exp_neg_mul_sq hb hs),
  { apply measurable.ae_strongly_measurable,
    exact (measurable_id'.neg.pow measurable_const).mul
      ((measurable_id'.pow measurable_const).const_mul (-b)).exp },
  { have : measurable_set (Ioi (0 : ‚Ñù)) := measurable_set_Ioi,
    filter_upwards [ae_restrict_mem this] with x hx,
    have h'x : 0 ‚â§ x := le_of_lt hx,
    rw [real.norm_eq_abs, abs_mul, abs_of_nonneg (exp_pos _).le],
    apply mul_le_mul_of_nonneg_right _ (exp_pos _).le,
    simpa [abs_of_nonneg h'x] using abs_rpow_le_abs_rpow (-x) s }
end

lemma integrable_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) :
  integrable (Œª x:‚Ñù, exp (-b * x^2)) :=
by simpa using integrable_rpow_mul_exp_neg_mul_sq hb (by norm_num : (-1 : ‚Ñù) < 0)

lemma integrable_on_Ioi_exp_neg_mul_sq_iff {b : ‚Ñù} :
  integrable_on (Œª x:‚Ñù, exp (-b * x^2)) (Ioi 0) ‚Üî 0 < b :=
begin
  refine ‚ü®Œª h, _, Œª h, (integrable_exp_neg_mul_sq h).integrable_on‚ü©,
  by_contra' hb,
  have : ‚à´‚Åª x:‚Ñù in Ioi 0, 1 ‚â§ ‚à´‚Åª x:‚Ñù in Ioi 0, ‚Äñexp (-b * x^2)‚Äñ‚Çä,
  { apply lintegral_mono (Œª x, _),
    simp only [neg_mul, ennreal.one_le_coe_iff, ‚Üê to_nnreal_one, to_nnreal_le_iff_le_coe,
      real.norm_of_nonneg (exp_pos _).le, coe_nnnorm, one_le_exp_iff, right.nonneg_neg_iff],
    exact mul_nonpos_of_nonpos_of_nonneg hb (sq_nonneg _) },
  simpa using this.trans_lt h.2,
end

lemma integrable_exp_neg_mul_sq_iff {b : ‚Ñù} : integrable (Œª x:‚Ñù, exp (-b * x^2)) ‚Üî 0 < b :=
‚ü®Œª h, integrable_on_Ioi_exp_neg_mul_sq_iff.mp h.integrable_on, integrable_exp_neg_mul_sq‚ü©

lemma integrable_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) : integrable (Œª x:‚Ñù, x * exp (-b * x^2)) :=
by simpa using integrable_rpow_mul_exp_neg_mul_sq hb (by norm_num : (-1 : ‚Ñù) < 1)

lemma norm_cexp_neg_mul_sq (b : ‚ÑÇ) (x : ‚Ñù) : ‚Äñcomplex.exp (-b * x^2)‚Äñ = exp (-b.re * x^2) :=
by rw [complex.norm_eq_abs, complex.abs_exp, ‚Üêof_real_pow, mul_comm (-b) _, of_real_mul_re,
  neg_re, mul_comm]

lemma integrable_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) : integrable (Œª x:‚Ñù, cexp (-b * x^2)) :=
begin
  refine ‚ü®(complex.continuous_exp.comp
    (continuous_const.mul (continuous_of_real.pow 2))).ae_strongly_measurable, _‚ü©,
  rw ‚Üêhas_finite_integral_norm_iff,
  simp_rw norm_cexp_neg_mul_sq,
  exact (integrable_exp_neg_mul_sq hb).2,
end

lemma integrable_mul_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) :
  integrable (Œª x:‚Ñù, ‚Üëx * cexp (-b * x^2)) :=
begin
  refine ‚ü®(continuous_of_real.mul (complex.continuous_exp.comp _)).ae_strongly_measurable, _‚ü©,
  { exact continuous_const.mul (continuous_of_real.pow 2) },
  have := (integrable_mul_exp_neg_mul_sq hb).has_finite_integral,
  rw ‚Üêhas_finite_integral_norm_iff at this ‚ä¢,
  convert this,
  ext1 x,
  rw [norm_mul, norm_mul, norm_cexp_neg_mul_sq b, complex.norm_eq_abs, abs_of_real,
    real.norm_eq_abs, norm_of_nonneg (exp_pos _).le],
end

lemma integral_mul_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) :
  ‚à´ r:‚Ñù in Ioi 0, (r : ‚ÑÇ) * cexp (-b * r ^ 2) = (2 * b)‚Åª¬π :=
begin
  have hb' : b ‚â† 0 := by { contrapose! hb, rw [hb, zero_re], },
  refine tendsto_nhds_unique (interval_integral_tendsto_integral_Ioi _
    (integrable_mul_cexp_neg_mul_sq hb).integrable_on filter.tendsto_id) _,
  have A : ‚àÄ x:‚ÑÇ, has_deriv_at (Œª x, - (2 * b)‚Åª¬π * cexp (-b * x^2)) (x * cexp (- b * x^2)) x,
  { intro x,
    convert (((has_deriv_at_pow 2 x)).const_mul (-b)).cexp.const_mul (- (2 * b)‚Åª¬π) using 1,
    field_simp [hb'],
    ring },
  have : ‚àÄ (y : ‚Ñù), ‚à´ x in 0..(id y), ‚Üëx * cexp (-b * x^2)
      = (- (2 * b)‚Åª¬π * cexp (-b * y^2)) - (- (2 * b)‚Åª¬π * cexp (-b * 0^2)) :=
    Œª y, interval_integral.integral_eq_sub_of_has_deriv_at
      (Œª x hx, (A x).comp_of_real) (integrable_mul_cexp_neg_mul_sq hb).interval_integrable,
  simp_rw this,
  have L : tendsto (Œª (x : ‚Ñù), (2 * b)‚Åª¬π - (2 * b)‚Åª¬π * cexp (-b * x ^ 2)) at_top
    (ùìù ((2 * b)‚Åª¬π - (2 * b)‚Åª¬π * 0)),
  { refine tendsto_const_nhds.sub (tendsto.const_mul _ $ tendsto_zero_iff_norm_tendsto_zero.mpr _),
    simp_rw norm_cexp_neg_mul_sq b,
    exact tendsto_exp_at_bot.comp
      (tendsto.neg_const_mul_at_top (neg_lt_zero.2 hb) (tendsto_pow_at_top two_ne_zero)) },
  simpa using L,
end

/-- The *square* of the Gaussian integral `‚à´ x:‚Ñù, exp (-b * x^2)` is equal to `œÄ / b`. -/
lemma integral_gaussian_sq_complex {b : ‚ÑÇ} (hb : 0 < b.re) :
  (‚à´ x:‚Ñù, cexp (-b * x^2)) ^ 2 = œÄ / b :=
begin
  /- We compute `(‚à´ exp (-b x^2))^2` as an integral over `‚Ñù^2`, and then make a polar change
  of coordinates. We are left with `‚à´ r * exp (-b r^2)`, which has been computed in
  `integral_mul_cexp_neg_mul_sq` using the fact that this function has an obvious primitive. -/
  calc
  (‚à´ x:‚Ñù, cexp (-b * (x:‚ÑÇ)^2)) ^ 2
      = ‚à´ p : ‚Ñù √ó ‚Ñù, cexp (-b * ((p.1) : ‚ÑÇ) ^ 2) * cexp (-b * ((p.2) : ‚ÑÇ) ^ 2) :
    by { rw [pow_two, ‚Üê integral_prod_mul], refl }
  ... = ‚à´ p : ‚Ñù √ó ‚Ñù, cexp (- b * (p.1 ^ 2 + p.2 ^ 2)) :
    by { congr, ext1 p, rw [‚Üê complex.exp_add, mul_add], }
  ... = ‚à´ p in polar_coord.target, (p.1) ‚Ä¢ cexp (- b * ((p.1 * cos p.2) ^ 2 + (p.1 * sin p.2)^2)) :
    begin
      rw ‚Üê integral_comp_polar_coord_symm,
      simp only [polar_coord_symm_apply, of_real_mul, of_real_cos, of_real_sin],
    end
  ... = (‚à´ r in Ioi (0 : ‚Ñù), r * cexp (-b * r^2)) * (‚à´ Œ∏ in Ioo (-œÄ) œÄ, 1) :
    begin
      rw ‚Üê set_integral_prod_mul,
      congr' with p : 1,
      rw mul_one,
      congr,
      conv_rhs { rw [‚Üê one_mul ((p.1 : ‚ÑÇ)^2), ‚Üê sin_sq_add_cos_sq (p.2 : ‚ÑÇ)], },
      ring_exp,
    end
  ... = ‚ÜëœÄ / b :
    begin
      have : 0 ‚â§ œÄ + œÄ, by linarith [real.pi_pos],
      simp only [integral_const, measure.restrict_apply', measurable_set_Ioo, univ_inter,
        volume_Ioo, sub_neg_eq_add, ennreal.to_real_of_real, this],
      rw [‚Üêtwo_mul, real_smul, mul_one, of_real_mul, of_real_bit0, of_real_one,
        integral_mul_cexp_neg_mul_sq hb],
      field_simp [(by { contrapose! hb, rw [hb, zero_re] } : b ‚â† 0)],
      ring,
    end
end

theorem integral_gaussian (b : ‚Ñù) : ‚à´ x, exp (-b * x^2) = sqrt (œÄ / b) :=
begin
  /- First we deal with the crazy case where `b ‚â§ 0`: then both sides vanish. -/
  rcases le_or_lt b 0 with hb|hb,
  { rw [integral_undef, sqrt_eq_zero_of_nonpos],
    { exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb },
    { simpa only [not_lt, integrable_exp_neg_mul_sq_iff] using hb } },
  /- Assume now `b > 0`. Then both sides are non-negative and their squares agree. -/
  refine (sq_eq_sq _ (sqrt_nonneg _)).1 _,
  { exact integral_nonneg (Œª x, (exp_pos _).le) },
  rw [‚Üêof_real_inj, of_real_pow, ‚Üêintegral_of_real, sq_sqrt (div_pos pi_pos hb).le, of_real_div],
  convert integral_gaussian_sq_complex (by rwa of_real_re : 0 < (b:‚ÑÇ).re),
  ext1 x,
  rw [of_real_exp, of_real_mul, of_real_pow, of_real_neg],
end

lemma continuous_at_gaussian_integral (b : ‚ÑÇ) (hb : 0 < re b) :
  continuous_at (Œª c:‚ÑÇ, ‚à´ x:‚Ñù, cexp (-c * x^2)) b :=
begin
  let f : ‚ÑÇ ‚Üí ‚Ñù ‚Üí ‚ÑÇ := Œª (c : ‚ÑÇ) (x : ‚Ñù), cexp (-c * x ^ 2),
  obtain ‚ü®d, hd, hd'‚ü© := exists_between hb,
  have f_meas : ‚àÄ (c:‚ÑÇ), ae_strongly_measurable (f c) volume := Œª c, by
  { apply continuous.ae_strongly_measurable,
    exact complex.continuous_exp.comp (continuous_const.mul (continuous_of_real.pow 2)) },
  have f_int : integrable (f b) volume,
  { simp_rw [‚Üêintegrable_norm_iff (f_meas b), norm_cexp_neg_mul_sq b],
    exact integrable_exp_neg_mul_sq hb, },
  have f_cts : ‚àÄ (x : ‚Ñù), continuous_at (Œª c, f c x) b :=
    Œª x, (complex.continuous_exp.comp (continuous_id'.neg.mul continuous_const)).continuous_at,
  have f_le_bd : ‚àÄ·∂† (c : ‚ÑÇ) in ùìù b, ‚àÄ·µê (x : ‚Ñù), ‚Äñf c x‚Äñ ‚â§ exp (-d * x ^ 2),
  { refine eventually_of_mem ((continuous_re.is_open_preimage _ is_open_Ioi).mem_nhds hd') _,
    refine Œª c hc, ae_of_all _ (Œª x, _),
    rw [norm_cexp_neg_mul_sq, exp_le_exp],
    exact mul_le_mul_of_nonneg_right (neg_le_neg (le_of_lt hc)) (sq_nonneg _) },
  exact continuous_at_of_dominated (eventually_of_forall f_meas) f_le_bd
    (integrable_exp_neg_mul_sq hd) (ae_of_all _ f_cts),
end

theorem integral_gaussian_complex {b : ‚ÑÇ} (hb : 0 < re b) :
  ‚à´ x:‚Ñù, cexp (-b * x^2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) :=
begin
  have nv : ‚àÄ {b : ‚ÑÇ}, (0 < re b) ‚Üí (b ‚â† 0),
  { intros b hb, contrapose! hb, rw hb, simp },
  refine (convex_halfspace_re_gt 0).is_preconnected.eq_of_sq_eq
    _ _ (Œª c hc, _) (Œª c hc, _) (by simp : 0 < re (1 : ‚ÑÇ)) _ hb,
  { -- integral is continuous
    exact continuous_at.continuous_on continuous_at_gaussian_integral, },
  { -- `(œÄ / b) ^ (1 / 2 : ‚ÑÇ)` is continuous
    refine continuous_at.continuous_on (Œª b hb, (continuous_at_cpow_const (or.inl _)).comp
      (continuous_at_const.div continuous_at_id (nv hb))),
    rw [div_re, of_real_im, of_real_re, zero_mul, zero_div, add_zero],
    exact div_pos (mul_pos pi_pos hb) (norm_sq_pos.mpr (nv hb)), },
  { -- squares of both sides agree
    dsimp only [pi.pow_apply],
    rw [integral_gaussian_sq_complex hc, sq],
    conv_lhs { rw ‚Üêcpow_one (‚ÜëœÄ / c)},
    rw ‚Üê cpow_add _ _ (div_ne_zero (of_real_ne_zero.mpr pi_ne_zero) (nv hc)),
    norm_num },
  { -- RHS doesn't vanish
    rw [ne.def, cpow_eq_zero_iff, not_and_distrib],
    exact or.inl (div_ne_zero (of_real_ne_zero.mpr pi_ne_zero) (nv hc)) },
  { -- equality at 1
    have : ‚àÄ (x : ‚Ñù), cexp (-1 * x ^ 2) = exp (-1 * x ^ 2),
    { intro x,
      simp only [of_real_exp, neg_mul, one_mul, of_real_neg, of_real_pow] },
    simp_rw [this, integral_of_real],
    conv_rhs {  congr, rw [‚Üêof_real_one, ‚Üêof_real_div], skip,
      rw [‚Üêof_real_one, ‚Üêof_real_bit0, ‚Üêof_real_div]  },
    rw [‚Üêof_real_cpow, of_real_inj],
    convert integral_gaussian (1 : ‚Ñù),
    { rwa [sqrt_eq_rpow] },
    { rw [div_one], exact pi_pos.le } },
end

/- The Gaussian integral on the half-line, `‚à´ x in Ioi 0, exp (-b * x^2)`, for complex `b`. -/
lemma integral_gaussian_complex_Ioi {b : ‚ÑÇ} (hb : 0 < re b) :
  ‚à´ x:‚Ñù in Ioi 0, cexp (-b * x^2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) / 2 :=
begin
  have full_integral := integral_gaussian_complex hb,
  have : measurable_set (Ioi (0:‚Ñù)) := measurable_set_Ioi,
  rw [‚Üêintegral_add_compl this (integrable_cexp_neg_mul_sq hb), compl_Ioi] at full_integral,
  suffices : ‚à´ x:‚Ñù in Iic 0, cexp (-b * x^2) = ‚à´ x:‚Ñù in Ioi 0, cexp (-b * x^2),
  { rw [this, ‚Üêmul_two] at full_integral,
    rwa eq_div_iff, exact two_ne_zero },
  have : ‚àÄ (c : ‚Ñù), ‚à´ x in 0 .. c, cexp (-b * x^2) = ‚à´ x in -c .. 0, cexp (-b * x^2),
  { intro c,
    have := @interval_integral.integral_comp_sub_left _ _ _ _ 0 c (Œª x, cexp (-b * x^2)) 0,
    simpa [zero_sub, neg_sq, neg_zero] using this },
  have t1 := interval_integral_tendsto_integral_Ioi _
     ((integrable_cexp_neg_mul_sq hb).integrable_on) tendsto_id,
  have t2 : tendsto (Œª c:‚Ñù, ‚à´ x:‚Ñù in 0..c,
    cexp (-b * x^2)) at_top (ùìù ‚à´ x:‚Ñù in Iic 0, cexp (-b * x^2)),
  { simp_rw this,
    refine interval_integral_tendsto_integral_Iic _ _ tendsto_neg_at_top_at_bot,
    apply (integrable_cexp_neg_mul_sq hb).integrable_on },
  exact tendsto_nhds_unique t2 t1,
end

/- The Gaussian integral on the half-line, `‚à´ x in Ioi 0, exp (-b * x^2)`, for real `b`. -/
lemma integral_gaussian_Ioi (b : ‚Ñù) : ‚à´ x in Ioi 0, exp (-b * x^2) = sqrt (œÄ / b) / 2 :=
begin
  rcases le_or_lt b 0 with hb|hb,
  { rw [integral_undef, sqrt_eq_zero_of_nonpos, zero_div],
    exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb,
    rwa [‚Üêintegrable_on, integrable_on_Ioi_exp_neg_mul_sq_iff, not_lt] },
  rw [‚Üêof_real_inj, ‚Üêintegral_of_real],
  convert integral_gaussian_complex_Ioi (by rwa of_real_re : 0 < (b:‚ÑÇ).re),
  { ext1 x, simp, },
  { rw [sqrt_eq_rpow, ‚Üêof_real_div, of_real_div, of_real_cpow],
    norm_num,
    exact (div_pos pi_pos hb).le, }
end

/-- The special-value formula `Œì(1/2) = ‚àöœÄ`, which is equivalent to the Gaussian integral. -/
lemma real.Gamma_one_half_eq : real.Gamma (1 / 2) = sqrt œÄ :=
begin
  rw [Gamma_eq_integral one_half_pos, ‚Üêintegral_comp_rpow_Ioi_of_pos zero_lt_two],
  convert congr_arg (Œª x:‚Ñù, 2 * x) (integral_gaussian_Ioi 1),
  { rw ‚Üêintegral_mul_left,
    refine set_integral_congr measurable_set_Ioi (Œª x hx, _),
    dsimp only,
    have : (x ^ (2:‚Ñù)) ^ (1 / (2:‚Ñù) - 1) = x‚Åª¬π,
    { rw ‚Üêrpow_mul (le_of_lt hx),
      norm_num,
      rw [rpow_neg (le_of_lt hx), rpow_one] },
    rw [smul_eq_mul, this],
    field_simp [(ne_of_lt hx).symm],
    norm_num, ring },
  { rw [div_one, ‚Üêmul_div_assoc, mul_comm, mul_div_cancel _ (two_ne_zero' ‚Ñù)], }
end

/-- The special-value formula `Œì(1/2) = ‚àöœÄ`, which is equivalent to the Gaussian integral. -/
lemma complex.Gamma_one_half_eq : complex.Gamma (1 / 2) = œÄ ^ (1 / 2 : ‚ÑÇ) :=
begin
  convert congr_arg coe real.Gamma_one_half_eq,
  { simpa only [one_div, of_real_inv, of_real_bit0] using Gamma_of_real (1 / 2)},
  { rw [sqrt_eq_rpow, of_real_cpow pi_pos.le, of_real_div, of_real_bit0, of_real_one] }
end

namespace gaussian_fourier
/-! ## Fourier transform of the Gaussian integral
-/
open interval_integral
open_locale real

variables {b : ‚ÑÇ}

/-- The integral of the Gaussian function over the vertical edges of a rectangle
with vertices at `(¬±T, 0)` and `(¬±T, c)`.  -/
def vertical_integral (b : ‚ÑÇ) (c T : ‚Ñù) : ‚ÑÇ :=
‚à´ (y : ‚Ñù) in 0..c, I * (cexp (-b * (T + y * I) ^ 2) - cexp (-b * (T - y * I) ^ 2))

/-- Explicit formula for the norm of the Gaussian function along the vertical
edges. -/
lemma norm_cexp_neg_mul_sq_add_mul_I (b : ‚ÑÇ) (c T : ‚Ñù) :
  ‚Äñcexp (-b * (T + c * I) ^ 2)‚Äñ = exp (-(b.re * T ^ 2 - 2 * b.im * c * T - b.re * c ^ 2)) :=
begin
  rw [complex.norm_eq_abs, complex.abs_exp, neg_mul, neg_re, ‚Üêre_add_im b],
  simp only [sq, re_add_im, mul_re, mul_im, add_re, add_im, of_real_re, of_real_im, I_re, I_im],
  ring_nf,
end

lemma norm_cexp_neg_mul_sq_add_mul_I' (hb : b.re ‚â† 0) (c T : ‚Ñù) :
  ‚Äñcexp (-b * (T + c * I) ^ 2)‚Äñ =
  exp (-(b.re * (T - b.im * c / b.re) ^ 2 - c ^ 2 * (b.im ^ 2 / b.re  + b.re))) :=
begin
  have : (b.re * T ^ 2 - 2 * b.im * c * T - b.re * c ^ 2) =
  b.re * (T - b.im * c / b.re) ^ 2 - c ^ 2 * (b.im ^ 2 / b.re  + b.re),
  { field_simp, ring },
  rw [norm_cexp_neg_mul_sq_add_mul_I, this],
end

lemma vertical_integral_norm_le (hb : 0 < b.re) (c : ‚Ñù) {T : ‚Ñù} (hT : 0 ‚â§ T) :
  ‚Äñvertical_integral b c T‚Äñ
    ‚â§ 2 * |c| * exp (-(b.re * T ^ 2 - 2 * |b.im| * |c| * T - b.re * c ^ 2)) :=
begin
  -- first get uniform bound for integrand
  have vert_norm_bound : ‚àÄ {T : ‚Ñù}, 0 ‚â§ T ‚Üí ‚àÄ {c y : ‚Ñù}, |y| ‚â§ |c| ‚Üí
    ‚Äñcexp (-b * (T + y * I) ^ 2)‚Äñ ‚â§ exp (-(b.re * T ^ 2 - 2 * |b.im| * |c| * T - b.re * c ^ 2)),
  { intros T hT c y hy,
    rw [norm_cexp_neg_mul_sq_add_mul_I b, exp_le_exp, neg_le_neg_iff],
    refine sub_le_sub (sub_le_sub (le_refl _) (mul_le_mul_of_nonneg_right _ hT)) _,
    { conv_lhs {rw mul_assoc}, conv_rhs {rw mul_assoc},
      refine mul_le_mul_of_nonneg_left ((le_abs_self _).trans _) zero_le_two,
      rw abs_mul,
      exact mul_le_mul_of_nonneg_left hy (abs_nonneg _), },
    { refine mul_le_mul_of_nonneg_left _ hb.le,
      rwa sq_le_sq, } },
  -- now main proof
  refine (interval_integral.norm_integral_le_of_norm_le_const _).trans _,
  swap 3,
  { rw sub_zero,
    conv_lhs { rw mul_comm },
    conv_rhs { conv { congr, rw mul_comm }, rw mul_assoc } },
  { intros y hy,
    have absy : |y| ‚â§ |c|,
    { rcases le_or_lt 0 c,
      { rw uIoc_of_le h at hy,
        rw [abs_of_nonneg h, abs_of_pos hy.1],
        exact hy.2, },
      { rw uIoc_of_lt h at hy,
        rw [abs_of_neg h, abs_of_nonpos hy.2, neg_le_neg_iff],
        exact hy.1.le } },
    rw [norm_mul, complex.norm_eq_abs, abs_I, one_mul, two_mul],
    refine (norm_sub_le _ _).trans (add_le_add (vert_norm_bound hT absy) _),
    rw ‚Üêabs_neg y at absy,
    simpa only [neg_mul, of_real_neg] using vert_norm_bound hT absy },
end

lemma tendsto_vertical_integral (hb : 0 < b.re) (c : ‚Ñù) :
  tendsto (vertical_integral b c) at_top (ùìù 0) :=
begin
  -- complete proof using squeeze theorem:
  rw tendsto_zero_iff_norm_tendsto_zero,
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds _
    (eventually_of_forall (Œª _, norm_nonneg _))
    ((eventually_ge_at_top (0:‚Ñù)).mp (eventually_of_forall
      (Œª T hT, vertical_integral_norm_le hb c hT))),
  rw (by ring : 0 = 2 * |c| * 0),
  refine (tendsto_exp_at_bot.comp (tendsto_neg_at_top_at_bot.comp _)).const_mul _ ,
  apply tendsto_at_top_add_const_right,
  simp_rw [sq, ‚Üêmul_assoc, ‚Üêsub_mul],
  refine tendsto.at_top_mul_at_top (tendsto_at_top_add_const_right _ _ _) tendsto_id,
  exact (tendsto_const_mul_at_top_of_pos hb).mpr tendsto_id,
end

lemma integrable_cexp_neg_mul_sq_add_real_mul_I (hb : 0 < b.re) (c : ‚Ñù) :
  integrable (Œª (x : ‚Ñù), cexp (-b * (x + c * I) ^ 2)) :=
begin
  refine ‚ü®(complex.continuous_exp.comp (continuous_const.mul ((continuous_of_real.add
    continuous_const).pow 2))).ae_strongly_measurable, _‚ü©,
  rw ‚Üêhas_finite_integral_norm_iff,
  simp_rw [norm_cexp_neg_mul_sq_add_mul_I' hb.ne', neg_sub _ (c ^ 2 * _),
    sub_eq_add_neg _ (b.re * _), real.exp_add],
  suffices : integrable (Œª (x : ‚Ñù), exp (-(b.re * x ^ 2))),
  { exact (integrable.comp_sub_right this (b.im * c / b.re)).has_finite_integral.const_mul _, },
  simp_rw ‚Üêneg_mul,
  apply integrable_exp_neg_mul_sq hb,
end

lemma integral_cexp_neg_mul_sq_add_real_mul_I (hb : 0 < b.re) (c : ‚Ñù) :
  ‚à´ (x : ‚Ñù), cexp (-b * (x + c * I) ^ 2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) :=
begin
  refine tendsto_nhds_unique (interval_integral_tendsto_integral
    (integrable_cexp_neg_mul_sq_add_real_mul_I hb c) tendsto_neg_at_top_at_bot tendsto_id) _,
  set I‚ÇÅ := (Œª T, ‚à´ (x : ‚Ñù) in -T..T, cexp (-b * (x + c * I) ^ 2)) with HI‚ÇÅ,
  let I‚ÇÇ := Œª (T : ‚Ñù), ‚à´ (x : ‚Ñù) in -T..T, cexp (-b * x ^ 2),
  let I‚ÇÑ := Œª (T : ‚Ñù), ‚à´ (y : ‚Ñù) in 0..c, cexp (-b * (T + y * I) ^ 2),
  let I‚ÇÖ := Œª (T : ‚Ñù), ‚à´ (y : ‚Ñù) in 0..c, cexp (-b * (-T + y * I) ^ 2),
  have C : ‚àÄ (T : ‚Ñù), I‚ÇÇ T - I‚ÇÅ T + I * I‚ÇÑ T - I * I‚ÇÖ T = 0,
  { assume T,
    have := integral_boundary_rect_eq_zero_of_differentiable_on
    (Œª z, cexp (-b * z ^ 2)) (-T) (T + c * I)
    (by { refine differentiable.differentiable_on (differentiable.const_mul _ _).cexp,
    exact differentiable_pow 2, }),
    simpa only [neg_im, of_real_im, neg_zero, of_real_zero, zero_mul, add_zero, neg_re, of_real_re,
      add_re, mul_re, I_re, mul_zero, I_im, tsub_zero, add_im, mul_im, mul_one, zero_add,
      algebra.id.smul_eq_mul, of_real_neg] using this },
  simp_rw [id.def, ‚ÜêHI‚ÇÅ],
  have : I‚ÇÅ = Œª (T : ‚Ñù), I‚ÇÇ T + vertical_integral b c T,
  { ext1 T,
    specialize C T,
    rw sub_eq_zero at C,
    unfold vertical_integral,
    rw [integral_const_mul, interval_integral.integral_sub],
    { simp_rw (Œª a b, by { rw sq, ring_nf } : ‚àÄ (a b : ‚ÑÇ), (a - b * I)^2 = (- a + b * I)^2),
      change I‚ÇÅ T = I‚ÇÇ T + I * (I‚ÇÑ T - I‚ÇÖ T),
      rw [mul_sub, ‚ÜêC],
      abel },
    all_goals { apply continuous.interval_integrable, continuity }, },
  rw [this, ‚Üêadd_zero ((œÄ / b : ‚ÑÇ) ^ (1 / 2 : ‚ÑÇ)), ‚Üêintegral_gaussian_complex hb],
  refine tendsto.add _ (tendsto_vertical_integral hb c),
  exact interval_integral_tendsto_integral (integrable_cexp_neg_mul_sq hb)
    tendsto_neg_at_top_at_bot tendsto_id,
end

lemma _root_.integral_cexp_neg_mul_sq_add_const (hb : 0 < b.re) (c : ‚ÑÇ) :
  ‚à´ (x : ‚Ñù), cexp (-b * (x + c) ^ 2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) :=
begin
  rw ‚Üêre_add_im c,
  simp_rw [‚Üêadd_assoc, ‚Üêof_real_add],
  rw integral_add_right_eq_self (Œª(x : ‚Ñù), cexp (-b * (‚Üëx + ‚Üë(c.im) * I) ^ 2)),
  { apply integral_cexp_neg_mul_sq_add_real_mul_I hb },
  { apply_instance },
end

lemma _root_.fourier_transform_gaussian (hb : 0 < b.re) (t : ‚ÑÇ) :
  ‚à´ (x : ‚Ñù), cexp (I * t * x) * cexp (-b * x ^ 2) = cexp (-t^2 / (4 * b)) * (œÄ / b) ^ (1 / 2 : ‚ÑÇ) :=
begin
  have : b ‚â† 0,
  { contrapose! hb, rw [hb, zero_re] },
  simp_rw [‚Üêcomplex.exp_add],
  have : ‚àÄ (x : ‚ÑÇ), I * t * x + (-b * x ^ 2) = -t ^ 2 / (4 * b) + -b * (x + (-I * t / 2 / b)) ^ 2,
  { intro x,
    ring_nf SOP,
    rw I_sq,
    field_simp, ring },
  simp_rw [this, complex.exp_add, integral_mul_left, integral_cexp_neg_mul_sq_add_const hb]
end

lemma _root_.fourier_transform_gaussian_pi (hb : 0 < b.re) (t : ‚ÑÇ) :
  ‚à´ (x : ‚Ñù), cexp (2 * œÄ * I * t * x) * cexp (-œÄ * b * x ^ 2) =
    1 / b ^ (1 / 2 : ‚ÑÇ) * cexp (-œÄ * (1 / b) * t ^ 2) :=
begin
  have h1 : 0 < re (œÄ * b) := by { rw of_real_mul_re, exact mul_pos pi_pos hb },
  have h2 : b ‚â† 0 := by { contrapose! hb, rw [hb, zero_re], },
  convert _root_.fourier_transform_gaussian h1 (2 * œÄ * t) using 1,
  { congr' 1,
    ext1 x,
    congr' 2,
    all_goals { ring } },
  { conv_lhs { rw mul_comm },
    congr' 2,
    { field_simp [of_real_ne_zero.mpr pi_ne_zero], ring, },
    { rw [‚Üêdiv_div, div_self (of_real_ne_zero.mpr pi_ne_zero), cpow_def_of_ne_zero h2,
        cpow_def_of_ne_zero (one_div_ne_zero h2), one_div, ‚Üêcomplex.exp_neg, ‚Üêneg_mul],
      congr' 2,
      rw [one_div, complex.log_inv],
      rw [ne.def, arg_eq_pi_iff, not_and_distrib, not_lt],
      exact or.inl hb.le } },
end

end gaussian_fourier
