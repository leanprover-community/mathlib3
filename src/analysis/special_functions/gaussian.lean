/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import analysis.special_functions.gamma
import analysis.special_functions.polar_coord

/-!
# Gaussian integral

We prove the formula `âˆ« x, exp (-b * x^2) = sqrt (Ï€ / b)`, in `integral_gaussian`.
-/

noncomputable theory

namespace complex

lemma is_connected_halfplane : is_connected {x:â„‚ | 0 < re x} :=
begin
  refine (convex.is_path_connected (Î» x hx y hy a b ha hb hab, _) (by {use 1, simp})).is_connected,
  rw [set.mem_set_of, add_re, smul_re, smul_re, smul_eq_mul, smul_eq_mul],
  rcases lt_or_eq_of_le ha with ha|rfl,
  { exact add_pos_of_pos_of_nonneg (mul_pos ha hx) (mul_nonneg hb $ le_of_lt hy) },
  { rw zero_add at hab, subst hab, simpa using hy },
end

end complex

open real set measure_theory filter asymptotics
open complex (hiding exp continuous_exp abs_of_nonneg)
notation `cexp` := complex.exp
open_locale real topological_space

lemma exp_neg_mul_sq_is_o_exp_neg {b : â„} (hb : 0 < b) :
  (Î» x:â„, exp (-b * x^2)) =o[at_top] (Î» x:â„, exp (-x)) :=
begin
  have A : (Î» (x : â„), -x - -b * x ^ 2) = (Î» x, x * (b * x + (- 1))), by { ext x, ring },
  rw [is_o_exp_comp_exp_comp, A],
  apply tendsto.at_top_mul_at_top tendsto_id,
  apply tendsto_at_top_add_const_right at_top (-1 : â„),
  exact tendsto.const_mul_at_top hb tendsto_id,
end

lemma rpow_mul_exp_neg_mul_sq_is_o_exp_neg {b : â„} (hb : 0 < b) (s : â„) :
  (Î» x:â„, x ^ s * exp (-b * x^2)) =o[at_top] (Î» x:â„, exp (-(1/2) * x)) :=
begin
  apply ((is_O_refl (Î» x:â„, x ^ s) at_top).mul_is_o (exp_neg_mul_sq_is_o_exp_neg hb)).trans,
  convert Gamma_integrand_is_o s,
  simp_rw [mul_comm],
end

lemma integrable_on_rpow_mul_exp_neg_mul_sq {b : â„} (hb : 0 < b) {s : â„} (hs : -1 < s) :
  integrable_on (Î» x:â„, x ^ s * exp (-b * x^2)) (Ioi 0) :=
begin
  rw [â† Ioc_union_Ioi_eq_Ioi (zero_le_one : (0 : â„) â‰¤ 1), integrable_on_union],
  split,
  { rw [â†integrable_on_Icc_iff_integrable_on_Ioc],
    refine integrable_on.mul_continuous_on _ _ is_compact_Icc,
    { refine (interval_integrable_iff_integrable_Icc_of_le zero_le_one).mp _,
      exact interval_integral.interval_integrable_rpow' hs },
    { exact (continuous_exp.comp (continuous_const.mul (continuous_pow 2))).continuous_on } },
  { have B : (0 : â„) < 1/2, by norm_num,
    apply integrable_of_is_O_exp_neg B _ (is_o.is_O (rpow_mul_exp_neg_mul_sq_is_o_exp_neg hb _)),
    assume x hx,
    have N : x â‰  0, { refine (zero_lt_one.trans_le _).ne', exact hx },
    apply ((continuous_at_rpow_const _ _ (or.inl N)).mul _).continuous_within_at,
    exact (continuous_exp.comp (continuous_const.mul (continuous_pow 2))).continuous_at },
end

lemma integrable_rpow_mul_exp_neg_mul_sq {b : â„} (hb : 0 < b) {s : â„} (hs : -1 < s) :
  integrable (Î» x:â„, x ^ s * exp (-b * x^2)) :=
begin
  rw [â† integrable_on_univ, â† @Iio_union_Ici _ _ (0 : â„), integrable_on_union,
      integrable_on_Ici_iff_integrable_on_Ioi],
  refine âŸ¨_, integrable_on_rpow_mul_exp_neg_mul_sq hb hsâŸ©,
  rw â† (measure.measure_preserving_neg (volume : measure â„)).integrable_on_comp_preimage
    ((homeomorph.neg â„).to_measurable_equiv.measurable_embedding),
  simp only [function.comp, neg_sq, neg_preimage, preimage_neg_Iio, neg_neg, neg_zero],
  apply integrable.mono' (integrable_on_rpow_mul_exp_neg_mul_sq hb hs),
  { apply measurable.ae_strongly_measurable,
    exact (measurable_id'.neg.pow measurable_const).mul
      ((measurable_id'.pow measurable_const).const_mul (-b)).exp },
  { have : measurable_set (Ioi (0 : â„)) := measurable_set_Ioi,
    filter_upwards [ae_restrict_mem this] with x hx,
    have h'x : 0 â‰¤ x := le_of_lt hx,
    rw [real.norm_eq_abs, abs_mul, abs_of_nonneg (exp_pos _).le],
    apply mul_le_mul_of_nonneg_right _ (exp_pos _).le,
    simpa [abs_of_nonneg h'x] using abs_rpow_le_abs_rpow (-x) s }
end

lemma integrable_exp_neg_mul_sq {b : â„} (hb : 0 < b) :
  integrable (Î» x:â„, exp (-b * x^2)) :=
begin
  have A : (-1 : â„) < 0, by norm_num,
  convert integrable_rpow_mul_exp_neg_mul_sq hb A,
  simp,
end

lemma integrable_on_Ioi_exp_neg_mul_sq_iff {b : â„} :
  integrable_on (Î» x:â„, exp (-b * x^2)) (Ioi 0) â†” 0 < b :=
begin
  refine âŸ¨Î» h, _, Î» h, (integrable_exp_neg_mul_sq h).integrable_onâŸ©,
  by_contra' hb,
  have : âˆ«â» x:â„ in Ioi 0, 1 â‰¤ âˆ«â» x:â„ in Ioi 0, â€–exp (-b * x^2)â€–â‚Š,
  { apply lintegral_mono (Î» x, _),
    simp only [neg_mul, ennreal.one_le_coe_iff, â† to_nnreal_one, to_nnreal_le_iff_le_coe,
      real.norm_of_nonneg (exp_pos _).le, coe_nnnorm, one_le_exp_iff, right.nonneg_neg_iff],
    exact mul_nonpos_of_nonpos_of_nonneg hb (sq_nonneg _) },
  simpa using this.trans_lt h.2,
end

lemma integrable_exp_neg_mul_sq_iff {b : â„} : integrable (Î» x:â„, exp (-b * x^2)) â†” 0 < b :=
âŸ¨Î» h, integrable_on_Ioi_exp_neg_mul_sq_iff.mp h.integrable_on, integrable_exp_neg_mul_sqâŸ©

lemma integrable_mul_exp_neg_mul_sq {b : â„} (hb : 0 < b) : integrable (Î» x:â„, x * exp (-b * x^2)) :=
begin
  convert integrable_rpow_mul_exp_neg_mul_sq hb (by norm_num : (-1 : â„) < 1),
  simp,
end

lemma norm_cexp_neg_mul_sq (b : â„‚) (x : â„) : â€–complex.exp (-b * x^2)â€– = exp (-b.re * x^2) :=
by rw [complex.norm_eq_abs, complex.abs_exp, â†of_real_pow, mul_comm (-b) _, of_real_mul_re,
  neg_re, mul_comm]

lemma integrable_cexp_neg_mul_sq {b : â„‚} (hb : 0 < b.re) : integrable (Î» x:â„, cexp (-b * x^2)) :=
begin
  refine âŸ¨(complex.continuous_exp.comp
    (continuous_const.mul (continuous_of_real.pow 2))).ae_strongly_measurable, _âŸ©,
  rw â†has_finite_integral_norm_iff,
  simp_rw norm_cexp_neg_mul_sq,
  exact (integrable_exp_neg_mul_sq hb).2,
end

lemma integral_mul_cexp_neg_mul_sq {b : â„‚} (hb : 0 < b.re) :
  âˆ« r:â„ in Ioi 0, â†‘r * cexp (-b * r ^ 2) = (2 * b)â»Â¹ :=
begin
  have hb' : b â‰  0 := by { contrapose! hb, rw [hb, zero_re], },
  have I : integrable (Î» x:â„, â†‘x * cexp (-b * x^2)),
  { refine âŸ¨(continuous_of_real.mul (complex.continuous_exp.comp _)).ae_strongly_measurable, _âŸ©,
    { exact continuous_const.mul (continuous_of_real.pow 2)},
    have := (integrable_mul_exp_neg_mul_sq hb).has_finite_integral,
    rw â†has_finite_integral_norm_iff at this âŠ¢,
    convert this,
    ext1 x,
    rw [norm_mul, norm_mul, norm_cexp_neg_mul_sq b, complex.norm_eq_abs, abs_of_real,
      real.norm_eq_abs, norm_of_nonneg (exp_pos _).le] },
  refine tendsto_nhds_unique
    (interval_integral_tendsto_integral_Ioi _ I.integrable_on filter.tendsto_id) _,
  have A : âˆ€ x:â„‚, has_deriv_at (Î» x, - (2 * b)â»Â¹ * cexp (-b * x^2)) (x * cexp (- b * x^2)) x,
  { intro x,
    convert (((has_deriv_at_pow 2 x)).const_mul (-b)).cexp.const_mul (- (2 * b)â»Â¹) using 1,
    field_simp [hb'],
    ring },
  have : âˆ€ (y : â„), âˆ« x in 0..(id y), â†‘x * cexp (-b * x^2)
      = (- (2 * b)â»Â¹ * cexp (-b * y^2)) - (- (2 * b)â»Â¹ * cexp (-b * 0^2)) :=
    Î» y, interval_integral.integral_eq_sub_of_has_deriv_at
      (Î» x hx, (A x).comp_of_real) I.interval_integrable,
  simp_rw this,
  have L : tendsto (Î» (x : â„), (2 * b)â»Â¹ - (2 * b)â»Â¹ * cexp (-b * x ^ 2)) at_top
    (ğ“ ((2 * b)â»Â¹ - (2 * b)â»Â¹ * 0)),
  { refine tendsto_const_nhds.sub (tendsto.const_mul _ _),
    rw tendsto_zero_iff_norm_tendsto_zero,
    simp_rw norm_cexp_neg_mul_sq b,
    apply tendsto_exp_at_bot.comp,
    exact tendsto.neg_const_mul_at_top (neg_lt_zero.2 hb) (tendsto_pow_at_top two_ne_zero) },
  simpa using L,
end

/-- The *square* of the Gaussian integral `âˆ« x:â„, exp (-b * x^2)` is equal to `Ï€ / b`. -/
lemma integral_gaussian_sq_complex {b : â„‚} (hb : 0 < b.re) :
  (âˆ« x:â„, cexp (-b * x^2)) ^ 2 = Ï€ / b :=
begin
  /- Adapted from sgouezel's proof of `integral_gaussian`. We compute `(âˆ« exp(-b x^2))^2` as an
  integral over `â„^2`, and then make a polar change of coordinates. We are left with
  `âˆ« r * exp (-b r^2)`, which has been computed in `integral_mul_cexp_neg_mul_sq` using the fact
  that this function has an obvious primitive. -/
  calc
  (âˆ« x:â„, cexp (-b * (x:â„‚)^2)) ^ 2
      = âˆ« p : â„ Ã— â„, cexp (-b * ((p.1) : â„‚) ^ 2) * cexp (-b * ((p.2) : â„‚) ^ 2) :
    by { rw [pow_two, â† integral_prod_mul], refl }
  ... = âˆ« p : â„ Ã— â„, cexp (- b * (p.1 ^ 2 + p.2 ^ 2)) :
    by { congr, ext1 p, rw [â† complex.exp_add, mul_add], }
  ... = âˆ« p in polar_coord.target, (p.1) â€¢ cexp (- b * ((p.1 * cos p.2) ^ 2 + (p.1 * sin p.2)^2)) :
    begin
      rw â† integral_comp_polar_coord_symm,
      simp only [polar_coord_symm_apply, of_real_mul, of_real_cos, of_real_sin],
    end
  ... = (âˆ« r in Ioi (0 : â„), r * cexp (-b * r^2)) * (âˆ« Î¸ in Ioo (-Ï€) Ï€, 1) :
    begin
      rw â† set_integral_prod_mul,
      congr' with p : 1,
      rw mul_one,
      congr,
      conv_rhs { rw [â† one_mul ((p.1 : â„‚)^2), â† sin_sq_add_cos_sq (p.2 : â„‚)], },
      ring_exp,
    end
  ... = â†‘Ï€ / b :
    begin
      have : 0 â‰¤ Ï€ + Ï€, by linarith [real.pi_pos],
      simp only [integral_const, measure.restrict_apply', measurable_set_Ioo, univ_inter,
        volume_Ioo, sub_neg_eq_add, ennreal.to_real_of_real, this],
      rw [â†two_mul, real_smul, mul_one, of_real_mul, of_real_bit0, of_real_one,
        integral_mul_cexp_neg_mul_sq hb],
      field_simp [(by { contrapose! hb, rw [hb, zero_re] } : b â‰  0)],
      ring,
    end
end

theorem integral_gaussian (b : â„) : âˆ« x, exp (-b * x^2) = sqrt (Ï€ / b) :=
begin
  /- First we deal with the crazy case where `b â‰¤ 0`: then both sides vanish. -/
  rcases le_or_lt b 0 with hb|hb,
  { rw [integral_undef, sqrt_eq_zero_of_nonpos],
    { exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb },
    { simpa only [not_lt, integrable_exp_neg_mul_sq_iff] using hb } },
  /- Assume now `b > 0`. Then both sides are non-negative and their squares agree. -/
  refine (sq_eq_sq _ (sqrt_nonneg _)).1 _,
  { exact integral_nonneg (Î» x, (exp_pos _).le) },
  rw [â†of_real_inj, of_real_pow, â†integral_of_real, sq_sqrt (div_pos pi_pos hb).le, of_real_div],
  convert integral_gaussian_sq_complex (by rwa of_real_re : 0 < (b:â„‚).re),
  { ext1 x,
    rw [of_real_exp, of_real_mul, of_real_pow, of_real_neg] },
end

lemma is_preconnected.constant_of_finite_range
  {Î± Î² : Type} [topological_space Î±] [topological_space Î²] [t1_space Î²]
  {S : set Î±} (hS : is_preconnected S) {T : set Î²} {f : Î± â†’ Î²}
  (hc : continuous_on f S) (hTf : finite T) (hTm : âˆ€ (a : Î±), a âˆˆ S â†’ f a âˆˆ T) :
  âˆ€ (x y : Î±), (x âˆˆ S) â†’ (y âˆˆ S) â†’ f x = f y :=
begin
  intros x y hx hy,
  let F : S â†’ T := (Î» x:S, âŸ¨f x.val, hTm x.val x.propertyâŸ©),
  suffices : F âŸ¨x, hxâŸ© = F âŸ¨y, hyâŸ©,
  { rw â†subtype.coe_inj at this, exact this },
  haveI : discrete_topology T := by apply discrete_of_t1_of_finite,
  exact (is_preconnected_iff_preconnected_space.mp hS).constant
    (continuous_induced_rng.mpr $ continuous_on_iff_continuous_restrict.mp hc)
end

/-- If `f, g` are continuous functions `0 < re z`, and `(f z) ^ 2 = (g z) ^ 2` holds on this region,
then as soon as `f 1 = g 1` we have `f z = g z` for all `z`. -/
lemma eq_of_sq_eq_of_continuous {f g : â„‚ â†’ â„‚}
  (h_one : f 1 = g 1) (hsq : âˆ€ x:â„‚, 0 < re x â†’ (f x) ^ 2 = (g x) ^ 2)
  (hf : continuous_on f {x : â„‚ | 0 < re x}) (hg : continuous_on g {x : â„‚ | 0 < re x})
  (hg_ne : âˆ€ x:â„‚, 0 < re x â†’ g x â‰  0)
  {z : â„‚} (hz : 0 < re z) : f z = g z :=
begin
  let r := Î» (z : â„‚), f z / g z,
  suffices : r z = 1, { rwa div_eq_one_iff_eq at this, exact hg_ne z hz },
  rw â† (by { rwa div_eq_one_iff_eq, apply hg_ne, simp } : r 1 = 1),
  have r_mem : âˆ€ (x : â„‚), 0 < re x â†’ r x âˆˆ ({-1, 1} : set â„‚),
  { intros x hx,
    specialize hg_ne x hx,
    specialize hsq x hx,
    rwa [mem_insert_iff, mem_singleton_iff, or.comm, â†sq_eq_one_iff, div_pow, div_eq_one_iff_eq],
    contrapose! hg_ne,
    rwa sq_eq_zero_iff at hg_ne },
  exact is_connected_halfplane.is_preconnected.constant_of_finite_range
    (hf.div hg hg_ne) (finite.of_fintype _) r_mem z 1 hz (by simp),
end

lemma continuous_at_gaussian_integral (b : â„‚) (hb : 0 < re b) :
  continuous_at (Î» c:â„‚, âˆ« x:â„, cexp (-c * x^2)) b :=
begin
  -- We show the integral is *differentiable* as a function of `b`, using the rather powerful
  -- theorem `has_deriv_at_integral_of_dominated_loc_of_deriv_le`, and deduce continuity from that.
  apply has_deriv_at.continuous_at,
  -- set up the variables to be used
  let Îµ := (re b) / 2,
  let f  : â„‚ â†’ â„ â†’ â„‚ := Î» (c : â„‚) (x : â„), cexp (-c * x ^ 2),
  let f' : â„‚ â†’ â„ â†’ â„‚ := Î» (c : â„‚) (x : â„), -x^2 * cexp (-c * x ^ 2),
  let bd : â„ â†’ â„     := Î» (x : â„), x ^ 2 * exp (-b.re / 2 * x ^ 2),
  -- the hypotheses
  have eps_pos : 0 < Îµ := div_pos hb two_pos,
  have f_meas : âˆ€ (c:â„‚), ae_strongly_measurable (f c) volume,
  { intro c,
    apply continuous.ae_strongly_measurable,
    exact complex.continuous_exp.comp (continuous_const.mul (continuous_of_real.pow 2)) },
  have f_int : integrable (f b) volume,
  { simp_rw [â†integrable_norm_iff (f_meas b), norm_cexp_neg_mul_sq b],
    exact integrable_exp_neg_mul_sq hb, },
  have f'b_meas : ae_strongly_measurable (f' b) volume,
  { apply continuous.ae_strongly_measurable,
    exact (continuous_of_real.pow 2).neg.mul
      (complex.continuous_exp.comp (continuous_const.mul (continuous_of_real.pow 2))) },
  have f'_le_bd : âˆ€áµ (x : â„), âˆ€ (c : â„‚), c âˆˆ metric.ball b Îµ â†’ â€–f' c xâ€– â‰¤ bd x,
  { refine ae_of_all _ (Î» x c hc, _),
    have : b.re / 2 < c.re,
    { rw [metric.mem_ball, dist_comm, dist_eq_norm_sub] at hc,
      dsimp only [Îµ] at hc,
      have := (re_le_abs $ _).trans_lt hc,
      rw sub_re at this,
      linarith },
    rw [norm_mul, norm_cexp_neg_mul_sq, norm_neg, â†of_real_pow, complex.norm_eq_abs, abs_of_real,
      abs_sq],
    apply mul_le_mul_of_nonneg_left,
    rw exp_le_exp,
    apply mul_le_mul_of_nonneg_right, linarith,
    apply sq_nonneg,
    apply sq_nonneg },
  have integrable_bd : integrable bd,
  { dsimp only [bd],
    convert integrable_rpow_mul_exp_neg_mul_sq eps_pos (by norm_num : (-1 : â„) < 2),
    ext1 x, congr' 1,
    { rw [â†rpow_nat_cast, nat.cast_bit0, nat.cast_one] },
    { rw neg_div } },
  have f_der : âˆ€áµ x:â„, âˆ€ (c : â„‚), c âˆˆ metric.ball b Îµ â†’ has_deriv_at (Î» d:â„‚, f d x) (f' c x) c,
  { refine ae_of_all _ (Î» x c hc, _),
    dsimp only [f, f'],
    conv {congr, skip, rw mul_comm, },
    refine (complex.has_deriv_at_exp _).comp c _,
    simp_rw neg_mul,
    refine has_deriv_at.neg _,
    apply has_deriv_at_mul_const, },
  exact and.elim_right (has_deriv_at_integral_of_dominated_loc_of_deriv_le eps_pos
    (eventually_of_forall f_meas) f_int f'b_meas f'_le_bd integrable_bd f_der),
end

theorem integral_gaussian_complex {b : â„‚} (hb : 0 < re b) :
  âˆ« x:â„, cexp (-b * x^2) = (Ï€ / b) ^ (1 / 2 : â„‚) :=
begin
  have nv : âˆ€ {b : â„‚}, (0 < re b) â†’ (b â‰  0),
  { intros b hb, contrapose! hb, rw hb, simp },
  convert eq_of_sq_eq_of_continuous _ (Î» c hc, _) _ _ (Î» c hc, _) hb,
  { -- first check equality at 1
    have : âˆ€ (x : â„), cexp (-1 * x ^ 2) = exp (-1 * x ^ 2),
    { intro x,
      simp only [of_real_exp, neg_mul, one_mul, of_real_neg, of_real_pow] },
    simp_rw [this, integral_of_real],
    conv_rhs {  congr, rw [â†of_real_one, â†of_real_div], skip,
      rw [â†of_real_one, â†of_real_bit0, â†of_real_div]  },
    rw [â†of_real_cpow, of_real_inj],
    convert integral_gaussian (1 : â„),
    { rwa [sqrt_eq_rpow] },
    { rw [div_one], exact pi_pos.le } },
  { -- squares of both sides agree
    rw [integral_gaussian_sq_complex hc, sq],
    conv_lhs { rw â†cpow_one (â†‘Ï€ / c)},
    rw â† cpow_add _ _ (div_ne_zero (of_real_ne_zero.mpr pi_ne_zero) (nv hc)),
    norm_num },
  { -- integral is continuous
    exact continuous_at.continuous_on (Î» b hb, continuous_at_gaussian_integral b hb), },
  { -- `(Ï€ / b) ^ (1 / 2 : â„‚)` is continuous
    refine continuous_at.continuous_on (Î» b hb, (continuous_at_cpow_const (or.inl _)).comp
      (continuous_at_const.div continuous_at_id (nv hb))),
    rw [div_re, of_real_im, of_real_re, zero_mul, zero_div, add_zero],
    exact div_pos (mul_pos pi_pos hb) (norm_sq_pos.mpr (nv hb)), },
  { -- RHS doesn't vanish
    rw [ne.def, cpow_eq_zero_iff, not_and_distrib],
    exact or.inl (div_ne_zero (of_real_ne_zero.mpr pi_ne_zero) (nv hc)) },
end

/- The Gaussian integral on the half-line, `âˆ« x in Ioi 0, exp (-b * x^2)`, for complex `b`. -/
lemma integral_gaussian_complex_Ioi {b : â„‚} (hb : 0 < re b) :
  âˆ« x:â„ in Ioi 0, cexp (-b * x^2) = (Ï€ / b) ^ (1 / 2 : â„‚) / 2 :=
begin
  have full_integral := integral_gaussian_complex hb,
  have : measurable_set (Ioi (0:â„)) := measurable_set_Ioi,
  rw [â†integral_add_compl this (integrable_cexp_neg_mul_sq hb), compl_Ioi] at full_integral,
  suffices : âˆ« x:â„ in Iic 0, cexp (-b * x^2) = âˆ« x:â„ in Ioi 0, cexp (-b * x^2),
  { rw [this, â†mul_two] at full_integral,
    rwa eq_div_iff, exact two_ne_zero },
  have : âˆ€ (c : â„), âˆ« x in 0 .. c, cexp (-b * x^2) = âˆ« x in -c .. 0, cexp (-b * x^2),
  { intro c,
    have := @interval_integral.integral_comp_sub_left _ _ _ _ 0 c (Î» x, cexp (-b * x^2)) 0,
    simpa [zero_sub, neg_sq, neg_zero] using this },
  have t1 := interval_integral_tendsto_integral_Ioi _
     ((integrable_cexp_neg_mul_sq hb).integrable_on) tendsto_id,
  have t2 : tendsto (Î» c:â„, âˆ« x:â„ in 0..c,
    cexp (-b * x^2)) at_top (ğ“ âˆ« x:â„ in Iic 0, cexp (-b * x^2)),
  { simp_rw this,
    refine interval_integral_tendsto_integral_Iic _ _ tendsto_neg_at_top_at_bot,
    apply (integrable_cexp_neg_mul_sq hb).integrable_on },
  exact tendsto_nhds_unique t2 t1,
end

/- The Gaussian integral on the half-line, `âˆ« x in Ioi 0, exp (-b * x^2)`, for real `b`. -/
lemma integral_gaussian_Ioi (b : â„) : âˆ« x in Ioi 0, exp (-b * x^2) = sqrt (Ï€ / b) / 2 :=
begin
  rcases le_or_lt b 0 with hb|hb,
  { rw [integral_undef, sqrt_eq_zero_of_nonpos, zero_div],
    exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb,
    rwa [â†integrable_on, integrable_on_Ioi_exp_neg_mul_sq_iff, not_lt] },
  rw [â†of_real_inj, â†integral_of_real],
  convert integral_gaussian_complex_Ioi (by rwa of_real_re : 0 < (b:â„‚).re),
  { ext1 x, simp, },
  { rw [sqrt_eq_rpow, â†of_real_div, of_real_div, of_real_cpow],
    norm_num,
    exact (div_pos pi_pos hb).le, }
end

namespace complex

/-- The special-value formula `Î“(1/2) = âˆšÏ€`, which is equivalent to the Gaussian integral. -/
lemma Gamma_one_half_eq : Gamma (1 / 2) = sqrt Ï€ :=
begin
  -- first reduce to real integrals
  have hh : (1 / 2 : â„‚) = â†‘(1 / 2 : â„),
  { simp only [one_div, of_real_inv, of_real_bit0, of_real_one] },
  have hh2 : (1 / 2 : â„‚).re = 1 / 2,
  { convert of_real_re (1 / 2 : â„) },
  replace hh2 : 0 < (1 / 2 : â„‚).re := by { rw hh2, exact one_half_pos, },
  rw [Gamma_eq_integral _ hh2, hh, Gamma_integral_of_real, of_real_inj, real.Gamma_integral],
  -- now do change-of-variables
  rw â†integral_comp_rpow_Ioi_of_pos zero_lt_two,
  have : eq_on (Î» x:â„, (2 * x^((2:â„) - 1)) â€¢ (real.exp (-x^(2:â„)) * (x^(2:â„)) ^ (1 / (2:â„) - 1)))
  (Î» x:â„, 2 * real.exp ((-1) * x ^ (2:â„•))) (Ioi 0),
  { intros x hx, dsimp only,
    have : (x^(2:â„)) ^ (1 / (2:â„) - 1) = xâ»Â¹,
    { rw â†rpow_mul (le_of_lt hx), norm_num,
      rw [rpow_neg (le_of_lt hx), rpow_one] },
    rw [smul_eq_mul, this],
    field_simp [(ne_of_lt hx).symm],
    norm_num, ring },
  rw [set_integral_congr measurable_set_Ioi this, integral_mul_left, integral_gaussian_Ioi],
  field_simp, ring,
end

end complex
