/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.special_functions.integrals

/-!
# Non integrable functions

In this file we prove that the derivative of a function that tends to infinity is not interval
integrable, see `interval_integral.not_integrable_has_deriv_at_of_tendsto_norm_at_top_filter` and
`interval_integral.not_integrable_has_deriv_at_of_tendsto_norm_at_top_punctured`.  Then we apply the
latter lemma to prove that the function `Î» x, xâ»Â¹` is integrable on `a..b` if and only if `a = b` or
`0 âˆ‰ [a, b]`.

## Tags

integrable function
-/

open_locale measure_theory topological_space interval
open measure_theory topological_space set filter asymptotics

namespace interval_integral

variables {E : Type*} [normed_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [second_countable_topology E] [complete_space E]

/-- If `f` has derivative `f'` eventually along a nontrivial filter `l : filter â„` that is generated
by convex sets, the norm of `f` tends to infinity along `l`, then `f'` is not integrable on any
interval `a..b` such that `[a, b] âˆˆ l`. -/
lemma not_integrable_has_deriv_at_of_tendsto_norm_at_top_filter {f f' : â„ â†’ E} {a b : â„}
  (l : filter â„) [ne_bot l] [tendsto_Ixx_class Icc l l] (hl : [a, b] âˆˆ l)
  (hd : âˆ€á¶  x in l, has_deriv_at f (f' x) x) (hf : tendsto (Î» x, âˆ¥f xâˆ¥) l at_top) :
  Â¬interval_integrable f' volume a b :=
begin
  intro hfi,
  have h : âˆ€á¶  x : â„ Ã— â„ in l.prod l, âˆ€ y âˆˆ [x.1, x.2], has_deriv_at f (f' y) y âˆ§ y âˆˆ [a, b],
    from (tendsto_fst.interval tendsto_snd).eventually (hd.and hl).lift'_powerset,
  rcases mem_prod_self_iff.1 h with âŸ¨s, hsl, hsâŸ©,
  rcases filter.nonempty_of_mem hsl with âŸ¨c, hcâŸ©,
  rcases ((hf.eventually (eventually_gt_at_top (âˆ¥f câˆ¥ + |âˆ« x in a..b, âˆ¥f' xâˆ¥|))).and hsl).exists
    with âŸ¨d, hdf, hds : d âˆˆ sâŸ©,
  have hcd : [c, d] âŠ† [a, b],
    from interval_subset_interval ((hs (mk_mem_prod hc hc) _ left_mem_interval).2)
      ((hs (mk_mem_prod hds hds) _ left_mem_interval).2),
  have hcd' : Î™ c d âŠ† Î™ a b,
    from interval_oc_subset_interval_oc_of_interval_subset_interval hcd,
  refine hdf.not_le (sub_le_iff_le_add'.1 _),
  calc âˆ¥f dâˆ¥ - âˆ¥f câˆ¥ â‰¤ âˆ¥f d - f câˆ¥ : norm_sub_norm_le _ _
  ... = âˆ¥âˆ« x in d..c, f' xâˆ¥ :
    begin
      rw [integral_symm, norm_neg],
      refine congr_arg _ (integral_eq_sub_of_has_deriv_at (Î» x hx, _) _).symm,
      exacts [(hs (mk_mem_prod hc hds) _ hx).1, hfi.mono hcd le_rfl]
    end
  ... â‰¤ |âˆ« x in d..c, âˆ¥f' xâˆ¥| : norm_integral_le_abs_integral_norm
  ... â‰¤ |âˆ« x in a..b, âˆ¥f' xâˆ¥| : abs_integral_mono_interval (interval_oc_swap c d â–¸ hcd')
    (eventually_of_forall $ Î» _, norm_nonneg _) hfi.norm
end

/-- If `âˆ¥f xâˆ¥ â†’ âˆ` as `x â†’ c` (more formally, along the filter `ğ“[{c}á¶œ] c`) and `[a, b] âˆ‹ c` is a
nontrivial closed (unordered) interval, then the derivative `f'` of `f` is not interval integrable
on `a..b`. -/
lemma not_integrable_has_deriv_at_of_tendsto_norm_at_top_punctured {f f' : â„ â†’ E} {a b c : â„}
  (hne : a â‰  b) (hc : c âˆˆ [a, b]) (h_deriv : âˆ€á¶  x in ğ“[{c}á¶œ] c, has_deriv_at f (f' x) x)
  (h_infty : tendsto (Î» x, âˆ¥f xâˆ¥) (ğ“[{c}á¶œ] c) at_top) :
  Â¬interval_integrable f' volume a b :=
begin
  wlog hlt : a < b := hne.lt_or_lt using [a b, b a] tactic.skip,
  { rw interval_of_le hlt.le at hc,
    rcases hc.1.eq_or_lt with (rfl|hac),
    { have : ğ“[Ioi a] a â‰¤ ğ“[{a}á¶œ] a, from nhds_within_mono _ (Î» _, ne_of_gt),
      exact not_integrable_has_deriv_at_of_tendsto_norm_at_top_filter _
        (Icc_mem_nhds_within_Ioi âŸ¨min_le_left _ _, lt_max_iff.2 (or.inr hlt)âŸ©)
        (h_deriv.filter_mono this) (h_infty.mono_left this) },
    { have : ğ“[Iio c] c â‰¤ ğ“[{c}á¶œ] c, from nhds_within_mono _ (Î» _, ne_of_lt),
      exact Î» h, not_integrable_has_deriv_at_of_tendsto_norm_at_top_filter _
        (Icc_mem_nhds_within_Iio âŸ¨min_lt_iff.2 (or.inl hac), le_max_right _ _âŸ©)
        (h_deriv.filter_mono this) (h_infty.mono_left this)
        (h.mono_set $ interval_subset_interval_left (Icc_subset_interval hc)) } },
  { exact Î» hab hc h, this hab.symm (interval_swap a b â–¸ hc) h.symm }
end

/-- The function `Î» x, xâ»Â¹` is integrable on `a..b` if and only if `a = b` or `0 âˆ‰ [a, b]`. -/
@[simp] lemma interval_integrable_inv_iff {a b : â„} :
  interval_integrable (Î» x, xâ»Â¹) volume a b â†” a = b âˆ¨ (0 : â„) âˆ‰ [a, b] :=
begin
  split,
  { refine Î» h, or_iff_not_imp_left.2 (Î» hne hâ‚€, _),
    have : âˆ€á¶  x in ğ“[{0}á¶œ] (0 : â„), has_deriv_at real.log xâ»Â¹ x,
      from eventually_nhds_with_of_forall (Î» x, real.has_deriv_at_log),
    refine not_integrable_has_deriv_at_of_tendsto_norm_at_top_punctured hne hâ‚€ this _ h,
    exact tendsto_abs_at_bot_at_top.comp real.tendsto_log_nhds_within_zero },
  { rintro (rfl|hâ‚€),
    exacts [interval_integrable.refl,
      interval_integrable_inv (Î» x hx, ne_of_mem_of_not_mem hx hâ‚€) continuous_on_id] }
end

end interval_integral
