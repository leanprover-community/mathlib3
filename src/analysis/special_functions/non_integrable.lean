/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.special_functions.integrals
import analysis.calculus.fderiv_measurable

/-!
# Non integrable functions

In this file we prove that the derivative of a function that tends to infinity is not interval
integrable, see `interval_integral.not_integrable_has_deriv_at_of_tendsto_norm_at_top_filter` and
`interval_integral.not_integrable_has_deriv_at_of_tendsto_norm_at_top_punctured`.  Then we apply the
latter lemma to prove that the function `Î» x, xâ»Â¹` is integrable on `a..b` if and only if `a = b` or
`0 âˆ‰ [a, b]`.

## Main results

* `not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_punctured`: if `f` tends to infinity
  along `ğ“[â‰ ] c` and `f' = O(g)` along the same filter, then `g` is not interval integrable on any
  nontrivial integral `a..b`, `c âˆˆ [a, b]`.

* `not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_filter`: a version of
  `not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_punctured` that works for one-sided
  neighborhoods;

* `not_interval_integrable_of_sub_inv_is_O_punctured`: if `1 / (x - c) = O(f)` as `x â†’ c`, `x â‰  c`,
  then `f` is not interval integrable on any nontrivial interval `a..b`, `c âˆˆ [a, b]`;

* `interval_integrable_sub_inv_iff`, `interval_integrable_inv_iff`: integrability conditions for
  `(x - c)â»Â¹` and `xâ»Â¹`.

## Tags

integrable function
-/

open_locale measure_theory topological_space interval nnreal ennreal
open measure_theory topological_space set filter asymptotics interval_integral

variables {E F : Type*} [normed_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [second_countable_topology E] [complete_space E] [normed_group F]
  [measurable_space F] [borel_space F]

/-- If `f` is eventually differentiable along a nontrivial filter `l : filter â„` that is generated
by convex sets, the norm of `f` tends to infinity along `l`, and `f' = O(g)` along `l`, where `f'`
is the derivative of `f`, then `g` is not integrable on any interval `a..b` such that
`[a, b] âˆˆ l`. -/
lemma not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_filter {f : â„ â†’ E} {g : â„ â†’ F}
  {a b : â„} (l : filter â„) [ne_bot l] [tendsto_Ixx_class Icc l l] (hl : [a, b] âˆˆ l)
  (hd : âˆ€á¶  x in l, differentiable_at â„ f x) (hf : tendsto (Î» x, âˆ¥f xâˆ¥) l at_top)
  (hfg : is_O (deriv f) g l) :
  Â¬interval_integrable g volume a b :=
begin
  intro hgi,
  obtain âŸ¨C, hCâ‚€, s, hsl, hsub, hfd, hgâŸ© : âˆƒ (C : â„) (hCâ‚€ : 0 â‰¤ C) (s âˆˆ l),
    (âˆ€ (x âˆˆ s) (y âˆˆ s), [x, y] âŠ† [a, b]) âˆ§
    (âˆ€ (x âˆˆ s) (y âˆˆ s) (z âˆˆ [x, y]), differentiable_at â„ f z) âˆ§
    (âˆ€ (x âˆˆ s) (y âˆˆ s) (z âˆˆ [x, y]), âˆ¥deriv f zâˆ¥ â‰¤ C * âˆ¥g zâˆ¥),
  { rcases hfg.exists_nonneg with âŸ¨C, Câ‚€, hCâŸ©,
    have h : âˆ€á¶  x : â„ Ã— â„ in l.prod l, âˆ€ y âˆˆ [x.1, x.2], (differentiable_at â„ f y âˆ§
      âˆ¥deriv f yâˆ¥ â‰¤ C * âˆ¥g yâˆ¥) âˆ§ y âˆˆ [a, b],
      from (tendsto_fst.interval tendsto_snd).eventually ((hd.and hC.bound).and hl).lift'_powerset,
    rcases mem_prod_self_iff.1 h with âŸ¨s, hsl, hsâŸ©,
    simp only [prod_subset_iff, mem_set_of_eq] at hs,
    exact âŸ¨C, Câ‚€, s, hsl, Î» x hx y hy z hz, (hs x hx y hy z hz).2,
      Î» x hx y hy z hz, (hs x hx y hy z hz).1.1, Î» x hx y hy z hz, (hs x hx y hy z hz).1.2âŸ© },
  replace hgi : interval_integrable (Î» x, C * âˆ¥g xâˆ¥) volume a b, by convert hgi.norm.smul C,
  obtain âŸ¨c, hc, d, hd, hltâŸ© : âˆƒ (c âˆˆ s) (d âˆˆ s), âˆ¥f câˆ¥ + âˆ« y in Î™ a b, C * âˆ¥g yâˆ¥ < âˆ¥f dâˆ¥,
  { rcases filter.nonempty_of_mem hsl with âŸ¨c, hcâŸ©,
    have : âˆ€á¶  x in l, âˆ¥f câˆ¥ + âˆ« y in Î™ a b, C * âˆ¥g yâˆ¥ < âˆ¥f xâˆ¥,
      from hf.eventually (eventually_gt_at_top _),
    exact âŸ¨c, hc, (this.and hsl).exists.imp (Î» d hd, âŸ¨hd.2, hd.1âŸ©)âŸ© },
  specialize hsub c hc d hd, specialize hfd c hc d hd,
  replace hg : âˆ€ x âˆˆ Î™ c d, âˆ¥deriv f xâˆ¥ â‰¤ C * âˆ¥g xâˆ¥, from Î» z hz, hg c hc d hd z âŸ¨hz.1.le, hz.2âŸ©,
  have hg_ae : âˆ€áµ x âˆ‚(volume.restrict (Î™ c d)), âˆ¥deriv f xâˆ¥ â‰¤ C * âˆ¥g xâˆ¥,
    from (ae_restrict_mem measurable_set_interval_oc).mono hg,
  have hsub' : Î™ c d âŠ† Î™ a b,
    from interval_oc_subset_interval_oc_of_interval_subset_interval hsub,
  have hfi : interval_integrable (deriv f) volume c d,
    from (hgi.mono_set hsub).mono_fun' (ae_measurable_deriv _ _) hg_ae,
  refine hlt.not_le (sub_le_iff_le_add'.1 _),
  calc âˆ¥f dâˆ¥ - âˆ¥f câˆ¥ â‰¤ âˆ¥f d - f câˆ¥ : norm_sub_norm_le _ _
  ... = âˆ¥âˆ« x in c..d, deriv f xâˆ¥ : congr_arg _ (integral_deriv_eq_sub hfd hfi).symm
  ... = âˆ¥âˆ« x in Î™ c d, deriv f xâˆ¥ : norm_integral_eq_norm_integral_Ioc _
  ... â‰¤ âˆ« x in Î™ c d, âˆ¥deriv f xâˆ¥ : norm_integral_le_integral_norm _
  ... â‰¤ âˆ« x in Î™ c d, C * âˆ¥g xâˆ¥ :
    set_integral_mono_on hfi.norm.def (hgi.def.mono_set hsub') measurable_set_interval_oc hg
  ... â‰¤ âˆ« x in Î™ a b, C * âˆ¥g xâˆ¥ :
    set_integral_mono_set hgi.def (ae_of_all _ $ Î» x, mul_nonneg hCâ‚€ (norm_nonneg _))
      hsub'.eventually_le
end

/-- If `a â‰  b`, `c âˆˆ [a, b]`, `f` is differentiable in the neighborhood of `c` within
`[a, b] \ {c}`, `âˆ¥f xâˆ¥ â†’ âˆ` as `x â†’ c` within `[a, b] \ {c}`, and `f' = O(g)` along
`ğ“[[a, b] \ {c}] c`, where `f'` is the derivative of `f`, then `g` is not interval integrable on
`a..b`. -/
lemma not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_within_diff_singleton
  {f : â„ â†’ E} {g : â„ â†’ F} {a b c : â„} (hne : a â‰  b) (hc : c âˆˆ [a, b])
  (h_deriv : âˆ€á¶  x in ğ“[[a, b] \ {c}] c, differentiable_at â„ f x)
  (h_infty : tendsto (Î» x, âˆ¥f xâˆ¥) (ğ“[[a, b] \ {c}] c) at_top)
  (hg : is_O (deriv f) g (ğ“[[a, b] \ {c}] c)) :
  Â¬interval_integrable g volume a b :=
begin
  obtain âŸ¨l, hl, hl', hle, hmemâŸ© : âˆƒ l : filter â„, tendsto_Ixx_class Icc l l âˆ§ l.ne_bot âˆ§
    l â‰¤ ğ“ c âˆ§ [a, b] \ {c} âˆˆ l,
  { cases (min_lt_max.2 hne).lt_or_lt c with hlt hlt,
    { refine âŸ¨ğ“[<] c, infer_instance, infer_instance, inf_le_left, _âŸ©,
      rw â† Iic_diff_right,
      exact diff_mem_nhds_within_diff (Icc_mem_nhds_within_Iic âŸ¨hlt, hc.2âŸ©) _ },
    { refine âŸ¨ğ“[>] c, infer_instance, infer_instance, inf_le_left, _âŸ©,
      rw â† Ici_diff_left,
      exact diff_mem_nhds_within_diff (Icc_mem_nhds_within_Ici âŸ¨hc.1, hltâŸ©) _ } },
  resetI,
  have : l â‰¤ ğ“[[a, b] \ {c}] c, from le_inf hle (le_principal_iff.2 hmem),
  exact not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_filter l
    (mem_of_superset hmem (diff_subset _ _))
    (h_deriv.filter_mono this) (h_infty.mono_left this) (hg.mono this),
end

/-- If `f` is differentiable in a punctured neighborhood of `c`, `âˆ¥f xâˆ¥ â†’ âˆ` as `x â†’ c` (more
formally, along the filter `ğ“[â‰ ] c`), and `f' = O(g)` along `ğ“[â‰ ] c`, where `f'` is the derivative
of `f`, then `g` is not interval integrable on any nontrivial interval `a..b` such that
`c âˆˆ [a, b]`. -/
lemma not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_punctured {f : â„ â†’ E} {g : â„ â†’ F}
  {a b c : â„} (h_deriv : âˆ€á¶  x in ğ“[â‰ ] c, differentiable_at â„ f x)
  (h_infty : tendsto (Î» x, âˆ¥f xâˆ¥) (ğ“[â‰ ] c) at_top) (hg : is_O (deriv f) g (ğ“[â‰ ] c))
  (hne : a â‰  b) (hc : c âˆˆ [a, b]) :
  Â¬interval_integrable g volume a b :=
have ğ“[[a, b] \ {c}] c â‰¤ ğ“[â‰ ] c, from nhds_within_mono _ (inter_subset_right _ _),
not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_within_diff_singleton hne hc
  (h_deriv.filter_mono this) (h_infty.mono_left this) (hg.mono this)

/-- If `f` grows in the punctured neighborhood of `c : â„` at least as fast as `1 / (x - c)`,
then it is not interval integrable on any nontrivial interval `a..b`, `c âˆˆ [a, b]`. -/
lemma not_interval_integrable_of_sub_inv_is_O_punctured {f : â„ â†’ F} {a b c : â„}
  (hf : is_O (Î» x, (x - c)â»Â¹) f (ğ“[â‰ ] c)) (hne : a â‰  b) (hc : c âˆˆ [a, b]) :
  Â¬interval_integrable f volume a b :=
begin
  have A : âˆ€á¶  x in ğ“[â‰ ] c, has_deriv_at (Î» x, real.log (x - c)) (x - c)â»Â¹ x,
  { filter_upwards [self_mem_nhds_within],
    intros x hx,
    simpa using ((has_deriv_at_id x).sub_const c).log (sub_ne_zero.2 hx) },
  have B : tendsto (Î» x, âˆ¥real.log (x - c)âˆ¥) (ğ“[â‰ ] c) at_top,
  { refine tendsto_abs_at_bot_at_top.comp (real.tendsto_log_nhds_within_zero.comp _),
    rw â† sub_self c,
    exact ((has_deriv_at_id c).sub_const c).tendsto_punctured_nhds one_ne_zero },
  exact not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_punctured
    (A.mono (Î» x hx, hx.differentiable_at)) B
    (hf.congr' (A.mono $ Î» x hx, hx.deriv.symm) eventually_eq.rfl) hne hc
end

/-- The function `Î» x, (x - c)â»Â¹` is integrable on `a..b` if and only if `a = b` or `c âˆ‰ [a, b]`. -/
@[simp] lemma interval_integrable_sub_inv_iff {a b c : â„} :
  interval_integrable (Î» x, (x - c)â»Â¹) volume a b â†” a = b âˆ¨ c âˆ‰ [a, b] :=
begin
  split,
  { refine Î» h, or_iff_not_imp_left.2 (Î» hne hc, _),
    exact not_interval_integrable_of_sub_inv_is_O_punctured (is_O_refl _ _) hne hc h },
  { rintro (rfl|hâ‚€),
    exacts [interval_integrable.refl,
      interval_integrable_inv (Î» x hx, sub_ne_zero.2 $ ne_of_mem_of_not_mem hx hâ‚€)
        (continuous_on_id.sub continuous_on_const)] }
end

/-- The function `Î» x, xâ»Â¹` is integrable on `a..b` if and only if `a = b` or `0 âˆ‰ [a, b]`. -/
@[simp] lemma interval_integrable_inv_iff {a b : â„} :
  interval_integrable (Î» x, xâ»Â¹) volume a b â†” a = b âˆ¨ (0 : â„) âˆ‰ [a, b] :=
by simp only [â† interval_integrable_sub_inv_iff, sub_zero]
