/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, S√©bastien Gou√´zel
-/
import analysis.special_functions.trigonometric
import analysis.calculus.extend_deriv

/-!
# Power function on `‚ÑÇ`, `‚Ñù` and `‚Ñù‚Å∫`

We construct the power functions `x ^ y` where `x` and `y` are complex numbers, or `x` and `y` are
real numbers, or `x` is a nonnegative real and `y` is real, and prove their basic properties.
-/

noncomputable theory

open_locale classical real topological_space nnreal

namespace complex

/-- The complex power function `x^y`, given by `x^y = exp(y log x)` (where `log` is the principal
determination of the logarithm), unless `x = 0` where one sets `0^0 = 1` and `0^y = 0` for
`y ‚â† 0`. -/
noncomputable def cpow (x y : ‚ÑÇ) : ‚ÑÇ :=
if x = 0
  then if y = 0
    then 1
    else 0
  else exp (log x * y)

noncomputable instance : has_pow ‚ÑÇ ‚ÑÇ := ‚ü®cpow‚ü©

@[simp] lemma cpow_eq_pow (x y : ‚ÑÇ) : cpow x y = x ^ y := rfl

lemma cpow_def (x y : ‚ÑÇ) : x ^ y =
  if x = 0
    then if y = 0
      then 1
      else 0
    else exp (log x * y) := rfl

@[simp] lemma cpow_zero (x : ‚ÑÇ) : x ^ (0 : ‚ÑÇ) = 1 := by simp [cpow_def]

@[simp] lemma cpow_eq_zero_iff (x y : ‚ÑÇ) : x ^ y = 0 ‚Üî x = 0 ‚àß y ‚â† 0 :=
by { simp only [cpow_def], split_ifs; simp [*, exp_ne_zero] }

@[simp] lemma zero_cpow {x : ‚ÑÇ} (h : x ‚â† 0) : (0 : ‚ÑÇ) ^ x = 0 :=
by simp [cpow_def, *]

@[simp] lemma cpow_one (x : ‚ÑÇ) : x ^ (1 : ‚ÑÇ) = x :=
if hx : x = 0 then by simp [hx, cpow_def]
else by rw [cpow_def, if_neg (one_ne_zero : (1 : ‚ÑÇ) ‚â† 0), if_neg hx, mul_one, exp_log hx]

@[simp] lemma one_cpow (x : ‚ÑÇ) : (1 : ‚ÑÇ) ^ x = 1 :=
by rw cpow_def; split_ifs; simp [one_ne_zero, *] at *

lemma cpow_add {x : ‚ÑÇ} (y z : ‚ÑÇ) (hx : x ‚â† 0) : x ^ (y + z) = x ^ y * x ^ z :=
by simp [cpow_def]; split_ifs; simp [*, exp_add, mul_add] at *

lemma cpow_mul {x y : ‚ÑÇ} (z : ‚ÑÇ) (h‚ÇÅ : -œÄ < (log x * y).im) (h‚ÇÇ : (log x * y).im ‚â§ œÄ) :
  x ^ (y * z) = (x ^ y) ^ z :=
begin
  simp [cpow_def],
  split_ifs;
  simp [*, exp_ne_zero, log_exp h‚ÇÅ h‚ÇÇ, mul_assoc] at *
end

lemma cpow_neg (x y : ‚ÑÇ) : x ^ -y = (x ^ y)‚Åª¬π :=
by simp [cpow_def]; split_ifs; simp [exp_neg]

lemma cpow_neg_one (x : ‚ÑÇ) : x ^ (-1 : ‚ÑÇ) = x‚Åª¬π :=
by simpa using cpow_neg x 1

@[simp] lemma cpow_nat_cast (x : ‚ÑÇ) : ‚àÄ (n : ‚Ñï), x ^ (n : ‚ÑÇ) = x ^ n
| 0       := by simp
| (n + 1) := if hx : x = 0 then by simp only [hx, pow_succ,
    complex.zero_cpow (nat.cast_ne_zero.2 (nat.succ_ne_zero _)), zero_mul]
  else by simp [cpow_add, hx, pow_add, cpow_nat_cast n]

@[simp] lemma cpow_int_cast (x : ‚ÑÇ) : ‚àÄ (n : ‚Ñ§), x ^ (n : ‚ÑÇ) = x ^ n
| (n : ‚Ñï) := by simp; refl
| -[1+ n] := by rw fpow_neg_succ_of_nat;
  simp only [int.neg_succ_of_nat_coe, int.cast_neg, complex.cpow_neg, inv_eq_one_div,
    int.cast_coe_nat, cpow_nat_cast]

lemma cpow_nat_inv_pow (x : ‚ÑÇ) {n : ‚Ñï} (hn : 0 < n) : (x ^ (n‚Åª¬π : ‚ÑÇ)) ^ n = x :=
have (log x * (‚Üën)‚Åª¬π).im = (log x).im / n,
  by rw [div_eq_mul_inv, ‚Üê of_real_nat_cast, ‚Üê of_real_inv, mul_im,
                of_real_re, of_real_im]; simp,
have h : -œÄ < (log x * (‚Üën)‚Åª¬π).im ‚àß (log x * (‚Üën)‚Åª¬π).im ‚â§ œÄ,
  from (le_total (log x).im 0).elim
    (Œª h, ‚ü®calc -œÄ < (log x).im : by simp [log, neg_pi_lt_arg]
            ... ‚â§ ((log x).im * 1) / n : le_div_of_mul_le (nat.cast_pos.2 hn)
              (mul_le_mul_of_nonpos_left (by rw ‚Üê nat.cast_one; exact nat.cast_le.2 hn) h)
            ... = (log x * (‚Üën)‚Åª¬π).im : by simp [this],
          this.symm ‚ñ∏ le_trans (div_nonpos_of_nonpos_of_pos h (nat.cast_pos.2 hn))
            (le_of_lt real.pi_pos)‚ü©)
    (Œª h, ‚ü®this.symm ‚ñ∏ lt_of_lt_of_le (neg_neg_of_pos real.pi_pos)
            (div_nonneg h (nat.cast_pos.2 hn)),
          calc (log x * (‚Üën)‚Åª¬π).im = (1 * (log x).im) / n : by simp [this]
            ... ‚â§ (log x).im : (div_le_of_le_mul (nat.cast_pos.2 hn)
              (mul_le_mul_of_nonneg_right (by rw ‚Üê nat.cast_one; exact nat.cast_le.2 hn) h))
            ... ‚â§ _ : by simp [log, arg_le_pi]‚ü©),
by rw [‚Üê cpow_nat_cast, ‚Üê cpow_mul _ h.1 h.2,
    inv_mul_cancel (show (n : ‚ÑÇ) ‚â† 0, from nat.cast_ne_zero.2 (nat.pos_iff_ne_zero.1 hn)),
    cpow_one]

end complex

namespace real

/-- The real power function `x^y`, defined as the real part of the complex power function.
For `x > 0`, it is equal to `exp(y log x)`. For `x = 0`, one sets `0^0=1` and `0^y=0` for `y ‚â† 0`.
For `x < 0`, the definition is somewhat arbitary as it depends on the choice of a complex
determination of the logarithm. With our conventions, it is equal to `exp (y log x) cos (œÄy)`. -/
noncomputable def rpow (x y : ‚Ñù) := ((x : ‚ÑÇ) ^ (y : ‚ÑÇ)).re

noncomputable instance : has_pow ‚Ñù ‚Ñù := ‚ü®rpow‚ü©

@[simp] lemma rpow_eq_pow (x y : ‚Ñù) : rpow x y = x ^ y := rfl

lemma rpow_def (x y : ‚Ñù) : x ^ y = ((x : ‚ÑÇ) ^ (y : ‚ÑÇ)).re := rfl

lemma rpow_def_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : x ^ y =
  if x = 0
    then if y = 0
      then 1
      else 0
    else exp (log x * y) :=
by simp only [rpow_def, complex.cpow_def];
  split_ifs;
  simp [*, (complex.of_real_log hx).symm, -complex.of_real_mul,
    (complex.of_real_mul _ _).symm, complex.exp_of_real_re] at *

lemma rpow_def_of_pos {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : x ^ y = exp (log x * y) :=
by rw [rpow_def_of_nonneg (le_of_lt hx), if_neg (ne_of_gt hx)]

lemma rpow_eq_zero_iff_of_nonneg {x y : ‚Ñù} (hx : 0 ‚â§ x) : x ^ y = 0 ‚Üî x = 0 ‚àß y ‚â† 0 :=
by { simp only [rpow_def_of_nonneg hx], split_ifs; simp [*, exp_ne_zero] }

open_locale real

lemma rpow_def_of_neg {x : ‚Ñù} (hx : x < 0) (y : ‚Ñù) : x ^ y = exp (log x * y) * cos (y * œÄ) :=
begin
  rw [rpow_def, complex.cpow_def, if_neg],
  have : complex.log x * y = ‚Üë(log(-x) * y) + ‚Üë(y * œÄ) * complex.I,
    simp only [complex.log, abs_of_neg hx, complex.arg_of_real_of_neg hx,
      complex.abs_of_real, complex.of_real_mul], ring,
  { rw [this, complex.exp_add_mul_I, ‚Üê complex.of_real_exp, ‚Üê complex.of_real_cos,
      ‚Üê complex.of_real_sin, mul_add, ‚Üê complex.of_real_mul, ‚Üê mul_assoc, ‚Üê complex.of_real_mul,
      complex.add_re, complex.of_real_re, complex.mul_re, complex.I_re, complex.of_real_im,
      real.log_neg_eq_log],
    ring },
  { rw complex.of_real_eq_zero, exact ne_of_lt hx }
end

lemma rpow_def_of_nonpos {x : ‚Ñù} (hx : x ‚â§ 0) (y : ‚Ñù) : x ^ y =
  if x = 0
    then if y = 0
      then 1
      else 0
    else exp (log x * y) * cos (y * œÄ) :=
by split_ifs; simp [rpow_def, *]; exact rpow_def_of_neg (lt_of_le_of_ne hx h) _

lemma rpow_pos_of_pos {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : 0 < x ^ y :=
by rw rpow_def_of_pos hx; apply exp_pos

lemma abs_rpow_le_abs_rpow (x y : ‚Ñù) : abs (x ^ y) ‚â§ abs (x) ^ y :=
abs_le_of_le_of_neg_le
begin
  cases lt_trichotomy 0 x, { rw abs_of_pos h },
  cases h, { simp [h.symm] },
  rw [rpow_def_of_neg h, rpow_def_of_pos (abs_pos_of_neg h), log_abs],
  calc exp (log x * y) * cos (y * œÄ) ‚â§ exp (log x * y) * 1 :
    mul_le_mul_of_nonneg_left (cos_le_one _) (le_of_lt $ exp_pos _)
  ... = _ : mul_one _
end
begin
  cases lt_trichotomy 0 x, { rw abs_of_pos h, have : 0 < x^y := rpow_pos_of_pos h _, linarith },
  cases h, { simp only [h.symm, abs_zero, rpow_def_of_nonneg], split_ifs, repeat {norm_num} },
  rw [rpow_def_of_neg h, rpow_def_of_pos (abs_pos_of_neg h), log_abs],
  calc -(exp (log x * y) * cos (y * œÄ)) = exp (log x * y) * (-cos (y * œÄ)) : by ring
    ... ‚â§ exp (log x * y) * 1 :
      mul_le_mul_of_nonneg_left (neg_le.2 $ neg_one_le_cos _) (le_of_lt $ exp_pos _)
    ... = exp (log x * y) : mul_one _
end

end real

namespace complex

lemma of_real_cpow {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : ((x ^ y : ‚Ñù) : ‚ÑÇ) = (x : ‚ÑÇ) ^ (y : ‚ÑÇ) :=
by simp [real.rpow_def_of_nonneg hx, complex.cpow_def]; split_ifs; simp [complex.of_real_log hx]

@[simp] lemma abs_cpow_real (x : ‚ÑÇ) (y : ‚Ñù) : abs (x ^ (y : ‚ÑÇ)) = x.abs ^ y :=
begin
  rw [real.rpow_def_of_nonneg (abs_nonneg _), complex.cpow_def],
  split_ifs;
  simp [*, abs_of_nonneg (le_of_lt (real.exp_pos _)), complex.log, complex.exp_add,
    add_mul, mul_right_comm _ I, exp_mul_I, abs_cos_add_sin_mul_I,
    (complex.of_real_mul _ _).symm, -complex.of_real_mul] at *
end

@[simp] lemma abs_cpow_inv_nat (x : ‚ÑÇ) (n : ‚Ñï) : abs (x ^ (n‚Åª¬π : ‚ÑÇ)) = x.abs ^ (n‚Åª¬π : ‚Ñù) :=
by rw ‚Üê abs_cpow_real; simp [-abs_cpow_real]

end complex

namespace real

variables {x y z : ‚Ñù}

@[simp] lemma rpow_zero (x : ‚Ñù) : x ^ (0 : ‚Ñù) = 1 := by simp [rpow_def]

@[simp] lemma zero_rpow {x : ‚Ñù} (h : x ‚â† 0) : (0 : ‚Ñù) ^ x = 0 :=
by simp [rpow_def, *]

@[simp] lemma rpow_one (x : ‚Ñù) : x ^ (1 : ‚Ñù) = x := by simp [rpow_def]

@[simp] lemma one_rpow (x : ‚Ñù) : (1 : ‚Ñù) ^ x = 1 := by simp [rpow_def]

lemma zero_rpow_le_one (x : ‚Ñù) : (0 : ‚Ñù) ^ x ‚â§ 1 :=
by { by_cases h : x = 0; simp [h, zero_le_one] }

lemma zero_rpow_nonneg (x : ‚Ñù) : 0 ‚â§ (0 : ‚Ñù) ^ x :=
by { by_cases h : x = 0; simp [h, zero_le_one] }

lemma rpow_nonneg_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : 0 ‚â§ x ^ y :=
by rw [rpow_def_of_nonneg hx];
  split_ifs; simp only [zero_le_one, le_refl, le_of_lt (exp_pos _)]

lemma rpow_add {x : ‚Ñù} (hx : 0 < x) (y z : ‚Ñù) : x ^ (y + z) = x ^ y * x ^ z :=
by simp only [rpow_def_of_pos hx, mul_add, exp_add]

lemma rpow_add' {x : ‚Ñù} (hx : 0 ‚â§ x) {y z : ‚Ñù} (h : y + z ‚â† 0) : x ^ (y + z) = x ^ y * x ^ z :=
begin
  rcases le_iff_eq_or_lt.1 hx with H|pos,
  { simp only [‚Üê H, h, rpow_eq_zero_iff_of_nonneg, true_and, zero_rpow, eq_self_iff_true, ne.def,
               not_false_iff, zero_eq_mul],
    by_contradiction F,
    push_neg at F,
    apply h,
    simp [F] },
  { exact rpow_add pos _ _ }
end

/-- For `0 ‚â§ x`, the only problematic case in the equality `x ^ y * x ^ z = x ^ (y + z)` is for
`x = 0` and `y + z = 0`, where the right hand side is `1` while the left hand side can vanish.
The inequality is always true, though, and given in this lemma. -/
lemma le_rpow_add {x : ‚Ñù} (hx : 0 ‚â§ x) (y z : ‚Ñù) : x ^ y * x ^ z ‚â§ x ^ (y + z) :=
begin
  rcases le_iff_eq_or_lt.1 hx with H|pos,
  { by_cases h : y + z = 0,
    { simp only [H.symm, h, rpow_zero],
      calc (0 : ‚Ñù) ^ y * 0 ^ z ‚â§ 1 * 1 :
        mul_le_mul (zero_rpow_le_one y) (zero_rpow_le_one z) (zero_rpow_nonneg z) zero_le_one
      ... = 1 : by simp },
    { simp [rpow_add', ‚Üê H, h] } },
  { simp [rpow_add pos] }
end

lemma rpow_mul {x : ‚Ñù} (hx : 0 ‚â§ x) (y z : ‚Ñù) : x ^ (y * z) = (x ^ y) ^ z :=
by rw [‚Üê complex.of_real_inj, complex.of_real_cpow (rpow_nonneg_of_nonneg hx _),
    complex.of_real_cpow hx, complex.of_real_mul, complex.cpow_mul, complex.of_real_cpow hx];
  simp only [(complex.of_real_mul _ _).symm, (complex.of_real_log hx).symm,
    complex.of_real_im, neg_lt_zero, pi_pos, le_of_lt pi_pos]

lemma rpow_neg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : x ^ -y = (x ^ y)‚Åª¬π :=
by simp only [rpow_def_of_nonneg hx]; split_ifs; simp [*, exp_neg] at *

lemma rpow_sub {x : ‚Ñù} (hx : 0 < x) (y z : ‚Ñù) : x ^ (y - z) = x ^ y / x ^ z :=
by simp only [sub_eq_add_neg, rpow_add hx, rpow_neg (le_of_lt hx), div_eq_mul_inv]

lemma rpow_sub' {x : ‚Ñù} (hx : 0 ‚â§ x) {y z : ‚Ñù} (h : y - z ‚â† 0) :
  x ^ (y - z) = x ^ y / x ^ z :=
by simp only [sub_eq_add_neg, rpow_add' hx h, rpow_neg hx, div_eq_mul_inv]

@[simp] lemma rpow_nat_cast (x : ‚Ñù) (n : ‚Ñï) : x ^ (n : ‚Ñù) = x ^ n :=
by simp only [rpow_def, (complex.of_real_pow _ _).symm, complex.cpow_nat_cast,
  complex.of_real_nat_cast, complex.of_real_re]

@[simp] lemma rpow_int_cast (x : ‚Ñù) (n : ‚Ñ§) : x ^ (n : ‚Ñù) = x ^ n :=
by simp only [rpow_def, (complex.of_real_fpow _ _).symm, complex.cpow_int_cast,
  complex.of_real_int_cast, complex.of_real_re]

lemma rpow_neg_one (x : ‚Ñù) : x ^ (-1 : ‚Ñù) = x‚Åª¬π :=
begin
  suffices H : x ^ ((-1 : ‚Ñ§) : ‚Ñù) = x‚Åª¬π, by exact_mod_cast H,
  simp only [rpow_int_cast, fpow_one, fpow_neg],
end

lemma mul_rpow {x y z : ‚Ñù} (h : 0 ‚â§ x) (h‚ÇÅ : 0 ‚â§ y) : (x*y)^z = x^z * y^z :=
begin
  iterate 3 { rw real.rpow_def_of_nonneg }, split_ifs; simp * at *,
  { have hx : 0 < x, cases lt_or_eq_of_le h with h‚ÇÇ h‚ÇÇ, exact h‚ÇÇ, exfalso, apply h_2, exact eq.symm h‚ÇÇ,
    have hy : 0 < y, cases lt_or_eq_of_le h‚ÇÅ with h‚ÇÇ h‚ÇÇ, exact h‚ÇÇ, exfalso, apply h_3, exact eq.symm h‚ÇÇ,
    rw [log_mul (ne_of_gt hx) (ne_of_gt hy), add_mul, exp_add]},
  { exact h‚ÇÅ},
  { exact h},
  { exact mul_nonneg h h‚ÇÅ},
end

lemma inv_rpow (hx : 0 ‚â§ x) (y : ‚Ñù) : (x‚Åª¬π)^y = (x^y)‚Åª¬π :=
begin
  by_cases hy0 : y = 0, { simp [*] },
  by_cases hx0 : x = 0, { simp [*] },
  simp only [real.rpow_def_of_nonneg hx, real.rpow_def_of_nonneg (inv_nonneg.2 hx), if_false,
    hx0, mt inv_eq_zero.1 hx0, log_inv, ‚Üê neg_mul_eq_neg_mul, exp_neg]
end

lemma div_rpow (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (z : ‚Ñù) : (x / y) ^ z = x^z / y^z :=
by simp only [div_eq_mul_inv, mul_rpow hx (inv_nonneg.2 hy), inv_rpow hy]

lemma one_le_rpow (h : 1 ‚â§ x) (h‚ÇÅ : 0 ‚â§ z) : 1 ‚â§ x^z :=
begin
  rw real.rpow_def_of_nonneg, split_ifs with h‚ÇÇ h‚ÇÉ,
  { refl},
  { simp [*, not_le_of_gt zero_lt_one] at *},
  { have hx : 0 < x, exact lt_of_lt_of_le zero_lt_one h,
    rw [‚Üêlog_le_log zero_lt_one hx, log_one] at h,
    have pos : 0 ‚â§ log x * z, exact mul_nonneg h h‚ÇÅ,
      rwa [‚Üêexp_le_exp, exp_zero] at pos},
  { exact le_trans zero_le_one h},
end

lemma rpow_lt_rpow (hx : 0 ‚â§ x) (hxy : x < y) (hz : 0 < z) : x^z < y^z :=
begin
  rw le_iff_eq_or_lt at hx, cases hx,
  { rw [‚Üê hx, zero_rpow (ne_of_gt hz)], exact rpow_pos_of_pos (by rwa ‚Üê hx at hxy) _ },
  rw [rpow_def_of_pos hx, rpow_def_of_pos (lt_trans hx hxy), exp_lt_exp],
  exact mul_lt_mul_of_pos_right (log_lt_log hx hxy) hz
end

lemma rpow_le_rpow {x y z: ‚Ñù} (h : 0 ‚â§ x) (h‚ÇÅ : x ‚â§ y) (h‚ÇÇ : 0 ‚â§ z) : x^z ‚â§ y^z :=
begin
  rcases eq_or_lt_of_le h‚ÇÅ with rfl|h‚ÇÅ', { refl },
  rcases eq_or_lt_of_le h‚ÇÇ with rfl|h‚ÇÇ', { simp },
  exact le_of_lt (rpow_lt_rpow h h‚ÇÅ' h‚ÇÇ')
end

lemma rpow_lt_rpow_iff (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z < y ^ z ‚Üî x < y :=
‚ü®lt_imp_lt_of_le_imp_le $ Œª h, rpow_le_rpow hy h (le_of_lt hz), Œª h, rpow_lt_rpow hx h hz‚ü©

lemma rpow_le_rpow_iff (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z ‚â§ y ^ z ‚Üî x ‚â§ y :=
le_iff_le_iff_lt_iff_lt.2 $ rpow_lt_rpow_iff hy hx hz

lemma rpow_lt_rpow_of_exponent_lt (hx : 1 < x) (hyz : y < z) : x^y < x^z :=
begin
  repeat {rw [rpow_def_of_pos (lt_trans zero_lt_one hx)]},
  rw exp_lt_exp, exact mul_lt_mul_of_pos_left hyz (log_pos hx),
end

lemma rpow_le_rpow_of_exponent_le (hx : 1 ‚â§ x) (hyz : y ‚â§ z) : x^y ‚â§ x^z :=
begin
  repeat {rw [rpow_def_of_pos (lt_of_lt_of_le zero_lt_one hx)]},
  rw exp_le_exp, exact mul_le_mul_of_nonneg_left hyz (log_nonneg hx),
end

lemma rpow_lt_rpow_of_exponent_gt (hx0 : 0 < x) (hx1 : x < 1) (hyz : z < y) :
  x^y < x^z :=
begin
  repeat {rw [rpow_def_of_pos hx0]},
  rw exp_lt_exp, exact mul_lt_mul_of_neg_left hyz (log_neg hx0 hx1),
end

lemma rpow_le_rpow_of_exponent_ge (hx0 : 0 < x) (hx1 : x ‚â§ 1) (hyz : z ‚â§ y) :
  x^y ‚â§ x^z :=
begin
  repeat {rw [rpow_def_of_pos hx0]},
  rw exp_le_exp, exact mul_le_mul_of_nonpos_left hyz (log_nonpos (le_of_lt hx0) hx1),
end

lemma rpow_le_one {x z : ‚Ñù} (hx : 0 ‚â§ x) (hx2 : x ‚â§ 1) (hz : 0 ‚â§ z) : x^z ‚â§ 1 :=
by rw ‚Üêone_rpow z; apply rpow_le_rpow; assumption

lemma one_lt_rpow (hx : 1 < x) (hz : 0 < z) : 1 < x^z :=
by { rw ‚Üê one_rpow z, exact rpow_lt_rpow zero_le_one hx hz }

lemma rpow_lt_one (hx : 0 < x) (hx1 : x < 1) (hz : 0 < z) : x^z < 1 :=
by { rw ‚Üê one_rpow z, exact rpow_lt_rpow (le_of_lt hx) hx1 hz }

lemma pow_nat_rpow_nat_inv {x : ‚Ñù} (hx : 0 ‚â§ x) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ n) ^ (n‚Åª¬π : ‚Ñù) = x :=
have hn0 : (n : ‚Ñù) ‚â† 0, by simpa [nat.pos_iff_ne_zero] using hn,
by rw [‚Üê rpow_nat_cast, ‚Üê rpow_mul hx, mul_inv_cancel hn0, rpow_one]

lemma rpow_nat_inv_pow_nat {x : ‚Ñù} (hx : 0 ‚â§ x) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ (n‚Åª¬π : ‚Ñù)) ^ n = x :=
have hn0 : (n : ‚Ñù) ‚â† 0, by simpa [nat.pos_iff_ne_zero] using hn,
by rw [‚Üê rpow_nat_cast, ‚Üê rpow_mul hx, inv_mul_cancel hn0, rpow_one]

section prove_rpow_is_continuous

lemma continuous_rpow_aux1 : continuous (Œªp : {p:‚Ñù√ó‚Ñù // 0 < p.1}, p.val.1 ^ p.val.2) :=
suffices h : continuous (Œª p : {p:‚Ñù√ó‚Ñù // 0 < p.1 }, exp (log p.val.1 * p.val.2)),
  by { convert h, ext p, rw rpow_def_of_pos p.2 },
continuous_exp.comp $
  (show continuous ((Œªp:{p:‚Ñù//0 < p}, log (p.val)) ‚àò (Œªp:{p:‚Ñù√ó‚Ñù//0<p.fst}, ‚ü®p.val.1, p.2‚ü©)), from
    continuous_log'.comp $ continuous_subtype_mk _ $ continuous_fst.comp continuous_subtype_val).mul
  (continuous_snd.comp $ continuous_subtype_val.comp continuous_id)

lemma continuous_rpow_aux2 : continuous (Œª p : {p:‚Ñù√ó‚Ñù // p.1 < 0}, p.val.1 ^ p.val.2) :=
suffices h : continuous (Œªp:{p:‚Ñù√ó‚Ñù // p.1 < 0}, exp (log (-p.val.1) * p.val.2) * cos (p.val.2 * œÄ)),
  by { convert h, ext p, rw [rpow_def_of_neg p.2, log_neg_eq_log] },
  (continuous_exp.comp $
    (show continuous $ (Œªp:{p:‚Ñù//0<p},
            log (p.val))‚àò(Œªp:{p:‚Ñù√ó‚Ñù//p.1<0}, ‚ü®-p.val.1, neg_pos_of_neg p.2‚ü©),
     from continuous_log'.comp $ continuous_subtype_mk _ $ continuous_neg.comp $
            continuous_fst.comp continuous_subtype_val).mul
    (continuous_snd.comp $ continuous_subtype_val.comp continuous_id)).mul
  (continuous_cos.comp $
    (continuous_snd.comp $ continuous_subtype_val.comp continuous_id).mul continuous_const)

lemma continuous_at_rpow_of_ne_zero (hx : x ‚â† 0) (y : ‚Ñù) :
  continuous_at (Œªp:‚Ñù√ó‚Ñù, p.1^p.2) (x, y) :=
begin
  cases lt_trichotomy 0 x,
  exact continuous_within_at.continuous_at
    (continuous_on_iff_continuous_restrict.2 continuous_rpow_aux1 _ h)
    (mem_nhds_sets (by { convert is_open_prod (is_open_lt' (0:‚Ñù)) is_open_univ, ext, finish }) h),
  cases h,
  { exact absurd h.symm hx },
  exact continuous_within_at.continuous_at
    (continuous_on_iff_continuous_restrict.2 continuous_rpow_aux2 _ h)
    (mem_nhds_sets (by { convert is_open_prod (is_open_gt' (0:‚Ñù)) is_open_univ, ext, finish }) h)
end

lemma continuous_rpow_aux3 : continuous (Œª p : {p:‚Ñù√ó‚Ñù // 0 < p.2}, p.val.1 ^ p.val.2) :=
continuous_iff_continuous_at.2 $ Œª ‚ü®(x‚ÇÄ, y‚ÇÄ), hy‚ÇÄ‚ü©,
begin
  by_cases hx‚ÇÄ : x‚ÇÄ = 0,
  { simp only [continuous_at, hx‚ÇÄ, zero_rpow (ne_of_gt hy‚ÇÄ), metric.tendsto_nhds_nhds],
    assume Œµ Œµ0,
    rcases exists_pos_rat_lt (half_pos hy‚ÇÄ) with ‚ü®q, q_pos, q_lt‚ü©,
    let q := (q:‚Ñù), replace q_pos : 0 < q := rat.cast_pos.2 q_pos,
    let Œ¥ := min (min q (Œµ ^ (1 / q))) (1/2),
    have Œ¥0 : 0 < Œ¥ := lt_min (lt_min q_pos (rpow_pos_of_pos Œµ0 _)) (by norm_num),
    have : Œ¥ ‚â§ q := le_trans (min_le_left _ _) (min_le_left _ _),
    have : Œ¥ ‚â§ Œµ ^ (1 / q) := le_trans (min_le_left _ _) (min_le_right _ _),
    have : Œ¥ < 1 := lt_of_le_of_lt (min_le_right _ _) (by norm_num),
    use Œ¥, use Œ¥0, rintros ‚ü®‚ü®x, y‚ü©, hy‚ü©,
    simp only [subtype.dist_eq, real.dist_eq, prod.dist_eq, sub_zero, subtype.coe_mk],
    assume h, rw max_lt_iff at h, cases h with xŒ¥ yy‚ÇÄ,
    have qy : q < y, calc q < y‚ÇÄ / 2 : q_lt
      ... = y‚ÇÄ - y‚ÇÄ / 2 : (sub_half _).symm
      ... ‚â§ y‚ÇÄ - Œ¥ : by linarith
      ... < y : sub_lt_of_abs_sub_lt_left yy‚ÇÄ,
    calc abs(x^y) ‚â§ abs(x)^y : abs_rpow_le_abs_rpow _ _
      ... < Œ¥ ^ y : rpow_lt_rpow (abs_nonneg _) xŒ¥ hy
      ... < Œ¥ ^ q : by { refine rpow_lt_rpow_of_exponent_gt _ _ _, repeat {linarith} }
      ... ‚â§ (Œµ ^ (1 / q)) ^ q : by { refine rpow_le_rpow _ _ _, repeat {linarith} }
      ... = Œµ : by { rw [‚Üê rpow_mul, div_mul_cancel, rpow_one], exact ne_of_gt q_pos, linarith }},
  { exact (continuous_within_at_iff_continuous_at_restrict (Œªp:‚Ñù√ó‚Ñù, p.1^p.2) _).1
      (continuous_at_rpow_of_ne_zero hx‚ÇÄ _).continuous_within_at }
end

lemma continuous_at_rpow_of_pos (hy : 0 < y) (x : ‚Ñù) :
  continuous_at (Œªp:‚Ñù√ó‚Ñù, p.1^p.2) (x, y) :=
continuous_within_at.continuous_at
  (continuous_on_iff_continuous_restrict.2 continuous_rpow_aux3 _ hy)
  (mem_nhds_sets (by { convert is_open_prod is_open_univ (is_open_lt' (0:‚Ñù)), ext, finish }) hy)

lemma continuous_at_rpow {x y : ‚Ñù} (h : x ‚â† 0 ‚à® 0 < y) :
  continuous_at (Œªp:‚Ñù√ó‚Ñù, p.1^p.2) (x, y) :=
by { cases h, exact continuous_at_rpow_of_ne_zero h _, exact continuous_at_rpow_of_pos h x }

variables {Œ± : Type*} [topological_space Œ±] {f g : Œ± ‚Üí ‚Ñù}

/--
`real.rpow` is continuous at all points except for the lower half of the y-axis.
In other words, the function `Œªp:‚Ñù√ó‚Ñù, p.1^p.2` is continuous at `(x, y)` if `x ‚â† 0` or `y > 0`.

Multiple forms of the claim is provided in the current section.
-/
lemma continuous_rpow (h : ‚àÄa, f a ‚â† 0 ‚à® 0 < g a) (hf : continuous f) (hg : continuous g):
  continuous (Œªa:Œ±, (f a) ^ (g a)) :=
continuous_iff_continuous_at.2 $ Œª a,
begin
  show continuous_at ((Œªp:‚Ñù√ó‚Ñù, p.1^p.2) ‚àò (Œªa, (f a, g a))) a,
  refine continuous_at.comp _ (continuous_iff_continuous_at.1 (hf.prod_mk hg) _),
  { replace h := h a, cases h,
    { exact continuous_at_rpow_of_ne_zero h _ },
    { exact continuous_at_rpow_of_pos h _ }},
end

lemma continuous_rpow_of_ne_zero (h : ‚àÄa, f a ‚â† 0) (hf : continuous f) (hg : continuous g):
  continuous (Œªa:Œ±, (f a) ^ (g a)) := continuous_rpow (Œªa, or.inl $ h a) hf hg

lemma continuous_rpow_of_pos (h : ‚àÄa, 0 < g a) (hf : continuous f) (hg : continuous g):
  continuous (Œªa:Œ±, (f a) ^ (g a)) := continuous_rpow (Œªa, or.inr $ h a) hf hg

end prove_rpow_is_continuous

section prove_rpow_is_differentiable

lemma has_deriv_at_rpow_of_pos {x : ‚Ñù} (h : 0 < x) (p : ‚Ñù) :
  has_deriv_at (Œª x, x^p) (p * x^(p-1)) x :=
begin
  have : has_deriv_at (Œª x, exp (log x * p)) (p * x^(p-1)) x,
  { convert (has_deriv_at_exp _).comp x ((has_deriv_at_log (ne_of_gt h)).mul_const p) using 1,
    field_simp [rpow_def_of_pos h, mul_sub, exp_sub, exp_log h, ne_of_gt h],
    ring },
  apply this.congr_of_mem_nhds,
  have : set.Ioi (0 : ‚Ñù) ‚àà ùìù x := mem_nhds_sets is_open_Ioi h,
  exact filter.eventually_of_mem this (Œª y hy, rpow_def_of_pos hy _)
end

lemma has_deriv_at_rpow_of_neg {x : ‚Ñù} (h : x < 0) (p : ‚Ñù) :
  has_deriv_at (Œª x, x^p) (p * x^(p-1)) x :=
begin
  have : has_deriv_at (Œª x, exp (log x * p) * cos (p * œÄ)) (p * x^(p-1)) x,
  { convert ((has_deriv_at_exp _).comp x ((has_deriv_at_log (ne_of_lt h)).mul_const p)).mul_const _
      using 1,
    field_simp [rpow_def_of_neg h, mul_sub, exp_sub, sub_mul, cos_sub, exp_log_of_neg h, ne_of_lt h],
    ring },
  apply this.congr_of_mem_nhds,
  have : set.Iio (0 : ‚Ñù) ‚àà ùìù x := mem_nhds_sets is_open_Iio h,
  exact filter.eventually_of_mem this (Œª y hy, rpow_def_of_neg hy _)
end

lemma has_deriv_at_rpow {x : ‚Ñù} (h : x ‚â† 0) (p : ‚Ñù) :
  has_deriv_at (Œª x, x^p) (p * x^(p-1)) x :=
begin
  rcases lt_trichotomy x 0 with H|H|H,
  { exact has_deriv_at_rpow_of_neg H p },
  { exact (h H).elim },
  { exact has_deriv_at_rpow_of_pos H p },
end

lemma has_deriv_at_rpow_zero_of_one_le {p : ‚Ñù} (h : 1 ‚â§ p) :
  has_deriv_at (Œª x, x^p) (p * (0 : ‚Ñù)^(p-1)) 0 :=
begin
  apply has_deriv_at_of_has_deriv_at_of_ne (Œª x hx, has_deriv_at_rpow hx p),
  { exact (continuous_rpow_of_pos (Œª _, (lt_of_lt_of_le zero_lt_one h))
      continuous_id continuous_const).continuous_at },
  { rcases le_iff_eq_or_lt.1 h with rfl|h,
    { simp [continuous_const.continuous_at] },
    { exact (continuous_const.mul (continuous_rpow_of_pos (Œª _, sub_pos_of_lt h)
        continuous_id continuous_const)).continuous_at } }
end

lemma has_deriv_at_rpow_of_one_le (x : ‚Ñù) {p : ‚Ñù} (h : 1 ‚â§ p) :
  has_deriv_at (Œª x, x^p) (p * x^(p-1)) x :=
begin
  by_cases hx : x = 0,
  { rw hx, exact has_deriv_at_rpow_zero_of_one_le h },
  { exact has_deriv_at_rpow hx p }
end

end prove_rpow_is_differentiable

section sqrt

lemma sqrt_eq_rpow : sqrt = Œªx:‚Ñù, x ^ (1/(2:‚Ñù)) :=
begin
  funext, by_cases h : 0 ‚â§ x,
  { rw [‚Üê mul_self_inj_of_nonneg, mul_self_sqrt h, ‚Üê pow_two, ‚Üê rpow_nat_cast, ‚Üê rpow_mul h],
    norm_num, exact sqrt_nonneg _, exact rpow_nonneg_of_nonneg h _ },
  { replace h : x < 0 := lt_of_not_ge h,
    have : 1 / (2:‚Ñù) * œÄ = œÄ / (2:‚Ñù), ring,
    rw [sqrt_eq_zero_of_nonpos (le_of_lt h), rpow_def_of_neg h, this, cos_pi_div_two, mul_zero] }
end

lemma continuous_sqrt : continuous sqrt :=
by rw sqrt_eq_rpow; exact continuous_rpow_of_pos (Œªa, by norm_num) continuous_id continuous_const

end sqrt

end real

section differentiability
open real

variables {f : ‚Ñù ‚Üí ‚Ñù} {x f' : ‚Ñù} {s : set ‚Ñù} (p : ‚Ñù)
/- Differentiability statements for the power of a function, when the function does not vanish
and the exponent is arbitrary-/

lemma has_deriv_within_at.rpow (hf : has_deriv_within_at f f' s x) (hx : f x ‚â† 0) :
  has_deriv_within_at (Œª y, (f y)^p) (f' * p * (f x)^(p-1)) s x :=
begin
  convert (has_deriv_at_rpow hx p).comp_has_deriv_within_at x hf using 1,
  ring
end

lemma has_deriv_at.rpow (hf : has_deriv_at f f' x) (hx : f x ‚â† 0) :
  has_deriv_at (Œª y, (f y)^p) (f' * p * (f x)^(p-1)) x :=
begin
  rw ‚Üê has_deriv_within_at_univ at *,
  exact hf.rpow p hx
end

lemma differentiable_within_at.rpow (hf : differentiable_within_at ‚Ñù f s x) (hx : f x ‚â† 0) :
  differentiable_within_at ‚Ñù (Œªx, (f x)^p) s x :=
(hf.has_deriv_within_at.rpow p hx).differentiable_within_at

@[simp] lemma differentiable_at.rpow (hf : differentiable_at ‚Ñù f x) (hx : f x ‚â† 0) :
  differentiable_at ‚Ñù (Œªx, (f x)^p) x :=
(hf.has_deriv_at.rpow p hx).differentiable_at

lemma differentiable_on.rpow (hf : differentiable_on ‚Ñù f s) (hx : ‚àÄ x ‚àà s, f x ‚â† 0) :
  differentiable_on ‚Ñù (Œªx, (f x)^p) s :=
Œªx h, (hf x h).rpow p (hx x h)

@[simp] lemma differentiable.rpow (hf : differentiable ‚Ñù f) (hx : ‚àÄ x, f x ‚â† 0) :
  differentiable ‚Ñù (Œªx, (f x)^p) :=
Œªx, (hf x).rpow p (hx x)

lemma deriv_within_rpow (hf : differentiable_within_at ‚Ñù f s x) (hx : f x ‚â† 0)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, (f x)^p) s x = (deriv_within f s x) * p * (f x)^(p-1) :=
(hf.has_deriv_within_at.rpow p hx).deriv_within hxs

@[simp] lemma deriv_rpow (hf : differentiable_at ‚Ñù f x) (hx : f x ‚â† 0) :
  deriv (Œªx, (f x)^p) x = (deriv f x) * p * (f x)^(p-1) :=
(hf.has_deriv_at.rpow p hx).deriv

/- Differentiability statements for the power of a function, when the function may vanish
but the exponent is at least one. -/

variable {p}

lemma has_deriv_within_at.rpow_of_one_le (hf : has_deriv_within_at f f' s x) (hp : 1 ‚â§ p) :
  has_deriv_within_at (Œª y, (f y)^p) (f' * p * (f x)^(p-1)) s x :=
begin
  convert (has_deriv_at_rpow_of_one_le (f x) hp).comp_has_deriv_within_at x hf using 1,
  ring
end

lemma has_deriv_at.rpow_of_one_le (hf : has_deriv_at f f' x) (hp : 1 ‚â§ p) :
  has_deriv_at (Œª y, (f y)^p) (f' * p * (f x)^(p-1)) x :=
begin
  rw ‚Üê has_deriv_within_at_univ at *,
  exact hf.rpow_of_one_le hp
end

lemma differentiable_within_at.rpow_of_one_le (hf : differentiable_within_at ‚Ñù f s x) (hp : 1 ‚â§ p) :
  differentiable_within_at ‚Ñù (Œªx, (f x)^p) s x :=
(hf.has_deriv_within_at.rpow_of_one_le hp).differentiable_within_at

@[simp] lemma differentiable_at.rpow_of_one_le (hf : differentiable_at ‚Ñù f x) (hp : 1 ‚â§ p) :
  differentiable_at ‚Ñù (Œªx, (f x)^p) x :=
(hf.has_deriv_at.rpow_of_one_le hp).differentiable_at

lemma differentiable_on.rpow_of_one_le (hf : differentiable_on ‚Ñù f s) (hp : 1 ‚â§ p) :
  differentiable_on ‚Ñù (Œªx, (f x)^p) s :=
Œªx h, (hf x h).rpow_of_one_le hp

@[simp] lemma differentiable.rpow_of_one_le (hf : differentiable ‚Ñù f) (hp : 1 ‚â§ p) :
  differentiable ‚Ñù (Œªx, (f x)^p) :=
Œªx, (hf x).rpow_of_one_le hp

lemma deriv_within_rpow_of_one_le (hf : differentiable_within_at ‚Ñù f s x) (hp : 1 ‚â§ p)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, (f x)^p) s x = (deriv_within f s x) * p * (f x)^(p-1) :=
(hf.has_deriv_within_at.rpow_of_one_le hp).deriv_within hxs

@[simp] lemma deriv_rpow_of_one_le (hf : differentiable_at ‚Ñù f x) (hp : 1 ‚â§ p) :
  deriv (Œªx, (f x)^p) x = (deriv f x) * p * (f x)^(p-1) :=
(hf.has_deriv_at.rpow_of_one_le hp).deriv

/- Differentiability statements for the square root of a function, when the function does not
vanish -/

lemma has_deriv_within_at.sqrt (hf : has_deriv_within_at f f' s x) (hx : f x ‚â† 0) :
  has_deriv_within_at (Œª y, sqrt (f y)) (f' / (2 * sqrt (f x))) s x :=
begin
  simp only [sqrt_eq_rpow],
  convert hf.rpow (1/2) hx,
  rcases lt_trichotomy (f x) 0 with H|H|H,
  { have A : (f x)^((1:‚Ñù)/2) = 0,
    { rw rpow_def_of_neg H,
      have : cos (1/2 * œÄ) = 0, by { convert cos_pi_div_two using 2, ring },
      rw [this],
      simp },
    have B : f x ^ ((1:‚Ñù) / 2 - 1) = 0,
    { rw rpow_def_of_neg H,
      have : cos (œÄ/2 - œÄ) = 0, by simp [cos_sub],
      have : cos (((1:‚Ñù)/2 - 1) * œÄ) = 0, by { convert this using 2, ring },
      rw this,
      simp },
    rw [A, B],
    simp },
  { exact (hx H).elim },
  { have A : 0 < (f x)^((1:‚Ñù)/2) := rpow_pos_of_pos H _,
    have B : (f x) ^ (-(1:‚Ñù)) = (f x)^(-((1:‚Ñù)/2)) * (f x)^(-((1:‚Ñù)/2)),
    { rw [‚Üê rpow_add H],
      congr,
      norm_num },
    rw [sub_eq_add_neg, rpow_add H, B, rpow_neg (le_of_lt H)],
    field_simp [hx, ne_of_gt A],
    ring }
end

lemma has_deriv_at.sqrt (hf : has_deriv_at f f' x) (hx : f x ‚â† 0) :
  has_deriv_at (Œª y, sqrt (f y)) (f' / (2 * sqrt(f x))) x :=
begin
  rw ‚Üê has_deriv_within_at_univ at *,
  exact hf.sqrt hx
end

lemma differentiable_within_at.sqrt (hf : differentiable_within_at ‚Ñù f s x) (hx : f x ‚â† 0) :
  differentiable_within_at ‚Ñù (Œªx, sqrt (f x)) s x :=
(hf.has_deriv_within_at.sqrt hx).differentiable_within_at

@[simp] lemma differentiable_at.sqrt (hf : differentiable_at ‚Ñù f x) (hx : f x ‚â† 0) :
  differentiable_at ‚Ñù (Œªx, sqrt (f x)) x :=
(hf.has_deriv_at.sqrt hx).differentiable_at

lemma differentiable_on.sqrt (hf : differentiable_on ‚Ñù f s) (hx : ‚àÄ x ‚àà s, f x ‚â† 0) :
  differentiable_on ‚Ñù (Œªx, sqrt (f x)) s :=
Œªx h, (hf x h).sqrt (hx x h)

@[simp] lemma differentiable.sqrt (hf : differentiable ‚Ñù f) (hx : ‚àÄ x, f x ‚â† 0) :
  differentiable ‚Ñù (Œªx, sqrt (f x)) :=
Œªx, (hf x).sqrt (hx x)

lemma deriv_within_sqrt (hf : differentiable_within_at ‚Ñù f s x) (hx : f x ‚â† 0)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, sqrt (f x)) s x = (deriv_within f s x) / (2 * sqrt (f x)) :=
(hf.has_deriv_within_at.sqrt hx).deriv_within hxs

@[simp] lemma deriv_sqrt (hf : differentiable_at ‚Ñù f x) (hx : f x ‚â† 0) :
  deriv (Œªx, sqrt (f x)) x = (deriv f x) / (2 * sqrt (f x)) :=
(hf.has_deriv_at.sqrt hx).deriv

end differentiability

namespace nnreal

/-- The nonnegative real power function `x^y`, defined for `x : ‚Ñù‚â•0` and `y : ‚Ñù ` as the
restriction of the real power function. For `x > 0`, it is equal to `exp (y log x)`. For `x = 0`,
one sets `0 ^ 0 = 1` and `0 ^ y = 0` for `y ‚â† 0`. -/
noncomputable def rpow (x : ‚Ñù‚â•0) (y : ‚Ñù) : ‚Ñù‚â•0 :=
‚ü®(x : ‚Ñù) ^ y, real.rpow_nonneg_of_nonneg x.2 y‚ü©

noncomputable instance : has_pow ‚Ñù‚â•0 ‚Ñù := ‚ü®rpow‚ü©

@[simp] lemma rpow_eq_pow (x : ‚Ñù‚â•0) (y : ‚Ñù) : rpow x y = x ^ y := rfl

@[simp, norm_cast] lemma coe_rpow (x : ‚Ñù‚â•0) (y : ‚Ñù) : ((x ^ y : ‚Ñù‚â•0) : ‚Ñù) = (x : ‚Ñù) ^ y := rfl

@[simp] lemma rpow_zero (x : ‚Ñù‚â•0) : x ^ (0 : ‚Ñù) = 1 :=
nnreal.eq $ real.rpow_zero _

@[simp] lemma rpow_eq_zero_iff {x : ‚Ñù‚â•0} {y : ‚Ñù} : x ^ y = 0 ‚Üî x = 0 ‚àß y ‚â† 0 :=
begin
  rw [‚Üê nnreal.coe_eq, coe_rpow, ‚Üê nnreal.coe_eq_zero],
  exact real.rpow_eq_zero_iff_of_nonneg x.2
end

@[simp] lemma zero_rpow {x : ‚Ñù} (h : x ‚â† 0) : (0 : ‚Ñù‚â•0) ^ x = 0 :=
nnreal.eq $ real.zero_rpow h

@[simp] lemma rpow_one (x : ‚Ñù‚â•0) : x ^ (1 : ‚Ñù) = x :=
nnreal.eq $ real.rpow_one _

@[simp] lemma one_rpow (x : ‚Ñù) : (1 : ‚Ñù‚â•0) ^ x = 1 :=
nnreal.eq $ real.one_rpow _

lemma rpow_add {x : ‚Ñù‚â•0} (hx : x ‚â† 0) (y z : ‚Ñù) : x ^ (y + z) = x ^ y * x ^ z :=
nnreal.eq $ real.rpow_add (zero_lt_iff_ne_zero.2 hx) _ _

lemma rpow_add' (x : ‚Ñù‚â•0) {y z : ‚Ñù} (h : y + z ‚â† 0) : x ^ (y + z) = x ^ y * x ^ z :=
nnreal.eq $ real.rpow_add' x.2 h

lemma rpow_mul (x : ‚Ñù‚â•0) (y z : ‚Ñù) : x ^ (y * z) = (x ^ y) ^ z :=
nnreal.eq $ real.rpow_mul x.2 y z

lemma rpow_neg (x : ‚Ñù‚â•0) (y : ‚Ñù) : x ^ -y = (x ^ y)‚Åª¬π :=
nnreal.eq $ real.rpow_neg x.2 _

lemma rpow_neg_one (x : ‚Ñù‚â•0) : x ^ (-1 : ‚Ñù) = x ‚Åª¬π :=
by simp [rpow_neg]

lemma rpow_sub {x : ‚Ñù‚â•0} (hx : x ‚â† 0) (y z : ‚Ñù) : x ^ (y - z) = x ^ y / x ^ z :=
nnreal.eq $ real.rpow_sub (zero_lt_iff_ne_zero.2 hx) y z

lemma rpow_sub' (x : ‚Ñù‚â•0) {y z : ‚Ñù} (h : y - z ‚â† 0) :
  x ^ (y - z) = x ^ y / x ^ z :=
nnreal.eq $ real.rpow_sub' x.2 h

lemma inv_rpow (x : ‚Ñù‚â•0) (y : ‚Ñù) : (x‚Åª¬π) ^ y = (x ^ y)‚Åª¬π :=
nnreal.eq $ real.inv_rpow x.2 y

lemma div_rpow (x y : ‚Ñù‚â•0) (z : ‚Ñù) : (x / y) ^ z = x ^ z / y ^ z :=
nnreal.eq $ real.div_rpow x.2 y.2 z

@[simp, norm_cast] lemma rpow_nat_cast (x : ‚Ñù‚â•0) (n : ‚Ñï) : x ^ (n : ‚Ñù) = x ^ n :=
nnreal.eq $ by simpa only [coe_rpow, coe_pow] using real.rpow_nat_cast x n

lemma mul_rpow {x y : ‚Ñù‚â•0} {z : ‚Ñù}  : (x*y)^z = x^z * y^z :=
nnreal.eq $ real.mul_rpow x.2 y.2

lemma one_le_rpow {x : ‚Ñù‚â•0} {z : ‚Ñù} (h : 1 ‚â§ x) (h‚ÇÅ : 0 ‚â§ z) : 1 ‚â§ x^z :=
real.one_le_rpow h h‚ÇÅ

lemma rpow_le_rpow {x y : ‚Ñù‚â•0} {z: ‚Ñù} (h‚ÇÅ : x ‚â§ y) (h‚ÇÇ : 0 ‚â§ z) : x^z ‚â§ y^z :=
real.rpow_le_rpow x.2 h‚ÇÅ h‚ÇÇ

lemma rpow_lt_rpow {x y : ‚Ñù‚â•0} {z: ‚Ñù} (h‚ÇÅ : x < y) (h‚ÇÇ : 0 < z) : x^z < y^z :=
real.rpow_lt_rpow x.2 h‚ÇÅ h‚ÇÇ

lemma rpow_lt_rpow_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : 0 < z) : x ^ z < y ^ z ‚Üî x < y :=
real.rpow_lt_rpow_iff x.2 y.2 hz

lemma rpow_le_rpow_iff {x y : ‚Ñù‚â•0} {z : ‚Ñù} (hz : 0 < z) : x ^ z ‚â§ y ^ z ‚Üî x ‚â§ y :=
real.rpow_le_rpow_iff x.2 y.2 hz

lemma rpow_lt_rpow_of_exponent_lt {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx : 1 < x) (hyz : y < z) : x^y < x^z :=
real.rpow_lt_rpow_of_exponent_lt hx hyz

lemma rpow_le_rpow_of_exponent_le {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx : 1 ‚â§ x) (hyz : y ‚â§ z) : x^y ‚â§ x^z :=
real.rpow_le_rpow_of_exponent_le hx hyz

lemma rpow_lt_rpow_of_exponent_gt {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx0 : 0 < x) (hx1 : x < 1) (hyz : z < y) :
  x^y < x^z :=
real.rpow_lt_rpow_of_exponent_gt hx0 hx1 hyz

lemma rpow_le_rpow_of_exponent_ge {x : ‚Ñù‚â•0} {y z : ‚Ñù} (hx0 : 0 < x) (hx1 : x ‚â§ 1) (hyz : z ‚â§ y) :
  x^y ‚â§ x^z :=
real.rpow_le_rpow_of_exponent_ge hx0 hx1 hyz

lemma rpow_le_one {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx2 : x ‚â§ 1) (hz : 0 ‚â§ z) : x^z ‚â§ 1 :=
real.rpow_le_one x.2 hx2 hz

lemma one_lt_rpow {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx : 1 < x) (hz : 0 < z) : 1 < x^z :=
real.one_lt_rpow hx hz

lemma rpow_lt_one {x : ‚Ñù‚â•0} {z : ‚Ñù} (hx : 0 < x) (hx1 : x < 1) (hz : 0 < z) : x^z < 1 :=
real.rpow_lt_one hx hx1 hz

lemma pow_nat_rpow_nat_inv (x : ‚Ñù‚â•0) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ n) ^ (n‚Åª¬π : ‚Ñù) = x :=
by { rw [‚Üê nnreal.coe_eq, coe_rpow, nnreal.coe_pow], exact real.pow_nat_rpow_nat_inv x.2 hn }

lemma rpow_nat_inv_pow_nat (x : ‚Ñù‚â•0) {n : ‚Ñï} (hn : 0 < n) :
  (x ^ (n‚Åª¬π : ‚Ñù)) ^ n = x :=
by { rw [‚Üê nnreal.coe_eq, nnreal.coe_pow, coe_rpow], exact real.rpow_nat_inv_pow_nat x.2 hn }

lemma continuous_at_rpow {x : ‚Ñù‚â•0} {y : ‚Ñù} (h : x ‚â† 0 ‚à® 0 < y) :
  continuous_at (Œªp:‚Ñù‚â•0√ó‚Ñù, p.1^p.2) (x, y) :=
begin
  have : (Œªp:‚Ñù‚â•0√ó‚Ñù, p.1^p.2) = nnreal.of_real ‚àò (Œªp:‚Ñù√ó‚Ñù, p.1^p.2) ‚àò (Œªp:‚Ñù‚â•0 √ó ‚Ñù, (p.1.1, p.2)),
  { ext p,
    rw [coe_rpow, nnreal.coe_of_real _ (real.rpow_nonneg_of_nonneg p.1.2 _)],
    refl },
  rw this,
  refine nnreal.continuous_of_real.continuous_at.comp (continuous_at.comp _ _),
  { apply real.continuous_at_rpow,
    simp at h,
    rw ‚Üê (nnreal.coe_eq_zero x) at h,
    exact h },
  { exact ((continuous_subtype_val.comp continuous_fst).prod_mk continuous_snd).continuous_at }
end

end nnreal

open filter

lemma filter.tendsto.nnrpow {Œ± : Type*} {f : filter Œ±} {u : Œ± ‚Üí ‚Ñù‚â•0} {v : Œ± ‚Üí ‚Ñù} {x : ‚Ñù‚â•0} {y : ‚Ñù}
  (hx : tendsto u f (ùìù x)) (hy : tendsto v f (ùìù y)) (h : x ‚â† 0 ‚à® 0 < y) :
  tendsto (Œª a, (u a) ^ (v a)) f (ùìù (x ^ y)) :=
tendsto.comp (nnreal.continuous_at_rpow h) (tendsto.prod_mk_nhds hx hy)

namespace ennreal

/-- The real power function `x^y` on extended nonnegative reals, defined for `x : ennreal` and
`y : ‚Ñù` as the restriction of the real power function if `0 < x < ‚ä§`, and with the natural values
for `0` and `‚ä§` (i.e., `0 ^ x = 0` for `x > 0`, `1` for `x = 0` and `‚ä§` for `x < 0`, and
`‚ä§ ^ x = 1 / 0 ^ x`). -/
noncomputable def rpow : ennreal ‚Üí ‚Ñù ‚Üí ennreal
| (some x) y := if x = 0 ‚àß y < 0 then ‚ä§ else (x ^ y : ‚Ñù‚â•0)
| none     y := if 0 < y then ‚ä§ else if y = 0 then 1 else 0

noncomputable instance : has_pow ennreal ‚Ñù := ‚ü®rpow‚ü©

@[simp] lemma rpow_eq_pow (x : ennreal) (y : ‚Ñù) : rpow x y = x ^ y := rfl

@[simp] lemma rpow_zero {x : ennreal} : x ^ (0 : ‚Ñù) = 1 :=
by cases x; { dsimp only [(^), rpow], simp [lt_irrefl] }

lemma top_rpow_def (y : ‚Ñù) : (‚ä§ : ennreal) ^ y = if 0 < y then ‚ä§ else if y = 0 then 1 else 0 :=
rfl

@[simp] lemma top_rpow_of_pos {y : ‚Ñù} (h : 0 < y) : (‚ä§ : ennreal) ^ y = ‚ä§ :=
by simp [top_rpow_def, h]

@[simp] lemma top_rpow_of_neg {y : ‚Ñù} (h : y < 0) : (‚ä§ : ennreal) ^ y = 0 :=
by simp [top_rpow_def, asymm h, ne_of_lt h]

@[simp] lemma zero_rpow_of_pos {y : ‚Ñù} (h : 0 < y) : (0 : ennreal) ^ y = 0 :=
begin
  rw [‚Üê ennreal.coe_zero, ‚Üê ennreal.some_eq_coe],
  dsimp only [(^), rpow],
  simp [h, asymm h, ne_of_gt h],
end

@[simp] lemma zero_rpow_of_neg {y : ‚Ñù} (h : y < 0) : (0 : ennreal) ^ y = ‚ä§ :=
begin
  rw [‚Üê ennreal.coe_zero, ‚Üê ennreal.some_eq_coe],
  dsimp only [(^), rpow],
  simp [h, ne_of_gt h],
end

lemma zero_rpow_def (y : ‚Ñù) : (0 : ennreal) ^ y = if 0 < y then 0 else if y = 0 then 1 else ‚ä§ :=
begin
  rcases lt_trichotomy 0 y with H|rfl|H,
  { simp [H, ne_of_gt, zero_rpow_of_pos, lt_irrefl] },
  { simp [lt_irrefl] },
  { simp [H, asymm H, ne_of_lt, zero_rpow_of_neg] }
end

@[norm_cast] lemma coe_rpow_of_ne_zero {x : ‚Ñù‚â•0} (h : x ‚â† 0) (y : ‚Ñù) :
  (x : ennreal) ^ y = (x ^ y : ‚Ñù‚â•0) :=
begin
  rw [‚Üê ennreal.some_eq_coe],
  dsimp only [(^), rpow],
  simp [h]
end

@[norm_cast] lemma coe_rpow_of_nonneg (x : ‚Ñù‚â•0) {y : ‚Ñù} (h : 0 ‚â§ y) :
  (x : ennreal) ^ y = (x ^ y : ‚Ñù‚â•0) :=
begin
  by_cases hx : x = 0,
  { rcases le_iff_eq_or_lt.1 h with H|H,
    { simp [hx, H.symm] },
    { simp [hx, zero_rpow_of_pos H, nnreal.zero_rpow (ne_of_gt H)] } },
  { exact coe_rpow_of_ne_zero hx _ }
end

@[simp] lemma rpow_one (x : ennreal) : x ^ (1 : ‚Ñù) = x :=
by cases x; dsimp only [(^), rpow]; simp [zero_lt_one, not_lt_of_le zero_le_one]

@[simp] lemma one_rpow (x : ‚Ñù) : (1 : ennreal) ^ x = 1 :=
by { rw [‚Üê coe_one, coe_rpow_of_ne_zero one_ne_zero], simp }

@[simp] lemma rpow_eq_zero_iff {x : ennreal} {y : ‚Ñù} :
  x ^ y = 0 ‚Üî (x = 0 ‚àß 0 < y) ‚à® (x = ‚ä§ ‚àß y < 0) :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with H|H|H;
    simp [H, top_rpow_of_neg, top_rpow_of_pos, le_of_lt] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with H|H|H;
      simp [h, H, zero_rpow_of_neg, zero_rpow_of_pos, le_of_lt] },
    { simp [coe_rpow_of_ne_zero h, h] } }
end

@[simp] lemma rpow_eq_top_iff {x : ennreal} {y : ‚Ñù} :
  x ^ y = ‚ä§ ‚Üî (x = 0 ‚àß y < 0) ‚à® (x = ‚ä§ ‚àß 0 < y) :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with H|H|H;
    simp [H, top_rpow_of_neg, top_rpow_of_pos, le_of_lt] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with H|H|H;
      simp [h, H, zero_rpow_of_neg, zero_rpow_of_pos, le_of_lt] },
    { simp [coe_rpow_of_ne_zero h, h] } }
end

lemma rpow_add {x : ennreal} (y z : ‚Ñù) (hx : x ‚â† 0) (h'x : x ‚â† ‚ä§) : x ^ (y + z) = x ^ y * x ^ z :=
begin
  cases x, { exact (h'x rfl).elim },
  have : x ‚â† 0 := Œª h, by simpa [h] using hx,
  simp [coe_rpow_of_ne_zero this, nnreal.rpow_add this]
end

lemma rpow_neg (x : ennreal) (y : ‚Ñù) : x ^ -y = (x ^ y)‚Åª¬π :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with H|H|H;
    simp [top_rpow_of_pos, top_rpow_of_neg, H, neg_pos.mpr] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with H|H|H;
      simp [h, zero_rpow_of_pos, zero_rpow_of_neg, H, neg_pos.mpr] },
    { have A : x ^ y ‚â† 0, by simp [h],
      simp [coe_rpow_of_ne_zero h, ‚Üê coe_inv A, nnreal.rpow_neg] } }
end

lemma rpow_neg_one (x : ennreal) : x ^ (-1 : ‚Ñù) = x ‚Åª¬π :=
by simp [rpow_neg]

lemma rpow_mul (x : ennreal) (y z : ‚Ñù) : x ^ (y * z) = (x ^ y) ^ z :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
    rcases lt_trichotomy z 0 with Hz|Hz|Hz;
    simp [Hy, Hz, zero_rpow_of_neg, zero_rpow_of_pos, top_rpow_of_neg, top_rpow_of_pos,
          mul_pos_of_neg_of_neg, mul_neg_of_neg_of_pos, mul_neg_of_pos_of_neg] },
  { by_cases h : x = 0,
    { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
      rcases lt_trichotomy z 0 with Hz|Hz|Hz;
      simp [h, Hy, Hz, zero_rpow_of_neg, zero_rpow_of_pos, top_rpow_of_neg, top_rpow_of_pos,
            mul_pos_of_neg_of_neg, mul_neg_of_neg_of_pos, mul_neg_of_pos_of_neg] },
    { have : x ^ y ‚â† 0, by simp [h],
      simp [coe_rpow_of_ne_zero h, coe_rpow_of_ne_zero this, nnreal.rpow_mul] } }
end

@[simp, norm_cast] lemma rpow_nat_cast (x : ennreal) (n : ‚Ñï) : x ^ (n : ‚Ñù) = x ^ n :=
begin
  cases x,
  { cases n;
    simp [top_rpow_of_pos (nat.cast_add_one_pos _), top_pow (nat.succ_pos _)] },
  { simp [coe_rpow_of_nonneg _ (nat.cast_nonneg n)] }
end

@[norm_cast] lemma coe_mul_rpow (x y : ‚Ñù‚â•0) (z : ‚Ñù) :
  ((x : ennreal) * y) ^ z = x^z * y^z :=
begin
  rcases lt_trichotomy z 0 with H|H|H,
  { by_cases hx : x = 0; by_cases hy : y = 0,
    { simp [hx, hy, zero_rpow_of_neg, H] },
    { have : (y : ennreal) ^ z ‚â† 0, by simp [rpow_eq_zero_iff, hy],
      simp [hx, hy, zero_rpow_of_neg, H, with_top.top_mul this] },
    { have : (x : ennreal) ^ z ‚â† 0, by simp [rpow_eq_zero_iff, hx],
      simp [hx, hy, zero_rpow_of_neg H, with_top.mul_top this] },
    { rw [‚Üê coe_mul, coe_rpow_of_ne_zero, nnreal.mul_rpow, coe_mul,
          coe_rpow_of_ne_zero hx, coe_rpow_of_ne_zero hy],
      simp [hx, hy] } },
  { simp [H] },
  { by_cases hx : x = 0; by_cases hy : y = 0,
    { simp [hx, hy, zero_rpow_of_pos, H] },
    { have : (y : ennreal) ^ z ‚â† 0, by simp [rpow_eq_zero_iff, hy],
      simp [hx, hy, zero_rpow_of_pos H, with_top.top_mul this] },
    { have : (x : ennreal) ^ z ‚â† 0, by simp [rpow_eq_zero_iff, hx],
      simp [hx, hy, zero_rpow_of_pos H, with_top.mul_top this] },
    { rw [‚Üê coe_mul, coe_rpow_of_ne_zero, nnreal.mul_rpow, coe_mul,
          coe_rpow_of_ne_zero hx, coe_rpow_of_ne_zero hy],
      simp [hx, hy] } },
end

lemma mul_rpow_of_ne_top {x y : ennreal} (hx : x ‚â† ‚ä§) (hy : y ‚â† ‚ä§) (z : ‚Ñù) :
  (x * y) ^ z = x^z * y^z :=
begin
  lift x to ‚Ñù‚â•0 using hx,
  lift y to ‚Ñù‚â•0 using hy,
  exact coe_mul_rpow x y z
end

lemma mul_rpow_of_ne_zero {x y : ennreal} (hx : x ‚â† 0) (hy : y ‚â† 0) (z : ‚Ñù) :
  (x * y) ^ z = x ^ z * y ^ z :=
begin
  rcases lt_trichotomy z 0 with H|H|H,
  { cases x; cases y,
    { simp [hx, hy, top_rpow_of_neg, H] },
    { have : y ‚â† 0, by simpa using hy,
      simp [hx, hy, top_rpow_of_neg, H, rpow_eq_zero_iff, this] },
    { have : x ‚â† 0, by simpa using hx,
      simp [hx, hy, top_rpow_of_neg, H, rpow_eq_zero_iff, this] },
    { have hx' : x ‚â† 0, by simpa using hx,
      have hy' : y ‚â† 0, by simpa using hy,
      simp only [some_eq_coe],
      rw [‚Üê coe_mul, coe_rpow_of_ne_zero, nnreal.mul_rpow, coe_mul,
          coe_rpow_of_ne_zero hx', coe_rpow_of_ne_zero hy'],
      simp [hx', hy'] } },
  { simp [H] },
  { cases x; cases y,
    { simp [hx, hy, top_rpow_of_pos, H] },
    { have : y ‚â† 0, by simpa using hy,
      simp [hx, hy, top_rpow_of_pos, H, rpow_eq_zero_iff, this] },
    { have : x ‚â† 0, by simpa using hx,
      simp [hx, hy, top_rpow_of_pos, H, rpow_eq_zero_iff, this] },
    { have hx' : x ‚â† 0, by simpa using hx,
      have hy' : y ‚â† 0, by simpa using hy,
      simp only [some_eq_coe],
      rw [‚Üê coe_mul, coe_rpow_of_ne_zero, nnreal.mul_rpow, coe_mul,
          coe_rpow_of_ne_zero hx', coe_rpow_of_ne_zero hy'],
      simp [hx', hy'] } }
end

lemma mul_rpow_of_nonneg (x y : ennreal) {z : ‚Ñù} (hz : 0 ‚â§ z) :
  (x * y) ^ z = x ^ z * y ^ z :=
begin
  rcases le_iff_eq_or_lt.1 hz with H|H, { simp [‚Üê H] },
  by_cases h : x = 0 ‚à® y = 0,
  { cases h; simp [h, zero_rpow_of_pos H] },
  push_neg at h,
  exact mul_rpow_of_ne_zero h.1 h.2 z
end

lemma one_le_rpow {x : ennreal} {z : ‚Ñù} (h : 1 ‚â§ x) (h‚ÇÅ : 0 ‚â§ z) : 1 ‚â§ x^z :=
begin
  cases x,
  { rcases le_iff_eq_or_lt.1 h‚ÇÅ with H|H,
    { simp [‚Üê H, le_refl] },
    { simp [top_rpow_of_pos H] } },
  { simp only [one_le_coe_iff, some_eq_coe] at h,
    simp [coe_rpow_of_nonneg _ h‚ÇÅ, nnreal.one_le_rpow h h‚ÇÅ] }
end

lemma rpow_le_rpow {x y : ennreal} {z : ‚Ñù} (h‚ÇÅ : x ‚â§ y) (h‚ÇÇ : 0 ‚â§ z) : x^z ‚â§ y^z :=
begin
  rcases le_iff_eq_or_lt.1 h‚ÇÇ with H|H, { simp [‚Üê H, le_refl] },
  cases y, { simp [top_rpow_of_pos H] },
  cases x, { exact (not_top_le_coe h‚ÇÅ).elim },
  simp at h‚ÇÅ,
  simp [coe_rpow_of_nonneg _ h‚ÇÇ, nnreal.rpow_le_rpow h‚ÇÅ h‚ÇÇ]
end

lemma rpow_lt_rpow {x y : ennreal} {z : ‚Ñù} (h‚ÇÅ : x < y) (h‚ÇÇ : 0 < z) : x^z < y^z :=
begin
  cases x, { exact (not_top_lt h‚ÇÅ).elim },
  cases y, { simp [top_rpow_of_pos h‚ÇÇ, coe_rpow_of_nonneg _ (le_of_lt h‚ÇÇ)] },
  simp at h‚ÇÅ,
  simp [coe_rpow_of_nonneg _ (le_of_lt h‚ÇÇ), nnreal.rpow_lt_rpow h‚ÇÅ h‚ÇÇ]
end

lemma rpow_lt_rpow_of_exponent_lt {x : ennreal} {y z : ‚Ñù} (hx : 1 < x) (hx' : x ‚â† ‚ä§) (hyz : y < z) :
  x^y < x^z :=
begin
  lift x to ‚Ñù‚â•0 using hx',
  rw [one_lt_coe_iff] at hx,
  simp [coe_rpow_of_ne_zero (ne_of_gt (lt_trans zero_lt_one hx)),
        nnreal.rpow_lt_rpow_of_exponent_lt hx hyz]
end

lemma rpow_le_rpow_of_exponent_le {x : ennreal} {y z : ‚Ñù} (hx : 1 ‚â§ x) (hyz : y ‚â§ z) : x^y ‚â§ x^z :=
begin
  cases x,
  { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
    rcases lt_trichotomy z 0 with Hz|Hz|Hz;
    simp [Hy, Hz, top_rpow_of_neg, top_rpow_of_pos, le_refl];
    linarith },
  { simp only [one_le_coe_iff, some_eq_coe] at hx,
    simp [coe_rpow_of_ne_zero (ne_of_gt (lt_of_lt_of_le zero_lt_one hx)),
          nnreal.rpow_le_rpow_of_exponent_le hx hyz] }
end

lemma rpow_lt_rpow_of_exponent_gt {x : ennreal} {y z : ‚Ñù} (hx0 : 0 < x) (hx1 : x < 1) (hyz : z < y) :
  x^y < x^z :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_lt_of_le hx1 le_top),
  simp at hx0 hx1,
  simp [coe_rpow_of_ne_zero (ne_of_gt hx0), nnreal.rpow_lt_rpow_of_exponent_gt hx0 hx1 hyz]
end

lemma rpow_le_rpow_of_exponent_ge {x : ennreal} {y z : ‚Ñù} (hx1 : x ‚â§ 1) (hyz : z ‚â§ y) :
  x^y ‚â§ x^z :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_le_of_lt hx1 coe_lt_top),
  by_cases h : x = 0,
  { rcases lt_trichotomy y 0 with Hy|Hy|Hy;
    rcases lt_trichotomy z 0 with Hz|Hz|Hz;
    simp [Hy, Hz, h, zero_rpow_of_neg, zero_rpow_of_pos, le_refl];
    linarith },
  { simp at hx1,
    simp [coe_rpow_of_ne_zero h,
          nnreal.rpow_le_rpow_of_exponent_ge (bot_lt_iff_ne_bot.mpr h) hx1 hyz] }
end

lemma rpow_le_one {x : ennreal} {z : ‚Ñù} (hx2 : x ‚â§ 1) (hz : 0 ‚â§ z) : x^z ‚â§ 1 :=
begin
  lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_le_of_lt hx2 coe_lt_top),
  simp at hx2,
  simp [coe_rpow_of_nonneg _ hz, nnreal.rpow_le_one hx2 hz]
end

lemma one_lt_rpow {x : ennreal} {z : ‚Ñù} (hx : 1 < x) (hz : 0 < z) : 1 < x^z :=
begin
  cases x,
  { simp [top_rpow_of_pos hz],
    exact coe_lt_top },
  { simp at hx,
    simp [coe_rpow_of_nonneg _ (le_of_lt hz), nnreal.one_lt_rpow hx hz] }
end

lemma rpow_lt_one {x : ennreal} {z : ‚Ñù} (hx1 : x < 1) (hz : 0 < z) : x^z < 1 :=
begin
  by_cases h : x = 0,
  { simp [h, zero_rpow_of_pos hz, ennreal.zero_lt_one] },
  { lift x to ‚Ñù‚â•0 using ne_of_lt (lt_of_lt_of_le hx1 le_top),
    simp at h hx1,
    have : 0 < x := bot_lt_iff_ne_bot.mpr h,
    simp [coe_rpow_of_nonneg _ (le_of_lt hz), nnreal.rpow_lt_one this hx1 hz] }
end

lemma to_real_rpow (x : ennreal) (z : ‚Ñù) :
  (x.to_real) ^ z = (x ^ z).to_real :=
begin
  rcases lt_trichotomy z 0 with H|H|H,
  { cases x, { simp [H, ne_of_lt] },
    by_cases hx : x = 0,
    { simp [hx, H, ne_of_lt] },
    { simp [coe_rpow_of_ne_zero hx] } },
  { simp [H] },
  { cases x, { simp [H, ne_of_gt] },
    simp [coe_rpow_of_nonneg _ (le_of_lt H)] }
end

end ennreal
