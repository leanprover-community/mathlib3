/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, S√©bastien Gou√´zel,
  R√©my Degenne
-/
import analysis.special_functions.pow
import analysis.special_functions.complex.log_deriv
import analysis.calculus.extend_deriv
import analysis.special_functions.log.deriv
import analysis.special_functions.trigonometric.deriv

/-!
# Derivatives of power function on `‚ÑÇ`, `‚Ñù`, `‚Ñù‚â•0`, and `‚Ñù‚â•0‚àû`

We also prove differentiability and provide derivatives for the power functions `x ^ y`.
-/

noncomputable theory

open_locale classical real topological_space nnreal ennreal filter
open filter

namespace complex

lemma has_strict_fderiv_at_cpow {p : ‚ÑÇ √ó ‚ÑÇ} (hp : 0 < p.1.re ‚à® p.1.im ‚â† 0) :
  has_strict_fderiv_at (Œª x : ‚ÑÇ √ó ‚ÑÇ, x.1 ^ x.2)
    ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ continuous_linear_map.fst ‚ÑÇ ‚ÑÇ ‚ÑÇ +
      (p.1 ^ p.2 * log p.1) ‚Ä¢ continuous_linear_map.snd ‚ÑÇ ‚ÑÇ ‚ÑÇ) p :=
begin
  have A : p.1 ‚â† 0, by { intro h, simpa [h, lt_irrefl] using hp },
  have : (Œª x : ‚ÑÇ √ó ‚ÑÇ, x.1 ^ x.2) =·∂†[ùìù p] (Œª x, exp (log x.1 * x.2)),
    from ((is_open_ne.preimage continuous_fst).eventually_mem A).mono
      (Œª p hp, cpow_def_of_ne_zero hp _),
  rw [cpow_sub _ _ A, cpow_one, mul_div_left_comm, mul_smul, mul_smul, ‚Üê smul_add],
  refine has_strict_fderiv_at.congr_of_eventually_eq _ this.symm,
  simpa only [cpow_def_of_ne_zero A, div_eq_mul_inv, mul_smul, add_comm]
    using ((has_strict_fderiv_at_fst.clog hp).mul has_strict_fderiv_at_snd).cexp
end

lemma has_strict_fderiv_at_cpow' {x y : ‚ÑÇ} (hp : 0 < x.re ‚à® x.im ‚â† 0) :
  has_strict_fderiv_at (Œª x : ‚ÑÇ √ó ‚ÑÇ, x.1 ^ x.2)
    ((y * x ^ (y - 1)) ‚Ä¢ continuous_linear_map.fst ‚ÑÇ ‚ÑÇ ‚ÑÇ +
      (x ^ y * log x) ‚Ä¢ continuous_linear_map.snd ‚ÑÇ ‚ÑÇ ‚ÑÇ) (x, y) :=
@has_strict_fderiv_at_cpow (x, y) hp

lemma has_strict_deriv_at_const_cpow {x y : ‚ÑÇ} (h : x ‚â† 0 ‚à® y ‚â† 0) :
  has_strict_deriv_at (Œª y, x ^ y) (x ^ y * log x) y :=
begin
  rcases em (x = 0) with rfl|hx,
  { replace h := h.neg_resolve_left rfl,
    rw [log_zero, mul_zero],
    refine (has_strict_deriv_at_const _ 0).congr_of_eventually_eq _,
    exact (is_open_ne.eventually_mem h).mono (Œª y hy, (zero_cpow hy).symm) },
  { simpa only [cpow_def_of_ne_zero hx, mul_one]
      using ((has_strict_deriv_at_id y).const_mul (log x)).cexp }
end

lemma has_fderiv_at_cpow {p : ‚ÑÇ √ó ‚ÑÇ} (hp : 0 < p.1.re ‚à® p.1.im ‚â† 0) :
  has_fderiv_at (Œª x : ‚ÑÇ √ó ‚ÑÇ, x.1 ^ x.2)
    ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ continuous_linear_map.fst ‚ÑÇ ‚ÑÇ ‚ÑÇ +
      (p.1 ^ p.2 * log p.1) ‚Ä¢ continuous_linear_map.snd ‚ÑÇ ‚ÑÇ ‚ÑÇ) p :=
(has_strict_fderiv_at_cpow hp).has_fderiv_at

end complex

section fderiv

open complex

variables {E : Type*} [normed_group E] [normed_space ‚ÑÇ E] {f g : E ‚Üí ‚ÑÇ} {f' g' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ}
  {x : E} {s : set E} {c : ‚ÑÇ}

lemma has_strict_fderiv_at.cpow (hf : has_strict_fderiv_at f f' x)
  (hg : has_strict_fderiv_at g g' x) (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_strict_fderiv_at (Œª x, f x ^ g x)
    ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * log (f x)) ‚Ä¢ g') x :=
by convert (@has_strict_fderiv_at_cpow ((Œª x, (f x, g x)) x) h0).comp x (hf.prod hg)

lemma has_strict_fderiv_at.const_cpow (hf : has_strict_fderiv_at f f' x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
  has_strict_fderiv_at (Œª x, c ^ f x) ((c ^ f x * log c) ‚Ä¢ f') x :=
(has_strict_deriv_at_const_cpow h0).comp_has_strict_fderiv_at x hf

lemma has_fderiv_at.cpow (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x)
  (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_fderiv_at (Œª x, f x ^ g x)
    ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * log (f x)) ‚Ä¢ g') x :=
by convert (@complex.has_fderiv_at_cpow ((Œª x, (f x, g x)) x) h0).comp x (hf.prod hg)

lemma has_fderiv_at.const_cpow (hf : has_fderiv_at f f' x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
  has_fderiv_at (Œª x, c ^ f x) ((c ^ f x * log c) ‚Ä¢ f') x :=
(has_strict_deriv_at_const_cpow h0).has_deriv_at.comp_has_fderiv_at x hf

lemma has_fderiv_within_at.cpow (hf : has_fderiv_within_at f f' s x)
  (hg : has_fderiv_within_at g g' s x) (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_fderiv_within_at (Œª x, f x ^ g x)
    ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * log (f x)) ‚Ä¢ g') s x :=
by convert (@complex.has_fderiv_at_cpow ((Œª x, (f x, g x)) x) h0).comp_has_fderiv_within_at x
  (hf.prod hg)

lemma has_fderiv_within_at.const_cpow (hf : has_fderiv_within_at f f' s x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
  has_fderiv_within_at (Œª x, c ^ f x) ((c ^ f x * log c) ‚Ä¢ f') s x :=
(has_strict_deriv_at_const_cpow h0).has_deriv_at.comp_has_fderiv_within_at x hf

lemma differentiable_at.cpow (hf : differentiable_at ‚ÑÇ f x) (hg : differentiable_at ‚ÑÇ g x)
  (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  differentiable_at ‚ÑÇ (Œª x, f x ^ g x) x :=
(hf.has_fderiv_at.cpow hg.has_fderiv_at h0).differentiable_at

lemma differentiable_at.const_cpow (hf : differentiable_at ‚ÑÇ f x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
  differentiable_at ‚ÑÇ (Œª x, c ^ f x) x :=
(hf.has_fderiv_at.const_cpow h0).differentiable_at

lemma differentiable_within_at.cpow (hf : differentiable_within_at ‚ÑÇ f s x)
  (hg : differentiable_within_at ‚ÑÇ g s x) (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  differentiable_within_at ‚ÑÇ (Œª x, f x ^ g x) s x :=
(hf.has_fderiv_within_at.cpow hg.has_fderiv_within_at h0).differentiable_within_at

lemma differentiable_within_at.const_cpow (hf : differentiable_within_at ‚ÑÇ f s x)
  (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
  differentiable_within_at ‚ÑÇ (Œª x, c ^ f x) s x :=
(hf.has_fderiv_within_at.const_cpow h0).differentiable_within_at

end fderiv

section deriv

open complex

variables {f g : ‚ÑÇ ‚Üí ‚ÑÇ} {s : set ‚ÑÇ} {f' g' x c : ‚ÑÇ}

/-- A private lemma that rewrites the output of lemmas like `has_fderiv_at.cpow` to the form
expected by lemmas like `has_deriv_at.cpow`. -/
private lemma aux :
  ((g x * f x ^ (g x - 1)) ‚Ä¢ (1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smul_right f' +
    (f x ^ g x * log (f x)) ‚Ä¢ (1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smul_right g') 1 =
    g x * f x ^ (g x - 1) * f' + f x ^ g x * log (f x) * g' :=
by simp only [algebra.id.smul_eq_mul, one_mul, continuous_linear_map.one_apply,
  continuous_linear_map.smul_right_apply, continuous_linear_map.add_apply, pi.smul_apply,
  continuous_linear_map.coe_smul']

lemma has_strict_deriv_at.cpow (hf : has_strict_deriv_at f f' x) (hg : has_strict_deriv_at g g' x)
  (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_strict_deriv_at (Œª x, f x ^ g x)
    (g x * f x ^ (g x - 1) * f' + f x ^ g x * log (f x) * g') x :=
by simpa only [aux] using (hf.cpow hg h0).has_strict_deriv_at

lemma has_strict_deriv_at.const_cpow (hf : has_strict_deriv_at f f' x) (h : c ‚â† 0 ‚à® f x ‚â† 0) :
  has_strict_deriv_at (Œª x, c ^ f x) (c ^ f x * log c * f') x :=
(has_strict_deriv_at_const_cpow h).comp x hf

lemma complex.has_strict_deriv_at_cpow_const (h : 0 < x.re ‚à® x.im ‚â† 0) :
  has_strict_deriv_at (Œª z : ‚ÑÇ, z ^ c) (c * x ^ (c - 1)) x :=
by simpa only [mul_zero, add_zero, mul_one]
  using (has_strict_deriv_at_id x).cpow (has_strict_deriv_at_const x c) h

lemma has_strict_deriv_at.cpow_const (hf : has_strict_deriv_at f f' x)
  (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_strict_deriv_at (Œª x, f x ^ c) (c * f x ^ (c - 1) * f') x :=
(complex.has_strict_deriv_at_cpow_const h0).comp x hf

lemma has_deriv_at.cpow (hf : has_deriv_at f f' x) (hg : has_deriv_at g g' x)
  (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_deriv_at (Œª x, f x ^ g x) (g x * f x ^ (g x - 1) * f' + f x ^ g x * log (f x) * g') x :=
by simpa only [aux] using (hf.has_fderiv_at.cpow hg h0).has_deriv_at

lemma has_deriv_at.const_cpow (hf : has_deriv_at f f' x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
  has_deriv_at (Œª x, c ^ f x) (c ^ f x * log c * f') x :=
(has_strict_deriv_at_const_cpow h0).has_deriv_at.comp x hf

lemma has_deriv_at.cpow_const (hf : has_deriv_at f f' x) (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_deriv_at (Œª x, f x ^ c) (c * f x ^ (c - 1) * f') x :=
(complex.has_strict_deriv_at_cpow_const h0).has_deriv_at.comp x hf

lemma has_deriv_within_at.cpow (hf : has_deriv_within_at f f' s x)
  (hg : has_deriv_within_at g g' s x) (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_deriv_within_at (Œª x, f x ^ g x)
    (g x * f x ^ (g x - 1) * f' + f x ^ g x * log (f x) * g') s x :=
by simpa only [aux] using (hf.has_fderiv_within_at.cpow hg h0).has_deriv_within_at

lemma has_deriv_within_at.const_cpow (hf : has_deriv_within_at f f' s x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
  has_deriv_within_at (Œª x, c ^ f x) (c ^ f x * log c * f') s x :=
(has_strict_deriv_at_const_cpow h0).has_deriv_at.comp_has_deriv_within_at x hf

lemma has_deriv_within_at.cpow_const (hf : has_deriv_within_at f f' s x)
  (h0 : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
  has_deriv_within_at (Œª x, f x ^ c) (c * f x ^ (c - 1) * f') s x :=
(complex.has_strict_deriv_at_cpow_const h0).has_deriv_at.comp_has_deriv_within_at x hf

end deriv

namespace real

variables {x y z : ‚Ñù}

/-- `(x, y) ‚Ü¶ x ^ y` is strictly differentiable at `p : ‚Ñù √ó ‚Ñù` such that `0 < p.fst`. -/
lemma has_strict_fderiv_at_rpow_of_pos (p : ‚Ñù √ó ‚Ñù) (hp : 0 < p.1) :
  has_strict_fderiv_at (Œª x : ‚Ñù √ó ‚Ñù, x.1 ^ x.2)
    ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ continuous_linear_map.fst ‚Ñù ‚Ñù ‚Ñù +
      (p.1 ^ p.2 * log p.1) ‚Ä¢ continuous_linear_map.snd ‚Ñù ‚Ñù ‚Ñù) p :=
begin
  have : (Œª x : ‚Ñù √ó ‚Ñù, x.1 ^ x.2) =·∂†[ùìù p] (Œª x, exp (log x.1 * x.2)),
    from (continuous_at_fst.eventually (lt_mem_nhds hp)).mono (Œª p hp, rpow_def_of_pos hp _),
  refine has_strict_fderiv_at.congr_of_eventually_eq _ this.symm,
  convert ((has_strict_fderiv_at_fst.log hp.ne').mul has_strict_fderiv_at_snd).exp,
  rw [rpow_sub_one hp.ne', ‚Üê rpow_def_of_pos hp, smul_add, smul_smul, mul_div_left_comm,
    div_eq_mul_inv, smul_smul, smul_smul, mul_assoc, add_comm]
end

/-- `(x, y) ‚Ü¶ x ^ y` is strictly differentiable at `p : ‚Ñù √ó ‚Ñù` such that `p.fst < 0`. -/
lemma has_strict_fderiv_at_rpow_of_neg (p : ‚Ñù √ó ‚Ñù) (hp : p.1 < 0) :
  has_strict_fderiv_at (Œª x : ‚Ñù √ó ‚Ñù, x.1 ^ x.2)
    ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ continuous_linear_map.fst ‚Ñù ‚Ñù ‚Ñù +
      (p.1 ^ p.2 * log p.1 - exp (log p.1 * p.2) * sin (p.2 * œÄ) * œÄ) ‚Ä¢
        continuous_linear_map.snd ‚Ñù ‚Ñù ‚Ñù) p :=
begin
  have : (Œª x : ‚Ñù √ó ‚Ñù, x.1 ^ x.2) =·∂†[ùìù p] (Œª x, exp (log x.1 * x.2) * cos (x.2 * œÄ)),
    from (continuous_at_fst.eventually (gt_mem_nhds hp)).mono (Œª p hp, rpow_def_of_neg hp _),
  refine has_strict_fderiv_at.congr_of_eventually_eq _ this.symm,
  convert ((has_strict_fderiv_at_fst.log hp.ne).mul has_strict_fderiv_at_snd).exp.mul
    (has_strict_fderiv_at_snd.mul_const _).cos using 1,
  simp_rw [rpow_sub_one hp.ne, smul_add, ‚Üê add_assoc, smul_smul, ‚Üê add_smul, ‚Üê mul_assoc,
    mul_comm (cos _), ‚Üê rpow_def_of_neg hp],
  rw [div_eq_mul_inv, add_comm], congr' 2; ring
end

/-- The function `Œª (x, y), x ^ y` is infinitely smooth at `(x, y)` unless `x = 0`. -/
lemma cont_diff_at_rpow_of_ne (p : ‚Ñù √ó ‚Ñù) (hp : p.1 ‚â† 0) {n : with_top ‚Ñï} :
  cont_diff_at ‚Ñù n (Œª p : ‚Ñù √ó ‚Ñù, p.1 ^ p.2) p :=
begin
  cases hp.lt_or_lt with hneg hpos,
  exacts [(((cont_diff_at_fst.log hneg.ne).mul cont_diff_at_snd).exp.mul
    (cont_diff_at_snd.mul cont_diff_at_const).cos).congr_of_eventually_eq
      ((continuous_at_fst.eventually (gt_mem_nhds hneg)).mono (Œª p hp, rpow_def_of_neg hp _)),
    ((cont_diff_at_fst.log hpos.ne').mul cont_diff_at_snd).exp.congr_of_eventually_eq
      ((continuous_at_fst.eventually (lt_mem_nhds hpos)).mono (Œª p hp, rpow_def_of_pos hp _))]
end

lemma differentiable_at_rpow_of_ne (p : ‚Ñù √ó ‚Ñù) (hp : p.1 ‚â† 0) :
  differentiable_at ‚Ñù (Œª p : ‚Ñù √ó ‚Ñù, p.1 ^ p.2) p :=
(cont_diff_at_rpow_of_ne p hp).differentiable_at le_rfl

lemma _root_.has_strict_deriv_at.rpow {f g : ‚Ñù ‚Üí ‚Ñù} {f' g' : ‚Ñù} (hf : has_strict_deriv_at f f' x)
  (hg : has_strict_deriv_at g g' x) (h : 0 < f x) :
  has_strict_deriv_at (Œª x, f x ^ g x)
    (f' * g x * (f x) ^ (g x - 1) + g' * f x ^ g x * log (f x)) x :=
begin
  convert (has_strict_fderiv_at_rpow_of_pos ((Œª x, (f x, g x)) x) h).comp_has_strict_deriv_at _
    (hf.prod hg) using 1,
  simp [mul_assoc, mul_comm, mul_left_comm]
end

lemma has_strict_deriv_at_rpow_const_of_ne {x : ‚Ñù} (hx : x ‚â† 0) (p : ‚Ñù) :
  has_strict_deriv_at (Œª x, x ^ p) (p * x ^ (p - 1)) x :=
begin
  cases hx.lt_or_lt with hx hx,
  { have := (has_strict_fderiv_at_rpow_of_neg (x, p) hx).comp_has_strict_deriv_at x
      ((has_strict_deriv_at_id x).prod (has_strict_deriv_at_const _ _)),
    convert this, simp },
  { simpa using (has_strict_deriv_at_id x).rpow (has_strict_deriv_at_const x p) hx }
end

lemma has_strict_deriv_at_const_rpow {a : ‚Ñù} (ha : 0 < a) (x : ‚Ñù) :
  has_strict_deriv_at (Œª x, a ^ x) (a ^ x * log a) x :=
by simpa using (has_strict_deriv_at_const _ _).rpow (has_strict_deriv_at_id x) ha

/-- This lemma says that `Œª x, a ^ x` is strictly differentiable for `a < 0`. Note that these
values of `a` are outside of the "official" domain of `a ^ x`, and we may redefine `a ^ x`
for negative `a` if some other definition will be more convenient. -/
lemma has_strict_deriv_at_const_rpow_of_neg {a x : ‚Ñù} (ha : a < 0) :
  has_strict_deriv_at (Œª x, a ^ x) (a ^ x * log a - exp (log a * x) * sin (x * œÄ) * œÄ) x :=
by simpa using (has_strict_fderiv_at_rpow_of_neg (a, x) ha).comp_has_strict_deriv_at x
  ((has_strict_deriv_at_const _ _).prod (has_strict_deriv_at_id _))

end real

namespace real

variables {z x y : ‚Ñù}

lemma has_deriv_at_rpow_const {x p : ‚Ñù} (h : x ‚â† 0 ‚à® 1 ‚â§ p) :
  has_deriv_at (Œª x, x ^ p) (p * x ^ (p - 1)) x :=
begin
  rcases ne_or_eq x 0 with hx | rfl,
  { exact (has_strict_deriv_at_rpow_const_of_ne hx _).has_deriv_at },
  replace h : 1 ‚â§ p := h.neg_resolve_left rfl,
  apply has_deriv_at_of_has_deriv_at_of_ne
    (Œª x hx, (has_strict_deriv_at_rpow_const_of_ne hx p).has_deriv_at),
  exacts [continuous_at_id.rpow_const (or.inr (zero_le_one.trans h)),
    continuous_at_const.mul (continuous_at_id.rpow_const (or.inr (sub_nonneg.2 h)))]
end

lemma differentiable_rpow_const {p : ‚Ñù} (hp : 1 ‚â§ p) :
  differentiable ‚Ñù (Œª x : ‚Ñù, x ^ p) :=
Œª x, (has_deriv_at_rpow_const (or.inr hp)).differentiable_at

lemma deriv_rpow_const {x p : ‚Ñù} (h : x ‚â† 0 ‚à® 1 ‚â§ p) :
  deriv (Œª x : ‚Ñù, x ^ p) x = p * x ^ (p - 1) :=
(has_deriv_at_rpow_const h).deriv

lemma deriv_rpow_const' {p : ‚Ñù} (h : 1 ‚â§ p) :
  deriv (Œª x : ‚Ñù, x ^ p) = Œª x, p * x ^ (p - 1) :=
funext $ Œª x, deriv_rpow_const (or.inr h)

lemma cont_diff_at_rpow_const_of_ne {x p : ‚Ñù} {n : with_top ‚Ñï} (h : x ‚â† 0) :
  cont_diff_at ‚Ñù n (Œª x, x ^ p) x :=
(cont_diff_at_rpow_of_ne (x, p) h).comp x
  (cont_diff_at_id.prod cont_diff_at_const)

lemma cont_diff_rpow_const_of_le {p : ‚Ñù} {n : ‚Ñï} (h : ‚Üën ‚â§ p) :
  cont_diff ‚Ñù n (Œª x : ‚Ñù, x ^ p) :=
begin
  induction n with n ihn generalizing p,
  { exact cont_diff_zero.2 (continuous_id.rpow_const (Œª x, or.inr h)) },
  { have h1 : 1 ‚â§ p, from le_trans (by simp) h,
    rw [nat.cast_succ, ‚Üê le_sub_iff_add_le] at h,
    simpa [cont_diff_succ_iff_deriv, differentiable_rpow_const, h1, deriv_rpow_const']
      using cont_diff_const.mul (ihn h) }
end

lemma cont_diff_at_rpow_const_of_le {x p : ‚Ñù} {n : ‚Ñï} (h : ‚Üën ‚â§ p) :
  cont_diff_at ‚Ñù n (Œª x : ‚Ñù, x ^ p) x :=
(cont_diff_rpow_const_of_le h).cont_diff_at

lemma cont_diff_at_rpow_const {x p : ‚Ñù} {n : ‚Ñï} (h : x ‚â† 0 ‚à® ‚Üën ‚â§ p) :
  cont_diff_at ‚Ñù n (Œª x : ‚Ñù, x ^ p) x :=
h.elim cont_diff_at_rpow_const_of_ne cont_diff_at_rpow_const_of_le

lemma has_strict_deriv_at_rpow_const {x p : ‚Ñù} (hx : x ‚â† 0 ‚à® 1 ‚â§ p) :
  has_strict_deriv_at (Œª x, x ^ p) (p * x ^ (p - 1)) x :=
cont_diff_at.has_strict_deriv_at'
  (cont_diff_at_rpow_const (by rwa nat.cast_one))
  (has_deriv_at_rpow_const hx) le_rfl

end real

section differentiability
open real

section fderiv

variables {E : Type*} [normed_group E] [normed_space ‚Ñù E] {f g : E ‚Üí ‚Ñù} {f' g' : E ‚ÜíL[‚Ñù] ‚Ñù}
  {x : E} {s : set E} {c p : ‚Ñù} {n : with_top ‚Ñï}

lemma has_fderiv_within_at.rpow (hf : has_fderiv_within_at f f' s x)
  (hg : has_fderiv_within_at g g' s x) (h : 0 < f x) :
  has_fderiv_within_at (Œª x, f x ^ g x)
    ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * log (f x)) ‚Ä¢ g') s x :=
(has_strict_fderiv_at_rpow_of_pos (f x, g x) h).has_fderiv_at.comp_has_fderiv_within_at x
  (hf.prod hg)

lemma has_fderiv_at.rpow (hf : has_fderiv_at f f' x) (hg : has_fderiv_at g g' x) (h : 0 < f x) :
  has_fderiv_at (Œª x, f x ^ g x) ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * log (f x)) ‚Ä¢ g') x :=
(has_strict_fderiv_at_rpow_of_pos (f x, g x) h).has_fderiv_at.comp x (hf.prod hg)

lemma has_strict_fderiv_at.rpow (hf : has_strict_fderiv_at f f' x)
  (hg : has_strict_fderiv_at g g' x) (h : 0 < f x) :
  has_strict_fderiv_at (Œª x, f x ^ g x)
    ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * log (f x)) ‚Ä¢ g') x :=
(has_strict_fderiv_at_rpow_of_pos (f x, g x) h).comp x (hf.prod hg)

lemma differentiable_within_at.rpow (hf : differentiable_within_at ‚Ñù f s x)
  (hg : differentiable_within_at ‚Ñù g s x) (h : f x ‚â† 0) :
  differentiable_within_at ‚Ñù (Œª x, f x ^ g x) s x :=
(differentiable_at_rpow_of_ne (f x, g x) h).comp_differentiable_within_at x (hf.prod hg)

lemma differentiable_at.rpow (hf : differentiable_at ‚Ñù f x) (hg : differentiable_at ‚Ñù g x)
  (h : f x ‚â† 0) :
  differentiable_at ‚Ñù (Œª x, f x ^ g x) x :=
(differentiable_at_rpow_of_ne (f x, g x) h).comp x (hf.prod hg)

lemma differentiable_on.rpow (hf : differentiable_on ‚Ñù f s) (hg : differentiable_on ‚Ñù g s)
  (h : ‚àÄ x ‚àà s, f x ‚â† 0) :
  differentiable_on ‚Ñù (Œª x, f x ^ g x) s :=
Œª x hx, (hf x hx).rpow (hg x hx) (h x hx)

lemma differentiable.rpow (hf : differentiable ‚Ñù f) (hg : differentiable ‚Ñù g) (h : ‚àÄ x, f x ‚â† 0) :
  differentiable ‚Ñù (Œª x, f x ^ g x) :=
Œª x, (hf x).rpow (hg x) (h x)

lemma has_fderiv_within_at.rpow_const (hf : has_fderiv_within_at f f' s x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  has_fderiv_within_at (Œª x, f x ^ p) ((p * f x ^ (p - 1)) ‚Ä¢ f') s x :=
(has_deriv_at_rpow_const h).comp_has_fderiv_within_at x hf

lemma has_fderiv_at.rpow_const (hf : has_fderiv_at f f' x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  has_fderiv_at (Œª x, f x ^ p) ((p * f x ^ (p - 1)) ‚Ä¢ f') x :=
(has_deriv_at_rpow_const h).comp_has_fderiv_at x hf

lemma has_strict_fderiv_at.rpow_const (hf : has_strict_fderiv_at f f' x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  has_strict_fderiv_at (Œª x, f x ^ p) ((p * f x ^ (p - 1)) ‚Ä¢ f') x :=
(has_strict_deriv_at_rpow_const h).comp_has_strict_fderiv_at x hf

lemma differentiable_within_at.rpow_const (hf : differentiable_within_at ‚Ñù f s x)
  (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  differentiable_within_at ‚Ñù (Œª x, f x ^ p) s x :=
(hf.has_fderiv_within_at.rpow_const h).differentiable_within_at

@[simp] lemma differentiable_at.rpow_const (hf : differentiable_at ‚Ñù f x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  differentiable_at ‚Ñù (Œª x, f x ^ p) x :=
(hf.has_fderiv_at.rpow_const h).differentiable_at

lemma differentiable_on.rpow_const (hf : differentiable_on ‚Ñù f s) (h : ‚àÄ x ‚àà s, f x ‚â† 0 ‚à® 1 ‚â§ p) :
  differentiable_on ‚Ñù (Œª x, f x ^ p) s :=
Œª x hx, (hf x hx).rpow_const (h x hx)

lemma differentiable.rpow_const (hf : differentiable ‚Ñù f) (h : ‚àÄ x, f x ‚â† 0 ‚à® 1 ‚â§ p) :
  differentiable ‚Ñù (Œª x, f x ^ p) :=
Œª x, (hf x).rpow_const (h x)

lemma has_fderiv_within_at.const_rpow (hf : has_fderiv_within_at f f' s x) (hc : 0 < c) :
  has_fderiv_within_at (Œª x, c ^ f x) ((c ^ f x * log c) ‚Ä¢ f') s x :=
(has_strict_deriv_at_const_rpow hc (f x)).has_deriv_at.comp_has_fderiv_within_at x hf

lemma has_fderiv_at.const_rpow (hf : has_fderiv_at f f' x) (hc : 0 < c) :
  has_fderiv_at (Œª x, c ^ f x) ((c ^ f x * log c) ‚Ä¢ f') x :=
(has_strict_deriv_at_const_rpow hc (f x)).has_deriv_at.comp_has_fderiv_at x hf

lemma has_strict_fderiv_at.const_rpow (hf : has_strict_fderiv_at f f' x) (hc : 0 < c) :
  has_strict_fderiv_at (Œª x, c ^ f x) ((c ^ f x * log c) ‚Ä¢ f') x :=
(has_strict_deriv_at_const_rpow hc (f x)).comp_has_strict_fderiv_at x hf

lemma cont_diff_within_at.rpow (hf : cont_diff_within_at ‚Ñù n f s x)
  (hg : cont_diff_within_at ‚Ñù n g s x) (h : f x ‚â† 0) :
  cont_diff_within_at ‚Ñù n (Œª x, f x ^ g x) s x :=
(cont_diff_at_rpow_of_ne (f x, g x) h).comp_cont_diff_within_at x (hf.prod hg)

lemma cont_diff_at.rpow (hf : cont_diff_at ‚Ñù n f x) (hg : cont_diff_at ‚Ñù n g x)
  (h : f x ‚â† 0) :
  cont_diff_at ‚Ñù n (Œª x, f x ^ g x) x :=
(cont_diff_at_rpow_of_ne (f x, g x) h).comp x (hf.prod hg)

lemma cont_diff_on.rpow (hf : cont_diff_on ‚Ñù n f s) (hg : cont_diff_on ‚Ñù n g s)
  (h : ‚àÄ x ‚àà s, f x ‚â† 0) :
  cont_diff_on ‚Ñù n (Œª x, f x ^ g x) s :=
Œª x hx, (hf x hx).rpow (hg x hx) (h x hx)

lemma cont_diff.rpow (hf : cont_diff ‚Ñù n f) (hg : cont_diff ‚Ñù n g)
  (h : ‚àÄ x, f x ‚â† 0) :
  cont_diff ‚Ñù n (Œª x, f x ^ g x) :=
cont_diff_iff_cont_diff_at.mpr $
  Œª x, hf.cont_diff_at.rpow hg.cont_diff_at (h x)

lemma cont_diff_within_at.rpow_const_of_ne (hf : cont_diff_within_at ‚Ñù n f s x)
  (h : f x ‚â† 0) :
  cont_diff_within_at ‚Ñù n (Œª x, f x ^ p) s x :=
hf.rpow cont_diff_within_at_const h

lemma cont_diff_at.rpow_const_of_ne (hf : cont_diff_at ‚Ñù n f x) (h : f x ‚â† 0) :
  cont_diff_at ‚Ñù n (Œª x, f x ^ p) x :=
hf.rpow cont_diff_at_const h

lemma cont_diff_on.rpow_const_of_ne (hf : cont_diff_on ‚Ñù n f s) (h : ‚àÄ x ‚àà s, f x ‚â† 0) :
  cont_diff_on ‚Ñù n (Œª x, f x ^ p) s :=
Œª x hx, (hf x hx).rpow_const_of_ne (h x hx)

lemma cont_diff.rpow_const_of_ne (hf : cont_diff ‚Ñù n f) (h : ‚àÄ x, f x ‚â† 0) :
  cont_diff ‚Ñù n (Œª x, f x ^ p) :=
hf.rpow cont_diff_const h

variable {m : ‚Ñï}

lemma cont_diff_within_at.rpow_const_of_le (hf : cont_diff_within_at ‚Ñù m f s x)
  (h : ‚Üëm ‚â§ p) :
  cont_diff_within_at ‚Ñù m (Œª x, f x ^ p) s x :=
(cont_diff_at_rpow_const_of_le h).comp_cont_diff_within_at x hf

lemma cont_diff_at.rpow_const_of_le (hf : cont_diff_at ‚Ñù m f x) (h : ‚Üëm ‚â§ p) :
  cont_diff_at ‚Ñù m (Œª x, f x ^ p) x :=
by { rw ‚Üê cont_diff_within_at_univ at *, exact hf.rpow_const_of_le h }

lemma cont_diff_on.rpow_const_of_le (hf : cont_diff_on ‚Ñù m f s) (h : ‚Üëm ‚â§ p) :
  cont_diff_on ‚Ñù m (Œª x, f x ^ p) s :=
Œª x hx, (hf x hx).rpow_const_of_le h

lemma cont_diff.rpow_const_of_le (hf : cont_diff ‚Ñù m f) (h : ‚Üëm ‚â§ p) :
  cont_diff ‚Ñù m (Œª x, f x ^ p) :=
cont_diff_iff_cont_diff_at.mpr $ Œª x, hf.cont_diff_at.rpow_const_of_le h

end fderiv

section deriv

variables {f g : ‚Ñù ‚Üí ‚Ñù} {f' g' x y p : ‚Ñù} {s : set ‚Ñù}

lemma has_deriv_within_at.rpow (hf : has_deriv_within_at f f' s x)
  (hg : has_deriv_within_at g g' s x) (h : 0 < f x) :
  has_deriv_within_at (Œª x, f x ^ g x)
    (f' * g x * (f x) ^ (g x - 1) + g' * f x ^ g x * log (f x)) s x :=
begin
  convert (hf.has_fderiv_within_at.rpow hg.has_fderiv_within_at h).has_deriv_within_at using 1,
  dsimp, ring
end

lemma has_deriv_at.rpow (hf : has_deriv_at f f' x) (hg : has_deriv_at g g' x) (h : 0 < f x) :
  has_deriv_at (Œª x, f x ^ g x) (f' * g x * (f x) ^ (g x - 1) + g' * f x ^ g x * log (f x)) x :=
begin
  rw ‚Üê has_deriv_within_at_univ at *,
  exact hf.rpow hg h
end

lemma has_deriv_within_at.rpow_const (hf : has_deriv_within_at f f' s x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  has_deriv_within_at (Œª y, (f y)^p) (f' * p * (f x) ^ (p - 1)) s x :=
begin
  convert (has_deriv_at_rpow_const hx).comp_has_deriv_within_at x hf using 1,
  ring
end

lemma has_deriv_at.rpow_const (hf : has_deriv_at f f' x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  has_deriv_at (Œª y, (f y)^p) (f' * p * (f x)^(p-1)) x :=
begin
  rw ‚Üê has_deriv_within_at_univ at *,
  exact hf.rpow_const hx
end

lemma deriv_within_rpow_const (hf : differentiable_within_at ‚Ñù f s x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p)
  (hxs : unique_diff_within_at ‚Ñù s x) :
  deriv_within (Œªx, (f x) ^ p) s x = (deriv_within f s x) * p * (f x) ^ (p - 1) :=
(hf.has_deriv_within_at.rpow_const hx).deriv_within hxs

@[simp] lemma deriv_rpow_const (hf : differentiable_at ‚Ñù f x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p) :
  deriv (Œªx, (f x)^p) x = (deriv f x) * p * (f x)^(p-1) :=
(hf.has_deriv_at.rpow_const hx).deriv

end deriv

end differentiability

section limits
open real filter

/-- The function `(1 + t/x) ^ x` tends to `exp t` at `+‚àû`. -/
lemma tendsto_one_plus_div_rpow_exp (t : ‚Ñù) :
  tendsto (Œª (x : ‚Ñù), (1 + t / x) ^ x) at_top (ùìù (exp t)) :=
begin
  apply ((real.continuous_exp.tendsto _).comp (tendsto_mul_log_one_plus_div_at_top t)).congr' _,
  have h‚ÇÅ : (1:‚Ñù)/2 < 1 := by linarith,
  have h‚ÇÇ : tendsto (Œª x : ‚Ñù, 1 + t / x) at_top (ùìù 1) :=
    by simpa using (tendsto_inv_at_top_zero.const_mul t).const_add 1,
  refine (eventually_ge_of_tendsto_gt h‚ÇÅ h‚ÇÇ).mono (Œª x hx, _),
  have hx' : 0 < 1 + t / x := by linarith,
  simp [mul_comm x, exp_mul, exp_log hx'],
end

/-- The function `(1 + t/x) ^ x` tends to `exp t` at `+‚àû` for naturals `x`. -/
lemma tendsto_one_plus_div_pow_exp (t : ‚Ñù) :
  tendsto (Œª (x : ‚Ñï), (1 + t / (x:‚Ñù)) ^ x) at_top (ùìù (real.exp t)) :=
((tendsto_one_plus_div_rpow_exp t).comp tendsto_coe_nat_at_top_at_top).congr (by simp)

end limits
