/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import algebra.quadratic_discriminant
import analysis.special_functions.complex.log
import analysis.complex.polynomial
import field_theory.is_alg_closed.basic

/-!
# Complex trigonometric functions

Basic facts and derivatives for the complex trigonometric functions.
-/

noncomputable theory

namespace complex

open set filter
open_locale real

theorem cos_eq_zero_iff {Œ∏ : ‚ÑÇ} : cos Œ∏ = 0 ‚Üî ‚àÉ k : ‚Ñ§, Œ∏ = (2 * k + 1) * œÄ / 2 :=
begin
  have h : (exp (Œ∏ * I) + exp (-Œ∏ * I)) / 2 = 0 ‚Üî exp (2 * Œ∏ * I) = -1,
  { rw [@div_eq_iff _ _ (exp (Œ∏ * I) + exp (-Œ∏ * I)) 2 0 two_ne_zero', zero_mul,
      add_eq_zero_iff_eq_neg, neg_eq_neg_one_mul, ‚Üê div_eq_iff (exp_ne_zero _), ‚Üê exp_sub],
    field_simp only, congr' 3, ring },
  rw [cos, h, ‚Üê exp_pi_mul_I, exp_eq_exp_iff_exists_int, mul_right_comm],
  refine exists_congr (Œª x, _),
  refine (iff_of_eq $ congr_arg _ _).trans (mul_right_inj' $ mul_ne_zero two_ne_zero' I_ne_zero),
  ring,
end

theorem cos_ne_zero_iff {Œ∏ : ‚ÑÇ} : cos Œ∏ ‚â† 0 ‚Üî ‚àÄ k : ‚Ñ§, Œ∏ ‚â† (2 * k + 1) * œÄ / 2 :=
by rw [‚Üê not_exists, not_iff_not, cos_eq_zero_iff]

theorem sin_eq_zero_iff {Œ∏ : ‚ÑÇ} : sin Œ∏ = 0 ‚Üî ‚àÉ k : ‚Ñ§, Œ∏ = k * œÄ :=
begin
  rw [‚Üê complex.cos_sub_pi_div_two, cos_eq_zero_iff],
  split,
  { rintros ‚ü®k, hk‚ü©,
    use k + 1,
    field_simp [eq_add_of_sub_eq hk],
    ring },
  { rintros ‚ü®k, rfl‚ü©,
    use k - 1,
    field_simp,
    ring }
end

theorem sin_ne_zero_iff {Œ∏ : ‚ÑÇ} : sin Œ∏ ‚â† 0 ‚Üî ‚àÄ k : ‚Ñ§, Œ∏ ‚â† k * œÄ :=
by rw [‚Üê not_exists, not_iff_not, sin_eq_zero_iff]


lemma tan_eq_zero_iff {Œ∏ : ‚ÑÇ} : tan Œ∏ = 0 ‚Üî ‚àÉ k : ‚Ñ§, Œ∏ = k * œÄ / 2 :=
begin
  have h := (sin_two_mul Œ∏).symm,
  rw mul_assoc at h,
  rw [tan, div_eq_zero_iff, ‚Üê mul_eq_zero, ‚Üê zero_mul ((1/2):‚ÑÇ), mul_one_div,
      cancel_factors.cancel_factors_eq_div h two_ne_zero', mul_comm],
  simpa only [zero_div, zero_mul, ne.def, not_false_iff] with field_simps using sin_eq_zero_iff,
end

lemma tan_ne_zero_iff {Œ∏ : ‚ÑÇ} : tan Œ∏ ‚â† 0 ‚Üî ‚àÄ k : ‚Ñ§, Œ∏ ‚â† k * œÄ / 2 :=
by rw [‚Üê not_exists, not_iff_not, tan_eq_zero_iff]

lemma tan_int_mul_pi_div_two (n : ‚Ñ§) : tan (n * œÄ/2) = 0 :=
tan_eq_zero_iff.mpr (by use n)

lemma cos_eq_cos_iff {x y : ‚ÑÇ} :
  cos x = cos y ‚Üî ‚àÉ k : ‚Ñ§, y = 2 * k * œÄ + x ‚à® y = 2 * k * œÄ - x :=
calc cos x = cos y ‚Üî cos x - cos y = 0 : sub_eq_zero.symm
... ‚Üî -2 * sin((x + y)/2) * sin((x - y)/2) = 0 : by rw cos_sub_cos
... ‚Üî sin((x + y)/2) = 0 ‚à® sin((x - y)/2) = 0 : by simp [(by norm_num : (2:‚ÑÇ) ‚â† 0)]
... ‚Üî sin((x - y)/2) = 0 ‚à® sin((x + y)/2) = 0 : or.comm
... ‚Üî (‚àÉ k : ‚Ñ§, y = 2 * k * œÄ + x) ‚à® (‚àÉ k :‚Ñ§, y = 2 * k * œÄ - x) :
begin
  apply or_congr;
    field_simp [sin_eq_zero_iff, (by norm_num : -(2:‚ÑÇ) ‚â† 0), eq_sub_iff_add_eq',
      sub_eq_iff_eq_add, mul_comm (2:‚ÑÇ), mul_right_comm _ (2:‚ÑÇ)],
  split; { rintros ‚ü®k, rfl‚ü©, use -k, simp, },
end
... ‚Üî ‚àÉ k : ‚Ñ§, y = 2 * k * œÄ + x ‚à® y = 2 * k * œÄ - x : exists_or_distrib.symm

lemma sin_eq_sin_iff {x y : ‚ÑÇ} :
  sin x = sin y ‚Üî ‚àÉ k : ‚Ñ§, y = 2 * k * œÄ + x ‚à® y = (2 * k + 1) * œÄ - x :=
begin
  simp only [‚Üê complex.cos_sub_pi_div_two, cos_eq_cos_iff, sub_eq_iff_eq_add],
  refine exists_congr (Œª k, or_congr _ _); refine eq.congr rfl _; field_simp; ring
end

lemma tan_add {x y : ‚ÑÇ}
  (h : ((‚àÄ k : ‚Ñ§, x ‚â† (2 * k + 1) * œÄ / 2) ‚àß ‚àÄ l : ‚Ñ§, y ‚â† (2 * l + 1) * œÄ / 2)
     ‚à® ((‚àÉ k : ‚Ñ§, x = (2 * k + 1) * œÄ / 2) ‚àß ‚àÉ l : ‚Ñ§, y = (2 * l + 1) * œÄ / 2)) :
  tan (x + y) = (tan x + tan y) / (1 - tan x * tan y) :=
begin
  rcases h with ‚ü®h1, h2‚ü© | ‚ü®‚ü®k, rfl‚ü©, ‚ü®l, rfl‚ü©‚ü©,
  { rw [tan, sin_add, cos_add,
        ‚Üê div_div_div_cancel_right (sin x * cos y + cos x * sin y)
            (mul_ne_zero (cos_ne_zero_iff.mpr h1) (cos_ne_zero_iff.mpr h2)),
        add_div, sub_div],
    simp only [‚Üêdiv_mul_div, ‚Üêtan, mul_one, one_mul,
              div_self (cos_ne_zero_iff.mpr h1), div_self (cos_ne_zero_iff.mpr h2)] },
  { obtain ‚ü®t, hx, hy, hxy‚ü© := ‚ü®tan_int_mul_pi_div_two, t (2*k+1), t (2*l+1), t (2*k+1+(2*l+1))‚ü©,
    simp only [int.cast_add, int.cast_bit0, int.cast_mul, int.cast_one, hx, hy] at hx hy hxy,
    rw [hx, hy, add_zero, zero_div,
        mul_div_assoc, mul_div_assoc, ‚Üê add_mul (2*(k:‚ÑÇ)+1) (2*l+1) (œÄ/2), ‚Üê mul_div_assoc, hxy] },
end

lemma tan_add' {x y : ‚ÑÇ}
  (h : ((‚àÄ k : ‚Ñ§, x ‚â† (2 * k + 1) * œÄ / 2) ‚àß ‚àÄ l : ‚Ñ§, y ‚â† (2 * l + 1) * œÄ / 2)) :
  tan (x + y) = (tan x + tan y) / (1 - tan x * tan y) :=
tan_add (or.inl h)

lemma tan_two_mul {z : ‚ÑÇ} : tan (2 * z) = 2 * tan z / (1 - tan z ^ 2) :=
begin
  by_cases h : ‚àÄ k : ‚Ñ§, z ‚â† (2 * k + 1) * œÄ / 2,
  { rw [two_mul, two_mul, sq, tan_add (or.inl ‚ü®h, h‚ü©)] },
  { rw not_forall_not at h,
    rw [two_mul, two_mul, sq, tan_add (or.inr ‚ü®h, h‚ü©)] },
end

lemma tan_add_mul_I {x y : ‚ÑÇ}
  (h : ((‚àÄ k : ‚Ñ§, x ‚â† (2 * k + 1) * œÄ / 2) ‚àß ‚àÄ l : ‚Ñ§, y * I ‚â† (2 * l + 1) * œÄ / 2)
     ‚à® ((‚àÉ k : ‚Ñ§, x = (2 * k + 1) * œÄ / 2) ‚àß ‚àÉ l : ‚Ñ§, y * I = (2 * l + 1) * œÄ / 2)) :
  tan (x + y*I) = (tan x + tanh y * I) / (1 - tan x * tanh y * I) :=
by rw [tan_add h, tan_mul_I, mul_assoc]

lemma tan_eq {z : ‚ÑÇ}
  (h : ((‚àÄ k : ‚Ñ§, (z.re:‚ÑÇ) ‚â† (2 * k + 1) * œÄ / 2) ‚àß ‚àÄ l : ‚Ñ§, (z.im:‚ÑÇ) * I ‚â† (2 * l + 1) * œÄ / 2)
     ‚à® ((‚àÉ k : ‚Ñ§, (z.re:‚ÑÇ) = (2 * k + 1) * œÄ / 2) ‚àß ‚àÉ l : ‚Ñ§, (z.im:‚ÑÇ) * I = (2 * l + 1) * œÄ / 2)) :
  tan z = (tan z.re + tanh z.im * I) / (1 - tan z.re * tanh z.im * I) :=
by convert tan_add_mul_I h; exact (re_add_im z).symm

lemma has_strict_deriv_at_tan {x : ‚ÑÇ} (h : cos x ‚â† 0) :
  has_strict_deriv_at tan (1 / (cos x)^2) x :=
begin
  convert (has_strict_deriv_at_sin x).div (has_strict_deriv_at_cos x) h,
  rw ‚Üê sin_sq_add_cos_sq x,
  ring,
end

lemma has_deriv_at_tan {x : ‚ÑÇ} (h : cos x ‚â† 0) :
  has_deriv_at tan (1 / (cos x)^2) x :=
(has_strict_deriv_at_tan h).has_deriv_at

open_locale topological_space

lemma tendsto_abs_tan_of_cos_eq_zero {x : ‚ÑÇ} (hx : cos x = 0) :
  tendsto (Œª x, abs (tan x)) (ùìù[{x}·∂ú] x) at_top :=
begin
  simp only [tan_eq_sin_div_cos, ‚Üê norm_eq_abs, normed_field.norm_div],
  have A : sin x ‚â† 0 := Œª h, by simpa [*, sq] using sin_sq_add_cos_sq x,
  have B : tendsto cos (ùìù[{x}·∂ú] (x)) (ùìù[{0}·∂ú] 0),
  { refine tendsto_inf.2 ‚ü®tendsto.mono_left _ inf_le_left, tendsto_principal.2 _‚ü©,
    exacts [continuous_cos.tendsto' x 0 hx,
      hx ‚ñ∏ (has_deriv_at_cos _).eventually_ne (neg_ne_zero.2 A)] },
  exact continuous_sin.continuous_within_at.norm.mul_at_top (norm_pos_iff.2 A)
    (tendsto_norm_nhds_within_zero.comp B).inv_tendsto_zero,
end

lemma tendsto_abs_tan_at_top (k : ‚Ñ§) :
  tendsto (Œª x, abs (tan x)) (ùìù[{(2 * k + 1) * œÄ / 2}·∂ú] ((2 * k + 1) * œÄ / 2)) at_top :=
tendsto_abs_tan_of_cos_eq_zero $ cos_eq_zero_iff.2 ‚ü®k, rfl‚ü©

@[simp] lemma continuous_at_tan {x : ‚ÑÇ} : continuous_at tan x ‚Üî cos x ‚â† 0 :=
begin
  refine ‚ü®Œª hc h‚ÇÄ, _, Œª h, (has_deriv_at_tan h).continuous_at‚ü©,
  exact not_tendsto_nhds_of_tendsto_at_top (tendsto_abs_tan_of_cos_eq_zero h‚ÇÄ) _
    (hc.norm.tendsto.mono_left inf_le_left)
end

@[simp] lemma differentiable_at_tan {x : ‚ÑÇ} : differentiable_at ‚ÑÇ tan x ‚Üî cos x ‚â† 0 :=
‚ü®Œª h, continuous_at_tan.1 h.continuous_at, Œª h, (has_deriv_at_tan h).differentiable_at‚ü©

@[simp] lemma deriv_tan (x : ‚ÑÇ) : deriv tan x = 1 / (cos x)^2 :=
if h : cos x = 0 then
  have ¬¨differentiable_at ‚ÑÇ tan x := mt differentiable_at_tan.1 (not_not.2 h),
  by simp [deriv_zero_of_not_differentiable_at this, h, sq]
else (has_deriv_at_tan h).deriv

lemma continuous_on_tan : continuous_on tan {x | cos x ‚â† 0} :=
continuous_on_sin.div continuous_on_cos $ Œª x, id

@[continuity]
lemma continuous_tan : continuous (Œª x : {x | cos x ‚â† 0}, tan x) :=
continuous_on_iff_continuous_restrict.1 continuous_on_tan

@[simp] lemma times_cont_diff_at_tan {x : ‚ÑÇ} {n : with_top ‚Ñï} :
  times_cont_diff_at ‚ÑÇ n tan x ‚Üî cos x ‚â† 0 :=
‚ü®Œª h, continuous_at_tan.1 h.continuous_at,
  times_cont_diff_sin.times_cont_diff_at.div times_cont_diff_cos.times_cont_diff_at‚ü©

lemma cos_eq_iff_quadratic {z w : ‚ÑÇ} :
  cos z = w ‚Üî (exp (z * I)) ^ 2 - 2 * w * exp (z * I) + 1 = 0 :=
begin
  rw ‚Üê sub_eq_zero,
  field_simp [cos, exp_neg, exp_ne_zero],
  refine eq.congr _ rfl,
  ring
end

lemma cos_surjective : function.surjective cos :=
begin
  intro x,
  obtain ‚ü®w, w‚ÇÄ, hw‚ü© : ‚àÉ w ‚â† 0, 1 * w * w + (-2 * x) * w + 1 = 0,
  { rcases exists_quadratic_eq_zero (@one_ne_zero ‚ÑÇ _ _) (is_alg_closed.exists_eq_mul_self _)
      with ‚ü®w, hw‚ü©,
    refine ‚ü®w, _, hw‚ü©,
    rintro rfl,
    simpa only [zero_add, one_ne_zero, mul_zero] using hw },
  refine ‚ü®log w / I, cos_eq_iff_quadratic.2 _‚ü©,
  rw [div_mul_cancel _ I_ne_zero, exp_log w‚ÇÄ],
  convert hw,
  ring
end

@[simp] lemma range_cos : range cos = set.univ :=
cos_surjective.range_eq

lemma sin_surjective : function.surjective sin :=
begin
  intro x,
  rcases cos_surjective x with ‚ü®z, rfl‚ü©,
  exact ‚ü®z + œÄ / 2, sin_add_pi_div_two z‚ü©
end

@[simp] lemma range_sin : range sin = set.univ :=
sin_surjective.range_eq

end complex

namespace real

open_locale real

theorem cos_eq_zero_iff {Œ∏ : ‚Ñù} : cos Œ∏ = 0 ‚Üî ‚àÉ k : ‚Ñ§, Œ∏ = (2 * k + 1) * œÄ / 2 :=
by exact_mod_cast @complex.cos_eq_zero_iff Œ∏

theorem cos_ne_zero_iff {Œ∏ : ‚Ñù} : cos Œ∏ ‚â† 0 ‚Üî ‚àÄ k : ‚Ñ§, Œ∏ ‚â† (2 * k + 1) * œÄ / 2 :=
by rw [‚Üê not_exists, not_iff_not, cos_eq_zero_iff]

lemma cos_eq_cos_iff {x y : ‚Ñù} :
  cos x = cos y ‚Üî ‚àÉ k : ‚Ñ§, y = 2 * k * œÄ + x ‚à® y = 2 * k * œÄ - x :=
by exact_mod_cast @complex.cos_eq_cos_iff x y

lemma sin_eq_sin_iff {x y : ‚Ñù} :
  sin x = sin y ‚Üî ‚àÉ k : ‚Ñ§, y = 2 * k * œÄ + x ‚à® y = (2 * k + 1) * œÄ - x :=
by exact_mod_cast @complex.sin_eq_sin_iff x y

end real
