/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import analysis.special_functions.trigonometric.basic

/-!
# Inverse trigonometric functions.

See also `analysis.special_functions.trigonometric.arctan` for the inverse tan function.
(This is delayed as it is easier to set up after developing complex trigonometric functions.)

Basic inequalities and derivatives.
-/

noncomputable theory
open_locale classical topological_space filter
open set filter

open_locale real

namespace real

/-- Inverse of the `sin` function, returns values in the range `-œÄ / 2 ‚â§ arcsin x ‚â§ œÄ / 2`.
It defaults to `-œÄ / 2` on `(-‚àû, -1)` and to `œÄ / 2` to `(1, ‚àû)`. -/
@[pp_nodot] noncomputable def arcsin : ‚Ñù ‚Üí ‚Ñù :=
coe ‚àò Icc_extend (neg_le_self zero_le_one) sin_order_iso.symm

lemma arcsin_mem_Icc (x : ‚Ñù) : arcsin x ‚àà Icc (-(œÄ / 2)) (œÄ / 2) := subtype.coe_prop _

@[simp] lemma range_arcsin : range arcsin = Icc (-(œÄ / 2)) (œÄ / 2) :=
by { rw [arcsin, range_comp coe], simp [Icc] }

lemma arcsin_le_pi_div_two (x : ‚Ñù) : arcsin x ‚â§ œÄ / 2 := (arcsin_mem_Icc x).2

lemma neg_pi_div_two_le_arcsin (x : ‚Ñù) : -(œÄ / 2) ‚â§ arcsin x := (arcsin_mem_Icc x).1

lemma arcsin_proj_Icc (x : ‚Ñù) :
  arcsin (proj_Icc (-1) 1 (neg_le_self $ @zero_le_one ‚Ñù _) x) = arcsin x :=
by rw [arcsin, function.comp_app, Icc_extend_coe, function.comp_app, Icc_extend]

lemma sin_arcsin' {x : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) : sin (arcsin x) = x :=
by simpa [arcsin, Icc_extend_of_mem _ _ hx, -order_iso.apply_symm_apply]
  using subtype.ext_iff.1 (sin_order_iso.apply_symm_apply ‚ü®x, hx‚ü©)

lemma sin_arcsin {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : sin (arcsin x) = x :=
sin_arcsin' ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©

lemma arcsin_sin' {x : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) : arcsin (sin x) = x :=
inj_on_sin (arcsin_mem_Icc _) hx $ by rw [sin_arcsin (neg_one_le_sin _) (sin_le_one _)]

lemma arcsin_sin {x : ‚Ñù} (hx‚ÇÅ : -(œÄ / 2) ‚â§ x) (hx‚ÇÇ : x ‚â§ œÄ / 2) : arcsin (sin x) = x :=
arcsin_sin' ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©

lemma strict_mono_incr_on_arcsin : strict_mono_incr_on arcsin (Icc (-1) 1) :=
(subtype.strict_mono_coe _).comp_strict_mono_incr_on $
  sin_order_iso.symm.strict_mono.strict_mono_incr_on_Icc_extend _

lemma monotone_arcsin : monotone arcsin :=
(subtype.mono_coe _).comp $ sin_order_iso.symm.monotone.Icc_extend _

lemma inj_on_arcsin : inj_on arcsin (Icc (-1) 1) := strict_mono_incr_on_arcsin.inj_on

lemma arcsin_inj {x y : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) (hy‚ÇÅ : -1 ‚â§ y) (hy‚ÇÇ : y ‚â§ 1) :
  arcsin x = arcsin y ‚Üî x = y :=
inj_on_arcsin.eq_iff ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©

@[continuity]
lemma continuous_arcsin : continuous arcsin :=
continuous_subtype_coe.comp sin_order_iso.symm.continuous.Icc_extend

lemma continuous_at_arcsin {x : ‚Ñù} : continuous_at arcsin x :=
continuous_arcsin.continuous_at

lemma arcsin_eq_of_sin_eq {x y : ‚Ñù} (h‚ÇÅ : sin x = y) (h‚ÇÇ : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
  arcsin y = x :=
begin
  subst y,
  exact inj_on_sin (arcsin_mem_Icc _) h‚ÇÇ (sin_arcsin' (sin_mem_Icc x))
end

@[simp] lemma arcsin_zero : arcsin 0 = 0 :=
arcsin_eq_of_sin_eq sin_zero ‚ü®neg_nonpos.2 pi_div_two_pos.le, pi_div_two_pos.le‚ü©

@[simp] lemma arcsin_one : arcsin 1 = œÄ / 2 :=
arcsin_eq_of_sin_eq sin_pi_div_two $ right_mem_Icc.2 (neg_le_self pi_div_two_pos.le)

lemma arcsin_of_one_le {x : ‚Ñù} (hx : 1 ‚â§ x) : arcsin x = œÄ / 2 :=
by rw [‚Üê arcsin_proj_Icc, proj_Icc_of_right_le _ hx, subtype.coe_mk, arcsin_one]

lemma arcsin_neg_one : arcsin (-1) = -(œÄ / 2) :=
arcsin_eq_of_sin_eq (by rw [sin_neg, sin_pi_div_two]) $
  left_mem_Icc.2 (neg_le_self pi_div_two_pos.le)

lemma arcsin_of_le_neg_one {x : ‚Ñù} (hx : x ‚â§ -1) : arcsin x = -(œÄ / 2) :=
by rw [‚Üê arcsin_proj_Icc, proj_Icc_of_le_left _ hx, subtype.coe_mk, arcsin_neg_one]

@[simp] lemma arcsin_neg (x : ‚Ñù) : arcsin (-x) = -arcsin x :=
begin
  cases le_total x (-1) with hx‚ÇÅ hx‚ÇÅ,
  { rw [arcsin_of_le_neg_one hx‚ÇÅ, neg_neg, arcsin_of_one_le (le_neg.2 hx‚ÇÅ)] },
  cases le_total 1 x with hx‚ÇÇ hx‚ÇÇ,
  { rw [arcsin_of_one_le hx‚ÇÇ, arcsin_of_le_neg_one (neg_le_neg hx‚ÇÇ)] },
  refine arcsin_eq_of_sin_eq _ _,
  { rw [sin_neg, sin_arcsin hx‚ÇÅ hx‚ÇÇ] },
  { exact ‚ü®neg_le_neg (arcsin_le_pi_div_two _), neg_le.2 (neg_pi_div_two_le_arcsin _)‚ü© }
end

lemma arcsin_le_iff_le_sin {x y : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) (hy : y ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
  arcsin x ‚â§ y ‚Üî x ‚â§ sin y :=
by rw [‚Üê arcsin_sin' hy, strict_mono_incr_on_arcsin.le_iff_le hx (sin_mem_Icc _), arcsin_sin' hy]

lemma arcsin_le_iff_le_sin' {x y : ‚Ñù} (hy : y ‚àà Ico (-(œÄ / 2)) (œÄ / 2)) :
  arcsin x ‚â§ y ‚Üî x ‚â§ sin y :=
begin
  cases le_total x (-1) with hx‚ÇÅ hx‚ÇÅ,
  { simp [arcsin_of_le_neg_one hx‚ÇÅ, hy.1, hx‚ÇÅ.trans (neg_one_le_sin _)] },
  cases lt_or_le 1 x with hx‚ÇÇ hx‚ÇÇ,
  { simp [arcsin_of_one_le hx‚ÇÇ.le, hy.2.not_le, (sin_le_one y).trans_lt hx‚ÇÇ] },
  exact arcsin_le_iff_le_sin ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© (mem_Icc_of_Ico hy)
end

lemma le_arcsin_iff_sin_le {x y : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) (hy : y ‚àà Icc (-1 : ‚Ñù) 1) :
  x ‚â§ arcsin y ‚Üî sin x ‚â§ y :=
by rw [‚Üê neg_le_neg_iff, ‚Üê arcsin_neg,
  arcsin_le_iff_le_sin ‚ü®neg_le_neg hy.2, neg_le.2 hy.1‚ü© ‚ü®neg_le_neg hx.2, neg_le.2 hx.1‚ü©,
  sin_neg, neg_le_neg_iff]

lemma le_arcsin_iff_sin_le' {x y : ‚Ñù} (hx : x ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)) :
  x ‚â§ arcsin y ‚Üî sin x ‚â§ y :=
by rw [‚Üê neg_le_neg_iff, ‚Üê arcsin_neg, arcsin_le_iff_le_sin' ‚ü®neg_le_neg hx.2, neg_lt.2 hx.1‚ü©,
  sin_neg, neg_le_neg_iff]

lemma arcsin_lt_iff_lt_sin {x y : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) (hy : y ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
  arcsin x < y ‚Üî x < sin y :=
not_le.symm.trans $ (not_congr $ le_arcsin_iff_sin_le hy hx).trans not_le

lemma arcsin_lt_iff_lt_sin' {x y : ‚Ñù} (hy : y ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)) :
  arcsin x < y ‚Üî x < sin y :=
not_le.symm.trans $ (not_congr $ le_arcsin_iff_sin_le' hy).trans not_le

lemma lt_arcsin_iff_sin_lt {x y : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) (hy : y ‚àà Icc (-1 : ‚Ñù) 1) :
  x < arcsin y ‚Üî sin x < y :=
not_le.symm.trans $ (not_congr $ arcsin_le_iff_le_sin hy hx).trans not_le

lemma lt_arcsin_iff_sin_lt' {x y : ‚Ñù} (hx : x ‚àà Ico (-(œÄ / 2)) (œÄ / 2)) :
  x < arcsin y ‚Üî sin x < y :=
not_le.symm.trans $ (not_congr $ arcsin_le_iff_le_sin' hx).trans not_le

lemma arcsin_eq_iff_eq_sin {x y : ‚Ñù} (hy : y ‚àà Ioo (-(œÄ / 2)) (œÄ / 2)) :
  arcsin x = y ‚Üî x = sin y :=
by simp only [le_antisymm_iff, arcsin_le_iff_le_sin' (mem_Ico_of_Ioo hy),
  le_arcsin_iff_sin_le' (mem_Ioc_of_Ioo hy)]

@[simp] lemma arcsin_nonneg {x : ‚Ñù} : 0 ‚â§ arcsin x ‚Üî 0 ‚â§ x :=
(le_arcsin_iff_sin_le' ‚ü®neg_lt_zero.2 pi_div_two_pos, pi_div_two_pos.le‚ü©).trans $ by rw [sin_zero]

@[simp] lemma arcsin_nonpos {x : ‚Ñù} : arcsin x ‚â§ 0 ‚Üî x ‚â§ 0 :=
neg_nonneg.symm.trans $ arcsin_neg x ‚ñ∏ arcsin_nonneg.trans neg_nonneg

@[simp] lemma arcsin_eq_zero_iff {x : ‚Ñù} : arcsin x = 0 ‚Üî x = 0 :=
by simp [le_antisymm_iff]

@[simp] lemma zero_eq_arcsin_iff {x} : 0 = arcsin x ‚Üî x = 0 :=
eq_comm.trans arcsin_eq_zero_iff

@[simp] lemma arcsin_pos {x : ‚Ñù} : 0 < arcsin x ‚Üî 0 < x :=
lt_iff_lt_of_le_iff_le arcsin_nonpos

@[simp] lemma arcsin_lt_zero {x : ‚Ñù} : arcsin x < 0 ‚Üî x < 0 :=
lt_iff_lt_of_le_iff_le arcsin_nonneg

@[simp] lemma arcsin_lt_pi_div_two {x : ‚Ñù} : arcsin x < œÄ / 2 ‚Üî x < 1 :=
(arcsin_lt_iff_lt_sin' (right_mem_Ioc.2 $ neg_lt_self pi_div_two_pos)).trans $
  by rw sin_pi_div_two

@[simp] lemma neg_pi_div_two_lt_arcsin {x : ‚Ñù} : -(œÄ / 2) < arcsin x ‚Üî -1 < x :=
(lt_arcsin_iff_sin_lt' $ left_mem_Ico.2 $ neg_lt_self pi_div_two_pos).trans $
  by rw [sin_neg, sin_pi_div_two]

@[simp] lemma arcsin_eq_pi_div_two {x : ‚Ñù} : arcsin x = œÄ / 2 ‚Üî 1 ‚â§ x :=
‚ü®Œª h, not_lt.1 $ Œª h', (arcsin_lt_pi_div_two.2 h').ne h, arcsin_of_one_le‚ü©

@[simp] lemma pi_div_two_eq_arcsin {x} : œÄ / 2 = arcsin x ‚Üî 1 ‚â§ x :=
eq_comm.trans arcsin_eq_pi_div_two

@[simp] lemma pi_div_two_le_arcsin {x} : œÄ / 2 ‚â§ arcsin x ‚Üî 1 ‚â§ x :=
(arcsin_le_pi_div_two x).le_iff_eq.trans pi_div_two_eq_arcsin

@[simp] lemma arcsin_eq_neg_pi_div_two {x : ‚Ñù} : arcsin x = -(œÄ / 2) ‚Üî x ‚â§ -1 :=
‚ü®Œª h, not_lt.1 $ Œª h', (neg_pi_div_two_lt_arcsin.2 h').ne' h, arcsin_of_le_neg_one‚ü©

@[simp] lemma neg_pi_div_two_eq_arcsin {x} : -(œÄ / 2) = arcsin x ‚Üî x ‚â§ -1 :=
eq_comm.trans arcsin_eq_neg_pi_div_two

@[simp] lemma arcsin_le_neg_pi_div_two {x} : arcsin x ‚â§ -(œÄ / 2) ‚Üî x ‚â§ -1 :=
(neg_pi_div_two_le_arcsin x).le_iff_eq.trans arcsin_eq_neg_pi_div_two

lemma maps_to_sin_Ioo : maps_to sin (Ioo (-(œÄ / 2)) (œÄ / 2)) (Ioo (-1) 1) :=
Œª x h, by rwa [mem_Ioo, ‚Üê arcsin_lt_pi_div_two, ‚Üê neg_pi_div_two_lt_arcsin,
  arcsin_sin h.1.le h.2.le]

/-- `real.sin` as a `local_homeomorph` between `(-œÄ / 2, œÄ / 2)` and `(-1, 1)`. -/
@[simp] def sin_local_homeomorph : local_homeomorph ‚Ñù ‚Ñù :=
{ to_fun := sin,
  inv_fun := arcsin,
  source := Ioo (-(œÄ / 2)) (œÄ / 2),
  target := Ioo (-1) 1,
  map_source' := maps_to_sin_Ioo,
  map_target' := Œª y hy, ‚ü®neg_pi_div_two_lt_arcsin.2 hy.1, arcsin_lt_pi_div_two.2 hy.2‚ü©,
  left_inv' := Œª x hx, arcsin_sin hx.1.le hx.2.le,
  right_inv' := Œª y hy, sin_arcsin hy.1.le hy.2.le,
  open_source := is_open_Ioo,
  open_target := is_open_Ioo,
  continuous_to_fun := continuous_sin.continuous_on,
  continuous_inv_fun := continuous_arcsin.continuous_on }

lemma cos_arcsin_nonneg (x : ‚Ñù) : 0 ‚â§ cos (arcsin x) :=
cos_nonneg_of_mem_Icc ‚ü®neg_pi_div_two_le_arcsin _, arcsin_le_pi_div_two _‚ü©

lemma cos_arcsin {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : cos (arcsin x) = sqrt (1 - x ^ 2) :=
have sin (arcsin x) ^ 2 + cos (arcsin x) ^ 2 = 1 := sin_sq_add_cos_sq (arcsin x),
begin
  rw [‚Üê eq_sub_iff_add_eq', ‚Üê sqrt_inj (sq_nonneg _) (sub_nonneg.2 (sin_sq_le_one (arcsin x))),
    sq, sqrt_mul_self (cos_arcsin_nonneg _)] at this,
  rw [this, sin_arcsin hx‚ÇÅ hx‚ÇÇ],
end

lemma deriv_arcsin_aux {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_strict_deriv_at arcsin (1 / sqrt (1 - x ^ 2)) x ‚àß times_cont_diff_at ‚Ñù ‚ä§ arcsin x :=
begin
  cases h‚ÇÅ.lt_or_lt with h‚ÇÅ h‚ÇÅ,
  { have : 1 - x ^ 2 < 0, by nlinarith [h‚ÇÅ],
    rw [sqrt_eq_zero'.2 this.le, div_zero],
    have : arcsin =·∂†[ùìù x] Œª _, -(œÄ / 2) :=
      (gt_mem_nhds h‚ÇÅ).mono (Œª y hy, arcsin_of_le_neg_one hy.le),
    exact ‚ü®(has_strict_deriv_at_const _ _).congr_of_eventually_eq this.symm,
      times_cont_diff_at_const.congr_of_eventually_eq this‚ü© },
  cases h‚ÇÇ.lt_or_lt with h‚ÇÇ h‚ÇÇ,
  { have : 0 < sqrt (1 - x ^ 2) := sqrt_pos.2 (by nlinarith [h‚ÇÅ, h‚ÇÇ]),
    simp only [‚Üê cos_arcsin h‚ÇÅ.le h‚ÇÇ.le, one_div] at this ‚ä¢,
    exact ‚ü®sin_local_homeomorph.has_strict_deriv_at_symm ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© this.ne'
      (has_strict_deriv_at_sin _),
      sin_local_homeomorph.times_cont_diff_at_symm_deriv this.ne' ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
        (has_deriv_at_sin _) times_cont_diff_sin.times_cont_diff_at‚ü© },
  { have : 1 - x ^ 2 < 0, by nlinarith [h‚ÇÇ],
    rw [sqrt_eq_zero'.2 this.le, div_zero],
    have : arcsin =·∂†[ùìù x] Œª _, œÄ / 2 := (lt_mem_nhds h‚ÇÇ).mono (Œª y hy, arcsin_of_one_le hy.le),
    exact ‚ü®(has_strict_deriv_at_const _ _).congr_of_eventually_eq this.symm,
      times_cont_diff_at_const.congr_of_eventually_eq this‚ü© }
end

lemma has_strict_deriv_at_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_strict_deriv_at arcsin (1 / sqrt (1 - x ^ 2)) x :=
(deriv_arcsin_aux h‚ÇÅ h‚ÇÇ).1

lemma has_deriv_at_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_deriv_at arcsin (1 / sqrt (1 - x ^ 2)) x :=
(has_strict_deriv_at_arcsin h‚ÇÅ h‚ÇÇ).has_deriv_at

lemma times_cont_diff_at_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arcsin x :=
(deriv_arcsin_aux h‚ÇÅ h‚ÇÇ).2.of_le le_top

lemma has_deriv_within_at_arcsin_Ici {x : ‚Ñù} (h : x ‚â† -1) :
  has_deriv_within_at arcsin (1 / sqrt (1 - x ^ 2)) (Ici x) x :=
begin
  rcases em (x = 1) with (rfl|h'),
  { convert (has_deriv_within_at_const _ _ (œÄ / 2)).congr _ _;
      simp [arcsin_of_one_le] { contextual := tt } },
  { exact (has_deriv_at_arcsin h h').has_deriv_within_at }
end

lemma has_deriv_within_at_arcsin_Iic {x : ‚Ñù} (h : x ‚â† 1) :
  has_deriv_within_at arcsin (1 / sqrt (1 - x ^ 2)) (Iic x) x :=
begin
  rcases em (x = -1) with (rfl|h'),
  { convert (has_deriv_within_at_const _ _ (-(œÄ / 2))).congr _ _;
      simp [arcsin_of_le_neg_one] { contextual := tt } },
  { exact (has_deriv_at_arcsin h' h).has_deriv_within_at }
end

lemma differentiable_within_at_arcsin_Ici {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arcsin (Ici x) x ‚Üî x ‚â† -1 :=
begin
  refine ‚ü®_, Œª h, (has_deriv_within_at_arcsin_Ici h).differentiable_within_at‚ü©,
  rintro h rfl,
  have : sin ‚àò arcsin =·∂†[ùìù[Ici (-1:‚Ñù)] (-1)] id,
  { filter_upwards [Icc_mem_nhds_within_Ici ‚ü®le_rfl, neg_lt_self (@zero_lt_one ‚Ñù _ _)‚ü©],
    exact Œª x, sin_arcsin' },
  have := h.has_deriv_within_at.sin.congr_of_eventually_eq this.symm (by simp),
  simpa using (unique_diff_on_Ici _ _ left_mem_Ici).eq_deriv _ this (has_deriv_within_at_id _ _)
end

lemma differentiable_within_at_arcsin_Iic {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arcsin (Iic x) x ‚Üî x ‚â† 1 :=
begin
  refine ‚ü®Œª h, _, Œª h, (has_deriv_within_at_arcsin_Iic h).differentiable_within_at‚ü©,
  rw [‚Üê neg_neg x, ‚Üê image_neg_Ici] at h,
  have := (h.comp (-x) differentiable_within_at_id.neg (maps_to_image _ _)).neg,
  simpa [(‚àò), differentiable_within_at_arcsin_Ici] using this
end

lemma differentiable_at_arcsin {x : ‚Ñù} :
  differentiable_at ‚Ñù arcsin x ‚Üî x ‚â† -1 ‚àß x ‚â† 1 :=
‚ü®Œª h, ‚ü®differentiable_within_at_arcsin_Ici.1 h.differentiable_within_at,
  differentiable_within_at_arcsin_Iic.1 h.differentiable_within_at‚ü©,
  Œª h, (has_deriv_at_arcsin h.1 h.2).differentiable_at‚ü©

@[simp] lemma deriv_arcsin : deriv arcsin = Œª x, 1 / sqrt (1 - x ^ 2) :=
begin
  funext x,
  by_cases h : x ‚â† -1 ‚àß x ‚â† 1,
  { exact (has_deriv_at_arcsin h.1 h.2).deriv },
  { rw [deriv_zero_of_not_differentiable_at (mt differentiable_at_arcsin.1 h)],
    simp only [not_and_distrib, ne.def, not_not] at h,
    rcases h with (rfl|rfl); simp }
end

lemma differentiable_on_arcsin : differentiable_on ‚Ñù arcsin {-1, 1}·∂ú :=
Œª x hx, (differentiable_at_arcsin.2
  ‚ü®Œª h, hx (or.inl h), Œª h, hx (or.inr h)‚ü©).differentiable_within_at

lemma times_cont_diff_on_arcsin {n : with_top ‚Ñï} :
  times_cont_diff_on ‚Ñù n arcsin {-1, 1}·∂ú :=
Œª x hx, (times_cont_diff_at_arcsin (mt or.inl hx) (mt or.inr hx)).times_cont_diff_within_at

lemma times_cont_diff_at_arcsin_iff {x : ‚Ñù} {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arcsin x ‚Üî n = 0 ‚à® (x ‚â† -1 ‚àß x ‚â† 1) :=
‚ü®Œª h, or_iff_not_imp_left.2 $ Œª hn, differentiable_at_arcsin.1 $ h.differentiable_at $
  with_top.one_le_iff_pos.2 (pos_iff_ne_zero.2 hn),
  Œª h, h.elim (Œª hn, hn.symm ‚ñ∏ (times_cont_diff_zero.2 continuous_arcsin).times_cont_diff_at) $
    Œª hx, times_cont_diff_at_arcsin hx.1 hx.2‚ü©

/-- Inverse of the `cos` function, returns values in the range `0 ‚â§ arccos x` and `arccos x ‚â§ œÄ`.
  If the argument is not between `-1` and `1` it defaults to `œÄ / 2` -/
@[pp_nodot] noncomputable def arccos (x : ‚Ñù) : ‚Ñù :=
œÄ / 2 - arcsin x

lemma arccos_eq_pi_div_two_sub_arcsin (x : ‚Ñù) : arccos x = œÄ / 2 - arcsin x := rfl

lemma arcsin_eq_pi_div_two_sub_arccos (x : ‚Ñù) : arcsin x = œÄ / 2 - arccos x :=
by simp [arccos]

lemma arccos_le_pi (x : ‚Ñù) : arccos x ‚â§ œÄ :=
by unfold arccos; linarith [neg_pi_div_two_le_arcsin x]

lemma arccos_nonneg (x : ‚Ñù) : 0 ‚â§ arccos x :=
by unfold arccos; linarith [arcsin_le_pi_div_two x]

lemma cos_arccos {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : cos (arccos x) = x :=
by rw [arccos, cos_pi_div_two_sub, sin_arcsin hx‚ÇÅ hx‚ÇÇ]

lemma arccos_cos {x : ‚Ñù} (hx‚ÇÅ : 0 ‚â§ x) (hx‚ÇÇ : x ‚â§ œÄ) : arccos (cos x) = x :=
by rw [arccos, ‚Üê sin_pi_div_two_sub, arcsin_sin]; simp [sub_eq_add_neg]; linarith

lemma strict_mono_decr_on_arccos : strict_mono_decr_on arccos (Icc (-1) 1) :=
Œª x hx y hy h, sub_lt_sub_left (strict_mono_incr_on_arcsin hx hy h) _

lemma arccos_inj_on : inj_on arccos (Icc (-1) 1) := strict_mono_decr_on_arccos.inj_on

lemma arccos_inj {x y : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) (hy‚ÇÅ : -1 ‚â§ y) (hy‚ÇÇ : y ‚â§ 1) :
  arccos x = arccos y ‚Üî x = y :=
arccos_inj_on.eq_iff ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©

@[simp] lemma arccos_zero : arccos 0 = œÄ / 2 := by simp [arccos]

@[simp] lemma arccos_one : arccos 1 = 0 := by simp [arccos]

@[simp] lemma arccos_neg_one : arccos (-1) = œÄ := by simp [arccos, add_halves]

@[simp] lemma arccos_eq_zero {x} : arccos x = 0 ‚Üî 1 ‚â§ x :=
by simp [arccos, sub_eq_zero]

@[simp] lemma arccos_eq_pi_div_two {x} : arccos x = œÄ / 2 ‚Üî x = 0 :=
by simp [arccos, sub_eq_iff_eq_add]

@[simp] lemma arccos_eq_pi {x} : arccos x = œÄ ‚Üî x ‚â§ -1 :=
by rw [arccos, sub_eq_iff_eq_add, ‚Üê sub_eq_iff_eq_add', div_two_sub_self, neg_pi_div_two_eq_arcsin]

lemma arccos_neg (x : ‚Ñù) : arccos (-x) = œÄ - arccos x :=
by rw [‚Üê add_halves œÄ, arccos, arcsin_neg, arccos, add_sub_assoc, sub_sub_self, sub_neg_eq_add]

lemma sin_arccos {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : sin (arccos x) = sqrt (1 - x ^ 2) :=
by rw [arccos_eq_pi_div_two_sub_arcsin, sin_pi_div_two_sub, cos_arcsin hx‚ÇÅ hx‚ÇÇ]

@[continuity]
lemma continuous_arccos : continuous arccos := continuous_const.sub continuous_arcsin

lemma has_strict_deriv_at_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_strict_deriv_at arccos (-(1 / sqrt (1 - x ^ 2))) x :=
(has_strict_deriv_at_arcsin h‚ÇÅ h‚ÇÇ).const_sub (œÄ / 2)

lemma has_deriv_at_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
  has_deriv_at arccos (-(1 / sqrt (1 - x ^ 2))) x :=
(has_deriv_at_arcsin h‚ÇÅ h‚ÇÇ).const_sub (œÄ / 2)

lemma times_cont_diff_at_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arccos x :=
times_cont_diff_at_const.sub (times_cont_diff_at_arcsin h‚ÇÅ h‚ÇÇ)

lemma has_deriv_within_at_arccos_Ici {x : ‚Ñù} (h : x ‚â† -1) :
  has_deriv_within_at arccos (-(1 / sqrt (1 - x ^ 2))) (Ici x) x :=
(has_deriv_within_at_arcsin_Ici h).const_sub _

lemma has_deriv_within_at_arccos_Iic {x : ‚Ñù} (h : x ‚â† 1) :
  has_deriv_within_at arccos (-(1 / sqrt (1 - x ^ 2))) (Iic x) x :=
(has_deriv_within_at_arcsin_Iic h).const_sub _

lemma differentiable_within_at_arccos_Ici {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arccos (Ici x) x ‚Üî x ‚â† -1 :=
(differentiable_within_at_const_sub_iff _).trans differentiable_within_at_arcsin_Ici

lemma differentiable_within_at_arccos_Iic {x : ‚Ñù} :
  differentiable_within_at ‚Ñù arccos (Iic x) x ‚Üî x ‚â† 1 :=
(differentiable_within_at_const_sub_iff _).trans differentiable_within_at_arcsin_Iic

lemma differentiable_at_arccos {x : ‚Ñù} :
  differentiable_at ‚Ñù arccos x ‚Üî x ‚â† -1 ‚àß x ‚â† 1 :=
(differentiable_at_const_sub_iff _).trans differentiable_at_arcsin

@[simp] lemma deriv_arccos : deriv arccos = Œª x, -(1 / sqrt (1 - x ^ 2)) :=
funext $ Œª x, (deriv_const_sub _).trans $ by simp only [deriv_arcsin]

lemma differentiable_on_arccos : differentiable_on ‚Ñù arccos {-1, 1}·∂ú :=
differentiable_on_arcsin.const_sub _

lemma times_cont_diff_on_arccos {n : with_top ‚Ñï} :
  times_cont_diff_on ‚Ñù n arccos {-1, 1}·∂ú :=
times_cont_diff_on_const.sub times_cont_diff_on_arcsin

lemma times_cont_diff_at_arccos_iff {x : ‚Ñù} {n : with_top ‚Ñï} :
  times_cont_diff_at ‚Ñù n arccos x ‚Üî n = 0 ‚à® (x ‚â† -1 ‚àß x ‚â† 1) :=
by refine iff.trans ‚ü®Œª h, _, Œª h, _‚ü© times_cont_diff_at_arcsin_iff;
  simpa [arccos] using (@times_cont_diff_at_const _ _ _ _ _ _ _ _ _ _ (œÄ / 2)).sub h

end real
