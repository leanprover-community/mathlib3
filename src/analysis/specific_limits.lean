/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl

A collection of specific limit computations.
-/
import analysis.normed_space.basic
import algebra.geom_sum
import topology.instances.ennreal
import tactic.ring_exp

noncomputable theory
open_locale classical topological_space

open classical function filter finset metric

open_locale big_operators

variables {Œ± : Type*} {Œ≤ : Type*} {Œπ : Type*}

lemma tendsto_norm_at_top_at_top : tendsto (norm : ‚Ñù ‚Üí ‚Ñù) at_top at_top :=
tendsto_abs_at_top_at_top

/-- If a function tends to infinity along a filter, then this function multiplied by a positive
constant (on the left) also tends to infinity. The archimedean assumption is convenient to get a
statement that works on `‚Ñï`, `‚Ñ§` and `‚Ñù`, although not necessary (a version in ordered fields is
given in `tendsto_at_top_mul_left'`). -/
lemma tendsto_at_top_mul_left [decidable_linear_ordered_semiring Œ±] [archimedean Œ±]
  {l : filter Œ≤} {r : Œ±} (hr : 0 < r) {f : Œ≤ ‚Üí Œ±} (hf : tendsto f l at_top) :
  tendsto (Œªx, r * f x) l at_top :=
begin
  apply (tendsto_at_top _ _).2 (Œªb, _),
  obtain ‚ü®n, hn‚ü© : ‚àÉ (n : ‚Ñï), (1 : Œ±) ‚â§ n ‚Ä¢ r := archimedean.arch 1 hr,
  have hn' : 1 ‚â§ r * n, by rwa nsmul_eq_mul' at hn,
  filter_upwards [(tendsto_at_top _ _).1 hf (n * max b 0)],
  assume x hx,
  calc b ‚â§ 1 * max b 0 : by { rw [one_mul], exact le_max_left _ _ }
  ... ‚â§ (r * n) * max b 0 : mul_le_mul_of_nonneg_right hn' (le_max_right _ _)
  ... = r * (n * max b 0) : by rw [mul_assoc]
  ... ‚â§ r * f x : mul_le_mul_of_nonneg_left hx (le_of_lt hr)
end

/-- If a function tends to infinity along a filter, then this function multiplied by a positive
constant (on the right) also tends to infinity. The archimedean assumption is convenient to get a
statement that works on `‚Ñï`, `‚Ñ§` and `‚Ñù`, although not necessary (a version in ordered fields is
given in `tendsto_at_top_mul_right'`). -/
lemma tendsto_at_top_mul_right [decidable_linear_ordered_semiring Œ±] [archimedean Œ±]
  {l : filter Œ≤} {r : Œ±} (hr : 0 < r) {f : Œ≤ ‚Üí Œ±} (hf : tendsto f l at_top) :
  tendsto (Œªx, f x * r) l at_top :=
begin
  apply (tendsto_at_top _ _).2 (Œªb, _),
  obtain ‚ü®n, hn‚ü© : ‚àÉ (n : ‚Ñï), (1 : Œ±) ‚â§ n ‚Ä¢ r := archimedean.arch 1 hr,
  have hn' : 1 ‚â§ (n : Œ±) * r, by rwa nsmul_eq_mul at hn,
  filter_upwards [(tendsto_at_top _ _).1 hf (max b 0 * n)],
  assume x hx,
  calc b ‚â§ max b 0 * 1 : by { rw [mul_one], exact le_max_left _ _ }
  ... ‚â§ max b 0 * (n * r) : mul_le_mul_of_nonneg_left hn' (le_max_right _ _)
  ... = (max b 0 * n) * r : by rw [mul_assoc]
  ... ‚â§ f x * r : mul_le_mul_of_nonneg_right hx (le_of_lt hr)
end

/-- If a function tends to infinity along a filter, then this function multiplied by a positive
constant (on the left) also tends to infinity. For a version working in `‚Ñï` or `‚Ñ§`, use
`tendsto_at_top_mul_left` instead. -/
lemma tendsto_at_top_mul_left' [linear_ordered_field Œ±]
  {l : filter Œ≤} {r : Œ±} (hr : 0 < r) {f : Œ≤ ‚Üí Œ±} (hf : tendsto f l at_top) :
  tendsto (Œªx, r * f x) l at_top :=
begin
  apply (tendsto_at_top _ _).2 (Œªb, _),
  filter_upwards [(tendsto_at_top _ _).1 hf (b/r)],
  assume x hx,
  simpa [div_le_iff' hr] using hx
end

/-- If a function tends to infinity along a filter, then this function multiplied by a positive
constant (on the right) also tends to infinity. For a version working in `‚Ñï` or `‚Ñ§`, use
`tendsto_at_top_mul_right` instead. -/
lemma tendsto_at_top_mul_right' [linear_ordered_field Œ±]
  {l : filter Œ≤} {r : Œ±} (hr : 0 < r) {f : Œ≤ ‚Üí Œ±} (hf : tendsto f l at_top) :
  tendsto (Œªx, f x * r) l at_top :=
by simpa [mul_comm] using tendsto_at_top_mul_left' hr hf

/-- If a function tends to infinity along a filter, then this function divided by a positive
constant also tends to infinity. -/
lemma tendsto_at_top_div [linear_ordered_field Œ±]
  {l : filter Œ≤} {r : Œ±} (hr : 0 < r) {f : Œ≤ ‚Üí Œ±} (hf : tendsto f l at_top) :
  tendsto (Œªx, f x / r) l at_top :=
tendsto_at_top_mul_right' (inv_pos.2 hr) hf

/-- The function `x ‚Ü¶ x‚Åª¬π` tends to `+‚àû` on the right of `0`. -/
lemma tendsto_inv_zero_at_top [discrete_linear_ordered_field Œ±] [topological_space Œ±]
  [order_topology Œ±] : tendsto (Œªx:Œ±, x‚Åª¬π) (nhds_within (0 : Œ±) (set.Ioi 0)) at_top :=
begin
  apply (tendsto_at_top _ _).2 (Œªb, _),
  refine mem_nhds_within_Ioi_iff_exists_Ioo_subset.2 ‚ü®(max b 1)‚Åª¬π, by simp [zero_lt_one], Œªx hx, _‚ü©,
  calc b ‚â§ max b 1 : le_max_left _ _
  ... ‚â§ x‚Åª¬π : begin
    apply (le_inv _ hx.1).2 (le_of_lt hx.2),
    exact lt_of_lt_of_le zero_lt_one (le_max_right _ _)
  end
end

/-- The function `r ‚Ü¶ r‚Åª¬π` tends to `0` on the right as `r ‚Üí +‚àû`. -/
lemma tendsto_inv_at_top_zero' [discrete_linear_ordered_field Œ±] [topological_space Œ±]
  [order_topology Œ±] : tendsto (Œªr:Œ±, r‚Åª¬π) at_top (nhds_within (0 : Œ±) (set.Ioi 0)) :=
begin
  assume s hs,
  rw mem_nhds_within_Ioi_iff_exists_Ioc_subset at hs,
  rcases hs with ‚ü®C, C0, hC‚ü©,
  change 0 < C at C0,
  refine filter.mem_map.2 (mem_sets_of_superset (mem_at_top C‚Åª¬π) (Œª x hx, hC _)),
  have : 0 < x, from lt_of_lt_of_le (inv_pos.2 C0) hx,
  exact ‚ü®inv_pos.2 this, (inv_le C0 this).1 hx‚ü©
end

lemma tendsto_inv_at_top_zero [discrete_linear_ordered_field Œ±] [topological_space Œ±]
  [order_topology Œ±] : tendsto (Œªr:Œ±, r‚Åª¬π) at_top (ùìù 0) :=
tendsto_le_right inf_le_left tendsto_inv_at_top_zero'

lemma summable_of_absolute_convergence_real {f : ‚Ñï ‚Üí ‚Ñù} :
  (‚àÉr, tendsto (Œªn, (‚àë i in range n, abs (f i))) at_top (ùìù r)) ‚Üí summable f
| ‚ü®r, hr‚ü© :=
  begin
    refine summable_of_summable_norm ‚ü®r, (has_sum_iff_tendsto_nat_of_nonneg _ _).2 _‚ü©,
    exact assume i, norm_nonneg _,
    simpa only using hr
  end

lemma tendsto_inverse_at_top_nhds_0_nat : tendsto (Œª n : ‚Ñï, (n : ‚Ñù)‚Åª¬π) at_top (ùìù 0) :=
tendsto_inv_at_top_zero.comp (tendsto_coe_nat_real_at_top_iff.2 tendsto_id)

lemma tendsto_const_div_at_top_nhds_0_nat (C : ‚Ñù) : tendsto (Œª n : ‚Ñï, C / n) at_top (ùìù 0) :=
by simpa only [mul_zero] using tendsto_const_nhds.mul tendsto_inverse_at_top_nhds_0_nat

lemma nnreal.tendsto_inverse_at_top_nhds_0_nat : tendsto (Œª n : ‚Ñï, (n : nnreal)‚Åª¬π) at_top (ùìù 0) :=
by { rw ‚Üê nnreal.tendsto_coe, convert tendsto_inverse_at_top_nhds_0_nat, simp }

lemma nnreal.tendsto_const_div_at_top_nhds_0_nat (C : nnreal) :
  tendsto (Œª n : ‚Ñï, C / n) at_top (ùìù 0) :=
by simpa using tendsto_const_nhds.mul nnreal.tendsto_inverse_at_top_nhds_0_nat

lemma tendsto_one_div_add_at_top_nhds_0_nat :
  tendsto (Œª n : ‚Ñï, 1 / ((n : ‚Ñù) + 1)) at_top (ùìù 0) :=
suffices tendsto (Œª n : ‚Ñï, 1 / (‚Üë(n + 1) : ‚Ñù)) at_top (ùìù 0), by simpa,
(tendsto_add_at_top_iff_nat 1).2 (tendsto_const_div_at_top_nhds_0_nat 1)

/-! ### Powers -/

lemma tendsto_add_one_pow_at_top_at_top_of_pos [linear_ordered_semiring Œ±] [archimedean Œ±] {r : Œ±}
  (h : 0 < r) :
  tendsto (Œª n:‚Ñï, (r + 1)^n) at_top at_top :=
tendsto_at_top_at_top_of_monotone' (Œª n m, pow_le_pow (le_add_of_nonneg_left' (le_of_lt h))) $
  not_bdd_above_iff.2 $ Œª x, set.exists_range_iff.2 $ add_one_pow_unbounded_of_pos _ h

lemma tendsto_pow_at_top_at_top_of_one_lt [linear_ordered_ring Œ±] [archimedean Œ±]
  {r : Œ±} (h : 1 < r) :
  tendsto (Œªn:‚Ñï, r ^ n) at_top at_top :=
sub_add_cancel r 1 ‚ñ∏ tendsto_add_one_pow_at_top_at_top_of_pos (sub_pos.2 h)

lemma nat.tendsto_pow_at_top_at_top_of_one_lt {m : ‚Ñï} (h : 1 < m) :
  tendsto (Œªn:‚Ñï, m ^ n) at_top at_top :=
begin
  simp only [‚Üê nat.pow_eq_pow],
  exact nat.sub_add_cancel (le_of_lt h) ‚ñ∏
    tendsto_add_one_pow_at_top_at_top_of_pos (nat.sub_pos_of_lt h)
end

lemma lim_norm_zero' {ùïú : Type*} [normed_group ùïú] :
  tendsto (norm : ùïú ‚Üí ‚Ñù) (nhds_within 0 {x | x ‚â† 0}) (nhds_within 0 (set.Ioi 0)) :=
lim_norm_zero.inf $ tendsto_principal_principal.2 $ Œª x hx, norm_pos_iff.2 hx

lemma normed_field.tendsto_norm_inverse_nhds_within_0_at_top {ùïú : Type*} [normed_field ùïú] :
  tendsto (Œª x:ùïú, ‚à•x‚Åª¬π‚à•) (nhds_within 0 {x | x ‚â† 0}) at_top :=
(tendsto_inv_zero_at_top.comp lim_norm_zero').congr $ Œª x, (normed_field.norm_inv x).symm

lemma tendsto_pow_at_top_nhds_0_of_lt_1 {r : ‚Ñù} (h‚ÇÅ : 0 ‚â§ r) (h‚ÇÇ : r < 1) :
  tendsto (Œªn:‚Ñï, r^n) at_top (ùìù 0) :=
by_cases
  (assume : r = 0, (tendsto_add_at_top_iff_nat 1).mp $ by simp [pow_succ, this, tendsto_const_nhds])
  (assume : r ‚â† 0,
    have tendsto (Œªn, (r‚Åª¬π ^ n)‚Åª¬π) at_top (ùìù 0),
      from tendsto_inv_at_top_zero.comp
        (tendsto_pow_at_top_at_top_of_one_lt $ one_lt_inv (lt_of_le_of_ne h‚ÇÅ this.symm) h‚ÇÇ),
    tendsto.congr' (univ_mem_sets' $ by simp *) this)

lemma geom_lt {u : ‚Ñï ‚Üí ‚Ñù} {k : ‚Ñù} (hk : 0 < k) {n : ‚Ñï} (h : ‚àÄ m ‚â§ n, k*u m < u (m + 1)) :
  k^(n + 1) *u 0 < u (n + 1) :=
begin
 induction n with n ih,
 { simpa using h 0 (le_refl _) },
 have : (‚àÄ (m : ‚Ñï), m ‚â§ n ‚Üí k * u m < u (m + 1)),
   intros m hm, apply h, exact nat.le_succ_of_le hm,
 specialize ih this,
 change k ^ (n + 2) * u 0 < u (n + 2),
 replace h : k * u (n + 1) < u (n + 2) := h (n+1) (le_refl _),
 calc k ^ (n + 2) * u 0 = k*(k ^ (n + 1) * u 0) : by ring_exp
  ... < k*(u (n + 1)) : mul_lt_mul_of_pos_left ih hk
  ... < u (n + 2) : h,
end

/-- If a sequence `v` of real numbers satisfies `k*v n < v (n+1)` with `1 < k`,
then it goes to +‚àû. -/
lemma tendsto_at_top_of_geom_lt {v : ‚Ñï ‚Üí ‚Ñù} {k : ‚Ñù} (h‚ÇÄ : 0 < v 0) (hk : 1 < k)
  (hu : ‚àÄ n, k*v n < v (n+1)) : tendsto v at_top at_top :=
begin
  apply tendsto_at_top_mono,
  show ‚àÄ n, k^n*v 0 ‚â§ v n,
  { intro n,
    induction n with n ih,
    { simp },
    calc
    k ^ (n + 1) * v 0 = k*(k^n*v 0) : by ring_exp
                  ... ‚â§ k*v n       : mul_le_mul_of_nonneg_left ih (by linarith)
                  ... ‚â§ v (n + 1)   : le_of_lt (hu n) },
  apply tendsto_at_top_mul_right h‚ÇÄ,
  exact tendsto_pow_at_top_at_top_of_one_lt hk,
end

lemma nnreal.tendsto_pow_at_top_nhds_0_of_lt_1 {r : nnreal} (hr : r < 1) :
  tendsto (Œª n:‚Ñï, r^n) at_top (ùìù 0) :=
nnreal.tendsto_coe.1 $ by simp only [nnreal.coe_pow, nnreal.coe_zero,
  tendsto_pow_at_top_nhds_0_of_lt_1 r.coe_nonneg hr]

lemma ennreal.tendsto_pow_at_top_nhds_0_of_lt_1 {r : ennreal} (hr : r < 1) :
  tendsto (Œª n:‚Ñï, r^n) at_top (ùìù 0) :=
begin
  rcases ennreal.lt_iff_exists_coe.1 hr with ‚ü®r, rfl, hr'‚ü©,
  rw [‚Üê ennreal.coe_zero],
  norm_cast at *,
  apply nnreal.tendsto_pow_at_top_nhds_0_of_lt_1 hr
end

lemma tendsto_pow_at_top_nhds_0_of_norm_lt_1 {K : Type*} [normed_field K] {Œæ : K}
  (_ : ‚à•Œæ‚à• < 1) : tendsto (Œª n : ‚Ñï, Œæ^n) at_top (ùìù 0) :=
begin
  rw [tendsto_iff_norm_tendsto_zero],
  convert tendsto_pow_at_top_nhds_0_of_lt_1 (norm_nonneg Œæ) ‚Äπ‚à•Œæ‚à• < 1‚Ä∫,
  ext n,
  simp
end

lemma tendsto_pow_at_top_nhds_0_of_abs_lt_1 {r : ‚Ñù} (h : abs r < 1) :
  tendsto (Œªn:‚Ñï, r^n) at_top (ùìù 0) :=
tendsto_pow_at_top_nhds_0_of_norm_lt_1 h

/-! ### Geometric series-/
section geometric

lemma has_sum_geometric_of_lt_1 {r : ‚Ñù} (h‚ÇÅ : 0 ‚â§ r) (h‚ÇÇ : r < 1) :
  has_sum (Œªn:‚Ñï, r ^ n) (1 - r)‚Åª¬π :=
have r ‚â† 1, from ne_of_lt h‚ÇÇ,
have r + -1 ‚â† 0,
  by rw [‚Üêsub_eq_add_neg, ne, sub_eq_iff_eq_add]; simp; assumption,
have tendsto (Œªn, (r ^ n - 1) * (r - 1)‚Åª¬π) at_top (ùìù ((0 - 1) * (r - 1)‚Åª¬π)),
  from ((tendsto_pow_at_top_nhds_0_of_lt_1 h‚ÇÅ h‚ÇÇ).sub tendsto_const_nhds).mul tendsto_const_nhds,
have (Œª n, (‚àë i in range n, r ^ i)) = (Œª n, geom_series r n) := rfl,
(has_sum_iff_tendsto_nat_of_nonneg (pow_nonneg h‚ÇÅ) _).mpr $
  by simp [neg_inv, geom_sum, div_eq_mul_inv, *] at *

lemma summable_geometric_of_lt_1 {r : ‚Ñù} (h‚ÇÅ : 0 ‚â§ r) (h‚ÇÇ : r < 1) : summable (Œªn:‚Ñï, r ^ n) :=
‚ü®_, has_sum_geometric_of_lt_1 h‚ÇÅ h‚ÇÇ‚ü©

lemma tsum_geometric_of_lt_1 {r : ‚Ñù} (h‚ÇÅ : 0 ‚â§ r) (h‚ÇÇ : r < 1) : (‚àë'n:‚Ñï, r ^ n) = (1 - r)‚Åª¬π :=
tsum_eq_has_sum (has_sum_geometric_of_lt_1 h‚ÇÅ h‚ÇÇ)

lemma has_sum_geometric_two : has_sum (Œªn:‚Ñï, ((1:‚Ñù)/2) ^ n) 2 :=
by convert has_sum_geometric_of_lt_1 _ _; norm_num

lemma summable_geometric_two : summable (Œªn:‚Ñï, ((1:‚Ñù)/2) ^ n) :=
‚ü®_, has_sum_geometric_two‚ü©

lemma tsum_geometric_two : (‚àë'n:‚Ñï, ((1:‚Ñù)/2) ^ n) = 2 :=
tsum_eq_has_sum has_sum_geometric_two

lemma sum_geometric_two_le (n : ‚Ñï) : ‚àë (i : ‚Ñï) in range n, (1 / (2 : ‚Ñù)) ^ i ‚â§ 2 :=
begin
  have : ‚àÄ i, 0 ‚â§ (1 / (2 : ‚Ñù)) ^ i,
  { intro i, apply pow_nonneg, norm_num },
  convert sum_le_tsum (range n) (Œª i _, this i) summable_geometric_two,
  exact tsum_geometric_two.symm
end

lemma has_sum_geometric_two' (a : ‚Ñù) : has_sum (Œªn:‚Ñï, (a / 2) / 2 ^ n) a :=
begin
  convert has_sum.mul_left (a / 2) (has_sum_geometric_of_lt_1
    (le_of_lt one_half_pos) one_half_lt_one),
  { funext n, simp, refl, },
  { norm_num }
end

lemma summable_geometric_two' (a : ‚Ñù) : summable (Œª n:‚Ñï, (a / 2) / 2 ^ n) :=
‚ü®a, has_sum_geometric_two' a‚ü©

lemma tsum_geometric_two' (a : ‚Ñù) : (‚àë' n:‚Ñï, (a / 2) / 2^n) = a :=
tsum_eq_has_sum $ has_sum_geometric_two' a

lemma nnreal.has_sum_geometric {r : nnreal} (hr : r < 1) :
  has_sum (Œª n : ‚Ñï, r ^ n) (1 - r)‚Åª¬π :=
begin
  apply nnreal.has_sum_coe.1,
  push_cast,
  rw [nnreal.coe_sub (le_of_lt hr)],
  exact has_sum_geometric_of_lt_1 r.coe_nonneg hr
end

lemma nnreal.summable_geometric {r : nnreal} (hr : r < 1) : summable (Œªn:‚Ñï, r ^ n) :=
‚ü®_, nnreal.has_sum_geometric hr‚ü©

lemma tsum_geometric_nnreal {r : nnreal} (hr : r < 1) : (‚àë'n:‚Ñï, r ^ n) = (1 - r)‚Åª¬π :=
tsum_eq_has_sum (nnreal.has_sum_geometric hr)

/-- The series `pow r` converges to `(1-r)‚Åª¬π`. For `r < 1` the RHS is a finite number,
and for `1 ‚â§ r` the RHS equals `‚àû`. -/
lemma ennreal.tsum_geometric (r : ennreal) : (‚àë'n:‚Ñï, r ^ n) = (1 - r)‚Åª¬π :=
begin
  cases lt_or_le r 1 with hr hr,
  { rcases ennreal.lt_iff_exists_coe.1 hr with ‚ü®r, rfl, hr'‚ü©,
    norm_cast at *,
    convert ennreal.tsum_coe_eq (nnreal.has_sum_geometric hr),
    rw [ennreal.coe_inv $ ne_of_gt $ nnreal.sub_pos.2 hr] },
  { rw [ennreal.sub_eq_zero_of_le hr, ennreal.inv_zero, ennreal.tsum_eq_supr_nat, supr_eq_top],
    refine Œª a ha, (ennreal.exists_nat_gt (lt_top_iff_ne_top.1 ha)).imp
      (Œª n hn, lt_of_lt_of_le hn _),
    have : ‚àÄ k:‚Ñï, 1 ‚â§ r^k, by simpa using canonically_ordered_semiring.pow_le_pow_of_le_left hr,
    calc (n:ennreal) = (‚àë i in range n, 1) : by rw [sum_const, nsmul_one, card_range]
    ... ‚â§ ‚àë i in range n, r ^ i : sum_le_sum (Œª k _, this k) }
end

variables {K : Type*} [normed_field K] {Œæ : K}

lemma has_sum_geometric_of_norm_lt_1 (h : ‚à•Œæ‚à• < 1) : has_sum (Œªn:‚Ñï, Œæ ^ n) (1 - Œæ)‚Åª¬π :=
begin
  have xi_ne_one : Œæ ‚â† 1, by { contrapose! h, simp [h] },
  have A : tendsto (Œªn, (Œæ ^ n - 1) * (Œæ - 1)‚Åª¬π) at_top (ùìù ((0 - 1) * (Œæ - 1)‚Åª¬π)),
    from ((tendsto_pow_at_top_nhds_0_of_norm_lt_1 h).sub tendsto_const_nhds).mul tendsto_const_nhds,
  have B : (Œª n, (‚àë i in range n, Œæ ^ i)) = (Œª n, geom_series Œæ n) := rfl,
  rw [has_sum_iff_tendsto_nat_of_summable_norm, B],
  { simpa [geom_sum, xi_ne_one, neg_inv] using A },
  { simp [normed_field.norm_pow, summable_geometric_of_lt_1 (norm_nonneg _) h] }
end

lemma summable_geometric_of_norm_lt_1 (h : ‚à•Œæ‚à• < 1) : summable (Œªn:‚Ñï, Œæ ^ n) :=
‚ü®_, has_sum_geometric_of_norm_lt_1 h‚ü©

lemma tsum_geometric_of_norm_lt_1 (h : ‚à•Œæ‚à• < 1) : (‚àë'n:‚Ñï, Œæ ^ n) = (1 - Œæ)‚Åª¬π :=
tsum_eq_has_sum (has_sum_geometric_of_norm_lt_1 h)

lemma has_sum_geometric_of_abs_lt_1 {r : ‚Ñù} (h : abs r < 1) : has_sum (Œªn:‚Ñï, r ^ n) (1 - r)‚Åª¬π :=
has_sum_geometric_of_norm_lt_1 h

lemma summable_geometric_of_abs_lt_1 {r : ‚Ñù} (h : abs r < 1) : summable (Œªn:‚Ñï, r ^ n) :=
summable_geometric_of_norm_lt_1 h

lemma tsum_geometric_of_abs_lt_1 {r : ‚Ñù} (h : abs r < 1) : (‚àë'n:‚Ñï, r ^ n) = (1 - r)‚Åª¬π :=
tsum_geometric_of_norm_lt_1 h

end geometric

/-!
### Sequences with geometrically decaying distance in metric spaces

In this paragraph, we discuss sequences in metric spaces or emetric spaces for which the distance
between two consecutive terms decays geometrically. We show that such sequences are Cauchy
sequences, and bound their distances to the limit. We also discuss series with geometrically
decaying terms.
-/
section edist_le_geometric

variables [emetric_space Œ±] (r C : ennreal) (hr : r < 1) (hC : C ‚â† ‚ä§) {f : ‚Ñï ‚Üí Œ±}
  (hu : ‚àÄn, edist (f n) (f (n+1)) ‚â§ C * r^n)

include hr hC hu

/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, `C ‚â† ‚àû`, `r < 1`,
then `f` is a Cauchy sequence.-/
lemma cauchy_seq_of_edist_le_geometric : cauchy_seq f :=
begin
  refine cauchy_seq_of_edist_le_of_tsum_ne_top _ hu _,
  rw [ennreal.tsum_mul_left, ennreal.tsum_geometric],
  refine ennreal.mul_ne_top hC (ennreal.inv_ne_top.2 _),
  exact ne_of_gt (ennreal.zero_lt_sub_iff_lt.2 hr)
end

omit hr hC

/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, then the distance from
`f n` to the limit of `f` is bounded above by `C * r^n / (1 - r)`. -/
lemma edist_le_of_edist_le_geometric_of_tendsto {a : Œ±} (ha : tendsto f at_top (ùìù a)) (n : ‚Ñï) :
  edist (f n) a ‚â§ (C * r^n) / (1 - r) :=
begin
  convert edist_le_tsum_of_edist_le_of_tendsto _ hu ha _,
  simp only [pow_add, ennreal.tsum_mul_left, ennreal.tsum_geometric, ennreal.div_def, mul_assoc]
end

/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, then the distance from
`f 0` to the limit of `f` is bounded above by `C / (1 - r)`. -/
lemma edist_le_of_edist_le_geometric_of_tendsto‚ÇÄ {a : Œ±} (ha : tendsto f at_top (ùìù a)) :
  edist (f 0) a ‚â§ C / (1 - r) :=
by simpa only [pow_zero, mul_one] using edist_le_of_edist_le_geometric_of_tendsto r C hu ha 0

end edist_le_geometric

section edist_le_geometric_two

variables [emetric_space Œ±] (C : ennreal) (hC : C ‚â† ‚ä§) {f : ‚Ñï ‚Üí Œ±}
  (hu : ‚àÄn, edist (f n) (f (n+1)) ‚â§ C / 2^n) {a : Œ±} (ha : tendsto f at_top (ùìù a))

include hC hu

/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then `f` is a Cauchy sequence.-/
lemma cauchy_seq_of_edist_le_geometric_two : cauchy_seq f :=
begin
  simp only [ennreal.div_def, ennreal.inv_pow] at hu,
  refine cauchy_seq_of_edist_le_geometric 2‚Åª¬π C _ hC hu,
  simp [ennreal.one_lt_two]
end

omit hC
include ha

/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then the distance from
`f n` to the limit of `f` is bounded above by `2 * C * 2^-n`. -/
lemma edist_le_of_edist_le_geometric_two_of_tendsto (n : ‚Ñï) :
  edist (f n) a ‚â§ 2 * C / 2^n :=
begin
  simp only [ennreal.div_def, ennreal.inv_pow] at hu,
  rw [ennreal.div_def, mul_assoc, mul_comm, ennreal.inv_pow],
  convert edist_le_of_edist_le_geometric_of_tendsto 2‚Åª¬π C hu ha n,
  rw [ennreal.one_sub_inv_two, ennreal.inv_inv]
end

/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then the distance from
`f 0` to the limit of `f` is bounded above by `2 * C`. -/
lemma edist_le_of_edist_le_geometric_two_of_tendsto‚ÇÄ: edist (f 0) a ‚â§ 2 * C :=
by simpa only [pow_zero, ennreal.div_def, ennreal.inv_one, mul_one]
  using edist_le_of_edist_le_geometric_two_of_tendsto C hu ha 0

end edist_le_geometric_two

section le_geometric

variables [metric_space Œ±] {r C : ‚Ñù} (hr : r < 1) {f : ‚Ñï ‚Üí Œ±}
  (hu : ‚àÄn, dist (f n) (f (n+1)) ‚â§ C * r^n)

include hr hu

lemma aux_has_sum_of_le_geometric : has_sum (Œª n : ‚Ñï, C * r^n) (C / (1 - r)) :=
begin
  have h0 : 0 ‚â§ C,
    by simpa using le_trans dist_nonneg (hu 0),
  rcases eq_or_lt_of_le h0 with rfl | Cpos,
  { simp [has_sum_zero] },
  { have rnonneg: r ‚â• 0, from nonneg_of_mul_nonneg_left
      (by simpa only [pow_one] using le_trans dist_nonneg (hu 1)) Cpos,
    refine has_sum.mul_left C _,
    by simpa using has_sum_geometric_of_lt_1 rnonneg hr }
end

variables (r C)

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then `f` is a Cauchy sequence.
Note that this lemma does not assume `0 ‚â§ C` or `0 ‚â§ r`. -/
lemma cauchy_seq_of_le_geometric : cauchy_seq f :=
cauchy_seq_of_dist_le_of_summable _ hu ‚ü®_, aux_has_sum_of_le_geometric hr hu‚ü©

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then the distance from
`f n` to the limit of `f` is bounded above by `C * r^n / (1 - r)`. -/
lemma dist_le_of_le_geometric_of_tendsto‚ÇÄ {a : Œ±} (ha : tendsto f at_top (ùìù a)) :
  dist (f 0) a ‚â§ C / (1 - r) :=
(tsum_eq_has_sum $ aux_has_sum_of_le_geometric hr hu) ‚ñ∏
  dist_le_tsum_of_dist_le_of_tendsto‚ÇÄ _ hu ‚ü®_, aux_has_sum_of_le_geometric hr hu‚ü© ha

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then the distance from
`f 0` to the limit of `f` is bounded above by `C / (1 - r)`. -/
lemma dist_le_of_le_geometric_of_tendsto {a : Œ±} (ha : tendsto f at_top (ùìù a)) (n : ‚Ñï) :
  dist (f n) a ‚â§ (C * r^n) / (1 - r) :=
begin
  have := aux_has_sum_of_le_geometric hr hu,
  convert dist_le_tsum_of_dist_le_of_tendsto _ hu ‚ü®_, this‚ü© ha n,
  simp only [pow_add, mul_left_comm C, mul_div_right_comm],
  rw [mul_comm],
  exact (eq.symm $ tsum_eq_has_sum $ this.mul_left _)
end

omit hr hu

variable (hu‚ÇÇ : ‚àÄ n, dist (f n) (f (n+1)) ‚â§ (C / 2) / 2^n)

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then `f` is a Cauchy sequence. -/
lemma cauchy_seq_of_le_geometric_two : cauchy_seq f :=
cauchy_seq_of_dist_le_of_summable _ hu‚ÇÇ $ ‚ü®_, has_sum_geometric_two' C‚ü©

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then the distance from
`f 0` to the limit of `f` is bounded above by `C`. -/
lemma dist_le_of_le_geometric_two_of_tendsto‚ÇÄ {a : Œ±} (ha : tendsto f at_top (ùìù a)) :
  dist (f 0) a ‚â§ C :=
(tsum_geometric_two' C) ‚ñ∏ dist_le_tsum_of_dist_le_of_tendsto‚ÇÄ _ hu‚ÇÇ (summable_geometric_two' C) ha

include hu‚ÇÇ

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then the distance from
`f n` to the limit of `f` is bounded above by `C / 2^n`. -/
lemma dist_le_of_le_geometric_two_of_tendsto {a : Œ±} (ha : tendsto f at_top (ùìù a)) (n : ‚Ñï) :
  dist (f n) a ‚â§ C / 2^n :=
begin
  convert dist_le_tsum_of_dist_le_of_tendsto _ hu‚ÇÇ (summable_geometric_two' C) ha n,
  simp only [add_comm n, pow_add, (div_div_eq_div_mul _ _ _).symm],
  symmetry,
  exact tsum_eq_has_sum (has_sum.mul_right _ $ has_sum_geometric_two' C)
end

end le_geometric

section summable_le_geometric

variables [normed_group Œ±] {r C : ‚Ñù} {f : ‚Ñï ‚Üí Œ±}

lemma dist_partial_sum_le_of_le_geometric (hf : ‚àÄn, ‚à•f n‚à• ‚â§ C * r^n) (n : ‚Ñï) :
  dist (‚àë i in range n, f i) (‚àë i in range (n+1), f i) ‚â§ C * r ^ n :=
begin
  rw [sum_range_succ, dist_eq_norm, ‚Üê norm_neg],
  convert hf n,
  rw [neg_sub, add_sub_cancel]
end

/-- If `‚à•f n‚à• ‚â§ C * r ^ n` for all `n : ‚Ñï` and some `r < 1`, then the partial sums of `f` form a
Cauchy sequence. This lemma does not assume `0 ‚â§ r` or `0 ‚â§ C`. -/
lemma cauchy_seq_finset_of_geometric_bound (hr : r < 1) (hf : ‚àÄn, ‚à•f n‚à• ‚â§ C * r^n) :
  cauchy_seq (Œª s : finset (‚Ñï), ‚àë x in s, f x) :=
cauchy_seq_finset_of_norm_bounded _
  (aux_has_sum_of_le_geometric hr (dist_partial_sum_le_of_le_geometric hf)).summable hf

/-- If `‚à•f n‚à• ‚â§ C * r ^ n` for all `n : ‚Ñï` and some `r < 1`, then the partial sums of `f` are within
distance `C * r ^ n / (1 - r)` of the sum of the series. This lemma does not assume `0 ‚â§ r` or
`0 ‚â§ C`. -/
lemma norm_sub_le_of_geometric_bound_of_has_sum (hr : r < 1) (hf : ‚àÄn, ‚à•f n‚à• ‚â§ C * r^n)
  {a : Œ±} (ha : has_sum f a) (n : ‚Ñï) :
  ‚à•(‚àë x in finset.range n, f x) - a‚à• ‚â§ (C * r ^ n) / (1 - r) :=
begin
  rw ‚Üê dist_eq_norm,
  apply dist_le_of_le_geometric_of_tendsto r C hr (dist_partial_sum_le_of_le_geometric hf),
  exact ha.tendsto_sum_nat
end

end summable_le_geometric

/-! ### Positive sequences with small sums on encodable types -/

/-- For any positive `Œµ`, define on an encodable type a positive sequence with sum less than `Œµ` -/
def pos_sum_of_encodable {Œµ : ‚Ñù} (hŒµ : 0 < Œµ)
  (Œπ) [encodable Œπ] : {Œµ' : Œπ ‚Üí ‚Ñù // (‚àÄ i, 0 < Œµ' i) ‚àß ‚àÉ c, has_sum Œµ' c ‚àß c ‚â§ Œµ} :=
begin
  let f := Œª n, (Œµ / 2) / 2 ^ n,
  have hf : has_sum f Œµ := has_sum_geometric_two' _,
  have f0 : ‚àÄ n, 0 < f n := Œª n, div_pos (half_pos hŒµ) (pow_pos two_pos _),
  refine ‚ü®f ‚àò encodable.encode, Œª i, f0 _, _‚ü©,
  rcases hf.summable.summable_comp_of_injective (@encodable.encode_injective Œπ _)
    with ‚ü®c, hg‚ü©,
  refine ‚ü®c, hg, has_sum_le_inj _ (@encodable.encode_injective Œπ _) _ _ hg hf‚ü©,
  { assume i _, exact le_of_lt (f0 _) },
  { assume n, exact le_refl _ }
end

namespace nnreal

theorem exists_pos_sum_of_encodable {Œµ : nnreal} (hŒµ : 0 < Œµ) (Œπ) [encodable Œπ] :
  ‚àÉ Œµ' : Œπ ‚Üí nnreal, (‚àÄ i, 0 < Œµ' i) ‚àß ‚àÉc, has_sum Œµ' c ‚àß c < Œµ :=
let ‚ü®a, a0, aŒµ‚ü© := dense hŒµ in
let ‚ü®Œµ', hŒµ', c, hc, hcŒµ‚ü© := pos_sum_of_encodable a0 Œπ in
‚ü® Œªi, ‚ü®Œµ' i, le_of_lt $ hŒµ' i‚ü©, assume i, nnreal.coe_lt_coe.2 $ hŒµ' i,
  ‚ü®c, has_sum_le (assume i, le_of_lt $ hŒµ' i) has_sum_zero hc ‚ü©, nnreal.has_sum_coe.1 hc,
   lt_of_le_of_lt (nnreal.coe_le_coe.1 hcŒµ) aŒµ ‚ü©

end nnreal

namespace ennreal

theorem exists_pos_sum_of_encodable {Œµ : ennreal} (hŒµ : 0 < Œµ) (Œπ) [encodable Œπ] :
  ‚àÉ Œµ' : Œπ ‚Üí nnreal, (‚àÄ i, 0 < Œµ' i) ‚àß (‚àë' i, (Œµ' i : ennreal)) < Œµ :=
begin
  rcases dense hŒµ with ‚ü®r, h0r, hrŒµ‚ü©,
  rcases lt_iff_exists_coe.1 hrŒµ with ‚ü®x, rfl, hx‚ü©,
  rcases nnreal.exists_pos_sum_of_encodable (coe_lt_coe.1 h0r) Œπ with ‚ü®Œµ', hp, c, hc, hcr‚ü©,
  exact ‚ü®Œµ', hp, (ennreal.tsum_coe_eq hc).symm ‚ñ∏ lt_trans (coe_lt_coe.2 hcr) hrŒµ‚ü©
end

end ennreal

/-!
### Harmonic series

Here we define the harmonic series and prove some basic lemmas about it,
leading to a proof of its divergence to +‚àû -/

/-- The harmonic series `1 + 1/2 + 1/3 + ... + 1/n`-/
def harmonic_series (n : ‚Ñï) : ‚Ñù :=
‚àë i in range n, 1/(i+1 : ‚Ñù)

lemma mono_harmonic : monotone harmonic_series :=
begin
  intros p q hpq,
  apply sum_le_sum_of_subset_of_nonneg,
  rwa range_subset,
  intros x h _,
  exact le_of_lt nat.one_div_pos_of_nat,
end

lemma half_le_harmonic_double_sub_harmonic (n : ‚Ñï) (hn : 0 < n) :
  1/2 ‚â§ harmonic_series (2*n) - harmonic_series n :=
begin
  suffices : harmonic_series n + 1 / 2 ‚â§ harmonic_series (n + n),
  { rw two_mul,
    linarith },
  have : harmonic_series n + ‚àë k in Ico n (n + n), 1/(k + 1 : ‚Ñù) = harmonic_series (n + n) :=
    sum_range_add_sum_Ico _ (show n ‚â§ n+n, by linarith),
  rw [‚Üê this,  add_le_add_iff_left],
  have : ‚àë k in Ico n (n + n), 1/(n+n : ‚Ñù) = 1/2,
  { have : (n : ‚Ñù) + n ‚â† 0,
    { norm_cast, linarith },
    rw [sum_const, Ico.card],
    field_simp [this],
    ring },
  rw ‚Üê this,
  apply sum_le_sum,
  intros x hx,
  rw one_div_le_one_div,
  { exact_mod_cast nat.succ_le_of_lt (Ico.mem.mp hx).2 },
  { norm_cast, linarith },
  { exact_mod_cast nat.zero_lt_succ x }
end

lemma self_div_two_le_harmonic_two_pow (n : ‚Ñï) : (n / 2 : ‚Ñù) ‚â§ harmonic_series (2^n) :=
begin
  induction n with n hn,
  unfold harmonic_series,
  simp only [one_div_eq_inv, nat.cast_zero, euclidean_domain.zero_div, nat.cast_succ, sum_singleton,
    inv_one, zero_add, nat.pow_zero, range_one, zero_le_one],
  have : harmonic_series (2^n) + 1 / 2 ‚â§ harmonic_series (2^(n+1)),
  { have := half_le_harmonic_double_sub_harmonic (2^n) (by {apply nat.pow_pos, linarith}),
    rw [nat.mul_comm, ‚Üê nat.pow_succ] at this,
    linarith },
  apply le_trans _ this,
  rw (show (n.succ / 2 : ‚Ñù) = (n/2 : ‚Ñù) + (1/2), by field_simp),
  linarith,
end

/-- The harmonic series diverges to +‚àû -/
theorem harmonic_tendsto_at_top : tendsto harmonic_series at_top at_top :=
begin
  suffices : tendsto (Œª n : ‚Ñï, harmonic_series (2^n)) at_top at_top, by
  { exact tendsto_at_top_of_monotone_of_subseq mono_harmonic at_top_ne_bot this },
  apply tendsto_at_top_mono self_div_two_le_harmonic_two_pow,
  apply tendsto_at_top_div,
  norm_num,
  exact tendsto_coe_nat_real_at_top_at_top
end
