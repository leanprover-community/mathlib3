/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Johannes HÃ¶lzl, Yury G. Kudryashov, Patrick Massot
-/
import algebra.geom_sum
import order.filter.archimedean
import order.iterate
import topology.instances.ennreal

/-!
# A collection of specific limit computations

This file, by design, is independent of `normed_space` in the import hierarchy.  It contains
important specific limit computations in metric spaces, in ordered rings/fields, and in specific
instances of these such as `â„`, `â„â‰¥0` and `â„â‰¥0âˆ`.
-/

noncomputable theory
open classical set function filter finset metric

open_locale classical topological_space nat big_operators uniformity nnreal ennreal

variables {Î± : Type*} {Î² : Type*} {Î¹ : Type*}

lemma tendsto_inverse_at_top_nhds_0_nat : tendsto (Î» n : â„•, (n : â„)â»Â¹) at_top (ğ“ 0) :=
tendsto_inv_at_top_zero.comp tendsto_coe_nat_at_top_at_top

lemma tendsto_const_div_at_top_nhds_0_nat (C : â„) : tendsto (Î» n : â„•, C / n) at_top (ğ“ 0) :=
by simpa only [mul_zero] using tendsto_const_nhds.mul tendsto_inverse_at_top_nhds_0_nat

lemma nnreal.tendsto_inverse_at_top_nhds_0_nat : tendsto (Î» n : â„•, (n : â„â‰¥0)â»Â¹) at_top (ğ“ 0) :=
by { rw â† nnreal.tendsto_coe, convert tendsto_inverse_at_top_nhds_0_nat, simp }

lemma nnreal.tendsto_const_div_at_top_nhds_0_nat (C : â„â‰¥0) :
  tendsto (Î» n : â„•, C / n) at_top (ğ“ 0) :=
by simpa using tendsto_const_nhds.mul nnreal.tendsto_inverse_at_top_nhds_0_nat

lemma tendsto_one_div_add_at_top_nhds_0_nat :
  tendsto (Î» n : â„•, 1 / ((n : â„) + 1)) at_top (ğ“ 0) :=
suffices tendsto (Î» n : â„•, 1 / (â†‘(n + 1) : â„)) at_top (ğ“ 0), by simpa,
(tendsto_add_at_top_iff_nat 1).2 (tendsto_const_div_at_top_nhds_0_nat 1)

/-! ### Powers -/

lemma tendsto_add_one_pow_at_top_at_top_of_pos [linear_ordered_semiring Î±] [archimedean Î±] {r : Î±}
  (h : 0 < r) :
  tendsto (Î» n:â„•, (r + 1)^n) at_top at_top :=
tendsto_at_top_at_top_of_monotone' (Î» n m, pow_le_pow (le_add_of_nonneg_left (le_of_lt h))) $
  not_bdd_above_iff.2 $ Î» x, set.exists_range_iff.2 $ add_one_pow_unbounded_of_pos _ h

lemma tendsto_pow_at_top_at_top_of_one_lt [linear_ordered_ring Î±] [archimedean Î±]
  {r : Î±} (h : 1 < r) :
  tendsto (Î»n:â„•, r ^ n) at_top at_top :=
sub_add_cancel r 1 â–¸ tendsto_add_one_pow_at_top_at_top_of_pos (sub_pos.2 h)

lemma nat.tendsto_pow_at_top_at_top_of_one_lt {m : â„•} (h : 1 < m) :
  tendsto (Î»n:â„•, m ^ n) at_top at_top :=
tsub_add_cancel_of_le (le_of_lt h) â–¸
  tendsto_add_one_pow_at_top_at_top_of_pos (tsub_pos_of_lt h)

lemma tendsto_pow_at_top_nhds_0_of_lt_1 {ğ•œ : Type*} [linear_ordered_field ğ•œ] [archimedean ğ•œ]
  [topological_space ğ•œ] [order_topology ğ•œ] {r : ğ•œ} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) :
  tendsto (Î»n:â„•, r^n) at_top (ğ“ 0) :=
hâ‚.eq_or_lt.elim
  (assume : 0 = r,
    (tendsto_add_at_top_iff_nat 1).mp $ by simp [pow_succ, â† this, tendsto_const_nhds])
  (assume : 0 < r,
    have tendsto (Î»n, (râ»Â¹ ^ n)â»Â¹) at_top (ğ“ 0),
      from tendsto_inv_at_top_zero.comp
        (tendsto_pow_at_top_at_top_of_one_lt $ one_lt_inv this hâ‚‚),
    this.congr (Î» n, by simp))

lemma tendsto_pow_at_top_nhds_within_0_of_lt_1 {ğ•œ : Type*} [linear_ordered_field ğ•œ] [archimedean ğ•œ]
  [topological_space ğ•œ] [order_topology ğ•œ] {r : ğ•œ} (hâ‚ : 0 < r) (hâ‚‚ : r < 1) :
  tendsto (Î»n:â„•, r^n) at_top (ğ“[>] 0) :=
tendsto_inf.2 âŸ¨tendsto_pow_at_top_nhds_0_of_lt_1 hâ‚.le hâ‚‚,
  tendsto_principal.2 $ eventually_of_forall $ Î» n, pow_pos hâ‚ _âŸ©

lemma uniformity_basis_dist_pow_of_lt_1 {Î± : Type*} [pseudo_metric_space Î±]
  {r : â„} (hâ‚€ : 0 < r) (hâ‚ : r < 1) :
  (ğ“¤ Î±).has_basis (Î» k : â„•, true) (Î» k, {p : Î± Ã— Î± | dist p.1 p.2 < r ^ k}) :=
metric.mk_uniformity_basis (Î» i _, pow_pos hâ‚€ _) $ Î» Îµ Îµ0,
  (exists_pow_lt_of_lt_one Îµ0 hâ‚).imp $ Î» k hk, âŸ¨trivial, hk.leâŸ©

lemma geom_lt {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) {n : â„•} (hn : 0 < n)
  (h : âˆ€ k < n, c * u k < u (k + 1)) :
  c ^ n * u 0 < u n :=
begin
  refine (monotone_mul_left_of_nonneg hc).seq_pos_lt_seq_of_le_of_lt hn _ _ h,
  { simp },
  { simp [pow_succ, mul_assoc, le_refl] }
end

lemma geom_le {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) (n : â„•) (h : âˆ€ k < n, c * u k â‰¤ u (k + 1)) :
  c ^ n * u 0 â‰¤ u n :=
by refine (monotone_mul_left_of_nonneg hc).seq_le_seq n _ _ h; simp [pow_succ, mul_assoc, le_refl]

lemma lt_geom {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) {n : â„•} (hn : 0 < n)
  (h : âˆ€ k < n, u (k + 1) < c * u k) :
  u n < c ^ n * u 0 :=
begin
  refine (monotone_mul_left_of_nonneg hc).seq_pos_lt_seq_of_lt_of_le hn _ h _,
  { simp },
  { simp [pow_succ, mul_assoc, le_refl] }
end

lemma le_geom {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) (n : â„•) (h : âˆ€ k < n, u (k + 1) â‰¤ c * u k) :
  u n â‰¤ (c ^ n) * u 0 :=
by refine (monotone_mul_left_of_nonneg hc).seq_le_seq n _ h _; simp [pow_succ, mul_assoc, le_refl]

/-- If a sequence `v` of real numbers satisfies `k * v n â‰¤ v (n+1)` with `1 < k`,
then it goes to +âˆ. -/
lemma tendsto_at_top_of_geom_le {v : â„• â†’ â„} {c : â„} (hâ‚€ : 0 < v 0) (hc : 1 < c)
  (hu : âˆ€ n, c * v n â‰¤ v (n + 1)) : tendsto v at_top at_top :=
tendsto_at_top_mono (Î» n, geom_le (zero_le_one.trans hc.le) n (Î» k hk, hu k)) $
  (tendsto_pow_at_top_at_top_of_one_lt hc).at_top_mul_const hâ‚€

lemma nnreal.tendsto_pow_at_top_nhds_0_of_lt_1 {r : â„â‰¥0} (hr : r < 1) :
  tendsto (Î» n:â„•, r^n) at_top (ğ“ 0) :=
nnreal.tendsto_coe.1 $ by simp only [nnreal.coe_pow, nnreal.coe_zero,
  tendsto_pow_at_top_nhds_0_of_lt_1 r.coe_nonneg hr]

lemma ennreal.tendsto_pow_at_top_nhds_0_of_lt_1 {r : â„â‰¥0âˆ} (hr : r < 1) :
  tendsto (Î» n:â„•, r^n) at_top (ğ“ 0) :=
begin
  rcases ennreal.lt_iff_exists_coe.1 hr with âŸ¨r, rfl, hr'âŸ©,
  rw [â† ennreal.coe_zero],
  norm_cast at *,
  apply nnreal.tendsto_pow_at_top_nhds_0_of_lt_1 hr
end

/-! ### Geometric series-/
section geometric

lemma has_sum_geometric_of_lt_1 {r : â„} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) :
  has_sum (Î»n:â„•, r ^ n) (1 - r)â»Â¹ :=
have r â‰  1, from ne_of_lt hâ‚‚,
have tendsto (Î»n, (r ^ n - 1) * (r - 1)â»Â¹) at_top (ğ“ ((0 - 1) * (r - 1)â»Â¹)),
  from ((tendsto_pow_at_top_nhds_0_of_lt_1 hâ‚ hâ‚‚).sub tendsto_const_nhds).mul tendsto_const_nhds,
(has_sum_iff_tendsto_nat_of_nonneg (pow_nonneg hâ‚) _).mpr $
  by simp [neg_inv, geom_sum_eq, div_eq_mul_inv, *] at *

lemma summable_geometric_of_lt_1 {r : â„} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) : summable (Î»n:â„•, r ^ n) :=
âŸ¨_, has_sum_geometric_of_lt_1 hâ‚ hâ‚‚âŸ©

lemma tsum_geometric_of_lt_1 {r : â„} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) : âˆ‘'n:â„•, r ^ n = (1 - r)â»Â¹ :=
(has_sum_geometric_of_lt_1 hâ‚ hâ‚‚).tsum_eq

lemma has_sum_geometric_two : has_sum (Î»n:â„•, ((1:â„)/2) ^ n) 2 :=
by convert has_sum_geometric_of_lt_1 _ _; norm_num

lemma summable_geometric_two : summable (Î»n:â„•, ((1:â„)/2) ^ n) :=
âŸ¨_, has_sum_geometric_twoâŸ©

lemma summable_geometric_two_encode {Î¹ : Type*} [encodable Î¹] :
  summable (Î» (i : Î¹), (1/2 : â„)^(encodable.encode i)) :=
summable_geometric_two.comp_injective encodable.encode_injective

lemma tsum_geometric_two : âˆ‘'n:â„•, ((1:â„)/2) ^ n = 2 :=
has_sum_geometric_two.tsum_eq

lemma sum_geometric_two_le (n : â„•) : âˆ‘ (i : â„•) in range n, (1 / (2 : â„)) ^ i â‰¤ 2 :=
begin
  have : âˆ€ i, 0 â‰¤ (1 / (2 : â„)) ^ i,
  { intro i, apply pow_nonneg, norm_num },
  convert sum_le_tsum (range n) (Î» i _, this i) summable_geometric_two,
  exact tsum_geometric_two.symm
end

lemma tsum_geometric_inv_two : âˆ‘' n : â„•, (2 : â„)â»Â¹ ^ n = 2 :=
(inv_eq_one_div (2 : â„)).symm â–¸ tsum_geometric_two

/-- The sum of `2â»Â¹ ^ i` for `n â‰¤ i` equals `2 * 2â»Â¹ ^ n`. -/
lemma tsum_geometric_inv_two_ge (n : â„•) :
  âˆ‘' i, ite (n â‰¤ i) ((2 : â„)â»Â¹ ^ i) 0 = 2 * 2â»Â¹ ^ n :=
begin
  have A : summable (Î» (i : â„•), ite (n â‰¤ i) ((2â»Â¹ : â„) ^ i) 0),
  { apply summable_of_nonneg_of_le _ _ summable_geometric_two;
    { intro i, by_cases hi : n â‰¤ i; simp [hi] } },
  have B : (finset.range n).sum (Î» (i : â„•), ite (n â‰¤ i) ((2â»Â¹ : â„)^i) 0) = 0 :=
    finset.sum_eq_zero (Î» i hi, ite_eq_right_iff.2 $ Î» h,
      (lt_irrefl _ ((finset.mem_range.1 hi).trans_le h)).elim),
  simp only [â† sum_add_tsum_nat_add n A, B, if_true, zero_add, zero_le',
    le_add_iff_nonneg_left, pow_add, tsum_mul_right, tsum_geometric_inv_two],
end

lemma has_sum_geometric_two' (a : â„) : has_sum (Î»n:â„•, (a / 2) / 2 ^ n) a :=
begin
  convert has_sum.mul_left (a / 2) (has_sum_geometric_of_lt_1
    (le_of_lt one_half_pos) one_half_lt_one),
  { funext n, simp, refl, },
  { norm_num }
end

lemma summable_geometric_two' (a : â„) : summable (Î» n:â„•, (a / 2) / 2 ^ n) :=
âŸ¨a, has_sum_geometric_two' aâŸ©

lemma tsum_geometric_two' (a : â„) : âˆ‘' n:â„•, (a / 2) / 2^n = a :=
(has_sum_geometric_two' a).tsum_eq

/-- **Sum of a Geometric Series** -/
lemma nnreal.has_sum_geometric {r : â„â‰¥0} (hr : r < 1) :
  has_sum (Î» n : â„•, r ^ n) (1 - r)â»Â¹ :=
begin
  apply nnreal.has_sum_coe.1,
  push_cast,
  rw [nnreal.coe_sub (le_of_lt hr)],
  exact has_sum_geometric_of_lt_1 r.coe_nonneg hr
end

lemma nnreal.summable_geometric {r : â„â‰¥0} (hr : r < 1) : summable (Î»n:â„•, r ^ n) :=
âŸ¨_, nnreal.has_sum_geometric hrâŸ©

lemma tsum_geometric_nnreal {r : â„â‰¥0} (hr : r < 1) : âˆ‘'n:â„•, r ^ n = (1 - r)â»Â¹ :=
(nnreal.has_sum_geometric hr).tsum_eq

/-- The series `pow r` converges to `(1-r)â»Â¹`. For `r < 1` the RHS is a finite number,
and for `1 â‰¤ r` the RHS equals `âˆ`. -/
@[simp] lemma ennreal.tsum_geometric (r : â„â‰¥0âˆ) : âˆ‘'n:â„•, r ^ n = (1 - r)â»Â¹ :=
begin
  cases lt_or_le r 1 with hr hr,
  { rcases ennreal.lt_iff_exists_coe.1 hr with âŸ¨r, rfl, hr'âŸ©,
    norm_cast at *,
    convert ennreal.tsum_coe_eq (nnreal.has_sum_geometric hr),
    rw [ennreal.coe_inv $ ne_of_gt $ tsub_pos_iff_lt.2 hr] },
  { rw [tsub_eq_zero_iff_le.mpr hr, ennreal.inv_zero, ennreal.tsum_eq_supr_nat, supr_eq_top],
    refine Î» a ha, (ennreal.exists_nat_gt (lt_top_iff_ne_top.1 ha)).imp
      (Î» n hn, lt_of_lt_of_le hn _),
    calc (n:â„â‰¥0âˆ) = âˆ‘ i in range n, 1     : by rw [sum_const, nsmul_one, card_range]
              ... â‰¤ âˆ‘ i in range n, r ^ i : sum_le_sum (Î» k _, one_le_pow_of_one_le' hr k) }
end

end geometric

/-!
### Sequences with geometrically decaying distance in metric spaces

In this paragraph, we discuss sequences in metric spaces or emetric spaces for which the distance
between two consecutive terms decays geometrically. We show that such sequences are Cauchy
sequences, and bound their distances to the limit. We also discuss series with geometrically
decaying terms.
-/
section edist_le_geometric

variables [pseudo_emetric_space Î±] (r C : â„â‰¥0âˆ) (hr : r < 1) (hC : C â‰  âŠ¤) {f : â„• â†’ Î±}
  (hu : âˆ€n, edist (f n) (f (n+1)) â‰¤ C * r^n)

include hr hC hu

/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, `C â‰  âˆ`, `r < 1`,
then `f` is a Cauchy sequence.-/
lemma cauchy_seq_of_edist_le_geometric : cauchy_seq f :=
begin
  refine cauchy_seq_of_edist_le_of_tsum_ne_top _ hu _,
  rw [ennreal.tsum_mul_left, ennreal.tsum_geometric],
  refine ennreal.mul_ne_top hC (ennreal.inv_ne_top.2 _),
  exact (tsub_pos_iff_lt.2 hr).ne'
end

omit hr hC

/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, then the distance from
`f n` to the limit of `f` is bounded above by `C * r^n / (1 - r)`. -/
lemma edist_le_of_edist_le_geometric_of_tendsto {a : Î±} (ha : tendsto f at_top (ğ“ a)) (n : â„•) :
  edist (f n) a â‰¤ (C * r^n) / (1 - r) :=
begin
  convert edist_le_tsum_of_edist_le_of_tendsto _ hu ha _,
  simp only [pow_add, ennreal.tsum_mul_left, ennreal.tsum_geometric, div_eq_mul_inv, mul_assoc]
end

/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, then the distance from
`f 0` to the limit of `f` is bounded above by `C / (1 - r)`. -/
lemma edist_le_of_edist_le_geometric_of_tendstoâ‚€ {a : Î±} (ha : tendsto f at_top (ğ“ a)) :
  edist (f 0) a â‰¤ C / (1 - r) :=
by simpa only [pow_zero, mul_one] using edist_le_of_edist_le_geometric_of_tendsto r C hu ha 0

end edist_le_geometric

section edist_le_geometric_two

variables [pseudo_emetric_space Î±] (C : â„â‰¥0âˆ) (hC : C â‰  âŠ¤) {f : â„• â†’ Î±}
  (hu : âˆ€n, edist (f n) (f (n+1)) â‰¤ C / 2^n) {a : Î±} (ha : tendsto f at_top (ğ“ a))

include hC hu

/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then `f` is a Cauchy sequence.-/
lemma cauchy_seq_of_edist_le_geometric_two : cauchy_seq f :=
begin
  simp only [div_eq_mul_inv, ennreal.inv_pow] at hu,
  refine cauchy_seq_of_edist_le_geometric 2â»Â¹ C _ hC hu,
  simp [ennreal.one_lt_two]
end

omit hC
include ha

/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then the distance from
`f n` to the limit of `f` is bounded above by `2 * C * 2^-n`. -/
lemma edist_le_of_edist_le_geometric_two_of_tendsto (n : â„•) :
  edist (f n) a â‰¤ 2 * C / 2^n :=
begin
  simp only [div_eq_mul_inv, ennreal.inv_pow] at *,
  rw [mul_assoc, mul_comm],
  convert edist_le_of_edist_le_geometric_of_tendsto 2â»Â¹ C hu ha n,
  rw [ennreal.one_sub_inv_two, inv_inv]
end

/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then the distance from
`f 0` to the limit of `f` is bounded above by `2 * C`. -/
lemma edist_le_of_edist_le_geometric_two_of_tendstoâ‚€: edist (f 0) a â‰¤ 2 * C :=
by simpa only [pow_zero, div_eq_mul_inv, ennreal.inv_one, mul_one]
  using edist_le_of_edist_le_geometric_two_of_tendsto C hu ha 0

end edist_le_geometric_two

section le_geometric

variables [pseudo_metric_space Î±] {r C : â„} (hr : r < 1) {f : â„• â†’ Î±}
  (hu : âˆ€n, dist (f n) (f (n+1)) â‰¤ C * r^n)

include hr hu

lemma aux_has_sum_of_le_geometric : has_sum (Î» n : â„•, C * r^n) (C / (1 - r)) :=
begin
  rcases sign_cases_of_C_mul_pow_nonneg (Î» n, dist_nonneg.trans (hu n)) with rfl | âŸ¨Câ‚€, râ‚€âŸ©,
  { simp [has_sum_zero] },
  { refine has_sum.mul_left C _,
    simpa using has_sum_geometric_of_lt_1 râ‚€ hr }
end

variables (r C)

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then `f` is a Cauchy sequence.
Note that this lemma does not assume `0 â‰¤ C` or `0 â‰¤ r`. -/
lemma cauchy_seq_of_le_geometric : cauchy_seq f :=
cauchy_seq_of_dist_le_of_summable _ hu âŸ¨_, aux_has_sum_of_le_geometric hr huâŸ©

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then the distance from
`f n` to the limit of `f` is bounded above by `C * r^n / (1 - r)`. -/
lemma dist_le_of_le_geometric_of_tendstoâ‚€ {a : Î±} (ha : tendsto f at_top (ğ“ a)) :
  dist (f 0) a â‰¤ C / (1 - r) :=
(aux_has_sum_of_le_geometric hr hu).tsum_eq â–¸
  dist_le_tsum_of_dist_le_of_tendstoâ‚€ _ hu âŸ¨_, aux_has_sum_of_le_geometric hr huâŸ© ha

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then the distance from
`f 0` to the limit of `f` is bounded above by `C / (1 - r)`. -/
lemma dist_le_of_le_geometric_of_tendsto {a : Î±} (ha : tendsto f at_top (ğ“ a)) (n : â„•) :
  dist (f n) a â‰¤ (C * r^n) / (1 - r) :=
begin
  have := aux_has_sum_of_le_geometric hr hu,
  convert dist_le_tsum_of_dist_le_of_tendsto _ hu âŸ¨_, thisâŸ© ha n,
  simp only [pow_add, mul_left_comm C, mul_div_right_comm],
  rw [mul_comm],
  exact (this.mul_left _).tsum_eq.symm
end

omit hr hu

variable (huâ‚‚ : âˆ€ n, dist (f n) (f (n+1)) â‰¤ (C / 2) / 2^n)

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then `f` is a Cauchy sequence. -/
lemma cauchy_seq_of_le_geometric_two : cauchy_seq f :=
cauchy_seq_of_dist_le_of_summable _ huâ‚‚ $ âŸ¨_, has_sum_geometric_two' CâŸ©

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then the distance from
`f 0` to the limit of `f` is bounded above by `C`. -/
lemma dist_le_of_le_geometric_two_of_tendstoâ‚€ {a : Î±} (ha : tendsto f at_top (ğ“ a)) :
  dist (f 0) a â‰¤ C :=
(tsum_geometric_two' C) â–¸ dist_le_tsum_of_dist_le_of_tendstoâ‚€ _ huâ‚‚ (summable_geometric_two' C) ha

include huâ‚‚

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then the distance from
`f n` to the limit of `f` is bounded above by `C / 2^n`. -/
lemma dist_le_of_le_geometric_two_of_tendsto {a : Î±} (ha : tendsto f at_top (ğ“ a)) (n : â„•) :
  dist (f n) a â‰¤ C / 2^n :=
begin
  convert dist_le_tsum_of_dist_le_of_tendsto _ huâ‚‚ (summable_geometric_two' C) ha n,
  simp only [add_comm n, pow_add, â† div_div],
  symmetry,
  exact ((has_sum_geometric_two' C).div_const _).tsum_eq
end

end le_geometric

/-! ### Summability tests based on comparison with geometric series -/

/-- A series whose terms are bounded by the terms of a converging geometric series converges. -/
lemma summable_one_div_pow_of_le {m : â„} {f : â„• â†’ â„•} (hm : 1 < m) (fi : âˆ€ i, i â‰¤ f i) :
  summable (Î» i, 1 / m ^ f i) :=
begin
  refine summable_of_nonneg_of_le
    (Î» a, one_div_nonneg.mpr (pow_nonneg (zero_le_one.trans hm.le) _)) (Î» a, _)
    (summable_geometric_of_lt_1 (one_div_nonneg.mpr (zero_le_one.trans hm.le))
      ((one_div_lt (zero_lt_one.trans hm) zero_lt_one).mpr (one_div_one.le.trans_lt hm))),
  rw [div_pow, one_pow],
  refine (one_div_le_one_div _ _).mpr (pow_le_pow hm.le (fi a));
  exact pow_pos (zero_lt_one.trans hm) _
end

/-! ### Positive sequences with small sums on encodable types -/

/-- For any positive `Îµ`, define on an encodable type a positive sequence with sum less than `Îµ` -/
def pos_sum_of_encodable {Îµ : â„} (hÎµ : 0 < Îµ)
  (Î¹) [encodable Î¹] : {Îµ' : Î¹ â†’ â„ // (âˆ€ i, 0 < Îµ' i) âˆ§ âˆƒ c, has_sum Îµ' c âˆ§ c â‰¤ Îµ} :=
begin
  let f := Î» n, (Îµ / 2) / 2 ^ n,
  have hf : has_sum f Îµ := has_sum_geometric_two' _,
  have f0 : âˆ€ n, 0 < f n := Î» n, div_pos (half_pos hÎµ) (pow_pos zero_lt_two _),
  refine âŸ¨f âˆ˜ encodable.encode, Î» i, f0 _, _âŸ©,
  rcases hf.summable.comp_injective (@encodable.encode_injective Î¹ _) with âŸ¨c, hgâŸ©,
  refine âŸ¨c, hg, has_sum_le_inj _ (@encodable.encode_injective Î¹ _) _ _ hg hfâŸ©,
  { assume i _, exact le_of_lt (f0 _) },
  { assume n, exact le_rfl }
end

lemma set.countable.exists_pos_has_sum_le {Î¹ : Type*} {s : set Î¹} (hs : s.countable)
  {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ Îµ' : Î¹ â†’ â„, (âˆ€ i, 0 < Îµ' i) âˆ§ âˆƒ c, has_sum (Î» i : s, Îµ' i) c âˆ§ c â‰¤ Îµ :=
begin
  haveI := hs.to_encodable,
  rcases pos_sum_of_encodable hÎµ s with âŸ¨f, hf0, âŸ¨c, hfc, hcÎµâŸ©âŸ©,
  refine âŸ¨Î» i, if h : i âˆˆ s then f âŸ¨i, hâŸ© else 1, Î» i, _, âŸ¨c, _, hcÎµâŸ©âŸ©,
  { split_ifs, exacts [hf0 _, zero_lt_one] },
  { simpa only [subtype.coe_prop, dif_pos, subtype.coe_eta] }
end

lemma set.countable.exists_pos_forall_sum_le {Î¹ : Type*} {s : set Î¹} (hs : s.countable)
  {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ Îµ' : Î¹ â†’ â„, (âˆ€ i, 0 < Îµ' i) âˆ§ âˆ€ t : finset Î¹, â†‘t âŠ† s â†’ âˆ‘ i in t, Îµ' i â‰¤ Îµ :=
begin
  rcases hs.exists_pos_has_sum_le hÎµ with âŸ¨Îµ', hpos, c, hÎµ'c, hcÎµâŸ©,
  refine âŸ¨Îµ', hpos, Î» t ht, _âŸ©,
  rw [â† sum_subtype_of_mem _ ht],
  refine (sum_le_has_sum _ _ hÎµ'c).trans hcÎµ,
  exact Î» _ _, (hpos _).le
end

namespace nnreal

theorem exists_pos_sum_of_encodable {Îµ : â„â‰¥0} (hÎµ : Îµ â‰  0) (Î¹) [encodable Î¹] :
  âˆƒ Îµ' : Î¹ â†’ â„â‰¥0, (âˆ€ i, 0 < Îµ' i) âˆ§ âˆƒc, has_sum Îµ' c âˆ§ c < Îµ :=
let âŸ¨a, a0, aÎµâŸ© := exists_between (pos_iff_ne_zero.2 hÎµ) in
let âŸ¨Îµ', hÎµ', c, hc, hcÎµâŸ© := pos_sum_of_encodable a0 Î¹ in
âŸ¨ Î»i, âŸ¨Îµ' i, le_of_lt $ hÎµ' iâŸ©, assume i, nnreal.coe_lt_coe.1 $ hÎµ' i,
  âŸ¨c, has_sum_le (assume i, le_of_lt $ hÎµ' i) has_sum_zero hc âŸ©, nnreal.has_sum_coe.1 hc,
   lt_of_le_of_lt (nnreal.coe_le_coe.1 hcÎµ) aÎµ âŸ©

end nnreal

namespace ennreal

theorem exists_pos_sum_of_encodable {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) (Î¹) [encodable Î¹] :
  âˆƒ Îµ' : Î¹ â†’ â„â‰¥0, (âˆ€ i, 0 < Îµ' i) âˆ§ âˆ‘' i, (Îµ' i : â„â‰¥0âˆ) < Îµ :=
begin
  rcases exists_between (pos_iff_ne_zero.2 hÎµ) with âŸ¨r, h0r, hrÎµâŸ©,
  rcases lt_iff_exists_coe.1 hrÎµ with âŸ¨x, rfl, hxâŸ©,
  rcases nnreal.exists_pos_sum_of_encodable (coe_pos.1 h0r).ne' Î¹ with âŸ¨Îµ', hp, c, hc, hcrâŸ©,
  exact âŸ¨Îµ', hp, (ennreal.tsum_coe_eq hc).symm â–¸ lt_trans (coe_lt_coe.2 hcr) hrÎµâŸ©
end

theorem exists_pos_sum_of_encodable' {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) (Î¹) [encodable Î¹] :
  âˆƒ Îµ' : Î¹ â†’ â„â‰¥0âˆ, (âˆ€ i, 0 < Îµ' i) âˆ§ (âˆ‘' i, Îµ' i) < Îµ :=
let âŸ¨Î´, Î´pos, hÎ´âŸ© := exists_pos_sum_of_encodable hÎµ Î¹ in
  âŸ¨Î» i, Î´ i, Î» i, ennreal.coe_pos.2 (Î´pos i), hÎ´âŸ©

theorem exists_pos_tsum_mul_lt_of_encodable {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) {Î¹} [encodable Î¹]
  (w : Î¹ â†’ â„â‰¥0âˆ) (hw : âˆ€ i, w i â‰  âˆ) :
  âˆƒ Î´ : Î¹ â†’ â„â‰¥0, (âˆ€ i, 0 < Î´ i) âˆ§ âˆ‘' i, (w i * Î´ i : â„â‰¥0âˆ) < Îµ :=
begin
  lift w to Î¹ â†’ â„â‰¥0 using hw,
  rcases exists_pos_sum_of_encodable hÎµ Î¹ with âŸ¨Î´', Hpos, HsumâŸ©,
  have : âˆ€ i, 0 < max 1 (w i), from Î» i, zero_lt_one.trans_le (le_max_left _ _),
  refine âŸ¨Î» i, Î´' i / max 1 (w i), Î» i, nnreal.div_pos (Hpos _) (this i), _âŸ©,
  refine lt_of_le_of_lt (ennreal.tsum_le_tsum $ Î» i, _) Hsum,
  rw [coe_div (this i).ne'],
  refine mul_le_of_le_div' (ennreal.mul_le_mul le_rfl $ ennreal.inv_le_inv.2 _),
  exact coe_le_coe.2 (le_max_right _ _)
end

end ennreal

/-!
### Factorial
-/

lemma factorial_tendsto_at_top : tendsto nat.factorial at_top at_top :=
tendsto_at_top_at_top_of_monotone nat.monotone_factorial (Î» n, âŸ¨n, n.self_le_factorialâŸ©)

lemma tendsto_factorial_div_pow_self_at_top : tendsto (Î» n, n! / n^n : â„• â†’ â„) at_top (ğ“ 0) :=
tendsto_of_tendsto_of_tendsto_of_le_of_le'
  tendsto_const_nhds
  (tendsto_const_div_at_top_nhds_0_nat 1)
  (eventually_of_forall $ Î» n, div_nonneg (by exact_mod_cast n.factorial_pos.le)
    (pow_nonneg (by exact_mod_cast n.zero_le) _))
  begin
    refine (eventually_gt_at_top 0).mono (Î» n hn, _),
    rcases nat.exists_eq_succ_of_ne_zero hn.ne.symm with âŸ¨k, rflâŸ©,
    rw [â† prod_range_add_one_eq_factorial, pow_eq_prod_const, div_eq_mul_inv, â† inv_eq_one_div,
      prod_nat_cast, nat.cast_succ, â† prod_inv_distrib', â† prod_mul_distrib,
      finset.prod_range_succ'],
    simp only [prod_range_succ', one_mul, nat.cast_add, zero_add, nat.cast_one],
    refine mul_le_of_le_one_left (inv_nonneg.mpr $ by exact_mod_cast hn.le) (prod_le_one _ _);
      intros x hx; rw finset.mem_range at hx,
    { refine mul_nonneg _ (inv_nonneg.mpr _); norm_cast; linarith },
    { refine (div_le_one $ by exact_mod_cast hn).mpr _, norm_cast, linarith }
  end

/-!
### Ceil and floor
-/

section

lemma tendsto_nat_floor_at_top {Î± : Type*} [linear_ordered_semiring Î±] [floor_semiring Î±] :
  tendsto (Î» (x : Î±), âŒŠxâŒ‹â‚Š) at_top at_top :=
nat.floor_mono.tendsto_at_top_at_top (Î» x, âŸ¨max 0 (x + 1), by simp [nat.le_floor_iff]âŸ©)

variables {R : Type*} [topological_space R] [linear_ordered_field R] [order_topology R]
[floor_ring R]

lemma tendsto_nat_floor_mul_div_at_top {a : R} (ha : 0 â‰¤ a) :
  tendsto (Î» x, (âŒŠa * xâŒ‹â‚Š : R) / x) at_top (ğ“ a) :=
begin
  have A : tendsto (Î» (x : R), a - xâ»Â¹) at_top (ğ“ (a - 0)) :=
    tendsto_const_nhds.sub tendsto_inv_at_top_zero,
  rw sub_zero at A,
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le' A tendsto_const_nhds,
  { refine eventually_at_top.2 âŸ¨1, Î» x hx, _âŸ©,
    simp only [le_div_iff (zero_lt_one.trans_le hx), sub_mul,
      inv_mul_cancel (zero_lt_one.trans_le hx).ne'],
    have := nat.lt_floor_add_one (a * x),
    linarith },
  { refine eventually_at_top.2 âŸ¨1, Î» x hx, _âŸ©,
    rw div_le_iff (zero_lt_one.trans_le hx),
    simp [nat.floor_le (mul_nonneg ha (zero_le_one.trans hx))] }
end

lemma tendsto_nat_floor_div_at_top :
  tendsto (Î» x, (âŒŠxâŒ‹â‚Š : R) / x) at_top (ğ“ 1) :=
by simpa using tendsto_nat_floor_mul_div_at_top (@zero_le_one R _)

lemma tendsto_nat_ceil_mul_div_at_top {a : R} (ha : 0 â‰¤ a) :
  tendsto (Î» x, (âŒˆa * xâŒ‰â‚Š : R) / x) at_top (ğ“ a) :=
begin
  have A : tendsto (Î» (x : R), a + xâ»Â¹) at_top (ğ“ (a + 0)) :=
    tendsto_const_nhds.add tendsto_inv_at_top_zero,
  rw add_zero at A,
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds A,
  { refine eventually_at_top.2 âŸ¨1, Î» x hx, _âŸ©,
    rw le_div_iff (zero_lt_one.trans_le hx),
    exact nat.le_ceil _ },
  { refine eventually_at_top.2 âŸ¨1, Î» x hx, _âŸ©,
    simp [div_le_iff (zero_lt_one.trans_le hx), inv_mul_cancel (zero_lt_one.trans_le hx).ne',
      (nat.ceil_lt_add_one ((mul_nonneg ha (zero_le_one.trans hx)))).le, add_mul] }
end

lemma tendsto_nat_ceil_div_at_top :
  tendsto (Î» x, (âŒˆxâŒ‰â‚Š : R) / x) at_top (ğ“ 1) :=
by simpa using tendsto_nat_ceil_mul_div_at_top (@zero_le_one R _)

end
