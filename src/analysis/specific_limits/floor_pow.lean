/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/

import analysis.specific_limits.basic
import analysis.special_functions.pow

/-!
# Results on discretized exponentials

We state several auxiliary results pertaining to sequences of the form `‚åäc^n‚åã‚Çä`.

* `tendsto_div_of_monotone_of_tendsto_div_floor_pow`: If a monotone sequence `u` is such that
  `u ‚åäc^n‚åã‚Çä / ‚åäc^n‚åã‚Çä` converges to a limit `l` for all `c > 1`, then `u n / n` tends to `l`.
* `sum_div_nat_floor_pow_sq_le_div_sq`: The sum of `1/‚åäc^i‚åã‚Çä^2` above a threshold `j` is comparable
  to `1/j^2`, up to a multiplicative constant.
-/

open filter finset
open_locale topological_space big_operators

/-- If a monotone sequence `u` is such that `u n / n` tends to a limit `l` along subsequences with
exponential growth rate arbitrarily close to `1`, then `u n / n` tends to `l`. -/
lemma tendsto_div_of_monotone_of_exists_subseq_tendsto_div (u : ‚Ñï ‚Üí ‚Ñù) (l : ‚Ñù) (hmono : monotone u)
  (hlim : ‚àÄ (a : ‚Ñù), 1 < a ‚Üí ‚àÉ c : ‚Ñï ‚Üí ‚Ñï, (‚àÄ·∂† n in at_top, (c (n+1) : ‚Ñù) ‚â§ a * c n) ‚àß
    tendsto c at_top at_top ‚àß tendsto (Œª n, u (c n) / (c n)) at_top (ùìù l)) :
  tendsto (Œª n, u n / n) at_top (ùìù l) :=
begin
  /- To check the result up to some `Œµ > 0`, we use a sequence `c` for which the ratio
  `c (N+1) / c N` is bounded by `1 + Œµ`. Sandwiching a given `n` between two consecutive values of
  `c`, say `c N` and `c (N+1)`, one can then bound `u n / n` from above by `u (c N) / c (N - 1)`
  and from below by `u (c (N - 1)) / c N` (using that `u` is monotone), which are both comparable
  to the limit `l` up to `1 + Œµ`.
  We give a version of this proof by clearing out denominators first, to avoid discussing the sign
  of different quantities. -/
  have lnonneg : 0 ‚â§ l,
  { rcases hlim 2 one_lt_two with ‚ü®c, cgrowth, ctop, clim‚ü©,
    have : tendsto (Œª n, u 0 / (c n)) at_top (ùìù 0) :=
      tendsto_const_nhds.div_at_top (tendsto_coe_nat_at_top_iff.2 ctop),
    apply le_of_tendsto_of_tendsto' this clim (Œª n, _),
    simp_rw [div_eq_inv_mul],
    exact mul_le_mul_of_nonneg_left (hmono (zero_le _)) (inv_nonneg.2 (nat.cast_nonneg _)) },
  have A : ‚àÄ (Œµ : ‚Ñù), 0 < Œµ ‚Üí ‚àÄ·∂† n in at_top, u n - n * l ‚â§ (Œµ * (1 + Œµ + l)) * n,
  { assume Œµ Œµpos,
    rcases hlim (1 + Œµ) ((lt_add_iff_pos_right _).2 Œµpos) with ‚ü®c, cgrowth, ctop, clim‚ü©,
    have L : ‚àÄ·∂† n in at_top, u (c n) - c n * l ‚â§ Œµ * c n,
    { rw [‚Üê tendsto_sub_nhds_zero_iff, ‚Üê asymptotics.is_o_one_iff ‚Ñù,
          asymptotics.is_o_iff] at clim,
      filter_upwards [clim Œµpos, ctop (Ioi_mem_at_top 0)] with n hn cnpos',
      have cnpos : 0 < c n := cnpos',
      calc u (c n) - c n * l
      = (u (c n) / c n - l) * c n:
        by simp only [cnpos.ne', ne.def, nat.cast_eq_zero, not_false_iff] with field_simps
      ... ‚â§ Œµ * c n :
        begin
          apply mul_le_mul_of_nonneg_right _ (nat.cast_nonneg _),
          simp only [mul_one, real.norm_eq_abs, abs_one] at hn,
          exact le_trans (le_abs_self _) hn,
        end },
    obtain ‚ü®a, ha‚ü© : ‚àÉ (a : ‚Ñï), ‚àÄ (b : ‚Ñï), a ‚â§ b ‚Üí (c (b + 1) : ‚Ñù) ‚â§ (1 + Œµ) * c b
        ‚àß u (c b) - c b * l ‚â§ Œµ * c b := eventually_at_top.1 (cgrowth.and L),
    let M := ((finset.range (a+1)).image (Œª i, c i)).max' (by simp),
    filter_upwards [Ici_mem_at_top M] with n hn,
    have exN : ‚àÉ N, n < c N,
    { rcases (tendsto_at_top.1 ctop (n+1)).exists with ‚ü®N, hN‚ü©,
      exact ‚ü®N, by linarith only [hN]‚ü© },
    let N := nat.find exN,
    have ncN : n < c N := nat.find_spec exN,
    have aN : a + 1 ‚â§ N,
    { by_contra' h,
      have cNM : c N ‚â§ M,
      { apply le_max',
        apply mem_image_of_mem,
        exact mem_range.2 h },
      exact lt_irrefl _ ((cNM.trans hn).trans_lt ncN) },
    have Npos : 0 < N := lt_of_lt_of_le (nat.succ_pos') aN,
    have cNn : c (N - 1) ‚â§ n,
    { have : N - 1 < N := nat.pred_lt Npos.ne',
      simpa only [not_lt] using nat.find_min exN this },
    have IcN : (c N : ‚Ñù) ‚â§ (1 + Œµ) * (c (N - 1)),
    { have A : a ‚â§ N - 1, by linarith only [aN, Npos],
      have B : N - 1 + 1 = N := nat.succ_pred_eq_of_pos Npos,
      have := (ha _ A).1,
      rwa B at this },
    calc u n - n * l ‚â§ u (c N) - c (N - 1) * l :
      begin
        apply sub_le_sub (hmono ncN.le),
        apply mul_le_mul_of_nonneg_right (nat.cast_le.2 cNn) lnonneg,
      end
    ... = (u (c N) - c N * l) + (c N - c (N - 1)) * l : by ring
    ... ‚â§ Œµ * c N + (Œµ * c (N - 1)) * l :
      begin
        apply add_le_add,
        { apply (ha _ _).2,
          exact le_trans (by simp only [le_add_iff_nonneg_right, zero_le']) aN },
        { apply mul_le_mul_of_nonneg_right _ lnonneg,
          linarith only [IcN] },
      end
    ... ‚â§ Œµ * ((1 + Œµ) * c (N-1)) + (Œµ * c (N - 1)) * l :
      add_le_add (mul_le_mul_of_nonneg_left IcN Œµpos.le) le_rfl
    ... = (Œµ * (1 + Œµ + l)) * c (N - 1) : by ring
    ... ‚â§ (Œµ * (1 + Œµ + l)) * n :
      begin
        refine mul_le_mul_of_nonneg_left (nat.cast_le.2 cNn) _,
        apply mul_nonneg Œµpos.le,
        linarith only [Œµpos, lnonneg]
      end },
  have B : ‚àÄ (Œµ : ‚Ñù), 0 < Œµ ‚Üí ‚àÄ·∂† (n : ‚Ñï) in at_top, (n : ‚Ñù) * l - u n ‚â§ (Œµ * (1 + l)) * n,
  { assume Œµ Œµpos,
    rcases hlim (1 + Œµ) ((lt_add_iff_pos_right _).2 Œµpos) with ‚ü®c, cgrowth, ctop, clim‚ü©,
    have L : ‚àÄ·∂† (n : ‚Ñï) in at_top, (c n : ‚Ñù) * l - u (c n) ‚â§ Œµ * c n,
    { rw [‚Üê tendsto_sub_nhds_zero_iff, ‚Üê asymptotics.is_o_one_iff ‚Ñù,
          asymptotics.is_o_iff] at clim,
      filter_upwards [clim Œµpos, ctop (Ioi_mem_at_top 0)] with n hn cnpos',
      have cnpos : 0 < c n := cnpos',
      calc (c n : ‚Ñù) * l - u (c n)
      = -(u (c n) / c n - l) * c n:
        by simp only [cnpos.ne', ne.def, nat.cast_eq_zero, not_false_iff, neg_sub] with field_simps
      ... ‚â§ Œµ * c n :
        begin
          apply mul_le_mul_of_nonneg_right _ (nat.cast_nonneg _),
          simp only [mul_one, real.norm_eq_abs, abs_one] at hn,
          exact le_trans (neg_le_abs_self _) hn,
        end },
    obtain ‚ü®a, ha‚ü© : ‚àÉ (a : ‚Ñï), ‚àÄ (b : ‚Ñï), a ‚â§ b ‚Üí (c (b + 1) : ‚Ñù) ‚â§ (1 + Œµ) * c b
        ‚àß (c b : ‚Ñù) * l - u (c b) ‚â§ Œµ * c b := eventually_at_top.1 (cgrowth.and L),
    let M := ((finset.range (a+1)).image (Œª i, c i)).max' (by simp),
    filter_upwards [Ici_mem_at_top M] with n hn,
    have exN : ‚àÉ N, n < c N,
    { rcases (tendsto_at_top.1 ctop (n+1)).exists with ‚ü®N, hN‚ü©,
      exact ‚ü®N, by linarith only [hN]‚ü© },
    let N := nat.find exN,
    have ncN : n < c N := nat.find_spec exN,
    have aN : a + 1 ‚â§ N,
    { by_contra' h,
      have cNM : c N ‚â§ M,
      { apply le_max',
        apply mem_image_of_mem,
        exact mem_range.2 h },
      exact lt_irrefl _ ((cNM.trans hn).trans_lt ncN) },
    have Npos : 0 < N := lt_of_lt_of_le (nat.succ_pos') aN,
    have aN' : a ‚â§ N - 1 := by linarith only [aN, Npos],
    have cNn : c (N - 1) ‚â§ n,
    { have : N - 1 < N := nat.pred_lt Npos.ne',
      simpa only [not_lt] using nat.find_min exN this },
    calc (n : ‚Ñù) * l - u n ‚â§ c N * l - u (c (N - 1)) :
      begin
        refine add_le_add (mul_le_mul_of_nonneg_right (nat.cast_le.2 ncN.le) lnonneg) _,
        exact neg_le_neg (hmono cNn),
      end
    ... ‚â§ ((1 + Œµ) * c (N - 1)) * l - u (c (N - 1)) :
      begin
        refine add_le_add (mul_le_mul_of_nonneg_right _ lnonneg) le_rfl,
        have B : N - 1 + 1 = N := nat.succ_pred_eq_of_pos Npos,
        have := (ha _ aN').1,
        rwa B at this,
      end
    ... = (c (N - 1) * l - u (c (N - 1))) + Œµ * c (N - 1) * l : by ring
    ... ‚â§ Œµ * c (N - 1) + Œµ * c (N - 1) * l :
      add_le_add (ha _ aN').2 le_rfl
    ... = (Œµ * (1 + l)) * c (N - 1) : by ring
    ... ‚â§ (Œµ * (1 + l)) * n :
      begin
        refine mul_le_mul_of_nonneg_left (nat.cast_le.2 cNn) _,
        exact mul_nonneg (Œµpos.le) (add_nonneg zero_le_one lnonneg),
      end },
  refine tendsto_order.2 ‚ü®Œª d hd, _, Œª d hd, _‚ü©,
  { obtain ‚ü®Œµ, hŒµ, Œµpos‚ü© : ‚àÉ (Œµ : ‚Ñù), d + Œµ * (1 + l) < l ‚àß 0 < Œµ,
    { have L : tendsto (Œª Œµ, d + (Œµ * (1 + l))) (ùìù[>] 0) (ùìù (d + 0 * (1 + l))),
      { apply tendsto.mono_left _ nhds_within_le_nhds,
        exact tendsto_const_nhds.add (tendsto_id.mul tendsto_const_nhds) },
      simp only [zero_mul, add_zero] at L,
      exact (((tendsto_order.1 L).2 l hd).and (self_mem_nhds_within)).exists },
    filter_upwards [B Œµ Œµpos, Ioi_mem_at_top 0] with n hn npos,
    simp_rw [div_eq_inv_mul],
    calc d < (n‚Åª¬π * n) * (l - Œµ * (1 + l)) :
      begin
        rw [inv_mul_cancel, one_mul],
        { linarith only [hŒµ] },
        { exact nat.cast_ne_zero.2 (ne_of_gt npos) }
      end
    ... = n‚Åª¬π * (n * l - Œµ * (1 + l) * n) : by ring
    ... ‚â§ n‚Åª¬π * u n :
      begin
        refine mul_le_mul_of_nonneg_left _ (inv_nonneg.2 (nat.cast_nonneg _)),
        linarith only [hn],
      end },
  { obtain ‚ü®Œµ, hŒµ, Œµpos‚ü© : ‚àÉ (Œµ : ‚Ñù), l + Œµ * (1 + Œµ + l) < d ‚àß 0 < Œµ,
    { have L : tendsto (Œª Œµ, l + (Œµ * (1 + Œµ + l))) (ùìù[>] 0) (ùìù (l + 0 * (1 + 0 + l))),
      { apply tendsto.mono_left _ nhds_within_le_nhds,
        exact tendsto_const_nhds.add
          (tendsto_id.mul ((tendsto_const_nhds.add tendsto_id).add tendsto_const_nhds)) },
      simp only [zero_mul, add_zero] at L,
      exact (((tendsto_order.1 L).2 d hd).and (self_mem_nhds_within)).exists },
    filter_upwards [A Œµ Œµpos, Ioi_mem_at_top 0] with n hn npos,
    simp_rw [div_eq_inv_mul],
    calc (n : ‚Ñù)‚Åª¬π * u n ‚â§ (n : ‚Ñù)‚Åª¬π * (n * l + Œµ * (1 + Œµ + l) * n) :
      begin
        refine mul_le_mul_of_nonneg_left _ (inv_nonneg.2 (nat.cast_nonneg _)),
        linarith only [hn],
      end
    ... = ((n : ‚Ñù) ‚Åª¬π * n) * (l + Œµ * (1 + Œµ + l)) : by ring
    ... < d :
      begin
        rwa [inv_mul_cancel, one_mul],
        exact nat.cast_ne_zero.2 (ne_of_gt npos),
      end }
end

/-- If a monotone sequence `u` is such that `u ‚åäc^n‚åã‚Çä / ‚åäc^n‚åã‚Çä` converges to a limit `l` for all
`c > 1`, then `u n / n` tends to `l`. It is even enough to have the assumption for a sequence of
`c`s converging to `1`. -/
lemma tendsto_div_of_monotone_of_tendsto_div_floor_pow
  (u : ‚Ñï ‚Üí ‚Ñù) (l : ‚Ñù) (hmono : monotone u)
  (c : ‚Ñï ‚Üí ‚Ñù) (cone : ‚àÄ k, 1 < c k) (clim : tendsto c at_top (ùìù 1))
  (hc : ‚àÄ k, tendsto (Œª (n : ‚Ñï), u (‚åä(c k) ^ n‚åã‚Çä) / ‚åä(c k)^n‚åã‚Çä) at_top (ùìù l)) :
  tendsto (Œª n, u n / n) at_top (ùìù l) :=
begin
  apply tendsto_div_of_monotone_of_exists_subseq_tendsto_div u l hmono,
  assume a ha,
  obtain ‚ü®k, hk‚ü© : ‚àÉ k, c k < a := ((tendsto_order.1 clim).2 a ha).exists,
  refine ‚ü®Œª n, ‚åä(c k)^n‚åã‚Çä, _,
    tendsto_nat_floor_at_top.comp (tendsto_pow_at_top_at_top_of_one_lt (cone k)), hc k‚ü©,
  have H : ‚àÄ (n : ‚Ñï), (0 : ‚Ñù) < ‚åäc k ^ n‚åã‚Çä,
  { assume n,
    refine zero_lt_one.trans_le _,
    simp only [nat.one_le_cast, nat.one_le_floor_iff, one_le_pow_of_one_le (cone k).le n] },
  have A : tendsto (Œª (n : ‚Ñï), ((‚åäc k ^ (n+1)‚åã‚Çä : ‚Ñù) / c k ^ (n+1)) * c k /
    (‚åäc k ^ n‚åã‚Çä / c k ^ n)) at_top (ùìù (1 * c k / 1)),
  { refine tendsto.div (tendsto.mul _ tendsto_const_nhds) _ one_ne_zero,
    { refine tendsto_nat_floor_div_at_top.comp _,
      exact (tendsto_pow_at_top_at_top_of_one_lt (cone k)).comp (tendsto_add_at_top_nat 1) },
    { refine tendsto_nat_floor_div_at_top.comp _,
      exact tendsto_pow_at_top_at_top_of_one_lt (cone k) } },
  have B : tendsto (Œª (n : ‚Ñï), (‚åäc k ^ (n+1)‚åã‚Çä : ‚Ñù) / ‚åäc k ^ n‚åã‚Çä) at_top (ùìù (c k)),
  { simp only [one_mul, div_one] at A,
    convert A,
    ext1 n,
    simp only [(zero_lt_one.trans (cone k)).ne', ne.def, not_false_iff, (H n).ne']
      with field_simps {discharger := tactic.field_simp.ne_zero},
    ring_exp },
  filter_upwards [(tendsto_order.1 B).2 a hk] with n hn,
  exact (div_le_iff (H n)).1 hn.le
end

/-- The sum of `1/(c^i)^2` above a threshold `j` is comparable to `1/j^2`, up to a multiplicative
constant. -/
lemma sum_div_pow_sq_le_div_sq (N : ‚Ñï) {j : ‚Ñù} (hj : 0 < j) {c : ‚Ñù} (hc : 1 < c) :
  ‚àë i in (range N).filter (Œª i, j < c ^ i), 1 / (c ^ i) ^ 2 ‚â§ (c^3 * (c - 1) ‚Åª¬π) / j ^ 2 :=
begin
  have cpos : 0 < c := zero_lt_one.trans hc,
  have A : 0 < (c‚Åª¬π) ^ 2 := sq_pos_of_pos (inv_pos.2 cpos),
  have B : c^2 * (1 - c‚Åª¬π ^ 2) ‚Åª¬π ‚â§ c^3 * (c - 1) ‚Åª¬π,
  { rw [‚Üê div_eq_mul_inv, ‚Üê div_eq_mul_inv, div_le_div_iff _ (sub_pos.2 hc)], swap,
    { exact sub_pos.2 (pow_lt_one (inv_nonneg.2 cpos.le) (inv_lt_one hc) two_ne_zero) },
    have : c ^ 3 = c^2 * c, by ring_exp,
    simp only [mul_sub, this, mul_one, inv_pow, sub_le_sub_iff_left],
    rw [mul_assoc, mul_comm c, ‚Üê mul_assoc, mul_inv_cancel (sq_pos_of_pos cpos).ne', one_mul],
    simpa using pow_le_pow hc.le one_le_two },
  calc
  ‚àë i in (range N).filter (Œª i, j < c ^ i), 1/ (c ^ i) ^ 2
    ‚â§ ‚àë i in Ico (‚åäreal.log j / real.log c‚åã‚Çä) N, 1 / (c ^ i) ^ 2 :
  begin
    refine sum_le_sum_of_subset_of_nonneg _ (Œª i hi hident, div_nonneg zero_le_one (sq_nonneg _)),
    assume i hi,
    simp only [mem_filter, mem_range] at hi,
    simp only [hi.1, mem_Ico, and_true],
    apply nat.floor_le_of_le,
    apply le_of_lt,
    rw [div_lt_iff (real.log_pos hc), ‚Üê real.log_pow],
    exact real.log_lt_log hj hi.2
  end
  ... = ‚àë i in Ico (‚åäreal.log j / real.log c‚åã‚Çä) N, ((c‚Åª¬π) ^ 2) ^ i :
  begin
    congr' 1 with i,
    simp [‚Üê pow_mul, mul_comm],
  end
  ... ‚â§ ((c‚Åª¬π) ^ 2) ^ (‚åäreal.log j / real.log c‚åã‚Çä) / (1 - (c‚Åª¬π) ^ 2) :
  begin
    apply geom_sum_Ico_le_of_lt_one (sq_nonneg _),
    rw sq_lt_one_iff (inv_nonneg.2 (zero_le_one.trans hc.le)),
    exact inv_lt_one hc
  end
  ... ‚â§ ((c‚Åª¬π) ^ 2) ^ (real.log j / real.log c - 1) / (1 - (c‚Åª¬π) ^ 2) :
  begin
    apply div_le_div _ _ _ le_rfl,
    { apply real.rpow_nonneg_of_nonneg (sq_nonneg _) },
    { rw ‚Üê real.rpow_nat_cast,
      apply real.rpow_le_rpow_of_exponent_ge A,
      { exact pow_le_one _ (inv_nonneg.2 (zero_le_one.trans hc.le)) (inv_le_one hc.le) },
      { exact (nat.sub_one_lt_floor _).le } },
    { simpa only [inv_pow, sub_pos] using inv_lt_one (one_lt_pow hc two_ne_zero) }
  end
  ... = (c^2 * (1 - c‚Åª¬π ^ 2) ‚Åª¬π) / j ^ 2 :
  begin
    have I : (c ‚Åª¬π ^ 2) ^ (real.log j / real.log c) = 1 / j ^ 2,
    { apply real.log_inj_on_pos (real.rpow_pos_of_pos A _),
      { rw [one_div], exact inv_pos.2 (sq_pos_of_pos hj) },
      rw real.log_rpow A,
      simp only [one_div, real.log_inv, real.log_pow, nat.cast_bit0, nat.cast_one, mul_neg,
        neg_inj],
      field_simp [(real.log_pos hc).ne'],
      ring },
    rw [real.rpow_sub A, I],
    have : c^2 - 1 ‚â† 0 := (sub_pos.2 (one_lt_pow hc two_ne_zero)).ne',
    field_simp [hj.ne', (zero_lt_one.trans hc).ne'],
    ring,
  end
  ... ‚â§ (c^3 * (c - 1) ‚Åª¬π) / j ^ 2 :
  begin
    apply div_le_div _ B (sq_pos_of_pos hj) le_rfl,
    exact mul_nonneg (pow_nonneg cpos.le _) (inv_nonneg.2 (sub_pos.2 hc).le),
  end
end

lemma mul_pow_le_nat_floor_pow {c : ‚Ñù} (hc : 1 < c) (i : ‚Ñï) :
  (1 - c‚Åª¬π) * c ^ i ‚â§ ‚åäc ^ i‚åã‚Çä :=
begin
  have cpos : 0 < c := zero_lt_one.trans hc,
  rcases nat.eq_zero_or_pos i with rfl|hi,
  { simp only [pow_zero, nat.floor_one, nat.cast_one, mul_one, sub_le_self_iff, inv_nonneg,
      cpos.le] },
  have hident : 1 ‚â§ i := hi,
  calc (1 - c‚Åª¬π) * c ^ i
      = c ^ i - c ^ i * c ‚Åª¬π : by ring
  ... ‚â§ c ^ i - 1 :
    by simpa only [‚Üêdiv_eq_mul_inv, sub_le_sub_iff_left, one_le_div cpos, pow_one]
      using pow_le_pow hc.le hident
  ... ‚â§ ‚åäc ^ i‚åã‚Çä : (nat.sub_one_lt_floor _).le
end

/-- The sum of `1/‚åäc^i‚åã‚Çä^2` above a threshold `j` is comparable to `1/j^2`, up to a multiplicative
constant. -/
lemma sum_div_nat_floor_pow_sq_le_div_sq (N : ‚Ñï) {j : ‚Ñù} (hj : 0 < j) {c : ‚Ñù} (hc : 1 < c) :
  ‚àë i in (range N).filter (Œª i, j < ‚åäc ^ i‚åã‚Çä), (1 : ‚Ñù) / ‚åäc ^ i‚åã‚Çä ^ 2
    ‚â§ (c ^ 5 * (c - 1) ‚Åª¬π ^ 3) / j ^ 2 :=
begin
  have cpos : 0 < c := zero_lt_one.trans hc,
  have A : 0 < 1 - c‚Åª¬π := sub_pos.2 (inv_lt_one hc),
  calc
  ‚àë i in (range N).filter (Œª i, j < ‚åäc ^ i‚åã‚Çä), (1 : ‚Ñù) / ‚åäc ^ i‚åã‚Çä ^ 2
      ‚â§ ‚àë i in (range N).filter (Œª i, j < c ^ i), (1 : ‚Ñù) / ‚åäc ^ i‚åã‚Çä ^ 2 :
  begin
    apply sum_le_sum_of_subset_of_nonneg,
    { assume i hi,
      simp only [mem_filter, mem_range] at hi,
      simpa only [hi.1, mem_filter, mem_range, true_and]
        using hi.2.trans_le (nat.floor_le (pow_nonneg cpos.le _)) },
    { assume i hi hident,
      exact div_nonneg zero_le_one (sq_nonneg _), }
  end
  ... ‚â§ ‚àë i in (range N).filter (Œª i, j < c ^ i), ((1 - c‚Åª¬π) ‚Åª¬π) ^ 2 * (1 / (c ^ i) ^ 2) :
  begin
    apply sum_le_sum (Œª i hi, _),
    rw [mul_div_assoc', mul_one, div_le_div_iff], rotate,
    { apply sq_pos_of_pos,
      refine zero_lt_one.trans_le _,
      simp only [nat.le_floor, one_le_pow_of_one_le, hc.le, nat.one_le_cast, nat.cast_one] },
    { exact sq_pos_of_pos (pow_pos cpos _) },
    rw [one_mul, ‚Üê mul_pow],
    apply pow_le_pow_of_le_left (pow_nonneg cpos.le _),
    rw [‚Üê div_eq_inv_mul, le_div_iff A, mul_comm],
    exact mul_pow_le_nat_floor_pow hc i,
  end
  ... ‚â§ ((1 - c‚Åª¬π) ‚Åª¬π) ^ 2 * (c^3 * (c - 1) ‚Åª¬π) / j ^ 2 :
  begin
    rw [‚Üê mul_sum, ‚Üê mul_div_assoc'],
    refine mul_le_mul_of_nonneg_left _ (sq_nonneg _),
    exact sum_div_pow_sq_le_div_sq N hj hc,
  end
  ... = (c ^ 5 * (c - 1) ‚Åª¬π ^ 3) / j ^ 2 :
  begin
    congr' 1,
    field_simp [cpos.ne', (sub_pos.2 hc).ne'],
    ring,
  end
end
