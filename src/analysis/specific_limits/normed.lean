/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, SÃ©bastien GouÃ«zel, Yury G. Kudryashov, Dylan MacKenzie, Patrick Massot
-/
import analysis.asymptotics.asymptotics
import analysis.specific_limits.basic

/-!
# A collection of specific limit computations

This file contains important specific limit computations in (semi-)normed groups/rings/spaces, as
as well as such computations in `â„` when the natural proof passes through a fact about normed
spaces.

-/

noncomputable theory
open classical set function filter finset metric asymptotics

open_locale classical topological_space nat big_operators uniformity nnreal ennreal

variables {Î± : Type*} {Î² : Type*} {Î¹ : Type*}

lemma tendsto_norm_at_top_at_top : tendsto (norm : â„ â†’ â„) at_top at_top :=
tendsto_abs_at_top_at_top

lemma summable_of_absolute_convergence_real {f : â„• â†’ â„} :
  (âˆƒr, tendsto (Î»n, (âˆ‘ i in range n, |f i|)) at_top (ğ“ r)) â†’ summable f
| âŸ¨r, hrâŸ© :=
  begin
    refine summable_of_summable_norm âŸ¨r, (has_sum_iff_tendsto_nat_of_nonneg _ _).2 _âŸ©,
    exact assume i, norm_nonneg _,
    simpa only using hr
  end

/-! ### Powers -/

lemma tendsto_norm_zero' {ğ•œ : Type*} [normed_group ğ•œ] :
  tendsto (norm : ğ•œ â†’ â„) (ğ“[â‰ ] 0) (ğ“[>] 0) :=
tendsto_norm_zero.inf $ tendsto_principal_principal.2 $ Î» x hx, norm_pos_iff.2 hx

namespace normed_field

lemma tendsto_norm_inverse_nhds_within_0_at_top {ğ•œ : Type*} [normed_field ğ•œ] :
  tendsto (Î» x:ğ•œ, âˆ¥xâ»Â¹âˆ¥) (ğ“[â‰ ] 0) at_top :=
(tendsto_inv_zero_at_top.comp tendsto_norm_zero').congr $ Î» x, (norm_inv x).symm

lemma tendsto_norm_zpow_nhds_within_0_at_top {ğ•œ : Type*} [normed_field ğ•œ] {m : â„¤}
  (hm : m < 0) :
  tendsto (Î» x : ğ•œ, âˆ¥x ^ mâˆ¥) (ğ“[â‰ ] 0) at_top :=
begin
  rcases neg_surjective m with âŸ¨m, rflâŸ©,
  rw neg_lt_zero at hm, lift m to â„• using hm.le, rw int.coe_nat_pos at hm,
  simp only [norm_pow, zpow_negâ‚€, zpow_coe_nat, â† inv_powâ‚€],
  exact (tendsto_pow_at_top hm).comp normed_field.tendsto_norm_inverse_nhds_within_0_at_top
end

/-- The (scalar) product of a sequence that tends to zero with a bounded one also tends to zero. -/
lemma tendsto_zero_smul_of_tendsto_zero_of_bounded {Î¹ ğ•œ ğ”¸ : Type*} [normed_field ğ•œ]
  [normed_group ğ”¸] [normed_space ğ•œ ğ”¸] {l : filter Î¹} {Îµ : Î¹ â†’ ğ•œ} {f : Î¹ â†’ ğ”¸}
  (hÎµ : tendsto Îµ l (ğ“ 0)) (hf : filter.is_bounded_under (â‰¤) l (norm âˆ˜ f)) :
  tendsto (Îµ â€¢ f) l (ğ“ 0) :=
begin
  rw â† is_o_one_iff ğ•œ at hÎµ âŠ¢,
  simpa using is_o.smul_is_O hÎµ (hf.is_O_const (one_ne_zero : (1 : ğ•œ) â‰  0))
end

@[simp] lemma continuous_at_zpow {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] {m : â„¤} {x : ğ•œ} :
  continuous_at (Î» x, x ^ m) x â†” x â‰  0 âˆ¨ 0 â‰¤ m :=
begin
  refine âŸ¨_, continuous_at_zpowâ‚€ _ _âŸ©,
  contrapose!, rintro âŸ¨rfl, hmâŸ© hc,
  exact not_tendsto_at_top_of_tendsto_nhds (hc.tendsto.mono_left nhds_within_le_nhds).norm
      (tendsto_norm_zpow_nhds_within_0_at_top hm)
end

@[simp] lemma continuous_at_inv {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] {x : ğ•œ} :
  continuous_at has_inv.inv x â†” x â‰  0 :=
by simpa [(@zero_lt_one â„¤ _ _).not_le] using @continuous_at_zpow _ _ (-1) x

end normed_field

lemma is_o_pow_pow_of_lt_left {râ‚ râ‚‚ : â„} (hâ‚ : 0 â‰¤ râ‚) (hâ‚‚ : râ‚ < râ‚‚) :
  is_o (Î» n : â„•, râ‚ ^ n) (Î» n, râ‚‚ ^ n) at_top :=
have H : 0 < râ‚‚ := hâ‚.trans_lt hâ‚‚,
is_o_of_tendsto (Î» n hn, false.elim $ H.ne' $ pow_eq_zero hn) $
  (tendsto_pow_at_top_nhds_0_of_lt_1 (div_nonneg hâ‚ (hâ‚.trans hâ‚‚.le)) ((div_lt_one H).2 hâ‚‚)).congr
    (Î» n, div_pow _ _ _)

lemma is_O_pow_pow_of_le_left {râ‚ râ‚‚ : â„} (hâ‚ : 0 â‰¤ râ‚) (hâ‚‚ : râ‚ â‰¤ râ‚‚) :
  is_O (Î» n : â„•, râ‚ ^ n) (Î» n, râ‚‚ ^ n) at_top :=
hâ‚‚.eq_or_lt.elim (Î» h, h â–¸ is_O_refl _ _) (Î» h, (is_o_pow_pow_of_lt_left hâ‚ h).is_O)

lemma is_o_pow_pow_of_abs_lt_left {râ‚ râ‚‚ : â„} (h : |râ‚| < |râ‚‚|) :
  is_o (Î» n : â„•, râ‚ ^ n) (Î» n, râ‚‚ ^ n) at_top :=
begin
  refine (is_o.of_norm_left _).of_norm_right,
  exact (is_o_pow_pow_of_lt_left (abs_nonneg râ‚) h).congr (pow_abs râ‚) (pow_abs râ‚‚)
end

/-- Various statements equivalent to the fact that `f n` grows exponentially slower than `R ^ n`.

* 0: $f n = o(a ^ n)$ for some $-R < a < R$;
* 1: $f n = o(a ^ n)$ for some $0 < a < R$;
* 2: $f n = O(a ^ n)$ for some $-R < a < R$;
* 3: $f n = O(a ^ n)$ for some $0 < a < R$;
* 4: there exist `a < R` and `C` such that one of `C` and `R` is positive and $|f n| â‰¤ Ca^n$
     for all `n`;
* 5: there exists `0 < a < R` and a positive `C` such that $|f n| â‰¤ Ca^n$ for all `n`;
* 6: there exists `a < R` such that $|f n| â‰¤ a ^ n$ for sufficiently large `n`;
* 7: there exists `0 < a < R` such that $|f n| â‰¤ a ^ n$ for sufficiently large `n`.

NB: For backwards compatibility, if you add more items to the list, please append them at the end of
the list. -/
lemma tfae_exists_lt_is_o_pow (f : â„• â†’ â„) (R : â„) :
  tfae [âˆƒ a âˆˆ Ioo (-R) R, is_o f (pow a) at_top,
    âˆƒ a âˆˆ Ioo 0 R, is_o f (pow a) at_top,
    âˆƒ a âˆˆ Ioo (-R) R, is_O f (pow a) at_top,
    âˆƒ a âˆˆ Ioo 0 R, is_O f (pow a) at_top,
    âˆƒ (a < R) C (hâ‚€ : 0 < C âˆ¨ 0 < R), âˆ€ n, |f n| â‰¤ C * a ^ n,
    âˆƒ (a âˆˆ Ioo 0 R) (C > 0), âˆ€ n, |f n| â‰¤ C * a ^ n,
    âˆƒ a < R, âˆ€á¶  n in at_top, |f n| â‰¤ a ^ n,
    âˆƒ a âˆˆ Ioo 0 R, âˆ€á¶  n in at_top, |f n| â‰¤ a ^ n] :=
begin
  have A : Ico 0 R âŠ† Ioo (-R) R,
    from Î» x hx, âŸ¨(neg_lt_zero.2 (hx.1.trans_lt hx.2)).trans_le hx.1, hx.2âŸ©,
  have B : Ioo 0 R âŠ† Ioo (-R) R := subset.trans Ioo_subset_Ico_self A,
  -- First we prove that 1-4 are equivalent using 2 â†’ 3 â†’ 4, 1 â†’ 3, and 2 â†’ 1
  tfae_have : 1 â†’ 3, from Î» âŸ¨a, ha, HâŸ©, âŸ¨a, ha, H.is_OâŸ©,
  tfae_have : 2 â†’ 1, from Î» âŸ¨a, ha, HâŸ©, âŸ¨a, B ha, HâŸ©,
  tfae_have : 3 â†’ 2,
  { rintro âŸ¨a, ha, HâŸ©,
    rcases exists_between (abs_lt.2 ha) with âŸ¨b, hab, hbRâŸ©,
    exact âŸ¨b, âŸ¨(abs_nonneg a).trans_lt hab, hbRâŸ©,
      H.trans_is_o (is_o_pow_pow_of_abs_lt_left (hab.trans_le (le_abs_self b)))âŸ© },
  tfae_have : 2 â†’ 4, from Î» âŸ¨a, ha, HâŸ©, âŸ¨a, ha, H.is_OâŸ©,
  tfae_have : 4 â†’ 3, from Î» âŸ¨a, ha, HâŸ©, âŸ¨a, B ha, HâŸ©,
  -- Add 5 and 6 using 4 â†’ 6 â†’ 5 â†’ 3
  tfae_have : 4 â†’ 6,
  { rintro âŸ¨a, ha, HâŸ©,
    rcases bound_of_is_O_nat_at_top H with âŸ¨C, hCâ‚€, hCâŸ©,
    refine âŸ¨a, ha, C, hCâ‚€, Î» n, _âŸ©,
    simpa only [real.norm_eq_abs, abs_pow, abs_of_nonneg ha.1.le]
      using hC (pow_ne_zero n ha.1.ne') },
  tfae_have : 6 â†’ 5, from Î» âŸ¨a, ha, C, Hâ‚€, HâŸ©, âŸ¨a, ha.2, C, or.inl Hâ‚€, HâŸ©,
  tfae_have : 5 â†’ 3,
  { rintro âŸ¨a, ha, C, hâ‚€, HâŸ©,
    rcases sign_cases_of_C_mul_pow_nonneg (Î» n, (abs_nonneg _).trans (H n)) with rfl | âŸ¨hCâ‚€, haâ‚€âŸ©,
    { obtain rfl : f = 0, by { ext n, simpa using H n },
      simp only [lt_irrefl, false_or] at hâ‚€,
      exact âŸ¨0, âŸ¨neg_lt_zero.2 hâ‚€, hâ‚€âŸ©, is_O_zero _ _âŸ© },
    exact âŸ¨a, A âŸ¨haâ‚€, haâŸ©,
      is_O_of_le' _ (Î» n, (H n).trans $ mul_le_mul_of_nonneg_left (le_abs_self _) hCâ‚€.le)âŸ© },
  -- Add 7 and 8 using 2 â†’ 8 â†’ 7 â†’ 3
  tfae_have : 2 â†’ 8,
  { rintro âŸ¨a, ha, HâŸ©,
    refine âŸ¨a, ha, (H.def zero_lt_one).mono (Î» n hn, _)âŸ©,
    rwa [real.norm_eq_abs, real.norm_eq_abs, one_mul, abs_pow, abs_of_pos ha.1] at hn },
  tfae_have : 8 â†’ 7, from Î» âŸ¨a, ha, HâŸ©, âŸ¨a, ha.2, HâŸ©,
  tfae_have : 7 â†’ 3,
  { rintro âŸ¨a, ha, HâŸ©,
    have : 0 â‰¤ a, from nonneg_of_eventually_pow_nonneg (H.mono $ Î» n, (abs_nonneg _).trans),
    refine âŸ¨a, A âŸ¨this, haâŸ©, is_O.of_bound 1 _âŸ©,
    simpa only [real.norm_eq_abs, one_mul, abs_pow, abs_of_nonneg this] },
  tfae_finish
end

/-- For any natural `k` and a real `r > 1` we have `n ^ k = o(r ^ n)` as `n â†’ âˆ`. -/
lemma is_o_pow_const_const_pow_of_one_lt {R : Type*} [normed_ring R] (k : â„•) {r : â„} (hr : 1 < r) :
  is_o (Î» n, n ^ k : â„• â†’ R) (Î» n, r ^ n) at_top :=
begin
  have : tendsto (Î» x : â„, x ^ k) (ğ“[>] 1) (ğ“ 1),
    from ((continuous_id.pow k).tendsto' (1 : â„) 1 (one_pow _)).mono_left inf_le_left,
  obtain âŸ¨r' : â„, hr' : r' ^ k < r, h1 : 1 < r'âŸ© :=
    ((this.eventually (gt_mem_nhds hr)).and self_mem_nhds_within).exists,
  have h0 : 0 â‰¤ r' := zero_le_one.trans h1.le,
  suffices : is_O _ (Î» n : â„•, (r' ^ k) ^ n) at_top,
    from this.trans_is_o (is_o_pow_pow_of_lt_left (pow_nonneg h0 _) hr'),
  conv in ((r' ^ _) ^ _) { rw [â† pow_mul, mul_comm, pow_mul] },
  suffices : âˆ€ n : â„•, âˆ¥(n : R)âˆ¥ â‰¤ (r' - 1)â»Â¹ * âˆ¥(1 : R)âˆ¥ * âˆ¥r' ^ nâˆ¥,
    from (is_O_of_le' _ this).pow _,
  intro n, rw mul_right_comm,
  refine n.norm_cast_le.trans (mul_le_mul_of_nonneg_right _ (norm_nonneg _)),
  simpa [div_eq_inv_mul, real.norm_eq_abs, abs_of_nonneg h0] using n.cast_le_pow_div_sub h1
end

/-- For a real `r > 1` we have `n = o(r ^ n)` as `n â†’ âˆ`. -/
lemma is_o_coe_const_pow_of_one_lt {R : Type*} [normed_ring R] {r : â„} (hr : 1 < r) :
  is_o (coe : â„• â†’ R) (Î» n, r ^ n) at_top :=
by simpa only [pow_one] using is_o_pow_const_const_pow_of_one_lt 1 hr

/-- If `âˆ¥râ‚âˆ¥ < râ‚‚`, then for any naturak `k` we have `n ^ k râ‚ ^ n = o (râ‚‚ ^ n)` as `n â†’ âˆ`. -/
lemma is_o_pow_const_mul_const_pow_const_pow_of_norm_lt {R : Type*} [normed_ring R] (k : â„•)
  {râ‚ : R} {râ‚‚ : â„} (h : âˆ¥râ‚âˆ¥ < râ‚‚) :
  is_o (Î» n, n ^ k * râ‚ ^ n : â„• â†’ R) (Î» n, râ‚‚ ^ n) at_top :=
begin
  by_cases h0 : râ‚ = 0,
  { refine (is_o_zero _ _).congr' (mem_at_top_sets.2 $ âŸ¨1, Î» n hn, _âŸ©) eventually_eq.rfl,
    simp [zero_pow (zero_lt_one.trans_le hn), h0] },
  rw [â† ne.def, â† norm_pos_iff] at h0,
  have A : is_o (Î» n, n ^ k : â„• â†’ R) (Î» n, (râ‚‚ / âˆ¥râ‚âˆ¥) ^ n) at_top,
    from is_o_pow_const_const_pow_of_one_lt k ((one_lt_div h0).2 h),
  suffices : is_O (Î» n, râ‚ ^ n) (Î» n, âˆ¥râ‚âˆ¥ ^ n) at_top,
    by simpa [div_mul_cancel _ (pow_pos h0 _).ne'] using A.mul_is_O this,
  exact is_O.of_bound 1 (by simpa using eventually_norm_pow_le râ‚)
end

lemma tendsto_pow_const_div_const_pow_of_one_lt (k : â„•) {r : â„} (hr : 1 < r) :
  tendsto (Î» n, n ^ k / r ^ n : â„• â†’ â„) at_top (ğ“ 0) :=
(is_o_pow_const_const_pow_of_one_lt k hr).tendsto_div_nhds_zero

/-- If `|r| < 1`, then `n ^ k r ^ n` tends to zero for any natural `k`. -/
lemma tendsto_pow_const_mul_const_pow_of_abs_lt_one (k : â„•) {r : â„} (hr : |r| < 1) :
  tendsto (Î» n, n ^ k * r ^ n : â„• â†’ â„) at_top (ğ“ 0) :=
begin
  by_cases h0 : r = 0,
  { exact tendsto_const_nhds.congr'
      (mem_at_top_sets.2 âŸ¨1, Î» n hn, by simp [zero_lt_one.trans_le hn, h0]âŸ©) },
  have hr' : 1 < (|r|)â»Â¹, from one_lt_inv (abs_pos.2 h0) hr,
  rw tendsto_zero_iff_norm_tendsto_zero,
  simpa [div_eq_mul_inv] using tendsto_pow_const_div_const_pow_of_one_lt k hr'
end

/-- If `0 â‰¤ r < 1`, then `n ^ k r ^ n` tends to zero for any natural `k`.
This is a specialized version of `tendsto_pow_const_mul_const_pow_of_abs_lt_one`, singled out
for ease of application. -/
lemma tendsto_pow_const_mul_const_pow_of_lt_one (k : â„•) {r : â„} (hr : 0 â‰¤ r) (h'r : r < 1) :
  tendsto (Î» n, n ^ k * r ^ n : â„• â†’ â„) at_top (ğ“ 0) :=
tendsto_pow_const_mul_const_pow_of_abs_lt_one k (abs_lt.2 âŸ¨neg_one_lt_zero.trans_le hr, h'râŸ©)

/-- If `|r| < 1`, then `n * r ^ n` tends to zero. -/
lemma tendsto_self_mul_const_pow_of_abs_lt_one {r : â„} (hr : |r| < 1) :
  tendsto (Î» n, n * r ^ n : â„• â†’ â„) at_top (ğ“ 0) :=
by simpa only [pow_one] using tendsto_pow_const_mul_const_pow_of_abs_lt_one 1 hr

/-- If `0 â‰¤ r < 1`, then `n * r ^ n` tends to zero. This is a specialized version of
`tendsto_self_mul_const_pow_of_abs_lt_one`, singled out for ease of application. -/
lemma tendsto_self_mul_const_pow_of_lt_one {r : â„} (hr : 0 â‰¤ r) (h'r : r < 1) :
  tendsto (Î» n, n * r ^ n : â„• â†’ â„) at_top (ğ“ 0) :=
by simpa only [pow_one] using tendsto_pow_const_mul_const_pow_of_lt_one 1 hr h'r

/-- In a normed ring, the powers of an element x with `âˆ¥xâˆ¥ < 1` tend to zero. -/
lemma tendsto_pow_at_top_nhds_0_of_norm_lt_1 {R : Type*} [normed_ring R] {x : R}
  (h : âˆ¥xâˆ¥ < 1) : tendsto (Î» (n : â„•), x ^ n) at_top (ğ“ 0) :=
begin
  apply squeeze_zero_norm' (eventually_norm_pow_le x),
  exact tendsto_pow_at_top_nhds_0_of_lt_1 (norm_nonneg _) h,
end

lemma tendsto_pow_at_top_nhds_0_of_abs_lt_1 {r : â„} (h : |r| < 1) :
  tendsto (Î»n:â„•, r^n) at_top (ğ“ 0) :=
tendsto_pow_at_top_nhds_0_of_norm_lt_1 h

/-! ### Geometric series-/
section geometric

variables {K : Type*} [normed_field K] {Î¾ : K}

lemma has_sum_geometric_of_norm_lt_1 (h : âˆ¥Î¾âˆ¥ < 1) : has_sum (Î»n:â„•, Î¾ ^ n) (1 - Î¾)â»Â¹ :=
begin
  have xi_ne_one : Î¾ â‰  1, by { contrapose! h, simp [h] },
  have A : tendsto (Î»n, (Î¾ ^ n - 1) * (Î¾ - 1)â»Â¹) at_top (ğ“ ((0 - 1) * (Î¾ - 1)â»Â¹)),
    from ((tendsto_pow_at_top_nhds_0_of_norm_lt_1 h).sub tendsto_const_nhds).mul tendsto_const_nhds,
  have B : (Î» n, (âˆ‘ i in range n, Î¾ ^ i)) = (Î» n, geom_sum Î¾ n) := rfl,
  rw [has_sum_iff_tendsto_nat_of_summable_norm, B],
  { simpa [geom_sum_eq, xi_ne_one, neg_inv, div_eq_mul_inv] using A },
  { simp [norm_pow, summable_geometric_of_lt_1 (norm_nonneg _) h] }
end

lemma summable_geometric_of_norm_lt_1 (h : âˆ¥Î¾âˆ¥ < 1) : summable (Î»n:â„•, Î¾ ^ n) :=
âŸ¨_, has_sum_geometric_of_norm_lt_1 hâŸ©

lemma tsum_geometric_of_norm_lt_1 (h : âˆ¥Î¾âˆ¥ < 1) : âˆ‘'n:â„•, Î¾ ^ n = (1 - Î¾)â»Â¹ :=
(has_sum_geometric_of_norm_lt_1 h).tsum_eq

lemma has_sum_geometric_of_abs_lt_1 {r : â„} (h : |r| < 1) : has_sum (Î»n:â„•, r ^ n) (1 - r)â»Â¹ :=
has_sum_geometric_of_norm_lt_1 h

lemma summable_geometric_of_abs_lt_1 {r : â„} (h : |r| < 1) : summable (Î»n:â„•, r ^ n) :=
summable_geometric_of_norm_lt_1 h

lemma tsum_geometric_of_abs_lt_1 {r : â„} (h : |r| < 1) : âˆ‘'n:â„•, r ^ n = (1 - r)â»Â¹ :=
tsum_geometric_of_norm_lt_1 h

/-- A geometric series in a normed field is summable iff the norm of the common ratio is less than
one. -/
@[simp] lemma summable_geometric_iff_norm_lt_1 : summable (Î» n : â„•, Î¾ ^ n) â†” âˆ¥Î¾âˆ¥ < 1 :=
begin
  refine âŸ¨Î» h, _, summable_geometric_of_norm_lt_1âŸ©,
  obtain âŸ¨k : â„•, hk : dist (Î¾ ^ k) 0 < 1âŸ© :=
    (h.tendsto_cofinite_zero.eventually (ball_mem_nhds _ zero_lt_one)).exists,
  simp only [norm_pow, dist_zero_right] at hk,
  rw [â† one_pow k] at hk,
  exact lt_of_pow_lt_pow _ zero_le_one hk
end

end geometric

section mul_geometric

lemma summable_norm_pow_mul_geometric_of_norm_lt_1 {R : Type*} [normed_ring R]
  (k : â„•) {r : R} (hr : âˆ¥râˆ¥ < 1) : summable (Î» n : â„•, âˆ¥(n ^ k * r ^ n : R)âˆ¥) :=
begin
  rcases exists_between hr with âŸ¨r', hrr', hâŸ©,
  exact summable_of_is_O_nat (summable_geometric_of_lt_1 ((norm_nonneg _).trans hrr'.le) h)
    (is_o_pow_const_mul_const_pow_const_pow_of_norm_lt _ hrr').is_O.norm_left
end

lemma summable_pow_mul_geometric_of_norm_lt_1 {R : Type*} [normed_ring R] [complete_space R]
  (k : â„•) {r : R} (hr : âˆ¥râˆ¥ < 1) : summable (Î» n, n ^ k * r ^ n : â„• â†’ R) :=
summable_of_summable_norm $ summable_norm_pow_mul_geometric_of_norm_lt_1 _ hr

/-- If `âˆ¥râˆ¥ < 1`, then `âˆ‘' n : â„•, n * r ^ n = r / (1 - r) ^ 2`, `has_sum` version. -/
lemma has_sum_coe_mul_geometric_of_norm_lt_1 {ğ•œ : Type*} [normed_field ğ•œ] [complete_space ğ•œ]
  {r : ğ•œ} (hr : âˆ¥râˆ¥ < 1) : has_sum (Î» n, n * r ^ n : â„• â†’ ğ•œ) (r / (1 - r) ^ 2) :=
begin
  have A : summable (Î» n, n * r ^ n : â„• â†’ ğ•œ),
    by simpa using summable_pow_mul_geometric_of_norm_lt_1 1 hr,
  have B : has_sum (pow r : â„• â†’ ğ•œ) (1 - r)â»Â¹, from has_sum_geometric_of_norm_lt_1 hr,
  refine A.has_sum_iff.2 _,
  have hr' : r â‰  1, by { rintro rfl, simpa [lt_irrefl] using hr },
  set s : ğ•œ := âˆ‘' n : â„•, n * r ^ n,
  calc s = (1 - r) * s / (1 - r) : (mul_div_cancel_left _ (sub_ne_zero.2 hr'.symm)).symm
  ... = (s - r * s) / (1 - r) : by rw [sub_mul, one_mul]
  ... = ((0 : â„•) * r ^ 0 + (âˆ‘' n : â„•, (n + 1) * r ^ (n + 1)) - r * s) / (1 - r) :
    by { congr, exact tsum_eq_zero_add A }
  ... = (r * (âˆ‘' n : â„•, (n + 1) * r ^ n) - r * s) / (1 - r) :
    by simp [pow_succ, mul_left_comm _ r, tsum_mul_left]
  ... = r / (1 - r) ^ 2 :
    by simp [add_mul, tsum_add A B.summable, mul_add, B.tsum_eq, â† div_eq_mul_inv, sq,
      div_div]
end

/-- If `âˆ¥râˆ¥ < 1`, then `âˆ‘' n : â„•, n * r ^ n = r / (1 - r) ^ 2`. -/
lemma tsum_coe_mul_geometric_of_norm_lt_1 {ğ•œ : Type*} [normed_field ğ•œ] [complete_space ğ•œ]
  {r : ğ•œ} (hr : âˆ¥râˆ¥ < 1) :
  (âˆ‘' n : â„•, n * r ^ n : ğ•œ) = (r / (1 - r) ^ 2) :=
(has_sum_coe_mul_geometric_of_norm_lt_1 hr).tsum_eq

end mul_geometric

section summable_le_geometric

variables [semi_normed_group Î±] {r C : â„} {f : â„• â†’ Î±}

lemma semi_normed_group.cauchy_seq_of_le_geometric {C : â„} {r : â„} (hr : r < 1)
  {u : â„• â†’ Î±} (h : âˆ€ n, âˆ¥u n - u (n + 1)âˆ¥ â‰¤ C*r^n) : cauchy_seq u :=
cauchy_seq_of_le_geometric r C hr (by simpa [dist_eq_norm] using h)

lemma dist_partial_sum_le_of_le_geometric (hf : âˆ€n, âˆ¥f nâˆ¥ â‰¤ C * r^n) (n : â„•) :
  dist (âˆ‘ i in range n, f i) (âˆ‘ i in range (n+1), f i) â‰¤ C * r ^ n :=
begin
  rw [sum_range_succ, dist_eq_norm, â† norm_neg, neg_sub, add_sub_cancel'],
  exact hf n,
end

/-- If `âˆ¥f nâˆ¥ â‰¤ C * r ^ n` for all `n : â„•` and some `r < 1`, then the partial sums of `f` form a
Cauchy sequence. This lemma does not assume `0 â‰¤ r` or `0 â‰¤ C`. -/
lemma cauchy_seq_finset_of_geometric_bound (hr : r < 1) (hf : âˆ€n, âˆ¥f nâˆ¥ â‰¤ C * r^n) :
  cauchy_seq (Î» s : finset (â„•), âˆ‘ x in s, f x) :=
cauchy_seq_finset_of_norm_bounded _
  (aux_has_sum_of_le_geometric hr (dist_partial_sum_le_of_le_geometric hf)).summable hf

/-- If `âˆ¥f nâˆ¥ â‰¤ C * r ^ n` for all `n : â„•` and some `r < 1`, then the partial sums of `f` are within
distance `C * r ^ n / (1 - r)` of the sum of the series. This lemma does not assume `0 â‰¤ r` or
`0 â‰¤ C`. -/
lemma norm_sub_le_of_geometric_bound_of_has_sum (hr : r < 1) (hf : âˆ€n, âˆ¥f nâˆ¥ â‰¤ C * r^n)
  {a : Î±} (ha : has_sum f a) (n : â„•) :
  âˆ¥(âˆ‘ x in finset.range n, f x) - aâˆ¥ â‰¤ (C * r ^ n) / (1 - r) :=
begin
  rw â† dist_eq_norm,
  apply dist_le_of_le_geometric_of_tendsto r C hr (dist_partial_sum_le_of_le_geometric hf),
  exact ha.tendsto_sum_nat
end

@[simp] lemma dist_partial_sum (u : â„• â†’ Î±) (n : â„•) :
 dist (âˆ‘ k in range (n + 1), u k) (âˆ‘ k in range n, u k) = âˆ¥u nâˆ¥ :=
by simp [dist_eq_norm, sum_range_succ]

@[simp] lemma dist_partial_sum' (u : â„• â†’ Î±) (n : â„•) :
 dist (âˆ‘ k in range n, u k) (âˆ‘ k in range (n+1), u k) = âˆ¥u nâˆ¥ :=
by simp [dist_eq_norm', sum_range_succ]

lemma cauchy_series_of_le_geometric {C : â„} {u : â„• â†’ Î±}
  {r : â„} (hr : r < 1) (h : âˆ€ n, âˆ¥u nâˆ¥ â‰¤ C*r^n) : cauchy_seq (Î» n, âˆ‘ k in range n, u k) :=
cauchy_seq_of_le_geometric r C hr (by simp [h])

lemma normed_group.cauchy_series_of_le_geometric' {C : â„} {u : â„• â†’ Î±} {r : â„} (hr : r < 1)
  (h : âˆ€ n, âˆ¥u nâˆ¥ â‰¤ C*r^n) : cauchy_seq (Î» n, âˆ‘ k in range (n + 1), u k) :=
(cauchy_series_of_le_geometric hr h).comp_tendsto $ tendsto_add_at_top_nat 1

lemma normed_group.cauchy_series_of_le_geometric'' {C : â„} {u : â„• â†’ Î±} {N : â„•} {r : â„}
  (hrâ‚€ : 0 < r) (hrâ‚ : r < 1)
  (h : âˆ€ n â‰¥ N, âˆ¥u nâˆ¥ â‰¤ C*r^n) : cauchy_seq (Î» n, âˆ‘ k in range (n + 1), u k) :=
begin
  set v : â„• â†’ Î± := Î» n, if n < N then 0 else u n,
  have hC : 0 â‰¤ C,
    from (zero_le_mul_right $ pow_pos hrâ‚€ N).mp ((norm_nonneg _).trans $ h N $ le_refl N),
  have : âˆ€ n â‰¥ N, u n = v n,
  { intros n hn,
    simp [v, hn, if_neg (not_lt.mpr hn)] },
  refine cauchy_seq_sum_of_eventually_eq this (normed_group.cauchy_series_of_le_geometric' hrâ‚ _),
  { exact C },
  intro n,
  dsimp [v],
  split_ifs with H H,
  { rw norm_zero,
    exact mul_nonneg hC (pow_nonneg hrâ‚€.le _) },
  { push_neg at H,
    exact h _ H }
end

end summable_le_geometric

section normed_ring_geometric
variables {R : Type*} [normed_ring R] [complete_space R]

open normed_space

/-- A geometric series in a complete normed ring is summable.
Proved above (same name, different namespace) for not-necessarily-complete normed fields. -/
lemma normed_ring.summable_geometric_of_norm_lt_1
  (x : R) (h : âˆ¥xâˆ¥ < 1) : summable (Î» (n:â„•), x ^ n) :=
begin
  have h1 : summable (Î» (n:â„•), âˆ¥xâˆ¥ ^ n) := summable_geometric_of_lt_1 (norm_nonneg _) h,
  refine summable_of_norm_bounded_eventually _ h1 _,
  rw nat.cofinite_eq_at_top,
  exact eventually_norm_pow_le x,
end

/-- Bound for the sum of a geometric series in a normed ring.  This formula does not assume that the
normed ring satisfies the axiom `âˆ¥1âˆ¥ = 1`. -/
lemma normed_ring.tsum_geometric_of_norm_lt_1
  (x : R) (h : âˆ¥xâˆ¥ < 1) : âˆ¥âˆ‘' n:â„•, x ^ nâˆ¥ â‰¤ âˆ¥(1:R)âˆ¥ - 1 + (1 - âˆ¥xâˆ¥)â»Â¹ :=
begin
  rw tsum_eq_zero_add (normed_ring.summable_geometric_of_norm_lt_1 x h),
  simp only [pow_zero],
  refine le_trans (norm_add_le _ _) _,
  have : âˆ¥âˆ‘' b : â„•, (Î» n, x ^ (n + 1)) bâˆ¥ â‰¤ (1 - âˆ¥xâˆ¥)â»Â¹ - 1,
  { refine tsum_of_norm_bounded _ (Î» b, norm_pow_le' _ (nat.succ_pos b)),
    convert (has_sum_nat_add_iff' 1).mpr (has_sum_geometric_of_lt_1 (norm_nonneg x) h),
    simp },
  linarith
end

lemma geom_series_mul_neg (x : R) (h : âˆ¥xâˆ¥ < 1) :
  (âˆ‘' i:â„•, x ^ i) * (1 - x) = 1 :=
begin
  have := ((normed_ring.summable_geometric_of_norm_lt_1 x h).has_sum.mul_right (1 - x)),
  refine tendsto_nhds_unique this.tendsto_sum_nat _,
  have : tendsto (Î» (n : â„•), 1 - x ^ n) at_top (ğ“ 1),
  { simpa using tendsto_const_nhds.sub (tendsto_pow_at_top_nhds_0_of_norm_lt_1 h) },
  convert â† this,
  ext n,
  rw [â†geom_sum_mul_neg, geom_sum_def, finset.sum_mul],
end

lemma mul_neg_geom_series (x : R) (h : âˆ¥xâˆ¥ < 1) :
  (1 - x) * âˆ‘' i:â„•, x ^ i = 1 :=
begin
  have := (normed_ring.summable_geometric_of_norm_lt_1 x h).has_sum.mul_left (1 - x),
  refine tendsto_nhds_unique this.tendsto_sum_nat _,
  have : tendsto (Î» (n : â„•), 1 - x ^ n) at_top (nhds 1),
  { simpa using tendsto_const_nhds.sub
      (tendsto_pow_at_top_nhds_0_of_norm_lt_1 h) },
  convert â† this,
  ext n,
  rw [â†mul_neg_geom_sum, geom_sum_def, finset.mul_sum]
end

end normed_ring_geometric

/-! ### Summability tests based on comparison with geometric series -/

lemma summable_of_ratio_norm_eventually_le {Î± : Type*} [semi_normed_group Î±] [complete_space Î±]
  {f : â„• â†’ Î±} {r : â„} (hrâ‚ : r < 1)
  (h : âˆ€á¶  n in at_top, âˆ¥f (n+1)âˆ¥ â‰¤ r * âˆ¥f nâˆ¥) : summable f :=
begin
  by_cases hrâ‚€ : 0 â‰¤ r,
  { rw eventually_at_top at h,
    rcases h with âŸ¨N, hNâŸ©,
    rw â† @summable_nat_add_iff Î± _ _ _ _ N,
    refine summable_of_norm_bounded (Î» n, âˆ¥f Nâˆ¥ * r^n)
      (summable.mul_left _ $ summable_geometric_of_lt_1 hrâ‚€ hrâ‚) (Î» n, _),
    conv_rhs {rw [mul_comm, â† zero_add N]},
    refine le_geom hrâ‚€ n (Î» i _, _),
    convert hN (i + N) (N.le_add_left i) using 3,
    ac_refl },
  { push_neg at hrâ‚€,
    refine summable_of_norm_bounded_eventually 0 summable_zero _,
    rw nat.cofinite_eq_at_top,
    filter_upwards [h] with _ hn,
    by_contra' h,
    exact not_lt.mpr (norm_nonneg _) (lt_of_le_of_lt hn $ mul_neg_of_neg_of_pos hrâ‚€ h), },
end

lemma summable_of_ratio_test_tendsto_lt_one {Î± : Type*} [normed_group Î±] [complete_space Î±]
  {f : â„• â†’ Î±} {l : â„} (hlâ‚ : l < 1) (hf : âˆ€á¶  n in at_top, f n â‰  0)
  (h : tendsto (Î» n, âˆ¥f (n+1)âˆ¥/âˆ¥f nâˆ¥) at_top (ğ“ l)) : summable f :=
begin
  rcases exists_between hlâ‚ with âŸ¨r, hrâ‚€, hrâ‚âŸ©,
  refine summable_of_ratio_norm_eventually_le hrâ‚ _,
  filter_upwards [eventually_le_of_tendsto_lt hrâ‚€ h, hf] with _ _ hâ‚,
  rwa â† div_le_iff (norm_pos_iff.mpr hâ‚),
end

lemma not_summable_of_ratio_norm_eventually_ge {Î± : Type*} [semi_normed_group Î±]
  {f : â„• â†’ Î±} {r : â„} (hr : 1 < r) (hf : âˆƒá¶  n in at_top, âˆ¥f nâˆ¥ â‰  0)
  (h : âˆ€á¶  n in at_top, r * âˆ¥f nâˆ¥ â‰¤ âˆ¥f (n+1)âˆ¥) : Â¬ summable f :=
begin
  rw eventually_at_top at h,
  rcases h with âŸ¨Nâ‚€, hNâ‚€âŸ©,
  rw frequently_at_top at hf,
  rcases hf Nâ‚€ with âŸ¨N, hNNâ‚€ : Nâ‚€ â‰¤ N, hNâŸ©,
  rw â† @summable_nat_add_iff Î± _ _ _ _ N,
  refine mt summable.tendsto_at_top_zero
    (Î» h', not_tendsto_at_top_of_tendsto_nhds (tendsto_norm_zero.comp h') _),
  convert tendsto_at_top_of_geom_le _ hr _,
  { refine lt_of_le_of_ne (norm_nonneg _) _,
    intro h'',
    specialize hNâ‚€ N hNNâ‚€,
    simp only [comp_app, zero_add] at h'',
    exact hN h''.symm },
  { intro i,
    dsimp only [comp_app],
    convert (hNâ‚€ (i + N) (hNNâ‚€.trans (N.le_add_left i))) using 3,
    ac_refl }
end

lemma not_summable_of_ratio_test_tendsto_gt_one {Î± : Type*} [semi_normed_group Î±]
  {f : â„• â†’ Î±} {l : â„} (hl : 1 < l)
  (h : tendsto (Î» n, âˆ¥f (n+1)âˆ¥/âˆ¥f nâˆ¥) at_top (ğ“ l)) : Â¬ summable f :=
begin
  have key : âˆ€á¶  n in at_top, âˆ¥f nâˆ¥ â‰  0,
  { filter_upwards [eventually_ge_of_tendsto_gt hl h] with _ hn hc,
    rw [hc, div_zero] at hn,
    linarith },
  rcases exists_between hl with âŸ¨r, hrâ‚€, hrâ‚âŸ©,
  refine not_summable_of_ratio_norm_eventually_ge hrâ‚€ key.frequently _,
  filter_upwards [eventually_ge_of_tendsto_gt hrâ‚ h, key] with _ _ hâ‚,
  rwa â† le_div_iff (lt_of_le_of_ne (norm_nonneg _) hâ‚.symm)
end

section
/-! ### Dirichlet and alternating series tests -/

variables {E : Type*} [normed_group E] [normed_space â„ E]
variables {b : â„} {f : â„• â†’ â„} {z : â„• â†’ E}

/-- **Dirichlet's Test** for monotone sequences. -/
theorem monotone.cauchy_seq_series_mul_of_tendsto_zero_of_bounded
  (hfa : monotone f) (hf0 : tendsto f at_top (ğ“ 0)) (hgb : âˆ€ n, âˆ¥âˆ‘ i in range n, z iâˆ¥ â‰¤ b) :
  cauchy_seq (Î» n, âˆ‘ i in range (n + 1), (f i) â€¢ z i) :=
begin
  simp_rw [finset.sum_range_by_parts _ _ (nat.succ _), sub_eq_add_neg,
           nat.succ_sub_succ_eq_sub, tsub_zero],
  apply (normed_field.tendsto_zero_smul_of_tendsto_zero_of_bounded hf0
    âŸ¨b, eventually_map.mpr $ eventually_of_forall $ Î» n, hgb $ n+1âŸ©).cauchy_seq.add,
  apply (cauchy_seq_range_of_norm_bounded _ _ (_ : âˆ€ n, _ â‰¤ b * |f(n+1) - f(n)|)).neg,
  { exact normed_uniform_group },
  { simp_rw [abs_of_nonneg (sub_nonneg_of_le (hfa (nat.le_succ _))), â† mul_sum],
    apply real.uniform_continuous_mul_const.comp_cauchy_seq,
    simp_rw [sum_range_sub, sub_eq_add_neg],
    exact (tendsto.cauchy_seq hf0).add_const },
  { intro n,
    rw [norm_smul, mul_comm],
    exact mul_le_mul_of_nonneg_right (hgb _) (abs_nonneg _) },
end

/-- **Dirichlet's test** for antitone sequences. -/
theorem antitone.cauchy_seq_series_mul_of_tendsto_zero_of_bounded
  (hfa : antitone f) (hf0 : tendsto f at_top (ğ“ 0)) (hzb : âˆ€ n, âˆ¥âˆ‘ i in range n, z iâˆ¥ â‰¤ b) :
  cauchy_seq (Î» n, âˆ‘ i in range (n+1), (f i) â€¢ z i) :=
begin
  have hfa': monotone (Î» n, -f n) := Î» _ _ hab, neg_le_neg $ hfa hab,
  have hf0': tendsto (Î» n, -f n) at_top (ğ“ 0) := by { convert hf0.neg, norm_num },
  convert (hfa'.cauchy_seq_series_mul_of_tendsto_zero_of_bounded hf0' hzb).neg,
  funext,
  simp
end

lemma norm_sum_neg_one_pow_le (n : â„•) : âˆ¥âˆ‘ i in range n, (-1 : â„) ^ iâˆ¥ â‰¤ 1 :=
by { rw [â†geom_sum_def, neg_one_geom_sum], split_ifs; norm_num }

/-- The **alternating series test** for monotone sequences.
See also `tendsto_alternating_series_of_monotone_tendsto_zero`. -/
theorem monotone.cauchy_seq_alternating_series_of_tendsto_zero
  (hfa : monotone f) (hf0 : tendsto f at_top (ğ“ 0)) :
  cauchy_seq (Î» n, âˆ‘ i in range (n+1), (-1) ^ i * f i) :=
begin
  simp_rw [mul_comm],
  exact hfa.cauchy_seq_series_mul_of_tendsto_zero_of_bounded hf0 norm_sum_neg_one_pow_le
end

/-- The **alternating series test** for monotone sequences. -/
theorem monotone.tendsto_alternating_series_of_tendsto_zero
  (hfa : monotone f) (hf0 : tendsto f at_top (ğ“ 0)) :
  âˆƒ l, tendsto (Î» n, âˆ‘ i in range (n+1), (-1) ^ i * f i) at_top (ğ“ l) :=
cauchy_seq_tendsto_of_complete $ hfa.cauchy_seq_alternating_series_of_tendsto_zero hf0

/-- The **alternating series test** for antitone sequences.
See also `tendsto_alternating_series_of_antitone_tendsto_zero`. -/
theorem antitone.cauchy_seq_alternating_series_of_tendsto_zero
  (hfa : antitone f) (hf0 : tendsto f at_top (ğ“ 0)) :
  cauchy_seq (Î» n, âˆ‘ i in range (n+1), (-1) ^ i * f i) :=
begin
  simp_rw [mul_comm],
  exact
    hfa.cauchy_seq_series_mul_of_tendsto_zero_of_bounded hf0 norm_sum_neg_one_pow_le
end

/-- The **alternating series test** for antitone sequences. -/
theorem antitone.tendsto_alternating_series_of_tendsto_zero
  (hfa : antitone f) (hf0 : tendsto f at_top (ğ“ 0)) :
  âˆƒ l, tendsto (Î» n, âˆ‘ i in range (n+1), (-1) ^ i * f i) at_top (ğ“ l) :=
cauchy_seq_tendsto_of_complete $ hfa.cauchy_seq_alternating_series_of_tendsto_zero hf0

end

/-!
### Factorial
-/

/-- The series `âˆ‘' n, x ^ n / n!` is summable of any `x : â„`. See also `exp_series_div_summable`
for a version that also works in `â„‚`, and `exp_series_summable'` for a version that works in
any normed algebra over `â„` or `â„‚`. -/
lemma real.summable_pow_div_factorial (x : â„) :
  summable (Î» n, x ^ n / n! : â„• â†’ â„) :=
begin
  -- We start with trivial extimates
  have A : (0 : â„) < âŒŠâˆ¥xâˆ¥âŒ‹â‚Š + 1, from zero_lt_one.trans_le (by simp),
  have B : âˆ¥xâˆ¥ / (âŒŠâˆ¥xâˆ¥âŒ‹â‚Š + 1) < 1, from (div_lt_one A).2 (nat.lt_floor_add_one _),
  -- Then we apply the ratio test. The estimate works for `n â‰¥ âŒŠâˆ¥xâˆ¥âŒ‹â‚Š`.
  suffices : âˆ€ n â‰¥ âŒŠâˆ¥xâˆ¥âŒ‹â‚Š, âˆ¥x ^ (n + 1) / (n + 1)!âˆ¥ â‰¤ âˆ¥xâˆ¥ / (âŒŠâˆ¥xâˆ¥âŒ‹â‚Š + 1) * âˆ¥x ^ n / â†‘n!âˆ¥,
    from summable_of_ratio_norm_eventually_le B (eventually_at_top.2 âŸ¨âŒŠâˆ¥xâˆ¥âŒ‹â‚Š, thisâŸ©),
  -- Finally, we prove the upper estimate
  intros n hn,
  calc âˆ¥x ^ (n + 1) / (n + 1)!âˆ¥ = (âˆ¥xâˆ¥ / (n + 1)) * âˆ¥x ^ n / n!âˆ¥ :
    by rw [pow_succ, nat.factorial_succ, nat.cast_mul, â† div_mul_div_comm,
      norm_mul, norm_div, real.norm_coe_nat, nat.cast_succ]
  ... â‰¤ (âˆ¥xâˆ¥ / (âŒŠâˆ¥xâˆ¥âŒ‹â‚Š + 1)) * âˆ¥x ^ n / n!âˆ¥ :
    by mono* with [0 â‰¤ âˆ¥x ^ n / n!âˆ¥, 0 â‰¤ âˆ¥xâˆ¥]; apply norm_nonneg
end

lemma real.tendsto_pow_div_factorial_at_top (x : â„) :
  tendsto (Î» n, x ^ n / n! : â„• â†’ â„) at_top (ğ“ 0) :=
(real.summable_pow_div_factorial x).tendsto_at_top_zero
