import algebra.algebra.subalgebra
import topology.algebra.polynomial
import topology.continuous_function.bounded
import analysis.special_functions.bernstein

noncomputable theory

open continuous_map

variables {X : Type*} [topological_space X]
variables {R : Type*} [comm_ring R] [topological_space R] [topological_ring R]

open filter
open_locale topological_space unit_interval

/--
A set is equivalent to its image under an equivalence.
-/
-- We could construct this using `equiv.set.image e s e.injective`,
-- but this definition provides an explicit inverse.
@[simps]
def equiv.image {Œ± Œ≤ : Type*} (e : Œ± ‚âÉ Œ≤) (s : set Œ±) : s ‚âÉ e '' s :=
{ to_fun := Œª x, ‚ü®e x.1, by simp‚ü©,
  inv_fun := Œª y, ‚ü®e.symm y.1, by { rcases y with ‚ü®-, ‚ü®a, ‚ü®m, rfl‚ü©‚ü©‚ü©, simpa using m, }‚ü©,
  left_inv := Œª x, by simp,
  right_inv := Œª y, by simp, }.

@[continuity]
lemma homeomorph.continuous_symm {Œ± Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  (e : Œ± ‚âÉ‚Çú Œ≤) : continuous (e.symm) :=
by continuity

/--
A subset of a topological space is homeomorphic to its image under a homeomorphism.
-/
def homeomorph.image {Œ± Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  (e : Œ± ‚âÉ‚Çú Œ≤) (s : set Œ±) : s ‚âÉ‚Çú e '' s :=
{ continuous_to_fun := by continuity!,
  continuous_inv_fun := by continuity!,
  ..e.to_equiv.image s, }

/--
The forward direction of a homeomorphism, as a bundled continuous map.
-/
@[simps]
def homeomorph.to_continuous_map {Œ± Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  (e : Œ± ‚âÉ‚Çú Œ≤) : C(Œ±, Œ≤) := ‚ü®e‚ü©


/--
The special case of the Weierstrass approximation theorem for the interval `[0,1]`.
This is just a matter of unravelling definitions and using the Bernstein approximations.
-/
theorem polynomial_functions_closure_eq_top' :
  (polynomial_functions I).topological_closure = ‚ä§ :=
begin
  apply eq_top_iff.mpr,
  rintros f -,
  refine filter.frequently.mem_closure _,
  refine filter.tendsto.frequently (bernstein_approximation_uniform f) _,
  apply frequently_of_forall,
  intro n,
  simp only [subalgebra.mem_coe],
  apply subalgebra.sum_mem,
  rintro n -,
  apply subalgebra.smul_mem,
  dsimp [bernstein, polynomial_functions],
  simp,
end

/-!
We now setup variations on `pullback_*` (that is, precomposition by a continuous map),
as a morphism `C(Y, T) ‚Üí C(X, T)`, respecting various types of structure.

In particular:
* `pullback_continuous_map`, the bundled continuous map (for this we need `X Y` compact).
* `pullback_homeomorph`, when we precompose by a homeomorphism.
* `pullback_alg_hom`, when `T = R` is a topological ring.
-/
section pullback

/--
Precomposition by a continuous map is itself a continuous map between spaces of continuous maps.
-/
def pullback_continuous_map {X Y : Type*} (T : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y] [normed_group T]
  (f : C(X, Y)) : C(C(Y, T), C(X, T)) :=
{ to_fun := Œª g, g.comp f,
  continuous_to_fun :=
  begin
    refine metric.continuous_iff.mpr _,
    intros g Œµ Œµ_pos,
    refine ‚ü®Œµ, Œµ_pos, Œª g' h, _‚ü©,
    rw continuous_map.dist_lt_iff _ _ Œµ_pos at h ‚ä¢,
    { exact Œª x, h (f x), },
  end }

@[simp] lemma pullback_continuous_map_apply {X Y : Type*} (T : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y] [normed_group T]
  (f : C(X, Y)) (g : C(Y, T)) :
  (pullback_continuous_map T f) g = g.comp f :=
rfl

/--
Precomposition by a homeomorphism is itself a homeomorphism between spaces of continuous maps.
-/
def pullback_homeomorph {X Y : Type*} (T : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y] [normed_group T]
  (f : X ‚âÉ‚Çú Y) : C(Y, T) ‚âÉ‚Çú C(X, T) :=
{ to_fun := pullback_continuous_map T f.to_continuous_map,
  inv_fun := pullback_continuous_map T f.symm.to_continuous_map,
  left_inv := by tidy,
  right_inv := by tidy, }

/--
Precomposition of functions into a normed ring by continuous map is an algebra homomorphism.
-/
def pullback_alg_hom {X Y : Type*} (R : Type*)
  [topological_space X] [topological_space Y] [normed_comm_ring R] (f : C(X, Y)) :
  C(Y, R) ‚Üí‚Çê[R] C(X, R) :=
{ to_fun := Œª g, g.comp f,
  map_zero' := by { ext, simp, },
  map_add' := Œª g‚ÇÅ g‚ÇÇ, by { ext, simp, },
  map_one' := by { ext, simp, },
  map_mul' := Œª g‚ÇÅ g‚ÇÇ, by { ext, simp, },
  commutes' := Œª r, by { ext, simp, }, }

@[simp] lemma pullback_alg_hom_apply {X Y : Type*} (R : Type*)
  [topological_space X] [topological_space Y] [normed_comm_ring R] (f : C(X, Y)) (g : C(Y, R)) :
  (pullback_alg_hom R f) g = g.comp f :=
rfl

lemma pullback_alg_hom_continuous {X Y : Type*} (R : Type*)
  [topological_space X] [compact_space X] [topological_space Y] [compact_space Y]
  [normed_comm_ring R] (f : C(X, Y)) :
  continuous (pullback_alg_hom R f) :=
begin
  change continuous (pullback_continuous_map R f),
  continuity,
end

end pullback

section
variables {ùïú : Type*} [field ùïú] [topological_space ùïú] [topological_ring ùïú]

/--
The map `Œª x, a * x + b`, as a homeomorphism from `ùïú` (a topological field) to itself, when `a ‚â† 0`.
-/
@[simps]
def affine_homeomorph (a b : ùïú) (h : a ‚â† 0) : ùïú ‚âÉ‚Çú ùïú :=
{ to_fun := Œª x, a * x + b,
  inv_fun := Œª y, (y - b) / a,
  left_inv := Œª x, by { simp only [add_sub_cancel], exact mul_div_cancel_left x h, },
  right_inv := Œª y, by { simp [mul_div_cancel' _ h], }, }

-- FIXME should be generated directly by `@[simps]`.
-- See https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/How.20do.20I.20configure.20an.20.60equiv.60.20to.20work.20with.20.60simps.60.3F/near/233291764
@[simp] lemma affine_homeomorph_apply (a b : ùïú) (h : a ‚â† 0) (x : ùïú) :
  affine_homeomorph a b h x = a * x + b := rfl

@[simp] lemma affine_homeomorph_symm_apply (a b : ùïú) (h : a ‚â† 0) (y : ùïú) :
  (affine_homeomorph a b h).symm y = (y - b) / a := rfl

end

section
variables {ùïú : Type*} [linear_ordered_field ùïú] [topological_space ùïú] [topological_ring ùïú]

/--
The image of `[0,1]` under the homeomorphism `Œª x, a * x + b` is `[b, a+b]`.
-/
-- We only need the ordering on `ùïú` here to avoid talking about flipping the interval over.
@[simp]
lemma affine_homeomorph_image_I (a b : ùïú) (h : 0 < a) (w) :
  affine_homeomorph a b w '' set.Icc 0 1 = set.Icc b (a + b) :=
begin
  ext,
  fsplit,
  { rintro ‚ü®x, ‚ü®‚ü®zero_le, le_one‚ü©, rfl‚ü©‚ü©,
    simp only [add_le_add_iff_right, affine_homeomorph_apply, le_add_iff_nonneg_left, set.mem_Icc],
    exact ‚ü®mul_nonneg h.le zero_le, (mul_le_iff_le_one_right h).mpr le_one‚ü©, },
  { intro m,
    simp only [set.image_congr, set.mem_image, affine_homeomorph_apply],
    use (x - b) / a,
    fsplit,
    { simp only [set.mem_Icc],
      fsplit,
      { apply div_nonneg,
        apply sub_nonneg.mpr,
        exact m.1,
        exact h.le, },
      { apply (div_le_one _).mpr,
        apply sub_le_iff_le_add.mpr,
        exact m.2,
        exact h, } },
    { rw mul_div_cancel' _ w,
      simp, } },
end

/--
The affine homeomorphism from a nontrivial interval `[a,b]` to `[0,1]`.
-/
def Icc_homeo (a b : ùïú) (h : a < b) : set.Icc a b ‚âÉ‚Çú set.Icc (0 : ùïú) (1 : ùïú) :=
begin
  let e := homeomorph.image (affine_homeomorph (b-a) a (sub_pos.mpr h).ne.symm) (set.Icc 0 1),
  refine (e.trans _).symm,
  apply homeomorph.set_congr,
  rw affine_homeomorph_image_I _ _ (sub_pos.mpr h),
  rw sub_add_cancel,
end

@[simp] lemma Icc_homeo_apply_coe (a b : ùïú) (h : a < b) (x : set.Icc a b) :
  ((Icc_homeo a b h) x : ùïú) = (x - a) / (b - a) :=
rfl

@[simp] lemma Icc_homeo_symm_apply_coe (a b : ùïú) (h : a < b) (x : set.Icc (0 : ùïú) (1 : ùïú)) :
  ((Icc_homeo a b h).symm x : ùïú) = (b - a) * x + a :=
rfl

end


/-- The preimage of polynomials on `[0,1]` under the pullback map by `x ‚Ü¶ (b-a) * x + a`
is the polynomials on `[a,b]`. -/
lemma polynomial_functions.comap'_pullback_alg_hom_Icc_homeo (a b : ‚Ñù) (h : a < b) :
  (polynomial_functions I).comap' (pullback_alg_hom ‚Ñù (Icc_homeo a b h).symm.to_continuous_map) =
    polynomial_functions (set.Icc a b) :=
begin
  ext f,
  fsplit,
  { rintro ‚ü®p, ‚ü®-,w‚ü©‚ü©,
    rw continuous_map.ext_iff at w,
    dsimp at w,
    let q := p.comp ((b - a)‚Åª¬π ‚Ä¢ polynomial.X + polynomial.C (-a * (b-a)‚Åª¬π)),
    refine ‚ü®q, ‚ü®_, _‚ü©‚ü©,
    { simp, },
    { ext x,
      simp only [neg_mul_eq_neg_mul_symm,
        ring_hom.map_neg, ring_hom.map_mul, alg_hom.coe_to_ring_hom,
        polynomial.eval_X, polynomial.eval_neg, polynomial.eval_C, polynomial.eval_smul,
        polynomial.eval_mul, polynomial.eval_add, polynomial.coe_aeval_eq_eval,
        polynomial.eval_comp, polynomial.as_continuous_map_on_alg_hom_apply,
        polynomial.as_continuous_map_on_to_fun, polynomial.as_continuous_map_to_fun],
      convert w ‚ü®_, _‚ü©; clear w,
      { -- FIXME why does `comm_ring.add` appear here?
        change x = (Icc_homeo a b h).symm ‚ü®_ + _, _‚ü©,
        ext,
        simp only [Icc_homeo_symm_apply_coe, subtype.coe_mk],
        replace h : b - a ‚â† 0 := sub_ne_zero_of_ne h.ne.symm,
        simp only [mul_add],
        field_simp, ring, },
      { change _ + _ ‚àà I,
        rw [mul_comm (b-a)‚Åª¬π, ‚Üêneg_mul_eq_neg_mul_symm, ‚Üêadd_mul, ‚Üêsub_eq_add_neg],
        have w‚ÇÅ : 0 < (b-a)‚Åª¬π := inv_pos.mpr (sub_pos.mpr h),
        have w‚ÇÇ : 0 ‚â§ (x : ‚Ñù) - a := sub_nonneg.mpr x.2.1,
        have w‚ÇÉ : (x : ‚Ñù) - a ‚â§ b - a := sub_le_sub_right x.2.2 a,
        fsplit,
        { exact mul_nonneg w‚ÇÇ (le_of_lt w‚ÇÅ), },
        { rw [‚Üêdiv_eq_mul_inv, div_le_one (sub_pos.mpr h)],
          exact w‚ÇÉ, }, }, }, },
  { rintro ‚ü®p, ‚ü®-,rfl‚ü©‚ü©,
    let q := p.comp ((b - a) ‚Ä¢ polynomial.X + polynomial.C a),
    refine ‚ü®q, ‚ü®_, _‚ü©‚ü©,
    { simp, },
    { ext x, simp [mul_comm], }, },
end

lemma continuous_map.subsingleton_subalgebra_ext [subsingleton X] (s‚ÇÅ s‚ÇÇ : subalgebra R C(X, R)) :
  s‚ÇÅ = s‚ÇÇ :=
begin
  by_cases n : nonempty X,
  { obtain ‚ü®x‚ü© := n,
    ext f,
    have h : f = algebra_map R C(X, R) (f x),
    { ext x', simp only [mul_one, algebra.id.smul_eq_mul, algebra_map_apply], congr, },
    rw h,
    simp only [subalgebra.algebra_map_mem], },
  { ext f,
    have h : f = 0,
    { ext x', exact false.elim (n ‚ü®x'‚ü©), },
    subst h,
    simp only [subalgebra.zero_mem], },
end

/--
The Weierstrass approximation theorem:
polynomials functions on `[a, b] ‚äÜ ‚Ñù` are dense in `C([a,b],‚Ñù)`

(While we could deduce this as an application of the Stone-Weierstrass theorem,
our proof of that relies on the fact that `abs` is in the closure of polynomials on `[-M, M]`,
so we may as well get this done first.)
-/
theorem polynomial_functions_closure_eq_top (a b : ‚Ñù) :
  (polynomial_functions (set.Icc a b)).topological_closure = ‚ä§ :=
begin
  by_cases h : a < b, -- (Otherwise it's easy; we'll deal with that later.)
  { -- We can pullback continuous functions to `[a,b]` to continuous functions on `[0,1]`,
    -- by precomposing with an affine map.
    let W : C(set.Icc a b, ‚Ñù) ‚Üí‚Çê[‚Ñù] C(I, ‚Ñù) :=
      pullback_alg_hom ‚Ñù (Icc_homeo a b h).symm.to_continuous_map,
    -- This operation is itself a homeomorphism
    -- (with respect to the norm topologies on continuous functions).
    let W' : C(set.Icc a b, ‚Ñù) ‚âÉ‚Çú C(I, ‚Ñù) := pullback_homeomorph ‚Ñù (Icc_homeo a b h).symm,
    have w : (W : C(set.Icc a b, ‚Ñù) ‚Üí C(I, ‚Ñù)) = W' := rfl,
    -- Thus we take the statement of the Weierstrass approximation theorem for `[0,1]`,
    have p := polynomial_functions_closure_eq_top',
    -- and pullback both sides, obtaining an equation between subalgebras of `C([a,b], ‚Ñù)`.
    apply_fun (Œª s, s.comap' W) at p,
    simp only [algebra.comap_top] at p,
    -- Since the pullback operation is continuous, it commutes with taking `topological_closure`,
    rw subalgebra.topological_closure_comap'_homeomorph _ W W' w at p,
    -- and precomposing with an affine map takes polynomial functions to polynomial functions.
    rw polynomial_functions.comap'_pullback_alg_hom_Icc_homeo at p,
    -- üéâ
    exact p },
  { -- Otherwise, `b ‚â§ a`, and the interval is a subsingleton,
    -- so all subalgebras are the same anyway.
    haveI : subsingleton (set.Icc a b) := ‚ü®Œª x y, le_antisymm
      ((x.2.2.trans (not_lt.mp h)).trans y.2.1) ((y.2.2.trans (not_lt.mp h)).trans x.2.1)‚ü©,
    apply continuous_map.subsingleton_subalgebra_ext, }
end

/--
An alternative statement of Weierstrass' theorem.
Every real-valued continuous function on `[a,b]` is a uniform limit of polynomials.
-/
theorem mem_polynomial_functions_closure (a b : ‚Ñù) (f : C(set.Icc a b, ‚Ñù)) :
  f ‚àà (polynomial_functions (set.Icc a b)).topological_closure :=
begin
  rw polynomial_functions_closure_eq_top _ _,
  simp,
end
