/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Adam Topaz
-/

import data.fintype.basic
import data.fin
import category_theory.concrete_category.bundled
import category_theory.concrete_category
import category_theory.full_subcategory
import category_theory.skeletal

/-!
# The category of finite types.

We define the category of finite types, denoted `Fintype` as
(bundled) types with a `fintype` instance.

We also define `Fintype.skeleton`, the standard skeleton of `Fintype` whose objects are `fin n`
for `n : ‚Ñï`. We prove that the obvious inclusion functor `Fintype.skeleton ‚•§ Fintype` is an
equivalence of categories in `Fintype.skeleton.equivalence`.
We prove that `Fintype.skeleton` is a skeleton of `Fintype` in `Fintype.is_skeleton`.
-/

open_locale classical
open category_theory

/-- The category of finite types. -/
@[derive has_coe_to_sort]
def Fintype := bundled fintype

namespace Fintype

/-- Construct a bundled `Fintype` from the underlying type and typeclass. -/
def of (X : Type*) [fintype X] : Fintype := bundled.of X
instance : inhabited Fintype := ‚ü®‚ü®pempty‚ü©‚ü©
instance {X : Fintype} : fintype X := X.2

instance : category Fintype := induced_category.category bundled.Œ±

/-- The fully faithful embedding of `Fintype` into the category of types. -/
@[derive [full, faithful], simps]
def incl : Fintype ‚•§ Type* := induced_functor _

instance : concrete_category Fintype := ‚ü®incl‚ü©

@[simp] lemma coe_id {A : Fintype} : (ùüô A : A ‚Üí A) = id := rfl

@[simp] lemma coe_comp {A B C : Fintype} (f : A ‚ü∂ B) (g : B ‚ü∂ C) :
  (f ‚â´ g : A ‚Üí C) = g ‚àò f := rfl

lemma id_apply {A : Fintype} (a : A) : (ùüô A : A ‚Üí A) a = a := rfl

lemma comp_apply {A B C : Fintype} (f : A ‚ü∂ B) (g : B ‚ü∂ C) (a : A) :
  (f ‚â´ g) a = g (f a) := rfl

/--
The "standard" skeleton for `Fintype`. This is the full subcategory of `Fintype` spanned by objects
of the form `fin n` for `n : ‚Ñï`. We parameterize the objects of `Fintype.skeleton` directly as `‚Ñï`,
as the type `fin m ‚âÉ fin n` is nonempty if and only if `n = m`.
-/
def skeleton := ‚Ñï

namespace skeleton

/-- Given any natural number `n`, this creates the associated object of `Fintype.skeleton`. -/
def mk : ‚Ñï ‚Üí skeleton := id

instance : inhabited skeleton := ‚ü®mk 0‚ü©

/-- Given any object of `Fintype.skeleton`, this returns the associated natural number. -/
def to_nat : skeleton ‚Üí ‚Ñï := id

instance : category skeleton :=
{ hom := Œª X Y, fin X ‚Üí fin Y,
  id := Œª _, id,
  comp := Œª _ _ _ f g, g ‚àò f }

lemma is_skeletal : skeletal skeleton := Œª X Y ‚ü®h‚ü©, fin.equiv_iff_eq.mp $ nonempty.intro $
{ to_fun := h.1,
  inv_fun := h.2,
  left_inv := Œª _, by {change (h.hom ‚â´ h.inv) _ = _, simpa},
  right_inv := Œª _, by {change (h.inv ‚â´ h.hom) _ = _, simpa} }

/-- The canonical fully faithful embedding of `Fintype.skeleton` into `Fintype`. -/
def incl : skeleton ‚•§ Fintype :=
{ obj := Œª X, Fintype.of (fin X),
  map := Œª _ _ f, f }

instance : full incl := { preimage := Œª _ _ f, f }
instance : faithful incl := {}
instance : ess_surj incl :=
{ mem_ess_image := Œª X,
  let F := fintype.equiv_fin X in
  ‚ü®fintype.card X, ‚ü®‚ü®F.symm, F, F.self_comp_symm, F.symm_comp_self‚ü©‚ü©‚ü© }

noncomputable instance : is_equivalence incl :=
equivalence.equivalence_of_fully_faithfully_ess_surj _

/-- The equivalence between `Fintype.skeleton` and `Fintype`. -/
noncomputable def equivalence : skeleton ‚âå Fintype := incl.as_equivalence

@[simp] lemma incl_mk_nat_card (n : ‚Ñï) : fintype.card (incl.obj (mk n)) = n := finset.card_fin n

end skeleton

/-- `Fintype.skeleton` is a skeleton of `Fintype`. -/
noncomputable def is_skeleton : is_skeleton_of Fintype skeleton skeleton.incl :=
{ skel := skeleton.is_skeletal,
  eqv := by apply_instance }

end Fintype
