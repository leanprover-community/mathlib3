/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Adam Topaz, Johan Commelin
-/
import category_theory.abelian.opposite
import category_theory.limits.preserves.shapes.zero
import category_theory.limits.preserves.shapes.kernels
import category_theory.adjunction.limits
import algebra.homology.exact
import tactic.tfae

/-!
# Exact sequences in abelian categories

In an abelian category, we get several interesting results related to exactness which are not
true in more general settings.

## Main results
* `(f, g)` is exact if and only if `f â‰« g = 0` and `kernel.Î¹ g â‰« cokernel.Ï€ f = 0`. This
  characterisation tends to be less cumbersome to work with than the original definition involving
  the comparison map `image f âŸ¶ kernel g`.
* If `(f, g)` is exact, then `image.Î¹ f` has the universal property of the kernel of `g`.
* `f` is a monomorphism iff `kernel.Î¹ f = 0` iff `exact 0 f`, and `f` is an epimorphism iff
  `cokernel.Ï€ = 0` iff `exact f 0`.
* A faithful functor between abelian categories that preserves zero morphisms reflects exact
  sequences.
* `X âŸ¶ Y âŸ¶ Z âŸ¶ 0` is exact if and only if the second map is a cokernel of the first, and
  `0 âŸ¶ X âŸ¶ Y âŸ¶ Z` is exact if and only if the first map is a kernel of the second.

-/

universes vâ‚ vâ‚‚ uâ‚ uâ‚‚

noncomputable theory

open category_theory
open category_theory.limits
open category_theory.preadditive

variables {C : Type uâ‚} [category.{vâ‚} C] [abelian C]

namespace category_theory

namespace abelian

variables {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)

local attribute [instance] has_equalizers_of_has_kernels

/--
In an abelian category, a pair of morphisms `f : X âŸ¶ Y`, `g : Y âŸ¶ Z` is exact
iff `image_subobject f = kernel_subobject g`.
-/
theorem exact_iff_image_eq_kernel : exact f g â†” image_subobject f = kernel_subobject g :=
begin
  split,
  { intro h,
    fapply subobject.eq_of_comm,
    { suffices : is_iso (image_to_kernel _ _ h.w),
      { exactI as_iso (image_to_kernel _ _ h.w), },
      exact is_iso_of_mono_of_epi _, },
    { simp, }, },
  { apply exact_of_image_eq_kernel, },
end

theorem exact_iff : exact f g â†” f â‰« g = 0 âˆ§ kernel.Î¹ g â‰« cokernel.Ï€ f = 0 :=
begin
  split,
  { intro h,
    exact âŸ¨h.1, kernel_comp_cokernel f g hâŸ© },
  { refine Î» h, âŸ¨h.1, _âŸ©,
    suffices hl : is_limit
      (kernel_fork.of_Î¹ (image_subobject f).arrow (image_subobject_arrow_comp_eq_zero h.1)),
    { have : image_to_kernel f g h.1 =
        (is_limit.cone_point_unique_up_to_iso hl (limit.is_limit _)).hom â‰«
          (kernel_subobject_iso _).inv,
      { ext, simp },
      rw this,
      apply_instance, },
    refine is_limit.of_Î¹ _ _ _ _ _,
    { refine Î» W u hu,
        kernel.lift (cokernel.Ï€ f) u _ â‰« (image_iso_image f).hom â‰« (image_subobject_iso _).inv,
      rw [â†kernel.lift_Î¹ g u hu, category.assoc, h.2, has_zero_morphisms.comp_zero] },
    { tidy },
    { intros, rw [â†cancel_mono (image_subobject f).arrow, w],
      simp, } }
end

theorem exact_iff' {cg : kernel_fork g} (hg : is_limit cg)
  {cf : cokernel_cofork f} (hf : is_colimit cf) : exact f g â†” f â‰« g = 0 âˆ§ cg.Î¹ â‰« cf.Ï€ = 0 :=
begin
  split,
  { intro h,
    exact âŸ¨h.1, fork_Î¹_comp_cofork_Ï€ f g h cg cfâŸ© },
  { rw exact_iff,
    refine Î» h, âŸ¨h.1, _âŸ©,
    apply zero_of_epi_comp (is_limit.cone_point_unique_up_to_iso hg (limit.is_limit _)).hom,
    apply zero_of_comp_mono
      (is_colimit.cocone_point_unique_up_to_iso (colimit.is_colimit _) hf).hom,
    simp [h.2] }
end

theorem exact_tfae :
  tfae [exact f g,
        f â‰« g = 0 âˆ§ kernel.Î¹ g â‰« cokernel.Ï€ f = 0,
        image_subobject f = kernel_subobject g] :=
begin
  tfae_have : 1 â†” 2, { apply exact_iff },
  tfae_have : 1 â†” 3, { apply exact_iff_image_eq_kernel },
  tfae_finish
end

lemma is_equivalence.exact_iff {D : Type uâ‚} [category.{vâ‚} D] [abelian D]
  (F : C â¥¤ D) [is_equivalence F] :
  exact (F.map f) (F.map g) â†” exact f g :=
begin
  simp only [exact_iff, â† F.map_eq_zero_iff, F.map_comp, category.assoc,
    â† kernel_comparison_comp_Î¹ g F, â† Ï€_comp_cokernel_comparison f F],
  rw [is_iso.comp_left_eq_zero (kernel_comparison g F), â† category.assoc,
    is_iso.comp_right_eq_zero _ (cokernel_comparison f F)],
end

/-- If `(f, g)` is exact, then `images.image.Î¹ f` is a kernel of `g`. -/
def is_limit_image (h : exact f g) :
  is_limit
    (kernel_fork.of_Î¹ (abelian.image.Î¹ f) (image_Î¹_comp_eq_zero h.1) : kernel_fork g) :=
begin
  rw exact_iff at h,
  refine is_limit.of_Î¹ _ _ _ _ _,
  { refine Î» W u hu, kernel.lift (cokernel.Ï€ f) u _,
    rw [â†kernel.lift_Î¹ g u hu, category.assoc, h.2, has_zero_morphisms.comp_zero] },
  tidy
end

/-- If `(f, g)` is exact, then `image.Î¹ f` is a kernel of `g`. -/
def is_limit_image' (h : exact f g) :
  is_limit (kernel_fork.of_Î¹ (limits.image.Î¹ f) (limits.image_Î¹_comp_eq_zero h.1)) :=
is_kernel.iso_kernel _ _ (is_limit_image f g h) (image_iso_image f).symm $ is_image.lift_fac _ _

/-- If `(f, g)` is exact, then `coimages.coimage.Ï€ g` is a cokernel of `f`. -/
def is_colimit_coimage (h : exact f g) : is_colimit (cokernel_cofork.of_Ï€ (abelian.coimage.Ï€ g)
  (abelian.comp_coimage_Ï€_eq_zero h.1) : cokernel_cofork f) :=
begin
  rw exact_iff at h,
  refine is_colimit.of_Ï€ _ _ _ _ _,
  { refine Î» W u hu, cokernel.desc (kernel.Î¹ g) u _,
    rw [â†cokernel.Ï€_desc f u hu, â†category.assoc, h.2, has_zero_morphisms.zero_comp] },
  tidy
end

/-- If `(f, g)` is exact, then `factor_thru_image g` is a cokernel of `f`. -/
def is_colimit_image (h : exact f g) : is_colimit
  (cokernel_cofork.of_Ï€ (limits.factor_thru_image g) (comp_factor_thru_image_eq_zero h.1)) :=
is_cokernel.cokernel_iso _ _ (is_colimit_coimage f g h) (coimage_iso_image' g) $
  (cancel_mono (limits.image.Î¹ g)).1 $ by simp

lemma exact_cokernel : exact f (cokernel.Ï€ f) :=
by { rw exact_iff, tidy }

instance (h : exact f g) : mono (cokernel.desc f g h.w) :=
suffices h : cokernel.desc f g h.w =
  (is_colimit.cocone_point_unique_up_to_iso (colimit.is_colimit _) (is_colimit_image f g h)).hom
    â‰« limits.image.Î¹ g, by { rw h, apply mono_comp },
(cancel_epi (cokernel.Ï€ f)).1 $ by simp

/-- If `ex : exact f g` and `epi g`, then `cokernel.desc _ _ ex.w` is an isomorphism. -/
instance (ex : exact f g) [epi g] : is_iso (cokernel.desc f g ex.w) :=
is_iso_of_mono_of_epi (limits.cokernel.desc f g ex.w)

@[simp, reassoc]
lemma cokernel.desc.inv [epi g] (ex : exact f g) :
  g â‰« inv (cokernel.desc _ _ ex.w) = cokernel.Ï€ _ :=
by simp

instance (ex : exact f g) [mono f] : is_iso (kernel.lift g f ex.w) :=
  is_iso_of_mono_of_epi (limits.kernel.lift g f ex.w)

@[simp, reassoc]
lemma kernel.lift.inv [mono f] (ex : exact f g) :
  inv (kernel.lift _ _ ex.w) â‰« f = kernel.Î¹ g :=
by simp

/-- If `X âŸ¶ Y âŸ¶ Z âŸ¶ 0` is exact, then the second map is a cokernel of the first. -/
def is_colimit_of_exact_of_epi [epi g] (h : exact f g) :
  is_colimit (cokernel_cofork.of_Ï€ _ h.w) :=
is_colimit.of_iso_colimit (colimit.is_colimit _) $ cocones.ext
  âŸ¨cokernel.desc _ _ h.w, epi_desc g (cokernel.Ï€ f) ((exact_iff _ _).1 h).2,
    (cancel_epi (cokernel.Ï€ f)).1 (by tidy), (cancel_epi g).1 (by tidy)âŸ© (Î» j, by cases j; simp)

/-- If `0 âŸ¶ X âŸ¶ Y âŸ¶ Z` is exact, then the first map is a kernel of the second. -/
def is_limit_of_exact_of_mono [mono f] (h : exact f g) :
  is_limit (kernel_fork.of_Î¹ _ h.w) :=
is_limit.of_iso_limit (limit.is_limit _) $ cones.ext
 âŸ¨mono_lift f (kernel.Î¹ g) ((exact_iff _ _).1 h).2, kernel.lift _ _ h.w,
  (cancel_mono (kernel.Î¹ g)).1 (by tidy), (cancel_mono f).1 (by tidy)âŸ© (Î» j, by cases j; simp)

lemma exact_of_is_cokernel (w : f â‰« g = 0)
  (h : is_colimit (cokernel_cofork.of_Ï€ _ w)) : exact f g :=
begin
  refine (exact_iff _ _).2 âŸ¨w, _âŸ©,
  have := h.fac (cokernel_cofork.of_Ï€ _ (cokernel.condition f)) walking_parallel_pair.one,
  simp only [cofork.of_Ï€_Î¹_app] at this,
  rw [â† this, â† category.assoc, kernel.condition, zero_comp]
end

lemma exact_of_is_kernel (w : f â‰« g = 0)
  (h : is_limit (kernel_fork.of_Î¹ _ w)) : exact f g :=
begin
  refine (exact_iff _ _).2 âŸ¨w, _âŸ©,
  have := h.fac (kernel_fork.of_Î¹ _ (kernel.condition g)) walking_parallel_pair.zero,
  simp only [fork.of_Î¹_Ï€_app] at this,
  rw [â† this, category.assoc, cokernel.condition, comp_zero]
end

section
variables (Z)

lemma tfae_mono : tfae [mono f, kernel.Î¹ f = 0, exact (0 : Z âŸ¶ X) f] :=
begin
  tfae_have : 3 â†’ 2,
  { exact kernel_Î¹_eq_zero_of_exact_zero_left Z },
  tfae_have : 1 â†’ 3,
  { introsI, exact exact_zero_left_of_mono Z },
  tfae_have : 2 â†’ 1,
  { exact mono_of_kernel_Î¹_eq_zero _ },
  tfae_finish
end

-- Note we've already proved `mono_iff_exact_zero_left : mono f â†” exact (0 : Z âŸ¶ X) f`
-- in any preadditive category with kernels and images.

lemma mono_iff_kernel_Î¹_eq_zero : mono f â†” kernel.Î¹ f = 0 :=
(tfae_mono X f).out 0 1

lemma tfae_epi : tfae [epi f, cokernel.Ï€ f = 0, exact f (0 : Y âŸ¶ Z)] :=
begin
  tfae_have : 3 â†’ 2,
  { rw exact_iff,
    rintro âŸ¨-, hâŸ©,
    exact zero_of_epi_comp _ h },
  tfae_have : 1 â†’ 3,
  { rw exact_iff,
    introI,
    exact âŸ¨by simp, by simp [cokernel.Ï€_of_epi]âŸ© },
  tfae_have : 2 â†’ 1,
  { exact epi_of_cokernel_Ï€_eq_zero _ },
  tfae_finish
end

-- Note we've already proved `epi_iff_exact_zero_right : epi f â†” exact f (0 : Y âŸ¶ Z)`
-- in any preadditive category with equalizers and images.

lemma epi_iff_cokernel_Ï€_eq_zero : epi f â†” cokernel.Ï€ f = 0 :=
(tfae_epi X f).out 0 1

end

section opposite

lemma exact.op (h : exact f g) : exact g.op f.op :=
begin
  rw exact_iff,
  refine âŸ¨by simp [â† op_comp, h.w], quiver.hom.unop_inj _âŸ©,
  simp only [unop_comp, cokernel.Ï€_op, eq_to_hom_refl, kernel.Î¹_op, category.id_comp,
    category.assoc, kernel_comp_cokernel_assoc _ _ h, zero_comp, comp_zero, unop_zero],
end

lemma exact.op_iff : exact g.op f.op â†” exact f g :=
âŸ¨Î» e, begin
  rw â† is_equivalence.exact_iff _ _ (op_op_equivalence C).inverse,
  exact exact.op _ _ e
end, exact.op _ _âŸ©


lemma exact.unop {X Y Z : Cáµ’áµ–} (g : X âŸ¶ Y) (f : Y âŸ¶ Z) (h : exact g f) : exact f.unop g.unop :=
begin
  rw [â† f.op_unop, â† g.op_unop] at h,
  rwa â† exact.op_iff,
end

lemma exact.unop_iff {X Y Z : Cáµ’áµ–} (g : X âŸ¶ Y) (f : Y âŸ¶ Z) : exact f.unop g.unop â†” exact g f :=
âŸ¨Î» e, by rwa [â† f.op_unop, â† g.op_unop, â† exact.op_iff] at e, Î» e, @@exact.unop _ _ g f eâŸ©

end opposite


end abelian

namespace functor
variables {D : Type uâ‚‚} [category.{vâ‚‚} D] [abelian D]

@[priority 100]
instance reflects_exact_sequences_of_preserves_zero_morphisms_of_faithful (F : C â¥¤ D)
  [preserves_zero_morphisms F] [faithful F] : reflects_exact_sequences F :=
{ reflects := Î» X Y Z f g hfg,
  begin
    rw [abelian.exact_iff, â† F.map_comp, F.map_eq_zero_iff] at hfg,
    refine (abelian.exact_iff _ _).2 âŸ¨hfg.1, F.zero_of_map_zero _ _âŸ©,
    obtain âŸ¨k, hkâŸ© := kernel.lift' (F.map g) (F.map (kernel.Î¹ g))
      (by simp only [â† F.map_comp, kernel.condition, category_theory.functor.map_zero]),
    obtain âŸ¨l, hlâŸ© := cokernel.desc' (F.map f) (F.map (cokernel.Ï€ f))
      (by simp only [â† F.map_comp, cokernel.condition, category_theory.functor.map_zero]),
    rw [F.map_comp, â† hk, â† hl, category.assoc, reassoc_of hfg.2, zero_comp, comp_zero]
  end }

end functor

section abelian

open limits abelian

universes u v

variables {ğ“ ğ“‘ : Type u} [category.{v} ğ“] [category.{v} ğ“‘]
variables [abelian ğ“] [abelian ğ“‘]
variables (L : ğ“ â¥¤ ğ“‘) [preserves_finite_limits L] [preserves_finite_colimits L]

lemma exact_of_exact_functor {X Y Z : ğ“} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (e1 : exact f g) :
  exact (L.map f) (L.map g) :=
have H : is_iso (image_to_kernel f g e1.w) := is_iso_of_mono_of_epi _,
begin
  rw abelian.exact_iff_image_eq_kernel,
  ext,
  work_on_goal 2
  { refine (preserves_image.iso L f) â‰ªâ‰« _ â‰ªâ‰« (preserves_kernel.iso _ _),
    exact
    { hom := L.map $ (image_subobject_iso _).inv â‰« image_to_kernel f g e1.w â‰«
        (kernel_subobject_iso _).hom,
      inv := L.map $ (kernel_subobject_iso _).inv â‰«
        (@as_iso _ _ _ _ (image_to_kernel _ _ e1.w) H).inv â‰« (image_subobject_iso _).hom,
      hom_inv_id' := begin
        simp only [â†L.map_comp, category.assoc],
        have h1 := (kernel_subobject_iso g).hom_inv_id,
        reassoc h1,
        rw [h1, â†category.assoc _ _ (image_subobject_iso _).hom, as_iso_inv,
          is_iso.hom_inv_id, category.id_comp, iso.inv_hom_id, L.map_id],
      end,
      inv_hom_id' := begin
        simp only [â†L.map_comp, category.assoc],
        have h1 := (image_subobject_iso f).hom_inv_id,
        reassoc h1,
        rw [h1, â†category.assoc _ _ (kernel_subobject_iso _).hom, as_iso_inv,
          is_iso.inv_hom_id, category.id_comp, iso.inv_hom_id, L.map_id],
      end } },
  { simp only [functor.map_comp, category.assoc, iso.trans_hom, preserves_kernel.iso_hom,
      kernel_comparison_comp_Î¹, limits.image.fac],
    simp only [â†L.map_comp, kernel_subobject_arrow, image_to_kernel_arrow, image_subobject_arrow'],
    rw [â†category.assoc, preserves_image.precomp_factor_thru_image, â†L.map_comp, limits.image.fac] }
end

end abelian

end category_theory
