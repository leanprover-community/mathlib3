/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import category_theory.limits.shapes.finite_products
import category_theory.limits.shapes.kernels
import category_theory.limits.shapes.normal_mono.equalizers
import category_theory.abelian.images
import category_theory.preadditive.basic

/-!
# Every non_preadditive_abelian category is preadditive

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In mathlib, we define an abelian category as a preadditive category with a zero object,
kernels and cokernels, products and coproducts and in which every monomorphism and epimorphis is
normal.

While virtually every interesting abelian category has a natural preadditive structure (which is why
it is included in the definition), preadditivity is not actually needed: Every category that has
all of the other properties appearing in the definition of an abelian category admits a preadditive
structure. This is the construction we carry out in this file.

The proof proceeds in roughly five steps:
1. Prove some results (for example that all equalizers exist) that would be trivial if we already
   had the preadditive structure but are a bit of work without it.
2. Develop images and coimages to show that every monomorphism is the kernel of its cokernel.

The results of the first two steps are also useful for the "normal" development of abelian
categories, and will be used there.

3. For every object `A`, define a "subtraction" morphism `Ïƒ : A â¨¯ A âŸ¶ A` and use it to define
   subtraction on morphisms as `f - g := prod.lift f g â‰« Ïƒ`.
4. Prove a small number of identities about this subtraction from the definition of `Ïƒ`.
5. From these identities, prove a large number of other identities that imply that defining
   `f + g := f - (0 - g)` indeed gives an abelian group structure on morphisms such that composition
   is bilinear.

The construction is non-trivial and it is quite remarkable that this abelian group structure can
be constructed purely from the existence of a few limits and colimits. Even more remarkably,
since abelian categories admit exactly one preadditive structure (see
`subsingleton_preadditive_of_has_binary_biproducts`), the construction manages to exactly
reconstruct any natural preadditive structure the category may have.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]

-/

noncomputable theory

open category_theory
open category_theory.limits

namespace category_theory
section
universes v u

variables (C : Type u) [category.{v} C]

/-- We call a category `non_preadditive_abelian` if it has a zero object, kernels, cokernels, binary
    products and coproducts, and every monomorphism and every epimorphism is normal. -/
class non_preadditive_abelian extends has_zero_morphisms C, normal_mono_category C,
  normal_epi_category C :=
[has_zero_object : has_zero_object C]
[has_kernels : has_kernels C]
[has_cokernels : has_cokernels C]
[has_finite_products : has_finite_products C]
[has_finite_coproducts : has_finite_coproducts C]

set_option default_priority 100

attribute [instance] non_preadditive_abelian.has_zero_object
attribute [instance] non_preadditive_abelian.has_kernels
attribute [instance] non_preadditive_abelian.has_cokernels
attribute [instance] non_preadditive_abelian.has_finite_products
attribute [instance] non_preadditive_abelian.has_finite_coproducts

end
end category_theory

open category_theory

universes v u

variables {C : Type u} [category.{v} C] [non_preadditive_abelian C]

namespace category_theory.non_preadditive_abelian

section factor

variables {P Q : C} (f : P âŸ¶ Q)

/-- The map `p : P âŸ¶ image f` is an epimorphism -/
instance : epi (abelian.factor_thru_image f) :=
let I := abelian.image f, p := abelian.factor_thru_image f,
    i := kernel.Î¹ (cokernel.Ï€ f) in
-- It will suffice to consider some g : I âŸ¶ R such that p â‰« g = 0 and show that g = 0.
normal_mono_category.epi_of_zero_cancel _ $ Î» R (g : I âŸ¶ R) (hpg : p â‰« g = 0),
begin
  -- Since C is abelian, u := ker g â‰« i is the kernel of some morphism h.
  let u := kernel.Î¹ g â‰« i,
  haveI : mono u := mono_comp _ _,
  haveI hu := normal_mono_of_mono u,
  let h := hu.g,
  -- By hypothesis, p factors through the kernel of g via some t.
  obtain âŸ¨t, htâŸ© := kernel.lift' g p hpg,
  have fh : f â‰« h = 0, calc
    f â‰« h = (p â‰« i) â‰« h : (abelian.image.fac f).symm â–¸ rfl
       ... = ((t â‰« kernel.Î¹ g) â‰« i) â‰« h : ht â–¸ rfl
       ... = t â‰« u â‰« h : by simp only [category.assoc]; conv_lhs { congr, skip, rw â†category.assoc }
       ... = t â‰« 0 : hu.w â–¸ rfl
       ... = 0 : has_zero_morphisms.comp_zero _ _,
  -- h factors through the cokernel of f via some l.
  obtain âŸ¨l, hlâŸ© := cokernel.desc' f h fh,
  have hih : i â‰« h = 0, calc
    i â‰« h = i â‰« cokernel.Ï€ f â‰« l : hl â–¸ rfl
       ... = 0 â‰« l : by rw [â†category.assoc, kernel.condition]
       ... = 0 : zero_comp,
  -- i factors through u = ker h via some s.
  obtain âŸ¨s, hsâŸ© := normal_mono.lift' u i hih,
  have hs' : (s â‰« kernel.Î¹ g) â‰« i = ğŸ™ I â‰« i, by rw [category.assoc, hs, category.id_comp],
  haveI : epi (kernel.Î¹ g) := epi_of_epi_fac ((cancel_mono _).1 hs'),
  -- ker g is an epimorphism, but ker g â‰« g = 0 = ker g â‰« 0, so g = 0 as required.
  exact zero_of_epi_comp _ (kernel.condition g)
end

instance is_iso_factor_thru_image [mono f] : is_iso (abelian.factor_thru_image f) :=
is_iso_of_mono_of_epi _

/-- The canonical morphism `i : coimage f âŸ¶ Q` is a monomorphism -/
instance : mono (abelian.factor_thru_coimage f) :=
let I := abelian.coimage f, i := abelian.factor_thru_coimage f,
    p := cokernel.Ï€ (kernel.Î¹ f) in
normal_epi_category.mono_of_cancel_zero _ $ Î» R (g : R âŸ¶ I) (hgi : g â‰« i = 0),
begin
  -- Since C is abelian, u := p â‰« coker g is the cokernel of some morphism h.
  let u := p â‰« cokernel.Ï€ g,
  haveI : epi u := epi_comp _ _,
  haveI hu := normal_epi_of_epi u,
  let h := hu.g,
  -- By hypothesis, i factors through the cokernel of g via some t.
  obtain âŸ¨t, htâŸ© := cokernel.desc' g i hgi,
  have hf : h â‰« f = 0, calc
    h â‰« f = h â‰« (p â‰« i) : (abelian.coimage.fac f).symm â–¸ rfl
    ... = h â‰« (p â‰« (cokernel.Ï€ g â‰« t)) : ht â–¸ rfl
    ... = h â‰« u â‰« t : by simp only [category.assoc]; conv_lhs { congr, skip, rw â†category.assoc }
    ... = 0 â‰« t : by rw [â†category.assoc, hu.w]
    ... = 0 : zero_comp,
  -- h factors through the kernel of f via some l.
  obtain âŸ¨l, hlâŸ© := kernel.lift' f h hf,
  have hhp : h â‰« p = 0, calc
    h â‰« p = (l â‰« kernel.Î¹ f) â‰« p : hl â–¸ rfl
    ... = l â‰« 0 : by rw [category.assoc, cokernel.condition]
    ... = 0 : comp_zero,
  -- p factors through u = coker h via some s.
  obtain âŸ¨s, hsâŸ© := normal_epi.desc' u p hhp,
  have hs' : p â‰« cokernel.Ï€ g â‰« s = p â‰« ğŸ™ I, by rw [â†category.assoc, hs, category.comp_id],
  haveI : mono (cokernel.Ï€ g) := mono_of_mono_fac ((cancel_epi _).1 hs'),
  -- coker g is a monomorphism, but g â‰« coker g = 0 = 0 â‰« coker g, so g = 0 as required.
  exact zero_of_comp_mono _ (cokernel.condition g)
end

instance is_iso_factor_thru_coimage [epi f] :
  is_iso (abelian.factor_thru_coimage f) :=
is_iso_of_mono_of_epi _

end factor

section cokernel_of_kernel
variables {X Y : C} {f : X âŸ¶ Y}

/-- In a `non_preadditive_abelian` category, an epi is the cokernel of its kernel. More precisely:
    If `f` is an epimorphism and `s` is some limit kernel cone on `f`, then `f` is a cokernel
    of `fork.Î¹ s`. -/
def epi_is_cokernel_of_kernel [epi f] (s : fork f 0) (h : is_limit s) :
  is_colimit (cokernel_cofork.of_Ï€ f (kernel_fork.condition s)) :=
is_cokernel.cokernel_iso _ _
  (cokernel.of_iso_comp _ _
    (limits.is_limit.cone_point_unique_up_to_iso (limit.is_limit _) h)
    (cone_morphism.w (limits.is_limit.unique_up_to_iso (limit.is_limit _) h).hom _))
  (as_iso $ abelian.factor_thru_coimage f) (abelian.coimage.fac f)

/-- In a `non_preadditive_abelian` category, a mono is the kernel of its cokernel. More precisely:
    If `f` is a monomorphism and `s` is some colimit cokernel cocone on `f`, then `f` is a kernel
    of `cofork.Ï€ s`. -/
def mono_is_kernel_of_cokernel [mono f] (s : cofork f 0) (h : is_colimit s) :
  is_limit (kernel_fork.of_Î¹ f (cokernel_cofork.condition s)) :=
is_kernel.iso_kernel _ _
  (kernel.of_comp_iso _ _
    (limits.is_colimit.cocone_point_unique_up_to_iso h (colimit.is_colimit _))
    (cocone_morphism.w (limits.is_colimit.unique_up_to_iso h $ colimit.is_colimit _).hom _))
  (as_iso $ abelian.factor_thru_image f) (abelian.image.fac f)

end cokernel_of_kernel
section

/-- The composite `A âŸ¶ A â¨¯ A âŸ¶ cokernel (Î” A)`, where the first map is `(ğŸ™ A, 0)` and the second map
    is the canonical projection into the cokernel. -/
abbreviation r (A : C) : A âŸ¶ cokernel (diag A) := prod.lift (ğŸ™ A) 0 â‰« cokernel.Ï€ (diag A)

instance mono_Î” {A : C} : mono (diag A) := mono_of_mono_fac $ prod.lift_fst _ _

instance mono_r {A : C} : mono (r A) :=
begin
  let hl : is_limit (kernel_fork.of_Î¹ (diag A) (cokernel.condition (diag A))),
  { exact mono_is_kernel_of_cokernel _ (colimit.is_colimit _) },
  apply normal_epi_category.mono_of_cancel_zero,
  intros Z x hx,
  have hxx : (x â‰« prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) â‰« cokernel.Ï€ (diag A) = 0,
  { rw [category.assoc, hx] },
  obtain âŸ¨y, hyâŸ© := kernel_fork.is_limit.lift' hl _ hxx,
  rw kernel_fork.Î¹_of_Î¹ at hy,
  have hyy : y = 0,
  { erw [â†category.comp_id y, â†limits.prod.lift_snd (ğŸ™ A) (ğŸ™ A),  â†category.assoc, hy,
      category.assoc, prod.lift_snd, has_zero_morphisms.comp_zero] },
  haveI : mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) := mono_of_mono_fac (prod.lift_fst _ _),
  apply (cancel_mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A))).1,
  rw [â†hy, hyy, zero_comp, zero_comp]
end

instance epi_r {A : C} : epi (r A) :=
begin
  have hlp : prod.lift (ğŸ™ A) (0 : A âŸ¶ A) â‰« limits.prod.snd = 0 := prod.lift_snd _ _,
  let hp1 : is_limit (kernel_fork.of_Î¹ (prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) hlp),
  { refine fork.is_limit.mk _ (Î» s, fork.Î¹ s â‰« limits.prod.fst) _ _,
    { intro s,
      ext; simp, erw category.comp_id },
    { intros s m h,
      haveI : mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A)) := mono_of_mono_fac (prod.lift_fst _ _),
      apply (cancel_mono (prod.lift (ğŸ™ A) (0 : A âŸ¶ A))).1,
      convert h,
      ext; simp } },
  let hp2 : is_colimit (cokernel_cofork.of_Ï€ (limits.prod.snd : A â¨¯ A âŸ¶ A) hlp),
  { exact epi_is_cokernel_of_kernel _ hp1 },
  apply normal_mono_category.epi_of_zero_cancel,
  intros Z z hz,
  have h : prod.lift (ğŸ™ A) (0 : A âŸ¶ A) â‰« cokernel.Ï€ (diag A) â‰« z = 0,
  { rw [â†category.assoc, hz] },
  obtain âŸ¨t, htâŸ© := cokernel_cofork.is_colimit.desc' hp2 _ h,
  rw cokernel_cofork.Ï€_of_Ï€ at ht,
  have htt : t = 0,
  { rw [â†category.id_comp t],
    change ğŸ™ A â‰« t = 0,
    rw [â†limits.prod.lift_snd (ğŸ™ A) (ğŸ™ A), category.assoc, ht, â†category.assoc,
      cokernel.condition, zero_comp] },
  apply (cancel_epi (cokernel.Ï€ (diag A))).1,
  rw [â†ht, htt, comp_zero, comp_zero]
end

instance is_iso_r {A : C} : is_iso (r A) :=
is_iso_of_mono_of_epi _

/-- The composite `A â¨¯ A âŸ¶ cokernel (diag A) âŸ¶ A` given by the natural projection into the cokernel
    followed by the inverse of `r`. In the category of modules, using the normal kernels and
    cokernels, this map is equal to the map `(a, b) â†¦ a - b`, hence the name `Ïƒ` for
    "subtraction". -/
abbreviation Ïƒ {A : C} : A â¨¯ A âŸ¶ A := cokernel.Ï€ (diag A) â‰« inv (r A)

end

@[simp, reassoc] lemma diag_Ïƒ {X : C} : diag X â‰« Ïƒ = 0 :=
by rw [cokernel.condition_assoc, zero_comp]

@[simp, reassoc] lemma lift_Ïƒ {X : C} : prod.lift (ğŸ™ X) 0 â‰« Ïƒ = ğŸ™ X :=
by rw [â†category.assoc, is_iso.hom_inv_id]

@[reassoc] lemma lift_map {X Y : C} (f : X âŸ¶ Y) :
  prod.lift (ğŸ™ X) 0 â‰« limits.prod.map f f = f â‰« prod.lift (ğŸ™ Y) 0 :=
by simp

/-- Ïƒ is a cokernel of Î” X. -/
def is_colimit_Ïƒ {X : C} : is_colimit (cokernel_cofork.of_Ï€ Ïƒ diag_Ïƒ) :=
cokernel.cokernel_iso _ Ïƒ (as_iso (r X)).symm (by rw [iso.symm_hom, as_iso_inv])

/-- This is the key identity satisfied by `Ïƒ`. -/
lemma Ïƒ_comp {X Y : C} (f : X âŸ¶ Y) : Ïƒ â‰« f = limits.prod.map f f â‰« Ïƒ :=
begin
  obtain âŸ¨g, hgâŸ© :=
    cokernel_cofork.is_colimit.desc' is_colimit_Ïƒ (limits.prod.map f f â‰« Ïƒ) (by simp),
  suffices hfg : f = g,
  { rw [â†hg, cofork.Ï€_of_Ï€, hfg] },
  calc f = f â‰« prod.lift (ğŸ™ Y) 0 â‰« Ïƒ : by rw [lift_Ïƒ, category.comp_id]
    ... = prod.lift (ğŸ™ X) 0 â‰« limits.prod.map f f â‰« Ïƒ : by rw lift_map_assoc
    ... = prod.lift (ğŸ™ X) 0 â‰« Ïƒ â‰« g : by rw [â†hg, cokernel_cofork.Ï€_of_Ï€]
    ... = g : by rw [â†category.assoc, lift_Ïƒ, category.id_comp]
end

section

/- We write `f - g` for `prod.lift f g â‰« Ïƒ`. -/
/-- Subtraction of morphisms in a `non_preadditive_abelian` category. -/
def has_sub {X Y : C} : has_sub (X âŸ¶ Y) := âŸ¨Î» f g, prod.lift f g â‰« ÏƒâŸ©
local attribute [instance] has_sub

/- We write `-f` for `0 - f`. -/
/-- Negation of morphisms in a `non_preadditive_abelian` category. -/
def has_neg {X Y : C} : has_neg (X âŸ¶ Y) := âŸ¨Î» f, 0 - fâŸ©
local attribute [instance] has_neg

/- We write `f + g` for `f - (-g)`. -/
/-- Addition of morphisms in a `non_preadditive_abelian` category. -/
def has_add {X Y : C} : has_add (X âŸ¶ Y) := âŸ¨Î» f g, f - (-g)âŸ©
local attribute [instance] has_add

lemma sub_def {X Y : C} (a b : X âŸ¶ Y) : a - b = prod.lift a b â‰« Ïƒ := rfl
lemma add_def {X Y : C} (a b : X âŸ¶ Y) : a + b = a - (-b) := rfl
lemma neg_def {X Y : C} (a : X âŸ¶ Y) : -a = 0 - a := rfl

lemma sub_zero {X Y : C} (a : X âŸ¶ Y) : a - 0 = a :=
begin
  rw sub_def,
  conv_lhs { congr, congr, rw â†category.comp_id a, skip, rw (show 0 = a â‰« (0 : Y âŸ¶ Y), by simp)},
  rw [â† prod.comp_lift, category.assoc, lift_Ïƒ, category.comp_id]
end

lemma sub_self {X Y : C} (a : X âŸ¶ Y) : a - a = 0 :=
by rw [sub_def, â†category.comp_id a, â† prod.comp_lift, category.assoc, diag_Ïƒ, comp_zero]

lemma lift_sub_lift {X Y : C} (a b c d : X âŸ¶ Y) :
  prod.lift a b - prod.lift c d = prod.lift (a - c) (b - d) :=
begin
  simp only [sub_def],
  ext,
  { rw [category.assoc, Ïƒ_comp, prod.lift_map_assoc, prod.lift_fst, prod.lift_fst, prod.lift_fst] },
  { rw [category.assoc, Ïƒ_comp, prod.lift_map_assoc, prod.lift_snd, prod.lift_snd, prod.lift_snd] }
end

lemma sub_sub_sub {X Y : C} (a b c d : X âŸ¶ Y) : (a - c) - (b - d) = (a - b) - (c - d) :=
begin
  rw [sub_def, â†lift_sub_lift, sub_def, category.assoc, Ïƒ_comp, prod.lift_map_assoc], refl
end

lemma neg_sub {X Y : C} (a b : X âŸ¶ Y) : (-a) - b = (-b) - a :=
by conv_lhs { rw [neg_def, â†sub_zero b, sub_sub_sub, sub_zero, â†neg_def] }

lemma neg_neg {X Y : C} (a : X âŸ¶ Y) : -(-a) = a :=
begin
  rw [neg_def, neg_def],
  conv_lhs { congr, rw â†sub_self a },
  rw [sub_sub_sub, sub_zero, sub_self, sub_zero]
end

lemma add_comm {X Y : C} (a b : X âŸ¶ Y) : a + b = b + a :=
begin
  rw [add_def],
  conv_lhs { rw â†neg_neg a },
  rw [neg_def, neg_def, neg_def, sub_sub_sub],
  conv_lhs {congr, skip, rw [â†neg_def, neg_sub] },
  rw [sub_sub_sub, add_def, â†neg_def, neg_neg b, neg_def]
end

lemma add_neg {X Y : C} (a b : X âŸ¶ Y) : a + (-b) = a - b :=
by rw [add_def, neg_neg]

lemma add_neg_self {X Y : C} (a : X âŸ¶ Y) : a + (-a) = 0 :=
by rw [add_neg, sub_self]

lemma neg_add_self {X Y : C} (a : X âŸ¶ Y) : (-a) + a = 0 :=
by rw [add_comm, add_neg_self]

lemma neg_sub' {X Y : C} (a b : X âŸ¶ Y) : -(a - b) = (-a) + b :=
begin
  rw [neg_def, neg_def],
  conv_lhs { rw â†sub_self (0 : X âŸ¶ Y) },
  rw [sub_sub_sub, add_def, neg_def]
end

lemma neg_add {X Y : C} (a b : X âŸ¶ Y) : -(a + b) = (-a) - b :=
by rw [add_def, neg_sub', add_neg]

lemma sub_add {X Y : C} (a b c : X âŸ¶ Y) : (a - b) + c = a - (b - c) :=
by rw [add_def, neg_def, sub_sub_sub, sub_zero]

lemma add_assoc {X Y : C} (a b c : X âŸ¶ Y) : (a + b) + c = a + (b + c) :=
begin
  conv_lhs { congr, rw add_def },
  rw [sub_add, â†add_neg, neg_sub', neg_neg]
end

lemma add_zero {X Y : C} (a : X âŸ¶ Y) : a + 0 = a :=
by rw [add_def, neg_def, sub_self, sub_zero]

lemma comp_sub {X Y Z : C} (f : X âŸ¶ Y) (g h : Y âŸ¶ Z) : f â‰« (g - h) = f â‰« g - f â‰« h :=
by rw [sub_def, â†category.assoc, prod.comp_lift, sub_def]

lemma sub_comp {X Y Z : C} (f g : X âŸ¶ Y) (h : Y âŸ¶ Z) : (f - g) â‰« h = f â‰« h - g â‰« h :=
by rw [sub_def, category.assoc, Ïƒ_comp, â†category.assoc, prod.lift_map, sub_def]

lemma comp_add (X Y Z : C) (f : X âŸ¶ Y) (g h : Y âŸ¶ Z) : f â‰« (g + h) = f â‰« g + f â‰« h :=
by rw [add_def, comp_sub, neg_def, comp_sub, comp_zero, add_def, neg_def]

lemma add_comp (X Y Z : C) (f g : X âŸ¶ Y) (h : Y âŸ¶ Z) : (f + g) â‰« h = f â‰« h + g â‰« h :=
by rw [add_def, sub_comp, neg_def, sub_comp, zero_comp, add_def, neg_def]

/-- Every `non_preadditive_abelian` category is preadditive. -/
def preadditive : preadditive C :=
{ hom_group := Î» X Y,
  { add := (+),
    add_assoc := add_assoc,
    zero := 0,
    zero_add := neg_neg,
    add_zero := add_zero,
    neg := Î» f, -f,
    add_left_neg := neg_add_self,
    add_comm := add_comm },
  add_comp' := add_comp,
  comp_add' := comp_add }

end

end category_theory.non_preadditive_abelian
