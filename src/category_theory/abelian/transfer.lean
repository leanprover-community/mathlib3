/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Jujian Zhang
-/
import category_theory.preadditive.additive_functor
import category_theory.abelian.basic
import category_theory.limits.preserves.shapes.kernels
import category_theory.adjunction.limits
import category_theory.abelian.exact
import category_theory.preadditive.injective

/-!
# Transferring categorical properties across a functor

## Transfering "abelian-ness" across a functor
If `C` is an additive category, `D` is an abelian category,
we have `F : C â¥¤ D` `G : D â¥¤ C` (both preserving zero morphisms),
`G` is left exact (that is, preserves finite limits),
and further we have `adj : G âŠ£ F` and `i : F â‹™ G â‰… ğŸ­ C`,
then `C` is also abelian.

See <https://stacks.math.columbia.edu/tag/03A3>

## Transfering "enough-injectiveness" across a functor
If `ğ“, ğ“‘` are abelian categories and `L âŠ£ R` is a pair of adjoint functors such that `L` is
faithful and exact (that is, preserves finite limits and colimits), then enough injectiveness of
`ğ“‘` implies enough injectiveness of `ğ“`.

## Notes
The hypotheses, following the statement from the Stacks project,
may appear suprising: we don't ask that the counit of the adjunction is an isomorphism,
but just that we have some potentially unrelated isomorphism `i : F â‹™ G â‰… ğŸ­ C`.

However Lemma A1.1.1 from [Elephant] shows that in this situation the counit itself
must be an isomorphism, and thus that `C` is a reflective subcategory of `D`.

Someone may like to formalize that lemma, and restate this theorem in terms of `reflective`.
(That lemma has a nice string diagrammatic proof that holds in any bicategory.)
-/

noncomputable theory

namespace category_theory
open category_theory.limits

universes v uâ‚ uâ‚‚

namespace abelian_of_adjunction

variables {C : Type uâ‚} [category.{v} C] [preadditive C]
variables {D : Type uâ‚‚} [category.{v} D] [abelian D]
variables (F : C â¥¤ D)
variables (G : D â¥¤ C) [functor.preserves_zero_morphisms G]
variables (i : F â‹™ G â‰… ğŸ­ C) (adj : G âŠ£ F)

include i

/-- No point making this an instance, as it requires `i`. -/
lemma has_kernels [preserves_finite_limits G] : has_kernels C :=
{ has_limit := Î» X Y f, begin
    have := nat_iso.naturality_1 i f,
    simp at this,
    rw â†this,
    haveI : has_kernel (G.map (F.map f) â‰« i.hom.app _) := limits.has_kernel_comp_mono _ _,
    apply limits.has_kernel_iso_comp,
  end }

include adj

/-- No point making this an instance, as it requires `i` and `adj`. -/
lemma has_cokernels : has_cokernels C :=
{ has_colimit := Î» X Y f, begin
    haveI : preserves_colimits G := adj.left_adjoint_preserves_colimits,
    have := nat_iso.naturality_1 i f,
    simp at this,
    rw â†this,
    haveI : has_cokernel (G.map (F.map f) â‰« i.hom.app _) := limits.has_cokernel_comp_iso _ _,
    apply limits.has_cokernel_epi_comp,
  end }

variables [limits.has_cokernels C]

/-- Auxiliary construction for `coimage_iso_image` -/
def cokernel_iso {X Y : C} (f : X âŸ¶ Y) : G.obj (cokernel (F.map f)) â‰… cokernel f :=
begin
  -- We have to write an explicit `preserves_colimits` type here,
  -- as `left_adjoint_preserves_colimits` has universe variables.
  haveI : preserves_colimits G := adj.left_adjoint_preserves_colimits,
  calc G.obj (cokernel (F.map f))
      â‰… cokernel (G.map (F.map f)) : (as_iso (cokernel_comparison _ G)).symm
  ... â‰… cokernel (_ â‰« f â‰« _)       : cokernel_iso_of_eq (nat_iso.naturality_2 i f).symm
  ... â‰… cokernel (f â‰« _)           : cokernel_epi_comp _ _
  ... â‰… cokernel f                 : cokernel_comp_is_iso _ _
end

variables [limits.has_kernels C] [preserves_finite_limits G]

/-- Auxiliary construction for `coimage_iso_image` -/
def coimage_iso_image_aux {X Y : C} (f : X âŸ¶ Y) :
  kernel (G.map (cokernel.Ï€ (F.map f))) â‰… kernel (cokernel.Ï€ f) :=
begin
  haveI : preserves_colimits G := adj.left_adjoint_preserves_colimits,
  calc kernel (G.map (cokernel.Ï€ (F.map f)))
      â‰… kernel (cokernel.Ï€ (G.map (F.map f)) â‰« cokernel_comparison (F.map f) G)
          : kernel_iso_of_eq (Ï€_comp_cokernel_comparison _ _).symm
  ... â‰… kernel (cokernel.Ï€ (G.map (F.map f))) : kernel_comp_mono _ _
  ... â‰… kernel (cokernel.Ï€ (_ â‰« f â‰« _) â‰« (cokernel_iso_of_eq _).hom)
          : kernel_iso_of_eq (Ï€_comp_cokernel_iso_of_eq_hom (nat_iso.naturality_2 i f)).symm
  ... â‰… kernel (cokernel.Ï€ (_ â‰« f â‰« _))       : kernel_comp_mono _ _
  ... â‰… kernel (cokernel.Ï€ (f â‰« i.inv.app Y) â‰« (cokernel_epi_comp (i.hom.app X) _).inv)
          : kernel_iso_of_eq (by simp only [cokernel.Ï€_desc, cokernel_epi_comp_inv])
  ... â‰… kernel (cokernel.Ï€ (f â‰« _))           : kernel_comp_mono _ _
  ... â‰… kernel (inv (i.inv.app Y) â‰« cokernel.Ï€ f â‰« (cokernel_comp_is_iso f (i.inv.app Y)).inv)
          : kernel_iso_of_eq (by simp only [cokernel.Ï€_desc, cokernel_comp_is_iso_inv,
              iso.hom_inv_id_app_assoc, nat_iso.inv_inv_app])
  ... â‰… kernel (cokernel.Ï€ f â‰« _)             : kernel_is_iso_comp _ _
  ... â‰… kernel (cokernel.Ï€ f)                 : kernel_comp_mono _ _
end

variables [functor.preserves_zero_morphisms F]

/--
Auxiliary definition: the abelian coimage and abelian image agree.
We still need to check that this agrees with the canonical morphism.
-/
def coimage_iso_image {X Y : C} (f : X âŸ¶ Y) : abelian.coimage f â‰… abelian.image f :=
begin
  haveI : preserves_limits F := adj.right_adjoint_preserves_limits,
  haveI : preserves_colimits G := adj.left_adjoint_preserves_colimits,
  calc abelian.coimage f
      â‰… cokernel (kernel.Î¹ f)                 : iso.refl _
  ... â‰… G.obj (cokernel (F.map (kernel.Î¹ f))) : (cokernel_iso _ _ i adj _).symm
  ... â‰… G.obj (cokernel (kernel_comparison f F â‰« (kernel.Î¹ (F.map f))))
                                              : G.map_iso (cokernel_iso_of_eq (by simp))
  ... â‰… G.obj (cokernel (kernel.Î¹ (F.map f))) : G.map_iso (cokernel_epi_comp _ _)
  ... â‰… G.obj (abelian.coimage (F.map f))     : iso.refl _
  ... â‰… G.obj (abelian.image (F.map f))       : G.map_iso (abelian.coimage_iso_image _)
  ... â‰… G.obj (kernel (cokernel.Ï€ (F.map f))) : iso.refl _
  ... â‰… kernel (G.map (cokernel.Ï€ (F.map f))) : preserves_kernel.iso _ _
  ... â‰… kernel (cokernel.Ï€ f)                 : coimage_iso_image_aux F G i adj f
  ... â‰… abelian.image f                       : iso.refl _,
end

local attribute [simp] cokernel_iso coimage_iso_image coimage_iso_image_aux

-- The account of this proof in the Stacks project omits this calculation.
lemma coimage_iso_image_hom {X Y : C} (f : X âŸ¶ Y) :
  (coimage_iso_image F G i adj f).hom = abelian.coimage_image_comparison f :=
begin
  ext,
  simpa only [â†G.map_comp_assoc, coimage_iso_image, nat_iso.inv_inv_app, cokernel_iso,
    coimage_iso_image_aux, iso.trans_symm, iso.symm_symm_eq, iso.refl_trans, iso.trans_refl,
    iso.trans_hom, iso.symm_hom, cokernel_comp_is_iso_inv, cokernel_epi_comp_inv, as_iso_hom,
    functor.map_iso_hom, cokernel_epi_comp_hom, preserves_kernel.iso_hom, kernel_comp_mono_hom,
    kernel_is_iso_comp_hom, cokernel_iso_of_eq_hom_comp_desc_assoc, cokernel.Ï€_desc_assoc,
    category.assoc, Ï€_comp_cokernel_iso_of_eq_inv_assoc, Ï€_comp_cokernel_comparison_assoc,
    kernel.lift_Î¹, kernel.lift_Î¹_assoc, kernel_iso_of_eq_hom_comp_Î¹_assoc,
    kernel_comparison_comp_Î¹_assoc,
    abelian.coimage_image_factorisation] using nat_iso.naturality_1 i f
end

end abelian_of_adjunction

open abelian_of_adjunction

/--
If `C` is an additive category, `D` is an abelian category,
we have `F : C â¥¤ D` `G : D â¥¤ C` (both preserving zero morphisms),
`G` is left exact (that is, preserves finite limits),
and further we have `adj : G âŠ£ F` and `i : F â‹™ G â‰… ğŸ­ C`,
then `C` is also abelian.

See <https://stacks.math.columbia.edu/tag/03A3>
-/
def abelian_of_adjunction
  {C : Type uâ‚} [category.{v} C] [preadditive C] [has_finite_products C]
  {D : Type uâ‚‚} [category.{v} D] [abelian D]
  (F : C â¥¤ D) [functor.preserves_zero_morphisms F]
  (G : D â¥¤ C) [functor.preserves_zero_morphisms G] [preserves_finite_limits G]
  (i : F â‹™ G â‰… ğŸ­ C) (adj : G âŠ£ F) : abelian C :=
begin
  haveI := has_kernels F G i, haveI := has_cokernels F G i adj,
  haveI : âˆ€ {X Y : C} (f : X âŸ¶ Y), is_iso (abelian.coimage_image_comparison f),
  { intros X Y f, rw â†coimage_iso_image_hom F G i adj f, apply_instance, },
  apply abelian.of_coimage_image_comparison_is_iso,
end

/--
If `C` is an additive category equivalent to an abelian category `D`
via a functor that preserves zero morphisms,
then `C` is also abelian.
-/
def abelian_of_equivalence
  {C : Type uâ‚} [category.{v} C] [preadditive C] [has_finite_products C]
  {D : Type uâ‚‚} [category.{v} D] [abelian D]
  (F : C â¥¤ D) [functor.preserves_zero_morphisms F] [is_equivalence F] : abelian C :=
abelian_of_adjunction F F.inv F.as_equivalence.unit_iso.symm F.as_equivalence.symm.to_adjunction

section transfer_enough_injectives

/-!
If `L âŠ£ R` are a pair of adjoint functors between abelian categories `ğ“` and `ğ“` and `L` is
faithful and exact, then if `ğ“‘` has enough injectives, so does `ğ“`. We achieve this by considering
an arbitrary injective presentation of `L(A) âŸ¶ J`: by adjunction, there is an `A âŸ¶ R(J)`, we will
prove that this `A âŸ¶ R(J)` is an injective presentation of `A`.
-/

open limits

universes vâ‚ vâ‚‚

variables {ğ“ : Type uâ‚} {ğ“‘ : Type uâ‚‚} [category.{vâ‚} ğ“] [category.{vâ‚‚} ğ“‘] [enough_injectives ğ“‘]
variables (L : ğ“ â¥¤ ğ“‘) (R : ğ“‘ â¥¤ ğ“)

namespace enough_injectives_of_adjunction

/--
Since `ğ“‘` has enough injectives, `L(A)` has an injective presentation for all `A âˆˆ ğ“`, i.e.
`L(A) â†’ J` where `J` is injective.-/
def injective_presentation_of_apply (A : ğ“) :
  injective_presentation (L.obj A) :=
(nonempty.some (enough_injectives.presentation (L.obj A)))

/--
Given injective presentation `L(A) â†’ J`, then `injective_object_of_adjunction A` is defined to be
`R(J)`. It will later be proven to be an injective object in `ğ“`.-/
def injective_object_of_adjunction (A : ğ“) : ğ“ := R.obj $ (injective_presentation_of_apply L A).J

variables (adj : L âŠ£ R)
variables {L R}

/--
If `g : X â†’ R(J)` and `f : X â†’ Y` is mono in `ğ“`, then there is an morphism `L(Y) â†’ J`
See the diagram below:

ğ“                             ğ“‘

A ---> R(J)                 L(A) -----> J <--------
      /                                /          |
     /                                /           |
    /  g                           by adjunction  |
   /                                /             |
  /                                /         by injectivity
X                              L(X)               |
|                               |                 |
v                               v                 |
Y                              L(Y) ---------------

-/
def to_J_of_injective_presentation_of_apply [preserves_finite_limits L]
  {A X Y : ğ“}
  (g : X âŸ¶ injective_object_of_adjunction L R A)
  (f : X âŸ¶ Y) [mono f] :
  L.obj Y âŸ¶ (injective_presentation_of_apply L A).J :=
let factors := (injective_presentation_of_apply L A).injective.factors in
(factors ((adj.hom_equiv X (injective_presentation_of_apply L A).J).symm g) (L.map f)).some

lemma comp_to_J_of_injective_presentation_of_apply [preserves_finite_limits L]
  {A X Y : ğ“}
  (g : X âŸ¶ injective_object_of_adjunction L R A)
  (f : X âŸ¶ Y) [mono f] :
  L.map f â‰« (to_J_of_injective_presentation_of_apply adj g f) =
  (adj.hom_equiv X (injective_presentation_of_apply L A).J).symm g :=
let factors := (injective_presentation_of_apply L A).injective.factors in
(factors ((adj.hom_equiv _ _).symm g) (L.map f)).some_spec


/--
If `g : X â†’ R(J)` and `f : X â†’ Y` is mono in `ğ“`, then there is an morphism `Y â†’ R(J)`
See the diagram below:

ğ“                                                  ğ“‘

A ---> R(J) <---                                   L(A) -----> J <--------
      /        |                                              /          |
     /         |                                             /           |
    /  g   by adjunction                                    /            |
   /           |                                           /             |
  /            |                                          /        by injectivity
X              |                                      L(X)               |
|              |                                       |                 |
v              |                                       v                 |
Y --------------                                      L(Y) ---------------

-/
def injective_object_of_adjunction.factor [preserves_finite_limits L]
  {A X Y : ğ“}
  (g: X âŸ¶ injective_object_of_adjunction L R A)
  (f : X âŸ¶ Y) [mono f] :
  Y âŸ¶ injective_object_of_adjunction L R A :=
adj.hom_equiv _ _ $ to_J_of_injective_presentation_of_apply adj g f

lemma injective_object_of_adjunction.comp [preserves_finite_limits L]
  {A X Y : ğ“}
  (g: X âŸ¶ injective_object_of_adjunction L R A)
  (f : X âŸ¶ Y) [mono f]:
  f â‰« injective_object_of_adjunction.factor adj g f = g :=
begin
  have := comp_to_J_of_injective_presentation_of_apply adj g f,
  rw â†adj.hom_equiv_apply_eq at this,
  rw [â†this],
  simp only [injective_object_of_adjunction.factor, to_J_of_injective_presentation_of_apply,
    adjunction.hom_equiv_counit, adjunction.hom_equiv_naturality_left_symm,
    adjunction.hom_equiv_naturality_right_symm, adjunction.left_triangle_components,
    category.id_comp, adjunction.hom_equiv_naturality_left,
    adjunction.hom_equiv_unit, functor.map_comp, adjunction.unit_naturality_assoc],
  congr,
  ext,
  generalize_proofs h1,
  rw h1.some_spec,
end

section

include adj

lemma injective_object_of_adjunction_is_injective [preserves_finite_limits L] (A : ğ“) :
  injective (injective_object_of_adjunction L R A) :=
{ factors := Î» X Y g f m,
  âŸ¨by { resetI, exact injective_object_of_adjunction.factor adj g f },
    by apply injective_object_of_adjunction.compâŸ© }

end

/-- just `R(J)`, rename for better clarity-/
def of_adjunction.presentation.J (A : ğ“) : ğ“ :=
injective_object_of_adjunction L R A

section

include adj

/-- This `R(J)` is injective-/
lemma of_adjunction.presentation.injective [preserves_finite_limits L] (A : ğ“) :
  injective (@of_adjunction.presentation.J ğ“ ğ“‘ _ _ _ L R A) :=
by apply injective_object_of_adjunction_is_injective adj

end

/-- the morphism `A â†’ R(J)` obtained by `L(A) â†’ J` via adjunction, this morphism is mono, so that
`A â†’ R(J)` is an injective presentation of `A` in `ğ“`.-/
def of_adjunction.presentation.f (A : ğ“) :
  A âŸ¶ injective_object_of_adjunction L R A :=
adj.hom_equiv A (injective_presentation_of_apply L A).J (injective_presentation_of_apply L A).f

instance of_adjunction.presentation.mono (A : ğ“)
  [abelian ğ“] [abelian ğ“‘] [preserves_finite_colimits L] [preserves_finite_limits L] [faithful L] :
  mono $ of_adjunction.presentation.f adj A :=
have e2 : exact (L.map (kernel.Î¹ (of_adjunction.presentation.f adj A)))
  (L.map (of_adjunction.presentation.f adj A)), from L.map_exact _ _ (exact_kernel_Î¹),
have eq1 : L.map (of_adjunction.presentation.f adj A) â‰« (adj.counit.app _) =
  (injective_presentation_of_apply L A).f, begin
  simp only [of_adjunction.presentation.f, adjunction.hom_equiv_unit, functor.map_comp,
  category.assoc, adjunction.counit_naturality, adjunction.left_triangle_components_assoc],
end,
have m1 : mono (L.map (of_adjunction.presentation.f adj A) â‰« (adj.counit.app _)), begin
  rw eq1, exactI (injective_presentation_of_apply L A).mono
end,
have m2 : mono (L.map (of_adjunction.presentation.f adj A)), begin
  exactI mono_of_mono _ (adj.counit.app (injective_presentation_of_apply L A).J),
end,
have eq2 : L.map (kernel.Î¹ (of_adjunction.presentation.f adj A)) =
  (preserves_kernel.iso L (of_adjunction.presentation.f adj A)).hom â‰«
      kernel.Î¹ (L.map (of_adjunction.presentation.f adj A)), begin
  simp only [preserves_kernel.iso_hom, kernel_comparison_comp_Î¹],
end,
have eq3 : kernel.Î¹ (of_adjunction.presentation.f adj A) = 0, from L.zero_of_map_zero _ begin
  rw abelian.mono_iff_kernel_Î¹_eq_zero at m2,
  rw [eq2, m2, comp_zero],
end,
by rw [abelian.mono_iff_kernel_Î¹_eq_zero, eq3]

end enough_injectives_of_adjunction

/--
faithful and exact left adjoint functor transfers enough injectiveness.-/
lemma enough_injectives.of_adjunction {ğ“ : Type uâ‚} {ğ“‘ : Type uâ‚‚}
  [category.{vâ‚} ğ“] [category.{vâ‚‚} ğ“‘] [abelian ğ“] [abelian ğ“‘]
  {L : ğ“ â¥¤ ğ“‘} {R : ğ“‘ â¥¤ ğ“} (adj : L âŠ£ R)
  [faithful L] [preserves_finite_limits L] [preserves_finite_colimits L]
  [enough_injectives ğ“‘] : enough_injectives ğ“ :=
{ presentation := Î» A, nonempty.intro
  { J := enough_injectives_of_adjunction.of_adjunction.presentation.J _,
    injective := enough_injectives_of_adjunction.of_adjunction.presentation.injective adj _,
    f := enough_injectives_of_adjunction.of_adjunction.presentation.f adj _,
    mono := enough_injectives_of_adjunction.of_adjunction.presentation.mono adj _ } }

/--
equivalence of category transfers enough injectiveness.-/
lemma enough_injectives.of_equivalence {ğ“ : Type uâ‚} {ğ“‘ : Type uâ‚‚}
  [category.{vâ‚} ğ“] [category.{vâ‚‚} ğ“‘] [abelian ğ“] [abelian ğ“‘]
  (e : ğ“ â¥¤ ğ“‘) [is_equivalence e] [enough_injectives ğ“‘] : enough_injectives ğ“ :=
@@enough_injectives.of_adjunction _ _ _ _ e.as_equivalence.to_adjunction _ _
  (@@limits.preserves_colimits.preserves_finite_colimits _ _ _
    (adjunction.is_equivalence_preserves_colimits e)) _

end transfer_enough_injectives

end category_theory
