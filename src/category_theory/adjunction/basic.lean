/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Johan Commelin, Bhavik Mehta
-/
import category_theory.equivalence
import data.equiv.basic

namespace category_theory
open category

-- declare the `v`'s first; see `category_theory.category` for an explanation
universes vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

local attribute [elab_simple] whisker_left whisker_right

variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚‚} D]

/--
`F âŠ£ G` represents the data of an adjunction between two functors
`F : C â¥¤ D` and `G : D â¥¤ C`. `F` is the left adjoint and `G` is the right adjoint.

To construct an `adjunction` between two functors, it's often easier to instead use the
constructors `mk_of_hom_equiv` or `mk_of_unit_counit`. To construct a left adjoint,
there are also constructors `left_adjoint_of_equiv` and `adjunction_of_equiv_left` (as
well as their duals) which can be simpler in practice.

Uniqueness of adjoints is shown in `category_theory.adjunction.opposites`.

See https://stacks.math.columbia.edu/tag/0037.
-/
structure adjunction (F : C â¥¤ D) (G : D â¥¤ C) :=
(hom_equiv : Î  (X Y), (F.obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G.obj Y))
(unit : ğŸ­ C âŸ¶ F.comp G)
(counit : G.comp F âŸ¶ ğŸ­ D)
(hom_equiv_unit' : Î  {X Y f}, (hom_equiv X Y) f = (unit : _ âŸ¶ _).app X â‰« G.map f . obviously)
(hom_equiv_counit' : Î  {X Y g}, (hom_equiv X Y).symm g = F.map g â‰« counit.app Y . obviously)

infix ` âŠ£ `:15 := adjunction

/-- A class giving a chosen right adjoint to the functor `left`. -/
class is_left_adjoint (left : C â¥¤ D) :=
(right : D â¥¤ C)
(adj : left âŠ£ right)

/-- A class giving a chosen left adjoint to the functor `right`. -/
class is_right_adjoint (right : D â¥¤ C) :=
(left : C â¥¤ D)
(adj : left âŠ£ right)

/-- Extract the left adjoint from the instance giving the chosen adjoint. -/
def left_adjoint (R : D â¥¤ C) [is_right_adjoint R] : C â¥¤ D :=
is_right_adjoint.left R
/-- Extract the right adjoint from the instance giving the chosen adjoint. -/
def right_adjoint (L : C â¥¤ D) [is_left_adjoint L] : D â¥¤ C :=
is_left_adjoint.right L

/-- The adjunction associated to a functor known to be a left adjoint. -/
def adjunction.of_left_adjoint (left : C â¥¤ D) [is_left_adjoint left] :
  adjunction left (right_adjoint left) :=
is_left_adjoint.adj
/-- The adjunction associated to a functor known to be a right adjoint. -/
def adjunction.of_right_adjoint (right : C â¥¤ D) [is_right_adjoint right] :
  adjunction (left_adjoint right) right :=
is_right_adjoint.adj

namespace adjunction

restate_axiom hom_equiv_unit'
restate_axiom hom_equiv_counit'
attribute [simp, priority 10] hom_equiv_unit hom_equiv_counit

section

variables {F : C â¥¤ D} {G : D â¥¤ C} (adj : F âŠ£ G) {X' X : C} {Y Y' : D}

@[simp, priority 10] lemma hom_equiv_naturality_left_symm (f : X' âŸ¶ X) (g : X âŸ¶ G.obj Y) :
  (adj.hom_equiv X' Y).symm (f â‰« g) = F.map f â‰« (adj.hom_equiv X Y).symm g :=
by rw [hom_equiv_counit, F.map_comp, assoc, adj.hom_equiv_counit.symm]

@[simp] lemma hom_equiv_naturality_left (f : X' âŸ¶ X) (g : F.obj X âŸ¶ Y) :
  (adj.hom_equiv X' Y) (F.map f â‰« g) = f â‰« (adj.hom_equiv X Y) g :=
by rw [â† equiv.eq_symm_apply]; simp [-hom_equiv_unit]

@[simp, priority 10] lemma hom_equiv_naturality_right (f : F.obj X âŸ¶ Y) (g : Y âŸ¶ Y') :
  (adj.hom_equiv X Y') (f â‰« g) = (adj.hom_equiv X Y) f â‰« G.map g :=
by rw [hom_equiv_unit, G.map_comp, â† assoc, â†hom_equiv_unit]

@[simp] lemma hom_equiv_naturality_right_symm (f : X âŸ¶ G.obj Y) (g : Y âŸ¶ Y') :
  (adj.hom_equiv X Y').symm (f â‰« G.map g) = (adj.hom_equiv X Y).symm f â‰« g :=
by rw [equiv.symm_apply_eq]; simp [-hom_equiv_counit]

@[simp] lemma left_triangle :
  (whisker_right adj.unit F) â‰« (whisker_left F adj.counit) = nat_trans.id _ :=
begin
  ext, dsimp,
  erw [â† adj.hom_equiv_counit, equiv.symm_apply_eq, adj.hom_equiv_unit],
  simp
end

@[simp] lemma right_triangle :
  (whisker_left G adj.unit) â‰« (whisker_right adj.counit G) = nat_trans.id _ :=
begin
  ext, dsimp,
  erw [â† adj.hom_equiv_unit, â† equiv.eq_symm_apply, adj.hom_equiv_counit],
  simp
end

@[simp, reassoc] lemma left_triangle_components :
  F.map (adj.unit.app X) â‰« adj.counit.app (F.obj X) = ğŸ™ (F.obj X) :=
congr_arg (Î» (t : nat_trans _ (ğŸ­ C â‹™ F)), t.app X) adj.left_triangle

@[simp, reassoc] lemma right_triangle_components {Y : D} :
  adj.unit.app (G.obj Y) â‰« G.map (adj.counit.app Y) = ğŸ™ (G.obj Y) :=
congr_arg (Î» (t : nat_trans _ (G â‹™ ğŸ­ C)), t.app Y) adj.right_triangle

@[simp, reassoc] lemma counit_naturality {X Y : D} (f : X âŸ¶ Y) :
  F.map (G.map f) â‰« (adj.counit).app Y = (adj.counit).app X â‰« f :=
adj.counit.naturality f

@[simp, reassoc] lemma unit_naturality {X Y : C} (f : X âŸ¶ Y) :
  (adj.unit).app X â‰« G.map (F.map f) = f â‰« (adj.unit).app Y :=
(adj.unit.naturality f).symm

lemma hom_equiv_apply_eq {A : C} {B : D} (f : F.obj A âŸ¶ B) (g : A âŸ¶ G.obj B) :
  adj.hom_equiv A B f = g â†” f = (adj.hom_equiv A B).symm g :=
âŸ¨Î» h, by {cases h, simp}, Î» h, by {cases h, simp}âŸ©

lemma eq_hom_equiv_apply {A : C} {B : D} (f : F.obj A âŸ¶ B) (g : A âŸ¶ G.obj B) :
  g = adj.hom_equiv A B f â†” (adj.hom_equiv A B).symm g = f :=
âŸ¨Î» h, by {cases h, simp}, Î» h, by {cases h, simp}âŸ©

end

end adjunction

namespace adjunction

/--
This is an auxiliary data structure useful for constructing adjunctions.
See `adjunction.mk_of_hom_equiv`.
This structure won't typically be used anywhere else.
-/
@[nolint has_inhabited_instance]
structure core_hom_equiv (F : C â¥¤ D) (G : D â¥¤ C) :=
(hom_equiv : Î  (X Y), (F.obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G.obj Y))
(hom_equiv_naturality_left_symm' : Î  {X' X Y} (f : X' âŸ¶ X) (g : X âŸ¶ G.obj Y),
  (hom_equiv X' Y).symm (f â‰« g) = F.map f â‰« (hom_equiv X Y).symm g . obviously)
(hom_equiv_naturality_right' : Î  {X Y Y'} (f : F.obj X âŸ¶ Y) (g : Y âŸ¶ Y'),
  (hom_equiv X Y') (f â‰« g) = (hom_equiv X Y) f â‰« G.map g . obviously)

namespace core_hom_equiv

restate_axiom hom_equiv_naturality_left_symm'
restate_axiom hom_equiv_naturality_right'
attribute [simp, priority 10] hom_equiv_naturality_left_symm hom_equiv_naturality_right

variables {F : C â¥¤ D} {G : D â¥¤ C} (adj : core_hom_equiv F G) {X' X : C} {Y Y' : D}

@[simp] lemma hom_equiv_naturality_left (f : X' âŸ¶ X) (g : F.obj X âŸ¶ Y) :
  (adj.hom_equiv X' Y) (F.map f â‰« g) = f â‰« (adj.hom_equiv X Y) g :=
by rw [â† equiv.eq_symm_apply]; simp

@[simp] lemma hom_equiv_naturality_right_symm (f : X âŸ¶ G.obj Y) (g : Y âŸ¶ Y') :
  (adj.hom_equiv X Y').symm (f â‰« G.map g) = (adj.hom_equiv X Y).symm f â‰« g :=
by rw [equiv.symm_apply_eq]; simp

end core_hom_equiv

/--
This is an auxiliary data structure useful for constructing adjunctions.
See `adjunction.mk_of_hom_equiv`.
This structure won't typically be used anywhere else.
-/
@[nolint has_inhabited_instance]
structure core_unit_counit (F : C â¥¤ D) (G : D â¥¤ C) :=
(unit : ğŸ­ C âŸ¶ F.comp G)
(counit : G.comp F âŸ¶ ğŸ­ D)
(left_triangle' : whisker_right unit F â‰« (functor.associator F G F).hom â‰« whisker_left F counit =
  nat_trans.id (ğŸ­ C â‹™ F) . obviously)
(right_triangle' : whisker_left G unit â‰« (functor.associator G F G).inv â‰« whisker_right counit G =
  nat_trans.id (G â‹™ ğŸ­ C) . obviously)

namespace core_unit_counit

restate_axiom left_triangle'
restate_axiom right_triangle'
attribute [simp] left_triangle right_triangle

end core_unit_counit

variables {F : C â¥¤ D} {G : D â¥¤ C}

/-- Construct an adjunction between `F` and `G` out of a natural bijection between each
`F.obj X âŸ¶ Y` and `X âŸ¶ G.obj Y`. -/
@[simps]
def mk_of_hom_equiv (adj : core_hom_equiv F G) : F âŠ£ G :=
{ unit :=
  { app := Î» X, (adj.hom_equiv X (F.obj X)) (ğŸ™ (F.obj X)),
    naturality' :=
    begin
      intros,
      erw [â† adj.hom_equiv_naturality_left, â† adj.hom_equiv_naturality_right],
      dsimp, simp -- See note [dsimp, simp].
    end },
  counit :=
  { app := Î» Y, (adj.hom_equiv _ _).inv_fun (ğŸ™ (G.obj Y)),
    naturality' :=
    begin
      intros,
      erw [â† adj.hom_equiv_naturality_left_symm, â† adj.hom_equiv_naturality_right_symm],
      dsimp, simp
    end },
  hom_equiv_unit' := Î» X Y f, by erw [â† adj.hom_equiv_naturality_right]; simp,
  hom_equiv_counit' := Î» X Y f, by erw [â† adj.hom_equiv_naturality_left_symm]; simp,
  .. adj }

/-- Construct an adjunction between functors `F` and `G` given a unit and counit for the adjunction
satisfying the triangle identities. -/
@[simps]
def mk_of_unit_counit (adj : core_unit_counit F G) : F âŠ£ G :=
{ hom_equiv := Î» X Y,
  { to_fun := Î» f, adj.unit.app X â‰« G.map f,
    inv_fun := Î» g, F.map g â‰« adj.counit.app Y,
    left_inv := Î» f, begin
      change F.map (_ â‰« _) â‰« _ = _,
      rw [F.map_comp, assoc, â†functor.comp_map, adj.counit.naturality, â†assoc],
      convert id_comp f,
      have t := congr_arg (Î» t : nat_trans _ _, t.app _) adj.left_triangle,
      dsimp at t,
      simp only [id_comp] at t,
      exact t,
    end,
    right_inv := Î» g, begin
      change _ â‰« G.map (_ â‰« _) = _,
      rw [G.map_comp, â†assoc, â†functor.comp_map, â†adj.unit.naturality, assoc],
      convert comp_id g,
      have t := congr_arg (Î» t : nat_trans _ _, t.app _) adj.right_triangle,
      dsimp at t,
      simp only [id_comp] at t,
      exact t,
  end },
  .. adj }

/-- The adjunction between the identity functor on a category and itself. -/
def id : ğŸ­ C âŠ£ ğŸ­ C :=
{ hom_equiv := Î» X Y, equiv.refl _,
  unit := ğŸ™ _,
  counit := ğŸ™ _ }

-- Satisfy the inhabited linter.
instance : inhabited (adjunction (ğŸ­ C) (ğŸ­ C)) := âŸ¨idâŸ©

/-- If F and G are naturally isomorphic functors, establish an equivalence of hom-sets. -/
@[simps]
def equiv_homset_left_of_nat_iso
  {F F' : C â¥¤ D} (iso : F â‰… F') {X : C} {Y : D} :
  (F.obj X âŸ¶ Y) â‰ƒ (F'.obj X âŸ¶ Y) :=
{ to_fun := Î» f, iso.inv.app _ â‰« f,
  inv_fun := Î» g, iso.hom.app _ â‰« g,
  left_inv := Î» f, by simp,
  right_inv := Î» g, by simp }

/-- If G and H are naturally isomorphic functors, establish an equivalence of hom-sets. -/
@[simps]
def equiv_homset_right_of_nat_iso
  {G G' : D â¥¤ C} (iso : G â‰… G') {X : C} {Y : D} :
  (X âŸ¶ G.obj Y) â‰ƒ (X âŸ¶ G'.obj Y) :=
{ to_fun := Î» f, f â‰« iso.hom.app _,
  inv_fun := Î» g, g â‰« iso.inv.app _,
  left_inv := Î» f, by simp,
  right_inv := Î» g, by simp }

/-- Transport an adjunction along an natural isomorphism on the left. -/
def of_nat_iso_left
  {F G : C â¥¤ D} {H : D â¥¤ C} (adj : F âŠ£ H) (iso : F â‰… G) :
  G âŠ£ H :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Î» X Y, (equiv_homset_left_of_nat_iso iso.symm).trans (adj.hom_equiv X Y) }

/-- Transport an adjunction along an natural isomorphism on the right. -/
def of_nat_iso_right
  {F : C â¥¤ D} {G H : D â¥¤ C} (adj : F âŠ£ G) (iso : G â‰… H) :
  F âŠ£ H :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Î» X Y, (adj.hom_equiv X Y).trans (equiv_homset_right_of_nat_iso iso) }

/-- Transport being a right adjoint along a natural isomorphism. -/
def right_adjoint_of_nat_iso {F G : C â¥¤ D} (h : F â‰… G) [r : is_right_adjoint F] :
  is_right_adjoint G :=
{ left := r.left,
  adj := of_nat_iso_right r.adj h }

/-- Transport being a left adjoint along a natural isomorphism. -/
def left_adjoint_of_nat_iso {F G : C â¥¤ D} (h : F â‰… G) [r : is_left_adjoint F] : is_left_adjoint G :=
{ right := r.right,
  adj := of_nat_iso_left r.adj h }

section
variables {E : Type uâ‚ƒ} [â„° : category.{vâ‚ƒ} E] (H : D â¥¤ E) (I : E â¥¤ D)

/--
Composition of adjunctions.

See https://stacks.math.columbia.edu/tag/0DV0.
-/
def comp (adjâ‚ : F âŠ£ G) (adjâ‚‚ : H âŠ£ I) : F â‹™ H âŠ£ I â‹™ G :=
{ hom_equiv := Î» X Z, equiv.trans (adjâ‚‚.hom_equiv _ _) (adjâ‚.hom_equiv _ _),
  unit := adjâ‚.unit â‰«
  (whisker_left F $ whisker_right adjâ‚‚.unit G) â‰« (functor.associator _ _ _).inv,
  counit := (functor.associator _ _ _).hom â‰«
    (whisker_left I $ whisker_right adjâ‚.counit H) â‰« adjâ‚‚.counit }

/-- If `F` and `G` are left adjoints then `F â‹™ G` is a left adjoint too. -/
instance left_adjoint_of_comp {E : Type uâ‚ƒ} [â„° : category.{vâ‚ƒ} E] (F : C â¥¤ D) (G : D â¥¤ E)
  [Fl : is_left_adjoint F] [Gl : is_left_adjoint G] : is_left_adjoint (F â‹™ G) :=
{ right := Gl.right â‹™ Fl.right,
  adj := comp _ _ Fl.adj Gl.adj }

/-- If `F` and `G` are right adjoints then `F â‹™ G` is a right adjoint too. -/
instance right_adjoint_of_comp {E : Type uâ‚ƒ} [â„° : category.{vâ‚ƒ} E] {F : C â¥¤ D} {G : D â¥¤ E}
  [Fr : is_right_adjoint F] [Gr : is_right_adjoint G] : is_right_adjoint (F â‹™ G) :=
{ left := Gr.left â‹™ Fr.left,
  adj := comp _ _ Gr.adj Fr.adj }

end

section construct_left
-- Construction of a left adjoint. In order to construct a left
-- adjoint to a functor G : D â†’ C, it suffices to give the object part
-- of a functor F : C â†’ D together with isomorphisms Hom(FX, Y) â‰ƒ
-- Hom(X, GY) natural in Y. The action of F on morphisms can be
-- constructed from this data.
variables {F_obj : C â†’ D} {G}
variables (e : Î  X Y, (F_obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G.obj Y))
variables (he : âˆ€ X Y Y' g h, e X Y' (h â‰« g) = e X Y h â‰« G.map g)
include he

private lemma he' {X Y Y'} (f g) : (e X Y').symm (f â‰« G.map g) = (e X Y).symm f â‰« g :=
by intros; rw [equiv.symm_apply_eq, he]; simp

/-- Construct a left adjoint functor to `G`, given the functor's value on objects `F_obj` and
a bijection `e` between `F_obj X âŸ¶ Y` and `X âŸ¶ G.obj Y` satisfying a naturality law
`he : âˆ€ X Y Y' g h, e X Y' (h â‰« g) = e X Y h â‰« G.map g`.
Dual to `right_adjoint_of_equiv`. -/
@[simps]
def left_adjoint_of_equiv : C â¥¤ D :=
{ obj := F_obj,
  map := Î» X X' f, (e X (F_obj X')).symm (f â‰« e X' (F_obj X') (ğŸ™ _)),
  map_comp' := Î» X X' X'' f f', begin
    rw [equiv.symm_apply_eq, he, equiv.apply_symm_apply],
    conv { to_rhs, rw [assoc, â†he, id_comp, equiv.apply_symm_apply] },
    simp
  end }

/-- Show that the functor given by `left_adjoint_of_equiv` is indeed left adjoint to `G`. Dual
to `adjunction_of_equiv_right`. -/
@[simps]
def adjunction_of_equiv_left : left_adjoint_of_equiv e he âŠ£ G :=
mk_of_hom_equiv
{ hom_equiv := e,
  hom_equiv_naturality_left_symm' :=
  begin
    intros,
    erw [â† he' e he, â† equiv.apply_eq_iff_eq],
    simp [(he _ _ _ _ _).symm]
  end }

end construct_left

section construct_right
-- Construction of a right adjoint, analogous to the above.
variables {F} {G_obj : D â†’ C}
variables (e : Î  X Y, (F.obj X âŸ¶ Y) â‰ƒ (X âŸ¶ G_obj Y))
variables (he : âˆ€ X' X Y f g, e X' Y (F.map f â‰« g) = f â‰« e X Y g)
include he

private lemma he' {X' X Y} (f g) : F.map f â‰« (e X Y).symm g = (e X' Y).symm (f â‰« g) :=
by intros; rw [equiv.eq_symm_apply, he]; simp

/-- Construct a right adjoint functor to `F`, given the functor's value on objects `G_obj` and
a bijection `e` between `F.obj X âŸ¶ Y` and `X âŸ¶ G_obj Y` satisfying a naturality law
`he : âˆ€ X Y Y' g h, e X' Y (F.map f â‰« g) = f â‰« e X Y g`.
Dual to `left_adjoint_of_equiv`. -/
@[simps]
def right_adjoint_of_equiv : D â¥¤ C :=
{ obj := G_obj,
  map := Î» Y Y' g, (e (G_obj Y) Y') ((e (G_obj Y) Y).symm (ğŸ™ _) â‰« g),
  map_comp' := Î» Y Y' Y'' g g', begin
    rw [â† equiv.eq_symm_apply, â† he' e he, equiv.symm_apply_apply],
    conv { to_rhs, rw [â† assoc, he' e he, comp_id, equiv.symm_apply_apply] },
    simp
  end }

/-- Show that the functor given by `right_adjoint_of_equiv` is indeed right adjoint to `F`. Dual
to `adjunction_of_equiv_left`. -/
@[simps]
def adjunction_of_equiv_right : F âŠ£ right_adjoint_of_equiv e he :=
mk_of_hom_equiv
{ hom_equiv := e,
  hom_equiv_naturality_left_symm' := by intros; rw [equiv.symm_apply_eq, he]; simp,
  hom_equiv_naturality_right' :=
  begin
    intros X Y Y' g h,
    erw [â†he, equiv.apply_eq_iff_eq, â†assoc, he' e he, comp_id, equiv.symm_apply_apply]
  end }

end construct_right

/--
If the unit and counit of a given adjunction are (pointwise) isomorphisms, then we can upgrade the
adjunction to an equivalence.
-/
@[simps]
noncomputable
def to_equivalence (adj : F âŠ£ G) [âˆ€ X, is_iso (adj.unit.app X)] [âˆ€ Y, is_iso (adj.counit.app Y)] :
  C â‰Œ D :=
{ functor := F,
  inverse := G,
  unit_iso := nat_iso.of_components (Î» X, as_iso (adj.unit.app X)) (by simp),
  counit_iso := nat_iso.of_components (Î» Y, as_iso (adj.counit.app Y)) (by simp) }

/--
If the unit and counit for the adjunction corresponding to a right adjoint functor are (pointwise)
isomorphisms, then the functor is an equivalence of categories.
-/
@[simps]
noncomputable
def is_right_adjoint_to_is_equivalence [is_right_adjoint G]
  [âˆ€ X, is_iso ((adjunction.of_right_adjoint G).unit.app X)]
  [âˆ€ Y, is_iso ((adjunction.of_right_adjoint G).counit.app Y)] :
  is_equivalence G :=
is_equivalence.of_equivalence_inverse (adjunction.of_right_adjoint G).to_equivalence

end adjunction

open adjunction

namespace equivalence

/-- The adjunction given by an equivalence of categories. (To obtain the opposite adjunction,
simply use `e.symm.to_adjunction`. -/
def to_adjunction (e : C â‰Œ D) : e.functor âŠ£ e.inverse :=
mk_of_unit_counit âŸ¨e.unit, e.counit,
  by { ext, dsimp, simp only [id_comp], exact e.functor_unit_comp _, },
  by { ext, dsimp, simp only [id_comp], exact e.unit_inverse_comp _, }âŸ©

end equivalence

namespace functor

/-- An equivalence `E` is left adjoint to its inverse. -/
def adjunction (E : C â¥¤ D) [is_equivalence E] : E âŠ£ E.inv :=
(E.as_equivalence).to_adjunction

/-- If `F` is an equivalence, it's a left adjoint. -/
@[priority 10]
instance left_adjoint_of_equivalence {F : C â¥¤ D} [is_equivalence F] : is_left_adjoint F :=
{ right := _,
  adj := functor.adjunction F }

@[simp]
lemma right_adjoint_of_is_equivalence {F : C â¥¤ D} [is_equivalence F] : right_adjoint F = inv F :=
rfl

/-- If `F` is an equivalence, it's a right adjoint. -/
@[priority 10]
instance right_adjoint_of_equivalence {F : C â¥¤ D} [is_equivalence F] : is_right_adjoint F :=
{ left := _,
  adj := functor.adjunction F.inv }

@[simp]
lemma left_adjoint_of_is_equivalence {F : C â¥¤ D} [is_equivalence F] : left_adjoint F = inv F :=
rfl

end functor

end category_theory
