/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.adjunction.basic
import category_theory.conj
import category_theory.yoneda

/-!
# Mate of natural transformations

This file establishes the bijection between the 2-cells

         Lâ‚                  Râ‚
      C --â†’ D             C â†-- D
    G â†“  â†—  â†“ H         G â†“  â†˜  â†“ H
      E --â†’ F             E â†-- F
         Lâ‚‚                  Râ‚‚

where `Lâ‚ âŠ£ Râ‚` and `Lâ‚‚ âŠ£ Râ‚‚`, and shows that in the special case where `G,H` are identity then the
bijection preserves and reflects isomorphisms (i.e. we have bijections `(Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ (Râ‚ âŸ¶ Râ‚‚)`, and
if either side is an iso then the other side is as well).

On its own, this bijection is not particularly useful but it includes a number of interesting cases
as specializations.

For instance, this generalises the fact that adjunctions are unique (since if `Lâ‚ â‰… Lâ‚‚` then we
deduce `Râ‚ â‰… Râ‚‚`).
Another example arises from considering the square representing that a functor `H` preserves
products, in particular the morphism `HA â¨¯ H- âŸ¶ H(A â¨¯ -)`. Then provided `(A â¨¯ -)` and `HA â¨¯ -` have
left adjoints (for instance if the relevant categories are cartesian closed), the transferred
natural transformation is the exponential comparison morphism: `H(A ^ -) âŸ¶ HA ^ H-`.
Furthermore if `H` has a left adjoint `L`, this morphism is an isomorphism iff its mate
`L(HA â¨¯ -) âŸ¶ A â¨¯ L-` is an isomorphism, see
https://ncatlab.org/nlab/show/Frobenius+reciprocity#InCategoryTheory.
This also relates to Grothendieck's yoga of six operations, though this is not spelled out in
mathlib: https://ncatlab.org/nlab/show/six+operations.
-/
universes vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

namespace category_theory
open category

variables {C : Type uâ‚} {D : Type uâ‚‚} [category.{vâ‚} C] [category.{vâ‚‚} D]

section square

variables {E : Type uâ‚ƒ} {F : Type uâ‚„} [category.{vâ‚ƒ} E] [category.{vâ‚„} F]

variables {G : C â¥¤ E} {H : D â¥¤ F} {Lâ‚ : C â¥¤ D} {Râ‚ : D â¥¤ C} {Lâ‚‚ : E â¥¤ F} {Râ‚‚ : F â¥¤ E}
variables (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚)

include adjâ‚ adjâ‚‚

/--
Suppose we have a square of functors (where the top and bottom are adjunctions `Lâ‚ âŠ£ Râ‚` and
`Lâ‚‚ âŠ£ Râ‚‚` respectively).

      C â†” D
    G â†“   â†“ H
      E â†” F

Then we have a bijection between natural transformations `G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H` and
`Râ‚ â‹™ G âŸ¶ H â‹™ Râ‚‚`.
This can be seen as a bijection of the 2-cells:

         Lâ‚                  Râ‚
      C --â†’ D             C â†-- D
    G â†“  â†—  â†“ H         G â†“  â†˜  â†“ H
      E --â†’ F             E â†-- F
         Lâ‚‚                  Râ‚‚

Note that if one of the transformations is an iso, it does not imply the other is an iso.
-/
def transfer_nat_trans : (G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) â‰ƒ (Râ‚ â‹™ G âŸ¶ H â‹™ Râ‚‚) :=
{ to_fun := Î» h,
  { app := Î» X, adjâ‚‚.unit.app _ â‰« Râ‚‚.map (h.app _ â‰« H.map (adjâ‚.counit.app _)),
    naturality' := Î» X Y f,
    begin
      dsimp,
      rw [assoc, â† Râ‚‚.map_comp, assoc, â† H.map_comp, â† adjâ‚.counit_naturality, H.map_comp,
          â†functor.comp_map Lâ‚, â†h.naturality_assoc],
      simp,
    end },
  inv_fun := Î» h,
  { app := Î» X, Lâ‚‚.map (G.map (adjâ‚.unit.app _) â‰« h.app _) â‰« adjâ‚‚.counit.app _,
  naturality' := Î» X Y f,
  begin
    dsimp,
    rw [â† Lâ‚‚.map_comp_assoc, â† G.map_comp_assoc, â† adjâ‚.unit_naturality, G.map_comp_assoc,
        â† functor.comp_map, h.naturality],
    simp,
  end },
  left_inv := Î» h,
  begin
    ext X,
    dsimp,
    simp only [Lâ‚‚.map_comp, assoc, adjâ‚‚.counit_naturality, adjâ‚‚.left_triangle_components_assoc,
      â†functor.comp_map G Lâ‚‚, h.naturality_assoc, functor.comp_map Lâ‚, â†H.map_comp,
      adjâ‚.left_triangle_components],
    dsimp,
    simp, -- See library note [dsimp, simp].
  end,
  right_inv := Î» h,
  begin
    ext X,
    dsimp,
    simp [-functor.comp_map, â†functor.comp_map H, functor.comp_map Râ‚, -nat_trans.naturality,
      â†h.naturality, -functor.map_comp, â†functor.map_comp_assoc G, Râ‚‚.map_comp],
  end }

lemma transfer_nat_trans_counit (f : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) (Y : D) :
  Lâ‚‚.map ((transfer_nat_trans adjâ‚ adjâ‚‚ f).app _) â‰« adjâ‚‚.counit.app _ =
    f.app _ â‰« H.map (adjâ‚.counit.app Y) :=
by { erw functor.map_comp, simp }

lemma unit_transfer_nat_trans (f : G â‹™ Lâ‚‚ âŸ¶ Lâ‚ â‹™ H) (X : C) :
  G.map (adjâ‚.unit.app X) â‰« (transfer_nat_trans adjâ‚ adjâ‚‚ f).app _ =
    adjâ‚‚.unit.app _ â‰« Râ‚‚.map (f.app _) :=
begin
  dsimp [transfer_nat_trans],
  rw [â†adjâ‚‚.unit_naturality_assoc, â†Râ‚‚.map_comp, â† functor.comp_map G Lâ‚‚, f.naturality_assoc,
    functor.comp_map, â† H.map_comp],
  dsimp, simp, -- See library note [dsimp, simp]
end

end square

section self

variables {Lâ‚ Lâ‚‚ Lâ‚ƒ : C â¥¤ D} {Râ‚ Râ‚‚ Râ‚ƒ : D â¥¤ C}
variables (adjâ‚ : Lâ‚ âŠ£ Râ‚) (adjâ‚‚ : Lâ‚‚ âŠ£ Râ‚‚) (adjâ‚ƒ : Lâ‚ƒ âŠ£ Râ‚ƒ)

/--
Given two adjunctions `Lâ‚ âŠ£ Râ‚` and `Lâ‚‚ âŠ£ Râ‚‚` both between categories `C`, `D`, there is a
bijection between natural transformations `Lâ‚‚ âŸ¶ Lâ‚` and natural transformations `Râ‚ âŸ¶ Râ‚‚`.
This is defined as a special case of `transfer_nat_trans`, where the two "vertical" functors are
identity.
TODO: Generalise to when the two vertical functors are equivalences rather than being exactly `ğŸ­`.

Furthermore, this bijection preserves (and reflects) isomorphisms, i.e. a transformation is an iso
iff its image under the bijection is an iso, see eg `category_theory.transfer_nat_trans_self_iso`.
This is in contrast to the general case `transfer_nat_trans` which does not in general have this
property.
-/
def transfer_nat_trans_self : (Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ (Râ‚ âŸ¶ Râ‚‚) :=
calc (Lâ‚‚ âŸ¶ Lâ‚) â‰ƒ _         : (iso.hom_congr Lâ‚‚.left_unitor Lâ‚.right_unitor).symm
           ... â‰ƒ _         : transfer_nat_trans adjâ‚ adjâ‚‚
           ... â‰ƒ (Râ‚ âŸ¶ Râ‚‚) : Râ‚.right_unitor.hom_congr Râ‚‚.left_unitor

lemma transfer_nat_trans_self_counit (f : Lâ‚‚ âŸ¶ Lâ‚) (X) :
  Lâ‚‚.map ((transfer_nat_trans_self adjâ‚ adjâ‚‚ f).app _) â‰« adjâ‚‚.counit.app X =
    f.app _ â‰« adjâ‚.counit.app X :=
begin
  dsimp [transfer_nat_trans_self],
  rw [id_comp, comp_id],
  have := transfer_nat_trans_counit adjâ‚ adjâ‚‚ (Lâ‚‚.left_unitor.hom â‰« f â‰« Lâ‚.right_unitor.inv) X,
  dsimp at this,
  rw this,
  simp,
end

lemma unit_transfer_nat_trans_self (f : Lâ‚‚ âŸ¶ Lâ‚) (X) :
  adjâ‚.unit.app _ â‰« (transfer_nat_trans_self adjâ‚ adjâ‚‚ f).app _ =
    adjâ‚‚.unit.app X â‰« functor.map _ (f.app _) :=
begin
  dsimp [transfer_nat_trans_self],
  rw [id_comp, comp_id],
  have := unit_transfer_nat_trans adjâ‚ adjâ‚‚ (Lâ‚‚.left_unitor.hom â‰« f â‰« Lâ‚.right_unitor.inv) X,
  dsimp at this,
  rw this,
  simp
end

@[simp]
lemma transfer_nat_trans_self_id : transfer_nat_trans_self adjâ‚ adjâ‚ (ğŸ™ _) = ğŸ™ _ :=
by { ext, dsimp [transfer_nat_trans_self, transfer_nat_trans], simp }
  -- See library note [dsimp, simp]

@[simp]
lemma transfer_nat_trans_self_symm_id :
  (transfer_nat_trans_self adjâ‚ adjâ‚).symm (ğŸ™ _) = ğŸ™ _ :=
by { rw equiv.symm_apply_eq, simp }

lemma transfer_nat_trans_self_comp (f g) :
  transfer_nat_trans_self adjâ‚ adjâ‚‚ f â‰« transfer_nat_trans_self adjâ‚‚ adjâ‚ƒ g =
    transfer_nat_trans_self adjâ‚ adjâ‚ƒ (g â‰« f) :=
begin
  ext,
  dsimp [transfer_nat_trans_self, transfer_nat_trans],
  simp only [id_comp, comp_id],
  rw [â†adjâ‚ƒ.unit_naturality_assoc, â†Râ‚ƒ.map_comp, g.naturality_assoc, Lâ‚‚.map_comp, assoc,
    adjâ‚‚.counit_naturality, adjâ‚‚.left_triangle_components_assoc, assoc],
end

lemma transfer_nat_trans_self_symm_comp (f g) :
  (transfer_nat_trans_self adjâ‚‚ adjâ‚).symm f â‰« (transfer_nat_trans_self adjâ‚ƒ adjâ‚‚).symm g =
    (transfer_nat_trans_self adjâ‚ƒ adjâ‚).symm (g â‰« f) :=
by { rw [equiv.eq_symm_apply, â† transfer_nat_trans_self_comp _ adjâ‚‚], simp }

lemma transfer_nat_trans_self_comm {f g} (gf : g â‰« f = ğŸ™ _) :
  transfer_nat_trans_self adjâ‚ adjâ‚‚ f â‰« transfer_nat_trans_self adjâ‚‚ adjâ‚ g = ğŸ™ _ :=
by rw [transfer_nat_trans_self_comp, gf, transfer_nat_trans_self_id]

lemma transfer_nat_trans_self_symm_comm {f g} (gf : g â‰« f = ğŸ™ _) :
  (transfer_nat_trans_self adjâ‚ adjâ‚‚).symm f â‰« (transfer_nat_trans_self adjâ‚‚ adjâ‚).symm g = ğŸ™ _ :=
by rw [transfer_nat_trans_self_symm_comp, gf, transfer_nat_trans_self_symm_id]

/--
If `f` is an isomorphism, then the transferred natural transformation is an isomorphism.
The converse is given in `transfer_nat_trans_self_of_iso`.
-/
instance transfer_nat_trans_self_iso (f : Lâ‚‚ âŸ¶ Lâ‚) [is_iso f] :
  is_iso (transfer_nat_trans_self adjâ‚ adjâ‚‚ f) :=
âŸ¨âŸ¨transfer_nat_trans_self adjâ‚‚ adjâ‚ (inv f),
  âŸ¨transfer_nat_trans_self_comm _ _ (by simp), transfer_nat_trans_self_comm _ _ (by simp)âŸ©âŸ©âŸ©

/--
If `f` is an isomorphism, then the un-transferred natural transformation is an isomorphism.
The converse is given in `transfer_nat_trans_self_symm_of_iso`.
-/
instance transfer_nat_trans_self_symm_iso (f : Râ‚ âŸ¶ Râ‚‚) [is_iso f] :
  is_iso ((transfer_nat_trans_self adjâ‚ adjâ‚‚).symm f) :=
âŸ¨âŸ¨(transfer_nat_trans_self adjâ‚‚ adjâ‚).symm (inv f),
  âŸ¨transfer_nat_trans_self_symm_comm _ _ (by simp),
   transfer_nat_trans_self_symm_comm _ _ (by simp)âŸ©âŸ©âŸ©

/--
If `f` is a natural transformation whose transferred natural transformation is an isomorphism,
then `f` is an isomorphism.
The converse is given in `transfer_nat_trans_self_iso`.
-/
lemma transfer_nat_trans_self_of_iso (f : Lâ‚‚ âŸ¶ Lâ‚) [is_iso (transfer_nat_trans_self adjâ‚ adjâ‚‚ f)] :
  is_iso f :=
begin
  suffices :
    is_iso ((transfer_nat_trans_self adjâ‚ adjâ‚‚).symm (transfer_nat_trans_self adjâ‚ adjâ‚‚ f)),
  { simpa using this },
  apply_instance,
end

/--
If `f` is a natural transformation whose un-transferred natural transformation is an isomorphism,
then `f` is an isomorphism.
The converse is given in `transfer_nat_trans_self_symm_iso`.
-/
lemma transfer_nat_trans_self_symm_of_iso (f : Râ‚ âŸ¶ Râ‚‚)
  [is_iso ((transfer_nat_trans_self adjâ‚ adjâ‚‚).symm f)] :
  is_iso f :=
begin
  suffices :
    is_iso ((transfer_nat_trans_self adjâ‚ adjâ‚‚) ((transfer_nat_trans_self adjâ‚ adjâ‚‚).symm f)),
  { simpa using this },
  apply_instance,
end

end self

end category_theory
