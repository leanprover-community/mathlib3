/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Thomas Read
-/

import category_theory.adjunction.basic
import category_theory.yoneda
import category_theory.opposites

/-!
# Opposite adjunctions

This file contains constructions to relate adjunctions of functors to adjunctions of their
opposites.
These constructions are used to show uniqueness of adjoints (up to natural isomorphism).

## Tags
adjunction, opposite, uniqueness
-/


open category_theory

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ -- morphism levels before object levels. See note [category_theory universes].

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]

namespace adjunction

/-- If `G.op` is adjoint to `F.op` then `F` is adjoint to `G`. -/
def adjoint_of_op_adjoint_op (F : C ‚•§ D) (G : D ‚•§ C) (h : G.op ‚ä£ F.op) : F ‚ä£ G :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X Y,
  ((h.hom_equiv (opposite.op Y) (opposite.op X)).trans (op_equiv _ _)).symm.trans (op_equiv _ _) }

/-- If `G` is adjoint to `F.op` then `F` is adjoint to `G.unop`. -/
def adjoint_unop_of_adjoint_op (F : C ‚•§ D) (G : D·µí·µñ ‚•§ C·µí·µñ) (h : G ‚ä£ F.op) : F ‚ä£ G.unop :=
adjoint_of_op_adjoint_op F G.unop (h.of_nat_iso_left G.op_unop_iso.symm)

/-- If `G.op` is adjoint to `F` then `F.unop` is adjoint to `G`. -/
def unop_adjoint_of_op_adjoint (F : C·µí·µñ ‚•§ D·µí·µñ) (G : D ‚•§ C) (h : G.op ‚ä£ F) : F.unop ‚ä£ G :=
adjoint_of_op_adjoint_op _ _ (h.of_nat_iso_right F.op_unop_iso.symm)

/-- If `G` is adjoint to `F` then `F.unop` is adjoint to `G.unop`. -/
def unop_adjoint_unop_of_adjoint (F : C·µí·µñ ‚•§ D·µí·µñ) (G : D·µí·µñ ‚•§ C·µí·µñ) (h : G ‚ä£ F) : F.unop ‚ä£ G.unop :=
adjoint_unop_of_adjoint_op F.unop G (h.of_nat_iso_right F.op_unop_iso.symm)

/-- If `G` is adjoint to `F` then `F.op` is adjoint to `G.op`. -/
def op_adjoint_op_of_adjoint (F : C ‚•§ D) (G : D ‚•§ C) (h : G ‚ä£ F) : F.op ‚ä£ G.op :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X Y,
  (op_equiv _ Y).trans ((h.hom_equiv _ _).symm.trans (op_equiv X (opposite.op _)).symm) }

/-- If `G` is adjoint to `F.unop` then `F` is adjoint to `G.op`. -/
def adjoint_op_of_adjoint_unop (F : C·µí·µñ ‚•§ D·µí·µñ) (G : D ‚•§ C) (h : G ‚ä£ F.unop) : F ‚ä£ G.op :=
(op_adjoint_op_of_adjoint F.unop _ h).of_nat_iso_left F.op_unop_iso

/-- If `G.unop` is adjoint to `F` then `F.op` is adjoint to `G`. -/
def op_adjoint_of_unop_adjoint (F : C ‚•§ D) (G : D·µí·µñ ‚•§ C·µí·µñ) (h : G.unop ‚ä£ F) : F.op ‚ä£ G :=
(op_adjoint_op_of_adjoint _ G.unop h).of_nat_iso_right G.op_unop_iso

/-- If `G.unop` is adjoint to `F.unop` then `F` is adjoint to `G`. -/
def adjoint_of_unop_adjoint_unop (F : C·µí·µñ ‚•§ D·µí·µñ) (G : D·µí·µñ ‚•§ C·µí·µñ) (h : G.unop ‚ä£ F.unop) : F ‚ä£ G :=
(adjoint_op_of_adjoint_unop _ _ h).of_nat_iso_right G.op_unop_iso

/--
If `F` and `F'` are both adjoint to `G`, there is a natural isomorphism
`F.op ‚ãô coyoneda ‚âÖ F'.op ‚ãô coyoneda`.
We use this in combination with `fully_faithful_cancel_right` to show left adjoints are unique.
-/
def left_adjoints_coyoneda_equiv {F F' : C ‚•§ D} {G : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F' ‚ä£ G):
  F.op ‚ãô coyoneda ‚âÖ F'.op ‚ãô coyoneda :=
nat_iso.of_components
  (Œª X, nat_iso.of_components
    (Œª Y, ((adj1.hom_equiv X.unop Y).trans (adj2.hom_equiv X.unop Y).symm).to_iso)
    (by tidy))
  (by tidy)

/-- If `F` and `F'` are both left adjoint to `G`, then they are naturally isomorphic. -/
def left_adjoint_uniq {F F' : C ‚•§ D} {G : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F' ‚ä£ G) : F ‚âÖ F' :=
nat_iso.remove_op (fully_faithful_cancel_right _ (left_adjoints_coyoneda_equiv adj2 adj1))

/-- If `G` and `G'` are both right adjoint to `F`, then they are naturally isomorphic. -/
def right_adjoint_uniq {F : C ‚•§ D} {G G' : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F ‚ä£ G') : G ‚âÖ G' :=
nat_iso.remove_op
  (left_adjoint_uniq (op_adjoint_op_of_adjoint _ F adj2) (op_adjoint_op_of_adjoint _ _ adj1))

lemma left_adjoint_uniq_hom_app {F F' : C ‚•§ D} {G : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F' ‚ä£ G) (X) :
  (left_adjoint_uniq adj1 adj2).hom.app X = (adj1.hom_equiv _ _).symm (adj2.hom_equiv _ _ (ùüô _)) :=
rfl

lemma left_adjoint_uniq_inv_app {F F' : C ‚•§ D} {G : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F' ‚ä£ G) (X) :
  (left_adjoint_uniq adj1 adj2).inv.app X = (adj2.hom_equiv _ _).symm (adj1.hom_equiv _ _ (ùüô _)) :=
rfl

lemma right_adjoint_uniq_hom_app {F : C ‚•§ D} {G G' : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F ‚ä£ G') (X) :
  (right_adjoint_uniq adj1 adj2).hom.app X = adj2.hom_equiv _ _ ((adj1.hom_equiv _ _).symm (ùüô _)) :=
rfl

lemma right_adjoint_uniq_inv_app {F : C ‚•§ D} {G G' : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F ‚ä£ G') (X) :
  (right_adjoint_uniq adj1 adj2).inv.app X = adj1.hom_equiv _ _ ((adj2.hom_equiv _ _).symm (ùüô _)) :=
rfl

/--
Given two adjunctions, if the left adjoints are naturally isomorphic, then so are the right
adjoints.
-/
def nat_iso_of_left_adjoint_nat_iso {F F' : C ‚•§ D} {G G' : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F' ‚ä£ G') (l : F ‚âÖ F') :
  G ‚âÖ G' :=
right_adjoint_uniq adj1 (adj2.of_nat_iso_left l.symm)

/--
Given two adjunctions, if the right adjoints are naturally isomorphic, then so are the left
adjoints.
-/
def nat_iso_of_right_adjoint_nat_iso {F F' : C ‚•§ D} {G G' : D ‚•§ C}
  (adj1 : F ‚ä£ G) (adj2 : F' ‚ä£ G') (r : G ‚âÖ G') :
  F ‚âÖ F' :=
left_adjoint_uniq adj1 (adj2.of_nat_iso_right r.symm)

end adjunction
