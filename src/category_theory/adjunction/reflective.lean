/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.limits.preserves.shapes.binary_products
import category_theory.limits.preserves.shapes.terminal
import category_theory.adjunction.fully_faithful

/-!
# Reflective functors

Basic properties of reflective functors, especially those relating to their essential image.

Note properties of reflective functors relating to limits and colimits are included in
`category_theory.monad.limits`.
-/

universes v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

noncomputable theory

namespace category_theory

open category adjunction limits

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} {E : Type u‚ÇÉ}
variables [category.{v‚ÇÅ} C] [category.{v‚ÇÇ} D] [category.{v‚ÇÉ} E]

/--
A functor is *reflective*, or *a reflective inclusion*, if it is fully faithful and right adjoint.
-/
class reflective (R : D ‚•§ C) extends is_right_adjoint R, full R, faithful R.

variables {i : D ‚•§ C}

/--
For a reflective functor `i` (with left adjoint `L`), with unit `Œ∑`, we have `Œ∑_iL = iL Œ∑`.
-/
-- TODO: This holds more generally for idempotent adjunctions, not just reflective adjunctions.
lemma unit_obj_eq_map_unit [reflective i] (X : C) :
  (of_right_adjoint i).unit.app (i.obj ((left_adjoint i).obj X))
    = i.map ((left_adjoint i).map ((of_right_adjoint i).unit.app X)) :=
begin
 rw [‚Üêcancel_mono (i.map ((of_right_adjoint i).counit.app ((left_adjoint i).obj X))),
     ‚Üêi.map_comp],
 simp,
end

/--
When restricted to objects in `D` given by `i : D ‚•§ C`, the unit is an isomorphism. In other words,
`Œ∑_iX` is an isomorphism for any `X` in `D`.
More generally this applies to objects essentially in the reflective subcategory, see
`functor.ess_image.unit_iso`.
-/
instance is_iso_unit_obj [reflective i] {B : D} :
  is_iso ((of_right_adjoint i).unit.app (i.obj B)) :=
begin
  have : (of_right_adjoint i).unit.app (i.obj B) =
            inv (i.map ((of_right_adjoint i).counit.app B)),
  { rw ‚Üê comp_hom_eq_id,
    apply (of_right_adjoint i).right_triangle_components },
  rw this,
  exact is_iso.inv_is_iso,
end

/--
If `A` is essentially in the image of a reflective functor `i`, then `Œ∑_A` is an isomorphism.
This gives that the "witness" for `A` being in the essential image can instead be given as the
reflection of `A`, with the isomorphism as `Œ∑_A`.

(For any `B` in the reflective subcategory, we automatically have that `Œµ_B` is an iso.)
-/
lemma functor.ess_image.unit_is_iso [reflective i] {A : C} (h : A ‚àà i.ess_image) :
  is_iso ((of_right_adjoint i).unit.app A) :=
begin
  suffices : (of_right_adjoint i).unit.app A =
                h.get_iso.inv ‚â´ (of_right_adjoint i).unit.app (i.obj h.witness) ‚â´
                  (left_adjoint i ‚ãô i).map h.get_iso.hom,
  { rw this,
    apply_instance },
  rw ‚Üê nat_trans.naturality,
  simp,
end

/-- If `Œ∑_A` is an isomorphism, then `A` is in the essential image of `i`. -/
lemma mem_ess_image_of_unit_is_iso [is_right_adjoint i] (A : C)
  [is_iso ((of_right_adjoint i).unit.app A)] : A ‚àà i.ess_image :=
‚ü®(left_adjoint i).obj A, ‚ü®(as_iso ((of_right_adjoint i).unit.app A)).symm‚ü©‚ü©

/-- If `Œ∑_A` is a split monomorphism, then `A` is in the reflective subcategory. -/
lemma mem_ess_image_of_unit_split_mono [reflective i] {A : C}
  [split_mono ((of_right_adjoint i).unit.app A)] : A ‚àà i.ess_image :=
begin
  let Œ∑ : ùü≠ C ‚ü∂ left_adjoint i ‚ãô i := (of_right_adjoint i).unit,
  haveI : is_iso (Œ∑.app (i.obj ((left_adjoint i).obj A))) := (i.obj_mem_ess_image _).unit_is_iso,
  have : epi (Œ∑.app A),
  { apply epi_of_epi (retraction (Œ∑.app A)) _,
    rw (show retraction _ ‚â´ Œ∑.app A = _, from Œ∑.naturality (retraction (Œ∑.app A))),
    apply epi_comp (Œ∑.app (i.obj ((left_adjoint i).obj A))) },
  resetI,
  haveI := is_iso_of_epi_of_split_mono (Œ∑.app A),
  exact mem_ess_image_of_unit_is_iso A,
end

/-- Composition of reflective functors. -/
instance reflective.comp (F : C ‚•§ D) (G : D ‚•§ E) [Fr : reflective F] [Gr : reflective G] :
  reflective (F ‚ãô G) := { to_faithful := faithful.comp F G, }

/-- (Implementation) Auxiliary definition for `unit_comp_partial_bijective`. -/
def unit_comp_partial_bijective_aux [reflective i] (A : C) (B : D) :
  (A ‚ü∂ i.obj B) ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ i.obj B) :=
((adjunction.of_right_adjoint i).hom_equiv _ _).symm.trans (equiv_of_fully_faithful i)

/-- The description of the inverse of the bijection `unit_comp_partial_bijective_aux`. -/
lemma unit_comp_partial_bijective_aux_symm_apply [reflective i] {A : C} {B : D}
  (f : i.obj ((left_adjoint i).obj A) ‚ü∂ i.obj B) :
  (unit_comp_partial_bijective_aux _ _).symm f = (of_right_adjoint i).unit.app A ‚â´ f :=
by simp [unit_comp_partial_bijective_aux]

/--
If `i` has a reflector `L`, then the function `(i.obj (L.obj A) ‚ü∂ B) ‚Üí (A ‚ü∂ B)` given by
precomposing with `Œ∑.app A` is a bijection provided `B` is in the essential image of `i`.
That is, the function `Œª (f : i.obj (L.obj A) ‚ü∂ B), Œ∑.app A ‚â´ f` is bijective, as long as `B` is in
the essential image of `i`.
This definition gives an equivalence: the key property that the inverse can be described
nicely is shown in `unit_comp_partial_bijective_symm_apply`.

This establishes there is a natural bijection `(A ‚ü∂ B) ‚âÉ (i.obj (L.obj A) ‚ü∂ B)`. In other words,
from the point of view of objects in `D`, `A` and `i.obj (L.obj A)` look the same: specifically
that `Œ∑.app A` is an isomorphism.
-/
def unit_comp_partial_bijective [reflective i] (A : C) {B : C} (hB : B ‚àà i.ess_image) :
  (A ‚ü∂ B) ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ B) :=
calc (A ‚ü∂ B) ‚âÉ (A ‚ü∂ i.obj hB.witness) : iso.hom_congr (iso.refl _) hB.get_iso.symm
     ...     ‚âÉ (i.obj _ ‚ü∂ i.obj hB.witness) : unit_comp_partial_bijective_aux _ _
     ...     ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ B) : iso.hom_congr (iso.refl _) hB.get_iso

@[simp]
lemma unit_comp_partial_bijective_symm_apply [reflective i] (A : C) {B : C}
  (hB : B ‚àà i.ess_image) (f) :
  (unit_comp_partial_bijective A hB).symm f = (of_right_adjoint i).unit.app A ‚â´ f :=
by simp [unit_comp_partial_bijective, unit_comp_partial_bijective_aux_symm_apply]

lemma unit_comp_partial_bijective_symm_natural [reflective i] (A : C) {B B' : C} (h : B ‚ü∂ B')
  (hB : B ‚àà i.ess_image) (hB' : B' ‚àà i.ess_image) (f : i.obj ((left_adjoint i).obj A) ‚ü∂ B) :
  (unit_comp_partial_bijective A hB').symm (f ‚â´ h) =
    (unit_comp_partial_bijective A hB).symm f ‚â´ h :=
by simp

lemma unit_comp_partial_bijective_natural [reflective i] (A : C) {B B' : C} (h : B ‚ü∂ B')
  (hB : B ‚àà i.ess_image) (hB' : B' ‚àà i.ess_image) (f : A ‚ü∂ B) :
  (unit_comp_partial_bijective A hB') (f ‚â´ h) = unit_comp_partial_bijective A hB f ‚â´ h :=
by rw [‚Üêequiv.eq_symm_apply, unit_comp_partial_bijective_symm_natural A h, equiv.symm_apply_apply]

end category_theory
