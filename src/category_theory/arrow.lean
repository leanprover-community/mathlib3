/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import category_theory.comma

/-!
# The category of arrows

The category of arrows, with morphisms commutative squares.
We set this up as a specialization of the comma category `comma L R`,
where `L` and `R` are both the identity functor.

We also define the typeclass `has_lift`, representing a choice of a lift
of a commutative square (that is, a diagonal morphism making the two triangles commute).

## Tags

comma, arrow
-/

namespace category_theory

universes v u -- declare the `v`'s first; see `category_theory.category` for an explanation
variables {T : Type u} [category.{v} T]

section
variables (T)

/-- The arrow category of `T` has as objects all morphisms in `T` and as morphisms commutative
     squares in `T`. -/
@[derive category]
def arrow := comma.{v v v} (ğŸ­ T) (ğŸ­ T)

-- Satisfying the inhabited linter
instance arrow.inhabited [inhabited T] : inhabited (arrow T) :=
{ default := show comma (ğŸ­ T) (ğŸ­ T), from default (comma (ğŸ­ T) (ğŸ­ T)) }

end

namespace arrow

@[simp] lemma id_left (f : arrow T) : comma_morphism.left (ğŸ™ f) = ğŸ™ (f.left) := rfl
@[simp] lemma id_right (f : arrow T) : comma_morphism.right (ğŸ™ f) = ğŸ™ (f.right) := rfl

/-- An object in the arrow category is simply a morphism in `T`. -/
@[simps]
def mk {X Y : T} (f : X âŸ¶ Y) : arrow T :=
{ left := X,
  right := Y,
  hom := f }

/-- A morphism in the arrow category is a commutative square connecting two objects of the arrow
    category. -/
@[simps]
def hom_mk {f g : arrow T} {u : f.left âŸ¶ g.left} {v : f.right âŸ¶ g.right}
  (w : u â‰« g.hom = f.hom â‰« v) : f âŸ¶ g :=
{ left := u,
  right := v,
  w' := w }

/-- We can also build a morphism in the arrow category out of any commutative square in `T`. -/
@[simps]
def hom_mk' {X Y : T} {f : X âŸ¶ Y} {P Q : T} {g : P âŸ¶ Q} {u : X âŸ¶ P} {v : Y âŸ¶ Q}
  (w : u â‰« g = f â‰« v) : arrow.mk f âŸ¶ arrow.mk g :=
{ left := u,
  right := v,
  w' := w }

@[simp, reassoc] lemma w {f g : arrow T} (sq : f âŸ¶ g) : sq.left â‰« g.hom = f.hom â‰« sq.right := sq.w

/-- A lift of a commutative square is a diagonal morphism making the two triangles commute. -/
@[ext] structure lift_struct {f g : arrow T} (sq : f âŸ¶ g) :=
(lift : f.right âŸ¶ g.left)
(fac_left : f.hom â‰« lift = sq.left)
(fac_right : lift â‰« g.hom = sq.right)

instance lift_struct_inhabited {X : T} : inhabited (lift_struct (ğŸ™ (arrow.mk (ğŸ™ X)))) :=
âŸ¨âŸ¨ğŸ™ _, category.id_comp _, category.comp_id _âŸ©âŸ©

/-- `has_lift sq` says that there is some `lift_struct sq`, i.e., that it is possible to find a
    diagonal morphism making the two triangles commute. -/
class has_lift {f g : arrow T} (sq : f âŸ¶ g) : Prop :=
mk' :: (exists_lift : nonempty (lift_struct sq))

lemma has_lift.mk {f g : arrow T} {sq : f âŸ¶ g} (s : lift_struct sq) : has_lift sq :=
âŸ¨nonempty.intro sâŸ©

attribute [simp, reassoc] lift_struct.fac_left lift_struct.fac_right

/-- Given `has_lift sq`, obtain a lift. -/
noncomputable def has_lift.struct {f g : arrow T} (sq : f âŸ¶ g) [has_lift sq] : lift_struct sq :=
classical.choice has_lift.exists_lift

/-- If there is a lift of a commutative square `sq`, we can access it by saying `lift sq`. -/
noncomputable abbreviation lift {f g : arrow T} (sq : f âŸ¶ g) [has_lift sq] : f.right âŸ¶ g.left :=
(has_lift.struct sq).lift

lemma lift.fac_left {f g : arrow T} (sq : f âŸ¶ g) [has_lift sq] : f.hom â‰« lift sq = sq.left :=
by simp

lemma lift.fac_right {f g : arrow T} (sq : f âŸ¶ g) [has_lift sq] : lift sq â‰« g.hom = sq.right :=
by simp

@[simp, reassoc]
lemma lift_mk'_left {X Y P Q : T} {f : X âŸ¶ Y} {g : P âŸ¶ Q} {u : X âŸ¶ P} {v : Y âŸ¶ Q}
  (h : u â‰« g = f â‰« v) [has_lift $ arrow.hom_mk' h] : f â‰« lift (arrow.hom_mk' h) = u :=
by simp only [â†arrow.mk_hom f, lift.fac_left, arrow.hom_mk'_left]

@[simp, reassoc]
lemma lift_mk'_right {X Y P Q : T} {f : X âŸ¶ Y} {g : P âŸ¶ Q} {u : X âŸ¶ P} {v : Y âŸ¶ Q}
  (h : u â‰« g = f â‰« v) [has_lift $ arrow.hom_mk' h] : lift (arrow.hom_mk' h) â‰« g = v :=
by simp only [â†arrow.mk_hom g, lift.fac_right, arrow.hom_mk'_right]

section

instance subsingleton_lift_struct_of_epi {f g : arrow T} (sq : f âŸ¶ g) [epi f.hom] :
  subsingleton (lift_struct sq) :=
subsingleton.intro $ Î» a b, lift_struct.ext a b $ (cancel_epi f.hom).1 $ by simp

instance subsingleton_lift_struct_of_mono {f g : arrow T} (sq : f âŸ¶ g) [mono g.hom] :
  subsingleton (lift_struct sq) :=
subsingleton.intro $ Î» a b, lift_struct.ext a b $ (cancel_mono g.hom).1 $ by simp

end

end arrow

namespace functor

universes vâ‚ vâ‚‚ uâ‚ uâ‚‚

variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚‚} D]

/-- A functor `C â¥¤ D` induces a functor between the corresponding arrow categories. -/
@[simps]
def map_arrow (F : C â¥¤ D) : arrow C â¥¤ arrow D :=
{ obj := Î» a,
  { left := F.obj a.left,
    right := F.obj a.right,
    hom := F.map a.hom, },
  map := Î» a b f,
  { left := F.map f.left,
    right := F.map f.right,
    w' := by { have w := f.w, simp only [id_map] at w, dsimp, simp only [â†F.map_comp, w], } } }

end functor

end category_theory
