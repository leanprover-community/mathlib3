/-
Copyright (c) 2021 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.isomorphism
import tactic.slice

/-!
# Bicategories

In this file we define typeclass for bicategories.

A bicategory `B` consists of
* objects `a : B`,
* 1-morphisms `f : a âŸ¶ b` between objects `a b : B`, and
* 2-morphisms `Î· : f âŸ¶ g` beween 1-morphisms `f g : a âŸ¶ b` between objects `a b : B`.

We use `u`, `v`, and `w` as the universe variables for objects, 1-morphisms, and 2-morphisms,
respectively.

A typeclass for bicategories extends `category_theory.category_struct` typeclass. This means that
we have
* a composition `f â‰« g : a âŸ¶ c` for each 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ c`, and
* a identity `ğŸ™ a : a âŸ¶ a` for each object `a : B`.

For each object `a b : B`, the collection of 1-morphisms `a âŸ¶ b` has a category structure. The
2-morphisms in the bicategory are implemented as the morphisms in this family of categories.

The composition of 1-morphisms is in fact a object part of a functor `(a âŸ¶ b) â¥¤ (b âŸ¶ c) â¥¤ (a âŸ¶ c)`.
The definition of bicategories in this file does not require this functor directly. Instead, it
requires the whiskering functions. For a 1-morphism `f : a âŸ¶ b` and a 2-morphism `Î· : g âŸ¶ h`
between 1-morphisms `g h : b âŸ¶ c`, there is a 2-morphism `whisker_left f Î· : f â‰« g âŸ¶ f â‰« h`.
Similarly, for a 2-morphism `Î· : f âŸ¶ g` between 1-morphisms `f g : a âŸ¶ b` and a 1-morphism
`f : b âŸ¶ c`, there is a 2-morphism `whisker_right Î· h : f â‰« h âŸ¶ g â‰« h`.
These satisfy the exchange law
`whisker_left f Î¸ â‰« whisker_right Î· i = whisker_right Î· h â‰« whisker_left g Î¸`,
which is required as an axiom in the definition here.

-/

open category_theory

universes w v u

open category_theory
open category_theory.category
open category_theory.iso

namespace category_theory

/--
In a bicategory, we can compose the 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ c` to obtain
a 1-morphism `f â‰« g : a âŸ¶ c`. This composition does not need to be strictly associative,
but there is a spesified associator, `Î±_ f g h : (f â‰« g) â‰« h â‰… f â‰« (g â‰« h)`.
There is a identity 1-morphism `ğŸ™ a : a âŸ¶ a`, with specified left and right unitor
isomorphisms `Î»_ f : ğŸ™ a â‰« f â‰… f` and `Ï_ f : f â‰« ğŸ™ a â‰… f`.
These associators and unitors satisfy the pentagon and triangle equations.

See https://ncatlab.org/nlab/show/bicategory.
-/
class bicategory (B : Type u) extends category_struct.{v} B :=
-- category structure on the collection of 1-morphisms:
(category_hom : âˆ€ (a b : B), category.{w} (a âŸ¶ b) . tactic.apply_instance)
-- left whiskering:
(whisker_left {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) : f â‰« g âŸ¶ f â‰« h)
(infixr ` â—ƒ `:70 := whisker_left)
-- functoriality of left whiskering:
(whisker_left_id' :
  âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c), f â—ƒ ğŸ™ g = ğŸ™ (f â‰« g) . obviously)
(whisker_left_comp' :
  âˆ€ {a b c} (f : a âŸ¶ b) {g h i : b âŸ¶ c} (Î· : g âŸ¶ h) (Î¸ : h âŸ¶ i),
  f â—ƒ (Î· â‰« Î¸) = (f â—ƒ Î·) â‰« (f â—ƒ Î¸) . obviously)
-- right whiskering:
(whisker_right {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) : f â‰« h âŸ¶ g â‰« h)
(infixr ` â–¹ `:70 := whisker_right)
-- functoriality of right whiskering:
(whisker_right_id' :
  âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c), ğŸ™ f â–¹ g = ğŸ™ (f â‰« g) . obviously)
(whisker_right_comp' :
  âˆ€ {a b c} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) (i : b âŸ¶ c),
  (Î· â‰« Î¸) â–¹ i = (Î· â–¹ i) â‰« (Î¸ â–¹ i) . obviously)
-- exchange law of left and right whiskerings:
(whisker_exchange' : âˆ€ {a b c} {f g : a âŸ¶ b} {h i : b âŸ¶ c} (Î· : f âŸ¶ g) (Î¸ : h âŸ¶ i),
  (f â—ƒ Î¸) â‰« (Î· â–¹ i) = (Î· â–¹ h) â‰« (g â—ƒ Î¸) . obviously)
-- associator:
(associator {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
  (f â‰« g) â‰« h â‰… f â‰« (g â‰« h))
(notation `Î±_` := associator)
(associator_naturality_left' :
  âˆ€ {a b c d} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d),
    ((Î· â–¹ g) â–¹ h) â‰« (Î±_ f' g h).hom = (Î±_ f g h).hom â‰« (Î· â–¹ (g â‰« h)) . obviously)
(associator_naturality_middle' :
  âˆ€ {a b c d} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d),
    ((f â—ƒ Î·) â–¹ h) â‰« (Î±_ f g' h).hom = (Î±_ f g h).hom â‰« (f â—ƒ (Î· â–¹ h)) . obviously)
(associator_naturality_right' :
  âˆ€ {a b c d} (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h'),
    ((f â‰« g) â—ƒ Î·) â‰« (Î±_ f g h').hom = (Î±_ f g h).hom â‰« (f â—ƒ (g â—ƒ Î·)) . obviously)
--left unitor:
(left_unitor {a b : B} (f : a âŸ¶ b) : ğŸ™ a â‰« f â‰… f)
(notation `Î»_` := left_unitor)
(left_unitor_naturality' : âˆ€ {a b} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f'),
  (ğŸ™ a â—ƒ Î·) â‰« (Î»_ f').hom = (Î»_ f ).hom â‰« Î· . obviously)
-- right unitor
(right_unitor {a b : B} (f : a âŸ¶ b) : f â‰« ğŸ™ b â‰… f)
(notation `Ï_` := right_unitor)
(right_unitor_naturality' : âˆ€ {a b} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f'),
  (Î· â–¹ ğŸ™ b) â‰« (Ï_ f').hom = (Ï_ f ).hom â‰« Î· . obviously)
-- pentagon identity:
(pentagon' : âˆ€ {a b c d e} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e),
  ((Î±_ f g h).hom â–¹ i) â‰« (Î±_ f (g â‰« h) i).hom â‰« (f â—ƒ (Î±_ g h i).hom)
  = (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom . obviously)
-- triangle identity:
(triangle' : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
  (Î±_ f (ğŸ™ b) g).hom â‰« (f â—ƒ (Î»_ g).hom) = (Ï_ f).hom â–¹ g . obviously)

restate_axiom bicategory.whisker_left_id'
attribute [simp] bicategory.whisker_left_id
restate_axiom bicategory.whisker_left_comp'
attribute [reassoc, simp] bicategory.whisker_left_comp
restate_axiom bicategory.whisker_right_id'
attribute [simp] bicategory.whisker_right_id
restate_axiom bicategory.whisker_right_comp'
attribute [reassoc, simp] bicategory.whisker_right_comp
restate_axiom bicategory.whisker_exchange'
attribute [simp, reassoc] bicategory.whisker_exchange
restate_axiom bicategory.associator_naturality_left'
attribute [reassoc] bicategory.associator_naturality_left
restate_axiom bicategory.associator_naturality_middle'
attribute [reassoc] bicategory.associator_naturality_middle
restate_axiom bicategory.associator_naturality_right'
attribute [reassoc] bicategory.associator_naturality_right
restate_axiom bicategory.left_unitor_naturality'
attribute [reassoc] bicategory.left_unitor_naturality
restate_axiom bicategory.right_unitor_naturality'
attribute [reassoc] bicategory.right_unitor_naturality
restate_axiom bicategory.pentagon'
attribute [reassoc] bicategory.pentagon
restate_axiom bicategory.triangle'
attribute [simp, reassoc] bicategory.triangle

infixr ` â—ƒ `:70 := bicategory.whisker_left
infixr ` â–¹ `:70 := bicategory.whisker_right
notation `Î±_` := bicategory.associator
notation `Î»_` := bicategory.left_unitor
notation `Ï_` := bicategory.right_unitor

instance {B : Type u} [bicategory.{w v} B] (a b : B) : category (a âŸ¶ b) :=
bicategory.category_hom a b

namespace bicategory

section

variables {B : Type u} [bicategory.{w v} B]
variables {a b c d e : B}

/-- The left whiskering of a 2-isomorphism is an 2-isomorphism. -/
@[simps]
def whisker_left_iso (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
  f â‰« g â‰… f â‰« h :=
{ hom := f â—ƒ Î·.hom,
  inv := f â—ƒ Î·.inv,
  hom_inv_id' := by { rw [â†whisker_left_comp, hom_inv_id], simp },
  inv_hom_id' := by { rw [â†whisker_left_comp, inv_hom_id], simp } }

instance whisker_left_is_iso (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) [is_iso Î·] :
  is_iso (f â—ƒ Î·) :=
is_iso.of_iso (whisker_left_iso f (as_iso Î·))

@[simp]
lemma inv_whisker_left (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) [is_iso Î·] :
  inv (f â—ƒ Î·) = f â—ƒ (inv Î·) :=
by { ext, rw [â†whisker_left_comp], simp }

/-- The right whiskering of a 2-isomorphism is an 2-isomorphism. -/
@[simps]
def whisker_right_iso {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
  f â‰« h â‰… g â‰« h :=
{ hom := Î·.hom â–¹ h,
  inv := Î·.inv â–¹ h,
  hom_inv_id' := by { rw [â†whisker_right_comp], simp },
  inv_hom_id' := by { rw [â†whisker_right_comp], simp } }

instance whisker_right_is_iso {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) [is_iso Î·] :
  is_iso (Î· â–¹ h) :=
is_iso.of_iso (whisker_right_iso (as_iso Î·) h)

@[simp]
lemma inv_whisker_right {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) [is_iso Î·] :
  inv (Î· â–¹ h) = (inv Î·) â–¹ h :=
by { ext, rw [â†whisker_right_comp], simp }

@[reassoc]
lemma left_unitor_inv_naturality {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
  Î· â‰« (Î»_ f').inv = (Î»_ f).inv â‰« (ğŸ™ a â—ƒ Î·) :=
begin
  apply (cancel_mono (Î»_ f').hom).1,
  simp only [category.assoc, category.comp_id, iso.inv_hom_id, left_unitor_naturality,
    inv_hom_id_assoc]
end

@[reassoc]
lemma right_unitor_inv_naturality {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') :
  Î· â‰« (Ï_ f').inv = (Ï_ f ).inv â‰« (Î· â–¹ ğŸ™ b) :=
begin
  apply (cancel_mono (Ï_ f').hom).1,
  simp only [category.assoc, category.comp_id, inv_hom_id, right_unitor_naturality,
    inv_hom_id_assoc],
end

@[simp]
lemma right_unitor_conjugation {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  (Ï_ f).inv â‰« (Î· â–¹ ğŸ™ b) â‰« (Ï_ g).hom = Î· :=
by rw [right_unitor_naturality, inv_hom_id_assoc]

@[simp]
lemma left_unitor_conjugation {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  (Î»_ f).inv â‰« (ğŸ™ a â—ƒ Î·) â‰« (Î»_ g).hom = Î· :=
by rw [left_unitor_naturality, inv_hom_id_assoc]

@[simp]
lemma whisker_left_iff {f g : a âŸ¶ b} (Î· Î¸ : f âŸ¶ g) :
  (ğŸ™ a â—ƒ Î· = ğŸ™ a â—ƒ Î¸) â†” (Î· = Î¸) :=
by { rw [â†cancel_mono (Î»_ g).hom, left_unitor_naturality, left_unitor_naturality], simp }

@[simp]
lemma whisker_right_iff {f g : a âŸ¶ b} (Î· Î¸ : f âŸ¶ g) :
  (Î· â–¹ ğŸ™ b = Î¸ â–¹ ğŸ™ b) â†” (Î· = Î¸) :=
by { rw [â†cancel_mono (Ï_ g).hom, right_unitor_naturality, right_unitor_naturality], simp }

@[reassoc]
lemma left_unitor_comp' (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î±_ (ğŸ™ a) f g).hom â‰« (Î»_ (f â‰« g)).hom = (Î»_ f).hom â–¹ g :=
by rw [â†whisker_left_iff, whisker_left_comp, â†cancel_epi (Î±_ (ğŸ™ a) (ğŸ™ a â‰« f) g).hom,
    â†cancel_epi ((Î±_ (ğŸ™ a) (ğŸ™ a) f).hom â–¹ g), pentagon_assoc, triangle,
    â†associator_naturality_middle, â†whisker_right_comp_assoc, triangle, associator_naturality_left,
    cancel_iso_hom_left]

@[reassoc, simp]
lemma left_unitor_comp (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ (f â‰« g)).hom = (Î±_ (ğŸ™ a) f g).inv â‰« ((Î»_ f).hom â–¹ g) :=
by { rw [â†left_unitor_comp'], simp }

lemma left_unitor_comp_inv' (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ (f â‰« g)).inv â‰« (Î±_ (ğŸ™ a) f g).inv = ((Î»_ f).inv â–¹ g) :=
eq_of_inv_eq_inv (by simp)

@[reassoc, simp]
lemma left_unitor_comp_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ (f â‰« g)).inv = ((Î»_ f).inv â–¹ g) â‰« (Î±_ (ğŸ™ a) f g).hom :=
by { rw [â†left_unitor_comp_inv'], simp }

@[reassoc, simp]
lemma right_unitor_comp (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Ï_ (f â‰« g)).hom = (Î±_ f g (ğŸ™ c)).hom â‰« (f â—ƒ (Ï_ g).hom) :=
by rw [â†whisker_right_iff, whisker_right_comp, â†cancel_mono (Î±_ f g (ğŸ™ c)).hom,
    assoc, associator_naturality_middle, â†triangle_assoc, â†triangle,
    whisker_left_comp, pentagon_assoc, â†associator_naturality_right]

@[reassoc, simp]
lemma right_unitor_comp_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Ï_ (f â‰« g)).inv = (f â—ƒ (Ï_ g).inv) â‰« (Î±_ f g (ğŸ™ c)).inv :=
eq_of_inv_eq_inv (by simp)

@[reassoc]
lemma whisker_left_right_unitor_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  f â—ƒ (Ï_ g).inv = (Ï_ _).inv â‰« (Î±_ _ _ _).hom :=
by simp only [right_unitor_comp_inv, comp_id, inv_hom_id, assoc]

@[reassoc]
lemma whisker_left_right_unitor (f : a âŸ¶ b) (g : b âŸ¶ c) :
  f â—ƒ (Ï_ g).hom = (Î±_ _ _ _).inv â‰« (Ï_ _).hom :=
by simp only [right_unitor_comp, inv_hom_id_assoc]

@[reassoc]
lemma left_unitor_inv_whisker_right (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ f).inv â–¹ g = (Î»_ _).inv â‰« (Î±_ _ _ _).inv :=
by simp only [left_unitor_comp_inv, assoc, comp_id, hom_inv_id]

@[reassoc]
lemma left_unitor_whisker_right (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ f).hom â–¹ g = (Î±_ _ _ _).hom â‰« (Î»_ _).hom :=
by simp only [left_unitor_comp, hom_inv_id_assoc]

@[reassoc]
lemma associator_inv_naturality_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
  (Î· â–¹ (g â‰« h)) â‰« (Î±_ f' g h).inv
  = (Î±_ f g h).inv â‰« ((Î· â–¹ g) â–¹ h) :=
by rw [comp_inv_eq, assoc, associator_naturality_left, inv_hom_id_assoc]

@[reassoc]
lemma associator_conjugation_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
  (Î±_ f g h).hom â‰« (Î· â–¹ (g â‰« h)) â‰« (Î±_ f' g h).inv
  = (Î· â–¹ g) â–¹ h :=
by rw [associator_inv_naturality_left, hom_inv_id_assoc]

@[reassoc]
lemma associator_inv_conjugation_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
  (Î±_ f g h).inv â‰« ((Î· â–¹ g) â–¹ h) â‰« (Î±_ f' g h).hom
  = Î· â–¹ (g â‰« h) :=
by rw [associator_naturality_left, inv_hom_id_assoc]

@[reassoc]
lemma associator_inv_naturality_middle (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
  (f â—ƒ (Î· â–¹ h)) â‰« (Î±_ f g' h).inv
  = (Î±_ f g h).inv â‰« ((f â—ƒ Î·) â–¹ h) :=
by rw [comp_inv_eq, assoc, associator_naturality_middle, inv_hom_id_assoc]

@[reassoc]
lemma associator_conjugation_middle (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
  (Î±_ f g h).hom â‰« (f â—ƒ (Î· â–¹ h)) â‰« (Î±_ f g' h).inv
  = (f â—ƒ Î·) â–¹ h :=
by rw [associator_inv_naturality_middle, hom_inv_id_assoc]

@[reassoc]
lemma associator_inv_conjugation_middle (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
  (Î±_ f g h).inv â‰« ((f â—ƒ Î·) â–¹ h) â‰« (Î±_ f g' h).hom
  = f â—ƒ (Î· â–¹ h) :=
by rw [associator_naturality_middle, inv_hom_id_assoc]

@[reassoc]
lemma associator_inv_naturality_right (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
  (f â—ƒ (g â—ƒ Î·)) â‰« (Î±_ f g h').inv
  = (Î±_ f g h).inv â‰« ((f â‰« g) â—ƒ Î·) :=
by rw [comp_inv_eq, assoc, associator_naturality_right, inv_hom_id_assoc]

@[reassoc]
lemma associator_conjugation_right (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
  (Î±_ f g h).hom â‰« (f â—ƒ (g â—ƒ Î·)) â‰« (Î±_ f g h').inv
  = (f â‰« g) â—ƒ Î· :=
by rw [associator_inv_naturality_right, hom_inv_id_assoc]

@[reassoc]
lemma associator_inv_conjugation_right (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
  (Î±_ f g h).inv â‰« ((f â‰« g) â—ƒ Î·) â‰« (Î±_ f g h').hom
  = f â—ƒ (g â—ƒ Î·) :=
by rw [associator_naturality_right, inv_hom_id_assoc]

@[reassoc]
lemma pentagon_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (f â—ƒ (Î±_ g h i).inv) â‰« (Î±_ f (g â‰« h) i).inv â‰« ((Î±_ f g h).inv â–¹ i)
  = (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv :=
eq_of_inv_eq_inv (by simp [pentagon])

@[reassoc]
lemma pentagon_inv_inv_hom_hom_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f (g â‰« h) i).inv â‰« ((Î±_ f g h).inv â–¹ i) â‰« (Î±_ (f â‰« g) h i).hom
  = (f â—ƒ (Î±_ g h i).hom) â‰« (Î±_ f g (h â‰« i)).inv :=
begin
  rw â†((eq_comp_inv _).mp (pentagon_inv f g h i)),
  slice_rhs 1 2 { rw [â†whisker_left_comp, hom_inv_id] },
  simp,
end

@[reassoc]
lemma pentagon_inv_hom_hom_hom_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ (f â‰« g) h i).inv â‰« ((Î±_ f g h).hom â–¹ i) â‰« (Î±_ f (g â‰« h) i).hom
  =  (Î±_ f g (h â‰« i)).hom â‰« (f â—ƒ (Î±_ g h i).inv) :=
eq_of_inv_eq_inv (by simp [pentagon_inv_inv_hom_hom_inv])

@[reassoc]
lemma pentagon_hom_inv_inv_inv_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (f â—ƒ (Î±_ g h i).hom) â‰« (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv
  = (Î±_ f (g â‰« h) i).inv â‰« ((Î±_ f g h).inv â–¹ i) :=
begin
  rw â†((eq_comp_inv _).mp (pentagon_inv f g h i)),
  slice_lhs 1 2 { rw [â†whisker_left_comp, hom_inv_id] },
  simp,
end

@[reassoc]
lemma pentagon_hom_hom_inv_hom_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom â‰« (f â—ƒ (Î±_ g h i).inv) =
    ((Î±_ f g h).hom â–¹ i) â‰« (Î±_ f (g â‰« h) i).hom :=
eq_of_inv_eq_inv (by simp [pentagon_hom_inv_inv_inv_inv])

@[reassoc]
lemma pentagon_hom_inv_inv_inv_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f g (h â‰« i)).hom â‰« (f â—ƒ (Î±_ g h i).inv) â‰« (Î±_ f (g â‰« h) i).inv
  = (Î±_ (f â‰« g) h i).inv â‰« ((Î±_ f g h).hom â–¹ i) :=
begin
  have pent := pentagon f g h i,
  rw â†inv_comp_eq at pent,
  rw [â†pent],
  simp [â†whisker_left_comp_assoc]
end

@[reassoc]
lemma pentagon_hom_hom_inv_inv_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f (g â‰« h) i).hom â‰« (f â—ƒ (Î±_ g h i).hom) â‰« (Î±_ f g (h â‰« i)).inv
  = ((Î±_ f g h).inv â–¹ i) â‰« (Î±_ (f â‰« g) h i).hom :=
eq_of_inv_eq_inv (by simp [pentagon_hom_inv_inv_inv_hom])

@[reassoc]
lemma pentagon_inv_hom_hom_hom_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  ((Î±_ f g h).inv â–¹ i) â‰« (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom
  = (Î±_ f (g â‰« h) i).hom â‰« (f â—ƒ (Î±_ g h i).hom) :=
begin
  rw â†pentagon f g h i,
  simp [â†whisker_right_comp_assoc]
end

@[reassoc]
lemma pentagon_inv_inv_hom_inv_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv â‰« ((Î±_ f g h).hom â–¹ i)
  = (f â—ƒ (Î±_ g h i).inv) â‰« (Î±_ f (g â‰« h) i).inv :=
eq_of_inv_eq_inv (by simp [pentagon_inv_hom_hom_hom_hom])

lemma triangle_assoc_comp_left (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î±_ f (ğŸ™ b) g).hom â‰« (f â—ƒ (Î»_ g).hom) = (Ï_ f).hom â–¹ g :=
bicategory.triangle f g

@[simp, reassoc]
lemma triangle_assoc_comp_right (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î±_ f (ğŸ™ b) g).inv â‰« ((Ï_ f).hom â–¹ g) = f â—ƒ (Î»_ g).hom :=
by rw [â†triangle, inv_hom_id_assoc]

@[simp, reassoc]
lemma triangle_assoc_comp_right_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  ((Ï_ f).inv â–¹ g) â‰« (Î±_ f (ğŸ™ b) g).hom = f â—ƒ (Î»_ g).inv :=
begin
  apply (cancel_mono (f â—ƒ (Î»_ g).hom)).1,
  simp [â†whisker_left_comp, â†whisker_right_comp]
end

@[simp, reassoc]
lemma triangle_assoc_comp_left_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (f â—ƒ (Î»_ g).inv) â‰« (Î±_ f (ğŸ™ b) g).inv = (Ï_ f).inv â–¹ g :=
begin
  apply (cancel_mono ((Ï_ f).hom â–¹ g)).1,
  simp [â†whisker_left_comp, â†whisker_right_comp]
end

lemma unitors_equal : (Î»_ (ğŸ™ a)).hom = (Ï_ (ğŸ™ a)).hom :=
by rw [â†whisker_left_iff, â†cancel_epi (Î±_ (ğŸ™ a) (ğŸ™ _) (ğŸ™ _)).hom,
       â†cancel_mono (Ï_ (ğŸ™ a)).hom, triangle, â†right_unitor_comp, right_unitor_naturality]

lemma unitors_inv_equal : (Î»_ (ğŸ™ a)).inv = (Ï_ (ğŸ™ a)).inv :=
by { ext, simp [â†unitors_equal] }

@[simp, reassoc]
lemma hom_inv_whisker_left (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
  (f â—ƒ Î·.hom) â‰« (f â—ƒ Î·.inv) = ğŸ™ (f â‰« g) :=
by rw [â†whisker_left_comp, Î·.hom_inv_id, whisker_left_id]

@[simp, reassoc]
lemma hom_inv_whisker_right {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
  (Î·.hom â–¹ h) â‰« (Î·.inv â–¹ h) = ğŸ™ (f â‰« h) :=
by rw [â†whisker_right_comp, Î·.hom_inv_id, whisker_right_id]

@[simp, reassoc]
lemma inv_hom_whisker_left (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
  (f â—ƒ Î·.inv) â‰« (f â—ƒ Î·.hom) = ğŸ™ (f â‰« h) :=
by rw [â†whisker_left_comp, Î·.inv_hom_id, whisker_left_id]

@[simp, reassoc]
lemma inv_hom_whisker_right {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
  (Î·.inv â–¹ h) â‰« (Î·.hom â–¹ h) = ğŸ™ (g â‰« h) :=
by rw [â†whisker_right_comp, Î·.inv_hom_id, whisker_right_id]

end

end bicategory

end category_theory
