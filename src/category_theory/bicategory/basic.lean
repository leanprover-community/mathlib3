/-
Copyright (c) 2021 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.isomorphism
import tactic.slice

/-!
# Bicategories

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define typeclass for bicategories.

A bicategory `B` consists of
* objects `a : B`,
* 1-morphisms `f : a âŸ¶ b` between objects `a b : B`, and
* 2-morphisms `Î· : f âŸ¶ g` beween 1-morphisms `f g : a âŸ¶ b` between objects `a b : B`.

We use `u`, `v`, and `w` as the universe variables for objects, 1-morphisms, and 2-morphisms,
respectively.

A typeclass for bicategories extends `category_theory.category_struct` typeclass. This means that
we have
* a composition `f â‰« g : a âŸ¶ c` for each 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ c`, and
* a identity `ğŸ™ a : a âŸ¶ a` for each object `a : B`.

For each object `a b : B`, the collection of 1-morphisms `a âŸ¶ b` has a category structure. The
2-morphisms in the bicategory are implemented as the morphisms in this family of categories.

The composition of 1-morphisms is in fact a object part of a functor
`(a âŸ¶ b) â¥¤ (b âŸ¶ c) â¥¤ (a âŸ¶ c)`. The definition of bicategories in this file does not
require this functor directly. Instead, it requires the whiskering functions. For a 1-morphism
`f : a âŸ¶ b` and a 2-morphism `Î· : g âŸ¶ h` between 1-morphisms `g h : b âŸ¶ c`, there is a
2-morphism `whisker_left f Î· : f â‰« g âŸ¶ f â‰« h`. Similarly, for a 2-morphism `Î· : f âŸ¶ g`
between 1-morphisms `f g : a âŸ¶ b` and a 1-morphism `f : b âŸ¶ c`, there is a 2-morphism
`whisker_right Î· h : f â‰« h âŸ¶ g â‰« h`. These satisfy the exchange law
`whisker_left f Î¸ â‰« whisker_right Î· i = whisker_right Î· h â‰« whisker_left g Î¸`,
which is required as an axiom in the definition here.
-/

namespace category_theory

universes w v u

open category iso

/--
In a bicategory, we can compose the 1-morphisms `f : a âŸ¶ b` and `g : b âŸ¶ c` to obtain
a 1-morphism `f â‰« g : a âŸ¶ c`. This composition does not need to be strictly associative,
but there is a specified associator, `Î±_ f g h : (f â‰« g) â‰« h â‰… f â‰« (g â‰« h)`.
There is an identity 1-morphism `ğŸ™ a : a âŸ¶ a`, with specified left and right unitor
isomorphisms `Î»_ f : ğŸ™ a â‰« f â‰… f` and `Ï_ f : f â‰« ğŸ™ a â‰… f`.
These associators and unitors satisfy the pentagon and triangle equations.

See https://ncatlab.org/nlab/show/bicategory.
-/
@[nolint check_univs] -- intended to be used with explicit universe parameters
class bicategory (B : Type u) extends category_struct.{v} B :=
-- category structure on the collection of 1-morphisms:
(hom_category : âˆ€ (a b : B), category.{w} (a âŸ¶ b) . tactic.apply_instance)
-- left whiskering:
(whisker_left {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) : f â‰« g âŸ¶ f â‰« h)
(infixr ` â— `:81 := whisker_left)
-- right whiskering:
(whisker_right {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) : f â‰« h âŸ¶ g â‰« h)
(infixl ` â–· `:81 := whisker_right)
-- associator:
(associator {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
  (f â‰« g) â‰« h â‰… f â‰« (g â‰« h))
(notation `Î±_` := associator)
-- left unitor:
(left_unitor {a b : B} (f : a âŸ¶ b) : ğŸ™ a â‰« f â‰… f)
(notation `Î»_` := left_unitor)
-- right unitor:
(right_unitor {a b : B} (f : a âŸ¶ b) : f â‰« ğŸ™ b â‰… f)
(notation `Ï_` := right_unitor)
-- axioms for left whiskering:
(whisker_left_id' : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
  f â— ğŸ™ g = ğŸ™ (f â‰« g) . obviously)
(whisker_left_comp' : âˆ€ {a b c} (f : a âŸ¶ b) {g h i : b âŸ¶ c} (Î· : g âŸ¶ h) (Î¸ : h âŸ¶ i),
  f â— (Î· â‰« Î¸) = f â— Î· â‰« f â— Î¸ . obviously)
(id_whisker_left' : âˆ€ {a b} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
  ğŸ™ a â— Î· = (Î»_ f).hom â‰« Î· â‰« (Î»_ g).inv . obviously)
(comp_whisker_left' : âˆ€ {a b c d} (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h'),
  (f â‰« g) â— Î· = (Î±_ f g h).hom â‰« f â— g â— Î· â‰« (Î±_ f g h').inv . obviously)
-- axioms for right whiskering:
(id_whisker_right' : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
  ğŸ™ f â–· g = ğŸ™ (f â‰« g) . obviously)
(comp_whisker_right' : âˆ€ {a b c} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) (i : b âŸ¶ c),
  (Î· â‰« Î¸) â–· i = Î· â–· i â‰« Î¸ â–· i . obviously)
(whisker_right_id' : âˆ€ {a b} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
  Î· â–· ğŸ™ b = (Ï_ f).hom â‰« Î· â‰« (Ï_ g).inv . obviously)
(whisker_right_comp' : âˆ€ {a b c d} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d),
  Î· â–· (g â‰« h) = (Î±_ f g h).inv â‰« Î· â–· g â–· h â‰« (Î±_ f' g h).hom . obviously)
-- associativity of whiskerings:
(whisker_assoc' : âˆ€ {a b c d} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d),
  (f â— Î·) â–· h = (Î±_ f g h).hom â‰« f â— (Î· â–· h) â‰« (Î±_ f g' h).inv . obviously)
-- exchange law of left and right whiskerings:
(whisker_exchange' : âˆ€ {a b c} {f g : a âŸ¶ b} {h i : b âŸ¶ c} (Î· : f âŸ¶ g) (Î¸ : h âŸ¶ i),
  f â— Î¸ â‰« Î· â–· i = Î· â–· h â‰« g â— Î¸ . obviously)
-- pentagon identity:
(pentagon' : âˆ€ {a b c d e} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e),
  (Î±_ f g h).hom â–· i â‰« (Î±_ f (g â‰« h) i).hom â‰« f â— (Î±_ g h i).hom =
    (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom . obviously)
-- triangle identity:
(triangle' : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
  (Î±_ f (ğŸ™ b) g).hom â‰« f â— (Î»_ g).hom = (Ï_ f).hom â–· g . obviously)

-- The precedence of the whiskerings is higher than that of the composition `â‰«`.
localized "infixr (name := bicategory.whisker_left) ` â— `:81 := bicategory.whisker_left"
  in bicategory
localized "infixl (name := bicategory.whisker_right) ` â–· `:81 := bicategory.whisker_right"
  in bicategory
localized "notation (name := bicategory.associator) `Î±_` := bicategory.associator"
  in bicategory
localized "notation (name := bicategory.left_unitor) `Î»_` := bicategory.left_unitor"
  in bicategory
localized "notation (name := bicategory.right_unitor) `Ï_` := bicategory.right_unitor"
  in bicategory

namespace bicategory

/-!
### Simp-normal form for 2-morphisms

Rewriting involving associators and unitors could be very complicated. We try to ease this
complexity by putting carefully chosen simp lemmas that rewrite any 2-morphisms into simp-normal
form defined below. Rewriting into simp-normal form is also useful when applying (forthcoming)
`coherence` tactic.

The simp-normal form of 2-morphisms is defined to be an expression that has the minimal number of
parentheses. More precisely,
1. it is a composition of 2-morphisms like `Î·â‚ â‰« Î·â‚‚ â‰« Î·â‚ƒ â‰« Î·â‚„ â‰« Î·â‚…` such that each `Î·áµ¢` is
  either a structural 2-morphisms (2-morphisms made up only of identities, associators, unitors)
  or non-structural 2-morphisms, and
2. each non-structural 2-morphism in the composition is of the form `fâ‚ â— fâ‚‚ â— fâ‚ƒ â— Î· â–· fâ‚„ â–· fâ‚…`,
  where each `fáµ¢` is a 1-morphism that is not the identity or a composite and `Î·` is a
  non-structural 2-morphisms that is also not the identity or a composite.

Note that `fâ‚ â— fâ‚‚ â— fâ‚ƒ â— Î· â–· fâ‚„ â–· fâ‚…` is actually `fâ‚ â— (fâ‚‚ â— (fâ‚ƒ â— ((Î· â–· fâ‚„) â–· fâ‚…)))`.
-/

restate_axiom whisker_left_id'
restate_axiom whisker_left_comp'
restate_axiom id_whisker_left'
restate_axiom comp_whisker_left'
restate_axiom id_whisker_right'
restate_axiom comp_whisker_right'
restate_axiom whisker_right_id'
restate_axiom whisker_right_comp'
restate_axiom whisker_assoc'
restate_axiom whisker_exchange'
restate_axiom pentagon'
restate_axiom triangle'

attribute [simp]  pentagon triangle
attribute [reassoc]
  whisker_left_comp id_whisker_left comp_whisker_left
  comp_whisker_right whisker_right_id whisker_right_comp
  whisker_assoc whisker_exchange pentagon triangle
/-
The following simp attributes are put in order to rewrite any 2-morphisms into normal forms. There
are associators and unitors in the RHS in the several simp lemmas here (e.g. `id_whisker_left`),
which at first glance look more complicated than the LHS, but they will be eventually reduced by the
pentagon or the triangle identities, and more generally, (forthcoming) `coherence` tactic.
-/
attribute [simp]
  whisker_left_id whisker_left_comp id_whisker_left comp_whisker_left
  id_whisker_right comp_whisker_right whisker_right_id whisker_right_comp
  whisker_assoc
attribute [instance] hom_category

variables {B : Type u} [bicategory.{w v} B] {a b c d e : B}

@[simp, reassoc]
lemma hom_inv_whisker_left (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
  f â— Î·.hom â‰« f â— Î·.inv = ğŸ™ (f â‰« g) :=
by rw [â†whisker_left_comp, hom_inv_id, whisker_left_id]

@[simp, reassoc]
lemma hom_inv_whisker_right {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
  Î·.hom â–· h â‰« Î·.inv â–· h = ğŸ™ (f â‰« h) :=
by rw [â†comp_whisker_right, hom_inv_id, id_whisker_right]

@[simp, reassoc]
lemma inv_hom_whisker_left (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
  f â— Î·.inv â‰« f â— Î·.hom = ğŸ™ (f â‰« h) :=
by rw [â†whisker_left_comp, inv_hom_id, whisker_left_id]

@[simp, reassoc]
lemma inv_hom_whisker_right {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
  Î·.inv â–· h â‰« Î·.hom â–· h = ğŸ™ (g â‰« h) :=
by rw [â†comp_whisker_right, inv_hom_id, id_whisker_right]

/-- The left whiskering of a 2-isomorphism is a 2-isomorphism. -/
@[simps]
def whisker_left_iso (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g â‰… h) :
  f â‰« g â‰… f â‰« h :=
{ hom := f â— Î·.hom,
  inv := f â— Î·.inv }

instance whisker_left_is_iso (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) [is_iso Î·] :
  is_iso (f â— Î·) :=
is_iso.of_iso (whisker_left_iso f (as_iso Î·))

@[simp]
lemma inv_whisker_left (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h) [is_iso Î·] :
  inv (f â— Î·) = f â— (inv Î·) :=
by { ext, simp only [â†whisker_left_comp, whisker_left_id, is_iso.hom_inv_id] }

/-- The right whiskering of a 2-isomorphism is a 2-isomorphism. -/
@[simps]
def whisker_right_iso {f g : a âŸ¶ b} (Î· : f â‰… g) (h : b âŸ¶ c) :
  f â‰« h â‰… g â‰« h :=
{ hom := Î·.hom â–· h,
  inv := Î·.inv â–· h }

instance whisker_right_is_iso {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) [is_iso Î·] :
  is_iso (Î· â–· h) :=
is_iso.of_iso (whisker_right_iso (as_iso Î·) h)

@[simp]
lemma inv_whisker_right {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c) [is_iso Î·] :
  inv (Î· â–· h) = (inv Î·) â–· h :=
by { ext, simp only [â†comp_whisker_right, id_whisker_right, is_iso.hom_inv_id] }

@[simp, reassoc]
lemma pentagon_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  f â— (Î±_ g h i).inv â‰« (Î±_ f (g â‰« h) i).inv â‰« (Î±_ f g h).inv â–· i =
    (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv :=
eq_of_inv_eq_inv (by simp)

@[simp, reassoc]
lemma pentagon_inv_inv_hom_hom_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f (g â‰« h) i).inv â‰« (Î±_ f g h).inv â–· i â‰« (Î±_ (f â‰« g) h i).hom =
    f â— (Î±_ g h i).hom â‰« (Î±_ f g (h â‰« i)).inv :=
by { rw [â†cancel_epi (f â— (Î±_ g h i).inv), â†cancel_mono (Î±_ (f â‰« g) h i).inv], simp }

@[simp, reassoc]
lemma pentagon_inv_hom_hom_hom_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ (f â‰« g) h i).inv â‰« (Î±_ f g h).hom â–· i â‰« (Î±_ f (g â‰« h) i).hom =
    (Î±_ f g (h â‰« i)).hom â‰« f â— (Î±_ g h i).inv :=
eq_of_inv_eq_inv (by simp)

@[simp, reassoc]
lemma pentagon_hom_inv_inv_inv_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  f â— (Î±_ g h i).hom â‰« (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv =
    (Î±_ f (g â‰« h) i).inv â‰« (Î±_ f g h).inv â–· i :=
by simp [â†cancel_epi (f â— (Î±_ g h i).inv)]

@[simp, reassoc]
lemma pentagon_hom_hom_inv_hom_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom â‰« f â— (Î±_ g h i).inv =
    (Î±_ f g h).hom â–· i â‰« (Î±_ f (g â‰« h) i).hom :=
eq_of_inv_eq_inv (by simp)

@[simp, reassoc]
lemma pentagon_hom_inv_inv_inv_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f g (h â‰« i)).hom â‰« f â— (Î±_ g h i).inv â‰« (Î±_ f (g â‰« h) i).inv =
    (Î±_ (f â‰« g) h i).inv â‰« (Î±_ f g h).hom â–· i :=
by { rw [â†cancel_epi (Î±_ f g (h â‰« i)).inv, â†cancel_mono ((Î±_ f g h).inv â–· i)], simp }

@[simp, reassoc]
lemma pentagon_hom_hom_inv_inv_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f (g â‰« h) i).hom â‰« f â— (Î±_ g h i).hom â‰« (Î±_ f g (h â‰« i)).inv =
    (Î±_ f g h).inv â–· i â‰« (Î±_ (f â‰« g) h i).hom :=
eq_of_inv_eq_inv (by simp)

@[simp, reassoc]
lemma pentagon_inv_hom_hom_hom_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f g h).inv â–· i â‰« (Î±_ (f â‰« g) h i).hom â‰« (Î±_ f g (h â‰« i)).hom =
    (Î±_ f (g â‰« h) i).hom â‰« f â— (Î±_ g h i).hom :=
by simp [â†cancel_epi ((Î±_ f g h).hom â–· i)]

@[simp, reassoc]
lemma pentagon_inv_inv_hom_inv_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : d âŸ¶ e) :
  (Î±_ f g (h â‰« i)).inv â‰« (Î±_ (f â‰« g) h i).inv â‰« (Î±_ f g h).hom â–· i =
    f â— (Î±_ g h i).inv â‰« (Î±_ f (g â‰« h) i).inv :=
eq_of_inv_eq_inv (by simp)

lemma triangle_assoc_comp_left (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î±_ f (ğŸ™ b) g).hom â‰« f â— (Î»_ g).hom = (Ï_ f).hom â–· g :=
triangle f g

@[simp, reassoc]
lemma triangle_assoc_comp_right (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î±_ f (ğŸ™ b) g).inv â‰« (Ï_ f).hom â–· g = f â— (Î»_ g).hom :=
by rw [â†triangle, inv_hom_id_assoc]

@[simp, reassoc]
lemma triangle_assoc_comp_right_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Ï_ f).inv â–· g â‰« (Î±_ f (ğŸ™ b) g).hom = f â— (Î»_ g).inv :=
by simp [â†cancel_mono (f â— (Î»_ g).hom)]

@[simp, reassoc]
lemma triangle_assoc_comp_left_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  f â— (Î»_ g).inv â‰« (Î±_ f (ğŸ™ b) g).inv = (Ï_ f).inv â–· g :=
by simp [â†cancel_mono ((Ï_ f).hom â–· g)]

@[reassoc]
lemma associator_naturality_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
  (Î· â–· g) â–· h â‰« (Î±_ f' g h).hom = (Î±_ f g h).hom â‰« Î· â–· (g â‰« h) :=
by simp

@[reassoc]
lemma associator_inv_naturality_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
  Î· â–· (g â‰« h) â‰« (Î±_ f' g h).inv = (Î±_ f g h).inv â‰« (Î· â–· g) â–· h :=
by simp

@[reassoc]
lemma whisker_right_comp_symm {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) (h : c âŸ¶ d) :
  (Î· â–· g) â–· h = (Î±_ f g h).hom â‰« Î· â–· (g â‰« h) â‰« (Î±_ f' g h).inv :=
by simp

@[reassoc]
lemma associator_naturality_middle (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
  (f â— Î·) â–· h â‰« (Î±_ f g' h).hom = (Î±_ f g h).hom â‰« f â— (Î· â–· h) :=
by simp

@[reassoc]
lemma associator_inv_naturality_middle (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
  f â— (Î· â–· h) â‰« (Î±_ f g' h).inv = (Î±_ f g h).inv â‰« (f â— Î·) â–· h :=
by simp

@[reassoc]
lemma whisker_assoc_symm (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') (h : c âŸ¶ d) :
  f â— (Î· â–· h) = (Î±_ f g h).inv â‰« (f â— Î·) â–· h â‰« (Î±_ f g' h).hom :=
by simp

@[reassoc]
lemma associator_naturality_right (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
  (f â‰« g) â— Î· â‰« (Î±_ f g h').hom = (Î±_ f g h).hom â‰« f â— (g â— Î·) :=
by simp

@[reassoc]
lemma associator_inv_naturality_right (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
  f â— (g â— Î·) â‰« (Î±_ f g h').inv = (Î±_ f g h).inv â‰« (f â‰« g) â— Î· :=
by simp

@[reassoc]
lemma comp_whisker_left_symm (f : a âŸ¶ b) (g : b âŸ¶ c) {h h' : c âŸ¶ d} (Î· : h âŸ¶ h') :
  f â— (g â— Î·) = (Î±_ f g h).inv â‰« (f â‰« g) â— Î· â‰« (Î±_ f g h').hom :=
by simp

@[reassoc]
lemma left_unitor_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  ğŸ™ a â— Î· â‰« (Î»_ g).hom = (Î»_ f).hom â‰« Î· :=
by simp

@[reassoc]
lemma left_unitor_inv_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  Î· â‰« (Î»_ g).inv = (Î»_ f).inv â‰« ğŸ™ a â— Î· :=
by simp

lemma id_whisker_left_symm {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  Î· = (Î»_ f).inv â‰« ğŸ™ a â— Î· â‰« (Î»_ g).hom :=
by simp

@[reassoc]
lemma right_unitor_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  Î· â–· ğŸ™ b â‰« (Ï_ g).hom = (Ï_ f).hom â‰« Î· :=
by simp

@[reassoc]
lemma right_unitor_inv_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  Î· â‰« (Ï_ g).inv = (Ï_ f).inv â‰« Î· â–· ğŸ™ b :=
by simp

lemma whisker_right_id_symm {f g : a âŸ¶ b} (Î· : f âŸ¶ g) :
  Î· = (Ï_ f).inv â‰« Î· â–· ğŸ™ b â‰« (Ï_ g).hom :=
by simp

lemma whisker_left_iff {f g : a âŸ¶ b} (Î· Î¸ : f âŸ¶ g) :
  (ğŸ™ a â— Î· = ğŸ™ a â— Î¸) â†” (Î· = Î¸) :=
by simp

lemma whisker_right_iff {f g : a âŸ¶ b} (Î· Î¸ : f âŸ¶ g) :
  (Î· â–· ğŸ™ b = Î¸ â–· ğŸ™ b) â†” (Î· = Î¸) :=
by simp

/--
We state it as a simp lemma, which is regarded as an involved version of
`id_whisker_right f g : ğŸ™ f â–· g = ğŸ™ (f â‰« g)`.
-/
@[reassoc, simp]
lemma left_unitor_whisker_right (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ f).hom â–· g = (Î±_ (ğŸ™ a) f g).hom â‰« (Î»_ (f â‰« g)).hom :=
by rw [â†whisker_left_iff, whisker_left_comp, â†cancel_epi (Î±_ _ _ _).hom,
  â†cancel_epi ((Î±_ _ _ _).hom â–· _), pentagon_assoc, triangle,
  â†associator_naturality_middle, â†comp_whisker_right_assoc, triangle,
  associator_naturality_left]; apply_instance

@[reassoc, simp]
lemma left_unitor_inv_whisker_right (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ f).inv â–· g = (Î»_ (f â‰« g)).inv â‰« (Î±_ (ğŸ™ a) f g).inv :=
eq_of_inv_eq_inv (by simp)

@[reassoc, simp]
lemma whisker_left_right_unitor (f : a âŸ¶ b) (g : b âŸ¶ c) :
  f â— (Ï_ g).hom = (Î±_ f g (ğŸ™ c)).inv â‰« (Ï_ (f â‰« g)).hom :=
by rw [â†whisker_right_iff, comp_whisker_right, â†cancel_epi (Î±_ _ _ _).inv,
  â†cancel_epi (f â— (Î±_ _ _ _).inv), pentagon_inv_assoc, triangle_assoc_comp_right,
  â†associator_inv_naturality_middle, â†whisker_left_comp_assoc, triangle_assoc_comp_right,
  associator_inv_naturality_right]; apply_instance

@[reassoc, simp]
lemma whisker_left_right_unitor_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  f â— (Ï_ g).inv = (Ï_ (f â‰« g)).inv â‰« (Î±_ f g (ğŸ™ c)).hom :=
eq_of_inv_eq_inv (by simp)

/-
It is not so obvious whether `left_unitor_whisker_right` or `left_unitor_comp` should be a simp
lemma. Our choice is the former. One reason is that the latter yields the following loop:
[id_whisker_left]   : ğŸ™ a â— (Ï_ f).hom ==> (Î»_ (f â‰« ğŸ™ b)).hom â‰« (Ï_ f).hom â‰« (Î»_ f).inv
[left_unitor_comp]  : (Î»_ (f â‰« ğŸ™ b)).hom ==> (Î±_ (ğŸ™ a) f (ğŸ™ b)).inv â‰« (Î»_ f).hom â–· ğŸ™ b
[whisker_right_id]  : (Î»_ f).hom â–· ğŸ™ b ==> (Ï_ (ğŸ™ a â‰« f)).hom â‰« (Î»_ f).hom â‰« (Ï_ f).inv
[right_unitor_comp] : (Ï_ (ğŸ™ a â‰« f)).hom ==> (Î±_ (ğŸ™ a) f (ğŸ™ b)).hom â‰« ğŸ™ a â— (Ï_ f).hom
-/
@[reassoc]
lemma left_unitor_comp (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ (f â‰« g)).hom = (Î±_ (ğŸ™ a) f g).inv â‰« (Î»_ f).hom â–· g :=
by simp

@[reassoc]
lemma left_unitor_comp_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Î»_ (f â‰« g)).inv = (Î»_ f).inv â–· g â‰« (Î±_ (ğŸ™ a) f g).hom :=
by simp

@[reassoc]
lemma right_unitor_comp (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Ï_ (f â‰« g)).hom = (Î±_ f g (ğŸ™ c)).hom â‰« f â— (Ï_ g).hom :=
by simp

@[reassoc]
lemma right_unitor_comp_inv (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (Ï_ (f â‰« g)).inv = f â— (Ï_ g).inv â‰« (Î±_ f g (ğŸ™ c)).inv :=
by simp

@[simp]
lemma unitors_equal : (Î»_ (ğŸ™ a)).hom = (Ï_ (ğŸ™ a)).hom :=
by rw [â†whisker_left_iff, â†cancel_epi (Î±_ _ _ _).hom, â†cancel_mono (Ï_ _).hom, triangle,
  â†right_unitor_comp, right_unitor_naturality]; apply_instance

@[simp]
lemma unitors_inv_equal : (Î»_ (ğŸ™ a)).inv = (Ï_ (ğŸ™ a)).inv :=
by simp [iso.inv_eq_inv]

end bicategory

end category_theory
