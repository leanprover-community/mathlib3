/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.coherence

/-!
# A `coherence` tactic for bicategories, and `âŠ—â‰«` (composition up to associators)

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We provide a `coherence` tactic,
which proves that any two 2-morphisms (with the same source and target)
in a bicategory which are built out of associators and unitors
are equal.

We also provide `f âŠ—â‰« g`, the `bicategorical_comp` operation,
which automatically inserts associators and unitors as needed
to make the target of `f` match the source of `g`.

This file mainly deals with the type class setup for the coherence tactic. The actual front end
tactic is given in `category_theory/monooidal/coherence.lean` at the same time as the coherence
tactic for monoidal categories.
-/

noncomputable theory

universes w v u

open category_theory
open category_theory.free_bicategory
open_locale bicategory

variables {B : Type u} [bicategory.{w v} B] {a b c d e : B}

namespace category_theory.bicategory

/-- A typeclass carrying a choice of lift of a 1-morphism from `B` to `free_bicategory B`. -/
class lift_hom {a b : B} (f : a âŸ¶ b) :=
(lift : of.obj a âŸ¶ of.obj b)

instance lift_hom_id : lift_hom (ðŸ™ a) := { lift := ðŸ™ (of.obj a), }
instance lift_hom_comp (f : a âŸ¶ b) (g : b âŸ¶ c) [lift_hom f] [lift_hom g] : lift_hom (f â‰« g) :=
{ lift := lift_hom.lift f â‰« lift_hom.lift g, }
@[priority 100]
instance lift_hom_of (f : a âŸ¶ b) : lift_hom f := { lift := of.map f, }

/-- A typeclass carrying a choice of lift of a 2-morphism from `B` to `free_bicategory B`. -/
class lift_homâ‚‚ {f g : a âŸ¶ b} [lift_hom f] [lift_hom g] (Î· : f âŸ¶ g) :=
(lift : lift_hom.lift f âŸ¶ lift_hom.lift g)

instance lift_homâ‚‚_id (f : a âŸ¶ b) [lift_hom f] : lift_homâ‚‚ (ðŸ™ f) :=
{ lift := ðŸ™ _, }
instance lift_homâ‚‚_left_unitor_hom (f : a âŸ¶ b) [lift_hom f] : lift_homâ‚‚ (Î»_ f).hom :=
{ lift := (Î»_ (lift_hom.lift f)).hom, }
instance lift_homâ‚‚_left_unitor_inv (f : a âŸ¶ b) [lift_hom f] : lift_homâ‚‚ (Î»_ f).inv :=
{ lift := (Î»_ (lift_hom.lift f)).inv, }
instance lift_homâ‚‚_right_unitor_hom (f : a âŸ¶ b) [lift_hom f] : lift_homâ‚‚ (Ï_ f).hom :=
{ lift := (Ï_ (lift_hom.lift f)).hom, }
instance lift_homâ‚‚_right_unitor_inv (f : a âŸ¶ b) [lift_hom f] : lift_homâ‚‚ (Ï_ f).inv :=
{ lift := (Ï_ (lift_hom.lift f)).inv, }
instance lift_homâ‚‚_associator_hom (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d)
  [lift_hom f] [lift_hom g] [lift_hom h] :
  lift_homâ‚‚ (Î±_ f g h).hom :=
{ lift := (Î±_ (lift_hom.lift f) (lift_hom.lift g) (lift_hom.lift h)).hom, }
instance lift_homâ‚‚_associator_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d)
  [lift_hom f] [lift_hom g] [lift_hom h] :
  lift_homâ‚‚ (Î±_ f g h).inv :=
{ lift := (Î±_ (lift_hom.lift f) (lift_hom.lift g) (lift_hom.lift h)).inv, }
instance lift_homâ‚‚_comp {f g h : a âŸ¶ b} [lift_hom f] [lift_hom g] [lift_hom h]
  (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) [lift_homâ‚‚ Î·] [lift_homâ‚‚ Î¸] : lift_homâ‚‚ (Î· â‰« Î¸) :=
{ lift := lift_homâ‚‚.lift Î· â‰« lift_homâ‚‚.lift Î¸ }
instance lift_homâ‚‚_whisker_left (f : a âŸ¶ b) [lift_hom f] {g h : b âŸ¶ c} (Î· : g âŸ¶ h)
  [lift_hom g] [lift_hom h] [lift_homâ‚‚ Î·] : lift_homâ‚‚ (f â— Î·) :=
{ lift := lift_hom.lift f â— lift_homâ‚‚.lift Î· }
instance lift_homâ‚‚_whisker_right {f g : a âŸ¶ b} (Î· : f âŸ¶ g) [lift_hom f] [lift_hom g] [lift_homâ‚‚ Î·]
  {h : b âŸ¶ c} [lift_hom h] : lift_homâ‚‚ (Î· â–· h) :=
{ lift := lift_homâ‚‚.lift Î· â–· lift_hom.lift h }

/--
A typeclass carrying a choice of bicategorical structural isomorphism between two objects.
Used by the `âŠ—â‰«` bicategorical composition operator, and the `coherence` tactic.
-/
class bicategorical_coherence (f g : a âŸ¶ b) [lift_hom f] [lift_hom g] :=
(hom [] : f âŸ¶ g)
[is_iso : is_iso hom . tactic.apply_instance]

attribute [instance] bicategorical_coherence.is_iso

namespace bicategorical_coherence

@[simps]
instance refl (f : a âŸ¶ b) [lift_hom f] : bicategorical_coherence f f := âŸ¨ðŸ™ _âŸ©

@[simps]
instance whisker_left
  (f : a âŸ¶ b) (g h : b âŸ¶ c) [lift_hom f][lift_hom g] [lift_hom h] [bicategorical_coherence g h] :
  bicategorical_coherence (f â‰« g) (f â‰« h) :=
âŸ¨f â— bicategorical_coherence.hom g hâŸ©

@[simps]
instance whisker_right
  (f g : a âŸ¶ b) (h : b âŸ¶ c) [lift_hom f] [lift_hom g] [lift_hom h] [bicategorical_coherence f g] :
  bicategorical_coherence (f â‰« h) (g â‰« h) :=
âŸ¨bicategorical_coherence.hom f g â–· hâŸ©

@[simps]
instance tensor_right (f : a âŸ¶ b) (g : b âŸ¶ b) [lift_hom f] [lift_hom g]
  [bicategorical_coherence (ðŸ™ b) g] :
  bicategorical_coherence f (f â‰« g) :=
âŸ¨(Ï_ f).inv â‰« (f â— bicategorical_coherence.hom (ðŸ™ b) g)âŸ©

@[simps]
instance tensor_right' (f : a âŸ¶ b) (g : b âŸ¶ b) [lift_hom f] [lift_hom g]
  [bicategorical_coherence g (ðŸ™ b)] :
  bicategorical_coherence (f â‰« g) f :=
âŸ¨(f â— bicategorical_coherence.hom g (ðŸ™ b)) â‰« (Ï_ f).homâŸ©

@[simps]
instance left (f g : a âŸ¶ b) [lift_hom f] [lift_hom g] [bicategorical_coherence f g] :
  bicategorical_coherence (ðŸ™ a â‰« f) g :=
âŸ¨(Î»_ f).hom â‰« bicategorical_coherence.hom f gâŸ©

@[simps]
instance left' (f g : a âŸ¶ b) [lift_hom f] [lift_hom g] [bicategorical_coherence f g] :
  bicategorical_coherence f (ðŸ™ a â‰« g) :=
âŸ¨bicategorical_coherence.hom f g â‰« (Î»_ g).invâŸ©

@[simps]
instance right (f g : a âŸ¶ b) [lift_hom f] [lift_hom g] [bicategorical_coherence f g] :
  bicategorical_coherence (f â‰« ðŸ™ b) g :=
âŸ¨(Ï_ f).hom â‰« bicategorical_coherence.hom f gâŸ©

@[simps]
instance right' (f g : a âŸ¶ b) [lift_hom f] [lift_hom g] [bicategorical_coherence f g] :
  bicategorical_coherence f (g â‰« ðŸ™ b) :=
âŸ¨bicategorical_coherence.hom f g â‰« (Ï_ g).invâŸ©

@[simps]
instance assoc (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : a âŸ¶ d)
  [lift_hom f] [lift_hom g] [lift_hom h] [lift_hom i] [bicategorical_coherence (f â‰« (g â‰« h)) i] :
  bicategorical_coherence ((f â‰« g) â‰« h) i :=
âŸ¨(Î±_ f g h).hom â‰« bicategorical_coherence.hom (f â‰« (g â‰« h)) iâŸ©

@[simps]
instance assoc' (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) (i : a âŸ¶ d)
  [lift_hom f] [lift_hom g] [lift_hom h] [lift_hom i] [bicategorical_coherence i (f â‰« (g â‰« h))] :
  bicategorical_coherence i ((f â‰« g) â‰« h) :=
âŸ¨bicategorical_coherence.hom i (f â‰« (g â‰« h)) â‰« (Î±_ f g h).invâŸ©

end bicategorical_coherence

/-- Construct an isomorphism between two objects in a bicategorical category
out of unitors and associators. -/
def bicategorical_iso (f g : a âŸ¶ b) [lift_hom f] [lift_hom g] [bicategorical_coherence f g] :
  f â‰… g :=
as_iso (bicategorical_coherence.hom f g)

/-- Compose two morphisms in a bicategorical category,
inserting unitors and associators between as necessary. -/
def bicategorical_comp {f g h i : a âŸ¶ b} [lift_hom g] [lift_hom h]
  [bicategorical_coherence g h] (Î· : f âŸ¶ g) (Î¸ : h âŸ¶ i) : f âŸ¶ i :=
Î· â‰« bicategorical_coherence.hom g h â‰« Î¸

localized "infixr (name := bicategorical_comp) ` âŠ—â‰« `:80 :=
  category_theory.bicategory.bicategorical_comp" in bicategory -- type as \ot \gg

/-- Compose two isomorphisms in a bicategorical category,
inserting unitors and associators between as necessary. -/
def bicategorical_iso_comp {f g h i : a âŸ¶ b} [lift_hom g] [lift_hom h]
  [bicategorical_coherence g h] (Î· : f â‰… g) (Î¸ : h â‰… i) : f â‰… i :=
Î· â‰ªâ‰« as_iso (bicategorical_coherence.hom g h) â‰ªâ‰« Î¸

localized "infixr (name := bicategorical_iso_comp) ` â‰ªâŠ—â‰« `:80 :=
  category_theory.bicategory.bicategorical_iso_comp" in bicategory -- type as \ot \gg

example {f' : a âŸ¶ d} {f : a âŸ¶ b} {g : b âŸ¶ c} {h : c âŸ¶ d} {h' : a âŸ¶ d}
  (Î· : f' âŸ¶ f â‰« (g â‰« h)) (Î¸ : (f â‰« g) â‰« h âŸ¶ h') : f' âŸ¶ h' := Î· âŠ—â‰« Î¸

-- To automatically insert unitors/associators at the beginning or end,
-- you can use `Î· âŠ—â‰« ðŸ™ _`
example {f' : a âŸ¶ d } {f : a âŸ¶ b} {g : b âŸ¶ c} {h : c âŸ¶ d} (Î· : f' âŸ¶ (f â‰« g) â‰« h) :
  f' âŸ¶ f â‰« (g â‰« h) := Î· âŠ—â‰« ðŸ™ _

@[simp] lemma bicategorical_comp_refl {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) :
  Î· âŠ—â‰« Î¸ = Î· â‰« Î¸ :=
by { dsimp [bicategorical_comp], simp, }

end category_theory.bicategory

open category_theory.bicategory

namespace tactic

setup_tactic_parser

/-- Coherence tactic for bicategories. -/
meta def bicategorical_coherence : tactic unit :=
focus1 $
do
  o â† get_options, set_options $ o.set_nat `class.instance_max_depth 128,
  try `[dsimp],
  `(%%lhs = %%rhs) â† target,
  to_expr  ``((free_bicategory.lift (prefunctor.id _)).mapâ‚‚ (lift_homâ‚‚.lift %%lhs) =
    (free_bicategory.lift (prefunctor.id _)).mapâ‚‚ (lift_homâ‚‚.lift %%rhs))
    >>= tactic.change,
  congr

namespace bicategory

/-- Simp lemmas for rewriting a 2-morphism into a normal form. -/
meta def whisker_simps : tactic unit :=
`[simp only [
    category_theory.category.assoc,
    category_theory.bicategory.comp_whisker_left,
    category_theory.bicategory.id_whisker_left,
    category_theory.bicategory.whisker_right_comp,
    category_theory.bicategory.whisker_right_id,
    category_theory.bicategory.whisker_left_comp,
    category_theory.bicategory.whisker_left_id,
    category_theory.bicategory.comp_whisker_right,
    category_theory.bicategory.id_whisker_right,
    category_theory.bicategory.whisker_assoc]]

namespace coherence

/--
Auxiliary simp lemma for the `coherence` tactic:
this move brackets to the left in order to expose a maximal prefix
built out of unitors and associators.
-/
-- We have unused typeclass arguments here.
-- They are intentional, to ensure that `simp only [assoc_lift_homâ‚‚]` only left associates
-- bicategorical structural morphisms.
@[nolint unused_arguments]
lemma assoc_lift_homâ‚‚ {f g h i : a âŸ¶ b} [lift_hom f] [lift_hom g] [lift_hom h]
  (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h) (Î¹ : h âŸ¶ i) [lift_homâ‚‚ Î·] [lift_homâ‚‚ Î¸] :
  Î· â‰« (Î¸ â‰« Î¹) = (Î· â‰« Î¸) â‰« Î¹ :=
(category.assoc _ _ _).symm

end coherence

end bicategory

end tactic
