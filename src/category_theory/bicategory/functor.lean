/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.basic

/-!
# Oplax functors and pseudofunctors

An oplax functor `F` between bicategories `B` and `C` consists of
* a function between objects `F.obj : B âŸ¶ C`,
* a family of functions between 1-morphisms `F.map : (a âŸ¶ b) â†’ (obj a âŸ¶ obj b)`,
* a family of functions between 2-morphisms `F.mapâ‚‚ : (f âŸ¶ g) â†’ (map f âŸ¶ map g)`,
* a family of 2-morphisms `F.map_id a : F.map (ğŸ™ a) âŸ¶ ğŸ™ (F.obj a)`,
* a family of 2-morphisms `F.map_comp f g : F.map (f â‰« g) âŸ¶ F.map f â‰« F.map g`, and
* certain consistency conditions on them.
A pseudofunctor is an oplax functor whose `map_id` and `map_comp` are isomorphisms.

## Main definitions

* `oplax_functor B C` : an oplax functor between bicategories `B` and `C`
* `oplax_functor.comp F G` : the composition of oplax functors
* `pseudofunctor B C` : a pseudofunctor between bicategories `B` and `C`
* `pseudofunctor.comp F G` : the composition of pseudofunctors

## Future work

There are two types of functors between bicategories, called lax and oplax functors, depending on
the directions of `map_id` and `map_comp`. We may need both in mathlib in the future, but for
now we only define oplax functors.
-/

set_option old_structure_cmd true

namespace category_theory

open category bicategory
open_locale bicategory

universes wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

section
variables (B : Type uâ‚) [quiver.{vâ‚+1} B] [âˆ€ a b : B, quiver.{wâ‚+1} (a âŸ¶ b)]
variables (C : Type uâ‚‚) [quiver.{vâ‚‚+1} C] [âˆ€ a b : C, quiver.{wâ‚‚+1} (a âŸ¶ b)]

/--
A prelax functor between bicategories consists of functions between objects,
1-morphisms, and 2-morphisms. This structure will be extended to define `oplax_functor`.
-/
structure prelax_functor extends prefunctor B C :=
(mapâ‚‚ {a b : B} {f g : a âŸ¶ b} : (f âŸ¶ g) â†’ (map f âŸ¶ map g))

/-- The prefunctor between the underlying quivers. -/
add_decl_doc prelax_functor.to_prefunctor

namespace prelax_functor

variables {B C} {D : Type uâ‚ƒ} [quiver.{vâ‚ƒ+1} D] [âˆ€ a b : D, quiver.{wâ‚ƒ+1} (a âŸ¶ b)]
variables (F : prelax_functor B C) (G : prelax_functor C D)

instance has_coe_to_prefunctor : has_coe (prelax_functor B C) (prefunctor B C) := âŸ¨to_prefunctorâŸ©

@[simp] lemma to_prefunctor_eq_coe : F.to_prefunctor = F := rfl
@[simp] lemma to_prefunctor_obj : (F : prefunctor B C).obj = F.obj := rfl
@[simp] lemma to_prefunctor_map : (F : prefunctor B C).map = F.map := rfl

variables (B)

/-- The identity prelax functor. -/
@[simps]
def id : prelax_functor B B :=
{ mapâ‚‚ := Î» a b f g Î·, Î·, .. prefunctor.id B }

instance : inhabited (prelax_functor B B) := âŸ¨prelax_functor.id BâŸ©

variables {B}

/-- Composition of prelax functors. -/
@[simps]
def comp : prelax_functor B D :=
{ mapâ‚‚ := Î» a b f g Î·, G.mapâ‚‚ (F.mapâ‚‚ Î·), .. (F : prefunctor B C).comp â†‘G }

end prelax_functor

end

section
variables {B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B] {C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]
variables {D : Type uâ‚ƒ} [bicategory.{wâ‚ƒ vâ‚ƒ} D]

/--
This auxiliary definition states that oplax functors preserve the associators
modulo some adjustments of domains and codomains of 2-morphisms.
-/
/-
We use this auxiliary definition instead of writing it directly in the definition
of oplax functors because doing so will cause a timeout.
-/
@[simp]
def oplax_functor.mapâ‚‚_associator_aux
  (obj : B â†’ C) (map : Î  {X Y : B}, (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y))
  (mapâ‚‚ : Î  {a b : B} {f g : a âŸ¶ b}, (f âŸ¶ g) â†’ (map f âŸ¶ map g))
  (map_comp : Î  {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c), map (f â‰« g) âŸ¶ map f â‰« map g)
  {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) : Prop :=
mapâ‚‚ (Î±_ f g h).hom â‰« map_comp f (g â‰« h) â‰« (map f â— map_comp g h) =
  map_comp (f â‰« g) h â‰« (map_comp f g â–· map h) â‰« (Î±_ (map f) (map g) (map h)).hom

variables (B C)

/--
An oplax functor `F` between bicategories `B` and `C` consists of functions between objects,
1-morphisms, and 2-morphisms.

Unlike functors between categories, functions between 1-morphisms do not need to strictly commute
with compositions, and do not need to strictly preserve the identity. Instead, there are
specified 2-morphisms `F.map (ğŸ™ a) âŸ¶ ğŸ™ (F.obj a)` and `F.map (f â‰« g) âŸ¶ F.map f â‰« F.map g`.

Functions between 2-morphisms strictly commute with compositions and preserve the identity.
They also preserve the associator, the left unitor, and the right unitor modulo some adjustments
of domains and codomains of 2-morphisms.
-/
structure oplax_functor extends prelax_functor B C :=
(map_id (a : B) : map (ğŸ™ a) âŸ¶ ğŸ™ (obj a))
(map_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : map (f â‰« g) âŸ¶ map f â‰« map g)
(map_comp_naturality_left' : âˆ€ {a b c : B} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c),
  mapâ‚‚ (Î· â–· g) â‰« map_comp f' g = map_comp f g â‰« (mapâ‚‚ Î· â–· map g) . obviously)
(map_comp_naturality_right' : âˆ€ {a b c : B} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g'),
  mapâ‚‚ (f â— Î·) â‰« map_comp f g' = map_comp f g â‰« (map f â— mapâ‚‚ Î·) . obviously)
(mapâ‚‚_id' : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) . obviously)
(mapâ‚‚_comp' : âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
  mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ . obviously)
(mapâ‚‚_associator' : âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
  oplax_functor.mapâ‚‚_associator_aux obj (Î» a b, map) (Î» a b f g, mapâ‚‚) (Î» a b c, map_comp) f g h
    . obviously)
(mapâ‚‚_left_unitor' : âˆ€ {a b : B} (f : a âŸ¶ b),
  mapâ‚‚ (Î»_ f).hom = map_comp (ğŸ™ a) f â‰« (map_id a â–· map f) â‰« (Î»_ (map f)).hom . obviously)
(mapâ‚‚_right_unitor' : âˆ€ {a b : B} (f : a âŸ¶ b),
  mapâ‚‚ (Ï_ f).hom = map_comp f (ğŸ™ b) â‰« (map f â— map_id b) â‰« (Ï_ (map f)).hom . obviously)

restate_axiom oplax_functor.map_comp_naturality_left'
restate_axiom oplax_functor.map_comp_naturality_right'
restate_axiom oplax_functor.mapâ‚‚_id'
restate_axiom oplax_functor.mapâ‚‚_comp'
restate_axiom oplax_functor.mapâ‚‚_associator'
restate_axiom oplax_functor.mapâ‚‚_left_unitor'
restate_axiom oplax_functor.mapâ‚‚_right_unitor'
attribute [simp]
  oplax_functor.map_comp_naturality_left oplax_functor.map_comp_naturality_right
  oplax_functor.mapâ‚‚_id oplax_functor.mapâ‚‚_associator
attribute [reassoc]
  oplax_functor.map_comp_naturality_left oplax_functor.map_comp_naturality_right
  oplax_functor.mapâ‚‚_comp oplax_functor.mapâ‚‚_associator
  oplax_functor.mapâ‚‚_left_unitor oplax_functor.mapâ‚‚_right_unitor
attribute [simp]
  oplax_functor.mapâ‚‚_comp oplax_functor.mapâ‚‚_left_unitor oplax_functor.mapâ‚‚_right_unitor

variables {B C}

namespace oplax_functor

section
variables (F : oplax_functor B C) (G : oplax_functor C D)

/-- Function between 1-morphisms as a functor. -/
@[simps]
def map_functor (a b : B) : (a âŸ¶ b) â¥¤ (F.obj a âŸ¶ F.obj b) :=
{ obj := Î» f, F.map f,
  map := Î» f g Î·, F.mapâ‚‚ Î· }

/-- The prelax functor between the underlying quivers. -/
add_decl_doc oplax_functor.to_prelax_functor

instance has_coe_to_prelax : has_coe (oplax_functor B C) (prelax_functor B C) :=
âŸ¨to_prelax_functorâŸ©

@[simp] lemma to_prelax_eq_coe : F.to_prelax_functor = F := rfl
@[simp] lemma to_prelax_functor_obj : (F : prelax_functor B C).obj = F.obj := rfl
@[simp] lemma to_prelax_functor_map : (F : prelax_functor B C).map = F.map := rfl
@[simp] lemma to_prelax_functor_mapâ‚‚ : (F : prelax_functor B C).mapâ‚‚ = F.mapâ‚‚ := rfl

variables (B)

/-- The identity oplax functor. -/
@[simps]
def id : oplax_functor B B :=
{ map_id := Î» a, ğŸ™ (ğŸ™ a),
  map_comp := Î» a b c f g, ğŸ™ (f â‰« g),
  .. prelax_functor.id B }

instance : inhabited (oplax_functor B B) := âŸ¨id BâŸ©

variables {B}

/-- Composition of oplax functors. -/
@[simps]
def comp : oplax_functor B D :=
{ map_id := Î» a,
    (G.map_functor _ _).map (F.map_id a) â‰« G.map_id (F.obj a),
  map_comp := Î» a b c f g,
    (G.map_functor _ _).map (F.map_comp f g) â‰« G.map_comp (F.map f) (F.map g),
  map_comp_naturality_left' := Î» a b c f f' Î· g, by
  { dsimp,
    rw [â†mapâ‚‚_comp_assoc, map_comp_naturality_left, mapâ‚‚_comp_assoc, map_comp_naturality_left,
      assoc] },
  map_comp_naturality_right' := Î» a b c f g g' Î·, by
  { dsimp,
    rw [â†mapâ‚‚_comp_assoc, map_comp_naturality_right, mapâ‚‚_comp_assoc, map_comp_naturality_right,
      assoc] },
  mapâ‚‚_associator' := Î» a b c d f g h, by
  { dsimp,
    simp only [mapâ‚‚_associator, â†mapâ‚‚_comp_assoc, â†map_comp_naturality_right_assoc,
      whisker_left_comp, assoc],
    simp only [mapâ‚‚_associator, mapâ‚‚_comp, map_comp_naturality_left_assoc,
      whisker_right_comp, assoc] },
  mapâ‚‚_left_unitor' := Î» a b f, by
  { dsimp,
    simp only [mapâ‚‚_left_unitor, mapâ‚‚_comp, map_comp_naturality_left_assoc,
      whisker_right_comp, assoc] },
  mapâ‚‚_right_unitor' := Î» a b f, by
  { dsimp,
    simp only [mapâ‚‚_right_unitor, mapâ‚‚_comp, map_comp_naturality_right_assoc,
      whisker_left_comp, assoc] },
  .. (F : prelax_functor B C).comp â†‘G }

structure pseudo_core (F : oplax_functor B C) :=
(map_id_iso (a : B) : F.map (ğŸ™ a) â‰… ğŸ™ (F.obj a))
(map_comp_iso {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : F.map (f â‰« g) â‰… F.map f â‰« F.map g)
(map_id_iso_hom' : âˆ€ {a : B}, (map_id_iso a).hom = F.map_id a . obviously)
(map_comp_iso_hom' : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
  (map_comp_iso f g).hom = F.map_comp f g . obviously)

restate_axiom pseudo_core.map_id_iso_hom'
restate_axiom pseudo_core.map_comp_iso_hom'
attribute [simp] pseudo_core.map_id_iso_hom pseudo_core.map_comp_iso_hom

class is_pseudo : Prop :=
(map_id_is_iso' : âˆ€ (a : B), is_iso (F.map_id a) . tactic.apply_instance)
(map_comp_is_iso' : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
  is_iso (F.map_comp f g) . tactic.apply_instance)

restate_axiom is_pseudo.map_id_is_iso'
restate_axiom is_pseudo.map_comp_is_iso'
attribute [instance] is_pseudo.map_id_is_iso is_pseudo.map_comp_is_iso

end

end oplax_functor

/--
The auxiliary definition that claims that pseudofunctors preserve the associators
modulo some adjustments of domains and codomains of 2-morphisms. -/
/-
The reason for using this auxiliary definition instead of writing it directly in the definition
of pseudofunctors is that doing so will cause a timeout.
-/
@[simp]
def pseudofunctor.mapâ‚‚_associator_aux
  (obj : B â†’ C) (map : Î  {X Y : B}, (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y))
  (mapâ‚‚ : Î  {a b : B} {f g : a âŸ¶ b}, (f âŸ¶ g) â†’ (map f âŸ¶ map g))
  (map_comp : Î  {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c), map (f â‰« g) â‰… map f â‰« map g)
  {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) : Prop :=
mapâ‚‚ (Î±_ f g h).hom = (map_comp (f â‰« g) h).hom â‰« ((map_comp f g).hom â–· map h) â‰«
  (Î±_ (map f) (map g) (map h)).hom â‰« (map f â— (map_comp g h).inv) â‰« (map_comp f (g â‰« h)).inv

variables (B C)

/--
A pseudofunctors `F` between bicategories `B` and `C` consists of functions between objects,
1-morphisms, and 2-morphisms.

Unlike functors between categories, functions between 1-morphisms do not need to strictly commute
with compositions, and do not need to strictly preserve the identity. Instead, there are
specified 2-isomorphisms `F.map (ğŸ™ a) â‰… ğŸ™ (F.obj a)` and `F.map (f â‰« g) â‰… F.map f â‰« F.map g`.

Functions between 2-morphisms strictly commute with compositions and preserve the identity.
They also preserve the associator, the left unitor, and the right unitor modulo some adjustments
of domains and codomains of 2-morphisms.
-/
structure pseudofunctor extends prelax_functor B C :=
(map_id (a : B) : map (ğŸ™ a) â‰… ğŸ™ (obj a))
(map_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : map (f â‰« g) â‰… map f â‰« map g)
(mapâ‚‚_whisker_right' : âˆ€ {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c),
  mapâ‚‚ (Î· â–· h) = (map_comp f h).hom â‰« (mapâ‚‚ Î· â–· map h) â‰« (map_comp g h).inv . obviously)
(mapâ‚‚_whisker_left' : âˆ€ {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h),
  mapâ‚‚ (f â— Î·) = (map_comp f g).hom â‰« (map f â— mapâ‚‚ Î·) â‰« (map_comp f h).inv . obviously)
(mapâ‚‚_id' : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) . obviously)
(mapâ‚‚_comp' : âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
  mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ . obviously)
(mapâ‚‚_associator' : âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
  pseudofunctor.mapâ‚‚_associator_aux obj (Î» a b, map) (Î» a b f g, mapâ‚‚) (Î» a b c, map_comp) f g h
    . obviously)
(mapâ‚‚_left_unitor' : âˆ€ {a b : B} (f : a âŸ¶ b),
  mapâ‚‚ (Î»_ f).hom = (map_comp (ğŸ™ a) f).hom â‰« ((map_id a).hom â–· map f) â‰« (Î»_ (map f)).hom
    . obviously)
(mapâ‚‚_right_unitor' : âˆ€ {a b : B} (f : a âŸ¶ b),
  mapâ‚‚ (Ï_ f).hom = (map_comp f (ğŸ™ b)).hom â‰« (map f â— (map_id b).hom) â‰« (Ï_ (map f)).hom
    . obviously)

restate_axiom pseudofunctor.mapâ‚‚_whisker_right'
restate_axiom pseudofunctor.mapâ‚‚_whisker_left'
restate_axiom pseudofunctor.mapâ‚‚_id'
restate_axiom pseudofunctor.mapâ‚‚_comp'
restate_axiom pseudofunctor.mapâ‚‚_associator'
restate_axiom pseudofunctor.mapâ‚‚_left_unitor'
restate_axiom pseudofunctor.mapâ‚‚_right_unitor'
attribute [simp]
  pseudofunctor.mapâ‚‚_whisker_right pseudofunctor.mapâ‚‚_whisker_left
  pseudofunctor.mapâ‚‚_id pseudofunctor.mapâ‚‚_associator
attribute [reassoc]
  pseudofunctor.mapâ‚‚_whisker_right pseudofunctor.mapâ‚‚_whisker_left
  pseudofunctor.mapâ‚‚_comp pseudofunctor.mapâ‚‚_associator
  pseudofunctor.mapâ‚‚_left_unitor pseudofunctor.mapâ‚‚_right_unitor
attribute [simp]
  pseudofunctor.mapâ‚‚_comp pseudofunctor.mapâ‚‚_left_unitor pseudofunctor.mapâ‚‚_right_unitor

variables {B C}

namespace pseudofunctor

section
open iso
variables (F : pseudofunctor B C) (G : pseudofunctor C D)

/-- Function on 1-morphisms as a functor. -/
@[simps]
def map_functor {a b : B} : (a âŸ¶ b) â¥¤ (F.obj a âŸ¶ F.obj b) :=
{ obj := Î» f, F.map f,
  map := Î» f g Î·, F.mapâ‚‚ Î· }

/-- The prelax functor between the underlying quivers. -/
add_decl_doc pseudofunctor.to_prelax_functor

instance has_coe_to_prelax : has_coe (pseudofunctor B C) (prelax_functor B C) :=
âŸ¨to_prelax_functorâŸ©

@[simp] lemma to_prelax_eq_coe : F.to_prelax_functor = F := rfl
@[simp] lemma to_prelax_functor_obj : (F : prelax_functor B C).obj = F.obj := rfl
@[simp] lemma to_prelax_functor_map : (F : prelax_functor B C).map = F.map := rfl
@[simp] lemma to_prelax_functor_mapâ‚‚ : (F : prelax_functor B C).mapâ‚‚ = F.mapâ‚‚ := rfl

/-- The oplax functor associated with a pseudofunctor. -/
def to_oplax : oplax_functor B C :=
{ map_id := Î» a, (F.map_id a).hom,
  map_comp := Î» a b c f g, (F.map_comp f g).hom,
  .. (F : prelax_functor B C) }

instance has_coe_to_oplax : has_coe (pseudofunctor B C) (oplax_functor B C) := âŸ¨to_oplaxâŸ©

@[simp] lemma to_oplax_eq_coe : F.to_oplax = F := rfl
@[simp] lemma to_oplax_obj : (F : oplax_functor B C).obj = F.obj := rfl
@[simp] lemma to_oplax_map : (F : oplax_functor B C).map = F.map := rfl
@[simp] lemma to_oplax_mapâ‚‚ : (F : oplax_functor B C).mapâ‚‚ = F.mapâ‚‚ := rfl
@[simp] lemma to_oplax_map_id (a : B) : (F : oplax_functor B C).map_id a = (F.map_id a).hom := rfl
@[simp] lemma to_oplax_map_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (F : oplax_functor B C).map_comp f g = (F.map_comp f g).hom := rfl

variables (B)

/-- The identity pseudofunctor. -/
@[simps]
def id : pseudofunctor B B :=
{ map_id := Î» a, iso.refl (ğŸ™ a),
  map_comp := Î» a b c f g, iso.refl (f â‰« g),
  .. prelax_functor.id B }

instance : inhabited (pseudofunctor B B) := âŸ¨id BâŸ©

variables {B}

/-- Composition of pseudofunctors. -/
@[simps]
def comp : pseudofunctor B D :=
{ map_id := Î» a, G.map_functor.map_iso (F.map_id a) â‰ªâ‰« G.map_id (F.obj a),
  map_comp := Î» a b c f g,
    G.map_functor.map_iso (F.map_comp f g) â‰ªâ‰« G.map_comp (F.map f) (F.map g),
  .. (F : prelax_functor B C).comp â†‘G }

/--
Construct a pseudofunctor from an oplax functor whose `map_id` and `map_comp` are isomorphisms.
-/
def mk_of_oplax {F : oplax_functor B C} (F' : oplax_functor.pseudo_core F) : pseudofunctor B C :=
{ map_id := F'.map_id_iso,
  map_comp := F'.map_comp_iso,
  mapâ‚‚_whisker_right' := Î» a b c f g Î· h, by
  { dsimp,
    rw [F'.map_comp_iso_hom f h, â†F.map_comp_naturality_left_assoc,
      â†F'.map_comp_iso_hom g h, hom_inv_id, comp_id] },
  mapâ‚‚_whisker_left' := Î» a b c f g h Î·, by
  { dsimp,
    rw [F'.map_comp_iso_hom f g, â†F.map_comp_naturality_right_assoc,
      â†F'.map_comp_iso_hom f h, hom_inv_id, comp_id] },
  mapâ‚‚_associator' := Î» a b c d f g h, by
  { dsimp,
    rw [F'.map_comp_iso_hom (f â‰« g) h, F'.map_comp_iso_hom f g, â†F.mapâ‚‚_associator_assoc,
      â†F'.map_comp_iso_hom f (g â‰« h), â†F'.map_comp_iso_hom g h,
      hom_inv_whisker_left_assoc, hom_inv_id, comp_id] },
  .. (F : prelax_functor B C) }

/--
Construct a pseudofunctor from an oplax functor whose `map_id` and `map_comp` are isomorphisms.
-/
noncomputable
def mk_of_oplax' {F : oplax_functor B C} [oplax_functor.is_pseudo F] : pseudofunctor B C :=
{ map_id := Î» a, as_iso (F.map_id a),
  map_comp := Î» a b c f g, as_iso (F.map_comp f g),
  mapâ‚‚_whisker_right' := Î» a b c f g Î· h, by
  { dsimp,
    rw [â†assoc, is_iso.eq_comp_inv, F.map_comp_naturality_left] },
  mapâ‚‚_whisker_left' := Î» a b c f g h Î·, by
  { dsimp,
    rw [â†assoc, is_iso.eq_comp_inv, F.map_comp_naturality_right] },
  mapâ‚‚_associator' := Î» a b c d f g h, by
  { dsimp,
    simp only [â†assoc],
    rw [is_iso.eq_comp_inv, â†inv_whisker_left, is_iso.eq_comp_inv],
    simp only [assoc, F.mapâ‚‚_associator] },
  .. (F : prelax_functor B C) }

end

end pseudofunctor

end

end category_theory
