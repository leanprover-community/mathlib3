/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.basic

/-!
# Oplax functors and pseudofunctors

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

An oplax functor `F` between bicategories `B` and `C` consists of
* a function between objects `F.obj : B ‚ü∂ C`,
* a family of functions between 1-morphisms `F.map : (a ‚ü∂ b) ‚Üí (F.obj a ‚ü∂ F.obj b)`,
* a family of functions between 2-morphisms `F.map‚ÇÇ : (f ‚ü∂ g) ‚Üí (F.map f ‚ü∂ F.map g)`,
* a family of 2-morphisms `F.map_id a : F.map (ùüô a) ‚ü∂ ùüô (F.obj a)`,
* a family of 2-morphisms `F.map_comp f g : F.map (f ‚â´ g) ‚ü∂ F.map f ‚â´ F.map g`, and
* certain consistency conditions on them.

A pseudofunctor is an oplax functor whose `map_id` and `map_comp` are isomorphisms. We provide
several constructors for pseudofunctors:
* `pseudofunctor.mk` : the default constructor, which requires `map‚ÇÇ_whisker_left` and
  `map‚ÇÇ_whisker_right` instead of naturality of `map_comp`.
* `pseudofunctor.mk_of_oplax` : construct a pseudofunctor from an oplax functor whose
  `map_id` and `map_comp` are isomorphisms. This constructor uses `iso` to describe isomorphisms.
* `pseudofunctor.mk_of_oplax'` : similar to `mk_of_oplax`, but uses `is_iso` to describe
  isomorphisms.

The additional constructors are useful when constructing a pseudofunctor where the construction
of the oplax functor associated with it is already done. For example, the composition of
pseudofunctors can be defined by using the composition of oplax functors as follows:
```lean
def pseudofunctor.comp (F : pseudofunctor B C) (G : pseudofunctor C D) : pseudofunctor B D :=
mk_of_oplax ((F : oplax_functor B C).comp G)
{ map_id_iso := Œª a, (G.map_functor _ _).map_iso (F.map_id a) ‚â™‚â´ G.map_id (F.obj a),
  map_comp_iso := Œª a b c f g,
    (G.map_functor _ _).map_iso (F.map_comp f g) ‚â™‚â´ G.map_comp (F.map f) (F.map g) }
```
although the composition of pseudofunctors in this file is defined by using the default constructor
because `obviously` is smart enough. Similarly, the composition is also defined by using
`mk_of_oplax'` after giving appropriate instances for `is_iso`. The former constructor
`mk_of_oplax` requires isomorphisms as data type `iso`, and so it is useful if you don't want
to forget the definitions of the inverses. On the other hand, the latter constructor
`mk_of_oplax'` is useful if you want to use propositional type class `is_iso`.

## Main definitions

* `category_theory.oplax_functor B C` : an oplax functor between bicategories `B` and `C`
* `category_theory.oplax_functor.comp F G` : the composition of oplax functors
* `category_theory.pseudofunctor B C` : a pseudofunctor between bicategories `B` and `C`
* `category_theory.pseudofunctor.comp F G` : the composition of pseudofunctors

## Future work

There are two types of functors between bicategories, called lax and oplax functors, depending on
the directions of `map_id` and `map_comp`. We may need both in mathlib in the future, but for
now we only define oplax functors.
-/

set_option old_structure_cmd true

namespace category_theory

open category bicategory
open_locale bicategory

universes w‚ÇÅ w‚ÇÇ w‚ÇÉ v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

section
variables {B : Type u‚ÇÅ} [quiver.{v‚ÇÅ+1} B] [‚àÄ a b : B, quiver.{w‚ÇÅ+1} (a ‚ü∂ b)]
variables {C : Type u‚ÇÇ} [quiver.{v‚ÇÇ+1} C] [‚àÄ a b : C, quiver.{w‚ÇÇ+1} (a ‚ü∂ b)]
variables {D : Type u‚ÇÉ} [quiver.{v‚ÇÉ+1} D] [‚àÄ a b : D, quiver.{w‚ÇÉ+1} (a ‚ü∂ b)]

/--
A prelax functor between bicategories consists of functions between objects,
1-morphisms, and 2-morphisms. This structure will be extended to define `oplax_functor`.
-/
structure prelax_functor
  (B : Type u‚ÇÅ) [quiver.{v‚ÇÅ+1} B] [‚àÄ a b : B, quiver.{w‚ÇÅ+1} (a ‚ü∂ b)]
  (C : Type u‚ÇÇ) [quiver.{v‚ÇÇ+1} C] [‚àÄ a b : C, quiver.{w‚ÇÇ+1} (a ‚ü∂ b)] extends prefunctor B C :=
(map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} : (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g))

/-- The prefunctor between the underlying quivers. -/
add_decl_doc prelax_functor.to_prefunctor

namespace prelax_functor

instance has_coe_to_prefunctor : has_coe (prelax_functor B C) (prefunctor B C) := ‚ü®to_prefunctor‚ü©

variables (F : prelax_functor B C)

@[simp] lemma to_prefunctor_eq_coe : F.to_prefunctor = F := rfl
@[simp] lemma to_prefunctor_obj : (F : prefunctor B C).obj = F.obj := rfl
@[simp] lemma to_prefunctor_map : @prefunctor.map B _ C _ F = @map _ _ _ _ _ _ F := rfl

/-- The identity prelax functor. -/
@[simps]
def id (B : Type u‚ÇÅ) [quiver.{v‚ÇÅ+1} B] [‚àÄ a b : B, quiver.{w‚ÇÅ+1} (a ‚ü∂ b)] : prelax_functor B B :=
{ map‚ÇÇ := Œª a b f g Œ∑, Œ∑, .. prefunctor.id B }

instance : inhabited (prelax_functor B B) := ‚ü®prelax_functor.id B‚ü©

/-- Composition of prelax functors. -/
@[simps]
def comp (F : prelax_functor B C) (G : prelax_functor C D) : prelax_functor B D :=
{ map‚ÇÇ := Œª a b f g Œ∑, G.map‚ÇÇ (F.map‚ÇÇ Œ∑), .. (F : prefunctor B C).comp ‚ÜëG }

end prelax_functor

end

section
variables {B : Type u‚ÇÅ} [bicategory.{w‚ÇÅ v‚ÇÅ} B] {C : Type u‚ÇÇ} [bicategory.{w‚ÇÇ v‚ÇÇ} C]
variables {D : Type u‚ÇÉ} [bicategory.{w‚ÇÉ v‚ÇÉ} D]

/--
This auxiliary definition states that oplax functors preserve the associators
modulo some adjustments of domains and codomains of 2-morphisms.
-/
/-
We use this auxiliary definition instead of writing it directly in the definition
of oplax functors because doing so will cause a timeout.
-/
@[simp]
def oplax_functor.map‚ÇÇ_associator_aux
  (obj : B ‚Üí C) (map : Œ† {X Y : B}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
  (map‚ÇÇ : Œ† {a b : B} {f g : a ‚ü∂ b}, (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g))
  (map_comp : Œ† {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c), map (f ‚â´ g) ‚ü∂ map f ‚â´ map g)
  {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d) : Prop :=
map‚ÇÇ (Œ±_ f g h).hom ‚â´ map_comp f (g ‚â´ h) ‚â´ map f ‚óÅ map_comp g h =
  map_comp (f ‚â´ g) h ‚â´ map_comp f g ‚ñ∑ map h ‚â´ (Œ±_ (map f) (map g) (map h)).hom

/--
An oplax functor `F` between bicategories `B` and `C` consists of a function between objects
`F.obj`, a function between 1-morphisms `F.map`, and a function between 2-morphisms `F.map‚ÇÇ`.

Unlike functors between categories, `F.map` do not need to strictly commute with the composition,
and do not need to strictly preserve the identity. Instead, there are specified 2-morphisms
`F.map (ùüô a) ‚ü∂ ùüô (F.obj a)` and `F.map (f ‚â´ g) ‚ü∂ F.map f ‚â´ F.map g`.

`F.map‚ÇÇ` strictly commute with compositions and preserve the identity. They also preserve the
associator, the left unitor, and the right unitor modulo some adjustments of domains and codomains
of 2-morphisms.
-/
structure oplax_functor (B : Type u‚ÇÅ) [bicategory.{w‚ÇÅ v‚ÇÅ} B] (C : Type u‚ÇÇ) [bicategory.{w‚ÇÇ v‚ÇÇ} C]
  extends prelax_functor B C :=
(map_id (a : B) : map (ùüô a) ‚ü∂ ùüô (obj a))
(map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : map (f ‚â´ g) ‚ü∂ map f ‚â´ map g)
(map_comp_naturality_left' : ‚àÄ {a b c : B} {f f' : a ‚ü∂ b} (Œ∑ : f ‚ü∂ f') (g : b ‚ü∂ c),
  map‚ÇÇ (Œ∑ ‚ñ∑ g) ‚â´ map_comp f' g = map_comp f g ‚â´ map‚ÇÇ Œ∑ ‚ñ∑ map g . obviously)
(map_comp_naturality_right' : ‚àÄ {a b c : B} (f : a ‚ü∂ b) {g g' : b ‚ü∂ c} (Œ∑ : g ‚ü∂ g'),
  map‚ÇÇ (f ‚óÅ Œ∑) ‚â´ map_comp f g' = map_comp f g ‚â´ map f ‚óÅ map‚ÇÇ Œ∑ . obviously)
(map‚ÇÇ_id' : ‚àÄ {a b : B} (f : a ‚ü∂ b), map‚ÇÇ (ùüô f) = ùüô (map f) . obviously)
(map‚ÇÇ_comp' : ‚àÄ {a b : B} {f g h : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (Œ∏ : g ‚ü∂ h),
  map‚ÇÇ (Œ∑ ‚â´ Œ∏) = map‚ÇÇ Œ∑ ‚â´ map‚ÇÇ Œ∏ . obviously)
(map‚ÇÇ_associator' : ‚àÄ {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d),
  oplax_functor.map‚ÇÇ_associator_aux obj (Œª _ _, map) (Œª a b f g, map‚ÇÇ) (Œª a b c, map_comp) f g h
    . obviously)
(map‚ÇÇ_left_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (Œª_ f).hom = map_comp (ùüô a) f ‚â´ map_id a ‚ñ∑ map f ‚â´ (Œª_ (map f)).hom . obviously)
(map‚ÇÇ_right_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (œÅ_ f).hom = map_comp f (ùüô b) ‚â´ map f ‚óÅ map_id b ‚â´ (œÅ_ (map f)).hom . obviously)

namespace oplax_functor

restate_axiom map_comp_naturality_left'
restate_axiom map_comp_naturality_right'
restate_axiom map‚ÇÇ_id'
restate_axiom map‚ÇÇ_comp'
restate_axiom map‚ÇÇ_associator'
restate_axiom map‚ÇÇ_left_unitor'
restate_axiom map‚ÇÇ_right_unitor'
attribute [simp] map_comp_naturality_left map_comp_naturality_right map‚ÇÇ_id map‚ÇÇ_associator
attribute [reassoc]
  map_comp_naturality_left map_comp_naturality_right map‚ÇÇ_comp
  map‚ÇÇ_associator map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor
attribute [simp] map‚ÇÇ_comp map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor

section

/-- The prelax functor between the underlying quivers. -/
add_decl_doc oplax_functor.to_prelax_functor

instance has_coe_to_prelax : has_coe (oplax_functor B C) (prelax_functor B C) :=
‚ü®to_prelax_functor‚ü©

variables (F : oplax_functor B C)

@[simp] lemma to_prelax_eq_coe : F.to_prelax_functor = F := rfl
@[simp] lemma to_prelax_functor_obj : (F : prelax_functor B C).obj = F.obj := rfl
@[simp] lemma to_prelax_functor_map : @prelax_functor.map B _ _ C _ _ F = @map _ _ _ _ F := rfl
@[simp] lemma to_prelax_functor_map‚ÇÇ : @prelax_functor.map‚ÇÇ B _ _ C _ _ F = @map‚ÇÇ _ _ _ _ F := rfl

/-- Function between 1-morphisms as a functor. -/
@[simps]
def map_functor (a b : B) : (a ‚ü∂ b) ‚•§ (F.obj a ‚ü∂ F.obj b) :=
{ obj := Œª f, F.map f,
  map := Œª f g Œ∑, F.map‚ÇÇ Œ∑ }

/-- The identity oplax functor. -/
@[simps]
def id (B : Type u‚ÇÅ) [bicategory.{w‚ÇÅ v‚ÇÅ} B] : oplax_functor B B :=
{ map_id := Œª a, ùüô (ùüô a),
  map_comp := Œª a b c f g, ùüô (f ‚â´ g),
  .. prelax_functor.id B }

instance : inhabited (oplax_functor B B) := ‚ü®id B‚ü©

/-- Composition of oplax functors. -/
@[simps]
def comp (F : oplax_functor B C) (G : oplax_functor C D) : oplax_functor B D :=
{ map_id := Œª a,
    (G.map_functor _ _).map (F.map_id a) ‚â´ G.map_id (F.obj a),
  map_comp := Œª a b c f g,
    (G.map_functor _ _).map (F.map_comp f g) ‚â´ G.map_comp (F.map f) (F.map g),
  map_comp_naturality_left' := Œª a b c f f' Œ∑ g, by
  { dsimp,
    rw [‚Üêmap‚ÇÇ_comp_assoc, map_comp_naturality_left, map‚ÇÇ_comp_assoc, map_comp_naturality_left,
      assoc] },
  map_comp_naturality_right' := Œª a b c f g g' Œ∑, by
  { dsimp,
    rw [‚Üêmap‚ÇÇ_comp_assoc, map_comp_naturality_right, map‚ÇÇ_comp_assoc, map_comp_naturality_right,
      assoc] },
  map‚ÇÇ_associator' := Œª a b c d f g h, by
  { dsimp,
    simp only [map‚ÇÇ_associator, ‚Üêmap‚ÇÇ_comp_assoc, ‚Üêmap_comp_naturality_right_assoc,
      whisker_left_comp, assoc],
    simp only [map‚ÇÇ_associator, map‚ÇÇ_comp, map_comp_naturality_left_assoc,
      comp_whisker_right, assoc] },
  map‚ÇÇ_left_unitor' := Œª a b f, by
  { dsimp,
    simp only [map‚ÇÇ_left_unitor, map‚ÇÇ_comp, map_comp_naturality_left_assoc,
      comp_whisker_right, assoc] },
  map‚ÇÇ_right_unitor' := Œª a b f, by
  { dsimp,
    simp only [map‚ÇÇ_right_unitor, map‚ÇÇ_comp, map_comp_naturality_right_assoc,
      whisker_left_comp, assoc] },
  .. (F : prelax_functor B C).comp ‚ÜëG }

/--
A structure on an oplax functor that promotes an oplax functor to a pseudofunctor.
See `pseudofunctor.mk_of_oplax`.
-/
@[nolint has_nonempty_instance]
structure pseudo_core (F : oplax_functor B C) :=
(map_id_iso (a : B) : F.map (ùüô a) ‚âÖ ùüô (F.obj a))
(map_comp_iso {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : F.map (f ‚â´ g) ‚âÖ F.map f ‚â´ F.map g)
(map_id_iso_hom' : ‚àÄ {a : B}, (map_id_iso a).hom = F.map_id a . obviously)
(map_comp_iso_hom' : ‚àÄ {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c),
  (map_comp_iso f g).hom = F.map_comp f g . obviously)

restate_axiom pseudo_core.map_id_iso_hom'
restate_axiom pseudo_core.map_comp_iso_hom'
attribute [simp] pseudo_core.map_id_iso_hom pseudo_core.map_comp_iso_hom

end

end oplax_functor

/--
This auxiliary definition states that pseudofunctors preserve the associators
modulo some adjustments of domains and codomains of 2-morphisms.
-/
/-
We use this auxiliary definition instead of writing it directly in the definition
of pseudofunctors because doing so will cause a timeout.
-/
@[simp]
def pseudofunctor.map‚ÇÇ_associator_aux
  (obj : B ‚Üí C) (map : Œ† {X Y : B}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
  (map‚ÇÇ : Œ† {a b : B} {f g : a ‚ü∂ b}, (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g))
  (map_comp : Œ† {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c), map (f ‚â´ g) ‚âÖ map f ‚â´ map g)
  {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d) : Prop :=
map‚ÇÇ (Œ±_ f g h).hom = (map_comp (f ‚â´ g) h).hom ‚â´ (map_comp f g).hom ‚ñ∑ map h ‚â´
  (Œ±_ (map f) (map g) (map h)).hom ‚â´ map f ‚óÅ (map_comp g h).inv ‚â´ (map_comp f (g ‚â´ h)).inv

/--
A pseudofunctor `F` between bicategories `B` and `C` consists of a function between objects
`F.obj`, a function between 1-morphisms `F.map`, and a function between 2-morphisms `F.map‚ÇÇ`.

Unlike functors between categories, `F.map` do not need to strictly commute with the compositions,
and do not need to strictly preserve the identity. Instead, there are specified 2-isomorphisms
`F.map (ùüô a) ‚âÖ ùüô (F.obj a)` and `F.map (f ‚â´ g) ‚âÖ F.map f ‚â´ F.map g`.

`F.map‚ÇÇ` strictly commute with compositions and preserve the identity. They also preserve the
associator, the left unitor, and the right unitor modulo some adjustments of domains and codomains
of 2-morphisms.
-/
structure pseudofunctor (B : Type u‚ÇÅ) [bicategory.{w‚ÇÅ v‚ÇÅ} B] (C : Type u‚ÇÇ) [bicategory.{w‚ÇÇ v‚ÇÇ} C]
  extends prelax_functor B C :=
(map_id (a : B) : map (ùüô a) ‚âÖ ùüô (obj a))
(map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : map (f ‚â´ g) ‚âÖ map f ‚â´ map g)
(map‚ÇÇ_id' : ‚àÄ {a b : B} (f : a ‚ü∂ b), map‚ÇÇ (ùüô f) = ùüô (map f) . obviously)
(map‚ÇÇ_comp' : ‚àÄ {a b : B} {f g h : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (Œ∏ : g ‚ü∂ h),
  map‚ÇÇ (Œ∑ ‚â´ Œ∏) = map‚ÇÇ Œ∑ ‚â´ map‚ÇÇ Œ∏ . obviously)
(map‚ÇÇ_whisker_left' : ‚àÄ {a b c : B} (f : a ‚ü∂ b) {g h : b ‚ü∂ c} (Œ∑ : g ‚ü∂ h),
  map‚ÇÇ (f ‚óÅ Œ∑) = (map_comp f g).hom ‚â´ map f ‚óÅ map‚ÇÇ Œ∑ ‚â´ (map_comp f h).inv . obviously)
(map‚ÇÇ_whisker_right' : ‚àÄ {a b c : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (h : b ‚ü∂ c),
  map‚ÇÇ (Œ∑ ‚ñ∑ h) = (map_comp f h).hom ‚â´ map‚ÇÇ Œ∑ ‚ñ∑ map h ‚â´ (map_comp g h).inv . obviously)
(map‚ÇÇ_associator' : ‚àÄ {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d),
  pseudofunctor.map‚ÇÇ_associator_aux obj (Œª a b, map) (Œª a b f g, map‚ÇÇ) (Œª a b c, map_comp) f g h
    . obviously)
(map‚ÇÇ_left_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (Œª_ f).hom = (map_comp (ùüô a) f).hom ‚â´ (map_id a).hom ‚ñ∑ map f ‚â´ (Œª_ (map f)).hom
    . obviously)
(map‚ÇÇ_right_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (œÅ_ f).hom = (map_comp f (ùüô b)).hom ‚â´ map f ‚óÅ (map_id b).hom ‚â´ (œÅ_ (map f)).hom
    . obviously)

namespace pseudofunctor

restate_axiom map‚ÇÇ_id'
restate_axiom map‚ÇÇ_comp'
restate_axiom map‚ÇÇ_whisker_left'
restate_axiom map‚ÇÇ_whisker_right'
restate_axiom map‚ÇÇ_associator'
restate_axiom map‚ÇÇ_left_unitor'
restate_axiom map‚ÇÇ_right_unitor'
attribute [reassoc]
  map‚ÇÇ_comp map‚ÇÇ_whisker_left map‚ÇÇ_whisker_right map‚ÇÇ_associator map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor
attribute [simp]
  map‚ÇÇ_id map‚ÇÇ_comp map‚ÇÇ_whisker_left map‚ÇÇ_whisker_right
  map‚ÇÇ_associator map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor

section
open iso

/-- The prelax functor between the underlying quivers. -/
add_decl_doc pseudofunctor.to_prelax_functor

instance has_coe_to_prelax_functor : has_coe (pseudofunctor B C) (prelax_functor B C) :=
‚ü®to_prelax_functor‚ü©

variables (F : pseudofunctor B C)

@[simp] lemma to_prelax_functor_eq_coe : F.to_prelax_functor = F := rfl
@[simp] lemma to_prelax_functor_obj : (F : prelax_functor B C).obj = F.obj := rfl
@[simp] lemma to_prelax_functor_map : @prelax_functor.map B _ _ C _ _ F = @map _ _ _ _ F := rfl
@[simp] lemma to_prelax_functor_map‚ÇÇ : @prelax_functor.map‚ÇÇ B _ _ C _ _ F = @map‚ÇÇ _ _ _ _ F := rfl

/-- The oplax functor associated with a pseudofunctor. -/
def to_oplax : oplax_functor B C :=
{ map_id := Œª a, (F.map_id a).hom,
  map_comp := Œª a b c f g, (F.map_comp f g).hom,
  .. (F : prelax_functor B C) }

instance has_coe_to_oplax : has_coe (pseudofunctor B C) (oplax_functor B C) := ‚ü®to_oplax‚ü©

@[simp] lemma to_oplax_eq_coe : F.to_oplax = F := rfl
@[simp] lemma to_oplax_obj : (F : oplax_functor B C).obj = F.obj := rfl
@[simp] lemma to_oplax_map : @oplax_functor.map B _ C _ F = @map _ _ _ _ F := rfl
@[simp] lemma to_oplax_map‚ÇÇ : @oplax_functor.map‚ÇÇ B _ C _ F = @map‚ÇÇ _ _ _ _ F := rfl
@[simp] lemma to_oplax_map_id (a : B) : (F : oplax_functor B C).map_id a = (F.map_id a).hom := rfl
@[simp] lemma to_oplax_map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (F : oplax_functor B C).map_comp f g = (F.map_comp f g).hom := rfl

/-- Function on 1-morphisms as a functor. -/
@[simps]
def map_functor (a b : B) : (a ‚ü∂ b) ‚•§ (F.obj a ‚ü∂ F.obj b) :=
(F : oplax_functor B C).map_functor a b

/-- The identity pseudofunctor. -/
@[simps]
def id (B : Type u‚ÇÅ) [bicategory.{w‚ÇÅ v‚ÇÅ} B] : pseudofunctor B B :=
{ map_id := Œª a, iso.refl (ùüô a),
  map_comp := Œª a b c f g, iso.refl (f ‚â´ g),
  .. prelax_functor.id B }

instance : inhabited (pseudofunctor B B) := ‚ü®id B‚ü©

/-- Composition of pseudofunctors. -/
@[simps]
def comp (F : pseudofunctor B C) (G : pseudofunctor C D) : pseudofunctor B D :=
{ map_id := Œª a, (G.map_functor _ _).map_iso (F.map_id a) ‚â™‚â´ G.map_id (F.obj a),
  map_comp := Œª a b c f g,
    (G.map_functor _ _).map_iso (F.map_comp f g) ‚â™‚â´ G.map_comp (F.map f) (F.map g),
  .. (F : prelax_functor B C).comp ‚ÜëG }

/--
Construct a pseudofunctor from an oplax functor whose `map_id` and `map_comp` are isomorphisms.
-/
@[simps]
def mk_of_oplax (F : oplax_functor B C) (F' : F.pseudo_core) : pseudofunctor B C :=
{ map_id := F'.map_id_iso,
  map_comp := Œª _ _ _, F'.map_comp_iso,
  map‚ÇÇ_whisker_left' := Œª a b c f g h Œ∑, by
  { dsimp,
    rw [F'.map_comp_iso_hom f g, ‚ÜêF.map_comp_naturality_right_assoc,
      ‚ÜêF'.map_comp_iso_hom f h, hom_inv_id, comp_id] },
  map‚ÇÇ_whisker_right' := Œª a b c f g Œ∑ h, by
  { dsimp,
    rw [F'.map_comp_iso_hom f h, ‚ÜêF.map_comp_naturality_left_assoc,
      ‚ÜêF'.map_comp_iso_hom g h, hom_inv_id, comp_id] },
  map‚ÇÇ_associator' := Œª a b c d f g h, by
  { dsimp,
    rw [F'.map_comp_iso_hom (f ‚â´ g) h, F'.map_comp_iso_hom f g, ‚ÜêF.map‚ÇÇ_associator_assoc,
      ‚ÜêF'.map_comp_iso_hom f (g ‚â´ h), ‚ÜêF'.map_comp_iso_hom g h,
      hom_inv_whisker_left_assoc, hom_inv_id, comp_id] },
  .. (F : prelax_functor B C) }

/--
Construct a pseudofunctor from an oplax functor whose `map_id` and `map_comp` are isomorphisms.
-/
@[simps]
noncomputable
def mk_of_oplax' (F : oplax_functor B C)
  [‚àÄ a, is_iso (F.map_id a)] [‚àÄ {a b c} (f : a ‚ü∂ b) (g : b ‚ü∂ c), is_iso (F.map_comp f g)] :
  pseudofunctor B C :=
{ map_id := Œª a, as_iso (F.map_id a),
  map_comp := Œª a b c f g, as_iso (F.map_comp f g),
  map‚ÇÇ_whisker_left' := Œª a b c f g h Œ∑, by
  { dsimp,
    rw [‚Üêassoc, is_iso.eq_comp_inv, F.map_comp_naturality_right] },
  map‚ÇÇ_whisker_right' := Œª a b c f g Œ∑ h, by
  { dsimp,
    rw [‚Üêassoc, is_iso.eq_comp_inv, F.map_comp_naturality_left] },
  map‚ÇÇ_associator' := Œª a b c d f g h, by
  { dsimp,
    simp only [‚Üêassoc],
    rw [is_iso.eq_comp_inv, ‚Üêinv_whisker_left, is_iso.eq_comp_inv],
    simp only [assoc, F.map‚ÇÇ_associator] },
  .. (F : prelax_functor B C) }

end

end pseudofunctor

end

end category_theory
