/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.basic

/-!
# Pseudofunctors

A pseudofunctor `F` between bicategories `B` and `C` consists of
* a function between objects `F.obj : B âŸ¶ C`,
* a family of functions between 1-morphisms `F.map : (a âŸ¶ b) â†’ (obj a âŸ¶ obj b)`,
* a family of functions between 2-morphisms `F.mapâ‚‚ : (f âŸ¶ g) â†’ (map f âŸ¶ map g)`,
* a family of isomorphisms `F.map_id a : ğŸ™ (obj a) â‰… map (ğŸ™ a)`,
* a family of isomorphisms `F.map_comp f g : map f â‰« map g â‰… map (f â‰« g)`, and
* certain consistency conditions on them.

The direction of isomorphisms `map_comp` and `map_id` here is the lax direction.

## TODO

* Lax and oplax functors.
-/

set_option old_structure_cmd true

namespace category_theory

open category bicategory
open_locale bicategory

universes wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

section

/--
A prepseudofunctor between bicategories consists of functions between objects,
1-morphisms, and 2-morphisms. This structure will be extended to define `pseudofunctor`.
-/
structure prepseudofunctor
  (B : Type uâ‚) [quiver.{vâ‚+1} B] [âˆ€ a b : B, quiver.{wâ‚+1} (a âŸ¶ b)]
  (C : Type uâ‚‚) [quiver.{vâ‚‚+1} C] [âˆ€ a b : C, quiver.{wâ‚‚+1} (a âŸ¶ b)]
  extends prefunctor B C : Type (max wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚) :=
(mapâ‚‚ {a b : B} {f g : a âŸ¶ b} : (f âŸ¶ g) â†’ (map f âŸ¶ map g))

/-- The prefunctor between the underlying quivers. -/
add_decl_doc prepseudofunctor.to_prefunctor

variables {B : Type uâ‚} [quiver.{vâ‚+1} B] [âˆ€ a b : B, quiver.{wâ‚+1} (a âŸ¶ b)]
variables {C : Type uâ‚‚} [quiver.{vâ‚‚+1} C] [âˆ€ a b : C, quiver.{wâ‚‚+1} (a âŸ¶ b)]
variables (F : prepseudofunctor B C)

@[simp] lemma prepseudofunctor.to_prefunctor_obj : F.to_prefunctor.obj = F.obj := rfl
@[simp] lemma prepseudofunctor.to_prefunctor_map : F.to_prefunctor.map = F.map := rfl

end

namespace prepseudofunctor

section
variables (B : Type uâ‚) [quiver.{vâ‚+1} B] [âˆ€ a b : B, quiver.{wâ‚+1} (a âŸ¶ b)]

/-- The identity prepseudofunctor. -/
@[simps]
def id : prepseudofunctor B B :=
{ mapâ‚‚ := Î» a b f g Î·, Î·, .. prefunctor.id B }

instance : inhabited (prepseudofunctor B B) := âŸ¨prepseudofunctor.id BâŸ©

end

section
variables {B : Type uâ‚} [quiver.{vâ‚+1} B] [âˆ€ a b : B, quiver.{wâ‚+1} (a âŸ¶ b)]
variables {C : Type uâ‚‚} [quiver.{vâ‚‚+1} C] [âˆ€ a b : C, quiver.{wâ‚‚+1} (a âŸ¶ b)]
variables {D : Type uâ‚ƒ} [quiver.{vâ‚ƒ+1} D] [âˆ€ a b : D, quiver.{wâ‚ƒ+1} (a âŸ¶ b)]
variables (F : prepseudofunctor B C) (G : prepseudofunctor C D)

/-- Composition of prepseudofunctors. -/
@[simps]
def comp : prepseudofunctor B D :=
{ mapâ‚‚ := Î» a b f g Î·, G.mapâ‚‚ (F.mapâ‚‚ Î·), .. F.to_prefunctor.comp G.to_prefunctor }

end

end prepseudofunctor

section

/--
A pseudofunctor `F` between bicategories `B` and `C` consists of functions between objects,
1-morphisms, and 2-morphisms.

Unlike functors between categories, functions between 1-morphisms do not need to strictly commute
with compositions, and do not need to strictly preserve the identity. Instead, there are
specified isomorphisms `ğŸ™ (F.obj a) â‰… F.map (ğŸ™ a)` and `F.map f â‰« F.map g â‰… F.map (f â‰« g)`.

Functions between 2-morphisms strictly commute with compositions and preserve the identity.
They also preserve the associator, the left unitor, and the right unitor modulo some adjustments
of domains and codomains of 2-morphisms.
-/
structure pseudofunctor (B : Type uâ‚) [bicategory.{wâ‚ vâ‚} B] (C : Type uâ‚‚) [bicategory.{wâ‚‚ vâ‚‚} C]
  extends prepseudofunctor B C : Type (max wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚) :=
(map_id (a : B) : ğŸ™ (obj a) â‰… map (ğŸ™ a))
(map_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : map f â‰« map g â‰… map (f â‰« g))
(map_comp_naturality_left' : âˆ€ {a b c : B} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c),
  (mapâ‚‚ Î· â–· map g) â‰« (map_comp f' g).hom = (map_comp f g).hom â‰« mapâ‚‚ (Î· â–· g) . obviously)
(map_comp_naturality_right' : âˆ€ {a b c : B} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g'),
  (map f â— mapâ‚‚ Î·) â‰« (map_comp f g').hom = (map_comp f g).hom â‰« mapâ‚‚ (f â— Î·) . obviously)
(mapâ‚‚_id' : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) . obviously)
(mapâ‚‚_comp' : âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
  mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ . obviously)
(mapâ‚‚_associator' : âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
  ((map_comp f g).hom â–· map h) â‰« (map_comp (f â‰« g) h).hom â‰« mapâ‚‚ (Î±_ f g h).hom =
    (Î±_ (map f) (map g) (map h)).hom â‰« (map f â— (map_comp g h).hom) â‰«
      (map_comp f (g â‰« h)).hom . obviously)
(mapâ‚‚_left_unitor' : âˆ€ {a b : B} (f : a âŸ¶ b),
  ((map_id a).hom â–· map f) â‰« (map_comp (ğŸ™ a) f).hom â‰« mapâ‚‚ (Î»_ f).hom =
    (Î»_ (map f)).hom . obviously)
(mapâ‚‚_right_unitor' : âˆ€ {a b : B} (f : a âŸ¶ b),
  (map f â— (map_id b).hom) â‰« (map_comp f (ğŸ™ b)).hom â‰« mapâ‚‚ (Ï_ f).hom =
    (Ï_ (map f)).hom . obviously)

set_option trace.class_instances false

restate_axiom pseudofunctor.map_comp_naturality_left'
restate_axiom pseudofunctor.map_comp_naturality_right'
restate_axiom pseudofunctor.mapâ‚‚_id'
restate_axiom pseudofunctor.mapâ‚‚_comp'
restate_axiom pseudofunctor.mapâ‚‚_associator'
restate_axiom pseudofunctor.mapâ‚‚_left_unitor'
restate_axiom pseudofunctor.mapâ‚‚_right_unitor'
attribute [simp]
  pseudofunctor.map_comp_naturality_left pseudofunctor.map_comp_naturality_right
  pseudofunctor.mapâ‚‚_id pseudofunctor.mapâ‚‚_associator
  pseudofunctor.mapâ‚‚_left_unitor pseudofunctor.mapâ‚‚_right_unitor
attribute [reassoc]
  pseudofunctor.map_comp_naturality_left pseudofunctor.map_comp_naturality_right
  pseudofunctor.mapâ‚‚_comp pseudofunctor.mapâ‚‚_associator
  pseudofunctor.mapâ‚‚_left_unitor pseudofunctor.mapâ‚‚_right_unitor
attribute [simp]
  pseudofunctor.mapâ‚‚_comp

variables {B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B] {C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]
variables (F : pseudofunctor B C)

/-- Function on 1-morphisms as a functor. -/
@[simps]
def pseudofunctor.map_functor (a b : B) : (a âŸ¶ b) â¥¤ (F.obj a âŸ¶ F.obj b) :=
{ obj := Î» f, F.map f,
  map := Î» f g Î·, F.mapâ‚‚ Î· }

/-- The prepseudofunctor between the underlying quivers. -/
add_decl_doc pseudofunctor.to_prepseudofunctor

@[simp] lemma pseudofunctor.to_prepseudofunctor_obj : F.to_prepseudofunctor.obj = F.obj := rfl
@[simp] lemma pseudofunctor.to_prepseudofunctor_map : F.to_prepseudofunctor.map = F.map := rfl
@[simp] lemma pseudofunctor.to_prepseudofunctor_mapâ‚‚ : F.to_prepseudofunctor.mapâ‚‚ = F.mapâ‚‚ := rfl

end

namespace pseudofunctor

section
variables (B : Type uâ‚) [bicategory.{wâ‚ vâ‚} B]

/-- The identity pseudofunctor. -/
@[simps]
def id : pseudofunctor B B :=
{ map_id := Î» a, iso.refl (ğŸ™ a),
  map_comp := Î» a b c f g, iso.refl (f â‰« g),
  .. prepseudofunctor.id B }

instance : inhabited (pseudofunctor B B) := âŸ¨id BâŸ©

end

section
variables {B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B]
variables {C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]
variables {D : Type uâ‚ƒ} [bicategory.{wâ‚ƒ vâ‚ƒ} D]
variables (F : pseudofunctor B C) (G : pseudofunctor C D)

/-- Composition of pseudofunctors. -/
@[simps]
def comp : pseudofunctor B D :=
{ map_id := Î» a,
    G.map_id (F.obj a) â‰ªâ‰« (G.map_functor _ _).map_iso (F.map_id a),
  map_comp := Î» a b c f g,
    G.map_comp (F.map f) (F.map g) â‰ªâ‰« (G.map_functor _ _).map_iso (F.map_comp f g),
  map_comp_naturality_left' := Î» a b c f f' Î· g, by
  { dsimp,
    rw [map_comp_naturality_left_assoc, â†mapâ‚‚_comp, map_comp_naturality_left],
    simp only [mapâ‚‚_comp, assoc] },
  map_comp_naturality_right' := Î» a b c f g g' Î·, by
  { dsimp,
    rw [map_comp_naturality_right_assoc, â†mapâ‚‚_comp, map_comp_naturality_right],
    simp only [mapâ‚‚_comp, assoc] },
  mapâ‚‚_associator' := Î» a b c d f g h, by
  { dsimp, simp only [whisker_right_comp, whisker_left_comp, assoc],
    rw [map_comp_naturality_left_assoc, map_comp_naturality_right_assoc, â†mapâ‚‚_associator_assoc],
    simp only [â†mapâ‚‚_comp],
    rw â†mapâ‚‚_associator },
  mapâ‚‚_left_unitor' := Î» a b f, by
  { dsimp, simp only [whisker_right_comp, assoc],
    rw [map_comp_naturality_left_assoc, â†mapâ‚‚_left_unitor],
    simp only [â†mapâ‚‚_comp],
    rw â†mapâ‚‚_left_unitor },
  mapâ‚‚_right_unitor' := Î» a b f, by
  { dsimp, simp only [whisker_left_comp, assoc],
    rw [map_comp_naturality_right_assoc, â†mapâ‚‚_right_unitor],
    simp only [â†mapâ‚‚_comp],
    rw â†mapâ‚‚_right_unitor },
  .. F.to_prepseudofunctor.comp G.to_prepseudofunctor }

end

end pseudofunctor

end category_theory
