/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.basic

/-!
# Oplax functors and pseudofunctors

An oplax functor `F` between bicategories `B` and `C` consists of
* a function between objects `F.obj : B ‚ü∂ C`,
* a family of functions between 1-morphisms `F.map : (a ‚ü∂ b) ‚Üí (obj a ‚ü∂ obj b)`,
* a family of functions between 2-morphisms `F.map‚ÇÇ : (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g)`,
* a family of 2-morphisms `F.map_id a : F.map (ùüô a) ‚ü∂ ùüô (F.obj a)`,
* a family of 2-morphisms `F.map_comp f g : F.map (f ‚â´ g) ‚ü∂ F.map f ‚â´ F.map g`, and
* certain consistency conditions on them.

A pseudofunctor is an oplax functor whose `map_id` and `map_comp` are isomorphisms. We provide
several constructors for pseudofunctors:
The former uses `iso` for the same type, and the latter uses` is_iso` for the same type.
* `pseudofunctor.mk` : the default constructor, which requires `map‚ÇÇ_whisker_left` and
  `map‚ÇÇ_whisker_right` instead of naturality of `map_comp`.
* `pseudofunctor.mk_of_oplax` : construct a pseudofunctor from an oplax functor whose
  `map_id` and `map_comp` are isomorphisms. This constructor uses `iso` to describe isomorphisms.
* `pseudofunctor.mk_of_oplax'` : similar to `mk_of_oplax`, but uses` is_iso` to describe
  isomorphisms.

## Main definitions

* `oplax_functor B C` : an oplax functor between bicategories `B` and `C`
* `oplax_functor.comp F G` : the composition of oplax functors
* `pseudofunctor B C` : a pseudofunctor between bicategories `B` and `C`
* `pseudofunctor.comp F G` : the composition of pseudofunctors

## Future work

There are two types of functors between bicategories, called lax and oplax functors, depending on
the directions of `map_id` and `map_comp`. We may need both in mathlib in the future, but for
now we only define oplax functors.
-/

set_option old_structure_cmd true

namespace category_theory

open category bicategory
open_locale bicategory

universes w‚ÇÅ w‚ÇÇ w‚ÇÉ v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

section
variables (B : Type u‚ÇÅ) [quiver.{v‚ÇÅ+1} B] [‚àÄ a b : B, quiver.{w‚ÇÅ+1} (a ‚ü∂ b)]
variables (C : Type u‚ÇÇ) [quiver.{v‚ÇÇ+1} C] [‚àÄ a b : C, quiver.{w‚ÇÇ+1} (a ‚ü∂ b)]

/--
A prelax functor between bicategories consists of functions between objects,
1-morphisms, and 2-morphisms. This structure will be extended to define `oplax_functor`.
-/
structure prelax_functor extends prefunctor B C :=
(map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} : (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g))

/-- The prefunctor between the underlying quivers. -/
add_decl_doc prelax_functor.to_prefunctor

namespace prelax_functor

variables {B C} {D : Type u‚ÇÉ} [quiver.{v‚ÇÉ+1} D] [‚àÄ a b : D, quiver.{w‚ÇÉ+1} (a ‚ü∂ b)]
variables (F : prelax_functor B C) (G : prelax_functor C D)

instance has_coe_to_prefunctor : has_coe (prelax_functor B C) (prefunctor B C) := ‚ü®to_prefunctor‚ü©

@[simp] lemma to_prefunctor_eq_coe : F.to_prefunctor = F := rfl
@[simp] lemma to_prefunctor_obj : (F : prefunctor B C).obj = F.obj := rfl
@[simp] lemma to_prefunctor_map : (F : prefunctor B C).map = F.map := rfl

variables (B)

/-- The identity prelax functor. -/
def id : prelax_functor B B :=
{ map‚ÇÇ := Œª a b f g Œ∑, Œ∑, .. prefunctor.id B }

@[simp] lemma id_obj (a : B) : (id B).obj a = a := rfl
@[simp] lemma id_map {a b : B} (f : a ‚ü∂ b) : (id B).map f = f := rfl
@[simp] lemma id_map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) : (id B).map‚ÇÇ Œ∑ = Œ∑ := rfl

instance : inhabited (prelax_functor B B) := ‚ü®prelax_functor.id B‚ü©

variables {B}

/-- Composition of prelax functors. -/
def comp : prelax_functor B D :=
{ map‚ÇÇ := Œª a b f g Œ∑, G.map‚ÇÇ (F.map‚ÇÇ Œ∑), .. (F : prefunctor B C).comp ‚ÜëG }

@[simp] lemma comp_obj (a : B) : (F.comp G).obj a = G.obj (F.obj a) := rfl
@[simp] lemma comp_map {a b : B} (f : a ‚ü∂ b) : (F.comp G).map f = G.map (F.map f) := rfl
@[simp] lemma comp_map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) :
  (F.comp G).map‚ÇÇ Œ∑ = G.map‚ÇÇ (F.map‚ÇÇ Œ∑) := rfl

end prelax_functor

end

section
variables {B : Type u‚ÇÅ} [bicategory.{w‚ÇÅ v‚ÇÅ} B] {C : Type u‚ÇÇ} [bicategory.{w‚ÇÇ v‚ÇÇ} C]
variables {D : Type u‚ÇÉ} [bicategory.{w‚ÇÉ v‚ÇÉ} D]

/--
This auxiliary definition states that oplax functors preserve the associators
modulo some adjustments of domains and codomains of 2-morphisms.
-/
/-
We use this auxiliary definition instead of writing it directly in the definition
of oplax functors because doing so will cause a timeout.
-/
@[simp]
def oplax_functor.map‚ÇÇ_associator_aux
  (obj : B ‚Üí C) (map : Œ† {X Y : B}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
  (map‚ÇÇ : Œ† {a b : B} {f g : a ‚ü∂ b}, (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g))
  (map_comp : Œ† {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c), map (f ‚â´ g) ‚ü∂ map f ‚â´ map g)
  {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d) : Prop :=
map‚ÇÇ (Œ±_ f g h).hom ‚â´ map_comp f (g ‚â´ h) ‚â´ (map f ‚óÅ map_comp g h) =
  map_comp (f ‚â´ g) h ‚â´ (map_comp f g ‚ñ∑ map h) ‚â´ (Œ±_ (map f) (map g) (map h)).hom

variables (B C)

/--
An oplax functor `F` between bicategories `B` and `C` consists of functions between objects,
1-morphisms, and 2-morphisms.

Unlike functors between categories, functions between 1-morphisms do not need to strictly commute
with compositions, and do not need to strictly preserve the identity. Instead, there are
specified 2-morphisms `F.map (ùüô a) ‚ü∂ ùüô (F.obj a)` and `F.map (f ‚â´ g) ‚ü∂ F.map f ‚â´ F.map g`.

Functions between 2-morphisms strictly commute with compositions and preserve the identity.
They also preserve the associator, the left unitor, and the right unitor modulo some adjustments
of domains and codomains of 2-morphisms.
-/
structure oplax_functor extends prelax_functor B C :=
(map_id (a : B) : map (ùüô a) ‚ü∂ ùüô (obj a))
(map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : map (f ‚â´ g) ‚ü∂ map f ‚â´ map g)
(map_comp_naturality_left' : ‚àÄ {a b c : B} {f f' : a ‚ü∂ b} (Œ∑ : f ‚ü∂ f') (g : b ‚ü∂ c),
  map‚ÇÇ (Œ∑ ‚ñ∑ g) ‚â´ map_comp f' g = map_comp f g ‚â´ (map‚ÇÇ Œ∑ ‚ñ∑ map g) . obviously)
(map_comp_naturality_right' : ‚àÄ {a b c : B} (f : a ‚ü∂ b) {g g' : b ‚ü∂ c} (Œ∑ : g ‚ü∂ g'),
  map‚ÇÇ (f ‚óÅ Œ∑) ‚â´ map_comp f g' = map_comp f g ‚â´ (map f ‚óÅ map‚ÇÇ Œ∑) . obviously)
(map‚ÇÇ_id' : ‚àÄ {a b : B} (f : a ‚ü∂ b), map‚ÇÇ (ùüô f) = ùüô (map f) . obviously)
(map‚ÇÇ_comp' : ‚àÄ {a b : B} {f g h : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (Œ∏ : g ‚ü∂ h),
  map‚ÇÇ (Œ∑ ‚â´ Œ∏) = map‚ÇÇ Œ∑ ‚â´ map‚ÇÇ Œ∏ . obviously)
(map‚ÇÇ_associator' : ‚àÄ {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d),
  oplax_functor.map‚ÇÇ_associator_aux obj (Œª a b, map) (Œª a b f g, map‚ÇÇ) (Œª a b c, map_comp) f g h
    . obviously)
(map‚ÇÇ_left_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (Œª_ f).hom = map_comp (ùüô a) f ‚â´ (map_id a ‚ñ∑ map f) ‚â´ (Œª_ (map f)).hom . obviously)
(map‚ÇÇ_right_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (œÅ_ f).hom = map_comp f (ùüô b) ‚â´ (map f ‚óÅ map_id b) ‚â´ (œÅ_ (map f)).hom . obviously)

variables {B C}

namespace oplax_functor

restate_axiom map_comp_naturality_left'
restate_axiom map_comp_naturality_right'
restate_axiom map‚ÇÇ_id'
restate_axiom map‚ÇÇ_comp'
restate_axiom map‚ÇÇ_associator'
restate_axiom map‚ÇÇ_left_unitor'
restate_axiom map‚ÇÇ_right_unitor'
attribute [simp] map_comp_naturality_left map_comp_naturality_right map‚ÇÇ_id map‚ÇÇ_associator
attribute [reassoc]
  map_comp_naturality_left map_comp_naturality_right map‚ÇÇ_comp
  map‚ÇÇ_associator map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor
attribute [simp] map‚ÇÇ_comp map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor

section
variables (F : oplax_functor B C) (G : oplax_functor C D)

/-- The prelax functor between the underlying quivers. -/
add_decl_doc oplax_functor.to_prelax_functor

instance has_coe_to_prelax : has_coe (oplax_functor B C) (prelax_functor B C) :=
‚ü®to_prelax_functor‚ü©

@[simp] lemma to_prelax_eq_coe : F.to_prelax_functor = F := rfl
@[simp] lemma to_prelax_functor_obj : (F : prelax_functor B C).obj = F.obj := rfl
@[simp] lemma to_prelax_functor_map : (F : prelax_functor B C).map = F.map := rfl
@[simp] lemma to_prelax_functor_map‚ÇÇ : (F : prelax_functor B C).map‚ÇÇ = F.map‚ÇÇ := rfl

/-- Function between 1-morphisms as a functor. -/
@[simps]
def map_functor (a b : B) : (a ‚ü∂ b) ‚•§ (F.obj a ‚ü∂ F.obj b) :=
{ obj := Œª f, F.map f,
  map := Œª f g Œ∑, F.map‚ÇÇ Œ∑ }

variables (B)

/-- The identity oplax functor. -/
def id : oplax_functor B B :=
{ map_id := Œª a, ùüô (ùüô a),
  map_comp := Œª a b c f g, ùüô (f ‚â´ g),
  .. prelax_functor.id B }

@[simp] lemma id_obj (a : B) : (id B).obj a = a := rfl
@[simp] lemma id_map {a b : B} (f : a ‚ü∂ b) : (id B).map f = f := rfl
@[simp] lemma id_map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) : (id B).map‚ÇÇ Œ∑ = Œ∑ := rfl
@[simp] lemma id_map_id (a : B) : (id B).map_id a = ùüô (ùüô a) := rfl
@[simp] lemma id_map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (id B).map_comp f g = ùüô (f ‚â´ g) := rfl

instance : inhabited (oplax_functor B B) := ‚ü®id B‚ü©

variables {B}

/-- Composition of oplax functors. -/
def comp : oplax_functor B D :=
{ map_id := Œª a,
    (G.map_functor _ _).map (F.map_id a) ‚â´ G.map_id (F.obj a),
  map_comp := Œª a b c f g,
    (G.map_functor _ _).map (F.map_comp f g) ‚â´ G.map_comp (F.map f) (F.map g),
  map_comp_naturality_left' := Œª a b c f f' Œ∑ g, by
  { dsimp,
    rw [‚Üêmap‚ÇÇ_comp_assoc, map_comp_naturality_left, map‚ÇÇ_comp_assoc, map_comp_naturality_left,
      assoc] },
  map_comp_naturality_right' := Œª a b c f g g' Œ∑, by
  { dsimp,
    rw [‚Üêmap‚ÇÇ_comp_assoc, map_comp_naturality_right, map‚ÇÇ_comp_assoc, map_comp_naturality_right,
      assoc] },
  map‚ÇÇ_associator' := Œª a b c d f g h, by
  { dsimp,
    simp only [map‚ÇÇ_associator, ‚Üêmap‚ÇÇ_comp_assoc, ‚Üêmap_comp_naturality_right_assoc,
      whisker_left_comp, assoc],
    simp only [map‚ÇÇ_associator, map‚ÇÇ_comp, map_comp_naturality_left_assoc,
      whisker_right_comp, assoc] },
  map‚ÇÇ_left_unitor' := Œª a b f, by
  { dsimp,
    simp only [map‚ÇÇ_left_unitor, map‚ÇÇ_comp, map_comp_naturality_left_assoc,
      whisker_right_comp, assoc] },
  map‚ÇÇ_right_unitor' := Œª a b f, by
  { dsimp,
    simp only [map‚ÇÇ_right_unitor, map‚ÇÇ_comp, map_comp_naturality_right_assoc,
      whisker_left_comp, assoc] },
  .. (F : prelax_functor B C).comp ‚ÜëG }

@[simp] lemma comp_obj (a : B) : (F.comp G).obj a = G.obj (F.obj a) := rfl
@[simp] lemma comp_map {a b : B} (f : a ‚ü∂ b) : (F.comp G).map f = G.map (F.map f) := rfl
@[simp] lemma comp_map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) :
  (F.comp G).map‚ÇÇ Œ∑ = G.map‚ÇÇ (F.map‚ÇÇ Œ∑) := rfl
@[simp] lemma comp_map_id (a : B) :
  (F.comp G).map_id a =
    (G.map_functor (F.obj a) (F.obj a)).map (F.map_id a) ‚â´ G.map_id (F.obj a) := rfl
@[simp] lemma comp_map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (F.comp G).map_comp f g =
    (G.map_functor (F.obj a) (F.obj c)).map (F.map_comp f g) ‚â´
      G.map_comp (F.map f) (F.map g) := rfl

/--
A structure on an oplax functor that promotes an oprax functors to a pseudofunctor.
See `pseudofunctor.mk_of_oplax`.
-/
@[nolint has_inhabited_instance]
structure pseudo_core :=
(map_id_iso (a : B) : F.map (ùüô a) ‚âÖ ùüô (F.obj a))
(map_comp_iso {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : F.map (f ‚â´ g) ‚âÖ F.map f ‚â´ F.map g)
(map_id_iso_hom' : ‚àÄ {a : B}, (map_id_iso a).hom = F.map_id a . obviously)
(map_comp_iso_hom' : ‚àÄ {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c),
  (map_comp_iso f g).hom = F.map_comp f g . obviously)

restate_axiom pseudo_core.map_id_iso_hom'
restate_axiom pseudo_core.map_comp_iso_hom'
attribute [simp] pseudo_core.map_id_iso_hom pseudo_core.map_comp_iso_hom

end

end oplax_functor

/--
The auxiliary definition that claims that pseudofunctors preserve the associators
modulo some adjustments of domains and codomains of 2-morphisms. -/
/-
The reason for using this auxiliary definition instead of writing it directly in the definition
of pseudofunctors is that doing so will cause a timeout.
-/
@[simp]
def pseudofunctor.map‚ÇÇ_associator_aux
  (obj : B ‚Üí C) (map : Œ† {X Y : B}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
  (map‚ÇÇ : Œ† {a b : B} {f g : a ‚ü∂ b}, (f ‚ü∂ g) ‚Üí (map f ‚ü∂ map g))
  (map_comp : Œ† {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c), map (f ‚â´ g) ‚âÖ map f ‚â´ map g)
  {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d) : Prop :=
map‚ÇÇ (Œ±_ f g h).hom = (map_comp (f ‚â´ g) h).hom ‚â´ ((map_comp f g).hom ‚ñ∑ map h) ‚â´
  (Œ±_ (map f) (map g) (map h)).hom ‚â´ (map f ‚óÅ (map_comp g h).inv) ‚â´ (map_comp f (g ‚â´ h)).inv

variables (B C)

/--
A pseudofunctors `F` between bicategories `B` and `C` consists of functions between objects,
1-morphisms, and 2-morphisms.

Unlike functors between categories, functions between 1-morphisms do not need to strictly commute
with compositions, and do not need to strictly preserve the identity. Instead, there are
specified 2-isomorphisms `F.map (ùüô a) ‚âÖ ùüô (F.obj a)` and `F.map (f ‚â´ g) ‚âÖ F.map f ‚â´ F.map g`.

Functions between 2-morphisms strictly commute with compositions and preserve the identity.
They also preserve the associator, the left unitor, and the right unitor modulo some adjustments
of domains and codomains of 2-morphisms.
-/
structure pseudofunctor extends prelax_functor B C :=
(map_id (a : B) : map (ùüô a) ‚âÖ ùüô (obj a))
(map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) : map (f ‚â´ g) ‚âÖ map f ‚â´ map g)
(map‚ÇÇ_id' : ‚àÄ {a b : B} (f : a ‚ü∂ b), map‚ÇÇ (ùüô f) = ùüô (map f) . obviously)
(map‚ÇÇ_comp' : ‚àÄ {a b : B} {f g h : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (Œ∏ : g ‚ü∂ h),
  map‚ÇÇ (Œ∑ ‚â´ Œ∏) = map‚ÇÇ Œ∑ ‚â´ map‚ÇÇ Œ∏ . obviously)
(map‚ÇÇ_whisker_left' : ‚àÄ {a b c : B} (f : a ‚ü∂ b) {g h : b ‚ü∂ c} (Œ∑ : g ‚ü∂ h),
  map‚ÇÇ (f ‚óÅ Œ∑) = (map_comp f g).hom ‚â´ (map f ‚óÅ map‚ÇÇ Œ∑) ‚â´ (map_comp f h).inv . obviously)
(map‚ÇÇ_whisker_right' : ‚àÄ {a b c : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) (h : b ‚ü∂ c),
  map‚ÇÇ (Œ∑ ‚ñ∑ h) = (map_comp f h).hom ‚â´ (map‚ÇÇ Œ∑ ‚ñ∑ map h) ‚â´ (map_comp g h).inv . obviously)
(map‚ÇÇ_associator' : ‚àÄ {a b c d : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) (h : c ‚ü∂ d),
  pseudofunctor.map‚ÇÇ_associator_aux obj (Œª a b, map) (Œª a b f g, map‚ÇÇ) (Œª a b c, map_comp) f g h
    . obviously)
(map‚ÇÇ_left_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (Œª_ f).hom = (map_comp (ùüô a) f).hom ‚â´ ((map_id a).hom ‚ñ∑ map f) ‚â´ (Œª_ (map f)).hom
    . obviously)
(map‚ÇÇ_right_unitor' : ‚àÄ {a b : B} (f : a ‚ü∂ b),
  map‚ÇÇ (œÅ_ f).hom = (map_comp f (ùüô b)).hom ‚â´ (map f ‚óÅ (map_id b).hom) ‚â´ (œÅ_ (map f)).hom
    . obviously)

variables {B C}

namespace pseudofunctor

restate_axiom map‚ÇÇ_id'
restate_axiom map‚ÇÇ_comp'
restate_axiom map‚ÇÇ_whisker_left'
restate_axiom map‚ÇÇ_whisker_right'
restate_axiom map‚ÇÇ_associator'
restate_axiom map‚ÇÇ_left_unitor'
restate_axiom map‚ÇÇ_right_unitor'
attribute [reassoc]
  map‚ÇÇ_comp map‚ÇÇ_whisker_left map‚ÇÇ_whisker_right map‚ÇÇ_associator map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor
attribute [simp]
  map‚ÇÇ_id map‚ÇÇ_comp map‚ÇÇ_whisker_left map‚ÇÇ_whisker_right
  map‚ÇÇ_associator map‚ÇÇ_left_unitor map‚ÇÇ_right_unitor

section
variables (F : pseudofunctor B C) (G : pseudofunctor C D)

open iso

/-- The prelax functor between the underlying quivers. -/
add_decl_doc pseudofunctor.to_prelax_functor

instance has_coe_to_prelax : has_coe (pseudofunctor B C) (prelax_functor B C) :=
‚ü®to_prelax_functor‚ü©

@[simp] lemma to_prelax_eq_coe : F.to_prelax_functor = F := rfl
@[simp] lemma to_prelax_functor_obj : (F : prelax_functor B C).obj = F.obj := rfl
@[simp] lemma to_prelax_functor_map : (F : prelax_functor B C).map = F.map := rfl
@[simp] lemma to_prelax_functor_map‚ÇÇ : (F : prelax_functor B C).map‚ÇÇ = F.map‚ÇÇ := rfl

/-- The oplax functor associated with a pseudofunctor. -/
def to_oplax : oplax_functor B C :=
{ map_id := Œª a, (F.map_id a).hom,
  map_comp := Œª a b c f g, (F.map_comp f g).hom,
  .. (F : prelax_functor B C) }

instance has_coe_to_oplax : has_coe (pseudofunctor B C) (oplax_functor B C) := ‚ü®to_oplax‚ü©

@[simp] lemma to_oplax_eq_coe : F.to_oplax = F := rfl
@[simp] lemma to_oplax_obj : (F : oplax_functor B C).obj = F.obj := rfl
@[simp] lemma to_oplax_map : (F : oplax_functor B C).map = F.map := rfl
@[simp] lemma to_oplax_map‚ÇÇ : (F : oplax_functor B C).map‚ÇÇ = F.map‚ÇÇ := rfl
@[simp] lemma to_oplax_map_id (a : B) : (F : oplax_functor B C).map_id a = (F.map_id a).hom := rfl
@[simp] lemma to_oplax_map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (F : oplax_functor B C).map_comp f g = (F.map_comp f g).hom := rfl

/-- Function on 1-morphisms as a functor. -/
def map_functor (a b : B) : (a ‚ü∂ b) ‚•§ (F.obj a ‚ü∂ F.obj b) :=
(F : oplax_functor B C).map_functor a b

@[simp] lemma map_functor_obj {a b : B} (f : a ‚ü∂ b) : (F.map_functor a b).obj f = F.map f := rfl
@[simp] lemma map_functor_map {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) :
  (F.map_functor a b).map Œ∑ = F.map‚ÇÇ Œ∑ := rfl

variables (B)

/-- The identity pseudofunctor. -/
def id : pseudofunctor B B :=
{ map_id := Œª a, iso.refl (ùüô a),
  map_comp := Œª a b c f g, iso.refl (f ‚â´ g),
  .. prelax_functor.id B }

@[simp] lemma id_obj (a : B) : (id B).obj a = a := rfl
@[simp] lemma id_map {a b : B} (f : a ‚ü∂ b) : (id B).map f = f := rfl
@[simp] lemma id_map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) : (id B).map‚ÇÇ Œ∑ = Œ∑ := rfl
@[simp] lemma id_map_id (a : B) : (id B).map_id a = iso.refl (ùüô a) := rfl
@[simp] lemma id_map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (id B).map_comp f g = iso.refl (f ‚â´ g) := rfl

instance : inhabited (pseudofunctor B B) := ‚ü®id B‚ü©

variables {B}

/-- Composition of pseudofunctors. -/
def comp : pseudofunctor B D :=
{ map_id := Œª a, (G.map_functor _ _).map_iso (F.map_id a) ‚â™‚â´ G.map_id (F.obj a),
  map_comp := Œª a b c f g,
    (G.map_functor _ _).map_iso (F.map_comp f g) ‚â™‚â´ G.map_comp (F.map f) (F.map g),
  .. (F : prelax_functor B C).comp ‚ÜëG }

@[simp] lemma comp_obj (a : B) : (F.comp G).obj a = G.obj (F.obj a) := rfl
@[simp] lemma comp_map {a b : B} (f : a ‚ü∂ b) : (F.comp G).map f = G.map (F.map f) := rfl
@[simp] lemma comp_map‚ÇÇ {a b : B} {f g : a ‚ü∂ b} (Œ∑ : f ‚ü∂ g) :
  (F.comp G).map‚ÇÇ Œ∑ = G.map‚ÇÇ (F.map‚ÇÇ Œ∑) := rfl
@[simp] lemma comp_map_id (a : B) :
  (F.comp G).map_id a =
    (G.map_functor (F.obj a) (F.obj a)).map_iso (F.map_id a) ‚â™‚â´ G.map_id (F.obj a) := rfl
@[simp] lemma comp_map_comp {a b c : B} (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (F.comp G).map_comp f g =
    (G.map_functor (F.obj a) (F.obj c)).map_iso (F.map_comp f g) ‚â™‚â´
      G.map_comp (F.map f) (F.map g) := rfl

/--
Construct a pseudofunctor from an oplax functor whose `map_id` and `map_comp` are isomorphisms.
-/
@[simps]
def mk_of_oplax (F : oplax_functor B C) (F' : F.pseudo_core) : pseudofunctor B C :=
{ map_id := F'.map_id_iso,
  map_comp := F'.map_comp_iso,
  map‚ÇÇ_whisker_right' := Œª a b c f g Œ∑ h, by
  { dsimp,
    rw [F'.map_comp_iso_hom f h, ‚ÜêF.map_comp_naturality_left_assoc,
      ‚ÜêF'.map_comp_iso_hom g h, hom_inv_id, comp_id] },
  map‚ÇÇ_whisker_left' := Œª a b c f g h Œ∑, by
  { dsimp,
    rw [F'.map_comp_iso_hom f g, ‚ÜêF.map_comp_naturality_right_assoc,
      ‚ÜêF'.map_comp_iso_hom f h, hom_inv_id, comp_id] },
  map‚ÇÇ_associator' := Œª a b c d f g h, by
  { dsimp,
    rw [F'.map_comp_iso_hom (f ‚â´ g) h, F'.map_comp_iso_hom f g, ‚ÜêF.map‚ÇÇ_associator_assoc,
      ‚ÜêF'.map_comp_iso_hom f (g ‚â´ h), ‚ÜêF'.map_comp_iso_hom g h,
      hom_inv_whisker_left_assoc, hom_inv_id, comp_id] },
  .. (F : prelax_functor B C) }

/--
Construct a pseudofunctor from an oplax functor whose `map_id` and `map_comp` are isomorphisms.
-/
@[simps]
noncomputable
def mk_of_oplax' {F : oplax_functor B C}
  [‚àÄ a, is_iso (F.map_id a)] [‚àÄ {a b c} (f : a ‚ü∂ b) (g : b ‚ü∂ c), is_iso (F.map_comp f g)] :
  pseudofunctor B C :=
{ map_id := Œª a, as_iso (F.map_id a),
  map_comp := Œª a b c f g, as_iso (F.map_comp f g),
  map‚ÇÇ_whisker_right' := Œª a b c f g Œ∑ h, by
  { dsimp,
    rw [‚Üêassoc, is_iso.eq_comp_inv, F.map_comp_naturality_left] },
  map‚ÇÇ_whisker_left' := Œª a b c f g h Œ∑, by
  { dsimp,
    rw [‚Üêassoc, is_iso.eq_comp_inv, F.map_comp_naturality_right] },
  map‚ÇÇ_associator' := Œª a b c d f g h, by
  { dsimp,
    simp only [‚Üêassoc],
    rw [is_iso.eq_comp_inv, ‚Üêinv_whisker_left, is_iso.eq_comp_inv],
    simp only [assoc, F.map‚ÇÇ_associator] },
  .. (F : prelax_functor B C) }

end

end pseudofunctor

end

end category_theory
