/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.natural_transformation

/-!
# The bicategory of oplax functors between two bicategories

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Given bicategories `B` and `C`, we give a bicategory structure on `oplax_functor B C` whose
* objects are oplax functors,
* 1-morphisms are oplax natural transformations, and
* 2-morphisms are modifications.
-/

namespace category_theory

open category bicategory
open_locale bicategory

universes wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variables {B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B] {C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]
variables {F G H I : oplax_functor B C}

namespace oplax_nat_trans

/-- Left whiskering of an oplax natural transformation and a modification. -/
@[simps]
def whisker_left (Î· : F âŸ¶ G) {Î¸ Î¹ : G âŸ¶ H} (Î“ : Î¸ âŸ¶ Î¹) : Î· â‰« Î¸ âŸ¶ Î· â‰« Î¹ :=
{ app := Î» a, Î·.app a â— Î“.app a,
  naturality' := Î» a b f, by
  { dsimp, rw [associator_inv_naturality_right_assoc, whisker_exchange_assoc], simp } }

/-- Right whiskering of an oplax natural transformation and a modification. -/
@[simps]
def whisker_right {Î· Î¸ : F âŸ¶ G} (Î“ : Î· âŸ¶ Î¸) (Î¹ : G âŸ¶ H) : Î· â‰« Î¹ âŸ¶ Î¸ â‰« Î¹ :=
{ app := Î» a, Î“.app a â–· Î¹.app a,
  naturality' := Î» a b f, by
  { dsimp, simp_rw [assoc, â†associator_inv_naturality_left, whisker_exchange_assoc], simp } }

/-- Associator for the vertical composition of oplax natural transformations. -/
@[simps]
def associator (Î· : F âŸ¶ G) (Î¸ : G âŸ¶ H) (Î¹ : H âŸ¶ I) : (Î· â‰« Î¸) â‰« Î¹ â‰… Î· â‰« (Î¸ â‰« Î¹) :=
modification_iso.of_components (Î» a, Î±_ (Î·.app a) (Î¸.app a) (Î¹.app a)) (by tidy)

/-- Left unitor for the vertical composition of oplax natural transformations. -/
@[simps]
def left_unitor (Î· : F âŸ¶ G) : ğŸ™ F â‰« Î· â‰… Î· :=
modification_iso.of_components (Î» a, Î»_ (Î·.app a)) (by tidy)

/-- Right unitor for the vertical composition of oplax natural transformations. -/
@[simps]
def right_unitor (Î· : F âŸ¶ G) : Î· â‰« ğŸ™ G â‰… Î· :=
modification_iso.of_components (Î» a, Ï_ (Î·.app a)) (by tidy)

end oplax_nat_trans

variables (B C)

/-- A bicategory structure on the oplax functors between bicategories. -/
@[simps]
instance oplax_functor.bicategory : bicategory (oplax_functor B C) :=
{ whisker_left  := Î» F G H Î· _ _ Î“, oplax_nat_trans.whisker_left Î· Î“,
  whisker_right := Î» F G H _ _ Î“ Î·, oplax_nat_trans.whisker_right Î“ Î·,
  associator    := Î» F G H I, oplax_nat_trans.associator,
  left_unitor   := Î» F G, oplax_nat_trans.left_unitor,
  right_unitor  := Î» F G, oplax_nat_trans.right_unitor,
  whisker_exchange' := by { intros, ext, apply whisker_exchange } }

end category_theory
