/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.discrete_category
import category_theory.bicategory.functor
import category_theory.bicategory.strict

/-!
# Locally discrete bicategories

A category `C` can be promoted to a strict bicategory `locally_discrete C`. The objects and the
1-morphisms in `locally_discrete C` are the same as the objects and the morphisms, respectively,
in `C`, and the 2-morphisms in `locally_discrete C` are the equalities between 1-morphisms. In
other words, the category consisting of the 1-morphisms between each pair of objects `X` and `Y`
in `locally_discrete C` is defined as the discrete category associated with the type `X ‚ü∂ Y`.
-/

namespace category_theory

open bicategory discrete
open_locale bicategory

universes w‚ÇÇ v v‚ÇÅ v‚ÇÇ u u‚ÇÅ u‚ÇÇ

variables (C : Type u)

/--
A type alias for promoting any category to a bicategory,
with the only 2-morphisms being equalities.
-/
def locally_discrete := C

namespace locally_discrete

instance : Œ† [inhabited C], inhabited (locally_discrete C) := id

instance : Œ† [category_struct.{v} C], category_struct (locally_discrete C) := id

end locally_discrete

variables (C) [category.{v} C]

/--
The locally discrete bicategory on a category is a bicategory in which the objects and the
1-morphisms are the same as those in the underlying category, and the 2-morphisms are the
equalities between 1-morphisms.
-/
instance locally_discrete_bicategory : bicategory (locally_discrete C) :=
{ hom_category := Œª X Y, category_theory.discrete_category (X ‚ü∂ Y),
  whisker_left  := Œª X Y Z f g h Œ∑, eq_to_hom (congr_arg2 (‚â´) rfl (eq_of_hom Œ∑)),
  whisker_right := Œª X Y Z f g Œ∑ h, eq_to_hom (congr_arg2 (‚â´) (eq_of_hom Œ∑) rfl),
  associator := Œª W X Y Z f g h, eq_to_iso (category.assoc f g h),
  left_unitor  := Œª X Y f, eq_to_iso (category.id_comp f),
  right_unitor := Œª X Y f, eq_to_iso (category.comp_id f) }

/-- A locally discrete bicategory is strict. -/
instance locally_discrete_bicategory.strict : strict (locally_discrete C) := { }

variables {I : Type u‚ÇÅ} [category.{v‚ÇÅ} I] {B : Type u‚ÇÇ} [bicategory.{w‚ÇÇ v‚ÇÇ} B]
  (F : oplax_functor (locally_discrete I) B)

namespace oplax_functor

@[simp] lemma map‚ÇÇ_eq {X Y : locally_discrete I} {f g : X ‚ü∂ Y} (Œ∑ : f ‚ü∂ g) :
  F.map‚ÇÇ Œ∑ = eq_to_hom (by rw eq_of_hom Œ∑) :=
by convert F.eq_to_hom_map‚ÇÇ f g (eq_of_hom Œ∑)

variables [strict B]

@[simp] lemma id_comp ‚¶ÉX Y : I‚¶Ñ (f : X ‚ü∂ Y) :
  F.map_comp (ùüô X) f ‚â´ (F.map_id X ‚ñ∑ F.map f) = eq_to_hom (by simp) :=
by convert eq_whisker (F.map‚ÇÇ_left_unitor' f).symm (Œª_ _).inv using 1; simp

@[simp] lemma comp_id ‚¶ÉX Y : I‚¶Ñ (f : X ‚ü∂ Y) :
  F.map_comp f (ùüô Y) ‚â´ (F.map f ‚óÅ F.map_id Y) = eq_to_hom (by simp) :=
by convert eq_whisker (F.map‚ÇÇ_right_unitor' f).symm (œÅ_ _).inv using 1; simp

@[simp] lemma assoc ‚¶ÉX Y Z W : I‚¶Ñ (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (h : Z ‚ü∂ W) :
  F.map_comp (f ‚â´ g) h ‚â´ (F.map_comp f g ‚ñ∑ F.map h) = eq_to_hom (by simp) ‚â´
  F.map_comp f (g ‚â´ h) ‚â´ (F.map f ‚óÅ F.map_comp g h) ‚â´ eq_to_hom (by simp) :=
by convert eq_whisker (F.map‚ÇÇ_associator' f g h).symm (Œ±_ _ _ _).inv using 1; simp

end oplax_functor

/--
If `B` is a strict bicategory and `I` is a (1-)category, any functor (of 1-categories) `I ‚•§ B` can
be promoted to an oplax functor from `locally_discrete I` to `B`.
-/
@[simps]
def functor.to_oplax_functor [strict B] (F : I ‚•§ B) : oplax_functor (locally_discrete I) B :=
{ map‚ÇÇ := Œª i j f g Œ∑, eq_to_hom (congr_arg _ (eq_of_hom Œ∑)),
  map_id := Œª i, eq_to_hom (F.map_id i),
  map_comp := Œª i j k f g, eq_to_hom (F.map_comp f g),
  .. F }

end category_theory
