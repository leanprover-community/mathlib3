/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.functor

/-!
# Oplax natural transformations

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

Just as there are natural transformations between functors, there are oplax natural transformations
between oplax functors. The equality in the naturality of natural transformations is replaced by a
specified 2-morphism `F.map f â‰« app b âŸ¶ app a â‰« G.map f` in the case of oplax natural
transformations.

## Main definitions

* `oplax_nat_trans F G` : oplax natural transformations between oplax functors `F` and `G`
* `oplax_nat_trans.vcomp Î· Î¸` : the vertical composition of oplax natural transformations `Î·`
  and `Î¸`
* `oplax_nat_trans.category F G` : the category structure on the oplax natural transformations
  between `F` and `G`
-/

namespace category_theory

open category bicategory
open_locale bicategory

universes wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variables {B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B] {C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]

/--
If `Î·` is an oplax natural transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have a 2-morphism
`Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism `f : a âŸ¶ b`.
These 2-morphisms satisfies the naturality condition, and preserve the identities and
the compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure oplax_nat_trans (F G : oplax_functor B C) :=
(app (a : B) : F.obj a âŸ¶ G.obj a)
(naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b âŸ¶ app a â‰« G.map f)
(naturality_naturality' : âˆ€ {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
  F.mapâ‚‚ Î· â–· app b â‰« naturality g = naturality f â‰« app a â— G.mapâ‚‚ Î· . obviously)
(naturality_id' : âˆ€ a : B,
  naturality (ğŸ™ a) â‰« app a â— G.map_id a =
    F.map_id a â–· app a â‰« (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv . obviously)
(naturality_comp' : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
  naturality (f â‰« g) â‰« app a â— G.map_comp f g =
    F.map_comp f g â–· app c â‰« (Î±_ _ _ _).hom â‰«
      F.map f â— naturality g â‰« (Î±_ _ _ _).inv â‰«
        naturality f â–· G.map g â‰« (Î±_ _ _ _).hom . obviously)

restate_axiom oplax_nat_trans.naturality_naturality'
restate_axiom oplax_nat_trans.naturality_id'
restate_axiom oplax_nat_trans.naturality_comp'
attribute [simp, reassoc] oplax_nat_trans.naturality_naturality
  oplax_nat_trans.naturality_id oplax_nat_trans.naturality_comp

namespace oplax_nat_trans

section
variables (F : oplax_functor B C)

/-- The identity oplax natural transformation. -/
@[simps]
def id : oplax_nat_trans F F :=
{ app := Î» a, ğŸ™ (F.obj a),
  naturality := Î» a b f, (Ï_ (F.map f)).hom â‰« (Î»_ (F.map f)).inv }

instance : inhabited (oplax_nat_trans F F) := âŸ¨id FâŸ©

variables {F} {G H : oplax_functor B C} (Î· : oplax_nat_trans F G) (Î¸ : oplax_nat_trans G H)

section
variables {a b c : B} {a' : C}

@[simp, reassoc]
lemma whisker_left_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
  f â— G.mapâ‚‚ Î² â–· Î¸.app b â‰« f â— Î¸.naturality h =
    f â— Î¸.naturality g â‰« f â— Î¸.app a â— H.mapâ‚‚ Î² :=
by simp_rw [â†bicategory.whisker_left_comp, naturality_naturality]

@[simp, reassoc]
lemma whisker_right_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
  F.mapâ‚‚ Î² â–· Î·.app b â–· h â‰« Î·.naturality g â–· h =
    Î·.naturality f â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h â‰« (Î±_ _ _ _).inv :=
by rw [â†comp_whisker_right, naturality_naturality, comp_whisker_right, whisker_assoc]

@[simp, reassoc]
lemma whisker_left_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
  f â— Î¸.naturality (g â‰« h) â‰« f â— Î¸.app a â— H.map_comp g h =
    f â— G.map_comp g h â–· Î¸.app c â‰« f â— (Î±_ _ _ _).hom â‰«
      f â— G.map g â— Î¸.naturality h â‰« f â— (Î±_ _ _ _).inv â‰«
        f â— Î¸.naturality g â–· H.map h â‰« f â— (Î±_ _ _ _).hom :=
by simp_rw [â†bicategory.whisker_left_comp, naturality_comp]

@[simp, reassoc]
lemma whisker_right_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
  Î·.naturality (f â‰« g) â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.map_comp f g â–· h =
    F.map_comp f g â–· Î·.app c â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom â‰«
      F.map f â— Î·.naturality g â–· h â‰« (Î±_ _ _ _).inv â‰« (Î±_ _ _ _).inv â–· h â‰«
        Î·.naturality f â–· G.map g â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom :=
by { rw [â†associator_naturality_middle, â†comp_whisker_right_assoc, naturality_comp], simp }

@[simp, reassoc]
lemma whisker_left_naturality_id (f : a' âŸ¶ G.obj a) :
  f â— Î¸.naturality (ğŸ™ a) â‰« f â— Î¸.app a â— H.map_id a =
    f â— G.map_id a â–· Î¸.app a â‰« f â— (Î»_ (Î¸.app a)).hom â‰« f â— (Ï_ (Î¸.app a)).inv :=
by simp_rw [â†bicategory.whisker_left_comp, naturality_id]

@[simp, reassoc]
lemma whisker_right_naturality_id (f : G.obj a âŸ¶ a') :
  Î·.naturality (ğŸ™ a) â–· f â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.map_id a â–· f =
    F.map_id a â–· Î·.app a â–· f â‰« (Î»_ (Î·.app a)).hom â–· f â‰«
      (Ï_ (Î·.app a)).inv â–· f â‰« (Î±_ _ _ _).hom :=
by { rw [â†associator_naturality_middle, â†comp_whisker_right_assoc, naturality_id], simp }

end

/-- Vertical composition of oplax natural transformations. -/
@[simps]
def vcomp (Î· : oplax_nat_trans F G) (Î¸ : oplax_nat_trans G H) : oplax_nat_trans F H :=
{ app := Î» a, Î·.app a â‰« Î¸.app a,
  naturality := Î» a b f,
    (Î±_ _ _ _).inv â‰« Î·.naturality f â–· Î¸.app b â‰« (Î±_ _ _ _).hom â‰«
      Î·.app a â— Î¸.naturality f â‰« (Î±_ _ _ _).inv,
  naturality_comp' := Î» a b c f g, by
  { calc _ =  _ â‰«
    F.map_comp f g â–· Î·.app c â–· Î¸.app c â‰« _ â‰«
      F.map f â— Î·.naturality g â–· Î¸.app c â‰« _ â‰«
        (F.map f â‰« Î·.app b) â— Î¸.naturality g â‰«
          Î·.naturality f â–· (Î¸.app b â‰« H.map g) â‰« _ â‰«
            Î·.app a â— Î¸.naturality f â–· H.map g â‰« _  : _
    ... =  _ : _,
    exact (Î±_ _ _ _).inv,
    exact (Î±_ _ _ _).hom â–· _ â‰« (Î±_ _ _ _).hom,
    exact _ â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv,
    exact (Î±_ _ _ _).hom â‰« _ â— (Î±_ _ _ _).inv,
    exact _ â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv,
    { rw [whisker_exchange_assoc], simp },
    { simp } } }

variables (B C)

@[simps]
instance : category_struct (oplax_functor B C) :=
{ hom  := oplax_nat_trans,
  id   := oplax_nat_trans.id,
  comp := Î» F G H, oplax_nat_trans.vcomp }

end

section
variables {F G : oplax_functor B C}

/--
A modification `Î“` between oplax natural transformations `Î·` and `Î¸` consists of a family of
2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« Î¸.naturality f = Î·.naturality f â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure modification (Î· Î¸ : F âŸ¶ G) :=
(app (a : B) : Î·.app a âŸ¶ Î¸.app a)
(naturality' : âˆ€ {a b : B} (f : a âŸ¶ b),
  (F.map f â— app b) â‰« Î¸.naturality f = Î·.naturality f â‰« (app a â–· G.map f) . obviously)

restate_axiom modification.naturality'
attribute [simp, reassoc] modification.naturality

variables {Î· Î¸ Î¹ : F âŸ¶ G}

namespace modification

variables (Î·)

/-- The identity modification. -/
@[simps]
def id : modification Î· Î· := { app := Î» a, ğŸ™ (Î·.app a) }

instance : inhabited (modification Î· Î·) := âŸ¨modification.id Î·âŸ©

variables {Î·}

section
variables (Î“ : modification Î· Î¸) {a b c : B} {a' : C}

@[simp, reassoc]
lemma whisker_left_naturality (f : a' âŸ¶ F.obj b) (g : b âŸ¶ c) :
  f â— F.map g â— Î“.app c â‰« f â— Î¸.naturality g =
    f â— Î·.naturality g â‰« f â— Î“.app b â–· G.map g :=
by simp_rw [â†bicategory.whisker_left_comp, naturality]

@[simp, reassoc]
lemma whisker_right_naturality (f : a âŸ¶ b) (g : G.obj b âŸ¶ a') :
  F.map f â— Î“.app b â–· g â‰« (Î±_ _ _ _).inv â‰« Î¸.naturality f â–· g =
    (Î±_ _ _ _).inv â‰« Î·.naturality f â–· g â‰« Î“.app a â–· G.map f â–· g :=
by simp_rw [associator_inv_naturality_middle_assoc, â†comp_whisker_right, naturality]

end

/-- Vertical composition of modifications. -/
@[simps]
def vcomp (Î“ : modification Î· Î¸) (Î” : modification Î¸ Î¹) : modification Î· Î¹ :=
{ app := Î» a, Î“.app a â‰« Î”.app a }

end modification

/-- Category structure on the oplax natural transformations between oplax_functors. -/
@[simps]
instance category (F G : oplax_functor B C) : category (F âŸ¶ G) :=
{ hom  := modification,
  id   := modification.id,
  comp := Î» Î· Î¸ Î¹, modification.vcomp }

/--
Construct a modification isomorphism between oplax natural transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def modification_iso.of_components
  (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
  (naturality : âˆ€ {a b} (f : a âŸ¶ b),
    F.map f â— (app b).hom â‰« Î¸.naturality f = Î·.naturality f â‰« (app a).hom â–· G.map f) :
  Î· â‰… Î¸ :=
{ hom := { app := Î» a, (app a).hom },
  inv :=
  { app := Î» a, (app a).inv,
    naturality' := Î» a b f, by simpa using
      congr_arg (Î» f, _ â— (app b).inv â‰« f â‰« (app a).inv â–· _) (naturality f).symm } }

end

end oplax_nat_trans

end category_theory
