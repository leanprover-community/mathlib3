 /-
Copyright (c) 2021 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.functor

/-!
# Pseudonatural transformations

Just as there are natural transformations between functors, there are pseudonatural
transformations between oplax_functors. The equality in the naturality of natural
transformations is replaced by an specified isomorphism
`F.map f â‰« app b â‰… app a â‰« G.map f`
in the case of pseudonatural transformations.

We give a bicategory structure on the oplax_functors between bicategories. In this bicategory,
* 1-morphisms are are the pseudonatural transformations, given by `oplax_nat_trans F G`,
* the composition of 1-morphisms is the vertical composition, given by `Î·.vcomp Î¸`,
* 2-morphisms are the modifications, given by `modification Î· Î¸`.
-/

open category_theory

universes wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace category_theory

open category bicategory
open_locale bicategory

variables
{B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B]
{C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]

/--
If `Î·` is a pseudonatural transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have an isomorphism
`Î·.naturality f : F.map f â‰« app b â‰… app a â‰« G.map f` for each 1-morphisms `f : a âŸ¶ b`.
This family of isomorphisms satisfies certain equations.
-/
structure oplax_nat_trans (F G : oplax_functor B C) :=
(app (a : B) : F.obj a âŸ¶ G.obj a)
(naturality {a b: B} (f : a âŸ¶ b) : F.map f â‰« app b âŸ¶ app a â‰« G.map f)
(naturality_naturality' : âˆ€ {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
  (F.mapâ‚‚ Î· â–· _) â‰« naturality g = naturality f â‰« (_ â— G.mapâ‚‚ Î·) . obviously)
(naturality_id' : âˆ€ a : B,
  (naturality (ğŸ™ a)) â‰« (_ â— G.map_id a)
  = (F.map_id a â–· _) â‰« (Î»_ _).hom  â‰« (Ï_ _).inv . obviously)
(naturality_comp' : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
   (naturality (f â‰« g)) â‰« (_ â— (G.map_comp f g))
  = (F.map_comp f g â–· _) â‰« (Î±_ _ _ _).hom â‰« (_ â— naturality g)
  â‰« (Î±_ _ _ _).inv â‰« (naturality f â–· G.map g)
  â‰« (Î±_ _ _ _).hom  . obviously)

restate_axiom oplax_nat_trans.naturality_naturality'
attribute [simp, reassoc] oplax_nat_trans.naturality_naturality
restate_axiom oplax_nat_trans.naturality_comp'
attribute [simp, reassoc] oplax_nat_trans.naturality_comp
restate_axiom oplax_nat_trans.naturality_id'
attribute [simp, reassoc] oplax_nat_trans.naturality_id

structure pseudonat_trans (F G : pseudofunctor B C) extends oplax_nat_trans F.to_oplax_functor G.to_oplax_functor :=
(naturality_iso {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b â‰… app a â‰« G.map f)
(naturality_iso_hom' : âˆ€ {a b : B} (f : a âŸ¶ b), (naturality_iso f).hom = naturality f . obviously)

restate_axiom pseudonat_trans.naturality_iso_hom'
attribute [simp] pseudonat_trans.naturality_iso_hom

namespace oplax_nat_trans

/--
The identity pseudonatural transformation.
-/
@[simps]
def id (F : oplax_functor B C) : oplax_nat_trans F F :=
{ app := Î» a, ğŸ™ (F.obj a),
  naturality := Î» a b f, (Ï_ _).hom â‰« (Î»_ _).inv,
  naturality_naturality' := Î» a b f f' Î·, by
  { rw [assoc, â†left_unitor_inv_naturality, â†right_unitor_naturality_assoc] },
  naturality_comp' := Î» a b c f g, by
  { rw [assoc, â†left_unitor_inv_naturality, â†right_unitor_naturality_assoc],
    simp only [triangle_assoc_comp_right_assoc, right_unitor_comp, left_unitor_comp_inv, bicategory.whisker_right_comp,
      inv_hom_whisker_left_assoc, assoc, bicategory.whisker_left_comp], },
  naturality_id' := Î» a, by
  { rw [assoc, â†left_unitor_inv_naturality, â†right_unitor_naturality_assoc,
      unitors_equal, unitors_inv_equal] } }

instance (F : oplax_functor B C) : inhabited (oplax_nat_trans F F) := âŸ¨id FâŸ©

section
variables {F G H : oplax_functor B C}
(Ï† : oplax_nat_trans F G) (Ïˆ : oplax_nat_trans G H) {a b c : B} {a' : C}

@[simp, reassoc]
lemma whisker_left_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î· : g âŸ¶ h) :
  (f â— (G.mapâ‚‚ Î· â–· Ïˆ.app b)) â‰« (f â— Ïˆ.naturality h)
  = (f â— Ïˆ.naturality g) â‰« (f â— (Ïˆ.app a â— H.mapâ‚‚ Î·)) :=
by { simp only [â†bicategory.whisker_left_comp], rw naturality_naturality }

@[simp, reassoc]
lemma whisker_right_naturality_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
  ((F.mapâ‚‚ Î· â–· Ï†.app b) â–· h) â‰« (Ï†.naturality g â–· h)
  = (Ï†.naturality f â–· h) â‰« ((Ï†.app a â— G.mapâ‚‚ Î·) â–· h) :=
by { simp only [â†bicategory.whisker_right_comp], rw naturality_naturality }

@[simp, reassoc]
lemma whisker_left_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
  (f â— Ïˆ.naturality (g â‰« h)) â‰« (f â— (_ â— H.map_comp g h))
  = (f â— (G.map_comp g h â–· _)) â‰«(f â— (Î±_ _ _ _).hom) â‰« (f â— (_ â— Ïˆ.naturality h))
  â‰« (f â— (Î±_ _ _ _).inv) â‰« (f â— (Ïˆ.naturality g â–· H.map h))
  â‰« (f â— (Î±_ _ _ _).hom)  :=
by { simp only [â†bicategory.whisker_left_comp], rw naturality_comp }

@[simp, reassoc]
lemma whisker_right_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a')  :
  (Ï†.naturality (f â‰« g) â–· h) â‰« ((_ â— G.map_comp f g) â–· h)
  = ((F.map_comp f g â–· _) â–· h) â‰« ((Î±_ _ _ _).hom â–· h) â‰« ((_ â— Ï†.naturality g) â–· h)
  â‰« ((Î±_ _ _ _).inv â–· h) â‰« ((Ï†.naturality f â–· G.map g) â–· h)
  â‰« ((Î±_ _ _ _).hom â–· h)  :=
by { simp only [â†bicategory.whisker_right_comp], rw naturality_comp }

@[simp, reassoc]
lemma whisker_left_naturality_id (f : a' âŸ¶ G.obj a) :
   (f â— Ïˆ.naturality (ğŸ™ a)) â‰« (f â— (_ â— H.map_id a))
  = (f â— (G.map_id a â–· _)) â‰« (f â— (Î»_ _).hom) â‰« (f â— (Ï_ _).inv) :=
by { simp only [â†bicategory.whisker_left_comp], rw naturality_id }

@[simp, reassoc]
lemma whisker_right_naturality_id (f : G.obj a âŸ¶ a') :
   ((Ï†.naturality (ğŸ™ a)) â–· f)â‰« ((_ â— (G.map_id a)) â–· f)
  = ((F.map_id a â–· _) â–· f) â‰« ((Î»_ _).hom â–· f) â‰« ((Ï_ _).inv â–· f) :=
by { simp only [â†bicategory.whisker_right_comp], rw naturality_id }

end

/--
Vertical composition of pseudonatural transformations.
-/
@[simps]
def vcomp {F G H : oplax_functor B C} (Î· : oplax_nat_trans F G) (Î¸ : oplax_nat_trans G H) :
  oplax_nat_trans F H :=
{ app := Î» a, Î·.app a â‰« Î¸.app a,
  naturality := Î» a b f,
    (Î±_ _ _ _).inv â‰« (Î·.naturality f â–· Î¸.app b) â‰« (Î±_ _ _ _).hom â‰«
    (Î·.app a â— Î¸.naturality f) â‰« (Î±_ _ _ _).inv,
  naturality_naturality' := Î» a b f g Î¹, by
  { simp only [bicategory.whisker_right_comp, assoc, bicategory.whisker_left_comp],
    rw [â†associator_inv_naturality_right, â†whisker_left_naturality_naturality_assoc,
        â†associator_naturality_middle_assoc, â†whisker_right_naturality_naturality_assoc,
        â†associator_inv_naturality_left_assoc] },
  naturality_comp' := Î» a b c f g, by
  { simp only [bicategory.whisker_right_comp, assoc, bicategory.whisker_left_comp],
    rw [â†associator_inv_naturality_right, whisker_left_naturality_comp_assoc,
        â†associator_naturality_middle_assoc, whisker_right_naturality_comp_assoc,
        â†associator_inv_naturality_left_assoc],
    rw [â†pentagon_hom_hom_inv_inv_hom, associator_naturality_middle_assoc,
        â†pentagon_inv_hom_hom_hom_inv_assoc, â†associator_naturality_middle_assoc],
    slice_rhs 5 13
    { rw [â†pentagon_inv_hom_hom_hom_hom_assoc, â†pentagon_hom_hom_inv_hom_hom,
          associator_naturality_left_assoc, â†associator_naturality_right_assoc,
          pentagon_inv_inv_hom_hom_inv_assoc, inv_hom_whisker_left_assoc, iso.hom_inv_id_assoc,
          whisker_exchange_assoc, associator_naturality_right_assoc,
          â†associator_naturality_left_assoc, â†pentagon_assoc] },
    simp only [assoc] },
  naturality_id' := Î» a, by
  { simp only [bicategory.whisker_right_comp, assoc, bicategory.whisker_left_comp],
    rw [â†associator_inv_naturality_right, whisker_left_naturality_id_assoc,
        â†associator_naturality_middle_assoc, whisker_right_naturality_id_assoc,
        â†associator_inv_naturality_left_assoc],
    simp only [left_unitor_comp, triangle_assoc, inv_hom_whisker_right_assoc, assoc,
      right_unitor_comp_inv] } }

end oplax_nat_trans

namespace pseudonat_trans

/--
The identity pseudonatural transformation.
-/
@[simps]
def id (F : pseudofunctor B C) : pseudonat_trans F F :=
{ naturality_iso := Î» a b f, (Ï_ _) â‰ªâ‰« (Î»_ _).symm,
  .. oplax_nat_trans.id F.to_oplax_functor }

/--
Vertical composition of pseudonatural transformations.
-/
@[simps]
def vcomp {F G H : pseudofunctor B C} (Î· : pseudonat_trans F G) (Î¸ : pseudonat_trans G H) :
  pseudonat_trans F H :=
{ naturality_iso := Î» a b f,
    (Î±_ _ _ _).symm â‰ªâ‰« (whisker_right_iso (Î·.naturality_iso f) (Î¸.app b)) â‰ªâ‰« (Î±_ _ _ _) â‰ªâ‰«
    (whisker_left_iso (Î·.app a) (Î¸.naturality_iso f)) â‰ªâ‰« (Î±_ _ _ _).symm,
  .. Î·.to_oplax_nat_trans.vcomp Î¸.to_oplax_nat_trans }

end pseudonat_trans

section
variables {F G H I : oplax_functor B C}

namespace oplax_nat_trans

/--
A modification `Î“` between pseudonatural transformations `Î·` and `Î¸` consists of a family of
2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« (Î¸.naturality f).hom = (Î·.naturality f).hom â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure modification (Î· Î¸ : oplax_nat_trans F G) :=
(app (a : B) : Î·.app a âŸ¶ Î¸.app a)
(naturality' : âˆ€ {a b : B} (f : a âŸ¶ b),
  (F.map f â— app b) â‰« (Î¸.naturality f)
  = (Î·.naturality f) â‰« (app a â–· G.map f) . obviously)

restate_axiom modification.naturality'
attribute [reassoc] modification.naturality

namespace modification

/--
Vertical composition of modifications.
-/
@[simps]
def vcomp  (Î· Î¸ Î¹ : oplax_nat_trans F G)
  (Î“ : modification Î· Î¸) (Î” : modification Î¸ Î¹) : modification Î· Î¹ :=
{ app := Î» a, Î“.app a â‰« Î”.app a,
  naturality' := Î» a b f, by { simp [naturality_assoc, naturality] } }

/--
The identity modification.
-/
@[simps]
def id (Î· : oplax_nat_trans F G) : modification Î· Î· :=
{ app := Î» a, ğŸ™ (Î·.app a) }

instance (Î· : oplax_nat_trans F G) : inhabited (modification Î· Î·) := âŸ¨modification.id Î·âŸ©

section

variables {Î· Î¸ : oplax_nat_trans F G} (Î“ : modification Î· Î¸) {a b c : B} {a' : C}

@[reassoc]
lemma whisker_left_naturality (f : a' âŸ¶ F.obj b) (g : b âŸ¶ c) :
  (f â— (_ â— Î“.app c)) â‰« (f â— (Î¸.naturality g))
  = (f â— (Î·.naturality g)) â‰« (f â— (Î“.app b â–· _)) :=
by { simp only [â†bicategory.whisker_left_comp], rw modification.naturality }

@[reassoc]
lemma whisker_right_naturality (f : a âŸ¶ b) (g : G.obj b âŸ¶ a') :
  ((_ â— Î“.app b) â–· g) â‰« ((Î¸.naturality f) â–· g)
  = ((Î·.naturality f) â–· g) â‰« ((Î“.app a â–· _) â–· g) :=
by { simp only [â†bicategory.whisker_right_comp], rw modification.naturality }

end

end modification

/--
A category structure on the pseudonatural transformations between oplax_functors.
-/
@[simps]
instance category (F G : oplax_functor B C) : category (oplax_nat_trans F G) :=
{ hom   := modification,
  id    := modification.id,
  comp  := modification.vcomp }

/--
Construct a modification isomorphism between pseudonatural transformation
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def modification_iso.of_components {Î· Î¸ : oplax_nat_trans F G}
  (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
  (naturality : âˆ€ {a b} (f : a âŸ¶ b),
    (_ â— (app b).hom) â‰« (Î¸.naturality f) = (Î·.naturality f) â‰« ((app a).hom â–· _)) :
      Î· â‰… Î¸ :=
{ hom := { app := Î» a, (app a).hom },
  inv :=
  { app := Î» a, (app a).inv,
    naturality' := Î» a b f, by
    { have h := congr_arg (Î» f, (_ â— (app b).inv) â‰« f â‰« ((app a).inv â–· _)) (naturality f).symm,
      simp only [category.comp_id, inv_hom_whisker_left_assoc, category.assoc,
        hom_inv_whisker_right] at h,
      exact h } } }

/--
Left whiskering of a pseudonatural transformation and a modification.
-/
@[simps] def whisker_left
  (Î· : oplax_nat_trans F G) {Î¸ Î¹ : oplax_nat_trans G H} (Î“ : modification Î¸ Î¹) :
    modification (Î·.vcomp Î¸) (Î·.vcomp Î¹) :=
{ app := Î» a, Î·.app a â— Î“.app a,
  naturality' := Î» a b f, by
  { dsimp,
    simp only [category.assoc],
    rw [associator_inv_naturality_right_assoc, whisker_exchange_assoc,
        associator_naturality_right_assoc, Î“.whisker_left_naturality_assoc,
        associator_inv_naturality_middle] } }

/--
Right whiskering of a pseudonatural transformation and a modification.
-/
@[simps] def whisker_right
  {Î· Î¸ : oplax_nat_trans F G} (Î“ : modification Î· Î¸) (Î¹ : oplax_nat_trans G H) :
    modification (Î·.vcomp Î¹) (Î¸.vcomp Î¹) :=
{ app := Î» a, Î“.app a â–· Î¹.app a,
  naturality' := Î» a b f, by
  { dsimp,
    simp only [category.assoc],
    rw [associator_inv_naturality_middle_assoc, Î“.whisker_right_naturality_assoc,
        associator_naturality_left_assoc, â†whisker_exchange_assoc,
        associator_inv_naturality_left] } }

/--
Associator for the vertical composition between pseudonatural transformations.
-/
@[simps] def associator
  (Î· : oplax_nat_trans F G) (Î¸ : oplax_nat_trans G H) (Î¹ : oplax_nat_trans H I) :
    (Î·.vcomp Î¸).vcomp Î¹ â‰… Î·.vcomp (Î¸.vcomp Î¹) :=
modification_iso.of_components (Î» a, Î±_ (Î·.app a) (Î¸.app a) (Î¹.app a))
begin
  intros a b f,
  dsimp,
  simp only [bicategory.whisker_right_comp, bicategory.whisker_left_comp, category.assoc],
  rw [â†pentagon_inv_inv_hom_hom_inv_assoc, â†associator_naturality_left_assoc,
      pentagon_hom_hom_inv_hom_hom_assoc, â†associator_naturality_middle_assoc,
      â†pentagon_inv_hom_hom_hom_hom_assoc, â†associator_naturality_right_assoc,
      pentagon_hom_inv_inv_inv_hom]
end

/--
Left unitor for the vertical composition between pseudonatural transformations.
-/
@[simps]
def left_unitor (Î· : oplax_nat_trans F G) : (oplax_nat_trans.id F).vcomp Î· â‰… Î· :=
modification_iso.of_components (Î» a, Î»_ (Î·.app a))
begin
  intros a b f,
  dsimp,
  simp [triangle_assoc_comp_right_assoc],
  rw [â†left_unitor_comp, left_unitor_naturality, left_unitor_comp],
  simp
end

/--
Right unitor for the vertical composition between pseudonatural transformations.
-/
@[simps]
def right_unitor  (Î· : oplax_nat_trans F G) : Î·.vcomp (oplax_nat_trans.id G) â‰… Î· :=
modification_iso.of_components (Î» a, Ï_ (Î·.app a))
begin
  intros a b f,
  dsimp,
  simp [triangle_assoc_comp_right_assoc],
  rw [â†right_unitor_comp, right_unitor_naturality, right_unitor_comp],
  simp
end

end oplax_nat_trans

end

section
variables (B C)

/--
A bicategory structure on the oplax_functors between bicategories. The 1-morphisms in this bicategory are
the pseudonatural transformations, and the composition of 1-morphisms is the vertical composition
of pseudonatural transformations. The 2-morphisms are the modifications.
-/
@[simps]
instance : bicategory (oplax_functor B C) :=
{ hom := oplax_nat_trans,
  id := oplax_nat_trans.id,
  hom_category := oplax_nat_trans.category,
  comp := Î» F G H, oplax_nat_trans.vcomp,
  whisker_left  := Î» F G H Î· _ _ Î“, oplax_nat_trans.whisker_left Î· Î“,
  whisker_right := Î» F G H _ _ Î“ Î·, oplax_nat_trans.whisker_right Î“ Î·,
  associator := Î» F G H I, oplax_nat_trans.associator,
  left_unitor   := Î» F G, oplax_nat_trans.left_unitor,
  right_unitor  := Î» F G, oplax_nat_trans.right_unitor,
  associator_naturality_left'   := by { intros, ext, dsimp, rw associator_naturality_left },
  associator_naturality_middle' := by { intros, ext, dsimp, rw associator_naturality_middle },
  associator_naturality_right'  := by { intros, ext, dsimp, rw associator_naturality_right },
  left_unitor_naturality'   := by { intros, ext, dsimp, rw left_unitor_naturality },
  right_unitor_naturality'  := by { intros, ext, dsimp, rw right_unitor_naturality },
  pentagon' := by { intros, ext, dsimp, rw pentagon },
  triangle' := by { intros, ext, dsimp, rw triangle } }

end

namespace pseudonat_trans

/--
A category structure on the pseudonatural transformations between oplax_functors.
-/
@[simps]
instance category (F G : pseudofunctor B C) : category (pseudonat_trans F G) :=
{ hom   := Î» Î· Î¸, oplax_nat_trans.modification Î·.to_oplax_nat_trans Î¸.to_oplax_nat_trans,
  id    := Î» Î·, oplax_nat_trans.modification.id Î·.to_oplax_nat_trans,
  comp  := Î» Î· Î¸ Î¹,
    oplax_nat_trans.modification.vcomp Î·.to_oplax_nat_trans Î¸.to_oplax_nat_trans Î¹.to_oplax_nat_trans }

/--
Construct a modification isomorphism between pseudonatural transformation
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def modification_iso.of_components {F G : pseudofunctor B C} {Î· Î¸ : pseudonat_trans F G}
  (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
  (naturality : âˆ€ {a b} (f : a âŸ¶ b),
    (_ â— (app b).hom) â‰« (Î¸.naturality f) = (Î·.naturality f) â‰« ((app a).hom â–· _)) :
      Î· â‰… Î¸ :=
{ hom := { app := Î» a, (app a).hom },
  inv :=
  { app := Î» a, (app a).inv,
    naturality' := Î» a b f, by
    { have h := congr_arg (Î» f, (_ â— (app b).inv) â‰« f â‰« ((app a).inv â–· _)) (naturality f).symm,
      simp only [category.comp_id, inv_hom_whisker_left_assoc, category.assoc,
        hom_inv_whisker_right] at h,
      exact h } } }

section
variables {F G H I : pseudofunctor B C}
/--
Associator for the vertical composition between pseudonatural transformations.
-/
@[simps] def associator
  (Î· : pseudonat_trans F G) (Î¸ : pseudonat_trans G H) (Î¹ : pseudonat_trans H I) :
    (Î·.vcomp Î¸).vcomp Î¹ â‰… Î·.vcomp (Î¸.vcomp Î¹) :=
modification_iso.of_components
  (Î» a, Î±_ _ _ _)
begin
  intros a b f,
  dsimp,
  simp only [bicategory.whisker_right_comp, bicategory.whisker_left_comp, category.assoc],
  rw [â†pentagon_inv_inv_hom_hom_inv_assoc, â†associator_naturality_left_assoc,
      pentagon_hom_hom_inv_hom_hom_assoc, â†associator_naturality_middle_assoc,
      â†pentagon_inv_hom_hom_hom_hom_assoc, â†associator_naturality_right_assoc,
      pentagon_hom_inv_inv_inv_hom]
end

/--
Left unitor for the vertical composition between pseudonatural transformations.
-/
@[simps]
def left_unitor (Î· : pseudonat_trans F G) : (pseudonat_trans.id F).vcomp Î· â‰… Î· :=
modification_iso.of_components (Î» a, Î»_ (Î·.app a))
begin
  intros a b f,
  dsimp,
  simp [triangle_assoc_comp_right_assoc],
  rw [â†left_unitor_comp, left_unitor_naturality, left_unitor_comp],
  simp
end

/--
Right unitor for the vertical composition between pseudonatural transformations.
-/
@[simps]
def right_unitor  (Î· : pseudonat_trans F G) : Î·.vcomp (pseudonat_trans.id G) â‰… Î· :=
modification_iso.of_components (Î» a, Ï_ (Î·.app a))
begin
  intros a b f,
  dsimp,
  simp [triangle_assoc_comp_right_assoc],
  rw [â†right_unitor_comp, right_unitor_naturality, right_unitor_comp],
  simp
end

end

end pseudonat_trans


/--
A bicategory structure on the oplax_functors between bicategories. The 1-morphisms in this bicategory are
the pseudonatural transformations, and the composition of 1-morphisms is the vertical composition
of pseudonatural transformations. The 2-morphisms are the modifications.
-/
@[simps]
instance : bicategory (pseudofunctor B C) :=
{ hom := pseudonat_trans,
  id := pseudonat_trans.id,
  hom_category := pseudonat_trans.category,
  comp := Î» F G H, pseudonat_trans.vcomp,
  whisker_left  := Î» F G H Î· _ _ Î“, oplax_nat_trans.whisker_left Î·.to_oplax_nat_trans Î“,
  whisker_right := Î» F G H _ _ Î“ Î·, oplax_nat_trans.whisker_right Î“ Î·.to_oplax_nat_trans,
  associator := Î» F G H I Î· Î¸ Î¹, pseudonat_trans.associator Î· Î¸ Î¹,
  left_unitor   := Î» F G Î·, pseudonat_trans.left_unitor Î·,
  right_unitor  := Î» F G Î·, pseudonat_trans.right_unitor Î·,
  associator_naturality_left'   := by { intros, ext, dsimp, rw associator_naturality_left },
  associator_naturality_middle' := by { intros, ext, dsimp, rw associator_naturality_middle },
  associator_naturality_right'  := by { intros, ext, dsimp, rw associator_naturality_right },
  left_unitor_naturality'   := by { intros, ext, dsimp, rw left_unitor_naturality },
  right_unitor_naturality'  := by { intros, ext, dsimp, rw right_unitor_naturality },
  pentagon' := by { intros, ext, dsimp, rw pentagon },
  triangle' := by { intros, ext, dsimp, rw triangle } }

end category_theory
