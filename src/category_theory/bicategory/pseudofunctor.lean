/-
Copyright (c) 2021 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.basic

/-!
# Pseudofunctors

A pseudofunctor `F` between bicategories `B` and `C` consists of
* a function on objects `mapâ‚€ : B âŸ¶ C`,
* a function on 1-morphisms `mapâ‚ : (a âŸ¶ b) â†’ (mapâ‚€ a âŸ¶ mapâ‚€ b)`,
* a function on 2-morphisms `mapâ‚‚ : (f âŸ¶ g) â†’ (mapâ‚ f âŸ¶ mapâ‚ g)`,
* an isormorphism `mapâ‚_comp : ğŸ™ (mapâ‚€ a) â‰… mapâ‚ (ğŸ™ a)`,
* an isormorphism `mapâ‚_id : mapâ‚ f â‰« mapâ‚ g â‰… mapâ‚ (f â‰« g)`, and
* certain conditions on them.

The direction of isomorphisms `mapâ‚_comp` and `mapâ‚_id` here is the lax direction.

## Future work
* Lax and oplax functors.
-/

open category_theory

universes wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open category_theory.bicategory

namespace category_theory

section

variables
(B : Type uâ‚) [bicategory.{wâ‚ vâ‚} B]
(C : Type uâ‚‚) [bicategory.{wâ‚‚ vâ‚‚} C]

/--
A pseudofunctor `F` between bicategories `B` and `C` consists of a function on objects,
a function on 1-morphisms, and a function on 2-morphisms,

Unlike functors between categories, functions between 1-morphisms do not need to strictly commute
with compositions, and do not need to strictly preserve identities. Instead, there are specified
isomorphisms `ğŸ™ (mapâ‚€ a) â‰… mapâ‚ (ğŸ™ a)` and `mapâ‚ f â‰« mapâ‚ g â‰… mapâ‚ (f â‰« g)`.

Functions between 2-morphisms preserve associator, left unitor, and right unitor.
-/
structure pseudofunctor :=
(mapâ‚€ : B â†’ C)
(mapâ‚ {a b : B} : (a âŸ¶ b) â†’ (mapâ‚€ a âŸ¶ mapâ‚€ b))
(mapâ‚‚ {a b : B} {f g : a âŸ¶ b} : (f âŸ¶ g) â†’ (mapâ‚ f âŸ¶ mapâ‚ g))
(mapâ‚_id (a : B) : ğŸ™ (mapâ‚€ a) â‰… mapâ‚ (ğŸ™ a))
(mapâ‚_comp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : mapâ‚ f â‰« mapâ‚ g â‰… mapâ‚ (f â‰« g))
(mapâ‚_comp_naturality_left' : âˆ€ {a b c} {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c),
  (mapâ‚‚ Î· â–· mapâ‚ g) â‰« (mapâ‚_comp f' g).hom
  = (mapâ‚_comp f g).hom â‰« mapâ‚‚ (Î· â–· g) . obviously)
(mapâ‚_comp_naturality_right' : âˆ€ {a b c} (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g'),
  (mapâ‚ f â— mapâ‚‚ Î·) â‰« (mapâ‚_comp f g').hom
  = (mapâ‚_comp f g).hom â‰« mapâ‚‚ (f â— Î·) . obviously)
(mapâ‚‚_id' : âˆ€ {a b} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (mapâ‚ f) . obviously)
(mapâ‚‚_comp' : âˆ€ {a b} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h),
  mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ . obviously)
(mapâ‚‚_associator' : âˆ€ {a b c d} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
  ((mapâ‚_comp f g).hom â–· mapâ‚ h) â‰« (mapâ‚_comp (f â‰« g) h).hom â‰« mapâ‚‚ (Î±_ f g h).hom
  = (Î±_ (mapâ‚ f) (mapâ‚ g) (mapâ‚ h)).hom â‰« (mapâ‚ f â— (mapâ‚_comp g h).hom)
  â‰« (mapâ‚_comp f (g â‰« h)).hom . obviously)
(mapâ‚‚_left_unitor' : âˆ€ {a b} (f : a âŸ¶ b),
  ((mapâ‚_id a).hom â–· mapâ‚ f) â‰« (mapâ‚_comp (ğŸ™ a) f).hom â‰« mapâ‚‚ (Î»_ f).hom
  = (Î»_ (mapâ‚ f)).hom . obviously)
(mapâ‚‚_right_unitor' : âˆ€ {a b} (f : a âŸ¶ b),
  (mapâ‚ f â— (mapâ‚_id b).hom) â‰« (mapâ‚_comp f (ğŸ™ b)).hom â‰« mapâ‚‚ (Ï_ f).hom
  = (Ï_ (mapâ‚ f)).hom . obviously)

restate_axiom pseudofunctor.mapâ‚_comp_naturality_left'
attribute [simp, reassoc] pseudofunctor.mapâ‚_comp_naturality_left
restate_axiom pseudofunctor.mapâ‚_comp_naturality_right'
attribute [simp, reassoc] pseudofunctor.mapâ‚_comp_naturality_right
restate_axiom pseudofunctor.mapâ‚‚_id'
attribute [simp] pseudofunctor.mapâ‚‚_id
restate_axiom pseudofunctor.mapâ‚‚_comp'
attribute [reassoc, simp] pseudofunctor.mapâ‚‚_comp
restate_axiom pseudofunctor.mapâ‚‚_associator'
attribute [reassoc] pseudofunctor.mapâ‚‚_associator
restate_axiom pseudofunctor.mapâ‚‚_left_unitor'
attribute [reassoc] pseudofunctor.mapâ‚‚_left_unitor
restate_axiom pseudofunctor.mapâ‚‚_right_unitor'
attribute [reassoc] pseudofunctor.mapâ‚‚_right_unitor

end

namespace pseudofunctor

section

variables (B : Type uâ‚) [bicategory.{wâ‚ vâ‚} B]

/-- The identity pseudofunctor. -/
@[simps] def id : pseudofunctor B B :=
{ mapâ‚€ := id,
  mapâ‚ := Î» a b, @id (a âŸ¶ b),
  mapâ‚‚ := Î» a b f g, @id (f âŸ¶ g),
  mapâ‚_comp := Î» a b c f g, iso.refl (f â‰« g),
  mapâ‚_id := Î» a, iso.refl (ğŸ™ a) }

instance : inhabited (pseudofunctor B B) := âŸ¨id BâŸ©

end

section
variables
{B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B]
{C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]
(F : pseudofunctor B C) {a b c d : B}

/--
Function on 1-morphisms as a functor.
-/
@[simps]
def mapâ‚_functor (a b : B) :
  (a âŸ¶ b) â¥¤ (F.mapâ‚€ a âŸ¶ F.mapâ‚€ b) :=
{ obj := Î» f, F.mapâ‚ f,
  map := Î» f g Î·, F.mapâ‚‚ Î·,
  map_id' := Î» f, F.mapâ‚‚_id f,
  map_comp' := Î» f g h Î· Î¸, F.mapâ‚‚_comp Î· Î¸ }

@[reassoc]
lemma mapâ‚‚_associator_inv (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
  ((F.mapâ‚ f) â— (F.mapâ‚_comp g h).hom) â‰« (F.mapâ‚_comp f (g â‰« h)).hom â‰« F.mapâ‚‚ (Î±_ f g h).inv
  = (Î±_ (F.mapâ‚ f) (F.mapâ‚ g) (F.mapâ‚ h)).inv â‰« ((F.mapâ‚_comp f g).hom â–· (F.mapâ‚ h))
  â‰« (F.mapâ‚_comp (f â‰« g) h).hom :=
begin
  rw [iso.eq_inv_comp, â†mapâ‚‚_associator_assoc,
    â†mapâ‚‚_comp, iso.hom_inv_id, mapâ‚‚_id, category.comp_id]
end

@[reassoc, simp]
lemma mapâ‚‚_associator_eq (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
  F.mapâ‚‚ (Î±_ f g h).hom
  = (F.mapâ‚_comp (f â‰« g) h).inv â‰« ((F.mapâ‚_comp f g).inv â–· F.mapâ‚ h)
  â‰« (Î±_ (F.mapâ‚ f) (F.mapâ‚ g) (F.mapâ‚ h)).hom â‰« (F.mapâ‚ f â— (F.mapâ‚_comp g h).hom)
  â‰« (F.mapâ‚_comp f (g â‰« h)).hom :=
begin
  apply (cancel_epi (F.mapâ‚_comp (f â‰« g) h).hom).1,
  apply (cancel_epi ((F.mapâ‚_comp f g).hom â–· F.mapâ‚ h)).1,
  rw mapâ‚‚_associator,
  simp
end

@[reassoc, simp]
lemma mapâ‚‚_associator_inv_eq (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
  F.mapâ‚‚ (Î±_ f g h).inv
  = (F.mapâ‚_comp f (g â‰« h)).inv â‰« (F.mapâ‚ f â— (F.mapâ‚_comp g h).inv)
  â‰« (Î±_ (F.mapâ‚ f) (F.mapâ‚ g) (F.mapâ‚ h)).inv â‰« ((F.mapâ‚_comp f g).hom â–· F.mapâ‚ h)
  â‰« (F.mapâ‚_comp (f â‰« g) h).hom :=
begin
  apply (cancel_epi (F.mapâ‚_comp f (g â‰« h)).hom).1,
  apply (cancel_epi ((F.mapâ‚ f) â— (F.mapâ‚_comp g h).hom)).1,
  rw mapâ‚‚_associator_inv,
  simp
end

@[reassoc, simp]
lemma mapâ‚‚_left_unitor_eq (f : a âŸ¶ b) :
  F.mapâ‚‚ (Î»_ f).hom
  = (F.mapâ‚_comp (ğŸ™ a) f).inv â‰« ((F.mapâ‚_id a).inv â–· F.mapâ‚ f) â‰« (Î»_ (F.mapâ‚ f)).hom :=
begin
  rw [iso.eq_inv_comp, â†mapâ‚‚_left_unitor, inv_hom_whisker_right_assoc]
end

@[reassoc, simp]
lemma mapâ‚‚_left_unitor_inv_eq (f : a âŸ¶ b) :
  F.mapâ‚‚ (Î»_ f).inv
  = (Î»_ (F.mapâ‚ f)).inv â‰« ((F.mapâ‚_id a).hom â–· F.mapâ‚ f) â‰« (F.mapâ‚_comp (ğŸ™ a) f).hom :=
begin
  rw [iso.eq_inv_comp, â†mapâ‚‚_left_unitor, category.assoc, category.assoc,
      â†mapâ‚‚_comp, iso.hom_inv_id, F.mapâ‚‚_id, category.comp_id]
end

@[reassoc, simp]
lemma mapâ‚‚_right_unitor_eq (f : a âŸ¶ b) :
  F.mapâ‚‚ (Ï_ f).hom
  = (F.mapâ‚_comp f (ğŸ™ b)).inv â‰« (F.mapâ‚ f â— (F.mapâ‚_id b).inv) â‰« (Ï_ (F.mapâ‚ f)).hom :=
begin
  rw [iso.eq_inv_comp, â†mapâ‚‚_right_unitor, inv_hom_whisker_left_assoc]
end

@[reassoc, simp]
lemma mapâ‚‚_right_unitor_inv_eq (f : a âŸ¶ b) :
  F.mapâ‚‚ (Ï_ f).inv
  = (Ï_ (F.mapâ‚ f)).inv â‰« (F.mapâ‚ f â— (F.mapâ‚_id b).hom) â‰« (F.mapâ‚_comp f (ğŸ™ b)).hom :=
begin
  rw [iso.eq_inv_comp, â†mapâ‚‚_right_unitor, category.assoc, category.assoc,
      â†mapâ‚‚_comp, iso.hom_inv_id, F.mapâ‚‚_id, category.comp_id]
end

@[simp, reassoc]
lemma mapâ‚_comp_inv_naturality_left {f f' : a âŸ¶ b} (Î· : f âŸ¶ f') (g : b âŸ¶ c) :
  (F.mapâ‚‚ (Î· â–· g)) â‰« (F.mapâ‚_comp f' g).inv
  = (F.mapâ‚_comp f g).inv â‰« (F.mapâ‚‚ Î· â–· F.mapâ‚ g) :=
by rw [iso.comp_inv_eq, category.assoc, mapâ‚_comp_naturality_left, iso.inv_hom_id_assoc]

@[simp, reassoc]
lemma mapâ‚_comp_inv_naturality_right (f : a âŸ¶ b) {g g' : b âŸ¶ c} (Î· : g âŸ¶ g') :
  (F.mapâ‚‚ (f â— Î·)) â‰« (F.mapâ‚_comp f g').inv
  = (F.mapâ‚_comp f g).inv â‰« (F.mapâ‚ f â— F.mapâ‚‚ Î·) :=
by rw [iso.comp_inv_eq, category.assoc, mapâ‚_comp_naturality_right, iso.inv_hom_id_assoc]

end

section

variables
{A : Type uâ‚} [bicategory.{wâ‚ vâ‚} A]
{B : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} B]
{C : Type uâ‚ƒ} [bicategory.{wâ‚ƒ vâ‚ƒ} C]
(F : pseudofunctor.{wâ‚ wâ‚‚ vâ‚ vâ‚‚} A B) (G : pseudofunctor.{wâ‚‚ wâ‚ƒ vâ‚‚ vâ‚ƒ} B C)

/--
If `F` is a pseudofunctor from `A` to `B` and `G` is a pseudofunctor from `B` to `C`,
`F.comp G` is a pseudofunctor from `A` to `C`.
-/
@[simps] def comp : pseudofunctor.{wâ‚ wâ‚ƒ vâ‚ vâ‚ƒ} A C :=
{ mapâ‚€ := Î» a, G.mapâ‚€ (F.mapâ‚€ a),
  mapâ‚ := Î» a b f, G.mapâ‚ (F.mapâ‚ f),
  mapâ‚‚ := Î» a b f g Î·, G.mapâ‚‚ (F.mapâ‚‚ Î·),
  mapâ‚_id := Î» a,
    G.mapâ‚_id (F.mapâ‚€ a) â‰ªâ‰« (G.mapâ‚_functor _ _).map_iso (F.mapâ‚_id a),
  mapâ‚_comp := Î» a b c f g,
    G.mapâ‚_comp (F.mapâ‚ f) (F.mapâ‚ g) â‰ªâ‰« (G.mapâ‚_functor _ _).map_iso (F.mapâ‚_comp f g),
  mapâ‚_comp_naturality_left' := Î» a b c f f' Î· g, by
  { dsimp,
    rw [mapâ‚_comp_naturality_left_assoc, â†mapâ‚‚_comp, mapâ‚_comp_naturality_left],
    simp},
  mapâ‚_comp_naturality_right' := Î» a b c f g g' Î·, by
  { dsimp,
    rw [mapâ‚_comp_naturality_right_assoc, â†mapâ‚‚_comp, mapâ‚_comp_naturality_right],
    simp },
  mapâ‚‚_id'    := by { intros, simp only [mapâ‚‚_id] },
  mapâ‚‚_comp'  := by { intros, simp only [mapâ‚‚_comp] },
  mapâ‚‚_associator' := Î» a b c d f g h, by
  { dsimp,
    simp only [whisker_right_comp, whisker_left_comp, category.assoc],
    rw [mapâ‚_comp_naturality_left_assoc, mapâ‚_comp_naturality_right_assoc,
        â†mapâ‚‚_associator_assoc],
    simp only [â†mapâ‚‚_comp],
    rw â†mapâ‚‚_associator },
  mapâ‚‚_left_unitor' := Î» a b f, by
  { dsimp,
    simp only [whisker_right_comp, category.assoc],
    rw [mapâ‚_comp_naturality_left_assoc, â†mapâ‚‚_left_unitor],
    simp only [â†mapâ‚‚_comp],
    rw â†mapâ‚‚_left_unitor },
  mapâ‚‚_right_unitor' := Î» a b f, by
  { dsimp,
    simp only [whisker_left_comp, category.assoc],
    rw [mapâ‚_comp_naturality_right_assoc, â†mapâ‚‚_right_unitor],
    simp only [â†mapâ‚‚_comp],
    rw â†mapâ‚‚_right_unitor } }

end

end pseudofunctor

end category_theory
