/-
Copyright (c) 2021 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import category_theory.bicategory.pseudofunctor

/-!
# Pseudonatural transformations

Just as there are natural transformations between functors, there are pseudonatural
transformations between pseudofunctors. The equality in the naturality of natural
transformations is replaced by an specified isomorphism
`F.mapâ‚ f â‰« app b â‰… app a â‰« G.mapâ‚ f`
in the case of pseudonatural transformations.

We give a bicategory structure on the pseudofunctors between bicategories. In this bicategory,
* 1-morphisms are are the pseudonatural transformations, given by `pseudonat_trans F G`,
* the composition of 1-morphisms is the vertical composition, given by `Î·.vcomp Î¸`,
* 2-morphisms are the modifications, given by `modification Î· Î¸`.
-/

open category_theory

universes wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace category_theory

open bicategory

variables
{B : Type uâ‚} [bicategory.{wâ‚ vâ‚} B]
{C : Type uâ‚‚} [bicategory.{wâ‚‚ vâ‚‚} C]

/--
If `Î·` is a pseudonatural transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.mapâ‚€ a âŸ¶ G.mapâ‚€ a` for each object `a : B`. We also have an isomorphism
`Î·.naturality f : F.mapâ‚ f â‰« app b â‰… app a â‰« G.mapâ‚ f` for each 1-morphisms `f : a âŸ¶ b`.
This family of isomorphisms satisfies certain equations.
-/
structure pseudonat_trans (F G : pseudofunctor B C) :=
(app (a : B) : F.mapâ‚€ a âŸ¶ G.mapâ‚€ a)
(naturality {a b} (f : a âŸ¶ b) : F.mapâ‚ f â‰« app b â‰… app a â‰« G.mapâ‚ f)
(naturality_naturality' : âˆ€ {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
  (F.mapâ‚‚ Î· â–· _) â‰« (naturality g).hom = (naturality f).hom â‰« (_ â— G.mapâ‚‚ Î·) . obviously)
(naturality_id' : âˆ€ (a),
  ((F.mapâ‚_id a).hom â–· _) â‰« (naturality (ğŸ™ a)).hom
  = (Î»_ _).hom  â‰« (Ï_ _).inv â‰« (_ â— (G.mapâ‚_id a).hom) . obviously)
(naturality_comp' : âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c),
  ((F.mapâ‚_comp f g).hom â–· _) â‰« (naturality (f â‰« g)).hom
  = (Î±_ _ _ _).hom â‰« (_ â— (naturality g).hom)
  â‰« (Î±_ _ _ _).inv â‰« ((naturality f).hom â–· G.mapâ‚ g)
  â‰« (Î±_ _ _ _).hom â‰« (_ â— (G.mapâ‚_comp f g).hom) . obviously)

restate_axiom pseudonat_trans.naturality_naturality'
attribute [simp, reassoc] pseudonat_trans.naturality_naturality
restate_axiom pseudonat_trans.naturality_comp'
attribute [simp, reassoc] pseudonat_trans.naturality_comp
restate_axiom pseudonat_trans.naturality_id'
attribute [simp, reassoc] pseudonat_trans.naturality_id

namespace pseudonat_trans

/--
The identity pseudonatural transformation.
-/
@[simps]
def id (F : pseudofunctor B C) : pseudonat_trans F F :=
{ app := Î» a, ğŸ™ (F.mapâ‚€ a),
  naturality := Î» a b f, (Ï_ _) â‰ªâ‰« (Î»_ _).symm,
  naturality_naturality' := Î» a b f f' Î·, by
  { dsimp, rw [right_unitor_naturality_assoc, left_unitor_inv_naturality], simp, },
  naturality_comp' := Î» a b c f g, by
  { dsimp,
    rw [right_unitor_naturality_assoc, left_unitor_inv_naturality],
    simp },
  naturality_id' := Î» a, by
  { dsimp,
    rw [right_unitor_naturality_assoc, left_unitor_inv_naturality,
      unitors_equal, unitors_inv_equal] } }

instance (F : pseudofunctor B C) : inhabited (pseudonat_trans F F) := âŸ¨id FâŸ©

section
variables {F G H : pseudofunctor B C}
(Ï† : pseudonat_trans F G) (Ïˆ : pseudonat_trans G H) {a b c : B} {a' : C}

@[simp, reassoc]
lemma whisker_left_naturality_naturality (f : a' âŸ¶ G.mapâ‚€ a) {g h : a âŸ¶ b} (Î· : g âŸ¶ h) :
  (f â— (G.mapâ‚‚ Î· â–· Ïˆ.app b)) â‰« (f â— (Ïˆ.naturality h).hom)
  = (f â— (Ïˆ.naturality g).hom) â‰« (f â— (Ïˆ.app a â— H.mapâ‚‚ Î·)) :=
by { simp only [â†whisker_left_comp], rw naturality_naturality }

@[simp, reassoc]
lemma whisker_right_naturality_naturality {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : G.mapâ‚€ b âŸ¶ a') :
  ((F.mapâ‚‚ Î· â–· Ï†.app b) â–· h) â‰« ((Ï†.naturality g).hom â–· h)
  = ((Ï†.naturality f).hom â–· h) â‰« ((Ï†.app a â— G.mapâ‚‚ Î·) â–· h) :=
by { simp only [â†whisker_right_comp], rw naturality_naturality }

@[simp, reassoc]
lemma whisker_left_naturality_comp (f : a' âŸ¶ G.mapâ‚€ a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
  (f â— ((G.mapâ‚_comp g h).hom â–· _)) â‰« (f â— (Ïˆ.naturality (g â‰« h)).hom)
  = (f â— (Î±_ _ _ _).hom) â‰« (f â— (_ â— (Ïˆ.naturality h).hom))
  â‰« (f â— (Î±_ _ _ _).inv) â‰« (f â— ((Ïˆ.naturality g).hom â–· H.mapâ‚ h))
  â‰« (f â— (Î±_ _ _ _).hom) â‰« (f â— (_ â— (H.mapâ‚_comp g h).hom)) :=
by { simp only [â†whisker_left_comp], rw naturality_comp }

@[simp, reassoc]
lemma whisker_right_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.mapâ‚€ c âŸ¶ a')  :
  (((F.mapâ‚_comp f g).hom â–· _) â–· h) â‰« ((Ï†.naturality (f â‰« g)).hom â–· h)
  = ((Î±_ _ _ _).hom â–· h) â‰« ((_ â— (Ï†.naturality g).hom) â–· h)
  â‰« ((Î±_ _ _ _).inv â–· h) â‰« (((Ï†.naturality f).hom â–· G.mapâ‚ g) â–· h)
  â‰« ((Î±_ _ _ _).hom â–· h) â‰« ((_ â— (G.mapâ‚_comp f g).hom) â–· h) :=
by { simp only [â†whisker_right_comp], rw naturality_comp }

@[simp, reassoc]
lemma whisker_left_naturality_id (f : a' âŸ¶ G.mapâ‚€ a) :
  (f â— ((G.mapâ‚_id a).hom â–· _)) â‰« (f â— (Ïˆ.naturality (ğŸ™ a)).hom)
  = (f â— (Î»_ _).hom) â‰« (f â— (Ï_ _).inv) â‰« (f â— (_ â— (H.mapâ‚_id a).hom)) :=
by { simp only [â†whisker_left_comp], rw naturality_id }

@[simp, reassoc]
lemma whisker_right_naturality_id (f : G.mapâ‚€ a âŸ¶ a') :
  (((F.mapâ‚_id a).hom â–· _) â–· f) â‰« ((Ï†.naturality (ğŸ™ a)).hom â–· f)
  = ((Î»_ _).hom â–· f) â‰« ((Ï_ _).inv â–· f) â‰« ((_ â— (G.mapâ‚_id a).hom) â–· f) :=
by { simp only [â†whisker_right_comp], rw naturality_id }

end

/--
Vertical composition of pseudonatural transformations.
-/
@[simps]
def vcomp {F G H : pseudofunctor B C} (Î· : pseudonat_trans F G) (Î¸ : pseudonat_trans G H) :
  pseudonat_trans F H :=
{ app := Î» a, Î·.app a â‰« Î¸.app a,
  naturality := Î» a b f,
    (Î±_ _ _ _).symm
    â‰ªâ‰« whisker_right_iso (Î·.naturality f) (Î¸.app b)
    â‰ªâ‰« (Î±_ _ _ _)
    â‰ªâ‰« whisker_left_iso (Î·.app a) (Î¸.naturality f)
    â‰ªâ‰« (Î±_ _ _ _).symm,
  naturality_naturality' := Î» a b f g Î¹, by
  { dsimp,
    rw [associator_inv_naturality_left_assoc, whisker_right_naturality_naturality_assoc,
        associator_naturality_middle_assoc, whisker_left_naturality_naturality_assoc,
        associator_inv_naturality_right],
    simp },
  naturality_comp' := Î» a b c f g, by
  { dsimp,
    rw [associator_inv_naturality_left_assoc, whisker_right_naturality_comp_assoc,
        associator_naturality_middle_assoc, whisker_left_naturality_comp_assoc,
        associator_inv_naturality_right],
    simp only [whisker_right_comp, whisker_left_comp, category.assoc],
    rw [â†pentagon_inv_hom_hom_hom_inv_assoc, â†associator_naturality_middle_assoc,
        â†pentagon_hom_hom_inv_inv_hom_assoc, associator_naturality_middle_assoc (Î·.app a)],
    slice_rhs 4 12
    { rw [â†pentagon_inv_hom_hom_hom_hom_assoc, â†pentagon_hom_hom_inv_hom_hom,
          associator_naturality_left_assoc, â†associator_naturality_right_assoc,
          pentagon_inv_inv_hom_hom_inv_assoc, inv_hom_whisker_left_assoc, iso.hom_inv_id_assoc,
          whisker_exchange_assoc, associator_naturality_right_assoc,
          â†associator_naturality_left_assoc, â†pentagon_assoc] },
    simp only [category.assoc] },
  naturality_id' := Î» a, by
  { dsimp,
    rw [associator_inv_naturality_left_assoc, whisker_right_naturality_id_assoc,
        associator_naturality_middle_assoc, whisker_left_naturality_id_assoc,
        associator_inv_naturality_right],
    simp } }


end pseudonat_trans

section
variables {F G H : pseudofunctor B C}

/--
A modification `Î“` between pseudonatural transformations `Î·` and `Î¸` consists of a family of
2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.mapâ‚ f â— app b) â‰« (Î¸.naturality f).hom = (Î·.naturality f).hom â‰« (app a â–· G.mapâ‚ f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure modification (Î· Î¸ : pseudonat_trans F G) :=
(app (a : B) : Î·.app a âŸ¶ Î¸.app a)
(naturality' : âˆ€ {a b : B} (f : a âŸ¶ b),
  (F.mapâ‚ f â— app b) â‰« (Î¸.naturality f).hom
  = (Î·.naturality f).hom â‰« (app a â–· G.mapâ‚ f) . obviously)

restate_axiom modification.naturality'
attribute [reassoc] modification.naturality

namespace modification

/--
Vertical composition of modifications.
-/
@[simps]
def vcomp  (Î· Î¸ Î¹ : pseudonat_trans F G)
  (Î“ : modification Î· Î¸) (Î” : modification Î¸ Î¹) : modification Î· Î¹ :=
{ app := Î» a, Î“.app a â‰« Î”.app a,
  naturality' := Î» a b f, by { simp [naturality_assoc, naturality] } }

/--
The identity modification.
-/
@[simps]
def id (Î· : pseudonat_trans F G) : modification Î· Î· :=
{ app := Î» a, ğŸ™ (Î·.app a) }

instance (Î· : pseudonat_trans F G) : inhabited (modification Î· Î·) := âŸ¨modification.id Î·âŸ©

section

variables {Î· Î¸ : pseudonat_trans F G} (Î“ : modification Î· Î¸) {a b c : B} {a' : C}

@[reassoc]
lemma whisker_left_naturality (f : a' âŸ¶ F.mapâ‚€ b) (g : b âŸ¶ c) :
  (f â— (_ â— Î“.app c)) â‰« (f â— (Î¸.naturality g).hom)
  = (f â— (Î·.naturality g).hom) â‰« (f â— (Î“.app b â–· _)) :=
by { simp only [â†whisker_left_comp], rw modification.naturality }

@[reassoc]
lemma whisker_right_naturality (f : a âŸ¶ b) (g : G.mapâ‚€ b âŸ¶ a') :
  ((_ â— Î“.app b) â–· g) â‰« ((Î¸.naturality f).hom â–· g)
  = ((Î·.naturality f).hom â–· g) â‰« ((Î“.app a â–· _) â–· g) :=
by { simp only [â†whisker_right_comp], rw modification.naturality }

end

end modification

variables (F G)

/--
A category structure on the pseudonatural transformations between pseudofunctors.
-/
@[simps]
instance : category (pseudonat_trans F G) :=
{ hom   := modification,
  id    := modification.id,
  comp  := modification.vcomp }

end

/--
Construct a modification isomorphism between pseudonatural transformation
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def modification_iso.of_components {F G : pseudofunctor B C} {Î· Î¸ : pseudonat_trans F G}
  (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
  (naturality : âˆ€ {a b} (f : a âŸ¶ b),
    (_ â— (app b).hom) â‰« (Î¸.naturality f).hom = (Î·.naturality f).hom â‰« ((app a).hom â–· _)) :
      Î· â‰… Î¸ :=
{ hom := { app := Î» a, (app a).hom },
  inv :=
  { app := Î» a, (app a).inv,
    naturality' := Î» a b f, by
    { have h := congr_arg (Î» f, (_ â— (app b).inv) â‰« f â‰« ((app a).inv â–· _)) (naturality f).symm,
      simp only [category.comp_id, inv_hom_whisker_left_assoc, category.assoc,
        hom_inv_whisker_right] at h,
      exact h } } }

namespace pseudonat_trans

section
variables {F G H I : pseudofunctor B C}

/--
Left whiskering of a pseudonatural transformation and a modification.
-/
@[simps] def whisker_left
  (Î· : pseudonat_trans F G) {Î¸ Î¹ : pseudonat_trans G H} (Î“ : modification Î¸ Î¹) :
    modification (Î·.vcomp Î¸) (Î·.vcomp Î¹) :=
{ app := Î» a, Î·.app a â— Î“.app a,
  naturality' := Î» a b f, by
  { dsimp,
    simp only [category.assoc],
    rw [associator_inv_naturality_right_assoc, whisker_exchange_assoc,
        associator_naturality_right_assoc, Î“.whisker_left_naturality_assoc,
        associator_inv_naturality_middle] } }

/--
Right whiskering of a pseudonatural transformation and a modification.
-/
@[simps] def whisker_right
  {Î· Î¸ : pseudonat_trans F G} (Î“ : modification Î· Î¸) (Î¹ : pseudonat_trans G H) :
    modification (Î·.vcomp Î¹) (Î¸.vcomp Î¹) :=
{ app := Î» a, Î“.app a â–· Î¹.app a,
  naturality' := Î» a b f, by
  { dsimp,
    simp only [category.assoc],
    rw [associator_inv_naturality_middle_assoc, Î“.whisker_right_naturality_assoc,
        associator_naturality_left_assoc, â†whisker_exchange_assoc,
        associator_inv_naturality_left] } }

/--
Associator for the vertical composition between pseudonatural transformations.
-/
@[simps] def associator
  (Î· : pseudonat_trans F G) (Î¸ : pseudonat_trans G H) (Î¹ : pseudonat_trans H I) :
    (Î·.vcomp Î¸).vcomp Î¹ â‰… Î·.vcomp (Î¸.vcomp Î¹) :=
modification_iso.of_components (Î» a, Î±_ (Î·.app a) (Î¸.app a) (Î¹.app a))
begin
  intros a b f,
  dsimp,
  simp only [whisker_right_comp, whisker_left_comp, category.assoc],
  rw [â†pentagon_inv_inv_hom_hom_inv_assoc, â†associator_naturality_left_assoc,
      pentagon_hom_hom_inv_hom_hom_assoc, â†associator_naturality_middle_assoc,
      â†pentagon_inv_hom_hom_hom_hom_assoc, â†associator_naturality_right_assoc,
      pentagon_hom_inv_inv_inv_hom]
end

/--
Left unitor for the vertical composition between pseudonatural transformations.
-/
@[simps]
def left_unitor (Î· : pseudonat_trans F G) : (pseudonat_trans.id F).vcomp Î· â‰… Î· :=
modification_iso.of_components (Î» a, Î»_ (Î·.app a))
begin
  intros a b f,
  dsimp,
  simp [triangle_assoc_comp_right_assoc],
  rw [â†left_unitor_comp, left_unitor_naturality, left_unitor_comp],
  simp
end

/--
Right unitor for the vertical composition between pseudonatural transformations.
-/
@[simps]
def right_unitor  (Î· : pseudonat_trans F G) : Î·.vcomp (pseudonat_trans.id G) â‰… Î· :=
modification_iso.of_components (Î» a, Ï_ (Î·.app a))
begin
  intros a b f,
  dsimp,
  simp [triangle_assoc_comp_right_assoc],
  rw [â†right_unitor_comp, right_unitor_naturality, right_unitor_comp],
  simp
end

end

end pseudonat_trans

section
variables (B C)

/--
A bicategory structure on the pseudofunctors between bicategories. The 1-morphisms in this bicategory are
the pseudonatural transformations, and the composition of 1-morphisms is the vertical composition
of pseudonatural transformations. The 2-morphisms are the modifications.
-/
@[simps]
instance : bicategory (pseudofunctor B C) :=
{ hom := pseudonat_trans,
  id := pseudonat_trans.id,
  hom_category := pseudonat_trans.category,
  comp := Î» F G H, pseudonat_trans.vcomp,
  whisker_left  := Î» F G H Î· _ _ Î“, pseudonat_trans.whisker_left Î· Î“,
  whisker_right := Î» F G H _ _ Î“ Î·, pseudonat_trans.whisker_right Î“ Î·,
  associator := Î» F G H I, pseudonat_trans.associator,
  left_unitor   := Î» F G, pseudonat_trans.left_unitor,
  right_unitor  := Î» F G, pseudonat_trans.right_unitor,
  associator_naturality_left'   := by { intros, ext, dsimp, rw associator_naturality_left },
  associator_naturality_middle' := by { intros, ext, dsimp, rw associator_naturality_middle },
  associator_naturality_right'  := by { intros, ext, dsimp, rw associator_naturality_right },
  left_unitor_naturality'   := by { intros, ext, dsimp, rw left_unitor_naturality },
  right_unitor_naturality'  := by { intros, ext, dsimp, rw right_unitor_naturality },
  pentagon' := by { intros, ext, dsimp, rw pentagon },
  triangle' := by { intros, ext, dsimp, rw triangle } }

end

end category_theory
