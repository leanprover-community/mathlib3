/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Scott Morrison, Johannes Hรถlzl, Reid Barton

Defines a category, as a typeclass parametrised by the type of objects.
Introduces notations
  `X โถ Y` for the morphism spaces,
  `f โซ g` for composition in the 'arrows' convention.

Users may like to add `f โ g` for composition in the standard convention, using
```
local notation f ` โ `:80 g:80 := category.comp g f    -- type as \oo
```
-/

import tactic.basic
import tactic.tidy

universes v u  -- The order in this declaration matters: v often needs to be explicitly specified while u often can be omitted

namespace category_theory

/-
The propositional fields of `category` are annotated with the auto_param `obviously`,
which is defined here as a
[`replacer` tactic](https://github.com/leanprover/mathlib/blob/master/docs/tactics.md#def_replacer).
We then immediately set up `obviously` to call `tidy`. Later, this can be replaced with more
powerful tactics.
-/
def_replacer obviously
@[obviously] meta def obviously' := tactic.tidy

class has_hom (obj : Type u) : Type (max u v) :=
(hom : obj โ obj โ Sort v)

infixr ` โถ `:10 := has_hom.hom -- type as \h

class category_struct (obj : Type u)
extends has_hom.{v} obj : Type (max u v) :=
(id       : ฮ X : obj, hom X X)
(comp     : ฮ {X Y Z : obj}, (X โถ Y) โ (Y โถ Z) โ (X โถ Z))

notation `๐` := category_struct.id -- type as \b1
infixr ` โซ `:80 := category_struct.comp -- type as \gg

/--
The typeclass `category C` describes morphisms associated to objects of type `C`.
The universe levels of the objects and morphisms are unconstrained, and will often need to be
specified explicitly, as `category.{v} C`. (See also `large_category` and `small_category`.)
-/
class category (obj : Type u)
extends category_struct.{v} obj : Type (max u v) :=
(id_comp' : โ {X Y : obj} (f : hom X Y), ๐ X โซ f = f . obviously)
(comp_id' : โ {X Y : obj} (f : hom X Y), f โซ ๐ Y = f . obviously)
(assoc'   : โ {W X Y Z : obj} (f : hom W X) (g : hom X Y) (h : hom Y Z),
  (f โซ g) โซ h = f โซ (g โซ h) . obviously)

-- `restate_axiom` is a command that creates a lemma from a structure field,
-- discarding any auto_param wrappers from the type.
-- (It removes a backtick from the name, if it finds one, and otherwise adds "_lemma".)
restate_axiom category.id_comp'
restate_axiom category.comp_id'
restate_axiom category.assoc'
attribute [simp] category.id_comp category.comp_id category.assoc
attribute [trans] category_struct.comp

lemma category.assoc_symm {C : Type u} [category.{v} C] {W X Y Z : C} (f : W โถ X) (g : X โถ Y) (h : Y โถ Z) :
  f โซ (g โซ h) = (f โซ g) โซ h :=
by rw โcategory.assoc

/--
A `large_category` has objects in one universe level higher than the universe level of
the morphisms. It is useful for examples such as the category of types, or the category
of groups, etc.
-/
abbreviation large_category (C : Type u) : Type u := category.{u} C
/--
A `small_category` has objects and morphisms in the same universe level.
-/
abbreviation small_category (C : Type u) : Type (u+1) := category.{u+1} C

section
variables {C : Type u} [๐ : category.{v} C] {X Y Z : C}
include ๐

lemma eq_of_comp_left_eq {f g : X โถ Y} (w : โ {Z : C} (h : Y โถ Z), f โซ h = g โซ h) : f = g :=
by { convert w (๐ Y), tidy }
lemma eq_of_comp_right_eq {f g : Y โถ Z} (w : โ {X : C} (h : X โถ Y), h โซ f = h โซ g) : f = g :=
by { convert w (๐ Y), tidy }

lemma eq_of_comp_left_eq' (f g : X โถ Y) (w : (ฮป {Z : C} (h : Y โถ Z), f โซ h) = (ฮป {Z : C} (h : Y โถ Z), g โซ h)) : f = g :=
eq_of_comp_left_eq (ฮป Z h, by convert congr_fun (congr_fun w Z) h)
lemma eq_of_comp_right_eq' (f g : Y โถ Z) (w : (ฮป {X : C} (h : X โถ Y), h โซ f) = (ฮป {X : C} (h : X โถ Y), h โซ g)) : f = g :=
eq_of_comp_right_eq (ฮป X h, by convert congr_fun (congr_fun w X) h)

lemma id_of_comp_left_id (f : X โถ X) (w : โ {Y : C} (g : X โถ Y), f โซ g = g) : f = ๐ X :=
by { convert w (๐ X), tidy }
lemma id_of_comp_right_id (f : X โถ X) (w : โ {Y : C} (g : Y โถ X), g โซ f = g) : f = ๐ X :=
by { convert w (๐ X), tidy }

class epi  (f : X โถ Y) : Prop :=
(left_cancellation : ฮ {Z : C} (g h : Y โถ Z) (w : f โซ g = f โซ h), g = h)
class mono (f : X โถ Y) : Prop :=
(right_cancellation : ฮ {Z : C} (g h : Z โถ X) (w : g โซ f = h โซ f), g = h)

@[simp] lemma cancel_epi  (f : X โถ Y) [epi f]  {g h : Y โถ Z} : (f โซ g = f โซ h) โ g = h :=
โจ ฮป p, epi.left_cancellation g h p, begin intro a, subst a end โฉ
@[simp] lemma cancel_mono (f : X โถ Y) [mono f] {g h : Z โถ X} : (g โซ f = h โซ f) โ g = h :=
โจ ฮป p, mono.right_cancellation g h p, begin intro a, subst a end โฉ
end

section
variable (C : Type u)
variable [category.{v} C]

universe u'

instance ulift_category : category.{v} (ulift.{u'} C) :=
{ hom  := ฮป X Y, (X.down โถ Y.down),
  id   := ฮป X, ๐ X.down,
  comp := ฮป _ _ _ f g, f โซ g }

-- We verify that this previous instance can lift small categories to large categories.
example (D : Type u) [small_category D] : large_category (ulift.{u+1} D) := by apply_instance
end

end category_theory

open category_theory

namespace preorder

variables (ฮฑ : Type u)

instance small_category [preorder ฮฑ] : category ฮฑ :=
{ hom  := (โค),
  id   := le_refl,
  comp := @le_trans _ _ }

end preorder
