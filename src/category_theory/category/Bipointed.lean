/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import category_theory.category.Pointed

/-!
# The category of bipointed types

This defines `Bipointed`, the category of bipointed types.

## TODO

Monoidal structure
-/

open category_theory

universes u
variables {Œ± Œ≤ : Type*}

/-- The category of bipointed types. -/
structure Bipointed : Type.{u + 1} :=
(X : Type.{u})
(to_prod : X √ó X)

namespace Bipointed

instance : has_coe_to_sort Bipointed Type* := ‚ü®X‚ü©

attribute [protected] Bipointed.X

/-- Turns a bipointing into a bipointed type. -/
def of {X : Type*} (to_prod : X √ó X) : Bipointed := ‚ü®X, to_prod‚ü©

@[simp] lemma coe_of {X : Type*} (to_prod : X √ó X) : ‚Ü•(of to_prod) = X := rfl

alias of ‚Üê prod.Bipointed

instance : inhabited Bipointed := ‚ü®of ((), ())‚ü©

/-- Morphisms in `Bipointed`. -/
@[ext] protected structure hom (X Y : Bipointed.{u}) : Type u :=
(to_fun : X ‚Üí Y)
(map_fst : to_fun X.to_prod.1 = Y.to_prod.1)
(map_snd : to_fun X.to_prod.2 = Y.to_prod.2)

namespace hom

/-- The identity morphism of `X : Bipointed`. -/
@[simps] def id (X : Bipointed) : hom X X := ‚ü®id, rfl, rfl‚ü©

instance (X : Bipointed) : inhabited (hom X X) := ‚ü®id X‚ü©

/-- Composition of morphisms of `Bipointed`. -/
@[simps] def comp {X Y Z : Bipointed.{u}} (f : hom X Y) (g : hom Y Z) : hom X Z :=
‚ü®g.to_fun ‚àò f.to_fun, by rw [function.comp_apply, f.map_fst, g.map_fst],
  by rw [function.comp_apply, f.map_snd, g.map_snd]‚ü©

end hom

instance large_category : large_category Bipointed :=
{ hom := hom,
  id := hom.id,
  comp := @hom.comp,
  id_comp' := Œª _ _ _, hom.ext _ _ rfl,
  comp_id' := Œª _ _ _, hom.ext _ _ rfl,
  assoc' := Œª _ _ _ _ _ _ _, hom.ext _ _ rfl }

instance concrete_category : concrete_category Bipointed :=
{ forget := { obj := Bipointed.X, map := @hom.to_fun },
  forget_faithful := ‚ü®@hom.ext‚ü© }

/-- Swaps the pointed elements of a bipointed type. `prod.swap` as a functor. -/
@[simps] def swap : Bipointed ‚•§ Bipointed :=
{ obj := Œª X, ‚ü®X, X.to_prod.swap‚ü©, map := Œª X Y f, ‚ü®f.to_fun, f.map_snd, f.map_fst‚ü© }

/-- The equivalence between `Bipointed` and itself induced by `prod.swap` both ways. -/
@[simps] def swap_equiv : Bipointed ‚âå Bipointed :=
equivalence.mk swap swap
  (nat_iso.of_components (Œª X, { hom := ‚ü®id, rfl, rfl‚ü©, inv := ‚ü®id, rfl, rfl‚ü© }) $ Œª X Y f, rfl)
  (nat_iso.of_components (Œª X, { hom := ‚ü®id, rfl, rfl‚ü©, inv := ‚ü®id, rfl, rfl‚ü© }) $ Œª X Y f, rfl)

@[simp] lemma swap_equiv_symm : swap_equiv.symm = swap_equiv := rfl

end Bipointed

/-- The forgetful functor from `Bipointed` to `Pointed` which forgets about the second point. -/
def Bipointed_to_Pointed_fst : Bipointed ‚•§ Pointed :=
{ obj := Œª X, ‚ü®X, X.to_prod.1‚ü©, map := Œª X Y f, ‚ü®f.to_fun, f.map_fst‚ü© }

/-- The forgetful functor from `Bipointed` to `Pointed` which forgets about the first point. -/
def Bipointed_to_Pointed_snd : Bipointed ‚•§ Pointed :=
{ obj := Œª X, ‚ü®X, X.to_prod.2‚ü©, map := Œª X Y f, ‚ü®f.to_fun, f.map_snd‚ü© }

@[simp] lemma Bipointed_to_Pointed_fst_comp_forget :
  Bipointed_to_Pointed_fst ‚ãô forget Pointed = forget Bipointed := rfl

@[simp] lemma Bipointed_to_Pointed_snd_comp_forget :
  Bipointed_to_Pointed_snd ‚ãô forget Pointed = forget Bipointed := rfl

@[simp] lemma swap_comp_Bipointed_to_Pointed_fst :
  Bipointed.swap ‚ãô Bipointed_to_Pointed_fst = Bipointed_to_Pointed_snd := rfl

@[simp] lemma swap_comp_Bipointed_to_Pointed_snd :
  Bipointed.swap ‚ãô Bipointed_to_Pointed_snd = Bipointed_to_Pointed_fst := rfl

/-- The functor from `Pointed` to `Bipointed` which bipoints the point. -/
def Pointed_to_Bipointed : Pointed.{u} ‚•§ Bipointed :=
{ obj := Œª X, ‚ü®X, X.point, X.point‚ü©, map := Œª X Y f, ‚ü®f.to_fun, f.map_point, f.map_point‚ü© }

/-- The functor from `Pointed` to `Bipointed` which adds a second point. -/
def Pointed_to_Bipointed_fst : Pointed.{u} ‚•§ Bipointed :=
{ obj := Œª X, ‚ü®option X, X.point, none‚ü©,
  map := Œª X Y f, ‚ü®option.map f.to_fun, congr_arg _ f.map_point, rfl‚ü©,
  map_id' := Œª X, Bipointed.hom.ext _ _ option.map_id,
  map_comp' := Œª X Y Z f g, Bipointed.hom.ext _ _ (option.map_comp_map  _ _).symm }

/-- The functor from `Pointed` to `Bipointed` which adds a first point. -/
def Pointed_to_Bipointed_snd : Pointed.{u} ‚•§ Bipointed :=
{ obj := Œª X, ‚ü®option X, none, X.point‚ü©,
  map := Œª X Y f, ‚ü®option.map f.to_fun, rfl, congr_arg _ f.map_point‚ü©,
  map_id' := Œª X, Bipointed.hom.ext _ _ option.map_id,
  map_comp' := Œª X Y Z f g, Bipointed.hom.ext _ _ (option.map_comp_map  _ _).symm }

@[simp] lemma Pointed_to_Bipointed_fst_comp_swap :
  Pointed_to_Bipointed_fst ‚ãô Bipointed.swap = Pointed_to_Bipointed_snd := rfl

@[simp] lemma Pointed_to_Bipointed_snd_comp_swap :
  Pointed_to_Bipointed_snd ‚ãô Bipointed.swap = Pointed_to_Bipointed_fst := rfl

/-- `Bipointed_to_Pointed_fst` is inverse to `Pointed_to_Bipointed`. -/
@[simps] def Pointed_to_Bipointed_comp_Bipointed_to_Pointed_fst :
  Pointed_to_Bipointed ‚ãô Bipointed_to_Pointed_fst ‚âÖ ùü≠ _ :=
nat_iso.of_components (Œª X, { hom := ‚ü®id, rfl‚ü©, inv := ‚ü®id, rfl‚ü© }) $ Œª X Y f, rfl

/-- `Bipointed_to_Pointed_snd` is inverse to `Pointed_to_Bipointed`. -/
@[simps] def Pointed_to_Bipointed_comp_Bipointed_to_Pointed_snd :
  Pointed_to_Bipointed ‚ãô Bipointed_to_Pointed_snd ‚âÖ ùü≠ _ :=
nat_iso.of_components (Œª X, { hom := ‚ü®id, rfl‚ü©, inv := ‚ü®id, rfl‚ü© }) $ Œª X Y f, rfl

/-- The free/forgetful adjunction between `Pointed_to_Bipointed_fst` and `Bipointed_to_Pointed_fst`.
-/
def Pointed_to_Bipointed_fst_Bipointed_to_Pointed_fst_adjunction :
  Pointed_to_Bipointed_fst ‚ä£ Bipointed_to_Pointed_fst :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X Y, { to_fun := Œª f, ‚ü®f.to_fun ‚àò option.some, f.map_fst‚ü©,
                        inv_fun := Œª f, ‚ü®Œª o, o.elim Y.to_prod.2 f.to_fun, f.map_point, rfl‚ü©,
                        left_inv := Œª f, by { ext, cases x, exact f.map_snd.symm, refl },
                        right_inv := Œª f, Pointed.hom.ext _ _ rfl },
  hom_equiv_naturality_left_symm' := Œª X' X Y f g, by { ext, cases x; refl } }

/-- The free/forgetful adjunction between `Pointed_to_Bipointed_snd` and `Bipointed_to_Pointed_snd`.
-/
def Pointed_to_Bipointed_snd_Bipointed_to_Pointed_snd_adjunction :
  Pointed_to_Bipointed_snd ‚ä£ Bipointed_to_Pointed_snd :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X Y, { to_fun := Œª f, ‚ü®f.to_fun ‚àò option.some, f.map_snd‚ü©,
                        inv_fun := Œª f, ‚ü®Œª o, o.elim Y.to_prod.1 f.to_fun, rfl, f.map_point‚ü©,
                        left_inv := Œª f, by { ext, cases x, exact f.map_fst.symm, refl },
                        right_inv := Œª f, Pointed.hom.ext _ _ rfl },
  hom_equiv_naturality_left_symm' := Œª X' X Y f g, by { ext, cases x; refl } }
