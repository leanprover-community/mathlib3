/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.limits.preserves.shapes.binary_products
import category_theory.limits.constructions.finite_products_of_binary_products
import category_theory.monad.limits
import category_theory.adjunction.fully_faithful
import category_theory.adjunction.reflective
import category_theory.closed.cartesian
import category_theory.subterminal

/-!
# Exponential ideals

An exponential ideal of a cartesian closed category `C` is a subcategory `D ‚äÜ C` such that for any
`B : D` and `A : C`, the exponential `A ‚üπ B` is in `D`: resembling ring theoretic ideals. We
define the notion here for inclusion functors `i : D ‚•§ C` rather than explicit subcategories to
preserve the principle of equivalence.

We additionally show that if `C` is cartesian closed and `i : D ‚•§ C` is a reflective functor, the
following are equivalent.
* The left adjoint to `i` preserves binary (equivalently, finite) products.
* `i` is an exponential ideal.
-/
universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable theory

namespace category_theory

open limits category

section ideal

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [category.{v‚ÇÅ} C] [category.{v‚ÇÅ} D] {i : D ‚•§ C}

variables (i) [has_finite_products C] [cartesian_closed C]

/--
The subcategory `D` of `C` expressed as an inclusion functor is an *exponential ideal* if
`B ‚àà D` implies `A ‚üπ B ‚àà D` for all `A`.
-/
class exponential_ideal : Prop :=
(exp_closed : ‚àÄ {B}, B ‚àà i.ess_image ‚Üí ‚àÄ A, (A ‚üπ B) ‚àà i.ess_image)

/--
To show `i` is an exponential ideal it suffices to show that `A ‚üπ iB` is "in" `D` for any `A` in
`C` and `B` in `D`.
-/
lemma exponential_ideal.mk' (h : ‚àÄ (B : D) (A : C), (A ‚üπ i.obj B) ‚àà i.ess_image) :
  exponential_ideal i :=
‚ü®Œª B hB A,
begin
  rcases hB with ‚ü®B', ‚ü®iB'‚ü©‚ü©,
  exact functor.ess_image.of_iso ((exp A).map_iso iB') (h B' A),
end‚ü©

/-- The entire category viewed as a subcategory is an exponential ideal. -/
instance : exponential_ideal (ùü≠ C) :=
exponential_ideal.mk' _ (Œª B A, ‚ü®_, ‚ü®iso.refl _‚ü©‚ü©)

open cartesian_closed

/-- The subcategory of subterminal objects is an exponential ideal. -/
instance : exponential_ideal (subterminal_inclusion C) :=
begin
  apply exponential_ideal.mk',
  intros B A,
  refine ‚ü®‚ü®A ‚üπ B.1, Œª Z g h, _‚ü©, ‚ü®iso.refl _‚ü©‚ü©,
  exact uncurry_injective (B.2 (cartesian_closed.uncurry g) (cartesian_closed.uncurry h))
end

/--
If `D` is a reflective subcategory, the property of being an exponential ideal is equivalent to
the presence of a natural isomorphism `i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A`, that is:
`(A ‚üπ iB) ‚âÖ i L (A ‚üπ iB)`, naturally in `B`.
The converse is given in `exponential_ideal.mk_of_iso`.
-/
def exponential_ideal_reflective (A : C) [reflective i] [exponential_ideal i] :
  i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A :=
begin
  symmetry,
  apply nat_iso.of_components _ _,
  { intro X,
    haveI := (exponential_ideal.exp_closed (i.obj_mem_ess_image X) A).unit_is_iso,
    apply as_iso ((adjunction.of_right_adjoint i).unit.app (A ‚üπ i.obj X)) },
  { simp }
end

/--
Given a natural isomorphism `i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A`, we can show `i`
is an exponential ideal.
-/
lemma exponential_ideal.mk_of_iso [reflective i]
  (h : Œ† (A : C), i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A) :
  exponential_ideal i :=
begin
  apply exponential_ideal.mk',
  intros B A,
  exact ‚ü®_, ‚ü®(h A).app B‚ü©‚ü©,
end

end ideal

section

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [category.{v‚ÇÅ} C] [category.{v‚ÇÅ} D]
variables (i : D ‚•§ C)

lemma reflective_products [has_finite_products C] [reflective i] : has_finite_products D :=
‚ü®Œª J ùí•‚ÇÅ ùí•‚ÇÇ, by exactI has_limits_of_shape_of_reflective i‚ü©

local attribute [instance, priority 10] reflective_products

open cartesian_closed

variables [has_finite_products C] [reflective i] [cartesian_closed C]

/--
If the reflector preserves binary products, the subcategory is an exponential ideal.
This is the converse of `preserves_binary_products_of_exponential_ideal`.
-/
@[priority 10]
instance exponential_ideal_of_preserves_binary_products
  [preserves_limits_of_shape (discrete walking_pair) (left_adjoint i)] :
  exponential_ideal i :=
begin
  let ir := adjunction.of_right_adjoint i,
  let L : C ‚•§ D := left_adjoint i,
  let Œ∑ : ùü≠ C ‚ü∂ L ‚ãô i := ir.unit,
  let Œµ : i ‚ãô L ‚ü∂ ùü≠ D := ir.counit,
  apply exponential_ideal.mk',
  intros B A,
  let q : i.obj (L.obj (A ‚üπ i.obj B)) ‚ü∂ A ‚üπ i.obj B,
    apply cartesian_closed.curry (ir.hom_equiv _ _ _),
    apply _ ‚â´ (ir.hom_equiv _ _).symm ((ev A).app (i.obj B)),
    refine prod_comparison L A _ ‚â´ limits.prod.map (ùüô _) (Œµ.app _) ‚â´ inv (prod_comparison _ _ _),
  have : Œ∑.app (A ‚üπ i.obj B) ‚â´ q = ùüô (A ‚üπ i.obj B),
  { dsimp,
    rw [‚Üê curry_natural_left, curry_eq_iff, uncurry_id_eq_ev, ‚Üê ir.hom_equiv_naturality_left,
        ir.hom_equiv_apply_eq, assoc, assoc, prod_comparison_natural_assoc, L.map_id,
        ‚Üê prod.map_id_comp_assoc, ir.left_triangle_components, prod.map_id_id, id_comp],
    apply is_iso.hom_inv_id_assoc },
  haveI : split_mono (Œ∑.app (A ‚üπ i.obj B)) := ‚ü®_, this‚ü©,
  apply mem_ess_image_of_unit_split_mono,
end

variables [exponential_ideal i]

/--
If `i` witnesses that `D` is a reflective subcategory and an exponential ideal, then `D` is
itself cartesian closed.
-/
def cartesian_closed_of_reflective : cartesian_closed D :=
{ closed := Œª B,
  { is_adj :=
    { right := i ‚ãô exp (i.obj B) ‚ãô left_adjoint i,
      adj :=
      begin
        apply adjunction.restrict_fully_faithful i i (exp.adjunction (i.obj B)),
        { symmetry,
          apply nat_iso.of_components _ _,
          { intro X,
            haveI := adjunction.right_adjoint_preserves_limits (adjunction.of_right_adjoint i),
            apply as_iso (prod_comparison i B X) },
          { intros X Y f,
            dsimp,
            rw prod_comparison_natural,
            simp, } },
        { apply (exponential_ideal_reflective i _).symm }
      end } } }

-- It's annoying that I need to do this.
local attribute [-instance]
  category_theory.preserves_limit_of_creates_limit_and_has_limit
  category_theory.preserves_limit_of_shape_of_creates_limits_of_shape_and_has_limits_of_shape

/--
We construct a bijection between morphisms `L(A ‚®Ø B) ‚ü∂ X` and morphisms `LA ‚®Ø LB ‚ü∂ X`.
This bijection has two key properties:
* It is natural in `X`: See `bijection_natural`.
* When `X = LA ‚®Ø LB`, then the backwards direction sends the identity morphism to the product
  comparison morphism: See `bijection_symm_apply_id`.

Together these help show that `L` preserves binary products. This should be considered
*internal implementation* towards `preserves_binary_products_of_exponential_ideal`.
-/
noncomputable def bijection (A B : C) (X : D) :
  ((left_adjoint i).obj (A ‚®Ø B) ‚ü∂ X) ‚âÉ ((left_adjoint i).obj A ‚®Ø (left_adjoint i).obj B ‚ü∂ X) :=
calc _ ‚âÉ (A ‚®Ø B ‚ü∂ i.obj X) :
              (adjunction.of_right_adjoint i).hom_equiv _ _
   ... ‚âÉ (B ‚®Ø A ‚ü∂ i.obj X) :
              (limits.prod.braiding _ _).hom_congr (iso.refl _)
   ... ‚âÉ (A ‚ü∂ B ‚üπ i.obj X) :
              (exp.adjunction _).hom_equiv _ _
   ... ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ B ‚üπ i.obj X) :
              unit_comp_partial_bijective _ (exponential_ideal.exp_closed (i.obj_mem_ess_image _) _)
   ... ‚âÉ (B ‚®Ø i.obj ((left_adjoint i).obj A) ‚ü∂ i.obj X) :
              ((exp.adjunction _).hom_equiv _ _).symm
   ... ‚âÉ (i.obj ((left_adjoint i).obj A) ‚®Ø B ‚ü∂ i.obj X) :
              (limits.prod.braiding _ _).hom_congr (iso.refl _)
   ... ‚âÉ (B ‚ü∂ i.obj ((left_adjoint i).obj A) ‚üπ i.obj X) :
              (exp.adjunction _).hom_equiv _ _
   ... ‚âÉ (i.obj ((left_adjoint i).obj B) ‚ü∂ i.obj ((left_adjoint i).obj A) ‚üπ i.obj X) :
              unit_comp_partial_bijective _ (exponential_ideal.exp_closed (i.obj_mem_ess_image _) _)
   ... ‚âÉ (i.obj ((left_adjoint i).obj A) ‚®Ø i.obj ((left_adjoint i).obj B) ‚ü∂ i.obj X) :
              ((exp.adjunction _).hom_equiv _ _).symm
   ... ‚âÉ (i.obj ((left_adjoint i).obj A ‚®Ø (left_adjoint i).obj B) ‚ü∂ i.obj X) :
     begin
       apply iso.hom_congr _ (iso.refl _),
       haveI : preserves_limits i := (adjunction.of_right_adjoint i).right_adjoint_preserves_limits,
       exact (preserves_limit_pair.iso _ _ _).symm,
     end
   ... ‚âÉ ((left_adjoint i).obj A ‚®Ø (left_adjoint i).obj B ‚ü∂ X) :
              (equiv_of_fully_faithful _).symm

lemma bijection_symm_apply_id (A B : C) :
  (bijection i A B _).symm (ùüô _) = prod_comparison _ _ _ :=
begin
  dsimp [bijection],
  rw [comp_id, comp_id, comp_id, i.map_id, comp_id, unit_comp_partial_bijective_symm_apply,
      unit_comp_partial_bijective_symm_apply, uncurry_natural_left, uncurry_curry,
      uncurry_natural_left, uncurry_curry, prod.lift_map_assoc, comp_id, prod.lift_map_assoc,
      comp_id, prod.comp_lift_assoc, prod.lift_snd, prod.lift_fst_assoc,
      prod.lift_fst_comp_snd_comp, ‚Üêadjunction.eq_hom_equiv_apply, adjunction.hom_equiv_unit,
      iso.comp_inv_eq, assoc, preserves_limit_pair.iso_hom],
  apply prod.hom_ext,
  { rw [limits.prod.map_fst, assoc, assoc, prod_comparison_fst, ‚Üêi.map_comp, prod_comparison_fst],
    apply (adjunction.of_right_adjoint i).unit.naturality },
  { rw [limits.prod.map_snd, assoc, assoc, prod_comparison_snd, ‚Üêi.map_comp, prod_comparison_snd],
    apply (adjunction.of_right_adjoint i).unit.naturality },
end

lemma bijection_natural
  (A B : C) (X X' : D) (f : ((left_adjoint i).obj (A ‚®Ø B) ‚ü∂ X)) (g : X ‚ü∂ X') :
  bijection i _ _ _ (f ‚â´ g) = bijection i _ _ _ f ‚â´ g :=
begin
  dsimp [bijection],
  apply i.map_injective,
  rw [i.image_preimage, i.map_comp, i.image_preimage, comp_id, comp_id, comp_id, comp_id, comp_id,
      comp_id, adjunction.hom_equiv_naturality_right, ‚Üê assoc, curry_natural_right _ (i.map g),
      unit_comp_partial_bijective_natural, uncurry_natural_right, ‚Üê assoc, curry_natural_right,
      unit_comp_partial_bijective_natural, uncurry_natural_right, assoc],
end

/--
The bijection allows us to show that `prod_comparison L A B` is an isomorphism, where the inverse
is the forward map of the identity morphism.
-/
lemma prod_comparison_iso (A B : C) :
  is_iso (prod_comparison (left_adjoint i) A B) :=
‚ü®‚ü®bijection i _ _ _ (ùüô _),
  by rw [‚Üê(bijection i _ _ _).injective.eq_iff, bijection_natural, ‚Üê bijection_symm_apply_id,
         equiv.apply_symm_apply, id_comp],
  by rw [‚Üêbijection_natural, id_comp, ‚Üêbijection_symm_apply_id, equiv.apply_symm_apply]‚ü©‚ü©

local attribute [instance] prod_comparison_iso

/--
If a reflective subcategory is an exponential ideal, then the reflector preserves binary products.
This is the converse of `exponential_ideal_of_preserves_binary_products`.
-/
noncomputable def preserves_binary_products_of_exponential_ideal :
  preserves_limits_of_shape (discrete walking_pair) (left_adjoint i) :=
{ preserves_limit := Œª K,
  begin
    apply limits.preserves_limit_of_iso_diagram _ (diagram_iso_pair K).symm,
    apply preserves_limit_pair.of_iso_prod_comparison,
  end }

/--
If a reflective subcategory is an exponential ideal, then the reflector preserves finite products.
-/
noncomputable def preserves_finite_products_of_exponential_ideal (J : Type*) [fintype J] :
  preserves_limits_of_shape (discrete J) (left_adjoint i) :=
begin
  letI := preserves_binary_products_of_exponential_ideal i,
  letI := left_adjoint_preserves_terminal_of_reflective i,
  apply preserves_finite_products_of_preserves_binary_and_terminal (left_adjoint i) J
end

end

end category_theory
