import category_theory.limits.shapes.binary_products
import category_theory.limits.shapes.constructions.preserve_binary_products
import category_theory.limits.preserves.basic
import category_theory.adjunction
import category_theory.monad.limits
import category_theory.adjunction.fully_faithful
import category_theory.closed.cartesian

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable theory

namespace category_theory

open limits category

section subcat

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [category.{v‚ÇÅ} C] [category.{v‚ÇÇ} D] {i : D ‚•§ C}

/--
Given a subcategory `D` of `C` expressed as an (inclusion) functor `i : D ‚•§ C`, the object `A : C`
is said to be "in" the subcategory if there is a witness in `D`, such that `i.obj witness` is
isomorphic to `A`.
This notion is useful primarily when `i` is faithful.
-/
def in_subcategory (i : D ‚•§ C) (A : C) : Prop := ‚àÉ (B : D), nonempty (i.obj B ‚âÖ A)

def in_subcategory.witness {A : C} (h : in_subcategory i A) : D := h.some

def in_subcategory.get_iso {A : C} (h : in_subcategory i A) : i.obj h.witness ‚âÖ A :=
classical.choice h.some_spec

/-- Being in the subcategory is a "hygenic" property: it is preserved under isomorphism. -/
lemma in_subcategory_of_iso {A A' : C} (h' : A ‚âÖ A') (hA : in_subcategory i A) :
  in_subcategory i A' :=
hA.imp (Œª B, nonempty.map (‚â™‚â´ h'))

lemma inclusion_is_in (i : D ‚•§ C) (B : D) : in_subcategory i (i.obj B) := ‚ü®B, ‚ü®iso.refl _‚ü©‚ü©

lemma hom_comp_eq_id {X Y : C} (g : X ‚ü∂ Y) [is_iso g] {f : Y ‚ü∂ X} : g ‚â´ f = ùüô X ‚Üî f = inv g :=
iso.hom_comp_eq_id (as_iso g)

lemma comp_hom_eq_id {X Y : C} (g : X ‚ü∂ Y) [is_iso g] {f : Y ‚ü∂ X} : f ‚â´ g = ùüô Y ‚Üî f = inv g :=
iso.comp_hom_eq_id (as_iso g)

/-- Auxiliary definition for `unit_comp_partial_bijective`. -/
def unit_comp_partial_bijective_aux [reflective i] (A : C) (B : D) :
  (A ‚ü∂ i.obj B) ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ i.obj B) :=
((adjunction.of_right_adjoint i).hom_equiv _ _).symm.trans (equiv_of_fully_faithful i)

/-- The description of the inverse of the bijection. -/
lemma unit_comp_partial_bijective_aux_symm_apply [reflective i] {A : C} {B : D}
  (f : i.obj ((left_adjoint i).obj A) ‚ü∂ i.obj B) :
  (unit_comp_partial_bijective_aux _ _).symm f = (adjunction.of_right_adjoint i).unit.app A ‚â´ f :=
by simp [unit_comp_partial_bijective_aux]

/--
If `i` has a reflector `L`, then the function `(i L A ‚ü∂ B) ‚Üí (A ‚ü∂ B)` given by precomposing with
`Œ∑.app A` is a bijection provided `B` is in the subcategory given by `i`.

This establishes there is a natural bijection `(A ‚ü∂ B) ‚âÉ (i L A ‚ü∂ B)`. In other words, from the
point of view of objects in `D`, `A` and `i L A` look the same: specifically that `Œ∑.app A` is
an isomorphism.
-/
def unit_comp_partial_bijective [reflective i] (A : C) {B : C} (hB : in_subcategory i B) :
  (A ‚ü∂ B) ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ B) :=
calc (A ‚ü∂ B) ‚âÉ (A ‚ü∂ i.obj hB.witness) : iso.hom_congr (iso.refl _) hB.get_iso.symm
     ...     ‚âÉ (i.obj _ ‚ü∂ i.obj hB.witness) : unit_comp_partial_bijective_aux _ _
     ...     ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ B) : iso.hom_congr (iso.refl _) hB.get_iso

@[simp]
lemma unit_comp_partial_bijective_symm_apply [reflective i] (A : C) {B : C}
  (hB : in_subcategory i B) (f) :
  (unit_comp_partial_bijective A hB).symm f = (adjunction.of_right_adjoint i).unit.app A ‚â´ f :=
by simp [unit_comp_partial_bijective, unit_comp_partial_bijective_aux_symm_apply]

lemma unit_comp_partial_bijective_symm_natural [reflective i] (A : C) {B B' : C} (h : B ‚ü∂ B')
  (hB : in_subcategory i B) (hB' : in_subcategory i B') (f : i.obj ((left_adjoint i).obj A) ‚ü∂ B) :
  (unit_comp_partial_bijective A hB').symm (f ‚â´ h) = (unit_comp_partial_bijective A hB).symm f ‚â´ h :=
by simp

lemma unit_comp_partial_bijective_natural [reflective i] (A : C) {B B' : C} (h : B ‚ü∂ B')
  (hB : in_subcategory i B) (hB' : in_subcategory i B') (f : A ‚ü∂ B) :
  (unit_comp_partial_bijective A hB') (f ‚â´ h) = unit_comp_partial_bijective A hB f ‚â´ h :=
by rw [‚Üê equiv.eq_symm_apply, unit_comp_partial_bijective_symm_natural A h hB, equiv.symm_apply_apply]

/--
If `A` is in the reflective subcategory, then `Œ∑_A` is an isomorphism.
This gives that the "witness" for `A` being in the subcategory can instead be given as the
reflection of `A`, with the isomorphism as `Œ∑_A`.

(For any `B` in the reflective subcategory, we automatically have that `Œµ_B` is an iso.)
-/
def in_subcategory.unit_iso [reflective i] {A : C} (h : in_subcategory i A) :
  is_iso ((adjunction.of_right_adjoint i).unit.app A) :=
begin
  have : ‚àÄ (B : D), is_iso ((adjunction.of_right_adjoint i).unit.app (i.obj B)),
  { intro B,
    have : (adjunction.of_right_adjoint i).unit.app (i.obj B) =
             inv (i.map ((adjunction.of_right_adjoint i).counit.app B)),
    { rw ‚Üê comp_hom_eq_id,
      apply (adjunction.of_right_adjoint i).right_triangle_components },
    rw this,
    apply_instance },
  resetI,
  suffices :
    (adjunction.of_right_adjoint i).unit.app A =
      h.get_iso.inv ‚â´
        (adjunction.of_right_adjoint i).unit.app (i.obj h.witness) ‚â´
          (left_adjoint i ‚ãô i).map h.get_iso.hom,
  { rw this,
    apply_instance },
  rw ‚Üê nat_trans.naturality,
  simp only [functor.id_map, iso.inv_hom_id_assoc],
end

/--  If `Œ∑_A` is an isomorphism, then `A` is in the subcategory. -/
def in_subcategory_of_unit_is_iso [is_right_adjoint i] (A : C)
  [is_iso ((adjunction.of_right_adjoint i).unit.app A)] : in_subcategory i A :=
‚ü®(left_adjoint i).obj A, ‚ü®(as_iso ((adjunction.of_right_adjoint i).unit.app A)).symm‚ü©‚ü©

/-- If `Œ∑_A` is a split monomorphism, then `A` is in the reflective subcategory. -/
def in_subcategory_of_unit_split_mono [reflective i] {A : C}
  [split_mono ((adjunction.of_right_adjoint i).unit.app A)] : in_subcategory i A :=
begin
  let Œ∑ : ùü≠ C ‚ü∂ left_adjoint i ‚ãô i := (adjunction.of_right_adjoint i).unit,
  haveI : is_iso (Œ∑.app (i.obj ((left_adjoint i).obj A))) := (inclusion_is_in _ _).unit_iso,
  have : epi (Œ∑.app A),
  { apply epi_of_epi (retraction (Œ∑.app A)) _,
    rw (show retraction _ ‚â´ Œ∑.app A = _, from Œ∑.naturality (retraction (Œ∑.app A))),
    apply epi_comp (Œ∑.app (i.obj ((left_adjoint i).obj A))) },
  resetI,
  haveI := is_iso_of_epi_of_split_mono (Œ∑.app A),
  exact in_subcategory_of_unit_is_iso A,
end

end subcat

section ideal

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [category.{v‚ÇÅ} C] [category.{v‚ÇÅ} D] {i : D ‚•§ C}
variables (i) [has_finite_products C] [cartesian_closed C]

/--
The subcategory `D` of `C` expressed as an inclusion functor is an *exponential ideal* if
`B ‚àà D` implies `B^A ‚àà D` for all `A`.
-/
class exponential_ideal : Prop :=
(exp_closed : ‚àÄ {B}, in_subcategory i B ‚Üí ‚àÄ A, in_subcategory i (A ‚üπ B))

/--
To show `i` is an exponential ideal it suffices to show that `(iB)^A` is `in` `D` for any `A` in `C`
and `B` in `D`.
-/
def exponential_ideal.mk' (h : ‚àÄ (B : D) (A : C), in_subcategory i (A ‚üπ i.obj B)) :
  exponential_ideal i :=
‚ü®Œª B hB A,
begin
  rcases hB with ‚ü®B', ‚ü®iB'‚ü©‚ü©,
  apply in_subcategory_of_iso _ (h B' A),
  apply (exp A).map_iso iB',
end‚ü©

/--
If `D` is a reflective subcategory, the property of being an exponential ideal is equivalent to
the presence of a natural isomorphism `i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A`, that is:
`(iB)^A ‚âÖ i L (iB)^A`, naturally in `B`.
THe converse is given in `exponential_ideal.mk_of_iso`.
-/
def exponential_ideal_reflective (A : C) [reflective i] [exponential_ideal i] :
  i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A :=
begin
  symmetry,
  apply nat_iso.of_components _ _,
  { intro X,
    haveI : is_iso ((adjunction.of_right_adjoint i).unit.app (i.obj X ^^ A)) :=
      in_subcategory.unit_iso
        (exponential_ideal.exp_closed (inclusion_is_in i X) A),
    apply as_iso ((adjunction.of_right_adjoint i).unit.app (i.obj X ^^ A)) },
  { simp }
end

/--
Given a natural isomorphism `i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A`, we can show `i`
is an exponential ideal.
-/
def exponential_ideal.mk_of_iso [reflective i]
  (h : Œ† (A : C), i ‚ãô exp A ‚ãô left_adjoint i ‚ãô i ‚âÖ i ‚ãô exp A) :
  exponential_ideal i :=
begin
  apply exponential_ideal.mk',
  intros B A,
  exact ‚ü®_, ‚ü®(h A).app B‚ü©‚ü©,
end

/--
The category of subterminals in `C` is the subcategory of objects for which the unique morphism to
the terminal object is a monomorphism.
TODO: If `C` is the category of sheaves on a topological space `X`, this category is equivalent
to the lattice of open subsets of `X`.
-/
@[derive category]
def subterminals (C : Type u‚ÇÅ) [category.{v‚ÇÅ} C] :=
{A : C // ‚àÄ {Z : C} (f g : Z ‚ü∂ A), f = g}

/-- The inclusion of the subterminal objects into the original category. -/
@[derive [full, faithful]]
def subterminal_inclusion : subterminals C ‚•§ C := full_subcategory_inclusion _

/-- The subcategory of subterminal objects is an exponential ideal. -/
instance : exponential_ideal (subterminal_inclusion : _ ‚•§ C) :=
begin
  apply exponential_ideal.mk',
  rintros B A,
  refine ‚ü®‚ü®B.1 ^^ A, Œª Z g h, _‚ü©, ‚ü®iso.refl _‚ü©‚ü©,
  exact uncurry_injective (B.2 (cartesian_closed.uncurry g) (cartesian_closed.uncurry h))
end

end ideal

section

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [category.{v‚ÇÅ} C] [category.{v‚ÇÅ} D]
variables (i : D ‚•§ C) [has_finite_products C] [cartesian_closed C]

def reflective_products [reflective i] : has_finite_products D :=
Œª J ùí•‚ÇÅ ùí•‚ÇÇ,
{ has_limit := Œª F, by { have := monadic_creates_limits i, exactI has_limit_of_created F i } }

local attribute [instance] reflective_products

/--
If `i` witnesses that `D` is a reflective subcategory and an exponential ideal, then `D` is
itself cartesian closed.
-/
def reflective_cc [reflective i] [exponential_ideal i] : cartesian_closed D :=
{ closed := Œª B,
  { is_adj :=
    { right := i ‚ãô exp (i.obj B) ‚ãô left_adjoint i,
      adj :=
      begin
        apply adjunction.restrict_fully_faithful i i (exp.adjunction (i.obj B)),
        { symmetry,
          apply nat_iso.of_components _ _,
          { intro X,
            haveI := adjunction.right_adjoint_preserves_limits (adjunction.of_right_adjoint i),
            apply as_iso (prod_comparison i B X) },
          { intros X Y f,
            dsimp,
            rw prod_comparison_natural,
            simp, } },
        { apply (exponential_ideal_reflective i _).symm }
      end } } }

/-- If the reflector preserves binary products, the subcategory is an exponential ideal. -/
def ideal_of_preserves_binary_products [reflective i]
  [preserves_limits_of_shape (discrete walking_pair) (left_adjoint i)] :
  exponential_ideal i :=
begin
  let ir := adjunction.of_right_adjoint i,
  let L : C ‚•§ D := left_adjoint i,
  let Œ∑ : ùü≠ C ‚ü∂ L ‚ãô i := ir.unit,
  let Œµ : i ‚ãô L ‚ü∂ ùü≠ D := ir.counit,
  apply exponential_ideal.mk',
  intros B A,
  let q : i.obj (L.obj (i.obj B ^^ A)) ‚ü∂ i.obj B ^^ A,
    apply cartesian_closed.curry (ir.hom_equiv _ _ _),
    apply _ ‚â´ (ir.hom_equiv _ _).symm ((ev A).app (i.obj B)),
    refine prod_comparison L A _ ‚â´ limits.prod.map (ùüô _) (Œµ.app _) ‚â´ inv (prod_comparison _ _ _),
  have : Œ∑.app (i.obj B ^^ A) ‚â´ q = ùüô (i.obj B ^^ A),
  { rw [‚Üê curry_natural_left, curry_eq_iff, uncurry_id_eq_ev],
    erw ‚Üê ir.hom_equiv_naturality_left,
    rw ir.hom_equiv_apply_eq,
    change L.map _ ‚â´ _ ‚â´ _ = _,
    rw [assoc, assoc],
    erw prod_comparison_natural_assoc,
    rw [limits.prod.map_map_assoc, L.map_id, id_comp, ir.left_triangle_components],
    erw prod.map_id_id,
    rw id_comp,
    apply is_iso.hom_inv_id_assoc },
  haveI : split_mono (Œ∑.app (i.obj B ^^ A)) := ‚ü®_, this‚ü©,
  apply in_subcategory_of_unit_split_mono,
end

noncomputable def bijection (A B : C) (C' : D) [reflective i] [exponential_ideal i] :
  ((left_adjoint i).obj (A ‚®Ø B) ‚ü∂ C') ‚âÉ ((left_adjoint i).obj A ‚®Ø (left_adjoint i).obj B ‚ü∂ C') :=
calc _ ‚âÉ (A ‚®Ø B ‚ü∂ i.obj C') :
              (adjunction.of_right_adjoint i).hom_equiv _ _
   ... ‚âÉ (B ‚®Ø A ‚ü∂ i.obj C') :
              (limits.prod.braiding _ _).hom_congr (iso.refl _)
   ... ‚âÉ (A ‚ü∂ B ‚üπ i.obj C') :
              (exp.adjunction _).hom_equiv _ _
   ... ‚âÉ (i.obj ((left_adjoint i).obj A) ‚ü∂ B ‚üπ i.obj C') :
              unit_comp_partial_bijective _ (exponential_ideal.exp_closed (inclusion_is_in i _) _)
   ... ‚âÉ (B ‚®Ø i.obj ((left_adjoint i).obj A) ‚ü∂ i.obj C') :
              ((exp.adjunction _).hom_equiv _ _).symm
   ... ‚âÉ (i.obj ((left_adjoint i).obj A) ‚®Ø B ‚ü∂ i.obj C') :
              (limits.prod.braiding _ _).hom_congr (iso.refl _)
   ... ‚âÉ (B ‚ü∂ i.obj ((left_adjoint i).obj A) ‚üπ i.obj C') :
              (exp.adjunction _).hom_equiv _ _
   ... ‚âÉ (i.obj ((left_adjoint i).obj B) ‚ü∂ i.obj ((left_adjoint i).obj A) ‚üπ i.obj C') :
              unit_comp_partial_bijective _ (exponential_ideal.exp_closed (inclusion_is_in i _) _)
   ... ‚âÉ (i.obj ((left_adjoint i).obj A) ‚®Ø i.obj ((left_adjoint i).obj B) ‚ü∂ i.obj C') :
              ((exp.adjunction _).hom_equiv _ _).symm
   ... ‚âÉ (i.obj ((left_adjoint i).obj A ‚®Ø (left_adjoint i).obj B) ‚ü∂ i.obj C') :
     begin
       apply iso.hom_congr _ (iso.refl _),
       apply (as_iso (prod_comparison _ _ _)).symm,
       haveI : preserves_limits i := (adjunction.of_right_adjoint i).right_adjoint_preserves_limits,
       apply_instance,
     end
   ... ‚âÉ ((left_adjoint i).obj A ‚®Ø (left_adjoint i).obj B ‚ü∂ C') :
              (equiv_of_fully_faithful _).symm

lemma bijection_symm_apply_id (A B : C) [reflective i] [exponential_ideal i] :
  (bijection i A B _).symm (ùüô _) = prod_comparison _ _ _ :=
begin
  let L := left_adjoint i,
  let adj : L ‚ä£ i := adjunction.of_right_adjoint i,
  let Œ∑ : _ ‚ü∂ L ‚ãô i := adj.unit,
  dsimp [bijection],
  rw [equiv.symm_symm, equiv.symm_symm, equiv.symm_symm, comp_id, comp_id, comp_id,
      equiv_of_fully_faithful_apply, i.map_id, comp_id, unit_comp_partial_bijective_symm_apply,
      unit_comp_partial_bijective_symm_apply],
  change (adj.hom_equiv _ _).symm
    ((limits.prod.braiding A B).hom ‚â´
      cartesian_closed.uncurry (Œ∑.app _ ‚â´
        cartesian_closed.curry ((limits.prod.braiding _ B).inv ‚â´
          cartesian_closed.uncurry (Œ∑.app _ ‚â´
            cartesian_closed.curry _)))) =
    prod_comparison L _ _,
  rw [uncurry_natural_left, uncurry_curry, uncurry_natural_left, uncurry_curry,
      ‚Üêbraid_natural_assoc, iso.hom_inv_id_assoc, limits.prod.map_map_assoc, comp_id, id_comp,
      ‚Üê adjunction.eq_hom_equiv_apply, adjunction.hom_equiv_unit, is_iso.comp_inv_eq, assoc],
  apply prod.hom_ext,
  { rw [limits.prod.map_fst, assoc, assoc, prod_comparison, prod_comparison, prod.lift_fst,
        ‚Üêi.map_comp, prod.lift_fst],
    apply Œ∑.naturality },
  { rw [limits.prod.map_snd, assoc, assoc, prod_comparison, prod_comparison, prod.lift_snd,
        ‚Üêi.map_comp, prod.lift_snd],
    apply Œ∑.naturality },
end.

lemma bijection_natural [reflective i] [exponential_ideal i]
  (A B : C) (C' C'' : D) (f : ((left_adjoint i).obj (A ‚®Ø B) ‚ü∂ C')) (g : C' ‚ü∂ C'') :
  bijection i _ _ _ (f ‚â´ g) = bijection i _ _ _ f ‚â´ g :=
begin
  apply i.map_injective,
  dsimp [bijection],
  rw [i.image_preimage, i.map_comp, i.image_preimage, comp_id, comp_id, comp_id, comp_id, comp_id,
      comp_id],
  change prod_comparison _ _ _ ‚â´
    cartesian_closed.uncurry
      (unit_comp_partial_bijective _ _
        (cartesian_closed.curry ((limits.prod.braiding _ _).hom ‚â´
          cartesian_closed.uncurry
            (unit_comp_partial_bijective _ _
              (cartesian_closed.curry
                ((limits.prod.braiding _ _).hom ‚â´ _)))))) = _,
  rw [adjunction.hom_equiv_naturality_right, ‚Üê assoc, curry_natural_right _ (i.map g),
      unit_comp_partial_bijective_natural, uncurry_natural_right, ‚Üê assoc, curry_natural_right,
      unit_comp_partial_bijective_natural, uncurry_natural_right, assoc],
  refl,
end

noncomputable def preserves_pair_of_exponential_ideal [reflective i] [exponential_ideal i]
  (A B : C) : preserves_limit (pair A B) (left_adjoint i) :=
begin
  let ir : is_right_adjoint i := by apply_instance,
  let L := ir.left,
  let adj : L ‚ä£ i := ir.adj,
  let Œ∑ : _ ‚ü∂ L ‚ãô i := adj.unit,
  apply preserves_binary_prod_of_prod_comparison_iso L _ _,
  refine is_iso.of_iso ‚ü®prod_comparison _ _ _, bijection i _ _ _ (ùüô _), _, _‚ü©,
  { dsimp,
    rw [‚Üê(bijection i _ _ _).injective.eq_iff, bijection_natural, ‚Üê bijection_symm_apply_id,
        equiv.apply_symm_apply, id_comp] },
  { dsimp,
    rw [‚Üêbijection_natural, id_comp, ‚Üêbijection_symm_apply_id, equiv.apply_symm_apply] }
end

noncomputable def preserves_binary_products_of_exponential_ideal
  [reflective i] [exponential_ideal i] :
  preserves_limits_of_shape (discrete walking_pair) (left_adjoint i) :=
{ preserves_limit := Œª K,
  begin
    apply limits.preserves_limit_of_iso_diagram _ (diagram_iso_pair K).symm,
    apply preserves_pair_of_exponential_ideal,
  end }
end

end category_theory
