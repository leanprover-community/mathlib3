/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.monoidal.category
import category_theory.adjunction.limits
import category_theory.adjunction.mates

/-!
# Closed monoidal categories

Define (right) closed objects and (right) closed monoidal categories.

## TODO
Some of the theorems proved about cartesian closed categories
should be generalised and moved to this file.
-/
universes v u uâ‚‚

namespace category_theory

open category monoidal_category

/-- An object `X` is (right) closed if `(X âŠ— -)` is a left adjoint. -/
class closed {C : Type u} [category.{v} C] [monoidal_category.{v} C] (X : C) :=
(is_adj : is_left_adjoint (tensor_left X))

/-- A monoidal category `C` is (right) monoidal closed if every object is (right) closed. -/
class monoidal_closed (C : Type u) [category.{v} C] [monoidal_category.{v} C] :=
(closed' : Î  (X : C), closed X)

attribute [instance, priority 100] monoidal_closed.closed'

variables {C : Type u} [category.{v} C] [monoidal_category.{v} C]

/--
If `X` and `Y` are closed then `X âŠ— Y` is.
This isn't an instance because it's not usually how we want to construct internal homs,
we'll usually prove all objects are closed uniformly.
-/
def tensor_closed {X Y : C}
  (hX : closed X) (hY : closed Y) : closed (X âŠ— Y) :=
{ is_adj :=
  begin
    haveI := hX.is_adj,
    haveI := hY.is_adj,
    exact adjunction.left_adjoint_of_nat_iso (monoidal_category.tensor_left_tensor _ _).symm
  end }

/--
The unit object is always closed.
This isn't an instance because most of the time we'll prove closedness for all objects at once,
rather than just for this one.
-/
def unit_closed : closed (ğŸ™_ C) :=
{ is_adj :=
  { right := ğŸ­ C,
    adj := adjunction.mk_of_hom_equiv
    { hom_equiv := Î» X _,
      { to_fun := Î» a, (left_unitor X).inv â‰« a,
        inv_fun := Î» a, (left_unitor X).hom â‰« a,
        left_inv := by tidy,
        right_inv := by tidy },
      hom_equiv_naturality_left_symm' := Î» X' X Y f g,
      by { dsimp, rw left_unitor_naturality_assoc } } } }

variables (A B : C) {X X' Y Y' Z : C}

variables [closed A]

/--
This is the internal hom `A âŸ¶[C] -`.
Note that this is essentially an opaque definition,
and so will not agree definitionally with any "native" internal hom the category has.

TODO: we could introduce a `has_ihom` class
that allows specifying a particular definition of the internal hom,
and provide a low priority opaque instance.
-/
def ihom : C â¥¤ C :=
(@closed.is_adj _ _ _ A _).right

namespace ihom

/-- The adjunction between `A âŠ— -` and `A âŸ¹ -`. -/
def adjunction : tensor_left A âŠ£ ihom A :=
closed.is_adj.adj

/-- The evaluation natural transformation. -/
def ev : ihom A â‹™ tensor_left A âŸ¶ ğŸ­ C :=
(ihom.adjunction A).counit

/-- The coevaluation natural transformation. -/
def coev : ğŸ­ C âŸ¶ tensor_left A â‹™ ihom A :=
(ihom.adjunction A).unit

@[simp] lemma ihom_adjunction_counit : (ihom.adjunction A).counit = ev A := rfl
@[simp] lemma ihom_adjunction_unit : (ihom.adjunction A).unit = coev A := rfl

@[simp, reassoc]
lemma ev_naturality {X Y : C} (f : X âŸ¶ Y) :
  ((ğŸ™ A) âŠ— ((ihom A).map f)) â‰« (ev A).app Y = (ev A).app X â‰« f :=
(ev A).naturality f

@[simp, reassoc]
lemma coev_naturality {X Y : C} (f : X âŸ¶ Y) :
  f â‰« (coev A).app Y = (coev A).app X â‰« (ihom A).map ((ğŸ™ A) âŠ— f) :=
(coev A).naturality f

notation A ` âŸ¶[C] `:20 B:19 := (ihom A).obj B

@[simp, reassoc] lemma ev_coev :
  ((ğŸ™ A) âŠ— ((coev A).app B)) â‰« (ev A).app (A âŠ— B) = ğŸ™ (A âŠ— B) :=
adjunction.left_triangle_components (ihom.adjunction A)

@[simp, reassoc] lemma coev_ev :
  (coev A).app (A âŸ¶[C] B) â‰« (ihom A).map ((ev A).app B) = ğŸ™ (A âŸ¶[C] B) :=
adjunction.right_triangle_components (ihom.adjunction A)

end ihom

open category_theory.limits

instance : preserves_colimits (tensor_left A) :=
(ihom.adjunction A).left_adjoint_preserves_colimits

variables {A}

-- Wrap these in a namespace so we don't clash with the core versions.
namespace monoidal_closed

/-- Currying in a monoidal closed category. -/
def curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¶[C] X) :=
(ihom.adjunction A).hom_equiv _ _
/-- Uncurrying in a monoidal closed category. -/
def uncurry : (Y âŸ¶ A âŸ¶[C] X) â†’ (A âŠ— Y âŸ¶ X) :=
((ihom.adjunction A).hom_equiv _ _).symm

@[simp] lemma hom_equiv_apply_eq (f : A âŠ— Y âŸ¶ X) :
  (ihom.adjunction A).hom_equiv _ _ f = curry f := rfl
@[simp] lemma hom_equiv_symm_apply_eq (f : Y âŸ¶ A âŸ¶[C] X) :
  ((ihom.adjunction A).hom_equiv _ _).symm f = uncurry f := rfl

@[reassoc]
lemma curry_natural_left (f : X âŸ¶ X') (g : A âŠ— X' âŸ¶ Y) :
  curry (((ğŸ™ _) âŠ— f) â‰« g) = f â‰« curry g :=
adjunction.hom_equiv_naturality_left _ _ _

@[reassoc]
lemma curry_natural_right (f : A âŠ— X âŸ¶ Y) (g : Y âŸ¶ Y') :
  curry (f â‰« g) = curry f â‰« (ihom _).map g :=
adjunction.hom_equiv_naturality_right _ _ _

@[reassoc]
lemma uncurry_natural_right  (f : X âŸ¶ A âŸ¶[C] Y) (g : Y âŸ¶ Y') :
  uncurry (f â‰« (ihom _).map g) = uncurry f â‰« g :=
adjunction.hom_equiv_naturality_right_symm _ _ _

@[reassoc]
lemma uncurry_natural_left  (f : X âŸ¶ X') (g : X' âŸ¶ A âŸ¶[C] Y) :
  uncurry (f â‰« g) = ((ğŸ™ _) âŠ— f) â‰« uncurry g :=
adjunction.hom_equiv_naturality_left_symm _ _ _

@[simp]
lemma uncurry_curry (f : A âŠ— X âŸ¶ Y) : uncurry (curry f) = f :=
(closed.is_adj.adj.hom_equiv _ _).left_inv f

@[simp]
lemma curry_uncurry (f : X âŸ¶ A âŸ¶[C] Y) : curry (uncurry f) = f :=
(closed.is_adj.adj.hom_equiv _ _).right_inv f

lemma curry_eq_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ A âŸ¶[C] X) :
  curry f = g â†” f = uncurry g :=
adjunction.hom_equiv_apply_eq _ f g

lemma eq_curry_iff (f : A âŠ— Y âŸ¶ X) (g : Y âŸ¶ A âŸ¶[C] X) :
  g = curry f â†” uncurry g = f :=
adjunction.eq_hom_equiv_apply _ f g

-- I don't think these two should be simp.
lemma uncurry_eq (g : Y âŸ¶ A âŸ¶[C] X) : uncurry g = ((ğŸ™ A) âŠ— g) â‰« (ihom.ev A).app X :=
adjunction.hom_equiv_counit _

lemma curry_eq (g : A âŠ— Y âŸ¶ X) : curry g = (ihom.coev A).app Y â‰« (ihom A).map g :=
adjunction.hom_equiv_unit _

lemma curry_injective : function.injective (curry : (A âŠ— Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¶[C] X)) :=
(closed.is_adj.adj.hom_equiv _ _).injective

lemma uncurry_injective : function.injective (uncurry : (Y âŸ¶ A âŸ¶[C] X) â†’ (A âŠ— Y âŸ¶ X)) :=
(closed.is_adj.adj.hom_equiv _ _).symm.injective

variables (A X)

lemma uncurry_id_eq_ev : uncurry (ğŸ™ (A âŸ¶[C] X)) = (ihom.ev A).app X :=
by rw [uncurry_eq, tensor_id, id_comp]

lemma curry_id_eq_coev : curry (ğŸ™ _) = (ihom.coev A).app X :=
by { rw [curry_eq, (ihom A).map_id (A âŠ— _)], apply comp_id }

section pre

variables {A B} [closed B]

/-- Pre-compose an internal hom with an external hom. -/
def pre (f : B âŸ¶ A) : ihom A âŸ¶ ihom B :=
transfer_nat_trans_self (ihom.adjunction _) (ihom.adjunction _) ((tensoring_left C).map f)

lemma id_tensor_pre_app_comp_ev (f : B âŸ¶ A) (X : C) :
  (ğŸ™ B âŠ— ((pre f).app X)) â‰« (ihom.ev B).app X =
    (f âŠ— (ğŸ™ (A âŸ¶[C] X))) â‰« (ihom.ev A).app X :=
transfer_nat_trans_self_counit _ _ ((tensoring_left C).map f) X

lemma uncurry_pre (f : B âŸ¶ A) (X : C) :
  monoidal_closed.uncurry ((pre f).app X) = (f âŠ— ğŸ™ _) â‰« (ihom.ev A).app X :=
by rw [uncurry_eq, id_tensor_pre_app_comp_ev]

lemma coev_app_comp_pre_app (f : B âŸ¶ A) :
  (ihom.coev A).app X â‰« (pre f).app (A âŠ— X) =
    (ihom.coev B).app X â‰« (ihom B).map (f âŠ— (ğŸ™ _)) :=
unit_transfer_nat_trans_self _ _ ((tensoring_left C).map f) X

@[simp]
lemma pre_id (A : C) [closed A] : pre (ğŸ™ A) = ğŸ™ _ :=
by { simp only [pre, functor.map_id], dsimp, simp, }

@[simp]
lemma pre_map {Aâ‚ Aâ‚‚ Aâ‚ƒ : C} [closed Aâ‚] [closed Aâ‚‚] [closed Aâ‚ƒ]
  (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Aâ‚‚ âŸ¶ Aâ‚ƒ) :
  pre (f â‰« g) = pre g â‰« pre f :=
by rw [pre, pre, pre, transfer_nat_trans_self_comp, (tensoring_left C).map_comp]

end pre

/-- The internal hom functor given by the monoidal closed structure. -/
def internal_hom [monoidal_closed C] : Cáµ’áµ– â¥¤ C â¥¤ C :=
{ obj := Î» X, ihom X.unop,
  map := Î» X Y f, pre f.unop }

end monoidal_closed

end category_theory
