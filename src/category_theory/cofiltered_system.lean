/-
Copyright (c) 2022 Kyle Miller, Adam Topaz, R√©mi Bottinelli, Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kyle Miller, Adam Topaz, R√©mi Bottinelli, Junyan Xu
-/
import category_theory.filtered
import data.set.finite
import topology.category.Top.limits

/-!
# Cofiltered systems

This file deals with properties of cofiltered (and inverse) systems.

## Main definitions

Given a functor `F : J ‚•§ Type v`:

* For `j : J`, `F.eventual_range j` is the intersections of all ranges of morphisms `F.map f`
  where `f` has codomain `j`.
* `F.is_mittag_leffler` states that the functor `F` satisfies the Mittag-Leffler
  condition: the ranges of morphisms `F.map f` (with `f` having codomain `j`) stabilize.
* If `J` is cofiltered `F.to_eventual_ranges` is the subfunctor of `F` obtained by restriction
  to `F.eventual_range`.
* `F.to_preimages` restricts a functor to preimages of a given set in some `F.obj i`. If `J` is
  cofiltered, then it is Mittag-Leffler if `F` is, see `is_mittag_leffler.to_preimages`.

## Main statements

* `nonempty_sections_of_finite_cofiltered_system` shows that if `J` is cofiltered and each
  `F.obj j` is nonempty and finite, `F.sections` is nonempty.
* `nonempty_sections_of_finite_inverse_system` is a specialization of the above to `J` being a
   directed set (and `F : J·µí·µñ ‚•§ Type v`).
* `is_mittag_leffler_of_exists_finite_range` shows that if `J` is cofiltered and for all `j`,
  there exists some `i` and `f : i ‚ü∂ j` such that the range of `F.map f` is finite, then
  `F` is Mittag-Leffler.
* `to_eventual_ranges_surjective` shows that if `F` is Mittag-Leffler, then `F.to_eventual_ranges`
  has all morphisms `F.map f` surjective.

## Todo

* Prove [Stacks: Lemma 0597](https://stacks.math.columbia.edu/tag/0597)

## References

* [Stacks: Mittag-Leffler systems](https://stacks.math.columbia.edu/tag/0594)

## Tags

Mittag-Leffler, surjective, eventual range, inverse system,

-/

universes u v w

open category_theory category_theory.is_cofiltered set category_theory.functor_to_types

section finite_konig

/-- This bootstraps `nonempty_sections_of_finite_inverse_system`. In this version,
the `F` functor is between categories of the same universe, and it is an easy
corollary to `Top.nonempty_limit_cone_of_compact_t2_inverse_system`. -/
lemma nonempty_sections_of_finite_cofiltered_system.init
  {J : Type u} [small_category J] [is_cofiltered_or_empty J] (F : J ‚•§ Type u)
  [hf : ‚àÄ j, finite (F.obj j)] [hne : ‚àÄ j, nonempty (F.obj j)] :
  F.sections.nonempty :=
begin
  let F' : J ‚•§ Top := F ‚ãô Top.discrete,
  haveI : ‚àÄ j, discrete_topology (F'.obj j) := Œª _, ‚ü®rfl‚ü©,
  haveI : ‚àÄ j, finite (F'.obj j) := hf,
  haveI : ‚àÄ j, nonempty (F'.obj j) := hne,
  obtain ‚ü®‚ü®u, hu‚ü©‚ü© := Top.nonempty_limit_cone_of_compact_t2_cofiltered_system F',
  exact ‚ü®u, Œª _ _, hu‚ü©,
end

/-- The cofiltered limit of nonempty finite types is nonempty.

See `nonempty_sections_of_finite_inverse_system` for a specialization to inverse limits. -/
theorem nonempty_sections_of_finite_cofiltered_system
  {J : Type u} [category.{w} J] [is_cofiltered_or_empty J] (F : J ‚•§ Type v)
  [‚àÄ (j : J), finite (F.obj j)] [‚àÄ (j : J), nonempty (F.obj j)] :
  F.sections.nonempty :=
begin
  -- Step 1: lift everything to the `max u v w` universe.
  let J' : Type (max w v u) := as_small.{max w v} J,
  let down : J' ‚•§ J := as_small.down,
  let F' : J' ‚•§ Type (max u v w) := down ‚ãô F ‚ãô ulift_functor.{(max u w) v},
  haveI : ‚àÄ i, nonempty (F'.obj i) := Œª i, ‚ü®‚ü®classical.arbitrary (F.obj (down.obj i))‚ü©‚ü©,
  haveI : ‚àÄ i, finite (F'.obj i) := Œª i, finite.of_equiv (F.obj (down.obj i)) equiv.ulift.symm,
  -- Step 2: apply the bootstrap theorem
  casesI is_empty_or_nonempty J,
  { fsplit; exact is_empty_elim },
  haveI : is_cofiltered J := ‚ü®‚ü©,
  obtain ‚ü®u, hu‚ü© := nonempty_sections_of_finite_cofiltered_system.init F',
  -- Step 3: interpret the results
  use Œª j, (u ‚ü®j‚ü©).down,
  intros j j' f,
  have h := @hu (‚ü®j‚ü© : J') (‚ü®j'‚ü© : J') (ulift.up f),
  simp only [as_small.down, functor.comp_map, ulift_functor_map, functor.op_map] at h,
  simp_rw [‚Üêh],
  refl,
end

/-- The inverse limit of nonempty finite types is nonempty.

See `nonempty_sections_of_finite_cofiltered_system` for a generalization to cofiltered limits.
That version applies in almost all cases, and the only difference is that this version
allows `J` to be empty.

This may be regarded as a generalization of K≈ënig's lemma.
To specialize: given a locally finite connected graph, take `J·µí·µñ` to be `‚Ñï` and
`F j` to be length-`j` paths that start from an arbitrary fixed vertex.
Elements of `F.sections` can be read off as infinite rays in the graph. -/
theorem nonempty_sections_of_finite_inverse_system
  {J : Type u} [preorder J] [is_directed J (‚â§)] (F : J·µí·µñ ‚•§ Type v)
  [‚àÄ (j : J·µí·µñ), finite (F.obj j)] [‚àÄ (j : J·µí·µñ), nonempty (F.obj j)] :
  F.sections.nonempty :=
begin
  casesI is_empty_or_nonempty J,
  { haveI : is_empty J·µí·µñ := ‚ü®Œª j, is_empty_elim j.unop‚ü©,  -- TODO: this should be a global instance
    exact ‚ü®is_empty_elim, is_empty_elim‚ü©, },
  { exact nonempty_sections_of_finite_cofiltered_system _, },
end

end finite_konig

namespace category_theory
namespace functor

variables {J : Type u} [category J] (F : J ‚•§ Type v) {i j k : J} (s : set (F.obj i))

/--
The eventual range of the functor `F : J ‚•§ Type v` at index `j : J` is the intersection
of the ranges of all maps `F.map f` with `i : J` and `f : i ‚ü∂ j`.
-/
def eventual_range (j : J) := ‚ãÇ i (f : i ‚ü∂ j), range (F.map f)

lemma mem_eventual_range_iff {x : F.obj j} :
  x ‚àà F.eventual_range j ‚Üî ‚àÄ ‚¶Éi‚¶Ñ (f : i ‚ü∂ j), x ‚àà range (F.map f) := mem_Inter‚ÇÇ

/--
The functor `F : J ‚•§ Type v` satisfies the Mittag-Leffler condition if for all `j : J`,
there exists some `i : J` and `f : i ‚ü∂ j` such that for all `k : J` and `g : k ‚ü∂ j`, the range
of `F.map f` is contained in that of `F.map g`;
in other words (see `is_mittag_leffler_iff_eventual_range`), the eventual range at `j` is attained
by some `f : i ‚ü∂ j`.
-/
def is_mittag_leffler : Prop :=
‚àÄ j : J, ‚àÉ i (f : i ‚ü∂ j), ‚àÄ ‚¶Ék‚¶Ñ (g : k ‚ü∂ j), range (F.map f) ‚äÜ range (F.map g)

lemma is_mittag_leffler_iff_eventual_range : F.is_mittag_leffler ‚Üî
  ‚àÄ j : J, ‚àÉ i (f : i ‚ü∂ j), F.eventual_range j = range (F.map f) :=
forall_congr $ Œª j, exists‚ÇÇ_congr $ Œª i f,
  ‚ü®Œª h, (Inter‚ÇÇ_subset _ _).antisymm $ subset_Inter‚ÇÇ h, Œª h, h ‚ñ∏ Inter‚ÇÇ_subset‚ü©

lemma is_mittag_leffler.subset_image_eventual_range (h : F.is_mittag_leffler) (f : j ‚ü∂ i) :
  F.eventual_range i ‚äÜ F.map f '' (F.eventual_range j) :=
begin
  obtain ‚ü®k, g, hg‚ü© := F.is_mittag_leffler_iff_eventual_range.1 h j,
  rw hg, intros x hx,
  obtain ‚ü®x, rfl‚ü© := F.mem_eventual_range_iff.1 hx (g ‚â´ f),
  refine ‚ü®_, ‚ü®x, rfl‚ü©, by simpa only [F.map_comp]‚ü©,
end

lemma eventual_range_eq_range_precomp (f : i ‚ü∂ j) (g : j ‚ü∂ k)
  (h : F.eventual_range k = range (F.map g)) :
  F.eventual_range k = range (F.map $ f ‚â´ g) :=
begin
  apply subset_antisymm,
  { apply Inter‚ÇÇ_subset, },
  { rw [h, F.map_comp], apply range_comp_subset_range, }
end

lemma is_mittag_leffler_of_surjective
  (h : ‚àÄ ‚¶Éi j : J‚¶Ñ (f :i ‚ü∂ j), (F.map f).surjective) : F.is_mittag_leffler :=
Œª j, ‚ü®j, ùüô j, Œª k g, by rw [map_id, types_id, range_id, (h g).range_eq]‚ü©

/-- The subfunctor of `F` obtained by restricting to the preimages of a set `s ‚àà F.obj i`. -/
@[simps] def to_preimages : J ‚•§ Type v :=
{ obj := Œª j, ‚ãÇ f : j ‚ü∂ i, F.map f ‚Åª¬π' s,
  map := Œª j k g, maps_to.restrict (F.map g) _ _ $ Œª x h, begin
    rw [mem_Inter] at h ‚ä¢, intro f,
    rw [‚Üê mem_preimage, preimage_preimage],
    convert h (g ‚â´ f), rw F.map_comp, refl,
  end,
  map_id' := Œª j, by { simp_rw F.map_id, ext, refl },
  map_comp' := Œª j k l f g, by { simp_rw F.map_comp, refl } }

instance to_preimages_finite [‚àÄ j, finite (F.obj j)] :
  ‚àÄ j, finite ((F.to_preimages s).obj j) := Œª j, subtype.finite

variable [is_cofiltered_or_empty J]

lemma eventual_range_maps_to (f : j ‚ü∂ i) :
  (F.eventual_range j).maps_to (F.map f) (F.eventual_range i) :=
Œª x hx, begin
  rw mem_eventual_range_iff at hx ‚ä¢,
  intros k f',
  obtain ‚ü®l, g, g', he‚ü© := cospan f f',
  obtain ‚ü®x, rfl‚ü© := hx g,
  rw [‚Üê map_comp_apply, he, F.map_comp],
  exact ‚ü®_, rfl‚ü©,
end

lemma is_mittag_leffler.eq_image_eventual_range (h : F.is_mittag_leffler) (f : j ‚ü∂ i) :
  F.eventual_range i = F.map f '' (F.eventual_range j) :=
(h.subset_image_eventual_range F f).antisymm $ maps_to'.1 (F.eventual_range_maps_to f)

lemma eventual_range_eq_iff {f : i ‚ü∂ j} :
  F.eventual_range j = range (F.map f) ‚Üî
  ‚àÄ ‚¶Ék‚¶Ñ (g : k ‚ü∂ i), range (F.map f) ‚äÜ range (F.map $ g ‚â´ f) :=
begin
  rw [subset_antisymm_iff, eventual_range, and_iff_right (Inter‚ÇÇ_subset _ _), subset_Inter‚ÇÇ_iff],
  refine ‚ü®Œª h k g, h _ _, Œª h j' f', _‚ü©,
  obtain ‚ü®k, g, g', he‚ü© := cospan f f',
  refine (h g).trans _,
  rw [he, F.map_comp],
  apply range_comp_subset_range,
end

lemma is_mittag_leffler_iff_subset_range_comp : F.is_mittag_leffler ‚Üî
  ‚àÄ j : J, ‚àÉ i (f : i ‚ü∂ j), ‚àÄ ‚¶Ék‚¶Ñ (g : k ‚ü∂ i), range (F.map f) ‚äÜ range (F.map $ g ‚â´ f) :=
by simp_rw [is_mittag_leffler_iff_eventual_range, eventual_range_eq_iff]

lemma is_mittag_leffler.to_preimages (h : F.is_mittag_leffler) :
  (F.to_preimages s).is_mittag_leffler :=
(is_mittag_leffler_iff_subset_range_comp _).2 $ Œª j, begin
  obtain ‚ü®j‚ÇÅ, g‚ÇÅ, f‚ÇÅ, -‚ü© := cone_objs i j,
  obtain ‚ü®j‚ÇÇ, f‚ÇÇ, h‚ÇÇ‚ü© := F.is_mittag_leffler_iff_eventual_range.1 h j‚ÇÅ,
  refine ‚ü®j‚ÇÇ, f‚ÇÇ ‚â´ f‚ÇÅ, Œª j‚ÇÉ f‚ÇÉ, _‚ü©,
  rintro _ ‚ü®‚ü®x, hx‚ü©, rfl‚ü©,
  have : F.map f‚ÇÇ x ‚àà F.eventual_range j‚ÇÅ, { rw h‚ÇÇ, exact ‚ü®_, rfl‚ü© },
  obtain ‚ü®y, hy, h‚ÇÉ‚ü© := h.subset_image_eventual_range F (f‚ÇÉ ‚â´ f‚ÇÇ) this,
  refine ‚ü®‚ü®y, mem_Inter.2 $ Œª g‚ÇÇ, _‚ü©, subtype.ext _‚ü©,
  { obtain ‚ü®j‚ÇÑ, f‚ÇÑ, h‚ÇÑ‚ü© := cone_maps g‚ÇÇ ((f‚ÇÉ ‚â´ f‚ÇÇ) ‚â´ g‚ÇÅ),
    obtain ‚ü®y, rfl‚ü© := F.mem_eventual_range_iff.1 hy f‚ÇÑ,
    rw ‚Üê map_comp_apply at h‚ÇÉ,
    rw [mem_preimage, ‚Üê map_comp_apply, h‚ÇÑ, ‚Üê category.assoc, map_comp_apply, h‚ÇÉ, ‚Üê map_comp_apply],
    apply mem_Inter.1 hx },
  { simp_rw [to_preimages_map, maps_to.coe_restrict_apply, subtype.coe_mk],
    rw [‚Üê category.assoc, map_comp_apply, h‚ÇÉ, map_comp_apply] },
end

lemma is_mittag_leffler_of_exists_finite_range
  (h : ‚àÄ (j : J), ‚àÉ i (f : i ‚ü∂ j), (range $ F.map f).finite) :
  F.is_mittag_leffler :=
Œª j, begin
  obtain ‚ü®i, hi, hf‚ü© := h j,
  obtain ‚ü®m, ‚ü®i, f, hm‚ü©, hmin‚ü© := finset.is_well_founded_lt.wf.has_min
    {s : finset (F.obj j) | ‚àÉ i (f : i ‚ü∂ j), ‚Üës = range (F.map f)} ‚ü®_, i, hi, hf.coe_to_finset‚ü©,
  refine ‚ü®i, f, Œª k g,
    (directed_on_range.mp $ F.ranges_directed j).is_bot_of_is_min ‚ü®‚ü®i, f‚ü©, rfl‚ü© _ _ ‚ü®‚ü®k, g‚ü©, rfl‚ü©‚ü©,
  rintro _ ‚ü®‚ü®k', g'‚ü©, rfl‚ü© hl,
  refine (eq_of_le_of_not_lt hl _).ge,
  have := hmin _ ‚ü®k', g', (m.finite_to_set.subset $ hm.substr hl).coe_to_finset‚ü©,
  rwa [finset.lt_iff_ssubset, ‚Üê finset.coe_ssubset, set.finite.coe_to_finset, hm] at this,
end

/--
The subfunctor of `F` obtained by restricting to the eventual range at each index.
-/
@[simps] def to_eventual_ranges : J ‚•§ Type v :=
{ obj := Œª j, F.eventual_range j,
  map := Œª i j f, (F.eventual_range_maps_to f).restrict _ _ _,
  map_id' := Œª i, by { simp_rw F.map_id, ext, refl },
  map_comp' := Œª _ _ _ _ _, by { simp_rw F.map_comp, refl } }

instance to_eventual_ranges_finite [‚àÄ j, finite (F.obj j)] :
  ‚àÄ j, finite (F.to_eventual_ranges.obj j) := Œª j, subtype.finite

/--
The sections of the functor `F : J ‚•§ Type v` are in bijection with the sections of
`F.eventual_ranges`.
-/
def to_eventual_ranges_sections_equiv : F.to_eventual_ranges.sections ‚âÉ F.sections :=
{ to_fun := Œª s, ‚ü®_, Œª i j f, subtype.coe_inj.2 $ s.prop f‚ü©,
  inv_fun := Œª s, ‚ü®Œª j, ‚ü®_, mem_Inter‚ÇÇ.2 $ Œª i f, ‚ü®_, s.prop f‚ü©‚ü©, Œª i j f, subtype.ext $ s.prop f‚ü©,
  left_inv := Œª _, by { ext, refl },
  right_inv := Œª _, by { ext, refl } }

/--
If `F` satisfies the Mittag-Leffler condition, its restriction to eventual ranges is a surjective
functor.
-/
lemma surjective_to_eventual_ranges (h : F.is_mittag_leffler) ‚¶Éi j‚¶Ñ (f : i ‚ü∂ j) :
  (F.to_eventual_ranges.map f).surjective :=
Œª ‚ü®x, hx‚ü©, by { obtain ‚ü®y, hy, rfl‚ü© := h.subset_image_eventual_range F f hx, exact ‚ü®‚ü®y, hy‚ü©, rfl‚ü© }

/-- If `F` is nonempty at each index and Mittag-Leffler, then so is `F.to_eventual_ranges`. -/
lemma to_eventual_ranges_nonempty (h : F.is_mittag_leffler) [‚àÄ (j : J), nonempty (F.obj j)]
  (j : J) : nonempty (F.to_eventual_ranges.obj j) :=
let ‚ü®i, f, h‚ü© := F.is_mittag_leffler_iff_eventual_range.1 h j in
by { rw [to_eventual_ranges_obj, h], apply_instance }

/-- If `F` has all arrows surjective, then it "factors through a poset". -/
lemma thin_diagram_of_surjective (Fsur : ‚àÄ ‚¶Éi j : J‚¶Ñ (f : i ‚ü∂ j), (F.map f).surjective)
  {i j} (f g : i ‚ü∂ j) : F.map f = F.map g :=
let ‚ü®k, œÜ, hœÜ‚ü© := cone_maps f g in
(Fsur œÜ).injective_comp_right $ by simp_rw [‚Üê types_comp, ‚Üê F.map_comp, hœÜ]

lemma to_preimages_nonempty_of_surjective [hFn : ‚àÄ (j : J), nonempty (F.obj j)]
  (Fsur : ‚àÄ ‚¶Éi j : J‚¶Ñ (f : i ‚ü∂ j), (F.map f).surjective)
  (hs : s.nonempty) (j) : nonempty ((F.to_preimages s).obj j) :=
begin
  simp only [to_preimages_obj, nonempty_coe_sort, nonempty_Inter, mem_preimage],
  obtain (h|‚ü®‚ü®ji‚ü©‚ü©) := is_empty_or_nonempty (j ‚ü∂ i),
  { exact ‚ü®(hFn j).some, Œª ji, h.elim ji‚ü©, },
  { obtain ‚ü®y, ys‚ü© := hs,
    obtain ‚ü®x, rfl‚ü© := Fsur ji y,
    exact ‚ü®x, Œª ji', (F.thin_diagram_of_surjective Fsur ji' ji).symm ‚ñ∏ ys‚ü©, },
end

lemma eval_section_injective_of_eventually_injective
  {j} (Finj : ‚àÄ i (f : i ‚ü∂ j), (F.map f).injective) (i) (f : i ‚ü∂ j) :
  (Œª s : F.sections, s.val j).injective :=
begin
  refine Œª s‚ÇÄ s‚ÇÅ h, subtype.ext $ funext $ Œª k, _,
  obtain ‚ü®m, mi, mk, _‚ü© := cone_objs i k,
  dsimp at h,
  rw [‚Üês‚ÇÄ.prop (mi ‚â´ f), ‚Üês‚ÇÅ.prop (mi ‚â´ f)] at h,
  rw [‚Üês‚ÇÄ.prop mk, ‚Üês‚ÇÅ.prop mk],
  refine congr_arg _ (Finj m (mi ‚â´ f) h),
end

section finite_cofiltered_system

variables [‚àÄ (j : J), nonempty (F.obj j)] [‚àÄ (j : J), finite (F.obj j)]
  (Fsur : ‚àÄ ‚¶Éi j : J‚¶Ñ (f :i ‚ü∂ j), (F.map f).surjective)

include Fsur
lemma eval_section_surjective_of_surjective (i : J) :
  (Œª s : F.sections, s.val i).surjective := Œª x,
begin
  let s : set (F.obj i) := {x},
  haveI := F.to_preimages_nonempty_of_surjective s Fsur (singleton_nonempty x),
  obtain ‚ü®sec, h‚ü© := nonempty_sections_of_finite_cofiltered_system (F.to_preimages s),
  refine ‚ü®‚ü®Œª j, (sec j).val, Œª j k jk, by simpa [subtype.ext_iff] using h jk‚ü©, _‚ü©,
  { have := (sec i).prop,
    simp only [mem_Inter, mem_preimage, mem_singleton_iff] at this,
    replace this := this (ùüô i), rwa [map_id_apply] at this, },
end

lemma eventually_injective [nonempty J] [finite F.sections] :
  ‚àÉ j, ‚àÄ i (f : i ‚ü∂ j), (F.map f).injective :=
begin
  haveI : ‚àÄ j, fintype (F.obj j) := Œª j, fintype.of_finite (F.obj j),
  haveI : fintype F.sections := fintype.of_finite F.sections,
  have card_le : ‚àÄ j, fintype.card (F.obj j) ‚â§ fintype.card F.sections :=
    Œª j, fintype.card_le_of_surjective _ (F.eval_section_surjective_of_surjective Fsur j),
  let fn := Œª j, fintype.card F.sections - fintype.card (F.obj j),
  refine ‚ü®fn.argmin nat.well_founded_lt.wf, Œª i f, ((fintype.bijective_iff_surjective_and_card _).2
    ‚ü®Fsur f, le_antisymm _ (fintype.card_le_of_surjective _ $ Fsur f)‚ü©).1‚ü©,
  rw [‚Üê nat.sub_sub_self (card_le i), tsub_le_iff_tsub_le],
  apply fn.argmin_le,
end

end finite_cofiltered_system

end functor
end category_theory
