/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johan Commelin, Bhavik Mehta
-/
import category_theory.natural_isomorphism

/-!
# Comma categories

A comma category is a construction in category theory, which builds a category out of two functors
with a common codomain. Specifically, for functors `L : A â¥¤ T` and `R : B â¥¤ T`, an object in
`comma L R` is a morphism `hom : L.obj left âŸ¶ R.obj right` for some objects `left : A` and
`right : B`, and a morphism in `comma L R` between `hom : L.obj left âŸ¶ R.obj right` and
`hom' : L.obj left' âŸ¶ R.obj right'` is a commutative square

```
L.obj left   âŸ¶   L.obj left'
      |               |
  hom |               | hom'
      â†“               â†“
R.obj right  âŸ¶   R.obj right',
```

where the top and bottom morphism come from morphisms `left âŸ¶ left'` and `right âŸ¶ right'`,
respectively.

## Main definitions

* `comma L R`: the comma category of the functors `L` and `R`.
* `over X`: the over category of the object `X` (developed in `over.lean`).
* `under X`: the under category of the object `X` (also developed in `over.lean`).
* `arrow T`: the arrow category of the category `T` (developed in `arrow.lean`).

## References

* <https://ncatlab.org/nlab/show/comma+category>

## Tags

comma, slice, coslice, over, under, arrow
-/


namespace category_theory

-- declare the `v`'s first; see `category_theory.category` for an explanation
universes vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ vâ‚… uâ‚ uâ‚‚ uâ‚ƒ uâ‚„ uâ‚…
variables {A : Type uâ‚} [category.{vâ‚} A]
variables {B : Type uâ‚‚} [category.{vâ‚‚} B]
variables {T : Type uâ‚ƒ} [category.{vâ‚ƒ} T]

/-- The objects of the comma category are triples of an object `left : A`, an object
   `right : B` and a morphism `hom : L.obj left âŸ¶ R.obj right`.  -/
structure comma (L : A â¥¤ T) (R : B â¥¤ T) : Type (max uâ‚ uâ‚‚ vâ‚ƒ) :=
(left : A . obviously)
(right : B . obviously)
(hom : L.obj left âŸ¶ R.obj right)

-- Satisfying the inhabited linter
instance comma.inhabited [inhabited T] : inhabited (comma (ğŸ­ T) (ğŸ­ T)) :=
{ default :=
  { left := default T,
    right := default T,
    hom := ğŸ™ (default T) } }

variables {L : A â¥¤ T} {R : B â¥¤ T}

/-- A morphism between two objects in the comma category is a commutative square connecting the
    morphisms coming from the two objects using morphisms in the image of the functors `L` and `R`.
-/
@[ext] structure comma_morphism (X Y : comma L R) :=
(left : X.left âŸ¶ Y.left . obviously)
(right : X.right âŸ¶ Y.right . obviously)
(w' : L.map left â‰« Y.hom = X.hom â‰« R.map right . obviously)

-- Satisfying the inhabited linter
instance comma_morphism.inhabited [inhabited (comma L R)] :
  inhabited (comma_morphism (default (comma L R)) (default (comma L R))) :=
{ default :=
  { left := ğŸ™ _,
    right := ğŸ™ _ } }

restate_axiom comma_morphism.w'
attribute [simp, reassoc] comma_morphism.w

instance comma_category : category (comma L R) :=
{ hom := comma_morphism,
  id := Î» X,
  { left := ğŸ™ X.left,
    right := ğŸ™ X.right },
  comp := Î» X Y Z f g,
  { left := f.left â‰« g.left,
    right := f.right â‰« g.right } }

namespace comma

section
variables {X Y Z : comma L R} {f : X âŸ¶ Y} {g : Y âŸ¶ Z}

@[simp] lemma id_left  : ((ğŸ™ X) : comma_morphism X X).left = ğŸ™ X.left := rfl
@[simp] lemma id_right : ((ğŸ™ X) : comma_morphism X X).right = ğŸ™ X.right := rfl
@[simp] lemma comp_left  : (f â‰« g).left  = f.left â‰« g.left   := rfl
@[simp] lemma comp_right : (f â‰« g).right = f.right â‰« g.right := rfl

end

variables (L) (R)

/-- The functor sending an object `X` in the comma category to `X.left`. -/
@[simps]
def fst : comma L R â¥¤ A :=
{ obj := Î» X, X.left,
  map := Î» _ _ f, f.left }

/-- The functor sending an object `X` in the comma category to `X.right`. -/
@[simps]
def snd : comma L R â¥¤ B :=
{ obj := Î» X, X.right,
  map := Î» _ _ f, f.right }

/-- We can interpret the commutative square constituting a morphism in the comma category as a
    natural transformation between the functors `fst â‹™ L` and `snd â‹™ R` from the comma category
    to `T`, where the components are given by the morphism that constitutes an object of the comma
    category. -/
@[simps]
def nat_trans : fst L R â‹™ L âŸ¶ snd L R â‹™ R :=
{ app := Î» X, X.hom }

section
variables {Lâ‚ Lâ‚‚ Lâ‚ƒ : A â¥¤ T} {Râ‚ Râ‚‚ Râ‚ƒ : B â¥¤ T}

/--
Construct an isomorphism in the comma category given isomorphisms of the objects whose forward
directions give a commutative square.
-/
@[simps]
def iso_mk {X Y : comma Lâ‚ Râ‚} (l : X.left â‰… Y.left) (r : X.right â‰… Y.right)
  (h : Lâ‚.map l.hom â‰« Y.hom = X.hom â‰« Râ‚.map r.hom) : X â‰… Y :=
{ hom := { left := l.hom, right := r.hom },
  inv :=
  { left := l.inv,
    right := r.inv,
    w' := begin
      rw [â†Lâ‚.map_iso_inv l, iso.inv_comp_eq, Lâ‚.map_iso_hom, reassoc_of h, â† Râ‚.map_comp],
      simp
    end, } }

/-- A natural transformation `Lâ‚ âŸ¶ Lâ‚‚` induces a functor `comma Lâ‚‚ R â¥¤ comma Lâ‚ R`. -/
@[simps]
def map_left (l : Lâ‚ âŸ¶ Lâ‚‚) : comma Lâ‚‚ R â¥¤ comma Lâ‚ R :=
{ obj := Î» X,
  { left  := X.left,
    right := X.right,
    hom   := l.app X.left â‰« X.hom },
  map := Î» X Y f,
  { left  := f.left,
    right := f.right } }

/-- The functor `comma L R â¥¤ comma L R` induced by the identity natural transformation on `L` is
    naturally isomorphic to the identity functor. -/
@[simps]
def map_left_id : map_left R (ğŸ™ L) â‰… ğŸ­ _ :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

/-- The functor `comma Lâ‚ R â¥¤ comma Lâ‚ƒ R` induced by the composition of two natural transformations
    `l : Lâ‚ âŸ¶ Lâ‚‚` and `l' : Lâ‚‚ âŸ¶ Lâ‚ƒ` is naturally isomorphic to the composition of the two functors
    induced by these natural transformations. -/
@[simps]
def map_left_comp (l : Lâ‚ âŸ¶ Lâ‚‚) (l' : Lâ‚‚ âŸ¶ Lâ‚ƒ) :
  (map_left R (l â‰« l')) â‰… (map_left R l') â‹™ (map_left R l) :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

/-- A natural transformation `Râ‚ âŸ¶ Râ‚‚` induces a functor `comma L Râ‚ â¥¤ comma L Râ‚‚`. -/
@[simps]
def map_right (r : Râ‚ âŸ¶ Râ‚‚) : comma L Râ‚ â¥¤ comma L Râ‚‚ :=
{ obj := Î» X,
  { left  := X.left,
    right := X.right,
    hom   := X.hom â‰« r.app X.right },
  map := Î» X Y f,
  { left  := f.left,
    right := f.right } }

/-- The functor `comma L R â¥¤ comma L R` induced by the identity natural transformation on `R` is
    naturally isomorphic to the identity functor. -/
@[simps]
def map_right_id : map_right L (ğŸ™ R) â‰… ğŸ­ _ :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

/-- The functor `comma L Râ‚ â¥¤ comma L Râ‚ƒ` induced by the composition of the natural transformations
    `r : Râ‚ âŸ¶ Râ‚‚` and `r' : Râ‚‚ âŸ¶ Râ‚ƒ` is naturally isomorphic to the composition of the functors
    induced by these natural transformations. -/
@[simps]
def map_right_comp (r : Râ‚ âŸ¶ Râ‚‚) (r' : Râ‚‚ âŸ¶ Râ‚ƒ) :
  (map_right L (r â‰« r')) â‰… (map_right L r) â‹™ (map_right L r') :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

end

section
variables {C : Type uâ‚„} [category.{vâ‚„} C] {D : Type uâ‚…} [category.{vâ‚…} D]

/-- The functor `(F â‹™ L, R) â¥¤ (L, R)` -/
@[simps] def pre_left (F: C â¥¤ A) (L : A â¥¤ T) (R : B â¥¤ T) : comma (F â‹™ L) R â¥¤ comma L R :=
{ obj := Î» X, { left := F.obj X.left, right := X.right, hom := X.hom },
  map := Î» X Y f, { left := F.map f.left, right := f.right, w' := by simpa using f.w } }

/-- The functor `(F â‹™ L, R) â¥¤ (L, R)` -/
@[simps] def pre_right (L : A â¥¤ T) (F: C â¥¤ B) (R : B â¥¤ T) : comma L (F â‹™ R) â¥¤ comma L R :=
{ obj := Î» X, { left := X.left, right := F.obj X.right, hom := X.hom },
  map := Î» X Y f, { left := f.left, right := F.map f.right, w' := by simp } }

/-- The functor `(L, R) â¥¤ (L â‹™ F, R â‹™ F)` -/
@[simps] def post (L : A â¥¤ T) (R : B â¥¤ T) (F: T â¥¤ C) : comma L R â¥¤ comma (L â‹™ F) (R â‹™ F) :=
{ obj := Î» X, { left := X.left, right := X.right, hom := F.map X.hom },
  map := Î» X Y f, { left := f.left, right := f.right, w' :=
    by { simp only [functor.comp_map, â†F.map_comp, f.w] } } }

end
end comma

end category_theory
