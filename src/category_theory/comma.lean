/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johan Commelin, Bhavik Mehta
-/
import category_theory.punit
import category_theory.reflect_isomorphisms

/-!
# Comma categories

A comma category is a construction in category theory, which builds a category out of two functors
with a common codomain. Specifically, for functors `L : A â¥¤ T` and `R : B â¥¤ T`, an object in
`comma L R` is a morphism `hom : L.obj left âŸ¶ R.obj right` for some objects `left : A` and
`right : B`, and a morphism in `comma L R` between `hom : L.obj left âŸ¶ R.obj right` and
`hom' : L.obj left' âŸ¶ R.obj right'` is a commutative square

L.obj left   âŸ¶   L.obj left'
      |               |
  hom |               | hom'
      â†“               â†“
R.obj right  âŸ¶   R.obj right',

where the top and bottom morphism come from morphisms `left âŸ¶ left'` and `right âŸ¶ right'`,
respectively.

Several important constructions are special cases of this construction.
* If `L` is the identity functor and `R` is a constant functor, then `comma L R` is the "slice" or
  "over" category over the object `R` maps to.
* Conversely, if `L` is a constant functor and `R` is the identity functor, then `comma L R` is the
  "coslice" or "under" category under the object `L` maps to.
* If `L` and `R` both are the identity functor, then `comma L R` is the arrow category of `T`.

## Main definitions

* `comma L R`: the comma category of the functors `L` and `R`.
* `over X`: the over category of the object `X`.
* `under X`: the under category of the object `X`.
* `arrow T`: the arrow category of the category `T`.

## References

* https://ncatlab.org/nlab/show/comma+category

## Tags

comma, slice, coslice, over, under, arrow
-/


namespace category_theory

universes vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ -- declare the `v`'s first; see `category_theory.category` for an explanation
variables {A : Type uâ‚} [category.{vâ‚} A]
variables {B : Type uâ‚‚} [category.{vâ‚‚} B]
variables {T : Type uâ‚ƒ} [category.{vâ‚ƒ} T]

/-- The objects of the comma category are triples of an object `left : A`, an object
   `right : B` and a morphism `hom : L.obj left âŸ¶ R.obj right`.  -/
structure comma (L : A â¥¤ T) (R : B â¥¤ T) : Type (max uâ‚ uâ‚‚ vâ‚ƒ) :=
(left : A . obviously)
(right : B . obviously)
(hom : L.obj left âŸ¶ R.obj right)

-- Satisfying the inhabited linter
instance comma.inhabited [inhabited T] : inhabited (comma (ğŸ­ T) (ğŸ­ T)) :=
{ default :=
  { left := default T,
    right := default T,
    hom := ğŸ™ (default T) } }

variables {L : A â¥¤ T} {R : B â¥¤ T}

/-- A morphism between two objects in the comma category is a commutative square connecting the
    morphisms coming from the two objects using morphisms in the image of the functors `L` and `R`.
-/
@[ext] structure comma_morphism (X Y : comma L R) :=
(left : X.left âŸ¶ Y.left . obviously)
(right : X.right âŸ¶ Y.right . obviously)
(w' : L.map left â‰« Y.hom = X.hom â‰« R.map right . obviously)

-- Satisfying the inhabited linter
instance comma_morphism.inhabited [inhabited (comma L R)] :
  inhabited (comma_morphism (default (comma L R)) (default (comma L R))) :=
{ default :=
  { left := ğŸ™ _,
    right := ğŸ™ _ } }

restate_axiom comma_morphism.w'
attribute [simp, reassoc] comma_morphism.w

instance comma_category : category (comma L R) :=
{ hom := comma_morphism,
  id := Î» X,
  { left := ğŸ™ X.left,
    right := ğŸ™ X.right },
  comp := Î» X Y Z f g,
  { left := f.left â‰« g.left,
    right := f.right â‰« g.right } }

namespace comma

section
variables {X Y Z : comma L R} {f : X âŸ¶ Y} {g : Y âŸ¶ Z}

@[simp] lemma id_left  : ((ğŸ™ X) : comma_morphism X X).left = ğŸ™ X.left := rfl
@[simp] lemma id_right : ((ğŸ™ X) : comma_morphism X X).right = ğŸ™ X.right := rfl
@[simp] lemma comp_left  : (f â‰« g).left  = f.left â‰« g.left   := rfl
@[simp] lemma comp_right : (f â‰« g).right = f.right â‰« g.right := rfl

end

variables (L) (R)

/-- The functor sending an object `X` in the comma category to `X.left`. -/
@[simps]
def fst : comma L R â¥¤ A :=
{ obj := Î» X, X.left,
  map := Î» _ _ f, f.left }

/-- The functor sending an object `X` in the comma category to `X.right`. -/
@[simps]
def snd : comma L R â¥¤ B :=
{ obj := Î» X, X.right,
  map := Î» _ _ f, f.right }

/-- We can interpret the commutative square constituting a morphism in the comma category as a
    natural transformation between the functors `fst â‹™ L` and `snd â‹™ R` from the comma category
    to `T`, where the components are given by the morphism that constitutes an object of the comma
    category. -/
@[simps]
def nat_trans : fst L R â‹™ L âŸ¶ snd L R â‹™ R :=
{ app := Î» X, X.hom }

section
variables {Lâ‚ Lâ‚‚ Lâ‚ƒ : A â¥¤ T} {Râ‚ Râ‚‚ Râ‚ƒ : B â¥¤ T}

/-- A natural transformation `Lâ‚ âŸ¶ Lâ‚‚` induces a functor `comma Lâ‚‚ R â¥¤ comma Lâ‚ R`. -/
@[simps]
def map_left (l : Lâ‚ âŸ¶ Lâ‚‚) : comma Lâ‚‚ R â¥¤ comma Lâ‚ R :=
{ obj := Î» X,
  { left  := X.left,
    right := X.right,
    hom   := l.app X.left â‰« X.hom },
  map := Î» X Y f,
  { left  := f.left,
    right := f.right } }

/-- The functor `comma L R â¥¤ comma L R` induced by the identity natural transformation on `L` is
    naturally isomorphic to the identity functor. -/
@[simps]
def map_left_id : map_left R (ğŸ™ L) â‰… ğŸ­ _ :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

/-- The functor `comma Lâ‚ R â¥¤ comma Lâ‚ƒ R` induced by the composition of two natural transformations
    `l : Lâ‚ âŸ¶ Lâ‚‚` and `l' : Lâ‚‚ âŸ¶ Lâ‚ƒ` is naturally isomorphic to the composition of the two functors
    induced by these natural transformations. -/
@[simps]
def map_left_comp (l : Lâ‚ âŸ¶ Lâ‚‚) (l' : Lâ‚‚ âŸ¶ Lâ‚ƒ) :
  (map_left R (l â‰« l')) â‰… (map_left R l') â‹™ (map_left R l) :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

/-- A natural transformation `Râ‚ âŸ¶ Râ‚‚` induces a functor `comma L Râ‚ â¥¤ comma L Râ‚‚`. -/
@[simps]
def map_right (r : Râ‚ âŸ¶ Râ‚‚) : comma L Râ‚ â¥¤ comma L Râ‚‚ :=
{ obj := Î» X,
  { left  := X.left,
    right := X.right,
    hom   := X.hom â‰« r.app X.right },
  map := Î» X Y f,
  { left  := f.left,
    right := f.right } }

/-- The functor `comma L R â¥¤ comma L R` induced by the identity natural transformation on `R` is
    naturally isomorphic to the identity functor. -/
@[simps]
def map_right_id : map_right L (ğŸ™ R) â‰… ğŸ­ _ :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

/-- The functor `comma L Râ‚ â¥¤ comma L Râ‚ƒ` induced by the composition of the natural transformations
    `r : Râ‚ âŸ¶ Râ‚‚` and `r' : Râ‚‚ âŸ¶ Râ‚ƒ` is naturally isomorphic to the composition of the functors
    induced by these natural transformations. -/
@[simps]
def map_right_comp (r : Râ‚ âŸ¶ Râ‚‚) (r' : Râ‚‚ âŸ¶ Râ‚ƒ) :
  (map_right L (r â‰« r')) â‰… (map_right L r) â‹™ (map_right L r') :=
{ hom :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } },
  inv :=
  { app := Î» X, { left := ğŸ™ _, right := ğŸ™ _ } } }

end

end comma

/-- The over category has as objects arrows in `T` with codomain `X` and as morphisms commutative
    triangles. -/
@[derive category]
def over (X : T) := comma.{vâ‚ƒ 0 vâ‚ƒ} (ğŸ­ T) (functor.from_punit X)

-- Satisfying the inhabited linter
instance over.inhabited [inhabited T] : inhabited (over (default T)) :=
{ default :=
  { left := default T,
    hom := ğŸ™ _ } }

namespace over

variables {X : T}

@[ext] lemma over_morphism.ext {X : T} {U V : over X} {f g : U âŸ¶ V}
  (h : f.left = g.left) : f = g :=
by tidy

@[simp] lemma over_right (U : over X) : U.right = punit.star := by tidy
-- @[simp] lemma over_morphism_right {U V : over X} (f : U âŸ¶ V) : f.right = sorry :=
-- begin

-- end

@[simp] lemma id_left (U : over X) : comma_morphism.left (ğŸ™ U) = ğŸ™ U.left := rfl
@[simp] lemma comp_left (a b c : over X) (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (f â‰« g).left = f.left â‰« g.left := rfl

@[simp, reassoc] lemma w {A B : over X} (f : A âŸ¶ B) : f.left â‰« B.hom = A.hom :=
by have := f.w; tidy

/-- To give an object in the over category, it suffices to give a morphism with codomain `X`. -/
def mk {X Y : T} (f : Y âŸ¶ X) : over X :=
{ left := Y, hom := f }

@[simp] lemma mk_left {X Y : T} (f : Y âŸ¶ X) : (mk f).left = Y := rfl
@[simp] lemma mk_hom {X Y : T} (f : Y âŸ¶ X) : (mk f).hom = f := rfl

/-- To give a morphism in the over category, it suffices to give an arrow fitting in a commutative
    triangle. -/
@[simps]
def hom_mk {U V : over X} (f : U.left âŸ¶ V.left) (w : f â‰« V.hom = U.hom . obviously) :
  U âŸ¶ V :=
{ left := f }

/-- The forgetful functor mapping an arrow to its domain. -/
def forget : (over X) â¥¤ T := comma.fst _ _

@[simp] lemma forget_obj {U : over X} : forget.obj U = U.left := rfl
@[simp] lemma forget_map {U V : over X} {f : U âŸ¶ V} : forget.map f = f.left := rfl

/-- A morphism `f : X âŸ¶ Y` induces a functor `over X â¥¤ over Y` in the obvious way. -/
def map {Y : T} (f : X âŸ¶ Y) : over X â¥¤ over Y := comma.map_right _ $ discrete.nat_trans (Î» _, f)

section
variables {Y : T} {f : X âŸ¶ Y} {U V : over X} {g : U âŸ¶ V}
@[simp] lemma map_obj_left : ((map f).obj U).left = U.left := rfl
@[simp] lemma map_obj_hom  : ((map f).obj U).hom  = U.hom â‰« f := rfl
@[simp] lemma map_map_left : ((map f).map g).left = g.left := rfl
end

instance forget_reflects_iso : reflects_isomorphisms (forget : over X â¥¤ T) :=
{ reflects := Î» X Y f t, by exactI
  { inv := over.hom_mk t.inv ((as_iso (forget.map f)).inv_comp_eq.2 (over.w f).symm) } }

section iterated_slice
variables (f : over X)

/-- Given f : Y âŸ¶ X, this is the obvious functor from (T/X)/f to T/Y -/
@[simps]
def iterated_slice_forward : over f â¥¤ over f.left :=
{ obj := Î» Î±, over.mk Î±.hom.left,
  map := Î» Î± Î² Îº, over.hom_mk Îº.left.left (by { rw auto_param_eq, rw â† over.w Îº, refl }) }

/-- Given f : Y âŸ¶ X, this is the obvious functor from T/Y to (T/X)/f -/
@[simps]
def iterated_slice_backward : over f.left â¥¤ over f :=
{ obj := Î» g, mk (hom_mk g.hom : mk (g.hom â‰« f.hom) âŸ¶ f),
  map := Î» g h Î±, hom_mk (hom_mk Î±.left (w_assoc Î± f.hom)) (over_morphism.ext (w Î±)) }

/-- Given f : Y âŸ¶ X, we have an equivalence between (T/X)/f and T/Y -/
@[simps]
def iterated_slice_equiv : over f â‰Œ over f.left :=
{ functor := iterated_slice_forward f,
  inverse := iterated_slice_backward f,
  unit_iso :=
    nat_iso.of_components
    (Î» g, âŸ¨hom_mk (hom_mk (ğŸ™ g.left.left)) (by apply_auto_param),
           hom_mk (hom_mk (ğŸ™ g.left.left)) (by apply_auto_param),
           by { ext, dsimp, simp }, by { ext, dsimp, simp }âŸ©)
    (Î» X Y g, by { ext, dsimp, simp }),
  counit_iso :=
    nat_iso.of_components
    (Î» g, âŸ¨hom_mk (ğŸ™ g.left) (by apply_auto_param),
           hom_mk (ğŸ™ g.left) (by apply_auto_param),
           by { ext, dsimp, simp }, by { ext, dsimp, simp }âŸ©)
    (Î» X Y g, by { ext, dsimp, simp }) }

lemma iterated_slice_forward_forget :
  iterated_slice_forward f â‹™ forget = forget â‹™ forget :=
rfl

lemma iterated_slice_backward_forget_forget :
  iterated_slice_backward f â‹™ forget â‹™ forget = forget :=
rfl

end iterated_slice

section
variables {D : Type uâ‚ƒ} [category.{vâ‚ƒ} D]

/-- A functor `F : T â¥¤ D` induces a functor `over X â¥¤ over (F.obj X)` in the obvious way. -/
def post (F : T â¥¤ D) : over X â¥¤ over (F.obj X) :=
{ obj := Î» Y, mk $ F.map Y.hom,
  map := Î» Yâ‚ Yâ‚‚ f,
  { left := F.map f.left,
    w' := by tidy; erw [â† F.map_comp, w] } }

end

end over

/-- The under category has as objects arrows with domain `X` and as morphisms commutative
    triangles. -/
@[derive category]
def under (X : T) := comma.{0 vâ‚ƒ vâ‚ƒ} (functor.from_punit X) (ğŸ­ T)

-- Satisfying the inhabited linter
instance under.inhabited [inhabited T] : inhabited (under (default T)) :=
{ default :=
  { right := default T,
    hom := ğŸ™ _ } }

namespace under

variables {X : T}

@[ext] lemma under_morphism.ext {X : T} {U V : under X} {f g : U âŸ¶ V}
  (h : f.right = g.right) : f = g :=
by tidy

@[simp] lemma under_left (U : under X) : U.left = punit.star := by tidy

@[simp] lemma id_right (U : under X) : comma_morphism.right (ğŸ™ U) = ğŸ™ U.right := rfl
@[simp] lemma comp_right (a b c : under X) (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (f â‰« g).right = f.right â‰« g.right := rfl

@[simp] lemma w {A B : under X} (f : A âŸ¶ B) : A.hom â‰« f.right = B.hom :=
by have := f.w; tidy

/-- To give an object in the under category, it suffices to give an arrow with domain `X`. -/
@[simps]
def mk {X Y : T} (f : X âŸ¶ Y) : under X :=
{ right := Y, hom := f }

/-- To give a morphism in the under category, it suffices to give a morphism fitting in a
    commutative triangle. -/
@[simps]
def hom_mk {U V : under X} (f : U.right âŸ¶ V.right) (w : U.hom â‰« f = V.hom . obviously) :
  U âŸ¶ V :=
{ right := f }

/-- The forgetful functor mapping an arrow to its domain. -/
def forget : (under X) â¥¤ T := comma.snd _ _

@[simp] lemma forget_obj {U : under X} : forget.obj U = U.right := rfl
@[simp] lemma forget_map {U V : under X} {f : U âŸ¶ V} : forget.map f = f.right := rfl

/-- A morphism `X âŸ¶ Y` induces a functor `under Y â¥¤ under X` in the obvious way. -/
def map {Y : T} (f : X âŸ¶ Y) : under Y â¥¤ under X := comma.map_left _ $ discrete.nat_trans (Î» _, f)

section
variables {Y : T} {f : X âŸ¶ Y} {U V : under Y} {g : U âŸ¶ V}
@[simp] lemma map_obj_right : ((map f).obj U).right = U.right := rfl
@[simp] lemma map_obj_hom   : ((map f).obj U).hom   = f â‰« U.hom := rfl
@[simp] lemma map_map_right : ((map f).map g).right = g.right := rfl
end

section
variables {D : Type uâ‚ƒ} [category.{vâ‚ƒ} D]

/-- A functor `F : T â¥¤ D` induces a functor `under X â¥¤ under (F.obj X)` in the obvious way. -/
def post {X : T} (F : T â¥¤ D) : under X â¥¤ under (F.obj X) :=
{ obj := Î» Y, mk $ F.map Y.hom,
  map := Î» Yâ‚ Yâ‚‚ f,
  { right := F.map f.right,
    w' := by tidy; erw [â† F.map_comp, w] } }

end

end under

section
variables (T)

/-- The arrow category of `T` has as objects all morphisms in `T` and as morphisms commutative
     squares in `T`. -/
@[derive category]
def arrow := comma.{vâ‚ƒ vâ‚ƒ vâ‚ƒ} (ğŸ­ T) (ğŸ­ T)

-- Satisfying the inhabited linter
instance arrow.inhabited [inhabited T] : inhabited (arrow T) :=
{ default := show comma (ğŸ­ T) (ğŸ­ T), from default (comma (ğŸ­ T) (ğŸ­ T)) }

end

namespace arrow

@[simp] lemma id_left (f : arrow T) : comma_morphism.left (ğŸ™ f) = ğŸ™ (f.left) := rfl
@[simp] lemma id_right (f : arrow T) : comma_morphism.right (ğŸ™ f) = ğŸ™ (f.right) := rfl

/-- An object in the arrow category is simply a morphism in `T`. -/
@[simps]
def mk {X Y : T} (f : X âŸ¶ Y) : arrow T :=
{ left := X,
  right := Y,
  hom := f }

/-- A morphism in the arrow category is a commutative square connecting two objects of the arrow
    category. -/
@[simps]
def hom_mk {f g : arrow T} {u : f.left âŸ¶ g.left} {v : f.right âŸ¶ g.right}
  (w : u â‰« g.hom = f.hom â‰« v) : f âŸ¶ g :=
{ left := u,
  right := v,
  w' := w }

/-- We can also build a morphism in the arrow category out of any commutative square in `T`. -/
@[simps]
def hom_mk' {X Y : T} {f : X âŸ¶ Y} {P Q : T} {g : P âŸ¶ Q} {u : X âŸ¶ P} {v : Y âŸ¶ Q}
  (w : u â‰« g = f â‰« v) : arrow.mk f âŸ¶ arrow.mk g :=
{ left := u,
  right := v,
  w' := w }

@[reassoc] lemma w {f g : arrow T} (sq : f âŸ¶ g) : sq.left â‰« g.hom = f.hom â‰« sq.right := sq.w

/-- A lift of a commutative square is a diagonal morphism making the two triangles commute. -/
@[ext] class has_lift {f g : arrow T} (sq : f âŸ¶ g) :=
(lift : f.right âŸ¶ g.left)
(fac_left : f.hom â‰« lift = sq.left)
(fac_right : lift â‰« g.hom = sq.right)

attribute [simp, reassoc] has_lift.fac_left has_lift.fac_right

/-- If we have chosen a lift of a commutative square `sq`, we can access it by saying `lift sq`. -/
abbreviation lift {f g : arrow T} (sq : f âŸ¶ g) [has_lift sq] : f.right âŸ¶ g.left :=
has_lift.lift sq

lemma lift.fac_left {f g : arrow T} (sq : f âŸ¶ g) [has_lift sq] : f.hom â‰« lift sq = sq.left :=
by simp

lemma lift.fac_right {f g : arrow T} (sq : f âŸ¶ g) [has_lift sq] : lift sq â‰« g.hom = sq.right :=
by simp

@[simp, reassoc]
lemma lift_mk'_left {X Y P Q : T} {f : X âŸ¶ Y} {g : P âŸ¶ Q} {u : X âŸ¶ P} {v : Y âŸ¶ Q}
  (h : u â‰« g = f â‰« v) [has_lift $ arrow.hom_mk' h] : f â‰« lift (arrow.hom_mk' h) = u :=
by simp only [â†arrow.mk_hom f, lift.fac_left, arrow.hom_mk'_left]

@[simp, reassoc]
lemma lift_mk'_right {X Y P Q : T} {f : X âŸ¶ Y} {g : P âŸ¶ Q} {u : X âŸ¶ P} {v : Y âŸ¶ Q}
  (h : u â‰« g = f â‰« v) [has_lift $ arrow.hom_mk' h] : lift (arrow.hom_mk' h) â‰« g = v :=
by simp only [â†arrow.mk_hom g, lift.fac_right, arrow.hom_mk'_right]

section

instance subsingleton_has_lift_of_epi {f g : arrow T} (sq : f âŸ¶ g) [epi f.hom] :
  subsingleton (has_lift sq) :=
subsingleton.intro $ Î» a b, has_lift.ext a b $ (cancel_epi f.hom).1 $ by simp

instance subsingleton_has_lift_of_mono {f g : arrow T} (sq : f âŸ¶ g) [mono g.hom] :
  subsingleton (has_lift sq) :=
subsingleton.intro $ Î» a b, has_lift.ext a b $ (cancel_mono g.hom).1 $ by simp

end

end arrow

end category_theory
