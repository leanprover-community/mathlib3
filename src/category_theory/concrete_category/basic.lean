/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johannes H√∂lzl, Reid Barton, Sean Leather, Yury Kudryashov
-/
import category_theory.types
import category_theory.epi_mono

/-!
# Concrete categories

A concrete category is a category `C` with a fixed faithful functor
`forget : C ‚•§ Type*`.  We define concrete categories using `class
concrete_category`.  In particular, we impose no restrictions on the
carrier type `C`, so `Type` is a concrete category with the identity
forgetful functor.

Each concrete category `C` comes with a canonical faithful functor
`forget C : C ‚•§ Type*`.  We say that a concrete category `C` admits a
*forgetful functor* to a concrete category `D`, if it has a functor
`forget‚ÇÇ C D : C ‚•§ D` such that `(forget‚ÇÇ C D) ‚ãô (forget D) = forget C`,
see `class has_forget‚ÇÇ`.  Due to `faithful.div_comp`, it suffices
to verify that `forget‚ÇÇ.obj` and `forget‚ÇÇ.map` agree with the equality
above; then `forget‚ÇÇ` will satisfy the functor laws automatically, see
`has_forget‚ÇÇ.mk'`.

Two classes helping construct concrete categories in the two most
common cases are provided in the files `bundled_hom` and
`unbundled_hom`, see their documentation for details.

## References

See [Ahrens and Lumsdaine, *Displayed Categories*][ahrens2017] for
related work.
-/

universes w v v' u

namespace category_theory

/--
A concrete category is a category `C` with a fixed faithful functor `forget : C ‚•§ Type`.

Note that `concrete_category` potentially depends on three independent universe levels,
* the universe level `w` appearing in `forget : C ‚•§ Type w`
* the universe level `v` of the morphisms (i.e. we have a `category.{v} C`)
* the universe level `u` of the objects (i.e `C : Type u`)
They are specified that order, to avoid unnecessary universe annotations.
-/
class concrete_category (C : Type u) [category.{v} C] :=
(forget [] : C ‚•§ Type w)
[forget_faithful : faithful forget]

attribute [instance] concrete_category.forget_faithful

/-- The forgetful functor from a concrete category to `Type u`. -/
@[reducible] def forget (C : Type v) [category C] [concrete_category.{u} C] : C ‚•§ Type u :=
concrete_category.forget C

instance concrete_category.types : concrete_category (Type u) :=
{ forget := ùü≠ _ }

/--
Provide a coercion to `Type u` for a concrete category. This is not marked as an instance
as it could potentially apply to every type, and so is too expensive in typeclass search.

You can use it on particular examples as:
```
instance : has_coe_to_sort X := concrete_category.has_coe_to_sort X
```
-/
def concrete_category.has_coe_to_sort (C : Type v) [category C] [concrete_category C] :
  has_coe_to_sort C (Type u) :=
‚ü®(concrete_category.forget C).obj‚ü©

section
local attribute [instance] concrete_category.has_coe_to_sort

variables {C : Type v} [category C] [concrete_category C]

@[simp] lemma forget_obj_eq_coe {X : C} : (forget C).obj X = X := rfl

/-- Usually a bundled hom structure already has a coercion to function
that works with different universes. So we don't use this as a global instance. -/
def concrete_category.has_coe_to_fun {X Y : C} : has_coe_to_fun (X ‚ü∂ Y) (Œª f, X ‚Üí Y) :=
‚ü®Œª f, (forget _).map f‚ü©

local attribute [instance] concrete_category.has_coe_to_fun

/-- In any concrete category, we can test equality of morphisms by pointwise evaluations.-/
lemma concrete_category.hom_ext {X Y : C} (f g : X ‚ü∂ Y) (w : ‚àÄ x : X, f x = g x) : f = g :=
begin
  apply faithful.map_injective (forget C),
  ext,
  exact w x,
end

@[simp] lemma forget_map_eq_coe {X Y : C} (f : X ‚ü∂ Y) : (forget C).map f = f := rfl

/--
Analogue of `congr_fun h x`,
when `h : f = g` is an equality between morphisms in a concrete category.
-/
lemma congr_hom {X Y : C} {f g : X ‚ü∂ Y} (h : f = g) (x : X) : f x = g x :=
congr_fun (congr_arg (Œª k : X ‚ü∂ Y, (k : X ‚Üí Y)) h) x

lemma coe_id {X : C} : ((ùüô X) : X ‚Üí X) = id :=
(forget _).map_id X

lemma coe_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
  (f ‚â´ g : X ‚Üí Z) = g ‚àò f :=
(forget _).map_comp f g

@[simp] lemma id_apply {X : C} (x : X) : ((ùüô X) : X ‚Üí X) x = x :=
congr_fun ((forget _).map_id X) x

@[simp] lemma comp_apply {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
  (f ‚â´ g) x = g (f x) :=
congr_fun ((forget _).map_comp _ _) x

lemma concrete_category.congr_hom {X Y : C} {f g : X ‚ü∂ Y} (h : f = g) (x : X) : f x = g x :=
congr_fun (congr_arg (Œª f : X ‚ü∂ Y, (f : X ‚Üí Y)) h) x

lemma concrete_category.congr_arg {X Y : C} (f : X ‚ü∂ Y) {x x' : X} (h : x = x') : f x = f x' :=
congr_arg (f : X ‚Üí Y) h

/-- In any concrete category, injective morphisms are monomorphisms. -/
lemma concrete_category.mono_of_injective {X Y : C} (f : X ‚ü∂ Y) (i : function.injective f) :
  mono f :=
faithful_reflects_mono (forget C) ((mono_iff_injective f).2 i)

/-- In any concrete category, surjective morphisms are epimorphisms. -/
lemma concrete_category.epi_of_surjective {X Y : C} (f : X ‚ü∂ Y) (s : function.surjective f) :
  epi f :=
faithful_reflects_epi (forget C) ((epi_iff_surjective f).2 s)

@[simp] lemma concrete_category.has_coe_to_fun_Type {X Y : Type u} (f : X ‚ü∂ Y) :
  coe_fn f = f :=
rfl

end

/--
`has_forget‚ÇÇ C D`, where `C` and `D` are both concrete categories, provides a functor
`forget‚ÇÇ C D : C ‚•§ D` and a proof that `forget‚ÇÇ ‚ãô (forget D) = forget C`.
-/
class has_forget‚ÇÇ (C : Type v) (D : Type v') [category C] [concrete_category.{u} C] [category D]
  [concrete_category.{u} D] :=
(forget‚ÇÇ : C ‚•§ D)
(forget_comp : forget‚ÇÇ ‚ãô (forget D) = forget C . obviously)

/-- The forgetful functor `C ‚•§ D` between concrete categories for which we have an instance
`has_forget‚ÇÇ C `. -/
@[reducible] def forget‚ÇÇ (C : Type v) (D : Type v') [category C] [concrete_category C] [category D]
  [concrete_category D] [has_forget‚ÇÇ C D] : C ‚•§ D :=
has_forget‚ÇÇ.forget‚ÇÇ

instance forget_faithful (C : Type v) (D : Type v') [category C] [concrete_category C] [category D]
  [concrete_category D] [has_forget‚ÇÇ C D] : faithful (forget‚ÇÇ C D) :=
has_forget‚ÇÇ.forget_comp.faithful_of_comp

instance induced_category.concrete_category {C : Type v} {D : Type v'} [category D]
  [concrete_category D] (f : C ‚Üí D) :
  concrete_category (induced_category D f) :=
{ forget := induced_functor f ‚ãô forget D }

instance induced_category.has_forget‚ÇÇ {C : Type v} {D : Type v'} [category D] [concrete_category D]
  (f : C ‚Üí D) :
  has_forget‚ÇÇ (induced_category D f) D :=
{ forget‚ÇÇ := induced_functor f,
  forget_comp := rfl }

instance full_subcategory.concrete_category {C : Type v} [category C] [concrete_category C]
  (Z : C ‚Üí Prop) : concrete_category {X : C // Z X} :=
{ forget := full_subcategory_inclusion Z ‚ãô forget C }

instance full_subcategory.has_forget‚ÇÇ {C : Type v} [category C] [concrete_category C]
  (Z : C ‚Üí Prop) : has_forget‚ÇÇ {X : C // Z X} C :=
{ forget‚ÇÇ := full_subcategory_inclusion Z,
  forget_comp := rfl }

/--
In order to construct a ‚Äúpartially forgetting‚Äù functor, we do not need to verify functor laws;
it suffices to ensure that compositions agree with `forget‚ÇÇ C D ‚ãô forget D = forget C`.
-/
def has_forget‚ÇÇ.mk' {C : Type v} {D : Type v'} [category C] [concrete_category C] [category D]
  [concrete_category D] (obj : C ‚Üí D) (h_obj : ‚àÄ X, (forget D).obj (obj X) = (forget C).obj X)
  (map : Œ† {X Y}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
  (h_map : ‚àÄ {X Y} {f : X ‚ü∂ Y}, (forget D).map (map f) == (forget C).map f) :
has_forget‚ÇÇ C D :=
{ forget‚ÇÇ := faithful.div _ _ _ @h_obj _ @h_map,
  forget_comp := by apply faithful.div_comp }

instance has_forget_to_Type (C : Type v) [category C] [concrete_category C] :
  has_forget‚ÇÇ C (Type u) :=
{ forget‚ÇÇ := forget C,
  forget_comp := functor.comp_id _ }

end category_theory
