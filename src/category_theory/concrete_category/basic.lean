/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johannes H√∂lzl, Reid Barton, Sean Leather, Yury Kudryashov
-/
import category_theory.types category_theory.full_subcategory

/-!
# Concrete categories

A concrete category is a category `C` with a fixed faithful functor
`forget : C ‚•§ Type*`.  We define concrete categories using `class
concrete_category`.  In particular, we impose no restrictions on the
carrier type `C`, so `Type` is a concrete category with the identity
forgetful functor.

Each concrete category `C` comes with a canonical faithful functor
`forget C : C ‚•§ Type*`.  We say that a concrete category `C` admits a
*forgetful functor* to a concrete category `D`, if it has a functor
`forget‚ÇÇ C D : C ‚•§ D` such that `(forget‚ÇÇ C D) ‚ãô (forget D) = forget
C`, see `class has_forget‚ÇÇ`.  Due to `faithful.div_comp`, it suffices
to verify that `forget‚ÇÇ.obj` and `forget‚ÇÇ.map` agree with the equality
above; then `forget‚ÇÇ` will satisfy the functor laws automatically, see
`has_forget‚ÇÇ.mk'`.

Two classes helping construct concrete categories in the two most
common cases are provided in the files `bundled_hom` and
`unbundled_hom`, see their documentation for details.

## References

See [Ahrens and Lumsdaine, *Displayed Categories*][ahrens2017] for
related work.
-/

universe u

namespace category_theory

/-- A concrete category is a category `C` with a fixed faithful functor `forget : C ‚•§ Type`. -/
class concrete_category (C : Type (u+1)) extends category.{u} C :=
(forget : C ‚•§ Type u)
[forget_faithful : faithful forget]

attribute [instance] concrete_category.forget_faithful

/-- The forgetful functor from a concrete category to `Type u`. -/
@[reducible] def forget (C : Type (u+1)) [concrete_category C] : C ‚•§ Type u :=
concrete_category.forget C

/--
Provide a coercion to `Type u` for a concrete category. This is not marked as an instance
as it could potentially apply to every type, and so is too expensive in typeclass search.

You can use it on particular examples as:
```
instance : has_coe_to_sort X := concrete_category.has_coe_to_sort X
```
-/
def concrete_category.has_coe_to_sort (C : Type (u+1)) [concrete_category C] : has_coe_to_sort C :=
{ S := Type u, coe := (concrete_category.forget C).obj }

section
local attribute [instance] concrete_category.has_coe_to_sort

variables {C : Type (u+1)} [concrete_category C]

@[simp] lemma forget_obj_eq_coe {X : C} : (forget C).obj X = X := rfl

/-- Usually a bundled hom structure already has a coercion to function
that works with different universes. So we don't use this as a global instance. -/
def concrete_category.has_coe_to_fun {X Y : C} : has_coe_to_fun (X ‚ü∂ Y) :=
{ F   := Œª f, X ‚Üí Y,
  coe := Œª f, (forget _).map f }

local attribute [instance] concrete_category.has_coe_to_fun

@[simp] lemma forget_map_eq_coe {X Y : C} (f : X ‚ü∂ Y) : (forget C).map f = f := rfl

@[simp] lemma coe_id {X : C} (x : X) : ((ùüô X) : X ‚Üí X) x = x :=
congr_fun ((forget _).map_id X) x

@[simp] lemma coe_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
  (f ‚â´ g) x = g (f x) :=
congr_fun ((forget _).map_comp _ _) x

end

instance concrete_category.types : concrete_category (Type u) :=
{ forget := ùü≠ _ }

/--
`has_forget‚ÇÇ C D`, where `C` and `D` are both concrete categories, provides a functor
`forget‚ÇÇ C D : C ‚•§ C` and a proof that `forget‚ÇÇ ‚ãô (forget D) = forget C`.
-/
class has_forget‚ÇÇ (C D : Type (u+1)) [concrete_category C] [concrete_category D] :=
(forget‚ÇÇ : C ‚•§ D)
(forget_comp : forget‚ÇÇ ‚ãô (forget D) = forget C)

/-- The forgetful functor `C ‚•§ D` between concrete categories for which we have an instance
`has_forget‚ÇÇ C `. -/
@[reducible] def forget‚ÇÇ (C D : Type (u+1)) [concrete_category C] [concrete_category D]
  [has_forget‚ÇÇ C D] : C ‚•§ D :=
has_forget‚ÇÇ.forget‚ÇÇ C D

instance forget_faithful (C D : Type (u+1)) [concrete_category C] [concrete_category D]
  [has_forget‚ÇÇ C D] : faithful (forget‚ÇÇ C D) :=
(has_forget‚ÇÇ.forget_comp C D).faithful_of_comp

instance induced_category.concrete_category {C D : Type (u+1)} [concrete_category D] (f : C ‚Üí D) :
  concrete_category (induced_category D f) :=
{ forget := induced_functor f ‚ãô forget D }

instance induced_category.has_forget‚ÇÇ {C D : Type (u+1)} [concrete_category D] (f : C ‚Üí D) :
  has_forget‚ÇÇ (induced_category D f) D :=
{ forget‚ÇÇ := induced_functor f,
  forget_comp := rfl }

/--
In order to construct a ‚Äúpartially forgetting‚Äù functor, we do not need to verify functor laws;
it suffices to ensure that compositions agree with `forget‚ÇÇ C D ‚ãô forget D = forget C`.
-/
def has_forget‚ÇÇ.mk' {C D : Type (u+1)} [concrete_category C] [concrete_category D]
  (obj : C ‚Üí D) (h_obj : ‚àÄ X, (forget D).obj (obj X) = (forget C).obj X)
  (map : Œ† {X Y}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))
  (h_map : ‚àÄ {X Y} {f : X ‚ü∂ Y}, (forget D).map (map f) == (forget C).map f) :
has_forget‚ÇÇ C D :=
{ forget‚ÇÇ := faithful.div _ _ _ @h_obj _ @h_map,
  forget_comp := by apply faithful.div_comp }

instance has_forget_to_Type (C : Type (u+1)) [concrete_category C] : has_forget‚ÇÇ C (Type u) :=
{ forget‚ÇÇ := forget C,
  forget_comp := functor.comp_id _ }

end category_theory
