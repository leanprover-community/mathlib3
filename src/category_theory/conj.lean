/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import category_theory.endomorphism
import algebra.group_power

/-!
# Conjugate morphisms by isomorphisms

An isomorphism `Î± : X â‰… Y` defines
- a monoid isomorphism `conj : End X â‰ƒ* End Y` by `Î±.conj f = Î±.inv â‰« f â‰« Î±.hom`;
- a group isomorphism `conj_Aut : Aut X â‰ƒ* Aut Y` by `Î±.conj_Aut f = Î±.symm â‰ªâ‰« f â‰ªâ‰« Î±`.

For completeness, we also define `hom_congr : (X â‰… Xâ‚) â†’ (Y â‰… Yâ‚) â†’ (X âŸ¶ Y) â‰ƒ (Xâ‚ âŸ¶ Yâ‚)`,
cf. `equiv.arrow_congr`.
-/

universes v u

namespace category_theory

namespace iso

variables {C : Type u} [category.{v} C]

/-- If `X` is isomorphic to `Xâ‚` and `Y` is isomorphic to `Yâ‚`, then
there is a natural bijection between `X âŸ¶ Y` and `Xâ‚ âŸ¶ Yâ‚`. See also `equiv.arrow_congr`. -/
def hom_congr {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) :
  (X âŸ¶ Y) â‰ƒ (Xâ‚ âŸ¶ Yâ‚) :=
{ to_fun := Î» f, Î±.inv â‰« f â‰« Î².hom,
  inv_fun := Î» f, Î±.hom â‰« f â‰« Î².inv,
  left_inv := Î» f, show Î±.hom â‰« (Î±.inv â‰« f â‰« Î².hom) â‰« Î².inv = f,
    by rw [category.assoc, category.assoc, Î².hom_inv_id, Î±.hom_inv_id_assoc, category.comp_id],
  right_inv := Î» f, show Î±.inv â‰« (Î±.hom â‰« f â‰« Î².inv) â‰« Î².hom = f,
    by rw [category.assoc, category.assoc, Î².inv_hom_id, Î±.inv_hom_id_assoc, category.comp_id] }

@[simp]
lemma hom_congr_apply {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (f : X âŸ¶ Y) :
  Î±.hom_congr Î² f = Î±.inv â‰« f â‰« Î².hom :=
rfl

lemma hom_congr_comp {X Y Z Xâ‚ Yâ‚ Zâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (Î³ : Z â‰… Zâ‚)
  (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  Î±.hom_congr Î³ (f â‰« g) = Î±.hom_congr Î² f â‰« Î².hom_congr Î³ g :=
by simp

@[simp] lemma hom_congr_refl {X Y : C} (f : X âŸ¶ Y) :
  (iso.refl X).hom_congr (iso.refl Y) f = f :=
by simp

@[simp] lemma hom_congr_trans {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ Xâ‚ƒ Yâ‚ƒ : C}
  (Î±â‚ : Xâ‚ â‰… Xâ‚‚) (Î²â‚ : Yâ‚ â‰… Yâ‚‚) (Î±â‚‚ : Xâ‚‚ â‰… Xâ‚ƒ) (Î²â‚‚ : Yâ‚‚ â‰… Yâ‚ƒ) (f : Xâ‚ âŸ¶ Yâ‚) :
  (Î±â‚ â‰ªâ‰« Î±â‚‚).hom_congr (Î²â‚ â‰ªâ‰« Î²â‚‚) f = (Î±â‚.hom_congr Î²â‚).trans (Î±â‚‚.hom_congr Î²â‚‚) f :=
by simp

@[simp] lemma hom_congr_symm {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (Î± : Xâ‚ â‰… Xâ‚‚) (Î² : Yâ‚ â‰… Yâ‚‚) :
  (Î±.hom_congr Î²).symm = Î±.symm.hom_congr Î².symm :=
rfl

variables {X Y : C} (Î± : X â‰… Y)

/-- An isomorphism between two objects defines a monoid isomorphism between their
monoid of endomorphisms. -/
def conj : End X â‰ƒ* End Y :=
{ map_mul' := Î» f g, hom_congr_comp Î± Î± Î± g f,
  .. hom_congr Î± Î± }

lemma conj_apply (f : End X) : Î±.conj f = Î±.inv â‰« f â‰« Î±.hom := rfl

@[simp] lemma conj_comp (f g : End X) : Î±.conj (f â‰« g) = (Î±.conj f) â‰« (Î±.conj g) :=
Î±.conj.map_mul g f

@[simp] lemma conj_id : Î±.conj (ğŸ™ X) = ğŸ™ Y :=
Î±.conj.map_one

@[simp] lemma refl_conj (f : End X) : (iso.refl X).conj f = f :=
by rw [conj_apply, iso.refl_inv, iso.refl_hom, category.id_comp, category.comp_id]

@[simp] lemma trans_conj {Z : C} (Î² : Y â‰… Z) (f : End X) : (Î± â‰ªâ‰« Î²).conj f = Î².conj (Î±.conj f) :=
hom_congr_trans Î± Î± Î² Î² f

@[simp] lemma symm_self_conj (f : End X) : Î±.symm.conj (Î±.conj f) = f :=
by rw [â† trans_conj, Î±.self_symm_id, refl_conj]

@[simp] lemma self_symm_conj (f : End Y) : Î±.conj (Î±.symm.conj f) = f :=
Î±.symm.symm_self_conj f

@[simp] lemma conj_pow (f : End X) (n : â„•) : Î±.conj (f^n) = (Î±.conj f)^n :=
Î±.conj.to_monoid_hom.map_pow f n

/-- `conj` defines a group isomorphisms between groups of automorphisms -/
def conj_Aut : Aut X â‰ƒ* Aut Y :=
(Aut.units_End_equiv_Aut X).symm.trans $
(units.map_equiv Î±.conj).trans $
Aut.units_End_equiv_Aut Y

lemma conj_Aut_apply (f : Aut X) : Î±.conj_Aut f = Î±.symm â‰ªâ‰« f â‰ªâ‰« Î± :=
by cases f; cases Î±; ext; refl

@[simp] lemma conj_Aut_hom (f : Aut X) : (Î±.conj_Aut f).hom = Î±.conj f.hom := rfl

@[simp] lemma trans_conj_Aut {Z : C} (Î² : Y â‰… Z) (f : Aut X) :
  (Î± â‰ªâ‰« Î²).conj_Aut f = Î².conj_Aut (Î±.conj_Aut f) :=
by simp only [conj_Aut_apply, iso.trans_symm, iso.trans_assoc]

@[simp] lemma conj_Aut_mul (f g : Aut X) : Î±.conj_Aut (f * g) = Î±.conj_Aut f * Î±.conj_Aut g :=
Î±.conj_Aut.map_mul f g

@[simp] lemma conj_Aut_trans (f g : Aut X) : Î±.conj_Aut (f â‰ªâ‰« g) = Î±.conj_Aut f â‰ªâ‰« Î±.conj_Aut g :=
conj_Aut_mul Î± g f

@[simp] lemma conj_Aut_pow (f : Aut X) (n : â„•) : Î±.conj_Aut (f^n) = (Î±.conj_Aut f)^n :=
Î±.conj_Aut.to_monoid_hom.map_pow f n

@[simp] lemma conj_Aut_gpow (f : Aut X) (n : â„¤) : Î±.conj_Aut (f^n) = (Î±.conj_Aut f)^n :=
Î±.conj_Aut.to_monoid_hom.map_gpow f n

end iso

namespace functor

universes vâ‚ uâ‚

variables {C : Type u} [category.{v} C] {D : Type uâ‚} [category.{vâ‚} D] (F : C â¥¤ D)

lemma map_hom_congr {X Y Xâ‚ Yâ‚ : C} (Î± : X â‰… Xâ‚) (Î² : Y â‰… Yâ‚) (f : X âŸ¶ Y) :
  F.map (iso.hom_congr Î± Î² f) = iso.hom_congr (F.map_iso Î±) (F.map_iso Î²) (F.map f) :=
by simp

lemma map_conj {X Y : C} (Î± : X â‰… Y) (f : End X) :
  F.map (Î±.conj f) = (F.map_iso Î±).conj (F.map f) :=
map_hom_congr F Î± Î± f

lemma map_conj_Aut (F : C â¥¤ D) {X Y : C} (Î± : X â‰… Y) (f : Aut X) :
  F.map_iso (Î±.conj_Aut f) = (F.map_iso Î±).conj_Aut (F.map_iso f) :=
by ext; simp only [map_iso_hom, iso.conj_Aut_hom, F.map_conj]
-- alternative proof: by simp only [iso.conj_Aut_apply, F.map_iso_trans, F.map_iso_symm]

end functor
end category_theory
