/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/

import category_theory.const
import category_theory.discrete_category
import category_theory.eq_to_hom

/-!
# Connected category

Define a connected category as a _nonempty_ category for which every functor
to a discrete category is isomorphic to the constant functor.

NB. Some authors include the empty category as connected, we do not.
We instead are interested in categories with exactly one 'connected
component'.

We give some equivalent definitions:
- A nonempty category for which every functor to a discrete category is
  constant on objects.
  See `any_functor_const_on_obj` and `connected.of_any_functor_const_on_obj`.
- A nonempty category for which every function `F` for which the presence of a
  morphism `f : j‚ÇÅ ‚ü∂ j‚ÇÇ` implies `F j‚ÇÅ = F j‚ÇÇ` must be constant everywhere.
  See `constant_of_preserves_morphisms` and `connected.of_constant_of_preserves_morphisms`.
- A nonempty category for which any subset of its elements containing the
  default and closed under morphisms is everything.
  See `induct_on_objects` and `connected.of_induct`.
- A nonempty category for which every object is related under the reflexive
  transitive closure of the relation "there is a morphism in some direction
  from `j‚ÇÅ` to `j‚ÇÇ`".
  See `connected_zigzag` and `zigzag_connected`.
- A nonempty category for which for any two objects there is a sequence of
  morphisms (some reversed) from one to the other.
  See `exists_zigzag'` and `connected_of_zigzag`.

We also prove the result that the functor given by `(X √ó -)` preserves any
connected limit. That is, any limit of shape `J` where `J` is a connected
category is preserved by the functor `(X √ó -)`.
-/

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

open category_theory.category
namespace category_theory

section connected
-- See note [default priority]
set_option default_priority 100
/--
We define a connected category as a _nonempty_ category for which every
functor to a discrete category is constant.

NB. Some authors include the empty category as connected, we do not.
We instead are interested in categories with exactly one 'connected
component'.

This allows us to show that the functor X ‚®Ø - preserves connected limits.
-/
class connected (J : Type v‚ÇÇ) [ùí• : category.{v‚ÇÅ} J] extends inhabited J :=
(iso_constant : Œ† {Œ± : Type v‚ÇÇ} (F : J ‚•§ discrete Œ±), F ‚âÖ (functor.const J).obj (F.obj default))
end connected

variables {J : Type v‚ÇÇ} [ùí• : category.{v‚ÇÅ} J]
include ùí•

/--
If J is connected, any functor to a discrete category is constant on objects.
The converse is given in `connected.of_any_functor_const_on_obj`.
-/
lemma any_functor_const_on_obj [connected J] {Œ± : Type v‚ÇÇ} (F : J ‚•§ discrete Œ±) (j : J) :
  F.obj j = F.obj (default J) :=
((connected.iso_constant F).hom.app j).down.1

/--
If any functor to a discrete category is constant on objects, J is connected.
The converse of `any_functor_const_on_obj`.
-/
def connected.of_any_functor_const_on_obj [inhabited J]
  (h : ‚àÄ {Œ± : Type v‚ÇÇ} (F : J ‚•§ discrete Œ±), ‚àÄ (j : J), F.obj j = F.obj (default J)) :
  connected J :=
{ iso_constant := Œª Œ± F, nat_iso.of_components (Œª B, eq_to_iso (h F B)) (Œª _ _ _, subsingleton.elim _ _) }

/--
If `J` is connected, then given any function `F` such that the presence of a
morphism `j‚ÇÅ ‚ü∂ j‚ÇÇ` implies `F j‚ÇÅ = F j‚ÇÇ`, we have that `F` is constant.
This can be thought of as a local-to-global property.

The converse is shown in `connected.of_constant_of_preserves_morphisms`
-/
lemma constant_of_preserves_morphisms [connected J] {Œ± : Type v‚ÇÇ} (F : J ‚Üí Œ±) (h : ‚àÄ (j‚ÇÅ j‚ÇÇ : J) (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), F j‚ÇÅ = F j‚ÇÇ) (j : J) :
  F j = F (default J) :=
any_functor_const_on_obj { obj := F, map := Œª _ _ f, eq_to_hom (h _ _ f) } j

/--
`J` is connected if: given any function `F : J ‚Üí Œ±` which is constant for any
`j‚ÇÅ, j‚ÇÇ` for which there is a morphism `j‚ÇÅ ‚ü∂ j‚ÇÇ`, then `F` is constant.
This can be thought of as a local-to-global property.

The converse of `constant_of_preserves_morphisms`.
-/
def connected.of_constant_of_preserves_morphisms [inhabited J]
  (h : ‚àÄ {Œ± : Type v‚ÇÇ} (F : J ‚Üí Œ±), (‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), F j‚ÇÅ = F j‚ÇÇ) ‚Üí (‚àÄ j : J, F j = F (default J))) :
  connected J :=
connected.of_any_functor_const_on_obj (Œª _ F, h F.obj (Œª _ _ f, (F.map f).down.1))

/--
An inductive-like property for the objects of a connected category.
If `default J` is in the set `p`, and `p` is closed under morphisms of `J`,
then `p` contains all of `J`.

The converse is given in `connected.of_induct`.
-/
lemma induct_on_objects [connected J] (p : set J) (h0 : default J ‚àà p)
  (h1 : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p) (j : J) :
  j ‚àà p :=
begin
  injection (constant_of_preserves_morphisms (Œª k, ulift.up (k ‚àà p)) (Œª j‚ÇÅ j‚ÇÇ f, _) j) with i,
  rwa i,
  dsimp,
  exact congr_arg ulift.up (propext (h1 f)),
end

/--
If any maximal connected component of J containing the default is all of J, then J is connected.

The converse of `induct_on_objects`.
-/
def connected.of_induct [inhabited J]
  (h : ‚àÄ (p : set J), default J ‚àà p ‚Üí (‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p) ‚Üí ‚àÄ (j : J), j ‚àà p) :
  connected J :=
connected.of_constant_of_preserves_morphisms (Œª Œ± F a, h {j | F j = F (default J)} rfl (Œª _ _ f, by simp [a f] ))

/-- j‚ÇÅ and j‚ÇÇ are related by `zag` if there is a morphism between them. -/
@[reducible]
def zag (j‚ÇÅ j‚ÇÇ : J) : Prop := nonempty (j‚ÇÅ ‚ü∂ j‚ÇÇ) ‚à® nonempty (j‚ÇÇ ‚ü∂ j‚ÇÅ)
/--
`j‚ÇÅ` and `j‚ÇÇ` are related by `zigzag` if there is a chain of
morphisms from `j‚ÇÅ` to `j‚ÇÇ`, with backward morphisms allowed.
-/
@[reducible]
def zigzag : J ‚Üí J ‚Üí Prop := relation.refl_trans_gen zag

/-- Any equivalence relation containing (‚ü∂) holds for all pairs of a connected category. -/
lemma equiv_relation [connected J] (r : J ‚Üí J ‚Üí Prop) (hr : _root_.equivalence r)
  (h : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), r j‚ÇÅ j‚ÇÇ) :
  ‚àÄ (j‚ÇÅ j‚ÇÇ : J), r j‚ÇÅ j‚ÇÇ :=
begin
  have z: ‚àÄ (j : J), r (default J) j :=
    induct_on_objects (Œª k, r (default J) k)
        (hr.1 (default J)) (Œª _ _ f, ‚ü®Œª t, hr.2.2 t (h f), Œª t, hr.2.2 t (hr.2.1 (h f))‚ü©),
  intros, apply hr.2.2 (hr.2.1 (z _)) (z _)
end

/-- In a connected category, any two objects are related by `zigzag`. -/
lemma connected_zigzag [connected J] (j‚ÇÅ j‚ÇÇ : J) : zigzag j‚ÇÅ j‚ÇÇ :=
equiv_relation _
  (mk_equivalence _
    relation.reflexive_refl_trans_gen
    (relation.refl_trans_gen.symmetric (Œª _ _ _, by rwa [zag, or_comm]))
    relation.transitive_refl_trans_gen)
  (Œª _ _ f, relation.refl_trans_gen.single (or.inl (nonempty.intro f))) _ _

/--
If any two objects in an inhabited category are related by `zigzag`, the category is connected.
-/
def zigzag_connected [inhabited J] (h : ‚àÄ (j‚ÇÅ j‚ÇÇ : J), zigzag j‚ÇÅ j‚ÇÇ) : connected J :=
begin
  apply connected.of_induct,
  intros,
  have: ‚àÄ (j‚ÇÅ j‚ÇÇ : J), zigzag j‚ÇÅ j‚ÇÇ ‚Üí (j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p),
  { introv k,
    induction k,
    { refl },
    { rw k_ih,
      rcases k_a_1 with ‚ü®‚ü®_‚ü©‚ü© | ‚ü®‚ü®_‚ü©‚ü©,
      apply a_1 k_a_1,
      apply (a_1 k_a_1).symm } },
  rwa this j (default J) (h _ _)
end

lemma exists_zigzag' [connected J] (j‚ÇÅ j‚ÇÇ : J) :
  ‚àÉ l, list.chain zag j‚ÇÅ l ‚àß list.last (j‚ÇÅ :: l) (list.cons_ne_nil _ _) = j‚ÇÇ :=
list.exists_chain_of_relation_refl_trans_gen (connected_zigzag _ _)

/--
If any two objects in an inhabited category are linked by a sequence of (potentially reversed)
morphisms, then J is connected.

The converse of `exists_zigzag'`.
-/
def connected_of_zigzag [inhabited J]
  (h : ‚àÄ (j‚ÇÅ j‚ÇÇ : J), ‚àÉ l, list.chain zag j‚ÇÅ l ‚àß list.last (j‚ÇÅ :: l) (list.cons_ne_nil _ _) = j‚ÇÇ) :
  connected J :=
begin
  apply connected.of_induct,
  intros p d k j,
  obtain ‚ü®l, zags, lst‚ü© := h j (default J),
  apply list.chain.induction p l zags lst _ d,
  rintros _ _ (‚ü®‚ü®_‚ü©‚ü© | ‚ü®‚ü®_‚ü©‚ü©),
  { exact (k a).2 },
  { exact (k a).1 }
end

variables {C : Type u‚ÇÇ} [ùíû : category.{v‚ÇÇ} C]
include ùíû

/--
For objects `X Y : C`, any natural transformation `Œ± : const X ‚ü∂ const Y` from a connected
category must be constant.
This is the key property of connected categories which we use to establish properties about limits.
-/
lemma nat_trans_from_connected [conn : connected J] {X Y : C}
  (Œ± : (functor.const J).obj X ‚ü∂ (functor.const J).obj Y) :
  ‚àÄ (j : J), Œ±.app j = (Œ±.app (default J) : X ‚ü∂ Y) :=
@constant_of_preserves_morphisms _ _ _
  (X ‚ü∂ Y)
  (Œª j, Œ±.app j)
  (Œª _ _ f, (by { have := Œ±.naturality f, erw [id_comp, comp_id] at this, exact this.symm }))

end category_theory
