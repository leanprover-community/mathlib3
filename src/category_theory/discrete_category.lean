/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import category_theory.eq_to_hom
import data.ulift

/-!
# Discrete categories

We define `discrete Œ± := Œ±` for any type `Œ±`, and use this type alias
to provide a `small_category` instance whose only morphisms are the identities.

There is an annoying technical difficulty that it has turned out to be inconvenient
to allow categories with morphisms living in `Prop`,
so instead of defining `X ‚ü∂ Y` in `discrete Œ±` as `X = Y`,
one might define it as `plift (X = Y)`.
In fact, to allow `discrete Œ±` to be a `small_category`
(i.e. with morphisms in the same universe as the objects),
we actually define the hom type `X ‚ü∂ Y` as `ulift (plift (X = Y))`.

`discrete.functor` promotes a function `f : I ‚Üí C` (for any category `C`) to a functor
`discrete.functor f : discrete I ‚•§ C`.

Similarly, `discrete.nat_trans` and `discrete.nat_iso` promote `I`-indexed families of morphisms,
or `I`-indexed families of isomorphisms to natural transformations or natural isomorphism.

We show equivalences of types are the same as (categorical) equivalences of the corresponding
discrete categories.
-/

namespace category_theory

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ -- morphism levels before object levels. See note [category_theory universes].

/--
A type synonym for promoting any type to a category,
with the only morphisms being equalities.
-/
def discrete (Œ± : Type u‚ÇÅ) := Œ±

/--
The "discrete" category on a type, whose morphisms are equalities.

Because we do not allow morphisms in `Prop` (only in `Type`),
somewhat annoyingly we have to define `X ‚ü∂ Y` as `ulift (plift (X = Y))`.

See https://stacks.math.columbia.edu/tag/001A
-/
instance discrete_category (Œ± : Type u‚ÇÅ) : small_category (discrete Œ±) :=
{ hom  := Œª X Y, ulift (plift (X = Y)),
  id   := Œª X, ulift.up (plift.up rfl),
  comp := Œª X Y Z g f, by { rcases f with ‚ü®‚ü®rfl‚ü©‚ü©, exact g } }

namespace discrete

variables {Œ± : Type u‚ÇÅ}

instance [inhabited Œ±] : inhabited (discrete Œ±) :=
by { dsimp [discrete], apply_instance }

instance [subsingleton Œ±] : subsingleton (discrete Œ±) :=
by { dsimp [discrete], apply_instance }

/-- Extract the equation from a morphism in a discrete category. -/
lemma eq_of_hom {X Y : discrete Œ±} (i : X ‚ü∂ Y) : X = Y := i.down.down

@[simp] lemma id_def (X : discrete Œ±) : ulift.up (plift.up (eq.refl X)) = ùüô X := rfl

variables {C : Type u‚ÇÇ} [category.{v‚ÇÇ} C]

instance {I : Type u‚ÇÅ} {i j : discrete I} (f : i ‚ü∂ j) : is_iso f :=
‚ü®‚ü®eq_to_hom (eq_of_hom f).symm, by tidy‚ü©‚ü©

/--
Any function `I ‚Üí C` gives a functor `discrete I ‚•§ C`.
-/
def functor {I : Type u‚ÇÅ} (F : I ‚Üí C) : discrete I ‚•§ C :=
{ obj := F,
  map := Œª X Y f, begin cases f, cases f, cases f, exact ùüô (F X) end }

@[simp] lemma functor_obj  {I : Type u‚ÇÅ} (F : I ‚Üí C) (i : I) :
  (discrete.functor F).obj i = F i := rfl

lemma functor_map  {I : Type u‚ÇÅ} (F : I ‚Üí C) {i : discrete I} (f : i ‚ü∂ i) :
  (discrete.functor F).map f = ùüô (F i) :=
by { cases f, cases f, cases f, refl }

/--
For functors out of a discrete category,
a natural transformation is just a collection of maps,
as the naturality squares are trivial.
-/
def nat_trans {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚ü∂ G.obj i) : F ‚ü∂ G :=
{ app := f }

@[simp] lemma nat_trans_app  {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚ü∂ G.obj i) (i) : (discrete.nat_trans f).app i = f i :=
rfl

/--
For functors out of a discrete category,
a natural isomorphism is just a collection of isomorphisms,
as the naturality squares are trivial.
-/
def nat_iso {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚âÖ G.obj i) : F ‚âÖ G :=
nat_iso.of_components f (by tidy)

@[simp]
lemma nat_iso_hom_app {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚âÖ G.obj i) (i : I) :
  (discrete.nat_iso f).hom.app i = (f i).hom :=
rfl

@[simp]
lemma nat_iso_inv_app {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚âÖ G.obj i) (i : I) :
  (discrete.nat_iso f).inv.app i = (f i).inv :=
rfl

@[simp]
lemma nat_iso_app {I : Type u‚ÇÅ} {F G : discrete I ‚•§ C}
  (f : Œ† i : discrete I, F.obj i ‚âÖ G.obj i) (i : I) :
  (discrete.nat_iso f).app i = f i :=
by tidy

/-- Every functor `F` from a discrete category is naturally isomorphic (actually, equal) to
  `discrete.functor (F.obj)`. -/
def nat_iso_functor {I : Type u‚ÇÅ} {F : discrete I ‚•§ C} : F ‚âÖ discrete.functor (F.obj) :=
nat_iso $ Œª i, iso.refl _

/--
We can promote a type-level `equiv` to
an equivalence between the corresponding `discrete` categories.
-/
@[simps]
def equivalence {I J : Type u‚ÇÅ} (e : I ‚âÉ J) : discrete I ‚âå discrete J :=
{ functor := discrete.functor (e : I ‚Üí J),
  inverse := discrete.functor (e.symm : J ‚Üí I),
  unit_iso := discrete.nat_iso (Œª i, eq_to_iso (by simp)),
  counit_iso := discrete.nat_iso (Œª j, eq_to_iso (by simp)), }

/-- We can convert an equivalence of `discrete` categories to a type-level `equiv`. -/
@[simps]
def equiv_of_equivalence {Œ± Œ≤ : Type u‚ÇÅ} (h : discrete Œ± ‚âå discrete Œ≤) : Œ± ‚âÉ Œ≤ :=
{ to_fun := h.functor.obj,
  inv_fun := h.inverse.obj,
  left_inv := Œª a, eq_of_hom (h.unit_iso.app a).2,
  right_inv := Œª a, eq_of_hom (h.counit_iso.app a).1 }

end discrete

namespace discrete
variables {J : Type v‚ÇÅ}

open opposite

/-- A discrete category is equivalent to its opposite category. -/
protected def opposite (Œ± : Type u‚ÇÅ) : (discrete Œ±)·µí·µñ ‚âå discrete Œ± :=
let F : discrete Œ± ‚•§ (discrete Œ±)·µí·µñ := discrete.functor (Œª x, op x) in
begin
  refine equivalence.mk (functor.left_op F) F _ (discrete.nat_iso $ Œª X, by simp [F]),
  refine nat_iso.of_components (Œª X, by simp [F]) _,
  tidy
end

variables {C : Type u‚ÇÇ} [category.{v‚ÇÇ} C]

@[simp] lemma functor_map_id
  (F : discrete J ‚•§ C) {j : discrete J} (f : j ‚ü∂ j) : F.map f = ùüô (F.obj j) :=
begin
  have h : f = ùüô j, { cases f, cases f, ext, },
  rw h,
  simp,
end

end discrete

end category_theory
