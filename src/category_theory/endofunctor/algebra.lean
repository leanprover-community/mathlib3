/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta, Johan Commelin, Reid Barton, Rob Lewis, Joseph Hua
-/
import category_theory.functor_category
import category_theory.concrete_category.reflects_isomorphisms
import category_theory.limits.final

/-!
# Algebras of endofunctors

This file defines algebras of an endofunctor,
and provides the category instance for them.
This extends to Eilenberg-Moore (co)algebras for a (co)monad.

-/
/-Further it defines the forgetful functor from the category of algebras.

## References
* [Riehl, *Category theory in context*, Section 5.2.4][riehl2017]
-/

universes vâ‚ uâ‚

namespace category_theory

namespace endofunctor

variables {C : Type uâ‚} [category.{vâ‚} C]

/-- An algebra of an endofunctor; `str` stands for "structure morphism" -/
structure algebra (F : C â¥¤ C) :=
(A : C)
(str : F.obj A âŸ¶ A)

namespace algebra

variables {F : C â¥¤ C} (A : algebra F) {Aâ‚€ Aâ‚ Aâ‚‚ : algebra F}

/-
        str
   F Aâ‚€ -----> Aâ‚€
    |          |
F f |          | f
    V          V
   F Aâ‚ -----> Aâ‚
        str
-/
/-- A morphism between algebras of endofunctor `F` -/
@[ext] structure hom (Aâ‚€ Aâ‚ : algebra F) :=
(f : Aâ‚€.1 âŸ¶ Aâ‚.1)
(h' : F.map f â‰« Aâ‚.str = Aâ‚€.str â‰« f . obviously)

restate_axiom hom.h'
attribute [simp, reassoc] hom.h
namespace hom

/-- The identity morphism of an algebra of endofunctor `F` -/
def id : hom A A := { f := ğŸ™ _ }

instance : inhabited (hom A A) := âŸ¨{ f := ğŸ™ _ }âŸ©

/-- The composition of morphisms between algebras of endofunctor `F` -/
def comp (f : hom Aâ‚€ Aâ‚) (g : hom Aâ‚ Aâ‚‚) : hom Aâ‚€ Aâ‚‚ := { f := f.1 â‰« g.1 }

end hom

instance (F : C â¥¤ C) : category_struct (algebra F) :=
{ hom := hom,
  id := hom.id,
  comp := @hom.comp _ _ _ }

@[simp] lemma id_eq_id : algebra.hom.id A = ğŸ™ A := rfl

@[simp] lemma id_f : (ğŸ™ _ : A âŸ¶ A).1 = ğŸ™ A.1 := rfl

variables {Aâ‚€ Aâ‚ Aâ‚‚} (f : Aâ‚€ âŸ¶ Aâ‚) (g : Aâ‚ âŸ¶ Aâ‚‚)

@[simp] lemma comp_eq_comp : algebra.hom.comp f g = f â‰« g := rfl

@[simp] lemma comp_f : (f â‰« g).1 = f.1 â‰« g.1 := rfl

/-- Algebras of an endofunctor `F` form a category -/
instance (F : C â¥¤ C) : category (algebra F) := {}

/--
To construct an isomorphism of algebras, it suffices to give an isomorphism of the As which
commutes with the structure morphisms.
-/
@[simps]
def iso_mk (h : Aâ‚€.1 â‰… Aâ‚.1) (w : F.map h.hom â‰« Aâ‚.str = Aâ‚€.str â‰« h.hom) : Aâ‚€ â‰… Aâ‚ :=
{ hom := { f := h.hom },
  inv :=
  { f := h.inv,
    h' := by { rw [h.eq_comp_inv, category.assoc, â†w, â†functor.map_comp_assoc], simp } } }

/-- The forgetful functor from the category of algebras, forgetting the algebraic structure. -/
@[simps] def forget (F : C â¥¤ C) : algebra F â¥¤ C :=
{ obj := Î» A, A.1,
  map := Î» A B f, f.1 }

/-- An algebra morphism with an underlying isomorphism hom in `C` is an algebra isomorphism. -/
lemma iso_of_iso (f : Aâ‚€ âŸ¶ Aâ‚) [is_iso f.1] : is_iso f :=
âŸ¨âŸ¨{ f := inv f.1,
    h' := by { rw [is_iso.eq_comp_inv f.1, category.assoc, â† f.h], simp } }, by tidyâŸ©âŸ©

instance forget_reflects_iso : reflects_isomorphisms (forget F) :=
{ reflects := Î» A B, iso_of_iso }

instance forget_faithful : faithful (forget F) := {}

/--
From a natural transformation `Î± : G â†’ F` we get a functor from
algebras of `F` to algebras of `G`.
-/
@[simps]
def functor_of_nat_trans {F G : C â¥¤ C} (Î± : G âŸ¶ F) : algebra F â¥¤ algebra G :=
{ obj := Î» A,
  { A := A.1,
    str := Î±.app A.1 â‰« A.str },
  map := Î» Aâ‚€ Aâ‚ f,
  { f := f.1 } }

/-- The identity transformation induces the identity endofunctor on the category of algebras. -/
@[simps {rhs_md := semireducible}]
def functor_of_nat_trans_id :
  functor_of_nat_trans (ğŸ™ F) â‰… ğŸ­ _ :=
nat_iso.of_components
  (Î» X, iso_mk (iso.refl _) (by { dsimp, simp, }))
  (Î» X Y f, by { ext, dsimp, simp })

/-- A composition of natural transformations gives the composition of corresponding functors. -/
@[simps {rhs_md := semireducible}]
def functor_of_nat_trans_comp {Fâ‚€ Fâ‚ Fâ‚‚ : C â¥¤ C} (Î± : Fâ‚€ âŸ¶ Fâ‚) (Î² : Fâ‚ âŸ¶ Fâ‚‚) :
  functor_of_nat_trans (Î± â‰« Î²) â‰…
    functor_of_nat_trans Î² â‹™ functor_of_nat_trans Î± :=
nat_iso.of_components
  (Î» X, iso_mk (iso.refl _) (by { dsimp, simp }))
  (Î» X Y f, by { ext, dsimp, simp })

/--
If `Î±` and `Î²` are two equal natural transformations, then the functors of algebras induced by them
are isomorphic.
We define it like this as opposed to using `eq_to_iso` so that the components are nicer to prove
lemmas about.
-/
@[simps {rhs_md := semireducible}]
def functor_of_nat_trans_eq {F G : C â¥¤ C} {Î± Î² : F âŸ¶ G} (h : Î± = Î²) :
  functor_of_nat_trans Î± â‰… functor_of_nat_trans Î² :=
nat_iso.of_components
  (Î» X, iso_mk (iso.refl _) (by { dsimp, simp [h] }))
  (Î» X Y f, by { ext, dsimp, simp })

/--
Naturally isomorphic endofunctors give equivalent categories of algebras.
Furthermore, they are equivalent as categories over `C`, that is,
we have `equiv_of_nat_iso h â‹™ forget = forget`.
-/
@[simps]
def equiv_of_nat_iso {F G : C â¥¤ C} (Î± : F â‰… G) :
  algebra F â‰Œ algebra G :=
{ functor := functor_of_nat_trans Î±.inv,
  inverse := functor_of_nat_trans Î±.hom,
  unit_iso :=
    functor_of_nat_trans_id.symm â‰ªâ‰«
    functor_of_nat_trans_eq (by simp) â‰ªâ‰«
    functor_of_nat_trans_comp _ _,
  counit_iso :=
    (functor_of_nat_trans_comp _ _).symm â‰ªâ‰«
    functor_of_nat_trans_eq (by simp) â‰ªâ‰«
    functor_of_nat_trans_id }.

namespace initial

variables {A} (h : limits.is_initial A)

/-- The inverse of the structure map of an initial algebra -/
@[simp] def str_inv : A.1 âŸ¶ F.obj A.1 := (h.to âŸ¨ F.obj A.1 , F.map A.str âŸ©).1

lemma left_inv' : (âŸ¨str_inv h â‰« A.strâŸ© : A âŸ¶ A) = ğŸ™ A :=
limits.is_initial.hom_ext h _ (ğŸ™ A)

lemma left_inv : str_inv h â‰« A.str = ğŸ™ _ := congr_arg hom.f (left_inv' h)

lemma right_inv : A.str â‰« str_inv h = ğŸ™ _ :=
by { rw [str_inv, â† (h.to âŸ¨ F.obj A.1 , F.map A.str âŸ©).h,
  â† F.map_id, â† F.map_comp], congr, exact (left_inv h) }

/--
The structure map of the inital algebra is an isomorphism,
hence endofunctors preserve their initial algebras
-/
lemma str_is_iso (h : limits.is_initial A) : is_iso A.str :=
{ out := âŸ¨ str_inv h, right_inv _ , left_inv _ âŸ© }

end initial

end algebra

end endofunctor

end category_theory
