/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.monoidal.types
import category_theory.monoidal.braided

/-!
# Enriched categories

We set up the basic theory of `V`-enriched categories,
for `V` an arbitrary monoidal category.

We do not assume here that `V` is a concrete category,
so there does not need to be a "honest" underlying category!

Use `X âŸ¶[V] Y` to obtain the `V` object of morphisms from `X` to `Y`.

This file contains the definitions of `V`-enriched categories and
`V`-functors.

We don't yet define the `V`-object of natural transformations
between a pair of `V`-functors (this requires limits in `V`),
but we do provide a presheaf isomorphic to the Yoneda embedding of this object.

We verify that when `V = Type v`, all these notion reduce to the usual ones.
-/

universes w v uâ‚ uâ‚‚ uâ‚ƒ

namespace category_theory

open opposite
open monoidal_category

variables (V : Type v) [category.{w} V] [monoidal_category V]

/--
A `V`-category is a category enriched in a monoidal category `V`.

Note that we do not assume that `V` is a concrete category,
so there may not be an "honest" underlying category at all!
-/
class enriched_category (C : Type uâ‚) :=
(hom : C â†’ C â†’ V)
(notation X ` âŸ¶[] ` Y:10 := hom X Y)
(id : Î  X, ğŸ™_ V âŸ¶ (X âŸ¶[] X))
(comp : Î  X Y Z, (X âŸ¶[] Y) âŠ— (Y âŸ¶[] Z) âŸ¶ (X âŸ¶[] Z))
(id_comp : Î  X Y, (Î»_ (X âŸ¶[] Y)).inv â‰« (id X âŠ— ğŸ™ _) â‰« comp X X Y = ğŸ™ _ . obviously)
(comp_id : Î  X Y, (Ï_ (X âŸ¶[] Y)).inv â‰« (ğŸ™ _ âŠ— id Y) â‰« comp X Y Y = ğŸ™ _ . obviously)
(assoc :
  Î  W X Y Z, (Î±_ _ _ _).inv â‰« (comp W X Y âŠ— ğŸ™ _) â‰« comp W Y Z = (ğŸ™ _ âŠ— comp X Y Z) â‰« comp W X Z
  . obviously)

notation X ` âŸ¶[`V`] ` Y:10 := (enriched_category.hom X Y : V)

variables (V) {C : Type uâ‚} [enriched_category V C]

/--
The `ğŸ™_ V`-shaped generalized element giving the identity in a `V`-enriched category.
-/
def e_id (X : C) : ğŸ™_ V âŸ¶ (X âŸ¶[V] X) := enriched_category.id X
/--
The composition `V`-morphism for a `V`-enriched category.
-/
def e_comp (X Y Z : C) : (X âŸ¶[V] Y) âŠ— (Y âŸ¶[V] Z) âŸ¶ (X âŸ¶[V] Z) := enriched_category.comp X Y Z

@[simp, reassoc]
lemma e_id_comp (X Y : C) :
  (Î»_ (X âŸ¶[V] Y)).inv â‰« (e_id V X âŠ— ğŸ™ _) â‰« e_comp V X X Y = ğŸ™ (X âŸ¶[V] Y) :=
enriched_category.id_comp X Y

@[simp, reassoc]
lemma e_comp_id (X Y : C) :
  (Ï_ (X âŸ¶[V] Y)).inv â‰« (ğŸ™ _ âŠ— e_id V Y) â‰« e_comp V X Y Y = ğŸ™ (X âŸ¶[V] Y) :=
enriched_category.comp_id X Y

@[simp, reassoc]
lemma e_assoc (W X Y Z : C) :
  (Î±_ _ _ _).inv â‰« (e_comp V W X Y âŠ— ğŸ™ _) â‰« e_comp V W Y Z =
    (ğŸ™ _ âŠ— e_comp V X Y Z) â‰« e_comp V W X Z :=
enriched_category.assoc W X Y Z

section
variables {V} {W : Type v} [category.{w} W] [monoidal_category W]

/--
A type synonym for `C`, which should should come equipped with a `V`-enriched category structure.
In a moment we will equip this with the `W`-enriched category structure
obtained by applying the functor `F : lax_monoidal_functor V W` to each hom object.
-/
@[nolint has_inhabited_instance unused_arguments]
def transport_enrichment (F : lax_monoidal_functor V W) (C : Type uâ‚) := C

instance (F : lax_monoidal_functor V W) :
  enriched_category W (transport_enrichment F C) :=
{ hom := Î» (X Y : C), F.obj (X âŸ¶[V] Y),
  id := Î» (X : C), F.Îµ â‰« F.map (e_id V X),
  comp := Î» (X Y Z : C), F.Î¼ _ _ â‰« F.map (e_comp V X Y Z),
  id_comp := Î» X Y, begin
    rw [comp_tensor_id, category.assoc,
      â†F.to_functor.map_id, F.Î¼_natural_assoc, F.to_functor.map_id, F.left_unitality_inv_assoc,
      â†F.to_functor.map_comp, â†F.to_functor.map_comp, e_id_comp, F.to_functor.map_id],
  end,
  comp_id := Î» X Y, begin
    rw [id_tensor_comp, category.assoc,
      â†F.to_functor.map_id, F.Î¼_natural_assoc, F.to_functor.map_id, F.right_unitality_inv_assoc,
      â†F.to_functor.map_comp, â†F.to_functor.map_comp, e_comp_id, F.to_functor.map_id],
  end,
  assoc := Î» P Q R S, begin
    rw [comp_tensor_id, category.assoc, â†F.to_functor.map_id, F.Î¼_natural_assoc,
      F.to_functor.map_id, â†F.associativity_inv_assoc, â†F.to_functor.map_comp,
      â†F.to_functor.map_comp, e_assoc, id_tensor_comp, category.assoc, â†F.to_functor.map_id,
      F.Î¼_natural_assoc, F.to_functor.map_comp],
  end, }

end

/--
A `V`-functor `F` between `V`-enriched categories
has a `V`-morphism from `X âŸ¶[V] Y` to `F.obj X âŸ¶[V] F.obj Y`,
satisfying the usual axioms.
-/
@[nolint has_inhabited_instance]
structure enriched_functor
  (C : Type uâ‚) [enriched_category V C] (D : Type uâ‚‚) [enriched_category V D] :=
(obj : C â†’ D)
(map : Î  X Y : C, (X âŸ¶[V] Y) âŸ¶ (obj X âŸ¶[V] obj Y))
(map_id' : âˆ€ X : C, e_id V X â‰« map X X = e_id V (obj X) . obviously)
(map_comp' : âˆ€ X Y Z : C,
  e_comp V X Y Z â‰« map X Z = (map X Y âŠ— map Y Z) â‰« e_comp V (obj X) (obj Y) (obj Z) . obviously)

restate_axiom enriched_functor.map_id'
restate_axiom enriched_functor.map_comp'
attribute [simp, reassoc] enriched_functor.map_id
attribute [simp, reassoc] enriched_functor.map_comp

@[simps]
def enriched_functor.id (C : Type uâ‚) [enriched_category V C] : enriched_functor V C C :=
{ obj := Î» X, X,
  map := Î» X Y, ğŸ™ _, }

@[simps]
def enriched_functor.comp {C : Type uâ‚} {D : Type uâ‚‚} {E : Type uâ‚ƒ}
  [enriched_category V C] [enriched_category V D] [enriched_category V E]
  (F : enriched_functor V C D) (G : enriched_functor V D E) :
  enriched_functor V C E :=
{ obj := Î» X, G.obj (F.obj X),
  map := Î» X Y, F.map _ _ â‰« G.map _ _, }

section
variables {V} [braided_category V]
variables {D : Type uâ‚‚} [enriched_category V D]

/-!
For general `V`-enriched categories `C D`, and `V`-functors `F G`,
it's not possible to make sense of natural transformations between `F` and `G` at all.

An essential ingredient is a braiding (or symmetry) on `V`.

Even assuming that, we should only get an object in `V` worth of natural transformations,
rather than a type. Moreover, it's only possible to define this object if `V` has certain limits.

Here, we define a presheaf which is isomorphic to the Yoneda embedding of that object,
which we can do without any further assumptions.
-/

/--
The type of `A`-graded natural transformations between `V`-functors `F` and `G`.
This is the type of morphisms in `V` from `A` to the `V`-object of natural transformations.
-/
@[ext, nolint has_inhabited_instance]
structure graded_nat_trans (A : V) (F G : enriched_functor V C D) :=
(app : Î  (X : C), A âŸ¶ (F.obj X âŸ¶[V] G.obj X))
(naturality :
  âˆ€ (X Y : C), (app Y âŠ— F.map X Y) â‰« (Î²_ _ _).hom â‰« e_comp V _ _ _ =
    (app X âŠ— G.map X Y) â‰« e_comp V _ _ _)

/--
A presheaf isomorphic to the Yoneda embedding of
the `V`-object of natural transformations from `F` to `G`.
-/
@[simps]
def enriched_nat_trans_yoneda (F G : enriched_functor V C D) : Váµ’áµ– â¥¤ (Type (max uâ‚ w)) :=
{ obj := Î» A, graded_nat_trans (unop A) F G,
  map := Î» A A' f Ïƒ,
  { app := Î» X, f.unop â‰« Ïƒ.app X,
    naturality := Î» X Y, begin
      rw [â†tensor_id_comp_id_tensor _ (f.unop â‰« Ïƒ.app Y),
        â†tensor_id_comp_id_tensor _ (f.unop â‰« Ïƒ.app X),
        comp_tensor_id, comp_tensor_id,
        category.assoc, category.assoc, category.assoc, category.assoc,
        tensor_id_comp_id_tensor_assoc, tensor_id_comp_id_tensor_assoc,
        Ïƒ.naturality],
     end }, }

-- TODO assuming `[has_limits C]` construct the actual object of natural transformations
-- and show that the functor category is `V`-enriched.

end

/--
Construct an honest category from a `Type v`-enriched category.
-/
def category_of_enriched_category_Type (C : Type uâ‚) [ğ’ : enriched_category (Type v) C] :
  category.{v} C :=
{ hom := ğ’.hom,
  id := Î» X, e_id (Type v) X punit.star,
  comp := Î» X Y Z f g, e_comp (Type v) X Y Z âŸ¨f, gâŸ©,
  id_comp' := Î» X Y f, congr_fun (e_id_comp (Type v) X Y) f,
  comp_id' := Î» X Y f, congr_fun (e_comp_id (Type v) X Y) f,
  assoc' := Î» W X Y Z f g h, (congr_fun (e_assoc (Type v) W X Y Z) âŸ¨f, g, hâŸ© : _), }

/--
Construct a `Type v`-enriched category from an honest category.
-/
def enriched_category_Type_of_category (C : Type uâ‚) [ğ’ : category.{v} C] :
  enriched_category (Type v) C :=
{ hom := ğ’.hom,
  id := Î» X p, ğŸ™ X,
  comp := Î» X Y Z p, p.1 â‰« p.2,
  id_comp := Î» X Y, by { ext, simp, },
  comp_id := Î» X Y, by { ext, simp, },
  assoc := Î» W X Y Z, by { ext âŸ¨f, g, hâŸ©, simp, }, }

/--
We verify that an enriched category in `Type u` is just the same thing as an honest category.
-/
def enriched_category_Type_equiv_category (C : Type uâ‚) :
  (enriched_category (Type v) C) â‰ƒ category.{v} C :=
{ to_fun := Î» ğ’, by exactI category_of_enriched_category_Type C,
  inv_fun := Î» ğ’, by exactI enriched_category_Type_of_category C,
  left_inv := Î» ğ’, begin
    cases ğ’,
    dsimp [enriched_category_Type_of_category],
    congr,
    { ext X âŸ¨âŸ©, refl, },
    { ext X Y Z âŸ¨f, gâŸ©, refl, }
  end,
  right_inv := Î» ğ’, by { rcases ğ’ with âŸ¨âŸ¨âŸ¨âŸ©âŸ©âŸ©, dsimp, congr, }, }.

section
local attribute [instance] category_of_enriched_category_Type

/--
We verify that an enriched functor between `Type v` enriched categories
is just the same thing as an honest functor.
-/
@[simps]
def enriched_functor_Type_equiv_functor
  {C : Type uâ‚} [ğ’ : enriched_category (Type v) C]
  {D : Type uâ‚‚} [ğ’Ÿ : enriched_category (Type v) D] :
  enriched_functor (Type v) C D â‰ƒ (C â¥¤ D) :=
{ to_fun := Î» F,
  { obj := Î» X, F.obj X,
    map := Î» X Y f, F.map X Y f,
    map_id' := Î» X, congr_fun (F.map_id X) punit.star,
    map_comp' := Î» X Y Z f g, congr_fun (F.map_comp X Y Z) âŸ¨f, gâŸ©, },
  inv_fun := Î» F,
  { obj := Î» X, F.obj X,
    map := Î» X Y f, F.map f,
    map_id' := Î» X, by { ext âŸ¨âŸ©, exact F.map_id X, },
    map_comp' := Î» X Y Z, by { ext âŸ¨f, gâŸ©, exact F.map_comp f g, }, },
  left_inv := Î» F, by { cases F, simp, },
  right_inv := Î» F, by { cases F, simp, }, }

/--
We verify that the presheaf representing natural transformations
between `Type v`-enriched functors is actually represented by
the usual type of natural transformations!
-/
def enriched_nat_trans_yoneda_Type_iso_yoneda_nat_trans
  {C : Type v} [ğ’ : enriched_category (Type v) C]
  {D : Type v} [ğ’Ÿ : enriched_category (Type v) D]
  (F G : enriched_functor (Type v) C D) :
  enriched_nat_trans_yoneda F G â‰…
  yoneda.obj ((enriched_functor_Type_equiv_functor F) âŸ¶ (enriched_functor_Type_equiv_functor G)) :=
nat_iso.of_components (Î» Î±,
  { hom := Î» Ïƒ x,
    { app := Î» X, Ïƒ.app X x,
      naturality' := Î» X Y f, congr_fun (Ïƒ.naturality X Y) âŸ¨x, fâŸ©, },
    inv := Î» Ïƒ,
    { app := Î» X x, (Ïƒ x).app X,
      naturality := Î» X Y, by { ext âŸ¨x, fâŸ©, exact ((Ïƒ x).naturality f), }, }})
  (by tidy)

end

end category_theory
