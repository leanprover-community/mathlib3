/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import category_theory.fully_faithful
import category_theory.full_subcategory
import category_theory.whiskering
import category_theory.essential_image
import tactic.slice

/-!
# Equivalence of categories

An equivalence of categories `C` and `D` is a pair of functors `F : C â¥¤ D` and `G : D â¥¤ C` such
that `Î· : ğŸ­ C â‰… F â‹™ G` and `Îµ : G â‹™ F â‰… ğŸ­ D`. In many situations, equivalences are a better
notion of "sameness" of categories than the stricter isomorphims of categories.

Recall that one way to express that two functors `F : C â¥¤ D` and `G : D â¥¤ C` are adjoint is using
two natural transformations `Î· : ğŸ­ C âŸ¶ F â‹™ G` and `Îµ : G â‹™ F âŸ¶ ğŸ­ D`, called the unit and the
counit, such that the compositions `F âŸ¶ FGF âŸ¶ F` and `G âŸ¶ GFG âŸ¶ G` are the identity. Unfortunately,
it is not the case that the natural isomorphisms `Î·` and `Îµ` in the definition of an equivalence
automatically give an adjunction. However, it is true that
* if one of the two compositions is the identity, then so is the other, and
* given an equivalence of categories, it is always possible to refine `Î·` in such a way that the
  identities are satisfied.

For this reason, in mathlib we define an equivalence to be a "half-adjoint equivalence", which is
a tuple `(F, G, Î·, Îµ)` as in the first paragraph such that the composite `F âŸ¶ FGF âŸ¶ F` is the
identity. By the remark above, this already implies that the tuple is an "adjoint equivalence",
i.e., that the composite `G âŸ¶ GFG âŸ¶ G` is also the identity.

We also define essentially surjective functors and show that a functor is an equivalence if and only
if it is full, faithful and essentially surjective.

## Main definitions

* `equivalence`: bundled (half-)adjoint equivalences of categories
* `is_equivalence`: type class on a functor `F` containing the data of the inverse `G` as well as
  the natural isomorphisms `Î·` and `Îµ`.
* `ess_surj`: type class on a functor `F` containing the data of the preimages and the isomorphisms
  `F.obj (preimage d) â‰… d`.

## Main results

* `equivalence.mk`: upgrade an equivalence to a (half-)adjoint equivalence
* `equivalence.of_fully_faithfully_ess_surj`: a fully faithful essentially surjective functor is an
  equivalence.

## Notations

We write `C â‰Œ D` (`\backcong`, not to be confused with `â‰…`/`\cong`) for a bundled equivalence.

-/

namespace category_theory
open category_theory.functor nat_iso category
-- declare the `v`'s first; see `category_theory.category` for an explanation
universes vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

/-- We define an equivalence as a (half)-adjoint equivalence, a pair of functors with
  a unit and counit which are natural isomorphisms and the triangle law `FÎ· â‰« ÎµF = 1`, or in other
  words the composite `F âŸ¶ FGF âŸ¶ F` is the identity.

  In `unit_inverse_comp`, we show that this is actually an adjoint equivalence, i.e., that the
  composite `G âŸ¶ GFG âŸ¶ G` is also the identity.

  The triangle equation is written as a family of equalities between morphisms, it is more
  complicated if we write it as an equality of natural transformations, because then we would have
  to insert natural transformations like `F âŸ¶ F1`.

See https://stacks.math.columbia.edu/tag/001J
-/
structure equivalence (C : Type uâ‚) [category.{vâ‚} C] (D : Type uâ‚‚) [category.{vâ‚‚} D] :=
mk' ::
(functor : C â¥¤ D)
(inverse : D â¥¤ C)
(unit_iso   : ğŸ­ C â‰… functor â‹™ inverse)
(counit_iso : inverse â‹™ functor â‰… ğŸ­ D)
(functor_unit_iso_comp' : âˆ€(X : C), functor.map ((unit_iso.hom : ğŸ­ C âŸ¶ functor â‹™ inverse).app X) â‰«
  counit_iso.hom.app (functor.obj X) = ğŸ™ (functor.obj X) . obviously)

restate_axiom equivalence.functor_unit_iso_comp'

infixr ` â‰Œ `:10  := equivalence

variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚‚} D]

namespace equivalence

/-- The unit of an equivalence of categories. -/
abbreviation unit (e : C â‰Œ D) : ğŸ­ C âŸ¶ e.functor â‹™ e.inverse := e.unit_iso.hom
/-- The counit of an equivalence of categories. -/
abbreviation counit (e : C â‰Œ D) : e.inverse â‹™ e.functor âŸ¶ ğŸ­ D := e.counit_iso.hom
/-- The inverse of the unit of an equivalence of categories. -/
abbreviation unit_inv (e : C â‰Œ D) : e.functor â‹™ e.inverse âŸ¶ ğŸ­ C := e.unit_iso.inv
/-- The inverse of the counit of an equivalence of categories. -/
abbreviation counit_inv (e : C â‰Œ D) : ğŸ­ D âŸ¶ e.inverse â‹™ e.functor := e.counit_iso.inv

/- While these abbreviations are convenient, they also cause some trouble,
preventing structure projections from unfolding. -/
@[simp] lemma equivalence_mk'_unit (functor inverse unit_iso counit_iso f) :
  (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).unit = unit_iso.hom := rfl
@[simp] lemma equivalence_mk'_counit (functor inverse unit_iso counit_iso f) :
  (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).counit = counit_iso.hom := rfl
@[simp] lemma equivalence_mk'_unit_inv (functor inverse unit_iso counit_iso f) :
  (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).unit_inv = unit_iso.inv := rfl
@[simp] lemma equivalence_mk'_counit_inv (functor inverse unit_iso counit_iso f) :
  (âŸ¨functor, inverse, unit_iso, counit_iso, fâŸ© : C â‰Œ D).counit_inv = counit_iso.inv := rfl

@[simp] lemma functor_unit_comp (e : C â‰Œ D) (X : C) :
  e.functor.map (e.unit.app X) â‰« e.counit.app (e.functor.obj X) = ğŸ™ (e.functor.obj X) :=
e.functor_unit_iso_comp X

@[simp] lemma counit_inv_functor_comp (e : C â‰Œ D) (X : C) :
  e.counit_inv.app (e.functor.obj X) â‰« e.functor.map (e.unit_inv.app X) = ğŸ™ (e.functor.obj X) :=
begin
  erw [iso.inv_eq_inv
    (e.functor.map_iso (e.unit_iso.app X) â‰ªâ‰« e.counit_iso.app (e.functor.obj X)) (iso.refl _)],
  exact e.functor_unit_comp X
end

lemma counit_inv_app_functor (e : C â‰Œ D) (X : C) :
  e.counit_inv.app (e.functor.obj X) = e.functor.map (e.unit.app X) :=
by { symmetry, erw [â†iso.comp_hom_eq_id (e.counit_iso.app _), functor_unit_comp], refl }

lemma counit_app_functor (e : C â‰Œ D) (X : C) :
  e.counit.app (e.functor.obj X) = e.functor.map (e.unit_inv.app X) :=
by { erw [â†iso.hom_comp_eq_id (e.functor.map_iso (e.unit_iso.app X)), functor_unit_comp], refl }

/-- The other triangle equality. The proof follows the following proof in Globular:
  http://globular.science/1905.001 -/
@[simp] lemma unit_inverse_comp (e : C â‰Œ D) (Y : D) :
  e.unit.app (e.inverse.obj Y) â‰« e.inverse.map (e.counit.app Y) = ğŸ™ (e.inverse.obj Y) :=
begin
  rw [â†id_comp (e.inverse.map _), â†map_id e.inverse, â†counit_inv_functor_comp, map_comp,
      â†iso.hom_inv_id_assoc (e.unit_iso.app _) (e.inverse.map (e.functor.map _)),
      app_hom, app_inv],
  slice_lhs 2 3 { erw [e.unit.naturality] },
  slice_lhs 1 2 { erw [e.unit.naturality] },
  slice_lhs 4 4
  { rw [â†iso.hom_inv_id_assoc (e.inverse.map_iso (e.counit_iso.app _)) (e.unit_inv.app _)] },
  slice_lhs 3 4 { erw [â†map_comp e.inverse, e.counit.naturality],
    erw [(e.counit_iso.app _).hom_inv_id, map_id] }, erw [id_comp],
  slice_lhs 2 3 { erw [â†map_comp e.inverse, e.counit_iso.inv.naturality, map_comp] },
  slice_lhs 3 4 { erw [e.unit_inv.naturality] },
  slice_lhs 4 5 { erw [â†map_comp (e.functor â‹™ e.inverse), (e.unit_iso.app _).hom_inv_id, map_id] },
  erw [id_comp],
  slice_lhs 3 4 { erw [â†e.unit_inv.naturality] },
  slice_lhs 2 3 { erw [â†map_comp e.inverse, â†e.counit_iso.inv.naturality,
    (e.counit_iso.app _).hom_inv_id, map_id] }, erw [id_comp, (e.unit_iso.app _).hom_inv_id], refl
end

@[simp] lemma inverse_counit_inv_comp (e : C â‰Œ D) (Y : D) :
  e.inverse.map (e.counit_inv.app Y) â‰« e.unit_inv.app (e.inverse.obj Y) = ğŸ™ (e.inverse.obj Y) :=
begin
  erw [iso.inv_eq_inv
    (e.unit_iso.app (e.inverse.obj Y) â‰ªâ‰« e.inverse.map_iso (e.counit_iso.app Y)) (iso.refl _)],
  exact e.unit_inverse_comp Y
end

lemma unit_app_inverse (e : C â‰Œ D) (Y : D) :
  e.unit.app (e.inverse.obj Y) = e.inverse.map (e.counit_inv.app Y) :=
by { erw [â†iso.comp_hom_eq_id (e.inverse.map_iso (e.counit_iso.app Y)), unit_inverse_comp], refl }

lemma unit_inv_app_inverse (e : C â‰Œ D) (Y : D) :
  e.unit_inv.app (e.inverse.obj Y) = e.inverse.map (e.counit.app Y) :=
by { symmetry, erw [â†iso.hom_comp_eq_id (e.unit_iso.app _), unit_inverse_comp], refl }

@[simp] lemma fun_inv_map (e : C â‰Œ D) (X Y : D) (f : X âŸ¶ Y) :
  e.functor.map (e.inverse.map f) = e.counit.app X â‰« f â‰« e.counit_inv.app Y :=
(nat_iso.naturality_2 (e.counit_iso) f).symm

@[simp] lemma inv_fun_map (e : C â‰Œ D) (X Y : C) (f : X âŸ¶ Y) :
  e.inverse.map (e.functor.map f) = e.unit_inv.app X â‰« f â‰« e.unit.app Y :=
(nat_iso.naturality_1 (e.unit_iso) f).symm

section
-- In this section we convert an arbitrary equivalence to a half-adjoint equivalence.
variables {F : C â¥¤ D} {G : D â¥¤ C} (Î· : ğŸ­ C â‰… F â‹™ G) (Îµ : G â‹™ F â‰… ğŸ­ D)

/-- If `Î· : ğŸ­ C â‰… F â‹™ G` is part of a (not necessarily half-adjoint) equivalence, we can upgrade it
to a refined natural isomorphism `adjointify_Î· Î· : ğŸ­ C â‰… F â‹™ G` which exhibits the properties
required for a half-adjoint equivalence. See `equivalence.mk`. -/
def adjointify_Î· : ğŸ­ C â‰… F â‹™ G :=
calc
  ğŸ­ C â‰… F â‹™ G               : Î·
  ... â‰… F â‹™ (ğŸ­ D â‹™ G)      : iso_whisker_left F (left_unitor G).symm
  ... â‰… F â‹™ ((G â‹™ F) â‹™ G) : iso_whisker_left F (iso_whisker_right Îµ.symm G)
  ... â‰… F â‹™ (G â‹™ (F â‹™ G)) : iso_whisker_left F (associator G F G)
  ... â‰… (F â‹™ G) â‹™ (F â‹™ G) : (associator F G (F â‹™ G)).symm
  ... â‰… ğŸ­ C â‹™ (F â‹™ G)      : iso_whisker_right Î·.symm (F â‹™ G)
  ... â‰… F â‹™ G               : left_unitor (F â‹™ G)

lemma adjointify_Î·_Îµ (X : C) :
  F.map ((adjointify_Î· Î· Îµ).hom.app X) â‰« Îµ.hom.app (F.obj X) = ğŸ™ (F.obj X) :=
begin
  dsimp [adjointify_Î·], simp,
  have := Îµ.hom.naturality (F.map (Î·.inv.app X)), dsimp at this, rw [this], clear this,
  rw [â†assoc _ _ (F.map _)],
  have := Îµ.hom.naturality (Îµ.inv.app $ F.obj X), dsimp at this, rw [this], clear this,
  have := (Îµ.app $ F.obj X).hom_inv_id, dsimp at this, rw [this], clear this,
  rw [id_comp], have := (F.map_iso $ Î·.app X).hom_inv_id, dsimp at this, rw [this]
end

end

/-- Every equivalence of categories consisting of functors `F` and `G` such that `F â‹™ G` and
    `G â‹™ F` are naturally isomorphic to identity functors can be transformed into a half-adjoint
    equivalence without changing `F` or `G`. -/
protected definition mk (F : C â¥¤ D) (G : D â¥¤ C)
  (Î· : ğŸ­ C â‰… F â‹™ G) (Îµ : G â‹™ F â‰… ğŸ­ D) : C â‰Œ D :=
âŸ¨F, G, adjointify_Î· Î· Îµ, Îµ, adjointify_Î·_Îµ Î· ÎµâŸ©

/-- Equivalence of categories is reflexive. -/
@[refl, simps] def refl : C â‰Œ C :=
âŸ¨ğŸ­ C, ğŸ­ C, iso.refl _, iso.refl _, Î» X, category.id_comp _âŸ©

instance : inhabited (C â‰Œ C) :=
âŸ¨reflâŸ©

/-- Equivalence of categories is symmetric. -/
@[symm, simps] def symm (e : C â‰Œ D) : D â‰Œ C :=
âŸ¨e.inverse, e.functor, e.counit_iso.symm, e.unit_iso.symm, e.inverse_counit_inv_compâŸ©

variables {E : Type uâ‚ƒ} [category.{vâ‚ƒ} E]

/-- Equivalence of categories is transitive. -/
@[trans, simps] def trans (e : C â‰Œ D) (f : D â‰Œ E) : C â‰Œ E :=
{ functor := e.functor â‹™ f.functor,
  inverse := f.inverse â‹™ e.inverse,
  unit_iso :=
  begin
    refine iso.trans e.unit_iso _,
    exact iso_whisker_left e.functor (iso_whisker_right f.unit_iso e.inverse) ,
  end,
  counit_iso :=
  begin
    refine iso.trans _ f.counit_iso,
    exact iso_whisker_left f.inverse (iso_whisker_right e.counit_iso f.functor)
  end,
  -- We wouldn't have needed to give this proof if we'd used `equivalence.mk`,
  -- but we choose to avoid using that here, for the sake of good structure projection `simp`
  -- lemmas.
  functor_unit_iso_comp' := Î» X,
  begin
    dsimp,
    rw [â† f.functor.map_comp_assoc, e.functor.map_comp, â†counit_inv_app_functor, fun_inv_map,
        iso.inv_hom_id_app_assoc, assoc, iso.inv_hom_id_app, counit_app_functor,
        â† functor.map_comp],
    erw [comp_id, iso.hom_inv_id_app, functor.map_id],
  end }

/-- Composing a functor with both functors of an equivalence yields a naturally isomorphic
functor. -/
def fun_inv_id_assoc (e : C â‰Œ D) (F : C â¥¤ E) : e.functor â‹™ e.inverse â‹™ F â‰… F :=
(functor.associator _ _ _).symm â‰ªâ‰« iso_whisker_right e.unit_iso.symm F â‰ªâ‰« F.left_unitor

@[simp] lemma fun_inv_id_assoc_hom_app (e : C â‰Œ D) (F : C â¥¤ E) (X : C) :
  (fun_inv_id_assoc e F).hom.app X = F.map (e.unit_inv.app X) :=
by { dsimp [fun_inv_id_assoc], tidy }

@[simp] lemma fun_inv_id_assoc_inv_app (e : C â‰Œ D) (F : C â¥¤ E) (X : C) :
  (fun_inv_id_assoc e F).inv.app X = F.map (e.unit.app X) :=
by { dsimp [fun_inv_id_assoc], tidy }

/-- Composing a functor with both functors of an equivalence yields a naturally isomorphic
functor. -/
def inv_fun_id_assoc (e : C â‰Œ D) (F : D â¥¤ E) : e.inverse â‹™ e.functor â‹™ F â‰… F :=
(functor.associator _ _ _).symm â‰ªâ‰« iso_whisker_right e.counit_iso F â‰ªâ‰« F.left_unitor

@[simp] lemma inv_fun_id_assoc_hom_app (e : C â‰Œ D) (F : D â¥¤ E) (X : D) :
  (inv_fun_id_assoc e F).hom.app X = F.map (e.counit.app X) :=
by { dsimp [inv_fun_id_assoc], tidy }

@[simp] lemma inv_fun_id_assoc_inv_app (e : C â‰Œ D) (F : D â¥¤ E) (X : D) :
  (inv_fun_id_assoc e F).inv.app X = F.map (e.counit_inv.app X) :=
by { dsimp [inv_fun_id_assoc], tidy }

/-- If `C` is equivalent to `D`, then `C â¥¤ E` is equivalent to `D â¥¤ E`. -/
@[simps functor inverse unit_iso counit_iso]
def congr_left (e : C â‰Œ D) : (C â¥¤ E) â‰Œ (D â¥¤ E) :=
equivalence.mk
  ((whiskering_left _ _ _).obj e.inverse)
  ((whiskering_left _ _ _).obj e.functor)
  (nat_iso.of_components (Î» F, (e.fun_inv_id_assoc F).symm) (by tidy))
  (nat_iso.of_components (Î» F, e.inv_fun_id_assoc F) (by tidy))

/-- If `C` is equivalent to `D`, then `E â¥¤ C` is equivalent to `E â¥¤ D`. -/
@[simps functor inverse unit_iso counit_iso]
def congr_right (e : C â‰Œ D) : (E â¥¤ C) â‰Œ (E â¥¤ D) :=
equivalence.mk
  ((whiskering_right _ _ _).obj e.functor)
  ((whiskering_right _ _ _).obj e.inverse)
  (nat_iso.of_components
    (Î» F, F.right_unitor.symm â‰ªâ‰« iso_whisker_left F e.unit_iso â‰ªâ‰« functor.associator _ _ _)
    (by tidy))
  (nat_iso.of_components
    (Î» F, functor.associator _ _ _ â‰ªâ‰« iso_whisker_left F e.counit_iso â‰ªâ‰« F.right_unitor)
    (by tidy))

section cancellation_lemmas
variables (e : C â‰Œ D)

/- We need special forms of `cancel_nat_iso_hom_right(_assoc)` and
`cancel_nat_iso_inv_right(_assoc)` for units and counits, because neither `simp` or `rw` will apply
those lemmas in this setting without providing `e.unit_iso` (or similar) as an explicit argument.
We also provide the lemmas for length four compositions, since they're occasionally useful.
(e.g. in proving that equivalences take monos to monos) -/

@[simp] lemma cancel_unit_right {X Y : C}
  (f f' : X âŸ¶ Y) :
  f â‰« e.unit.app Y = f' â‰« e.unit.app Y â†” f = f' :=
by simp only [cancel_mono]

@[simp] lemma cancel_unit_inv_right {X Y : C}
  (f f' : X âŸ¶ e.inverse.obj (e.functor.obj Y))   :
  f â‰« e.unit_inv.app Y = f' â‰« e.unit_inv.app Y â†” f = f' :=
by simp only [cancel_mono]

@[simp] lemma cancel_counit_right {X Y : D}
  (f f' : X âŸ¶ e.functor.obj (e.inverse.obj Y))   :
  f â‰« e.counit.app Y = f' â‰« e.counit.app Y â†” f = f' :=
by simp only [cancel_mono]

@[simp] lemma cancel_counit_inv_right {X Y : D}
  (f f' : X âŸ¶ Y) :
  f â‰« e.counit_inv.app Y = f' â‰« e.counit_inv.app Y â†” f = f' :=
by simp only [cancel_mono]

@[simp] lemma cancel_unit_right_assoc {W X X' Y : C}
  (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X') (g' : X' âŸ¶ Y) :
  f â‰« g â‰« e.unit.app Y = f' â‰« g' â‰« e.unit.app Y â†” f â‰« g = f' â‰« g' :=
by simp only [â†category.assoc, cancel_mono]

@[simp] lemma cancel_counit_inv_right_assoc {W X X' Y : D}
  (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X') (g' : X' âŸ¶ Y) :
  f â‰« g â‰« e.counit_inv.app Y = f' â‰« g' â‰« e.counit_inv.app Y â†” f â‰« g = f' â‰« g' :=
by simp only [â†category.assoc, cancel_mono]

@[simp] lemma cancel_unit_right_assoc' {W X X' Y Y' Z : C}
  (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) (f' : W âŸ¶ X') (g' : X' âŸ¶ Y') (h' : Y' âŸ¶ Z) :
  f â‰« g â‰« h â‰« e.unit.app Z = f' â‰« g' â‰« h' â‰« e.unit.app Z â†” f â‰« g â‰« h = f' â‰« g' â‰« h' :=
by simp only [â†category.assoc, cancel_mono]

@[simp] lemma cancel_counit_inv_right_assoc' {W X X' Y Y' Z : D}
  (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) (f' : W âŸ¶ X') (g' : X' âŸ¶ Y') (h' : Y' âŸ¶ Z) :
  f â‰« g â‰« h â‰« e.counit_inv.app Z = f' â‰« g' â‰« h' â‰« e.counit_inv.app Z â†” f â‰« g â‰« h = f' â‰« g' â‰« h' :=
by simp only [â†category.assoc, cancel_mono]

end cancellation_lemmas

section

-- There's of course a monoid structure on `C â‰Œ C`,
-- but let's not encourage using it.
-- The power structure is nevertheless useful.

/-- Natural number powers of an auto-equivalence.  Use `(^)` instead. -/
def pow_nat (e : C â‰Œ C) : â„• â†’ (C â‰Œ C)
| 0 := equivalence.refl
| 1 := e
| (n+2) := e.trans (pow_nat (n+1))

/-- Powers of an auto-equivalence.  Use `(^)` instead. -/
def pow (e : C â‰Œ C) : â„¤ â†’ (C â‰Œ C)
| (int.of_nat n) := e.pow_nat n
| (int.neg_succ_of_nat n) := e.symm.pow_nat (n+1)

instance : has_pow (C â‰Œ C) â„¤ := âŸ¨powâŸ©

@[simp] lemma pow_zero (e : C â‰Œ C) : e^(0 : â„¤) = equivalence.refl := rfl
@[simp] lemma pow_one (e : C â‰Œ C) : e^(1 : â„¤) = e := rfl
@[simp] lemma pow_neg_one (e : C â‰Œ C) : e^(-1 : â„¤) = e.symm := rfl

-- TODO as necessary, add the natural isomorphisms `(e^a).trans e^b â‰… e^(a+b)`.
-- At this point, we haven't even defined the category of equivalences.

end

end equivalence


/-- A functor that is part of a (half) adjoint equivalence -/
class is_equivalence (F : C â¥¤ D) :=
mk' ::
(inverse    : D â¥¤ C)
(unit_iso   : ğŸ­ C â‰… F â‹™ inverse)
(counit_iso : inverse â‹™ F â‰… ğŸ­ D)
(functor_unit_iso_comp' : âˆ€ (X : C), F.map ((unit_iso.hom : ğŸ­ C âŸ¶ F â‹™ inverse).app X) â‰«
  counit_iso.hom.app (F.obj X) = ğŸ™ (F.obj X) . obviously)

restate_axiom is_equivalence.functor_unit_iso_comp'

namespace is_equivalence

instance of_equivalence (F : C â‰Œ D) : is_equivalence F.functor :=
{ ..F }

instance of_equivalence_inverse (F : C â‰Œ D) : is_equivalence F.inverse :=
is_equivalence.of_equivalence F.symm

open equivalence
/-- To see that a functor is an equivalence, it suffices to provide an inverse functor `G` such that
    `F â‹™ G` and `G â‹™ F` are naturally isomorphic to identity functors. -/
protected definition mk {F : C â¥¤ D} (G : D â¥¤ C)
  (Î· : ğŸ­ C â‰… F â‹™ G) (Îµ : G â‹™ F â‰… ğŸ­ D) : is_equivalence F :=
âŸ¨G, adjointify_Î· Î· Îµ, Îµ, adjointify_Î·_Îµ Î· ÎµâŸ©

end is_equivalence


namespace functor

/-- Interpret a functor that is an equivalence as an equivalence. -/
def as_equivalence (F : C â¥¤ D) [is_equivalence F] : C â‰Œ D :=
âŸ¨F, is_equivalence.inverse F, is_equivalence.unit_iso, is_equivalence.counit_iso,
  is_equivalence.functor_unit_iso_compâŸ©

instance is_equivalence_refl : is_equivalence (ğŸ­ C) :=
is_equivalence.of_equivalence equivalence.refl

/-- The inverse functor of a functor that is an equivalence. -/
def inv (F : C â¥¤ D) [is_equivalence F] : D â¥¤ C :=
is_equivalence.inverse F

instance is_equivalence_inv (F : C â¥¤ D) [is_equivalence F] : is_equivalence F.inv :=
is_equivalence.of_equivalence F.as_equivalence.symm

@[simp] lemma as_equivalence_functor (F : C â¥¤ D) [is_equivalence F] :
  F.as_equivalence.functor = F := rfl

@[simp] lemma as_equivalence_inverse (F : C â¥¤ D) [is_equivalence F] :
  F.as_equivalence.inverse = inv F := rfl

@[simp] lemma inv_inv (F : C â¥¤ D) [is_equivalence F] :
  inv (inv F) = F := rfl

variables {E : Type uâ‚ƒ} [category.{vâ‚ƒ} E]

instance is_equivalence_trans (F : C â¥¤ D) (G : D â¥¤ E) [is_equivalence F] [is_equivalence G] :
  is_equivalence (F â‹™ G) :=
is_equivalence.of_equivalence (equivalence.trans (as_equivalence F) (as_equivalence G))

end functor

namespace equivalence

@[simp]
lemma functor_inv (E : C â‰Œ D) : E.functor.inv = E.inverse := rfl

@[simp]
lemma inverse_inv (E : C â‰Œ D) : E.inverse.inv = E.functor := rfl

@[simp]
lemma functor_as_equivalence (E : C â‰Œ D) : E.functor.as_equivalence = E :=
by { cases E, congr, }

@[simp]
lemma inverse_as_equivalence (E : C â‰Œ D) : E.inverse.as_equivalence = E.symm :=
by { cases E, congr, }

end equivalence

namespace is_equivalence

@[simp] lemma fun_inv_map (F : C â¥¤ D) [is_equivalence F] (X Y : D) (f : X âŸ¶ Y) :
  F.map (F.inv.map f) = F.as_equivalence.counit.app X â‰« f â‰« F.as_equivalence.counit_inv.app Y :=
begin
  erw [nat_iso.naturality_2],
  refl
end
@[simp] lemma inv_fun_map (F : C â¥¤ D) [is_equivalence F] (X Y : C) (f : X âŸ¶ Y) :
  F.inv.map (F.map f) = F.as_equivalence.unit_inv.app X â‰« f â‰« F.as_equivalence.unit.app Y :=
begin
  erw [nat_iso.naturality_1],
  refl
end

end is_equivalence

namespace equivalence

/--
An equivalence is essentially surjective.

See https://stacks.math.columbia.edu/tag/02C3.
-/
lemma ess_surj_of_equivalence (F : C â¥¤ D) [is_equivalence F] : ess_surj F :=
âŸ¨Î» Y, âŸ¨F.inv.obj Y, âŸ¨F.as_equivalence.counit_iso.app YâŸ©âŸ©âŸ©

/--
An equivalence is faithful.

See https://stacks.math.columbia.edu/tag/02C3.
-/
@[priority 100] -- see Note [lower instance priority]
instance faithful_of_equivalence (F : C â¥¤ D) [is_equivalence F] : faithful F :=
{ map_injective' := Î» X Y f g w,
  begin
    have p := congr_arg (@category_theory.functor.map _ _ _ _ F.inv _ _) w,
    simpa only [cancel_epi, cancel_mono, is_equivalence.inv_fun_map] using p
  end }.

/--
An equivalence is full.

See https://stacks.math.columbia.edu/tag/02C3.
-/
@[priority 100] -- see Note [lower instance priority]
instance full_of_equivalence (F : C â¥¤ D) [is_equivalence F] : full F :=
{ preimage := Î» X Y f, F.as_equivalence.unit.app X â‰« F.inv.map f â‰« F.as_equivalence.unit_inv.app Y,
  witness' := Î» X Y f, F.inv.map_injective $
  by simpa only [is_equivalence.inv_fun_map, assoc, iso.inv_hom_id_app_assoc, iso.inv_hom_id_app]
    using comp_id _ }

@[simps] private noncomputable def equivalence_inverse (F : C â¥¤ D) [full F] [faithful F]
  [ess_surj F] : D â¥¤ C :=
{ obj  := Î» X, F.obj_preimage X,
  map := Î» X Y f, F.preimage ((F.obj_obj_preimage_iso X).hom â‰« f â‰« (F.obj_obj_preimage_iso Y).inv),
  map_id' := Î» X, begin apply F.map_injective, tidy end,
  map_comp' := Î» X Y Z f g, by apply F.map_injective; simp }

/--
A functor which is full, faithful, and essentially surjective is an equivalence.

See https://stacks.math.columbia.edu/tag/02C3.
-/
noncomputable def of_fully_faithfully_ess_surj
  (F : C â¥¤ D) [full F] [faithful F] [ess_surj F] : is_equivalence F :=
is_equivalence.mk (equivalence_inverse F)
  (nat_iso.of_components
    (Î» X, (preimage_iso $ F.obj_obj_preimage_iso $ F.obj X).symm)
    (Î» X Y f, by { apply F.map_injective, obviously }))
  (nat_iso.of_components F.obj_obj_preimage_iso (by tidy))

@[simp] lemma functor_map_inj_iff (e : C â‰Œ D) {X Y : C} (f g : X âŸ¶ Y) :
  e.functor.map f = e.functor.map g â†” f = g :=
âŸ¨Î» h, e.functor.map_injective h, Î» h, h â–¸ rflâŸ©

@[simp] lemma inverse_map_inj_iff (e : C â‰Œ D) {X Y : D} (f g : X âŸ¶ Y) :
  e.inverse.map f = e.inverse.map g â†” f = g :=
functor_map_inj_iff e.symm f g

instance ess_surj_induced_functor {C' : Type*} (e : C' â‰ƒ D) : ess_surj (induced_functor e) :=
{ mem_ess_image := Î» Y, âŸ¨e.symm Y, by simpâŸ©, }

noncomputable
instance induced_functor_of_equiv {C' : Type*} (e : C' â‰ƒ D) : is_equivalence (induced_functor e) :=
equivalence.of_fully_faithfully_ess_surj _

end equivalence

section partial_order
variables {Î± Î² : Type*} [partial_order Î±] [partial_order Î²]

/--
A categorical equivalence between partial orders is just an order isomorphism.
-/
def equivalence.to_order_iso (e : Î± â‰Œ Î²) : Î± â‰ƒo Î² :=
{ to_fun := e.functor.obj,
  inv_fun := e.inverse.obj,
  left_inv := Î» a, (e.unit_iso.app a).to_eq.symm,
  right_inv := Î» b, (e.counit_iso.app b).to_eq,
  map_rel_iff' := Î» a a',
    âŸ¨Î» h, ((equivalence.unit e).app a â‰« e.inverse.map h.hom â‰« (equivalence.unit_inv e).app a').le,
     Î» (h : a â‰¤ a'), (e.functor.map h.hom).leâŸ©, }

-- `@[simps]` on `equivalence.to_order_iso` produces lemmas that fail the `simp_nf` linter,
-- so we provide them by hand:
@[simp]
lemma equivalence.to_order_iso_apply (e : Î± â‰Œ Î²) (a : Î±) :
  e.to_order_iso a = e.functor.obj a := rfl

@[simp]
lemma equivalence.to_order_iso_symm_apply (e : Î± â‰Œ Î²) (b : Î²) :
  e.to_order_iso.symm b = e.inverse.obj b := rfl

end partial_order

end category_theory
