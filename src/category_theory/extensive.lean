/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import category_theory.limits.shapes.comm_sq
import category_theory.limits.shapes.strict_initial
import category_theory.limits.shapes.types
import topology.category.Top.limits
import category_theory.limits.functor_category

/-!

# Extensive categories

## Main definitions
- `category_theory.is_van_kampen_colimit`: A (colimit) cocone over a diagram `F : J ‚•§ C` is van
  Kampen if for every cocone `c'` over the pullback of the diagram `F' : J ‚•§ C'`,
  `c'` is colimiting iff `c'` is the pullback of `c`.
- `category_theory.finitary_extensive`: A category is (finitary) extensive if it has finite
  coproducts, and binary coproducts are van Kampen.

## Main Results
- `category_theory.has_strict_initial_objects_of_finitary_extensive`: The initial object
  in extensive categories is strict.
- `category_theory.finitary_extensive.mono_inr_of_is_colimit`: Coproduct injections are monic in
  extensive categories.
- `category_theory.binary_cofan.is_pullback_initial_to_of_is_van_kampen`: In extensive categories,
  sums are disjoint, i.e. the pullback of `X ‚ü∂ X ‚®ø Y` and `Y ‚ü∂ X ‚®ø Y` is the initial object.
- `category_theory.types.finitary_extensive`: The category of types is extensive.

## TODO

Show that the following are finitary extensive:
- the categories of sheaves over a site
- `Scheme`
- `AffineScheme` (`CommRing·µí·µñ`)

## References
- https://ncatlab.org/nlab/show/extensive+category
- [Carboni et al, Introduction to extensive and distributive categories][CARBONI1993145]

-/

open category_theory.limits

namespace category_theory

universes v' u' v u

variables {J : Type v'} [category.{u'} J] {C : Type u} [category.{v} C]

/-- A natural transformation is equifibered if every commutative square of the following form is
a pullback.
```
F(X) ‚Üí F(Y)
 ‚Üì      ‚Üì
G(X) ‚Üí G(Y)
```
-/
def nat_trans.equifibered {F G : J ‚•§ C} (Œ± : F ‚ü∂ G) : Prop :=
‚àÄ ‚¶Éi j : J‚¶Ñ (f : i ‚ü∂ j), is_pullback (F.map f) (Œ±.app i) (Œ±.app j) (G.map f)

lemma nat_trans.equifibered_of_is_iso {F G : J ‚•§ C} (Œ± : F ‚ü∂ G) [is_iso Œ±] : Œ±.equifibered :=
Œª _ _ f, is_pullback.of_vert_is_iso ‚ü®nat_trans.naturality _ f‚ü©

lemma nat_trans.equifibered.comp {F G H : J ‚•§ C} {Œ± : F ‚ü∂ G} {Œ≤ : G ‚ü∂ H}
  (hŒ± : Œ±.equifibered) (hŒ≤ : Œ≤.equifibered) : (Œ± ‚â´ Œ≤).equifibered :=
Œª i j f, (hŒ± f).paste_vert (hŒ≤ f)

/-- A (colimit) cocone over a diagram `F : J ‚•§ C` is universal if it is stable under pullbacks. -/
def is_universal_colimit {F : J ‚•§ C} (c : cocone F) : Prop :=
‚àÄ ‚¶ÉF' : J ‚•§ C‚¶Ñ (c' : cocone F') (Œ± : F' ‚ü∂ F) (f : c'.X ‚ü∂ c.X)
  (h : Œ± ‚â´ c.Œπ = c'.Œπ ‚â´ (functor.const J).map f) (hŒ± : Œ±.equifibered),
  (‚àÄ j : J, is_pullback (c'.Œπ.app j) (Œ±.app j) f (c.Œπ.app j)) ‚Üí nonempty (is_colimit c')

/-- A (colimit) cocone over a diagram `F : J ‚•§ C` is van Kampen if for every cocone `c'` over the
pullback of the diagram `F' : J ‚•§ C'`, `c'` is colimiting iff `c'` is the pullback of `c`.

TODO: Show that this is iff the functor `C ‚•§ Cat·µí·µñ` sending `x` to `C/x` preserves it.
TODO: Show that this is iff the inclusion functor `C ‚•§ Span(C)` preserves it.
-/
def is_van_kampen_colimit {F : J ‚•§ C} (c : cocone F) : Prop :=
‚àÄ ‚¶ÉF' : J ‚•§ C‚¶Ñ (c' : cocone F') (Œ± : F' ‚ü∂ F) (f : c'.X ‚ü∂ c.X)
  (h : Œ± ‚â´ c.Œπ = c'.Œπ ‚â´ (functor.const J).map f) (hŒ± : Œ±.equifibered),
  nonempty (is_colimit c') ‚Üî ‚àÄ j : J, is_pullback (c'.Œπ.app j) (Œ±.app j) f (c.Œπ.app j)

lemma is_van_kampen_colimit.is_universal {F : J ‚•§ C} {c : cocone F} (H : is_van_kampen_colimit c) :
  is_universal_colimit c :=
Œª _ c' Œ± f h hŒ±, (H c' Œ± f h hŒ±).mpr

/-- A van Kampen colimit is a colimit. -/
noncomputable
def is_van_kampen_colimit.is_colimit {F : J ‚•§ C} {c : cocone F} (h : is_van_kampen_colimit c) :
  is_colimit c :=
begin
  refine ((h c (ùüô F) (ùüô c.X : _) (by rw [functor.map_id, category.comp_id, category.id_comp])
    (nat_trans.equifibered_of_is_iso _)).mpr $ Œª j, _).some,
  haveI : is_iso (ùüô c.X) := infer_instance,
  exact is_pullback.of_vert_is_iso ‚ü®by erw [nat_trans.id_app, category.comp_id, category.id_comp]‚ü©,
end

lemma is_initial.is_van_kampen_colimit [has_strict_initial_objects C] {X : C} (h : is_initial X) :
  is_van_kampen_colimit (as_empty_cocone X) :=
begin
  intros F' c' Œ± f hf hŒ±,
  have : F' = functor.empty C := by apply functor.hext; rintro ‚ü®‚ü®‚ü©‚ü©,
  subst this,
  haveI := h.is_iso_to f,
  refine ‚ü®by rintro _ ‚ü®‚ü®‚ü©‚ü©, Œª _,
    ‚ü®is_colimit.of_iso_colimit h (cocones.ext (as_iso f).symm $ by rintro ‚ü®‚ü®‚ü©‚ü©)‚ü©‚ü©
end

section extensive

variables {X Y : C}

/--
A category is (finitary) extensive if it has finite coproducts,
and binary coproducts are van Kampen.

TODO: Show that this is iff all finite coproducts are van Kampen. -/
class finitary_extensive (C : Type u) [category.{v} C] : Prop :=
[has_finite_coproducts : has_finite_coproducts C]
(van_kampen' : ‚àÄ {X Y : C} (c : binary_cofan X Y), is_colimit c ‚Üí is_van_kampen_colimit c)

attribute [priority 100, instance] finitary_extensive.has_finite_coproducts

lemma finitary_extensive.van_kampen [finitary_extensive C] {F : discrete walking_pair ‚•§ C}
  (c : cocone F) (hc : is_colimit c) : is_van_kampen_colimit c :=
begin
  let X := F.obj ‚ü®walking_pair.left‚ü©, let Y := F.obj ‚ü®walking_pair.right‚ü©,
  have : F = pair X Y,
  { apply functor.hext, { rintros ‚ü®‚ü®‚ü©‚ü©; refl }, { rintros ‚ü®‚ü®‚ü©‚ü© ‚ü®j‚ü© ‚ü®‚ü®rfl : _ = j‚ü©‚ü©; simpa } },
  clear_value X Y, subst this,
  exact finitary_extensive.van_kampen' c hc
end

lemma map_pair_equifibered {F F' : discrete walking_pair ‚•§ C} (Œ± : F ‚ü∂ F') : Œ±.equifibered :=
begin
  rintros ‚ü®‚ü®‚ü©‚ü© ‚ü®j‚ü© ‚ü®‚ü®rfl : _ = j‚ü©‚ü©,
  all_goals { dsimp, simp only [discrete.functor_map_id],
    exact is_pullback.of_horiz_is_iso ‚ü®by simp only [category.comp_id, category.id_comp]‚ü© }
end

lemma binary_cofan.is_van_kampen_iff (c : binary_cofan X Y) :
  is_van_kampen_colimit c ‚Üî
  ‚àÄ {X' Y' : C} (c' : binary_cofan X' Y') (Œ±X : X' ‚ü∂ X) (Œ±Y : Y' ‚ü∂ Y)
    (f : c'.X ‚ü∂ c.X) (hŒ±X : Œ±X ‚â´ c.inl = c'.inl ‚â´ f) (hŒ±Y : Œ±Y ‚â´ c.inr = c'.inr ‚â´ f),
    nonempty (is_colimit c') ‚Üî is_pullback c'.inl Œ±X f c.inl ‚àß is_pullback c'.inr Œ±Y f c.inr :=
begin
  split,
  { introv H hŒ±X hŒ±Y,
    rw H c' (map_pair Œ±X Œ±Y) f (by ext ‚ü®‚ü®‚ü©‚ü©; dsimp; assumption) (map_pair_equifibered _),
    split, { intro H, exact ‚ü®H _, H _‚ü© }, { rintros H ‚ü®‚ü®‚ü©‚ü©, exacts [H.1, H.2] } },
  { introv H F' hŒ± h,
    let X' := F'.obj ‚ü®walking_pair.left‚ü©, let Y' := F'.obj ‚ü®walking_pair.right‚ü©,
    have : F' = pair X' Y',
    { apply functor.hext, { rintros ‚ü®‚ü®‚ü©‚ü©; refl }, { rintros ‚ü®‚ü®‚ü©‚ü© ‚ü®j‚ü© ‚ü®‚ü®rfl : _ = j‚ü©‚ü©; simpa } },
    clear_value X' Y', subst this, change binary_cofan X' Y' at c',
    rw H c' _ _ _ (nat_trans.congr_app hŒ± ‚ü®walking_pair.left‚ü©)
      (nat_trans.congr_app hŒ± ‚ü®walking_pair.right‚ü©),
    split, { rintros H ‚ü®‚ü®‚ü©‚ü©, exacts [H.1, H.2] }, { intro H, exact ‚ü®H _, H _‚ü© } }
end

lemma binary_cofan.is_van_kampen_mk {X Y : C} (c : binary_cofan X Y)
  (cofans : ‚àÄ (X Y : C), binary_cofan X Y) (colimits : ‚àÄ X Y, is_colimit (cofans X Y))
  (cones : ‚àÄ {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z), pullback_cone f g)
  (limits : ‚àÄ {X Y Z : C} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z),  is_limit (cones f g))
  (h‚ÇÅ : ‚àÄ {X' Y' : C} (Œ±X : X' ‚ü∂ X) (Œ±Y : Y' ‚ü∂ Y) (f : (cofans X' Y').X ‚ü∂ c.X)
    (hŒ±X : Œ±X ‚â´ c.inl = (cofans X' Y').inl ‚â´ f) (hŒ±Y : Œ±Y ‚â´ c.inr = (cofans X' Y').inr ‚â´ f),
    is_pullback (cofans X' Y').inl Œ±X f c.inl ‚àß is_pullback (cofans X' Y').inr Œ±Y f c.inr)
  (h‚ÇÇ : ‚àÄ {Z : C} (f : Z ‚ü∂ c.X),
    is_colimit (binary_cofan.mk (cones f c.inl).fst (cones f c.inr).fst)) :
  is_van_kampen_colimit c :=
begin
  rw binary_cofan.is_van_kampen_iff,
  introv hX hY,
  split,
  { rintros ‚ü®h‚ü©,
    let e := h.cocone_point_unique_up_to_iso (colimits _ _),
    obtain ‚ü®hl, hr‚ü© := h‚ÇÅ Œ±X Œ±Y (e.inv ‚â´ f) (by simp [hX]) (by simp [hY]),
    split,
    { rw [‚Üê category.id_comp Œ±X, ‚Üê iso.hom_inv_id_assoc e f],
      have : c'.inl ‚â´ e.hom = ùüô X' ‚â´ (cofans X' Y').inl := by { dsimp, simp },
      haveI : is_iso (ùüô X') := infer_instance,
      exact (is_pullback.of_vert_is_iso ‚ü®this‚ü©).paste_vert hl },
    { rw [‚Üê category.id_comp Œ±Y, ‚Üê iso.hom_inv_id_assoc e f],
      have : c'.inr ‚â´ e.hom = ùüô Y' ‚â´ (cofans X' Y').inr := by { dsimp, simp },
      haveI : is_iso (ùüô Y') := infer_instance,
      exact (is_pullback.of_vert_is_iso ‚ü®this‚ü©).paste_vert hr } },
  { rintro ‚ü®H‚ÇÅ, H‚ÇÇ‚ü©,
    refine ‚ü®is_colimit.of_iso_colimit _ $ (iso_binary_cofan_mk _).symm‚ü©,
    let e‚ÇÅ : X' ‚âÖ _ := H‚ÇÅ.is_limit.cone_point_unique_up_to_iso (limits _ _),
    let e‚ÇÇ : Y' ‚âÖ _ := H‚ÇÇ.is_limit.cone_point_unique_up_to_iso (limits _ _),
    have he‚ÇÅ : c'.inl = e‚ÇÅ.hom ‚â´ (cones f c.inl).fst := by simp,
    have he‚ÇÇ : c'.inr = e‚ÇÇ.hom ‚â´ (cones f c.inr).fst := by simp,
    rw [he‚ÇÅ, he‚ÇÇ],
    apply binary_cofan.is_colimit_comp_right_iso (binary_cofan.mk _ _),
    apply binary_cofan.is_colimit_comp_left_iso (binary_cofan.mk _ _),
    exact h‚ÇÇ f }
end
.
lemma binary_cofan.mono_inr_of_is_van_kampen [has_initial C] {X Y : C} {c : binary_cofan X Y}
  (h : is_van_kampen_colimit c) : mono c.inr :=
begin
  refine pullback_cone.mono_of_is_limit_mk_id_id _ (is_pullback.is_limit _),
  refine (h (binary_cofan.mk (initial.to Y) (ùüô Y))
    (map_pair (initial.to X) (ùüô Y)) c.inr _ (map_pair_equifibered _)).mp ‚ü®_‚ü© ‚ü®walking_pair.right‚ü©,
  { ext ‚ü®‚ü®‚ü©‚ü©; dsimp; simp },
  { exact ((binary_cofan.is_colimit_iff_is_iso_inr initial_is_initial _).mpr
      (by { dsimp, apply_instance })).some }
end

lemma finitary_extensive.mono_inr_of_is_colimit [finitary_extensive C]
  {c : binary_cofan X Y} (hc : is_colimit c) : mono c.inr :=
binary_cofan.mono_inr_of_is_van_kampen (finitary_extensive.van_kampen c hc)

lemma finitary_extensive.mono_inl_of_is_colimit [finitary_extensive C]
  {c : binary_cofan X Y} (hc : is_colimit c) : mono c.inl :=
finitary_extensive.mono_inr_of_is_colimit (binary_cofan.is_colimit_flip hc)

instance [finitary_extensive C] (X Y : C) : mono (coprod.inl : X ‚ü∂ X ‚®ø Y) :=
(finitary_extensive.mono_inl_of_is_colimit (coprod_is_coprod X Y) : _)

instance [finitary_extensive C] (X Y : C) : mono (coprod.inr : Y ‚ü∂ X ‚®ø Y) :=
(finitary_extensive.mono_inr_of_is_colimit (coprod_is_coprod X Y) : _)

lemma binary_cofan.is_pullback_initial_to_of_is_van_kampen [has_initial C]
  {c : binary_cofan X Y}
  (h : is_van_kampen_colimit c) : is_pullback (initial.to _) (initial.to _) c.inl c.inr :=
begin
  refine ((h (binary_cofan.mk (initial.to Y) (ùüô Y)) (map_pair (initial.to X) (ùüô Y)) c.inr _
    (map_pair_equifibered _)).mp ‚ü®_‚ü© ‚ü®walking_pair.left‚ü©).flip,
  { ext ‚ü®‚ü®‚ü©‚ü©; dsimp; simp },
  { exact ((binary_cofan.is_colimit_iff_is_iso_inr initial_is_initial _).mpr
      (by { dsimp, apply_instance })).some }
end

lemma finitary_extensive.is_pullback_initial_to_binary_cofan [finitary_extensive C]
  {c : binary_cofan X Y} (hc : is_colimit c) :
  is_pullback (initial.to _) (initial.to _) c.inl c.inr :=
binary_cofan.is_pullback_initial_to_of_is_van_kampen (finitary_extensive.van_kampen c hc)

lemma has_strict_initial_of_is_universal [has_initial C]
  (H : is_universal_colimit (binary_cofan.mk (ùüô (‚ä•_ C)) (ùüô (‚ä•_ C)))) :
  has_strict_initial_objects C :=
has_strict_initial_objects_of_initial_is_strict
begin
  intros A f,
  suffices : is_colimit (binary_cofan.mk (ùüô A) (ùüô A)),
  { obtain ‚ü®l, h‚ÇÅ, h‚ÇÇ‚ü© := limits.binary_cofan.is_colimit.desc' this (f ‚â´ initial.to A) (ùüô A),
    rcases (category.id_comp _).symm.trans h‚ÇÇ with rfl,
    exact ‚ü®‚ü®_, ((category.id_comp _).symm.trans h‚ÇÅ).symm, initial_is_initial.hom_ext _ _‚ü©‚ü© },
  refine (H (binary_cofan.mk (ùüô _) (ùüô _)) (map_pair f f) f (by ext ‚ü®‚ü®‚ü©‚ü©; dsimp; simp)
    (map_pair_equifibered _) _).some,
  rintro ‚ü®‚ü®‚ü©‚ü©; dsimp;
    exact is_pullback.of_horiz_is_iso ‚ü®(category.id_comp _).trans (category.comp_id _).symm‚ü©
end

@[priority 100]
instance has_strict_initial_objects_of_finitary_extensive [finitary_extensive C] :
  has_strict_initial_objects C :=
has_strict_initial_of_is_universal (finitary_extensive.van_kampen _
  ((binary_cofan.is_colimit_iff_is_iso_inr initial_is_initial _).mpr
    (by { dsimp, apply_instance })).some).is_universal

lemma finitary_extensive_iff_of_is_terminal (C : Type u) [category.{v} C] [has_finite_coproducts C]
  (T : C) (HT : is_terminal T) (c‚ÇÄ : binary_cofan T T) (hc‚ÇÄ : is_colimit c‚ÇÄ) :
  finitary_extensive C ‚Üî is_van_kampen_colimit c‚ÇÄ :=
begin
  refine ‚ü®Œª H, H.2 c‚ÇÄ hc‚ÇÄ, Œª H, _‚ü©,
  constructor,
  simp_rw binary_cofan.is_van_kampen_iff at H ‚ä¢,
  intros X Y c hc X' Y' c' Œ±X Œ±Y f hX hY,
  obtain ‚ü®d, hd, hd'‚ü© := limits.binary_cofan.is_colimit.desc' hc
    (HT.from _ ‚â´ c‚ÇÄ.inl) (HT.from _ ‚â´ c‚ÇÄ.inr),
  rw H c' (Œ±X ‚â´ HT.from _) (Œ±Y ‚â´ HT.from _) (f ‚â´ d)
    (by rw [‚Üê reassoc_of hX, hd, category.assoc])
    (by rw [‚Üê reassoc_of hY, hd', category.assoc]),
  obtain ‚ü®hl, hr‚ü© := (H c (HT.from _) (HT.from _) d hd.symm hd'.symm).mp ‚ü®hc‚ü©,
  rw [hl.paste_vert_iff hX.symm, hr.paste_vert_iff hY.symm]
end

instance types.finitary_extensive : finitary_extensive (Type u) :=
begin
  rw [finitary_extensive_iff_of_is_terminal (Type u) punit types.is_terminal_punit _
    (types.binary_coproduct_colimit _ _)],
  apply binary_cofan.is_van_kampen_mk _ _ (Œª X Y, types.binary_coproduct_colimit X Y) _
    (Œª X Y Z f g, (limits.types.pullback_limit_cone f g).2),
  { intros,
    split,
    { refine ‚ü®‚ü®hŒ±X.symm‚ü©, ‚ü®pullback_cone.is_limit_aux' _ _‚ü©‚ü©,
      intro s,
      have : ‚àÄ x, ‚àÉ! y, s.fst x = sum.inl y,
      { intro x,
        cases h : s.fst x,
        { simp_rw sum.inl_injective.eq_iff, exact exists_unique_eq' },
        { apply_fun f at h,
          cases ((congr_fun s.condition x).symm.trans h).trans (congr_fun hŒ±Y val : _).symm } },
      delta exists_unique at this,
      choose l hl hl',
      exact ‚ü®l, (funext hl).symm, types.is_terminal_punit.hom_ext _ _,
        Œª l' h‚ÇÅ h‚ÇÇ, funext $ Œª x, hl' x (l' x) (congr_fun h‚ÇÅ x).symm‚ü© },
    { refine ‚ü®‚ü®hŒ±Y.symm‚ü©, ‚ü®pullback_cone.is_limit_aux' _ _‚ü©‚ü©,
      intro s, dsimp,
      have : ‚àÄ x, ‚àÉ! y, s.fst x = sum.inr y,
      { intro x,
        cases h : s.fst x,
        { apply_fun f at h,
          cases ((congr_fun s.condition x).symm.trans h).trans (congr_fun hŒ±X val : _).symm },
        { simp_rw sum.inr_injective.eq_iff, exact exists_unique_eq' } },
      delta exists_unique at this,
      choose l hl hl',
      exact ‚ü®l, (funext hl).symm, types.is_terminal_punit.hom_ext _ _,
        Œª l' h‚ÇÅ h‚ÇÇ, funext $ Œª x, hl' x (l' x) (congr_fun h‚ÇÅ x).symm‚ü© } },
  { intros Z f,
    dsimp [limits.types.binary_coproduct_cocone],
    delta types.pullback_obj,
    have : ‚àÄ x, f x = sum.inl punit.star ‚à® f x = sum.inr punit.star,
    { intro x, rcases f x with (‚ü®‚ü®‚ü©‚ü©|‚ü®‚ü®‚ü©‚ü©), exacts [or.inl rfl, or.inr rfl] },
    let eX : {p : Z √ó punit // f p.fst = sum.inl p.snd} ‚âÉ {x : Z // f x = sum.inl punit.star } :=
      ‚ü®Œª p, ‚ü®p.1.1, by convert p.2‚ü©, Œª x, ‚ü®‚ü®_, _‚ü©, x.2‚ü©, Œª _, by ext; refl, Œª _, by ext; refl‚ü©,
    let eY : {p : Z √ó punit // f p.fst = sum.inr p.snd} ‚âÉ {x : Z // f x = sum.inr punit.star } :=
      ‚ü®Œª p, ‚ü®p.1.1, p.2.trans (congr_arg sum.inr $ subsingleton.elim _ _)‚ü©,
        Œª x, ‚ü®‚ü®_, _‚ü©, x.2‚ü©, Œª _, by ext; refl, Œª _, by ext; refl‚ü©,
    fapply binary_cofan.is_colimit_mk,
    { exact Œª s x, dite _ (Œª h, s.inl $ eX.symm ‚ü®x, h‚ü©)
        (Œª h, s.inr $ eY.symm ‚ü®x, (this x).resolve_left h‚ü©) },
    { intro s, ext ‚ü®‚ü®x, ‚ü®‚ü©‚ü©, _‚ü©, dsimp, split_ifs; refl },
    { intro s, ext ‚ü®‚ü®x, ‚ü®‚ü©‚ü©, hx‚ü©, dsimp, split_ifs, { cases h.symm.trans hx }, { refl } },
    { intros s m e‚ÇÅ e‚ÇÇ, ext x, split_ifs, { rw ‚Üê e‚ÇÅ, refl }, { rw ‚Üê e‚ÇÇ, refl } } }
end

section Top

/-- (Implementation) An auxiliary lemma for the proof that `Top` is finitary extensive. -/
def finitary_extensive_Top_aux (Z : Top.{u}) (f : Z ‚ü∂ Top.of (punit.{u+1} ‚äï punit.{u+1})) :
  is_colimit (binary_cofan.mk
    (Top.pullback_fst f (Top.binary_cofan (Top.of punit) (Top.of punit)).inl)
    (Top.pullback_fst f (Top.binary_cofan (Top.of punit) (Top.of punit)).inr)) :=
begin
  have : ‚àÄ x, f x = sum.inl punit.star ‚à® f x = sum.inr punit.star,
  { intro x, rcases f x with (‚ü®‚ü®‚ü©‚ü©|‚ü®‚ü®‚ü©‚ü©), exacts [or.inl rfl, or.inr rfl] },
  let eX : {p : Z √ó punit // f p.fst = sum.inl p.snd} ‚âÉ { x : Z // f x = sum.inl punit.star } :=
    ‚ü®Œª p, ‚ü®p.1.1, p.2.trans (congr_arg sum.inl $ subsingleton.elim _ _)‚ü©,
      Œª x, ‚ü®‚ü®_, _‚ü©, x.2‚ü©, Œª _, by ext; refl, Œª _, by ext; refl‚ü©,
  let eY : {p : Z √ó punit // f p.fst = sum.inr p.snd} ‚âÉ { x : Z // f x = sum.inr punit.star } :=
    ‚ü®Œª p, ‚ü®p.1.1, p.2.trans (congr_arg sum.inr $ subsingleton.elim _ _)‚ü©,
      Œª x, ‚ü®‚ü®_, _‚ü©, x.2‚ü©, Œª _, by ext; refl, Œª _, by ext; refl‚ü©,
  fapply binary_cofan.is_colimit_mk,
  { refine Œª s, ‚ü®Œª x, dite _ (Œª h, s.inl $ eX.symm ‚ü®x, h‚ü©)
      (Œª h, s.inr $ eY.symm ‚ü®x, (this x).resolve_left h‚ü©), _‚ü©,
    rw continuous_iff_continuous_at,
    intro x,
    by_cases f x = sum.inl punit.star,
    { revert h x,
      apply (is_open.continuous_on_iff _).mp,
      { rw continuous_on_iff_continuous_restrict,
        convert_to continuous (Œª x : {x|f x = sum.inl punit.star}, s.inl ‚ü®(x, punit.star), x.2‚ü©),
        { ext ‚ü®x, hx‚ü©, exact dif_pos hx },
        continuity },
      { convert f.2.1 _ (open_embedding_inl).open_range, ext x, exact ‚ü®Œª h, ‚ü®_, h.symm‚ü©,
          Œª ‚ü®e, h‚ü©, h.symm.trans (congr_arg sum.inl $ subsingleton.elim _ _)‚ü© } },
    { revert h x,
      apply (is_open.continuous_on_iff _).mp,
      { rw continuous_on_iff_continuous_restrict,
        convert_to continuous (Œª x : {x|f x ‚â† sum.inl punit.star},
          s.inr ‚ü®(x, punit.star), (this _).resolve_left x.2‚ü©),
        { ext ‚ü®x, hx‚ü©, exact dif_neg hx },
        continuity },
      { convert f.2.1 _ (open_embedding_inr).open_range, ext x,
        change f x ‚â† sum.inl punit.star ‚Üî f x ‚àà set.range sum.inr,
        transitivity f x = sum.inr punit.star,
        { rcases f x with (‚ü®‚ü®‚ü©‚ü©|‚ü®‚ü®‚ü©‚ü©);
            simp only [iff_self, eq_self_iff_true, not_true, ne.def, not_false_iff] },
        { exact ‚ü®Œª h, ‚ü®_, h.symm‚ü©, Œª ‚ü®e, h‚ü©,
            h.symm.trans (congr_arg sum.inr $ subsingleton.elim _ _)‚ü© } } } },
  { intro s, ext ‚ü®‚ü®x, ‚ü®‚ü©‚ü©, _‚ü©, change dite _ _ _ = _, split_ifs; refl },
  { intro s, ext ‚ü®‚ü®x, ‚ü®‚ü©‚ü©, hx‚ü©, change dite _ _ _ = _,
    split_ifs, { cases h.symm.trans hx }, { refl } },
  { intros s m e‚ÇÅ e‚ÇÇ, ext x, change m x = dite _ _ _,
    split_ifs, { rw ‚Üê e‚ÇÅ, refl }, { rw ‚Üê e‚ÇÇ, refl } }
end

instance : finitary_extensive Top.{u} :=
begin
  rw [finitary_extensive_iff_of_is_terminal Top.{u} _ Top.is_terminal_punit _
    (Top.binary_cofan_is_colimit _ _)],
  apply binary_cofan.is_van_kampen_mk _ _ (Œª X Y, Top.binary_cofan_is_colimit X Y) _
    (Œª X Y Z f g, Top.pullback_cone_is_limit f g),
  { intros,
    split,
    { refine ‚ü®‚ü®hŒ±X.symm‚ü©, ‚ü®pullback_cone.is_limit_aux' _ _‚ü©‚ü©,
      intro s,
      have : ‚àÄ x, ‚àÉ! y, s.fst x = sum.inl y,
      { intro x,
        cases h : s.fst x,
        { simp_rw sum.inl_injective.eq_iff, exact exists_unique_eq' },
        { apply_fun f at h,
          cases ((concrete_category.congr_hom s.condition x).symm.trans h).trans
            (concrete_category.congr_hom hŒ±Y val : _).symm } },
      delta exists_unique at this,
      choose l hl hl',
      refine ‚ü®‚ü®l, _‚ü©, continuous_map.ext (Œª a, (hl a).symm), Top.is_terminal_punit.hom_ext _ _,
        Œª l' h‚ÇÅ h‚ÇÇ, continuous_map.ext $ Œª x,
          hl' x (l' x) (concrete_category.congr_hom h‚ÇÅ x).symm‚ü©,
      apply embedding_inl.to_inducing.continuous_iff.mpr,
      convert s.fst.2 using 1, exact (funext hl).symm },
    { refine ‚ü®‚ü®hŒ±Y.symm‚ü©, ‚ü®pullback_cone.is_limit_aux' _ _‚ü©‚ü©,
      intro s, dsimp,
      have : ‚àÄ x, ‚àÉ! y, s.fst x = sum.inr y,
      { intro x,
        cases h : s.fst x,
        { apply_fun f at h,
          cases ((concrete_category.congr_hom s.condition x).symm.trans h).trans
            (concrete_category.congr_hom hŒ±X val : _).symm },
        { simp_rw sum.inr_injective.eq_iff, exact exists_unique_eq' } },
      delta exists_unique at this,
      choose l hl hl',
      refine ‚ü®‚ü®l, _‚ü©, continuous_map.ext (Œª a, (hl a).symm), Top.is_terminal_punit.hom_ext _ _,
        Œª l' h‚ÇÅ h‚ÇÇ, continuous_map.ext $
          Œª x, hl' x (l' x) (concrete_category.congr_hom h‚ÇÅ x).symm‚ü©,
      apply embedding_inr.to_inducing.continuous_iff.mpr,
      convert s.fst.2 using 1, exact (funext hl).symm } },
  { intros Z f, exact finitary_extensive_Top_aux Z f }
end

end Top

section functor

universes v'' u''

variables {D : Type u''} [category.{v''} D]

lemma nat_trans.equifibered.whisker_right {F G : J ‚•§ C} {Œ± : F ‚ü∂ G} (hŒ± : Œ±.equifibered)
  (H : C ‚•§ D) [preserves_limits_of_shape walking_cospan H] : (whisker_right Œ± H).equifibered :=
Œª i j f, (hŒ± f).map H

lemma is_van_kampen_colimit.of_iso {F : J ‚•§ C} {c c' : cocone F} (H : is_van_kampen_colimit c)
  (e : c ‚âÖ c') : is_van_kampen_colimit c' :=
begin
  intros F' c'' Œ± f h hŒ±,
  have : c'.Œπ ‚â´ (functor.const J).map e.inv.hom = c.Œπ,
  { ext j, exact e.inv.2 j },
  rw H c'' Œ± (f ‚â´ e.inv.1) (by rw [functor.map_comp, ‚Üê reassoc_of h, this]) hŒ±,
  apply forall_congr,
  intro j,
  conv_lhs { rw [‚Üê category.comp_id (Œ±.app j)] },
  haveI : is_iso e.inv.hom := functor.map_is_iso (cocones.forget _) e.inv,
  exact (is_pullback.of_vert_is_iso ‚ü®by simp‚ü©).paste_vert_iff (nat_trans.congr_app h j).symm
end

lemma is_van_kampen_colimit.of_map {D : Type*} [category D] (G : C ‚•§ D) {F : J ‚•§ C} {c : cocone F}
  [preserves_limits_of_shape walking_cospan G] [reflects_limits_of_shape walking_cospan G]
    [preserves_colimits_of_shape J G] [reflects_colimits_of_shape J G]
   (H : is_van_kampen_colimit (G.map_cocone c)) : is_van_kampen_colimit c :=
begin
  intros F' c' Œ± f h hŒ±,
  refine (iff.trans _ (H (G.map_cocone c') (whisker_right Œ± G) (G.map f)
    (by { ext j, simpa using G.congr_map (nat_trans.congr_app h j) })
    (hŒ±.whisker_right G))).trans (forall_congr $ Œª j, _),
  { exact ‚ü®Œª h, ‚ü®is_colimit_of_preserves G h.some‚ü©, Œª h, ‚ü®is_colimit_of_reflects G h.some‚ü©‚ü© },
  { exact is_pullback.map_iff G (nat_trans.congr_app h.symm j) }
end

lemma is_van_kampen_colimit_of_evaluation [has_pullbacks D] [has_colimits_of_shape J D]
  (F : J ‚•§ C ‚•§ D) (c : cocone F)
  (hc : ‚àÄ x : C, is_van_kampen_colimit (((evaluation C D).obj x).map_cocone c)) :
  is_van_kampen_colimit c :=
begin
  intros F' c' Œ± f e hŒ±,
  have := Œª x, hc x (((evaluation C D).obj x).map_cocone c') (whisker_right Œ± _)
    (((evaluation C D).obj x).map f)
    (by { ext y, dsimp, exact nat_trans.congr_app (nat_trans.congr_app e y) x })
    (hŒ±.whisker_right _),
  split,
  { rintros ‚ü®hc'‚ü© j,
    refine ‚ü®‚ü®(nat_trans.congr_app e j).symm‚ü©, ‚ü®evaluation_jointly_reflects_limits _ _‚ü©‚ü©,
    refine Œª x, (is_limit_map_cone_pullback_cone_equiv _ _).symm _,
    exact ((this x).mp ‚ü®preserves_colimit.preserves hc'‚ü© _).is_limit },
  { exact Œª H, ‚ü®evaluation_jointly_reflects_colimits _
      (Œª x, ((this x).mpr (Œª j, (H j).map ((evaluation C D).obj x))).some)‚ü© }
end

instance [has_pullbacks C] [finitary_extensive C] : finitary_extensive (D ‚•§ C) :=
begin
  haveI : has_finite_coproducts (D ‚•§ C) := ‚ü®Œª n, limits.functor_category_has_colimits_of_shape‚ü©,
  exact ‚ü®Œª X Y c hc, is_van_kampen_colimit_of_evaluation _ c
    (Œª x, finitary_extensive.van_kampen _ $ preserves_colimit.preserves hc)‚ü©
end

lemma finitary_extensive_of_preserves_and_reflects (F : C ‚•§ D)
  [finitary_extensive D] [has_finite_coproducts C]
    [preserves_limits_of_shape walking_cospan F]
    [reflects_limits_of_shape walking_cospan F]
    [preserves_colimits_of_shape (discrete walking_pair) F]
    [reflects_colimits_of_shape (discrete walking_pair) F] :
  finitary_extensive C :=
‚ü®Œª X Y c hc, (finitary_extensive.van_kampen _ (is_colimit_of_preserves F hc)).of_map F‚ü©

lemma finitary_extensive_of_preserves_and_reflects_isomorphism (F : C ‚•§ D)
  [finitary_extensive D] [has_finite_coproducts C] [has_pullbacks C]
    [preserves_limits_of_shape walking_cospan F]
    [preserves_colimits_of_shape (discrete walking_pair) F]
    [reflects_isomorphisms F] :
  finitary_extensive C :=
begin
  haveI : reflects_limits_of_shape walking_cospan F :=
    reflects_limits_of_shape_of_reflects_isomorphisms,
  haveI : reflects_colimits_of_shape (discrete walking_pair) F :=
    reflects_colimits_of_shape_of_reflects_isomorphisms,
  exact finitary_extensive_of_preserves_and_reflects F,
end

end functor

end extensive

end category_theory
