/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import category_theory.limits.shapes.comm_sq
import category_theory.limits.shapes.strict_initial
import category_theory.limits.shapes.types
import topology.category.Top.limits
import category_theory.limits.functor_category
import category_theory.limits.preserves.finite
import category_theory.limits.preserves.shapes.pullbacks
/-!

# Extensive categories

## Main definitions
- `category_theory.is_van_kampen_colimit`: A (colimit) cocone over a diagram `F : J â¥¤ C` is van
  Kampen if for every cocone `c'` over the pullback of the diagram `F' : J â¥¤ C'`,
  `c'` is colimiting iff `c'` is the pullback of `c`.
- `category_theory.finitary_extensive`: A category is (finitary) extensive if it has finite
  coproducts, and binary coproducts are van Kampen.

## Main Results
- `category_theory.has_strict_initial_objects_of_finitary_extensive`: The initial object
  in extensive categories is strict.
- `category_theory.finitary_extensive.mono_inr_of_is_colimit`: Coproduct injections are monic in
  extensive categories.
- `category_theory.binary_cofan.is_pullback_initial_to_of_is_van_kampen`: In extensive categories,
  sums are disjoint, i.e. the pullback of `X âŸ¶ X â¨¿ Y` and `Y âŸ¶ X â¨¿ Y` is the initial object.
- `category_theory.types.finitary_extensive`: The category of types is extensive.

## TODO

Show that the following are finitary extensive:
- the categories of sheaves over a site
- `Scheme`
- `AffineScheme` (`CommRingáµ’áµ–`)

## References
- https://ncatlab.org/nlab/show/extensive+category
- [Carboni et al, Introduction to extensive and distributive categories][CARBONI1993145]

-/

open category_theory.limits

namespace category_theory

universes v' u' v u v'' u''

variables {J : Type v'} [category.{u'} J] {C : Type u} [category.{v} C]
variables {D : Type u''} [category.{v''} D]


/-- A natural transformation is equifibered if every commutative square of the following form is
a pullback.
```
F(X) â†’ F(Y)
 â†“      â†“
G(X) â†’ G(Y)
```
-/
def nat_trans.equifibered {F G : J â¥¤ C} (Î± : F âŸ¶ G) : Prop :=
âˆ€ â¦ƒi j : Jâ¦„ (f : i âŸ¶ j), is_pullback (F.map f) (Î±.app i) (Î±.app j) (G.map f)

lemma nat_trans.equifibered_of_is_iso {F G : J â¥¤ C} (Î± : F âŸ¶ G) [is_iso Î±] : Î±.equifibered :=
Î» _ _ f, is_pullback.of_vert_is_iso âŸ¨nat_trans.naturality _ fâŸ©

lemma nat_trans.equifibered.comp {F G H : J â¥¤ C} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H}
  (hÎ± : Î±.equifibered) (hÎ² : Î².equifibered) : (Î± â‰« Î²).equifibered :=
Î» i j f, (hÎ± f).paste_vert (hÎ² f)

/-- A (colimit) cocone over a diagram `F : J â¥¤ C` is universal if it is stable under pullbacks. -/
def is_universal_colimit {F : J â¥¤ C} (c : cocone F) : Prop :=
âˆ€ â¦ƒF' : J â¥¤ Câ¦„ (c' : cocone F') (Î± : F' âŸ¶ F) (f : c'.X âŸ¶ c.X)
  (h : Î± â‰« c.Î¹ = c'.Î¹ â‰« (functor.const J).map f) (hÎ± : Î±.equifibered),
  (âˆ€ j : J, is_pullback (c'.Î¹.app j) (Î±.app j) f (c.Î¹.app j)) â†’ nonempty (is_colimit c')

/-- A (colimit) cocone over a diagram `F : J â¥¤ C` is van Kampen if for every cocone `c'` over the
pullback of the diagram `F' : J â¥¤ C'`, `c'` is colimiting iff `c'` is the pullback of `c`.

TODO: Show that this is iff the functor `C â¥¤ Catáµ’áµ–` sending `x` to `C/x` preserves it.
TODO: Show that this is iff the inclusion functor `C â¥¤ Span(C)` preserves it.
-/
def is_van_kampen_colimit {F : J â¥¤ C} (c : cocone F) : Prop :=
âˆ€ â¦ƒF' : J â¥¤ Câ¦„ (c' : cocone F') (Î± : F' âŸ¶ F) (f : c'.X âŸ¶ c.X)
  (h : Î± â‰« c.Î¹ = c'.Î¹ â‰« (functor.const J).map f) (hÎ± : Î±.equifibered),
  nonempty (is_colimit c') â†” âˆ€ j : J, is_pullback (c'.Î¹.app j) (Î±.app j) f (c.Î¹.app j)

lemma is_van_kampen_colimit.is_universal {F : J â¥¤ C} {c : cocone F} (H : is_van_kampen_colimit c) :
  is_universal_colimit c :=
Î» _ c' Î± f h hÎ±, (H c' Î± f h hÎ±).mpr

/-- A van Kampen colimit is a colimit. -/
noncomputable
def is_van_kampen_colimit.is_colimit {F : J â¥¤ C} {c : cocone F} (h : is_van_kampen_colimit c) :
  is_colimit c :=
begin
  refine ((h c (ğŸ™ F) (ğŸ™ c.X : _) (by rw [functor.map_id, category.comp_id, category.id_comp])
    (nat_trans.equifibered_of_is_iso _)).mpr $ Î» j, _).some,
  haveI : is_iso (ğŸ™ c.X) := infer_instance,
  exact is_pullback.of_vert_is_iso âŸ¨by erw [nat_trans.id_app, category.comp_id, category.id_comp]âŸ©,
end

lemma is_initial.is_van_kampen_colimit [has_strict_initial_objects C] {X : C} (h : is_initial X) :
  is_van_kampen_colimit (as_empty_cocone X) :=
begin
  intros F' c' Î± f hf hÎ±,
  have : F' = functor.empty C := by apply functor.hext; rintro âŸ¨âŸ¨âŸ©âŸ©,
  subst this,
  haveI := h.is_iso_to f,
  refine âŸ¨by rintro _ âŸ¨âŸ¨âŸ©âŸ©, Î» _,
    âŸ¨is_colimit.of_iso_colimit h (cocones.ext (as_iso f).symm $ by rintro âŸ¨âŸ¨âŸ©âŸ©)âŸ©âŸ©
end

lemma nat_trans.equifibered.whisker_right {F G : J â¥¤ C} {Î± : F âŸ¶ G} (hÎ± : Î±.equifibered)
  (H : C â¥¤ D) [preserves_limits_of_shape walking_cospan H] : (whisker_right Î± H).equifibered :=
Î» i j f, (hÎ± f).map H

lemma is_van_kampen_colimit.of_iso {F : J â¥¤ C} {c c' : cocone F} (H : is_van_kampen_colimit c)
  (e : c â‰… c') : is_van_kampen_colimit c' :=
begin
  intros F' c'' Î± f h hÎ±,
  have : c'.Î¹ â‰« (functor.const J).map e.inv.hom = c.Î¹,
  { ext j, exact e.inv.2 j },
  rw H c'' Î± (f â‰« e.inv.1) (by rw [functor.map_comp, â† reassoc_of h, this]) hÎ±,
  apply forall_congr,
  intro j,
  conv_lhs { rw [â† category.comp_id (Î±.app j)] },
  haveI : is_iso e.inv.hom := functor.map_is_iso (cocones.forget _) e.inv,
  exact (is_pullback.of_vert_is_iso âŸ¨by simpâŸ©).paste_vert_iff (nat_trans.congr_app h j).symm
end

lemma is_van_kampen_colimit.precompose_iso {F G : C â¥¤ D} {c : cocone G}
  (h : is_van_kampen_colimit c)
  (Î· : F âŸ¶ G) [is_iso Î·] : is_van_kampen_colimit ((cocones.precompose Î·).obj c) :=
begin
  intros F' c' Î± f e hÎ±,
  rw h c' (Î± â‰« Î·) f ((category.assoc _ _ _).trans e) (hÎ±.comp $ nat_trans.equifibered_of_is_iso Î·),
  apply forall_congr (Î» j, _),
  conv_lhs { rw â† category.comp_id f },
  refine is_pullback.paste_vert_iff _ _,
  { exact is_pullback.of_vert_is_iso âŸ¨category.comp_id _âŸ© },
  { exact nat_trans.congr_app e.symm j }
end

lemma is_van_kampen_colimit.of_precompose_iso {F G : C â¥¤ D} (c : cocone G)
  (Î· : F âŸ¶ G) [is_iso Î·]  (h : is_van_kampen_colimit ((cocones.precompose Î·).obj c))
  : is_van_kampen_colimit c :=
begin
  apply is_van_kampen_colimit.of_iso (h.precompose_iso $ inv Î·),
  exact cocones.ext (iso.refl c.X) (Î» j, by { dsimp, simp })
end

section extensive

variables {X Y : C}

/--
A category is (finitary) extensive if it has finite coproducts,
and binary coproducts are van Kampen.

TODO: Show that this is iff all finite coproducts are van Kampen. -/
class finitary_extensive (C : Type u) [category.{v} C] : Prop :=
[has_finite_coproducts : has_finite_coproducts C]
(van_kampen' : âˆ€ {X Y : C} (c : binary_cofan X Y), is_colimit c â†’ is_van_kampen_colimit c)

attribute [priority 100, instance] finitary_extensive.has_finite_coproducts

lemma finitary_extensive.van_kampen [finitary_extensive C] {F : discrete walking_pair â¥¤ C}
  (c : cocone F) (hc : is_colimit c) : is_van_kampen_colimit c :=
begin
  let X := F.obj âŸ¨walking_pair.leftâŸ©, let Y := F.obj âŸ¨walking_pair.rightâŸ©,
  have : F = pair X Y,
  { apply functor.hext, { rintros âŸ¨âŸ¨âŸ©âŸ©; refl }, { rintros âŸ¨âŸ¨âŸ©âŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : _ = jâŸ©âŸ©; simpa } },
  clear_value X Y, subst this,
  exact finitary_extensive.van_kampen' c hc
end

lemma map_pair_equifibered {F F' : discrete walking_pair â¥¤ C} (Î± : F âŸ¶ F') : Î±.equifibered :=
begin
  rintros âŸ¨âŸ¨âŸ©âŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : _ = jâŸ©âŸ©,
  all_goals { dsimp, simp only [discrete.functor_map_id],
    exact is_pullback.of_horiz_is_iso âŸ¨by simp only [category.comp_id, category.id_comp]âŸ© }
end

lemma binary_cofan.is_van_kampen_iff (c : binary_cofan X Y) :
  is_van_kampen_colimit c â†”
  âˆ€ {X' Y' : C} (c' : binary_cofan X' Y') (Î±X : X' âŸ¶ X) (Î±Y : Y' âŸ¶ Y)
    (f : c'.X âŸ¶ c.X) (hÎ±X : Î±X â‰« c.inl = c'.inl â‰« f) (hÎ±Y : Î±Y â‰« c.inr = c'.inr â‰« f),
    nonempty (is_colimit c') â†” is_pullback c'.inl Î±X f c.inl âˆ§ is_pullback c'.inr Î±Y f c.inr :=
begin
  split,
  { introv H hÎ±X hÎ±Y,
    rw H c' (map_pair Î±X Î±Y) f (by ext âŸ¨âŸ¨âŸ©âŸ©; dsimp; assumption) (map_pair_equifibered _),
    split, { intro H, exact âŸ¨H _, H _âŸ© }, { rintros H âŸ¨âŸ¨âŸ©âŸ©, exacts [H.1, H.2] } },
  { introv H F' hÎ± h,
    let X' := F'.obj âŸ¨walking_pair.leftâŸ©, let Y' := F'.obj âŸ¨walking_pair.rightâŸ©,
    have : F' = pair X' Y',
    { apply functor.hext, { rintros âŸ¨âŸ¨âŸ©âŸ©; refl }, { rintros âŸ¨âŸ¨âŸ©âŸ© âŸ¨jâŸ© âŸ¨âŸ¨rfl : _ = jâŸ©âŸ©; simpa } },
    clear_value X' Y', subst this, change binary_cofan X' Y' at c',
    rw H c' _ _ _ (nat_trans.congr_app hÎ± âŸ¨walking_pair.leftâŸ©)
      (nat_trans.congr_app hÎ± âŸ¨walking_pair.rightâŸ©),
    split, { rintros H âŸ¨âŸ¨âŸ©âŸ©, exacts [H.1, H.2] }, { intro H, exact âŸ¨H _, H _âŸ© } }
end

lemma binary_cofan.is_van_kampen_mk {X Y : C} (c : binary_cofan X Y)
  (cofans : âˆ€ (X Y : C), binary_cofan X Y) (colimits : âˆ€ X Y, is_colimit (cofans X Y))
  (cones : âˆ€ {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z), pullback_cone f g)
  (limits : âˆ€ {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z),  is_limit (cones f g))
  (hâ‚ : âˆ€ {X' Y' : C} (Î±X : X' âŸ¶ X) (Î±Y : Y' âŸ¶ Y) (f : (cofans X' Y').X âŸ¶ c.X)
    (hÎ±X : Î±X â‰« c.inl = (cofans X' Y').inl â‰« f) (hÎ±Y : Î±Y â‰« c.inr = (cofans X' Y').inr â‰« f),
    is_pullback (cofans X' Y').inl Î±X f c.inl âˆ§ is_pullback (cofans X' Y').inr Î±Y f c.inr)
  (hâ‚‚ : âˆ€ {Z : C} (f : Z âŸ¶ c.X),
    is_colimit (binary_cofan.mk (cones f c.inl).fst (cones f c.inr).fst)) :
  is_van_kampen_colimit c :=
begin
  rw binary_cofan.is_van_kampen_iff,
  introv hX hY,
  split,
  { rintros âŸ¨hâŸ©,
    let e := h.cocone_point_unique_up_to_iso (colimits _ _),
    obtain âŸ¨hl, hrâŸ© := hâ‚ Î±X Î±Y (e.inv â‰« f) (by simp [hX]) (by simp [hY]),
    split,
    { rw [â† category.id_comp Î±X, â† iso.hom_inv_id_assoc e f],
      have : c'.inl â‰« e.hom = ğŸ™ X' â‰« (cofans X' Y').inl := by { dsimp, simp },
      haveI : is_iso (ğŸ™ X') := infer_instance,
      exact (is_pullback.of_vert_is_iso âŸ¨thisâŸ©).paste_vert hl },
    { rw [â† category.id_comp Î±Y, â† iso.hom_inv_id_assoc e f],
      have : c'.inr â‰« e.hom = ğŸ™ Y' â‰« (cofans X' Y').inr := by { dsimp, simp },
      haveI : is_iso (ğŸ™ Y') := infer_instance,
      exact (is_pullback.of_vert_is_iso âŸ¨thisâŸ©).paste_vert hr } },
  { rintro âŸ¨Hâ‚, Hâ‚‚âŸ©,
    refine âŸ¨is_colimit.of_iso_colimit _ $ (iso_binary_cofan_mk _).symmâŸ©,
    let eâ‚ : X' â‰… _ := Hâ‚.is_limit.cone_point_unique_up_to_iso (limits _ _),
    let eâ‚‚ : Y' â‰… _ := Hâ‚‚.is_limit.cone_point_unique_up_to_iso (limits _ _),
    have heâ‚ : c'.inl = eâ‚.hom â‰« (cones f c.inl).fst := by simp,
    have heâ‚‚ : c'.inr = eâ‚‚.hom â‰« (cones f c.inr).fst := by simp,
    rw [heâ‚, heâ‚‚],
    apply binary_cofan.is_colimit_comp_right_iso (binary_cofan.mk _ _),
    apply binary_cofan.is_colimit_comp_left_iso (binary_cofan.mk _ _),
    exact hâ‚‚ f }
end
.
lemma binary_cofan.mono_inr_of_is_van_kampen [has_initial C] {X Y : C} {c : binary_cofan X Y}
  (h : is_van_kampen_colimit c) : mono c.inr :=
begin
  refine pullback_cone.mono_of_is_limit_mk_id_id _ (is_pullback.is_limit _),
  refine (h (binary_cofan.mk (initial.to Y) (ğŸ™ Y))
    (map_pair (initial.to X) (ğŸ™ Y)) c.inr _ (map_pair_equifibered _)).mp âŸ¨_âŸ© âŸ¨walking_pair.rightâŸ©,
  { ext âŸ¨âŸ¨âŸ©âŸ©; dsimp; simp },
  { exact ((binary_cofan.is_colimit_iff_is_iso_inr initial_is_initial _).mpr
      (by { dsimp, apply_instance })).some }
end

lemma finitary_extensive.mono_inr_of_is_colimit [finitary_extensive C]
  {c : binary_cofan X Y} (hc : is_colimit c) : mono c.inr :=
binary_cofan.mono_inr_of_is_van_kampen (finitary_extensive.van_kampen c hc)

lemma finitary_extensive.mono_inl_of_is_colimit [finitary_extensive C]
  {c : binary_cofan X Y} (hc : is_colimit c) : mono c.inl :=
finitary_extensive.mono_inr_of_is_colimit (binary_cofan.is_colimit_flip hc)

instance [finitary_extensive C] (X Y : C) : mono (coprod.inl : X âŸ¶ X â¨¿ Y) :=
(finitary_extensive.mono_inl_of_is_colimit (coprod_is_coprod X Y) : _)

instance [finitary_extensive C] (X Y : C) : mono (coprod.inr : Y âŸ¶ X â¨¿ Y) :=
(finitary_extensive.mono_inr_of_is_colimit (coprod_is_coprod X Y) : _)

lemma binary_cofan.is_pullback_initial_to_of_is_van_kampen [has_initial C]
  {c : binary_cofan X Y}
  (h : is_van_kampen_colimit c) : is_pullback (initial.to _) (initial.to _) c.inl c.inr :=
begin
  refine ((h (binary_cofan.mk (initial.to Y) (ğŸ™ Y)) (map_pair (initial.to X) (ğŸ™ Y)) c.inr _
    (map_pair_equifibered _)).mp âŸ¨_âŸ© âŸ¨walking_pair.leftâŸ©).flip,
  { ext âŸ¨âŸ¨âŸ©âŸ©; dsimp; simp },
  { exact ((binary_cofan.is_colimit_iff_is_iso_inr initial_is_initial _).mpr
      (by { dsimp, apply_instance })).some }
end

lemma finitary_extensive.is_pullback_initial_to_binary_cofan [finitary_extensive C]
  {c : binary_cofan X Y} (hc : is_colimit c) :
  is_pullback (initial.to _) (initial.to _) c.inl c.inr :=
binary_cofan.is_pullback_initial_to_of_is_van_kampen (finitary_extensive.van_kampen c hc)

lemma has_strict_initial_of_is_universal [has_initial C]
  (H : is_universal_colimit (binary_cofan.mk (ğŸ™ (âŠ¥_ C)) (ğŸ™ (âŠ¥_ C)))) :
  has_strict_initial_objects C :=
has_strict_initial_objects_of_initial_is_strict
begin
  intros A f,
  suffices : is_colimit (binary_cofan.mk (ğŸ™ A) (ğŸ™ A)),
  { obtain âŸ¨l, hâ‚, hâ‚‚âŸ© := limits.binary_cofan.is_colimit.desc' this (f â‰« initial.to A) (ğŸ™ A),
    rcases (category.id_comp _).symm.trans hâ‚‚ with rfl,
    exact âŸ¨âŸ¨_, ((category.id_comp _).symm.trans hâ‚).symm, initial_is_initial.hom_ext _ _âŸ©âŸ© },
  refine (H (binary_cofan.mk (ğŸ™ _) (ğŸ™ _)) (map_pair f f) f (by ext âŸ¨âŸ¨âŸ©âŸ©; dsimp; simp)
    (map_pair_equifibered _) _).some,
  rintro âŸ¨âŸ¨âŸ©âŸ©; dsimp;
    exact is_pullback.of_horiz_is_iso âŸ¨(category.id_comp _).trans (category.comp_id _).symmâŸ©
end

@[priority 100]
instance has_strict_initial_objects_of_finitary_extensive [finitary_extensive C] :
  has_strict_initial_objects C :=
has_strict_initial_of_is_universal (finitary_extensive.van_kampen _
  ((binary_cofan.is_colimit_iff_is_iso_inr initial_is_initial _).mpr
    (by { dsimp, apply_instance })).some).is_universal

lemma finitary_extensive_iff_of_is_terminal (C : Type u) [category.{v} C] [has_finite_coproducts C]
  (T : C) (HT : is_terminal T) (câ‚€ : binary_cofan T T) (hcâ‚€ : is_colimit câ‚€) :
  finitary_extensive C â†” is_van_kampen_colimit câ‚€ :=
begin
  refine âŸ¨Î» H, H.2 câ‚€ hcâ‚€, Î» H, _âŸ©,
  constructor,
  simp_rw binary_cofan.is_van_kampen_iff at H âŠ¢,
  intros X Y c hc X' Y' c' Î±X Î±Y f hX hY,
  obtain âŸ¨d, hd, hd'âŸ© := limits.binary_cofan.is_colimit.desc' hc
    (HT.from _ â‰« câ‚€.inl) (HT.from _ â‰« câ‚€.inr),
  rw H c' (Î±X â‰« HT.from _) (Î±Y â‰« HT.from _) (f â‰« d)
    (by rw [â† reassoc_of hX, hd, category.assoc])
    (by rw [â† reassoc_of hY, hd', category.assoc]),
  obtain âŸ¨hl, hrâŸ© := (H c (HT.from _) (HT.from _) d hd.symm hd'.symm).mp âŸ¨hcâŸ©,
  rw [hl.paste_vert_iff hX.symm, hr.paste_vert_iff hY.symm]
end

instance types.finitary_extensive : finitary_extensive (Type u) :=
begin
  rw [finitary_extensive_iff_of_is_terminal (Type u) punit types.is_terminal_punit _
    (types.binary_coproduct_colimit _ _)],
  apply binary_cofan.is_van_kampen_mk _ _ (Î» X Y, types.binary_coproduct_colimit X Y) _
    (Î» X Y Z f g, (limits.types.pullback_limit_cone f g).2),
  { intros,
    split,
    { refine âŸ¨âŸ¨hÎ±X.symmâŸ©, âŸ¨pullback_cone.is_limit_aux' _ _âŸ©âŸ©,
      intro s,
      have : âˆ€ x, âˆƒ! y, s.fst x = sum.inl y,
      { intro x,
        cases h : s.fst x,
        { simp_rw sum.inl_injective.eq_iff, exact exists_unique_eq' },
        { apply_fun f at h,
          cases ((congr_fun s.condition x).symm.trans h).trans (congr_fun hÎ±Y val : _).symm } },
      delta exists_unique at this,
      choose l hl hl',
      exact âŸ¨l, (funext hl).symm, types.is_terminal_punit.hom_ext _ _,
        Î» l' hâ‚ hâ‚‚, funext $ Î» x, hl' x (l' x) (congr_fun hâ‚ x).symmâŸ© },
    { refine âŸ¨âŸ¨hÎ±Y.symmâŸ©, âŸ¨pullback_cone.is_limit_aux' _ _âŸ©âŸ©,
      intro s, dsimp,
      have : âˆ€ x, âˆƒ! y, s.fst x = sum.inr y,
      { intro x,
        cases h : s.fst x,
        { apply_fun f at h,
          cases ((congr_fun s.condition x).symm.trans h).trans (congr_fun hÎ±X val : _).symm },
        { simp_rw sum.inr_injective.eq_iff, exact exists_unique_eq' } },
      delta exists_unique at this,
      choose l hl hl',
      exact âŸ¨l, (funext hl).symm, types.is_terminal_punit.hom_ext _ _,
        Î» l' hâ‚ hâ‚‚, funext $ Î» x, hl' x (l' x) (congr_fun hâ‚ x).symmâŸ© } },
  { intros Z f,
    dsimp [limits.types.binary_coproduct_cocone],
    delta types.pullback_obj,
    have : âˆ€ x, f x = sum.inl punit.star âˆ¨ f x = sum.inr punit.star,
    { intro x, rcases f x with (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨âŸ¨âŸ©âŸ©), exacts [or.inl rfl, or.inr rfl] },
    let eX : {p : Z Ã— punit // f p.fst = sum.inl p.snd} â‰ƒ {x : Z // f x = sum.inl punit.star } :=
      âŸ¨Î» p, âŸ¨p.1.1, by convert p.2âŸ©, Î» x, âŸ¨âŸ¨_, _âŸ©, x.2âŸ©, Î» _, by ext; refl, Î» _, by ext; reflâŸ©,
    let eY : {p : Z Ã— punit // f p.fst = sum.inr p.snd} â‰ƒ {x : Z // f x = sum.inr punit.star } :=
      âŸ¨Î» p, âŸ¨p.1.1, p.2.trans (congr_arg sum.inr $ subsingleton.elim _ _)âŸ©,
        Î» x, âŸ¨âŸ¨_, _âŸ©, x.2âŸ©, Î» _, by ext; refl, Î» _, by ext; reflâŸ©,
    fapply binary_cofan.is_colimit_mk,
    { exact Î» s x, dite _ (Î» h, s.inl $ eX.symm âŸ¨x, hâŸ©)
        (Î» h, s.inr $ eY.symm âŸ¨x, (this x).resolve_left hâŸ©) },
    { intro s, ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, _âŸ©, dsimp, split_ifs; refl },
    { intro s, ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, hxâŸ©, dsimp, split_ifs, { cases h.symm.trans hx }, { refl } },
    { intros s m eâ‚ eâ‚‚, ext x, split_ifs, { rw â† eâ‚, refl }, { rw â† eâ‚‚, refl } } }
end

section Top

/-- (Implementation) An auxiliary lemma for the proof that `Top` is finitary extensive. -/
def finitary_extensive_Top_aux (Z : Top.{u}) (f : Z âŸ¶ Top.of (punit.{u+1} âŠ• punit.{u+1})) :
  is_colimit (binary_cofan.mk
    (Top.pullback_fst f (Top.binary_cofan (Top.of punit) (Top.of punit)).inl)
    (Top.pullback_fst f (Top.binary_cofan (Top.of punit) (Top.of punit)).inr)) :=
begin
  have : âˆ€ x, f x = sum.inl punit.star âˆ¨ f x = sum.inr punit.star,
  { intro x, rcases f x with (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨âŸ¨âŸ©âŸ©), exacts [or.inl rfl, or.inr rfl] },
  let eX : {p : Z Ã— punit // f p.fst = sum.inl p.snd} â‰ƒ { x : Z // f x = sum.inl punit.star } :=
    âŸ¨Î» p, âŸ¨p.1.1, p.2.trans (congr_arg sum.inl $ subsingleton.elim _ _)âŸ©,
      Î» x, âŸ¨âŸ¨_, _âŸ©, x.2âŸ©, Î» _, by ext; refl, Î» _, by ext; reflâŸ©,
  let eY : {p : Z Ã— punit // f p.fst = sum.inr p.snd} â‰ƒ { x : Z // f x = sum.inr punit.star } :=
    âŸ¨Î» p, âŸ¨p.1.1, p.2.trans (congr_arg sum.inr $ subsingleton.elim _ _)âŸ©,
      Î» x, âŸ¨âŸ¨_, _âŸ©, x.2âŸ©, Î» _, by ext; refl, Î» _, by ext; reflâŸ©,
  fapply binary_cofan.is_colimit_mk,
  { refine Î» s, âŸ¨Î» x, dite _ (Î» h, s.inl $ eX.symm âŸ¨x, hâŸ©)
      (Î» h, s.inr $ eY.symm âŸ¨x, (this x).resolve_left hâŸ©), _âŸ©,
    rw continuous_iff_continuous_at,
    intro x,
    by_cases f x = sum.inl punit.star,
    { revert h x,
      apply (is_open.continuous_on_iff _).mp,
      { rw continuous_on_iff_continuous_restrict,
        convert_to continuous (Î» x : {x|f x = sum.inl punit.star}, s.inl âŸ¨(x, punit.star), x.2âŸ©),
        { ext âŸ¨x, hxâŸ©, exact dif_pos hx },
        continuity },
      { convert f.2.1 _ (open_embedding_inl).open_range, ext x, exact âŸ¨Î» h, âŸ¨_, h.symmâŸ©,
          Î» âŸ¨e, hâŸ©, h.symm.trans (congr_arg sum.inl $ subsingleton.elim _ _)âŸ© } },
    { revert h x,
      apply (is_open.continuous_on_iff _).mp,
      { rw continuous_on_iff_continuous_restrict,
        convert_to continuous (Î» x : {x|f x â‰  sum.inl punit.star},
          s.inr âŸ¨(x, punit.star), (this _).resolve_left x.2âŸ©),
        { ext âŸ¨x, hxâŸ©, exact dif_neg hx },
        continuity },
      { convert f.2.1 _ (open_embedding_inr).open_range, ext x,
        change f x â‰  sum.inl punit.star â†” f x âˆˆ set.range sum.inr,
        transitivity f x = sum.inr punit.star,
        { rcases f x with (âŸ¨âŸ¨âŸ©âŸ©|âŸ¨âŸ¨âŸ©âŸ©);
            simp only [iff_self, eq_self_iff_true, not_true, ne.def, not_false_iff] },
        { exact âŸ¨Î» h, âŸ¨_, h.symmâŸ©, Î» âŸ¨e, hâŸ©,
            h.symm.trans (congr_arg sum.inr $ subsingleton.elim _ _)âŸ© } } } },
  { intro s, ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, _âŸ©, change dite _ _ _ = _, split_ifs; refl },
  { intro s, ext âŸ¨âŸ¨x, âŸ¨âŸ©âŸ©, hxâŸ©, change dite _ _ _ = _,
    split_ifs, { cases h.symm.trans hx }, { refl } },
  { intros s m eâ‚ eâ‚‚, ext x, change m x = dite _ _ _,
    split_ifs, { rw â† eâ‚, refl }, { rw â† eâ‚‚, refl } }
end

instance : finitary_extensive Top.{u} :=
begin
  rw [finitary_extensive_iff_of_is_terminal Top.{u} _ Top.is_terminal_punit _
    (Top.binary_cofan_is_colimit _ _)],
  apply binary_cofan.is_van_kampen_mk _ _ (Î» X Y, Top.binary_cofan_is_colimit X Y) _
    (Î» X Y Z f g, Top.pullback_cone_is_limit f g),
  { intros,
    split,
    { refine âŸ¨âŸ¨hÎ±X.symmâŸ©, âŸ¨pullback_cone.is_limit_aux' _ _âŸ©âŸ©,
      intro s,
      have : âˆ€ x, âˆƒ! y, s.fst x = sum.inl y,
      { intro x,
        cases h : s.fst x,
        { simp_rw sum.inl_injective.eq_iff, exact exists_unique_eq' },
        { apply_fun f at h,
          cases ((concrete_category.congr_hom s.condition x).symm.trans h).trans
            (concrete_category.congr_hom hÎ±Y val : _).symm } },
      delta exists_unique at this,
      choose l hl hl',
      refine âŸ¨âŸ¨l, _âŸ©, continuous_map.ext (Î» a, (hl a).symm), Top.is_terminal_punit.hom_ext _ _,
        Î» l' hâ‚ hâ‚‚, continuous_map.ext $ Î» x,
          hl' x (l' x) (concrete_category.congr_hom hâ‚ x).symmâŸ©,
      apply embedding_inl.to_inducing.continuous_iff.mpr,
      convert s.fst.2 using 1, exact (funext hl).symm },
    { refine âŸ¨âŸ¨hÎ±Y.symmâŸ©, âŸ¨pullback_cone.is_limit_aux' _ _âŸ©âŸ©,
      intro s, dsimp,
      have : âˆ€ x, âˆƒ! y, s.fst x = sum.inr y,
      { intro x,
        cases h : s.fst x,
        { apply_fun f at h,
          cases ((concrete_category.congr_hom s.condition x).symm.trans h).trans
            (concrete_category.congr_hom hÎ±X val : _).symm },
        { simp_rw sum.inr_injective.eq_iff, exact exists_unique_eq' } },
      delta exists_unique at this,
      choose l hl hl',
      refine âŸ¨âŸ¨l, _âŸ©, continuous_map.ext (Î» a, (hl a).symm), Top.is_terminal_punit.hom_ext _ _,
        Î» l' hâ‚ hâ‚‚, continuous_map.ext $
          Î» x, hl' x (l' x) (concrete_category.congr_hom hâ‚ x).symmâŸ©,
      apply embedding_inr.to_inducing.continuous_iff.mpr,
      convert s.fst.2 using 1, exact (funext hl).symm } },
  { intros Z f, exact finitary_extensive_Top_aux Z f }
end

end Top

section functor

lemma is_universal_colimit.map_reflective [has_colimits_of_shape J C]
  [has_pullbacks C] [has_pullbacks D]
  {Gl : C â¥¤ D} {Gr : D â¥¤ C} (adj : Gl âŠ£ Gr) [full Gr] [faithful Gr]
  [preserves_limits_of_shape walking_cospan Gl] {F : J â¥¤ D} {c : cocone (F â‹™ Gr)}
  (H : is_universal_colimit c) :
  is_universal_colimit (Gl.map_cocone c) :=
begin
  haveI := adj.right_adjoint_preserves_limits,
  haveI : preserves_colimits_of_size.{u' v'} Gl := adj.left_adjoint_preserves_colimits,
  haveI : reflects_limits_of_shape walking_cospan Gr :=
    reflects_limits_of_shape_of_reflects_isomorphisms,
  intros F' c' Î± f h hÎ± hc',
  let Î±' := Î± â‰« (functor.associator _ _ _).hom â‰« whisker_left F adj.counit â‰« F.right_unitor.hom,
  have hÎ±' : Î±'.equifibered := hÎ±.comp (nat_trans.equifibered_of_is_iso _),
  have hadj : âˆ€ X, Gl.map (adj.unit.app X) = inv (adj.counit.app _),
  { intro X, apply is_iso.eq_inv_of_inv_hom_id, exact adj.left_triangle_components },
  haveI : âˆ€ X, is_iso (Gl.map (adj.unit.app X)) := by { simp_rw hadj, apply_instance },
  have hÎ±'' : âˆ€ j, Gl.map (Gr.map $ Î±'.app j) = adj.counit.app _ â‰« Î±.app j,
  { intro j, rw â† cancel_mono (adj.counit.app $ F.obj j), dsimp,
    simp only [category.comp_id, adjunction.counit_naturality_assoc, category.id_comp,
      adjunction.counit_naturality, category.assoc, functor.map_comp] },
  have hc'' : âˆ€ j, Î±.app j â‰« Gl.map (c.Î¹.app j) = c'.Î¹.app j â‰« f := nat_trans.congr_app h,
  let Î² := iso_whisker_left F' (as_iso adj.counit) â‰ªâ‰« F'.right_unitor,
  let c'' : cocone (F' â‹™ Gr),
  { refine { X := pullback (Gr.map f) (adj.unit.app _), Î¹ := { app := Î» j,
      pullback.lift (Gr.map $ c'.Î¹.app j) (Gr.map (Î±'.app j) â‰« c.Î¹.app j) _,
      naturality' := _ } },
    { rw [â† Gr.map_comp, â† hc''], erw â† adj.unit_naturality, rw [Gl.map_comp, hÎ±''], dsimp,
      simp only [category.assoc, functor.map_comp, adj.right_triangle_components_assoc], },
    { intros i j g, ext; dsimp; simp only [category.comp_id, category.id_comp, category.assoc,
        â† functor.map_comp, pullback.lift_fst, pullback.lift_snd, â† functor.map_comp_assoc],
      { congr' 1, exact c'.w _ },
      { rw [Î±.naturality_assoc], dsimp, rw [adj.counit_naturality, â† category.assoc,
          Gr.map_comp_assoc], congr' 1, exact c.w _ } } },
  let cf : (cocones.precompose Î².hom).obj c' âŸ¶ Gl.map_cocone c'',
  { refine { hom := pullback.lift _ f _ â‰« (preserves_pullback.iso _ _ _).inv, w' := _ },
    exact (inv $ adj.counit.app c'.X),
    { rw [is_iso.inv_comp_eq, â† adj.counit_naturality_assoc, â† cancel_mono (adj.counit.app $
        Gl.obj c.X), category.assoc, category.assoc, adj.left_triangle_components],
      erw category.comp_id, refl },
    { intro j, rw [â† category.assoc, iso.comp_inv_eq], ext; simp only
        [preserves_pullback.iso_hom_fst, preserves_pullback.iso_hom_snd, pullback.lift_fst,
          pullback.lift_snd, category.assoc, functor.map_cocone_Î¹_app, â† Gl.map_comp],
      { rw [is_iso.comp_inv_eq, adj.counit_naturality], dsimp, rw category.comp_id },
      { rw [Gl.map_comp, hÎ±'', category.assoc, hc''], dsimp,
        rw [category.comp_id, category.assoc] } } },
  have : cf.hom â‰« (preserves_pullback.iso _ _ _).hom â‰« pullback.fst â‰« adj.counit.app _ = ğŸ™ _,
  { simp only [is_iso.inv_hom_id, iso.inv_hom_id_assoc, category.assoc, pullback.lift_fst_assoc] },
  haveI : is_iso cf,
  { apply_with cocones.cocone_iso_of_hom_iso { instances := ff },
    rw â† is_iso.eq_comp_inv at this,
    rw this,
    apply_instance },
  obtain âŸ¨Hc''âŸ© := H c'' (whisker_right Î±' Gr) pullback.snd _ (hÎ±'.whisker_right Gr) _,
  rotate,
  { ext j, dsimp, simp only [category.comp_id, category.id_comp, category.assoc, functor.map_comp,
      pullback.lift_snd] },
  { intro j,
    apply is_pullback.of_right _ _ (is_pullback.of_has_pullback _ _),
    { dsimp, simp only [category.comp_id, category.id_comp, category.assoc, functor.map_comp,
        pullback.lift_fst],
      rw â† category.comp_id (Gr.map f),
      refine ((hc' j).map Gr).paste_vert (is_pullback.of_vert_is_iso âŸ¨_âŸ©),
      rw [â† adj.unit_naturality, category.comp_id, â† category.assoc,
        â† category.id_comp (Gr.map ((Gl.map_cocone c).Î¹.app j))],
      congr' 1,
      rw â† cancel_mono (Gr.map (adj.counit.app (F.obj j))),
      dsimp,
      simp only [category.comp_id, adjunction.right_triangle_components, category.id_comp,
        category.assoc] },
    { dsimp, simp only [category.comp_id, category.id_comp, category.assoc, functor.map_comp,
        pullback.lift_snd] } },
  exact âŸ¨is_colimit.precompose_hom_equiv Î² c' $
    (is_colimit_of_preserves Gl Hc'').of_iso_colimit (as_iso cf).symmâŸ©
end

lemma is_van_kampen_colimit.map_reflective [has_colimits_of_shape J C]
  [has_pullbacks C] [has_pullbacks D]
  {Gl : C â¥¤ D} {Gr : D â¥¤ C} (adj : Gl âŠ£ Gr) [full Gr] [faithful Gr]
  [preserves_limits_of_shape walking_cospan Gl]
  [is_iso adj.counit] {F : J â¥¤ D} {c : cocone (F â‹™ Gr)} (H : is_van_kampen_colimit c) :
   is_van_kampen_colimit (Gl.map_cocone c) :=
begin
  haveI := adj.right_adjoint_preserves_limits,
  haveI : preserves_colimits_of_size.{u' v'} Gl := adj.left_adjoint_preserves_colimits,
  haveI : reflects_limits_of_shape walking_cospan Gr :=
    reflects_limits_of_shape_of_reflects_isomorphisms,
  intros F' c' Î± f h hÎ±,
  refine âŸ¨_, H.is_universal.map_reflective adj c' Î± f h hÎ±âŸ©,
  rintro âŸ¨hc'âŸ© j,
  let Î±' := Î± â‰« (functor.associator _ _ _).hom â‰« whisker_left F adj.counit â‰« F.right_unitor.hom,
  have hÎ±' : Î±'.equifibered := hÎ±.comp (nat_trans.equifibered_of_is_iso _),
  have hÎ±'' : âˆ€ j, Gl.map (Gr.map $ Î±'.app j) = adj.counit.app _ â‰« Î±.app j,
  { intro j, rw â† cancel_mono (adj.counit.app $ F.obj j), dsimp,
    simp only [category.comp_id, adjunction.counit_naturality_assoc, category.id_comp,
      adjunction.counit_naturality, category.assoc, functor.map_comp] },
  have hc'' : âˆ€ j, Î±.app j â‰« Gl.map (c.Î¹.app j) = c'.Î¹.app j â‰« f := nat_trans.congr_app h,
  let Î² := iso_whisker_left F' (as_iso adj.counit) â‰ªâ‰« F'.right_unitor,
  let hl := (is_colimit.precompose_hom_equiv Î² c').symm hc',
  let hr := is_colimit_of_preserves Gl (colimit.is_colimit $ F' â‹™ Gr),
  have : Î±.app j = Î².inv.app _ â‰« Gl.map (Gr.map $ Î±'.app j),
  { rw hÎ±'', simp },
  rw this,
  have : f = (hl.cocone_point_unique_up_to_iso hr).hom â‰«
    Gl.map (colimit.desc _ âŸ¨_, whisker_right Î±' Gr â‰« c.2âŸ©),
  { symmetry,
    convert @is_colimit.cocone_point_unique_up_to_iso_hom_desc _ _ _ _ ((F' â‹™ Gr) â‹™ Gl)
      (Gl.map_cocone âŸ¨_, (whisker_right Î±' Gr â‰« c.2 : _)âŸ©) _ _ hl hr using 3,
    { apply hr.hom_ext, intro j, rw [hr.fac, functor.map_cocone_Î¹_app, â† Gl.map_comp,
        colimit.cocone_Î¹, colimit.Î¹_desc], refl },
    { clear_value Î±', apply hl.hom_ext, intro j, rw hl.fac, dsimp,
      simp only [category.comp_id, hÎ±'', category_theory.category.assoc, Gl.map_comp],
      congr' 1, exact (nat_trans.congr_app h j).symm } },
  rw this,
  have := ((H (colimit.cocone $ F' â‹™ Gr) (whisker_right Î±' Gr)
    (colimit.desc _ âŸ¨_, whisker_right Î±' Gr â‰« c.2âŸ©) _ (hÎ±'.whisker_right Gr)).mp
    âŸ¨(get_colimit_cocone $ F' â‹™ Gr).2âŸ© j).map Gl,
  convert is_pullback.paste_vert _ this,
  { refine is_pullback.of_vert_is_iso âŸ¨_âŸ©,
    rw [â† is_iso.inv_comp_eq, â† category.assoc, nat_iso.inv_inv_app],
    exact is_colimit.comp_cocone_point_unique_up_to_iso_hom hl hr _ },
  { clear_value Î±', ext j, simp }
end

lemma is_van_kampen_colimit.of_map {D : Type*} [category D] (G : C â¥¤ D) {F : J â¥¤ C} {c : cocone F}
  [preserves_limits_of_shape walking_cospan G] [reflects_limits_of_shape walking_cospan G]
    [preserves_colimits_of_shape J G] [reflects_colimits_of_shape J G]
   (H : is_van_kampen_colimit (G.map_cocone c)) : is_van_kampen_colimit c :=
begin
  intros F' c' Î± f h hÎ±,
  refine (iff.trans _ (H (G.map_cocone c') (whisker_right Î± G) (G.map f)
    (by { ext j, simpa using G.congr_map (nat_trans.congr_app h j) })
    (hÎ±.whisker_right G))).trans (forall_congr $ Î» j, _),
  { exact âŸ¨Î» h, âŸ¨is_colimit_of_preserves G h.someâŸ©, Î» h, âŸ¨is_colimit_of_reflects G h.someâŸ©âŸ© },
  { exact is_pullback.map_iff G (nat_trans.congr_app h.symm j) }
end

lemma is_van_kampen_colimit_of_evaluation [has_pullbacks D] [has_colimits_of_shape J D]
  (F : J â¥¤ C â¥¤ D) (c : cocone F)
  (hc : âˆ€ x : C, is_van_kampen_colimit (((evaluation C D).obj x).map_cocone c)) :
  is_van_kampen_colimit c :=
begin
  intros F' c' Î± f e hÎ±,
  have := Î» x, hc x (((evaluation C D).obj x).map_cocone c') (whisker_right Î± _)
    (((evaluation C D).obj x).map f)
    (by { ext y, dsimp, exact nat_trans.congr_app (nat_trans.congr_app e y) x })
    (hÎ±.whisker_right _),
  split,
  { rintros âŸ¨hc'âŸ© j,
    refine âŸ¨âŸ¨(nat_trans.congr_app e j).symmâŸ©, âŸ¨evaluation_jointly_reflects_limits _ _âŸ©âŸ©,
    refine Î» x, (is_limit_map_cone_pullback_cone_equiv _ _).symm _,
    exact ((this x).mp âŸ¨preserves_colimit.preserves hc'âŸ© _).is_limit },
  { exact Î» H, âŸ¨evaluation_jointly_reflects_colimits _
      (Î» x, ((this x).mpr (Î» j, (H j).map ((evaluation C D).obj x))).some)âŸ© }
end

instance [has_pullbacks C] [finitary_extensive C] : finitary_extensive (D â¥¤ C) :=
begin
  haveI : has_finite_coproducts (D â¥¤ C) :=
    âŸ¨Î» J hJ, by exactI limits.functor_category_has_colimits_of_shapeâŸ©,
  exact âŸ¨Î» X Y c hc, is_van_kampen_colimit_of_evaluation _ c
    (Î» x, finitary_extensive.van_kampen _ $ preserves_colimit.preserves hc)âŸ©
end

lemma finitary_extensive_of_preserves_and_reflects (F : C â¥¤ D)
  [finitary_extensive D] [has_finite_coproducts C]
    [preserves_limits_of_shape walking_cospan F]
    [reflects_limits_of_shape walking_cospan F]
    [preserves_colimits_of_shape (discrete walking_pair) F]
    [reflects_colimits_of_shape (discrete walking_pair) F] :
  finitary_extensive C :=
âŸ¨Î» X Y c hc, (finitary_extensive.van_kampen _ (is_colimit_of_preserves F hc)).of_map FâŸ©

lemma finitary_extensive_of_preserves_and_reflects_isomorphism (F : C â¥¤ D)
  [finitary_extensive D] [has_finite_coproducts C] [has_pullbacks C]
    [preserves_limits_of_shape walking_cospan F]
    [preserves_colimits_of_shape (discrete walking_pair) F]
    [reflects_isomorphisms F] :
  finitary_extensive C :=
begin
  haveI : reflects_limits_of_shape walking_cospan F :=
    reflects_limits_of_shape_of_reflects_isomorphisms,
  haveI : reflects_colimits_of_shape (discrete walking_pair) F :=
    reflects_colimits_of_shape_of_reflects_isomorphisms,
  exact finitary_extensive_of_preserves_and_reflects F,
end

lemma finitary_extensive_of_reflective [has_finite_coproducts D] [has_pullbacks D]
  [finitary_extensive C] [has_pullbacks C]
  {Gl : C â¥¤ D} {Gr : D â¥¤ C} (adj : Gl âŠ£ Gr) [full Gr] [faithful Gr]
  [preserves_limits_of_shape walking_cospan Gl] :
  finitary_extensive D :=
begin
  haveI : preserves_colimits_of_size Gl := adj.left_adjoint_preserves_colimits,
  constructor,
  intros X Y c hc,
  apply is_van_kampen_colimit.of_precompose_iso _
    (iso_whisker_left _ (as_iso adj.counit) â‰ªâ‰« functor.right_unitor _).hom,
  refine ((finitary_extensive.van_kampen _ (colimit.is_colimit $ pair X Y â‹™ _)).map_reflective
    adj).of_iso (is_colimit.unique_up_to_iso _ _),
  { exact is_colimit_of_preserves Gl (colimit.is_colimit _) },
  { exact (is_colimit.precompose_hom_equiv _ _).symm hc },
  { apply_instance }
end

end functor

end extensive

end category_theory
