/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Scott Morrison
-/
import category_theory.fin_category
import category_theory.limits.cones
import category_theory.adjunction.basic
import category_theory.category.preorder
import category_theory.category.ulift

/-!
# Filtered categories

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A category is filtered if every finite diagram admits a cocone.
We give a simple characterisation of this condition as
1. for every pair of objects there exists another object "to the right",
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal, and
3. there exists some object.

Filtered colimits are often better behaved than arbitrary colimits.
See `category_theory/limits/types` for some details.

Filtered categories are nice because colimits indexed by filtered categories tend to be
easier to describe than general colimits (and more often preserved by functors).

In this file we show that any functor from a finite category to a filtered category admits a cocone:
* `cocone_nonempty [fin_category J] [is_filtered C] (F : J ‚•§ C) : nonempty (cocone F)`
More generally,
for any finite collection of objects and morphisms between them in a filtered category
(even if not closed under composition) there exists some object `Z` receiving maps from all of them,
so that all the triangles (one edge from the finite set, two from morphisms to `Z`) commute.
This formulation is often more useful in practice and is available via `sup_exists`,
which takes a finset of objects, and an indexed family (indexed by source and target)
of finsets of morphisms.

Furthermore, we give special support for two diagram categories: The `bowtie` and the `tulip`.
This is because these shapes show up in the proofs that forgetful functors of algebraic categories
(e.g. `Mon`, `CommRing`, ...) preserve filtered colimits.

All of the above API, except for the `bowtie` and the `tulip`, is also provided for cofiltered
categories.

## See also
In `category_theory.limits.filtered_colimit_commutes_finite_limit` we show that filtered colimits
commute with finite limits.

-/

open function

-- declare the `v`'s first; see `category_theory.category` for an explanation
universes w v v‚ÇÅ u u‚ÇÅ u‚ÇÇ

namespace category_theory

variables (C : Type u) [category.{v} C]

/--
A category `is_filtered_or_empty` if
1. for every pair of objects there exists another object "to the right", and
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal.
-/
class is_filtered_or_empty : Prop :=
(cocone_objs : ‚àÄ (X Y : C), ‚àÉ Z (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z), true)
(cocone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ Z (h : Y ‚ü∂ Z), f ‚â´ h = g ‚â´ h)

/--
A category `is_filtered` if
1. for every pair of objects there exists another object "to the right",
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal, and
3. there exists some object.

See <https://stacks.math.columbia.edu/tag/002V>. (They also define a diagram being filtered.)
-/
class is_filtered extends is_filtered_or_empty C : Prop :=
[nonempty : nonempty C]

@[priority 100]
instance is_filtered_or_empty_of_semilattice_sup
  (Œ± : Type u) [semilattice_sup Œ±] : is_filtered_or_empty Œ± :=
{ cocone_objs := Œª X Y, ‚ü®X ‚äî Y, hom_of_le le_sup_left, hom_of_le le_sup_right, trivial‚ü©,
  cocone_maps := Œª X Y f g, ‚ü®Y, ùüô _, (by ext)‚ü©, }

@[priority 100]
instance is_filtered_of_semilattice_sup_nonempty
  (Œ± : Type u) [semilattice_sup Œ±] [nonempty Œ±] : is_filtered Œ± := {}

@[priority 100]
instance is_filtered_or_empty_of_directed_le (Œ± : Type u) [preorder Œ±] [is_directed Œ± (‚â§)] :
  is_filtered_or_empty Œ± :=
{ cocone_objs := Œª X Y, let ‚ü®Z, h1, h2‚ü© := exists_ge_ge X Y in
    ‚ü®Z, hom_of_le h1, hom_of_le h2, trivial‚ü©,
  cocone_maps := Œª X Y f g, ‚ü®Y, ùüô _, by simp‚ü© }

@[priority 100]
instance is_filtered_of_directed_le_nonempty (Œ± : Type u) [preorder Œ±] [is_directed Œ± (‚â§)]
  [nonempty Œ±] :
  is_filtered Œ± := {}

-- Sanity checks
example (Œ± : Type u) [semilattice_sup Œ±] [order_bot Œ±] : is_filtered Œ± := by apply_instance
example (Œ± : Type u) [semilattice_sup Œ±] [order_top Œ±] : is_filtered Œ± := by apply_instance

instance : is_filtered (discrete punit) :=
{ cocone_objs := Œª X Y, ‚ü®‚ü®punit.star‚ü©, ‚ü®‚ü®dec_trivial‚ü©‚ü©, ‚ü®‚ü®dec_trivial‚ü©‚ü©, trivial‚ü©,
  cocone_maps := Œª X Y f g, ‚ü®‚ü®punit.star‚ü©, ‚ü®‚ü®dec_trivial‚ü©‚ü©, dec_trivial‚ü©,
  nonempty := ‚ü®‚ü®punit.star‚ü©‚ü© }

namespace is_filtered

section allow_empty

variables {C} [is_filtered_or_empty C]

lemma cocone_objs : ‚àÄ (X Y : C), ‚àÉ Z (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z), true :=
is_filtered_or_empty.cocone_objs
lemma cocone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ Z (h : Y ‚ü∂ Z), f ‚â´ h = g ‚â´ h :=
is_filtered_or_empty.cocone_maps

/--
`max j j'` is an arbitrary choice of object to the right of both `j` and `j'`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def max (j j' : C) : C :=
(cocone_objs j j').some

/--
`left_to_max j j'` is an arbitrary choice of morphism from `j` to `max j j'`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def left_to_max (j j' : C) : j ‚ü∂ max j j' :=
(cocone_objs j j').some_spec.some

/--
`right_to_max j j'` is an arbitrary choice of morphism from `j'` to `max j j'`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def right_to_max (j j' : C) : j' ‚ü∂ max j j' :=
(cocone_objs j j').some_spec.some_spec.some

/--
`coeq f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of object
which admits a morphism `coeq_hom f f' : j' ‚ü∂ coeq f f'` such that
`coeq_condition : f ‚â´ coeq_hom f f' = f' ‚â´ coeq_hom f f'`.
Its existence is ensured by `is_filtered`.
-/
noncomputable def coeq {j j' : C} (f f' : j ‚ü∂ j') : C :=
(cocone_maps f f').some

/--
`coeq_hom f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of morphism
`coeq_hom f f' : j' ‚ü∂ coeq f f'` such that
`coeq_condition : f ‚â´ coeq_hom f f' = f' ‚â´ coeq_hom f f'`.
Its existence is ensured by `is_filtered`.
-/
noncomputable def coeq_hom {j j' : C} (f f' : j ‚ü∂ j') : j' ‚ü∂ coeq f f' :=
(cocone_maps f f').some_spec.some

/--
`coeq_condition f f'`, for morphisms `f f' : j ‚ü∂ j'`, is the proof that
`f ‚â´ coeq_hom f f' = f' ‚â´ coeq_hom f f'`.
-/
@[simp, reassoc]
lemma coeq_condition {j j' : C} (f f' : j ‚ü∂ j') : f ‚â´ coeq_hom f f' = f' ‚â´ coeq_hom f f' :=
(cocone_maps f f').some_spec.some_spec

end allow_empty

section nonempty

open category_theory.limits

variables {C} [is_filtered C]

/--
Any finite collection of objects in a filtered category has an object "to the right".
-/
lemma sup_objs_exists (O : finset C) : ‚àÉ (S : C), ‚àÄ {X}, X ‚àà O ‚Üí _root_.nonempty (X ‚ü∂ S) :=
begin
  classical,
  apply finset.induction_on O,
  { exact ‚ü®is_filtered.nonempty.some, (by rintros - ‚ü®‚ü©)‚ü©, },
  { rintros X O' nm ‚ü®S', w'‚ü©,
    use max X S',
    rintros Y mY,
    obtain rfl|h := eq_or_ne Y X,
    { exact ‚ü®left_to_max _ _‚ü©, },
    { exact ‚ü®(w' (finset.mem_of_mem_insert_of_ne mY h)).some ‚â´ right_to_max _ _‚ü©, }, }
end

variables (O : finset C) (H : finset (Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y))

/--
Given any `finset` of objects `{X, ...}` and
indexed collection of `finset`s of morphisms `{f, ...}` in `C`,
there exists an object `S`, with a morphism `T X : X ‚ü∂ S` from each `X`,
such that the triangles commute: `f ‚â´ T Y = T X`, for `f : X ‚ü∂ Y` in the `finset`.
-/
lemma sup_exists :
  ‚àÉ (S : C) (T : Œ† {X : C}, X ‚àà O ‚Üí (X ‚ü∂ S)), ‚àÄ {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y},
    (‚ü®X, Y, mX, mY, f‚ü© : (Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y)) ‚àà H ‚Üí f ‚â´ T mY = T mX :=
begin
  classical,
  apply finset.induction_on H,
  { obtain ‚ü®S, f‚ü© := sup_objs_exists O,
    refine ‚ü®S, Œª X mX, (f mX).some, _‚ü©,
    rintros - - - - - ‚ü®‚ü©, },
  { rintros ‚ü®X, Y, mX, mY, f‚ü© H' nmf ‚ü®S', T', w'‚ü©,
    refine ‚ü®coeq (f ‚â´ T' mY) (T' mX), Œª Z mZ, T' mZ ‚â´ coeq_hom (f ‚â´ T' mY) (T' mX), _‚ü©,
    intros X' Y' mX' mY' f' mf',
    rw [‚Üêcategory.assoc],
    by_cases h : X = X' ‚àß Y = Y',
    { rcases h with ‚ü®rfl, rfl‚ü©,
      by_cases hf : f = f',
      { subst hf,
        apply coeq_condition, },
      { rw @w' _ _ mX mY f' (by simpa [hf ‚àò eq.symm] using mf') }, },
    { rw @w' _ _ mX' mY' f' _,
      apply finset.mem_of_mem_insert_of_ne mf',
      contrapose! h,
      obtain ‚ü®rfl, h‚ü© := h,
      rw [heq_iff_eq, psigma.mk.inj_iff] at h,
      exact ‚ü®rfl, h.1.symm‚ü© }, },
end

/--
An arbitrary choice of object "to the right"
of a finite collection of objects `O` and morphisms `H`,
making all the triangles commute.
-/
noncomputable
def sup : C :=
(sup_exists O H).some

/--
The morphisms to `sup O H`.
-/
noncomputable
def to_sup {X : C} (m : X ‚àà O) :
  X ‚ü∂ sup O H :=
(sup_exists O H).some_spec.some m

/--
The triangles of consisting of a morphism in `H` and the maps to `sup O H` commute.
-/
lemma to_sup_commutes
  {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y}
  (mf : (‚ü®X, Y, mX, mY, f‚ü© : Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y) ‚àà H) :
  f ‚â´ to_sup O H mY = to_sup O H mX :=
(sup_exists O H).some_spec.some_spec mX mY mf

variables {J : Type v} [small_category J] [fin_category J]

/--
If we have `is_filtered C`, then for any functor `F : J ‚•§ C` with `fin_category J`,
there exists a cocone over `F`.
-/
lemma cocone_nonempty (F : J ‚•§ C) : _root_.nonempty (cocone F) :=
begin
  classical,
  let O := (finset.univ.image F.obj),
  let H : finset (Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y) :=
    finset.univ.bUnion (Œª X : J, finset.univ.bUnion (Œª Y : J, finset.univ.image (Œª f : X ‚ü∂ Y,
      ‚ü®F.obj X, F.obj Y, by simp, by simp, F.map f‚ü©))),
  obtain ‚ü®Z, f, w‚ü© := sup_exists O H,
  refine ‚ü®‚ü®Z, ‚ü®Œª X, f (by simp), _‚ü©‚ü©‚ü©,
  intros j j' g,
  dsimp,
  simp only [category.comp_id],
  apply w,
  simp only [finset.mem_univ, finset.mem_bUnion, exists_and_distrib_left,
    exists_prop_of_true, finset.mem_image],
  exact ‚ü®j, rfl, j', g, (by simp)‚ü©,
end

/--
An arbitrary choice of cocone over `F : J ‚•§ C`, for `fin_category J` and `is_filtered C`.
-/
noncomputable def cocone (F : J ‚•§ C) : cocone F :=
(cocone_nonempty F).some

variables {D : Type u‚ÇÅ} [category.{v‚ÇÅ} D]

/--
If `C` is filtered, and we have a functor `R : C ‚•§ D` with a left adjoint, then `D` is filtered.
-/
lemma of_right_adjoint {L : D ‚•§ C} {R : C ‚•§ D} (h : L ‚ä£ R) : is_filtered D :=
{ cocone_objs := Œª X Y,
    ‚ü®_, h.hom_equiv _ _ (left_to_max _ _), h.hom_equiv _ _ (right_to_max _ _), ‚ü®‚ü©‚ü©,
  cocone_maps := Œª X Y f g,
    ‚ü®_, h.hom_equiv _ _ (coeq_hom _ _),
     by rw [‚Üê h.hom_equiv_naturality_left, ‚Üê h.hom_equiv_naturality_left, coeq_condition]‚ü©,
  nonempty := is_filtered.nonempty.map R.obj }

/-- If `C` is filtered, and we have a right adjoint functor `R : C ‚•§ D`, then `D` is filtered. -/
lemma of_is_right_adjoint (R : C ‚•§ D) [is_right_adjoint R] : is_filtered D :=
of_right_adjoint (adjunction.of_right_adjoint R)

/-- Being filtered is preserved by equivalence of categories. -/
lemma of_equivalence (h : C ‚âå D) : is_filtered D :=
of_right_adjoint h.symm.to_adjunction

end nonempty

section special_shapes

variables {C} [is_filtered_or_empty C]

/--
`max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of object to the right of `j‚ÇÅ`, `j‚ÇÇ` and `j‚ÇÉ`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def max‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : C := max (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ

/--
`first_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of morphism from `j‚ÇÅ` to `max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def first_to_max‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : j‚ÇÅ ‚ü∂ max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ :=
left_to_max j‚ÇÅ j‚ÇÇ ‚â´ left_to_max (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ

/--
`second_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of morphism from `j‚ÇÇ` to `max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def second_to_max‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : j‚ÇÇ ‚ü∂ max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ :=
right_to_max j‚ÇÅ j‚ÇÇ ‚â´ left_to_max (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ

/--
`third_to_max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ` is an arbitrary choice of morphism from `j‚ÇÉ` to `max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def third_to_max‚ÇÉ (j‚ÇÅ j‚ÇÇ j‚ÇÉ : C) : j‚ÇÉ ‚ü∂ max‚ÇÉ j‚ÇÅ j‚ÇÇ j‚ÇÉ :=
right_to_max (max j‚ÇÅ j‚ÇÇ) j‚ÇÉ

/--
`coeq‚ÇÉ f g h`, for morphisms `f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ`, is an arbitrary choice of object
which admits a morphism `coeq‚ÇÉ_hom f g h : j‚ÇÇ ‚ü∂ coeq‚ÇÉ f g h` such that
`coeq‚ÇÉ_condition‚ÇÅ`, `coeq‚ÇÉ_condition‚ÇÇ` and `coeq‚ÇÉ_condition‚ÇÉ` are satisfied.
Its existence is ensured by `is_filtered`.
-/
noncomputable def coeq‚ÇÉ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) : C :=
coeq (coeq_hom f g ‚â´ left_to_max (coeq f g) (coeq g h))
  (coeq_hom g h ‚â´ right_to_max (coeq f g) (coeq g h))

/--
`coeq‚ÇÉ_hom f g h`, for morphisms `f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ`, is an arbitrary choice of morphism
`j‚ÇÇ ‚ü∂ coeq‚ÇÉ f g h` such that `coeq‚ÇÉ_condition‚ÇÅ`, `coeq‚ÇÉ_condition‚ÇÇ` and `coeq‚ÇÉ_condition‚ÇÉ`
are satisfied. Its existence is ensured by `is_filtered`.
-/
noncomputable def coeq‚ÇÉ_hom {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) : j‚ÇÇ ‚ü∂ coeq‚ÇÉ f g h :=
coeq_hom f g ‚â´ left_to_max (coeq f g) (coeq g h) ‚â´
coeq_hom (coeq_hom f g ‚â´ left_to_max (coeq f g) (coeq g h))
  (coeq_hom g h ‚â´ right_to_max (coeq f g) (coeq g h))

lemma coeq‚ÇÉ_condition‚ÇÅ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) :
  f ‚â´ coeq‚ÇÉ_hom f g h = g ‚â´ coeq‚ÇÉ_hom f g h :=
by rw [coeq‚ÇÉ_hom, reassoc_of (coeq_condition f g)]

lemma coeq‚ÇÉ_condition‚ÇÇ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) :
  g ‚â´ coeq‚ÇÉ_hom f g h = h ‚â´ coeq‚ÇÉ_hom f g h :=
begin
  dsimp [coeq‚ÇÉ_hom],
  slice_lhs 2 4 { rw [‚Üê category.assoc, coeq_condition _ _] },
  slice_rhs 2 4 { rw [‚Üê category.assoc, coeq_condition _ _] },
  slice_lhs 1 3 { rw [‚Üê category.assoc, coeq_condition _ _] },
  simp only [category.assoc],
end

lemma coeq‚ÇÉ_condition‚ÇÉ {j‚ÇÅ j‚ÇÇ : C} (f g h : j‚ÇÅ ‚ü∂ j‚ÇÇ) :
  f ‚â´ coeq‚ÇÉ_hom f g h = h ‚â´ coeq‚ÇÉ_hom f g h :=
eq.trans (coeq‚ÇÉ_condition‚ÇÅ f g h) (coeq‚ÇÉ_condition‚ÇÇ f g h)

/-- For every span `j ‚üµ i ‚ü∂ j'`, there
   exists a cocone `j ‚ü∂ k ‚üµ j'` such that the square commutes. -/
lemma span {i j j' : C} (f : i ‚ü∂ j) (f' : i ‚ü∂ j') :
  ‚àÉ (k : C) (g : j ‚ü∂ k) (g' : j' ‚ü∂ k), f ‚â´ g = f' ‚â´ g' :=
let ‚ü®K, G, G', _‚ü© := cocone_objs j j', ‚ü®k, e, he‚ü© := cocone_maps (f ‚â´ G) (f' ‚â´ G') in
‚ü®k, G ‚â´ e, G' ‚â´ e, by simpa only [‚Üê category.assoc]‚ü©

/--
Given a "bowtie" of morphisms
```
 j‚ÇÅ   j‚ÇÇ
 |\  /|
 | \/ |
 | /\ |
 |/  \‚à£
 vv  vv
 k‚ÇÅ  k‚ÇÇ
```
in a filtered category, we can construct an object `s` and two morphisms from `k‚ÇÅ` and `k‚ÇÇ` to `s`,
making the resulting squares commute.
-/
lemma bowtie {j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ : C}
  (f‚ÇÅ : j‚ÇÅ ‚ü∂ k‚ÇÅ) (g‚ÇÅ : j‚ÇÅ ‚ü∂ k‚ÇÇ) (f‚ÇÇ : j‚ÇÇ ‚ü∂ k‚ÇÅ) (g‚ÇÇ : j‚ÇÇ ‚ü∂ k‚ÇÇ) :
  ‚àÉ (s : C) (Œ± : k‚ÇÅ ‚ü∂ s) (Œ≤ : k‚ÇÇ ‚ü∂ s), f‚ÇÅ ‚â´ Œ± = g‚ÇÅ ‚â´ Œ≤ ‚àß f‚ÇÇ ‚â´ Œ± = g‚ÇÇ ‚â´ Œ≤ :=
begin
  obtain ‚ü®t, k‚ÇÅt, k‚ÇÇt, ht‚ü© := span f‚ÇÅ g‚ÇÅ,
  obtain ‚ü®s, ts, hs‚ü© := cocone_maps (f‚ÇÇ ‚â´ k‚ÇÅt) (g‚ÇÇ ‚â´ k‚ÇÇt),
  simp_rw category.assoc at hs,
  exact ‚ü®s, k‚ÇÅt ‚â´ ts, k‚ÇÇt ‚â´ ts, by rw reassoc_of ht, hs‚ü©,
end

/--
Given a "tulip" of morphisms
```
 j‚ÇÅ    j‚ÇÇ    j‚ÇÉ
 |\   / \   / |
 | \ /   \ /  |
 |  vv    vv  |
 \  k‚ÇÅ    k‚ÇÇ /
  \         /
   \       /
    \     /
     \   /
      v v
       l
```
in a filtered category, we can construct an object `s` and three morphisms from `k‚ÇÅ`, `k‚ÇÇ` and `l`
to `s`, making the resulting squares commute.
-/
lemma tulip {j‚ÇÅ j‚ÇÇ j‚ÇÉ k‚ÇÅ k‚ÇÇ l : C} (f‚ÇÅ : j‚ÇÅ ‚ü∂ k‚ÇÅ) (f‚ÇÇ : j‚ÇÇ ‚ü∂ k‚ÇÅ) (f‚ÇÉ : j‚ÇÇ ‚ü∂ k‚ÇÇ) (f‚ÇÑ : j‚ÇÉ ‚ü∂ k‚ÇÇ)
  (g‚ÇÅ : j‚ÇÅ ‚ü∂ l) (g‚ÇÇ : j‚ÇÉ ‚ü∂ l) :
  ‚àÉ (s : C) (Œ± : k‚ÇÅ ‚ü∂ s) (Œ≤ : l ‚ü∂ s) (Œ≥ : k‚ÇÇ ‚ü∂ s),
    f‚ÇÅ ‚â´ Œ± = g‚ÇÅ ‚â´ Œ≤ ‚àß f‚ÇÇ ‚â´ Œ± = f‚ÇÉ ‚â´ Œ≥ ‚àß f‚ÇÑ ‚â´ Œ≥ = g‚ÇÇ ‚â´ Œ≤ :=
begin
  obtain ‚ü®l', k‚ÇÅl, k‚ÇÇl, hl‚ü© := span f‚ÇÇ f‚ÇÉ,
  obtain ‚ü®s, ls, l's, hs‚ÇÅ, hs‚ÇÇ‚ü© := bowtie g‚ÇÅ (f‚ÇÅ ‚â´ k‚ÇÅl) g‚ÇÇ (f‚ÇÑ ‚â´ k‚ÇÇl),
  refine ‚ü®s, k‚ÇÅl ‚â´ l's, ls, k‚ÇÇl ‚â´ l's, _, by rw reassoc_of hl, _‚ü©;
  simp only [hs‚ÇÅ, hs‚ÇÇ, category.assoc],
end

end special_shapes

end is_filtered

/--
A category `is_cofiltered_or_empty` if
1. for every pair of objects there exists another object "to the left", and
2. for every pair of parallel morphisms there exists a morphism to the left so the compositions
   are equal.
-/
class is_cofiltered_or_empty : Prop :=
(cone_objs : ‚àÄ (X Y : C), ‚àÉ W (f : W ‚ü∂ X) (g : W ‚ü∂ Y), true)
(cone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ W (h : W ‚ü∂ X), h ‚â´ f = h ‚â´ g)

/--
A category `is_cofiltered` if
1. for every pair of objects there exists another object "to the left",
2. for every pair of parallel morphisms there exists a morphism to the left so the compositions
   are equal, and
3. there exists some object.

See <https://stacks.math.columbia.edu/tag/04AZ>.
-/
class is_cofiltered extends is_cofiltered_or_empty C : Prop :=
[nonempty : nonempty C]

@[priority 100]
instance is_cofiltered_or_empty_of_semilattice_inf
  (Œ± : Type u) [semilattice_inf Œ±] : is_cofiltered_or_empty Œ± :=
{ cone_objs := Œª X Y, ‚ü®X ‚äì Y, hom_of_le inf_le_left, hom_of_le inf_le_right, trivial‚ü©,
  cone_maps := Œª X Y f g, ‚ü®X, ùüô _, (by ext)‚ü©, }

@[priority 100]
instance is_cofiltered_of_semilattice_inf_nonempty
  (Œ± : Type u) [semilattice_inf Œ±] [nonempty Œ±] : is_cofiltered Œ± := {}

@[priority 100]
instance is_cofiltered_or_empty_of_directed_ge (Œ± : Type u) [preorder Œ±]
  [is_directed Œ± (‚â•)] :
  is_cofiltered_or_empty Œ± :=
{ cone_objs := Œª X Y, let ‚ü®Z, hX, hY‚ü© := exists_le_le X Y in
    ‚ü®Z, hom_of_le hX, hom_of_le hY, trivial‚ü©,
  cone_maps := Œª X Y f g, ‚ü®X, ùüô _, by simp‚ü© }

@[priority 100]
instance is_cofiltered_of_directed_ge_nonempty (Œ± : Type u) [preorder Œ±] [is_directed Œ± (‚â•)]
  [nonempty Œ±] :
  is_cofiltered Œ± := {}

-- Sanity checks
example (Œ± : Type u) [semilattice_inf Œ±] [order_bot Œ±] : is_cofiltered Œ± := by apply_instance
example (Œ± : Type u) [semilattice_inf Œ±] [order_top Œ±] : is_cofiltered Œ± := by apply_instance

instance : is_cofiltered (discrete punit) :=
{ cone_objs := Œª X Y, ‚ü®‚ü®punit.star‚ü©, ‚ü®‚ü®dec_trivial‚ü©‚ü©, ‚ü®‚ü®dec_trivial‚ü©‚ü©, trivial‚ü©,
  cone_maps := Œª X Y f g, ‚ü®‚ü®punit.star‚ü©, ‚ü®‚ü®dec_trivial‚ü©‚ü©, dec_trivial‚ü©,
  nonempty := ‚ü®‚ü®punit.star‚ü©‚ü© }

namespace is_cofiltered

section allow_empty

variables {C} [is_cofiltered_or_empty C]

lemma cone_objs : ‚àÄ (X Y : C), ‚àÉ W (f : W ‚ü∂ X) (g : W ‚ü∂ Y), true := is_cofiltered_or_empty.cone_objs
lemma cone_maps : ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f g : X ‚ü∂ Y), ‚àÉ W (h : W ‚ü∂ X), h ‚â´ f = h ‚â´ g :=
is_cofiltered_or_empty.cone_maps

/--
`min j j'` is an arbitrary choice of object to the left of both `j` and `j'`,
whose existence is ensured by `is_cofiltered`.
-/
noncomputable def min (j j' : C) : C :=
(cone_objs j j').some

/--
`min_to_left j j'` is an arbitrary choice of morphism from `min j j'` to `j`,
whose existence is ensured by `is_cofiltered`.
-/
noncomputable def min_to_left (j j' : C) : min j j' ‚ü∂ j :=
(cone_objs j j').some_spec.some

/--
`min_to_right j j'` is an arbitrary choice of morphism from `min j j'` to `j'`,
whose existence is ensured by `is_cofiltered`.
-/
noncomputable def min_to_right (j j' : C) : min j j' ‚ü∂ j' :=
(cone_objs j j').some_spec.some_spec.some

/--
`eq f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of object
which admits a morphism `eq_hom f f' : eq f f' ‚ü∂ j` such that
`eq_condition : eq_hom f f' ‚â´ f = eq_hom f f' ‚â´ f'`.
Its existence is ensured by `is_cofiltered`.
-/
noncomputable def eq {j j' : C} (f f' : j ‚ü∂ j') : C :=
(cone_maps f f').some

/--
`eq_hom f f'`, for morphisms `f f' : j ‚ü∂ j'`, is an arbitrary choice of morphism
`eq_hom f f' : eq f f' ‚ü∂ j` such that
`eq_condition : eq_hom f f' ‚â´ f = eq_hom f f' ‚â´ f'`.
Its existence is ensured by `is_cofiltered`.
-/
noncomputable def eq_hom {j j' : C} (f f' : j ‚ü∂ j') : eq f f' ‚ü∂ j :=
(cone_maps f f').some_spec.some

/--
`eq_condition f f'`, for morphisms `f f' : j ‚ü∂ j'`, is the proof that
`eq_hom f f' ‚â´ f = eq_hom f f' ‚â´ f'`.
-/
@[simp, reassoc]
lemma eq_condition {j j' : C} (f f' : j ‚ü∂ j') : eq_hom f f' ‚â´ f = eq_hom f f' ‚â´ f' :=
(cone_maps f f').some_spec.some_spec

/-- For every cospan `j ‚ü∂ i ‚üµ j'`,
 there exists a cone `j ‚üµ k ‚ü∂ j'` such that the square commutes. -/
lemma cospan {i j j' : C} (f : j ‚ü∂ i) (f' : j' ‚ü∂ i) :
  ‚àÉ (k : C) (g : k ‚ü∂ j) (g' : k ‚ü∂ j'), g ‚â´ f = g' ‚â´ f' :=
let ‚ü®K, G, G', _‚ü© := cone_objs j j', ‚ü®k, e, he‚ü© := cone_maps (G ‚â´ f) (G' ‚â´ f') in
‚ü®k, e ‚â´ G, e ‚â´ G', by simpa only [category.assoc] using he‚ü©

lemma _root_.category_theory.functor.ranges_directed (F : C ‚•§ Type*) (j : C) :
  directed (‚äá) (Œª (f : Œ£' i, i ‚ü∂ j), set.range (F.map f.2)) :=
Œª ‚ü®i, ij‚ü© ‚ü®k, kj‚ü©, let ‚ü®l, li, lk, e‚ü© := cospan ij kj in
by refine ‚ü®‚ü®l, lk ‚â´ kj‚ü©, e ‚ñ∏ _, _‚ü©; simp_rw F.map_comp; apply set.range_comp_subset_range

end allow_empty

section nonempty

open category_theory.limits

variables {C} [is_cofiltered C]

/--
Any finite collection of objects in a cofiltered category has an object "to the left".
-/
lemma inf_objs_exists (O : finset C) : ‚àÉ (S : C), ‚àÄ {X}, X ‚àà O ‚Üí _root_.nonempty (S ‚ü∂ X) :=
begin
  classical,
  apply finset.induction_on O,
  { exact ‚ü®is_cofiltered.nonempty.some, (by rintros - ‚ü®‚ü©)‚ü©, },
  { rintros X O' nm ‚ü®S', w'‚ü©,
    use min X S',
    rintros Y mY,
    obtain rfl|h := eq_or_ne Y X,
    { exact ‚ü®min_to_left _ _‚ü©, },
    { exact ‚ü®min_to_right _ _ ‚â´ (w' (finset.mem_of_mem_insert_of_ne mY h)).some‚ü©, }, }
end

variables (O : finset C) (H : finset (Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y))

/--
Given any `finset` of objects `{X, ...}` and
indexed collection of `finset`s of morphisms `{f, ...}` in `C`,
there exists an object `S`, with a morphism `T X : S ‚ü∂ X` from each `X`,
such that the triangles commute: `T X ‚â´ f = T Y`, for `f : X ‚ü∂ Y` in the `finset`.
-/
lemma inf_exists :
  ‚àÉ (S : C) (T : Œ† {X : C}, X ‚àà O ‚Üí (S ‚ü∂ X)), ‚àÄ {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y},
    (‚ü®X, Y, mX, mY, f‚ü© : (Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y)) ‚àà H ‚Üí T mX ‚â´ f = T mY :=
begin
  classical,
  apply finset.induction_on H,
  { obtain ‚ü®S, f‚ü© := inf_objs_exists O,
    refine ‚ü®S, Œª X mX, (f mX).some, _‚ü©,
    rintros - - - - - ‚ü®‚ü©, },
  { rintros ‚ü®X, Y, mX, mY, f‚ü© H' nmf ‚ü®S', T', w'‚ü©,
    refine ‚ü®eq (T' mX ‚â´ f) (T' mY), Œª Z mZ, eq_hom (T' mX ‚â´ f) (T' mY) ‚â´ T' mZ, _‚ü©,
    intros X' Y' mX' mY' f' mf',
    rw [category.assoc],
    by_cases h : X = X' ‚àß Y = Y',
    { rcases h with ‚ü®rfl, rfl‚ü©,
      by_cases hf : f = f',
      { subst hf,
        apply eq_condition, },
      { rw @w' _ _ mX mY f' (by simpa [hf ‚àò eq.symm] using mf') }, },
    { rw @w' _ _ mX' mY' f' _,
      apply finset.mem_of_mem_insert_of_ne mf',
      contrapose! h,
      obtain ‚ü®rfl, h‚ü© := h,
      rw [heq_iff_eq, psigma.mk.inj_iff] at h,
      exact ‚ü®rfl, h.1.symm‚ü© }, },
end

/--
An arbitrary choice of object "to the left"
of a finite collection of objects `O` and morphisms `H`,
making all the triangles commute.
-/
noncomputable
def inf : C :=
(inf_exists O H).some

/--
The morphisms from `inf O H`.
-/
noncomputable
def inf_to {X : C} (m : X ‚àà O) :
  inf O H ‚ü∂ X :=
(inf_exists O H).some_spec.some m

/--
The triangles consisting of a morphism in `H` and the maps from `inf O H` commute.
-/
lemma inf_to_commutes
  {X Y : C} (mX : X ‚àà O) (mY : Y ‚àà O) {f : X ‚ü∂ Y}
  (mf : (‚ü®X, Y, mX, mY, f‚ü© : Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y) ‚àà H) :
  inf_to O H mX ‚â´ f = inf_to O H mY :=
(inf_exists O H).some_spec.some_spec mX mY mf

variables {J : Type w} [small_category J] [fin_category J]

/--
If we have `is_cofiltered C`, then for any functor `F : J ‚•§ C` with `fin_category J`,
there exists a cone over `F`.
-/
lemma cone_nonempty (F : J ‚•§ C) : _root_.nonempty (cone F) :=
begin
  classical,
  let O := (finset.univ.image F.obj),
  let H : finset (Œ£' (X Y : C) (mX : X ‚àà O) (mY : Y ‚àà O), X ‚ü∂ Y) :=
    finset.univ.bUnion (Œª X : J, finset.univ.bUnion (Œª Y : J, finset.univ.image (Œª f : X ‚ü∂ Y,
      ‚ü®F.obj X, F.obj Y, by simp, by simp, F.map f‚ü©))),
  obtain ‚ü®Z, f, w‚ü© := inf_exists O H,
  refine ‚ü®‚ü®Z, ‚ü®Œª X, f (by simp), _‚ü©‚ü©‚ü©,
  intros j j' g,
  dsimp,
  simp only [category.id_comp],
  symmetry,
  apply w,
  simp only [finset.mem_univ, finset.mem_bUnion, exists_and_distrib_left,
    exists_prop_of_true, finset.mem_image],
  exact ‚ü®j, rfl, j', g, (by simp)‚ü©,
end

/--
An arbitrary choice of cone over `F : J ‚•§ C`, for `fin_category J` and `is_cofiltered C`.
-/
noncomputable def cone (F : J ‚•§ C) : cone F :=
(cone_nonempty F).some

variables {D : Type u‚ÇÅ} [category.{v‚ÇÅ} D]

/--
If `C` is cofiltered, and we have a functor `L : C ‚•§ D` with a right adjoint,
then `D` is cofiltered.
-/
lemma of_left_adjoint {L : C ‚•§ D} {R : D ‚•§ C} (h : L ‚ä£ R) : is_cofiltered D :=
{ cone_objs := Œª X Y,
    ‚ü®L.obj (min (R.obj X) (R.obj Y)),
      (h.hom_equiv _ X).symm (min_to_left _ _), (h.hom_equiv _ Y).symm (min_to_right _ _), ‚ü®‚ü©‚ü©,
  cone_maps := Œª X Y f g,
    ‚ü®L.obj (eq (R.map f) (R.map g)), (h.hom_equiv _ _).symm (eq_hom _ _),
     by rw [‚Üê h.hom_equiv_naturality_right_symm, ‚Üê h.hom_equiv_naturality_right_symm,
       eq_condition]‚ü©,
  nonempty := is_cofiltered.nonempty.map L.obj }

/-- If `C` is cofiltered, and we have a left adjoint functor `L : C ‚•§ D`, then `D` is cofiltered. -/
lemma of_is_left_adjoint (L : C ‚•§ D) [is_left_adjoint L] : is_cofiltered D :=
of_left_adjoint (adjunction.of_left_adjoint L)

/-- Being cofiltered is preserved by equivalence of categories. -/
lemma of_equivalence (h : C ‚âå D) : is_cofiltered D :=
of_left_adjoint h.to_adjunction

end nonempty

end is_cofiltered

section opposite
open opposite

instance is_cofiltered_op_of_is_filtered [is_filtered C] : is_cofiltered C·µí·µñ :=
{ cone_objs := Œª X Y, ‚ü®op (is_filtered.max X.unop Y.unop),
    (is_filtered.left_to_max _ _).op, (is_filtered.right_to_max _ _).op, trivial‚ü©,
  cone_maps := Œª X Y f g, ‚ü®op (is_filtered.coeq f.unop g.unop),
    (is_filtered.coeq_hom _ _).op, begin
      rw [(show f = f.unop.op, by simp), (show g = g.unop.op, by simp),
        ‚Üê op_comp, ‚Üê op_comp],
      congr' 1,
      exact is_filtered.coeq_condition f.unop g.unop,
    end‚ü©,
  nonempty := ‚ü®op is_filtered.nonempty.some‚ü© }

instance is_filtered_op_of_is_cofiltered [is_cofiltered C] : is_filtered C·µí·µñ :=
{ cocone_objs := Œª X Y, ‚ü®op (is_cofiltered.min X.unop Y.unop),
    (is_cofiltered.min_to_left X.unop Y.unop).op,
    (is_cofiltered.min_to_right X.unop Y.unop).op, trivial‚ü©,
  cocone_maps := Œª X Y f g, ‚ü®op (is_cofiltered.eq f.unop g.unop),
    (is_cofiltered.eq_hom f.unop g.unop).op, begin
      rw [(show f = f.unop.op, by simp), (show g = g.unop.op, by simp),
        ‚Üê op_comp, ‚Üê op_comp],
      congr' 1,
      exact is_cofiltered.eq_condition f.unop g.unop,
    end‚ü©,
  nonempty := ‚ü®op is_cofiltered.nonempty.some‚ü© }

end opposite

section ulift

instance [is_filtered C] : is_filtered (ulift.{u‚ÇÇ} C) :=
is_filtered.of_equivalence ulift.equivalence

instance [is_cofiltered C] : is_cofiltered (ulift.{u‚ÇÇ} C) :=
is_cofiltered.of_equivalence ulift.equivalence

instance [is_filtered C] : is_filtered (ulift_hom C) :=
is_filtered.of_equivalence ulift_hom.equiv

instance [is_cofiltered C] : is_cofiltered (ulift_hom C) :=
is_cofiltered.of_equivalence ulift_hom.equiv

instance [is_filtered C] : is_filtered (as_small C) :=
is_filtered.of_equivalence as_small.equiv

instance [is_cofiltered C] : is_cofiltered (as_small C) :=
is_cofiltered.of_equivalence as_small.equiv

end ulift

end category_theory
