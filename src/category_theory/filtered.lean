/-
Copyright (c) 2019 Reid Barton. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton
-/
import category_theory.fin_category
import category_theory.limits.cones
import order.bounded_lattice
import tactic.slice

/-!
# Filtered categories

A category is filtered if every finite diagram admits a cocone.
We give a simple characterisation of this condition as
1. for every pair of objects there exists another object "to the right",
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal, and
3. there exists some object.

Filtered colimits are often better behaved than arbitrary colimits.
See `category_theory/limits/types` for some details.

Filtered categories are nice because colimits indexed by filtered categories tend to be
easier to describe than general colimits (and often often preserved by functors).

In this file we show that any functor from a finite category to a filtered category admits a cocone:
* `cocone_nonempty [fin_category J] [is_filtered C] (F : J â¥¤ C) : nonempty (cocone F)`
More generally,
for any finite collection of objects and morphisms between them in a filtered category
(even if not closed under composition) there exists some object `Z` receiving maps from all of them,
so that all the triangles (one edge from the finite set, two from morphisms to `Z`) commute.
This formulation is often more useful in practice. We give two variants,
`sup_exists'`, which takes a single finset of objects, and a finset of morphisms
(bundled with their sources and targets), and
`sup_exists`, which takes a finset of objects, and an indexed family (indexed by source and target)
of finsets of morphisms.

## Future work
* Finite limits commute with filtered colimits
* Forgetful functors for algebraic categories typically preserve filtered colimits.
-/

universes v u -- declare the `v`'s first; see `category_theory.category` for an explanation

namespace category_theory

variables (C : Type u) [category.{v} C]

/--
A category `is_filtered_or_empty` if
1. for every pair of objects there exists another object "to the right", and
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal.
-/
class is_filtered_or_empty : Prop :=
(cocone_objs : âˆ€ (X Y : C), âˆƒ Z (f : X âŸ¶ Z) (g : Y âŸ¶ Z), true)
(cocone_maps : âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), âˆƒ Z (h : Y âŸ¶ Z), f â‰« h = g â‰« h)

/--
A category `is_filtered` if
1. for every pair of objects there exists another object "to the right",
2. for every pair of parallel morphisms there exists a morphism to the right so the compositions
   are equal, and
3. there exists some object.

See https://stacks.math.columbia.edu/tag/002V. (They also define a diagram being filtered.)
-/
class is_filtered extends is_filtered_or_empty C : Prop :=
[nonempty : nonempty C]

@[priority 100]
instance is_filtered_or_empty_of_semilattice_sup
  (Î± : Type u) [semilattice_sup Î±] : is_filtered_or_empty Î± :=
{ cocone_objs := Î» X Y, âŸ¨X âŠ” Y, hom_of_le le_sup_left, hom_of_le le_sup_right, trivialâŸ©,
  cocone_maps := Î» X Y f g, âŸ¨Y, ğŸ™ _, (by ext)âŸ©, }

@[priority 100]
instance is_filtered_of_semilattice_sup_top
  (Î± : Type u) [semilattice_sup_top Î±] : is_filtered Î± :=
{ nonempty := âŸ¨âŠ¤âŸ©,
  ..category_theory.is_filtered_or_empty_of_semilattice_sup Î± }

namespace is_filtered

variables {C} [is_filtered C]

/--
`max j j'` is an arbitrary choice of object to the right of both `j` and `j'`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def max (j j' : C) : C :=
(is_filtered_or_empty.cocone_objs j j').some

/--
`left_to_max j j'` is an arbitrarily choice of morphism from `j` to `max j j'`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def left_to_max (j j' : C) : j âŸ¶ max j j' :=
(is_filtered_or_empty.cocone_objs j j').some_spec.some

/--
`right_to_max j j'` is an arbitrarily choice of morphism from `j'` to `max j j'`,
whose existence is ensured by `is_filtered`.
-/
noncomputable def right_to_max (j j' : C) : j' âŸ¶ max j j' :=
(is_filtered_or_empty.cocone_objs j j').some_spec.some_spec.some

/--
`coeq f f'`, for morphisms `f f' : j âŸ¶ j'`, is an arbitrary choice of object
which admits a morphism `coeq_hom f f' : j' âŸ¶ coeq f f'` such that
`coeq_condition : f â‰« coeq_hom f f' = f' â‰« coeq_hom f f'`.
Its existence is ensured by `is_filtered`.
-/
noncomputable def coeq {j j' : C} (f f' : j âŸ¶ j') : C :=
(is_filtered_or_empty.cocone_maps f f').some

/--
`coeq_hom f f'`, for morphisms `f f' : j âŸ¶ j'`, is an arbitrary choice of morphism
`coeq_hom f f' : j' âŸ¶ coeq f f'` such that
`coeq_condition : f â‰« coeq_hom f f' = f' â‰« coeq_hom f f'`.
Its existence is ensured by `is_filtered`.
-/
noncomputable def coeq_hom {j j' : C} (f f' : j âŸ¶ j') : j' âŸ¶ coeq f f' :=
(is_filtered_or_empty.cocone_maps f f').some_spec.some

/--
`coeq_condition f f'`, for morphisms `f f' : j âŸ¶ j'`, is the proof that
`f â‰« coeq_hom f f' = f' â‰« coeq_hom f f'`.
-/
lemma coeq_condition {j j' : C} (f f' : j âŸ¶ j') : f â‰« coeq_hom f f' = f' â‰« coeq_hom f f' :=
(is_filtered_or_empty.cocone_maps f f').some_spec.some_spec

open category_theory.limits

/--
Any finite collection of objects in a filtered category has an object "to the right".
-/
lemma sup_objs_exists (O : finset C) : âˆƒ (S : C), âˆ€ {X}, X âˆˆ O â†’ _root_.nonempty (X âŸ¶ S) :=
begin
  classical,
  apply finset.induction_on O,
  { exact âŸ¨is_filtered.nonempty.some, (by rintros - âŸ¨âŸ©)âŸ©, },
  { rintros X O' nm âŸ¨S', w'âŸ©,
    use max X S',
    rintros Y mY,
    by_cases h : X = Y,
    { subst h, exact âŸ¨left_to_max _ _âŸ©, },
    { exact âŸ¨(w' (by finish)).some â‰« right_to_max _ _âŸ©, }, }
end

variables (O : finset C) (H : finset (Î£' (X Y : C) (mX : X âˆˆ O) (mY : Y âˆˆ O), X âŸ¶ Y))

/--
Given any `finset` of objects `{X, ...}` and
indexed collection of `finset`s of morphisms `{f, ...}` in `C`,
there exists an object `S`, with a morphism `T X : X âŸ¶ S` from each `X`,
such that the triangles commute: `f â‰« T X = T Y`, for `f : X âŸ¶ Y` in the `finset`.
-/
lemma sup_exists :
  âˆƒ (S : C) (T : Î  {X : C}, X âˆˆ O â†’ (X âŸ¶ S)), âˆ€ {X Y : C} (mX : X âˆˆ O) (mY : Y âˆˆ O) {f : X âŸ¶ Y},
    (âŸ¨X, Y, mX, mY, fâŸ© : (Î£' (X Y : C) (mX : X âˆˆ O) (mY : Y âˆˆ O), X âŸ¶ Y)) âˆˆ H â†’ f â‰« T mY = T mX :=
begin
  classical,
  apply finset.induction_on H,
  { obtain âŸ¨S, fâŸ© := sup_objs_exists O,
    refine âŸ¨S, Î» X mX, (f mX).some, _âŸ©,
    rintros - - - - - âŸ¨âŸ©, },
  { rintros âŸ¨X, Y, mX, mY, fâŸ© H' nmf âŸ¨S', T', w'âŸ©,
    refine âŸ¨coeq (f â‰« T' mY) (T' mX), Î» Z mZ, T' mZ â‰« coeq_hom (f â‰« T' mY) (T' mX), _âŸ©,
    intros X' Y' mX' mY' f' mf',
    rw [â†category.assoc],
    by_cases h : X = X' âˆ§ Y = Y',
    { rcases h with âŸ¨rfl, rflâŸ©,
      by_cases hf : f = f',
      { subst hf,
        apply coeq_condition, },
      { rw w' _ _ (by finish), }, },
    { rw w' _ _ (by finish), }, },
end

/--
An arbitrary choice of object "to the right" of a finite collection of objects `O` and morphisms `H`,
making all the triangles commute.
-/
noncomputable
def sup : C :=
(sup_exists O H).some

/--
The morphisms to `sup O H`.
-/
noncomputable
def to_sup {X : C} (m : X âˆˆ O) :
  X âŸ¶ sup O H :=
(sup_exists O H).some_spec.some m

/--
The triangles of consisting of a morphism in `H` and the maps to `sup O H` commute.
-/
lemma to_sup_commutes
  {X Y : C} (mX : X âˆˆ O) (mY : Y âˆˆ O) {f : X âŸ¶ Y}
  (mf : (âŸ¨X, Y, mX, mY, fâŸ© : Î£' (X Y : C) (mX : X âˆˆ O) (mY : Y âˆˆ O), X âŸ¶ Y) âˆˆ H) :
  f â‰« to_sup O H mY = to_sup O H mX :=
(sup_exists O H).some_spec.some_spec mX mY mf

variables {J : Type v} [small_category J] [fin_category J]

/--
If we have `is_filtered C`, then for any functor `F : J â¥¤ C` with `fin_category J`,
there exists a cocone over `F`.
-/
lemma cocone_nonempty (F : J â¥¤ C) : _root_.nonempty (cocone F) :=
begin
  classical,
  let O := (finset.univ.image F.obj),
  let H : finset (Î£' (X Y : C) (mX : X âˆˆ O) (mY : Y âˆˆ O), X âŸ¶ Y) :=
    finset.univ.bind (Î» X : J, finset.univ.bind (Î» Y : J, finset.univ.image (Î» f : X âŸ¶ Y,
      âŸ¨F.obj X, F.obj Y, by simp, by simp, F.map fâŸ©))),
  obtain âŸ¨Z, f, wâŸ© := sup_exists O H,
  refine âŸ¨âŸ¨Z, âŸ¨Î» X, f (by simp), _âŸ©âŸ©âŸ©,
  intros j j' g,
  dsimp,
  simp only [category.comp_id],
  apply w,
  simp only [finset.mem_univ, finset.mem_bind, exists_and_distrib_left,
    exists_prop_of_true, finset.mem_image],
  exact âŸ¨j, rfl, j', g, (by simp)âŸ©,
end

/--
An arbitrary choice of cocone over `F : J â¥¤ C`, for `fin_category J` and `is_filtered C`.
-/
noncomputable def cocone (F : J â¥¤ C) : cocone F :=
(cocone_nonempty F).some

/--
Given a "bowtie" of morphisms
```
jx -> jâ‚
  \  ^
   \/
   /\
  /  v
jy -> jâ‚‚
```
in a filtered category, construct a morphism `j` sitting to the right of `jâ‚` and `jâ‚‚`,
making both of the resulting squares commute.
-/
noncomputable lemma bowtie {jx jy jâ‚ jâ‚‚ : C}
  (ixâ‚ : jx âŸ¶ jâ‚) (ixâ‚‚ : jx âŸ¶ jâ‚‚) (iyâ‚ : jy âŸ¶ jâ‚) (iyâ‚‚ : jy âŸ¶ jâ‚‚) :
  Î£' (j : C) (iâ‚ : jâ‚ âŸ¶ j) (iâ‚‚ : jâ‚‚ âŸ¶ j), ixâ‚ â‰« iâ‚ = ixâ‚‚ â‰« iâ‚‚ âˆ§ iyâ‚ â‰« iâ‚ = iyâ‚‚ â‰« iâ‚‚ :=
begin
  let ja := max jâ‚ jâ‚‚,
  let jb := coeq (ixâ‚ â‰« left_to_max _ _) (ixâ‚‚ â‰« right_to_max _ _),
  let jc := coeq (iyâ‚ â‰« left_to_max _ _) (iyâ‚‚ â‰« right_to_max _ _),
  let jd := max jb jc,
  let j := coeq ((coeq_hom _ _ : ja âŸ¶ jb) â‰« left_to_max _ _) ((coeq_hom _ _ : ja âŸ¶ jc) â‰« right_to_max _ _),
  use j,
  fsplit,
  exact left_to_max jâ‚ jâ‚‚ â‰« coeq_hom _ _ â‰« left_to_max jb jc â‰« coeq_hom _ _,
  fsplit,
  exact right_to_max jâ‚ jâ‚‚ â‰« coeq_hom _ _ â‰« right_to_max jb jc â‰« coeq_hom _ _,
  fsplit,
  { slice_lhs 1 3 { rw [â†category.assoc, coeq_condition], },
    slice_lhs 3 5 { rw [â†category.assoc, coeq_condition], },
    simp only [category.assoc], },
  { slice_lhs 3 5 { rw [â†category.assoc, coeq_condition], },
    slice_lhs 1 3 { rw [â†category.assoc, coeq_condition], },
    simp only [category.assoc], }
end

end is_filtered

end category_theory
