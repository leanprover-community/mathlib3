/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Scott Morrison

Defines a functor between categories.

(As it is a 'bundled' object rather than the `is_functorial` typeclass parametrised
by the underlying function on objects, the name is capitalised.)

Introduces notations
  `C â¥¤ D` for the type of all functors from `C` to `D`.
    (I would like a better arrow here, unfortunately â‡’ (`\functor`) is taken by core.)
-/
import tactic.reassoc_axiom
import tactic.monotonicity

namespace category_theory

-- declare the `v`'s first; see `category_theory.category` for an explanation
universes v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

/--
`functor C D` represents a functor between categories `C` and `D`.

To apply a functor `F` to an object use `F.obj X`, and to a morphism use `F.map f`.

The axiom `map_id` expresses preservation of identities, and
`map_comp` expresses functoriality.

See https://stacks.math.columbia.edu/tag/001B.
-/
structure functor (C : Type uâ‚) [category.{vâ‚} C] (D : Type uâ‚‚) [category.{vâ‚‚} D] :
  Type (max vâ‚ vâ‚‚ uâ‚ uâ‚‚) :=
(obj []    : C â†’ D)
(map       : Î  {X Y : C}, (X âŸ¶ Y) â†’ ((obj X) âŸ¶ (obj Y)))
(map_id'   : âˆ€ (X : C), map (ğŸ™ X) = ğŸ™ (obj X) . obviously)
(map_comp' : âˆ€ {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z), map (f â‰« g) = (map f) â‰« (map g) . obviously)

-- A functor is basically a function, so give â¥¤ a similar precedence to â†’ (25).
-- For example, `C Ã— D â¥¤ E` should parse as `(C Ã— D) â¥¤ E` not `C Ã— (D â¥¤ E)`.
infixr ` â¥¤ `:26 := functor       -- type as \func --

restate_axiom functor.map_id'
attribute [simp] functor.map_id
restate_axiom functor.map_comp'
attribute [reassoc, simp] functor.map_comp

namespace functor

section
variables (C : Type uâ‚) [category.{vâ‚} C]

/-- `ğŸ­ C` is the identity functor on a category `C`. -/
protected def id : C â¥¤ C :=
{ obj := Î» X, X,
  map := Î» _ _ f, f }

notation `ğŸ­` := functor.id -- Type this as `\sb1`

instance : inhabited (C â¥¤ C) := âŸ¨functor.id CâŸ©

variable {C}

@[simp] lemma id_obj (X : C) : (ğŸ­ C).obj X = X := rfl
@[simp] lemma id_map {X Y : C} (f : X âŸ¶ Y) : (ğŸ­ C).map f = f := rfl
end

section
variables {C : Type uâ‚} [category.{vâ‚} C]
          {D : Type uâ‚‚} [category.{vâ‚‚} D]
          {E : Type uâ‚ƒ} [category.{vâ‚ƒ} E]

/--
`F â‹™ G` is the composition of a functor `F` and a functor `G` (`F` first, then `G`).
-/
def comp (F : C â¥¤ D) (G : D â¥¤ E) : C â¥¤ E :=
{ obj := Î» X, G.obj (F.obj X),
  map := Î» _ _ f, G.map (F.map f) }

infixr ` â‹™ `:80 := comp

@[simp] lemma comp_obj (F : C â¥¤ D) (G : D â¥¤ E) (X : C) : (F â‹™ G).obj X = G.obj (F.obj X) := rfl
@[simp] lemma comp_map (F : C â¥¤ D) (G : D â¥¤ E) {X Y : C} (f : X âŸ¶ Y) :
  (F â‹™ G).map f = G.map (F.map f) := rfl

-- These are not simp lemmas because rewriting along equalities between functors
-- is not necessarily a good idea.
-- Natural isomorphisms are also provided in `whiskering.lean`.
protected lemma comp_id (F : C â¥¤ D) : F â‹™ (ğŸ­ D) = F := by cases F; refl
protected lemma id_comp (F : C â¥¤ D) : (ğŸ­ C) â‹™ F = F := by cases F; refl
end

section
variables {B : Type u} [â„¬ : category.{v} B]
          {C : Type uâ‚} [ğ’ : category.{vâ‚} C]
          {D : Type uâ‚‚} [ğ’Ÿ : category.{vâ‚‚} D]
          {E : Type uâ‚ƒ} [â„° : category.{vâ‚ƒ} E]
include â„¬ ğ’ ğ’Ÿ â„°
protected lemma assoc (F : B â¥¤ C) (G : C â¥¤ D) (H : D â¥¤ E) : (F â‹™ G) â‹™ H = F â‹™ (G â‹™ H) := rfl
end

@[mono] lemma monotone {Î± Î² : Type*} [preorder Î±] [preorder Î²] (F : Î± â¥¤ Î²) :
  monotone F.obj :=
Î» a b h, le_of_hom (F.map (hom_of_le h))

end functor

end category_theory
