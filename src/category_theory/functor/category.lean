/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import category_theory.natural_transformation
import category_theory.isomorphism

/-!
# The category of functors and natural transformations between two fixed categories.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We provide the category instance on `C â¥¤ D`, with morphisms the natural transformations.

## Universes

If `C` and `D` are both small categories at the same universe level,
this is another small category at that level.
However if `C` and `D` are both large categories at the same universe level,
this is a small category at the next higher level.
-/

namespace category_theory

-- declare the `v`'s first; see `category_theory.category` for an explanation
universes vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open nat_trans category category_theory.functor

variables (C : Type uâ‚) [category.{vâ‚} C] (D : Type uâ‚‚) [category.{vâ‚‚} D]

local attribute [simp] vcomp_app
/--
`functor.category C D` gives the category structure on functors and natural transformations
between categories `C` and `D`.

Notice that if `C` and `D` are both small categories at the same universe level,
this is another small category at that level.
However if `C` and `D` are both large categories at the same universe level,
this is a small category at the next higher level.
-/
instance functor.category : category.{(max uâ‚ vâ‚‚)} (C â¥¤ D) :=
{ hom     := Î» F G, nat_trans F G,
  id      := Î» F, nat_trans.id F,
  comp    := Î» _ _ _ Î± Î², vcomp Î± Î² }

variables {C D} {E : Type uâ‚ƒ} [category.{vâ‚ƒ} E]
variables {F G H I : C â¥¤ D}

namespace nat_trans

@[simp] lemma vcomp_eq_comp (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) : vcomp Î± Î² = Î± â‰« Î² := rfl

lemma vcomp_app' (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (X : C) :
  (Î± â‰« Î²).app X = (Î±.app X) â‰« (Î².app X) := rfl

lemma congr_app {Î± Î² : F âŸ¶ G} (h : Î± = Î²) (X : C) : Î±.app X = Î².app X := by rw h
@[simp] lemma id_app (F : C â¥¤ D) (X : C) : (ğŸ™ F : F âŸ¶ F).app X = ğŸ™ (F.obj X) := rfl
@[simp] lemma comp_app {F G H : C â¥¤ D} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H) (X : C) :
  (Î± â‰« Î²).app X = Î±.app X â‰« Î².app X := rfl

lemma app_naturality {F G : C â¥¤ (D â¥¤ E)} (T : F âŸ¶ G) (X : C) {Y Z : D} (f : Y âŸ¶ Z) :
  ((F.obj X).map f) â‰« ((T.app X).app Z) = ((T.app X).app Y) â‰« ((G.obj X).map f) :=
(T.app X).naturality f

lemma naturality_app {F G : C â¥¤ (D â¥¤ E)} (T : F âŸ¶ G) (Z : D) {X Y : C} (f : X âŸ¶ Y) :
  ((F.map f).app Z) â‰« ((T.app Y).app Z) = ((T.app X).app Z) â‰« ((G.map f).app Z) :=
congr_fun (congr_arg app (T.naturality f)) Z

/-- A natural transformation is a monomorphism if each component is. -/
lemma mono_of_mono_app (Î± : F âŸ¶ G) [âˆ€ (X : C), mono (Î±.app X)] : mono Î± :=
âŸ¨Î» H g h eq, by { ext X, rw [â†cancel_mono (Î±.app X), â†comp_app, eq, comp_app] }âŸ©

/-- A natural transformation is an epimorphism if each component is. -/
lemma epi_of_epi_app (Î± : F âŸ¶ G) [âˆ€ (X : C), epi (Î±.app X)] : epi Î± :=
âŸ¨Î» H g h eq, by { ext X, rw [â†cancel_epi (Î±.app X), â†comp_app, eq, comp_app] }âŸ©

/-- `hcomp Î± Î²` is the horizontal composition of natural transformations. -/
@[simps] def hcomp {H I : D â¥¤ E} (Î± : F âŸ¶ G) (Î² : H âŸ¶ I) : (F â‹™ H) âŸ¶ (G â‹™ I) :=
{ app         := Î» X : C, (Î².app (F.obj X)) â‰« (I.map (Î±.app X)),
  naturality' := Î» X Y f,
  begin
    rw [functor.comp_map, functor.comp_map, â†assoc, naturality, assoc,
        â†map_comp I, naturality, map_comp, assoc]
  end }

infix ` â—« `:80 := hcomp

@[simp] lemma hcomp_id_app {H : D â¥¤ E} (Î± : F âŸ¶ G) (X : C) : (Î± â—« ğŸ™ H).app X = H.map (Î±.app X) :=
  by {dsimp, simp} -- See note [dsimp, simp].

lemma id_hcomp_app {H : E â¥¤ C} (Î± : F âŸ¶ G) (X : E) : (ğŸ™ H â—« Î±).app X = Î±.app _ := by simp

-- Note that we don't yet prove a `hcomp_assoc` lemma here: even stating it is painful, because we
-- need to use associativity of functor composition. (It's true without the explicit associator,
-- because functor composition is definitionally associative,
-- but relying on the definitional equality causes bad problems with elaboration later.)

lemma exchange {I J K : D â¥¤ E} (Î± : F âŸ¶ G) (Î² : G âŸ¶ H)
  (Î³ : I âŸ¶ J) (Î´ : J âŸ¶ K) : (Î± â‰« Î²) â—« (Î³ â‰« Î´) = (Î± â—« Î³) â‰« (Î² â—« Î´) :=
by ext; simp

end nat_trans
open nat_trans
namespace functor

/-- Flip the arguments of a bifunctor. See also `currying.lean`. -/
@[simps] protected def flip (F : C â¥¤ (D â¥¤ E)) : D â¥¤ (C â¥¤ E) :=
{ obj := Î» k,
  { obj := Î» j, (F.obj j).obj k,
    map := Î» j j' f, (F.map f).app k,
    map_id' := Î» X, begin rw category_theory.functor.map_id, refl end,
    map_comp' := Î» X Y Z f g, by rw [map_comp, â†comp_app] },
  map := Î» c c' f,
  { app := Î» j, (F.obj j).map f } }.

end functor

@[simp, reassoc] lemma map_hom_inv_app (F : C â¥¤ D â¥¤ E) {X Y : C} (e : X â‰… Y) (Z : D) :
  (F.map e.hom).app Z â‰« (F.map e.inv).app Z = ğŸ™ _ :=
by simp [â† nat_trans.comp_app, â† functor.map_comp]

@[simp, reassoc] lemma map_inv_hom_app (F : C â¥¤ D â¥¤ E) {X Y : C} (e : X â‰… Y) (Z : D) :
  (F.map e.inv).app Z â‰« (F.map e.hom).app Z = ğŸ™ _ :=
by simp [â† nat_trans.comp_app, â† functor.map_comp]

end category_theory
