/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.opposites

/-!
# The constant functor

`const J : C â¥¤ (J â¥¤ C)` is the functor that sends an object `X : C` to the functor `J â¥¤ C` sending
every object in `J` to `X`, and every morphism to `ğŸ™ X`.

When `J` is nonempty, `const` is faithful.

We have `(const J).obj X â‹™ F â‰… (const J).obj (F.obj X)` for any `F : C â¥¤ D`.
-/

-- declare the `v`'s first; see `category_theory.category` for an explanation
universes vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open category_theory

namespace category_theory.functor

variables (J : Type uâ‚) [category.{vâ‚} J]
variables {C : Type uâ‚‚} [category.{vâ‚‚} C]

/--
The functor sending `X : C` to the constant functor `J â¥¤ C` sending everything to `X`.
-/
def const : C â¥¤ (J â¥¤ C) :=
{ obj := Î» X,
  { obj := Î» j, X,
    map := Î» j j' f, ğŸ™ X },
  map := Î» X Y f, { app := Î» j, f } }

namespace const
open opposite

variables {J}

@[simp] lemma obj_obj (X : C) (j : J) : ((const J).obj X).obj j = X := rfl
@[simp] lemma obj_map (X : C) {j j' : J} (f : j âŸ¶ j') : ((const J).obj X).map f = ğŸ™ X := rfl
@[simp] lemma map_app {X Y : C} (f : X âŸ¶ Y) (j : J) : ((const J).map f).app j = f := rfl

/--
The contant functor `Jáµ’áµ– â¥¤ Cáµ’áµ–` sending everything to `op X`
is (naturally isomorphic to) the opposite of the constant functor `J â¥¤ C` sending everything to `X`.
-/
def op_obj_op (X : C) :
  (const Jáµ’áµ–).obj (op X) â‰… ((const J).obj X).op :=
{ hom := { app := Î» j, ğŸ™ _ },
  inv := { app := Î» j, ğŸ™ _ } }

@[simp] lemma op_obj_op_hom_app (X : C) (j : Jáµ’áµ–) : (op_obj_op X).hom.app j = ğŸ™ _ := rfl
@[simp] lemma op_obj_op_inv_app (X : C) (j : Jáµ’áµ–) : (op_obj_op X).inv.app j = ğŸ™ _ := rfl

/--
The contant functor `Jáµ’áµ– â¥¤ C` sending everything to `unop X`
is (naturally isomorphic to) the opposite of
the constant functor `J â¥¤ Cáµ’áµ–` sending everything to `X`.
-/
def op_obj_unop (X : Cáµ’áµ–) :
  (const Jáµ’áµ–).obj (unop X) â‰… ((const J).obj X).left_op :=
{ hom := { app := Î» j, ğŸ™ _ },
  inv := { app := Î» j, ğŸ™ _ } }

-- Lean needs some help with universes here.
@[simp] lemma op_obj_unop_hom_app (X : Cáµ’áµ–) (j : Jáµ’áµ–) : (op_obj_unop.{vâ‚ vâ‚‚} X).hom.app j = ğŸ™ _ :=
rfl
@[simp] lemma op_obj_unop_inv_app (X : Cáµ’áµ–) (j : Jáµ’áµ–) : (op_obj_unop.{vâ‚ vâ‚‚} X).inv.app j = ğŸ™ _ :=
rfl

@[simp] lemma unop_functor_op_obj_map (X : Cáµ’áµ–) {jâ‚ jâ‚‚ : J} (f : jâ‚ âŸ¶ jâ‚‚) :
  (unop ((functor.op (const J)).obj X)).map f = ğŸ™ (unop X) := rfl
end const



section
variables {D : Type uâ‚ƒ} [category.{vâ‚ƒ} D]

/-- These are actually equal, of course, but not definitionally equal
  (the equality requires F.map (ğŸ™ _) = ğŸ™ _). A natural isomorphism is
  more convenient than an equality between functors (compare id_to_iso). -/
@[simps] def const_comp (X : C) (F : C â¥¤ D) :
  (const J).obj X â‹™ F â‰… (const J).obj (F.obj X) :=
{ hom := { app := Î» _, ğŸ™ _ },
  inv := { app := Î» _, ğŸ™ _ } }

/-- If `J` is nonempty, then the constant functor over `J` is faithful. -/
instance [nonempty J] : faithful (const J : C â¥¤ J â¥¤ C) :=
{ map_injective' := Î» X Y f g e, nat_trans.congr_app e (classical.arbitrary J) }

end

end category_theory.functor
