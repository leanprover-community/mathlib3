/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import category_theory.limits.filtered_colimit_commutes_finite_limit
import category_theory.limits.preserves.functor_category
import category_theory.limits.bicones
import category_theory.limits.comma
import category_theory.limits.preserves.finite
import category_theory.limits.shapes.finite_limits

/-!
# Representably flat functors

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define representably flat functors as functors such that the category of structured arrows
over `X` is cofiltered for each `X`. This concept is also known as flat functors as in [Elephant]
Remark C2.3.7, and this name is suggested by Mike Shulman in
https://golem.ph.utexas.edu/category/2011/06/flat_functors_and_morphisms_of.html to avoid
confusion with other notions of flatness.

This definition is equivalent to left exact functors (functors that preserves finite limits) when
`C` has all finite limits.

## Main results

* `flat_of_preserves_finite_limits`: If `F : C â¥¤ D` preserves finite limits and `C` has all finite
  limits, then `F` is flat.
* `preserves_finite_limits_of_flat`: If `F : C â¥¤ D` is flat, then it preserves all finite limits.
* `preserves_finite_limits_iff_flat`: If `C` has all finite limits,
  then `F` is flat iff `F` is left_exact.
* `Lan_preserves_finite_limits_of_flat`: If `F : C â¥¤ D` is a flat functor between small categories,
  then the functor `Lan F.op` between presheaves of sets preserves all finite limits.
* `flat_iff_Lan_flat`: If `C`, `D` are small and `C` has all finite limits, then `F` is flat iff
  `Lan F.op : (Cáµ’áµ– â¥¤ Type*) â¥¤ (Dáµ’áµ– â¥¤ Type*)` is flat.
* `preserves_finite_limits_iff_Lan_preserves_finite_limits`: If `C`, `D` are small and `C` has all
  finite limits, then `F` preserves finite limits iff `Lan F.op : (Cáµ’áµ– â¥¤ Type*) â¥¤ (Dáµ’áµ– â¥¤ Type*)`
  does.

-/

universes w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open category_theory
open category_theory.limits
open opposite

namespace category_theory


namespace structured_arrow_cone
open structured_arrow
variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚} D]
variables {J : Type w} [small_category J]
variables {K : J â¥¤ C} (F : C â¥¤ D) (c : cone K)

/--
Given a cone `c : cone K` and a map `f : X âŸ¶ c.X`, we can construct a cone of structured
arrows over `X` with `f` as the cone point. This is the underlying diagram.
-/
@[simps]
def to_diagram : J â¥¤ structured_arrow c.X K :=
{ obj := Î» j, structured_arrow.mk (c.Ï€.app j),
  map := Î» j k g, structured_arrow.hom_mk g (by simpa) }

/-- Given a diagram of `structured_arrow X F`s, we may obtain a cone with cone point `X`. -/
@[simps]
def diagram_to_cone {X : D} (G : J â¥¤ structured_arrow X F) : cone (G â‹™ proj X F â‹™ F) :=
{ X := X, Ï€ := { app := Î» j, (G.obj j).hom } }

/--
Given a cone `c : cone K` and a map `f : X âŸ¶ F.obj c.X`, we can construct a cone of structured
arrows over `X` with `f` as the cone point.
-/
@[simps]
def to_cone {X : D} (f : X âŸ¶ F.obj c.X) :
  cone (to_diagram (F.map_cone c) â‹™ map f â‹™ pre _ K F) :=
{ X := mk f, Ï€ := { app := Î» j, hom_mk (c.Ï€.app j) rfl,
                    naturality' := Î» j k g, by { ext, dsimp, simp } } }

end structured_arrow_cone

section representably_flat
variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚‚} D]
variables {E : Type uâ‚ƒ} [category.{vâ‚ƒ} E]

/--
A functor `F : C â¥¤ D` is representably-flat functor if the comma category `(X/F)`
is cofiltered for each `X : C`.
-/
class representably_flat (F : C â¥¤ D) : Prop :=
(cofiltered : âˆ€ (X : D), is_cofiltered (structured_arrow X F))

attribute [instance] representably_flat.cofiltered

local attribute [instance] is_cofiltered.nonempty

instance representably_flat.id : representably_flat (ğŸ­ C) :=
begin
  constructor,
  intro X,
  haveI : nonempty (structured_arrow X (ğŸ­ C)) := âŸ¨structured_arrow.mk (ğŸ™ _)âŸ©,
  rsufficesI : is_cofiltered_or_empty (structured_arrow X (ğŸ­ C)),
  { constructor },
  constructor,
  { intros Y Z,
    use structured_arrow.mk (ğŸ™ _),
    use structured_arrow.hom_mk Y.hom (by erw [functor.id_map, category.id_comp]),
    use structured_arrow.hom_mk Z.hom (by erw [functor.id_map, category.id_comp]) },
  { intros Y Z f g,
    use structured_arrow.mk (ğŸ™ _),
    use structured_arrow.hom_mk Y.hom (by erw [functor.id_map, category.id_comp]),
    ext,
    transitivity Z.hom; simp }
end

instance representably_flat.comp (F : C â¥¤ D) (G : D â¥¤ E)
  [representably_flat F] [representably_flat G] : representably_flat (F â‹™ G) :=
begin
  constructor,
  intro X,
  haveI : nonempty (structured_arrow X (F â‹™ G)),
  { have fâ‚ : structured_arrow X G := nonempty.some infer_instance,
    have fâ‚‚ : structured_arrow fâ‚.right F := nonempty.some infer_instance,
    exact âŸ¨structured_arrow.mk (fâ‚.hom â‰« G.map fâ‚‚.hom)âŸ© },
  rsufficesI : is_cofiltered_or_empty (structured_arrow X (F â‹™ G)),
  { constructor },
  constructor,
  { intros Y Z,
    let W := @is_cofiltered.min (structured_arrow X G) _ _
      (structured_arrow.mk Y.hom) (structured_arrow.mk Z.hom),
    let Y' : W âŸ¶ _ := is_cofiltered.min_to_left _ _,
    let Z' : W âŸ¶ _ := is_cofiltered.min_to_right _ _,

    let W' := @is_cofiltered.min (structured_arrow W.right F) _ _
      (structured_arrow.mk Y'.right) (structured_arrow.mk Z'.right),
    let Y'' : W' âŸ¶ _ := is_cofiltered.min_to_left _ _,
    let Z'' : W' âŸ¶ _ := is_cofiltered.min_to_right _ _,

    use structured_arrow.mk (W.hom â‰« G.map W'.hom),
    use structured_arrow.hom_mk Y''.right (by simp [â† G.map_comp]),
    use structured_arrow.hom_mk Z''.right (by simp [â† G.map_comp]) },
  { intros Y Z f g,
    let W := @is_cofiltered.eq (structured_arrow X G) _ _
        (structured_arrow.mk Y.hom) (structured_arrow.mk Z.hom)
        (structured_arrow.hom_mk (F.map f.right) (structured_arrow.w f))
        (structured_arrow.hom_mk (F.map g.right) (structured_arrow.w g)),
    let h : W âŸ¶ _ := is_cofiltered.eq_hom _ _,
    let h_cond : h â‰« _ = h â‰« _ := is_cofiltered.eq_condition _ _,

    let W' := @is_cofiltered.eq (structured_arrow W.right F) _ _
        (structured_arrow.mk h.right) (structured_arrow.mk (h.right â‰« F.map f.right))
        (structured_arrow.hom_mk f.right rfl)
        (structured_arrow.hom_mk g.right (congr_arg comma_morphism.right h_cond).symm),
    let h' : W' âŸ¶ _ := is_cofiltered.eq_hom _ _,
    let h'_cond : h' â‰« _ = h' â‰« _ := is_cofiltered.eq_condition _ _,

    use structured_arrow.mk (W.hom â‰« G.map W'.hom),
    use structured_arrow.hom_mk h'.right (by simp [â† G.map_comp]),
    ext,
    exact (congr_arg comma_morphism.right h'_cond : _) }
end

end representably_flat

section has_limit
variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚} D]

local attribute [instance] has_finite_limits_of_has_finite_limits_of_size

lemma cofiltered_of_has_finite_limits [has_finite_limits C] : is_cofiltered C :=
{ cone_objs := Î» A B, âŸ¨limits.prod A B, limits.prod.fst, limits.prod.snd, trivialâŸ©,
  cone_maps :=  Î» A B f g, âŸ¨equalizer f g, equalizer.Î¹ f g, equalizer.condition f gâŸ©,
  nonempty := âŸ¨âŠ¤_ CâŸ© }

lemma flat_of_preserves_finite_limits [has_finite_limits C] (F : C â¥¤ D)
  [preserves_finite_limits F] : representably_flat F := âŸ¨Î» X,
begin
  haveI : has_finite_limits (structured_arrow X F) :=
  begin
    apply has_finite_limits_of_has_finite_limits_of_size.{vâ‚} (structured_arrow X F),
    intros J sJ fJ, resetI, constructor
  end,
  exact cofiltered_of_has_finite_limits
endâŸ©

namespace preserves_finite_limits_of_flat
open structured_arrow
open structured_arrow_cone
variables {J : Type vâ‚} [small_category J] [fin_category J] {K : J â¥¤ C}
variables (F : C â¥¤ D) [representably_flat F] {c : cone K} (hc : is_limit c) (s : cone (K â‹™ F))
include hc

/--
(Implementation).
Given a limit cone `c : cone K` and a cone `s : cone (K â‹™ F)` with `F` representably flat,
`s` can factor through `F.map_cone c`.
-/
noncomputable def lift : s.X âŸ¶ F.obj c.X :=
let s' := is_cofiltered.cone (to_diagram s â‹™ structured_arrow.pre _ K F) in
s'.X.hom â‰« (F.map $ hc.lift $
  (cones.postcompose ({ app := Î» X, ğŸ™ _, naturality' := by simp }
      : (to_diagram s â‹™ pre s.X K F) â‹™ proj s.X F âŸ¶ K)).obj $
  (structured_arrow.proj s.X F).map_cone s')

lemma fac (x : J) : lift F hc s â‰« (F.map_cone c).Ï€.app x = s.Ï€.app x :=
by simpa [lift, â†functor.map_comp]

local attribute [simp] eq_to_hom_map

lemma uniq {K : J â¥¤ C} {c : cone K} (hc : is_limit c)
  (s : cone (K â‹™ F)) (fâ‚ fâ‚‚ : s.X âŸ¶ F.obj c.X)
  (hâ‚ : âˆ€ (j : J), fâ‚ â‰« (F.map_cone c).Ï€.app j = s.Ï€.app j)
  (hâ‚‚ : âˆ€ (j : J), fâ‚‚ â‰« (F.map_cone c).Ï€.app j = s.Ï€.app j) : fâ‚ = fâ‚‚ :=
begin
  -- We can make two cones over the diagram of `s` via `fâ‚` and `fâ‚‚`.
  let Î±â‚ : to_diagram (F.map_cone c) â‹™ map fâ‚ âŸ¶ to_diagram s :=
  { app := Î» X, eq_to_hom (by simp [â†hâ‚]), naturality' := Î» _ _ _, by { ext, simp } },
  let Î±â‚‚ : to_diagram (F.map_cone c) â‹™ map fâ‚‚ âŸ¶ to_diagram s :=
  { app := Î» X, eq_to_hom (by simp [â†hâ‚‚]), naturality' := Î» _ _ _, by { ext, simp } },
  let câ‚ : cone (to_diagram s â‹™ pre s.X K F) :=
    (cones.postcompose (whisker_right Î±â‚ (pre s.X K F) : _)).obj (to_cone F c fâ‚),
  let câ‚‚ : cone (to_diagram s â‹™ pre s.X K F) :=
    (cones.postcompose (whisker_right Î±â‚‚ (pre s.X K F) : _)).obj (to_cone F c fâ‚‚),

  -- The two cones can then be combined and we may obtain a cone over the two cones since
  -- `structured_arrow s.X F` is cofiltered.
  let câ‚€ := is_cofiltered.cone (bicone_mk _ câ‚ câ‚‚),
  let gâ‚ : câ‚€.X âŸ¶ câ‚.X := câ‚€.Ï€.app (bicone.left),
  let gâ‚‚ : câ‚€.X âŸ¶ câ‚‚.X := câ‚€.Ï€.app (bicone.right),

  -- Then `gâ‚.right` and `gâ‚‚.right` are two maps from the same cone into the `c`.
  have : âˆ€ (j : J), gâ‚.right â‰« c.Ï€.app j = gâ‚‚.right â‰« c.Ï€.app j,
  { intro j,
    injection câ‚€.Ï€.naturality (bicone_hom.left  j) with _ eâ‚,
    injection câ‚€.Ï€.naturality (bicone_hom.right j) with _ eâ‚‚,
    simpa using eâ‚.symm.trans eâ‚‚ },
  have : c.extend gâ‚.right = c.extend gâ‚‚.right,
  { unfold cone.extend, congr' 1, ext x, apply this },

  -- And thus they are equal as `c` is the limit.
  have : gâ‚.right = gâ‚‚.right,
  calc gâ‚.right = hc.lift (c.extend gâ‚.right) : by { apply hc.uniq (c.extend _), tidy }
            ... = hc.lift (c.extend gâ‚‚.right) : by { congr, exact this }
            ... = gâ‚‚.right                    : by { symmetry, apply hc.uniq (c.extend _), tidy },

  -- Finally, since `fáµ¢` factors through `F(gáµ¢)`, the result follows.
  calc fâ‚ = ğŸ™ _ â‰« fâ‚                  : by simp
      ... = câ‚€.X.hom â‰« F.map gâ‚.right : gâ‚.w
      ... = câ‚€.X.hom â‰« F.map gâ‚‚.right : by rw this
      ... = ğŸ™ _ â‰« fâ‚‚                  : gâ‚‚.w.symm
      ... = fâ‚‚                         : by simp
end

end preserves_finite_limits_of_flat

/-- Representably flat functors preserve finite limits. -/
noncomputable
def preserves_finite_limits_of_flat (F : C â¥¤ D) [representably_flat F] :
  preserves_finite_limits F :=
begin
  apply preserves_finite_limits_of_preserves_finite_limits_of_size,
  intros J _ _, constructor,
  intros K, constructor,
  intros c hc,
  exactI { lift := preserves_finite_limits_of_flat.lift F hc,
    fac' := preserves_finite_limits_of_flat.fac F hc,
    uniq' := Î» s m h, by
    { apply preserves_finite_limits_of_flat.uniq F hc,
      exact h,
      exact preserves_finite_limits_of_flat.fac F hc s } }
end

/--
If `C` is finitely cocomplete, then `F : C â¥¤ D` is representably flat iff it preserves
finite limits.
-/
noncomputable
def preserves_finite_limits_iff_flat [has_finite_limits C] (F : C â¥¤ D) :
  representably_flat F â‰ƒ preserves_finite_limits F :=
{ to_fun := Î» _, by exactI preserves_finite_limits_of_flat F,
  inv_fun := Î» _, by exactI flat_of_preserves_finite_limits F,
  left_inv := Î» _, proof_irrel _ _,
  right_inv := Î» x, by { cases x, unfold preserves_finite_limits_of_flat,
    dunfold preserves_finite_limits_of_preserves_finite_limits_of_size, congr } }

end has_limit


section small_category
variables {C D : Type uâ‚} [small_category C] [small_category D] (E : Type uâ‚‚) [category.{uâ‚} E]

/--
(Implementation)
The evaluation of `Lan F` at `X` is the colimit over the costructured arrows over `X`.
-/
noncomputable
def Lan_evaluation_iso_colim (F : C â¥¤ D) (X : D)
  [âˆ€ (X : D), has_colimits_of_shape (costructured_arrow F X) E] :
  Lan F â‹™ (evaluation D E).obj X â‰…
  ((whiskering_left _ _ E).obj (costructured_arrow.proj F X)) â‹™ colim :=
nat_iso.of_components (Î» G, colim.map_iso (iso.refl _))
begin
  intros G H i,
  ext,
  simp only [functor.comp_map, colimit.Î¹_desc_assoc, functor.map_iso_refl, evaluation_obj_map,
    whiskering_left_obj_map, category.comp_id, Lan_map_app, category.assoc],
  erw [colimit.Î¹_pre_assoc (Lan.diagram F H X) (costructured_arrow.map j.hom),
    category.id_comp, category.comp_id, colimit.Î¹_map],
  rcases j with âŸ¨j_left, âŸ¨âŸ¨âŸ©âŸ©, j_homâŸ©,
  congr,
  rw [costructured_arrow.map_mk, category.id_comp, costructured_arrow.mk]
end

variables [concrete_category.{uâ‚} E] [has_limits E] [has_colimits E]
variables [reflects_limits (forget E)] [preserves_filtered_colimits (forget E)]
variables [preserves_limits (forget E)]

/--
If `F : C â¥¤ D` is a representably flat functor between small categories, then the functor
`Lan F.op` that takes presheaves over `C` to presheaves over `D` preserves finite limits.
-/
noncomputable
instance Lan_preserves_finite_limits_of_flat (F : C â¥¤ D) [representably_flat F] :
  preserves_finite_limits (Lan F.op : _ â¥¤ (Dáµ’áµ– â¥¤ E)) :=
begin
  apply preserves_finite_limits_of_preserves_finite_limits_of_size.{uâ‚},
  intros J _ _, resetI,
  apply preserves_limits_of_shape_of_evaluation (Lan F.op : (Cáµ’áµ– â¥¤ E) â¥¤ (Dáµ’áµ– â¥¤ E)) J,
  intro K,
  haveI : is_filtered (costructured_arrow F.op K) :=
    is_filtered.of_equivalence (structured_arrow_op_equivalence F (unop K)),
  exact preserves_limits_of_shape_of_nat_iso (Lan_evaluation_iso_colim _ _ _).symm,
end

instance Lan_flat_of_flat (F : C â¥¤ D) [representably_flat F] :
  representably_flat (Lan F.op : _ â¥¤ (Dáµ’áµ– â¥¤ E)) := flat_of_preserves_finite_limits _

variable [has_finite_limits C]

noncomputable
instance Lan_preserves_finite_limits_of_preserves_finite_limits (F : C â¥¤ D)
  [preserves_finite_limits F] : preserves_finite_limits (Lan F.op : _ â¥¤ (Dáµ’áµ– â¥¤ E)) :=
begin
  haveI := flat_of_preserves_finite_limits F,
  apply_instance
end

lemma flat_iff_Lan_flat (F : C â¥¤ D) :
  representably_flat F â†” representably_flat (Lan F.op : _ â¥¤ (Dáµ’áµ– â¥¤ Type uâ‚)) :=
âŸ¨Î» H, by exactI infer_instance, Î» H,
begin
  resetI,
  haveI := preserves_finite_limits_of_flat (Lan F.op : _ â¥¤ (Dáµ’áµ– â¥¤ Type uâ‚)),
  haveI : preserves_finite_limits F :=
    begin
      apply preserves_finite_limits_of_preserves_finite_limits_of_size.{uâ‚},
      intros, resetI, apply preserves_limit_of_Lan_preserves_limit
    end,
  apply flat_of_preserves_finite_limits
endâŸ©

/--
If `C` is finitely complete, then `F : C â¥¤ D` preserves finite limits iff
`Lan F.op : (Cáµ’áµ– â¥¤ Type*) â¥¤ (Dáµ’áµ– â¥¤ Type*)` preserves finite limits.
-/
noncomputable
def preserves_finite_limits_iff_Lan_preserves_finite_limits (F : C â¥¤ D) :
  preserves_finite_limits F â‰ƒ preserves_finite_limits (Lan F.op : _ â¥¤ (Dáµ’áµ– â¥¤ Type uâ‚)) :=
{ to_fun := Î» _, by exactI infer_instance,
  inv_fun := Î» _,
  begin
    apply preserves_finite_limits_of_preserves_finite_limits_of_size.{uâ‚},
    intros, resetI, apply preserves_limit_of_Lan_preserves_limit
  end,
  left_inv := Î» x,
  begin
    cases x, unfold preserves_finite_limits_of_flat,
    dunfold preserves_finite_limits_of_preserves_finite_limits_of_size, congr
  end,
  right_inv := Î» x,
  begin
    cases x,
    unfold preserves_finite_limits_of_flat,
    congr,
    unfold category_theory.Lan_preserves_finite_limits_of_preserves_finite_limits
      category_theory.Lan_preserves_finite_limits_of_flat,
    dunfold preserves_finite_limits_of_preserves_finite_limits_of_size, congr
  end }

end small_category
end category_theory
