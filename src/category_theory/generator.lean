/-
Copyright (c) 2022 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import category_theory.balanced
import category_theory.limits.essentially_small
import category_theory.limits.opposites
import category_theory.limits.shapes.zero_morphisms
import category_theory.subobject.lattice
import category_theory.subobject.well_powered
import data.set.opposite

/-!
# Separating and detecting sets

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

There are several non-equivalent notions of a generator of a category. Here, we consider two of
them:

* We say that `ğ’¢` is a separating set if the functors `C(G, -)` for `G âˆˆ ğ’¢` are collectively
    faithful, i.e., if `h â‰« f = h â‰« g` for all `h` with domain in `ğ’¢` implies `f = g`.
* We say that `ğ’¢` is a detecting set if the functors `C(G, -)` collectively reflect isomorphisms,
    i.e., if any `h` with domain in `ğ’¢` uniquely factors through `f`, then `f` is an isomorphism.

There are, of course, also the dual notions of coseparating and codetecting sets.

## Main results

We
* define predicates `is_separating`, `is_coseparating`, `is_detecting` and `is_codetecting` on
  sets of objects;
* show that separating and coseparating are dual notions;
* show that detecting and codetecting are dual notions;
* show that if `C` has equalizers, then detecting implies separating;
* show that if `C` has coequalizers, then codetecting implies separating;
* show that if `C` is balanced, then separating implies detecting and coseparating implies
  codetecting;
* show that `âˆ…` is separating if and only if `âˆ…` is coseparating if and only if `C` is thin;
* show that `âˆ…` is detecting if and only if `âˆ…` is codetecting if and only if `C` is a groupoid;
* define predicates `is_separator`, `is_coseparator`, `is_detector` and `is_codetector` as the
  singleton counterparts to the definitions for sets above and restate the above results in this
  situation;
* show that `G` is a separator if and only if `coyoneda.obj (op G)` is faithful (and the dual);
* show that `G` is a detector if and only if `coyoneda.obj (op G)` reflects isomorphisms (and the
  dual).

## Future work

* We currently don't have any examples yet.
* We will want typeclasses `has_separator C` and similar.

-/

universes w vâ‚ vâ‚‚ uâ‚ uâ‚‚

open category_theory.limits opposite

namespace category_theory
variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚‚} D]

/-- We say that `ğ’¢` is a separating set if the functors `C(G, -)` for `G âˆˆ ğ’¢` are collectively
    faithful, i.e., if `h â‰« f = h â‰« g` for all `h` with domain in `ğ’¢` implies `f = g`. -/
def is_separating (ğ’¢ : set C) : Prop :=
âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ (G âˆˆ ğ’¢) (h : G âŸ¶ X), h â‰« f = h â‰« g) â†’ f = g

/-- We say that `ğ’¢` is a coseparating set if the functors `C(-, G)` for `G âˆˆ ğ’¢` are collectively
    faithful, i.e., if `f â‰« h = g â‰« h` for all `h` with codomain in `ğ’¢` implies `f = g`. -/
def is_coseparating (ğ’¢ : set C) : Prop :=
âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ (G âˆˆ ğ’¢) (h : Y âŸ¶ G), f â‰« h = g â‰« h) â†’ f = g

/-- We say that `ğ’¢` is a detecting set if the functors `C(G, -)` collectively reflect isomorphisms,
    i.e., if any `h` with domain in `ğ’¢` uniquely factors through `f`, then `f` is an isomorphism. -/
def is_detecting (ğ’¢ : set C) : Prop :=
âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ (G âˆˆ ğ’¢) (h : G âŸ¶ Y), âˆƒ! (h' : G âŸ¶ X), h' â‰« f = h) â†’ is_iso f

/-- We say that `ğ’¢` is a codetecting set if the functors `C(-, G)` collectively reflect
    isomorphisms, i.e., if any `h` with codomain in `G` uniquely factors through `f`, then `f` is
    an isomorphism. -/
def is_codetecting (ğ’¢ : set C) : Prop :=
âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ (G âˆˆ ğ’¢) (h : X âŸ¶ G), âˆƒ! (h' : Y âŸ¶ G), f â‰« h' = h) â†’ is_iso f

section dual

lemma is_separating_op_iff (ğ’¢ : set C) : is_separating ğ’¢.op â†” is_coseparating ğ’¢ :=
begin
  refine âŸ¨Î» hğ’¢ X Y f g hfg, _, Î» hğ’¢ X Y f g hfg, _âŸ©,
  { refine quiver.hom.op_inj (hğ’¢ _ _ (Î» G hG h, quiver.hom.unop_inj _)),
    simpa only [unop_comp, quiver.hom.unop_op] using hfg _ (set.mem_op.1 hG) _ },
  { refine quiver.hom.unop_inj (hğ’¢ _ _ (Î» G hG h, quiver.hom.op_inj _)),
    simpa only [op_comp, quiver.hom.op_unop] using hfg _ (set.op_mem_op.2 hG) _ }
end

lemma is_coseparating_op_iff (ğ’¢ : set C) : is_coseparating ğ’¢.op â†” is_separating ğ’¢ :=
begin
  refine âŸ¨Î» hğ’¢ X Y f g hfg, _, Î» hğ’¢ X Y f g hfg, _âŸ©,
  { refine quiver.hom.op_inj (hğ’¢ _ _ (Î» G hG h, quiver.hom.unop_inj _)),
    simpa only [unop_comp, quiver.hom.unop_op] using hfg _ (set.mem_op.1 hG) _ },
  { refine quiver.hom.unop_inj (hğ’¢ _ _ (Î» G hG h, quiver.hom.op_inj _)),
    simpa only [op_comp, quiver.hom.op_unop] using hfg _ (set.op_mem_op.2 hG) _ }
end

lemma is_coseparating_unop_iff (ğ’¢ : set Cáµ’áµ–) : is_coseparating ğ’¢.unop â†” is_separating ğ’¢ :=
by rw [â† is_separating_op_iff, set.unop_op]

lemma is_separating_unop_iff (ğ’¢ : set Cáµ’áµ–) : is_separating ğ’¢.unop â†” is_coseparating ğ’¢ :=
by rw [â† is_coseparating_op_iff, set.unop_op]

lemma is_detecting_op_iff (ğ’¢ : set C) : is_detecting ğ’¢.op â†” is_codetecting ğ’¢ :=
begin
  refine âŸ¨Î» hğ’¢ X Y f hf, _, Î» hğ’¢ X Y f hf, _âŸ©,
  { refine (is_iso_op_iff _).1 (hğ’¢ _ (Î» G hG h, _)),
    obtain âŸ¨t, ht, ht'âŸ© := hf (unop G) (set.mem_op.1 hG) h.unop,
    exact âŸ¨t.op, quiver.hom.unop_inj ht, Î» y hy,
      quiver.hom.unop_inj (ht' _ (quiver.hom.op_inj hy))âŸ© },
  { refine (is_iso_unop_iff _).1 (hğ’¢ _ (Î» G hG h, _)),
    obtain âŸ¨t, ht, ht'âŸ© := hf (op G) (set.op_mem_op.2 hG) h.op,
    refine âŸ¨t.unop, quiver.hom.op_inj ht, Î» y hy, quiver.hom.op_inj (ht' _ _)âŸ©,
    exact quiver.hom.unop_inj (by simpa only using hy) }
end

lemma is_codetecting_op_iff (ğ’¢ : set C) : is_codetecting ğ’¢.op â†” is_detecting ğ’¢ :=
begin
  refine âŸ¨Î» hğ’¢ X Y f hf, _, Î» hğ’¢ X Y f hf, _âŸ©,
  { refine (is_iso_op_iff _).1 (hğ’¢ _ (Î» G hG h, _)),
    obtain âŸ¨t, ht, ht'âŸ© := hf (unop G) (set.mem_op.1 hG) h.unop,
    exact âŸ¨t.op, quiver.hom.unop_inj ht, Î» y hy,
      quiver.hom.unop_inj (ht' _ (quiver.hom.op_inj hy))âŸ© },
  { refine (is_iso_unop_iff _).1 (hğ’¢ _ (Î» G hG h, _)),
    obtain âŸ¨t, ht, ht'âŸ© := hf (op G) (set.op_mem_op.2 hG) h.op,
    refine âŸ¨t.unop, quiver.hom.op_inj ht, Î» y hy, quiver.hom.op_inj (ht' _ _)âŸ©,
    exact quiver.hom.unop_inj (by simpa only using hy) }
end

lemma is_detecting_unop_iff (ğ’¢ : set Cáµ’áµ–) : is_detecting ğ’¢.unop â†” is_codetecting ğ’¢ :=
by rw [â† is_codetecting_op_iff, set.unop_op]

lemma is_codetecting_unop_iff {ğ’¢ : set Cáµ’áµ–} : is_codetecting ğ’¢.unop â†” is_detecting ğ’¢ :=
by rw [â† is_detecting_op_iff, set.unop_op]

end dual

lemma is_detecting.is_separating [has_equalizers C] {ğ’¢ : set C} (hğ’¢ : is_detecting ğ’¢) :
  is_separating ğ’¢ :=
Î» X Y f g hfg,
  have is_iso (equalizer.Î¹ f g), from hğ’¢ _ (Î» G hG h, equalizer.exists_unique _ (hfg _ hG _)),
  by exactI eq_of_epi_equalizer

section

lemma is_codetecting.is_coseparating [has_coequalizers C] {ğ’¢ : set C} :
  is_codetecting ğ’¢ â†’ is_coseparating ğ’¢ :=
by simpa only [â† is_separating_op_iff, â† is_detecting_op_iff] using is_detecting.is_separating

end

lemma is_separating.is_detecting [balanced C] {ğ’¢ : set C} (hğ’¢ : is_separating ğ’¢) :
  is_detecting ğ’¢ :=
begin
  intros X Y f hf,
  refine (is_iso_iff_mono_and_epi _).2 âŸ¨âŸ¨Î» Z g h hgh, hğ’¢ _ _ (Î» G hG i, _)âŸ©, âŸ¨Î» Z g h hgh, _âŸ©âŸ©,
  { obtain âŸ¨t, -, htâŸ© := hf G hG (i â‰« g â‰« f),
    rw [ht (i â‰« g) (category.assoc _ _ _), ht (i â‰« h) (hgh.symm â–¸ category.assoc _ _ _)] },
  { refine hğ’¢ _ _ (Î» G hG i, _),
    obtain âŸ¨t, rfl, -âŸ© := hf G hG i,
    rw [category.assoc, hgh, category.assoc] }
end

section
local attribute [instance] balanced_opposite

lemma is_coseparating.is_codetecting [balanced C] {ğ’¢ : set C} :
  is_coseparating ğ’¢ â†’ is_codetecting ğ’¢ :=
by simpa only [â† is_detecting_op_iff, â† is_separating_op_iff] using is_separating.is_detecting

end

lemma is_detecting_iff_is_separating [has_equalizers C] [balanced C] (ğ’¢ : set C) :
  is_detecting ğ’¢ â†” is_separating ğ’¢ :=
âŸ¨is_detecting.is_separating, is_separating.is_detectingâŸ©

lemma is_codetecting_iff_is_coseparating [has_coequalizers C] [balanced C] {ğ’¢ : set C} :
  is_codetecting ğ’¢ â†” is_coseparating ğ’¢ :=
âŸ¨is_codetecting.is_coseparating, is_coseparating.is_codetectingâŸ©

section mono

lemma is_separating.mono {ğ’¢ : set C} (hğ’¢ : is_separating ğ’¢) {â„‹ : set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
  is_separating â„‹ :=
Î» X Y f g hfg, hğ’¢ _ _ $ Î» G hG h, hfg _ (hğ’¢â„‹ hG) _

lemma is_coseparating.mono {ğ’¢ : set C} (hğ’¢ : is_coseparating ğ’¢) {â„‹ : set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
  is_coseparating â„‹ :=
Î» X Y f g hfg, hğ’¢ _ _ $ Î» G hG h, hfg _ (hğ’¢â„‹ hG) _

lemma is_detecting.mono {ğ’¢ : set C} (hğ’¢ : is_detecting ğ’¢) {â„‹ : set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
  is_detecting â„‹ :=
Î» X Y f hf, hğ’¢ _ $ Î» G hG h, hf _ (hğ’¢â„‹ hG) _

lemma is_codetecting.mono {ğ’¢ : set C} (hğ’¢ : is_codetecting ğ’¢) {â„‹ : set C} (hğ’¢â„‹ : ğ’¢ âŠ† â„‹) :
  is_codetecting â„‹ :=
Î» X Y f hf, hğ’¢ _ $ Î» G hG h, hf _ (hğ’¢â„‹ hG) _

end mono

section empty

lemma thin_of_is_separating_empty (h : is_separating (âˆ… : set C)) : quiver.is_thin C :=
Î» _ _, âŸ¨Î» f g, h _ _ $ Î» G, false.elimâŸ©

lemma is_separating_empty_of_thin [quiver.is_thin C] : is_separating (âˆ… : set C) :=
Î» X Y f g hfg, subsingleton.elim _ _

lemma thin_of_is_coseparating_empty (h : is_coseparating (âˆ… : set C)) : quiver.is_thin C :=
Î» _ _, âŸ¨Î» f g, h _ _ $ Î» G, false.elimâŸ©

lemma is_coseparating_empty_of_thin [quiver.is_thin C] :
  is_coseparating (âˆ… : set C) :=
Î» X Y f g hfg, subsingleton.elim _ _

lemma groupoid_of_is_detecting_empty (h : is_detecting (âˆ… : set C)) {X Y : C} (f : X âŸ¶ Y) :
  is_iso f :=
h _ $ Î» G, false.elim

lemma is_detecting_empty_of_groupoid [âˆ€ {X Y : C} (f : X âŸ¶ Y), is_iso f] :
  is_detecting (âˆ… : set C) :=
Î» X Y f hf, infer_instance

lemma groupoid_of_is_codetecting_empty (h : is_codetecting (âˆ… : set C)) {X Y : C} (f : X âŸ¶ Y) :
  is_iso f :=
h _ $ Î» G, false.elim

lemma is_codetecting_empty_of_groupoid [âˆ€ {X Y : C} (f : X âŸ¶ Y), is_iso f] :
  is_codetecting (âˆ… : set C) :=
Î» X Y f hf, infer_instance

end empty

lemma is_separating_iff_epi (ğ’¢ : set C)
  [Î  (A : C), has_coproduct (Î» f : Î£ G : ğ’¢, (G : C) âŸ¶ A, (f.1 : C))] :
  is_separating ğ’¢ â†” âˆ€ A : C, epi (sigma.desc (@sigma.snd ğ’¢ (Î» G, (G : C) âŸ¶ A))) :=
begin
  refine âŸ¨Î» h A, âŸ¨Î» Z u v huv, h _ _ (Î» G hG f, _)âŸ©, Î» h X Y f g hh, _âŸ©,
  { simpa using (sigma.Î¹ (Î» f : Î£ G : ğ’¢, (G : C) âŸ¶ A, (f.1 : C)) âŸ¨âŸ¨G, hGâŸ©, fâŸ©) â‰«= huv },
  { haveI := h X,
    refine (cancel_epi (sigma.desc (@sigma.snd ğ’¢ (Î» G, (G : C) âŸ¶ X)))).1 (colimit.hom_ext (Î» j, _)),
    simpa using hh j.as.1.1 j.as.1.2 j.as.2 }
end

lemma is_coseparating_iff_mono (ğ’¢ : set C)
  [Î  (A : C), has_product (Î» f : Î£ G : ğ’¢, A âŸ¶ (G : C), (f.1 : C))] :
  is_coseparating ğ’¢ â†” âˆ€ A : C, mono (pi.lift (@sigma.snd ğ’¢ (Î» G, A âŸ¶ (G : C)))) :=
begin
  refine âŸ¨Î» h A, âŸ¨Î» Z u v huv, h _ _ (Î» G hG f, _)âŸ©, Î» h X Y f g hh, _âŸ©,
  { simpa using huv =â‰« (pi.Ï€ (Î» f : Î£ G : ğ’¢, A âŸ¶ (G : C), (f.1 : C)) âŸ¨âŸ¨G, hGâŸ©, fâŸ©) },
  { haveI := h Y,
    refine (cancel_mono (pi.lift (@sigma.snd ğ’¢ (Î» G, Y âŸ¶ (G : C))))).1 (limit.hom_ext (Î» j, _)),
    simpa using hh j.as.1.1 j.as.1.2 j.as.2 }
end

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a complete well-powered
    category with a small coseparating set has an initial object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already cocomplete,
    see `has_colimits_of_has_limits_of_is_coseparating`. -/
lemma has_initial_of_is_coseparating [well_powered C] [has_limits C] {ğ’¢ : set C} [small.{vâ‚} ğ’¢]
  (hğ’¢ : is_coseparating ğ’¢) : has_initial C :=
begin
  haveI := has_products_of_shape_of_small C ğ’¢,
  haveI := Î» A, has_products_of_shape_of_small.{vâ‚} C (Î£ G : ğ’¢, A âŸ¶ (G : C)),
  letI := complete_lattice_of_complete_semilattice_Inf (subobject (pi_obj (coe : ğ’¢ â†’ C))),
  suffices : âˆ€ A : C, unique (((âŠ¥ : subobject (pi_obj (coe : ğ’¢ â†’ C))) : C) âŸ¶ A),
  { exactI has_initial_of_unique ((âŠ¥ : subobject (pi_obj (coe : ğ’¢ â†’ C))) : C) },
  refine Î» A, âŸ¨âŸ¨_âŸ©, Î» f, _âŸ©,
  { let s := pi.lift (Î» f : Î£ G : ğ’¢, A âŸ¶ (G : C), id (pi.Ï€ (coe : ğ’¢ â†’ C)) f.1),
    let t := pi.lift (@sigma.snd ğ’¢ (Î» G, A âŸ¶ (G : C))),
    haveI : mono t := (is_coseparating_iff_mono ğ’¢).1 hğ’¢ A,
    exact subobject.of_le_mk _ (pullback.fst : pullback s t âŸ¶ _) bot_le â‰« pullback.snd },
  { generalize : default = g,
    suffices : is_split_epi (equalizer.Î¹ f g),
    { exactI eq_of_epi_equalizer },
    exact is_split_epi.mk' âŸ¨subobject.of_le_mk _ (equalizer.Î¹ f g â‰« subobject.arrow _)
      bot_le, by { ext, simp }âŸ© }
end

/-- An ingredient of the proof of the Special Adjoint Functor Theorem: a cocomplete well-copowered
    category with a small separating set has a terminal object.

    In fact, it follows from the Special Adjoint Functor Theorem that `C` is already complete, see
    `has_limits_of_has_colimits_of_is_separating`. -/
lemma has_terminal_of_is_separating [well_powered Cáµ’áµ–] [has_colimits C] {ğ’¢ : set C} [small.{vâ‚} ğ’¢]
  (hğ’¢ : is_separating ğ’¢) : has_terminal C :=
begin
  haveI : small.{vâ‚} ğ’¢.op := small_of_injective (set.op_equiv_self ğ’¢).injective,
  haveI : has_initial Cáµ’áµ– := has_initial_of_is_coseparating ((is_coseparating_op_iff _).2 hğ’¢),
  exact has_terminal_of_has_initial_op
end

section well_powered

namespace subobject

lemma eq_of_le_of_is_detecting {ğ’¢ : set C} (hğ’¢ : is_detecting ğ’¢) {X : C} (P Q : subobject X)
  (hâ‚ : P â‰¤ Q) (hâ‚‚ : âˆ€ (G âˆˆ ğ’¢) {f : G âŸ¶ X}, Q.factors f â†’ P.factors f) : P = Q :=
begin
  suffices : is_iso (of_le _ _ hâ‚),
  { exactI le_antisymm hâ‚ (le_of_comm (inv (of_le _ _ hâ‚)) (by simp)) },
  refine hğ’¢ _ (Î» G hG f, _),
  have : P.factors (f â‰« Q.arrow) := hâ‚‚ _ hG ((factors_iff _ _).2 âŸ¨_, rflâŸ©),
  refine âŸ¨factor_thru _ _ this, _, Î» g (hg : g â‰« _ = f), _âŸ©,
  { simp only [â† cancel_mono Q.arrow, category.assoc, of_le_arrow, factor_thru_arrow] },
  { simp only [â† cancel_mono (subobject.of_le _ _ hâ‚), â† cancel_mono Q.arrow, hg,
      category.assoc, of_le_arrow, factor_thru_arrow] }
end

lemma inf_eq_of_is_detecting [has_pullbacks C] {ğ’¢ : set C} (hğ’¢ : is_detecting ğ’¢) {X : C}
  (P Q : subobject X) (h : âˆ€ (G âˆˆ ğ’¢) {f : G âŸ¶ X}, P.factors f â†’ Q.factors f) : P âŠ“ Q = P :=
eq_of_le_of_is_detecting hğ’¢ _ _ _root_.inf_le_left (Î» G hG f hf, (inf_factors _).2 âŸ¨hf, h _ hG hfâŸ©)

lemma eq_of_is_detecting [has_pullbacks C] {ğ’¢ : set C} (hğ’¢ : is_detecting ğ’¢) {X : C}
  (P Q : subobject X) (h : âˆ€ (G âˆˆ ğ’¢) {f : G âŸ¶ X}, P.factors f â†” Q.factors f) : P = Q :=
calc P = P âŠ“ Q : eq.symm $ inf_eq_of_is_detecting hğ’¢ _ _ $ Î» G hG f hf, (h G hG).1 hf
   ... = Q âŠ“ P : inf_comm
   ... = Q     : inf_eq_of_is_detecting hğ’¢ _ _ $ Î» G hG f hf, (h G hG).2 hf

end subobject

/-- A category with pullbacks and a small detecting set is well-powered. -/
lemma well_powered_of_is_detecting [has_pullbacks C] {ğ’¢ : set C} [small.{vâ‚} ğ’¢]
  (hğ’¢ : is_detecting ğ’¢) : well_powered C :=
âŸ¨Î» X, @small_of_injective _ _ _ (Î» P : subobject X, { f : Î£ G : ğ’¢, G.1 âŸ¶ X | P.factors f.2 }) $
  Î» P Q h, subobject.eq_of_is_detecting hğ’¢ _ _ (by simpa [set.ext_iff] using h)âŸ©

end well_powered

namespace structured_arrow
variables (S : D) (T : C â¥¤ D)

lemma is_coseparating_proj_preimage {ğ’¢ : set C} (hğ’¢ : is_coseparating ğ’¢) :
  is_coseparating ((proj S T).obj â»Â¹' ğ’¢) :=
begin
  refine Î» X Y f g hfg, ext _ _ (hğ’¢ _ _ (Î» G hG h, _)),
  exact congr_arg comma_morphism.right (hfg (mk (Y.hom â‰« T.map h)) hG (hom_mk h rfl))
end

end structured_arrow

namespace costructured_arrow
variables (S : C â¥¤ D) (T : D)

lemma is_separating_proj_preimage {ğ’¢ : set C} (hğ’¢ : is_separating ğ’¢) :
  is_separating ((proj S T).obj â»Â¹' ğ’¢) :=
begin
  refine Î» X Y f g hfg, ext _ _ (hğ’¢ _ _ (Î» G hG h, _)),
  convert congr_arg comma_morphism.left (hfg (mk (S.map h â‰« X.hom)) hG (hom_mk h rfl))
end

end costructured_arrow

/-- We say that `G` is a separator if the functor `C(G, -)` is faithful. -/
def is_separator (G : C) : Prop :=
is_separating ({G} : set C)

/-- We say that `G` is a coseparator if the functor `C(-, G)` is faithful. -/
def is_coseparator (G : C) : Prop :=
is_coseparating ({G} : set C)

/-- We say that `G` is a detector if the functor `C(G, -)` reflects isomorphisms. -/
def is_detector (G : C) : Prop :=
is_detecting ({G} : set C)

/-- We say that `G` is a codetector if the functor `C(-, G)` reflects isomorphisms. -/
def is_codetector (G : C) : Prop :=
is_codetecting ({G} : set C)

section dual

lemma is_separator_op_iff (G : C) : is_separator (op G) â†” is_coseparator G :=
by rw [is_separator, is_coseparator, â† is_separating_op_iff, set.singleton_op]

lemma is_coseparator_op_iff (G : C) : is_coseparator (op G) â†” is_separator G :=
by rw [is_separator, is_coseparator, â† is_coseparating_op_iff, set.singleton_op]

lemma is_coseparator_unop_iff (G : Cáµ’áµ–) : is_coseparator (unop G) â†” is_separator G :=
by rw [is_separator, is_coseparator, â† is_coseparating_unop_iff, set.singleton_unop]

lemma is_separator_unop_iff (G : Cáµ’áµ–) : is_separator (unop G) â†” is_coseparator G :=
by rw [is_separator, is_coseparator, â† is_separating_unop_iff, set.singleton_unop]

lemma is_detector_op_iff (G : C) : is_detector (op G) â†” is_codetector G :=
by rw [is_detector, is_codetector, â† is_detecting_op_iff, set.singleton_op]

lemma is_codetector_op_iff (G : C) : is_codetector (op G) â†” is_detector G :=
by rw [is_detector, is_codetector, â† is_codetecting_op_iff, set.singleton_op]

lemma is_codetector_unop_iff (G : Cáµ’áµ–) : is_codetector (unop G) â†” is_detector G :=
by rw [is_detector, is_codetector, â† is_codetecting_unop_iff, set.singleton_unop]

lemma is_detector_unop_iff (G : Cáµ’áµ–) : is_detector (unop G) â†” is_codetector G :=
by rw [is_detector, is_codetector, â† is_detecting_unop_iff, set.singleton_unop]

end dual

lemma is_detector.is_separator [has_equalizers C] {G : C} : is_detector G â†’ is_separator G :=
is_detecting.is_separating

lemma is_codetector.is_coseparator [has_coequalizers C] {G : C} :
  is_codetector G â†’ is_coseparator G :=
is_codetecting.is_coseparating

lemma is_separator.is_detector [balanced C] {G : C} : is_separator G â†’ is_detector G :=
is_separating.is_detecting

lemma is_cospearator.is_codetector [balanced C] {G : C} : is_coseparator G â†’ is_codetector G :=
is_coseparating.is_codetecting

lemma is_separator_def (G : C) :
  is_separator G â†” âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : G âŸ¶ X, h â‰« f = h â‰« g) â†’ f = g :=
âŸ¨Î» hG X Y f g hfg, hG _ _ $ Î» H hH h, by { obtain rfl := set.mem_singleton_iff.1 hH, exact hfg h },
 Î» hG X Y f g hfg, hG _ _ $ Î» h, hfg _ (set.mem_singleton _) _âŸ©

lemma is_separator.def {G : C} :
  is_separator G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : G âŸ¶ X, h â‰« f = h â‰« g) â†’ f = g :=
(is_separator_def _).1

lemma is_coseparator_def (G : C) :
  is_coseparator G â†” âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : Y âŸ¶ G, f â‰« h = g â‰« h) â†’ f = g :=
âŸ¨Î» hG X Y f g hfg, hG _ _ $ Î» H hH h, by { obtain rfl := set.mem_singleton_iff.1 hH, exact hfg h },
 Î» hG X Y f g hfg, hG _ _ $ Î» h, hfg _ (set.mem_singleton _) _âŸ©

lemma is_coseparator.def {G : C} :
  is_coseparator G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f g : X âŸ¶ Y), (âˆ€ h : Y âŸ¶ G, f â‰« h = g â‰« h) â†’ f = g :=
(is_coseparator_def _).1

lemma is_detector_def (G : C) :
  is_detector G â†” âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : G âŸ¶ Y, âˆƒ! h', h' â‰« f = h) â†’ is_iso f :=
âŸ¨Î» hG X Y f hf, hG _ $ Î» H hH h, by { obtain rfl := set.mem_singleton_iff.1 hH, exact hf h },
 Î» hG X Y f hf, hG _ $ Î» h, hf _ (set.mem_singleton _) _âŸ©

lemma is_detector.def {G : C} :
  is_detector G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : G âŸ¶ Y, âˆƒ! h', h' â‰« f = h) â†’ is_iso f :=
(is_detector_def _).1

lemma is_codetector_def (G : C) :
  is_codetector G â†” âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : X âŸ¶ G, âˆƒ! h', f â‰« h' = h) â†’ is_iso f :=
âŸ¨Î» hG X Y f hf, hG _ $ Î» H hH h, by { obtain rfl := set.mem_singleton_iff.1 hH, exact hf h },
 Î» hG X Y f hf, hG _ $ Î» h, hf _ (set.mem_singleton _) _âŸ©

lemma is_codetector.def {G : C} :
  is_codetector G â†’ âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), (âˆ€ h : X âŸ¶ G, âˆƒ! h', f â‰« h' = h) â†’ is_iso f :=
(is_codetector_def _).1

lemma is_separator_iff_faithful_coyoneda_obj (G : C) :
  is_separator G â†” faithful (coyoneda.obj (op G)) :=
âŸ¨Î» hG, âŸ¨Î» X Y f g hfg, hG.def _ _ (congr_fun hfg)âŸ©,
 Î» h, (is_separator_def _).2 $ Î» X Y f g hfg,
  by exactI (coyoneda.obj (op G)).map_injective (funext hfg)âŸ©

lemma is_coseparator_iff_faithful_yoneda_obj (G : C) :
  is_coseparator G â†” faithful (yoneda.obj G) :=
âŸ¨Î» hG, âŸ¨Î» X Y f g hfg, quiver.hom.unop_inj (hG.def _ _ (congr_fun hfg))âŸ©,
 Î» h, (is_coseparator_def _).2 $ Î» X Y f g hfg, quiver.hom.op_inj $
  by exactI (yoneda.obj G).map_injective (funext hfg)âŸ©

lemma is_separator_iff_epi (G : C) [Î  A : C, has_coproduct (Î» (f : G âŸ¶ A), G)] :
  is_separator G â†” âˆ€ (A : C), epi (sigma.desc (Î» (f : G âŸ¶ A), f)) :=
begin
  rw is_separator_def,
  refine âŸ¨Î» h A, âŸ¨Î» Z u v huv, h _ _ (Î» i, _)âŸ©, Î» h X Y f g hh, _âŸ©,
  { simpa using (sigma.Î¹ _ i) â‰«= huv },
  { haveI := h X,
    refine (cancel_epi (sigma.desc (Î» (f : G âŸ¶ X), f))).1 (colimit.hom_ext (Î» j, _)),
    simpa using hh j.as }
end

lemma is_coseparator_iff_mono (G : C) [Î  A : C, has_product (Î» (f : A âŸ¶ G), G)] :
  is_coseparator G â†” âˆ€ (A : C), mono (pi.lift (Î» (f : A âŸ¶ G), f)) :=
begin
  rw is_coseparator_def,
  refine âŸ¨Î» h A, âŸ¨Î» Z u v huv, h _ _ (Î» i, _)âŸ©, Î» h X Y f g hh, _âŸ©,
  { simpa using huv =â‰« (pi.Ï€ _ i) },
  { haveI := h Y,
    refine (cancel_mono (pi.lift (Î» (f : Y âŸ¶ G), f))).1 (limit.hom_ext (Î» j, _)),
    simpa using hh j.as }
end

section zero_morphisms
variables [has_zero_morphisms C]

lemma is_separator_coprod (G H : C) [has_binary_coproduct G H] :
  is_separator (G â¨¿ H) â†” is_separating ({G, H} : set C) :=
begin
  refine âŸ¨Î» h X Y u v huv, _, Î» h, (is_separator_def _).2 (Î» X Y u v huv, h _ _ (Î» Z hZ g, _))âŸ©,
  { refine h.def _ _ (Î» g, coprod.hom_ext _ _),
    { simpa using huv G (by simp) (coprod.inl â‰« g) },
    { simpa using huv H (by simp) (coprod.inr â‰« g) } },
  { simp only [set.mem_insert_iff, set.mem_singleton_iff] at hZ,
    unfreezingI { rcases hZ with rfl|rfl },
    { simpa using coprod.inl â‰«= huv (coprod.desc g 0) },
    { simpa using coprod.inr â‰«= huv (coprod.desc 0 g) } }
end

lemma is_separator_coprod_of_is_separator_left (G H : C) [has_binary_coproduct G H]
  (hG : is_separator G) : is_separator (G â¨¿ H) :=
(is_separator_coprod _ _).2 $ is_separating.mono hG $ by simp

lemma is_separator_coprod_of_is_separator_right (G H : C) [has_binary_coproduct G H]
  (hH : is_separator H) : is_separator (G â¨¿ H) :=
(is_separator_coprod _ _).2 $ is_separating.mono hH $ by simp

lemma is_separator_sigma {Î² : Type w} (f : Î² â†’ C) [has_coproduct f] :
  is_separator (âˆ f) â†” is_separating (set.range f) :=
begin
  refine âŸ¨Î» h X Y u v huv, _, Î» h, (is_separator_def _).2 (Î» X Y u v huv, h _ _ (Î» Z hZ g, _))âŸ©,
  { refine h.def _ _ (Î» g, colimit.hom_ext (Î» b, _)),
    simpa using huv (f b.as) (by simp) (colimit.Î¹ (discrete.functor f) _ â‰« g) },
  { obtain âŸ¨b, rflâŸ© := set.mem_range.1 hZ,
    classical,
    simpa using sigma.Î¹ f b â‰«= huv (sigma.desc (pi.single b g)) }
end

lemma is_separator_sigma_of_is_separator {Î² : Type w} (f : Î² â†’ C) [has_coproduct f]
  (b : Î²) (hb : is_separator (f b)) : is_separator (âˆ f) :=
(is_separator_sigma _).2 $ is_separating.mono hb $ by simp

lemma is_coseparator_prod (G H : C) [has_binary_product G H] :
  is_coseparator (G â¨¯ H) â†” is_coseparating ({G, H} : set C) :=
begin
  refine âŸ¨Î» h X Y u v huv, _, Î» h, (is_coseparator_def _).2 (Î» X Y u v huv, h _ _ (Î» Z hZ g, _))âŸ©,
  { refine h.def _ _ (Î» g, prod.hom_ext _ _),
    { simpa using huv G (by simp) (g â‰« limits.prod.fst) },
    { simpa using huv H (by simp) (g â‰« limits.prod.snd) } },
  { simp only [set.mem_insert_iff, set.mem_singleton_iff] at hZ,
    unfreezingI { rcases hZ with rfl|rfl },
    { simpa using huv (prod.lift g 0) =â‰« limits.prod.fst },
    { simpa using huv (prod.lift 0 g) =â‰« limits.prod.snd } }
end

lemma is_coseparator_prod_of_is_coseparator_left (G H : C) [has_binary_product G H]
  (hG : is_coseparator G) : is_coseparator (G â¨¯ H) :=
(is_coseparator_prod _ _).2 $ is_coseparating.mono hG $ by simp

lemma is_coseparator_prod_of_is_coseparator_right (G H : C) [has_binary_product G H]
  (hH : is_coseparator H) : is_coseparator (G â¨¯ H) :=
(is_coseparator_prod _ _).2 $ is_coseparating.mono hH $ by simp

lemma is_coseparator_pi {Î² : Type w} (f : Î² â†’ C) [has_product f] :
  is_coseparator (âˆ f) â†” is_coseparating (set.range f) :=
begin
  refine âŸ¨Î» h X Y u v huv, _, Î» h, (is_coseparator_def _).2 (Î» X Y u v huv, h _ _ (Î» Z hZ g, _))âŸ©,
  { refine h.def _ _ (Î» g, limit.hom_ext (Î» b, _)),
    simpa using huv (f b.as) (by simp) (g â‰« limit.Ï€ (discrete.functor f) _ ) },
  { obtain âŸ¨b, rflâŸ© := set.mem_range.1 hZ,
    classical,
    simpa using huv (pi.lift (pi.single b g)) =â‰« pi.Ï€ f b }
end

lemma is_coseparator_pi_of_is_coseparator {Î² : Type w} (f : Î² â†’ C) [has_product f]
  (b : Î²) (hb : is_coseparator (f b)) : is_coseparator (âˆ f) :=
(is_coseparator_pi _).2 $ is_coseparating.mono hb $ by simp

end zero_morphisms

lemma is_detector_iff_reflects_isomorphisms_coyoneda_obj (G : C) :
  is_detector G â†” reflects_isomorphisms (coyoneda.obj (op G)) :=
begin
  refine âŸ¨Î» hG, âŸ¨Î» X Y f hf, hG.def _ (Î» h, _)âŸ©, Î» h, (is_detector_def _).2 (Î» X Y f hf, _)âŸ©,
  { rw [is_iso_iff_bijective, function.bijective_iff_exists_unique] at hf,
    exact hf h },
  { suffices : is_iso ((coyoneda.obj (op G)).map f),
    { exactI @is_iso_of_reflects_iso _ _ _ _ _ _ _ (coyoneda.obj (op G)) _ h },
    rwa [is_iso_iff_bijective, function.bijective_iff_exists_unique] }
end

lemma is_codetector_iff_reflects_isomorphisms_yoneda_obj (G : C) :
  is_codetector G â†” reflects_isomorphisms (yoneda.obj G) :=
begin
  refine âŸ¨Î» hG, âŸ¨Î» X Y f hf, _ âŸ©, Î» h, (is_codetector_def _).2 (Î» X Y f hf, _)âŸ©,
  { refine (is_iso_unop_iff _).1 (hG.def _ _),
    rwa [is_iso_iff_bijective, function.bijective_iff_exists_unique] at hf },
  { rw â† is_iso_op_iff,
    suffices : is_iso ((yoneda.obj G).map f.op),
    { exactI @is_iso_of_reflects_iso _ _ _ _ _ _ _ (yoneda.obj G) _ h },
    rwa [is_iso_iff_bijective, function.bijective_iff_exists_unique] }
end

lemma well_powered_of_is_detector [has_pullbacks C] (G : C) (hG : is_detector G) :
  well_powered C :=
well_powered_of_is_detecting hG

end category_theory
