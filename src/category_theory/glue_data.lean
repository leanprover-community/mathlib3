/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import tactic.elementwise
import category_theory.limits.shapes.multiequalizer
import category_theory.limits.constructions.epi_mono
import category_theory.limits.preserves.limits
import category_theory.limits.shapes.types

/-!
# Gluing data

We define `glue_data` as a family of data needed to glue topological spaces, schemes, etc. We
provide the API to realize it as a multispan diagram, and also states lemmas about its
interaction with a functor that preserves certain pullbacks.

-/

noncomputable theory

open category_theory.limits
namespace category_theory

universes v uâ‚ uâ‚‚

variables (C : Type uâ‚) [category.{v} C] {C' : Type uâ‚‚} [category.{v} C']

/--
A gluing datum consists of
1. An index type `J`
2. An object `U i` for each `i : J`.
3. An object `V i j` for each `i j : J`.
4. A monomorphism `f i j : V i j âŸ¶ U i` for each `i j : J`.
5. A transition map `t i j : V i j âŸ¶ V j i` for each `i j : J`.
such that
6. `f i i` is an isomorphism.
7. `t i i` is the identity.
8. The pullback for `f i j` and `f i k` exists.
9. `V i j Ã—[U i] V i k âŸ¶ V i j âŸ¶ V j i` factors through `V j k Ã—[U j] V j i âŸ¶ V j i` via some
    `t' : V i j Ã—[U i] V i k âŸ¶ V j k Ã—[U j] V j i`.
10. `t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _`.
-/
@[nolint has_inhabited_instance]
structure glue_data :=
(J : Type v)
(U : J â†’ C)
(V : J Ã— J â†’ C)
(f : Î  i j, V (i, j) âŸ¶ U i)
(f_mono : âˆ€ i j, mono (f i j) . tactic.apply_instance)
(f_has_pullback : âˆ€ i j k, has_pullback (f i j) (f i k) . tactic.apply_instance)
(f_id : âˆ€ i, is_iso (f i i) . tactic.apply_instance)
(t : Î  i j, V (i, j) âŸ¶ V (j, i))
(t_id : âˆ€ i, t i i = ğŸ™ _)
(t' : Î  i j k, pullback (f i j) (f i k) âŸ¶ pullback (f j k) (f j i))
(t_fac : âˆ€ i j k, t' i j k â‰« pullback.snd = pullback.fst â‰« t i j)
(cocycle : âˆ€ i j k , t' i j k â‰« t' j k i â‰« t' k i j = ğŸ™ _)

attribute [simp] glue_data.t_id
attribute [instance] glue_data.f_id glue_data.f_mono glue_data.f_has_pullback
attribute [reassoc] glue_data.t_fac glue_data.cocycle

namespace glue_data

variables {C} (D : glue_data C)

@[simp] lemma t'_iij (i j : D.J) : D.t' i i j = (pullback_symmetry _ _).hom :=
begin
  have eqâ‚ := D.t_fac i i j,
  have eqâ‚‚ := (is_iso.eq_comp_inv (D.f i i)).mpr (@pullback.condition _ _ _ _ _ _ (D.f i j) _),
  rw [D.t_id, category.comp_id, eqâ‚‚] at eqâ‚,
  have eqâ‚ƒ := (is_iso.eq_comp_inv (D.f i i)).mp eqâ‚,
  rw [category.assoc, â†pullback.condition, â†category.assoc] at eqâ‚ƒ,
  exact mono.right_cancellation _ _
    ((mono.right_cancellation _ _ eqâ‚ƒ).trans (pullback_symmetry_hom_comp_fst _ _).symm)
end

lemma t'_jii (i j : D.J) : D.t' j i i = pullback.fst â‰« D.t j i â‰« inv pullback.snd :=
by { rw [â†category.assoc, â†D.t_fac], simp }

lemma t'_iji (i j : D.J) : D.t' i j i = pullback.fst â‰« D.t i j â‰« inv pullback.snd :=
by { rw [â†category.assoc, â†D.t_fac], simp }

@[simp, reassoc, elementwise] lemma t_inv (i j : D.J) :
  D.t i j â‰« D.t j i = ğŸ™ _ :=
begin
  have eq : (pullback_symmetry (D.f i i) (D.f i j)).hom = pullback.snd â‰« inv pullback.fst,
  { simp },
  have := D.cocycle i j i,
  rw [D.t'_iij, D.t'_jii, D.t'_iji, fst_eq_snd_of_mono_eq, eq] at this,
  simp only [category.assoc, is_iso.inv_hom_id_assoc] at this,
  rw [â†is_iso.eq_inv_comp, â†category.assoc, is_iso.comp_inv_eq] at this,
  simpa using this,
end

lemma t'_inv (i j k : D.J) : D.t' i j k â‰« (pullback_symmetry _ _).hom â‰«
  D.t' j i k â‰« (pullback_symmetry _ _).hom = ğŸ™ _ :=
begin
  rw â† cancel_mono (pullback.fst : pullback (D.f i j) (D.f i k) âŸ¶ _),
  simp [t_fac, t_fac_assoc]
end

instance t_is_iso (i j : D.J) : is_iso (D.t i j) :=
âŸ¨âŸ¨D.t j i, D.t_inv _ _, D.t_inv _ _âŸ©âŸ©

instance t'_is_iso (i j k : D.J) : is_iso (D.t' i j k) :=
âŸ¨âŸ¨D.t' j k i â‰« D.t' k i j, D.cocycle _ _ _, (by simpa using D.cocycle _ _ _)âŸ©âŸ©

@[reassoc]
lemma t'_comp_eq_pullback_symmetry (i j k : D.J) :
  D.t' j k i â‰« D.t' k i j = (pullback_symmetry _ _).hom â‰«
  D.t' j i k â‰« (pullback_symmetry _ _).hom :=
begin
  transitivity inv (D.t' i j k),
  { exact is_iso.eq_inv_of_hom_inv_id (D.cocycle _ _ _) },
  { rw â† cancel_mono (pullback.fst : pullback (D.f i j) (D.f i k) âŸ¶ _),
    simp [t_fac, t_fac_assoc] }
end

/-- (Implementation) The disjoint union of `U i`. -/
def sigma_opens [has_coproduct D.U] : C := âˆ D.U

/-- (Implementation) The diagram to take colimit of. -/
def diagram : multispan_index C :=
{ L := D.J Ã— D.J, R := D.J,
  fst_from := _root_.prod.fst, snd_from := _root_.prod.snd,
  left := D.V, right := D.U,
  fst := Î» âŸ¨i, jâŸ©, D.f i j,
  snd := Î» âŸ¨i, jâŸ©, D.t i j â‰« D.f j i }

@[simp] lemma diagram_L : D.diagram.L = (D.J Ã— D.J) := rfl
@[simp] lemma diagram_R : D.diagram.R = D.J := rfl
@[simp] lemma diagram_fst_from (i j : D.J) : D.diagram.fst_from âŸ¨i, jâŸ© = i := rfl
@[simp] lemma diagram_snd_from (i j : D.J) : D.diagram.snd_from âŸ¨i, jâŸ© = j := rfl
@[simp] lemma diagram_fst (i j : D.J) : D.diagram.fst âŸ¨i, jâŸ© = D.f i j := rfl
@[simp] lemma diagram_snd (i j : D.J) : D.diagram.snd âŸ¨i, jâŸ© = D.t i j â‰« D.f j i := rfl
@[simp] lemma diagram_left : D.diagram.left = D.V := rfl
@[simp] lemma diagram_right : D.diagram.right = D.U := rfl

section

variable [has_multicoequalizer D.diagram]

/-- The glued object given a family of gluing data. -/
def glued : C := multicoequalizer D.diagram

/-- The map `D.U i âŸ¶ D.glued` for each `i`. -/
def Î¹ (i : D.J) : D.U i âŸ¶ D.glued :=
multicoequalizer.Ï€ D.diagram i

@[simp, elementwise]
lemma glue_condition (i j : D.J) :
  D.t i j â‰« D.f j i â‰« D.Î¹ j = D.f i j â‰« D.Î¹ i :=
(category.assoc _ _ _).symm.trans (multicoequalizer.condition D.diagram âŸ¨i, jâŸ©).symm

/-- The pullback cone spanned by `V i j âŸ¶ U i` and `V i j âŸ¶ U j`.
This will often be a pullback diagram. -/
 def V_pullback_cone (i j : D.J) : pullback_cone (D.Î¹ i) (D.Î¹ j) :=
 pullback_cone.mk (D.f i j) (D.t i j â‰« D.f j i) (by simp)

variables [has_colimits C]

/-- The projection `âˆ D.U âŸ¶ D.glued` given by the colimit. -/
def Ï€ : D.sigma_opens âŸ¶ D.glued := multicoequalizer.sigma_Ï€ D.diagram

instance Ï€_epi : epi D.Ï€ := by { unfold Ï€, apply_instance }

end

lemma types_Ï€_surjective (D : glue_data Type*) :
  function.surjective D.Ï€ := (epi_iff_surjective _).mp infer_instance

lemma types_Î¹_jointly_surjective (D : glue_data Type*) (x : D.glued) :
  âˆƒ i (y : D.U i), D.Î¹ i y = x :=
begin
  delta category_theory.glue_data.Î¹,
  simp_rw â† multicoequalizer.Î¹_sigma_Ï€ D.diagram,
  rcases D.types_Ï€_surjective x with âŸ¨x', rflâŸ©,
  have := colimit.iso_colimit_cocone (types.coproduct_colimit_cocone _),
  rw â† (show (colimit.iso_colimit_cocone (types.coproduct_colimit_cocone _)).inv _ = x',
    from concrete_category.congr_hom
      ((colimit.iso_colimit_cocone (types.coproduct_colimit_cocone _)).hom_inv_id) x'),
  rcases (colimit.iso_colimit_cocone (types.coproduct_colimit_cocone _)).hom x' with âŸ¨i, yâŸ©,
  exact âŸ¨i, y, by { simpa [â† multicoequalizer.Î¹_sigma_Ï€, -multicoequalizer.Î¹_sigma_Ï€] }âŸ©
end

variables (F : C â¥¤ C') [H : âˆ€ i j k, preserves_limit (cospan (D.f i j) (D.f i k)) F]

include H

instance (i j k : D.J) : has_pullback (F.map (D.f i j)) (F.map (D.f i k)) :=
âŸ¨âŸ¨âŸ¨_, is_limit_of_has_pullback_of_preserves_limit F (D.f i j) (D.f i k)âŸ©âŸ©âŸ©

/-- A functor that preserves the pullbacks of `f i j` and `f i k` can map a family of glue data. -/
@[simps] def map_glue_data :
  glue_data C' :=
{ J := D.J,
  U := Î» i, F.obj (D.U i),
  V := Î» i, F.obj (D.V i),
  f := Î» i j, F.map (D.f i j),
  f_mono := Î» i j, category_theory.preserves_mono F (D.f i j),
  f_id := Î» i, infer_instance,
  t := Î» i j, F.map (D.t i j),
  t_id := Î» i, by { rw D.t_id i, simp },
  t' := Î» i j k, (preserves_pullback.iso F (D.f i j) (D.f i k)).inv â‰«
    F.map (D.t' i j k) â‰« (preserves_pullback.iso F (D.f j k) (D.f j i)).hom,
  t_fac := Î» i j k, by simpa [iso.inv_comp_eq] using congr_arg (Î» f, F.map f) (D.t_fac i j k),
  cocycle := Î» i j k, by simp only [category.assoc, iso.hom_inv_id_assoc, â† functor.map_comp_assoc,
    D.cocycle, iso.inv_hom_id, category_theory.functor.map_id, category.id_comp] }

/--
The diagram of the image of a `glue_data` under a functor `F` is naturally isomorphic to the
original diagram of the `glue_data` via `F`.
-/
def diagram_iso : D.diagram.multispan â‹™ F â‰… (D.map_glue_data F).diagram.multispan :=
nat_iso.of_components
  (Î» x, match x with
    | walking_multispan.left a := iso.refl _
    | walking_multispan.right b := iso.refl _
    end)
  (begin
    rintros (âŸ¨_,_âŸ©|_) _ (_|_|_),
    { erw [category.comp_id, category.id_comp, functor.map_id], refl },
    { erw [category.comp_id, category.id_comp], refl },
    { erw [category.comp_id, category.id_comp, functor.map_comp], refl },
    { erw [category.comp_id, category.id_comp, functor.map_id], refl },
  end)

@[simp] lemma diagram_iso_app_left (i : D.J Ã— D.J) :
  (D.diagram_iso F).app (walking_multispan.left i) = iso.refl _ := rfl

@[simp] lemma diagram_iso_app_right (i : D.J) :
  (D.diagram_iso F).app (walking_multispan.right i) = iso.refl _ := rfl

@[simp] lemma diagram_iso_hom_app_left (i : D.J Ã— D.J) :
  (D.diagram_iso F).hom.app (walking_multispan.left i) = ğŸ™ _ := rfl

@[simp] lemma diagram_iso_hom_app_right (i : D.J) :
  (D.diagram_iso F).hom.app (walking_multispan.right i) = ğŸ™ _ := rfl

@[simp] lemma diagram_iso_inv_app_left (i : D.J Ã— D.J) :
  (D.diagram_iso F).inv.app (walking_multispan.left i) = ğŸ™ _ := rfl

@[simp] lemma diagram_iso_inv_app_right (i : D.J) :
  (D.diagram_iso F).inv.app (walking_multispan.right i) = ğŸ™ _ := rfl

variables [has_multicoequalizer D.diagram] [preserves_colimit D.diagram.multispan F]

omit H

lemma has_colimit_multispan_comp : has_colimit (D.diagram.multispan â‹™ F) :=
âŸ¨âŸ¨âŸ¨_,preserves_colimit.preserves (colimit.is_colimit _)âŸ©âŸ©âŸ©

include H

local attribute [instance] has_colimit_multispan_comp

lemma has_colimit_map_glue_data_diagram : has_multicoequalizer (D.map_glue_data F).diagram :=
has_colimit_of_iso (D.diagram_iso F).symm

local attribute [instance] has_colimit_map_glue_data_diagram

/-- If `F` preserves the gluing, we obtain an iso between the glued objects. -/
def glued_iso : F.obj D.glued â‰… (D.map_glue_data F).glued :=
preserves_colimit_iso F D.diagram.multispan â‰ªâ‰«
  (limits.has_colimit.iso_of_nat_iso (D.diagram_iso F))

@[simp, reassoc]
lemma Î¹_glued_iso_hom (i : D.J) :
  F.map (D.Î¹ i) â‰« (D.glued_iso F).hom = (D.map_glue_data F).Î¹ i :=
by { erw Î¹_preserves_colimits_iso_hom_assoc, rw has_colimit.iso_of_nat_iso_Î¹_hom,
  erw category.id_comp, refl }

@[simp, reassoc]
lemma Î¹_glued_iso_inv (i : D.J) :
  (D.map_glue_data F).Î¹ i â‰« (D.glued_iso F).inv = F.map (D.Î¹ i) :=
by rw [iso.comp_inv_eq, Î¹_glued_iso_hom]

/-- If `F` preserves the gluing, and reflects the pullback of `U i âŸ¶ glued` and `U j âŸ¶ glued`,
then `F` reflects the fact that `V_pullback_cone` is a pullback. -/
def V_pullback_cone_is_limit_of_map (i j : D.J) [reflects_limit (cospan (D.Î¹ i) (D.Î¹ j)) F]
  (hc : is_limit ((D.map_glue_data F).V_pullback_cone i j)) :
  is_limit (D.V_pullback_cone i j) :=
begin
  apply is_limit_of_reflects F,
  apply (is_limit_map_cone_pullback_cone_equiv _ _).symm _,
  let e : cospan (F.map (D.Î¹ i)) (F.map (D.Î¹ j)) â‰…
    cospan ((D.map_glue_data F).Î¹ i) ((D.map_glue_data F).Î¹ j),
  exact nat_iso.of_components
    (Î» x, by { cases x, exacts [D.glued_iso F, iso.refl _] })
    (by rintros (_|_) (_|_) (_|_|_); simp),
  apply is_limit.postcompose_hom_equiv e _ _,
  apply hc.of_iso_limit,
  refine cones.ext (iso.refl _) _,
  { rintro (_|_|_),
    change _ = _ â‰« (_ â‰« _) â‰« _,
    all_goals { change _ = ğŸ™ _ â‰« _ â‰« _, simpa } }
end

omit H

/-- If there is a forgetful functor into `Type` that preserves enough (co)limits, then `D.Î¹` will
be jointly surjective. -/
lemma Î¹_jointly_surjective (F : C â¥¤ Type v) [preserves_colimit D.diagram.multispan F]
  [Î  (i j k : D.J), preserves_limit (cospan (D.f i j) (D.f i k)) F] (x : F.obj (D.glued)) :
  âˆƒ i (y : F.obj (D.U i)), F.map (D.Î¹ i) y = x :=
begin
  let e := D.glued_iso F,
  obtain âŸ¨i, y, eqâŸ© := (D.map_glue_data F).types_Î¹_jointly_surjective (e.hom x),
  replace eq := congr_arg e.inv eq,
  change ((D.map_glue_data F).Î¹ i â‰« e.inv) y = (e.hom â‰« e.inv) x at eq,
  rw [e.hom_inv_id, D.Î¹_glued_iso_inv] at eq,
  exact âŸ¨i, y, eqâŸ©
end

end glue_data

end category_theory
