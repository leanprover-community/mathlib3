/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import tactic.elementwise
import category_theory.limits.shapes.multiequalizer
import category_theory.limits.constructions.epi_mono

/-!
# Gluing Topological spaces

Given a family of gluing data, consisting of
1. An index type `Œπ`
2. A topological space `U i` for each `i : Œπ`.
3. An open subset `V i j ‚äÜ U i` for each `i j : Œπ`.
4. A transition map `f i j : V i j ‚ü∂ V j i` for each `i j : Œπ`.
such that
5. `V i i = U i`.
6. `f i i` is the identity.
7. `f i j x ‚àà V j k` for all `x ‚àà V i j ‚à© V i k`.
8. `f i j ‚â´ f j k = f i k`.

We can then glue the topological spaces `U i` along `V i j`.

THe construction should be "sealed" and considered as a black box, while only using the API
provided.

## Main definitions

* `Top.gluing_data`: A structure containing the family of gluing data.
* `Top.gluing_data.glued`: The glued topological space.
    This is defined as the coequalizer of `‚àê V i j ‚áâ ‚àê U i`, so that the general colimit API can
    be used.
* `Top.gluing_data.imm`: The immersion `imm i : U i ‚ü∂ glued` for each `i : Œπ`.
* `Top.gluing_data.rel`: A relation on `Œ£ i, D.U i` defined by `‚ü®i, x‚ü© ~ ‚ü®j, y‚ü©` iff
    `‚ü®i, x‚ü© = ‚ü®j, y‚ü©` or `f i j x = y`. See `Top.gluing_data.imm_eq_iff_rel`.

## Main results

* `Top.gluing_data.is_open_iff`: A set in `glued` is open iff its preimage along each `imm i` is
    open.
* `Top.gluing_data.imm_jointly_surjective`: The `imm i`s are jointly surjective.
* `Top.gluing_data.glue_condition` : `f i j ‚â´ imm j = imm i`.
* `Top.gluing_data.rel_equiv`: `rel` is an equivalence relation.
* `Top.gluing_data.imm_eq_iff_rel`: `imm i x = imm j y ‚Üî ‚ü®i, x‚ü© ~ ‚ü®j, y‚ü©`.
* `Top.gluing_data.image_inter`: The intersection of the images of `U i` and `U j` in `glued` is
    `V i j`.
* `Top.gluing_data.preimage_range`: The preimage of the image of `U i` in `U j` is `V i j`.
* `Top.gluing_data.preimage_image_eq_preimage_f`: The preimage of the image of some `U ‚äÜ U i` is
    given by the preimage along `f j i`.
* `Top.gluing_data.imm_open_embedding`: Each of the `imm i`s are open embeddings.

-/

noncomputable theory

open category_theory.limits
namespace category_theory

universes v u‚ÇÅ u‚ÇÇ

variables (C : Type u‚ÇÅ) [category.{v} C] {C' : Type u‚ÇÇ} [category.{v} C']

/--
A family of gluing data consists of
1. An index type `Œπ`
2. A topological space `U i` for each `i : Œπ`.
3. An open subset `V i j ‚äÜ U i` for each `i j : Œπ`.
4. A transition map `f i j : V i j ‚ü∂ V j i` for each `i j : Œπ`.
such that
5. `V i i = U i`.
6. `f i i` is the identity.
7. `f i j x ‚àà V j k` for all `x ‚àà V i j ‚à© V i k`.
8. `f i j ‚â´ f j k = f i k`.

We can then glue the topological spaces `U i` along `V i j`.
-/
@[nolint has_inhabited_instance]
structure glue_data :=
  (Œπ : Type v)
  (U : Œπ ‚Üí C)
  (V : Œπ √ó Œπ ‚Üí C)
  (f : Œ† i j, V (i, j) ‚ü∂ U i)
  (f_mono : ‚àÄ i j, mono (f i j) . tactic.apply_instance)
  (f_has_pullback : ‚àÄ i j k, has_pullback (f i j) (f i k) . tactic.apply_instance)
  (f_id : ‚àÄ i, is_iso (f i i))
  (t : Œ† i j, V (i, j) ‚ü∂ V (j, i))
  (t_id : ‚àÄ i, t i i = ùüô _)
  (t' : Œ† i j k, pullback (f i j) (f i k) ‚ü∂ pullback (f j k) (f j i))
  (t_fac : ‚àÄ i j k, t' i j k ‚â´ pullback.snd = pullback.fst ‚â´ t i j)
  (cocycle : ‚àÄ i j k , t' i j k ‚â´ t' j k i ‚â´ t' k i j = ùüô _)

attribute [simp] glue_data.t_id
attribute [instance] glue_data.f_id glue_data.f_mono glue_data.f_has_pullback
attribute [reassoc] glue_data.t_fac glue_data.cocycle

namespace glue_data

variables {C} (D : glue_data C)

@[simp] lemma t'_iij (i j : D.Œπ) : D.t' i i j = (pullback_symmetry _ _).hom :=
begin
  have eq‚ÇÅ := D.t_fac i i j,
  have eq‚ÇÇ := (is_iso.eq_comp_inv (D.f i i)).mpr (@pullback.condition _ _ _ _ _ _ (D.f i j) _),
  rw [D.t_id, category.comp_id, eq‚ÇÇ] at eq‚ÇÅ,
  have eq‚ÇÉ := (is_iso.eq_comp_inv (D.f i i)).mp eq‚ÇÅ,
  rw [category.assoc, ‚Üêpullback.condition, ‚Üêcategory.assoc] at eq‚ÇÉ,
  exact mono.right_cancellation _ _
    ((mono.right_cancellation _ _ eq‚ÇÉ).trans (pullback_symmetry_hom_comp_fst _ _).symm)
end

lemma t'_jii (i j : D.Œπ) : D.t' j i i = pullback.fst ‚â´ D.t j i ‚â´ inv pullback.snd :=
by { rw [‚Üêcategory.assoc, ‚ÜêD.t_fac], simp }

lemma t'_iji (i j : D.Œπ) : D.t' i j i = pullback.fst ‚â´ D.t i j ‚â´ inv pullback.snd :=
by { rw [‚Üêcategory.assoc, ‚ÜêD.t_fac], simp }

@[simp, reassoc, elementwise] lemma t_inv (i j : D.Œπ) :
  D.t i j ‚â´ D.t j i = ùüô _ :=
begin
  have eq : (pullback_symmetry (D.f i i) (D.f i j)).hom = pullback.snd ‚â´ inv pullback.fst,
  simp,
  have := D.cocycle i j i,
  rw [D.t'_iij, D.t'_jii, D.t'_iji, fst_eq_snd_of_mono_eq, eq] at this,
  simp only [category.assoc, is_iso.inv_hom_id_assoc] at this,
  rw [‚Üêis_iso.eq_inv_comp, ‚Üêcategory.assoc, is_iso.comp_inv_eq] at this,
  simpa using this,
end

instance t_is_iso (i j : D.Œπ) : is_iso (D.t i j) :=
‚ü®‚ü®D.t j i, D.t_inv _ _, D.t_inv _ _‚ü©‚ü©

instance t'_is_iso (i j k : D.Œπ) : is_iso (D.t' i j k) :=
‚ü®‚ü®D.t' j k i ‚â´ D.t' k i j, D.cocycle _ _ _, (by simpa using D.cocycle _ _ _)‚ü©‚ü©

/-- (Implementation) The disjoint union of `U i`. -/
def sigma_opens [has_coproduct D.U] : C := ‚àê D.U

/-- (Implementation) The diagram to take colimit of. -/
def diagram : multispan_index C :=
{ L := D.Œπ √ó D.Œπ, R := D.Œπ,
  fst_from := _root_.prod.fst, snd_from := _root_.prod.snd,
  left := D.V, right := D.U,
  fst := Œª ‚ü®i, j‚ü©, D.f i j,
  snd := Œª ‚ü®i, j‚ü©, D.t i j ‚â´ D.f j i }

variable [has_multicoequalizer D.diagram]

/-- The glued topological space given a family of gluing data. -/
def glued : C := multicoequalizer D.diagram

/-- The open immersion `D.U i ‚ü∂ D.glued` for each `i`. -/
def imm (i : D.Œπ) : D.U i ‚ü∂ D.glued :=
multicoequalizer.œÄ D.diagram i

variables [has_colimits C]

/-- (Implementation) The projection `‚àê D.U ‚ü∂ D.glued` given by the colimit. -/
def œÄ : D.sigma_opens ‚ü∂ D.glued := multicoequalizer.sigma_œÄ D.diagram

instance œÄ_epi : epi D.œÄ := by { unfold œÄ, apply_instance }

variables (F : C ‚•§ C') [H : ‚àÄ i j k, preserves_limit (cospan (D.f i j) (D.f i k)) F]

include H

instance (i j k : D.Œπ) : has_pullback (F.map (D.f i j)) (F.map (D.f i k)) :=
‚ü®‚ü®‚ü®_, is_limit_of_has_pullback_of_preserves_limit F (D.f i j) (D.f i k)‚ü©‚ü©‚ü©

def map_glue_data :
  glue_data C' :=
{ Œπ := D.Œπ,
  U := Œª i, F.obj (D.U i),
  V := Œª i, F.obj (D.V i),
  f := Œª i j, F.map (D.f i j),
  f_mono := Œª i j, category_theory.preserves_mono F (D.f i j),
  f_id := Œª i, infer_instance,
  t := Œª i j, F.map (D.t i j),
  t_id := Œª i, by { rw D.t_id i, simp },
  t' := Œª i j k, (preserves_pullback.iso F (D.f i j) (D.f i k)).inv ‚â´
    F.map (D.t' i j k) ‚â´ (preserves_pullback.iso F (D.f j k) (D.f j i)).hom,
  t_fac := Œª i j k, by simpa [iso.inv_comp_eq] using congr_arg (Œª f, F.map f) (D.t_fac i j k),
  cocycle := Œª i j k, by simp only [category.assoc, iso.hom_inv_id_assoc, ‚Üê functor.map_comp_assoc,
    D.cocycle, iso.inv_hom_id, category_theory.functor.map_id, category.id_comp] }

def diagram_iso : D.diagram.multispan ‚ãô F ‚âÖ (D.map_glue_data F).diagram.multispan :=
nat_iso.of_components
  (Œª x, match x with
    | walking_multispan.left a := iso.refl _
    | walking_multispan.right b := iso.refl _
    end)
  (begin
    rintros (‚ü®_,_‚ü©|b) _ (_|_|_),
    all_goals
    { try { erw category.comp_id },
      try { erw category.id_comp },
      try { erw functor.map_id },
      try { erw functor.map_comp },
      refl },
  end)

end glue_data

end category_theory
