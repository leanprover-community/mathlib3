/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.shift
import category_theory.limits.shapes.zero

/-!
# The category of graded objects

For any type `Î²`, a `Î²`-graded object over some category `C` is just
a function `Î² â†’ C` into the objects of `C`.
We define the category structure on these.

We describe the `comap` functors obtained by precomposing with functions `Î² â†’ Î³`.

As a consequence a fixed element (e.g. `1`) in an additive group `Î²` provides a shift
functor on `Î²`-graded objects

When `C` has coproducts we construct the `total` functor `graded_object Î² C â¥¤ C`,
show that it is faithful, and deduce that when `C` is concrete so is `graded_object Î² C`.
-/

open category_theory.limits

namespace category_theory

universes w v u

/-- A type synonym for `Î² â†’ C`, used for `Î²`-graded objects in a category `C`. -/
def graded_object (Î² : Type w) (C : Type u) : Type (max w u) := Î² â†’ C

-- Satisfying the inhabited linter...
instance inhabited_graded_object (Î² : Type w) (C : Type u) [inhabited C] :
  inhabited (graded_object Î² C) :=
âŸ¨Î» b, inhabited.default CâŸ©

/--
A type synonym for `Î² â†’ C`, used for `Î²`-graded objects in a category `C`
with a shift functor given by translation by `s`.
-/
@[nolint unused_arguments] -- `s` is here to distinguish type synonyms asking for different shifts
abbreviation graded_object_with_shift {Î² : Type w} [add_comm_group Î²] (s : Î²) (C : Type u) : Type (max w u) := graded_object Î² C

namespace graded_object

variables {C : Type u} [ğ’ : category.{v} C]
include ğ’

instance category_of_graded_objects (Î² : Type w) : category.{(max w v)} (graded_object Î² C) :=
{ hom := Î» X Y, Î  b : Î², X b âŸ¶ Y b,
  id := Î» X b, ğŸ™ (X b),
  comp := Î» X Y Z f g b, f b â‰« g b, }

@[simp]
lemma id_apply {Î² : Type w} (X : graded_object Î² C) (b : Î²) :
  ((ğŸ™ X) : Î  b, X b âŸ¶ X b) b = ğŸ™ (X b) := rfl

@[simp]
lemma comp_apply {Î² : Type w} {X Y Z : graded_object Î² C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (b : Î²) :
  ((f â‰« g) : Î  b, X b âŸ¶ Z b) b = f b â‰« g b := rfl

section
variable (C)

/-- Pull back a graded object along a change-of-grading function. -/
@[simps]
def comap {Î² Î³ : Type w} (f : Î² â†’ Î³) :
  (graded_object Î³ C) â¥¤ (graded_object Î² C) :=
{ obj := Î» X, X âˆ˜ f,
  map := Î» X Y g b, g (f b) }

/--
The natural isomorphism between
pulling back a grading along the identity function,
and the identity functor. -/
@[simps]
def comap_id (Î² : Type w) : comap C (id : Î² â†’ Î²) â‰… ğŸ­ (graded_object Î² C) :=
{ hom := { app := Î» X, ğŸ™ X },
  inv := { app := Î» X, ğŸ™ X } }.

/--
The natural isomorphism comparing between
pulling back along two successive functions, and
pulling back along their composition
-/
@[simps]
def comap_comp {Î² Î³ Î´ : Type w} (f : Î² â†’ Î³) (g : Î³ â†’ Î´) : comap C g â‹™ comap C f â‰… comap C (g âˆ˜ f) :=
{ hom := { app := Î» X b, ğŸ™ (X (g (f b))) },
  inv := { app := Î» X b, ğŸ™ (X (g (f b))) } }

/--
The natural isomorphism comparing between
pulling back along two propositionally equal functions.
-/
@[simps]
def comap_eq {Î² Î³ : Type w} {f g : Î² â†’ Î³} (h : f = g) : comap C f â‰… comap C g :=
{ hom := { app := Î» X b, eq_to_hom begin dsimp [comap], subst h, end },
  inv := { app := Î» X b, eq_to_hom begin dsimp [comap], subst h, end }, }

@[simp]
lemma comap_eq_symm {Î² Î³ : Type w} {f g : Î² â†’ Î³} (h : f = g) : comap_eq C h.symm = (comap_eq C h).symm :=
by tidy

@[simp]
lemma comap_eq_trans {Î² Î³ : Type w} {f g h : Î² â†’ Î³} (k : f = g) (l : g = h) :
  comap_eq C (k.trans l) = comap_eq C k â‰ªâ‰« comap_eq C l :=
begin
  ext X b,
  simp,
end

/--
The equivalence between Î²-graded objects and Î³-graded objects,
given an equivalence between Î² and Î³.
-/
@[simps]
def comap_equiv {Î² Î³ : Type w} (e : Î² â‰ƒ Î³) :
  (graded_object Î² C) â‰Œ (graded_object Î³ C) :=
{ functor := comap C (e.symm : Î³ â†’ Î²),
  inverse := comap C (e : Î² â†’ Î³),
  counit_iso := (comap_comp C _ _).trans (comap_eq C (by { ext, simp } )),
  unit_iso := (comap_eq C (by { ext, simp} )).trans (comap_comp _ _ _).symm,
  functor_unit_iso_comp' := Î» X, begin ext b, dsimp, simp, end, }

end

instance has_shift {Î² : Type} [add_comm_group Î²] (s : Î²) : has_shift.{v} (graded_object_with_shift s C) :=
{ shift := comap_equiv C
  { to_fun := Î» b, b-s,
    inv_fun := Î» b, b+s,
    left_inv := Î» x, (by simp),
    right_inv := Î» x, (by simp), } }

instance has_zero_morphisms [has_zero_morphisms.{v} C] (Î² : Type w) :
  has_zero_morphisms.{(max w v)} (graded_object Î² C) :=
{ has_zero := Î» X Y,
  { zero := Î» b, 0 } }.

section
local attribute [instance] has_zero_object.has_zero

instance has_zero_object [has_zero_object.{v} C] [has_zero_morphisms.{v} C] (Î² : Type w) :
  has_zero_object.{(max w v)} (graded_object Î² C) :=
{ zero := Î» b, (0 : C),
  unique_to := Î» X, âŸ¨âŸ¨Î» b, 0âŸ©, Î» f, (by ext)âŸ©,
  unique_from := Î» X, âŸ¨âŸ¨Î» b, 0âŸ©, Î» f, (by ext)âŸ©, }
end

end graded_object

namespace graded_object
-- The universes get a little hairy here, so we restrict the universe level for the grading to 0.
-- Since we're typically interested in grading by â„¤ or a finite group, this should be okay.
-- If you're grading by things in higher universes, have fun!
variables (Î² : Type)
variables (C : Type u) [ğ’ : category.{v} C]
include ğ’
variables [has_coproducts.{v} C]

/--
The total object of a graded object is the coproduct of the graded components.
-/
def total : graded_object Î² C â¥¤ C :=
{ obj := Î» X, âˆ (Î» i : ulift.{v} Î², X i.down),
  map := Î» X Y f, limits.sigma.map (Î» i, f i.down) }.

variables [decidable_eq Î²] [has_zero_morphisms.{v} C]

/--
The `total` functor taking a graded object to the coproduct of its graded components is faithful.
To prove this, we need to know that the coprojections into the coproduct are monomorphisms,
which follows from the fact we have zero morphisms and decidable equality for the grading.
-/
instance : faithful.{v} (total.{v u} Î² C) :=
{ injectivity' := Î» X Y f g w,
  begin
    ext i,
    replace w := sigma.Î¹ (Î» i : ulift Î², X i.down) âŸ¨iâŸ© â‰«= w,
    erw [colimit.Î¹_map, colimit.Î¹_map] at w,
    exact mono.right_cancellation _ _ w,
  end }

end graded_object

namespace graded_object

variables (Î² : Type) [decidable_eq Î²]
variables (C : Type (u+1)) [large_category C] [ğ’ : concrete_category C]
  [has_coproducts.{u} C] [has_zero_morphisms.{u} C]
include ğ’

instance : concrete_category (graded_object Î² C) :=
{ forget := total Î² C â‹™ forget C }

instance : has_forgetâ‚‚ (graded_object Î² C) C :=
{ forgetâ‚‚ := total Î² C }

end graded_object

end category_theory
