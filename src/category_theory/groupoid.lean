/-
Copyright (c) 2018 Reid Barton All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Reid Barton, Scott Morrison, David WÃ¤rn
-/
import category_theory.full_subcategory
import category_theory.products.basic
import category_theory.pi.basic
import category_theory.category.basic
import tactic.nth_rewrite

/-!
# Groupoids

We define `groupoid` as a typeclass extending `category`,
asserting that all morphisms have inverses.

The instance `is_iso.of_groupoid (f : X âŸ¶ Y) : is_iso f` means that you can then write
`inv f` to access the inverse of any morphism `f`.

`groupoid.iso_equiv_hom : (X â‰… Y) â‰ƒ (X âŸ¶ Y)` provides the equivalence between
isomorphisms and morphisms in a groupoid.

We provide a (non-instance) constructor `groupoid.of_is_iso` from an existing category
with `is_iso f` for every `f`.

## See also

See also `category_theory.core` for the groupoid of isomorphisms in a category.
-/

namespace category_theory

universes v vâ‚‚ u uâ‚‚ -- morphism levels before object levels. See note [category_theory universes].

/-- A `groupoid` is a category such that all morphisms are isomorphisms. -/
class groupoid (obj : Type u) extends category.{v} obj : Type (max u (v+1)) :=
(inv       : Î  {X Y : obj}, (X âŸ¶ Y) â†’ (Y âŸ¶ X))
(inv_comp' : âˆ€ {X Y : obj} (f : X âŸ¶ Y), comp (inv f) f = id Y . obviously)
(comp_inv' : âˆ€ {X Y : obj} (f : X âŸ¶ Y), comp f (inv f) = id X . obviously)

restate_axiom groupoid.inv_comp'
restate_axiom groupoid.comp_inv'

attribute [simp] groupoid.inv_comp groupoid.comp_inv

/--
A `large_groupoid` is a groupoid
where the objects live in `Type (u+1)` while the morphisms live in `Type u`.
-/
abbreviation large_groupoid (C : Type (u+1)) : Type (u+1) := groupoid.{u} C
/--
A `small_groupoid` is a groupoid
where the objects and morphisms live in the same universe.
-/
abbreviation small_groupoid (C : Type u) : Type (u+1) := groupoid.{u} C

section

variables {C : Type u} [groupoid.{v} C] {X Y : C}

@[priority 100] -- see Note [lower instance priority]
instance is_iso.of_groupoid (f : X âŸ¶ Y) : is_iso f :=
âŸ¨âŸ¨groupoid.inv f, by simpâŸ©âŸ©

variables (X Y)

/-- In a groupoid, isomorphisms are equivalent to morphisms. -/
def groupoid.iso_equiv_hom : (X â‰… Y) â‰ƒ (X âŸ¶ Y) :=
{ to_fun := iso.hom,
  inv_fun := Î» f, âŸ¨f, groupoid.inv fâŸ©,
  left_inv := Î» i, iso.ext rfl,
  right_inv := Î» f, rfl }

end

section

variables {C : Type u} [category.{v} C]

/-- A category where every morphism `is_iso` is a groupoid. -/
noncomputable
def groupoid.of_is_iso (all_is_iso : âˆ€ {X Y : C} (f : X âŸ¶ Y), is_iso f) : groupoid.{v} C :=
{ inv := Î» X Y f, inv f }

/-- A category with a unique morphism between any two objects is a groupoid -/
def groupoid.of_hom_unique (all_unique : âˆ€ {X Y : C}, unique (X âŸ¶ Y)) : groupoid.{v} C :=
{ inv := Î» X Y f, all_unique.default }

end

instance induced_category.groupoid {C : Type u} (D : Type uâ‚‚) [groupoid.{v} D] (F : C â†’ D) :
   groupoid.{v} (induced_category D F) :=
{ inv       := Î» X Y f, groupoid.inv f,
  inv_comp' := Î» X Y f, groupoid.inv_comp f,
  comp_inv' := Î» X Y f, groupoid.comp_inv f,
  .. induced_category.category F }

section

instance groupoid_pi {I : Type u} {J : I â†’ Type uâ‚‚} [âˆ€ i, groupoid.{v} (J i)] :
  groupoid.{max u v} (Î  i : I, J i) :=
{ inv := Î» (x y : Î  i, J i) (f : Î  i, x i âŸ¶ y i), (Î» i : I, groupoid.inv (f i)), }

instance groupoid_prod {Î± : Type u} {Î² : Type v} [groupoid.{uâ‚‚} Î±] [groupoid.{vâ‚‚} Î²] :
  groupoid.{max uâ‚‚ vâ‚‚} (Î± Ã— Î²) :=
{ inv := Î» (x y : Î± Ã— Î²) (f : x âŸ¶ y), (groupoid.inv f.1, groupoid.inv f.2) }

@[simp] lemma groupoid.inv_id {V : Type*} [G : groupoid V] (v : V) :
  G.inv (ğŸ™ v) = ğŸ™ v :=
calc G.inv (ğŸ™ v)
   = (G.inv (ğŸ™ v)) â‰« (ğŸ™ v) : (category.comp_id (G.inv (ğŸ™ v))).symm
...= ğŸ™ v                   : groupoid.inv_comp' (ğŸ™ v)

@[simp] lemma groupoid.inv_of_comp {V : Type*} [G : groupoid V]
  {u v w : V} (f : u âŸ¶ v) (g : v âŸ¶ w) : G.inv (f â‰« g) = (G.inv g) â‰« (G.inv f) :=
( calc (G.inv g) â‰« (G.inv f)
     = (G.inv g) â‰« (G.inv f) â‰« (ğŸ™ _) : by simp
  ...= (G.inv g) â‰« (G.inv f) â‰« (f â‰« g) â‰« (G.inv $ f â‰« g) : by simp
  ...= (G.inv g) â‰« g â‰« (G.inv $ f â‰« g) : by {rw category.assoc, nth_rewrite 1 â†category.assoc, simp,}
  ...= G.inv (f â‰« g) : by {rw â†category.assoc, simp, }
).symm

@[simp] lemma groupoid.inv_inv {V : Type*} [G : groupoid V] (u v : V) (f : u âŸ¶ v) :
  G.inv (G.inv f) = f :=
calc G.inv (G.inv f)
   = (G.inv (G.inv f)) â‰« (ğŸ™ v) : by rw category.comp_id
...= (G.inv (G.inv f)) â‰« (G.inv f â‰« f) : by rw â†groupoid.inv_comp
...= (G.inv (G.inv f) â‰« G.inv f) â‰« f : by rw â†category.assoc
...= (ğŸ™ u) â‰« f : by rw groupoid.inv_comp
...= f : by rw category.id_comp

@[simp]
lemma groupoid.functor_map_inv  {C D : Type*} [G : groupoid C] [H : groupoid D] (Ï† : C â¥¤ D)
  {c d : C} (f : c âŸ¶ d) :
  Ï†.map (G.inv f) = H.inv (Ï†.map f) :=
calc Ï†.map (G.inv f)
   = (Ï†.map $ G.inv f) â‰« (ğŸ™ $ Ï†.obj c) : by rw [category.comp_id]
...= (Ï†.map $ G.inv f) â‰« ((Ï†.map f) â‰« (H.inv $ Ï†.map f)) : by rw [groupoid.comp_inv]
...= ((Ï†.map $ G.inv f) â‰« (Ï†.map f)) â‰« (H.inv $ Ï†.map f) : by rw [category.assoc]
...= (Ï†.map $ G.inv f â‰« f) â‰« (H.inv $ Ï†.map f) : by rw [functor.map_comp']
...= (H.inv $ Ï†.map f) : by rw [groupoid.inv_comp,functor.map_id,category.id_comp]


end

end category_theory
