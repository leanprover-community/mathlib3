/-
Copyright (c) 2022 R√©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©mi Bottinelli
-/
import category_theory.groupoid
import category_theory.groupoid.basic
import category_theory.groupoid.subgroupoid

/-!
# Quotient of a groupoid by a normal subgroupoid

This file defines the quotient of a groupoid by a normal (i.e. wide and closed under conjugation)
subgroupoid, and proves the associated universal property of the quotient.

Given a groupoid `C` and normal subgroupoid `S` of `C`:

* `quotient_groupoid S Sn` is the vertex type of the quotient (where `Sn` witnesses normality).
* A groupoid instance on this type is defined.
* `quotient_groupoid.of S Sn` is the functor from `C` to `quotient_groupoid S Sn`.
* `lift`, `lift_spec`, `lift_unique` witness the universal property of the quotient.
* `ker_eq` proves that the kernel of `quotient_groupoid.of` is exactly `S`.

## Implementation notes

The quotient of `C` by `S` is taken in two steps:

* In `isotropy`:
  First only quotienting by the isotropy groups of `S`, so that no collapse of vertices occurs.

* In `thin`:
  Then by quotienting by the "remainder" of `S` in this quotient.
  This remainder `is_thin`, and the second quotient can be constructed as a full subgroupoid
  of the first.

-/

namespace category_theory

open subgroupoid
open groupoid

universes u v

variables {C : Type u} [groupoid C] (S : subgroupoid C)

namespace quotient_groupoid

section isotropy
/-!
We first define what's here called ‚Äúisotropy quotient‚Äù:
Given a normal subgroupoid `S`, this quotient collapses all loops of `S`, i.e.
all vertex groups.
After quotienting by the vertex groups, the image of `S` in the quotient `is_thin`.
-/

section cgr
/-!
Given a the wide subgroupoid `S`, two arrows `f g : c ‚ü∂ d` are equivalent modulo the isotropy
groups of `S` iff there exist `Œ≥ : c ‚ü∂ c` and `Œ¥ : d ‚ü∂ d` both in `S` such that `g = Œ≥ ‚â´ f ‚â´ Œ¥`.
This is an equivalence relation by wideness and since `S` is a subgroupoid.
-/

variables  (Sw : S.is_wide) {c d : C} (f g h : c ‚ü∂ d)

/-- The relation on arrows -/
private def cgr (c) (d) (f) (g) := ‚àÉ (Œ≥ ‚àà S.arrows c c) (Œ¥ ‚àà S.arrows d d), g = Œ≥ ‚â´ f ‚â´ Œ¥

lemma cgr.refl (f : c ‚ü∂ d) : cgr S c d f f :=  ‚ü®(ùüô c), Sw.wide c, (ùüô d), Sw.wide d, by simp ‚ü©

lemma cgr.symm {f g : c ‚ü∂ d} : cgr S c d f g ‚Üí cgr S c d g f :=
Œª ‚ü®Œ≥,hŒ≥,Œ¥,hŒ¥,e‚ü©, ‚ü®inv Œ≥, S.inv hŒ≥, inv Œ¥, S.inv hŒ¥, by { rw e, simp, } ‚ü©

lemma cgr.tran {f g h : c ‚ü∂ d} : cgr S c d f g ‚Üí cgr S c d g h ‚Üí cgr S c d f h :=
Œª ‚ü®Œ≥,hŒ≥,Œ¥,hŒ¥,e‚ü© ‚ü®Œ¥',hŒ¥',Œµ,hŒµ,e'‚ü©,
‚ü®Œ¥' ‚â´ Œ≥, S.mul hŒ¥' hŒ≥, Œ¥ ‚â´ Œµ, S.mul hŒ¥ hŒµ, by {rw [e',e], simp, }‚ü©

/-- `cgr` defines a setoid on `c ‚ü∂ d` for all `c d : C`. -/
private def cgr.setoid : setoid (c ‚ü∂ d) :=
{ r := cgr S c d , iseqv := ‚ü®Œª f, cgr.refl S Sw f, Œª f g, cgr.symm S, Œª f g h, cgr.tran S‚ü© }

end cgr

/--
Type synonym for the vertex set underlying the isotropy quotient
-/
@[nolint unused_arguments]
def isotropy.quotient (S : subgroupoid C) (Sn : S.is_normal) := C

instance (S : subgroupoid C) (Sn : S.is_normal) [h : nonempty C] :
  nonempty (isotropy.quotient S Sn) := h

namespace isotropy

variable (Sn : S.is_normal)

/-- The groupoid structure on the quotient -/
instance : groupoid (isotropy.quotient S Sn) :=
{ hom := Œª c d, quot (cgr S c d),
  id := Œª c, quot.mk _ (ùüô c),
  comp := Œª a b c f g,
    quot.lift_on‚ÇÇ f g
      ( Œª f g, quot.mk (cgr S a c) (f ‚â´ g) )
      ( Œª f g‚ÇÅ g‚ÇÇ ‚ü®Œ≥,hŒ≥,Œ¥,hŒ¥,e‚ü©,
        quot.sound ‚ü®(f ‚â´ Œ≥ ‚â´ inv f), Sn.conj' f hŒ≥, Œ¥, hŒ¥, by
        { rw e, simp only [inv_eq_inv, category.assoc, is_iso.inv_hom_id_assoc], } ‚ü© )
      ( Œª f‚ÇÅ f‚ÇÇ g ‚ü®Œ≥,hŒ≥,Œ¥,hŒ¥,e‚ü©,
        quot.sound ‚ü®Œ≥, hŒ≥, (inv g ‚â´ Œ¥ ‚â´ g), Sn.conj g hŒ¥, by
        { rw e, simp only [category.assoc, inv_eq_inv, is_iso.hom_inv_id_assoc], } ‚ü© ),
  comp_id' := Œª a b, by
    { refine quot.ind (Œª f, _),
      simp only [quot.lift_on‚ÇÇ_mk, category.comp_id], },
  id_comp' := Œª a b, by
    { refine quot.ind (Œª f, _),
      simp only [quot.lift_on‚ÇÇ_mk, category.id_comp], },
  assoc' :=  Œª a b c d f g h, by
    { refine quot.induction_on‚ÇÉ f g h (Œª f g h, _),
      simp only [quot.lift_on‚ÇÇ_mk, category.assoc],  },
  inv := Œª a b f,
    quot.lift_on f
      ( Œª f, quot.mk (cgr S b a) (inv f) )
      ( Œª f‚ÇÅ f‚ÇÇ ‚ü®Œ≥,hŒ≥,Œ¥,hŒ¥,e‚ü©, quot.sound ‚ü®inv Œ¥, S.inv hŒ¥, inv Œ≥, S.inv hŒ≥, by
        { rw e, simp only [inv_eq_inv, is_iso.inv_comp, category.assoc], } ‚ü© ),
  comp_inv' := Œª a b f, by
    { refine quot.induction_on f (Œª f, _),
      simp only [quot.lift_on‚ÇÇ_mk, inv_eq_inv, is_iso.hom_inv_id], },
  inv_comp' := Œª a b f, by
    { refine quot.induction_on f (Œª f, _),
      simp only [quot.lift_on‚ÇÇ_mk, inv_eq_inv, is_iso.inv_hom_id], }, }

/-- The functor from `C` to its isotropy quotient by `S` -/
def of : C ‚•§ (quotient S Sn) :=
{ obj := Œª c, c,
  map := Œª c d f, quot.mk _ f,
  map_id' := Œª _, rfl,
  map_comp' := Œª _ _ _ _ _, rfl, }

lemma of_inj_on_objects : function.injective (of S Sn).obj := Œª a b e, e

lemma of_onto : im (of S Sn) (of_inj_on_objects S Sn) = (‚ä§ : subgroupoid $ quotient S Sn) :=
le_antisymm (le_top) $ Œª ‚ü®c,d,f‚ü© _, quot.induction_on f (Œª f, by { constructor, constructor, })


/-- The image of `S` via the quotient is graph-like (since every loop is killed, essentially) -/
lemma map_is_thin : (map (of S Sn) (of_inj_on_objects S Sn) S).is_thin :=
begin
  rw subgroupoid.is_thin_iff,
  refine Œª c d, subsingleton.intro _,
  rintro ‚ü®_,‚ü®f,hf‚ü©‚ü© ‚ü®_,‚ü®g,hg‚ü©‚ü©,
  simp only [subtype.mk_eq_mk],
  apply quot.sound,
  refine ‚ü®ùüô _, Sn.wide _, inv f ‚â´ g, S.mul (S.inv hf) hg, _‚ü©,
  simp only [inv_eq_inv, is_iso.hom_inv_id_assoc, category.id_comp],
end

section ump
/-!
The universal mapping property of the isotropy quotient of a normal subgroupoid.
-/

variables  {D : Type*} [groupoid D]
  (œÜ : C ‚•§ D)
  (hœÜ : S.disconnect ‚â§ ker œÜ)

include hœÜ

/--
Given a morphism `œÜ : C ‚•§ D` containing the isotropy part of `S` in its kernel, get a
morphism from the isotropy quotient.
-/
def lift : (quotient S Sn) ‚•§ D :=
{ obj := Œª c, œÜ.obj c,
  map := Œª c d f,
    quot.lift_on f
      ( Œª f, œÜ.map f )
      ( Œª f‚ÇÅ f‚ÇÇ ‚ü®Œ≥,hŒ≥,Œ¥,hŒ¥,e‚ü©, by
        { rw subgroupoid.le_iff at hœÜ,
          let hœÜŒ≥ := hœÜ ‚ü®rfl,hŒ≥‚ü©,
          let hœÜŒ¥ := hœÜ ‚ü®rfl,hŒ¥‚ü©,
          simp only [mem_ker_iff, eq_self_iff_true,
                     exists_true_left] at hœÜŒ≥ hœÜŒ¥,
          simp only [e, functor.map_comp, hœÜŒ≥, hœÜŒ¥, category.comp_id, category.id_comp,
                     eq_to_hom_refl], } ),
  map_id' := Œª c, by
  { rw ‚Üêfunctor.map_id œÜ c, refl, },
  map_comp' := Œª a b c f g, by
  { apply quot.induction_on‚ÇÇ f g, rintros, rw ‚Üêfunctor.map_comp œÜ, refl, } }

lemma lift_spec : (of S Sn) ‚ãô (lift S Sn œÜ hœÜ) = œÜ :=
begin
  apply functor.ext,
  { rintros, dsimp only [of, lift],
    simp only [functor.comp_map, eq_to_hom_refl, category.comp_id, category.id_comp], },
  { rintros, dsimp only [of, lift],
    simp only [functor.comp_obj], },
end

lemma lift_unique (Œ¶ : (quotient S Sn) ‚•§ D) (hŒ¶ : (of S Sn) ‚ãô Œ¶ = œÜ) :
  Œ¶ = (lift S Sn œÜ hœÜ) :=
begin
  subst_vars,
  apply functor.ext,
  { rintros, dsimp [of, lift],
    apply quot.induction_on f,
    rintro f,
    simp only [quot.lift_on_mk, functor.comp_map, category.comp_id, category.id_comp], },
  { rintros, dsimp only [of, lift], refl, }
end

end ump

/-- The kernel of the morphism `of S Sn : C ‚•§ (quotient S Sn)` is exactly `S.disconnect`. -/
lemma ker_eq : ker (of S Sn) = S.disconnect :=
begin
  ext c d f,
  split,
  { rintro hf,
    rw mem_ker_iff at hf,
    obtain ‚ü®h,e‚ü© := hf,
    dsimp [of] at h e, subst h,
    have := @quotient.exact (c ‚ü∂ c) (cgr.setoid S ‚ü®Sn.wide‚ü©) _ _ e,
    rcases cgr.symm S this with ‚ü®Œ≥,hŒ≥,Œ¥,hŒ¥,rfl‚ü©,
    exact ‚ü®rfl,S.mul hŒ≥ (S.mul (id_mem_of_tgt S hŒ¥) hŒ¥)‚ü©, },
  { rintro ‚ü®rfl,hf‚ü©,
    rw mem_ker_iff,
    refine ‚ü®rfl,_‚ü©,
    show quot.mk _ f = quot.mk _ (ùüô _),
    apply quot.sound,
    refine ‚ü®inv f, S.inv hf, ùüô _, id_mem_of_tgt S hf,_‚ü©,
    simp only [inv_eq_inv, category.comp_id, is_iso.inv_hom_id], }
end


end isotropy

end isotropy

namespace thin
/-!
Quotient of a groupoid by a wide, graph-like subgroupoid.
By graph-likeness, the quotient can be represented by the full subgroupoid induced by taking any
set of representatives of the vertices.
-/

variables (Sw : S.is_wide)  (Sg : S.is_thin)

/-- Two vertices of `C` are related iff there exists an arrow of `S` joining them. -/
abbreviation r := Œª c d, nonempty (S.arrows c d)

lemma r.refl (c : C) : r S c c := ‚ü®‚ü®ùüô c, Sw.wide c‚ü©‚ü©

lemma r.symm {c d : C} : r S c d ‚Üí r S d c := Œª ‚ü®‚ü®f,fS‚ü©‚ü©, ‚ü®‚ü®inv f, S.inv fS‚ü©‚ü©

lemma r.tran {c d e : C} : r S c d ‚Üí r S d e ‚Üí r S c e := Œª ‚ü®‚ü®f,fS‚ü©‚ü© ‚ü®‚ü®g,gS‚ü©‚ü©, ‚ü®‚ü®f‚â´g, S.mul fS gS‚ü©‚ü©

/-- This relation makes `C` into a setoid. -/
def R : setoid C :=
{ r := r S ,  iseqv := ‚ü®Œª _, r.refl S Sw _, Œª _ _, r.symm S, Œª _ _ _, r.tran S‚ü© }

/-- A set of representatives for the relation `r` -/
def reps : set C := set.range (@quotient.out C (R S Sw))

/-- Mapping any `c : C` to its representative -/
noncomputable def to_reps : C ‚Üí reps S Sw :=
Œª c,
‚ü® @_root_.quotient.out C (R S Sw) (@_root_.quotient.mk C (R S Sw) c),
  ‚ü® @_root_.quotient.mk C (R S Sw) c, rfl ‚ü© ‚ü©

/-- Mapping a representative to its underlying element of `C` -/
def of_reps : reps S Sw ‚Üí C := Œª c, c.val

lemma of_to_reps_congr (c : C) : (R S Sw).r (of_reps S Sw (to_reps S Sw c)) c :=
begin
  letI := R S Sw,
  change (of_reps S Sw (to_reps S Sw c)) ‚âà c,
  apply quotient.exact,
  dsimp [of_reps, to_reps],
  rw quotient.out_eq,
end

/--
Given `c : C`, there is an arrow of `S` mapping (the underlying element of) the representative
of `c` to `c`.
-/
noncomputable def to_reps_arrow (c : C) : of_reps S Sw (to_reps S Sw c)  ‚ü∂ c :=
(of_to_reps_congr S Sw c).some.val

lemma to_reps_arrow_mem (c : C) :
  (to_reps_arrow S Sw c) ‚àà (S.arrows (of_reps S Sw (to_reps S Sw c)) c) :=
(of_to_reps_congr S Sw c).some.prop

include Sg Sw
lemma to_reps_arrow_unique {c : C}
  {Œ≥ : of_reps S Sw (to_reps S Sw c) ‚ü∂ c}
  (hŒ≥ : Œ≥ ‚àà S.arrows (of_reps S Sw (to_reps S Sw c)) c) :
  Œ≥ = to_reps_arrow S Sw c :=
begin
  rw [subgroupoid.is_thin_iff, (is_wide_iff_objs_eq_univ S).mp Sw] at Sg,
  simp only [set.top_eq_univ, set.mem_univ, set.subsingleton_coe, set_coe.forall,
             forall_true_left] at Sg,
  exact Sg _ _ hŒ≥ (to_reps_arrow_mem S Sw c),
end
omit Sg Sw

lemma to_of_reps_eq (cc : reps S Sw) : (to_reps S Sw $ of_reps S Sw cc) = cc :=
begin
  dsimp [of_reps, to_reps],
  rcases cc with ‚ü®_,‚ü®x,rfl‚ü©‚ü©,
  simp only [subtype.coe_mk, quotient.out_eq, subtype.mk_eq_mk],
end

/-- The vertex set of the quotient of `C` by `S` -/
def quotient := (subgroupoid.full $ reps S Sw).objs

instance : groupoid (quotient S Sw) := (subgroupoid.full $ reps S Sw).coe

/-- The morphism from `C` to its -/
noncomputable def of : C ‚•§ quotient S Sw :=
{ obj := Œª c,
  ‚ü® to_reps S Sw c,
    by { dsimp [quotient], rw full_objs, simp only [subtype.coe_prop], } ‚ü©,
  map := Œª c d f,
    let
      Œ≥ := (to_reps_arrow S Sw c),
      Œ¥ := inv (to_reps_arrow S Sw d)
    in
      ‚ü®Œ≥ ‚â´ f ‚â´ Œ¥, by { constructor; simp only [subtype.coe_prop], } ‚ü©,
  map_id' := Œª _, by
    { simp only [subtype.val_eq_coe, inv_eq_inv, category.id_comp, is_iso.hom_inv_id],
      refl, },
  map_comp' := Œª x y z f g, by
    { ext, simp only [inv_eq_inv, category.assoc, subtype.coe_mk,
                      coe_to_category_comp_coe, is_iso.inv_hom_id_assoc], } }

/-- Since the quotient is defined as a full groupoid, it embeds in `C`. -/
def fo : (quotient S Sw) ‚•§ C := subgroupoid.hom _

lemma of_fo_obj (c: quotient S Sw) : (of S Sw).obj ((fo S Sw).obj c) = c :=
begin
  dsimp [quotient] at c,
  rcases c with ‚ü®c',h‚ü©,
  rw mem_full_objs_iff at h,
  rcases h with ‚ü®_,rfl‚ü©,
  dsimp [of, fo, subgroupoid.hom, subgroupoid.full, to_reps],
  simp only [quotient.out_eq, subtype.mk_eq_mk],
end

/-- This definition is needed to state the lemma below without lean complaining. -/
private def arrow_val {c d : quotient S Sw} (h : c ‚ü∂ d) : c.val ‚ü∂ d.val := h.val

private lemma eq_to_hom_val_mem_S {c d : quotient S Sw} (h : c = d) :
  arrow_val S Sw (eq_to_hom h) ‚àà S.arrows c.val d.val := by
{ cases h, apply Sw.wide, }

include Sg
lemma of_fo_map {c d : quotient S Sw} (f : c ‚ü∂ d) :
  (of S Sw).map ((fo S Sw).map f)
= (eq_to_hom $ of_fo_obj S Sw c) ‚â´ f ‚â´ (eq_to_hom $ (of_fo_obj S Sw d).symm) :=
begin
  letI := R S Sw,
  dsimp only [quotient] at c d,
  rcases c with ‚ü®c',hc‚ü©,
  rcases d with ‚ü®d',hd‚ü©,
  rw mem_full_objs_iff at hc hd,
  rcases hc with ‚ü®c',rfl‚ü©,
  rcases hd with ‚ü®d',rfl‚ü©,
  dsimp only [of, fo, hom],
  let ec := of_fo_obj S Sw ‚ü®c'.out,hc‚ü©,
  let ed := of_fo_obj S Sw ‚ü®d'.out,hd‚ü©,
  congr,
  { --change to_reps_arrow S Sw c'.out = arrow_val S Sw (eq_to_hom ec),
    exact (to_reps_arrow_unique S Sw Sg (eq_to_hom_val_mem_S S Sw ec)).symm, },
  { change groupoid.inv (to_reps_arrow S Sw d'.out) = arrow_val S Sw (eq_to_hom ed.symm),
    suffices : to_reps_arrow S Sw d'.out = inv (arrow_val S Sw (eq_to_hom ed.symm)),
    { simp only [this, inv_eq_inv, is_iso.inv_inv], },
    symmetry,
    refine to_reps_arrow_unique S Sw Sg (S.inv (eq_to_hom_val_mem_S S Sw _)), }
end

lemma of_fo_eq_id : (fo S Sw) ‚ãô (of S Sw) = functor.id _ :=
begin
  apply functor.ext,
  { rintro, simp only [functor.comp_map, functor.id_map], rw of_fo_map, exact Sg, },
end

lemma ker_eq : ker (of S Sw) = S :=
begin
  apply le_antisymm,
  { rw le_iff,
    rintro c d f hf,
    rw mem_ker_iff at hf,
    dsimp [of] at hf,
    obtain ‚ü®h,e‚ü© := hf,
    rw subtype.ext_iff at e,
    simp only [inv_eq_inv, subtype.coe_mk] at e,
    suffices :
      to_reps_arrow S Sw c ‚â´ f ‚â´ category_theory.inv (to_reps_arrow S Sw d) ‚àà
      S.arrows (of_reps S Sw (to_reps S Sw c)) (of_reps S Sw (to_reps S Sw d)),
    { rw [subgroupoid.mul_mem_cancel_left S (to_reps_arrow_mem S Sw c),
          ‚Üêinv_eq_inv,
          subgroupoid.mul_mem_cancel_right S (S.inv (to_reps_arrow_mem S Sw d))] at this,
      exact this, },
    rw e, apply eq_to_hom_val_mem_S, },
  { rw le_iff,
    rintro c d f fS, rw mem_ker_iff,
    dsimp [of, to_reps],
    simp only [inv_eq_inv, subtype.mk_eq_mk, quotient.out_inj, quotient.eq],
    letI := R S Sw,
    have : c ‚âà d := ‚ü®‚ü®f,fS‚ü©‚ü©,
    use ‚ü®‚ü®f,fS‚ü©‚ü©,
    simp only [subtype.ext_iff, subtype.coe_mk],
    simp only [subgroupoid.is_thin_iff, (subgroupoid.is_wide_iff_objs_eq_univ S).mp Sw,
               set.top_eq_univ, set.mem_univ, set.subsingleton_coe, set_coe.forall,
               forall_true_left] at Sg,
    let lhsS := S.mul (to_reps_arrow_mem S Sw c) (S.mul fS $ S.inv $ to_reps_arrow_mem S Sw d),
    let rhsS := eq_to_hom_val_mem_S S Sw
                  (((subtype.mk_eq_mk.trans quotient.out_inj).trans quotient.eq).mpr this),
    let ss := Sg (of_reps S Sw (to_reps S Sw c)) (of_reps S Sw (to_reps S Sw d)),
    simpa only [inv_eq_inv] using ss lhsS rhsS, },
end
omit Sg

section ump

variables {D : Type*} [groupoid D] (œÜ : C ‚•§ D) (hœÜ : S ‚â§ ker œÜ)

/-- Any functor `C ‚•§ D` containing `S` in its kernel lifts to `quotient S Sw ‚•§ D`. -/
def lift : quotient S Sw ‚•§ D := (fo S Sw) ‚ãô œÜ

include hœÜ
lemma lift_spec : (of S Sw) ‚ãô (lift S Sw œÜ) = œÜ :=
begin
  dsimp only [lift, of, fo, subgroupoid.full, subgroupoid.hom],
  rw le_iff at hœÜ,
  fapply functor.ext,
  { rintro c,
    obtain ‚ü®Œ≥,Œ≥S‚ü© := (of_to_reps_congr S Sw c).some,
    let := hœÜ Œ≥S, rw mem_ker_iff at this,
    exact this.some, },
  { rintro c d f,
    simp only [subtype.val_eq_coe, inv_eq_inv, functor.comp_map,
               functor.map_comp, functor.map_inv],
    let hŒ≥' := hœÜ (to_reps_arrow_mem S Sw c),
    let hŒ¥' := hœÜ (to_reps_arrow_mem S Sw d),
    rw mem_ker_iff at hŒ≥' hŒ¥',
    obtain ‚ü®eŒ≥,hŒ≥''‚ü© := hŒ≥',
    obtain ‚ü®eŒ¥,hŒ¥''‚ü© := hŒ¥',
    simp only [subtype.coe_mk, hŒ¥'', hŒ≥'',inv_eq_to_hom], refl,  },
end

include Sg
lemma lift_unique (Œ¶ : quotient S Sw ‚•§ D) (hŒ¶ : (of S Sw) ‚ãô Œ¶ = œÜ) :
  Œ¶ = (lift S Sw œÜ) :=
begin
  letI := R S Sw,
  subst_vars,
  dsimp [lift],
  rw le_iff at hœÜ,
  fapply functor.ext,
  { simp only [functor.comp_obj, set_coe.forall],
    rintro x h,
    rw of_fo_obj, },
  { simp only [set_coe.forall, functor.comp_map],
    rintro x h y k f,
    rw of_fo_map,
    simp only [eq_to_hom_map, functor.map_comp, category.assoc, eq_to_hom_trans,
               eq_to_hom_refl, category.comp_id, eq_to_hom_trans_assoc, category.id_comp],
    exact Sg, },
end
omit Sg

end ump

end thin

section quotient
/-!
The _actual_ quotient of `C` by the normal subgroupoid `S`, obtained
by first taking the `isotropy.quotient`, and then the `thin.quotient` on the image of `S`
under this quotient.
-/

variable (Sn : S.is_normal)

/-- The vertex set of the quotient -/
def _root_.category_theory.quotient_groupoid :=
  thin.quotient
    (map (isotropy.of S Sn) (isotropy.of_inj_on_objects S Sn) S)
    (subgroupoid.is_normal_map
      _ /-S-/
      _ /-(isotropy.of S Sn)-/
      (isotropy.of_inj_on_objects S Sn)
      (isotropy.of_onto S Sn)
      Sn).to_is_wide

instance : groupoid (quotient_groupoid S Sn) :=
  thin.quotient.category_theory.groupoid
    (map /-(isotropy.of S Sn)-/ _ (isotropy.of_inj_on_objects S Sn) S)
    (is_normal_map
      /-S-/ _
      /-(isotropy.of S Sn)-/ _
      (isotropy.of_inj_on_objects S Sn)
      (isotropy.of_onto S Sn)
      Sn).to_is_wide

/-- The morphism to the quotient -/
noncomputable def of : C ‚•§ quotient_groupoid S Sn := (isotropy.of _ _) ‚ãô (thin.of _ _)

section ump

variables {D : Type*} [groupoid D] (œÜ : C ‚•§ D) (hœÜ : S ‚â§ ker œÜ)

include hœÜ
/-- Any functor containing `S` in its kernel lifts to a functor from the quotient. -/
def lift : quotient_groupoid S Sn ‚•§ D :=
begin
  apply thin.lift,
  fapply isotropy.lift,
  exact œÜ,
  exact (disconnect_le S).trans hœÜ,
end

lemma lift_spec : (of S Sn) ‚ãô (lift S Sn œÜ hœÜ) = œÜ :=
begin
  change isotropy.of S Sn ‚ãô (thin.of (map (isotropy.of S Sn) _ S) _) ‚ãô
    thin.lift (map (isotropy.of S Sn) _ S) _ (isotropy.lift S Sn œÜ _) = œÜ,
  rw thin.lift_spec,
  apply isotropy.lift_spec,
  { rw le_iff at hœÜ ‚ä¢,
    rintros a b f ‚ü®g,gS‚ü©,
    exact hœÜ gS, },
end

lemma lift_unique (Œ¶ : quotient_groupoid S Sn ‚•§ D) (hŒ¶ : (of S Sn) ‚ãô Œ¶ = œÜ) :
  Œ¶ = (lift S Sn œÜ hœÜ) :=
begin
  apply thin.lift_unique,
  apply isotropy.map_is_thin,
  { rw le_iff at hœÜ ‚ä¢,
    rintros a b f ‚ü®g,gS‚ü©,
    exact hœÜ gS, },
  apply isotropy.lift_unique,
  exact hŒ¶,
end

end ump

lemma ker_eq : ker (of S Sn) = S :=
begin
  change ker (isotropy.of S Sn ‚ãô (thin.of (map (isotropy.of S Sn) _ S) _)) = S,
  rw [ker_comp, thin.ker_eq],
  apply le_antisymm,
  { rw le_iff, rintros c d f hf,
    dsimp [comap] at hf, rw mem_map_iff at hf,
    obtain ‚ü®c',d',g,cc',dd',gS,e‚ü© := hf,
    cases isotropy.of_inj_on_objects S Sn cc',
    cases isotropy.of_inj_on_objects S Sn dd',
    simp only [eq_to_hom_refl, category.comp_id, category.id_comp] at e,
    letI := @cgr.setoid _ _ S Sn.to_is_wide c d,
    obtain ‚ü®Œ≥,Œ≥S,Œ¥,Œ¥S,e‚ü© := quotient.exact e,
    have : f = inv Œ≥ ‚â´ g ‚â´ inv Œ¥, by { rw e, simp, },
    rw this,
    apply S.mul (S.inv Œ≥S) (S.mul gS $ S.inv Œ¥S), },
  { apply subgroupoid.le_comap_map, },
  apply isotropy.map_is_thin,
end

end quotient

end quotient_groupoid

end category_theory
