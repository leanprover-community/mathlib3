/-
Copyright (c) 2022 RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli, Junyan Xu
-/
import category_theory.groupoid.vertex_group
import category_theory.groupoid.basic
import category_theory.groupoid
import algebra.group.defs
import data.set.lattice
import group_theory.subgroup.basic
import order.galois_connection
/-!
# Subgroupoid

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines subgroupoids as `structure`s containing the subsets of arrows and their
stability under composition and inversion.
Also defined are:

* containment of subgroupoids is a complete lattice;
* images and preimages of subgroupoids under a functor;
* the notion of normality of subgroupoids and its stability under intersection and preimage;
* compatibility of the above with `groupoid.vertex_group`.


## Main definitions

Given a type `C` with associated `groupoid C` instance.

* `subgroupoid C` is the type of subgroupoids of `C`
* `subgroupoid.is_normal` is the property that the subgroupoid is stable under conjugation
  by arbitrary arrows, _and_ that all identity arrows are contained in the subgroupoid.
* `subgroupoid.comap` is the "preimage" map of subgroupoids along a functor.
* `subgroupoid.map` is the "image" map of subgroupoids along a functor _injective on objects_.
* `subgroupoid.vertex_subgroup` is the subgroup of the `vertex group` at a given vertex `v`,
  assuming `v` is contained in the `subgroupoid` (meaning, by definition, that the arrow `ğŸ™ v`
  is contained in the subgroupoid).

## Implementation details

The structure of this file is copied from/inspired by `group_theory.subgroup.basic`
and `combinatorics.simple_graph.subgraph`.

## TODO

* Equivalent inductive characterization of generated (normal) subgroupoids.
* Characterization of normal subgroupoids as kernels.
* Prove that `full` and `disconnect` preserve intersections (and `disconnect` also unions)

## Tags

subgroupoid

-/

namespace category_theory

open set groupoid

local attribute [protected] category_theory.inv

universes u v

variables {C : Type u} [groupoid C]

/--
A sugroupoid of `C` consists of a choice of arrows for each pair of vertices, closed
under composition and inverses.
-/
@[ext] structure subgroupoid (C : Type u) [groupoid C] :=
(arrows : âˆ€ (c d : C), set (c âŸ¶ d))
(inv : âˆ€ {c d} {p : c âŸ¶ d} (hp : p âˆˆ arrows c d),
          inv p âˆˆ arrows d c)
(mul : âˆ€ {c d e} {p} (hp : p âˆˆ arrows c d) {q} (hq : q âˆˆ arrows d e),
          p â‰« q âˆˆ arrows c e)

attribute [protected] subgroupoid.inv subgroupoid.mul

namespace subgroupoid

variable (S : subgroupoid C)

lemma inv_mem_iff {c d : C} (f : c âŸ¶ d) : inv f âˆˆ S.arrows d c â†” f âˆˆ S.arrows c d :=
begin
  split,
  { rintro h,
    suffices : inv (inv f) âˆˆ S.arrows c d,
    { simpa only [inv_eq_inv, is_iso.inv_inv] using this, },
    { apply S.inv h, }, },
  { apply S.inv, },
end

lemma mul_mem_cancel_left {c d e : C} {f : c âŸ¶ d} {g : d âŸ¶ e} (hf : f âˆˆ S.arrows c d) :
  f â‰« g âˆˆ S.arrows c e â†” g âˆˆ S.arrows d e :=
begin
  split,
  { rintro h,
    suffices : (inv f) â‰« f â‰« g âˆˆ S.arrows d e,
    { simpa only [inv_eq_inv, is_iso.inv_hom_id_assoc] using this, },
    { apply S.mul (S.inv hf) h, }, },
  { apply S.mul hf, },
end

lemma mul_mem_cancel_right {c d e : C} {f : c âŸ¶ d} {g : d âŸ¶ e} (hg : g âˆˆ S.arrows d e) :
  f â‰« g âˆˆ S.arrows c e â†” f âˆˆ S.arrows c d :=
begin
  split,
  { rintro h,
    suffices : (f â‰« g) â‰« (inv g) âˆˆ S.arrows c d,
    { simpa only [inv_eq_inv, is_iso.hom_inv_id, category.comp_id, category.assoc] using this, },
    { apply S.mul h (S.inv hg), }, },
  { exact Î» hf, S.mul hf hg, },
end

/-- The vertices of `C` on which `S` has non-trivial isotropy -/
def objs : set C := {c : C | (S.arrows c c).nonempty}

lemma mem_objs_of_src {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : c âˆˆ S.objs :=
âŸ¨f â‰« inv f, S.mul h (S.inv h)âŸ©

lemma mem_objs_of_tgt {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : d âˆˆ S.objs :=
âŸ¨(inv f) â‰« f, S.mul (S.inv h) hâŸ©

lemma id_mem_of_nonempty_isotropy (c : C) :
  c âˆˆ objs S â†’ ğŸ™ c âˆˆ S.arrows c c :=
begin
  rintro âŸ¨Î³,hÎ³âŸ©,
  convert S.mul hÎ³ (S.inv hÎ³),
  simp only [inv_eq_inv, is_iso.hom_inv_id],
end

lemma id_mem_of_src {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : (ğŸ™ c) âˆˆ S.arrows c c :=
id_mem_of_nonempty_isotropy S c (mem_objs_of_src S h)

lemma id_mem_of_tgt {c d : C} {f : c âŸ¶ d} (h : f âˆˆ S.arrows c d) : (ğŸ™ d) âˆˆ S.arrows d d :=
id_mem_of_nonempty_isotropy S d (mem_objs_of_tgt S h)

/-- A subgroupoid seen as a quiver on vertex set `C` -/
def as_wide_quiver : quiver C := âŸ¨Î» c d, subtype $ S.arrows c dâŸ©

/-- The coercion of a subgroupoid as a groupoid -/
@[simps to_category_comp_coe, simps inv_coe (lemmas_only)]
instance coe : groupoid S.objs :=
{ hom := Î» a b, S.arrows a.val b.val,
  id := Î» a, âŸ¨ğŸ™ a.val, id_mem_of_nonempty_isotropy S a.val a.propâŸ©,
  comp := Î» a b c p q, âŸ¨p.val â‰« q.val, S.mul p.prop q.propâŸ©,
  id_comp' := Î» a b âŸ¨p,hpâŸ©, by simp only [category.id_comp],
  comp_id' := Î» a b âŸ¨p,hpâŸ©, by simp only [category.comp_id],
  assoc' := Î» a b c d âŸ¨p,hpâŸ© âŸ¨q,hqâŸ© âŸ¨r,hrâŸ©, by simp only [category.assoc],
  inv := Î» a b p, âŸ¨inv p.val, S.inv p.propâŸ©,
  inv_comp' := Î» a b âŸ¨p,hpâŸ©, by simp only [inv_comp],
  comp_inv' := Î» a b âŸ¨p,hpâŸ©, by simp only [comp_inv] }

@[simp] lemma coe_inv_coe' {c d : S.objs} (p : c âŸ¶ d) :
  (category_theory.inv p).val = category_theory.inv p.val :=
by { simp only [subtype.val_eq_coe, â†inv_eq_inv, coe_inv_coe], }

/-- The embedding of the coerced subgroupoid to its parent-/
def hom : S.objs â¥¤ C :=
{ obj := Î» c, c.val,
  map := Î» c d f, f.val,
  map_id' := Î» c, rfl,
  map_comp' := Î» c d e f g, rfl }

lemma hom.inj_on_objects : function.injective (hom S).obj :=
by { rintros âŸ¨c,hcâŸ© âŸ¨d,hdâŸ© hcd, simp only [subtype.mk_eq_mk], exact hcd }

lemma hom.faithful :
  âˆ€ c d, function.injective (Î» (f : c âŸ¶ d), (hom S).map f) :=
by { rintros âŸ¨c,hcâŸ© âŸ¨d,hdâŸ© âŸ¨f,hfâŸ© âŸ¨g,hgâŸ© hfg, simp only [subtype.mk_eq_mk], exact hfg, }

/-- The subgroup of the vertex group at `c` given by the subgroupoid -/
def vertex_subgroup {c : C} (hc : c âˆˆ S.objs) : subgroup (c âŸ¶ c) :=
{ carrier  := S.arrows c c,
  mul_mem' := Î» f g hf hg, S.mul hf hg,
  one_mem' := id_mem_of_nonempty_isotropy _ _ hc,
  inv_mem' := Î» f hf, S.inv hf }

instance : set_like (subgroupoid C) (Î£ (c d : C), c âŸ¶ d) :=
{ coe := Î» S, {F | F.2.2 âˆˆ S.arrows F.1 F.2.1},
  coe_injective' := Î» âŸ¨S, _, _âŸ© âŸ¨T, _, _âŸ© h, by { ext c d f, apply set.ext_iff.1 h âŸ¨c, d, fâŸ© } }

lemma mem_iff (S : subgroupoid C) (F : Î£ c d, c âŸ¶ d) :
  F âˆˆ S â†” F.2.2 âˆˆ S.arrows F.1 F.2.1 := iff.rfl

lemma le_iff (S T : subgroupoid C) : (S â‰¤ T) â†” (âˆ€ {c d}, (S.arrows c d) âŠ† (T.arrows c d)) :=
by { rw [set_like.le_def, sigma.forall], exact forall_congr (Î» c, sigma.forall) }

instance : has_top (subgroupoid C) :=
âŸ¨ { arrows := (Î» _ _, set.univ),
    mul    := by { rintros, trivial, },
    inv    := by { rintros, trivial, } } âŸ©

lemma mem_top {c d : C} (f : c âŸ¶ d) : f âˆˆ (âŠ¤ : subgroupoid C).arrows c d := trivial

lemma mem_top_objs (c : C) : c âˆˆ (âŠ¤ : subgroupoid C).objs :=
by { dsimp [has_top.top,objs], simp only [univ_nonempty], }

instance : has_bot (subgroupoid C) :=
âŸ¨ { arrows := (Î» _ _, âˆ…),
    mul    := Î» _ _ _ _, false.elim,
    inv    := Î» _ _ _, false.elim } âŸ©

instance : inhabited (subgroupoid C) := âŸ¨âŠ¤âŸ©

instance : has_inf (subgroupoid C) :=
âŸ¨ Î» S T,
  { arrows := (Î» c d, (S.arrows c d) âˆ© (T.arrows c d)),
    inv    := by { rintros, exact âŸ¨S.inv hp.1, T.inv hp.2âŸ©, },
    mul    := by { rintros, exact âŸ¨S.mul hp.1 hq.1, T.mul hp.2 hq.2âŸ©, } } âŸ©

instance : has_Inf (subgroupoid C) :=
âŸ¨ Î» s,
  { arrows := Î» c d, â‹‚ S âˆˆ s, (subgroupoid.arrows S c d),
    inv := by { intros, rw mem_Interâ‚‚ at hp âŠ¢, exact Î» S hS, S.inv (hp S hS) },
    mul := by { intros, rw mem_Interâ‚‚ at hp hq âŠ¢,exact Î» S hS, S.mul (hp S hS) (hq S hS) } } âŸ©

instance : complete_lattice (subgroupoid C) :=
{ bot          := (âŠ¥),
  bot_le       := Î» S, empty_subset _,
  top          := (âŠ¤),
  le_top       := Î» S, subset_univ _,
  inf          := (âŠ“),
  le_inf       := Î» R S T RS RT _ pR, âŸ¨RS pR, RT pRâŸ©,
  inf_le_left  := Î» R S _, and.left,
  inf_le_right := Î» R S _, and.right,
  .. complete_lattice_of_Inf (subgroupoid C)
  begin
    refine (Î» s, âŸ¨Î» S Ss F, _, Î» T Tl F fT, _âŸ©);
      simp only [Inf, mem_iff, mem_Inter],
    exacts [Î» hp, hp S Ss, Î» S Ss, Tl Ss fT],
  end }

lemma le_objs {S T : subgroupoid C} (h : S â‰¤ T) : S.objs âŠ† T.objs :=
Î» s âŸ¨Î³, hÎ³âŸ©, âŸ¨Î³, @h âŸ¨s, s, Î³âŸ© hÎ³âŸ©

/-- The functor associated to the embedding of subgroupoids -/
def inclusion {S T : subgroupoid C} (h : S â‰¤ T) : S.objs â¥¤ T.objs :=
{ obj := Î» s, âŸ¨s.val, le_objs h s.propâŸ©,
  map := Î» s t f, âŸ¨f.val, @h âŸ¨s, t, f.valâŸ© f.propâŸ©,
  map_id' := Î» _, rfl,
  map_comp' := Î» _ _ _ _ _, rfl }

lemma inclusion_inj_on_objects {S T : subgroupoid C} (h : S â‰¤ T) :
  function.injective (inclusion h).obj :=
Î» âŸ¨s,hsâŸ© âŸ¨t,htâŸ©, by simpa only [inclusion, subtype.mk_eq_mk] using id

lemma inclusion_faithful {S T : subgroupoid C} (h : S â‰¤ T) (s t : S.objs) :
  function.injective (Î» (f : s âŸ¶ t), (inclusion h).map f) :=
Î» âŸ¨f,hfâŸ© âŸ¨g,hgâŸ©, by { dsimp only [inclusion], simpa only [subtype.mk_eq_mk] using id }

lemma inclusion_refl {S : subgroupoid C} : inclusion (le_refl S) = ğŸ­ S.objs :=
functor.hext (Î» âŸ¨s,hsâŸ©, rfl) (Î» âŸ¨s,hsâŸ© âŸ¨t,htâŸ© âŸ¨f,hfâŸ©, heq_of_eq rfl)

lemma inclusion_trans {R S T : subgroupoid C} (k : R â‰¤ S) (h : S â‰¤ T) :
  inclusion (k.trans h) = (inclusion k) â‹™ (inclusion h) := rfl

lemma inclusion_comp_embedding {S T : subgroupoid C} (h : S â‰¤ T) :
  (inclusion h) â‹™ T.hom = S.hom := rfl

/-- The family of arrows of the discrete groupoid -/
inductive discrete.arrows : Î  (c d : C), (c âŸ¶ d) â†’ Prop
| id (c : C) : discrete.arrows c c (ğŸ™ c)

/-- The only arrows of the discrete groupoid are the identity arrows. -/
def discrete : subgroupoid C :=
{ arrows := discrete.arrows,
  inv := by { rintros _ _ _ âŸ¨âŸ©, simp only [inv_eq_inv, is_iso.inv_id], split, },
  mul := by { rintros _ _ _ _ âŸ¨âŸ© _ âŸ¨âŸ©, rw category.comp_id, split, } }

lemma mem_discrete_iff {c d : C} (f : c âŸ¶ d) :
  (f âˆˆ (discrete).arrows c d) â†” (âˆƒ (h : c = d), f = eq_to_hom h) :=
âŸ¨by { rintro âŸ¨âŸ©, exact âŸ¨rfl, rflâŸ© }, by { rintro âŸ¨rfl, rflâŸ©, split }âŸ©

/-- A subgroupoid is wide if its carrier set is all of `C`-/
structure is_wide : Prop :=
(wide : âˆ€ c, (ğŸ™ c) âˆˆ (S.arrows c c))

lemma is_wide_iff_objs_eq_univ : S.is_wide â†” S.objs = set.univ :=
begin
  split,
  { rintro h,
    ext, split; simp only [top_eq_univ, mem_univ, implies_true_iff, forall_true_left],
    apply mem_objs_of_src S (h.wide x), },
  { rintro h,
    refine âŸ¨Î» c, _âŸ©,
    obtain âŸ¨Î³,Î³SâŸ© := (le_of_eq h.symm : âŠ¤ âŠ† S.objs) (set.mem_univ c),
    exact id_mem_of_src S Î³S, },
end

lemma is_wide.id_mem {S : subgroupoid C} (Sw : S.is_wide) (c : C) :
  (ğŸ™ c) âˆˆ S.arrows c c := Sw.wide c

lemma is_wide.eq_to_hom_mem {S : subgroupoid C} (Sw : S.is_wide) {c d : C} (h : c = d) :
  (eq_to_hom h) âˆˆ S.arrows c d := by
{ cases h, simp only [eq_to_hom_refl], apply Sw.id_mem c, }

/-- AÂ subgroupoid is normal if it is wide and satisfies the expected stability under conjugacy. -/
structure is_normal extends (is_wide S) : Prop :=
(conj : âˆ€ {c d} (p : c âŸ¶ d) {Î³ : c âŸ¶ c} (hs : Î³ âˆˆ S.arrows c c),
              ((inv p) â‰« Î³ â‰« p) âˆˆ (S.arrows d d))

lemma is_normal.conj' {S : subgroupoid C} (Sn : is_normal S) :
  âˆ€ {c d} (p : d âŸ¶ c) {Î³ : c âŸ¶ c} (hs : Î³ âˆˆ S.arrows c c), (p â‰« Î³ â‰« (inv p)) âˆˆ (S.arrows d d) :=
Î» c d p Î³ hs, by { convert Sn.conj (inv p) hs, simp, }

lemma is_normal.conjugation_bij (Sn : is_normal S) {c d} (p : c âŸ¶ d) :
  set.bij_on (Î» Î³ : c âŸ¶ c, (inv p) â‰« Î³ â‰« p) (S.arrows c c) (S.arrows d d) :=
begin
  refine âŸ¨Î» Î³ Î³S, Sn.conj p Î³S, Î» Î³â‚ Î³â‚S Î³â‚‚ Î³â‚‚S h, _, Î» Î´ Î´S, âŸ¨p â‰« Î´ â‰« (inv p), Sn.conj' p Î´S, _âŸ©âŸ©,
  { simpa only [inv_eq_inv, category.assoc, is_iso.hom_inv_id,
                category.comp_id, is_iso.hom_inv_id_assoc] using p â‰«= h =â‰« inv p },
  { simp only [inv_eq_inv, category.assoc, is_iso.inv_hom_id,
               category.comp_id, is_iso.inv_hom_id_assoc] },
end

lemma top_is_normal : is_normal (âŠ¤ : subgroupoid C) :=
{ wide := (Î» c, trivial),
  conj := (Î» a b c d e, trivial) }

lemma Inf_is_normal (s : set $ subgroupoid C) (sn : âˆ€ S âˆˆ s, is_normal S) : is_normal (Inf s) :=
{ wide := by { simp_rw [Inf, mem_Interâ‚‚], exact Î» c S Ss, (sn S Ss).wide c },
  conj := by { simp_rw [Inf, mem_Interâ‚‚], exact Î» c d p Î³ hÎ³ S Ss, (sn S Ss).conj p (hÎ³ S Ss) } }

lemma discrete_is_normal : (@discrete C _).is_normal :=
{ wide := Î» c, by { constructor, },
  conj := Î» c d f Î³ hÎ³, by
  { cases hÎ³, simp only [inv_eq_inv, category.id_comp, is_iso.inv_hom_id], constructor, } }

lemma is_normal.vertex_subgroup (Sn : is_normal S) (c : C) (cS : c âˆˆ S.objs) :
  (S.vertex_subgroup cS).normal :=
{ conj_mem := Î» x hx y, by { rw mul_assoc, exact Sn.conj' y hx } }

section generated_subgroupoid

-- TODO:Â proof that generated is just "words in X" and generated_normal is similarly
variable (X : âˆ€ c d : C, set (c âŸ¶ d))

/-- The subgropoid generated by the set of arrows `X` -/
def generated : subgroupoid C :=
Inf {S : subgroupoid C | âˆ€ c d, X c d âŠ† S.arrows c d}

lemma subset_generated (c d : C) : X c d âŠ† (generated X).arrows c d :=
begin
  dsimp only [generated, Inf],
  simp only [subset_Interâ‚‚_iff],
  exact Î» S hS f fS, hS _ _ fS,
end

/-- The normal sugroupoid generated by the set of arrows `X` -/
def generated_normal : subgroupoid C :=
Inf {S : subgroupoid C | (âˆ€ c d, X c d âŠ† S.arrows c d) âˆ§ S.is_normal}

lemma generated_le_generated_normal : generated X â‰¤ generated_normal X :=
begin
  apply @Inf_le_Inf (subgroupoid C) _,
  exact Î» S âŸ¨h,_âŸ©, h,
end

lemma generated_normal_is_normal : (generated_normal X).is_normal :=
Inf_is_normal _ (Î» S h, h.right)

lemma is_normal.generated_normal_le {S : subgroupoid C} (Sn : S.is_normal) :
  generated_normal X â‰¤ S â†” âˆ€ c d, X c d âŠ† S.arrows c d :=
begin
  split,
  { rintro h c d,
    let h' := generated_le_generated_normal X,
    rw le_iff at h h',
    exact ((subset_generated X c d).trans (@h' c d)).trans (@h c d), },
  { rintro h,
    apply @Inf_le (subgroupoid C) _,
    exact âŸ¨h,SnâŸ©, },
end

end generated_subgroupoid

section hom

variables {D : Type*} [groupoid D] (Ï† : C â¥¤ D)

/--
A functor between groupoid defines a map of subgroupoids in the reverse direction
by taking preimages.
 -/
def comap (S : subgroupoid D) : subgroupoid C :=
{ arrows := Î» c d, {f : c âŸ¶ d | Ï†.map f âˆˆ S.arrows (Ï†.obj c) (Ï†.obj d)},
  inv := Î» c d p hp, by { rw [mem_set_of, inv_eq_inv, Ï†.map_inv p, â† inv_eq_inv], exact S.inv hp },
  mul := begin
    rintros,
    simp only [mem_set_of, functor.map_comp],
    apply S.mul; assumption,
  end }

lemma comap_mono (S T : subgroupoid D) :
  S â‰¤ T â†’ comap Ï† S â‰¤ comap Ï† T := Î» ST âŸ¨c,d,pâŸ©, @ST âŸ¨_,_,_âŸ©

lemma is_normal_comap {S : subgroupoid D} (Sn : is_normal S) : is_normal (comap Ï† S) :=
{ wide := Î» c, by { rw [comap, mem_set_of, functor.map_id], apply Sn.wide, },
  conj := Î» c d f Î³ hÎ³, by
  { simp_rw [inv_eq_inv f, comap, mem_set_of, functor.map_comp, functor.map_inv, â†inv_eq_inv],
    exact Sn.conj _ hÎ³, } }

@[simp] lemma comap_comp {E : Type*} [groupoid E] (Ïˆ : D â¥¤ E) :
  comap (Ï† â‹™ Ïˆ) = (comap Ï†) âˆ˜ (comap Ïˆ) := rfl

/-- The kernel of a functor between subgroupoid is the preimage. -/
def ker : subgroupoid C := comap Ï† discrete

lemma mem_ker_iff {c d : C} (f : c âŸ¶ d) :
  f âˆˆ (ker Ï†).arrows c d â†” âˆƒ (h : Ï†.obj c = Ï†.obj d), Ï†.map f = eq_to_hom h :=
mem_discrete_iff (Ï†.map f)

lemma ker_is_normal : (ker Ï†).is_normal := is_normal_comap Ï† (discrete_is_normal)

@[simp]
lemma ker_comp {E : Type*} [groupoid E] (Ïˆ : D â¥¤ E) : ker (Ï† â‹™ Ïˆ) = comap Ï† (ker Ïˆ) := rfl

/-- The family of arrows of the image of a subgroupoid under a functor injective on objects -/
inductive map.arrows (hÏ† : function.injective Ï†.obj) (S : subgroupoid C) :
  Î  (c d : D), (c âŸ¶ d) â†’ Prop
| im {c d : C} (f : c âŸ¶ d) (hf : f âˆˆ S.arrows c d) : map.arrows (Ï†.obj c) (Ï†.obj d) (Ï†.map f)

lemma map.arrows_iff (hÏ† : function.injective Ï†.obj) (S : subgroupoid C) {c d : D} (f : c âŸ¶ d) :
  map.arrows Ï† hÏ† S c d f â†”
  âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d) (hg : g âˆˆ S.arrows a b),
    f = (eq_to_hom ha.symm) â‰« Ï†.map g â‰« (eq_to_hom hb) :=
begin
  split,
  { rintro âŸ¨g,hgâŸ©, exact âŸ¨_,_,g,rfl,rfl,hg, eq_conj_eq_to_hom _âŸ© },
  { rintro âŸ¨a,b,g,rfl,rfl,hg,rflâŸ©, rw â† eq_conj_eq_to_hom, split, exact hg },
end

/-- The "forward" image of a subgroupoid under a functor injective on objects -/
def map (hÏ† : function.injective Ï†.obj) (S : subgroupoid C) : subgroupoid D :=
{ arrows := map.arrows Ï† hÏ† S,
  inv := begin
    rintro _ _ _ âŸ¨âŸ©,
    rw [inv_eq_inv, â†functor.map_inv, â†inv_eq_inv],
    split, apply S.inv, assumption,
  end,
  mul := begin
    rintro _ _ _ _ âŸ¨f,hfâŸ© q hq,
    obtain âŸ¨câ‚ƒ,câ‚„,g,he,rfl,hg,gqâŸ© := (map.arrows_iff Ï† hÏ† S q).mp hq,
    cases hÏ† he, rw [gq, â† eq_conj_eq_to_hom, â† Ï†.map_comp],
    split, exact S.mul hf hg,
  end }

lemma mem_map_iff (hÏ† : function.injective Ï†.obj) (S : subgroupoid C) {c d : D} (f : c âŸ¶ d) :
  f âˆˆ (map Ï† hÏ† S).arrows c d â†”
  âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d) (hg : g âˆˆ S.arrows a b),
    f = (eq_to_hom ha.symm) â‰« Ï†.map g â‰« (eq_to_hom hb) := map.arrows_iff Ï† hÏ† S f

lemma galois_connection_map_comap (hÏ† : function.injective Ï†.obj) :
  galois_connection (map Ï† hÏ†) (comap Ï†) :=
begin
  rintro S T, simp_rw [le_iff], split,
  { exact Î» h c d f fS, h (map.arrows.im f fS), },
  { rintros h _ _ g âŸ¨a,gÏ†SâŸ©,
    exact h gÏ†S, },
end

lemma map_mono (hÏ† : function.injective Ï†.obj) (S T : subgroupoid C) :
  S â‰¤ T â†’ map Ï† hÏ† S â‰¤ map Ï† hÏ† T :=
Î» h, (galois_connection_map_comap Ï† hÏ†).monotone_l h

lemma le_comap_map (hÏ† : function.injective Ï†.obj) (S : subgroupoid C) :
  S â‰¤ comap Ï† (map Ï† hÏ† S) := (galois_connection_map_comap Ï† hÏ†).le_u_l S

lemma map_comap_le (hÏ† : function.injective Ï†.obj) (T : subgroupoid D) :
  map Ï† hÏ† (comap Ï† T) â‰¤ T := (galois_connection_map_comap Ï† hÏ†).l_u_le T

lemma map_le_iff_le_comap (hÏ† : function.injective Ï†.obj)
  (S : subgroupoid C) (T : subgroupoid D) :
  map Ï† hÏ† S â‰¤ T â†” S â‰¤ comap Ï† T := (galois_connection_map_comap Ï† hÏ†).le_iff_le

lemma mem_map_objs_iff (hÏ† : function.injective Ï†.obj) (d : D) :
  d âˆˆ (map Ï† hÏ† S).objs â†” âˆƒ c âˆˆ S.objs, Ï†.obj c = d :=
begin
  dsimp [objs, map],
  split,
  { rintro âŸ¨f,hfâŸ©,
    change map.arrows Ï† hÏ† S d d f at hf, rw map.arrows_iff at hf,
    obtain âŸ¨c,d,g,ec,ed,eg,gS,egâŸ© := hf,
    exact âŸ¨c, âŸ¨mem_objs_of_src S eg, ecâŸ©âŸ©, },
  { rintros âŸ¨c,âŸ¨Î³,Î³SâŸ©,rflâŸ©,
    exact âŸ¨Ï†.map Î³,âŸ¨Î³,Î³SâŸ©âŸ©, }
end

@[simp]
lemma map_objs_eq (hÏ† : function.injective Ï†.obj) : (map Ï† hÏ† S).objs = Ï†.obj '' S.objs :=
by { ext, convert mem_map_objs_iff S Ï† hÏ† x, simp only [mem_image, exists_prop], }

/-- The image of a functor injective on objects -/
def im (hÏ† : function.injective Ï†.obj) := map Ï† hÏ† (âŠ¤)

lemma mem_im_iff (hÏ† : function.injective Ï†.obj) {c d : D} (f : c âŸ¶ d) :
  f âˆˆ (im Ï† hÏ†).arrows c d â†”
  âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d),
    f = (eq_to_hom ha.symm) â‰« Ï†.map g â‰« (eq_to_hom hb) :=
by { convert map.arrows_iff Ï† hÏ† âŠ¤ f, simp only [has_top.top, mem_univ, exists_true_left] }

lemma mem_im_objs_iff (hÏ† : function.injective Ï†.obj) (d : D) :
  d âˆˆ (im Ï† hÏ†).objs â†” âˆƒ c : C, Ï†.obj c = d := by
{ simp only [im, mem_map_objs_iff, mem_top_objs, exists_true_left], }

lemma obj_surjective_of_im_eq_top (hÏ† : function.injective Ï†.obj) (hÏ†' : im Ï† hÏ† = âŠ¤) :
  function.surjective Ï†.obj :=
begin
  rintro d,
  rw [â†mem_im_objs_iff, hÏ†'],
  apply mem_top_objs,
end

lemma is_normal_map (hÏ† : function.injective Ï†.obj) (hÏ†' : im Ï† hÏ† = âŠ¤) (Sn : S.is_normal) :
  (map Ï† hÏ† S).is_normal :=
{ wide := Î» d, by
  { obtain âŸ¨c,rflâŸ© := obj_surjective_of_im_eq_top Ï† hÏ† hÏ†' d,
    change map.arrows Ï† hÏ† S _ _ (ğŸ™ _), rw â†functor.map_id,
    constructor, exact Sn.wide c, },
  conj := Î» d d' g Î´ hÎ´, by
  { rw mem_map_iff at hÎ´,
    obtain âŸ¨c,c',Î³,cd,cd',Î³S,hÎ³âŸ© := hÎ´, subst_vars, cases hÏ† cd',
    have : d' âˆˆ (im Ï† hÏ†).objs, by { rw hÏ†', apply mem_top_objs, },
    rw mem_im_objs_iff at this,
    obtain âŸ¨c',rflâŸ© := this,
    have : g âˆˆ (im Ï† hÏ†).arrows (Ï†.obj c) (Ï†.obj c'), by
    { rw hÏ†', trivial, },
    rw mem_im_iff at this,
    obtain âŸ¨b,b',f,hb,hb',_,hfâŸ© := this, subst_vars, cases hÏ† hb, cases hÏ† hb',
    change map.arrows Ï† hÏ† S (Ï†.obj c') (Ï†.obj c') _,
    simp only [eq_to_hom_refl, category.comp_id, category.id_comp, inv_eq_inv],
    suffices : map.arrows Ï† hÏ† S (Ï†.obj c') (Ï†.obj c') (Ï†.map $ inv f â‰« Î³ â‰« f),
    { simp only [inv_eq_inv, functor.map_comp, functor.map_inv] at this, exact this, },
    { constructor, apply Sn.conj f Î³S, } } }

end hom

section thin

/-- A subgroupoid `is_thin` if it has at most one arrow between any two vertices. -/
abbreviation is_thin := quiver.is_thin S.objs

lemma is_thin_iff : S.is_thin â†” âˆ€ (c : S.objs), subsingleton (S.arrows c c) :=
by apply is_thin_iff

end thin

section disconnected

/-- A subgroupoid `is_totally_disconnected` if it has only isotropy arrows. -/
abbreviation is_totally_disconnected := is_totally_disconnected S.objs

lemma is_totally_disconnected_iff :
  S.is_totally_disconnected â†” âˆ€ c d, (S.arrows c d).nonempty â†’ c = d :=
begin
  split,
  { rintro h c d âŸ¨f,fSâŸ©,
    rw â†@subtype.mk_eq_mk _ _ c (mem_objs_of_src S fS) d (mem_objs_of_tgt S fS),
    exact h âŸ¨c, mem_objs_of_src S fSâŸ© âŸ¨d, mem_objs_of_tgt S fSâŸ© âŸ¨f, fSâŸ©, },
  { rintros h âŸ¨c, hcâŸ© âŸ¨d, hdâŸ© âŸ¨f, fSâŸ©,
    simp only [subtype.mk_eq_mk],
    exact h c d âŸ¨f, fSâŸ©, },
end

/-- The isotropy subgroupoid of `S` -/
def disconnect : subgroupoid C :=
{ arrows := Î» c d f, c = d âˆ§ f âˆˆ S.arrows c d,
  inv := by { rintros _ _ _ âŸ¨rfl, hâŸ©, exact âŸ¨rfl, S.inv hâŸ©, },
  mul := by { rintros _ _ _ _ âŸ¨rfl, hâŸ© _ âŸ¨rfl, h'âŸ©, exact âŸ¨rfl, S.mul h h'âŸ©, } }

lemma disconnect_le : S.disconnect â‰¤ S :=
by { rw le_iff, rintros _ _ _ âŸ¨âŸ©, assumption, }

lemma disconnect_normal (Sn : S.is_normal) : S.disconnect.is_normal :=
{ wide := Î» c, âŸ¨rfl, Sn.wide câŸ©,
  conj := Î» c d p Î³ âŸ¨_,h'âŸ©, âŸ¨rfl, Sn.conj _ h'âŸ© }

@[simp] lemma mem_disconnect_objs_iff {c : C} : c âˆˆ S.disconnect.objs â†” c âˆˆ S.objs :=
âŸ¨Î» âŸ¨Î³, h, Î³SâŸ©, âŸ¨Î³, Î³SâŸ©, Î» âŸ¨Î³, Î³SâŸ©, âŸ¨Î³, rfl, Î³SâŸ©âŸ©

lemma disconnect_objs : S.disconnect.objs = S.objs :=
by { apply set.ext, apply mem_disconnect_objs_iff, }

lemma disconnect_is_totally_disconnected : S.disconnect.is_totally_disconnected :=
by { rw is_totally_disconnected_iff, exact Î» c d âŸ¨f, h, fSâŸ©, h }

end disconnected

section full

variable (D : set C)

/-- The full subgroupoid on a set `D : set C` -/
def full : subgroupoid C :=
{ arrows := Î» c d _, c âˆˆ D âˆ§ d âˆˆ D,
  inv := by { rintros _ _ _ âŸ¨âŸ©, constructor; assumption, },
  mul := by { rintros _ _ _ _ âŸ¨âŸ© _ âŸ¨âŸ©, constructor; assumption,} }

lemma full_objs : (full D).objs = D :=
set.ext $ Î» _, âŸ¨Î» âŸ¨f, h, _âŸ©, h , Î» h, âŸ¨ğŸ™ _, h, hâŸ©âŸ©

@[simp] lemma mem_full_iff {c d : C} {f : c âŸ¶ d} : f âˆˆ (full D).arrows c d â†” c âˆˆ D âˆ§ d âˆˆ D :=
iff.rfl

@[simp] lemma mem_full_objs_iff {c : C} : c âˆˆ (full D).objs â†” c âˆˆ D :=
by rw full_objs

@[simp] lemma full_empty : full âˆ… = (âŠ¥ : subgroupoid C) :=
by { ext, simp only [has_bot.bot, mem_full_iff, mem_empty_iff_false, and_self], }

@[simp] lemma full_univ : full set.univ = (âŠ¤ : subgroupoid C) :=
by { ext, simp only [mem_full_iff, mem_univ, and_self, true_iff], }

lemma full_mono {D E : set C} (h : D â‰¤ E) : full D â‰¤ full E :=
begin
  rw le_iff,
  rintro c d f,
  simp only [mem_full_iff],
  exact Î» âŸ¨hc, hdâŸ©, âŸ¨h hc, h hdâŸ©,
end

lemma full_arrow_eq_iff {c d : (full D).objs} {f g : c âŸ¶ d} :
  f = g â†” (â†‘f : c.val âŸ¶ d.val) = â†‘g :=
by apply subtype.ext_iff

end full

end subgroupoid

end category_theory
