/-
Copyright (c) 2022 RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli
-/
import category_theory.groupoid.vertex_group
import category_theory.groupoid
import algebra.group.defs
import algebra.hom.group
import algebra.hom.equiv
import data.set.lattice
import combinatorics.quiver.connected_component
import combinatorics.quiver.subquiver
import group_theory.subgroup.basic
/-!
# Subgroupoid

This file defines subgroupoids as `structure`s containing the subsets of arrows and their
stability under composition and inversion.
Also defined are

* containment of subgroupoids is a complete lattice;
* images and preimages of subgroupoids under a functor;
* the notion of normality of subgroupoids and its stability under intersection and preimage;
* compatibility of the above with `groupoid.vertex_group`.


## Main definitions

Given a type `C` with associated `groupoid C` instance.

* `subgroupoid C` is the type of subgroupoids of `C`
* `subgroupoid.is_normal` is the property that the subgroupoid is stable under conjugation
  by arbitrary arrows, _and_ that all identity arrows are contained in the subgroupoid.
* `subgroupoid.comap` is the "preimage" map of subgroupoids along a functor.
* `subgroupoid.map` is the "image" map of subgroupoids along a functor _injective on objects_.
* `subgroupoid.vertex_subgroup` is the subgroup of the `vertex group` at a given vertex `v`,
  assuming `v` is contained in the `subgroupoid` (meaning, by definition, that the arrow `ğŸ™ v`
  is contained in the subgroupoid).

## Implementation details

The structure of this file is copied from/inspired by `group_theory.subgroup.basic`
and `combinatorics.simple_graph.subgraph`.

## TODO

* Equivalent inductive characterization of generated (normal) subgroupoids.
* Characterization of normal subgroupoids as kernels.

## Tags

subgroupoid

-/

open set classical function
local attribute [instance] prop_decidable

namespace category_theory

namespace groupoid

universes u v

variables {C : Type u} [groupoid C]


/--
A sugroupoid of `C` consists of a choice of arrows for each pair of vertices, closed
under composition and inverses
-/
@[ext] structure subgroupoid (C : Type u) [groupoid C] :=
  (arrws : âˆ€ (c d : C), set (c âŸ¶ d))
  (inv' : âˆ€ {c d} {p : c âŸ¶ d} (hp : p âˆˆ arrws c d),
            inv p âˆˆ arrws d c)
  (mul' : âˆ€ {c d e} {p} (hp : p âˆˆ arrws c d) {q} (hq : q âˆˆ arrws d e),
            p â‰« q âˆˆ arrws c e)

namespace subgroupoid

variable (S : subgroupoid C)

/-- The vertices of `C` on which `S` has non-trivial isotropy -/
def carrier : set C := {c : C | (S.arrws c c).nonempty }

lemma id_mem_of_nonempty_isotropy (c : C) :
  c âˆˆ carrier S â†’ ğŸ™ c âˆˆ S.arrws c c :=
begin
  rintro âŸ¨Î³,hÎ³âŸ©,
  have : ğŸ™ c = Î³ * (inv Î³), by simp only [vertex_group_mul, comp_inv],
  rw this,
  apply S.mul' hÎ³ (S.inv' hÎ³),
end

/-- A subgroupoid seen as a quiver on vertex set `C` -/
def as_wide_quiver : quiver C := âŸ¨Î» c d, subtype $ S.arrws c dâŸ©

/-- Type synonym to coerce a subgroupoid into a groupoid -/
def coe := subtype $ S.carrier

/-- A subgroupoid as a groupoid -/
@[simps] def coe_groupoid : groupoid (coe S) :=
{ to_category :=
  { to_category_struct :=
    { to_quiver :=
      { hom := Î» a b, S.arrws a.val b.val }
    , id := Î» a, âŸ¨ğŸ™ a.val, id_mem_of_nonempty_isotropy S a.val a.propâŸ©
    , comp := Î» a b c p q, âŸ¨p.val â‰« q.val, S.mul' p.prop q.propâŸ©, }
  , id_comp' := Î» a b âŸ¨p,hpâŸ©, by simp only [category.id_comp]
  , comp_id' := Î» a b âŸ¨p,hpâŸ©, by simp only [category.comp_id]
  , assoc' := Î» a b c d âŸ¨p,hpâŸ© âŸ¨q,hqâŸ© âŸ¨r,hrâŸ©, by simp only [category.assoc] }
, inv := Î» a b p, âŸ¨inv p.val, S.inv' p.propâŸ©
, inv_comp' := Î» a b âŸ¨p,hpâŸ©, by simp only [inv_comp]
, comp_inv' := Î» a b âŸ¨p,hpâŸ©, by simp only [comp_inv] }

/-- The `groupoid` instance for `subgroupoid.coe` -/
instance (S : subgroupoid C) : groupoid S.coe := coe_groupoid S

/-- There is an embedding of the coerced subgroupoid to its parent-/
def coe_embedding : (coe S) â¥¤ C :=
{ obj := Î» c, c.val,
  map := Î» c d f, f.val,
  map_id' := Î» c, by simp only [subtype.val_eq_coe, coe_groupoid_to_category_id_coe],
  map_comp' := Î» c d e f g, by simp only [subtype.val_eq_coe, coe_groupoid_to_category_comp_coe] }

lemma coe_embedding.inj_on_objects : function.injective (coe_embedding S).obj := by
{ rintros âŸ¨c,hcâŸ© âŸ¨d,hdâŸ© hcd, simp only [subtype.mk_eq_mk], exact hcd }

lemma coe_embedding.faithful :
  âˆ€ c d, function.injective (Î» (f : c âŸ¶ d), (coe_embedding S).map f) := by
{ rintros âŸ¨c,hcâŸ© âŸ¨d,hdâŸ© âŸ¨f,hfâŸ© âŸ¨g,hgâŸ© hfg, simp only [subtype.mk_eq_mk], exact hfg, }

/-- The subgroup of the vertex group at `c` given by the subgroupoid -/
def vertex_subgroup {c : C} (hc : c âˆˆ S.carrier) : subgroup (c âŸ¶ c) :=
âŸ¨ S.arrws c c
, Î» f g hf hg, S.mul' hf hg
, by {apply id_mem_of_nonempty_isotropy, use hc,}
, Î» f hf, S.inv' hfâŸ©

/-- `S` is a subgroupoid of `T` if it is contained in it -/
def is_subgroupoid (S T : subgroupoid C) : Prop :=
  âˆ€ {c d}, S.arrws c d âŠ† T.arrws c d

instance subgroupoid_le : has_le (subgroupoid C) := âŸ¨is_subgroupoidâŸ©

lemma le_refl (S : subgroupoid C) : S â‰¤ S :=
by {rintro c d p, exact id,}

lemma le_trans (R S T : subgroupoid C) : R â‰¤ S â†’ S â‰¤ T â†’ R â‰¤ T :=
by {rintro RS ST c d, exact (@RS c d).trans (@ST c d), }

lemma le_antisymm (R S : subgroupoid C) : R â‰¤ S â†’ S â‰¤ R â†’ R = S :=
by {rintro RS SR, ext c d p, exact âŸ¨(@RS c d p), (@SR c d p)âŸ©,}

instance : partial_order (subgroupoid C) :=
{ le := is_subgroupoid,
  le_refl := le_refl,
  le_trans := le_trans,
  le_antisymm := le_antisymm}

instance : has_top (subgroupoid C) :=
âŸ¨âŸ¨(Î» _ _, set.univ), by { rintros, trivial, }, by { rintros, trivial, }âŸ©âŸ©
instance : has_bot (subgroupoid C) :=
âŸ¨âŸ¨(Î» _ _, âˆ…), by { rintros, exfalso, assumption, }, by { rintros, exfalso, assumption, }âŸ©âŸ©

instance : inhabited (subgroupoid C) := âŸ¨âŠ¤âŸ©

instance : has_inf (subgroupoid C) :=
âŸ¨ Î» S T,
  âŸ¨(Î» c d, (S.arrws c d)âˆ©(T.arrws c d))
  , by { rintros, exact âŸ¨S.inv' hp.1,T.inv' hp.2âŸ©, }
  , by { rintros, exact âŸ¨S.mul' hp.1 hq.1, T.mul' hp.2 hq.2âŸ©, }âŸ©âŸ©

instance : has_Inf (subgroupoid C) :=
âŸ¨ Î» s,
  âŸ¨(Î» c d, set.Inter (Î» (S : s), S.val.arrws c d))
  , by
    { rintros,
      simp only [Inter_coe_set, mem_Inter] at hp âŠ¢,
      rintro S Ss,
      exact S.inv' (hp S Ss)}
  , by
    { rintros,
      simp only [Inter_coe_set, mem_Inter] at hp hq âŠ¢,
      rintro S Ss,
      apply S.mul' (hp S Ss) (hq S Ss), }âŸ©âŸ©

instance : complete_lattice (subgroupoid C) :=
{ bot          := (âŠ¥),
  bot_le       := Î» S c d, by {apply empty_subset,},
  top          := (âŠ¤),
  le_top       := Î» S c d, by {apply subset_univ,},
  inf          := (âŠ“),
  le_inf       := Î» R S T RS RT c d p pR, âŸ¨RS pR, RT pRâŸ©,
  inf_le_left  := Î» R S c d p pRS, pRS.left,
  inf_le_right := Î» R S c d p pRS, pRS.right,
  .. complete_lattice_of_Inf (subgroupoid C)
       ( by
        { dsimp only [Inf], rintro s, constructor,
          { rintro S Ss c d p hp,
            simp only [Inter_coe_set, mem_Inter] at hp,
            exact hp S Ss, },
          { rintro T Tl c d p pT,
            simp only [Inter_coe_set, mem_Inter],
            rintros S Ss, apply Tl Ss, exact pT,}}) }

/-- The family of arrows of the full subgroupoid on vertex set `V` -/
inductive full_on.arrws (V : set C) : Î  (c d : C), (c âŸ¶ d) â†’ Prop
| intro {c : C} (hc : c âˆˆ V) {d : C} (hd : d âˆˆ V) (f : c âŸ¶ d) : full_on.arrws c d f

@[simp] lemma full_on.mem_arrws_iff (V : set C) {c d : C} (f : c âŸ¶ d) :
  full_on.arrws V c d f â†” c âˆˆ V âˆ§ d âˆˆ V :=
begin
  split,
  { rintros âŸ¨c,hc,d,hd,fâŸ©, exact âŸ¨hc,hdâŸ©, },
  { rintros âŸ¨hc,hdâŸ©, constructor; assumption, },
end

/-- The full subgroupoid on vertex set `V` -/
def full_on (V : set C) : subgroupoid C :=
âŸ¨ full_on.arrws V
, by { rintros, induction hp, constructor; assumption, }
, by { rintros, induction hp, induction hq, constructor; assumption } âŸ©

/-- The family of arrows of the discrete subgroupoid -/
inductive discrete.arrws : Î  (c d : C), (c âŸ¶ d) â†’ Prop
| id (c : C) : discrete.arrws c c (ğŸ™ c)

/-- The only arrows of the discrete subgroupoid are the identity arrows-/
def discrete : subgroupoid C :=
âŸ¨ discrete.arrws
, by { rintros _ _ _ hp, induction hp, simp only [inv_eq_inv, is_iso.inv_id], constructor, }
, by { rintros _ _ _ _ hp _ hq, induction hp, induction hq, rw category.comp_id, constructor,} âŸ©

lemma mem_discrete_iff {c d : C} (f : c âŸ¶ d):
  (f âˆˆ (discrete).arrws c d) â†” (âˆƒ (h : c = d), f = h.rec_on (ğŸ™ c)) :=
begin
  split,
  { intro hf, induction hf, simp only [eq_self_iff_true, exists_true_left], },
  { rintro âŸ¨h,heâŸ©, subst_vars, constructor, }
end

section normal

/-- AÂ subgroupoid is normal if it is â€œwideâ€ (meaning that its carrier set is all of `C`)
    and satisfies the expected stability under conjugacy -/
structure is_normal : Prop :=
  (wide : âˆ€ c, (ğŸ™ c) âˆˆ (S.arrws c c))
  (conj : âˆ€ {c d} (p : c âŸ¶ d) (Î³ : c âŸ¶ c) (hs : Î³ âˆˆ S.arrws c c),
                ((inv p) â‰« Î³ â‰« p) âˆˆ (S.arrws d d))
  (conj' : âˆ€ {c d} (p : d âŸ¶ c) (Î³ : c âŸ¶ c) (hs : Î³ âˆˆ S.arrws c c),
                (p â‰« Î³ â‰« (inv p)) âˆˆ (S.arrws d d)
         := Î» c d p Î³ hs, by { convert conj (inv p) Î³ hs, simp, })

lemma is_normal.conjugation_eq (Sn : is_normal S) {c d} (p : c âŸ¶ d) :
  set.bij_on (Î» Î³ : c âŸ¶ c, (inv p) â‰« Î³ â‰« p) (S.arrws c c) (S.arrws d d) :=
begin
  split,
  { rintro Î³ Î³S, apply Sn.conj, exact Î³S },
  split,
  { rintro Î³â‚ Î³â‚S Î³â‚‚ Î³â‚‚S h,
    let := p â‰«=(h =â‰« (inv p)),
    simp only [inv_eq_inv, category.assoc, is_iso.hom_inv_id, category.comp_id,
               is_iso.hom_inv_id_assoc] at this âŠ¢,
    exact this, }, -- what's the quickest way here?
  { rintro Î´ Î´S, use (p â‰« Î´ â‰« (inv p)), split,
    { have : p = inv (inv p), by {simp only [inv_eq_inv, is_iso.inv_inv]},
      nth_rewrite 0 this,
      apply Sn.conj, exact Î´S, },
    { simp only [category.assoc, inv_comp, category.comp_id],
      simp only [â†category.assoc, inv_comp, category.id_comp], }}
end

lemma top_is_normal : is_normal (âŠ¤ : subgroupoid C) :=
{ wide := (Î» c, trivial)
, conj := (Î» a b c d e, trivial) }

lemma discrete_is_normal : is_normal (discrete : subgroupoid C) :=
{ wide := (Î» c, discrete.arrws.id c)
, conj := (Î» a b f d e , by
  { simp only [mem_discrete_iff, eq_self_iff_true, inv_eq_inv, is_iso.inv_comp_eq,
               category.comp_id, exists_true_left] at e âŠ¢,
    simp only [e, category.id_comp], }) }

lemma Inf_is_normal (s : set $ subgroupoid C) (sn : âˆ€ S âˆˆ s, is_normal S) : is_normal (Inf s) :=
{ wide := by
  { rintro c _ âŸ¨âŸ¨S,SsâŸ©,rflâŸ©,
    exact (sn S Ss).wide c, },
  conj := by
  { rintros c d p Î³ hÎ³ _ âŸ¨âŸ¨S,SsâŸ©,rflâŸ©,
    apply (sn S Ss).conj p Î³,
    apply hÎ³,
    use âŸ¨S,SsâŸ©, } }

lemma is_normal.vertex_subgroup (Sn : is_normal S) (c : C) (cS : c âˆˆ S.carrier) :
  (S.vertex_subgroup cS).normal :=
begin
  constructor,
  rintros x hx y,
  simp only [vertex_group_mul, vertex_group.inv_eq_inv, category.assoc],
  have : y = inv (inv y), by { simp only [inv_eq_inv, is_iso.inv_inv], },
  nth_rewrite 0 this,
  simp only [â†inv_eq_inv],
  apply Sn.conj, exact hx,
end

end normal

section graph_like

def is_graph_like := âˆ€ (c d : C), subsingleton (S.arrws c d)

lemma is_graph_like_iff : (is_graph_like S) â†” âˆ€ (c : C), subsingleton (S.arrws c c) :=
begin
  split,
  { rintro h c, exact h c c,},
  { rintros h c d, constructor, rintro âŸ¨f,fSâŸ© âŸ¨g,gSâŸ©,
    ext,
    simp only [subtype.coe_mk],
    have ifgS : inv f â‰« g âˆˆ S.arrws d d := S.mul' (S.inv' fS) gS,
    have idS : ğŸ™ d âˆˆ S.arrws d d := id_mem_of_nonempty_isotropy S d âŸ¨_,ifgSâŸ©,
    obtain âŸ¨ssâŸ© := h d,
    have : inv f â‰« g = ğŸ™ _, by
    { rw â†@subtype.mk_eq_mk _ (S.arrws d d) (inv f â‰« g) ifgS (ğŸ™ d) (idS),
      apply ss, },
    calc f
       = f â‰« (inv g â‰« g) : by simp
    ...= f â‰« (inv f â‰« g) : by { apply congr_arg2, refl, rw this, simp, }
    ...= g                : by simp, }
end

abbreviation maximal_graph_like (T : subgroupoid C) :=
  (T.is_graph_like âˆ§ T â‰¤ S) âˆ§ âˆ€ (T' : subgroupoid C), (T'.is_graph_like âˆ§ T' â‰¤ S) â†’ T â‰¤ T' â†’ T = T'

lemma is_graph_like.maximal_iff_full (T : subgroupoid C) (Tg : T.is_graph_like)  (TS : T â‰¤ S) :
  maximal_graph_like S T â†” (âˆ€ c d, (S.arrws c d).nonempty â†’ (T.arrws c d).nonempty) := sorry
-- should use the fact that a graph_like has a generating forest, and then it's graph-theoretic
-- argumentsâ€¦

lemma is_graph_like.exists_maximal :
  âˆƒ (T : subgroupoid C), maximal_graph_like S T := sorry

end graph_like

section disconnected

/-- A subgroupoid is disconnected if it only has loops -/
def is_disconnected := âˆ€ (c d : C), c â‰  d â†’ is_empty (S.arrws c d)

/-- The arrow set of `S.disconnect`, which drops all arrows but the loops -/
inductive disconnect.arrws : Î   (c d : C), (c âŸ¶ d) â†’ Prop
| vert {c : C} {Î³ : c âŸ¶ c} : Î³ âˆˆ S.arrws c c â†’  disconnect.arrws c c Î³

lemma disconnect.mem_arrws_iff {c d : C} (f : c âŸ¶ d) :
  disconnect.arrws S c d f â†” âˆƒ (h : c = d), (h.symm.rec_on f) âˆˆ S.arrws c c :=
begin
  split,
  { rintros âŸ¨c,Î³,hÎ³âŸ©, simp only [eq_self_iff_true, exists_true_left], exact hÎ³ },
  { rintro âŸ¨rfl,hâŸ©, constructor, simp only at h, exact h, },
end

/-- Only keep the loops of `S` -/
def disconnect : subgroupoid C :=
âŸ¨ disconnect.arrws S
, Î» _ _ f hf, by {induction hf, constructor, apply S.inv', assumption, }
, Î» _ _ _ f hf g hg, by
  { obtain âŸ¨hf,efâŸ© := (disconnect.mem_arrws_iff S f).mp hf,
    obtain âŸ¨hg,egâŸ© := (disconnect.mem_arrws_iff S g).mp hg,
    subst_vars, constructor, apply S.mul' ef eg, }âŸ©

lemma disconnect_is_normal_of_normal (Sn : is_normal S) : S.disconnect.is_normal :=
{ wide := Î» c, disconnect.arrws.vert $ Sn.wide c
, conj := Î» _ _ f _ h, by {cases h, constructor, apply Sn.conj, assumption, } }

end disconnected

section generated_subgroupoid

-- TODO:Â proof that generated is just "words in X" and generated_normal is similarly
variable (X : âˆ€ (c d : C), set (c âŸ¶ d))

/-- The subgropoid generated by the set of arrows `X` -/
def generated : subgroupoid C :=
  Inf {S : subgroupoid C | âˆ€ c d, X c d âŠ† S.arrws c d}

/- TODOÂ to prove this:
* Define the â€œforestâ€ property below properly on quivers:Â this is equivalent to the symmetrification
  containing only subsingletons
* Show that the free groupoid on a â€œforestâ€ quiver is graph_like, and conversely
  that if the free groupoid is graph_like, then it is generated by a â€œforestâ€ quiver.
* Prove that `generated X` is just words in `X` by using the free groupoid on `X`.
  (need to be careful about only keeping the strictly necessary vertices :Â only those incident to
   arrows of `X` need appear in the quiver defined by `X` and in the free groupoid).

Now for the actual statement:

* If S is graph_like, take a maximal forest contained in S.
-/

--lemma is_graph_like.is_free_groupoid_on_maximal_forest := sorry

lemma generated.is_graph_like_iff_generating_forest :
  S.is_graph_like â†”
  âˆƒ (X : âˆ€ (c d : C), set $ c âŸ¶ d),
      (generated X) = S
    âˆ§ (âˆ€ c d : C, subsingleton
        ((@quiver.path C âŸ¨Î» c d, subtype $ X c dâŸ© d c) âŠ• (@quiver.path C âŸ¨Î» c d, subtype $ X c dâŸ© d c)))   := sorry


/-- The normal sugroupoid generated by the set of arrows `X` -/
def generated_normal : subgroupoid C :=
  Inf {S : subgroupoid C | (âˆ€ c d, X c d âŠ† S.arrws c d) âˆ§ S.is_normal }

lemma generated_normal_is_normal : (generated_normal X).is_normal :=
Inf_is_normal _ (Î» S h, h.right)

end generated_subgroupoid

section hom

variables {D : Type*}
variables [groupoid D] (Ï† : C â¥¤ D)

section comap

variables (T U : subgroupoid D)

/--
A functor between groupoid defines a map of subgroupoids in the reverse direction
by taking preimages.
 -/
def comap : subgroupoid C :=
âŸ¨ Î» c d, {f : c âŸ¶ d | Ï†.map f âˆˆ T.arrws (Ï†.obj c) (Ï†.obj d)}
, by
  { rintros,
    simp only [inv_eq_inv, mem_set_of_eq, functor.map_inv],
    simp only [â†inv_eq_inv],
    simp only [mem_set_of_eq] at hp,
    apply T.inv', assumption, }
, by
  { rintros,
    simp only [mem_set_of_eq, functor.map_comp],
    apply T.mul';
    assumption, }âŸ©


lemma comap_mono {T U} :
  T â‰¤ U â†’ comap Ï† T â‰¤ comap Ï† U :=
begin
  rintro TU,
  dsimp only [subgroupoid.comap],
  rintro c d p hp,
  exact TU hp,
end

lemma is_normal_comap {T} (Tn : is_normal T) : is_normal (comap Ï† T) :=
{ wide := by
  { rintro c,
    dsimp only [comap],
    simp only [mem_set_of_eq, functor.map_id],
    apply Tn.wide, }
, conj := by
  { rintros c d f Î³ hÎ³,
    dsimp only [comap],
    simp only [mem_set_of_eq, functor.map_comp, functor.map_inv, inv_eq_inv],
    rw [â†inv_eq_inv],
    apply Tn.conj, exact hÎ³, } }

/-- The kernel of a functor between subgroupoid is the preimage. -/
def ker : subgroupoid C := comap Ï† (discrete)

lemma mem_ker_iff {c d : C} (f : c âŸ¶ d) :
  f âˆˆ (ker Ï†).arrws c d â†” âˆƒ (h : Ï†.obj c = Ï†.obj d), Ï†.map f = h.rec_on (ğŸ™ $ Ï†.obj c) :=
mem_discrete_iff (Ï†.map f)

end comap

section map

variables (hÏ† : function.injective Ï†.obj) (S)

/-- The family of arrows of the image of a subgroupoid under a functor injective on objects -/
inductive map.arrws :
  Î  (c d : D), (c âŸ¶ d) â†’ Prop
| im {c d : C} (f : c âŸ¶ d) (hf : f âˆˆ S.arrws c d) : map.arrws (Ï†.obj c) (Ï†.obj d) (Ï†.map f)

lemma map.mem_arrws_iff {c d : D} (f : c âŸ¶ d) :
  map.arrws S Ï† c d f â†”
  âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d) (hg : g âˆˆ S.arrws a b),
    f = @eq.rec_on _ (Ï†.obj a) (Î» x, x âŸ¶ d) (c) ha (hb.rec_on $ Ï†.map g) :=
begin
  split,
  { rintro âŸ¨a,b,g,hgâŸ©, use [a,b,g,rfl,rfl,hg,rfl], },
  { rintro âŸ¨a,b,g,ha,hb,hg,heâŸ©, subst_vars,
    simp only [congr_arg_mpr_hom_right, eq_to_hom_refl, category.comp_id],
    constructor, exact hg, },
end

/-- The "forward" image of a subgroupoid under a functor injective on objects -/
def map (hÏ† : function.injective Ï†.obj) (S) : subgroupoid D :=
âŸ¨ map.arrws S Ï†
, by
  { rintro _ _ _ hp, induction hp,
    rw [inv_eq_inv,â†functor.map_inv], constructor,
    rw â†inv_eq_inv, apply S.inv', assumption, }
, by -- Is there no way to prove this â†“ directly without the help of `map.mem_arrws_iff` ?
  { rintro _ _ _ _ hp _ hq,
    obtain âŸ¨fâ‚€,fâ‚,f,hfâ‚€,hfâ‚,hf,fpâŸ© := (map.mem_arrws_iff S Ï† p).mp hp,
    obtain âŸ¨gâ‚€,gâ‚,g,hgâ‚€,hgâ‚,hg,gqâŸ© := (map.mem_arrws_iff S Ï† q).mp hq,
    simp only [has_mem.mem, map.mem_arrws_iff],
    have : fâ‚ = gâ‚€, by {apply hÏ†, exact hfâ‚.trans hgâ‚€.symm, },
    induction this,
    refine âŸ¨fâ‚€,gâ‚,f â‰« g,hfâ‚€,hgâ‚,S.mul' hf hg,_âŸ©,
    simp only [functor.map_comp],
    subst_vars } âŸ©

lemma map_mono (S T : subgroupoid C) :
  S â‰¤ T â†’ map Ï† hÏ† S â‰¤ map Ï† hÏ† T :=
begin
  rintros le _ _ _ âŸ¨a,b,f,hâŸ©,
  constructor,
  apply le h,
end

/-- The image of a functor injective on objects -/
def im := map Ï† hÏ† (âŠ¤)

lemma mem_im_iff {c d : D} (f : c âŸ¶ d) :
  f âˆˆ (im Ï† hÏ†).arrws c d â†”
  âˆƒ (a b : C) (g : a âŸ¶ b) (ha : Ï†.obj a = c) (hb : Ï†.obj b = d),
    f = @eq.rec_on _ (Ï†.obj a) (Î» x, x âŸ¶ d) (c) ha (hb.rec_on $ Ï†.map g) :=
begin
  convert map.mem_arrws_iff âŠ¤ Ï† f,
  dsimp [âŠ¤,has_top.top],
  simp only [mem_univ, exists_true_left],
end

lemma is_normal_map (hÏ†s : im Ï† hÏ† = âŠ¤) :
  S.is_normal â†’ (map Ï† hÏ† S).is_normal :=
begin
  rintro âŸ¨Swide,SconjâŸ©,
  sorry
end

lemma is_graph_like_map : S.is_graph_like â†’ (map Ï† hÏ† S).is_graph_like := sorry

lemma is_disconnected_map : S.is_disconnected â†’ (map Ï† hÏ† S).is_disconnected := sorry

end map

end hom

end subgroupoid

end groupoid

end category_theory
