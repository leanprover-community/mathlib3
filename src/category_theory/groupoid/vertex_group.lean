/-
Copyright (c) 2022 RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli
-/
import category_theory.groupoid
import category_theory.path_category
import algebra.group.defs
import algebra.hom.group
import algebra.hom.equiv.basic
import combinatorics.quiver.path

/-!
# Vertex group

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the vertex group (*aka* isotropy group) of a groupoid at a vertex.

## Implementation notes

* The instance is defined "manually", instead of relying on `category_theory.Aut.group` or
  using `category_theory.inv`.
* The multiplication order therefore matches the categorical one : `x * y = x â‰« y`.
* The inverse is directly defined in terms of the groupoidal inverse : `x â»Â¹ = groupoid.inv x`.

## Tags

isotropy, vertex group, groupoid
-/

namespace category_theory

namespace groupoid

universes u v

variables {C : Type u} [groupoid C]

/-- The vertex group at `c`. -/
@[simps] instance vertex_group (c : C): group (c âŸ¶ c) :=
{ mul := Î» (x y : c âŸ¶ c), x â‰« y,
  mul_assoc := category.assoc,
  one := ğŸ™ c,
  one_mul := category.id_comp,
  mul_one := category.comp_id,
  inv := groupoid.inv,
  mul_left_inv := inv_comp }

/-- The inverse in the group is equal to the inverse given by `category_theory.inv`. -/
lemma vertex_group.inv_eq_inv (c : C) (Î³ : c âŸ¶ c) :
  Î³ â»Â¹ = category_theory.inv Î³ := groupoid.inv_eq_inv Î³

/--
An arrow in the groupoid defines, by conjugation, an isomorphism of groups between
its endpoints.
-/
@[simps] def vertex_group_isom_of_map {c d : C} (f : c âŸ¶ d) : (c âŸ¶ c) â‰ƒ* (d âŸ¶ d) :=
{ to_fun  := Î» Î³, inv f â‰« Î³ â‰« f,
  inv_fun := Î» Î´, f â‰« Î´ â‰« inv f,
  left_inv := Î» Î³, by simp_rw [category.assoc, comp_inv, category.comp_id,
                              â†category.assoc, comp_inv, category.id_comp],
  right_inv := Î» Î´, by simp_rw [category.assoc, inv_comp, â†category.assoc,
                                inv_comp, category.id_comp, category.comp_id],
  map_mul' := Î» Î³â‚ Î³â‚‚, by simp only [vertex_group_mul, inv_eq_inv,
                                     category.assoc, is_iso.hom_inv_id_assoc] }

/--
A path in the groupoid defines an isomorphism between its endpoints.
-/
def vertex_group_isom_of_path {c d : C} (p : quiver.path c d) : (c âŸ¶ c) â‰ƒ* (d âŸ¶ d) :=
vertex_group_isom_of_map (compose_path p)

/-- A functor defines a morphism of vertex group. -/
@[simps] def _root_.category_theory.functor.map_vertex_group {D : Type v} [groupoid D]
  (Ï† : C â¥¤ D) (c : C) : (c âŸ¶ c) â†’* (Ï†.obj c âŸ¶ Ï†.obj c) :=
{ to_fun := Ï†.map,
  map_one' := Ï†.map_id c,
  map_mul' := Ï†.map_comp }

end groupoid

end category_theory
