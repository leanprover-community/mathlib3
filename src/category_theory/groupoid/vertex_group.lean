/-
Copyright (c) 2022 RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli
-/
import category_theory.groupoid
import algebra.group.defs
import algebra.hom.group
import algebra.hom.equiv
import combinatorics.quiver.path

/-!
# Vertex group

This file defines the vertex group (*aka* isotropy group) of a groupoid at a vertex.

## Implementation notes

* The instance is defined "manually", instead of relying on `category_theory.Aut.group` or
  using `category_theory.inv`.
* The multiplication order therefore matches the categorical one : `x * y = x â‰« y`.
* The inverse is directly defined in terms of the groupoidal inverse : `x â»Â¹ = groupoid.inv x`.

## Tags

isotropy, vertex group, groupoid
-/

namespace category_theory

namespace groupoid

universes u v

variables {C : Type u} [groupoid C]

/-- The vertex group at `c`. -/
@[simps] instance vertex_group (c : C): group (c âŸ¶ c) :=
{ mul := Î» (x y : c âŸ¶ c), x â‰« y,
  mul_assoc := category.assoc,
  one := ğŸ™ c,
  one_mul := category.id_comp,
  mul_one := category.comp_id,
  inv := groupoid.inv,
  mul_left_inv := inv_comp }

/-- The inverse in the group is equal to the inverse given by `category_theory.inv`. -/
lemma vertex_group.inv_eq_inv (c : C) (Î³ : c âŸ¶ c) :
  Î³ â»Â¹ = category_theory.inv Î³ := groupoid.inv_eq_inv Î³

/--
An arrow in the groupoid defines, by conjugation, an isomorphism of groups between
its endpoints
-/
def vertex_group_isom_of_map {c d : C} (f : c âŸ¶ d) : (c âŸ¶ c) â‰ƒ* (d âŸ¶ d) :=
âŸ¨ Î» Î³, (groupoid.inv f) â‰« Î³ â‰« f, Î» Î´, f â‰« Î´ â‰« (groupoid.inv f),
  Î» x, by
  { simp_rw [category.assoc, groupoid.comp_inv, category.comp_id,â†category.assoc,
             groupoid.comp_inv, category.id_comp], },
  Î» x, by
  { simp_rw [category.assoc, groupoid.inv_comp, â†category.assoc, groupoid.inv_comp,
             category.id_comp, category.comp_id], },
  Î» x y, by
  { have : x â‰« y = x â‰« f â‰« (groupoid.inv f) â‰« y, by
    { congr, rw [â†category.assoc,groupoid.comp_inv,category.id_comp], },
    simp only [this, groupoid.vertex_group_mul, category.assoc], } âŸ©

/--
A path in the groupoid defines an isomorphism between its endpoints.
-/
def vertex_group_isom_of_path {c : C} : Î  {d : C} (p : quiver.path c d), (c âŸ¶ c) â‰ƒ* (d âŸ¶ d)
| _ quiver.path.nil := by refl
| _ (quiver.path.cons q f) := (vertex_group_isom_of_path q).trans (vertex_group_isom_of_map f)

/-- A functor defines a morphism of vertex group. -/
def vertex_group_hom_of_functor {D : Type v} [groupoid D] (Ï† : C â¥¤ D) (c : C) :
  (c âŸ¶ c) â†’* (Ï†.obj c âŸ¶ Ï†.obj c) :=
âŸ¨ Î» Î³, Ï†.map Î³,
  functor.map_id Ï† c,
  Î» Î³ Î´, functor.map_comp Ï† Î³ Î´ âŸ©

end groupoid

end category_theory
