/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import category_theory.abelian.basic

/-!
# Idempotent complete categories

In this file, we define the notion of idempotent complete categories
(also known as Karoubian categories, or pseudoabelian in the case of
preadditive categories).

## Main definitions

- `is_idempotent_complete C` expresses that `C` is idempotent complete, i.e.
all idempotents in `C` split. Other characterisations of idempotent completeness are given
by `is_idempotent_complete_iff_has_equalizer_of_id_and_idempotent` and
`is_idempotent_complete_iff_idempotents_have_kernels`.
- `is_idempotent_complete_of_abelian` expresses that abelian categories are
idempotent complete.
- `is_idempotent_complete_iff_of_equivalence` expresses that if two categories `C` and `D`
are equivalent, then `C` is idempotent complete iff `D` is.
- `is_idempotent_complete_iff_opposite` expresses that `Cáµ’áµ–` is idempotent complete
iff `C` is.

## References
* [Stacks: Karoubian categories] https://stacks.math.columbia.edu/tag/09SF

-/

open category_theory
open category_theory.category
open category_theory.limits
open category_theory.preadditive
open opposite

namespace category_theory

variables (C : Type*) [category C]

/-- A category is idempotent complete iff all idempotent endomorphisms `p`
split as a composition `p = e â‰« i` with `i â‰« e = ğŸ™ _` -/
class is_idempotent_complete : Prop :=
(idempotents_split : âˆ€ (X : C) (p : X âŸ¶ X), p â‰« p = p â†’
  âˆƒ (Y : C) (i : Y âŸ¶ X) (e : X âŸ¶ Y), i â‰« e = ğŸ™ Y âˆ§ e â‰« i = p)

namespace idempotents

/-- A category is idempotent complete iff for all idempotent endomorphisms,
the equalizer of the identity and this idempotent exists. -/
lemma is_idempotent_complete_iff_has_equalizer_of_id_and_idempotent :
  is_idempotent_complete C â†” âˆ€ (X : C) (p : X âŸ¶ X), p â‰« p = p â†’ has_equalizer (ğŸ™ X) p :=
begin
  split,
  { introI,
    intros X p hp,
    rcases is_idempotent_complete.idempotents_split X p hp with âŸ¨Y, i, e, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©,
    exact âŸ¨nonempty.intro
      { cone := fork.of_Î¹ i
          (show i â‰« ğŸ™ X = i â‰« p, by rw [comp_id, â† hâ‚‚, â† assoc, hâ‚, id_comp]),
        is_limit := begin
          apply fork.is_limit.mk',
          intro s,
          refine âŸ¨s.Î¹ â‰« e, _âŸ©,
          split,
          { erw [assoc, hâ‚‚, â† limits.fork.condition s, comp_id], },
          { intros m hm,
            rw fork.Î¹_of_Î¹ at hm,
            rw [â† hm],
            simp only [â† hm, assoc, hâ‚],
            exact (comp_id m).symm }
        end }âŸ©, },
  { intro h,
    refine âŸ¨_âŸ©,
    intros X p hp,
    haveI := h X p hp,
    use equalizer (ğŸ™ X) p,
    use equalizer.Î¹ (ğŸ™ X) p,
    use equalizer.lift p (show p â‰« ğŸ™ X = p â‰« p, by rw [hp, comp_id]),
    split,
    { ext,
      rw [assoc, equalizer.lift_Î¹, id_comp],
      conv { to_rhs, erw [â† comp_id (equalizer.Î¹ (ğŸ™ X) p)], },
      exact (limits.fork.condition (equalizer.fork (ğŸ™ X) p)).symm, },
    { rw [equalizer.lift_Î¹], }, }
end

variables {C}

/-- In a preadditive category, when `p : X âŸ¶ X` is idempotent,
then `ğŸ™ X - p` is also idempotent. -/
lemma idem_of_id_sub_idem [preadditive C]
  {X : C} (p : X âŸ¶ X) (hp : p â‰« p = p) :
  (ğŸ™ _ - p) â‰« (ğŸ™ _ - p) = (ğŸ™ _ - p) :=
by simp only [comp_sub, sub_comp, id_comp, comp_id, hp, sub_self, sub_zero]

variables (C)

/-- A preadditive category is pseudoabelian iff all idempotent endomorphisms have a kernel. -/
lemma is_idempotent_complete_iff_idempotents_have_kernels [preadditive C] :
  is_idempotent_complete C â†” âˆ€ (X : C) (p : X âŸ¶ X), p â‰« p = p â†’ has_kernel p :=
begin
  rw is_idempotent_complete_iff_has_equalizer_of_id_and_idempotent,
  split,
  { intros h X p hp,
    haveI := h X (ğŸ™ _ - p) (idem_of_id_sub_idem p hp),
    convert has_kernel_of_has_equalizer (ğŸ™ X) (ğŸ™ X - p),
    rw [sub_sub_cancel], },
  { intros h X p hp,
    haveI : has_kernel (ğŸ™ _ - p) := h X (ğŸ™ _ - p) (idem_of_id_sub_idem p hp),
    apply preadditive.has_equalizer_of_has_kernel, },
end

/-- An abelian category is idempotent complete. -/
@[priority 100]
instance is_idempotent_complete_of_abelian (D : Type*) [category D] [abelian D] :
  is_idempotent_complete D :=
by { rw is_idempotent_complete_iff_idempotents_have_kernels, intros, apply_instance, }

variables {C}

lemma split_imp_of_iso {X X' : C} (Ï† : X â‰… X') (p : X âŸ¶ X) (p' : X' âŸ¶ X')
  (hpp' : p â‰« Ï†.hom = Ï†.hom â‰« p')
  (h : âˆƒ (Y : C) (i : Y âŸ¶ X) (e : X âŸ¶ Y), i â‰« e = ğŸ™ Y âˆ§ e â‰« i = p) :
  (âˆƒ (Y' : C) (i' : Y' âŸ¶ X') (e' : X' âŸ¶ Y'), i' â‰« e' = ğŸ™ Y' âˆ§ e' â‰« i' = p') :=
begin
  rcases h with âŸ¨Y, i, e, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©,
  use [Y, i â‰« Ï†.hom, Ï†.inv â‰« e],
  split,
  { slice_lhs 2 3 { rw Ï†.hom_inv_id, },
    rw [id_comp, hâ‚], },
  { slice_lhs 2 3 { rw hâ‚‚, },
    rw [hpp', â† assoc, Ï†.inv_hom_id, id_comp], }
end

lemma split_iff_of_iso {X X' : C} (Ï† : X â‰… X') (p : X âŸ¶ X) (p' : X' âŸ¶ X')
  (hpp' : p â‰« Ï†.hom = Ï†.hom â‰« p') :
  (âˆƒ (Y : C) (i : Y âŸ¶ X) (e : X âŸ¶ Y), i â‰« e = ğŸ™ Y âˆ§ e â‰« i = p) â†”
  (âˆƒ (Y' : C) (i' : Y' âŸ¶ X') (e' : X' âŸ¶ Y'), i' â‰« e' = ğŸ™ Y' âˆ§ e' â‰« i' = p') :=
begin
  split,
  { exact split_imp_of_iso Ï† p p' hpp', },
  { apply split_imp_of_iso Ï†.symm p' p,
    rw [â† comp_id p, â† Ï†.hom_inv_id],
    slice_rhs 2 3 { rw hpp', },
    slice_rhs 1 2 { erw Ï†.inv_hom_id, },
    simpa only [id_comp], },
end

lemma equivalence.is_idempotent_complete {D : Type*} [category D] (Îµ : C â‰Œ D)
  (h : is_idempotent_complete C) : is_idempotent_complete D :=
begin
  refine âŸ¨_âŸ©,
  intros X' p hp,
  let Ï† := Îµ.counit_iso.symm.app X',
  erw split_iff_of_iso Ï† p (Ï†.inv â‰« p â‰« Ï†.hom)
    (by { slice_rhs 1 2 { rw Ï†.hom_inv_id, }, rw id_comp,}),
  rcases is_idempotent_complete.idempotents_split (Îµ.inverse.obj X') (Îµ.inverse.map p)
    (by rw [â† Îµ.inverse.map_comp, hp]) with âŸ¨Y, i, e, âŸ¨hâ‚,hâ‚‚âŸ©âŸ©,
  use [Îµ.functor.obj Y, Îµ.functor.map i, Îµ.functor.map e],
  split,
  { rw [â† Îµ.functor.map_comp, hâ‚, Îµ.functor.map_id], },
  { simpa only [â† Îµ.functor.map_comp, hâ‚‚, equivalence.fun_inv_map], },
end

/-- If `C` and `D` are equivalent categories, that `C` is idempotent complete iff `D` is. -/
lemma is_idempotent_complete_iff_of_equivalence {D : Type*} [category D] (Îµ : C â‰Œ D) :
  is_idempotent_complete C â†” is_idempotent_complete D :=
begin
  split,
  { exact equivalence.is_idempotent_complete Îµ, },
  { exact equivalence.is_idempotent_complete Îµ.symm, },
end

lemma is_idempotent_complete_of_is_idempotent_complete_opposite
  (h : is_idempotent_complete Cáµ’áµ–) : is_idempotent_complete C :=
begin
  refine âŸ¨_âŸ©,
  intros X p hp,
  rcases is_idempotent_complete.idempotents_split (op X) p.op
    (by rw [â† op_comp, hp]) with âŸ¨Y, i, e, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©,
  use [Y.unop, e.unop, i.unop],
  split,
  { simpa only [â† unop_comp, hâ‚], },
  { simpa only [â† unop_comp, hâ‚‚], },
end

lemma is_idempotent_complete_iff_opposite :
  is_idempotent_complete Cáµ’áµ– â†” is_idempotent_complete C :=
begin
  split,
  { exact is_idempotent_complete_of_is_idempotent_complete_opposite, },
  { intro h,
    apply is_idempotent_complete_of_is_idempotent_complete_opposite,
    rw is_idempotent_complete_iff_of_equivalence (op_op_equivalence C),
    exact h, },
end

instance [is_idempotent_complete C] : is_idempotent_complete (Cáµ’áµ–) :=
by rwa is_idempotent_complete_iff_opposite

end idempotents

end category_theory
