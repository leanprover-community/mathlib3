/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import category_theory.idempotents.basic
import category_theory.preadditive.additive_functor
import category_theory.equivalence

/-!
# The Karoubi envelope of a category

In this file, we define the Karoubi envelope `karoubi C` of a category `C`.

## Main constructions and definitions

- `karoubi C` is the Karoubi envelope of a category `C`: it is an idempotent
complete category. It is also preadditive when `C` is preadditive.
- `to_karoubi C : C â¥¤ karoubi C` is a fully faithful functor, which is an equivalence
(`to_karoubi_is_equivalence`) when `C` is idempotent complete.

-/

noncomputable theory

open category_theory.category
open category_theory.preadditive
open category_theory.limits
open_locale big_operators

namespace category_theory

variables (C : Type*) [category C]

namespace idempotents

/-- In a preadditive category `C`, when an object `X` decomposes as `X â‰… P â¨¿ Q`, one may
consider `P` as a direct factor of `X` and up to unique isomorphism, it is determined by the
obvious idempotent `X âŸ¶ P âŸ¶ X` which is the projection onto `P` with kernel `Q`. More generally,
one may define a formal direct factor of an object `X : C` : it consists of an idempotent
`p : X âŸ¶ X` which is thought as the "formal image" of `p`. The type `karoubi C` shall be the
type of the objects of the karoubi enveloppe of `C`. It makes sense for any category `C`. -/
@[nolint has_nonempty_instance]
structure karoubi := (X : C) (p : X âŸ¶ X) (idem : p â‰« p = p)

namespace karoubi

variables {C}

attribute [simp, reassoc] idem

@[ext]
lemma ext {P Q : karoubi C} (h_X : P.X = Q.X)
  (h_p : P.p â‰« eq_to_hom h_X = eq_to_hom h_X â‰« Q.p) : P = Q :=
begin
  cases P,
  cases Q,
  dsimp at h_X h_p,
  subst h_X,
  simpa only [true_and, eq_self_iff_true, id_comp, eq_to_hom_refl,
    heq_iff_eq, comp_id] using h_p,
end

/-- A morphism `P âŸ¶ Q` in the category `karoubi C` is a morphism in the underlying category
`C` which satisfies a relation, which in the preadditive case, expresses that it induces a
map between the corresponding "formal direct factors" and that it vanishes on the complement
formal direct factor. -/
@[ext]
structure hom (P Q : karoubi C) := (f : P.X âŸ¶ Q.X) (comm : f = P.p â‰« f â‰« Q.p)

instance [preadditive C] (P Q : karoubi C) : inhabited (hom P Q) :=
âŸ¨âŸ¨0, by rw [zero_comp, comp_zero]âŸ©âŸ©

@[simp]
lemma hom_ext {P Q : karoubi C} {f g : hom P Q} : f = g â†” f.f = g.f :=
begin
  split,
  { intro h, rw h, },
  { ext, }
end

@[simp, reassoc]
lemma p_comp {P Q : karoubi C} (f : hom P Q) : P.p â‰« f.f = f.f :=
by rw [f.comm, â† assoc, P.idem]

@[simp, reassoc]
lemma comp_p {P Q : karoubi C} (f : hom P Q) : f.f â‰« Q.p = f.f :=
by rw [f.comm, assoc, assoc, Q.idem]

lemma p_comm {P Q : karoubi C} (f : hom P Q) : P.p â‰« f.f = f.f â‰« Q.p :=
by rw [p_comp, comp_p]

lemma comp_proof {P Q R : karoubi C} (g : hom Q R) (f : hom P Q) :
  f.f â‰« g.f = P.p â‰« (f.f â‰« g.f) â‰« R.p :=
by rw [assoc, comp_p, â† assoc, p_comp]

/-- The category structure on the karoubi envelope of a category. -/
instance : category (karoubi C) :=
{ hom      := karoubi.hom,
  id       := Î» P, âŸ¨P.p, by { repeat { rw P.idem, }, }âŸ©,
  comp     := Î» P Q R f g, âŸ¨f.f â‰« g.f, karoubi.comp_proof g fâŸ©, }

@[simp]
lemma comp_f {P Q R : karoubi C} (f : P âŸ¶ Q) (g : Q âŸ¶ R) :
  (f â‰« g).f = f.f â‰« g.f := by refl

@[simp]
lemma id_eq {P : karoubi C} : ğŸ™ P = âŸ¨P.p, by repeat { rw P.idem, }âŸ© := by refl

/-- It is possible to coerce an object of `C` into an object of `karoubi C`.
See also the functor `to_karoubi`. -/
instance coe : has_coe_t C (karoubi C) := âŸ¨Î» X, âŸ¨X, ğŸ™ X, by rw comp_idâŸ©âŸ©

@[simp]
lemma coe_X (X : C) : (X : karoubi C).X = X := by refl

@[simp]
lemma coe_p (X : C) : (X : karoubi C).p = ğŸ™ X := by refl

@[simp]
lemma eq_to_hom_f {P Q : karoubi C} (h : P = Q) :
  karoubi.hom.f (eq_to_hom h) = P.p â‰« eq_to_hom (congr_arg karoubi.X h) :=
by { subst h, simp only [eq_to_hom_refl, karoubi.id_eq, comp_id], }

end karoubi

/-- The obvious fully faithful functor `to_karoubi` sends an object `X : C` to the obvious
formal direct factor of `X` given by `ğŸ™ X`. -/
@[simps]
def to_karoubi : C â¥¤ karoubi C :=
{ obj := Î» X, âŸ¨X, ğŸ™ X, by rw comp_idâŸ©,
  map := Î» X Y f, âŸ¨f, by simp only [comp_id, id_comp]âŸ© }

instance : full (to_karoubi C) :=
{ preimage := Î» X Y f, f.f, }

instance : faithful (to_karoubi C) := { }

variables {C}

@[simps]
instance [preadditive C] {P Q : karoubi C} : add_comm_group (P âŸ¶ Q) :=
{ add := Î» f g, âŸ¨f.f+g.f, begin
    rw [add_comp, comp_add],
    congr',
    exacts [f.comm, g.comm],
  endâŸ©,
  zero := âŸ¨0, by simp only [comp_zero, zero_comp]âŸ©,
  zero_add := Î» f, by { ext, simp only [zero_add], },
  add_zero := Î» f, by { ext, simp only [add_zero], },
  add_assoc := Î» f g h', by simp only [add_assoc],
  add_comm := Î» f g, by { ext, apply_rules [add_comm], },
  neg := Î» f, âŸ¨-f.f, by simpa only [neg_comp, comp_neg, neg_inj] using f.commâŸ©,
  add_left_neg := Î» f, by { ext, apply_rules [add_left_neg], }, }

namespace karoubi

lemma hom_eq_zero_iff [preadditive C] {P Q : karoubi C} {f : hom P Q} : f = 0 â†” f.f = 0 := hom_ext

/-- The map sending `f : P âŸ¶ Q` to `f.f : P.X âŸ¶ Q.X` is additive. -/
@[simps]
def inclusion_hom [preadditive C] (P Q : karoubi C) : add_monoid_hom (P âŸ¶ Q) (P.X âŸ¶ Q.X) :=
{ to_fun    := Î» f, f.f,
  map_zero' := rfl,
  map_add'  := Î» f g, rfl }

@[simp]
lemma sum_hom [preadditive C] {P Q : karoubi C} {Î± : Type*} (s : finset Î±) (f : Î± â†’ (P âŸ¶ Q)) :
  (âˆ‘ x in s, f x).f = âˆ‘ x in s, (f x).f  :=
add_monoid_hom.map_sum (inclusion_hom P Q) f s

end karoubi

/-- The category `karoubi C` is preadditive if `C` is. -/
instance [preadditive C] : preadditive (karoubi C) :=
{ hom_group := Î» P Q, by apply_instance, }

instance [preadditive C] : functor.additive (to_karoubi C) := { }

open karoubi

variables (C)

instance : is_idempotent_complete (karoubi C) :=
begin
  refine âŸ¨_âŸ©,
  intros P p hp,
  have hp' := hom_ext.mp hp,
  simp only [comp_f] at hp',
  use âŸ¨P.X, p.f, hp'âŸ©,
  use âŸ¨p.f, by rw [comp_p p, hp']âŸ©,
  use âŸ¨p.f, by rw [hp', p_comp p]âŸ©,
  split; simpa only [hom_ext] using hp',
end

instance [is_idempotent_complete C] : ess_surj (to_karoubi C) := âŸ¨Î» P, begin
  have h : is_idempotent_complete C := infer_instance,
  rcases is_idempotent_complete.idempotents_split P.X P.p P.idem
    with âŸ¨Y,i,e,âŸ¨hâ‚,hâ‚‚âŸ©âŸ©,
  use Y,
  exact nonempty.intro
    { hom := âŸ¨i, by erw [id_comp, â† hâ‚‚, â† assoc, hâ‚, id_comp]âŸ©,
      inv := âŸ¨e, by erw [comp_id, â† hâ‚‚, assoc, hâ‚, comp_id]âŸ©, },
endâŸ©

/-- If `C` is idempotent complete, the functor `to_karoubi : C â¥¤ karoubi C` is an equivalence. -/
def to_karoubi_is_equivalence [is_idempotent_complete C] :
  is_equivalence (to_karoubi C) :=
equivalence.of_fully_faithfully_ess_surj (to_karoubi C)

namespace karoubi

variables {C}

/-- The split mono which appears in the factorisation `decomp_id P`. -/
@[simps]
def decomp_id_i (P : karoubi C) : P âŸ¶ P.X := âŸ¨P.p, by erw [coe_p, comp_id, P.idem]âŸ©

/-- The split epi which appears in the factorisation `decomp_id P`. -/
@[simps]
def decomp_id_p (P : karoubi C) : (P.X : karoubi C) âŸ¶ P :=
âŸ¨P.p, by erw [coe_p, id_comp, P.idem]âŸ©

/-- The formal direct factor of `P.X` given by the idempotent `P.p` in the category `C`
is actually a direct factor in the category `karoubi C`. -/
lemma decomp_id (P : karoubi C) :
  ğŸ™ P = (decomp_id_i P) â‰« (decomp_id_p P) :=
by { ext, simp only [comp_f, id_eq, P.idem, decomp_id_i, decomp_id_p], }

lemma decomp_p (P : karoubi C) :
  (to_karoubi C).map P.p = (decomp_id_p P) â‰« (decomp_id_i P) :=
by { ext, simp only [comp_f, decomp_id_p_f, decomp_id_i_f, P.idem, to_karoubi_map_f], }

lemma decomp_id_i_to_karoubi (X : C) : decomp_id_i ((to_karoubi C).obj X) = ğŸ™ _ :=
by { ext, refl, }

lemma decomp_id_p_to_karoubi (X : C) : decomp_id_p ((to_karoubi C).obj X) = ğŸ™ _ :=
by { ext, refl, }

lemma decomp_id_i_naturality {P Q : karoubi C} (f : P âŸ¶ Q) : f â‰« decomp_id_i _ =
  decomp_id_i _ â‰« âŸ¨f.f, by erw [comp_id, id_comp]âŸ© :=
by { ext, simp only [comp_f, decomp_id_i_f, karoubi.comp_p, karoubi.p_comp], }

lemma decomp_id_p_naturality {P Q : karoubi C} (f : P âŸ¶ Q) : decomp_id_p P â‰« f =
  (âŸ¨f.f, by erw [comp_id, id_comp]âŸ© : (P.X : karoubi C) âŸ¶ Q.X) â‰« decomp_id_p Q :=
by { ext, simp only [comp_f, decomp_id_p_f, karoubi.comp_p, karoubi.p_comp], }

@[simp]
lemma zsmul_hom [preadditive C] {P Q : karoubi C} (n : â„¤) (f : P âŸ¶ Q) :
  (n â€¢ f).f = n â€¢ f.f :=
map_zsmul (inclusion_hom P Q) n f

end karoubi

end idempotents

end category_theory
