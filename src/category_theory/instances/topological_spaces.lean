-- Copyright (c) 2017 Scott Morrison. All rights reserved.
-- Released under Apache 2.0 license as described in the file LICENSE.
-- Authors: Patrick Massot, Scott Morrison, Mario Carneiro

import category_theory.concrete_category
import category_theory.full_subcategory
import category_theory.functor_category
import category_theory.adjunction
import category_theory.limits.types
import category_theory.natural_isomorphism
import category_theory.eq_to_hom
import topology.basic
import topology.opens
import order.galois_connection

open category_theory
open category_theory.nat_iso
open topological_space

universe u

namespace category_theory.instances

/-- The category of topological spaces and continuous maps. -/
@[reducible] def Top : Type (u+1) := bundled topological_space

instance (x : Top) : topological_space x := x.str

namespace Top
instance : concrete_category @continuous := âŸ¨@continuous_id, @continuous.compâŸ©

-- local attribute [class] continuous
-- instance {R S : Top} (f : R âŸ¶ S) : continuous (f : R â†’ S) := f.2

section
open category_theory.limits

variables {J : Type u} [small_category J]

def limit (F : J â¥¤ Top.{u}) : cone F :=
{ X := âŸ¨limit (F â‹™ forget), â¨† j, (F.obj j).str.induced (limit.Ï€ (F â‹™ forget) j)âŸ©,
  Ï€ :=
  { app := Î» j, âŸ¨limit.Ï€ (F â‹™ forget) j, continuous_iff_induced_le.mpr (lattice.le_supr _ j)âŸ©,
    naturality' := Î» j j' f, subtype.eq ((limit.cone (F â‹™ forget)).Ï€.naturality f) } }

def limit_is_limit (F : J â¥¤ Top.{u}) : is_limit (limit F) :=
by refine is_limit.of_faithful forget (limit.is_limit _) (Î» s, âŸ¨_, _âŸ©) (Î» s, rfl);
   exact continuous_iff_le_coinduced.mpr (lattice.supr_le $ Î» j,
     induced_le_iff_le_coinduced.mpr $ continuous_iff_le_coinduced.mp (s.Ï€.app j).property)

instance : has_limits.{u} Top.{u} :=
Î» J ğ’¥ F, by exactI { cone := limit F, is_limit := limit_is_limit F }

instance : preserves_limits (forget : Top.{u} â¥¤ Type u) :=
Î» J ğ’¥ F, by exactI preserves_limit_of_preserves_limit_cone
  (limit.is_limit F) (limit.is_limit (F â‹™ forget))

def colimit (F : J â¥¤ Top.{u}) : cocone F :=
{ X := âŸ¨colimit (F â‹™ forget), â¨… j, (F.obj j).str.coinduced (colimit.Î¹ (F â‹™ forget) j)âŸ©,
  Î¹ :=
  { app := Î» j, âŸ¨colimit.Î¹ (F â‹™ forget) j, continuous_iff_le_coinduced.mpr (lattice.infi_le _ j)âŸ©,
    naturality' := Î» j j' f, subtype.eq ((colimit.cocone (F â‹™ forget)).Î¹.naturality f) } }

def colimit_is_colimit (F : J â¥¤ Top.{u}) : is_colimit (colimit F) :=
by refine is_colimit.of_faithful forget (colimit.is_colimit _) (Î» s, âŸ¨_, _âŸ©) (Î» s, rfl);
   exact continuous_iff_induced_le.mpr (lattice.le_infi $ Î» j,
     induced_le_iff_le_coinduced.mpr $ continuous_iff_le_coinduced.mp (s.Î¹.app j).property)

instance : has_colimits.{u} Top.{u} :=
Î» J ğ’¥ F, by exactI { cocone := colimit F, is_colimit := colimit_is_colimit F }

instance : preserves_colimits (forget : Top.{u} â¥¤ Type u) :=
Î» J ğ’¥ F, by exactI preserves_colimit_of_preserves_colimit_cocone
  (colimit.is_colimit F) (colimit.is_colimit (F â‹™ forget))

end

def discrete : Type u â¥¤ Top.{u} :=
{ obj := Î» X, âŸ¨X, âŠ¤âŸ©,
  map := Î» X Y f, âŸ¨f, continuous_topâŸ© }

def trivial : Type u â¥¤ Top.{u} :=
{ obj := Î» X, âŸ¨X, âŠ¥âŸ©,
  map := Î» X Y f, âŸ¨f, continuous_botâŸ© }

def adjâ‚ : adjunction discrete forget :=
{ hom_equiv := Î» X Y,
  { to_fun := Î» f, f,
    inv_fun := Î» f, âŸ¨f, continuous_topâŸ©,
    left_inv := by tidy,
    right_inv := by tidy },
  unit := { app := Î» X, id },
  counit := { app := Î» X, âŸ¨id, continuous_topâŸ© } }

def adjâ‚‚ : adjunction forget trivial :=
{ hom_equiv := Î» X Y,
  { to_fun := Î» f, âŸ¨f, continuous_botâŸ©,
    inv_fun := Î» f, f,
    left_inv := by tidy,
    right_inv := by tidy },
  unit := { app := Î» X, âŸ¨id, continuous_botâŸ© },
  counit := { app := Î» X, id } }

end Top

variables {X : Top.{u}}

instance : category (opens X) := category_theory.preorder_category (opens X)

def nbhd (x : X.Î±) := { U : opens X // x âˆˆ U }
def nbhds (x : X.Î±) : category (nbhd x) := begin unfold nbhd, apply_instance end

end category_theory.instances

open category_theory.instances

namespace topological_space.opens

/-- `opens.map f` gives the functor from open sets in Y to open set in X,
    given by taking preimages under f. -/
def map
  {X Y : Top.{u}} (f : X âŸ¶ Y) : opens Y â¥¤ opens X :=
{ obj := Î» U, âŸ¨ f.val â»Â¹' U, f.property _ U.property âŸ©,
  map := Î» U V i a b, i b }.

@[simp] lemma map_id_obj (X : Top.{u}) (U : opens X) : (map (ğŸ™ X)).obj U = U := by tidy

@[simp] def map_id (X : Top.{u}) : map (ğŸ™ X) â‰… functor.id (opens X) :=
{ hom := { app := Î» U, ğŸ™ U },
  inv := { app := Î» U, ğŸ™ U } }
@[simp] def map_comp {X Y Z : Top.{u}} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : map (f â‰« g) â‰… map g â‹™ map f :=
{ hom := { app := Î» U, ğŸ™ _ },
  inv := { app := Î» U, ğŸ™ _ } }

-- We could make f g implicit here, but it's nice to be able to see when
-- they are the identity (often!)
def map_iso {X Y : Top.{u}} (f g : X âŸ¶ Y) (h : f = g) : map f â‰… map g :=
nat_iso.of_components (Î» U, eq_to_iso (congr_fun (congr_arg _ (congr_arg _ h)) _) ) (by obviously)

@[simp] def map_iso_id {X : Top.{u}} (h) : map_iso (ğŸ™ X) (ğŸ™ X) h = iso.refl (map _) := rfl

end topological_space.opens
