/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import data.list.chain
import category_theory.punit
import category_theory.groupoid

/-!
# Connected category

Define a connected category as a _nonempty_ category for which every functor
to a discrete category is isomorphic to the constant functor.

NB. Some authors include the empty category as connected, we do not.
We instead are interested in categories with exactly one 'connected
component'.

We give some equivalent definitions:
- A nonempty category for which every functor to a discrete category is
  constant on objects.
  See `any_functor_const_on_obj` and `connected.of_any_functor_const_on_obj`.
- A nonempty category for which every function `F` for which the presence of a
  morphism `f : j‚ÇÅ ‚ü∂ j‚ÇÇ` implies `F j‚ÇÅ = F j‚ÇÇ` must be constant everywhere.
  See `constant_of_preserves_morphisms` and `connected.of_constant_of_preserves_morphisms`.
- A nonempty category for which any subset of its elements containing the
  default and closed under morphisms is everything.
  See `induct_on_objects` and `connected.of_induct`.
- A nonempty category for which every object is related under the reflexive
  transitive closure of the relation "there is a morphism in some direction
  from `j‚ÇÅ` to `j‚ÇÇ`".
  See `connected_zigzag` and `zigzag_connected`.
- A nonempty category for which for any two objects there is a sequence of
  morphisms (some reversed) from one to the other.
  See `exists_zigzag'` and `connected_of_zigzag`.

We also prove the result that the functor given by `(X √ó -)` preserves any
connected limit. That is, any limit of shape `J` where `J` is a connected
category is preserved by the functor `(X √ó -)`. This appears in `category_theory.limits.connected`.
-/

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable theory

open category_theory.category
open opposite

namespace category_theory

/--
A possibly empty category for which every functor to a discrete category is constant.
-/
class is_preconnected (J : Type u‚ÇÅ) [category.{v‚ÇÅ} J] : Prop :=
(iso_constant : Œ† {Œ± : Type u‚ÇÅ} (F : J ‚•§ discrete Œ±) (j : J),
  nonempty (F ‚âÖ (functor.const J).obj (F.obj j)))

/--
We define a connected category as a _nonempty_ category for which every
functor to a discrete category is constant.

NB. Some authors include the empty category as connected, we do not.
We instead are interested in categories with exactly one 'connected
component'.

This allows us to show that the functor X ‚®Ø - preserves connected limits.

See https://stacks.math.columbia.edu/tag/002S
-/
class is_connected (J : Type u‚ÇÅ) [category.{v‚ÇÅ} J] extends is_preconnected J : Prop :=
[is_nonempty : nonempty J]

attribute [instance, priority 100] is_connected.is_nonempty

variables {J : Type u‚ÇÅ} [category.{v‚ÇÅ} J]
variables {K : Type u‚ÇÇ} [category.{v‚ÇÇ} K]

/--
If `J` is connected, any functor `F : J ‚•§ discrete Œ±` is isomorphic to
the constant functor with value `F.obj j` (for any choice of `j`).
-/
def iso_constant [is_preconnected J] {Œ± : Type u‚ÇÅ} (F : J ‚•§ discrete Œ±) (j : J) :
  F ‚âÖ (functor.const J).obj (F.obj j) :=
  (is_preconnected.iso_constant F j).some

/--
If J is connected, any functor to a discrete category is constant on objects.
The converse is given in `is_connected.of_any_functor_const_on_obj`.
-/
lemma any_functor_const_on_obj [is_preconnected J]
  {Œ± : Type u‚ÇÅ} (F : J ‚•§ discrete Œ±) (j j' : J) :
  F.obj j = F.obj j' :=
((iso_constant F j').hom.app j).down.1

/--
If any functor to a discrete category is constant on objects, J is connected.
The converse of `any_functor_const_on_obj`.
-/
lemma is_connected.of_any_functor_const_on_obj [nonempty J]
  (h : ‚àÄ {Œ± : Type u‚ÇÅ} (F : J ‚•§ discrete Œ±), ‚àÄ (j j' : J), F.obj j = F.obj j') :
  is_connected J :=
{ iso_constant := Œª Œ± F j',
  ‚ü®nat_iso.of_components (Œª j, eq_to_iso (h F j j')) (Œª _ _ _, subsingleton.elim _ _)‚ü© }

/--
If `J` is connected, then given any function `F` such that the presence of a
morphism `j‚ÇÅ ‚ü∂ j‚ÇÇ` implies `F j‚ÇÅ = F j‚ÇÇ`, we have that `F` is constant.
This can be thought of as a local-to-global property.

The converse is shown in `is_connected.of_constant_of_preserves_morphisms`
-/
lemma constant_of_preserves_morphisms [is_preconnected J] {Œ± : Type u‚ÇÅ} (F : J ‚Üí Œ±)
  (h : ‚àÄ (j‚ÇÅ j‚ÇÇ : J) (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), F j‚ÇÅ = F j‚ÇÇ) (j j' : J) :
  F j = F j' :=
any_functor_const_on_obj { obj := F, map := Œª _ _ f, eq_to_hom (h _ _ f) } j j'

/--
`J` is connected if: given any function `F : J ‚Üí Œ±` which is constant for any
`j‚ÇÅ, j‚ÇÇ` for which there is a morphism `j‚ÇÅ ‚ü∂ j‚ÇÇ`, then `F` is constant.
This can be thought of as a local-to-global property.

The converse of `constant_of_preserves_morphisms`.
-/
lemma is_connected.of_constant_of_preserves_morphisms [nonempty J]
  (h : ‚àÄ {Œ± : Type u‚ÇÅ} (F : J ‚Üí Œ±), (‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), F j‚ÇÅ = F j‚ÇÇ) ‚Üí
    (‚àÄ j j' : J, F j = F j')) :
  is_connected J :=
is_connected.of_any_functor_const_on_obj (Œª _ F, h F.obj (Œª _ _ f, (F.map f).down.1))

/--
An inductive-like property for the objects of a connected category.
If the set `p` is nonempty, and `p` is closed under morphisms of `J`,
then `p` contains all of `J`.

The converse is given in `is_connected.of_induct`.
-/
lemma induct_on_objects [is_preconnected J] (p : set J) {j‚ÇÄ : J} (h0 : j‚ÇÄ ‚àà p)
  (h1 : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p) (j : J) :
  j ‚àà p :=
begin
  injection (constant_of_preserves_morphisms (Œª k, ulift.up (k ‚àà p)) (Œª j‚ÇÅ j‚ÇÇ f, _) j j‚ÇÄ) with i,
  rwa i,
  dsimp,
  exact congr_arg ulift.up (propext (h1 f)),
end

/--
If any maximal connected component containing some element j‚ÇÄ of J is all of J, then J is connected.

The converse of `induct_on_objects`.
-/
lemma is_connected.of_induct [nonempty J] {j‚ÇÄ : J}
  (h : ‚àÄ (p : set J), j‚ÇÄ ‚àà p ‚Üí (‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p) ‚Üí ‚àÄ (j : J), j ‚àà p) :
  is_connected J :=
is_connected.of_constant_of_preserves_morphisms (Œª Œ± F a,
begin
  have w := h {j | F j = F j‚ÇÄ} rfl (Œª _ _ f, by simp [a f]),
  dsimp at w,
  intros j j',
  rw [w j, w j'],
end)

/--
Another induction principle for `is_preconnected J`:
given a type family `Z : J ‚Üí Sort*` and
a rule for transporting in *both* directions along a morphism in `J`,
we can transport an `x : Z j‚ÇÄ` to a point in `Z j` for any `j`.
-/
lemma is_preconnected_induction [is_preconnected J] (Z : J ‚Üí Sort*)
  (h‚ÇÅ : Œ† {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), Z j‚ÇÅ ‚Üí Z j‚ÇÇ)
  (h‚ÇÇ : Œ† {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), Z j‚ÇÇ ‚Üí Z j‚ÇÅ)
  {j‚ÇÄ : J} (x : Z j‚ÇÄ) (j : J) : nonempty (Z j) :=
(induct_on_objects {j | nonempty (Z j)} ‚ü®x‚ü©
  (Œª j‚ÇÅ j‚ÇÇ f, ‚ü®by { rintro ‚ü®y‚ü©, exact ‚ü®h‚ÇÅ f y‚ü©, }, by { rintro ‚ü®y‚ü©, exact ‚ü®h‚ÇÇ f y‚ü©, }‚ü©) j : _)

/-- If `J` and `K` are equivalent, then if `J` is preconnected then `K` is as well. -/
lemma is_preconnected_of_equivalent {K : Type u‚ÇÅ} [category.{v‚ÇÇ} K] [is_preconnected J]
  (e : J ‚âå K) :
  is_preconnected K :=
{ iso_constant := Œª Œ± F k, ‚ü®
  calc F ‚âÖ e.inverse ‚ãô e.functor ‚ãô F : (e.inv_fun_id_assoc F).symm
     ... ‚âÖ e.inverse ‚ãô (functor.const J).obj ((e.functor ‚ãô F).obj (e.inverse.obj k)) :
                       iso_whisker_left e.inverse (iso_constant (e.functor ‚ãô F) (e.inverse.obj k))

     ... ‚âÖ e.inverse ‚ãô (functor.const J).obj (F.obj k) :
          iso_whisker_left _ ((F ‚ãô functor.const J).map_iso (e.counit_iso.app k))
     ... ‚âÖ (functor.const K).obj (F.obj k) : nat_iso.of_components (Œª X, iso.refl _) (by simp),
  ‚ü© }

/-- If `J` and `K` are equivalent, then if `J` is connected then `K` is as well. -/
lemma is_connected_of_equivalent {K : Type u‚ÇÅ} [category.{v‚ÇÇ} K]
  (e : J ‚âå K) [is_connected J] :
  is_connected K :=
{ is_nonempty := nonempty.map e.functor.obj (by apply_instance),
  to_is_preconnected := is_preconnected_of_equivalent e }

/-- If `J` is preconnected, then `J·µí·µñ` is preconnected as well. -/
instance is_preconnected_op [is_preconnected J] : is_preconnected J·µí·µñ :=
{ iso_constant := Œª Œ± F X, ‚ü®
    nat_iso.of_components
      (Œª Y, (nonempty.some $ is_preconnected.iso_constant
        (F.right_op ‚ãô (discrete.opposite Œ±).functor) (unop X)).app (unop Y))
      (Œª Y Z f, subsingleton.elim _ _)
  ‚ü© }

/-- If `J` is connected, then `J·µí·µñ` is connected as well. -/
instance is_connected_op [is_connected J] : is_connected J·µí·µñ :=
{ is_nonempty := nonempty.intro (op (classical.arbitrary J)) }

lemma is_preconnected_of_is_preconnected_op [is_preconnected J·µí·µñ] : is_preconnected J :=
is_preconnected_of_equivalent (op_op_equivalence J)

lemma is_connected_of_is_connected_op [is_connected J·µí·µñ] : is_connected J :=
is_connected_of_equivalent (op_op_equivalence J)

/-- j‚ÇÅ and j‚ÇÇ are related by `zag` if there is a morphism between them. -/
@[reducible]
def zag (j‚ÇÅ j‚ÇÇ : J) : Prop := nonempty (j‚ÇÅ ‚ü∂ j‚ÇÇ) ‚à® nonempty (j‚ÇÇ ‚ü∂ j‚ÇÅ)

lemma zag_symmetric : symmetric (@zag J _) :=
Œª j‚ÇÇ j‚ÇÅ h, h.swap

/--
`j‚ÇÅ` and `j‚ÇÇ` are related by `zigzag` if there is a chain of
morphisms from `j‚ÇÅ` to `j‚ÇÇ`, with backward morphisms allowed.
-/
@[reducible]
def zigzag : J ‚Üí J ‚Üí Prop := relation.refl_trans_gen zag

lemma zigzag_symmetric : symmetric (@zigzag J _) :=
relation.refl_trans_gen.symmetric zag_symmetric

lemma zigzag_equivalence : _root_.equivalence (@zigzag J _) :=
mk_equivalence _
    relation.reflexive_refl_trans_gen
    zigzag_symmetric
    relation.transitive_refl_trans_gen

/--
The setoid given by the equivalence relation `zigzag`. A quotient for this
setoid is a connected component of the category.
-/
def zigzag.setoid (J : Type u‚ÇÇ) [category.{v‚ÇÅ} J] : setoid J :=
{ r := zigzag,
  iseqv := zigzag_equivalence }

/--
If there is a zigzag from `j‚ÇÅ` to `j‚ÇÇ`, then there is a zigzag from `F j‚ÇÅ` to
`F j‚ÇÇ` as long as `F` is a functor.
-/
lemma zigzag_obj_of_zigzag (F : J ‚•§ K) {j‚ÇÅ j‚ÇÇ : J} (h : zigzag j‚ÇÅ j‚ÇÇ) :
  zigzag (F.obj j‚ÇÅ) (F.obj j‚ÇÇ) :=
h.lift _ $ Œª j k, or.imp (nonempty.map (Œª f, F.map f)) (nonempty.map (Œª f, F.map f))

-- TODO: figure out the right way to generalise this to `zigzag`.
lemma zag_of_zag_obj (F : J ‚•§ K) [full F] {j‚ÇÅ j‚ÇÇ : J} (h : zag (F.obj j‚ÇÅ) (F.obj j‚ÇÇ)) :
  zag j‚ÇÅ j‚ÇÇ :=
or.imp (nonempty.map F.preimage) (nonempty.map F.preimage) h

/-- Any equivalence relation containing (‚ü∂) holds for all pairs of a connected category. -/
lemma equiv_relation [is_connected J] (r : J ‚Üí J ‚Üí Prop) (hr : _root_.equivalence r)
  (h : ‚àÄ {j‚ÇÅ j‚ÇÇ : J} (f : j‚ÇÅ ‚ü∂ j‚ÇÇ), r j‚ÇÅ j‚ÇÇ) :
  ‚àÄ (j‚ÇÅ j‚ÇÇ : J), r j‚ÇÅ j‚ÇÇ :=
begin
  have z : ‚àÄ (j : J), r (classical.arbitrary J) j :=
    induct_on_objects (Œª k, r (classical.arbitrary J) k)
      (hr.1 (classical.arbitrary J)) (Œª _ _ f, ‚ü®Œª t, hr.2.2 t (h f), Œª t, hr.2.2 t (hr.2.1 (h f))‚ü©),
  intros, apply hr.2.2 (hr.2.1 (z _)) (z _)
end

/-- In a connected category, any two objects are related by `zigzag`. -/
lemma is_connected_zigzag [is_connected J] (j‚ÇÅ j‚ÇÇ : J) : zigzag j‚ÇÅ j‚ÇÇ :=
equiv_relation _ zigzag_equivalence
  (Œª _ _ f, relation.refl_trans_gen.single (or.inl (nonempty.intro f))) _ _

/--
If any two objects in an nonempty category are related by `zigzag`, the category is connected.
-/
lemma zigzag_is_connected [nonempty J] (h : ‚àÄ (j‚ÇÅ j‚ÇÇ : J), zigzag j‚ÇÅ j‚ÇÇ) : is_connected J :=
begin
  apply is_connected.of_induct,
  intros p hp hjp j,
  have: ‚àÄ (j‚ÇÅ j‚ÇÇ : J), zigzag j‚ÇÅ j‚ÇÇ ‚Üí (j‚ÇÅ ‚àà p ‚Üî j‚ÇÇ ‚àà p),
  { introv k,
    induction k with _ _ rt_zag zag,
    { refl },
    { rw k_ih,
      rcases zag with ‚ü®‚ü®_‚ü©‚ü© | ‚ü®‚ü®_‚ü©‚ü©,
      apply hjp zag,
      apply (hjp zag).symm } },
  rwa this j (classical.arbitrary J) (h _ _)
end

lemma exists_zigzag' [is_connected J] (j‚ÇÅ j‚ÇÇ : J) :
  ‚àÉ l, list.chain zag j‚ÇÅ l ‚àß list.last (j‚ÇÅ :: l) (list.cons_ne_nil _ _) = j‚ÇÇ :=
list.exists_chain_of_relation_refl_trans_gen (is_connected_zigzag _ _)

/--
If any two objects in an nonempty category are linked by a sequence of (potentially reversed)
morphisms, then J is connected.

The converse of `exists_zigzag'`.
-/
lemma is_connected_of_zigzag [nonempty J]
  (h : ‚àÄ (j‚ÇÅ j‚ÇÇ : J), ‚àÉ l, list.chain zag j‚ÇÅ l ‚àß list.last (j‚ÇÅ :: l) (list.cons_ne_nil _ _) = j‚ÇÇ) :
  is_connected J :=
begin
  apply zigzag_is_connected,
  intros j‚ÇÅ j‚ÇÇ,
  rcases h j‚ÇÅ j‚ÇÇ with ‚ü®l, hl‚ÇÅ, hl‚ÇÇ‚ü©,
  apply list.relation_refl_trans_gen_of_exists_chain l hl‚ÇÅ hl‚ÇÇ,
end

/-- If `discrete Œ±` is connected, then `Œ±` is (type-)equivalent to `punit`. -/
def discrete_is_connected_equiv_punit {Œ± : Type u‚ÇÅ} [is_connected (discrete Œ±)] : Œ± ‚âÉ punit :=
discrete.equiv_of_equivalence
  { functor := functor.star Œ±,
    inverse := discrete.functor (Œª _, classical.arbitrary _),
    unit_iso := by { exact (iso_constant _ (classical.arbitrary _)), },
    counit_iso := functor.punit_ext _ _ }

variables {C : Type u‚ÇÇ} [category.{u‚ÇÅ} C]

/--
For objects `X Y : C`, any natural transformation `Œ± : const X ‚ü∂ const Y` from a connected
category must be constant.
This is the key property of connected categories which we use to establish properties about limits.
-/
lemma nat_trans_from_is_connected [is_preconnected J] {X Y : C}
  (Œ± : (functor.const J).obj X ‚ü∂ (functor.const J).obj Y) :
  ‚àÄ (j j' : J), Œ±.app j = (Œ±.app j' : X ‚ü∂ Y) :=
@constant_of_preserves_morphisms _ _ _
  (X ‚ü∂ Y)
  (Œª j, Œ±.app j)
  (Œª _ _ f, (by { have := Œ±.naturality f, erw [id_comp, comp_id] at this, exact this.symm }))

instance [is_connected J] : full (functor.const J : C ‚•§ J ‚•§ C) :=
{ preimage := Œª X Y f, f.app (classical.arbitrary J),
  witness' := Œª X Y f,
  begin
    ext j,
    apply nat_trans_from_is_connected f (classical.arbitrary J) j,
  end }

instance nonempty_hom_of_connected_groupoid {G} [groupoid G] [is_connected G] :
  ‚àÄ (x y : G), nonempty (x ‚ü∂ y) :=
begin
  refine equiv_relation _ _ (Œª j‚ÇÅ j‚ÇÇ, nonempty.intro),
  exact ‚ü®Œª j, ‚ü®ùüô _‚ü©, Œª j‚ÇÅ j‚ÇÇ, nonempty.map (Œª f, inv f), Œª _ _ _, nonempty.map2 (‚â´)‚ü©,
end

end category_theory
