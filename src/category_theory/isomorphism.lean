/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import category_theory.functor

universes v u -- declare the `v`'s first; see `category_theory.category` for an explanation

namespace category_theory
open category

structure iso {C : Type u} [category.{v} C] (X Y : C) :=
(hom : X âŸ¶ Y)
(inv : Y âŸ¶ X)
(hom_inv_id' : hom â‰« inv = ğŸ™ X . obviously)
(inv_hom_id' : inv â‰« hom = ğŸ™ Y . obviously)

restate_axiom iso.hom_inv_id'
restate_axiom iso.inv_hom_id'
attribute [simp] iso.hom_inv_id iso.inv_hom_id

infixr ` â‰… `:10  := iso             -- type as \cong or \iso

variables {C : Type u} [ğ’ : category.{v} C]
include ğ’
variables {X Y Z : C}

namespace iso

@[simp] lemma hom_inv_id_assoc (Î± : X â‰… Y) (f : X âŸ¶ Z) : Î±.hom â‰« Î±.inv â‰« f = f :=
by rw [â†category.assoc, Î±.hom_inv_id, category.id_comp]

@[simp] lemma inv_hom_id_assoc (Î± : X â‰… Y) (f : Y âŸ¶ Z) : Î±.inv â‰« Î±.hom â‰« f = f :=
by rw [â†category.assoc, Î±.inv_hom_id, category.id_comp]

@[extensionality] lemma ext (Î± Î² : X â‰… Y) (w : Î±.hom = Î².hom) : Î± = Î² :=
suffices Î±.inv = Î².inv, by cases Î±; cases Î²; cc,
calc Î±.inv
    = Î±.inv â‰« (Î².hom â‰« Î².inv) : by rw [iso.hom_inv_id, category.comp_id]
... = (Î±.inv â‰« Î±.hom) â‰« Î².inv : by rw [category.assoc, â†w]
... = Î².inv                   : by rw [iso.inv_hom_id, category.id_comp]

@[symm] def symm (I : X â‰… Y) : Y â‰… X :=
{ hom := I.inv,
  inv := I.hom,
  hom_inv_id' := I.inv_hom_id',
  inv_hom_id' := I.hom_inv_id' }

@[simp] lemma symm_hom (Î± : X â‰… Y) : Î±.symm.hom = Î±.inv := rfl
@[simp] lemma symm_inv (Î± : X â‰… Y) : Î±.symm.inv = Î±.hom := rfl

@[simp] lemma symm_mk {X Y : C} (hom : X âŸ¶ Y) (inv : Y âŸ¶ X) (hom_inv_id) (inv_hom_id) :
  iso.symm {hom := hom, inv := inv, hom_inv_id' := hom_inv_id, inv_hom_id' := inv_hom_id} =
    {hom := inv, inv := hom, hom_inv_id' := inv_hom_id, inv_hom_id' := hom_inv_id} := rfl

@[refl] def refl (X : C) : X â‰… X :=
{ hom := ğŸ™ X,
  inv := ğŸ™ X }

@[simp] lemma refl_hom (X : C) : (iso.refl X).hom = ğŸ™ X := rfl
@[simp] lemma refl_inv (X : C) : (iso.refl X).inv = ğŸ™ X := rfl

@[trans] def trans (Î± : X â‰… Y) (Î² : Y â‰… Z) : X â‰… Z :=
{ hom := Î±.hom â‰« Î².hom,
  inv := Î².inv â‰« Î±.inv }

infixr ` â‰ªâ‰« `:80 := iso.trans -- type as `\ll \gg`.

@[simp] lemma trans_hom (Î± : X â‰… Y) (Î² : Y â‰… Z) : (Î± â‰ªâ‰« Î²).hom = Î±.hom â‰« Î².hom := rfl
@[simp] lemma trans_inv (Î± : X â‰… Y) (Î² : Y â‰… Z) : (Î± â‰ªâ‰« Î²).inv = Î².inv â‰« Î±.inv := rfl

@[simp] lemma trans_mk {X Y Z : C}
  (hom : X âŸ¶ Y) (inv : Y âŸ¶ X) (hom_inv_id) (inv_hom_id)
  (hom' : Y âŸ¶ Z) (inv' : Z âŸ¶ Y) (hom_inv_id') (inv_hom_id') (hom_inv_id'') (inv_hom_id'') :
  iso.trans
    {hom := hom, inv := inv, hom_inv_id' := hom_inv_id, inv_hom_id' := inv_hom_id}
    {hom := hom', inv := inv', hom_inv_id' := hom_inv_id', inv_hom_id' := inv_hom_id'} =
  {hom := hom â‰« hom', inv := inv' â‰« inv, hom_inv_id' := hom_inv_id'', inv_hom_id' := inv_hom_id''} :=
rfl

@[simp] lemma refl_symm (X : C) : (iso.refl X).hom = ğŸ™ X := rfl
@[simp] lemma trans_symm (Î± : X â‰… Y) (Î² : Y â‰… Z) : (Î± â‰ªâ‰« Î²).inv = Î².inv â‰« Î±.inv := rfl

lemma inv_comp_eq (Î± : X â‰… Y) {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : Î±.inv â‰« f = g â†” f = Î±.hom â‰« g :=
âŸ¨Î» H, by simp [H.symm], Î» H, by simp [H]âŸ©

lemma eq_inv_comp (Î± : X â‰… Y) {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : g = Î±.inv â‰« f â†” Î±.hom â‰« g = f :=
(inv_comp_eq Î±.symm).symm

lemma comp_inv_eq (Î± : X â‰… Y) {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : f â‰« Î±.inv = g â†” f = g â‰« Î±.hom :=
âŸ¨Î» H, by simp [H.symm], Î» H, by simp [H]âŸ©

lemma eq_comp_inv (Î± : X â‰… Y) {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : g = f â‰« Î±.inv â†” g â‰« Î±.hom = f :=
(comp_inv_eq Î±.symm).symm

lemma inv_eq_inv (f g : X â‰… Y) : f.inv = g.inv â†” f.hom = g.hom :=
have âˆ€{X Y : C} (f g : X â‰… Y), f.hom = g.hom â†’ f.inv = g.inv, from Î» X Y f g h, by rw [ext _ _ h],
âŸ¨this f.symm g.symm, this f gâŸ©

lemma hom_comp_eq_id (Î± : X â‰… Y) {f : Y âŸ¶ X} : Î±.hom â‰« f = ğŸ™ X â†” f = Î±.inv :=
by rw [â†eq_inv_comp, comp_id]

lemma comp_hom_eq_id (Î± : X â‰… Y) {f : Y âŸ¶ X} : f â‰« Î±.hom = ğŸ™ Y â†” f = Î±.inv :=
by rw [â†eq_comp_inv, id_comp]

lemma hom_eq_inv (Î± : X â‰… Y) (Î² : Y â‰… X) : Î±.hom = Î².inv â†” Î².hom = Î±.inv :=
by { erw [inv_eq_inv Î±.symm Î², eq_comm], refl }

end iso

/-- `is_iso` typeclass expressing that a morphism is invertible.
    This contains the data of the inverse, but is a subsingleton type. -/
class is_iso (f : X âŸ¶ Y) :=
(inv : Y âŸ¶ X)
(hom_inv_id' : f â‰« inv = ğŸ™ X . obviously)
(inv_hom_id' : inv â‰« f = ğŸ™ Y . obviously)

def inv (f : X âŸ¶ Y) [is_iso f] := is_iso.inv f

namespace is_iso

@[simp] lemma hom_inv_id (f : X âŸ¶ Y) [is_iso f] : f â‰« category_theory.inv f = ğŸ™ X :=
is_iso.hom_inv_id' f
@[simp] lemma inv_hom_id (f : X âŸ¶ Y) [is_iso f] : category_theory.inv f â‰« f = ğŸ™ Y :=
is_iso.inv_hom_id' f

@[simp] lemma hom_inv_id_assoc {Z} (f : X âŸ¶ Y) [is_iso f] (g : X âŸ¶ Z) : f â‰« category_theory.inv f â‰« g = g :=
by rw [â†category.assoc, hom_inv_id, category.id_comp]
@[simp] lemma inv_hom_id_assoc {Z} (f : X âŸ¶ Y) [is_iso f] (g : Y âŸ¶ Z) : category_theory.inv f â‰« f â‰« g = g :=
by rw [â†category.assoc, inv_hom_id, category.id_comp]

instance (X : C) : is_iso (ğŸ™ X) :=
{ inv := ğŸ™ X }

instance of_iso (f : X â‰… Y) : is_iso f.hom :=
{ inv := f.inv }
instance of_iso_inverse (f : X â‰… Y) : is_iso f.inv :=
{ inv := f.hom }

variables {f g : X âŸ¶ Y} {h : Y âŸ¶ Z}

instance inv_is_iso [is_iso f] : is_iso (category_theory.inv f) :=
{ inv := f,
  hom_inv_id' := inv_hom_id f,
  inv_hom_id' := hom_inv_id f }
instance comp_is_iso [is_iso f] [is_iso h] : is_iso (f â‰« h) :=
{ inv := category_theory.inv h â‰« category_theory.inv f,
  hom_inv_id' := begin erw [category.assoc, hom_inv_id_assoc], exact hom_inv_id f, end,
  inv_hom_id' := begin erw [category.assoc, inv_hom_id_assoc], exact inv_hom_id h, end }

@[simp] lemma inv_id : category_theory.inv (ğŸ™ X) = ğŸ™ X := rfl
@[simp] lemma inv_comp [is_iso f] [is_iso h] :
  category_theory.inv (f â‰« h) = category_theory.inv h â‰« category_theory.inv f := rfl
@[simp] lemma is_iso.inv_inv [is_iso f] : category_theory.inv (category_theory.inv f) = f := rfl
@[simp] lemma iso.inv_inv (f : X â‰… Y) :
  category_theory.inv (f.inv) = f.hom := rfl
@[simp] lemma iso.inv_hom (f : X â‰… Y) :
  category_theory.inv (f.hom) = f.inv := rfl

instance epi_of_iso (f : X âŸ¶ Y) [is_iso f] : epi f  :=
{ left_cancellation := Î» Z g h w,
  -- This is an interesting test case for better rewrite automation.
  by rw [â†category.id_comp C g, â†category.id_comp C h, â†is_iso.inv_hom_id f, category.assoc, w, category.assoc] }
instance mono_of_iso (f : X âŸ¶ Y) [is_iso f] : mono f :=
{ right_cancellation := Î» Z g h w,
  by rw [â†category.comp_id C g, â†category.comp_id C h, â†is_iso.hom_inv_id f, â†category.assoc, w, â†category.assoc] }

end is_iso

open is_iso

lemma eq_of_inv_eq_inv {f g : X âŸ¶ Y} [is_iso f] [is_iso g] (p : inv f = inv g) : f = g :=
begin
  apply (cancel_epi (inv f)).1,
  erw [inv_hom_id, p, inv_hom_id],
end

def as_iso (f : X âŸ¶ Y) [is_iso f] : X â‰… Y :=
{ hom := f, inv := inv f }

@[simp] lemma as_iso_hom (f : X âŸ¶ Y) [is_iso f] : (as_iso f).hom = f := rfl
@[simp] lemma as_iso_inv (f : X âŸ¶ Y) [is_iso f] : (as_iso f).inv = inv f := rfl

instance (f : X âŸ¶ Y) : subsingleton (is_iso f) :=
âŸ¨Î» a b,
 suffices a.inv = b.inv, by cases a; cases b; congr; exact this,
 show (@as_iso C _ _ _ f a).inv = (@as_iso C _ _ _ f b).inv,
 by congr' 1; ext; reflâŸ©

lemma is_iso.inv_eq_inv {f g : X âŸ¶ Y} [is_iso f] [is_iso g] : inv f = inv g â†” f = g :=
iso.inv_eq_inv (as_iso f) (as_iso g)

instance is_iso_comp (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [is_iso f] [is_iso g] : is_iso (f â‰« g) :=
{ inv := inv g â‰« inv f }

instance is_iso_id : is_iso (ğŸ™ X) := { inv := ğŸ™ X }

namespace functor

universes uâ‚ vâ‚ uâ‚‚ vâ‚‚
variables {D : Type uâ‚‚}

variables [ğ’Ÿ : category.{vâ‚‚} D]
include ğ’Ÿ

def map_iso (F : C â¥¤ D) {X Y : C} (i : X â‰… Y) : F.obj X â‰… F.obj Y :=
{ hom := F.map i.hom,
  inv := F.map i.inv,
  hom_inv_id' := by rw [â†map_comp, iso.hom_inv_id, â†map_id],
  inv_hom_id' := by rw [â†map_comp, iso.inv_hom_id, â†map_id] }

@[simp] lemma map_iso_hom (F : C â¥¤ D) {X Y : C} (i : X â‰… Y) : (F.map_iso i).hom = F.map i.hom := rfl
@[simp] lemma map_iso_inv (F : C â¥¤ D) {X Y : C} (i : X â‰… Y) : (F.map_iso i).inv = F.map i.inv := rfl

@[simp] lemma map_iso_trans (F : C â¥¤ D) {X Y Z : C} (i : X â‰… Y) (j : Y â‰… Z) :
  F.map_iso (i â‰ªâ‰« j) = (F.map_iso i) â‰ªâ‰« (F.map_iso j) :=
by ext; apply functor.map_comp

instance (F : C â¥¤ D) (f : X âŸ¶ Y) [is_iso f] : is_iso (F.map f) :=
{ ..(F.map_iso (as_iso f)) }

@[simp] lemma map_hom_inv (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [is_iso f] :
  F.map f â‰« F.map (inv f) = ğŸ™ (F.obj X) :=
by rw [â†map_comp, is_iso.hom_inv_id, map_id]

@[simp] lemma map_inv_hom (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [is_iso f] :
  F.map (inv f) â‰« F.map f = ğŸ™ (F.obj Y) :=
by rw [â†map_comp, is_iso.inv_hom_id, map_id]

@[simp] lemma map_inv (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [is_iso f] : F.map (inv f) = inv (F.map f) := rfl

end functor

end category_theory
