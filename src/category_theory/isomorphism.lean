/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import category_theory.functor

/-!
# Isomorphisms

This file defines isomorphisms between objects of a category.

## Main definitions

- `structure iso` : a bundled isomorphism between two objects of a category;
- `class is_iso` : an unbundled version of `iso`;
  note that `is_iso f` is a `Prop`, and only asserts the existence of an inverse.
  Of course, this inverse is unique, so it doesn't cost us much to use choice to retrieve it.
- `inv f`, for the inverse of a morphism with `[is_iso f]`
- `as_iso` : convert from `is_iso` to `iso` (noncomputable);
- `of_iso` : convert from `iso` to `is_iso`;
- standard operations on isomorphisms (composition, inverse etc)

## Notations

- `X â‰… Y` : same as `iso X Y`;
- `Î± â‰ªâ‰« Î²` : composition of two isomorphisms; it is called `iso.trans`

## Tags

category, category theory, isomorphism
-/

universes v u -- morphism levels before object levels. See note [category_theory universes].

namespace category_theory
open category

/--
An isomorphism (a.k.a. an invertible morphism) between two objects of a category.
The inverse morphism is bundled.

See also `category_theory.core` for the category with the same objects and isomorphisms playing
the role of morphisms.

See https://stacks.math.columbia.edu/tag/0017.
-/
structure iso {C : Type u} [category.{v} C] (X Y : C) :=
(hom : X âŸ¶ Y)
(inv : Y âŸ¶ X)
(hom_inv_id' : hom â‰« inv = ğŸ™ X . obviously)
(inv_hom_id' : inv â‰« hom = ğŸ™ Y . obviously)

restate_axiom iso.hom_inv_id'
restate_axiom iso.inv_hom_id'
attribute [simp, reassoc] iso.hom_inv_id iso.inv_hom_id

infixr ` â‰… `:10  := iso             -- type as \cong or \iso

variables {C : Type u} [category.{v} C]
variables {X Y Z : C}

namespace iso

@[ext] lemma ext â¦ƒÎ± Î² : X â‰… Yâ¦„ (w : Î±.hom = Î².hom) : Î± = Î² :=
suffices Î±.inv = Î².inv, by cases Î±; cases Î²; cc,
calc Î±.inv
    = Î±.inv â‰« (Î².hom â‰« Î².inv) : by rw [iso.hom_inv_id, category.comp_id]
... = (Î±.inv â‰« Î±.hom) â‰« Î².inv : by rw [category.assoc, â†w]
... = Î².inv                   : by rw [iso.inv_hom_id, category.id_comp]

/-- Inverse isomorphism. -/
@[symm] def symm (I : X â‰… Y) : Y â‰… X :=
{ hom := I.inv,
  inv := I.hom,
  hom_inv_id' := I.inv_hom_id',
  inv_hom_id' := I.hom_inv_id' }

@[simp] lemma symm_hom (Î± : X â‰… Y) : Î±.symm.hom = Î±.inv := rfl
@[simp] lemma symm_inv (Î± : X â‰… Y) : Î±.symm.inv = Î±.hom := rfl

@[simp] lemma symm_mk {X Y : C} (hom : X âŸ¶ Y) (inv : Y âŸ¶ X) (hom_inv_id) (inv_hom_id) :
  iso.symm {hom := hom, inv := inv, hom_inv_id' := hom_inv_id, inv_hom_id' := inv_hom_id} =
    {hom := inv, inv := hom, hom_inv_id' := inv_hom_id, inv_hom_id' := hom_inv_id} := rfl

@[simp] lemma symm_symm_eq {X Y : C} (Î± : X â‰… Y) : Î±.symm.symm = Î± :=
by cases Î±; refl

@[simp] lemma symm_eq_iff {X Y : C} {Î± Î² : X â‰… Y} : Î±.symm = Î².symm â†” Î± = Î² :=
âŸ¨Î» h, symm_symm_eq Î± â–¸ symm_symm_eq Î² â–¸ congr_arg symm h, congr_arg symmâŸ©

/-- Identity isomorphism. -/
@[refl, simps] def refl (X : C) : X â‰… X :=
{ hom := ğŸ™ X,
  inv := ğŸ™ X }

instance : inhabited (X â‰… X) := âŸ¨iso.refl XâŸ©

@[simp] lemma refl_symm (X : C) : (iso.refl X).symm = iso.refl X := rfl

/-- Composition of two isomorphisms -/
@[trans, simps] def trans (Î± : X â‰… Y) (Î² : Y â‰… Z) : X â‰… Z :=
{ hom := Î±.hom â‰« Î².hom,
  inv := Î².inv â‰« Î±.inv }

infixr ` â‰ªâ‰« `:80 := iso.trans -- type as `\ll \gg`.

@[simp] lemma trans_mk {X Y Z : C}
  (hom : X âŸ¶ Y) (inv : Y âŸ¶ X) (hom_inv_id) (inv_hom_id)
  (hom' : Y âŸ¶ Z) (inv' : Z âŸ¶ Y) (hom_inv_id') (inv_hom_id') (hom_inv_id'') (inv_hom_id'') :
  iso.trans
    {hom := hom, inv := inv, hom_inv_id' := hom_inv_id, inv_hom_id' := inv_hom_id}
    {hom := hom', inv := inv', hom_inv_id' := hom_inv_id', inv_hom_id' := inv_hom_id'} =
  { hom := hom â‰« hom', inv := inv' â‰« inv, hom_inv_id' := hom_inv_id'',
    inv_hom_id' := inv_hom_id''} :=
rfl

@[simp] lemma trans_symm (Î± : X â‰… Y) (Î² : Y â‰… Z) : (Î± â‰ªâ‰« Î²).symm = Î².symm â‰ªâ‰« Î±.symm := rfl
@[simp] lemma trans_assoc {Z' : C} (Î± : X â‰… Y) (Î² : Y â‰… Z) (Î³ : Z â‰… Z') :
  (Î± â‰ªâ‰« Î²) â‰ªâ‰« Î³ = Î± â‰ªâ‰« Î² â‰ªâ‰« Î³ :=
by ext; simp only [trans_hom, category.assoc]

@[simp] lemma refl_trans (Î± : X â‰… Y) : (iso.refl X) â‰ªâ‰« Î± = Î± := by ext; apply category.id_comp
@[simp] lemma trans_refl (Î± : X â‰… Y) : Î± â‰ªâ‰« (iso.refl Y) = Î± := by ext; apply category.comp_id

@[simp] lemma symm_self_id (Î± : X â‰… Y) : Î±.symm â‰ªâ‰« Î± = iso.refl Y := ext Î±.inv_hom_id
@[simp] lemma self_symm_id (Î± : X â‰… Y) : Î± â‰ªâ‰« Î±.symm = iso.refl X := ext Î±.hom_inv_id

@[simp] lemma symm_self_id_assoc (Î± : X â‰… Y) (Î² : Y â‰… Z) : Î±.symm â‰ªâ‰« Î± â‰ªâ‰« Î² = Î² :=
by rw [â† trans_assoc, symm_self_id, refl_trans]

@[simp] lemma self_symm_id_assoc (Î± : X â‰… Y) (Î² : X â‰… Z) : Î± â‰ªâ‰« Î±.symm â‰ªâ‰« Î² = Î² :=
by rw [â† trans_assoc, self_symm_id, refl_trans]

lemma inv_comp_eq (Î± : X â‰… Y) {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : Î±.inv â‰« f = g â†” f = Î±.hom â‰« g :=
âŸ¨Î» H, by simp [H.symm], Î» H, by simp [H]âŸ©

lemma eq_inv_comp (Î± : X â‰… Y) {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : g = Î±.inv â‰« f â†” Î±.hom â‰« g = f :=
(inv_comp_eq Î±.symm).symm

lemma comp_inv_eq (Î± : X â‰… Y) {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : f â‰« Î±.inv = g â†” f = g â‰« Î±.hom :=
âŸ¨Î» H, by simp [H.symm], Î» H, by simp [H]âŸ©

lemma eq_comp_inv (Î± : X â‰… Y) {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : g = f â‰« Î±.inv â†” g â‰« Î±.hom = f :=
(comp_inv_eq Î±.symm).symm

lemma inv_eq_inv (f g : X â‰… Y) : f.inv = g.inv â†” f.hom = g.hom :=
have âˆ€{X Y : C} (f g : X â‰… Y), f.hom = g.hom â†’ f.inv = g.inv, from Î» X Y f g h, by rw [ext h],
âŸ¨this f.symm g.symm, this f gâŸ©

lemma hom_comp_eq_id (Î± : X â‰… Y) {f : Y âŸ¶ X} : Î±.hom â‰« f = ğŸ™ X â†” f = Î±.inv :=
by rw [â†eq_inv_comp, comp_id]

lemma comp_hom_eq_id (Î± : X â‰… Y) {f : Y âŸ¶ X} : f â‰« Î±.hom = ğŸ™ Y â†” f = Î±.inv :=
by rw [â†eq_comp_inv, id_comp]

lemma hom_eq_inv (Î± : X â‰… Y) (Î² : Y â‰… X) : Î±.hom = Î².inv â†” Î².hom = Î±.inv :=
by { erw [inv_eq_inv Î±.symm Î², eq_comm], refl }

end iso

/-- `is_iso` typeclass expressing that a morphism is invertible. -/
class is_iso (f : X âŸ¶ Y) : Prop :=
(out : âˆƒ inv : Y âŸ¶ X, f â‰« inv = ğŸ™ X âˆ§ inv â‰« f = ğŸ™ Y)

/--
The inverse of a morphism `f` when we have `[is_iso f]`.
-/
noncomputable def inv (f : X âŸ¶ Y) [I : is_iso f] := classical.some I.1

namespace is_iso

@[simp, reassoc] lemma hom_inv_id (f : X âŸ¶ Y) [I : is_iso f] : f â‰« inv f = ğŸ™ X :=
(classical.some_spec I.1).left
@[simp, reassoc] lemma inv_hom_id (f : X âŸ¶ Y) [I : is_iso f] : inv f â‰« f = ğŸ™ Y :=
(classical.some_spec I.1).right

end is_iso

open is_iso

/-- Reinterpret a morphism `f` with an `is_iso f` instance as an `iso`. -/
noncomputable
def as_iso (f : X âŸ¶ Y) [h : is_iso f] : X â‰… Y := âŸ¨f, inv f, hom_inv_id f, inv_hom_id fâŸ©

@[simp] lemma as_iso_hom (f : X âŸ¶ Y) [is_iso f] : (as_iso f).hom = f := rfl
@[simp] lemma as_iso_inv (f : X âŸ¶ Y) [is_iso f] : (as_iso f).inv = inv f := rfl

namespace is_iso

@[priority 100] -- see Note [lower instance priority]
instance epi_of_iso (f : X âŸ¶ Y) [is_iso f] : epi f  :=
{ left_cancellation := Î» Z g h w,
  -- This is an interesting test case for better rewrite automation.
  by rw [â† is_iso.inv_hom_id_assoc f g, w, is_iso.inv_hom_id_assoc f h] }
@[priority 100] -- see Note [lower instance priority]
instance mono_of_iso (f : X âŸ¶ Y) [is_iso f] : mono f :=
{ right_cancellation := Î» Z g h w,
  by rw [â† category.comp_id g, â† category.comp_id h, â† is_iso.hom_inv_id f, â† category.assoc, w,
    â† category.assoc] }

@[ext] lemma inv_eq_of_hom_inv_id {f : X âŸ¶ Y} [is_iso f] {g : Y âŸ¶ X}
  (hom_inv_id : f â‰« g = ğŸ™ X) : inv f = g :=
begin
  apply (cancel_epi f).mp,
  simp [hom_inv_id],
end

lemma inv_eq_of_inv_hom_id {f : X âŸ¶ Y} [is_iso f] {g : Y âŸ¶ X}
  (inv_hom_id : g â‰« f = ğŸ™ Y) : inv f = g :=
begin
  apply (cancel_mono f).mp,
  simp [inv_hom_id],
end

@[ext] lemma eq_inv_of_hom_inv_id {f : X âŸ¶ Y} [is_iso f] {g : Y âŸ¶ X}
  (hom_inv_id : f â‰« g = ğŸ™ X) : g = inv f :=
(inv_eq_of_hom_inv_id hom_inv_id).symm

lemma eq_inv_of_inv_hom_id {f : X âŸ¶ Y} [is_iso f] {g : Y âŸ¶ X}
  (inv_hom_id : g â‰« f = ğŸ™ Y) : g = inv f :=
(inv_eq_of_inv_hom_id inv_hom_id).symm


instance id (X : C) : is_iso (ğŸ™ X) :=
âŸ¨âŸ¨ğŸ™ X, by simpâŸ©âŸ©

instance of_iso (f : X â‰… Y) : is_iso f.hom :=
âŸ¨âŸ¨f.inv, by simpâŸ©âŸ©

instance of_iso_inv (f : X â‰… Y) : is_iso f.inv :=
is_iso.of_iso f.symm

variables {f g : X âŸ¶ Y} {h : Y âŸ¶ Z}

instance inv_is_iso [is_iso f] : is_iso (inv f) :=
is_iso.of_iso_inv (as_iso f)

instance comp_is_iso [is_iso f] [is_iso h] : is_iso (f â‰« h) :=
is_iso.of_iso $ (as_iso f) â‰ªâ‰« (as_iso h)

@[simp] lemma inv_id : inv (ğŸ™ X) = ğŸ™ X := by { ext, simp, }
@[simp] lemma inv_comp [is_iso f] [is_iso h] : inv (f â‰« h) = inv h â‰« inv f := by { ext, simp, }
@[simp] lemma inv_inv [is_iso f] : inv (inv f) = f := by { ext, simp, }
@[simp] lemma iso.inv_inv (f : X â‰… Y) : inv (f.inv) = f.hom := by { ext, simp, }
@[simp] lemma iso.inv_hom (f : X â‰… Y) : inv (f.hom) = f.inv := by { ext, simp, }

@[simp]
lemma inv_comp_eq (Î± : X âŸ¶ Y) [is_iso Î±] {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : inv Î± â‰« f = g â†” f = Î± â‰« g :=
(as_iso Î±).inv_comp_eq

@[simp]
lemma eq_inv_comp (Î± : X âŸ¶ Y) [is_iso Î±] {f : X âŸ¶ Z} {g : Y âŸ¶ Z} : g = inv Î± â‰« f â†” Î± â‰« g = f :=
(as_iso Î±).eq_inv_comp

@[simp]
lemma comp_inv_eq (Î± : X âŸ¶ Y) [is_iso Î±] {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : f â‰« inv Î± = g â†” f = g â‰« Î± :=
(as_iso Î±).comp_inv_eq

@[simp]
lemma eq_comp_inv (Î± : X âŸ¶ Y) [is_iso Î±] {f : Z âŸ¶ Y} {g : Z âŸ¶ X} : g = f â‰« inv Î± â†” g â‰« Î± = f :=
(as_iso Î±).eq_comp_inv

end is_iso

open is_iso

lemma eq_of_inv_eq_inv {f g : X âŸ¶ Y} [is_iso f] [is_iso g] (p : inv f = inv g) : f = g :=
begin
  apply (cancel_epi (inv f)).1,
  erw [inv_hom_id, p, inv_hom_id],
end

lemma is_iso.inv_eq_inv {f g : X âŸ¶ Y} [is_iso f] [is_iso g] : inv f = inv g â†” f = g :=
iso.inv_eq_inv (as_iso f) (as_iso g)

lemma hom_comp_eq_id (g : X âŸ¶ Y) [is_iso g] {f : Y âŸ¶ X} : g â‰« f = ğŸ™ X â†” f = inv g :=
(as_iso g).hom_comp_eq_id

lemma comp_hom_eq_id (g : X âŸ¶ Y) [is_iso g] {f : Y âŸ¶ X} : f â‰« g = ğŸ™ Y â†” f = inv g :=
(as_iso g).comp_hom_eq_id

namespace iso

@[ext] lemma inv_ext {f : X â‰… Y} {g : Y âŸ¶ X}
  (hom_inv_id : f.hom â‰« g = ğŸ™ X) : f.inv = g :=
begin
  apply (cancel_epi f.hom).mp,
  simp [hom_inv_id],
end

@[ext] lemma inv_ext' {f : X â‰… Y} {g : Y âŸ¶ X}
  (hom_inv_id : f.hom â‰« g = ğŸ™ X) : g = f.inv :=
by { symmetry, ext, assumption, }

/-!
All these cancellation lemmas can be solved by `simp [cancel_mono]` (or `simp [cancel_epi]`),
but with the current design `cancel_mono` is not a good `simp` lemma,
because it generates a typeclass search.

When we can see syntactically that a morphism is a `mono` or an `epi`
because it came from an isomorphism, it's fine to do the cancellation via `simp`.

In the longer term, it might be worth exploring making `mono` and `epi` structures,
rather than typeclasses, with coercions back to `X âŸ¶ Y`.
Presumably we could write `X â†ª Y` and `X â†  Y`.
-/

@[simp] lemma cancel_iso_hom_left {X Y Z : C} (f : X â‰… Y) (g g' : Y âŸ¶ Z) :
  f.hom â‰« g = f.hom â‰« g' â†” g = g' :=
by simp only [cancel_epi]

@[simp] lemma cancel_iso_inv_left {X Y Z : C} (f : Y â‰… X) (g g' : Y âŸ¶ Z) :
  f.inv â‰« g = f.inv â‰« g' â†” g = g' :=
by simp only [cancel_epi]

@[simp] lemma cancel_iso_hom_right {X Y Z : C} (f f' : X âŸ¶ Y) (g : Y â‰… Z) :
  f â‰« g.hom = f' â‰« g.hom â†” f = f' :=
by simp only [cancel_mono]

@[simp] lemma cancel_iso_inv_right {X Y Z : C} (f f' : X âŸ¶ Y) (g : Z â‰… Y) :
  f â‰« g.inv = f' â‰« g.inv â†” f = f' :=
by simp only [cancel_mono]

/-
Unfortunately cancelling an isomorphism from the right of a chain of compositions is awkward.
We would need separate lemmas for each chain length (worse: for each pair of chain lengths).

We provide two more lemmas, for case of three morphisms, because this actually comes up in practice,
but then stop.
-/

@[simp] lemma cancel_iso_hom_right_assoc {W X X' Y Z : C}
  (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X') (g' : X' âŸ¶ Y)
  (h : Y â‰… Z) :
  f â‰« g â‰« h.hom = f' â‰« g' â‰« h.hom â†” f â‰« g = f' â‰« g' :=
by simp only [â†category.assoc, cancel_mono]

@[simp] lemma cancel_iso_inv_right_assoc {W X X' Y Z : C}
  (f : W âŸ¶ X) (g : X âŸ¶ Y) (f' : W âŸ¶ X') (g' : X' âŸ¶ Y)
  (h : Z â‰… Y) :
  f â‰« g â‰« h.inv = f' â‰« g' â‰« h.inv â†” f â‰« g = f' â‰« g' :=
by simp only [â†category.assoc, cancel_mono]

end iso

namespace functor

universes uâ‚ vâ‚ uâ‚‚ vâ‚‚
variables {D : Type uâ‚‚}

variables [category.{vâ‚‚} D]

/-- A functor `F : C â¥¤ D` sends isomorphisms `i : X â‰… Y` to isomorphisms `F.obj X â‰… F.obj Y` -/
@[simps]
def map_iso (F : C â¥¤ D) {X Y : C} (i : X â‰… Y) : F.obj X â‰… F.obj Y :=
{ hom := F.map i.hom,
  inv := F.map i.inv,
  hom_inv_id' := by rw [â†map_comp, iso.hom_inv_id, â†map_id],
  inv_hom_id' := by rw [â†map_comp, iso.inv_hom_id, â†map_id] }

@[simp] lemma map_iso_symm (F : C â¥¤ D) {X Y : C} (i : X â‰… Y) :
  F.map_iso i.symm = (F.map_iso i).symm :=
rfl

@[simp] lemma map_iso_trans (F : C â¥¤ D) {X Y Z : C} (i : X â‰… Y) (j : Y â‰… Z) :
  F.map_iso (i â‰ªâ‰« j) = (F.map_iso i) â‰ªâ‰« (F.map_iso j) :=
by ext; apply functor.map_comp

@[simp] lemma map_iso_refl (F : C â¥¤ D) (X : C) : F.map_iso (iso.refl X) = iso.refl (F.obj X) :=
iso.ext $ F.map_id X

instance map_is_iso (F : C â¥¤ D) (f : X âŸ¶ Y) [is_iso f] : is_iso (F.map f) :=
is_iso.of_iso $ F.map_iso (as_iso f)

@[simp] lemma map_inv (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [is_iso f] :
  F.map (inv f) = inv (F.map f) :=
by { ext, simp [â†F.map_comp], }

lemma map_hom_inv (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [is_iso f] :
  F.map f â‰« F.map (inv f) = ğŸ™ (F.obj X) :=
by simp

lemma map_inv_hom (F : C â¥¤ D) {X Y : C} (f : X âŸ¶ Y) [is_iso f] :
  F.map (inv f) â‰« F.map f = ğŸ™ (F.obj Y) :=
by simp

end functor

end category_theory
