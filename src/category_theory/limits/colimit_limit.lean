/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.limits.types
import category_theory.currying
import category_theory.limits.functor_category

/-!
# The morphism comparing a colimit of limits with the corresponding limit of colimits.

For `F : J Ã— K â¥¤ C` there is always a morphism $\colim_k \lim_j F(j,k) â†’ \lim_j \colim_k F(j, k)$.
While it is not usually an isomorphism, with additional hypotheses on `J` and `K` it may be,
in which case we say that "colimits commute with limits".

The prototypical example, proved in `category_theory.limits.filtered_colimit_commutes_finite_limit`,
is that when `C = Type`, filtered colimits commute with finite limits.

## References
* Borceux, Handbook of categorical algebra 1, Section 2.13
* [Stacks: Filtered colimits](https://stacks.math.columbia.edu/tag/002W)
-/

universes vâ‚‚ v u

open category_theory

namespace category_theory.limits

variables {J K : Type v} [small_category J] [small_category K]
variables {C : Type u} [category.{v} C]

variables (F : J Ã— K â¥¤ C)

open category_theory.prod

lemma map_id_left_eq_curry_map {j : J} {k k' : K} {f : k âŸ¶ k'} :
  F.map ((ğŸ™ j, f) : (j, k) âŸ¶ (j, k')) = ((curry.obj F).obj j).map f :=
rfl

lemma map_id_right_eq_curry_swap_map {j j' : J} {f : j âŸ¶ j'} {k : K} :
  F.map ((f, ğŸ™ k) : (j, k) âŸ¶ (j', k)) = ((curry.obj (swap K J â‹™ F)).obj k).map f :=
rfl

variables [has_limits_of_shape J C]
variables [has_colimits_of_shape K C]

/--
The universal morphism
$\colim_k \lim_j F(j,k) â†’ \lim_j \colim_k F(j, k)$.
-/
noncomputable
def colimit_limit_to_limit_colimit :
  colimit ((curry.obj (swap K J â‹™ F)) â‹™ lim) âŸ¶ limit ((curry.obj F) â‹™ colim) :=
limit.lift ((curry.obj F) â‹™ colim)
{ X := _,
  Ï€ :=
  { app := Î» j, colimit.desc ((curry.obj (swap K J â‹™ F)) â‹™ lim)
    { X := _,
      Î¹ :=
      { app := Î» k,
          limit.Ï€ ((curry.obj (swap K J â‹™ F)).obj k) j â‰« colimit.Î¹ ((curry.obj F).obj j) k,
        naturality' :=
        begin
          dsimp,
          intros k k' f,
          simp only [functor.comp_map, curry.obj_map_app, limits.lim_map_Ï€_assoc, swap_map,
            category.comp_id, map_id_left_eq_curry_map, colimit.w],
        end }, },
    naturality' :=
    begin
      dsimp,
      intros j j' f,
      ext k,
      simp only [limits.colimit.Î¹_map, curry.obj_map_app, limits.colimit.Î¹_desc_assoc,
        limits.colimit.Î¹_desc, category.id_comp, category.assoc, map_id_right_eq_curry_swap_map,
        limit.w_assoc],
    end } }

/--
Since `colimit_limit_to_limit_colimit` is a morphism from a colimit to a limit,
this lemma characterises it.
-/
@[simp, reassoc] lemma Î¹_colimit_limit_to_limit_colimit_Ï€ (j) (k) :
  colimit.Î¹ _ k â‰« colimit_limit_to_limit_colimit F â‰« limit.Ï€ _ j =
    limit.Ï€ ((curry.obj (swap K J â‹™ F)).obj k) j â‰« colimit.Î¹ ((curry.obj F).obj j) k :=
by { dsimp [colimit_limit_to_limit_colimit], simp, }

@[simp] lemma Î¹_colimit_limit_to_limit_colimit_Ï€_apply (F : J Ã— K â¥¤ Type v) (j) (k) (f) :
   limit.Ï€ ((curry.obj F) â‹™ colim) j
     (colimit_limit_to_limit_colimit F (colimit.Î¹ ((curry.obj (swap K J â‹™ F)) â‹™ lim) k f)) =
     colimit.Î¹ ((curry.obj F).obj j) k (limit.Ï€ ((curry.obj (swap K J â‹™ F)).obj k) j f) :=
by { dsimp [colimit_limit_to_limit_colimit], simp, }

/-- The map `colimit_limit_to_limit_colimit` realized as a map of cones. -/
@[simps] noncomputable def colimit_limit_to_limit_colimit_cone (G : J â¥¤ K â¥¤ C) [has_limit G] :
  colim.map_cone (limit.cone G) âŸ¶ limit.cone (G â‹™ colim) :=
{ hom := colim.map (limit_iso_swap_comp_lim G).hom â‰«
    colimit_limit_to_limit_colimit (uncurry.obj G : _) â‰«
    lim.map (whisker_right (currying.unit_iso.app G).inv colim),
  w' := Î» j,
  begin
    ext1 k,
    simp only [limit_obj_iso_limit_comp_evaluation_hom_Ï€_assoc, iso.app_inv,
      Î¹_colimit_limit_to_limit_colimit_Ï€_assoc, whisker_right_app,
      colimit.Î¹_map, functor.map_cone_Ï€_app, category.id_comp,
      eq_to_hom_refl, eq_to_hom_app, colimit.Î¹_map_assoc, limit.cone_Ï€,
      lim_map_Ï€_assoc, lim_map_Ï€, category.assoc, currying_unit_iso_inv_app_app_app,
      limit_iso_swap_comp_lim_hom_app, lim_map_eq_lim_map],
    erw category.id_comp,
  end }

end category_theory.limits
