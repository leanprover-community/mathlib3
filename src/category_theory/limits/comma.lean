/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.arrow
import category_theory.over
import category_theory.limits.punit
import category_theory.limits.preserves.basic
import category_theory.limits.creates

/-!
# Limits and colimits in comma categories

We build limits in the comma category `comma L R` provided that the two source categories have
limits and `R` preserves them.
This is used to construct limits in the arrow category, structured arrow category and under
category, and show that the appropriate forgetful functors create limits.

The duals of all the above are also given.
-/

namespace category_theory
open category limits

universes v uâ‚ uâ‚‚ uâ‚ƒ

variables {J : Type v} [small_category J]
variables {A : Type uâ‚} [category.{v} A]
variables {B : Type uâ‚‚} [category.{v} B]
variables {T : Type uâ‚ƒ} [category.{v} T]

namespace comma

variables {L : A â¥¤ T} {R : B â¥¤ T}

variables (F : J â¥¤ comma L R)

@[simps]
def limit_auxiliary_cone (câ‚ : cone (F â‹™ fst L R)) :
  cone ((F â‹™ snd L R) â‹™ R) :=
(cones.postcompose (whisker_left F (comma.nat_trans L R) : _)).obj (L.map_cone câ‚)

@[simps]
def cone_of_preserves [preserves_limit (F â‹™ snd L R) R]
  (câ‚ : cone (F â‹™ fst L R)) {câ‚‚ : cone (F â‹™ snd L R)} (tâ‚‚ : is_limit câ‚‚) :
  cone F :=
{ X :=
  { left := câ‚.X,
    right := câ‚‚.X,
    hom := (is_limit_of_preserves R tâ‚‚).lift (limit_auxiliary_cone _ câ‚) },
  Ï€ :=
  { app := Î» j,
    { left := câ‚.Ï€.app j,
      right := câ‚‚.Ï€.app j,
      w' := ((is_limit_of_preserves R tâ‚‚).fac (limit_auxiliary_cone F câ‚) j).symm },
    naturality' := Î» jâ‚ jâ‚‚ t, by ext; dsimp; simp [â†câ‚.w t, â†câ‚‚.w t] } }

def cone_of_preserves_is_limit [preserves_limit (F â‹™ snd L R) R]
  {câ‚ : cone (F â‹™ fst L R)} (tâ‚ : is_limit câ‚)
  {câ‚‚ : cone (F â‹™ snd L R)} (tâ‚‚ : is_limit câ‚‚) :
  is_limit (cone_of_preserves F câ‚ tâ‚‚) :=
{ lift := Î» s,
  { left := tâ‚.lift ((fst L R).map_cone s),
    right := tâ‚‚.lift ((snd L R).map_cone s),
    w' := (is_limit_of_preserves R tâ‚‚).hom_ext $ Î» j,
    begin
      rw [cone_of_preserves_X_hom, assoc, assoc, (is_limit_of_preserves R tâ‚‚).fac,
        limit_auxiliary_cone_Ï€_app, â†L.map_comp_assoc, tâ‚.fac, R.map_cone_Ï€_app, â†R.map_comp,
        tâ‚‚.fac],
      exact (s.Ï€.app j).w,
    end },
  uniq' := Î» s m w, comma_morphism.ext _ _
      (tâ‚.uniq ((fst L R).map_cone s) _ (Î» j, by simp [â†w]))
      (tâ‚‚.uniq ((snd L R).map_cone s) _ (Î» j, by simp [â†w])) }

@[simps]
def limit_auxiliary_cocone (câ‚‚ : cocone (F â‹™ snd L R)) :
  cocone ((F â‹™ fst L R) â‹™ L) :=
(cocones.precompose (whisker_left F (comma.nat_trans L R) : _)).obj (R.map_cocone câ‚‚)

@[simps]
def cocone_of_preserves [preserves_colimit (F â‹™ fst L R) L]
  {câ‚ : cocone (F â‹™ fst L R)} (tâ‚ : is_colimit câ‚) (câ‚‚ : cocone (F â‹™ snd L R))  :
  cocone F :=
{ X :=
  { left := câ‚.X,
    right := câ‚‚.X,
    hom := (is_colimit_of_preserves L tâ‚).desc (limit_auxiliary_cocone _ câ‚‚) },
  Î¹ :=
  { app := Î» j,
    { left := câ‚.Î¹.app j,
      right := câ‚‚.Î¹.app j,
      w' := ((is_colimit_of_preserves L tâ‚).fac (limit_auxiliary_cocone _ câ‚‚) j) },
    naturality' := Î» jâ‚ jâ‚‚ t, by ext; dsimp; simp [â†câ‚.w t, â†câ‚‚.w t] } }

def cocone_of_preserves_is_colimit [preserves_colimit (F â‹™ fst L R) L]
  {câ‚ : cocone (F â‹™ fst L R)} (tâ‚ : is_colimit câ‚)
  {câ‚‚ : cocone (F â‹™ snd L R)} (tâ‚‚ : is_colimit câ‚‚) :
  is_colimit (cocone_of_preserves F tâ‚ câ‚‚) :=
{ desc := Î» s,
  { left := tâ‚.desc ((fst L R).map_cocone s),
    right := tâ‚‚.desc ((snd L R).map_cocone s),
    w' := (is_colimit_of_preserves L tâ‚).hom_ext $ Î» j,
    begin
      rw [cocone_of_preserves_X_hom, (is_colimit_of_preserves L tâ‚).fac_assoc,
        limit_auxiliary_cocone_Î¹_app, assoc, â†R.map_comp, tâ‚‚.fac, L.map_cocone_Î¹_app,
        â†L.map_comp_assoc, tâ‚.fac],
      exact (s.Î¹.app j).w,
    end },
  uniq' := Î» s m w, comma_morphism.ext _ _
      (tâ‚.uniq ((fst L R).map_cocone s) _ (by simp [â†w]))
      (tâ‚‚.uniq ((snd L R).map_cocone s) _ (by simp [â†w])) }

instance has_limit (F : J â¥¤ comma L R)
  [has_limit (F â‹™ fst L R)] [has_limit (F â‹™ snd L R)]
  [preserves_limit (F â‹™ snd L R) R] :
  has_limit F :=
has_limit.mk âŸ¨_, cone_of_preserves_is_limit _ (limit.is_limit _) (limit.is_limit _)âŸ©

instance has_limits_of_shape
  [has_limits_of_shape J A] [has_limits_of_shape J B] [preserves_limits_of_shape J R] :
  has_limits_of_shape J (comma L R) := {}

instance has_limits [has_limits A] [has_limits B] [preserves_limits R] :
  has_limits (comma L R) := {}

instance has_colimit (F : J â¥¤ comma L R)
  [has_colimit (F â‹™ fst L R)] [has_colimit (F â‹™ snd L R)]
  [preserves_colimit (F â‹™ fst L R) L] :
  has_colimit F :=
has_colimit.mk âŸ¨_, cocone_of_preserves_is_colimit _ (colimit.is_colimit _) (colimit.is_colimit _)âŸ©

instance has_colimits_of_shape
  [has_colimits_of_shape J A] [has_colimits_of_shape J B] [preserves_colimits_of_shape J L] :
  has_colimits_of_shape J (comma L R) := {}

instance has_colimits [has_colimits A] [has_colimits B] [preserves_colimits L] :
  has_colimits (comma L R) := {}

end comma

namespace arrow

instance has_limit (F : J â¥¤ arrow T)
  [has_limit (F â‹™ comma.fst (ğŸ­ T) (ğŸ­ T))] [has_limit (F â‹™ comma.snd (ğŸ­ T) (ğŸ­ T))] :
  has_limit F :=
comma.has_limit _

instance has_limits_of_shape [has_limits_of_shape J T] : has_limits_of_shape J (arrow T) := {}
instance has_limits [has_limits T] : has_limits (arrow T) := {}

end arrow

namespace structured_arrow

variables {X : T} {G : A â¥¤ T} (F : J â¥¤ structured_arrow X G)

instance has_limits_of_shape [has_limits_of_shape J A] [preserves_limits_of_shape J G] :
  has_limits_of_shape J (structured_arrow X G) := {}

instance has_limits [has_limits A] [preserves_limits G] :
  has_limits (structured_arrow X G) := {}

noncomputable instance creates_limit [preserves_limit (F â‹™ proj X G) G] :
  creates_limit F (proj X G) :=
creates_limit_of_reflects_iso $ Î» c t,
{ lifted_cone := comma.cone_of_preserves _ punit_cone t,
  makes_limit := comma.cone_of_preserves_is_limit _ punit_cone_is_limit _,
  valid_lift := cones.ext (iso.refl _) $ Î» j, (category.id_comp _).symm }

noncomputable instance creates_limits_of_shape [preserves_limits_of_shape J G] :
  creates_limits_of_shape J (structured_arrow.proj X G) :=
{}

noncomputable instance creates_limits [preserves_limits G] :
  creates_limits (structured_arrow.proj X G : _) :=
{}

end structured_arrow

namespace under

variables {X : T}

instance [has_limits_of_shape J T] : has_limits_of_shape J (under X) := {}
instance [has_limits T] : has_limits (under X) := {}

end under

namespace costructured_arrow

variables {G : A â¥¤ T} {X : T} (F : J â¥¤ costructured_arrow G X)

instance has_colimit [iâ‚ : has_colimit (F â‹™ proj G X)] [iâ‚‚ : preserves_colimit (F â‹™ proj G X) G] :
  has_colimit F :=
@@comma.has_colimit _ _ _ _ _ iâ‚ _ iâ‚‚

instance has_colimits_of_shape [has_colimits_of_shape J A] [preserves_colimits_of_shape J G] :
  has_colimits_of_shape J (costructured_arrow G X) :=
{}

instance has_colimits [has_colimits A] [preserves_colimits G] :
  has_colimits (costructured_arrow G X) :=
{}

noncomputable instance creates_colimit [preserves_colimit (F â‹™ proj G X) G] :
  creates_colimit F (proj G X) :=
creates_colimit_of_reflects_iso $ Î» c t,
{ lifted_cocone := comma.cocone_of_preserves _ t punit_cocone,
  makes_colimit := comma.cocone_of_preserves_is_colimit _ _ punit_cocone_is_colimit,
  valid_lift := cocones.ext (iso.refl _) $ Î» j, category.comp_id _ }

noncomputable instance creates_colimits_of_shape [preserves_colimits_of_shape J G] :
  creates_colimits_of_shape J (proj G X) :=
{}

noncomputable instance creates_colimits [preserves_colimits G] :
  creates_colimits (proj G X : _) :=
{}

end costructured_arrow

namespace over

variables {X : T}

instance [has_colimits_of_shape J T] : has_colimits_of_shape J (over X) := {}
instance [has_colimits T] : has_colimits (over X) := {}

end over

end category_theory
