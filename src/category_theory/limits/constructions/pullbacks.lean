/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import category_theory.limits.shapes.binary_products
import category_theory.limits.shapes.equalizers
import category_theory.limits.shapes.pullbacks

/-!
# Constructing pullbacks from binary products and equalizers

If a category as binary products and equalizers, then it has pullbacks.
Also, if a category has binary coproducts and coequalizers, then it has pushouts
-/

universes v u

open category_theory

namespace category_theory.limits

/-- If the product `X â¨¯ Y` and the equalizer of `Ï€â‚ â‰« f` and `Ï€â‚‚ â‰« g` exist, then the
    pullback of `f` and `g` exists: It is given by composing the equalizer with the projections. -/
lemma has_limit_cospan_of_has_limit_pair_of_has_limit_parallel_pair
  {C : Type u} [ğ’ : category.{v} C] {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [has_limit (pair X Y)]
  [has_limit (parallel_pair (prod.fst â‰« f) (prod.snd â‰« g))] : has_limit (cospan f g) :=
let Ï€â‚ : X â¨¯ Y âŸ¶ X := prod.fst, Ï€â‚‚ : X â¨¯ Y âŸ¶ Y := prod.snd, e := equalizer.Î¹ (Ï€â‚ â‰« f) (Ï€â‚‚ â‰« g) in
has_limit.mk
{ cone := pullback_cone.mk (e â‰« Ï€â‚) (e â‰« Ï€â‚‚) $ by simp only [category.assoc, equalizer.condition],
  is_limit := pullback_cone.is_limit.mk _
    (Î» s, equalizer.lift (prod.lift (s.Ï€.app walking_cospan.left)
      (s.Ï€.app walking_cospan.right)) $ by
        rw [â†category.assoc, limit.lift_Ï€, â†category.assoc, limit.lift_Ï€];
        exact pullback_cone.condition _)
    (by simp) (by simp) $ Î» s m hâ‚ hâ‚‚, by { ext,
      { simpa using hâ‚ },
      { simpa using hâ‚‚ } } }

section

local attribute [instance] has_limit_cospan_of_has_limit_pair_of_has_limit_parallel_pair

/-- If a category has all binary products and all equalizers, then it also has all pullbacks.
    As usual, this is not an instance, since there may be a more direct way to construct
    pullbacks. -/
lemma has_pullbacks_of_has_binary_products_of_has_equalizers
  (C : Type u) [ğ’ : category.{v} C] [has_binary_products C] [has_equalizers C] :
  has_pullbacks C :=
{ has_limit := Î» F, has_limit_of_iso (diagram_iso_cospan F).symm }

end

/-- If the coproduct `Y â¨¿ Z` and the coequalizer of `f â‰« Î¹â‚` and `g â‰« Î¹â‚‚` exist, then the
    pushout of `f` and `g` exists: It is given by composing the inclusions with the coequalizer. -/
lemma has_colimit_span_of_has_colimit_pair_of_has_colimit_parallel_pair
  {C : Type u} [ğ’ : category.{v} C] {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [has_colimit (pair Y Z)]
  [has_colimit (parallel_pair (f â‰« coprod.inl) (g â‰« coprod.inr))] : has_colimit (span f g) :=
let Î¹â‚ : Y âŸ¶ Y â¨¿ Z := coprod.inl, Î¹â‚‚ : Z âŸ¶ Y â¨¿ Z := coprod.inr,
  c := coequalizer.Ï€ (f â‰« Î¹â‚) (g â‰« Î¹â‚‚) in
has_colimit.mk
{ cocone := pushout_cocone.mk (Î¹â‚ â‰« c) (Î¹â‚‚ â‰« c) $
    by rw [â†category.assoc, â†category.assoc, coequalizer.condition],
  is_colimit := pushout_cocone.is_colimit.mk _
    (Î» s, coequalizer.desc (coprod.desc (s.Î¹.app walking_span.left)
      (s.Î¹.app walking_span.right)) $ by
        rw [category.assoc, colimit.Î¹_desc, category.assoc, colimit.Î¹_desc];
        exact pushout_cocone.condition _)
    (by simp) (by simp) $ Î» s m hâ‚ hâ‚‚, by { ext,
      { simpa using hâ‚ },
      { simpa using hâ‚‚ } } }

section

local attribute [instance] has_colimit_span_of_has_colimit_pair_of_has_colimit_parallel_pair

/-- If a category has all binary coproducts and all coequalizers, then it also has all pushouts.
    As usual, this is not an instance, since there may be a more direct way to construct
    pushouts. -/
lemma has_pushouts_of_has_binary_coproducts_of_has_coequalizers
  (C : Type u) [ğ’ : category.{v} C] [has_binary_coproducts C] [has_coequalizers C] :
  has_pushouts C :=
has_pushouts_of_has_colimit_span C

end

end category_theory.limits
