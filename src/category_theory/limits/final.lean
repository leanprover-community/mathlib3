/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.punit
import category_theory.structured_arrow
import category_theory.is_connected
import category_theory.limits.yoneda
import category_theory.limits.types

/-!
# Final and initial functors

A functor `F : C ‚•§ D` is final if for every `d : D`,
the comma category of morphisms `d ‚ü∂ F.obj c` is connected.

Dually, a functor `F : C ‚•§ D` is initial if for every `d : D`,
the comma category of morphisms `F.obj c ‚ü∂ d` is connected.

We show that right adjoints are examples of final functors, while
left adjoints are examples of initial functors.

For final functors, we prove that the following three statements are equivalent:
1. `F : C ‚•§ D` is final.
2. Every functor `G : D ‚•§ E` has a colimit if and only if `F ‚ãô G` does,
   and these colimits are isomorphic via `colimit.pre G F`.
3. `colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ punit`.

Starting at 1. we show (in `cocones_equiv`) that
the categories of cocones over `G : D ‚•§ E` and over `F ‚ãô G` are equivalent.
(In fact, via an equivalence which does not change the cocone point.)
This readily implies 2., as `comp_has_colimit`, `has_colimit_of_comp`, and `colimit_iso`.

From 2. we can specialize to `G = coyoneda.obj (op d)` to obtain 3., as `colimit_comp_coyoneda_iso`.

From 3., we prove 1. directly in `cofinal_of_colimit_comp_coyoneda_iso_punit`.

Dually, we prove that if a functor `F : C ‚•§ D` is initial, then any functor `G : D ‚•§ E` has a
limit if and only if `F ‚ãô G` does, and these limits are isomorphic via `limit.pre G F`.


## Naming
There is some discrepancy in the literature about naming; some say 'cofinal' instead of 'final'.
The explanation for this is that the 'co' prefix here is *not* the usual category-theoretic one
indicating duality, but rather indicating the sense of "along with".

## Future work
Dualise condition 3 above and the implications 2 ‚áí 3 and 3 ‚áí 1 to initial functors.

## References
* https://stacks.math.columbia.edu/tag/09WN
* https://ncatlab.org/nlab/show/final+functor
* Borceux, Handbook of Categorical Algebra I, Section 2.11.
  (Note he reverses the roles of definition and main result relative to here!)
-/

noncomputable theory

universes v u

namespace category_theory

namespace functor

open opposite
open category_theory.limits

variables {C : Type v} [small_category C]
variables {D : Type v} [small_category D]

/--
A functor `F : C ‚•§ D` is final if for every `d : D`, the comma category of morphisms `d ‚ü∂ F.obj c`
is connected.

See <https://stacks.math.columbia.edu/tag/04E6>
-/
class final (F : C ‚•§ D) : Prop :=
(out (d : D) : is_connected (structured_arrow d F))

attribute [instance] final.out

/--
A functor `F : C ‚•§ D` is initial if for every `d : D`, the comma category of morphisms
`F.obj c ‚ü∂ d` is connected.
-/
class initial (F : C ‚•§ D) : Prop :=
(out (d : D) : is_connected (costructured_arrow F d))

attribute [instance] initial.out

instance final_op_of_initial (F : C ‚•§ D) [initial F] : final F.op :=
{ out := Œª d, is_connected_of_equivalent (costructured_arrow_op_equivalence F (unop d)) }

instance initial_op_of_final (F : C ‚•§ D) [final F] : initial F.op :=
{ out := Œª d, is_connected_of_equivalent (structured_arrow_op_equivalence F (unop d)) }

lemma final_of_initial_op (F : C ‚•§ D) [initial F.op] : final F :=
{ out := Œª d, @is_connected_of_is_connected_op _ _
  (is_connected_of_equivalent (structured_arrow_op_equivalence F d).symm) }

lemma initial_of_final_op (F : C ‚•§ D) [final F.op] : initial F :=
{ out := Œª d, @is_connected_of_is_connected_op _ _
  (is_connected_of_equivalent (costructured_arrow_op_equivalence F d).symm) }

/-- If a functor `R : D ‚•§ C` is a right adjoint, it is final. -/
lemma final_of_adjunction {L : C ‚•§ D} {R : D ‚•§ C} (adj : L ‚ä£ R) : final R :=
{ out := Œª c,
  let u : structured_arrow c R := structured_arrow.mk (adj.unit.app c) in
  @zigzag_is_connected _ _ ‚ü®u‚ü© $ Œª f g, relation.refl_trans_gen.trans
    (relation.refl_trans_gen.single (show zag f u, from
      or.inr ‚ü®structured_arrow.hom_mk ((adj.hom_equiv c f.right).symm f.hom) (by simp)‚ü©))
    (relation.refl_trans_gen.single (show zag u g, from
      or.inl ‚ü®structured_arrow.hom_mk ((adj.hom_equiv c g.right).symm g.hom) (by simp)‚ü©)) }

/-- If a functor `L : C ‚•§ D` is a left adjoint, it is initial. -/
lemma initial_of_adjunction {L : C ‚•§ D} {R : D ‚•§ C} (adj : L ‚ä£ R) : initial L :=
{ out := Œª d,
  let u : costructured_arrow L d := costructured_arrow.mk (adj.counit.app d) in
  @zigzag_is_connected _ _ ‚ü®u‚ü© $ Œª f g, relation.refl_trans_gen.trans
    (relation.refl_trans_gen.single (show zag f u, from
      or.inl ‚ü®costructured_arrow.hom_mk (adj.hom_equiv f.left d f.hom) (by simp)‚ü©))
    (relation.refl_trans_gen.single (show zag u g, from
      or.inr ‚ü®costructured_arrow.hom_mk (adj.hom_equiv g.left d g.hom) (by simp)‚ü©)) }

@[priority 100]
instance final_of_is_right_adjoint (F : C ‚•§ D) [h : is_right_adjoint F] : final F :=
final_of_adjunction h.adj

@[priority 100]
instance initial_of_is_left_adjoint (F : C ‚•§ D) [h : is_left_adjoint F] : initial F :=
initial_of_adjunction h.adj

namespace final

variables (F : C ‚•§ D) [final F]

instance (d : D) : nonempty (structured_arrow d F) := is_connected.is_nonempty

variables {E : Type u} [category.{v} E] (G : D ‚•§ E)

/--
When `F : C ‚•§ D` is cofinal, we denote by `lift F d` an arbitrary choice of object in `C` such that
there exists a morphism `d ‚ü∂ F.obj (lift F d)`.
-/
def lift (d : D) : C :=
(classical.arbitrary (structured_arrow d F)).right

/--
When `F : C ‚•§ D` is cofinal, we denote by `hom_to_lift` an arbitrary choice of morphism
`d ‚ü∂ F.obj (lift F d)`.
-/
def hom_to_lift (d : D) : d ‚ü∂ F.obj (lift F d) :=
(classical.arbitrary (structured_arrow d F)).hom

/--
We provide an induction principle for reasoning about `lift` and `hom_to_lift`.
We want to perform some construction (usually just a proof) about
the particular choices `lift F d` and `hom_to_lift F d`,
it suffices to perform that construction for some other pair of choices
(denoted `X‚ÇÄ : C` and `k‚ÇÄ : d ‚ü∂ F.obj X‚ÇÄ` below),
and to show how to transport such a construction
*both* directions along a morphism between such choices.
-/
def induction {d : D} (Z : Œ† (X : C) (k : d ‚ü∂ F.obj X), Sort*)
  (h‚ÇÅ : Œ† X‚ÇÅ X‚ÇÇ (k‚ÇÅ : d ‚ü∂ F.obj X‚ÇÅ) (k‚ÇÇ : d ‚ü∂ F.obj X‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
    (k‚ÇÅ ‚â´ F.map f = k‚ÇÇ) ‚Üí Z X‚ÇÅ k‚ÇÅ ‚Üí Z X‚ÇÇ k‚ÇÇ)
  (h‚ÇÇ : Œ† X‚ÇÅ X‚ÇÇ (k‚ÇÅ : d ‚ü∂ F.obj X‚ÇÅ) (k‚ÇÇ : d ‚ü∂ F.obj X‚ÇÇ) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
    (k‚ÇÅ ‚â´ F.map f = k‚ÇÇ) ‚Üí Z X‚ÇÇ k‚ÇÇ ‚Üí Z X‚ÇÅ k‚ÇÅ)
  {X‚ÇÄ : C} {k‚ÇÄ : d ‚ü∂ F.obj X‚ÇÄ} (z : Z X‚ÇÄ k‚ÇÄ) : Z (lift F d) (hom_to_lift F d) :=
begin
  apply nonempty.some,
  apply @is_preconnected_induction _ _ _
    (Œª (Y : structured_arrow d F), Z Y.right Y.hom) _ _ { right := X‚ÇÄ, hom := k‚ÇÄ, } z,
  { intros j‚ÇÅ j‚ÇÇ f a, fapply h‚ÇÅ _ _ _ _ f.right _ a, convert f.w.symm, dsimp, simp, },
  { intros j‚ÇÅ j‚ÇÇ f a, fapply h‚ÇÇ _ _ _ _ f.right _ a, convert f.w.symm, dsimp, simp, },
end

variables {F G}

/--
Given a cocone over `F ‚ãô G`, we can construct a `cocone G` with the same cocone point.
-/
@[simps]
def extend_cocone : cocone (F ‚ãô G) ‚•§ cocone G :=
{ obj := Œª c,
  { X := c.X,
    Œπ :=
    { app := Œª X, G.map (hom_to_lift F X) ‚â´ c.Œπ.app (lift F X),
      naturality' := Œª X Y f,
      begin
        dsimp, simp,
        -- This would be true if we'd chosen `lift F X` to be `lift F Y`
        -- and `hom_to_lift F X` to be `f ‚â´ hom_to_lift F Y`.
        apply induction F
          (Œª Z k, G.map f ‚â´ G.map (hom_to_lift F Y) ‚â´ c.Œπ.app (lift F Y) = G.map k ‚â´ c.Œπ.app Z),
        { intros Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z,
        rw [‚Üêa, functor.map_comp, category.assoc, ‚Üêfunctor.comp_map, c.w, z], },
        { intros Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z,
        rw [‚Üêa, functor.map_comp, category.assoc, ‚Üêfunctor.comp_map, c.w] at z,
        rw z, },
        { rw [‚Üêfunctor.map_comp_assoc], },
      end } },
  map := Œª X Y f,
  { hom := f.hom, } }

@[simp]
lemma colimit_cocone_comp_aux (s : cocone (F ‚ãô G)) (j : C) :
  G.map (hom_to_lift F (F.obj j)) ‚â´ s.Œπ.app (lift F (F.obj j)) =
    s.Œπ.app j :=
begin
  -- This point is that this would be true if we took `lift (F.obj j)` to just be `j`
  -- and `hom_to_lift (F.obj j)` to be `ùüô (F.obj j)`.
  apply induction F (Œª X k, G.map k ‚â´ s.Œπ.app X = (s.Œπ.app j : _)),
  { intros j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h, rw ‚Üêw, rw ‚Üê s.w f at h, simpa using h, },
  { intros j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h, rw ‚Üêw at h, rw ‚Üê s.w f, simpa using h, },
  { exact s.w (ùüô _), },
end

variables (F G)

/--
If `F` is cofinal,
the category of cocones on `F ‚ãô G` is equivalent to the category of cocones on `G`,
for any `G : D ‚•§ E`.
-/
@[simps]
def cocones_equiv : cocone (F ‚ãô G) ‚âå cocone G :=
{ functor := extend_cocone,
  inverse := cocones.whiskering F,
  unit_iso := nat_iso.of_components (Œª c, cocones.ext (iso.refl _) (by tidy)) (by tidy),
  counit_iso := nat_iso.of_components (Œª c, cocones.ext (iso.refl _) (by tidy)) (by tidy), }.

variables {G}

/--
When `F : C ‚•§ D` is cofinal, and `t : cocone G` for some `G : D ‚•§ E`,
`t.whisker F` is a colimit cocone exactly when `t` is.
-/
def is_colimit_whisker_equiv (t : cocone G) : is_colimit (t.whisker F) ‚âÉ is_colimit t :=
is_colimit.of_cocone_equiv (cocones_equiv F G).symm

/--
When `F` is cofinal, and `t : cocone (F ‚ãô G)`,
`extend_cocone.obj t` is a colimit coconne exactly when `t` is.
-/
def is_colimit_extend_cocone_equiv (t : cocone (F ‚ãô G)) :
  is_colimit (extend_cocone.obj t) ‚âÉ is_colimit t :=
is_colimit.of_cocone_equiv (cocones_equiv F G)

/-- Given a colimit cocone over `G : D ‚•§ E` we can construct a colimit cocone over `F ‚ãô G`. -/
@[simps]
def colimit_cocone_comp (t : colimit_cocone G) :
  colimit_cocone (F ‚ãô G) :=
{ cocone := _,
  is_colimit := (is_colimit_whisker_equiv F _).symm (t.is_colimit) }

@[priority 100]
instance comp_has_colimit [has_colimit G] :
  has_colimit (F ‚ãô G) :=
has_colimit.mk (colimit_cocone_comp F (get_colimit_cocone G))

lemma colimit_pre_is_iso_aux {t : cocone G} (P : is_colimit t) :
  ((is_colimit_whisker_equiv F _).symm P).desc (t.whisker F) = ùüô t.X :=
begin
  dsimp [is_colimit_whisker_equiv],
  apply P.hom_ext,
  intro j,
  dsimp, simp, dsimp, simp, -- See library note [dsimp, simp].
end

instance colimit_pre_is_iso [has_colimit G] :
  is_iso (colimit.pre G F) :=
begin
  rw colimit.pre_eq (colimit_cocone_comp F (get_colimit_cocone G)) (get_colimit_cocone G),
  erw colimit_pre_is_iso_aux,
  dsimp,
  apply_instance,
end

section
variables (G)

/--
When `F : C ‚•§ D` is cofinal, and `G : D ‚•§ E` has a colimit, then `F ‚ãô G` has a colimit also and
`colimit (F ‚ãô G) ‚âÖ colimit G`

https://stacks.math.columbia.edu/tag/04E7
-/
def colimit_iso [has_colimit G] : colimit (F ‚ãô G) ‚âÖ colimit G := as_iso (colimit.pre G F)

end

/-- Given a colimit cocone over `F ‚ãô G` we can construct a colimit cocone over `G`. -/
@[simps]
def colimit_cocone_of_comp (t : colimit_cocone (F ‚ãô G)) :
  colimit_cocone G :=
{ cocone := extend_cocone.obj t.cocone,
  is_colimit := (is_colimit_extend_cocone_equiv F _).symm (t.is_colimit), }

/--
When `F` is cofinal, and `F ‚ãô G` has a colimit, then `G` has a colimit also.

We can't make this an instance, because `F` is not determined by the goal.
(Even if this weren't a problem, it would cause a loop with `comp_has_colimit`.)
-/
lemma has_colimit_of_comp [has_colimit (F ‚ãô G)] :
  has_colimit G :=
has_colimit.mk (colimit_cocone_of_comp F (get_colimit_cocone (F ‚ãô G)))


section
local attribute [instance] has_colimit_of_comp

/--
When `F` is cofinal, and `F ‚ãô G` has a colimit, then `G` has a colimit also and
`colimit (F ‚ãô G) ‚âÖ colimit G`

https://stacks.math.columbia.edu/tag/04E7
-/
def colimit_iso' [has_colimit (F ‚ãô G)] : colimit (F ‚ãô G) ‚âÖ colimit G := as_iso (colimit.pre G F)


end

/--
If the universal morphism `colimit (F ‚ãô coyoneda.obj (op d)) ‚ü∂ colimit (coyoneda.obj (op d))`
is an isomorphism (as it always is when `F` is cofinal),
then `colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ punit`
(simply because `colimit (coyoneda.obj (op d)) ‚âÖ punit`).
-/
def colimit_comp_coyoneda_iso (d : D) [is_iso (colimit.pre (coyoneda.obj (op d)) F)] :
  colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ punit :=
as_iso (colimit.pre (coyoneda.obj (op d)) F) ‚â™‚â´ coyoneda.colimit_coyoneda_iso (op d)

lemma zigzag_of_eqv_gen_quot_rel {F : C ‚•§ D} {d : D} {f‚ÇÅ f‚ÇÇ : Œ£ X, d ‚ü∂ F.obj X}
  (t : eqv_gen (types.quot.rel.{v v} (F ‚ãô coyoneda.obj (op d))) f‚ÇÅ f‚ÇÇ) :
  zigzag (structured_arrow.mk f‚ÇÅ.2) (structured_arrow.mk f‚ÇÇ.2) :=
begin
  induction t,
  case eqv_gen.rel : x y r
  { obtain ‚ü®f, w‚ü© := r,
    fconstructor,
    swap 2, fconstructor,
    left, fsplit,
    exact { right := f, } },
  case eqv_gen.refl
  { fconstructor, },
  case eqv_gen.symm : x y h ih
  { apply zigzag_symmetric,
    exact ih, },
  case eqv_gen.trans : x y z h‚ÇÅ h‚ÇÇ ih‚ÇÅ ih‚ÇÇ
  { apply relation.refl_trans_gen.trans,
    exact ih‚ÇÅ, exact ih‚ÇÇ, }
end

/--
If `colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ punit` for all `d : D`, then `F` is cofinal.
-/
lemma cofinal_of_colimit_comp_coyoneda_iso_punit
  (I : Œ† d, colimit (F ‚ãô coyoneda.obj (op d)) ‚âÖ punit) : final F :=
‚ü®Œª d, begin
  haveI : nonempty (structured_arrow d F),
  { have := (I d).inv punit.star,
    obtain ‚ü®j, y, rfl‚ü© := limits.types.jointly_surjective'.{v v} this,
    exact ‚ü®structured_arrow.mk y‚ü©, },
  apply zigzag_is_connected,
  rintros ‚ü®‚ü®‚ü©,X‚ÇÅ,f‚ÇÅ‚ü© ‚ü®‚ü®‚ü©,X‚ÇÇ,f‚ÇÇ‚ü©,
  dsimp at *,
  let y‚ÇÅ := colimit.Œπ (F ‚ãô coyoneda.obj (op d)) X‚ÇÅ f‚ÇÅ,
  let y‚ÇÇ := colimit.Œπ (F ‚ãô coyoneda.obj (op d)) X‚ÇÇ f‚ÇÇ,
  have e : y‚ÇÅ = y‚ÇÇ,
  { apply (I d).to_equiv.injective, ext, },
  have t := types.colimit_eq.{v v} e,
  clear e y‚ÇÅ y‚ÇÇ,
  exact zigzag_of_eqv_gen_quot_rel t,
end‚ü©

end final


namespace initial

variables (F : C ‚•§ D) [initial F]

instance (d : D) : nonempty (costructured_arrow F d) := is_connected.is_nonempty

variables {E : Type u} [category.{v} E] (G : D ‚•§ E)

/--
When `F : C ‚•§ D` is initial, we denote by `lift F d` an arbitrary choice of object in `C` such that
there exists a morphism `F.obj (lift F d) ‚ü∂ d`.
-/
def lift (d : D) : C := (classical.arbitrary (costructured_arrow F d)).left

/--
When `F : C ‚•§ D` is initial, we denote by `hom_to_lift` an arbitrary choice of morphism
`F.obj (lift F d) ‚ü∂ d`.
-/
def hom_to_lift (d : D) : F.obj (lift F d) ‚ü∂ d :=
  (classical.arbitrary (costructured_arrow F d)).hom

/--
We provide an induction principle for reasoning about `lift` and `hom_to_lift`.
We want to perform some construction (usually just a proof) about
the particular choices `lift F d` and `hom_to_lift F d`,
it suffices to perform that construction for some other pair of choices
(denoted `X‚ÇÄ : C` and `k‚ÇÄ : F.obj X‚ÇÄ ‚ü∂ d` below),
and to show how to transport such a construction
*both* directions along a morphism between such choices.
-/
def induction {d : D} (Z : Œ† (X : C) (k : F.obj X ‚ü∂ d), Sort*)
  (h‚ÇÅ : Œ† X‚ÇÅ X‚ÇÇ (k‚ÇÅ : F.obj X‚ÇÅ ‚ü∂ d) (k‚ÇÇ : F.obj X‚ÇÇ ‚ü∂ d) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
    (F.map f ‚â´ k‚ÇÇ = k‚ÇÅ) ‚Üí Z X‚ÇÅ k‚ÇÅ ‚Üí Z X‚ÇÇ k‚ÇÇ)
  (h‚ÇÇ : Œ† X‚ÇÅ X‚ÇÇ (k‚ÇÅ : F.obj X‚ÇÅ ‚ü∂ d) (k‚ÇÇ : F.obj X‚ÇÇ ‚ü∂ d) (f : X‚ÇÅ ‚ü∂ X‚ÇÇ),
    (F.map f ‚â´ k‚ÇÇ = k‚ÇÅ) ‚Üí Z X‚ÇÇ k‚ÇÇ ‚Üí Z X‚ÇÅ k‚ÇÅ)
  {X‚ÇÄ : C} {k‚ÇÄ : F.obj X‚ÇÄ ‚ü∂ d} (z : Z X‚ÇÄ k‚ÇÄ) : Z (lift F d) (hom_to_lift F d) :=
begin
  apply nonempty.some,
  apply @is_preconnected_induction _ _ _
    (Œª Y : costructured_arrow F d, Z Y.left Y.hom) _ _ { left := X‚ÇÄ, hom := k‚ÇÄ } z,
  { intros j‚ÇÅ j‚ÇÇ f a, fapply h‚ÇÅ _ _ _ _ f.left _ a, convert f.w, dsimp, simp, },
  { intros j‚ÇÅ j‚ÇÇ f a, fapply h‚ÇÇ _ _ _ _ f.left _ a, convert f.w, dsimp, simp, },
end

variables {F G}

/--
Given a cone over `F ‚ãô G`, we can construct a `cone G` with the same cocone point.
-/
@[simps]
def extend_cone : cone (F ‚ãô G) ‚•§ cone G :=
{ obj := Œª c,
  { X := c.X,
    œÄ :=
    { app := Œª d, c.œÄ.app (lift F d) ‚â´ G.map (hom_to_lift F d),
      naturality' := Œª X Y f,
      begin
        dsimp, simp,
        -- This would be true if we'd chosen `lift F Y` to be `lift F X`
        -- and `hom_to_lift F Y` to be `hom_to_lift F X ‚â´ f`.
        apply induction F (Œª Z k, (c.œÄ.app Z ‚â´ G.map k : c.X ‚ü∂ _) =
          c.œÄ.app (lift F X) ‚â´ G.map (hom_to_lift F X) ‚â´ G.map f),
        { intros Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z,
        rw [‚Üêa, functor.map_comp, ‚Üêfunctor.comp_map, ‚Üêcategory.assoc, ‚Üêcategory.assoc, c.w] at z,
        rw [z, category.assoc] },
        { intros Z‚ÇÅ Z‚ÇÇ k‚ÇÅ k‚ÇÇ g a z,
        rw [‚Üêa, functor.map_comp, ‚Üêfunctor.comp_map, ‚Üêcategory.assoc, ‚Üêcategory.assoc,
          c.w, z, category.assoc] },
        { rw [‚Üêfunctor.map_comp], },
      end } },
  map := Œª X Y f,
  { hom := f.hom, } }

@[simp]
lemma limit_cone_comp_aux (s : cone (F ‚ãô G)) (j : C) :
  s.œÄ.app (lift F (F.obj j)) ‚â´ G.map (hom_to_lift F (F.obj j)) =
    s.œÄ.app j :=
begin
  -- This point is that this would be true if we took `lift (F.obj j)` to just be `j`
  -- and `hom_to_lift (F.obj j)` to be `ùüô (F.obj j)`.
  apply induction F (Œª X k, s.œÄ.app X ‚â´ G.map k = (s.œÄ.app j : _)),
  { intros j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h, rw ‚Üês.w f, rw ‚Üêw at h, simpa using h, },
  { intros j‚ÇÅ j‚ÇÇ k‚ÇÅ k‚ÇÇ f w h, rw ‚Üês.w f at h, rw ‚Üêw, simpa using h, },
  { exact s.w (ùüô _), },
end

variables (F G)

/--
If `F` is initial,
the category of cones on `F ‚ãô G` is equivalent to the category of cones on `G`,
for any `G : D ‚•§ E`.
-/
@[simps]
def cones_equiv : cone (F ‚ãô G) ‚âå cone G :=
{ functor := extend_cone,
  inverse := cones.whiskering F,
  unit_iso := nat_iso.of_components (Œª c, cones.ext (iso.refl _) (by tidy)) (by tidy),
  counit_iso := nat_iso.of_components (Œª c, cones.ext (iso.refl _) (by tidy)) (by tidy), }.

variables {G}

/--
When `F : C ‚•§ D` is initial, and `t : cone G` for some `G : D ‚•§ E`,
`t.whisker F` is a limit cone exactly when `t` is.
-/
def is_limit_whisker_equiv (t : cone G) : is_limit (t.whisker F) ‚âÉ is_limit t :=
is_limit.of_cone_equiv (cones_equiv F G).symm

/--
When `F` is initial, and `t : cone (F ‚ãô G)`,
`extend_cone.obj t` is a limit cone exactly when `t` is.
-/
def is_limit_extend_cone_equiv (t : cone (F ‚ãô G)) :
  is_limit (extend_cone.obj t) ‚âÉ is_limit t :=
is_limit.of_cone_equiv (cones_equiv F G)

/-- Given a limit cone over `G : D ‚•§ E` we can construct a limit cone over `F ‚ãô G`. -/
@[simps]
def limit_cone_comp (t : limit_cone G) :
  limit_cone (F ‚ãô G) :=
{ cone := _,
  is_limit := (is_limit_whisker_equiv F _).symm (t.is_limit) }

@[priority 100]
instance comp_has_limit [has_limit G] :
  has_limit (F ‚ãô G) :=
has_limit.mk (limit_cone_comp F (get_limit_cone G))

lemma limit_pre_is_iso_aux {t : cone G} (P : is_limit t) :
  ((is_limit_whisker_equiv F _).symm P).lift (t.whisker F) = ùüô t.X :=
begin
  dsimp [is_limit_whisker_equiv],
  apply P.hom_ext,
  intro j,
  simp,
end

instance limit_pre_is_iso [has_limit G] :
  is_iso (limit.pre G F) :=
begin
  rw limit.pre_eq (limit_cone_comp F (get_limit_cone G)) (get_limit_cone G),
  erw limit_pre_is_iso_aux,
  dsimp,
  apply_instance,
end

section
variables (G)

/--
When `F : C ‚•§ D` is initial, and `G : D ‚•§ E` has a limit, then `F ‚ãô G` has a limit also and
`limit (F ‚ãô G) ‚âÖ limit G`

https://stacks.math.columbia.edu/tag/04E7
-/
def limit_iso [has_limit G] : limit (F ‚ãô G) ‚âÖ limit G := (as_iso (limit.pre G F)).symm

end

/-- Given a limit cone over `F ‚ãô G` we can construct a limit cone over `G`. -/
@[simps]
def limit_cone_of_comp (t : limit_cone (F ‚ãô G)) :
  limit_cone G :=
{ cone := extend_cone.obj t.cone,
  is_limit := (is_limit_extend_cone_equiv F _).symm (t.is_limit), }

/--
When `F` is initial, and `F ‚ãô G` has a limit, then `G` has a limit also.

We can't make this an instance, because `F` is not determined by the goal.
(Even if this weren't a problem, it would cause a loop with `comp_has_limit`.)
-/
lemma has_limit_of_comp [has_limit (F ‚ãô G)] :
  has_limit G :=
has_limit.mk (limit_cone_of_comp F (get_limit_cone (F ‚ãô G)))

section
local attribute [instance] has_limit_of_comp

/--
When `F` is initial, and `F ‚ãô G` has a limit, then `G` has a limit also and
`limit (F ‚ãô G) ‚âÖ limit G`

https://stacks.math.columbia.edu/tag/04E7
-/
def limit_iso' [has_limit (F ‚ãô G)] : limit (F ‚ãô G) ‚âÖ limit G :=
(as_iso (limit.pre G F)).symm

end

end initial

end functor

end category_theory
