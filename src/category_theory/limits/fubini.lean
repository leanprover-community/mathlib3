/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.limits.has_limits
import category_theory.products.basic
import category_theory.currying

/-!
# A Fubini theorem for categorical limits

We prove that $lim_{J Ã— K} G = lim_J (lim_K G(j, -))$ for a functor `G : J Ã— K â¥¤ C`,
when all the appropriate limits exist.

We begin working with a functor `F : J â¥¤ K â¥¤ C`. We'll write `G : J Ã— K â¥¤ C` for the associated
"uncurried" functor.

In the first part, given a coherent family `D` of limit cones over the functors `F.obj j`,
and a cone `c` over `G`, we construct a cone over the cone points of `D`.
We then show that if `c` is a limit cone, the constructed cone is also a limit cone.

In the second part, we state the Fubini theorem in the setting where limits are
provided by suitable `has_limit` classes.

We construct
`limit_uncurry_iso_limit_comp_lim F : limit (uncurry.obj F) â‰… limit (F â‹™ lim)`
and give simp lemmas characterising it.
For convenience, we also provide
`limit_iso_limit_curry_comp_lim G : limit G â‰… limit ((curry.obj G) â‹™ lim)`
in terms of the uncurried functor.

## Future work

The dual statement.
-/

universes v u

open category_theory

namespace category_theory.limits

variables {J K : Type v} [small_category J] [small_category K]
variables {C : Type u} [category.{v} C]

variables (F : J â¥¤ K â¥¤ C)

/--
A structure carrying a diagram of cones over the functors `F.obj j`.
-/
-- We could try introducing a "dependent functor type" to handle this?
structure diagram_of_cones :=
(obj : Î  j : J, cone (F.obj j))
(map : Î  {j j' : J} (f : j âŸ¶ j'), (cones.postcompose (F.map f)).obj (obj j) âŸ¶ obj j')
(id : âˆ€ j : J, (map (ğŸ™ j)).hom = ğŸ™ _ . obviously)
(comp : âˆ€ {jâ‚ jâ‚‚ jâ‚ƒ : J} (f : jâ‚ âŸ¶ jâ‚‚) (g : jâ‚‚ âŸ¶ jâ‚ƒ),
  (map (f â‰« g)).hom = (map f).hom â‰« (map g).hom . obviously)

variables {F}

/--
Extract the functor `J â¥¤ C` consisting of the cone points and the maps between them,
from a `diagram_of_cones`.
-/
@[simps]
def diagram_of_cones.cone_points (D : diagram_of_cones F) :
  J â¥¤ C :=
{ obj := Î» j, (D.obj j).X,
  map := Î» j j' f, (D.map f).hom,
  map_id' := Î» j, D.id j,
  map_comp' := Î» jâ‚ jâ‚‚ jâ‚ƒ f g, D.comp f g, }

/--
Given a diagram `D` of limit cones over the `F.obj j`, and a cone over `uncurry.obj F`,
we can construct a cone over the diagram consisting of the cone points from `D`.
-/
@[simps]
def cone_of_cone_uncurry
  {D : diagram_of_cones F} (Q : Î  j, is_limit (D.obj j))
  (c : cone (uncurry.obj F)) :
  cone (D.cone_points) :=
{ X := c.X,
  Ï€ :=
  { app := Î» j, (Q j).lift
    { X := c.X,
      Ï€ :=
      { app := Î» k, c.Ï€.app (j, k),
        naturality' := Î» k k' f,
        begin
          dsimp, simp only [category.id_comp],
          have := @nat_trans.naturality _ _ _ _ _ _ c.Ï€ (j, k) (j, k') (ğŸ™ j, f),
          dsimp at this,
          simp only [category.id_comp, category_theory.functor.map_id, nat_trans.id_app] at this,
          exact this,
        end } },
    naturality' := Î» j j' f, (Q j').hom_ext
    begin
      dsimp,
      intro k,
      simp only [limits.cone_morphism.w, limits.cones.postcompose_obj_Ï€, limits.is_limit.fac_assoc,
        limits.is_limit.fac, nat_trans.comp_app, category.id_comp, category.assoc],
      have := @nat_trans.naturality _ _ _ _ _ _ c.Ï€ (j, k) (j', k) (f, ğŸ™ k),
      dsimp at this,
      simp only [category.id_comp, category.comp_id,
        category_theory.functor.map_id, nat_trans.id_app] at this,
      exact this,
    end, } }.

/--
`cone_of_cone_uncurry Q c` is a limit cone when `c` is a limit cone.`
-/
def cone_of_cone_uncurry_is_limit
  {D : diagram_of_cones F} (Q : Î  j, is_limit (D.obj j))
  {c : cone (uncurry.obj F)} (P : is_limit c) :
  is_limit (cone_of_cone_uncurry Q c) :=
{ lift := Î» s, P.lift
  { X := s.X,
    Ï€ :=
    { app := Î» p, s.Ï€.app p.1 â‰« (D.obj p.1).Ï€.app p.2,
      naturality' := Î» p p' f,
      begin
        dsimp, simp only [category.id_comp, category.assoc],
        rcases p with âŸ¨j, kâŸ©,
        rcases p' with âŸ¨j', k'âŸ©,
        rcases f with âŸ¨fj, fkâŸ©,
        dsimp,
        slice_rhs 3 4 { rw â†nat_trans.naturality, },
        slice_rhs 2 3 { rw â†(D.obj j).Ï€.naturality, },
        simp only [functor.const.obj_map, category.id_comp, category.assoc],
        have w := (D.map fj).w k',
        dsimp at w,
        rw â†w,
        have n := s.Ï€.naturality fj,
        dsimp at n,
        simp only [category.id_comp] at n,
        rw n,
        simp,
      end, } },
  fac' := Î» s j,
  begin
    apply (Q j).hom_ext,
    intro k,
    simp,
  end,
  uniq' := Î» s m w,
  begin
    refine P.uniq { X := s.X, Ï€ := _, } m _,
    rintro âŸ¨j, kâŸ©,
    dsimp,
    rw [â†w j],
    simp,
  end, }

section
variables (F)
variables [has_limits_of_shape K C]

/--
Given a functor `F : J â¥¤ K â¥¤ C`, with all needed limits,
we can construct a diagram consisting of the limit cone over each functor `F.obj j`,
and the universal cone morphisms between these.
-/
@[simps]
noncomputable def diagram_of_cones.mk_of_has_limits : diagram_of_cones F :=
{ obj := Î» j, limit.cone (F.obj j),
  map := Î» j j' f, { hom := lim.map (F.map f), }, }

-- Satisfying the inhabited linter.
noncomputable instance diagram_of_cones_inhabited : inhabited (diagram_of_cones F) :=
âŸ¨diagram_of_cones.mk_of_has_limits FâŸ©

@[simp]
lemma diagram_of_cones.mk_of_has_limits_cone_points :
  (diagram_of_cones.mk_of_has_limits F).cone_points = (F â‹™ lim) :=
rfl

variables [has_limit (uncurry.obj F)]
variables [has_limit (F â‹™ lim)]

/--
The Fubini theorem for a functor `F : J â¥¤ K â¥¤ C`,
showing that the limit of `uncurry.obj F` can be computed as
the limit of the limits of the functors `F.obj j`.
-/
noncomputable def limit_uncurry_iso_limit_comp_lim : limit (uncurry.obj F) â‰… limit (F â‹™ lim) :=
begin
  let c := limit.cone (uncurry.obj F),
  let P : is_limit c := limit.is_limit _,
  let G := diagram_of_cones.mk_of_has_limits F,
  let Q : Î  j, is_limit (G.obj j) := Î» j, limit.is_limit _,
  have Q' := cone_of_cone_uncurry_is_limit Q P,
  have Q'' := (limit.is_limit (F â‹™ lim)),
  exact is_limit.cone_point_unique_up_to_iso Q' Q'',
end

@[simp]
lemma limit_uncurry_iso_limit_comp_lim_hom_Ï€_Ï€ {j} {k} :
  (limit_uncurry_iso_limit_comp_lim F).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k = limit.Ï€ _ (j, k) :=
begin
  dsimp [limit_uncurry_iso_limit_comp_lim, is_limit.cone_point_unique_up_to_iso,
    is_limit.unique_up_to_iso],
  simp,
end

@[simp]
lemma limit_uncurry_iso_limit_comp_lim_inv_Ï€ {j} {k} :
  (limit_uncurry_iso_limit_comp_lim F).inv â‰« limit.Ï€ _ (j, k) = limit.Ï€ _ j â‰« limit.Ï€ _ k :=
begin
  rw [â†cancel_epi (limit_uncurry_iso_limit_comp_lim F).hom],
  simp,
end
end

section
variables (G : J Ã— K â¥¤ C)

section
variables [has_limits_of_shape K C]
variables [has_limit G]
variables [has_limit ((curry.obj G) â‹™ lim)]

/--
The Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that the limit of `G` can be computed as
the limit of the limits of the functors `G.obj (j, _)`.
-/
noncomputable def limit_iso_limit_curry_comp_lim : limit G â‰… limit ((curry.obj G) â‹™ lim) :=
begin
  have i : G â‰… uncurry.obj ((@curry J _ K _ C _).obj G) := currying.symm.unit_iso.app G,
  haveI : limits.has_limit (uncurry.obj ((@curry J _ K _ C _).obj G)) :=
    has_limit_of_iso i,
  transitivity limit (uncurry.obj ((@curry J _ K _ C _).obj G)),
  apply has_limit.iso_of_nat_iso i,
  exact limit_uncurry_iso_limit_comp_lim ((@curry J _ K _ C _).obj G),
end

@[simp, reassoc]
lemma limit_iso_limit_curry_comp_lim_hom_Ï€_Ï€ {j} {k} :
  (limit_iso_limit_curry_comp_lim G).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k = limit.Ï€ _ (j, k) :=
by simp [limit_iso_limit_curry_comp_lim, is_limit.cone_point_unique_up_to_iso,
  is_limit.unique_up_to_iso]

@[simp, reassoc]
lemma limit_iso_limit_curry_comp_lim_inv_Ï€ {j} {k} :
  (limit_iso_limit_curry_comp_lim G).inv â‰« limit.Ï€ _ (j, k) = limit.Ï€ _ j â‰« limit.Ï€ _ k :=
begin
  rw [â†cancel_epi (limit_iso_limit_curry_comp_lim G).hom],
  simp,
end
end


section
variables [has_limits C] -- Certainly one could weaken the hypotheses here.

open category_theory.prod

/--
A variant of the Fubini theorem for a functor `G : J Ã— K â¥¤ C`,
showing that $\lim_k \lim_j G(j,k) â‰… \lim_j \lim_k G(j,k)$.
-/
noncomputable
def limit_curry_swap_comp_lim_iso_limit_curry_comp_lim :
  limit ((curry.obj (swap K J â‹™ G)) â‹™ lim) â‰… limit ((curry.obj G) â‹™ lim) :=
calc
  limit ((curry.obj (swap K J â‹™ G)) â‹™ lim)
      â‰… limit (swap K J â‹™ G) : (limit_iso_limit_curry_comp_lim _).symm
  ... â‰… limit G : has_limit.iso_of_equivalence (braiding K J) (iso.refl _)
  ... â‰… limit ((curry.obj G) â‹™ lim) : limit_iso_limit_curry_comp_lim _

@[simp]
lemma limit_curry_swap_comp_lim_iso_limit_curry_comp_lim_hom_Ï€_Ï€ {j} {k} :
  (limit_curry_swap_comp_lim_iso_limit_curry_comp_lim G).hom â‰« limit.Ï€ _ j â‰« limit.Ï€ _ k =
   limit.Ï€ _ k â‰« limit.Ï€ _ j :=
begin
  dsimp [limit_curry_swap_comp_lim_iso_limit_curry_comp_lim],
  simp only [iso.refl_hom, braiding_counit_iso_hom_app, limits.has_limit.iso_of_equivalence_hom_Ï€,
    iso.refl_inv, limit_iso_limit_curry_comp_lim_hom_Ï€_Ï€, eq_to_iso_refl, category.assoc],
  erw [nat_trans.id_app], -- Why can't `simp` do this`?
  dsimp, simp,
end

@[simp]
lemma limit_curry_swap_comp_lim_iso_limit_curry_comp_lim_inv_Ï€_Ï€ {j} {k} :
  (limit_curry_swap_comp_lim_iso_limit_curry_comp_lim G).inv â‰« limit.Ï€ _ k â‰« limit.Ï€ _ j =
   limit.Ï€ _ j â‰« limit.Ï€ _ k :=
begin
  dsimp [limit_curry_swap_comp_lim_iso_limit_curry_comp_lim],
  simp only [iso.refl_hom, braiding_counit_iso_hom_app, limits.has_limit.iso_of_equivalence_inv_Ï€,
    iso.refl_inv, limit_iso_limit_curry_comp_lim_hom_Ï€_Ï€, eq_to_iso_refl, category.assoc],
  erw [nat_trans.id_app], -- Why can't `simp` do this`?
  dsimp, simp,
end

end

end

end category_theory.limits
