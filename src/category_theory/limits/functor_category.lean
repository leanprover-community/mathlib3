/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.limits.preserves.limits

/-!
# (Co)limits in functor categories.

We show that if `D` has limits, then the functor category `C ‚•§ D` also has limits
(`category_theory.limits.functor_category_has_limits`),
and the evaluation functors preserve limits
(`category_theory.limits.evaluation_preserves_limits`)
(and similarly for colimits).

We also show that `F : D ‚•§ K ‚•§ C` preserves (co)limits if it does so for each `k : K`
(`category_theory.limits.preserves_limits_of_evaluation` and
`category_theory.limits.preserves_colimits_of_evaluation`).
-/

open category_theory category_theory.category

-- morphism levels before object levels. See note [category_theory universes].
universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ v v' u u'

namespace category_theory.limits

variables {C : Type u} [category.{v} C] {D : Type u'} [category.{v'} D]

variables {J : Type u‚ÇÅ} [category.{v‚ÇÅ} J] {K : Type u‚ÇÇ} [category.{v‚ÇÇ} K]

@[simp, reassoc]
lemma limit.lift_œÄ_app (H : J ‚•§ K ‚•§ C) [has_limit H] (c : cone H) (j : J) (k : K) :
  (limit.lift H c).app k ‚â´ (limit.œÄ H j).app k = (c.œÄ.app j).app k :=
congr_app (limit.lift_œÄ c j) k

@[simp, reassoc]
lemma colimit.Œπ_desc_app (H : J ‚•§ K ‚•§ C) [has_colimit H] (c : cocone H) (j : J) (k : K) :
  (colimit.Œπ H j).app k ‚â´ (colimit.desc H c).app k = (c.Œπ.app j).app k :=
congr_app (colimit.Œπ_desc c j) k

/--
The evaluation functors jointly reflect limits: that is, to show a cone is a limit of `F`
it suffices to show that each evaluation cone is a limit. In other words, to prove a cone is
limiting you can show it's pointwise limiting.
-/
def evaluation_jointly_reflects_limits {F : J ‚•§ K ‚•§ C} (c : cone F)
  (t : Œ† (k : K), is_limit (((evaluation K C).obj k).map_cone c)) : is_limit c :=
{ lift := Œª s,
  { app := Œª k, (t k).lift ‚ü®s.X.obj k, whisker_right s.œÄ ((evaluation K C).obj k)‚ü©,
    naturality' := Œª X Y f, (t Y).hom_ext $ Œª j,
    begin
      rw [assoc, (t Y).fac _ j],
      simpa using
        ((t X).fac_assoc ‚ü®s.X.obj X, whisker_right s.œÄ ((evaluation K C).obj X)‚ü© j _).symm,
    end },
  fac' := Œª s j, nat_trans.ext _ _ $ funext $ Œª k, (t k).fac _ j,
  uniq' := Œª s m w, nat_trans.ext _ _ $ funext $ Œª x, (t x).hom_ext $ Œª j,
      (congr_app (w j) x).trans
        ((t x).fac ‚ü®s.X.obj _, whisker_right s.œÄ ((evaluation K C).obj _)‚ü© j).symm }

/--
Given a functor `F` and a collection of limit cones for each diagram `X ‚Ü¶ F X k`, we can stitch
them together to give a cone for the diagram `F`.
`combined_is_limit` shows that the new cone is limiting, and `eval_combined` shows it is
(essentially) made up of the original cones.
-/
@[simps] def combine_cones (F : J ‚•§ K ‚•§ C) (c : Œ† (k : K), limit_cone (F.flip.obj k)) :
  cone F :=
{ X :=
  { obj := Œª k, (c k).cone.X,
    map := Œª k‚ÇÅ k‚ÇÇ f, (c k‚ÇÇ).is_limit.lift ‚ü®_, (c k‚ÇÅ).cone.œÄ ‚â´ F.flip.map f‚ü©,
    map_id' := Œª k, (c k).is_limit.hom_ext (Œª j, by { dsimp, simp }),
    map_comp' := Œª k‚ÇÅ k‚ÇÇ k‚ÇÉ f‚ÇÅ f‚ÇÇ, (c k‚ÇÉ).is_limit.hom_ext (Œª j, by simp) },
  œÄ :=
  { app := Œª j, { app := Œª k, (c k).cone.œÄ.app j },
    naturality' := Œª j‚ÇÅ j‚ÇÇ g, nat_trans.ext _ _ $ funext $ Œª k, (c k).cone.œÄ.naturality g } }

/-- The stitched together cones each project down to the original given cones (up to iso). -/
def evaluate_combined_cones (F : J ‚•§ K ‚•§ C) (c : Œ† (k : K), limit_cone (F.flip.obj k)) (k : K) :
  ((evaluation K C).obj k).map_cone (combine_cones F c) ‚âÖ (c k).cone :=
cones.ext (iso.refl _) (by tidy)

/-- Stitching together limiting cones gives a limiting cone. -/
def combined_is_limit (F : J ‚•§ K ‚•§ C) (c : Œ† (k : K), limit_cone (F.flip.obj k)) :
  is_limit (combine_cones F c) :=
evaluation_jointly_reflects_limits _
  (Œª k, (c k).is_limit.of_iso_limit (evaluate_combined_cones F c k).symm)

/--
The evaluation functors jointly reflect colimits: that is, to show a cocone is a colimit of `F`
it suffices to show that each evaluation cocone is a colimit. In other words, to prove a cocone is
colimiting you can show it's pointwise colimiting.
-/
def evaluation_jointly_reflects_colimits {F : J ‚•§ K ‚•§ C} (c : cocone F)
  (t : Œ† (k : K), is_colimit (((evaluation K C).obj k).map_cocone c)) : is_colimit c :=
{ desc := Œª s,
  { app := Œª k, (t k).desc ‚ü®s.X.obj k, whisker_right s.Œπ ((evaluation K C).obj k)‚ü©,
    naturality' := Œª X Y f, (t X).hom_ext $ Œª j,
    begin
      rw [(t X).fac_assoc _ j],
      erw ‚Üê (c.Œπ.app j).naturality_assoc f,
      erw (t Y).fac ‚ü®s.X.obj _, whisker_right s.Œπ _‚ü© j,
      dsimp,
      simp,
    end },
  fac' := Œª s j, nat_trans.ext _ _ $ funext $ Œª k, (t k).fac _ j,
  uniq' := Œª s m w, nat_trans.ext _ _ $ funext $ Œª x, (t x).hom_ext $ Œª j,
      (congr_app (w j) x).trans
        ((t x).fac ‚ü®s.X.obj _, whisker_right s.Œπ ((evaluation K C).obj _)‚ü© j).symm }

/--
Given a functor `F` and a collection of colimit cocones for each diagram `X ‚Ü¶ F X k`, we can stitch
them together to give a cocone for the diagram `F`.
`combined_is_colimit` shows that the new cocone is colimiting, and `eval_combined` shows it is
(essentially) made up of the original cocones.
-/
@[simps] def combine_cocones (F : J ‚•§ K ‚•§ C) (c : Œ† (k : K), colimit_cocone (F.flip.obj k)) :
  cocone F :=
{ X :=
  { obj := Œª k, (c k).cocone.X,
    map := Œª k‚ÇÅ k‚ÇÇ f, (c k‚ÇÅ).is_colimit.desc ‚ü®_, F.flip.map f ‚â´ (c k‚ÇÇ).cocone.Œπ‚ü©,
    map_id' := Œª k, (c k).is_colimit.hom_ext (Œª j, by { dsimp, simp }),
    map_comp' := Œª k‚ÇÅ k‚ÇÇ k‚ÇÉ f‚ÇÅ f‚ÇÇ, (c k‚ÇÅ).is_colimit.hom_ext (Œª j, by simp) },
  Œπ :=
  { app := Œª j, { app := Œª k, (c k).cocone.Œπ.app j },
    naturality' := Œª j‚ÇÅ j‚ÇÇ g, nat_trans.ext _ _ $ funext $ Œª k, (c k).cocone.Œπ.naturality g } }

/-- The stitched together cocones each project down to the original given cocones (up to iso). -/
def evaluate_combined_cocones
  (F : J ‚•§ K ‚•§ C) (c : Œ† (k : K), colimit_cocone (F.flip.obj k)) (k : K) :
  ((evaluation K C).obj k).map_cocone (combine_cocones F c) ‚âÖ (c k).cocone :=
cocones.ext (iso.refl _) (by tidy)

/-- Stitching together colimiting cocones gives a colimiting cocone. -/
def combined_is_colimit (F : J ‚•§ K ‚•§ C) (c : Œ† (k : K), colimit_cocone (F.flip.obj k)) :
  is_colimit (combine_cocones F c) :=
evaluation_jointly_reflects_colimits _
  (Œª k, (c k).is_colimit.of_iso_colimit (evaluate_combined_cocones F c k).symm)

noncomputable theory

instance functor_category_has_limits_of_shape
  [has_limits_of_shape J C] : has_limits_of_shape J (K ‚•§ C) :=
{ has_limit := Œª F, has_limit.mk
  { cone := combine_cones F (Œª k, get_limit_cone _),
    is_limit := combined_is_limit _ _ } }

instance functor_category_has_colimits_of_shape
  [has_colimits_of_shape J C] : has_colimits_of_shape J (K ‚•§ C) :=
{ has_colimit := Œª F, has_colimit.mk
  { cocone := combine_cocones _ (Œª k, get_colimit_cocone _),
    is_colimit := combined_is_colimit _ _ } }

instance functor_category_has_limits_of_size [has_limits_of_size.{v‚ÇÅ u‚ÇÅ} C] :
  has_limits_of_size.{v‚ÇÅ u‚ÇÅ} (K ‚•§ C) := ‚ü®infer_instance‚ü©

instance functor_category_has_colimits_of_size [has_colimits_of_size.{v‚ÇÅ u‚ÇÅ} C] :
  has_colimits_of_size.{v‚ÇÅ u‚ÇÅ} (K ‚•§ C) := ‚ü®infer_instance‚ü©

end category_theory.limits

namespace category_theory.limits

variables {C : Type u} [category.{v} C] {D : Type u'} [category.{v} D]

variables {J : Type v} [category.{v} J] {K : Type v} [category.{v‚ÇÇ} K]

instance evaluation_preserves_limits_of_shape [has_limits_of_shape J C] (k : K) :
  preserves_limits_of_shape J ((evaluation K C).obj k) :=
{ preserves_limit :=
  Œª F, preserves_limit_of_preserves_limit_cone (combined_is_limit _ _) $
    is_limit.of_iso_limit (limit.is_limit _)
      (evaluate_combined_cones F _ k).symm }

/--
If `F : J ‚•§ K ‚•§ C` is a functor into a functor category which has a limit,
then the evaluation of that limit at `k` is the limit of the evaluations of `F.obj j` at `k`.
-/
def limit_obj_iso_limit_comp_evaluation [has_limits_of_shape J C] (F : J ‚•§ K ‚•§ C) (k : K) :
  (limit F).obj k ‚âÖ limit (F ‚ãô ((evaluation K C).obj k)) :=
preserves_limit_iso ((evaluation K C).obj k) F

@[simp, reassoc]
lemma limit_obj_iso_limit_comp_evaluation_hom_œÄ
  [has_limits_of_shape J C] (F : J ‚•§ (K ‚•§ C)) (j : J) (k : K) :
  (limit_obj_iso_limit_comp_evaluation F k).hom ‚â´ limit.œÄ (F ‚ãô ((evaluation K C).obj k)) j =
    (limit.œÄ F j).app k :=
begin
  dsimp [limit_obj_iso_limit_comp_evaluation],
  simp,
end

@[simp, reassoc]
lemma limit_obj_iso_limit_comp_evaluation_inv_œÄ_app
  [has_limits_of_shape J C] (F : J ‚•§ (K ‚•§ C)) (j : J) (k : K):
  (limit_obj_iso_limit_comp_evaluation F k).inv ‚â´ (limit.œÄ F j).app k =
    limit.œÄ (F ‚ãô ((evaluation K C).obj k)) j :=
begin
  dsimp [limit_obj_iso_limit_comp_evaluation],
  rw iso.inv_comp_eq,
  simp,
end

@[simp, reassoc]
lemma limit_map_limit_obj_iso_limit_comp_evaluation_hom
  [has_limits_of_shape J C] {i j : K} (F : J ‚•§ K ‚•§ C) (f : i ‚ü∂ j) :
  (limit F).map f ‚â´ (limit_obj_iso_limit_comp_evaluation _ _).hom =
  (limit_obj_iso_limit_comp_evaluation _ _).hom ‚â´
  lim_map (whisker_left _ ((evaluation _ _).map f)) :=
by { ext, dsimp, simp }

@[simp, reassoc]
lemma limit_obj_iso_limit_comp_evaluation_inv_limit_map
  [has_limits_of_shape J C] {i j : K} (F : J ‚•§ K ‚•§ C) (f : i ‚ü∂ j) :
  (limit_obj_iso_limit_comp_evaluation _ _).inv ‚â´ (limit F).map f =
  lim_map (whisker_left _ ((evaluation _ _).map f)) ‚â´
  (limit_obj_iso_limit_comp_evaluation _ _).inv :=
by rw [iso.inv_comp_eq, ‚Üê category.assoc, iso.eq_comp_inv,
  limit_map_limit_obj_iso_limit_comp_evaluation_hom]

@[ext]
lemma limit_obj_ext {H : J ‚•§ K ‚•§ C} [has_limits_of_shape J C]
  {k : K} {W : C} {f g : W ‚ü∂ (limit H).obj k}
  (w : ‚àÄ j, f ‚â´ (limits.limit.œÄ H j).app k = g ‚â´ (limits.limit.œÄ H j).app k) : f = g :=
begin
  apply (cancel_mono (limit_obj_iso_limit_comp_evaluation H k).hom).1,
  ext,
  simpa using w j,
end

instance evaluation_preserves_colimits_of_shape [has_colimits_of_shape J C] (k : K) :
  preserves_colimits_of_shape J ((evaluation K C).obj k) :=
{ preserves_colimit :=
  Œª F, preserves_colimit_of_preserves_colimit_cocone (combined_is_colimit _ _) $
    is_colimit.of_iso_colimit (colimit.is_colimit _)
      (evaluate_combined_cocones F _ k).symm }

/--
If `F : J ‚•§ K ‚•§ C` is a functor into a functor category which has a colimit,
then the evaluation of that colimit at `k` is the colimit of the evaluations of `F.obj j` at `k`.
-/
def colimit_obj_iso_colimit_comp_evaluation [has_colimits_of_shape J C] (F : J ‚•§ K ‚•§ C) (k : K) :
  (colimit F).obj k ‚âÖ colimit (F ‚ãô ((evaluation K C).obj k)) :=
preserves_colimit_iso ((evaluation K C).obj k) F

@[simp, reassoc]
lemma colimit_obj_iso_colimit_comp_evaluation_Œπ_inv
  [has_colimits_of_shape J C] (F : J ‚•§ (K ‚•§ C)) (j : J) (k : K) :
  colimit.Œπ (F ‚ãô ((evaluation K C).obj k)) j ‚â´ (colimit_obj_iso_colimit_comp_evaluation F k).inv =
    (colimit.Œπ F j).app k :=
begin
  dsimp [colimit_obj_iso_colimit_comp_evaluation],
  simp,
end

@[simp, reassoc]
lemma colimit_obj_iso_colimit_comp_evaluation_Œπ_app_hom
  [has_colimits_of_shape J C] (F : J ‚•§ (K ‚•§ C)) (j : J) (k : K) :
  (colimit.Œπ F j).app k ‚â´ (colimit_obj_iso_colimit_comp_evaluation F k).hom =
     colimit.Œπ (F ‚ãô ((evaluation K C).obj k)) j :=
begin
  dsimp [colimit_obj_iso_colimit_comp_evaluation],
  rw ‚Üêiso.eq_comp_inv,
  simp,
end

@[simp, reassoc]
lemma colimit_obj_iso_colimit_comp_evaluation_inv_colimit_map
  [has_colimits_of_shape J C] (F : J ‚•§ K ‚•§ C) {i j : K} (f : i ‚ü∂ j) :
  (colimit_obj_iso_colimit_comp_evaluation _ _).inv ‚â´ (colimit F).map f =
  colim_map (whisker_left _ ((evaluation _ _).map f)) ‚â´
  (colimit_obj_iso_colimit_comp_evaluation _ _).inv :=
by { ext, dsimp, simp }

@[simp, reassoc]
lemma colimit_map_colimit_obj_iso_colimit_comp_evaluation_hom
  [has_colimits_of_shape J C] (F : J ‚•§ K ‚•§ C) {i j : K} (f : i ‚ü∂ j) :
  (colimit F).map f ‚â´ (colimit_obj_iso_colimit_comp_evaluation _ _).hom =
  (colimit_obj_iso_colimit_comp_evaluation _ _).hom ‚â´
  colim_map (whisker_left _ ((evaluation _ _).map f)) :=
by rw [‚Üê iso.inv_comp_eq, ‚Üê category.assoc, ‚Üê iso.eq_comp_inv,
  colimit_obj_iso_colimit_comp_evaluation_inv_colimit_map]

@[ext]
lemma colimit_obj_ext {H : J ‚•§ K ‚•§ C} [has_colimits_of_shape J C]
  {k : K} {W : C} {f g : (colimit H).obj k ‚ü∂ W}
  (w : ‚àÄ j, (colimit.Œπ H j).app k ‚â´ f = (colimit.Œπ H j).app k ‚â´ g) : f = g :=
begin
  apply (cancel_epi (colimit_obj_iso_colimit_comp_evaluation H k).inv).1,
  ext,
  simpa using w j,
end

instance evaluation_preserves_limits [has_limits C] (k : K) :
  preserves_limits ((evaluation K C).obj k) :=
{ preserves_limits_of_shape := Œª J ùí•, by resetI; apply_instance }

/-- `F : D ‚•§ K ‚•§ C` preserves the limit of some `G : J ‚•§ D` if it does for each `k : K`. -/
def preserves_limit_of_evaluation (F : D ‚•§ K ‚•§ C) (G : J ‚•§ D)
  (H : Œ† (k : K), preserves_limit G (F ‚ãô (evaluation K C).obj k : D ‚•§ C)) :
  preserves_limit G F := ‚ü®Œª c hc,
begin
  apply evaluation_jointly_reflects_limits,
  intro X,
  haveI := H X,
  change is_limit ((F ‚ãô (evaluation K C).obj X).map_cone c),
  exact preserves_limit.preserves hc,
end‚ü©

/-- `F : D ‚•§ K ‚•§ C` preserves limits of shape `J` if it does for each `k : K`. -/
def preserves_limits_of_shape_of_evaluation (F : D ‚•§ K ‚•§ C) (J : Type v) [small_category J]
  (H : Œ† (k : K), preserves_limits_of_shape J (F ‚ãô (evaluation K C).obj k)) :
  preserves_limits_of_shape J F :=
‚ü®Œª G, preserves_limit_of_evaluation F G (Œª k, preserves_limits_of_shape.preserves_limit)‚ü©

/-- `F : D ‚•§ K ‚•§ C` preserves all limits if it does for each `k : K`. -/
def preserves_limits_of_evaluation (F : D ‚•§ K ‚•§ C)
  (H : Œ† (k : K), preserves_limits (F ‚ãô (evaluation K C).obj k)) :
  preserves_limits F :=
‚ü®Œª L hL, by exactI preserves_limits_of_shape_of_evaluation
    F L (Œª k, preserves_limits_of_size.preserves_limits_of_shape)‚ü©

instance evaluation_preserves_colimits [has_colimits C] (k : K) :
  preserves_colimits ((evaluation K C).obj k) :=
{ preserves_colimits_of_shape := Œª J ùí•, by resetI; apply_instance }

/-- `F : D ‚•§ K ‚•§ C` preserves the colimit of some `G : J ‚•§ D` if it does for each `k : K`. -/
def preserves_colimit_of_evaluation (F : D ‚•§ K ‚•§ C) (G : J ‚•§ D)
  (H : Œ† (k), preserves_colimit G (F ‚ãô (evaluation K C).obj k)) : preserves_colimit G F := ‚ü®Œª c hc,
begin
  apply evaluation_jointly_reflects_colimits,
  intro X,
  haveI := H X,
  change is_colimit ((F ‚ãô (evaluation K C).obj X).map_cocone c),
  exact preserves_colimit.preserves hc,
end‚ü©

/-- `F : D ‚•§ K ‚•§ C` preserves all colimits of shape `J` if it does for each `k : K`. -/
def preserves_colimits_of_shape_of_evaluation (F : D ‚•§ K ‚•§ C) (J : Type v) [small_category J]
  (H : Œ† (k : K), preserves_colimits_of_shape J (F ‚ãô (evaluation K C).obj k)) :
  preserves_colimits_of_shape J F :=
‚ü®Œª G, preserves_colimit_of_evaluation F G (Œª k, preserves_colimits_of_shape.preserves_colimit)‚ü©

/-- `F : D ‚•§ K ‚•§ C` preserves all colimits if it does for each `k : K`. -/
def preserves_colimits_of_evaluation (F : D ‚•§ K ‚•§ C)
  (H : Œ† (k : K), preserves_colimits (F ‚ãô (evaluation K C).obj k)) :
  preserves_colimits F :=
‚ü®Œª L hL, by exactI preserves_colimits_of_shape_of_evaluation
    F L (Œª k, preserves_colimits_of_size.preserves_colimits_of_shape)‚ü©
open category_theory.prod

/-- The limit of a diagram `F : J ‚•§ K ‚•§ C` is isomorphic to the functor given by
the individual limits on objects. -/
@[simps]
def limit_iso_flip_comp_lim [has_limits_of_shape J C] (F : J ‚•§ K ‚•§ C) :
  limit F ‚âÖ F.flip ‚ãô lim :=
nat_iso.of_components (limit_obj_iso_limit_comp_evaluation F) $ by tidy

/-- A variant of `limit_iso_flip_comp_lim` where the arguemnts of `F` are flipped. -/
@[simps]
def limit_flip_iso_comp_lim [has_limits_of_shape J C] (F : K ‚•§ J ‚•§ C) :
  limit F.flip ‚âÖ F ‚ãô lim :=
nat_iso.of_components (Œª k,
  limit_obj_iso_limit_comp_evaluation F.flip k ‚â™‚â´
  has_limit.iso_of_nat_iso (flip_comp_evaluation _ _)) $ by tidy

/--
For a functor `G : J ‚•§ K ‚•§ C`, its limit `K ‚•§ C` is given by `(G' : K ‚•§ J ‚•§ C) ‚ãô lim`.
Note that this does not require `K` to be small.
-/
@[simps] def limit_iso_swap_comp_lim [has_limits_of_shape J C] (G : J ‚•§ K ‚•§ C) :
  limit G ‚âÖ curry.obj (swap K J ‚ãô uncurry.obj G) ‚ãô lim :=
limit_iso_flip_comp_lim G ‚â™‚â´ iso_whisker_right (flip_iso_curry_swap_uncurry _) _

/-- The colimit of a diagram `F : J ‚•§ K ‚•§ C` is isomorphic to the functor given by
the individual colimits on objects. -/
@[simps]
def colimit_iso_flip_comp_colim [has_colimits_of_shape J C] (F : J ‚•§ K ‚•§ C) :
  colimit F ‚âÖ F.flip ‚ãô colim :=
nat_iso.of_components (colimit_obj_iso_colimit_comp_evaluation F) $ by tidy

/-- A variant of `colimit_iso_flip_comp_colim` where the arguemnts of `F` are flipped. -/
@[simps]
def colimit_flip_iso_comp_colim [has_colimits_of_shape J C] (F : K ‚•§ J ‚•§ C) :
  colimit F.flip ‚âÖ F ‚ãô colim :=
nat_iso.of_components (Œª k,
  colimit_obj_iso_colimit_comp_evaluation _ _ ‚â™‚â´
  has_colimit.iso_of_nat_iso (flip_comp_evaluation _ _)) $ by tidy

/--
For a functor `G : J ‚•§ K ‚•§ C`, its colimit `K ‚•§ C` is given by `(G' : K ‚•§ J ‚•§ C) ‚ãô colim`.
Note that this does not require `K` to be small.
-/
@[simps]
def colimit_iso_swap_comp_colim [has_colimits_of_shape J C] (G : J ‚•§ K ‚•§ C) :
  colimit G ‚âÖ curry.obj (swap K J ‚ãô uncurry.obj G) ‚ãô colim :=
colimit_iso_flip_comp_colim G ‚â™‚â´ iso_whisker_right (flip_iso_curry_swap_uncurry _) _

end category_theory.limits
