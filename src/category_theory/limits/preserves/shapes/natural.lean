/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.limits.shapes
import category_theory.is_connected
import category_theory.limits.preserves.shapes.binary_products
import category_theory.limits.preserves.shapes.terminal

/-!
# Preserving limits from naturality

The definition of `G : C â¥¤ D` preserving limits of shape `J` can equivalently be stated as:
for any `F : J â¥¤ C`, the canonical morphism `G.obj (limit F) âŸ¶ limit (G â‹™ F)` is an isomorphism.
Note that this morphism is natural in `F`.
However in certain cases, to show `G` preserves limits of shape `J`, it suffices to show there are
natural isomorphisms `G.obj (limit F) âŸ¶ limit (G â‹™ F)`: in particular these might not be the
canonical isomorphisms.
For instance, there are cases where the natural isomorphisms are not unique, but their mere
existence is enough to establish that the canonical morphism is isomorphic.

At the moment, this file shows only a special case (Theorem 3.3 of [Winskel]):
If `G` preserves the terminal object and there are natural isomorphisms `G (X â¨¯ Y) â‰… G X â¨¯ G Y`,
then `G` preserves binary products.

## References

* [Winskel] https://www.cl.cam.ac.uk/~gw104/preservation.pdf
-/

noncomputable theory

universes v uâ‚ uâ‚‚

namespace category_theory
open category limits

variables {C : Type uâ‚} [category.{v} C]
variables {D : Type uâ‚‚} [category.{v} D]
variables (G : C â¥¤ D)

namespace preserves_connected

variables {J : Type v} [small_category J] [is_connected J]

@[simps {rhs_md := semireducible}]
def same_cones {c : C} : yoneda.obj c â‰… ((functor.const J).obj c).cones :=
nat_iso.of_components
  (Î» d, (equiv_of_fully_faithful (functor.const J : C â¥¤ _)).to_iso)
  (Î» d d' f, rfl)

instance constant_has_limit {c : C} : has_limit ((functor.const J).obj c) :=
has_limit.mk âŸ¨_, is_limit.of_nat_iso same_conesâŸ©

def limit_constant_iso (c : C) : limit ((functor.const J).obj c) â‰… c :=
limit.iso_limit_cone âŸ¨_, is_limit.of_nat_iso same_conesâŸ©

-- Note the LHS does not depend on `j`, so this shouldn't be a simp lemma
lemma limit_constant_iso_hom {c : C} (j : J) :
  (limit_constant_iso c).hom = limit.Ï€ ((functor.const J).obj c) j :=
nat_trans_from_is_connected _ (classical.arbitrary J) j

@[reassoc, simp]
lemma limit_constant_iso_inv (c : C) (j : J) :
  (limit_constant_iso c).inv â‰« limit.Ï€ ((functor.const J).obj c) j = ğŸ™ c :=
limit.lift_Ï€ _ _

variables [has_limits_of_shape J C] [has_limits_of_shape J D]

def preserves_of_shape_of_natural_iso (Î¸ : Î  (K : J â¥¤ C), G.obj (limit K) â‰… limit (K â‹™ G))
  (hÎ¸ : âˆ€ {K K'} Î±, (Î¸ K).hom â‰« lim_map (whisker_right Î± G) = G.map (lim_map Î±) â‰« (Î¸ K').hom) :
  preserves_limits_of_shape J G :=
{ preserves_limit := Î» K,
  { preserves := Î» c t,
    begin
      apply (limit.is_limit (K â‹™ G)).of_point_iso,
      let iâ‚‚ : limit ((functor.const J).obj c.X) â‰… limit K :=
        limit_constant_iso c.X â‰ªâ‰« t.cone_point_unique_up_to_iso (limit.is_limit _),
      have q : iâ‚‚.hom = lim_map c.Ï€,
      { ext j,
        simp [limit_constant_iso_hom j] },
      let iâ‚ : (functor.const J).obj (G.obj c.X) â‰… (functor.const J).obj c.X â‹™ G :=
        nat_iso.of_components (Î» _, iso.refl _) (by tidy),
      let i := (limit_constant_iso (G.obj c.X)).symm â‰ªâ‰« has_limit.iso_of_nat_iso iâ‚,
      have z : limit.lift (K â‹™ G) (G.map_cone c) = i.hom â‰« lim_map (whisker_right c.Ï€ G : _),
      { ext1,
        simp [limit_constant_iso_inv_assoc (G.obj c.X)] },
      specialize hÎ¸ c.Ï€,
      rw â† iso.eq_inv_comp at hÎ¸,
      dsimp,
      rw [z, hÎ¸, â† q],
      apply_instance,
    end } }

end preserves_connected

namespace preserves_pair

variables [has_finite_products C] [has_finite_products D]
variables [preserves_limit (functor.empty _) G]

@[simps]
def left_unit (X : C) {T : C} (hT : is_terminal T) : T â¨¯ X â‰… X :=
{ hom := limits.prod.snd,
  inv := prod.lift (hT.from X) (ğŸ™ X),
  hom_inv_id' := prod.hom_ext (hT.hom_ext _ _) (by simp) }

@[simps]
def right_unit (X : C) {T : C} (hT : is_terminal T) : X â¨¯ T â‰… X :=
{ hom := limits.prod.fst,
  inv := prod.lift (ğŸ™ X) (hT.from X),
  hom_inv_id' := prod.hom_ext (by simp) (hT.hom_ext _ _) }

/--
If we have natural isomorphisms `G (X â¨¯ Y) â‰… G X â¨¯ G Y` and `G` preserves the terminal objects,
then `G` preserves binary products. In particular, the isomorphisms do not need to be the canonical
isomorphisms, but using the terminal object we can show binary products are preserved.

Proof is taken from Theorem 3.3 of [Winskel].
-/
def preserves_pair_of_natural_isomorphism {X Y : C} (s : Î  X Y, G.obj (X â¨¯ Y) â‰… G.obj X â¨¯ G.obj Y)
  (w : âˆ€ {X X' Y Y'} (f) (g), (s X Y).hom â‰« limits.prod.map (G.map f) (G.map g) = G.map (limits.prod.map f g) â‰« (s X' Y').hom) :
  preserves_limit (pair X Y) G :=
begin
  refine preserves_limit_of_preserves_limit_cone (prod_is_prod _ _) _,
  apply (binary_fan_map_cone_limit _ _ _).symm _,
  -- This isomorphism is the main idea of the proof: we use an isomorphism which is (in general)
  -- not the identity isomorphism, but it gives nice naturality
  let sâ‚ : G.obj X â‰… G.obj X,
  { apply _ â‰ªâ‰« s X (âŠ¤_ C) â‰ªâ‰« _,
    { apply G.map_iso (right_unit _ terminal_is_terminal).symm },
    { apply right_unit _ (preserves_terminal.is_limit_of_has_terminal_of_preserves_limit G) } },
  let sâ‚‚ : G.obj Y â‰… G.obj Y,
  { apply _ â‰ªâ‰« s (âŠ¤_ C) Y â‰ªâ‰« _,
    { apply G.map_iso (left_unit _ terminal_is_terminal).symm },
    { apply left_unit _ (preserves_terminal.is_limit_of_has_terminal_of_preserves_limit G) } },
  have hsâ‚ : (s X Y).hom â‰« limits.prod.fst = G.map limits.prod.fst â‰« sâ‚.hom,
  { have := w (ğŸ™ X) (terminal.from Y) =â‰« limits.prod.fst,
    simp only [functor.map_id, assoc, comp_id, limits.prod.map_fst] at this,
    change _ = _ â‰« G.map (right_unit X terminal_is_terminal).inv â‰« _ â‰« _,
    rw [this, â† G.map_comp_assoc],
    congr' 2,
    rw iso.eq_comp_inv,
    simp },
  have hsâ‚‚ : (s X Y).hom â‰« limits.prod.snd = G.map limits.prod.snd â‰« sâ‚‚.hom,
  { have := w (terminal.from X) (ğŸ™ Y) =â‰« limits.prod.snd,
    simp only [functor.map_id, assoc, limits.prod.map_snd, comp_id] at this,
    change _ = _ â‰« G.map (left_unit _ terminal_is_terminal).inv â‰« _ â‰« _,
    rw [this, â† G.map_comp_assoc],
    congr' 2,
    rw iso.eq_comp_inv,
    simp },
  refine is_limit.of_iso_limit
            ((is_limit.postcompose_inv_equiv _ _).symm
              (prod_is_prod (G.obj X) (G.obj Y))) _,
  { apply discrete.nat_iso _,
    intro j,
    cases j,
    { apply sâ‚ },
    { apply sâ‚‚ } },
  { symmetry,
    refine cones.ext (s _ _) _,
    rintro (_ | _),
    { change G.map _ = (s X Y).hom â‰« limits.prod.fst â‰« sâ‚.inv,
      rw [reassoc_of hsâ‚, iso.hom_inv_id, comp_id] },
    { change G.map _ = _ â‰« limits.prod.snd â‰« sâ‚‚.inv,
      rw [reassoc_of hsâ‚‚, iso.hom_inv_id, comp_id] } }
end

end preserves_pair
end category_theory
