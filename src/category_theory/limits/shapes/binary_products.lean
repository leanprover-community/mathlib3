/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.limits.shapes.terminal

/-!
# Binary (co)products

We define a category `walking_pair`, which is the index category
for a binary (co)product diagram. A convenience method `pair X Y`
constructs the functor from the walking pair, hitting the given objects.

We define `prod X Y` and `coprod X Y` as limits and colimits of such functors.

Typeclasses `has_binary_products` and `has_binary_coproducts` assert the existence
of (co)limits shaped as walking pairs.

We include lemmas for simplifying equations involving projections and coprojections, and define
braiding and associating isomorphisms, and the product comparison morphism.
-/

universes v u uâ‚‚

open category_theory

namespace category_theory.limits

/-- The type of objects for the diagram indexing a binary (co)product. -/
@[derive decidable_eq, derive inhabited]
inductive walking_pair : Type v
| left | right

open walking_pair

instance fintype_walking_pair : fintype walking_pair :=
{ elems := {left, right},
  complete := Î» x, by { cases x; simp } }

/--
The equivalence swapping left and right.
-/
def walking_pair.swap : walking_pair â‰ƒ walking_pair :=
{ to_fun := Î» j, walking_pair.rec_on j right left,
  inv_fun := Î» j, walking_pair.rec_on j right left,
  left_inv := Î» j, by { cases j; refl, },
  right_inv := Î» j, by { cases j; refl, }, }

@[simp] lemma walking_pair.swap_apply_left : walking_pair.swap left = right := rfl
@[simp] lemma walking_pair.swap_apply_right : walking_pair.swap right = left := rfl
@[simp] lemma walking_pair.swap_symm_apply_tt : walking_pair.swap.symm left = right := rfl
@[simp] lemma walking_pair.swap_symm_apply_ff : walking_pair.swap.symm right = left := rfl

/--
An equivalence from `walking_pair` to `bool`, sometimes useful when reindexing limits.
-/
def walking_pair.equiv_bool : walking_pair â‰ƒ bool :=
{ to_fun := Î» j, walking_pair.rec_on j tt ff, -- to match equiv.sum_equiv_sigma_bool
  inv_fun := Î» b, bool.rec_on b right left,
  left_inv := Î» j, by { cases j; refl, },
  right_inv := Î» b, by { cases b; refl, }, }

@[simp] lemma walking_pair.equiv_bool_apply_left : walking_pair.equiv_bool left = tt := rfl
@[simp] lemma walking_pair.equiv_bool_apply_right : walking_pair.equiv_bool right = ff := rfl
@[simp] lemma walking_pair.equiv_bool_symm_apply_tt : walking_pair.equiv_bool.symm tt = left := rfl
@[simp] lemma walking_pair.equiv_bool_symm_apply_ff : walking_pair.equiv_bool.symm ff = right := rfl

variables {C : Type u} [category.{v} C]

/-- The diagram on the walking pair, sending the two points to `X` and `Y`. -/
def pair (X Y : C) : discrete walking_pair â¥¤ C :=
discrete.functor (Î» j, walking_pair.cases_on j X Y)

@[simp] lemma pair_obj_left (X Y : C) : (pair X Y).obj left = X := rfl
@[simp] lemma pair_obj_right (X Y : C) : (pair X Y).obj right = Y := rfl

section
variables {F G : discrete walking_pair.{v} â¥¤ C} (f : F.obj left âŸ¶ G.obj left) (g : F.obj right âŸ¶ G.obj right)

/-- The natural transformation between two functors out of the walking pair, specified by its components. -/
def map_pair : F âŸ¶ G :=
{ app := Î» j, match j with
  | left := f
  | right := g
  end }

@[simp] lemma map_pair_left : (map_pair f g).app left = f := rfl
@[simp] lemma map_pair_right : (map_pair f g).app right = g := rfl

/-- The natural isomorphism between two functors out of the walking pair, specified by its components. -/
@[simps]
def map_pair_iso (f : F.obj left â‰… G.obj left) (g : F.obj right â‰… G.obj right) : F â‰… G :=
{ hom := map_pair f.hom g.hom,
  inv := map_pair f.inv g.inv,
  hom_inv_id' := begin ext âŸ¨âŸ©; { dsimp, simp, } end,
  inv_hom_id' := begin ext âŸ¨âŸ©; { dsimp, simp, } end }
end

section
variables {D : Type u} [category.{v} D]

/-- The natural isomorphism between `pair X Y â‹™ F` and `pair (F.obj X) (F.obj Y)`. -/
def pair_comp (X Y : C) (F : C â¥¤ D) : pair X Y â‹™ F â‰… pair (F.obj X) (F.obj Y) :=
map_pair_iso (eq_to_iso rfl) (eq_to_iso rfl)
end

/-- Every functor out of the walking pair is naturally isomorphic (actually, equal) to a `pair` -/
def diagram_iso_pair (F : discrete walking_pair â¥¤ C) :
  F â‰… pair (F.obj walking_pair.left) (F.obj walking_pair.right) :=
map_pair_iso (eq_to_iso rfl) (eq_to_iso rfl)

/-- A binary fan is just a cone on a diagram indexing a product. -/
abbreviation binary_fan (X Y : C) := cone (pair X Y)

/-- The first projection of a binary fan. -/
abbreviation binary_fan.fst {X Y : C} (s : binary_fan X Y) := s.Ï€.app walking_pair.left

/-- The second projection of a binary fan. -/
abbreviation binary_fan.snd {X Y : C} (s : binary_fan X Y) := s.Ï€.app walking_pair.right

lemma binary_fan.is_limit.hom_ext {W X Y : C} {s : binary_fan X Y} (h : is_limit s)
  {f g : W âŸ¶ s.X} (hâ‚ : f â‰« s.fst = g â‰« s.fst) (hâ‚‚ : f â‰« s.snd = g â‰« s.snd) : f = g :=
h.hom_ext $ Î» j, walking_pair.cases_on j hâ‚ hâ‚‚

/-- A binary cofan is just a cocone on a diagram indexing a coproduct. -/
abbreviation binary_cofan (X Y : C) := cocone (pair X Y)

/-- The first inclusion of a binary cofan. -/
abbreviation binary_cofan.inl {X Y : C} (s : binary_cofan X Y) := s.Î¹.app walking_pair.left

/-- The second inclusion of a binary cofan. -/
abbreviation binary_cofan.inr {X Y : C} (s : binary_cofan X Y) := s.Î¹.app walking_pair.right

lemma binary_cofan.is_colimit.hom_ext {W X Y : C} {s : binary_cofan X Y} (h : is_colimit s)
  {f g : s.X âŸ¶ W} (hâ‚ : s.inl â‰« f = s.inl â‰« g) (hâ‚‚ : s.inr â‰« f = s.inr â‰« g) : f = g :=
h.hom_ext $ Î» j, walking_pair.cases_on j hâ‚ hâ‚‚

variables {X Y : C}

/-- A binary fan with vertex `P` consists of the two projections `Ï€â‚ : P âŸ¶ X` and `Ï€â‚‚ : P âŸ¶ Y`. -/
def binary_fan.mk {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) : binary_fan X Y :=
{ X := P,
  Ï€ := { app := Î» j, walking_pair.cases_on j Ï€â‚ Ï€â‚‚ }}

/-- A binary cofan with vertex `P` consists of the two inclusions `Î¹â‚ : X âŸ¶ P` and `Î¹â‚‚ : Y âŸ¶ P`. -/
def binary_cofan.mk {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) : binary_cofan X Y :=
{ X := P,
  Î¹ := { app := Î» j, walking_pair.cases_on j Î¹â‚ Î¹â‚‚ }}

@[simp] lemma binary_fan.mk_Ï€_app_left {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) :
  (binary_fan.mk Ï€â‚ Ï€â‚‚).Ï€.app walking_pair.left = Ï€â‚ := rfl
@[simp] lemma binary_fan.mk_Ï€_app_right {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) :
  (binary_fan.mk Ï€â‚ Ï€â‚‚).Ï€.app walking_pair.right = Ï€â‚‚ := rfl
@[simp] lemma binary_cofan.mk_Î¹_app_left {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) :
  (binary_cofan.mk Î¹â‚ Î¹â‚‚).Î¹.app walking_pair.left = Î¹â‚ := rfl
@[simp] lemma binary_cofan.mk_Î¹_app_right {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) :
  (binary_cofan.mk Î¹â‚ Î¹â‚‚).Î¹.app walking_pair.right = Î¹â‚‚ := rfl

/-- If `s` is a limit binary fan over `X` and `Y`, then every pair of morphisms `f : W âŸ¶ X` and
    `g : W âŸ¶ Y` induces a morphism `l : W âŸ¶ s.X` satisfying `l â‰« s.fst = f` and `l â‰« s.snd = g`.
    -/
def binary_fan.is_limit.lift' {W X Y : C} {s : binary_fan X Y} (h : is_limit s) (f : W âŸ¶ X)
  (g : W âŸ¶ Y) : {l : W âŸ¶ s.X // l â‰« s.fst = f âˆ§ l â‰« s.snd = g} :=
âŸ¨h.lift $ binary_fan.mk f g, h.fac _ _, h.fac _ _âŸ©

/-- If `s` is a colimit binary cofan over `X` and `Y`,, then every pair of morphisms `f : X âŸ¶ W` and
    `g : Y âŸ¶ W` induces a morphism `l : s.X âŸ¶ W` satisfying `s.inl â‰« l = f` and `s.inr â‰« l = g`.
    -/
def binary_cofan.is_colimit.desc' {W X Y : C} {s : binary_cofan X Y} (h : is_colimit s) (f : X âŸ¶ W)
  (g : Y âŸ¶ W) : {l : s.X âŸ¶ W // s.inl â‰« l = f âˆ§ s.inr â‰« l = g} :=
âŸ¨h.desc $ binary_cofan.mk f g, h.fac _ _, h.fac _ _âŸ©

/-- An abbreviation for `has_limit (pair X Y)`. -/
abbreviation has_binary_product (X Y : C) := has_limit (pair X Y)
/-- An abbreviation for `has_colimit (pair X Y)`. -/
abbreviation has_binary_coproduct (X Y : C) := has_colimit (pair X Y)

/-- If we have chosen a product of `X` and `Y`, we can access it using `prod X Y` or
    `X â¨¯ Y`. -/
abbreviation prod (X Y : C) [has_binary_product X Y] := limit (pair X Y)

/-- If we have chosen a coproduct of `X` and `Y`, we can access it using `coprod X Y ` or
    `X â¨¿ Y`. -/
abbreviation coprod (X Y : C) [has_binary_coproduct X Y] := colimit (pair X Y)

notation X ` â¨¯ `:20 Y:20 := prod X Y
notation X ` â¨¿ `:20 Y:20 := coprod X Y

/-- The projection map to the first component of the product. -/
abbreviation prod.fst {X Y : C} [has_binary_product X Y] : X â¨¯ Y âŸ¶ X :=
limit.Ï€ (pair X Y) walking_pair.left

/-- The projecton map to the second component of the product. -/
abbreviation prod.snd {X Y : C} [has_binary_product X Y] : X â¨¯ Y âŸ¶ Y :=
limit.Ï€ (pair X Y) walking_pair.right

/-- The inclusion map from the first component of the coproduct. -/
abbreviation coprod.inl {X Y : C} [has_binary_coproduct X Y] : X âŸ¶ X â¨¿ Y :=
colimit.Î¹ (pair X Y) walking_pair.left

/-- The inclusion map from the second component of the coproduct. -/
abbreviation coprod.inr {X Y : C} [has_binary_coproduct X Y] : Y âŸ¶ X â¨¿ Y :=
colimit.Î¹ (pair X Y) walking_pair.right

@[ext] lemma prod.hom_ext {W X Y : C} [has_binary_product X Y] {f g : W âŸ¶ X â¨¯ Y}
  (hâ‚ : f â‰« prod.fst = g â‰« prod.fst) (hâ‚‚ : f â‰« prod.snd = g â‰« prod.snd) : f = g :=
binary_fan.is_limit.hom_ext (limit.is_limit _) hâ‚ hâ‚‚

@[ext] lemma coprod.hom_ext {W X Y : C} [has_binary_coproduct X Y] {f g : X â¨¿ Y âŸ¶ W}
  (hâ‚ : coprod.inl â‰« f = coprod.inl â‰« g) (hâ‚‚ : coprod.inr â‰« f = coprod.inr â‰« g) : f = g :=
binary_cofan.is_colimit.hom_ext (colimit.is_colimit _) hâ‚ hâ‚‚

/-- If the product of `X` and `Y` exists, then every pair of morphisms `f : W âŸ¶ X` and `g : W âŸ¶ Y`
    induces a morphism `prod.lift f g : W âŸ¶ X â¨¯ Y`. -/
abbreviation prod.lift {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) : W âŸ¶ X â¨¯ Y :=
limit.lift _ (binary_fan.mk f g)

/-- If the coproduct of `X` and `Y` exists, then every pair of morphisms `f : X âŸ¶ W` and
    `g : Y âŸ¶ W` induces a morphism `coprod.desc f g : X â¨¿ Y âŸ¶ W`. -/
abbreviation coprod.desc {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) : X â¨¿ Y âŸ¶ W :=
colimit.desc _ (binary_cofan.mk f g)

@[simp, reassoc]
lemma prod.lift_fst {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  prod.lift f g â‰« prod.fst = f :=
limit.lift_Ï€ _ _

@[simp, reassoc]
lemma prod.lift_snd {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  prod.lift f g â‰« prod.snd = g :=
limit.lift_Ï€ _ _

/- The redundant simp lemma linter says that simp can prove the reassoc version of this lemma. -/
@[reassoc, simp]
lemma prod.lift_comp_comp {V W X Y : C} [has_binary_product X Y] (f : V âŸ¶ W) (g : W âŸ¶ X) (h : W âŸ¶ Y) :
  prod.lift (f â‰« g) (f â‰« h) = f â‰« prod.lift g h :=
by tidy

@[simp, reassoc]
lemma coprod.inl_desc {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  coprod.inl â‰« coprod.desc f g = f :=
colimit.Î¹_desc _ _

@[simp, reassoc]
lemma coprod.inr_desc {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  coprod.inr â‰« coprod.desc f g = g :=
colimit.Î¹_desc _ _

/- The redundant simp lemma linter says that simp can prove the reassoc version of this lemma. -/
@[reassoc, simp]
lemma coprod.desc_comp_comp {V W X Y : C} [has_binary_coproduct X Y] (f : V âŸ¶ W) (g : X âŸ¶ V)
  (h : Y âŸ¶ V) : coprod.desc (g â‰« f) (h â‰« f) = coprod.desc g h â‰« f :=
by tidy

instance prod.mono_lift_of_mono_left {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
  [mono f] : mono (prod.lift f g) :=
mono_of_mono_fac $ prod.lift_fst _ _

instance prod.mono_lift_of_mono_right {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
  [mono g] : mono (prod.lift f g) :=
mono_of_mono_fac $ prod.lift_snd _ _

instance coprod.epi_desc_of_epi_left {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
  [epi f] : epi (coprod.desc f g) :=
epi_of_epi_fac $ coprod.inl_desc _ _

instance coprod.epi_desc_of_epi_right {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
  [epi g] : epi (coprod.desc f g) :=
epi_of_epi_fac $ coprod.inr_desc _ _

/-- If the product of `X` and `Y` exists, then every pair of morphisms `f : W âŸ¶ X` and `g : W âŸ¶ Y`
    induces a morphism `l : W âŸ¶ X â¨¯ Y` satisfying `l â‰« prod.fst = f` and `l â‰« prod.snd = g`. -/
def prod.lift' {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  {l : W âŸ¶ X â¨¯ Y // l â‰« prod.fst = f âˆ§ l â‰« prod.snd = g} :=
âŸ¨prod.lift f g, prod.lift_fst _ _, prod.lift_snd _ _âŸ©

/-- If the coproduct of `X` and `Y` exists, then every pair of morphisms `f : X âŸ¶ W` and
    `g : Y âŸ¶ W` induces a morphism `l : X â¨¿ Y âŸ¶ W` satisfying `coprod.inl â‰« l = f` and
    `coprod.inr â‰« l = g`. -/
def coprod.desc' {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  {l : X â¨¿ Y âŸ¶ W // coprod.inl â‰« l = f âˆ§ coprod.inr â‰« l = g} :=
âŸ¨coprod.desc f g, coprod.inl_desc _ _, coprod.inr_desc _ _âŸ©

/-- If the products `W â¨¯ X` and `Y â¨¯ Z` exist, then every pair of morphisms `f : W âŸ¶ Y` and
    `g : X âŸ¶ Z` induces a morphism `prod.map f g : W â¨¯ X âŸ¶ Y â¨¯ Z`. -/
abbreviation prod.map {W X Y Z : C} [has_limits_of_shape.{v} (discrete walking_pair) C]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W â¨¯ X âŸ¶ Y â¨¯ Z :=
lim.map (map_pair f g)

/-- If the products `W â¨¯ X` and `Y â¨¯ Z` exist, then every pair of isomorphisms `f : W â‰… Y` and
    `g : X â‰… Z` induces a isomorphism `prod.map_iso f g : W â¨¯ X â‰… Y â¨¯ Z`. -/
abbreviation prod.map_iso {W X Y Z : C} [has_limits_of_shape.{v} (discrete walking_pair) C]
  (f : W â‰… Y) (g : X â‰… Z) : W â¨¯ X â‰… Y â¨¯ Z :=
lim.map_iso (map_pair_iso f g)

-- Note that the next two `simp` lemmas are proved by `simp`,
-- but nevertheless are useful,
-- because they state the right hand side in terms of `prod.map`
-- rather than `lim.map`.
@[simp] lemma prod.map_iso_hom {W X Y Z : C} [has_limits_of_shape.{v} (discrete walking_pair) C]
  (f : W â‰… Y) (g : X â‰… Z) : (prod.map_iso f g).hom = prod.map f.hom g.hom := by simp

@[simp] lemma prod.map_iso_inv {W X Y Z : C} [has_limits_of_shape.{v} (discrete walking_pair) C]
  (f : W â‰… Y) (g : X â‰… Z) : (prod.map_iso f g).inv = prod.map f.inv g.inv := by simp

/-- If the coproducts `W â¨¿ X` and `Y â¨¿ Z` exist, then every pair of morphisms `f : W âŸ¶ Y` and
    `g : W âŸ¶ Z` induces a morphism `coprod.map f g : W â¨¿ X âŸ¶ Y â¨¿ Z`. -/
abbreviation coprod.map {W X Y Z : C} [has_colimits_of_shape.{v} (discrete walking_pair) C]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W â¨¿ X âŸ¶ Y â¨¿ Z :=
colim.map (map_pair f g)

/-- If the coproducts `W â¨¿ X` and `Y â¨¿ Z` exist, then every pair of isomorphisms `f : W â‰… Y` and
    `g : W â‰… Z` induces a isomorphism `coprod.map_iso f g : W â¨¿ X â‰… Y â¨¿ Z`. -/
abbreviation coprod.map_iso {W X Y Z : C} [has_colimits_of_shape.{v} (discrete walking_pair) C]
  (f : W â‰… Y) (g : X â‰… Z) : W â¨¿ X â‰… Y â¨¿ Z :=
colim.map_iso (map_pair_iso f g)

@[simp] lemma coprod.map_iso_hom {W X Y Z : C} [has_colimits_of_shape.{v} (discrete walking_pair) C]
  (f : W â‰… Y) (g : X â‰… Z) : (coprod.map_iso f g).hom = coprod.map f.hom g.hom := by simp

@[simp] lemma coprod.map_iso_inv {W X Y Z : C} [has_colimits_of_shape.{v} (discrete walking_pair) C]
  (f : W â‰… Y) (g : X â‰… Z) : (coprod.map_iso f g).inv = coprod.map f.inv g.inv := by simp


section prod_lemmas
variable [has_limits_of_shape.{v} (discrete walking_pair) C]

@[reassoc]
lemma prod.map_fst {W X Y Z : C}
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : prod.map f g â‰« prod.fst = prod.fst â‰« f := by simp

@[reassoc]
lemma prod.map_snd {W X Y Z : C}
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : prod.map f g â‰« prod.snd = prod.snd â‰« g := by simp

@[simp] lemma prod_map_id_id {X Y : C} :
  prod.map (ğŸ™ X) (ğŸ™ Y) = ğŸ™ _ :=
by tidy

@[simp] lemma prod_lift_fst_snd {X Y : C} :
  prod.lift prod.fst prod.snd = ğŸ™ (X â¨¯ Y) :=
by tidy

-- I don't think it's a good idea to make any of the following simp lemmas.
@[reassoc]
lemma prod_map_map {A B X Y : C} (f : A âŸ¶ B) (g : X âŸ¶ Y) :
  prod.map (ğŸ™ X) f â‰« prod.map g (ğŸ™ B) = prod.map g (ğŸ™ A) â‰« prod.map (ğŸ™ Y) f :=
by tidy

@[reassoc] lemma prod_map_comp_id {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  prod.map (f â‰« g) (ğŸ™ W) = prod.map f (ğŸ™ W) â‰« prod.map g (ğŸ™ W) :=
by tidy

@[reassoc] lemma prod_map_id_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  prod.map (ğŸ™ W) (f â‰« g) = prod.map (ğŸ™ W) f â‰« prod.map (ğŸ™ W) g :=
by tidy

@[reassoc] lemma prod.lift_map (V W X Y Z : C) (f : V âŸ¶ W) (g : V âŸ¶ X) (h : W âŸ¶ Y) (k : X âŸ¶ Z) :
  prod.lift f g â‰« prod.map h k = prod.lift (f â‰« h) (g â‰« k) :=
by tidy

end prod_lemmas

section coprod_lemmas
variable [has_colimits_of_shape.{v} (discrete walking_pair) C]

@[reassoc]
lemma coprod.inl_map {W X Y Z : C}
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : coprod.inl â‰« coprod.map f g = f â‰« coprod.inl := by simp

@[reassoc]
lemma coprod.inr_map {W X Y Z : C}
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : coprod.inr â‰« coprod.map f g = g â‰« coprod.inr := by simp

@[simp] lemma coprod_map_id_id {X Y : C} :
  coprod.map (ğŸ™ X) (ğŸ™ Y) = ğŸ™ _ :=
by tidy

@[simp] lemma coprod_desc_inl_inr {X Y : C} :
  coprod.desc coprod.inl coprod.inr = ğŸ™ (X â¨¿ Y) :=
by tidy

-- I don't think it's a good idea to make any of the following simp lemmas.
@[reassoc]
lemma coprod_map_map {A B X Y : C} (f : A âŸ¶ B) (g : X âŸ¶ Y) :
  coprod.map (ğŸ™ X) f â‰« coprod.map g (ğŸ™ B) = coprod.map g (ğŸ™ A) â‰« coprod.map (ğŸ™ Y) f :=
by tidy

@[reassoc] lemma coprod_map_comp_id {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  coprod.map (f â‰« g) (ğŸ™ W) = coprod.map f (ğŸ™ W) â‰« coprod.map g (ğŸ™ W) :=
by tidy

@[reassoc] lemma coprod_map_id_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  coprod.map (ğŸ™ W) (f â‰« g) = coprod.map (ğŸ™ W) f â‰« coprod.map (ğŸ™ W) g :=
by tidy

@[reassoc] lemma coprod.map_desc {S T U V W : C} (f : U âŸ¶ S) (g : W âŸ¶ S) (h : T âŸ¶ U) (k : V âŸ¶ W) :
  coprod.map h k â‰« coprod.desc f g = coprod.desc (h â‰« f) (k â‰« g) :=
by tidy

end coprod_lemmas


variables (C)

/-- `has_binary_products` represents a choice of product for every pair of objects. -/
class has_binary_products :=
(has_limits_of_shape : has_limits_of_shape.{v} (discrete walking_pair) C)

/-- `has_binary_coproducts` represents a choice of coproduct for every pair of objects. -/
class has_binary_coproducts :=
(has_colimits_of_shape : has_colimits_of_shape.{v} (discrete walking_pair) C)

attribute [instance] has_binary_products.has_limits_of_shape has_binary_coproducts.has_colimits_of_shape

@[priority 200] -- see Note [lower instance priority]
instance [has_finite_products.{v} C] : has_binary_products.{v} C :=
{ has_limits_of_shape := by apply_instance }
@[priority 200] -- see Note [lower instance priority]
instance [has_finite_coproducts.{v} C] : has_binary_coproducts.{v} C :=
{ has_colimits_of_shape := by apply_instance }

/-- If `C` has all limits of diagrams `pair X Y`, then it has all binary products -/
def has_binary_products_of_has_limit_pair [Î  {X Y : C}, has_limit (pair X Y)] :
  has_binary_products.{v} C :=
{ has_limits_of_shape := { has_limit := Î» F, has_limit_of_iso (diagram_iso_pair F).symm } }

/-- If `C` has all colimits of diagrams `pair X Y`, then it has all binary coproducts -/
def has_binary_coproducts_of_has_colimit_pair [Î  {X Y : C}, has_colimit (pair X Y)] :
  has_binary_coproducts.{v} C :=
{ has_colimits_of_shape := { has_colimit := Î» F, has_colimit_of_iso (diagram_iso_pair F) } }

section

variables {C} [has_binary_products.{v} C]
variables {D : Type uâ‚‚} [category.{v} D] [has_binary_products.{v} D]

-- FIXME deterministic timeout with `-T50000`
/-- The binary product functor. -/
@[simps]
def prod_functor : C â¥¤ C â¥¤ C :=
{ obj := Î» X, { obj := Î» Y, X â¨¯ Y, map := Î» Y Z, prod.map (ğŸ™ X) },
  map := Î» Y Z f, { app := Î» T, prod.map f (ğŸ™ T) }}

/-- The braiding isomorphism which swaps a binary product. -/
@[simps] def prod.braiding (P Q : C) : P â¨¯ Q â‰… Q â¨¯ P :=
{ hom := prod.lift prod.snd prod.fst,
  inv := prod.lift prod.snd prod.fst }

/-- The braiding isomorphism can be passed through a map by swapping the order. -/
@[reassoc] lemma braid_natural {W X Y Z : C} (f : X âŸ¶ Y) (g : Z âŸ¶ W) :
  prod.map f g â‰« (prod.braiding _ _).hom = (prod.braiding _ _).hom â‰« prod.map g f :=
by tidy

@[simp, reassoc] lemma prod.symmetry' (P Q : C) :
  prod.lift prod.snd prod.fst â‰« prod.lift prod.snd prod.fst = ğŸ™ (P â¨¯ Q) :=
by tidy

/-- The braiding isomorphism is symmetric. -/
@[reassoc] lemma prod.symmetry (P Q : C) :
  (prod.braiding P Q).hom â‰« (prod.braiding Q P).hom = ğŸ™ _ :=
by simp

/-- The associator isomorphism for binary products. -/
@[simps] def prod.associator
  (P Q R : C) : (P â¨¯ Q) â¨¯ R â‰… P â¨¯ (Q â¨¯ R) :=
{ hom :=
  prod.lift
    (prod.fst â‰« prod.fst)
    (prod.lift (prod.fst â‰« prod.snd) prod.snd),
  inv :=
  prod.lift
    (prod.lift prod.fst (prod.snd â‰« prod.fst))
    (prod.snd â‰« prod.snd) }

/-- The product functor can be decomposed. -/
def prod_functor_left_comp (X Y : C) :
  prod_functor.obj (X â¨¯ Y) â‰… prod_functor.obj Y â‹™ prod_functor.obj X :=
nat_iso.of_components (prod.associator _ _) (by tidy)

@[reassoc]
lemma prod.pentagon (W X Y Z : C) :
  prod.map ((prod.associator W X Y).hom) (ğŸ™ Z) â‰«
      (prod.associator W (X â¨¯ Y) Z).hom â‰« prod.map (ğŸ™ W) ((prod.associator X Y Z).hom) =
    (prod.associator (W â¨¯ X) Y Z).hom â‰« (prod.associator W X (Y â¨¯ Z)).hom :=
by tidy

@[reassoc]
lemma prod.associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  prod.map (prod.map fâ‚ fâ‚‚) fâ‚ƒ â‰« (prod.associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
    (prod.associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« prod.map fâ‚ (prod.map fâ‚‚ fâ‚ƒ) :=
by tidy

/--
The product comparison morphism.

In `category_theory/limits/preserves` we show this is always an iso iff F preserves binary products.
-/
def prod_comparison (F : C â¥¤ D) (A B : C) : F.obj (A â¨¯ B) âŸ¶ F.obj A â¨¯ F.obj B :=
prod.lift (F.map prod.fst) (F.map prod.snd)

/-- Naturality of the prod_comparison morphism in both arguments. -/
@[reassoc] lemma prod_comparison_natural (F : C â¥¤ D) {A A' B B' : C} (f : A âŸ¶ A') (g : B âŸ¶ B') :
  F.map (prod.map f g) â‰« prod_comparison F A' B' = prod_comparison F A B â‰« prod.map (F.map f) (F.map g) :=
begin
  rw [prod_comparison, prod_comparison, prod.lift_map],
  apply prod.hom_ext,
  { simp only [â† F.map_comp, category.assoc, prod.lift_fst, prod.map_fst, category.comp_id] },
  { simp only [â† F.map_comp, category.assoc, prod.lift_snd, prod.map_snd, prod.lift_snd_assoc] },
end

@[reassoc]
lemma inv_prod_comparison_map_fst (F : C â¥¤ D) (A B : C) [is_iso (prod_comparison F A B)] :
  inv (prod_comparison F A B) â‰« F.map prod.fst = prod.fst :=
begin
  erw (as_iso (prod_comparison F A B)).inv_comp_eq,
  dsimp [as_iso_hom, prod_comparison],
  rw prod.lift_fst,
end

@[reassoc]
lemma inv_prod_comparison_map_snd (F : C â¥¤ D) (A B : C) [is_iso (prod_comparison F A B)] :
  inv (prod_comparison F A B) â‰« F.map prod.snd = prod.snd :=
begin
  erw (as_iso (prod_comparison F A B)).inv_comp_eq,
  dsimp [as_iso_hom, prod_comparison],
  rw prod.lift_snd,
end

/-- If the product comparison morphism is an iso, its inverse is natural. -/
@[reassoc]
lemma prod_comparison_inv_natural (F : C â¥¤ D) {A A' B B' : C} (f : A âŸ¶ A') (g : B âŸ¶ B')
  [is_iso (prod_comparison F A B)] [is_iso (prod_comparison F A' B')] :
  inv (prod_comparison F A B) â‰« F.map (prod.map f g) = prod.map (F.map f) (F.map g) â‰« inv (prod_comparison F A' B') :=
by { erw [(as_iso (prod_comparison F A' B')).eq_comp_inv, category.assoc,
    (as_iso (prod_comparison F A B)).inv_comp_eq, prod_comparison_natural], refl }

variables [has_terminal.{v} C]

/-- The left unitor isomorphism for binary products with the terminal object. -/
@[simps] def prod.left_unitor
  (P : C) : âŠ¤_ C â¨¯ P â‰… P :=
{ hom := prod.snd,
  inv := prod.lift (terminal.from P) (ğŸ™ _) }

/-- The right unitor isomorphism for binary products with the terminal object. -/
@[simps] def prod.right_unitor
  (P : C) : P â¨¯ âŠ¤_ C â‰… P :=
{ hom := prod.fst,
  inv := prod.lift (ğŸ™ _) (terminal.from P) }

@[reassoc]
lemma prod_left_unitor_hom_naturality (f : X âŸ¶ Y):
  prod.map (ğŸ™ _) f â‰« (prod.left_unitor Y).hom = (prod.left_unitor X).hom â‰« f :=
prod.map_snd _ _

@[reassoc]
lemma prod_left_unitor_inv_naturality (f : X âŸ¶ Y):
  (prod.left_unitor X).inv â‰« prod.map (ğŸ™ _) f = f â‰« (prod.left_unitor Y).inv :=
by rw [iso.inv_comp_eq, â† category.assoc, iso.eq_comp_inv, prod_left_unitor_hom_naturality]

@[reassoc]
lemma prod_right_unitor_hom_naturality (f : X âŸ¶ Y):
  prod.map f (ğŸ™ _) â‰« (prod.right_unitor Y).hom = (prod.right_unitor X).hom â‰« f :=
prod.map_fst _ _

@[reassoc]
lemma prod_right_unitor_inv_naturality (f : X âŸ¶ Y):
  (prod.right_unitor X).inv â‰« prod.map f (ğŸ™ _) = f â‰« (prod.right_unitor Y).inv :=
by rw [iso.inv_comp_eq, â† category.assoc, iso.eq_comp_inv, prod_right_unitor_hom_naturality]

lemma prod.triangle (X Y : C) :
  (prod.associator X (âŠ¤_ C) Y).hom â‰« prod.map (ğŸ™ X) ((prod.left_unitor Y).hom) =
    prod.map ((prod.right_unitor X).hom) (ğŸ™ Y) :=
by tidy

end

section
variables {C} [has_binary_coproducts.{v} C]

/-- The braiding isomorphism which swaps a binary coproduct. -/
@[simps] def coprod.braiding (P Q : C) : P â¨¿ Q â‰… Q â¨¿ P :=
{ hom := coprod.desc coprod.inr coprod.inl,
  inv := coprod.desc coprod.inr coprod.inl }

@[simp] lemma coprod.symmetry' (P Q : C) :
  coprod.desc coprod.inr coprod.inl â‰« coprod.desc coprod.inr coprod.inl = ğŸ™ (P â¨¿ Q) :=
by tidy

/-- The braiding isomorphism is symmetric. -/
lemma coprod.symmetry (P Q : C) :
  (coprod.braiding P Q).hom â‰« (coprod.braiding Q P).hom = ğŸ™ _ :=
by simp

/-- The associator isomorphism for binary coproducts. -/
@[simps] def coprod.associator
  (P Q R : C) : (P â¨¿ Q) â¨¿ R â‰… P â¨¿ (Q â¨¿ R) :=
{ hom :=
  coprod.desc
    (coprod.desc coprod.inl (coprod.inl â‰« coprod.inr))
    (coprod.inr â‰« coprod.inr),
  inv :=
  coprod.desc
    (coprod.inl â‰« coprod.inl)
    (coprod.desc (coprod.inr â‰« coprod.inl) coprod.inr) }

lemma coprod.pentagon (W X Y Z : C) :
  coprod.map ((coprod.associator W X Y).hom) (ğŸ™ Z) â‰«
      (coprod.associator W (X â¨¿ Y) Z).hom â‰« coprod.map (ğŸ™ W) ((coprod.associator X Y Z).hom) =
    (coprod.associator (W â¨¿ X) Y Z).hom â‰« (coprod.associator W X (Y â¨¿ Z)).hom :=
by tidy

lemma coprod.associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  coprod.map (coprod.map fâ‚ fâ‚‚) fâ‚ƒ â‰« (coprod.associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
    (coprod.associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« coprod.map fâ‚ (coprod.map fâ‚‚ fâ‚ƒ) :=
by tidy

variables [has_initial.{v} C]

/-- The left unitor isomorphism for binary coproducts with the initial object. -/
@[simps] def coprod.left_unitor
  (P : C) : âŠ¥_ C â¨¿ P â‰… P :=
{ hom := coprod.desc (initial.to P) (ğŸ™ _),
  inv := coprod.inr }

/-- The right unitor isomorphism for binary coproducts with the initial object. -/
@[simps] def coprod.right_unitor
  (P : C) : P â¨¿ âŠ¥_ C â‰… P :=
{ hom := coprod.desc (ğŸ™ _) (initial.to P),
  inv := coprod.inl }

lemma coprod.triangle (X Y : C) :
  (coprod.associator X (âŠ¥_ C) Y).hom â‰« coprod.map (ğŸ™ X) ((coprod.left_unitor Y).hom) =
    coprod.map ((coprod.right_unitor X).hom) (ğŸ™ Y) :=
by tidy

end

end category_theory.limits
