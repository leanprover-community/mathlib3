/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.limits.shapes.finite_products
import category_theory.limits.shapes.terminal
import category_theory.discrete_category

/-!
# Pullbacks

We define a category `walking_pair`, which is the index category
for a binary (co)product diagram. A convenience method `pair X Y`
constructs the functor from the walking pair, hitting the given objects.

We define `prod X Y` and `coprod X Y` as limits and colimits of such functors.

Typeclasses `has_binary_products` and `has_binary_coproducts` assert the existence
of (co)limits shaped as walking pairs.
-/

universes v u

open category_theory

namespace category_theory.limits

/-- The type of objects for the diagram indexing a binary (co)product. -/
@[derive decidable_eq]
inductive walking_pair : Type v
| left | right

instance fintype_walking_pair : fintype walking_pair :=
{ elems := [walking_pair.left, walking_pair.right].to_finset,
  complete := Œª x, by { cases x; simp } }

def pair_function {C : Type u} (X Y : C) : walking_pair ‚Üí C
| walking_pair.left := X
| walking_pair.right := Y

variables {C : Type u} [ùíû : category.{v+1} C]
include ùíû

def pair (X Y : C) : discrete walking_pair ‚•§ C :=
functor.of_function (pair_function X Y)

@[simp] lemma pair_obj_left (X Y : C) : (pair X Y).obj walking_pair.left = X := rfl
@[simp] lemma pair_obj_right (X Y : C) : (pair X Y).obj walking_pair.right = Y := rfl

def map_pair {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) : pair W X ‚ü∂ pair Y Z :=
{ app := Œª j, match j with
  | walking_pair.left := f
  | walking_pair.right := g
  end }

@[simp] lemma map_pair_left {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) : (map_pair f g).app walking_pair.left = f := rfl
@[simp] lemma map_pair_right {W X Y Z : C} (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) : (map_pair f g).app walking_pair.right = g := rfl

abbreviation binary_fan (X Y : C) := cone (pair X Y)
abbreviation binary_cofan (X Y : C) := cocone (pair X Y)

variables {X Y : C}

def binary_fan.mk {P : C} (œÄ‚ÇÅ : P ‚ü∂ X) (œÄ‚ÇÇ : P ‚ü∂ Y) : binary_fan X Y :=
{ X := P,
  œÄ := { app := Œª j, walking_pair.cases_on j œÄ‚ÇÅ œÄ‚ÇÇ }}
def binary_cofan.mk {P : C} (Œπ‚ÇÅ : X ‚ü∂ P) (Œπ‚ÇÇ : Y ‚ü∂ P) : binary_cofan X Y :=
{ X := P,
  Œπ := { app := Œª j, walking_pair.cases_on j Œπ‚ÇÅ Œπ‚ÇÇ }}

@[simp] lemma binary_fan.mk_œÄ_app_left {P : C} (œÄ‚ÇÅ : P ‚ü∂ X) (œÄ‚ÇÇ : P ‚ü∂ Y) :
  (binary_fan.mk œÄ‚ÇÅ œÄ‚ÇÇ).œÄ.app walking_pair.left = œÄ‚ÇÅ := rfl
@[simp] lemma binary_fan.mk_œÄ_app_right {P : C} (œÄ‚ÇÅ : P ‚ü∂ X) (œÄ‚ÇÇ : P ‚ü∂ Y) :
  (binary_fan.mk œÄ‚ÇÅ œÄ‚ÇÇ).œÄ.app walking_pair.right = œÄ‚ÇÇ := rfl
@[simp] lemma binary_cofan.mk_œÄ_app_left {P : C} (Œπ‚ÇÅ : X ‚ü∂ P) (Œπ‚ÇÇ : Y ‚ü∂ P) :
  (binary_cofan.mk Œπ‚ÇÅ Œπ‚ÇÇ).Œπ.app walking_pair.left = Œπ‚ÇÅ := rfl
@[simp] lemma binary_cofan.mk_œÄ_app_right {P : C} (Œπ‚ÇÅ : X ‚ü∂ P) (Œπ‚ÇÇ : Y ‚ü∂ P) :
  (binary_cofan.mk Œπ‚ÇÅ Œπ‚ÇÇ).Œπ.app walking_pair.right = Œπ‚ÇÇ := rfl

abbreviation prod (X Y : C) [has_limit (pair X Y)] := limit (pair X Y)
abbreviation coprod (X Y : C) [has_colimit (pair X Y)] := colimit (pair X Y)

abbreviation prod.fst {X Y : C} [has_limit (pair X Y)] : prod X Y ‚ü∂ X :=
limit.œÄ (pair X Y) walking_pair.left
abbreviation prod.snd {X Y : C} [has_limit (pair X Y)] : prod X Y ‚ü∂ Y :=
limit.œÄ (pair X Y) walking_pair.right
abbreviation coprod.inl {X Y : C} [has_colimit (pair X Y)] : X ‚ü∂ coprod X Y :=
colimit.Œπ (pair X Y) walking_pair.left
abbreviation coprod.inr {X Y : C} [has_colimit (pair X Y)] : Y ‚ü∂ coprod X Y :=
colimit.Œπ (pair X Y) walking_pair.right

abbreviation prod.lift {W X Y : C} [has_limit (pair X Y)] (f : W ‚ü∂ X) (g : W ‚ü∂ Y) : W ‚ü∂ prod X Y :=
limit.lift _ (binary_fan.mk f g)
abbreviation coprod.desc {W X Y : C} [has_colimit (pair X Y)] (f : X ‚ü∂ W) (g : Y ‚ü∂ W) : coprod X Y ‚ü∂ W :=
colimit.desc _ (binary_cofan.mk f g)

abbreviation prod.map {W X Y Z : C} [has_limits_of_shape.{v} (discrete walking_pair) C]
  (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) : prod W X ‚ü∂ prod Y Z :=
lim.map (map_pair f g)
abbreviation coprod.map {W X Y Z : C} [has_colimits_of_shape.{v} (discrete walking_pair) C]
  (f : W ‚ü∂ Y) (g : X ‚ü∂ Z) : coprod W X ‚ü∂ coprod Y Z :=
colim.map (map_pair f g)

variables (C)

class has_binary_products :=
(has_limits_of_shape : has_limits_of_shape.{v} (discrete walking_pair) C)
class has_binary_coproducts :=
(has_colimits_of_shape : has_colimits_of_shape.{v} (discrete walking_pair) C)

attribute [instance] has_binary_products.has_limits_of_shape has_binary_coproducts.has_colimits_of_shape

instance [has_finite_products.{v} C] : has_binary_products.{v} C :=
{ has_limits_of_shape := by apply_instance }
instance [has_finite_coproducts.{v} C] : has_binary_coproducts.{v} C :=
{ has_colimits_of_shape := by apply_instance }

section
variables {C} [has_binary_products.{v} C]

local attribute [tidy] tactic.case_bash

/-- The braiding isomorphism which swaps a binary product. -/
@[simp] def prod.braiding (P Q : C) : prod P Q ‚âÖ prod Q P :=
{ hom := prod.lift prod.snd prod.fst,
  inv := prod.lift prod.snd prod.fst }

/-- The braiding isomorphism is symmetric. -/
def prod.symmetry (P Q : C) :
  (prod.braiding P Q).hom ‚â´ (prod.braiding Q P).hom = ùüô _ :=
by tidy

/-- The associator isomorphism for binary products. -/
@[simp] def prod.associator
  (P Q R : C) : (prod (prod P Q) R) ‚âÖ (prod P (prod Q R)) :=
{ hom :=
  prod.lift
    (prod.fst ‚â´ prod.fst)
    (prod.lift (prod.fst ‚â´ prod.snd) prod.snd),
  inv :=
  prod.lift
    (prod.lift prod.fst (prod.snd ‚â´ prod.fst))
    (prod.snd ‚â´ prod.snd) }

variables [has_terminal.{v} C]

/-- The left unitor isomorphism for binary products with the terminal object. -/
@[simp] def prod.left_unitor
  (P : C) : (prod (terminal C) P) ‚âÖ P :=
{ hom := prod.snd,
  inv := prod.lift (terminal.from P) (ùüô _) }

/-- The right unitor isomorphism for binary products with the terminal object. -/
@[simp] def prod.right_unitor
  (P : C) : (prod P (terminal C)) ‚âÖ P :=
{ hom := prod.fst,
  inv := prod.lift (ùüô _) (terminal.from P) }
end

section
variables {C} [has_binary_coproducts.{v} C]

local attribute [tidy] tactic.case_bash

/-- The braiding isomorphism which swaps a binary coproduct. -/
@[simp] def coprod.braiding (P Q : C) : coprod P Q ‚âÖ coprod Q P :=
{ hom := coprod.desc coprod.inr coprod.inl,
  inv := coprod.desc coprod.inr coprod.inl }

/-- The braiding isomorphism is symmetric. -/
def coprod.symmetry (P Q : C) :
  (coprod.braiding P Q).hom ‚â´ (coprod.braiding Q P).hom = ùüô _ :=
by tidy

/-- The associator isomorphism for binary coproducts. -/
@[simp] def coprod.associator
  (P Q R : C) : (coprod (coprod P Q) R) ‚âÖ (coprod P (coprod Q R)) :=
{ hom :=
  coprod.desc
    (coprod.desc coprod.inl (coprod.inl ‚â´ coprod.inr))
    (coprod.inr ‚â´ coprod.inr),
  inv :=
  coprod.desc
    (coprod.inl‚â´ coprod.inl)
    (coprod.desc (coprod.inr ‚â´ coprod.inl) coprod.inr) }

variables [has_initial.{v} C]

/-- The left unitor isomorphism for binary coproducts with the initial object. -/
@[simp] def coprod.left_unitor
  (P : C) : (coprod (initial C) P) ‚âÖ P :=
{ hom := coprod.desc (initial.to P) (ùüô _),
  inv := coprod.inr }

/-- The right unitor isomorphism for binary coproducts with the initial object. -/
@[simp] def coprod.right_unitor
  (P : C) : (coprod P (initial C)) ‚âÖ P :=
{ hom := coprod.desc (ùüô _) (initial.to P),
  inv := coprod.inl }
end

end category_theory.limits
