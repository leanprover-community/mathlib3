/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.limits.shapes.terminal
import category_theory.discrete_category
import category_theory.epi_mono
import category_theory.over

/-!
# Binary (co)products

We define a category `walking_pair`, which is the index category
for a binary (co)product diagram. A convenience method `pair X Y`
constructs the functor from the walking pair, hitting the given objects.

We define `prod X Y` and `coprod X Y` as limits and colimits of such functors.

Typeclasses `has_binary_products` and `has_binary_coproducts` assert the existence
of (co)limits shaped as walking pairs.

We include lemmas for simplifying equations involving projections and coprojections, and define
braiding and associating isomorphisms, and the product comparison morphism.

## References
* [Stacks: Products of pairs](https://stacks.math.columbia.edu/tag/001R)
* [Stacks: coproducts of pairs](https://stacks.math.columbia.edu/tag/04AN)
-/

noncomputable theory

universes v u uâ‚‚

open category_theory

namespace category_theory.limits

/-- The type of objects for the diagram indexing a binary (co)product. -/
@[derive decidable_eq, derive inhabited]
inductive walking_pair : Type v
| left | right

open walking_pair

/--
The equivalence swapping left and right.
-/
def walking_pair.swap : walking_pair â‰ƒ walking_pair :=
{ to_fun := Î» j, walking_pair.rec_on j right left,
  inv_fun := Î» j, walking_pair.rec_on j right left,
  left_inv := Î» j, by { cases j; refl, },
  right_inv := Î» j, by { cases j; refl, }, }

@[simp] lemma walking_pair.swap_apply_left : walking_pair.swap left = right := rfl
@[simp] lemma walking_pair.swap_apply_right : walking_pair.swap right = left := rfl
@[simp] lemma walking_pair.swap_symm_apply_tt : walking_pair.swap.symm left = right := rfl
@[simp] lemma walking_pair.swap_symm_apply_ff : walking_pair.swap.symm right = left := rfl

/--
An equivalence from `walking_pair` to `bool`, sometimes useful when reindexing limits.
-/
def walking_pair.equiv_bool : walking_pair â‰ƒ bool :=
{ to_fun := Î» j, walking_pair.rec_on j tt ff, -- to match equiv.sum_equiv_sigma_bool
  inv_fun := Î» b, bool.rec_on b right left,
  left_inv := Î» j, by { cases j; refl, },
  right_inv := Î» b, by { cases b; refl, }, }

@[simp] lemma walking_pair.equiv_bool_apply_left : walking_pair.equiv_bool left = tt := rfl
@[simp] lemma walking_pair.equiv_bool_apply_right : walking_pair.equiv_bool right = ff := rfl
@[simp] lemma walking_pair.equiv_bool_symm_apply_tt : walking_pair.equiv_bool.symm tt = left := rfl
@[simp] lemma walking_pair.equiv_bool_symm_apply_ff : walking_pair.equiv_bool.symm ff = right := rfl

variables {C : Type u} [category.{v} C]

/-- The diagram on the walking pair, sending the two points to `X` and `Y`. -/
def pair (X Y : C) : discrete walking_pair â¥¤ C :=
discrete.functor (Î» j, walking_pair.cases_on j X Y)

@[simp] lemma pair_obj_left (X Y : C) : (pair X Y).obj left = X := rfl
@[simp] lemma pair_obj_right (X Y : C) : (pair X Y).obj right = Y := rfl

section
variables {F G : discrete walking_pair.{v} â¥¤ C} (f : F.obj left âŸ¶ G.obj left)
  (g : F.obj right âŸ¶ G.obj right)

/-- The natural transformation between two functors out of the walking pair, specified by its
components. -/
def map_pair : F âŸ¶ G := { app := Î» j, walking_pair.cases_on j f g }

@[simp] lemma map_pair_left : (map_pair f g).app left = f := rfl
@[simp] lemma map_pair_right : (map_pair f g).app right = g := rfl

/-- The natural isomorphism between two functors out of the walking pair, specified by its
components. -/
@[simps]
def map_pair_iso (f : F.obj left â‰… G.obj left) (g : F.obj right â‰… G.obj right) : F â‰… G :=
nat_iso.of_components (Î» j, walking_pair.cases_on j f g) (by tidy)

end

/-- Every functor out of the walking pair is naturally isomorphic (actually, equal) to a `pair` -/
@[simps]
def diagram_iso_pair (F : discrete walking_pair â¥¤ C) :
  F â‰… pair (F.obj walking_pair.left) (F.obj walking_pair.right) :=
map_pair_iso (iso.refl _) (iso.refl _)

section
variables {D : Type u} [category.{v} D]

/-- The natural isomorphism between `pair X Y â‹™ F` and `pair (F.obj X) (F.obj Y)`. -/
def pair_comp (X Y : C) (F : C â¥¤ D) : pair X Y â‹™ F â‰… pair (F.obj X) (F.obj Y) :=
diagram_iso_pair _

end

/-- A binary fan is just a cone on a diagram indexing a product. -/
abbreviation binary_fan (X Y : C) := cone (pair X Y)

/-- The first projection of a binary fan. -/
abbreviation binary_fan.fst {X Y : C} (s : binary_fan X Y) := s.Ï€.app walking_pair.left

/-- The second projection of a binary fan. -/
abbreviation binary_fan.snd {X Y : C} (s : binary_fan X Y) := s.Ï€.app walking_pair.right

@[simp] lemma binary_fan.Ï€_app_left {X Y : C} (s : binary_fan X Y) :
  s.Ï€.app walking_pair.left = s.fst := rfl
@[simp] lemma binary_fan.Ï€_app_right {X Y : C} (s : binary_fan X Y) :
  s.Ï€.app walking_pair.right = s.snd := rfl

lemma binary_fan.is_limit.hom_ext {W X Y : C} {s : binary_fan X Y} (h : is_limit s)
  {f g : W âŸ¶ s.X} (hâ‚ : f â‰« s.fst = g â‰« s.fst) (hâ‚‚ : f â‰« s.snd = g â‰« s.snd) : f = g :=
h.hom_ext $ Î» j, walking_pair.cases_on j hâ‚ hâ‚‚

/-- A binary cofan is just a cocone on a diagram indexing a coproduct. -/
abbreviation binary_cofan (X Y : C) := cocone (pair X Y)

/-- The first inclusion of a binary cofan. -/
abbreviation binary_cofan.inl {X Y : C} (s : binary_cofan X Y) := s.Î¹.app walking_pair.left

/-- The second inclusion of a binary cofan. -/
abbreviation binary_cofan.inr {X Y : C} (s : binary_cofan X Y) := s.Î¹.app walking_pair.right

@[simp] lemma binary_cofan.Î¹_app_left {X Y : C} (s : binary_cofan X Y) :
  s.Î¹.app walking_pair.left = s.inl := rfl
@[simp] lemma binary_cofan.Î¹_app_right {X Y : C} (s : binary_cofan X Y) :
  s.Î¹.app walking_pair.right = s.inr := rfl

lemma binary_cofan.is_colimit.hom_ext {W X Y : C} {s : binary_cofan X Y} (h : is_colimit s)
  {f g : s.X âŸ¶ W} (hâ‚ : s.inl â‰« f = s.inl â‰« g) (hâ‚‚ : s.inr â‰« f = s.inr â‰« g) : f = g :=
h.hom_ext $ Î» j, walking_pair.cases_on j hâ‚ hâ‚‚

variables {X Y : C}

/-- A binary fan with vertex `P` consists of the two projections `Ï€â‚ : P âŸ¶ X` and `Ï€â‚‚ : P âŸ¶ Y`. -/
@[simps X]
def binary_fan.mk {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) : binary_fan X Y :=
{ X := P,
  Ï€ := { app := Î» j, walking_pair.cases_on j Ï€â‚ Ï€â‚‚ }}

/-- A binary cofan with vertex `P` consists of the two inclusions `Î¹â‚ : X âŸ¶ P` and `Î¹â‚‚ : Y âŸ¶ P`. -/
@[simps X]
def binary_cofan.mk {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) : binary_cofan X Y :=
{ X := P,
  Î¹ := { app := Î» j, walking_pair.cases_on j Î¹â‚ Î¹â‚‚ }}

@[simp] lemma binary_fan.mk_Ï€_app_left {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) :
  (binary_fan.mk Ï€â‚ Ï€â‚‚).Ï€.app walking_pair.left = Ï€â‚ := rfl
@[simp] lemma binary_fan.mk_Ï€_app_right {P : C} (Ï€â‚ : P âŸ¶ X) (Ï€â‚‚ : P âŸ¶ Y) :
  (binary_fan.mk Ï€â‚ Ï€â‚‚).Ï€.app walking_pair.right = Ï€â‚‚ := rfl
@[simp] lemma binary_cofan.mk_Î¹_app_left {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) :
  (binary_cofan.mk Î¹â‚ Î¹â‚‚).Î¹.app walking_pair.left = Î¹â‚ := rfl
@[simp] lemma binary_cofan.mk_Î¹_app_right {P : C} (Î¹â‚ : X âŸ¶ P) (Î¹â‚‚ : Y âŸ¶ P) :
  (binary_cofan.mk Î¹â‚ Î¹â‚‚).Î¹.app walking_pair.right = Î¹â‚‚ := rfl

/-- If `s` is a limit binary fan over `X` and `Y`, then every pair of morphisms `f : W âŸ¶ X` and
    `g : W âŸ¶ Y` induces a morphism `l : W âŸ¶ s.X` satisfying `l â‰« s.fst = f` and `l â‰« s.snd = g`.
    -/
@[simps]
def binary_fan.is_limit.lift' {W X Y : C} {s : binary_fan X Y} (h : is_limit s) (f : W âŸ¶ X)
  (g : W âŸ¶ Y) : {l : W âŸ¶ s.X // l â‰« s.fst = f âˆ§ l â‰« s.snd = g} :=
âŸ¨h.lift $ binary_fan.mk f g, h.fac _ _, h.fac _ _âŸ©

/-- If `s` is a colimit binary cofan over `X` and `Y`,, then every pair of morphisms `f : X âŸ¶ W` and
    `g : Y âŸ¶ W` induces a morphism `l : s.X âŸ¶ W` satisfying `s.inl â‰« l = f` and `s.inr â‰« l = g`.
    -/
@[simps]
def binary_cofan.is_colimit.desc' {W X Y : C} {s : binary_cofan X Y} (h : is_colimit s) (f : X âŸ¶ W)
  (g : Y âŸ¶ W) : {l : s.X âŸ¶ W // s.inl â‰« l = f âˆ§ s.inr â‰« l = g} :=
âŸ¨h.desc $ binary_cofan.mk f g, h.fac _ _, h.fac _ _âŸ©

/-- An abbreviation for `has_limit (pair X Y)`. -/
abbreviation has_binary_product (X Y : C) := has_limit (pair X Y)
/-- An abbreviation for `has_colimit (pair X Y)`. -/
abbreviation has_binary_coproduct (X Y : C) := has_colimit (pair X Y)

/-- If we have a product of `X` and `Y`, we can access it using `prod X Y` or
    `X â¨¯ Y`. -/
abbreviation prod (X Y : C) [has_binary_product X Y] := limit (pair X Y)

/-- If we have a coproduct of `X` and `Y`, we can access it using `coprod X Y ` or
    `X â¨¿ Y`. -/
abbreviation coprod (X Y : C) [has_binary_coproduct X Y] := colimit (pair X Y)

notation X ` â¨¯ `:20 Y:20 := prod X Y
notation X ` â¨¿ `:20 Y:20 := coprod X Y

/-- The projection map to the first component of the product. -/
abbreviation prod.fst {X Y : C} [has_binary_product X Y] : X â¨¯ Y âŸ¶ X :=
limit.Ï€ (pair X Y) walking_pair.left

/-- The projecton map to the second component of the product. -/
abbreviation prod.snd {X Y : C} [has_binary_product X Y] : X â¨¯ Y âŸ¶ Y :=
limit.Ï€ (pair X Y) walking_pair.right

/-- The inclusion map from the first component of the coproduct. -/
abbreviation coprod.inl {X Y : C} [has_binary_coproduct X Y] : X âŸ¶ X â¨¿ Y :=
colimit.Î¹ (pair X Y) walking_pair.left

/-- The inclusion map from the second component of the coproduct. -/
abbreviation coprod.inr {X Y : C} [has_binary_coproduct X Y] : Y âŸ¶ X â¨¿ Y :=
colimit.Î¹ (pair X Y) walking_pair.right

/-- The binary fan constructed from the projection maps is a limit. -/
def prod_is_prod (X Y : C) [has_binary_product X Y] :
  is_limit (binary_fan.mk (prod.fst : X â¨¯ Y âŸ¶ X) prod.snd) :=
(limit.is_limit _).of_iso_limit (cones.ext (iso.refl _) (by { rintro (_ | _), tidy }))

/-- The binary cofan constructed from the coprojection maps is a colimit. -/
def coprod_is_coprod (X Y : C) [has_binary_coproduct X Y] :
  is_colimit (binary_cofan.mk (coprod.inl : X âŸ¶ X â¨¿ Y) coprod.inr) :=
(colimit.is_colimit _).of_iso_colimit (cocones.ext (iso.refl _) (by { rintro (_ | _), tidy }))

@[ext] lemma prod.hom_ext {W X Y : C} [has_binary_product X Y] {f g : W âŸ¶ X â¨¯ Y}
  (hâ‚ : f â‰« prod.fst = g â‰« prod.fst) (hâ‚‚ : f â‰« prod.snd = g â‰« prod.snd) : f = g :=
binary_fan.is_limit.hom_ext (limit.is_limit _) hâ‚ hâ‚‚

@[ext] lemma coprod.hom_ext {W X Y : C} [has_binary_coproduct X Y] {f g : X â¨¿ Y âŸ¶ W}
  (hâ‚ : coprod.inl â‰« f = coprod.inl â‰« g) (hâ‚‚ : coprod.inr â‰« f = coprod.inr â‰« g) : f = g :=
binary_cofan.is_colimit.hom_ext (colimit.is_colimit _) hâ‚ hâ‚‚

/-- If the product of `X` and `Y` exists, then every pair of morphisms `f : W âŸ¶ X` and `g : W âŸ¶ Y`
    induces a morphism `prod.lift f g : W âŸ¶ X â¨¯ Y`. -/
abbreviation prod.lift {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) : W âŸ¶ X â¨¯ Y :=
limit.lift _ (binary_fan.mk f g)

/-- diagonal arrow of the binary product in the category `fam I` -/
abbreviation diag (X : C) [has_binary_product X X] : X âŸ¶ X â¨¯ X :=
prod.lift (ğŸ™ _) (ğŸ™ _)

/-- If the coproduct of `X` and `Y` exists, then every pair of morphisms `f : X âŸ¶ W` and
    `g : Y âŸ¶ W` induces a morphism `coprod.desc f g : X â¨¿ Y âŸ¶ W`. -/
abbreviation coprod.desc {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  X â¨¿ Y âŸ¶ W :=
colimit.desc _ (binary_cofan.mk f g)

/-- codiagonal arrow of the binary coproduct -/
abbreviation codiag (X : C) [has_binary_coproduct X X] : X â¨¿ X âŸ¶ X :=
coprod.desc (ğŸ™ _) (ğŸ™ _)

@[simp, reassoc]
lemma prod.lift_fst {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  prod.lift f g â‰« prod.fst = f :=
limit.lift_Ï€ _ _

@[simp, reassoc]
lemma prod.lift_snd {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  prod.lift f g â‰« prod.snd = g :=
limit.lift_Ï€ _ _

-- The simp linter says simp can prove the reassoc version of this lemma.
@[reassoc, simp]
lemma coprod.inl_desc {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  coprod.inl â‰« coprod.desc f g = f :=
colimit.Î¹_desc _ _

-- The simp linter says simp can prove the reassoc version of this lemma.
@[reassoc, simp]
lemma coprod.inr_desc {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  coprod.inr â‰« coprod.desc f g = g :=
colimit.Î¹_desc _ _

instance prod.mono_lift_of_mono_left {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
  [mono f] : mono (prod.lift f g) :=
mono_of_mono_fac $ prod.lift_fst _ _

instance prod.mono_lift_of_mono_right {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y)
  [mono g] : mono (prod.lift f g) :=
mono_of_mono_fac $ prod.lift_snd _ _

instance coprod.epi_desc_of_epi_left {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
  [epi f] : epi (coprod.desc f g) :=
epi_of_epi_fac $ coprod.inl_desc _ _

instance coprod.epi_desc_of_epi_right {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W)
  [epi g] : epi (coprod.desc f g) :=
epi_of_epi_fac $ coprod.inr_desc _ _

/-- If the product of `X` and `Y` exists, then every pair of morphisms `f : W âŸ¶ X` and `g : W âŸ¶ Y`
    induces a morphism `l : W âŸ¶ X â¨¯ Y` satisfying `l â‰« prod.fst = f` and `l â‰« prod.snd = g`. -/
def prod.lift' {W X Y : C} [has_binary_product X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  {l : W âŸ¶ X â¨¯ Y // l â‰« prod.fst = f âˆ§ l â‰« prod.snd = g} :=
âŸ¨prod.lift f g, prod.lift_fst _ _, prod.lift_snd _ _âŸ©

/-- If the coproduct of `X` and `Y` exists, then every pair of morphisms `f : X âŸ¶ W` and
    `g : Y âŸ¶ W` induces a morphism `l : X â¨¿ Y âŸ¶ W` satisfying `coprod.inl â‰« l = f` and
    `coprod.inr â‰« l = g`. -/
def coprod.desc' {W X Y : C} [has_binary_coproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  {l : X â¨¿ Y âŸ¶ W // coprod.inl â‰« l = f âˆ§ coprod.inr â‰« l = g} :=
âŸ¨coprod.desc f g, coprod.inl_desc _ _, coprod.inr_desc _ _âŸ©

/-- If the products `W â¨¯ X` and `Y â¨¯ Z` exist, then every pair of morphisms `f : W âŸ¶ Y` and
    `g : X âŸ¶ Z` induces a morphism `prod.map f g : W â¨¯ X âŸ¶ Y â¨¯ Z`. -/
def prod.map {W X Y Z : C} [has_binary_product W X] [has_binary_product Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W â¨¯ X âŸ¶ Y â¨¯ Z :=
lim_map (map_pair f g)

/-- If the coproducts `W â¨¿ X` and `Y â¨¿ Z` exist, then every pair of morphisms `f : W âŸ¶ Y` and
    `g : W âŸ¶ Z` induces a morphism `coprod.map f g : W â¨¿ X âŸ¶ Y â¨¿ Z`. -/
def coprod.map {W X Y Z : C} [has_binary_coproduct W X] [has_binary_coproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W â¨¿ X âŸ¶ Y â¨¿ Z :=
colim_map (map_pair f g)

section prod_lemmas

-- Making the reassoc version of this a simp lemma seems to be more harmful than helpful.
@[reassoc, simp]
lemma prod.comp_lift {V W X Y : C} [has_binary_product X Y] (f : V âŸ¶ W) (g : W âŸ¶ X) (h : W âŸ¶ Y) :
  f â‰« prod.lift g h = prod.lift (f â‰« g) (f â‰« h) :=
by { ext; simp }

lemma prod.comp_diag {X Y : C} [has_binary_product Y Y] (f : X âŸ¶ Y) :
  f â‰« diag Y = prod.lift f f :=
by simp

@[simp, reassoc]
lemma prod.map_fst {W X Y Z : C} [has_binary_product W X] [has_binary_product Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : prod.map f g â‰« prod.fst = prod.fst â‰« f :=
lim_map_Ï€ _ _

@[simp, reassoc]
lemma prod.map_snd {W X Y Z : C} [has_binary_product W X] [has_binary_product Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : prod.map f g â‰« prod.snd = prod.snd â‰« g :=
lim_map_Ï€ _ _

@[simp] lemma prod.map_id_id {X Y : C} [has_binary_product X Y] :
  prod.map (ğŸ™ X) (ğŸ™ Y) = ğŸ™ _ :=
by { ext; simp }

@[simp] lemma prod.lift_fst_snd {X Y : C} [has_binary_product X Y] :
  prod.lift prod.fst prod.snd = ğŸ™ (X â¨¯ Y) :=
by { ext; simp }

@[simp, reassoc] lemma prod.lift_map {V W X Y Z : C} [has_binary_product W X]
  [has_binary_product Y Z] (f : V âŸ¶ W) (g : V âŸ¶ X) (h : W âŸ¶ Y) (k : X âŸ¶ Z) :
  prod.lift f g â‰« prod.map h k = prod.lift (f â‰« h) (g â‰« k) :=
by { ext; simp }

@[simp] lemma prod.lift_fst_comp_snd_comp {W X Y Z : C} [has_binary_product W Y]
  [has_binary_product X Z] (g : W âŸ¶ X) (g' : Y âŸ¶ Z) :
  prod.lift (prod.fst â‰« g) (prod.snd â‰« g') = prod.map g g' :=
by { rw â† prod.lift_map, simp }

-- We take the right hand side here to be simp normal form, as this way composition lemmas for
-- `f â‰« h` and `g â‰« k` can fire (eg `id_comp`) , while `map_fst` and `map_snd` can still work just
-- as well.
@[simp, reassoc]
lemma prod.map_map {Aâ‚ Aâ‚‚ Aâ‚ƒ Bâ‚ Bâ‚‚ Bâ‚ƒ : C}
  [has_binary_product Aâ‚ Bâ‚] [has_binary_product Aâ‚‚ Bâ‚‚] [has_binary_product Aâ‚ƒ Bâ‚ƒ]
  (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Bâ‚ âŸ¶ Bâ‚‚) (h : Aâ‚‚ âŸ¶ Aâ‚ƒ) (k : Bâ‚‚ âŸ¶ Bâ‚ƒ) :
  prod.map f g â‰« prod.map h k = prod.map (f â‰« h) (g â‰« k) :=
by { ext; simp }

-- TODO: is it necessary to weaken the assumption here?
@[reassoc]
lemma prod.map_swap {A B X Y : C} (f : A âŸ¶ B) (g : X âŸ¶ Y)
  [has_limits_of_shape (discrete walking_pair) C] :
  prod.map (ğŸ™ X) f â‰« prod.map g (ğŸ™ B) = prod.map g (ğŸ™ A) â‰« prod.map (ğŸ™ Y) f :=
by simp

@[reassoc] lemma prod.map_comp_id {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [has_binary_product X W] [has_binary_product Z W] [has_binary_product Y W] :
  prod.map (f â‰« g) (ğŸ™ W) = prod.map f (ğŸ™ W) â‰« prod.map g (ğŸ™ W) :=
by simp

@[reassoc] lemma prod.map_id_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [has_binary_product W X] [has_binary_product W Y] [has_binary_product W Z] :
  prod.map (ğŸ™ W) (f â‰« g) = prod.map (ğŸ™ W) f â‰« prod.map (ğŸ™ W) g :=
by simp

/-- If the products `W â¨¯ X` and `Y â¨¯ Z` exist, then every pair of isomorphisms `f : W â‰… Y` and
    `g : X â‰… Z` induces an isomorphism `prod.map_iso f g : W â¨¯ X â‰… Y â¨¯ Z`. -/
@[simps]
def prod.map_iso {W X Y Z : C} [has_binary_product W X] [has_binary_product Y Z]
  (f : W â‰… Y) (g : X â‰… Z) : W â¨¯ X â‰… Y â¨¯ Z :=
{ hom := prod.map f.hom g.hom,
  inv := prod.map f.inv g.inv }

instance is_iso_prod {W X Y Z : C} [has_binary_product W X] [has_binary_product Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) [is_iso f] [is_iso g] : is_iso (prod.map f g) :=
is_iso.of_iso (prod.map_iso (as_iso f) (as_iso g))

@[simp, reassoc]
lemma prod.diag_map {X Y : C} (f : X âŸ¶ Y) [has_binary_product X X] [has_binary_product Y Y] :
  diag X â‰« prod.map f f = f â‰« diag Y :=
by simp

@[simp, reassoc]
lemma prod.diag_map_fst_snd {X Y : C} [has_binary_product X Y]
  [has_binary_product (X â¨¯ Y) (X â¨¯ Y)] :
  diag (X â¨¯ Y) â‰« prod.map prod.fst prod.snd = ğŸ™ (X â¨¯ Y) :=
by simp

@[simp, reassoc]
lemma prod.diag_map_fst_snd_comp  [has_limits_of_shape (discrete walking_pair) C]
  {X X' Y Y' : C} (g : X âŸ¶ Y) (g' : X' âŸ¶ Y') :
  diag (X â¨¯ X') â‰« prod.map (prod.fst â‰« g) (prod.snd â‰« g') = prod.map g g' :=
by simp

instance {X : C} [has_binary_product X X] : split_mono (diag X) :=
{ retraction := prod.fst }

end prod_lemmas

section coprod_lemmas

@[simp, reassoc]
lemma coprod.desc_comp {V W X Y : C} [has_binary_coproduct X Y] (f : V âŸ¶ W) (g : X âŸ¶ V)
  (h : Y âŸ¶ V) :
  coprod.desc g h â‰« f = coprod.desc (g â‰« f) (h â‰« f) :=
by { ext; simp }

lemma coprod.diag_comp {X Y : C} [has_binary_coproduct X X] (f : X âŸ¶ Y) :
  codiag X â‰« f = coprod.desc f f :=
by simp

@[simp, reassoc]
lemma coprod.inl_map {W X Y Z : C} [has_binary_coproduct W X] [has_binary_coproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : coprod.inl â‰« coprod.map f g = f â‰« coprod.inl :=
Î¹_colim_map _ _

@[simp, reassoc]
lemma coprod.inr_map {W X Y Z : C} [has_binary_coproduct W X] [has_binary_coproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : coprod.inr â‰« coprod.map f g = g â‰« coprod.inr :=
Î¹_colim_map _ _

@[simp]
lemma coprod.map_id_id {X Y : C} [has_binary_coproduct X Y] :
  coprod.map (ğŸ™ X) (ğŸ™ Y) = ğŸ™ _ :=
by { ext; simp }

@[simp]
lemma coprod.desc_inl_inr {X Y : C} [has_binary_coproduct X Y] :
  coprod.desc coprod.inl coprod.inr = ğŸ™ (X â¨¿ Y) :=
by { ext; simp }

-- The simp linter says simp can prove the reassoc version of this lemma.
@[reassoc, simp]
lemma coprod.map_desc {S T U V W : C} [has_binary_coproduct U W] [has_binary_coproduct T V]
  (f : U âŸ¶ S) (g : W âŸ¶ S) (h : T âŸ¶ U) (k : V âŸ¶ W) :
  coprod.map h k â‰« coprod.desc f g = coprod.desc (h â‰« f) (k â‰« g) :=
by { ext; simp }

@[simp]
lemma coprod.desc_comp_inl_comp_inr {W X Y Z : C}
  [has_binary_coproduct W Y] [has_binary_coproduct X Z]
  (g : W âŸ¶ X) (g' : Y âŸ¶ Z) :
  coprod.desc (g â‰« coprod.inl) (g' â‰« coprod.inr) = coprod.map g g' :=
by { rw â† coprod.map_desc, simp }

-- We take the right hand side here to be simp normal form, as this way composition lemmas for
-- `f â‰« h` and `g â‰« k` can fire (eg `id_comp`) , while `inl_map` and `inr_map` can still work just
-- as well.
@[simp, reassoc]
lemma coprod.map_map {Aâ‚ Aâ‚‚ Aâ‚ƒ Bâ‚ Bâ‚‚ Bâ‚ƒ : C}
  [has_binary_coproduct Aâ‚ Bâ‚] [has_binary_coproduct Aâ‚‚ Bâ‚‚] [has_binary_coproduct Aâ‚ƒ Bâ‚ƒ]
  (f : Aâ‚ âŸ¶ Aâ‚‚) (g : Bâ‚ âŸ¶ Bâ‚‚) (h : Aâ‚‚ âŸ¶ Aâ‚ƒ) (k : Bâ‚‚ âŸ¶ Bâ‚ƒ) :
  coprod.map f g â‰« coprod.map h k = coprod.map (f â‰« h) (g â‰« k) :=
by { ext; simp }

-- I don't think it's a good idea to make any of the following three simp lemmas.
@[reassoc]
lemma coprod.map_swap {A B X Y : C} (f : A âŸ¶ B) (g : X âŸ¶ Y)
  [has_colimits_of_shape (discrete walking_pair) C] :
  coprod.map (ğŸ™ X) f â‰« coprod.map g (ğŸ™ B) = coprod.map g (ğŸ™ A) â‰« coprod.map (ğŸ™ Y) f :=
by simp

@[reassoc] lemma coprod.map_comp_id {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [has_binary_coproduct Z W] [has_binary_coproduct Y W] [has_binary_coproduct X W] :
  coprod.map (f â‰« g) (ğŸ™ W) = coprod.map f (ğŸ™ W) â‰« coprod.map g (ğŸ™ W) :=
by simp

@[reassoc] lemma coprod.map_id_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [has_binary_coproduct W X] [has_binary_coproduct W Y] [has_binary_coproduct W Z] :
  coprod.map (ğŸ™ W) (f â‰« g) = coprod.map (ğŸ™ W) f â‰« coprod.map (ğŸ™ W) g :=
by simp

/-- If the coproducts `W â¨¿ X` and `Y â¨¿ Z` exist, then every pair of isomorphisms `f : W â‰… Y` and
    `g : W â‰… Z` induces a isomorphism `coprod.map_iso f g : W â¨¿ X â‰… Y â¨¿ Z`. -/
@[simps]
def coprod.map_iso {W X Y Z : C} [has_binary_coproduct W X] [has_binary_coproduct Y Z]
  (f : W â‰… Y) (g : X â‰… Z) : W â¨¿ X â‰… Y â¨¿ Z :=
{ hom := coprod.map f.hom g.hom,
  inv := coprod.map f.inv g.inv }

instance is_iso_coprod {W X Y Z : C} [has_binary_coproduct W X] [has_binary_coproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) [is_iso f] [is_iso g] : is_iso (coprod.map f g) :=
is_iso.of_iso (coprod.map_iso (as_iso f) (as_iso g))

-- The simp linter says simp can prove the reassoc version of this lemma.
@[reassoc, simp]
lemma coprod.map_codiag {X Y : C} (f : X âŸ¶ Y) [has_binary_coproduct X X]
  [has_binary_coproduct Y Y] :
  coprod.map f f â‰« codiag Y = codiag X â‰« f :=
by simp

-- The simp linter says simp can prove the reassoc version of this lemma.
@[reassoc, simp]
lemma coprod.map_inl_inr_codiag {X Y : C} [has_binary_coproduct X Y]
  [has_binary_coproduct (X â¨¿ Y) (X â¨¿ Y)] :
  coprod.map coprod.inl coprod.inr â‰« codiag (X â¨¿ Y) = ğŸ™ (X â¨¿ Y) :=
by simp

-- The simp linter says simp can prove the reassoc version of this lemma.
@[reassoc, simp]
lemma coprod.map_comp_inl_inr_codiag [has_colimits_of_shape (discrete walking_pair) C]
  {X X' Y Y' : C} (g : X âŸ¶ Y) (g' : X' âŸ¶ Y') :
  coprod.map (g â‰« coprod.inl) (g' â‰« coprod.inr) â‰« codiag (Y â¨¿ Y') = coprod.map g g' :=
by simp

end coprod_lemmas

variables (C)

/--
`has_binary_products` represents a choice of product for every pair of objects.

See https://stacks.math.columbia.edu/tag/001T.
-/
abbreviation has_binary_products := has_limits_of_shape (discrete walking_pair) C

/--
`has_binary_coproducts` represents a choice of coproduct for every pair of objects.

See https://stacks.math.columbia.edu/tag/04AP.
-/
abbreviation has_binary_coproducts := has_colimits_of_shape (discrete walking_pair) C

/-- If `C` has all limits of diagrams `pair X Y`, then it has all binary products -/
lemma has_binary_products_of_has_limit_pair [Î  {X Y : C}, has_limit (pair X Y)] :
  has_binary_products C :=
{ has_limit := Î» F, has_limit_of_iso (diagram_iso_pair F).symm }

/-- If `C` has all colimits of diagrams `pair X Y`, then it has all binary coproducts -/
lemma has_binary_coproducts_of_has_colimit_pair [Î  {X Y : C}, has_colimit (pair X Y)] :
  has_binary_coproducts C :=
{ has_colimit := Î» F, has_colimit_of_iso (diagram_iso_pair F) }

section
variables {C}

/-- The braiding isomorphism which swaps a binary product. -/
@[simps] def prod.braiding (P Q : C) [has_binary_product P Q] [has_binary_product Q P] :
  P â¨¯ Q â‰… Q â¨¯ P :=
{ hom := prod.lift prod.snd prod.fst,
  inv := prod.lift prod.snd prod.fst }

/-- The braiding isomorphism can be passed through a map by swapping the order. -/
@[reassoc] lemma braid_natural [has_binary_products C] {W X Y Z : C} (f : X âŸ¶ Y) (g : Z âŸ¶ W) :
  prod.map f g â‰« (prod.braiding _ _).hom = (prod.braiding _ _).hom â‰« prod.map g f :=
by simp

@[reassoc] lemma prod.symmetry' (P Q : C) [has_binary_product P Q] [has_binary_product Q P] :
  prod.lift prod.snd prod.fst â‰« prod.lift prod.snd prod.fst = ğŸ™ (P â¨¯ Q) :=
(prod.braiding _ _).hom_inv_id

/-- The braiding isomorphism is symmetric. -/
@[reassoc] lemma prod.symmetry (P Q : C) [has_binary_product P Q] [has_binary_product Q P] :
  (prod.braiding P Q).hom â‰« (prod.braiding Q P).hom = ğŸ™ _ :=
(prod.braiding _ _).hom_inv_id

/-- The associator isomorphism for binary products. -/
@[simps] def prod.associator [has_binary_products C] (P Q R : C) :
  (P â¨¯ Q) â¨¯ R â‰… P â¨¯ (Q â¨¯ R) :=
{ hom :=
  prod.lift
    (prod.fst â‰« prod.fst)
    (prod.lift (prod.fst â‰« prod.snd) prod.snd),
  inv :=
  prod.lift
    (prod.lift prod.fst (prod.snd â‰« prod.fst))
    (prod.snd â‰« prod.snd) }

@[reassoc]
lemma prod.pentagon [has_binary_products C] (W X Y Z : C) :
  prod.map ((prod.associator W X Y).hom) (ğŸ™ Z) â‰«
      (prod.associator W (X â¨¯ Y) Z).hom â‰« prod.map (ğŸ™ W) ((prod.associator X Y Z).hom) =
    (prod.associator (W â¨¯ X) Y Z).hom â‰« (prod.associator W X (Y â¨¯ Z)).hom :=
by simp

@[reassoc]
lemma prod.associator_naturality [has_binary_products C] {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C}
  (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  prod.map (prod.map fâ‚ fâ‚‚) fâ‚ƒ â‰« (prod.associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
    (prod.associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« prod.map fâ‚ (prod.map fâ‚‚ fâ‚ƒ) :=
by simp

variables [has_terminal C]

/-- The left unitor isomorphism for binary products with the terminal object. -/
@[simps] def prod.left_unitor (P : C) [has_binary_product (âŠ¤_ C) P] :
  âŠ¤_ C â¨¯ P â‰… P :=
{ hom := prod.snd,
  inv := prod.lift (terminal.from P) (ğŸ™ _) }

/-- The right unitor isomorphism for binary products with the terminal object. -/
@[simps] def prod.right_unitor (P : C) [has_binary_product P (âŠ¤_ C)] :
  P â¨¯ âŠ¤_ C â‰… P :=
{ hom := prod.fst,
  inv := prod.lift (ğŸ™ _) (terminal.from P) }

@[reassoc]
lemma prod.left_unitor_hom_naturality [has_binary_products C] (f : X âŸ¶ Y) :
  prod.map (ğŸ™ _) f â‰« (prod.left_unitor Y).hom = (prod.left_unitor X).hom â‰« f :=
prod.map_snd _ _

@[reassoc]
lemma prod.left_unitor_inv_naturality [has_binary_products C] (f : X âŸ¶ Y) :
  (prod.left_unitor X).inv â‰« prod.map (ğŸ™ _) f = f â‰« (prod.left_unitor Y).inv :=
by rw [iso.inv_comp_eq, â† category.assoc, iso.eq_comp_inv, prod.left_unitor_hom_naturality]

@[reassoc]
lemma prod.right_unitor_hom_naturality [has_binary_products C] (f : X âŸ¶ Y) :
  prod.map f (ğŸ™ _) â‰« (prod.right_unitor Y).hom = (prod.right_unitor X).hom â‰« f :=
prod.map_fst _ _

@[reassoc]
lemma prod_right_unitor_inv_naturality [has_binary_products C] (f : X âŸ¶ Y) :
  (prod.right_unitor X).inv â‰« prod.map f (ğŸ™ _) = f â‰« (prod.right_unitor Y).inv :=
by rw [iso.inv_comp_eq, â† category.assoc, iso.eq_comp_inv, prod.right_unitor_hom_naturality]

lemma prod.triangle [has_binary_products C] (X Y : C) :
  (prod.associator X (âŠ¤_ C) Y).hom â‰« prod.map (ğŸ™ X) ((prod.left_unitor Y).hom) =
    prod.map ((prod.right_unitor X).hom) (ğŸ™ Y) :=
by tidy

end

section

variables {C} [has_binary_coproducts C]

/-- The braiding isomorphism which swaps a binary coproduct. -/
@[simps] def coprod.braiding (P Q : C) : P â¨¿ Q â‰… Q â¨¿ P :=
{ hom := coprod.desc coprod.inr coprod.inl,
  inv := coprod.desc coprod.inr coprod.inl }

@[reassoc] lemma coprod.symmetry' (P Q : C) :
  coprod.desc coprod.inr coprod.inl â‰« coprod.desc coprod.inr coprod.inl = ğŸ™ (P â¨¿ Q) :=
(coprod.braiding _ _).hom_inv_id

/-- The braiding isomorphism is symmetric. -/
lemma coprod.symmetry (P Q : C) :
  (coprod.braiding P Q).hom â‰« (coprod.braiding Q P).hom = ğŸ™ _ :=
coprod.symmetry' _ _

/-- The associator isomorphism for binary coproducts. -/
@[simps] def coprod.associator
  (P Q R : C) : (P â¨¿ Q) â¨¿ R â‰… P â¨¿ (Q â¨¿ R) :=
{ hom :=
  coprod.desc
    (coprod.desc coprod.inl (coprod.inl â‰« coprod.inr))
    (coprod.inr â‰« coprod.inr),
  inv :=
  coprod.desc
    (coprod.inl â‰« coprod.inl)
    (coprod.desc (coprod.inr â‰« coprod.inl) coprod.inr) }

lemma coprod.pentagon (W X Y Z : C) :
  coprod.map ((coprod.associator W X Y).hom) (ğŸ™ Z) â‰«
      (coprod.associator W (X â¨¿ Y) Z).hom â‰« coprod.map (ğŸ™ W) ((coprod.associator X Y Z).hom) =
    (coprod.associator (W â¨¿ X) Y Z).hom â‰« (coprod.associator W X (Y â¨¿ Z)).hom :=
by simp

lemma coprod.associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
  (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  coprod.map (coprod.map fâ‚ fâ‚‚) fâ‚ƒ â‰« (coprod.associator Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
    (coprod.associator Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« coprod.map fâ‚ (coprod.map fâ‚‚ fâ‚ƒ) :=
by simp

variables [has_initial C]

/-- The left unitor isomorphism for binary coproducts with the initial object. -/
@[simps] def coprod.left_unitor
  (P : C) : âŠ¥_ C â¨¿ P â‰… P :=
{ hom := coprod.desc (initial.to P) (ğŸ™ _),
  inv := coprod.inr }

/-- The right unitor isomorphism for binary coproducts with the initial object. -/
@[simps] def coprod.right_unitor
  (P : C) : P â¨¿ âŠ¥_ C â‰… P :=
{ hom := coprod.desc (ğŸ™ _) (initial.to P),
  inv := coprod.inl }

lemma coprod.triangle (X Y : C) :
  (coprod.associator X (âŠ¥_ C) Y).hom â‰« coprod.map (ğŸ™ X) ((coprod.left_unitor Y).hom) =
    coprod.map ((coprod.right_unitor X).hom) (ğŸ™ Y) :=
by tidy

end

section prod_functor
variables {C} [has_binary_products C]

/-- The binary product functor. -/
@[simps]
def prod.functor : C â¥¤ C â¥¤ C :=
{ obj := Î» X, { obj := Î» Y, X â¨¯ Y, map := Î» Y Z, prod.map (ğŸ™ X) },
  map := Î» Y Z f, { app := Î» T, prod.map f (ğŸ™ T) }}

/-- The product functor can be decomposed. -/
def prod.functor_left_comp (X Y : C) :
  prod.functor.obj (X â¨¯ Y) â‰… prod.functor.obj Y â‹™ prod.functor.obj X :=
nat_iso.of_components (prod.associator _ _) (by tidy)

end prod_functor

section coprod_functor
variables {C} [has_binary_coproducts C]

/-- The binary coproduct functor. -/
@[simps]
def coprod.functor : C â¥¤ C â¥¤ C :=
{ obj := Î» X, { obj := Î» Y, X â¨¿ Y, map := Î» Y Z, coprod.map (ğŸ™ X) },
  map := Î» Y Z f, { app := Î» T, coprod.map f (ğŸ™ T) }}

/-- The coproduct functor can be decomposed. -/
def coprod.functor_left_comp (X Y : C) :
  coprod.functor.obj (X â¨¿ Y) â‰… coprod.functor.obj Y â‹™ coprod.functor.obj X :=
nat_iso.of_components (coprod.associator _ _) (by tidy)

end coprod_functor

section prod_comparison

variables {C} {D : Type uâ‚‚} [category.{v} D]
variables (F : C â¥¤ D) {A A' B B' : C}
variables [has_binary_product A B] [has_binary_product A' B']
variables [has_binary_product (F.obj A) (F.obj B)] [has_binary_product (F.obj A') (F.obj B')]
/--
The product comparison morphism.

In `category_theory/limits/preserves` we show this is always an iso iff F preserves binary products.
-/
def prod_comparison (F : C â¥¤ D) (A B : C)
  [has_binary_product A B] [has_binary_product (F.obj A) (F.obj B)] :
  F.obj (A â¨¯ B) âŸ¶ F.obj A â¨¯ F.obj B :=
prod.lift (F.map prod.fst) (F.map prod.snd)

@[simp, reassoc]
lemma prod_comparison_fst :
  prod_comparison F A B â‰« prod.fst = F.map prod.fst :=
prod.lift_fst _ _

@[simp, reassoc]
lemma prod_comparison_snd :
  prod_comparison F A B â‰« prod.snd = F.map prod.snd :=
prod.lift_snd _ _

/-- Naturality of the prod_comparison morphism in both arguments. -/
@[reassoc] lemma prod_comparison_natural (f : A âŸ¶ A') (g : B âŸ¶ B') :
  F.map (prod.map f g) â‰« prod_comparison F A' B' =
    prod_comparison F A B â‰« prod.map (F.map f) (F.map g) :=
begin
  rw [prod_comparison, prod_comparison, prod.lift_map, â† F.map_comp, â† F.map_comp,
      prod.comp_lift, â† F.map_comp, prod.map_fst, â† F.map_comp, prod.map_snd]
end

/--
The product comparison morphism from `F(A â¨¯ -)` to `FA â¨¯ F-`, whose components are given by
`prod_comparison`.
-/
@[simps]
def prod_comparison_nat_trans [has_binary_products C] [has_binary_products D]
  (F : C â¥¤ D) (A : C) :
  prod.functor.obj A â‹™ F âŸ¶ F â‹™ prod.functor.obj (F.obj A) :=
{ app := Î» B, prod_comparison F A B,
  naturality' := Î» B B' f, by simp [prod_comparison_natural] }

@[reassoc]
lemma inv_prod_comparison_map_fst [is_iso (prod_comparison F A B)] :
  inv (prod_comparison F A B) â‰« F.map prod.fst = prod.fst :=
by simp [is_iso.inv_comp_eq]

@[reassoc]
lemma inv_prod_comparison_map_snd [is_iso (prod_comparison F A B)] :
  inv (prod_comparison F A B) â‰« F.map prod.snd = prod.snd :=
by simp [is_iso.inv_comp_eq]

/-- If the product comparison morphism is an iso, its inverse is natural. -/
@[reassoc]
lemma prod_comparison_inv_natural (f : A âŸ¶ A') (g : B âŸ¶ B')
  [is_iso (prod_comparison F A B)] [is_iso (prod_comparison F A' B')] :
  inv (prod_comparison F A B) â‰« F.map (prod.map f g) =
    prod.map (F.map f) (F.map g) â‰« inv (prod_comparison F A' B') :=
by rw [is_iso.eq_comp_inv, category.assoc, is_iso.inv_comp_eq, prod_comparison_natural]

/--
The natural isomorphism `F(A â¨¯ -) â‰… FA â¨¯ F-`, provided each `prod_comparison F A B` is an
isomorphism (as `B` changes).
-/
@[simps {rhs_md := semireducible}]
def prod_comparison_nat_iso [has_binary_products C] [has_binary_products D]
  (A : C) [âˆ€ B, is_iso (prod_comparison F A B)] :
  prod.functor.obj A â‹™ F â‰… F â‹™ prod.functor.obj (F.obj A) :=
{ hom := prod_comparison_nat_trans F A
  ..(@as_iso _ _ _ _ _ (nat_iso.is_iso_of_is_iso_app âŸ¨_, _âŸ©)) }

end prod_comparison

section coprod_comparison

variables {C} {D : Type uâ‚‚} [category.{v} D]
variables (F : C â¥¤ D) {A A' B B' : C}
variables [has_binary_coproduct A B] [has_binary_coproduct A' B']
variables [has_binary_coproduct (F.obj A) (F.obj B)] [has_binary_coproduct (F.obj A') (F.obj B')]
/--
The coproduct comparison morphism.

In `category_theory/limits/preserves` we show
this is always an iso iff F preserves binary coproducts.
-/
def coprod_comparison (F : C â¥¤ D) (A B : C)
  [has_binary_coproduct A B] [has_binary_coproduct (F.obj A) (F.obj B)] :
  F.obj A â¨¿ F.obj B âŸ¶ F.obj (A â¨¿ B) :=
coprod.desc (F.map coprod.inl) (F.map coprod.inr)

@[simp, reassoc]
lemma coprod_comparison_inl :
  coprod.inl â‰« coprod_comparison F A B  = F.map coprod.inl :=
coprod.inl_desc _ _

@[simp, reassoc]
lemma coprod_comparison_inr :
  coprod.inr â‰« coprod_comparison F A B = F.map coprod.inr :=
coprod.inr_desc _ _

/-- Naturality of the coprod_comparison morphism in both arguments. -/
@[reassoc] lemma coprod_comparison_natural (f : A âŸ¶ A') (g : B âŸ¶ B') :
  coprod_comparison F A B â‰« F.map (coprod.map f g) =
    coprod.map (F.map f) (F.map g) â‰« coprod_comparison F A' B' :=
begin
  rw [coprod_comparison, coprod_comparison, coprod.map_desc, â† F.map_comp, â† F.map_comp,
      coprod.desc_comp, â† F.map_comp, coprod.inl_map, â† F.map_comp, coprod.inr_map]
end

/--
The coproduct comparison morphism from `FA â¨¿ F-` to `F(A â¨¿ -)`, whose components are given by
`coprod_comparison`.
-/
@[simps]
def coprod_comparison_nat_trans [has_binary_coproducts C] [has_binary_coproducts D]
  (F : C â¥¤ D) (A : C) :
  F â‹™ coprod.functor.obj (F.obj A) âŸ¶ coprod.functor.obj A â‹™ F :=
{ app := Î» B, coprod_comparison F A B,
  naturality' := Î» B B' f, by simp [coprod_comparison_natural] }

@[reassoc]
lemma map_inl_inv_coprod_comparison [is_iso (coprod_comparison F A B)] :
  F.map coprod.inl â‰« inv (coprod_comparison F A B) = coprod.inl :=
by simp [is_iso.inv_comp_eq]

@[reassoc]
lemma map_inr_inv_coprod_comparison [is_iso (coprod_comparison F A B)] :
  F.map coprod.inr â‰« inv (coprod_comparison F A B) = coprod.inr :=
by simp [is_iso.inv_comp_eq]

/-- If the coproduct comparison morphism is an iso, its inverse is natural. -/
@[reassoc]
lemma coprod_comparison_inv_natural (f : A âŸ¶ A') (g : B âŸ¶ B')
  [is_iso (coprod_comparison F A B)] [is_iso (coprod_comparison F A' B')] :
  inv (coprod_comparison F A B) â‰« coprod.map (F.map f) (F.map g) =
    F.map (coprod.map f g) â‰« inv (coprod_comparison F A' B') :=
by rw [is_iso.eq_comp_inv, category.assoc, is_iso.inv_comp_eq, coprod_comparison_natural]

/--
The natural isomorphism `FA â¨¿ F- â‰… F(A â¨¿ -)`, provided each `coprod_comparison F A B` is an
isomorphism (as `B` changes).
-/
@[simps {rhs_md := semireducible}]
def coprod_comparison_nat_iso [has_binary_coproducts C] [has_binary_coproducts D]
  (A : C) [âˆ€ B, is_iso (coprod_comparison F A B)] :
  F â‹™ coprod.functor.obj (F.obj A) â‰… coprod.functor.obj A â‹™ F :=
{ hom := coprod_comparison_nat_trans F A
  ..(@as_iso _ _ _ _ _ (nat_iso.is_iso_of_is_iso_app âŸ¨_, _âŸ©)) }

end coprod_comparison

end category_theory.limits

open category_theory.limits

namespace category_theory

variables {C : Type u} [category.{v} C]

/-- Auxiliary definition for `over.coprod`. -/
@[simps]
def over.coprod_obj [has_binary_coproducts C] {A : C} : over A â†’ over A â¥¤ over A := Î» f,
{ obj := Î» g, over.mk (coprod.desc f.hom g.hom),
  map := Î» gâ‚ gâ‚‚ k, over.hom_mk (coprod.map (ğŸ™ _) k.left) }

/-- A category with binary coproducts has a functorial `sup` operation on over categories. -/
@[simps]
def over.coprod [has_binary_coproducts C] {A : C} : over A â¥¤ over A â¥¤ over A :=
{ obj := Î» f, over.coprod_obj f,
  map := Î» fâ‚ fâ‚‚ k,
  { app := Î» g, over.hom_mk (coprod.map k.left (ğŸ™ _))
      (by { dsimp, rw [coprod.map_desc, category.id_comp, over.w k] }),
    naturality' := Î» f g k, by ext; { dsimp, simp, }, },
  map_id' := Î» X, by ext; { dsimp, simp, },
  map_comp' := Î» X Y Z f g, by ext; { dsimp, simp, }, }.

end category_theory
