/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.epi_mono
import category_theory.limits.shapes.binary_products
import category_theory.preadditive

/-!
# Biproducts and binary biproducts

We introduce the notion of biproducts and binary biproducts.

These are slightly unusual relative to the other shapes in the library,
as they are simultaneously limits and colimits.
(Zero objects are similar; they are "biterminal".)

We model these here using a `bicone`, with a cone point `X`,
and natural transformations `Ï€` from the constant functor with value `X` to `F`
and `Î¹` in the other direction.

We implement `has_bilimit` as a `bicone`, equipped with the evidence
`is_limit bicone.to_cone` and `is_colimit bicone.to_cocone`.

In practice, of course, we are only interested in the special case of bilimits
over `discrete J` for `[fintype J] [decidable_eq J]`,
which corresponds to finite biproducts.

TODO: We should provide a constructor that takes `has_limit F`, `has_colimit F`, and
and iso `limit F â‰… colimit F`, and produces `has_bilimit F`.

TODO: perhaps it makes sense to unify the treatment of zero objects with this a bit.

In addition to biproducts and binary biproducts, we define the notion of preadditive binary
biproduct, which is a preadditive version of binary biproducts. We show that a preadditive binary
biproduct is a binary biproduct and construct preadditive binary biproducts both from binary
products and from binary coproducts.

TODO: the preadditive version of finite biproducts

## Notation
As `âŠ•` is already taken for the sum of types, we introduce the notation `X âŠ Y` for
a binary biproduct. We introduce `â¨ f` for the indexed biproduct.
-/

universes v u

open category_theory
open category_theory.functor

namespace category_theory.limits

variables {J : Type v} [small_category J]
variables {C : Type u} [category.{v} C]

/--
A `c : bicone F` is:
* an object `c.X` and
* a natural transformation `c.Ï€ : c.X âŸ¶ F` from the constant `c.X` functor to `F`.
* a natural transformation `c.Î¹ : F âŸ¶ c.X` from `F` to the constant `c.X` functor.
-/
@[nolint has_inhabited_instance]
structure bicone {J : Type v} [small_category J] (F : J â¥¤ C) :=
(X : C)
(Ï€ : (const J).obj X âŸ¶ F)
(Î¹ : F âŸ¶ (const J).obj X)

variables {F : J â¥¤ C}

namespace bicone
/-- Extract the cone from a bicone. -/
@[simps]
def to_cone (B : bicone F) : cone F :=
{ .. B }
/-- Extract the cocone from a bicone. -/
@[simps]
def to_cocone (B : bicone F) : cocone F :=
{ .. B }
end bicone

/--
`has_bilimit F` represents a particular chosen bicone which is
simultaneously a limit and a colimit of the diagram `F`.

(This is only interesting when the source category is discrete.)
-/
class has_bilimit (F : J â¥¤ C) :=
(bicone : bicone F)
(is_limit : is_limit bicone.to_cone)
(is_colimit : is_colimit bicone.to_cocone)

@[priority 100]
instance has_limit_of_has_bilimit [has_bilimit F] : has_limit F :=
{ cone := has_bilimit.bicone.to_cone,
  is_limit := has_bilimit.is_limit, }

@[priority 100]
instance has_colimit_of_has_bilimit [has_bilimit F] : has_colimit F :=
{ cocone := has_bilimit.bicone.to_cocone,
  is_colimit := has_bilimit.is_colimit, }

variables (J C)

/--
`C` has bilimits of shape `J` if we have chosen
a particular limit and a particular colimit, with the same cone points,
of every functor `F : J â¥¤ C`.

(This is only interesting if `J` is discrete.)
-/
class has_bilimits_of_shape :=
(has_bilimit : Î  F : J â¥¤ C, has_bilimit F)

attribute [instance, priority 100] has_bilimits_of_shape.has_bilimit

@[priority 100]
instance [has_bilimits_of_shape J C] : has_limits_of_shape J C :=
{ has_limit := Î» F, by apply_instance }
@[priority 100]
instance [has_bilimits_of_shape J C] : has_colimits_of_shape J C :=
{ has_colimit := Î» F, by apply_instance }

/-- `has_finite_biproducts C` represents a choice of biproduct for every family of objects in `C`
indexed by a finite type with decidable equality. -/
class has_finite_biproducts :=
(has_bilimits_of_shape : Î  (J : Type v) [fintype J] [decidable_eq J],
  has_bilimits_of_shape.{v} (discrete J) C)

attribute [instance] has_finite_biproducts.has_bilimits_of_shape

/--
The isomorphism between the specified limit and the specified colimit for
a functor with a bilimit.
-/
def biproduct_iso {J : Type v} (F : J â†’ C) [has_bilimit (discrete.functor F)] :
  limits.pi_obj F â‰… limits.sigma_obj F :=
eq_to_iso rfl

end category_theory.limits

namespace category_theory.limits
variables {J : Type v}
variables {C : Type u} [category.{v} C]

/-- `biproduct f` computes the biproduct of a family of elements `f`. (It is defined as an
   abbreviation for `limit (discrete.functor f)`, so for most facts about `biproduct f`, you will
   just use general facts about limits and colimits.) -/
abbreviation biproduct (f : J â†’ C) [has_bilimit (discrete.functor f)] :=
limit (discrete.functor f)

notation `â¨ ` f:20 := biproduct f

/-- The projection onto a summand of a biproduct. -/
abbreviation biproduct.Ï€ (f : J â†’ C) [has_bilimit (discrete.functor f)] (b : J) : â¨ f âŸ¶ f b :=
limit.Ï€ (discrete.functor f) b
/-- The inclusion into a summand of a biproduct. -/
abbreviation biproduct.Î¹ (f : J â†’ C) [has_bilimit (discrete.functor f)] (b : J) : f b âŸ¶ â¨ f :=
colimit.Î¹ (discrete.functor f) b

/-- Given a collection of maps into the summands, we obtain a map into the biproduct. -/
abbreviation biproduct.lift
  {f : J â†’ C} [has_bilimit (discrete.functor f)] {P : C} (p : Î  b, P âŸ¶ f b) : P âŸ¶ â¨ f :=
limit.lift _ (fan.mk p)
/-- Given a collection of maps out of the summands, we obtain a map out of the biproduct. -/
abbreviation biproduct.desc
  {f : J â†’ C} [has_bilimit (discrete.functor f)] {P : C} (p : Î  b, f b âŸ¶ P) : â¨ f âŸ¶ P :=
colimit.desc _ (cofan.mk p)

/-- Given a collection of maps between corresponding summands of a pair of biproducts
indexed by the same type, we obtain a map betweeen the biproducts. -/
abbreviation biproduct.map [fintype J] [decidable_eq J] {f g : J â†’ C} [has_finite_biproducts.{v} C]
  (p : Î  b, f b âŸ¶ g b) : â¨ f âŸ¶ â¨ g :=
(@lim (discrete J) _ C _ _).map (discrete.nat_trans p)

instance biproduct.Î¹_mono [decidable_eq J] (f : J â†’ C) [has_bilimit (discrete.functor f)]
  (b : J) : split_mono (biproduct.Î¹ f b) :=
{ retraction := biproduct.desc $
    Î» b', if h : b' = b then eq_to_hom (congr_arg f h) else biproduct.Î¹ f b' â‰« biproduct.Ï€ f b }

instance biproduct.Ï€_epi [decidable_eq J] (f : J â†’ C) [has_bilimit (discrete.functor f)]
  (b : J) : split_epi (biproduct.Ï€ f b) :=
{ section_ := biproduct.lift $
    Î» b', if h : b = b' then eq_to_hom (congr_arg f h) else biproduct.Î¹ f b â‰« biproduct.Ï€ f b' }

variables {C}

/--
A binary bicone for a pair of objects `P Q : C` consists of the cone point `X`,
maps from `X` to both `P` and `Q`, and maps from both `P` and `Q` to `X`.
-/
@[nolint has_inhabited_instance]
structure binary_bicone (P Q : C) :=
(X : C)
(Ï€â‚ : X âŸ¶ P)
(Ï€â‚‚ : X âŸ¶ Q)
(Î¹â‚ : P âŸ¶ X)
(Î¹â‚‚ : Q âŸ¶ X)

namespace binary_bicone
variables {P Q : C}

/-- Extract the cone from a binary bicone. -/
@[simp]
def to_cone (c : binary_bicone.{v} P Q) : cone (pair P Q) :=
binary_fan.mk c.Ï€â‚ c.Ï€â‚‚
/-- Extract the cocone from a binary bicone. -/
@[simp]
def to_cocone (c : binary_bicone.{v} P Q) : cocone (pair P Q) :=
binary_cofan.mk c.Î¹â‚ c.Î¹â‚‚

end binary_bicone

/--
`has_binary_biproduct P Q` represents a particular chosen bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`.
-/
class has_binary_biproduct (P Q : C) :=
(bicone : binary_bicone.{v} P Q)
(is_limit : is_limit bicone.to_cone)
(is_colimit : is_colimit bicone.to_cocone)

section
variable (C)

/--
`has_binary_biproducts C` represents a particular chosen bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`, for every `P Q : C`.
-/
class has_binary_biproducts :=
(has_binary_biproduct : Î  (P Q : C), has_binary_biproduct.{v} P Q)

attribute [instance, priority 100] has_binary_biproducts.has_binary_biproduct

end

variables {P Q : C}

instance has_binary_biproduct.has_limit_pair [has_binary_biproduct.{v} P Q] :
  has_limit (pair P Q) :=
{ cone := has_binary_biproduct.bicone.to_cone,
  is_limit := has_binary_biproduct.is_limit.{v}, }

instance has_binary_biproduct.has_colimit_pair [has_binary_biproduct.{v} P Q] :
  has_colimit (pair P Q) :=
{ cocone := has_binary_biproduct.bicone.to_cocone,
  is_colimit := has_binary_biproduct.is_colimit.{v}, }

@[priority 100]
instance has_limits_of_shape_walking_pair [has_binary_biproducts.{v} C] :
  has_limits_of_shape.{v} (discrete walking_pair) C :=
{ has_limit := Î» F, has_limit_of_iso (diagram_iso_pair F).symm }
@[priority 100]
instance has_colimits_of_shape_walking_pair [has_binary_biproducts.{v} C] :
  has_colimits_of_shape.{v} (discrete walking_pair) C :=
{ has_colimit := Î» F, has_colimit_of_iso (diagram_iso_pair F) }

@[priority 100]
instance has_binary_products_of_has_binary_biproducts [has_binary_biproducts.{v} C] :
  has_binary_products.{v} C :=
âŸ¨by apply_instanceâŸ©

@[priority 100]
instance has_binary_coproducts_of_has_binary_biproducts [has_binary_biproducts.{v} C] :
  has_binary_coproducts.{v} C :=
âŸ¨by apply_instanceâŸ©

/--
The isomorphism between the specified binary product and the specified binary coproduct for
a pair for a binary biproduct.
-/
def biprod_iso (X Y : C) [has_binary_biproduct.{v} X Y]  :
  limits.prod X Y â‰… limits.coprod X Y :=
eq_to_iso rfl

/-- The chosen biproduct of a pair of objects. -/
abbreviation biprod (X Y : C) [has_binary_biproduct.{v} X Y] := limit (pair X Y)

notation X ` âŠ `:20 Y:20 := biprod X Y

/-- The projection onto the first summand of a binary biproduct. -/
abbreviation biprod.fst {X Y : C} [has_binary_biproduct.{v} X Y] : X âŠ Y âŸ¶ X :=
limit.Ï€ (pair X Y) walking_pair.left
/-- The projection onto the second summand of a binary biproduct. -/
abbreviation biprod.snd {X Y : C} [has_binary_biproduct.{v} X Y] : X âŠ Y âŸ¶ Y :=
limit.Ï€ (pair X Y) walking_pair.right
/-- The inclusion into the first summand of a binary biproduct. -/
abbreviation biprod.inl {X Y : C} [has_binary_biproduct.{v} X Y] : X âŸ¶ X âŠ Y :=
colimit.Î¹ (pair X Y) walking_pair.left
/-- The inclusion into the second summand of a binary biproduct. -/
abbreviation biprod.inr {X Y : C} [has_binary_biproduct.{v} X Y] : Y âŸ¶ X âŠ Y :=
colimit.Î¹ (pair X Y) walking_pair.right

/-- Given a pair of maps into the summands of a binary biproduct,
we obtain a map into the binary biproduct. -/
abbreviation biprod.lift {W X Y : C} [has_binary_biproduct.{v} X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  W âŸ¶ X âŠ Y :=
limit.lift _ (binary_fan.mk f g)
/-- Given a pair of maps out of the summands of a binary biproduct,
we obtain a map out of the binary biproduct. -/
abbreviation biprod.desc {W X Y : C} [has_binary_biproduct.{v} X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  X âŠ Y âŸ¶ W :=
colimit.desc _ (binary_cofan.mk f g)

/-- Given a pair of maps between the summands of a pair of binary biproducts,
we obtain a map between the binary biproducts. -/
abbreviation biprod.map {W X Y Z : C} [has_binary_biproducts.{v} C]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W âŠ X âŸ¶ Y âŠ Z :=
(@lim (discrete walking_pair) _ C _ _).map (@map_pair _ _ (pair W X) (pair Y Z) f g)

instance biprod.inl_mono {X Y : C} [has_binary_biproduct.{v} X Y] :
  split_mono (biprod.inl : X âŸ¶ X âŠ Y) :=
{ retraction := biprod.desc (ğŸ™ X) (biprod.inr â‰« biprod.fst) }

instance biprod.inr_mono {X Y : C} [has_binary_biproduct.{v} X Y] :
  split_mono (biprod.inr : Y âŸ¶ X âŠ Y) :=
{ retraction := biprod.desc (biprod.inl â‰« biprod.snd) (ğŸ™ Y)}

instance biprod.fst_epi {X Y : C} [has_binary_biproduct.{v} X Y] :
  split_epi (biprod.fst : X âŠ Y âŸ¶ X) :=
{ section_ := biprod.lift (ğŸ™ X) (biprod.inl â‰« biprod.snd) }

instance biprod.snd_epi {X Y : C} [has_binary_biproduct.{v} X Y] :
  split_epi (biprod.snd : X âŠ Y âŸ¶ Y) :=
{ section_ := biprod.lift (biprod.inr â‰« biprod.fst) (ğŸ™ Y) }

@[ext] lemma biprod.hom_ext {X Y Z : C} [has_binary_biproduct.{v} X Y] (f g : Z âŸ¶ X âŠ Y)
  (hâ‚€ : f â‰« biprod.fst = g â‰« biprod.fst) (hâ‚ : f â‰« biprod.snd = g â‰« biprod.snd) : f = g :=
binary_fan.is_limit.hom_ext has_binary_biproduct.is_limit hâ‚€ hâ‚

@[ext] lemma biprod.hom_ext' {X Y Z : C} [has_binary_biproduct.{v} X Y] (f g : X âŠ Y âŸ¶ Z)
  (hâ‚€ : biprod.inl â‰« f = biprod.inl â‰« g) (hâ‚ : biprod.inr â‰« f = biprod.inr â‰« g) : f = g :=
binary_cofan.is_colimit.hom_ext has_binary_biproduct.is_colimit hâ‚€ hâ‚

-- TODO:
-- If someone is interested, they could provide the constructions:
--   has_binary_biproducts â†” has_finite_biproducts

section preadditive
variables [preadditive.{v} C]

open category_theory.preadditive

/-- A preadditive binary biproduct is a bicone on two objects `X` and `Y` satisfying a set of five
    axioms expressing the properties of a biproduct in additive terms. The notion of preadditive
    binary biproduct is strictly stronger than the notion of binary biproduct (but it in any
    preadditive category, the existence of a binary biproduct implies the existence of a
    preadditive binary biproduct: a biproduct is, in particular, a product, and every product gives
    rise to a preadditive binary biproduct, see `has_preadditive_binary_biproduct.of_has_limit_pair`). -/
class has_preadditive_binary_biproduct (X Y : C) :=
(bicone : binary_bicone.{v} X Y)
(Î¹â‚_Ï€â‚' : bicone.Î¹â‚ â‰« bicone.Ï€â‚ = ğŸ™ X . obviously)
(Î¹â‚‚_Ï€â‚‚' : bicone.Î¹â‚‚ â‰« bicone.Ï€â‚‚ = ğŸ™ Y . obviously)
(Î¹â‚‚_Ï€â‚' : bicone.Î¹â‚‚ â‰« bicone.Ï€â‚ = 0 . obviously)
(Î¹â‚_Ï€â‚‚' : bicone.Î¹â‚ â‰« bicone.Ï€â‚‚ = 0 . obviously)
(total' : bicone.Ï€â‚ â‰« bicone.Î¹â‚ + bicone.Ï€â‚‚ â‰« bicone.Î¹â‚‚ = ğŸ™ bicone.X . obviously)

restate_axiom has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚'
restate_axiom has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚‚'
restate_axiom has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚'
restate_axiom has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚‚'
restate_axiom has_preadditive_binary_biproduct.total'
attribute [simp, reassoc] has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚
  has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚‚ has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚
  has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚‚
attribute [simp] has_preadditive_binary_biproduct.total

section
local attribute [tidy] tactic.case_bash

/-- A preadditive binary biproduct is a binary biproduct. -/
@[priority 100]
instance (X Y : C) [has_preadditive_binary_biproduct.{v} X Y] : has_binary_biproduct.{v} X Y :=
{ bicone := has_preadditive_binary_biproduct.bicone,
  is_limit :=
  { lift := Î» s, binary_fan.fst s â‰« has_preadditive_binary_biproduct.bicone.Î¹â‚ +
      binary_fan.snd s â‰« has_preadditive_binary_biproduct.bicone.Î¹â‚‚,
    uniq' := Î» s m h, by erw [â†category.comp_id m, â†has_preadditive_binary_biproduct.total,
      comp_add, reassoc_of (h walking_pair.left), reassoc_of (h walking_pair.right)] },
  is_colimit :=
  { desc := Î» s, has_preadditive_binary_biproduct.bicone.Ï€â‚ â‰« binary_cofan.inl s +
      has_preadditive_binary_biproduct.bicone.Ï€â‚‚ â‰« binary_cofan.inr s,
    uniq' := Î» s m h, by erw [â†category.id_comp m, â†has_preadditive_binary_biproduct.total,
      add_comp, category.assoc, category.assoc, h walking_pair.left, h walking_pair.right] } }

end

section
variables (X Y : C) [has_preadditive_binary_biproduct.{v} X Y]

@[simp, reassoc] lemma biprod.inl_fst : (biprod.inl : X âŸ¶ X âŠ Y) â‰« biprod.fst = ğŸ™ X :=
has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚
@[simp, reassoc] lemma biprod.inr_snd : (biprod.inr : Y âŸ¶ X âŠ Y) â‰« biprod.snd = ğŸ™ Y :=
has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚‚
@[simp, reassoc] lemma biprod.inr_fst : (biprod.inr : Y âŸ¶ X âŠ Y) â‰« biprod.fst = 0 :=
has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚
@[simp, reassoc] lemma biprod.inl_snd : (biprod.inl : X âŸ¶ X âŠ Y) â‰« biprod.snd = 0 :=
has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚‚
@[simp] lemma biprod.total : biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y) :=
has_preadditive_binary_biproduct.total

lemma biprod.inl_add_inr {T : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} :
  f â‰« biprod.inl + g â‰« biprod.inr = biprod.lift f g := rfl

lemma biprod.fst_add_snd {T : C} {f : X âŸ¶ T} {g : Y âŸ¶ T} :
  biprod.fst â‰« f + biprod.snd â‰« g = biprod.desc f g := rfl

@[simp, reassoc] lemma biprod.lift_desc {T U : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} {h : X âŸ¶ U} {i : Y âŸ¶ U} :
  biprod.lift f g â‰« biprod.desc h i = f â‰« h + g â‰« i :=
by simp [â†biprod.inl_add_inr, â†biprod.fst_add_snd]

end

section has_limit_pair

/-- In a preadditive category, if the product of `X` and `Y` exists, then the preadditive binary
    biproduct of `X` and `Y` exists. -/
def has_preadditive_binary_biproduct.of_has_limit_pair (X Y : C) [has_limit.{v} (pair X Y)] :
  has_preadditive_binary_biproduct.{v} X Y :=
{ bicone :=
  { X := X â¨¯ Y,
    Ï€â‚ := category_theory.limits.prod.fst,
    Ï€â‚‚ := category_theory.limits.prod.snd,
    Î¹â‚ := prod.lift (ğŸ™ X) 0,
    Î¹â‚‚ := prod.lift 0 (ğŸ™ Y) } }

/-- In a preadditive category, if the coproduct of `X` and `Y` exists, then the preadditive binary
    biproduct of `X` and `Y` exists. -/
def has_preadditive_binary_biproduct.of_has_colimit_pair (X Y : C) [has_colimit.{v} (pair X Y)] :
  has_preadditive_binary_biproduct.{v} X Y :=
{ bicone :=
  { X := X â¨¿ Y,
    Ï€â‚ := coprod.desc (ğŸ™ X) 0,
    Ï€â‚‚ := coprod.desc 0 (ğŸ™ Y),
    Î¹â‚ := category_theory.limits.coprod.inl,
    Î¹â‚‚ := category_theory.limits.coprod.inr } }

end has_limit_pair

section
variable (C)

/-- A preadditive category `has_preadditive_binary_biproducts` if the preadditive binary biproduct
    exists for every pair of objects. -/
class has_preadditive_binary_biproducts :=
(has_preadditive_binary_biproduct : Î  (X Y : C), has_preadditive_binary_biproduct.{v} X Y)

attribute [instance, priority 100] has_preadditive_binary_biproducts.has_preadditive_binary_biproduct

@[priority 100]
instance [has_preadditive_binary_biproducts.{v} C] : has_binary_biproducts.{v} C :=
âŸ¨Î» X Y, by apply_instanceâŸ©

/-- If a preadditive category has all binary products, then it has all preadditive binary
    biproducts. -/
-- This particularly dangerous as an instance,
-- as we can deduce `has_binary_products` from `has_preadditive_binary_biproducts`.
def has_preadditive_binary_biproducts_of_has_binary_products [has_binary_products.{v} C] :
  has_preadditive_binary_biproducts.{v} C :=
âŸ¨Î» X Y, has_preadditive_binary_biproduct.of_has_limit_pair X YâŸ©

/-- If a preadditive category has all binary coproducts, then it has all preadditive binary
    biproducts. -/
-- This particularly dangerous as an instance,
-- as we can deduce `has_binary_products` from `has_preadditive_binary_biproducts`.
def has_preadditive_binary_biproducts_of_has_binary_coproducts [has_binary_coproducts.{v} C] :
  has_preadditive_binary_biproducts.{v} C :=
âŸ¨Î» X Y, has_preadditive_binary_biproduct.of_has_colimit_pair X YâŸ©

end

end preadditive

end category_theory.limits
