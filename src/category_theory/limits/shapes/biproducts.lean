/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.epi_mono
import category_theory.limits.shapes.binary_products
import category_theory.preadditive
import algebra.big_operators

/-!
# Biproducts and binary biproducts

We introduce the notion of biproducts and binary biproducts.

These are slightly unusual relative to the other shapes in the library,
as they are simultaneously limits and colimits.
(Zero objects are similar; they are "biterminal".)

We treat first the case of a general category with zero morphisms,
and subsequently the case of a preadditive category.

In a category with zero morphisms, we model the (binary) biproduct of `P Q : C`
using a `binary_bicone`, which has a cone point `X`,
and morphisms `fst : X âŸ¶ P`, `snd : X âŸ¶ Q`, `inl : P âŸ¶ X` and `inr : X âŸ¶ Q`,
such that `inl â‰« fst = ğŸ™ P`, `inl â‰« snd = 0`, `inr â‰« fst = 0`, and `inr â‰« snd = ğŸ™ Q`.
Such a `binary_bicone` is a biproduct if the cone is a limit cone, and the cocone is a colimit cocone.

In a preadditive category,
* any `binary_biproduct` satisfies `total : fst â‰« inl + snd â‰« inr = ğŸ™ X`
* any `binary_product` is a `binary_biproduct`
* any `binary_coproduct` is a `binary_biproduct`

For biproducts indexed by a `fintype J`, a `bicone` again consists of a cone point `X`
and morphisms `Ï€ j : X âŸ¶ F j` and `Î¹ j : F j âŸ¶ X` for each `j`,
such that `Î¹ j â‰« Ï€ j'` is the identity when `j = j'` and zero otherwise.

In a preadditive category,
* any `biproduct` satisfies `total : âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f)`
* any `product` is a `biproduct`
* any `coproduct` is a `biproduct`

## Notation
As `âŠ•` is already taken for the sum of types, we introduce the notation `X âŠ Y` for
a binary biproduct. We introduce `â¨ f` for the indexed biproduct.
-/

universes v u

open category_theory
open category_theory.functor

namespace category_theory.limits

variables {J : Type v} [decidable_eq J]
variables {C : Type u} [category.{v} C] [has_zero_morphisms C]

/--
A `c : bicone F` is:
* an object `c.X` and
* morphisms `Ï€ j : X âŸ¶ F j` and `Î¹ j : F j âŸ¶ X` for each `j`,
* such that `Î¹ j â‰« Ï€ j'` is the identity when `j = j'` and zero otherwise.
-/
@[nolint has_inhabited_instance]
structure bicone (F : J â†’ C) :=
(X : C)
(Ï€ : Î  j, X âŸ¶ F j)
(Î¹ : Î  j, F j âŸ¶ X)
(Î¹_Ï€ : âˆ€ j j', Î¹ j â‰« Ï€ j' = if h : j = j' then eq_to_hom (congr_arg F h) else 0)

@[simp] lemma bicone_Î¹_Ï€_self {F : J â†’ C} (B : bicone F) (j : J) : B.Î¹ j â‰« B.Ï€ j = ğŸ™ (F j) :=
by simpa using B.Î¹_Ï€ j j

@[simp] lemma bicone_Î¹_Ï€_ne {F : J â†’ C} (B : bicone F) {j j' : J} (h : j â‰  j') :
  B.Î¹ j â‰« B.Ï€ j' = 0 :=
by simpa [h] using B.Î¹_Ï€ j j'

variables {F : J â†’ C}

namespace bicone
/-- Extract the cone from a bicone. -/
@[simps]
def to_cone (B : bicone F) : cone (discrete.functor F) :=
{ X := B.X,
  Ï€ := { app := Î» j, B.Ï€ j }, }
/-- Extract the cocone from a bicone. -/
@[simps]
def to_cocone (B : bicone F) : cocone (discrete.functor F) :=
{ X := B.X,
  Î¹ := { app := Î» j, B.Î¹ j }, }
end bicone

/--
`has_biproduct F` represents a particular chosen bicone which is
simultaneously a limit and a colimit of the diagram `F`.
-/
class has_biproduct (F : J â†’ C) :=
(bicone : bicone F)
(is_limit : is_limit bicone.to_cone)
(is_colimit : is_colimit bicone.to_cocone)

@[priority 100]
instance has_product_of_has_biproduct [has_biproduct F] : has_limit (discrete.functor F) :=
{ cone := has_biproduct.bicone.to_cone,
  is_limit := has_biproduct.is_limit, }

@[priority 100]
instance has_coproduct_of_has_biproduct [has_biproduct F] : has_colimit (discrete.functor F) :=
{ cocone := has_biproduct.bicone.to_cocone,
  is_colimit := has_biproduct.is_colimit, }

variables (J C)

/--
`C` has biproducts of shape `J` if we have chosen
a particular limit and a particular colimit, with the same cone points,
of every function `F : J â†’ C`.
-/
class has_biproducts_of_shape :=
(has_biproduct : Î  F : J â†’ C, has_biproduct F)

attribute [instance, priority 100] has_biproducts_of_shape.has_biproduct

/-- `has_finite_biproducts C` represents a choice of biproduct for every family of objects in `C`
indexed by a finite type with decidable equality. -/
class has_finite_biproducts :=
(has_biproducts_of_shape : Î  (J : Type v) [fintype J] [decidable_eq J],
  has_biproducts_of_shape J C)

attribute [instance, priority 100] has_finite_biproducts.has_biproducts_of_shape

variables {J C}

/--
The isomorphism between the specified limit and the specified colimit for
a functor with a bilimit.
-/
def biproduct_iso (F : J â†’ C) [has_biproduct F] :
  limits.pi_obj F â‰… limits.sigma_obj F :=
eq_to_iso rfl

end category_theory.limits

namespace category_theory.limits
variables {J : Type v} [decidable_eq J]
variables {C : Type u} [category.{v} C] [has_zero_morphisms C]

/-- `biproduct f` computes the biproduct of a family of elements `f`. (It is defined as an
   abbreviation for `limit (discrete.functor f)`, so for most facts about `biproduct f`, you will
   just use general facts about limits and colimits.) -/
abbreviation biproduct (f : J â†’ C) [has_biproduct f] : C :=
limit (discrete.functor f)

notation `â¨ ` f:20 := biproduct f

/-- The chosen bicone over a family of elements. -/
abbreviation biproduct.bicone (f : J â†’ C) [has_biproduct f] : bicone f :=
has_biproduct.bicone

/-- The cone coming from the chosen bicone is a limit cone. -/
abbreviation biproduct.is_limit (f : J â†’ C) [has_biproduct f] :
  is_limit (biproduct.bicone f).to_cone :=
has_biproduct.is_limit

/-- The cocone coming from the chosen bicone is a colimit cocone. -/
abbreviation biproduct.is_colimit (f : J â†’ C) [has_biproduct f] :
  is_colimit (biproduct.bicone f).to_cocone :=
has_biproduct.is_colimit

/-- The projection onto a summand of a biproduct. -/
abbreviation biproduct.Ï€ (f : J â†’ C) [has_biproduct f] (b : J) : â¨ f âŸ¶ f b :=
limit.Ï€ (discrete.functor f) b
/-- The inclusion into a summand of a biproduct. -/
abbreviation biproduct.Î¹ (f : J â†’ C) [has_biproduct f] (b : J) : f b âŸ¶ â¨ f :=
colimit.Î¹ (discrete.functor f) b

@[reassoc]
lemma biproduct.Î¹_Ï€ (f : J â†’ C) [has_biproduct f] (j j' : J) :
  biproduct.Î¹ f j â‰« biproduct.Ï€ f j' = if h : j = j' then eq_to_hom (congr_arg f h) else 0 :=
has_biproduct.bicone.Î¹_Ï€ j j'

@[simp,reassoc]
lemma biproduct.Î¹_Ï€_self (f : J â†’ C) [has_biproduct f] (j : J) :
  biproduct.Î¹ f j â‰« biproduct.Ï€ f j = ğŸ™ _ :=
by simp [biproduct.Î¹_Ï€]

@[simp,reassoc]
lemma biproduct.Î¹_Ï€_ne (f : J â†’ C) [has_biproduct f] {j j' : J} (h : j â‰  j') :
  biproduct.Î¹ f j â‰« biproduct.Ï€ f j' = 0 :=
by simp [biproduct.Î¹_Ï€, h]

/-- Given a collection of maps into the summands, we obtain a map into the biproduct. -/
abbreviation biproduct.lift
  {f : J â†’ C} [has_biproduct f] {P : C} (p : Î  b, P âŸ¶ f b) : P âŸ¶ â¨ f :=
limit.lift _ (fan.mk p)
/-- Given a collection of maps out of the summands, we obtain a map out of the biproduct. -/
abbreviation biproduct.desc
  {f : J â†’ C} [has_biproduct f] {P : C} (p : Î  b, f b âŸ¶ P) : â¨ f âŸ¶ P :=
colimit.desc _ (cofan.mk p)

/-- Given a collection of maps between corresponding summands of a pair of biproducts
indexed by the same type, we obtain a map between the biproducts. -/
abbreviation biproduct.map [fintype J] {f g : J â†’ C} [has_finite_biproducts C]
  (p : Î  b, f b âŸ¶ g b) : â¨ f âŸ¶ â¨ g :=
lim_map (discrete.nat_trans p)

/-- An alternative to `biproduct.map` constructed via colimits.
This construction only exists in order to show it is equal to `biproduct.map`. -/
abbreviation biproduct.map' [fintype J] {f g : J â†’ C} [has_finite_biproducts C]
  (p : Î  b, f b âŸ¶ g b) : â¨ f âŸ¶ â¨ g :=
@colim_map _ _ _ _ (discrete.functor f) (discrete.functor g) _ _ (discrete.nat_trans p)

@[ext] lemma biproduct.hom_ext {f : J â†’ C} [has_biproduct f]
  {Z : C} (g h : Z âŸ¶ â¨ f)
  (w : âˆ€ j, g â‰« biproduct.Ï€ f j = h â‰« biproduct.Ï€ f j) : g = h :=
limit.hom_ext w

@[ext] lemma biproduct.hom_ext' {f : J â†’ C} [has_biproduct f]
  {Z : C} (g h : â¨ f âŸ¶ Z)
  (w : âˆ€ j, biproduct.Î¹ f j â‰« g =  biproduct.Î¹ f j â‰« h) : g = h :=
colimit.hom_ext w

lemma biproduct.map_eq_map' [fintype J] {f g : J â†’ C} [has_finite_biproducts C]
  (p : Î  b, f b âŸ¶ g b) : biproduct.map p = biproduct.map' p :=
begin
  ext j j',
  simp only [discrete.nat_trans_app, limits.Î¹_colim_map, limits.lim_map_Ï€, category.assoc],
  rw [biproduct.Î¹_Ï€_assoc, biproduct.Î¹_Ï€],
  split_ifs,
  { subst h, rw [eq_to_hom_refl, category.id_comp], erw category.comp_id, },
  { simp, },
end

instance biproduct.Î¹_mono (f : J â†’ C) [has_biproduct f]
  (b : J) : split_mono (biproduct.Î¹ f b) :=
{ retraction := biproduct.desc $
    Î» b', if h : b' = b then eq_to_hom (congr_arg f h) else biproduct.Î¹ f b' â‰« biproduct.Ï€ f b }

instance biproduct.Ï€_epi (f : J â†’ C) [has_biproduct f]
  (b : J) : split_epi (biproduct.Ï€ f b) :=
{ section_ := biproduct.lift $
    Î» b', if h : b = b' then eq_to_hom (congr_arg f h) else biproduct.Î¹ f b â‰« biproduct.Ï€ f b' }

-- Because `biproduct.map` is defined in terms of `lim` rather than `colim`,
-- we need to provide additional `simp` lemmas.
@[simp]
lemma biproduct.inl_map [fintype J] {f g : J â†’ C} [has_finite_biproducts C]
  (p : Î  j, f j âŸ¶ g j) (j : J) :
  biproduct.Î¹ f j â‰« biproduct.map p = p j â‰« biproduct.Î¹ g j :=
begin
  rw biproduct.map_eq_map',
  simp,
end

variables {C}

/--
A binary bicone for a pair of objects `P Q : C` consists of the cone point `X`,
maps from `X` to both `P` and `Q`, and maps from both `P` and `Q` to `X`,
so that `inl â‰« fst = ğŸ™ P`, `inl â‰« snd = 0`, `inr â‰« fst = 0`, and `inr â‰« snd = ğŸ™ Q`
-/
@[nolint has_inhabited_instance]
structure binary_bicone (P Q : C) :=
(X : C)
(fst : X âŸ¶ P)
(snd : X âŸ¶ Q)
(inl : P âŸ¶ X)
(inr : Q âŸ¶ X)
(inl_fst' : inl â‰« fst = ğŸ™ P . obviously)
(inl_snd' : inl â‰« snd = 0 . obviously)
(inr_fst' : inr â‰« fst = 0 . obviously)
(inr_snd' : inr â‰« snd = ğŸ™ Q . obviously)

restate_axiom binary_bicone.inl_fst'
restate_axiom binary_bicone.inl_snd'
restate_axiom binary_bicone.inr_fst'
restate_axiom binary_bicone.inr_snd'
attribute [simp, reassoc] binary_bicone.inl_fst binary_bicone.inl_snd
  binary_bicone.inr_fst binary_bicone.inr_snd

namespace binary_bicone
variables {P Q : C}

/-- Extract the cone from a binary bicone. -/
def to_cone (c : binary_bicone P Q) : cone (pair P Q) :=
binary_fan.mk c.fst c.snd

@[simp]
lemma to_cone_X (c : binary_bicone P Q) :
  c.to_cone.X = c.X := rfl

@[simp]
lemma to_cone_Ï€_app_left (c : binary_bicone P Q) :
  c.to_cone.Ï€.app (walking_pair.left) = c.fst := rfl
@[simp]
lemma to_cone_Ï€_app_right (c : binary_bicone P Q) :
  c.to_cone.Ï€.app (walking_pair.right) = c.snd := rfl

/-- Extract the cocone from a binary bicone. -/
def to_cocone (c : binary_bicone P Q) : cocone (pair P Q) :=
binary_cofan.mk c.inl c.inr

@[simp]
lemma to_cocone_X (c : binary_bicone P Q) :
  c.to_cocone.X = c.X := rfl

@[simp]
lemma to_cocone_Î¹_app_left (c : binary_bicone P Q) :
  c.to_cocone.Î¹.app (walking_pair.left) = c.inl := rfl
@[simp]
lemma to_cocone_Î¹_app_right (c : binary_bicone P Q) :
  c.to_cocone.Î¹.app (walking_pair.right) = c.inr := rfl

end binary_bicone

namespace bicone

/-- Convert a `bicone` over a function on `walking_pair` to a binary_bicone. -/
@[simps]
def to_binary_bicone {X Y : C} (b : bicone (pair X Y).obj) : binary_bicone X Y :=
{ X := b.X,
  fst := b.Ï€ walking_pair.left,
  snd := b.Ï€ walking_pair.right,
  inl := b.Î¹ walking_pair.left,
  inr := b.Î¹ walking_pair.right,
  inl_fst' := by { simp [bicone.Î¹_Ï€], refl, },
  inr_fst' := by simp [bicone.Î¹_Ï€],
  inl_snd' := by simp [bicone.Î¹_Ï€],
  inr_snd' := by { simp [bicone.Î¹_Ï€], refl, }, }

/--
If the cone obtained from a bicone over `pair X Y` is a limit cone,
so is the cone obtained by converting that bicone to a binary_bicone, then to a cone.
-/
def to_binary_bicone_is_limit {X Y : C} {b : bicone (pair X Y).obj}
  (c : is_limit (b.to_cone)) :
  is_limit (b.to_binary_bicone.to_cone) :=
{ lift := Î» s, c.lift s,
   fac' := Î» s j, by { cases j; erw c.fac, },
   uniq' := Î» s m w,
   begin
     apply c.uniq s,
     rintro (âŸ¨âŸ©|âŸ¨âŸ©),
     exact w walking_pair.left,
     exact w walking_pair.right,
   end, }

/--
If the cocone obtained from a bicone over `pair X Y` is a colimit cocone,
so is the cocone obtained by converting that bicone to a binary_bicone, then to a cocone.
-/
def to_binary_bicone_is_colimit {X Y : C} {b : bicone (pair X Y).obj}
  (c : is_colimit (b.to_cocone)) :
  is_colimit (b.to_binary_bicone.to_cocone) :=
{ desc := Î» s, c.desc s,
   fac' := Î» s j, by { cases j; erw c.fac, },
   uniq' := Î» s m w,
   begin
     apply c.uniq s,
     rintro (âŸ¨âŸ©|âŸ¨âŸ©),
     exact w walking_pair.left,
     exact w walking_pair.right,
   end, }

end bicone

/--
`has_binary_biproduct P Q` represents a particular chosen bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`.
-/
class has_binary_biproduct (P Q : C) :=
(bicone : binary_bicone P Q)
(is_limit : is_limit bicone.to_cone)
(is_colimit : is_colimit bicone.to_cocone)

section
variable (C)

/--
`has_binary_biproducts C` represents a particular chosen bicone which is
simultaneously a limit and a colimit of the diagram `pair P Q`, for every `P Q : C`.
-/
class has_binary_biproducts :=
(has_binary_biproduct : Î  (P Q : C), has_binary_biproduct P Q)

attribute [instance, priority 100] has_binary_biproducts.has_binary_biproduct

/--
A category with finite biproducts has binary biproducts.

This is not an instance as typically in concrete categories there will be
an alternative construction with nicer definitional properties.
-/
def has_binary_biproducts_of_finite_biproducts [has_finite_biproducts C] :
  has_binary_biproducts C :=
{ has_binary_biproduct := Î» P Q,
  { bicone := (biproduct.bicone (pair P Q).obj).to_binary_bicone,
    is_limit := bicone.to_binary_bicone_is_limit (biproduct.is_limit _),
    is_colimit := bicone.to_binary_bicone_is_colimit (biproduct.is_colimit _) } }

end

variables {P Q : C}

instance has_binary_biproduct.has_limit_pair [has_binary_biproduct P Q] :
  has_limit (pair P Q) :=
{ cone := has_binary_biproduct.bicone.to_cone,
  is_limit := has_binary_biproduct.is_limit, }

instance has_binary_biproduct.has_colimit_pair [has_binary_biproduct P Q] :
  has_colimit (pair P Q) :=
{ cocone := has_binary_biproduct.bicone.to_cocone,
  is_colimit := has_binary_biproduct.is_colimit, }

@[priority 100]
instance has_limits_of_shape_walking_pair [has_binary_biproducts C] :
  has_limits_of_shape (discrete walking_pair) C :=
{ has_limit := Î» F, has_limit_of_iso (diagram_iso_pair F).symm }
@[priority 100]
instance has_colimits_of_shape_walking_pair [has_binary_biproducts C] :
  has_colimits_of_shape (discrete walking_pair) C :=
{ has_colimit := Î» F, has_colimit_of_iso (diagram_iso_pair F) }

@[priority 100]
instance has_binary_products_of_has_binary_biproducts [has_binary_biproducts C] :
  has_binary_products C :=
âŸ¨by apply_instanceâŸ©

@[priority 100]
instance has_binary_coproducts_of_has_binary_biproducts [has_binary_biproducts C] :
  has_binary_coproducts C :=
âŸ¨by apply_instanceâŸ©

/--
The isomorphism between the specified binary product and the specified binary coproduct for
a pair for a binary biproduct.
-/
def biprod_iso (X Y : C) [has_binary_biproduct X Y]  :
  limits.prod X Y â‰… limits.coprod X Y :=
eq_to_iso rfl

/-- The chosen biproduct of a pair of objects. -/
abbreviation biprod (X Y : C) [has_binary_biproduct X Y] := limit (pair X Y)

notation X ` âŠ `:20 Y:20 := biprod X Y

/-- The projection onto the first summand of a binary biproduct. -/
abbreviation biprod.fst {X Y : C} [has_binary_biproduct X Y] : X âŠ Y âŸ¶ X :=
limit.Ï€ (pair X Y) walking_pair.left
/-- The projection onto the second summand of a binary biproduct. -/
abbreviation biprod.snd {X Y : C} [has_binary_biproduct X Y] : X âŠ Y âŸ¶ Y :=
limit.Ï€ (pair X Y) walking_pair.right
/-- The inclusion into the first summand of a binary biproduct. -/
abbreviation biprod.inl {X Y : C} [has_binary_biproduct X Y] : X âŸ¶ X âŠ Y :=
colimit.Î¹ (pair X Y) walking_pair.left
/-- The inclusion into the second summand of a binary biproduct. -/
abbreviation biprod.inr {X Y : C} [has_binary_biproduct X Y] : Y âŸ¶ X âŠ Y :=
colimit.Î¹ (pair X Y) walking_pair.right

@[simp,reassoc]
lemma biprod.inl_fst {X Y : C} [has_binary_biproduct X Y] :
  (biprod.inl : X âŸ¶ X âŠ Y) â‰« (biprod.fst : X âŠ Y âŸ¶ X) = ğŸ™ X :=
has_binary_biproduct.bicone.inl_fst
@[simp,reassoc]
lemma biprod.inl_snd {X Y : C} [has_binary_biproduct X Y] :
  (biprod.inl : X âŸ¶ X âŠ Y) â‰« (biprod.snd : X âŠ Y âŸ¶ Y) = 0 :=
has_binary_biproduct.bicone.inl_snd
@[simp,reassoc]
lemma biprod.inr_fst {X Y : C} [has_binary_biproduct X Y] :
  (biprod.inr : Y âŸ¶ X âŠ Y) â‰« (biprod.fst : X âŠ Y âŸ¶ X) = 0 :=
has_binary_biproduct.bicone.inr_fst
@[simp,reassoc]
lemma biprod.inr_snd {X Y : C} [has_binary_biproduct X Y] :
  (biprod.inr : Y âŸ¶ X âŠ Y) â‰« (biprod.snd : X âŠ Y âŸ¶ Y) = ğŸ™ Y :=
has_binary_biproduct.bicone.inr_snd

/-- Given a pair of maps into the summands of a binary biproduct,
we obtain a map into the binary biproduct. -/
abbreviation biprod.lift {W X Y : C} [has_binary_biproduct X Y] (f : W âŸ¶ X) (g : W âŸ¶ Y) :
  W âŸ¶ X âŠ Y :=
limit.lift _ (binary_fan.mk f g)
/-- Given a pair of maps out of the summands of a binary biproduct,
we obtain a map out of the binary biproduct. -/
abbreviation biprod.desc {W X Y : C} [has_binary_biproduct X Y] (f : X âŸ¶ W) (g : Y âŸ¶ W) :
  X âŠ Y âŸ¶ W :=
colimit.desc _ (binary_cofan.mk f g)

/-- Given a pair of maps between the summands of a pair of binary biproducts,
we obtain a map between the binary biproducts. -/
abbreviation biprod.map {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W âŠ X âŸ¶ Y âŠ Z :=
lim_map (@map_pair _ _ (pair W X) (pair Y Z) f g)

/-- Given a pair of isomorphisms between the summands of a pair of binary biproducts,
we obtain an isomorphism between the binary biproducts. -/
@[simps]
def biprod.map_iso {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W â‰… Y) (g : X â‰… Z) : W âŠ X â‰… Y âŠ Z :=
{ hom := biprod.map f.hom g.hom,
  inv := biprod.map f.inv g.inv, }

/-- An alternative to `biprod.map` constructed via colimits.
This construction only exists in order to show it is equal to `biprod.map`. -/
abbreviation biprod.map' {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : W âŠ X âŸ¶ Y âŠ Z :=
colim_map (@map_pair _ _ (pair W X) (pair Y Z) f g)

@[ext] lemma biprod.hom_ext {X Y Z : C} [has_binary_biproduct X Y] (f g : Z âŸ¶ X âŠ Y)
  (hâ‚€ : f â‰« biprod.fst = g â‰« biprod.fst) (hâ‚ : f â‰« biprod.snd = g â‰« biprod.snd) : f = g :=
binary_fan.is_limit.hom_ext has_binary_biproduct.is_limit hâ‚€ hâ‚

@[ext] lemma biprod.hom_ext' {X Y Z : C} [has_binary_biproduct X Y] (f g : X âŠ Y âŸ¶ Z)
  (hâ‚€ : biprod.inl â‰« f = biprod.inl â‰« g) (hâ‚ : biprod.inr â‰« f = biprod.inr â‰« g) : f = g :=
binary_cofan.is_colimit.hom_ext has_binary_biproduct.is_colimit hâ‚€ hâ‚

lemma biprod.map_eq_map' {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) : biprod.map f g = biprod.map' f g :=
begin
  ext,
  { simp only [map_pair_left, Î¹_colim_map, lim_map_Ï€, biprod.inl_fst_assoc, category.assoc],
    erw [biprod.inl_fst, category.comp_id], },
  { simp only [map_pair_left, Î¹_colim_map, lim_map_Ï€, has_zero_morphisms.zero_comp,
      biprod.inl_snd_assoc, category.assoc],
    erw [biprod.inl_snd], simp, },
  { simp only [map_pair_right, biprod.inr_fst_assoc, Î¹_colim_map, lim_map_Ï€,
      has_zero_morphisms.zero_comp, category.assoc],
    erw [biprod.inr_fst], simp, },
  { simp only [map_pair_right, Î¹_colim_map, lim_map_Ï€, biprod.inr_snd_assoc, category.assoc],
    erw [biprod.inr_snd, category.comp_id], },
end

instance biprod.inl_mono {X Y : C} [has_binary_biproduct X Y] :
  split_mono (biprod.inl : X âŸ¶ X âŠ Y) :=
{ retraction := biprod.desc (ğŸ™ X) (biprod.inr â‰« biprod.fst) }

instance biprod.inr_mono {X Y : C} [has_binary_biproduct X Y] :
  split_mono (biprod.inr : Y âŸ¶ X âŠ Y) :=
{ retraction := biprod.desc (biprod.inl â‰« biprod.snd) (ğŸ™ Y)}

instance biprod.fst_epi {X Y : C} [has_binary_biproduct X Y] :
  split_epi (biprod.fst : X âŠ Y âŸ¶ X) :=
{ section_ := biprod.lift (ğŸ™ X) (biprod.inl â‰« biprod.snd) }

instance biprod.snd_epi {X Y : C} [has_binary_biproduct X Y] :
  split_epi (biprod.snd : X âŠ Y âŸ¶ Y) :=
{ section_ := biprod.lift (biprod.inr â‰« biprod.fst) (ğŸ™ Y) }

@[simp,reassoc]
lemma biprod.map_fst {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) :
  biprod.map f g â‰« biprod.fst = biprod.fst â‰« f :=
by simp

@[simp,reassoc]
lemma biprod.map_snd {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) :
  biprod.map f g â‰« biprod.snd = biprod.snd â‰« g :=
by simp

-- Because `biprod.map` is defined in terms of `lim` rather than `colim`,
-- we need to provide additional `simp` lemmas.
@[simp,reassoc]
lemma biprod.inl_map {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) :
  biprod.inl â‰« biprod.map f g = f â‰« biprod.inl :=
begin
  rw biprod.map_eq_map',
  simp,
end

@[simp,reassoc]
lemma biprod.inr_map {W X Y Z : C} [has_binary_biproduct W X] [has_binary_biproduct Y Z]
  (f : W âŸ¶ Y) (g : X âŸ¶ Z) :
  biprod.inr â‰« biprod.map f g = g â‰« biprod.inr :=
begin
  rw biprod.map_eq_map',
  simp,
end

section
variables [has_binary_biproducts C]

/-- The braiding isomorphism which swaps a binary biproduct. -/
@[simps] def biprod.braiding (P Q : C) : P âŠ Q â‰… Q âŠ P :=
{ hom := biprod.lift biprod.snd biprod.fst,
  inv := biprod.lift biprod.snd biprod.fst }

/--
An alternative formula for the braiding isomorphism which swaps a binary biproduct,
using the fact that the biproduct is a coproduct.
-/
@[simps]
def biprod.braiding' (P Q : C) : P âŠ Q â‰… Q âŠ P :=
{ hom := biprod.desc biprod.inr biprod.inl,
  inv := biprod.desc biprod.inr biprod.inl }

lemma biprod.braiding'_eq_braiding {P Q : C} :
  biprod.braiding' P Q = biprod.braiding P Q :=
by tidy

/-- The braiding isomorphism can be passed through a map by swapping the order. -/
@[reassoc] lemma biprod.braid_natural {W X Y Z : C} (f : X âŸ¶ Y) (g : Z âŸ¶ W) :
  biprod.map f g â‰« (biprod.braiding _ _).hom = (biprod.braiding _ _).hom â‰« biprod.map g f :=
by tidy

@[reassoc] lemma biprod.braiding_map_braiding {W X Y Z : C} (f : W âŸ¶ Y) (g : X âŸ¶ Z) :
  (biprod.braiding X W).hom â‰« biprod.map f g â‰« (biprod.braiding Y Z).hom = biprod.map g f :=
by tidy

@[simp, reassoc] lemma biprod.symmetry' (P Q : C) :
  biprod.lift biprod.snd biprod.fst â‰« biprod.lift biprod.snd biprod.fst = ğŸ™ (P âŠ Q) :=
by tidy

/-- The braiding isomorphism is symmetric. -/
@[reassoc] lemma biprod.symmetry (P Q : C) :
  (biprod.braiding P Q).hom â‰« (biprod.braiding Q P).hom = ğŸ™ _ :=
by simp

end

-- TODO:
-- If someone is interested, they could provide the constructions:
--   has_binary_biproducts â†” has_finite_biproducts

end category_theory.limits

namespace category_theory.limits

section preadditive
variables {C : Type u} [category.{v} C] [preadditive C]
variables {J : Type v} [fintype J] [decidable_eq J]

open category_theory.preadditive
open_locale big_operators

/--
In a preadditive category, we can construct a biproduct for `f : J â†’ C` from
any bicone `b` for `f` satisfying `total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
def has_biproduct_of_total {f : J â†’ C} (b : bicone f) (total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X) :
  has_biproduct f :=
{ bicone := b,
  is_limit :=
  { lift := Î» s, âˆ‘ j, s.Ï€.app j â‰« b.Î¹ j,
    uniq' := Î» s m h,
    begin
      erw [â†category.comp_id m, â†total, comp_sum],
      apply finset.sum_congr rfl,
      intros j m,
      erw [reassoc_of (h j)],
    end,
    fac' := Î» s j,
    begin
      simp only [sum_comp, category.assoc, bicone.to_cone_Ï€_app, b.Î¹_Ï€, comp_dite],
      dsimp, simp,
    end },
  is_colimit :=
  { desc := Î» s, âˆ‘ j, b.Ï€ j â‰« s.Î¹.app j,
    uniq' := Î» s m h,
    begin
      erw [â†category.id_comp m, â†total, sum_comp],
            apply finset.sum_congr rfl,
      intros j m,
      erw [category.assoc, h],
    end,
    fac' := Î» s j,
    begin
      simp only [comp_sum, â†category.assoc, bicone.to_cocone_Î¹_app, b.Î¹_Ï€, dite_comp],
      dsimp, simp,
    end } }

/-- In a preadditive category, if the product over `f : J â†’ C` exists,
    then the biproduct over `f` exists. -/
def has_biproduct.of_has_product (f : J â†’ C) [has_product f] :
  has_biproduct f :=
has_biproduct_of_total
{ X := pi_obj f,
  Ï€ := limits.pi.Ï€ f,
  Î¹ := Î» j, pi.lift (Î» j', if h : j = j' then eq_to_hom (congr_arg f h) else 0),
  Î¹_Ï€ := Î» j j', by simp, }
(by { ext, simp [sum_comp, comp_dite] })

/-- In a preadditive category, if the coproduct over `f : J â†’ C` exists,
    then the biproduct over `f` exists. -/
def has_biproduct.of_has_coproduct (f : J â†’ C) [has_coproduct f] :
  has_biproduct f :=
has_biproduct_of_total
{ X := sigma_obj f,
  Ï€ := Î» j, sigma.desc (Î» j', if h : j' = j then eq_to_hom (congr_arg f h) else 0),
  Î¹ := limits.sigma.Î¹ f,
  Î¹_Ï€ := Î» j j', by simp, }
begin
  ext,
  simp only [comp_sum, limits.cofan.mk_Ï€_app, limits.colimit.Î¹_desc_assoc, eq_self_iff_true,
    limits.colimit.Î¹_desc, category.comp_id],
  dsimp,
  simp only [dite_comp, finset.sum_dite_eq, finset.mem_univ, if_true, category.id_comp,
    eq_to_hom_refl, limits.has_zero_morphisms.zero_comp],
end

section
variables {f : J â†’ C} [has_biproduct f]

/--
In any preadditive category, any biproduct satsifies
`âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f)`
-/
@[simp] lemma biproduct.total : âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f) :=
begin
  ext j j',
  simp [comp_sum, sum_comp, biproduct.Î¹_Ï€, comp_dite, dite_comp],
end

lemma biproduct.lift_eq {T : C} {g : Î  j, T âŸ¶ f j} :
  biproduct.lift g = âˆ‘ j, g j â‰« biproduct.Î¹ f j :=
begin
  ext j,
  simp [sum_comp, biproduct.Î¹_Ï€, comp_dite],
end

lemma biproduct.desc_eq {T : C} {g : Î  j, f j âŸ¶ T} :
  biproduct.desc g = âˆ‘ j, biproduct.Ï€ f j â‰« g j :=
begin
  ext j,
  simp [comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp],
end

@[simp, reassoc] lemma biproduct.lift_desc {T U : C} {g : Î  j, T âŸ¶ f j} {h : Î  j, f j âŸ¶ U} :
  biproduct.lift g â‰« biproduct.desc h = âˆ‘ j : J, g j â‰« h j :=
by simp [biproduct.lift_eq, biproduct.desc_eq, comp_sum, sum_comp, biproduct.Î¹_Ï€_assoc,
  comp_dite, dite_comp]

lemma biproduct.map_eq [has_finite_biproducts C] {f g : J â†’ C} {h : Î  j, f j âŸ¶ g j} :
  biproduct.map h = âˆ‘ j : J, biproduct.Ï€ f j â‰« h j â‰« biproduct.Î¹ g j :=
begin
  ext,
  simp [biproduct.Î¹_Ï€, biproduct.Î¹_Ï€_assoc, comp_sum, sum_comp, comp_dite, dite_comp],
end

end

/--
In a preadditive category, we can construct a binary biproduct for `X Y : C` from
any binary bicone `b` satisfying `total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
def has_binary_biproduct_of_total {X Y : C} (b : binary_bicone X Y)
  (total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X) :
  has_binary_biproduct X Y :=
{ bicone := b,
  is_limit :=
  { lift := Î» s, binary_fan.fst s â‰« b.inl +
      binary_fan.snd s â‰« b.inr,
    uniq' := Î» s m h, by erw [â†category.comp_id m, â†total,
      comp_add, reassoc_of (h walking_pair.left), reassoc_of (h walking_pair.right)],
    fac' := Î» s j, by cases j; simp, },
  is_colimit :=
  { desc := Î» s, b.fst â‰« binary_cofan.inl s +
      b.snd â‰« binary_cofan.inr s,
    uniq' := Î» s m h, by erw [â†category.id_comp m, â†total,
      add_comp, category.assoc, category.assoc, h walking_pair.left, h walking_pair.right],
    fac' := Î» s j, by cases j; simp, } }

/-- In a preadditive category, if the product of `X` and `Y` exists, then the
    binary biproduct of `X` and `Y` exists. -/
def has_binary_biproduct.of_has_binary_product (X Y : C) [has_binary_product X Y] :
  has_binary_biproduct X Y :=
has_binary_biproduct_of_total
{ X := X â¨¯ Y,
  fst := category_theory.limits.prod.fst,
  snd := category_theory.limits.prod.snd,
  inl := prod.lift (ğŸ™ X) 0,
  inr := prod.lift 0 (ğŸ™ Y) }
begin
  ext; simp [add_comp],
end

/-- In a preadditive category, if all binary products exist,
    then the all binary biproducts exist. -/
def has_binary_biproducts.of_has_binary_products [has_binary_products C] :
  has_binary_biproducts C :=
{ has_binary_biproduct := Î» X Y, has_binary_biproduct.of_has_binary_product X Y, }

/-- In a preadditive category, if the product of `X` and `Y` exists, then the
    binary biproduct of `X` and `Y` exists. -/
def has_binary_biproduct.of_has_binary_coproduct (X Y : C) [has_binary_coproduct X Y] :
  has_binary_biproduct X Y :=
has_binary_biproduct_of_total
{ X := X â¨¿ Y,
  fst := coprod.desc (ğŸ™ X) 0,
  snd := coprod.desc 0 (ğŸ™ Y),
  inl := category_theory.limits.coprod.inl,
  inr := category_theory.limits.coprod.inr }
begin
  ext; simp [add_comp],
end

/-- In a preadditive category, if all binary coproducts exist,
    then the all binary biproducts exist. -/
def has_binary_biproducts.of_has_binary_coproducts [has_binary_coproducts C] :
  has_binary_biproducts C :=
{ has_binary_biproduct := Î» X Y, has_binary_biproduct.of_has_binary_coproduct X Y, }

section
variables {X Y : C} [has_binary_biproduct X Y]

/--
In any preadditive category, any binary biproduct satsifies
`biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y)`.
-/
@[simp] lemma biprod.total : biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y) :=
begin
  ext; simp [add_comp],
end

lemma biprod.lift_eq {T : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} :
  biprod.lift f g = f â‰« biprod.inl + g â‰« biprod.inr :=
begin
  ext; simp [add_comp],
end

lemma biprod.desc_eq {T : C} {f : X âŸ¶ T} {g : Y âŸ¶ T} :
  biprod.desc f g = biprod.fst â‰« f + biprod.snd â‰« g :=
begin
  ext; simp [add_comp],
end

@[simp, reassoc] lemma biprod.lift_desc {T U : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} {h : X âŸ¶ U} {i : Y âŸ¶ U} :
  biprod.lift f g â‰« biprod.desc h i = f â‰« h + g â‰« i :=
by simp [biprod.lift_eq, biprod.desc_eq]


lemma biprod.map_eq [has_binary_biproducts C] {W X Y Z : C} {f : W âŸ¶ Y} {g : X âŸ¶ Z} :
  biprod.map f g = biprod.fst â‰« f â‰« biprod.inl + biprod.snd â‰« g â‰« biprod.inr :=
by apply biprod.hom_ext; apply biprod.hom_ext'; simp

end

end preadditive

end category_theory.limits
