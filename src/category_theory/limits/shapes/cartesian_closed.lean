/-
Copyright (c) 2020 Bhavik Mehta, Edward Ayers, Thomas Read. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Edward Ayers, Thomas Read
-/

import category_theory.limits.shapes.binary_products
import category_theory.adjunction
import category_theory.epi_mono
import tactic

/-!
# Cartesian closed categories

Define exponentiable objects and cartesian closed categories.
Show that exponential forms a difunctor.
Define the exponential comparison morphisms.
-/
universes v u uâ‚‚

namespace category_theory

open limits category

class exponentiable {C : Type u} [category.{v} C] [bp : has_finite_products.{v} C] (X : C) :=
(is_adj : is_left_adjoint (prod_functor.obj X))

def binary_product_exponentiable {C : Type u} [category.{v} C] [bp : has_finite_products.{v} C] {X Y : C}
  (hX : exponentiable X) (hY : exponentiable Y) : exponentiable (X â¨¯ Y) :=
{ is_adj :=
  { right := hX.is_adj.right â‹™ hY.is_adj.right,
    adj := adjunction.of_nat_iso_left (adjunction.comp _ _ hY.is_adj.adj hX.is_adj.adj) (prod_functor_left_comp _ _).symm } }

class is_cartesian_closed (C : Type u) [category.{v} C] [has_finite_products.{v} C] :=
(cart_closed : Î  (X : C), exponentiable X)

attribute [instance] is_cartesian_closed.cart_closed

variables {C : Type u} [ğ’ : category.{v} C] (A B : C) {X X' Y Y' Z : C}
include ğ’

section exp
variables [has_finite_products.{v} C] [exponentiable A]

/-- This is (-)^A -/
def exp.functor (A : C) [exponentiable A] : C â¥¤ C :=
(@exponentiable.is_adj _ _ _ A _).right

def exp.adjunction (A : C) [exponentiable A] : prod_functor.obj A âŠ£ exp.functor A :=
exponentiable.is_adj.adj

def ev.nat_trans (A : C) [exponentiable A] : exp.functor A â‹™ prod_functor.obj A âŸ¶ ğŸ­ C :=
exponentiable.is_adj.adj.counit

def coev.nat_trans (A : C) [exponentiable A] : ğŸ­ C âŸ¶ prod_functor.obj A â‹™ exp.functor A :=
exponentiable.is_adj.adj.unit

/-- `B ^ A` or `A âŸ¹ B` -/
def exp (A : C) (B : C) [exponentiable A] : C := (exp.functor A).obj B

infixl `âŸ¹`:20 := exp
infixr `^^`:30 := pow

-- [todo] rename as 'post compose' or similar?
def post (A : C) [exponentiable A] {X Y : C} (f : X âŸ¶ Y) : AâŸ¹X âŸ¶ AâŸ¹Y :=
(exp.functor A).map f

lemma post.map_comp {f : X âŸ¶ Y} {g : Y âŸ¶ Z} : post A (f â‰« g) = post A f â‰« post A g :=
(exp.functor A).map_comp _ _

def ev : A â¨¯ (AâŸ¹B) âŸ¶ B :=
(ev.nat_trans A).app B

def coev : B âŸ¶ AâŸ¹(Aâ¨¯B) :=
(coev.nat_trans A).app B

@[simp, reassoc] lemma ev_coev : limits.prod.map (ğŸ™ A) (coev A B) â‰« ev A (A â¨¯ B) = ğŸ™ (A â¨¯ B) :=
adjunction.left_triangle_components (exp.adjunction A)

@[simp, reassoc] lemma coev_ev : coev A (AâŸ¹B) â‰« post A (ev A B) = ğŸ™ (AâŸ¹B) :=
adjunction.right_triangle_components (exp.adjunction A)

@[simp, reassoc, priority 10]
lemma coev_nat (f : X âŸ¶ Y) : f â‰« coev A Y = coev A X â‰« post A (limits.prod.map (ğŸ™ A) f) :=
(coev.nat_trans A).naturality f

@[simp, reassoc, priority 10]
lemma ev_nat (f : X âŸ¶ Y) : limits.prod.map (ğŸ™ A) (post _ f) â‰« ev A Y = ev A X â‰« f :=
(ev.nat_trans A).naturality f

end exp

variables {A}

-- Wrap these in a namespace so we don't clash with the core versions.
namespace cart_closed

variables [has_finite_products.{v} C] [exponentiable A]

def curry : (A â¨¯ Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¹ X) :=
(exponentiable.is_adj.adj.hom_equiv _ _).to_fun
def uncurry : (Y âŸ¶ A âŸ¹ X) â†’ (A â¨¯ Y âŸ¶ X) :=
(exponentiable.is_adj.adj.hom_equiv _ _).inv_fun

end cart_closed

open cart_closed

variables [has_finite_products.{v} C] [exponentiable A]

@[reassoc]
lemma curry_natural_left (f : X âŸ¶ X') (g : A â¨¯ X' âŸ¶ Y) :
  curry (limits.prod.map (ğŸ™ _) f â‰« g) = f â‰« curry g :=
adjunction.hom_equiv_naturality_left _ _ _

@[reassoc]
lemma curry_natural_right (f : A â¨¯ X âŸ¶ Y) (g : Y âŸ¶ Y') :
  curry (f â‰« g) = curry f â‰« post _ g :=
adjunction.hom_equiv_naturality_right _ _ _

@[reassoc]
lemma uncurry_natural_right  (f : X âŸ¶ AâŸ¹Y) (g : Y âŸ¶ Y') :
  uncurry (f â‰« post A g) = uncurry f â‰« g :=
adjunction.hom_equiv_naturality_right_symm _ _ _

@[reassoc]
lemma uncurry_natural_left  (f : X âŸ¶ X') (g : X' âŸ¶ AâŸ¹Y) :
  uncurry (f â‰« g) = limits.prod.map (ğŸ™ _) f â‰« uncurry g :=
adjunction.hom_equiv_naturality_left_symm _ _ _

@[simp]
lemma uncurry_curry (f : A â¨¯ X âŸ¶ Y) : uncurry (curry f) = f :=
(exponentiable.is_adj.adj.hom_equiv _ _).left_inv f

@[simp]
lemma curry_uncurry (f : X âŸ¶ AâŸ¹Y) : curry (uncurry f) = f :=
(exponentiable.is_adj.adj.hom_equiv _ _).right_inv f

lemma curry_eq_iff (f : A â¨¯ Y âŸ¶ X) (g : Y âŸ¶ A âŸ¹ X) :
  curry f = g â†” f = uncurry g :=
adjunction.hom_equiv_apply_eq _ f g

lemma eq_curry_iff [exponentiable A] (f : A â¨¯ Y âŸ¶ X) (g : Y âŸ¶ A âŸ¹ X) :
  g = curry f â†” uncurry g = f :=
adjunction.eq_hom_equiv_apply _ f g

-- I don't think these two should be simp.
lemma uncurry_eq (g : Y âŸ¶ A âŸ¹ X) : uncurry g = limits.prod.map (ğŸ™ A) g â‰« ev A X :=
adjunction.hom_equiv_counit _

lemma curry_eq (g : A â¨¯ Y âŸ¶ X) : curry g = coev A Y â‰« post A g :=
adjunction.hom_equiv_unit _

lemma uncurry_id_eq_ev (A X : C) [exponentiable A] : uncurry (ğŸ™ (A âŸ¹ X)) = ev A X :=
by rw [uncurry_eq, prod_map_id_id, id_comp]

lemma curry_id_eq_coev (A X : C) [exponentiable A] : curry (ğŸ™ _) = coev A X :=
by { rw [curry_eq, post, functor.map_id], apply comp_id }

lemma curry_injective : function.injective (curry : (A â¨¯ Y âŸ¶ X) â†’ (Y âŸ¶ A âŸ¹ X)) :=
(exponentiable.is_adj.adj.hom_equiv _ _).injective

lemma uncurry_injective : function.injective (uncurry : (Y âŸ¶ A âŸ¹ X) â†’ (A â¨¯ Y âŸ¶ X)) :=
(exponentiable.is_adj.adj.hom_equiv _ _).symm.injective

section terminal
variable [has_finite_products.{v} C]


-- This isn't an instance because most of the time we'll prove cartesian closed for all objects
-- at once, rather than just for this one.
def terminal_exponentiable : exponentiable âŠ¤_C :=
{ is_adj :=
  { right := ğŸ­ C,
    adj := adjunction.mk_of_hom_equiv
    { hom_equiv := Î» X _, have unitor : _, from prod.left_unitor X,
        âŸ¨Î» a, unitor.inv â‰« a, Î» a, unitor.hom â‰« a, by tidy, by tidyâŸ© } } }

/-- The typeclass argument is explicit so that any instance can be used, not just the above. -/
def exp_terminal_iso [exponentiable âŠ¤_C] : (âŠ¤_C âŸ¹ X) â‰… X :=
yoneda.ext (âŠ¤_ C âŸ¹ X) X
  (Î» Y f, (prod.left_unitor Y).inv â‰« uncurry f)
  (Î» Y f, curry ((prod.left_unitor Y).hom â‰« f))
  (Î» Z g, by rw [curry_eq_iff, iso.hom_inv_id_assoc] )
  (Î» Z g, by simp)
  (Î» Z W f g, by rw [uncurry_natural_left, prod_left_unitor_inv_naturality_assoc f] )

@[reducible]
def point_at_hom [exponentiable A] (f : A âŸ¶ Y) : âŠ¤_C âŸ¶ (A âŸ¹ Y) :=
curry (limits.prod.fst â‰« f)

end terminal

section pre

variables [has_finite_products.{v} C] {B}

def pre (X : C) (f : B âŸ¶ A) [exponentiable A] [exponentiable B] :  (AâŸ¹X) âŸ¶ BâŸ¹X :=
curry (limits.prod.map f (ğŸ™ _) â‰« ev A X)

lemma pre_id (A X : C) [exponentiable A] : pre X (ğŸ™ A) = ğŸ™ (AâŸ¹X) :=
by { rw [pre, prod_map_id_id, id_comp, â† uncurry_id_eq_ev], simp }

-- There's probably a better proof of this somehow
lemma pre_map [exponentiable A] [exponentiable B] {D : C} [exponentiable D] (f : A âŸ¶ B) (g : B âŸ¶ D) :
  pre X (f â‰« g) = pre X g â‰« pre X f :=
begin
  rw [pre, curry_eq_iff, pre, pre, uncurry_natural_left, uncurry_curry, prod_map_comm_assoc,
      prod_functorial, assoc, â† uncurry_id_eq_ev, â† uncurry_id_eq_ev, â† uncurry_natural_left,
      curry_natural_right, comp_id, uncurry_natural_right, uncurry_curry],
end

end pre

@[simps]
def pre.functor [has_finite_products.{v} C] [is_cartesian_closed C] (X : C) : Cáµ’áµ– â¥¤ C :=
{ obj := Î» A, (A.unop) âŸ¹ X,
  map := Î» A B f, pre X f.unop,
  map_id' := Î» B, pre_id B.unop X,
  map_comp' := Î» P Q R f g, pre_map g.unop f.unop }

lemma exp_natural [has_finite_products.{v} C] [is_cartesian_closed C] {A B : C} {X Y : Cáµ’áµ–} (f : A âŸ¶ B) (g : X âŸ¶ Y) :
  (pre.functor A).map g â‰« post (opposite.unop Y) f = post (opposite.unop X) f â‰« (pre.functor B).map g :=
begin
  dsimp [pre],
  rw [â† curry_natural_left, eq_curry_iff, uncurry_natural_right, uncurry_curry, prod_map_comm_assoc],
  simp,
end

def exp.difunctor [has_finite_products.{v} C] [is_cartesian_closed C] : C â¥¤ Cáµ’áµ– â¥¤ C :=
{ obj := pre.functor,
  map := Î» A B f, { app := Î» X, post X.unop f, naturality' := Î» X Y g, exp_natural _ _ },
  map_id' := Î» X, by { ext, apply functor.map_id },
  map_comp' := Î» X Y Z f g, by { ext, apply functor.map_comp } }

@[simps]
def zero_mul [has_initial.{v} C] : A â¨¯ âŠ¥_ C â‰… âŠ¥_ C :=
{ hom := limits.prod.snd,
  inv := default (âŠ¥_ C âŸ¶ A â¨¯ âŠ¥_ C),
  hom_inv_id' :=
  begin
    have: (limits.prod.snd : A â¨¯ âŠ¥_ C âŸ¶ âŠ¥_ C) = uncurry (default _),
      rw â† curry_eq_iff,
      apply subsingleton.elim,
    rw [this, â† uncurry_natural_right, â† eq_curry_iff],
    apply subsingleton.elim
  end,
  }

def mul_zero [has_initial.{v} C] : âŠ¥_ C â¨¯ A â‰… âŠ¥_ C :=
limits.prod.braiding _ _ â‰ªâ‰« zero_mul

def pow_zero [has_initial.{v} C] [is_cartesian_closed C] : âŠ¥_C âŸ¹ B â‰… âŠ¤_ C :=
{ hom := default _,
  inv := curry (mul_zero.hom â‰« default (âŠ¥_ C âŸ¶ B)),
  hom_inv_id' :=
  begin
    rw [â† curry_natural_left, curry_eq_iff, â† cancel_epi mul_zero.inv],
    { apply subsingleton.elim },
    { apply_instance },
    { apply_instance }
  end }

-- MOVE TO SOMEWHERE ELSE BEFORE PR
def is_iso_of_mono_of_split_epi {X Y : C} (f : X âŸ¶ Y) [mono f] [split_epi f] : is_iso f :=
{ inv := section_ f,
  hom_inv_id' := by simp [â† cancel_mono f] }

instance strict_initial [has_initial.{v} C] {f : A âŸ¶ âŠ¥_ C} : is_iso f :=
begin
  haveI : mono (limits.prod.lift (ğŸ™ A) f â‰« zero_mul.hom) := mono_comp _ _,
  rw [zero_mul_hom, prod.lift_snd] at _inst,
  haveI: split_epi f := âŸ¨default _, subsingleton.elim _ _âŸ©,
  apply is_iso_of_mono_of_split_epi
end

instance initial_mono (B : C) [has_initial.{v} C] [is_cartesian_closed C] : mono (initial.to B) :=
âŸ¨Î» B g h _, eq_of_inv_eq_inv (subsingleton.elim (inv g) (inv h))âŸ©

variables {D : Type uâ‚‚} [category.{v} D]
section functor

variables [has_finite_products.{v} C] [has_finite_products.{v} D]

/--
Note we didn't require any coherence between the choice of finite products here, since we transport
along the `mult_comparison` isomorphism.
-/
def cartesian_closed_of_equiv (e : C â‰Œ D) [h : is_cartesian_closed C] : is_cartesian_closed D :=
{ cart_closed := Î» X,
  { is_adj :=
    begin
      haveI q : exponentiable (e.inverse.obj X) := infer_instance,
      have := q.is_adj,
      have: e.functor â‹™ prod_functor.obj X â‹™ e.inverse â‰… prod_functor.obj (e.inverse.obj X),
      apply nat_iso.of_components _ _,
      intro Y,
      apply mult_comparison e.inverse X (e.functor.obj Y) â‰ªâ‰« _,
      refine âŸ¨limits.prod.map (ğŸ™ _) (e.unit_inv.app _),
              limits.prod.map (ğŸ™ _) (e.unit.app _),
              by simpa [â† prod_functorial', prod_map_id_id],
              by simpa [â† prod_functorial', prod_map_id_id]âŸ©,
      intros Y Z g,
      simp only [mult_comparison, prod.lift_map, equivalence.unit_inv, functor.comp_map,
                 prod_functor_obj_map, assoc, comp_id, iso.trans_hom],
      apply prod.hom_ext,
      rw [assoc, prod.lift_fst, prod.lift_fst, â† functor.map_comp, limits.prod.map_fst, comp_id],
      rw [assoc, prod.lift_snd, prod.lift_snd, â† functor.map_comp_assoc, limits.prod.map_snd],
      simp only [equivalence.unit, equivalence.unit_inv, nat_iso.hom_inv_id_app, assoc, equivalence.inv_fun_map, functor.map_comp, comp_id],
      erw comp_id,
      haveI : is_left_adjoint (e.functor â‹™ prod_functor.obj X â‹™ e.inverse) := adjunction.left_adjoint_of_nat_iso this.symm,
      haveI : is_left_adjoint e.inverse := functor.left_adjoint_of_equivalence,
      haveI : is_left_adjoint e.functor := functor.left_adjoint_of_equivalence,
      haveI : is_left_adjoint (e.inverse â‹™ e.functor â‹™ prod_functor.obj X â‹™ e.inverse) := adjunction.left_adjoint_of_comp e.inverse _,
      haveI := adjunction.left_adjoint_of_comp (e.inverse â‹™ e.functor â‹™ prod_functor.obj X â‹™ e.inverse) e.functor,
      have : (e.inverse â‹™ e.functor â‹™ prod_functor.obj X â‹™ e.inverse) â‹™ e.functor â‰… prod_functor.obj X,
        apply iso_whisker_right e.counit_iso (prod_functor.obj X â‹™ e.inverse â‹™ e.functor) â‰ªâ‰« _,
        change prod_functor.obj X â‹™ e.inverse â‹™ e.functor â‰… prod_functor.obj X,
        apply iso_whisker_left (prod_functor.obj X) e.counit_iso,
      apply adjunction.left_adjoint_of_nat_iso this,
    end
  }
}

variables [is_cartesian_closed C] [is_cartesian_closed D]
variables (F : C â¥¤ D) [preserves_limits_of_shape (discrete walking_pair) F]

/--
The exponential comparison map.
`F` is a cartesian closed functor if this is an iso for all `A,B`.
-/
def exp_comparison (A B : C) :
  F.obj (A âŸ¹ B) âŸ¶ F.obj A âŸ¹ F.obj B :=
curry ((mult_comparison F A _).inv â‰« F.map (ev _ _))

lemma exp_comparison_natural_left (A A' B : C) (f : A' âŸ¶ A) :
  exp_comparison F A B â‰« pre (F.obj B) (F.map f) = F.map (pre B f) â‰« exp_comparison F A' B :=
by rw [exp_comparison, exp_comparison, â† curry_natural_left, eq_curry_iff, uncurry_natural_left,
       pre, uncurry_curry, prod_map_comm_assoc, curry_eq, prod_functorial', assoc, ev_nat,
       ev_coev_assoc, â† F.map_id, â† mult_comparison_inv_natural_assoc, â† F.map_id,
       â† mult_comparison_inv_natural_assoc, â† F.map_comp, â† F.map_comp, pre, curry_eq,
       prod_functorial', assoc, ev_nat, ev_coev_assoc]

lemma exp_comparison_natural_right (A B B' : C) (f : B âŸ¶ B') :
  exp_comparison F A B â‰« post (F.obj A) (F.map f) = F.map (post A f) â‰« exp_comparison F A B' :=
by rw [exp_comparison, â† curry_natural_right, curry_eq_iff, exp_comparison, uncurry_natural_left,
      uncurry_curry, assoc, â† F.map_comp, â† ev_nat, F.map_comp, mult_comparison_inv_natural_assoc,
      F.map_id]

-- TODO: If F has a left adjoint L, then F is cartesian closed if and only if
-- L (B â¨¯ F A) âŸ¶ L B â¨¯ L F A âŸ¶ L B â¨¯ A
-- is an iso for all A âˆˆ D, B âˆˆ C.
-- Corollary: If F has a left adjoint L which preserves finite products, F is cartesian closed iff
-- F is full and faithful.

end functor

end category_theory
