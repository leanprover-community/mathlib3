/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Jo√´l Riou
-/
import category_theory.comm_sq
import category_theory.limits.opposites
import category_theory.limits.shapes.biproducts
import category_theory.limits.shapes.zero_morphisms
import category_theory.limits.constructions.binary_products
import category_theory.limits.constructions.zero_objects

/-!
# Pullback and pushout squares, and bicartesian squares

We provide another API for pullbacks and pushouts.

`is_pullback fst snd f g` is the proposition that
```
  P --fst--> X
  |          |
 snd         f
  |          |
  v          v
  Y ---g---> Z

```
is a pullback square.

(And similarly for `is_pushout`.)

We provide the glue to go back and forth to the usual `is_limit` API for pullbacks, and prove
`is_pullback (pullback.fst : pullback f g ‚ü∂ X) (pullback.snd : pullback f g ‚ü∂ Y) f g`
for the usual `pullback f g` provided by the `has_limit` API.

We don't attempt to restate everything we know about pullbacks in this language,
but do restate the pasting lemmas.

We define bicartesian squares, and
show that the pullback and pushout squares for a biproduct are bicartesian.
-/

noncomputable theory

open category_theory
open category_theory.limits

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace category_theory

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C]

attribute [simp] comm_sq.mk

namespace comm_sq

variables {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

/--
The (not necessarily limiting) `pullback_cone h i` implicit in the statement
that we have `comm_sq f g h i`.
-/
def cone (s : comm_sq f g h i) : pullback_cone h i := pullback_cone.mk _ _ s.w

/--
The (not necessarily limiting) `pushout_cocone f g` implicit in the statement
that we have `comm_sq f g h i`.
-/
def cocone (s : comm_sq f g h i) : pushout_cocone f g := pushout_cocone.mk _ _ s.w

@[simp] lemma cone_fst (s : comm_sq f g h i) : s.cone.fst = f := rfl
@[simp] lemma cone_snd (s : comm_sq f g h i) : s.cone.snd = g := rfl
@[simp] lemma cocone_inl (s : comm_sq f g h i) : s.cocone.inl = h := rfl
@[simp] lemma cocone_inr (s : comm_sq f g h i) : s.cocone.inr = i := rfl

/-- The pushout cocone in the opposite category associated to the cone of
a commutative square identifies to the cocone of the flipped commutative square in
the opposite category -/
def cone_op (p : comm_sq f g h i) : p.cone.op ‚âÖ p.flip.op.cocone :=
pushout_cocone.ext (iso.refl _) (by tidy) (by tidy)

/-- The pullback cone in the opposite category associated to the cocone of
a commutative square identifies to the cone of the flipped commutative square in
the opposite category -/
def cocone_op (p : comm_sq f g h i) : p.cocone.op ‚âÖ p.flip.op.cone :=
pullback_cone.ext (iso.refl _) (by tidy) (by tidy)

/-- The pushout cocone obtained from the pullback cone associated to a
commutative square in the opposite category identifies to the cocone associated
to the flipped square. -/
def cone_unop {W X Y Z : C·µí·µñ} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : p.cone.unop ‚âÖ p.flip.unop.cocone :=
pushout_cocone.ext (iso.refl _) (by tidy) (by tidy)

/-- The pullback cone obtained from the pushout cone associated to a
commutative square in the opposite category identifies to the cone associated
to the flipped square. -/
def cocone_unop {W X Y Z : C·µí·µñ} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}
  (p : comm_sq f g h i) : p.cocone.unop ‚âÖ p.flip.unop.cone :=
pullback_cone.ext (iso.refl _) (by tidy) (by tidy)

end comm_sq

/-- The proposition that a square
```
  P --fst--> X
  |          |
 snd         f
  |          |
  v          v
  Y ---g---> Z

```
is a pullback square. (Also known as a fibered product or cartesian square.)
-/
structure is_pullback {P X Y Z : C} (fst : P ‚ü∂ X) (snd : P ‚ü∂ Y) (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z)
  extends comm_sq fst snd f g : Prop :=
(is_limit' : nonempty (is_limit (pullback_cone.mk _ _ w)))

/-- The proposition that a square
```
  Z ---f---> X
  |          |
  g         inl
  |          |
  v          v
  Y --inr--> P

```
is a pushout square. (Also known as a fiber coproduct or cocartesian square.)
-/
structure is_pushout {Z X Y P : C} (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) (inl : X ‚ü∂ P) (inr : Y ‚ü∂ P)
  extends comm_sq f g inl inr : Prop :=
(is_colimit' : nonempty (is_colimit (pushout_cocone.mk _ _ w)))


section
set_option old_structure_cmd true

/-- A *bicartesian* square is a commutative square
```
  W ---f---> X
  |          |
  g          h
  |          |
  v          v
  Y ---i---> Z

```
that is both a pullback square and a pushout square.
-/
structure bicartesian_sq {W X Y Z : C} (f : W ‚ü∂ X) (g : W ‚ü∂ Y) (h : X ‚ü∂ Z) (i : Y ‚ü∂ Z)
  extends is_pullback f g h i, is_pushout f g h i : Prop

-- Lean should make these parent projections as `lemma`, not `def`.
attribute [nolint def_lemma doc_blame] bicartesian_sq.to_is_pullback bicartesian_sq.to_is_pushout

end

/-!
We begin by providing some glue between `is_pullback` and the `is_limit` and `has_limit` APIs.
(And similarly for `is_pushout`.)
-/

namespace is_pullback

variables {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

/--
The (limiting) `pullback_cone f g` implicit in the statement
that we have a `is_pullback fst snd f g`.
-/
def cone (h : is_pullback fst snd f g) : pullback_cone f g := h.to_comm_sq.cone

@[simp] lemma cone_fst (h : is_pullback fst snd f g) : h.cone.fst = fst := rfl
@[simp] lemma cone_snd (h : is_pullback fst snd f g) : h.cone.snd = snd := rfl

/--
The cone obtained from `is_pullback fst snd f g` is a limit cone.
-/
noncomputable def is_limit (h : is_pullback fst snd f g) : is_limit h.cone :=
h.is_limit'.some

/-- If `c` is a limiting pullback cone, then we have a `is_pullback c.fst c.snd f g`. -/
lemma of_is_limit {c : pullback_cone f g} (h : limits.is_limit c) :
  is_pullback c.fst c.snd f g :=
{ w := c.condition,
  is_limit' := ‚ü®is_limit.of_iso_limit h
    (limits.pullback_cone.ext (iso.refl _) (by tidy) (by tidy))‚ü©, }

/-- A variant of `of_is_limit` that is more useful with `apply`. -/
lemma of_is_limit' (w : comm_sq fst snd f g) (h : limits.is_limit w.cone) :
  is_pullback fst snd f g :=
of_is_limit h

/-- The pullback provided by `has_pullback f g` fits into a `is_pullback`. -/
lemma of_has_pullback (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [has_pullback f g] :
  is_pullback (pullback.fst : pullback f g ‚ü∂ X) (pullback.snd : pullback f g ‚ü∂ Y) f g :=
of_is_limit (limit.is_limit (cospan f g))

/-- If `c` is a limiting binary product cone, and we have a terminal object,
then we have `is_pullback c.fst c.snd 0 0`
(where each `0` is the unique morphism to the terminal object). -/
lemma of_is_product {c : binary_fan X Y} (h : limits.is_limit c) (t : is_terminal Z) :
  is_pullback c.fst c.snd (t.from _) (t.from _) :=
of_is_limit (is_pullback_of_is_terminal_is_product _ _ _ _ t
  (is_limit.of_iso_limit h (limits.cones.ext (iso.refl c.X) (by rintro ‚ü®‚ü®‚ü©‚ü©; { dsimp, simp, }))))

/-- A variant of `of_is_product` that is more useful with `apply`. -/
lemma of_is_product' (h : limits.is_limit (binary_fan.mk fst snd)) (t : is_terminal Z) :
  is_pullback fst snd (t.from _) (t.from _) :=
of_is_product h t

variables (X Y)

lemma of_has_binary_product' [has_binary_product X Y] [has_terminal C] :
  is_pullback limits.prod.fst limits.prod.snd (terminal.from X) (terminal.from Y) :=
of_is_product (limit.is_limit _) terminal_is_terminal

open_locale zero_object

lemma of_has_binary_product [has_binary_product X Y] [has_zero_object C] [has_zero_morphisms C] :
  is_pullback limits.prod.fst limits.prod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
by convert of_is_product (limit.is_limit _) has_zero_object.zero_is_terminal

variables {X Y}

/-- Any object at the top left of a pullback square is
isomorphic to the pullback provided by the `has_limit` API. -/
noncomputable
def iso_pullback (h : is_pullback fst snd f g) [has_pullback f g] : P ‚âÖ pullback f g :=
(limit.iso_limit_cone ‚ü®_, h.is_limit‚ü©).symm

@[simp] lemma iso_pullback_hom_fst (h : is_pullback fst snd f g) [has_pullback f g] :
  h.iso_pullback.hom ‚â´ pullback.fst = fst :=
by { dsimp [iso_pullback, cone, comm_sq.cone], simp, }
@[simp] lemma iso_pullback_hom_snd (h : is_pullback fst snd f g) [has_pullback f g] :
  h.iso_pullback.hom ‚â´ pullback.snd = snd :=
by { dsimp [iso_pullback, cone, comm_sq.cone], simp, }
@[simp] lemma iso_pullback_inv_fst (h : is_pullback fst snd f g) [has_pullback f g] :
  h.iso_pullback.inv ‚â´ fst = pullback.fst :=
by simp [iso.inv_comp_eq]
@[simp] lemma iso_pullback_inv_snd (h : is_pullback fst snd f g) [has_pullback f g] :
  h.iso_pullback.inv ‚â´ snd = pullback.snd :=
by simp [iso.inv_comp_eq]

lemma of_iso_pullback (h : comm_sq fst snd f g) [has_pullback f g] (i : P ‚âÖ pullback f g)
  (w‚ÇÅ : i.hom ‚â´ pullback.fst = fst) (w‚ÇÇ : i.hom ‚â´ pullback.snd = snd) : is_pullback fst snd f g :=
of_is_limit' h (limits.is_limit.of_iso_limit (limit.is_limit _)
  (@pullback_cone.ext _ _ _ _ _ _ _ (pullback_cone.mk _ _ _) _ i w‚ÇÅ.symm w‚ÇÇ.symm).symm)

lemma of_horiz_is_iso [is_iso fst] [is_iso g] (sq : comm_sq fst snd f g) :
  is_pullback fst snd f g := of_is_limit' sq
begin
  refine pullback_cone.is_limit.mk _ (Œª s, s.fst ‚â´ inv fst) (by tidy) (Œª s, _) (by tidy),
  simp only [‚Üê cancel_mono g, category.assoc, ‚Üê sq.w, is_iso.inv_hom_id_assoc, s.condition],
end

end is_pullback

namespace is_pushout

variables {Z X Y P : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}

/--
The (colimiting) `pushout_cocone f g` implicit in the statement
that we have a `is_pushout f g inl inr`.
-/
def cocone (h : is_pushout f g inl inr) : pushout_cocone f g := h.to_comm_sq.cocone

@[simp] lemma cocone_inl (h : is_pushout f g inl inr) : h.cocone.inl = inl := rfl
@[simp] lemma cocone_inr (h : is_pushout f g inl inr) : h.cocone.inr = inr := rfl

/--
The cocone obtained from `is_pushout f g inl inr` is a colimit cocone.
-/
noncomputable def is_colimit (h : is_pushout f g inl inr) : is_colimit h.cocone :=
h.is_colimit'.some

/-- If `c` is a colimiting pushout cocone, then we have a `is_pushout f g c.inl c.inr`. -/
lemma of_is_colimit {c : pushout_cocone f g} (h : limits.is_colimit c) :
  is_pushout f g c.inl c.inr :=
{ w := c.condition,
  is_colimit' := ‚ü®is_colimit.of_iso_colimit h
    (limits.pushout_cocone.ext (iso.refl _) (by tidy) (by tidy))‚ü©, }

/-- A variant of `of_is_colimit` that is more useful with `apply`. -/
lemma of_is_colimit' (w : comm_sq f g inl inr) (h : limits.is_colimit w.cocone) :
  is_pushout f g inl inr :=
of_is_colimit h

/-- The pushout provided by `has_pushout f g` fits into a `is_pushout`. -/
lemma of_has_pushout (f : Z ‚ü∂ X) (g : Z ‚ü∂ Y) [has_pushout f g] :
  is_pushout f g (pushout.inl : X ‚ü∂ pushout f g) (pushout.inr : Y ‚ü∂ pushout f g) :=
of_is_colimit (colimit.is_colimit (span f g))

/-- If `c` is a colimiting binary coproduct cocone, and we have an initial object,
then we have `is_pushout 0 0 c.inl c.inr`
(where each `0` is the unique morphism from the initial object). -/
lemma of_is_coproduct {c : binary_cofan X Y} (h : limits.is_colimit c) (t : is_initial Z) :
  is_pushout (t.to _) (t.to _) c.inl c.inr :=
of_is_colimit (is_pushout_of_is_initial_is_coproduct _ _ _ _ t
  (is_colimit.of_iso_colimit h
    (limits.cocones.ext (iso.refl c.X) (by rintro ‚ü®‚ü®‚ü©‚ü©; { dsimp, simp, }))))

/-- A variant of `of_is_coproduct` that is more useful with `apply`. -/
lemma of_is_coproduct' (h : limits.is_colimit (binary_cofan.mk inl inr)) (t : is_initial Z) :
  is_pushout (t.to _) (t.to _) inl inr :=
of_is_coproduct h t

variables (X Y)

lemma of_has_binary_coproduct' [has_binary_coproduct X Y] [has_initial C] :
  is_pushout (initial.to _) (initial.to _) (coprod.inl : X ‚ü∂ _) (coprod.inr : Y ‚ü∂ _)  :=
of_is_coproduct (colimit.is_colimit _) initial_is_initial

open_locale zero_object

lemma of_has_binary_coproduct
  [has_binary_coproduct X Y] [has_zero_object C] [has_zero_morphisms C] :
  is_pushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) coprod.inl coprod.inr :=
by convert of_is_coproduct (colimit.is_colimit _) has_zero_object.zero_is_initial

variables {X Y}

/-- Any object at the top left of a pullback square is
isomorphic to the pullback provided by the `has_limit` API. -/
noncomputable
def iso_pushout (h : is_pushout f g inl inr) [has_pushout f g] : P ‚âÖ pushout f g :=
(colimit.iso_colimit_cocone ‚ü®_, h.is_colimit‚ü©).symm

@[simp] lemma inl_iso_pushout_inv (h : is_pushout f g inl inr) [has_pushout f g] :
  pushout.inl ‚â´ h.iso_pushout.inv = inl :=
by { dsimp [iso_pushout, cocone, comm_sq.cocone], simp, }
@[simp] lemma inr_iso_pushout_inv (h : is_pushout f g inl inr) [has_pushout f g] :
  pushout.inr ‚â´ h.iso_pushout.inv = inr :=
by { dsimp [iso_pushout, cocone, comm_sq.cocone], simp, }
@[simp] lemma inl_iso_pushout_hom (h : is_pushout f g inl inr) [has_pushout f g] :
  inl ‚â´ h.iso_pushout.hom = pushout.inl :=
by simp [‚Üêiso.eq_comp_inv]
@[simp] lemma inr_iso_pushout_hom (h : is_pushout f g inl inr) [has_pushout f g] :
  inr ‚â´ h.iso_pushout.hom = pushout.inr :=
by simp [‚Üêiso.eq_comp_inv]

lemma of_iso_pushout (h : comm_sq f g inl inr) [has_pushout f g] (i : P ‚âÖ pushout f g)
  (w‚ÇÅ : inl ‚â´ i.hom = pushout.inl) (w‚ÇÇ : inr ‚â´ i.hom = pushout.inr) : is_pushout f g inl inr :=
of_is_colimit' h (limits.is_colimit.of_iso_colimit (colimit.is_colimit _)
  (@pushout_cocone.ext _ _ _ _ _ _ _ (pushout_cocone.mk _ _ _) _ i w‚ÇÅ w‚ÇÇ).symm)

end is_pushout

namespace is_pullback

variables {P X Y Z : C} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

lemma flip (h : is_pullback fst snd f g) : is_pullback snd fst g f :=
of_is_limit (@pullback_cone.flip_is_limit _ _ _ _ _ _ _ _ _ _ h.w.symm h.is_limit)

lemma flip_iff : is_pullback fst snd f g ‚Üî is_pullback snd fst g f :=
‚ü®flip, flip‚ü©

section

variables [has_zero_object C] [has_zero_morphisms C]
open_locale zero_object

/-- The square with `0 : 0 ‚ü∂ 0` on the left and `ùüô X` on the right is a pullback square. -/
@[simp] lemma zero_left (X : C) : is_pullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ X) :=
{ w := by simp,
  is_limit' :=
  ‚ü®{ lift := Œª s, 0,
     fac' := Œª s, by simpa using @pullback_cone.equalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _)
       (by simpa using (pullback_cone.condition s).symm), }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the top and `ùüô X` on the bottom is a pullback square. -/
@[simp] lemma zero_top (X : C) : is_pullback (0 : 0 ‚ü∂ 0) (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ X) (ùüô X) :=
(zero_left X).flip

/-- The square with `0 : 0 ‚ü∂ 0` on the right and `ùüô X` on the left is a pullback square. -/
@[simp] lemma zero_right (X : C) : is_pullback (0 : X ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ 0) (0 : X ‚ü∂ 0) :=
of_iso_pullback (by simp) ((zero_prod_iso X).symm ‚â™‚â´ (pullback_zero_zero_iso _ _).symm)
  (by simp) (by simp)

/-- The square with `0 : 0 ‚ü∂ 0` on the bottom and `ùüô X` on the top is a pullback square. -/
@[simp] lemma zero_bot (X : C) : is_pullback (ùüô X) (0 : X ‚ü∂ 0) (0 : X ‚ü∂ 0) (0 : 0 ‚ü∂ 0) :=
(zero_right X).flip

end

/-- Paste two pullback squares "vertically" to obtain another pullback square. -/
-- Objects here are arranged in a 3x2 grid, and indexed by their xy coordinates.
-- Morphisms are named `h·µ¢‚±º` for a horizontal morphism starting at `(i,j)`,
-- and `v·µ¢‚±º` for a vertical morphism starting at `(i,j)`.
lemma paste_vert {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
  is_pullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ :=
(of_is_limit
  (big_square_is_pullback _ _ _ _ _ _ _ s.w t.w t.is_limit s.is_limit))

/-- Paste two pullback squares "horizontally" to obtain another pullback square. -/
lemma paste_horiz {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
  is_pullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) :=
(paste_vert s.flip t.flip).flip

/-- Given a pullback square assembled from a commuting square on the top and
a pullback square on the bottom, the top square is a pullback square. -/
lemma of_bot {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ) (p : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ)
  (t : is_pullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
  is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
of_is_limit (left_square_is_pullback _ _ _ _ _ _ _ p _ t.is_limit s.is_limit)

/-- Given a pullback square assembled from a commuting square on the left and
a pullback square on the right, the left square is a pullback square. -/
lemma of_right {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ)) (p : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ)
  (t : is_pullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
  is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
(of_bot s.flip p.symm t.flip).flip

lemma paste_vert_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pullback h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) (e : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ) :
  is_pullback h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ ‚Üî is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
‚ü®Œª h, h.of_bot e s, Œª h, h.paste_vert s‚ü©

lemma paste_horiz_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pullback h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) (e : h‚ÇÅ‚ÇÅ ‚â´ v‚ÇÅ‚ÇÇ = v‚ÇÅ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÅ) :
  is_pullback (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) ‚Üî is_pullback h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ :=
‚ü®Œª h, h.of_right e s, Œª h, h.paste_horiz s‚ü©

section

variables [has_zero_object C] [has_zero_morphisms C]
open_locale zero_object

lemma of_is_bilimit {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pullback b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
by convert is_pullback.of_is_product' h.is_limit has_zero_object.zero_is_terminal

@[simp] lemma of_has_biproduct (X Y : C) [has_binary_biproduct X Y] :
  is_pullback biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
of_is_bilimit (binary_biproduct.is_bilimit X Y)

lemma inl_snd' {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pullback b.inl (0 : X ‚ü∂ 0) b.snd (0 : 0 ‚ü∂ Y) :=
by { refine of_right _ (by simp) (of_is_bilimit h), simp, }

/--
The square
```
  X --inl--> X ‚äû Y
  |            |
  0           snd
  |            |
  v            v
  0 ---0-----> Y
```
is a pullback square.
-/
@[simp] lemma inl_snd (X Y : C) [has_binary_biproduct X Y] :
  is_pullback biprod.inl (0 : X ‚ü∂ 0) biprod.snd (0 : 0 ‚ü∂ Y) :=
inl_snd' (binary_biproduct.is_bilimit X Y)

lemma inr_fst' {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pullback b.inr (0 : Y ‚ü∂ 0) b.fst (0 : 0 ‚ü∂ X) :=
by { apply flip, refine of_bot _ (by simp) (of_is_bilimit h), simp, }

/--
The square
```
  Y --inr--> X ‚äû Y
  |            |
  0           fst
  |            |
  v            v
  0 ---0-----> X
```
is a pullback square.
-/
@[simp] lemma inr_fst (X Y : C) [has_binary_biproduct X Y] :
  is_pullback biprod.inr (0 : Y ‚ü∂ 0) biprod.fst (0 : 0 ‚ü∂ X) :=
inr_fst' (binary_biproduct.is_bilimit X Y)

lemma of_is_bilimit' {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr :=
by { refine is_pullback.of_right _ (by simp) (is_pullback.inl_snd' h).flip, simp, }

lemma of_has_binary_biproduct (X Y : C) [has_binary_biproduct X Y] :
  is_pullback (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr :=
of_is_bilimit' (binary_biproduct.is_bilimit X Y)

instance has_pullback_biprod_fst_biprod_snd [has_binary_biproduct X Y] :
  has_pullback (biprod.inl : X ‚ü∂ _) (biprod.inr : Y ‚ü∂ _) :=
has_limit.mk ‚ü®_, (of_has_binary_biproduct X Y).is_limit‚ü©

/-- The pullback of `biprod.inl` and `biprod.inr` is the zero object. -/
def pullback_biprod_inl_biprod_inr [has_binary_biproduct X Y] :
  pullback (biprod.inl : X ‚ü∂ _) (biprod.inr : Y ‚ü∂ _) ‚âÖ 0 :=
limit.iso_limit_cone ‚ü®_, (of_has_binary_biproduct X Y).is_limit‚ü©

end

lemma op (h : is_pullback fst snd f g) : is_pushout g.op f.op snd.op fst.op :=
is_pushout.of_is_colimit (is_colimit.of_iso_colimit
  (limits.pullback_cone.is_limit_equiv_is_colimit_op h.flip.cone h.flip.is_limit)
  h.to_comm_sq.flip.cone_op)

lemma unop {P X Y Z : C·µí·µñ} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}
  (h : is_pullback fst snd f g) : is_pushout g.unop f.unop snd.unop fst.unop :=
is_pushout.of_is_colimit (is_colimit.of_iso_colimit
  (limits.pullback_cone.is_limit_equiv_is_colimit_unop h.flip.cone h.flip.is_limit)
  h.to_comm_sq.flip.cone_unop)

lemma of_vert_is_iso [is_iso snd] [is_iso f] (sq : comm_sq fst snd f g) :
  is_pullback fst snd f g := is_pullback.flip (of_horiz_is_iso sq.flip)

end is_pullback

namespace is_pushout

variables {Z X Y P : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}

lemma flip (h : is_pushout f g inl inr) : is_pushout g f inr inl :=
of_is_colimit (@pushout_cocone.flip_is_colimit _ _ _ _ _ _ _ _ _ _ h.w.symm h.is_colimit)

lemma flip_iff : is_pushout f g inl inr ‚Üî is_pushout g f inr inl :=
‚ü®flip, flip‚ü©

section

variables [has_zero_object C] [has_zero_morphisms C]
open_locale zero_object

/-- The square with `0 : 0 ‚ü∂ 0` on the right and `ùüô X` on the left is a pushout square. -/
@[simp] lemma zero_right (X : C) : is_pushout (0 : X ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ 0) (0 : X ‚ü∂ 0) :=
{ w := by simp,
  is_colimit' :=
  ‚ü®{ desc := Œª s, 0,
     fac' := Œª s, begin
       have c := @pushout_cocone.coequalizer_ext _ _ _ _ _ _ _ s _ 0 (ùüô _) (by simp)
         (by simpa using (pushout_cocone.condition s)),
      dsimp at c,
      simpa using c,
     end }‚ü© }

/-- The square with `0 : 0 ‚ü∂ 0` on the bottom and `ùüô X` on the top is a pushout square. -/
@[simp] lemma zero_bot (X : C) : is_pushout (ùüô X) (0 : X ‚ü∂ 0) (0 : X ‚ü∂ 0) (0 : 0 ‚ü∂ 0) :=
(zero_right X).flip

/-- The square with `0 : 0 ‚ü∂ 0` on the right left `ùüô X` on the right is a pushout square. -/
@[simp] lemma zero_left (X : C) : is_pushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ 0) (ùüô X) (0 : 0 ‚ü∂ X) :=
of_iso_pushout (by simp) ((coprod_zero_iso X).symm ‚â™‚â´ (pushout_zero_zero_iso _ _).symm)
  (by simp) (by simp)

/-- The square with `0 : 0 ‚ü∂ 0` on the top and `ùüô X` on the bottom is a pushout square. -/
@[simp] lemma zero_top (X : C) : is_pushout (0 : 0 ‚ü∂ 0) (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ X) (ùüô X) :=
(zero_left X).flip

end

/-- Paste two pushout squares "vertically" to obtain another pushout square. -/
-- Objects here are arranged in a 3x2 grid, and indexed by their xy coordinates.
-- Morphisms are named `h·µ¢‚±º` for a horizontal morphism starting at `(i,j)`,
-- and `v·µ¢‚±º` for a vertical morphism starting at `(i,j)`.
lemma paste_vert {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ) :
  is_pushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ :=
(of_is_colimit
  (big_square_is_pushout _ _ _ _ _ _ _ s.w t.w t.is_colimit s.is_colimit))

/-- Paste two pushout squares "horizontally" to obtain another pushout square. -/
lemma paste_horiz {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (t : is_pushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ) :
  is_pushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) :=
(paste_vert s.flip t.flip).flip

/-- Given a pushout square assembled from a pushout square on the top and
a commuting square on the bottom, the bottom square is a pushout square. -/
lemma of_bot {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ) (p : h‚ÇÇ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÇ = v‚ÇÇ‚ÇÅ ‚â´ h‚ÇÉ‚ÇÅ)
  (t : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) :
  is_pushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ :=
of_is_colimit (right_square_is_pushout _ _ _ _ _ _ _ _ p t.is_colimit s.is_colimit)

/-- Given a pushout square assembled from a pushout square on the left and
a commuting square on the right, the right square is a pushout square. -/
lemma of_right {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ)) (p : h‚ÇÅ‚ÇÇ ‚â´ v‚ÇÅ‚ÇÉ = v‚ÇÅ‚ÇÇ ‚â´ h‚ÇÇ‚ÇÇ)
  (t : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) :
  is_pushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ :=
(of_bot s.flip p.symm t.flip).flip

lemma paste_vert_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÉ‚ÇÅ X‚ÇÉ‚ÇÇ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÉ‚ÇÅ : X‚ÇÉ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÇ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÉ‚ÇÅ} {v‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÉ‚ÇÇ}
  (s : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (e : h‚ÇÇ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÇ = v‚ÇÇ‚ÇÅ ‚â´ h‚ÇÉ‚ÇÅ) :
  is_pushout h‚ÇÅ‚ÇÅ (v‚ÇÅ‚ÇÅ ‚â´ v‚ÇÇ‚ÇÅ) (v‚ÇÅ‚ÇÇ ‚â´ v‚ÇÇ‚ÇÇ) h‚ÇÉ‚ÇÅ ‚Üî is_pushout h‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÅ v‚ÇÇ‚ÇÇ h‚ÇÉ‚ÇÅ :=
‚ü®Œª h, h.of_bot e s, s.paste_vert‚ü©

lemma paste_horiz_iff {X‚ÇÅ‚ÇÅ X‚ÇÅ‚ÇÇ X‚ÇÅ‚ÇÉ X‚ÇÇ‚ÇÅ X‚ÇÇ‚ÇÇ X‚ÇÇ‚ÇÉ : C}
  {h‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÅ‚ÇÇ} {h‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÅ‚ÇÉ} {h‚ÇÇ‚ÇÅ : X‚ÇÇ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÇ} {h‚ÇÇ‚ÇÇ : X‚ÇÇ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÉ}
  {v‚ÇÅ‚ÇÅ : X‚ÇÅ‚ÇÅ ‚ü∂ X‚ÇÇ‚ÇÅ} {v‚ÇÅ‚ÇÇ : X‚ÇÅ‚ÇÇ ‚ü∂ X‚ÇÇ‚ÇÇ} {v‚ÇÅ‚ÇÉ : X‚ÇÅ‚ÇÉ ‚ü∂ X‚ÇÇ‚ÇÉ}
  (s : is_pushout h‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) (e : h‚ÇÅ‚ÇÇ ‚â´ v‚ÇÅ‚ÇÉ = v‚ÇÅ‚ÇÇ ‚â´ h‚ÇÇ‚ÇÇ) :
  is_pushout (h‚ÇÅ‚ÇÅ ‚â´ h‚ÇÅ‚ÇÇ) v‚ÇÅ‚ÇÅ v‚ÇÅ‚ÇÉ (h‚ÇÇ‚ÇÅ ‚â´ h‚ÇÇ‚ÇÇ) ‚Üî is_pushout h‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÇ v‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÇ :=
‚ü®Œª h, h.of_right e s, s.paste_horiz‚ü©

section

variables [has_zero_object C] [has_zero_morphisms C]
open_locale zero_object

lemma of_is_bilimit {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr :=
by convert is_pushout.of_is_coproduct' h.is_colimit has_zero_object.zero_is_initial

@[simp] lemma of_has_biproduct (X Y : C) [has_binary_biproduct X Y] :
  is_pushout (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr :=
of_is_bilimit (binary_biproduct.is_bilimit X Y)

lemma inl_snd' {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pushout b.inl (0 : X ‚ü∂ 0) b.snd (0 : 0 ‚ü∂ Y) :=
by { apply flip, refine of_right _ (by simp) (of_is_bilimit h), simp, }

/--
The square
```
  X --inl--> X ‚äû Y
  |            |
  0           snd
  |            |
  v            v
  0 ---0-----> Y
```
is a pushout square.
-/
lemma inl_snd (X Y : C) [has_binary_biproduct X Y] :
  is_pushout biprod.inl (0 : X ‚ü∂ 0) biprod.snd (0 : 0 ‚ü∂ Y) :=
inl_snd' (binary_biproduct.is_bilimit X Y)

lemma inr_fst' {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pushout b.inr (0 : Y ‚ü∂ 0) b.fst (0 : 0 ‚ü∂ X) :=
by { refine of_bot _ (by simp) (of_is_bilimit h), simp, }

/--
The square
```
  Y --inr--> X ‚äû Y
  |            |
  0           fst
  |            |
  v            v
  0 ---0-----> X
```
is a pushout square.
-/
lemma inr_fst (X Y : C) [has_binary_biproduct X Y] :
  is_pushout biprod.inr (0 : Y ‚ü∂ 0) biprod.fst (0 : 0 ‚ü∂ X) :=
inr_fst' (binary_biproduct.is_bilimit X Y)

lemma of_is_bilimit' {b : binary_bicone X Y} (h : b.is_bilimit) :
  is_pushout b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
by { refine is_pushout.of_right _ (by simp) (is_pushout.inl_snd' h), simp, }

lemma of_has_binary_biproduct (X Y : C) [has_binary_biproduct X Y] :
  is_pushout biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
of_is_bilimit' (binary_biproduct.is_bilimit X Y)

instance has_pushout_biprod_fst_biprod_snd [has_binary_biproduct X Y] :
  has_pushout (biprod.fst : _ ‚ü∂ X) (biprod.snd : _ ‚ü∂ Y) :=
has_colimit.mk ‚ü®_, (of_has_binary_biproduct X Y).is_colimit‚ü©

/-- The pushout of `biprod.fst` and `biprod.snd` is the zero object. -/
def pushout_biprod_fst_biprod_snd [has_binary_biproduct X Y] :
  pushout (biprod.fst : _ ‚ü∂ X) (biprod.snd : _ ‚ü∂ Y) ‚âÖ 0 :=
colimit.iso_colimit_cocone ‚ü®_, (of_has_binary_biproduct X Y).is_colimit‚ü©

end

lemma op (h : is_pushout f g inl inr) : is_pullback inr.op inl.op g.op f.op :=
is_pullback.of_is_limit (is_limit.of_iso_limit
  (limits.pushout_cocone.is_colimit_equiv_is_limit_op h.flip.cocone h.flip.is_colimit)
  h.to_comm_sq.flip.cocone_op)

lemma unop {Z X Y P : C·µí·µñ} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ P} {inr : Y ‚ü∂ P}
  (h : is_pushout f g inl inr) : is_pullback inr.unop inl.unop g.unop f.unop :=
is_pullback.of_is_limit (is_limit.of_iso_limit
  (limits.pushout_cocone.is_colimit_equiv_is_limit_unop h.flip.cocone h.flip.is_colimit)
  h.to_comm_sq.flip.cocone_unop)

lemma of_horiz_is_iso [is_iso f] [is_iso inr] (sq : comm_sq f g inl inr) :
  is_pushout f g inl inr := of_is_colimit' sq
begin
  refine pushout_cocone.is_colimit.mk _ (Œª s, inv inr ‚â´ s.inr) (Œª s, _) (by tidy) (by tidy),
  simp only [‚Üê cancel_epi f, s.condition, sq.w_assoc, is_iso.hom_inv_id_assoc],
end

lemma of_vert_is_iso [is_iso g] [is_iso inl] (sq : comm_sq f g inl inr) :
  is_pushout f g inl inr := (of_horiz_is_iso sq.flip).flip

end is_pushout

section equalizer

variables {X Y Z : C} {f f' : X ‚ü∂ Y} {g g' : Y ‚ü∂ Z}

/-- If `f : X ‚ü∂ Y`, `g g' : Y ‚ü∂ Z` forms a pullback square, then `f` is the equalizer of
`g` and `g'`. -/
noncomputable
def is_pullback.is_limit_fork (H : is_pullback f f g g') :
  is_limit (fork.of_Œπ f H.w) :=
begin
  fapply fork.is_limit.mk,
  { exact Œª s, H.is_limit.lift (pullback_cone.mk s.Œπ s.Œπ s.condition) },
  { exact Œª s, H.is_limit.fac _ walking_cospan.left },
  { intros s m e, apply pullback_cone.is_limit.hom_ext H.is_limit; refine e.trans _;
      symmetry; exact H.is_limit.fac _ _ }
end

/-- If `f f' : X ‚ü∂ Y`, `g : Y ‚ü∂ Z` forms a pushout square, then `g` is the coequalizer of
`f` and `f'`. -/
noncomputable
def is_pushout.is_limit_fork (H : is_pushout f f' g g) :
  is_colimit (cofork.of_œÄ g H.w) :=
begin
  fapply cofork.is_colimit.mk,
  { exact Œª s, H.is_colimit.desc (pushout_cocone.mk s.œÄ s.œÄ s.condition) },
  { exact Œª s, H.is_colimit.fac _ walking_span.left },
  { intros s m e, apply pushout_cocone.is_colimit.hom_ext H.is_colimit; refine e.trans _;
      symmetry; exact H.is_colimit.fac _ _ }
end

end equalizer

namespace bicartesian_sq

variables {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

lemma of_is_pullback_is_pushout (p‚ÇÅ : is_pullback f g h i) (p‚ÇÇ : is_pushout f g h i) :
  bicartesian_sq f g h i :=
bicartesian_sq.mk p‚ÇÅ.to_comm_sq ‚ü®p‚ÇÅ.is_limit‚ü© ‚ü®p‚ÇÇ.is_colimit‚ü©

lemma flip (p : bicartesian_sq f g h i) : bicartesian_sq g f i h :=
of_is_pullback_is_pushout p.to_is_pullback.flip p.to_is_pushout.flip

variables [has_zero_object C] [has_zero_morphisms C]
open_locale zero_object

/--
```
 X ‚äû Y --fst--> X
   |            |
  snd           0
   |            |
   v            v
   Y -----0---> 0
```
is a bicartesian square.
-/
lemma of_is_biproduct‚ÇÅ {b : binary_bicone X Y} (h : b.is_bilimit) :
  bicartesian_sq b.fst b.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
of_is_pullback_is_pushout (is_pullback.of_is_bilimit h) (is_pushout.of_is_bilimit' h)

/--
```
   0 -----0---> X
   |            |
   0           inl
   |            |
   v            v
   Y --inr--> X ‚äû Y
```
is a bicartesian square.
-/
lemma of_is_biproduct‚ÇÇ {b : binary_bicone X Y} (h : b.is_bilimit) :
  bicartesian_sq (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) b.inl b.inr :=
of_is_pullback_is_pushout (is_pullback.of_is_bilimit' h) (is_pushout.of_is_bilimit h)

/--
```
 X ‚äû Y --fst--> X
   |            |
  snd           0
   |            |
   v            v
   Y -----0---> 0
```
is a bicartesian square.
-/
@[simp] lemma of_has_biproduct‚ÇÅ [has_binary_biproduct X Y] :
  bicartesian_sq biprod.fst biprod.snd (0 : X ‚ü∂ 0) (0 : Y ‚ü∂ 0) :=
by convert of_is_biproduct‚ÇÅ (binary_biproduct.is_bilimit X Y)

/--
```
   0 -----0---> X
   |            |
   0           inl
   |            |
   v            v
   Y --inr--> X ‚äû Y
```
is a bicartesian square.
-/
@[simp] lemma of_has_biproduct‚ÇÇ [has_binary_biproduct X Y] :
  bicartesian_sq (0 : 0 ‚ü∂ X) (0 : 0 ‚ü∂ Y) biprod.inl biprod.inr :=
by convert of_is_biproduct‚ÇÇ (binary_biproduct.is_bilimit X Y)

end bicartesian_sq

section functor

variables {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]
variables (F : C ‚•§ D) {W X Y Z : C} {f : W ‚ü∂ X} {g : W ‚ü∂ Y} {h : X ‚ü∂ Z} {i : Y ‚ü∂ Z}

lemma functor.map_is_pullback [preserves_limit (cospan h i) F] (s : is_pullback f g h i) :
  is_pullback (F.map f) (F.map g) (F.map h) (F.map i) :=
-- This is made slightly awkward because `C` and `D` have different universes,
-- and so the relevant `walking_cospan` diagrams live in different universes too!
begin
  refine is_pullback.of_is_limit' (F.map_comm_sq s.to_comm_sq)
    (is_limit.equiv_of_nat_iso_of_iso (cospan_comp_iso F h i) _ _ (walking_cospan.ext _ _ _)
      (is_limit_of_preserves F s.is_limit)),
  { refl, },
  { dsimp, simp, },
  { dsimp, simp, },
end

lemma functor.map_is_pushout [preserves_colimit (span f g) F] (s : is_pushout f g h i) :
  is_pushout (F.map f) (F.map g) (F.map h) (F.map i) :=
begin
  refine is_pushout.of_is_colimit' (F.map_comm_sq s.to_comm_sq)
    (is_colimit.equiv_of_nat_iso_of_iso (span_comp_iso F f g) _ _ (walking_span.ext _ _ _)
      (is_colimit_of_preserves F s.is_colimit)),
  { refl, },
  { dsimp, simp, },
  { dsimp, simp, },
end

alias functor.map_is_pullback ‚Üê is_pullback.map
alias functor.map_is_pushout ‚Üê is_pushout.map

lemma is_pullback.of_map [reflects_limit (cospan h i) F] (e : f ‚â´ h = g ‚â´ i)
  (H : is_pullback (F.map f) (F.map g) (F.map h) (F.map i)) : is_pullback f g h i :=
begin
  refine ‚ü®‚ü®e‚ü©, ‚ü®is_limit_of_reflects F $ _‚ü©‚ü©,
  refine (is_limit.equiv_of_nat_iso_of_iso (cospan_comp_iso F h i) _ _
    (walking_cospan.ext _ _ _)).symm H.is_limit,
  exacts [iso.refl _, (category.comp_id _).trans (category.id_comp _).symm,
    (category.comp_id _).trans (category.id_comp _).symm]
end

lemma is_pullback.of_map_of_faithful [reflects_limit (cospan h i) F] [faithful F]
  (H : is_pullback (F.map f) (F.map g) (F.map h) (F.map i)) : is_pullback f g h i :=
H.of_map F (F.map_injective $ by simpa only [F.map_comp] using H.w)

lemma is_pullback.map_iff {D : Type*} [category D] (F : C ‚•§ D)
  [preserves_limit (cospan h i) F] [reflects_limit (cospan h i) F] (e : f ‚â´ h = g ‚â´ i) :
  is_pullback (F.map f) (F.map g) (F.map h) (F.map i) ‚Üî is_pullback f g h i  :=
‚ü®Œª h, h.of_map F e, Œª h, h.map F‚ü©

lemma is_pushout.of_map [reflects_colimit (span f g) F] (e : f ‚â´ h = g ‚â´ i)
  (H : is_pushout (F.map f) (F.map g) (F.map h) (F.map i)) : is_pushout f g h i :=
begin
  refine ‚ü®‚ü®e‚ü©, ‚ü®is_colimit_of_reflects F $ _‚ü©‚ü©,
  refine (is_colimit.equiv_of_nat_iso_of_iso (span_comp_iso F f g) _ _
    (walking_span.ext _ _ _)).symm H.is_colimit,
  exacts [iso.refl _, (category.comp_id _).trans (category.id_comp _),
    (category.comp_id _).trans (category.id_comp _)]
end

lemma is_pushout.of_map_of_faithful [reflects_colimit (span f g) F] [faithful F]
  (H : is_pushout (F.map f) (F.map g) (F.map h) (F.map i)) : is_pushout f g h i :=
H.of_map F (F.map_injective $ by simpa only [F.map_comp] using H.w)

lemma is_pushout.map_iff {D : Type*} [category D] (F : C ‚•§ D)
  [preserves_colimit (span f g) F] [reflects_colimit (span f g) F] (e : f ‚â´ h = g ‚â´ i) :
  is_pushout (F.map f) (F.map g) (F.map h) (F.map i) ‚Üî is_pushout f g h i  :=
‚ü®Œª h, h.of_map F e, Œª h, h.map F‚ü©

end functor

end category_theory
