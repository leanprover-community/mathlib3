/-
-- Copyright (c) 2020 Bhavik Mehta. All rights reserved.
-- Released under Apache 2.0 license as described in the file LICENSE.
-- Authors: Bhavik Mehta
-/
import category_theory.limits.shapes.finite_products
import category_theory.limits.shapes.binary_products
import category_theory.limits.preserves.shapes.products
import category_theory.limits.preserves.shapes.binary_products
import category_theory.limits.shapes.pullbacks
import category_theory.pempty
import data.equiv.fin

/-!
# Constructing finite products from binary products and terminal.

If a category has binary products and a terminal object then it has finite products.
If a functor preserves binary products and the terminal object then it preserves finite products.

# TODO

Provide the dual results.
Show the analogous results for functors which reflect or create (co)limits.
-/

universes v u u'

noncomputable theory
open category_theory category_theory.category category_theory.limits
namespace category_theory

variables {J : Type v} [small_category J]
variables {C : Type u} [category.{v} C]
variables {D : Type u'} [category.{v} D]

/--
Given `n+1` objects of `C`, a fan for the last `n` with point `câ‚.X` and a binary fan on `câ‚.X` and
`f 0`, we can build a fan for all `n+1`.

In `build_limit` we show that if the two given fans are limits, then this fan is also a limit.
-/
@[simps {rhs_md := semireducible}]
def build_prod {n : â„•} {f : ulift (fin (n+1)) â†’ C}
  (câ‚ : fan (Î» (i : ulift (fin n)), f âŸ¨i.down.succâŸ©))
  (câ‚‚ : binary_fan (f âŸ¨0âŸ©) câ‚.X) :
fan f :=
fan.mk câ‚‚.X
begin
  rintro âŸ¨iâŸ©,
  revert i,
  refine fin.cases _ _,
  { apply câ‚‚.fst },
  { intro i,
    apply câ‚‚.snd â‰« câ‚.Ï€.app (ulift.up i) },
end

/--
Show that if the two given fans in `build_prod` are limits, then the constructed fan is also a
limit.
-/
def build_limit {n : â„•} (f : ulift (fin (n+1)) â†’ C)
  {câ‚ : fan (Î» (i : ulift (fin n)), f âŸ¨i.down.succâŸ©)} {câ‚‚ : binary_fan (f âŸ¨0âŸ©) câ‚.X}
  (tâ‚ : is_limit câ‚) (tâ‚‚ : is_limit câ‚‚) :
  is_limit (build_prod câ‚ câ‚‚) :=
{ lift := Î» s,
  begin
    apply (binary_fan.is_limit.lift' tâ‚‚ (s.Ï€.app âŸ¨0âŸ©) _).1,
    apply tâ‚.lift âŸ¨_, discrete.nat_trans (Î» i, s.Ï€.app âŸ¨i.down.succâŸ©)âŸ©
  end,
  fac' := Î» s,
  begin
    rintro âŸ¨jâŸ©,
    apply fin.induction_on j,
    { apply (binary_fan.is_limit.lift' tâ‚‚ _ _).2.1 },
    { rintro i -,
      dsimp only [build_prod_Ï€_app],
      rw [fin.cases_succ, â† assoc, (binary_fan.is_limit.lift' tâ‚‚ _ _).2.2, tâ‚.fac],
      refl }
  end,
  uniq' := Î» s m w,
  begin
    apply binary_fan.is_limit.hom_ext tâ‚‚,
    { rw (binary_fan.is_limit.lift' tâ‚‚ _ _).2.1,
      apply w âŸ¨0âŸ© },
    { rw (binary_fan.is_limit.lift' tâ‚‚ _ _).2.2,
      apply tâ‚.uniq âŸ¨_, _âŸ©,
      rintro âŸ¨jâŸ©,
      rw assoc,
      dsimp only [discrete.nat_trans_app],
      rw â† w âŸ¨j.succâŸ©,
      dsimp only [build_prod_Ï€_app],
      rw fin.cases_succ }
  end }

section
variables [has_binary_products.{v} C] [has_terminal C]

/--
If `C` has a terminal object and binary products, then it has a product for objects indexed by
`ulift (fin n)`.
-/
def has_product_ulift_fin :
  Î  (n : â„•) (f : ulift (fin n) â†’ C), has_product f
| 0 := Î» f,
  begin
    letI : has_limits_of_shape (discrete (ulift (fin 0))) C :=
      has_limits_of_shape_of_equivalence
        (discrete.equivalence (equiv.ulift.trans fin_zero_equiv').symm),
    apply_instance,
  end
| (n+1) := Î» f,
  begin
    haveI := has_product_ulift_fin n,
    apply has_limit.mk âŸ¨_, build_limit f (limit.is_limit _) (limit.is_limit _)âŸ©,
  end

/--
If `C` has a terminal object and binary products, then it has limits of shape
`discrete (ulift (fin n))` for any `n : â„•`.
-/
def has_limits_of_shape_ulift_fin (n : â„•) :
  has_limits_of_shape (discrete (ulift (fin n))) C :=
{ has_limit := Î» K,
begin
  letI := has_product_ulift_fin n K.obj,
  let : discrete.functor K.obj â‰… K := discrete.nat_iso (Î» i, iso.refl _),
  apply has_limit_of_iso this,
end }

/--
If `C` has a terminal object and binary products, then it
-/
def has_finite_products_of_has_binary_and_terminal : has_finite_products C :=
Î» J ğ’¥â‚ ğ’¥â‚‚,
begin
  resetI,
  refine trunc.rec_on_subsingleton (fintype.equiv_fin J) (Î» e, _),
  apply has_limits_of_shape_of_equivalence (discrete.equivalence (e.trans equiv.ulift.symm)).symm,
  refine has_limits_of_shape_ulift_fin (fintype.card J),
end
end

section preserves
variables (F : C â¥¤ D)
variables [preserves_limits_of_shape (discrete walking_pair) F]
variables [preserves_limits_of_shape (discrete pempty) F]
variables [has_finite_products.{v} C] [has_finite_products.{v} D]

noncomputable def preserves_fin_of_preserves_binary_and_terminal  :
  Î  (n : â„•) (f : ulift (fin n) â†’ C), preserves_limit (discrete.functor f) F
| 0 := Î» f,
  begin
    letI : preserves_limits_of_shape (discrete (ulift (fin 0))) F :=
      preserves_limits_of_shape_of_equiv
        (discrete.equivalence (equiv.ulift.trans fin_zero_equiv').symm) _,
    apply_instance,
  end
| (n+1) :=
  begin
    haveI := preserves_fin_of_preserves_binary_and_terminal n,
    intro f,
    refine preserves_limit_of_preserves_limit_cone
      (build_limit f (limit.is_limit _) (limit.is_limit _)) _,
    apply (is_limit_map_cone_fan_mk_equiv _ _ _).symm _,
    let := build_limit (Î» i, F.obj (f i))
              (is_limit_of_has_product_of_preserves_limit F _)
              (is_limit_of_has_binary_product_of_preserves_limit F _ _),
    refine is_limit.of_iso_limit this _,
    apply cones.ext _ _,
    apply iso.refl _,
    rintro âŸ¨jâŸ©,
    apply fin.induction_on j,
    { apply (category.id_comp _).symm },
    { rintro i -,
      dsimp only [build_prod_Ï€_app, iso.refl_hom, fan.mk_Ï€_app],
      rw [fin.cases_succ, fin.cases_succ],
      change F.map _ â‰« _ = ğŸ™ _ â‰« _,
      rw [id_comp, â†F.map_comp],
      refl }
  end

def preserves_ulift_fin_of_preserves_binary_and_terminal (n : â„•) :
  preserves_limits_of_shape (discrete (ulift (fin n))) F :=
{ preserves_limit := Î» K,
  begin
    let : discrete.functor K.obj â‰… K := discrete.nat_iso (Î» i, iso.refl _),
    haveI := preserves_fin_of_preserves_binary_and_terminal F n K.obj,
    apply preserves_limit_of_iso_diagram F this,
  end }

def preserves_finite_products_of_preserves_binary_and_terminal
  (J : Type v) [fintype J] :
  preserves_limits_of_shape.{v} (discrete J) F :=
begin
  classical,
  refine trunc.rec_on_subsingleton (fintype.equiv_fin J) _,
  intro e,
  haveI := preserves_ulift_fin_of_preserves_binary_and_terminal F (fintype.card J),
  apply preserves_limits_of_shape_of_equiv (discrete.equivalence (e.trans equiv.ulift.symm)).symm,
end

end preserves
end category_theory
