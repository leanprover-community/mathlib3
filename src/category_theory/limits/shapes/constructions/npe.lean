/-
-- Copyright (c) 2020 Bhavik Mehta. All rights reserved.
-- Released under Apache 2.0 license as described in the file LICENSE.
-- Authors: Bhavik Mehta
-/
import category_theory.limits.shapes.equalizers
import category_theory.limits.shapes.finite_products
import category_theory.limits.preserves.shapes

/-!
# Constructing limits from products and equalizers.

If a category has all products, and all equalizers, then it has all limits.
Similarly, if it has all finite products, and all equalizers, then it has all finite limits.

If a functor preserves all products and equalizers, then it preserves all limits.
Similarly, if it preserves all finite products and equalizers, then it preserves all finite limits.

TODO: provide the dual results.
-/

open category_theory
open opposite

namespace category_theory.limits

universes v u uâ‚‚
variables {C : Type u} [category.{v} C]

variables {J : Type v} [small_category J]

-- We hide the "implementation details" inside a namespace
namespace has_limit_of_has_products_of_has_equalizers

variables {F : J â¥¤ C}
          {câ‚ : fan F.obj}
          {câ‚‚ : fan (Î» f : (Î£ p : J Ã— J, p.1 âŸ¶ p.2), F.obj f.1.2)}
          (s t : câ‚.X âŸ¶ câ‚‚.X)
          (hs : âˆ€ (f : Î£ p : J Ã— J, p.1 âŸ¶ p.2), s â‰« câ‚‚.Ï€.app f = câ‚.Ï€.app f.1.1 â‰« F.map f.2)
          (ht : âˆ€ (f : Î£ p : J Ã— J, p.1 âŸ¶ p.2), t â‰« câ‚‚.Ï€.app f = câ‚.Ï€.app f.1.2)
          (i : fork s t)

include hs ht
/--
(Implementation) Given the appropriate product and equalizer cones, build the cone for `F` which is
limiting if the given cones are also.
-/
@[simps]
def build_limit : cone F :=
{ X := i.X,
  Ï€ := { app := Î» j, i.Î¹ â‰« câ‚.Ï€.app _,
         naturality' := Î» jâ‚ jâ‚‚ f, by { dsimp, simp [â† hs âŸ¨âŸ¨_, _âŸ©, fâŸ©, i.condition_assoc, ht] } } }

variable {i}
/--
(Implementation) Show the cone constructed in `build_limit` is limiting, provided the cones used in
its construction are.
-/
def built_is_limit (tâ‚ : is_limit câ‚) (tâ‚‚ : is_limit câ‚‚) (hi : is_limit i) :
  is_limit (build_limit s t hs ht i) :=
{ lift := Î» q,
  begin
    refine hi.lift (fork.of_Î¹ _ _),
    refine tâ‚.lift (limits.fan.mk (Î» j, _)),
    apply q.Ï€.app j,
    apply tâ‚‚.hom_ext,
    simp [hs, ht],
  end,
  uniq' := Î» q m w, hi.hom_ext (i.equalizer_ext (tâ‚.hom_ext (by simpa using w))) }

end has_limit_of_has_products_of_has_equalizers

open has_limit_of_has_products_of_has_equalizers

/--
Given the existence of the appropriate (possibly finite) products and equalizers, we know a limit of
`F` exists.
(This assumes the existence of all equalizers, which is technically stronger than needed.)
-/
lemma has_limit_of_equalizer_and_product (F : J â¥¤ C)
  [has_limit (discrete.functor F.obj)]
  [has_limit (discrete.functor (Î» f : (Î£ p : J Ã— J, p.1 âŸ¶ p.2), F.obj f.1.2))]
  [has_equalizers C] : has_limit F :=
has_limit.mk
{ cone := _,
  is_limit :=
    built_is_limit
      (pi.lift (Î» f, limit.Ï€ _ _ â‰« F.map f.2))
      (pi.lift (Î» f, limit.Ï€ _ f.1.2))
      (by simp)
      (by simp)
      (limit.is_limit _)
      (limit.is_limit _)
      (limit.is_limit _) }

/--
Any category with products and equalizers has all limits.

See https://stacks.math.columbia.edu/tag/002N.
-/
lemma limits_from_equalizers_and_products
  [has_products C] [has_equalizers C] : has_limits C :=
{ has_limits_of_shape := Î» J ğ’¥,
  { has_limit := Î» F, by exactI has_limit_of_equalizer_and_product F } }

/--
Any category with finite products and equalizers has all finite limits.

See https://stacks.math.columbia.edu/tag/002O.
(We do not prove equivalence with the third condition.)
-/
lemma finite_limits_from_equalizers_and_finite_products
  [has_finite_products C] [has_equalizers C] : has_finite_limits C :=
Î» J _ _, { has_limit := Î» F, by exactI has_limit_of_equalizer_and_product F }

variables {D : Type uâ‚‚} [category.{v} D]
noncomputable theory

section

variables [has_limits_of_shape (discrete J) C]
          [has_limits_of_shape (discrete (Î£ p : J Ã— J, p.1 âŸ¶ p.2)) C]
          [has_equalizers C]
variables (G : C â¥¤ D)
          [preserves_limits_of_shape walking_parallel_pair G]
          [preserves_limits_of_shape (discrete J) G]
          [preserves_limits_of_shape (discrete (Î£ p : J Ã— J, p.1 âŸ¶ p.2)) G]

/-- If a functor preserves equalizers and the appropriate products, it preserves limits. -/
def preserves_limit_of_preserves_equalizers_and_product :
  preserves_limits_of_shape J G :=
{ preserves_limit := Î» K,
  begin
    let P := âˆ K.obj,
    let Q := âˆ (Î» (f : (Î£ (p : J Ã— J), p.fst âŸ¶ p.snd)), K.obj f.1.2),
    let s : P âŸ¶ Q := pi.lift (Î» f, limit.Ï€ _ _ â‰« K.map f.2),
    let t : P âŸ¶ Q := pi.lift (Î» f, limit.Ï€ _ f.1.2),
    let I := equalizer s t,
    let i : I âŸ¶ P := equalizer.Î¹ s t,
    apply preserves_limit_of_preserves_limit_cone
      (built_is_limit s t (by simp) (by simp) (limit.is_limit _) (limit.is_limit _) (limit.is_limit _)),
    refine is_limit.of_iso_limit (built_is_limit _ _ _ _ _ _ _) _,
    { exact fan.mk (Î» j, G.map (pi.Ï€ _ j)) },
    { refine @fan.mk _ D _ (Î» f, _) (G.obj Q) (Î» f, _),
      exact G.map (pi.Ï€ _ f) },
    { apply G.map s },
    { apply G.map t },
    { intro f,
      dsimp, simp only [â†G.map_comp, limit.lift_Ï€, fan.mk_Ï€_app] },
    { intro f,
      dsimp, simp only [â†G.map_comp, limit.lift_Ï€, fan.mk_Ï€_app] },
    { apply fork.of_Î¹ (G.map i) _,
      simp only [â† G.map_comp, equalizer.condition] },
    { apply preserves_the_product },
    { apply preserves_the_product },
    { apply map_is_limit_of_preserves_of_is_limit, apply equalizer_is_equalizer },
    refine cones.ext (iso.refl _) _,
    intro j,
    dsimp,
    simp,
  end }
end

/-- If G preserves equalizers and finite products, it preserves finite limits. -/
def preserves_finite_limits_of_preserves_equalizers_and_finite_products
  [has_equalizers C] [has_finite_products C]
  (G : C â¥¤ D) [preserves_limits_of_shape walking_parallel_pair G]
  [âˆ€ J [fintype J], preserves_limits_of_shape (discrete J) G]
  (J : Type v) [small_category J] [fin_category J] :
preserves_limits_of_shape J G :=
preserves_limit_of_preserves_equalizers_and_product G

/-- If G preserves equalizers and products, it preserves all limits. -/
def preserves_limits_of_preserves_equalizers_and_products
  [has_equalizers C] [has_products C]
  (G : C â¥¤ D) [preserves_limits_of_shape walking_parallel_pair G]
  [âˆ€ J, preserves_limits_of_shape (discrete J) G]
  (J : Type v) [small_category J] :
preserves_limits G :=
{ preserves_limits_of_shape := Î» J ğ’¥,
  by exactI preserves_limit_of_preserves_equalizers_and_product G }

end category_theory.limits
