/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import category_theory.limits.shapes.pullbacks
import category_theory.limits.shapes.kernel_pair

/-!
# The diagonal object of a morphism.

We provide various API and isomorphisms considering the diagonal object `Î”_{Y/X} := pullback f f`
of a morphism `f : X âŸ¶ Y`.

-/

open category_theory

noncomputable theory

namespace category_theory.limits

variables {C : Type*} [category C] {X Y Z : C}

namespace pullback

section diagonal

variables (f : X âŸ¶ Y) [has_pullback f f]

/-- The diagonal object of a morphism `f : X âŸ¶ Y` is `Î”_{X/Y} := pullback f f`. -/
abbreviation diagonal_obj : C := pullback f f

/-- The diagonal morphism `X âŸ¶ Î”_{X/Y}` for a morphism `f : X âŸ¶ Y`. -/
def diagonal : X âŸ¶ diagonal_obj f :=
pullback.lift (ğŸ™ _) (ğŸ™ _) rfl

@[simp, reassoc] lemma diagonal_fst : diagonal f â‰« pullback.fst = ğŸ™ _ :=
pullback.lift_fst _ _ _

@[simp, reassoc] lemma diagonal_snd : diagonal f â‰« pullback.snd = ğŸ™ _ :=
pullback.lift_snd _ _ _

instance : split_mono (diagonal f) :=
âŸ¨pullback.fst, diagonal_fst fâŸ©

instance : split_epi (pullback.fst : pullback f f âŸ¶ X) :=
âŸ¨diagonal f, diagonal_fst fâŸ©

instance : split_epi (pullback.snd : pullback f f âŸ¶ X) :=
âŸ¨diagonal f, diagonal_snd fâŸ©

instance [mono f] : is_iso (diagonal f) :=
begin
  rw (is_iso.inv_eq_of_inv_hom_id (diagonal_fst f)).symm,
  apply_instance
end

/-- The two projections `Î”_{X/Y} âŸ¶ X` forms a kernel pair for `f : X âŸ¶ Y`. -/
def diagonal_is_kernel_pair :
  is_kernel_pair f (pullback.fst : diagonal_obj f âŸ¶ _) pullback.snd :=
âŸ¨pullback.condition, pullback_is_pullback _ _âŸ©

end diagonal

end pullback

variable [has_pullbacks C]

open pullback

section

variables {U Vâ‚ Vâ‚‚ : C} (f : X âŸ¶ Y) (i : U âŸ¶ Y)
variables (iâ‚ : Vâ‚ âŸ¶ pullback f i) (iâ‚‚ : Vâ‚‚ âŸ¶ pullback f i)

@[simp, reassoc]
lemma pullback_diagonal_map_snd_fst_fst :
  (pullback.snd : pullback (diagonal f) (map (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) f f (iâ‚ â‰« fst) (iâ‚‚ â‰« fst) i
    (by simp [condition]) (by simp [condition])) âŸ¶ _) â‰« fst â‰« iâ‚ â‰« fst = pullback.fst :=
begin
  conv_rhs { rw â† category.comp_id pullback.fst },
  rw [â† diagonal_fst f, pullback.condition_assoc, pullback.lift_fst]
end

@[simp, reassoc]
lemma pullback_diagonal_map_snd_snd_fst :
  (pullback.snd : pullback (diagonal f) (map (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) f f (iâ‚ â‰« fst) (iâ‚‚ â‰« fst) i
    (by simp [condition]) (by simp [condition])) âŸ¶ _) â‰« snd â‰« iâ‚‚ â‰« fst = pullback.fst :=
begin
  conv_rhs { rw â† category.comp_id pullback.fst },
  rw [â† diagonal_snd f, pullback.condition_assoc, pullback.lift_snd]
end

variable [has_pullback iâ‚ iâ‚‚]

/--
This iso witnesses the fact that
given `f : X âŸ¶ Y`, `i : U âŸ¶ Y`, and `iâ‚ : Vâ‚ âŸ¶ X Ã—[Y] U`, `iâ‚‚ : Vâ‚‚ âŸ¶ X Ã—[Y] U`, the diagram

Vâ‚ Ã—[X Ã—[Y] U] Vâ‚‚ âŸ¶ Vâ‚ Ã—[U] Vâ‚‚
        |                 |
        |                 |
        â†“                 â†“
        X         âŸ¶  X Ã—[Y] X

is a pullback square.
-/
def pullback_diagonal_map_iso :
  pullback (diagonal f) (map (iâ‚ â‰« snd) (iâ‚‚ â‰« snd) f f (iâ‚ â‰« fst) (iâ‚‚ â‰« fst) i
    (by simp [condition]) (by simp [condition])) â‰… pullback iâ‚ iâ‚‚ :=
{ hom := pullback.lift (pullback.snd â‰« pullback.fst) (pullback.snd â‰« pullback.snd)
    begin
      ext; simp only [category.assoc, pullback.condition, pullback_diagonal_map_snd_fst_fst,
        pullback_diagonal_map_snd_snd_fst],
    end,
  inv := pullback.lift (pullback.fst â‰« iâ‚ â‰« pullback.fst) (pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _)
      pullback.snd (category.id_comp _).symm (category.id_comp _).symm)
    begin
      ext; simp only [diagonal_fst, diagonal_snd, category.comp_id, pullback.condition_assoc,
        category.assoc, lift_fst, lift_fst_assoc, lift_snd, lift_snd_assoc],
    end,
  hom_inv_id' := by ext; simp only [category.id_comp, category.assoc, lift_fst_assoc,
    pullback_diagonal_map_snd_fst_fst, lift_fst, lift_snd, category.comp_id],
  inv_hom_id' := by ext; simp }

@[simp, reassoc]
lemma pullback_diagonal_map_iso_hom_fst :
  (pullback_diagonal_map_iso f i iâ‚ iâ‚‚).hom â‰« pullback.fst = pullback.snd â‰« pullback.fst :=
by { delta pullback_diagonal_map_iso, simp }

@[simp, reassoc]
lemma pullback_diagonal_map_iso_hom_snd :
  (pullback_diagonal_map_iso f i iâ‚ iâ‚‚).hom â‰« pullback.snd = pullback.snd â‰« pullback.snd :=
by { delta pullback_diagonal_map_iso, simp }

@[simp, reassoc]
lemma pullback_diagonal_map_iso_inv_fst :
  (pullback_diagonal_map_iso f i iâ‚ iâ‚‚).inv â‰« pullback.fst = pullback.fst â‰« iâ‚ â‰« pullback.fst :=
by { delta pullback_diagonal_map_iso, simp }

@[simp, reassoc]
lemma pullback_diagonal_map_iso_inv_snd_fst :
  (pullback_diagonal_map_iso f i iâ‚ iâ‚‚).inv â‰« pullback.snd â‰« pullback.fst = pullback.fst :=
by { delta pullback_diagonal_map_iso, simp }

@[simp, reassoc]
lemma pullback_diagonal_map_iso_inv_snd_snd :
  (pullback_diagonal_map_iso f i iâ‚ iâ‚‚).inv â‰« pullback.snd â‰« pullback.snd = pullback.snd :=
by { delta pullback_diagonal_map_iso, simp }

end

section

variables {S T : C} (f : X âŸ¶ T) (g : Y âŸ¶ T) (i : T âŸ¶ S)
variables [has_pullback i i] [has_pullback f g] [has_pullback (f â‰« i) (g â‰« i)]
variable [has_pullback (diagonal i) (pullback.map (f â‰« i) (g â‰« i) i i f g (ğŸ™ _)
    (category.comp_id _) (category.comp_id _))]

/--
This iso witnesses the fact that
given `f : X âŸ¶ T`, `g : Y âŸ¶ T`, and `i : T âŸ¶ S`, the diagram

X Ã—â‚œ Y âŸ¶ X Ã—â‚› Y
   |         |
   |         |
   â†“         â†“
   T   âŸ¶ T Ã—â‚› T

is a pullback square.
-/
def pullback_diagonal_map_id_iso :
  pullback (diagonal i) (pullback.map (f â‰« i) (g â‰« i) i i f g (ğŸ™ _)
    (category.comp_id _) (category.comp_id _)) â‰… pullback f g :=
begin
  refine (as_iso $ pullback.map _ _ _ _ (ğŸ™ _) (pullback.congr_hom _ _).hom (ğŸ™ _) _ _) â‰ªâ‰«
    pullback_diagonal_map_iso i (ğŸ™ _) (f â‰« inv pullback.fst) (g â‰« inv pullback.fst) â‰ªâ‰«
      (as_iso $ pullback.map _ _ _ _ (ğŸ™ _) (ğŸ™ _) pullback.fst _ _),
  { rw [â† category.comp_id pullback.snd, â† condition, category.assoc, is_iso.inv_hom_id_assoc] },
  { rw [â† category.comp_id pullback.snd, â† condition, category.assoc, is_iso.inv_hom_id_assoc] },
  { rw [category.comp_id, category.id_comp] },
  { ext; simp },
  { apply_instance },
  { rw [category.assoc, category.id_comp, is_iso.inv_hom_id, category.comp_id] },
  { rw [category.assoc, category.id_comp, is_iso.inv_hom_id, category.comp_id] },
  { apply_instance },
end

@[simp, reassoc]
lemma pullback_diagonal_map_id_iso_hom_fst :
  (pullback_diagonal_map_id_iso f g i).hom â‰« pullback.fst = pullback.snd â‰« pullback.fst :=
by { delta pullback_diagonal_map_id_iso, simp }

@[simp, reassoc]
lemma pullback_diagonal_map_id_iso_hom_snd :
  (pullback_diagonal_map_id_iso f g i).hom â‰« pullback.snd = pullback.snd â‰« pullback.snd :=
by { delta pullback_diagonal_map_id_iso, simp }

@[simp, reassoc]
lemma pullback_diagonal_map_id_iso_inv_fst :
  (pullback_diagonal_map_id_iso f g i).inv â‰« pullback.fst = pullback.fst â‰« f :=
begin
  rw [iso.inv_comp_eq, â† category.comp_id pullback.fst, â† diagonal_fst i, pullback.condition_assoc],
  simp,
end

@[simp, reassoc]
lemma pullback_diagonal_map_id_iso_inv_snd_fst :
  (pullback_diagonal_map_id_iso f g i).inv â‰« pullback.snd â‰« pullback.fst = pullback.fst :=
by { rw iso.inv_comp_eq, simp }

@[simp, reassoc]
lemma pullback_diagonal_map_id_iso_inv_snd_snd :
  (pullback_diagonal_map_id_iso f g i).inv â‰« pullback.snd â‰« pullback.snd = pullback.snd :=
by { rw iso.inv_comp_eq, simp }

lemma pullback.diagonal_comp (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [has_pullback f f] [has_pullback g g]
  [has_pullback (f â‰« g) (f â‰« g)] :
  diagonal (f â‰« g) = diagonal f â‰« (pullback_diagonal_map_id_iso f f g).inv â‰« pullback.snd :=
by ext; simp

/-- The diagonal object of `X Ã—[Z] Y âŸ¶ X` is isomorphic to `Î”_{Y/Z} Ã—[Z] X`. -/
def diagonal_obj_pullback_fst_iso {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  diagonal_obj (pullback.fst : pullback f g âŸ¶ X) â‰…
    pullback (pullback.snd â‰« g : diagonal_obj g âŸ¶ Z) f :=
pullback_right_pullback_fst_iso _ _ _ â‰ªâ‰« pullback.congr_hom pullback.condition rfl â‰ªâ‰«
  pullback_assoc _ _ _ _ â‰ªâ‰« pullback_symmetry _ _ â‰ªâ‰« pullback.congr_hom pullback.condition rfl

@[simp, reassoc] lemma diagonal_obj_pullback_fst_iso_hom_fst_fst {X Y Z : C} (f : X âŸ¶ Z)
  (g : Y âŸ¶ Z) :
  (diagonal_obj_pullback_fst_iso f g).hom â‰« pullback.fst â‰« pullback.fst =
    pullback.fst â‰« pullback.snd :=
by { delta diagonal_obj_pullback_fst_iso, simp }

@[simp, reassoc] lemma diagonal_obj_pullback_fst_iso_hom_fst_snd {X Y Z : C} (f : X âŸ¶ Z)
  (g : Y âŸ¶ Z) :
  (diagonal_obj_pullback_fst_iso f g).hom â‰« pullback.fst â‰« pullback.snd =
    pullback.snd â‰« pullback.snd :=
by { delta diagonal_obj_pullback_fst_iso, simp }

@[simp, reassoc] lemma diagonal_obj_pullback_fst_iso_hom_snd {X Y Z : C} (f : X âŸ¶ Z)
  (g : Y âŸ¶ Z) :
  (diagonal_obj_pullback_fst_iso f g).hom â‰« pullback.snd = pullback.fst â‰« pullback.fst :=
by { delta diagonal_obj_pullback_fst_iso, simp }

@[simp, reassoc] lemma diagonal_obj_pullback_fst_iso_inv_fst_fst {X Y Z : C} (f : X âŸ¶ Z)
  (g : Y âŸ¶ Z) :
  (diagonal_obj_pullback_fst_iso f g).inv â‰« pullback.fst â‰« pullback.fst =
    pullback.snd :=
by { delta diagonal_obj_pullback_fst_iso, simp }

@[simp, reassoc] lemma diagonal_obj_pullback_fst_iso_inv_fst_snd {X Y Z : C} (f : X âŸ¶ Z)
  (g : Y âŸ¶ Z) :
  (diagonal_obj_pullback_fst_iso f g).inv â‰« pullback.fst â‰« pullback.snd =
    pullback.fst â‰« pullback.fst :=
by { delta diagonal_obj_pullback_fst_iso, simp }

@[simp, reassoc] lemma diagonal_obj_pullback_fst_iso_inv_snd_fst {X Y Z : C} (f : X âŸ¶ Z)
  (g : Y âŸ¶ Z) :
  (diagonal_obj_pullback_fst_iso f g).inv â‰« pullback.snd â‰« pullback.fst = pullback.snd :=
by { delta diagonal_obj_pullback_fst_iso, simp }

@[simp, reassoc] lemma diagonal_obj_pullback_fst_iso_inv_snd_snd {X Y Z : C} (f : X âŸ¶ Z)
  (g : Y âŸ¶ Z) :
  (diagonal_obj_pullback_fst_iso f g).inv â‰« pullback.snd â‰« pullback.snd =
    pullback.fst â‰« pullback.snd :=
by { delta diagonal_obj_pullback_fst_iso, simp }

lemma diagonal_pullback_fst {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  diagonal (pullback.fst : pullback f g âŸ¶ _) =
    (pullback_symmetry _ _).hom â‰« ((base_change f).map
      (over.hom_mk (diagonal g) (by simp) : over.mk g âŸ¶ over.mk (pullback.snd â‰« g))).left â‰«
    (diagonal_obj_pullback_fst_iso f g).inv :=
by ext; simp

end

/--
Given the following diagram with `S âŸ¶ S'` a monomorphism,

    X  âŸ¶ X'
      â†˜      â†˜
        S  âŸ¶ S'
      â†—      â†—
    Y  âŸ¶ Y'

This iso witnesses the fact that

      X Ã—[S] Y âŸ¶ (X' Ã—[S'] Y') Ã—[X'] X
          |                  |
          |                  |
          â†“                  â†“
(X' Ã—[S'] Y') Ã—[X'] X âŸ¶ X' Ã—[S'] Y'

is a pullback square. The diagonal map of this square is `pullback.map`.

-/
@[simps]
def pullback_fst_fst_iso {X Y S X' Y' S' : C} (f : X âŸ¶ S) (g : Y âŸ¶ S) (f' : X' âŸ¶ S')
  (g' : Y' âŸ¶ S') (iâ‚ : X âŸ¶ X') (iâ‚‚ : Y âŸ¶ Y') (iâ‚ƒ : S âŸ¶ S') (eâ‚ : f â‰« iâ‚ƒ = iâ‚ â‰« f')
  (eâ‚‚ : g â‰« iâ‚ƒ = iâ‚‚ â‰« g') [mono iâ‚ƒ] :
    pullback (pullback.fst : pullback (pullback.fst : pullback f' g' âŸ¶ _) iâ‚ âŸ¶ _)
      (pullback.fst : pullback (pullback.snd : pullback f' g' âŸ¶ _) iâ‚‚ âŸ¶ _) â‰… pullback f g :=
{ hom := pullback.lift (pullback.fst â‰« pullback.snd) (pullback.snd â‰« pullback.snd)
    begin
      rw [â† cancel_mono iâ‚ƒ, category.assoc, category.assoc, category.assoc, category.assoc, eâ‚, eâ‚‚,
        â† pullback.condition_assoc, pullback.condition_assoc, pullback.condition,
        pullback.condition_assoc]
    end,
  inv := pullback.lift
    (pullback.lift (pullback.map _ _ _ _ _ _ _ eâ‚ eâ‚‚) pullback.fst (pullback.lift_fst _ _ _))
    (pullback.lift (pullback.map _ _ _ _ _ _ _ eâ‚ eâ‚‚) pullback.snd (pullback.lift_snd _ _ _))
    begin
      rw [pullback.lift_fst, pullback.lift_fst]
    end,
  hom_inv_id' := by ext; simp only [category.assoc, category.id_comp, lift_fst, lift_snd,
    lift_fst_assoc, lift_snd_assoc, condition, â† condition_assoc],
  inv_hom_id' := by ext; simp only [category.assoc, category.id_comp, lift_fst, lift_snd,
    lift_fst_assoc, lift_snd_assoc], }

lemma pullback_map_eq_pullback_fst_fst_iso_inv {X Y S X' Y' S' : C} (f : X âŸ¶ S) (g : Y âŸ¶ S)
  (f' : X' âŸ¶ S')
  (g' : Y' âŸ¶ S') (iâ‚ : X âŸ¶ X') (iâ‚‚ : Y âŸ¶ Y') (iâ‚ƒ : S âŸ¶ S') (eâ‚ : f â‰« iâ‚ƒ = iâ‚ â‰« f')
  (eâ‚‚ : g â‰« iâ‚ƒ = iâ‚‚ â‰« g') [mono iâ‚ƒ] :
  pullback.map f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚ =
    (pullback_fst_fst_iso f g f' g' iâ‚ iâ‚‚ iâ‚ƒ eâ‚ eâ‚‚).inv â‰« pullback.snd â‰« pullback.fst :=
begin
  ext; simp only [category.assoc, category.id_comp, lift_fst, lift_snd, lift_fst_assoc,
    lift_snd_assoc, pullback_fst_fst_iso_inv, â† pullback.condition, â† pullback.condition_assoc],
end

end category_theory.limits
