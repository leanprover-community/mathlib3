/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel
-/
import data.fintype.basic
import category_theory.epi_mono
import category_theory.limits.limits
import category_theory.limits.shapes.finite_limits

/-!
# Equalizers and coequalizers

This file defines (co)equalizers as special cases of (co)limits.

An equalizer is the categorical generalization of the subobject {a âˆˆ A | f(a) = g(a)} known
from abelian groups or modules. It is a limit cone over the diagram formed by `f` and `g`.

A coequalizer is the dual concept.

## Main definitions

* `walking_parallel_pair` is the indexing category used for (co)equalizer_diagrams
* `parallel_pair` is a functor from `walking_parallel_pair` to our category `C`.
* a `fork` is a cone over a parallel pair.
  * there is really only one interesting morphism in a fork: the arrow from the vertex of the fork
    to the domain of f and g. It is called `fork.Î¹`.
* an `equalizer` is now just a `limit (parallel_pair f g)`

Each of these has a dual.

## Main statements

* `equalizer.Î¹_mono` states that every equalizer map is a monomorphism
* `is_limit_cone_parallel_pair_self` states that the identity on the domain of `f` is an equalizer
  of `f` and `f`.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 1*][borceux-vol1]
-/

open category_theory

namespace category_theory.limits

local attribute [tidy] tactic.case_bash

universes v u

/-- The type of objects for the diagram indexing a (co)equalizer. -/
@[derive decidable_eq] inductive walking_parallel_pair : Type v
| zero | one

instance fintype_walking_parallel_pair : fintype walking_parallel_pair :=
{ elems := [walking_parallel_pair.zero, walking_parallel_pair.one].to_finset,
  complete := Î» x, by { cases x; simp } }

open walking_parallel_pair

/-- The type family of morphisms for the diagram indexing a (co)equalizer. -/
@[derive _root_.decidable_eq] inductive walking_parallel_pair_hom :
  walking_parallel_pair â†’ walking_parallel_pair â†’ Type v
| left : walking_parallel_pair_hom zero one
| right : walking_parallel_pair_hom zero one
| id : Î  X : walking_parallel_pair.{v}, walking_parallel_pair_hom X X

open walking_parallel_pair_hom

instance (j j' : walking_parallel_pair) : fintype (walking_parallel_pair_hom j j') :=
{ elems := walking_parallel_pair.rec_on j
    (walking_parallel_pair.rec_on j' [walking_parallel_pair_hom.id zero].to_finset
      [left, right].to_finset)
    (walking_parallel_pair.rec_on j' âˆ… [walking_parallel_pair_hom.id one].to_finset),
  complete := by tidy }

def walking_parallel_pair_hom.comp :
  Î  (X Y Z : walking_parallel_pair)
    (f : walking_parallel_pair_hom X Y) (g : walking_parallel_pair_hom Y Z),
    walking_parallel_pair_hom X Z
  | _ _ _ (id _) h := h
  | _ _ _ left   (id one) := left
  | _ _ _ right  (id one) := right
.

instance walking_parallel_pair_hom_category : small_category.{v} walking_parallel_pair :=
{ hom  := walking_parallel_pair_hom,
  id   := walking_parallel_pair_hom.id,
  comp := walking_parallel_pair_hom.comp }

instance : fin_category.{v} walking_parallel_pair.{v} := { }

@[simp]
lemma walking_parallel_pair_hom_id (X : walking_parallel_pair.{v}) :
  walking_parallel_pair_hom.id X = ğŸ™ X :=
rfl

variables {C : Type u} [ğ’ : category.{v} C]
include ğ’
variables {X Y : C}

def parallel_pair (f g : X âŸ¶ Y) : walking_parallel_pair.{v} â¥¤ C :=
{ obj := Î» x, match x with
  | zero := X
  | one := Y
  end,
  map := Î» x y h, match x, y, h with
  | _, _, (id _) := ğŸ™ _
  | _, _, left := f
  | _, _, right := g
  end,
  -- `tidy` can cope with this, but it's too slow:
  map_comp' := begin rintros (âŸ¨âŸ©|âŸ¨âŸ©) (âŸ¨âŸ©|âŸ¨âŸ©) (âŸ¨âŸ©|âŸ¨âŸ©) âŸ¨âŸ©âŸ¨âŸ©; { unfold_aux, simp; refl }, end, }.

@[simp] lemma parallel_pair_obj_zero (f g : X âŸ¶ Y) : (parallel_pair f g).obj zero = X := rfl
@[simp] lemma parallel_pair_obj_one (f g : X âŸ¶ Y) : (parallel_pair f g).obj one = Y := rfl

@[simp] lemma parallel_pair_map_left (f g : X âŸ¶ Y) : (parallel_pair f g).map left = f := rfl
@[simp] lemma parallel_pair_map_right (f g : X âŸ¶ Y) : (parallel_pair f g).map right = g := rfl

@[simp] lemma parallel_pair_functor_obj
  {F : walking_parallel_pair.{v} â¥¤ C} (j : walking_parallel_pair.{v}) :
  (parallel_pair (F.map left) (F.map right)).obj j = F.obj j :=
begin
  cases j; refl
end

/-- Every functor indexing a (co)equalizer is naturally isomorphic (actually, equal) to a
    `parallel_pair` -/
def diagram_iso_parallel_pair (F : walking_parallel_pair.{v} â¥¤ C) :
  F â‰… parallel_pair (F.map left) (F.map right) :=
nat_iso.of_components (Î» j, eq_to_iso $ by cases j; tidy) $ by tidy

abbreviation fork (f g : X âŸ¶ Y) := cone (parallel_pair f g)
abbreviation cofork (f g : X âŸ¶ Y) := cocone (parallel_pair f g)

variables {f g : X âŸ¶ Y}

@[simp] lemma cone_parallel_pair_left (s : cone (parallel_pair f g)) :
  (s.Ï€).app zero â‰« f = (s.Ï€).app one :=
by { conv_lhs { congr, skip, rw â†parallel_pair_map_left f g }, rw s.w }

@[simp] lemma cone_parallel_pair_right (s : cone (parallel_pair f g)) :
  (s.Ï€).app zero â‰« g = (s.Ï€).app one :=
by { conv_lhs { congr, skip, rw â†parallel_pair_map_right f g }, rw s.w }

@[simp] lemma cocone_parallel_pair_left (s : cocone (parallel_pair f g)) :
  f â‰« (s.Î¹).app one = (s.Î¹).app zero :=
by { conv_lhs { congr, rw â†parallel_pair_map_left f g }, rw s.w }

@[simp] lemma cocone_parallel_pair_right (s : cocone (parallel_pair f g)) :
  g â‰« (s.Î¹).app one = (s.Î¹).app zero :=
by { conv_lhs { congr, rw â†parallel_pair_map_right f g }, rw s.w }

/-- To check whether two maps are equalized by both maps of a fork, it suffices to check it for the
    first map -/
lemma cone_parallel_pair_ext (s : cone (parallel_pair f g)) {W : C} {k l : W âŸ¶ s.X}
  (h : k â‰« s.Ï€.app zero = l â‰« s.Ï€.app zero) : âˆ€ (j : walking_parallel_pair),
    k â‰« s.Ï€.app j = l â‰« s.Ï€.app j
| zero := h
| one := by { rw [â†cone_parallel_pair_left, â†category.assoc, â†category.assoc], congr, exact h }

/-- To check whether two maps are coequalized by both maps of a cofork, it suffices to check it for
    the second map -/
lemma cocone_parallel_pair_ext (s : cocone (parallel_pair f g)) {W : C} {k l : s.X âŸ¶ W}
  (h : s.Î¹.app one â‰« k = s.Î¹.app one â‰« l) : âˆ€ (j : walking_parallel_pair),
    s.Î¹.app j â‰« k = s.Î¹.app j â‰« l
| zero := by { rw [â†cocone_parallel_pair_right, category.assoc, category.assoc], congr, exact h }
| one := h

def fork.of_Î¹ {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) : fork f g :=
{ X := P,
  Ï€ :=
  { app := Î» X, begin cases X, exact Î¹, exact Î¹ â‰« f, end,
    naturality' := Î» X Y f,
    begin
      cases X; cases Y; cases f; dsimp; simp,
      { dsimp, simp, }, -- TODO If someone could decipher why these aren't done on the previous line, that would be great
      { exact w },
      { dsimp, simp, }, -- TODO idem
    end } }
def cofork.of_Ï€ {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) : cofork f g :=
{ X := P,
  Î¹ :=
  { app := Î» X, begin cases X, exact f â‰« Ï€, exact Ï€, end,
    naturality' := Î» X Y f,
    begin
      cases X; cases Y; cases f; dsimp; simp,
      { dsimp, simp, }, -- TODO idem
      { exact w.symm },
      { dsimp, simp, }, -- TODO idem
    end } }

@[simp] lemma fork.of_Î¹_app_zero {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) :
  (fork.of_Î¹ Î¹ w).Ï€.app zero = Î¹ := rfl
@[simp] lemma fork.of_Î¹_app_one {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) :
  (fork.of_Î¹ Î¹ w).Ï€.app one = Î¹ â‰« f := rfl
@[simp] lemma cofork.of_Ï€_app_zero {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) :
  (cofork.of_Ï€ Ï€ w).Î¹.app zero = f â‰« Ï€ := rfl
@[simp] lemma cofork.of_Ï€_app_one {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) :
  (cofork.of_Ï€ Ï€ w).Î¹.app one = Ï€ := rfl

def fork.Î¹ (t : fork f g) := t.Ï€.app zero
def cofork.Ï€ (t : cofork f g) := t.Î¹.app one
lemma fork.condition (t : fork f g) : (fork.Î¹ t) â‰« f = (fork.Î¹ t) â‰« g :=
begin
  erw [t.w left, â† t.w right], refl
end
lemma cofork.condition (t : cofork f g) : f â‰« (cofork.Ï€ t) = g â‰« (cofork.Ï€ t) :=
begin
  erw [t.w left, â† t.w right], refl
end

/-- This is a slightly more convenient method to verify that a fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def fork.is_limit.mk (t : fork f g)
  (lift : Î  (s : fork f g), s.X âŸ¶ t.X)
  (fac : âˆ€ (s : fork f g), lift s â‰« fork.Î¹ t = fork.Î¹ s)
  (uniq : âˆ€ (s : fork f g) (m : s.X âŸ¶ t.X)
    (w : âˆ€ j : walking_parallel_pair, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) :
  is_limit t :=
{ lift := lift,
  fac' := Î» s j, walking_parallel_pair.cases_on j (fac s) $
    by erw [â†s.w left, â†t.w left, â†category.assoc, fac]; refl,
  uniq' := uniq }

/-- This is a slightly more convenient method to verify that a cofork is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content -/
def cofork.is_colimit.mk (t : cofork f g)
  (desc : Î  (s : cofork f g), t.X âŸ¶ s.X)
  (fac : âˆ€ (s : cofork f g), cofork.Ï€ t â‰« desc s = cofork.Ï€ s)
  (uniq : âˆ€ (s : cofork f g) (m : t.X âŸ¶ s.X)
    (w : âˆ€ j : walking_parallel_pair, t.Î¹.app j â‰« m = s.Î¹.app j), m = desc s) :
  is_colimit t :=
{ desc := desc,
  fac' := Î» s j, walking_parallel_pair.cases_on j
    (by erw [â†s.w left, â†t.w left, category.assoc, fac]; refl) (fac s),
  uniq' := uniq }

section
local attribute [ext] cone

/-- The fork induced by the Î¹ map of some fork `t` is the same as `t` -/
lemma fork.eq_of_Î¹_Î¹ (t : fork f g) : t = fork.of_Î¹ (fork.Î¹ t) (fork.condition t) :=
begin
  have h : t.Ï€ = (fork.of_Î¹ (fork.Î¹ t) (fork.condition t)).Ï€,
  { ext j, cases j,
    { refl },
    { rw â†cone_parallel_pair_left, refl } },
  tidy
end

end

def cone.of_fork
  {F : walking_parallel_pair.{v} â¥¤ C} (t : fork (F.map left) (F.map right)) : cone F :=
{ X := t.X,
  Ï€ :=
  { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy),
    naturality' := Î» j j' g, by { cases j; cases j'; cases g; dsimp; simp } } }

section
local attribute [ext] cocone

/-- The cofork induced by the Ï€ map of some fork `t` is the same as `t` -/
lemma cofork.eq_of_Ï€_Ï€ (t : cofork f g) : t = cofork.of_Ï€ (cofork.Ï€ t) (cofork.condition t) :=
begin
  have h : t.Î¹ = (cofork.of_Ï€ (cofork.Ï€ t) (cofork.condition t)).Î¹,
  { ext j, cases j,
    { rw â†cocone_parallel_pair_left, refl },
    { refl } },
  tidy
end

end

def cocone.of_cofork
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cofork (F.map left) (F.map right)) : cocone F :=
{ X := t.X,
  Î¹ :=
  { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X,
    naturality' := Î» j j' g, by { cases j; cases j'; cases g; dsimp; simp } } }

@[simp] lemma cone.of_fork_Ï€
  {F : walking_parallel_pair.{v} â¥¤ C} (t : fork (F.map left) (F.map right)) (j) :
  (cone.of_fork t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl

@[simp] lemma cocone.of_cofork_Î¹
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cofork (F.map left) (F.map right)) (j) :
  (cocone.of_cofork t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

def fork.of_cone
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cone F) : fork (F.map left) (F.map right) :=
{ X := t.X,
  Ï€ := { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy) } }
def cofork.of_cocone
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cocone F) : cofork (F.map left) (F.map right) :=
{ X := t.X,
  Î¹ := { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X } }

@[simp] lemma fork.of_cone_Ï€ {F : walking_parallel_pair.{v} â¥¤ C} (t : cone F) (j) :
  (fork.of_cone t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl
@[simp] lemma cofork.of_cocone_Î¹ {F : walking_parallel_pair.{v} â¥¤ C} (t : cocone F) (j) :
  (cofork.of_cocone t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

variables (f g)

section
variables [has_limit (parallel_pair f g)]

abbreviation equalizer := limit (parallel_pair f g)

abbreviation equalizer.Î¹ : equalizer f g âŸ¶ X :=
limit.Ï€ (parallel_pair f g) zero

@[simp] lemma equalizer.Î¹.fork :
  fork.Î¹ (limit.cone (parallel_pair f g)) = equalizer.Î¹ f g := rfl

@[simp] lemma equalizer.Î¹.eq_app_zero :
  (limit.cone (parallel_pair f g)).Ï€.app zero = equalizer.Î¹ f g := rfl

@[reassoc] lemma equalizer.condition : equalizer.Î¹ f g â‰« f = equalizer.Î¹ f g â‰« g :=
fork.condition $ limit.cone $ parallel_pair f g

variables {f g}

abbreviation equalizer.lift {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) : W âŸ¶ equalizer f g :=
limit.lift (parallel_pair f g) (fork.of_Î¹ k h)

/-- Two maps into an equalizer are equal if they are are equal when composed with the equalizer
    map. -/
@[ext] lemma equalizer.hom_ext {W : C} {k l : W âŸ¶ equalizer f g}
  (h : k â‰« equalizer.Î¹ f g = l â‰« equalizer.Î¹ f g) : k = l :=
limit.hom_ext $ cone_parallel_pair_ext _ h

/-- An equalizer morphism is a monomorphism -/
instance equalizer.Î¹_mono : mono (equalizer.Î¹ f g) :=
{ right_cancellation := Î» Z h k w, equalizer.hom_ext w }

end

section
variables {f g}
/-- The equalizer morphism in any limit cone is a monomorphism. -/
lemma mono_of_is_limit_parallel_pair {c : cone (parallel_pair f g)} (i : is_limit c) : mono (c.Ï€.app zero) :=
{ right_cancellation := Î» Z h k w, i.hom_ext $ cone_parallel_pair_ext _ w }

end

section
/-- The identity determines a cone on the equalizer diagram of f and f -/
def cone_parallel_pair_self : cone (parallel_pair f f) :=
{ X := X,
  Ï€ :=
  { app := Î» j, match j with | zero := ğŸ™ X | one := f end } }

@[simp] lemma cone_parallel_pair_self_Ï€_app_zero : (cone_parallel_pair_self f).Ï€.app zero = ğŸ™ X :=
rfl

@[simp] lemma cone_parallel_pair_self_X : (cone_parallel_pair_self f).X = X := rfl

/-- The identity on X is an equalizer of (f, f) -/
def is_limit_cone_parallel_pair_self : is_limit (cone_parallel_pair_self f) :=
{ lift := Î» s, s.Ï€.app zero,
  fac' := Î» s j,
  match j with
  | zero := by erw category.comp_id
  | one := by erw cone_parallel_pair_left
  end,
  uniq' := Î» s m w, by { convert w zero, erw category.comp_id } }

/-- Every equalizer of (f, f) is an isomorphism -/
def limit_cone_parallel_pair_self_is_iso (c : cone (parallel_pair f f)) (h : is_limit c) :
  is_iso (c.Ï€.app zero) :=
  let c' := cone_parallel_pair_self f,
    z : c â‰… c' := is_limit.unique_up_to_iso h (is_limit_cone_parallel_pair_self f) in
  is_iso.of_iso (functor.map_iso (cones.forget _) z)

/-- The equalizer of (f, f) is an isomorphism -/
def equalizer.Î¹_of_self [has_limit (parallel_pair f f)] : is_iso (equalizer.Î¹ f f) :=
limit_cone_parallel_pair_self_is_iso _ _ $ limit.is_limit _

/-- Every equalizer of (f, g), where f = g, is an isomorphism -/
def limit_cone_parallel_pair_self_is_iso' (c : cone (parallel_pair f g)) (hâ‚€ : is_limit c)
  (hâ‚ : f = g) : is_iso (c.Ï€.app zero) :=
begin
  rw fork.eq_of_Î¹_Î¹ c at *,
  have hâ‚‚ : is_limit (fork.of_Î¹ (c.Ï€.app zero) rfl : fork f f), by convert hâ‚€,
  exact limit_cone_parallel_pair_self_is_iso f (fork.of_Î¹ (c.Ï€.app zero) rfl) hâ‚‚
end

/-- The equalizer of (f, g), where f = g, is an isomorphism -/
def equalizer.Î¹_of_self' [has_limit (parallel_pair f g)] (h : f = g) : is_iso (equalizer.Î¹ f g) :=
limit_cone_parallel_pair_self_is_iso' _ _ _ (limit.is_limit _) h

/-- An equalizer that is an epimorphism is an isomorphism -/
def epi_limit_cone_parallel_pair_is_iso (c : cone (parallel_pair f g))
  (h : is_limit c) [epi (c.Ï€.app zero)] : is_iso (c.Ï€.app zero) :=
limit_cone_parallel_pair_self_is_iso' f g c h $
  (cancel_epi (c.Ï€.app zero)).1 (fork.condition c)

end

section
variables [has_colimit (parallel_pair f g)]

abbreviation coequalizer := colimit (parallel_pair f g)

abbreviation coequalizer.Ï€ : Y âŸ¶ coequalizer f g :=
colimit.Î¹ (parallel_pair f g) one

@[simp] lemma coequalizer.Ï€.cofork :
  cofork.Ï€ (colimit.cocone (parallel_pair f g)) = coequalizer.Ï€ f g := rfl

@[simp] lemma coequalizer.Ï€.eq_app_one :
  (colimit.cocone (parallel_pair f g)).Î¹.app one = coequalizer.Ï€ f g := rfl

@[reassoc] lemma coequalizer.condition : f â‰« coequalizer.Ï€ f g = g â‰« coequalizer.Ï€ f g :=
cofork.condition $ colimit.cocone $ parallel_pair f g

variables {f g}

abbreviation coequalizer.desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) : coequalizer f g âŸ¶ W :=
colimit.desc (parallel_pair f g) (cofork.of_Ï€ k h)

/-- Two maps from a coequalizer are equal if they are equal when composed with the coequalizer
    map -/
@[ext] lemma coequalizer.hom_ext {W : C} {k l : coequalizer f g âŸ¶ W}
  (h : coequalizer.Ï€ f g â‰« k = coequalizer.Ï€ f g â‰« l) : k = l :=
colimit.hom_ext $ cocone_parallel_pair_ext _ h

/-- A coequalizer morphism is an epimorphism -/
instance coequalizer.Ï€_epi : epi (coequalizer.Ï€ f g) :=
{ left_cancellation := Î» Z h k w, coequalizer.hom_ext w }

end

section
variables {f g}

/-- The coequalizer morphism in any colimit cocone is an epimorphism. -/
lemma epi_of_is_colimit_parallel_pair {c : cocone (parallel_pair f g)} (i : is_colimit c) : epi (c.Î¹.app one) :=
{ left_cancellation := Î» Z h k w, i.hom_ext $ cocone_parallel_pair_ext _ w }

end

section

/-- The identity determines a cocone on the coequalizer diagram of f and f -/
def cocone_parallel_pair_self : cocone (parallel_pair f f) :=
{ X := Y,
  Î¹ :=
  { app := Î» j, match j with | zero := f | one := ğŸ™ Y end,
    naturality' := Î» j j' g,
    begin
      cases g,
      { refl },
      { erw category.comp_id f },
      { dsimp, simp }
    end } }

@[simp] lemma cocone_parallel_pair_self_Î¹_app_one : (cocone_parallel_pair_self f).Î¹.app one = ğŸ™ Y :=
rfl

@[simp] lemma cocone_parallel_pair_self_X : (cocone_parallel_pair_self f).X  = Y := rfl

/-- The identity on Y is a colimit of (f, f) -/
def is_colimit_cocone_parallel_pair_self : is_colimit (cocone_parallel_pair_self f) :=
{ desc := Î» s, s.Î¹.app one,
  fac' := Î» s j,
  match j with
  | zero := by erw cocone_parallel_pair_right
  | one := by erw category.id_comp
  end,
  uniq' := Î» s m w, by { convert w one, erw category.id_comp } }

/-- Every coequalizer of (f, f) is an isomorphism -/
def colimit_cocone_parallel_pair_self_is_iso (c : cocone (parallel_pair f f)) (h : is_colimit c) :
  is_iso (c.Î¹.app one) :=
  let c' := cocone_parallel_pair_self f,
    z : c' â‰… c := is_colimit.unique_up_to_iso (is_colimit_cocone_parallel_pair_self f) h in
  is_iso.of_iso $ functor.map_iso (cocones.forget _) z

/-- The coequalizer of (f, f) is an isomorphism -/
def coequalizer.Ï€_of_self [has_colimit (parallel_pair f f)] : is_iso (coequalizer.Ï€ f f) :=
colimit_cocone_parallel_pair_self_is_iso _ _ $ colimit.is_colimit _

/-- Every coequalizer of (f, g), where f = g, is an isomorphism -/
def colimit_cocone_parallel_pair_self_is_iso' (c : cocone (parallel_pair f g)) (hâ‚€ : is_colimit c)
  (hâ‚ : f = g) : is_iso (c.Î¹.app one) :=
begin
  rw cofork.eq_of_Ï€_Ï€ c at *,
  have hâ‚‚ : is_colimit (cofork.of_Ï€ (c.Î¹.app one) rfl : cofork f f), by convert hâ‚€,
  exact colimit_cocone_parallel_pair_self_is_iso f (cofork.of_Ï€ (c.Î¹.app one) rfl) hâ‚‚
end

/-- The coequalizer of (f, g), where f = g, is an isomorphism -/
def coequalizer.Ï€_of_self' [has_colimit (parallel_pair f g)] (h : f = g) :
  is_iso (coequalizer.Ï€ f g) :=
colimit_cocone_parallel_pair_self_is_iso' _ _ _ (colimit.is_colimit _) h

/-- A coequalizer that is a monomorphism is an isomorphism -/
def mono_limit_cocone_parallel_pair_is_iso (c : cocone (parallel_pair f g))
  (h : is_colimit c) [mono (c.Î¹.app one)] : is_iso (c.Î¹.app one) :=
colimit_cocone_parallel_pair_self_is_iso' f g c h $
  (cancel_mono (c.Î¹.app one)).1 (cofork.condition c)

end

variables (C)

/-- `has_equalizers` represents a choice of equalizer for every pair of morphisms -/
class has_equalizers :=
(has_limits_of_shape : has_limits_of_shape.{v} walking_parallel_pair C)

/-- `has_coequalizers` represents a choice of coequalizer for every pair of morphisms -/
class has_coequalizers :=
(has_colimits_of_shape : has_colimits_of_shape.{v} walking_parallel_pair C)

attribute [instance] has_equalizers.has_limits_of_shape has_coequalizers.has_colimits_of_shape

/-- Equalizers are finite limits, so if `C` has all finite limits, it also has all equalizers -/
def has_equalizers_of_has_finite_limits [has_finite_limits.{v} C] : has_equalizers.{v} C :=
{ has_limits_of_shape := infer_instance }

/-- Coequalizers are finite colimits, of if `C` has all finite colimits, it also has all
    coequalizers -/
def has_coequalizers_of_has_finite_colimits [has_finite_colimits.{v} C] : has_coequalizers.{v} C :=
{ has_colimits_of_shape := infer_instance }

/-- If `C` has all limits of diagrams `parallel_pair f g`, then it has all equalizers -/
def has_equalizers_of_has_limit_parallel_pair
  [Î  {X Y : C} {f g : X âŸ¶ Y}, has_limit (parallel_pair f g)] : has_equalizers.{v} C :=
{ has_limits_of_shape := { has_limit := Î» F, has_limit_of_iso (diagram_iso_parallel_pair F).symm } }

/-- If `C` has all colimits of diagrams `parallel_pair f g`, then it has all coequalizers -/
def has_coequalizers_of_has_colimit_parallel_pair
  [Î  {X Y : C} {f g : X âŸ¶ Y}, has_colimit (parallel_pair f g)] : has_coequalizers.{v} C :=
{ has_colimits_of_shape := { has_colimit := Î» F, has_colimit_of_iso (diagram_iso_parallel_pair F) } }

section
-- In this section we show that a split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
variables {C} [split_mono f]

/--
A split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
Here we build the cone, and show in `split_mono_equalizes` that it is a limit cone.
-/
def cone_of_split_mono : cone (parallel_pair (ğŸ™ Y) (retraction f â‰« f)) :=
fork.of_Î¹ f (by tidy)

@[simp] lemma cone_of_split_mono_Ï€_app_zero : (cone_of_split_mono f).Ï€.app zero = f := rfl
@[simp] lemma cone_of_split_mono_Ï€_app_one : (cone_of_split_mono f).Ï€.app one = f â‰« ğŸ™ Y := rfl

/--
A split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
-/
def split_mono_equalizes {X Y : C} (f : X âŸ¶ Y) [split_mono f] : is_limit (cone_of_split_mono f) :=
{ lift := Î» s, s.Ï€.app zero â‰« retraction f,
  fac' := Î» s,
  begin
    rintros (âŸ¨âŸ©|âŸ¨âŸ©),
    { rw [cone_of_split_mono_Ï€_app_zero],
      erw [category.assoc, â† s.Ï€.naturality right, s.Ï€.naturality left, category.comp_id], },
    { erw [cone_of_split_mono_Ï€_app_one, category.comp_id, category.assoc,
            â† s.Ï€.naturality right, category.id_comp], }
  end,
  uniq' := Î» s m w, begin rw â†(w zero), simp, end, }

end

section
-- In this section we show that a split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
variables {C} [split_epi f]

/--
A split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
Here we build the cocone, and show in `split_epi_coequalizes` that it is a colimit cocone.
-/
def cocone_of_split_epi : cocone (parallel_pair (ğŸ™ X) (f â‰« section_ f)) :=
cofork.of_Ï€ f (by tidy)

@[simp] lemma cocone_of_split_epi_Î¹_app_one : (cocone_of_split_epi f).Î¹.app one = f := rfl
@[simp] lemma cocone_of_split_epi_Î¹_app_zero : (cocone_of_split_epi f).Î¹.app zero = ğŸ™ X â‰« f := rfl

/--
A split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
-/
def split_epi_coequalizes {X Y : C} (f : X âŸ¶ Y) [split_epi f] : is_colimit (cocone_of_split_epi f) :=
{ desc := Î» s, section_ f â‰« s.Î¹.app one,
  fac' := Î» s,
  begin
    rintros (âŸ¨âŸ©|âŸ¨âŸ©),
    { erw [cocone_of_split_epi_Î¹_app_zero, category.assoc, category.id_comp, â†category.assoc,
            s.Î¹.naturality right, functor.const.obj_map, category.comp_id], },
    { erw [cocone_of_split_epi_Î¹_app_one, â†category.assoc, s.Î¹.naturality right,
            â†s.Î¹.naturality left, category.id_comp] }
  end,
  uniq' := Î» s m w, begin rw â†(w one), simp, end, }

end

end category_theory.limits
