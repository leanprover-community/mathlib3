/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel
-/
import data.fintype
import category_theory.limits.limits

/-!
# Equalizers and coequalizers

This file defines (co)equalizers as special cases of (co)limits.

An equalizer is the categorical generalization of the subobject {a âˆˆ A | f(a) = g(a)} known
from abelian groups or modules. It is a limit cone over the diagram formed by `f` and `g`.

A coequalizer is the dual concept.

## Main definitions

* `walking_parallel_pair` is the indexing category used for (co)equalizer_diagrams
* `parallel_pair` is a functor from `walking_parallel_pair` to our category `C`.
* a `fork` is a cone over a parallel pair.
  * there is really only one interesting morphism in a fork: the arrow from the vertex of the fork
    to the domain of f and g. It is called `fork.Î¹`.
* an `equalizer` is now just a `limit (parallel_pair f g)`

Each of these has a dual.

## Main statements

* `equalizer.Î¹_mono` states that every equalizer map is a monomorphism
* `is_limit_cone_parallel_pair_self` states that the identity on the domain of `f` is an equalizer
  of `f` and `f`.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 1*][borceux-vol1]
-/

open category_theory

namespace category_theory.limits

local attribute [tidy] tactic.case_bash

universes v u

/-- The type of objects for the diagram indexing a (co)equalizer. -/
@[derive decidable_eq] inductive walking_parallel_pair : Type v
| zero | one

instance fintype_walking_parallel_pair : fintype walking_parallel_pair :=
{ elems := [walking_parallel_pair.zero, walking_parallel_pair.one].to_finset,
  complete := Î» x, by { cases x; simp } }

open walking_parallel_pair

/-- The type family of morphisms for the diagram indexing a (co)equalizer. -/
inductive walking_parallel_pair_hom : walking_parallel_pair â†’ walking_parallel_pair â†’ Type v
| left : walking_parallel_pair_hom zero one
| right : walking_parallel_pair_hom zero one
| id : Î  X : walking_parallel_pair.{v}, walking_parallel_pair_hom X X

open walking_parallel_pair_hom

def walking_parallel_pair_hom.comp :
  Î  (X Y Z : walking_parallel_pair)
    (f : walking_parallel_pair_hom X Y) (g : walking_parallel_pair_hom Y Z),
    walking_parallel_pair_hom X Z
  | _ _ _ (id _) h := h
  | _ _ _ left   (id one) := left
  | _ _ _ right  (id one) := right
.

instance walking_parallel_pair_hom_category : small_category.{v} walking_parallel_pair :=
{ hom  := walking_parallel_pair_hom,
  id   := walking_parallel_pair_hom.id,
  comp := walking_parallel_pair_hom.comp }

lemma walking_parallel_pair_hom_id (X : walking_parallel_pair.{v}) :
  walking_parallel_pair_hom.id X = ğŸ™ X :=
rfl

variables {C : Type u} [ğ’ : category.{v} C]
include ğ’
variables {X Y : C}

def parallel_pair (f g : X âŸ¶ Y) : walking_parallel_pair.{v} â¥¤ C :=
{ obj := Î» x, match x with
  | zero := X
  | one := Y
  end,
  map := Î» x y h, match x, y, h with
  | _, _, (id _) := ğŸ™ _
  | _, _, left := f
  | _, _, right := g
  end }.

@[simp] lemma parallel_pair_map_left (f g : X âŸ¶ Y) : (parallel_pair f g).map left = f := rfl
@[simp] lemma parallel_pair_map_right (f g : X âŸ¶ Y) : (parallel_pair f g).map right = g := rfl

@[simp] lemma parallel_pair_functor_obj
  {F : walking_parallel_pair.{v} â¥¤ C} (j : walking_parallel_pair.{v}) :
  (parallel_pair (F.map left) (F.map right)).obj j = F.obj j :=
begin
  cases j; refl
end

abbreviation fork (f g : X âŸ¶ Y) := cone (parallel_pair f g)
abbreviation cofork (f g : X âŸ¶ Y) := cocone (parallel_pair f g)

variables {f g : X âŸ¶ Y}

@[simp] lemma cone_parallel_pair_left (s : limits.cone (parallel_pair f g)) :
  (s.Ï€).app zero â‰« f = (s.Ï€).app one :=
begin
  conv { to_lhs, congr, skip, rw â†parallel_pair_map_left f g, },
  rw s.w,
end

@[simp] lemma cone_parallel_pair_right (s : limits.cone (parallel_pair f g)) :
  (s.Ï€).app zero â‰« g = (s.Ï€).app one :=
begin
  conv { to_lhs, congr, skip, rw â†parallel_pair_map_right f g, },
  rw s.w,
end

attribute [simp] walking_parallel_pair_hom_id

def fork.of_Î¹ {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) : fork f g :=
{ X := P,
  Ï€ :=
  { app := Î» X, begin cases X, exact Î¹, exact Î¹ â‰« f, end,
    naturality' := Î» X Y f,
    begin
      cases X; cases Y; cases f; dsimp; simp,
      exact w
    end }}
def cofork.of_Ï€ {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) : cofork f g :=
{ X := P,
  Î¹ :=
  { app := Î» X, begin cases X, exact f â‰« Ï€, exact Ï€, end,
    naturality' := Î» X Y f,
    begin
      cases X; cases Y; cases f; dsimp; simp,
      exact eq.symm w
    end }}

@[simp] lemma fork.of_Î¹_app_zero {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) :
  (fork.of_Î¹ Î¹ w).Ï€.app zero = Î¹ := rfl
@[simp] lemma fork.of_Î¹_app_one {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) :
  (fork.of_Î¹ Î¹ w).Ï€.app one = Î¹ â‰« f := rfl

def fork.Î¹ (t : fork f g) := t.Ï€.app zero
def cofork.Ï€ (t : cofork f g) := t.Î¹.app one
lemma fork.condition (t : fork f g) : (fork.Î¹ t) â‰« f = (fork.Î¹ t) â‰« g :=
begin
  erw [t.w left, â† t.w right], refl
end
lemma cofork.condition (t : cofork f g) : f â‰« (cofork.Ï€ t) = g â‰« (cofork.Ï€ t) :=
begin
  erw [t.w left, â† t.w right], refl
end

section
local attribute [ext] cone

/-- The fork induced by the Î¹ map of some fork t is the same as t -/
lemma fork.eq_of_Î¹_Î¹ (t : fork f g) : t = fork.of_Î¹ (fork.Î¹ t) (fork.condition t) :=
begin
  have h : t.Ï€ = (fork.of_Î¹ (fork.Î¹ t) (fork.condition t)).Ï€ :=
  begin
    ext j, cases j,
    { refl },
    { rw â†cone_parallel_pair_left, refl }
  end,
  tidy
end

end

def cone.of_fork
  {F : walking_parallel_pair.{v} â¥¤ C} (t : fork (F.map left) (F.map right)) : cone F :=
{ X := t.X,
  Ï€ :=
  { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy),
    naturality' := Î» j j' g,
    begin
      cases j; cases j'; cases g; dsimp; simp,
    end } }.
def cocone.of_cofork
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cofork (F.map left) (F.map right)) : cocone F :=
{ X := t.X,
  Î¹ :=
  { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X,
    naturality' := Î» j j' g,
    begin
      cases j; cases j'; cases g; dsimp; simp,
      erw â† t.w left, refl,
      erw â† t.w right, refl,
    end } }.

@[simp] lemma cone.of_fork_Ï€
  {F : walking_parallel_pair.{v} â¥¤ C} (t : fork (F.map left) (F.map right)) (j) :
  (cone.of_fork t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl

@[simp] lemma cocone.of_cofork_Î¹
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cofork (F.map left) (F.map right)) (j) :
  (cocone.of_cofork t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

def fork.of_cone
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cone F) : fork (F.map left) (F.map right) :=
{ X := t.X,
  Ï€ := { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy) } }
def cofork.of_cocone
  {F : walking_parallel_pair.{v} â¥¤ C} (t : cocone F) : cofork (F.map left) (F.map right) :=
{ X := t.X,
  Î¹ := { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X } }

@[simp] lemma fork.of_cone_Ï€ {F : walking_parallel_pair.{v} â¥¤ C} (t : cone F) (j) :
  (fork.of_cone t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl
@[simp] lemma cofork.of_cocone_Î¹ {F : walking_parallel_pair.{v} â¥¤ C} (t : cocone F) (j) :
  (cofork.of_cocone t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

variables (f g)

section
variables [has_limit (parallel_pair f g)]

abbreviation equalizer := limit (parallel_pair f g)

abbreviation equalizer.Î¹ : equalizer f g âŸ¶ X :=
limit.Ï€ (parallel_pair f g) zero

@[simp] lemma equalizer.Î¹.fork :
  fork.Î¹ (limits.limit.cone (parallel_pair f g)) = equalizer.Î¹ f g := rfl

@[simp] lemma equalizer.Î¹.eq_app_zero :
  (limit.cone (parallel_pair f g)).Ï€.app zero = equalizer.Î¹ f g := rfl

@[reassoc] lemma equalizer.condition : equalizer.Î¹ f g â‰« f = equalizer.Î¹ f g â‰« g :=
begin
  erw limit.w (parallel_pair f g) walking_parallel_pair_hom.left,
  erw limit.w (parallel_pair f g) walking_parallel_pair_hom.right
end

abbreviation equalizer.lift {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) : W âŸ¶ equalizer f g :=
limit.lift (parallel_pair f g) (fork.of_Î¹ k h)

/-- If a map `h` equalizes `f` and `g`, then by the limit property, it factors uniquely through
    the equalizer. This lemma is an equalizer-specific API to the "uniquely" part of that
    statement. -/
lemma equalizer.lift.uniq {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) (l : W âŸ¶ equalizer f g)
  (i : l â‰« (equalizer.Î¹ f g) = k) : l = equalizer.lift f g k h :=
begin
  refine is_limit.uniq (limit.is_limit (parallel_pair f g)) (fork.of_Î¹ k h) l _,
  intro j, cases j,
  { simp only [fork.of_Î¹_app_zero, limit.cone_Ï€], exact i },
  { rw [â†cone_parallel_pair_left, fork.of_Î¹_app_one, â†category.assoc, equalizer.Î¹.eq_app_zero, i] }
end

/-- An equalizer morphism is a monomorphism -/
lemma equalizer.Î¹_mono : mono (equalizer.Î¹ f g) :=
{ right_cancellation := Î» Z h k w,
  begin
    have hâ‚€ : (h â‰« (equalizer.Î¹ f g)) â‰« f = (h â‰« (equalizer.Î¹ f g)) â‰« g :=
      by simp only [category.assoc, equalizer.condition],
    rw equalizer.lift.uniq f g (h â‰« (equalizer.Î¹ f g)) hâ‚€ h rfl,
    rw equalizer.lift.uniq f g (h â‰« (equalizer.Î¹ f g)) hâ‚€ k w.symm
  end }
end

/-- The identity determines a cone on the equalizer diagram of f and f -/
def cone_parallel_pair_self : cone (parallel_pair f f) :=
{ X := X,
  Ï€ :=
  { app := Î» j, match j with | zero := ğŸ™ X | one := f end }}

@[simp] lemma cone_parallel_pair_self_Ï€_app_zero : (cone_parallel_pair_self f).Ï€.app zero = ğŸ™ X :=
rfl

@[simp] lemma cone_parallel_pair_self_X : (cone_parallel_pair_self f).X = X := rfl

/-- The identity on X is an equalizer of (f, f) -/
def is_limit_cone_parallel_pair_self : is_limit (cone_parallel_pair_self f) :=
{ lift := Î» s, s.Ï€.app zero,
  fac' := Î» s j,
  match j with
  | zero := by erw category.comp_id
  | one := by erw cone_parallel_pair_left
  end,
  uniq' := Î» s m w, by { convert w zero, erw category.comp_id } }

/-- Every equalizer of (f, f) is an isomorphism -/
def limit_cone_parallel_pair_self_is_iso (c : cone (parallel_pair f f)) (h : is_limit c) :
  is_iso (c.Ï€.app zero) :=
  let c' := cone_parallel_pair_self f,
    z : c â‰… c' := is_limit.unique_up_to_iso h (is_limit_cone_parallel_pair_self f) in
  is_iso.of_iso (functor.map_iso cones.forget z)

/-- An equalizer that is an epimorphism is an isomorphism -/
def epi_limit_cone_parallel_pair_is_iso (c : cone (parallel_pair f g))
  (h : is_limit c) [epi (c.Ï€.app zero)] : is_iso (c.Ï€.app zero) :=
begin
  have t : f = g, from (cancel_epi (c.Ï€.app zero)).1 (fork.condition c),
  let hâ‚ := fork.eq_of_Î¹_Î¹ c,
  rw hâ‚ at h,
  have hâ‚‚ : is_limit (fork.of_Î¹ (c.Ï€.app zero) rfl : fork f f), by convert h,
  exact limit_cone_parallel_pair_self_is_iso f (fork.of_Î¹ (c.Ï€.app zero) rfl) hâ‚‚
end

section
variables [has_colimit (parallel_pair f g)]

abbreviation coequalizer := colimit (parallel_pair f g)

abbreviation coequalizer.Ï€ : Y âŸ¶ coequalizer f g :=
colimit.Î¹ (parallel_pair f g) one

@[reassoc] lemma coequalizer.condition : f â‰« coequalizer.Ï€ f g = g â‰« coequalizer.Ï€ f g :=
begin
  erw colimit.w (parallel_pair f g) walking_parallel_pair_hom.left,
  erw colimit.w (parallel_pair f g) walking_parallel_pair_hom.right
end

abbreviation coequalizer.desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) : coequalizer f g âŸ¶ W :=
colimit.desc (parallel_pair f g) (cofork.of_Ï€ k h)
end

variables (C)

/-- `has_equalizers` represents a choice of equalizer for every pair of morphisms -/
class has_equalizers :=
(has_limits_of_shape : has_limits_of_shape.{v} walking_parallel_pair C)

/-- `has_coequalizers` represents a choice of coequalizer for every pair of morphisms -/
class has_coequalizers :=
(has_colimits_of_shape : has_colimits_of_shape.{v} walking_parallel_pair C)

attribute [instance] has_equalizers.has_limits_of_shape has_coequalizers.has_colimits_of_shape

end category_theory.limits
