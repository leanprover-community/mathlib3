/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel
-/
import category_theory.epi_mono
import category_theory.limits.has_limits

/-!
# Equalizers and coequalizers

This file defines (co)equalizers as special cases of (co)limits.

An equalizer is the categorical generalization of the subobject {a âˆˆ A | f(a) = g(a)} known
from abelian groups or modules. It is a limit cone over the diagram formed by `f` and `g`.

A coequalizer is the dual concept.

## Main definitions

* `walking_parallel_pair` is the indexing category used for (co)equalizer_diagrams
* `parallel_pair` is a functor from `walking_parallel_pair` to our category `C`.
* a `fork` is a cone over a parallel pair.
  * there is really only one interesting morphism in a fork: the arrow from the vertex of the fork
    to the domain of f and g. It is called `fork.Î¹`.
* an `equalizer` is now just a `limit (parallel_pair f g)`

Each of these has a dual.

## Main statements

* `equalizer.Î¹_mono` states that every equalizer map is a monomorphism
* `is_iso_limit_cone_parallel_pair_of_self` states that the identity on the domain of `f` is an
  equalizer of `f` and `f`.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 1*][borceux-vol1]
-/

noncomputable theory

open category_theory

namespace category_theory.limits

local attribute [tidy] tactic.case_bash

universes v u uâ‚‚

/-- The type of objects for the diagram indexing a (co)equalizer. -/
@[derive decidable_eq, derive inhabited] inductive walking_parallel_pair : Type v
| zero | one

open walking_parallel_pair

/-- The type family of morphisms for the diagram indexing a (co)equalizer. -/
@[derive decidable_eq] inductive walking_parallel_pair_hom :
  walking_parallel_pair â†’ walking_parallel_pair â†’ Type v
| left : walking_parallel_pair_hom zero one
| right : walking_parallel_pair_hom zero one
| id : Î  X : walking_parallel_pair.{v}, walking_parallel_pair_hom X X

/-- Satisfying the inhabited linter -/
instance : inhabited (walking_parallel_pair_hom zero one) :=
{ default := walking_parallel_pair_hom.left }

open walking_parallel_pair_hom

/-- Composition of morphisms in the indexing diagram for (co)equalizers. -/
def walking_parallel_pair_hom.comp :
  Î  (X Y Z : walking_parallel_pair)
    (f : walking_parallel_pair_hom X Y) (g : walking_parallel_pair_hom Y Z),
    walking_parallel_pair_hom X Z
  | _ _ _ (id _) h := h
  | _ _ _ left   (id one) := left
  | _ _ _ right  (id one) := right
.

instance walking_parallel_pair_hom_category : small_category walking_parallel_pair :=
{ hom  := walking_parallel_pair_hom,
  id   := walking_parallel_pair_hom.id,
  comp := walking_parallel_pair_hom.comp }

@[simp]
lemma walking_parallel_pair_hom_id (X : walking_parallel_pair) :
  walking_parallel_pair_hom.id X = ğŸ™ X :=
rfl

variables {C : Type u} [category.{v} C]
variables {X Y : C}

/-- `parallel_pair f g` is the diagram in `C` consisting of the two morphisms `f` and `g` with
    common domain and codomain. -/
def parallel_pair (f g : X âŸ¶ Y) : walking_parallel_pair.{v} â¥¤ C :=
{ obj := Î» x, match x with
  | zero := X
  | one := Y
  end,
  map := Î» x y h, match x, y, h with
  | _, _, (id _) := ğŸ™ _
  | _, _, left := f
  | _, _, right := g
  end,
  -- `tidy` can cope with this, but it's too slow:
  map_comp' := begin rintros (âŸ¨âŸ©|âŸ¨âŸ©) (âŸ¨âŸ©|âŸ¨âŸ©) (âŸ¨âŸ©|âŸ¨âŸ©) âŸ¨âŸ©âŸ¨âŸ©; { unfold_aux, simp; refl }, end, }.

@[simp] lemma parallel_pair_obj_zero (f g : X âŸ¶ Y) : (parallel_pair f g).obj zero = X := rfl
@[simp] lemma parallel_pair_obj_one (f g : X âŸ¶ Y) : (parallel_pair f g).obj one = Y := rfl

@[simp] lemma parallel_pair_map_left (f g : X âŸ¶ Y) : (parallel_pair f g).map left = f := rfl
@[simp] lemma parallel_pair_map_right (f g : X âŸ¶ Y) : (parallel_pair f g).map right = g := rfl

@[simp] lemma parallel_pair_functor_obj
  {F : walking_parallel_pair â¥¤ C} (j : walking_parallel_pair) :
  (parallel_pair (F.map left) (F.map right)).obj j = F.obj j :=
begin
  cases j; refl
end

/-- Every functor indexing a (co)equalizer is naturally isomorphic (actually, equal) to a
    `parallel_pair` -/
@[simps]
def diagram_iso_parallel_pair (F : walking_parallel_pair â¥¤ C) :
  F â‰… parallel_pair (F.map left) (F.map right) :=
nat_iso.of_components (Î» j, eq_to_iso $ by cases j; tidy) $ by tidy

/-- Construct a morphism between parallel pairs. -/
def parallel_pair_hom {X' Y' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : X âŸ¶ X') (q : Y âŸ¶ Y')
  (wf : f â‰« q = p â‰« f') (wg : g â‰« q = p â‰« g') : parallel_pair f g âŸ¶ parallel_pair f' g' :=
{ app := Î» j, match j with
  | zero := p
  | one := q
  end,
  naturality' := begin
    rintros (âŸ¨âŸ©|âŸ¨âŸ©) (âŸ¨âŸ©|âŸ¨âŸ©) âŸ¨âŸ©; { unfold_aux, simp [wf, wg], },
  end }

@[simp] lemma parallel_pair_hom_app_zero
  {X' Y' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : X âŸ¶ X') (q : Y âŸ¶ Y')
  (wf : f â‰« q = p â‰« f') (wg : g â‰« q = p â‰« g') :
  (parallel_pair_hom f g f' g' p q wf wg).app zero = p := rfl

@[simp] lemma parallel_pair_hom_app_one
  {X' Y' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : X âŸ¶ X') (q : Y âŸ¶ Y')
  (wf : f â‰« q = p â‰« f') (wg : g â‰« q = p â‰« g') :
  (parallel_pair_hom f g f' g' p q wf wg).app one = q := rfl

/-- A fork on `f` and `g` is just a `cone (parallel_pair f g)`. -/
abbreviation fork (f g : X âŸ¶ Y) := cone (parallel_pair f g)

/-- A cofork on `f` and `g` is just a `cocone (parallel_pair f g)`. -/
abbreviation cofork (f g : X âŸ¶ Y) := cocone (parallel_pair f g)

variables {f g : X âŸ¶ Y}

/-- A fork `t` on the parallel pair `f g : X âŸ¶ Y` consists of two morphisms `t.Ï€.app zero : t.X âŸ¶ X`
    and `t.Ï€.app one : t.X âŸ¶ Y`. Of these, only the first one is interesting, and we give it the
    shorter name `fork.Î¹ t`. -/
abbreviation fork.Î¹ (t : fork f g) := t.Ï€.app zero

/-- A cofork `t` on the parallel_pair `f g : X âŸ¶ Y` consists of two morphisms
    `t.Î¹.app zero : X âŸ¶ t.X` and `t.Î¹.app one : Y âŸ¶ t.X`. Of these, only the second one is
    interesting, and we give it the shorter name `cofork.Ï€ t`. -/
abbreviation cofork.Ï€ (t : cofork f g) := t.Î¹.app one

@[simp] lemma fork.Î¹_eq_app_zero (t : fork f g) : t.Î¹ = t.Ï€.app zero := rfl
@[simp] lemma cofork.Ï€_eq_app_one (t : cofork f g) : t.Ï€ = t.Î¹.app one := rfl

@[simp, reassoc] lemma fork.app_zero_left (s : fork f g) :
  s.Ï€.app zero â‰« f = s.Ï€.app one :=
by rw [â†s.w left, parallel_pair_map_left]

@[simp, reassoc] lemma fork.app_zero_right (s : fork f g) :
  s.Ï€.app zero â‰« g = s.Ï€.app one :=
by rw [â†s.w right, parallel_pair_map_right]

@[simp, reassoc] lemma cofork.left_app_one (s : cofork f g) :
  f â‰« s.Î¹.app one = s.Î¹.app zero :=
by rw [â†s.w left, parallel_pair_map_left]

@[simp, reassoc] lemma cofork.right_app_one (s : cofork f g) :
  g â‰« s.Î¹.app one = s.Î¹.app zero :=
by rw [â†s.w right, parallel_pair_map_right]

/-- A fork on `f g : X âŸ¶ Y` is determined by the morphism `Î¹ : P âŸ¶ X` satisfying `Î¹ â‰« f = Î¹ â‰« g`.
-/
@[simps]
def fork.of_Î¹ {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) : fork f g :=
{ X := P,
  Ï€ :=
  { app := Î» X, begin cases X, exact Î¹, exact Î¹ â‰« f, end,
    naturality' := Î» X Y f,
    begin
      cases X; cases Y; cases f; dsimp; simp,
      { dsimp, simp, }, -- See note [dsimp, simp].
      { exact w },
      { dsimp, simp, },
    end } }

/-- A cofork on `f g : X âŸ¶ Y` is determined by the morphism `Ï€ : Y âŸ¶ P` satisfying
    `f â‰« Ï€ = g â‰« Ï€`. -/
@[simps]
def cofork.of_Ï€ {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) : cofork f g :=
{ X := P,
  Î¹ :=
  { app := Î» X, walking_parallel_pair.cases_on X (f â‰« Ï€) Ï€,
    naturality' := Î» i j f, by { cases f; dsimp; simp [w] } } } -- See note [dsimp, simp]

lemma fork.Î¹_of_Î¹ {P : C} (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = Î¹ â‰« g) :
  (fork.of_Î¹ Î¹ w).Î¹ = Î¹ := rfl
lemma cofork.Ï€_of_Ï€ {P : C} (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = g â‰« Ï€) :
  (cofork.of_Ï€ Ï€ w).Ï€ = Ï€ := rfl

@[reassoc]
lemma fork.condition (t : fork f g) : t.Î¹ â‰« f = t.Î¹ â‰« g :=
by rw [t.app_zero_left, t.app_zero_right]
@[reassoc]
lemma cofork.condition (t : cofork f g) : f â‰« t.Ï€ = g â‰« t.Ï€ :=
by rw [t.left_app_one, t.right_app_one]

/-- To check whether two maps are equalized by both maps of a fork, it suffices to check it for the
    first map -/
lemma fork.equalizer_ext (s : fork f g) {W : C} {k l : W âŸ¶ s.X}
  (h : k â‰« fork.Î¹ s = l â‰« fork.Î¹ s) : âˆ€ (j : walking_parallel_pair),
    k â‰« s.Ï€.app j = l â‰« s.Ï€.app j
| zero := h
| one := by rw [â†fork.app_zero_left, reassoc_of h]

/-- To check whether two maps are coequalized by both maps of a cofork, it suffices to check it for
    the second map -/
lemma cofork.coequalizer_ext (s : cofork f g) {W : C} {k l : s.X âŸ¶ W}
  (h : cofork.Ï€ s â‰« k = cofork.Ï€ s â‰« l) : âˆ€ (j : walking_parallel_pair),
    s.Î¹.app j â‰« k = s.Î¹.app j â‰« l
| zero := by simp only [â†cofork.left_app_one, category.assoc, h]
| one := h

lemma fork.is_limit.hom_ext {s : fork f g} (hs : is_limit s) {W : C} {k l : W âŸ¶ s.X}
  (h : k â‰« fork.Î¹ s = l â‰« fork.Î¹ s) : k = l :=
hs.hom_ext $ fork.equalizer_ext _ h

lemma cofork.is_colimit.hom_ext {s : cofork f g} (hs : is_colimit s) {W : C} {k l : s.X âŸ¶ W}
  (h : cofork.Ï€ s â‰« k = cofork.Ï€ s â‰« l) : k = l :=
hs.hom_ext $ cofork.coequalizer_ext _ h

/-- If `s` is a limit fork over `f` and `g`, then a morphism `k : W âŸ¶ X` satisfying
    `k â‰« f = k â‰« g` induces a morphism `l : W âŸ¶ s.X` such that `l â‰« fork.Î¹ s = k`. -/
def fork.is_limit.lift' {s : fork f g} (hs : is_limit s) {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
  {l : W âŸ¶ s.X // l â‰« fork.Î¹ s = k} :=
âŸ¨hs.lift $ fork.of_Î¹ _ h, hs.fac _ _âŸ©

/-- If `s` is a colimit cofork over `f` and `g`, then a morphism `k : Y âŸ¶ W` satisfying
    `f â‰« k = g â‰« k` induces a morphism `l : s.X âŸ¶ W` such that `cofork.Ï€ s â‰« l = k`. -/
def cofork.is_colimit.desc' {s : cofork f g} (hs : is_colimit s) {W : C} (k : Y âŸ¶ W)
  (h : f â‰« k = g â‰« k) : {l : s.X âŸ¶ W // cofork.Ï€ s â‰« l = k} :=
âŸ¨hs.desc $ cofork.of_Ï€ _ h, hs.fac _ _âŸ©

/-- This is a slightly more convenient method to verify that a fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def fork.is_limit.mk (t : fork f g)
  (lift : Î  (s : fork f g), s.X âŸ¶ t.X)
  (fac : âˆ€ (s : fork f g), lift s â‰« fork.Î¹ t = fork.Î¹ s)
  (uniq : âˆ€ (s : fork f g) (m : s.X âŸ¶ t.X)
    (w : âˆ€ j : walking_parallel_pair, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) :
  is_limit t :=
{ lift := lift,
  fac' := Î» s j, walking_parallel_pair.cases_on j (fac s) $
    by erw [â†s.w left, â†t.w left, â†category.assoc, fac]; refl,
  uniq' := uniq }

/-- This is another convenient method to verify that a fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def fork.is_limit.mk' {X Y : C} {f g : X âŸ¶ Y} (t : fork f g)
  (create : Î  (s : fork f g), {l // l â‰« t.Î¹ = s.Î¹ âˆ§ âˆ€ {m}, m â‰« t.Î¹ = s.Î¹ â†’ m = l}) :
is_limit t :=
fork.is_limit.mk t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s m w, (create s).2.2 (w zero))

/-- This is a slightly more convenient method to verify that a cofork is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content -/
def cofork.is_colimit.mk (t : cofork f g)
  (desc : Î  (s : cofork f g), t.X âŸ¶ s.X)
  (fac : âˆ€ (s : cofork f g), cofork.Ï€ t â‰« desc s = cofork.Ï€ s)
  (uniq : âˆ€ (s : cofork f g) (m : t.X âŸ¶ s.X)
    (w : âˆ€ j : walking_parallel_pair, t.Î¹.app j â‰« m = s.Î¹.app j), m = desc s) :
  is_colimit t :=
{ desc := desc,
  fac' := Î» s j, walking_parallel_pair.cases_on j
    (by erw [â†s.w left, â†t.w left, category.assoc, fac]; refl) (fac s),
  uniq' := uniq }

/-- This is another convenient method to verify that a fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def cofork.is_colimit.mk' {X Y : C} {f g : X âŸ¶ Y} (t : cofork f g)
  (create : Î  (s : cofork f g), {l : t.X âŸ¶ s.X // t.Ï€ â‰« l = s.Ï€ âˆ§ âˆ€ {m}, t.Ï€ â‰« m = s.Ï€ â†’ m = l}) :
is_colimit t :=
cofork.is_colimit.mk t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s m w, (create s).2.2 (w one))

/--
Given a limit cone for the pair `f g : X âŸ¶ Y`, for any `Z`, morphisms from `Z` to its point are in
bijection with morphisms `h : Z âŸ¶ X` such that `h â‰« f = h â‰« g`.
Further, this bijection is natural in `Z`: see `fork.is_limit.hom_iso_natural`.
This is a special case of `is_limit.hom_iso'`, often useful to construct adjunctions.
-/
@[simps]
def fork.is_limit.hom_iso {X Y : C} {f g : X âŸ¶ Y} {t : fork f g} (ht : is_limit t) (Z : C) :
  (Z âŸ¶ t.X) â‰ƒ {h : Z âŸ¶ X // h â‰« f = h â‰« g} :=
{ to_fun := Î» k, âŸ¨k â‰« t.Î¹, by simpâŸ©,
  inv_fun := Î» h, (fork.is_limit.lift' ht _ h.prop).1,
  left_inv := Î» k, fork.is_limit.hom_ext ht (fork.is_limit.lift' _ _ _).prop,
  right_inv := Î» h, subtype.ext (fork.is_limit.lift' ht _ _).prop }

/-- The bijection of `fork.is_limit.hom_iso` is natural in `Z`. -/
lemma fork.is_limit.hom_iso_natural {X Y : C} {f g : X âŸ¶ Y} {t : fork f g} (ht : is_limit t)
  {Z Z' : C} (q : Z' âŸ¶ Z) (k : Z âŸ¶ t.X) :
  (fork.is_limit.hom_iso ht _ (q â‰« k) : Z' âŸ¶ X) = q â‰« (fork.is_limit.hom_iso ht _ k : Z âŸ¶ X) :=
category.assoc _ _ _

/--
Given a colimit cocone for the pair `f g : X âŸ¶ Y`, for any `Z`, morphisms from the cocone point
to `Z` are in bijection with morphisms `h : Y âŸ¶ Z` such that `f â‰« h = g â‰« h`.
Further, this bijection is natural in `Z`: see `cofork.is_colimit.hom_iso_natural`.
This is a special case of `is_colimit.hom_iso'`, often useful to construct adjunctions.
-/
@[simps]
def cofork.is_colimit.hom_iso {X Y : C} {f g : X âŸ¶ Y} {t : cofork f g} (ht : is_colimit t) (Z : C) :
  (t.X âŸ¶ Z) â‰ƒ {h : Y âŸ¶ Z // f â‰« h = g â‰« h} :=
{ to_fun := Î» k, âŸ¨t.Ï€ â‰« k, by simpâŸ©,
  inv_fun := Î» h, (cofork.is_colimit.desc' ht _ h.prop).1,
  left_inv := Î» k, cofork.is_colimit.hom_ext ht (cofork.is_colimit.desc' _ _ _).prop,
  right_inv := Î» h, subtype.ext (cofork.is_colimit.desc' ht _ _).prop }

/-- The bijection of `cofork.is_colimit.hom_iso` is natural in `Z`. -/
lemma cofork.is_colimit.hom_iso_natural {X Y : C} {f g : X âŸ¶ Y} {t : cofork f g} {Z Z' : C}
  (q : Z âŸ¶ Z') (ht : is_colimit t) (k : t.X âŸ¶ Z) :
    (cofork.is_colimit.hom_iso ht _ (k â‰« q) : Y âŸ¶ Z') =
    (cofork.is_colimit.hom_iso ht _ k : Y âŸ¶ Z) â‰« q :=
(category.assoc _ _ _).symm

/-- This is a helper construction that can be useful when verifying that a category has all
    equalizers. Given `F : walking_parallel_pair â¥¤ C`, which is really the same as
    `parallel_pair (F.map left) (F.map right)`, and a fork on `F.map left` and `F.map right`,
    we get a cone on `F`.

    If you're thinking about using this, have a look at `has_equalizers_of_has_limit_parallel_pair`,
    which you may find to be an easier way of achieving your goal. -/
def cone.of_fork
  {F : walking_parallel_pair â¥¤ C} (t : fork (F.map left) (F.map right)) : cone F :=
{ X := t.X,
  Ï€ :=
  { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy),
    naturality' := Î» j j' g, by { cases j; cases j'; cases g; dsimp; simp } } }

/-- This is a helper construction that can be useful when verifying that a category has all
    coequalizers. Given `F : walking_parallel_pair â¥¤ C`, which is really the same as
    `parallel_pair (F.map left) (F.map right)`, and a cofork on `F.map left` and `F.map right`,
    we get a cocone on `F`.

    If you're thinking about using this, have a look at
    `has_coequalizers_of_has_colimit_parallel_pair`, which you may find to be an easier way of
    achieving your goal. -/
def cocone.of_cofork
  {F : walking_parallel_pair â¥¤ C} (t : cofork (F.map left) (F.map right)) : cocone F :=
{ X := t.X,
  Î¹ :=
  { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X,
    naturality' := Î» j j' g, by { cases j; cases j'; cases g; dsimp; simp } } }

@[simp] lemma cone.of_fork_Ï€
  {F : walking_parallel_pair â¥¤ C} (t : fork (F.map left) (F.map right)) (j) :
  (cone.of_fork t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl

@[simp] lemma cocone.of_cofork_Î¹
  {F : walking_parallel_pair â¥¤ C} (t : cofork (F.map left) (F.map right)) (j) :
  (cocone.of_cofork t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

/-- Given `F : walking_parallel_pair â¥¤ C`, which is really the same as
    `parallel_pair (F.map left) (F.map right)` and a cone on `F`, we get a fork on
    `F.map left` and `F.map right`. -/
def fork.of_cone
  {F : walking_parallel_pair â¥¤ C} (t : cone F) : fork (F.map left) (F.map right) :=
{ X := t.X,
  Ï€ := { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy) } }

/-- Given `F : walking_parallel_pair â¥¤ C`, which is really the same as
    `parallel_pair (F.map left) (F.map right)` and a cocone on `F`, we get a cofork on
    `F.map left` and `F.map right`. -/
def cofork.of_cocone
  {F : walking_parallel_pair â¥¤ C} (t : cocone F) : cofork (F.map left) (F.map right) :=
{ X := t.X,
  Î¹ := { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X } }

@[simp] lemma fork.of_cone_Ï€ {F : walking_parallel_pair â¥¤ C} (t : cone F) (j) :
  (fork.of_cone t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl
@[simp] lemma cofork.of_cocone_Î¹ {F : walking_parallel_pair â¥¤ C} (t : cocone F) (j) :
  (cofork.of_cocone t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

/--
Helper function for constructing morphisms between equalizer forks.
-/
@[simps]
def fork.mk_hom {s t : fork f g} (k : s.X âŸ¶ t.X) (w : k â‰« t.Î¹ = s.Î¹) : s âŸ¶ t :=
{ hom := k,
  w' :=
  begin
    rintro âŸ¨_|_âŸ©,
    { exact w },
    { simpa using w =â‰« f },
  end }

/--
To construct an isomorphism between forks,
it suffices to give an isomorphism between the cone points
and check that it commutes with the `Î¹` morphisms.
-/
@[simps]
def fork.ext {s t : fork f g} (i : s.X â‰… t.X) (w : i.hom â‰« t.Î¹ = s.Î¹) : s â‰… t :=
{ hom := fork.mk_hom i.hom w,
  inv := fork.mk_hom i.inv (by rw [â† w, iso.inv_hom_id_assoc]) }

/--
Helper function for constructing morphisms between coequalizer coforks.
-/
@[simps]
def cofork.mk_hom {s t : cofork f g} (k : s.X âŸ¶ t.X) (w : s.Ï€ â‰« k = t.Ï€) : s âŸ¶ t :=
{ hom := k,
  w' :=
  begin
    rintro âŸ¨_|_âŸ©,
    simpa using f â‰«= w,
    exact w,
  end }

/--
To construct an isomorphism between coforks,
it suffices to give an isomorphism between the cocone points
and check that it commutes with the `Ï€` morphisms.
-/
def cofork.ext {s t : cofork f g} (i : s.X â‰… t.X) (w : s.Ï€ â‰« i.hom = t.Ï€) : s â‰… t :=
{ hom := cofork.mk_hom i.hom w,
  inv := cofork.mk_hom i.inv (by rw [iso.comp_inv_eq, w]) }

variables (f g)

section
/--
`has_equalizer f g` represents a particular choice of limiting cone
for the parallel pair of morphisms `f` and `g`.
-/
abbreviation has_equalizer := has_limit (parallel_pair f g)

variables [has_equalizer f g]

/-- If an equalizer of `f` and `g` exists, we can access an arbitrary choice of such by
    saying `equalizer f g`. -/
abbreviation equalizer : C := limit (parallel_pair f g)

/-- If an equalizer of `f` and `g` exists, we can access the inclusion
    `equalizer f g âŸ¶ X` by saying `equalizer.Î¹ f g`. -/
abbreviation equalizer.Î¹ : equalizer f g âŸ¶ X :=
limit.Ï€ (parallel_pair f g) zero

/--
An equalizer cone for a parallel pair `f` and `g`.
-/
abbreviation equalizer.fork : fork f g := limit.cone (parallel_pair f g)

@[simp] lemma equalizer.fork_Î¹ :
  (equalizer.fork f g).Î¹ = equalizer.Î¹ f g := rfl

@[simp] lemma equalizer.fork_Ï€_app_zero :
  (equalizer.fork f g).Ï€.app zero = equalizer.Î¹ f g := rfl

@[reassoc] lemma equalizer.condition : equalizer.Î¹ f g â‰« f = equalizer.Î¹ f g â‰« g :=
fork.condition $ limit.cone $ parallel_pair f g

/-- The equalizer built from `equalizer.Î¹ f g` is limiting. -/
def equalizer_is_equalizer : is_limit (fork.of_Î¹ (equalizer.Î¹ f g) (equalizer.condition f g)) :=
is_limit.of_iso_limit (limit.is_limit _) (fork.ext (iso.refl _) (by tidy))

variables {f g}

/-- A morphism `k : W âŸ¶ X` satisfying `k â‰« f = k â‰« g` factors through the equalizer of `f` and `g`
    via `equalizer.lift : W âŸ¶ equalizer f g`. -/
abbreviation equalizer.lift {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) : W âŸ¶ equalizer f g :=
limit.lift (parallel_pair f g) (fork.of_Î¹ k h)

@[simp, reassoc]
lemma equalizer.lift_Î¹ {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
  equalizer.lift k h â‰« equalizer.Î¹ f g = k :=
limit.lift_Ï€ _ _

/-- A morphism `k : W âŸ¶ X` satisfying `k â‰« f = k â‰« g` induces a morphism `l : W âŸ¶ equalizer f g`
    satisfying `l â‰« equalizer.Î¹ f g = k`. -/
def equalizer.lift' {W : C} (k : W âŸ¶ X) (h : k â‰« f = k â‰« g) :
  {l : W âŸ¶ equalizer f g // l â‰« equalizer.Î¹ f g = k} :=
âŸ¨equalizer.lift k h, equalizer.lift_Î¹ _ _âŸ©

/-- Two maps into an equalizer are equal if they are are equal when composed with the equalizer
    map. -/
@[ext] lemma equalizer.hom_ext {W : C} {k l : W âŸ¶ equalizer f g}
  (h : k â‰« equalizer.Î¹ f g = l â‰« equalizer.Î¹ f g) : k = l :=
fork.is_limit.hom_ext (limit.is_limit _) h

/-- An equalizer morphism is a monomorphism -/
instance equalizer.Î¹_mono : mono (equalizer.Î¹ f g) :=
{ right_cancellation := Î» Z h k w, equalizer.hom_ext w }

end

section
variables {f g}
/-- The equalizer morphism in any limit cone is a monomorphism. -/
lemma mono_of_is_limit_parallel_pair {c : cone (parallel_pair f g)} (i : is_limit c) :
  mono (fork.Î¹ c) :=
{ right_cancellation := Î» Z h k w, fork.is_limit.hom_ext i w }

end

section
variables {f g}

/-- The identity determines a cone on the equalizer diagram of `f` and `g` if `f = g`. -/
def id_fork (h : f = g) : fork f g :=
fork.of_Î¹ (ğŸ™ X) $ h â–¸ rfl

/-- The identity on `X` is an equalizer of `(f, g)`, if `f = g`. -/
def is_limit_id_fork (h : f = g) : is_limit (id_fork h) :=
fork.is_limit.mk _
  (Î» s, fork.Î¹ s)
  (Î» s, category.comp_id _)
  (Î» s m h, by { convert h zero, exact (category.comp_id _).symm })

/-- Every equalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
lemma is_iso_limit_cone_parallel_pair_of_eq (hâ‚€ : f = g) {c : cone (parallel_pair f g)}
  (h : is_limit c) : is_iso (c.Ï€.app zero) :=
is_iso.of_iso $ is_limit.cone_point_unique_up_to_iso h $ is_limit_id_fork hâ‚€

/-- The equalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
lemma equalizer.Î¹_of_eq [has_equalizer f g] (h : f = g) : is_iso (equalizer.Î¹ f g) :=
is_iso_limit_cone_parallel_pair_of_eq h $ limit.is_limit _

/-- Every equalizer of `(f, f)` is an isomorphism. -/
lemma is_iso_limit_cone_parallel_pair_of_self {c : cone (parallel_pair f f)} (h : is_limit c) :
  is_iso (c.Ï€.app zero) :=
is_iso_limit_cone_parallel_pair_of_eq rfl h

/-- An equalizer that is an epimorphism is an isomorphism. -/
lemma is_iso_limit_cone_parallel_pair_of_epi {c : cone (parallel_pair f g)}
  (h : is_limit c) [epi (c.Ï€.app zero)] : is_iso (c.Ï€.app zero) :=
is_iso_limit_cone_parallel_pair_of_eq ((cancel_epi _).1 (fork.condition c)) h

end

instance has_equalizer_of_self : has_equalizer f f :=
has_limit.mk
{ cone := id_fork rfl,
  is_limit := is_limit_id_fork rfl }

/-- The equalizer inclusion for `(f, f)` is an isomorphism. -/
instance equalizer.Î¹_of_self : is_iso (equalizer.Î¹ f f) :=
equalizer.Î¹_of_eq rfl

/-- The equalizer of a morphism with itself is isomorphic to the source. -/
def equalizer.iso_source_of_self : equalizer f f â‰… X :=
as_iso (equalizer.Î¹ f f)

@[simp] lemma equalizer.iso_source_of_self_hom :
  (equalizer.iso_source_of_self f).hom = equalizer.Î¹ f f :=
rfl

@[simp] lemma equalizer.iso_source_of_self_inv :
  (equalizer.iso_source_of_self f).inv = equalizer.lift (ğŸ™ X) (by simp) :=
by { ext, simp [equalizer.iso_source_of_self], }

section
/--
`has_coequalizer f g` represents a particular choice of colimiting cocone
for the parallel pair of morphisms `f` and `g`.
-/
abbreviation has_coequalizer := has_colimit (parallel_pair f g)

variables [has_coequalizer f g]

/-- If a coequalizer of `f` and `g` exists, we can access an arbitrary choice of such by
    saying `coequalizer f g`. -/
abbreviation coequalizer : C := colimit (parallel_pair f g)

/--  If a coequalizer of `f` and `g` exists, we can access the corresponding projection by
    saying `coequalizer.Ï€ f g`. -/
abbreviation coequalizer.Ï€ : Y âŸ¶ coequalizer f g :=
colimit.Î¹ (parallel_pair f g) one

/--
An arbitrary choice of coequalizer cocone for a parallel pair `f` and `g`.
-/
abbreviation coequalizer.cofork : cofork f g := colimit.cocone (parallel_pair f g)

@[simp] lemma coequalizer.cofork_Ï€ :
  (coequalizer.cofork f g).Ï€ = coequalizer.Ï€ f g := rfl

@[simp] lemma coequalizer.cofork_Î¹_app_one :
  (coequalizer.cofork f g).Î¹.app one = coequalizer.Ï€ f g := rfl

@[reassoc] lemma coequalizer.condition : f â‰« coequalizer.Ï€ f g = g â‰« coequalizer.Ï€ f g :=
cofork.condition $ colimit.cocone $ parallel_pair f g

/-- The cofork built from `coequalizer.Ï€ f g` is colimiting. -/
def coequalizer_is_coequalizer :
  is_colimit (cofork.of_Ï€ (coequalizer.Ï€ f g) (coequalizer.condition f g)) :=
is_colimit.of_iso_colimit (colimit.is_colimit _) (cofork.ext (iso.refl _) (by tidy))

variables {f g}

/-- Any morphism `k : Y âŸ¶ W` satisfying `f â‰« k = g â‰« k` factors through the coequalizer of `f`
    and `g` via `coequalizer.desc : coequalizer f g âŸ¶ W`. -/
abbreviation coequalizer.desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) : coequalizer f g âŸ¶ W :=
colimit.desc (parallel_pair f g) (cofork.of_Ï€ k h)

@[simp, reassoc]
lemma coequalizer.Ï€_desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) :
  coequalizer.Ï€ f g â‰« coequalizer.desc k h = k :=
colimit.Î¹_desc _ _

/-- Any morphism `k : Y âŸ¶ W` satisfying `f â‰« k = g â‰« k` induces a morphism
    `l : coequalizer f g âŸ¶ W` satisfying `coequalizer.Ï€ â‰« g = l`. -/
def coequalizer.desc' {W : C} (k : Y âŸ¶ W) (h : f â‰« k = g â‰« k) :
  {l : coequalizer f g âŸ¶ W // coequalizer.Ï€ f g â‰« l = k} :=
âŸ¨coequalizer.desc k h, coequalizer.Ï€_desc _ _âŸ©

/-- Two maps from a coequalizer are equal if they are equal when composed with the coequalizer
    map -/
@[ext] lemma coequalizer.hom_ext {W : C} {k l : coequalizer f g âŸ¶ W}
  (h : coequalizer.Ï€ f g â‰« k = coequalizer.Ï€ f g â‰« l) : k = l :=
cofork.is_colimit.hom_ext (colimit.is_colimit _) h

/-- A coequalizer morphism is an epimorphism -/
instance coequalizer.Ï€_epi : epi (coequalizer.Ï€ f g) :=
{ left_cancellation := Î» Z h k w, coequalizer.hom_ext w }

end

section
variables {f g}

/-- The coequalizer morphism in any colimit cocone is an epimorphism. -/
lemma epi_of_is_colimit_parallel_pair {c : cocone (parallel_pair f g)} (i : is_colimit c) :
  epi (c.Î¹.app one) :=
{ left_cancellation := Î» Z h k w, cofork.is_colimit.hom_ext i w }

end

section
variables {f g}

/-- The identity determines a cocone on the coequalizer diagram of `f` and `g`, if `f = g`. -/
def id_cofork (h : f = g) : cofork f g :=
cofork.of_Ï€ (ğŸ™ Y) $ h â–¸ rfl

/-- The identity on `Y` is a coequalizer of `(f, g)`, where `f = g`.  -/
def is_colimit_id_cofork (h : f = g) : is_colimit (id_cofork h) :=
cofork.is_colimit.mk _
  (Î» s, cofork.Ï€ s)
  (Î» s, category.id_comp _)
  (Î» s m h, by { convert h one, exact (category.id_comp _).symm })

/-- Every coequalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
lemma is_iso_colimit_cocone_parallel_pair_of_eq (hâ‚€ : f = g) {c : cocone (parallel_pair f g)}
  (h : is_colimit c) : is_iso (c.Î¹.app one) :=
is_iso.of_iso $ is_colimit.cocone_point_unique_up_to_iso (is_colimit_id_cofork hâ‚€) h

/-- The coequalizer of `(f, g)`, where `f = g`, is an isomorphism. -/
lemma coequalizer.Ï€_of_eq [has_coequalizer f g] (h : f = g) :
  is_iso (coequalizer.Ï€ f g) :=
is_iso_colimit_cocone_parallel_pair_of_eq h $ colimit.is_colimit _

/-- Every coequalizer of `(f, f)` is an isomorphism. -/
lemma is_iso_colimit_cocone_parallel_pair_of_self {c : cocone (parallel_pair f f)}
  (h : is_colimit c) : is_iso (c.Î¹.app one) :=
is_iso_colimit_cocone_parallel_pair_of_eq rfl h

/-- A coequalizer that is a monomorphism is an isomorphism. -/
lemma is_iso_limit_cocone_parallel_pair_of_epi {c : cocone (parallel_pair f g)}
  (h : is_colimit c) [mono (c.Î¹.app one)] : is_iso (c.Î¹.app one) :=
is_iso_colimit_cocone_parallel_pair_of_eq ((cancel_mono _).1 (cofork.condition c)) h

end

instance has_coequalizer_of_self : has_coequalizer f f :=
has_colimit.mk
{ cocone := id_cofork rfl,
  is_colimit := is_colimit_id_cofork rfl }

/-- The coequalizer projection for `(f, f)` is an isomorphism. -/
instance coequalizer.Ï€_of_self : is_iso (coequalizer.Ï€ f f) :=
coequalizer.Ï€_of_eq rfl

/-- The coequalizer of a morphism with itself is isomorphic to the target. -/
def coequalizer.iso_target_of_self : coequalizer f f â‰… Y :=
(as_iso (coequalizer.Ï€ f f)).symm

@[simp] lemma coequalizer.iso_target_of_self_hom :
  (coequalizer.iso_target_of_self f).hom = coequalizer.desc (ğŸ™ Y) (by simp) :=
by { ext, simp [coequalizer.iso_target_of_self], }

@[simp] lemma coequalizer.iso_target_of_self_inv :
  (coequalizer.iso_target_of_self f).inv = coequalizer.Ï€ f f :=
rfl

section comparison

variables {D : Type uâ‚‚} [category.{v} D] (G : C â¥¤ D)

/--
The comparison morphism for the equalizer of `f,g`.
This is an isomorphism iff `G` preserves the equalizer of `f,g`; see
`category_theory/limits/preserves/shapes/equalizers.lean`
-/
def equalizer_comparison [has_equalizer f g] [has_equalizer (G.map f) (G.map g)] :
  G.obj (equalizer f g) âŸ¶ equalizer (G.map f) (G.map g) :=
equalizer.lift (G.map (equalizer.Î¹ _ _)) (by simp only [â†G.map_comp, equalizer.condition])

@[simp, reassoc]
lemma equalizer_comparison_comp_Ï€ [has_equalizer f g] [has_equalizer (G.map f) (G.map g)] :
  equalizer_comparison f g G â‰« equalizer.Î¹ (G.map f) (G.map g) = G.map (equalizer.Î¹ f g) :=
equalizer.lift_Î¹ _ _

@[simp, reassoc]
lemma map_lift_equalizer_comparison [has_equalizer f g] [has_equalizer (G.map f) (G.map g)]
  {Z : C} {h : Z âŸ¶ X} (w : h â‰« f = h â‰« g) :
    G.map (equalizer.lift h w) â‰« equalizer_comparison f g G =
      equalizer.lift (G.map h) (by simp only [â†G.map_comp, w]) :=
by { ext, simp [â† G.map_comp] }

/-- The comparison morphism for the coequalizer of `f,g`. -/
def coequalizer_comparison [has_coequalizer f g] [has_coequalizer (G.map f) (G.map g)] :
  coequalizer (G.map f) (G.map g) âŸ¶ G.obj (coequalizer f g) :=
coequalizer.desc (G.map (coequalizer.Ï€ _ _)) (by simp only [â†G.map_comp, coequalizer.condition])

@[simp, reassoc]
lemma Î¹_comp_coequalizer_comparison [has_coequalizer f g] [has_coequalizer (G.map f) (G.map g)] :
  coequalizer.Ï€ _ _ â‰« coequalizer_comparison f g G = G.map (coequalizer.Ï€ _ _) :=
coequalizer.Ï€_desc _ _

@[simp, reassoc]
lemma coequalizer_comparison_map_desc [has_coequalizer f g] [has_coequalizer (G.map f) (G.map g)]
  {Z : C} {h : Y âŸ¶ Z} (w : f â‰« h = g â‰« h) :
  coequalizer_comparison f g G â‰« G.map (coequalizer.desc h w) =
    coequalizer.desc (G.map h) (by simp only [â†G.map_comp, w]) :=
by { ext, simp [â† G.map_comp] }

end comparison

variables (C)

/-- `has_equalizers` represents a choice of equalizer for every pair of morphisms -/
abbreviation has_equalizers := has_limits_of_shape walking_parallel_pair C

/-- `has_coequalizers` represents a choice of coequalizer for every pair of morphisms -/
abbreviation has_coequalizers := has_colimits_of_shape walking_parallel_pair C

/-- If `C` has all limits of diagrams `parallel_pair f g`, then it has all equalizers -/
lemma has_equalizers_of_has_limit_parallel_pair
  [Î  {X Y : C} {f g : X âŸ¶ Y}, has_limit (parallel_pair f g)] : has_equalizers C :=
{ has_limit := Î» F, has_limit_of_iso (diagram_iso_parallel_pair F).symm }

/-- If `C` has all colimits of diagrams `parallel_pair f g`, then it has all coequalizers -/
lemma has_coequalizers_of_has_colimit_parallel_pair
  [Î  {X Y : C} {f g : X âŸ¶ Y}, has_colimit (parallel_pair f g)] : has_coequalizers C :=
{ has_colimit := Î» F, has_colimit_of_iso (diagram_iso_parallel_pair F) }


section
-- In this section we show that a split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
variables {C} [split_mono f]

/--
A split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
Here we build the cone, and show in `split_mono_equalizes` that it is a limit cone.
-/
@[simps {rhs_md := semireducible}]
def cone_of_split_mono : cone (parallel_pair (ğŸ™ Y) (retraction f â‰« f)) :=
fork.of_Î¹ f (by simp)


/--
A split mono `f` equalizes `(retraction f â‰« f)` and `(ğŸ™ Y)`.
-/
def split_mono_equalizes {X Y : C} (f : X âŸ¶ Y) [split_mono f] : is_limit (cone_of_split_mono f) :=
fork.is_limit.mk' _ $ Î» s,
âŸ¨s.Î¹ â‰« retraction f,
 by { dsimp, rw [category.assoc, â†s.condition], apply category.comp_id },
 Î» m hm, by simp [â†hm]âŸ©

end

section
-- In this section we show that a split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
variables {C} [split_epi f]

/--
A split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
Here we build the cocone, and show in `split_epi_coequalizes` that it is a colimit cocone.
-/
@[simps {rhs_md := semireducible}]
def cocone_of_split_epi : cocone (parallel_pair (ğŸ™ X) (f â‰« section_ f)) :=
cofork.of_Ï€ f (by simp)

/--
A split epi `f` coequalizes `(f â‰« section_ f)` and `(ğŸ™ X)`.
-/
def split_epi_coequalizes {X Y : C} (f : X âŸ¶ Y) [split_epi f] :
  is_colimit (cocone_of_split_epi f) :=
cofork.is_colimit.mk' _ $ Î» s,
âŸ¨section_ f â‰« s.Ï€,
 by { dsimp, rw [â† category.assoc, â† s.condition, category.id_comp] },
 Î» m hm, by simp [â† hm]âŸ©

end

end category_theory.limits
