/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.limits.shapes.equalizers
import category_theory.limits.shapes.comm_sq
import category_theory.limits.shapes.regular_mono

/-!
# Kernel pairs

This file defines what it means for a parallel pair of morphisms `a b : R âŸ¶ X` to be the kernel pair
for a morphism `f`.
Some properties of kernel pairs are given, namely allowing one to transfer between
the kernel pair of `fâ‚ â‰« fâ‚‚` to the kernel pair of `fâ‚`.
It is also proved that if `f` is a coequalizer of some pair, and `a`,`b` is a kernel pair for `f`
then it is a coequalizer of `a`,`b`.

## Implementation

The definition is essentially just a wrapper for `is_limit (pullback_cone.mk _ _ _)`, but the
constructions given here are useful, yet awkward to present in that language, so a basic API
is developed here.

## TODO

- Internal equivalence relations (or congruences) and the fact that every kernel pair induces one,
  and the converse in an effective regular category (WIP by b-mehta).

-/

universes v u uâ‚‚

namespace category_theory

open category_theory category_theory.category category_theory.limits

variables {C : Type u} [category.{v} C]

variables {R X Y Z : C} (f : X âŸ¶ Y) (a b : R âŸ¶ X)

/--
`is_kernel_pair f a b` expresses that `(a, b)` is a kernel pair for `f`, i.e. `a â‰« f = b â‰« f`
and the square
  R â†’ X
  â†“   â†“
  X â†’ Y
is a pullback square.
This is just an abbreviation for `is_pullback a b f f`.
-/
abbreviation is_kernel_pair := is_pullback a b f f

namespace is_kernel_pair

/-- The data expressing that `(a, b)` is a kernel pair is subsingleton. -/
instance : subsingleton (is_kernel_pair f a b) :=
âŸ¨Î» P Q, by { cases P, cases Q, congr, }âŸ©

/-- If `f` is a monomorphism, then `(ğŸ™ _, ğŸ™ _)`  is a kernel pair for `f`. -/
lemma id_of_mono [mono f] : is_kernel_pair f (ğŸ™ _) (ğŸ™ _) :=
âŸ¨âŸ¨rflâŸ©, âŸ¨pullback_cone.is_limit_mk_id_id _âŸ©âŸ©

instance [mono f] : inhabited (is_kernel_pair f (ğŸ™ _) (ğŸ™ _)) := âŸ¨id_of_mono fâŸ©

variables {f a b}

/--
Given a pair of morphisms `p`, `q` to `X` which factor through `f`, they factor through any kernel
pair of `f`.
-/
noncomputable
def lift' {S : C} (k : is_kernel_pair f a b) (p q : S âŸ¶ X) (w : p â‰« f = q â‰« f) :
  { t : S âŸ¶ R // t â‰« a = p âˆ§ t â‰« b = q } :=
pullback_cone.is_limit.lift' k.is_limit _ _ w

/--
If `(a,b)` is a kernel pair for `fâ‚ â‰« fâ‚‚` and `a â‰« fâ‚ = b â‰« fâ‚`, then `(a,b)` is a kernel pair for
just `fâ‚`.
That is, to show that `(a,b)` is a kernel pair for `fâ‚` it suffices to only show the square
commutes, rather than to additionally show it's a pullback.
-/
lemma cancel_right {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} (comm : a â‰« fâ‚ = b â‰« fâ‚)
  (big_k : is_kernel_pair (fâ‚ â‰« fâ‚‚) a b) :
  is_kernel_pair fâ‚ a b :=
{ w := comm,
  is_limit' := âŸ¨pullback_cone.is_limit_aux' _ $ Î» s,
  begin
    let s' : pullback_cone (fâ‚ â‰« fâ‚‚) (fâ‚ â‰« fâ‚‚) :=
      pullback_cone.mk s.fst s.snd (s.condition_assoc _),
    refine âŸ¨big_k.is_limit.lift s',
            big_k.is_limit.fac _ walking_cospan.left,
            big_k.is_limit.fac _ walking_cospan.right,
            Î» m mâ‚ mâ‚‚, _âŸ©,
    apply big_k.is_limit.hom_ext,
    refine ((pullback_cone.mk a b _) : pullback_cone (fâ‚ â‰« fâ‚‚) _).equalizer_ext _ _,
    apply mâ‚.trans (big_k.is_limit.fac s' walking_cospan.left).symm,
    apply mâ‚‚.trans (big_k.is_limit.fac s' walking_cospan.right).symm,
  endâŸ© }

/--
If `(a,b)` is a kernel pair for `fâ‚ â‰« fâ‚‚` and `fâ‚‚` is mono, then `(a,b)` is a kernel pair for
just `fâ‚`.
The converse of `comp_of_mono`.
-/
lemma cancel_right_of_mono {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} [mono fâ‚‚]
  (big_k : is_kernel_pair (fâ‚ â‰« fâ‚‚) a b) :
  is_kernel_pair fâ‚ a b :=
cancel_right (begin rw [â† cancel_mono fâ‚‚, assoc, assoc, big_k.w] end) big_k

/--
If `(a,b)` is a kernel pair for `fâ‚` and `fâ‚‚` is mono, then `(a,b)` is a kernel pair for `fâ‚ â‰« fâ‚‚`.
The converse of `cancel_right_of_mono`.
-/
lemma comp_of_mono {fâ‚ : X âŸ¶ Y} {fâ‚‚ : Y âŸ¶ Z} [mono fâ‚‚] (small_k : is_kernel_pair fâ‚ a b) :
  is_kernel_pair (fâ‚ â‰« fâ‚‚) a b :=
{ w := by rw [small_k.w_assoc],
  is_limit' := âŸ¨pullback_cone.is_limit_aux' _ $ Î» s,
  begin
    refine âŸ¨_, _, _, _âŸ©,
    apply (pullback_cone.is_limit.lift' small_k.is_limit s.fst s.snd _).1,
    rw [â† cancel_mono fâ‚‚, assoc, s.condition, assoc],
    apply (pullback_cone.is_limit.lift' small_k.is_limit s.fst s.snd _).2.1,
    apply (pullback_cone.is_limit.lift' small_k.is_limit s.fst s.snd _).2.2,
    intros m mâ‚ mâ‚‚,
    apply small_k.is_limit.hom_ext,
    refine ((pullback_cone.mk a b _) : pullback_cone fâ‚ _).equalizer_ext _ _,
    { exact mâ‚.trans (pullback_cone.is_limit.lift' small_k.is_limit s.fst s.snd _).2.1.symm },
    { exact mâ‚‚.trans (pullback_cone.is_limit.lift' small_k.is_limit s.fst s.snd _).2.2.symm },
  endâŸ© }

/--
If `(a,b)` is the kernel pair of `f`, and `f` is a coequalizer morphism for some parallel pair, then
`f` is a coequalizer morphism of `a` and `b`.
-/
def to_coequalizer (k : is_kernel_pair f a b) [r : regular_epi f] :
  is_colimit (cofork.of_Ï€ f k.w) :=
begin
  let t := k.is_limit.lift (pullback_cone.mk _ _ r.w),
  have ht : t â‰« a = r.left := k.is_limit.fac _ walking_cospan.left,
  have kt : t â‰« b = r.right := k.is_limit.fac _ walking_cospan.right,
  apply cofork.is_colimit.mk _ _ _ _,
  { intro s,
    apply (cofork.is_colimit.desc' r.is_colimit s.Ï€ _).1,
    rw [â† ht, assoc, s.condition, reassoc_of kt] },
  { intro s,
    apply (cofork.is_colimit.desc' r.is_colimit s.Ï€ _).2 },
  { intros s m w,
    apply r.is_colimit.hom_ext,
    rintro âŸ¨âŸ©,
    change (r.left â‰« f) â‰« m = (r.left â‰« f) â‰« _,
    rw [assoc, assoc],
    congr' 1,
    erw (cofork.is_colimit.desc' r.is_colimit s.Ï€ _).2,
    apply w,
    erw (cofork.is_colimit.desc' r.is_colimit s.Ï€ _).2,
    apply w }
end

/-- If `aâ‚ aâ‚‚ : A âŸ¶ Y` is a kernel pair for `g : Y âŸ¶ Z`, then `aâ‚ Ã—[Z] X` and `aâ‚‚ Ã—[Z] X`
(`A Ã—[Z] X âŸ¶ Y Ã—[Z] X`) is a kernel pair for `Y Ã—[Z] X âŸ¶ X`. -/
protected
lemma pullback {X Y Z A : C} {g : Y âŸ¶ Z} {aâ‚ aâ‚‚ : A âŸ¶ Y}
  (h : is_kernel_pair g aâ‚ aâ‚‚) (f : X âŸ¶ Z) [has_pullback f g] [has_pullback f (aâ‚ â‰« g)] :
    is_kernel_pair (pullback.fst : pullback f g âŸ¶ X)
      (pullback.map f _ f _ (ğŸ™ X) aâ‚ (ğŸ™ Z) (by simp) $ category.comp_id _)
      (pullback.map _ _ _ _ (ğŸ™ X) aâ‚‚ (ğŸ™ Z) (by simp) $ (category.comp_id _).trans h.1.1) :=
begin
  refine âŸ¨âŸ¨_âŸ©, âŸ¨_âŸ©âŸ©,
  { rw [pullback.lift_fst, pullback.lift_fst] },
  { fapply pullback_cone.is_limit_aux',
    intro s,
    refine âŸ¨pullback.lift (s.fst â‰« pullback.fst)
      (h.lift' (s.fst â‰« pullback.snd) (s.snd â‰« pullback.snd) _).1 _, _, _, _âŸ©,
    { simp_rw [category.assoc, â† pullback.condition, â† category.assoc, s.condition] },
    { rw [â† category.assoc, (h.lift' _ _ _).2.1, category.assoc,
        category.assoc, pullback.condition] },
    { rw limits.pullback_cone.mk_fst,
      ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd, pullback.lift_snd_assoc,
        category.comp_id, (h.lift' _ _ _).2.1] },
    { rw limits.pullback_cone.mk_snd,
      ext; simp only [category.assoc, pullback.lift_fst, pullback.lift_snd, pullback.lift_snd_assoc,
        category.comp_id, (h.lift' _ _ _).2.2, s.condition] },
    { intros m hâ‚ hâ‚‚,
      ext,
      { rw pullback.lift_fst,
        conv_rhs { rw [â† hâ‚, category.assoc, pullback_cone.mk_fst] },
        congr' 1,
        refine ((pullback.lift_fst _ _ _).trans $ category.comp_id _).symm },
      { rw pullback.lift_snd,
        apply pullback_cone.is_limit.hom_ext h.is_limit;
          dsimp only [is_pullback.cone, comm_sq.cone];
          simp only [pullback_cone.mk_fst, pullback_cone.mk_snd, category.assoc,
            (h.lift' _ _ _).2.1, (h.lift' _ _ _).2.2],
        { conv_rhs { rw [â† hâ‚, category.assoc, pullback_cone.mk_fst, pullback.lift_snd] } },
        { conv_rhs { rw [â† hâ‚‚, category.assoc, pullback_cone.mk_snd, pullback.lift_snd] } } } } }
end

lemma mono_of_is_iso_fst (h : is_kernel_pair f a b) [is_iso a] :
  mono f :=
begin
  obtain âŸ¨l, hâ‚, hâ‚‚âŸ© := limits.pullback_cone.is_limit.lift' h.is_limit (ğŸ™ _) (ğŸ™ _) (by simp [h.w]),
  rw [is_pullback.cone_fst, â† is_iso.eq_comp_inv, category.id_comp] at hâ‚,
  rw [hâ‚, is_iso.inv_comp_eq, category.comp_id] at hâ‚‚,
  constructor,
  intros Z gâ‚ gâ‚‚ e,
  obtain âŸ¨l', rfl, rflâŸ© := limits.pullback_cone.is_limit.lift' h.is_limit _ _ e,
  rw [is_pullback.cone_fst, hâ‚‚],
end

lemma is_iso_of_mono (h : is_kernel_pair f a b) [mono f] :
  is_iso a :=
begin
  rw â† show _ = a, from (category.comp_id _).symm.trans ((is_kernel_pair.id_of_mono f)
    .is_limit.cone_point_unique_up_to_iso_inv_comp h.is_limit walking_cospan.left),
  apply_instance,
end

lemma of_is_iso_of_mono [is_iso a] [mono f] : is_kernel_pair f a a :=
begin
  delta is_kernel_pair,
  convert_to is_pullback a (a â‰« ğŸ™ X) (ğŸ™ X â‰« f) f,
  { rw category.comp_id }, { rw category.id_comp },
  exact (is_pullback.of_horiz_is_iso âŸ¨rflâŸ©).paste_vert (is_kernel_pair.id_of_mono f)
end

end is_kernel_pair
end category_theory
