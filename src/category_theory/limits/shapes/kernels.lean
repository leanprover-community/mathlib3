/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel
-/
import category_theory.limits.shapes.zero
import category_theory.limits.shapes.equalizers

/-!
# Kernels and cokernels

In a category with zero morphisms, the kernel of a morphism `f : X âŸ¶ Y` is just the equalizer of `f`
and `0 : X âŸ¶ Y`. (Similarly the cokernel is the coequalizer.)

We don't yet prove much here, just provide
* `kernel : (X âŸ¶ Y) â†’ C`
* `kernel.Î¹ : kernel f âŸ¶ X`
* `kernel.condition : kernel.Î¹ f â‰« f = 0` and
* `kernel.lift (k : W âŸ¶ X) (h : k â‰« f = 0) : W âŸ¶ kernel f` (as well as the dual versions)

## Main statements

Besides the definition and lifts,
* `kernel.Î¹_zero_is_iso`: a kernel map of a zero morphism is an isomorphism
* `kernel.is_limit_cone_zero_cone`: if our category has a zero object, then the map from the zero
  obect is a kernel map of any monomorphism

## Future work
* TODO: images and coimages, and then abelian categories.
* TODO: connect this with existing working in the group theory and ring theory libraries.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]
-/

universes v u

open category_theory
open category_theory.limits.walking_parallel_pair

namespace category_theory.limits

variables {C : Type u} [ğ’ : category.{v} C]
include ğ’

variables {X Y : C} (f : X âŸ¶ Y)

section
variables [has_zero_morphisms.{v} C]

/-- A kernel fork is just a fork where the second morphism is a zero morphism. -/
abbreviation kernel_fork := fork f 0

variables {f}

@[simp, reassoc] lemma kernel_fork.condition (s : kernel_fork f) : fork.Î¹ s â‰« f = 0 :=
by erw [fork.condition, has_zero_morphisms.comp_zero]

@[simp] lemma kernel_fork.app_one (s : kernel_fork f) : s.Ï€.app one = 0 :=
by rw [â†fork.app_zero_left, kernel_fork.condition]

/-- A morphism `Î¹` satisfying `Î¹ â‰« f = 0` determines a kernel fork over `f`. -/
abbreviation kernel_fork.of_Î¹ {Z : C} (Î¹ : Z âŸ¶ X) (w : Î¹ â‰« f = 0) : kernel_fork f :=
fork.of_Î¹ Î¹ $ by rw [w, has_zero_morphisms.comp_zero]

/-- If `s` is a limit kernel fork and `k : W âŸ¶ X` satisfies ``k â‰« f = 0`, then there is some
    `l : W âŸ¶ s.X` sich that `l â‰« fork.Î¹ s = k`. -/
def kernel_fork.is_limit.lift' {s : kernel_fork f} (hs : is_limit s) {W : C} (k : W âŸ¶ X)
  (h : k â‰« f = 0) : {l : W âŸ¶ s.X // l â‰« fork.Î¹ s = k} :=
âŸ¨hs.lift $ kernel_fork.of_Î¹ _ h, hs.fac _ _âŸ©

end

section
variables [has_zero_morphisms.{v} C] [has_limit (parallel_pair f 0)]

/-- The kernel of a morphism, expressed as the equalizer with the 0 morphism. -/
abbreviation kernel : C := equalizer f 0

/-- The map from `kernel f` into the source of `f`. -/
abbreviation kernel.Î¹ : kernel f âŸ¶ X := equalizer.Î¹ f 0

@[simp, reassoc] lemma kernel.condition : kernel.Î¹ f â‰« f = 0 :=
kernel_fork.condition _

/-- Given any morphism `k : W âŸ¶ X` satisfying `k â‰« f = 0`, `k` factors through `kernel.Î¹ f`
    via `kernel.lift : W âŸ¶ kernel f`. -/
abbreviation kernel.lift {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : W âŸ¶ kernel f :=
limit.lift (parallel_pair f 0) (kernel_fork.of_Î¹ k h)

@[simp, reassoc]
lemma kernel.lift_Î¹ {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : kernel.lift f k h â‰« kernel.Î¹ f = k :=
limit.lift_Ï€ _ _

/-- Any morphism `k : W âŸ¶ X` satisfying `k â‰« f = 0` induces a morphism `l : W âŸ¶ kernel f` such that
    `l â‰« kernel.Î¹ f = k`. -/
def kernel.lift' {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : {l : W âŸ¶ kernel f // l â‰« kernel.Î¹ f = k} :=
âŸ¨kernel.lift f k h, kernel.lift_Î¹ _ _ _âŸ©

/-- Every kernel of the zero morphism is an isomorphism -/
def kernel.Î¹_zero_is_iso [has_limit (parallel_pair (0 : X âŸ¶ Y) 0)] :
  is_iso (kernel.Î¹ (0 : X âŸ¶ Y)) :=
equalizer.Î¹_of_self _

end

section has_zero_object
variables [has_zero_object.{v} C]

local attribute [instance] has_zero_object.has_zero
variables [has_zero_morphisms.{v} C]

/-- The morphism from the zero object determines a cone on a kernel diagram -/
def kernel.zero_cone : cone (parallel_pair f 0) :=
{ X := 0,
  Ï€ := { app := Î» j, 0 }}

/-- The map from the zero object is a kernel of a monomorphism -/
def kernel.is_limit_cone_zero_cone [mono f] : is_limit (kernel.zero_cone f) :=
fork.is_limit.mk _ (Î» s, 0)
  (Î» s, by { erw has_zero_morphisms.zero_comp,
    convert (@zero_of_comp_mono _ _ _ _ _ _ _ f _ _).symm,
    exact kernel_fork.condition _ })
  (Î» _ _ _, has_zero_object.zero_of_to_zero _)

/-- The kernel of a monomorphism is isomorphic to the zero object -/
def kernel.of_mono [has_limit (parallel_pair f 0)] [mono f] : kernel f â‰… 0 :=
functor.map_iso (cones.forget _) $ is_limit.unique_up_to_iso
  (limit.is_limit (parallel_pair f 0)) (kernel.is_limit_cone_zero_cone f)

/-- The kernel morphism of a monomorphism is a zero morphism -/
lemma kernel.Î¹_of_mono [has_limit (parallel_pair f 0)] [mono f] : kernel.Î¹ f = 0 :=
by rw [â†category.id_comp (kernel.Î¹ f), â†iso.hom_inv_id (kernel.of_mono f), category.assoc,
  has_zero_object.zero_of_to_zero (kernel.of_mono f).hom, has_zero_morphisms.zero_comp]

end has_zero_object

section
variables (X) (Y) [has_zero_morphisms.{v} C]

/-- The kernel morphism of a zero morphism is an isomorphism -/
def kernel.Î¹_of_zero [has_limit (parallel_pair (0 : X âŸ¶ Y) 0)] : is_iso (kernel.Î¹ (0 : X âŸ¶ Y)) :=
equalizer.Î¹_of_self _

end

section
variables [has_zero_morphisms.{v} C]

/-- A cokernel cofork is just a cofork where the second morphism is a zero morphism. -/
abbreviation cokernel_cofork := cofork f 0

variables {f}

@[simp, reassoc] lemma cokernel_cofork.condition (s : cokernel_cofork f) : f â‰« cofork.Ï€ s = 0 :=
by rw [cofork.condition, has_zero_morphisms.zero_comp]

@[simp] lemma cokernel_cofork.app_zero (s : cokernel_cofork f) : s.Î¹.app zero = 0 :=
by rw [â†cofork.left_app_one, cokernel_cofork.condition]

/-- A morphism `Ï€` satisfying `f â‰« Ï€ = 0` determines a cokernel cofork on `f`. -/
abbreviation cokernel_cofork.of_Ï€ {Z : C} (Ï€ : Y âŸ¶ Z) (w : f â‰« Ï€ = 0) : cokernel_cofork f :=
cofork.of_Ï€ Ï€ $ by rw [w, has_zero_morphisms.zero_comp]

/-- If `s` is a colimit cokernel cofork, then every `k : Y âŸ¶ W` satisfying `f â‰« k = 0` induces
    `l : s.X âŸ¶ W` such that `cofork.Ï€ s â‰« l = k`. -/
def cokernel_cofork.is_colimit.desc' {s : cokernel_cofork f} (hs : is_colimit s) {W : C} (k : Y âŸ¶ W)
  (h : f â‰« k = 0) : {l : s.X âŸ¶ W // cofork.Ï€ s â‰« l = k} :=
âŸ¨hs.desc $ cokernel_cofork.of_Ï€ _ h, hs.fac _ _âŸ©

end

section
variables [has_zero_morphisms.{v} C] [has_colimit (parallel_pair f 0)]

/-- The cokernel of a morphism, expressed as the coequalizer with the 0 morphism. -/
abbreviation cokernel : C := coequalizer f 0

/-- The map from the target of `f` to `cokernel f`. -/
abbreviation cokernel.Ï€ : Y âŸ¶ cokernel f := coequalizer.Ï€ f 0

@[simp, reassoc] lemma cokernel.condition : f â‰« cokernel.Ï€ f = 0 :=
cokernel_cofork.condition _

/-- Given any morphism `k : Y âŸ¶ W` such that `f â‰« k = 0`, `k` factors through `cokernel.Ï€ f`
    via `cokernel.desc : cokernel f âŸ¶ W`. -/
abbreviation cokernel.desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) : cokernel f âŸ¶ W :=
colimit.desc (parallel_pair f 0) (cokernel_cofork.of_Ï€ k h)

@[simp, reassoc]
lemma cokernel.Ï€_desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) :
  cokernel.Ï€ f â‰« cokernel.desc f k h = k :=
colimit.Î¹_desc _ _

/-- Any morphism `k : Y âŸ¶ W` satisfying `f â‰« k = 0` induces `l : cokernel f âŸ¶ W` such that
    `cokernel.Ï€ f â‰« l = k`. -/
def cokernel.desc' {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) :
  {l : cokernel f âŸ¶ W // cokernel.Ï€ f â‰« l = k} :=
âŸ¨cokernel.desc f k h, cokernel.Ï€_desc _ _ _âŸ©

end

section has_zero_object
variables [has_zero_object.{v} C]

local attribute [instance] has_zero_object.has_zero

variable [has_zero_morphisms.{v} C]

/-- The morphism to the zero object determines a cocone on a cokernel diagram -/
def cokernel.zero_cocone : cocone (parallel_pair f 0) :=
{ X := 0,
  Î¹ := { app := Î» j, 0 } }

/-- The morphism to the zero object is a cokernel of an epimorphism -/
def cokernel.is_colimit_cocone_zero_cocone [epi f] : is_colimit (cokernel.zero_cocone f) :=
cofork.is_colimit.mk _ (Î» s, 0)
  (Î» s, by { erw has_zero_morphisms.zero_comp,
    convert (@zero_of_comp_epi _ _ _ _ _ _ f _ _ _).symm,
    exact cokernel_cofork.condition _ })
  (Î» _ _ _, has_zero_object.zero_of_from_zero _)

/-- The cokernel of an epimorphism is isomorphic to the zero object -/
def cokernel.of_epi [has_colimit (parallel_pair f 0)] [epi f] : cokernel f â‰… 0 :=
functor.map_iso (cocones.forget _) $ is_colimit.unique_up_to_iso
  (colimit.is_colimit (parallel_pair f 0)) (cokernel.is_colimit_cocone_zero_cocone f)

/-- The cokernel morphism if an epimorphism is a zero morphism -/
lemma cokernel.Ï€_of_epi [has_colimit (parallel_pair f 0)] [epi f] : cokernel.Ï€ f = 0 :=
by rw [â†category.comp_id (cokernel.Ï€ f), â†iso.hom_inv_id (cokernel.of_epi f), â†category.assoc,
  has_zero_object.zero_of_from_zero (cokernel.of_epi f).inv, has_zero_morphisms.comp_zero]

end has_zero_object

section
variables (X) (Y) [has_zero_morphisms.{v} C]

/-- The cokernel of a zero morphism is an isomorphism -/
def cokernel.Ï€_of_zero [has_colimit (parallel_pair (0 : X âŸ¶ Y) 0)] :
  is_iso (cokernel.Ï€ (0 : X âŸ¶ Y)) :=
coequalizer.Ï€_of_self _

end


section has_zero_object
variables [has_zero_object.{v} C]

local attribute [instance] has_zero_object.has_zero
variables [has_zero_morphisms.{v} C]

/-- The kernel of the cokernel of an epimorphism is an isomorphism -/
instance kernel.of_cokernel_of_epi [has_colimit (parallel_pair f 0)]
  [has_limit (parallel_pair (cokernel.Ï€ f) 0)] [epi f] : is_iso (kernel.Î¹ (cokernel.Ï€ f)) :=
equalizer.Î¹_of_eq $ cokernel.Ï€_of_epi f

/-- The cokernel of the kernel of a monomorphism is an isomorphism -/
instance cokernel.of_kernel_of_mono [has_limit (parallel_pair f 0)]
  [has_colimit (parallel_pair (kernel.Î¹ f) 0)] [mono f] : is_iso (cokernel.Ï€ (kernel.Î¹ f)) :=
coequalizer.Ï€_of_eq $ kernel.Î¹_of_mono f

end has_zero_object

end category_theory.limits
namespace category_theory.limits
variables (C : Type u) [ğ’ : category.{v} C]
include ğ’

variables [has_zero_morphisms.{v} C]

/-- `has_kernels` represents a choice of kernel for every morphism -/
class has_kernels :=
(has_limit : Î  {X Y : C} (f : X âŸ¶ Y), has_limit (parallel_pair f 0))

/-- `has_cokernels` represents a choice of cokernel for every morphism -/
class has_cokernels :=
(has_colimit : Î  {X Y : C} (f : X âŸ¶ Y), has_colimit (parallel_pair f 0))

attribute [instance] has_kernels.has_limit has_cokernels.has_colimit

/-- Kernels are finite limits, so if `C` has all finite limits, it also has all kernels -/
def has_kernels_of_has_finite_limits [has_finite_limits.{v} C] : has_kernels.{v} C :=
{ has_limit := infer_instance }

/-- Cokernels are finite limits, so if `C` has all finite colimits, it also has all cokernels -/
def has_cokernels_of_has_finite_colimits [has_finite_colimits.{v} C] : has_cokernels.{v} C :=
{ has_colimit := infer_instance }

end category_theory.limits
