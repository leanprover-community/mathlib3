/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel
-/
import category_theory.limits.shapes.zero

/-!
# Kernels and cokernels

In a category with zero morphisms, the kernel of a morphism `f : X âŸ¶ Y` is
the equalizer of `f` and `0 : X âŸ¶ Y`. (Similarly the cokernel is the coequalizer.)

The basic definitions are
* `kernel : (X âŸ¶ Y) â†’ C`
* `kernel.Î¹ : kernel f âŸ¶ X`
* `kernel.condition : kernel.Î¹ f â‰« f = 0` and
* `kernel.lift (k : W âŸ¶ X) (h : k â‰« f = 0) : W âŸ¶ kernel f` (as well as the dual versions)

## Main statements

Besides the definition and lifts, we prove
* `kernel.Î¹_zero_is_iso`: a kernel map of a zero morphism is an isomorphism
* `kernel.eq_zero_of_epi_kernel`: if `kernel.Î¹ f` is an epimorphism, then `f = 0`
* `kernel.of_mono`: the kernel of a monomorphism is the zero object
* `kernel.lift_mono`: the lift of a monomorphism `k : W âŸ¶ X` such that `k â‰« f = 0`
  is still a monomorphism
* `kernel.is_limit_cone_zero_cone`: if our category has a zero object, then the map from the zero
  obect is a kernel map of any monomorphism
* `kernel.Î¹_of_zero`: `kernel.Î¹ (0 : X âŸ¶ Y)` is an isomorphism

and the corresponding dual statements.

## Future work
* TODO: connect this with existing working in the group theory and ring theory libraries.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]
-/

noncomputable theory

universes v u u'

open category_theory
open category_theory.limits.walking_parallel_pair

namespace category_theory.limits

variables {C : Type u} [category.{v} C]
variables [has_zero_morphisms C]

/-- A morphism `f` has a kernel if the functor `parallel_pair f 0` has a limit. -/
abbreviation has_kernel {X Y : C} (f : X âŸ¶ Y) : Prop := has_limit (parallel_pair f 0)
/-- A morphism `f` has a cokernel if the functor `parallel_pair f 0` has a colimit. -/
abbreviation has_cokernel {X Y : C} (f : X âŸ¶ Y) : Prop := has_colimit (parallel_pair f 0)

variables {X Y : C} (f : X âŸ¶ Y)

section

/-- A kernel fork is just a fork where the second morphism is a zero morphism. -/
abbreviation kernel_fork := fork f 0

variables {f}

@[simp, reassoc] lemma kernel_fork.condition (s : kernel_fork f) : fork.Î¹ s â‰« f = 0 :=
by erw [fork.condition, has_zero_morphisms.comp_zero]

@[simp] lemma kernel_fork.app_one (s : kernel_fork f) : s.Ï€.app one = 0 :=
by rw [â†fork.app_zero_left, kernel_fork.condition]

/-- A morphism `Î¹` satisfying `Î¹ â‰« f = 0` determines a kernel fork over `f`. -/
abbreviation kernel_fork.of_Î¹ {Z : C} (Î¹ : Z âŸ¶ X) (w : Î¹ â‰« f = 0) : kernel_fork f :=
fork.of_Î¹ Î¹ $ by rw [w, has_zero_morphisms.comp_zero]

@[simp] lemma kernel_fork.Î¹_of_Î¹ {X Y P : C} (f : X âŸ¶ Y) (Î¹ : P âŸ¶ X) (w : Î¹ â‰« f = 0) :
  fork.Î¹ (kernel_fork.of_Î¹ Î¹ w) = Î¹ := rfl

section
local attribute [tidy] tactic.case_bash

/-- Every kernel fork `s` is isomorphic (actually, equal) to `fork.of_Î¹ (fork.Î¹ s) _`. -/
def iso_of_Î¹ (s : fork f 0) : s â‰… fork.of_Î¹ (fork.Î¹ s) (fork.condition s) :=
cones.ext (iso.refl _) $ by tidy

/-- If `Î¹ = Î¹'`, then `fork.of_Î¹ Î¹ _` and `fork.of_Î¹ Î¹' _` are isomorphic. -/
def of_Î¹_congr {P : C} {Î¹ Î¹' : P âŸ¶ X} {w : Î¹ â‰« f = 0} (h : Î¹ = Î¹') :
  kernel_fork.of_Î¹ Î¹ w â‰… kernel_fork.of_Î¹ Î¹' (by rw [â†h, w]) :=
cones.ext (iso.refl _) $ by tidy

/-- If `F` is an equivalence, then applying `F` to a diagram indexing a (co)kernel of `f` yields
    the diagram indexing the (co)kernel of `F.map f`. -/
def comp_nat_iso {D : Type u'} [category.{v} D] [has_zero_morphisms D] (F : C â¥¤ D)
  [is_equivalence F] : parallel_pair f 0 â‹™ F â‰… parallel_pair (F.map f) 0 :=
nat_iso.of_components (Î» j, match j with
  | zero := iso.refl _
  | one := iso.refl _
  end) $ by tidy

end

/-- If `s` is a limit kernel fork and `k : W âŸ¶ X` satisfies ``k â‰« f = 0`, then there is some
    `l : W âŸ¶ s.X` such that `l â‰« fork.Î¹ s = k`. -/
def kernel_fork.is_limit.lift' {s : kernel_fork f} (hs : is_limit s) {W : C} (k : W âŸ¶ X)
  (h : k â‰« f = 0) : {l : W âŸ¶ s.X // l â‰« fork.Î¹ s = k} :=
âŸ¨hs.lift $ kernel_fork.of_Î¹ _ h, hs.fac _ _âŸ©

/-- This is a slightly more convenient method to verify that a kernel fork is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def is_limit_aux (t : kernel_fork f)
  (lift : Î  (s : kernel_fork f), s.X âŸ¶ t.X)
  (fac : âˆ€ (s : kernel_fork f), lift s â‰« t.Î¹ = s.Î¹)
  (uniq : âˆ€ (s : kernel_fork f) (m : s.X âŸ¶ t.X) (w : m â‰« t.Î¹ = s.Î¹), m = lift s) :
  is_limit t :=
{ lift := lift,
  fac' := Î» s j, by { cases j, { exact fac s, }, { simp, }, },
  uniq' := Î» s m w, uniq s m (w limits.walking_parallel_pair.zero), }

/--
This is a more convenient formulation to show that a `kernel_fork` constructed using
`kernel_fork.of_Î¹` is a limit cone.
-/
def is_limit.of_Î¹ {W : C} (g : W âŸ¶ X) (eq : g â‰« f = 0)
  (lift : Î  {W' : C} (g' : W' âŸ¶ X) (eq' : g' â‰« f = 0), W' âŸ¶ W)
  (fac : âˆ€ {W' : C} (g' : W' âŸ¶ X) (eq' : g' â‰« f = 0), lift g' eq' â‰« g = g')
  (uniq :
    âˆ€ {W' : C} (g' : W' âŸ¶ X) (eq' : g' â‰« f = 0) (m : W' âŸ¶ W) (w : m â‰« g = g'), m = lift g' eq') :
  is_limit (kernel_fork.of_Î¹ g eq) :=
is_limit_aux _ (Î» s, lift s.Î¹ s.condition) (Î» s, fac s.Î¹ s.condition) (Î» s, uniq s.Î¹ s.condition)

end

section
variables [has_kernel f]

/-- The kernel of a morphism, expressed as the equalizer with the 0 morphism. -/
abbreviation kernel : C := equalizer f 0

/-- The map from `kernel f` into the source of `f`. -/
abbreviation kernel.Î¹ : kernel f âŸ¶ X := equalizer.Î¹ f 0

@[simp] lemma equalizer_as_kernel : equalizer.Î¹ f 0 = kernel.Î¹ f := rfl

@[simp, reassoc] lemma kernel.condition : kernel.Î¹ f â‰« f = 0 :=
kernel_fork.condition _

/-- Given any morphism `k : W âŸ¶ X` satisfying `k â‰« f = 0`, `k` factors through `kernel.Î¹ f`
    via `kernel.lift : W âŸ¶ kernel f`. -/
abbreviation kernel.lift {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : W âŸ¶ kernel f :=
limit.lift (parallel_pair f 0) (kernel_fork.of_Î¹ k h)

@[simp, reassoc]
lemma kernel.lift_Î¹ {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : kernel.lift f k h â‰« kernel.Î¹ f = k :=
limit.lift_Ï€ _ _

@[simp]
lemma kernel.lift_zero {W : C} {h} : kernel.lift f (0 : W âŸ¶ X) h = 0 :=
by { ext, simp, }

instance kernel.lift_mono {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) [mono k] : mono (kernel.lift f k h) :=
âŸ¨Î» Z g g' w,
begin
  replace w := w =â‰« kernel.Î¹ f,
  simp only [category.assoc, kernel.lift_Î¹] at w,
  exact (cancel_mono k).1 w,
endâŸ©

/-- Any morphism `k : W âŸ¶ X` satisfying `k â‰« f = 0` induces a morphism `l : W âŸ¶ kernel f` such that
    `l â‰« kernel.Î¹ f = k`. -/
def kernel.lift' {W : C} (k : W âŸ¶ X) (h : k â‰« f = 0) : {l : W âŸ¶ kernel f // l â‰« kernel.Î¹ f = k} :=
âŸ¨kernel.lift f k h, kernel.lift_Î¹ _ _ _âŸ©

/-- A commuting square induces a morphism of kernels. -/
abbreviation kernel.map {X' Y' : C} (f' : X' âŸ¶ Y') [has_kernel f']
  (p : X âŸ¶ X') (q : Y âŸ¶ Y') (w : f â‰« q = p â‰« f') : kernel f âŸ¶ kernel f' :=
kernel.lift f' (kernel.Î¹ f â‰« p) (by simp [â†w])

/--
Given a commutative diagram
    X --f--> Y --g--> Z
    |        |        |
    |        |        |
    v        v        v
    X' -f'-> Y' -g'-> Z'
with horizontal arrows composing to zero,
then we obtain a commutative square
   X ---> kernel g
   |         |
   |         | kernel.map
   |         |
   v         v
   X' --> kernel g'
-/
lemma kernel.lift_map {X Y Z X' Y' Z' : C}
  (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [has_kernel g] (w : f â‰« g = 0)
  (f' : X' âŸ¶ Y') (g' : Y' âŸ¶ Z') [has_kernel g'] (w' : f' â‰« g' = 0)
  (p : X âŸ¶ X') (q : Y âŸ¶ Y') (r : Z âŸ¶ Z') (hâ‚ : f â‰« q = p â‰« f') (hâ‚‚ : g â‰« r = q â‰« g') :
  kernel.lift g f w â‰« kernel.map g g' q r hâ‚‚ = p â‰« kernel.lift g' f' w' :=
by { ext, simp [hâ‚], }

/-- Every kernel of the zero morphism is an isomorphism -/
instance kernel.Î¹_zero_is_iso : is_iso (kernel.Î¹ (0 : X âŸ¶ Y)) :=
equalizer.Î¹_of_self _

lemma eq_zero_of_epi_kernel [epi (kernel.Î¹ f)] : f = 0 :=
(cancel_epi (kernel.Î¹ f)).1 (by simp)

/-- The kernel of a zero morphism is isomorphic to the source. -/
def kernel_zero_iso_source : kernel (0 : X âŸ¶ Y) â‰… X :=
equalizer.iso_source_of_self 0

@[simp] lemma kernel_zero_iso_source_hom :
  kernel_zero_iso_source.hom = kernel.Î¹ (0 : X âŸ¶ Y) := rfl

@[simp] lemma kernel_zero_iso_source_inv :
  kernel_zero_iso_source.inv = kernel.lift (0 : X âŸ¶ Y) (ğŸ™ X) (by simp) :=
by { ext, simp [kernel_zero_iso_source], }

/-- If two morphisms are known to be equal, then their kernels are isomorphic. -/
def kernel_iso_of_eq {f g : X âŸ¶ Y} [has_kernel f] [has_kernel g] (h : f = g) :
  kernel f â‰… kernel g :=
has_limit.iso_of_nat_iso (by simp[h])

@[simp]
lemma kernel_iso_of_eq_refl {h : f = f} : kernel_iso_of_eq h = iso.refl (kernel f) :=
by { ext, simp [kernel_iso_of_eq], }

@[simp]
lemma kernel_iso_of_eq_trans {f g h : X âŸ¶ Y} [has_kernel f] [has_kernel g] [has_kernel h]
  (wâ‚ : f = g) (wâ‚‚ : g = h) :
  kernel_iso_of_eq wâ‚ â‰ªâ‰« kernel_iso_of_eq wâ‚‚ = kernel_iso_of_eq (wâ‚.trans wâ‚‚) :=
by { unfreezingI { induction wâ‚, induction wâ‚‚, }, ext, simp [kernel_iso_of_eq], }

variables {f}

lemma kernel_not_epi_of_nonzero (w : f â‰  0) : Â¬epi (kernel.Î¹ f) :=
Î» I, by exactI w (eq_zero_of_epi_kernel f)

lemma kernel_not_iso_of_nonzero (w : f â‰  0) : (is_iso (kernel.Î¹ f)) â†’ false :=
Î» I, kernel_not_epi_of_nonzero w $ by { resetI, apply_instance }

-- TODO the remainder of this section has obvious generalizations to `has_equalizer f g`.

instance has_kernel_comp_mono {X Y Z : C} (f : X âŸ¶ Y) [has_kernel f] (g : Y âŸ¶ Z) [mono g] :
  has_kernel (f â‰« g) :=
{ exists_limit :=
  âŸ¨{ cone := kernel_fork.of_Î¹ (kernel.Î¹ f) (by simp),
     is_limit := is_limit_aux _ (Î» s, kernel.lift _ s.Î¹ ((cancel_mono g).mp (by simp)))
      (by tidy) (by tidy) }âŸ© }

/--
When `g` is a monomorphism, the kernel of `f â‰« g` is isomorphic to the kernel of `f`.
-/
@[simps]
def kernel_comp_mono {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [has_kernel f] [mono g] :
  kernel (f â‰« g) â‰… kernel f :=
{ hom := kernel.lift _ (kernel.Î¹ _) (by { rw [â†cancel_mono g], simp, }),
  inv := kernel.lift _ (kernel.Î¹ _) (by simp), }

instance has_kernel_iso_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [is_iso f] [has_kernel g] :
  has_kernel (f â‰« g) :=
{ exists_limit :=
  âŸ¨{ cone := kernel_fork.of_Î¹ (kernel.Î¹ g â‰« inv f) (by simp),
     is_limit := is_limit_aux _ (Î» s, kernel.lift _ (s.Î¹ â‰« f) (by tidy)) (by tidy)
       (Î» s m w, by { simp_rw [â†w], ext, simp, }), }âŸ© }

/--
When `f` is an isomorphism, the kernel of `f â‰« g` is isomorphic to the kernel of `g`.
-/
@[simps]
def kernel_is_iso_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [is_iso f] [has_kernel g] :
  kernel (f â‰« g) â‰… kernel g :=
{ hom := kernel.lift _ (kernel.Î¹ _ â‰« f) (by simp),
  inv := kernel.lift _ (kernel.Î¹ _ â‰« inv f) (by simp), }

end

section has_zero_object
variables [has_zero_object C]

open_locale zero_object

/-- The morphism from the zero object determines a cone on a kernel diagram -/
def kernel.zero_cone : cone (parallel_pair f 0) :=
{ X := 0,
  Ï€ := { app := Î» j, 0 }}

/-- The map from the zero object is a kernel of a monomorphism -/
def kernel.is_limit_cone_zero_cone [mono f] : is_limit (kernel.zero_cone f) :=
fork.is_limit.mk _ (Î» s, 0)
  (Î» s, by { erw zero_comp,
    convert (zero_of_comp_mono f _).symm,
    exact kernel_fork.condition _ })
  (Î» _ _ _, zero_of_to_zero _)

/-- The kernel of a monomorphism is isomorphic to the zero object -/
def kernel.of_mono [has_kernel f] [mono f] : kernel f â‰… 0 :=
functor.map_iso (cones.forget _) $ is_limit.unique_up_to_iso
  (limit.is_limit (parallel_pair f 0)) (kernel.is_limit_cone_zero_cone f)

/-- The kernel morphism of a monomorphism is a zero morphism -/
lemma kernel.Î¹_of_mono [has_kernel f] [mono f] : kernel.Î¹ f = 0 :=
zero_of_source_iso_zero _ (kernel.of_mono f)

end has_zero_object

section transport

/-- If `i` is an isomorphism such that `l â‰« i.hom = f`, then any kernel of `f` is a kernel of `l`.-/
def is_kernel.of_comp_iso {Z : C} (l : X âŸ¶ Z) (i : Z â‰… Y) (h : l â‰« i.hom = f)
  {s : kernel_fork f} (hs : is_limit s) : is_limit (kernel_fork.of_Î¹ (fork.Î¹ s) $
    show fork.Î¹ s â‰« l = 0, by simp [â†i.comp_inv_eq.2 h.symm]) :=
fork.is_limit.mk _
  (Î» s, hs.lift $ kernel_fork.of_Î¹ (fork.Î¹ s) $ by simp [â†h])
  (Î» s, by simp)
  (Î» s m h, by { apply fork.is_limit.hom_ext hs, simpa using h walking_parallel_pair.zero })

/-- If `i` is an isomorphism such that `l â‰« i.hom = f`, then the kernel of `f` is a kernel of `l`.-/
def kernel.of_comp_iso [has_kernel f]
  {Z : C} (l : X âŸ¶ Z) (i : Z â‰… Y) (h : l â‰« i.hom = f) :
  is_limit (kernel_fork.of_Î¹ (kernel.Î¹ f) $
    show kernel.Î¹ f â‰« l = 0, by simp [â†i.comp_inv_eq.2 h.symm]) :=
is_kernel.of_comp_iso f l i h $ limit.is_limit _

/-- If `s` is any limit kernel cone over `f` and if  `i` is an isomorphism such that
    `i.hom â‰« s.Î¹  = l`, then `l` is a kernel of `f`. -/
def is_kernel.iso_kernel {Z : C} (l : Z âŸ¶ X) {s : kernel_fork f} (hs : is_limit s)
  (i : Z â‰… s.X) (h : i.hom â‰« fork.Î¹ s = l) : is_limit (kernel_fork.of_Î¹ l $
    show l â‰« f = 0, by simp [â†h]) :=
is_limit.of_iso_limit hs $ cones.ext i.symm $ Î» j,
  by { cases j, { exact (iso.eq_inv_comp i).2 h }, { simp } }

/-- If `i` is an isomorphism such that `i.hom â‰« kernel.Î¹ f = l`, then `l` is a kernel of `f`. -/
def kernel.iso_kernel [has_kernel f]
  {Z : C} (l : Z âŸ¶ X) (i : Z â‰… kernel f) (h : i.hom â‰« kernel.Î¹ f = l) :
  is_limit (kernel_fork.of_Î¹ l $ by simp [â†h]) :=
is_kernel.iso_kernel f l (limit.is_limit _) i h

end transport

section
variables (X Y)

/-- The kernel morphism of a zero morphism is an isomorphism -/
lemma kernel.Î¹_of_zero : is_iso (kernel.Î¹ (0 : X âŸ¶ Y)) :=
equalizer.Î¹_of_self _

end

section

/-- A cokernel cofork is just a cofork where the second morphism is a zero morphism. -/
abbreviation cokernel_cofork := cofork f 0

variables {f}

@[simp, reassoc] lemma cokernel_cofork.condition (s : cokernel_cofork f) : f â‰« cofork.Ï€ s = 0 :=
by rw [cofork.condition, zero_comp]

@[simp] lemma cokernel_cofork.app_zero (s : cokernel_cofork f) : s.Î¹.app zero = 0 :=
by rw [â†cofork.left_app_one, cokernel_cofork.condition]

/-- A morphism `Ï€` satisfying `f â‰« Ï€ = 0` determines a cokernel cofork on `f`. -/
abbreviation cokernel_cofork.of_Ï€ {Z : C} (Ï€ : Y âŸ¶ Z) (w : f â‰« Ï€ = 0) : cokernel_cofork f :=
cofork.of_Ï€ Ï€ $ by rw [w, zero_comp]

@[simp] lemma cokernel_cofork.Ï€_of_Ï€ {X Y P : C} (f : X âŸ¶ Y) (Ï€ : Y âŸ¶ P) (w : f â‰« Ï€ = 0) :
  cofork.Ï€ (cokernel_cofork.of_Ï€ Ï€ w) = Ï€ := rfl

/-- Every cokernel cofork `s` is isomorphic (actually, equal) to `cofork.of_Ï€ (cofork.Ï€ s) _`. -/
def iso_of_Ï€ (s : cofork f 0) : s â‰… cofork.of_Ï€ (cofork.Ï€ s) (cofork.condition s) :=
cocones.ext (iso.refl _) $ Î» j, by cases j; tidy

/-- If `Ï€ = Ï€'`, then `cokernel_cofork.of_Ï€ Ï€ _` and `cokernel_cofork.of_Ï€ Ï€' _` are isomorphic. -/
def of_Ï€_congr {P : C} {Ï€ Ï€' : Y âŸ¶ P} {w : f â‰« Ï€ = 0} (h : Ï€ = Ï€') :
  cokernel_cofork.of_Ï€ Ï€ w â‰… cokernel_cofork.of_Ï€ Ï€' (by rw [â†h, w]) :=
cocones.ext (iso.refl _) $ Î» j, by cases j; tidy

/-- If `s` is a colimit cokernel cofork, then every `k : Y âŸ¶ W` satisfying `f â‰« k = 0` induces
    `l : s.X âŸ¶ W` such that `cofork.Ï€ s â‰« l = k`. -/
def cokernel_cofork.is_colimit.desc' {s : cokernel_cofork f} (hs : is_colimit s) {W : C} (k : Y âŸ¶ W)
  (h : f â‰« k = 0) : {l : s.X âŸ¶ W // cofork.Ï€ s â‰« l = k} :=
âŸ¨hs.desc $ cokernel_cofork.of_Ï€ _ h, hs.fac _ _âŸ©

/--
This is a slightly more convenient method to verify that a cokernel cofork is a colimit cocone.
It only asks for a proof of facts that carry any mathematical content -/
def is_colimit_aux (t : cokernel_cofork f)
  (desc : Î  (s : cokernel_cofork f), t.X âŸ¶ s.X)
  (fac : âˆ€ (s : cokernel_cofork f), t.Ï€ â‰« desc s = s.Ï€)
  (uniq : âˆ€ (s : cokernel_cofork f) (m : t.X âŸ¶ s.X) (w : t.Ï€ â‰« m = s.Ï€), m = desc s) :
  is_colimit t :=
{ desc := desc,
  fac' := Î» s j, by { cases j, { simp, }, { exact fac s, }, },
  uniq' := Î» s m w, uniq s m (w limits.walking_parallel_pair.one), }

/--
This is a more convenient formulation to show that a `cokernel_cofork` constructed using
`cokernel_cofork.of_Ï€` is a limit cone.
-/
def is_colimit.of_Ï€ {Z : C} (g : Y âŸ¶ Z) (eq : f â‰« g = 0)
  (desc : Î  {Z' : C} (g' : Y âŸ¶ Z') (eq' : f â‰« g' = 0), Z âŸ¶ Z')
  (fac : âˆ€ {Z' : C} (g' : Y âŸ¶ Z') (eq' : f â‰« g' = 0), g â‰« desc g' eq' = g')
  (uniq :
    âˆ€ {Z' : C} (g' : Y âŸ¶ Z') (eq' : f â‰« g' = 0) (m : Z âŸ¶ Z') (w : g â‰« m = g'), m = desc g' eq') :
  is_colimit (cokernel_cofork.of_Ï€ g eq) :=
is_colimit_aux _ (Î» s, desc s.Ï€ s.condition) (Î» s, fac s.Ï€ s.condition) (Î» s, uniq s.Ï€ s.condition)

end

section
variables [has_cokernel f]

/-- The cokernel of a morphism, expressed as the coequalizer with the 0 morphism. -/
abbreviation cokernel : C := coequalizer f 0

/-- The map from the target of `f` to `cokernel f`. -/
abbreviation cokernel.Ï€ : Y âŸ¶ cokernel f := coequalizer.Ï€ f 0

@[simp] lemma coequalizer_as_cokernel : coequalizer.Ï€ f 0 = cokernel.Ï€ f := rfl

@[simp, reassoc] lemma cokernel.condition : f â‰« cokernel.Ï€ f = 0 :=
cokernel_cofork.condition _

/-- Given any morphism `k : Y âŸ¶ W` such that `f â‰« k = 0`, `k` factors through `cokernel.Ï€ f`
    via `cokernel.desc : cokernel f âŸ¶ W`. -/
abbreviation cokernel.desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) : cokernel f âŸ¶ W :=
colimit.desc (parallel_pair f 0) (cokernel_cofork.of_Ï€ k h)

@[simp, reassoc]
lemma cokernel.Ï€_desc {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) :
  cokernel.Ï€ f â‰« cokernel.desc f k h = k :=
colimit.Î¹_desc _ _

@[simp]
lemma cokernel.desc_zero {W : C} {h} : cokernel.desc f (0 : Y âŸ¶ W) h = 0 :=
by { ext, simp, }

instance cokernel.desc_epi
  {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) [epi k] : epi (cokernel.desc f k h) :=
âŸ¨Î» Z g g' w,
begin
  replace w := cokernel.Ï€ f â‰«= w,
  simp only [cokernel.Ï€_desc_assoc] at w,
  exact (cancel_epi k).1 w,
endâŸ©

/-- Any morphism `k : Y âŸ¶ W` satisfying `f â‰« k = 0` induces `l : cokernel f âŸ¶ W` such that
    `cokernel.Ï€ f â‰« l = k`. -/
def cokernel.desc' {W : C} (k : Y âŸ¶ W) (h : f â‰« k = 0) :
  {l : cokernel f âŸ¶ W // cokernel.Ï€ f â‰« l = k} :=
âŸ¨cokernel.desc f k h, cokernel.Ï€_desc _ _ _âŸ©

/-- A commuting square induces a morphism of cokernels. -/
abbreviation cokernel.map {X' Y' : C} (f' : X' âŸ¶ Y') [has_cokernel f']
  (p : X âŸ¶ X') (q : Y âŸ¶ Y') (w : f â‰« q = p â‰« f') : cokernel f âŸ¶ cokernel f' :=
cokernel.desc f (q â‰« cokernel.Ï€ f') (by simp [reassoc_of w])

/--
Given a commutative diagram
    X --f--> Y --g--> Z
    |        |        |
    |        |        |
    v        v        v
    X' -f'-> Y' -g'-> Z'
with horizontal arrows composing to zero,
then we obtain a commutative square
   cokernel f ---> Z
   |               |
   | cokernel.map  |
   |               |
   v               v
   cokernel f' --> Z'
-/
lemma cokernel.map_desc {X Y Z X' Y' Z' : C}
  (f : X âŸ¶ Y) [has_cokernel f] (g : Y âŸ¶ Z) (w : f â‰« g = 0)
  (f' : X' âŸ¶ Y') [has_cokernel f'] (g' : Y' âŸ¶ Z') (w' : f' â‰« g' = 0)
  (p : X âŸ¶ X') (q : Y âŸ¶ Y') (r : Z âŸ¶ Z') (hâ‚ : f â‰« q = p â‰« f') (hâ‚‚ : g â‰« r = q â‰« g') :
  cokernel.map f f' p q hâ‚ â‰« cokernel.desc f' g' w' = cokernel.desc f g w â‰« r :=
by { ext, simp [hâ‚‚], }

/-- The cokernel of the zero morphism is an isomorphism -/
instance cokernel.Ï€_zero_is_iso :
  is_iso (cokernel.Ï€ (0 : X âŸ¶ Y)) :=
coequalizer.Ï€_of_self _

lemma eq_zero_of_mono_cokernel [mono (cokernel.Ï€ f)] : f = 0 :=
(cancel_mono (cokernel.Ï€ f)).1 (by simp)

/-- The cokernel of a zero morphism is isomorphic to the target. -/
def cokernel_zero_iso_target : cokernel (0 : X âŸ¶ Y) â‰… Y :=
coequalizer.iso_target_of_self 0

@[simp] lemma cokernel_zero_iso_target_hom :
  cokernel_zero_iso_target.hom = cokernel.desc (0 : X âŸ¶ Y) (ğŸ™ Y) (by simp) :=
by { ext, simp [cokernel_zero_iso_target], }

@[simp] lemma cokernel_zero_iso_target_inv :
  cokernel_zero_iso_target.inv = cokernel.Ï€ (0 : X âŸ¶ Y) := rfl

/-- If two morphisms are known to be equal, then their cokernels are isomorphic. -/
def cokernel_iso_of_eq {f g : X âŸ¶ Y} [has_cokernel f] [has_cokernel g] (h : f = g) :
  cokernel f â‰… cokernel g :=
has_colimit.iso_of_nat_iso (by simp[h])

@[simp]
lemma cokernel_iso_of_eq_refl {h : f = f} : cokernel_iso_of_eq h = iso.refl (cokernel f) :=
by { ext, simp [cokernel_iso_of_eq], }

@[simp]
lemma cokernel_iso_of_eq_trans {f g h : X âŸ¶ Y} [has_cokernel f] [has_cokernel g] [has_cokernel h]
  (wâ‚ : f = g) (wâ‚‚ : g = h) :
  cokernel_iso_of_eq wâ‚ â‰ªâ‰« cokernel_iso_of_eq wâ‚‚ = cokernel_iso_of_eq (wâ‚.trans wâ‚‚) :=
by { unfreezingI { induction wâ‚, induction wâ‚‚, }, ext, simp [cokernel_iso_of_eq], }

variables {f}

lemma cokernel_not_mono_of_nonzero (w : f â‰  0) : Â¬mono (cokernel.Ï€ f) :=
Î» I, by exactI w (eq_zero_of_mono_cokernel f)

lemma cokernel_not_iso_of_nonzero (w : f â‰  0) : (is_iso (cokernel.Ï€ f)) â†’ false :=
Î» I, cokernel_not_mono_of_nonzero w $ by { resetI, apply_instance }

-- TODO the remainder of this section has obvious generalizations to `has_coequalizer f g`.

instance has_cokernel_comp_iso {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [has_cokernel f] [is_iso g] :
  has_cokernel (f â‰« g) :=
{ exists_colimit :=
  âŸ¨{ cocone := cokernel_cofork.of_Ï€ (inv g â‰« cokernel.Ï€ f) (by simp),
     is_colimit := is_colimit_aux _ (Î» s, cokernel.desc _ (g â‰« s.Ï€)
       (by { rw [â†category.assoc, cokernel_cofork.condition], }))
       (by tidy)
       (Î» s m w, by { simp_rw [â†w], ext, simp, }), }âŸ© }

/--
When `g` is an isomorphism, the cokernel of `f â‰« g` is isomorphic to the cokernel of `f`.
-/
@[simps]
def cokernel_comp_is_iso {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [has_cokernel f] [is_iso g] :
  cokernel (f â‰« g) â‰… cokernel f :=
{ hom := cokernel.desc _ (inv g â‰« cokernel.Ï€ f) (by simp),
  inv := cokernel.desc _ (g â‰« cokernel.Ï€ (f â‰« g)) (by rw [â†category.assoc, cokernel.condition]), }

instance has_cokernel_epi_comp {X Y Z : C} (f : X âŸ¶ Y) [epi f] (g : Y âŸ¶ Z) [has_cokernel g] :
  has_cokernel (f â‰« g) :=
{ exists_colimit :=
  âŸ¨{ cocone := cokernel_cofork.of_Ï€ (cokernel.Ï€ g) (by simp),
     is_colimit := is_colimit_aux _
      (Î» s, cokernel.desc _ s.Ï€ ((cancel_epi f).mp (by { rw â† category.assoc, simp })))
      (by tidy) (by tidy) }âŸ© }

/--
When `f` is an epimorphism, the cokernel of `f â‰« g` is isomorphic to the cokernel of `g`.
-/
@[simps]
def cokernel_epi_comp {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [epi f] [has_cokernel g] :
  cokernel (f â‰« g) â‰… cokernel g :=
{ hom := cokernel.desc _ (cokernel.Ï€ g) (by simp),
  inv := cokernel.desc _ (cokernel.Ï€ (f â‰« g)) (by { rw [â†cancel_epi f, â†category.assoc], simp, }), }

end

section has_zero_object
variables [has_zero_object C]

open_locale zero_object

/-- The morphism to the zero object determines a cocone on a cokernel diagram -/
def cokernel.zero_cocone : cocone (parallel_pair f 0) :=
{ X := 0,
  Î¹ := { app := Î» j, 0 } }

/-- The morphism to the zero object is a cokernel of an epimorphism -/
def cokernel.is_colimit_cocone_zero_cocone [epi f] : is_colimit (cokernel.zero_cocone f) :=
cofork.is_colimit.mk _ (Î» s, 0)
  (Î» s, by { erw zero_comp,
    convert (zero_of_epi_comp f _).symm,
    exact cokernel_cofork.condition _ })
  (Î» _ _ _, zero_of_from_zero _)

/-- The cokernel of an epimorphism is isomorphic to the zero object -/
def cokernel.of_epi [has_cokernel f] [epi f] : cokernel f â‰… 0 :=
functor.map_iso (cocones.forget _) $ is_colimit.unique_up_to_iso
  (colimit.is_colimit (parallel_pair f 0)) (cokernel.is_colimit_cocone_zero_cocone f)

/-- The cokernel morphism of an epimorphism is a zero morphism -/
lemma cokernel.Ï€_of_epi [has_cokernel f] [epi f] : cokernel.Ï€ f = 0 :=
zero_of_target_iso_zero _ (cokernel.of_epi f)

end has_zero_object

section has_image

/--
The cokernel of the image inclusion of a morphism `f` is isomorphic to the cokernel of `f`.

(This result requires that the factorisation through the image is an epimorphism.
This holds in any category with equalizers.)
-/
@[simps]
def cokernel_image_Î¹ {X Y : C} (f : X âŸ¶ Y)
  [has_image f] [has_cokernel (image.Î¹ f)] [has_cokernel f] [epi (factor_thru_image f)] :
  cokernel (image.Î¹ f) â‰… cokernel f :=
{ hom := cokernel.desc _ (cokernel.Ï€ f)
  begin
    have w := cokernel.condition f,
    conv at w { to_lhs, congr, rw â†image.fac f, },
    rw [â†has_zero_morphisms.comp_zero (limits.factor_thru_image f), category.assoc, cancel_epi]
      at w,
    exact w,
  end,
  inv := cokernel.desc _ (cokernel.Ï€ _)
  begin
    conv { to_lhs, congr, rw â†image.fac f, },
    rw [category.assoc, cokernel.condition, has_zero_morphisms.comp_zero],
  end, }

end has_image

section
variables (X Y)

/-- The cokernel of a zero morphism is an isomorphism -/
lemma cokernel.Ï€_of_zero :
  is_iso (cokernel.Ï€ (0 : X âŸ¶ Y)) :=
coequalizer.Ï€_of_self _

end


section has_zero_object
variables [has_zero_object C]

open_locale zero_object

/-- The kernel of the cokernel of an epimorphism is an isomorphism -/
instance kernel.of_cokernel_of_epi [has_cokernel f]
  [has_kernel (cokernel.Ï€ f)] [epi f] : is_iso (kernel.Î¹ (cokernel.Ï€ f)) :=
equalizer.Î¹_of_eq $ cokernel.Ï€_of_epi f

/-- The cokernel of the kernel of a monomorphism is an isomorphism -/
instance cokernel.of_kernel_of_mono [has_kernel f]
  [has_cokernel (kernel.Î¹ f)] [mono f] : is_iso (cokernel.Ï€ (kernel.Î¹ f)) :=
coequalizer.Ï€_of_eq $ kernel.Î¹_of_mono f

end has_zero_object

section transport

/-- If `i` is an isomorphism such that `i.hom â‰« l = f`, then any cokernel of `f` is a cokernel of
    `l`. -/
def is_cokernel.of_iso_comp {Z : C} (l : Z âŸ¶ Y) (i : X â‰… Z) (h : i.hom â‰« l = f)
  {s : cokernel_cofork f} (hs : is_colimit s) : is_colimit (cokernel_cofork.of_Ï€ (cofork.Ï€ s) $
    show l â‰« cofork.Ï€ s = 0, by simp [i.eq_inv_comp.2 h]) :=
cofork.is_colimit.mk _
  (Î» s, hs.desc $ cokernel_cofork.of_Ï€ (cofork.Ï€ s) $ by simp [â†h])
  (Î» s, by simp)
  (Î» s m h, by { apply cofork.is_colimit.hom_ext hs, simpa using h walking_parallel_pair.one })

/-- If `i` is an isomorphism such that `i.hom â‰« l = f`, then the cokernel of `f` is a cokernel of
    `l`. -/
def cokernel.of_iso_comp [has_cokernel f]
  {Z : C} (l : Z âŸ¶ Y) (i : X â‰… Z) (h : i.hom â‰« l = f) :
  is_colimit (cokernel_cofork.of_Ï€ (cokernel.Ï€ f) $
    show l â‰« cokernel.Ï€ f = 0, by simp [i.eq_inv_comp.2 h]) :=
is_cokernel.of_iso_comp f l i h $ colimit.is_colimit _

/-- If `s` is any colimit cokernel cocone over `f` and `i` is an isomorphism such that
    `s.Ï€ â‰« i.hom = l`, then `l` is a cokernel of `f`. -/
def is_cokernel.cokernel_iso {Z : C} (l : Y âŸ¶ Z) {s : cokernel_cofork f} (hs : is_colimit s)
  (i : s.X â‰… Z) (h : cofork.Ï€ s â‰« i.hom = l) : is_colimit (cokernel_cofork.of_Ï€ l $
    show f â‰« l = 0, by simp [â†h]) :=
is_colimit.of_iso_colimit hs $ cocones.ext i $ Î» j, by { cases j, { simp }, { exact h } }

/-- If `i` is an isomorphism such that `cokernel.Ï€ f â‰« i.hom = l`, then `l` is a cokernel of `f`. -/
def cokernel.cokernel_iso [has_cokernel f]
  {Z : C} (l : Y âŸ¶ Z) (i : cokernel f â‰… Z) (h : cokernel.Ï€ f â‰« i.hom = l) :
  is_colimit (cokernel_cofork.of_Ï€ l $ by simp [â†h]) :=
is_cokernel.cokernel_iso f l (colimit.is_colimit _) i h

end transport

end category_theory.limits

namespace category_theory.limits
variables (C : Type u) [category.{v} C]

variables [has_zero_morphisms C]

/-- `has_kernels` represents the existence of kernels for every morphism. -/
class has_kernels : Prop :=
(has_limit : Î  {X Y : C} (f : X âŸ¶ Y), has_kernel f . tactic.apply_instance)

/-- `has_cokernels` represents the existence of cokernels for every morphism. -/
class has_cokernels : Prop :=
(has_colimit : Î  {X Y : C} (f : X âŸ¶ Y), has_cokernel f . tactic.apply_instance)

attribute [instance, priority 100] has_kernels.has_limit has_cokernels.has_colimit

@[priority 100]
instance has_kernels_of_has_equalizers [has_equalizers C] : has_kernels C :=
{}

@[priority 100]
instance has_cokernels_of_has_coequalizers [has_coequalizers C] : has_cokernels C :=
{}

end category_theory.limits
