/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import category_theory.limits.has_limits
import category_theory.limits.shapes.products
import category_theory.limits.shapes.equalizers

/-!

# Multi-(co)equalizers

A *multiequalizer* is an equalizer of two morphisms between two products.
Since both products and equalizers are limits, such an object is again a limit.
This file provides the diagram whose limit is indeed such an object.
In fact, it is well-known that any limit can be obtained as a multiequalizer.
The dual construction (multicoequalizers) is also provided.

## Projects

Prove that a multiequalizer can be identified with
an equalizer between products (and analogously for multicoequalizers).

Prove that the limit of any diagram is a multiequalizer (and similarly for colimits).

-/

namespace category_theory.limits

open category_theory

universes v u

/-- The type underlying the multiequalizer diagram. -/
@[nolint unused_arguments]
inductive walking_multicospan {L R : Type v} (fst snd : R â†’ L) : Type v
| left : L â†’ walking_multicospan
| right : R â†’ walking_multicospan

/-- The type underlying the multiecoqualizer diagram. -/
@[nolint unused_arguments]
inductive walking_multispan {L R : Type v} (fst snd : L â†’ R) : Type v
| left : L â†’ walking_multispan
| right : R â†’ walking_multispan

namespace walking_multicospan

variables {L R : Type v} {fst snd : R â†’ L}

instance [inhabited L] : inhabited (walking_multicospan fst snd) :=
âŸ¨left (default _)âŸ©

/-- Morphisms for `walking_multicospan`. -/
inductive hom : Î  (a b : walking_multicospan fst snd), Type v
| id (A)  : hom A A
| fst (b) : hom (left (fst b)) (right b)
| snd (b) : hom (left (snd b)) (right b)

instance {a : walking_multicospan fst snd} : inhabited (hom a a) :=
âŸ¨hom.id _âŸ©

/-- Composition of morphisms for `walking_multicospan`. -/
def hom.comp : Î  {A B C : walking_multicospan fst snd} (f : hom A B) (g : hom B C),
  hom A C
| _ _ _ (hom.id X) f := f
| _ _ _ (hom.fst b) (hom.id X) := hom.fst b
| _ _ _ (hom.snd b) (hom.id X) := hom.snd b

instance : small_category (walking_multicospan fst snd) :=
{ hom := hom,
  id := hom.id,
  comp := Î» X Y Z, hom.comp,
  id_comp' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  comp_id' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  assoc' := by { rintro (_|_) (_|_) (_|_) (_|_) (_|_|_) (_|_|_) (_|_|_), tidy } }

end walking_multicospan

namespace walking_multispan

variables {L R : Type v} {fst snd : L â†’ R}

instance [inhabited L] : inhabited (walking_multispan fst snd) :=
âŸ¨left (default _)âŸ©

/-- Morphisms for `walking_multispan`. -/
inductive hom : Î  (a b : walking_multispan fst snd), Type v
| id (A)  : hom A A
| fst (a) : hom (left a) (right (fst a))
| snd (a) : hom (left a) (right (snd a))

instance {a : walking_multispan fst snd} : inhabited (hom a a) :=
âŸ¨hom.id _âŸ©

/-- Composition of morphisms for `walking_multispan`. -/
def hom.comp : Î  {A B C : walking_multispan fst snd} (f : hom A B) (g : hom B C),
  hom A C
| _ _ _ (hom.id X) f := f
| _ _ _ (hom.fst a) (hom.id X) := hom.fst a
| _ _ _ (hom.snd a) (hom.id X) := hom.snd a

instance : small_category (walking_multispan fst snd) :=
{ hom := hom,
  id := hom.id,
  comp := Î» X Y Z, hom.comp,
  id_comp' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  comp_id' := by { rintro (_|_) (_|_) (_|_|_), tidy },
  assoc' := by { rintro (_|_) (_|_) (_|_) (_|_) (_|_|_) (_|_|_) (_|_|_), tidy } }

end walking_multispan

/-- This is a structure encapsulating the data necessary to define a `multicospan`. -/
@[nolint has_inhabited_instance]
structure multicospan_index (C : Type u) [category.{v} C] :=
(L R : Type v)
(fst_to snd_to : R â†’ L)
(left : L â†’ C)
(right : R â†’ C)
(fst : Î  b, left (fst_to b) âŸ¶ right b)
(snd : Î  b, left (snd_to b) âŸ¶ right b)

/-- This is a structure encapsulating the data necessary to define a `multispan`. -/
@[nolint has_inhabited_instance]
structure multispan_index (C : Type u) [category.{v} C] :=
(L R : Type v)
(fst_from snd_from : L â†’ R)
(left : L â†’ C)
(right : R â†’ C)
(fst : Î  a, left a âŸ¶ right (fst_from a))
(snd : Î  a, left a âŸ¶ right (snd_from a))

namespace multicospan_index

variables {C : Type u} [category.{v} C] (I : multicospan_index C)

/-- The multicospan associated to `I : multicospan_index`. -/
def multicospan : walking_multicospan I.fst_to I.snd_to â¥¤ C :=
{ obj := Î» x,
  match x with
  | walking_multicospan.left a := I.left a
  | walking_multicospan.right b := I.right b
  end,
  map := Î» x y f,
  match x, y, f with
  | _, _, walking_multicospan.hom.id x := ğŸ™ _
  | _, _, walking_multicospan.hom.fst b := I.fst _
  | _, _, walking_multicospan.hom.snd b := I.snd _
  end,
  map_id' := by { rintros (_|_), tidy },
  map_comp' := by { rintros (_|_) (_|_) (_|_) (_|_|_) (_|_|_), tidy } }

@[simp] lemma multicospan_obj_left (a) :
  I.multicospan.obj (walking_multicospan.left a) = I.left a := rfl

@[simp] lemma multicospan_obj_right (b) :
  I.multicospan.obj (walking_multicospan.right b) = I.right b := rfl

@[simp] lemma multicospan_map_fst (b) :
  I.multicospan.map (walking_multicospan.hom.fst b) = I.fst b := rfl

@[simp] lemma multicospan_map_snd (b) :
  I.multicospan.map (walking_multicospan.hom.snd b) = I.snd b := rfl

variables [has_product I.left] [has_product I.right]

noncomputable
def fst_pi_map : âˆ I.left âŸ¶ âˆ I.right := pi.lift (Î» b, pi.Ï€ I.left (I.fst_to b) â‰« I.fst b)

noncomputable
def snd_pi_map : âˆ I.left âŸ¶ âˆ I.right := pi.lift (Î» b, pi.Ï€ I.left (I.snd_to b) â‰« I.snd b)

@[simp, reassoc]
lemma fst_product_map_Ï€ (b) : I.fst_pi_map â‰« pi.Ï€ I.right b = pi.Ï€ I.left _ â‰« I.fst b :=
by simp [fst_pi_map]

@[simp, reassoc]
lemma snd_product_map_Ï€ (b) : I.snd_pi_map â‰« pi.Ï€ I.right b = pi.Ï€ I.left _ â‰« I.snd b :=
by simp [snd_pi_map]

@[simps] protected noncomputable
def parallel_pair := parallel_pair I.fst_pi_map I.snd_pi_map

end multicospan_index

namespace multispan_index

variables {C : Type u} [category.{v} C] (I : multispan_index C)

/-- The multispan associated to `I : multispan_index`. -/
def multispan : walking_multispan I.fst_from I.snd_from â¥¤ C :=
{ obj := Î» x,
  match x with
  | walking_multispan.left a := I.left a
  | walking_multispan.right b := I.right b
  end,
  map := Î» x y f,
  match x, y, f with
  | _, _, walking_multispan.hom.id x := ğŸ™ _
  | _, _, walking_multispan.hom.fst b := I.fst _
  | _, _, walking_multispan.hom.snd b := I.snd _
  end,
  map_id' := by { rintros (_|_), tidy },
  map_comp' := by { rintros (_|_) (_|_) (_|_) (_|_|_) (_|_|_), tidy } }

@[simp] lemma multispan_obj_left (a) :
  I.multispan.obj (walking_multispan.left a) = I.left a := rfl

@[simp] lemma multispan_obj_right (b) :
  I.multispan.obj (walking_multispan.right b) = I.right b := rfl

@[simp] lemma multispan_map_fst (a) :
  I.multispan.map (walking_multispan.hom.fst a) = I.fst a := rfl

@[simp] lemma multispan_map_snd (a) :
  I.multispan.map (walking_multispan.hom.snd a) = I.snd a := rfl

end multispan_index

variables {C : Type u} [category.{v} C]

/-- A multifork is a cone over a multicospan. -/
@[nolint has_inhabited_instance]
abbreviation multifork (I : multicospan_index C) := cone I.multicospan

/-- A multicofork is a cocone over a multispan. -/
@[nolint has_inhabited_instance]
def multicofork (I : multispan_index C) := cocone I.multispan

namespace multifork

variables {I : multicospan_index C} (K : multifork I)

/-- The maps from the cone point of a multifork to the objects on the left. -/
def Î¹ (a : I.L) : K.X âŸ¶ I.left a :=
K.Ï€.app (walking_multicospan.left _)

@[simp] lemma Î¹_eq_app_left (a) : K.Î¹ a = K.Ï€.app (walking_multicospan.left _) := rfl

@[simp] lemma app_left_fst (b) :
  K.Ï€.app (walking_multicospan.left (I.fst_to b)) â‰« I.fst b =
    K.Ï€.app (walking_multicospan.right b) :=
by { rw â† K.w (walking_multicospan.hom.fst b), refl }

@[simp] lemma app_left_snd (b) :
  K.Ï€.app (walking_multicospan.left (I.snd_to b)) â‰« I.snd b =
    K.Ï€.app (walking_multicospan.right b) :=
by { rw â† K.w (walking_multicospan.hom.snd b), refl }

/-- Construct a multifork using a collection `Î¹` of morphisms. -/
@[simps]
def of_Î¹ (I : multicospan_index C) (P : C) (Î¹ : Î  a, P âŸ¶ I.left a)
  (w : âˆ€ b, Î¹ (I.fst_to b) â‰« I.fst b = Î¹ (I.snd_to b) â‰« I.snd b) :
  multifork I :=
{ X := P,
  Ï€ :=
  { app := Î» x,
    match x with
    | walking_multicospan.left a := Î¹ _
    | walking_multicospan.right b := Î¹ (I.fst_to b) â‰« I.fst b
    end,
    naturality' := begin
      rintros (_|_) (_|_) (_|_|_),
      any_goals { symmetry, dsimp, rw category.id_comp, apply category.comp_id },
      { dsimp, rw category.id_comp, refl },
      { dsimp, rw category.id_comp, apply w }
    end } }

@[reassoc]
lemma condition (b) :
  K.Î¹ (I.fst_to b) â‰« I.fst b = K.Î¹ (I.snd_to b) â‰« I.snd b := by simp

variables [has_product I.left] [has_product I.right]

@[simp, reassoc]
lemma pi_condition :
  pi.lift K.Î¹ â‰« I.fst_pi_map = pi.lift K.Î¹ â‰« I.snd_pi_map := by { ext, simp }

@[simps X] noncomputable
def to_pi_fork (K : multifork I) : fork I.fst_pi_map I.snd_pi_map :=
{ X := K.X,
  Ï€ :=
  { app := Î» x,
    match x with
    | walking_parallel_pair.zero := pi.lift K.Î¹
    | walking_parallel_pair.one := pi.lift K.Î¹ â‰« I.fst_pi_map
    end,
    naturality' :=
    begin
      rintros (_|_) (_|_) (_|_|_),
      any_goals { symmetry, dsimp, rw category.id_comp, apply category.comp_id },
      all_goals { change ğŸ™ _ â‰« _ â‰« _ = pi.lift _ â‰« _, simp }
    end } }

section end

@[simp] lemma to_pi_fork_Ï€_app_zero :
  K.to_pi_fork.Ï€.app walking_parallel_pair.zero = pi.lift K.Î¹ := rfl

@[simp] lemma to_pi_fork_Ï€_app_one :
  K.to_pi_fork.Ï€.app walking_parallel_pair.one = pi.lift K.Î¹ â‰« I.fst_pi_map := rfl

variable (I)

@[simps X] noncomputable
def of_pi_fork (c : fork I.fst_pi_map I.snd_pi_map) : multifork I :=
{ X := c.X,
  Ï€ :=
  { app := Î» x,
    match x with
    | walking_multicospan.left a := c.Î¹ â‰« pi.Ï€ _ _
    | walking_multicospan.right b := c.Î¹ â‰« I.fst_pi_map â‰« pi.Ï€ _ _
    end,
    naturality' :=
    begin
      rintros (_|_) (_|_) (_|_|_),
      any_goals { symmetry, dsimp, rw category.id_comp, apply category.comp_id },
      { change ğŸ™ _ â‰« _ â‰« _ = (_ â‰« _) â‰« _, simp },
      { change ğŸ™ _ â‰« _ â‰« _ = (_ â‰« _) â‰« _, rw c.condition_assoc, simp }
    end } }

@[simp] lemma of_pi_fork_Ï€_app_left (c : fork I.fst_pi_map I.snd_pi_map) (a) :
  (of_pi_fork I c).Ï€.app (walking_multicospan.left a) = c.Î¹ â‰« pi.Ï€ _ _ := rfl

@[simp] lemma of_pi_fork_Ï€_app_right (c : fork I.fst_pi_map I.snd_pi_map) (a) :
  (of_pi_fork I c).Ï€.app (walking_multicospan.right a) = c.Î¹ â‰« I.fst_pi_map â‰« pi.Ï€ _ _ := rfl


end multifork

namespace multicospan_index

variables (I : multicospan_index C) [has_product I.left] [has_product I.right]

local attribute [tidy] tactic.case_bash

@[simps] noncomputable
def to_pi_fork_functor : multifork I â¥¤ fork I.fst_pi_map I.snd_pi_map :=
{ obj := multifork.to_pi_fork, map := Î» Kâ‚ Kâ‚‚ f, { hom := f.hom } }

@[simps] noncomputable
def of_pi_fork_functor : fork I.fst_pi_map I.snd_pi_map â¥¤ multifork I :=
{ obj := multifork.of_pi_fork I, map := Î» Kâ‚ Kâ‚‚ f, { hom := f.hom, w' := by rintros (_|_); simp } }

@[simps] noncomputable
def multifork_equiv_pi_fork : multifork I â‰Œ fork I.fst_pi_map I.snd_pi_map :=
{ functor := to_pi_fork_functor I,
  inverse := of_pi_fork_functor I,
  unit_iso := nat_iso.of_components (Î» K, cones.ext (iso.refl _) (by rintros (_|_); dsimp; simp))
    (Î» Kâ‚ Kâ‚‚ f, by { ext, simp }),
  counit_iso := nat_iso.of_components (Î» K, fork.ext (iso.refl _) (by { ext, dsimp, simp }))
    (Î» Kâ‚ Kâ‚‚ f, by { ext, simp }) }

end multicospan_index

namespace multicofork

variables {I : multispan_index C} (K : multicofork I)

/-- The maps to the cocone point of a multicofork from the objects on the right. -/
def Ï€ (b : I.R) : I.right b âŸ¶ K.X :=
K.Î¹.app (walking_multispan.right _)

@[simp] lemma Ï€_eq_app_right (b) : K.Ï€ b = K.Î¹.app (walking_multispan.right _) := rfl

@[simp] lemma fst_app_right (a) :
  I.fst a â‰« K.Î¹.app (walking_multispan.right (I.fst_from a)) =
    K.Î¹.app (walking_multispan.left a) :=
by { rw â† K.w (walking_multispan.hom.fst a), refl }

@[simp] lemma snd_app_right (a) :
  I.snd a â‰« K.Î¹.app (walking_multispan.right (I.snd_from a)) =
    K.Î¹.app (walking_multispan.left a) :=
by { rw â† K.w (walking_multispan.hom.snd a), refl }

/-- Construct a multicofork using a collection `Ï€` of morphisms. -/
@[simps]
def of_Ï€ (I : multispan_index C) (P : C) (Ï€ : Î  b, I.right b âŸ¶ P)
  (w : âˆ€ a, I.fst a â‰« Ï€ (I.fst_from a) = I.snd a â‰« Ï€ (I.snd_from a)) :
  multicofork I :=
{ X := P,
  Î¹ :=
  { app := Î» x,
    match x with
    | walking_multispan.left a := I.fst a â‰« Ï€ _
    | walking_multispan.right b := Ï€ _
    end,
    naturality' := begin
      rintros (_|_) (_|_) (_|_|_),
      any_goals { dsimp, rw category.comp_id, apply category.id_comp },
      { dsimp, rw category.comp_id, refl },
      { dsimp, rw category.comp_id, apply (w _).symm }
    end } }

@[reassoc]
lemma condition (a) :
  I.fst a â‰« K.Ï€ (I.fst_from a) = I.snd a â‰« K.Ï€ (I.snd_from a) := by simp

end multicofork

/-- For `I : multicospan_index C`, we say that it has a multiequalizer if the associated
  multicospan has a limit. -/
abbreviation has_multiequalizer (I : multicospan_index C) :=
  has_limit I.multicospan

noncomputable theory

/-- The multiequalizer of `I : multicospan_index C`. -/
abbreviation multiequalizer (I : multicospan_index C) [has_multiequalizer I] : C :=
  limit I.multicospan

/-- For `I : multispan_index C`, we say that it has a multicoequalizer if
  the associated multicospan has a limit. -/
abbreviation has_multicoequalizer (I : multispan_index C) :=
  has_colimit I.multispan

/-- The multiecoqualizer of `I : multispan_index C`. -/
abbreviation multicoequalizer (I : multispan_index C) [has_multicoequalizer I] : C :=
  colimit I.multispan

namespace multiequalizer

variables (I : multicospan_index C) [has_multiequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbreviation Î¹ (a : I.L) : multiequalizer I âŸ¶ I.left a :=
limit.Ï€ _ (walking_multicospan.left a)

/-- The multifork associated to the multiequalizer. -/
abbreviation multifork : multifork I :=
limit.cone _

@[simp]
lemma multifork_Î¹ (a) :
  (multiequalizer.multifork I).Î¹ a = multiequalizer.Î¹ I a := rfl

@[simp]
lemma multifork_Ï€_app_left (a) :
  (multiequalizer.multifork I).Ï€.app (walking_multicospan.left a) =
  multiequalizer.Î¹ I a := rfl

@[reassoc]
lemma condition (b) :
  multiequalizer.Î¹ I (I.fst_to b) â‰« I.fst b =
  multiequalizer.Î¹ I (I.snd_to b) â‰« I.snd b :=
multifork.condition _ _

/-- Construct a morphism to the multiequalizer from its universal property. -/
abbreviation lift (W : C) (k : Î  a, W âŸ¶ I.left a)
  (h : âˆ€ b, k (I.fst_to b) â‰« I.fst b = k (I.snd_to b) â‰« I.snd b) :
  W âŸ¶ multiequalizer I :=
limit.lift _ (multifork.of_Î¹ I _ k h)

@[simp, reassoc]
lemma lift_Î¹ (W : C) (k : Î  a, W âŸ¶ I.left a)
  (h : âˆ€ b, k (I.fst_to b) â‰« I.fst b = k (I.snd_to b) â‰« I.snd b) (a) :
  multiequalizer.lift I _ k h â‰« multiequalizer.Î¹ I a = k _ :=
limit.lift_Ï€ _ _

@[ext]
lemma hom_ext {W : C} (i j : W âŸ¶ multiequalizer I)
  (h : âˆ€ a, i â‰« multiequalizer.Î¹ I a =
  j â‰« multiequalizer.Î¹ I a) :
  i = j :=
limit.hom_ext
begin
  rintro (a|b),
  { apply h },
  simp_rw [â† limit.w I.multicospan (walking_multicospan.hom.fst b),
    â† category.assoc, h],
end

variables [has_product I.left] [has_product I.right] [has_equalizer I.fst_pi_map I.snd_pi_map]

def iso_equalizer : multiequalizer I â‰… equalizer I.fst_pi_map I.snd_pi_map :=
has_limit.iso_of_equivalence I.multifork_equiv_pi_fork _

end multiequalizer

namespace multicoequalizer

variables (I : multispan_index C) [has_multicoequalizer I]

/-- The canonical map from the multiequalizer to the objects on the left. -/
abbreviation Ï€ (b : I.R) : I.right b âŸ¶ multicoequalizer I :=
colimit.Î¹ I.multispan (walking_multispan.right _)

/-- The multicofork associated to the multicoequalizer. -/
abbreviation multicofork : multicofork I :=
colimit.cocone _

@[simp]
lemma multicofork_Ï€ (b) :
  (multicoequalizer.multicofork I).Ï€ b = multicoequalizer.Ï€ I b := rfl

@[simp]
lemma multicofork_Î¹_app_right (b) :
  (multicoequalizer.multicofork I).Î¹.app (walking_multispan.right b) =
  multicoequalizer.Ï€ I b := rfl

@[reassoc]
lemma condition (a) :
  I.fst a â‰« multicoequalizer.Ï€ I (I.fst_from a) =
  I.snd a â‰« multicoequalizer.Ï€ I (I.snd_from a) :=
multicofork.condition _ _

/-- Construct a morphism from the multicoequalizer from its universal property. -/
abbreviation desc (W : C) (k : Î  b, I.right b âŸ¶ W)
  (h : âˆ€ a, I.fst a â‰«  k (I.fst_from a) = I.snd a â‰« k (I.snd_from a)) :
  multicoequalizer I âŸ¶ W :=
colimit.desc _ (multicofork.of_Ï€ I _ k h)

@[simp, reassoc]
lemma Ï€_desc (W : C) (k : Î  b, I.right b âŸ¶ W)
  (h : âˆ€ a, I.fst a â‰«  k (I.fst_from a) = I.snd a â‰« k (I.snd_from a)) (b) :
  multicoequalizer.Ï€ I b â‰« multicoequalizer.desc I _ k h = k _ :=
colimit.Î¹_desc _ _

@[ext]
lemma hom_ext {W : C} (i j : multicoequalizer I âŸ¶ W)
  (h : âˆ€ b, multicoequalizer.Ï€ I b â‰« i = multicoequalizer.Ï€ I b â‰« j) :
  i = j :=
colimit.hom_ext
begin
  rintro (a|b),
  { simp_rw [â† colimit.w I.multispan (walking_multispan.hom.fst a),
    category.assoc, h] },
  { apply h },
end

end multicoequalizer

end category_theory.limits
