/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import category_theory.limits.shapes.normal_mono.basic
import category_theory.limits.shapes.finite_products

/-!
# Normal mono categories with finite products and kernels have all equalizers.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This, and the dual result, are used in the development of abelian categories.
-/

noncomputable theory

open category_theory
open category_theory.limits

variables {C : Type*} [category C] [has_zero_morphisms C]

namespace category_theory.normal_mono_category

variables [has_finite_products C] [has_kernels C] [normal_mono_category C]

/-- The pullback of two monomorphisms exists. -/
@[irreducible]
lemma pullback_of_mono {X Y Z : C} (a : X âŸ¶ Z) (b : Y âŸ¶ Z) [mono a] [mono b] :
  has_limit (cospan a b) :=
let âŸ¨P, f, haf, iâŸ© := normal_mono_of_mono a in
let âŸ¨Q, g, hbg, i'âŸ© := normal_mono_of_mono b in
let âŸ¨a', ha'âŸ© := kernel_fork.is_limit.lift' i (kernel.Î¹ (prod.lift f g)) $
    calc kernel.Î¹ (prod.lift f g) â‰« f
        = kernel.Î¹ (prod.lift f g) â‰« prod.lift f g â‰« limits.prod.fst : by rw prod.lift_fst
    ... = (0 : kernel (prod.lift f g) âŸ¶ P â¨¯ Q) â‰« limits.prod.fst : by rw kernel.condition_assoc
    ... = 0 : zero_comp in
let âŸ¨b', hb'âŸ© := kernel_fork.is_limit.lift' i' (kernel.Î¹ (prod.lift f g)) $
    calc kernel.Î¹ (prod.lift f g) â‰« g
        = kernel.Î¹ (prod.lift f g) â‰« (prod.lift f g) â‰« limits.prod.snd : by rw prod.lift_snd
    ... = (0 : kernel (prod.lift f g) âŸ¶ P â¨¯ Q) â‰« limits.prod.snd : by rw kernel.condition_assoc
    ... = 0 : zero_comp in
has_limit.mk { cone := pullback_cone.mk a' b' $ by { simp at ha' hb', rw [ha', hb'] },
  is_limit := pullback_cone.is_limit.mk _
    (Î» s, kernel.lift (prod.lift f g) (pullback_cone.snd s â‰« b) $ prod.hom_ext
      (calc ((pullback_cone.snd s â‰« b) â‰« prod.lift f g) â‰« limits.prod.fst
            = pullback_cone.snd s â‰« b â‰« f : by simp only [prod.lift_fst, category.assoc]
        ... = pullback_cone.fst s â‰« a â‰« f : by rw pullback_cone.condition_assoc
        ... = pullback_cone.fst s â‰« 0 : by rw haf
        ... = 0 â‰« limits.prod.fst :
          by rw [comp_zero, zero_comp])
      (calc ((pullback_cone.snd s â‰« b) â‰« prod.lift f g) â‰« limits.prod.snd
            = pullback_cone.snd s â‰« b â‰« g : by simp only [prod.lift_snd, category.assoc]
        ... = pullback_cone.snd s â‰« 0 : by rw hbg
        ... = 0 â‰« limits.prod.snd :
          by rw [comp_zero, zero_comp]))
    (Î» s, (cancel_mono a).1 $
      by { rw kernel_fork.Î¹_of_Î¹ at ha', simp [ha', pullback_cone.condition s] })
    (Î» s, (cancel_mono b).1 $
      by { rw kernel_fork.Î¹_of_Î¹ at hb', simp [hb'] })
    (Î» s m hâ‚ hâ‚‚, (cancel_mono (kernel.Î¹ (prod.lift f g))).1 $ calc m â‰« kernel.Î¹ (prod.lift f g)
          = m â‰« a' â‰« a : by { congr, exact ha'.symm }
      ... = pullback_cone.fst s â‰« a : by rw [â†category.assoc, hâ‚]
      ... = pullback_cone.snd s â‰« b : pullback_cone.condition s
      ... = kernel.lift (prod.lift f g) (pullback_cone.snd s â‰« b) _ â‰« kernel.Î¹ (prod.lift f g) :
        by rw kernel.lift_Î¹) }

section

local attribute [instance] pullback_of_mono

/-- The pullback of `(ğŸ™ X, f)` and `(ğŸ™ X, g)` -/
private abbreviation P {X Y : C} (f g : X âŸ¶ Y)
  [mono (prod.lift (ğŸ™ X) f)] [mono (prod.lift (ğŸ™ X) g)] : C :=
pullback (prod.lift (ğŸ™ X) f) (prod.lift (ğŸ™ X) g)

/-- The equalizer of `f` and `g` exists. -/
@[irreducible]
lemma has_limit_parallel_pair {X Y : C} (f g : X âŸ¶ Y) : has_limit (parallel_pair f g) :=
have huv : (pullback.fst : P f g âŸ¶ X) = pullback.snd, from
  calc (pullback.fst : P f g âŸ¶ X) = pullback.fst â‰« ğŸ™ _ : eq.symm $ category.comp_id _
    ... = pullback.fst â‰« prod.lift (ğŸ™ X) f â‰« limits.prod.fst : by rw prod.lift_fst
    ... = pullback.snd â‰« prod.lift (ğŸ™ X) g â‰« limits.prod.fst : by rw pullback.condition_assoc
    ... = pullback.snd : by rw [prod.lift_fst, category.comp_id],
have hvu : (pullback.fst : P f g âŸ¶ X) â‰« f = pullback.snd â‰« g, from
  calc (pullback.fst : P f g âŸ¶ X) â‰« f
        = pullback.fst â‰« prod.lift (ğŸ™ X) f â‰« limits.prod.snd : by rw prod.lift_snd
    ... = pullback.snd â‰« prod.lift (ğŸ™ X) g â‰« limits.prod.snd : by rw pullback.condition_assoc
    ... = pullback.snd â‰« g : by rw prod.lift_snd,
have huu : (pullback.fst : P f g âŸ¶ X) â‰« f = pullback.fst â‰« g, by rw [hvu, â†huv],
has_limit.mk { cone := fork.of_Î¹ pullback.fst huu,
  is_limit := fork.is_limit.mk _
  (Î» s, pullback.lift (fork.Î¹ s) (fork.Î¹ s) $ prod.hom_ext
    (by simp only [prod.lift_fst, category.assoc])
    (by simp only [prod.comp_lift, fork.condition]))
  (Î» s, by simp only [fork.Î¹_of_Î¹, pullback.lift_fst])
  (Î» s m h, pullback.hom_ext
    (by simpa only [pullback.lift_fst] using h)
    (by simpa only [huv.symm, pullback.lift_fst] using h)) }

end

section
local attribute [instance] has_limit_parallel_pair

/-- A `normal_mono_category` category with finite products and kernels has all equalizers. -/
@[priority 100] instance has_equalizers : has_equalizers C :=
has_equalizers_of_has_limit_parallel_pair _

end

/-- If a zero morphism is a cokernel of `f`, then `f` is an epimorphism. -/
lemma epi_of_zero_cokernel {X Y : C} (f : X âŸ¶ Y) (Z : C)
  (l : is_colimit (cokernel_cofork.of_Ï€ (0 : Y âŸ¶ Z) (show f â‰« 0 = 0, by simp))) : epi f :=
âŸ¨Î» P u v huv,
 begin
  obtain âŸ¨W, w, hw, hlâŸ© := normal_mono_of_mono (equalizer.Î¹ u v),
  obtain âŸ¨m, hmâŸ© := equalizer.lift' f huv,
  have hwf : f â‰« w = 0,
  { rw [â†hm, category.assoc, hw, comp_zero] },
  obtain âŸ¨n, hnâŸ© := cokernel_cofork.is_colimit.desc' l _ hwf,
  rw [cofork.Ï€_of_Ï€, zero_comp] at hn,
  haveI : is_iso (equalizer.Î¹ u v),
  { apply is_iso_limit_cone_parallel_pair_of_eq hn.symm hl },
  apply (cancel_epi (equalizer.Î¹ u v)).1,
  exact equalizer.condition _ _
 endâŸ©

section
variables [has_zero_object C]
open_locale zero_object

/-- If `f â‰« g = 0` implies `g = 0` for all `g`, then `g` is a monomorphism. -/
lemma epi_of_zero_cancel {X Y : C} (f : X âŸ¶ Y)
  (hf : âˆ€ (Z : C) (g : Y âŸ¶ Z) (hgf : f â‰« g = 0), g = 0) : epi f :=
epi_of_zero_cokernel f 0 $ zero_cokernel_of_zero_cancel f hf

end

end category_theory.normal_mono_category

namespace category_theory.normal_epi_category

variables [has_finite_coproducts C] [has_cokernels C] [normal_epi_category C]

/-- The pushout of two epimorphisms exists. -/
@[irreducible]
lemma pushout_of_epi {X Y Z : C} (a : X âŸ¶ Y) (b : X âŸ¶ Z) [epi a] [epi b] :
  has_colimit (span a b) :=
let âŸ¨P, f, hfa, iâŸ© := normal_epi_of_epi a in
let âŸ¨Q, g, hgb, i'âŸ© := normal_epi_of_epi b in
let âŸ¨a', ha'âŸ© := cokernel_cofork.is_colimit.desc' i (cokernel.Ï€ (coprod.desc f g)) $
  calc f â‰« cokernel.Ï€ (coprod.desc f g)
      = coprod.inl â‰« coprod.desc f g â‰« cokernel.Ï€ (coprod.desc f g) : by rw coprod.inl_desc_assoc
  ... = coprod.inl â‰« (0 : P â¨¿ Q âŸ¶ cokernel (coprod.desc f g)) : by rw cokernel.condition
  ... = 0 : has_zero_morphisms.comp_zero _ _ in
let âŸ¨b', hb'âŸ© := cokernel_cofork.is_colimit.desc' i' (cokernel.Ï€ (coprod.desc f g)) $
  calc g â‰« cokernel.Ï€ (coprod.desc f g)
      = coprod.inr â‰« coprod.desc f g â‰« cokernel.Ï€ (coprod.desc f g) : by rw coprod.inr_desc_assoc
  ... = coprod.inr â‰« (0 : P â¨¿ Q âŸ¶ cokernel (coprod.desc f g)) :  by rw cokernel.condition
  ... = 0 : has_zero_morphisms.comp_zero _ _ in
has_colimit.mk
{ cocone := pushout_cocone.mk a' b' $ by { simp only [cofork.Ï€_of_Ï€] at ha' hb', rw [ha', hb'] },
  is_colimit := pushout_cocone.is_colimit.mk _
  (Î» s, cokernel.desc (coprod.desc f g) (b â‰« pushout_cocone.inr s) $ coprod.hom_ext
    (calc coprod.inl â‰« coprod.desc f g â‰« b â‰« pushout_cocone.inr s
          = f â‰« b â‰« pushout_cocone.inr s : by rw coprod.inl_desc_assoc
      ... = f â‰« a â‰« pushout_cocone.inl s : by rw pushout_cocone.condition
      ... = 0 â‰« pushout_cocone.inl s : by rw reassoc_of hfa
      ... = coprod.inl â‰« 0 : by rw [comp_zero, zero_comp])
    (calc coprod.inr â‰« coprod.desc f g â‰« b â‰« pushout_cocone.inr s
          = g â‰« b â‰« pushout_cocone.inr s : by rw coprod.inr_desc_assoc
      ... = 0 â‰« pushout_cocone.inr s : by rw reassoc_of hgb
      ... = coprod.inr â‰« 0 : by rw [comp_zero, zero_comp]))
  (Î» s, (cancel_epi a).1 $
    by { rw cokernel_cofork.Ï€_of_Ï€ at ha', simp [reassoc_of ha', pushout_cocone.condition s] })
  (Î» s, (cancel_epi b).1 $ by { rw cokernel_cofork.Ï€_of_Ï€ at hb', simp [reassoc_of hb'] })
  (Î» s m hâ‚ hâ‚‚, (cancel_epi (cokernel.Ï€ (coprod.desc f g))).1 $
  calc cokernel.Ï€ (coprod.desc f g) â‰« m
        = (a â‰« a') â‰« m : by { congr, exact ha'.symm }
    ... = a â‰« pushout_cocone.inl s : by rw [category.assoc, hâ‚]
    ... = b â‰« pushout_cocone.inr s : pushout_cocone.condition s
    ... = cokernel.Ï€ (coprod.desc f g) â‰«
            cokernel.desc (coprod.desc f g) (b â‰« pushout_cocone.inr s) _ :
      by rw cokernel.Ï€_desc) }



section
local attribute [instance] pushout_of_epi

/-- The pushout of `(ğŸ™ Y, f)` and `(ğŸ™ Y, g)`. -/
private abbreviation Q {X Y : C} (f g : X âŸ¶ Y)
  [epi (coprod.desc (ğŸ™ Y) f)] [epi (coprod.desc (ğŸ™ Y) g)] : C :=
pushout (coprod.desc (ğŸ™ Y) f) (coprod.desc (ğŸ™ Y) g)

/-- The coequalizer of `f` and `g` exists. -/
@[irreducible]
lemma has_colimit_parallel_pair {X Y : C} (f g : X âŸ¶ Y) : has_colimit (parallel_pair f g) :=
have huv : (pushout.inl : Y âŸ¶ Q f g) = pushout.inr, from
  calc (pushout.inl : Y âŸ¶ Q f g) = ğŸ™ _ â‰« pushout.inl : eq.symm $ category.id_comp _
    ... = (coprod.inl â‰« coprod.desc (ğŸ™ Y) f) â‰« pushout.inl : by rw coprod.inl_desc
    ... = (coprod.inl â‰« coprod.desc (ğŸ™ Y) g) â‰« pushout.inr :
      by simp only [category.assoc, pushout.condition]
    ... = pushout.inr : by rw [coprod.inl_desc, category.id_comp],
have hvu : f â‰« (pushout.inl : Y âŸ¶ Q f g) = g â‰« pushout.inr, from
  calc f â‰« (pushout.inl : Y âŸ¶ Q f g)
        = (coprod.inr â‰« coprod.desc (ğŸ™ Y) f) â‰« pushout.inl : by rw coprod.inr_desc
    ... = (coprod.inr â‰« coprod.desc (ğŸ™ Y) g) â‰« pushout.inr :
      by simp only [category.assoc, pushout.condition]
    ... = g â‰« pushout.inr : by rw coprod.inr_desc,
have huu : f â‰« (pushout.inl : Y âŸ¶ Q f g) = g â‰« pushout.inl, by rw [hvu, huv],
has_colimit.mk { cocone := cofork.of_Ï€ pushout.inl huu,
  is_colimit := cofork.is_colimit.mk _
  (Î» s, pushout.desc (cofork.Ï€ s) (cofork.Ï€ s) $ coprod.hom_ext
    (by simp only [coprod.inl_desc_assoc])
    (by simp only [coprod.desc_comp, cofork.condition]))
  (Î» s, by simp only [pushout.inl_desc, cofork.Ï€_of_Ï€])
  (Î» s m h, pushout.hom_ext
    (by simpa only [pushout.inl_desc] using h)
    (by simpa only [huv.symm, pushout.inl_desc] using h)) }

end

section
local attribute [instance] has_colimit_parallel_pair

/-- A `normal_epi_category` category with finite coproducts and cokernels has all coequalizers. -/
@[priority 100] instance has_coequalizers : has_coequalizers C :=
has_coequalizers_of_has_colimit_parallel_pair _

end

/-- If a zero morphism is a kernel of `f`, then `f` is a monomorphism. -/
lemma mono_of_zero_kernel {X Y : C} (f : X âŸ¶ Y) (Z : C)
  (l : is_limit (kernel_fork.of_Î¹ (0 : Z âŸ¶ X) (show 0 â‰« f = 0, by simp))) : mono f :=
âŸ¨Î» P u v huv,
 begin
  obtain âŸ¨W, w, hw, hlâŸ© := normal_epi_of_epi (coequalizer.Ï€ u v),
  obtain âŸ¨m, hmâŸ© := coequalizer.desc' f huv,
  have hwf : w â‰« f = 0,
  { rw [â†hm, reassoc_of hw, zero_comp] },
  obtain âŸ¨n, hnâŸ© := kernel_fork.is_limit.lift' l _ hwf,
  rw [fork.Î¹_of_Î¹, has_zero_morphisms.comp_zero] at hn,
  haveI : is_iso (coequalizer.Ï€ u v),
  { apply is_iso_colimit_cocone_parallel_pair_of_eq hn.symm hl },
  apply (cancel_mono (coequalizer.Ï€ u v)).1,
  exact coequalizer.condition _ _
 endâŸ©

section
variables [has_zero_object C]
open_locale zero_object

/-- If `g â‰« f = 0` implies `g = 0` for all `g`, then `f` is a monomorphism. -/
lemma mono_of_cancel_zero {X Y : C} (f : X âŸ¶ Y)
  (hf : âˆ€ (Z : C) (g : Z âŸ¶ X) (hgf : g â‰« f = 0), g = 0) : mono f :=
mono_of_zero_kernel f 0 $ zero_kernel_of_cancel_zero f hf

end

end category_theory.normal_epi_category
