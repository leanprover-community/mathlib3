/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import data.fintype.basic
import category_theory.limits.limits
import category_theory.limits.shapes.finite_limits
import category_theory.sparse

/-!
# Pullbacks

We define a category `walking_cospan` (resp. `walking_span`), which is the index category
for the given data for a pullback (resp. pushout) diagram. Convenience methods `cospan f g`
and `span f g` construct functors from the walking (co)span, hitting the given morphisms.

We define `pullback f g` and `pushout f g` as limits and colimits of such functors.

Typeclasses `has_pullbacks` and `has_pushouts` assert the existence of (co)limits shaped as
walking (co)spans.
-/

open category_theory

namespace category_theory.limits

universes v u

local attribute [tidy] tactic.case_bash

/-- The type of objects for the diagram indexing a pullback. -/
@[derive decidable_eq] inductive walking_cospan : Type v
| left | right | one
/-- The type of objects for the diagram indexing a pushout. -/
@[derive decidable_eq] inductive walking_span : Type v
| zero | left | right

instance fintype_walking_cospan : fintype walking_cospan :=
{ elems := [walking_cospan.left, walking_cospan.right, walking_cospan.one].to_finset,
  complete := Î» x, by { cases x; simp } }

instance fintype_walking_span : fintype walking_span :=
{ elems := [walking_span.zero, walking_span.left, walking_span.right].to_finset,
  complete := Î» x, by { cases x; simp } }

namespace walking_cospan

/-- The arrows in a pullback diagram. -/
@[derive _root_.decidable_eq] inductive hom : walking_cospan â†’ walking_cospan â†’ Type v
| inl : hom left one
| inr : hom right one
| id : Î  X : walking_cospan.{v}, hom X X

open hom

instance fintype_walking_cospan_hom (j j' : walking_cospan) : fintype (hom j j') :=
{ elems := walking_cospan.rec_on j
    (walking_cospan.rec_on j' [hom.id left].to_finset âˆ… [inl].to_finset)
    (walking_cospan.rec_on j' âˆ… [hom.id right].to_finset [inr].to_finset)
    (walking_cospan.rec_on j' âˆ… âˆ… [hom.id one].to_finset),
  complete := by tidy }

def hom.comp : Î  (X Y Z : walking_cospan) (f : hom X Y) (g : hom Y Z), hom X Z
| _ _ _ (id _) h := h
| _ _ _ inl    (id one) := inl
| _ _ _ inr    (id one) := inr
.

instance category_struct : category_struct walking_cospan :=
{ hom  := hom,
  id   := hom.id,
  comp := hom.comp, }

instance (X Y : walking_cospan) : subsingleton (X âŸ¶ Y) := by tidy

-- We make this a @[simp] lemma later; if we do it now there's a mysterious
-- failure in `cospan`, below.
lemma hom_id (X : walking_cospan.{v}) : hom.id X = ğŸ™ X := rfl

/-- The walking_cospan is the index diagram for a pullback. -/
instance : small_category.{v} walking_cospan.{v} := sparse_category

instance : fin_category.{v} walking_cospan.{v} :=
{ fintype_hom := walking_cospan.fintype_walking_cospan_hom }

end walking_cospan

namespace walking_span

/-- The arrows in a pushout diagram. -/
@[derive _root_.decidable_eq] inductive hom : walking_span â†’ walking_span â†’ Type v
| fst : hom zero left
| snd : hom zero right
| id : Î  X : walking_span.{v}, hom X X

open hom

instance fintype_walking_span_hom (j j' : walking_span) : fintype (hom j j') :=
{ elems := walking_span.rec_on j
    (walking_span.rec_on j' [hom.id zero].to_finset [fst].to_finset [snd].to_finset)
    (walking_span.rec_on j' âˆ… [hom.id left].to_finset âˆ…)
    (walking_span.rec_on j' âˆ… âˆ… [hom.id right].to_finset),
  complete := by tidy }

def hom.comp : Î  (X Y Z : walking_span) (f : hom X Y) (g : hom Y Z), hom X Z
  | _ _ _ (id _) h := h
  | _ _ _ fst    (id left) := fst
  | _ _ _ snd    (id right) := snd
.

instance category_struct : category_struct walking_span :=
{ hom  := hom,
  id   := hom.id,
  comp := hom.comp }

instance (X Y : walking_span) : subsingleton (X âŸ¶ Y) := by tidy

-- We make this a @[simp] lemma later; if we do it now there's a mysterious
-- failure in `span`, below.
lemma hom_id (X : walking_span.{v}) : hom.id X = ğŸ™ X := rfl

/-- The walking_span is the index diagram for a pushout. -/
instance : small_category.{v} walking_span.{v} := sparse_category

instance : fin_category.{v} walking_span.{v} :=
{ fintype_hom := walking_span.fintype_walking_span_hom }

end walking_span

open walking_span walking_cospan walking_span.hom walking_cospan.hom

variables {C : Type u} [ğ’ : category.{v} C]
include ğ’

/-- `cospan f g` is the functor from the walking cospan hitting `f` and `g`. -/
def cospan {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : walking_cospan.{v} â¥¤ C :=
{ obj := Î» x, match x with
  | left := X
  | right := Y
  | one := Z
  end,
  map := Î» x y h, match x, y, h with
  | _, _, (id _) := ğŸ™ _
  | _, _, inl := f
  | _, _, inr := g
  end }

/-- `span f g` is the functor from the walking span hitting `f` and `g`. -/
def span {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) : walking_span.{v} â¥¤ C :=
{ obj := Î» x, match x with
  | zero := X
  | left := Y
  | right := Z
  end,
  map := Î» x y h, match x, y, h with
  | _, _, (id _) := ğŸ™ _
  | _, _, fst := f
  | _, _, snd := g
  end }

@[simp] lemma cospan_left {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.left = X := rfl
@[simp] lemma span_left {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.left = Y := rfl

@[simp] lemma cospan_right {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.right = Y := rfl
@[simp] lemma span_right {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.right = Z := rfl

@[simp] lemma cospan_one {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.one = Z := rfl
@[simp] lemma span_zero {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.zero = X := rfl

@[simp] lemma cospan_map_inl {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).map walking_cospan.hom.inl = f := rfl
@[simp] lemma span_map_fst {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).map walking_span.hom.fst = f := rfl

@[simp] lemma cospan_map_inr {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).map walking_cospan.hom.inr = g := rfl
@[simp] lemma span_map_snd {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).map walking_span.hom.snd = g := rfl

lemma cospan_map_id {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (w : walking_cospan) :
  (cospan f g).map (walking_cospan.hom.id w) = ğŸ™ _ := rfl
lemma span_map_id {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (w : walking_span) :
  (span f g).map (walking_span.hom.id w) = ğŸ™ _ := rfl

/-- Every diagram indexing an equalizer is naturally isomorphic (actually, equal) to a `cospan` -/
def diagram_iso_cospan (F : walking_cospan â¥¤ C) :
  F â‰… cospan (F.map inl) (F.map inr) :=
nat_iso.of_components (Î» j, eq_to_iso $ by cases j; tidy) $ by tidy

/-- Every diagram indexing a coequalizer naturally isomorphic (actually, equal) to a `span` -/
def diagram_iso_span (F : walking_span â¥¤ C) :
  F â‰… span (F.map fst) (F.map snd) :=
nat_iso.of_components (Î» j, eq_to_iso $ by cases j; tidy) $ by tidy

variables {X Y Z : C}

attribute [simp] walking_cospan.hom_id walking_span.hom_id

abbreviation pullback_cone (f : X âŸ¶ Z) (g : Y âŸ¶ Z) := cone (cospan f g)

namespace pullback_cone
variables {f : X âŸ¶ Z} {g : Y âŸ¶ Z}

abbreviation fst (t : pullback_cone f g) : t.X âŸ¶ X := t.Ï€.app left
abbreviation snd (t : pullback_cone f g) : t.X âŸ¶ Y := t.Ï€.app right

def mk {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) : pullback_cone f g :=
{ X := W,
  Ï€ :=
  { app := Î» j, walking_cospan.cases_on j fst snd (fst â‰« f),
    naturality' := Î» j j' f, by cases f; obviously } }

@[simp] lemma mk_Ï€_app_left {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app left = fst := rfl
@[simp] lemma mk_Ï€_app_right {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app right = snd := rfl
@[simp] lemma mk_Ï€_app_one {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app one = fst â‰« f := rfl

@[reassoc] lemma condition (t : pullback_cone f g) : fst t â‰« f = snd t â‰« g :=
begin
  erw [t.w inl, â† t.w inr], refl
end

/-- To check whether a morphism is equalized by the maps of a pullback cone, it suffices to check
  it for `fst t` and `snd t` -/
lemma equalizer_ext (t : pullback_cone f g) {W : C} {k l : W âŸ¶ t.X}
  (hâ‚€ : k â‰« fst t = l â‰« fst t)
  (hâ‚ : k â‰« snd t = l â‰« snd t) :
  âˆ€ (j : walking_cospan), k â‰« t.Ï€.app j = l â‰« t.Ï€.app j
| left := hâ‚€
| right := hâ‚
| one := calc k â‰« t.Ï€.app one = k â‰« t.Ï€.app left â‰« (cospan f g).map inl : by rw â†t.w
    ... = l â‰« t.Ï€.app left â‰« (cospan f g).map inl : by rw [â†category.assoc, hâ‚€, category.assoc]
    ... = l â‰« t.Ï€.app one : by rw t.w

/-- This is a slightly more convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def is_limit.mk (t : pullback_cone f g) (lift : Î  (s : cone (cospan f g)), s.X âŸ¶ t.X)
  (fac_left : âˆ€ (s : cone (cospan f g)), lift s â‰« t.Ï€.app left = s.Ï€.app left)
  (fac_right : âˆ€ (s : cone (cospan f g)), lift s â‰« t.Ï€.app right = s.Ï€.app right)
  (uniq : âˆ€ (s : cone (cospan f g)) (m : s.X âŸ¶ t.X)
    (w : âˆ€ j : walking_cospan, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) :
  is_limit t :=
{ lift := lift,
  fac' := Î» s j, walking_cospan.cases_on j (fac_left s) (fac_right s) $
    by rw [â†t.w inl, â†s.w inl, â†fac_left s, category.assoc],
  uniq' := uniq }

end pullback_cone

abbreviation pushout_cocone (f : X âŸ¶ Y) (g : X âŸ¶ Z) := cocone (span f g)

namespace pushout_cocone

variables {f : X âŸ¶ Y} {g : X âŸ¶ Z}

abbreviation inl (t : pushout_cocone f g) : Y âŸ¶ t.X := t.Î¹.app left
abbreviation inr (t : pushout_cocone f g) : Z âŸ¶ t.X := t.Î¹.app right

def mk {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) : pushout_cocone f g :=
{ X := W,
  Î¹ :=
  { app := Î» j, walking_span.cases_on j (f â‰« inl) inl inr,
    naturality' := Î» j j' f, by cases f; obviously } }

@[simp] lemma mk_Î¹_app_left {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app left = inl := rfl
@[simp] lemma mk_Î¹_app_right {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app right = inr := rfl
@[simp] lemma mk_Î¹_app_zero {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app zero = f â‰« inl := rfl

@[reassoc] lemma condition (t : pushout_cocone f g) : f â‰« (inl t) = g â‰« (inr t) :=
begin
  erw [t.w fst, â† t.w snd], refl
end

/-- To check whether a morphism is coequalized by the maps of a pushout cocone, it suffices to check
  it for `inl t` and `inr t` -/
lemma coequalizer_ext (t : pushout_cocone f g) {W : C} {k l : t.X âŸ¶ W}
  (hâ‚€ : inl t â‰« k = inl t â‰« l)
  (hâ‚ : inr t â‰« k = inr t â‰« l) :
  âˆ€ (j : walking_span), t.Î¹.app j â‰« k = t.Î¹.app j â‰« l
| left := hâ‚€
| right := hâ‚
| zero := calc t.Î¹.app zero â‰« k = ((span f g).map fst â‰« t.Î¹.app left) â‰« k : by rw â†t.w
    ... = ((span f g).map fst â‰« t.Î¹.app left) â‰« l : by rw [category.assoc, hâ‚€, â†category.assoc]
    ... = t.Î¹.app zero â‰« l : by rw t.w

/-- This is a slightly more convenient method to verify that a pushout cocone is a colimit cocone.
    It only asks for a proof of facts that carry any mathematical content -/
def is_colimit.mk (t : pushout_cocone f g) (desc : Î  (s : cocone (span f g)), t.X âŸ¶ s.X)
  (fac_left : âˆ€ (s : cocone (span f g)), t.Î¹.app left â‰« desc s = s.Î¹.app left)
  (fac_right : âˆ€ (s : cocone (span f g)), t.Î¹.app right â‰« desc s = s.Î¹.app right)
  (uniq : âˆ€ (s : cocone (span f g)) (m : t.X âŸ¶ s.X)
    (w : âˆ€ j : walking_span, t.Î¹.app j â‰« m = s.Î¹.app j), m = desc s) :
  is_colimit t :=
{ desc := desc,
  fac' := Î» s j, walking_span.cases_on j (by rw [â†s.w fst, â†t.w fst, category.assoc, fac_left s])
    (fac_left s) (fac_right s),
  uniq' := uniq }

end pushout_cocone

def cone.of_pullback_cone
  {F : walking_cospan.{v} â¥¤ C} (t : pullback_cone (F.map inl) (F.map inr)) : cone F :=
{ X := t.X,
  Ï€ :=
  { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy),
    naturality' := Î» j j' g,
    begin
      cases j; cases j'; cases g; dsimp; simp,
      exact (t.w inl).symm,
      exact (t.w inr).symm
    end } }.

@[simp] lemma cone.of_pullback_cone_Ï€
  {F : walking_cospan.{v} â¥¤ C} (t : pullback_cone (F.map inl) (F.map inr)) (j) :
  (cone.of_pullback_cone t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl

def cocone.of_pushout_cocone
  {F : walking_span.{v} â¥¤ C} (t : pushout_cocone (F.map fst) (F.map snd)) : cocone F :=
{ X := t.X,
  Î¹ :=
  { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X,
    naturality' := Î» j j' g,
    begin
      cases j; cases j'; cases g; dsimp; simp,
      exact t.w fst,
      exact t.w snd
    end } }.

@[simp] lemma cocone.of_pushout_cocone_Î¹
  {F : walking_span.{v} â¥¤ C} (t : pushout_cocone (F.map fst) (F.map snd)) (j) :
  (cocone.of_pushout_cocone t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

def pullback_cone.of_cone
  {F : walking_cospan.{v} â¥¤ C} (t : cone F) : pullback_cone (F.map inl) (F.map inr) :=
{ X := t.X,
  Ï€ := { app := Î» j, t.Ï€.app j â‰« eq_to_hom (by tidy) } }

@[simp] lemma pullback_cone.of_cone_Ï€ {F : walking_cospan.{v} â¥¤ C} (t : cone F) (j) :
  (pullback_cone.of_cone t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl

def pushout_cocone.of_cocone
  {F : walking_span.{v} â¥¤ C} (t : cocone F) : pushout_cocone (F.map fst) (F.map snd) :=
{ X := t.X,
  Î¹ := { app := Î» j, eq_to_hom (by tidy) â‰« t.Î¹.app j } }

@[simp] lemma pushout_cocone.of_cocone_Î¹ {F : walking_span.{v} â¥¤ C} (t : cocone F) (j) :
  (pushout_cocone.of_cocone t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

/-- `pullback f g` computes the pullback of a pair of morphisms with the same target. -/
abbreviation pullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [has_limit (cospan f g)] :=
limit (cospan f g)
/-- `pushout f g` computes the pushout of a pair of morphisms with the same source. -/
abbreviation pushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [has_colimit (span f g)] :=
colimit (span f g)

abbreviation pullback.fst {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)] : pullback f g âŸ¶ X :=
limit.Ï€ (cospan f g) walking_cospan.left
abbreviation pullback.snd {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)] : pullback f g âŸ¶ Y :=
limit.Ï€ (cospan f g) walking_cospan.right
abbreviation pushout.inl {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] : Y âŸ¶ pushout f g :=
colimit.Î¹ (span f g) walking_span.left
abbreviation pushout.inr {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] : Z âŸ¶ pushout f g :=
colimit.Î¹ (span f g) walking_span.right

abbreviation pullback.lift {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : W âŸ¶ pullback f g :=
limit.lift _ (pullback_cone.mk h k w)
abbreviation pushout.desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout f g âŸ¶ W :=
colimit.desc _ (pushout_cocone.mk h k w)

lemma pullback.condition {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)] :
  (pullback.fst : pullback f g âŸ¶ X) â‰« f = pullback.snd â‰« g :=
(limit.w (cospan f g) walking_cospan.hom.inl).trans
(limit.w (cospan f g) walking_cospan.hom.inr).symm

lemma pushout.condition {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] :
  f â‰« (pushout.inl : Y âŸ¶ pushout f g) = g â‰« pushout.inr :=
(colimit.w (span f g) walking_span.hom.fst).trans
(colimit.w (span f g) walking_span.hom.snd).symm

/-- Two morphisms into a pullback are equal if their compositions with the pullback morphisms are
    equal -/
@[ext] lemma pullback.hom_ext {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  {W : C} {k l : W âŸ¶ pullback f g} (hâ‚€ : k â‰« pullback.fst = l â‰« pullback.fst)
  (hâ‚ : k â‰« pullback.snd = l â‰« pullback.snd) : k = l :=
limit.hom_ext $ pullback_cone.equalizer_ext _ hâ‚€ hâ‚

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.fst_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  [mono g] : mono (pullback.fst : pullback f g âŸ¶ X) :=
âŸ¨Î» W u v h, pullback.hom_ext h $ (cancel_mono g).1 $
  calc (u â‰« pullback.snd) â‰« g = u â‰« pullback.fst â‰« f : by rw [category.assoc, pullback.condition]
    ... = v â‰« pullback.fst â‰« f : by rw [â†category.assoc, h, category.assoc]
    ... = (v â‰« pullback.snd) â‰« g : by rw [pullback.condition, â†category.assoc]âŸ©

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.snd_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  [mono f] : mono (pullback.snd : pullback f g âŸ¶ Y) :=
âŸ¨Î» W u v h, pullback.hom_ext ((cancel_mono f).1 $
  calc (u â‰« pullback.fst) â‰« f = u â‰« pullback.snd â‰« g : by rw [category.assoc, pullback.condition]
    ... = v â‰« pullback.snd â‰« g : by rw [â†category.assoc, h, category.assoc]
    ... = (v â‰« pullback.fst) â‰« f : by rw [â†pullback.condition, â†category.assoc]) hâŸ©

/-- Two morphisms out of a pushout are equal if their compositions with the pushout morphisms are
    equal -/
@[ext] lemma pushout.hom_ext {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)]
  {W : C} {k l : pushout f g âŸ¶ W} (hâ‚€ : pushout.inl â‰« k = pushout.inl â‰« l)
  (hâ‚ : pushout.inr â‰« k = pushout.inr â‰« l) : k = l :=
colimit.hom_ext $ pushout_cocone.coequalizer_ext _ hâ‚€ hâ‚

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inl_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] [epi g] :
  epi (pushout.inl : Y âŸ¶ pushout f g) :=
âŸ¨Î» W u v h, pushout.hom_ext h $ (cancel_epi g).1 $
  calc g â‰« pushout.inr â‰« u = (f â‰« pushout.inl) â‰« u : by rw [â†category.assoc, â†pushout.condition]
    ... = f â‰« pushout.inl â‰« v : by rw [category.assoc, h]
    ... = g â‰« pushout.inr â‰« v : by rw [â†category.assoc, pushout.condition, category.assoc]âŸ©

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inr_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] [epi f] :
  epi (pushout.inr : Z âŸ¶ pushout f g) :=
âŸ¨Î» W u v h, pushout.hom_ext ((cancel_epi f).1 $
  calc f â‰« pushout.inl â‰« u = (g â‰« pushout.inr) â‰« u : by rw [â†category.assoc, pushout.condition]
    ... = g â‰« pushout.inr â‰« v : by rw [category.assoc, h]
    ... = f â‰« pushout.inl â‰« v : by rw [â†category.assoc, â†pushout.condition, category.assoc]) hâŸ©

variables (C)

/-- `has_pullbacks` represents a choice of pullback for every pair of morphisms -/
class has_pullbacks :=
(has_limits_of_shape : has_limits_of_shape.{v} walking_cospan C)

/-- `has_pushouts` represents a choice of pushout for every pair of morphisms -/
class has_pushouts :=
(has_colimits_of_shape : has_colimits_of_shape.{v} walking_span C)

attribute [instance] has_pullbacks.has_limits_of_shape has_pushouts.has_colimits_of_shape

/-- Pullbacks are finite limits, so if `C` has all finite limits, it also has all pullbacks -/
def has_pullbacks_of_has_finite_limits [has_finite_limits.{v} C] : has_pullbacks.{v} C :=
{ has_limits_of_shape := infer_instance }

/-- Pushouts are finite colimits, so if `C` has all finite colimits, it also has all pushouts -/
def has_pushouts_of_has_finite_colimits [has_finite_colimits.{v} C] : has_pushouts.{v} C :=
{ has_colimits_of_shape := infer_instance }

/-- If `C` has all limits of diagrams `cospan f g`, then it has all pullbacks -/
def has_pullbacks_of_has_limit_cospan
  [Î  {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z}, has_limit (cospan f g)] :
  has_pullbacks.{v} C :=
{ has_limits_of_shape := { has_limit := Î» F, has_limit_of_iso (diagram_iso_cospan F).symm } }

/-- If `C` has all colimits of diagrams `span f g`, then it has all pushouts -/
def has_pushouts_of_has_colimit_span
  [Î  {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z}, has_colimit (span f g)] :
  has_pushouts.{v} C :=
{ has_colimits_of_shape := { has_colimit := Î» F, has_colimit_of_iso (diagram_iso_span F) } }

end category_theory.limits
