/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel, Bhavik Mehta
-/
import category_theory.limits.shapes.wide_pullbacks
import category_theory.limits.shapes.binary_products

/-!
# Pullbacks

We define a category `walking_cospan` (resp. `walking_span`), which is the index category
for the given data for a pullback (resp. pushout) diagram. Convenience methods `cospan f g`
and `span f g` construct functors from the walking (co)span, hitting the given morphisms.

We define `pullback f g` and `pushout f g` as limits and colimits of such functors.

## References
* [Stacks: Fibre products](https://stacks.math.columbia.edu/tag/001U)
* [Stacks: Pushouts](https://stacks.math.columbia.edu/tag/0025)
-/

noncomputable theory

open category_theory

namespace category_theory.limits

universes v u uâ‚‚

local attribute [tidy] tactic.case_bash

/--
The type of objects for the diagram indexing a pullback, defined as a special case of
`wide_pullback_shape`.
-/
abbreviation walking_cospan : Type v := wide_pullback_shape walking_pair

/-- The left point of the walking cospan. -/
@[pattern] abbreviation walking_cospan.left : walking_cospan := some walking_pair.left
/-- The right point of the walking cospan. -/
@[pattern] abbreviation walking_cospan.right : walking_cospan := some walking_pair.right
/-- The central point of the walking cospan. -/
@[pattern] abbreviation walking_cospan.one : walking_cospan := none

/--
The type of objects for the diagram indexing a pushout, defined as a special case of
`wide_pushout_shape`.
-/
abbreviation walking_span : Type v := wide_pushout_shape walking_pair

/-- The left point of the walking span. -/
@[pattern] abbreviation walking_span.left : walking_span := some walking_pair.left
/-- The right point of the walking span. -/
@[pattern] abbreviation walking_span.right : walking_span := some walking_pair.right
/-- The central point of the walking span. -/
@[pattern] abbreviation walking_span.zero : walking_span := none

namespace walking_cospan

/-- The type of arrows for the diagram indexing a pullback. -/
abbreviation hom : walking_cospan â†’ walking_cospan â†’ Type v := wide_pullback_shape.hom

/-- The left arrow of the walking cospan. -/
@[pattern] abbreviation hom.inl : left âŸ¶ one := wide_pullback_shape.hom.term _
/-- The right arrow of the walking cospan. -/
@[pattern] abbreviation hom.inr : right âŸ¶ one := wide_pullback_shape.hom.term _
/-- The identity arrows of the walking cospan. -/
@[pattern] abbreviation hom.id (X : walking_cospan) : X âŸ¶ X := wide_pullback_shape.hom.id X

instance (X Y : walking_cospan) : subsingleton (X âŸ¶ Y) := by tidy

end walking_cospan

namespace walking_span

/-- The type of arrows for the diagram indexing a pushout. -/
abbreviation hom : walking_span â†’ walking_span â†’ Type v := wide_pushout_shape.hom

/-- The left arrow of the walking span. -/
@[pattern] abbreviation hom.fst : zero âŸ¶ left := wide_pushout_shape.hom.init _
/-- The right arrow of the walking span. -/
@[pattern] abbreviation hom.snd : zero âŸ¶ right := wide_pushout_shape.hom.init _
/-- The identity arrows of the walking span. -/
@[pattern] abbreviation hom.id (X : walking_span) : X âŸ¶ X := wide_pushout_shape.hom.id X

instance (X Y : walking_span) : subsingleton (X âŸ¶ Y) := by tidy

end walking_span

open walking_span.hom walking_cospan.hom wide_pullback_shape.hom wide_pushout_shape.hom

variables {C : Type u} [category.{v} C]

/-- `cospan f g` is the functor from the walking cospan hitting `f` and `g`. -/
def cospan {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : walking_cospan â¥¤ C :=
wide_pullback_shape.wide_cospan Z
  (Î» j, walking_pair.cases_on j X Y) (Î» j, walking_pair.cases_on j f g)

/-- `span f g` is the functor from the walking span hitting `f` and `g`. -/
def span {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) : walking_span â¥¤ C :=
wide_pushout_shape.wide_span X
  (Î» j, walking_pair.cases_on j Y Z) (Î» j, walking_pair.cases_on j f g)

@[simp] lemma cospan_left {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.left = X := rfl
@[simp] lemma span_left {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.left = Y := rfl

@[simp] lemma cospan_right {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.right = Y := rfl
@[simp] lemma span_right {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.right = Z := rfl

@[simp] lemma cospan_one {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.one = Z := rfl
@[simp] lemma span_zero {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.zero = X := rfl

@[simp] lemma cospan_map_inl {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).map walking_cospan.hom.inl = f := rfl
@[simp] lemma span_map_fst {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).map walking_span.hom.fst = f := rfl

@[simp] lemma cospan_map_inr {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).map walking_cospan.hom.inr = g := rfl
@[simp] lemma span_map_snd {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).map walking_span.hom.snd = g := rfl

lemma cospan_map_id {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (w : walking_cospan) :
  (cospan f g).map (walking_cospan.hom.id w) = ğŸ™ _ := rfl
lemma span_map_id {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (w : walking_span) :
  (span f g).map (walking_span.hom.id w) = ğŸ™ _ := rfl

/-- Every diagram indexing an pullback is naturally isomorphic (actually, equal) to a `cospan` -/
@[simps {rhs_md := semireducible}]
def diagram_iso_cospan (F : walking_cospan â¥¤ C) :
  F â‰… cospan (F.map inl) (F.map inr) :=
nat_iso.of_components (Î» j, eq_to_iso (by tidy)) (by tidy)

/-- Every diagram indexing a pushout is naturally isomorphic (actually, equal) to a `span` -/
@[simps {rhs_md := semireducible}]
def diagram_iso_span (F : walking_span â¥¤ C) :
  F â‰… span (F.map fst) (F.map snd) :=
nat_iso.of_components (Î» j, eq_to_iso (by tidy)) (by tidy)

variables {W X Y Z : C}

/-- A pullback cone is just a cone on the cospan formed by two morphisms `f : X âŸ¶ Z` and
    `g : Y âŸ¶ Z`.-/
abbreviation pullback_cone (f : X âŸ¶ Z) (g : Y âŸ¶ Z) := cone (cospan f g)

namespace pullback_cone
variables {f : X âŸ¶ Z} {g : Y âŸ¶ Z}

/-- The first projection of a pullback cone. -/
abbreviation fst (t : pullback_cone f g) : t.X âŸ¶ X := t.Ï€.app walking_cospan.left

/-- The second projection of a pullback cone. -/
abbreviation snd (t : pullback_cone f g) : t.X âŸ¶ Y := t.Ï€.app walking_cospan.right

/-- This is a slightly more convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def is_limit_aux (t : pullback_cone f g) (lift : Î  (s : pullback_cone f g), s.X âŸ¶ t.X)
  (fac_left : âˆ€ (s : pullback_cone f g), lift s â‰« t.fst = s.fst)
  (fac_right : âˆ€ (s : pullback_cone f g), lift s â‰« t.snd = s.snd)
  (uniq : âˆ€ (s : pullback_cone f g) (m : s.X âŸ¶ t.X)
    (w : âˆ€ j : walking_cospan, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) :
  is_limit t :=
{ lift := lift,
  fac' := Î» s j, option.cases_on j
    (by { rw [â† s.w inl, â† t.w inl, â†category.assoc], congr, exact fac_left s, } )
    (Î» j', walking_pair.cases_on j' (fac_left s) (fac_right s)),
  uniq' := uniq }

/-- This is another convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def is_limit_aux' (t : pullback_cone f g)
  (create : Î  (s : pullback_cone f g),
    {l // l â‰« t.fst = s.fst âˆ§ l â‰« t.snd = s.snd âˆ§
            âˆ€ {m}, m â‰« t.fst = s.fst â†’ m â‰« t.snd = s.snd â†’ m = l}) :
limits.is_limit t :=
pullback_cone.is_limit_aux t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s, (create s).2.2.1)
  (Î» s m w, (create s).2.2.2 (w walking_cospan.left) (w walking_cospan.right))

/-- A pullback cone on `f` and `g` is determined by morphisms `fst : W âŸ¶ X` and `snd : W âŸ¶ Y`
    such that `fst â‰« f = snd â‰« g`. -/
@[simps]
def mk {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) : pullback_cone f g :=
{ X := W,
  Ï€ := { app := Î» j, option.cases_on j (fst â‰« f) (Î» j', walking_pair.cases_on j' fst snd) } }

@[simp] lemma mk_Ï€_app_left {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app walking_cospan.left = fst := rfl
@[simp] lemma mk_Ï€_app_right {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app walking_cospan.right = snd := rfl
@[simp] lemma mk_Ï€_app_one {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app walking_cospan.one = fst â‰« f := rfl

@[simp] lemma mk_fst {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).fst = fst := rfl
@[simp] lemma mk_snd {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).snd = snd := rfl

@[reassoc] lemma condition (t : pullback_cone f g) : fst t â‰« f = snd t â‰« g :=
(t.w inl).trans (t.w inr).symm

/-- To check whether a morphism is equalized by the maps of a pullback cone, it suffices to check
  it for `fst t` and `snd t` -/
lemma equalizer_ext (t : pullback_cone f g) {W : C} {k l : W âŸ¶ t.X}
  (hâ‚€ : k â‰« fst t = l â‰« fst t) (hâ‚ : k â‰« snd t = l â‰« snd t) :
  âˆ€ (j : walking_cospan), k â‰« t.Ï€.app j = l â‰« t.Ï€.app j
| (some walking_pair.left) := hâ‚€
| (some walking_pair.right) := hâ‚
| none := by rw [â† t.w inl, reassoc_of hâ‚€]

lemma is_limit.hom_ext {t : pullback_cone f g} (ht : is_limit t) {W : C} {k l : W âŸ¶ t.X}
  (hâ‚€ : k â‰« fst t = l â‰« fst t) (hâ‚ : k â‰« snd t = l â‰« snd t) : k = l :=
ht.hom_ext $ equalizer_ext _ hâ‚€ hâ‚

lemma mono_snd_of_is_pullback_of_mono {t : pullback_cone f g} (ht : is_limit t) [mono f] :
  mono t.snd :=
âŸ¨Î» W h k i, is_limit.hom_ext ht (by simp [â†cancel_mono f, t.condition, reassoc_of i]) iâŸ©

lemma mono_fst_of_is_pullback_of_mono {t : pullback_cone f g} (ht : is_limit t) [mono g] :
  mono t.fst :=
âŸ¨Î» W h k i, is_limit.hom_ext ht i (by simp [â†cancel_mono g, â†t.condition, reassoc_of i])âŸ©

/-- If `t` is a limit pullback cone over `f` and `g` and `h : W âŸ¶ X` and `k : W âŸ¶ Y` are such that
    `h â‰« f = k â‰« g`, then we have `l : W âŸ¶ t.X` satisfying `l â‰« fst t = h` and `l â‰« snd t = k`.
    -/
def is_limit.lift' {t : pullback_cone f g} (ht : is_limit t) {W : C} (h : W âŸ¶ X) (k : W âŸ¶ Y)
  (w : h â‰« f = k â‰« g) : {l : W âŸ¶ t.X // l â‰« fst t = h âˆ§ l â‰« snd t = k} :=
âŸ¨ht.lift $ pullback_cone.mk _ _ w, ht.fac _ _, ht.fac _ _âŸ©

/--
This is a more convenient formulation to show that a `pullback_cone` constructed using
`pullback_cone.mk` is a limit cone.
-/
def is_limit.mk {W : C} {fst : W âŸ¶ X} {snd : W âŸ¶ Y} (eq : fst â‰« f = snd â‰« g)
  (lift : Î  (s : pullback_cone f g), s.X âŸ¶ W)
  (fac_left : âˆ€ (s : pullback_cone f g), lift s â‰« fst = s.fst)
  (fac_right : âˆ€ (s : pullback_cone f g), lift s â‰« snd = s.snd)
  (uniq : âˆ€ (s : pullback_cone f g) (m : s.X âŸ¶ W)
    (w_fst : m â‰« fst = s.fst) (w_snd : m â‰« snd = s.snd), m = lift s) :
  is_limit (mk fst snd eq) :=
is_limit_aux _ lift fac_left fac_right
  (Î» s m w, uniq s m (w walking_cospan.left) (w walking_cospan.right))

/-- The flip of a pullback square is a pullback square. -/
def flip_is_limit {W : C} {h : W âŸ¶ X} {k : W âŸ¶ Y}
  {comm : h â‰« f = k â‰« g} (t : is_limit (mk _ _ comm.symm)) :
  is_limit (mk _ _ comm) :=
is_limit_aux' _ $ Î» s,
begin
  refine âŸ¨(is_limit.lift' t _ _ s.condition.symm).1,
          (is_limit.lift' t _ _ _).2.2,
          (is_limit.lift' t _ _ _).2.1, Î» m mâ‚ mâ‚‚, t.hom_ext _âŸ©,
  apply (mk k h _).equalizer_ext,
  { rwa (is_limit.lift' t _ _ _).2.1 },
  { rwa (is_limit.lift' t _ _ _).2.2 },
end

/--
The pullback cone `(ğŸ™ X, ğŸ™ X)` for the pair `(f, f)` is a limit if `f` is a mono. The converse is
shown in `mono_of_pullback_is_id`.
-/
def is_limit_mk_id_id (f : X âŸ¶ Y) [mono f] :
  is_limit (mk (ğŸ™ X) (ğŸ™ X) rfl : pullback_cone f f) :=
is_limit.mk _
  (Î» s, s.fst)
  (Î» s, category.comp_id _)
  (Î» s, by rw [â†cancel_mono f, category.comp_id, s.condition])
  (Î» s m mâ‚ mâ‚‚, by simpa using mâ‚)

/--
`f` is a mono if the pullback cone `(ğŸ™ X, ğŸ™ X)` is a limit for the pair `(f, f)`. The converse is
given in `pullback_cone.is_id_of_mono`.
-/
lemma mono_of_is_limit_mk_id_id (f : X âŸ¶ Y)
  (t : is_limit (mk (ğŸ™ X) (ğŸ™ X) rfl : pullback_cone f f)) :
  mono f :=
âŸ¨Î» Z g h eq, by { rcases pullback_cone.is_limit.lift' t _ _ eq with âŸ¨_, rfl, rflâŸ©, refl } âŸ©

/-- Suppose `f` and `g` are two morphisms with a common codomain and `s` is a limit cone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through a monomorphism `h` via
    `x` and `y`, respectively.  Then `s` is also a limit cone over the diagram formed by `x` and
    `y`.  -/
def is_limit_of_factors (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (h : W âŸ¶ Z) [mono h]
  (x : X âŸ¶ W) (y : Y âŸ¶ W) (hxh : x â‰« h = f) (hyh : y â‰« h = g) (s : pullback_cone f g)
  (hs : is_limit s) : is_limit (pullback_cone.mk _ _ (show s.fst â‰« x = s.snd â‰« y,
    from (cancel_mono h).1 $ by simp only [category.assoc, hxh, hyh, s.condition])) :=
pullback_cone.is_limit_aux' _ $ Î» t,
  âŸ¨hs.lift (pullback_cone.mk t.fst t.snd $ by rw [â†hxh, â†hyh, reassoc_of t.condition]),
  âŸ¨hs.fac _ walking_cospan.left, hs.fac _ walking_cospan.right, Î» r hr hr',
  begin
    apply pullback_cone.is_limit.hom_ext hs;
    simp only [pullback_cone.mk_fst, pullback_cone.mk_snd] at âŠ¢ hr hr';
    simp only [hr, hr'];
    symmetry,
    exacts [hs.fac _ walking_cospan.left, hs.fac _ walking_cospan.right]
  endâŸ©âŸ©

end pullback_cone

/-- A pushout cocone is just a cocone on the span formed by two morphisms `f : X âŸ¶ Y` and
    `g : X âŸ¶ Z`.-/
abbreviation pushout_cocone (f : X âŸ¶ Y) (g : X âŸ¶ Z) := cocone (span f g)

namespace pushout_cocone

variables {f : X âŸ¶ Y} {g : X âŸ¶ Z}

/-- The first inclusion of a pushout cocone. -/
abbreviation inl (t : pushout_cocone f g) : Y âŸ¶ t.X := t.Î¹.app walking_span.left

/-- The second inclusion of a pushout cocone. -/
abbreviation inr (t : pushout_cocone f g) : Z âŸ¶ t.X := t.Î¹.app walking_span.right

/-- This is a slightly more convenient method to verify that a pushout cocone is a colimit cocone.
    It only asks for a proof of facts that carry any mathematical content -/
def is_colimit_aux (t : pushout_cocone f g) (desc : Î  (s : pushout_cocone f g), t.X âŸ¶ s.X)
  (fac_left : âˆ€ (s : pushout_cocone f g), t.inl â‰« desc s = s.inl)
  (fac_right : âˆ€ (s : pushout_cocone f g), t.inr â‰« desc s = s.inr)
  (uniq : âˆ€ (s : pushout_cocone f g) (m : t.X âŸ¶ s.X)
    (w : âˆ€ j : walking_span, t.Î¹.app j â‰« m = s.Î¹.app j), m = desc s) :
  is_colimit t :=
{ desc := desc,
  fac' := Î» s j, option.cases_on j (by { simp [â† s.w fst, â† t.w fst, fac_left s] } )
                    (Î» j', walking_pair.cases_on j' (fac_left s) (fac_right s)),
  uniq' := uniq }

/-- This is another convenient method to verify that a pushout cocone is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def is_colimit_aux' (t : pushout_cocone f g)
  (create : Î  (s : pushout_cocone f g),
    {l // t.inl â‰« l = s.inl âˆ§ t.inr â‰« l = s.inr âˆ§
            âˆ€ {m}, t.inl â‰« m = s.inl â†’ t.inr â‰« m = s.inr â†’ m = l}) :
is_colimit t :=
is_colimit_aux t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s, (create s).2.2.1)
  (Î» s m w, (create s).2.2.2 (w walking_cospan.left) (w walking_cospan.right))

/-- A pushout cocone on `f` and `g` is determined by morphisms `inl : Y âŸ¶ W` and `inr : Z âŸ¶ W` such
    that `f â‰« inl = g â†  inr`. -/
@[simps]
def mk {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) : pushout_cocone f g :=
{ X := W,
  Î¹ := { app := Î» j, option.cases_on j (f â‰« inl) (Î» j', walking_pair.cases_on j' inl inr) } }

@[simp] lemma mk_Î¹_app_left {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app walking_span.left = inl := rfl
@[simp] lemma mk_Î¹_app_right {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app walking_span.right = inr := rfl
@[simp] lemma mk_Î¹_app_zero {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app walking_span.zero = f â‰« inl := rfl

@[simp] lemma mk_inl {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).inl = inl := rfl
@[simp] lemma mk_inr {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).inr = inr := rfl

@[reassoc] lemma condition (t : pushout_cocone f g) : f â‰« (inl t) = g â‰« (inr t) :=
(t.w fst).trans (t.w snd).symm

/-- To check whether a morphism is coequalized by the maps of a pushout cocone, it suffices to check
  it for `inl t` and `inr t` -/
lemma coequalizer_ext (t : pushout_cocone f g) {W : C} {k l : t.X âŸ¶ W}
  (hâ‚€ : inl t â‰« k = inl t â‰« l) (hâ‚ : inr t â‰« k = inr t â‰« l) :
  âˆ€ (j : walking_span), t.Î¹.app j â‰« k = t.Î¹.app j â‰« l
| (some walking_pair.left) := hâ‚€
| (some walking_pair.right) := hâ‚
| none := by rw [â† t.w fst, category.assoc, category.assoc, hâ‚€]

lemma is_colimit.hom_ext {t : pushout_cocone f g} (ht : is_colimit t) {W : C} {k l : t.X âŸ¶ W}
  (hâ‚€ : inl t â‰« k = inl t â‰« l) (hâ‚ : inr t â‰« k = inr t â‰« l) : k = l :=
ht.hom_ext $ coequalizer_ext _ hâ‚€ hâ‚

/-- If `t` is a colimit pushout cocone over `f` and `g` and `h : Y âŸ¶ W` and `k : Z âŸ¶ W` are
    morphisms satisfying `f â‰« h = g â‰« k`, then we have a factorization `l : t.X âŸ¶ W` such that
    `inl t â‰« l = h` and `inr t â‰« l = k`. -/
def is_colimit.desc' {t : pushout_cocone f g} (ht : is_colimit t) {W : C} (h : Y âŸ¶ W) (k : Z âŸ¶ W)
  (w : f â‰« h = g â‰« k) : {l : t.X âŸ¶ W // inl t â‰« l = h âˆ§ inr t â‰« l = k } :=
âŸ¨ht.desc $ pushout_cocone.mk _ _ w, ht.fac _ _, ht.fac _ _âŸ©

lemma epi_inr_of_is_pushout_of_epi {t : pushout_cocone f g} (ht : is_colimit t) [epi f] :
  epi t.inr :=
âŸ¨Î» W h k i, is_colimit.hom_ext ht (by simp [â†cancel_epi f, t.condition_assoc, i]) iâŸ©

lemma epi_inl_of_is_pushout_of_epi {t : pushout_cocone f g} (ht : is_colimit t) [epi g] :
  epi t.inl :=
âŸ¨Î» W h k i, is_colimit.hom_ext ht i (by simp [â†cancel_epi g, â†t.condition_assoc, i])âŸ©

/--
This is a more convenient formulation to show that a `pushout_cocone` constructed using
`pushout_cocone.mk` is a colimit cocone.
-/
def is_colimit.mk {W : C} {inl : Y âŸ¶ W} {inr : Z âŸ¶ W} (eq : f â‰« inl = g â‰« inr)
  (desc : Î  (s : pushout_cocone f g), W âŸ¶ s.X)
  (fac_left : âˆ€ (s : pushout_cocone f g), inl â‰« desc s = s.inl)
  (fac_right : âˆ€ (s : pushout_cocone f g), inr â‰« desc s = s.inr)
  (uniq : âˆ€ (s : pushout_cocone f g) (m : W âŸ¶ s.X)
    (w_inl : inl â‰« m = s.inl) (w_inr : inr â‰« m = s.inr), m = desc s) :
  is_colimit (mk inl inr eq) :=
is_colimit_aux _ desc fac_left fac_right
  (Î» s m w, uniq s m (w walking_cospan.left) (w walking_cospan.right))

/-- The flip of a pushout square is a pushout square. -/
def flip_is_colimit {W : C} {h : Y âŸ¶ W} {k : Z âŸ¶ W}
  {comm : f â‰« h = g â‰« k} (t : is_colimit (mk _ _ comm.symm)) :
  is_colimit (mk _ _ comm) :=
is_colimit_aux' _ $ Î» s,
begin
  refine âŸ¨(is_colimit.desc' t _ _ s.condition.symm).1,
          (is_colimit.desc' t _ _ _).2.2,
          (is_colimit.desc' t _ _ _).2.1, Î» m mâ‚ mâ‚‚, t.hom_ext _âŸ©,
  apply (mk k h _).coequalizer_ext,
  { rwa (is_colimit.desc' t _ _ _).2.1 },
  { rwa (is_colimit.desc' t _ _ _).2.2 },
end

/--
The pushout cocone `(ğŸ™ X, ğŸ™ X)` for the pair `(f, f)` is a colimit if `f` is an epi. The converse is
shown in `epi_of_is_colimit_mk_id_id`.
-/
def is_colimit_mk_id_id (f : X âŸ¶ Y) [epi f] :
  is_colimit (mk (ğŸ™ Y) (ğŸ™ Y) rfl : pushout_cocone f f) :=
is_colimit.mk _
  (Î» s, s.inl)
  (Î» s, category.id_comp _)
  (Î» s, by rw [â†cancel_epi f, category.id_comp, s.condition])
  (Î» s m mâ‚ mâ‚‚, by simpa using mâ‚)

/--
`f` is an epi if the pushout cocone `(ğŸ™ X, ğŸ™ X)` is a colimit for the pair `(f, f)`.
The converse is given in `pushout_cocone.is_colimit_mk_id_id`.
-/
lemma epi_of_is_colimit_mk_id_id (f : X âŸ¶ Y)
  (t : is_colimit (mk (ğŸ™ Y) (ğŸ™ Y) rfl : pushout_cocone f f)) :
  epi f :=
âŸ¨Î» Z g h eq, by { rcases pushout_cocone.is_colimit.desc' t _ _ eq with âŸ¨_, rfl, rflâŸ©, refl }âŸ©

/-- Suppose `f` and `g` are two morphisms with a common domain and `s` is a colimit cocone over the
    diagram formed by `f` and `g`. Suppose `f` and `g` both factor through an epimorphism `h` via
    `x` and `y`, respectively. Then `s` is also a colimit cocone over the diagram formed by `x` and
    `y`.  -/
def is_colimit_of_factors (f : X âŸ¶ Y) (g : X âŸ¶ Z) (h : X âŸ¶ W) [epi h]
  (x : W âŸ¶ Y) (y : W âŸ¶ Z) (hhx : h â‰« x = f) (hhy : h â‰« y = g) (s : pushout_cocone f g)
  (hs : is_colimit s) : is_colimit (pushout_cocone.mk _ _ (show x â‰« s.inl = y â‰« s.inr,
    from (cancel_epi h).1 $ by rw [reassoc_of hhx, reassoc_of hhy, s.condition])) :=
pushout_cocone.is_colimit_aux' _ $ Î» t,
  âŸ¨hs.desc (pushout_cocone.mk t.inl t.inr $
    by rw [â†hhx, â†hhy, category.assoc, category.assoc, t.condition]),
  âŸ¨hs.fac _ walking_span.left, hs.fac _ walking_span.right, Î» r hr hr',
  begin
    apply pushout_cocone.is_colimit.hom_ext hs;
    simp only [pushout_cocone.mk_inl, pushout_cocone.mk_inr] at âŠ¢ hr hr';
    simp only [hr, hr'];
    symmetry,
    exacts [hs.fac _ walking_span.left, hs.fac _ walking_span.right]
  endâŸ©âŸ©

end pushout_cocone

/-- This is a helper construction that can be useful when verifying that a category has all
    pullbacks. Given `F : walking_cospan â¥¤ C`, which is really the same as
    `cospan (F.map inl) (F.map inr)`, and a pullback cone on `F.map inl` and `F.map inr`, we
    get a cone on `F`.

    If you're thinking about using this, have a look at `has_pullbacks_of_has_limit_cospan`,
    which you may find to be an easier way of achieving your goal. -/
@[simps]
def cone.of_pullback_cone
  {F : walking_cospan â¥¤ C} (t : pullback_cone (F.map inl) (F.map inr)) : cone F :=
{ X := t.X,
  Ï€ := t.Ï€ â‰« (diagram_iso_cospan F).inv }

/-- This is a helper construction that can be useful when verifying that a category has all
    pushout. Given `F : walking_span â¥¤ C`, which is really the same as
    `span (F.map fst) (F.mal snd)`, and a pushout cocone on `F.map fst` and `F.map snd`,
    we get a cocone on `F`.

    If you're thinking about using this, have a look at `has_pushouts_of_has_colimit_span`, which
    you may find to be an easiery way of achieving your goal.  -/
@[simps]
def cocone.of_pushout_cocone
  {F : walking_span â¥¤ C} (t : pushout_cocone (F.map fst) (F.map snd)) : cocone F :=
{ X := t.X,
  Î¹ := (diagram_iso_span F).hom â‰« t.Î¹ }

/-- Given `F : walking_cospan â¥¤ C`, which is really the same as `cospan (F.map inl) (F.map inr)`,
    and a cone on `F`, we get a pullback cone on `F.map inl` and `F.map inr`. -/
@[simps]
def pullback_cone.of_cone
  {F : walking_cospan â¥¤ C} (t : cone F) : pullback_cone (F.map inl) (F.map inr) :=
{ X := t.X,
  Ï€ := t.Ï€ â‰« (diagram_iso_cospan F).hom }

/-- Given `F : walking_span â¥¤ C`, which is really the same as `span (F.map fst) (F.map snd)`,
    and a cocone on `F`, we get a pushout cocone on `F.map fst` and `F.map snd`. -/
@[simps]
def pushout_cocone.of_cocone
  {F : walking_span â¥¤ C} (t : cocone F) : pushout_cocone (F.map fst) (F.map snd) :=
{ X := t.X,
  Î¹ := (diagram_iso_span F).inv â‰« t.Î¹ }

/--
`has_pullback f g` represents a particular choice of limiting cone
for the pair of morphisms `f : X âŸ¶ Z` and `g : Y âŸ¶ Z`.
-/
abbreviation has_pullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) := has_limit (cospan f g)
/--
`has_pushout f g` represents a particular choice of colimiting cocone
for the pair of morphisms `f : X âŸ¶ Y` and `g : X âŸ¶ Z`.
-/
abbreviation has_pushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) := has_colimit (span f g)

/-- `pullback f g` computes the pullback of a pair of morphisms with the same target. -/
abbreviation pullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [has_pullback f g] :=
limit (cospan f g)
/-- `pushout f g` computes the pushout of a pair of morphisms with the same source. -/
abbreviation pushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [has_pushout f g] :=
colimit (span f g)

/-- The first projection of the pullback of `f` and `g`. -/
abbreviation pullback.fst {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g] :
  pullback f g âŸ¶ X :=
limit.Ï€ (cospan f g) walking_cospan.left

/-- The second projection of the pullback of `f` and `g`. -/
abbreviation pullback.snd {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g] :
  pullback f g âŸ¶ Y :=
limit.Ï€ (cospan f g) walking_cospan.right

/-- The first inclusion into the pushout of `f` and `g`. -/
abbreviation pushout.inl {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g] :
  Y âŸ¶ pushout f g :=
colimit.Î¹ (span f g) walking_span.left

/-- The second inclusion into the pushout of `f` and `g`. -/
abbreviation pushout.inr {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g] :
  Z âŸ¶ pushout f g :=
colimit.Î¹ (span f g) walking_span.right

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
    `pullback.lift : W âŸ¶ pullback f g`. -/
abbreviation pullback.lift {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : W âŸ¶ pullback f g :=
limit.lift _ (pullback_cone.mk h k w)

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
    `pushout.desc : pushout f g âŸ¶ W`. -/
abbreviation pushout.desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout f g âŸ¶ W :=
colimit.desc _ (pushout_cocone.mk h k w)

@[simp, reassoc]
lemma pullback.lift_fst {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.fst = h :=
limit.lift_Ï€ _ _

@[simp, reassoc]
lemma pullback.lift_snd {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.snd = k :=
limit.lift_Ï€ _ _

@[simp, reassoc]
lemma pushout.inl_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inl â‰« pushout.desc h k w = h :=
colimit.Î¹_desc _ _

@[simp, reassoc]
lemma pushout.inr_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inr â‰« pushout.desc h k w = k :=
colimit.Î¹_desc _ _

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
    `l : W âŸ¶ pullback f g` such that `l â‰« pullback.fst = h` and `l â‰« pullback.snd = k`. -/
def pullback.lift' {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) :
  {l : W âŸ¶ pullback f g // l â‰« pullback.fst = h âˆ§ l â‰« pullback.snd = k} :=
âŸ¨pullback.lift h k w, pullback.lift_fst _ _ _, pullback.lift_snd _ _ _âŸ©

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
    `l : pushout f g âŸ¶ W` such that `pushout.inl â‰« l = h` and `pushout.inr â‰« l = k`. -/
def pullback.desc' {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) :
  {l : pushout f g âŸ¶ W // pushout.inl â‰« l = h âˆ§ pushout.inr â‰« l = k} :=
âŸ¨pushout.desc h k w, pushout.inl_desc _ _ _, pushout.inr_desc _ _ _âŸ©

@[reassoc]
lemma pullback.condition {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g] :
  (pullback.fst : pullback f g âŸ¶ X) â‰« f = pullback.snd â‰« g :=
pullback_cone.condition _

@[reassoc]
lemma pushout.condition {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g] :
  f â‰« (pushout.inl : Y âŸ¶ pushout f g) = g â‰« pushout.inr :=
pushout_cocone.condition _

/-- Two morphisms into a pullback are equal if their compositions with the pullback morphisms are
    equal -/
@[ext] lemma pullback.hom_ext {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g]
  {W : C} {k l : W âŸ¶ pullback f g} (hâ‚€ : k â‰« pullback.fst = l â‰« pullback.fst)
  (hâ‚ : k â‰« pullback.snd = l â‰« pullback.snd) : k = l :=
limit.hom_ext $ pullback_cone.equalizer_ext _ hâ‚€ hâ‚

/-- The pullback cone built from the pullback projections is a pullback. -/
def pullback_is_pullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [has_pullback f g] :
  is_limit (pullback_cone.mk (pullback.fst : pullback f g âŸ¶ _) pullback.snd pullback.condition) :=
pullback_cone.is_limit.mk _ (Î» s, pullback.lift s.fst s.snd s.condition)
  (by simp) (by simp) (by tidy)

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.fst_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g]
  [mono g] : mono (pullback.fst : pullback f g âŸ¶ X) :=
pullback_cone.mono_fst_of_is_pullback_of_mono (limit.is_limit _)

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.snd_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_pullback f g]
  [mono f] : mono (pullback.snd : pullback f g âŸ¶ Y) :=
pullback_cone.mono_snd_of_is_pullback_of_mono (limit.is_limit _)

/-- Two morphisms out of a pushout are equal if their compositions with the pushout morphisms are
    equal -/
@[ext] lemma pushout.hom_ext {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g]
  {W : C} {k l : pushout f g âŸ¶ W} (hâ‚€ : pushout.inl â‰« k = pushout.inl â‰« l)
  (hâ‚ : pushout.inr â‰« k = pushout.inr â‰« l) : k = l :=
colimit.hom_ext $ pushout_cocone.coequalizer_ext _ hâ‚€ hâ‚

/-- The pushout cocone built from the pushout coprojections is a pushout. -/
def pushout_is_pushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [has_pushout f g] :
  is_colimit (pushout_cocone.mk (pushout.inl : _ âŸ¶ pushout f g) pushout.inr pushout.condition) :=
pushout_cocone.is_colimit.mk _ (Î» s, pushout.desc s.inl s.inr s.condition)
  (by simp) (by simp) (by tidy)

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inl_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g] [epi g] :
  epi (pushout.inl : Y âŸ¶ pushout f g) :=
pushout_cocone.epi_inl_of_is_pushout_of_epi (colimit.is_colimit _)

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inr_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_pushout f g] [epi f] :
  epi (pushout.inr : Z âŸ¶ pushout f g) :=
pushout_cocone.epi_inr_of_is_pushout_of_epi (colimit.is_colimit _)

section

variables {D : Type uâ‚‚} [category.{v} D] (G : C â¥¤ D)

/--
The comparison morphism for the pullback of `f,g`.
This is an isomorphism iff `G` preserves the pullback of `f,g`; see
`category_theory/limits/preserves/shapes/pullbacks.lean`
-/
def pullback_comparison (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
  [has_pullback f g] [has_pullback (G.map f) (G.map g)] :
  G.obj (pullback f g) âŸ¶ pullback (G.map f) (G.map g) :=
pullback.lift (G.map pullback.fst) (G.map pullback.snd)
  (by simp only [â†G.map_comp, pullback.condition])

@[simp, reassoc]
lemma pullback_comparison_comp_fst (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
  [has_pullback f g] [has_pullback (G.map f) (G.map g)] :
  pullback_comparison G f g â‰« pullback.fst = G.map pullback.fst :=
pullback.lift_fst _ _ _

@[simp, reassoc]
lemma pullback_comparison_comp_snd (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
  [has_pullback f g] [has_pullback (G.map f) (G.map g)] :
  pullback_comparison G f g â‰« pullback.snd = G.map pullback.snd :=
pullback.lift_snd _ _ _

@[simp, reassoc]
lemma map_lift_pullback_comparison (f : X âŸ¶ Z) (g : Y âŸ¶ Z)
  [has_pullback f g] [has_pullback (G.map f) (G.map g)]
  {W : C} {h : W âŸ¶ X} {k : W âŸ¶ Y} (w : h â‰« f = k â‰« g) :
    G.map (pullback.lift _ _ w) â‰« pullback_comparison G f g =
      pullback.lift (G.map h) (G.map k) (by simp only [â†G.map_comp, w]) :=
by { ext; simp [â† G.map_comp] }

end

section pullback_symmetry

open walking_cospan

variables (f : X âŸ¶ Z) (g : Y âŸ¶ Z)

/-- Making this a global instance would make the typeclass seach go in an infinite loop. -/
lemma has_pullback_symmetry [has_pullback f g] : has_pullback g f :=
âŸ¨âŸ¨âŸ¨pullback_cone.mk _ _ pullback.condition.symm,
  pullback_cone.flip_is_limit (pullback_is_pullback _ _)âŸ©âŸ©âŸ©

local attribute [instance] has_pullback_symmetry

/-- The isomorphism `X Ã—[Z] Y â‰… Y Ã—[Z] X`. -/
def pullback_symmetry [has_pullback f g] :
  pullback f g â‰… pullback g f :=
is_limit.cone_point_unique_up_to_iso
  (pullback_cone.flip_is_limit (pullback_is_pullback f g) :
    is_limit (pullback_cone.mk _ _ pullback.condition.symm))
  (limit.is_limit _)

@[simp, reassoc] lemma pullback_symmetry_hom_comp_fst [has_pullback f g] :
  (pullback_symmetry f g).hom â‰« pullback.fst = pullback.snd := by simp [pullback_symmetry]

@[simp, reassoc] lemma pullback_symmetry_hom_comp_snd [has_pullback f g] :
  (pullback_symmetry f g).hom â‰« pullback.snd = pullback.fst := by simp [pullback_symmetry]

@[simp, reassoc] lemma pullback_symmetry_inv_comp_fst [has_pullback f g] :
  (pullback_symmetry f g).inv â‰« pullback.fst = pullback.snd := by simp [iso.inv_comp_eq]

@[simp, reassoc] lemma pullback_symmetry_inv_comp_snd [has_pullback f g] :
  (pullback_symmetry f g).inv â‰« pullback.snd = pullback.fst := by simp [iso.inv_comp_eq]

end pullback_symmetry

section pushout_symmetry

open walking_cospan

variables (f : X âŸ¶ Y) (g : X âŸ¶ Z)

/-- Making this a global instance would make the typeclass seach go in an infinite loop. -/
lemma has_pushout_symmetry [has_pushout f g] : has_pushout g f :=
âŸ¨âŸ¨âŸ¨pushout_cocone.mk _ _ pushout.condition.symm,
  pushout_cocone.flip_is_colimit (pushout_is_pushout _ _)âŸ©âŸ©âŸ©

local attribute [instance] has_pushout_symmetry

/-- The isomorphism `Y â¨¿[X] Z â‰… Z â¨¿[X] Y`. -/
def pushout_symmetry [has_pushout f g] :
  pushout f g â‰… pushout g f :=
is_colimit.cocone_point_unique_up_to_iso
  (pushout_cocone.flip_is_colimit (pushout_is_pushout f g) :
    is_colimit (pushout_cocone.mk _ _ pushout.condition.symm))
  (colimit.is_colimit _)

@[simp, reassoc] lemma inl_comp_pushout_symmetry_hom [has_pushout f g] :
  pushout.inl â‰« (pushout_symmetry f g).hom = pushout.inr :=
(colimit.is_colimit (span f g)).comp_cocone_point_unique_up_to_iso_hom
  (pushout_cocone.flip_is_colimit (pushout_is_pushout g f)) _

@[simp, reassoc] lemma inr_comp_pushout_symmetry_hom [has_pushout f g] :
  pushout.inr â‰« (pushout_symmetry f g).hom = pushout.inl :=
(colimit.is_colimit (span f g)).comp_cocone_point_unique_up_to_iso_hom
  (pushout_cocone.flip_is_colimit (pushout_is_pushout g f)) _

@[simp, reassoc] lemma inl_comp_pushout_symmetry_inv [has_pushout f g] :
  pushout.inl â‰« (pushout_symmetry f g).inv = pushout.inr := by simp [iso.comp_inv_eq]

@[simp, reassoc] lemma inr_comp_pushout_symmetry_inv [has_pushout f g] :
  pushout.inr â‰« (pushout_symmetry f g).inv = pushout.inl := by simp [iso.comp_inv_eq]

end pushout_symmetry

section pullback_left_iso

open walking_cospan

variables (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [is_iso f]

/-- If `f : X âŸ¶ Z` is iso, then `X Ã—[Z] Y â‰… Y`. This is the explicit limit cone. -/
def pullback_cone_of_left_iso : pullback_cone f g :=
pullback_cone.mk (g â‰« inv f) (ğŸ™ _) $ by simp

@[simp] lemma pullback_cone_of_left_iso_X :
  (pullback_cone_of_left_iso f g).X = Y := rfl

@[simp] lemma pullback_cone_of_left_iso_fst :
  (pullback_cone_of_left_iso f g).fst = g â‰« inv f := rfl

@[simp] lemma pullback_cone_of_left_iso_snd :
  (pullback_cone_of_left_iso f g).snd = ğŸ™ _ := rfl

@[simp] lemma pullback_cone_of_left_iso_Ï€_app_none :
  (pullback_cone_of_left_iso f g).Ï€.app none = g := by { delta pullback_cone_of_left_iso, simp }

@[simp] lemma pullback_cone_of_left_iso_Ï€_app_left :
  (pullback_cone_of_left_iso f g).Ï€.app left = g â‰« inv f := rfl

@[simp] lemma pullback_cone_of_left_iso_Ï€_app_right :
  (pullback_cone_of_left_iso f g).Ï€.app right = ğŸ™ _ := rfl

/-- Verify that the constructed limit cone is indeed a limit. -/
def pullback_cone_of_left_iso_is_limit :
  is_limit (pullback_cone_of_left_iso f g) :=
pullback_cone.is_limit_aux' _ (Î» s, âŸ¨s.snd, by simp [â† s.condition_assoc]âŸ©)

lemma has_pullback_of_left_iso : has_pullback f g :=
âŸ¨âŸ¨âŸ¨_, pullback_cone_of_left_iso_is_limit f gâŸ©âŸ©âŸ©

local attribute [instance] has_pullback_of_left_iso

instance pullback_snd_iso_of_left_iso : is_iso (pullback.snd : pullback f g âŸ¶ _) :=
begin
  refine âŸ¨âŸ¨pullback.lift (g â‰« inv f) (ğŸ™ _) (by simp), _, by simpâŸ©âŸ©,
  ext,
  { simp [â† pullback.condition_assoc] },
  { simp [pullback.condition_assoc] },
end

end pullback_left_iso

section pullback_right_iso

open walking_cospan

variables (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [is_iso g]

/-- If `g : Y âŸ¶ Z` is iso, then `X Ã—[Z] Y â‰… X`. This is the explicit limit cone. -/
def pullback_cone_of_right_iso : pullback_cone f g :=
pullback_cone.mk (ğŸ™ _) (f â‰« inv g) $ by simp

@[simp] lemma pullback_cone_of_right_iso_X :
  (pullback_cone_of_right_iso f g).X = X := rfl

@[simp] lemma pullback_cone_of_right_iso_fst :
  (pullback_cone_of_right_iso f g).fst = ğŸ™ _ := rfl

@[simp] lemma pullback_cone_of_right_iso_snd :
  (pullback_cone_of_right_iso f g).snd = f â‰« inv g := rfl

@[simp] lemma pullback_cone_of_right_iso_Ï€_app_none :
  (pullback_cone_of_right_iso f g).Ï€.app none = f := category.id_comp _

@[simp] lemma pullback_cone_of_right_iso_Ï€_app_left :
  (pullback_cone_of_right_iso f g).Ï€.app left = ğŸ™ _ := rfl

@[simp] lemma pullback_cone_of_right_iso_Ï€_app_right :
  (pullback_cone_of_right_iso f g).Ï€.app right = f â‰« inv g := rfl

/-- Verify that the constructed limit cone is indeed a limit. -/
def pullback_cone_of_right_iso_is_limit :
  is_limit (pullback_cone_of_right_iso f g) :=
pullback_cone.is_limit_aux' _ (Î» s, âŸ¨s.fst, by simp [s.condition_assoc]âŸ©)

lemma has_pullback_of_right_iso : has_pullback f g :=
âŸ¨âŸ¨âŸ¨_, pullback_cone_of_right_iso_is_limit f gâŸ©âŸ©âŸ©

local attribute [instance] has_pullback_of_right_iso

instance pullback_snd_iso_of_right_iso : is_iso (pullback.fst : pullback f g âŸ¶ _) :=
begin
  refine âŸ¨âŸ¨pullback.lift (ğŸ™ _) (f â‰« inv g) (by simp), _, by simpâŸ©âŸ©,
  ext,
  { simp },
  { simp [pullback.condition_assoc] },
end

end pullback_right_iso

section pushout_left_iso

open walking_span

variables (f : X âŸ¶ Y) (g : X âŸ¶ Z) [is_iso f]

/-- If `f : X âŸ¶ Y` is iso, then `Y â¨¿[X] Z â‰… Z`. This is the explicit colimit cocone. -/
def pushout_cocone_of_left_iso : pushout_cocone f g :=
pushout_cocone.mk (inv f â‰« g) (ğŸ™ _) $ by simp

@[simp] lemma pushout_cocone_of_left_iso_X :
  (pushout_cocone_of_left_iso f g).X = Z := rfl

@[simp] lemma pushout_cocone_of_left_iso_inl :
  (pushout_cocone_of_left_iso f g).inl = inv f â‰« g := rfl

@[simp] lemma pushout_cocone_of_left_iso_inr :
  (pushout_cocone_of_left_iso f g).inr = ğŸ™ _ := rfl

@[simp] lemma pushout_cocone_of_left_iso_Î¹_app_none :
  (pushout_cocone_of_left_iso f g).Î¹.app none = g := by { delta pushout_cocone_of_left_iso, simp }

@[simp] lemma pushout_cocone_of_left_iso_Î¹_app_left :
  (pushout_cocone_of_left_iso f g).Î¹.app left = inv f â‰« g := rfl

@[simp] lemma pushout_cocone_of_left_iso_Î¹_app_right :
  (pushout_cocone_of_left_iso f g).Î¹.app right = ğŸ™ _ := rfl

/-- Verify that the constructed cocone is indeed a colimit. -/
def pushout_cocone_of_left_iso_is_limit :
  is_colimit (pushout_cocone_of_left_iso f g) :=
pushout_cocone.is_colimit_aux' _ (Î» s, âŸ¨s.inr, by simp [â† s.condition]âŸ©)

lemma has_pushout_of_left_iso : has_pushout f g :=
âŸ¨âŸ¨âŸ¨_, pushout_cocone_of_left_iso_is_limit f gâŸ©âŸ©âŸ©

local attribute [instance] has_pushout_of_left_iso

instance pushout_inr_iso_of_left_iso : is_iso (pushout.inr : _ âŸ¶ pushout f g) :=
begin
  refine âŸ¨âŸ¨pushout.desc (inv f â‰« g) (ğŸ™ _) (by simp), (by simp), _âŸ©âŸ©,
  ext,
  { simp [â† pushout.condition] },
  { simp [pushout.condition_assoc] },
end

end pushout_left_iso

section pushout_right_iso

open walking_span

variables (f : X âŸ¶ Y) (g : X âŸ¶ Z) [is_iso g]

/-- If `f : X âŸ¶ Z` is iso, then `Y â¨¿[X] Z â‰… Y`. This is the explicit colimit cocone. -/
def pushout_cocone_of_right_iso : pushout_cocone f g :=
pushout_cocone.mk (ğŸ™ _) (inv g â‰« f) $ by simp

@[simp] lemma pushout_cocone_of_right_iso_X :
  (pushout_cocone_of_right_iso f g).X = Y := rfl

@[simp] lemma pushout_cocone_of_right_iso_inl :
  (pushout_cocone_of_right_iso f g).inl = ğŸ™ _ := rfl

@[simp] lemma pushout_cocone_of_right_iso_inr :
  (pushout_cocone_of_right_iso f g).inr = inv g â‰« f := rfl

@[simp] lemma pushout_cocone_of_right_iso_Î¹_app_none :
  (pushout_cocone_of_right_iso f g).Î¹.app none = f := by { delta pushout_cocone_of_right_iso, simp }

@[simp] lemma pushout_cocone_of_right_iso_Î¹_app_left :
  (pushout_cocone_of_right_iso f g).Î¹.app left = ğŸ™ _ := rfl

@[simp] lemma pushout_cocone_of_right_iso_Î¹_app_right :
  (pushout_cocone_of_right_iso f g).Î¹.app right = inv g â‰« f := rfl

/-- Verify that the constructed cocone is indeed a colimit. -/
def pushout_cocone_of_right_iso_is_limit :
  is_colimit (pushout_cocone_of_right_iso f g) :=
pushout_cocone.is_colimit_aux' _ (Î» s, âŸ¨s.inl, by simp [â†s.condition]âŸ©)

lemma has_pushout_of_right_iso : has_pushout f g :=
âŸ¨âŸ¨âŸ¨_, pushout_cocone_of_right_iso_is_limit f gâŸ©âŸ©âŸ©

local attribute [instance] has_pushout_of_right_iso

instance pushout_inl_iso_of_right_iso : is_iso (pushout.inl : _ âŸ¶ pushout f g) :=
begin
  refine âŸ¨âŸ¨pushout.desc (ğŸ™ _) (inv g â‰« f) (by simp), (by simp), _âŸ©âŸ©,
  ext,
  { simp [â†pushout.condition] },
  { simp [pushout.condition] },
end

end pushout_right_iso

section

open walking_cospan

variable (f : X âŸ¶ Y)

instance has_kernel_pair_of_mono [mono f] : has_pullback f f :=
âŸ¨âŸ¨âŸ¨_, pullback_cone.is_limit_mk_id_id fâŸ©âŸ©âŸ©

lemma fst_eq_snd_of_mono_eq [mono f] : (pullback.fst : pullback f f âŸ¶ _) = pullback.snd :=
((pullback_cone.is_limit_mk_id_id f).fac (get_limit_cone (cospan f f)).cone left).symm.trans
  ((pullback_cone.is_limit_mk_id_id f).fac (get_limit_cone (cospan f f)).cone right : _)

@[simp] lemma pullback_symmetry_hom_of_mono_eq [mono f] :
  (pullback_symmetry f f).hom = ğŸ™ _ := by ext; simp [fst_eq_snd_of_mono_eq]

instance fst_iso_of_mono_eq [mono f] : is_iso (pullback.fst : pullback f f âŸ¶ _) :=
begin
  refine âŸ¨âŸ¨pullback.lift (ğŸ™ _) (ğŸ™ _) (by simp), _, by simpâŸ©âŸ©,
  ext,
  { simp },
  { simp [fst_eq_snd_of_mono_eq] }
end

instance snd_iso_of_mono_eq [mono f] : is_iso (pullback.snd : pullback f f âŸ¶ _) :=
by { rw â† fst_eq_snd_of_mono_eq, apply_instance }

end

section

open walking_span

variable (f : X âŸ¶ Y)

instance has_cokernel_pair_of_epi [epi f] : has_pushout f f :=
âŸ¨âŸ¨âŸ¨_, pushout_cocone.is_colimit_mk_id_id fâŸ©âŸ©âŸ©

lemma inl_eq_inr_of_epi_eq [epi f] : (pushout.inl : _ âŸ¶ pushout f f) = pushout.inr :=
((pushout_cocone.is_colimit_mk_id_id f).fac (get_colimit_cocone (span f f)).cocone left).symm.trans
  ((pushout_cocone.is_colimit_mk_id_id f).fac (get_colimit_cocone (span f f)).cocone right : _)

@[simp] lemma pullback_symmetry_hom_of_epi_eq [epi f] :
  (pushout_symmetry f f).hom = ğŸ™ _ := by ext; simp [inl_eq_inr_of_epi_eq]

instance inl_iso_of_epi_eq [epi f] : is_iso (pushout.inl : _ âŸ¶ pushout f f) :=
begin
  refine âŸ¨âŸ¨pushout.desc (ğŸ™ _) (ğŸ™ _) (by simp), by simp, _âŸ©âŸ©,
  ext,
  { simp },
  { simp [inl_eq_inr_of_epi_eq] }
end

instance inr_iso_of_epi_eq [epi f] : is_iso (pushout.inr : _ âŸ¶ pushout f f) :=
by { rw â† inl_eq_inr_of_epi_eq, apply_instance }

end

variables (C)

/--
`has_pullbacks` represents a choice of pullback for every pair of morphisms

See https://stacks.math.columbia.edu/tag/001W.
-/
abbreviation has_pullbacks := has_limits_of_shape walking_cospan C

/-- `has_pushouts` represents a choice of pushout for every pair of morphisms -/
abbreviation has_pushouts := has_colimits_of_shape walking_span C

/-- If `C` has all limits of diagrams `cospan f g`, then it has all pullbacks -/
lemma has_pullbacks_of_has_limit_cospan
  [Î  {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z}, has_limit (cospan f g)] :
  has_pullbacks C :=
{ has_limit := Î» F, has_limit_of_iso (diagram_iso_cospan F).symm }

/-- If `C` has all colimits of diagrams `span f g`, then it has all pushouts -/
lemma has_pushouts_of_has_colimit_span
  [Î  {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z}, has_colimit (span f g)] :
  has_pushouts C :=
{ has_colimit := Î» F, has_colimit_of_iso (diagram_iso_span F) }

end category_theory.limits
