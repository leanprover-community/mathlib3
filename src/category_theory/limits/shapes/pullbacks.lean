/-
Copyright (c) 2018 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel, Bhavik Mehta
-/
import category_theory.limits.shapes.finite_limits
import category_theory.limits.shapes.wide_pullbacks
import category_theory.limits.shapes.binary_products
import category_theory.sparse

/-!
# Pullbacks

We define a category `walking_cospan` (resp. `walking_span`), which is the index category
for the given data for a pullback (resp. pushout) diagram. Convenience methods `cospan f g`
and `span f g` construct functors from the walking (co)span, hitting the given morphisms.

We define `pullback f g` and `pushout f g` as limits and colimits of such functors.

Typeclasses `has_pullbacks` and `has_pushouts` assert the existence of (co)limits shaped as
walking (co)spans.
-/

open category_theory

namespace category_theory.limits

universes v u

local attribute [tidy] tactic.case_bash

/--
The type of objects for the diagram indexing a pullback, defined as a special case of
`wide_pullback_shape`.
-/
abbreviation walking_cospan : Type v := wide_pullback_shape walking_pair

/-- The left point of the walking cospan. -/
abbreviation walking_cospan.left : walking_cospan := some walking_pair.left
/-- The right point of the walking cospan. -/
abbreviation walking_cospan.right : walking_cospan := some walking_pair.right
/-- The central point of the walking cospan. -/
abbreviation walking_cospan.one : walking_cospan := none

/--
The type of objects for the diagram indexing a pushout, defined as a special case of
`wide_pushout_shape`.
-/
abbreviation walking_span : Type v := wide_pushout_shape walking_pair

/-- The left point of the walking span. -/
abbreviation walking_span.left : walking_span := some walking_pair.left
/-- The right point of the walking span. -/
abbreviation walking_span.right : walking_span := some walking_pair.right
/-- The central point of the walking span. -/
abbreviation walking_span.zero : walking_span := none

namespace walking_cospan

/-- The type of arrows for the diagram indexing a pullback. -/
abbreviation hom : walking_cospan â†’ walking_cospan â†’ Type v := wide_pullback_shape.hom

/-- The left arrow of the walking cospan. -/
abbreviation hom.inl : left âŸ¶ one := wide_pullback_shape.hom.term _
/-- The right arrow of the walking cospan. -/
abbreviation hom.inr : right âŸ¶ one := wide_pullback_shape.hom.term _
/-- The identity arrows of the walking cospan. -/
abbreviation hom.id (X : walking_cospan) : X âŸ¶ X := wide_pullback_shape.hom.id X

instance (X Y : walking_cospan) : subsingleton (X âŸ¶ Y) := by tidy

end walking_cospan

namespace walking_span

/-- The type of arrows for the diagram indexing a pushout. -/
abbreviation hom : walking_span â†’ walking_span â†’ Type v := wide_pushout_shape.hom

/-- The left arrow of the walking span. -/
abbreviation hom.fst : zero âŸ¶ left := wide_pushout_shape.hom.init _
/-- The right arrow of the walking span. -/
abbreviation hom.snd : zero âŸ¶ right := wide_pushout_shape.hom.init _
/-- The identity arrows of the walking span. -/
abbreviation hom.id (X : walking_span) : X âŸ¶ X := wide_pushout_shape.hom.id X

instance (X Y : walking_span) : subsingleton (X âŸ¶ Y) := by tidy

end walking_span

open walking_span.hom walking_cospan.hom wide_pullback_shape.hom wide_pushout_shape.hom

variables {C : Type u} [category.{v} C]

/-- `cospan f g` is the functor from the walking cospan hitting `f` and `g`. -/
def cospan {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) : walking_cospan â¥¤ C :=
wide_pullback_shape.wide_cospan Z (Î» j, walking_pair.cases_on j X Y) (Î» j, walking_pair.cases_on j f g)

/-- `span f g` is the functor from the walking span hitting `f` and `g`. -/
def span {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) : walking_span â¥¤ C :=
wide_pushout_shape.wide_span X (Î» j, walking_pair.cases_on j Y Z) (Î» j, walking_pair.cases_on j f g)

@[simp] lemma cospan_left {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.left = X := rfl
@[simp] lemma span_left {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.left = Y := rfl

@[simp] lemma cospan_right {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.right = Y := rfl
@[simp] lemma span_right {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.right = Z := rfl

@[simp] lemma cospan_one {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).obj walking_cospan.one = Z := rfl
@[simp] lemma span_zero {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).obj walking_span.zero = X := rfl

@[simp] lemma cospan_map_inl {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).map walking_cospan.hom.inl = f := rfl
@[simp] lemma span_map_fst {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).map walking_span.hom.fst = f := rfl

@[simp] lemma cospan_map_inr {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) :
  (cospan f g).map walking_cospan.hom.inr = g := rfl
@[simp] lemma span_map_snd {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) :
  (span f g).map walking_span.hom.snd = g := rfl

lemma cospan_map_id {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) (w : walking_cospan) :
  (cospan f g).map (walking_cospan.hom.id w) = ğŸ™ _ := rfl
lemma span_map_id {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) (w : walking_span) :
  (span f g).map (walking_span.hom.id w) = ğŸ™ _ := rfl

/-- Every diagram indexing an pullback is naturally isomorphic (actually, equal) to a `cospan` -/
def diagram_iso_cospan (F : walking_cospan â¥¤ C) :
  F â‰… cospan (F.map inl) (F.map inr) :=
nat_iso.of_components (Î» j, eq_to_iso (by tidy)) (by tidy)

/-- Every diagram indexing a pushout is naturally isomorphic (actually, equal) to a `span` -/
def diagram_iso_span (F : walking_span â¥¤ C) :
  F â‰… span (F.map fst) (F.map snd) :=
nat_iso.of_components (Î» j, eq_to_iso (by tidy)) (by tidy)

variables {X Y Z : C}

/-- A pullback cone is just a cone on the cospan formed by two morphisms `f : X âŸ¶ Z` and
    `g : Y âŸ¶ Z`.-/
abbreviation pullback_cone (f : X âŸ¶ Z) (g : Y âŸ¶ Z) := cone (cospan f g)

namespace pullback_cone
variables {f : X âŸ¶ Z} {g : Y âŸ¶ Z}

/-- The first projection of a pullback cone. -/
abbreviation fst (t : pullback_cone f g) : t.X âŸ¶ X := t.Ï€.app walking_cospan.left

/-- The second projection of a pullback cone. -/
abbreviation snd (t : pullback_cone f g) : t.X âŸ¶ Y := t.Ï€.app walking_cospan.right

/-- A pullback cone on `f` and `g` is determined by morphisms `fst : W âŸ¶ X` and `snd : W âŸ¶ Y`
    such that `fst â‰« f = snd â‰« g`. -/
@[simps]
def mk {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) : pullback_cone f g :=
{ X := W,
  Ï€ := { app := Î» j, option.cases_on j (fst â‰« f) (Î» j', walking_pair.cases_on j' fst snd) } }

@[simp] lemma mk_Ï€_app_left {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app walking_cospan.left = fst := rfl
@[simp] lemma mk_Ï€_app_right {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app walking_cospan.right = snd := rfl
@[simp] lemma mk_Ï€_app_one {W : C} (fst : W âŸ¶ X) (snd : W âŸ¶ Y) (eq : fst â‰« f = snd â‰« g) :
  (mk fst snd eq).Ï€.app walking_cospan.one = fst â‰« f := rfl

@[reassoc] lemma condition (t : pullback_cone f g) : fst t â‰« f = snd t â‰« g :=
(t.w inl).trans (t.w inr).symm

/-- To check whether a morphism is equalized by the maps of a pullback cone, it suffices to check
  it for `fst t` and `snd t` -/
lemma equalizer_ext (t : pullback_cone f g) {W : C} {k l : W âŸ¶ t.X}
  (hâ‚€ : k â‰« fst t = l â‰« fst t) (hâ‚ : k â‰« snd t = l â‰« snd t) :
  âˆ€ (j : walking_cospan), k â‰« t.Ï€.app j = l â‰« t.Ï€.app j
| (some walking_pair.left) := hâ‚€
| (some walking_pair.right) := hâ‚
| none := by rw [â† t.w inl, reassoc_of hâ‚€]

lemma is_limit.hom_ext {t : pullback_cone f g} (ht : is_limit t) {W : C} {k l : W âŸ¶ t.X}
  (hâ‚€ : k â‰« fst t = l â‰« fst t) (hâ‚ : k â‰« snd t = l â‰« snd t) : k = l :=
ht.hom_ext $ equalizer_ext _ hâ‚€ hâ‚

/-- If `t` is a limit pullback cone over `f` and `g` and `h : W âŸ¶ X` and `k : W âŸ¶ Y` are such that
    `h â‰« f = k â‰« g`, then we have `l : W âŸ¶ t.X` satisfying `l â‰« fst t = h` and `l â‰« snd t = k`.
    -/
def is_limit.lift' {t : pullback_cone f g} (ht : is_limit t) {W : C} (h : W âŸ¶ X) (k : W âŸ¶ Y)
  (w : h â‰« f = k â‰« g) : {l : W âŸ¶ t.X // l â‰« fst t = h âˆ§ l â‰« snd t = k} :=
âŸ¨ht.lift $ pullback_cone.mk _ _ w, ht.fac _ _, ht.fac _ _âŸ©

/-- This is a slightly more convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def is_limit.mk (t : pullback_cone f g) (lift : Î  (s : cone (cospan f g)), s.X âŸ¶ t.X)
  (fac_left : âˆ€ (s : cone (cospan f g)), lift s â‰« t.Ï€.app walking_cospan.left = s.Ï€.app walking_cospan.left)
  (fac_right : âˆ€ (s : cone (cospan f g)), lift s â‰« t.Ï€.app walking_cospan.right = s.Ï€.app walking_cospan.right)
  (uniq : âˆ€ (s : cone (cospan f g)) (m : s.X âŸ¶ t.X)
    (w : âˆ€ j : walking_cospan, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) :
  is_limit t :=
{ lift := lift,
  fac' := Î» s j, option.cases_on j
    (by { simp [â† s.w inl, â† t.w inl, â† fac_left s] } )
    (Î» j', walking_pair.cases_on j' (fac_left s) (fac_right s)),
  uniq' := uniq }

/-- This is another convenient method to verify that a pullback cone is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def is_limit.mk' (t : pullback_cone f g)
  (create : Î  (s : pullback_cone f g),
    {l // l â‰« t.fst = s.fst âˆ§ l â‰« t.snd = s.snd âˆ§ âˆ€ {m}, m â‰« t.fst = s.fst â†’ m â‰« t.snd = s.snd â†’ m = l}) :
is_limit t :=
pullback_cone.is_limit.mk t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s, (create s).2.2.1)
  (Î» s m w, (create s).2.2.2 (w walking_cospan.left) (w walking_cospan.right))

/-- The flip of a pullback square is a pullback square. -/
def flip_is_limit {W : C} {h : W âŸ¶ X} {k : W âŸ¶ Y}
  {comm : h â‰« f = k â‰« g} (t : is_limit (mk _ _ comm.symm)) :
  is_limit (mk _ _ comm) :=
is_limit.mk' _ $ Î» s,
begin
  refine âŸ¨(is_limit.lift' t _ _ s.condition.symm).1,
          (is_limit.lift' t _ _ _).2.2,
          (is_limit.lift' t _ _ _).2.1, Î» m mâ‚ mâ‚‚, t.hom_ext _âŸ©,
  apply (mk k h _).equalizer_ext,
  { rwa (is_limit.lift' t _ _ _).2.1 },
  { rwa (is_limit.lift' t _ _ _).2.2 },
end

end pullback_cone

/-- A pushout cocone is just a cocone on the span formed by two morphisms `f : X âŸ¶ Y` and
    `g : X âŸ¶ Z`.-/
abbreviation pushout_cocone (f : X âŸ¶ Y) (g : X âŸ¶ Z) := cocone (span f g)

namespace pushout_cocone

variables {f : X âŸ¶ Y} {g : X âŸ¶ Z}

/-- The first inclusion of a pushout cocone. -/
abbreviation inl (t : pushout_cocone f g) : Y âŸ¶ t.X := t.Î¹.app walking_span.left

/-- The second inclusion of a pushout cocone. -/
abbreviation inr (t : pushout_cocone f g) : Z âŸ¶ t.X := t.Î¹.app walking_span.right

/-- A pushout cocone on `f` and `g` is determined by morphisms `inl : Y âŸ¶ W` and `inr : Z âŸ¶ W` such
    that `f â‰« inl = g â†  inr`. -/
@[simps]
def mk {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) : pushout_cocone f g :=
{ X := W,
  Î¹ := { app := Î» j, option.cases_on j (f â‰« inl) (Î» j', walking_pair.cases_on j' inl inr) } }

@[simp] lemma mk_Î¹_app_left {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app walking_span.left = inl := rfl
@[simp] lemma mk_Î¹_app_right {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app walking_span.right = inr := rfl
@[simp] lemma mk_Î¹_app_zero {W : C} (inl : Y âŸ¶ W) (inr : Z âŸ¶ W) (eq : f â‰« inl = g â‰« inr) :
  (mk inl inr eq).Î¹.app walking_span.zero = f â‰« inl := rfl

@[reassoc] lemma condition (t : pushout_cocone f g) : f â‰« (inl t) = g â‰« (inr t) :=
(t.w fst).trans (t.w snd).symm

/-- To check whether a morphism is coequalized by the maps of a pushout cocone, it suffices to check
  it for `inl t` and `inr t` -/
lemma coequalizer_ext (t : pushout_cocone f g) {W : C} {k l : t.X âŸ¶ W}
  (hâ‚€ : inl t â‰« k = inl t â‰« l) (hâ‚ : inr t â‰« k = inr t â‰« l) :
  âˆ€ (j : walking_span), t.Î¹.app j â‰« k = t.Î¹.app j â‰« l
| (some walking_pair.left) := hâ‚€
| (some walking_pair.right) := hâ‚
| none := by rw [â† t.w fst, category.assoc, category.assoc, hâ‚€]

lemma is_colimit.hom_ext {t : pushout_cocone f g} (ht : is_colimit t) {W : C} {k l : t.X âŸ¶ W}
  (hâ‚€ : inl t â‰« k = inl t â‰« l) (hâ‚ : inr t â‰« k = inr t â‰« l) : k = l :=
ht.hom_ext $ coequalizer_ext _ hâ‚€ hâ‚

/-- If `t` is a colimit pushout cocone over `f` and `g` and `h : Y âŸ¶ W` and `k : Z âŸ¶ W` are
    morphisms satisfying `f â‰« h = g â‰« k`, then we have a factorization `l : t.X âŸ¶ W` such that
    `inl t â‰« l = h` and `inr t â‰« l = k`. -/
def is_colimit.desc' {t : pushout_cocone f g} (ht : is_colimit t) {W : C} (h : Y âŸ¶ W) (k : Z âŸ¶ W)
  (w : f â‰« h = g â‰« k) : {l : t.X âŸ¶ W // inl t â‰« l = h âˆ§ inr t â‰« l = k } :=
âŸ¨ht.desc $ pushout_cocone.mk _ _ w, ht.fac _ _, ht.fac _ _âŸ©

/-- This is a slightly more convenient method to verify that a pushout cocone is a colimit cocone.
    It only asks for a proof of facts that carry any mathematical content -/
def is_colimit.mk (t : pushout_cocone f g) (desc : Î  (s : cocone (span f g)), t.X âŸ¶ s.X)
  (fac_left : âˆ€ (s : cocone (span f g)), t.Î¹.app walking_span.left â‰« desc s = s.Î¹.app walking_span.left)
  (fac_right : âˆ€ (s : cocone (span f g)), t.Î¹.app walking_span.right â‰« desc s = s.Î¹.app walking_span.right)
  (uniq : âˆ€ (s : cocone (span f g)) (m : t.X âŸ¶ s.X)
    (w : âˆ€ j : walking_span, t.Î¹.app j â‰« m = s.Î¹.app j), m = desc s) :
  is_colimit t :=
{ desc := desc,
  fac' := Î» s j, option.cases_on j (by { simp [â† s.w fst, â† t.w fst, fac_left s] } )
                    (Î» j', walking_pair.cases_on j' (fac_left s) (fac_right s)),
  uniq' := uniq }

/-- This is another convenient method to verify that a pushout cocone is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def is_colimit.mk' (t : pushout_cocone f g)
  (create : Î  (s : pushout_cocone f g),
    {l // t.inl â‰« l = s.inl âˆ§ t.inr â‰« l = s.inr âˆ§ âˆ€ {m}, t.inl â‰« m = s.inl â†’ t.inr â‰« m = s.inr â†’ m = l}) :
is_colimit t :=
is_colimit.mk t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s, (create s).2.2.1)
  (Î» s m w, (create s).2.2.2 (w walking_cospan.left) (w walking_cospan.right))

/-- The flip of a pushout square is a pushout square. -/
def flip_is_colimit {W : C} {h : Y âŸ¶ W} {k : Z âŸ¶ W}
  {comm : f â‰« h = g â‰« k} (t : is_colimit (mk _ _ comm.symm)) :
  is_colimit (mk _ _ comm) :=
is_colimit.mk' _ $ Î» s,
begin
  refine âŸ¨(is_colimit.desc' t _ _ s.condition.symm).1,
          (is_colimit.desc' t _ _ _).2.2,
          (is_colimit.desc' t _ _ _).2.1, Î» m mâ‚ mâ‚‚, t.hom_ext _âŸ©,
  apply (mk k h _).coequalizer_ext,
  { rwa (is_colimit.desc' t _ _ _).2.1 },
  { rwa (is_colimit.desc' t _ _ _).2.2 },
end

end pushout_cocone

/-- This is a helper construction that can be useful when verifying that a category has all
    pullbacks. Given `F : walking_cospan â¥¤ C`, which is really the same as
    `cospan (F.map inl) (F.map inr)`, and a pullback cone on `F.map inl` and `F.map inr`, we
    get a cone on `F`.

    If you're thinking about using this, have a look at `has_pullbacks_of_has_limit_cospan`,
    which you may find to be an easier way of achieving your goal. -/
@[simps]
def cone.of_pullback_cone
  {F : walking_cospan â¥¤ C} (t : pullback_cone (F.map inl) (F.map inr)) : cone F :=
{ X := t.X,
  Ï€ := t.Ï€ â‰« (diagram_iso_cospan F).inv }

/-- This is a helper construction that can be useful when verifying that a category has all
    pushout. Given `F : walking_span â¥¤ C`, which is really the same as
    `span (F.map fst) (F.mal snd)`, and a pushout cocone on `F.map fst` and `F.map snd`,
    we get a cocone on `F`.

    If you're thinking about using this, have a look at `has_pushouts_of_has_colimit_span`, which
    you may find to be an easiery way of achieving your goal.  -/
@[simps]
def cocone.of_pushout_cocone
  {F : walking_span â¥¤ C} (t : pushout_cocone (F.map fst) (F.map snd)) : cocone F :=
{ X := t.X,
  Î¹ := (diagram_iso_span F).hom â‰« t.Î¹ }

/-- Given `F : walking_cospan â¥¤ C`, which is really the same as `cospan (F.map inl) (F.map inr)`,
    and a cone on `F`, we get a pullback cone on `F.map inl` and `F.map inr`. -/
@[simps]
def pullback_cone.of_cone
  {F : walking_cospan â¥¤ C} (t : cone F) : pullback_cone (F.map inl) (F.map inr) :=
{ X := t.X,
  Ï€ := t.Ï€ â‰« (diagram_iso_cospan F).hom }

/-- Given `F : walking_span â¥¤ C`, which is really the same as `span (F.map fst) (F.map snd)`,
    and a cocone on `F`, we get a pushout cocone on `F.map fst` and `F.map snd`. -/
@[simps]
def pushout_cocone.of_cocone
  {F : walking_span â¥¤ C} (t : cocone F) : pushout_cocone (F.map fst) (F.map snd) :=
{ X := t.X,
  Î¹ := (diagram_iso_span F).inv â‰« t.Î¹ }

/-- `pullback f g` computes the pullback of a pair of morphisms with the same target. -/
abbreviation pullback {X Y Z : C} (f : X âŸ¶ Z) (g : Y âŸ¶ Z) [has_limit (cospan f g)] :=
limit (cospan f g)
/-- `pushout f g` computes the pushout of a pair of morphisms with the same source. -/
abbreviation pushout {X Y Z : C} (f : X âŸ¶ Y) (g : X âŸ¶ Z) [has_colimit (span f g)] :=
colimit (span f g)

/-- The first projection of the pullback of `f` and `g`. -/
abbreviation pullback.fst {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)] :
  pullback f g âŸ¶ X :=
limit.Ï€ (cospan f g) walking_cospan.left

/-- The second projection of the pullback of `f` and `g`. -/
abbreviation pullback.snd {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)] :
  pullback f g âŸ¶ Y :=
limit.Ï€ (cospan f g) walking_cospan.right

/-- The first inclusion into the pushout of `f` and `g`. -/
abbreviation pushout.inl {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] :
  Y âŸ¶ pushout f g :=
colimit.Î¹ (span f g) walking_span.left

/-- The second inclusion into the pushout of `f` and `g`. -/
abbreviation pushout.inr {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] :
  Z âŸ¶ pushout f g :=
colimit.Î¹ (span f g) walking_span.right

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
    `pullback.lift : W âŸ¶ pullback f g`. -/
abbreviation pullback.lift {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : W âŸ¶ pullback f g :=
limit.lift _ (pullback_cone.mk h k w)

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
    `pushout.desc : pushout f g âŸ¶ W`. -/
abbreviation pushout.desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout f g âŸ¶ W :=
colimit.desc _ (pushout_cocone.mk h k w)

@[simp, reassoc]
lemma pullback.lift_fst {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.fst = h :=
limit.lift_Ï€ _ _

@[simp, reassoc]
lemma pullback.lift_snd {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) : pullback.lift h k w â‰« pullback.snd = k :=
limit.lift_Ï€ _ _

@[simp, reassoc]
lemma pushout.inl_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inl â‰« pushout.desc h k w = h :=
colimit.Î¹_desc _ _

@[simp, reassoc]
lemma pushout.inr_desc {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) : pushout.inr â‰« pushout.desc h k w = k :=
colimit.Î¹_desc _ _

/-- A pair of morphisms `h : W âŸ¶ X` and `k : W âŸ¶ Y` satisfying `h â‰« f = k â‰« g` induces a morphism
    `l : W âŸ¶ pullback f g` such that `l â‰« pullback.fst = h` and `l â‰« pullback.snd = k`. -/
def pullback.lift' {W X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  (h : W âŸ¶ X) (k : W âŸ¶ Y) (w : h â‰« f = k â‰« g) :
  {l : W âŸ¶ pullback f g // l â‰« pullback.fst = h âˆ§ l â‰« pullback.snd = k} :=
âŸ¨pullback.lift h k w, pullback.lift_fst _ _ _, pullback.lift_snd _ _ _âŸ©

/-- A pair of morphisms `h : Y âŸ¶ W` and `k : Z âŸ¶ W` satisfying `f â‰« h = g â‰« k` induces a morphism
    `l : pushout f g âŸ¶ W` such that `pushout.inl â‰« l = h` and `pushout.inr â‰« l = k`. -/
def pullback.desc' {W X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)]
  (h : Y âŸ¶ W) (k : Z âŸ¶ W) (w : f â‰« h = g â‰« k) :
  {l : pushout f g âŸ¶ W // pushout.inl â‰« l = h âˆ§ pushout.inr â‰« l = k} :=
âŸ¨pushout.desc h k w, pushout.inl_desc _ _ _, pushout.inr_desc _ _ _âŸ©

@[reassoc]
lemma pullback.condition {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)] :
  (pullback.fst : pullback f g âŸ¶ X) â‰« f = pullback.snd â‰« g :=
pullback_cone.condition _

@[reassoc]
lemma pushout.condition {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] :
  f â‰« (pushout.inl : Y âŸ¶ pushout f g) = g â‰« pushout.inr :=
pushout_cocone.condition _

/-- Two morphisms into a pullback are equal if their compositions with the pullback morphisms are
    equal -/
@[ext] lemma pullback.hom_ext {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  {W : C} {k l : W âŸ¶ pullback f g} (hâ‚€ : k â‰« pullback.fst = l â‰« pullback.fst)
  (hâ‚ : k â‰« pullback.snd = l â‰« pullback.snd) : k = l :=
limit.hom_ext $ pullback_cone.equalizer_ext _ hâ‚€ hâ‚

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.fst_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  [mono g] : mono (pullback.fst : pullback f g âŸ¶ X) :=
âŸ¨Î» W u v h, pullback.hom_ext h $ (cancel_mono g).1 $ by simp [â† pullback.condition, reassoc_of h]âŸ©

/-- The pullback of a monomorphism is a monomorphism -/
instance pullback.snd_of_mono {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z} [has_limit (cospan f g)]
  [mono f] : mono (pullback.snd : pullback f g âŸ¶ Y) :=
âŸ¨Î» W u v h, pullback.hom_ext ((cancel_mono f).1 $ by simp [pullback.condition, reassoc_of h]) hâŸ©

/-- Two morphisms out of a pushout are equal if their compositions with the pushout morphisms are
    equal -/
@[ext] lemma pushout.hom_ext {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)]
  {W : C} {k l : pushout f g âŸ¶ W} (hâ‚€ : pushout.inl â‰« k = pushout.inl â‰« l)
  (hâ‚ : pushout.inr â‰« k = pushout.inr â‰« l) : k = l :=
colimit.hom_ext $ pushout_cocone.coequalizer_ext _ hâ‚€ hâ‚

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inl_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] [epi g] :
  epi (pushout.inl : Y âŸ¶ pushout f g) :=
âŸ¨Î» W u v h, pushout.hom_ext h $ (cancel_epi g).1 $ by simp [â† pushout.condition_assoc, h] âŸ©

/-- The pushout of an epimorphism is an epimorphism -/
instance pushout.inr_of_epi {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z} [has_colimit (span f g)] [epi f] :
  epi (pushout.inr : Z âŸ¶ pushout f g) :=
âŸ¨Î» W u v h, pushout.hom_ext ((cancel_epi f).1 $ by simp [pushout.condition_assoc, h]) hâŸ©

variables (C)

/-- `has_pullbacks` represents a choice of pullback for every pair of morphisms -/
class has_pullbacks :=
(has_limits_of_shape : has_limits_of_shape walking_cospan C)

/-- `has_pushouts` represents a choice of pushout for every pair of morphisms -/
class has_pushouts :=
(has_colimits_of_shape : has_colimits_of_shape walking_span C)

attribute [instance] has_pullbacks.has_limits_of_shape has_pushouts.has_colimits_of_shape

/-- Pullbacks are finite limits, so if `C` has all finite limits, it also has all pullbacks -/
def has_pullbacks_of_has_finite_limits [has_finite_limits C] : has_pullbacks C :=
{ has_limits_of_shape := infer_instance }

/-- Pushouts are finite colimits, so if `C` has all finite colimits, it also has all pushouts -/
def has_pushouts_of_has_finite_colimits [has_finite_colimits C] : has_pushouts C :=
{ has_colimits_of_shape := infer_instance }

/-- If `C` has all limits of diagrams `cospan f g`, then it has all pullbacks -/
def has_pullbacks_of_has_limit_cospan
  [Î  {X Y Z : C} {f : X âŸ¶ Z} {g : Y âŸ¶ Z}, has_limit (cospan f g)] :
  has_pullbacks C :=
{ has_limits_of_shape := { has_limit := Î» F, has_limit_of_iso (diagram_iso_cospan F).symm } }

/-- If `C` has all colimits of diagrams `span f g`, then it has all pushouts -/
def has_pushouts_of_has_colimit_span
  [Î  {X Y Z : C} {f : X âŸ¶ Y} {g : X âŸ¶ Z}, has_colimit (span f g)] :
  has_pushouts C :=
{ has_colimits_of_shape := { has_colimit := Î» F, has_colimit_of_iso (diagram_iso_span F) } }

end category_theory.limits
