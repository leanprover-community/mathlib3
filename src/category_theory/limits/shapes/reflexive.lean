/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.adjunction
import category_theory.limits.shapes.equalizers
import category_theory.limits.shapes.kernel_pair

/-!
# Reflexive coequalizers

We define reflexive pairs as a pair of morphisms which have a common section. We say a category has
reflexive coequalizers if it has coequalizers of all reflexive pairs.
Reflexive coequalizers often enjoy nicer properties than general coequalizers, and feature heavily
in some versions of the monadicity theorem.

We also give some examples of reflexive pairs: for an adjunction `F âŠ£ G` with counit `Îµ`, the pair
`(FGÎµ_B, Îµ_FGB)` is reflexive. If a pair `f,g` is a kernel pair for some morphism, then it is
reflexive.

# TODO
* If `C` has binary coproducts and reflexive coequalizers, then it has all coequalizers.
* If `T` is a monad on cocomplete category `C`, then `algebra T` is cocomplete iff it has reflexive
  coequalizers.
* If `C` is locally cartesian closed and has reflexive coequalizers, then it has images: in fact
  regular epi (and hence strong epi) images.
-/

namespace category_theory

universes v vâ‚‚ u uâ‚‚

variables {C : Type u} [category.{v} C]
variables {D : Type uâ‚‚} [category.{vâ‚‚} D]
variables {A B : C} {f g : A âŸ¶ B}

/--
The pair `f g : A âŸ¶ B` is reflexive if there is a morphism `B âŸ¶ A` which is a section for both.
-/
class is_reflexive_pair (f g : A âŸ¶ B) : Prop :=
(common_section [] : âˆƒ (s : B âŸ¶ A), s â‰« f = ğŸ™ B âˆ§ s â‰« g = ğŸ™ B)

/--
The pair `f g : A âŸ¶ B` is coreflexive if there is a morphism `B âŸ¶ A` which is a retraction for both.
-/
class is_coreflexive_pair (f g : A âŸ¶ B) : Prop :=
(common_retraction [] : âˆƒ (s : B âŸ¶ A), f â‰« s = ğŸ™ A âˆ§ g â‰« s = ğŸ™ A)

lemma is_reflexive_pair.mk' (s : B âŸ¶ A) (sf : s â‰« f = ğŸ™ B) (sg : s â‰« g = ğŸ™ B) :
  is_reflexive_pair f g :=
âŸ¨âŸ¨s, sf, sgâŸ©âŸ©

lemma is_coreflexive_pair.mk' (s : B âŸ¶ A) (fs : f â‰« s = ğŸ™ A) (gs : g â‰« s = ğŸ™ A) :
  is_coreflexive_pair f g :=
âŸ¨âŸ¨s, fs, gsâŸ©âŸ©

/-- Get the common section for a reflexive pair. -/
noncomputable def common_section (f g : A âŸ¶ B) [is_reflexive_pair f g] : B âŸ¶ A :=
(is_reflexive_pair.common_section f g).some

@[simp, reassoc]
lemma section_comp_left (f g : A âŸ¶ B) [is_reflexive_pair f g] :
  common_section f g â‰« f = ğŸ™ B :=
(is_reflexive_pair.common_section f g).some_spec.1

@[simp, reassoc]
lemma section_comp_right (f g : A âŸ¶ B) [is_reflexive_pair f g] :
  common_section f g â‰« g = ğŸ™ B :=
(is_reflexive_pair.common_section f g).some_spec.2

/-- Get the common retraction for a coreflexive pair. -/
noncomputable def common_retraction (f g : A âŸ¶ B) [is_coreflexive_pair f g] :
  B âŸ¶ A :=
(is_coreflexive_pair.common_retraction f g).some

@[simp, reassoc]
lemma left_comp_retraction (f g : A âŸ¶ B) [is_coreflexive_pair f g] :
  f â‰« common_retraction f g = ğŸ™ A :=
(is_coreflexive_pair.common_retraction f g).some_spec.1

@[simp, reassoc]
lemma right_comp_retraction (f g : A âŸ¶ B) [is_coreflexive_pair f g] :
  g â‰« common_retraction f g = ğŸ™ A :=
(is_coreflexive_pair.common_retraction f g).some_spec.2

/-- If `f,g` is a kernel pair for some morphism `q`, then it is reflexive. -/
lemma is_kernel_pair.is_reflexive_pair {R : C} {f g : R âŸ¶ A} {q : A âŸ¶ B}
  (h : is_kernel_pair q f g) :
  is_reflexive_pair f g :=
is_reflexive_pair.mk' _ (h.lift' _ _ rfl).2.1 (h.lift' _ _ _).2.2

/-- If `f,g` is reflexive, then `g,f` is reflexive. -/
-- This shouldn't be an instance as it would instantly loop.
lemma is_reflexive_pair.swap [is_reflexive_pair f g] :
  is_reflexive_pair g f :=
is_reflexive_pair.mk' _ (section_comp_right f g) (section_comp_left f g)

/-- If `f,g` is coreflexive, then `g,f` is coreflexive. -/
-- This shouldn't be an instance as it would instantly loop.
lemma is_coreflexive_pair.swap [is_coreflexive_pair f g] :
  is_coreflexive_pair g f :=
is_coreflexive_pair.mk' _ (right_comp_retraction f g) (left_comp_retraction f g)

variables {F : C â¥¤ D} {G : D â¥¤ C} (adj : F âŠ£ G)

/-- For an adjunction `F âŠ£ G` with counit `Îµ`, the pair `(FGÎµ_B, Îµ_FGB)` is reflexive. -/
instance (B : D) :
  is_reflexive_pair (F.map (G.map (adj.counit.app B))) (adj.counit.app (F.obj (G.obj B))) :=
is_reflexive_pair.mk'
  (F.map (adj.unit.app (G.obj B)))
  (by { rw [â† F.map_comp, adj.right_triangle_components], apply F.map_id })
  adj.left_triangle_components

namespace limits

variables (C)

/-- `C` has reflexive coequalizers if it has coequalizers for every reflexive pair. -/
class has_reflexive_coequalizers : Prop :=
(has_coeq : âˆ€ â¦ƒA B : Câ¦„ (f g : A âŸ¶ B) [is_reflexive_pair f g], has_coequalizer f g)

/-- `C` has coreflexive equalizers if it has equalizers for every coreflexive pair. -/
class has_coreflexive_equalizers : Prop :=
(has_eq : âˆ€ â¦ƒA B : Câ¦„ (f g : A âŸ¶ B) [is_coreflexive_pair f g], has_equalizer f g)

attribute [instance, priority 1] has_reflexive_coequalizers.has_coeq
attribute [instance, priority 1] has_coreflexive_equalizers.has_eq

lemma has_coequalizer_of_common_section [has_reflexive_coequalizers C]
  {A B : C} {f g : A âŸ¶ B} (r : B âŸ¶ A)
  (rf : r â‰« f = ğŸ™ _) (rg : r â‰« g = ğŸ™ _) :
  has_coequalizer f g :=
begin
  letI := is_reflexive_pair.mk' r rf rg,
  apply_instance
end

lemma has_equalizer_of_common_retraction [has_coreflexive_equalizers C]
  {A B : C} {f g : A âŸ¶ B} (r : B âŸ¶ A)
  (fr : f â‰« r = ğŸ™ _) (gr : g â‰« r = ğŸ™ _) :
  has_equalizer f g :=
begin
  letI := is_coreflexive_pair.mk' r fr gr,
  apply_instance
end

/-- If `C` has coequalizers, then it has reflexive coequalizers. -/
@[priority 100]
instance has_reflexive_coequalizers_of_has_coequalizers [has_coequalizers C] :
  has_reflexive_coequalizers C :=
{ has_coeq := Î» A B f g i, by apply_instance }

/-- If `C` has equalizers, then it has coreflexive equalizers. -/
@[priority 100]
instance has_coreflexive_equalizers_of_has_equalizers [has_equalizers C] :
  has_coreflexive_equalizers C :=
{ has_eq := Î» A B f g i, by apply_instance }

end limits

open limits

end category_theory
