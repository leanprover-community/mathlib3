/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.limits.preserves.basic
import category_theory.limits.shapes.equalizers
import category_theory.limits.shapes.strong_epi
import category_theory.limits.shapes.pullbacks

/-!
# Definitions and basic properties of regular monomorphisms and epimorphisms.

A regular monomorphism is a morphism that is the equalizer of some parallel pair.

We give the constructions
* `split_mono â†’ regular_mono` and
* `regular_mono â†’ mono`
as well as the dual constructions for regular epimorphisms. Additionally, we give the
construction
* `regular_epi âŸ¶ strong_epi`.

-/

noncomputable theory

namespace category_theory
open category_theory.limits

universes vâ‚ uâ‚ uâ‚‚

variables {C : Type uâ‚} [category.{vâ‚} C]

variables {X Y : C}

/-- A regular monomorphism is a morphism which is the equalizer of some parallel pair. -/
class regular_mono (f : X âŸ¶ Y) :=
(Z : C)
(left right : Y âŸ¶ Z)
(w : f â‰« left = f â‰« right)
(is_limit : is_limit (fork.of_Î¹ f w))

attribute [reassoc] regular_mono.w

/-- Every regular monomorphism is a monomorphism. -/
@[priority 100]
instance regular_mono.mono (f : X âŸ¶ Y) [regular_mono f] : mono f :=
mono_of_is_limit_parallel_pair regular_mono.is_limit

instance equalizer_regular (g h : X âŸ¶ Y) [has_limit (parallel_pair g h)] :
  regular_mono (equalizer.Î¹ g h) :=
{ Z := Y,
  left := g,
  right := h,
  w := equalizer.condition g h,
  is_limit := fork.is_limit.mk _ (Î» s, limit.lift _ s) (by simp) (Î» s m w, by { ext1, simp [â†w] }) }

/-- Every split monomorphism is a regular monomorphism. -/
@[priority 100]
instance regular_mono.of_split_mono (f : X âŸ¶ Y) [split_mono f] : regular_mono f :=
{ Z     := Y,
  left  := ğŸ™ Y,
  right := retraction f â‰« f,
  w     := by tidy,
  is_limit := split_mono_equalizes f }

/-- If `f` is a regular mono, then any map `k : W âŸ¶ Y` equalizing `regular_mono.left` and
    `regular_mono.right` induces a morphism `l : W âŸ¶ X` such that `l â‰« f = k`. -/
def regular_mono.lift' {W : C} (f : X âŸ¶ Y) [regular_mono f] (k : W âŸ¶ Y)
  (h : k â‰« (regular_mono.left : Y âŸ¶ @regular_mono.Z _ _ _ _ f _) = k â‰« regular_mono.right) :
  {l : W âŸ¶ X // l â‰« f = k} :=
fork.is_limit.lift' regular_mono.is_limit _ h

/--
The second leg of a pullback cone is a regular monomorphism if the right component is too.

See also `pullback.snd_of_mono` for the basic monomorphism version, and
`regular_of_is_pullback_fst_of_regular` for the flipped version.
-/
def regular_of_is_pullback_snd_of_regular {P Q R S : C} {f : P âŸ¶ Q} {g : P âŸ¶ R} {h : Q âŸ¶ S}
  {k : R âŸ¶ S} [hr : regular_mono h] (comm : f â‰« h = g â‰« k)
  (t : is_limit (pullback_cone.mk _ _ comm)) :
regular_mono g :=
{ Z := hr.Z,
  left := k â‰« hr.left,
  right := k â‰« hr.right,
  w := by rw [â† reassoc_of comm, â† reassoc_of comm, hr.w],
  is_limit :=
  begin
    apply fork.is_limit.mk' _ _,
    intro s,
    have lâ‚ : (fork.Î¹ s â‰« k) â‰« regular_mono.left = (fork.Î¹ s â‰« k) â‰« regular_mono.right,
      rw [category.assoc, s.condition, category.assoc],
    obtain âŸ¨l, hlâŸ© := fork.is_limit.lift' hr.is_limit _ lâ‚,
    obtain âŸ¨p, hpâ‚, hpâ‚‚âŸ© := pullback_cone.is_limit.lift' t _ _ hl,
    refine âŸ¨p, hpâ‚‚, _âŸ©,
    intros m w,
    have z : m â‰« g = p â‰« g := w.trans hpâ‚‚.symm,
    apply t.hom_ext,
    apply (pullback_cone.mk f g comm).equalizer_ext,
    { erw [â† cancel_mono h, category.assoc, category.assoc, comm, reassoc_of z] },
    { exact z },
  end }

/--
The first leg of a pullback cone is a regular monomorphism if the left component is too.

See also `pullback.fst_of_mono` for the basic monomorphism version, and
`regular_of_is_pullback_snd_of_regular` for the flipped version.
-/
def regular_of_is_pullback_fst_of_regular {P Q R S : C} {f : P âŸ¶ Q} {g : P âŸ¶ R} {h : Q âŸ¶ S}
  {k : R âŸ¶ S} [hr : regular_mono k] (comm : f â‰« h = g â‰« k)
  (t : is_limit (pullback_cone.mk _ _ comm)) :
regular_mono f :=
regular_of_is_pullback_snd_of_regular comm.symm (pullback_cone.flip_is_limit t)

/-- A regular monomorphism is an isomorphism if it is an epimorphism. -/
lemma is_iso_of_regular_mono_of_epi (f : X âŸ¶ Y) [regular_mono f] [e : epi f] : is_iso f :=
@is_iso_limit_cone_parallel_pair_of_epi _ _ _ _ _ _ _ regular_mono.is_limit e

/-- A regular epimorphism is a morphism which is the coequalizer of some parallel pair. -/
class regular_epi (f : X âŸ¶ Y) :=
(W : C)
(left right : W âŸ¶ X)
(w : left â‰« f = right â‰« f)
(is_colimit : is_colimit (cofork.of_Ï€ f w))

attribute [reassoc] regular_epi.w

/-- Every regular epimorphism is an epimorphism. -/
@[priority 100]
instance regular_epi.epi (f : X âŸ¶ Y) [regular_epi f] : epi f :=
epi_of_is_colimit_parallel_pair regular_epi.is_colimit

instance coequalizer_regular (g h : X âŸ¶ Y) [has_colimit (parallel_pair g h)] :
  regular_epi (coequalizer.Ï€ g h) :=
{ W := X,
  left := g,
  right := h,
  w := coequalizer.condition g h,
  is_colimit := cofork.is_colimit.mk _ (Î» s, colimit.desc _ s) (by simp)
    (Î» s m w, by { ext1, simp [â†w] }) }

/-- Every split epimorphism is a regular epimorphism. -/
@[priority 100]
instance regular_epi.of_split_epi (f : X âŸ¶ Y) [split_epi f] : regular_epi f :=
{ W     := X,
  left  := ğŸ™ X,
  right := f â‰« section_ f,
  w     := by tidy,
  is_colimit := split_epi_coequalizes f }

/-- If `f` is a regular epi, then every morphism `k : X âŸ¶ W` coequalizing `regular_epi.left` and
    `regular_epi.right` induces `l : Y âŸ¶ W` such that `f â‰« l = k`. -/
def regular_epi.desc' {W : C} (f : X âŸ¶ Y) [regular_epi f] (k : X âŸ¶ W)
  (h : (regular_epi.left : regular_epi.W f âŸ¶ X) â‰« k = regular_epi.right â‰« k) :
  {l : Y âŸ¶ W // f â‰« l = k} :=
cofork.is_colimit.desc' (regular_epi.is_colimit) _ h

/--
The second leg of a pushout cocone is a regular epimorphism if the right component is too.

See also `pushout.snd_of_epi` for the basic epimorphism version, and
`regular_of_is_pushout_fst_of_regular` for the flipped version.
-/
def regular_of_is_pushout_snd_of_regular
  {P Q R S : C} {f : P âŸ¶ Q} {g : P âŸ¶ R} {h : Q âŸ¶ S} {k : R âŸ¶ S}
  [gr : regular_epi g] (comm : f â‰« h = g â‰« k) (t : is_colimit (pushout_cocone.mk _ _ comm)) :
regular_epi h :=
{ W := gr.W,
  left := gr.left â‰« f,
  right := gr.right â‰« f,
  w := by rw [category.assoc, category.assoc, comm, reassoc_of gr.w],
  is_colimit :=
  begin
    apply cofork.is_colimit.mk' _ _,
    intro s,
    have lâ‚ : gr.left â‰« f â‰« s.Ï€ = gr.right â‰« f â‰« s.Ï€,
      rw [â† category.assoc, â† category.assoc, s.condition],
    obtain âŸ¨l, hlâŸ© := cofork.is_colimit.desc' gr.is_colimit (f â‰« cofork.Ï€ s) lâ‚,
    obtain âŸ¨p, hpâ‚, hpâ‚‚âŸ© := pushout_cocone.is_colimit.desc' t _ _ hl.symm,
    refine âŸ¨p, hpâ‚, _âŸ©,
    intros m w,
    have z := w.trans hpâ‚.symm,
    apply t.hom_ext,
    apply (pushout_cocone.mk _ _ comm).coequalizer_ext,
    { exact z },
    { erw [â† cancel_epi g, â† reassoc_of comm, â† reassoc_of comm, z], refl },
  end }

/--
The first leg of a pushout cocone is a regular epimorphism if the left component is too.

See also `pushout.fst_of_epi` for the basic epimorphism version, and
`regular_of_is_pushout_snd_of_regular` for the flipped version.
-/
def regular_of_is_pushout_fst_of_regular
  {P Q R S : C} {f : P âŸ¶ Q} {g : P âŸ¶ R} {h : Q âŸ¶ S} {k : R âŸ¶ S}
  [fr : regular_epi f] (comm : f â‰« h = g â‰« k) (t : is_colimit (pushout_cocone.mk _ _ comm)) :
regular_epi k :=
regular_of_is_pushout_snd_of_regular comm.symm (pushout_cocone.flip_is_colimit t)

/-- A regular epimorphism is an isomorphism if it is a monomorphism. -/
lemma is_iso_of_regular_epi_of_mono (f : X âŸ¶ Y) [regular_epi f] [m : mono f] : is_iso f :=
@is_iso_limit_cocone_parallel_pair_of_epi _ _ _ _ _ _ _ regular_epi.is_colimit m

@[priority 100]
instance strong_epi_of_regular_epi (f : X âŸ¶ Y) [regular_epi f] : strong_epi f :=
{ epi := by apply_instance,
  has_lift :=
  begin
    introsI,
    have : (regular_epi.left : regular_epi.W f âŸ¶ X) â‰« u = regular_epi.right â‰« u,
    { apply (cancel_mono z).1,
      simp only [category.assoc, h, regular_epi.w_assoc] },
    obtain âŸ¨t, htâŸ© := regular_epi.desc' f u this,
    exact arrow.has_lift.mk âŸ¨t, ht, (cancel_epi f).1
      (by simp only [â†category.assoc, ht, â†h, arrow.mk_hom, arrow.hom_mk'_right])âŸ©,
  end }

end category_theory
