/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.limits.shapes.kernels
import category_theory.limits.shapes.strong_epi
import category_theory.limits.shapes.pullbacks

/-!
# Definitions and basic properties of regular and normal monomorphisms and epimorphisms.

A regular monomorphism is a morphism that is the equalizer of some parallel pair.
A normal monomorphism is a morphism that is the kernel of some other morphism.

We give the constructions
* `split_mono ‚Üí regular_mono`
* `normal_mono ‚Üí regular_mono`, and
* `regular_mono ‚Üí mono`
as well as the dual constructions for regular and normal epimorphisms. Additionally, we give the
construction
* `regular_epi ‚ü∂ strong_epi`.

-/

namespace category_theory
open category_theory.limits

universes v‚ÇÅ u‚ÇÅ

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C]

variables {X Y : C}

/-- A regular monomorphism is a morphism which is the equalizer of some parallel pair. -/
class regular_mono (f : X ‚ü∂ Y) :=
(Z : C)
(left right : Y ‚ü∂ Z)
(w : f ‚â´ left = f ‚â´ right)
(is_limit : is_limit (fork.of_Œπ f w))

attribute [reassoc] regular_mono.w

/-- Every regular monomorphism is a monomorphism. -/
@[priority 100]
instance regular_mono.mono (f : X ‚ü∂ Y) [regular_mono f] : mono f :=
mono_of_is_limit_parallel_pair regular_mono.is_limit

instance equalizer_regular (g h : X ‚ü∂ Y) [has_limit (parallel_pair g h)] :
  regular_mono (equalizer.Œπ g h) :=
{ Z := Y,
  left := g,
  right := h,
  w := equalizer.condition g h,
  is_limit := fork.is_limit.mk _ (Œª s, limit.lift _ s) (by simp) (Œª s m w, by { ext1, simp [‚Üêw] }) }

/-- Every split monomorphism is a regular monomorphism. -/
@[priority 100]
instance regular_mono.of_split_mono (f : X ‚ü∂ Y) [split_mono f] : regular_mono f :=
{ Z     := Y,
  left  := ùüô Y,
  right := retraction f ‚â´ f,
  w     := by tidy,
  is_limit := split_mono_equalizes f }

/-- If `f` is a regular mono, then any map `k : W ‚ü∂ Y` equalizing `regular_mono.left` and
    `regular_mono.right` induces a morphism `l : W ‚ü∂ X` such that `l ‚â´ f = k`. -/
def regular_mono.lift' {W : C} (f : X ‚ü∂ Y) [regular_mono f] (k : W ‚ü∂ Y)
  (h : k ‚â´ (regular_mono.left : Y ‚ü∂ @regular_mono.Z _ _ _ _ f _) = k ‚â´ regular_mono.right) :
  {l : W ‚ü∂ X // l ‚â´ f = k} :=
fork.is_limit.lift' regular_mono.is_limit _ h

/--
The second leg of a pullback cone is a regular monomorphism if the right component is too.

See also `pullback.snd_of_mono` for the basic monomorphism version, and
`regular_of_is_pullback_fst_of_regular` for the flipped version.
-/
def regular_of_is_pullback_snd_of_regular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [hr : regular_mono h] (comm : f ‚â´ h = g ‚â´ k) (t : is_limit (pullback_cone.mk _ _ comm)) :
regular_mono g :=
{ Z := hr.Z,
  left := k ‚â´ hr.left,
  right := k ‚â´ hr.right,
  w := by rw [‚Üê reassoc_of comm, ‚Üê reassoc_of comm, hr.w],
  is_limit :=
  begin
    apply fork.is_limit.mk' _ _,
    intro s,
    have l‚ÇÅ : (fork.Œπ s ‚â´ k) ‚â´ regular_mono.left = (fork.Œπ s ‚â´ k) ‚â´ regular_mono.right,
      rw [category.assoc, s.condition, category.assoc],
    obtain ‚ü®l, hl‚ü© := fork.is_limit.lift' hr.is_limit _ l‚ÇÅ,
    obtain ‚ü®p, hp‚ÇÅ, hp‚ÇÇ‚ü© := pullback_cone.is_limit.lift' t _ _ hl,
    refine ‚ü®p, hp‚ÇÇ, _‚ü©,
    intros m w,
    have z : m ‚â´ g = p ‚â´ g := w.trans hp‚ÇÇ.symm,
    apply t.hom_ext,
    apply (pullback_cone.mk f g comm).equalizer_ext,
    { erw [‚Üê cancel_mono h, category.assoc, category.assoc, comm, reassoc_of z] },
    { exact z },
  end }

/--
The first leg of a pullback cone is a regular monomorphism if the left component is too.

See also `pullback.fst_of_mono` for the basic monomorphism version, and
`regular_of_is_pullback_snd_of_regular` for the flipped version.
-/
def regular_of_is_pullback_fst_of_regular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [hr : regular_mono k] (comm : f ‚â´ h = g ‚â´ k) (t : is_limit (pullback_cone.mk _ _ comm)) :
regular_mono f :=
regular_of_is_pullback_snd_of_regular comm.symm (pullback_cone.flip_is_limit t)

/-- A regular monomorphism is an isomorphism if it is an epimorphism. -/
def is_iso_of_regular_mono_of_epi (f : X ‚ü∂ Y) [regular_mono f] [e : epi f] : is_iso f :=
@is_iso_limit_cone_parallel_pair_of_epi _ _ _ _ _ _ _ regular_mono.is_limit e

section
variables [has_zero_morphisms C]
/-- A normal monomorphism is a morphism which is the kernel of some morphism. -/
class normal_mono (f : X ‚ü∂ Y) :=
(Z : C)
(g : Y ‚ü∂ Z)
(w : f ‚â´ g = 0)
(is_limit : is_limit (kernel_fork.of_Œπ f w))

/-- Every normal monomorphism is a regular monomorphism. -/
@[priority 100]
instance normal_mono.regular_mono (f : X ‚ü∂ Y) [I : normal_mono f] : regular_mono f :=
{ left := I.g,
  right := 0,
  w := (by simpa using I.w),
  ..I }

/-- If `f` is a normal mono, then any map `k : W ‚ü∂ Y` such that `k ‚â´ normal_mono.g = 0` induces
    a morphism `l : W ‚ü∂ X` such that `l ‚â´ f = k`. -/
def normal_mono.lift' {W : C} (f : X ‚ü∂ Y) [normal_mono f] (k : W ‚ü∂ Y) (h : k ‚â´ normal_mono.g = 0) :
  {l : W ‚ü∂ X // l ‚â´ f = k} :=
kernel_fork.is_limit.lift' normal_mono.is_limit _ h

/--
The second leg of a pullback cone is a normal monomorphism if the right component is too.

See also `pullback.snd_of_mono` for the basic monomorphism version, and
`normal_of_is_pullback_fst_of_normal` for the flipped version.
-/
def normal_of_is_pullback_snd_of_normal {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [hn : normal_mono h] (comm : f ‚â´ h = g ‚â´ k) (t : is_limit (pullback_cone.mk _ _ comm)) :
normal_mono g :=
{ Z := hn.Z,
  g := k ‚â´ hn.g,
  w := by rw [‚Üê reassoc_of comm, hn.w, has_zero_morphisms.comp_zero],
  is_limit :=
  begin
    letI gr := regular_of_is_pullback_snd_of_regular comm t,
    have q := (has_zero_morphisms.comp_zero k hn.Z).symm,
    convert gr.is_limit,
    dunfold kernel_fork.of_Œπ fork.of_Œπ,
    congr, exact q, exact q, exact q, apply proof_irrel_heq,
  end }

/--
The first leg of a pullback cone is a normal monomorphism if the left component is too.

See also `pullback.fst_of_mono` for the basic monomorphism version, and
`normal_of_is_pullback_snd_of_normal` for the flipped version.
-/
def normal_of_is_pullback_fst_of_normal {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [hn : normal_mono k] (comm : f ‚â´ h = g ‚â´ k) (t : is_limit (pullback_cone.mk _ _ comm)) :
normal_mono f :=
normal_of_is_pullback_snd_of_normal comm.symm (pullback_cone.flip_is_limit t)

end
/-- A regular epimorphism is a morphism which is the coequalizer of some parallel pair. -/
class regular_epi (f : X ‚ü∂ Y) :=
(W : C)
(left right : W ‚ü∂ X)
(w : left ‚â´ f = right ‚â´ f)
(is_colimit : is_colimit (cofork.of_œÄ f w))

attribute [reassoc] regular_epi.w

/-- Every regular epimorphism is an epimorphism. -/
@[priority 100]
instance regular_epi.epi (f : X ‚ü∂ Y) [regular_epi f] : epi f :=
epi_of_is_colimit_parallel_pair regular_epi.is_colimit

instance coequalizer_regular (g h : X ‚ü∂ Y) [has_colimit (parallel_pair g h)] :
  regular_epi (coequalizer.œÄ g h) :=
{ W := X,
  left := g,
  right := h,
  w := coequalizer.condition g h,
  is_colimit := cofork.is_colimit.mk _ (Œª s, colimit.desc _ s) (by simp) (Œª s m w, by { ext1, simp [‚Üêw] }) }

/-- Every split epimorphism is a regular epimorphism. -/
@[priority 100]
instance regular_epi.of_split_epi (f : X ‚ü∂ Y) [split_epi f] : regular_epi f :=
{ W     := X,
  left  := ùüô X,
  right := f ‚â´ section_ f,
  w     := by tidy,
  is_colimit := split_epi_coequalizes f }

/-- If `f` is a regular epi, then every morphism `k : X ‚ü∂ W` coequalizing `regular_epi.left` and
    `regular_epi.right` induces `l : Y ‚ü∂ W` such that `f ‚â´ l = k`. -/
def regular_epi.desc' {W : C} (f : X ‚ü∂ Y) [regular_epi f] (k : X ‚ü∂ W)
  (h : (regular_epi.left : regular_epi.W f ‚ü∂ X) ‚â´ k = regular_epi.right ‚â´ k) :
  {l : Y ‚ü∂ W // f ‚â´ l = k} :=
cofork.is_colimit.desc' (regular_epi.is_colimit) _ h

/--
The second leg of a pushout cocone is a regular epimorphism if the right component is too.

See also `pushout.snd_of_epi` for the basic epimorphism version, and
`regular_of_is_pushout_fst_of_regular` for the flipped version.
-/
def regular_of_is_pushout_snd_of_regular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [gr : regular_epi g] (comm : f ‚â´ h = g ‚â´ k) (t : is_colimit (pushout_cocone.mk _ _ comm)) :
regular_epi h :=
{ W := gr.W,
  left := gr.left ‚â´ f,
  right := gr.right ‚â´ f,
  w := by rw [category.assoc, category.assoc, comm, reassoc_of gr.w],
  is_colimit :=
  begin
    apply cofork.is_colimit.mk' _ _,
    intro s,
    have l‚ÇÅ : gr.left ‚â´ f ‚â´ s.œÄ = gr.right ‚â´ f ‚â´ s.œÄ,
      rw [‚Üê category.assoc, ‚Üê category.assoc, s.condition],
    obtain ‚ü®l, hl‚ü© := cofork.is_colimit.desc' gr.is_colimit (f ‚â´ cofork.œÄ s) l‚ÇÅ,
    obtain ‚ü®p, hp‚ÇÅ, hp‚ÇÇ‚ü© := pushout_cocone.is_colimit.desc' t _ _ hl.symm,
    refine ‚ü®p, hp‚ÇÅ, _‚ü©,
    intros m w,
    have z := w.trans hp‚ÇÅ.symm,
    apply t.hom_ext,
    apply (pushout_cocone.mk _ _ comm).coequalizer_ext,
    { exact z },
    { erw [‚Üê cancel_epi g, ‚Üê reassoc_of comm, ‚Üê reassoc_of comm, z], refl },
  end }

/--
The first leg of a pushout cocone is a regular epimorphism if the left component is too.

See also `pushout.fst_of_epi` for the basic epimorphism version, and
`regular_of_is_pushout_snd_of_regular` for the flipped version.
-/
def regular_of_is_pushout_fst_of_regular {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [fr : regular_epi f] (comm : f ‚â´ h = g ‚â´ k) (t : is_colimit (pushout_cocone.mk _ _ comm)) :
regular_epi k :=
regular_of_is_pushout_snd_of_regular comm.symm (pushout_cocone.flip_is_colimit t)

/-- A regular epimorphism is an isomorphism if it is a monomorphism. -/
def is_iso_of_regular_epi_of_mono (f : X ‚ü∂ Y) [regular_epi f] [m : mono f] : is_iso f :=
@is_iso_limit_cocone_parallel_pair_of_epi _ _ _ _ _ _ _ regular_epi.is_colimit m

@[priority 100]
instance strong_epi_of_regular_epi (f : X ‚ü∂ Y) [regular_epi f] : strong_epi f :=
{ epi := by apply_instance,
  has_lift :=
  begin
    introsI,
    have : (regular_epi.left : regular_epi.W f ‚ü∂ X) ‚â´ u = regular_epi.right ‚â´ u,
    { apply (cancel_mono z).1,
      simp only [category.assoc, h, regular_epi.w_assoc] },
    obtain ‚ü®t, ht‚ü© := regular_epi.desc' f u this,
    exact ‚ü®t, ht, (cancel_epi f).1
      (by simp only [‚Üêcategory.assoc, ht, ‚Üêh, arrow.mk_hom, arrow.hom_mk'_right])‚ü©,
  end }

section
variables [has_zero_morphisms C]
/-- A normal epimorphism is a morphism which is the cokernel of some morphism. -/
class normal_epi (f : X ‚ü∂ Y) :=
(W : C)
(g : W ‚ü∂ X)
(w : g ‚â´ f = 0)
(is_colimit : is_colimit (cokernel_cofork.of_œÄ f w))

/-- Every normal epimorphism is a regular epimorphism. -/
@[priority 100]
instance normal_epi.regular_epi (f : X ‚ü∂ Y) [I : normal_epi f] : regular_epi f :=
{ left := I.g,
  right := 0,
  w := (by simpa using I.w),
  ..I }

/-- If `f` is a normal epi, then every morphism `k : X ‚ü∂ W` satisfying `normal_epi.g ‚â´ k = 0`
    induces `l : Y ‚ü∂ W` such that `f ‚â´ l = k`. -/
def normal_epi.desc' {W : C} (f : X ‚ü∂ Y) [normal_epi f] (k : X ‚ü∂ W) (h : normal_epi.g ‚â´ k = 0) :
  {l : Y ‚ü∂ W // f ‚â´ l = k} :=
cokernel_cofork.is_colimit.desc' (normal_epi.is_colimit) _ h

/--
The second leg of a pushout cocone is a normal epimorphism if the right component is too.

See also `pushout.snd_of_epi` for the basic epimorphism version, and
`normal_of_is_pushout_fst_of_normal` for the flipped version.
-/
def normal_of_is_pushout_snd_of_normal {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [gn : normal_epi g] (comm : f ‚â´ h = g ‚â´ k) (t : is_colimit (pushout_cocone.mk _ _ comm)) :
normal_epi h :=
{ W := gn.W,
  g := gn.g ‚â´ f,
  w := by rw [category.assoc, comm, reassoc_of gn.w, has_zero_morphisms.zero_comp],
  is_colimit :=
  begin
    letI hn := regular_of_is_pushout_snd_of_regular comm t,
    have q := (has_zero_morphisms.zero_comp gn.W f).symm,
    convert hn.is_colimit,
    dunfold cokernel_cofork.of_œÄ cofork.of_œÄ,
    congr, exact q, exact q, exact q, apply proof_irrel_heq,
  end }

/--
The first leg of a pushout cocone is a normal epimorphism if the left component is too.

See also `pushout.fst_of_epi` for the basic epimorphism version, and
`normal_of_is_pushout_snd_of_normal` for the flipped version.
-/
def normal_of_is_pushout_fst_of_normal {P Q R S : C} {f : P ‚ü∂ Q} {g : P ‚ü∂ R} {h : Q ‚ü∂ S} {k : R ‚ü∂ S}
  [hn : normal_epi f] (comm : f ‚â´ h = g ‚â´ k) (t : is_colimit (pushout_cocone.mk _ _ comm)) :
normal_epi k :=
normal_of_is_pushout_snd_of_normal comm.symm (pushout_cocone.flip_is_colimit t)

end

end category_theory
