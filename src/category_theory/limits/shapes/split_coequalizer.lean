/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.limits.shapes.equalizers

/-!
# Split coequalizers

We define what it means for a triple of morphisms `f g : X âŸ¶ Y`, `Ï€ : Y âŸ¶ Z` to be a split
coequalizer: there is a section `s` of `Ï€` and a section `t` of `g`, which additionally satisfy
`t â‰« f = Ï€ â‰« s`.

In addition, we show that every split coequalizer is a coequalizer
(`category_theory.is_split_coequalizer.is_coequalizer`) and absolute
(`category_theory.is_split_coequalizer.map`)

A pair `f g : X âŸ¶ Y` has a split coequalizer if there is a `Z` and `Ï€ : Y âŸ¶ Z` making `f,g,Ï€` a
split coequalizer.
A pair `f g : X âŸ¶ Y` has a `G`-split coequalizer if `G f, G g` has a split coequalizer.

These definitions and constructions are useful in particular for the monadicity theorems.

## TODO

Dualise to split equalizers.
-/

namespace category_theory

universes v vâ‚‚ u uâ‚‚

variables {C : Type u} [category.{v} C]
variables {D : Type uâ‚‚} [category.{v} D]
variables (G : C â¥¤ D)
variables {X Y : C} (f g : X âŸ¶ Y)

/--
A split coequalizer diagram consists of morphisms

      f   Ï€
    X â‡‰ Y â†’ Z
      g

satisfying `f â‰« Ï€ = g â‰« Ï€` together with morphisms

      t   s
    X â† Y â† Z

satisfying `s â‰« Ï€ = ğŸ™ Z`, `t â‰« g = ğŸ™ Y` and `t â‰« f = Ï€ â‰« s`.

The name "coequalizer" is appropriate, since any split coequalizer is a coequalizer, see
`category_theory.is_split_coequalizer.is_coequalizer`.
Split coequalizers are also absolute, since a functor preserves all the structure above.
-/
structure is_split_coequalizer {Z : C} (Ï€ : Y âŸ¶ Z) :=
(right_section : Z âŸ¶ Y)
(left_section : Y âŸ¶ X)
(condition : f â‰« Ï€ = g â‰« Ï€)
(right_section_Ï€ : right_section â‰« Ï€ = ğŸ™ Z)
(left_section_bottom : left_section â‰« g = ğŸ™ Y)
(left_section_top : left_section â‰« f = Ï€ â‰« right_section)

instance {X : C} : inhabited (is_split_coequalizer (ğŸ™ X) (ğŸ™ X) (ğŸ™ X)) :=
âŸ¨âŸ¨ğŸ™ _, ğŸ™ _, rfl, category.id_comp _, category.id_comp _, rflâŸ©âŸ©

open is_split_coequalizer
attribute [reassoc] condition
attribute [simp, reassoc] right_section_Ï€ left_section_bottom left_section_top

variables {f g}

/-- Split coequalizers are absolute: they are preserved by any functor. -/
@[simps]
def is_split_coequalizer.map {Z : C} {Ï€ : Y âŸ¶ Z} (q : is_split_coequalizer f g Ï€) (F : C â¥¤ D) :
  is_split_coequalizer (F.map f) (F.map g) (F.map Ï€) :=
{ right_section := F.map q.right_section,
  left_section := F.map q.left_section,
  condition := by rw [â†F.map_comp, q.condition, F.map_comp],
  right_section_Ï€ := by rw [â†F.map_comp, q.right_section_Ï€, F.map_id],
  left_section_bottom := by rw [â†F.map_comp, q.left_section_bottom, F.map_id],
  left_section_top := by rw [â†F.map_comp, q.left_section_top, F.map_comp] }

section

open limits

/-- A split coequalizer clearly induces a cofork. -/
@[simps]
def is_split_coequalizer.as_cofork {Z : C} {h : Y âŸ¶ Z} (t : is_split_coequalizer f g h) :
  cofork f g :=
cofork.of_Ï€ h t.condition

/--
The cofork induced by a split coequalizer is a coequalizer, justifying the name. In some cases it
is more convenient to show a given cofork is a coequalizer by showing it is split.
-/
def is_split_coequalizer.is_coequalizer
  {Z : C} {h : Y âŸ¶ Z} (t : is_split_coequalizer f g h) :
  is_colimit t.as_cofork :=
cofork.is_colimit.mk' _ $ Î» s,
âŸ¨t.right_section â‰« s.Ï€,
 by { dsimp, rw [â† t.left_section_top_assoc, s.condition, t.left_section_bottom_assoc] },
 Î» m hm, by { simp [â†hm] }âŸ©

end

variables (f g)
/--
The pair `f,g` is a split pair if there is a `h : Y âŸ¶ Z` so that `f, g, h` forms a split coequalizer
in `C`.
-/
class has_split_coequalizer : Prop :=
(splittable [] : âˆƒ {Z : C} (h : Y âŸ¶ Z), nonempty (is_split_coequalizer f g h))

/--
The pair `f,g` is a `G`-split pair if there is a `h : G Y âŸ¶ Z` so that `G f, G g, h` forms a split
coequalizer in `D`.
-/
abbreviation functor.is_split_pair : Prop := has_split_coequalizer (G.map f) (G.map g)

/-- Get the coequalizer object from the typeclass `is_split_pair`. -/
noncomputable def has_split_coequalizer.coequalizer_of_split [has_split_coequalizer f g] : C :=
(has_split_coequalizer.splittable f g).some

/-- Get the coequalizer morphism from the typeclass `is_split_pair`. -/
noncomputable def has_split_coequalizer.coequalizer_Ï€ [has_split_coequalizer f g] :
  Y âŸ¶ has_split_coequalizer.coequalizer_of_split f g :=
(has_split_coequalizer.splittable f g).some_spec.some

/-- The coequalizer morphism `coequalizer_Î¹` gives a split coequalizer on `f,g`. -/
noncomputable def has_split_coequalizer.is_split_coequalizer [has_split_coequalizer f g] :
  is_split_coequalizer f g (has_split_coequalizer.coequalizer_Ï€ f g) :=
classical.choice (has_split_coequalizer.splittable f g).some_spec.some_spec

/-- If `f, g` is split, then `G f, G g` is split. -/
instance map_is_split_pair [has_split_coequalizer f g] :
  has_split_coequalizer (G.map f) (G.map g) :=
{ splittable :=
  âŸ¨_, _, âŸ¨is_split_coequalizer.map (has_split_coequalizer.is_split_coequalizer f g) _âŸ©âŸ© }

namespace limits

/-- If a pair has a split coequalizer, it has a coequalizer. -/
@[priority 1]
instance has_coequalizer_of_has_split_coequalizer [has_split_coequalizer f g] :
  has_coequalizer f g :=
has_colimit.mk âŸ¨_, (has_split_coequalizer.is_split_coequalizer f g).is_coequalizerâŸ©

end limits

end category_theory
