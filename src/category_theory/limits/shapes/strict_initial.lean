/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/

import category_theory.limits.shapes.terminal
import category_theory.limits.shapes.binary_products

/-!
# Strict initial objects

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file sets up the basic theory of strict initial objects: initial objects where every morphism
to it is an isomorphism. This generalises a property of the empty set in the category of sets:
namely that the only function to the empty set is from itself.

We say `C` has strict initial objects if every initial object is strict, ie given any morphism
`f : A ‚ü∂ I` where `I` is initial, then `f` is an isomorphism.
Strictly speaking, this says that *any* initial object must be strict, rather than that strict
initial objects exist, which turns out to be a more useful notion to formalise.

If the binary product of `X` with a strict initial object exists, it is also initial.

To show a category `C` with an initial object has strict initial objects, the most convenient way
is to show any morphism to the (chosen) initial object is an isomorphism and use
`has_strict_initial_objects_of_initial_is_strict`.

The dual notion (strict terminal objects) occurs much less frequently in practice so is ignored.

## TODO

* Construct examples of this: `Type*`, `Top`, `Groupoid`, simplicial types, posets.
* Construct the bottom element of the subobject lattice given strict initials.
* Show cartesian closed categories have strict initials

## References
* https://ncatlab.org/nlab/show/strict+initial+object
-/


universes v u

namespace category_theory
namespace limits
open category

variables (C : Type u) [category.{v} C]

section strict_initial

/--
We say `C` has strict initial objects if every initial object is strict, ie given any morphism
`f : A ‚ü∂ I` where `I` is initial, then `f` is an isomorphism.

Strictly speaking, this says that *any* initial object must be strict, rather than that strict
initial objects exist.
-/
class has_strict_initial_objects : Prop :=
(out : ‚àÄ {I A : C} (f : A ‚ü∂ I), is_initial I ‚Üí is_iso f)

variables {C}

section
variables [has_strict_initial_objects C] {I : C}

lemma is_initial.is_iso_to (hI : is_initial I) {A : C} (f : A ‚ü∂ I) :
  is_iso f :=
has_strict_initial_objects.out f hI

lemma is_initial.strict_hom_ext (hI : is_initial I) {A : C} (f g : A ‚ü∂ I) :
  f = g :=
begin
  haveI := hI.is_iso_to f,
  haveI := hI.is_iso_to g,
  exact eq_of_inv_eq_inv (hI.hom_ext (inv f) (inv g)),
end

lemma is_initial.subsingleton_to (hI : is_initial I) {A : C} :
  subsingleton (A ‚ü∂ I) :=
‚ü®hI.strict_hom_ext‚ü©

@[priority 100] instance initial_mono_of_strict_initial_objects : initial_mono_class C :=
{ is_initial_mono_from := Œª I A hI,
  { right_cancellation := Œª B g h i, hI.strict_hom_ext _ _ } }

/-- If `I` is initial, then `X ‚®Ø I` is isomorphic to it. -/
@[simps hom]
noncomputable def mul_is_initial (X : C) [has_binary_product X I] (hI : is_initial I) :
  X ‚®Ø I ‚âÖ I :=
@@as_iso _ prod.snd (hI.is_iso_to _)

@[simp] lemma mul_is_initial_inv (X : C) [has_binary_product X I] (hI : is_initial I) :
  (mul_is_initial X hI).inv = hI.to _ :=
hI.hom_ext _ _

/-- If `I` is initial, then `I ‚®Ø X` is isomorphic to it. -/
@[simps hom]
noncomputable def is_initial_mul (X : C) [has_binary_product I X] (hI : is_initial I) :
  I ‚®Ø X ‚âÖ I :=
@@as_iso _ prod.fst (hI.is_iso_to _)

@[simp] lemma is_initial_mul_inv (X : C) [has_binary_product I X] (hI : is_initial I) :
  (is_initial_mul X hI).inv = hI.to _ :=
hI.hom_ext _ _

variable [has_initial C]

instance initial_is_iso_to {A : C} (f : A ‚ü∂ ‚ä•_ C) : is_iso f :=
initial_is_initial.is_iso_to _

@[ext] lemma initial.hom_ext {A : C} (f g : A ‚ü∂ ‚ä•_ C) : f = g :=
initial_is_initial.strict_hom_ext _ _

lemma initial.subsingleton_to {A : C} : subsingleton (A ‚ü∂ ‚ä•_ C) :=
initial_is_initial.subsingleton_to

/--
The product of `X` with an initial object in a category with strict initial objects is itself
initial.
This is the generalisation of the fact that `X √ó empty ‚âÉ empty` for types (or `n * 0 = 0`).
-/
@[simps hom]
noncomputable def mul_initial (X : C) [has_binary_product X ‚ä•_ C] :
  X ‚®Ø ‚ä•_ C ‚âÖ ‚ä•_ C :=
mul_is_initial _ initial_is_initial

@[simp] lemma mul_initial_inv (X : C) [has_binary_product X ‚ä•_ C] :
  (mul_initial X).inv = initial.to _ :=
subsingleton.elim _ _

/--
The product of `X` with an initial object in a category with strict initial objects is itself
initial.
This is the generalisation of the fact that `empty √ó X ‚âÉ empty` for types (or `0 * n = 0`).
-/
@[simps hom]
noncomputable def initial_mul (X : C) [has_binary_product (‚ä•_ C) X] :
  ‚ä•_ C ‚®Ø X ‚âÖ ‚ä•_ C :=
is_initial_mul _ initial_is_initial

@[simp] lemma initial_mul_inv (X : C) [has_binary_product (‚ä•_ C) X] :
  (initial_mul X).inv = initial.to _ :=
subsingleton.elim _ _
end

/-- If `C` has an initial object such that every morphism *to* it is an isomorphism, then `C`
has strict initial objects. -/
lemma has_strict_initial_objects_of_initial_is_strict [has_initial C]
  (h : ‚àÄ A (f : A ‚ü∂ ‚ä•_ C), is_iso f) :
  has_strict_initial_objects C :=
{ out := Œª I A f hI,
  begin
    haveI := h A (f ‚â´ hI.to _),
    exact ‚ü®‚ü®hI.to _ ‚â´ inv (f ‚â´ hI.to ‚ä•_ C), by rw [‚Üêassoc, is_iso.hom_inv_id], hI.hom_ext _ _‚ü©‚ü©,
  end }

end strict_initial

section strict_terminal

/--
We say `C` has strict terminal objects if every terminal object is strict, ie given any morphism
`f : I ‚ü∂ A` where `I` is terminal, then `f` is an isomorphism.

Strictly speaking, this says that *any* terminal object must be strict, rather than that strict
terminal objects exist.
-/
class has_strict_terminal_objects : Prop :=
(out : ‚àÄ {I A : C} (f : I ‚ü∂ A), is_terminal I ‚Üí is_iso f)

variables {C}

section
variables [has_strict_terminal_objects C] {I : C}

lemma is_terminal.is_iso_from (hI : is_terminal I) {A : C} (f : I ‚ü∂ A) :
  is_iso f :=
has_strict_terminal_objects.out f hI

lemma is_terminal.strict_hom_ext (hI : is_terminal I) {A : C} (f g : I ‚ü∂ A) :
  f = g :=
begin
  haveI := hI.is_iso_from f,
  haveI := hI.is_iso_from g,
  exact eq_of_inv_eq_inv (hI.hom_ext (inv f) (inv g)),
end

lemma is_terminal.subsingleton_to (hI : is_terminal I) {A : C} :
  subsingleton (I ‚ü∂ A) :=
‚ü®hI.strict_hom_ext‚ü©

variables {J : Type v} [small_category J]

/-- If all but one object in a diagram is strict terminal, the the limit is isomorphic to the
said object via `limit.œÄ`. -/
lemma limit_œÄ_is_iso_of_is_strict_terminal (F : J ‚•§ C) [has_limit F] (i : J)
  (H : ‚àÄ j ‚â† i, is_terminal (F.obj j)) [subsingleton (i ‚ü∂ i)] :
  is_iso (limit.œÄ F i) :=
begin
  classical,
  refine ‚ü®‚ü®limit.lift _ ‚ü®_,‚ü®_,_‚ü©‚ü©,_,_‚ü©‚ü©,
  { exact Œª j, dite (j = i) (Œª h, eq_to_hom (by { cases h, refl })) (Œª h, (H _ h).from _) },
  { intros j k f,
    split_ifs,
    { cases h, cases h_1, obtain rfl : f = ùüô _ := subsingleton.elim _ _, simpa },
    { cases h, erw category.comp_id,
      haveI : is_iso (F.map f) := (H _ h_1).is_iso_from _,
      rw ‚Üê is_iso.comp_inv_eq,
      apply (H _ h_1).hom_ext },
    { cases h_1, apply (H _ h).hom_ext },
    { apply (H _ h).hom_ext } },
  { ext,
    rw [assoc, limit.lift_œÄ],
    dsimp only,
    split_ifs,
    { cases h, rw [id_comp, eq_to_hom_refl], exact comp_id _ },
    { apply (H _ h).hom_ext } },
  { rw limit.lift_œÄ, simpa }
end

variable [has_terminal C]

instance terminal_is_iso_from {A : C} (f :  ‚ä§_ C ‚ü∂ A) : is_iso f :=
terminal_is_terminal.is_iso_from _

@[ext] lemma terminal.hom_ext {A : C} (f g : ‚ä§_ C ‚ü∂ A) : f = g :=
terminal_is_terminal.strict_hom_ext _ _

lemma terminal.subsingleton_to {A : C} : subsingleton (‚ä§_ C ‚ü∂ A) :=
terminal_is_terminal.subsingleton_to

end

/-- If `C` has an object such that every morphism *from* it is an isomorphism, then `C`
has strict terminal objects. -/
lemma has_strict_terminal_objects_of_terminal_is_strict (I : C) (h : ‚àÄ A (f : I ‚ü∂ A), is_iso f) :
  has_strict_terminal_objects C :=
{ out := Œª I' A f hI',
  begin
    haveI := h A (hI'.from _ ‚â´ f),
    exact ‚ü®‚ü®inv (hI'.from I ‚â´ f) ‚â´ hI'.from I,
      hI'.hom_ext _ _, by rw [assoc, is_iso.inv_hom_id]‚ü©‚ü©,
  end }

end strict_terminal
end limits
end category_theory
