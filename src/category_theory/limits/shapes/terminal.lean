/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.limits.shapes.finite_products
import category_theory.pempty

/-!
# Initial and terminal objects in a category.
-/

universes v u

open category_theory

namespace category_theory.limits

variables (C : Type u) [ùíû : category.{v} C]
include ùíû

/-- A category has a terminal object if it has a limit over the empty diagram. -/
-- Use `has_terminal_of_unique` to construct instances.
class has_terminal :=
(has_limits_of_shape : has_limits_of_shape.{v} pempty C)
/-- A category has an initial object if it has a colimit over the empty diagram. -/
-- Use `has_initial_of_unique` to construct instances.
class has_initial :=
(has_colimits_of_shape : has_colimits_of_shape.{v} pempty C)

attribute [instance] has_terminal.has_limits_of_shape has_initial.has_colimits_of_shape

instance has_terminal_of_has_finite_products [has_finite_products.{v} C] : has_terminal.{v} C :=
{ has_limits_of_shape :=
  { has_limit := Œª F, has_limit_of_equivalence_comp ((functor.empty (discrete pempty)).as_equivalence.symm) } }
instance has_initial_of_has_finite_coproducts [has_finite_coproducts.{v} C] : has_initial.{v} C :=
{ has_colimits_of_shape :=
  { has_colimit := Œª F, has_colimit_of_equivalence_comp ((functor.empty (discrete pempty)).as_equivalence.symm) } }

abbreviation terminal [has_terminal.{v} C] : C := limit (functor.empty C)
abbreviation initial [has_initial.{v} C] : C := colimit (functor.empty C)

notation `‚ä§_` C:20 := terminal C
notation `‚ä•_` C:20 := initial C

section
variables {C}

/-- We can more explicitly show that a category has a terminal object by specifying the object,
and showing there is a unique morphism to it from any other object. -/
def has_terminal_of_unique (X : C) [h : Œ† Y : C, unique (Y ‚ü∂ X)] : has_terminal.{v} C :=
{ has_limits_of_shape :=
  { has_limit := Œª F,
    { cone     := { X := X, œÄ := { app := pempty.rec _ } },
      is_limit := { lift := Œª s, (h s.X).default } } } }
/-- We can more explicitly show that a category has an initial object by specifying the object,
and showing there is a unique morphism from it to any other object. -/
def has_initial_of_unique (X : C) [h : Œ† Y : C, unique (X ‚ü∂ Y)] : has_initial.{v} C :=
{ has_colimits_of_shape :=
  { has_colimit := Œª F,
    { cocone     := { X := X, Œπ := { app := pempty.rec _ } },
      is_colimit := { desc := Œª s, (h s.X).default } } } }

/-- The map from an object to the terminal object. -/
abbreviation terminal.from [has_terminal.{v} C] (P : C) : P ‚ü∂ ‚ä§_ C :=
limit.lift (functor.empty C) { X := P, œÄ := by tidy }.
/-- The map to an object from the initial object. -/
abbreviation initial.to [has_initial.{v} C] (P : C) : ‚ä•_ C ‚ü∂ P :=
colimit.desc (functor.empty C) { X := P, Œπ := by tidy }.

instance unique_to_terminal [has_terminal.{v} C] (P : C) : unique (P ‚ü∂ ‚ä§_ C) :=
{ default := terminal.from P,
  uniq := Œª m, by { apply limit.hom_ext, rintro ‚ü®‚ü© } }

instance unique_from_initial [has_initial.{v} C] (P : C) : unique (‚ä•_ C ‚ü∂ P) :=
{ default := initial.to P,
  uniq := Œª m, by { apply colimit.hom_ext, rintro ‚ü®‚ü© } }
end

end category_theory.limits
