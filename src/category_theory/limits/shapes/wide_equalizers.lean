/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/
import category_theory.limits.has_limits
import category_theory.limits.shapes.equalizers

/-!
# Wide equalizers and wide coequalizers

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines wide (co)equalizers as special cases of (co)limits.

A wide equalizer for the family of morphisms `X âŸ¶ Y` indexed by `J` is the categorical
generalization of the subobject `{a âˆˆ A | âˆ€ jâ‚ jâ‚‚, f(jâ‚, a) = f(jâ‚‚, a)}`. Note that if `J` has
fewer than two morphisms this condition is trivial, so some lemmas and definitions assume `J` is
nonempty.

## Main definitions

* `walking_parallel_family` is the indexing category used for wide (co)equalizer diagrams
* `parallel_family` is a functor from `walking_parallel_family` to our category `C`.
* a `trident` is a cone over a parallel family.
  * there is really only one interesting morphism in a trident: the arrow from the vertex of the
    trident to the domain of f and g. It is called `trident.Î¹`.
* a `wide_equalizer` is now just a `limit (parallel_family f)`

Each of these has a dual.

## Main statements

* `wide_equalizer.Î¹_mono` states that every wide_equalizer map is a monomorphism
* `is_iso_limit_cone_parallel_family_of_self` states that the identity on the domain of `f` is an
  equalizer of `f` and `f`.

## Implementation notes
As with the other special shapes in the limits library, all the definitions here are given as
`abbreviation`s of the general statements for limits, so all the `simp` lemmas and theorems about
general limits can be used.

## References

* [F. Borceux, *Handbook of Categorical Algebra 1*][borceux-vol1]
-/

noncomputable theory

namespace category_theory.limits

open category_theory

universes w v u uâ‚‚

variables {J : Type w}

/-- The type of objects for the diagram indexing a wide (co)equalizer. -/
inductive walking_parallel_family (J : Type w) : Type w
| zero : walking_parallel_family
| one : walking_parallel_family

open walking_parallel_family

instance : decidable_eq (walking_parallel_family J)
| zero zero := is_true rfl
| zero one := is_false (Î» t, walking_parallel_family.no_confusion t)
| one zero := is_false (Î» t, walking_parallel_family.no_confusion t)
| one one := is_true rfl

instance : inhabited (walking_parallel_family J) := âŸ¨zeroâŸ©

/-- The type family of morphisms for the diagram indexing a wide (co)equalizer. -/
@[derive decidable_eq] inductive walking_parallel_family.hom (J : Type w) :
  walking_parallel_family J â†’ walking_parallel_family J â†’ Type w
| id : Î  X : walking_parallel_family.{w} J, walking_parallel_family.hom X X
| line : Î  (j : J), walking_parallel_family.hom zero one

/-- Satisfying the inhabited linter -/
instance (J : Type v) : inhabited (walking_parallel_family.hom J zero zero) :=
{ default := hom.id _ }

open walking_parallel_family.hom

/-- Composition of morphisms in the indexing diagram for wide (co)equalizers. -/
def walking_parallel_family.hom.comp :
  Î  (X Y Z : walking_parallel_family J)
    (f : walking_parallel_family.hom J X Y) (g : walking_parallel_family.hom J Y Z),
    walking_parallel_family.hom J X Z
  | _ _ _ (id _)   h := h
  | _ _ _ (line j) (id one) := line j.

local attribute [tidy] tactic.case_bash

instance walking_parallel_family.category : small_category (walking_parallel_family J) :=
{ hom  := walking_parallel_family.hom J,
  id   := walking_parallel_family.hom.id,
  comp := walking_parallel_family.hom.comp }

@[simp]
lemma walking_parallel_family.hom_id (X : walking_parallel_family J) :
  walking_parallel_family.hom.id X = ğŸ™ X :=
rfl

variables {C : Type u} [category.{v} C]
variables {X Y : C} (f : J â†’ (X âŸ¶ Y))

/--
`parallel_family f` is the diagram in `C` consisting of the given family of morphisms, each with
common domain and codomain.
-/
def parallel_family : walking_parallel_family J â¥¤ C :=
{ obj := Î» x, walking_parallel_family.cases_on x X Y,
  map := Î» x y h, match x, y, h with
  | _, _, (id _) := ğŸ™ _
  | _, _, (line j) := f j
  end,
  map_comp' :=
  begin
    rintro _ _ _ âŸ¨âŸ© âŸ¨âŸ©;
    { unfold_aux, simp; refl },
  end }

@[simp] lemma parallel_family_obj_zero : (parallel_family f).obj zero = X := rfl
@[simp] lemma parallel_family_obj_one : (parallel_family f).obj one = Y := rfl

@[simp] lemma parallel_family_map_left {j : J} : (parallel_family f).map (line j) = f j := rfl

/-- Every functor indexing a wide (co)equalizer is naturally isomorphic (actually, equal) to a
    `parallel_family` -/
@[simps]
def diagram_iso_parallel_family (F : walking_parallel_family J â¥¤ C) :
  F â‰… parallel_family (Î» j, F.map (line j)) :=
nat_iso.of_components (Î» j, eq_to_iso $ by cases j; tidy) $ by tidy

/-- `walking_parallel_pair` as a category is equivalent to a special case of
`walking_parallel_family`.  -/
@[simps]
def walking_parallel_family_equiv_walking_parallel_pair :
  walking_parallel_family.{w} (ulift bool) â‰Œ walking_parallel_pair :=
{ functor := parallel_family
      (Î» p, cond p.down walking_parallel_pair_hom.left walking_parallel_pair_hom.right),
  inverse := parallel_pair (line (ulift.up tt)) (line (ulift.up ff)),
  unit_iso := nat_iso.of_components (Î» X, eq_to_iso (by cases X; refl)) (by tidy),
  counit_iso := nat_iso.of_components (Î» X, eq_to_iso (by cases X; refl)) (by tidy) }

/-- A trident on `f` is just a `cone (parallel_family f)`. -/
abbreviation trident := cone (parallel_family f)

/-- A cotrident on `f` and `g` is just a `cocone (parallel_family f)`. -/
abbreviation cotrident := cocone (parallel_family f)

variables {f}

/-- A trident `t` on the parallel family `f : J â†’ (X âŸ¶ Y)` consists of two morphisms
    `t.Ï€.app zero : t.X âŸ¶ X` and `t.Ï€.app one : t.X âŸ¶ Y`. Of these, only the first one is
    interesting, and we give it the shorter name `trident.Î¹ t`. -/
abbreviation trident.Î¹ (t : trident f) := t.Ï€.app zero

/-- A cotrident `t` on the parallel family `f : J â†’ (X âŸ¶ Y)` consists of two morphisms
    `t.Î¹.app zero : X âŸ¶ t.X` and `t.Î¹.app one : Y âŸ¶ t.X`. Of these, only the second one is
    interesting, and we give it the shorter name `cotrident.Ï€ t`. -/
abbreviation cotrident.Ï€ (t : cotrident f) := t.Î¹.app one

@[simp] lemma trident.Î¹_eq_app_zero (t : trident f) : t.Î¹ = t.Ï€.app zero := rfl
@[simp] lemma cotrident.Ï€_eq_app_one (t : cotrident f) : t.Ï€ = t.Î¹.app one := rfl

@[simp, reassoc] lemma trident.app_zero (s : trident f) (j : J) :
  s.Ï€.app zero â‰« f j = s.Ï€.app one :=
by rw [â†s.w (line j), parallel_family_map_left]

@[simp, reassoc] lemma cotrident.app_one (s : cotrident f) (j : J) :
  f j â‰« s.Î¹.app one = s.Î¹.app zero :=
by rw [â†s.w (line j), parallel_family_map_left]

/--
A trident on `f : J â†’ (X âŸ¶ Y)` is determined by the morphism `Î¹ : P âŸ¶ X` satisfying
`âˆ€ jâ‚ jâ‚‚, Î¹ â‰« f jâ‚ = Î¹ â‰« f jâ‚‚`.
-/
@[simps]
def trident.of_Î¹ [nonempty J] {P : C} (Î¹ : P âŸ¶ X) (w : âˆ€ jâ‚ jâ‚‚, Î¹ â‰« f jâ‚ = Î¹ â‰« f jâ‚‚) :
  trident f :=
{ X := P,
  Ï€ :=
  { app := Î» X, walking_parallel_family.cases_on X Î¹ (Î¹ â‰« f (classical.arbitrary J)),
    naturality' := Î» i j f,
      begin
        dsimp,
        cases f with _ k,
        { simp },
        { simp [w (classical.arbitrary J) k] },
      end } }

/--
A cotrident on `f : J â†’ (X âŸ¶ Y)` is determined by the morphism `Ï€ : Y âŸ¶ P` satisfying
`âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« Ï€ = f jâ‚‚ â‰« Ï€`.
-/
@[simps]
def cotrident.of_Ï€ [nonempty J] {P : C} (Ï€ : Y âŸ¶ P) (w : âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« Ï€ = f jâ‚‚ â‰« Ï€) :
  cotrident f :=
{ X := P,
  Î¹ :=
  { app := Î» X, walking_parallel_family.cases_on X (f (classical.arbitrary J) â‰« Ï€) Ï€,
    naturality' := Î» i j f,
      begin
        dsimp,
        cases f with _ k,
        { simp },
        { simp [w (classical.arbitrary J) k] }
      end } } -- See note [dsimp, simp]

lemma trident.Î¹_of_Î¹ [nonempty J] {P : C} (Î¹ : P âŸ¶ X) (w : âˆ€ jâ‚ jâ‚‚, Î¹ â‰« f jâ‚ = Î¹ â‰« f jâ‚‚) :
  (trident.of_Î¹ Î¹ w).Î¹ = Î¹ := rfl
lemma cotrident.Ï€_of_Ï€ [nonempty J] {P : C} (Ï€ : Y âŸ¶ P) (w : âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« Ï€ = f jâ‚‚ â‰« Ï€) :
  (cotrident.of_Ï€ Ï€ w).Ï€ = Ï€ := rfl

@[reassoc]
lemma trident.condition (jâ‚ jâ‚‚ : J) (t : trident f) : t.Î¹ â‰« f jâ‚ = t.Î¹ â‰« f jâ‚‚ :=
by rw [t.app_zero, t.app_zero]

@[reassoc]
lemma cotrident.condition (jâ‚ jâ‚‚ : J) (t : cotrident f) : f jâ‚ â‰« t.Ï€ = f jâ‚‚ â‰« t.Ï€ :=
by rw [t.app_one, t.app_one]

/-- To check whether two maps are equalized by both maps of a trident, it suffices to check it for
the first map -/
lemma trident.equalizer_ext [nonempty J] (s : trident f) {W : C} {k l : W âŸ¶ s.X}
  (h : k â‰« s.Î¹ = l â‰« s.Î¹) : âˆ€ (j : walking_parallel_family J),
    k â‰« s.Ï€.app j = l â‰« s.Ï€.app j
| zero := h
| one := by rw [â†s.app_zero (classical.arbitrary J), reassoc_of h]

/-- To check whether two maps are coequalized by both maps of a cotrident, it suffices to check it
for the second map -/
lemma cotrident.coequalizer_ext [nonempty J] (s : cotrident f) {W : C} {k l : s.X âŸ¶ W}
  (h : s.Ï€ â‰« k = s.Ï€ â‰« l) : âˆ€ (j : walking_parallel_family J),
    s.Î¹.app j â‰« k = s.Î¹.app j â‰« l
| zero := by rw [â†s.app_one (classical.arbitrary J), category.assoc, category.assoc, h]
| one := h

lemma trident.is_limit.hom_ext [nonempty J] {s : trident f} (hs : is_limit s)
  {W : C} {k l : W âŸ¶ s.X} (h : k â‰« s.Î¹ = l â‰« s.Î¹) :
  k = l :=
hs.hom_ext $ trident.equalizer_ext _ h

lemma cotrident.is_colimit.hom_ext [nonempty J] {s : cotrident f} (hs : is_colimit s)
  {W : C} {k l : s.X âŸ¶ W} (h : s.Ï€ â‰« k = s.Ï€ â‰« l) :
  k = l :=
hs.hom_ext $ cotrident.coequalizer_ext _ h

/-- If `s` is a limit trident over `f`, then a morphism `k : W âŸ¶ X` satisfying
    `âˆ€ jâ‚ jâ‚‚, k â‰« f jâ‚ = k â‰« f jâ‚‚` induces a morphism `l : W âŸ¶ s.X` such that
    `l â‰« trident.Î¹ s = k`. -/
def trident.is_limit.lift' [nonempty J] {s : trident f} (hs : is_limit s) {W : C} (k : W âŸ¶ X)
  (h : âˆ€ jâ‚ jâ‚‚, k â‰« f jâ‚ = k â‰« f jâ‚‚) :
  {l : W âŸ¶ s.X // l â‰« trident.Î¹ s = k} :=
âŸ¨hs.lift $ trident.of_Î¹ _ h, hs.fac _ _âŸ©

/-- If `s` is a colimit cotrident over `f`, then a morphism `k : Y âŸ¶ W` satisfying
    `âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« k = f jâ‚‚ â‰« k` induces a morphism `l : s.X âŸ¶ W` such that
    `cotrident.Ï€ s â‰« l = k`. -/
def cotrident.is_colimit.desc' [nonempty J] {s : cotrident f} (hs : is_colimit s) {W : C}
  (k : Y âŸ¶ W) (h : âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« k = f jâ‚‚ â‰« k) :
  {l : s.X âŸ¶ W // cotrident.Ï€ s â‰« l = k} :=
âŸ¨hs.desc $ cotrident.of_Ï€ _ h, hs.fac _ _âŸ©

/-- This is a slightly more convenient method to verify that a trident is a limit cone. It
    only asks for a proof of facts that carry any mathematical content -/
def trident.is_limit.mk [nonempty J] (t : trident f)
  (lift : Î  (s : trident f), s.X âŸ¶ t.X)
  (fac : âˆ€ (s : trident f), lift s â‰« t.Î¹ = s.Î¹)
  (uniq : âˆ€ (s : trident f) (m : s.X âŸ¶ t.X)
  (w : âˆ€ j : walking_parallel_family J, m â‰« t.Ï€.app j = s.Ï€.app j), m = lift s) :
  is_limit t :=
{ lift := lift,
  fac' := Î» s j, walking_parallel_family.cases_on j (fac s)
    (by rw [â†t.w (line (classical.arbitrary J)), reassoc_of fac, s.w]),
  uniq' := uniq }

/-- This is another convenient method to verify that a trident is a limit cone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def trident.is_limit.mk' [nonempty J] (t : trident f)
  (create : Î  (s : trident f), {l // l â‰« t.Î¹ = s.Î¹ âˆ§ âˆ€ {m}, m â‰« t.Î¹ = s.Î¹ â†’ m = l}) :
is_limit t :=
trident.is_limit.mk t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s m w, (create s).2.2 (w zero))

/-- This is a slightly more convenient method to verify that a cotrident is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content -/
def cotrident.is_colimit.mk [nonempty J] (t : cotrident f)
  (desc : Î  (s : cotrident f), t.X âŸ¶ s.X)
  (fac : âˆ€ (s : cotrident f), t.Ï€ â‰« desc s = s.Ï€)
  (uniq : âˆ€ (s : cotrident f) (m : t.X âŸ¶ s.X)
  (w : âˆ€ j : walking_parallel_family J, t.Î¹.app j â‰« m = s.Î¹.app j), m = desc s) :
  is_colimit t :=
{ desc := desc,
  fac' := Î» s j, walking_parallel_family.cases_on j
    (by rw [â†t.w_assoc (line (classical.arbitrary J)), fac, s.w]) (fac s),
  uniq' := uniq }

/-- This is another convenient method to verify that a cotrident is a colimit cocone. It
    only asks for a proof of facts that carry any mathematical content, and allows access to the
    same `s` for all parts. -/
def cotrident.is_colimit.mk' [nonempty J] (t : cotrident f)
  (create : Î  (s : cotrident f), {l : t.X âŸ¶ s.X // t.Ï€ â‰« l = s.Ï€ âˆ§ âˆ€ {m}, t.Ï€ â‰« m = s.Ï€ â†’ m = l}) :
  is_colimit t :=
cotrident.is_colimit.mk t
  (Î» s, (create s).1)
  (Î» s, (create s).2.1)
  (Î» s m w, (create s).2.2 (w one))

/--
Given a limit cone for the family `f : J â†’ (X âŸ¶ Y)`, for any `Z`, morphisms from `Z` to its point
are in bijection with morphisms `h : Z âŸ¶ X` such that `âˆ€ jâ‚ jâ‚‚, h â‰« f jâ‚ = h â‰« f jâ‚‚`.
Further, this bijection is natural in `Z`: see `trident.is_limit.hom_iso_natural`.
-/
@[simps]
def trident.is_limit.hom_iso [nonempty J] {t : trident f} (ht : is_limit t) (Z : C) :
  (Z âŸ¶ t.X) â‰ƒ {h : Z âŸ¶ X // âˆ€ jâ‚ jâ‚‚, h â‰« f jâ‚ = h â‰« f jâ‚‚} :=
{ to_fun := Î» k, âŸ¨k â‰« t.Î¹, by simpâŸ©,
  inv_fun := Î» h, (trident.is_limit.lift' ht _ h.prop).1,
  left_inv := Î» k, trident.is_limit.hom_ext ht (trident.is_limit.lift' _ _ _).prop,
  right_inv := Î» h, subtype.ext (trident.is_limit.lift' ht _ _).prop }

/-- The bijection of `trident.is_limit.hom_iso` is natural in `Z`. -/
lemma trident.is_limit.hom_iso_natural [nonempty J] {t : trident f} (ht : is_limit t)
  {Z Z' : C} (q : Z' âŸ¶ Z) (k : Z âŸ¶ t.X) :
  (trident.is_limit.hom_iso ht _ (q â‰« k) : Z' âŸ¶ X) =
  q â‰« (trident.is_limit.hom_iso ht _ k : Z âŸ¶ X) :=
category.assoc _ _ _

/--
Given a colimit cocone for the family `f : J â†’ (X âŸ¶ Y)`, for any `Z`, morphisms from the cocone
point to `Z` are in bijection with morphisms `h : Z âŸ¶ X` such that
`âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« h = f jâ‚‚ â‰« h`.  Further, this bijection is natural in `Z`: see
`cotrident.is_colimit.hom_iso_natural`.
-/
@[simps]
def cotrident.is_colimit.hom_iso [nonempty J] {t : cotrident f} (ht : is_colimit t) (Z : C) :
  (t.X âŸ¶ Z) â‰ƒ {h : Y âŸ¶ Z // âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« h = f jâ‚‚ â‰« h} :=
{ to_fun := Î» k, âŸ¨t.Ï€ â‰« k, by simpâŸ©,
  inv_fun := Î» h, (cotrident.is_colimit.desc' ht _ h.prop).1,
  left_inv := Î» k, cotrident.is_colimit.hom_ext ht (cotrident.is_colimit.desc' _ _ _).prop,
  right_inv := Î» h, subtype.ext (cotrident.is_colimit.desc' ht _ _).prop }

/-- The bijection of `cotrident.is_colimit.hom_iso` is natural in `Z`. -/
lemma cotrident.is_colimit.hom_iso_natural [nonempty J] {t : cotrident f} {Z Z' : C}
  (q : Z âŸ¶ Z') (ht : is_colimit t) (k : t.X âŸ¶ Z) :
    (cotrident.is_colimit.hom_iso ht _ (k â‰« q) : Y âŸ¶ Z') =
    (cotrident.is_colimit.hom_iso ht _ k : Y âŸ¶ Z) â‰« q :=
(category.assoc _ _ _).symm

/-- This is a helper construction that can be useful when verifying that a category has certain wide
    equalizers. Given `F : walking_parallel_family â¥¤ C`, which is really the same as
    `parallel_family (Î» j, F.map (line j))`, and a trident on `Î» j, F.map (line j)`, we get a cone
    on `F`.

    If you're thinking about using this, have a look at
    `has_wide_equalizers_of_has_limit_parallel_family`, which you may find to be an easier way of
    achieving your goal. -/
def cone.of_trident
  {F : walking_parallel_family J â¥¤ C} (t : trident (Î» j, F.map (line j))) : cone F :=
{ X := t.X,
  Ï€ :=
  { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy),
    naturality' := Î» j j' g, by { cases g; { dsimp, simp } } } }

/-- This is a helper construction that can be useful when verifying that a category has all
    coequalizers. Given `F : walking_parallel_family â¥¤ C`, which is really the same as
    `parallel_family (Î» j, F.map (line j))`, and a cotrident on `Î» j, F.map (line j)` we get a
    cocone on `F`.

    If you're thinking about using this, have a look at
    `has_wide_coequalizers_of_has_colimit_parallel_family`, which you may find to be an easier way
    of achieving your goal. -/
def cocone.of_cotrident
  {F : walking_parallel_family J â¥¤ C} (t : cotrident (Î» j, F.map (line j))) : cocone F :=
{ X := t.X,
  Î¹ :=
  { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X,
    naturality' := Î» j j' g, by { cases g; dsimp; simp [cotrident.app_one t] } } }

@[simp] lemma cone.of_trident_Ï€
  {F : walking_parallel_family J â¥¤ C} (t : trident (Î» j, F.map (line j))) (j) :
  (cone.of_trident t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl

@[simp] lemma cocone.of_cotrident_Î¹
  {F : walking_parallel_family J â¥¤ C} (t : cotrident (Î» j, F.map (line j))) (j) :
  (cocone.of_cotrident t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

/-- Given `F : walking_parallel_family â¥¤ C`, which is really the same as
    `parallel_family (Î» j, F.map (line j))` and a cone on `F`, we get a trident on
    `Î» j, F.map (line j)`. -/
def trident.of_cone
  {F : walking_parallel_family J â¥¤ C} (t : cone F) : trident (Î» j, F.map (line j)) :=
{ X := t.X,
  Ï€ := { app := Î» X, t.Ï€.app X â‰« eq_to_hom (by tidy) } }

/-- Given `F : walking_parallel_family â¥¤ C`, which is really the same as
    `parallel_family (F.map left) (F.map right)` and a cocone on `F`, we get a cotrident on
    `Î» j, F.map (line j)`. -/
def cotrident.of_cocone
  {F : walking_parallel_family J â¥¤ C} (t : cocone F) : cotrident (Î» j, F.map (line j)) :=
{ X := t.X,
  Î¹ := { app := Î» X, eq_to_hom (by tidy) â‰« t.Î¹.app X } }

@[simp] lemma trident.of_cone_Ï€ {F : walking_parallel_family J â¥¤ C} (t : cone F) (j) :
  (trident.of_cone t).Ï€.app j = t.Ï€.app j â‰« eq_to_hom (by tidy) := rfl
@[simp] lemma cotrident.of_cocone_Î¹ {F : walking_parallel_family J â¥¤ C} (t : cocone F) (j) :
  (cotrident.of_cocone t).Î¹.app j = eq_to_hom (by tidy) â‰« t.Î¹.app j := rfl

/--
Helper function for constructing morphisms between wide equalizer tridents.
-/
@[simps]
def trident.mk_hom [nonempty J] {s t : trident f} (k : s.X âŸ¶ t.X) (w : k â‰« t.Î¹ = s.Î¹) : s âŸ¶ t :=
{ hom := k,
  w' :=
  begin
    rintro âŸ¨_|_âŸ©,
    { exact w },
    { simpa using w =â‰« f (classical.arbitrary J) },
  end }

/--
To construct an isomorphism between tridents,
it suffices to give an isomorphism between the cone points
and check that it commutes with the `Î¹` morphisms.
-/
@[simps]
def trident.ext [nonempty J] {s t : trident f} (i : s.X â‰… t.X) (w : i.hom â‰« t.Î¹ = s.Î¹) : s â‰… t :=
{ hom := trident.mk_hom i.hom w,
  inv := trident.mk_hom i.inv (by rw [â† w, iso.inv_hom_id_assoc]) }

/--
Helper function for constructing morphisms between coequalizer cotridents.
-/
@[simps]
def cotrident.mk_hom [nonempty J] {s t : cotrident f} (k : s.X âŸ¶ t.X) (w : s.Ï€ â‰« k = t.Ï€) :
  s âŸ¶ t :=
{ hom := k,
  w' :=
  begin
    rintro âŸ¨_|_âŸ©,
    { simpa using f (classical.arbitrary J) â‰«= w },
    { exact w },
  end }

/--
To construct an isomorphism between cotridents,
it suffices to give an isomorphism between the cocone points
and check that it commutes with the `Ï€` morphisms.
-/
def cotrident.ext [nonempty J] {s t : cotrident f} (i : s.X â‰… t.X) (w : s.Ï€ â‰« i.hom = t.Ï€) :
  s â‰… t :=
{ hom := cotrident.mk_hom i.hom w,
  inv := cotrident.mk_hom i.inv (by rw [iso.comp_inv_eq, w]) }

variables (f)

section
/--
`has_wide_equalizer f` represents a particular choice of limiting cone for the parallel family of
morphisms `f`.
-/
abbreviation has_wide_equalizer := has_limit (parallel_family f)

variables [has_wide_equalizer f]

/-- If a wide equalizer of `f` exists, we can access an arbitrary choice of such by
    saying `wide_equalizer f`. -/
abbreviation wide_equalizer : C := limit (parallel_family f)

/-- If a wide equalizer of `f` exists, we can access the inclusion `wide_equalizer f âŸ¶ X` by
    saying `wide_equalizer.Î¹ f`. -/
abbreviation wide_equalizer.Î¹ : wide_equalizer f âŸ¶ X :=
limit.Ï€ (parallel_family f) zero

/--
A wide equalizer cone for a parallel family `f`.
-/
abbreviation wide_equalizer.trident : trident f := limit.cone (parallel_family f)

@[simp] lemma wide_equalizer.trident_Î¹ :
  (wide_equalizer.trident f).Î¹ = wide_equalizer.Î¹ f := rfl

@[simp] lemma wide_equalizer.trident_Ï€_app_zero :
  (wide_equalizer.trident f).Ï€.app zero = wide_equalizer.Î¹ f := rfl

@[reassoc] lemma wide_equalizer.condition (jâ‚ jâ‚‚ : J) :
  wide_equalizer.Î¹ f â‰« f jâ‚ = wide_equalizer.Î¹ f â‰« f jâ‚‚ :=
trident.condition jâ‚ jâ‚‚ $ limit.cone $ parallel_family f

/-- The wide_equalizer built from `wide_equalizer.Î¹ f` is limiting. -/
def wide_equalizer_is_wide_equalizer [nonempty J] :
  is_limit (trident.of_Î¹ (wide_equalizer.Î¹ f) (wide_equalizer.condition f)) :=
is_limit.of_iso_limit (limit.is_limit _) (trident.ext (iso.refl _) (by tidy))

variables {f}

/-- A morphism `k : W âŸ¶ X` satisfying `âˆ€ jâ‚ jâ‚‚, k â‰« f jâ‚ = k â‰« f jâ‚‚` factors through the
    wide equalizer of `f` via `wide_equalizer.lift : W âŸ¶ wide_equalizer f`. -/
abbreviation wide_equalizer.lift [nonempty J] {W : C} (k : W âŸ¶ X)
  (h : âˆ€ jâ‚ jâ‚‚, k â‰« f jâ‚ = k â‰« f jâ‚‚) :
  W âŸ¶ wide_equalizer f :=
limit.lift (parallel_family f) (trident.of_Î¹ k h)

@[simp, reassoc]
lemma wide_equalizer.lift_Î¹ [nonempty J] {W : C} (k : W âŸ¶ X) (h : âˆ€ jâ‚ jâ‚‚, k â‰« f jâ‚ = k â‰« f jâ‚‚) :
  wide_equalizer.lift k h â‰« wide_equalizer.Î¹ f = k :=
limit.lift_Ï€ _ _

/-- A morphism `k : W âŸ¶ X` satisfying `âˆ€ jâ‚ jâ‚‚, k â‰« f jâ‚ = k â‰« f jâ‚‚` induces a morphism
    `l : W âŸ¶ wide_equalizer f` satisfying `l â‰« wide_equalizer.Î¹ f = k`. -/
def wide_equalizer.lift' [nonempty J] {W : C} (k : W âŸ¶ X) (h : âˆ€ jâ‚ jâ‚‚, k â‰« f jâ‚ = k â‰« f jâ‚‚) :
  {l : W âŸ¶ wide_equalizer f // l â‰« wide_equalizer.Î¹ f = k} :=
âŸ¨wide_equalizer.lift k h, wide_equalizer.lift_Î¹ _ _âŸ©

/-- Two maps into a wide equalizer are equal if they are are equal when composed with the wide
    equalizer map. -/
@[ext] lemma wide_equalizer.hom_ext [nonempty J] {W : C} {k l : W âŸ¶ wide_equalizer f}
  (h : k â‰« wide_equalizer.Î¹ f = l â‰« wide_equalizer.Î¹ f) : k = l :=
trident.is_limit.hom_ext (limit.is_limit _) h

/-- A wide equalizer morphism is a monomorphism -/
instance wide_equalizer.Î¹_mono [nonempty J] : mono (wide_equalizer.Î¹ f) :=
{ right_cancellation := Î» Z h k w, wide_equalizer.hom_ext w }

end

section
variables {f}
/-- The wide equalizer morphism in any limit cone is a monomorphism. -/
lemma mono_of_is_limit_parallel_family [nonempty J] {c : cone (parallel_family f)}
  (i : is_limit c) :
  mono (trident.Î¹ c) :=
{ right_cancellation := Î» Z h k w, trident.is_limit.hom_ext i w }

end

section
/--
`has_wide_coequalizer f g` represents a particular choice of colimiting cocone
for the parallel family of morphisms `f`.
-/
abbreviation has_wide_coequalizer := has_colimit (parallel_family f)

variables [has_wide_coequalizer f]

/-- If a wide coequalizer of `f`, we can access an arbitrary choice of such by
    saying `wide_coequalizer f`. -/
abbreviation wide_coequalizer : C := colimit (parallel_family f)

/--  If a wide_coequalizer of `f` exists, we can access the corresponding projection by
    saying `wide_coequalizer.Ï€ f`. -/
abbreviation wide_coequalizer.Ï€ : Y âŸ¶ wide_coequalizer f :=
colimit.Î¹ (parallel_family f) one

/--
An arbitrary choice of coequalizer cocone for a parallel family `f`.
-/
abbreviation wide_coequalizer.cotrident : cotrident f := colimit.cocone (parallel_family f)

@[simp] lemma wide_coequalizer.cotrident_Ï€ :
  (wide_coequalizer.cotrident f).Ï€ = wide_coequalizer.Ï€ f := rfl

@[simp] lemma wide_coequalizer.cotrident_Î¹_app_one :
  (wide_coequalizer.cotrident f).Î¹.app one = wide_coequalizer.Ï€ f := rfl

@[reassoc] lemma wide_coequalizer.condition (jâ‚ jâ‚‚ : J) :
  f jâ‚ â‰« wide_coequalizer.Ï€ f = f jâ‚‚ â‰« wide_coequalizer.Ï€ f :=
cotrident.condition jâ‚ jâ‚‚ $ colimit.cocone $ parallel_family f

/-- The cotrident built from `wide_coequalizer.Ï€ f` is colimiting. -/
def wide_coequalizer_is_wide_coequalizer [nonempty J] :
  is_colimit (cotrident.of_Ï€ (wide_coequalizer.Ï€ f) (wide_coequalizer.condition f)) :=
is_colimit.of_iso_colimit (colimit.is_colimit _) (cotrident.ext (iso.refl _) (by tidy))

variables {f}

/-- Any morphism `k : Y âŸ¶ W` satisfying `âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« k = f jâ‚‚ â‰« k` factors through the
    wide coequalizer of `f` via `wide_coequalizer.desc : wide_coequalizer f âŸ¶ W`. -/
abbreviation wide_coequalizer.desc [nonempty J] {W : C} (k : Y âŸ¶ W)
  (h : âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« k = f jâ‚‚ â‰« k) :
  wide_coequalizer f âŸ¶ W :=
colimit.desc (parallel_family f) (cotrident.of_Ï€ k h)

@[simp, reassoc]
lemma wide_coequalizer.Ï€_desc [nonempty J] {W : C} (k : Y âŸ¶ W) (h : âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« k = f jâ‚‚ â‰« k) :
  wide_coequalizer.Ï€ f â‰« wide_coequalizer.desc k h = k :=
colimit.Î¹_desc _ _

/-- Any morphism `k : Y âŸ¶ W` satisfying `âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« k = f jâ‚‚ â‰« k` induces a morphism
    `l : wide_coequalizer f âŸ¶ W` satisfying `wide_coequalizer.Ï€ â‰« g = l`. -/
def wide_coequalizer.desc' [nonempty J] {W : C} (k : Y âŸ¶ W) (h : âˆ€ jâ‚ jâ‚‚, f jâ‚ â‰« k = f jâ‚‚ â‰« k) :
  {l : wide_coequalizer f âŸ¶ W // wide_coequalizer.Ï€ f â‰« l = k} :=
âŸ¨wide_coequalizer.desc k h, wide_coequalizer.Ï€_desc _ _âŸ©

/-- Two maps from a wide coequalizer are equal if they are equal when composed with the wide
    coequalizer map -/
@[ext] lemma wide_coequalizer.hom_ext [nonempty J] {W : C} {k l : wide_coequalizer f âŸ¶ W}
  (h : wide_coequalizer.Ï€ f â‰« k = wide_coequalizer.Ï€ f â‰« l) : k = l :=
cotrident.is_colimit.hom_ext (colimit.is_colimit _) h

/-- A wide coequalizer morphism is an epimorphism -/
instance wide_coequalizer.Ï€_epi [nonempty J] : epi (wide_coequalizer.Ï€ f) :=
{ left_cancellation := Î» Z h k w, wide_coequalizer.hom_ext w }

end

section
variables {f}

/-- The wide coequalizer morphism in any colimit cocone is an epimorphism. -/
lemma epi_of_is_colimit_parallel_family [nonempty J] {c : cocone (parallel_family f)}
  (i : is_colimit c) :
  epi (c.Î¹.app one) :=
{ left_cancellation := Î» Z h k w, cotrident.is_colimit.hom_ext i w }

end

variables (C)

/-- `has_wide_equalizers` represents a choice of wide equalizer for every family of morphisms -/
abbreviation has_wide_equalizers := Î  J, has_limits_of_shape (walking_parallel_family.{w} J) C

/-- `has_wide_coequalizers` represents a choice of wide coequalizer for every family of morphisms -/
abbreviation has_wide_coequalizers := Î  J, has_colimits_of_shape (walking_parallel_family.{w} J) C

/-- If `C` has all limits of diagrams `parallel_family f`, then it has all wide equalizers -/
lemma has_wide_equalizers_of_has_limit_parallel_family
  [Î  {J : Type w} {X Y : C} {f : J â†’ (X âŸ¶ Y)}, has_limit (parallel_family f)] :
  has_wide_equalizers.{w} C :=
Î» J, { has_limit := Î» F, has_limit_of_iso (diagram_iso_parallel_family F).symm }

/-- If `C` has all colimits of diagrams `parallel_family f`, then it has all wide coequalizers -/
lemma has_wide_coequalizers_of_has_colimit_parallel_family
  [Î  {J : Type w} {X Y : C} {f : J â†’ (X âŸ¶ Y)}, has_colimit (parallel_family f)] :
  has_wide_coequalizers.{w} C :=
Î» J, { has_colimit := Î» F, has_colimit_of_iso (diagram_iso_parallel_family F) }

@[priority 10]
instance has_equalizers_of_has_wide_equalizers [has_wide_equalizers.{w} C] : has_equalizers C :=
has_limits_of_shape_of_equivalence.{w} walking_parallel_family_equiv_walking_parallel_pair

@[priority 10]
instance has_coequalizers_of_has_wide_coequalizers [has_wide_coequalizers.{w} C] :
  has_coequalizers C :=
has_colimits_of_shape_of_equivalence.{w} walking_parallel_family_equiv_walking_parallel_pair

end category_theory.limits
