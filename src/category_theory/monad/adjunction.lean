/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.adjunction.reflective
import category_theory.monad.algebra

/-!
# Adjunctions and monads

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We develop the basic relationship between adjunctions and monads.

Given an adjunction `h : L ‚ä£ R`, we have `h.to_monad : monad C` and `h.to_comonad : comonad D`.
We then have
`monad.comparison (h : L ‚ä£ R) : D ‚•§ h.to_monad.algebra`
sending `Y : D` to the Eilenberg-Moore algebra for `L ‚ãô R` with underlying object `R.obj X`,
and dually `comonad.comparison`.

We say `R : D ‚•§ C` is `monadic_right_adjoint`, if it is a right adjoint and its `monad.comparison`
is an equivalence of categories. (Similarly for `monadic_left_adjoint`.)

Finally we prove that reflective functors are `monadic_right_adjoint`.
-/

namespace category_theory
open category

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ -- morphism levels before object levels. See note [category_theory universes].

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]
variables {L : C ‚•§ D} {R : D ‚•§ C}

namespace adjunction

/--
For a pair of functors `L : C ‚•§ D`, `R : D ‚•§ C`, an adjunction `h : L ‚ä£ R` induces a monad on
the category `C`.
-/
@[simps]
def to_monad (h : L ‚ä£ R) : monad C :=
{ to_functor := L ‚ãô R,
  Œ∑' := h.unit,
  Œº' := whisker_right (whisker_left L h.counit) R,
  assoc' := Œª X, by { dsimp, rw [‚ÜêR.map_comp], simp },
  right_unit' := Œª X, by { dsimp, rw [‚ÜêR.map_comp], simp } }

/--
For a pair of functors `L : C ‚•§ D`, `R : D ‚•§ C`, an adjunction `h : L ‚ä£ R` induces a comonad on
the category `D`.
-/
@[simps]
def to_comonad (h : L ‚ä£ R) : comonad D :=
{ to_functor := R ‚ãô L,
  Œµ' := h.counit,
  Œ¥' := whisker_right (whisker_left R h.unit) L,
  coassoc' := Œª X, by { dsimp, rw ‚Üê L.map_comp, simp },
  right_counit' := Œª X, by { dsimp, rw ‚Üê L.map_comp, simp } }

/-- The monad induced by the Eilenberg-Moore adjunction is the original monad.  -/
@[simps]
def adj_to_monad_iso (T : monad C) : T.adj.to_monad ‚âÖ T :=
monad_iso.mk (nat_iso.of_components (Œª X, iso.refl _) (by tidy))
  (Œª X, by { dsimp, simp })
  (Œª X, by { dsimp, simp })

/-- The comonad induced by the Eilenberg-Moore adjunction is the original comonad. -/
@[simps]
def adj_to_comonad_iso (G : comonad C) : G.adj.to_comonad ‚âÖ G :=
comonad_iso.mk (nat_iso.of_components (Œª X, iso.refl _) (by tidy))
  (Œª X, by { dsimp, simp })
  (Œª X, by { dsimp, simp })

end adjunction

/--
Gven any adjunction `L ‚ä£ R`, there is a comparison functor `category_theory.monad.comparison R`
sending objects `Y : D` to Eilenberg-Moore algebras for `L ‚ãô R` with underlying object `R.obj X`.

We later show that this is full when `R` is full, faithful when `R` is faithful,
and essentially surjective when `R` is reflective.
-/
@[simps]
def monad.comparison (h : L ‚ä£ R) : D ‚•§ h.to_monad.algebra :=
{ obj := Œª X,
  { A := R.obj X,
    a := R.map (h.counit.app X),
    assoc' := by { dsimp, rw [‚Üê R.map_comp, ‚Üê adjunction.counit_naturality, R.map_comp], refl } },
  map := Œª X Y f,
  { f := R.map f,
    h' := by { dsimp, rw [‚Üê R.map_comp, adjunction.counit_naturality, R.map_comp] } } }.

/--
The underlying object of `(monad.comparison R).obj X` is just `R.obj X`.
-/
@[simps]
def monad.comparison_forget (h : L ‚ä£ R) :
  monad.comparison h ‚ãô h.to_monad.forget ‚âÖ R :=
{ hom := { app := Œª X, ùüô _, },
  inv := { app := Œª X, ùüô _, } }

lemma monad.left_comparison (h : L ‚ä£ R) : L ‚ãô monad.comparison h = h.to_monad.free := rfl

instance [faithful R] (h : L ‚ä£ R) :
  faithful (monad.comparison h) :=
{ map_injective' := Œª X Y f g w, R.map_injective (congr_arg monad.algebra.hom.f w : _) }

instance (T : monad C) : full (monad.comparison T.adj) :=
{ preimage := Œª X Y f, ‚ü®f.f, by simpa using f.h‚ü© }

instance (T : monad C) : ess_surj (monad.comparison T.adj) :=
{ mem_ess_image := Œª X,
  ‚ü®{ A := X.A, a := X.a, unit' := by simpa using X.unit, assoc' := by simpa using X.assoc },
    ‚ü®monad.algebra.iso_mk (iso.refl _) (by simp)‚ü©‚ü© }

/--
Gven any adjunction `L ‚ä£ R`, there is a comparison functor `category_theory.comonad.comparison L`
sending objects `X : C` to Eilenberg-Moore coalgebras for `L ‚ãô R` with underlying object
`L.obj X`.
-/
@[simps]
def comonad.comparison (h : L ‚ä£ R) : C ‚•§ h.to_comonad.coalgebra :=
{ obj := Œª X,
  { A := L.obj X,
    a := L.map (h.unit.app X),
    coassoc' := by { dsimp, rw [‚Üê L.map_comp, ‚Üê adjunction.unit_naturality, L.map_comp], refl } },
  map := Œª X Y f,
  { f := L.map f,
    h' := by { dsimp, rw ‚Üê L.map_comp, simp } } }

/--
The underlying object of `(comonad.comparison L).obj X` is just `L.obj X`.
-/
@[simps]
def comonad.comparison_forget {L : C ‚•§ D} {R : D ‚•§ C} (h : L ‚ä£ R) :
  comonad.comparison h ‚ãô h.to_comonad.forget ‚âÖ L :=
{ hom := { app := Œª X, ùüô _, },
  inv := { app := Œª X, ùüô _, } }

lemma comonad.left_comparison (h : L ‚ä£ R) : R ‚ãô comonad.comparison h = h.to_comonad.cofree := rfl

instance comonad.comparison_faithful_of_faithful [faithful L] (h : L ‚ä£ R) :
  faithful (comonad.comparison h) :=
{ map_injective' := Œª X Y f g w, L.map_injective (congr_arg comonad.coalgebra.hom.f w : _) }

instance (G : comonad C) : full (comonad.comparison G.adj) :=
{ preimage := Œª X Y f, ‚ü®f.f, by simpa using f.h‚ü© }

instance (G : comonad C) : ess_surj (comonad.comparison G.adj) :=
{ mem_ess_image := Œª X,
  ‚ü®{ A := X.A, a := X.a, counit' := by simpa using X.counit, coassoc' := by simpa using X.coassoc },
    ‚ü®comonad.coalgebra.iso_mk (iso.refl _) (by simp)‚ü©‚ü© }

/--
A right adjoint functor `R : D ‚•§ C` is *monadic* if the comparison functor `monad.comparison R`
from `D` to the category of Eilenberg-Moore algebras for the adjunction is an equivalence.
-/
class monadic_right_adjoint (R : D ‚•§ C) extends is_right_adjoint R :=
(eqv : is_equivalence (monad.comparison (adjunction.of_right_adjoint R)))

/--
A left adjoint functor `L : C ‚•§ D` is *comonadic* if the comparison functor `comonad.comparison L`
from `C` to the category of Eilenberg-Moore algebras for the adjunction is an equivalence.
-/
class comonadic_left_adjoint (L : C ‚•§ D) extends is_left_adjoint L :=
(eqv : is_equivalence (comonad.comparison (adjunction.of_left_adjoint L)))

noncomputable instance (T : monad C) : monadic_right_adjoint T.forget :=
‚ü®(equivalence.of_fully_faithfully_ess_surj _ : is_equivalence (monad.comparison T.adj))‚ü©

noncomputable instance (G : comonad C) : comonadic_left_adjoint G.forget :=
‚ü®(equivalence.of_fully_faithfully_ess_surj _ : is_equivalence (comonad.comparison G.adj))‚ü©

-- TODO: This holds more generally for idempotent adjunctions, not just reflective adjunctions.
instance Œº_iso_of_reflective [reflective R] : is_iso (adjunction.of_right_adjoint R).to_monad.Œº :=
by { dsimp, apply_instance }

attribute [instance] monadic_right_adjoint.eqv
attribute [instance] comonadic_left_adjoint.eqv

namespace reflective

instance [reflective R] (X : (adjunction.of_right_adjoint R).to_monad.algebra) :
  is_iso ((adjunction.of_right_adjoint R).unit.app X.A) :=
‚ü®‚ü®X.a, ‚ü®X.unit, begin
    dsimp only [functor.id_obj],
    rw ‚Üê (adjunction.of_right_adjoint R).unit_naturality,
    dsimp only [functor.comp_obj, adjunction.to_monad_coe],
    rw [unit_obj_eq_map_unit, ‚Üêfunctor.map_comp, ‚Üêfunctor.map_comp],
    erw X.unit,
    simp,
  end‚ü©‚ü©‚ü©

instance comparison_ess_surj [reflective R] :
  ess_surj (monad.comparison (adjunction.of_right_adjoint R)) :=
begin
  refine ‚ü®Œª X, ‚ü®(left_adjoint R).obj X.A, ‚ü®_‚ü©‚ü©‚ü©,
  symmetry,
  refine monad.algebra.iso_mk _ _,
  { exact as_iso ((adjunction.of_right_adjoint R).unit.app X.A) },
  dsimp only [functor.comp_map, monad.comparison_obj_a, as_iso_hom, functor.comp_obj,
    monad.comparison_obj_A, monad_to_functor_eq_coe, adjunction.to_monad_coe],
  rw [‚Üêcancel_epi ((adjunction.of_right_adjoint R).unit.app X.A), adjunction.unit_naturality_assoc,
      adjunction.right_triangle_components, comp_id],
  apply (X.unit_assoc _).symm,
end

instance comparison_full [full R] [is_right_adjoint R] :
  full (monad.comparison (adjunction.of_right_adjoint R)) :=
{ preimage := Œª X Y f, R.preimage f.f }

end reflective

-- It is possible to do this computably since the construction gives the data of the inverse, not
-- just the existence of an inverse on each object.
/-- Any reflective inclusion has a monadic right adjoint.
    cf Prop 5.3.3 of [Riehl][riehl2017] -/
@[priority 100] -- see Note [lower instance priority]
noncomputable instance monadic_of_reflective [reflective R] : monadic_right_adjoint R :=
{ eqv := equivalence.of_fully_faithfully_ess_surj _ }

end category_theory
