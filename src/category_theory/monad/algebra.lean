/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.monad.basic
import category_theory.adjunction.basic

/-!
# Eilenberg-Moore algebras for a monad

This file defines Eilenberg-Moore algebras for a monad, and provides the category instance for them.
Further it defines the adjoint pair of free and forgetful functors, respectively
from and to the original category.

## References
* [Riehl, *Category theory in context*, Section 5.2.4][riehl2017]
-/

namespace category_theory
open category

universes v‚ÇÅ u‚ÇÅ -- declare the `v`'s first; see `category_theory.category` for an explanation

variables {C : Type u‚ÇÅ} [ùíû : category.{v‚ÇÅ} C]
include ùíû

namespace monad

/-- An Eilenberg-Moore algebra for a monad `T`.
    cf Definition 5.2.3 in [Riehl][riehl2017]. -/
structure algebra (T : C ‚•§ C) [monad.{v‚ÇÅ} T] : Type (max u‚ÇÅ v‚ÇÅ) :=
(A : C)
(a : T.obj A ‚ü∂ A)
(unit' : (Œ∑_ T).app A ‚â´ a = ùüô A . obviously)
(assoc' : ((Œº_ T).app A ‚â´ a) = (T.map a ‚â´ a) . obviously)

restate_axiom algebra.unit'
restate_axiom algebra.assoc'

namespace algebra
variables {T : C ‚•§ C} [monad.{v‚ÇÅ} T]

structure hom (A B : algebra T) :=
(f : A.A ‚ü∂ B.A)
(h' : T.map f ‚â´ B.a = A.a ‚â´ f . obviously)

restate_axiom hom.h'
attribute [simp] hom.h

namespace hom
@[extensionality] lemma ext {A B : algebra T} (f g : hom A B) (w : f.f = g.f) : f = g :=
by { cases f, cases g, congr, assumption }

@[simps] def id (A : algebra T) : hom A A :=
{ f := ùüô A.A }

@[simps] def comp {P Q R : algebra T} (f : hom P Q) (g : hom Q R) : hom P R :=
{ f := f.f ‚â´ g.f,
  h' := by rw [functor.map_comp, category.assoc, g.h, ‚Üêcategory.assoc, f.h, category.assoc] }

end hom

/-- The category of Eilenberg-Moore algebras for a monad.
    cf Definition 5.2.4 in [Riehl][riehl2017]. -/
@[simps] instance EilenbergMoore : category (algebra T) :=
{ hom := hom,
  id := hom.id,
  comp := @hom.comp _ _ _ _ }

end algebra

variables (T : C ‚•§ C) [monad.{v‚ÇÅ} T]

@[simps] def forget : algebra T ‚•§ C :=
{ obj := Œª A, A.A,
  map := Œª A B f, f.f }

@[simps] def free : C ‚•§ algebra T :=
{ obj := Œª X,
  { A := T.obj X,
    a := (Œº_ T).app X,
    assoc' := (monad.assoc T _).symm },
  map := Œª X Y f,
  { f := T.map f,
    h' := by erw (Œº_ T).naturality } }

/-- The adjunction between the free and forgetful constructions for Eilenberg-Moore algebras for a monad.
    cf Lemma 5.2.8 of [Riehl][riehl2017]. -/
def adj : free T ‚ä£ forget T :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X Y,
  { to_fun := Œª f, (Œ∑_ T).app X ‚â´ f.f,
    inv_fun := Œª f,
    { f := T.map f ‚â´ Y.a,
      h' :=
      begin
        dsimp, simp,
        conv { to_rhs, rw [‚Üêcategory.assoc, ‚Üê(Œº_ T).naturality, category.assoc], erw algebra.assoc },
        refl,
      end },
    left_inv := Œª f,
    begin
      ext1, dsimp,
      simp only [free_obj_a, functor.map_comp, algebra.hom.h, category.assoc],
      erw [‚Üêcategory.assoc, monad.right_unit, id_comp],
    end,
    right_inv := Œª f,
    begin
      dsimp,
      erw [‚Üêcategory.assoc, ‚Üê(Œ∑_ T).naturality, functor.id_map,
            category.assoc, Y.unit, comp_id],
    end }}

end monad

end category_theory
