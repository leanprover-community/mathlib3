/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.monad.basic
import category_theory.adjunction.basic
import category_theory.functor.epi_mono

/-!
# Eilenberg-Moore (co)algebras for a (co)monad

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines Eilenberg-Moore (co)algebras for a (co)monad,
and provides the category instance for them.

Further it defines the adjoint pair of free and forgetful functors, respectively
from and to the original category, as well as the adjoint pair of forgetful and
cofree functors, respectively from and to the original category.

## References
* [Riehl, *Category theory in context*, Section 5.2.4][riehl2017]
-/

namespace category_theory
open category

universes v‚ÇÅ u‚ÇÅ -- morphism levels before object levels. See note [category_theory universes].

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C]

namespace monad

/-- An Eilenberg-Moore algebra for a monad `T`.
    cf Definition 5.2.3 in [Riehl][riehl2017]. -/
structure algebra (T : monad C) : Type (max u‚ÇÅ v‚ÇÅ) :=
(A : C)
(a : (T : C ‚•§ C).obj A ‚ü∂ A)
(unit' : T.Œ∑.app A ‚â´ a = ùüô A . obviously)
(assoc' : T.Œº.app A ‚â´ a = (T : C ‚•§ C).map a ‚â´ a . obviously)

restate_axiom algebra.unit'
restate_axiom algebra.assoc'
attribute [reassoc] algebra.unit algebra.assoc

namespace algebra
variables {T : monad C}

/-- A morphism of Eilenberg‚ÄìMoore algebras for the monad `T`. -/
@[ext] structure hom (A B : algebra T) :=
(f : A.A ‚ü∂ B.A)
(h' : (T : C ‚•§ C).map f ‚â´ B.a = A.a ‚â´ f . obviously)

restate_axiom hom.h'
attribute [simp, reassoc] hom.h

namespace hom

/-- The identity homomorphism for an Eilenberg‚ÄìMoore algebra. -/
def id (A : algebra T) : hom A A :=
{ f := ùüô A.A }

instance (A : algebra T) : inhabited (hom A A) := ‚ü®{ f := ùüô _ }‚ü©

/-- Composition of Eilenberg‚ÄìMoore algebra homomorphisms. -/
def comp {P Q R : algebra T} (f : hom P Q) (g : hom Q R) : hom P R :=
{ f := f.f ‚â´ g.f }

end hom

instance : category_struct (algebra T) :=
{ hom := hom,
  id := hom.id,
  comp := @hom.comp _ _ _ }

@[simp] lemma comp_eq_comp {A A' A'' : algebra T} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') :
  algebra.hom.comp f g = f ‚â´ g := rfl
@[simp] lemma id_eq_id (A : algebra T) :
  algebra.hom.id A = ùüô A := rfl

@[simp] lemma id_f (A : algebra T) : (ùüô A : A ‚ü∂ A).f = ùüô A.A := rfl
@[simp] lemma comp_f {A A' A'' : algebra T} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') :
  (f ‚â´ g).f = f.f ‚â´ g.f := rfl

/-- The category of Eilenberg-Moore algebras for a monad.
    cf Definition 5.2.4 in [Riehl][riehl2017]. -/
instance EilenbergMoore : category (algebra T) := {}.

/--
To construct an isomorphism of algebras, it suffices to give an isomorphism of the carriers which
commutes with the structure morphisms.
-/
@[simps]
def iso_mk {A B : algebra T} (h : A.A ‚âÖ B.A) (w : (T : C ‚•§ C).map h.hom ‚â´ B.a = A.a ‚â´ h.hom) :
  A ‚âÖ B :=
{ hom := { f := h.hom },
  inv :=
  { f := h.inv,
    h' := by { rw [h.eq_comp_inv, category.assoc, ‚Üêw, ‚Üêfunctor.map_comp_assoc], simp } } }

end algebra

variables (T : monad C)

/-- The forgetful functor from the Eilenberg-Moore category, forgetting the algebraic structure. -/
@[simps] def forget : algebra T ‚•§ C :=
{ obj := Œª A, A.A,
  map := Œª A B f, f.f }

/-- The free functor from the Eilenberg-Moore category, constructing an algebra for any object. -/
@[simps] def free : C ‚•§ algebra T :=
{ obj := Œª X,
  { A := T.obj X,
    a := T.Œº.app X,
    assoc' := (T.assoc _).symm },
  map := Œª X Y f,
  { f := T.map f,
    h' := T.Œº.naturality _ } }

instance [inhabited C] : inhabited (algebra T) :=
‚ü®(free T).obj default‚ü©

/-- The adjunction between the free and forgetful constructions for Eilenberg-Moore algebras for
  a monad. cf Lemma 5.2.8 of [Riehl][riehl2017]. -/
-- The other two `simps` projection lemmas can be derived from these two, so `simp_nf` complains if
-- those are added too
@[simps unit counit]
def adj : T.free ‚ä£ T.forget :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X Y,
  { to_fun := Œª f, T.Œ∑.app X ‚â´ f.f,
    inv_fun := Œª f,
    { f := T.map f ‚â´ Y.a,
      h' := by { dsimp, simp [‚ÜêY.assoc, ‚ÜêT.Œº.naturality_assoc] } },
    left_inv := Œª f, by { ext, dsimp, simp },
    right_inv := Œª f,
    begin
      dsimp only [forget_obj, monad_to_functor_eq_coe],
      rw [‚ÜêT.Œ∑.naturality_assoc, Y.unit],
      apply category.comp_id,
    end }}

/--
Given an algebra morphism whose carrier part is an isomorphism, we get an algebra isomorphism.
-/
lemma algebra_iso_of_iso {A B : algebra T} (f : A ‚ü∂ B) [is_iso f.f] : is_iso f :=
‚ü®‚ü®{ f := inv f.f,
    h' := by { rw [is_iso.eq_comp_inv f.f, category.assoc, ‚Üê f.h], simp } }, by tidy‚ü©‚ü©

instance forget_reflects_iso : reflects_isomorphisms T.forget :=
{ reflects := Œª A B, algebra_iso_of_iso T }

instance forget_faithful : faithful T.forget := {}

/-- Given an algebra morphism whose carrier part is an epimorphism, we get an algebra epimorphism.
-/
lemma algebra_epi_of_epi {X Y : algebra T} (f : X ‚ü∂ Y) [h : epi f.f] : epi f :=
(forget T).epi_of_epi_map h

/-- Given an algebra morphism whose carrier part is a monomorphism, we get an algebra monomorphism.
-/
lemma algebra_mono_of_mono {X Y : algebra T} (f : X ‚ü∂ Y) [h : mono f.f] : mono f :=
(forget T).mono_of_mono_map h

instance : is_right_adjoint T.forget := ‚ü®T.free, T.adj‚ü©
@[simp] lemma left_adjoint_forget : left_adjoint T.forget = T.free := rfl
@[simp] lemma of_right_adjoint_forget : adjunction.of_right_adjoint T.forget = T.adj := rfl

/--
Given a monad morphism from `T‚ÇÇ` to `T‚ÇÅ`, we get a functor from the algebras of `T‚ÇÅ` to algebras of
`T‚ÇÇ`.
-/
@[simps]
def algebra_functor_of_monad_hom {T‚ÇÅ T‚ÇÇ : monad C} (h : T‚ÇÇ ‚ü∂ T‚ÇÅ) :
  algebra T‚ÇÅ ‚•§ algebra T‚ÇÇ :=
{ obj := Œª A,
  { A := A.A,
    a := h.app A.A ‚â´ A.a,
    unit' := by { dsimp, simp [A.unit] },
    assoc' := by { dsimp, simp [A.assoc] } },
  map := Œª A‚ÇÅ A‚ÇÇ f,
  { f := f.f } }

/--
The identity monad morphism induces the identity functor from the category of algebras to itself.
-/
@[simps {rhs_md := semireducible}]
def algebra_functor_of_monad_hom_id {T‚ÇÅ : monad C} :
  algebra_functor_of_monad_hom (ùüô T‚ÇÅ) ‚âÖ ùü≠ _ :=
nat_iso.of_components
  (Œª X, algebra.iso_mk (iso.refl _) (by { dsimp, simp, }))
  (Œª X Y f, by { ext, dsimp, simp })

/--
A composition of monad morphisms gives the composition of corresponding functors.
-/
@[simps {rhs_md := semireducible}]
def algebra_functor_of_monad_hom_comp {T‚ÇÅ T‚ÇÇ T‚ÇÉ : monad C} (f : T‚ÇÅ ‚ü∂ T‚ÇÇ) (g : T‚ÇÇ ‚ü∂ T‚ÇÉ) :
  algebra_functor_of_monad_hom (f ‚â´ g) ‚âÖ
    algebra_functor_of_monad_hom g ‚ãô algebra_functor_of_monad_hom f :=
nat_iso.of_components
  (Œª X, algebra.iso_mk (iso.refl _) (by { dsimp, simp }))
  (Œª X Y f, by { ext, dsimp, simp })

/--
If `f` and `g` are two equal morphisms of monads, then the functors of algebras induced by them
are isomorphic.
We define it like this as opposed to using `eq_to_iso` so that the components are nicer to prove
lemmas about.
-/
@[simps {rhs_md := semireducible}]
def algebra_functor_of_monad_hom_eq {T‚ÇÅ T‚ÇÇ : monad C} {f g : T‚ÇÅ ‚ü∂ T‚ÇÇ} (h : f = g) :
  algebra_functor_of_monad_hom f ‚âÖ algebra_functor_of_monad_hom g :=
nat_iso.of_components
  (Œª X, algebra.iso_mk (iso.refl _) (by { dsimp, simp [h] }))
  (Œª X Y f, by { ext, dsimp, simp })

/--
Isomorphic monads give equivalent categories of algebras. Furthermore, they are equivalent as
categories over `C`, that is, we have `algebra_equiv_of_iso_monads h ‚ãô forget = forget`.
-/
@[simps]
def algebra_equiv_of_iso_monads {T‚ÇÅ T‚ÇÇ : monad C} (h : T‚ÇÅ ‚âÖ T‚ÇÇ) :
  algebra T‚ÇÅ ‚âå algebra T‚ÇÇ :=
{ functor := algebra_functor_of_monad_hom h.inv,
  inverse := algebra_functor_of_monad_hom h.hom,
  unit_iso :=
    algebra_functor_of_monad_hom_id.symm ‚â™‚â´
    algebra_functor_of_monad_hom_eq (by simp) ‚â™‚â´
    algebra_functor_of_monad_hom_comp _ _,
  counit_iso :=
    (algebra_functor_of_monad_hom_comp _ _).symm ‚â™‚â´
    algebra_functor_of_monad_hom_eq (by simp) ‚â™‚â´
    algebra_functor_of_monad_hom_id }

@[simp] lemma algebra_equiv_of_iso_monads_comp_forget {T‚ÇÅ T‚ÇÇ : monad C} (h : T‚ÇÅ ‚ü∂ T‚ÇÇ) :
  algebra_functor_of_monad_hom h ‚ãô forget _ = forget _ :=
rfl

end monad

namespace comonad

/-- An Eilenberg-Moore coalgebra for a comonad `T`. -/
@[nolint has_nonempty_instance]
structure coalgebra (G : comonad C) : Type (max u‚ÇÅ v‚ÇÅ) :=
(A : C)
(a : A ‚ü∂ (G : C ‚•§ C).obj A)
(counit' : a ‚â´ G.Œµ.app A = ùüô A . obviously)
(coassoc' : a ‚â´ G.Œ¥.app A = a ‚â´ G.map a . obviously)

restate_axiom coalgebra.counit'
restate_axiom coalgebra.coassoc'
attribute [reassoc] coalgebra.counit coalgebra.coassoc

namespace coalgebra
variables {G : comonad C}

/-- A morphism of Eilenberg-Moore coalgebras for the comonad `G`. -/
@[ext, nolint has_nonempty_instance] structure hom (A B : coalgebra G) :=
(f : A.A ‚ü∂ B.A)
(h' : A.a ‚â´ (G : C ‚•§ C).map f = f ‚â´ B.a . obviously)

restate_axiom hom.h'
attribute [simp, reassoc] hom.h

namespace hom

/-- The identity homomorphism for an Eilenberg‚ÄìMoore coalgebra. -/
def id (A : coalgebra G) : hom A A :=
{ f := ùüô A.A }

/-- Composition of Eilenberg‚ÄìMoore coalgebra homomorphisms. -/
def comp {P Q R : coalgebra G} (f : hom P Q) (g : hom Q R) : hom P R :=
{ f := f.f ‚â´ g.f }

end hom

/-- The category of Eilenberg-Moore coalgebras for a comonad. -/
instance : category_struct (coalgebra G) :=
{ hom := hom,
  id := hom.id,
  comp := @hom.comp _ _ _ }

@[simp] lemma comp_eq_comp {A A' A'' : coalgebra G} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') :
  coalgebra.hom.comp f g = f ‚â´ g := rfl
@[simp] lemma id_eq_id (A : coalgebra G) :
  coalgebra.hom.id A = ùüô A := rfl

@[simp] lemma id_f (A : coalgebra G) : (ùüô A : A ‚ü∂ A).f = ùüô A.A := rfl
@[simp] lemma comp_f {A A' A'' : coalgebra G} (f : A ‚ü∂ A') (g : A' ‚ü∂ A'') :
  (f ‚â´ g).f = f.f ‚â´ g.f := rfl

/-- The category of Eilenberg-Moore coalgebras for a comonad. -/
instance EilenbergMoore : category (coalgebra G) := {}.

/--
To construct an isomorphism of coalgebras, it suffices to give an isomorphism of the carriers which
commutes with the structure morphisms.
-/
@[simps]
def iso_mk {A B : coalgebra G} (h : A.A ‚âÖ B.A) (w : A.a ‚â´ (G : C ‚•§ C).map h.hom = h.hom ‚â´ B.a) :
  A ‚âÖ B :=
{ hom := { f := h.hom },
  inv :=
  { f := h.inv,
    h' := by { rw [h.eq_inv_comp, ‚Üêreassoc_of w, ‚Üêfunctor.map_comp], simp } } }

end coalgebra

variables (G : comonad C)

/-- The forgetful functor from the Eilenberg-Moore category, forgetting the coalgebraic
structure. -/
@[simps] def forget : coalgebra G ‚•§ C :=
{ obj := Œª A, A.A,
  map := Œª A B f, f.f }

/-- The cofree functor from the Eilenberg-Moore category, constructing a coalgebra for any
object. -/
@[simps] def cofree : C ‚•§ coalgebra G :=
{ obj := Œª X,
  { A := G.obj X,
    a := G.Œ¥.app X,
    coassoc' := (G.coassoc _).symm },
  map := Œª X Y f,
  { f := G.map f,
    h' := (G.Œ¥.naturality _).symm } }

/--
The adjunction between the cofree and forgetful constructions for Eilenberg-Moore coalgebras
for a comonad.
-/
-- The other two `simps` projection lemmas can be derived from these two, so `simp_nf` complains if
-- those are added too
@[simps unit counit]
def adj : G.forget ‚ä£ G.cofree :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Œª X Y,
  { to_fun := Œª f,
    { f := X.a ‚â´ G.map f,
      h' := by { dsimp, simp [‚Üêcoalgebra.coassoc_assoc] } },
    inv_fun := Œª g, g.f ‚â´ G.Œµ.app Y,
    left_inv := Œª f,
      by { dsimp, rw [category.assoc, G.Œµ.naturality, functor.id_map, X.counit_assoc] },
    right_inv := Œª g,
    begin
      ext1, dsimp,
      rw [functor.map_comp, g.h_assoc, cofree_obj_a, comonad.right_counit],
      apply comp_id,
    end }}

/--
Given a coalgebra morphism whose carrier part is an isomorphism, we get a coalgebra isomorphism.
-/
lemma coalgebra_iso_of_iso {A B : coalgebra G} (f : A ‚ü∂ B) [is_iso f.f] : is_iso f :=
‚ü®‚ü®{ f := inv f.f,
    h' := by { rw [is_iso.eq_inv_comp f.f, ‚Üêf.h_assoc], simp } }, by tidy‚ü©‚ü©

instance forget_reflects_iso : reflects_isomorphisms G.forget :=
{ reflects := Œª A B, coalgebra_iso_of_iso G }

instance forget_faithful : faithful (forget G) := {}

/-- Given a coalgebra morphism whose carrier part is an epimorphism, we get an algebra epimorphism.
-/
lemma algebra_epi_of_epi {X Y : coalgebra G} (f : X ‚ü∂ Y) [h : epi f.f] : epi f :=
(forget G).epi_of_epi_map h

/-- Given a coalgebra morphism whose carrier part is a monomorphism, we get an algebra monomorphism.
-/
lemma algebra_mono_of_mono {X Y : coalgebra G} (f : X ‚ü∂ Y) [h : mono f.f] : mono f :=
(forget G).mono_of_mono_map h

instance : is_left_adjoint G.forget := ‚ü®_, G.adj‚ü©
@[simp] lemma right_adjoint_forget : right_adjoint G.forget = G.cofree := rfl
@[simp] lemma of_left_adjoint_forget : adjunction.of_left_adjoint G.forget = G.adj := rfl

end comonad

end category_theory
