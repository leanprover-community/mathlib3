/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.monad.basic
import category_theory.adjunction.basic

/-!
# Eilenberg-Moore algebras for a monad

This file defines Eilenberg-Moore algebras for a monad, and provides the category instance for them.
Further it defines the adjoint pair of free and forgetful functors, respectively
from and to the original category.

## References
* [Riehl, *Category theory in context*, Section 5.2.4][riehl2017]
-/

namespace category_theory
open category

universes vâ‚ uâ‚ -- declare the `v`'s first; see `category_theory.category` for an explanation

variables {C : Type uâ‚} [ğ’ : category.{vâ‚} C]
include ğ’

namespace monad

/-- An Eilenberg-Moore algebra for a monad `T`.
    cf Definition 5.2.3 in [Riehl][riehl2017]. -/
structure algebra (T : C â¥¤ C) [monad.{vâ‚} T] : Type (max uâ‚ vâ‚) :=
(A : C)
(a : T.obj A âŸ¶ A)
(unit' : (Î·_ T).app A â‰« a = ğŸ™ A . obviously)
(assoc' : ((Î¼_ T).app A â‰« a) = (T.map a â‰« a) . obviously)

restate_axiom algebra.unit'
restate_axiom algebra.assoc'

namespace algebra
variables {T : C â¥¤ C} [monad.{vâ‚} T]

structure hom (A B : algebra T) :=
(f : A.A âŸ¶ B.A)
(h' : T.map f â‰« B.a = A.a â‰« f . obviously)

restate_axiom hom.h'
attribute [simp] hom.h

namespace hom
@[extensionality] lemma ext {A B : algebra T} (f g : hom A B) (w : f.f = g.f) : f = g :=
by { cases f, cases g, congr, assumption }

def id (A : algebra T) : hom A A :=
{ f := ğŸ™ A.A }

@[simp] lemma id_f (A : algebra T) : (id A).f = ğŸ™ A.A := rfl

def comp {P Q R : algebra T} (f : hom P Q) (g : hom Q R) : hom P R :=
{ f := f.f â‰« g.f,
  h' := by rw [functor.map_comp, category.assoc, g.h, â†category.assoc, f.h, category.assoc] }

@[simp] lemma comp_f {P Q R : algebra T} (f : hom P Q) (g : hom Q R) : (comp f g).f = f.f â‰« g.f := rfl
end hom

/-- The category of Eilenberg-Moore algebras for a monad.
    cf Definition 5.2.4 in [Riehl][riehl2017]. -/
instance EilenbergMoore : category (algebra T) :=
{ hom := hom,
  id := hom.id,
  comp := @hom.comp _ _ _ _ }

@[simp] lemma id_f (P : algebra T) : hom.f (ğŸ™ P) = ğŸ™ P.A := rfl
@[simp] lemma comp_f {P Q R : algebra T} (f : P âŸ¶ Q) (g : Q âŸ¶ R) : (f â‰« g).f = f.f â‰« g.f := rfl

end algebra

variables (T : C â¥¤ C) [monad.{vâ‚} T]

def forget : algebra T â¥¤ C :=
{ obj := Î» A, A.A,
  map := Î» A B f, f.f }

@[simp] lemma forget_map {X Y : algebra T} (f : X âŸ¶ Y) : (forget T).map f = f.f := rfl

def free : C â¥¤ algebra T :=
{ obj := Î» X,
  { A := T.obj X,
    a := (Î¼_ T).app X,
    assoc' := (monad.assoc T _).symm },
  map := Î» X Y f,
  { f := T.map f,
    h' := by erw (Î¼_ T).naturality } }

@[simp] lemma free_obj_a (X) : ((free T).obj X).a = (Î¼_ T).app X := rfl
@[simp] lemma free_map_f {X Y : C} (f : X âŸ¶ Y) : ((free T).map f).f = T.map f := rfl

/-- The adjunction between the free and forgetful constructions for Eilenberg-Moore algebras for a monad.
    cf Lemma 5.2.8 of [Riehl][riehl2017]. -/
def adj : free T âŠ£ forget T :=
adjunction.mk_of_hom_equiv
{ hom_equiv := Î» X Y,
  { to_fun := Î» f, (Î·_ T).app X â‰« f.f,
    inv_fun := Î» f,
    { f := T.map f â‰« Y.a,
      h' :=
      begin
        dsimp, simp,
        conv { to_rhs, rw [â†category.assoc, â†(Î¼_ T).naturality, category.assoc], erw algebra.assoc },
        refl,
      end },
    left_inv := Î» f,
    begin
      ext1, dsimp,
      simp only [free_obj_a, functor.map_comp, algebra.hom.h, category.assoc],
      erw [â†category.assoc, monad.right_unit, id_comp],
    end,
    right_inv := Î» f,
    begin
      dsimp,
      erw [â†category.assoc, â†(Î·_ T).naturality, functor.id_map,
            category.assoc, Y.unit, comp_id],
    end }}

end monad

end category_theory
