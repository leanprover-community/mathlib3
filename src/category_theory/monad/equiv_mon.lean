/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import category_theory.monad.basic
import category_theory.monoidal.End
import category_theory.monoidal.Mon_
import category_theory.category.Cat

/-!

# The equivalence between `Monad C` and `Mon_ (C ‚•§ C)`.

A monad "is just" a monoid in the category of endofunctors.

# Definitions/Theorems

1. `to_Mon` associates a monoid object in `C ‚•§ C` to any monad on `C`.
2. `Monad_to_Mon` is the functorial version of `to_Mon`.
3. `of_Mon` associates a monad on `C` to any monoid object in `C ‚•§ C`.
4. `Monad_Mon_equiv` is the equivalence between `Monad C` and `Mon_ (C ‚•§ C)`.

-/

namespace category_theory
open category

universes v u -- morphism levels before object levels. See note [category_theory universes].
variables {C : Type u} [category.{v} C]

namespace Monad
local attribute [instance, reducible] endofunctor_monoidal_category

/-- To every `Monad C` we associated a monoid object in `C ‚•§ C`.-/
@[simps]
def to_Mon : monad C ‚Üí Mon_ (C ‚•§ C) := Œª M,
{ X := (M : C ‚•§ C),
  one := M.Œ∑,
  mul := M.Œº,
  mul_assoc' := by { ext, dsimp, simp [M.assoc] } }

variable (C)
/-- Passing from `Monad C` to `Mon_ (C ‚•§ C)` is functorial. -/
@[simps]
def Monad_to_Mon : monad C ‚•§ Mon_ (C ‚•§ C) :=
{ obj := to_Mon,
  map := Œª _ _ f, { hom := f.to_nat_trans } }
variable {C}

/-- To every monoid object in `C ‚•§ C` we associate a `Monad C`. -/
@[simps]
def of_Mon : Mon_ (C ‚•§ C) ‚Üí monad C := Œª M,
{ to_functor := M.X,
  Œ∑' := M.one,
  Œº' := M.mul,
  left_unit' := Œª X, by { rw [‚ÜêM.one.id_hcomp_app, ‚Üênat_trans.comp_app, M.mul_one], refl },
  right_unit' := Œª X, by { rw [‚ÜêM.one.hcomp_id_app, ‚Üênat_trans.comp_app, M.one_mul], refl },
  assoc' := Œª X, by { rw [‚Üênat_trans.hcomp_id_app, ‚Üênat_trans.comp_app], simp } }

variable (C)
/-- Passing from `Mon_ (C ‚•§ C)` to `Monad C` is functorial. -/
@[simps]
def Mon_to_Monad : Mon_ (C ‚•§ C) ‚•§ monad C :=
{ obj := of_Mon,
  map := Œª _ _ f,
  { app_Œ∑' := begin
      intro X,
      erw [‚Üênat_trans.comp_app, f.one_hom],
      refl,
    end,
    app_Œº' := begin
      intro X,
      erw [‚Üênat_trans.comp_app, f.mul_hom], -- `finish` closes this goal
      simpa only [nat_trans.naturality, nat_trans.hcomp_app, assoc, nat_trans.comp_app, of_Mon_Œº],
    end,
    ..f.hom } }

namespace Monad_Mon_equiv
variable {C}

/-- Isomorphism of functors used in `Monad_Mon_equiv` -/
@[simps {rhs_md := semireducible}]
def counit_iso : Mon_to_Monad C ‚ãô Monad_to_Mon C ‚âÖ ùü≠ _ :=
{ hom := { app := Œª _, { hom := ùüô _ } },
  inv := { app := Œª _, { hom := ùüô _ } } }

/-- Auxiliary definition for `Monad_Mon_equiv` -/
@[simps]
def unit_iso_hom : ùü≠ _ ‚ü∂ Monad_to_Mon C ‚ãô Mon_to_Monad C :=
{ app := Œª _, { app := Œª _, ùüô _ } }

/-- Auxiliary definition for `Monad_Mon_equiv` -/
@[simps]
def unit_iso_inv : Monad_to_Mon C ‚ãô Mon_to_Monad C ‚ü∂ ùü≠ _ :=
{ app := Œª _, { app := Œª _, ùüô _ } }

/-- Isomorphism of functors used in `Monad_Mon_equiv` -/
@[simps]
def unit_iso : ùü≠ _ ‚âÖ Monad_to_Mon C ‚ãô Mon_to_Monad C :=
{ hom := unit_iso_hom,
  inv := unit_iso_inv }

end Monad_Mon_equiv

open Monad_Mon_equiv

/-- Oh, monads are just monoids in the category of endofunctors (equivalence of categories). -/
@[simps]
def Monad_Mon_equiv : (monad C) ‚âå (Mon_ (C ‚•§ C)) :=
{ functor := Monad_to_Mon _,
  inverse := Mon_to_Monad _,
  unit_iso := unit_iso,
  counit_iso := counit_iso }

-- Sanity check
example (A : monad C) {X : C} : ((Monad_Mon_equiv C).unit_iso.app A).hom.app X = ùüô _ := rfl

end Monad
end category_theory
