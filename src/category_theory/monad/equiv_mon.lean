/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import category_theory.monad.bundled
import category_theory.monoidal.End
import category_theory.monoidal.internal
import category_theory.category.Cat

/-!

# The equivalence between `Monad C` and `Mon_ (C ‚•§ C)`.

A monad "is just" a monoid in the category of endofunctors.

# Definitions/Theorems

1. `to_Mon` associates a monoid object in `C ‚•§ C` to any monad on `C`.
2. `Monad_to_Mon` is the functorial version of `to_Mon`.
3. `of_Mon` associates a monad on `C` to any monoid object in `C ‚•§ C`.
4. `Monad_Mon_equiv` is the equivalence between `Monad C` and `Mon_ (C ‚•§ C)`.

-/

namespace category_theory
open category

universes v u -- declare the `v`'s first; see `category_theory.category` for an explanation
variables {C : Type u} [category.{v} C]

namespace Monad
local attribute [instance, reducible] endofunctor_monoidal_category

/-- To every `Monad C` we associated a monoid object in `C ‚•§ C`.-/
@[simps]
def to_Mon : Monad C ‚Üí Mon_ (C ‚•§ C) := Œª M,
{ X := M.func,
  one := Œ∑_ _,
  mul := Œº_ _ }

variable (C)
/-- Passing from `Monad C` to `Mon_ (C ‚•§ C)` is functorial. -/
@[simps]
def Monad_to_Mon : Monad C ‚•§ Mon_ (C ‚•§ C) :=
{ obj := to_Mon,
  map := Œª M N f,
  { hom := f.to_nat_trans } }
variable {C}

/-- To every monoid object in `C ‚•§ C` we associate a `Monad C`. -/
@[simps]
def of_Mon : Mon_ (C ‚•§ C) ‚Üí Monad C := Œª M,
{ func := M.X,
  str :=
  { Œ∑ := M.one,
    Œº := M.mul,
    assoc' := begin
      intro X,
      rw [‚Üênat_trans.hcomp_id_app, ‚Üênat_trans.comp_app],
      simp,
    end,
    left_unit' := begin
      intro X,
      rw [‚Üênat_trans.id_hcomp_app, ‚Üênat_trans.comp_app, M.mul_one],
      refl,
    end,
    right_unit' := begin
      intro X,
      rw [‚Üênat_trans.hcomp_id_app, ‚Üênat_trans.comp_app, M.one_mul],
      refl,
    end } }

variable (C)
/-- Passing from `Mon_ (C ‚•§ C)` to `Monad C` is functorial. -/
@[simps]
def Mon_to_Monad : Mon_ (C ‚•§ C) ‚•§ Monad C :=
{ obj := of_Mon,
  map := Œª M N f,
  { app_Œ∑' := begin
      intro X,
      erw [‚Üênat_trans.comp_app, f.one_hom],
      refl,
    end,
    app_Œº' := begin
      intro X,
      erw [‚Üênat_trans.comp_app, f.mul_hom],
      finish,
    end,
    ..f.hom } }
variable {C}

@[simp]
theorem of_to_mon_end_obj (M : Mon_ (C ‚•§ C)) : (of_Mon M).to_Mon = M :=
by {apply Mon_.hext, repeat {refl}}

@[simp]
theorem to_of_mon_end_obj (M : Monad C) : of_Mon M.to_Mon = M :=
by {apply Monad.hext, repeat {refl}}

/-- Isomorphism of functors used in `Monad_Mon_equiv` -/
@[simps]
def of_to_mon_end_iso : Mon_to_Monad C ‚ãô Monad_to_Mon C ‚âÖ ùü≠ _ :=
{ hom :=
  { app := Œª M,
  { hom := ùüô _ } },
  inv :=
  { app := Œª M,
  { hom := ùüô _ } } }

/-- Isomorphism of functors used in `Monad_Mon_equiv` -/
@[simps]
def to_of_mon_end_iso : Monad_to_Mon C ‚ãô Mon_to_Monad C ‚âÖ ùü≠ _ :=
{ hom :=
  { app := Œª M,
  { app := Œª X, ùüô _ } },
  inv :=
  { app := Œª M,
  { app := Œª X, ùüô _ } } }

variable (C)
/-- Oh, monads are just monoids in the category of endofunctors (equivalence of categories). -/
@[simps]
def Monad_Mon_equiv : (Monad C) ‚âå (Mon_ (C ‚•§ C)) :=
{ functor := Monad_to_Mon _,
  inverse := Mon_to_Monad _,
  unit_iso := to_of_mon_end_iso.symm,
  counit_iso := of_to_mon_end_iso }

-- Sanity check
example (A : Monad C) {X : C} : ((Monad_Mon_equiv C).unit_iso.app A).hom.app X = ùüô _ := rfl

end Monad
end category_theory
