/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Bhavik Mehta
-/
import category_theory.monad.adjunction
import category_theory.adjunction.limits
import category_theory.limits.shapes.terminal

/-!
# Limits and colimits in the category of algebras

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file shows that the forgetful functor `forget T : algebra T â¥¤ C` for a monad `T : C â¥¤ C`
creates limits and creates any colimits which `T` preserves.
This is used to show that `algebra T` has any limits which `C` has, and any colimits which `C` has
and `T` preserves.
This is generalised to the case of a monadic functor `D â¥¤ C`.

## TODO

Dualise for the category of coalgebras and comonadic left adjoints.
-/

namespace category_theory
open category
open category_theory.limits

universes v u vâ‚ vâ‚‚ uâ‚ uâ‚‚
-- morphism levels before object levels. See note [category_theory universes].

namespace monad

variables {C : Type uâ‚} [category.{vâ‚} C]
variables {T : monad C}

variables {J : Type u} [category.{v} J]

namespace forget_creates_limits

variables (D : J â¥¤ algebra T) (c : cone (D â‹™ T.forget)) (t : is_limit c)

/-- (Impl) The natural transformation used to define the new cone -/
@[simps] def Î³ : (D â‹™ T.forget â‹™ â†‘T) âŸ¶ D â‹™ T.forget := { app := Î» j, (D.obj j).a }

/-- (Impl) This new cone is used to construct the algebra structure -/
@[simps Ï€_app] def new_cone : cone (D â‹™ forget T) :=
{ X := T.obj c.X,
  Ï€ := (functor.const_comp _ _ â†‘T).inv â‰« whisker_right c.Ï€ T â‰« Î³ D }

/-- The algebra structure which will be the apex of the new limit cone for `D`. -/
@[simps] def cone_point : algebra T :=
{ A := c.X,
  a := t.lift (new_cone D c),
  unit' := t.hom_ext $ Î» j,
  begin
    rw [category.assoc, t.fac, new_cone_Ï€_app, â†T.Î·.naturality_assoc, functor.id_map,
      (D.obj j).unit],
    dsimp, simp -- See library note [dsimp, simp]
  end,
  assoc' := t.hom_ext $ Î» j,
  begin
    rw [category.assoc, category.assoc, t.fac (new_cone D c), new_cone_Ï€_app,
      â†functor.map_comp_assoc, t.fac (new_cone D c), new_cone_Ï€_app, â†T.Î¼.naturality_assoc,
      (D.obj j).assoc, functor.map_comp, category.assoc],
    refl,
  end }

/-- (Impl) Construct the lifted cone in `algebra T` which will be limiting. -/
@[simps] def lifted_cone : cone D :=
{ X := cone_point D c t,
  Ï€ := { app := Î» j, { f := c.Ï€.app j },
         naturality' := Î» X Y f, by { ext1, dsimp, erw c.w f, simp } } }

/-- (Impl) Prove that the lifted cone is limiting. -/
@[simps]
def lifted_cone_is_limit : is_limit (lifted_cone D c t) :=
{ lift := Î» s,
  { f := t.lift ((forget T).map_cone s),
    h' := t.hom_ext $ Î» j,
    begin
      dsimp,
      rw [category.assoc, category.assoc, t.fac, new_cone_Ï€_app, â†functor.map_comp_assoc, t.fac,
        functor.map_cone_Ï€_app],
      apply (s.Ï€.app j).h,
    end },
  uniq' := Î» s m J,
  begin
    ext1,
    apply t.hom_ext,
    intro j,
    simpa [t.fac ((forget T).map_cone s) j] using congr_arg algebra.hom.f (J j),
  end }

end forget_creates_limits

-- Theorem 5.6.5 from [Riehl][riehl2017]
/-- The forgetful functor from the Eilenberg-Moore category creates limits. -/
noncomputable
instance forget_creates_limits : creates_limits_of_size (forget T) :=
{ creates_limits_of_shape := Î» J ğ’¥, by exactI
  { creates_limit := Î» D,
    creates_limit_of_reflects_iso (Î» c t,
    { lifted_cone := forget_creates_limits.lifted_cone D c t,
      valid_lift := cones.ext (iso.refl _) (Î» j, (id_comp _).symm),
      makes_limit := forget_creates_limits.lifted_cone_is_limit _ _ _ } ) } }

/-- `D â‹™ forget T` has a limit, then `D` has a limit. -/
lemma has_limit_of_comp_forget_has_limit (D : J â¥¤ algebra T) [has_limit (D â‹™ forget T)] :
  has_limit D :=
has_limit_of_created D (forget T)

namespace forget_creates_colimits

-- Let's hide the implementation details in a namespace
variables {D : J â¥¤ algebra T} (c : cocone (D â‹™ forget T)) (t : is_colimit c)

-- We have a diagram D of shape J in the category of algebras, and we assume that we are given a
-- colimit for its image D â‹™ forget T under the forgetful functor, say its apex is L.

-- We'll construct a colimiting coalgebra for D, whose carrier will also be L.
-- To do this, we must find a map TL âŸ¶ L. Since T preserves colimits, TL is also a colimit.
-- In particular, it is a colimit for the diagram `(D â‹™ forget T) â‹™ T`
-- so to construct a map TL âŸ¶ L it suffices to show that L is the apex of a cocone for this diagram.
-- In other words, we need a natural transformation from const L to `(D â‹™ forget T) â‹™ T`.
-- But we already know that L is the apex of a cocone for the diagram `D â‹™ forget T`, so it
-- suffices to give a natural transformation `((D â‹™ forget T) â‹™ T) âŸ¶ (D â‹™ forget T)`:

/--
(Impl)
The natural transformation given by the algebra structure maps, used to construct a cocone `c` with
apex `colimit (D â‹™ forget T)`.
 -/
@[simps] def Î³ : ((D â‹™ forget T) â‹™ â†‘T) âŸ¶ (D â‹™ forget T) := { app := Î» j, (D.obj j).a }

/--
(Impl)
A cocone for the diagram `(D â‹™ forget T) â‹™ T` found by composing the natural transformation `Î³`
with the colimiting cocone for `D â‹™ forget T`.
-/
@[simps]
def new_cocone : cocone ((D â‹™ forget T) â‹™ â†‘T) :=
{ X := c.X,
  Î¹ := Î³ â‰« c.Î¹ }

variables [preserves_colimit (D â‹™ forget T) (T : C â¥¤ C)]

/--
(Impl)
Define the map `Î» : TL âŸ¶ L`, which will serve as the structure of the coalgebra on `L`, and
we will show is the colimiting object. We use the cocone constructed by `c` and the fact that
`T` preserves colimits to produce this morphism.
-/
@[reducible]
def lambda : ((T : C â¥¤ C).map_cocone c).X âŸ¶ c.X :=
(is_colimit_of_preserves _ t).desc (new_cocone c)

/-- (Impl) The key property defining the map `Î» : TL âŸ¶ L`. -/
lemma commuting (j : J) :
(T : C â¥¤ C).map (c.Î¹.app j) â‰« lambda c t = (D.obj j).a â‰« c.Î¹.app j :=
(is_colimit_of_preserves _ t).fac (new_cocone c) j

variables [preserves_colimit ((D â‹™ forget T) â‹™ â†‘T) (T : C â¥¤ C)]

/--
(Impl)
Construct the colimiting algebra from the map `Î» : TL âŸ¶ L` given by `lambda`. We are required to
show it satisfies the two algebra laws, which follow from the algebra laws for the image of `D` and
our `commuting` lemma.
-/
@[simps] def cocone_point :
algebra T :=
{ A := c.X,
  a := lambda c t,
  unit' :=
  begin
    apply t.hom_ext,
    intro j,
    rw [(show c.Î¹.app j â‰« T.Î·.app c.X â‰« _ = T.Î·.app (D.obj j).A â‰« _ â‰« _,
                  from T.Î·.naturality_assoc _ _), commuting, algebra.unit_assoc (D.obj j)],
    dsimp, simp -- See library note [dsimp, simp]
  end,
  assoc' :=
  begin
    refine (is_colimit_of_preserves _ (is_colimit_of_preserves _ t)).hom_ext (Î» j, _),
    rw [functor.map_cocone_Î¹_app, functor.map_cocone_Î¹_app,
      (show (T : C â¥¤ C).map ((T : C â¥¤ C).map _) â‰« _ â‰« _ = _, from T.Î¼.naturality_assoc _ _),
      â†functor.map_comp_assoc, commuting, functor.map_comp, category.assoc, commuting],
    apply (D.obj j).assoc_assoc _,
  end }

/-- (Impl) Construct the lifted cocone in `algebra T` which will be colimiting. -/
@[simps] def lifted_cocone : cocone D :=
{ X := cocone_point c t,
  Î¹ := { app := Î» j, { f := c.Î¹.app j, h' := commuting _ _ _ },
         naturality' := Î» A B f, by { ext1, dsimp, rw [comp_id], apply c.w } } }

/-- (Impl) Prove that the lifted cocone is colimiting. -/
@[simps]
def lifted_cocone_is_colimit : is_colimit (lifted_cocone c t) :=
{ desc := Î» s,
  { f := t.desc ((forget T).map_cocone s),
    h' := (is_colimit_of_preserves (T : C â¥¤ C) t).hom_ext $ Î» j,
    begin
      dsimp,
      rw [â†functor.map_comp_assoc, â†category.assoc, t.fac, commuting, category.assoc, t.fac],
      apply algebra.hom.h,
    end },
  uniq' := Î» s m J,
  by { ext1, apply t.hom_ext, intro j, simpa using congr_arg algebra.hom.f (J j) } }

end forget_creates_colimits

open forget_creates_colimits

-- TODO: the converse of this is true as well
/--
The forgetful functor from the Eilenberg-Moore category for a monad creates any colimit
which the monad itself preserves.
-/
noncomputable
instance forget_creates_colimit (D : J â¥¤ algebra T)
  [preserves_colimit (D â‹™ forget T) (T : C â¥¤ C)]
  [preserves_colimit ((D â‹™ forget T) â‹™ â†‘T) (T : C â¥¤ C)] :
  creates_colimit D (forget T) :=
creates_colimit_of_reflects_iso $ Î» c t,
{ lifted_cocone :=
  { X := cocone_point c t,
    Î¹ :=
    { app := Î» j, { f := c.Î¹.app j, h' := commuting _ _ _ },
      naturality' := Î» A B f, by { ext1, dsimp, erw [comp_id, c.w] } } },
  valid_lift := cocones.ext (iso.refl _) (by tidy),
  makes_colimit := lifted_cocone_is_colimit _ _ }

noncomputable
instance forget_creates_colimits_of_shape
  [preserves_colimits_of_shape J (T : C â¥¤ C)] :
  creates_colimits_of_shape J (forget T) :=
{ creates_colimit := Î» K, by apply_instance }

noncomputable
instance forget_creates_colimits
  [preserves_colimits_of_size.{v u} (T : C â¥¤ C)] :
  creates_colimits_of_size.{v u} (forget T) :=
{ creates_colimits_of_shape := Î» J ğ’¥â‚, by apply_instance }

/--
For `D : J â¥¤ algebra T`, `D â‹™ forget T` has a colimit, then `D` has a colimit provided colimits
of shape `J` are preserved by `T`.
-/
lemma forget_creates_colimits_of_monad_preserves
  [preserves_colimits_of_shape J (T : C â¥¤ C)] (D : J â¥¤ algebra T) [has_colimit (D â‹™ forget T)] :
has_colimit D :=
has_colimit_of_created D (forget T)

end monad

variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚‚} D]
variables {J : Type u} [category.{v} J]

instance comp_comparison_forget_has_limit
  (F : J â¥¤ D) (R : D â¥¤ C) [monadic_right_adjoint R] [has_limit (F â‹™ R)] :
  has_limit ((F â‹™ monad.comparison (adjunction.of_right_adjoint R)) â‹™ monad.forget _) :=
@has_limit_of_iso _ _ _ _ (F â‹™ R) _ _
  (iso_whisker_left F (monad.comparison_forget (adjunction.of_right_adjoint R)).symm)

instance comp_comparison_has_limit
  (F : J â¥¤ D) (R : D â¥¤ C) [monadic_right_adjoint R] [has_limit (F â‹™ R)] :
  has_limit (F â‹™ monad.comparison (adjunction.of_right_adjoint R)) :=
monad.has_limit_of_comp_forget_has_limit (F â‹™ monad.comparison (adjunction.of_right_adjoint R))

/-- Any monadic functor creates limits. -/
noncomputable
def monadic_creates_limits (R : D â¥¤ C) [monadic_right_adjoint R] :
  creates_limits_of_size.{v u} R :=
creates_limits_of_nat_iso (monad.comparison_forget (adjunction.of_right_adjoint R))

/--
The forgetful functor from the Eilenberg-Moore category for a monad creates any colimit
which the monad itself preserves.
-/
noncomputable
def monadic_creates_colimit_of_preserves_colimit (R : D â¥¤ C) (K : J â¥¤ D)
  [monadic_right_adjoint R]
  [preserves_colimit (K â‹™ R) (left_adjoint R â‹™ R)]
  [preserves_colimit ((K â‹™ R) â‹™ left_adjoint R â‹™ R) (left_adjoint R â‹™ R)] :
  creates_colimit K R :=
begin
  apply creates_colimit_of_nat_iso (monad.comparison_forget (adjunction.of_right_adjoint R)),
  apply category_theory.comp_creates_colimit _ _,
  apply_instance,
  let i : ((K â‹™ monad.comparison (adjunction.of_right_adjoint R)) â‹™ monad.forget _) â‰… K â‹™ R :=
    functor.associator _ _ _ â‰ªâ‰«
      iso_whisker_left K (monad.comparison_forget (adjunction.of_right_adjoint R)),
  apply category_theory.monad.forget_creates_colimit _,
  { dsimp,
    refine preserves_colimit_of_iso_diagram _ i.symm },
  { dsimp,
    refine preserves_colimit_of_iso_diagram _ (iso_whisker_right i (left_adjoint R â‹™ R)).symm },
end

/-- A monadic functor creates any colimits of shapes it preserves. -/
noncomputable
def monadic_creates_colimits_of_shape_of_preserves_colimits_of_shape (R : D â¥¤ C)
  [monadic_right_adjoint R] [preserves_colimits_of_shape J R] : creates_colimits_of_shape J R :=
begin
  have : preserves_colimits_of_shape J (left_adjoint R â‹™ R),
  { apply category_theory.limits.comp_preserves_colimits_of_shape _ _,
    apply (adjunction.left_adjoint_preserves_colimits (adjunction.of_right_adjoint R)).1,
    apply_instance },
  exactI âŸ¨Î» K, monadic_creates_colimit_of_preserves_colimit _ _âŸ©,
end

/-- A monadic functor creates colimits if it preserves colimits. -/
noncomputable
def monadic_creates_colimits_of_preserves_colimits (R : D â¥¤ C) [monadic_right_adjoint R]
  [preserves_colimits_of_size.{v u} R] : creates_colimits_of_size.{v u} R :=
{ creates_colimits_of_shape := Î» J ğ’¥â‚,
    by exactI monadic_creates_colimits_of_shape_of_preserves_colimits_of_shape _ }

section

lemma has_limit_of_reflective (F : J â¥¤ D) (R : D â¥¤ C) [has_limit (F â‹™ R)] [reflective R] :
  has_limit F :=
by { haveI := monadic_creates_limits.{v u} R, exact has_limit_of_created F R }

/-- If `C` has limits of shape `J` then any reflective subcategory has limits of shape `J`. -/
lemma has_limits_of_shape_of_reflective [has_limits_of_shape J C] (R : D â¥¤ C) [reflective R] :
  has_limits_of_shape J D :=
{ has_limit := Î» F, has_limit_of_reflective F R }

/-- If `C` has limits then any reflective subcategory has limits. -/
lemma has_limits_of_reflective (R : D â¥¤ C) [has_limits_of_size.{v u} C] [reflective R] :
  has_limits_of_size.{v u} D :=
{ has_limits_of_shape := Î» J ğ’¥â‚, by exactI has_limits_of_shape_of_reflective R }

/-- If `C` has colimits of shape `J` then any reflective subcategory has colimits of shape `J`. -/
lemma has_colimits_of_shape_of_reflective (R : D â¥¤ C)
  [reflective R] [has_colimits_of_shape J C] : has_colimits_of_shape J D :=
{ has_colimit := Î» F,
begin
  let c := (left_adjoint R).map_cocone (colimit.cocone (F â‹™ R)),
  letI : preserves_colimits_of_shape J _ :=
    (adjunction.of_right_adjoint R).left_adjoint_preserves_colimits.1,
  let t : is_colimit c := is_colimit_of_preserves (left_adjoint R) (colimit.is_colimit _),
  apply has_colimit.mk âŸ¨_, (is_colimit.precompose_inv_equiv _ _).symm tâŸ©,
  apply (iso_whisker_left F (as_iso (adjunction.of_right_adjoint R).counit) : _) â‰ªâ‰« F.right_unitor,
end }

/-- If `C` has colimits then any reflective subcategory has colimits. -/
lemma has_colimits_of_reflective (R : D â¥¤ C) [reflective R] [has_colimits_of_size.{v u} C] :
  has_colimits_of_size.{v u} D :=
{ has_colimits_of_shape := Î» J ğ’¥, by exactI has_colimits_of_shape_of_reflective R }



/--
The reflector always preserves terminal objects. Note this in general doesn't apply to any other
limit.
-/
noncomputable def left_adjoint_preserves_terminal_of_reflective (R : D â¥¤ C) [reflective R] :
  preserves_limits_of_shape (discrete.{v} pempty) (left_adjoint R) :=
{ preserves_limit := Î» K, let F := functor.empty.{v} D in
  begin
    apply preserves_limit_of_iso_diagram _ (functor.empty_ext (F â‹™ R) _),
    fsplit, intros c h, haveI : has_limit (F â‹™ R) := âŸ¨âŸ¨âŸ¨c,hâŸ©âŸ©âŸ©,
    haveI : has_limit F := has_limit_of_reflective F R,
    apply is_limit_change_empty_cone D (limit.is_limit F),
    apply (as_iso ((adjunction.of_right_adjoint R).counit.app _)).symm.trans,
    { apply (left_adjoint R).map_iso, letI := monadic_creates_limits.{v v} R,
      let := (category_theory.preserves_limit_of_creates_limit_and_has_limit F R).preserves,
      apply (this (limit.is_limit F)).cone_point_unique_up_to_iso h },
    apply_instance,
  end }

end
end category_theory
