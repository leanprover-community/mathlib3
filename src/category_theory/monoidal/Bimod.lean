/-
Copyright (c) 2022 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Oleksandr Manzyuk
-/
import category_theory.bicategory.basic
import category_theory.monoidal.Mon_
import category_theory.limits.preserves.shapes.equalizers

/-!
# The category of bimodule objects over a pair of monoid objects.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.
-/

universes vâ‚ vâ‚‚ uâ‚ uâ‚‚

open category_theory
open category_theory.monoidal_category

variables {C : Type uâ‚} [category.{vâ‚} C] [monoidal_category.{vâ‚} C]

section

open category_theory.limits

variables [has_coequalizers C]

section

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]

lemma id_tensor_Ï€_preserves_coequalizer_inv_desc
  {W X Y Z : C} (f g : X âŸ¶ Y)
  (h : Z âŠ— Y âŸ¶ W) (wh : (ğŸ™ Z âŠ— f) â‰« h = (ğŸ™ Z âŠ— g) â‰« h) :
  (ğŸ™ Z âŠ— coequalizer.Ï€ f g) â‰« (preserves_coequalizer.iso (tensor_left Z) f g).inv â‰«
    coequalizer.desc h wh = h :=
map_Ï€_preserves_coequalizer_inv_desc (tensor_left Z) f g h wh

lemma id_tensor_Ï€_preserves_coequalizer_inv_colim_map_desc
  {X Y Z X' Y' Z' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : Z âŠ— X âŸ¶ X') (q : Z âŠ— Y âŸ¶ Y')
  (wf : (ğŸ™ Z âŠ— f) â‰« q = p â‰« f') (wg : (ğŸ™ Z âŠ— g) â‰« q = p â‰« g')
  (h : Y' âŸ¶ Z') (wh : f' â‰« h = g' â‰« h) :
  (ğŸ™ Z âŠ— coequalizer.Ï€ f g) â‰« (preserves_coequalizer.iso (tensor_left Z) f g).inv â‰«
  colim_map (parallel_pair_hom (ğŸ™ Z âŠ— f) (ğŸ™ Z âŠ— g) f' g' p q wf wg) â‰«
  coequalizer.desc h wh =
  q â‰« h :=
map_Ï€_preserves_coequalizer_inv_colim_map_desc (tensor_left Z) f g f' g' p q wf wg h wh

end

section

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

lemma Ï€_tensor_id_preserves_coequalizer_inv_desc
  {W X Y Z : C} (f g : X âŸ¶ Y)
  (h : Y âŠ— Z âŸ¶ W) (wh : (f âŠ— ğŸ™ Z) â‰« h = (g âŠ— ğŸ™ Z) â‰« h) :
  (coequalizer.Ï€ f g âŠ— ğŸ™ Z) â‰« (preserves_coequalizer.iso (tensor_right Z) f g).inv â‰«
    coequalizer.desc h wh = h :=
map_Ï€_preserves_coequalizer_inv_desc (tensor_right Z) f g h wh

lemma Ï€_tensor_id_preserves_coequalizer_inv_colim_map_desc
  {X Y Z X' Y' Z' : C} (f g : X âŸ¶ Y) (f' g' : X' âŸ¶ Y') (p : X âŠ— Z âŸ¶ X') (q : Y âŠ— Z âŸ¶ Y')
  (wf : (f âŠ— ğŸ™ Z) â‰« q = p â‰« f') (wg : (g âŠ— ğŸ™ Z) â‰« q = p â‰« g')
  (h : Y' âŸ¶ Z') (wh : f' â‰« h = g' â‰« h) :
  (coequalizer.Ï€ f g âŠ— ğŸ™ Z) â‰« (preserves_coequalizer.iso (tensor_right Z) f g).inv â‰«
  colim_map (parallel_pair_hom (f âŠ— ğŸ™ Z) (g âŠ— ğŸ™ Z) f' g' p q wf wg) â‰«
  coequalizer.desc h wh =
  q â‰« h :=
map_Ï€_preserves_coequalizer_inv_colim_map_desc (tensor_right Z) f g f' g' p q wf wg h wh

end

end

/-- A bimodule object for a pair of monoid objects, all internal to some monoidal category. -/
structure Bimod (A B : Mon_ C) :=
(X : C)
(act_left : A.X âŠ— X âŸ¶ X)
(one_act_left' : (A.one âŠ— ğŸ™ X) â‰« act_left = (Î»_ X).hom . obviously)
(left_assoc' :
  (A.mul âŠ— ğŸ™ X) â‰« act_left = (Î±_ A.X A.X X).hom â‰« (ğŸ™ A.X âŠ— act_left) â‰« act_left . obviously)
(act_right : X âŠ— B.X âŸ¶ X)
(act_right_one' : (ğŸ™ X âŠ— B.one) â‰« act_right = (Ï_ X).hom . obviously)
(right_assoc' :
  (ğŸ™ X âŠ— B.mul) â‰« act_right = (Î±_ X B.X B.X).inv â‰« (act_right âŠ— ğŸ™ B.X) â‰« act_right . obviously)
(middle_assoc' :
  (act_left âŠ— ğŸ™ B.X) â‰« act_right = (Î±_ A.X X B.X).hom â‰« (ğŸ™ A.X âŠ— act_right) â‰« act_left . obviously)

restate_axiom Bimod.one_act_left'
restate_axiom Bimod.act_right_one'
restate_axiom Bimod.left_assoc'
restate_axiom Bimod.right_assoc'
restate_axiom Bimod.middle_assoc'
attribute [simp, reassoc]
Bimod.one_act_left Bimod.act_right_one Bimod.left_assoc Bimod.right_assoc Bimod.middle_assoc

namespace Bimod

variables {A B : Mon_ C} (M : Bimod A B)

/-- A morphism of bimodule objects. -/
@[ext]
structure hom (M N : Bimod A B) :=
(hom : M.X âŸ¶ N.X)
(left_act_hom' : M.act_left â‰« hom = (ğŸ™ A.X âŠ— hom) â‰« N.act_left . obviously)
(right_act_hom' : M.act_right â‰« hom = (hom âŠ— ğŸ™ B.X) â‰« N.act_right . obviously)

restate_axiom hom.left_act_hom'
restate_axiom hom.right_act_hom'
attribute [simp, reassoc] hom.left_act_hom hom.right_act_hom

/-- The identity morphism on a bimodule object. -/
@[simps]
def id' (M : Bimod A B) : hom M M :=
{ hom := ğŸ™ M.X, }

instance hom_inhabited (M : Bimod A B) : inhabited (hom M M) := âŸ¨id' MâŸ©

/-- Composition of bimodule object morphisms. -/
@[simps]
def comp {M N O : Bimod A B} (f : hom M N) (g : hom N O) : hom M O :=
{ hom := f.hom â‰« g.hom, }

instance : category (Bimod A B) :=
{ hom := Î» M N, hom M N,
  id := id',
  comp := Î» M N O f g, comp f g, }

@[simp] lemma id_hom' (M : Bimod A B) : (ğŸ™ M : hom M M).hom = ğŸ™ M.X := rfl
@[simp] lemma comp_hom' {M N K : Bimod A B} (f : M âŸ¶ N) (g : N âŸ¶ K) :
  (f â‰« g : hom M K).hom = f.hom â‰« g.hom := rfl

/--
Construct an isomorphism of bimodules by giving an isomorphism between the underlying objects
and checking compatibility with left and right actions only in the forward direction.
-/
@[simps]
def iso_of_iso {X Y : Mon_ C} {P Q : Bimod X Y}
  (f : P.X â‰… Q.X)
  (f_left_act_hom : P.act_left â‰« f.hom = (ğŸ™ X.X âŠ— f.hom) â‰« Q.act_left)
  (f_right_act_hom : P.act_right â‰« f.hom = (f.hom âŠ— ğŸ™ Y.X) â‰« Q.act_right) :
  P â‰… Q :=
{ hom := âŸ¨f.homâŸ©,
  inv :=
  { hom := f.inv,
    left_act_hom' := begin
      rw [â†(cancel_mono f.hom), category.assoc, category.assoc, iso.inv_hom_id, category.comp_id,
          f_left_act_hom, â†category.assoc, â†id_tensor_comp, iso.inv_hom_id,
          monoidal_category.tensor_id, category.id_comp],
    end,
    right_act_hom' := begin
      rw [â†(cancel_mono f.hom), category.assoc, category.assoc, iso.inv_hom_id, category.comp_id,
          f_right_act_hom, â†category.assoc, â†comp_tensor_id, iso.inv_hom_id,
          monoidal_category.tensor_id, category.id_comp],
    end },
  hom_inv_id' := begin
    ext, dsimp, rw iso.hom_inv_id,
  end,
  inv_hom_id' := begin
    ext, dsimp, rw iso.inv_hom_id,
  end }

variables (A)

/-- A monoid object as a bimodule over itself. -/
@[simps]
def regular : Bimod A A :=
{ X := A.X,
  act_left := A.mul,
  act_right := A.mul, }

instance : inhabited (Bimod A A) := âŸ¨regular AâŸ©

/-- The forgetful functor from bimodule objects to the ambient category. -/
def forget : Bimod A B â¥¤ C :=
{ obj := Î» A, A.X,
  map := Î» A B f, f.hom, }

open category_theory.limits

variables [has_coequalizers C]

namespace tensor_Bimod
variables {R S T : Mon_ C} (P : Bimod R S) (Q : Bimod S T)

/-- The underlying object of the tensor product of two bimodules. -/
noncomputable
def X : C := coequalizer (P.act_right âŠ— ğŸ™ Q.X) ((Î±_ _ _ _).hom â‰« (ğŸ™ P.X âŠ— Q.act_left))

section

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]

/-- Left action for the tensor product of two bimodules. -/
noncomputable
def act_left : R.X âŠ— X P Q âŸ¶ X P Q :=
(preserves_coequalizer.iso (tensor_left R.X) _ _).inv â‰«
colim_map
  (parallel_pair_hom _ _ _ _
    ((ğŸ™ _ âŠ— (Î±_ _ _ _).hom) â‰« (Î±_ _ _ _).inv â‰« (P.act_left âŠ— ğŸ™ S.X âŠ— ğŸ™ Q.X) â‰« (Î±_ _ _ _).inv)
    ((Î±_ _ _ _).inv â‰« (P.act_left âŠ— ğŸ™ Q.X))
    begin
      dsimp,
      slice_lhs 1 2 { rw associator_inv_naturality },
      slice_rhs 3 4 { rw associator_inv_naturality },
      slice_rhs 4 5 { rw [â†tensor_comp, middle_assoc, tensor_comp, comp_tensor_id] },
      coherence,
    end
    begin
      dsimp,
      slice_lhs 1 1 { rw id_tensor_comp },
      slice_lhs 2 3 { rw associator_inv_naturality },
      slice_lhs 3 4 { rw [tensor_id, id_tensor_comp_tensor_id] },
      slice_rhs 4 6 { rw iso.inv_hom_id_assoc },
      slice_rhs 3 4 { rw [tensor_id, tensor_id_comp_id_tensor] },
    end)

lemma id_tensor_Ï€_act_left :
  (ğŸ™ R.X âŠ— coequalizer.Ï€ _ _) â‰« act_left P Q =
  (Î±_ _ _ _).inv â‰« (P.act_left âŠ— ğŸ™ Q.X) â‰« coequalizer.Ï€ _ _ :=
begin
  erw map_Ï€_preserves_coequalizer_inv_colim_map (tensor_left _),
  simp only [category.assoc],
end

lemma one_act_left' : (R.one âŠ— ğŸ™ _) â‰« act_left P Q = (Î»_ _).hom :=
begin
  refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp [X],
  slice_lhs 1 2 { rw [id_tensor_comp_tensor_id, â†tensor_id_comp_id_tensor] },
  slice_lhs 2 3 { rw id_tensor_Ï€_act_left },
  slice_lhs 1 2 { rw [â†monoidal_category.tensor_id, associator_inv_naturality] },
  slice_lhs 2 3 { rw [â†comp_tensor_id, one_act_left] },
  slice_rhs 1 2 { rw left_unitor_naturality },
  coherence,
end

lemma left_assoc' :
  (R.mul âŠ— ğŸ™ _) â‰« act_left P Q =
  (Î±_ R.X R.X _).hom â‰« (ğŸ™ R.X âŠ— act_left P Q) â‰« act_left P Q :=
begin
  refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp [X],
  slice_lhs 1 2 { rw [id_tensor_comp_tensor_id, â†tensor_id_comp_id_tensor] },
  slice_lhs 2 3 { rw id_tensor_Ï€_act_left },
  slice_lhs 1 2 { rw [â†monoidal_category.tensor_id, associator_inv_naturality] },
  slice_lhs 2 3 { rw [â†comp_tensor_id, left_assoc, comp_tensor_id, comp_tensor_id] },
  slice_rhs 1 2 { rw [â†monoidal_category.tensor_id, associator_naturality] },
  slice_rhs 2 3 { rw [â†id_tensor_comp, id_tensor_Ï€_act_left, id_tensor_comp, id_tensor_comp] },
  slice_rhs 4 5 { rw id_tensor_Ï€_act_left },
  slice_rhs 3 4 { rw associator_inv_naturality },
  coherence,
end

end

section

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

/-- Right action for the tensor product of two bimodules. -/
noncomputable
def act_right : X P Q âŠ— T.X âŸ¶ X P Q :=
(preserves_coequalizer.iso (tensor_right T.X) _ _).inv â‰«
colim_map
  (parallel_pair_hom _ _ _ _
    ((Î±_ _ _ _).hom â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ P.X âŠ— ğŸ™ S.X âŠ— Q.act_right) â‰« (Î±_ _ _ _).inv)
    ((Î±_ _ _ _).hom â‰« (ğŸ™ P.X âŠ— Q.act_right))
    begin
      dsimp,
      slice_lhs 1 2 { rw associator_naturality },
      slice_lhs 2 3 { rw [tensor_id, tensor_id_comp_id_tensor] },
      slice_rhs 3 4 { rw associator_inv_naturality },
      slice_rhs 2 4 { rw iso.hom_inv_id_assoc },
      slice_rhs 2 3 { rw [tensor_id, id_tensor_comp_tensor_id] },
    end
    begin
      dsimp,
      slice_lhs 1 1 { rw comp_tensor_id },
      slice_lhs 2 3 { rw associator_naturality },
      slice_lhs 3 4 { rw [â†id_tensor_comp, middle_assoc, id_tensor_comp] },
      slice_rhs 4 6 { rw iso.inv_hom_id_assoc },
      slice_rhs 3 4 { rw â†id_tensor_comp },
      coherence,
    end)

lemma Ï€_tensor_id_act_right :
  (coequalizer.Ï€ _ _ âŠ— ğŸ™ T.X) â‰« act_right P Q =
  (Î±_ _ _ _).hom â‰« (ğŸ™ P.X âŠ— Q.act_right) â‰« coequalizer.Ï€ _ _ :=
begin
  erw map_Ï€_preserves_coequalizer_inv_colim_map (tensor_right _),
  simp only [category.assoc],
end

lemma act_right_one' : (ğŸ™ _ âŠ— T.one) â‰« act_right P Q = (Ï_ _).hom :=
begin
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp [X],
  slice_lhs 1 2 { rw [tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
  slice_lhs 2 3 { rw Ï€_tensor_id_act_right },
  slice_lhs 1 2 { rw [â†monoidal_category.tensor_id, associator_naturality] },
  slice_lhs 2 3 { rw [â†id_tensor_comp, act_right_one] },
  slice_rhs 1 2 { rw right_unitor_naturality },
  coherence,
end

lemma right_assoc' :
  (ğŸ™ _ âŠ— T.mul) â‰« act_right P Q =
  (Î±_ _ T.X T.X).inv â‰« (act_right P Q âŠ— ğŸ™ T.X) â‰« act_right P Q :=
begin
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp [X],
  slice_lhs 1 2 { rw [tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
  slice_lhs 2 3 { rw Ï€_tensor_id_act_right },
  slice_lhs 1 2 { rw [â†monoidal_category.tensor_id, associator_naturality] },
  slice_lhs 2 3 { rw [â†id_tensor_comp, right_assoc, id_tensor_comp, id_tensor_comp] },
  slice_rhs 1 2 { rw [â†monoidal_category.tensor_id, associator_inv_naturality] },
  slice_rhs 2 3 { rw [â†comp_tensor_id, Ï€_tensor_id_act_right, comp_tensor_id, comp_tensor_id] },
  slice_rhs 4 5 { rw Ï€_tensor_id_act_right },
  slice_rhs 3 4 { rw associator_naturality },
  coherence,
end

end

section

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]
variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

lemma middle_assoc' :
  (act_left P Q âŠ— ğŸ™ T.X) â‰« act_right P Q =
  (Î±_ R.X _ T.X).hom â‰« (ğŸ™ R.X âŠ— act_right P Q) â‰« act_left P Q :=
begin
  refine (cancel_epi ((tensor_left _ â‹™ tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp [X],
  slice_lhs 1 2 { rw [â†comp_tensor_id, id_tensor_Ï€_act_left, comp_tensor_id, comp_tensor_id] },
  slice_lhs 3 4 { rw Ï€_tensor_id_act_right },
  slice_lhs 2 3 { rw associator_naturality },
  slice_lhs 3 4 { rw [monoidal_category.tensor_id, tensor_id_comp_id_tensor] },
  slice_rhs 1 2 { rw associator_naturality },
  slice_rhs 2 3 { rw [â†id_tensor_comp, Ï€_tensor_id_act_right, id_tensor_comp, id_tensor_comp] },
  slice_rhs 4 5 { rw id_tensor_Ï€_act_left },
  slice_rhs 3 4 { rw associator_inv_naturality },
  slice_rhs 4 5 { rw [monoidal_category.tensor_id, id_tensor_comp_tensor_id] },
  coherence,
end

end

end tensor_Bimod

section

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]
variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

/-- Tensor product of two bimodule objects as a bimodule object. -/
@[simps]
noncomputable
def tensor_Bimod {X Y Z : Mon_ C} (M : Bimod X Y) (N : Bimod Y Z) : Bimod X Z :=
{ X := tensor_Bimod.X M N,
  act_left := tensor_Bimod.act_left M N,
  act_right := tensor_Bimod.act_right M N,
  one_act_left' := tensor_Bimod.one_act_left' M N,
  act_right_one' := tensor_Bimod.act_right_one' M N,
  left_assoc' := tensor_Bimod.left_assoc' M N,
  right_assoc' := tensor_Bimod.right_assoc' M N,
  middle_assoc' := tensor_Bimod.middle_assoc' M N, }

/-- Tensor product of two morphisms of bimodule objects. -/
@[simps]
noncomputable
def tensor_hom {X Y Z : Mon_ C} {Mâ‚ Mâ‚‚ : Bimod X Y} {Nâ‚ Nâ‚‚ : Bimod Y Z}
  (f : Mâ‚ âŸ¶ Mâ‚‚) (g : Nâ‚ âŸ¶ Nâ‚‚) : Mâ‚.tensor_Bimod Nâ‚ âŸ¶ Mâ‚‚.tensor_Bimod Nâ‚‚ :=
{ hom :=
    colim_map
      (parallel_pair_hom _ _ _ _ ((f.hom âŠ— ğŸ™ Y.X) âŠ— g.hom) (f.hom âŠ— g.hom)
        (by rw [â†tensor_comp, â†tensor_comp, hom.right_act_hom, category.id_comp, category.comp_id])
        begin
          slice_lhs 2 3 { rw [â†tensor_comp, hom.left_act_hom, category.id_comp] },
          slice_rhs 1 2 { rw associator_naturality },
          slice_rhs 2 3 { rw [â†tensor_comp, category.comp_id] },
        end),
  left_act_hom' := begin
    refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
    dsimp,
    slice_lhs 1 2 { rw tensor_Bimod.id_tensor_Ï€_act_left },
    slice_lhs 3 4 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
    slice_lhs 2 3 { rw [â†tensor_comp, hom.left_act_hom, category.id_comp] },
    slice_rhs 1 2 { rw [â†id_tensor_comp, Î¹_colim_map, parallel_pair_hom_app_one, id_tensor_comp] },
    slice_rhs 2 3 { rw tensor_Bimod.id_tensor_Ï€_act_left },
    slice_rhs 1 2 { rw associator_inv_naturality },
    slice_rhs 2 3 { rw [â†tensor_comp, category.comp_id] },
  end,
  right_act_hom' := begin
    refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
    dsimp,
    slice_lhs 1 2 { rw tensor_Bimod.Ï€_tensor_id_act_right },
    slice_lhs 3 4 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
    slice_lhs 2 3 { rw [â†tensor_comp, category.id_comp, hom.right_act_hom] },
    slice_rhs 1 2 { rw [â†comp_tensor_id, Î¹_colim_map, parallel_pair_hom_app_one, comp_tensor_id] },
    slice_rhs 2 3 { rw tensor_Bimod.Ï€_tensor_id_act_right },
    slice_rhs 1 2 { rw associator_naturality },
    slice_rhs 2 3 { rw [â†tensor_comp, category.comp_id] },
  end }

lemma tensor_id {X Y Z : Mon_ C} {M : Bimod X Y} {N : Bimod Y Z} :
  tensor_hom (ğŸ™ M) (ğŸ™ N) = ğŸ™ (M.tensor_Bimod N) :=
begin
  ext,
  simp only [id_hom', tensor_id, tensor_hom_hom, Î¹_colim_map, parallel_pair_hom_app_one],
  dsimp, dunfold tensor_Bimod.X,
  simp only [category.id_comp, category.comp_id],
end

lemma tensor_comp {X Y Z : Mon_ C} {Mâ‚ Mâ‚‚ Mâ‚ƒ : Bimod X Y} {Nâ‚ Nâ‚‚ Nâ‚ƒ : Bimod Y Z}
  (fâ‚ : Mâ‚ âŸ¶ Mâ‚‚) (fâ‚‚ : Mâ‚‚ âŸ¶ Mâ‚ƒ) (gâ‚ : Nâ‚ âŸ¶ Nâ‚‚) (gâ‚‚ : Nâ‚‚ âŸ¶ Nâ‚ƒ) :
  tensor_hom (fâ‚ â‰« fâ‚‚) (gâ‚ â‰« gâ‚‚) = tensor_hom fâ‚ gâ‚ â‰« tensor_hom fâ‚‚ gâ‚‚ :=
begin
  ext,
  simp only [comp_hom', tensor_comp, tensor_hom_hom, Î¹_colim_map, parallel_pair_hom_app_one,
    category.assoc, Î¹_colim_map_assoc]
end

end

namespace associator_Bimod

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]
variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

variables {R S T U : Mon_ C} (P : Bimod R S) (Q : Bimod S T) (L : Bimod T U)

/-- An auxiliary morphism for the definition of the underlying morphism of the forward component of
the associator isomorphism. -/
noncomputable
def hom_aux : (P.tensor_Bimod Q).X âŠ— L.X âŸ¶ (P.tensor_Bimod (Q.tensor_Bimod L)).X :=
(preserves_coequalizer.iso (tensor_right L.X) _ _).inv â‰«
coequalizer.desc
  ((Î±_ _ _ _).hom â‰« (ğŸ™ P.X âŠ— (coequalizer.Ï€ _ _)) â‰« (coequalizer.Ï€ _ _))
  begin
    dsimp, dsimp [tensor_Bimod.X],
    slice_lhs 1 2 { rw associator_naturality },
    slice_lhs 2 3 { rw [monoidal_category.tensor_id,
                        tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
    slice_lhs 3 4 { rw coequalizer.condition },
    slice_lhs 2 3 { rw [â†monoidal_category.tensor_id, associator_naturality] },
    slice_lhs 3 4 { rw [â†id_tensor_comp, tensor_Bimod.id_tensor_Ï€_act_left, id_tensor_comp] },
    slice_rhs 1 1 { rw comp_tensor_id },
    slice_rhs 2 3 { rw associator_naturality },
    slice_rhs 3 4 { rw â†id_tensor_comp },
    coherence,
  end

/-- The underlying morphism of the forward component of the associator isomorphism. -/
noncomputable
def hom : ((P.tensor_Bimod Q).tensor_Bimod L).X âŸ¶ (P.tensor_Bimod (Q.tensor_Bimod L)).X :=
coequalizer.desc
  (hom_aux P Q L)
  begin
    dsimp [hom_aux],
    refine (cancel_epi ((tensor_right _ â‹™ tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
    dsimp [tensor_Bimod.X],
    slice_lhs 1 2 { rw [â†comp_tensor_id,
                        tensor_Bimod.Ï€_tensor_id_act_right,
                        comp_tensor_id, comp_tensor_id] },
    slice_lhs 3 5 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
    slice_lhs 2 3 { rw associator_naturality },
    slice_lhs 3 4 { rw [â†id_tensor_comp, coequalizer.condition, id_tensor_comp, id_tensor_comp] },
    slice_rhs 1 2 { rw associator_naturality },
    slice_rhs 2 3 { rw [monoidal_category.tensor_id,
                        tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
    slice_rhs 3 5 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
    slice_rhs 2 3 { rw [â†monoidal_category.tensor_id, associator_naturality] },
    coherence,
  end

lemma hom_left_act_hom' :
  ((P.tensor_Bimod Q).tensor_Bimod L).act_left â‰« hom P Q L =
  (ğŸ™ R.X âŠ— hom P Q L) â‰« (P.tensor_Bimod (Q.tensor_Bimod L)).act_left :=
begin
  dsimp, dsimp [hom, hom_aux],
  refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  rw tensor_left_map,
  slice_lhs 1 2 { rw tensor_Bimod.id_tensor_Ï€_act_left },
  slice_lhs 3 4 { rw coequalizer.Ï€_desc },
  slice_rhs 1 2 { rw [â†id_tensor_comp, coequalizer.Ï€_desc, id_tensor_comp] },
  refine (cancel_epi ((tensor_right _ â‹™ tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp, dsimp [tensor_Bimod.X],
  slice_lhs 1 2 { rw associator_inv_naturality },
  slice_lhs 2 3 { rw [â†comp_tensor_id,
                      tensor_Bimod.id_tensor_Ï€_act_left,
                      comp_tensor_id, comp_tensor_id] },
  slice_lhs 4 6 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_lhs 3 4 { rw associator_naturality },
  slice_lhs 4 5 { rw [monoidal_category.tensor_id, tensor_id_comp_id_tensor] },
  slice_rhs 1 3 { rw [â†id_tensor_comp, â†id_tensor_comp,
                      Ï€_tensor_id_preserves_coequalizer_inv_desc,
                      id_tensor_comp, id_tensor_comp] },
  slice_rhs 3 4 { erw tensor_Bimod.id_tensor_Ï€_act_left P (Q.tensor_Bimod L) },
  slice_rhs 2 3 { erw associator_inv_naturality },
  slice_rhs 3 4 { erw [monoidal_category.tensor_id, id_tensor_comp_tensor_id] },
  coherence,
end

lemma hom_right_act_hom' :
  ((P.tensor_Bimod Q).tensor_Bimod L).act_right â‰« hom P Q L =
  (hom P Q L âŠ— ğŸ™ U.X) â‰« (P.tensor_Bimod (Q.tensor_Bimod L)).act_right :=
begin
  dsimp, dsimp [hom, hom_aux],
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  rw tensor_right_map,
  slice_lhs 1 2 { rw tensor_Bimod.Ï€_tensor_id_act_right },
  slice_lhs 3 4 { rw coequalizer.Ï€_desc },
  slice_rhs 1 2 { rw [â†comp_tensor_id, coequalizer.Ï€_desc, comp_tensor_id] },
  refine (cancel_epi ((tensor_right _ â‹™ tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp, dsimp [tensor_Bimod.X],
  slice_lhs 1 2 { rw associator_naturality },
  slice_lhs 2 3 { rw [monoidal_category.tensor_id,
                      tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
  slice_lhs 3 5 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_lhs 2 3 { rw [â†monoidal_category.tensor_id,
                      associator_naturality] },
  slice_rhs 1 3 { rw [â†comp_tensor_id, â†comp_tensor_id,
                      Ï€_tensor_id_preserves_coequalizer_inv_desc,
                      comp_tensor_id, comp_tensor_id] },
  slice_rhs 3 4 { erw tensor_Bimod.Ï€_tensor_id_act_right P (Q.tensor_Bimod L) },
  slice_rhs 2 3 { erw associator_naturality },
  dsimp,
  slice_rhs 3 4 { rw [â†id_tensor_comp,
                      tensor_Bimod.Ï€_tensor_id_act_right,
                      id_tensor_comp, id_tensor_comp] },
  coherence,
end

/-- An auxiliary morphism for the definition of the underlying morphism of the inverse component of
the associator isomorphism. -/
noncomputable
def inv_aux : P.X âŠ— (Q.tensor_Bimod L).X âŸ¶ ((P.tensor_Bimod Q).tensor_Bimod L).X :=
(preserves_coequalizer.iso (tensor_left P.X) _ _).inv â‰«
coequalizer.desc
  ((Î±_ _ _ _).inv â‰« ((coequalizer.Ï€ _ _) âŠ— ğŸ™ L.X) â‰« (coequalizer.Ï€ _ _))
  begin
    dsimp, dsimp [tensor_Bimod.X],
    slice_lhs 1 2 { rw associator_inv_naturality },
    rw [â†(iso.inv_hom_id_assoc (Î±_ _ _ _) (ğŸ™ P.X âŠ— Q.act_right)), comp_tensor_id],
    slice_lhs 3 4 { rw [â†comp_tensor_id, category.assoc, â†tensor_Bimod.Ï€_tensor_id_act_right,
                        comp_tensor_id] },
    slice_lhs 4 5 { rw coequalizer.condition },
    slice_lhs 3 4 { rw associator_naturality },
    slice_lhs 4 5 { rw [monoidal_category.tensor_id, tensor_id_comp_id_tensor] },
    slice_rhs 1 2 { rw id_tensor_comp },
    slice_rhs 2 3 { rw associator_inv_naturality },
    slice_rhs 3 4 { rw [monoidal_category.tensor_id, id_tensor_comp_tensor_id] },
    coherence,
  end

/-- The underlying morphism of the inverse component of the associator isomorphism. -/
noncomputable
def inv : (P.tensor_Bimod (Q.tensor_Bimod L)).X âŸ¶ ((P.tensor_Bimod Q).tensor_Bimod L).X :=
coequalizer.desc
  (inv_aux P Q L)
  begin
    dsimp [inv_aux],
    refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
    dsimp [tensor_Bimod.X],
    slice_lhs 1 2 { rw [id_tensor_comp_tensor_id, â†tensor_id_comp_id_tensor] },
    slice_lhs 2 4 { rw id_tensor_Ï€_preserves_coequalizer_inv_desc },
    slice_lhs 1 2 { rw [â†monoidal_category.tensor_id, associator_inv_naturality] },
    slice_lhs 2 3 { rw [â†comp_tensor_id, coequalizer.condition, comp_tensor_id, comp_tensor_id] },
    slice_rhs 1 2 { rw [â†monoidal_category.tensor_id, associator_naturality] },
    slice_rhs 2 3 { rw [â†id_tensor_comp,
                        tensor_Bimod.id_tensor_Ï€_act_left,
                        id_tensor_comp, id_tensor_comp] },
    slice_rhs 4 6 { rw id_tensor_Ï€_preserves_coequalizer_inv_desc },
    slice_rhs 3 4 { rw associator_inv_naturality },
    coherence,
  end

lemma hom_inv_id : hom P Q L â‰« inv P Q L = ğŸ™ _ :=
begin
  dsimp [hom, hom_aux, inv, inv_aux],
  ext,
  slice_lhs 1 2 { rw coequalizer.Ï€_desc },
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  rw tensor_right_map,
  slice_lhs 1 3 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_lhs 3 4 { rw coequalizer.Ï€_desc },
  slice_lhs 2 4 { rw id_tensor_Ï€_preserves_coequalizer_inv_desc },
  slice_lhs 1 3 { rw iso.hom_inv_id_assoc },
  dunfold tensor_Bimod.X,
  slice_rhs 2 3 { rw category.comp_id },
  refl,
end

lemma inv_hom_id : inv P Q L â‰« hom P Q L = ğŸ™ _ :=
begin
  dsimp [hom, hom_aux, inv, inv_aux],
  ext,
  slice_lhs 1 2 { rw coequalizer.Ï€_desc },
  refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  rw tensor_left_map,
  slice_lhs 1 3 { rw id_tensor_Ï€_preserves_coequalizer_inv_desc },
  slice_lhs 3 4 { rw coequalizer.Ï€_desc },
  slice_lhs 2 4 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_lhs 1 3 { rw iso.inv_hom_id_assoc },
  dunfold tensor_Bimod.X,
  slice_rhs 2 3 { rw category.comp_id },
  refl,
end

end associator_Bimod

namespace left_unitor_Bimod
variables {R S : Mon_ C} (P : Bimod R S)

/-- The underlying morphism of the forward component of the left unitor isomorphism. -/
noncomputable
def hom : tensor_Bimod.X (regular R) P âŸ¶ P.X :=
coequalizer.desc P.act_left (by { dsimp, rw [category.assoc, left_assoc] })

/-- The underlying morphism of the inverse component of the left unitor isomorphism. -/
noncomputable
def inv : P.X âŸ¶ tensor_Bimod.X (regular R) P :=
(Î»_ P.X).inv â‰« (R.one âŠ— ğŸ™ _) â‰« coequalizer.Ï€ _ _

lemma hom_inv_id : hom P â‰« inv P = ğŸ™ _ :=
begin
  dunfold hom inv tensor_Bimod.X,
  ext, dsimp,
  slice_lhs 1 2 { rw coequalizer.Ï€_desc },
  slice_lhs 1 2 { rw left_unitor_inv_naturality },
  slice_lhs 2 3 { rw [id_tensor_comp_tensor_id, â†tensor_id_comp_id_tensor] },
  slice_lhs 3 3 { rw â†(iso.inv_hom_id_assoc (Î±_ R.X R.X P.X) (ğŸ™ R.X âŠ— P.act_left)) },
  slice_lhs 4 6 { rw [â†category.assoc, â†coequalizer.condition] },
  slice_lhs 2 3 { rw [â†monoidal_category.tensor_id, associator_inv_naturality] },
  slice_lhs 3 4 { rw [â†comp_tensor_id, Mon_.one_mul] },
  slice_rhs 1 2 { rw category.comp_id },
  coherence,
end

lemma inv_hom_id : inv P â‰« hom P = ğŸ™ _ :=
begin
  dsimp [hom, inv],
  slice_lhs 3 4 { rw coequalizer.Ï€_desc },
  rw [one_act_left, iso.inv_hom_id],
end

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]
variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

lemma hom_left_act_hom' :
  ((regular R).tensor_Bimod P).act_left â‰« hom P = (ğŸ™ R.X âŠ— hom P) â‰« P.act_left :=
begin
  dsimp, dsimp [hom, tensor_Bimod.act_left, regular],
  refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp,
  slice_lhs 1 4 { rw id_tensor_Ï€_preserves_coequalizer_inv_colim_map_desc },
  slice_lhs 2 3 { rw left_assoc },
  slice_rhs 1 2 { rw [â†id_tensor_comp, coequalizer.Ï€_desc] },
  rw iso.inv_hom_id_assoc,
end

lemma hom_right_act_hom' :
  ((regular R).tensor_Bimod P).act_right â‰« hom P = (hom P âŠ— ğŸ™ S.X) â‰« P.act_right :=
begin
  dsimp, dsimp [hom, tensor_Bimod.act_right, regular],
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp,
  slice_lhs 1 4 { rw Ï€_tensor_id_preserves_coequalizer_inv_colim_map_desc },
  slice_rhs 1 2 { rw [â†comp_tensor_id, coequalizer.Ï€_desc] },
  slice_rhs 1 2 { rw middle_assoc },
  simp only [category.assoc],
end

end left_unitor_Bimod

namespace right_unitor_Bimod
variables {R S : Mon_ C} (P : Bimod R S)

/-- The underlying morphism of the forward component of the right unitor isomorphism. -/
noncomputable
def hom : tensor_Bimod.X P (regular S) âŸ¶ P.X :=
coequalizer.desc P.act_right
  (by { dsimp, rw [category.assoc, right_assoc, iso.hom_inv_id_assoc] })

/-- The underlying morphism of the inverse component of the right unitor isomorphism. -/
noncomputable
def inv : P.X âŸ¶ tensor_Bimod.X P (regular S) :=
(Ï_ P.X).inv â‰« (ğŸ™ _ âŠ— S.one) â‰« coequalizer.Ï€ _ _

lemma hom_inv_id : hom P â‰« inv P = ğŸ™ _ :=
begin
  dunfold hom inv tensor_Bimod.X,
  ext, dsimp,
  slice_lhs 1 2 { rw coequalizer.Ï€_desc },
  slice_lhs 1 2 { rw right_unitor_inv_naturality },
  slice_lhs 2 3 { rw [tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
  slice_lhs 3 4 { rw coequalizer.condition },
  slice_lhs 2 3 { rw [â†monoidal_category.tensor_id, associator_naturality] },
  slice_lhs 3 4 { rw [â†id_tensor_comp, Mon_.mul_one] },
  slice_rhs 1 2 { rw category.comp_id },
  coherence,
end

lemma inv_hom_id : inv P â‰« hom P = ğŸ™ _ :=
begin
  dsimp [hom, inv],
  slice_lhs 3 4 { rw coequalizer.Ï€_desc },
  rw [act_right_one, iso.inv_hom_id],
end

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]
variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

lemma hom_left_act_hom' :
  (P.tensor_Bimod (regular S)).act_left â‰« hom P = (ğŸ™ R.X âŠ— hom P) â‰« P.act_left :=
begin
  dsimp, dsimp [hom, tensor_Bimod.act_left, regular],
  refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp,
  slice_lhs 1 4 { rw id_tensor_Ï€_preserves_coequalizer_inv_colim_map_desc },
  slice_lhs 2 3 { rw middle_assoc },
  slice_rhs 1 2 { rw [â†id_tensor_comp, coequalizer.Ï€_desc] },
  rw iso.inv_hom_id_assoc,
end

lemma hom_right_act_hom' :
  (P.tensor_Bimod (regular S)).act_right â‰« hom P = (hom P âŠ— ğŸ™ S.X) â‰« P.act_right :=
begin
  dsimp, dsimp [hom, tensor_Bimod.act_right, regular],
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp,
  slice_lhs 1 4 { rw Ï€_tensor_id_preserves_coequalizer_inv_colim_map_desc },
  slice_lhs 2 3 { rw right_assoc },
  slice_rhs 1 2 { rw [â†comp_tensor_id, coequalizer.Ï€_desc] },
  rw iso.hom_inv_id_assoc,
end

end right_unitor_Bimod

variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_left X)]
variables [âˆ€ X : C, preserves_colimits_of_size.{0 0} (tensor_right X)]

/-- The associator as a bimodule isomorphism. -/
noncomputable
def associator_Bimod {W X Y Z : Mon_ C} (L : Bimod W X) (M : Bimod X Y) (N : Bimod Y Z) :
   (L.tensor_Bimod M).tensor_Bimod N â‰… L.tensor_Bimod (M.tensor_Bimod N) :=
iso_of_iso
  { hom := associator_Bimod.hom L M N,
    inv := associator_Bimod.inv L M N,
    hom_inv_id' := associator_Bimod.hom_inv_id L M N,
    inv_hom_id' := associator_Bimod.inv_hom_id L M N }
  (associator_Bimod.hom_left_act_hom' L M N)
  (associator_Bimod.hom_right_act_hom' L M N)

/-- The left unitor as a bimodule isomorphism. -/
noncomputable
def left_unitor_Bimod {X Y : Mon_ C} (M : Bimod X Y) : (regular X).tensor_Bimod M â‰… M :=
iso_of_iso
  { hom := left_unitor_Bimod.hom M,
    inv := left_unitor_Bimod.inv M,
    hom_inv_id' := left_unitor_Bimod.hom_inv_id M,
    inv_hom_id' := left_unitor_Bimod.inv_hom_id M }
  (left_unitor_Bimod.hom_left_act_hom' M)
  (left_unitor_Bimod.hom_right_act_hom' M)

/-- The right unitor as a bimodule isomorphism. -/
noncomputable
def right_unitor_Bimod {X Y : Mon_ C} (M : Bimod X Y) : M.tensor_Bimod (regular Y) â‰… M :=
iso_of_iso
  { hom := right_unitor_Bimod.hom M,
    inv := right_unitor_Bimod.inv M,
    hom_inv_id' := right_unitor_Bimod.hom_inv_id M,
    inv_hom_id' := right_unitor_Bimod.inv_hom_id M }
  (right_unitor_Bimod.hom_left_act_hom' M)
  (right_unitor_Bimod.hom_right_act_hom' M)

lemma whisker_left_comp_Bimod {X Y Z : Mon_ C}
  (M : Bimod X Y) {N P Q : Bimod Y Z} (f : N âŸ¶ P) (g : P âŸ¶ Q) :
  tensor_hom (ğŸ™ M) (f â‰« g) = tensor_hom (ğŸ™ M) f â‰« tensor_hom (ğŸ™ M) g :=
by rw [â†tensor_comp, category.comp_id]

lemma id_whisker_left_Bimod {X Y : Mon_ C} {M N : Bimod X Y} (f : M âŸ¶ N) :
  tensor_hom (ğŸ™ (regular X)) f = (left_unitor_Bimod M).hom â‰« f â‰« (left_unitor_Bimod N).inv :=
begin
  dsimp [tensor_hom, regular, left_unitor_Bimod],
  ext, dsimp,
  slice_lhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [left_unitor_Bimod.hom],
  slice_rhs 1 2 { rw coequalizer.Ï€_desc },
  dsimp [left_unitor_Bimod.inv],
  slice_rhs 1 2 { rw hom.left_act_hom },
  slice_rhs 2 3 { rw left_unitor_inv_naturality },
  slice_rhs 3 4 { rw [id_tensor_comp_tensor_id, â†tensor_id_comp_id_tensor] },
  slice_rhs 4 4 { rw â†(iso.inv_hom_id_assoc (Î±_ X.X X.X N.X) (ğŸ™ X.X âŠ— N.act_left)) },
  slice_rhs 5 7 { rw [â†category.assoc, â†coequalizer.condition] },
  slice_rhs 3 4 { rw [â†monoidal_category.tensor_id, associator_inv_naturality] },
  slice_rhs 4 5 { rw [â†comp_tensor_id, Mon_.one_mul] },
  have :
    (Î»_ (X.X âŠ— N.X)).inv â‰« (Î±_ (ğŸ™_ C) X.X N.X).inv â‰« ((Î»_ X.X).hom âŠ— ğŸ™ N.X) = ğŸ™ _ :=
    by pure_coherence,
  slice_rhs 2 4 { rw this },
  slice_rhs 1 2 { rw category.comp_id },
end

lemma comp_whisker_left_Bimod {W X Y Z : Mon_ C}
  (M : Bimod W X) (N : Bimod X Y) {P P' : Bimod Y Z} (f : P âŸ¶ P') :
  tensor_hom (ğŸ™ (M.tensor_Bimod N)) f =
  (associator_Bimod M N P).hom â‰« tensor_hom (ğŸ™ M) (tensor_hom (ğŸ™ N) f) â‰«
    (associator_Bimod M N P').inv :=
begin
  dsimp [tensor_hom, tensor_Bimod, associator_Bimod],
  ext, dsimp,
  slice_lhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [tensor_Bimod.X, associator_Bimod.hom],
  slice_rhs 1 2 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.hom_aux, associator_Bimod.inv],
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  rw tensor_right_map,
  slice_rhs 1 3 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_rhs 3 4 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 2 3 { rw [â†id_tensor_comp, Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 3 4 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.inv_aux],
  slice_rhs 2 2 { rw id_tensor_comp },
  slice_rhs 3 5 { rw id_tensor_Ï€_preserves_coequalizer_inv_desc },
  slice_rhs 2 3 { rw associator_inv_naturality },
  slice_rhs 1 3 { rw [iso.hom_inv_id_assoc, monoidal_category.tensor_id] },
  slice_lhs 1 2 { rw [tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
  dunfold tensor_Bimod.X,
  simp only [category.assoc],
end

lemma comp_whisker_right_Bimod {X Y Z : Mon_ C}
  {M N P : Bimod X Y} (f : M âŸ¶ N) (g : N âŸ¶ P) (Q : Bimod Y Z) :
  tensor_hom (f â‰« g) (ğŸ™ Q) = tensor_hom f (ğŸ™ Q) â‰« tensor_hom g (ğŸ™ Q) :=
by rw [â†tensor_comp, category.comp_id]

lemma whisker_right_id_Bimod {X Y : Mon_ C} {M N : Bimod X Y} (f : M âŸ¶ N) :
  tensor_hom f (ğŸ™ (regular Y)) = (right_unitor_Bimod M).hom â‰« f â‰« (right_unitor_Bimod N).inv :=
begin
  dsimp [tensor_hom, regular, right_unitor_Bimod],
  ext, dsimp,
  slice_lhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [right_unitor_Bimod.hom],
  slice_rhs 1 2 { rw coequalizer.Ï€_desc },
  dsimp [right_unitor_Bimod.inv],
  slice_rhs 1 2 { rw hom.right_act_hom },
  slice_rhs 2 3 { rw right_unitor_inv_naturality },
  slice_rhs 3 4 { rw [tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
  slice_rhs 4 5 { rw coequalizer.condition },
  slice_rhs 3 4 { rw [â†monoidal_category.tensor_id, associator_naturality] },
  slice_rhs 4 5 { rw [â†id_tensor_comp, Mon_.mul_one] },
  have :
    (Ï_ (N.X âŠ— Y.X)).inv â‰« (Î±_ N.X Y.X (ğŸ™_ C)).hom â‰« (ğŸ™ N.X âŠ— (Ï_ Y.X).hom) = ğŸ™ _ :=
    by pure_coherence,
  slice_rhs 2 4 { rw this },
  slice_rhs 1 2 { rw category.comp_id },
end

lemma whisker_right_comp_Bimod {W X Y Z : Mon_ C}
  {M M' : Bimod W X} (f : M âŸ¶ M') (N : Bimod X Y) (P : Bimod Y Z) :
  tensor_hom f (ğŸ™ (N.tensor_Bimod P)) =
  (associator_Bimod M N P).inv â‰« tensor_hom (tensor_hom f (ğŸ™ N)) (ğŸ™ P) â‰«
    (associator_Bimod M' N P).hom :=
begin
  dsimp [tensor_hom, tensor_Bimod, associator_Bimod],
  ext, dsimp,
  slice_lhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [tensor_Bimod.X, associator_Bimod.inv],
  slice_rhs 1 2 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.inv_aux, associator_Bimod.hom],
  refine (cancel_epi ((tensor_left _).map (coequalizer.Ï€ _ _))).1 _,
  rw tensor_left_map,
  slice_rhs 1 3 { rw id_tensor_Ï€_preserves_coequalizer_inv_desc },
  slice_rhs 3 4 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 2 3 { rw [â†comp_tensor_id, Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 3 4 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.hom_aux],
  slice_rhs 2 2 { rw comp_tensor_id },
  slice_rhs 3 5 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_rhs 2 3 { rw associator_naturality },
  slice_rhs 1 3 { rw [iso.inv_hom_id_assoc, monoidal_category.tensor_id] },
  slice_lhs 1 2 { rw [id_tensor_comp_tensor_id, â†tensor_id_comp_id_tensor] },
  dunfold tensor_Bimod.X,
  simp only [category.assoc],
end

lemma whisker_assoc_Bimod {W X Y Z : Mon_ C}
  (M : Bimod W X) {N N' : Bimod X Y} (f : N âŸ¶ N') (P : Bimod Y Z) :
  tensor_hom (tensor_hom (ğŸ™ M) f) (ğŸ™ P) =
  (associator_Bimod M N P).hom â‰« tensor_hom (ğŸ™ M) (tensor_hom f (ğŸ™ P)) â‰«
    (associator_Bimod M N' P).inv :=
begin
  dsimp [tensor_hom, tensor_Bimod, associator_Bimod],
  ext, dsimp,
  slice_lhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [associator_Bimod.hom],
  slice_rhs 1 2 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.hom_aux],
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  rw tensor_right_map,
  slice_lhs 1 2 { rw [â†comp_tensor_id, Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 1 3 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_rhs 3 4 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 2 3 { rw [â†id_tensor_comp, Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [associator_Bimod.inv],
  slice_rhs 3 4 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.inv_aux],
  slice_rhs 2 2 { rw id_tensor_comp },
  slice_rhs 3 5 { rw id_tensor_Ï€_preserves_coequalizer_inv_desc },
  slice_rhs 2 3 { rw associator_inv_naturality },
  slice_rhs 1 3 { rw iso.hom_inv_id_assoc },
  slice_lhs 1 1 { rw comp_tensor_id },
end

lemma whisker_exchange_Bimod {X Y Z : Mon_ C}
  {M N : Bimod X Y} {P Q : Bimod Y Z} (f : M âŸ¶ N) (g : P âŸ¶ Q) :
  tensor_hom (ğŸ™ M) g â‰« tensor_hom f (ğŸ™ Q) = tensor_hom f (ğŸ™ P) â‰« tensor_hom (ğŸ™ N) g :=
begin
  dsimp [tensor_hom],
  ext, dsimp,
  slice_lhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_lhs 2 3 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_lhs 1 2 { rw id_tensor_comp_tensor_id },
  slice_rhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 2 3 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_rhs 1 2 { rw tensor_id_comp_id_tensor },
end

lemma pentagon_Bimod {V W X Y Z : Mon_ C}
  (M : Bimod V W) (N : Bimod W X) (P : Bimod X Y) (Q : Bimod Y Z) :
  tensor_hom (associator_Bimod M N P).hom (ğŸ™ Q) â‰« (associator_Bimod M (N.tensor_Bimod P) Q).hom â‰«
    tensor_hom (ğŸ™ M) (associator_Bimod N P Q).hom =
  (associator_Bimod (M.tensor_Bimod N) P Q).hom â‰« (associator_Bimod M N (P.tensor_Bimod Q)).hom :=
begin
  dsimp [tensor_hom, associator_Bimod], ext, dsimp,
  dunfold associator_Bimod.hom,
  slice_lhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_lhs 2 3 { rw coequalizer.Ï€_desc },
  slice_rhs 1 2 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.hom_aux],
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp,
  slice_lhs 1 2 { rw [â†comp_tensor_id, coequalizer.Ï€_desc] },
  slice_rhs 1 3 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_rhs 3 4 { rw coequalizer.Ï€_desc },
  refine (cancel_epi ((tensor_right _ â‹™ tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp,
  slice_lhs 1 2 { rw [â†comp_tensor_id,
                      Ï€_tensor_id_preserves_coequalizer_inv_desc,
                      comp_tensor_id, comp_tensor_id ]},
  slice_lhs 3 5 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  dunfold tensor_Bimod.X,
  slice_lhs 2 3 { rw associator_naturality },
  slice_lhs 5 6 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  slice_lhs 4 5 { rw [â†id_tensor_comp, coequalizer.Ï€_desc] },
  slice_lhs 3 4 { rw [â†id_tensor_comp,
                      Ï€_tensor_id_preserves_coequalizer_inv_desc,
                      id_tensor_comp, id_tensor_comp] },
  slice_rhs 1 2 { rw associator_naturality },
  slice_rhs 2 3 { rw [monoidal_category.tensor_id,
                      tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id] },
  slice_rhs 3 5 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_rhs 2 3 { rw [â†monoidal_category.tensor_id, associator_naturality] },
  coherence,
end

lemma triangle_Bimod {X Y Z : Mon_ C} (M : Bimod X Y) (N : Bimod Y Z) :
  (associator_Bimod M (regular Y) N).hom â‰« tensor_hom (ğŸ™ M) (left_unitor_Bimod N).hom =
  tensor_hom (right_unitor_Bimod M).hom (ğŸ™ N) :=
begin
  dsimp [tensor_hom, associator_Bimod, left_unitor_Bimod, right_unitor_Bimod],
  ext, dsimp,
  dsimp [associator_Bimod.hom],
  slice_lhs 1 2 { rw coequalizer.Ï€_desc },
  dsimp [associator_Bimod.hom_aux],
  slice_rhs 1 2 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [right_unitor_Bimod.hom],
  refine (cancel_epi ((tensor_right _).map (coequalizer.Ï€ _ _))).1 _,
  dsimp [regular],
  slice_lhs 1 3 { rw Ï€_tensor_id_preserves_coequalizer_inv_desc },
  slice_lhs 3 4 { rw [Î¹_colim_map, parallel_pair_hom_app_one] },
  dsimp [left_unitor_Bimod.hom],
  slice_lhs 2 3 { rw [â†id_tensor_comp, coequalizer.Ï€_desc] },
  slice_rhs 1 2 { rw [â†comp_tensor_id, coequalizer.Ï€_desc] },
  slice_rhs 1 2 { rw coequalizer.condition },
  simp only [category.assoc],
end

/-- The bicategory of algebras (monoids) and bimodules, all internal to some monoidal category. -/
noncomputable
def Mon_bicategory : bicategory (Mon_ C) :=
{ hom := Î» X Y, Bimod X Y,
  id := Î» X, regular X,
  comp := Î» _ _ _ M N, tensor_Bimod M N,
  whisker_left := Î» _ _ _ L _ _ f, tensor_hom (ğŸ™ L) f,
  whisker_right := Î» _ _ _ _ _ f N, tensor_hom f (ğŸ™ N),
  associator := Î» _ _ _ _ L M N, associator_Bimod L M N,
  left_unitor := Î» _ _ M, left_unitor_Bimod M,
  right_unitor := Î» _ _ M, right_unitor_Bimod M,
  whisker_left_id' := Î» _ _ _ _ _, tensor_id,
  whisker_left_comp' := Î» _ _ _ M _ _ _ f g, whisker_left_comp_Bimod M f g,
  id_whisker_left' := Î» _ _ _ _ f, id_whisker_left_Bimod f,
  comp_whisker_left' := Î» _ _ _ _ M N _ _ f, comp_whisker_left_Bimod M N f,
  id_whisker_right' := Î» _ _ _ _ _, tensor_id,
  comp_whisker_right' := Î» _ _ _ _ _ _ f g Q, comp_whisker_right_Bimod f g Q,
  whisker_right_id' := Î» _ _ _ _ f, whisker_right_id_Bimod f,
  whisker_right_comp' := Î» _ _ _ _ _ _ f N P, whisker_right_comp_Bimod f N P,
  whisker_assoc' := Î» _ _ _ _ M _ _ f P, whisker_assoc_Bimod M f P,
  whisker_exchange' := Î» _ _ _ _ _ _ _ f g, whisker_exchange_Bimod f g,
  pentagon' := Î» _ _ _ _ _ M N P Q, pentagon_Bimod M N P Q,
  triangle' := Î» _ _ _ M N, triangle_Bimod M N }

end Bimod
