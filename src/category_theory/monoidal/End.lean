/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Andrew Yang
-/
import category_theory.monoidal.functor

/-!
# Endofunctors as a monoidal category.

We give the monoidal category structure on `C â¥¤ C`,
and show that when `C` itself is monoidal, it embeds via a monoidal functor into `C â¥¤ C`.

## TODO

Can we use this to show coherence results, e.g. a cheap proof that `Î»_ (ğŸ™_ C) = Ï_ (ğŸ™_ C)`?
I suspect this is harder than is usually made out.
-/

universes v u

namespace category_theory

variables (C : Type u) [category.{v} C]

/--
The category of endofunctors of any category is a monoidal category,
with tensor product given by composition of functors
(and horizontal composition of natural transformations).
-/
def endofunctor_monoidal_category : monoidal_category (C â¥¤ C) :=
{ tensor_obj   := Î» F G, F â‹™ G,
  tensor_hom   := Î» F G F' G' Î± Î², Î± â—« Î²,
  tensor_unit  := ğŸ­ C,
  associator   := Î» F G H, functor.associator F G H,
  left_unitor  := Î» F, functor.left_unitor F,
  right_unitor := Î» F, functor.right_unitor F, }.

open category_theory.monoidal_category

local attribute [instance] endofunctor_monoidal_category
local attribute [reducible] endofunctor_monoidal_category

/--
Tensoring on the right gives a monoidal functor from `C` into endofunctors of `C`.
-/
@[simps]
def tensoring_right_monoidal [monoidal_category.{v} C] : monoidal_functor C (C â¥¤ C) :=
{ Îµ := (right_unitor_nat_iso C).inv,
  Î¼ := Î» X Y,
  { app := Î» Z, (Î±_ Z X Y).hom,
    naturality' := Î» Z Z' f, by { dsimp, rw associator_naturality, simp, } },
  Î¼_natural' := Î» X Y X' Y' f g, by { ext Z, dsimp, simp [associator_naturality], },
  associativity' := Î» X Y Z, by { ext W, dsimp, simp [pentagon], },
  left_unitality' := Î» X, by { ext Y, dsimp, rw [category.id_comp, triangle, â†tensor_comp], simp, },
  right_unitality' := Î» X,
  begin
    ext Y, dsimp,
    rw [tensor_id, category.comp_id, right_unitor_tensor_inv, category.assoc, iso.inv_hom_id_assoc,
      â†id_tensor_comp, iso.inv_hom_id, tensor_id],
  end,
  Îµ_is_iso := by apply_instance,
  Î¼_is_iso := Î» X Y,
    -- We could avoid needing to do this explicitly by
    -- constructing a partially applied analogue of `associator_nat_iso`.
  âŸ¨âŸ¨{ app := Î» Z, (Î±_ Z X Y).inv,
      naturality' := Î» Z Z' f, by { dsimp, rw â†associator_inv_naturality, simp, } },
    by tidyâŸ©âŸ©,
  ..tensoring_right C }.

variable {C}
variables {M : Type*} [category M] [monoidal_category M] (F : monoidal_functor M (C â¥¤ C))

@[simp, reassoc]
lemma Î¼_hom_inv_app (i j : M) (X : C) :
  (F.Î¼ i j).app X â‰« (F.Î¼_iso i j).inv.app X = ğŸ™ _ := (F.Î¼_iso i j).hom_inv_id_app X

@[simp, reassoc]
lemma Î¼_inv_hom_app (i j : M) (X : C) :
   (F.Î¼_iso i j).inv.app X â‰« (F.Î¼ i j).app X = ğŸ™ _ := (F.Î¼_iso i j).inv_hom_id_app X

@[simp, reassoc]
lemma Îµ_hom_inv_app (X : C) :
  F.Îµ.app X â‰« F.Îµ_iso.inv.app X = ğŸ™ _ := F.Îµ_iso.hom_inv_id_app X

@[simp, reassoc]
lemma Îµ_inv_hom_app (X : C) :
  F.Îµ_iso.inv.app X â‰« F.Îµ.app X = ğŸ™ _ := F.Îµ_iso.inv_hom_id_app X

@[simp, reassoc]
lemma Îµ_naturality {X Y : C} (f : X âŸ¶ Y) :
  F.Îµ.app X â‰« (F.obj (ğŸ™_M)).map f = f â‰« F.Îµ.app Y := (F.Îµ.naturality f).symm

@[simp, reassoc]
lemma Îµ_inv_naturality {X Y : C} (f : X âŸ¶ Y) :
  (F.obj (ğŸ™_M)).map f â‰« F.Îµ_iso.inv.app Y = F.Îµ_iso.inv.app X â‰« f :=
F.Îµ_iso.inv.naturality f

@[simp, reassoc]
lemma Î¼_naturality {m n : M} {X Y : C} (f : X âŸ¶ Y) :
  (F.obj n).map ((F.obj m).map f) â‰« (F.Î¼ m n).app Y = (F.Î¼ m n).app X â‰« (F.obj _).map f :=
(F.to_lax_monoidal_functor.Î¼ m n).naturality f

-- This is a simp lemma in the reverse direction via `nat_trans.naturality`.
@[reassoc]
lemma Î¼_inv_naturality {m n : M} {X Y : C} (f : X âŸ¶ Y) :
  (F.Î¼_iso m n).inv.app X â‰« (F.obj n).map ((F.obj m).map f) =
    (F.obj _).map f â‰« (F.Î¼_iso m n).inv.app Y :=
((F.Î¼_iso m n).inv.naturality f).symm

-- This is not a simp lemma since it could be proved by the lemmas later.
@[reassoc]
lemma Î¼_naturalityâ‚‚ {m n m' n' : M} (f : m âŸ¶ m') (g : n âŸ¶ n') (X : C) :
  (F.map g).app ((F.obj m).obj X) â‰« (F.obj n').map ((F.map f).app X) â‰« (F.Î¼ m' n').app X =
    (F.Î¼ m n).app X â‰« (F.map (f âŠ— g)).app X :=
begin
  have := congr_app (F.to_lax_monoidal_functor.Î¼_natural f g) X,
  dsimp at this,
  simpa using this,
end

@[simp, reassoc]
lemma Î¼_naturalityâ‚— {m n m' : M} (f : m âŸ¶ m') (X : C) :
  (F.obj n).map ((F.map f).app X) â‰« (F.Î¼ m' n).app X =
    (F.Î¼ m n).app X â‰« (F.map (f âŠ— ğŸ™ n)).app X :=
begin
  rw â† Î¼_naturalityâ‚‚ F f (ğŸ™ n) X,
  simp,
end

@[simp, reassoc]
lemma Î¼_naturalityáµ£ {m n n' : M} (g : n âŸ¶ n') (X : C) :
  (F.map g).app ((F.obj m).obj X) â‰« (F.Î¼ m n').app X =
    (F.Î¼ m n).app X â‰« (F.map (ğŸ™ m âŠ— g)).app X :=
begin
  rw â† Î¼_naturalityâ‚‚ F (ğŸ™ m) g X,
  simp,
end

@[simp, reassoc]
lemma Î¼_inv_naturalityâ‚— {m n m' : M} (f : m âŸ¶ m') (X : C) :
  (F.Î¼_iso m n).inv.app X â‰« (F.obj n).map ((F.map f).app X) =
    (F.map (f âŠ— ğŸ™ n)).app X â‰« (F.Î¼_iso m' n).inv.app X :=
begin
  rw [â† is_iso.comp_inv_eq, category.assoc, â† is_iso.eq_inv_comp],
  simp,
end

@[simp, reassoc]
lemma Î¼_inv_naturalityáµ£ {m n n' : M} (g : n âŸ¶ n') (X : C) :
  (F.Î¼_iso m n).inv.app X â‰« (F.map g).app ((F.obj m).obj X) =
    (F.map (ğŸ™ m âŠ— g)).app X â‰« (F.Î¼_iso m n').inv.app X :=
begin
  rw [â† is_iso.comp_inv_eq, category.assoc, â† is_iso.eq_inv_comp],
  simp,
end

@[reassoc]
lemma left_unitality_app (n : M) (X : C) :
  (F.obj n).map (F.Îµ.app X) â‰« (F.Î¼ (ğŸ™_M) n).app X
    â‰« (F.map (Î»_ n).hom).app X = ğŸ™ _ :=
begin
  have := congr_app (F.to_lax_monoidal_functor.left_unitality n) X,
  dsimp at this,
  simpa using this.symm,
end

@[reassoc, simp]
lemma obj_Îµ_app (n : M) (X : C) :
  (F.obj n).map (F.Îµ.app X) =
    (F.map (Î»_ n).inv).app X â‰« (F.Î¼_iso (ğŸ™_M) n).inv.app X :=
begin
  refine eq.trans _ (category.id_comp _),
  rw [â† category.assoc, â† is_iso.comp_inv_eq, â† is_iso.comp_inv_eq, category.assoc],
  convert left_unitality_app F n X,
  { simp },
  { ext, simpa }
end

@[reassoc, simp]
lemma obj_Îµ_inv_app (n : M) (X : C) :
  (F.obj n).map (F.Îµ_iso.inv.app X) =
    (F.Î¼ (ğŸ™_M) n).app X â‰« (F.map (Î»_ n).hom).app X  :=
begin
  rw [â† cancel_mono ((F.obj n).map (F.Îµ.app X)), â† functor.map_comp],
  simpa,
end

@[reassoc]
lemma right_unitality_app (n : M) (X : C) :
  F.Îµ.app ((F.obj n).obj X) â‰« (F.Î¼ n (ğŸ™_M)).app X â‰« (F.map (Ï_ n).hom).app X = ğŸ™ _ :=
begin
  have := congr_app (F.to_lax_monoidal_functor.right_unitality n) X,
  dsimp at this,
  simpa using this.symm,
end

@[simp]
lemma Îµ_app_obj (n : M) (X : C) :
  F.Îµ.app ((F.obj n).obj X) =
    (F.map (Ï_ n).inv).app X â‰« (F.Î¼_iso n (ğŸ™_M)).inv.app X :=
begin
  refine eq.trans _ (category.id_comp _),
  rw [â† category.assoc, â† is_iso.comp_inv_eq, â† is_iso.comp_inv_eq, category.assoc],
  convert right_unitality_app F n X,
  { simp },
  { ext, simpa }
end

@[simp]
lemma Îµ_inv_app_obj (n : M) (X : C) :
  F.Îµ_iso.inv.app ((F.obj n).obj X) =
    (F.Î¼ n (ğŸ™_M)).app X â‰« (F.map (Ï_ n).hom).app X :=
begin
  rw [â† cancel_mono (F.Îµ.app ((F.obj n).obj X)), Îµ_inv_hom_app],
  simpa
end

@[reassoc]
lemma associativity_app (mâ‚ mâ‚‚ mâ‚ƒ: M) (X : C) :
  (F.obj mâ‚ƒ).map ((F.Î¼ mâ‚ mâ‚‚).app X) â‰« (F.Î¼ (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).app X â‰«
    (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).hom).app X =
  (F.Î¼ mâ‚‚ mâ‚ƒ).app ((F.obj mâ‚).obj X) â‰« (F.Î¼ mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).app X :=
begin
  have := congr_app (F.to_lax_monoidal_functor.associativity mâ‚ mâ‚‚ mâ‚ƒ) X,
  dsimp at this,
  simpa using this,
end

@[reassoc, simp]
lemma obj_Î¼_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) :
  (F.obj mâ‚ƒ).map ((F.Î¼ mâ‚ mâ‚‚).app X) =
  (F.Î¼ mâ‚‚ mâ‚ƒ).app ((F.obj mâ‚).obj X) â‰« (F.Î¼ mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).app X â‰«
    (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).inv).app X â‰« (F.Î¼_iso (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).inv.app X :=
begin
  rw [â† associativity_app_assoc],
  dsimp,
  simp,
  dsimp,
  simp,
end

@[reassoc, simp]
lemma obj_Î¼_inv_app (mâ‚ mâ‚‚ mâ‚ƒ : M) (X : C) :
  (F.obj mâ‚ƒ).map ((F.Î¼_iso mâ‚ mâ‚‚).inv.app X) =
  (F.Î¼ (mâ‚ âŠ— mâ‚‚) mâ‚ƒ).app X â‰« (F.map (Î±_ mâ‚ mâ‚‚ mâ‚ƒ).hom).app X â‰«
  (F.Î¼_iso mâ‚ (mâ‚‚ âŠ— mâ‚ƒ)).inv.app X â‰«
  (F.Î¼_iso mâ‚‚ mâ‚ƒ).inv.app ((F.obj mâ‚).obj X) :=
begin
  rw â† is_iso.inv_eq_inv,
  convert obj_Î¼_app F mâ‚ mâ‚‚ mâ‚ƒ X using 1,
  { ext, rw â† functor.map_comp, simp },
  { simp only [monoidal_functor.Î¼_iso_hom, category.assoc, nat_iso.inv_inv_app, is_iso.inv_comp],
    congr,
    { ext, simp },
    { ext, simpa } }
end

@[simp, reassoc]
lemma obj_zero_map_Î¼_app {m : M} {X Y : C} (f : X âŸ¶ (F.obj m).obj Y) :
  (F.obj (ğŸ™_M)).map f â‰« (F.Î¼ m (ğŸ™_M)).app _ =
    F.Îµ_iso.inv.app _ â‰« f â‰« (F.map (Ï_ m).inv).app _ :=
begin
  rw [â† is_iso.inv_comp_eq, â† is_iso.comp_inv_eq],
  simp,
end

@[simp]
lemma obj_Î¼_zero_app (mâ‚ mâ‚‚ : M) (X : C) :
  (F.obj mâ‚‚).map ((F.Î¼ mâ‚ (ğŸ™_M)).app X) =
  (F.Î¼ (ğŸ™_M) mâ‚‚).app ((F.obj mâ‚).obj X) â‰« (F.map (Î»_ mâ‚‚).hom).app ((F.obj mâ‚).obj X) â‰«
    (F.obj mâ‚‚).map ((F.map (Ï_ mâ‚).inv).app X) :=
begin
  rw [â† obj_Îµ_inv_app_assoc, â† functor.map_comp],
  congr, simp,
end

/-- If `m âŠ— n â‰… ğŸ™_M`, then `F.obj m` is a left inverse of `F.obj n`. -/
@[simps] noncomputable
def unit_of_tensor_iso_unit (m n : M) (h : m âŠ— n â‰… ğŸ™_M) : F.obj m â‹™ F.obj n â‰… ğŸ­ C :=
F.Î¼_iso m n â‰ªâ‰« F.to_functor.map_iso h â‰ªâ‰« F.Îµ_iso.symm

/-- If `m âŠ— n â‰… ğŸ™_M` and `n âŠ— m â‰… ğŸ™_M` (subject to some commuting constraints),
  then `F.obj m` and `F.obj n` forms a self-equivalence of `C`. -/
@[simps] noncomputable
def equiv_of_tensor_iso_unit (m n : M) (hâ‚ : m âŠ— n â‰… ğŸ™_M) (hâ‚‚ : n âŠ— m â‰… ğŸ™_M)
  (H : (hâ‚.hom âŠ— ğŸ™ m) â‰« (Î»_ m).hom = (Î±_ m n m).hom â‰« (ğŸ™ m âŠ— hâ‚‚.hom) â‰« (Ï_ m).hom) : C â‰Œ C :=
{ functor := F.obj m,
  inverse := F.obj n,
  unit_iso := (unit_of_tensor_iso_unit F m n hâ‚).symm,
  counit_iso := unit_of_tensor_iso_unit F n m hâ‚‚,
  functor_unit_iso_comp' :=
  begin
    intro X,
    dsimp,
    simp only [Î¼_naturalityáµ£_assoc, Î¼_naturalityâ‚—_assoc, Îµ_inv_app_obj, category.assoc,
      obj_Î¼_inv_app, functor.map_comp, Î¼_inv_hom_app_assoc, obj_Îµ_app,
      unit_of_tensor_iso_unit_inv_app],
    simp [â† nat_trans.comp_app, â† F.to_functor.map_comp, â† H, - functor.map_comp]
  end }

end category_theory
