/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.monoidal.natural_transformation
import category_theory.monoidal.discrete

/-!
# Braided and symmetric monoidal categories

The basic definitions of braided monoidal categories, and symmetric monoidal categories,
as well as braided functors.

## Implementation note

We make `braided_monoidal_category` another typeclass, but then have `symmetric_monoidal_category`
extend this. The rationale is that we are not carrying any additional data,
just requiring a property.

## Future work

* Construct the Drinfeld center of a monoidal category as a braided monoidal category.
* Say something about pseudo-natural transformations.

-/

open category_theory

universes v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ

namespace category_theory

/--
A braided monoidal category is a monoidal category equipped with a braiding isomorphism
`Î²_ X Y : X âŠ— Y â‰… Y âŠ— X`
which is natural in both arguments,
and also satisfies the two hexagon identities.
-/
class braided_category (C : Type u) [category.{v} C] [monoidal_category.{v} C] :=
-- braiding natural iso:
(braiding             : Î  X Y : C, X âŠ— Y â‰… Y âŠ— X)
(braiding_naturality' : âˆ€ {X X' Y Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y'),
  (f âŠ— g) â‰« (braiding Y Y').hom = (braiding X X').hom â‰« (g âŠ— f) . obviously)
-- hexagon identities:
(hexagon_forward'     : Î  X Y Z : C,
    (Î±_ X Y Z).hom â‰« (braiding X (Y âŠ— Z)).hom â‰« (Î±_ Y Z X).hom
  = ((braiding X Y).hom âŠ— (ğŸ™ Z)) â‰« (Î±_ Y X Z).hom â‰« ((ğŸ™ Y) âŠ— (braiding X Z).hom)
  . obviously)
(hexagon_reverse'     : Î  X Y Z : C,
    (Î±_ X Y Z).inv â‰« (braiding (X âŠ— Y) Z).hom â‰« (Î±_ Z X Y).inv
  = ((ğŸ™ X) âŠ— (braiding Y Z).hom) â‰« (Î±_ X Z Y).inv â‰« ((braiding X Z).hom âŠ— (ğŸ™ Y))
  . obviously)

restate_axiom braided_category.braiding_naturality'
attribute [simp,reassoc] braided_category.braiding_naturality
restate_axiom braided_category.hexagon_forward'
restate_axiom braided_category.hexagon_reverse'

open category
open monoidal_category
open braided_category

notation `Î²_` := braiding

section
/-!
We now establish how the braiding interacts with the unitors.

I couldn't find a detailed proof in print, but this is discussed in:

* Proposition 1 of AndrÃ© Joyal and Ross Street,
  "Braided monoidal categories", Macquarie Math Reports 860081 (1986).
* Proposition 2.1 of AndrÃ© Joyal and Ross Street,
  "Braided tensor categories" , Adv. Math. 102 (1993), 20â€“78.
* Exercise 8.1.6 of Etingof, Gelaki, Nikshych, Ostrik,
  "Tensor categories", vol 25, Mathematical Surveys and Monographs (2015), AMS.
-/

variables (C : Type uâ‚) [category.{vâ‚} C] [monoidal_category C] [braided_category C]

lemma braiding_left_unitor_auxâ‚ (X : C) :
  (Î±_ (ğŸ™_ C) (ğŸ™_ C) X).hom â‰« (ğŸ™ _ âŠ— (Î²_ X (ğŸ™_ C)).inv) â‰« (Î±_ _ X _).inv â‰« ((Î»_ X).hom âŠ— ğŸ™ _) =
  ((Î»_ _).hom âŠ— ğŸ™ X) â‰« (Î²_ X _).inv :=
by { rw [â†left_unitor_tensor, left_unitor_naturality], simp, }

lemma braiding_left_unitor_auxâ‚‚ (X : C) :
  ((Î²_ X (ğŸ™_ C)).hom âŠ— (ğŸ™ (ğŸ™_ C))) â‰« ((Î»_ X).hom âŠ— (ğŸ™ (ğŸ™_ C))) = (Ï_ X).hom âŠ— (ğŸ™ (ğŸ™_ C)) :=
calc ((Î²_ X (ğŸ™_ C)).hom âŠ— (ğŸ™ (ğŸ™_ C))) â‰« ((Î»_ X).hom âŠ— (ğŸ™ (ğŸ™_ C)))
    = ((Î²_ X (ğŸ™_ C)).hom âŠ— (ğŸ™ (ğŸ™_ C))) â‰« (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv â‰«
        ((Î»_ X).hom âŠ— (ğŸ™ (ğŸ™_ C)))
         : by simp
... = ((Î²_ X (ğŸ™_ C)).hom âŠ— (ğŸ™ (ğŸ™_ C))) â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î²_ X _).hom) â‰«
        (ğŸ™ _ âŠ— (Î²_ X _).inv) â‰« (Î±_ _ _ _).inv â‰« ((Î»_ X).hom âŠ— (ğŸ™ (ğŸ™_ C)))
         : by { slice_rhs 3 4 { rw [â†id_tensor_comp, iso.hom_inv_id, tensor_id], }, rw [id_comp], }
... = (Î±_ _ _ _).hom â‰« (Î²_ _ _).hom â‰«
        (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î²_ X _).inv) â‰« (Î±_ _ _ _).inv â‰« ((Î»_ X).hom âŠ— (ğŸ™ (ğŸ™_ C)))
         : by { slice_lhs 1 3 { rw â†hexagon_forward }, simp only [assoc], }
... = (Î±_ _ _ _).hom â‰« (Î²_ _ _).hom â‰« ((Î»_ _).hom âŠ— ğŸ™ X) â‰« (Î²_ X _).inv
         : by rw braiding_left_unitor_auxâ‚
... = (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î»_ _).hom) â‰« (Î²_ _ _).hom â‰« (Î²_ X _).inv
         : by { slice_lhs 2 3 { rw [â†braiding_naturality] }, simp only [assoc], }
... = (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— (Î»_ _).hom)
         : by rw [iso.hom_inv_id, comp_id]
... = (Ï_ X).hom âŠ— (ğŸ™ (ğŸ™_ C))
         : by rw triangle

@[simp]
lemma braiding_left_unitor (X : C) : (Î²_ X (ğŸ™_ C)).hom â‰« (Î»_ X).hom = (Ï_ X).hom :=
by rw [â†tensor_right_iff, comp_tensor_id, braiding_left_unitor_auxâ‚‚]

lemma braiding_right_unitor_auxâ‚ (X : C) :
  (Î±_ X (ğŸ™_ C) (ğŸ™_ C)).inv â‰« ((Î²_ (ğŸ™_ C) X).inv âŠ— ğŸ™ _) â‰« (Î±_ _ X _).hom â‰« (ğŸ™ _ âŠ— (Ï_ X).hom) =
  (ğŸ™ X âŠ— (Ï_ _).hom) â‰« (Î²_ _ X).inv :=
by { rw [â†right_unitor_tensor, right_unitor_naturality], simp, }

lemma braiding_right_unitor_auxâ‚‚ (X : C) :
  ((ğŸ™ (ğŸ™_ C)) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« ((ğŸ™ (ğŸ™_ C)) âŠ— (Ï_ X).hom) = (ğŸ™ (ğŸ™_ C)) âŠ— (Î»_ X).hom :=
calc ((ğŸ™ (ğŸ™_ C)) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« ((ğŸ™ (ğŸ™_ C)) âŠ— (Ï_ X).hom)
    = ((ğŸ™ (ğŸ™_ C)) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« (Î±_ _ _ _).inv â‰« (Î±_ _ _ _).hom â‰«
        ((ğŸ™ (ğŸ™_ C)) âŠ— (Ï_ X).hom)
         : by simp
... = ((ğŸ™ (ğŸ™_ C)) âŠ— (Î²_ (ğŸ™_ C) X).hom) â‰« (Î±_ _ _ _).inv â‰« ((Î²_ _ X).hom âŠ— ğŸ™ _) â‰«
        ((Î²_ _ X).inv âŠ— ğŸ™ _) â‰« (Î±_ _ _ _).hom â‰« ((ğŸ™ (ğŸ™_ C)) âŠ— (Ï_ X).hom)
         : by { slice_rhs 3 4 { rw [â†comp_tensor_id, iso.hom_inv_id, tensor_id], }, rw [id_comp], }
... = (Î±_ _ _ _).inv â‰« (Î²_ _ _).hom â‰«
        (Î±_ _ _ _).inv â‰« ((Î²_ _ X).inv âŠ— ğŸ™ _) â‰« (Î±_ _ _ _).hom â‰« ((ğŸ™ (ğŸ™_ C)) âŠ— (Ï_ X).hom)
         : by { slice_lhs 1 3 { rw â†hexagon_reverse }, simp only [assoc], }
... = (Î±_ _ _ _).inv â‰« (Î²_ _ _).hom â‰« (ğŸ™ X âŠ— (Ï_ _).hom) â‰« (Î²_ _ X).inv
         : by rw braiding_right_unitor_auxâ‚
... = (Î±_ _ _ _).inv â‰« ((Ï_ _).hom âŠ— ğŸ™ _) â‰« (Î²_ _ X).hom â‰« (Î²_ _ _).inv
         : by { slice_lhs 2 3 { rw [â†braiding_naturality] }, simp only [assoc], }
... = (Î±_ _ _ _).inv â‰« ((Ï_ _).hom âŠ— ğŸ™ _)
         : by rw [iso.hom_inv_id, comp_id]
... = (ğŸ™ (ğŸ™_ C)) âŠ— (Î»_ X).hom
         : by rw [triangle_assoc_comp_right]

@[simp]
lemma braiding_right_unitor (X : C) : (Î²_ (ğŸ™_ C) X).hom â‰« (Ï_ X).hom = (Î»_ X).hom :=
by rw [â†tensor_left_iff, id_tensor_comp, braiding_right_unitor_auxâ‚‚]

end

/--
A symmetric monoidal category is a braided monoidal category for which the braiding is symmetric.

See https://stacks.math.columbia.edu/tag/0FFW.
-/
class symmetric_category (C : Type u) [category.{v} C] [monoidal_category.{v} C]
   extends braided_category.{v} C :=
-- braiding symmetric:
(symmetry' : âˆ€ X Y : C, (Î²_ X Y).hom â‰« (Î²_ Y X).hom = ğŸ™ (X âŠ— Y) . obviously)

restate_axiom symmetric_category.symmetry'
attribute [simp,reassoc] symmetric_category.symmetry

variables (C : Type uâ‚) [category.{vâ‚} C] [monoidal_category C] [braided_category C]
variables (D : Type uâ‚‚) [category.{vâ‚‚} D] [monoidal_category D] [braided_category D]
variables (E : Type uâ‚ƒ) [category.{vâ‚ƒ} E] [monoidal_category E] [braided_category E]

/--
A lax braided functor between braided monoidal categories is a lax monoidal functor
which preserves the braiding.
-/
structure lax_braided_functor extends lax_monoidal_functor C D :=
(braided' : âˆ€ X Y : C, Î¼ X Y â‰« map (Î²_ X Y).hom = (Î²_ (obj X) (obj Y)).hom â‰« Î¼ Y X . obviously)

restate_axiom lax_braided_functor.braided'

namespace lax_braided_functor

/-- The identity lax braided monoidal functor. -/
@[simps] def id : lax_braided_functor C C :=
{ .. monoidal_functor.id C }

instance : inhabited (lax_braided_functor C C) := âŸ¨id CâŸ©

variables {C D E}

/-- The composition of lax braided monoidal functors. -/
@[simps]
def comp (F : lax_braided_functor C D) (G : lax_braided_functor D E) : lax_braided_functor C E :=
{ braided' := Î» X Y,
  begin
    dsimp,
    slice_lhs 2 3 { rw [â†category_theory.functor.map_comp, F.braided,
      category_theory.functor.map_comp], },
    slice_lhs 1 2 { rw [G.braided], },
    simp only [category.assoc],
  end,
  ..(lax_monoidal_functor.comp F.to_lax_monoidal_functor G.to_lax_monoidal_functor) }

instance category_lax_braided_functor : category (lax_braided_functor C D) :=
induced_category.category lax_braided_functor.to_lax_monoidal_functor

@[simp] lemma comp_to_nat_trans {F G H : lax_braided_functor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
  (Î± â‰« Î²).to_nat_trans =
    @category_struct.comp (C â¥¤ D) _ _ _ _ (Î±.to_nat_trans) (Î².to_nat_trans) := rfl

/--
Interpret a natural isomorphism of the underlyling lax monoidal functors as an
isomorphism of the lax braided monoidal functors.
-/
@[simps]
def mk_iso {F G : lax_braided_functor C D}
  (i : F.to_lax_monoidal_functor â‰… G.to_lax_monoidal_functor) : F â‰… G :=
{ ..i }

end lax_braided_functor

/--
A braided functor between braided monoidal categories is a monoidal functor
which preserves the braiding.
-/
structure braided_functor extends monoidal_functor C D :=
-- Note this is stated differently than for `lax_braided_functor`.
-- We move the `Î¼ X Y` to the right hand side,
-- so that this makes a good `@[simp]` lemma.
(braided' :
  âˆ€ X Y : C, map (Î²_ X Y).hom = inv (Î¼ X Y) â‰« (Î²_ (obj X) (obj Y)).hom â‰« Î¼ Y X . obviously)

restate_axiom braided_functor.braided'
attribute [simp] braided_functor.braided

namespace braided_functor

/-- Turn a braided functor into a lax braided functor. -/
@[simps]
def to_lax_braided_functor (F : braided_functor C D) : lax_braided_functor C D :=
{ braided' := Î» X Y, by { rw F.braided, simp, }
  .. F }

/-- The identity braided monoidal functor. -/
@[simps] def id : braided_functor C C :=
{ .. monoidal_functor.id C }

instance : inhabited (braided_functor C C) := âŸ¨id CâŸ©

variables {C D E}

/-- The composition of braided monoidal functors. -/
@[simps]
def comp (F : braided_functor C D) (G : braided_functor D E) : braided_functor C E :=
{ ..(monoidal_functor.comp F.to_monoidal_functor G.to_monoidal_functor) }

instance category_braided_functor : category (braided_functor C D) :=
induced_category.category braided_functor.to_monoidal_functor

@[simp] lemma comp_to_nat_trans {F G H : braided_functor C D} {Î± : F âŸ¶ G} {Î² : G âŸ¶ H} :
  (Î± â‰« Î²).to_nat_trans =
    @category_struct.comp (C â¥¤ D) _ _ _ _ (Î±.to_nat_trans) (Î².to_nat_trans) := rfl

/--
Interpret a natural isomorphism of the underlyling monoidal functors as an
isomorphism of the braided monoidal functors.
-/
@[simps]
def mk_iso {F G : braided_functor C D}
  (i : F.to_monoidal_functor â‰… G.to_monoidal_functor) : F â‰… G :=
{ ..i }


end braided_functor

section comm_monoid

variables (M : Type u) [comm_monoid M]

instance comm_monoid_discrete : comm_monoid (discrete M) := by { dsimp [discrete], apply_instance }

instance : braided_category (discrete M) :=
{ braiding := Î» X Y, eq_to_iso (mul_comm X Y), }

variables {M} {N : Type u} [comm_monoid N]

/--
A multiplicative morphism between commutative monoids gives a braided functor between
the corresponding discrete braided monoidal categories.
-/
@[simps]
def discrete.braided_functor (F : M â†’* N) : braided_functor (discrete M) (discrete N) :=
{ ..discrete.monoidal_functor F }

end comm_monoid

end category_theory
