/-
Copyright (c) 2018 Michael Jendrusch. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Jendrusch, Scott Morrison, Bhavik Mehta, Jakob von Raumer
-/
import category_theory.products.basic

/-!
# Monoidal categories

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A monoidal category is a category equipped with a tensor product, unitors, and an associator.
In the definition, we provide the tensor product as a pair of functions
* `tensor_obj : C â†’ C â†’ C`
* `tensor_hom : (Xâ‚ âŸ¶ Yâ‚) â†’ (Xâ‚‚ âŸ¶ Yâ‚‚) â†’ ((Xâ‚ âŠ— Xâ‚‚) âŸ¶ (Yâ‚ âŠ— Yâ‚‚))`
and allow use of the overloaded notation `âŠ—` for both.
The unitors and associator are provided componentwise.

The tensor product can be expressed as a functor via `tensor : C Ã— C â¥¤ C`.
The unitors and associator are gathered together as natural
isomorphisms in `left_unitor_nat_iso`, `right_unitor_nat_iso` and `associator_nat_iso`.

Some consequences of the definition are proved in other files,
e.g. `(Î»_ (ğŸ™_ C)).hom = (Ï_ (ğŸ™_ C)).hom` in `category_theory.monoidal.unitors_equal`.

## Implementation
Dealing with unitors and associators is painful, and at this stage we do not have a useful
implementation of coherence for monoidal categories.

In an effort to lessen the pain, we put some effort into choosing the right `simp` lemmas.
Generally, the rule is that the component index of a natural transformation "weighs more"
in considering the complexity of an expression than does a structural isomorphism (associator, etc).

As an example when we prove Proposition 2.2.4 of
<http://www-math.mit.edu/~etingof/egnobookfinal.pdf>
we state it as a `@[simp]` lemma as
```
(Î»_ (X âŠ— Y)).hom = (Î±_ (ğŸ™_ C) X Y).inv â‰« (Î»_ X).hom âŠ— (ğŸ™ Y)
```

This is far from completely effective, but seems to prove a useful principle.

## References
* Tensor categories, Etingof, Gelaki, Nikshych, Ostrik,
  http://www-math.mit.edu/~etingof/egnobookfinal.pdf
* <https://stacks.math.columbia.edu/tag/0FFK>.
-/

open category_theory

universes v u

open category_theory
open category_theory.category
open category_theory.iso

namespace category_theory

/--
In a monoidal category, we can take the tensor product of objects, `X âŠ— Y` and of morphisms `f âŠ— g`.
Tensor product does not need to be strictly associative on objects, but there is a
specified associator, `Î±_ X Y Z : (X âŠ— Y) âŠ— Z â‰… X âŠ— (Y âŠ— Z)`. There is a tensor unit `ğŸ™_ C`,
with specified left and right unitor isomorphisms `Î»_ X : ğŸ™_ C âŠ— X â‰… X` and `Ï_ X : X âŠ— ğŸ™_ C â‰… X`.
These associators and unitors satisfy the pentagon and triangle equations.

See <https://stacks.math.columbia.edu/tag/0FFK>.
-/
class monoidal_category (C : Type u) [ğ’ : category.{v} C] :=
-- curried tensor product of objects:
(tensor_obj               : C â†’ C â†’ C)
(infixr (name := tensor_obj) ` âŠ— `:70 := tensor_obj) -- This notation is only temporary
-- curried tensor product of morphisms:
(tensor_hom               :
  Î  {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C}, (Xâ‚ âŸ¶ Yâ‚) â†’ (Xâ‚‚ âŸ¶ Yâ‚‚) â†’ ((Xâ‚ âŠ— Xâ‚‚) âŸ¶ (Yâ‚ âŠ— Yâ‚‚)))
(infixr ` âŠ—' `:69         := tensor_hom) -- This notation is only temporary
-- tensor product laws:
(tensor_id'               :
  âˆ€ (Xâ‚ Xâ‚‚ : C), (ğŸ™ Xâ‚) âŠ—' (ğŸ™ Xâ‚‚) = ğŸ™ (Xâ‚ âŠ— Xâ‚‚) . obviously)
(tensor_comp'             :
  âˆ€ {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚),
  (fâ‚ â‰« gâ‚) âŠ—' (fâ‚‚ â‰« gâ‚‚) = (fâ‚ âŠ—' fâ‚‚) â‰« (gâ‚ âŠ—' gâ‚‚) . obviously)
-- tensor unit:
(tensor_unit []           : C)
(notation `ğŸ™_`            := tensor_unit)
-- associator:
(associator               :
  Î  X Y Z : C, (X âŠ— Y) âŠ— Z â‰… X âŠ— (Y âŠ— Z))
(notation `Î±_`            := associator)
(associator_naturality'   :
  âˆ€ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ),
  ((fâ‚ âŠ—' fâ‚‚) âŠ—' fâ‚ƒ) â‰« (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (fâ‚ âŠ—' (fâ‚‚ âŠ—' fâ‚ƒ)) . obviously)
-- left unitor:
(left_unitor              : Î  X : C, ğŸ™_ âŠ— X â‰… X)
(notation `Î»_`            := left_unitor)
(left_unitor_naturality'  :
  âˆ€ {X Y : C} (f : X âŸ¶ Y), ((ğŸ™ ğŸ™_) âŠ—' f) â‰« (Î»_ Y).hom = (Î»_ X).hom â‰« f . obviously)
-- right unitor:
(right_unitor             : Î  X : C, X âŠ— ğŸ™_ â‰… X)
(notation `Ï_`            := right_unitor)
(right_unitor_naturality' :
  âˆ€ {X Y : C} (f : X âŸ¶ Y), (f âŠ—' (ğŸ™ ğŸ™_)) â‰« (Ï_ Y).hom = (Ï_ X).hom â‰« f . obviously)
-- pentagon identity:
(pentagon'                : âˆ€ W X Y Z : C,
  ((Î±_ W X Y).hom âŠ—' (ğŸ™ Z)) â‰« (Î±_ W (X âŠ— Y) Z).hom â‰« ((ğŸ™ W) âŠ—' (Î±_ X Y Z).hom)
  = (Î±_ (W âŠ— X) Y Z).hom â‰« (Î±_ W X (Y âŠ— Z)).hom . obviously)
-- triangle identity:
(triangle'                :
  âˆ€ X Y : C, (Î±_ X ğŸ™_ Y).hom â‰« ((ğŸ™ X) âŠ—' (Î»_ Y).hom) = (Ï_ X).hom âŠ—' (ğŸ™ Y) . obviously)

restate_axiom monoidal_category.tensor_id'
attribute [simp] monoidal_category.tensor_id
restate_axiom monoidal_category.tensor_comp'
attribute [reassoc] monoidal_category.tensor_comp -- This would be redundant in the simp set.
attribute [simp] monoidal_category.tensor_comp
restate_axiom monoidal_category.associator_naturality'
attribute [reassoc] monoidal_category.associator_naturality
restate_axiom monoidal_category.left_unitor_naturality'
attribute [reassoc] monoidal_category.left_unitor_naturality
restate_axiom monoidal_category.right_unitor_naturality'
attribute [reassoc] monoidal_category.right_unitor_naturality
restate_axiom monoidal_category.pentagon'
restate_axiom monoidal_category.triangle'
attribute [reassoc] monoidal_category.pentagon
attribute [simp, reassoc] monoidal_category.triangle

open monoidal_category

infixr (name := tensor_obj) ` âŠ— `:70 := tensor_obj
infixr (name := tensor_hom) ` âŠ— `:70 := tensor_hom

notation `ğŸ™_` := tensor_unit
notation `Î±_` := associator
notation `Î»_` := left_unitor
notation `Ï_` := right_unitor

/-- The tensor product of two isomorphisms is an isomorphism. -/
@[simps]
def tensor_iso {C : Type u} {X Y X' Y' : C} [category.{v} C] [monoidal_category.{v} C]
  (f : X â‰… Y) (g : X' â‰… Y') :
    X âŠ— X' â‰… Y âŠ— Y' :=
{ hom := f.hom âŠ— g.hom,
  inv := f.inv âŠ— g.inv,
  hom_inv_id' := by rw [â†tensor_comp, iso.hom_inv_id, iso.hom_inv_id, â†tensor_id],
  inv_hom_id' := by rw [â†tensor_comp, iso.inv_hom_id, iso.inv_hom_id, â†tensor_id] }

infixr (name := tensor_iso) ` âŠ— `:70 := tensor_iso

namespace monoidal_category

section

variables {C : Type u} [category.{v} C] [monoidal_category.{v} C]

instance tensor_is_iso {W X Y Z : C} (f : W âŸ¶ X) [is_iso f] (g : Y âŸ¶ Z) [is_iso g] :
  is_iso (f âŠ— g) :=
is_iso.of_iso (as_iso f âŠ— as_iso g)

@[simp] lemma inv_tensor {W X Y Z : C} (f : W âŸ¶ X) [is_iso f] (g : Y âŸ¶ Z) [is_iso g] :
  inv (f âŠ— g) = inv f âŠ— inv g :=
by { ext, simp [â†tensor_comp], }

variables {U V W X Y Z : C}

lemma tensor_dite {P : Prop} [decidable P]
  {W X Y Z : C} (f : W âŸ¶ X) (g : P â†’ (Y âŸ¶ Z)) (g' : Â¬P â†’ (Y âŸ¶ Z)) :
  f âŠ— (if h : P then g h else g' h) = if h : P then f âŠ— g h else f âŠ— g' h :=
by { split_ifs; refl }

lemma dite_tensor {P : Prop} [decidable P]
  {W X Y Z : C} (f : W âŸ¶ X) (g : P â†’ (Y âŸ¶ Z)) (g' : Â¬P â†’ (Y âŸ¶ Z)) :
  (if h : P then g h else g' h) âŠ— f  = if h : P then g h âŠ— f else g' h âŠ— f :=
by { split_ifs; refl }

@[reassoc, simp] lemma comp_tensor_id (f : W âŸ¶ X) (g : X âŸ¶ Y) :
  (f â‰« g) âŠ— (ğŸ™ Z) = (f âŠ— (ğŸ™ Z)) â‰« (g âŠ— (ğŸ™ Z)) :=
by { rw â†tensor_comp, simp }

@[reassoc, simp] lemma id_tensor_comp (f : W âŸ¶ X) (g : X âŸ¶ Y) :
  (ğŸ™ Z) âŠ— (f â‰« g) = (ğŸ™ Z âŠ— f) â‰« (ğŸ™ Z âŠ— g) :=
by { rw â†tensor_comp, simp }

@[simp, reassoc] lemma id_tensor_comp_tensor_id (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
  ((ğŸ™ Y) âŠ— f) â‰« (g âŠ— (ğŸ™ X)) = g âŠ— f :=
by { rw [â†tensor_comp], simp }

@[simp, reassoc] lemma tensor_id_comp_id_tensor (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
  (g âŠ— (ğŸ™ W)) â‰« ((ğŸ™ Z) âŠ— f) = g âŠ— f :=
by { rw [â†tensor_comp], simp }

@[simp]
lemma right_unitor_conjugation {X Y : C} (f : X âŸ¶ Y) :
  (f âŠ— (ğŸ™ (ğŸ™_ C))) = (Ï_ X).hom â‰« f â‰« (Ï_ Y).inv :=
by rw [â†right_unitor_naturality_assoc, iso.hom_inv_id, category.comp_id]

@[simp]
lemma left_unitor_conjugation {X Y : C} (f : X âŸ¶ Y) :
  ((ğŸ™ (ğŸ™_ C)) âŠ— f) = (Î»_ X).hom â‰« f â‰« (Î»_ Y).inv :=
by rw [â†left_unitor_naturality_assoc, iso.hom_inv_id, category.comp_id]

@[reassoc]
lemma left_unitor_inv_naturality {X X' : C} (f : X âŸ¶ X') :
  f â‰« (Î»_ X').inv = (Î»_ X).inv â‰« (ğŸ™ _ âŠ— f) :=
by simp

@[reassoc]
lemma right_unitor_inv_naturality {X X' : C} (f : X âŸ¶ X') :
  f â‰« (Ï_ X').inv = (Ï_ X).inv â‰« (f âŠ— ğŸ™ _) :=
by simp

lemma tensor_left_iff
  {X Y : C} (f g : X âŸ¶ Y) :
  ((ğŸ™ (ğŸ™_ C)) âŠ— f = (ğŸ™ (ğŸ™_ C)) âŠ— g) â†” (f = g) :=
by simp

lemma tensor_right_iff
  {X Y : C} (f g : X âŸ¶ Y) :
  (f âŠ— (ğŸ™ (ğŸ™_ C)) = g âŠ— (ğŸ™ (ğŸ™_ C))) â†” (f = g) :=
by simp

/-! The lemmas in the next section are true by coherence,
but we prove them directly as they are used in proving the coherence theorem. -/
section

@[reassoc]
lemma pentagon_inv (W X Y Z : C) :
  ((ğŸ™ W) âŠ— (Î±_ X Y Z).inv) â‰« (Î±_ W (X âŠ— Y) Z).inv â‰« ((Î±_ W X Y).inv âŠ— (ğŸ™ Z))
    = (Î±_ W X (Y âŠ— Z)).inv â‰« (Î±_ (W âŠ— X) Y Z).inv :=
category_theory.eq_of_inv_eq_inv (by simp [pentagon])

@[reassoc, simp]
lemma right_unitor_tensor (X Y : C) :
  (Ï_ (X âŠ— Y)).hom = (Î±_ X Y (ğŸ™_ C)).hom â‰« ((ğŸ™ X) âŠ— (Ï_ Y).hom) :=
by
  rw [â†tensor_right_iff, comp_tensor_id, â†cancel_mono (Î±_ X Y (ğŸ™_ C)).hom, assoc,
      associator_naturality, â†triangle_assoc, â†triangle, id_tensor_comp, pentagon_assoc,
      â†associator_naturality, tensor_id]

@[reassoc, simp]
lemma right_unitor_tensor_inv (X Y : C) :
  ((Ï_ (X âŠ— Y)).inv) = ((ğŸ™ X) âŠ— (Ï_ Y).inv) â‰« (Î±_ X Y (ğŸ™_ C)).inv :=
eq_of_inv_eq_inv (by simp)

@[simp, reassoc] lemma triangle_assoc_comp_right (X Y : C) :
  (Î±_ X (ğŸ™_ C) Y).inv â‰« ((Ï_ X).hom âŠ— ğŸ™ Y) = ((ğŸ™ X) âŠ— (Î»_ Y).hom) :=
by rw [â†triangle, iso.inv_hom_id_assoc]

@[simp, reassoc] lemma triangle_assoc_comp_left_inv (X Y : C) :
  ((ğŸ™ X) âŠ— (Î»_ Y).inv) â‰« (Î±_ X (ğŸ™_ C) Y).inv = ((Ï_ X).inv âŠ— ğŸ™ Y) :=
begin
  apply (cancel_mono ((Ï_ X).hom âŠ— ğŸ™ Y)).1,
  simp only [triangle_assoc_comp_right, assoc],
  rw [â†id_tensor_comp, iso.inv_hom_id, â†comp_tensor_id, iso.inv_hom_id]
end

end

@[reassoc]
lemma associator_inv_naturality {X Y Z X' Y' Z' : C} (f : X âŸ¶ X') (g : Y âŸ¶ Y') (h : Z âŸ¶ Z') :
  (f âŠ— (g âŠ— h)) â‰« (Î±_ X' Y' Z').inv = (Î±_ X Y Z).inv â‰« ((f âŠ— g) âŠ— h) :=
by { rw [comp_inv_eq, assoc, associator_naturality], simp }

@[reassoc, simp]
lemma associator_conjugation {X X' Y Y' Z Z' : C} (f : X âŸ¶ X') (g : Y âŸ¶ Y') (h : Z âŸ¶ Z') :
  (f âŠ— g) âŠ— h = (Î±_ X Y Z).hom â‰« (f âŠ— (g âŠ— h)) â‰« (Î±_ X' Y' Z').inv :=
by rw [associator_inv_naturality, hom_inv_id_assoc]

@[reassoc]
lemma associator_inv_conjugation {X X' Y Y' Z Z' : C} (f : X âŸ¶ X') (g : Y âŸ¶ Y') (h : Z âŸ¶ Z') :
  f âŠ— g âŠ— h = (Î±_ X Y Z).inv â‰« ((f âŠ— g) âŠ— h) â‰« (Î±_ X' Y' Z').hom :=
by rw [associator_naturality, inv_hom_id_assoc]

-- TODO these next two lemmas aren't so fundamental, and perhaps could be removed
-- (replacing their usages by their proofs).
@[reassoc]
lemma id_tensor_associator_naturality {X Y Z Z' : C} (h : Z âŸ¶ Z') :
  (ğŸ™ (X âŠ— Y) âŠ— h) â‰« (Î±_ X Y Z').hom = (Î±_ X Y Z).hom â‰« (ğŸ™ X âŠ— (ğŸ™ Y âŠ— h)) :=
by { rw [â†tensor_id, associator_naturality], }

@[reassoc]
lemma id_tensor_associator_inv_naturality {X Y Z X' : C} (f : X âŸ¶ X')  :
  (f âŠ— ğŸ™ (Y âŠ— Z)) â‰« (Î±_ X' Y Z).inv = (Î±_ X Y Z).inv â‰« ((f âŠ— ğŸ™ Y) âŠ— ğŸ™ Z) :=
by { rw [â†tensor_id, associator_inv_naturality] }

@[simp, reassoc]
lemma hom_inv_id_tensor {V W X Y Z : C} (f : V â‰… W) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (f.hom âŠ— g) â‰« (f.inv âŠ— h) = (ğŸ™ V âŠ— g) â‰« (ğŸ™ V âŠ— h) :=
by rw [â†tensor_comp, f.hom_inv_id, id_tensor_comp]

@[simp, reassoc]
lemma inv_hom_id_tensor {V W X Y Z : C} (f : V â‰… W) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (f.inv âŠ— g) â‰« (f.hom âŠ— h) = (ğŸ™ W âŠ— g) â‰« (ğŸ™ W âŠ— h) :=
by rw [â†tensor_comp, f.inv_hom_id, id_tensor_comp]

@[simp, reassoc]
lemma tensor_hom_inv_id {V W X Y Z : C} (f : V â‰… W) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (g âŠ— f.hom) â‰« (h âŠ— f.inv) = (g âŠ— ğŸ™ V) â‰« (h âŠ— ğŸ™ V) :=
by rw [â†tensor_comp, f.hom_inv_id, comp_tensor_id]

@[simp, reassoc]
lemma tensor_inv_hom_id {V W X Y Z : C} (f : V â‰… W) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (g âŠ— f.inv) â‰« (h âŠ— f.hom) = (g âŠ— ğŸ™ W) â‰« (h âŠ— ğŸ™ W) :=
by rw [â†tensor_comp, f.inv_hom_id, comp_tensor_id]

@[simp, reassoc]
lemma hom_inv_id_tensor' {V W X Y Z : C} (f : V âŸ¶ W) [is_iso f] (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (f âŠ— g) â‰« (inv f âŠ— h) = (ğŸ™ V âŠ— g) â‰« (ğŸ™ V âŠ— h) :=
by rw [â†tensor_comp, is_iso.hom_inv_id, id_tensor_comp]

@[simp, reassoc]
lemma inv_hom_id_tensor' {V W X Y Z : C} (f : V âŸ¶ W) [is_iso f] (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (inv f âŠ— g) â‰« (f âŠ— h) = (ğŸ™ W âŠ— g) â‰« (ğŸ™ W âŠ— h) :=
by rw [â†tensor_comp, is_iso.inv_hom_id, id_tensor_comp]

@[simp, reassoc]
lemma tensor_hom_inv_id' {V W X Y Z : C} (f : V âŸ¶ W) [is_iso f] (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (g âŠ— f) â‰« (h âŠ— inv f) = (g âŠ— ğŸ™ V) â‰« (h âŠ— ğŸ™ V) :=
by rw [â†tensor_comp, is_iso.hom_inv_id, comp_tensor_id]

@[simp, reassoc]
lemma tensor_inv_hom_id' {V W X Y Z : C} (f : V âŸ¶ W) [is_iso f] (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :
  (g âŠ— inv f) â‰« (h âŠ— f) = (g âŠ— ğŸ™ W) â‰« (h âŠ— ğŸ™ W) :=
by rw [â†tensor_comp, is_iso.inv_hom_id, comp_tensor_id]

end

section
variables (C : Type u) [category.{v} C] [monoidal_category.{v} C]

/-- The tensor product expressed as a functor. -/
@[simps] def tensor : (C Ã— C) â¥¤ C :=
{ obj := Î» X, X.1 âŠ— X.2,
  map := Î» {X Y : C Ã— C} (f : X âŸ¶ Y), f.1 âŠ— f.2 }

/-- The left-associated triple tensor product as a functor. -/
def left_assoc_tensor : (C Ã— C Ã— C) â¥¤ C :=
{ obj := Î» X, (X.1 âŠ— X.2.1) âŠ— X.2.2,
  map := Î» {X Y : C Ã— C Ã— C} (f : X âŸ¶ Y), (f.1 âŠ— f.2.1) âŠ— f.2.2 }

@[simp] lemma left_assoc_tensor_obj (X) :
  (left_assoc_tensor C).obj X = (X.1 âŠ— X.2.1) âŠ— X.2.2 := rfl
@[simp] lemma left_assoc_tensor_map {X Y} (f : X âŸ¶ Y) :
  (left_assoc_tensor C).map f = (f.1 âŠ— f.2.1) âŠ— f.2.2 := rfl

/-- The right-associated triple tensor product as a functor. -/
def right_assoc_tensor : (C Ã— C Ã— C) â¥¤ C :=
{ obj := Î» X, X.1 âŠ— (X.2.1 âŠ— X.2.2),
  map := Î» {X Y : C Ã— C Ã— C} (f : X âŸ¶ Y), f.1 âŠ— (f.2.1 âŠ— f.2.2) }

@[simp] lemma right_assoc_tensor_obj (X) :
  (right_assoc_tensor C).obj X = X.1 âŠ— (X.2.1 âŠ— X.2.2) := rfl
@[simp] lemma right_assoc_tensor_map {X Y} (f : X âŸ¶ Y) :
  (right_assoc_tensor C).map f = f.1 âŠ— (f.2.1 âŠ— f.2.2) := rfl

/-- The functor `Î» X, ğŸ™_ C âŠ— X`. -/
def tensor_unit_left : C â¥¤ C :=
{ obj := Î» X, ğŸ™_ C âŠ— X,
  map := Î» {X Y : C} (f : X âŸ¶ Y), (ğŸ™ (ğŸ™_ C)) âŠ— f }
/-- The functor `Î» X, X âŠ— ğŸ™_ C`. -/
def tensor_unit_right : C â¥¤ C :=
{ obj := Î» X, X âŠ— ğŸ™_ C,
  map := Î» {X Y : C} (f : X âŸ¶ Y), f âŠ— (ğŸ™ (ğŸ™_ C)) }

-- We can express the associator and the unitors, given componentwise above,
-- as natural isomorphisms.

/-- The associator as a natural isomorphism. -/
@[simps]
def associator_nat_iso :
  left_assoc_tensor C â‰… right_assoc_tensor C :=
nat_iso.of_components
  (by { intros, apply monoidal_category.associator })
  (by { intros, apply monoidal_category.associator_naturality })

/-- The left unitor as a natural isomorphism. -/
@[simps]
def left_unitor_nat_iso :
  tensor_unit_left C â‰… ğŸ­ C :=
nat_iso.of_components
  (by { intros, apply monoidal_category.left_unitor })
  (by { intros, apply monoidal_category.left_unitor_naturality })

/-- The right unitor as a natural isomorphism. -/
@[simps]
def right_unitor_nat_iso :
  tensor_unit_right C â‰… ğŸ­ C :=
nat_iso.of_components
  (by { intros, apply monoidal_category.right_unitor })
  (by { intros, apply monoidal_category.right_unitor_naturality })



section
variables {C}

/-- Tensoring on the left with a fixed object, as a functor. -/
@[simps]
def tensor_left (X : C) : C â¥¤ C :=
{ obj := Î» Y, X âŠ— Y,
  map := Î» Y Y' f, (ğŸ™ X) âŠ— f, }

/--
Tensoring on the left with `X âŠ— Y` is naturally isomorphic to
tensoring on the left with `Y`, and then again with `X`.
-/
def tensor_left_tensor (X Y : C) : tensor_left (X âŠ— Y) â‰… tensor_left Y â‹™ tensor_left X :=
nat_iso.of_components
  (associator _ _)
  (Î» Z Z' f, by { dsimp, rw[â†tensor_id], apply associator_naturality })

@[simp] lemma tensor_left_tensor_hom_app (X Y Z : C) :
  (tensor_left_tensor X Y).hom.app Z = (associator X Y Z).hom :=
rfl
@[simp] lemma tensor_left_tensor_inv_app (X Y Z : C) :
  (tensor_left_tensor X Y).inv.app Z = (associator X Y Z).inv :=
by { simp [tensor_left_tensor], }

/-- Tensoring on the right with a fixed object, as a functor. -/
@[simps]
def tensor_right (X : C) : C â¥¤ C :=
{ obj := Î» Y, Y âŠ— X,
  map := Î» Y Y' f, f âŠ— (ğŸ™ X), }

variables (C)

/--
Tensoring on the left, as a functor from `C` into endofunctors of `C`.

TODO: show this is a op-monoidal functor.
-/
@[simps]
def tensoring_left : C â¥¤ C â¥¤ C :=
{ obj := tensor_left,
  map := Î» X Y f,
  { app := Î» Z, f âŠ— (ğŸ™ Z) } }

instance : faithful (tensoring_left C) :=
{ map_injective' := Î» X Y f g h,
  begin
    injections with h,
    replace h := congr_fun h (ğŸ™_ C),
    simpa using h,
  end }

/--
Tensoring on the right, as a functor from `C` into endofunctors of `C`.

We later show this is a monoidal functor.
-/
@[simps]
def tensoring_right : C â¥¤ C â¥¤ C :=
{ obj := tensor_right,
  map := Î» X Y f,
  { app := Î» Z, (ğŸ™ Z) âŠ— f } }

instance : faithful (tensoring_right C) :=
{ map_injective' := Î» X Y f g h,
  begin
    injections with h,
    replace h := congr_fun h (ğŸ™_ C),
    simpa using h,
  end }

variables {C}

/--
Tensoring on the right with `X âŠ— Y` is naturally isomorphic to
tensoring on the right with `X`, and then again with `Y`.
-/
def tensor_right_tensor (X Y : C) : tensor_right (X âŠ— Y) â‰… tensor_right X â‹™ tensor_right Y :=
nat_iso.of_components
  (Î» Z, (associator Z X Y).symm)
  (Î» Z Z' f, by { dsimp, rw[â†tensor_id], apply associator_inv_naturality })

@[simp] lemma tensor_right_tensor_hom_app (X Y Z : C) :
  (tensor_right_tensor X Y).hom.app Z = (associator Z X Y).inv :=
rfl
@[simp] lemma tensor_right_tensor_inv_app (X Y Z : C) :
  (tensor_right_tensor X Y).inv.app Z = (associator Z X Y).hom :=
by simp [tensor_right_tensor]

end

end

section

universes vâ‚ vâ‚‚ uâ‚ uâ‚‚

variables (Câ‚ : Type uâ‚) [category.{vâ‚} Câ‚] [monoidal_category.{vâ‚} Câ‚]
variables (Câ‚‚ : Type uâ‚‚) [category.{vâ‚‚} Câ‚‚] [monoidal_category.{vâ‚‚} Câ‚‚]

local attribute [simp]
associator_naturality left_unitor_naturality right_unitor_naturality pentagon

@[simps tensor_obj tensor_hom tensor_unit associator]
instance prod_monoidal : monoidal_category (Câ‚ Ã— Câ‚‚) :=
{ tensor_obj := Î» X Y, (X.1 âŠ— Y.1, X.2 âŠ— Y.2),
  tensor_hom := Î» _ _ _ _ f g, (f.1 âŠ— g.1, f.2 âŠ— g.2),
  tensor_unit := (ğŸ™_ Câ‚, ğŸ™_ Câ‚‚),
  associator := Î» X Y Z, (Î±_ X.1 Y.1 Z.1).prod (Î±_ X.2 Y.2 Z.2),
  left_unitor := Î» âŸ¨Xâ‚, Xâ‚‚âŸ©, (Î»_ Xâ‚).prod (Î»_ Xâ‚‚),
  right_unitor := Î» âŸ¨Xâ‚, Xâ‚‚âŸ©, (Ï_ Xâ‚).prod (Ï_ Xâ‚‚) }

@[simp] lemma prod_monoidal_left_unitor_hom_fst (X : Câ‚ Ã— Câ‚‚) :
  ((Î»_ X).hom : (ğŸ™_ _) âŠ— X âŸ¶ X).1 = (Î»_ X.1).hom := by { cases X, refl }

@[simp] lemma prod_monoidal_left_unitor_hom_snd (X : Câ‚ Ã— Câ‚‚) :
  ((Î»_ X).hom : (ğŸ™_ _) âŠ— X âŸ¶ X).2 = (Î»_ X.2).hom := by { cases X, refl }

@[simp] lemma prod_monoidal_left_unitor_inv_fst (X : Câ‚ Ã— Câ‚‚) :
  ((Î»_ X).inv : X âŸ¶ (ğŸ™_ _) âŠ— X).1 = (Î»_ X.1).inv := by { cases X, refl }

@[simp] lemma prod_monoidal_left_unitor_inv_snd (X : Câ‚ Ã— Câ‚‚) :
  ((Î»_ X).inv : X âŸ¶ (ğŸ™_ _) âŠ— X).2 = (Î»_ X.2).inv := by { cases X, refl }

@[simp] lemma prod_monoidal_right_unitor_hom_fst (X : Câ‚ Ã— Câ‚‚) :
  ((Ï_ X).hom : X âŠ— (ğŸ™_ _) âŸ¶ X).1 = (Ï_ X.1).hom := by { cases X, refl }

@[simp] lemma prod_monoidal_right_unitor_hom_snd (X : Câ‚ Ã— Câ‚‚) :
  ((Ï_ X).hom : X âŠ— (ğŸ™_ _) âŸ¶ X).2 = (Ï_ X.2).hom := by { cases X, refl }

@[simp] lemma prod_monoidal_right_unitor_inv_fst (X : Câ‚ Ã— Câ‚‚) :
  ((Ï_ X).inv : X âŸ¶ X âŠ— (ğŸ™_ _)).1 = (Ï_ X.1).inv := by { cases X, refl }

@[simp] lemma prod_monoidal_right_unitor_inv_snd (X : Câ‚ Ã— Câ‚‚) :
  ((Ï_ X).inv : X âŸ¶ X âŠ— (ğŸ™_ _)).2 = (Ï_ X.2).inv := by { cases X, refl }

end

end monoidal_category

end category_theory
