-- Copyright (c) 2018 Michael Jendrusch. All rights reserved.
-- Released under Apache 2.0 license as described in the file LICENSE.
-- Authors: Michael Jendrusch, Scott Morrison
import category_theory.monoidal.tensor_product
import category_theory.natural_isomorphism
import tactic -- TODO switch this back to tactic.basic once relevant PRs land.
import tactic.slice

open category_theory
open tactic

universes v u

open category_theory
open category_theory.category
open category_theory.iso
open category_theory.monoidal

namespace category_theory.monoidal
class monoidal_category (C : Sort u) extends category.{v} C :=
-- curried tensor product of objects:
(tensor_obj               : C â†’ C â†’ C)
-- curried tensor product of morphisms:
(tensor_hom               :
  Î  {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C}, hom Xâ‚ Yâ‚ â†’ hom Xâ‚‚ Yâ‚‚ â†’ hom (tensor_obj Xâ‚ Xâ‚‚) (tensor_obj Yâ‚ Yâ‚‚))
-- tensor product laws:
(tensor_id'               :
  âˆ€ (Xâ‚ Xâ‚‚ : C), tensor_hom (ğŸ™ Xâ‚) (ğŸ™ Xâ‚‚) = ğŸ™ (tensor_obj Xâ‚ Xâ‚‚) . obviously)
(tensor_comp'             :
  âˆ€ {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚),
  tensor_hom (fâ‚ â‰« gâ‚) (fâ‚‚ â‰« gâ‚‚) = (tensor_hom fâ‚ fâ‚‚) â‰« (tensor_hom gâ‚ gâ‚‚) . obviously)
-- tensor unit:
(tensor_unit              : C)
-- associator:
(associator               :
  Î  X Y Z : C, (tensor_obj (tensor_obj X Y) Z) â‰… (tensor_obj X (tensor_obj Y Z)))
(associator_naturality'   :
  assoc_natural tensor_obj @tensor_hom associator . obviously)
-- left unitor:
(left_unitor              : Î  X : C, tensor_obj tensor_unit X â‰… X)
(left_unitor_naturality'  :
  left_unitor_natural tensor_obj @tensor_hom tensor_unit left_unitor . obviously)
-- right unitor:
(right_unitor             : Î  X : C, tensor_obj X tensor_unit â‰… X)
(right_unitor_naturality' :
  right_unitor_natural tensor_obj @tensor_hom tensor_unit right_unitor . obviously)
-- pentagon identity:
(pentagon'                : pentagon @tensor_hom associator . obviously)
-- triangle identity:
(triangle'                :
  triangle @tensor_hom left_unitor right_unitor associator . obviously)

restate_axiom monoidal_category.tensor_id'
attribute [simp] monoidal_category.tensor_id
restate_axiom monoidal_category.tensor_comp'
attribute [simp] monoidal_category.tensor_comp
restate_axiom monoidal_category.associator_naturality'
restate_axiom monoidal_category.left_unitor_naturality'
restate_axiom monoidal_category.right_unitor_naturality'
restate_axiom monoidal_category.pentagon'
restate_axiom monoidal_category.triangle'
attribute [simp] monoidal_category.triangle

open monoidal_category

infixr ` âŠ— `:70 := tensor_obj
infixr ` âŠ— `:70 := tensor_hom

def tensor_iso {C : Sort u} {X Y X' Y' : C} [monoidal_category.{v} C] (f : X â‰… Y) (g : X' â‰… Y') :
    X âŠ— X' â‰… Y âŠ— Y' :=
{ hom := f.hom âŠ— g.hom,
  inv := f.inv âŠ— g.inv,
  hom_inv_id' := by rw [â†tensor_comp, iso.hom_inv_id, iso.hom_inv_id, â†tensor_id],
  inv_hom_id' := by rw [â†tensor_comp, iso.inv_hom_id, iso.inv_hom_id, â†tensor_id] }

infixr ` âŠ— `:70 := tensor_iso

section

variables {C : Sort u} [ğ’ : monoidal_category.{v} C]
include ğ’

instance tensor_is_iso {W X Y Z : C} (f : W âŸ¶ X) [is_iso f] (g : Y âŸ¶ Z) [is_iso g] : is_iso (f âŠ— g) :=
{ ..(as_iso f âŠ— as_iso g) }

@[simp] lemma inv_tensor {W X Y Z : C} (f : W âŸ¶ X) [is_iso f] (g : Y âŸ¶ Z) [is_iso g] :
  inv (f âŠ— g) = inv f âŠ— inv g := rfl

variables {U V W X Y Z : C}

-- When `rewrite_search` lands, add @[search] attributes to

-- monoidal_category.tensor_id monoidal_category.tensor_comp monoidal_category.associator_naturality
-- monoidal_category.left_unitor_naturality monoidal_category.right_unitor_naturality
-- monoidal_category.pentagon monoidal_category.triangle

-- interchange interchange_left_identity interchange_right_identity interchange_identities
-- triangle_1 triangle_2 triangle_3 triangle_4
-- left_unitor_product left_unitor_product_inv
-- right_unitor_product right_unitor_product_inv
-- pentagon_inv
-- associator_inv_naturality
-- left_unitor_inv_naturality
-- right_unitor_inv_naturality

@[simp] lemma tensor_comp_id (f : W âŸ¶ X) (g : X âŸ¶ Y) :
  (f â‰« g) âŠ— (ğŸ™ Z) = (f âŠ— (ğŸ™ Z)) â‰« (g âŠ— (ğŸ™ Z)) :=
by { rw â†tensor_comp, simp }

@[simp] lemma tensor_id_comp (f : W âŸ¶ X) (g : X âŸ¶ Y) :
  (ğŸ™ Z) âŠ— (f â‰« g) = (ğŸ™ Z âŠ— f) â‰« (ğŸ™ Z âŠ— g) :=
by { rw â†tensor_comp, simp }

lemma comp_id_tensor_tensor_id (f : W âŸ¶ X) (g : Y âŸ¶ Z) :
  ((ğŸ™ Y) âŠ— f) â‰« (g âŠ— (ğŸ™ X)) = (g âŠ— (ğŸ™ W)) â‰« ((ğŸ™ Z) âŠ— f) :=
by { rw [â†tensor_comp, â†tensor_comp], simp }

lemma left_unitor_inv_naturality {X X' : C} (f : X âŸ¶ X') :
  f â‰« (left_unitor X').inv = (left_unitor X).inv â‰« (ğŸ™ _ âŠ— f) :=
begin
  apply (cancel_mono (left_unitor X').hom).1,
  simp only [assoc, comp_id, iso.inv_hom_id],
  conv_rhs { congr, skip, rw [left_unitor_naturality] },
  conv_rhs { rw [â†category.assoc], congr, rw [iso.inv_hom_id] },
  conv_rhs { rw [category.id_comp] }
end

lemma right_unitor_inv_naturality {X X' : C} (f : X âŸ¶ X') :
  f â‰« (right_unitor X').inv = (right_unitor X).inv â‰« (f âŠ— ğŸ™ _) :=
begin
  apply (cancel_mono (right_unitor X').hom).1,
  simp only [assoc, comp_id, iso.inv_hom_id],
  conv_rhs { congr, skip, rw [right_unitor_naturality] },
  conv_rhs { rw [â†category.assoc], congr, rw [iso.inv_hom_id] },
  conv_rhs { rw [category.id_comp] }
end

@[simp] lemma tensor_left_iff
  {X Y : C} (f g : X âŸ¶ Y) :
  ((ğŸ™ (tensor_unit C)) âŠ— f = (ğŸ™ (tensor_unit C)) âŠ— g) â†” (f = g) :=
begin
  split,
  { intro h,
    have h' := congr_arg (Î» k, (left_unitor _).inv â‰« k) h,
    dsimp at h',
    rw â†left_unitor_inv_naturality at h',
    rw â†left_unitor_inv_naturality at h',
    exact (cancel_mono _).1 h', },
  { intro h, subst h, }
end

@[simp] lemma tensor_right_iff
  {X Y : C} (f g : X âŸ¶ Y) :
  (f âŠ— (ğŸ™ (tensor_unit C)) = g âŠ— (ğŸ™ (tensor_unit C))) â†” (f = g) :=
begin
  split,
  { intro h,
    have h' := congr_arg (Î» k, (right_unitor _).inv â‰« k) h,
    dsimp at h',
    rw â†right_unitor_inv_naturality at h',
    rw â†right_unitor_inv_naturality at h',
    exact (cancel_mono _).1 h' },
  { intro h, subst h, }
end

-- We now prove:
--   ((associator (tensor_unit C) X Y).hom) â‰«
--     ((left_unitor (X âŠ— Y)).hom)
--   = ((left_unitor X).hom âŠ— (ğŸ™ Y))
-- (and the corresponding fact for right unitors)
-- following the proof on nLab.

lemma left_unitor_product_aux_perimeter (X Y : C) :
    ((associator (tensor_unit C) (tensor_unit C) X).hom âŠ— (ğŸ™ Y)) â‰«
    (associator (tensor_unit C) ((tensor_unit C) âŠ— X) Y).hom â‰«
    ((ğŸ™ (tensor_unit C)) âŠ— (associator (tensor_unit C) X Y).hom) â‰«
    ((ğŸ™ (tensor_unit C)) âŠ— (left_unitor (X âŠ— Y)).hom)
  = (((right_unitor (tensor_unit C)).hom âŠ— (ğŸ™ X)) âŠ— (ğŸ™ Y)) â‰«
    (associator (tensor_unit C) X Y).hom :=
begin
  conv_lhs { congr, skip, rw [â†category.assoc] },
  conv_lhs { rw [â†category.assoc], congr, rw [monoidal_category.pentagon] },
  conv_rhs { rw [associator_naturality], congr, skip, congr, skip, rw [tensor_id] },
  conv_rhs { congr, skip, rw [â†monoidal_category.triangle] },
  conv_rhs { rw [â†category.assoc] }
end

lemma left_unitor_product_aux_triangle (X Y : C) :
    ((associator (tensor_unit C) (tensor_unit C) X).hom âŠ— (ğŸ™ Y)) â‰«
    (((ğŸ™ (tensor_unit C)) âŠ— (left_unitor X).hom) âŠ— (ğŸ™ Y))
  = ((right_unitor (tensor_unit C)).hom âŠ— (ğŸ™ X)) âŠ— (ğŸ™ Y) :=
begin
  conv_lhs { rw [â†tensor_comp_id] },
  conv_rhs { congr, rw [â†monoidal_category.triangle] }
end

lemma left_unitor_product_aux_square (X Y : C) :
    (associator (tensor_unit C) ((tensor_unit C) âŠ— X) Y).hom â‰«
    ((ğŸ™ (tensor_unit C)) âŠ— (left_unitor X).hom âŠ— (ğŸ™ Y))
  = (((ğŸ™ (tensor_unit C)) âŠ— (left_unitor X).hom) âŠ— (ğŸ™ Y)) â‰«
    (associator (tensor_unit C) X Y).hom :=
begin
  conv_rhs { rw [associator_naturality] }
end

lemma left_unitor_product_aux (X Y : C) :
    ((ğŸ™ (tensor_unit C)) âŠ— (associator (tensor_unit C) X Y).hom) â‰«
    ((ğŸ™ (tensor_unit C)) âŠ— (left_unitor (X âŠ— Y)).hom)
  = (ğŸ™ (tensor_unit C)) âŠ— ((left_unitor X).hom âŠ— (ğŸ™ Y)) :=
begin
  rw â†(cancel_epi (associator (tensor_unit C) ((tensor_unit C) âŠ— X) Y).hom),
  rw left_unitor_product_aux_square,
  rw â†(cancel_epi ((associator (tensor_unit C) (tensor_unit C) X).hom âŠ— (ğŸ™ Y))),
  slice_rhs 1 2 { rw left_unitor_product_aux_triangle },
  conv_lhs { rw [left_unitor_product_aux_perimeter] }
end

lemma right_unitor_product_aux_perimeter (X Y : C) :
    ((associator X Y (tensor_unit C)).hom âŠ— (ğŸ™ (tensor_unit C))) â‰«
    (associator X (Y âŠ— (tensor_unit C)) (tensor_unit C)).hom â‰«
    ((ğŸ™ X) âŠ— (associator Y (tensor_unit C) (tensor_unit C)).hom) â‰«
    ((ğŸ™ X) âŠ— (ğŸ™ Y) âŠ— (left_unitor (tensor_unit C)).hom)
  = ((right_unitor (X âŠ— Y)).hom âŠ— (ğŸ™ (tensor_unit C))) â‰«
    (associator X Y (tensor_unit C)).hom :=
begin
  transitivity (((associator X Y _).hom âŠ— ğŸ™ _) â‰« (associator X _ _).hom â‰«
    (ğŸ™ X âŠ— (associator Y _ _).hom)) â‰«
    (ğŸ™ X âŠ— ğŸ™ Y âŠ— (monoidal_category.left_unitor _).hom),
  { conv_lhs { congr, skip, rw [â†category.assoc] },
    conv_rhs { rw [category.assoc] } },
  { conv_lhs { congr, rw [monoidal_category.pentagon] },
    conv_rhs { congr, rw [â†monoidal_category.triangle] },
    conv_rhs { rw [category.assoc] },
    conv_rhs { congr, skip, congr, congr, rw [â†tensor_id] },
    conv_rhs { congr, skip, rw [associator_naturality] },
    conv_rhs { rw [â†category.assoc] } }
end

lemma right_unitor_product_aux_triangle (X Y : C) :
    ((ğŸ™ X) âŠ— (associator Y (tensor_unit C) (tensor_unit C)).hom) â‰«
    ((ğŸ™ X) âŠ— (ğŸ™ Y) âŠ— (left_unitor (tensor_unit C)).hom)
  = (ğŸ™ X) âŠ— (right_unitor Y).hom âŠ— (ğŸ™ (tensor_unit C)) :=
begin
  conv_lhs { rw [â†tensor_id_comp] },
  conv_rhs { congr, skip, rw [â†monoidal_category.triangle] }
end

lemma right_unitor_product_aux_square (X Y : C) :
    (associator X (Y âŠ— (tensor_unit C)) (tensor_unit C)).hom â‰«
    ((ğŸ™ X) âŠ— (right_unitor Y).hom âŠ— (ğŸ™ (tensor_unit C)))
  = (((ğŸ™ X) âŠ— (right_unitor Y).hom) âŠ— (ğŸ™ (tensor_unit C))) â‰«
    (associator X Y (tensor_unit C)).hom :=
begin
  conv_rhs { rw [associator_naturality] }
end

lemma right_unitor_product_aux (X Y : C) :
    ((associator X Y (tensor_unit C)).hom âŠ— (ğŸ™ (tensor_unit C))) â‰«
    (((ğŸ™ X) âŠ— (right_unitor Y).hom) âŠ— (ğŸ™ (tensor_unit C)))
  = ((right_unitor (X âŠ— Y)).hom âŠ— (ğŸ™ (tensor_unit C))) :=
begin
  rw â†(cancel_mono (associator X Y (tensor_unit C)).hom),
  slice_lhs 2 3 { rw â†right_unitor_product_aux_square },
  conv_lhs { congr, skip, congr, skip, rw [â†right_unitor_product_aux_triangle] },
  conv_rhs { rw [â†right_unitor_product_aux_perimeter] }
end

-- See Proposition 2.2.4 of http://www-math.mit.edu/~etingof/egnobookfinal.pdf
@[simp] lemma left_unitor_product (X Y : C) :
  ((associator (tensor_unit C) X Y).hom) â‰« ((left_unitor (X âŠ— Y)).hom) =
    ((left_unitor X).hom âŠ— (ğŸ™ Y)) :=
by rw [â†tensor_left_iff, tensor_id_comp, left_unitor_product_aux]

@[simp] lemma left_unitor_product_inv (X Y : C) :
  ((left_unitor (X âŠ— Y)).inv) â‰« ((associator (tensor_unit C) X Y).inv) =
    ((left_unitor X).inv âŠ— (ğŸ™ Y)) :=
eq_of_inv_eq_inv (by simp)

@[simp] lemma right_unitor_product (X Y : C) :
  ((associator X Y (tensor_unit C)).hom) â‰« ((ğŸ™ X) âŠ— (right_unitor Y).hom) =
    ((right_unitor (X âŠ— Y)).hom) :=
by rw [â†tensor_right_iff, tensor_comp_id, right_unitor_product_aux]

@[simp] lemma right_unitor_product_inv (X Y : C) :
  ((ğŸ™ X) âŠ— (right_unitor Y).inv) â‰« ((associator X Y (tensor_unit C)).inv) =
    ((right_unitor (X âŠ— Y)).inv) :=
eq_of_inv_eq_inv (by simp)

lemma associator_inv_naturality {X Y Z X' Y' Z' : C} (f : X âŸ¶ X') (g : Y âŸ¶ Y') (h : Z âŸ¶ Z') :
  (f âŠ— (g âŠ— h)) â‰« (associator X' Y' Z').inv = (associator X Y Z).inv â‰« ((f âŠ— g) âŠ— h) :=
begin
  apply (cancel_mono (associator X' Y' Z').hom).1,
  simp only [assoc, comp_id, iso.inv_hom_id],
  conv_rhs { congr, skip, rw [associator_naturality] },
  conv_rhs { rw [â†category.assoc], congr, rw [iso.inv_hom_id] },
  conv_rhs { rw [category.id_comp] }
end

lemma pentagon_inv (W X Y Z : C) :
  ((ğŸ™ W) âŠ— (associator X Y Z).inv) â‰« (associator W (X âŠ— Y) Z).inv â‰« ((associator W X Y).inv âŠ— (ğŸ™ Z))
    = (associator W X (Y âŠ— Z)).inv â‰« (associator (W âŠ— X) Y Z).inv :=
begin
  apply category_theory.eq_of_inv_eq_inv,
  dsimp,
  rw [category.assoc, monoidal_category.pentagon]
end

@[simp] lemma triangle_1 (X Y : C) :
  (associator X (tensor_unit C) Y).hom â‰« ((ğŸ™ X) âŠ— (left_unitor Y).hom) = (right_unitor X).hom âŠ— ğŸ™ Y :=
monoidal_category.triangle C X Y

@[simp] lemma triangle_2 (X Y : C) :
  (associator X (tensor_unit C) Y).inv â‰« ((right_unitor X).hom âŠ— ğŸ™ Y) = ((ğŸ™ X) âŠ— (left_unitor Y).hom) :=
begin
  conv_lhs { congr, skip, rw [â†triangle_1] },
  conv_lhs { rw [â†category.assoc], congr, rw [iso.inv_hom_id] },
  conv_lhs { rw [category.id_comp] }
end

@[simp] lemma triangle_3 (X Y : C) :
  ((right_unitor X).inv âŠ— ğŸ™ Y) â‰« (associator X (tensor_unit C) Y).hom = ((ğŸ™ X) âŠ— (left_unitor Y).inv) :=
begin
  apply (cancel_mono (ğŸ™ X âŠ— (left_unitor Y).hom)).1,
  simp only [assoc, triangle_1],
  conv_lhs { rw [â†tensor_comp_id, iso.inv_hom_id] },
  conv_rhs { rw [â†tensor_id_comp, iso.inv_hom_id] }
end

@[simp] lemma triangle_4 (X Y : C) :
  ((ğŸ™ X) âŠ— (left_unitor Y).inv) â‰« (associator X (tensor_unit C) Y).inv = ((right_unitor X).inv âŠ— ğŸ™ Y) :=
begin
  apply (cancel_mono ((right_unitor X).hom âŠ— ğŸ™ Y)).1,
  simp only [triangle_2, assoc],
  conv_lhs { rw [â†tensor_id_comp, iso.inv_hom_id] },
  conv_rhs { rw [â†tensor_comp_id, iso.inv_hom_id] }
end

end

namespace monoidal_category

-- In order to be able to describe the tensor product as a functor, we
-- need to be up in at least `Type 0` for both objects and morphisms,
-- so that we can construct products.
variables (C : Type u) [ğ’ : monoidal_category.{v+1} C]
include ğ’

def tensor : (C Ã— C) â¥¤ C :=
{ obj := Î» X, tensor_obj X.1 X.2,
  map := Î» {X Y : C Ã— C} (f : X âŸ¶ Y), tensor_hom f.1 f.2 }

def left_assoc_tensor : (C Ã— C Ã— C) â¥¤ C :=
{ obj := Î» X, (X.1 âŠ— X.2.1) âŠ— X.2.2,
  map := Î» {X Y : C Ã— C Ã— C} (f : X âŸ¶ Y), (f.1 âŠ— f.2.1) âŠ— f.2.2 }

@[simp] lemma left_assoc_tensor_obj (X) :
  (left_assoc_tensor C).obj X = (X.1 âŠ— X.2.1) âŠ— X.2.2 := rfl
@[simp] lemma left_assoc_tensor_map {X Y} (f : X âŸ¶ Y) :
  (left_assoc_tensor C).map f = (f.1 âŠ— f.2.1) âŠ— f.2.2 := rfl

def right_assoc_tensor : (C Ã— C Ã— C) â¥¤ C :=
{ obj := Î» X, X.1 âŠ— (X.2.1 âŠ— X.2.2),
  map := Î» {X Y : C Ã— C Ã— C} (f : X âŸ¶ Y), f.1 âŠ— (f.2.1 âŠ— f.2.2) }

@[simp] lemma right_assoc_tensor_obj (X) :
  (right_assoc_tensor C).obj X = X.1 âŠ— (X.2.1 âŠ— X.2.2) := rfl
@[simp] lemma right_assoc_tensor_map {X Y} (f : X âŸ¶ Y) :
  (right_assoc_tensor C).map f = f.1 âŠ— (f.2.1 âŠ— f.2.2) := rfl

def tensor_unit_left : C â¥¤ C :=
{ obj := Î» X, tensor_unit C âŠ— X,
  map := Î» {X Y : C} (f : X âŸ¶ Y), (ğŸ™ (tensor_unit C)) âŠ— f }
def tensor_unit_right : C â¥¤ C :=
{ obj := Î» X, X âŠ— tensor_unit C,
  map := Î» {X Y : C} (f : X âŸ¶ Y), f âŠ— (ğŸ™ (tensor_unit C)) }

-- We can express the associator and the unitors, given componentwise above,
-- as natural isomorphisms.

def associator_nat_iso :
  left_assoc_tensor C â‰… right_assoc_tensor C :=
nat_iso.of_components
  (by intros; dsimp; apply monoidal_category.associator)
  (by intros; dsimp; apply monoidal_category.associator_naturality)

def left_unitor_nat_iso :
  tensor_unit_left C â‰… functor.id C :=
nat_iso.of_components
  (by intros; dsimp; apply monoidal_category.left_unitor)
  (by intros; dsimp; apply monoidal_category.left_unitor_naturality)

def right_unitor_nat_iso :
  tensor_unit_right C â‰… functor.id C :=
nat_iso.of_components
  (by intros; dsimp; apply monoidal_category.right_unitor)
  (by intros; dsimp; apply monoidal_category.right_unitor_naturality)

end monoidal_category

end category_theory.monoidal
