/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.monoidal.braided
import category_theory.functor.reflects_isomorphisms
import category_theory.monoidal.coherence

/-!
# Half braidings and the Drinfeld center of a monoidal category

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define `center C` to be pairs `âŸ¨X, bâŸ©`, where `X : C` and `b` is a half-braiding on `X`.

We show that `center C` is braided monoidal,
and provide the monoidal functor `center.forget` from `center C` back to `C`.

## Future work

Verifying the various axioms here is done by tedious rewriting.
Using the `slice` tactic may make the proofs marginally more readable.

More exciting, however, would be to make possible one of the following options:
1. Integration with homotopy.io / globular to give "picture proofs".
2. The monoidal coherence theorem, so we can ignore associators
   (after which most of these proofs are trivial;
   I'm unsure if the monoidal coherence theorem is even usable in dependent type theory).
3. Automating these proofs using `rewrite_search` or some relative.

-/

open category_theory
open category_theory.monoidal_category

universes v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ
noncomputable theory

namespace category_theory

variables {C : Type uâ‚} [category.{vâ‚} C] [monoidal_category C]

/--
A half-braiding on `X : C` is a family of isomorphisms `X âŠ— U â‰… U âŠ— X`,
monoidally natural in `U : C`.

Thinking of `C` as a 2-category with a single `0`-morphism, these are the same as natural
transformations (in the pseudo- sense) of the identity 2-functor on `C`, which send the unique
`0`-morphism to `X`.
-/
@[nolint has_nonempty_instance]
structure half_braiding (X : C) :=
(Î² : Î  U, X âŠ— U â‰… U âŠ— X)
(monoidal' : âˆ€ U U', (Î² (U âŠ— U')).hom =
  (Î±_ _ _ _).inv â‰« ((Î² U).hom âŠ— ğŸ™ U') â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ U âŠ— (Î² U').hom) â‰« (Î±_ _ _ _).inv
  . obviously)
(naturality' : âˆ€ {U U'} (f : U âŸ¶ U'), (ğŸ™ X âŠ— f) â‰« (Î² U').hom = (Î² U).hom â‰« (f âŠ— ğŸ™ X) . obviously)

restate_axiom half_braiding.monoidal'
attribute [reassoc, simp] half_braiding.monoidal -- the reassoc lemma is redundant as a simp lemma
restate_axiom half_braiding.naturality'
attribute [simp, reassoc] half_braiding.naturality

variables (C)
/--
The Drinfeld center of a monoidal category `C` has as objects pairs `âŸ¨X, bâŸ©`, where `X : C`
and `b` is a half-braiding on `X`.
-/
@[nolint has_nonempty_instance]
def center := Î£ X : C, half_braiding X

namespace center

variables {C}

/-- A morphism in the Drinfeld center of `C`. -/
@[ext, nolint has_nonempty_instance]
structure hom (X Y : center C) :=
(f : X.1 âŸ¶ Y.1)
(comm' : âˆ€ U, (f âŠ— ğŸ™ U) â‰« (Y.2.Î² U).hom = (X.2.Î² U).hom â‰« (ğŸ™ U âŠ— f) . obviously)

restate_axiom hom.comm'
attribute [simp, reassoc] hom.comm

instance : category (center C) :=
{ hom := hom,
  id := Î» X, { f := ğŸ™ X.1, },
  comp := Î» X Y Z f g, { f := f.f â‰« g.f, }, }

@[simp] lemma id_f (X : center C) : hom.f (ğŸ™ X) = ğŸ™ X.1 := rfl
@[simp] lemma comp_f {X Y Z : center C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : (f â‰« g).f = f.f â‰« g.f := rfl

@[ext]
lemma ext {X Y : center C} (f g : X âŸ¶ Y) (w : f.f = g.f) : f = g :=
by { cases f, cases g, congr, exact w, }

/--
Construct an isomorphism in the Drinfeld center from
a morphism whose underlying morphism is an isomorphism.
-/
@[simps]
def iso_mk {X Y : center C} (f : X âŸ¶ Y) [is_iso f.f] : X â‰… Y :=
{ hom := f,
  inv := âŸ¨inv f.f, Î» U, by simp [â†cancel_epi (f.f âŠ— ğŸ™ U), â†comp_tensor_id_assoc, â†id_tensor_comp]âŸ© }

instance is_iso_of_f_is_iso {X Y : center C} (f : X âŸ¶ Y) [is_iso f.f] : is_iso f :=
begin
  change is_iso (iso_mk f).hom,
  apply_instance,
end

/-- Auxiliary definition for the `monoidal_category` instance on `center C`. -/
@[simps]
def tensor_obj (X Y : center C) : center C :=
âŸ¨X.1 âŠ— Y.1,
  { Î² := Î» U, Î±_ _ _ _ â‰ªâ‰« (iso.refl X.1 âŠ— Y.2.Î² U) â‰ªâ‰« (Î±_ _ _ _).symm
      â‰ªâ‰« (X.2.Î² U âŠ— iso.refl Y.1) â‰ªâ‰« Î±_ _ _ _,
    monoidal' := Î» U U',
    begin
      dsimp,
      simp only [comp_tensor_id, id_tensor_comp, category.assoc, half_braiding.monoidal],
      -- On the RHS, we'd like to commute `((X.snd.Î² U).hom âŠ— ğŸ™ Y.fst) âŠ— ğŸ™ U'`
      -- and `ğŸ™ U âŠ— ğŸ™ X.fst âŠ— (Y.snd.Î² U').hom` past each other,
      -- but there are some associators we need to get out of the way first.
      slice_rhs 6 8 { rw pentagon, },
      slice_rhs 5 6 { rw associator_naturality, },
      slice_rhs 7 8 { rw â†associator_naturality, },
      slice_rhs 6 7 { rw [tensor_id, tensor_id, tensor_id_comp_id_tensor, â†id_tensor_comp_tensor_id,
        â†tensor_id, â†tensor_id], },
      -- Now insert associators as needed to make the four half-braidings look identical
      slice_rhs 10 10 { rw associator_inv_conjugation, },
      slice_rhs 7 7 { rw associator_inv_conjugation, },
      slice_rhs 6 6 { rw associator_conjugation, },
      slice_rhs 3 3 { rw associator_conjugation, },
      -- Finish with an application of the coherence theorem.
      coherence,
    end,
    naturality' := Î» U U' f,
    begin
      dsimp,
      rw [category.assoc, category.assoc, category.assoc, category.assoc,
        id_tensor_associator_naturality_assoc, â†id_tensor_comp_assoc, half_braiding.naturality,
        id_tensor_comp_assoc, associator_inv_naturality_assoc, â†comp_tensor_id_assoc,
        half_braiding.naturality, comp_tensor_id_assoc, associator_naturality, â†tensor_id],
    end, }âŸ©

/-- Auxiliary definition for the `monoidal_category` instance on `center C`. -/
@[simps]
def tensor_hom {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : center C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
  tensor_obj Xâ‚ Xâ‚‚ âŸ¶ tensor_obj Yâ‚ Yâ‚‚ :=
{ f := f.f âŠ— g.f,
  comm' := Î» U, begin
    dsimp,
    rw [category.assoc, category.assoc, category.assoc, category.assoc,
      associator_naturality_assoc, â†tensor_id_comp_id_tensor, category.assoc,
      â†id_tensor_comp_assoc, g.comm, id_tensor_comp_assoc, tensor_id_comp_id_tensor_assoc,
      â†id_tensor_comp_tensor_id, category.assoc, associator_inv_naturality_assoc,
      id_tensor_associator_inv_naturality_assoc, tensor_id,
      id_tensor_comp_tensor_id_assoc, â†tensor_id_comp_id_tensor g.f, category.assoc,
      â†comp_tensor_id_assoc, f.comm, comp_tensor_id_assoc, id_tensor_associator_naturality,
      associator_naturality_assoc, â†id_tensor_comp, tensor_id_comp_id_tensor],
  end }

/-- Auxiliary definition for the `monoidal_category` instance on `center C`. -/
@[simps]
def tensor_unit : center C :=
âŸ¨ğŸ™_ C,
  { Î² := Î» U, (Î»_ U) â‰ªâ‰« (Ï_ U).symm,
    monoidal' := Î» U U', by simp,
    naturality' := Î» U U' f, begin
      dsimp,
      rw [left_unitor_naturality_assoc, right_unitor_inv_naturality, category.assoc],
    end, }âŸ©

/-- Auxiliary definition for the `monoidal_category` instance on `center C`. -/
def associator (X Y Z : center C) : tensor_obj (tensor_obj X Y) Z â‰… tensor_obj X (tensor_obj Y Z) :=
iso_mk âŸ¨(Î±_ X.1 Y.1 Z.1).hom, Î» U, begin
  dsimp,
  simp only [comp_tensor_id, id_tensor_comp, â†tensor_id, associator_conjugation],
  coherence,
endâŸ©

/-- Auxiliary definition for the `monoidal_category` instance on `center C`. -/
def left_unitor (X : center C) : tensor_obj tensor_unit X â‰… X :=
iso_mk âŸ¨(Î»_ X.1).hom, Î» U, begin
  dsimp,
  simp only [category.comp_id, category.assoc, tensor_inv_hom_id, comp_tensor_id,
    tensor_id_comp_id_tensor, triangle_assoc_comp_right_inv],
  rw [â†left_unitor_tensor, left_unitor_naturality, left_unitor_tensor'_assoc],
endâŸ©

/-- Auxiliary definition for the `monoidal_category` instance on `center C`. -/
def right_unitor (X : center C) : tensor_obj X tensor_unit â‰… X :=
iso_mk âŸ¨(Ï_ X.1).hom, Î» U, begin
  dsimp,
  simp only [tensor_id_comp_id_tensor_assoc, triangle_assoc, id_tensor_comp, category.assoc],
  rw [â†tensor_id_comp_id_tensor_assoc (Ï_ U).inv, cancel_epi, â†right_unitor_tensor_inv_assoc,
    â†right_unitor_inv_naturality_assoc],
  simp,
endâŸ©

section
local attribute [simp] associator_naturality left_unitor_naturality right_unitor_naturality
  pentagon
local attribute [simp] center.associator center.left_unitor center.right_unitor

instance : monoidal_category (center C) :=
{ tensor_obj := Î» X Y, tensor_obj X Y,
  tensor_hom := Î» Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ f g, tensor_hom f g,
  tensor_unit := tensor_unit,
  associator := associator,
  left_unitor := left_unitor,
  right_unitor := right_unitor, }

@[simp] lemma tensor_fst (X Y : center C) : (X âŠ— Y).1 = X.1 âŠ— Y.1 := rfl

@[simp] lemma tensor_Î² (X Y : center C) (U : C) :
  (X âŠ— Y).2.Î² U =
    Î±_ _ _ _ â‰ªâ‰« (iso.refl X.1 âŠ— Y.2.Î² U) â‰ªâ‰« (Î±_ _ _ _).symm
      â‰ªâ‰« (X.2.Î² U âŠ— iso.refl Y.1) â‰ªâ‰« Î±_ _ _ _ :=
rfl
@[simp] lemma tensor_f {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : center C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
  (f âŠ— g).f = f.f âŠ— g.f :=
rfl

@[simp] lemma tensor_unit_Î² (U : C) : (ğŸ™_ (center C)).2.Î² U = (Î»_ U) â‰ªâ‰« (Ï_ U).symm := rfl

@[simp] lemma associator_hom_f (X Y Z : center C) : hom.f (Î±_ X Y Z).hom = (Î±_ X.1 Y.1 Z.1).hom :=
rfl

@[simp] lemma associator_inv_f (X Y Z : center C) : hom.f (Î±_ X Y Z).inv = (Î±_ X.1 Y.1 Z.1).inv :=
by { ext, rw [â†associator_hom_f, â†comp_f, iso.hom_inv_id], refl, }

@[simp] lemma left_unitor_hom_f (X : center C) : hom.f (Î»_ X).hom = (Î»_ X.1).hom :=
rfl

@[simp] lemma left_unitor_inv_f (X : center C) : hom.f (Î»_ X).inv = (Î»_ X.1).inv :=
by { ext, rw [â†left_unitor_hom_f, â†comp_f, iso.hom_inv_id], refl, }

@[simp] lemma right_unitor_hom_f (X : center C) : hom.f (Ï_ X).hom = (Ï_ X.1).hom :=
rfl

@[simp] lemma right_unitor_inv_f (X : center C) : hom.f (Ï_ X).inv = (Ï_ X.1).inv :=
by { ext, rw [â†right_unitor_hom_f, â†comp_f, iso.hom_inv_id], refl, }

end

section
variables (C)

/-- The forgetful monoidal functor from the Drinfeld center to the original category. -/
@[simps]
def forget : monoidal_functor (center C) C :=
{ obj := Î» X, X.1,
  map := Î» X Y f, f.f,
  Îµ := ğŸ™ (ğŸ™_ C),
  Î¼ := Î» X Y, ğŸ™ (X.1 âŠ— Y.1), }

instance : reflects_isomorphisms (forget C).to_functor :=
{ reflects := Î» A B f i, by { dsimp at i, resetI, change is_iso (iso_mk f).hom, apply_instance, } }

end

/-- Auxiliary definition for the `braided_category` instance on `center C`. -/
@[simps]
def braiding (X Y : center C) : X âŠ— Y â‰… Y âŠ— X :=
iso_mk âŸ¨(X.2.Î² Y.1).hom, Î» U, begin
  dsimp,
  simp only [category.assoc],
  rw [â†is_iso.inv_comp_eq, is_iso.iso.inv_hom, â†half_braiding.monoidal_assoc,
    â†half_braiding.naturality_assoc, half_braiding.monoidal],
  simp,
endâŸ©

instance braided_category_center : braided_category (center C) :=
{ braiding := braiding,
  braiding_naturality' := Î» X Y X' Y' f g, begin
    ext,
    dsimp,
    rw [â†tensor_id_comp_id_tensor, category.assoc, half_braiding.naturality, f.comm_assoc,
      id_tensor_comp_tensor_id],
  end, } -- `obviously` handles the hexagon axioms

section
variables [braided_category C]

open braided_category

/-- Auxiliary construction for `of_braided`. -/
@[simps]
def of_braided_obj (X : C) : center C :=
âŸ¨X, { Î² := Î» Y, Î²_ X Y,
  monoidal' := Î» U U', begin
    rw [iso.eq_inv_comp, â†category.assoc, â†category.assoc, iso.eq_comp_inv,
      category.assoc, category.assoc],
    exact hexagon_forward X U U',
  end }âŸ©

variables (C)

/--
The functor lifting a braided category to its center, using the braiding as the half-braiding.
-/
@[simps]
def of_braided : monoidal_functor C (center C) :=
{ obj := of_braided_obj,
  map := Î» X X' f,
  { f := f,
    comm' := Î» U, braiding_naturality _ _, },
  Îµ :=
  { f := ğŸ™ _,
    comm' := Î» U, begin
      dsimp,
      rw [tensor_id, category.id_comp, tensor_id, category.comp_id, â†braiding_right_unitor,
        category.assoc, iso.hom_inv_id, category.comp_id],
    end, },
  Î¼ := Î» X Y,
  { f := ğŸ™ _,
    comm' := Î» U, begin
      dsimp,
      rw [tensor_id, tensor_id, category.id_comp, category.comp_id,
        â†iso.inv_comp_eq, â†category.assoc, â†category.assoc, â†iso.comp_inv_eq,
        category.assoc, hexagon_reverse, category.assoc],
    end, }, }

end

end center

end category_theory
