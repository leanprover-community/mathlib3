/-
Copyright (c) 2022. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Yuma Mizuno, Oleksandr Manzyuk
-/
import category_theory.monoidal.free.coherence
import category_theory.bicategory.coherence_tactic

/-!
# A `coherence` tactic for monoidal categories, and `âŠ—â‰«` (composition up to associators)

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We provide a `coherence` tactic,
which proves equations where the two sides differ by replacing
strings of monoidal structural morphisms with other such strings.
(The replacements are always equalities by the monoidal coherence theorem.)

A simpler version of this tactic is `pure_coherence`,
which proves that any two morphisms (with the same source and target)
in a monoidal category which are built out of associators and unitors
are equal.

We also provide `f âŠ—â‰« g`, the `monoidal_comp` operation,
which automatically inserts associators and unitors as needed
to make the target of `f` match the source of `g`.
-/

noncomputable theory

universes v u

open category_theory
open category_theory.free_monoidal_category

variables {C : Type u} [category.{v} C] [monoidal_category C]

namespace category_theory.monoidal_category

/-- A typeclass carrying a choice of lift of an object from `C` to `free_monoidal_category C`. -/
class lift_obj (X : C) :=
(lift : free_monoidal_category C)

instance lift_obj_unit : lift_obj (ğŸ™_ C) := { lift := unit, }
instance lift_obj_tensor (X Y : C) [lift_obj X] [lift_obj Y] : lift_obj (X âŠ— Y) :=
{ lift := lift_obj.lift X âŠ— lift_obj.lift Y, }
@[priority 100]
instance lift_obj_of (X : C) : lift_obj X := { lift := of X, }

/-- A typeclass carrying a choice of lift of a morphism from `C` to `free_monoidal_category C`. -/
class lift_hom {X Y : C} [lift_obj X] [lift_obj Y] (f : X âŸ¶ Y) :=
(lift : lift_obj.lift X âŸ¶ lift_obj.lift Y)

instance lift_hom_id (X : C) [lift_obj X] : lift_hom (ğŸ™ X) :=
{ lift := ğŸ™ _, }
instance lift_hom_left_unitor_hom (X : C) [lift_obj X] : lift_hom (Î»_ X).hom :=
{ lift := (Î»_ (lift_obj.lift X)).hom, }
instance lift_hom_left_unitor_inv (X : C) [lift_obj X] : lift_hom (Î»_ X).inv :=
{ lift := (Î»_ (lift_obj.lift X)).inv, }
instance lift_hom_right_unitor_hom (X : C) [lift_obj X] : lift_hom (Ï_ X).hom :=
{ lift := (Ï_ (lift_obj.lift X)).hom, }
instance lift_hom_right_unitor_inv (X : C) [lift_obj X] : lift_hom (Ï_ X).inv :=
{ lift := (Ï_ (lift_obj.lift X)).inv, }
instance lift_hom_associator_hom (X Y Z : C) [lift_obj X] [lift_obj Y] [lift_obj Z] :
  lift_hom (Î±_ X Y Z).hom :=
{ lift := (Î±_ (lift_obj.lift X) (lift_obj.lift Y) (lift_obj.lift Z)).hom, }
instance lift_hom_associator_inv (X Y Z : C) [lift_obj X] [lift_obj Y] [lift_obj Z] :
  lift_hom (Î±_ X Y Z).inv :=
{ lift := (Î±_ (lift_obj.lift X) (lift_obj.lift Y) (lift_obj.lift Z)).inv, }
instance lift_hom_comp {X Y Z : C} [lift_obj X] [lift_obj Y] [lift_obj Z] (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  [lift_hom f] [lift_hom g] : lift_hom (f â‰« g) :=
{ lift := lift_hom.lift f â‰« lift_hom.lift g }
instance lift_hom_tensor {W X Y Z : C} [lift_obj W] [lift_obj X] [lift_obj Y] [lift_obj Z]
  (f : W âŸ¶ X) (g : Y âŸ¶ Z) [lift_hom f] [lift_hom g] : lift_hom (f âŠ— g) :=
{ lift := lift_hom.lift f âŠ— lift_hom.lift g }

/--
A typeclass carrying a choice of monoidal structural isomorphism between two objects.
Used by the `âŠ—â‰«` monoidal composition operator, and the `coherence` tactic.
-/
-- We could likely turn this into a `Prop` valued existential if that proves useful.
class monoidal_coherence (X Y : C) [lift_obj X] [lift_obj Y] :=
(hom [] : X âŸ¶ Y)
[is_iso : is_iso hom . tactic.apply_instance]

attribute [instance] monoidal_coherence.is_iso

namespace monoidal_coherence

@[simps]
instance refl (X : C) [lift_obj X] : monoidal_coherence X X := âŸ¨ğŸ™ _âŸ©

@[simps]
instance tensor (X Y Z : C) [lift_obj X] [lift_obj Y] [lift_obj Z] [monoidal_coherence Y Z] :
  monoidal_coherence (X âŠ— Y) (X âŠ— Z) :=
âŸ¨ğŸ™ X âŠ— monoidal_coherence.hom Y ZâŸ©

@[simps]
instance tensor_right (X Y : C) [lift_obj X] [lift_obj Y] [monoidal_coherence (ğŸ™_ C) Y] :
  monoidal_coherence X (X âŠ— Y) :=
âŸ¨(Ï_ X).inv â‰« (ğŸ™ X âŠ— monoidal_coherence.hom (ğŸ™_ C) Y)âŸ©

@[simps]
instance tensor_right' (X Y : C) [lift_obj X] [lift_obj Y] [monoidal_coherence Y (ğŸ™_ C)] :
  monoidal_coherence (X âŠ— Y) X :=
âŸ¨(ğŸ™ X âŠ— monoidal_coherence.hom Y (ğŸ™_ C)) â‰« (Ï_ X).homâŸ©

@[simps]
instance left (X Y : C) [lift_obj X] [lift_obj Y] [monoidal_coherence X Y] :
  monoidal_coherence (ğŸ™_ C âŠ— X) Y :=
âŸ¨(Î»_ X).hom â‰« monoidal_coherence.hom X YâŸ©

@[simps]
instance left' (X Y : C) [lift_obj X] [lift_obj Y] [monoidal_coherence X Y] :
  monoidal_coherence X (ğŸ™_ C âŠ— Y) :=
âŸ¨monoidal_coherence.hom X Y â‰« (Î»_ Y).invâŸ©

@[simps]
instance right (X Y : C) [lift_obj X] [lift_obj Y] [monoidal_coherence X Y] :
  monoidal_coherence (X âŠ— ğŸ™_ C) Y :=
âŸ¨(Ï_ X).hom â‰« monoidal_coherence.hom X YâŸ©

@[simps]
instance right' (X Y : C) [lift_obj X] [lift_obj Y] [monoidal_coherence X Y] :
  monoidal_coherence X (Y âŠ— ğŸ™_ C) :=
âŸ¨monoidal_coherence.hom X Y â‰« (Ï_ Y).invâŸ©

@[simps]
instance assoc (X Y Z W : C) [lift_obj W] [lift_obj X] [lift_obj Y] [lift_obj Z]
  [monoidal_coherence (X âŠ— (Y âŠ— Z)) W] : monoidal_coherence ((X âŠ— Y) âŠ— Z) W :=
âŸ¨(Î±_ X Y Z).hom â‰« monoidal_coherence.hom (X âŠ— (Y âŠ— Z)) WâŸ©

@[simps]
instance assoc' (W X Y Z : C) [lift_obj W] [lift_obj X] [lift_obj Y] [lift_obj Z]
  [monoidal_coherence W (X âŠ— (Y âŠ— Z))] : monoidal_coherence W ((X âŠ— Y) âŠ— Z) :=
âŸ¨monoidal_coherence.hom W (X âŠ— (Y âŠ— Z)) â‰« (Î±_ X Y Z).invâŸ©

end monoidal_coherence

/-- Construct an isomorphism between two objects in a monoidal category
out of unitors and associators. -/
def monoidal_iso (X Y : C) [lift_obj X] [lift_obj Y] [monoidal_coherence X Y] : X â‰… Y :=
as_iso (monoidal_coherence.hom X Y)

example (X : C) : X â‰… (X âŠ— (ğŸ™_ C âŠ— ğŸ™_ C)) := monoidal_iso _ _

example (X1 X2 X3 X4 X5 X6 X7 X8 X9 : C) :
  (ğŸ™_ C âŠ— (X1 âŠ— X2 âŠ— ((X3 âŠ— X4) âŠ— X5)) âŠ— X6 âŠ— (X7 âŠ— X8 âŠ— X9)) â‰…
  (X1 âŠ— (X2 âŠ— X3) âŠ— X4 âŠ— (X5 âŠ— (ğŸ™_ C âŠ— X6) âŠ— X7) âŠ— X8 âŠ— X9) :=
monoidal_iso _ _

/-- Compose two morphisms in a monoidal category,
inserting unitors and associators between as necessary. -/
def monoidal_comp {W X Y Z : C} [lift_obj X] [lift_obj Y]
  [monoidal_coherence X Y] (f : W âŸ¶ X) (g : Y âŸ¶ Z) : W âŸ¶ Z :=
f â‰« monoidal_coherence.hom X Y â‰« g

infixr ` âŠ—â‰« `:80 := monoidal_comp -- type as \ot \gg

/-- Compose two isomorphisms in a monoidal category,
inserting unitors and associators between as necessary. -/
def monoidal_iso_comp {W X Y Z : C} [lift_obj X] [lift_obj Y]
  [monoidal_coherence X Y] (f : W â‰… X) (g : Y â‰… Z) : W â‰… Z :=
f â‰ªâ‰« as_iso (monoidal_coherence.hom X Y) â‰ªâ‰« g

infixr ` â‰ªâŠ—â‰« `:80 := monoidal_iso_comp -- type as \ot \gg

example {U V W X Y : C} (f : U âŸ¶ V âŠ— (W âŠ— X)) (g : (V âŠ— W) âŠ— X âŸ¶ Y) : U âŸ¶ Y := f âŠ—â‰« g

-- To automatically insert unitors/associators at the beginning or end,
-- you can use `f âŠ—â‰« ğŸ™ _`
example {W X Y Z : C} (f : W âŸ¶ (X âŠ— Y) âŠ— Z) : W âŸ¶ X âŠ— (Y âŠ— Z) := f âŠ—â‰« ğŸ™ _

@[simp] lemma monoidal_comp_refl {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  f âŠ—â‰« g = f â‰« g :=
by { dsimp [monoidal_comp], simp, }

example {U V W X Y : C} (f : U âŸ¶ V âŠ— (W âŠ— X)) (g : (V âŠ— W) âŠ— X âŸ¶ Y) :
  f âŠ—â‰« g = f â‰« (Î±_ _ _ _).inv â‰« g :=
by simp [monoidal_comp]

end category_theory.monoidal_category

open category_theory.monoidal_category

namespace tactic

open tactic
setup_tactic_parser

/--
Auxilliary definition of `monoidal_coherence`,
being careful with namespaces to avoid shadowing.
-/
meta def mk_project_map_expr (e : expr) : tactic expr :=
  to_expr ``(category_theory.free_monoidal_category.project_map _root_.id _ _
    (category_theory.monoidal_category.lift_hom.lift %%e))

/-- Coherence tactic for monoidal categories. -/
meta def monoidal_coherence : tactic unit :=
do
  o â† get_options, set_options $ o.set_nat `class.instance_max_depth 128,
  try `[dsimp],
  `(%%lhs = %%rhs) â† target,
  project_map_lhs â† mk_project_map_expr lhs,
  project_map_rhs â† mk_project_map_expr rhs,
  to_expr  ``(%%project_map_lhs = %%project_map_rhs) >>= tactic.change,
  congr

/--
`pure_coherence` uses the coherence theorem for monoidal categories to prove the goal.
It can prove any equality made up only of associators, unitors, and identities.
```lean
example {C : Type} [category C] [monoidal_category C] :
  (Î»_ (ğŸ™_ C)).hom = (Ï_ (ğŸ™_ C)).hom :=
by pure_coherence
```

Users will typicall just use the `coherence` tactic, which can also cope with identities of the form
`a â‰« f â‰« b â‰« g â‰« c = a' â‰« f â‰« b' â‰« g â‰« c'`
where `a = a'`, `b = b'`, and `c = c'` can be proved using `pure_coherence`
-/
meta def pure_coherence : tactic unit := monoidal_coherence <|> bicategorical_coherence

example (Xâ‚ Xâ‚‚ : C) :
  ((Î»_ (ğŸ™_ C)).inv âŠ— ğŸ™ (Xâ‚ âŠ— Xâ‚‚)) â‰« (Î±_ (ğŸ™_ C) (ğŸ™_ C) (Xâ‚ âŠ— Xâ‚‚)).hom â‰«
    (ğŸ™ (ğŸ™_ C) âŠ— (Î±_ (ğŸ™_ C) Xâ‚ Xâ‚‚).inv) =
  ğŸ™ (ğŸ™_ C) âŠ— ((Î»_ Xâ‚).inv âŠ— ğŸ™ Xâ‚‚) :=
by pure_coherence

namespace coherence

/--
Auxiliary simp lemma for the `coherence` tactic:
this moves brackets to the left in order to expose a maximal prefix
built out of unitors and associators.
-/
-- We have unused typeclass arguments here.
-- They are intentional, to ensure that `simp only [assoc_lift_hom]` only left associates
-- monoidal structural morphisms.
@[nolint unused_arguments]
lemma assoc_lift_hom {W X Y Z : C} [lift_obj W] [lift_obj X] [lift_obj Y]
  (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) [lift_hom f] [lift_hom g] :
  f â‰« (g â‰« h) = (f â‰« g) â‰« h :=
(category.assoc _ _ _).symm

/--
Internal tactic used in `coherence`.

Rewrites an equation `f = g` as `fâ‚€ â‰« fâ‚ = gâ‚€ â‰« gâ‚`,
where `fâ‚€` and `gâ‚€` are maximal prefixes of `f` and `g` (possibly after reassociating)
which are "liftable" (i.e. expressible as compositions of unitors and associators).
-/
meta def liftable_prefixes : tactic unit :=
do
  o â† get_options, set_options $ o.set_nat `class.instance_max_depth 128,
  try `[simp only [monoidal_comp, category_theory.category.assoc]] >>
    `[apply (cancel_epi (ğŸ™ _)).1; try { apply_instance }] >>
    try `[simp only [tactic.coherence.assoc_lift_hom, tactic.bicategory.coherence.assoc_lift_homâ‚‚]]

example {W X Y Z : C} (f : Y âŸ¶ Z) (g) (w : false) : (Î»_ _).hom â‰« f = g :=
begin
  liftable_prefixes,
  guard_target (ğŸ™ _ â‰« (Î»_ _).hom) â‰« f = (ğŸ™ _) â‰« g,
  cases w,
end

lemma insert_id_lhs {C : Type*} [category C] {X Y : C} (f g : X âŸ¶ Y) (w : f â‰« ğŸ™ _ = g) : f = g :=
by simpa using w
lemma insert_id_rhs {C : Type*} [category C] {X Y : C} (f g : X âŸ¶ Y) (w : f = g â‰« ğŸ™ _) : f = g :=
by simpa using w

end coherence

open coherence

/-- The main part of `coherence` tactic. -/
meta def coherence_loop : tactic unit :=
do
  -- To prove an equality `f = g` in a monoidal category,
  -- first try the `pure_coherence` tactic on the entire equation:
  pure_coherence <|> do
  -- Otherwise, rearrange so we have a maximal prefix of each side
  -- that is built out of unitors and associators:
  liftable_prefixes <|>
    fail ("Something went wrong in the `coherence` tactic: " ++
      "is the target an equation in a monoidal category?"),
  -- The goal should now look like `fâ‚€ â‰« fâ‚ = gâ‚€ â‰« gâ‚`,
  tactic.congr_core',
  -- and now we have two goals `fâ‚€ = gâ‚€` and `fâ‚ = gâ‚`.
  -- Discharge the first using `coherence`,
  focus1 pure_coherence <|>
    fail "`coherence` tactic failed, subgoal not true in the free monoidal_category",
  -- Then check that either `gâ‚€` is identically `gâ‚`,
  reflexivity <|> (do
    -- or that both are compositions,
    (do `(_ â‰« _ = _) â† target, skip) <|> `[apply tactic.coherence.insert_id_lhs],
    (do `(_ = _ â‰« _) â† target, skip) <|> `[apply tactic.coherence.insert_id_rhs],
    `(_ â‰« _ = _ â‰« _) â† target |
      fail "`coherence` tactic failed, non-structural morphisms don't match",
    tactic.congr_core',
    -- with identical first terms,
    reflexivity <|> fail "`coherence` tactic failed, non-structural morphisms don't match",
    -- and whose second terms can be identified by recursively called `coherence`.
    coherence_loop)

/--
Use the coherence theorem for monoidal categories to solve equations in a monoidal equation,
where the two sides only differ by replacing strings of monoidal structural morphisms
(that is, associators, unitors, and identities)
with different strings of structural morphisms with the same source and target.

That is, `coherence` can handle goals of the form
`a â‰« f â‰« b â‰« g â‰« c = a' â‰« f â‰« b' â‰« g â‰« c'`
where `a = a'`, `b = b'`, and `c = c'` can be proved using `pure_coherence`.

(If you have very large equations on which `coherence` is unexpectedly failing,
you may need to increase the typeclass search depth,
using e.g. `set_option class.instance_max_depth 500`.)
-/
meta def coherence : tactic unit :=
do
  try `[simp only [bicategorical_comp]],
  try `[simp only [monoidal_comp]],
  -- TODO: put similar normalization simp lemmas for monoidal categories
  try bicategory.whisker_simps,
  coherence_loop

run_cmd add_interactive [`pure_coherence, `coherence]

add_tactic_doc
{ name        := "coherence",
  category    := doc_category.tactic,
  decl_names  := [`tactic.interactive.coherence],
  tags        := ["category theory"] }

example (f) : (Î»_ (ğŸ™_ C)).hom â‰« f â‰« (Î»_ (ğŸ™_ C)).hom = (Ï_ (ğŸ™_ C)).hom â‰« f â‰« (Ï_ (ğŸ™_ C)).hom :=
by coherence

example {U V W X Y : C} (f : U âŸ¶ V âŠ— (W âŠ— X)) (g : (V âŠ— W) âŠ— X âŸ¶ Y) :
  f âŠ—â‰« g = f â‰« (Î±_ _ _ _).inv â‰« g :=
by coherence

example {U : C} (f : U âŸ¶ ğŸ™_ C) : f â‰« (Ï_ (ğŸ™_ C)).inv â‰« (Î»_ (ğŸ™_ C)).hom = f :=
by coherence

example (W X Y Z : C) (f) :
  ((Î±_ W X Y).hom âŠ— ğŸ™ Z) â‰« (Î±_ W (X âŠ— Y) Z).hom â‰« (ğŸ™ W âŠ— (Î±_ X Y Z).hom) â‰« f â‰«
    (Î±_ (W âŠ— X) Y Z).hom â‰« (Î±_ W X (Y âŠ— Z)).hom =
  (Î±_ (W âŠ— X) Y Z).hom â‰« (Î±_ W X (Y âŠ— Z)).hom â‰« f â‰«
    ((Î±_ W X Y).hom âŠ— ğŸ™ Z) â‰« (Î±_ W (X âŠ— Y) Z).hom â‰« (ğŸ™ W âŠ— (Î±_ X Y Z).hom) :=
by coherence

end tactic
