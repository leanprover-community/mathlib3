/-
Copyright (c) 2018 Michael Jendrusch. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Jendrusch, Scott Morrison, Bhavik Mehta
-/
import category_theory.monoidal.category
import category_theory.adjunction.basic

/-!
# (Lax) monoidal functors

A lax monoidal functor `F` between monoidal categories `C` and `D`
is a functor between the underlying categories equipped with morphisms
* `Îµ : ğŸ™_ D âŸ¶ F.obj (ğŸ™_ C)` (called the unit morphism)
* `Î¼ X Y : (F.obj X) âŠ— (F.obj Y) âŸ¶ F.obj (X âŠ— Y)` (called the tensorator, or strength).
satisfying various axioms.

A monoidal functor is a lax monoidal functor for which `Îµ` and `Î¼` are isomorphisms.

We show that the composition of (lax) monoidal functors gives a (lax) monoidal functor.

See also `category_theory.monoidal.functorial` for a typeclass decorating an object-level
function with the additional data of a monoidal functor.
This is useful when stating that a pre-existing functor is monoidal.

See `category_theory.monoidal.natural_transformation` for monoidal natural transformations.

We show in `category_theory.monoidal.Mon_` that lax monoidal functors take monoid objects
to monoid objects.

## Future work
* Oplax monoidal functors.

## References

See https://stacks.math.columbia.edu/tag/0FFL.
-/

open category_theory

universes vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open category_theory.category
open category_theory.functor

namespace category_theory

section

open monoidal_category

variables (C : Type uâ‚) [category.{vâ‚} C] [monoidal_category.{vâ‚} C]
          (D : Type uâ‚‚) [category.{vâ‚‚} D] [monoidal_category.{vâ‚‚} D]

/-- A lax monoidal functor is a functor `F : C â¥¤ D` between monoidal categories,
equipped with morphisms `Îµ : ğŸ™ _D âŸ¶ F.obj (ğŸ™_ C)` and `Î¼ X Y : F.obj X âŠ— F.obj Y âŸ¶ F.obj (X âŠ— Y)`,
satisfying the appropriate coherences. -/
-- The direction of `left_unitality` and `right_unitality` as simp lemmas may look strange:
-- remember the rule of thumb that component indices of natural transformations
-- "weigh more" than structural maps.
-- (However by this argument `associativity` is currently stated backwards!)
structure lax_monoidal_functor extends C â¥¤ D :=
-- unit morphism
(Îµ               : ğŸ™_ D âŸ¶ obj (ğŸ™_ C))
-- tensorator
(Î¼                : Î  X Y : C, (obj X) âŠ— (obj Y) âŸ¶ obj (X âŠ— Y))
(Î¼_natural'       : âˆ€ {X Y X' Y' : C}
  (f : X âŸ¶ Y) (g : X' âŸ¶ Y'),
  ((map f) âŠ— (map g)) â‰« Î¼ Y Y' = Î¼ X X' â‰« map (f âŠ— g)
  . obviously)
-- associativity of the tensorator
(associativity'   : âˆ€ (X Y Z : C),
    (Î¼ X Y âŠ— ğŸ™ (obj Z)) â‰« Î¼ (X âŠ— Y) Z â‰« map (Î±_ X Y Z).hom
  = (Î±_ (obj X) (obj Y) (obj Z)).hom â‰« (ğŸ™ (obj X) âŠ— Î¼ Y Z) â‰« Î¼ X (Y âŠ— Z)
  . obviously)
-- unitality
(left_unitality'  : âˆ€ X : C,
    (Î»_ (obj X)).hom
  = (Îµ âŠ— ğŸ™ (obj X)) â‰« Î¼ (ğŸ™_ C) X â‰« map (Î»_ X).hom
  . obviously)
(right_unitality' : âˆ€ X : C,
    (Ï_ (obj X)).hom
  = (ğŸ™ (obj X) âŠ— Îµ) â‰« Î¼ X (ğŸ™_ C) â‰« map (Ï_ X).hom
  . obviously)

restate_axiom lax_monoidal_functor.Î¼_natural'
attribute [simp, reassoc] lax_monoidal_functor.Î¼_natural
restate_axiom lax_monoidal_functor.left_unitality'
attribute [simp] lax_monoidal_functor.left_unitality
restate_axiom lax_monoidal_functor.right_unitality'
attribute [simp] lax_monoidal_functor.right_unitality
restate_axiom lax_monoidal_functor.associativity'
attribute [simp, reassoc] lax_monoidal_functor.associativity

-- When `rewrite_search` lands, add @[search] attributes to
-- lax_monoidal_functor.Î¼_natural lax_monoidal_functor.left_unitality
-- lax_monoidal_functor.right_unitality lax_monoidal_functor.associativity

section
variables {C D}

@[simp, reassoc]
lemma lax_monoidal_functor.left_unitality_inv (F : lax_monoidal_functor C D) (X : C) :
  (Î»_ (F.obj X)).inv â‰« (F.Îµ âŠ— ğŸ™ (F.obj X)) â‰« F.Î¼ (ğŸ™_ C) X = F.map (Î»_ X).inv :=
begin
  rw [iso.inv_comp_eq, F.left_unitality, category.assoc, category.assoc,
    â†F.to_functor.map_comp, iso.hom_inv_id, F.to_functor.map_id, comp_id],
end

@[simp, reassoc]
lemma lax_monoidal_functor.right_unitality_inv (F : lax_monoidal_functor C D) (X : C) :
  (Ï_ (F.obj X)).inv â‰« (ğŸ™ (F.obj X) âŠ— F.Îµ) â‰« F.Î¼ X (ğŸ™_ C) = F.map (Ï_ X).inv :=
begin
  rw [iso.inv_comp_eq, F.right_unitality, category.assoc, category.assoc,
    â†F.to_functor.map_comp, iso.hom_inv_id, F.to_functor.map_id, comp_id],
end

@[simp, reassoc]
lemma lax_monoidal_functor.associativity_inv (F : lax_monoidal_functor C D) (X Y Z : C) :
  (ğŸ™ (F.obj X) âŠ— F.Î¼ Y Z) â‰« F.Î¼ X (Y âŠ— Z) â‰« F.map (Î±_ X Y Z).inv =
    (Î±_ (F.obj X) (F.obj Y) (F.obj Z)).inv â‰« (F.Î¼ X Y âŠ— ğŸ™ (F.obj Z)) â‰« F.Î¼ (X âŠ— Y) Z :=
begin
  rw [iso.eq_inv_comp, â†F.associativity_assoc,
    â†F.to_functor.map_comp, iso.hom_inv_id, F.to_functor.map_id, comp_id],
end

end

/--
A monoidal functor is a lax monoidal functor for which the tensorator and unitor as isomorphisms.

See https://stacks.math.columbia.edu/tag/0FFL.
-/
structure monoidal_functor
extends lax_monoidal_functor.{vâ‚ vâ‚‚} C D :=
(Îµ_is_iso            : is_iso Îµ . tactic.apply_instance)
(Î¼_is_iso            : Î  X Y : C, is_iso (Î¼ X Y) . tactic.apply_instance)

attribute [instance] monoidal_functor.Îµ_is_iso monoidal_functor.Î¼_is_iso

variables {C D}

/--
The unit morphism of a (strong) monoidal functor as an isomorphism.
-/
noncomputable
def monoidal_functor.Îµ_iso (F : monoidal_functor.{vâ‚ vâ‚‚} C D) :
  tensor_unit D â‰… F.obj (tensor_unit C) :=
as_iso F.Îµ

/--
The tensorator of a (strong) monoidal functor as an isomorphism.
-/
noncomputable
def monoidal_functor.Î¼_iso (F : monoidal_functor.{vâ‚ vâ‚‚} C D) (X Y : C) :
  (F.obj X) âŠ— (F.obj Y) â‰… F.obj (X âŠ— Y) :=
as_iso (F.Î¼ X Y)

end

open monoidal_category

namespace lax_monoidal_functor

variables (C : Type uâ‚) [category.{vâ‚} C] [monoidal_category.{vâ‚} C]

/-- The identity lax monoidal functor. -/
@[simps] def id : lax_monoidal_functor.{vâ‚ vâ‚} C C :=
{ Îµ := ğŸ™ _,
  Î¼ := Î» X Y, ğŸ™ _,
  .. ğŸ­ C }

instance : inhabited (lax_monoidal_functor C C) := âŸ¨id CâŸ©

end lax_monoidal_functor

namespace monoidal_functor

section
variables {C : Type uâ‚} [category.{vâ‚} C] [monoidal_category.{vâ‚} C]
variables {D : Type uâ‚‚} [category.{vâ‚‚} D] [monoidal_category.{vâ‚‚} D]
variable (F : monoidal_functor.{vâ‚ vâ‚‚} C D)

lemma map_tensor {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') :
  F.map (f âŠ— g) = inv (F.Î¼ X X') â‰« ((F.map f) âŠ— (F.map g)) â‰« F.Î¼ Y Y' :=
by simp

lemma map_left_unitor (X : C) :
  F.map (Î»_ X).hom = inv (F.Î¼ (ğŸ™_ C) X) â‰« (inv F.Îµ âŠ— ğŸ™ (F.obj X)) â‰« (Î»_ (F.obj X)).hom :=
begin
  simp only [lax_monoidal_functor.left_unitality],
  slice_rhs 2 3 { rw â†comp_tensor_id, simp, },
  simp,
end

lemma map_right_unitor (X : C) :
  F.map (Ï_ X).hom = inv (F.Î¼ X (ğŸ™_ C)) â‰« (ğŸ™ (F.obj X) âŠ— inv F.Îµ) â‰« (Ï_ (F.obj X)).hom :=
begin
  simp only [lax_monoidal_functor.right_unitality],
  slice_rhs 2 3 { rw â†id_tensor_comp, simp, },
  simp,
end

/-- The tensorator as a natural isomorphism. -/
noncomputable
def Î¼_nat_iso :
  (functor.prod F.to_functor F.to_functor) â‹™ (tensor D) â‰… (tensor C) â‹™ F.to_functor :=
nat_iso.of_components
  (by { intros, apply F.Î¼_iso })
  (by { intros, apply F.to_lax_monoidal_functor.Î¼_natural })

@[simp] lemma Î¼_iso_hom (X Y : C) : (F.Î¼_iso X Y).hom = F.Î¼ X Y := rfl
@[simp, reassoc] lemma Î¼_inv_hom_id (X Y : C) : (F.Î¼_iso X Y).inv â‰« F.Î¼ X Y = ğŸ™ _ :=
(F.Î¼_iso X Y).inv_hom_id
@[simp] lemma Î¼_hom_inv_id (X Y : C) : F.Î¼ X Y â‰« (F.Î¼_iso X Y).inv = ğŸ™ _ :=
(F.Î¼_iso X Y).hom_inv_id

@[simp] lemma Îµ_iso_hom : F.Îµ_iso.hom = F.Îµ := rfl
@[simp, reassoc] lemma Îµ_inv_hom_id : F.Îµ_iso.inv â‰« F.Îµ = ğŸ™ _ := F.Îµ_iso.inv_hom_id
@[simp] lemma Îµ_hom_inv_id : F.Îµ â‰« F.Îµ_iso.inv = ğŸ™ _ := F.Îµ_iso.hom_inv_id

end

section
variables (C : Type uâ‚) [category.{vâ‚} C] [monoidal_category.{vâ‚} C]

/-- The identity monoidal functor. -/
@[simps] def id : monoidal_functor.{vâ‚ vâ‚} C C :=
{ Îµ := ğŸ™ _,
  Î¼ := Î» X Y, ğŸ™ _,
  .. ğŸ­ C }

instance : inhabited (monoidal_functor C C) := âŸ¨id CâŸ©

end

end monoidal_functor

variables {C : Type uâ‚} [category.{vâ‚} C] [monoidal_category.{vâ‚} C]
variables {D : Type uâ‚‚} [category.{vâ‚‚} D] [monoidal_category.{vâ‚‚} D]
variables {E : Type uâ‚ƒ} [category.{vâ‚ƒ} E] [monoidal_category.{vâ‚ƒ} E]

namespace lax_monoidal_functor
variables (F : lax_monoidal_functor.{vâ‚ vâ‚‚} C D) (G : lax_monoidal_functor.{vâ‚‚ vâ‚ƒ} D E)

-- The proofs here are horrendous; rewrite_search helps a lot.
/-- The composition of two lax monoidal functors is again lax monoidal. -/
@[simps] def comp : lax_monoidal_functor.{vâ‚ vâ‚ƒ} C E :=
{ Îµ                := G.Îµ â‰« (G.map F.Îµ),
  Î¼                := Î» X Y, G.Î¼ (F.obj X) (F.obj Y) â‰« G.map (F.Î¼ X Y),
  Î¼_natural'       := Î» _ _ _ _ f g,
  begin
    simp only [functor.comp_map, assoc],
    rw [â†category.assoc, lax_monoidal_functor.Î¼_natural, category.assoc, â†map_comp, â†map_comp,
        â†lax_monoidal_functor.Î¼_natural]
  end,
  associativity'   := Î» X Y Z,
  begin
    dsimp,
    rw id_tensor_comp,
    slice_rhs 3 4 { rw [â† G.to_functor.map_id, G.Î¼_natural], },
    slice_rhs 1 3 { rw â†G.associativity, },
    rw comp_tensor_id,
    slice_lhs 2 3 { rw [â† G.to_functor.map_id, G.Î¼_natural], },
    rw [category.assoc, category.assoc, category.assoc, category.assoc, category.assoc,
        â†G.to_functor.map_comp, â†G.to_functor.map_comp, â†G.to_functor.map_comp,
        â†G.to_functor.map_comp, F.associativity],
  end,
  left_unitality'  := Î» X,
  begin
    dsimp,
    rw [G.left_unitality, comp_tensor_id, category.assoc, category.assoc],
    apply congr_arg,
    rw [F.left_unitality, map_comp, â†nat_trans.id_app, â†category.assoc,
        â†lax_monoidal_functor.Î¼_natural, nat_trans.id_app, map_id, â†category.assoc, map_comp],
  end,
  right_unitality' := Î» X,
  begin
    dsimp,
    rw [G.right_unitality, id_tensor_comp, category.assoc, category.assoc],
    apply congr_arg,
    rw [F.right_unitality, map_comp, â†nat_trans.id_app, â†category.assoc,
        â†lax_monoidal_functor.Î¼_natural, nat_trans.id_app, map_id, â†category.assoc, map_comp],
  end,
  .. (F.to_functor) â‹™ (G.to_functor) }.

infixr ` âŠ—â‹™ `:80 := comp

end lax_monoidal_functor

namespace monoidal_functor

variables (F : monoidal_functor.{vâ‚ vâ‚‚} C D) (G : monoidal_functor.{vâ‚‚ vâ‚ƒ} D E)

/-- The composition of two monoidal functors is again monoidal. -/
@[simps]
def comp : monoidal_functor.{vâ‚ vâ‚ƒ} C E :=
{ Îµ_is_iso := by { dsimp, apply_instance },
  Î¼_is_iso := by { dsimp, apply_instance },
  .. (F.to_lax_monoidal_functor).comp (G.to_lax_monoidal_functor) }.

infixr ` âŠ—â‹™ `:80 := comp -- We overload notation; potentially dangerous, but it seems to work.

end monoidal_functor

/--
If we have a right adjoint functor `G` to a monoidal functor `F`, then `G` has a lax monoidal
structure as well.
-/
@[simps]
noncomputable
def monoidal_adjoint (F : monoidal_functor C D) {G : D â¥¤ C} (h : F.to_functor âŠ£ G) :
  lax_monoidal_functor D C :=
{ to_functor := G,
  Îµ := h.hom_equiv _ _ (inv F.Îµ),
  Î¼ := Î» X Y,
    h.hom_equiv _ (X âŠ— Y) (inv (F.Î¼ (G.obj X) (G.obj Y)) â‰« (h.counit.app X âŠ— h.counit.app Y)),
  Î¼_natural' := Î» X Y X' Y' f g,
  begin
    rw [â†h.hom_equiv_naturality_left, â†h.hom_equiv_naturality_right, equiv.apply_eq_iff_eq, assoc,
      is_iso.eq_inv_comp, â†F.to_lax_monoidal_functor.Î¼_natural_assoc, is_iso.hom_inv_id_assoc,
      â†tensor_comp, adjunction.counit_naturality, adjunction.counit_naturality, tensor_comp],
  end,
  associativity' := Î» X Y Z,
  begin
    rw [â†h.hom_equiv_naturality_right, â†h.hom_equiv_naturality_left, â†h.hom_equiv_naturality_left,
      â†h.hom_equiv_naturality_left, equiv.apply_eq_iff_eq,
      â† cancel_epi (F.to_lax_monoidal_functor.Î¼ (G.obj X âŠ— G.obj Y) (G.obj Z)),
      â† cancel_epi (F.to_lax_monoidal_functor.Î¼ (G.obj X) (G.obj Y) âŠ— ğŸ™ (F.obj (G.obj Z))),
      F.to_lax_monoidal_functor.associativity_assoc (G.obj X) (G.obj Y) (G.obj Z),
      â†F.to_lax_monoidal_functor.Î¼_natural_assoc, assoc, is_iso.hom_inv_id_assoc,
      â†F.to_lax_monoidal_functor.Î¼_natural_assoc, is_iso.hom_inv_id_assoc, â†tensor_comp,
      â†tensor_comp, id_comp, functor.map_id, functor.map_id, id_comp, â†tensor_comp_assoc,
      â†tensor_comp_assoc, id_comp, id_comp, h.hom_equiv_unit, h.hom_equiv_unit, functor.map_comp,
      assoc, assoc, h.counit_naturality, h.left_triangle_components_assoc, is_iso.hom_inv_id_assoc,
      functor.map_comp, assoc, h.counit_naturality, h.left_triangle_components_assoc,
      is_iso.hom_inv_id_assoc],
    exact associator_naturality (h.counit.app X) (h.counit.app Y) (h.counit.app Z),
  end,
  left_unitality' := Î» X,
  begin
    rw [â†h.hom_equiv_naturality_right, â†h.hom_equiv_naturality_left, â†equiv.symm_apply_eq,
      h.hom_equiv_counit, F.map_left_unitor, h.hom_equiv_unit, assoc, assoc, assoc, F.map_tensor,
      assoc, assoc, is_iso.hom_inv_id_assoc, â†tensor_comp_assoc, functor.map_id, id_comp,
      functor.map_comp, assoc, h.counit_naturality, h.left_triangle_components_assoc,
      â†left_unitor_naturality, â†tensor_comp_assoc, id_comp, comp_id],
  end,
  right_unitality' := Î» X,
  begin
    rw [â†h.hom_equiv_naturality_right, â†h.hom_equiv_naturality_left, â†equiv.symm_apply_eq,
      h.hom_equiv_counit, F.map_right_unitor, assoc, assoc, â†right_unitor_naturality,
      â†tensor_comp_assoc, comp_id, id_comp, h.hom_equiv_unit, F.map_tensor, assoc, assoc, assoc,
      is_iso.hom_inv_id_assoc, functor.map_comp, functor.map_id, â†tensor_comp_assoc, assoc,
      h.counit_naturality, h.left_triangle_components_assoc, id_comp],
  end }.

/-- If a monoidal functor `F` is an equivalence of categories then its inverse is also monoidal. -/
noncomputable
def monoidal_inverse (F : monoidal_functor C D) [is_equivalence F.to_functor] :
  monoidal_functor D C :=
{ to_lax_monoidal_functor := monoidal_adjoint F (as_equivalence _).to_adjunction,
  Îµ_is_iso := by { dsimp [equivalence.to_adjunction], apply_instance },
  Î¼_is_iso := Î» X Y, by { dsimp [equivalence.to_adjunction], apply_instance } }

@[simp]
lemma monoidal_inverse_to_functor (F : monoidal_functor C D) [is_equivalence F.to_functor] :
  (monoidal_inverse F).to_functor = F.to_functor.inv := rfl

end category_theory
