/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.monoidal.functorial
import category_theory.monoidal.functor_category
import category_theory.limits.has_limits

/-!
# `lim : (J â¥¤ C) â¥¤ C` is lax monoidal when `C` is a monoidal category.

When `C` is a monoidal category, the functorial association `F â†¦ limit F` is lax monoidal,
i.e. there are morphisms
* `lim_lax.Îµ : (ğŸ™_ C) â†’ limit (ğŸ™_ (J â¥¤ C))`
* `lim_lax.Î¼ : limit F âŠ— limit G âŸ¶ limit (F âŠ— G)`
satisfying the laws of a lax monoidal functor.
-/

open category_theory
open category_theory.monoidal_category

namespace category_theory.limits

universes v u

noncomputable theory

variables {J : Type v} [small_category J]
variables {C : Type u} [category.{v} C] [has_limits C]

instance limit_functorial : functorial (Î» F : J â¥¤ C, limit F) := { ..limits.lim }

@[simp] lemma limit_functorial_map {F G : J â¥¤ C} (Î± : F âŸ¶ G) :
  map (Î» F : J â¥¤ C, limit F) Î± = limits.lim.map Î± := rfl

variables  [monoidal_category.{v} C]

@[simps]
instance limit_lax_monoidal : lax_monoidal (Î» F : J â¥¤ C, limit F) :=
{ Îµ := limit.lift _ { X := _, Ï€ := { app := Î» j, ğŸ™ _, } },
  Î¼ := Î» F G, limit.lift (F âŠ— G)
    { X := limit F âŠ— limit G,
      Ï€ :=
      { app := Î» j, limit.Ï€ F j âŠ— limit.Ï€ G j,
        naturality' := Î» j j' f,
        begin
          dsimp,
          simp only [category.id_comp, â†tensor_comp, limit.w],
        end, } },
  Î¼_natural' := Î» X Y X' Y' f g,
  begin
    ext, dsimp,
    simp only [limit.lift_Ï€, cones.postcompose_obj_Ï€, monoidal.tensor_hom_app, limit.lift_map,
      nat_trans.comp_app, category.assoc, â†tensor_comp, lim_map_Ï€],
  end,
  associativity' := Î» X Y Z,
  begin
    ext, dsimp,
    simp only [limit.lift_Ï€, cones.postcompose_obj_Ï€, monoidal.associator_hom_app, limit.lift_map,
      nat_trans.comp_app, category.assoc],
    slice_lhs 2 2 { rw [â†tensor_id_comp_id_tensor], },
    slice_lhs 1 2 { rw [â†comp_tensor_id, limit.lift_Ï€], dsimp, },
    slice_lhs 1 2 { rw [tensor_id_comp_id_tensor], },
    conv_lhs { rw [associator_naturality], },
    conv_rhs { rw [â†id_tensor_comp_tensor_id (limit.Ï€ (Y âŠ— Z) j)], },
    slice_rhs 2 3 { rw [â†id_tensor_comp, limit.lift_Ï€], dsimp, },
    dsimp, simp,
  end,
  left_unitality' := Î» X,
  begin
    ext, dsimp,
    simp,
    conv_rhs { rw [â†tensor_id_comp_id_tensor (limit.Ï€ X j)], },
    slice_rhs 1 2 { rw [â†comp_tensor_id], erw [limit.lift_Ï€], dsimp, },
    slice_rhs 2 3 { rw [left_unitor_naturality], },
    simp,
  end,
  right_unitality' := Î» X,
  begin
    ext, dsimp,
    simp,
    conv_rhs { rw [â†id_tensor_comp_tensor_id _ (limit.Ï€ X j)], },
    slice_rhs 1 2 { rw [â†id_tensor_comp], erw [limit.lift_Ï€], dsimp, },
    slice_rhs 2 3 { rw [right_unitor_naturality], },
    simp,
  end, }

/-- The limit functor `F â†¦ limit F` bundled as a lax monoidal functor. -/
def lim_lax : lax_monoidal_functor (J â¥¤ C) C := lax_monoidal_functor.of (Î» F : J â¥¤ C, limit F)

@[simp] lemma lim_lax_obj (F : J â¥¤ C) : lim_lax.obj F = limit F := rfl

lemma lim_lax_obj' (F : J â¥¤ C) : lim_lax.obj F = lim.obj F := rfl

@[simp] lemma lim_lax_map {F G : J â¥¤ C} (Î± : F âŸ¶ G) : lim_lax.map Î± = lim.map Î± := rfl

@[simp] lemma lim_lax_Îµ :
  (@lim_lax J _ C _ _ _).Îµ = limit.lift _ { X := _, Ï€ := { app := Î» j, ğŸ™ _, } } := rfl

@[simp] lemma lim_lax_Î¼ (F G : J â¥¤ C) :
  (@lim_lax J _ C _ _ _).Î¼ F G = limit.lift (F âŠ— G)
    { X := limit F âŠ— limit G,
      Ï€ :=
      { app := Î» j, limit.Ï€ F j âŠ— limit.Ï€ G j,
        naturality' := Î» j j' f,
        begin
          dsimp,
          simp only [category.id_comp, â†tensor_comp, limit.w],
        end, } } := rfl

end category_theory.limits
