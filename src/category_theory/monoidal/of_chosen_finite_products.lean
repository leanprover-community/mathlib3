/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Simon Hudon
-/
import category_theory.monoidal.braided
import category_theory.limits.shapes.binary_products
import category_theory.limits.shapes.terminal
import category_theory.pempty

/-!
# The monoidal structure on a category with chosen finite products.

This is a variant of the development in `category_theory.monoidal.of_has_finite_products`,
which uses specified choices of the terminal object and binary product,
enabling the construction of a cartesian category with specific definitions of the tensor unit
and tensor product.

(Because the construction in `category_theory.monoidal.of_has_finite_products` uses `has_limit`
classes, the actual definitions there are opaque behind `classical.choice`.)

We use this in `category_theory.monoidal.types` to construct the monoidal category of types
so that the tensor product is the usual cartesian product of types.

For now we only do the construction from products, and not from coproducts,
which seems less often useful.
-/

universes v u

noncomputable theory

namespace category_theory

variables (C : Type u) [category.{v} C] {X Y : C}

namespace limits

section
variables {C}

/-- Swap the two sides of a `binary_fan`. -/
def binary_fan.swap {P Q : C} (t : binary_fan P Q) : binary_fan Q P :=
binary_fan.mk t.snd t.fst

@[simp] lemma binary_fan.swap_fst {P Q : C} (t : binary_fan P Q) : t.swap.fst = t.snd := rfl
@[simp] lemma binary_fan.swap_snd {P Q : C} (t : binary_fan P Q) : t.swap.snd = t.fst := rfl

/--
If a cone `t` over `P Q` is a limit cone, then `t.swap` is a limit cone over `Q P`.
-/
@[simps]
def is_limit.swap_binary_fan {P Q : C} {t : binary_fan P Q} (I : is_limit t) : is_limit t.swap :=
{ lift := Î» s, I.lift (binary_fan.swap s),
  fac' := Î» s, by { rintro âŸ¨âŸ©; simp, },
  uniq' := Î» s m w,
  begin
    have h := I.uniq (binary_fan.swap s) m,
    rw h,
    intro j,
    specialize w j.swap,
    cases j; exact w,
  end }

/--
Construct `has_binary_product Q P` from `has_binary_product P Q`.
This can't be an instance, as it would cause a loop in typeclass search.
-/
lemma has_binary_product.swap (P Q : C) [has_binary_product P Q] : has_binary_product Q P :=
has_limit.mk âŸ¨binary_fan.swap (limit.cone (pair P Q)), (limit.is_limit (pair P Q)).swap_binary_fanâŸ©

/--
Given a limit cone over `X` and `Y`, and another limit cone over `Y` and `X`, we can construct
an isomorphism between the cone points. Relative to some fixed choice of limits cones for every
pair, these isomorphisms constitute a braiding.
-/
def binary_fan.braiding {X Y : C}
  {s : binary_fan X Y} (P : is_limit s) {t : binary_fan Y X} (Q : is_limit t) :
  s.X â‰… t.X :=
is_limit.cone_point_unique_up_to_iso P Q.swap_binary_fan

/--
Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `sXY.X Z`,
if `sYZ` is a limit cone we can construct a binary fan over `X sYZ.X`.

This is an ingredient of building the associator for a cartesian category.
-/
def binary_fan.assoc {X Y Z : C}
  {sXY : binary_fan X Y} {sYZ : binary_fan Y Z} (Q : is_limit sYZ) (s : binary_fan sXY.X Z) :
  binary_fan X sYZ.X :=
binary_fan.mk (s.fst â‰« sXY.fst) (Q.lift (binary_fan.mk (s.fst â‰« sXY.snd) s.snd))

@[simp] lemma binary_fan.assoc_fst {X Y Z : C}
  {sXY : binary_fan X Y} {sYZ : binary_fan Y Z} (Q : is_limit sYZ) (s : binary_fan sXY.X Z) :
  (s.assoc Q).fst = s.fst â‰« sXY.fst := rfl
@[simp] lemma binary_fan.assoc_snd {X Y Z : C}
  {sXY : binary_fan X Y} {sYZ : binary_fan Y Z} (Q : is_limit sYZ) (s : binary_fan sXY.X Z) :
  (s.assoc Q).snd = Q.lift (binary_fan.mk (s.fst â‰« sXY.snd) s.snd) := rfl

/--
Given binary fans `sXY` over `X Y`, and `sYZ` over `Y Z`, and `s` over `X sYZ.X`,
if `sYZ` is a limit cone we can construct a binary fan over `sXY.X Z`.

This is an ingredient of building the associator for a cartesian category.
-/
def binary_fan.assoc_inv {X Y Z : C}
  {sXY : binary_fan X Y} (P : is_limit sXY) {sYZ : binary_fan Y Z} (s : binary_fan X sYZ.X) :
  binary_fan sXY.X Z :=
binary_fan.mk (P.lift (binary_fan.mk s.fst (s.snd â‰« sYZ.fst))) (s.snd â‰« sYZ.snd)

@[simp] lemma binary_fan.assoc_inv_fst {X Y Z : C}
  {sXY : binary_fan X Y} (P : is_limit sXY) {sYZ : binary_fan Y Z} (s : binary_fan X sYZ.X) :
  (s.assoc_inv P).fst = P.lift (binary_fan.mk s.fst (s.snd â‰« sYZ.fst)) := rfl
@[simp] lemma binary_fan.assoc_inv_snd {X Y Z : C}
  {sXY : binary_fan X Y} (P : is_limit sXY) {sYZ : binary_fan Y Z} (s : binary_fan X sYZ.X) :
  (s.assoc_inv P).snd = s.snd â‰« sYZ.snd := rfl

/--
If all the binary fans involved a limit cones, `binary_fan.assoc` produces another limit cone.
-/
@[simps]
def is_limit.assoc {X Y Z : C}
  {sXY : binary_fan X Y} (P : is_limit sXY) {sYZ : binary_fan Y Z} (Q : is_limit sYZ)
  {s : binary_fan sXY.X Z} (R : is_limit s) : is_limit (s.assoc Q) :=
{ lift := Î» t, R.lift (binary_fan.assoc_inv P t),
  fac' := Î» t,
  begin
    rintro âŸ¨âŸ©; simp,
    apply Q.hom_ext,
    rintro âŸ¨âŸ©; simp,
  end,
  uniq' := Î» t m w,
  begin
    have h := R.uniq (binary_fan.assoc_inv P t) m,
    rw h,
    rintro âŸ¨âŸ©; simp,
    apply P.hom_ext,
    rintro âŸ¨âŸ©; simp,
    { exact w walking_pair.left, },
    { specialize w walking_pair.right,
      simp at w,
      rw [â†w], simp, },
    { specialize w walking_pair.right,
      simp at w,
      rw [â†w], simp, },
  end, }

/--
Given two pairs of limit cones corresponding to the parenthesisations of `X Ã— Y Ã— Z`,
we obtain an isomorphism between the cone points.
-/
@[reducible]
def binary_fan.associator {X Y Z : C}
  {sXY : binary_fan X Y} (P : is_limit sXY) {sYZ : binary_fan Y Z} (Q : is_limit sYZ)
  {s : binary_fan sXY.X Z} (R : is_limit s) {t : binary_fan X sYZ.X} (S : is_limit t) :
  s.X â‰… t.X :=
is_limit.cone_point_unique_up_to_iso (is_limit.assoc P Q R) S

/--
Given a fixed family of limit data for every pair `X Y`, we obtain an associator.
-/
@[reducible]
def binary_fan.associator_of_limit_cone
  (L : Î  X Y : C, limit_cone (pair X Y)) (X Y Z : C) :
  (L (L X Y).cone.X Z).cone.X â‰… (L X (L Y Z).cone.X).cone.X :=
binary_fan.associator
  (L X Y).is_limit (L Y Z).is_limit
  (L (L X Y).cone.X Z).is_limit (L X (L Y Z).cone.X).is_limit

/--
Construct a left unitor from specified limit cones.
-/
@[simps]
def binary_fan.left_unitor
  {X : C} {s : cone (functor.empty C)} (P : is_limit s) {t : binary_fan s.X X} (Q : is_limit t) :
  t.X â‰… X :=
{ hom := t.snd,
  inv := Q.lift (binary_fan.mk (P.lift { X := X, Ï€ := { app := pempty.rec _ } }) (ğŸ™ X) ),
  hom_inv_id' := by { apply Q.hom_ext, rintro âŸ¨âŸ©, { apply P.hom_ext, rintro âŸ¨âŸ©, }, { simp, }, }, }

/--
Construct a right unitor from specified limit cones.
-/
@[simps]
def binary_fan.right_unitor
  {X : C} {s : cone (functor.empty C)} (P : is_limit s) {t : binary_fan X s.X} (Q : is_limit t) :
  t.X â‰… X :=
{ hom := t.fst,
  inv := Q.lift (binary_fan.mk (ğŸ™ X) (P.lift { X := X, Ï€ := { app := pempty.rec _ } })),
  hom_inv_id' := by { apply Q.hom_ext, rintro âŸ¨âŸ©, { simp, }, { apply P.hom_ext, rintro âŸ¨âŸ©, }, }, }

end

end limits

open category_theory.limits

section
local attribute [tidy] tactic.case_bash

variables {C}
variables (ğ’¯ : limit_cone (functor.empty C))
variables (â„¬ : Î  (X Y : C), limit_cone (pair X Y))

namespace monoidal_of_chosen_finite_products

/-- Implementation of the tensor product for `monoidal_of_chosen_finite_products`. -/
@[reducible]
def tensor_obj (X Y : C) : C := (â„¬ X Y).cone.X

/-- Implementation of the tensor product of morphisms for `monoidal_of_chosen_finite_products`. -/
@[reducible]
def tensor_hom {W X Y Z : C} (f : W âŸ¶ X) (g : Y âŸ¶ Z) : tensor_obj â„¬ W Y âŸ¶ tensor_obj â„¬ X Z :=
  (binary_fan.is_limit.lift' (â„¬ X Z).is_limit
    ((â„¬ W Y).cone.Ï€.app walking_pair.left â‰« f)
    (((â„¬ W Y).cone.Ï€.app walking_pair.right : (â„¬ W Y).cone.X âŸ¶ Y) â‰« g)).val

lemma tensor_id (Xâ‚ Xâ‚‚ : C) : tensor_hom â„¬ (ğŸ™ Xâ‚) (ğŸ™ Xâ‚‚) = ğŸ™ (tensor_obj â„¬ Xâ‚ Xâ‚‚) :=
begin
  apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©;
  { dsimp [tensor_hom], simp, },
end

lemma tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C}
  (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
  tensor_hom â„¬ (fâ‚ â‰« gâ‚) (fâ‚‚ â‰« gâ‚‚) =
    tensor_hom â„¬ fâ‚ fâ‚‚ â‰« tensor_hom â„¬ gâ‚ gâ‚‚ :=
begin
  apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©;
  { dsimp [tensor_hom], simp, },
end

lemma pentagon (W X Y Z : C) :
  tensor_hom â„¬ (binary_fan.associator_of_limit_cone â„¬ W X Y).hom (ğŸ™ Z) â‰«
    (binary_fan.associator_of_limit_cone â„¬ W (tensor_obj â„¬ X Y) Z).hom â‰«
      tensor_hom â„¬ (ğŸ™ W) (binary_fan.associator_of_limit_cone â„¬ X Y Z).hom =
  (binary_fan.associator_of_limit_cone â„¬ (tensor_obj â„¬ W X) Y Z).hom â‰«
    (binary_fan.associator_of_limit_cone â„¬ W X (tensor_obj â„¬ Y Z)).hom :=
begin
  dsimp [tensor_hom],
  apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©,
  { simp, },
  { apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©,
    { simp, },
    apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©,
    { simp, },
    { simp, }, }
end

lemma triangle (X Y : C) :
  (binary_fan.associator_of_limit_cone â„¬ X ğ’¯.cone.X Y).hom â‰«
    tensor_hom â„¬ (ğŸ™ X) (binary_fan.left_unitor ğ’¯.is_limit (â„¬ ğ’¯.cone.X Y).is_limit).hom =
  tensor_hom â„¬ (binary_fan.right_unitor ğ’¯.is_limit (â„¬ X ğ’¯.cone.X).is_limit).hom (ğŸ™ Y) :=
begin
  dsimp [tensor_hom],
  apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©; simp,
end

lemma left_unitor_naturality {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) :
  tensor_hom â„¬ (ğŸ™ ğ’¯.cone.X) f â‰« (binary_fan.left_unitor ğ’¯.is_limit (â„¬ ğ’¯.cone.X Xâ‚‚).is_limit).hom =
    (binary_fan.left_unitor ğ’¯.is_limit (â„¬ ğ’¯.cone.X Xâ‚).is_limit).hom â‰« f :=
begin
  dsimp [tensor_hom],
  simp,
end

lemma right_unitor_naturality {Xâ‚ Xâ‚‚ : C} (f : Xâ‚ âŸ¶ Xâ‚‚) :
  tensor_hom â„¬ f (ğŸ™ ğ’¯.cone.X) â‰«
    (binary_fan.right_unitor ğ’¯.is_limit (â„¬ Xâ‚‚ ğ’¯.cone.X).is_limit).hom =
    (binary_fan.right_unitor ğ’¯.is_limit (â„¬ Xâ‚ ğ’¯.cone.X).is_limit).hom â‰« f :=
begin
  dsimp [tensor_hom],
  simp,
end

lemma associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  tensor_hom â„¬ (tensor_hom â„¬ fâ‚ fâ‚‚) fâ‚ƒ â‰« (binary_fan.associator_of_limit_cone â„¬ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom =
    (binary_fan.associator_of_limit_cone â„¬ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰«
      tensor_hom â„¬ fâ‚ (tensor_hom â„¬ fâ‚‚ fâ‚ƒ) :=
begin
  dsimp [tensor_hom],
  apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©,
  { simp, },
  { apply is_limit.hom_ext (â„¬ _ _).is_limit, rintro âŸ¨âŸ©,
    { simp, },
    { simp, }, },
end

end monoidal_of_chosen_finite_products

open monoidal_of_chosen_finite_products

/-- A category with a terminal object and binary products has a natural monoidal structure. -/
def monoidal_of_chosen_finite_products :
  monoidal_category C :=
{ tensor_unit  := ğ’¯.cone.X,
  tensor_obj   := Î» X Y, tensor_obj â„¬ X Y,
  tensor_hom   := Î» _ _ _ _ f g, tensor_hom â„¬ f g,
  tensor_id'   := tensor_id â„¬,
  tensor_comp' := Î» _ _ _ _ _ _ fâ‚ fâ‚‚ gâ‚ gâ‚‚, tensor_comp â„¬ fâ‚ fâ‚‚ gâ‚ gâ‚‚,
  associator   := Î» X Y Z, binary_fan.associator_of_limit_cone â„¬ X Y Z,
  left_unitor  := Î» X, binary_fan.left_unitor (ğ’¯.is_limit) (â„¬ ğ’¯.cone.X X).is_limit,
  right_unitor := Î» X, binary_fan.right_unitor (ğ’¯.is_limit) (â„¬ X ğ’¯.cone.X).is_limit,
  pentagon'    := pentagon â„¬,
  triangle'    := triangle ğ’¯ â„¬,
  left_unitor_naturality' := Î» _ _ f, left_unitor_naturality ğ’¯ â„¬ f,
  right_unitor_naturality' := Î» _ _ f, right_unitor_naturality ğ’¯ â„¬ f,
  associator_naturality' := Î» _ _ _ _ _ _ fâ‚ fâ‚‚ fâ‚ƒ, associator_naturality â„¬ fâ‚ fâ‚‚ fâ‚ƒ, }

namespace monoidal_of_chosen_finite_products

open monoidal_category

/--
A type synonym for `C` carrying a monoidal category structure corresponding to
a fixed choice of limit data for the empty functor, and for `pair X Y` for every `X Y : C`.

This is an implementation detail for `symmetric_of_chosen_finite_products`.
-/
@[derive category, nolint unused_arguments has_inhabited_instance]
def monoidal_of_chosen_finite_products_synonym
  (ğ’¯ : limit_cone (functor.empty C)) (â„¬ : Î  (X Y : C), limit_cone (pair X Y)):= C

instance : monoidal_category (monoidal_of_chosen_finite_products_synonym ğ’¯ â„¬) :=
monoidal_of_chosen_finite_products ğ’¯ â„¬

lemma braiding_naturality {X X' Y Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') :
  (tensor_hom â„¬ f g) â‰« (limits.binary_fan.braiding (â„¬ Y Y').is_limit (â„¬ Y' Y).is_limit).hom =
    (limits.binary_fan.braiding (â„¬ X X').is_limit (â„¬ X' X).is_limit).hom â‰« (tensor_hom â„¬ g f) :=
begin
  dsimp [tensor_hom, limits.binary_fan.braiding],
  apply (â„¬ _ _).is_limit.hom_ext, rintro âŸ¨âŸ©;
  { dsimp [limits.is_limit.cone_point_unique_up_to_iso], simp, },
end

lemma hexagon_forward (X Y Z : C) :
  (binary_fan.associator_of_limit_cone â„¬ X Y Z).hom â‰«
    (limits.binary_fan.braiding
      (â„¬ X (tensor_obj â„¬ Y Z)).is_limit
      (â„¬ (tensor_obj â„¬ Y Z) X).is_limit).hom â‰«
    (binary_fan.associator_of_limit_cone â„¬ Y Z X).hom =
    (tensor_hom â„¬ (limits.binary_fan.braiding (â„¬ X Y).is_limit (â„¬ Y X).is_limit).hom (ğŸ™ Z)) â‰«
      (binary_fan.associator_of_limit_cone â„¬ Y X Z).hom â‰«
        (tensor_hom â„¬ (ğŸ™ Y) (limits.binary_fan.braiding (â„¬ X Z).is_limit (â„¬ Z X).is_limit).hom) :=
begin
  dsimp [tensor_hom, limits.binary_fan.braiding],
  apply (â„¬ _ _).is_limit.hom_ext, rintro âŸ¨âŸ©,
  { dsimp [limits.is_limit.cone_point_unique_up_to_iso], simp, },
  { apply (â„¬ _ _).is_limit.hom_ext, rintro âŸ¨âŸ©;
    { dsimp [limits.is_limit.cone_point_unique_up_to_iso], simp, }, }
end

lemma hexagon_reverse (X Y Z : C) :
  (binary_fan.associator_of_limit_cone â„¬ X Y Z).inv â‰«
    (limits.binary_fan.braiding
      (â„¬ (tensor_obj â„¬ X Y) Z).is_limit
      (â„¬ Z (tensor_obj â„¬ X Y)).is_limit).hom â‰«
    (binary_fan.associator_of_limit_cone â„¬ Z X Y).inv =
    (tensor_hom â„¬ (ğŸ™ X) (limits.binary_fan.braiding (â„¬ Y Z).is_limit (â„¬ Z Y).is_limit).hom) â‰«
      (binary_fan.associator_of_limit_cone â„¬ X Z Y).inv â‰«
        (tensor_hom â„¬ (limits.binary_fan.braiding (â„¬ X Z).is_limit (â„¬ Z X).is_limit).hom (ğŸ™ Y)) :=
begin
  dsimp [tensor_hom, limits.binary_fan.braiding],
  apply (â„¬ _ _).is_limit.hom_ext, rintro âŸ¨âŸ©,
  { apply (â„¬ _ _).is_limit.hom_ext, rintro âŸ¨âŸ©;
    { dsimp [binary_fan.associator_of_limit_cone, binary_fan.associator,
        limits.is_limit.cone_point_unique_up_to_iso],
      simp, }, },
  { dsimp [binary_fan.associator_of_limit_cone, binary_fan.associator,
      limits.is_limit.cone_point_unique_up_to_iso],
    simp, },
end

lemma symmetry (X Y : C) :
  (limits.binary_fan.braiding (â„¬ X Y).is_limit (â„¬ Y X).is_limit).hom â‰«
      (limits.binary_fan.braiding (â„¬ Y X).is_limit (â„¬ X Y).is_limit).hom =
    ğŸ™ (tensor_obj â„¬ X Y) :=
begin
  dsimp [tensor_hom, limits.binary_fan.braiding],
  apply (â„¬ _ _).is_limit.hom_ext, rintro âŸ¨âŸ©;
  { dsimp [limits.is_limit.cone_point_unique_up_to_iso], simp, },
end

end monoidal_of_chosen_finite_products

open monoidal_of_chosen_finite_products

/--
The monoidal structure coming from finite products is symmetric.
-/
def symmetric_of_chosen_finite_products :
  symmetric_category (monoidal_of_chosen_finite_products_synonym ğ’¯ â„¬) :=
{ braiding := Î» X Y, limits.binary_fan.braiding (â„¬ _ _).is_limit (â„¬ _ _).is_limit,
  braiding_naturality' := Î» X X' Y Y' f g, braiding_naturality â„¬ f g,
  hexagon_forward' := Î» X Y Z, hexagon_forward â„¬ X Y Z,
  hexagon_reverse' := Î» X Y Z, hexagon_reverse â„¬ X Y Z,
  symmetry' := Î» X Y, symmetry â„¬ X Y, }

end

end category_theory
