/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.preadditive.additive_functor
import category_theory.monoidal.functor

/-!
# Preadditive monoidal categories

A monoidal category is `monoidal_preadditive` if it is preadditive and tensor product of morphisms
is linear in both factors.
-/

noncomputable theory
open_locale classical

namespace category_theory

open category_theory.limits
open category_theory.monoidal_category

variables (C : Type*) [category C] [preadditive C] [monoidal_category C]

/--
A category is `monoidal_preadditive` if tensoring is additive in both factors.

Note we don't `extend preadditive C` here, as `abelian C` already extends it,
and we'll need to have both typeclasses sometimes.
-/
class monoidal_preadditive : Prop :=
(tensor_zero' : âˆ€ {W X Y Z : C} (f : W âŸ¶ X), f âŠ— (0 : Y âŸ¶ Z) = 0 . obviously)
(zero_tensor' : âˆ€ {W X Y Z : C} (f : Y âŸ¶ Z), (0 : W âŸ¶ X) âŠ— f = 0 . obviously)
(tensor_add' : âˆ€ {W X Y Z : C} (f : W âŸ¶ X) (g h : Y âŸ¶ Z), f âŠ— (g + h) = f âŠ— g + f âŠ— h . obviously)
(add_tensor' : âˆ€ {W X Y Z : C} (f g : W âŸ¶ X) (h : Y âŸ¶ Z), (f + g) âŠ— h = f âŠ— h + g âŠ— h . obviously)

restate_axiom monoidal_preadditive.tensor_zero'
restate_axiom monoidal_preadditive.zero_tensor'
restate_axiom monoidal_preadditive.tensor_add'
restate_axiom monoidal_preadditive.add_tensor'
attribute [simp] monoidal_preadditive.tensor_zero monoidal_preadditive.zero_tensor

variables {C} [monoidal_preadditive C]

local attribute [simp] monoidal_preadditive.tensor_add monoidal_preadditive.add_tensor

instance tensor_left_additive (X : C) : (tensor_left X).additive := {}
instance tensor_right_additive (X : C) : (tensor_right X).additive := {}
instance tensoring_left_additive (X : C) : ((tensoring_left C).obj X).additive := {}
instance tensoring_right_additive (X : C) : ((tensoring_right C).obj X).additive := {}

/-- A faithful additive monoidal functor to a monoidal preadditive category
ensures that the domain is monoidal preadditive. -/
lemma monoidal_preadditive_of_faithful {D} [category D] [preadditive D] [monoidal_category D]
  (F : monoidal_functor D C) [faithful F.to_functor] [F.to_functor.additive] :
  monoidal_preadditive D :=
{ tensor_zero' := by { intros, apply F.to_functor.map_injective, simp [F.map_tensor], },
  zero_tensor' := by { intros, apply F.to_functor.map_injective, simp [F.map_tensor], },
  tensor_add' := begin
    intros,
    apply F.to_functor.map_injective,
    simp only [F.map_tensor, F.to_functor.map_add, preadditive.comp_add, preadditive.add_comp,
      monoidal_preadditive.tensor_add],
  end,
  add_tensor' := begin
    intros,
    apply F.to_functor.map_injective,
    simp only [F.map_tensor, F.to_functor.map_add, preadditive.comp_add, preadditive.add_comp,
      monoidal_preadditive.add_tensor],
  end, }

open_locale big_operators

lemma tensor_sum {P Q R S : C} {J : Type*} (s : finset J) (f : P âŸ¶ Q) (g : J â†’ (R âŸ¶ S)) :
  f âŠ— âˆ‘ j in s, g j = âˆ‘ j in s, f âŠ— g j :=
begin
  rw â†tensor_id_comp_id_tensor,
  let tQ := (((tensoring_left C).obj Q).map_add_hom : (R âŸ¶ S) â†’+ _),
  change _ â‰« tQ _ = _,
  rw [tQ.map_sum, preadditive.comp_sum],
  dsimp [tQ],
  simp only [tensor_id_comp_id_tensor],
end

lemma sum_tensor {P Q R S : C} {J : Type*} (s : finset J) (f : P âŸ¶ Q) (g : J â†’ (R âŸ¶ S)) :
  (âˆ‘ j in s, g j) âŠ— f = âˆ‘ j in s, g j âŠ— f :=
begin
  rw â†tensor_id_comp_id_tensor,
  let tQ := (((tensoring_right C).obj P).map_add_hom : (R âŸ¶ S) â†’+ _),
  change tQ _ â‰« _ = _,
  rw [tQ.map_sum, preadditive.sum_comp],
  dsimp [tQ],
  simp only [tensor_id_comp_id_tensor],
end

variables {C}

-- In a closed monoidal category, this would hold because
-- `tensor_left X` is a left adjoint and hence preserves all colimits.
-- In any case it is true in any preadditive category.
instance (X : C) : preserves_finite_biproducts (tensor_left X) :=
{ preserves := Î» J _, by exactI
  { preserves := Î» f,
    { preserves := Î» b i, is_bilimit_of_total _ begin
      dsimp,
      simp only [â†tensor_comp, category.comp_id, â†tensor_sum, â†tensor_id, is_bilimit.total i],
    end } } }

instance (X : C) : preserves_finite_biproducts (tensor_right X) :=
{ preserves := Î» J _, by exactI
  { preserves := Î» f,
    { preserves := Î» b i, is_bilimit_of_total _ begin
      dsimp,
      simp only [â†tensor_comp, category.comp_id, â†sum_tensor, â†tensor_id, is_bilimit.total i],
    end } } }

variables [has_finite_biproducts C]

/-- The isomorphism showing how tensor product on the left distributes over direct sums. -/
def left_distributor {J : Type} [fintype J] (X : C) (f : J â†’ C) :
  X âŠ— (â¨ f) â‰… â¨ (Î» j, X âŠ— f j) :=
(tensor_left X).map_biproduct f

@[simp]
lemma left_distributor_hom {J : Type} [fintype J] (X : C) (f : J â†’ C) :
  (left_distributor X f).hom = âˆ‘ j : J, (ğŸ™ X âŠ— biproduct.Ï€ f j) â‰« biproduct.Î¹ _ j :=
begin
  ext, dsimp [tensor_left, left_distributor],
  simp [preadditive.sum_comp, biproduct.Î¹_Ï€, comp_dite],
end

@[simp]
lemma left_distributor_inv {J : Type} [fintype J] (X : C) (f : J â†’ C) :
  (left_distributor X f).inv = âˆ‘ j : J, biproduct.Ï€ _ j â‰« (ğŸ™ X âŠ— biproduct.Î¹ f j) :=
begin
  ext, dsimp [tensor_left, left_distributor],
  simp [preadditive.comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp],
end

lemma left_distributor_assoc {J : Type} [fintype J] (X Y : C) (f : J â†’ C) :
   (as_iso (ğŸ™ X) âŠ— left_distributor Y f) â‰ªâ‰« left_distributor X _ =
     (Î±_ X Y (â¨ f)).symm â‰ªâ‰« left_distributor (X âŠ— Y) f â‰ªâ‰« biproduct.map_iso (Î» j, Î±_ X Y _) :=
begin
  ext,
  simp only [category.comp_id,  category.assoc, eq_to_hom_refl,
    iso.trans_hom, iso.symm_hom, as_iso_hom, comp_zero, comp_dite,
    preadditive.sum_comp, preadditive.comp_sum,
    tensor_sum, id_tensor_comp, tensor_iso_hom, left_distributor_hom,
    biproduct.map_iso_hom, biproduct.Î¹_map, biproduct.Î¹_Ï€,
    finset.sum_dite_irrel, finset.sum_dite_eq', finset.sum_const_zero],
  simp only [â†id_tensor_comp, biproduct.Î¹_Ï€],
  simp only [id_tensor_comp, tensor_dite, comp_dite],
  simp only [category.comp_id, comp_zero, monoidal_preadditive.tensor_zero, eq_to_hom_refl,
    tensor_id, if_true, dif_ctx_congr, finset.sum_congr, finset.mem_univ, finset.sum_dite_eq'],
  simp only [â†tensor_id, associator_naturality, iso.inv_hom_id_assoc],
end

/-- The isomorphism showing how tensor product on the right distributes over direct sums. -/
def right_distributor {J : Type} [fintype J] (X : C) (f : J â†’ C) :
  (â¨ f) âŠ— X â‰… â¨ (Î» j, f j âŠ— X)  :=
(tensor_right X).map_biproduct f

@[simp]
lemma right_distributor_hom {J : Type} [fintype J] (X : C) (f : J â†’ C) :
  (right_distributor X f).hom = âˆ‘ j : J, (biproduct.Ï€ f j âŠ— ğŸ™ X) â‰« biproduct.Î¹ _ j :=
begin
  ext, dsimp [tensor_right, right_distributor],
  simp [preadditive.sum_comp, biproduct.Î¹_Ï€, comp_dite],
end

@[simp]
lemma right_distributor_inv {J : Type} [fintype J] (X : C) (f : J â†’ C) :
  (right_distributor X f).inv = âˆ‘ j : J, biproduct.Ï€ _ j â‰« (biproduct.Î¹ f j âŠ— ğŸ™ X) :=
begin
  ext, dsimp [tensor_right, right_distributor],
  simp [preadditive.comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp],
end

lemma right_distributor_assoc {J : Type} [fintype J] (X Y : C) (f : J â†’ C) :
   (right_distributor X f âŠ— as_iso (ğŸ™ Y)) â‰ªâ‰« right_distributor Y _ =
     Î±_ (â¨ f) X Y â‰ªâ‰« right_distributor (X âŠ— Y) f â‰ªâ‰« biproduct.map_iso (Î» j, (Î±_ _ X Y).symm) :=
begin
  ext,
  simp only [category.comp_id, category.assoc, eq_to_hom_refl, iso.symm_hom,
    iso.trans_hom, as_iso_hom, comp_zero, comp_dite, preadditive.sum_comp, preadditive.comp_sum,
    sum_tensor, comp_tensor_id, tensor_iso_hom, right_distributor_hom,
    biproduct.map_iso_hom, biproduct.Î¹_map, biproduct.Î¹_Ï€,
    finset.sum_dite_irrel, finset.sum_dite_eq', finset.sum_const_zero, finset.mem_univ, if_true],
  simp only [â†comp_tensor_id, biproduct.Î¹_Ï€, dite_tensor, comp_dite],
  simp only [category.comp_id, comp_tensor_id, eq_to_hom_refl, tensor_id, comp_zero,
    monoidal_preadditive.zero_tensor,
    if_true, dif_ctx_congr, finset.mem_univ, finset.sum_congr, finset.sum_dite_eq'],
  simp only [â†tensor_id, associator_inv_naturality, iso.hom_inv_id_assoc]
end

lemma left_distributor_right_distributor_assoc
  {J : Type*} [fintype J] (X Y : C) (f : J â†’ C) :
  (left_distributor X f âŠ— as_iso (ğŸ™ Y)) â‰ªâ‰« right_distributor Y _ =
    Î±_ X (â¨ f) Y â‰ªâ‰« (as_iso (ğŸ™ X) âŠ— right_distributor Y _) â‰ªâ‰« left_distributor X _ â‰ªâ‰«
      biproduct.map_iso (Î» j, (Î±_ _ _ _).symm) :=
begin
  ext,
  simp only [category.comp_id, category.assoc, eq_to_hom_refl, iso.symm_hom,
    iso.trans_hom, as_iso_hom, comp_zero, comp_dite, preadditive.sum_comp, preadditive.comp_sum,
    sum_tensor, tensor_sum, comp_tensor_id, tensor_iso_hom,
    left_distributor_hom, right_distributor_hom,
    biproduct.map_iso_hom, biproduct.Î¹_map, biproduct.Î¹_Ï€,
    finset.sum_dite_irrel, finset.sum_dite_eq', finset.sum_const_zero, finset.mem_univ, if_true],
  simp only [â†comp_tensor_id, â†id_tensor_comp_assoc, category.assoc, biproduct.Î¹_Ï€,
    comp_dite, dite_comp, tensor_dite, dite_tensor],
  simp only [category.comp_id, category.id_comp, category.assoc, id_tensor_comp,
    comp_zero, zero_comp, monoidal_preadditive.tensor_zero, monoidal_preadditive.zero_tensor,
    comp_tensor_id, eq_to_hom_refl, tensor_id,
    if_true, dif_ctx_congr, finset.sum_congr, finset.mem_univ, finset.sum_dite_eq'],
  simp only [associator_inv_naturality, iso.hom_inv_id_assoc]
end

end category_theory
