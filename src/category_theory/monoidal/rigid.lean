/-
Copyright (c) 2021 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/

import category_theory.monoidal.category


/-!
# Rigid (autonomous) monoidal categories

This file defines rigid (autonomous) monoidal categories and the necessary theory about
exact pairings and duals.

## Main definitions

* `exact_pairing` of two objects of a monoidal category
* Type classes `has_left_dual` and `has_right_dual` that capture that a pairing exists
* The `right_adjoint_mate f` as a morphism `fá˜ : Yá˜ âŸ¶ Xá˜` for a morphism `f : X âŸ¶ Y`
* The classes of `right_rigid_category`, `left_rigid_category` and `rigid_category`

## Main statements

* `comp_right_adjoint_mate`: The adjoint mates of the composition is the composition of
  adjoint mates.

## Notations

* `Î·_` and `Îµ_` denote the coevaluation and evaluation morphism of an exact pairing.
* `Xá˜` and `á˜X` denote the right and left dual of an object, as well as the adjoint
  mate of a morphism.

## Future work

* Show that `X âŠ— Y` and `Yá˜ âŠ— Xá˜` form an exact pairing.
* Show that the left adjoint mate of the right adjoint mate of a morphism is the morphism itself.
* Simplify constructions in the case where a symmetry or braiding is present.

## References

* <https://ncatlab.org/nlab/show/rigid+monoidal+category>

## Tags

rigid category, monoidal category

-/
open category_theory

universes v vâ‚ vâ‚‚ vâ‚ƒ u uâ‚ uâ‚‚ uâ‚ƒ
noncomputable theory

namespace category_theory

variables {C : Type uâ‚} [category.{vâ‚} C] [monoidal_category C]

/-- An exact pairing is a pair of objects `X Y : C` which admit
  a coevaluation and evaluation morphism which fulfill two triangle equalities. -/
class exact_pairing (X Y : C) :=
(coevaluation [] : ğŸ™_ C âŸ¶ X âŠ— Y)
(evaluation [] : Y âŠ— X âŸ¶ ğŸ™_ C)
(coevaluation_evaluation' [] :
  (ğŸ™ Y âŠ— coevaluation) â‰« (Î±_ _ _ _).inv â‰« (evaluation âŠ— ğŸ™ Y)
  = (Ï_ Y).hom â‰« (Î»_ Y).inv . obviously)
(evaluation_coevaluation' [] :
  (coevaluation âŠ— ğŸ™ X) â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ X âŠ— evaluation)
  = (Î»_ X).hom â‰« (Ï_ X).inv . obviously)

open exact_pairing

notation `Î·_` := exact_pairing.coevaluation
notation `Îµ_` := exact_pairing.evaluation

restate_axiom coevaluation_evaluation'
attribute [reassoc, simp] exact_pairing.coevaluation_evaluation
restate_axiom evaluation_coevaluation'
attribute [reassoc, simp] exact_pairing.evaluation_coevaluation

instance exact_pairing_unit : exact_pairing (ğŸ™_ C) (ğŸ™_ C) :=
{ coevaluation := (Ï_ _).inv,
  evaluation := (Ï_ _).hom,
  coevaluation_evaluation' := by {
    rw[monoidal_category.triangle_assoc_comp_right,
      monoidal_category.unitors_inv_equal,
      monoidal_category.unitors_equal], simp },
  evaluation_coevaluation' := by {
    rw[monoidal_category.triangle_assoc_comp_right_inv_assoc,
      monoidal_category.unitors_inv_equal,
      monoidal_category.unitors_equal], simp } }

/-- A class of objects which have a right dual. -/
class has_right_dual (X : C) :=
  (right_dual : C)
  [exact : exact_pairing X right_dual]

/-- A class of objects with have a left dual. -/
class has_left_dual (Y : C) :=
  (left_dual : C)
  [exact : exact_pairing left_dual Y]

attribute [instance] has_right_dual.exact
attribute [instance] has_left_dual.exact

open exact_pairing has_right_dual has_left_dual monoidal_category

prefix `á˜`:1025 := left_dual
postfix `á˜`:1025 := right_dual

instance has_right_dual_unit : has_right_dual (ğŸ™_ C) :=
{ right_dual := ğŸ™_ C }

instance has_left_dual_unit : has_left_dual (ğŸ™_ C) :=
{ left_dual := ğŸ™_ C }

instance has_right_dual_left_dual {X : C} [has_left_dual X] : has_right_dual (á˜X) :=
{ right_dual := X }

instance has_left_dual_right_dual {X : C} [has_right_dual X] : has_left_dual Xá˜ :=
{ left_dual := X }

@[simp]
lemma left_dual_right_dual {X : C} [has_right_dual X] : á˜(Xá˜) = X := rfl

@[simp]
lemma right_dual_left_dual {X : C} [has_left_dual X] : (á˜X)á˜ = X := rfl

/-- The right adjoint mate `fá˜ : Xá˜ âŸ¶ Yá˜` of a morphism `f : X âŸ¶ Y`. -/
def right_adjoint_mate {X Y : C} [has_right_dual X] [has_right_dual Y] (f : X âŸ¶ Y) : Yá˜ âŸ¶ Xá˜ :=
(Ï_ _).inv â‰« (ğŸ™ _ âŠ— Î·_ _ _) â‰« (ğŸ™ _ âŠ— (f âŠ— ğŸ™ _))
 â‰« (Î±_ _ _ _).inv â‰« ((Îµ_ _ _) âŠ— ğŸ™ _) â‰« (Î»_ _).hom

/-- The left adjoint mate `á˜f : á˜Y âŸ¶ á˜X` of a morphism `f : X âŸ¶ Y`. -/
def left_adjoint_mate {X Y : C} [has_left_dual X] [has_left_dual Y] (f : X âŸ¶ Y) : á˜Y âŸ¶ á˜X :=
(Î»_ _).inv â‰« (Î·_ (á˜X) X âŠ— ğŸ™ _) â‰« ((ğŸ™ _ âŠ— f) âŠ— ğŸ™ _)
 â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— Îµ_ _ _) â‰« (Ï_ _).hom

notation f `á˜` := right_adjoint_mate f
notation `á˜` f := left_adjoint_mate f

@[simp]
lemma right_adjoint_mate_id {X : C} [has_right_dual X] : (ğŸ™ X)á˜ = ğŸ™ (Xá˜) :=
by simp only [right_adjoint_mate, monoidal_category.tensor_id, category.id_comp,
  coevaluation_evaluation_assoc, category.comp_id, iso.inv_hom_id]

@[simp]
lemma left_adjoint_mate_id {X : C} [has_left_dual X] : á˜(ğŸ™ X) = ğŸ™ (á˜X) :=
by simp only [left_adjoint_mate, monoidal_category.tensor_id, category.id_comp,
  evaluation_coevaluation_assoc, category.comp_id, iso.inv_hom_id]

lemma right_adjoint_mate_comp {X Y Z : C} [has_right_dual X]
  [has_right_dual Y] {f : X âŸ¶ Y} {g : Xá˜ âŸ¶ Z} :
  fá˜ â‰« g
  = (Ï_ Yá˜).inv â‰« (ğŸ™ _ âŠ— Î·_ X Xá˜) â‰« (ğŸ™ _ âŠ— f âŠ— g)
    â‰« (Î±_ Yá˜ Y Z).inv â‰« (Îµ_ Y Yá˜ âŠ— ğŸ™ _) â‰« (Î»_ Z).hom :=
begin
  dunfold right_adjoint_mate,
  rw [category.assoc, category.assoc, associator_inv_naturality_assoc,
    associator_inv_naturality_assoc, â†tensor_id_comp_id_tensor g, category.assoc, category.assoc,
    category.assoc, category.assoc, id_tensor_comp_tensor_id_assoc, â†left_unitor_naturality,
    tensor_id_comp_id_tensor_assoc],
end

lemma left_adjoint_mate_comp {X Y Z : C} [has_left_dual X] [has_left_dual Y]
  {f : X âŸ¶ Y} {g : á˜X âŸ¶ Z} :
  á˜f â‰« g
  = (Î»_ _).inv â‰« (Î·_ (á˜X) X âŠ— ğŸ™ _) â‰« ((g âŠ— f) âŠ— ğŸ™ _)
    â‰« (Î±_ _ _ _).hom â‰« (ğŸ™ _ âŠ— Îµ_ _ _) â‰« (Ï_ _).hom :=
begin
  dunfold left_adjoint_mate,
  rw [category.assoc, category.assoc, associator_naturality_assoc, associator_naturality_assoc,
  â†id_tensor_comp_tensor_id _ g, category.assoc, category.assoc, category.assoc, category.assoc,
  tensor_id_comp_id_tensor_assoc, â†right_unitor_naturality, id_tensor_comp_tensor_id_assoc],
end

/-- The composition of right adjoint mates is the adjoint mate of the composition. -/
@[reassoc]
lemma comp_right_adjoint_mate {X Y Z : C}
  [has_right_dual X] [has_right_dual Y] [has_right_dual Z] {f : X âŸ¶ Y} {g : Y âŸ¶ Z} :
  (f â‰« g)á˜ = gá˜ â‰« fá˜ :=
begin
  rw right_adjoint_mate_comp,
  simp only [right_adjoint_mate, comp_tensor_id, iso.cancel_iso_inv_left, id_tensor_comp,
    category.assoc],
  symmetry, iterate 5 { transitivity, rw [â†category.id_comp g, tensor_comp] },
  rw â†category.assoc,
  symmetry, iterate 2 { transitivity, rw â†category.assoc }, apply eq_whisker,
  repeat { rw â†id_tensor_comp }, congr' 1,
  rw [â†id_tensor_comp_tensor_id (Î»_ Xá˜).hom g, id_tensor_right_unitor_inv, category.assoc,
    category.assoc, right_unitor_inv_naturality_assoc, â†associator_naturality_assoc, tensor_id,
    tensor_id_comp_id_tensor_assoc, â†associator_naturality_assoc],
  slice_rhs 2 3 { rw [â†tensor_comp, tensor_id, category.comp_id,
    â†category.id_comp (Î·_ Y Yá˜), tensor_comp] },
  rw [â†id_tensor_comp_tensor_id _ (Î·_ Y Yá˜), â†tensor_id],
  repeat { rw category.assoc },
  rw [pentagon_hom_inv_assoc, â†associator_naturality_assoc, associator_inv_naturality_assoc],
  slice_rhs 5 7 { rw [â†comp_tensor_id, â†comp_tensor_id, evaluation_coevaluation, comp_tensor_id] },
  rw associator_inv_naturality_assoc,
  slice_rhs 4 5 { rw [â†tensor_comp, left_unitor_naturality, tensor_comp] },
  repeat { rw category.assoc },
  rw [triangle_assoc_comp_right_inv_assoc, â†left_unitor_tensor_assoc,
    left_unitor_naturality_assoc, unitors_equal, â†category.assoc, â†category.assoc], simp
end

/-- The composition of left adjoint mates is the adjoint mate of the composition. -/
@[reassoc]
lemma comp_left_adjoint_mate {X Y Z : C}
  [has_left_dual X] [has_left_dual Y] [has_left_dual Z] {f : X âŸ¶ Y} {g : Y âŸ¶ Z} :
  á˜(f â‰« g) = á˜g â‰« á˜f :=
begin
  rw left_adjoint_mate_comp,
  simp only [left_adjoint_mate, id_tensor_comp, iso.cancel_iso_inv_left,
    comp_tensor_id, category.assoc],
  symmetry, iterate 5 { transitivity, rw [â†category.id_comp g, tensor_comp] },
  rw â† category.assoc,
  symmetry, iterate 2 { transitivity, rw â†category.assoc }, apply eq_whisker,
  repeat { rw â†comp_tensor_id }, congr' 1,
  rw [â†tensor_id_comp_id_tensor g (Ï_ (á˜X)).hom, left_unitor_inv_tensor_id, category.assoc,
    category.assoc, left_unitor_inv_naturality_assoc, â†associator_inv_naturality_assoc, tensor_id,
    id_tensor_comp_tensor_id_assoc, â†associator_inv_naturality_assoc],
  slice_rhs 2 3 { rw [â†tensor_comp, tensor_id, category.comp_id,
    â†category.id_comp (Î·_ (á˜Y) Y), tensor_comp] },
  rw [â†tensor_id_comp_id_tensor (Î·_ (á˜Y) Y), â†tensor_id],
  repeat { rw category.assoc },
  rw [pentagon_inv_hom_assoc, â†associator_inv_naturality_assoc, associator_naturality_assoc],
  slice_rhs 5 7 { rw [â†id_tensor_comp, â†id_tensor_comp, coevaluation_evaluation, id_tensor_comp ]},
  rw associator_naturality_assoc,
  slice_rhs 4 5 { rw [â†tensor_comp, right_unitor_naturality, tensor_comp] },
  repeat { rw category.assoc },
  rw [triangle_assoc_comp_left_inv_assoc, â†right_unitor_tensor_assoc,
    right_unitor_naturality_assoc, â†unitors_equal, â†category.assoc, â†category.assoc], simp
end

/-- Right duals are isomorphic. -/
def right_dual_iso {X Yâ‚ Yâ‚‚ : C} (_ : exact_pairing X Yâ‚) (_ : exact_pairing X Yâ‚‚) :
  Yâ‚ â‰… Yâ‚‚ :=
{ hom := @right_adjoint_mate C _ _ X X âŸ¨Yâ‚‚âŸ© âŸ¨Yâ‚âŸ© (ğŸ™ X),
  inv := @right_adjoint_mate C _ _ X X âŸ¨Yâ‚âŸ© âŸ¨Yâ‚‚âŸ© (ğŸ™ X),
  hom_inv_id' := by rw [â†comp_right_adjoint_mate, category.comp_id, right_adjoint_mate_id],
  inv_hom_id' := by rw [â†comp_right_adjoint_mate, category.comp_id, right_adjoint_mate_id] }

/-- Left duals are isomorphic. -/
def left_dual_iso {Xâ‚ Xâ‚‚ Y : C} (pâ‚ : exact_pairing Xâ‚ Y) (pâ‚‚ : exact_pairing Xâ‚‚ Y) :
  Xâ‚ â‰… Xâ‚‚ :=
{ hom := @left_adjoint_mate C _ _ Y Y âŸ¨Xâ‚‚âŸ© âŸ¨Xâ‚âŸ© (ğŸ™ Y),
  inv := @left_adjoint_mate C _ _ Y Y âŸ¨Xâ‚âŸ© âŸ¨Xâ‚‚âŸ© (ğŸ™ Y),
  hom_inv_id' := by rw [â†comp_left_adjoint_mate, category.comp_id, left_adjoint_mate_id],
  inv_hom_id' := by rw [â†comp_left_adjoint_mate, category.comp_id, left_adjoint_mate_id] }

@[simp]
lemma right_dual_iso_id {X Y : C} (p : exact_pairing X Y) :
  right_dual_iso p p = iso.refl Y :=
by { ext, simp only [right_dual_iso, iso.refl_hom, right_adjoint_mate_id] }

@[simp]
lemma left_dual_iso_id {X Y : C} (p : exact_pairing X Y) :
  left_dual_iso p p = iso.refl X :=
by { ext, simp only [left_dual_iso, iso.refl_hom, left_adjoint_mate_id] }

/-- A right rigid monoidal category is one in which every object has a right dual. -/
class right_rigid_category (C : Type u) [category.{v} C] [monoidal_category.{v} C] :=
  [right_dual : Î  (X : C), has_right_dual X]

/-- A left rigid monoidal category is one in which every object has a right dual. -/
class left_rigid_category (C : Type u) [category.{v} C] [monoidal_category.{v} C] :=
  [left_dual : Î  (X : C), has_left_dual X]

attribute [instance, priority 100] right_rigid_category.right_dual
attribute [instance, priority 100] left_rigid_category.left_dual

/-- A rigid monoidal category is a monoidal category which is left rigid and right rigid. -/
class rigid_category (C : Type u) [category.{v} C] [monoidal_category.{v} C]
  extends right_rigid_category C, left_rigid_category C

end category_theory
