/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import category_theory.limits.shapes.pullbacks

/-!
# Properties of morphisms

We provide the basic framework for talking about properties of morphisms.
The following meta-properties are defined

* `respects_iso`: `P` respects isomorphisms if `P f â†’ P (e â‰« f)` and `P f â†’ P (f â‰« e)`, where
  `e` is an isomorphism.
* `stable_under_composition`: `P` is stable under composition if `P f â†’ P g â†’ P (f â‰« g)`.
* `stable_under_base_change`: `P` is stable under base change if `P (Y âŸ¶ S) â†’ P (X Ã—[S] Y âŸ¶ X)`.

-/

universes v u

open category_theory category_theory.limits opposite

noncomputable theory

namespace category_theory

variables (C : Type u) [category.{v} C]

/-- A `morphism_property C` is a class of morphisms between objects in `C`. -/
@[derive complete_lattice]
def morphism_property := âˆ€ â¦ƒX Y : Câ¦„ (f : X âŸ¶ Y), Prop

instance : inhabited (morphism_property C) := âŸ¨âŠ¤âŸ©

variable {C}

namespace morphism_property

/-- A morphism property `respects_iso` if it still holds when composed with an isomorphism -/
def respects_iso (P : morphism_property C) : Prop :=
  (âˆ€ {X Y Z} (e : X â‰… Y) (f : Y âŸ¶ Z), P f â†’ P (e.hom â‰« f)) âˆ§
  (âˆ€ {X Y Z} (e : Y â‰… Z) (f : X âŸ¶ Y), P f â†’ P (f â‰« e.hom))

/-- A morphism property is `stable_under_composition` if the composition of two such morphisms
still falls in the class. -/
def stable_under_composition (P : morphism_property C) : Prop :=
  âˆ€ â¦ƒX Y Zâ¦„ (f : X âŸ¶ Y) (g : Y âŸ¶ Z), P f â†’ P g â†’ P (f â‰« g)

/-- A morphism property is `stable_under_composition` if the base change of such a morphism
still falls in the class. -/
def stable_under_base_change [has_pullbacks C] (P : morphism_property C) : Prop :=
âˆ€ â¦ƒX Y S : Câ¦„ (f : X âŸ¶ S) (g : Y âŸ¶ S), P g â†’ P (pullback.fst : pullback f g âŸ¶ X)

lemma stable_under_composition.respects_iso {P : morphism_property C}
  (hP : stable_under_composition P) (hP' : âˆ€ {X Y} (e : X â‰… Y), P e.hom) : respects_iso P :=
âŸ¨Î» X Y Z e f hf, hP _ _ (hP' e) hf, Î» X Y Z e f hf, hP _ _ hf (hP' e)âŸ©

lemma respects_iso.cancel_left_is_iso {P : morphism_property C}
  (hP : respects_iso P) {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [is_iso f] :
    P (f â‰« g) â†” P g :=
âŸ¨Î» h, by simpa using hP.1 (as_iso f).symm (f â‰« g) h, hP.1 (as_iso f) gâŸ©

lemma respects_iso.cancel_right_is_iso {P : morphism_property C}
  (hP : respects_iso P) {X Y Z : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) [is_iso g] :
    P (f â‰« g) â†” P f :=
âŸ¨Î» h, by simpa using hP.2 (as_iso g).symm (f â‰« g) h, hP.2 (as_iso g) fâŸ©

-- This is here to mirror `stable_under_base_change.snd`.
@[nolint unused_arguments]
lemma stable_under_base_change.fst [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) (hP' : respects_iso P) {X Y S : C} (f : X âŸ¶ S)
  (g : Y âŸ¶ S) (H : P g) : P (pullback.fst : pullback f g âŸ¶ X) :=
hP f g H

lemma stable_under_base_change.snd [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) (hP' : respects_iso P) {X Y S : C} (f : X âŸ¶ S)
  (g : Y âŸ¶ S) (H : P f) : P (pullback.snd : pullback f g âŸ¶ Y) :=
begin
  rw [â† pullback_symmetry_hom_comp_fst, hP'.cancel_left_is_iso],
  exact hP g f H
end

lemma stable_under_base_change.base_change_obj [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) (hP' : respects_iso P) {S S' : C} (f : S' âŸ¶ S)
  (X : over S) (H : P X.hom) : P ((base_change f).obj X).hom :=
hP.snd hP' X.hom f H

lemma stable_under_base_change.base_change_map [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) (hP' : respects_iso P) {S S' : C} (f : S' âŸ¶ S)
  {X Y : over S} (g : X âŸ¶ Y) (H : P g.left) : P ((base_change f).map g).left :=
begin
  let e := pullback_right_pullback_fst_iso Y.hom f g.left â‰ªâ‰«
    pullback.congr_hom (g.w.trans (category.comp_id _)) rfl,
  have : e.inv â‰« pullback.snd = ((base_change f).map g).left,
  { apply pullback.hom_ext; dsimp; simp },
  rw [â† this, hP'.cancel_left_is_iso],
  apply hP.snd hP',
  exact H
end

lemma stable_under_base_change.pullback_map [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) (hP' : respects_iso P)
  (hP'' : stable_under_composition P) {S X X' Y Y' : C}
  {f : X âŸ¶ S} {g : Y âŸ¶ S} {f' : X' âŸ¶ S} {g' : Y' âŸ¶ S} {iâ‚ : X âŸ¶ X'} {iâ‚‚ : Y âŸ¶ Y'}
  (hâ‚ : P iâ‚) (hâ‚‚ : P iâ‚‚) (eâ‚ : f = iâ‚ â‰« f') (eâ‚‚ : g = iâ‚‚ â‰« g') :
    P (pullback.map f g f' g' iâ‚ iâ‚‚ (ğŸ™ _)
      ((category.comp_id _).trans eâ‚) ((category.comp_id _).trans eâ‚‚)) :=
begin
  have : pullback.map f g f' g' iâ‚ iâ‚‚ (ğŸ™ _)
    ((category.comp_id _).trans eâ‚) ((category.comp_id _).trans eâ‚‚) =
      ((pullback_symmetry _ _).hom â‰«
      ((base_change _).map (over.hom_mk _ eâ‚‚.symm : over.mk g âŸ¶ over.mk g')).left) â‰«
      (pullback_symmetry _ _).hom â‰«
      ((base_change g').map (over.hom_mk _ eâ‚.symm : over.mk f âŸ¶ over.mk f')).left,
  { apply pullback.hom_ext; dsimp; simp },
  rw this,
  apply hP''; rw hP'.cancel_left_is_iso,
  exacts [hP.base_change_map hP' _ (over.hom_mk _ eâ‚‚.symm : over.mk g âŸ¶ over.mk g') hâ‚‚,
    hP.base_change_map hP' _ (over.hom_mk _ eâ‚.symm : over.mk f âŸ¶ over.mk f') hâ‚],
end

end morphism_property

end category_theory
