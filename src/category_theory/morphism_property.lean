/-
Copyright (c) 2022 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import category_theory.limits.shapes.diagonal
import category_theory.arrow
import category_theory.limits.shapes.comm_sq
import category_theory.concrete_category.basic

/-!
# Properties of morphisms

We provide the basic framework for talking about properties of morphisms.
The following meta-properties are defined

* `respects_iso`: `P` respects isomorphisms if `P f ‚Üí P (e ‚â´ f)` and `P f ‚Üí P (f ‚â´ e)`, where
  `e` is an isomorphism.
* `stable_under_composition`: `P` is stable under composition if `P f ‚Üí P g ‚Üí P (f ‚â´ g)`.
* `stable_under_base_change`: `P` is stable under base change if in all pullback
  squares, the left map satisfies `P` if the right map satisfies it.
* `stable_under_cobase_change`: `P` is stable under cobase change if in all pushout
  squares, the right map satisfies `P` if the left map satisfies it.

-/

universes v u

open category_theory category_theory.limits opposite

noncomputable theory

namespace category_theory

variables (C : Type u) [category.{v} C] {D : Type*} [category D]

/-- A `morphism_property C` is a class of morphisms between objects in `C`. -/
@[derive complete_lattice]
def morphism_property := ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y), Prop

instance : inhabited (morphism_property C) := ‚ü®‚ä§‚ü©

variable {C}

namespace morphism_property

instance : has_subset (morphism_property C) :=
‚ü®Œª P‚ÇÅ P‚ÇÇ, ‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) (hf : P‚ÇÅ f), P‚ÇÇ f‚ü©
instance : has_inter (morphism_property C) :=
‚ü®Œª P‚ÇÅ P‚ÇÇ X Y f, P‚ÇÅ f ‚àß P‚ÇÇ f‚ü©

/-- The morphism property in `C·µí·µñ` associated to a morphism property in `C` -/
@[simp] def op (P : morphism_property C) : morphism_property C·µí·µñ := Œª X Y f, P f.unop

/-- The morphism property in `C` associated to a morphism property in `C·µí·µñ` -/
@[simp] def unop (P : morphism_property C·µí·µñ) : morphism_property C := Œª X Y f, P f.op

lemma unop_op (P : morphism_property C) : P.op.unop = P := rfl
lemma op_unop (P : morphism_property C·µí·µñ) : P.unop.op = P := rfl

/-- The inverse image of a `morphism_property D` by a functor `C ‚•§ D` -/
def inverse_image (P : morphism_property D) (F : C ‚•§ D) : morphism_property C :=
Œª X Y f, P (F.map f)

/-- A morphism property `respects_iso` if it still holds when composed with an isomorphism -/
def respects_iso (P : morphism_property C) : Prop :=
  (‚àÄ {X Y Z} (e : X ‚âÖ Y) (f : Y ‚ü∂ Z), P f ‚Üí P (e.hom ‚â´ f)) ‚àß
  (‚àÄ {X Y Z} (e : Y ‚âÖ Z) (f : X ‚ü∂ Y), P f ‚Üí P (f ‚â´ e.hom))

lemma respects_iso.op {P : morphism_property C} (h : respects_iso P) : respects_iso P.op :=
‚ü®Œª X Y Z e f hf, h.2 e.unop f.unop hf, Œª X Y Z e f hf, h.1 e.unop f.unop hf‚ü©

lemma respects_iso.unop {P : morphism_property C·µí·µñ} (h : respects_iso P) : respects_iso P.unop :=
‚ü®Œª X Y Z e f hf, h.2 e.op f.op hf, Œª X Y Z e f hf, h.1 e.op f.op hf‚ü©

/-- A morphism property is `stable_under_composition` if the composition of two such morphisms
still falls in the class. -/
def stable_under_composition (P : morphism_property C) : Prop :=
  ‚àÄ ‚¶ÉX Y Z‚¶Ñ (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z), P f ‚Üí P g ‚Üí P (f ‚â´ g)

lemma stable_under_composition.op {P : morphism_property C} (h : stable_under_composition P) :
  stable_under_composition P.op := Œª X Y Z f g hf hg, h g.unop f.unop hg hf

lemma stable_under_composition.unop {P : morphism_property C·µí·µñ} (h : stable_under_composition P) :
  stable_under_composition P.unop := Œª X Y Z f g hf hg, h g.op f.op hg hf

/-- A morphism property is `stable_under_inverse` if the inverse of a morphism satisfying
the property still falls in the class. -/
def stable_under_inverse (P : morphism_property C) : Prop :=
‚àÄ ‚¶ÉX Y‚¶Ñ (e : X ‚âÖ Y), P e.hom ‚Üí P e.inv

lemma stable_under_inverse.op {P : morphism_property C} (h : stable_under_inverse P) :
  stable_under_inverse P.op := Œª X Y e he, h e.unop he

lemma stable_under_inverse.unop {P : morphism_property C·µí·µñ} (h : stable_under_inverse P) :
  stable_under_inverse P.unop := Œª X Y e he, h e.op he

/-- A morphism property is `stable_under_base_change` if the base change of such a morphism
still falls in the class. -/
def stable_under_base_change (P : morphism_property C) : Prop :=
‚àÄ ‚¶ÉX Y Y' S : C‚¶Ñ ‚¶Éf : X ‚ü∂ S‚¶Ñ ‚¶Ég : Y ‚ü∂ S‚¶Ñ ‚¶Éf' : Y' ‚ü∂ Y‚¶Ñ ‚¶Ég' : Y' ‚ü∂ X‚¶Ñ
  (sq : is_pullback f' g' g f) (hg : P g), P g'

/-- A morphism property is `stable_under_cobase_change` if the cobase change of such a morphism
still falls in the class. -/
def stable_under_cobase_change (P : morphism_property C) : Prop :=
‚àÄ ‚¶ÉA A' B B' : C‚¶Ñ ‚¶Éf : A ‚ü∂ A'‚¶Ñ ‚¶Ég : A ‚ü∂ B‚¶Ñ ‚¶Éf' : B ‚ü∂ B'‚¶Ñ ‚¶Ég' : A' ‚ü∂ B'‚¶Ñ
  (sq : is_pushout g f f' g') (hf : P f), P f'

lemma stable_under_composition.respects_iso {P : morphism_property C}
  (hP : stable_under_composition P) (hP' : ‚àÄ {X Y} (e : X ‚âÖ Y), P e.hom) : respects_iso P :=
‚ü®Œª X Y Z e f hf, hP _ _ (hP' e) hf, Œª X Y Z e f hf, hP _ _ hf (hP' e)‚ü©

lemma respects_iso.cancel_left_is_iso {P : morphism_property C}
  (hP : respects_iso P) {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [is_iso f] :
    P (f ‚â´ g) ‚Üî P g :=
‚ü®Œª h, by simpa using hP.1 (as_iso f).symm (f ‚â´ g) h, hP.1 (as_iso f) g‚ü©

lemma respects_iso.cancel_right_is_iso {P : morphism_property C}
  (hP : respects_iso P) {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [is_iso g] :
    P (f ‚â´ g) ‚Üî P f :=
‚ü®Œª h, by simpa using hP.2 (as_iso g).symm (f ‚â´ g) h, hP.2 (as_iso g) f‚ü©

lemma respects_iso.arrow_iso_iff {P : morphism_property C}
  (hP : respects_iso P) {f g : arrow C} (e : f ‚âÖ g) : P f.hom ‚Üî P g.hom :=
by { rw [‚Üê arrow.inv_left_hom_right e.hom, hP.cancel_left_is_iso, hP.cancel_right_is_iso], refl }

lemma respects_iso.arrow_mk_iso_iff {P : morphism_property C}
  (hP : respects_iso P) {W X Y Z : C} {f : W ‚ü∂ X} {g : Y ‚ü∂ Z} (e : arrow.mk f ‚âÖ arrow.mk g) :
    P f ‚Üî P g :=
hP.arrow_iso_iff e

lemma respects_iso.of_respects_arrow_iso (P : morphism_property C)
  (hP : ‚àÄ (f g : arrow C) (e : f ‚âÖ g) (hf : P f.hom), P g.hom) : respects_iso P :=
begin
  split,
  { intros X Y Z e f hf,
    refine hP (arrow.mk f) (arrow.mk (e.hom ‚â´ f)) (arrow.iso_mk e.symm (iso.refl _) _) hf,
    dsimp,
    simp only [iso.inv_hom_id_assoc, category.comp_id], },
  { intros X Y Z e f hf,
    refine hP (arrow.mk f) (arrow.mk (f ‚â´ e.hom)) (arrow.iso_mk (iso.refl _) e _) hf,
    dsimp,
    simp only [category.id_comp], },
end

lemma stable_under_base_change.mk {P : morphism_property C} [has_pullbacks C]
  (hP‚ÇÅ : respects_iso P)
  (hP‚ÇÇ : ‚àÄ (X Y S : C) (f : X ‚ü∂ S) (g : Y ‚ü∂ S) (hg : P g), P (pullback.fst : pullback f g ‚ü∂ X)) :
  stable_under_base_change P := Œª X Y Y' S f g f' g' sq hg,
begin
  let e := sq.flip.iso_pullback,
  rw [‚Üê hP‚ÇÅ.cancel_left_is_iso e.inv, sq.flip.iso_pullback_inv_fst],
  exact hP‚ÇÇ _ _ _ f g hg,
end

lemma stable_under_base_change.respects_iso {P : morphism_property C}
  (hP : stable_under_base_change P) : respects_iso P :=
begin
  apply respects_iso.of_respects_arrow_iso,
  intros f g e,
  exact hP (is_pullback.of_horiz_is_iso (comm_sq.mk e.inv.w)),
end

lemma stable_under_base_change.fst {P : morphism_property C}
  (hP : stable_under_base_change P) {X Y S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [has_pullback f g]
  (H : P g) : P (pullback.fst : pullback f g ‚ü∂ X) :=
hP (is_pullback.of_has_pullback f g).flip H

lemma stable_under_base_change.snd {P : morphism_property C}
  (hP : stable_under_base_change P) {X Y S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [has_pullback f g]
  (H : P f) : P (pullback.snd : pullback f g ‚ü∂ Y) :=
hP (is_pullback.of_has_pullback f g) H

lemma stable_under_base_change.base_change_obj [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) {S S' : C} (f : S' ‚ü∂ S)
  (X : over S) (H : P X.hom) : P ((base_change f).obj X).hom :=
hP.snd X.hom f H

lemma stable_under_base_change.base_change_map [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) {S S' : C} (f : S' ‚ü∂ S)
  {X Y : over S} (g : X ‚ü∂ Y) (H : P g.left) : P ((base_change f).map g).left :=
begin
  let e := pullback_right_pullback_fst_iso Y.hom f g.left ‚â™‚â´
    pullback.congr_hom (g.w.trans (category.comp_id _)) rfl,
  have : e.inv ‚â´ pullback.snd = ((base_change f).map g).left,
  { apply pullback.hom_ext; dsimp; simp },
  rw [‚Üê this, hP.respects_iso.cancel_left_is_iso],
  exact hP.snd _ _ H,
end

lemma stable_under_base_change.pullback_map [has_pullbacks C] {P : morphism_property C}
  (hP : stable_under_base_change P) (hP' : stable_under_composition P) {S X X' Y Y' : C}
  {f : X ‚ü∂ S} {g : Y ‚ü∂ S} {f' : X' ‚ü∂ S} {g' : Y' ‚ü∂ S} {i‚ÇÅ : X ‚ü∂ X'} {i‚ÇÇ : Y ‚ü∂ Y'}
  (h‚ÇÅ : P i‚ÇÅ) (h‚ÇÇ : P i‚ÇÇ) (e‚ÇÅ : f = i‚ÇÅ ‚â´ f') (e‚ÇÇ : g = i‚ÇÇ ‚â´ g') :
    P (pullback.map f g f' g' i‚ÇÅ i‚ÇÇ (ùüô _)
      ((category.comp_id _).trans e‚ÇÅ) ((category.comp_id _).trans e‚ÇÇ)) :=
begin
  have : pullback.map f g f' g' i‚ÇÅ i‚ÇÇ (ùüô _)
    ((category.comp_id _).trans e‚ÇÅ) ((category.comp_id _).trans e‚ÇÇ) =
      ((pullback_symmetry _ _).hom ‚â´
      ((base_change _).map (over.hom_mk _ e‚ÇÇ.symm : over.mk g ‚ü∂ over.mk g')).left) ‚â´
      (pullback_symmetry _ _).hom ‚â´
      ((base_change g').map (over.hom_mk _ e‚ÇÅ.symm : over.mk f ‚ü∂ over.mk f')).left,
  { apply pullback.hom_ext; dsimp; simp },
  rw this,
  apply hP'; rw hP.respects_iso.cancel_left_is_iso,
  exacts [hP.base_change_map _ (over.hom_mk _ e‚ÇÇ.symm : over.mk g ‚ü∂ over.mk g') h‚ÇÇ,
    hP.base_change_map _ (over.hom_mk _ e‚ÇÅ.symm : over.mk f ‚ü∂ over.mk f') h‚ÇÅ],
end

lemma stable_under_cobase_change.mk {P : morphism_property C} [has_pushouts C]
  (hP‚ÇÅ : respects_iso P)
  (hP‚ÇÇ : ‚àÄ (A B A' : C) (f : A ‚ü∂ A') (g : A ‚ü∂ B) (hf : P f), P (pushout.inr : B ‚ü∂ pushout f g)) :
  stable_under_cobase_change P := Œª A A' B B' f g f' g' sq hf,
begin
  let e := sq.flip.iso_pushout,
  rw [‚Üê hP‚ÇÅ.cancel_right_is_iso _ e.hom, sq.flip.inr_iso_pushout_hom],
  exact hP‚ÇÇ _ _ _ f g hf,
end

lemma stable_under_cobase_change.respects_iso {P : morphism_property C}
  (hP : stable_under_cobase_change P) : respects_iso P :=
respects_iso.of_respects_arrow_iso _ (Œª f g e, hP (is_pushout.of_horiz_is_iso (comm_sq.mk e.hom.w)))

lemma stable_under_cobase_change.inl {P : morphism_property C}
  (hP : stable_under_cobase_change P) {A B A' : C} (f : A ‚ü∂ A') (g : A ‚ü∂ B) [has_pushout f g]
  (H : P g) : P (pushout.inl : A' ‚ü∂ pushout f g) :=
hP (is_pushout.of_has_pushout f g) H

lemma stable_under_cobase_change.inr {P : morphism_property C}
  (hP : stable_under_cobase_change P) {A B A' : C} (f : A ‚ü∂ A') (g : A ‚ü∂ B) [has_pushout f g]
  (H : P f) : P (pushout.inr : B ‚ü∂ pushout f g) :=
hP (is_pushout.of_has_pushout f g).flip H

lemma stable_under_cobase_change.op {P : morphism_property C}
  (hP : stable_under_cobase_change P) : stable_under_base_change P.op :=
Œª X Y Y' S f g f' g' sq hg, hP sq.unop hg

lemma stable_under_cobase_change.unop {P : morphism_property C·µí·µñ}
  (hP : stable_under_cobase_change P) : stable_under_base_change P.unop :=
Œª X Y Y' S f g f' g' sq hg, hP sq.op hg

lemma stable_under_base_change.op {P : morphism_property C}
  (hP : stable_under_base_change P) : stable_under_cobase_change P.op :=
Œª A A' B B' f g f' g' sq hf, hP sq.unop hf

lemma stable_under_base_change.unop {P : morphism_property C·µí·µñ}
  (hP : stable_under_base_change P) : stable_under_cobase_change P.unop :=
Œª A A' B B' f g f' g' sq hf, hP sq.op hf

/-- If `P : morphism_property C` and `F : C ‚•§ D`, then
`P.is_inverted_by F` means that all morphisms in `P` are mapped by `F`
to isomorphisms in `D`. -/
def is_inverted_by (P : morphism_property C) (F : C ‚•§ D) : Prop :=
‚àÄ ‚¶ÉX Y : C‚¶Ñ (f : X ‚ü∂ Y) (hf : P f), is_iso (F.map f)

namespace is_inverted_by

lemma of_comp {C‚ÇÅ C‚ÇÇ C‚ÇÉ : Type*} [category C‚ÇÅ] [category C‚ÇÇ] [category C‚ÇÉ]
  (W : morphism_property C‚ÇÅ) (F : C‚ÇÅ ‚•§ C‚ÇÇ) (hF : W.is_inverted_by F) (G : C‚ÇÇ ‚•§ C‚ÇÉ) :
  W.is_inverted_by (F ‚ãô G) :=
Œª X Y f hf, by { haveI := hF f hf, dsimp, apply_instance, }

lemma op {W : morphism_property C} {L : C ‚•§ D} (h : W.is_inverted_by L) :
  W.op.is_inverted_by L.op :=
Œª X Y f hf, by { haveI := h f.unop hf, dsimp, apply_instance, }

lemma right_op {W : morphism_property C} {L : C·µí·µñ ‚•§ D} (h : W.op.is_inverted_by L) :
  W.is_inverted_by L.right_op :=
Œª X Y f hf, by { haveI := h f.op hf, dsimp, apply_instance, }

lemma left_op {W : morphism_property C} {L : C ‚•§ D·µí·µñ} (h : W.is_inverted_by L) :
  W.op.is_inverted_by L.left_op :=
Œª X Y f hf, by { haveI := h f.unop hf, dsimp, apply_instance, }

lemma unop {W : morphism_property C} {L : C·µí·µñ ‚•§ D·µí·µñ} (h : W.op.is_inverted_by L) :
  W.is_inverted_by L.unop :=
Œª X Y f hf, by { haveI := h f.op hf, dsimp, apply_instance, }

end is_inverted_by

/-- Given `app : Œ† X, F‚ÇÅ.obj X ‚ü∂ F‚ÇÇ.obj X` where `F‚ÇÅ` and `F‚ÇÇ` are two functors,
this is the `morphism_property C` satisfied by the morphisms in `C` with respect
to whom `app` is natural. -/
@[simp]
def naturality_property {F‚ÇÅ F‚ÇÇ : C ‚•§ D} (app : Œ† X, F‚ÇÅ.obj X ‚ü∂ F‚ÇÇ.obj X) :
  morphism_property C := Œª X Y f, F‚ÇÅ.map f ‚â´ app Y = app X ‚â´ F‚ÇÇ.map f

namespace naturality_property

lemma is_stable_under_composition {F‚ÇÅ F‚ÇÇ : C ‚•§ D} (app : Œ† X, F‚ÇÅ.obj X ‚ü∂ F‚ÇÇ.obj X) :
  (naturality_property app).stable_under_composition := Œª X Y Z f g hf hg,
begin
  simp only [naturality_property] at ‚ä¢ hf hg,
  simp only [functor.map_comp, category.assoc, hg],
  slice_lhs 1 2 { rw hf },
  rw category.assoc,
end

lemma is_stable_under_inverse {F‚ÇÅ F‚ÇÇ : C ‚•§ D} (app : Œ† X, F‚ÇÅ.obj X ‚ü∂ F‚ÇÇ.obj X) :
  (naturality_property app).stable_under_inverse := Œª X Y e he,
begin
  simp only [naturality_property] at ‚ä¢ he,
  rw ‚Üê cancel_epi (F‚ÇÅ.map e.hom),
  slice_rhs 1 2 { rw he },
  simp only [category.assoc, ‚Üê F‚ÇÅ.map_comp_assoc, ‚Üê F‚ÇÇ.map_comp,
    e.hom_inv_id, functor.map_id, category.id_comp, category.comp_id],
end

end naturality_property

lemma respects_iso.inverse_image {P : morphism_property D} (h : respects_iso P) (F : C ‚•§ D) :
  respects_iso (P.inverse_image F) :=
begin
  split,
  all_goals
  { intros X Y Z e f hf,
    dsimp [inverse_image],
    rw F.map_comp, },
  exacts [h.1 (F.map_iso e) (F.map f) hf, h.2 (F.map_iso e) (F.map f) hf],
end

lemma stable_under_composition.inverse_image {P : morphism_property D}
  (h : stable_under_composition P) (F : C ‚•§ D) : stable_under_composition (P.inverse_image F) :=
Œª X Y Z f g hf hg, by simpa only [‚Üê F.map_comp] using h (F.map f) (F.map g) hf hg

variable (C)

/-- The `morphism_property C` satisfied by isomorphisms in `C`. -/
def isomorphisms : morphism_property C := Œª X Y f, is_iso f

/-- The `morphism_property C` satisfied by monomorphisms in `C`. -/
def monomorphisms : morphism_property C := Œª X Y f, mono f

/-- The `morphism_property C` satisfied by epimorphisms in `C`. -/
def epimorphisms : morphism_property C := Œª X Y f, epi f

section

variables {C} {X Y : C} (f : X ‚ü∂ Y)

@[simp] lemma isomorphisms.iff : (isomorphisms C) f ‚Üî is_iso f := by refl
@[simp] lemma monomorphisms.iff : (monomorphisms C) f ‚Üî mono f := by refl
@[simp] lemma epimorphisms.iff : (epimorphisms C) f ‚Üî epi f := by refl

lemma isomorphisms.infer_property [hf : is_iso f] : (isomorphisms C) f := hf
lemma monomorphisms.infer_property [hf : mono f] : (monomorphisms C) f := hf
lemma epimorphisms.infer_property [hf : epi f] : (epimorphisms C) f := hf

end

lemma respects_iso.monomorphisms : respects_iso (monomorphisms C) :=
by { split; { intros X Y Z e f, simp only [monomorphisms.iff], introI, apply mono_comp, }, }

lemma respects_iso.epimorphisms : respects_iso (epimorphisms C) :=
by { split; { intros X Y Z e f, simp only [epimorphisms.iff], introI, apply epi_comp, }, }

lemma respects_iso.isomorphisms : respects_iso (isomorphisms C) :=
by { split; { intros X Y Z e f, simp only [isomorphisms.iff], introI, apply_instance, }, }

lemma stable_under_composition.isomorphisms : stable_under_composition (isomorphisms C) :=
Œª X Y Z f g hf hg, begin
  rw isomorphisms.iff at hf hg ‚ä¢,
  haveI := hf,
  haveI := hg,
  apply_instance,
end

lemma stable_under_composition.monomorphisms : stable_under_composition (monomorphisms C) :=
Œª X Y Z f g hf hg, begin
  rw monomorphisms.iff at hf hg ‚ä¢,
  haveI := hf,
  haveI := hg,
  apply mono_comp,
end

lemma stable_under_composition.epimorphisms : stable_under_composition (epimorphisms C) :=
Œª X Y Z f g hf hg, begin
  rw epimorphisms.iff at hf hg ‚ä¢,
  haveI := hf,
  haveI := hg,
  apply epi_comp,
end

variable {C}

/-- The full subcategory of `C ‚•§ D` consisting of functors inverting morphisms in `W` -/
@[derive category, nolint has_nonempty_instance]
def functors_inverting (W : morphism_property C) (D : Type*) [category D] :=
full_subcategory (Œª (F : C ‚•§ D), W.is_inverted_by F)

/-- A constructor for `W.functors_inverting D` -/
def functors_inverting.mk {W : morphism_property C} {D : Type*} [category D]
(F : C ‚•§ D) (hF : W.is_inverted_by F) : W.functors_inverting D := ‚ü®F, hF‚ü©

lemma is_inverted_by.iff_of_iso (W : morphism_property C) {F‚ÇÅ F‚ÇÇ : C ‚•§ D} (e : F‚ÇÅ ‚âÖ F‚ÇÇ) :
  W.is_inverted_by F‚ÇÅ ‚Üî W.is_inverted_by F‚ÇÇ :=
begin
  suffices : ‚àÄ (X Y : C) (f : X ‚ü∂ Y), is_iso (F‚ÇÅ.map f) ‚Üî is_iso (F‚ÇÇ.map f),
  { split,
    exact Œª h X Y f hf, by { rw ‚Üê this, exact h f hf, },
    exact Œª h X Y f hf, by { rw this, exact h f hf, }, },
  intros X Y f,
  exact (respects_iso.isomorphisms D).arrow_mk_iso_iff
    (arrow.iso_mk (e.app X) (e.app Y) (by simp)),
end

section diagonal

variables [has_pullbacks C] {P : morphism_property C}

/-- For `P : morphism_property C`, `P.diagonal` is a morphism property that holds for `f : X ‚ü∂ Y`
whenever `P` holds for `X ‚ü∂ Y x‚Çì Y`. -/
def diagonal (P : morphism_property C) : morphism_property C :=
Œª X Y f, P (pullback.diagonal f)

lemma diagonal_iff {X Y : C} {f : X ‚ü∂ Y} : P.diagonal f ‚Üî P (pullback.diagonal f) := iff.rfl

lemma respects_iso.diagonal (hP : P.respects_iso) : P.diagonal.respects_iso :=
begin
  split,
  { introv H,
    rwa [diagonal_iff, pullback.diagonal_comp, hP.cancel_left_is_iso, hP.cancel_left_is_iso,
      ‚Üê hP.cancel_right_is_iso _ _, ‚Üê pullback.condition, hP.cancel_left_is_iso],
    apply_instance },
  { introv H,
    delta diagonal,
    rwa [pullback.diagonal_comp, hP.cancel_right_is_iso] }
end

lemma stable_under_composition.diagonal
  (hP : stable_under_composition P) (hP' : respects_iso P) (hP'' : stable_under_base_change P) :
  P.diagonal.stable_under_composition :=
begin
  introv X h‚ÇÅ h‚ÇÇ,
  rw [diagonal_iff, pullback.diagonal_comp],
  apply hP, { assumption },
  rw hP'.cancel_left_is_iso,
  apply hP''.snd,
  assumption
end

lemma stable_under_base_change.diagonal
  (hP : stable_under_base_change P) (hP' : respects_iso P) :
  P.diagonal.stable_under_base_change :=
stable_under_base_change.mk hP'.diagonal
begin
  introv h,
  rw [diagonal_iff, diagonal_pullback_fst, hP'.cancel_left_is_iso, hP'.cancel_right_is_iso],
  convert hP.base_change_map f _ _; simp; assumption
end

end diagonal

section universally

/-- `P.universally` holds for a morphism `f : X ‚ü∂ Y` iff `P` holds for all `X √ó[Y] Y' ‚ü∂ Y'`. -/
def universally (P : morphism_property C) : morphism_property C :=
Œª X Y f, ‚àÄ ‚¶ÉX' Y' : C‚¶Ñ (i‚ÇÅ : X' ‚ü∂ X) (i‚ÇÇ : Y' ‚ü∂ Y) (f' : X' ‚ü∂ Y')
  (h : is_pullback f' i‚ÇÅ i‚ÇÇ f), P f'

lemma universally_respects_iso (P : morphism_property C) :
  P.universally.respects_iso :=
begin
  constructor,
  { intros X Y Z e f hf X' Z' i‚ÇÅ i‚ÇÇ f' H,
    have : is_pullback (ùüô _) (i‚ÇÅ ‚â´ e.hom) i‚ÇÅ e.inv := is_pullback.of_horiz_is_iso
      ‚ü®by rw [category.id_comp, category.assoc, e.hom_inv_id, category.comp_id]‚ü©,
    replace this := this.paste_horiz H,
    rw [iso.inv_hom_id_assoc, category.id_comp] at this,
    exact hf _ _ _ this },
  { intros X Y Z e f hf X' Z' i‚ÇÅ i‚ÇÇ f' H,
    have : is_pullback (ùüô _) i‚ÇÇ (i‚ÇÇ ‚â´ e.inv) e.inv :=
      is_pullback.of_horiz_is_iso ‚ü®category.id_comp _‚ü©,
    replace this := H.paste_horiz this,
    rw [category.assoc, iso.hom_inv_id, category.comp_id, category.comp_id] at this,
    exact hf _ _ _ this },
end

lemma universally_stable_under_base_change (P : morphism_property C) :
  P.universally.stable_under_base_change :=
Œª X Y Y' S f g f' g' H h‚ÇÅ Y'' X'' i‚ÇÅ i‚ÇÇ f'' H', h‚ÇÅ _ _ _ (H'.paste_vert H.flip)

lemma stable_under_composition.universally [has_pullbacks C]
  {P : morphism_property C} (hP : P.stable_under_composition) :
  P.universally.stable_under_composition :=
begin
  intros X Y Z f g hf hg X' Z' i‚ÇÅ i‚ÇÇ f' H,
  have := pullback.lift_fst _ _ (H.w.trans (category.assoc _ _ _).symm),
  rw ‚Üê this at H ‚ä¢,
  apply hP _ _ _ (hg _ _ _ $ is_pullback.of_has_pullback _ _),
  exact hf _ _ _ (H.of_right (pullback.lift_snd _ _ _) (is_pullback.of_has_pullback i‚ÇÇ g))
end

lemma universally_le (P : morphism_property C) :
  P.universally ‚â§ P :=
begin
  intros X Y f hf,
  exact hf (ùüô _) (ùüô _) _ (is_pullback.of_vert_is_iso ‚ü®by rw [category.comp_id, category.id_comp]‚ü©)
end

lemma stable_under_base_change.universally_eq
  {P : morphism_property C} (hP : P.stable_under_base_change) :
  P.universally = P :=
P.universally_le.antisymm $ Œª X Y f hf X' Y' i‚ÇÅ i‚ÇÇ f' H, hP H.flip hf

lemma universally_mono : monotone (universally : morphism_property C ‚Üí morphism_property C) :=
Œª P‚ÇÅ P‚ÇÇ h X Y f h‚ÇÅ X' Y' i‚ÇÅ i‚ÇÇ f' H, h _ _ _ (h‚ÇÅ _ _ _ H)

end universally

section bijective

variables [concrete_category C]

open function

local attribute [instance] concrete_category.has_coe_to_fun concrete_category.has_coe_to_sort

variable (C)

/-- Injectiveness (in a concrete category) as a `morphism_property` -/
protected def injective : morphism_property C := Œª X Y f, injective f

/-- Surjectiveness (in a concrete category) as a `morphism_property` -/
protected def surjective : morphism_property C := Œª X Y f, surjective f

/-- Bijectiveness (in a concrete category) as a `morphism_property` -/
protected def bijective : morphism_property C := Œª X Y f, bijective f

lemma bijective_eq_sup : morphism_property.bijective C =
  morphism_property.injective C ‚äì morphism_property.surjective C :=
rfl

lemma injective_stable_under_composition :
  (morphism_property.injective C).stable_under_composition :=
Œª X Y Z f g hf hg, by { delta morphism_property.injective, rw coe_comp, exact hg.comp hf }

lemma surjective_stable_under_composition :
  (morphism_property.surjective C).stable_under_composition :=
Œª X Y Z f g hf hg, by { delta morphism_property.surjective, rw coe_comp, exact hg.comp hf }

lemma bijective_stable_under_composition :
  (morphism_property.bijective C).stable_under_composition :=
Œª X Y Z f g hf hg, by { delta morphism_property.bijective, rw coe_comp, exact hg.comp hf }

lemma injective_respects_iso :
  (morphism_property.injective C).respects_iso :=
(injective_stable_under_composition C).respects_iso
  (Œª X Y e, ((forget C).map_iso e).to_equiv.injective)

lemma surjective_respects_iso :
  (morphism_property.surjective C).respects_iso :=
(surjective_stable_under_composition C).respects_iso
  (Œª X Y e, ((forget C).map_iso e).to_equiv.surjective)

lemma bijective_respects_iso :
  (morphism_property.bijective C).respects_iso :=
(bijective_stable_under_composition C).respects_iso
  (Œª X Y e, ((forget C).map_iso e).to_equiv.bijective)

end bijective

end morphism_property

end category_theory
