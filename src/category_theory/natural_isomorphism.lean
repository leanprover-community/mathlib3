/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Tim Baumann, Stephen Morgan, Scott Morrison, Floris van Doorn
-/
import category_theory.functor.category
import category_theory.isomorphism

/-!
# Natural isomorphisms

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

For the most part, natural isomorphisms are just another sort of isomorphism.

We provide some special support for extracting components:
* if `Î± : F â‰… G`, then `a.app X : F.obj X â‰… G.obj X`,
and building natural isomorphisms from components:
*
```
nat_iso.of_components
  (app : âˆ€ X : C, F.obj X â‰… G.obj X)
  (naturality : âˆ€ {X Y : C} (f : X âŸ¶ Y), F.map f â‰« (app Y).hom = (app X).hom â‰« G.map f) :
F â‰… G
```
only needing to check naturality in one direction.

## Implementation

Note that `nat_iso` is a namespace without a corresponding definition;
we put some declarations that are specifically about natural isomorphisms in the `iso`
namespace so that they are available using dot notation.
-/

open category_theory

-- declare the `v`'s first; see `category_theory.category` for an explanation
universes vâ‚ vâ‚‚ vâ‚ƒ vâ‚„ uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

namespace category_theory
open nat_trans

variables {C : Type uâ‚} [category.{vâ‚} C] {D : Type uâ‚‚} [category.{vâ‚‚} D]
  {E : Type uâ‚ƒ} [category.{vâ‚ƒ} E]

namespace iso

/-- The application of a natural isomorphism to an object. We put this definition in a different
namespace, so that we can use `Î±.app` -/
@[simps] def app {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) : F.obj X â‰… G.obj X :=
{ hom := Î±.hom.app X,
  inv := Î±.inv.app X,
  hom_inv_id' := begin rw [â† comp_app, iso.hom_inv_id], refl end,
  inv_hom_id' := begin rw [â† comp_app, iso.inv_hom_id], refl end }

@[simp, reassoc]
lemma hom_inv_id_app {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) :
  Î±.hom.app X â‰« Î±.inv.app X = ğŸ™ (F.obj X) :=
congr_fun (congr_arg nat_trans.app Î±.hom_inv_id) X

@[simp, reassoc]
lemma inv_hom_id_app {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) :
  Î±.inv.app X â‰« Î±.hom.app X = ğŸ™ (G.obj X) :=
congr_fun (congr_arg nat_trans.app Î±.inv_hom_id) X

end iso

namespace nat_iso

open category_theory.category category_theory.functor

@[simp] lemma trans_app {F G H : C â¥¤ D} (Î± : F â‰… G) (Î² : G â‰… H) (X : C) :
  (Î± â‰ªâ‰« Î²).app X = Î±.app X â‰ªâ‰« Î².app X := rfl

lemma app_hom {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) : (Î±.app X).hom = Î±.hom.app X := rfl
lemma app_inv {F G : C â¥¤ D} (Î± : F â‰… G) (X : C) : (Î±.app X).inv = Î±.inv.app X := rfl

variables {F G : C â¥¤ D}

instance hom_app_is_iso (Î± : F â‰… G) (X : C) : is_iso (Î±.hom.app X) :=
âŸ¨âŸ¨Î±.inv.app X,
  âŸ¨by rw [â†comp_app, iso.hom_inv_id, â†id_app], by rw [â†comp_app, iso.inv_hom_id, â†id_app]âŸ©âŸ©âŸ©

instance inv_app_is_iso (Î± : F â‰… G) (X : C) : is_iso (Î±.inv.app X) :=
âŸ¨âŸ¨Î±.hom.app X,
  âŸ¨by rw [â†comp_app, iso.inv_hom_id, â†id_app], by rw [â†comp_app, iso.hom_inv_id, â†id_app]âŸ©âŸ©âŸ©

section
/-!
Unfortunately we need a separate set of cancellation lemmas for components of natural isomorphisms,
because the `simp` normal form is `Î±.hom.app X`, rather than `Î±.app.hom X`.

(With the later, the morphism would be visibly part of an isomorphism, so general lemmas about
isomorphisms would apply.)

In the future, we should consider a redesign that changes this simp norm form,
but for now it breaks too many proofs.
-/

variables (Î± : F â‰… G)

@[simp] lemma cancel_nat_iso_hom_left {X : C} {Z : D} (g g' : G.obj X âŸ¶ Z) :
  Î±.hom.app X â‰« g = Î±.hom.app X â‰« g' â†” g = g' :=
by simp only [cancel_epi]

@[simp] lemma cancel_nat_iso_inv_left {X : C} {Z : D} (g g' : F.obj X âŸ¶ Z) :
  Î±.inv.app X â‰« g = Î±.inv.app X â‰« g' â†” g = g' :=
by simp only [cancel_epi]

@[simp] lemma cancel_nat_iso_hom_right {X : D} {Y : C} (f f' : X âŸ¶ F.obj Y) :
  f â‰« Î±.hom.app Y = f' â‰« Î±.hom.app Y â†” f = f' :=
by simp only [cancel_mono]

@[simp] lemma cancel_nat_iso_inv_right {X : D} {Y : C} (f f' : X âŸ¶ G.obj Y) :
  f â‰« Î±.inv.app Y = f' â‰« Î±.inv.app Y â†” f = f' :=
by simp only [cancel_mono]

@[simp] lemma cancel_nat_iso_hom_right_assoc {W X X' : D} {Y : C}
  (f : W âŸ¶ X) (g : X âŸ¶ F.obj Y) (f' : W âŸ¶ X') (g' : X' âŸ¶ F.obj Y)  :
  f â‰« g â‰« Î±.hom.app Y = f' â‰« g' â‰« Î±.hom.app Y â†” f â‰« g = f' â‰« g' :=
by simp only [â†category.assoc, cancel_mono]

@[simp] lemma cancel_nat_iso_inv_right_assoc {W X X' : D} {Y : C}
  (f : W âŸ¶ X) (g : X âŸ¶ G.obj Y) (f' : W âŸ¶ X') (g' : X' âŸ¶ G.obj Y)  :
  f â‰« g â‰« Î±.inv.app Y = f' â‰« g' â‰« Î±.inv.app Y â†” f â‰« g = f' â‰« g' :=
by simp only [â†category.assoc, cancel_mono]

@[simp] lemma inv_inv_app {F G : C â¥¤ D} (e : F â‰… G) (X : C) :
  inv (e.inv.app X) = e.hom.app X := by { ext, simp }

end

variables {X Y : C}

lemma naturality_1 (Î± : F â‰… G) (f : X âŸ¶ Y) :
  Î±.inv.app X â‰« F.map f â‰« Î±.hom.app Y = G.map f :=
by simp
lemma naturality_2 (Î± : F â‰… G) (f : X âŸ¶ Y) :
  Î±.hom.app X â‰« G.map f â‰« Î±.inv.app Y = F.map f :=
by simp

lemma naturality_1' (Î± : F âŸ¶ G) (f : X âŸ¶ Y) [is_iso (Î±.app X)] :
  inv (Î±.app X) â‰« F.map f â‰« Î±.app Y = G.map f :=
by simp
@[simp, reassoc] lemma naturality_2' (Î± : F âŸ¶ G) (f : X âŸ¶ Y) [is_iso (Î±.app Y)] :
  Î±.app X â‰« G.map f â‰« inv (Î±.app Y) = F.map f :=
by rw [â†category.assoc, â†naturality, category.assoc, is_iso.hom_inv_id, category.comp_id]

/--
The components of a natural isomorphism are isomorphisms.
-/
instance is_iso_app_of_is_iso (Î± : F âŸ¶ G) [is_iso Î±] (X) : is_iso (Î±.app X) :=
âŸ¨âŸ¨(inv Î±).app X,
  âŸ¨congr_fun (congr_arg nat_trans.app (is_iso.hom_inv_id Î±)) X,
   congr_fun (congr_arg nat_trans.app (is_iso.inv_hom_id Î±)) XâŸ©âŸ©âŸ©

@[simp] lemma is_iso_inv_app (Î± : F âŸ¶ G) [is_iso Î±] (X) : (inv Î±).app X = inv (Î±.app X) :=
by { ext, rw â†nat_trans.comp_app, simp, }

@[simp] lemma inv_map_inv_app (F : C â¥¤ D â¥¤ E) {X Y : C} (e : X â‰… Y) (Z : D) :
  inv ((F.map e.inv).app Z) = (F.map e.hom).app Z :=
by { ext, simp, }

/--
Construct a natural isomorphism between functors by giving object level isomorphisms,
and checking naturality only in the forward direction.
-/
@[simps] def of_components (app : âˆ€ X : C, F.obj X â‰… G.obj X)
  (naturality : âˆ€ {X Y : C} (f : X âŸ¶ Y), F.map f â‰« (app Y).hom = (app X).hom â‰« G.map f) :
  F â‰… G :=
{ hom := { app := Î» X, (app X).hom },
  inv :=
  { app := Î» X, (app X).inv,
    naturality' := Î» X Y f,
    begin
      have h := congr_arg (Î» f, (app X).inv â‰« (f â‰« (app Y).inv)) (naturality f).symm,
      simp only [iso.inv_hom_id_assoc, iso.hom_inv_id, assoc, comp_id, cancel_mono] at h,
      exact h
    end }, }

@[simp] lemma of_components.app (app' : âˆ€ X : C, F.obj X â‰… G.obj X) (naturality) (X) :
  (of_components app' naturality).app X = app' X :=
by tidy

/--
A natural transformation is an isomorphism if all its components are isomorphisms.
-/
-- Making this an instance would cause a typeclass inference loop with `is_iso_app_of_is_iso`.
lemma is_iso_of_is_iso_app (Î± : F âŸ¶ G) [âˆ€ X : C, is_iso (Î±.app X)] : is_iso Î± :=
âŸ¨(is_iso.of_iso (of_components (Î» X, as_iso (Î±.app X)) (by tidy))).1âŸ©

/-- Horizontal composition of natural isomorphisms. -/
@[simps]
def hcomp {F G : C â¥¤ D} {H I : D â¥¤ E} (Î± : F â‰… G) (Î² : H â‰… I) : F â‹™ H â‰… G â‹™ I :=
begin
  refine âŸ¨Î±.hom â—« Î².hom, Î±.inv â—« Î².inv, _, _âŸ©,
  { ext, rw [â†nat_trans.exchange], simp, refl },
  ext, rw [â†nat_trans.exchange], simp, refl
end

lemma is_iso_map_iff {Fâ‚ Fâ‚‚ : C â¥¤ D} (e : Fâ‚ â‰… Fâ‚‚) {X Y : C} (f : X âŸ¶ Y) :
  is_iso (Fâ‚.map f) â†” is_iso (Fâ‚‚.map f) :=
begin
  revert Fâ‚ Fâ‚‚,
  suffices : âˆ€ {Fâ‚ Fâ‚‚ : C â¥¤ D} (e : Fâ‚ â‰… Fâ‚‚) (hf : is_iso (Fâ‚.map f)), is_iso (Fâ‚‚.map f),
  { exact Î» Fâ‚ Fâ‚‚ e, âŸ¨this e, this e.symmâŸ©, },
  introsI Fâ‚ Fâ‚‚ e hf,
  refine is_iso.mk âŸ¨e.inv.app Y â‰« inv (Fâ‚.map f) â‰« e.hom.app X, _, _âŸ©,
  { simp only [nat_trans.naturality_assoc, is_iso.hom_inv_id_assoc, iso.inv_hom_id_app], },
  { simp only [assoc, â† e.hom.naturality, is_iso.inv_hom_id_assoc, iso.inv_hom_id_app], },
end

end nat_iso

end category_theory
