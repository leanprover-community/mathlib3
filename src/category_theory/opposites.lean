-- Copyright (c) 2017 Scott Morrison. All rights reserved.
-- Released under Apache 2.0 license as described in the file LICENSE.
-- Authors: Stephen Morgan, Scott Morrison

import category_theory.products
import category_theory.types
import category_theory.natural_isomorphism

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ -- declare the `v`'s first; see `category_theory.category` for an explanation

namespace category_theory

/-- The type of objects of the opposite of C (which should be a category).

  In order to avoid confusion between C and its opposite category, we
  set up the type of objects `opposite C` using the following pattern,
  which will be repeated later for the morphisms.

  1. Define `opposite C := C`.
  2. Define the isomorphisms `op : C ‚Üí opposite C`, `unop : opposite C ‚Üí C`.
  3. Make the definition `opposite` irreducible.

  This has the following consequences.

  * `opposite C` and `C` are distinct types in the elaborator, so you
    must use `op` and `unop` explicitly to convert between them.
  * Both `unop (op X) = X` and `op (unop X) = X` are definitional
    equalities. Notably, every object of the opposite category is
    definitionally of the form `op X`, which greatly simplifies the
    definition of the structure of the opposite category, for example.

  (If Lean supported definitional eta equality for records, we could
  achieve the same goals using a structure with one field.)
-/
def opposite (C : Sort u‚ÇÅ) : Sort u‚ÇÅ := C

-- Use a high right binding power (like that of postfix ‚Åª¬π) so that, for example,
-- `presheaf C·µí·µñ` parses as `presheaf (C·µí·µñ)` and not `(presheaf C)·µí·µñ`.
notation C `·µí·µñ`:std.prec.max_plus := opposite C

variables {C : Sort u‚ÇÅ}

def op (X : C) : C·µí·µñ := X
def unop (X : C·µí·µñ) : C := X

attribute [irreducible] opposite

@[simp] lemma unop_op (X : C) : unop (op X) = X := rfl
@[simp] lemma op_unop (X : C·µí·µñ) : op (unop X) = X := rfl

lemma op_inj : function.injective (@op C) :=
by { rintros _ _ ‚ü® ‚ü©, refl }
lemma unop_inj : function.injective (@unop C) :=
by { rintros _ _ ‚ü® ‚ü©, refl }

def op_induction {F : Œ† (X : C·µí·µñ), Sort v‚ÇÅ} (h : Œ† X, F (op X)) : Œ† X, F X :=
Œª X, h (unop X)

end category_theory

namespace tactic.interactive

open interactive interactive.types lean.parser tactic

meta def op_induction (h : parse ident) : tactic unit :=
do h' ‚Üê tactic.get_local h,
   revert_lst [h'],
   applyc `category_theory.op_induction,
   tactic.intro h,
   skip

end tactic.interactive

namespace category_theory

variables {C : Sort u‚ÇÅ}

section has_hom

variables [ùíû : has_hom.{v‚ÇÅ} C]
include ùíû

/-- The hom types of the opposite of a category (or graph).

  As with the objects, we'll make this irreducible below.
  Use `f.op` and `f.unop` to convert between morphisms of C
  and morphisms of C·µí·µñ.
-/
instance has_hom.opposite : has_hom C·µí·µñ :=
{ hom := Œª X Y, unop Y ‚ü∂ unop X }

def has_hom.hom.op {X Y : C} (f : X ‚ü∂ Y) : op Y ‚ü∂ op X := f
def has_hom.hom.unop {X Y : C·µí·µñ} (f : X ‚ü∂ Y) : unop Y ‚ü∂ unop X := f

attribute [irreducible] has_hom.opposite

lemma has_hom.hom.op_inj {X Y : C} :
  function.injective (has_hom.hom.op : (X ‚ü∂ Y) ‚Üí (op Y ‚ü∂ op X)) :=
Œª _ _ H, congr_arg has_hom.hom.unop H

lemma has_hom.hom.unop_inj {X Y : C·µí·µñ} :
  function.injective (has_hom.hom.unop : (X ‚ü∂ Y) ‚Üí (unop Y ‚ü∂ unop X)) :=
Œª _ _ H, congr_arg has_hom.hom.op H

@[simp] lemma has_hom.hom.unop_op {X Y : C} {f : X ‚ü∂ Y} : f.op.unop = f := rfl
@[simp] lemma has_hom.hom.op_unop {X Y : C·µí·µñ} {f : X ‚ü∂ Y} : f.unop.op = f := rfl

end has_hom

variables [ùíû : category.{v‚ÇÅ} C]
include ùíû

instance category.opposite : category.{v‚ÇÅ} C·µí·µñ :=
{ comp := Œª _ _ _ f g, (g.unop ‚â´ f.unop).op,
  id   := Œª X, (ùüô (unop X)).op }

@[simp] lemma op_comp {X Y Z : C} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} :
  (f ‚â´ g).op = g.op ‚â´ f.op := rfl
@[simp] lemma op_id {X : C} : (ùüô X).op = ùüô (op X) := rfl

@[simp] lemma unop_comp {X Y Z : C·µí·µñ} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} :
  (f ‚â´ g).unop = g.unop ‚â´ f.unop := rfl
@[simp] lemma unop_id {X : C·µí·µñ} : (ùüô X).unop = ùüô (unop X) := rfl

@[simp] lemma unop_id_op {X : C} : (ùüô (op X)).unop = ùüô X := rfl
@[simp] lemma op_id_unop {X : C·µí·µñ} : (ùüô (unop X)).op = ùüô X := rfl

def op_op : (C·µí·µñ)·µí·µñ ‚•§ C :=
{ obj := Œª X, unop (unop X),
  map := Œª X Y f, f.unop.unop }

-- TODO this is an equivalence

namespace functor

section

variables {D : Sort u‚ÇÇ} [ùíü : category.{v‚ÇÇ} D]
include ùíü

variables {C D}

protected definition op (F : C ‚•§ D) : C·µí·µñ ‚•§ D·µí·µñ :=
{ obj := Œª X, op (F.obj (unop X)),
  map := Œª X Y f, (F.map f.unop).op }

@[simp] lemma op_obj (F : C ‚•§ D) (X : C·µí·µñ) : (F.op).obj X = op (F.obj (unop X)) := rfl
@[simp] lemma op_map (F : C ‚•§ D) {X Y : C·µí·µñ} (f : X ‚ü∂ Y) : (F.op).map f = (F.map f.unop).op := rfl

protected definition unop (F : C·µí·µñ ‚•§ D·µí·µñ) : C ‚•§ D :=
{ obj := Œª X, unop (F.obj (op X)),
  map := Œª X Y f, (F.map f.op).unop }

@[simp] lemma unop_obj (F : C·µí·µñ ‚•§ D·µí·µñ) (X : C) : (F.unop).obj X = unop (F.obj (op X)) := rfl
@[simp] lemma unop_map (F : C·µí·µñ ‚•§ D·µí·µñ) {X Y : C} (f : X ‚ü∂ Y) : (F.unop).map f = (F.map f.op).unop := rfl

variables (C D)

definition op_hom : (C ‚•§ D)·µí·µñ ‚•§ (C·µí·µñ ‚•§ D·µí·µñ) :=
{ obj := Œª F, (unop F).op,
  map := Œª F G Œ±,
  { app := Œª X, (Œ±.unop.app (unop X)).op,
    naturality' := Œª X Y f, has_hom.hom.unop_inj $ eq.symm (Œ±.unop.naturality f.unop) } }

@[simp] lemma op_hom.obj (F : (C ‚•§ D)·µí·µñ) : (op_hom C D).obj F = (unop F).op := rfl
@[simp] lemma op_hom.map_app {F G : (C ‚•§ D)·µí·µñ} (Œ± : F ‚ü∂ G) (X : C·µí·µñ) :
  ((op_hom C D).map Œ±).app X = (Œ±.unop.app (unop X)).op := rfl

definition op_inv : (C·µí·µñ ‚•§ D·µí·µñ) ‚•§ (C ‚•§ D)·µí·µñ :=
{ obj := Œª F, op F.unop,
  map := Œª F G Œ±, has_hom.hom.op
  { app := Œª X, (Œ±.app (op X)).unop,
    naturality' := Œª X Y f, has_hom.hom.op_inj $ eq.symm (Œ±.naturality f.op) } }

@[simp] lemma op_inv.obj (F : C·µí·µñ ‚•§ D·µí·µñ) : (op_inv C D).obj F = op F.unop := rfl
@[simp] lemma op_inv.map_app {F G : C·µí·µñ ‚•§ D·µí·µñ} (Œ± : F ‚ü∂ G) (X : C) :
  (((op_inv C D).map Œ±).unop).app X = (Œ±.app (op X)).unop := rfl

-- TODO show these form an equivalence

variables {C D}

protected definition left_op (F : C ‚•§ D·µí·µñ) : C·µí·µñ ‚•§ D :=
{ obj := Œª X, unop (F.obj (unop X)),
  map := Œª X Y f, (F.map f.unop).unop }

@[simp] lemma left_op_obj (F : C ‚•§ D·µí·µñ) (X : C·µí·µñ) : (F.left_op).obj X = unop (F.obj (unop X)) := rfl
@[simp] lemma left_op_map (F : C ‚•§ D·µí·µñ) {X Y : C·µí·µñ} (f : X ‚ü∂ Y) :
  (F.left_op).map f = (F.map f.unop).unop :=
rfl

protected definition right_op (F : C·µí·µñ ‚•§ D) : C ‚•§ D·µí·µñ :=
{ obj := Œª X, op (F.obj (op X)),
  map := Œª X Y f, (F.map f.op).op }

@[simp] lemma right_op_obj (F : C·µí·µñ ‚•§ D) (X : C) : (F.right_op).obj X = op (F.obj (op X)) := rfl
@[simp] lemma right_op_map (F : C·µí·µñ ‚•§ D) {X Y : C} (f : X ‚ü∂ Y) :
  (F.right_op).map f = (F.map f.op).op :=
rfl

-- TODO show these form an equivalence

instance {F : C ‚•§ D} [full F] : full F.op :=
{ preimage := Œª X Y f, (F.preimage f.unop).op }

instance {F : C ‚•§ D} [faithful F] : faithful F.op :=
{ injectivity' := Œª X Y f g h,
    has_hom.hom.unop_inj $ by simpa using injectivity F (has_hom.hom.op_inj h) }

end

section

omit ùíû
variables (E : Type u‚ÇÅ) [‚Ñ∞ : category.{v‚ÇÅ+1} E]
include ‚Ñ∞

/-- `functor.hom` is the hom-pairing, sending (X,Y) to X ‚Üí Y, contravariant in X and covariant in Y. -/
definition hom : E·µí·µñ √ó E ‚•§ Type v‚ÇÅ :=
{ obj       := Œª p, unop p.1 ‚ü∂ p.2,
  map       := Œª X Y f, Œª h, f.1.unop ‚â´ h ‚â´ f.2 }

@[simp] lemma hom_obj (X : E·µí·µñ √ó E) : (functor.hom E).obj X = (unop X.1 ‚ü∂ X.2) := rfl
@[simp] lemma hom_pairing_map {X Y : E·µí·µñ √ó E} (f : X ‚ü∂ Y) :
  (functor.hom E).map f = Œª h, f.1.unop ‚â´ h ‚â´ f.2 := rfl

end

end functor

namespace nat_trans

variables {D : Sort u‚ÇÇ} [ùíü : category.{v‚ÇÇ} D]
include ùíü

section
variables {F G : C ‚•§ D}

protected definition op (Œ± : F ‚ü∂ G) : G.op ‚ü∂ F.op :=
{ app         := Œª X, (Œ±.app (unop X)).op,
  naturality' := begin tidy, erw Œ±.naturality, refl, end }

@[simp] lemma op_app (Œ± : F ‚ü∂ G) (X) : (nat_trans.op Œ±).app X = (Œ±.app (unop X)).op := rfl

protected definition unop (Œ± : F.op ‚ü∂ G.op) : G ‚ü∂ F :=
{ app         := Œª X, (Œ±.app (op X)).unop,
  naturality' := begin tidy, erw Œ±.naturality, refl, end }

@[simp] lemma unop_app (Œ± : F.op ‚ü∂ G.op) (X) : (nat_trans.unop Œ±).app X = (Œ±.app (op X)).unop := rfl

end

section
variables {F G : C ‚•§ D·µí·µñ}

protected definition left_op (Œ± : F ‚ü∂ G) : G.left_op ‚ü∂ F.left_op :=
{ app         := Œª X, (Œ±.app (unop X)).unop,
  naturality' := begin tidy, erw Œ±.naturality, refl, end }

@[simp] lemma left_op_app (Œ± : F ‚ü∂ G) (X) :
  (nat_trans.left_op Œ±).app X = (Œ±.app (unop X)).unop :=
rfl

protected definition right_op (Œ± : F.left_op ‚ü∂ G.left_op) : G ‚ü∂ F :=
{ app         := Œª X, (Œ±.app (op X)).op,
  naturality' := begin tidy, erw Œ±.naturality, refl, end }

@[simp] lemma right_op_app (Œ± : F.left_op ‚ü∂ G.left_op) (X) :
  (nat_trans.right_op Œ±).app X = (Œ±.app (op X)).op :=
rfl

end
end nat_trans

namespace iso

variables {X Y : C}

protected definition op (Œ± : X ‚âÖ Y) : op Y ‚âÖ op X :=
{ hom := Œ±.hom.op,
  inv := Œ±.inv.op,
  hom_inv_id' := has_hom.hom.unop_inj Œ±.inv_hom_id,
  inv_hom_id' := has_hom.hom.unop_inj Œ±.hom_inv_id }

@[simp] lemma op_hom {Œ± : X ‚âÖ Y} : Œ±.op.hom = Œ±.hom.op := rfl
@[simp] lemma op_inv {Œ± : X ‚âÖ Y} : Œ±.op.inv = Œ±.inv.op := rfl

end iso

namespace nat_iso

variables {D : Sort u‚ÇÇ} [ùíü : category.{v‚ÇÇ} D]
include ùíü
variables {F G : C ‚•§ D}

protected definition op (Œ± : F ‚âÖ G) : G.op ‚âÖ F.op :=
{ hom := nat_trans.op Œ±.hom,
  inv := nat_trans.op Œ±.inv,
  hom_inv_id' := begin ext, dsimp, rw ‚Üêop_comp, rw inv_hom_id_app, refl, end,
  inv_hom_id' := begin ext, dsimp, rw ‚Üêop_comp, rw hom_inv_id_app, refl, end }

@[simp] lemma op_hom (Œ± : F ‚âÖ G) : (nat_iso.op Œ±).hom = nat_trans.op Œ±.hom := rfl
@[simp] lemma op_inv (Œ± : F ‚âÖ G) : (nat_iso.op Œ±).inv = nat_trans.op Œ±.inv := rfl

end nat_iso

end category_theory
