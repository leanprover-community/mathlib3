/-
Copyright (c) 2019 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Bhavik Mehta
-/
import category_theory.structured_arrow
import category_theory.punit
import category_theory.reflects_isomorphisms
import category_theory.epi_mono

/-!
# Over and under categories

Over (and under) categories are special cases of comma categories.
* If `L` is the identity functor and `R` is a constant functor, then `comma L R` is the "slice" or
  "over" category over the object `R` maps to.
* Conversely, if `L` is a constant functor and `R` is the identity functor, then `comma L R` is the
  "coslice" or "under" category under the object `L` maps to.

## Tags

comma, slice, coslice, over, under
-/

namespace category_theory

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ -- morphism levels before object levels. See note [category_theory universes].
variables {T : Type u‚ÇÅ} [category.{v‚ÇÅ} T]

/--
The over category has as objects arrows in `T` with codomain `X` and as morphisms commutative
triangles.

See https://stacks.math.columbia.edu/tag/001G.
-/
@[derive category]
def over (X : T) := costructured_arrow (ùü≠ T) X

-- Satisfying the inhabited linter
instance over.inhabited [inhabited T] : inhabited (over (default T)) :=
{ default :=
  { left := default T,
    hom := ùüô _ } }

namespace over

variables {X : T}

@[ext] lemma over_morphism.ext {X : T} {U V : over X} {f g : U ‚ü∂ V}
  (h : f.left = g.left) : f = g :=
by tidy

@[simp] lemma over_right (U : over X) : U.right = punit.star := by tidy

@[simp] lemma id_left (U : over X) : comma_morphism.left (ùüô U) = ùüô U.left := rfl
@[simp] lemma comp_left (a b c : over X) (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (f ‚â´ g).left = f.left ‚â´ g.left := rfl

@[simp, reassoc] lemma w {A B : over X} (f : A ‚ü∂ B) : f.left ‚â´ B.hom = A.hom :=
by have := f.w; tidy

/-- To give an object in the over category, it suffices to give a morphism with codomain `X`. -/
@[simps]
def mk {X Y : T} (f : Y ‚ü∂ X) : over X :=
costructured_arrow.mk f

/-- We can set up a coercion from arrows with codomain `X` to `over X`. This most likely should not
    be a global instance, but it is sometimes useful. -/
def coe_from_hom {X Y : T} : has_coe (Y ‚ü∂ X) (over X) :=
{ coe := mk }

section
local attribute [instance] coe_from_hom

@[simp] lemma coe_hom {X Y : T} (f : Y ‚ü∂ X) : (f : over X).hom = f := rfl
end

/-- To give a morphism in the over category, it suffices to give an arrow fitting in a commutative
    triangle. -/
@[simps]
def hom_mk {U V : over X} (f : U.left ‚ü∂ V.left) (w : f ‚â´ V.hom = U.hom . obviously) :
  U ‚ü∂ V :=
costructured_arrow.hom_mk f w

/--
Construct an isomorphism in the over category given isomorphisms of the objects whose forward
direction gives a commutative triangle.
-/
@[simps]
def iso_mk {f g : over X} (hl : f.left ‚âÖ g.left) (hw : hl.hom ‚â´ g.hom = f.hom . obviously) :
  f ‚âÖ g :=
costructured_arrow.iso_mk hl hw

section
variable (X)
/--
The forgetful functor mapping an arrow to its domain.

See https://stacks.math.columbia.edu/tag/001G.
-/
def forget : over X ‚•§ T := comma.fst _ _

end

@[simp] lemma forget_obj {U : over X} : (forget X).obj U = U.left := rfl
@[simp] lemma forget_map {U V : over X} {f : U ‚ü∂ V} : (forget X).map f = f.left := rfl

/--
A morphism `f : X ‚ü∂ Y` induces a functor `over X ‚•§ over Y` in the obvious way.

See https://stacks.math.columbia.edu/tag/001G.
-/
def map {Y : T} (f : X ‚ü∂ Y) : over X ‚•§ over Y := comma.map_right _ $ discrete.nat_trans (Œª _, f)

section
variables {Y : T} {f : X ‚ü∂ Y} {U V : over X} {g : U ‚ü∂ V}
@[simp] lemma map_obj_left : ((map f).obj U).left = U.left := rfl
@[simp] lemma map_obj_hom  : ((map f).obj U).hom  = U.hom ‚â´ f := rfl
@[simp] lemma map_map_left : ((map f).map g).left = g.left := rfl

/-- Mapping by the identity morphism is just the identity functor. -/
def map_id : map (ùüô Y) ‚âÖ ùü≠ _ :=
nat_iso.of_components (Œª X, iso_mk (iso.refl _) (by tidy)) (by tidy)

/-- Mapping by the composite morphism `f ‚â´ g` is the same as mapping by `f` then by `g`. -/
def map_comp {Y Z : T} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : map (f ‚â´ g) ‚âÖ map f ‚ãô map g :=
nat_iso.of_components (Œª X, iso_mk (iso.refl _) (by tidy)) (by tidy)

end

instance forget_reflects_iso : reflects_isomorphisms (forget X) :=
{ reflects := Œª Y Z f t, by exactI
  ‚ü®‚ü®over.hom_mk (inv ((forget X).map f))
      ((as_iso ((forget X).map f)).inv_comp_eq.2 (over.w f).symm),
    by tidy‚ü©‚ü© }

instance forget_faithful : faithful (forget X) := {}.

/--
If `k.left` is an epimorphism, then `k` is an epimorphism. In other words, `over.forget X` reflects
epimorphisms.
The converse does not hold without additional assumptions on the underlying category.
-/
-- TODO: Show the converse holds if `T` has binary products or pushouts.
lemma epi_of_epi_left {f g : over X} (k : f ‚ü∂ g) [hk : epi k.left] : epi k :=
faithful_reflects_epi (forget X) hk

/--
If `k.left` is a monomorphism, then `k` is a monomorphism. In other words, `over.forget X` reflects
monomorphisms.
The converse of `category_theory.over.mono_left_of_mono`.

This lemma is not an instance, to avoid loops in type class inference.
-/
lemma mono_of_mono_left {f g : over X} (k : f ‚ü∂ g) [hk : mono k.left] : mono k :=
faithful_reflects_mono (forget X) hk

/--
If `k` is a monomorphism, then `k.left` is a monomorphism. In other words, `over.forget X` preserves
monomorphisms.
The converse of `category_theory.over.mono_of_mono_left`.
-/
instance mono_left_of_mono {f g : over X} (k : f ‚ü∂ g) [mono k] : mono k.left :=
begin
  refine ‚ü®Œª (Y : T) l m a, _‚ü©,
  let l' : mk (m ‚â´ f.hom) ‚ü∂ f := hom_mk l (by { dsimp, rw [‚Üêover.w k, reassoc_of a] }),
  suffices : l' = hom_mk m,
  { apply congr_arg comma_morphism.left this },
  rw ‚Üê cancel_mono k,
  ext,
  apply a,
end

section iterated_slice
variables (f : over X)

/-- Given f : Y ‚ü∂ X, this is the obvious functor from (T/X)/f to T/Y -/
@[simps]
def iterated_slice_forward : over f ‚•§ over f.left :=
{ obj := Œª Œ±, over.mk Œ±.hom.left,
  map := Œª Œ± Œ≤ Œ∫, over.hom_mk Œ∫.left.left (by { rw auto_param_eq, rw ‚Üê over.w Œ∫, refl }) }

/-- Given f : Y ‚ü∂ X, this is the obvious functor from T/Y to (T/X)/f -/
@[simps]
def iterated_slice_backward : over f.left ‚•§ over f :=
{ obj := Œª g, mk (hom_mk g.hom : mk (g.hom ‚â´ f.hom) ‚ü∂ f),
  map := Œª g h Œ±, hom_mk (hom_mk Œ±.left (w_assoc Œ± f.hom)) (over_morphism.ext (w Œ±)) }

/-- Given f : Y ‚ü∂ X, we have an equivalence between (T/X)/f and T/Y -/
@[simps]
def iterated_slice_equiv : over f ‚âå over f.left :=
{ functor := iterated_slice_forward f,
  inverse := iterated_slice_backward f,
  unit_iso :=
    nat_iso.of_components
    (Œª g, over.iso_mk (over.iso_mk (iso.refl _) (by tidy)) (by tidy))
    (Œª X Y g, by { ext, dsimp, simp }),
  counit_iso :=
    nat_iso.of_components
    (Œª g, over.iso_mk (iso.refl _) (by tidy))
    (Œª X Y g, by { ext, dsimp, simp }) }

lemma iterated_slice_forward_forget :
  iterated_slice_forward f ‚ãô forget f.left = forget f ‚ãô forget X :=
rfl

lemma iterated_slice_backward_forget_forget :
  iterated_slice_backward f ‚ãô forget f ‚ãô forget X = forget f.left :=
rfl

end iterated_slice

section
variables {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]

/-- A functor `F : T ‚•§ D` induces a functor `over X ‚•§ over (F.obj X)` in the obvious way. -/
@[simps]
def post (F : T ‚•§ D) : over X ‚•§ over (F.obj X) :=
{ obj := Œª Y, mk $ F.map Y.hom,
  map := Œª Y‚ÇÅ Y‚ÇÇ f,
  { left := F.map f.left,
    w' := by tidy; erw [‚Üê F.map_comp, w] } }

end

end over

/-- The under category has as objects arrows with domain `X` and as morphisms commutative
    triangles. -/
@[derive category]
def under (X : T) := structured_arrow X (ùü≠ T)

-- Satisfying the inhabited linter
instance under.inhabited [inhabited T] : inhabited (under (default T)) :=
{ default :=
  { right := default T,
    hom := ùüô _ } }

namespace under

variables {X : T}

@[ext] lemma under_morphism.ext {X : T} {U V : under X} {f g : U ‚ü∂ V}
  (h : f.right = g.right) : f = g :=
by tidy

@[simp] lemma under_left (U : under X) : U.left = punit.star := by tidy

@[simp] lemma id_right (U : under X) : comma_morphism.right (ùüô U) = ùüô U.right := rfl
@[simp] lemma comp_right (a b c : under X) (f : a ‚ü∂ b) (g : b ‚ü∂ c) :
  (f ‚â´ g).right = f.right ‚â´ g.right := rfl

@[simp, reassoc] lemma w {A B : under X} (f : A ‚ü∂ B) : A.hom ‚â´ f.right = B.hom :=
by have := f.w; tidy

/-- To give an object in the under category, it suffices to give an arrow with domain `X`. -/
@[simps]
def mk {X Y : T} (f : X ‚ü∂ Y) : under X :=
structured_arrow.mk f

/-- To give a morphism in the under category, it suffices to give a morphism fitting in a
    commutative triangle. -/
@[simps]
def hom_mk {U V : under X} (f : U.right ‚ü∂ V.right) (w : U.hom ‚â´ f = V.hom . obviously) :
  U ‚ü∂ V :=
structured_arrow.hom_mk f w

/--
Construct an isomorphism in the over category given isomorphisms of the objects whose forward
direction gives a commutative triangle.
-/
def iso_mk {f g : under X} (hr : f.right ‚âÖ g.right) (hw : f.hom ‚â´ hr.hom = g.hom) : f ‚âÖ g :=
structured_arrow.iso_mk hr hw

@[simp]
lemma iso_mk_hom_right {f g : under X} (hr : f.right ‚âÖ g.right) (hw : f.hom ‚â´ hr.hom = g.hom) :
  (iso_mk hr hw).hom.right = hr.hom := rfl

@[simp]
lemma iso_mk_inv_right {f g : under X} (hr : f.right ‚âÖ g.right) (hw : f.hom ‚â´ hr.hom = g.hom) :
  (iso_mk hr hw).inv.right = hr.inv := rfl

section
variables (X)
/-- The forgetful functor mapping an arrow to its domain. -/
def forget : under X ‚•§ T := comma.snd _ _

end

@[simp] lemma forget_obj {U : under X} : (forget X).obj U = U.right := rfl
@[simp] lemma forget_map {U V : under X} {f : U ‚ü∂ V} : (forget X).map f = f.right := rfl

/-- A morphism `X ‚ü∂ Y` induces a functor `under Y ‚•§ under X` in the obvious way. -/
def map {Y : T} (f : X ‚ü∂ Y) : under Y ‚•§ under X := comma.map_left _ $ discrete.nat_trans (Œª _, f)

section
variables {Y : T} {f : X ‚ü∂ Y} {U V : under Y} {g : U ‚ü∂ V}
@[simp] lemma map_obj_right : ((map f).obj U).right = U.right := rfl
@[simp] lemma map_obj_hom   : ((map f).obj U).hom   = f ‚â´ U.hom := rfl
@[simp] lemma map_map_right : ((map f).map g).right = g.right := rfl

/-- Mapping by the identity morphism is just the identity functor. -/
def map_id : map (ùüô Y) ‚âÖ ùü≠ _ :=
nat_iso.of_components (Œª X, iso_mk (iso.refl _) (by tidy)) (by tidy)

/-- Mapping by the composite morphism `f ‚â´ g` is the same as mapping by `f` then by `g`. -/
def map_comp {Y Z : T} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : map (f ‚â´ g) ‚âÖ map g ‚ãô map f :=
nat_iso.of_components (Œª X, iso_mk (iso.refl _) (by tidy)) (by tidy)

end

instance forget_reflects_iso : reflects_isomorphisms (forget X) :=
{ reflects := Œª Y Z f t, by exactI
  ‚ü®‚ü®under.hom_mk (inv ((under.forget X).map f)) ((is_iso.comp_inv_eq _).2 (under.w f).symm),
    by tidy‚ü©‚ü© }

instance forget_faithful : faithful (forget X) := {}.

section
variables {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]

/-- A functor `F : T ‚•§ D` induces a functor `under X ‚•§ under (F.obj X)` in the obvious way. -/
@[simps]
def post {X : T} (F : T ‚•§ D) : under X ‚•§ under (F.obj X) :=
{ obj := Œª Y, mk $ F.map Y.hom,
  map := Œª Y‚ÇÅ Y‚ÇÇ f,
  { right := F.map f.right,
    w' := by tidy; erw [‚Üê F.map_comp, w] } }

end

end under

end category_theory
