/-
Copyright (c) 2019 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Bhavik Mehta
-/
import category_theory.comma
import category_theory.punit
import category_theory.reflects_isomorphisms

/-!
# Over and under categories

Over (and under) categories are special cases of comma categories.
* If `L` is the identity functor and `R` is a constant functor, then `comma L R` is the "slice" or
  "over" category over the object `R` maps to.
* Conversely, if `L` is a constant functor and `R` is the identity functor, then `comma L R` is the
  "coslice" or "under" category under the object `L` maps to.

## Tags

comma, slice, coslice, over, under
-/

namespace category_theory

universes vâ‚ vâ‚‚ uâ‚ uâ‚‚ -- declare the `v`'s first; see `category_theory.category` for an explanation
variables {T : Type uâ‚} [category.{vâ‚} T]

/-- The over category has as objects arrows in `T` with codomain `X` and as morphisms commutative
    triangles. -/
@[derive category]
def over (X : T) := comma.{vâ‚ 0 vâ‚} (ğŸ­ T) (functor.from_punit X)

-- Satisfying the inhabited linter
instance over.inhabited [inhabited T] : inhabited (over (default T)) :=
{ default :=
  { left := default T,
    hom := ğŸ™ _ } }

namespace over

variables {X : T}

@[ext] lemma over_morphism.ext {X : T} {U V : over X} {f g : U âŸ¶ V}
  (h : f.left = g.left) : f = g :=
by tidy

@[simp] lemma over_right (U : over X) : U.right = punit.star := by tidy

@[simp] lemma id_left (U : over X) : comma_morphism.left (ğŸ™ U) = ğŸ™ U.left := rfl
@[simp] lemma comp_left (a b c : over X) (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (f â‰« g).left = f.left â‰« g.left := rfl

@[simp, reassoc] lemma w {A B : over X} (f : A âŸ¶ B) : f.left â‰« B.hom = A.hom :=
by have := f.w; tidy

/-- To give an object in the over category, it suffices to give a morphism with codomain `X`. -/
@[simps]
def mk {X Y : T} (f : Y âŸ¶ X) : over X :=
{ left := Y, hom := f }

/-- To give a morphism in the over category, it suffices to give an arrow fitting in a commutative
    triangle. -/
@[simps]
def hom_mk {U V : over X} (f : U.left âŸ¶ V.left) (w : f â‰« V.hom = U.hom . obviously) :
  U âŸ¶ V :=
{ left := f }

/--
Construct an isomorphism in the over category given isomorphisms of the objects whose forward
direction gives a commutative triangle.
-/
def iso_mk {f g : over X} (hl : f.left â‰… g.left) (hw : hl.hom â‰« g.hom = f.hom) : f â‰… g :=
comma.iso_mk hl (eq_to_iso (subsingleton.elim _ _)) (by simp [hw])

@[simp]
lemma iso_mk_hom_left {f g : over X} (hl : f.left â‰… g.left) (hw : hl.hom â‰« g.hom = f.hom) :
  (iso_mk hl hw).hom.left = hl.hom := rfl

@[simp]
lemma iso_mk_inv_left {f g : over X} (hl : f.left â‰… g.left) (hw : hl.hom â‰« g.hom = f.hom) :
  (iso_mk hl hw).inv.left = hl.inv := rfl

/-- The forgetful functor mapping an arrow to its domain. -/
def forget : over X â¥¤ T := comma.fst _ _

@[simp] lemma forget_obj {U : over X} : forget.obj U = U.left := rfl
@[simp] lemma forget_map {U V : over X} {f : U âŸ¶ V} : forget.map f = f.left := rfl

/-- A morphism `f : X âŸ¶ Y` induces a functor `over X â¥¤ over Y` in the obvious way. -/
def map {Y : T} (f : X âŸ¶ Y) : over X â¥¤ over Y := comma.map_right _ $ discrete.nat_trans (Î» _, f)

section
variables {Y : T} {f : X âŸ¶ Y} {U V : over X} {g : U âŸ¶ V}
@[simp] lemma map_obj_left : ((map f).obj U).left = U.left := rfl
@[simp] lemma map_obj_hom  : ((map f).obj U).hom  = U.hom â‰« f := rfl
@[simp] lemma map_map_left : ((map f).map g).left = g.left := rfl

/-- Mapping by the identity morphism is just the identity functor. -/
def map_id : map (ğŸ™ Y) â‰… ğŸ­ _ :=
nat_iso.of_components (Î» X, iso_mk (iso.refl _) (by tidy)) (by tidy)

/-- Mapping by the composite morphism `f â‰« g` is the same as mapping by `f` then by `g`. -/
def map_comp {Y Z : T} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : map (f â‰« g) â‰… map f â‹™ map g :=
nat_iso.of_components (Î» X, iso_mk (iso.refl _) (by tidy)) (by tidy)

end

instance forget_reflects_iso : reflects_isomorphisms (forget : over X â¥¤ T) :=
{ reflects := Î» X Y f t, by exactI
  { inv := over.hom_mk t.inv ((as_iso (forget.map f)).inv_comp_eq.2 (over.w f).symm) } }

section iterated_slice
variables (f : over X)

/-- Given f : Y âŸ¶ X, this is the obvious functor from (T/X)/f to T/Y -/
@[simps]
def iterated_slice_forward : over f â¥¤ over f.left :=
{ obj := Î» Î±, over.mk Î±.hom.left,
  map := Î» Î± Î² Îº, over.hom_mk Îº.left.left (by { rw auto_param_eq, rw â† over.w Îº, refl }) }

/-- Given f : Y âŸ¶ X, this is the obvious functor from T/Y to (T/X)/f -/
@[simps]
def iterated_slice_backward : over f.left â¥¤ over f :=
{ obj := Î» g, mk (hom_mk g.hom : mk (g.hom â‰« f.hom) âŸ¶ f),
  map := Î» g h Î±, hom_mk (hom_mk Î±.left (w_assoc Î± f.hom)) (over_morphism.ext (w Î±)) }

/-- Given f : Y âŸ¶ X, we have an equivalence between (T/X)/f and T/Y -/
@[simps]
def iterated_slice_equiv : over f â‰Œ over f.left :=
{ functor := iterated_slice_forward f,
  inverse := iterated_slice_backward f,
  unit_iso :=
    nat_iso.of_components
    (Î» g, over.iso_mk (over.iso_mk (iso.refl _) (by tidy)) (by tidy))
    (Î» X Y g, by { ext, dsimp, simp }),
  counit_iso :=
    nat_iso.of_components
    (Î» g, over.iso_mk (iso.refl _) (by tidy))
    (Î» X Y g, by { ext, dsimp, simp }) }

lemma iterated_slice_forward_forget :
  iterated_slice_forward f â‹™ forget = forget â‹™ forget :=
rfl

lemma iterated_slice_backward_forget_forget :
  iterated_slice_backward f â‹™ forget â‹™ forget = forget :=
rfl

end iterated_slice

section
variables {D : Type uâ‚‚} [category.{vâ‚‚} D]

/-- A functor `F : T â¥¤ D` induces a functor `over X â¥¤ over (F.obj X)` in the obvious way. -/
@[simps]
def post (F : T â¥¤ D) : over X â¥¤ over (F.obj X) :=
{ obj := Î» Y, mk $ F.map Y.hom,
  map := Î» Yâ‚ Yâ‚‚ f,
  { left := F.map f.left,
    w' := by tidy; erw [â† F.map_comp, w] } }

end

end over

/-- The under category has as objects arrows with domain `X` and as morphisms commutative
    triangles. -/
@[derive category]
def under (X : T) := comma.{0 vâ‚ vâ‚} (functor.from_punit X) (ğŸ­ T)

-- Satisfying the inhabited linter
instance under.inhabited [inhabited T] : inhabited (under (default T)) :=
{ default :=
  { right := default T,
    hom := ğŸ™ _ } }

namespace under

variables {X : T}

@[ext] lemma under_morphism.ext {X : T} {U V : under X} {f g : U âŸ¶ V}
  (h : f.right = g.right) : f = g :=
by tidy

@[simp] lemma under_left (U : under X) : U.left = punit.star := by tidy

@[simp] lemma id_right (U : under X) : comma_morphism.right (ğŸ™ U) = ğŸ™ U.right := rfl
@[simp] lemma comp_right (a b c : under X) (f : a âŸ¶ b) (g : b âŸ¶ c) :
  (f â‰« g).right = f.right â‰« g.right := rfl

@[simp] lemma w {A B : under X} (f : A âŸ¶ B) : A.hom â‰« f.right = B.hom :=
by have := f.w; tidy

/-- To give an object in the under category, it suffices to give an arrow with domain `X`. -/
@[simps]
def mk {X Y : T} (f : X âŸ¶ Y) : under X :=
{ right := Y, hom := f }

/-- To give a morphism in the under category, it suffices to give a morphism fitting in a
    commutative triangle. -/
@[simps]
def hom_mk {U V : under X} (f : U.right âŸ¶ V.right) (w : U.hom â‰« f = V.hom . obviously) :
  U âŸ¶ V :=
{ right := f }

/--
Construct an isomorphism in the over category given isomorphisms of the objects whose forward
direction gives a commutative triangle.
-/
def iso_mk {f g : under X} (hr : f.right â‰… g.right) (hw : f.hom â‰« hr.hom = g.hom) : f â‰… g :=
comma.iso_mk (eq_to_iso (subsingleton.elim _ _)) hr (by simp [hw])

@[simp]
lemma iso_mk_hom_right {f g : under X} (hr : f.right â‰… g.right) (hw : f.hom â‰« hr.hom = g.hom) :
  (iso_mk hr hw).hom.right = hr.hom := rfl

@[simp]
lemma iso_mk_inv_right {f g : under X} (hr : f.right â‰… g.right) (hw : f.hom â‰« hr.hom = g.hom) :
  (iso_mk hr hw).inv.right = hr.inv := rfl

/-- The forgetful functor mapping an arrow to its domain. -/
def forget : under X â¥¤ T := comma.snd _ _

@[simp] lemma forget_obj {U : under X} : forget.obj U = U.right := rfl
@[simp] lemma forget_map {U V : under X} {f : U âŸ¶ V} : forget.map f = f.right := rfl

/-- A morphism `X âŸ¶ Y` induces a functor `under Y â¥¤ under X` in the obvious way. -/
def map {Y : T} (f : X âŸ¶ Y) : under Y â¥¤ under X := comma.map_left _ $ discrete.nat_trans (Î» _, f)

section
variables {Y : T} {f : X âŸ¶ Y} {U V : under Y} {g : U âŸ¶ V}
@[simp] lemma map_obj_right : ((map f).obj U).right = U.right := rfl
@[simp] lemma map_obj_hom   : ((map f).obj U).hom   = f â‰« U.hom := rfl
@[simp] lemma map_map_right : ((map f).map g).right = g.right := rfl

/-- Mapping by the identity morphism is just the identity functor. -/
def map_id : map (ğŸ™ Y) â‰… ğŸ­ _ :=
nat_iso.of_components (Î» X, iso_mk (iso.refl _) (by tidy)) (by tidy)

/-- Mapping by the composite morphism `f â‰« g` is the same as mapping by `f` then by `g`. -/
def map_comp {Y Z : T} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) : map (f â‰« g) â‰… map g â‹™ map f :=
nat_iso.of_components (Î» X, iso_mk (iso.refl _) (by tidy)) (by tidy)

end

section
variables {D : Type uâ‚‚} [category.{vâ‚‚} D]

/-- A functor `F : T â¥¤ D` induces a functor `under X â¥¤ under (F.obj X)` in the obvious way. -/
@[simps]
def post {X : T} (F : T â¥¤ D) : under X â¥¤ under (F.obj X) :=
{ obj := Î» Y, mk $ F.map Y.hom,
  map := Î» Yâ‚ Yâ‚‚ f,
  { right := F.map f.right,
    w' := by tidy; erw [â† F.map_comp, w] } }

end

end under

end category_theory
