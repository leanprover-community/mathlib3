/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Simon Hudon, Scott Morrison
-/
import category_theory.natural_isomorphism
import category_theory.eq_to_hom

/-!
# Categories of indexed families of objects.

We define the pointwise category structure on indexed families of objects in a category
(and also the dependent generalization).

-/

namespace category_theory

universes wâ‚€ wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

variables {I : Type wâ‚€} (C : I â†’ Type uâ‚) [Î  i, category.{vâ‚} (C i)]

/--
`pi C` gives the cartesian product of an indexed family of categories.
-/
instance pi : category.{max wâ‚€ vâ‚} (Î  i, C i) :=
{ hom := Î» X Y, Î  i, X i âŸ¶ Y i,
  id := Î» X i, ğŸ™ (X i),
  comp := Î» X Y Z f g i, f i â‰« g i }

/--
This provides some assistance to typeclass search in a common situation,
which otherwise fails. (Without this `category_theory.pi.has_limit_of_has_limit_comp_eval` fails.)
-/
abbreviation pi' {I : Type vâ‚} (C : I â†’ Type uâ‚) [Î  i, category.{vâ‚} (C i)] :
  category.{vâ‚} (Î  i, C i) :=
category_theory.pi C

attribute [instance] pi'

namespace pi

@[simp] lemma id_apply (X : Î  i, C i) (i) : (ğŸ™ X : Î  i, X i âŸ¶ X i) i = ğŸ™ (X i) := rfl
@[simp] lemma comp_apply {X Y Z : Î  i, C i} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (i) :
  (f â‰« g : Î  i, X i âŸ¶ Z i) i = f i â‰« g i := rfl

/--
The evaluation functor at `i : I`, sending an `I`-indexed family of objects to the object over `i`.
-/
@[simps]
def eval (i : I) : (Î  i, C i) â¥¤ C i :=
{ obj := Î» f, f i,
  map := Î» f g Î±, Î± i, }

section
variables {J : Type wâ‚}

/--
Pull back an `I`-indexed family of objects to an `J`-indexed family, along a function `J â†’ I`.
-/
@[simps]
def comap (h : J â†’ I) : (Î  i, C i) â¥¤ (Î  j, C (h j)) :=
{ obj := Î» f i, f (h i),
  map := Î» f g Î± i, Î± (h i), }

variables (I)
/--
The natural isomorphism between
pulling back a grading along the identity function,
and the identity functor. -/
@[simps]
def comap_id : comap C (id : I â†’ I) â‰… ğŸ­ (Î  i, C i) :=
{ hom := { app := Î» X, ğŸ™ X },
  inv := { app := Î» X, ğŸ™ X } }.

variables {I}
variables {K : Type wâ‚‚}

/--
The natural isomorphism comparing between
pulling back along two successive functions, and
pulling back along their composition
-/
@[simps]
def comap_comp (f : K â†’ J) (g : J â†’ I) : comap C g â‹™ comap (C âˆ˜ g) f â‰… comap C (g âˆ˜ f) :=
{ hom := { app := Î» X b, ğŸ™ (X (g (f b))) },
  inv := { app := Î» X b, ğŸ™ (X (g (f b))) } }

/-- The natural isomorphism between pulling back then evaluating, and just evaluating. -/
@[simps]
def comap_eval_iso_eval (h : J â†’ I) (j : J) : comap C h â‹™ eval (C âˆ˜ h) j â‰… eval C (h j) :=
nat_iso.of_components (Î» f, iso.refl _) (by tidy)

end

section
variables {J : Type wâ‚€} {D : J â†’ Type uâ‚} [Î  j, category.{vâ‚} (D j)]

instance sum_elim_category : Î  (s : I âŠ• J), category.{vâ‚} (sum.elim C D s)
| (sum.inl i) := by { dsimp, apply_instance, }
| (sum.inr j) := by { dsimp, apply_instance, }

/--
The bifunctor combining an `I`-indexed family of objects with a `J`-indexed family of objects
to obtain an `I âŠ• J`-indexed family of objects.
-/
@[simps]
def sum : (Î  i, C i) â¥¤ (Î  j, D j) â¥¤ (Î  s : I âŠ• J, sum.elim C D s) :=
{ obj := Î» f,
  { obj := Î» g s, sum.rec f g s,
    map := Î» g g' Î± s, sum.rec (Î» i, ğŸ™ (f i)) Î± s },
  map := Î» f f' Î±,
  { app := Î» g s, sum.rec Î± (Î» j, ğŸ™ (g j)) s, }}

end

variables {C}

/-- An isomorphism between `I`-indexed objects gives an isomorphism between each
pair of corresponding components. -/
@[simps] def iso_app {X Y : Î  i, C i} (f : X â‰… Y) (i : I) : X i â‰… Y i :=
âŸ¨f.hom i, f.inv i, by { dsimp, rw [â† comp_apply, iso.hom_inv_id, id_apply] },
  by { dsimp, rw [â† comp_apply, iso.inv_hom_id, id_apply] }âŸ©

@[simp] lemma iso_app_refl (X : Î  i, C i) (i : I) : iso_app (iso.refl X) i = iso.refl (X i) := rfl
@[simp] lemma iso_app_symm {X Y : Î  i, C i} (f : X â‰… Y) (i : I) :
  iso_app f.symm i = (iso_app f i).symm := rfl
@[simp] lemma iso_app_trans {X Y Z : Î  i, C i} (f : X â‰… Y) (g : Y â‰… Z) (i : I) :
  iso_app (f â‰ªâ‰« g) i = iso_app f i â‰ªâ‰« iso_app g i := rfl

end pi

namespace functor

variables {C}
variables {D : I â†’ Type uâ‚} [âˆ€ i, category.{vâ‚} (D i)]

/--
Assemble an `I`-indexed family of functors into a functor between the pi types.
-/
@[simps]
def pi (F : Î  i, C i â¥¤ D i) : (Î  i, C i) â¥¤ (Î  i, D i) :=
{ obj := Î» f i, (F i).obj (f i),
  map := Î» f g Î± i, (F i).map (Î± i) }

-- One could add some natural isomorphisms showing
-- how `functor.pi` commutes with `pi.eval` and `pi.comap`.

end functor

namespace nat_trans

variables {C}
variables {D : I â†’ Type uâ‚} [âˆ€ i, category.{vâ‚} (D i)]
variables {F G : Î  i, C i â¥¤ D i}

/--
Assemble an `I`-indexed family of natural transformations into a single natural transformation.
-/
@[simps]
def pi (Î± : Î  i, F i âŸ¶ G i) : functor.pi F âŸ¶ functor.pi G :=
{ app := Î» f i, (Î± i).app (f i), }

end nat_trans

end category_theory
