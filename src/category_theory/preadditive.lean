/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel
-/
import algebra.group.hom
import category_theory.limits.shapes.kernels
import category_theory.limits.shapes.biproducts

/-!
# Preadditive categories

A preadditive category is a category in which `X âŸ¶ Y` is an abelian group in such a way that
composition of morphisms is linear in both variables.

This file contains a definition of preadditive category that directly encodes the definition given
above. The definition could also be phrased as follows: A preadditive category is a category
enriched over the category of Abelian groups. Once the general framework to state this in Lean is
available, the contents of this file should become obsolete.

## Main results

We define preadditive categories and show their basic properties, including the following
statements:

* In a preadditive category, `f : Q âŸ¶ R` is mono if and only if `g â‰« f = 0 â†’ g = 0` for all
  composable `g`.
* A preadditive category with kernels has equalizers.

Furthermore, we define the notion of "preadditive binary biproduct", which is a preadditive version
of the notion of biproduct. We show that a preadditive binary biproduct is a binary biproduct and
construct preadditive binary biproducts both from binary products and binary coproducts.

## Implementation notes

The simp normal form for negation and composition is to push negations as far as possible to
the outside. For example, `f â‰« (-g)` and `(-f) â‰« g` both become `-(f â‰« g)`, and `(-f) â‰« (-g)`
is simplified to `f â‰« g`.

## References

* [F. Borceux, *Handbook of Categorical Algebra 2*][borceux-vol2]

## Tags

additive, preadditive, Hom group, Ab-category, Ab-enriched
-/

universes v u

open category_theory.limits
open add_monoid_hom

namespace category_theory

variables (C : Type u) [category.{v} C]

/-- A category is called preadditive if `P âŸ¶ Q` is an abelian group such that composition is
    linear in both variables. -/
class preadditive :=
(hom_group : Î  P Q : C, add_comm_group (P âŸ¶ Q) . tactic.apply_instance)
(add_comp' : âˆ€ (P Q R : C) (f f' : P âŸ¶ Q) (g : Q âŸ¶ R),
  (f + f') â‰« g = f â‰« g + f' â‰« g . obviously)
(comp_add' : âˆ€ (P Q R : C) (f : P âŸ¶ Q) (g g' : Q âŸ¶ R),
  f â‰« (g + g') = f â‰« g + f â‰« g' . obviously)

attribute [instance] preadditive.hom_group
restate_axiom preadditive.add_comp'
restate_axiom preadditive.comp_add'
attribute [simp] preadditive.add_comp preadditive.comp_add

end category_theory

open category_theory

namespace category_theory.preadditive

section preadditive
variables {C : Type u} [category.{v} C] [preadditive.{v} C]

/-- Composition by a fixed left argument as a group homomorphism -/
def left_comp {P Q : C} (R : C) (f : P âŸ¶ Q) : (Q âŸ¶ R) â†’+ (P âŸ¶ R) :=
mk' (Î» g, f â‰« g) $ Î» g g', by simp

/-- Composition by a fixed right argument as a group homomorphism -/
def right_comp (P : C) {Q R : C} (g : Q âŸ¶ R) : (P âŸ¶ Q) â†’+ (P âŸ¶ R) :=
mk' (Î» f, f â‰« g) $ Î» f f', by simp

@[simp] lemma sub_comp {P Q R : C} (f f' : P âŸ¶ Q) (g : Q âŸ¶ R) :
  (f - f') â‰« g = f â‰« g - f' â‰« g :=
map_sub (right_comp P g) f f'

@[simp] lemma comp_sub {P Q R : C} (f : P âŸ¶ Q) (g g' : Q âŸ¶ R) :
  f â‰« (g - g') = f â‰« g - f â‰« g' :=
map_sub (left_comp R f) g g'

@[simp] lemma neg_comp {P Q R : C} (f : P âŸ¶ Q) (g : Q âŸ¶ R) : (-f) â‰« g = -(f â‰« g) :=
map_neg (right_comp _ _) _

@[simp] lemma comp_neg {P Q R : C} (f : P âŸ¶ Q) (g : Q âŸ¶ R) : f â‰« (-g) = -(f â‰« g) :=
map_neg (left_comp _ _) _

lemma neg_comp_neg {P Q R : C} (f : P âŸ¶ Q) (g : Q âŸ¶ R) : (-f) â‰« (-g) = f â‰« g :=
by simp

instance {P Q : C} {f : P âŸ¶ Q} [epi f] : epi (-f) :=
âŸ¨Î» R g g', by { rw [neg_comp, neg_comp, â†comp_neg, â†comp_neg, cancel_epi], exact neg_inj }âŸ©

instance {P Q : C} {f : P âŸ¶ Q} [mono f] : mono (-f) :=
âŸ¨Î» R g g', by { rw [comp_neg, comp_neg, â†neg_comp, â†neg_comp, cancel_mono], exact neg_inj }âŸ©

@[priority 100]
instance preadditive_has_zero_morphisms : has_zero_morphisms.{v} C :=
{ has_zero := infer_instance,
  comp_zero' := Î» P Q f R, map_zero $ left_comp R f,
  zero_comp' := Î» P Q R f, map_zero $ right_comp P f }

lemma mono_of_cancel_zero {Q R : C} (f : Q âŸ¶ R) (h : âˆ€ {P : C} (g : P âŸ¶ Q), g â‰« f = 0 â†’ g = 0) :
  mono f :=
âŸ¨Î» P g g' hg, sub_eq_zero.1 $ h _ $ (map_sub (right_comp P f) g g').trans $ sub_eq_zero.2 hgâŸ©

lemma mono_iff_cancel_zero {Q R : C} (f : Q âŸ¶ R) :
  mono f â†” âˆ€ (P : C) (g : P âŸ¶ Q), g â‰« f = 0 â†’ g = 0 :=
âŸ¨Î» m P g, by exactI zero_of_comp_mono _, mono_of_cancel_zero fâŸ©

lemma epi_of_cancel_zero {P Q : C} (f : P âŸ¶ Q) (h : âˆ€ {R : C} (g : Q âŸ¶ R), f â‰« g = 0 â†’ g = 0) :
  epi f :=
âŸ¨Î» R g g' hg, sub_eq_zero.1 $ h _ $ (map_sub (left_comp R f) g g').trans $ sub_eq_zero.2 hgâŸ©

lemma epi_iff_cancel_zero {P Q : C} (f : P âŸ¶ Q) :
  epi f â†” âˆ€ (R : C) (g : Q âŸ¶ R), f â‰« g = 0 â†’ g = 0 :=
âŸ¨Î» e R g, by exactI zero_of_epi_comp _, epi_of_cancel_zero fâŸ©

end preadditive

section equalizers
variables {C : Type u} [category.{v} C] [preadditive.{v} C]

section
variables {X Y : C} (f : X âŸ¶ Y) (g : X âŸ¶ Y)

/-- A kernel of `f - g` is an equalizer of `f` and `g`. -/
def has_limit_parallel_pair [has_limit (parallel_pair (f - g) 0)] :
  has_limit (parallel_pair f g) :=
{ cone := fork.of_Î¹ (kernel.Î¹ (f - g)) (sub_eq_zero.1 $
    by { rw â†comp_sub, exact kernel.condition _ }),
  is_limit := fork.is_limit.mk _
    (Î» s, kernel.lift (f - g) (fork.Î¹ s) $
      by { rw comp_sub, apply sub_eq_zero.2, exact fork.condition _ })
    (Î» s, by simp)
    (Î» s m h, by { ext, simpa using h walking_parallel_pair.zero }) }

end

section

/-- If a preadditive category has all kernels, then it also has all equalizers. -/
def has_equalizers_of_has_kernels [has_kernels.{v} C] : has_equalizers.{v} C :=
@has_equalizers_of_has_limit_parallel_pair _ _ (Î» _ _ f g, has_limit_parallel_pair f g)

end

section
variables {X Y : C} (f : X âŸ¶ Y) (g : X âŸ¶ Y)

/-- A cokernel of `f - g` is a coequalizer of `f` and `g`. -/
def has_colimit_parallel_pair [has_colimit (parallel_pair (f - g) 0)] :
  has_colimit (parallel_pair f g) :=
{ cocone := cofork.of_Ï€ (cokernel.Ï€ (f - g)) (sub_eq_zero.1 $
    by { rw â†sub_comp, exact cokernel.condition _ }),
  is_colimit := cofork.is_colimit.mk _
    (Î» s, cokernel.desc (f - g) (cofork.Ï€ s) $
      by { rw sub_comp, apply sub_eq_zero.2, exact cofork.condition _ })
    (Î» s, by simp)
    (Î» s m h, by { ext, simpa using h walking_parallel_pair.one }) }

end

section

/-- If a preadditive category has all cokernels, then it also has all coequalizers. -/
def has_coequalizers_of_has_cokernels [has_cokernels.{v} C] : has_coequalizers.{v} C :=
@has_coequalizers_of_has_colimit_parallel_pair _ _ (Î» _ _ f g, has_colimit_parallel_pair f g)

end

end equalizers

section biproduct
variables {C : Type u} [category.{v} C] [preadditive.{v} C]

/-- A preadditive binary biproduct is a bicone on two objects `X` and `Y` satisfying a set of five
    axioms expressing the properties of a biproduct in additive terms. The notion of preadditive
    binary biproduct is strictly stronger than the notion of binary biproduct (but it can be shown
    that in any preadditive category, the existence of a binary biproduct implies the existence of
    a preadditive binary biproduct). -/
class has_preadditive_binary_biproduct (X Y : C) :=
(bicone : binary_bicone.{v} X Y)
(Î¹â‚_Ï€â‚' : bicone.Î¹â‚ â‰« bicone.Ï€â‚ = ğŸ™ X . obviously)
(Î¹â‚‚_Ï€â‚‚' : bicone.Î¹â‚‚ â‰« bicone.Ï€â‚‚ = ğŸ™ Y . obviously)
(Î¹â‚‚_Ï€â‚' : bicone.Î¹â‚‚ â‰« bicone.Ï€â‚ = 0 . obviously)
(Î¹â‚_Ï€â‚‚' : bicone.Î¹â‚ â‰« bicone.Ï€â‚‚ = 0 . obviously)
(total' : bicone.Ï€â‚ â‰« bicone.Î¹â‚ + bicone.Ï€â‚‚ â‰« bicone.Î¹â‚‚ = ğŸ™ bicone.X . obviously)

restate_axiom has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚'
restate_axiom has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚‚'
restate_axiom has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚'
restate_axiom has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚‚'
restate_axiom has_preadditive_binary_biproduct.total'
attribute [simp, reassoc] has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚
  has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚‚ has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚
  has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚‚
attribute [simp] has_preadditive_binary_biproduct.total

section
local attribute [tidy] tactic.case_bash

/-- A preadditive binary biproduct is a binary biproduct. -/
@[priority 100]
instance (X Y : C) [has_preadditive_binary_biproduct.{v} X Y] : has_binary_biproduct.{v} X Y :=
{ bicone := has_preadditive_binary_biproduct.bicone,
  is_limit :=
  { lift := Î» s, binary_fan.fst s â‰« has_preadditive_binary_biproduct.bicone.Î¹â‚ +
      binary_fan.snd s â‰« has_preadditive_binary_biproduct.bicone.Î¹â‚‚,
    uniq' := Î» s m h, by erw [â†category.comp_id m, â†has_preadditive_binary_biproduct.total,
      comp_add, reassoc_of (h walking_pair.left), reassoc_of (h walking_pair.right)] },
  is_colimit :=
  { desc := Î» s, has_preadditive_binary_biproduct.bicone.Ï€â‚ â‰« binary_cofan.inl s +
      has_preadditive_binary_biproduct.bicone.Ï€â‚‚ â‰« binary_cofan.inr s,
    uniq' := Î» s m h, by erw [â†category.id_comp m, â†has_preadditive_binary_biproduct.total,
      add_comp, category.assoc, category.assoc, h walking_pair.left, h walking_pair.right] } }

end

section
variables (X Y : C) [has_preadditive_binary_biproduct.{v} X Y]

@[simp, reassoc] lemma biprod.inl_fst : (biprod.inl : X âŸ¶ X âŠ Y) â‰« biprod.fst = ğŸ™ X :=
has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚
@[simp, reassoc] lemma biprod.inr_snd : (biprod.inr : Y âŸ¶ X âŠ Y) â‰« biprod.snd = ğŸ™ Y :=
has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚‚
@[simp, reassoc] lemma biprod.inr_fst : (biprod.inr : Y âŸ¶ X âŠ Y) â‰« biprod.fst = 0 :=
has_preadditive_binary_biproduct.Î¹â‚‚_Ï€â‚
@[simp, reassoc] lemma biprod.inl_snd : (biprod.inl : X âŸ¶ X âŠ Y) â‰« biprod.snd = 0 :=
has_preadditive_binary_biproduct.Î¹â‚_Ï€â‚‚
@[simp] lemma biprod.total : biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y) :=
has_preadditive_binary_biproduct.total

end

section has_limit_pair

/-- In a preadditive category, if the product of `X` and `Y` exists, then the preadditive binary
    biproduct of `X` and `Y` exists. -/
def has_preadditive_binary_biproduct.of_has_limit_pair (X Y : C) [has_limit.{v} (pair X Y)] :
  has_preadditive_binary_biproduct.{v} X Y :=
{ bicone :=
  { X := X â¨¯ Y,
    Ï€â‚ := category_theory.limits.prod.fst,
    Ï€â‚‚ := category_theory.limits.prod.snd,
    Î¹â‚ := prod.lift (ğŸ™ X) 0,
    Î¹â‚‚ := prod.lift 0 (ğŸ™ Y) } }

/-- In a preadditive category, if the coproduct of `X` and `Y` exists, then the preadditive binary
    biproduct of `X` and `Y` exists. -/
def has_preadditive_binary_biproduct.of_has_colimit_pair (X Y : C) [has_colimit.{v} (pair X Y)] :
  has_preadditive_binary_biproduct.{v} X Y :=
{ bicone :=
  { X := X â¨¿ Y,
    Ï€â‚ := coprod.desc (ğŸ™ X) 0,
    Ï€â‚‚ := coprod.desc 0 (ğŸ™ Y),
    Î¹â‚ := category_theory.limits.coprod.inl,
    Î¹â‚‚ := category_theory.limits.coprod.inr } }

end has_limit_pair

section
variable (C)

/-- A preadditive category `has_preadditive_binary_biproducts` if the preadditive binary biproduct
    exists for every pair of objects. -/
class has_preadditive_binary_biproducts :=
(has_preadditive_binary_biproduct : Î  (X Y : C), has_preadditive_binary_biproduct.{v} X Y)

attribute [instance, priority 100] has_preadditive_binary_biproducts.has_preadditive_binary_biproduct

/-- If a preadditive category has all binary products, then it has all preadditive binary
    biproducts. -/
def has_preadditive_binary_biproducts_of_has_binary_products [has_binary_products.{v} C] :
  has_preadditive_binary_biproducts.{v} C :=
âŸ¨Î» X Y, has_preadditive_binary_biproduct.of_has_limit_pair X YâŸ©

/-- If a preadditive category has all binary coproducts, then it has all preadditive binary
    biproducts. -/
def has_preadditive_binary_biproducts_of_has_binary_coproducts [has_binary_coproducts.{v} C] :
  has_preadditive_binary_biproducts.{v} C :=
âŸ¨Î» X Y, has_preadditive_binary_biproduct.of_has_colimit_pair X YâŸ©

end

end biproduct
end category_theory.preadditive
