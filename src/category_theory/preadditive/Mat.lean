/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.preadditive.default
import category_theory.preadditive.single_obj
import category_theory.preadditive.additive_functor
import category_theory.limits.shapes.biproducts
import category_theory.Fintype
import algebra.big_operators.basic
import data.matrix.notation

/-!
# Matrices over a category.

When `C` is a preadditive category, `Mat_ C` is the preadditive category
whose objects are finite tuples of objects in `C`, and
whose morphisms are matrices of morphisms from `C`.

There is a functor `Mat_.embedding : C â¥¤ Mat_ C` sending morphisms to one-by-one matrices.

`Mat_ C` has finite biproducts.

## The additive envelope

We show that this construction is the "additive envelope" of `C`,
in the sense that any additive functor `F : C â¥¤ D` to a category `D` with biproducts
lifts to a functor `Mat_.lift F : Mat_ C â¥¤ D`,
Moreover, this functor is unique (up to natural isomorphisms) amongst functors `L : Mat_ C â¥¤ D`
such that `embedding C â‹™ L â‰… F`.
(As we don't have 2-category theory, we can't explicitly state that `Mat_ C` is
the initial object in the 2-category of categories under `C` which have biproducts.)

As a consequence, when `C` already has finite biproducts we have `Mat_ C â‰Œ C`.

## Future work

We should provide a more convenient `Mat R`, when `R` is a ring,
as a category with objects `n : FinType`,
and whose morphisms are matrices with components in `R`.

Ideally this would conveniently interact with both `Mat_` and `matrix`.

-/

open category_theory category_theory.preadditive
open_locale big_operators
noncomputable theory

namespace category_theory

universes w vâ‚ vâ‚‚ uâ‚ uâ‚‚
variables (C : Type uâ‚) [category.{vâ‚} C] [preadditive C]

/--
An object in `Mat_ C` is a finite tuple of objects in `C`.
-/
structure Mat_ : Type (max (vâ‚+1) uâ‚) :=
(Î¹ : Type vâ‚)
[F : fintype Î¹]
[D : decidable_eq Î¹]
(X : Î¹ â†’ C)

attribute [instance] Mat_.F Mat_.D

namespace Mat_

variables {C}

/-- A morphism in `Mat_ C` is a dependently typed matrix of morphisms. -/
@[nolint has_inhabited_instance]
def hom (M N : Mat_ C) : Type vâ‚ := dmatrix M.Î¹ N.Î¹ (Î» i j, M.X i âŸ¶ N.X j)

namespace hom

/-- The identity matrix consists of identity morphisms on the diagonal, and zeros elsewhere. -/
def id (M : Mat_ C) : hom M M := Î» i j, if h : i = j then eq_to_hom (congr_arg M.X h) else 0

/-- Composition of matrices using matrix multiplication. -/
def comp {M N K : Mat_ C} (f : hom M N) (g : hom N K) : hom M K :=
Î» i k, âˆ‘ j : N.Î¹, f i j â‰« g j k

end hom

section
local attribute [simp] hom.id hom.comp

instance : category.{vâ‚} (Mat_ C) :=
{ hom := hom,
  id := hom.id,
  comp := Î» M N K f g, f.comp g,
  id_comp' := Î» M N f, by simp [dite_comp],
  comp_id' := Î» M N f, by simp [comp_dite],
  assoc' := Î» M N K L f g h, begin
    ext i k,
    simp_rw [hom.comp, sum_comp, comp_sum, category.assoc],
    rw finset.sum_comm,
  end, }.

lemma id_def (M : Mat_ C) :
  (ğŸ™ M : hom M M) = Î» i j, if h : i = j then eq_to_hom (congr_arg M.X h) else 0 :=
rfl

lemma id_apply (M : Mat_ C) (i j : M.Î¹) :
  (ğŸ™ M : hom M M) i j = if h : i = j then eq_to_hom (congr_arg M.X h) else 0 :=
rfl

@[simp] lemma id_apply_self (M : Mat_ C) (i : M.Î¹) :
  (ğŸ™ M : hom M M) i i = ğŸ™ _ :=
by simp [id_apply]

@[simp] lemma id_apply_of_ne (M : Mat_ C) (i j : M.Î¹) (h : i â‰  j) :
  (ğŸ™ M : hom M M) i j = 0 :=
by simp [id_apply, h]

lemma comp_def {M N K : Mat_ C} (f : M âŸ¶ N) (g : N âŸ¶ K) :
  (f â‰« g) = Î» i k, âˆ‘ j : N.Î¹, f i j â‰« g j k := rfl

@[simp] lemma comp_apply {M N K : Mat_ C} (f : M âŸ¶ N) (g : N âŸ¶ K) (i k) :
  (f â‰« g) i k = âˆ‘ j : N.Î¹, f i j â‰« g j k := rfl

instance (M N : Mat_ C) : inhabited (M âŸ¶ N) := âŸ¨Î» i j, (0 : M.X i âŸ¶ N.X j)âŸ©

end

instance : preadditive (Mat_ C) :=
{ hom_group := Î» M N, by { change add_comm_group (dmatrix M.Î¹ N.Î¹ _), apply_instance, },
  add_comp' := Î» M N K f f' g, by { ext, simp [finset.sum_add_distrib], },
  comp_add' := Î» M N K f g g', by { ext, simp [finset.sum_add_distrib], }, }

@[simp] lemma add_apply {M N : Mat_ C} (f g : M âŸ¶ N) (i j) : (f + g) i j = f i j + g i j := rfl

open category_theory.limits

/--
We now prove that `Mat_ C` has finite biproducts.

Be warned, however, that `Mat_ C` is not necessarily Krull-Schmidt,
and so the internal indexing of a biproduct may have nothing to do with the external indexing,
even though the construction we give uses a sigma type.
See however `iso_biproduct_embedding`.
-/
instance has_finite_biproducts : has_finite_biproducts (Mat_ C) :=
{ has_biproducts_of_shape := Î» J ğ’Ÿ â„±, by exactI
  { has_biproduct := Î» f,
    has_biproduct_of_total
    { X := âŸ¨Î£ j : J, (f j).Î¹, Î» p, (f p.1).X p.2âŸ©,
      Ï€ := Î» j x y,
      begin
        dsimp at x âŠ¢,
        refine if h : x.1 = j then _ else 0,
        refine if h' : (@eq.rec J x.1 (Î» j, (f j).Î¹) x.2 _ h) = y then _ else 0,
        apply eq_to_hom,
        substs h h', -- Notice we were careful not to use `subst` until we had a goal in `Prop`.
      end,
      Î¹ := Î» j x y,
      begin
        dsimp at y âŠ¢,
        refine if h : y.1 = j then _ else 0,
        refine if h' : (@eq.rec J y.1 (Î» j, (f j).Î¹) y.2 _ h) = x then _ else 0,
        apply eq_to_hom,
        substs h h',
      end,
      Î¹_Ï€ := Î» j j',
      begin
        ext x y,
        dsimp,
        simp_rw [dite_comp, comp_dite],
        simp only [if_t_t, dite_eq_ite, dif_ctx_congr, limits.comp_zero, limits.zero_comp,
          eq_to_hom_trans, finset.sum_congr],
        erw finset.sum_sigma,
        dsimp,
        simp only [if_congr, if_true, dif_ctx_congr, finset.sum_dite_irrel, finset.mem_univ,
          finset.sum_const_zero, finset.sum_congr, finset.sum_dite_eq'],
        split_ifs with h h',
        { substs h h', simp, },
        { subst h, simp at h', simp [h'], },
        { refl, },
      end, }
    begin
      funext iâ‚,
      dsimp at iâ‚ âŠ¢,
      rcases iâ‚ with âŸ¨jâ‚, iâ‚âŸ©,
      -- I'm not sure why we can't just `simp` by `finset.sum_apply`: something doesn't quite match
      convert finset.sum_apply _ _ _,
      { refl, },
      { apply heq_of_eq,
        symmetry,
        funext iâ‚‚,
        rcases iâ‚‚ with âŸ¨jâ‚‚, iâ‚‚âŸ©,
        simp only [comp_apply, dite_comp, comp_dite,
          if_t_t, dite_eq_ite, if_congr, if_true, dif_ctx_congr,
          finset.sum_dite_irrel, finset.sum_dite_eq, finset.mem_univ, finset.sum_const_zero,
          finset.sum_congr, finset.sum_dite_eq, finset.sum_apply,
          limits.comp_zero, limits.zero_comp, eq_to_hom_trans, Mat_.id_apply],
        by_cases h : jâ‚ = jâ‚‚,
        { subst h, simp, },
        { simp [h], }, },
    end }}.

end Mat_

namespace functor
variables {C} {D : Type*} [category.{vâ‚} D] [preadditive D]

local attribute [simp] Mat_.id_apply

/--
A functor induces a functor of matrix categories.
-/
@[simps]
def map_Mat_ (F : C â¥¤ D) [functor.additive F] : Mat_ C â¥¤ Mat_ D :=
{ obj := Î» M, âŸ¨M.Î¹, Î» i, F.obj (M.X i)âŸ©,
  map := Î» M N f i j, F.map (f i j),
  map_comp' := Î» M N K f g, by { ext i k, simp,}, }

/--
The identity functor induces the identity functor on matrix categories.
-/
@[simps]
def map_Mat_id : (ğŸ­ C).map_Mat_ â‰… ğŸ­ (Mat_ C) :=
nat_iso.of_components (Î» M, eq_to_iso (by { cases M, refl, }))
(Î» M N f, begin
  ext i j,
  cases M, cases N,
  simp [comp_dite, dite_comp],
end)

/--
Composite functors induce composite functors on matrix categories.
-/
@[simps]
def map_Mat_comp {E : Type*} [category.{vâ‚} E] [preadditive E]
  (F : C â¥¤ D) [functor.additive F] (G : D â¥¤ E) [functor.additive G] :
  (F â‹™ G).map_Mat_ â‰… F.map_Mat_ â‹™ G.map_Mat_ :=
nat_iso.of_components (Î» M, eq_to_iso (by { cases M, refl, }))
(Î» M N f, begin
  ext i j,
  cases M, cases N,
  simp [comp_dite, dite_comp],
end)

end functor

namespace Mat_

variables (C)

/-- The embedding of `C` into `Mat_ C` as one-by-one matrices.
(We index the summands by `punit`.) -/
@[simps]
def embedding : C â¥¤ Mat_ C :=
{ obj := Î» X, âŸ¨punit, Î» _, XâŸ©,
  map := Î» X Y f, Î» _ _, f,
  map_id' := Î» X, by { ext âŸ¨âŸ© âŸ¨âŸ©, simp, },
  map_comp' := Î» X Y Z f g, by { ext âŸ¨âŸ© âŸ¨âŸ©, simp, }, }

namespace embedding

instance : faithful (embedding C) :=
{ map_injective' := Î» X Y f g h, congr_fun (congr_fun h punit.star) punit.star, }

instance : full (embedding C) :=
{ preimage := Î» X Y f, f punit.star punit.star, }

instance : functor.additive (embedding C) := {}

end embedding

instance [inhabited C] : inhabited (Mat_ C) := âŸ¨(embedding C).obj (default C)âŸ©

open category_theory.limits

variables {C}

/--
Every object in `Mat_ C` is isomorphic to the biproduct of its summands.
-/
@[simps]
def iso_biproduct_embedding (M : Mat_ C) : M â‰… â¨ (Î» i, (embedding C).obj (M.X i)) :=
{ hom := biproduct.lift (Î» i j k, if h : j = i then eq_to_hom (congr_arg M.X h) else 0),
  inv := biproduct.desc (Î» i j k, if h : i = k then eq_to_hom (congr_arg M.X h) else 0),
  hom_inv_id' :=
  begin
    simp only [biproduct.lift_desc],
    funext i,
    dsimp,
    convert finset.sum_apply _ _ _,
    { dsimp, refl, },
    { apply heq_of_eq,
      symmetry,
      funext j,
      simp only [finset.sum_apply],
      dsimp,
      simp [dite_comp, comp_dite, Mat_.id_apply], }
  end,
  inv_hom_id' :=
  begin
    apply biproduct.hom_ext,
    intro i,
    apply biproduct.hom_ext',
    intro j,
    simp only [category.id_comp, category.assoc,
      biproduct.lift_Ï€, biproduct.Î¹_desc_assoc, biproduct.Î¹_Ï€],
    ext âŸ¨âŸ© âŸ¨âŸ©,
    simp [dite_comp, comp_dite],
    split_ifs,
    { subst h, simp, },
    { simp [h], },
  end, }.

variables {D : Type uâ‚} [category.{vâ‚} D] [preadditive D]

/-- Every `M` is a direct sum of objects from `C`, and `F` preserves biproducts. -/
@[simps]
def additive_obj_iso_biproduct (F : Mat_ C â¥¤ D) [functor.additive F] (M : Mat_ C) :
  F.obj M â‰… â¨ (Î» i, F.obj ((embedding C).obj (M.X i))) :=
(F.map_iso (iso_biproduct_embedding M)) â‰ªâ‰« (F.map_biproduct _)

variables [has_finite_biproducts D]

@[reassoc] lemma additive_obj_iso_biproduct_naturality (F : Mat_ C â¥¤ D) [functor.additive F]
  {M N : Mat_ C} (f : M âŸ¶ N) :
  F.map f â‰« (additive_obj_iso_biproduct F N).hom =
    (additive_obj_iso_biproduct F M).hom â‰«
      biproduct.matrix (Î» i j, F.map ((embedding C).map (f i j))) :=
begin
  -- This is disappointingly tedious.
  ext,
  dsimp [embedding],
  simp only [â†F.map_comp, biproduct.lift_Ï€, biproduct.matrix_Ï€, category.assoc],
  simp only [â†F.map_comp, â†F.map_sum, biproduct.lift_desc, biproduct.lift_Ï€_assoc, comp_sum],
  simp only [comp_def, comp_dite, comp_zero, finset.sum_dite_eq', finset.mem_univ, if_true],
  dsimp,
  simp only [finset.sum_singleton, dite_comp, zero_comp],
  congr,
  symmetry,
  convert finset.sum_fn _ _, -- It's hard to use this as a simp lemma!
  simp only [finset.sum_fn, finset.sum_dite_eq],
  ext,
  simp,
end

@[reassoc] lemma additive_obj_iso_biproduct_naturality' (F : Mat_ C â¥¤ D) [functor.additive F]
  {M N : Mat_ C} (f : M âŸ¶ N) :
  (additive_obj_iso_biproduct F M).inv â‰« F.map f =
    biproduct.matrix (Î» i j, F.map ((embedding C).map (f i j)) : _) â‰«
      (additive_obj_iso_biproduct F N).inv :=
by rw [iso.inv_comp_eq, â†category.assoc, iso.eq_comp_inv, additive_obj_iso_biproduct_naturality]

/-- Any additive functor `C â¥¤ D` to a category `D` with finite biproducts extends to
a functor `Mat_ C â¥¤ D`. -/
@[simps]
def lift (F : C â¥¤ D) [functor.additive F] : Mat_ C â¥¤ D :=
{ obj := Î» X, â¨ (Î» i, F.obj (X.X i)),
  map := Î» X Y f, biproduct.matrix (Î» i j, F.map (f i j)),
  map_id' := Î» X, begin
    ext i j,
    by_cases h : i = j,
    { subst h, simp, },
    { simp [h, Mat_.id_apply], },
  end,
  map_comp' := Î» X Y Z f g, by { ext i j, simp, }, }.

instance lift_additive (F : C â¥¤ D) [functor.additive F] : functor.additive (lift F) := {}

/-- An additive functor `C â¥¤ D` factors through its lift to `Mat_ C â¥¤ D`. -/
@[simps]
def embedding_lift_iso (F : C â¥¤ D) [functor.additive F] : embedding C â‹™ lift F â‰… F :=
nat_iso.of_components (Î» X,
  { hom := biproduct.desc (Î» P, ğŸ™ (F.obj X)),
    inv := biproduct.lift (Î» P, ğŸ™ (F.obj X)), })
(Î» X Y f, begin
  dsimp,
  ext,
  simp only [category.id_comp, biproduct.Î¹_desc_assoc],
  erw biproduct.Î¹_matrix_assoc, -- Not sure why this doesn't fire via `simp`.
  simp,
end).

/--
`Mat_.lift F` is the unique additive functor `L : Mat_ C â¥¤ D` such that `F â‰… embedding C â‹™ L`.
-/
def lift_unique (F : C â¥¤ D) [functor.additive F] (L : Mat_ C â¥¤ D) [functor.additive L]
  (Î± : embedding C â‹™ L â‰… F) :
  L â‰… lift F :=
nat_iso.of_components
  (Î» M, (additive_obj_iso_biproduct L M) â‰ªâ‰«
    (biproduct.map_iso (Î» i, Î±.app (M.X i))) â‰ªâ‰«
    (biproduct.map_iso (Î» i, (embedding_lift_iso F).symm.app (M.X i))) â‰ªâ‰«
    (additive_obj_iso_biproduct (lift F) M).symm)
(Î» M N f, begin
  dsimp only [iso.trans_hom, iso.symm_hom, biproduct.map_iso_hom],
  simp only [additive_obj_iso_biproduct_naturality_assoc],
  simp only [biproduct.matrix_map_assoc, category.assoc],
  simp only [additive_obj_iso_biproduct_naturality'],
  simp only [biproduct.map_matrix_assoc, category.assoc],
  congr,
  ext j k âŸ¨âŸ©,
  dsimp, simp,
  convert Î±.hom.naturality (f j k),
  erw [biproduct.matrix_Ï€],
  simp,
end).

-- TODO is there some uniqueness statement for the natural isomorphism in `lift_unique`?

/-- Two additive functors `Mat_ C â¥¤ D` are naturally isomorphic if
their precompositions with `embedding C` are naturally isomorphic as functors `C â¥¤ D`. -/
@[ext]
def ext {F G : Mat_ C â¥¤ D} [functor.additive F] [functor.additive G]
  (Î± : embedding C â‹™ F â‰… embedding C â‹™ G) : F â‰… G :=
(lift_unique (embedding C â‹™ G) _ Î±) â‰ªâ‰« (lift_unique _ _ (iso.refl _)).symm

/--
Natural isomorphism needed in the construction of `equivalence_self_of_has_finite_biproducts`.
-/
def equivalence_self_of_has_finite_biproducts_aux [has_finite_biproducts C] :
  embedding C â‹™ ğŸ­ (Mat_ C) â‰… embedding C â‹™ lift (ğŸ­ C) â‹™ embedding C :=
functor.right_unitor _ â‰ªâ‰«
  (functor.left_unitor _).symm â‰ªâ‰«
  (iso_whisker_right (embedding_lift_iso _).symm _) â‰ªâ‰«
  functor.associator _ _ _

/--
A preadditive category that already has finite biproducts is equivalent to its additive envelope.

Note that we only prove this for a large category;
otherwise there are universe issues that I haven't attempted to sort out.
-/
def equivalence_self_of_has_finite_biproducts
  (C : Type (uâ‚+1)) [large_category C] [preadditive C] [has_finite_biproducts C] :
  Mat_ C â‰Œ C :=
equivalence.mk -- I suspect this is already an adjoint equivalence, but it seems painful to verify.
  (lift (ğŸ­ C))
  (embedding C)
  (ext equivalence_self_of_has_finite_biproducts_aux)
  (embedding_lift_iso (ğŸ­ C))

@[simp] lemma equivalence_self_of_has_finite_biproducts_functor
  {C : Type (uâ‚+1)} [large_category C] [preadditive C] [has_finite_biproducts C] :
  (equivalence_self_of_has_finite_biproducts C).functor = lift (ğŸ­ C) :=
rfl

@[simp] lemma equivalence_self_of_has_finite_biproducts_inverse
  {C : Type (uâ‚+1)} [large_category C] [preadditive C] [has_finite_biproducts C] :
  (equivalence_self_of_has_finite_biproducts C).inverse = embedding C :=
rfl

end Mat_

universe u

/-- A type synonym for `Fintype`, which we will equip with a category structure
where the morphisms are matrices with components in `R`. -/
@[nolint unused_arguments, derive [has_coe_to_sort]]
def Mat (R : Type u) := Fintype

open_locale classical matrix

instance (R : Type u) [semiring R] : category (Mat R) :=
{ hom := Î» X Y, matrix X Y R,
  id := Î» X, 1,
  comp := Î» X Y Z f g, f â¬ g,
  assoc' := by { intros, simp [matrix.mul_assoc], }, }

namespace Mat

section
variables (R : Type u) [semiring R]

lemma id_def (M : Mat R) :
  ğŸ™ M = Î» i j, if h : i = j then 1 else 0 :=
rfl

lemma id_apply (M : Mat R) (i j : M) :
  (ğŸ™ M : matrix M M R) i j = if h : i = j then 1 else 0 :=
rfl

@[simp] lemma id_apply_self (M : Mat R) (i : M) :
  (ğŸ™ M : matrix M M R) i i = 1 :=
by simp [id_apply]

@[simp] lemma id_apply_of_ne (M : Mat R) (i j : M) (h : i â‰  j) :
  (ğŸ™ M : matrix M M R) i j = 0 :=
by simp [id_apply, h]

lemma comp_def {M N K : Mat R} (f : M âŸ¶ N) (g : N âŸ¶ K) :
  (f â‰« g) = Î» i k, âˆ‘ j : N, f i j * g j k := rfl

@[simp] lemma comp_apply {M N K : Mat R} (f : M âŸ¶ N) (g : N âŸ¶ K) (i k) :
  (f â‰« g) i k = âˆ‘ j : N, f i j * g j k := rfl

instance (M N : Mat R) : inhabited (M âŸ¶ N) := âŸ¨Î» (i : M) (j : N), (0 : R)âŸ©

end

variables (R : Type u) [ring R]

def functor_single_obj : Mat R â¥¤ Mat_ (single_obj R) :=
{ obj := Î» X, { Î¹ := X, X := Î» _, punit.star },
  map := Î» X Y f, f, }

def equivalence_single_obj : Mat R â‰Œ Mat_ (single_obj R) := sorry

end Mat

end category_theory
