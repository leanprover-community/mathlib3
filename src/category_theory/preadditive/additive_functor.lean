/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, Scott Morrison
-/
import category_theory.preadditive
import category_theory.limits.shapes.biproducts

/-!
# Additive Functors

A functor between two preadditive categories is called *additive*
provided that the induced map on hom types is a morphism of abelian
groups.

An additive functor between preadditive categories creates and preserves biproducts.

# Implementation details

`functor.additive` is a `Prop`-valued class, defined by saying that
for every two objects `X` and `Y`, the map
`F.map : (X ‚ü∂ Y) ‚Üí (F.obj X ‚ü∂ F.obj Y)` is a morphism of abelian
groups.

# Project:

- Prove that a functor is additive if it preserves finite biproducts
  (See https://stacks.math.columbia.edu/tag/010M.)
-/

namespace category_theory

/-- A functor `F` is additive provided `F.map` is an additive homomorphism. -/
class functor.additive {C D : Type*} [category C] [category D]
  [preadditive C] [preadditive D] (F : C ‚•§ D) : Prop :=
(map_zero' : Œ† {X Y : C}, F.map (0 : X ‚ü∂ Y) = 0 . obviously)
(map_add' : Œ† {X Y : C} {f g : X ‚ü∂ Y}, F.map (f + g) = F.map f + F.map g . obviously)

section preadditive

namespace functor

section
variables {C D : Type*} [category C] [category D] [preadditive C]
  [preadditive D] (F : C ‚•§ D) [functor.additive F]

@[simp]
lemma map_zero {X Y : C} : F.map (0 : X ‚ü∂ Y) = 0 :=
functor.additive.map_zero'

@[simp]
lemma map_add {X Y : C} {f g : X ‚ü∂ Y} : F.map (f + g) = F.map f + F.map g :=
functor.additive.map_add'

instance : additive (ùü≠ C) :=
{}

instance {E : Type*} [category E] [preadditive E] (G : D ‚•§ E) [functor.additive G] :
  additive (F ‚ãô G) :=
{}

/-- `F.map_add_hom` is an additive homomorphism whose underlying function is `F.map`. -/
@[simps]
def map_add_hom {X Y : C} : (X ‚ü∂ Y) ‚Üí+ (F.obj X ‚ü∂ F.obj Y) :=
{ to_fun := Œª f, F.map f,
  map_zero' := F.map_zero,
  map_add' := Œª _ _, F.map_add }

lemma coe_map_add_hom {X Y : C} : ‚áë(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ _) = @map C _ D _ F X Y := rfl

@[simp]
lemma map_neg {X Y : C} {f : X ‚ü∂ Y} : F.map (-f) = - F.map f :=
F.map_add_hom.map_neg _

@[simp]
lemma map_sub {X Y : C} {f g : X ‚ü∂ Y} : F.map (f - g) = F.map f - F.map g :=
F.map_add_hom.map_sub _ _

-- You can alternatively just use `functor.map_smul` here, with an explicit `(r : ‚Ñ§)` argument.
lemma map_gsmul {X Y : C} {f : X ‚ü∂ Y} {r : ‚Ñ§} : F.map (r ‚Ä¢ f) = r ‚Ä¢ F.map f :=
F.map_add_hom.map_gsmul _ _

open_locale big_operators

@[simp]
lemma map_sum {X Y : C} {Œ± : Type*} (f : Œ± ‚Üí (X ‚ü∂ Y)) (s : finset Œ±) :
  F.map (‚àë a in s, f a) = ‚àë a in s, F.map (f a) :=
(F.map_add_hom : (X ‚ü∂ Y) ‚Üí+ _).map_sum f s

open category_theory.limits
open_locale zero_object

/-- An additive functor takes the zero object to the zero object (up to isomorphism). -/
@[simps]
def map_zero_object [has_zero_object C] [has_zero_object D] : F.obj 0 ‚âÖ 0 :=
{ hom := 0,
  inv := 0,
  hom_inv_id' := by { rw ‚ÜêF.map_id, simp, } }

end

section induced_category
variables {C : Type*} {D : Type*} [category D] [preadditive D] (F : C ‚Üí D)

instance induced_functor_additive : functor.additive (induced_functor F) := {}

end induced_category

section
-- To talk about preservation of biproducts we need to specify universes explicitly.

noncomputable theory
universes v u‚ÇÅ u‚ÇÇ

variables {C : Type u‚ÇÅ} {D : Type u‚ÇÇ} [category.{v} C] [category.{v} D]
  [preadditive C] [preadditive D] (F : C ‚•§ D) [functor.additive F]

open category_theory.limits

/--
An additive functor between preadditive categories creates finite biproducts.
-/
instance map_has_biproduct {J : Type v} [fintype J] [decidable_eq J] (f : J ‚Üí C) [has_biproduct f] :
  has_biproduct (Œª j, F.obj (f j)) :=
has_biproduct_of_total
{ X := F.obj (‚®Å f),
  œÄ := Œª j, F.map (biproduct.œÄ f j),
  Œπ := Œª j, F.map (biproduct.Œπ f j),
  Œπ_œÄ := Œª j j', by { simp only [‚ÜêF.map_comp], split_ifs, { subst h, simp, }, { simp [h], }, }, }
(by simp_rw [‚ÜêF.map_comp, ‚ÜêF.map_sum, biproduct.total, functor.map_id])

/--
An additive functor between preadditive categories preserves finite biproducts.
-/
-- This essentially repeats the work of the previous instance,
-- but gives good definitional reduction to `biproduct.lift` and `biproduct.desc`.
@[simps]
def map_biproduct {J : Type v} [fintype J] [decidable_eq J] (f : J ‚Üí C) [has_biproduct f] :
  F.obj (‚®Å f) ‚âÖ ‚®Å (Œª j, F.obj (f j)) :=
{ hom := biproduct.lift (Œª j, F.map (biproduct.œÄ f j)),
  inv := biproduct.desc (Œª j, F.map (biproduct.Œπ f j)),
  hom_inv_id' :=
  by simp only [biproduct.lift_desc, ‚ÜêF.map_comp, ‚ÜêF.map_sum, biproduct.total, F.map_id],
  inv_hom_id' :=
  begin
    ext j j',
    simp only [category.comp_id,  category.assoc, biproduct.lift_œÄ, biproduct.Œπ_desc_assoc,
      ‚ÜêF.map_comp, biproduct.Œπ_œÄ, F.map_dite, dif_ctx_congr, eq_to_hom_map, F.map_zero],
  end }

end

end functor

namespace equivalence

variables {C D : Type*} [category C] [category D] [preadditive C] [preadditive D]

instance inverse_additive (e : C ‚âå D) [e.functor.additive] : e.inverse.additive :=
{ map_zero' := Œª X Y, by { apply e.functor.map_injective, simp, },
  map_add' := Œª X Y f g, by { apply e.functor.map_injective, simp, }, }

end equivalence

end preadditive

end category_theory
