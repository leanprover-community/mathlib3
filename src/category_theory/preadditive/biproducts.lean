/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import algebra.group.ext
import category_theory.limits.shapes.biproducts
import category_theory.limits.preserves.shapes.binary_products
import category_theory.limits.preserves.shapes.biproducts
import category_theory.limits.preserves.shapes.products
import category_theory.preadditive.basic
import tactic.abel

/-!
# Basic facts about biproducts in preadditive categories.

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In (or between) preadditive categories,

* Any biproduct satisfies the equality
  `total : âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f)`,
  or, in the binary case, `total : fst â‰« inl + snd â‰« inr = ğŸ™ X`.

* Any (binary) `product` or (binary) `coproduct` is a (binary) `biproduct`.

* In any category (with zero morphisms), if `biprod.map f g` is an isomorphism,
  then both `f` and `g` are isomorphisms.

* If `f` is a morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
  then we can construct isomorphisms `L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚` and `R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚`
  so that `L.hom â‰« g â‰« R.hom` is diagonal (with `Xâ‚ âŸ¶ Yâ‚` component still `f`),
  via Gaussian elimination.

* As a corollary of the previous two facts,
  if we have an isomorphism `Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
  we can construct an isomorphism `Xâ‚‚ â‰… Yâ‚‚`.

* If `f : W âŠ X âŸ¶ Y âŠ Z` is an isomorphism, either `ğŸ™ W = 0`,
  or at least one of the component maps `W âŸ¶ Y` and `W âŸ¶ Z` is nonzero.

* If `f : â¨ S âŸ¶ â¨ T` is an isomorphism,
  then every column (corresponding to a nonzero summand in the domain)
  has some nonzero matrix entry.

* A functor preserves a biproduct if and only if it preserves
  the corresponding product if and only if it preserves the corresponding coproduct.
-/

open category_theory
open category_theory.preadditive
open category_theory.limits
open category_theory.functor
open category_theory.preadditive

open_locale classical
open_locale big_operators

universes v v' u u'

noncomputable theory

namespace category_theory

variables {C : Type u} [category.{v} C] [preadditive C]

namespace limits

variables {J : Type} [fintype J]

/--
In a preadditive category, we can construct a biproduct for `f : J â†’ C` from
any bicone `b` for `f` satisfying `total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
def is_bilimit_of_total {f : J â†’ C} (b : bicone f) (total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X) :
  b.is_bilimit :=
{ is_limit :=
  { lift := Î» s, âˆ‘ (j : J), s.Ï€.app âŸ¨jâŸ© â‰« b.Î¹ j,
    uniq' := Î» s m h,
    begin
      erw [â†category.comp_id m, â†total, comp_sum],
      apply finset.sum_congr rfl,
      intros j m,
      erw [reassoc_of (h âŸ¨jâŸ©)],
    end,
    fac' := Î» s j,
    begin
      cases j,
      simp only [sum_comp, category.assoc, bicone.to_cone_Ï€_app, b.Î¹_Ï€, comp_dite],
      -- See note [dsimp, simp].
      dsimp, simp,
    end },
  is_colimit :=
  { desc := Î» s, âˆ‘ (j : J), b.Ï€ j â‰« s.Î¹.app âŸ¨jâŸ©,
    uniq' := Î» s m h,
    begin
      erw [â†category.id_comp m, â†total, sum_comp],
            apply finset.sum_congr rfl,
      intros j m,
      erw [category.assoc, h âŸ¨jâŸ©],
    end,
    fac' := Î» s j,
    begin
      cases j,
      simp only [comp_sum, â†category.assoc, bicone.to_cocone_Î¹_app, b.Î¹_Ï€, dite_comp],
      dsimp, simp,
    end } }

lemma is_bilimit.total {f : J â†’ C} {b : bicone f} (i : b.is_bilimit) :
  âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X :=
i.is_limit.hom_ext (Î» j, by { cases j, simp [sum_comp, b.Î¹_Ï€, comp_dite] })

/--
In a preadditive category, we can construct a biproduct for `f : J â†’ C` from
any bicone `b` for `f` satisfying `total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
lemma has_biproduct_of_total {f : J â†’ C} (b : bicone f) (total : âˆ‘ j : J, b.Ï€ j â‰« b.Î¹ j = ğŸ™ b.X) :
  has_biproduct f :=
has_biproduct.mk
{ bicone := b,
  is_bilimit := is_bilimit_of_total b total }

/-- In a preadditive category, any finite bicone which is a limit cone is in fact a bilimit
    bicone. -/
def is_bilimit_of_is_limit {f : J â†’ C} (t : bicone f) (ht : is_limit t.to_cone) : t.is_bilimit :=
is_bilimit_of_total _ $ ht.hom_ext $
  Î» j, by { cases j, simp [sum_comp, t.Î¹_Ï€, dite_comp, comp_dite] }

/-- We can turn any limit cone over a pair into a bilimit bicone. -/
def bicone_is_bilimit_of_limit_cone_of_is_limit {f : J â†’ C} {t : cone (discrete.functor f)}
  (ht : is_limit t) : (bicone.of_limit_cone ht).is_bilimit :=
is_bilimit_of_is_limit _ $
  is_limit.of_iso_limit ht $ cones.ext (iso.refl _) (by { rintro âŸ¨jâŸ©, tidy })

/-- In a preadditive category, if the product over `f : J â†’ C` exists,
    then the biproduct over `f` exists. -/
lemma has_biproduct.of_has_product {J : Type} [finite J] (f : J â†’ C) [has_product f] :
  has_biproduct f :=
by casesI nonempty_fintype J; exact
has_biproduct.mk
{ bicone := _,
  is_bilimit := bicone_is_bilimit_of_limit_cone_of_is_limit (limit.is_limit _) }

/-- In a preadditive category, any finite bicone which is a colimit cocone is in fact a bilimit
    bicone. -/
def is_bilimit_of_is_colimit {f : J â†’ C} (t : bicone f) (ht : is_colimit t.to_cocone) :
  t.is_bilimit :=
is_bilimit_of_total _ $ ht.hom_ext $ Î» j, begin
  cases j,
  simp_rw [bicone.to_cocone_Î¹_app, comp_sum, â† category.assoc, t.Î¹_Ï€, dite_comp],
  tidy
end

/-- We can turn any limit cone over a pair into a bilimit bicone. -/
def bicone_is_bilimit_of_colimit_cocone_of_is_colimit {f : J â†’ C} {t : cocone (discrete.functor f)}
  (ht : is_colimit t) : (bicone.of_colimit_cocone ht).is_bilimit :=
is_bilimit_of_is_colimit _ $
  is_colimit.of_iso_colimit ht $ cocones.ext (iso.refl _) (by { rintro âŸ¨jâŸ©, tidy })

/-- In a preadditive category, if the coproduct over `f : J â†’ C` exists,
    then the biproduct over `f` exists. -/
lemma has_biproduct.of_has_coproduct {J : Type} [finite J] (f : J â†’ C) [has_coproduct f] :
  has_biproduct f :=
by casesI nonempty_fintype J; exact
has_biproduct.mk
{ bicone := _,
  is_bilimit := bicone_is_bilimit_of_colimit_cocone_of_is_colimit (colimit.is_colimit _) }

/-- A preadditive category with finite products has finite biproducts. -/
lemma has_finite_biproducts.of_has_finite_products [has_finite_products C] :
  has_finite_biproducts C :=
âŸ¨Î» n, { has_biproduct := Î» F, has_biproduct.of_has_product _ }âŸ©

/-- A preadditive category with finite coproducts has finite biproducts. -/
lemma has_finite_biproducts.of_has_finite_coproducts [has_finite_coproducts C] :
  has_finite_biproducts C :=
âŸ¨Î» n, { has_biproduct := Î» F, has_biproduct.of_has_coproduct _ }âŸ©

section
variables {f : J â†’ C} [has_biproduct f]

/--
In any preadditive category, any biproduct satsifies
`âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f)`
-/
@[simp] lemma biproduct.total : âˆ‘ j : J, biproduct.Ï€ f j â‰« biproduct.Î¹ f j = ğŸ™ (â¨ f) :=
is_bilimit.total (biproduct.is_bilimit _)

lemma biproduct.lift_eq {T : C} {g : Î  j, T âŸ¶ f j} :
  biproduct.lift g = âˆ‘ j, g j â‰« biproduct.Î¹ f j :=
begin
  ext j,
  simp only [sum_comp, biproduct.Î¹_Ï€, comp_dite, biproduct.lift_Ï€, category.assoc, comp_zero,
    finset.sum_dite_eq', finset.mem_univ, eq_to_hom_refl, category.comp_id, if_true],
end

lemma biproduct.desc_eq {T : C} {g : Î  j, f j âŸ¶ T} :
  biproduct.desc g = âˆ‘ j, biproduct.Ï€ f j â‰« g j :=
begin
  ext j,
  simp [comp_sum, biproduct.Î¹_Ï€_assoc, dite_comp],
end

@[simp, reassoc] lemma biproduct.lift_desc {T U : C} {g : Î  j, T âŸ¶ f j} {h : Î  j, f j âŸ¶ U} :
  biproduct.lift g â‰« biproduct.desc h = âˆ‘ j : J, g j â‰« h j :=
by simp [biproduct.lift_eq, biproduct.desc_eq, comp_sum, sum_comp, biproduct.Î¹_Ï€_assoc,
  comp_dite, dite_comp]

lemma biproduct.map_eq [has_finite_biproducts C] {f g : J â†’ C} {h : Î  j, f j âŸ¶ g j} :
  biproduct.map h = âˆ‘ j : J, biproduct.Ï€ f j â‰« h j â‰« biproduct.Î¹ g j :=
begin
  ext,
  simp [biproduct.Î¹_Ï€, biproduct.Î¹_Ï€_assoc, comp_sum, sum_comp, comp_dite, dite_comp],
end

@[simp, reassoc]
lemma biproduct.matrix_desc
  {K : Type} [fintype K] [has_finite_biproducts C]
  {f : J â†’ C} {g : K â†’ C} (m : Î  j k, f j âŸ¶ g k) {P} (x : Î  k, g k âŸ¶ P) :
  biproduct.matrix m â‰« biproduct.desc x = biproduct.desc (Î» j, âˆ‘ k, m j k â‰« x k) :=
by { ext, simp, }

@[simp, reassoc]
lemma biproduct.lift_matrix
  {K : Type} [fintype K] [has_finite_biproducts C]
  {f : J â†’ C} {g : K â†’ C} {P} (x : Î  j, P âŸ¶ f j) (m : Î  j k, f j âŸ¶ g k)  :
  biproduct.lift x â‰« biproduct.matrix m = biproduct.lift (Î» k, âˆ‘ j, x j â‰« m j k) :=
by { ext, simp, }

@[reassoc]
lemma biproduct.matrix_map
  {K : Type} [fintype K] [has_finite_biproducts C]
  {f : J â†’ C} {g : K â†’ C} {h : K â†’ C} (m : Î  j k, f j âŸ¶ g k) (n : Î  k, g k âŸ¶ h k) :
  biproduct.matrix m â‰« biproduct.map n = biproduct.matrix (Î» j k, m j k â‰« n k) :=
by { ext, simp, }

@[reassoc]
lemma biproduct.map_matrix
  {K : Type} [fintype K] [has_finite_biproducts C]
  {f : J â†’ C} {g : J â†’ C} {h : K â†’ C} (m : Î  k, f k âŸ¶ g k) (n : Î  j k, g j âŸ¶ h k) :
  biproduct.map m â‰« biproduct.matrix n = biproduct.matrix (Î» j k, m j â‰« n j k) :=
by { ext, simp, }

end

/-- Reindex a categorical biproduct via an equivalence of the index types. -/
@[simps]
def biproduct.reindex {Î² Î³ : Type} [fintype Î²] [decidable_eq Î²] [decidable_eq Î³]
  (Îµ : Î² â‰ƒ Î³) (f : Î³ â†’ C) [has_biproduct f] [has_biproduct (f âˆ˜ Îµ)] : (â¨ (f âˆ˜ Îµ)) â‰… (â¨ f) :=
{ hom := biproduct.desc (Î» b, biproduct.Î¹ f (Îµ b)),
  inv := biproduct.lift (Î» b, biproduct.Ï€ f (Îµ b)),
  hom_inv_id' := by { ext b b', by_cases h : b = b', { subst h, simp, }, { simp [h], }, },
  inv_hom_id' := begin
    ext g g',
    by_cases h : g = g';
    simp [preadditive.sum_comp, preadditive.comp_sum, biproduct.Î¹_Ï€, biproduct.Î¹_Ï€_assoc, comp_dite,
      equiv.apply_eq_iff_eq_symm_apply, finset.sum_dite_eq' finset.univ (Îµ.symm g') _, h],
  end, }

/--
In a preadditive category, we can construct a binary biproduct for `X Y : C` from
any binary bicone `b` satisfying `total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
def is_binary_bilimit_of_total {X Y : C} (b : binary_bicone X Y)
  (total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X) : b.is_bilimit :=
{ is_limit :=
  { lift := Î» s, binary_fan.fst s â‰« b.inl +
      binary_fan.snd s â‰« b.inr,
    uniq' := Î» s m h, by erw [â†category.comp_id m, â†total,
      comp_add, reassoc_of (h âŸ¨walking_pair.leftâŸ©), reassoc_of (h âŸ¨walking_pair.rightâŸ©)],
    fac' := Î» s j, by rcases j with âŸ¨âŸ¨âŸ©âŸ©; simp, },
  is_colimit :=
  { desc := Î» s, b.fst â‰« binary_cofan.inl s +
      b.snd â‰« binary_cofan.inr s,
    uniq' := Î» s m h, by erw [â†category.id_comp m, â†total,
      add_comp, category.assoc, category.assoc, h âŸ¨walking_pair.leftâŸ©, h âŸ¨walking_pair.rightâŸ©],
    fac' := Î» s j, by rcases j with âŸ¨âŸ¨âŸ©âŸ©; simp, } }

lemma is_bilimit.binary_total {X Y : C} {b : binary_bicone X Y} (i : b.is_bilimit) :
  b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X :=
i.is_limit.hom_ext (Î» j, by { rcases j with âŸ¨âŸ¨âŸ©âŸ©; simp, })

/--
In a preadditive category, we can construct a binary biproduct for `X Y : C` from
any binary bicone `b` satisfying `total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X`.

(That is, such a bicone is a limit cone and a colimit cocone.)
-/
lemma has_binary_biproduct_of_total {X Y : C} (b : binary_bicone X Y)
  (total : b.fst â‰« b.inl + b.snd â‰« b.inr = ğŸ™ b.X) : has_binary_biproduct X Y :=
has_binary_biproduct.mk
{ bicone := b,
  is_bilimit := is_binary_bilimit_of_total b total }

/-- We can turn any limit cone over a pair into a bicone. -/
@[simps]
def binary_bicone.of_limit_cone {X Y : C} {t : cone (pair X Y)} (ht : is_limit t) :
  binary_bicone X Y :=
{ X := t.X,
  fst := t.Ï€.app âŸ¨walking_pair.leftâŸ©,
  snd := t.Ï€.app âŸ¨walking_pair.rightâŸ©,
  inl := ht.lift (binary_fan.mk (ğŸ™ X) 0),
  inr := ht.lift (binary_fan.mk 0 (ğŸ™ Y)) }

lemma inl_of_is_limit {X Y : C} {t : binary_bicone X Y} (ht : is_limit t.to_cone) :
  t.inl = ht.lift (binary_fan.mk (ğŸ™ X) 0) :=
by apply ht.uniq (binary_fan.mk (ğŸ™ X) 0); rintro âŸ¨âŸ¨âŸ©âŸ©; dsimp; simp

lemma inr_of_is_limit {X Y : C} {t : binary_bicone X Y} (ht : is_limit t.to_cone) :
  t.inr = ht.lift (binary_fan.mk 0 (ğŸ™ Y)) :=
by apply ht.uniq (binary_fan.mk 0 (ğŸ™ Y)); rintro âŸ¨âŸ¨âŸ©âŸ©; dsimp; simp

/-- In a preadditive category, any binary bicone which is a limit cone is in fact a bilimit
    bicone. -/
def is_binary_bilimit_of_is_limit {X Y : C} (t : binary_bicone X Y) (ht : is_limit t.to_cone) :
  t.is_bilimit :=
is_binary_bilimit_of_total _ (by refine binary_fan.is_limit.hom_ext ht _ _; simp)

/-- We can turn any limit cone over a pair into a bilimit bicone. -/
def binary_bicone_is_bilimit_of_limit_cone_of_is_limit {X Y : C} {t : cone (pair X Y)}
  (ht : is_limit t) : (binary_bicone.of_limit_cone ht).is_bilimit :=
is_binary_bilimit_of_total _ $ binary_fan.is_limit.hom_ext ht (by simp) (by simp)

/-- In a preadditive category, if the product of `X` and `Y` exists, then the
    binary biproduct of `X` and `Y` exists. -/
lemma has_binary_biproduct.of_has_binary_product (X Y : C) [has_binary_product X Y] :
  has_binary_biproduct X Y :=
has_binary_biproduct.mk
{ bicone := _,
  is_bilimit := binary_bicone_is_bilimit_of_limit_cone_of_is_limit (limit.is_limit _) }

/-- In a preadditive category, if all binary products exist, then all binary biproducts exist. -/
lemma has_binary_biproducts.of_has_binary_products [has_binary_products C] :
  has_binary_biproducts C :=
{ has_binary_biproduct := Î» X Y, has_binary_biproduct.of_has_binary_product X Y, }

/-- We can turn any colimit cocone over a pair into a bicone. -/
@[simps]
def binary_bicone.of_colimit_cocone {X Y : C} {t : cocone (pair X Y)} (ht : is_colimit t) :
  binary_bicone X Y :=
{ X := t.X,
  fst := ht.desc (binary_cofan.mk (ğŸ™ X) 0),
  snd := ht.desc (binary_cofan.mk 0 (ğŸ™ Y)),
  inl := t.Î¹.app âŸ¨walking_pair.leftâŸ©,
  inr := t.Î¹.app âŸ¨walking_pair.rightâŸ© }

lemma fst_of_is_colimit {X Y : C} {t : binary_bicone X Y} (ht : is_colimit t.to_cocone) :
  t.fst = ht.desc (binary_cofan.mk (ğŸ™ X) 0) :=
begin
  apply ht.uniq (binary_cofan.mk (ğŸ™ X) 0),
  rintro âŸ¨âŸ¨âŸ©âŸ©; dsimp; simp
end

lemma snd_of_is_colimit {X Y : C} {t : binary_bicone X Y} (ht : is_colimit t.to_cocone) :
  t.snd = ht.desc (binary_cofan.mk 0 (ğŸ™ Y)) :=
begin
  apply ht.uniq (binary_cofan.mk 0 (ğŸ™ Y)),
  rintro âŸ¨âŸ¨âŸ©âŸ©; dsimp; simp
end

/-- In a preadditive category, any binary bicone which is a colimit cocone is in fact a
    bilimit bicone. -/
def is_binary_bilimit_of_is_colimit {X Y : C} (t : binary_bicone X Y)
  (ht : is_colimit t.to_cocone) : t.is_bilimit :=
is_binary_bilimit_of_total _
begin
  refine binary_cofan.is_colimit.hom_ext ht _ _; simp,
  { rw [category.comp_id t.inl] },
  { rw [category.comp_id t.inr] }
end

/-- We can turn any colimit cocone over a pair into a bilimit bicone. -/
def binary_bicone_is_bilimit_of_colimit_cocone_of_is_colimit {X Y : C} {t : cocone (pair X Y)}
  (ht : is_colimit t) : (binary_bicone.of_colimit_cocone ht).is_bilimit :=
is_binary_bilimit_of_is_colimit (binary_bicone.of_colimit_cocone ht) $
  is_colimit.of_iso_colimit ht $ cocones.ext (iso.refl _) $ Î» j, by { rcases j with âŸ¨âŸ¨âŸ©âŸ©, tidy }

/-- In a preadditive category, if the coproduct of `X` and `Y` exists, then the
    binary biproduct of `X` and `Y` exists. -/
lemma has_binary_biproduct.of_has_binary_coproduct (X Y : C) [has_binary_coproduct X Y] :
  has_binary_biproduct X Y :=
has_binary_biproduct.mk
{ bicone := _,
  is_bilimit := binary_bicone_is_bilimit_of_colimit_cocone_of_is_colimit (colimit.is_colimit _) }

/-- In a preadditive category, if all binary coproducts exist, then all binary biproducts exist. -/
lemma has_binary_biproducts.of_has_binary_coproducts [has_binary_coproducts C] :
  has_binary_biproducts C :=
{ has_binary_biproduct := Î» X Y, has_binary_biproduct.of_has_binary_coproduct X Y, }

section
variables {X Y : C} [has_binary_biproduct X Y]

/--
In any preadditive category, any binary biproduct satsifies
`biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y)`.
-/
@[simp] lemma biprod.total : biprod.fst â‰« biprod.inl + biprod.snd â‰« biprod.inr = ğŸ™ (X âŠ Y) :=
begin
  ext; simp [add_comp],
end

lemma biprod.lift_eq {T : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} :
  biprod.lift f g = f â‰« biprod.inl + g â‰« biprod.inr :=
begin
  ext; simp [add_comp],
end

lemma biprod.desc_eq {T : C} {f : X âŸ¶ T} {g : Y âŸ¶ T} :
  biprod.desc f g = biprod.fst â‰« f + biprod.snd â‰« g :=
begin
  ext; simp [add_comp],
end

@[simp, reassoc] lemma biprod.lift_desc {T U : C} {f : T âŸ¶ X} {g : T âŸ¶ Y} {h : X âŸ¶ U} {i : Y âŸ¶ U} :
  biprod.lift f g â‰« biprod.desc h i = f â‰« h + g â‰« i :=
by simp [biprod.lift_eq, biprod.desc_eq]

lemma biprod.map_eq [has_binary_biproducts C] {W X Y Z : C} {f : W âŸ¶ Y} {g : X âŸ¶ Z} :
  biprod.map f g = biprod.fst â‰« f â‰« biprod.inl + biprod.snd â‰« g â‰« biprod.inr :=
by apply biprod.hom_ext; apply biprod.hom_ext'; simp

/--
Every split mono `f` with a cokernel induces a binary bicone with `f` as its `inl` and
the cokernel map as its `snd`.
We will show in `is_bilimit_binary_bicone_of_split_mono_of_cokernel` that this binary bicone is in
fact already a biproduct. -/
@[simps]
def binary_bicone_of_is_split_mono_of_cokernel {X Y : C} {f : X âŸ¶ Y} [is_split_mono f]
  {c : cokernel_cofork f} (i : is_colimit c) : binary_bicone X c.X :=
{ X := Y,
  fst := retraction f,
  snd := c.Ï€,
  inl := f,
  inr :=
    let c' : cokernel_cofork (ğŸ™ Y - (ğŸ™ Y - retraction f â‰« f)) :=
      cokernel_cofork.of_Ï€ (cofork.Ï€ c) (by simp) in
    let i' : is_colimit c' := is_cokernel_epi_comp i (retraction f) (by simp) in
    let i'' := is_colimit_cofork_of_cokernel_cofork i' in
    (split_epi_of_idempotent_of_is_colimit_cofork C (by simp) i'').section_,
  inl_fst' := by simp,
  inl_snd' := by simp,
  inr_fst' :=
  begin
    dsimp only,
    rw [split_epi_of_idempotent_of_is_colimit_cofork_section_,
      is_colimit_cofork_of_cokernel_cofork_desc, is_cokernel_epi_comp_desc],
    dsimp only [cokernel_cofork_of_cofork_of_Ï€],
    letI := epi_of_is_colimit_cofork i,
    apply zero_of_epi_comp c.Ï€,
    simp only [sub_comp, comp_sub, category.comp_id, category.assoc, is_split_mono.id, sub_self,
      cofork.is_colimit.Ï€_desc_assoc, cokernel_cofork.Ï€_of_Ï€, is_split_mono.id_assoc],
    apply sub_eq_zero_of_eq,
    apply category.id_comp
  end,
  inr_snd' := by apply split_epi.id }

/-- The bicone constructed in `binary_bicone_of_split_mono_of_cokernel` is a bilimit.
This is a version of the splitting lemma that holds in all preadditive categories. -/
def is_bilimit_binary_bicone_of_is_split_mono_of_cokernel {X Y : C} {f : X âŸ¶ Y} [is_split_mono f]
  {c : cokernel_cofork f} (i : is_colimit c) :
  (binary_bicone_of_is_split_mono_of_cokernel i).is_bilimit :=
is_binary_bilimit_of_total _
begin
  simp only [binary_bicone_of_is_split_mono_of_cokernel_fst,
    binary_bicone_of_is_split_mono_of_cokernel_inr, binary_bicone_of_is_split_mono_of_cokernel_snd,
    split_epi_of_idempotent_of_is_colimit_cofork_section_],
  dsimp only [binary_bicone_of_is_split_mono_of_cokernel_X],
  rw [is_colimit_cofork_of_cokernel_cofork_desc, is_cokernel_epi_comp_desc],
  simp only [binary_bicone_of_is_split_mono_of_cokernel_inl, cofork.is_colimit.Ï€_desc,
    cokernel_cofork_of_cofork_Ï€, cofork.Ï€_of_Ï€, add_sub_cancel'_right]
end

/-- If `b` is a binary bicone such that `b.inl` is a kernel of `b.snd`, then `b` is a bilimit
    bicone. -/
def binary_bicone.is_bilimit_of_kernel_inl {X Y : C} (b : binary_bicone X Y)
  (hb : is_limit b.snd_kernel_fork) : b.is_bilimit :=
is_binary_bilimit_of_is_limit _ $ binary_fan.is_limit.mk _
  (Î» T f g, f â‰« b.inl + g â‰« b.inr) (Î» T f g, by simp) (Î» T f g, by simp) $ Î» T f g m hâ‚ hâ‚‚,
  begin
    have hâ‚' : (m - (f â‰« b.inl + g â‰« b.inr)) â‰« b.fst = 0 := by simpa using sub_eq_zero.2 hâ‚,
    have hâ‚‚' : (m - (f â‰« b.inl + g â‰« b.inr)) â‰« b.snd = 0 := by simpa using sub_eq_zero.2 hâ‚‚,
    obtain âŸ¨q : T âŸ¶ X, hq : q â‰« b.inl = m - (f â‰« b.inl + g â‰« b.inr)âŸ© :=
      kernel_fork.is_limit.lift' hb _ hâ‚‚',
    rw [â†sub_eq_zero, â†hq, â†category.comp_id q, â†b.inl_fst, â†category.assoc, hq, hâ‚', zero_comp]
  end

/-- If `b` is a binary bicone such that `b.inr` is a kernel of `b.fst`, then `b` is a bilimit
    bicone. -/
def binary_bicone.is_bilimit_of_kernel_inr {X Y : C} (b : binary_bicone X Y)
  (hb : is_limit b.fst_kernel_fork) : b.is_bilimit :=
is_binary_bilimit_of_is_limit _ $ binary_fan.is_limit.mk _
  (Î» T f g, f â‰« b.inl + g â‰« b.inr) (Î» t f g, by simp) (Î» t f g, by simp) $ Î» T f g m hâ‚ hâ‚‚,
  begin
    have hâ‚' : (m - (f â‰« b.inl + g â‰« b.inr)) â‰« b.fst = 0 := by simpa using sub_eq_zero.2 hâ‚,
    have hâ‚‚' : (m - (f â‰« b.inl + g â‰« b.inr)) â‰« b.snd = 0 := by simpa using sub_eq_zero.2 hâ‚‚,
    obtain âŸ¨q : T âŸ¶ Y, hq : q â‰« b.inr = m - (f â‰« b.inl + g â‰« b.inr)âŸ© :=
      kernel_fork.is_limit.lift' hb _ hâ‚',
    rw [â†sub_eq_zero, â†hq, â†category.comp_id q, â†b.inr_snd, â†category.assoc, hq, hâ‚‚', zero_comp]
  end

/-- If `b` is a binary bicone such that `b.fst` is a cokernel of `b.inr`, then `b` is a bilimit
    bicone. -/
def binary_bicone.is_bilimit_of_cokernel_fst {X Y : C} (b : binary_bicone X Y)
  (hb : is_colimit b.inr_cokernel_cofork) : b.is_bilimit :=
is_binary_bilimit_of_is_colimit _ $ binary_cofan.is_colimit.mk _
  (Î» T f g, b.fst â‰« f + b.snd â‰« g) (Î» T f g, by simp) (Î» T f g, by simp) $ Î» T f g m hâ‚ hâ‚‚,
  begin
    have hâ‚' : b.inl â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚,
    have hâ‚‚' : b.inr â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚‚,
    obtain âŸ¨q : X âŸ¶ T, hq : b.fst â‰« q = m - (b.fst â‰« f + b.snd â‰« g)âŸ© :=
      cokernel_cofork.is_colimit.desc' hb _ hâ‚‚',
    rw [â†sub_eq_zero, â†hq, â†category.id_comp q, â†b.inl_fst, category.assoc, hq, hâ‚', comp_zero]
  end

/-- If `b` is a binary bicone such that `b.snd` is a cokernel of `b.inl`, then `b` is a bilimit
    bicone. -/
def binary_bicone.is_bilimit_of_cokernel_snd {X Y : C} (b : binary_bicone X Y)
  (hb : is_colimit b.inl_cokernel_cofork) : b.is_bilimit :=
is_binary_bilimit_of_is_colimit _ $ binary_cofan.is_colimit.mk _
  (Î» T f g, b.fst â‰« f + b.snd â‰« g) (Î» T f g, by simp) (Î» T f g, by simp) $ Î» T f g m hâ‚ hâ‚‚,
  begin
    have hâ‚' : b.inl â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚,
    have hâ‚‚' : b.inr â‰« (m - (b.fst â‰« f + b.snd â‰« g)) = 0 := by simpa using sub_eq_zero.2 hâ‚‚,
    obtain âŸ¨q : Y âŸ¶ T, hq : b.snd â‰« q = m - (b.fst â‰« f + b.snd â‰« g)âŸ© :=
      cokernel_cofork.is_colimit.desc' hb _ hâ‚',
    rw [â†sub_eq_zero, â†hq, â†category.id_comp q, â†b.inr_snd, category.assoc, hq, hâ‚‚', comp_zero]
  end

/--
Every split epi `f` with a kernel induces a binary bicone with `f` as its `snd` and
the kernel map as its `inl`.
We will show in `binary_bicone_of_is_split_mono_of_cokernel` that this binary bicone is in fact
already a biproduct. -/
@[simps]
def binary_bicone_of_is_split_epi_of_kernel {X Y : C} {f : X âŸ¶ Y} [is_split_epi f]
  {c : kernel_fork f} (i : is_limit c) : binary_bicone c.X Y :=
{ X := X,
  fst :=
    let c' : kernel_fork (ğŸ™ X - (ğŸ™ X - f â‰« section_ f)) :=
      kernel_fork.of_Î¹ (fork.Î¹ c) (by simp) in
    let i' : is_limit c' := is_kernel_comp_mono i (section_ f) (by simp) in
    let i'' := is_limit_fork_of_kernel_fork i' in
    (split_mono_of_idempotent_of_is_limit_fork C (by simp) i'').retraction,
  snd := f,
  inl := c.Î¹,
  inr := section_ f,
  inl_fst' := by apply split_mono.id,
  inl_snd' := by simp,
  inr_fst' :=
  begin
    dsimp only,
    rw [split_mono_of_idempotent_of_is_limit_fork_retraction,
      is_limit_fork_of_kernel_fork_lift, is_kernel_comp_mono_lift],
    dsimp only [kernel_fork_of_fork_Î¹],
    letI := mono_of_is_limit_fork i,
    apply zero_of_comp_mono c.Î¹,
    simp only [comp_sub, category.comp_id, category.assoc, sub_self, fork.is_limit.lift_Î¹,
      fork.Î¹_of_Î¹, is_split_epi.id_assoc]
  end,
  inr_snd' := by simp }

/-- The bicone constructed in `binary_bicone_of_is_split_epi_of_kernel` is a bilimit.
This is a version of the splitting lemma that holds in all preadditive categories. -/
def is_bilimit_binary_bicone_of_is_split_epi_of_kernel {X Y : C} {f : X âŸ¶ Y} [is_split_epi f]
  {c : kernel_fork f} (i : is_limit c) :
  (binary_bicone_of_is_split_epi_of_kernel i).is_bilimit :=
binary_bicone.is_bilimit_of_kernel_inl _ $ i.of_iso_limit $ fork.ext (iso.refl _) (by simp)

end

section
variables {X Y : C} (f g : X âŸ¶ Y)

/-- The existence of binary biproducts implies that there is at most one preadditive structure. -/
lemma biprod.add_eq_lift_id_desc [has_binary_biproduct X X] :
  f + g = biprod.lift (ğŸ™ X) (ğŸ™ X) â‰« biprod.desc f g :=
by simp

/-- The existence of binary biproducts implies that there is at most one preadditive structure. -/
lemma biprod.add_eq_lift_desc_id [has_binary_biproduct Y Y] :
  f + g = biprod.lift f g â‰« biprod.desc (ğŸ™ Y) (ğŸ™ Y) :=
by simp

end

end limits

open category_theory.limits

section
local attribute [ext] preadditive

/-- The existence of binary biproducts implies that there is at most one preadditive structure. -/
instance subsingleton_preadditive_of_has_binary_biproducts {C : Type u} [category.{v} C]
  [has_zero_morphisms C] [has_binary_biproducts C] : subsingleton (preadditive C) :=
subsingleton.intro $ Î» a b,
begin
  ext X Y f g,
  have hâ‚ := @biprod.add_eq_lift_id_desc _ _ a _ _ f g
    (by convert (infer_instance : has_binary_biproduct X X)),
  have hâ‚‚ := @biprod.add_eq_lift_id_desc _ _ b _ _ f g
    (by convert (infer_instance : has_binary_biproduct X X)),
  refine hâ‚.trans (eq.trans _ hâ‚‚.symm),
  congr' 2;
  exact subsingleton.elim _ _
end
end

section
variables  [has_binary_biproducts.{v} C]

variables {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ : C}
variables (fâ‚â‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚â‚‚ : Xâ‚ âŸ¶ Yâ‚‚) (fâ‚‚â‚ : Xâ‚‚ âŸ¶ Yâ‚) (fâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)

/--
The "matrix" morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` with specified components.
-/
def biprod.of_components : Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚ :=
biprod.fst â‰« fâ‚â‚ â‰« biprod.inl +
biprod.fst â‰« fâ‚â‚‚ â‰« biprod.inr +
biprod.snd â‰« fâ‚‚â‚ â‰« biprod.inl +
biprod.snd â‰« fâ‚‚â‚‚ â‰« biprod.inr

@[simp]
lemma biprod.inl_of_components :
  biprod.inl â‰« biprod.of_components fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ =
    fâ‚â‚ â‰« biprod.inl + fâ‚â‚‚ â‰« biprod.inr :=
by simp [biprod.of_components]

@[simp]
lemma biprod.inr_of_components :
  biprod.inr â‰« biprod.of_components fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ =
    fâ‚‚â‚ â‰« biprod.inl + fâ‚‚â‚‚ â‰« biprod.inr :=
by simp [biprod.of_components]

@[simp]
lemma biprod.of_components_fst :
  biprod.of_components fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ â‰« biprod.fst =
    biprod.fst â‰« fâ‚â‚ + biprod.snd â‰« fâ‚‚â‚ :=
by simp [biprod.of_components]

@[simp]
lemma biprod.of_components_snd :
  biprod.of_components fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ â‰« biprod.snd =
    biprod.fst â‰« fâ‚â‚‚ + biprod.snd â‰« fâ‚‚â‚‚ :=
by simp [biprod.of_components]

@[simp]
lemma biprod.of_components_eq (f : Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚) :
  biprod.of_components (biprod.inl â‰« f â‰« biprod.fst) (biprod.inl â‰« f â‰« biprod.snd)
    (biprod.inr â‰« f â‰« biprod.fst) (biprod.inr â‰« f â‰« biprod.snd) = f :=
begin
  ext;
  simp only [category.comp_id, biprod.inr_fst, biprod.inr_snd, biprod.inl_snd, add_zero, zero_add,
    biprod.inl_of_components, biprod.inr_of_components, eq_self_iff_true, category.assoc, comp_zero,
    biprod.inl_fst, preadditive.add_comp],
end

@[simp]
lemma biprod.of_components_comp {Xâ‚ Xâ‚‚ Yâ‚ Yâ‚‚ Zâ‚ Zâ‚‚ : C}
  (fâ‚â‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚â‚‚ : Xâ‚ âŸ¶ Yâ‚‚) (fâ‚‚â‚ : Xâ‚‚ âŸ¶ Yâ‚) (fâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚)
  (gâ‚â‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚â‚‚ : Yâ‚ âŸ¶ Zâ‚‚) (gâ‚‚â‚ : Yâ‚‚ âŸ¶ Zâ‚) (gâ‚‚â‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
  biprod.of_components fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚ â‰« biprod.of_components gâ‚â‚ gâ‚â‚‚ gâ‚‚â‚ gâ‚‚â‚‚ =
    biprod.of_components
      (fâ‚â‚ â‰« gâ‚â‚ + fâ‚â‚‚ â‰« gâ‚‚â‚) (fâ‚â‚ â‰« gâ‚â‚‚ + fâ‚â‚‚ â‰« gâ‚‚â‚‚)
      (fâ‚‚â‚ â‰« gâ‚â‚ + fâ‚‚â‚‚ â‰« gâ‚‚â‚) (fâ‚‚â‚ â‰« gâ‚â‚‚ + fâ‚‚â‚‚ â‰« gâ‚‚â‚‚) :=
begin
  dsimp [biprod.of_components],
  apply biprod.hom_ext; apply biprod.hom_ext';
  simp only [add_comp, comp_add, add_comp_assoc, add_zero, zero_add,
    biprod.inl_fst, biprod.inl_snd, biprod.inr_fst, biprod.inr_snd,
    biprod.inl_fst_assoc, biprod.inl_snd_assoc, biprod.inr_fst_assoc, biprod.inr_snd_assoc,
    comp_zero, zero_comp,
    category.comp_id, category.assoc],
end

/--
The unipotent upper triangular matrix
```
(1 r)
(0 1)
```
as an isomorphism.
-/
@[simps]
def biprod.unipotent_upper {Xâ‚ Xâ‚‚ : C} (r : Xâ‚ âŸ¶ Xâ‚‚) : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚ :=
{ hom := biprod.of_components (ğŸ™ _) r 0 (ğŸ™ _),
  inv := biprod.of_components (ğŸ™ _) (-r) 0 (ğŸ™ _), }

/--
The unipotent lower triangular matrix
```
(1 0)
(r 1)
```
as an isomorphism.
-/
@[simps]
def biprod.unipotent_lower {Xâ‚ Xâ‚‚ : C} (r : Xâ‚‚ âŸ¶ Xâ‚) : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚ :=
{ hom := biprod.of_components (ğŸ™ _) 0 r (ğŸ™ _),
  inv := biprod.of_components (ğŸ™ _) 0 (-r) (ğŸ™ _), }

/--
If `f` is a morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct isomorphisms `L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚` and `R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚`
so that `L.hom â‰« g â‰« R.hom` is diagonal (with `Xâ‚ âŸ¶ Yâ‚` component still `f`),
via Gaussian elimination.

(This is the version of `biprod.gaussian` written in terms of components.)
-/
def biprod.gaussian' [is_iso fâ‚â‚] :
  Î£' (L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚) (R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚) (gâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚),
    L.hom â‰« (biprod.of_components fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚) â‰« R.hom = biprod.map fâ‚â‚ gâ‚‚â‚‚ :=
âŸ¨biprod.unipotent_lower (-(fâ‚‚â‚ â‰« inv fâ‚â‚)),
 biprod.unipotent_upper (-(inv fâ‚â‚ â‰« fâ‚â‚‚)),
 fâ‚‚â‚‚ - fâ‚‚â‚ â‰« (inv fâ‚â‚) â‰« fâ‚â‚‚,
 by ext; simp; abelâŸ©

/--
If `f` is a morphism `Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct isomorphisms `L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚` and `R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚`
so that `L.hom â‰« g â‰« R.hom` is diagonal (with `Xâ‚ âŸ¶ Yâ‚` component still `f`),
via Gaussian elimination.
-/
def biprod.gaussian (f : Xâ‚ âŠ Xâ‚‚ âŸ¶ Yâ‚ âŠ Yâ‚‚) [is_iso (biprod.inl â‰« f â‰« biprod.fst)] :
  Î£' (L : Xâ‚ âŠ Xâ‚‚ â‰… Xâ‚ âŠ Xâ‚‚) (R : Yâ‚ âŠ Yâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚) (gâ‚‚â‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚),
    L.hom â‰« f â‰« R.hom = biprod.map (biprod.inl â‰« f â‰« biprod.fst) gâ‚‚â‚‚ :=
begin
  let := biprod.gaussian'
    (biprod.inl â‰« f â‰« biprod.fst) (biprod.inl â‰« f â‰« biprod.snd)
    (biprod.inr â‰« f â‰« biprod.fst) (biprod.inr â‰« f â‰« biprod.snd),
  simpa [biprod.of_components_eq],
end

/--
If `Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚` via a two-by-two matrix whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct an isomorphism `Xâ‚‚ â‰… Yâ‚‚`, via Gaussian elimination.
-/
def biprod.iso_elim' [is_iso fâ‚â‚] [is_iso (biprod.of_components fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚)] : Xâ‚‚ â‰… Yâ‚‚ :=
begin
  obtain âŸ¨L, R, g, wâŸ© := biprod.gaussian' fâ‚â‚ fâ‚â‚‚ fâ‚‚â‚ fâ‚‚â‚‚,
  letI : is_iso (biprod.map fâ‚â‚ g) := by { rw â†w, apply_instance, },
  letI : is_iso g := (is_iso_right_of_is_iso_biprod_map fâ‚â‚ g),
  exact as_iso g,
end

/--
If `f` is an isomorphism `Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚` whose `Xâ‚ âŸ¶ Yâ‚` entry is an isomorphism,
then we can construct an isomorphism `Xâ‚‚ â‰… Yâ‚‚`, via Gaussian elimination.
-/
def biprod.iso_elim (f : Xâ‚ âŠ Xâ‚‚ â‰… Yâ‚ âŠ Yâ‚‚) [is_iso (biprod.inl â‰« f.hom â‰« biprod.fst)] : Xâ‚‚ â‰… Yâ‚‚ :=
begin
  letI : is_iso (biprod.of_components
       (biprod.inl â‰« f.hom â‰« biprod.fst)
       (biprod.inl â‰« f.hom â‰« biprod.snd)
       (biprod.inr â‰« f.hom â‰« biprod.fst)
       (biprod.inr â‰« f.hom â‰« biprod.snd)) :=
  by { simp only [biprod.of_components_eq], apply_instance, },
  exact biprod.iso_elim'
    (biprod.inl â‰« f.hom â‰« biprod.fst)
    (biprod.inl â‰« f.hom â‰« biprod.snd)
    (biprod.inr â‰« f.hom â‰« biprod.fst)
    (biprod.inr â‰« f.hom â‰« biprod.snd)
end

lemma biprod.column_nonzero_of_iso {W X Y Z : C}
  (f : W âŠ X âŸ¶ Y âŠ Z) [is_iso f] :
  ğŸ™ W = 0 âˆ¨ biprod.inl â‰« f â‰« biprod.fst â‰  0 âˆ¨ biprod.inl â‰« f â‰« biprod.snd â‰  0 :=
begin
  by_contra' h,
  rcases h with âŸ¨nz, aâ‚, aâ‚‚âŸ©,
  set x := biprod.inl â‰« f â‰« inv f â‰« biprod.fst,
  have hâ‚ : x = ğŸ™ W, by simp [x],
  have hâ‚€ : x = 0,
  { dsimp [x],
    rw [â†category.id_comp (inv f), category.assoc, â†biprod.total],
    conv_lhs { slice 2 3, rw [comp_add], },
    simp only [category.assoc],
    rw [comp_add_assoc, add_comp],
    conv_lhs { congr, skip, slice 1 3, rw aâ‚‚, },
    simp only [zero_comp, add_zero],
    conv_lhs { slice 1 3, rw aâ‚, },
    simp only [zero_comp], },
  exact nz (hâ‚.symm.trans hâ‚€),
end

end

lemma biproduct.column_nonzero_of_iso'
  {Ïƒ Ï„ : Type} [finite Ï„]
  {S : Ïƒ â†’ C} [has_biproduct S] {T : Ï„ â†’ C} [has_biproduct T]
  (s : Ïƒ) (f : â¨ S âŸ¶ â¨ T) [is_iso f] :
  (âˆ€ t : Ï„, biproduct.Î¹ S s â‰« f â‰« biproduct.Ï€ T t = 0) â†’ ğŸ™ (S s) = 0 :=
begin
  casesI nonempty_fintype Ï„,
  intro z,
  set x := biproduct.Î¹ S s â‰« f â‰« inv f â‰« biproduct.Ï€ S s,
  have hâ‚ : x = ğŸ™ (S s), by simp [x],
  have hâ‚€ : x = 0,
  { dsimp [x],
    rw [â†category.id_comp (inv f), category.assoc, â†biproduct.total],
    simp only [comp_sum_assoc],
    conv_lhs { congr, apply_congr, skip, simp only [reassoc_of z], },
    simp, },
  exact hâ‚.symm.trans hâ‚€,
end

/--
If `f : â¨ S âŸ¶ â¨ T` is an isomorphism, and `s` is a non-trivial summand of the source,
then there is some `t` in the target so that the `s, t` matrix entry of `f` is nonzero.
-/
def biproduct.column_nonzero_of_iso
  {Ïƒ Ï„ : Type} [fintype Ï„]
  {S : Ïƒ â†’ C} [has_biproduct S] {T : Ï„ â†’ C} [has_biproduct T]
  (s : Ïƒ) (nz : ğŸ™ (S s) â‰  0)
  (f : â¨ S âŸ¶ â¨ T) [is_iso f] :
  trunc (Î£' t : Ï„, biproduct.Î¹ S s â‰« f â‰« biproduct.Ï€ T t â‰  0) :=
begin
  classical,
  apply trunc_sigma_of_exists,
  have t := biproduct.column_nonzero_of_iso'.{v} s f,
  by_contradiction h,
  simp only [not_exists_not] at h,
  exact nz (t h)
end

section preadditive
variables {D : Type.{u'}} [category.{v'} D] [preadditive.{v'} D]
variables (F : C â¥¤ D) [preserves_zero_morphisms F]

namespace limits

section fintype
variables {J : Type} [fintype J]

local attribute [tidy] tactic.discrete_cases

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite products. -/
def preserves_product_of_preserves_biproduct {f : J â†’ C} [preserves_biproduct f F] :
  preserves_limit (discrete.functor f) F :=
{ preserves := Î» c hc, is_limit.of_iso_limit
  ((is_limit.postcompose_inv_equiv (discrete.comp_nat_iso_discrete _ _) _).symm
    (is_bilimit_of_preserves F (bicone_is_bilimit_of_limit_cone_of_is_limit hc)).is_limit) $
  cones.ext (iso.refl _) (by tidy) }

section
local attribute [instance] preserves_product_of_preserves_biproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite products. -/
def preserves_products_of_shape_of_preserves_biproducts_of_shape
  [preserves_biproducts_of_shape J F] : preserves_limits_of_shape (discrete J) F :=
{ preserves_limit := Î» f, preserves_limit_of_iso_diagram _ discrete.nat_iso_functor.symm }

end

/-- A functor between preadditive categories that preserves (zero morphisms and) finite products
    preserves finite biproducts. -/
def preserves_biproduct_of_preserves_product {f : J â†’ C} [preserves_limit (discrete.functor f) F] :
  preserves_biproduct f F :=
{ preserves := Î» b hb, is_bilimit_of_is_limit _ $
    is_limit.of_iso_limit ((is_limit.postcompose_hom_equiv (discrete.comp_nat_iso_discrete _ _)
      (F.map_cone b.to_cone)).symm (is_limit_of_preserves F hb.is_limit)) $
      cones.ext (iso.refl _) (by tidy) }

/-- If the (product-like) biproduct comparison for `F` and `f` is a monomorphism, then `F`
    preserves the biproduct of `f`. For the converse, see `map_biproduct`. -/
def preserves_biproduct_of_mono_biproduct_comparison {f : J â†’ C} [has_biproduct f]
  [has_biproduct (F.obj âˆ˜ f)] [mono (biproduct_comparison F f)] : preserves_biproduct f F :=
begin
  have : pi_comparison F f = (F.map_iso (biproduct.iso_product f)).inv â‰«
    biproduct_comparison F f â‰« (biproduct.iso_product _).hom,
  { ext, convert pi_comparison_comp_Ï€ F f j.as; simp [â† functor.map_comp] },
  haveI : is_iso (biproduct_comparison F f) := is_iso_of_mono_of_is_split_epi _,
  haveI : is_iso (pi_comparison F f) := by { rw this, apply_instance },
  haveI := preserves_product.of_iso_comparison F f,
  apply preserves_biproduct_of_preserves_product
end

/-- If the (coproduct-like) biproduct comparison for `F` and `f` is an epimorphism, then `F`
    preserves the biproduct of `F` and `f`. For the converse, see `map_biproduct`. -/
def preserves_biproduct_of_epi_biproduct_comparison' {f : J â†’ C} [has_biproduct f]
  [has_biproduct (F.obj âˆ˜ f)] [epi (biproduct_comparison' F f)] : preserves_biproduct f F :=
begin
  haveI : epi ((split_epi_biproduct_comparison F f).section_) := by simpa,
  haveI : is_iso (biproduct_comparison F f) := is_iso.of_epi_section'
    (split_epi_biproduct_comparison F f),
  apply preserves_biproduct_of_mono_biproduct_comparison
end

/-- A functor between preadditive categories that preserves (zero morphisms and) finite products
    preserves finite biproducts. -/
def preserves_biproducts_of_shape_of_preserves_products_of_shape
  [preserves_limits_of_shape (discrete J) F] : preserves_biproducts_of_shape J F :=
{ preserves := Î» f, preserves_biproduct_of_preserves_product F }

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite coproducts. -/
def preserves_coproduct_of_preserves_biproduct {f : J â†’ C} [preserves_biproduct f F] :
  preserves_colimit (discrete.functor f) F :=
{ preserves := Î» c hc, is_colimit.of_iso_colimit
    ((is_colimit.precompose_hom_equiv (discrete.comp_nat_iso_discrete _ _) _).symm
      (is_bilimit_of_preserves F
        (bicone_is_bilimit_of_colimit_cocone_of_is_colimit hc)).is_colimit) $
    cocones.ext (iso.refl _) (by tidy) }

section
local attribute [instance] preserves_coproduct_of_preserves_biproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) finite biproducts
    preserves finite coproducts. -/
def preserves_coproducts_of_shape_of_preserves_biproducts_of_shape
  [preserves_biproducts_of_shape J F] : preserves_colimits_of_shape (discrete J) F :=
{ preserves_colimit := Î» f, preserves_colimit_of_iso_diagram _ discrete.nat_iso_functor.symm }

end

/-- A functor between preadditive categories that preserves (zero morphisms and) finite coproducts
    preserves finite biproducts. -/
def preserves_biproduct_of_preserves_coproduct {f : J â†’ C}
  [preserves_colimit (discrete.functor f) F] : preserves_biproduct f F :=
{ preserves := Î» b hb, is_bilimit_of_is_colimit _ $
    is_colimit.of_iso_colimit ((is_colimit.precompose_inv_equiv (discrete.comp_nat_iso_discrete _ _)
      (F.map_cocone b.to_cocone)).symm (is_colimit_of_preserves F hb.is_colimit)) $
      cocones.ext (iso.refl _) (by tidy) }

/-- A functor between preadditive categories that preserves (zero morphisms and) finite coproducts
    preserves finite biproducts. -/
def preserves_biproducts_of_shape_of_preserves_coproducts_of_shape
  [preserves_colimits_of_shape (discrete J) F] : preserves_biproducts_of_shape J F :=
{ preserves := Î» f, preserves_biproduct_of_preserves_coproduct F }

end fintype

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary products. -/
def preserves_binary_product_of_preserves_binary_biproduct {X Y : C}
  [preserves_binary_biproduct X Y F] : preserves_limit (pair X Y) F :=
{ preserves := Î» c hc, is_limit.of_iso_limit
    ((is_limit.postcompose_inv_equiv (by exact diagram_iso_pair _) _).symm
      (is_binary_bilimit_of_preserves F
        (binary_bicone_is_bilimit_of_limit_cone_of_is_limit hc)).is_limit) $
    cones.ext (iso.refl _) (Î» j, by { rcases j with âŸ¨âŸ¨âŸ©âŸ©, tidy }) }

section
local attribute [instance] preserves_binary_product_of_preserves_binary_biproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary products. -/
def preserves_binary_products_of_preserves_binary_biproducts
  [preserves_binary_biproducts F] : preserves_limits_of_shape (discrete walking_pair) F :=
{ preserves_limit := Î» K, preserves_limit_of_iso_diagram _ (diagram_iso_pair _).symm }

end

/-- A functor between preadditive categories that preserves (zero morphisms and) binary products
    preserves binary biproducts. -/
def preserves_binary_biproduct_of_preserves_binary_product {X Y : C}
  [preserves_limit (pair X Y) F] : preserves_binary_biproduct X Y F :=
{ preserves := Î» b hb, is_binary_bilimit_of_is_limit _ $
    is_limit.of_iso_limit ((is_limit.postcompose_hom_equiv (by exact diagram_iso_pair _)
      (F.map_cone b.to_cone)).symm (is_limit_of_preserves F hb.is_limit)) $
        cones.ext (iso.refl _) (Î» j, by { rcases j with âŸ¨âŸ¨âŸ©âŸ©, tidy }) }

/-- If the (product-like) biproduct comparison for `F`, `X` and `Y` is a monomorphism, then
    `F` preserves the biproduct of `X` and `Y`. For the converse, see `map_biprod`. -/
def preserves_binary_biproduct_of_mono_biprod_comparison {X Y : C} [has_binary_biproduct X Y]
  [has_binary_biproduct (F.obj X) (F.obj Y)] [mono (biprod_comparison F X Y)] :
  preserves_binary_biproduct X Y F :=
begin
  have : prod_comparison F X Y = (F.map_iso (biprod.iso_prod X Y)).inv â‰«
    biprod_comparison F X Y â‰« (biprod.iso_prod _ _).hom := by { ext; simp [â† functor.map_comp] },
  haveI : is_iso (biprod_comparison F X Y) := is_iso_of_mono_of_is_split_epi _,
  haveI : is_iso (prod_comparison F X Y) := by { rw this, apply_instance },
  haveI := preserves_limit_pair.of_iso_prod_comparison F X Y,
  apply preserves_binary_biproduct_of_preserves_binary_product
end

/-- If the (coproduct-like) biproduct comparison for `F`, `X` and `Y` is an epimorphism, then
    `F` preserves the biproduct of `X` and `Y`. For the converse, see `map_biprod`. -/
def preserves_binary_biproduct_of_epi_biprod_comparison' {X Y : C} [has_binary_biproduct X Y]
  [has_binary_biproduct (F.obj X) (F.obj Y)] [epi (biprod_comparison' F X Y)] :
  preserves_binary_biproduct X Y F :=
begin
  haveI : epi ((split_epi_biprod_comparison F X Y).section_) := by simpa,
  haveI : is_iso (biprod_comparison F X Y) := is_iso.of_epi_section'
    (split_epi_biprod_comparison F X Y),
  apply preserves_binary_biproduct_of_mono_biprod_comparison
end

/-- A functor between preadditive categories that preserves (zero morphisms and) binary products
    preserves binary biproducts. -/
def preserves_binary_biproducts_of_preserves_binary_products
  [preserves_limits_of_shape (discrete walking_pair) F] : preserves_binary_biproducts F :=
{ preserves := Î» X Y, preserves_binary_biproduct_of_preserves_binary_product F }

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary coproducts. -/
def preserves_binary_coproduct_of_preserves_binary_biproduct {X Y : C}
  [preserves_binary_biproduct X Y F] : preserves_colimit (pair X Y) F :=
{ preserves := Î» c hc, is_colimit.of_iso_colimit
    ((is_colimit.precompose_hom_equiv (by exact diagram_iso_pair _) _).symm
      (is_binary_bilimit_of_preserves F
        (binary_bicone_is_bilimit_of_colimit_cocone_of_is_colimit hc)).is_colimit) $
      cocones.ext (iso.refl _) (Î» j, by { rcases j with âŸ¨âŸ¨âŸ©âŸ©, tidy }) }

section
local attribute [instance] preserves_binary_coproduct_of_preserves_binary_biproduct

/-- A functor between preadditive categories that preserves (zero morphisms and) binary biproducts
    preserves binary coproducts. -/
def preserves_binary_coproducts_of_preserves_binary_biproducts
  [preserves_binary_biproducts F] : preserves_colimits_of_shape (discrete walking_pair) F :=
{ preserves_colimit := Î» K, preserves_colimit_of_iso_diagram _ (diagram_iso_pair _).symm }

end

/-- A functor between preadditive categories that preserves (zero morphisms and) binary coproducts
    preserves binary biproducts. -/
def preserves_binary_biproduct_of_preserves_binary_coproduct {X Y : C}
  [preserves_colimit (pair X Y) F] : preserves_binary_biproduct X Y F :=
{ preserves := Î» b hb, is_binary_bilimit_of_is_colimit _ $
    is_colimit.of_iso_colimit ((is_colimit.precompose_inv_equiv (by exact diagram_iso_pair _)
      (F.map_cocone b.to_cocone)).symm (is_colimit_of_preserves F hb.is_colimit)) $
        cocones.ext (iso.refl _) (Î» j, by { rcases j with âŸ¨âŸ¨âŸ©âŸ©, tidy }) }

/-- A functor between preadditive categories that preserves (zero morphisms and) binary coproducts
    preserves binary biproducts. -/
def preserves_binary_biproducts_of_preserves_binary_coproducts
  [preserves_colimits_of_shape (discrete walking_pair) F] : preserves_binary_biproducts F :=
{ preserves := Î» X Y, preserves_binary_biproduct_of_preserves_binary_coproduct F }

end limits

end preadditive

end category_theory
