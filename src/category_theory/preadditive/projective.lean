/-
Copyright (c) 2020 Markus Himmel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Scott Morrison
-/
import algebra.homology.exact
import category_theory.types
import category_theory.limits.shapes.biproducts
import category_theory.adjunction.limits
import category_theory.preadditive.yoneda.basic
import algebra.category.Group.epi_mono
import algebra.category.Module.epi_mono

/-!
# Projective objects and categories with enough projectives

An object `P` is called projective if every morphism out of `P` factors through every epimorphism.

A category `C` has enough projectives if every object admits an epimorphism from some
projective object.

`projective.over X` picks an arbitrary such projective object,
and `projective.œÄ X : projective.over X ‚ü∂ X` is the corresponding epimorphism.

Given a morphism `f : X ‚ü∂ Y`, `projective.left f` is a projective object over `kernel f`,
and `projective.d f : projective.left f ‚ü∂ X` is the morphism `œÄ (kernel f) ‚â´ kernel.Œπ f`.
-/

noncomputable theory

open category_theory
open category_theory.limits
open opposite

universes v u

namespace category_theory
variables {C : Type u} [category.{v} C]

/--
An object `P` is called projective if every morphism out of `P` factors through every epimorphism.
-/
class projective (P : C) : Prop :=
(factors : ‚àÄ {E X : C} (f : P ‚ü∂ X) (e : E ‚ü∂ X) [epi e], ‚àÉ f', f' ‚â´ e = f)

section

/--
A projective presentation of an object `X` consists of an epimorphism `f : P ‚ü∂ X`
from some projective object `P`.
-/
@[nolint has_nonempty_instance]
structure projective_presentation (X : C) :=
(P : C)
(projective : projective P . tactic.apply_instance)
(f : P ‚ü∂ X)
(epi : epi f . tactic.apply_instance)

attribute [instance] projective_presentation.projective projective_presentation.epi

variables (C)

/-- A category "has enough projectives" if for every object `X` there is a projective object `P` and
    an epimorphism `P ‚Ü† X`. -/
class enough_projectives : Prop :=
(presentation : ‚àÄ (X : C), nonempty (projective_presentation X))

end

namespace projective

/--
An arbitrarily chosen factorisation of a morphism out of a projective object through an epimorphism.
-/
def factor_thru {P X E : C} [projective P] (f : P ‚ü∂ X) (e : E ‚ü∂ X) [epi e] : P ‚ü∂ E :=
(projective.factors f e).some

@[simp] lemma factor_thru_comp {P X E : C} [projective P] (f : P ‚ü∂ X) (e : E ‚ü∂ X) [epi e] :
  factor_thru f e ‚â´ e = f :=
(projective.factors f e).some_spec

section
open_locale zero_object

instance zero_projective [has_zero_object C] [has_zero_morphisms C] : projective (0 : C) :=
{ factors := Œª E X f e epi, by { use 0, ext, }}

end

lemma of_iso {P Q : C} (i : P ‚âÖ Q) (hP : projective P) : projective Q :=
begin
  fsplit,
  introsI E X f e e_epi,
  obtain ‚ü®f', hf'‚ü© := projective.factors (i.hom ‚â´ f) e,
  exact ‚ü®i.inv ‚â´ f', by simp [hf']‚ü©
end

lemma iso_iff {P Q : C} (i : P ‚âÖ Q) : projective P ‚Üî projective Q :=
‚ü®of_iso i, of_iso i.symm‚ü©

/-- The axiom of choice says that every type is a projective object in `Type`. -/
instance (X : Type u) : projective X :=
{ factors := Œª E X' f e epi,
  ‚ü®Œª x, ((epi_iff_surjective _).mp epi (f x)).some,
  by { ext x, exact ((epi_iff_surjective _).mp epi (f x)).some_spec, }‚ü© }

instance Type.enough_projectives : enough_projectives (Type u) :=
{ presentation := Œª X, ‚ü®{ P := X, f := ùüô X, }‚ü©, }

instance {P Q : C} [has_binary_coproduct P Q] [projective P] [projective Q] :
  projective (P ‚®ø Q) :=
{ factors := Œª E X' f e epi, by exactI
  ‚ü®coprod.desc (factor_thru (coprod.inl ‚â´ f) e) (factor_thru (coprod.inr ‚â´ f) e), by tidy‚ü©, }

section
local attribute [tidy] tactic.discrete_cases

instance {Œ≤ : Type v} (g : Œ≤ ‚Üí C) [has_coproduct g] [‚àÄ b, projective (g b)] :
  projective (‚àê g) :=
{ factors := Œª E X' f e epi, by exactI
  ‚ü®sigma.desc (Œª b, factor_thru (sigma.Œπ g b ‚â´ f) e), by tidy‚ü©, }

end

instance {P Q : C} [has_zero_morphisms C] [has_binary_biproduct P Q]
  [projective P] [projective Q] :
  projective (P ‚äû Q) :=
{ factors := Œª E X' f e epi, by exactI
  ‚ü®biprod.desc (factor_thru (biprod.inl ‚â´ f) e) (factor_thru (biprod.inr ‚â´ f) e), by tidy‚ü©, }

instance {Œ≤ : Type v} (g : Œ≤ ‚Üí C) [has_zero_morphisms C] [has_biproduct g]
  [‚àÄ b, projective (g b)] : projective (‚®Å g) :=
{ factors := Œª E X' f e epi, by exactI
  ‚ü®biproduct.desc (Œª b, factor_thru (biproduct.Œπ g b ‚â´ f) e), by tidy‚ü©, }

lemma projective_iff_preserves_epimorphisms_coyoneda_obj (P : C) :
  projective P ‚Üî (coyoneda.obj (op P)).preserves_epimorphisms :=
‚ü®Œª hP, ‚ü®Œª X Y f hf, (epi_iff_surjective _).2 $ Œª g, have projective (unop (op P)), from hP,
  by exactI ‚ü®factor_thru g f, factor_thru_comp _ _‚ü©‚ü©,
 Œª h, ‚ü®Œª E X f e he, by exactI (epi_iff_surjective _).1
  (infer_instance : epi ((coyoneda.obj (op P)).map e)) f‚ü©‚ü©

section preadditive
variables [preadditive C]

lemma projective_iff_preserves_epimorphisms_preadditive_coyoneda_obj (P : C) :
  projective P ‚Üî (preadditive_coyoneda.obj (op P)).preserves_epimorphisms :=
begin
  rw projective_iff_preserves_epimorphisms_coyoneda_obj,
  refine ‚ü®Œª (h : (preadditive_coyoneda.obj (op P) ‚ãô (forget _)).preserves_epimorphisms), _, _‚ü©,
  { exactI functor.preserves_epimorphisms_of_preserves_of_reflects (preadditive_coyoneda.obj (op P))
      (forget _) },
  { introI,
    exact (infer_instance : (preadditive_coyoneda.obj (op P) ‚ãô forget _).preserves_epimorphisms) }
end

lemma projective_iff_preserves_epimorphisms_preadditive_coyoneda_obj' (P : C) :
  projective P ‚Üî (preadditive_coyoneda_obj (op P)).preserves_epimorphisms :=
begin
  rw projective_iff_preserves_epimorphisms_coyoneda_obj,
  refine ‚ü®Œª (h : (preadditive_coyoneda_obj (op P) ‚ãô (forget _)).preserves_epimorphisms), _, _‚ü©,
  { exactI functor.preserves_epimorphisms_of_preserves_of_reflects (preadditive_coyoneda_obj (op P))
      (forget _) },
  { introI,
    exact (infer_instance : (preadditive_coyoneda_obj (op P) ‚ãô forget _).preserves_epimorphisms) }
end

end preadditive

section enough_projectives
variables [enough_projectives C]

/--
`projective.over X` provides an arbitrarily chosen projective object equipped with
an epimorphism `projective.œÄ : projective.over X ‚ü∂ X`.
-/
def over (X : C) : C :=
(enough_projectives.presentation X).some.P

instance projective_over (X : C) : projective (over X) :=
(enough_projectives.presentation X).some.projective

/--
The epimorphism `projective.œÄ : projective.over X ‚ü∂ X`
from the arbitrarily chosen projective object over `X`.
-/
def œÄ (X : C) : over X ‚ü∂ X :=
(enough_projectives.presentation X).some.f

instance œÄ_epi (X : C) : epi (œÄ X) :=
(enough_projectives.presentation X).some.epi

section
variables [has_zero_morphisms C] {X Y : C} (f : X ‚ü∂ Y) [has_kernel f]

/--
When `C` has enough projectives, the object `projective.syzygies f` is
an arbitrarily chosen projective object over `kernel f`.
-/
@[derive projective]
def syzygies : C := over (kernel f)

/--
When `C` has enough projectives,
`projective.d f : projective.syzygies f ‚ü∂ X` is the composition
`œÄ (kernel f) ‚â´ kernel.Œπ f`.

(When `C` is abelian, we have `exact (projective.d f) f`.)
-/
abbreviation d : syzygies f ‚ü∂ X :=
œÄ (kernel f) ‚â´ kernel.Œπ f

end

end enough_projectives

end projective
namespace adjunction

variables {D : Type*} [category D] {F : C ‚•§ D} {G : D ‚•§ C}

lemma map_projective (adj : F ‚ä£ G) [G.preserves_epimorphisms] (P : C) (hP : projective P) :
  projective (F.obj P) :=
‚ü®Œª X Y f g, begin
  introI,
  rcases hP.factors (adj.unit.app P ‚â´ G.map f) (G.map g),
  use F.map w ‚â´ adj.counit.app X,
  rw [category.assoc, ‚Üêadjunction.counit_naturality, ‚Üêcategory.assoc, ‚ÜêF.map_comp, h],
  simp,
end‚ü©

lemma projective_of_map_projective (adj : F ‚ä£ G) [full F] [faithful F] (P : C)
  (hP : projective (F.obj P)) : projective P :=
‚ü®Œª X Y f g, begin
  introI,
  haveI := adj.left_adjoint_preserves_colimits,
  rcases @hP.1 (F.map f) (F.map g),
  use adj.unit.app _ ‚â´ G.map w ‚â´ (inv $ adj.unit.app _),
  refine faithful.map_injective F _,
  simpa
end‚ü©

/-- Given an adjunction `F ‚ä£ G` such that `G` preserves epis, `F` maps a projective presentation of
`X` to a projective presentation of `F(X)`. -/
def map_projective_presentation (adj : F ‚ä£ G) [G.preserves_epimorphisms] (X : C)
  (Y : projective_presentation X) : projective_presentation (F.obj X) :=
{ P := F.obj Y.P,
  projective := adj.map_projective _ Y.projective,
  f := F.map Y.f,
  epi := by haveI := adj.left_adjoint_preserves_colimits; apply_instance }

end adjunction
namespace equivalence

variables {D : Type*} [category D] (F : C ‚âå D)

/-- Given an equivalence of categories `F`, a projective presentation of `F(X)` induces a
projective presentation of `X.` -/
def projective_presentation_of_map_projective_presentation
  (X : C) (Y : projective_presentation (F.functor.obj X)) : projective_presentation X :=
{ P := F.inverse.obj Y.P,
  projective := adjunction.map_projective F.symm.to_adjunction Y.P Y.projective,
  f := F.inverse.map Y.f ‚â´ F.unit_inv.app _,
  epi := epi_comp _ _ }

lemma enough_projectives_iff (F : C ‚âå D) :
  enough_projectives C ‚Üî enough_projectives D :=
begin
  split,
  all_goals { intro H, constructor, intro X, constructor },
  { exact F.symm.projective_presentation_of_map_projective_presentation _
      (nonempty.some (H.presentation (F.inverse.obj X))) },
  { exact F.projective_presentation_of_map_projective_presentation X
      (nonempty.some (H.presentation (F.functor.obj X))) },
end

end equivalence
open projective
section
variables [has_zero_morphisms C] [has_equalizers C] [has_images C]

/--
Given a projective object `P` mapping via `h` into
the middle object `R` of a pair of exact morphisms `f : Q ‚ü∂ R` and `g : R ‚ü∂ S`,
such that `h ‚â´ g = 0`, there is a lift of `h` to `Q`.
-/
def exact.lift {P Q R S : C} [projective P] (h : P ‚ü∂ R) (f : Q ‚ü∂ R) (g : R ‚ü∂ S)
  (hfg : exact f g) (w : h ‚â´ g = 0) : P ‚ü∂ Q :=
factor_thru
  (factor_thru
    (factor_thru_kernel_subobject g h w)
    (image_to_kernel f g hfg.w))
  (factor_thru_image_subobject f)

@[simp] lemma exact.lift_comp {P Q R S : C} [projective P] (h : P ‚ü∂ R) (f : Q ‚ü∂ R) (g : R ‚ü∂ S)
  (hfg : exact f g) (w : h ‚â´ g = 0) : exact.lift h f g hfg w ‚â´ f = h :=
begin
  simp [exact.lift],
  conv_lhs { congr, skip, rw ‚Üê image_subobject_arrow_comp f, },
  rw [‚Üêcategory.assoc, factor_thru_comp, ‚Üêimage_to_kernel_arrow,
    ‚Üêcategory.assoc, category_theory.projective.factor_thru_comp,
    factor_thru_kernel_subobject_comp_arrow],
end

end

end category_theory
