/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Scott Morrison
-/
import category_theory.simple
import category_theory.linear
import category_theory.endomorphism
import field_theory.algebraic_closure

/-!
# Schur's lemma
We first prove the part of Schur's Lemma that holds in any preadditive category with kernels,
that any nonzero morphism between simple objects
is an isomorphism.

Second, we prove Schur's lemma for `ğ•œ`-linear categories with finite dimensional hom spaces,
over an algebraically closed field `ğ•œ`:
the hom space `X âŸ¶ Y` between simple objects `X` and `Y` is at most one dimensional,
and is 1-dimensional iff `X` and `Y` are isomorphic.

## Future work
It might be nice to provide a `division_ring` instance on `End X` when `X` is simple.
This is an easy consequence of the results here,
but may take some care setting up usable instances.
-/

namespace category_theory

open category_theory.limits

universes v u
variables {C : Type u} [category.{v} C]
variables [preadditive C]

/--
The part of **Schur's lemma** that holds in any preadditive category with kernels:
that a nonzero morphism between simple objects is an isomorphism.
-/
lemma is_iso_of_hom_simple [has_kernels C] {X Y : C} [simple X] [simple Y] {f : X âŸ¶ Y} (w : f â‰  0) :
  is_iso f :=
begin
  haveI : mono f := preadditive.mono_of_kernel_zero (kernel_zero_of_nonzero_from_simple w),
  exact is_iso_of_mono_of_nonzero w
end

/--
As a corollary of Schur's lemma for preadditive categories,
any morphism between simple objects is (exclusively) either an isomorphism or zero.
-/
lemma is_iso_iff_nonzero [has_kernels C] {X Y : C} [simple.{v} X] [simple.{v} Y] (f : X âŸ¶ Y) :
  is_iso.{v} f â†” f â‰  0 :=
âŸ¨Î» I,
  begin
    introI h,
    apply id_nonzero X,
    simp only [â†is_iso.hom_inv_id f, h, zero_comp],
  end,
  Î» w, is_iso_of_hom_simple wâŸ©

open finite_dimensional

variables (ğ•œ : Type*) [field ğ•œ]

/--
Part of **Schur's lemma** for `ğ•œ`-linear categories:
the hom space between two non-isomorphic simple objects is 0-dimensional.
-/
lemma finrank_hom_simple_simple_eq_zero_of_not_iso
  [has_kernels C] [linear ğ•œ C] {X Y : C} [simple.{v} X] [simple.{v} Y]
  (h : (X â‰… Y) â†’ false):
  finrank ğ•œ (X âŸ¶ Y) = 0 :=
begin
  haveI := subsingleton_of_forall_eq (0 : X âŸ¶ Y) (Î» f, begin
    have p := not_congr (is_iso_iff_nonzero f),
    simp only [not_not, ne.def] at p,
    refine p.mp (Î» _, by exactI h (as_iso f)),
  end),
  exact finrank_zero_of_subsingleton,
end

variables [is_alg_closed ğ•œ] [linear ğ•œ C]

-- In the proof below we have some difficulty using `I : finite_dimensional ğ•œ (X âŸ¶ X)`
-- where we need a `finite_dimensional ğ•œ (End X)`.
-- These are definitionally equal, but without eta reduction Lean can't see this.
-- To get around this, we use `convert I`,
-- then check the various instances agree field-by-field,
-- using `ext` equipped with the following extra lemmas:
local attribute [ext] add_comm_group module distrib_mul_action mul_action has_scalar

/--
An auxiliary lemma for Schur's lemma.

If `X âŸ¶ X` is finite dimensional, and every nonzero endomorphism is invertible,
then `X âŸ¶ X` is 1-dimensional.
-/
-- We prove this with the explicit `is_iso_iff_nonzero` assumption,
-- rather than just `[simple X]`, as this form is useful for
-- MÃ¼ger's formulation of semisimplicity.
lemma finrank_endomorphism_eq_one
  {X : C} (is_iso_iff_nonzero : âˆ€ f : X âŸ¶ X, is_iso f â†” f â‰  0)
  [I : finite_dimensional ğ•œ (X âŸ¶ X)] :
  finrank ğ•œ (X âŸ¶ X) = 1 :=
begin
  have id_nonzero := (is_iso_iff_nonzero (ğŸ™ X)).mp (by apply_instance),
  apply finrank_eq_one (ğŸ™ X),
  { exact id_nonzero, },
  { intro f,
    haveI : nontrivial (End X) := nontrivial_of_ne _ _ id_nonzero,
    obtain âŸ¨c, nuâŸ© := @exists_spectrum_of_is_alg_closed_of_finite_dimensional ğ•œ _ _ (End X) _ _ _
      (by { convert I, ext; refl, ext; refl, }) (End.of f),
    use c,
    rw [is_unit_iff_is_iso, is_iso_iff_nonzero, ne.def, not_not, sub_eq_zero,
      algebra.algebra_map_eq_smul_one] at nu,
    exact nu.symm, },
end

variables [has_kernels C]

/--
**Schur's lemma** for endomorphisms in `ğ•œ`-linear categories.
-/
lemma finrank_endomorphism_simple_eq_one
  (X : C) [simple.{v} X] [I : finite_dimensional ğ•œ (X âŸ¶ X)] :
  finrank ğ•œ (X âŸ¶ X) = 1 :=
finrank_endomorphism_eq_one ğ•œ is_iso_iff_nonzero

lemma endomorphism_simple_eq_smul_id
  {X : C} [simple.{v} X] [I : finite_dimensional ğ•œ (X âŸ¶ X)] (f : X âŸ¶ X) :
  âˆƒ c : ğ•œ, c â€¢ ğŸ™ X = f :=
(finrank_eq_one_iff_of_nonzero' (ğŸ™ X) (id_nonzero X)).mp (finrank_endomorphism_simple_eq_one ğ•œ X) f

/--
**Schur's lemma** for `ğ•œ`-linear categories:
if hom spaces are finite dimensional, then the hom space between simples is at most 1-dimensional.

See `finrank_hom_simple_simple_eq_one_iff` and `finrank_hom_simple_simple_eq_zero_iff` below
for the refinements when we know whether or not the simples are isomorphic.
-/
-- We don't really need `[âˆ€ X Y : C, finite_dimensional ğ•œ (X âŸ¶ Y)]` here,
-- just at least one of `[finite_dimensional ğ•œ (X âŸ¶ X)]` or `[finite_dimensional ğ•œ (Y âŸ¶ Y)]`.
lemma finrank_hom_simple_simple_le_one
  (X Y : C) [âˆ€ X Y : C, finite_dimensional ğ•œ (X âŸ¶ Y)] [simple.{v} X] [simple.{v} Y] :
  finrank ğ•œ (X âŸ¶ Y) â‰¤ 1 :=
begin
  cases subsingleton_or_nontrivial (X âŸ¶ Y) with h,
  { resetI,
    convert zero_le_one,
    exact finrank_zero_of_subsingleton, },
  { obtain âŸ¨f, nzâŸ© := (nontrivial_iff_exists_ne 0).mp h,
    haveI fi := (is_iso_iff_nonzero f).mpr nz,
    apply finrank_le_one f,
    intro g,
    obtain âŸ¨c, wâŸ© := endomorphism_simple_eq_smul_id ğ•œ (g â‰« inv f),
    exact âŸ¨c, by simpa using w =â‰« fâŸ©, },
end

lemma finrank_hom_simple_simple_eq_one_iff
  (X Y : C) [âˆ€ X Y : C, finite_dimensional ğ•œ (X âŸ¶ Y)] [simple.{v} X] [simple.{v} Y] :
  finrank ğ•œ (X âŸ¶ Y) = 1 â†” nonempty (X â‰… Y) :=
begin
  fsplit,
  { intro h,
    rw finrank_eq_one_iff' at h,
    obtain âŸ¨f, nz, -âŸ© := h,
    rw â†is_iso_iff_nonzero at nz,
    exactI âŸ¨as_iso fâŸ©, },
  { rintro âŸ¨fâŸ©,
    have le_one := finrank_hom_simple_simple_le_one ğ•œ X Y,
    have zero_lt : 0 < finrank ğ•œ (X âŸ¶ Y) :=
      finrank_pos_iff_exists_ne_zero.mpr âŸ¨f.hom, (is_iso_iff_nonzero f.hom).mp infer_instanceâŸ©,
    linarith, }
end

lemma finrank_hom_simple_simple_eq_zero_iff
  (X Y : C) [âˆ€ X Y : C, finite_dimensional ğ•œ (X âŸ¶ Y)] [simple.{v} X] [simple.{v} Y] :
  finrank ğ•œ (X âŸ¶ Y) = 0 â†” Â¬ nonempty (X â‰… Y) :=
begin
  rw â†not_congr (finrank_hom_simple_simple_eq_one_iff ğ•œ X Y),
  refine âŸ¨Î» h, by { rw h, simp, }, Î» h, _âŸ©,
  have := finrank_hom_simple_simple_le_one ğ•œ X Y,
  interval_cases finrank ğ•œ (X âŸ¶ Y) with h',
  { exact h', },
  { exact false.elim (h h'), },
end

end category_theory
