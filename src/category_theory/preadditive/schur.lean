/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Markus Himmel, Scott Morrison
-/
import category_theory.simple
import category_theory.linear
import category_theory.endomorphism
import field_theory.algebraic_closure

/-!
# Schur's lemma
We prove the part of Schur's Lemma that holds in any preadditive category with kernels,
that any nonzero morphism between simple objects
is an isomorphism.

## TODO
If the category is enriched over finite dimensional vector spaces
over an algebraically closed field, then we can further prove that
`dim (X âŸ¶ Y) â‰¤ 1`.

(Probably easiest to prove this for endomorphisms first:
some polynomial `p` in `f : X âŸ¶ X` vanishes by finite dimensionality,
that polynomial factors linearly,
and at least one factor must be non-invertible, hence zero,
so `f` is a scalar multiple of the identity.
Then for any two nonzero `f g : X âŸ¶ Y`,
observe `f â‰« gâ»Â¹` is a multiple of the identity.)
-/

namespace category_theory

open category_theory.limits

universes v u
variables {C : Type u} [category.{v} C]
variables [preadditive C] [has_kernels C]

/--
Schur's Lemma (for a general preadditive category),
that a nonzero morphism between simple objects is an isomorphism.
-/
lemma is_iso_of_hom_simple {X Y : C} [simple X] [simple Y] {f : X âŸ¶ Y} (w : f â‰  0) :
  is_iso f :=
begin
  haveI : mono f := preadditive.mono_of_kernel_zero (kernel_zero_of_nonzero_from_simple w),
  exact is_iso_of_mono_of_nonzero w
end

/--
As a corollary of Schur's lemma,
any morphism between simple objects is (exclusively) either an isomorphism or zero.
-/
lemma is_iso_iff_nonzero {X Y : C} [simple.{v} X] [simple.{v} Y] (f : X âŸ¶ Y) :
  is_iso.{v} f â†” f â‰  0 :=
âŸ¨Î» I,
  begin
    introI h,
    apply id_nonzero X,
    simp only [â†is_iso.hom_inv_id f, h, zero_comp],
  end,
  Î» w, is_iso_of_hom_simple wâŸ©

-- TODO move to `category_theory.endomorphism`
lemma is_iso_iff_is_unit {X : C} (f : End X) : is_iso f â†” is_unit (f : End X) :=
âŸ¨Î» h, by exactI âŸ¨âŸ¨f, inv f, by simp, by simpâŸ©, rflâŸ©,
  Î» h, { out := âŸ¨h.unit.inv,
    âŸ¨by { convert h.unit.inv_val, exact h.unit_spec.symm, },
      by { convert h.unit.val_inv, exact h.unit_spec.symm, }âŸ©âŸ© }âŸ©

instance (X : C) [simple.{v} X] : nontrivial (End X) :=
nontrivial_of_ne 1 0 (id_nonzero X)

open finite_dimensional

variables (ğ•œ : Type*) [field ğ•œ] [is_alg_closed ğ•œ]

-- TODO try out Sebastien's workaround
local attribute [ext] add_comm_group module distrib_mul_action mul_action has_scalar

/--
An auxiliary lemma for Schur's lemma.

If `X âŸ¶ X` is finite dimensional, and every nonzero endomorphism is invertible,
then `X âŸ¶ X` is 1-dimensional.
-/
-- We prove this with the explicit `is_iso_iff_nonzero` assumption,
-- rather than just `[simple X]`, as this form is useful for
-- MÃ¼ger's formulation of semisimplicity.
lemma finrank_endomorphism_eq_one
  [linear ğ•œ C] {X : C} (is_iso_iff_nonzero : âˆ€ f : X âŸ¶ X, is_iso f â†” f â‰  0)
  [I : finite_dimensional ğ•œ (X âŸ¶ X)] :
  finrank ğ•œ (X âŸ¶ X) = 1 :=
begin
  have id_nonzero := (is_iso_iff_nonzero (ğŸ™ X)).mp (by apply_instance),
  apply finrank_eq_one (ğŸ™ X),
  { exact id_nonzero, },
  { intro f,
    haveI : nontrivial (End X) := nontrivial_of_ne _ _ id_nonzero,
    obtain âŸ¨c, nuâŸ© := @exists_spectrum_of_is_alg_closed_of_finite_dimensional ğ•œ _ _ (End X) _ _ _
      (by { convert I, ext; refl, ext; refl, }) (End.of f),
    use c,
    rw [â†is_iso_iff_is_unit, is_iso_iff_nonzero, ne.def, not_not, sub_eq_zero,
      algebra.algebra_map_eq_smul_one] at nu,
    exact nu.symm, },
end

/--
Schur's lemma for endomorphisms in `ğ•œ`-linear categories.
-/
lemma finrank_endomorphism_simple_eq_one
  [linear ğ•œ C] (X : C) [simple.{v} X] [I : finite_dimensional ğ•œ (X âŸ¶ X)] :
  finrank ğ•œ (X âŸ¶ X) = 1 :=
finrank_endomorphism_eq_one ğ•œ is_iso_iff_nonzero

lemma endomorphism_simple_eq_smul_id
  [linear ğ•œ C] {X : C} [simple.{v} X] [I : finite_dimensional ğ•œ (X âŸ¶ X)] (f : X âŸ¶ X) :
  âˆƒ c : ğ•œ, c â€¢ ğŸ™ X = f :=
(finrank_eq_one_iff_of_nonzero' (ğŸ™ X) (id_nonzero X)).mp (finrank_endomorphism_simple_eq_one ğ•œ X) f

/--
Schur's lemma for `ğ•œ`-linear categories.
-/
lemma finrank_hom_simple_simple_le_one
  [linear ğ•œ C] {X Y : C} [finite_dimensional ğ•œ (X âŸ¶ X)] [simple.{v} X] [simple.{v} Y] :
  finrank ğ•œ (X âŸ¶ Y) â‰¤ 1 :=
begin
  cases subsingleton_or_nontrivial (X âŸ¶ Y) with h,
  { resetI,
    convert zero_le_one,
    exact finrank_zero_of_subsingleton, },
  { obtain âŸ¨f, nzâŸ© := (nontrivial_iff_exists_ne 0).mp h,
    haveI fi := (is_iso_iff_nonzero f).mpr nz,
    apply finrank_le_one f,
    intro g,
    obtain âŸ¨c, wâŸ© := endomorphism_simple_eq_smul_id ğ•œ (g â‰« inv f),
    exact âŸ¨c, by simpa using w =â‰« fâŸ©, },
end

end category_theory
