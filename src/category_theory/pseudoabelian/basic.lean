/-
Copyright (c) 2022 JoÃ«l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: JoÃ«l Riou
-/

import category_theory.abelian.basic

/-!
# Idempotent complete categories

In this file, we define the notion of idempotent complete categories
(also known as Karoubian categories).

## Main constructions and definitions

- `is_idempotent_complete C` expresses that `C` is idempotent complete, i.e.
all idempotents in `C` split.
- `is_idempotent_complete_of_abelian` expresses that abelian categories are
idempotent complete.

## References
* [Stacks: Karoubian categories] https://stacks.math.columbia.edu/tag/09SF

-/

open category_theory
open category_theory.category
open category_theory.limits
open category_theory.preadditive

namespace category_theory

variables (C : Type*) [category C]

class is_idempotent_complete : Prop :=
(idempotents_split : âˆ€ (X : C) (p : X âŸ¶ X), p â‰« p = p â†’
  âˆƒ (Y : C) (i : Y âŸ¶ X) (e : X âŸ¶ Y), i â‰« e = ğŸ™ Y âˆ§ p = e â‰« i)

/-- A category is idempotent complete iff for all idempotents endomorphisms,
the equalizer of the identity and this idempotent exists. -/
lemma is_idempotent_complete_iff_has_equalizer_of_id_and_idempotent :
  is_idempotent_complete C â†” âˆ€ (X : C) (p : X âŸ¶ X), p â‰« p = p â†’ has_equalizer (ğŸ™ X) p :=
begin
  split,
  { introI,
    intros X p hp,
    rcases is_idempotent_complete.idempotents_split X p hp with âŸ¨Y, i, e, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©,
    exact âŸ¨nonempty.intro
      { cone := fork.of_Î¹ i
          (show i â‰« ğŸ™ X = i â‰« p, by rw [comp_id, hâ‚‚, â† assoc, hâ‚, id_comp]),
        is_limit := begin
          apply fork.is_limit.mk',
          intro s,
          refine âŸ¨s.Î¹ â‰« e, _âŸ©,
          split,
          { erw [assoc, â† hâ‚‚, â† limits.fork.condition s, comp_id], },
          { intros m hm,
            erw [â† hm],
            simp only [â† hm, assoc, fork.Î¹_eq_app_zero,
              fork.of_Î¹_Ï€_app, hâ‚],
            erw comp_id m, }
        end
      }âŸ©, },
  { intro h,
    refine âŸ¨_âŸ©,
    intros X p hp,
    haveI := h X p hp,
    use equalizer (ğŸ™ X) p,
    use equalizer.Î¹ (ğŸ™ X) p,
    use equalizer.lift p (show p â‰« ğŸ™ X = p â‰« p, by rw [hp, comp_id]),
    split,
    { ext,
      rw [assoc, equalizer.lift_Î¹, id_comp],
      conv { to_rhs, erw [â† comp_id (equalizer.Î¹ (ğŸ™ X) p)], },
      exact (limits.fork.condition (equalizer.fork (ğŸ™ X) p)).symm, },
    { rw [equalizer.lift_Î¹], }, }
end

variables {C}

/-- In a preadditive category, when `p : X âŸ¶ X` is idempotent,
then `ğŸ™ X - p` is also idempotent. -/
lemma idempotence_of_id_sub_idempotent [preadditive C]
  {X : C} (p : X âŸ¶ X) (hp : p â‰« p = p) :
  (ğŸ™ _ - p) â‰« (ğŸ™ _ - p) = (ğŸ™ _ - p) :=
by simp only [comp_sub, sub_comp, id_comp, comp_id, hp, sub_self, sub_zero]

/-- for preadditive/default.lean -/
lemma has_kernel_of_has_equalizer [preadditive C] {X Y : C} (f g : X âŸ¶ Y)
  [has_equalizer f g] : has_kernel (f - g) :=
has_limit.mk
  { cone := fork.of_Î¹ (equalizer.Î¹ f g)
      (by erw [comp_zero, comp_sub, equalizer.condition f g, sub_self]),
  is_limit := fork.is_limit.mk _
    (Î» s, equalizer.lift s.Î¹ (by simpa only [comp_sub, comp_zero, sub_eq_zero] using s.condition))
    (Î» s, by simp only [fork.Î¹_eq_app_zero, fork.of_Î¹_Ï€_app, equalizer.lift_Î¹])
    (Î» s m h, by { ext, simpa only [equalizer.lift_Î¹] using h walking_parallel_pair.zero, }), }

variables (C)

/-- A preadditive category is pseudoabelian iff all idempotent endomorphisms have a kernel. -/
lemma is_idempotent_complete_iff_idempotents_have_kernels [preadditive C] :
  is_idempotent_complete C â†” âˆ€ (X : C) (p : X âŸ¶ X), p â‰« p = p â†’ has_kernel p :=
begin
  rw is_idempotent_complete_iff_has_equalizer_of_id_and_idempotent,
  split,
  { intros h X p hp,
    haveI := h X (ğŸ™ _ - p) (idempotence_of_id_sub_idempotent p hp),
    convert has_kernel_of_has_equalizer (ğŸ™ X) (ğŸ™ X - p),
    rw [sub_sub_cancel], },
  { intros h X p hp,
    haveI : has_kernel (ğŸ™ _ - p) := h X (ğŸ™ _ - p) (idempotence_of_id_sub_idempotent p hp),
    apply preadditive.has_limit_parallel_pair, },
end

/-- An abelian category is idempotent complete. -/
@[priority 100]
instance is_idempotent_complete_of_abelian (D : Type*) [category D] [abelian D] :
  is_idempotent_complete D :=
begin
  rw is_idempotent_complete_iff_idempotents_have_kernels,
  intros X p hp,
  apply_instance,
end

end category_theory
