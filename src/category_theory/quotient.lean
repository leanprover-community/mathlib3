/-
Copyright (c) 2020 David WÃ¤rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David WÃ¤rn
-/
import category_theory.natural_isomorphism
import category_theory.equivalence
import category_theory.eq_to_hom

/-!
# Quotient category

Constructs the quotient of a category by an arbitrary family of relations on its hom-sets,
by introducing a type synonym for the objects, and identifying homs as necessary.

This is analogous to 'the quotient of a group by the normal closure of a subset', rather
than 'the quotient of a group by a normal subgroup'. When taking the quotient by a congruence
relation, `functor_map_eq_iff` says that no unnecessary identifications have been made.
-/

/-- A `hom_rel` on `C` consists of a relation on every hom-set. -/
@[derive inhabited]
def hom_rel (C) [quiver C] := Î  â¦ƒX Y : Câ¦„, (X âŸ¶ Y) â†’ (X âŸ¶ Y) â†’ Prop

namespace category_theory

variables {C : Type*} [category C] (r : hom_rel C)

include r

/-- A `hom_rel` is a congruence when it's an equivalence on every hom-set, and it can be composed
from left and right. -/
class congruence : Prop :=
(is_equiv : âˆ€ {X Y}, is_equiv _ (@r X Y))
(comp_left : âˆ€ {X Y Z} (f : X âŸ¶ Y) {g g' : Y âŸ¶ Z}, r g g' â†’ r (f â‰« g) (f â‰« g'))
(comp_right : âˆ€ {X Y Z} {f f' : X âŸ¶ Y} (g : Y âŸ¶ Z), r f f' â†’ r (f â‰« g) (f' â‰« g))

attribute [instance] congruence.is_equiv

/-- A type synonym for `C`, thought of as the objects of the quotient category. -/
@[ext]
structure quotient := (as : C)

instance [inhabited C] : inhabited (quotient r) := âŸ¨ { as := default } âŸ©

namespace quotient

/-- Generates the closure of a family of relations w.r.t. composition from left and right. -/
inductive comp_closure â¦ƒs t : Câ¦„ : (s âŸ¶ t) â†’ (s âŸ¶ t) â†’ Prop
| intro {a b} (f : s âŸ¶ a) (mâ‚ mâ‚‚ : a âŸ¶ b) (g : b âŸ¶ t) (h : r mâ‚ mâ‚‚) :
  comp_closure (f â‰« mâ‚ â‰« g) (f â‰« mâ‚‚ â‰« g)

lemma comp_closure.of {a b} (mâ‚ mâ‚‚ : a âŸ¶ b) (h : r mâ‚ mâ‚‚) : comp_closure r mâ‚ mâ‚‚ :=
by simpa using comp_closure.intro (ğŸ™ _) mâ‚ mâ‚‚ (ğŸ™ _) h

lemma comp_left {a b c : C} (f : a âŸ¶ b) : Î  (gâ‚ gâ‚‚ : b âŸ¶ c) (h : comp_closure r gâ‚ gâ‚‚),
  comp_closure r (f â‰« gâ‚) (f â‰« gâ‚‚)
| _ _ âŸ¨x, mâ‚, mâ‚‚, y, hâŸ© := by simpa using comp_closure.intro (f â‰« x) mâ‚ mâ‚‚ y h

lemma comp_right {a b c : C} (g : b âŸ¶ c) : Î  (fâ‚ fâ‚‚ : a âŸ¶ b) (h : comp_closure r fâ‚ fâ‚‚),
  comp_closure r (fâ‚ â‰« g) (fâ‚‚ â‰« g)
| _ _ âŸ¨x, mâ‚, mâ‚‚, y, hâŸ© := by simpa using comp_closure.intro x mâ‚ mâ‚‚ (y â‰« g) h

/-- Hom-sets of the quotient category. -/
def hom (s t : quotient r) := quot $ @comp_closure C _ r s.as t.as

instance (a : quotient r) : inhabited (hom r a a) := âŸ¨quot.mk _ (ğŸ™ a.as)âŸ©

/-- Composition in the quotient category. -/
def comp â¦ƒa b c : quotient râ¦„ : hom r a b â†’ hom r b c â†’ hom r a c :=
Î» hf hg, quot.lift_on hf ( Î» f, quot.lift_on hg (Î» g, quot.mk _ (f â‰« g))
  (Î» gâ‚ gâ‚‚ h, quot.sound $ comp_left r f gâ‚ gâ‚‚ h) )
  (Î» fâ‚ fâ‚‚ h, quot.induction_on hg $ Î» g, quot.sound $ comp_right r g fâ‚ fâ‚‚ h)

@[simp]
lemma comp_mk {a b c : quotient r} (f : a.as âŸ¶ b.as) (g : b.as âŸ¶ c.as) :
  comp r (quot.mk _ f) (quot.mk _ g) = quot.mk _ (f â‰« g) := rfl

instance category : category (quotient r) :=
{ hom := hom r,
  id := Î» a, quot.mk _ (ğŸ™ a.as),
  comp := comp r }

/-- The functor from a category to its quotient. -/
@[simps]
def functor : C â¥¤ quotient r :=
{ obj := Î» a, { as := a },
  map := Î» _ _ f, quot.mk _ f }

noncomputable instance : full (functor r) :=
{ preimage := Î» X Y f, quot.out f, }

instance : ess_surj (functor r) :=
{ mem_ess_image := Î» Y, âŸ¨Y.as, âŸ¨eq_to_iso (by { ext, refl, })âŸ©âŸ© }

protected lemma induction {P : Î  {a b : quotient r}, (a âŸ¶ b) â†’ Prop}
  (h : âˆ€ {x y : C} (f : x âŸ¶ y), P ((functor r).map f)) :
  âˆ€ {a b : quotient r} (f : a âŸ¶ b), P f :=
by { rintros âŸ¨xâŸ© âŸ¨yâŸ© âŸ¨fâŸ©, exact h f, }

protected lemma sound {a b : C} {fâ‚ fâ‚‚ : a âŸ¶ b} (h : r fâ‚ fâ‚‚) :
  (functor r).map fâ‚ = (functor r).map fâ‚‚ :=
by simpa using quot.sound (comp_closure.intro (ğŸ™ a) fâ‚ fâ‚‚ (ğŸ™ b) h)

lemma functor_map_eq_iff [congruence r] {X Y : C} (f f' : X âŸ¶ Y) :
  (functor r).map f = (functor r).map f' â†” r f f' :=
begin
  split,
  { erw quot.eq,
    intro h,
    induction h with m m' hm,
    { cases hm, apply congruence.comp_left, apply congruence.comp_right, assumption, },
    { apply refl },
    { apply symm, assumption },
    { apply trans; assumption }, },
  { apply quotient.sound },
end

variables {D : Type*} [category D]
  (F : C â¥¤ D)
  (H : âˆ€ (x y : C) (fâ‚ fâ‚‚ : x âŸ¶ y), r fâ‚ fâ‚‚ â†’ F.map fâ‚ = F.map fâ‚‚)
include H

/-- The induced functor on the quotient category. -/
@[simps]
def lift : quotient r â¥¤ D :=
{ obj := Î» a, F.obj a.as,
  map := Î» a b hf, quot.lift_on hf (Î» f, F.map f)
    (by { rintro> _ _ âŸ¨_, _, _, _, hâŸ©, simp [H _ _ _ _ h], }),
  map_id' := Î» a, F.map_id a.as,
  map_comp' := by { rintros a b c âŸ¨fâŸ© âŸ¨gâŸ©, exact F.map_comp f g, } }

/-- The original functor factors through the induced functor. -/
def lift.is_lift : (functor r) â‹™ lift r F H â‰… F :=
nat_iso.of_components (Î» X, iso.refl _) (by tidy)

@[simp]
lemma lift.is_lift_hom (X : C) : (lift.is_lift r F H).hom.app X = ğŸ™ (F.obj X) :=
rfl
@[simp]
lemma lift.is_lift_inv (X : C) : (lift.is_lift r F H).inv.app X = ğŸ™ (F.obj X) :=
rfl

lemma lift_map_functor_map {X Y : C} (f : X âŸ¶ Y) :
  (lift r F H).map ((functor r).map f) = F.map f :=
by { rw â†(nat_iso.naturality_1 (lift.is_lift r F H)), dsimp, simp, }

end quotient

end category_theory
