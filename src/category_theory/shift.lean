/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import category_theory.limits.shapes.zero

/-!
# Shift

A `shift` on a category is nothing more than an automorphism of the category. An example to
keep in mind might be the category of complexes â‹¯ â†’ C_{n-1} â†’ C_n â†’ C_{n+1} â†’ â‹¯ with the shift
operator re-indexing the terms, so the degree `n` term of `shift C` would be the degree `n+1`
term of `C`.

-/
namespace category_theory

universes v u

variables (C : Type u) (A : Type*) [category.{v} C]

section defs

variables [add_monoid A]

/-- A category has a shift, or translation, if it is equipped with an automorphism. -/
class has_shift (C : Type u) (A : Type*) [category.{v} C] [add_monoid A] :=
(shift : Î  (i : A), C â¥¤ C)
(shift_add : Î  i j, shift (i + j) â‰… shift i â‹™ shift j)
(iso_whisker_right_shift_add : âˆ€ i j k, iso_whisker_right (shift_add i j) (shift k) =
  (shift_add (i+j) k).symm â‰ªâ‰« (eq_to_iso $ by rw add_assoc) â‰ªâ‰« (shift_add i (j+k)) â‰ªâ‰«
    iso_whisker_left _ (shift_add j k) â‰ªâ‰« (functor.associator _ _ _).symm)

variables [has_shift C A] {A}

/-- The shift autoequivalence, moving objects and morphisms 'up'. -/
def shift_functor (i : A) : C â¥¤ C := has_shift.shift i

/-- Shifting by `i + j` is the same as shifting by `i` and then shifting by `j`. -/
def shift_functor_add (i j : A) :
  shift_functor C (i + j) â‰… shift_functor C i â‹™ shift_functor C j :=
has_shift.shift_add i j

lemma iso_whisker_right_shift_functor_add (i j k : A) :
  iso_whisker_right (shift_functor_add C i j) (shift_functor C k) =
  (shift_functor_add C (i+j) k).symm â‰ªâ‰« (eq_to_iso $ by rw add_assoc) â‰ªâ‰«
    (shift_functor_add C i (j+k)) â‰ªâ‰«
    iso_whisker_left _ (shift_functor_add C j k) â‰ªâ‰« (functor.associator _ _ _).symm :=
has_shift.iso_whisker_right_shift_add i j k

variables (A)

/-- Shifting by zero is the identity functor. -/
def shift_functor_zero [is_equivalence (shift_functor C (0:A))] :
  shift_functor C (0 : A) â‰… ğŸ­ C :=
(functor.right_unitor _).symm â‰ªâ‰«
  iso_whisker_left (shift_functor _ _) is_equivalence.unit_iso â‰ªâ‰«
  (iso_whisker_right (eq_to_iso (by rw add_zero) â‰ªâ‰« (shift_functor_add C (0:A) 0))
    (shift_functor C (0 : A)).inv â‰ªâ‰«
  (functor.associator _ _ _)).symm â‰ªâ‰«
  is_equivalence.unit_iso.symm

end defs

-- Any better notational suggestions?
notation X`âŸ¦`n`âŸ§`:20 := (shift_functor _ n).obj X
notation f`âŸ¦`n`âŸ§'`:80 := (shift_functor _ n).map f

section examples
variables [has_shift C â„¤]

example {X Y : C} (f : X âŸ¶ Y) : XâŸ¦(1 : â„¤)âŸ§ âŸ¶ YâŸ¦1âŸ§ := fâŸ¦1âŸ§'
example {X Y : C} (f : X âŸ¶ Y) : XâŸ¦(-2 : â„¤)âŸ§ âŸ¶ YâŸ¦-2âŸ§ := fâŸ¦-2âŸ§'

end examples

section add_monoid

variables {C A} [add_monoid A] [has_shift C A] (X Y : C) (f : X âŸ¶ Y)

/-- Shifting by `i + j` is the same as shifting by `i` and then shifting by `j`. -/
def shift_add (i j : A) : XâŸ¦i + jâŸ§ â‰… XâŸ¦iâŸ§âŸ¦jâŸ§ := (shift_functor_add C i j).app _

/-- Shifting by `i` and then by `j` is the same as shifting by `i + j`. -/
abbreviation shift_shift (i j : A) : XâŸ¦iâŸ§âŸ¦jâŸ§ â‰… XâŸ¦i + jâŸ§ := (shift_add X i j).symm

@[simp] lemma shift_add_symm (i j : A) : (shift_add X i j).symm = shift_shift X i j := rfl

@[simp] lemma shift_shift_symm (i j : A) : (shift_shift X i j).symm = shift_add X i j :=
(shift_add X i j).symm_symm_eq

lemma shift_add' (i j : A) :
  fâŸ¦i + jâŸ§' = (shift_add X i j).hom â‰« fâŸ¦iâŸ§'âŸ¦jâŸ§' â‰« (shift_add Y i j).inv :=
by { symmetry, apply nat_iso.naturality_2 }

@[reassoc] lemma shift_add_hom_comp (i j : A) :
  (shift_add X i j).hom â‰« fâŸ¦iâŸ§'âŸ¦jâŸ§' = fâŸ¦i + jâŸ§' â‰« (shift_add Y i j).hom :=
by rw [shift_add', category.assoc, category.assoc, iso.inv_hom_id, category.comp_id]

lemma shift_shift' (i j : A) :
  fâŸ¦iâŸ§'âŸ¦jâŸ§' = (shift_shift X i j).hom â‰« fâŸ¦i + jâŸ§' â‰« (shift_shift Y i j).inv :=
by { symmetry, apply nat_iso.naturality_1 }

lemma shift_shift_add_hom' (i j k : A) :
  (shift_add X i j).homâŸ¦kâŸ§' =
    (shift_add X (i+j) k).inv â‰« (eq_to_hom $ by rw add_assoc) â‰« (shift_add X i (j+k)).hom â‰«
      (shift_add (XâŸ¦iâŸ§) j k).hom :=
begin
  have := congr_arg iso.hom (iso_whisker_right_shift_functor_add C i j k),
  apply_fun (Î» Î±, nat_trans.app Î± X) at this,
  simpa only [iso_whisker_left_hom, iso_whisker_right_hom, iso.symm_hom, functor.associator_inv_app,
    eq_to_hom_app, whisker_right_app, whisker_left_app, eq_to_iso.hom, category.comp_id,
    iso.trans_hom, nat_trans.comp_app] using this,
end

lemma shift_functor_map_iso_shift_add (i j k : A) :
  (shift_functor C k).map_iso (shift_add X i j) =
    (shift_add X (i+j) k).symm â‰ªâ‰« (eq_to_iso $ by rw add_assoc) â‰ªâ‰« (shift_add X i (j+k)) â‰ªâ‰«
      (shift_add (XâŸ¦iâŸ§) j k) :=
by { ext, apply shift_shift_add_hom', }

lemma shift_add_assoc (i j k : A) :
  shift_add X (i + j) k =
    eq_to_iso (by rw add_assoc) â‰ªâ‰« shift_add X i (j + k) â‰ªâ‰«
    shift_add _ j k â‰ªâ‰« (functor.map_iso _ (shift_add X i j)).symm :=
begin
  ext,
  simp only [iso.symm_hom, eq_to_iso.hom, iso.trans_hom, â† category.assoc],
  rw [iso.eq_comp_inv, â† iso.eq_inv_comp, functor.map_iso_hom, shift_shift_add_hom',
    category.assoc],
end

/-- Shifting by zero is the identity functor. -/
def shift_zero [is_equivalence (shift_functor C (0:A))] :
  XâŸ¦0âŸ§ â‰… X := (shift_functor_zero C A).app _

lemma shift_zero' [is_equivalence (shift_functor C (0:A))] :
  fâŸ¦(0 : A)âŸ§' = (shift_zero X).hom â‰« f â‰« (shift_zero Y).inv :=
by { symmetry, apply nat_iso.naturality_2 }

end add_monoid

section add_group

variables {A} [add_group A] [has_shift C A] [is_equivalence (shift_functor C (0 : A))]
variables (X Y : C) (f : X âŸ¶ Y)

/-- Shifting by `i` and then shifting by `-i` is the identity. -/
def shift_functor_comp_shift_functor_neg (i : A) :
  shift_functor C i â‹™ shift_functor C (-i) â‰… ğŸ­ C :=
(shift_functor_add C i (-i)).symm â‰ªâ‰« (eq_to_iso $ by simp) â‰ªâ‰« (shift_functor_zero C A)

/-- Shifting by `-i` and then shifting by `i` is the identity. -/
def shift_functor_neg_comp_shift_functor (i : A) :
  shift_functor C (-i) â‹™ shift_functor C i â‰… ğŸ­ C :=
(shift_functor_add C (-i) i).symm â‰ªâ‰« (eq_to_iso $ by simp) â‰ªâ‰« (shift_functor_zero C A)

section

variables (C)

/-- Shifting by `n` is a faithful functor. -/
lemma shift_functor_faithful (i : A) : faithful (shift_functor C i) :=
faithful.of_comp_iso (shift_functor_comp_shift_functor_neg C i)

local attribute [instance] shift_functor_faithful

/-- Shifting by `n` is a full functor. -/
def shift_functor_full (i : A) : full (shift_functor C i) :=
begin
  haveI : full (shift_functor C i â‹™ shift_functor C (-i)) :=
    full.of_iso (shift_functor_comp_shift_functor_neg C i).symm,
  exact full.of_comp_faithful _ (shift_functor C (-i))
end

/-- Shifting by `n` is an essentially surjective functor. -/
lemma shift_functor_ess_surj (i : A) : ess_surj (shift_functor C i) :=
{ mem_ess_image := Î» Y, âŸ¨YâŸ¦-iâŸ§, âŸ¨(shift_functor_neg_comp_shift_functor C i).app YâŸ©âŸ© }

local attribute [instance] shift_functor_full shift_functor_ess_surj

/-- Shifting by `n` is an equivalence. -/
noncomputable def shift_functor_is_equivalence (n : A) : is_equivalence (shift_functor C n) :=
equivalence.of_fully_faithfully_ess_surj _

end

-- Unfortunately it is dangerous to make this a global instance,
-- because it creates a loop for `shift_functor C 0`.
local attribute [instance] shift_functor_is_equivalence

variables {C}

/-- Shifting by `i` and then shifting by `-i` is the identity. -/
def shift_shift_neg (i : A) : XâŸ¦iâŸ§âŸ¦-iâŸ§ â‰… X := (shift_functor_comp_shift_functor_neg C i).app _

/-- Shifting by `-i` and then shifting by `i` is the identity. -/
def shift_neg_shift (i : A) : XâŸ¦-iâŸ§âŸ¦iâŸ§ â‰… X := (shift_functor_neg_comp_shift_functor C i).app _

variables {X Y}

lemma shift_shift_neg' (i : A) :
  fâŸ¦iâŸ§'âŸ¦-iâŸ§' = (shift_shift_neg X i).hom â‰« f â‰« (shift_shift_neg Y i).inv :=
by { symmetry, apply nat_iso.naturality_2 }

lemma shift_neg_shift' (i : A) :
  fâŸ¦-iâŸ§'âŸ¦iâŸ§' = (shift_neg_shift X i).hom â‰« f â‰« (shift_neg_shift Y i).inv :=
by { symmetry, apply nat_iso.naturality_2 }

variables (C)

open category_theory.limits
variables [has_zero_morphisms C]

@[simp]
lemma shift_zero_eq_zero (X Y : C) (n : A) : (0 : X âŸ¶ Y)âŸ¦nâŸ§' = (0 : XâŸ¦nâŸ§ âŸ¶ YâŸ¦nâŸ§) :=
by apply is_equivalence_preserves_zero_morphisms _ (shift_functor C n)

end add_group

section add_comm_monoid

variables {C A} [add_comm_monoid A] [has_shift C A]
variables (X Y : C) (f : X âŸ¶ Y)

/-- When shifts are indexed by an additive commutative monoid, then shifts commute. -/
def shift_comm (i j : A) : XâŸ¦iâŸ§âŸ¦jâŸ§ â‰… XâŸ¦jâŸ§âŸ¦iâŸ§ :=
shift_shift X i j â‰ªâ‰« eq_to_iso (by rw add_comm) â‰ªâ‰« shift_add X j i

@[simp] lemma shift_comm_symm (i j : A) : (shift_comm X i j).symm = shift_comm X j i :=
begin
  dsimp [shift_comm], rw [shift_shift_symm],
  ext, simp only [eq_to_iso.inv, iso.symm_hom, iso.trans_assoc, eq_to_iso.hom, iso.trans_hom],
end

variables {X Y}

/-- When shifts are indexed by an additive commutative monoid, then shifts commute. -/
lemma shift_comm' (i j : A) :
  fâŸ¦iâŸ§'âŸ¦jâŸ§' = (shift_comm _ _ _).hom â‰« fâŸ¦jâŸ§'âŸ¦iâŸ§' â‰« (shift_comm _ _ _).hom :=
begin
  rw [shift_shift', â† category.assoc, iso.comp_inv_eq, shift_comm, shift_comm],
  dsimp,
  simp only [category.assoc, iso.hom_inv_id, category.comp_id, cancel_epi],
  erw reassoc_of (nat_iso.naturality_2 (shift_functor_add C j i) f),
  generalize_proofs h1 h2, revert h1 h2,
  generalize hij : i + j = ij, generalize hji : j + i = ji,
  intros h1 h2,
  have H : ij = ji, { rw [â† hij, add_comm, hji] },
  clear hij hji,
  cases H,
  rw [eq_to_hom_refl, eq_to_hom_refl, category.comp_id, category.id_comp],
end

@[reassoc] lemma shift_comm_hom_comp (i j : A) :
  (shift_comm X i j).hom â‰« fâŸ¦jâŸ§'âŸ¦iâŸ§' = fâŸ¦iâŸ§'âŸ¦jâŸ§' â‰« (shift_comm Y i j).hom :=
by rw [shift_comm', â† shift_comm_symm, iso.symm_hom, iso.inv_hom_id_assoc]

end add_comm_monoid

end category_theory
