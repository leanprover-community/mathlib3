/-
Copyright (c) 2020 Bhavik Mehta, E. W. Ayers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, E. W. Ayers
-/

import category_theory.sites.sieves
import category_theory.full_subcategory
import category_theory.types
import category_theory.limits.shapes.pullbacks

/-!
# Grothendieck topologies

Definition and lemmas about Grothendieck topologies.
A Grothendieck topology for a category `C` is a set of sieves on each object `X` satisfying
certain closure conditions.

Alternate versions of the axioms (in arrow form) are also described.
Two explicit examples of Grothendieck topologies are given:
* The dense topology
* The atomic topology
as well as the complete lattice structure on Grothendieck topologies (which gives two additional
explicit topologies: the discrete and trivial topologies.)

## Tags

Grothendieck topology, coverage, pretopology, site
-/

universes v u
namespace category_theory

open category_theory category

variables (C : Type u) [category.{v} C]

/--
The definition of a Grothendieck topology: a set of sieves `J X` on each object `X` satisfying
three axioms:
1. For every object `X`, the maximal sieve is in `J X`.
2. If `S ‚àà J X` then its pullback along any `h : Y ‚ü∂ X` is in `J Y`.
3. If `S ‚àà J X` and `R` is a sieve on `X`, then provided that the pullback of `R` along any arrow
   `f : Y ‚ü∂ X` in `S` is in `J Y`, we have that `R` itself is in `J X`.

A sieve `S` on `X` is referred to as `J`-covering, (or just covering), if `S ‚àà J X`.
-/
@[ext]
structure grothendieck_topology :=
(sieves : Œ† (X : C), set (sieve X))
(top_mem' : ‚àÄ X, ‚ä§ ‚àà sieves X)
(pullback_stable' : ‚àÄ ‚¶ÉX Y : C‚¶Ñ ‚¶ÉS : sieve X‚¶Ñ (f : Y ‚ü∂ X), S ‚àà sieves X ‚Üí S.pullback f ‚àà sieves Y)
(transitive' : ‚àÄ ‚¶ÉX‚¶Ñ ‚¶ÉS : sieve X‚¶Ñ (hS : S ‚àà sieves X) (R : sieve X),
              (‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S.arrows f ‚Üí R.pullback f ‚àà sieves Y) ‚Üí R ‚àà sieves X)

namespace grothendieck_topology

instance : has_coe_to_fun (grothendieck_topology C) :=
‚ü®_, Œª J, J.sieves‚ü©

variables {C} {X Y : C} {S R : sieve X}
variables (J : grothendieck_topology C)

@[simp] lemma mem_sieves_iff_coe : S ‚àà J.sieves X ‚Üî S ‚àà J X := iff.rfl

-- Also known as the maximality axiom.
@[simp] lemma top_mem (X : C) : ‚ä§ ‚àà J X := J.top_mem' X
-- Also known as the stability axiom.
@[simp] lemma pullback_stable (f : Y ‚ü∂ X) (hS : S ‚àà J X) : S.pullback f ‚àà J Y :=
J.pullback_stable' f hS
lemma transitive (hS : S ‚àà J X) (R : sieve X)
  (h : ‚àÄ ‚¶ÉY‚¶Ñ ‚¶Éf : Y ‚ü∂ X‚¶Ñ, S.arrows f ‚Üí R.pullback f ‚àà J Y) :
  R ‚àà J X :=
J.transitive' hS R h

lemma covering_of_eq_top : S = ‚ä§ ‚Üí S ‚àà J X := Œª h, h.symm ‚ñ∏ J.top_mem X

lemma superset_covering (Hss : S ‚â§ R) (sjx : S ‚àà J X) : R ‚àà J X :=
begin
  apply J.transitive sjx R (Œª Y f hf, _),
  apply covering_of_eq_top,
  rw [‚Üê top_le_iff, ‚Üê S.pullback_eq_top_of_mem hf],
  apply sieve.pullback_monotone _ Hss,
end

lemma intersection_covering (rj : R ‚àà J X) (sj : S ‚àà J X) : R ‚äì S ‚àà J X :=
begin
  apply J.transitive rj _ (Œª Y f Hf, _),
  rw [sieve.pullback_inter, R.pullback_eq_top_of_mem Hf],
  simp [sj],
end

@[simp]
lemma intersection_covering_iff : R ‚äì S ‚àà J X ‚Üî R ‚àà J X ‚àß S ‚àà J X :=
‚ü®Œª h, ‚ü®J.superset_covering inf_le_left h, J.superset_covering inf_le_right h‚ü©,
 Œª t, intersection_covering _ t.1 t.2‚ü©

/--
The sieve `S` on `X` `J`-covers an arrow `f` to `X` if `S.pullback f ‚àà J Y`.
This definition is an alternate way of presenting a Grothendieck topology.
-/
def covers (S : sieve X) (f : Y ‚ü∂ X) : Prop := S.pullback f ‚àà J Y

lemma covers_iff (S : sieve X) (f : Y ‚ü∂ X) : J.covers S f ‚Üî S.pullback f ‚àà J Y :=
iff.rfl

lemma covering_iff_covers_id (S : sieve X) : S ‚àà J X ‚Üî J.covers S (ùüô X) :=
by simp [covers_iff]

/-- The maximality axiom in 'arrow' form: Any arrow `f` in `S` is covered by `S`. -/
lemma arrow_max (f : Y ‚ü∂ X) (S : sieve X) (hf : S.arrows f) : J.covers S f :=
begin
  rw [covers, (sieve.pullback_eq_top_iff_mem f).1 hf],
  apply J.top_mem,
end

/-- The stability axiom in 'arrow' form: If `S` covers `f` then `S` covers `g ‚â´ f` for any `g`. -/
lemma arrow_stab (f : Y ‚ü∂ X) (S : sieve X) (h : J.covers S f) {Z : C} (g : Z ‚ü∂ Y) :
  J.covers S (g ‚â´ f) :=
begin
  rw covers_iff at h ‚ä¢,
  simp [h, sieve.pullback_comp],
end

/--
The transitivity axiom in 'arrow' form: If `S` covers `f` and every arrow in `S` is covered by
`R`, then `R` covers `f`.
-/
lemma arrow_trans (f : Y ‚ü∂ X) (S R : sieve X) (h : J.covers S f) :
  (‚àÄ {Z : C} (g : Z ‚ü∂ X), S.arrows g ‚Üí J.covers R g) ‚Üí J.covers R f :=
begin
  intro k,
  apply J.transitive h,
  intros Z g hg,
  rw ‚Üê sieve.pullback_comp,
  apply k (g ‚â´ f) hg,
end

lemma arrow_intersect (f : Y ‚ü∂ X) (S R : sieve X) (hS : J.covers S f) (hR : J.covers R f) :
  J.covers (S ‚äì R) f :=
by simpa [covers_iff] using and.intro hS hR

instance : partial_order (grothendieck_topology C) :=
{ le := Œª J‚ÇÅ J‚ÇÇ, (J‚ÇÅ : Œ† (X : C), set (sieve X)) ‚â§ (J‚ÇÇ : Œ† (X : C), set (sieve X)),
  le_refl := Œª J‚ÇÅ, le_refl _,
  le_trans := Œª J‚ÇÅ J‚ÇÇ J‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ, le_trans h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ,
  le_antisymm := Œª J‚ÇÅ J‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ, grothendieck_topology.ext _ _ (le_antisymm h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ) }

instance : has_Inf (grothendieck_topology C) :=
{ Inf := Œª T,
  { sieves := Inf (sieves '' T),
    top_mem' :=
    begin
      rintro X S ‚ü®‚ü®_, J, hJ, rfl‚ü©, rfl‚ü©,
      simp,
    end,
    pullback_stable' :=
    begin
      rintro X Y S hS f _ ‚ü®‚ü®_, J, hJ, rfl‚ü©, rfl‚ü©,
      apply J.pullback_stable _ (f _ ‚ü®‚ü®_, _, hJ, rfl‚ü©, rfl‚ü©),
    end,
    transitive' :=
    begin
      rintro X S hS R h _ ‚ü®‚ü®_, J, hJ, rfl‚ü©, rfl‚ü©,
      apply J.transitive (hS _ ‚ü®‚ü®_, _, hJ, rfl‚ü©, rfl‚ü©) _ (Œª Y f hf, h hf _ ‚ü®‚ü®_, _, hJ, rfl‚ü©, rfl‚ü©),
    end } }

instance : complete_lattice (grothendieck_topology C) :=
complete_lattice_of_Inf _
begin
  intro T,
  refine @is_glb.of_image _ _ _ _ sieves _ _ _ _,
  { intros, refl },
  { exact is_glb_Inf _ },
end

variable (C)
def trivial : grothendieck_topology C :=
{ sieves := Œª X, {‚ä§},
  top_mem' := Œª X, rfl,
  pullback_stable' := Œª X Y S f hf,
  begin
    rw set.mem_singleton_iff at ‚ä¢ hf,
    simp [hf],
  end,
  transitive' := Œª X S hS R hR,
  begin
    rw [set.mem_singleton_iff, ‚Üê sieve.id_mem_iff_eq_top] at hS,
    simpa using hR hS,
  end }
variable {C}

lemma trivial_covering : S ‚àà trivial C X ‚Üî S = ‚ä§ := set.mem_singleton_iff

lemma trivial_covers (S : sieve X) (f : Y ‚ü∂ X) : (trivial C).covers S f ‚Üî S.arrows f :=
by rw [covers_iff, trivial_covering, ‚Üê sieve.pullback_eq_top_iff_mem]

lemma trivial_eq_bot : trivial C = ‚ä• :=
begin
  rw ‚Üê le_bot_iff,
  intros X S hS,
  rw trivial_covering at hS,
  apply covering_of_eq_top _ hS,
end

def discrete : grothendieck_topology C :=
{ sieves := Œª X, set.univ,
  top_mem' := by simp,
  pullback_stable' := Œª X Y f, by simp,
  transitive' := by simp }

lemma discrete_eq_top : discrete C = ‚ä§ := top_unique (Œª S hS, set.mem_univ _)

/-- The dense Grothendieck topology. -/
def dense : grothendieck_topology C :=
{ sieves := Œª X S, ‚àÄ {Y : C} (f : Y ‚ü∂ X), ‚àÉ Z (g : Z ‚ü∂ Y), S.arrows (g ‚â´ f),
  top_mem' := Œª X Y f, ‚ü®Y, ùüô Y, ‚ü®‚ü©‚ü©,
  pullback_stable' :=
  begin
    intros X Y S h H Z f,
    rcases H (f ‚â´ h) with ‚ü®W, g, H'‚ü©,
    exact ‚ü®W, g, by simpa‚ü©,
  end,
  transitive' :=
  begin
    intros X S H‚ÇÅ R H‚ÇÇ Y f,
    rcases H‚ÇÅ f with ‚ü®Z, g, H‚ÇÉ‚ü©,
    rcases H‚ÇÇ H‚ÇÉ (ùüô Z) with ‚ü®W, h, H‚ÇÑ‚ü©,
    exact ‚ü®W, (h ‚â´ g), by simpa using H‚ÇÑ‚ü©,
  end }

instance : inhabited (grothendieck_topology C) := ‚ü®dense‚ü©

/--
A category satisfies the right Ore condition if any span can be completed to a
commutative square.
NB. Any category with pullbacks obviously satisfies the right Ore condition.
-/
def right_ore_condition (C : Type u) [category.{v} C] : Prop :=
‚àÄ {X Y Z : C} (yx : Y ‚ü∂ X) (zx : Z ‚ü∂ X), ‚àÉ W (wy : W ‚ü∂ Y) (wz : W ‚ü∂ Z), wy ‚â´ yx = wz ‚â´ zx

lemma right_ore_of_pullbacks [limits.has_pullbacks C] : right_ore_condition C :=
Œª X Y Z yx zx, ‚ü®_, _, _, limits.pullback.condition‚ü©

/--
The atomic Grothendieck topology: a sieve is covering iff it is nonempty.
For the pullback stability condition, we need the right Ore condition to hold.
-/
def atomic (hro : right_ore_condition C) : grothendieck_topology C :=
{ sieves := Œª X S, ‚àÉ Y (f : Y ‚ü∂ X), S.arrows f,
  top_mem' := Œª X, ‚ü®_, ùüô _, ‚ü®‚ü©‚ü©,
  pullback_stable' :=
  begin
    rintros X Y S h ‚ü®Z, f, hf‚ü©,
    rcases hro h f with ‚ü®W, g, k, comm‚ü©,
    refine ‚ü®_, g, _‚ü©,
    simp [comm, hf],
  end,
  transitive' :=
  begin
    rintros X S ‚ü®Y, f, hf‚ü© R h,
    rcases h hf with ‚ü®Z, g, hg‚ü©,
    exact ‚ü®_, _, hg‚ü©,
  end }

lemma dense_covering : S ‚àà dense X ‚Üî ‚àÄ {Y} (f : Y ‚ü∂ X), ‚àÉ Z (g : Z ‚ü∂ Y), S.arrows (g ‚â´ f) :=
iff.rfl

end grothendieck_topology

end category_theory
