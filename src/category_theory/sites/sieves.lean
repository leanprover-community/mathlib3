/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, E. W. Ayers
-/

import category_theory.over
import category_theory.limits.shapes.finite_limits
import category_theory.yoneda
import order.complete_lattice
import data.set.lattice

/-!
# Theory of sieves

- For an object `X` of a category `C`, a `sieve X` is a set of morphisms to `X`
  which is closed under left-composition.
- The complete lattice structure on sieves is given, as well as the Galois insertion
  given by downward-closing.
- A `sieve X` (functorially) induces a presheaf on `C` together with a monomorphism to
  the yoneda embedding of `X`.

## Tags

sieve, pullback
-/

universes v u
namespace category_theory

/-
For an object `X` of a category `C`, a `sieve X` is a set of morphisms to `X`
which is closed under left-composition.
In practice it seems easier to work with this if left-composition is stated by
quantifying over objects `Y` and arrows `Y âŸ¶ X` rather than quantifying over `over X`.
-/
structure sieve {C : Type u} [category.{v} C] (X : C) :=
(arrows : set (over X))
(subs : âˆ€ {Y Z : C} {f : Y âŸ¶ X} (g : Z âŸ¶ Y), over.mk f âˆˆ arrows â†’ over.mk (g â‰« f) âˆˆ arrows)

namespace sieve

variables {C : Type u} [category.{v} C]

variables {X Y Z : C} {S R : sieve X}

@[simp, priority 100]
lemma downward_closed (S : sieve X) {f : Y âŸ¶ X} (Hf : over.mk f âˆˆ S.arrows) (g : Z âŸ¶ Y) :
  over.mk (g â‰« f) âˆˆ S.arrows :=
S.subs g Hf

lemma arrows_ext : Î  {R S : sieve X}, R.arrows = S.arrows â†’ R = S
| âŸ¨Ra, _âŸ© âŸ¨Sa, _âŸ© rfl := rfl

@[ext] lemma ext {R S : sieve X}
  (h : âˆ€ {Y} (f : Y âŸ¶ X), over.mk f âˆˆ R.arrows â†” over.mk f âˆˆ S.arrows) :
  R = S :=
begin
  apply arrows_ext,
  ext âŸ¨_, _, f'âŸ©,
  convert h f';
  apply subsingleton.elim,
end

lemma ext_iff {R S : sieve X} :
  R = S â†” (âˆ€ {Y} (f : Y âŸ¶ X), over.mk f âˆˆ R.arrows â†” over.mk f âˆˆ S.arrows) :=
âŸ¨Î» h Y f, h â–¸ iff.rfl, sieve.extâŸ©

open lattice

/-- The supremum of a collection of sieves: just the union of them all. -/
protected def Sup (ğ’® : set (sieve X)) : (sieve X) :=
{ arrows := {f | âˆƒ S âˆˆ ğ’®, f âˆˆ sieve.arrows S},
  subs := Î» Y Z f g, by { rintro âŸ¨S, hS, hfâŸ©, exact âŸ¨S, hS, S.downward_closed hf _âŸ© } }

/-- The infimum of a collection of sieves: the intersection of them all. -/
protected def Inf (ğ’® : set (sieve X)) : (sieve X) :=
{ arrows := {f | âˆ€ S âˆˆ ğ’®, f âˆˆ sieve.arrows S},
  subs := Î» Y Z f g hf S H, S.downward_closed (hf S H) g }

/-- The union of two sieves is a sieve. -/
protected def union (S R : sieve X) : sieve X :=
{ arrows := S.arrows âˆª R.arrows,
  subs := by { rintros Y Z f g (h | h); simp [h] } }

/-- The intersection of two sieves is a sieve. -/
protected def inter (S R : sieve X) : sieve X :=
{ arrows := S.arrows âˆ© R.arrows,
  subs := by { rintros Y Z f g âŸ¨hâ‚, hâ‚‚âŸ©; simp [hâ‚, hâ‚‚] } }

/--
Sieves on an object `X` form a complete lattice.
We generate this directly rather than using the galois insertion for nicer definitional
properties.
-/
instance : complete_lattice (sieve X) :=
{ le           := Î» S R, âˆ€ Y (f : Y âŸ¶ X), over.mk f âˆˆ S.arrows â†’ over.mk f âˆˆ R.arrows,
  le_refl      := Î» S f q, id,
  le_trans     := Î» Sâ‚ Sâ‚‚ Sâ‚ƒ Sâ‚â‚‚ Sâ‚‚â‚ƒ Y f h, Sâ‚‚â‚ƒ _ _ (Sâ‚â‚‚ _ _ h),
  le_antisymm  := Î» S R p q, sieve.ext (Î» Y f, âŸ¨p _ _, q _ _âŸ©),
  top          := { arrows := set.univ, subs := Î» Y Z f g h, âŸ¨âŸ© },
  bot          := { arrows := âˆ…, subs := Î» _ _ _ _, false.elim },
  sup          := sieve.union,
  inf          := sieve.inter,
  Sup          := sieve.Sup,
  Inf          := sieve.Inf,
  le_Sup       := Î» ğ’® S hS Y f hf, âŸ¨S, hS, hfâŸ©,
  Sup_le       := Î» â„° S hS Y f, by { rintro âŸ¨R, hR, hfâŸ©, apply hS R hR _ _ hf },
  Inf_le       := Î» _ _ hS _ _ h, h _ hS,
  le_Inf       := Î» _ _ hS _ _ hf _ hR, hS _ hR _ _ hf,
  le_sup_left  := Î» _ _ _ _, or.inl,
  le_sup_right := Î» _ _ _ _, or.inr,
  sup_le       := Î» _ _ _ a b _ _ hf, hf.elim (a _ _) (b _ _),
  inf_le_left  := Î» _ _ _ _, and.left,
  inf_le_right := Î» _ _ _ _, and.right,
  le_inf       := Î» _ _ _ p q _ _ z, âŸ¨p _ _ z, q _ _ zâŸ©,
  le_top       := Î» _ _ _ _, trivial,
  bot_le       := Î» _ _ _, false.elim }

instance : inhabited (sieve X) := âŸ¨âŠ¤âŸ©

@[simp]
lemma mem_Inf {Ss : set (sieve X)} {Y} (f : Y âŸ¶ X) :
  over.mk f âˆˆ (Inf Ss).arrows â†” âˆ€ S âˆˆ Ss, over.mk f âˆˆ sieve.arrows S :=
iff.rfl

@[simp]
lemma mem_Sup {Ss : set (sieve X)} {Y} (f : Y âŸ¶ X) :
  over.mk f âˆˆ (Sup Ss).arrows â†” âˆƒ S âˆˆ Ss, over.mk f âˆˆ sieve.arrows S :=
iff.rfl

@[simp]
lemma mem_inter {R S : sieve X} {Y} (f : Y âŸ¶ X) :
  over.mk f âˆˆ (R âŠ“ S).arrows â†” over.mk f âˆˆ R.arrows âˆ§ over.mk f âˆˆ S.arrows :=
iff.rfl

@[simp]
lemma mem_union {R S : sieve X} {Y} (f : Y âŸ¶ X) :
  over.mk f âˆˆ (R âŠ” S).arrows â†” over.mk f âˆˆ R.arrows âˆ¨ over.mk f âˆˆ S.arrows :=
iff.rfl

@[simp]
lemma mem_top (f : Y âŸ¶ X) : over.mk f âˆˆ (âŠ¤ : sieve X).arrows := trivial

/-- Take the downward-closure of a set of morphisms to `X`. -/
inductive generate_sets (ğ’¢ : set (over X)) : set (over X)
| basic : Î  {f : over X}, f âˆˆ ğ’¢ â†’ generate_sets f
| subs  : Î  {Y Z} {f : Y âŸ¶ X} (g : Z âŸ¶ Y), generate_sets (over.mk f) â†’ generate_sets (over.mk (g â‰« f))

/-- Generate the smallest sieve containing the given set of arrows. -/
def generate (ğ’¢ : set (over X)) : sieve X :=
{ arrows := generate_sets ğ’¢,
  subs   := Î» _ _ _, generate_sets.subs }

open order lattice

lemma sets_iff_generate {ğ’¢ : set (over X)} : generate ğ’¢ â‰¤ S â†” ğ’¢ âŠ† S.arrows :=
iff.intro
  (Î» H g hg,
    begin
      have : over.mk g.hom = g,
        cases g, dsimp [over.mk],
        congr' 1, apply subsingleton.elim,
      rw â† this at *,
      exact H _ g.hom (generate_sets.basic hg),
    end )
  (Î» ss Y f hf,
    begin
      induction hf,
      case basic : f hf { exact ss hf },
      case subs : Y Z f g hfâ‚ hfâ‚‚ { exact downward_closed S hfâ‚‚ _  }
    end)

/-- Show that there is a galois insertion (generate, .arrows). -/
def gi_generate :
  @galois_insertion (set (over X)) (sieve X) (by apply_instance) _ generate sieve.arrows :=
  { gc        := Î» _ _, sets_iff_generate,
    choice    := Î» ğ’¢ _, generate ğ’¢,
    choice_eq := Î» _ _, rfl,
    le_l_u    := Î» _ _ _, generate_sets.basic }

/-- Given a morphism `h : Y âŸ¶ X`, send a sieve S on X to a sieve on Y
    as the inverse image of S with `_ â‰« h`.
    That is, `sieve.pullback S h := (â‰« h) 'â»Â¹ S`. -/
def pullback (S : sieve X) (h : Y âŸ¶ X) : sieve Y :=
{ arrows := {sl | over.mk (sl.hom â‰« h) âˆˆ S.arrows },
  subs := Î» f hf Z g k, by { dsimp at k, simp [k] } }

@[simp] lemma mem_pullback (h : Y âŸ¶ X) {f : Z âŸ¶ Y} :
  over.mk f âˆˆ (pullback S h).arrows â†” over.mk (f â‰« h) âˆˆ S.arrows := iff.rfl

/--
Push a sieve `R` on `Y` forward along an arrow `f : Y âŸ¶ X`: `gf : Z âŸ¶ X`
is in the sieve if `gf` factors through some `g : Z âŸ¶ Y` which is in `R`.
-/
def comp (R : sieve Y) (f : Y âŸ¶ X) : sieve X :=
{ arrows := Î» gf, âˆƒ (g : gf.left âŸ¶ Y), over.mk g âˆˆ R.arrows âˆ§ g â‰« f = gf.hom,
  subs :=
  begin
    rintros Zâ‚ Zâ‚‚ g h âŸ¨j, k, zâŸ©,
    exact âŸ¨h â‰« j, by simp [k], by simp [z]âŸ©,
  end }

/-- Pullback is monotonic -/
lemma pullback_le_map {S R : sieve X} (Hss : S â‰¤ R) (f : Y âŸ¶ X) : pullback S f â‰¤ pullback R f :=
Î» Z H, Hss _ _

lemma pullback_top {f : Y âŸ¶ X} : pullback âŠ¤ f = âŠ¤ :=
top_unique (Î» _ g, id)

lemma pullback_comp {f : Y âŸ¶ X} {g : Z âŸ¶ Y} (S : sieve X) : S.pullback (g â‰« f) = (S.pullback f).pullback g :=
by simp [sieve.ext_iff]

lemma pullback_inter {f : Y âŸ¶ X} (S R : sieve X) : (S âŠ“ R).pullback f = S.pullback f âŠ“ R.pullback f :=
by simp [sieve.ext_iff]

lemma le_pullback_comp {R : sieve Y} {f : Y âŸ¶ X} :
  R â‰¤ pullback (comp R f) f :=
begin rintros Z g b, refine âŸ¨_, _, rflâŸ©, simpa end

/-- If the identity arrow is in a sieve, the sieve is maximal. -/
lemma id_mem_iff_eq_top : over.mk (ğŸ™ X) âˆˆ S.arrows â†” S = âŠ¤ :=
âŸ¨Î» h, top_unique
begin
  rintros Y f âŸ¨âŸ©,
  suffices : over.mk (f â‰« (ğŸ™ _)) âˆˆ S.arrows,
    simpa using this,
  apply downward_closed _ h,
end,
Î» h, h.symm â–¸ trivial âŸ©

lemma pullback_eq_top_iff_mem (f : Y âŸ¶ X) : over.mk f âˆˆ S.arrows â†” S.pullback f = âŠ¤ :=
by rw [â† id_mem_iff_eq_top, mem_pullback, category.id_comp]

/-- A sieve induces a presheaf. -/
@[simps]
def functor (S : sieve X) : Cáµ’áµ– â¥¤ Type v :=
{ obj := Î» Y, {g : Y.unop âŸ¶ X // over.mk g âˆˆ S.arrows},
  map := Î» Y Z f g, âŸ¨f.unop â‰« g.1, downward_closed _ g.2 _âŸ© }

/--
If a sieve S is contained in a sieve T, then we have a morphism of presheaves on their induced
presheaves.
-/
@[simps]
def le_functor {S T : sieve X} (h : S â‰¤ T) : S.functor âŸ¶ T.functor :=
{ app := Î» Y f, âŸ¨f.1, h _ _ f.2âŸ© }.

/-- The natural inclusion from the functor induced by a sieve to the yoneda embedding. -/
@[simps]
def functor_inclusion (S : sieve X) : S.functor âŸ¶ yoneda.obj X :=
{ app := Î» Y f, f.1 }.

lemma le_functor_comm {S T : sieve X} (h : S â‰¤ T) :
  le_functor h â‰« functor_inclusion _ = functor_inclusion _ :=
by { ext c t, refl }

/-- The presheaf induced by a sieve is a subobject of the yoneda embedding. -/
instance functor_inclusion_is_mono : mono (functor_inclusion S) :=
âŸ¨Î» Z f g h, begin
  ext Y y,
  have : (f â‰« functor_inclusion S).app Y y = (g â‰« functor_inclusion S).app Y y,
    rw h,
  exact this
endâŸ©

end sieve
end category_theory
