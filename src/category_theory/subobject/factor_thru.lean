/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Scott Morrison
-/
import category_theory.subobject.basic
import category_theory.preadditive.basic

/-!
# Factoring through subobjects

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

The predicate `h : P.factors f`, for `P : subobject Y` and `f : X ‚ü∂ Y`
asserts the existence of some `P.factor_thru f : X ‚ü∂ (P : C)` making the obvious diagram commute.

-/

universes v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

noncomputable theory

open category_theory category_theory.category category_theory.limits

variables {C : Type u‚ÇÅ} [category.{v‚ÇÅ} C] {X Y Z : C}
variables {D : Type u‚ÇÇ} [category.{v‚ÇÇ} D]

namespace category_theory

namespace mono_over

/-- When `f : X ‚ü∂ Y` and `P : mono_over Y`,
`P.factors f` expresses that there exists a factorisation of `f` through `P`.
Given `h : P.factors f`, you can recover the morphism as `P.factor_thru f h`.
-/
def factors {X Y : C} (P : mono_over Y) (f : X ‚ü∂ Y) : Prop := ‚àÉ g : X ‚ü∂ (P : C), g ‚â´ P.arrow = f

lemma factors_congr {X : C} {f g : mono_over X} {Y : C} (h : Y ‚ü∂ X) (e : f ‚âÖ g) :
  f.factors h ‚Üî g.factors h :=
‚ü®Œª ‚ü®u, hu‚ü©, ‚ü®u ‚â´ (((mono_over.forget _).map e.hom)).left, by simp [hu]‚ü©,
 Œª ‚ü®u, hu‚ü©, ‚ü®u ‚â´ (((mono_over.forget _).map e.inv)).left, by simp [hu]‚ü©‚ü©

/-- `P.factor_thru f h` provides a factorisation of `f : X ‚ü∂ Y` through some `P : mono_over Y`,
given the evidence `h : P.factors f` that such a factorisation exists. -/
def factor_thru {X Y : C} (P : mono_over Y) (f : X ‚ü∂ Y) (h : factors P f) : X ‚ü∂ (P : C) :=
classical.some h

end mono_over

namespace subobject

/-- When `f : X ‚ü∂ Y` and `P : subobject Y`,
`P.factors f` expresses that there exists a factorisation of `f` through `P`.
Given `h : P.factors f`, you can recover the morphism as `P.factor_thru f h`.
-/
def factors {X Y : C} (P : subobject Y) (f : X ‚ü∂ Y) : Prop :=
quotient.lift_on' P (Œª P, P.factors f)
begin
  rintros P Q ‚ü®h‚ü©,
  apply propext,
  split,
  { rintro ‚ü®i, w‚ü©,
    exact ‚ü®i ‚â´ h.hom.left, by erw [category.assoc, over.w h.hom, w]‚ü©, },
  { rintro ‚ü®i, w‚ü©,
    exact ‚ü®i ‚â´ h.inv.left, by erw [category.assoc, over.w h.inv, w]‚ü©, },
end

@[simp] lemma mk_factors_iff {X Y Z : C} (f : Y ‚ü∂ X) [mono f] (g : Z ‚ü∂ X) :
  (subobject.mk f).factors g ‚Üî (mono_over.mk' f).factors g :=
iff.rfl

lemma mk_factors_self (f : X ‚ü∂ Y) [mono f] : (mk f).factors f := ‚ü®ùüô _, by simp‚ü©

lemma factors_iff {X Y : C} (P : subobject Y) (f : X ‚ü∂ Y) :
  P.factors f ‚Üî (representative.obj P).factors f :=
quot.induction_on P $ Œª a, mono_over.factors_congr _ (representative_iso _).symm

lemma factors_self {X : C} (P : subobject X) : P.factors P.arrow :=
(factors_iff _ _).mpr ‚ü®ùüô P, (by simp)‚ü©

lemma factors_comp_arrow {X Y : C} {P : subobject Y} (f : X ‚ü∂ P) : P.factors (f ‚â´ P.arrow) :=
(factors_iff _ _).mpr ‚ü®f, rfl‚ü©

lemma factors_of_factors_right {X Y Z : C} {P : subobject Z} (f : X ‚ü∂ Y) {g : Y ‚ü∂ Z}
  (h : P.factors g) : P.factors (f ‚â´ g) :=
begin
  revert P,
  refine quotient.ind' _,
  intro P,
  rintro ‚ü®g, rfl‚ü©,
  exact ‚ü®f ‚â´ g, by simp‚ü©,
end

lemma factors_zero [has_zero_morphisms C] {X Y : C} {P : subobject Y} :
  P.factors (0 : X ‚ü∂ Y) :=
(factors_iff _ _).mpr ‚ü®0, by simp‚ü©

lemma factors_of_le {Y Z : C} {P Q : subobject Y} (f : Z ‚ü∂ Y) (h : P ‚â§ Q) :
  P.factors f ‚Üí Q.factors f :=
by { simp only [factors_iff], exact Œª ‚ü®u, hu‚ü©, ‚ü®u ‚â´ of_le _ _ h, by simp [‚Üêhu]‚ü© }

/-- `P.factor_thru f h` provides a factorisation of `f : X ‚ü∂ Y` through some `P : subobject Y`,
given the evidence `h : P.factors f` that such a factorisation exists. -/
def factor_thru {X Y : C} (P : subobject Y) (f : X ‚ü∂ Y) (h : factors P f) : X ‚ü∂ P :=
classical.some ((factors_iff _ _).mp h)

@[simp, reassoc] lemma factor_thru_arrow {X Y : C} (P : subobject Y) (f : X ‚ü∂ Y) (h : factors P f) :
  P.factor_thru f h ‚â´ P.arrow = f :=
classical.some_spec ((factors_iff _ _).mp h)

@[simp] lemma factor_thru_self {X : C} (P : subobject X) (h) :
  P.factor_thru P.arrow h = ùüô P :=
by { ext, simp, }

@[simp] lemma factor_thru_mk_self (f : X ‚ü∂ Y) [mono f] :
  (mk f).factor_thru f (mk_factors_self f) = (underlying_iso f).inv :=
by { ext, simp, }

@[simp] lemma factor_thru_comp_arrow {X Y : C} {P : subobject Y} (f : X ‚ü∂ P) (h) :
  P.factor_thru (f ‚â´ P.arrow) h = f :=
by { ext, simp, }

@[simp] lemma factor_thru_eq_zero [has_zero_morphisms C]
  {X Y : C} {P : subobject Y} {f : X ‚ü∂ Y} {h : factors P f} :
  P.factor_thru f h = 0 ‚Üî f = 0 :=
begin
  fsplit,
  { intro w,
    replace w := w =‚â´ P.arrow,
    simpa using w, },
  { rintro rfl,
    ext, simp, },
end

lemma factor_thru_right {X Y Z : C} {P : subobject Z} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (h : P.factors g) :
  f ‚â´ P.factor_thru g h = P.factor_thru (f ‚â´ g) (factors_of_factors_right f h) :=
begin
  apply (cancel_mono P.arrow).mp,
  simp,
end

@[simp]
lemma factor_thru_zero
  [has_zero_morphisms C] {X Y : C} {P : subobject Y} (h : P.factors (0 : X ‚ü∂ Y)) :
  P.factor_thru 0 h = 0 :=
by simp

-- `h` is an explicit argument here so we can use
-- `rw factor_thru_le h`, obtaining a subgoal `P.factors f`.
-- (While the reverse direction looks plausible as a simp lemma, it seems to be unproductive.)
lemma factor_thru_of_le
  {Y Z : C} {P Q : subobject Y} {f : Z ‚ü∂ Y} (h : P ‚â§ Q) (w : P.factors f) :
  Q.factor_thru f (factors_of_le f h w) = P.factor_thru f w ‚â´ of_le P Q h :=
by { ext, simp, }

section preadditive

variables [preadditive C]

lemma factors_add {X Y : C} {P : subobject Y} (f g : X ‚ü∂ Y) (wf : P.factors f) (wg : P.factors g) :
  P.factors (f + g) :=
(factors_iff _ _).mpr ‚ü®P.factor_thru f wf + P.factor_thru g wg, by simp‚ü©

-- This can't be a `simp` lemma as `wf` and `wg` may not exist.
-- However you can `rw` by it to assert that `f` and `g` factor through `P` separately.
lemma factor_thru_add {X Y : C} {P : subobject Y} (f g : X ‚ü∂ Y)
   (w : P.factors (f + g)) (wf : P.factors f) (wg : P.factors g) :
  P.factor_thru (f + g) w = P.factor_thru f wf + P.factor_thru g wg :=
by { ext, simp, }

lemma factors_left_of_factors_add {X Y : C} {P : subobject Y} (f g : X ‚ü∂ Y)
  (w : P.factors (f + g)) (wg : P.factors g) : P.factors f :=
(factors_iff _ _).mpr ‚ü®P.factor_thru (f + g) w - P.factor_thru g wg, by simp‚ü©

@[simp]
lemma factor_thru_add_sub_factor_thru_right {X Y : C} {P : subobject Y} (f g : X ‚ü∂ Y)
  (w : P.factors (f + g)) (wg : P.factors g) :
  P.factor_thru (f + g) w - P.factor_thru g wg =
    P.factor_thru f (factors_left_of_factors_add f g w wg) :=
by { ext, simp, }

lemma factors_right_of_factors_add {X Y : C} {P : subobject Y} (f g : X ‚ü∂ Y)
  (w : P.factors (f + g)) (wf : P.factors f) : P.factors g :=
(factors_iff _ _).mpr ‚ü®P.factor_thru (f + g) w - P.factor_thru f wf, by simp‚ü©

@[simp]
lemma factor_thru_add_sub_factor_thru_left {X Y : C} {P : subobject Y} (f g : X ‚ü∂ Y)
  (w : P.factors (f + g)) (wf : P.factors f) :
  P.factor_thru (f + g) w - P.factor_thru f wf =
    P.factor_thru g (factors_right_of_factors_add f g w wf) :=
by { ext, simp, }

end preadditive

end subobject

end category_theory
