/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stephen Morgan, Scott Morrison, Johannes H√∂lzl
-/
import category_theory.fully_faithful
import data.equiv.basic

/-!
# The category `Type`.

In this section we set up the theory so that Lean's types and functions between them
can be viewed as a `large_category` in our framework.

Lean can not transparently view a function as a morphism in this category, and needs a hint in
order to be able to type check. We provide the abbreviation `as_hom f` to guide type checking,
as well as a corresponding notation `‚Üæ f`. (Entered as `\upr `.) The notation is enabled using
`open_locale category_theory.Type`.

We provide various simplification lemmas for functors and natural transformations valued in `Type`.

We define `ulift_functor`, from `Type u` to `Type (max u v)`, and show that it is fully faithful
(but not, of course, essentially surjective).

We prove some basic facts about the category `Type`:
*  epimorphisms are surjections and monomorphisms are injections,
* `iso` is both `iso` and `equiv` to `equiv` (at least within a fixed universe),
* every type level `is_lawful_functor` gives a categorical functor `Type ‚•§ Type`
  (the corresponding fact about monads is in `src/category_theory/monad/types.lean`).
-/

namespace category_theory

-- morphism levels before object levels. See note [category_theory universes].
universes v v' w u u'

/- The `@[to_additive]` attribute is just a hint that expressions involving this instance can
  still be additivized. -/
@[to_additive category_theory.types]
instance types : large_category (Type u) :=
{ hom     := Œª a b, (a ‚Üí b),
  id      := Œª a, id,
  comp    := Œª _ _ _ f g, g ‚àò f }

lemma types_hom {Œ± Œ≤ : Type u} : (Œ± ‚ü∂ Œ≤) = (Œ± ‚Üí Œ≤) := rfl
lemma types_id (X : Type u) : ùüô X = id := rfl
lemma types_comp {X Y Z : Type u} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : f ‚â´ g = g ‚àò f := rfl

@[simp]
lemma types_id_apply (X : Type u) (x : X) : ((ùüô X) : X ‚Üí X) x = x := rfl
@[simp]
lemma types_comp_apply {X Y Z : Type u} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) : (f ‚â´ g) x = g (f x) := rfl

@[simp]
lemma hom_inv_id_apply {X Y : Type u} (f : X ‚âÖ Y) (x : X) : f.inv (f.hom x) = x :=
congr_fun f.hom_inv_id x
@[simp]
lemma inv_hom_id_apply {X Y : Type u} (f : X ‚âÖ Y) (y : Y) : f.hom (f.inv y) = y :=
congr_fun f.inv_hom_id y

/-- `as_hom f` helps Lean type check a function as a morphism in the category `Type`. -/
-- Unfortunately without this wrapper we can't use `category_theory` idioms, such as `is_iso f`.
abbreviation as_hom {Œ± Œ≤ : Type u} (f : Œ± ‚Üí Œ≤) : Œ± ‚ü∂ Œ≤ := f
-- If you don't mind some notation you can use fewer keystrokes:
localized "notation  `‚Üæ` f : 200 := as_hom f" in category_theory.Type -- type as \upr in VScode

section -- We verify the expected type checking behaviour of `as_hom`.
variables (Œ± Œ≤ Œ≥ : Type u) (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥)

example : Œ± ‚Üí Œ≥ := ‚Üæf ‚â´ ‚Üæg
example [is_iso ‚Üæf] : mono ‚Üæf := by apply_instance
example [is_iso ‚Üæf] : ‚Üæf ‚â´ inv ‚Üæf = ùüô Œ± := by simp
end

namespace functor
variables {J : Type u} [category.{v} J]

/--
The sections of a functor `J ‚•§ Type` are
the choices of a point `u j : F.obj j` for each `j`,
such that `F.map f (u j) = u j` for every morphism `f : j ‚ü∂ j'`.

We later use these to define limits in `Type` and in many concrete categories.
-/
def sections (F : J ‚•§ Type w) : set (Œ† j, F.obj j) :=
{ u | ‚àÄ {j j'} (f : j ‚ü∂ j'), F.map f (u j) = u j'}
end functor

namespace functor_to_types
variables {C : Type u} [category.{v} C] (F G H : C ‚•§ Type w) {X Y Z : C}
variables (œÉ : F ‚ü∂ G) (œÑ : G ‚ü∂ H)

@[simp] lemma map_comp_apply (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (a : F.obj X) :
  (F.map (f ‚â´ g)) a = (F.map g) ((F.map f) a) :=
by simp [types_comp]

@[simp] lemma map_id_apply (a : F.obj X) : (F.map (ùüô X)) a = a :=
by simp [types_id]

lemma naturality (f : X ‚ü∂ Y) (x : F.obj X) : œÉ.app Y ((F.map f) x) = (G.map f) (œÉ.app X x) :=
congr_fun (œÉ.naturality f) x

@[simp] lemma comp (x : F.obj X) : (œÉ ‚â´ œÑ).app X x = œÑ.app X (œÉ.app X x) := rfl

variables {D : Type u'} [ùíü : category.{u'} D] (I J : D ‚•§ C) (œÅ : I ‚ü∂ J) {W : D}

@[simp] lemma hcomp (x : (I ‚ãô F).obj W) :
  (œÅ ‚ó´ œÉ).app W x = (G.map (œÅ.app W)) (œÉ.app (I.obj W) x) :=
rfl

@[simp] lemma map_inv_map_hom_apply (f : X ‚âÖ Y) (x : F.obj X) : F.map f.inv (F.map f.hom x) = x :=
congr_fun (F.map_iso f).hom_inv_id x
@[simp] lemma map_hom_map_inv_apply (f : X ‚âÖ Y) (y : F.obj Y) : F.map f.hom (F.map f.inv y) = y :=
congr_fun (F.map_iso f).inv_hom_id y

@[simp] lemma hom_inv_id_app_apply (Œ± : F ‚âÖ G) (X) (x) : Œ±.inv.app X (Œ±.hom.app X x) = x :=
congr_fun (Œ±.hom_inv_id_app X) x
@[simp] lemma inv_hom_id_app_apply (Œ± : F ‚âÖ G) (X) (x) : Œ±.hom.app X (Œ±.inv.app X x) = x :=
congr_fun (Œ±.inv_hom_id_app X) x

end functor_to_types

/--
The isomorphism between a `Type` which has been `ulift`ed to the same universe,
and the original type.
-/
def ulift_trivial (V : Type u) : ulift.{u} V ‚âÖ V := by tidy

/--
The functor embedding `Type u` into `Type (max u v)`.
Write this as `ulift_functor.{5 2}` to get `Type 2 ‚•§ Type 5`.
-/
def ulift_functor : Type u ‚•§ Type (max u v) :=
{ obj := Œª X, ulift.{v} X,
  map := Œª X Y f, Œª x : ulift.{v} X, ulift.up (f x.down) }

@[simp] lemma ulift_functor_map {X Y : Type u} (f : X ‚ü∂ Y) (x : ulift.{v} X) :
  ulift_functor.map f x = ulift.up (f x.down) := rfl

instance ulift_functor_full : full.{u} ulift_functor :=
{ preimage := Œª X Y f x, (f (ulift.up x)).down }
instance ulift_functor_faithful : faithful ulift_functor :=
{ map_injective' := Œª X Y f g p, funext $ Œª x,
    congr_arg ulift.down ((congr_fun p (ulift.up x)) : ((ulift.up (f x)) = (ulift.up (g x)))) }

/--
The functor embedding `Type u` into `Type u` via `ulift` is isomorphic to the identity functor.
 -/
def ulift_functor_trivial : ulift_functor.{u u} ‚âÖ ùü≠ _ :=
nat_iso.of_components ulift_trivial (by tidy)

/-- Any term `x` of a type `X` corresponds to a morphism `punit ‚ü∂ X`. -/
-- TODO We should connect this to a general story about concrete categories
-- whose forgetful functor is representable.
def hom_of_element {X : Type u} (x : X) : punit ‚ü∂ X := Œª _, x

lemma hom_of_element_eq_iff {X : Type u} (x y : X) :
  hom_of_element x = hom_of_element y ‚Üî x = y :=
‚ü®Œª H, congr_fun H punit.star, by cc‚ü©

/--
A morphism in `Type` is a monomorphism if and only if it is injective.

See https://stacks.math.columbia.edu/tag/003C.
-/
lemma mono_iff_injective {X Y : Type u} (f : X ‚ü∂ Y) : mono f ‚Üî function.injective f :=
begin
  split,
  { intros H x x' h,
    resetI,
    rw ‚Üêhom_of_element_eq_iff at ‚ä¢ h,
    exact (cancel_mono f).mp h },
  { refine Œª H, ‚ü®Œª Z g h H‚ÇÇ, _‚ü©,
    ext z,
    replace H‚ÇÇ := congr_fun H‚ÇÇ z,
    exact H H‚ÇÇ }
end

/--
A morphism in `Type` is an epimorphism if and only if it is surjective.

See https://stacks.math.columbia.edu/tag/003C.
-/
lemma epi_iff_surjective {X Y : Type u} (f : X ‚ü∂ Y) : epi f ‚Üî function.surjective f :=
begin
  split,
  { intros H,
    let g : Y ‚ü∂ ulift Prop := Œª y, ‚ü®true‚ü©,
    let h : Y ‚ü∂ ulift Prop := Œª y, ‚ü®‚àÉ x, f x = y‚ü©,
    suffices : f ‚â´ g = f ‚â´ h,
    { resetI,
      rw cancel_epi at this,
      intro y,
      replace this := congr_fun this y,
      replace this : true = ‚àÉ x, f x = y := congr_arg ulift.down this,
      rw ‚Üêthis,
      trivial },
    ext x,
    change true ‚Üî ‚àÉ x', f x' = f x,
    rw true_iff,
    exact ‚ü®x, rfl‚ü© },
  { intro H,
    constructor,
    intros Z g h H‚ÇÇ,
    apply funext,
    rw ‚Üêforall_iff_forall_surj H,
    intro x,
    exact (congr_fun H‚ÇÇ x : _) }
end

section

/-- `of_type_functor m` converts from Lean's `Type`-based `category` to `category_theory`. This
allows us to use these functors in category theory. -/
def of_type_functor (m : Type u ‚Üí Type v) [_root_.functor m] [is_lawful_functor m] :
  Type u ‚•§ Type v :=
{ obj       := m,
  map       := ŒªŒ± Œ≤, _root_.functor.map,
  map_id'   := assume Œ±, _root_.functor.map_id,
  map_comp' := assume Œ± Œ≤ Œ≥ f g, funext $ assume a, is_lawful_functor.comp_map f g _ }

variables (m : Type u ‚Üí Type v) [_root_.functor m] [is_lawful_functor m]

@[simp]
lemma of_type_functor_obj : (of_type_functor m).obj = m := rfl

@[simp]
lemma of_type_functor_map {Œ± Œ≤} (f : Œ± ‚Üí Œ≤) :
  (of_type_functor m).map f = (_root_.functor.map f : m Œ± ‚Üí m Œ≤) := rfl

end

end category_theory

-- Isomorphisms in Type and equivalences.

namespace equiv

universe u

variables {X Y : Type u}

/--
Any equivalence between types in the same universe gives
a categorical isomorphism between those types.
-/
def to_iso (e : X ‚âÉ Y) : X ‚âÖ Y :=
{ hom := e.to_fun,
  inv := e.inv_fun,
  hom_inv_id' := funext e.left_inv,
  inv_hom_id' := funext e.right_inv }

@[simp] lemma to_iso_hom {e : X ‚âÉ Y} : e.to_iso.hom = e := rfl
@[simp] lemma to_iso_inv {e : X ‚âÉ Y} : e.to_iso.inv = e.symm := rfl

end equiv

universe u

namespace category_theory.iso
open category_theory

variables {X Y : Type u}

/--
Any isomorphism between types gives an equivalence.
-/
def to_equiv (i : X ‚âÖ Y) : X ‚âÉ Y :=
{ to_fun := i.hom,
  inv_fun := i.inv,
  left_inv := Œª x, congr_fun i.hom_inv_id x,
  right_inv := Œª y, congr_fun i.inv_hom_id y }

@[simp] lemma to_equiv_fun (i : X ‚âÖ Y) : (i.to_equiv : X ‚Üí Y) = i.hom := rfl
@[simp] lemma to_equiv_symm_fun (i : X ‚âÖ Y) : (i.to_equiv.symm : Y ‚Üí X) = i.inv := rfl

@[simp] lemma to_equiv_id (X : Type u) : (iso.refl X).to_equiv = equiv.refl X := rfl
@[simp] lemma to_equiv_comp {X Y Z : Type u} (f : X ‚âÖ Y) (g : Y ‚âÖ Z) :
  (f ‚â™‚â´ g).to_equiv = f.to_equiv.trans (g.to_equiv) := rfl

end category_theory.iso

namespace category_theory

/-- A morphism in `Type u` is an isomorphism if and only if it is bijective. -/
lemma is_iso_iff_bijective {X Y : Type u} (f : X ‚ü∂ Y) : is_iso f ‚Üî function.bijective f :=
iff.intro
  (Œª i, (by exactI as_iso f : X ‚âÖ Y).to_equiv.bijective)
  (Œª b, is_iso.of_iso (equiv.of_bijective f b).to_iso)

end category_theory

-- We prove `equiv_iso_iso` and then use that to sneakily construct `equiv_equiv_iso`.
-- (In this order the proofs are handled by `obviously`.)

/-- Equivalences (between types in the same universe) are the same as (isomorphic to) isomorphisms
of types. -/
@[simps] def equiv_iso_iso {X Y : Type u} : (X ‚âÉ Y) ‚âÖ (X ‚âÖ Y) :=
{ hom := Œª e, e.to_iso,
  inv := Œª i, i.to_equiv, }

/-- Equivalences (between types in the same universe) are the same as (equivalent to) isomorphisms
of types. -/
def equiv_equiv_iso {X Y : Type u} : (X ‚âÉ Y) ‚âÉ (X ‚âÖ Y) :=
(equiv_iso_iso).to_equiv

@[simp] lemma equiv_equiv_iso_hom {X Y : Type u} (e : X ‚âÉ Y) :
  equiv_equiv_iso e = e.to_iso := rfl

@[simp] lemma equiv_equiv_iso_inv {X Y : Type u} (e : X ‚âÖ Y) :
  equiv_equiv_iso.symm e = e.to_equiv := rfl
