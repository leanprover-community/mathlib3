-- Copyright (c) 2017 Scott Morrison. All rights reserved.
-- Released under Apache 2.0 license as described in the file LICENSE.
-- Authors: Stephen Morgan, Scott Morrison, Johannes H√∂lzl

import category_theory.functor_category
import category_theory.fully_faithful
import data.equiv.basic

namespace category_theory

universes v v' w u u' -- declare the `v`'s first; see `category_theory.category` for an explanation

instance types : large_category (Sort u) :=
{ hom     := Œª a b, (a ‚Üí b),
  id      := Œª a, id,
  comp    := Œª _ _ _ f g, g ‚àò f }

@[simp] lemma types_hom {Œ± Œ≤ : Sort u} : (Œ± ‚ü∂ Œ≤) = (Œ± ‚Üí Œ≤) := rfl
@[simp] lemma types_id (X : Sort u) : ùüô X = id := rfl
@[simp] lemma types_comp {X Y Z : Sort u} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) : f ‚â´ g = g ‚àò f := rfl

namespace functor_to_types
variables {C : Sort u} [ùíû : category.{v} C] (F G H : C ‚•§ Sort w) {X Y Z : C}
include ùíû
variables (œÉ : F ‚ü∂ G) (œÑ : G ‚ü∂ H)

@[simp] lemma map_comp (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (a : F.obj X) : (F.map (f ‚â´ g)) a = (F.map g) ((F.map f) a) :=
by simp

@[simp] lemma map_id (a : F.obj X) : (F.map (ùüô X)) a = a :=
by simp

lemma naturality (f : X ‚ü∂ Y) (x : F.obj X) : œÉ.app Y ((F.map f) x) = (G.map f) (œÉ.app X x) :=
congr_fun (œÉ.naturality f) x

@[simp] lemma comp (x : F.obj X) : (œÉ ‚â´ œÑ).app X x = œÑ.app X (œÉ.app X x) := rfl

variables {D : Sort u'} [ùíü : category.{u'} D] (I J : D ‚•§ C) (œÅ : I ‚ü∂ J) {W : D}

@[simp] lemma hcomp (x : (I ‚ãô F).obj W) : (œÅ ‚ó´ œÉ).app W x = (G.map (œÅ.app W)) (œÉ.app (I.obj W) x) := rfl

end functor_to_types

def ulift_trivial (V : Type u) : ulift.{u} V ‚âÖ V := by tidy

def ulift_functor : Type u ‚•§ Type (max u v) :=
{ obj := Œª X, ulift.{v} X,
  map := Œª X Y f, Œª x : ulift.{v} X, ulift.up (f x.down) }

@[simp] lemma ulift_functor.map {X Y : Type u} (f : X ‚ü∂ Y) (x : ulift.{v} X) :
  ulift_functor.map f x = ulift.up (f x.down) := rfl

instance ulift_functor_faithful : fully_faithful ulift_functor :=
{ preimage := Œª X Y f x, (f (ulift.up x)).down,
  injectivity' := Œª X Y f g p, funext $ Œª x,
    congr_arg ulift.down ((congr_fun p (ulift.up x)) : ((ulift.up (f x)) = (ulift.up (g x)))) }

def hom_of_element {X : Type u} (x : X) : punit ‚ü∂ X := Œª _, x

lemma hom_of_element_eq_iff {X : Type u} (x y : X) :
  hom_of_element x = hom_of_element y ‚Üî x = y :=
‚ü®Œª H, congr_fun H punit.star, by cc‚ü©

lemma mono_iff_injective {X Y : Type u} (f : X ‚ü∂ Y) : mono f ‚Üî function.injective f :=
begin
  split,
  { intros H x x' h,
    resetI,
    rw ‚Üêhom_of_element_eq_iff at ‚ä¢ h,
    exact (cancel_mono f).mp h },
  { refine Œª H, ‚ü®Œª Z g h H‚ÇÇ, _‚ü©,
    ext z,
    replace H‚ÇÇ := congr_fun H‚ÇÇ z,
    exact H H‚ÇÇ }
end

lemma epi_iff_surjective {X Y : Type u} (f : X ‚ü∂ Y) : epi f ‚Üî function.surjective f :=
begin
  split,
  { intros H,
    let g : Y ‚ü∂ ulift Prop := Œª y, ‚ü®true‚ü©,
    let h : Y ‚ü∂ ulift Prop := Œª y, ‚ü®‚àÉ x, f x = y‚ü©,
    suffices : f ‚â´ g = f ‚â´ h,
    { resetI,
      rw cancel_epi at this,
      intro y,
      replace this := congr_fun this y,
      replace this : true = ‚àÉ x, f x = y := congr_arg ulift.down this,
      rw ‚Üêthis,
      trivial },
    ext x,
    change true ‚Üî ‚àÉ x', f x' = f x,
    rw true_iff,
    exact ‚ü®x, rfl‚ü© },
  { intro H,
    constructor,
    intros Z g h H‚ÇÇ,
    apply funext,
    rw ‚Üêforall_iff_forall_surj H,
    intro x,
    exact (congr_fun H‚ÇÇ x : _) }
end

end category_theory

-- Isomorphisms in Type and equivalences.

namespace equiv

universe u

variables {X Y : Sort u}

def to_iso (e : X ‚âÉ Y) : X ‚âÖ Y :=
{ hom := e.to_fun,
  inv := e.inv_fun,
  hom_inv_id' := funext e.left_inv,
  inv_hom_id' := funext e.right_inv }

@[simp] lemma to_iso_hom {e : X ‚âÉ Y} : e.to_iso.hom = e := rfl
@[simp] lemma to_iso_inv {e : X ‚âÉ Y} : e.to_iso.inv = e.symm := rfl

end equiv

namespace category_theory.iso

universe u

variables {X Y : Sort u}

def to_equiv (i : X ‚âÖ Y) : X ‚âÉ Y :=
{ to_fun := i.hom,
  inv_fun := i.inv,
  left_inv := Œª x, congr_fun i.hom_inv_id x,
  right_inv := Œª y, congr_fun i.inv_hom_id y }

@[simp] lemma to_equiv_fun (i : X ‚âÖ Y) : (i.to_equiv : X ‚Üí Y) = i.hom := rfl
@[simp] lemma to_equiv_symm_fun (i : X ‚âÖ Y) : (i.to_equiv.symm : Y ‚Üí X) = i.inv := rfl

end category_theory.iso
