/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import category_theory.limits.shapes.terminal

/-!

# `with_init` and `with_term`

Given a category `C`, this file constructs two objects:
1. `with_term C`, the category built from `C` by formally adjoining a terminal object.
2. `with_init C`, the category built from `C` by formally adjoining an initial object.

The terminal resp. initial object is `with_term.star` resp. `with_init.star`, and 
the proofs that these are terminal resp. initial are in `with_term.star_terminal`
and `with_init.star_initial`.

The inclusion from `C` intro `with_term C` resp. `with_init C` is denoted
`with_term.incl` resp. `with_init.incl`.

The relevant constructions needed for the universal properties of these constructions are:
1. `lift`, which lifts `F : C â¥¤ D` to a functor from `with_term C` resp. `with_init C` in
  the case where an object `Z : D` is provided satisfying some additional conditions.
2. `incl_lift` shows that the composition of `lift` with `incl` is isomorphic to the
  functor which was lifted.
3. `lift_unique` provides the uniqueness property of `lift`.

In addition to this, we provide `with_term.map` and `with_init.map` providing the functoriality
of these constructions with respect to functors on the base categories.

-/

namespace category_theory

universes v u

variables (C : Type u) [category.{v} C]

/-- Formally adjoin a terminal object to a category. -/
@[derive inhabited]
inductive with_terminal : Type u
| of : C â†’ with_terminal
| star : with_terminal

/-- Formally adjoin an initial object to a category. -/
@[derive inhabited]
inductive with_initial : Type u
| of : C â†’ with_initial
| star : with_initial

namespace with_terminal

local attribute [tidy] tactic.case_bash
variable {C}

/-- Morphisms for `with_term C`. -/
@[simp, nolint has_inhabited_instance]
def hom : with_terminal C â†’ with_terminal C â†’ Type v
| (of X) (of Y) := X âŸ¶ Y
| star (of X) := pempty
| _ star := punit

/-- Identity morphisms for `with_term C`. -/
@[simp]
def id : Î  (X : with_terminal C), hom X X
| (of X) := ğŸ™ _
| star := punit.star

/-- Composition of morphisms for `with_term C`. -/
@[simp]
def comp : Î  {X Y Z : with_terminal C}, hom X Y â†’ hom Y Z â†’ hom X Z
| (of X) (of Y) (of Z) := Î» f g, f â‰« g
| (of X) _ star := Î» f g, punit.star
| star (of X) _ := Î» f g, pempty.elim f
| _ star (of Y) := Î» f g, pempty.elim g
| star star star := Î» _ _, punit.star

instance : category.{v} (with_terminal C) :=
{ hom := Î» X Y, hom X Y,
  id := Î» X, id _,
  comp := Î» X Y Z f g, comp f g }

/-- The inclusion from `C` into `with_term C`. -/
def incl : C â¥¤ (with_terminal C) :=
{ obj := of,
  map := Î» X Y f, f }

instance : full (incl : C â¥¤ _) :=
{ preimage := Î» X Y f, f }

instance : faithful (incl : C â¥¤ _) := {}

/-- Map `with_term` with respect to a functor `F : C â¥¤ D`. -/
def map {D : Type*} [category D] (F : C â¥¤ D) : with_terminal C â¥¤ with_terminal D :=
{ obj := Î» X,
    match X with
    | of x := of $ F.obj x
    | star := star
    end,
  map := Î» X Y f,
    match X, Y, f with
    | of x, of y, f := F.map f
    | of x, star, punit.star := punit.star
    | star, star, punit.star := punit.star
    end }

instance {X : with_terminal C} : unique (X âŸ¶ star) :=
{ default :=
    match X with
    | of x := punit.star
    | star := punit.star
    end,
  uniq := by tidy }

/-- `with_term.star` is terminal. -/
def star_terminal : limits.is_terminal (star : with_terminal C) :=
limits.is_terminal.of_unique _

/-- Lift a functor `F : C â¥¤ D` to `with_term C â¥¤ D`, given a terminal object of `D`. -/
@[simps]
def lift {D : Type*} [category D] {Z : D} (F : C â¥¤ D) (M : Î  (x : C), F.obj x âŸ¶ Z)
  (hM : âˆ€ (x y : C) (f : x âŸ¶ y), F.map f â‰« M y = M x) :
  (with_terminal C) â¥¤ D :=
{ obj := Î» X,
    match X with
    | of x := F.obj x
    | star := Z
    end,
  map := Î» X Y f,
    match X, Y, f with
    | of x, of y, f := F.map f
    | of x, star, punit.star := M x
    | star, star, punit.star := ğŸ™ Z
    end,
  map_id' := by {rintros (X|X), tidy},
  map_comp' := by {rintros (X|X) (Y|Y) (Z|Z) f g, tidy}, }

/-- The isomorphism between `incl â‹™ lift F _` with `F`. -/
@[simps]
def incl_lift {D : Type*} [category D] {Z : D} (F : C â¥¤ D) (M : Î  (x : C), F.obj x âŸ¶ Z)
  (hM : âˆ€ (x y : C) (f : x âŸ¶ y), F.map f â‰« M y = M x) :
  incl â‹™ lift F M hM â‰… F :=
{ hom := { app := Î» X, ğŸ™ _ },
  inv := { app := Î» X, ğŸ™ _ } }

/-- The isomorphism between `(lift F _).obj with_term.star` with `Z`. -/
@[simps]
def lift_star {D : Type*} [category D] {Z : D} (F : C â¥¤ D) (M : Î  (x : C), F.obj x âŸ¶ Z)
  (hM : âˆ€ (x y : C) (f : x âŸ¶ y), F.map f â‰« M y = M x) :
  (lift F M hM).obj star â‰… Z := eq_to_iso rfl

lemma lift_map_lift_star {D : Type*} [category D] {Z : D} (F : C â¥¤ D) (M : Î  (x : C), F.obj x âŸ¶ Z)
  (hM : âˆ€ (x y : C) (f : x âŸ¶ y), F.map f â‰« M y = M x) (x : C) :
  (lift F M hM).map (star_terminal.from (incl.obj x)) â‰« (lift_star F M hM).hom =
  (incl_lift F M hM).hom.app x â‰« M x :=
begin
  erw [category.id_comp, category.comp_id],
  refl,
end

/-- The uniqueness of `lift`. -/
@[simps]
def lift_unique {D : Type*} [category D] {Z : D} (F : C â¥¤ D)
  (M : Î  (x : C), F.obj x âŸ¶ Z) (hM : âˆ€ (x y : C) (f : x âŸ¶ y), F.map f â‰« M y = M x)
  (G : with_terminal C â¥¤ D) (h : incl â‹™ G â‰… F) (hG : G.obj star â‰… Z)
  (hh : âˆ€ x : C, G.map (star_terminal.from (incl.obj x)) â‰« hG.hom = h.hom.app x â‰« M x) :
  G â‰… lift F M hM :=
nat_iso.of_components (Î» X,
  match X with
  | of x := h.app x
  | star := hG
  end)
begin
  rintro (X|X) (Y|Y) f,
  { apply h.hom.naturality },
  { cases f, exact hh _ },
  { cases f, },
  { cases f,
    change G.map (ğŸ™ _) â‰« hG.hom = hG.hom â‰« ğŸ™ _,
    simp }
end

end with_terminal

namespace with_initial

local attribute [tidy] tactic.case_bash
variable {C}

/-- Morphisms for `with_init C`. -/
@[simp, nolint has_inhabited_instance]
def hom : with_initial C â†’ with_initial C â†’ Type v
| (of X) (of Y) := X âŸ¶ Y
| (of X) _ := pempty
| star _ := punit

/-- Identity morphisms for `with_init C`. -/
@[simp]
def id : Î  (X : with_initial C), hom X X
| (of X) := ğŸ™ _
| star := punit.star

/-- Composition of morphisms for `with_init C`. -/
@[simp]
def comp : Î  {X Y Z : with_initial C}, hom X Y â†’ hom Y Z â†’ hom X Z
| (of X) (of Y) (of Z) := Î» f g, f â‰« g
| star _ (of X) := Î» f g, punit.star
| _ (of X) star := Î» f g, pempty.elim g
| (of Y) star _ := Î» f g, pempty.elim f
| star star star := Î» _ _, punit.star

instance : category.{v} (with_initial C) :=
{ hom := Î» X Y, hom X Y,
  id := Î» X, id _,
  comp := Î» X Y Z f g, comp f g }

/-- The inclusion of `C` into `with_init C`. -/
def incl : C â¥¤ (with_initial C) :=
{ obj := of,
  map := Î» X Y f, f }

instance : full (incl : C â¥¤ _) :=
{ preimage := Î» X Y f, f }

instance : faithful (incl : C â¥¤ _) := {}

/-- Map `with_init` with respect to a functor `F : C â¥¤ D`. -/
def map {D : Type*} [category D] (F : C â¥¤ D) : with_initial C â¥¤ with_initial D :=
{ obj := Î» X,
    match X with
    | of x := of $ F.obj x
    | star := star
    end,
  map := Î» X Y f,
    match X, Y, f with
    | of x, of y, f := F.map f
    | star, of x, punit.star := punit.star
    | star, star, punit.star := punit.star
    end }

instance {X : with_initial C} : unique (star âŸ¶ X) :=
{ default :=
    match X with
    | of x := punit.star
    | star := punit.star
    end,
  uniq := by tidy }

/-- `with_init.star` is initial. -/
def star_initial : limits.is_initial (star : with_initial C) :=
limits.is_initial.of_unique _

/-- Lift a functor `F : C â¥¤ D` to `with_init C â¥¤ D`, given an initial object of `D`. -/
@[simps]
def lift {D : Type*} [category D] {Z : D} (F : C â¥¤ D) (M : Î  (x : C), Z âŸ¶ F.obj x)
  (hM : âˆ€ (x y : C) (f : x âŸ¶ y), M x â‰« F.map f = M y) :
  (with_initial C) â¥¤ D :=
{ obj := Î» X,
    match X with
    | of x := F.obj x
    | star := Z
    end,
  map := Î» X Y f,
    match X, Y, f with
    | of x, of y, f := F.map f
    | star, of x, punit.star := M _
    | star, star, punit.star := ğŸ™ _
    end,
  map_id' := by {rintros (X|X), tidy},
  map_comp' := by {rintros (X|X) (Y|Y) (Z|Z) f g, tidy} }

/-- The isomorphism between `incl â‹™ lift _ F` with `F`. -/
@[simps]
def incl_lift {D : Type*} [category D] {Z : D} (F : C â¥¤ D)
  (M : Î  (x : C), Z âŸ¶ F.obj x) (hM : âˆ€ (x y : C) (f : x âŸ¶ y), M x â‰« F.map f = M y) :
  incl â‹™ lift F M hM â‰… F :=
{ hom := { app := Î» X, ğŸ™ _ },
  inv := { app := Î» X, ğŸ™ _ } }

/-- The isomorphism between `(lift F _).obj with_term.star` with `Z`. -/
@[simps]
def lift_star {D : Type*} [category D] {Z : D} (F : C â¥¤ D)
  (M : Î  (x : C), Z âŸ¶ F.obj x) (hM : âˆ€ (x y : C) (f : x âŸ¶ y), M x â‰« F.map f = M y) :
  (lift F M hM).obj star â‰… Z := eq_to_iso rfl

lemma lift_star_lift_map {D : Type*} [category D] {Z : D} (F : C â¥¤ D)
  (M : Î  (x : C), Z âŸ¶ F.obj x) (hM : âˆ€ (x y : C) (f : x âŸ¶ y), M x â‰« F.map f = M y) (x : C) :
  (lift_star F M hM).hom â‰« (lift F M hM).map (star_initial.to (incl.obj x)) =
  M x â‰« (incl_lift F M hM).hom.app x :=
begin
  erw [category.id_comp, category.comp_id],
  refl,
end

/-- The uniqueness of `lift _ F`. -/
@[simps]
def lift_unique {D : Type*} [category D] {Z : D} (F : C â¥¤ D)
  (M : Î  (x : C), Z âŸ¶ F.obj x) (hM : âˆ€ (x y : C) (f : x âŸ¶ y), M x â‰« F.map f = M y)
  (G : with_initial C â¥¤ D) (h : incl â‹™ G â‰… F) (hG : G.obj star â‰… Z)
  (hh : âˆ€ x : C, hG.symm.hom â‰« G.map (star_initial.to (incl.obj x)) = M x â‰« h.symm.hom.app x) :
  G â‰… lift F M hM :=
nat_iso.of_components
(Î» X,
  match X with
  | of x := h.app x
  | star := hG
  end)
begin
  rintro (X|X) (Y|Y) f,
  { apply h.hom.naturality },
  { cases f, },
  { cases f,
    change G.map _ â‰« h.hom.app _ = hG.hom â‰« _,
    symmetry,
    erw [â† iso.eq_inv_comp, â† category.assoc, hh],
    simpa },
  { cases f,
    change G.map (ğŸ™ _) â‰« hG.hom = hG.hom â‰« ğŸ™ _,
    simp }
end

end with_initial

end category_theory
