/-
Copyright (c) 2022 Wrenna Robson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Wrenna Robson
-/

import analysis.normed_space.basic

/-!
# Hamming spaces

The Hamming metric counts the number of places two members of a (finite) Pi type
differ. The Hamming norm is the same as the Hamming metric over additive groups, and
counts the number of places a member of a (finite) Pi type differs from zero.

This is a useful notion in various applications, but in particular it is relevant
in coding theory, in which it is fundamental for defining the minimum distance of a
code.

In this file we define `hamm Î²`, the type synonym of a Pi type with the Hamming
distance `hamm_dist` and weight `hamm_wt` attached, and the various instances that arise
from the properties of these definitions.

-/

/-
We define `hamm_dist` and `hamm_wt` over Pi types, and will later attach them to our type
synonym.
-/
section hamm_dist_wt

open fintype function

variables {Î± Î¹ : Type*} {Î² : Î¹ â†’ Type*} [fintype Î¹] [Î  i, decidable_eq (Î² i)]
variables {Î³ : Î¹ â†’ Type*} [Î  i, decidable_eq (Î³ i)]

/-- The Hamming distance function to the naturals. -/
def hamm_dist (x y : Î  i, Î² i) := card {i // x i â‰  y i}

lemma hamm_dist_nonneg {x y : Î  i, Î² i} : 0 â‰¤ hamm_dist x y := zero_le _

lemma hamm_dist_le_card_fintype {x y : Î  i, Î² i} : hamm_dist x y â‰¤ card Î¹ := card_subtype_le _

lemma hamm_dist_comp_le_hamm_dist (f : Î  i, Î³ i â†’ Î² i) {x y : Î  i, Î³ i} :
  hamm_dist (Î» i, f i (x i)) (Î» i, f i (y i)) â‰¤ hamm_dist x y :=
card_subtype_mono _ _ $ Î» x H1 H2, H1 $ congr_arg (f x) H2

lemma hamm_dist_comp (f : Î  i, Î³ i â†’ Î² i) {x y : Î  i, Î³ i} (hf : Î  i, injective (f i))
  : hamm_dist (Î» i, f i (x i)) (Î» i, f i (y i)) = hamm_dist x y :=
le_antisymm (hamm_dist_comp_le_hamm_dist  _) (card_subtype_mono _ _ (Î» i H1 H2, H1 (hf i H2)))

lemma hamm_dist_smul_le_hamm_dist [Î  i, has_scalar Î± (Î² i)] {k : Î±} {x y : Î  i, Î² i} :
  hamm_dist (k â€¢ x) (k â€¢ y) â‰¤ hamm_dist x y :=
hamm_dist_comp_le_hamm_dist $ Î» i, (â€¢) k

lemma hamm_dist_smul [Î  i, has_scalar Î± (Î² i)] {k : Î±} {x y : Î  i, Î² i}
  (hk : Î  i, is_smul_regular (Î² i) k) : hamm_dist (k â€¢ x) (k â€¢ y) = hamm_dist x y :=
hamm_dist_comp (Î» i (c : Î² i), k â€¢ c) hk

@[simp] lemma hamm_dist_eq_zero {x y : Î  i, Î² i} : hamm_dist x y = 0 â†” x = y :=
by {  rw [function.funext_iff, hamm_dist, card_eq_zero_iff],
      exact âŸ¨ Î» h i, imp_of_not_imp_not _ _ (Î» H, h.elim' âŸ¨i, HâŸ©) h,
              Î» h, subtype.is_empty_of_false (Î» i H, H (h _))âŸ© }

@[simp] lemma hamm_dist_self (x : Î  i, Î² i) : hamm_dist x x = 0 := hamm_dist_eq_zero.mpr rfl

lemma hamm_dist_comm (x y : Î  i, Î² i) : hamm_dist x y = hamm_dist y x :=
by simp_rw [hamm_dist, ne_comm]

lemma hamm_dist_triangle (x y z : Î  i, Î² i) :
  hamm_dist x z â‰¤ hamm_dist x y + hamm_dist y z :=
begin
  simp_rw hamm_dist,
  refine le_trans (card_subtype_mono _ _ (Î» _ h, _)) (card_subtype_or _ _),
  by_contra' H, exact h (eq.trans H.1 H.2)
end

lemma eq_of_hamm_dist_eq_zero (x y : Î  i, Î² i) : hamm_dist x y = 0 â†’ x = y :=
hamm_dist_eq_zero.mp

lemma hamm_dist_ne_zero {x y : Î  i, Î² i} : hamm_dist x y â‰  0 â†” x â‰  y :=
not_iff_not.mpr hamm_dist_eq_zero

lemma hamm_dist_pos {x y : Î  i, Î² i} : 0 < hamm_dist x y â†” x â‰  y :=
by rw [â†hamm_dist_ne_zero, iff_not_comm, not_lt, nat.le_zero_iff]

@[simp] lemma hamm_dist_lt_one {x y : Î  i, Î² i} : hamm_dist x y < 1 â†” x = y :=
by {rw nat.lt_one_iff, exact hamm_dist_eq_zero}

lemma hamm_dist_eq_zero_iff_forall_eq {x y : Î  i, Î² i} :
  hamm_dist x y = 0 â†” âˆ€ i, x i = y i :=
by rw [hamm_dist_eq_zero, function.funext_iff]

lemma hamm_dist_ne_zero_iff_exists_ne {x y : Î  i, Î² i} :
  hamm_dist x y â‰  0 â†” âˆƒ i, x i â‰  y i :=
by rw [hamm_dist_ne_zero, function.ne_iff]

section has_zero

variables [Î  i, has_zero (Î² i)] [Î  i, has_zero (Î³ i)]

/-- The Hamming weight function to the naturals. -/

def hamm_wt (x : Î  i, Î² i) : â„• := hamm_dist x 0

lemma hamm_wt_eq (x : Î  i, Î² i) : hamm_wt x = card {i // x i â‰  0} := rfl

lemma hamm_wt_eq_hamm_dist_zero (x : Î  i, Î² i) : hamm_wt x = hamm_dist x 0 := rfl

lemma hamm_wt_nonneg {x : Î  i, Î² i} : 0 â‰¤ hamm_wt x := hamm_dist_nonneg

lemma hamm_wt_le_card_fintype {x : Î  i, Î² i} : hamm_wt x â‰¤ card Î¹ := hamm_dist_le_card_fintype

lemma hamm_wt_comp_le_hamm_wt (f : Î  i, Î³ i â†’ Î² i) {x : Î  i, Î³ i} (hf : Î  i, f i 0 = 0) :
  hamm_wt (Î» i, f i (x i)) â‰¤ hamm_wt x :=
by {  simp_rw hamm_wt_eq_hamm_dist_zero, convert hamm_dist_comp_le_hamm_dist f,
      simp_rw [pi.zero_apply, hf], refl }

lemma hamm_wt_comp (f : Î  i, Î³ i â†’ Î² i) {x : Î  i, Î³ i} (hfâ‚ : Î  i, injective (f i))
  (hfâ‚‚ : Î  i, f i 0 = 0) : hamm_wt (Î» i, f i (x i)) = hamm_wt x :=
by {  simp_rw hamm_wt_eq_hamm_dist_zero, convert hamm_dist_comp f hfâ‚,
      simp_rw [pi.zero_apply, hfâ‚‚], refl }

lemma hamm_wt_smul_le_hamm_wt [has_zero Î±] [Î  i, smul_with_zero Î± (Î² i)] {k : Î±}
  {x : Î  i, Î² i} : hamm_wt (k â€¢ x) â‰¤ hamm_wt x :=
hamm_wt_comp_le_hamm_wt (Î» i (c : Î² i), k â€¢ c) (Î» i, by simp_rw smul_zero')

lemma hamm_wt_smul [has_zero Î±] [Î  i, smul_with_zero Î± (Î² i)] {k : Î±}
  (hk : âˆ€ i, is_smul_regular (Î² i) k) (x : Î  i, Î² i) : hamm_wt (k â€¢ x) = hamm_wt x :=
hamm_wt_comp (Î» i (c : Î² i), k â€¢ c) hk (Î» i, by simp_rw smul_zero')

@[simp] lemma hamm_wt_eq_zero {x : Î  i, Î² i} : hamm_wt x = 0 â†” x = 0 := hamm_dist_eq_zero

@[simp] lemma hamm_wt_zero : hamm_wt (0 : Î  i, Î² i) = 0 := hamm_dist_self _

lemma hamm_wt_ne_zero {x : Î  i, Î² i} : hamm_wt x â‰  0 â†” x â‰  0 := hamm_dist_ne_zero

lemma hamm_wt_pos {x : Î  i, Î² i} : 0 < hamm_wt x â†” x â‰  0 := hamm_dist_pos

@[simp] lemma hamm_wt_lt_one {x : Î  i, Î² i} : hamm_wt x < 1 â†” x = 0 := hamm_dist_lt_one

lemma hamm_wt_zero_iff_forall_zero {x : Î  i, Î² i} : hamm_wt x = 0 â†” âˆ€ i, x i = 0 :=
hamm_dist_eq_zero_iff_forall_eq

lemma hamm_wt_pos_iff_exists_nz {x : Î  i, Î² i} : hamm_wt x â‰  0 â†” âˆƒ i, x i â‰  0 :=
hamm_dist_ne_zero_iff_exists_ne

end has_zero

lemma hamm_dist_eq_hamm_wt_sub [Î  i, add_group (Î² i)] (x y : Î  i, Î² i) :
  hamm_dist x y = hamm_wt (x - y) :=
by simp_rw [hamm_wt_eq, hamm_dist, pi.sub_apply, sub_ne_zero]

end hamm_dist_wt

/--
Type synonym for a Pi type which we equip with the Hamming metric, adding relevant
instances as needed.
-/
def hamm {Î¹ : Type*} (Î² : Î¹ â†’ Type*) : Type* := Î  i, Î² i

namespace hamm

section

local notation `ð“—[` K`,` n`]` := hamm (Î» _ : fin n, K)

variables {Î± Î¹ : Type*} {Î² : Î¹ â†’ Type*}

instance [Î  i, inhabited (Î² i)] : inhabited (hamm Î²) := âŸ¨Î» i, defaultâŸ©
instance [decidable_eq Î¹] [fintype Î¹] [Î  i, fintype (Î² i)] : fintype (hamm Î²) := pi.fintype
instance [inhabited Î¹] [inst : âˆ€ i, nonempty (Î² i)] [nontrivial (Î² default)] :
  nontrivial (hamm Î²) := pi.nontrivial

/-- `to_hamm` is the identity function to the `hamm` of a type.  -/
@[pattern] def to_hamm : (Î  i, Î² i) â‰ƒ hamm Î² := equiv.refl _

/-- `of_hamm` is the identity function from the `hamm` of a type.  -/
@[pattern] def of_hamm : hamm Î² â‰ƒ Î  i, Î² i := equiv.refl _

@[simp] lemma to_hamm_symm_eq                 : (@to_hamm _ Î²).symm = of_hamm := rfl
@[simp] lemma of_hamm_symm_eq                 : (@of_hamm _ Î²).symm = to_hamm := rfl
@[simp] lemma to_hamm_of_hamm (x : hamm Î²)    : to_hamm (of_hamm x) = x := rfl
@[simp] lemma of_hamm_to_hamm (x : Î  i, Î² i)  : of_hamm (to_hamm x) = x := rfl
@[simp] lemma to_hamm_inj {x y : Î  i, Î² i}    : to_hamm x = to_hamm y â†” x = y := iff.rfl
@[simp] lemma of_hamm_inj {x y : hamm Î²}      : of_hamm x = of_hamm y â†” x = y := iff.rfl

instance [Î  i, has_zero (Î² i)] : has_zero (hamm Î²) := pi.has_zero
instance [Î  i, has_sub (Î² i)] : has_sub (hamm Î²) := pi.has_sub
instance [Î  i, has_scalar Î± (Î² i)] : has_scalar Î± (hamm Î²) := pi.has_scalar
instance [has_zero Î±] [Î  i, has_zero (Î² i)] [Î  i, smul_with_zero Î± (Î² i)] :
  smul_with_zero Î± (hamm Î²) := pi.smul_with_zero _
instance [Î  i, add_monoid (Î² i)] : add_monoid (hamm Î²) := pi.add_monoid
instance [Î  i, add_comm_monoid (Î² i)] : add_comm_monoid (hamm Î²) := pi.add_comm_monoid
instance [Î  i, add_comm_group (Î² i)] : add_comm_group (hamm Î²) := pi.add_comm_group
instance (Î±) [semiring Î±] (Î²: Î¹ â†’ Type*) [Î  i, add_comm_monoid (Î² i)]
  [Î  i, module Î± (Î² i)] : module Î± (hamm Î²) := pi.module _ _ _

end

section

variables {Î± Î¹ : Type*} {Î² : Î¹ â†’ Type*} [fintype Î¹] [Î  i, decidable_eq (Î² i)]

instance : has_dist (hamm Î²) := âŸ¨Î» x y, hamm_dist (of_hamm x) (of_hamm y)âŸ©

@[simp, push_cast] lemma dist_eq_hamm_dist (x y : hamm Î²) : dist x y =
  hamm_dist (of_hamm x) (of_hamm y) := rfl

instance : pseudo_metric_space (hamm Î²) :=
{ dist_self           :=  by {push_cast, exact_mod_cast hamm_dist_self},
  dist_comm           :=  by {push_cast, exact_mod_cast hamm_dist_comm},
  dist_triangle       :=  by {push_cast, exact_mod_cast hamm_dist_triangle},
  to_uniform_space    :=  âŠ¥,
  uniformity_dist     :=  uniformity_dist_of_mem_uniformity _ _ $ Î» s,
                          by {push_cast, split,
                          { refine Î» hs, âŸ¨1, zero_lt_one, Î» _ _ hab, _âŸ©,
                            rw_mod_cast [hamm_dist_lt_one] at hab,
                            rw [of_hamm_inj, â† mem_id_rel] at hab, exact hs hab },
                          { rintros âŸ¨_, hÎµ, hsâŸ© âŸ¨_, _âŸ© hab, rw mem_id_rel at hab, rw hab,
                            refine hs (lt_of_eq_of_lt _ hÎµ), exact_mod_cast hamm_dist_self _ }},
  to_bornology        :=  âŸ¨âŠ¥, bot_leâŸ©,
  cobounded_sets      :=  by  {ext, push_cast,
                              refine iff_of_true  (filter.mem_sets.mpr filter.mem_bot)
                                                  âŸ¨fintype.card Î¹, Î» _ _ _ _, _âŸ©,
                              exact_mod_cast hamm_dist_le_card_fintype},
  ..hamm.has_dist }

instance : metric_space (hamm Î²) :=
{ eq_of_dist_eq_zero  := by {push_cast, exact_mod_cast eq_of_hamm_dist_eq_zero},
  ..hamm.pseudo_metric_space }

instance [Î  i, has_zero (Î² i)] : has_norm (hamm Î²) := âŸ¨Î» x, hamm_wt (of_hamm x)âŸ©

@[simp, push_cast] lemma norm_eq_hamm_wt [Î  i, has_zero (Î² i)] (x : hamm Î²) :
  âˆ¥xâˆ¥ = hamm_wt (of_hamm x) := rfl

instance [Î  i, add_comm_group (Î² i)] : semi_normed_group (hamm Î²) :=
{ dist_eq := by {push_cast, exact_mod_cast hamm_dist_eq_hamm_wt_sub}, ..pi.add_comm_group }

instance [Î  i, add_comm_group (Î² i)] : normed_group (hamm Î²) := { ..hamm.semi_normed_group }

end

end hamm
