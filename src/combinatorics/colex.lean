/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import algebra.geom_sum
import data.finset.slice

/-!
# Colex

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We define the colex ordering for finite sets, and give a couple of important
lemmas and properties relating to it.

The colex ordering likes to avoid large values - it can be thought of on
`finset â„•` as the "binary" ordering. That is, order A based on
`âˆ‘_{i âˆˆ A} 2^i`.
It's defined here in a slightly more general way, requiring only `has_lt Î±` in
the definition of colex on `finset Î±`. In the context of the Kruskal-Katona
theorem, we are interested in particular on how colex behaves for sets of a
fixed size. If the size is 3, colex on â„• starts
123, 124, 134, 234, 125, 135, 235, 145, 245, 345, ...

## Main statements

* Colex order properties - linearity, decidability and so on.
* `finset.colex.forall_lt_mono`: if `s < t` in colex, and everything in `t` is `< a`, then
  everything in `s` is `< a`. This confirms the idea that an enumeration under colex will exhaust
  all sets using elements `< a` before allowing `a` to be included.
* `finset.to_colex_image_lt_to_colex_image`: Strictly monotone functions preserve colex.
* `finset.sum_two_pow_le_iff_colex_le`: colex for Î± = â„• is the same as binary
  (this also proves binary expansions are unique)

## See also

Related files are:
* `data.list.lex`: Lexicographic order on lists.
* `data.pi.lex`: Lexicographic order on `Î â‚— i, Î± i`.
* `data.psigma.order`: Lexicographic order on `Î£' i, Î± i`.
* `data.sigma.order`: Lexicographic order on `Î£ i, Î± i`.
* `data.prod.lex`: Lexicographic order on `Î± Ã— Î²`.

## TODO

* Generalise `colex.init_seg` so that it applies to `â„•`.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf

## Tags

colex, colexicographic, binary
-/

namespace finset
variables {Î± : Type*} [decidable_eq Î±] {s t : finset Î±}

@[simp] lemma symm_diff_eq_empty : s âˆ† t = âˆ… â†” s = t := symm_diff_eq_bot

@[simp] lemma symm_diff_nonempty : (s âˆ† t).nonempty â†” s â‰  t :=
nonempty_iff_ne_empty.trans symm_diff_eq_empty.not

end finset

section
variables {Î± Î² : Type*} [decidable_eq Î²] {f : Î± â†’ Î²}

open finset function

lemma function.injective.finset_image (hf : injective f) : injective (image f) :=
Î» s t hst, coe_injective $ hf.image_injective $ by simpa using congr_arg (coe : _ â†’ set Î²) hst

end

section
variables {k : â„•} {s : finset â„•}

open finset
open_locale big_operators

/-- If everything in `A` is less than `k`, we can bound the sum of powers. -/
lemma nat.sum_two_pow_lt (hâ‚ : âˆ€ a âˆˆ s, a < k) : âˆ‘ a in s, 2 ^ a < 2^k :=
(sum_le_sum_of_subset $ Î» t, mem_range.2 âˆ˜ hâ‚ _).trans_lt $
  by simpa [one_add_one_eq_two] using (geom_sum_mul_add 1 k).le

end

variables {Î± Î² : Type*}

open finset
open_locale big_operators

namespace finset

/-- Type synonym of `finset Î±` equipped with the colexicographic order rather than the inclusion
order. -/
@[derive inhabited] def colex (Î±) := finset Î±

/-- `to_colex` is the "identity" function between `finset Î±` and `finset.colex Î±`. -/
def to_colex : finset Î± â‰ƒ colex Î± := equiv.refl _
/-- `of_colex` is the "identity" function between `finset.colex Î±` and `finset Î±`. -/
def of_colex : colex Î± â‰ƒ finset Î± := equiv.refl _

@[simp] lemma to_colex_symm_eq : (@to_colex Î±).symm = of_colex := rfl
@[simp] lemma of_colex_symm_eq : (@of_colex Î±).symm = to_colex := rfl
@[simp] lemma to_colex_of_colex (s : colex Î±) : to_colex (of_colex s) = s := rfl
@[simp] lemma of_colex_to_colex (s : finset Î±) : of_colex (to_colex s) = s := rfl
@[simp] lemma to_colex_inj {s t : finset Î±} : to_colex s = to_colex t â†” s = t := iff.rfl
@[simp] lemma of_colex_inj {s t : colex Î±} : of_colex s = of_colex t â†” s = t := iff.rfl
lemma to_colex_ne_to_colex {s t : finset Î±} : to_colex s â‰  to_colex t â†” s â‰  t := iff.rfl
lemma of_colex_ne_of_colex {s t : colex Î±} : of_colex s â‰  of_colex t â†” s â‰  t := iff.rfl

/-- Recursor for `colex Î±`. -/
@[elab_as_eliminator]
def colex.rec {C : colex Î± â†’ Sort*} (h : Î  s, C (to_colex s)) : Î  s, C s := h

namespace colex

section has_lt
variables [has_lt Î±] {s t : finset Î±}

/--
`A` is less than `B` in the colex ordering if the largest thing that's not in both sets is in B.
In other words, `max (A âˆ† B) âˆˆ B` (if the maximum exists).
-/
instance : has_lt (colex Î±) :=
âŸ¨Î» A B, âˆƒ k, (âˆ€ â¦ƒxâ¦„, k < x â†’ (x âˆˆ of_colex A â†” x âˆˆ of_colex B)) âˆ§ k âˆ‰ of_colex A âˆ§ k âˆˆ of_colex BâŸ©

/-- We can define (â‰¤) in the obvious way. -/
instance : has_le (colex Î±) := âŸ¨Î» A B, A = B âˆ¨ A < BâŸ©

lemma lt_def : s.to_colex < t.to_colex â†” âˆƒ k, (âˆ€ â¦ƒxâ¦„, k < x â†’ (x âˆˆ s â†” x âˆˆ t)) âˆ§ k âˆ‰ s âˆ§ k âˆˆ t :=
iff.rfl

lemma le_def :
  s.to_colex â‰¤ t.to_colex â†” s = t âˆ¨ âˆƒ k, (âˆ€ â¦ƒxâ¦„, k < x â†’ (x âˆˆ s â†” x âˆˆ t)) âˆ§ k âˆ‰ s âˆ§ k âˆˆ t :=
iff.rfl

instance : is_irrefl (colex Î±) (<) := âŸ¨by simp [(<)]âŸ©

/-- Colex doesn't care if you remove the other set -/
@[simp] lemma sdiff_lt_sdiff_iff_lt [decidable_eq Î±] (s t : finset Î±) :
  (s \ t).to_colex < (t \ s).to_colex â†” s.to_colex < t.to_colex :=
begin
  rw [lt_def, lt_def],
  refine exists_congr (Î» k, _),
  simp only [mem_sdiff, not_and, not_not],
  split,
  { rintro âŸ¨z, kAB, kB, kAâŸ©,
    refine âŸ¨Î» x hx, _, kA, kBâŸ©,
    specialize z hx,
    tauto },
  { rintro âŸ¨z, kA, kBâŸ©,
    refine âŸ¨Î» x hx, _, Î» _, kB, kB, kAâŸ©,
    rw z hx }
end

end has_lt

section linear_order
variables [linear_order Î±] [linear_order Î²] {f : Î± â†’ Î²} {ğ’œ ğ’œâ‚ ğ’œâ‚‚ : finset (finset Î±)}
  {s t : finset Î±} {a b : Î±} {r : â„•}

instance : is_strict_total_order (colex Î±) (<) :=
{ irrefl := irrefl_of (<),
  trans := Î» s t u, begin
    rintro âŸ¨kâ‚, kâ‚z, notinA, inBâŸ© âŸ¨kâ‚‚, kâ‚‚z, notinB, inCâŸ©,
    cases (ne_of_mem_of_not_mem inB notinB).lt_or_lt,
    { refine âŸ¨kâ‚‚, Î» x hx, _, by rwa kâ‚z h, inCâŸ©,
      rw â† kâ‚‚z hx,
      exact kâ‚z (trans h hx) },
    { refine âŸ¨kâ‚, Î» x hx, _, notinA, by rwa â† kâ‚‚z hâŸ©,
      rw kâ‚z hx,
      exact kâ‚‚z (trans h hx) }
  end,
  trichotomous := Î» s t, begin
    classical,
    obtain rfl | hts := eq_or_ne t s,
    { simp },
    obtain âŸ¨k, hk, zâŸ© := exists_max_image (of_colex t âˆ† of_colex s) id (symm_diff_nonempty.2 hts),
    refine (mem_symm_diff.1 hk).imp (Î» hk, âŸ¨k, Î» a ha, _, hk.2, hk.1âŸ©)
      (Î» hk, or.inr âŸ¨k, Î» a ha, _, hk.2, hk.1âŸ©);
      simpa [mem_symm_diff, not_or_distrib, iff_iff_implies_and_implies, and_comm]
        using not_imp_not.2 (z a) ha.not_le,
  end }

instance decidable_lt : @decidable_rel (colex Î±) (<) :=
Î» s t, decidable_of_iff'
  (âˆƒ k âˆˆ of_colex t, (âˆ€ x âˆˆ of_colex s âˆª of_colex t, k < x â†’ (x âˆˆ of_colex s â†” x âˆˆ of_colex t))
    âˆ§ k âˆ‰ of_colex s)
  begin
    apply exists_congr,
    simp only [mem_union, exists_prop, or_imp_distrib, and_comm (_ âˆˆ of_colex t), and_assoc],
    exact Î» k, and_congr_left' (forall_congr $ by tauto),
  end

instance : linear_order (colex Î±) := linear_order_of_STO (<)

instance : order_bot (colex Î±) :=
{ bot := (âˆ… : finset Î±).to_colex,
  bot_le := Î» s, begin
    induction s using finset.colex.rec,
    rw le_def,
    obtain rfl | hs := s.eq_empty_or_nonempty,
    { simp },
    refine or.inr âŸ¨max' _ hs, _, by simp, max'_mem _ _âŸ©,
    simp only [false_iff, not_mem_empty],
    exact Î» x hx t, not_le_of_lt hx (le_max' _ _ t),
  end }

@[simp] lemma to_colex_empty : to_colex (âˆ… : finset Î±) = âŠ¥ := rfl
--TODO: This lemma should be simp, but its LHS is not in simp normal form because
-- `finset.bot_eq_empty` (wrongfully) applies
lemma of_colex_bot : of_colex (âŠ¥ : colex Î±) = âˆ… := rfl

/-- Colex doesn't care if you remove the other set -/
@[simp] lemma sdiff_le_sdiff_iff_le (A B : finset Î±) :
  (A \ B).to_colex â‰¤ (B \ A).to_colex â†” A.to_colex â‰¤ B.to_colex :=
by rw [le_iff_le_iff_lt_iff_lt, sdiff_lt_sdiff_iff_lt]

/-- If `A âŠ‚ B`, then `A` is less than `B` in the colex order. Note the converse does not hold, as
`âŠ†` is not a linear order. -/
lemma colex_lt_of_ssubset (h : s âŠ‚ t) : s.to_colex < t.to_colex :=
begin
  rw [â†sdiff_lt_sdiff_iff_lt, sdiff_eq_empty_iff_subset.2 h.1, to_colex_empty, bot_lt_iff_ne_bot,
    â†to_colex_empty, to_colex_ne_to_colex],
  simpa using h.not_subset,
end

/-- If `A âŠ† B`, then `A â‰¤ B` in the colex order. Note the converse does not hold, as `âŠ†` is not a
linear order. -/
lemma colex_le_of_subset (h : s âŠ† t) : s.to_colex â‰¤ t.to_colex :=
by { rw [â†sdiff_le_sdiff_iff_le, sdiff_eq_empty_iff_subset.2 h, to_colex_empty], exact bot_le }

instance [fintype Î±] : bounded_order (colex Î±) :=
{ top := univ.to_colex,
  le_top := Î» x, colex_le_of_subset (subset_univ _),
  ..colex.order_bot }

@[simp] lemma to_colex_univ [fintype Î±] : to_colex (univ : finset Î±) = âŠ¤ := rfl
--TODO: This lemma should be simp, but its LHS is not in simp normal form because
-- `finset.top_eq_univ` (wrongfully) applies
lemma of_colex_top [fintype Î±] : of_colex (âŠ¤ : colex Î±) = univ := rfl

/-- `s < {a}` in colex iff all elements of `s` are strictly less than `a`. -/
lemma to_colex_lt_singleton : s.to_colex < ({a} : finset Î±).to_colex â†” âˆ€ x âˆˆ s, x < a :=
begin
  simp only [lt_def, mem_singleton, â†and_assoc, exists_eq_right],
  split,
  { intros t x hx,
    rw â†not_le,
    intro h,
    rcases lt_or_eq_of_le h with hâ‚ | rfl,
    { exact ne_of_irrefl hâ‚ ((t.1 hâ‚).1 hx).symm },
    { exact t.2 hx } },
  { exact Î» h, âŸ¨Î» z hz, âŸ¨Î» i, (asymm hz (h _ i)).elim, Î» i, (hz.ne' i).elimâŸ©, by simpa using h aâŸ© }
end

/-- `{a} â‰¤ s` in colex iff `r` contains an element greated than or equal to `a`. -/
lemma singleton_le_to_colex : ({a} : finset Î±).to_colex â‰¤ s.to_colex â†” âˆƒ x âˆˆ s, a â‰¤ x :=
by simp [â†not_lt, to_colex_lt_singleton]

/-- Colex is an extension of the base order. -/
lemma singleton_lt_singleton : ({a} : finset Î±).to_colex < ({b} : finset Î±).to_colex â†” a < b :=
by simp [to_colex_lt_singleton]

/-- Colex is an extension of the base order. -/
lemma singleton_le_singleton : ({a} : finset Î±).to_colex â‰¤ ({b} : finset Î±).to_colex â†” a â‰¤ b :=
by rw [le_iff_le_iff_lt_iff_lt, singleton_lt_singleton]

/-- If `s` is before `t` in colex, and everything in `t` is small, then everything in `s` is small.
-/
lemma forall_lt_mono (hâ‚ : s.to_colex â‰¤ t.to_colex) (hâ‚‚ : âˆ€ x âˆˆ t, x < a) : âˆ€ x âˆˆ s, x < a :=
begin
  rw le_def at hâ‚,
  obtain rfl | âŸ¨k, z, -, hkâŸ© := hâ‚,
  { assumption },
  { refine Î» x hx, lt_of_not_le (Î» h, h.not_lt $ hâ‚‚ x _),
    rwa â†z ((hâ‚‚ k hk).trans_le h) }
end

/-- Strictly monotone functions preserve the colex ordering. -/
lemma _root_.finset.to_colex_image_lt_to_colex_image (hf : strict_mono f) :
  (s.image f).to_colex < (t.image f).to_colex â†” s.to_colex < t.to_colex :=
begin
  simp only [lt_def, not_exists, mem_image, exists_prop, not_and],
  split,
  { rintro âŸ¨k, z, q, k', _, rflâŸ©,
    exact âŸ¨k', Î» x hx, by simpa [hf.injective.eq_iff] using z (hf hx), Î» t, q _ t rfl, â€¹k' âˆˆ tâ€ºâŸ© },
  rintro âŸ¨k, z, ka, _âŸ©,
  refine âŸ¨f k, Î» x hx, _, _, k, â€¹k âˆˆ tâ€º, rflâŸ©,
  { split,
    any_goals
    { rintro âŸ¨x', hx', rflâŸ©,
      refine âŸ¨x', _, rflâŸ©,
      rwa â† z _ <|> rwa z _,
      rwa strict_mono.lt_iff_lt hf at hx } },
  { simp only [hf.injective, function.injective.eq_iff],
    exact Î» x hx, ne_of_mem_of_not_mem hx ka }
end

/-- Strictly monotone functions preserve the colex ordering. -/
lemma _root_.finset.to_colex_image_le_to_colex_image (hf : strict_mono f) :
  (s.image f).to_colex â‰¤ (t.image f).to_colex â†” s.to_colex â‰¤ t.to_colex :=
by rw [le_iff_le_iff_lt_iff_lt, to_colex_image_lt_to_colex_image hf]

/-! ### Initial segments -/

/-- `ğ’œ` is an initial segment of the colexigraphic order on sets of `r`, and that if `B` is below
`A` in colex where `B` has size `r` and `A` is in `ğ’œ`, then `B` is also in `ğ’œ`. In effect, `ğ’œ` is
downwards closed with respect to colex among sets of size `r`. -/
def is_init_seg (ğ’œ : finset (finset Î±)) (r : â„•) : Prop :=
(ğ’œ : set (finset Î±)).sized r âˆ§
  âˆ€ â¦ƒA B : finset Î±â¦„, A âˆˆ ğ’œ â†’ B.to_colex < A.to_colex âˆ§ B.card = r â†’ B âˆˆ ğ’œ

@[simp] lemma is_init_set_empty : is_init_seg (âˆ… : finset (finset Î±)) r := by simp [is_init_seg]

/-- Initial segments are nested in some way. In particular, if they're the same size they're equal.
-/
lemma is_init_seg.total (hâ‚ : is_init_seg ğ’œâ‚ r) (hâ‚‚ : is_init_seg ğ’œâ‚‚ r) : ğ’œâ‚ âŠ† ğ’œâ‚‚ âˆ¨ ğ’œâ‚‚ âŠ† ğ’œâ‚ :=
begin
  classical,
  simp_rw [â†sdiff_eq_empty_iff_subset, â†not_nonempty_iff_eq_empty],
  by_contra' h,
  obtain âŸ¨âŸ¨A, AhâŸ©, B, hBâŸ© := h,
  rw mem_sdiff at Ah hB,
  obtain lt | eq | gt := trichotomous_of (<) A.to_colex B.to_colex,
  { exact Ah.2 (hâ‚‚.2 hB.1 âŸ¨lt, hâ‚.1 Ah.1âŸ©) },
  { rw to_colex_inj.1 eq at Ah,
    exact hB.2 Ah.1 },
  { exact hB.2 (hâ‚.2 Ah.1 âŸ¨gt, hâ‚‚.1 hB.1âŸ©) }
end

variables [fintype Î±]

/-- Gives all sets up to `A` with the same size as it: this is equivalent to
being an initial segment of colex. -/
def init_seg (s : finset Î±) : finset (finset Î±) :=
univ.filter $ Î» t, s.card = t.card âˆ§ t.to_colex â‰¤ s.to_colex

@[simp] lemma mem_init_seg : t âˆˆ init_seg s â†” s.card = t.card âˆ§ t.to_colex â‰¤ s.to_colex :=
by simp [init_seg]

@[simp] lemma mem_init_seg_self : s âˆˆ init_seg s := by simp

@[simp] lemma init_seg_nonempty : (init_seg s).nonempty := âŸ¨s, mem_init_seg_selfâŸ©

/-- Being a nonempty initial segment of colex if equivalent to being an `init_seg`. -/
lemma is_init_seg_iff_exists_init_seg :
  ğ’œ.nonempty âˆ§ is_init_seg ğ’œ r â†” âˆƒ s : finset Î±, s.card = r âˆ§ ğ’œ = init_seg s :=
begin
  split,
  { rintro âŸ¨ne, layer, ISâŸ©,
    have Ah := @max'_mem (colex Î±) _ ğ’œ ne,
    refine âŸ¨@max' (colex Î±) _ ğ’œ ne, layer Ah, _âŸ©,
    ext B,
    rw mem_init_seg,
    refine âŸ¨Î» p, _, _âŸ©,
    { rw [layer p, layer Ah],
      exact âŸ¨rfl, le_max' _ _ pâŸ© },
    rintro âŸ¨cards, leâŸ©,
    obtain p | p := le.eq_or_lt,
    { rwa to_colex_inj.1 p, },
    { exact IS Ah âŸ¨p, cards â–¸ layer AhâŸ© } },
  { rintro âŸ¨s, hs, rflâŸ©,
    refine âŸ¨init_seg_nonempty, Î» B hB, (mem_init_seg.1 hB).1.symm.trans hs,
      Î» Bâ‚ Bâ‚‚ hBâ‚ hBâ‚‚, mem_init_seg.2 âŸ¨_, _âŸ©âŸ©,
    { rwa hBâ‚‚.2 },
    { rw mem_init_seg at hBâ‚,
      exact hBâ‚‚.1.le.trans hBâ‚.2 } }
end

lemma is_init_seg_init_seg : is_init_seg (init_seg s) s.card :=
(is_init_seg_iff_exists_init_seg.2 âŸ¨s, rfl, rflâŸ©).2

end linear_order
end colex

open colex

/-!
### Colex on `â„•`

The colexicographic order agrees with the order induced by interpreting a set of naturals as a
binary expansion.
-/

section nat

/-- For subsets of â„•, we can show that colex is equivalent to binary. -/
lemma sum_two_pow_lt_iff_colex_lt (A B : finset â„•) :
  âˆ‘ i in A, 2^i < âˆ‘ i in B, 2^i â†” A.to_colex < B.to_colex :=
begin
  have z : âˆ€ (A B : finset â„•), A.to_colex < B.to_colex â†’ âˆ‘ i in A, 2^i < âˆ‘ i in B, 2^i,
  { intros A B,
    rw [â† sdiff_lt_sdiff_iff_lt, lt_def],
    rintro âŸ¨k, z, kA, kBâŸ©,
    rw â† sdiff_union_inter A B,
    conv_rhs { rw â† sdiff_union_inter B A },
    rw [sum_union (disjoint_sdiff_inter _ _), sum_union (disjoint_sdiff_inter _ _),
        inter_comm, add_lt_add_iff_right],
    apply lt_of_lt_of_le (@nat.sum_two_pow_lt k (A \ B) _),
    { apply single_le_sum (Î» _ _, nat.zero_le _) kB },
    intros x hx,
    apply lt_of_le_of_ne (le_of_not_lt (Î» kx, _)),
    { exact (ne_of_mem_of_not_mem hx kA) },
    have := (z kx).1 hx,
    rw mem_sdiff at this hx,
    exact hx.2 this.1 },
  refine âŸ¨Î» h, (lt_trichotomy (to_colex A) $ to_colex B).resolve_right
    (Î» hâ‚, hâ‚.elim _ (not_lt_of_gt h âˆ˜ z _ _)), z A BâŸ©,
  rw to_colex_inj,
  rintro rfl,
  exact irrefl _ h,
end

/-- For subsets of â„•, we can show that colex is equivalent to binary. -/
lemma sum_two_pow_le_iff_colex_le (A B : finset â„•) :
  âˆ‘ i in A, 2^i â‰¤ âˆ‘ i in B, 2^i â†” A.to_colex â‰¤ B.to_colex :=
by rw [le_iff_le_iff_lt_iff_lt, sum_two_pow_lt_iff_colex_lt]

end nat
end finset
