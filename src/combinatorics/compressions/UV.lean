/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta
-/

import data.finset
import data.fintype.basic
import combinatorics.basic
import combinatorics.shadows
import combinatorics.colex

/-!
# compressions/UV

UV compressions are immensely useful to prove the Kruskal-Katona theorem.
The idea is that compressing a set family might decrease the size of its
shadow, and so iterated compressions should hopefully minimise the shadow.

The main result proved is:

theorem compression_reduces_shadow {ğ’œ : finset (finset Î±)} {U V : finset Î±}
    (hâ‚ : âˆ€ x âˆˆ U, âˆƒ y âˆˆ V, is_compressed (erase U x) (erase V y) ğ’œ)
    (hâ‚‚ : U.card = V.card) : (âˆ‚ compress_family U V ğ’œ).card â‰¤ (âˆ‚ğ’œ).card

It says that the shadow size decreases under particular conditions -
most importantly, that for every x âˆˆ U, there is y âˆˆ V such that the family
is (U-x, V-y) compressed.

## Tags

compression, UV-compression, shadow

## References

* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
-/

open fintype
open finset

variable {Î± : Type*}
variables [decidable_eq Î±]

variables {n : â„•}

-- The namespace here is useful to distinguish between other compressions.
namespace UV
  /--
  To UV-compress A, if it doesn't touch U and does contain V, we remove V and
  put U in. We'll only really use this when |U| = |V| and U âˆ© V = âˆ….
  -/
  def compress (U V : finset Î±) (A : finset Î±) :=
  if disjoint U A âˆ§ (V âŠ† A)
    then (A âˆª U) \ V
    else A

  /-- Compression doesn't change the size of a set. -/
  lemma compress_size (U V : finset Î±) (A : finset Î±) (hâ‚ : U.card = V.card) :
    (compress U V A).card = A.card :=
  begin
    rw compress, split_ifs,
      rw [card_sdiff (subset.trans h.2 (subset_union_left _ _)),
          card_disjoint_union h.1.symm, hâ‚, nat.add_sub_cancel],
    refl
  end

  /-- Compressing a set is idempotent. -/
  lemma compress_idem (U V : finset Î±) (A : finset Î±) :
    compress U V (compress U V A) = compress U V A :=
  begin
    simp only [compress], split_ifs,
      suffices: U = âˆ…,
        rw [this, union_empty, union_empty, sdiff_idem],
      have: U \ V = U := sdiff_eq_self_of_disjoint
                         (disjoint_of_subset_right h.2 h.1),
      rw â† disjoint_self_iff_empty,
      apply disjoint_of_subset_right (subset_union_right (A\V) _),
      rw [union_sdiff_distrib, â€¹U \ V = Uâ€º] at h_1,
    all_goals { tauto }
  end

  /--
  Part of the compressed family, where we keep sets whose compression is
  already present.
  -/
  @[reducible]
  def compress_remains (U V : finset Î±) (ğ’œ : finset (finset Î±)) :=
  ğ’œ.filter (Î» A, compress U V A âˆˆ ğ’œ)
  /--
  Part of the compressed family, where we move the sets whose compression is
  not there.
  -/
  @[reducible]
  def compress_motion (U V : finset Î±) (ğ’œ : finset (finset Î±)) :=
  (ğ’œ.filter (Î» A, compress U V A âˆ‰ ğ’œ)).image (Î» A, compress U V A)

  /--
  To UV-compress a set family, we keep all the sets whose compression is
  present, and move all the sets whose compression is not there (and take this
  union).
  -/
  def compress_family (U V : finset Î±) (ğ’œ : finset (finset Î±)) :=
  compress_motion U V ğ’œ âˆª compress_remains U V ğ’œ
  local notation `CC` := compress_family

  lemma mem_compress_remains  {ğ’œ : finset (finset Î±)} (U V A : finset Î±) :
  A âˆˆ compress_remains U V ğ’œ â†” A âˆˆ ğ’œ âˆ§ compress U V A âˆˆ ğ’œ :=
  by rw mem_filter

  lemma mem_compress_motion {ğ’œ : finset (finset Î±)} (U V A : finset Î±) :
  A âˆˆ compress_motion U V ğ’œ â†” A âˆ‰ ğ’œ âˆ§ (âˆƒ B âˆˆ ğ’œ, compress U V B = A) :=
  begin
    simp [compress_motion],
    split; rintro âŸ¨p, q, râŸ©,
      exact âŸ¨r â–¸ q.2, p, âŸ¨q.1, râŸ©âŸ©,
    exact âŸ¨q, âŸ¨r.1, r.2.symm â–¸ pâŸ©, r.2âŸ©,
  end

  /--
  `A` is in the UV-compressed family iff it's in the original and its
  compression is in the original, or it's not in the original but it's the
  compression of something in the original
  -/
  lemma mem_compress {ğ’œ : finset (finset Î±)} (U V : finset Î±) {A : finset Î±} :
    A âˆˆ CC U V ğ’œ â†”
    (A âˆ‰ ğ’œ âˆ§ (âˆƒ B âˆˆ ğ’œ, compress U V B = A)) âˆ¨ (A âˆˆ ğ’œ âˆ§ compress U V A âˆˆ ğ’œ) :=
  by rw [compress_family, mem_union, mem_compress_remains, mem_compress_motion]

  /-- `is_compressed U V ğ’œ` expresses that ğ’œ is UV-compressed -/
  @[reducible]
  def is_compressed (U V : finset Î±) (ğ’œ : finset (finset Î±)) :=
  CC U V ğ’œ = ğ’œ

  /-- The empty family is compressed. -/
  lemma is_compressed_empty (ğ’œ : finset (finset Î±)) : is_compressed âˆ… âˆ… ğ’œ :=
  begin
    have q: âˆ€ (A : finset Î±), compress âˆ… âˆ… A = A, simp [compress],
    simp [is_compressed, compress_family], --ext, q]s
    sorry,
  end

  /--
  Compressing a set doesn't change it's size, so compressing a family keeps
  all sets the same size.
  -/
  lemma compress_family_sized {r : â„•} {ğ’œ : finset (finset Î±)} {U V : finset Î±}
    (hâ‚ : U.card = V.card) (hâ‚‚ : all_sized ğ’œ r) :
    all_sized (CC U V ğ’œ) r :=
  begin
    intros A HA, rw mem_compress at HA,
    rcases HA with âŸ¨_, _, zâ‚, rflâŸ© | âŸ¨zâ‚, _âŸ©,
      rw compress_size _ _ _ hâ‚,
    all_goals {apply hâ‚‚ _ zâ‚}
  end

  /-- Compressing a family is idempotent. -/
  lemma compress_family_idempotent (U V : finset Î±) (ğ’œ : finset (finset Î±)) :
    CC U V (CC U V ğ’œ) = CC U V ğ’œ :=
  begin
    have: âˆ€ A âˆˆ CC U V ğ’œ, compress U V A âˆˆ CC U V ğ’œ,
      intros A HA, rw mem_compress at HA âŠ¢, simp [compress_idem],
      rcases HA with âŸ¨_, B, _, rflâŸ© | âŸ¨_, _âŸ©,
        left, refine âŸ¨_, B, â€¹_â€º, _âŸ©; rwa compress_idem,
      right, assumption,
    have: filter (Î» A, compress U V A âˆ‰ CC U V ğ’œ) (CC U V ğ’œ) = âˆ…,
      rw â† filter_false (CC U V ğ’œ), apply filter_congr, simpa,
    rw [compress_family, compress_motion, this, image_empty, union_comm,
        compress_remains, â† this],
    apply filter_union_filter_neg_eq (compress_family U V ğ’œ)
  end

  lemma compress_disjoint {ğ’œ : finset (finset Î±)} (U V : finset Î±) :
    disjoint (compress_motion U V ğ’œ) (compress_remains U V ğ’œ) :=
  begin
    rw disjoint_left, intros A HA HB,
    rw mem_compress_remains at HB,
    rw mem_compress_motion at HA,
    exact HA.1 HB.1
  end

  /-- Compression is kinda injective. -/
  lemma inj_ish {U V : finset Î±} {A B : finset Î±}
    (hA : disjoint U A âˆ§ V âŠ† A) (hB : disjoint U B âˆ§ V âŠ† B)
    (Z : (A âˆª U) \ V = (B âˆª U) \ V) : A = B :=
  begin
    ext x, split,
    all_goals {
      intro p, by_cases hâ‚: (x âˆˆ V), { exact hB.2 hâ‚ <|> exact hA.2 hâ‚ },
      have := mem_sdiff.2 âŸ¨mem_union_left U â€¹_â€º, hâ‚âŸ©,
      rw Z at this <|> rw â† Z at this,
      rw [mem_sdiff, mem_union] at this,
      suffices: x âˆ‰ U, tauto,
      apply disjoint_right.1 â€¹disjoint _ _ âˆ§ _â€º.1 p }
  end

  /-- Compressing a set family doesn't change its size. -/
  lemma compressed_size {ğ’œ : finset (finset Î±)} (U V : finset Î±) :
   (CC U V ğ’œ).card = ğ’œ.card :=
  begin
    rw [compress_family, card_disjoint_union (compress_disjoint _ _),
        card_image_of_inj_on],
      rw [â† card_disjoint_union, union_comm, filter_union_filter_neg_eq],
      rw [disjoint_iff_inter_eq_empty, inter_comm],
      apply filter_inter_filter_neg_eq,
    intros A HA B HB Z,
    rw mem_filter at HA HB, rw compress at HA Z, split_ifs at HA Z,
      rw compress at HB Z,
      split_ifs at HB Z,
        exact inj_ish h h_1 Z,
      tauto,
    tauto
  end

  /--
  If A is in the compressed family but V is a subset of A, A must have been
  in the original family.
  -/
  lemma compress_held {ğ’œ : finset (finset Î±)} {U V A : finset Î±}
    (hâ‚ : A âˆˆ CC U V ğ’œ) (hâ‚‚ : V âŠ† A) (hâ‚ƒ : U.card = V.card) :
    A âˆˆ ğ’œ :=
  begin
    rw mem_compress at hâ‚, rcases hâ‚ with âŸ¨_, B, H, HBâŸ© | _,
      rw compress at HB, split_ifs at HB,
        have: V = âˆ…,
          apply eq_empty_of_forall_not_mem,
          intros x xV, replace hâ‚‚ := hâ‚‚ xV,
          rw [â† HB, mem_sdiff] at hâ‚‚, exact hâ‚‚.2 xV,
        have: U = âˆ…,
          rwa [â† card_eq_zero, hâ‚ƒ, card_eq_zero],
        rw [â€¹U = âˆ…â€º, â€¹V = âˆ…â€º, union_empty, sdiff_empty] at HB, rwa â† HB,
      rwa â† HB,
    tauto
  end

  /--
  If A is not in the original family but is in the compressed family, then
  A has been compressed, and its original was in the original family.
  -/
  lemma compress_moved {ğ’œ : finset (finset Î±)} {U V A : finset Î±}
    (hâ‚ : A âˆˆ CC U V ğ’œ) (hâ‚‚ : A âˆ‰ ğ’œ) :
    U âŠ† A âˆ§ disjoint V A âˆ§ (A âˆª V) \ U âˆˆ ğ’œ :=
  begin
    rw mem_compress at hâ‚,
    rcases hâ‚ with âŸ¨_, B, H, HBâŸ© | _,
    { rw compress at HB,
      split_ifs at HB,
      { rw â† HB at *, refine âŸ¨_, disjoint_sdiff, _âŸ©,
          have: disjoint U V := disjoint_of_subset_right h.2 h.1,
          rw union_sdiff_distrib, rw sdiff_eq_self_of_disjoint this,
          apply subset_union_right _ _,
        rwa [sdiff_union_of_subset, union_sdiff_self,
             sdiff_eq_self_of_disjoint h.1.symm],
        apply trans h.2 (subset_union_left _ _) },
      { rw HB at *, tauto } },
    tauto
  end

  /--
  If A is in the compressed family and does move under compression, then the
  compressed version was in the original family.
  -/
  lemma uncompressed_was_already_there {ğ’œ : finset (finset Î±)}
    {U V A : finset Î±} (hâ‚ : A âˆˆ CC U V ğ’œ) (hâ‚‚ : V âŠ† A) (hâ‚ƒ : disjoint U A) :
    (A âˆª U) \ V âˆˆ ğ’œ :=
  begin
    rw mem_compress at hâ‚, have: disjoint U A âˆ§ V âŠ† A := âŸ¨hâ‚ƒ, hâ‚‚âŸ©,
    rcases hâ‚ with âŸ¨_, B, B_in_A, cB_eq_AâŸ© | âŸ¨_, cA_in_AâŸ©,
    { by_cases a: (A âˆª U) \ V = A,
        have: U \ V = U, apply sdiff_eq_self_of_disjoint,
          apply (disjoint_of_subset_right hâ‚‚ hâ‚ƒ),
        have: U = âˆ…,
          rw â† disjoint_self_iff_empty,
          suffices: disjoint U (U \ V), rw â€¹U \ V = Uâ€º at this, assumption,
          apply disjoint_of_subset_right (subset_union_right (A \ V) _),
          rwa [â† union_sdiff_distrib, a],
        have: V = âˆ…,
          rw â† disjoint_self_iff_empty, apply disjoint_of_subset_right hâ‚‚,
          rw â† a, apply disjoint_sdiff,
        simpa [a, cB_eq_A.symm, compress, â€¹U = âˆ…â€º, â€¹V = âˆ…â€º],
      have: compress U V A = (A âˆª U) \ V, rw compress, split_ifs, refl,
      exfalso, apply a, rw [â† this, â† cB_eq_A, compress_idem] },
    { rw compress at cA_in_A, split_ifs at cA_in_A, assumption }
  end

  lemma sdiff_sdiff {A B C : finset Î±} (h : C âŠ† A) : A \ (B \ C) = A \ B âˆª C :=
  begin
    ext1 i,
    simp only [mem_union, not_and, mem_sdiff],
    push_neg,
    refine âŸ¨_, _âŸ©,
    rintro âŸ¨iA, iBCâŸ©,
    by_cases (i âˆˆ C),
    right, exact h,
    left,
    refine âŸ¨iA, mt iBC hâŸ©,
    rintro (âŸ¨iA, niBâŸ© | iC),
    refine âŸ¨iA, Î» iB, (niB iB).elimâŸ©,
    refine âŸ¨h iC, Î» _, iCâŸ©,
  end

  lemma sdiff_erase {A : finset Î±} {x : Î±} (h : x âˆˆ A) : A \ A.erase x = {x} :=
  begin
    rw [â† sdiff_singleton_eq_erase, sdiff_sdiff, sdiff_self, empty_union],
    rwa singleton_subset_iff,
  end

  /--
  Here's the key fact about compression for KK. If, for all `x âˆˆ U` there is
  `y âˆˆ V` such that `ğ’œ` is `(U-x,V-y)`-compressed, then UV-compression will
  reduce the size of `ğ’œ`'s shadow.
  -/
  theorem compression_reduces_shadow {ğ’œ : finset (finset Î±)} {U V : finset Î±}
    (hâ‚ : âˆ€ x âˆˆ U, âˆƒ y âˆˆ V, is_compressed (erase U x) (erase V y) ğ’œ)
    (hâ‚‚ : U.card = V.card) : (âˆ‚ CC U V ğ’œ).card â‰¤ (âˆ‚ğ’œ).card :=
  begin
    set ğ’œ' := compress_family U V ğ’œ,
    suffices: (âˆ‚ğ’œ' \ âˆ‚ğ’œ).card â‰¤ (âˆ‚ğ’œ \ âˆ‚ğ’œ').card,
    { suffices z: card (âˆ‚ğ’œ' \ âˆ‚ğ’œ âˆª âˆ‚ğ’œ' âˆ© âˆ‚ğ’œ) â‰¤ card (âˆ‚ğ’œ \ âˆ‚ğ’œ' âˆª âˆ‚ğ’œ âˆ© âˆ‚ğ’œ'),
      { rwa [sdiff_union_inter, sdiff_union_inter] at z },
      rw [card_disjoint_union, card_disjoint_union, inter_comm],
      apply add_le_add_right â€¹_â€º,
      any_goals { apply disjoint_sdiff_inter } },

    -- We'll define an injection âˆ‚ğ’œ' \ âˆ‚ğ’œ â†’ âˆ‚ğ’œ \ âˆ‚ğ’œ'. First, let's prove
    -- a few facts about things in the domain:
    have qâ‚: âˆ€ B âˆˆ âˆ‚ğ’œ' \ âˆ‚ğ’œ, U âŠ† B âˆ§ disjoint V B âˆ§ (B âˆª V) \ U âˆˆ âˆ‚ğ’œ \ âˆ‚ğ’œ',
    { intros B HB,
      obtain âŸ¨k, k'âŸ©: B âˆˆ âˆ‚ğ’œ' âˆ§ B âˆ‰ âˆ‚ğ’œ := mem_sdiff.1 HB,
      -- This is gonna be useful a couple of times so let's name it.
      have m: âˆ€ y âˆ‰ B, insert y B âˆ‰ ğ’œ := Î» y H a, k' (mem_shadow'.2 âŸ¨y, H, aâŸ©),
      rcases mem_shadow'.1 k with âŸ¨x, _, _âŸ©,
      have q := compress_moved â€¹insert x B âˆˆ ğ’œ'â€º (m _ â€¹x âˆ‰ Bâ€º),
      have : disjoint V B := (disjoint_insert_right.1 q.2.1).2,
      have dVU : disjoint V U := disjoint_of_subset_right q.1 q.2.1,
      have: V \ U = V := sdiff_eq_self_of_disjoint â€¹disjoint V Uâ€º,
      -- The first key part is that x âˆ‰ U
      have: x âˆ‰ U,
      { intro a,
        rcases hâ‚ x â€¹x âˆˆ Uâ€º with âŸ¨y, Hy, xy_compâŸ©,
        -- If `x âˆˆ U`, we can get `y âˆˆ V` so that `ğ’œ` is `(U-x,V-y)`-compressed
        apply m y (disjoint_left.1 â€¹disjoint V Bâ€º Hy),
        -- and we'll use this `y` to contradict `m`.
        rw is_compressed at xy_comp,
        have: (insert x B âˆª V) \ U âˆˆ compress_family (erase U x) (erase V y) ğ’œ,
          rw xy_comp, exact q.2.2,
        -- So we'd like to show insert y B âˆˆ ğ’œ.
        -- We do this by showing the below
        have: ((insert x B âˆª V) \ U âˆª erase U x) \ erase V y âˆˆ ğ’œ,
          apply uncompressed_was_already_there this _,
            apply disjoint_of_subset_left (erase_subset _ _) disjoint_sdiff,
          rw [union_sdiff_distrib, â€¹V \ U = Vâ€º],
          apply subset.trans (erase_subset _ _) (subset_union_right _ _),
        -- and then arguing that it's the same
        suffices : ((insert x B âˆª V) \ U âˆª erase U x) \ erase V y = insert y B,
          rwa â† this,
        have : x âˆ‰ B âˆª V := not_mem_union.2 âŸ¨â€¹x âˆ‰ Bâ€º, disjoint_right.1 â€¹disjoint V Uâ€º aâŸ©,
        have : erase U x âŠ† insert x B âˆª V := trans (erase_subset x _)
                                             (trans q.1 (subset_union_left _ V)),
        -- which is just a pain.
        rw [â† sdiff_sdiff â€¹U.erase x âŠ† insert x B âˆª Vâ€º, sdiff_erase â€¹x âˆˆ Uâ€º,
            sdiff_singleton_eq_erase, insert_union, erase_insert â€¹x âˆ‰ B âˆª Vâ€º, union_sdiff_distrib,
            sdiff_erase â€¹y âˆˆ Vâ€º, sdiff_eq_self_of_disjoint, union_comm, insert_eq],
        rw [disjoint.comm],
        apply disjoint_of_subset_left (erase_subset _ _) â€¹disjoint V Bâ€º },
      -- Now that that's done, it's immediate that U âŠ† B
      have: U âŠ† B, rw [â† erase_eq_of_not_mem â€¹x âˆ‰ Uâ€º, â† subset_insert_iff], exact q.1,
      -- and we already had that V and B are disjoint
      refine âŸ¨â€¹_â€º, â€¹_â€º, _âŸ©,
      -- so it only remains to get (B âˆª V) \ U âˆˆ âˆ‚ğ’œ \ âˆ‚ğ’œ'
      rw mem_sdiff,
      have: x âˆ‰ V := disjoint_right.1 q.2.1 (mem_insert_self _ _),
      split,
        -- (B âˆª V) \ U âˆˆ âˆ‚ğ’œ is pretty direct:
        rw mem_shadow',
        refine âŸ¨x, _, _âŸ©,
        { simp [mem_sdiff, mem_union], tauto! },
        convert q.2.2,
        rw [insert_eq, insert_eq, union_assoc, union_sdiff_distrib _ (B âˆª V),
            sdiff_eq_self_of_disjoint (singleton_disjoint.2 â€¹x âˆ‰ Uâ€º)],
      -- For (B âˆª V) \ U âˆ‰ âˆ‚ğ’œ', we split up based on w âˆˆ U
      rw mem_shadow', rintro âŸ¨w, _, _âŸ©, by_cases (w âˆˆ U),
        -- If w âˆˆ U, we find z âˆˆ V, and contradict m again
        rcases hâ‚ w â€¹w âˆˆ Uâ€º with âŸ¨z, Hz, xy_compâŸ©,
        apply m z (disjoint_left.1 â€¹disjoint V Bâ€º Hz),
        have: insert w ((B âˆª V) \ U) âˆˆ ğ’œ,
        { apply compress_held a_h_h _ hâ‚‚,
          apply subset.trans _ (subset_insert _ _),
          rw union_sdiff_distrib, rw â€¹V \ U = Vâ€º, apply subset_union_right },
        have: (insert w ((B âˆª V) \ U) âˆª erase U w) \ erase V z âˆˆ ğ’œ,
          refine uncompressed_was_already_there _ _ _,
              rw is_compressed at xy_comp, rwa xy_comp,
            apply subset.trans (erase_subset _ _),
            apply subset.trans _ (subset_insert _ _),
            rw [union_sdiff_distrib, â€¹V \ U = Vâ€º], apply subset_union_right,
          rw disjoint_insert_right, split, apply not_mem_erase,
          apply disjoint_of_subset_left (erase_subset _ _), apply disjoint_sdiff,
        have: (insert w ((B âˆª V) \ U) âˆª erase U w) \ erase V z = insert z B,
          rw [insert_union, â† union_insert, insert_erase h,
              sdiff_union_of_subset (subset.trans â€¹U âŠ† Bâ€º (subset_union_left _ _)),
              union_sdiff_distrib,
              sdiff_eq_self_of_disjoint (disjoint_of_subset_right (erase_subset _ _) â€¹disjoint V Bâ€º.symm),
              â† sdiff_singleton_eq_erase, sdiff_sdiff_self_left,
              inter_singleton_of_mem Hz, union_comm], refl,
        rwa â† this,
      -- If w âˆ‰ U, we contradict m again
      rw [mem_sdiff, â† not_imp, not_not] at a_h_w,
      have: w âˆ‰ V := h âˆ˜ a_h_w âˆ˜ mem_union_right _,
      have: w âˆ‰ B := h âˆ˜ a_h_w âˆ˜ mem_union_left _,
      apply m w this,

      have: (insert w ((B âˆª V) \ U) âˆª U) \ V âˆˆ ğ’œ,
        refine uncompressed_was_already_there â€¹insert w ((B âˆª V) \ U) âˆˆ ğ’œ'â€º
               (trans _ (subset_insert _ _)) _,
          rw [union_sdiff_distrib, â€¹V \ U = Vâ€º], apply subset_union_right,
          rw disjoint_insert_right, exact âŸ¨â€¹_â€º, disjoint_sdiffâŸ©,
      convert this, rw [insert_union, sdiff_union_of_subset (trans â€¹U âŠ† Bâ€º (subset_union_left _ _)),
                        â† insert_union, union_sdiff_self], symmetry,
      rw [sdiff_eq_self_iff_disjoint, disjoint_insert_left], split, assumption,
      rwa disjoint.comm },
    apply card_le_card_of_inj_on (Î» B, (B âˆª V) \ U) (Î» B HB, (qâ‚ B HB).2.2),
    intros Bâ‚ HBâ‚ Bâ‚‚ HBâ‚‚ k,
    exact inj_ish âŸ¨(qâ‚ Bâ‚ HBâ‚).2.1, (qâ‚ Bâ‚ HBâ‚).1âŸ© âŸ¨(qâ‚ Bâ‚‚ HBâ‚‚).2.1, (qâ‚ Bâ‚‚ HBâ‚‚).1âŸ© k
  end
end UV
