/-
Copyright (c) 2023 Adam Topaz, RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz, RÃ©mi Bottinelli
-/
import combinatorics.quiver.basic
import combinatorics.quiver.cast
import logic.equiv.basic
import tactic.nth_rewrite
/-!
# Isomorphisms of quivers

Isomorphisms of quivers, defined as pairs of prefunctors that compose to the identities.

## Main definitions

* For quivers `U` and `V`, `iso U V` is the type of isomorphisms between `U` and `V`, with
  associated `iso.refl`, `iso.symm`, and `iso.trans` definitions.
* `iso.of_bijective` is the isomorphism defined by a prefunctor that is bijective on vertices and
  arrows.

## Notation

* `U â‰ƒq V` is notation for `iso U V`

-/

universes u v w z

namespace quiver

/--
An isomorphism of quivers is given by a pair of prefunctors whose two compositions
are the identities.
-/
structure iso (U V : Type*) [quiver.{u+1} U] [quiver.{v+1} V] extends prefunctor U V :=
(inv_prefunctor : V â¥¤q U)
(left_inv : to_prefunctor â‹™q inv_prefunctor = ğŸ­q _)
(right_inv : inv_prefunctor â‹™q to_prefunctor = ğŸ­q _)

infix ` â‰ƒq `:60 := iso

variables {U V W Z : Type*} [quiver.{u+1} U] [quiver.{v+1} V] [quiver.{w+1} W] [quiver.{z+1} Z]

instance : has_coe (iso U V) (prefunctor U V) := âŸ¨iso.to_prefunctorâŸ©

namespace iso

/--
Two isomorphisms are equal iff their `to_prefunctor` and `inv_prefunctor` agree.
Not tagged `@[ext]` because `to_prefunctor_ext` will be.
-/
lemma ext (Ï† Ïˆ : iso U V)
  (hto : Ï†.to_prefunctor = Ïˆ.to_prefunctor) (hinv : Ï†.inv_prefunctor = Ïˆ.inv_prefunctor) : Ï† = Ïˆ :=
by { cases Ï†, cases Ïˆ, cases hto, cases hinv, refl, }

/-- The identity prefunctor defines an isomorphism. -/
@[simps] def refl (U : Type*) [quiver.{u+1} U] : iso U U := âŸ¨ğŸ­q _, ğŸ­q _, rfl, rflâŸ©

instance : inhabited (iso U U) := âŸ¨iso.refl UâŸ©

/-- Swapping `to_prefunctor` and `inv_prefunctor` inverts an isomorphism. -/
@[simps] def symm (Ï† : iso U V) : iso V U :=
âŸ¨Ï†.inv_prefunctor, Ï†.to_prefunctor, Ï†.right_inv, Ï†.left_invâŸ©

/-- Composing the components of two isomorphisms. -/
@[simps] def trans (Ï† : iso U V) (Ïˆ : iso V W) : iso U W :=
{ to_prefunctor := Ï†.to_prefunctor â‹™q Ïˆ.to_prefunctor,
  inv_prefunctor := Ïˆ.inv_prefunctor â‹™q Ï†.inv_prefunctor,
  left_inv := by
  { rw [â†prefunctor.comp_assoc, prefunctor.comp_assoc Ï†.to_prefunctor,
        Ïˆ.left_inv, prefunctor.comp_id, Ï†.left_inv], },
  right_inv := by
  { rw [â†prefunctor.comp_assoc, prefunctor.comp_assoc Ïˆ.inv_prefunctor,
        Ï†.right_inv, prefunctor.comp_id, Ïˆ.right_inv], }, }

/--
The equivalence on vertices induced by an isomorphism.
-/
@[simps] def to_equiv (Ï† : iso U V) : U â‰ƒ V :=
{ to_fun := Ï†.to_prefunctor.obj,
  inv_fun := Ï†.inv_prefunctor.obj,
  left_inv := Î» x, congr_arg (Î» (F : U â¥¤q U), F.obj x) Ï†.left_inv,
  right_inv := Î» x, congr_arg (Î» (F : V â¥¤q V), F.obj x) Ï†.right_inv }

lemma inv_obj_obj_eq (Ï† : iso U V) (X : U) : Ï†.inv_prefunctor.obj (Ï†.to_prefunctor.obj X) = X :=
Ï†.to_equiv.left_inv X

lemma obj_inv_obj_eq (Ï† : iso U V) (X : V) : Ï†.to_prefunctor.obj (Ï†.inv_prefunctor.obj X) = X :=
Ï†.to_equiv.right_inv X

lemma to_obj_injective (Ï† : iso U V) : Ï†.to_prefunctor.obj.injective :=
Ï†.to_equiv.injective

lemma inv_obj_injective (Ï† : iso U V) : Ï†.inv_prefunctor.obj.injective :=
Ï†.symm.to_equiv.injective

/--
The equivalence on arrows `X âŸ¶ Y â‰ƒ (Ï†.obj X âŸ¶ Ï†.obj Y)` induced by the isomorphism `Ï†`,
The forward map is `Ï†.to_prefunctor.map`, but the backward map is the composite of
* `Ï†.inv_prefunctor.map : Ï†.obj X âŸ¶ Ï†.obj Y â†’ Ï†.symm.obj (Ï†.obj X) âŸ¶ Ï†.symm.obj (Ï†.obj Y)`, and
* `hom.equiv_cast _ _ : Ï†.symm.obj (Ï†.obj X) âŸ¶ Ï†.symm.obj (Ï†.obj Y) â†’ X âŸ¶ Y`.
-/
@[simps] def to_equiv_hom (Ï† : iso U V) {X Y : U} : (X âŸ¶ Y) â‰ƒ (Ï†.obj X âŸ¶ Ï†.obj Y) :=
{ to_fun := Ï†.to_prefunctor.map,
  inv_fun := hom.equiv_cast (Ï†.to_equiv.left_inv X) (Ï†.to_equiv.left_inv Y) âˆ˜ Ï†.inv_prefunctor.map,
  left_inv := Î» e, by
    begin
      nth_rewrite_rhs 0 â†((prefunctor.id_map _ _ _ e).rec_on $
                           prefunctor.map_cast_eq_of_eq Ï†.left_inv e),
      simp only [function.comp_app, prefunctor.comp_map, hom.equiv_cast_apply],
      apply hom.cast_congr,
    end,
  right_inv := Î» e, by
    begin
      nth_rewrite_rhs 0 â†((prefunctor.id_map _ _ _ e).rec_on $
                           prefunctor.map_cast_eq_of_eq Ï†.right_inv e),
      simp only [prefunctor.map_cast, function.comp_app, prefunctor.comp_map, hom.equiv_cast_apply],
      apply hom.cast_congr,
    end }

lemma inv_map_map_eq_cast (Ï† : iso U V) {X Y : U} (f : X âŸ¶ Y) :
  Ï†.inv_prefunctor.map (Ï†.to_prefunctor.map f) =
  f.cast (Ï†.to_equiv.left_inv X).symm (Ï†.to_equiv.left_inv Y).symm :=
by { rw â†hom.cast_eq_iff_eq_cast, exact Ï†.to_equiv_hom.left_inv f, }

lemma map_inv_map_eq_cast (Ï† : iso U V) {X Y : V} (f : X âŸ¶ Y) :
  Ï†.to_prefunctor.map (Ï†.inv_prefunctor.map f) =
  f.cast (Ï†.to_equiv.right_inv X).symm (Ï†.to_equiv.right_inv Y).symm :=
Ï†.symm.inv_map_map_eq_cast _

/-- The inverse of a bijective (on objects and arrows) prefunctor. -/
@[simps] noncomputable def of_bijective_inverse_aux (Ï† : U â¥¤q V) (hÏ†obj : Ï†.obj.bijective)
  (hÏ†map : âˆ€ (x y : U), (Ï†.map : (x âŸ¶ y) â†’ (Ï†.obj x âŸ¶ Ï†.obj y)).bijective ) :
  V â¥¤q U :=
let
  Eobj : U â‰ƒ V := equiv.of_bijective _ hÏ†obj,
  Ehom : Î  X Y : U, (X âŸ¶ Y) â‰ƒ (Ï†.obj X âŸ¶ Ï†.obj Y) := Î» X Y, equiv.of_bijective _ (hÏ†map _ _)
in
{ obj := Eobj.symm,
  map := Î» X Y, (Ehom _ _).symm âˆ˜ hom.equiv_cast
    (show X = Eobj _, by rw Eobj.apply_symm_apply) (show Y = Eobj _, by rw Eobj.apply_symm_apply) }

/-- A bijective (on objects and arrows) prefunctor defines an isomorphism. -/
noncomputable def of_bijective (Ï† : U â¥¤q V) (hÏ†obj : function.bijective Ï†.obj)
  (hÏ†map : âˆ€ (x y : U), function.bijective (Ï†.map : (x âŸ¶ y) â†’ (Ï†.obj x âŸ¶ Ï†.obj y))) :
  iso U V :=
{ to_prefunctor := Ï†,
  inv_prefunctor := iso.of_bijective_inverse_aux Ï† hÏ†obj hÏ†map,
  left_inv := begin
    fapply prefunctor.ext,
    { intros X, simp, },
    { intros X Y f,
      change (equiv.of_bijective Ï†.map _).symm ((Ï†.map f).cast _ _) = f.cast _ _,
      generalize_proofs _ _ _ hâ‚„ hâ‚…,
      change (equiv.of_bijective Ï†.map _).symm
        (hom.cast (congr_arg Ï†.obj hâ‚„) (congr_arg Ï†.obj hâ‚…) (Ï†.map f)) = hom.cast hâ‚„ hâ‚… f,
      rw â†prefunctor.map_cast,
      apply equiv.of_bijective_symm_apply_apply, },
  end,
  right_inv := begin
    fapply prefunctor.ext,
    { intros X, dsimp, apply (equiv.of_bijective Ï†.obj hÏ†obj).apply_symm_apply, },
    { intros X Y f, dsimp,
      let Eo := (equiv.of_bijective Ï†.obj hÏ†obj),
      let E := equiv.of_bijective _ (hÏ†map (Eo.symm X) (Eo.symm Y)),
      apply E.symm.injective,
      generalize_proofs h1 h2,
      simpa only [equiv.of_bijective_symm_apply_apply, embedding_like.apply_eq_iff_eq], },
  end }

/-- Two isomorphisms agreeing on `.prefunctor` are equal. -/
@[ext] lemma to_prefunctor_ext (Ï† Ïˆ : iso U V) : Ï†.to_prefunctor = Ïˆ.to_prefunctor â†’ Ï† = Ïˆ :=
begin
  refine Î» h, iso.ext _ _ h (prefunctor.ext (Î» X, Ïˆ.to_equiv.injective _)
                                            (Î» X Y f, Ïˆ.to_equiv_hom.injective _)),
  { dsimp,
    rw [Ïˆ.obj_inv_obj_eq X, â†h, Ï†.obj_inv_obj_eq X], },
  { change Ïˆ.map (Ï†.inv_prefunctor.map f) = Ïˆ.map ((Ïˆ.inv_prefunctor.map f).cast _ _),
    rw [prefunctor.map_cast, Ïˆ.map_inv_map_eq_cast, hom.cast_cast, â†prefunctor.map_cast_eq_of_eq h,
        Ï†.map_inv_map_eq_cast, hom.cast_cast], },
end

end iso

end quiver
