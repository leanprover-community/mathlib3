/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov
-/
import combinatorics.set_family.shadow
import data.fintype.basic

/-!
# Basic definitions for finite sets which are useful for combinatorics

We define a proposition asserting that a set is a set of r-sets.
-/

open finset nat
open_locale finset_family

variables {Î± : Type*}

namespace finset

section sized
variables {A B : finset (finset Î±)} {r : â„•}

/-! ### Sized -/

/-- `sized A r` states that every set in A has size r. -/
def sized (A : finset (finset Î±)) (r : â„•) : Prop := âˆ€ â¦ƒxâ¦„, x âˆˆ A â†’ card x = r

lemma sized.mono (h : A âŠ† B) (hB : B.sized r) : A.sized r := Î» x hx, hB $ h hx

/-- All sets in the union have size `r` iff both sets individually have this property. -/
lemma sized_union [decidable_eq Î±] : sized (A âˆª B) r â†” sized A r âˆ§ sized B r :=
begin
  refine âŸ¨Î» hA, âŸ¨hA.mono $ subset_union_left _ _, hA.mono $ subset_union_right _ _âŸ©, Î» hA x hx, _âŸ©,
  rw mem_union at hx,
  exact hx.elim (Î» h, hA.1 h) (Î» h, hA.2 h),
end

variables [fintype Î±] {ğ’œ : finset (finset Î±)} {s : finset Î±}

lemma mem_powerset_len_univ_iff : s âˆˆ powerset_len r (univ : finset Î±) â†” card s = r :=
mem_powerset_len.trans $ and_iff_right (subset_univ _)

lemma subset_powerset_len_univ_iff : ğ’œ âŠ† powerset_len r univ â†” sized ğ’œ r :=
by rw sized; apply forall_congr _; intro A; rw mem_powerset_len_univ_iff

alias subset_powerset_len_univ_iff  â†” _ finset.sized.subset_powerset_len_univ

lemma sized.card_le (hğ’œ : sized ğ’œ r) : card ğ’œ â‰¤ (fintype.card Î±).choose r :=
begin
  rw [fintype.card, â†card_powerset_len],
  exact card_le_of_subset hğ’œ.subset_powerset_len_univ,
end

end sized

/-!
### Slices

The `r`th slice of a set family the subset of its elements which have
cardinality `r`.
A few basic facts about slices.
-/
section slice
variables {ğ’œ : finset (finset Î±)} {A : finset Î±} {r : â„•}

/-- The `r`th slice of a set family the subset of its elements which have cardinality `r`. -/
def slice (ğ’œ : finset (finset Î±)) (r : â„•) : finset (finset Î±) := ğ’œ.filter (Î» i, i.card = r)

localized "infix ` # `:90 := finset.slice" in finset_family

/-- `A` is in the `r`th slice of `ğ’œ` iff it's in `ğ’œ` and has cardinality `r`. -/
lemma mem_slice : A âˆˆ ğ’œ # r â†” A âˆˆ ğ’œ âˆ§ A.card = r := mem_filter

/-- The `r`th slice of `ğ’œ` is a subset of `ğ’œ`. -/
lemma slice_subset : ğ’œ#r âŠ† ğ’œ := filter_subset _ _

/-- Everything in the `r`th slice of `ğ’œ` has size `r`. -/
lemma sized_slice : sized (ğ’œ#r) r := Î» _, and.right âˆ˜ mem_slice.mp

/-- Elements in distinct slices must be distinct. -/
lemma ne_of_diff_slice {ğ’œ : finset (finset Î±)} {râ‚ râ‚‚ : â„•}
  {Aâ‚ Aâ‚‚ : finset Î±} (hâ‚ : Aâ‚ âˆˆ ğ’œ#râ‚) (hâ‚‚ : Aâ‚‚ âˆˆ ğ’œ#râ‚‚) :
  râ‚ â‰  râ‚‚ â†’ Aâ‚ â‰  Aâ‚‚ :=
mt $ Î» h, (sized_slice hâ‚).symm.trans ((congr_arg card h).trans (sized_slice hâ‚‚))

variables [decidable_eq Î±]

lemma pairwise_disjoint_slice : (set.univ : set â„•).pairwise_disjoint (slice ğ’œ) :=
Î» m _ n _ hmn, disjoint_filter.2 $ Î» s hs hm hn, hmn $ hm.symm.trans hn

end slice

variables [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {A B : finset Î±} {r k : â„•}

/-- Iterated shadow of the empty set is empty. -/
lemma iter_shadow_empty (k : â„•) : shadow^[k] (âˆ… : finset (finset Î±)) = âˆ… :=
begin
  induction k with k ih,
  { refl },
  { rwa [iterate, shadow_empty] }
end

/-- Everything in the shadow is one smaller than things in the original. -/
lemma sized.shadow (hğ’œ : sized ğ’œ r) : sized (âˆ‚ğ’œ) (r - 1) :=
begin
  intros A h,
  obtain âŸ¨A, hA, i, hi, rflâŸ© := mem_shadow_iff.1 h,
  rw [card_erase_of_mem hi, hğ’œ hA],
  refl,
end

/-- `B âˆˆ âˆ‚ğ’œ` iff `B` is exactly one element less than something from `ğ’œ` -/
lemma sub_iff_shadow_one : B âˆˆ âˆ‚ğ’œ â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ (A \ B).card = 1 :=
begin
  rw mem_shadow_iff_insert_mem,
  split,
  { rintro âŸ¨i, ih, inAâŸ©,
    refine âŸ¨insert i B, inA, subset_insert _ _, _âŸ©,
    rw card_sdiff (subset_insert _ _),
    simp [card_insert_of_not_mem ih] },
  { rintro âŸ¨A, hA, a_h_hâŸ©,
    rw card_eq_one at a_h_h,
    rcases a_h_h with âŸ¨subs, j, eqâŸ©,
    refine âŸ¨j, _, _âŸ©,
    { intro a,
      have : j âˆ‰ A \ B := not_mem_sdiff_of_mem_right a,
      apply this,
      rw eq,
      apply mem_singleton_self },
    { rwa [insert_eq j B, â†eq, sdiff_union_of_subset subs] } }
end

/-- `B âˆˆ âˆ‚^k ğ’œ` iff `B` is exactly `k` elements less than something from `ğ’œ`. -/
lemma sub_iff_shadow_iter {ğ’œ : finset (finset Î±)} {B : finset Î±} (k : â„•) :
  B âˆˆ (shadow^[k] ğ’œ) â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ (A \ B).card = k :=
begin
  induction k with k ih generalizing ğ’œ B,
  { simp [sdiff_eq_empty_iff_subset],
    split,
    { intro p,
      exact âŸ¨B, p, subset.refl _, subset.refl _âŸ© },
    { rintro âŸ¨A, _, qâŸ©,
      rw â†subset.antisymm_iff at q,
      rwa q } },
  { simp only [exists_prop, function.comp_app, function.iterate_succ],
    rw @ih (âˆ‚ğ’œ) B,
    clear ih,
    split,
    { rintro âŸ¨A, hA, BsubA, card_AdiffB_is_kâŸ©,
      rw sub_iff_shadow_one at hA,
      rcases hA with âŸ¨C, CinA, AsubC, card_CdiffA_is_1âŸ©,
      refine âŸ¨C, CinA, trans BsubA AsubC, _âŸ©,
      rw card_sdiff (trans BsubA AsubC),
      rw card_sdiff BsubA at card_AdiffB_is_k,
      rw card_sdiff AsubC at card_CdiffA_is_1,
      rw [â†nat.sub_add_cancel (card_le_of_subset AsubC),
          nat.add_sub_assoc (card_le_of_subset BsubA), card_CdiffA_is_1,
          card_AdiffB_is_k, add_comm] },
    { rintro âŸ¨A, hA, hBA, hABâŸ©,
      have z : (A \ B).nonempty,
      { rw [â†finset.card_pos, hAB],
        exact nat.succ_pos _ },
      rcases z with âŸ¨i, hiâŸ©,
      have : i âˆˆ A, rw mem_sdiff at hi,
      { exact hi.1 },
      have : B âŠ† erase A i,
      { intros t th,
        apply mem_erase_of_ne_of_mem _ (hBA th),
        intro a,
        rw mem_sdiff at hi,
        rw a at th,
        exact hi.2 th },
      refine âŸ¨erase A i, _, â€¹_â€º, _âŸ©,
      { rw mem_shadow_iff,
        refine âŸ¨A, hA, i, â€¹_â€º, rflâŸ© },
      rw [card_sdiff â€¹B âŠ† erase A iâ€º,
        card_erase_of_mem â€¹i âˆˆ Aâ€º, nat.pred_sub,
        â†card_sdiff hBA, hAB],
      simp } }
end

/-- Everything in the `k`-th shadow is `k` smaller than things in the original. -/
lemma sized.shadow_iter (hğ’œ : sized ğ’œ r) : sized (shadow^[k] ğ’œ) (r-k) :=
begin
  intro B,
  rw sub_iff_shadow_iter,
  rintro âŸ¨A, hA, hBA, cardâŸ©,
  rw [card_sdiff hBA, hğ’œ hA] at card,
  rw [â†card, â†hğ’œ hA, nat.sub_sub_self (card_le_of_subset hBA)],
end

end finset
