/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import data.fintype.basic

/-!
# Down-compressions

This file defines down-compression. It is an operation on a set family that reduces its shadow.

Down-compressing `a : Î±` along `u v : Î±` means replacing `a` by `(a âŠ” u) \ v` if `a` and `u` are
disjoint and `v â‰¤ a`. In some sense, it's moving `a` from `v` to `u`.

Down-compressions are immensely useful to prove the Kruskal-Katona theorem. The idea is that
compressing a set family might decrease the size of its shadow, so iterated compressions hopefully
minimise the shadow.

## Main declarations

* `finset.non_member_section`: `ğ’œ.non_member_section a` is the subfamily of sets not containing `a`.
* `finset.member_section`: `ğ’œ.member_section a` is the image of the subfamily of sets containing
  `a` under removing `a`.
* `down.compress`: `compress u v a` is `a` compressed along `u` and `v`.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf

## Tags

compression, down-compression
-/

variables {Î± : Type*} [decidable_eq Î±] {ğ’œ â„¬ : finset (finset Î±)} {s : finset Î±} {a : Î±}

namespace finset

/-- ELements of `ğ’œ` that do not contain `a`. -/
def non_member_section (a : Î±) (ğ’œ : finset (finset Î±)) : finset (finset Î±) :=
ğ’œ.filter $ Î» s, a âˆ‰ s

/-- Image of the elements of `ğ’œ` which contain `a` under removing `a`. Finsets that do not contain
`a` such that `insert a s âˆˆ ğ’œ`. -/
def member_section (a : Î±) (ğ’œ : finset (finset Î±)) : finset (finset Î±) :=
(ğ’œ.filter $ Î» s, a âˆˆ s).image $ Î» s, erase s a

@[simp] lemma mem_non_member_section : s âˆˆ ğ’œ.non_member_section a â†” s âˆˆ ğ’œ âˆ§ a âˆ‰ s := mem_filter
@[simp] lemma mem_member_section : s âˆˆ ğ’œ.member_section a â†” insert a s âˆˆ ğ’œ âˆ§ a âˆ‰ s :=
begin
  simp_rw [member_section, mem_image, mem_filter],
  refine âŸ¨_, Î» h, âŸ¨insert a s, âŸ¨h.1, mem_insert_self _ _âŸ©, erase_insert h.2âŸ©âŸ©,
  rintro âŸ¨s, hs, rflâŸ©,
  rw insert_erase hs.2,
  exact âŸ¨hs.1, not_mem_erase _ _âŸ©,
end

lemma non_member_section_inter (a : Î±) (ğ’œ â„¬ : finset (finset Î±)) :
  (ğ’œ âˆ© â„¬).non_member_section a = ğ’œ.non_member_section a âˆ© â„¬.non_member_section a :=
filter_inter_distrib _ _ _

lemma member_section_inter (a : Î±) (ğ’œ â„¬ : finset (finset Î±)) :
  (ğ’œ âˆ© â„¬).member_section a = ğ’œ.member_section a âˆ© â„¬.member_section a :=
begin
  unfold member_section,
  rw [filter_inter_distrib, image_inter_of_inj_on _ _ ((erase_inj_on' _).mono _)],
  rw [â†coe_union, â†filter_union, coe_filter],
  exact set.inter_subset_right _ _,
end

lemma non_member_section_union (a : Î±) (ğ’œ â„¬ : finset (finset Î±)) :
  (ğ’œ âˆª â„¬).non_member_section a = ğ’œ.non_member_section a âˆª â„¬.non_member_section a :=
filter_union _ _ _

lemma member_section_union (a : Î±) (ğ’œ â„¬ : finset (finset Î±)) :
  (ğ’œ âˆª â„¬).member_section a = ğ’œ.member_section a âˆª â„¬.member_section a :=
by simp_rw [member_section, filter_union, image_union]

lemma card_member_section_add_card_non_member_section (a : Î±) (ğ’œ : finset (finset Î±)) :
  (ğ’œ.member_section a).card + (ğ’œ.non_member_section a).card = ğ’œ.card :=
begin
  rw [member_section, non_member_section, card_image_of_inj_on,
    filter_card_add_filter_neg_card_eq_card],
  exact (erase_inj_on' _).mono (Î» s hs, (mem_filter.1 hs).2),
end

@[simp] lemma member_section_member_section : (ğ’œ.member_section a).member_section a = âˆ… :=
by { ext, simp }

@[simp] lemma member_section_non_member_section : (ğ’œ.non_member_section a).member_section a = âˆ… :=
by { ext, simp }

@[simp] lemma non_member_section_member_section :
  (ğ’œ.member_section a).non_member_section a = ğ’œ.member_section a :=
by { ext, simp }

@[simp] lemma non_member_section_non_member_section :
  (ğ’œ.non_member_section a).non_member_section a = ğ’œ.non_member_section a :=
by { ext, simp }

end finset

open finset

-- The namespace is here to distinguish from other compressions.
namespace down

/-- `a`-down-compressing `ğ’œ` means removing `a` from the elements of `ğ’œ` that contain it. -/
def compress (a : Î±) (ğ’œ : finset (finset Î±)) : finset (finset Î±) :=
ğ’œ.member_section a âˆª ğ’œ.non_member_section a

localized "notation `ğ““ ` := down.compress" in finset_family

/-- `a` is in the down-compressed family iff it's in the original and its compression is in the
original, or it's not in the original but it's the compression of something in the original. -/
lemma mem_compress : s âˆˆ ğ““ a ğ’œ â†” (insert a s âˆˆ ğ’œ âˆ¨ s âˆˆ ğ’œ) âˆ§ a âˆ‰ s :=
by simp_rw [compress, mem_union, mem_member_section, mem_non_member_section, â†or_and_distrib_right]

lemma compress_union (a : Î±) (ğ’œ â„¬ : finset (finset Î±)) : ğ““ a (ğ’œ âˆª â„¬) = ğ““ a ğ’œ âˆª ğ““ a â„¬ :=
by simp_rw [compress, member_section_union, non_member_section_union, union_union_union_comm]

/-- Down-compressing a family is idempotent. -/
@[simp] lemma compress_idem (a : Î±) (ğ’œ : finset (finset Î±)) : ğ““ a (ğ““ a ğ’œ) = ğ““ a ğ’œ :=
(compress_union _ _ _).trans $ by simp [compress]

/-- Down-compressing a family reduces its size. -/
lemma card_compress_le (a : Î±) (ğ’œ : finset (finset Î±)) : (ğ““ a ğ’œ).card â‰¤ ğ’œ.card :=
(card_union_le _ _).trans_eq $ card_member_section_add_card_non_member_section _ _

end down
