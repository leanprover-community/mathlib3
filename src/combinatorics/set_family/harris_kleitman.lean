/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import algebra.big_operators.basic
import order.upper_lower

/-!
# Harris-Kleitman inequality

This file proves the Harris-Kleitman inequality. This relates `ğ’œ.card * â„¬.card` and
`2 ^ card Î± * (ğ’œ âˆ© â„¬).card` where `ğ’œ` and `â„¬` are upward- or downcard-closed finite families of
finsets. This can be interpreted as saying that any two lower sets (resp. any two upper sets)
correlate in the uniform measure.

## Main declarations

* `finset.non_member_slice`: `ğ’œ.non_member_slice a` is the subfamily of sets not containing `a`.
* `finset.member_slice`: `ğ’œ.member_slice a` is the image of the subfamily of sets containing `a`
  under removing `a`.
* `is_lower_set.le_card_inter_finset`: One form of the Harris-Kleitman inequality.

## References

* [D. J. Kleitman, *Families of non-disjoint subsets*][kleitman1966]
-/

section rearrangement
variables {Î± : Type*}

/-
TODO: The assumptions are all over the place because of the algebraic order hierarchy weirdness.
* `mul_add_mul_le_mul_add_mul_aux` ought to prove `mul_add_mul_le_mul_add_mul'` but it doesn't
  because `canonically_ordered_comm_semiring` doesn't imply `ordered_semiring`.
* The situation for `mul_add_mul_lt_mul_add_mul'` is worse because we don't have the typeclasses to
  express the correct lemma, so we are restricted to `â„•`.

Possibly, both lemmas are true without `âˆƒ e, b = a + e` and `âˆƒ f, d = c + f`, in which case the
above is nonsense.
-/

lemma mul_add_mul_le_mul_add_mul_aux [ordered_semiring Î±] {a b c d : Î±} (hb : âˆƒ e, b = a + e)
  (hd : âˆƒ f, d = c + f) (hab : a â‰¤ b) (hcd : c â‰¤ d) :
  a * d + b * c â‰¤ a * c + b * d :=
begin
  obtain âŸ¨b, rflâŸ© := hb,
  obtain âŸ¨d, rflâŸ© := hd,
  rw [mul_add, add_right_comm, mul_add, â†add_assoc],
  exact add_le_add_left (mul_le_mul_of_nonneg_right hab $ (le_add_iff_nonneg_right _).1 hcd) _,
end

lemma mul_add_mul_lt_mul_add_mul_aux [ordered_semiring Î±] {a b c d : Î±} (hb : âˆƒ e, b = a + e)
  (hd : âˆƒ f, d = c + f) (hab : a < b) (hcd : c < d) :
  a * d + b * c < a * c + b * d :=
begin
  obtain âŸ¨b, rflâŸ© := hb,
  obtain âŸ¨d, rflâŸ© := hd,
  rw [mul_add, add_right_comm, mul_add, â†add_assoc],
  exact add_lt_add_left (mul_lt_mul_of_pos_right hab $ (lt_add_iff_pos_right _).1 hcd) _,
end

lemma mul_add_mul_le_mul_add_mul [ordered_ring Î±] {a b c d : Î±} :
  a â‰¤ b â†’ c â‰¤ d â†’ a * d + b * c â‰¤ a * c + b * d :=
mul_add_mul_le_mul_add_mul_aux âŸ¨_, (add_sub_cancel'_right _ _).symmâŸ©
  âŸ¨_, (add_sub_cancel'_right _ _).symmâŸ©

lemma mul_add_mul_lt_mul_add_mul [ordered_ring Î±] {a b c d : Î±} :
  a < b â†’ c < d â†’ a * d + b * c < a * c + b * d :=
mul_add_mul_lt_mul_add_mul_aux âŸ¨_, (add_sub_cancel'_right _ _).symmâŸ©
  âŸ¨_, (add_sub_cancel'_right _ _).symmâŸ©

lemma mul_add_mul_le_mul_add_mul' [canonically_ordered_comm_semiring Î±] {a b c d : Î±} (hab : a â‰¤ b)
  (hcd : c â‰¤ d) : a * d + b * c â‰¤ a * c + b * d :=
begin
  obtain âŸ¨b, rflâŸ© := le_iff_exists_add.1 hab,
  obtain âŸ¨d, rflâŸ© := le_iff_exists_add.1 hcd,
  rw [mul_add, add_right_comm, mul_add, â†add_assoc],
  exact add_le_add_left (mul_le_mul_right' hab _) _,
end

lemma mul_add_mul_lt_mul_add_mul' {a b c d : â„•} (hab : a < b) (hcd : c < d) :
  a * d + b * c < a * c + b * d :=
mul_add_mul_lt_mul_add_mul_aux (le_iff_exists_add.1 hab.le) (le_iff_exists_add.1 hcd.le) hab hcd

end rearrangement

open_locale big_operators

variables {Î± : Type*} [decidable_eq Î±] {ğ’œ â„¬ : finset (finset Î±)} {s : finset Î±} {a : Î±}

namespace finset

lemma erase_inj_on' (a : Î±) : {s : finset Î± | a âˆˆ s}.inj_on (Î» s, erase s a) :=
Î» s hs t ht (h : s.erase a =  _), by rw [â†insert_erase hs, â†insert_erase ht, h]

lemma subset_insert_iff_of_not_mem {s t : finset Î±} {a : Î±} (h : a âˆ‰ s) : s âŠ† insert a t â†” s âŠ† t :=
by rw [subset_insert_iff, erase_eq_of_not_mem h]

lemma filter_inter_distrib (p : Î± â†’ Prop) [decidable_pred p] (s t : finset Î±) :
  (s âˆ© t).filter p = s.filter p âˆ© t.filter p :=
by { ext, simp only [mem_filter, mem_inter], exact and_and_distrib_right _ _ _ }

lemma image_inter_of_inj_on {Î² : Type*} [decidable_eq Î²] {f : Î± â†’ Î²} (s t : finset Î±)
  (hf : set.inj_on f (s âˆª t)) :
  (s âˆ© t).image f = s.image f âˆ© t.image f :=
(image_inter_subset _ _ _).antisymm $ Î» x, begin
  simp only [mem_inter, mem_image],
  rintro âŸ¨âŸ¨a, ha, rflâŸ©, b, hb, hâŸ©,
  exact âŸ¨a, âŸ¨ha, by rwa â†hf (or.inr hb) (or.inl ha) hâŸ©, rflâŸ©,
end

/-- ELements of `ğ’œ` that do not contain `a`. -/
def non_member_slice (ğ’œ : finset (finset Î±)) (a : Î±) : finset (finset Î±) := ğ’œ.filter $ Î» s, a âˆ‰ s

/-- Image of the eLements of `ğ’œ` which contain `a` under removing `a`. Finsets that do not contain
`a` such that `insert a s âˆˆ ğ’œ`. -/
def member_slice (ğ’œ : finset (finset Î±)) (a : Î±) : finset (finset Î±) :=
(ğ’œ.filter $ Î» s, a âˆˆ s).image $ Î» s, erase s a

@[simp] lemma mem_non_member_slice : s âˆˆ ğ’œ.non_member_slice a â†” s âˆˆ ğ’œ âˆ§ a âˆ‰ s := mem_filter
@[simp] lemma mem_member_slice : s âˆˆ ğ’œ.member_slice a â†” insert a s âˆˆ ğ’œ âˆ§ a âˆ‰ s :=
begin
  simp_rw [member_slice, mem_image, mem_filter],
  refine âŸ¨_, Î» h, âŸ¨insert a s, âŸ¨h.1, mem_insert_self _ _âŸ©, erase_insert h.2âŸ©âŸ©,
  rintro âŸ¨s, hs, rflâŸ©,
  rw insert_erase hs.2,
  exact âŸ¨hs.1, not_mem_erase _ _âŸ©,
end

lemma non_member_slice_inter (ğ’œ â„¬ : finset (finset Î±)) (a : Î±) :
  (ğ’œ âˆ© â„¬).non_member_slice a = ğ’œ.non_member_slice a âˆ© â„¬.non_member_slice a :=
filter_inter_distrib _ _ _

lemma member_slice_inter (ğ’œ â„¬ : finset (finset Î±)) (a : Î±) :
  (ğ’œ âˆ© â„¬).member_slice a = ğ’œ.member_slice a âˆ© â„¬.member_slice a :=
begin
  unfold member_slice,
  rw [filter_inter_distrib, image_inter_of_inj_on _ _ ((erase_inj_on' _).mono _)],
  rw [â†coe_union, â†filter_union, coe_filter],
  exact set.inter_subset_right _ _,
end

lemma card_member_slice_add_card_non_member_slice (ğ’œ : finset (finset Î±)) (a : Î±) :
  (ğ’œ.member_slice a).card + (ğ’œ.non_member_slice a).card = ğ’œ.card :=
begin
  rw [member_slice, non_member_slice, card_image_of_inj_on,
    filter_card_add_filter_neg_card_eq_card],
  exact (erase_inj_on' _).mono (Î» s hs, (mem_filter.1 hs).2),
end

end finset

open finset

lemma is_lower_set.non_member_slice (h : is_lower_set (ğ’œ : set (finset Î±))) :
  is_lower_set (ğ’œ.non_member_slice a : set (finset Î±)) :=
by { intros s t hts, simp_rw [mem_coe, mem_non_member_slice], exact and.imp (h hts) (mt $ @hts _) }

lemma is_lower_set.member_slice (h : is_lower_set (ğ’œ : set (finset Î±))) :
  is_lower_set (ğ’œ.member_slice a : set (finset Î±)) :=
begin
  rintro s t hts,
  simp_rw [mem_coe, mem_member_slice],
  exact and.imp (h $ insert_subset_insert _ hts) (mt $ @hts _),
end

lemma is_lower_set.member_slice_subset_non_member_slice (h : is_lower_set (ğ’œ : set (finset Î±))) :
  ğ’œ.member_slice a âŠ† ğ’œ.non_member_slice a :=
Î» s, by { rw [mem_member_slice, mem_non_member_slice], exact and.imp_left (h $ subset_insert _ _) }

/-- **Harris-Kleitman inequality**: Any two lower sets of finsets correlate. -/
lemma is_lower_set.le_card_inter_finset'
  (hğ’œ : is_lower_set (ğ’œ : set (finset Î±))) (hâ„¬ : is_lower_set (â„¬ : set (finset Î±)))
  (hğ’œs : âˆ€ t âˆˆ ğ’œ, t âŠ† s) (hâ„¬s : âˆ€ t âˆˆ â„¬, t âŠ† s) :
  ğ’œ.card * â„¬.card â‰¤ 2 ^ s.card * (ğ’œ âˆ© â„¬).card :=
begin
  induction s using finset.induction with a s hs ih generalizing ğ’œ â„¬,
  { simp_rw [subset_empty, â†subset_singleton_iff', subset_singleton_iff] at hğ’œs hâ„¬s,
    obtain rfl | rfl := hğ’œs,
    { simp only [card_empty, empty_inter, mul_zero, zero_mul] },
    obtain rfl | rfl := hâ„¬s,
    { simp only [card_empty, inter_empty, mul_zero, zero_mul] },
    { simp only [card_empty, pow_zero, inter_singleton_of_mem, mem_singleton, card_singleton] } },
  rw [card_insert_of_not_mem hs, â†card_member_slice_add_card_non_member_slice ğ’œ a,
    â†card_member_slice_add_card_non_member_slice â„¬ a, add_mul, mul_add, mul_add, add_comm (_ * _), add_add_add_comm],
  refine (add_le_add_right (mul_add_mul_le_mul_add_mul'
    (card_le_of_subset hğ’œ.member_slice_subset_non_member_slice) $
    card_le_of_subset hâ„¬.member_slice_subset_non_member_slice) _).trans _,
  rw [â†two_mul, pow_succ, mul_assoc],
  have hâ‚€ : âˆ€ ğ’ : finset (finset Î±), (âˆ€ t âˆˆ ğ’, t âŠ† insert a s) â†’ âˆ€ t âˆˆ ğ’.non_member_slice a, t âŠ† s,
  { rintro ğ’ hğ’ t ht,
    rw mem_non_member_slice at ht,
    exact (subset_insert_iff_of_not_mem ht.2).1 (hğ’ _ ht.1) },
  have hâ‚ : âˆ€ ğ’ : finset (finset Î±), (âˆ€ t âˆˆ ğ’, t âŠ† insert a s) â†’ âˆ€ t âˆˆ ğ’.member_slice a, t âŠ† s,
  { rintro ğ’ hğ’ t ht,
    rw mem_member_slice at ht,
    exact (subset_insert_iff_of_not_mem ht.2).1 ((subset_insert _ _).trans $ hğ’ _ ht.1) },
  refine mul_le_mul_left' _ _,
  refine (add_le_add (ih (hğ’œ.member_slice) (hâ„¬.member_slice) (hâ‚ _ hğ’œs) $ hâ‚ _ hâ„¬s) $
    ih (hğ’œ.non_member_slice) (hâ„¬.non_member_slice) (hâ‚€ _ hğ’œs) $ hâ‚€ _ hâ„¬s).trans_eq _,
  rw [â†mul_add, â†member_slice_inter, â†non_member_slice_inter,
    card_member_slice_add_card_non_member_slice],
end

variables [fintype Î±]

/-- **Harris-Kleitman inequality**: Any two lower sets of finsets correlate. -/
lemma is_lower_set.le_card_inter_finset
  (hğ’œ : is_lower_set (ğ’œ : set (finset Î±))) (hâ„¬ : is_lower_set (â„¬ : set (finset Î±))) :
  ğ’œ.card * â„¬.card â‰¤ 2 ^ fintype.card Î± * (ğ’œ âˆ© â„¬).card :=
hğ’œ.le_card_inter_finset' hâ„¬ (Î» _ _, subset_univ _) $ Î» _ _, subset_univ _

/-- **Harris-Kleitman inequality**: Upper sets and lower sets of finsets anticorrelate. -/
lemma is_upper_set.card_inter_le_finset
  (hğ’œ : is_upper_set (ğ’œ : set (finset Î±))) (hâ„¬ : is_lower_set (â„¬ : set (finset Î±))) :
  2 ^ fintype.card Î± * (ğ’œ âˆ© â„¬).card â‰¤ ğ’œ.card * â„¬.card :=
begin
  rw [â†is_lower_set_compl, â†coe_compl] at hğ’œ,
  have := hğ’œ.le_card_inter_finset hâ„¬,
  rwa [card_compl, fintype.card_finset, tsub_mul, tsub_le_iff_tsub_le, â†mul_tsub, â†card_sdiff
    (inter_subset_right _ _), sdiff_inter_self_right, sdiff_compl, _root_.inf_comm] at this,
end

/-- **Harris-Kleitman inequality**: Lower sets and upper sets of finsets anticorrelate. -/
lemma is_lower_set.card_inter_le_finset
  (hğ’œ : is_lower_set (ğ’œ : set (finset Î±))) (hâ„¬ : is_upper_set (â„¬ : set (finset Î±))) :
  2 ^ fintype.card Î± * (ğ’œ âˆ© â„¬).card â‰¤ ğ’œ.card * â„¬.card :=
by { rw [inter_comm, mul_comm ğ’œ.card], exact hâ„¬.card_inter_le_finset hğ’œ }

/-- **Harris-Kleitman inequality**: Any two upper sets of finsets correlate. -/
lemma is_upper_set.le_card_inter_finset
  (hğ’œ : is_upper_set (ğ’œ : set (finset Î±))) (hâ„¬ : is_upper_set (â„¬ : set (finset Î±))) :
  ğ’œ.card * â„¬.card â‰¤ 2 ^ fintype.card Î± * (ğ’œ âˆ© â„¬).card :=
begin
  rw [â†is_lower_set_compl, â†coe_compl] at hğ’œ,
  have := hğ’œ.card_inter_le_finset hâ„¬,
  rwa [card_compl, fintype.card_finset, tsub_mul, le_tsub_iff_le_tsub, â†mul_tsub, â†card_sdiff
    (inter_subset_right _ _), sdiff_inter_self_right, sdiff_compl, _root_.inf_comm] at this,
  { exact mul_le_mul_left' (card_le_of_subset $ inter_subset_right _ _) _ },
  { rw â†fintype.card_finset,
    exact mul_le_mul_right' (card_le_univ _) _ }
end
