/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import data.fintype.card
import order.upper_lower.basic

/-!
# Intersecting families

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines intersecting families and proves their basic properties.

## Main declarations

* `set.intersecting`: Predicate for a set of elements in a generalized boolean algebra to be an
  intersecting family.
* `set.intersecting.card_le`: An intersecting family can only take up to half the elements, because
  `a` and `aá¶œ` cannot simultaneously be in it.
* `set.intersecting.is_max_iff_card_eq`: Any maximal intersecting family takes up half the elements.

## References

* [D. J. Kleitman, *Families of non-disjoint subsets*][kleitman1966]
-/

open finset

variables {Î± : Type*}

namespace set
section semilattice_inf
variables [semilattice_inf Î±] [order_bot Î±] {s t : set Î±} {a b c : Î±}

/-- A set family is intersecting if every pair of elements is non-disjoint. -/
def intersecting (s : set Î±) : Prop := âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ âˆ€ â¦ƒbâ¦„, b âˆˆ s â†’ Â¬ disjoint a b

@[mono] lemma intersecting.mono (h : t âŠ† s) (hs : s.intersecting) : t.intersecting :=
Î» a ha b hb, hs (h ha) (h hb)

lemma intersecting.not_bot_mem (hs : s.intersecting) : âŠ¥ âˆ‰ s := Î» h, hs h h disjoint_bot_left

lemma intersecting.ne_bot (hs : s.intersecting) (ha : a âˆˆ s) : a â‰  âŠ¥ :=
ne_of_mem_of_not_mem ha hs.not_bot_mem

lemma intersecting_empty : (âˆ… : set Î±).intersecting := Î» _, false.elim

@[simp] lemma intersecting_singleton : ({a} : set Î±).intersecting â†” a â‰  âŠ¥ := by simp [intersecting]

lemma intersecting.insert (hs : s.intersecting) (ha : a â‰  âŠ¥) (h : âˆ€ b âˆˆ s, Â¬ disjoint a b) :
  (insert a s).intersecting :=
begin
  rintro b (rfl | hb) c (rfl | hc),
  { rwa disjoint_self },
  { exact h _ hc },
  { exact Î» H, h _ hb H.symm },
  { exact hs hb hc }
end

lemma intersecting_insert :
  (insert a s).intersecting â†” s.intersecting âˆ§ a â‰  âŠ¥ âˆ§ âˆ€ b âˆˆ s, Â¬ disjoint a b :=
âŸ¨Î» h, âŸ¨h.mono $ subset_insert _ _, h.ne_bot $ mem_insert _ _,
  Î» b hb, h (mem_insert _ _) $ mem_insert_of_mem _ hbâŸ©, Î» h, h.1.insert h.2.1 h.2.2âŸ©

lemma intersecting_iff_pairwise_not_disjoint :
  s.intersecting â†” s.pairwise (Î» a b, Â¬ disjoint a b) âˆ§ s â‰  {âŠ¥} :=
begin
  refine âŸ¨Î» h, âŸ¨Î» a ha b hb _, h ha hb, _âŸ©, Î» h a ha b hb hab, _âŸ©,
  { rintro rfl,
    exact intersecting_singleton.1 h rfl },
  { have := h.1.eq ha hb (not_not.2 hab),
    rw [this, disjoint_self] at hab,
    rw hab at hb,
    exact h.2 (eq_singleton_iff_unique_mem.2
      âŸ¨hb, Î» c hc, not_ne_iff.1 $ Î» H, h.1 hb hc H.symm disjoint_bot_leftâŸ©) }
end

protected lemma subsingleton.intersecting (hs : s.subsingleton) : s.intersecting â†” s â‰  {âŠ¥} :=
intersecting_iff_pairwise_not_disjoint.trans $ and_iff_right $ hs.pairwise _

lemma intersecting_iff_eq_empty_of_subsingleton [subsingleton Î±] (s : set Î±) :
  s.intersecting â†” s = âˆ… :=
begin
  refine subsingleton_of_subsingleton.intersecting.trans
    âŸ¨not_imp_comm.2 $ Î» h, subsingleton_of_subsingleton.eq_singleton_of_mem _, _âŸ©,
  { obtain âŸ¨a, haâŸ© := nonempty_iff_ne_empty.2 h,
    rwa subsingleton.elim âŠ¥ a },
  { rintro rfl,
    exact (set.singleton_nonempty _).ne_empty.symm }
end

/-- Maximal intersecting families are upper sets. -/
protected lemma intersecting.is_upper_set (hs : s.intersecting)
  (h : âˆ€ t : set Î±, t.intersecting â†’ s âŠ† t â†’ s = t) :
  is_upper_set s :=
begin
  classical,
  rintro a b hab ha,
  rw h (insert b s) _ (subset_insert _ _),
  { exact mem_insert _ _ },
  exact hs.insert (mt (eq_bot_mono hab) $ hs.ne_bot ha)
    (Î» c hc hbc, hs ha hc $ hbc.mono_left hab),
end

/-- Maximal intersecting families are upper sets. Finset version. -/
lemma intersecting.is_upper_set' {s : finset Î±} (hs : (s : set Î±).intersecting)
  (h : âˆ€ t : finset Î±, (t : set Î±).intersecting â†’ s âŠ† t â†’ s = t) :
  is_upper_set (s : set Î±) :=
begin
  classical,
  rintro a b hab ha,
  rw h (insert b s) _ (finset.subset_insert _ _),
  { exact mem_insert_self _ _ },
  rw coe_insert,
  exact hs.insert (mt (eq_bot_mono hab) $ hs.ne_bot ha)
    (Î» c hc hbc, hs ha hc $ hbc.mono_left hab),
end

end semilattice_inf

lemma intersecting.exists_mem_set {ğ’œ : set (set Î±)} (hğ’œ : ğ’œ.intersecting) {s t : set Î±}
  (hs : s âˆˆ ğ’œ) (ht : t âˆˆ ğ’œ) : âˆƒ a, a âˆˆ s âˆ§ a âˆˆ t :=
not_disjoint_iff.1 $ hğ’œ hs ht

lemma intersecting.exists_mem_finset [decidable_eq Î±] {ğ’œ : set (finset Î±)} (hğ’œ : ğ’œ.intersecting)
  {s t : finset Î±} (hs : s âˆˆ ğ’œ) (ht : t âˆˆ ğ’œ) : âˆƒ a, a âˆˆ s âˆ§ a âˆˆ t :=
not_disjoint_iff.1 $ disjoint_coe.not.2 $ hğ’œ hs ht

variables [boolean_algebra Î±]

lemma intersecting.not_compl_mem {s : set Î±} (hs : s.intersecting) {a : Î±} (ha : a âˆˆ s) : aá¶œ âˆ‰ s :=
Î» h, hs ha h disjoint_compl_right

lemma intersecting.not_mem {s : set Î±} (hs : s.intersecting) {a : Î±} (ha : aá¶œ âˆˆ s) : a âˆ‰ s :=
Î» h, hs ha h disjoint_compl_left

lemma intersecting.disjoint_map_compl {s : finset Î±}
  (hs : (s : set Î±).intersecting) :
  disjoint s (s.map âŸ¨compl, compl_injectiveâŸ©) :=
begin
  rw finset.disjoint_left,
  rintro x hx hxc,
  obtain âŸ¨x, hx', rflâŸ© := mem_map.mp hxc,
  exact hs.not_compl_mem hx' hx,
end

lemma intersecting.card_le [fintype Î±] {s : finset Î±}
  (hs : (s : set Î±).intersecting) : 2 * s.card â‰¤ fintype.card Î± :=
begin
  classical,
  refine (s.disj_union _ hs.disjoint_map_compl).card_le_univ.trans_eq' _,
  rw [two_mul, card_disj_union, card_map],
end

variables [nontrivial Î±] [fintype Î±] {s : finset Î±}

-- Note, this lemma is false when `Î±` has exactly one element and boring when `Î±` is empty.
lemma intersecting.is_max_iff_card_eq (hs : (s : set Î±).intersecting) :
  (âˆ€ t : finset Î±, (t : set Î±).intersecting â†’ s âŠ† t â†’ s = t) â†” 2 * s.card = fintype.card Î± :=
begin
  classical,
  refine âŸ¨Î» h, _, Î» h t ht hst, finset.eq_of_subset_of_card_le hst $
    le_of_mul_le_mul_left (ht.card_le.trans_eq h.symm) two_posâŸ©,
  suffices : s.disj_union (s.map âŸ¨compl, compl_injectiveâŸ©) (hs.disjoint_map_compl) = finset.univ,
  { rw [fintype.card, â†this, two_mul, card_disj_union, card_map] },
  rw [â†coe_eq_univ, disj_union_eq_union, coe_union, coe_map, function.embedding.coe_fn_mk,
    image_eq_preimage_of_inverse compl_compl compl_compl],
  refine eq_univ_of_forall (Î» a, _),
  simp_rw [mem_union, mem_preimage],
  by_contra' ha,
  refine s.ne_insert_of_not_mem _ ha.1 (h _ _ $ s.subset_insert _),
  rw coe_insert,
  refine hs.insert _ (Î» b hb hab, ha.2 $ (hs.is_upper_set' h) hab.le_compl_left hb),
  rintro rfl,
  have := h {âŠ¤} (by { rw coe_singleton, exact intersecting_singleton.2 top_ne_bot }),
  rw compl_bot at ha,
  rw coe_eq_empty.1 ((hs.is_upper_set' h).not_top_mem.1 ha.2) at this,
  exact finset.singleton_ne_empty _ (this $ empty_subset _).symm,
end

lemma intersecting.exists_card_eq (hs : (s : set Î±).intersecting) :
  âˆƒ t, s âŠ† t âˆ§ 2 * t.card = fintype.card Î± âˆ§ (t : set Î±).intersecting :=
begin
  have := hs.card_le,
  rw [mul_comm, â†nat.le_div_iff_mul_le' two_pos] at this,
  revert hs,
  refine s.strong_downward_induction_on _ this,
  rintro s ih hcard hs,
  by_cases âˆ€ t : finset Î±, (t : set Î±).intersecting â†’ s âŠ† t â†’ s = t,
  { exact âŸ¨s, subset.rfl, hs.is_max_iff_card_eq.1 h, hsâŸ© },
  push_neg at h,
  obtain âŸ¨t, ht, hstâŸ© := h,
  refine (ih _ (_root_.ssubset_iff_subset_ne.2 hst) ht).imp (Î» u, and.imp_left hst.1.trans),
  rw [nat.le_div_iff_mul_le' two_pos, mul_comm],
  exact ht.card_le,
end

end set
