/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov
-/

import data.fintype.basic
import data.nat.choose
import combinatorics.set_family.basic
import order.antichain

/-!
# Shadows

This file proves the local LYM and LYM theorems, as well as Sperner's theorem.

## Main declarations

* `local_lym`
* `lubell_yamamoto_meshalkin`
* `sperner`

## Notation

We introduce the notation âˆ‚ to denote the shadow.
We also maintain the convention that A, B, ... denote sets (usually finset Î±),
ğ’œ, â„¬, ... denote set families, i.e. `finset (finset Î±)` and lower-case letters
denote elements of the ground set Î±.

## References

* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, lym, slice, sperner, antichain
-/

open finset fintype nat
open_locale finset_family

variables {Î± : Type*}

/-!
### Build up and proof of local LYM

If there is a fintype Î± which is our universe, informally write `Î±^(r)` for the
`set {A : finset Î± | |A| = r}`. Then if `ğ’œ` is a subset of `Î±^(r)`, we get that `âˆ‚ğ’œ`
is a subset of `Î±^(r-1)`.
The local LYM inequality says `ğ’œ` 'takes up less' of `Î±^(r)` than `âˆ‚ğ’œ` takes up of
`Î±^(r-1)`. In particular,
`|ğ’œ| / choose |Î±| r â‰¤ |âˆ‚ğ’œ| / choose |Î±| (r-1)`
-/

namespace finset

section local_lym
variables [decidable_eq Î±]

/-- Start by multiplying out the inequality so it's in a slightly nicer form. -/
lemma multiply_out {A B n r : â„•} (hr1 : 1 â‰¤ r) (hr2 : r â‰¤ n)
  (h : A * r â‰¤ B * (n - r + 1)) :
  (A : â„š) / nat.choose n r â‰¤ B / nat.choose n (r-1) :=
begin
  rw div_le_div_iff; norm_cast,
  { apply le_of_mul_le_mul_right _ â€¹0 < râ€º,
    cases r,
    { simp },
    rw nat.succ_eq_add_one at *,
    rw [â†nat.sub_add_comm hr2, nat.add_sub_add_right] at h,
    convert nat.mul_le_mul_right (n.choose r) h using 1;
    { simp [mul_assoc, nat.choose_succ_right_eq],
      left,
      ac_refl } },
{ apply nat.choose_pos hr2 },
  { apply nat.choose_pos (le_trans (nat.pred_le _) hr2) }
end

variables {ğ’œ : finset (finset Î±)} {r : â„•}

/-- We'll prove local LYM by a double counting argument. Here's the first set
we'll count, which is effectively `{(A, B) | A âˆˆ ğ’œ, B âˆˆ A.image (erase A)}`. -/
def from_above (ğ’œ : finset (finset Î±)) : finset (finset Î± Ã— finset Î±) :=
ğ’œ.sup $ Î» A, A.image $ Î» x, (A, erase A x)

/-- Find how big `from_above` is: for each `A âˆˆ ğ’œ` there are `r` possible `B`, giving the
exact cardinality. -/
lemma _root_.set.sized.card_from_above (hğ’œ : (ğ’œ : set (finset Î±)).sized r) :
  (from_above ğ’œ).card = ğ’œ.card * r :=
begin
  rw [from_above, sup_eq_bUnion, card_bUnion],
  { convert sum_const_nat _,
    refine Î» x hx, (card_image_of_inj_on $ Î» a ha b hb h, _).trans (hğ’œ hx),
    exact x.erase_inj_on ha hb (prod.mk.inj h).2 },
  { simp only [disjoint_left, mem_image],
    rintro _ _ _ _ h a âŸ¨_, _, rflâŸ© âŸ¨_, _, aâ‚‚âŸ©,
    exact h (prod.mk.inj aâ‚‚.symm).1 }
end

variables [fintype Î±]

/-- Here's the second set we'll count. We're trying to get the same set, but we
count `B` first, so we overestimate a bit. It's pretty much
`{(A, B) | B âˆˆ âˆ‚ğ’œ, âˆƒ i âˆ‰ B: A = B âˆª i}` -/
def from_below (ğ’œ : finset (finset Î±)) : finset (finset Î± Ã— finset Î±) :=
(âˆ‚ğ’œ).sup (Î» B, (univ \ B).image (Î» x, (insert x B, B)))

lemma from_above_subset_from_below : from_above ğ’œ âŠ† from_below ğ’œ :=
begin
  rintro âŸ¨A, BâŸ©,
  simp only [from_above, from_below, mem_sup, mem_shadow_iff, true_and, and_imp,
    exists_prop, mem_sdiff, mem_image, prod.mk.inj_iff, mem_univ, exists_imp_distrib],
  rintro A hA x hx rfl rfl,
  exact âŸ¨A.erase x, âŸ¨A, hA, x, hx, rflâŸ©, x, not_mem_erase _ _, insert_erase hx, rflâŸ©,
end

/-- We can also find how big the second set is: for each `B` there are `|Î±| - r + 1` choices for
what to put into it. -/
lemma _root_.set.sized.card_from_below (hğ’œ : (ğ’œ : set (finset Î±)).sized r) :
  (from_below ğ’œ).card = (âˆ‚ğ’œ).card * (fintype.card Î± - (r - 1)) :=
begin
  rw [from_below, sup_eq_bUnion, card_bUnion],
  { apply sum_const_nat,
    intros,
    rw [card_image_of_inj_on, card_univ_diff, hğ’œ.shadow H],
    intros x1 x1h _ _ h,
    injection h,
    have q := mem_insert_self x1 x,
    rw [h_1, mem_insert] at q,
    apply q.resolve_right (mem_sdiff.1 x1h).2 },
  intros _ _ _ _ hxy,
  rw disjoint_left,
  simp_rw [mem_image, not_exists, exists_prop, mem_sdiff,
            mem_univ, true_and, exists_imp_distrib,
            prod.forall, prod.mk.inj_iff, and_imp, not_and],
  rintro _ b i hi rfl rfl j hj k,
  exact hxy.symm,
end

/-- The local LYM inequality says `ğ’œ` 'takes up less' of `Î±^(r)` than `âˆ‚ğ’œ` takes up of `Î±^(r - 1)`.
In particular, `|ğ’œ| / choose |Î±| r â‰¤ |âˆ‚ğ’œ| / choose |Î±| (r-1)`. -/
theorem local_lym (hr : 1 â‰¤ r) (hğ’œ : (ğ’œ : set (finset Î±)).sized r) :
  (ğ’œ.card : â„š) / (fintype.card Î±).choose r â‰¤ (âˆ‚ğ’œ).card / (fintype.card Î±).choose (r-1) :=
begin
  cases lt_or_le (fintype.card Î±) r with z hr',
  -- Take care of the r > n case: it's trivial
  { rw [choose_eq_zero_of_lt z, cast_zero, div_zero],
    refine div_nonneg _ _; norm_cast,
    any_goals { apply nat.zero_le } },
  { apply multiply_out hr hr',
  -- Multiply out, convert to the cardinality forms we got above and done
    rw [â†hğ’œ.card_from_above, â†tsub_tsub_assoc hr' hr, â†hğ’œ.card_from_below],
    exact card_le_of_subset from_above_subset_from_below }
end

end local_lym

/-!
The LYM inequality says âˆ‘_i |A#i|/(n choose i) â‰¤ 1 for an antichain A.
Observe that A#i is all the stuff in A which has size i, and the collection of
subsets of (fin n) with size i has size (n choose i).
So, |A#i|/(n choose i) represents how much of each that A can take up.

Other proofs of LYM exist, but we'll do it by applying local LYM.
-/
section lym

variables [fintype Î±]

/-- An inductive definition, from the top down.
`falling ğ’œ k` is all the sets with cardinality (card Î± - k) which are a
subset of something in ğ’œ. -/
def falling [decidable_eq Î±] (ğ’œ : finset (finset Î±)) : Î  (k : â„•), finset (finset Î±)
| 0       := ğ’œ#(fintype.card Î±)
| (k + 1) := ğ’œ#(fintype.card Î± - (k + 1)) âˆª shadow (falling k)

/--
Everything in the kth fallen has size `n-k`
-/
lemma falling_sized [decidable_eq Î±] (ğ’œ : finset (finset Î±)) (k : â„•) :
  (falling ğ’œ k : set (finset Î±)).sized (fintype.card Î± - k) :=
begin
  induction k with k ih,
  { exact sized_slice },
  { rw [falling, coe_union],
    exact set.sized_union.2 âŸ¨sized_slice, ih.shadowâŸ© }
end

/--
Here's the first key proposition, helping to give the disjointness
property in the next lemma.
-/
theorem antichain_prop [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {r k : â„•}
  (hk : k â‰¤ fintype.card Î±) (hr : r < k) (H : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  âˆ€ A âˆˆ ğ’œ#(fintype.card Î± - k), âˆ€ B âˆˆ âˆ‚ (falling ğ’œ r), Â¬(A âŠ† B) :=
begin
  intros A HA B HB k,
  obtain âŸ¨C, HC, _âŸ© := exists_subset_of_mem_shadow HB,
  replace k := trans k â€¹B âŠ† Câ€º,
  clear HB h_h B,
  induction r with r ih generalizing A C;
  rw falling at HC,
  any_goals { rw mem_union at HC, cases HC },
  any_goals { refine H (mem_slice.1 HA).1 (mem_slice.1 HC).1 _ â€¹A âŠ† Câ€º,
              apply ne_of_mem_slice HA HC _,
              apply ne_of_lt },
  { apply nat.sub_lt_of_pos_le _ _ hr hk },
  { mono },
  { obtain âŸ¨_, HB', HB''âŸ© := exists_subset_of_mem_shadow HC,
    exact ih (lt_of_succ_lt hr) _ _ HA HB' (trans k_1 HB'') }
end

/-- This tells us that `falling ğ’œ k` is disjoint from the` n - (k + 1)`-sized elements of `ğ’œ`,
thanks to the antichain property. -/
lemma _root_.is_antichain.disjoint_falling_slice [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {k : â„•}
  (H : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) (hk : k + 1 â‰¤ fintype.card Î±) :
  disjoint (âˆ‚ (falling ğ’œ k)) (ğ’œ#(fintype.card Î± - (k + 1))) :=
disjoint_right.2 $ Î» A HA HB,
  antichain_prop hk (lt_add_one k) H A HA A HB (subset.refl _)

/-- In particular, we can use induction and local LYM to get a bound on any top
part of the sum in LYM in terms of the size of `falling ğ’œ k`. -/
lemma card_falling [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {k : â„•} (hk : k â‰¤ fintype.card Î±)
  (H : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  (range (k + 1)).sum
    (Î» r, ((ğ’œ#(fintype.card Î± - r)).card : â„š) / (fintype.card Î±).choose (fintype.card Î± - r))
  â‰¤ (falling ğ’œ k).card / (fintype.card Î±).choose (fintype.card Î± - k) :=
begin
  induction k with k ih,
  { simp [falling] },
  rw [sum_range_succ, falling, union_comm, card_disjoint_union (H.disjoint_falling_slice hk),
    cast_add, _root_.add_div],
  exact add_le_add_right
    ((ih $ k.le_succ.trans hk).trans $ local_lym (le_tsub_of_add_le_left hk) $ falling_sized _ _) _,
end

/-- A stepping-stone lemma to get to LYM. -/
lemma card_fallen [decidable_eq Î±] {ğ’œ : finset (finset Î±)}
  (H : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  (range (fintype.card Î± + 1)).sum (Î» r, ((ğ’œ#r).card : â„š) / (fintype.card Î±).choose r)
  â‰¤ (falling ğ’œ (fintype.card Î±)).card / (fintype.card Î±).choose 0 :=
begin
  rw [â†nat.sub_self (fintype.card Î±)],
  convert â†card_falling le_rfl H using 1,
  apply sum_flip (Î» r, ((ğ’œ#r).card : â„š) / (fintype.card Î±).choose r),
end

/-- The LYM inequality says `âˆ‘_i |A#i|/(n choose i) â‰¤ 1` for an antichain `A`.
Observe that `A#i` is all the stuff in `A` which has size `i`, and the collection of
sets of `fin n` with size `i` has size `n choose i`.
So `|A#i|/(n choose i)` represents how much of each `A` can take up.

The proof is easy using the developed lemmas above. -/
theorem lubell_yamamoto_meshalkin {ğ’œ : finset (finset Î±)} (H : is_antichain (âŠ†)
  (ğ’œ : set (finset Î±))) :
  (range (fintype.card Î± + 1)).sum (Î» r, ((ğ’œ#r).card : â„š) / (fintype.card Î±).choose r) â‰¤ 1 :=
begin
  classical,
  transitivity,
  { apply card_fallen H },
  rw div_le_iff; norm_cast,
  { simpa only [mul_one, nat.choose_zero_right, nat.sub_self]
      using (falling_sized ğ’œ (fintype.card Î±)).card_le },
  exact nat.choose_pos (nat.zero_le _),
end

end lym

/-- Sperner's theorem gives a bound on the size of an antichain. This can be proved in a few ways,
but this uses the machinery already developed about LYM. The idea is simple: with LYM, we get a
bound on how much of `A` can have any particular size.  So, to maximise the size of A, we'd like to
fit it all into the term with the biggest denominator. In other words,
`âˆ‘_i |A#i|/(n choose (n/2)) â‰¤ âˆ‘_i |A#i|/(n choose i) â‰¤ 1`, so `âˆ‘_i |A#i| â‰¤ (n choose (n/2))` as
required. -/
theorem sperner [fintype Î±] {ğ’œ : finset (finset Î±)} (H : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  ğ’œ.card â‰¤ (fintype.card Î±).choose (fintype.card Î± / 2) :=
begin
  classical,
  have : (range (fintype.card Î± + 1)).sum (Î» (r : â„•), ((ğ’œ#r).card : â„š) /
    (fintype.card Î±).choose (fintype.card Î±/2)) â‰¤ 1,
  { apply le_trans _ (lubell_yamamoto_meshalkin H),
    apply sum_le_sum,
    intros r hr,
    apply div_le_div_of_le_left; norm_cast,
    { apply nat.zero_le },
    { apply choose_pos, rw mem_range at hr, rwa â†nat.lt_succ_iff },
    { apply choose_le_middle } },
  rw [â†sum_div, â†nat.cast_sum, div_le_one] at this,
  { norm_cast at this,
    rw â†card_bUnion at this,
    convert this,
    simp only [ext_iff, mem_slice, mem_bUnion, exists_prop, mem_range, lt_succ_iff],
    intro a,
    split,
    { intro ha,
      refine âŸ¨a.card, card_le_of_subset (subset_univ _), ha, rflâŸ© },
    { rintro âŸ¨_, _, q, _âŸ©,
      exact q },
    intros x _ y _ ne,
    rw disjoint_left,
    intros a Ha k,
    exact ne_of_mem_slice Ha k ne rfl },
  norm_cast,
  apply choose_pos,
  apply nat.div_le_self,
end

end finset
