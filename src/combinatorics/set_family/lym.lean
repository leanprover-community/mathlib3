/-
Copyright (c) 2022 Bhavik Mehta, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import algebra.big_operators.ring
import algebra.order.field.basic
import combinatorics.double_counting
import combinatorics.set_family.shadow
import data.rat.order

/-!
# Lubell-Yamamoto-Meshalkin inequality and Sperner's theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file proves the local LYM and LYM inequalities as well as Sperner's theorem.

## Main declarations

* `finset.card_div_choose_le_card_shadow_div_choose`: Local Lubell-Yamamoto-Meshalkin inequality.
  The shadow of a set `ğ’œ` in a layer takes a greater proportion of its layer than `ğ’œ` does.
* `finset.sum_card_slice_div_choose_le_one`: Lubell-Yamamoto-Meshalkin inequality. The sum of
  densities of `ğ’œ` in each layer is at most `1` for any antichain `ğ’œ`.
* `is_antichain.sperner`: Sperner's theorem. The size of any antichain in `finset Î±` is at most the
  size of the maximal layer of `finset Î±`. It is a corollary of `sum_card_slice_div_choose_le_one`.

## TODO

Prove upward local LYM.

Provide equality cases. Local LYM gives that the equality case of LYM and Sperner is precisely when
`ğ’œ` is a middle layer.

`falling` could be useful more generally in grade orders.

## References

* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, lym, slice, sperner, antichain
-/

open finset nat
open_locale big_operators finset_family

variables {ğ•œ Î± : Type*} [linear_ordered_field ğ•œ]

namespace finset

/-! ### Local LYM inequality -/

section local_lym
variables [decidable_eq Î±] [fintype Î±] {ğ’œ : finset (finset Î±)} {r : â„•}

/-- The downward **local LYM inequality**, with cancelled denominators. `ğ’œ` takes up less of `Î±^(r)`
(the finsets of card `r`) than `âˆ‚ğ’œ` takes up of `Î±^(r - 1)`. -/
lemma card_mul_le_card_shadow_mul (hğ’œ : (ğ’œ : set (finset Î±)).sized r) :
  ğ’œ.card * r â‰¤ (âˆ‚ğ’œ).card * (fintype.card Î± - r + 1) :=
begin
  refine card_mul_le_card_mul' (âŠ†) (Î» s hs, _) (Î» s hs, _),
  { rw [â†hğ’œ hs, â†card_image_of_inj_on s.erase_inj_on],
    refine card_le_of_subset _,
    simp_rw [image_subset_iff, mem_bipartite_below],
    exact Î» a ha, âŸ¨erase_mem_shadow hs ha, erase_subset _ _âŸ© },
  refine le_trans _ tsub_tsub_le_tsub_add,
  rw [â†hğ’œ.shadow hs, â†card_compl, â†card_image_of_inj_on (insert_inj_on' _)],
  refine card_le_of_subset (Î» t ht, _),
  apply_instance,
  rw mem_bipartite_above at ht,
  have : âˆ… âˆ‰ ğ’œ,
  { rw [â†mem_coe, hğ’œ.empty_mem_iff, coe_eq_singleton],
    rintro rfl,
    rwa shadow_singleton_empty at hs },
  obtain âŸ¨a, ha, rflâŸ© :=
    exists_eq_insert_iff.2 âŸ¨ht.2, by rw [(sized_shadow_iff this).1 hğ’œ.shadow ht.1, hğ’œ.shadow hs]âŸ©,
  exact mem_image_of_mem _ (mem_compl.2 ha),
end

/-- The downward **local LYM inequality**. `ğ’œ` takes up less of `Î±^(r)` (the finsets of card `r`)
than `âˆ‚ğ’œ` takes up of `Î±^(r - 1)`. -/
lemma card_div_choose_le_card_shadow_div_choose (hr : r â‰  0) (hğ’œ : (ğ’œ : set (finset Î±)).sized r) :
  (ğ’œ.card : ğ•œ) / (fintype.card Î±).choose r â‰¤ (âˆ‚ğ’œ).card / (fintype.card Î±).choose (r - 1) :=
begin
  obtain hr' | hr' := lt_or_le (fintype.card Î±) r,
  { rw [choose_eq_zero_of_lt hr', cast_zero, div_zero],
    exact div_nonneg (cast_nonneg _) (cast_nonneg _) },
  replace hğ’œ := card_mul_le_card_shadow_mul hğ’œ,
  rw div_le_div_iff; norm_cast,
  { cases r,
    { exact (hr rfl).elim },
    rw nat.succ_eq_add_one at *,
    rw [tsub_add_eq_add_tsub hr', add_tsub_add_eq_tsub_right] at hğ’œ,
    apply le_of_mul_le_mul_right _ (pos_iff_ne_zero.2 hr),
    convert nat.mul_le_mul_right ((fintype.card Î±).choose r) hğ’œ using 1,
    { simp [mul_assoc, nat.choose_succ_right_eq],
      exact or.inl (mul_comm _ _) },
    { simp only [mul_assoc, choose_succ_right_eq, mul_eq_mul_left_iff],
      exact or.inl (mul_comm _ _) } },
  { exact nat.choose_pos hr' },
  { exact nat.choose_pos (r.pred_le.trans hr') }
end

end local_lym

/-! ### LYM inequality -/

section lym
section falling
variables [decidable_eq Î±] (k : â„•) (ğ’œ : finset (finset Î±))

/-- `falling k ğ’œ` is all the finsets of cardinality `k` which are a subset of something in `ğ’œ`. -/
def falling : finset (finset Î±) := ğ’œ.sup $ powerset_len k

variables {ğ’œ k} {s : finset Î±}

lemma mem_falling : s âˆˆ falling k ğ’œ â†” (âˆƒ t âˆˆ ğ’œ, s âŠ† t) âˆ§ s.card = k :=
by simp_rw [falling, mem_sup, mem_powerset_len, exists_and_distrib_right]

variables (ğ’œ k)

lemma sized_falling : (falling k ğ’œ : set (finset Î±)).sized k := Î» s hs, (mem_falling.1 hs).2

lemma slice_subset_falling : ğ’œ # k âŠ† falling k ğ’œ :=
Î» s hs, mem_falling.2 $ (mem_slice.1 hs).imp_left $ Î» h, âŸ¨s, h, subset.refl _âŸ©

lemma falling_zero_subset : falling 0 ğ’œ âŠ† {âˆ…} :=
subset_singleton_iff'.2 $ Î» t ht, card_eq_zero.1 $ sized_falling _ _ ht

lemma slice_union_shadow_falling_succ : ğ’œ # k âˆª âˆ‚ (falling (k + 1) ğ’œ) = falling k ğ’œ :=
begin
  ext s,
  simp_rw [mem_union, mem_slice, mem_shadow_iff, exists_prop, mem_falling],
  split,
  { rintro (h | âŸ¨s, âŸ¨âŸ¨t, ht, hstâŸ©, hsâŸ©, a, ha, rflâŸ©),
    { exact âŸ¨âŸ¨s, h.1, subset.refl _âŸ©, h.2âŸ© },
    refine âŸ¨âŸ¨t, ht, (erase_subset _ _).trans hstâŸ©, _âŸ©,
    rw [card_erase_of_mem ha, hs],
    refl },
  { rintro âŸ¨âŸ¨t, ht, hstâŸ©, hsâŸ©,
    by_cases s âˆˆ ğ’œ,
    { exact or.inl âŸ¨h, hsâŸ© },
    obtain âŸ¨a, ha, hstâŸ© := ssubset_iff.1
      (ssubset_of_subset_of_ne hst (ht.ne_of_not_mem h).symm),
    refine or.inr âŸ¨insert a s, âŸ¨âŸ¨t, ht, hstâŸ©, _âŸ©, a, mem_insert_self _ _, erase_insert haâŸ©,
    rw [card_insert_of_not_mem ha, hs] }
end

variables {ğ’œ k}

/-- The shadow of `falling m ğ’œ` is disjoint from the `n`-sized elements of `ğ’œ`, thanks to the
antichain property. -/
lemma _root_.is_antichain.disjoint_slice_shadow_falling {m n : â„•}
  (hğ’œ : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  disjoint (ğ’œ # m) (âˆ‚ (falling n ğ’œ)) :=
disjoint_right.2 $ Î» s hâ‚ hâ‚‚,
begin
  simp_rw [mem_shadow_iff, exists_prop, mem_falling] at hâ‚,
  obtain âŸ¨s, âŸ¨âŸ¨t, ht, hstâŸ©, hsâŸ©, a, ha, rflâŸ© := hâ‚,
  refine hğ’œ (slice_subset hâ‚‚) ht _ ((erase_subset _ _).trans hst),
  rintro rfl,
  exact not_mem_erase _ _ (hst ha),
end

/-- A bound on any top part of the sum in LYM in terms of the size of `falling k ğ’œ`. -/
lemma le_card_falling_div_choose [fintype Î±] (hk : k â‰¤ fintype.card Î±)
  (hğ’œ : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  âˆ‘ r in range (k + 1),
    ((ğ’œ # (fintype.card Î± - r)).card : ğ•œ) / (fintype.card Î±).choose (fintype.card Î± - r)
      â‰¤ (falling (fintype.card Î± - k) ğ’œ).card / (fintype.card Î±).choose (fintype.card Î± - k) :=
begin
  induction k with k ih,
  { simp only [tsub_zero, cast_one, cast_le, sum_singleton, div_one, choose_self, range_one],
    exact card_le_of_subset (slice_subset_falling _ _) },
  rw succ_eq_add_one at *,
  rw [sum_range_succ, â†slice_union_shadow_falling_succ,
    card_disjoint_union hğ’œ.disjoint_slice_shadow_falling, cast_add, _root_.add_div, add_comm],
  rw [â†tsub_tsub, tsub_add_cancel_of_le (le_tsub_of_add_le_left hk)],
  exact add_le_add_left ((ih $ le_of_succ_le hk).trans $ card_div_choose_le_card_shadow_div_choose
    (tsub_pos_iff_lt.2 $ nat.succ_le_iff.1 hk).ne' $ sized_falling _ _) _,
end

end falling

variables {ğ’œ : finset (finset Î±)} {s : finset Î±} {k : â„•}

/-- The **Lubell-Yamamoto-Meshalkin inequality**. If `ğ’œ` is an antichain, then the sum of the
proportion of elements it takes from each layer is less than `1`. -/
lemma sum_card_slice_div_choose_le_one [fintype Î±] (hğ’œ : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  âˆ‘ r in range (fintype.card Î± + 1), ((ğ’œ # r).card : ğ•œ) / (fintype.card Î±).choose r â‰¤ 1 :=
begin
  classical,
  rw â†sum_flip,
  refine (le_card_falling_div_choose le_rfl hğ’œ).trans _,
  rw div_le_iff; norm_cast,
  { simpa only [nat.sub_self, one_mul, nat.choose_zero_right, falling]
      using (sized_falling 0 ğ’œ).card_le },
  { rw [tsub_self, choose_zero_right],
    exact zero_lt_one }
end

end lym

/-! ### Sperner's theorem -/

/-- **Sperner's theorem**. The size of an antichain in `finset Î±` is bounded by the size of the
maximal layer in `finset Î±`. This precisely means that `finset Î±` is a Sperner order. -/
lemma _root_.is_antichain.sperner [fintype Î±] {ğ’œ : finset (finset Î±)}
  (hğ’œ : is_antichain (âŠ†) (ğ’œ : set (finset Î±))) :
  ğ’œ.card â‰¤ (fintype.card Î±).choose (fintype.card Î± / 2) :=
begin
  classical,
  suffices : âˆ‘ r in Iic (fintype.card Î±),
    ((ğ’œ # r).card : â„š) / (fintype.card Î±).choose (fintype.card Î± / 2) â‰¤ 1,
  { rwa [â†sum_div, â†nat.cast_sum, div_le_one, cast_le, sum_card_slice] at this,
    norm_cast,
    exact choose_pos (nat.div_le_self _ _) },
  rw [Iic_eq_Icc, â†Ico_succ_right, bot_eq_zero, Ico_zero_eq_range],
  refine (sum_le_sum $ Î» r hr, _).trans (sum_card_slice_div_choose_le_one hğ’œ),
  rw mem_range at hr,
  refine div_le_div_of_le_left _ _ _; norm_cast,
  { exact nat.zero_le _ },
  { exact choose_pos (lt_succ_iff.1 hr) },
  { exact choose_le_middle _ _ }
end

end finset
