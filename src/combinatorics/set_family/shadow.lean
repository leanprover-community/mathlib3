/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import data.finset.lattice

/-!
# Shadows

This file defines shadows of a set family. The shadow of a set family is the set family of sets we
get by removing any element from any set of the original family. If one pictures `finset Î±` as a big
hypercube (each dimension being membership of a given element), then taking the shadow corresponds
to projecting each finset down once in all available directions.

## Main definitions

The `shadow` of a set family is everything we can get by removing an element from each set.

## Notation

`âˆ‚ ğ’œ` is notation for `shadow ğ’œ`. It is situated in locale `finset_family`.

We also maintain the convention that `a, b : Î±` are elements of the ground type, `A, B : finset Î±`
are finsets, and `ğ’œ, â„¬ : finset (finset Î±)` are finset families.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, set family
-/

open finset nat

variables {Î± : Type*}

/-!
### Shadows

The shadow of a set family is everything we can get by removing an element
from each set.

This section develops the introductory theory of shadows, with some lemmas on
iterated shadows as well.
-/

variables [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {A B : finset Î±} {a : Î±}

/-- The shadow of a set family `ğ’œ` is all sets we can get by removing one element
from any set in `ğ’œ`, and the (`k` times) iterated shadow is all sets we can get
by removing k elements from any set in `ğ’œ`. -/
def shadow (ğ’œ : finset (finset Î±)) : finset (finset Î±) := ğ’œ.sup (Î» A, A.image (erase A))

localized "notation `âˆ‚ `:90 := shadow" in finset_family

/-- The shadow of the empty set is empty. -/
lemma shadow_empty : âˆ‚ (âˆ… : finset (finset Î±)) = âˆ… := rfl

/-- The shadow is monotone. -/
lemma shadow_monotone : monotone (shadow : finset (finset Î±) â†’ finset (finset Î±)) :=
Î» ğ’œ â„¬, sup_mono

/-- `A` is in the shadow of `ğ’œ` iff there is an `B âˆˆ ğ’œ` from which we can remove one element to
get `A`. -/
lemma mem_shadow_iff_eq_erase : A âˆˆ âˆ‚ ğ’œ â†” âˆƒ B âˆˆ ğ’œ, âˆƒ a âˆˆ B, erase B a = A :=
by simp only [shadow, mem_sup, mem_image]

lemma erase_mem_shadow (hA : A âˆˆ ğ’œ) (ha : a âˆˆ A) : erase A a âˆˆ âˆ‚ ğ’œ :=
mem_shadow_iff_eq_erase.2 âŸ¨A, hA, a, ha, rflâŸ©

/-- `B` is in the shadow of `ğ’œ` iff we can an element to it so that the resulting finset is in `ğ’œ`.
-/
lemma mem_shadow_iff_insert_mem : A âˆˆ âˆ‚ ğ’œ â†” âˆƒ a âˆ‰ A, insert a A âˆˆ ğ’œ :=
begin
  refine mem_shadow_iff_eq_erase.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨A, hA, a, ha, rflâŸ©,
    refine âŸ¨a, not_mem_erase a A, _âŸ©,
    rwa insert_erase ha },
  { rintro âŸ¨a, ha, hAâŸ©,
    exact âŸ¨insert a A, hA, a, mem_insert_self _ _, erase_insert haâŸ© }
end

/-- `A âˆˆ âˆ‚ğ’œ` iff `A` is exactly one element less than something from `ğ’œ` -/
lemma mem_shadow_iff_exists_mem_card_add_one :
  A âˆˆ âˆ‚ ğ’œ â†” âˆƒ B âˆˆ ğ’œ, A âŠ† B âˆ§ B.card = A.card + 1 :=
begin
  refine mem_shadow_iff_insert_mem.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨a, ha, hAâŸ©,
    exact âŸ¨insert a A, hA, subset_insert _ _, card_insert_of_not_mem haâŸ© },
  { rintro âŸ¨B, hB, hAB, hâŸ©,
    obtain âŸ¨a, haâŸ© : âˆƒ a, B \ A = {a} :=
      card_eq_one.1 (by rw [card_sdiff hAB, h, add_tsub_cancel_left]),
    exact âŸ¨a, Î» haB,
      not_mem_sdiff_of_mem_right haB ((ha.ge : _ âŠ† _) $ mem_singleton_self a),
      by rwa [insert_eq a A, â†ha, sdiff_union_of_subset hAB]âŸ© }
end

/-- Being in the shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
lemma exists_subset_of_mem_shadow (hA : A âˆˆ âˆ‚ğ’œ) : âˆƒ B âˆˆ ğ’œ, A âŠ† B :=
let âŸ¨B, hB, hABâŸ© :=  mem_shadow_iff_exists_mem_card_add_one.1 hA in âŸ¨B, hB, hAB.1âŸ©

/-- `B âˆˆ âˆ‚^k ğ’œ` iff `B` is exactly `k` elements less than something in `ğ’œ`. -/
lemma mem_shadow_iff_exists_mem_card_add {k : â„•} :
  A âˆˆ (shadow^[k] ğ’œ) â†” âˆƒ B âˆˆ ğ’œ, A âŠ† B âˆ§ B.card = A.card + k :=
begin
  induction k with k ih generalizing ğ’œ A,
  { refine âŸ¨Î» hA, âŸ¨A, hA, subset.refl _, rflâŸ©, _âŸ©,
    rintro âŸ¨B, hB, hAB, hcardâŸ©,
    rwa eq_of_subset_of_card_le hAB hcard.le },
  simp only [exists_prop, function.comp_app, function.iterate_succ],
  refine ih.trans _,
  clear ih,
  split,
  { rintro âŸ¨B, hB, hAB, hcardABâŸ©,
    obtain âŸ¨C, hC, hBC, hcardBCâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 hB,
    refine âŸ¨C, hC, hAB.trans hBC, _âŸ©,
    rw [hcardBC, hcardAB],
    refl },
  { rintro âŸ¨B, hB, hAB, hcardâŸ©,
    obtain âŸ¨C, hAC, hCB, hCâŸ© := finset.exists_intermediate_set k _ hAB,
    rw add_comm at hC,
    refine âŸ¨C, mem_shadow_iff_exists_mem_card_add_one.2 âŸ¨B, hB, hCB, _âŸ©, hAC, hCâŸ©,
    rw [hcard, hC],
    refl,
    { rw [add_comm, hcard],
      exact le_succ _ } }
end
