/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import data.fintype.basic
import logic.function.iterate

/-!
# Shadows

This file defines shadows of a set family. The shadow of a set family is the set family of sets we
get by removing any element from any set of the original family. If one pictures `finset Î±` as a big
hypercube (each dimension being membership of a given element), then taking the shadow corresponds
to projecting each finset down once in all available directions.

## Main definitions

* `finset.shadow`: The shadow of a set family. Everything we can get by removing a new element from
  some set.
* `finset.up_shadow`: The upper shadow of a set family. Everything we can get by adding an element
  to some set.

## Notation

We define notation in locale `finset_family`:
* `âˆ‚ ğ’œ`: Shadow of `ğ’œ`.
* `âˆ‚âº ğ’œ`: Upper shadow of `ğ’œ`.

We also maintain the convention that `a, b : Î±` are elements of the ground type, `s, t : finset Î±`
are finsets, and `ğ’œ, â„¬ : finset (finset Î±)` are finset families.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, set family
-/

open finset nat

variables {Î± : Type*}

namespace finset
section shadow
variables [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {s t : finset Î±} {a : Î±} {k : â„•}

/-- The shadow of a set family `ğ’œ` is all sets we can get by removing one element from any set in
`ğ’œ`, and the (`k` times) iterated shadow (`shadow^[k]`) is all sets we can get by removing `k`
elements from any set in `ğ’œ`. -/
def shadow (ğ’œ : finset (finset Î±)) : finset (finset Î±) := ğ’œ.sup (Î» s, s.image (erase s))

localized "notation `âˆ‚ `:90 := finset.shadow" in finset_family

/-- The shadow of the empty set is empty. -/
@[simp] lemma shadow_empty : âˆ‚ (âˆ… : finset (finset Î±)) = âˆ… := rfl

/-- The shadow is monotone. -/
@[mono] lemma shadow_monotone : monotone (shadow : finset (finset Î±) â†’ finset (finset Î±)) :=
Î» ğ’œ â„¬, sup_mono

/-- `s` is in the shadow of `ğ’œ` iff there is an `t âˆˆ ğ’œ` from which we can remove one element to
get `s`. -/
lemma mem_shadow_iff : s âˆˆ âˆ‚ ğ’œ â†” âˆƒ t âˆˆ ğ’œ, âˆƒ a âˆˆ t, erase t a = s :=
by simp only [shadow, mem_sup, mem_image]

lemma erase_mem_shadow (hs : s âˆˆ ğ’œ) (ha : a âˆˆ s) : erase s a âˆˆ âˆ‚ ğ’œ :=
mem_shadow_iff.2 âŸ¨s, hs, a, ha, rflâŸ©

/-- `t` is in the shadow of `ğ’œ` iff we can add an element to it so that the resulting finset is in
`ğ’œ`. -/
lemma mem_shadow_iff_insert_mem : s âˆˆ âˆ‚ ğ’œ â†” âˆƒ a âˆ‰ s, insert a s âˆˆ ğ’œ :=
begin
  refine mem_shadow_iff.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨s, hs, a, ha, rflâŸ©,
    refine âŸ¨a, not_mem_erase a s, _âŸ©,
    rwa insert_erase ha },
  { rintro âŸ¨a, ha, hsâŸ©,
    exact âŸ¨insert a s, hs, a, mem_insert_self _ _, erase_insert haâŸ© }
end

/-- `s âˆˆ âˆ‚ ğ’œ` iff `s` is exactly one element less than something from `ğ’œ` -/
lemma mem_shadow_iff_exists_mem_card_add_one :
  s âˆˆ âˆ‚ ğ’œ â†” âˆƒ t âˆˆ ğ’œ, s âŠ† t âˆ§ t.card = s.card + 1 :=
begin
  refine mem_shadow_iff_insert_mem.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨a, ha, hsâŸ©,
    exact âŸ¨insert a s, hs, subset_insert _ _, card_insert_of_not_mem haâŸ© },
  { rintro âŸ¨t, ht, hst, hâŸ©,
    obtain âŸ¨a, haâŸ© : âˆƒ a, t \ s = {a} :=
      card_eq_one.1 (by rw [card_sdiff hst, h, add_tsub_cancel_left]),
    exact âŸ¨a, Î» hat,
      not_mem_sdiff_of_mem_right hat ((ha.ge : _ âŠ† _) $ mem_singleton_self a),
      by rwa [insert_eq a s, â†ha, sdiff_union_of_subset hst]âŸ© }
end

/-- Being in the shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
lemma exists_subset_of_mem_shadow (hs : s âˆˆ âˆ‚ ğ’œ) : âˆƒ t âˆˆ ğ’œ, s âŠ† t :=
let âŸ¨t, ht, hstâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 hs in âŸ¨t, ht, hst.1âŸ©

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements less than something in `ğ’œ`. -/
lemma mem_shadow_iff_exists_mem_card_add :
  s âˆˆ (âˆ‚^[k]) ğ’œ â†” âˆƒ t âˆˆ ğ’œ, s âŠ† t âˆ§ t.card = s.card + k :=
begin
  induction k with k ih generalizing ğ’œ s,
  { refine âŸ¨Î» hs, âŸ¨s, hs, subset.refl _, rflâŸ©, _âŸ©,
    rintro âŸ¨t, ht, hst, hcardâŸ©,
    rwa eq_of_subset_of_card_le hst hcard.le },
  simp only [exists_prop, function.comp_app, function.iterate_succ],
  refine ih.trans _,
  clear ih,
  split,
  { rintro âŸ¨t, ht, hst, hcardstâŸ©,
    obtain âŸ¨u, hu, htu, hcardtuâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 ht,
    refine âŸ¨u, hu, hst.trans htu, _âŸ©,
    rw [hcardtu, hcardst],
    refl },
  { rintro âŸ¨t, ht, hst, hcardâŸ©,
    obtain âŸ¨u, hsu, hut, huâŸ© := finset.exists_intermediate_set k
      (by { rw [add_comm, hcard], exact le_succ _ }) hst,
    rw add_comm at hu,
    refine âŸ¨u, mem_shadow_iff_exists_mem_card_add_one.2 âŸ¨t, ht, hut, _âŸ©, hsu, huâŸ©,
    rw [hcard, hu],
    refl }
end

end shadow

open_locale finset_family

section up_shadow
variables [decidable_eq Î±] [fintype Î±] {ğ’œ : finset (finset Î±)} {s t : finset Î±} {a : Î±} {k : â„•}

/-- The upper shadow of a set family `ğ’œ` is all sets we can get by adding one element to any set in
`ğ’œ`, and the (`k` times) iterated upper shadow (`up_shadow^[k]`) is all sets we can get by adding
`k`
elements from any set in `ğ’œ`. -/
def up_shadow (ğ’œ : finset (finset Î±)) : finset (finset Î±) :=
ğ’œ.sup $ Î» s, sá¶œ.image $ Î» a, insert a s

localized "notation `âˆ‚âº `:90 := finset.up_shadow" in finset_family

/-- The up_shadow of the empty set is empty. -/
@[simp] lemma up_shadow_empty : âˆ‚âº (âˆ… : finset (finset Î±)) = âˆ… := rfl

/-- The up_shadow is monotone. -/
@[mono] lemma up_shadow_monotone : monotone (up_shadow : finset (finset Î±) â†’ finset (finset Î±)) :=
Î» ğ’œ â„¬, sup_mono

/-- `s` is in the upper shadow of `ğ’œ` iff there is an `t âˆˆ ğ’œ` from which we can remove one element
to get `s`. -/
lemma mem_up_shadow_iff : s âˆˆ âˆ‚âº ğ’œ â†” âˆƒ t âˆˆ ğ’œ, âˆƒ a âˆ‰ t, insert a t = s :=
by simp_rw [up_shadow, mem_sup, mem_image, exists_prop, mem_compl]

lemma insert_mem_up_shadow (hs : s âˆˆ ğ’œ) (ha : a âˆ‰ s) : insert a s âˆˆ âˆ‚âº ğ’œ :=
mem_up_shadow_iff.2 âŸ¨s, hs, a, ha, rflâŸ©

/-- `t` is in the upper shadow of `ğ’œ` iff we can remove an element from it so that the resulting
finset is in `ğ’œ`. -/
lemma mem_up_shadow_iff_erase_mem : s âˆˆ âˆ‚âº ğ’œ â†” âˆƒ a âˆˆ s, s.erase a âˆˆ ğ’œ :=
begin
  refine mem_up_shadow_iff.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨s, hs, a, ha, rflâŸ©,
    refine âŸ¨a, mem_insert_self a s, _âŸ©,
    rwa erase_insert ha },
  { rintro âŸ¨a, ha, hsâŸ©,
    exact âŸ¨s.erase a, hs, a, not_mem_erase _ _, insert_erase haâŸ© }
end

/-- `s âˆˆ âˆ‚âº ğ’œ` iff `s` is exactly one element less than something from `ğ’œ`. -/
lemma mem_up_shadow_iff_exists_mem_card_add_one :
  s âˆˆ âˆ‚âº ğ’œ â†” âˆƒ t âˆˆ ğ’œ, t âŠ† s âˆ§ t.card + 1 = s.card :=
begin
  refine mem_up_shadow_iff_erase_mem.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨a, ha, hsâŸ©,
    exact âŸ¨s.erase a, hs, erase_subset _ _, card_erase_add_one haâŸ© },
  { rintro âŸ¨t, ht, hts, hâŸ©,
    obtain âŸ¨a, haâŸ© : âˆƒ a, s \ t = {a} :=
      card_eq_one.1 (by rw [card_sdiff hts, â†h, add_tsub_cancel_left]),
    refine âŸ¨a, sdiff_subset _ _ ((ha.ge : _ âŠ† _) $ mem_singleton_self a), _âŸ©,
    rwa [â†sdiff_singleton_eq_erase, â†ha, sdiff_sdiff_eq_self hts] }
end

/-- Being in the up_shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
lemma exists_subset_of_mem_up_shadow (hs : s âˆˆ âˆ‚âº ğ’œ) : âˆƒ t âˆˆ ğ’œ, t âŠ† s :=
let âŸ¨t, ht, hts, _âŸ© := mem_up_shadow_iff_exists_mem_card_add_one.1 hs in âŸ¨t, ht, htsâŸ©

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements more than something in `ğ’œ`. -/
lemma mem_up_shadow_iff_exists_mem_card_add :
  s âˆˆ (âˆ‚âº^[k]) ğ’œ â†” âˆƒ t âˆˆ ğ’œ, t âŠ† s âˆ§ t.card + k = s.card :=
begin
  induction k with k ih generalizing ğ’œ s,
  { refine âŸ¨Î» hs, âŸ¨s, hs, subset.refl _, rflâŸ©, _âŸ©,
    rintro âŸ¨t, ht, hst, hcardâŸ©,
    rwa â†eq_of_subset_of_card_le hst hcard.ge },
  simp only [exists_prop, function.comp_app, function.iterate_succ],
  refine ih.trans _,
  clear ih,
  split,
  { rintro âŸ¨t, ht, hts, hcardstâŸ©,
    obtain âŸ¨u, hu, hut, hcardtuâŸ© := mem_up_shadow_iff_exists_mem_card_add_one.1 ht,
    refine âŸ¨u, hu, hut.trans hts, _âŸ©,
    rw [â†hcardst, â†hcardtu, add_right_comm],
    refl },
  { rintro âŸ¨t, ht, hts, hcardâŸ©,
    obtain âŸ¨u, htu, hus, huâŸ© := finset.exists_intermediate_set 1
      (by { rw [add_comm, â†hcard], exact add_le_add_left (zero_lt_succ _) _ }) hts,
    rw add_comm at hu,
    refine âŸ¨u, mem_up_shadow_iff_exists_mem_card_add_one.2 âŸ¨t, ht, htu, hu.symmâŸ©, hus, _âŸ©,
    rw [hu, â†hcard, add_right_comm],
    refl }
end

@[simp] lemma shadow_image_compl : (âˆ‚ ğ’œ).image compl = âˆ‚âº (ğ’œ.image compl) :=
begin
  ext s,
  simp only [mem_image, exists_prop, mem_shadow_iff, mem_up_shadow_iff],
  split,
  { rintro âŸ¨_, âŸ¨s, hs, a, ha, rflâŸ©, rflâŸ©,
    exact âŸ¨sá¶œ, âŸ¨s, hs, rflâŸ©, a, not_mem_compl.2 ha, compl_erase.symmâŸ© },
  { rintro âŸ¨_, âŸ¨s, hs, rflâŸ©, a, ha, rflâŸ©,
    exact âŸ¨s.erase a, âŸ¨s, hs, a, not_mem_compl.1 ha, rflâŸ©, compl_eraseâŸ© }
end

@[simp] lemma up_shadow_image_compl : (âˆ‚âº ğ’œ).image compl = âˆ‚ (ğ’œ.image compl) :=
begin
  ext s,
  simp only [mem_image, exists_prop, mem_shadow_iff, mem_up_shadow_iff],
  split,
  { rintro âŸ¨_, âŸ¨s, hs, a, ha, rflâŸ©, rflâŸ©,
    exact âŸ¨sá¶œ, âŸ¨s, hs, rflâŸ©, a, mem_compl.2 ha, compl_insert.symmâŸ© },
  { rintro âŸ¨_, âŸ¨s, hs, rflâŸ©, a, ha, rflâŸ©,
    exact âŸ¨insert a s, âŸ¨s, hs, a, mem_compl.1 ha, rflâŸ©, compl_insertâŸ© }
end

end up_shadow
end finset
