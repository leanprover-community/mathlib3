/-
Copyright (c) 2021 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import data.finset.lattice
import logic.function.iterate

/-!
# Shadows

This file defines shadows of a set family. The shadow of a set family is the set family of sets we
get by removing any element from any set of the original family. If one pictures `finset Î±` as a big
hypercube (each dimension being membership of a given element), then taking the shadow corresponds
to projecting each finset down once in all available directions.

## Main definitions

The `shadow` of a set family is everything we can get by removing an element from each set.

## Notation

`âˆ‚ ğ’œ` is notation for `shadow ğ’œ`. It is situated in locale `finset_family`.

We also maintain the convention that `a, b : Î±` are elements of the ground type, `s, t : finset Î±`
are finsets, and `ğ’œ, â„¬ : finset (finset Î±)` are finset families.

## References

* https://github.com/b-mehta/maths-notes/blob/master/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, set family
-/

open finset nat

variables {Î± : Type*}

namespace finset
variables [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {s t : finset Î±} {a : Î±} {k : â„•}

/-- The shadow of a set family `ğ’œ` is all sets we can get by removing one element from any set in
`ğ’œ`, and the (`k` times) iterated shadow (`shadow^[k]`) is all sets we can get by removing `k`
elements from any set in `ğ’œ`. -/
def shadow (ğ’œ : finset (finset Î±)) : finset (finset Î±) := ğ’œ.sup (Î» s, s.image (erase s))

localized "notation `âˆ‚ `:90 := finset.shadow" in finset_family

/-- The shadow of the empty set is empty. -/
@[simp] lemma shadow_empty : âˆ‚ (âˆ… : finset (finset Î±)) = âˆ… := rfl

/-- The shadow is monotone. -/
@[mono] lemma shadow_monotone : monotone (shadow : finset (finset Î±) â†’ finset (finset Î±)) :=
Î» ğ’œ â„¬, sup_mono

/-- `s` is in the shadow of `ğ’œ` iff there is an `t âˆˆ ğ’œ` from which we can remove one element to
get `s`. -/
lemma mem_shadow_iff : s âˆˆ âˆ‚ ğ’œ â†” âˆƒ t âˆˆ ğ’œ, âˆƒ a âˆˆ t, erase t a = s :=
by simp only [shadow, mem_sup, mem_image]

lemma erase_mem_shadow (hs : s âˆˆ ğ’œ) (ha : a âˆˆ s) : erase s a âˆˆ âˆ‚ ğ’œ :=
mem_shadow_iff.2 âŸ¨s, hs, a, ha, rflâŸ©

/-- `t` is in the shadow of `ğ’œ` iff we can add an element to it so that the resulting finset is in
`ğ’œ`. -/
lemma mem_shadow_iff_insert_mem : s âˆˆ âˆ‚ ğ’œ â†” âˆƒ a âˆ‰ s, insert a s âˆˆ ğ’œ :=
begin
  refine mem_shadow_iff.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨s, hs, a, ha, rflâŸ©,
    refine âŸ¨a, not_mem_erase a s, _âŸ©,
    rwa insert_erase ha },
  { rintro âŸ¨a, ha, hsâŸ©,
    exact âŸ¨insert a s, hs, a, mem_insert_self _ _, erase_insert haâŸ© }
end

/-- `s âˆˆ âˆ‚ ğ’œ` iff `s` is exactly one element less than something from `ğ’œ` -/
lemma mem_shadow_iff_exists_mem_card_add_one :
  s âˆˆ âˆ‚ ğ’œ â†” âˆƒ t âˆˆ ğ’œ, s âŠ† t âˆ§ t.card = s.card + 1 :=
begin
  refine mem_shadow_iff_insert_mem.trans âŸ¨_, _âŸ©,
  { rintro âŸ¨a, ha, hsâŸ©,
    exact âŸ¨insert a s, hs, subset_insert _ _, card_insert_of_not_mem haâŸ© },
  { rintro âŸ¨t, ht, hst, hâŸ©,
    obtain âŸ¨a, haâŸ© : âˆƒ a, t \ s = {a} :=
      card_eq_one.1 (by rw [card_sdiff hst, h, add_tsub_cancel_left]),
    exact âŸ¨a, Î» hat,
      not_mem_sdiff_of_mem_right hat ((ha.ge : _ âŠ† _) $ mem_singleton_self a),
      by rwa [insert_eq a s, â†ha, sdiff_union_of_subset hst]âŸ© }
end

/-- Being in the shadow of `ğ’œ` means we have a superset in `ğ’œ`. -/
lemma exists_subset_of_mem_shadow (hs : s âˆˆ âˆ‚ ğ’œ) : âˆƒ t âˆˆ ğ’œ, s âŠ† t :=
let âŸ¨t, ht, hstâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 hs in âŸ¨t, ht, hst.1âŸ©

/-- `t âˆˆ âˆ‚^k ğ’œ` iff `t` is exactly `k` elements less than something in `ğ’œ`. -/
lemma mem_shadow_iff_exists_mem_card_add :
  s âˆˆ (âˆ‚^[k]) ğ’œ â†” âˆƒ t âˆˆ ğ’œ, s âŠ† t âˆ§ t.card = s.card + k :=
begin
  induction k with k ih generalizing ğ’œ s,
  { refine âŸ¨Î» hs, âŸ¨s, hs, subset.refl _, rflâŸ©, _âŸ©,
    rintro âŸ¨t, ht, hst, hcardâŸ©,
    rwa eq_of_subset_of_card_le hst hcard.le },
  simp only [exists_prop, function.comp_app, function.iterate_succ],
  refine ih.trans _,
  clear ih,
  split,
  { rintro âŸ¨t, ht, hst, hcardstâŸ©,
    obtain âŸ¨u, hu, htu, hcardtuâŸ© := mem_shadow_iff_exists_mem_card_add_one.1 ht,
    refine âŸ¨u, hu, hst.trans htu, _âŸ©,
    rw [hcardtu, hcardst],
    refl },
  { rintro âŸ¨t, ht, hst, hcardâŸ©,
    obtain âŸ¨u, hsu, hut, huâŸ© := finset.exists_intermediate_set k
      (by { rw [add_comm, hcard], exact le_succ _ }) hst,
    rw add_comm at hu,
    refine âŸ¨u, mem_shadow_iff_exists_mem_card_add_one.2 âŸ¨t, ht, hut, _âŸ©, hsu, huâŸ©,
    rw [hcard, hu],
    refl }
end

end finset
