/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta, Alena Gusakov
-/

import data.fintype.basic
import data.nat.choose
import order.antichain

/-!
# Shadows

This file defines shadows of a set family and proves the local LYM and LYM theorems, as well as
Sperner's theorem.

## Main definitions

The `shadow` of a set family is everything we can get by removing an element from each set.

The rth slice of a set family ğ’œ is given by `slice ğ’œ r`, and is the subset of its elements which
have cardinality `r`.

## Main statements

* `local_lym`
* `lubell_yamamoto_meshalkin`
* `sperner`

## Notation

We introduce the notation âˆ‚ to denote the shadow.
We also maintain the convention that A, B, ... denote sets (usually finset Î±),
ğ’œ, â„¬, ... denote set families, i.e. `finset (finset Î±)` and lower-case letters
denote elements of the ground set Î±.

## References

* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags

shadow, lym, slice, sperner, antichain
-/

open finset fintype nat

variables {Î± : Type*}

/-!
### Shadows

The shadow of a set family is everything we can get by removing an element
from each set.

This section develops the introductory theory of shadows, with some lemmas on
iterated shadows as well.
-/

section shadow
variables [decidable_eq Î±]

/-- Everything we get by removing one element from the set `A`, used to define the shadow. -/
def all_removals (A : finset Î±) : finset (finset Î±) := A.image (erase A)

/-- `B âˆˆ all_removals A` iff we can remove something from `A` to get `B`. -/
lemma mem_all_removals {A : finset Î±} {B : finset Î±} :
  B âˆˆ all_removals A â†” âˆƒ i âˆˆ A, erase A i = B :=
by simp only [all_removals, mem_image]

/-- If `A` has size `r`, then there are `r` things we get by removing one element. -/
lemma card_all_removals {A : finset Î±} {r : â„•} (H : A.card = r) :
  (all_removals A).card = r :=
begin
  rwa [all_removals, card_image_of_inj_on],
  intros i ih j _ k,
  have q: i âˆ‰ erase A j := k â–¸ not_mem_erase i A,
  rw [mem_erase, not_and] at q,
  by_contra a,
  apply q a ih
end

/-- The shadow of a set family `ğ’œ` is all sets we can get by removing one element
from any set in `ğ’œ`, and the (`k` times) iterated shadow is all sets we can get
by removing k elements from any set in `ğ’œ`. -/
def shadow (ğ’œ : finset (finset Î±)) : finset (finset Î±) := ğ’œ.sup all_removals

reserve prefix `âˆ‚`:90
notation âˆ‚ğ’œ := shadow ğ’œ

/-- Shadow of the empty set is empty. -/
lemma shadow_empty : shadow (âˆ… : finset (finset Î±)) = âˆ… := rfl

/-- Iterated shadow of the empty set is empty. -/
lemma iter_shadow_empty (k : â„•) : shadow^[k] (âˆ… : finset (finset Î±)) = âˆ… :=
begin
  induction k with k ih,
  { refl },
  { rwa [iterate, shadow_empty] }
end

/-- The shadow is monotonic (though not strictly so). -/
lemma shadow_monotone {ğ’œ â„¬ : finset (finset Î±)} (h : ğ’œ âŠ† â„¬) : âˆ‚ğ’œ âŠ† âˆ‚â„¬ :=
le_iff_subset.1 $ sup_mono h

/-- `B âˆˆ âˆ‚ğ’œ` iff there is an `A âˆˆ ğ’œ` from which we can remove something to get `B`.
-/
lemma mem_shadow {ğ’œ : finset (finset Î±)} (B : finset Î±) :
  B âˆˆ shadow ğ’œ â†” âˆƒ A âˆˆ ğ’œ, âˆƒ i âˆˆ A, erase A i = B :=
by simp only [shadow, all_removals, mem_sup, mem_image]

/-- Alternatively, `B âˆˆ âˆ‚ğ’œ` iff we can put something new into `B`, and land in `ğ’œ`. -/
lemma mem_shadow' {ğ’œ : finset (finset Î±)} {B : finset Î±} :
  B âˆˆ shadow ğ’œ â†” âˆƒ j âˆ‰ B, insert j B âˆˆ ğ’œ :=
begin
  rw mem_shadow,
  split,
  { rintro âŸ¨A, HA, i, Hi, kâŸ©,
    rw â† k,
    refine âŸ¨i, not_mem_erase i A, _âŸ©,
    rwa insert_erase Hi },
  { rintro âŸ¨i, Hi, kâŸ©,
    refine âŸ¨insert i B, k, i, mem_insert_self _ _, _âŸ©,
    rw erase_insert Hi }
end

/-- `B âˆˆ âˆ‚ğ’œ` iff `B` is exactly one element less than something from `ğ’œ` -/
lemma sub_iff_shadow_one {ğ’œ : finset (finset Î±)} {B : finset Î±} :
  B âˆˆ âˆ‚ğ’œ â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ (A \ B).card = 1 :=
begin
  rw mem_shadow',
  split,
  { rintro âŸ¨i, ih, inAâŸ©,
    refine âŸ¨insert i B, inA, subset_insert _ _, _âŸ©,
    rw card_sdiff (subset_insert _ _),
    simp [card_insert_of_not_mem ih] },
  { rintro âŸ¨A, hA, a_h_hâŸ©,
    rw card_eq_one at a_h_h,
    rcases a_h_h with âŸ¨subs, j, eqâŸ©,
    refine âŸ¨j, _, _âŸ©,
    { intro a,
      have : j âˆ‰ A \ B := not_mem_sdiff_of_mem_right a,
      apply this,
      rw eq,
      apply mem_singleton_self },
    { rwa [insert_eq j B, â† eq, sdiff_union_of_subset subs] } },
end

/--
In particular, being in the shadow means we're a subset of something in `ğ’œ`.
-/
lemma sub_of_shadow {ğ’œ : finset (finset Î±)} {B : finset Î±} :
  B âˆˆ âˆ‚ğ’œ â†’ âˆƒ A âˆˆ ğ’œ, B âŠ† A :=
by rw sub_iff_shadow_one; tauto

/-- `B âˆˆ âˆ‚^k ğ’œ` iff `B` is exactly `k` elements less than something from `ğ’œ`. -/
lemma sub_iff_shadow_iter {ğ’œ : finset (finset Î±)} {B : finset Î±} (k : â„•) :
  B âˆˆ (shadow^[k] ğ’œ) â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ (A \ B).card = k :=
begin
  induction k with k ih generalizing ğ’œ B,
  { simp [sdiff_eq_empty_iff_subset],
    split,
    { intro p,
      exact âŸ¨B, p, subset.refl _, subset.refl _âŸ© },
    { rintro âŸ¨A, _, qâŸ©,
      rw â† subset.antisymm_iff at q,
      rwa q } },
  { simp only [exists_prop, function.comp_app, function.iterate_succ],
    rw @ih (âˆ‚ğ’œ) B,
    clear ih,
    split,
    { rintro âŸ¨A, hA, BsubA, card_AdiffB_is_kâŸ©,
      rw sub_iff_shadow_one at hA,
      rcases hA with âŸ¨C, CinA, AsubC, card_CdiffA_is_1âŸ©,
      refine âŸ¨C, CinA, trans BsubA AsubC, _âŸ©,
      rw card_sdiff (trans BsubA AsubC),
      rw card_sdiff BsubA at card_AdiffB_is_k,
      rw card_sdiff AsubC at card_CdiffA_is_1,
      rw [â† nat.sub_add_cancel (card_le_of_subset AsubC),
          nat.add_sub_assoc (card_le_of_subset BsubA), card_CdiffA_is_1,
          card_AdiffB_is_k, add_comm] },
    { rintro âŸ¨A, hA, hBA, hABâŸ©,
      have z: (A \ B).nonempty,
      { rw [â† finset.card_pos, hAB],
        exact nat.succ_pos _ },
      rcases z with âŸ¨i, hiâŸ©,
      have: i âˆˆ A, rw mem_sdiff at hi,
      { exact hi.1 },
      have: B âŠ† erase A i,
      { intros t th,
        apply mem_erase_of_ne_of_mem _ (hBA th),
        intro a,
        rw mem_sdiff at hi,
        rw a at th,
        exact hi.2 th },
      refine âŸ¨erase A i, _, â€¹_â€º, _âŸ©,
      { rw mem_shadow,
        refine âŸ¨A, hA, i, â€¹_â€º, rflâŸ© },
      rw [card_sdiff â€¹B âŠ† erase A iâ€º,
        card_erase_of_mem â€¹i âˆˆ Aâ€º, nat.pred_sub,
        â† card_sdiff hBA, hAB],
      simp } }
end

end shadow

/-!
### Build up and proof of local LYM

If there is a fintype Î± which is our universe, informally write `Î±^(r)` for the
`set {A : finset Î± | |A| = r}`. Then if `ğ’œ` is a subset of `Î±^(r)`, we get that `âˆ‚ğ’œ`
is a subset of `Î±^(r-1)`.
The local LYM inequality says `ğ’œ` 'takes up less' of `Î±^(r)` than `âˆ‚ğ’œ` takes up of
`Î±^(r-1)`. In particular,
`|ğ’œ| / choose |Î±| r â‰¤ |âˆ‚ğ’œ| / choose |Î±| (r-1)`
-/
section local_lym
variables [decidable_eq Î±]

/-- Start by multiplying out the inequality so it's in a slightly nicer form. -/
lemma multiply_out {A B n r : â„•} (hr1 : 1 â‰¤ r) (hr2 : r â‰¤ n)
  (h : A * r â‰¤ B * (n - r + 1)) :
  (A : â„š) / nat.choose n r â‰¤ B / nat.choose n (r-1) :=
begin
  rw div_le_div_iff; norm_cast,
  { apply le_of_mul_le_mul_right _ â€¹0 < râ€º,
    cases r,
    { simp },
    { rw nat.succ_eq_add_one at *,
      rw [â† nat.sub_add_comm hr2, nat.add_sub_add_right] at h,
      convert nat.mul_le_mul_right (choose n r) h using 1;
      { simp [mul_assoc, nat.choose_succ_right_eq],
        left,
        ac_refl } } },
  { apply nat.choose_pos hr2 },
  { apply nat.choose_pos (le_trans (nat.pred_le _) hr2) },
end

/-- We'll prove local LYM by a double counting argument. Here's the first set
we'll count, which is effectively `{(A, B) | A âˆˆ ğ’œ, B âˆˆ all_removals A}`. -/
def the_pairs (ğ’œ : finset (finset Î±)) : finset (finset Î± Ã— finset Î±) :=
ğ’œ.sup (Î» A, (all_removals A).image (prod.mk A))

/--
Here's the second set we'll count. We're trying to get the same set, but we
count B first, so we overestimate a bit. It's pretty much
{(A,B) | B âˆˆ âˆ‚ğ’œ, âˆƒ i âˆ‰ B: A = B âˆª i}
-/
def from_below [fintype Î±] (ğ’œ : finset (finset Î±)) :
  finset (finset Î± Ã— finset Î±) :=
(âˆ‚ğ’œ).sup (Î» B, (univ \ B).image (Î» x, (insert x B, B)))

/--
Note the first is a subset of the second: if A âˆˆ ğ’œ and B âˆˆ all_removals A
then certainly B âˆˆ âˆ‚ğ’œ, and there's some i that was removed from A to make B.
-/
lemma above_sub_below [fintype Î±] (ğ’œ : finset (finset Î±)) :
  the_pairs ğ’œ âŠ† from_below ğ’œ :=
begin
  rintro âŸ¨A,BâŸ©,
  simp only [the_pairs, from_below, mem_sup, mem_all_removals, mem_shadow, true_and, and_imp,
    exists_prop, mem_sdiff, mem_image, prod.mk.inj_iff, mem_univ, exists_imp_distrib],
  rintro A Ah B i ih z rfl rfl,
  exact âŸ¨B, âŸ¨A, Ah, i, ih, zâŸ©, i, z â–¸ not_mem_erase _ _,
          z â–¸ insert_erase ih, rflâŸ©
end

end local_lym
