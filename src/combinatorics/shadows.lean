/-
Copyright (c) 2020 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov
-/

import data.finset
import data.fintype.basic
import data.nat.choose
import combinatorics.basic

/-!
# Shadows

This file defines shadows of a set family and proves the local LYM and LYM
theorems, as well as Sperner's theorem.

## Main definitions
The `shadow` of a set family is everything we can get by removing an element
from each set.

The rth slice of a set family ğ’œ is given by `slice ğ’œ r`, and is the subset of
its elements which have cardinality r.

## Main statements
* local_lym
* lubell_yamamoto_meshalkin
* sperner

## Notation
We introduce the notation âˆ‚ to denote the shadow.
We also maintain the convention that A, B, ... denote sets (usually finset Î±),
ğ’œ, â„¬, ... denote set families, i.e. `finset (finset Î±)` and lower-case letters
denote elements of the ground set Î±.

## References
* http://b-mehta.github.io/maths-notes/iii/mich/combinatorics.pdf
* http://discretemath.imp.fu-berlin.de/DMII-2015-16/kruskal.pdf

## Tags
shadow, lym, slice, sperner, antichain
-/

open fintype
open finset
open nat

variables {Î± : Type*}

/-!
### Shadows
The shadow of a set family is everything we can get by removing an element
from each set.

This section develops the introductory theory of shadows, with some lemmas on
iterated shadows as well.
-/
section shadow

variables [decidable_eq Î±]
/--
Everything we get by removing one element from the set `A`, used to define
the shadow.
-/
def all_removals (A : finset Î±) : finset (finset Î±) := A.image (erase A)

/-- B âˆˆ all_removals A iff we can remove something from A to get B. -/
lemma mem_all_removals {A : finset Î±} {B : finset Î±} :
  B âˆˆ all_removals A â†” âˆƒ i âˆˆ A, erase A i = B :=
by simp only [all_removals, mem_image]

/--
If A has size r, then there are r things we get by removing one element.
-/
lemma card_all_removals {A : finset Î±} {r : â„•} (H : card A = r) :
  (all_removals A).card = r :=
begin
  rwa [all_removals, card_image_of_inj_on],
  intros i ih j _ k,
  have q: i âˆ‰ erase A j := k â–¸ not_mem_erase i A,
  rw [mem_erase, not_and] at q,
  by_contra a,
  apply q a ih
end

/--
The shadow of a set family ğ’œ is all sets we can get by removing one element
from any set in ğ’œ, and the (k times) iterated shadow is all sets we can get
by removing k elements from any set in ğ’œ.
-/
def shadow (ğ’œ : finset (finset Î±)) : finset (finset Î±) := ğ’œ.bind all_removals

reserve prefix `âˆ‚`:90
notation âˆ‚ğ’œ := shadow ğ’œ

/-- Shadow of the empty set is empty. -/
lemma shadow_empty : shadow (âˆ… : finset (finset Î±)) = âˆ… := rfl

/-- Iterated shadow of the empty set is empty. -/
lemma iter_shadow_empty (k : â„•) : shadow^[k] (âˆ… : finset (finset Î±)) = âˆ… :=
begin
  induction k with k ih,
  { refl },
  { rwa [iterate, shadow_empty] },
end

/-- The shadow is monotonic (though not strictly so). -/
lemma shadow_monotone {ğ’œ â„¬ : finset (finset Î±)} : ğ’œ âŠ† â„¬ â†’ âˆ‚ğ’œ âŠ† âˆ‚â„¬ :=
bind_subset_bind_of_subset_left _

/--
B âˆˆ âˆ‚ğ’œ iff there is an A âˆˆ ğ’œ from which we can remove something to get B.
-/
lemma mem_shadow {ğ’œ : finset (finset Î±)} (B : finset Î±) :
  B âˆˆ shadow ğ’œ â†” âˆƒ A âˆˆ ğ’œ, âˆƒ i âˆˆ A, erase A i = B :=
by simp only [shadow, all_removals, mem_bind, mem_image]

/--
Alternatively, B âˆˆ âˆ‚ğ’œ iff we can put something new into B, and land in ğ’œ.
-/
lemma mem_shadow' {ğ’œ : finset (finset Î±)} {B : finset Î±} :
  B âˆˆ shadow ğ’œ â†” âˆƒ j âˆ‰ B, insert j B âˆˆ ğ’œ :=
begin
  rw mem_shadow,
  split,
  { rintro âŸ¨A, HA, i, Hi, kâŸ©,
    rw â† k,
    refine âŸ¨i, not_mem_erase i A, _âŸ©,
    rwa insert_erase Hi },
  { rintro âŸ¨i, Hi, kâŸ©,
    refine âŸ¨insert i B, k, i, mem_insert_self _ _, _âŸ©,
    rw erase_insert Hi }
end

/-- Everything in the shadow is one smaller than things in the original. -/
lemma shadow_sized {ğ’œ : finset (finset Î±)} {r : â„•} (a : all_sized ğ’œ r) :
  all_sized (âˆ‚ğ’œ) (r-1) :=
begin
  intros A H,
  simp_rw [shadow, mem_bind, all_removals, mem_image] at H,
  rcases H with âŸ¨A, hA, i, hi, rflâŸ©,
  rw [card_erase_of_mem hi, a _ hA],
  refl,
end

/--
B âˆˆ âˆ‚ğ’œ iff B is exactly one element less than something from ğ’œ
-/
lemma sub_iff_shadow_one {ğ’œ : finset (finset Î±)} {B : finset Î±} :
  B âˆˆ âˆ‚ğ’œ â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ card (A \ B) = 1 :=
begin
  rw mem_shadow',
  split,
  { rintro âŸ¨i, ih, inAâŸ©,
    refine âŸ¨insert i B, inA, subset_insert _ _, _âŸ©,
    rw card_sdiff (subset_insert _ _),
    simp [card_insert_of_not_mem ih] },
  { rintro âŸ¨A, hA, a_h_hâŸ©,
    rw card_eq_one at a_h_h,
    rcases a_h_h with âŸ¨subs, j, eqâŸ©,
    refine âŸ¨j, _, _âŸ©,
    { intro a,
      have : j âˆ‰ A \ B := not_mem_sdiff_of_mem_right a,
      apply this,
      rw eq,
      apply mem_singleton_self },
    { rwa [insert_eq j B, â† eq, sdiff_union_of_subset subs] } },
end

/--
In particular, being in the shadow means we're a subset of something in ğ’œ.
-/
lemma sub_of_shadow {ğ’œ : finset (finset Î±)} {B : finset Î±} :
  B âˆˆ âˆ‚ğ’œ â†’ âˆƒ A âˆˆ ğ’œ, B âŠ† A :=
by rw sub_iff_shadow_one; tauto

/-- B âˆˆ âˆ‚^k ğ’œ iff B is exactly k elements less than something from ğ’œ. -/
lemma sub_iff_shadow_iter {ğ’œ : finset (finset Î±)} {B : finset Î±} (k : â„•) :
  B âˆˆ (shadow^[k] ğ’œ) â†” âˆƒ A âˆˆ ğ’œ, B âŠ† A âˆ§ card (A \ B) = k :=
begin
  induction k with k ih generalizing ğ’œ B,
  { simp [sdiff_eq_empty_iff_subset],
    split,
    { intro p,
      exact âŸ¨B, p, subset.refl _, subset.refl _âŸ© },
    { rintro âŸ¨A, _, qâŸ©,
      rw â† subset.antisymm_iff at q,
      rwa q } },
  { simp,
    rw iterate,
    rw @ih (âˆ‚ğ’œ) B,
    clear ih,
    split,
    { rintro âŸ¨A, hA, BsubA, card_AdiffB_is_kâŸ©,
      rw sub_iff_shadow_one at hA,
      rcases hA with âŸ¨C, CinA, AsubC, card_CdiffA_is_1âŸ©,
      refine âŸ¨C, CinA, trans BsubA AsubC, _âŸ©,
      rw card_sdiff (trans BsubA AsubC),
      rw card_sdiff BsubA at card_AdiffB_is_k,
      rw card_sdiff AsubC at card_CdiffA_is_1,
      rw [â† nat.sub_add_cancel (card_le_of_subset AsubC),
          nat.add_sub_assoc (card_le_of_subset BsubA), card_CdiffA_is_1,
          card_AdiffB_is_k, add_comm] },
    { rintro âŸ¨A, hA, a_h_right_left, a_h_right_rightâŸ©,
      have z: (A \ B).nonempty,
      { rw [â† card_pos, a_h_right_right],
        exact nat.succ_pos _ },
      rcases z with âŸ¨i, hiâŸ©,
      have: i âˆˆ A, rw mem_sdiff at hi,
      { exact hi.1 },
      have: B âŠ† erase A i,
      { intros t th,
        apply mem_erase_of_ne_of_mem _ (a_h_right_left th),
        intro a,
        rw mem_sdiff at hi,
        rw a at th,
        exact hi.2 th },
      refine âŸ¨erase A i, _, â€¹_â€º, _âŸ©,
      { rw mem_shadow,
        refine âŸ¨A, hA, i, â€¹_â€º, rflâŸ© },
      rw [card_sdiff â€¹B âŠ† erase A iâ€º,
        card_erase_of_mem â€¹i âˆˆ Aâ€º, nat.pred_sub,
        â† card_sdiff a_h_right_left, a_h_right_right],
      simp } }
end
/--
Everything in the `k`th shadow is `k` smaller than things in the original.
-/
lemma iter_shadow_sized {ğ’œ : finset (finset Î±)} {r k : â„•}
  (a : all_sized ğ’œ r) : all_sized (shadow^[k] ğ’œ) (r-k) :=
begin
  intro B,
  rw sub_iff_shadow_iter,
  rintro âŸ¨A, hA, subs, cardâŸ©,
  rw [card_sdiff â€¹B âŠ† Aâ€º, a _ hA] at card,
  rw [â† card, nat.sub_sub_self],
  rw â† a _ hA,
  apply card_le_of_subset â€¹B âŠ† Aâ€º
end

end shadow

/-!
### Build up and proof of local LYM

If there is a fintype Î± which is our universe, informally write Î±^(r) for the
set {A : finset Î± | |A| = r}. Then if ğ’œ is a subset of Î±^(r), we get that âˆ‚ğ’œ
is a subset of Î±^(r-1).
The local LYM inequality says ğ’œ 'takes up less' of Î±^(r) than âˆ‚ğ’œ takes up of
Î±^(r-1). In particular,
|ğ’œ| / choose |Î±| r â‰¤ |âˆ‚ğ’œ| / choose |Î±| (r-1)
-/
section local_lym
variables [decidable_eq Î±]
/--
Start by multiplying out the inequality so it's in a slightly nicer form.
-/
lemma multiply_out {A B n r : â„•} (hr1 : 1 â‰¤ r) (hr2 : r â‰¤ n)
  (h : A * r â‰¤ B * (n - r + 1)) :
  (A : â„š) / nat.choose n r â‰¤ B / nat.choose n (r-1) :=
begin
  rw div_le_div_iff; norm_cast,
  { apply le_of_mul_le_mul_right _ â€¹0 < râ€º,
    cases r,
    { simp },
    { rw nat.succ_eq_add_one at *,
      rw [â† nat.sub_add_comm hr2, nat.add_sub_add_right] at h,
      convert nat.mul_le_mul_right (choose n r) h using 1;
      { simp [mul_assoc, nat.choose_succ_right_eq],
        left,
        ac_refl } } },
  { apply nat.choose_pos hr2 },
  { apply nat.choose_pos (le_trans (nat.pred_le _) hr2) },
end

/--
We'll prove local LYM by a double counting argument. Here's the first set
we'll count, which is effectively {(A,B) | A âˆˆ ğ’œ, B âˆˆ all_removals A}
-/
def the_pairs (ğ’œ : finset (finset Î±)) : finset (finset Î± Ã— finset Î±) :=
ğ’œ.bind (Î» A, (all_removals A).image (prod.mk A))

/--
Find how big the_pairs is: for each A âˆˆ ğ’œ there are r possible B, giving the
exact cardinality.
-/
lemma card_the_pairs {r : â„•} (ğ’œ : finset (finset Î±)) (a : all_sized ğ’œ r) :
  (the_pairs ğ’œ).card = ğ’œ.card * r :=
begin
  rw [the_pairs, card_bind],
  { convert sum_const_nat _,
    intros x hx,
    rw card_image_of_inj_on,
    rw card_all_removals (a _ hx),
    exact (Î» _ _ _ _ k, (prod.mk.inj k).2) },
  simp only [disjoint_left, mem_image],
  rintros _ _ _ _ k a âŸ¨_, _, rflâŸ© âŸ¨_, _, aâ‚‚âŸ©,
  exact k (prod.mk.inj aâ‚‚.symm).1,
end

/--
Here's the second set we'll count. We're trying to get the same set, but we
count B first, so we overestimate a bit. It's pretty much
{(A,B) | B âˆˆ âˆ‚ğ’œ, âˆƒ i âˆ‰ B: A = B âˆª i}
-/
def from_below [fintype Î±] (ğ’œ : finset (finset Î±)) :
  finset (finset Î± Ã— finset Î±) :=
(âˆ‚ğ’œ).bind (Î» B, (univ \ B).image (Î» x, (insert x B, B)))

/--
Note the first is a subset of the second: if A âˆˆ ğ’œ and B âˆˆ all_removals A
then certainly B âˆˆ âˆ‚ğ’œ, and there's some i that was removed from A to make B.
-/
lemma above_sub_below [fintype Î±] (ğ’œ : finset (finset Î±)) :
  the_pairs ğ’œ âŠ† from_below ğ’œ :=
begin
  rintros âŸ¨A,BâŸ©,
  simp only [the_pairs, from_below, mem_bind, mem_all_removals, mem_shadow,
              true_and, and_imp, exists_prop, mem_sdiff, mem_image,
              prod.mk.inj_iff, mem_univ, exists_imp_distrib],
  rintros A Ah B i ih z rfl rfl,
  exact âŸ¨B, âŸ¨A, Ah, i, ih, zâŸ©, i, z â–¸ not_mem_erase _ _,
          z â–¸ insert_erase ih, rflâŸ©
end

/-
It'll be useful to abbreviate this. It's helpful to think of Î± = fin n.
-/
local notation `n` := card Î±
/--
We can also find how big the second set is: for each B there are
(|Î±| - r + 1) choices for what to put into it.
-/
lemma card_from_below [fintype Î±] {ğ’œ : finset (finset Î±)} {r : â„•}
  (a : all_sized ğ’œ r) :
  (from_below ğ’œ).card = (âˆ‚ğ’œ).card * (n - (r - 1)) :=
begin
  rw [from_below, card_bind],
  { apply sum_const_nat,
    intros,
    rw [card_image_of_inj_on,
    card_univ_diff,
    shadow_sized a _ H],
    intros x1 x1h _ _ h,
    injection h,
    have q := mem_insert_self x1 x,
    rw [h_1, mem_insert] at q,
    apply q.resolve_right (mem_sdiff.1 x1h).2 },
  intros _ _ _ _ t,
  rw disjoint_left,
  simp_rw [mem_image, not_exists, exists_prop, mem_sdiff,
            mem_univ, true_and, exists_imp_distrib,
            prod.forall, prod.mk.inj_iff, and_imp, not_and],
  rintro _ b i hi rfl rfl j hj k,
  rwa eq_comm,
end

/--
The local LYM inequality says ğ’œ 'takes up less' of Î±^(r) than âˆ‚ğ’œ takes up of
Î±^(r-1). In particular,
|ğ’œ| / choose |Î±| r â‰¤ |âˆ‚ğ’œ| / choose |Î±| (r-1)
Its proof is just the double counting argument we've now set up.
-/
theorem local_lym [fintype Î±] {ğ’œ : finset (finset Î±)} {r : â„•} (hr1 : 1 â‰¤ r)
  (H : all_sized ğ’œ r) :
  (ğ’œ.card : â„š) / nat.choose n r â‰¤ (âˆ‚ğ’œ).card / nat.choose n (r-1) :=
begin
  cases lt_or_le n r with z hr2,
  -- Take care of the r > n case: it's trivial
  { rw [choose_eq_zero_of_lt z, cast_zero, div_zero],
    refine div_nonneg _ _; norm_cast,
    any_goals { apply nat.zero_le } },
  { apply multiply_out hr1 hr2,
  -- Multiply out, convert to the cardinality forms we got above and done
    rw [â† card_the_pairs _ H, â† nat.sub_sub_assoc hr2 hr1,
        â† card_from_below H],
    apply card_le_of_subset,
    apply above_sub_below }
end

end local_lym

/-!
### Slices

The `r`th slice of a set family the subset of its elements which have
cardinality `r`.
A few basic facts about slices.
-/
section slice
/--
The `r`th slice of a set family the subset of its elements which have
cardinality `r`.
-/
def slice (ğ’œ : finset (finset Î±)) (r : â„•) : finset (finset Î±) :=
ğ’œ.filter (Î» i, card i = r)

reserve infix `#`:100
notation ğ’œ#r := slice ğ’œ r

/-- `A` is in the `r`th slice of `ğ’œ` iff it's in `ğ’œ` and has cardinality `r`. -/
lemma mem_slice {ğ’œ : finset (finset Î±)} {r : â„•} {A : finset Î±} :
  A âˆˆ ğ’œ#r â†” A âˆˆ ğ’œ âˆ§ A.card = r :=
by rw [slice, mem_filter]

/-- The `r`th slice of `ğ’œ` is a subset of `ğ’œ`. -/
lemma slice_subset {ğ’œ : finset (finset Î±)} {r : â„•} : ğ’œ#r âŠ† ğ’œ :=
Î» _, and.left âˆ˜ mem_slice.mp

/-- Everything in the `r`th slice of `ğ’œ` has size `r`. -/
lemma sized_slice {ğ’œ : finset (finset Î±)} {r : â„•} : all_sized (ğ’œ#r) r :=
Î» _, and.right âˆ˜ mem_slice.mp

/-- Elements in distinct slices must be distinct. -/
lemma ne_of_diff_slice {ğ’œ : finset (finset Î±)} {râ‚ râ‚‚ : â„•}
  {Aâ‚ Aâ‚‚ : finset Î±} (hâ‚ : Aâ‚ âˆˆ ğ’œ#râ‚) (hâ‚‚ : Aâ‚‚ âˆˆ ğ’œ#râ‚‚) :
  râ‚ â‰  râ‚‚ â†’ Aâ‚ â‰  Aâ‚‚ :=
mt $ Î» h, (sized_slice Aâ‚ hâ‚).symm.trans ((congr_arg card h).trans (sized_slice Aâ‚‚ hâ‚‚))

end slice

/- It's useful to abbreviate this. We can think of Î± = fin n. -/
local notation `n` := card Î±

/-!
The LYM inequality says âˆ‘_i |A#i|/(n choose i) â‰¤ 1 for an antichain A.
Observe that A#i is all the stuff in A which has size i, and the collection of
subsets of (fin n) with size i has size (n choose i).
So, |A#i|/(n choose i) represents how much of each that A can take up.

Other proofs of LYM exist, but we'll do it by applying local LYM.
-/
section lym

variables [fintype Î±]

/--
An inductive definition, from the top down.
`falling ğ’œ k` is all the sets with cardinality (card Î± - k) which are a
subset of something in ğ’œ.
-/
def falling [decidable_eq Î±] (ğ’œ : finset (finset Î±)) : Î  (k : â„•), finset (finset Î±)
  | 0 := ğ’œ#n
  | (k+1) := ğ’œ#(n - (k+1)) âˆª shadow (falling k)

/--
Everything in the kth fallen has size `n-k`
-/
lemma falling_sized [decidable_eq Î±] (ğ’œ : finset (finset Î±)) (k : â„•) :
  all_sized (falling ğ’œ k) (n - k) :=
begin
  induction k with k ih; rw falling,
  { apply sized_slice },
  { rw â† union_layer,
    split,
    { apply sized_slice },
    { apply shadow_sized ih } },
end

/--
Here's the first key proposition, helping to give the disjointness
property in the next lemma.
-/
theorem antichain_prop [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {r k : â„•}
  (hk : k â‰¤ n) (hr : r < k) (H : antichain ğ’œ) :
  âˆ€ A âˆˆ ğ’œ#(n - k), âˆ€ B âˆˆ âˆ‚falling ğ’œ r, Â¬(A âŠ† B) :=
begin
  intros A HA B HB k,
  rcases sub_of_shadow HB with âŸ¨C, HC, _âŸ©,
  replace k := trans k â€¹B âŠ† Câ€º,
  clear HB h_h B,
  induction r with r ih generalizing A C;
  rw falling at HC,
  any_goals { rw mem_union at HC, cases HC },
  any_goals { refine H A (mem_slice.1 HA).1 C (mem_slice.1 HC).1 _ â€¹A âŠ† Câ€º,
              apply ne_of_diff_slice HA HC _,
              apply ne_of_lt },
  { apply nat.sub_lt_of_pos_le _ _ hr hk },
  { mono },
  { obtain âŸ¨_, HB', HB''âŸ© := sub_of_shadow HC,
    exact ih (lt_of_succ_lt hr) _ _ HA HB' (trans k_1 HB'') }
end

/--
This tells us that `falling ğ’œ k` is disjoint from the n - (k+1) -sized
elements of ğ’œ, thanks to the antichain property.
-/
lemma disjoint_of_antichain [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {k : â„•}
  (hk : k + 1 â‰¤ n) (H : antichain ğ’œ) :
  disjoint (ğ’œ#(n - (k + 1))) (âˆ‚falling ğ’œ k) :=
disjoint_left.2 $ Î» A HA HB,
  antichain_prop hk (lt_add_one k) H A HA A HB (subset.refl _)

/--
In particular, we can use induction and local LYM to get a bound on any top
part of the sum in LYM in terms of the size of `falling ğ’œ k`.
-/
lemma card_falling [decidable_eq Î±] {ğ’œ : finset (finset Î±)} {k : â„•} (hk : k â‰¤ card Î±)
  (H : antichain ğ’œ) :
  (range (k+1)).sum
    (Î» r, ((ğ’œ#(n - r)).card : â„š) / nat.choose n (n - r))
  â‰¤ (falling ğ’œ k).card / nat.choose n (n - k) :=
begin
  induction k with k ih,
  { simp [falling] },
  { rw [sum_range_succ, falling],
    convert add_le_add_left (trans (ih (le_of_lt hk)) _) _,
    { rw [card_disjoint_union, â† add_div, cast_add],
      exact disjoint_of_antichain hk H },
    { exact local_lym (nat.le_sub_left_of_add_le hk) (falling_sized _ _) } }
end

/--
A stepping-stone lemma to get to LYM.
-/
lemma card_fallen [decidable_eq Î±] {ğ’œ : finset (finset Î±)} (H : antichain ğ’œ) :
  (range (n+1)).sum (Î» r, ((ğ’œ#r).card : â„š) / nat.choose n r)
â‰¤ (falling ğ’œ n).card / nat.choose n 0 :=
begin
  rw [â† nat.sub_self n],
  convert â† card_falling (le_refl _) H using 1,
  apply sum_flip (Î» r, ((ğ’œ#r).card : â„š) / nat.choose n r),
end

/--
The LYM inequality says âˆ‘_i |A#i|/(n choose i) â‰¤ 1 for an antichain A.
Observe that A#i is all the stuff in A which has size i, and the collection of
subsets of (fin n) with size i has size (n choose i).
So, |A#i|/(n choose i) represents how much of each that A can take up.

The proof is easy using the developed lemmas above.
-/
theorem lubell_yamamoto_meshalkin {ğ’œ : finset (finset Î±)} (H : antichain ğ’œ) :
  (range (n + 1)).sum (Î» r, ((ğ’œ#r).card : â„š) / nat.choose n r) â‰¤ 1 :=
begin
  classical,
  transitivity,
  { apply card_fallen H },
  rw div_le_iff; norm_cast,
  { simpa only [mul_one, nat.choose_zero_right, nat.sub_self]
      using number_of_fixed_size (falling_sized ğ’œ (card Î±)) },
  apply nat.choose_pos (nat.zero_le _)
end

end lym

/--
Sperner's theorem gives a bound on the size of an antichain. This can be
proved in a few ways, but this uses the machinery already developed about LYM.
The idea is simple: with LYM, we get a bound on how much of A can have any
particular size.  So to maximise the size of A, we'd like to fit it all into
the term with the biggest denominator.
In other words,
âˆ‘_i |A#i|/(n choose i) â‰¤ 1, so
âˆ‘_i |A#i|/(n choose (n/2)) â‰¤ 1, so
âˆ‘_i |A#i| â‰¤ (n choose (n/2)), as required.
-/
theorem sperner [fintype Î±] {ğ’œ : finset (finset Î±)} (H : antichain ğ’œ) :
  ğ’œ.card â‰¤ nat.choose n (n / 2) :=
begin
  classical,
  have: (range (n + 1)).sum (Î» (r : â„•), ((ğ’œ#r).card : â„š) / nat.choose n (n/2)) â‰¤ 1,
  { apply le_trans _ (lubell_yamamoto_meshalkin H),
    apply sum_le_sum,
    intros r hr,
    apply div_le_div_of_le_left; norm_cast,
    { apply nat.zero_le },
    { apply choose_pos, rw mem_range at hr, rwa â† nat.lt_succ_iff },
    { apply choose_le_middle } },
  rw [â† sum_div, â† sum_nat_cast, div_le_one] at this,
  { norm_cast at this,
    rw â† card_bind at this,
    convert this,
    simp only [ext_iff, mem_slice, mem_bind, exists_prop, mem_range, lt_succ_iff],
    intro a,
    split,
    { intro ha,
      refine âŸ¨a.card, card_le_of_subset (subset_univ _), ha, rflâŸ© },
    { rintro âŸ¨_, _, q, _âŸ©,
      exact q },
    intros x _ y _ ne,
    rw disjoint_left,
    intros a Ha k,
    exact ne_of_diff_slice Ha k ne rfl },
  norm_cast,
  apply choose_pos,
  apply nat.div_le_self,
end
