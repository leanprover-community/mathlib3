/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.basic
import order.partition.finpartition
import tactic.positivity

/-!
# Edge density

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the number and density of edges of a relation/graph.

## Main declarations

Between two finsets of vertices,
* `rel.interedges`: Finset of edges of a relation.
* `rel.edge_density`: Edge density of a relation.
* `simple_graph.interedges`: Finset of edges of a graph.
* `simple_graph.edge_density`: Edge density of a graph.
-/

open finset
open_locale big_operators

variables {ğ•œ Î¹ Îº Î± Î² : Type*}

/-! ### Density of a relation -/

namespace rel
section asymmetric
variables [linear_ordered_field ğ•œ] (r : Î± â†’ Î² â†’ Prop) [Î  a, decidable_pred (r a)]
  {s sâ‚ sâ‚‚ : finset Î±} {t tâ‚ tâ‚‚ : finset Î²} {a : Î±} {b : Î²} {Î´ : ğ•œ}

/-- Finset of edges of a relation between two finsets of vertices. -/
def interedges (s : finset Î±) (t : finset Î²) : finset (Î± Ã— Î²) := (s Ã—Ë¢ t).filter $ Î» e, r e.1 e.2

/-- Edge density of a relation between two finsets of vertices. -/
def edge_density (s : finset Î±) (t : finset Î²) : â„š := (interedges r s t).card / (s.card * t.card)

variables {r}

lemma mem_interedges_iff {x : Î± Ã— Î²} : x âˆˆ interedges r s t â†” x.1 âˆˆ s âˆ§ x.2 âˆˆ t âˆ§ r x.1 x.2 :=
by simp only [interedges, and_assoc, mem_filter, finset.mem_product]

lemma mk_mem_interedges_iff : (a, b) âˆˆ interedges r s t â†” a âˆˆ s âˆ§ b âˆˆ t âˆ§ r a b :=
mem_interedges_iff

@[simp] lemma interedges_empty_left (t : finset Î²) : interedges r âˆ… t = âˆ… :=
by rw [interedges, finset.empty_product, filter_empty]

lemma interedges_mono (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) : interedges r sâ‚‚ tâ‚‚ âŠ† interedges r sâ‚ tâ‚ :=
Î» x, by { simp_rw mem_interedges_iff, exact Î» h, âŸ¨hs h.1, ht h.2.1, h.2.2âŸ© }

variables (r)

lemma card_interedges_add_card_interedges_compl (s : finset Î±) (t : finset Î²) :
  (interedges r s t).card + (interedges (Î» x y, Â¬r x y) s t).card = s.card * t.card :=
begin
  classical,
  rw [â†card_product, interedges, interedges, â†card_union_eq, filter_union_filter_neg_eq],
  convert disjoint_filter.2 (Î» x _, not_not.2),
end

lemma interedges_disjoint_left {s s' : finset Î±} (hs : disjoint s s') (t : finset Î²) :
  disjoint (interedges r s t) (interedges r s' t) :=
begin
  rw finset.disjoint_left at âŠ¢ hs,
  rintro x hx hy,
  rw [mem_interedges_iff] at hx hy,
  exact hs hx.1 hy.1,
end

lemma interedges_disjoint_right (s : finset Î±) {t t' : finset Î²} (ht : disjoint t t') :
  disjoint (interedges r s t) (interedges r s t') :=
begin
  rw finset.disjoint_left at âŠ¢ ht,
  rintro x hx hy,
  rw [mem_interedges_iff] at hx hy,
  exact ht hx.2.1 hy.2.1,
end

section decidable_eq
variables [decidable_eq Î±] [decidable_eq Î²]

lemma interedges_bUnion_left (s : finset Î¹) (t : finset Î²) (f : Î¹ â†’ finset Î±) :
  interedges r (s.bUnion f) t = s.bUnion (Î» a, interedges r (f a) t) :=
ext $ Î» a, by simp only [mem_bUnion, mem_interedges_iff, exists_and_distrib_right]

lemma interedges_bUnion_right (s : finset Î±) (t : finset Î¹) (f : Î¹ â†’ finset Î²) :
  interedges r s (t.bUnion f) = t.bUnion (Î» b, interedges r s (f b)) :=
ext $ Î» a, by simp only [mem_interedges_iff, mem_bUnion, â†exists_and_distrib_left,
  â†exists_and_distrib_right]

lemma interedges_bUnion (s : finset Î¹) (t : finset Îº) (f : Î¹ â†’ finset Î±) (g : Îº â†’ finset Î²) :
  interedges r (s.bUnion f) (t.bUnion g) =
    (s Ã—Ë¢ t).bUnion (Î» ab, interedges r (f ab.1) (g ab.2)) :=
by simp_rw [product_bUnion, interedges_bUnion_left, interedges_bUnion_right]

end decidable_eq

lemma card_interedges_le_mul (s : finset Î±) (t : finset Î²) :
  (interedges r s t).card â‰¤ s.card * t.card :=
(card_filter_le _ _).trans (card_product _ _).le

lemma edge_density_nonneg (s : finset Î±) (t : finset Î²) : 0 â‰¤ edge_density r s t :=
by { apply div_nonneg; exact_mod_cast nat.zero_le _ }

lemma edge_density_le_one (s : finset Î±) (t : finset Î²) : edge_density r s t â‰¤ 1 :=
div_le_one_of_le (by exact_mod_cast (card_interedges_le_mul _ _ _)) $
  by exact_mod_cast (nat.zero_le _)

lemma edge_density_add_edge_density_compl (hs : s.nonempty) (ht : t.nonempty) :
  edge_density r s t + edge_density (Î» x y, Â¬r x y) s t = 1 :=
begin
  rw [edge_density, edge_density, div_add_div_same, div_eq_one_iff_eq],
  { exact_mod_cast card_interedges_add_card_interedges_compl r s t },
  { exact_mod_cast (mul_pos hs.card_pos ht.card_pos).ne' }
end

@[simp] lemma edge_density_empty_left (t : finset Î²) : edge_density r âˆ… t = 0 :=
by rw [edge_density, finset.card_empty, nat.cast_zero, zero_mul, div_zero]

@[simp] lemma edge_density_empty_right (s : finset Î±) : edge_density r s âˆ… = 0 :=
by rw [edge_density, finset.card_empty, nat.cast_zero, mul_zero, div_zero]

lemma card_interedges_finpartition_left [decidable_eq Î±] (P : finpartition s) (t : finset Î²) :
  (interedges r s t).card = âˆ‘ a in P.parts, (interedges r a t).card :=
begin
  classical,
  simp_rw [â†P.bUnion_parts, interedges_bUnion_left, id.def],
  rw card_bUnion,
  exact Î» x hx y hy h, interedges_disjoint_left r (P.disjoint hx hy h) _,
end

lemma card_interedges_finpartition_right [decidable_eq Î²] (s : finset Î±) (P : finpartition t) :
  (interedges r s t).card = âˆ‘ b in P.parts, (interedges r s b).card :=
begin
  classical,
  simp_rw [â†P.bUnion_parts, interedges_bUnion_right, id],
  rw card_bUnion,
  exact Î» x hx y hy h, interedges_disjoint_right r _ (P.disjoint hx hy h),
end

lemma card_interedges_finpartition [decidable_eq Î±] [decidable_eq Î²] (P : finpartition s)
  (Q : finpartition t) :
  (interedges r s t).card = âˆ‘ ab in P.parts Ã—Ë¢ Q.parts, (interedges r ab.1 ab.2).card :=
by simp_rw [card_interedges_finpartition_left _ P, card_interedges_finpartition_right _ _ Q,
  sum_product]

lemma mul_edge_density_le_edge_density (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) (hsâ‚‚ : sâ‚‚.nonempty)
  (htâ‚‚ : tâ‚‚.nonempty) :
  (sâ‚‚.card : â„š)/sâ‚.card * (tâ‚‚.card/tâ‚.card) * edge_density r sâ‚‚ tâ‚‚ â‰¤ edge_density r sâ‚ tâ‚ :=
begin
  have hst : (sâ‚‚.card : â„š) * tâ‚‚.card â‰  0 := by simp [hsâ‚‚.ne_empty, htâ‚‚.ne_empty],
  rw [edge_density, edge_density, div_mul_div_comm, mul_comm, div_mul_div_cancel _ hst],
  refine div_le_div_of_le (by exact_mod_cast (sâ‚.card * tâ‚.card).zero_le) _,
  exact_mod_cast card_le_of_subset (interedges_mono hs ht),
end

lemma edge_density_sub_edge_density_le_one_sub_mul (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) (hsâ‚‚ : sâ‚‚.nonempty)
  (htâ‚‚ : tâ‚‚.nonempty) :
  edge_density r sâ‚‚ tâ‚‚ - edge_density r sâ‚ tâ‚ â‰¤ 1 - (sâ‚‚.card)/sâ‚.card * (tâ‚‚.card/tâ‚.card) :=
begin
  refine (sub_le_sub_left (mul_edge_density_le_edge_density r hs ht hsâ‚‚ htâ‚‚) _).trans _,
  refine le_trans _ (mul_le_of_le_one_right _ (edge_density_le_one r sâ‚‚ tâ‚‚)),
  { rw [sub_mul, one_mul] },
  refine sub_nonneg_of_le (mul_le_one _ (by positivity) _);
  exact div_le_one_of_le (nat.cast_le.2 (card_le_of_subset â€¹_â€º)) (nat.cast_nonneg _),
end

lemma abs_edge_density_sub_edge_density_le_one_sub_mul (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚)
  (hsâ‚‚ : sâ‚‚.nonempty) (htâ‚‚ : tâ‚‚.nonempty) :
  |edge_density r sâ‚‚ tâ‚‚ - edge_density r sâ‚ tâ‚| â‰¤ 1 - sâ‚‚.card/sâ‚.card * (tâ‚‚.card/tâ‚.card) :=
begin
  have habs : abs (edge_density r sâ‚‚ tâ‚‚ - edge_density r sâ‚ tâ‚) â‰¤ 1,
  { rw [abs_sub_le_iff, â†sub_zero (1 : â„š)],
    split; exact sub_le_sub (edge_density_le_one r _ _) (edge_density_nonneg r _ _) },
  refine abs_sub_le_iff.2 âŸ¨edge_density_sub_edge_density_le_one_sub_mul r hs ht hsâ‚‚ htâ‚‚, _âŸ©,
  rw [â†add_sub_cancel (edge_density r sâ‚ tâ‚) (edge_density (Î» x y, Â¬r x y) sâ‚ tâ‚),
    â†add_sub_cancel (edge_density r sâ‚‚ tâ‚‚) (edge_density (Î» x y, Â¬r x y) sâ‚‚ tâ‚‚),
    edge_density_add_edge_density_compl _ (hsâ‚‚.mono hs) (htâ‚‚.mono ht),
    edge_density_add_edge_density_compl _ hsâ‚‚ htâ‚‚, sub_sub_sub_cancel_left],
  exact edge_density_sub_edge_density_le_one_sub_mul _ hs ht hsâ‚‚ htâ‚‚,
end

lemma abs_edge_density_sub_edge_density_le_two_mul_sub_sq (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚)
  (hÎ´â‚€ : 0 â‰¤ Î´) (hÎ´â‚ : Î´ < 1) (hsâ‚‚ : (1 - Î´) * sâ‚.card â‰¤ sâ‚‚.card)
  (htâ‚‚ : (1 - Î´) * tâ‚.card â‰¤ tâ‚‚.card) :
  |(edge_density r sâ‚‚ tâ‚‚ : ğ•œ) - edge_density r sâ‚ tâ‚| â‰¤ 2*Î´ - Î´^2 :=
begin
  have hÎ´' : 0 â‰¤ 2 * Î´ - Î´ ^ 2,
  { rw [sub_nonneg, sq],
    exact mul_le_mul_of_nonneg_right (hÎ´â‚.le.trans (by norm_num)) hÎ´â‚€ },
  rw â†sub_pos at hÎ´â‚,
  obtain rfl | hsâ‚‚' := sâ‚‚.eq_empty_or_nonempty,
  { rw [finset.card_empty, nat.cast_zero] at hsâ‚‚,
    simpa [edge_density, (nonpos_of_mul_nonpos_right hsâ‚‚ hÎ´â‚).antisymm (nat.cast_nonneg _)]
      using hÎ´' },
  obtain rfl | htâ‚‚' := tâ‚‚.eq_empty_or_nonempty,
  { rw [finset.card_empty, nat.cast_zero] at htâ‚‚,
    simpa [edge_density, (nonpos_of_mul_nonpos_right htâ‚‚ hÎ´â‚).antisymm (nat.cast_nonneg _)]
      using hÎ´' },
  rw [show 2 * Î´ - Î´ ^ 2 = 1 - (1 - Î´) * (1 - Î´), by ring],
  norm_cast,
  refine (rat.cast_le.2 $
    abs_edge_density_sub_edge_density_le_one_sub_mul r hs ht hsâ‚‚' htâ‚‚').trans _,
  push_cast,
  have := hsâ‚‚'.mono hs,
  have := htâ‚‚'.mono ht,
  refine sub_le_sub_left (mul_le_mul ((le_div_iff _).2 hsâ‚‚) ((le_div_iff _).2 htâ‚‚) hÎ´â‚.le _) _;
  positivity,
end

/-- If `sâ‚‚ âŠ† sâ‚`, `tâ‚‚ âŠ† tâ‚` and they take up all but a `Î´`-proportion, then the difference in edge
densities is at most `2 * Î´`. -/
lemma abs_edge_density_sub_edge_density_le_two_mul (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) (hÎ´ : 0 â‰¤ Î´)
  (hscard : (1 - Î´) * sâ‚.card â‰¤ sâ‚‚.card) (htcard : (1 - Î´) * tâ‚.card â‰¤ tâ‚‚.card) :
  |(edge_density r sâ‚‚ tâ‚‚ : ğ•œ) - edge_density r sâ‚ tâ‚| â‰¤ 2 * Î´ :=
begin
  cases lt_or_le Î´ 1,
  { exact (abs_edge_density_sub_edge_density_le_two_mul_sub_sq r hs ht hÎ´ h hscard htcard).trans
    ((sub_le_self_iff _).2 $ sq_nonneg Î´) },
  rw two_mul,
  refine (abs_sub _ _).trans (add_le_add (le_trans _ h) (le_trans _ h));
  { rw abs_of_nonneg,
    exact_mod_cast edge_density_le_one r _ _,
    exact_mod_cast edge_density_nonneg r _ _ }
end

end asymmetric

section symmetric
variables (r : Î± â†’ Î± â†’ Prop) [decidable_rel r] {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ : finset Î±} {a b : Î±}
variables {r} (hr : symmetric r)
include hr

@[simp] lemma swap_mem_interedges_iff {x : Î± Ã— Î±} :
  x.swap âˆˆ interedges r s t â†” x âˆˆ interedges r t s :=
by { rw [mem_interedges_iff, mem_interedges_iff, hr.iff], exact and.left_comm }

lemma mk_mem_interedges_comm : (a, b) âˆˆ interedges r s t â†” (b, a) âˆˆ interedges r t s :=
@swap_mem_interedges_iff _ _ _ _ _ hr (b, a)

lemma card_interedges_comm (s t : finset Î±) : (interedges r s t).card = (interedges r t s).card :=
finset.card_congr (Î» (x : Î± Ã— Î±) _, x.swap) (Î» x, (swap_mem_interedges_iff hr).2)
  (Î» _ _ _ _ h, prod.swap_injective h)
  (Î» x h, âŸ¨x.swap, (swap_mem_interedges_iff hr).2 h, x.swap_swapâŸ©)

lemma edge_density_comm (s t : finset Î±) : edge_density r s t = edge_density r t s :=
by rw [edge_density, mul_comm, card_interedges_comm hr, edge_density]

end symmetric
end rel

open rel

/-! ### Density of a graph -/

namespace simple_graph
variables (G : simple_graph Î±) [decidable_rel G.adj] {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ : finset Î±} {a b : Î±}

/-- Finset of edges of a relation between two finsets of vertices. -/
def interedges (s t : finset Î±) : finset (Î± Ã— Î±) := interedges G.adj s t

/-- Density of edges of a graph between two finsets of vertices. -/
def edge_density : finset Î± â†’ finset Î± â†’ â„š := edge_density G.adj

lemma interedges_def (s t : finset Î±) :
  G.interedges s t = (s Ã—Ë¢ t).filter (Î» e, G.adj e.1 e.2) := rfl

lemma edge_density_def (s t : finset Î±) :
  G.edge_density s t = (G.interedges s t).card / (s.card * t.card) := rfl

@[simp] lemma card_interedges_div_card (s t : finset Î±) :
  ((G.interedges s t).card : â„š) / (s.card * t.card) = G.edge_density s t := rfl

lemma mem_interedges_iff {x : Î± Ã— Î±} : x âˆˆ G.interedges s t â†” x.1 âˆˆ s âˆ§ x.2 âˆˆ t âˆ§ G.adj x.1 x.2 :=
mem_interedges_iff

lemma mk_mem_interedges_iff : (a, b) âˆˆ G.interedges s t â†” a âˆˆ s âˆ§ b âˆˆ t âˆ§ G.adj a b :=
mk_mem_interedges_iff

@[simp] lemma interedges_empty_left (t : finset Î±) : G.interedges âˆ… t = âˆ… := interedges_empty_left _

lemma interedges_mono : sâ‚‚ âŠ† sâ‚ â†’ tâ‚‚ âŠ† tâ‚ â†’ G.interedges sâ‚‚ tâ‚‚ âŠ† G.interedges sâ‚ tâ‚ :=
interedges_mono

lemma interedges_disjoint_left (hs : disjoint sâ‚ sâ‚‚) (t : finset Î±) :
  disjoint (G.interedges sâ‚ t) (G.interedges sâ‚‚ t) :=
interedges_disjoint_left _ hs _

lemma interedges_disjoint_right (s : finset Î±) (ht : disjoint tâ‚ tâ‚‚) :
  disjoint (G.interedges s tâ‚) (G.interedges s tâ‚‚) :=
interedges_disjoint_right _ _ ht

section decidable_eq
variables [decidable_eq Î±]

lemma interedges_bUnion_left (s : finset Î¹) (t : finset Î±) (f : Î¹ â†’ finset Î±) :
  G.interedges (s.bUnion f) t = s.bUnion (Î» a, G.interedges (f a) t) :=
interedges_bUnion_left _ _ _ _

lemma interedges_bUnion_right (s : finset Î±) (t : finset Î¹) (f : Î¹ â†’ finset Î±) :
  G.interedges s (t.bUnion f) = t.bUnion (Î» b, G.interedges s (f b)) :=
interedges_bUnion_right _ _ _ _

lemma interedges_bUnion (s : finset Î¹) (t : finset Îº) (f : Î¹ â†’ finset Î±) (g : Îº â†’ finset Î±) :
  G.interedges (s.bUnion f) (t.bUnion g) =
    (s Ã—Ë¢ t).bUnion (Î» ab, G.interedges (f ab.1) (g ab.2)) :=
interedges_bUnion _ _ _ _ _

lemma card_interedges_add_card_interedges_compl (h : disjoint s t) :
  (G.interedges s t).card + (Gá¶œ.interedges s t).card = s.card * t.card :=
begin
  rw [â†card_product, interedges_def, interedges_def],
  have : (s Ã—Ë¢ t).filter (Î» e , Gá¶œ.adj e.1 e.2) = (s Ã—Ë¢ t).filter (Î» e , Â¬ G.adj e.1 e.2),
  { refine filter_congr (Î» x hx, _),
    rw mem_product at hx,
    rw [compl_adj, and_iff_right (h.forall_ne_finset hx.1 hx.2)] },
  rw [this, â†card_union_eq, filter_union_filter_neg_eq],
  exact disjoint_filter.2 (Î» x _, not_not.2),
end

lemma edge_density_add_edge_density_compl (hs : s.nonempty) (ht : t.nonempty) (h : disjoint s t) :
  G.edge_density s t + Gá¶œ.edge_density s t = 1 :=
begin
  rw [edge_density_def, edge_density_def, div_add_div_same, div_eq_one_iff_eq],
  { exact_mod_cast card_interedges_add_card_interedges_compl _ h },
  { positivity }
end

end decidable_eq

lemma card_interedges_le_mul (s t : finset Î±) : (G.interedges s t).card â‰¤ s.card * t.card :=
card_interedges_le_mul _ _ _

lemma edge_density_nonneg (s t : finset Î±) : 0 â‰¤ G.edge_density s t := edge_density_nonneg _ _ _
lemma edge_density_le_one (s t : finset Î±) : G.edge_density s t â‰¤ 1 := edge_density_le_one _ _ _

@[simp] lemma edge_density_empty_left (t : finset Î±) : G.edge_density âˆ… t = 0 :=
edge_density_empty_left _ _

@[simp] lemma edge_density_empty_right (s : finset Î±) : G.edge_density s âˆ… = 0 :=
edge_density_empty_right _ _

@[simp] lemma swap_mem_interedges_iff {x : Î± Ã— Î±} :
  x.swap âˆˆ G.interedges s t â†” x âˆˆ G.interedges t s :=
swap_mem_interedges_iff G.symm

lemma mk_mem_interedges_comm : (a, b) âˆˆ G.interedges s t â†” (b, a) âˆˆ G.interedges t s :=
mk_mem_interedges_comm G.symm

lemma edge_density_comm (s t : finset Î±) : G.edge_density s t = G.edge_density t s :=
edge_density_comm G.symm s t

end simple_graph

namespace tactic
open positivity

/-- Extension for the `positivity` tactic: `rel.edge_density` and `simple_graph.edge_density` are
always nonnegative. -/
@[positivity]
meta def positivity_edge_density : expr â†’ tactic strictness
| `(rel.edge_density %%r %%s %%t) := nonnegative <$>
                                       mk_mapp ``rel.edge_density_nonneg [none, none, r, none, s, t]
| `(simple_graph.edge_density %%G %%s %%t) := nonnegative <$>
                                    mk_mapp ``simple_graph.edge_density_nonneg [none, G, none, s, t]
| e := pp e >>= fail âˆ˜ format.bracket "The expression `"
    "` isn't of the form `rel.edge_density r s t` nor `simple_graph.edge_density G s t`"

end tactic
