/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.basic
import order.partition.finpartition

/-!
# Edge density and uniform graphs

This file defines the number and density of edges of a relation/graph and define uniformity in
graphs.

Two finsets of vertices `s` and `t` are `Îµ`-uniform in a graph `G` if the density of edges between
any pair of big subsets of `s` and `t` is within `Îµ` of the density of edges between `s` and `t`.
This captures the idea of the edges between `s` and `t` being randomly distributed.

## Main declarations

Between two finsets of vertices,
* `relation.edge_finset`: Finset of edges of a relation.
* `relation.edge_count`: Number of edges of a relation.
* `relation.edge_density`: Edge density of a relation.
* `simple_graph.edge_count`: Number of edges of a graph.
* `simple_graph.edge_density`: Edge density of a graph.

* `simple_graph.is_uniform`: `G.is_uniform Îµ s t` means that `s` and `t` are `Îµ`-uniform in `G`.
-/

open finset

variables {Î¹ Îº Î± Î² ğ•œ : Type*}

/-! ### Density of a relation -/

namespace relation
section asymmetric
variables (r : Î± â†’ Î² â†’ Prop) [Î  a, decidable_pred (r a)] {s sâ‚ sâ‚‚ : finset Î±} {t tâ‚ tâ‚‚ : finset Î²}
  {a : Î±} {b : Î²}

/-- Finset of edges of a relation between two finsets of vertices. -/
def edge_finset (s : finset Î±) (t : finset Î²) : finset (Î± Ã— Î²) :=
(s.product t).filter $ Î» e, r e.1 e.2

/-- Number of edges of a relation between two finsets of vertices. -/
def edge_count (s : finset Î±) (t : finset Î²) : â„• := (edge_finset r s t).card

/-- Edge density of a relation between two finsets of vertices. -/
def edge_density (s : finset Î±) (t : finset Î²) : â„š := edge_count r s t / (s.card * t.card)

variables {r}

lemma mem_edge_finset_iff {x : Î± Ã— Î²} : x âˆˆ edge_finset r s t â†” x.1 âˆˆ s âˆ§ x.2 âˆˆ t âˆ§ r x.1 x.2 :=
by simp only [edge_finset, and_assoc, mem_filter, finset.mem_product]

lemma mk_mem_edge_finset_iff : (a, b) âˆˆ edge_finset r s t â†” a âˆˆ s âˆ§ b âˆˆ t âˆ§ r a b :=
mem_edge_finset_iff

@[simp] lemma edge_finset_empty_left (t : finset Î²) : edge_finset r âˆ… t = âˆ… :=
by rw [edge_finset, finset.empty_product, filter_empty]

lemma edge_finset_mono (hs : sâ‚‚ âŠ† sâ‚) (ht : tâ‚‚ âŠ† tâ‚) : edge_finset r sâ‚‚ tâ‚‚ âŠ† edge_finset r sâ‚ tâ‚ :=
Î» x, by { simp_rw mem_edge_finset_iff, exact Î» h, âŸ¨hs h.1, ht h.2.1, h.2.2âŸ© }

variables (r)

lemma card_edge_finset_compl (s : finset Î±) (t : finset Î²) :
  (edge_finset r s t).card + (edge_finset (Î» x y, Â¬r x y) s t).card = s.card * t.card :=
begin
  classical,
  rw [â†card_product, edge_finset, edge_finset, â†card_union_eq, filter_union_filter_neg_eq],
  convert disjoint_filter.2 (Î» x _, not_not.2),
end

section decidable_eq
variables [decidable_eq Î±] [decidable_eq Î²]

lemma edge_finset_disjoint_left {s s' : finset Î±} (hs : disjoint s s') (t : finset Î²) :
  disjoint (edge_finset r s t) (edge_finset r s' t) :=
begin
  rintro x hx,
  rw [inf_eq_inter, mem_inter, mem_edge_finset_iff, mem_edge_finset_iff] at hx,
  exact hs (mem_inter.2 âŸ¨hx.1.1, hx.2.1âŸ©),
end

lemma edge_finset_disjoint_right (s : finset Î±) {t t' : finset Î²} (ht : disjoint t t') :
  disjoint (edge_finset r s t) (edge_finset r s t') :=
begin
  rintro x hx,
  rw [inf_eq_inter, mem_inter, mem_edge_finset_iff, mem_edge_finset_iff] at hx,
  exact ht (mem_inter.2 âŸ¨hx.1.2.1, hx.2.2.1âŸ©),
end

lemma edge_finset_bUnion_left (s : finset Î¹) (t : finset Î²) (f : Î¹ â†’ finset Î±) :
  edge_finset r (s.bUnion f) t = s.bUnion (Î» a, edge_finset r (f a) t) :=
ext $ Î» a, by simp only [mem_bUnion, mem_edge_finset_iff, exists_and_distrib_right]

lemma edge_finset_bUnion_right (s : finset Î±) (t : finset Î¹) (f : Î¹ â†’ finset Î²) :
  edge_finset r s (t.bUnion f) = t.bUnion (Î» b, edge_finset r s (f b)) :=
ext $ Î» a, by simp only [mem_edge_finset_iff, mem_bUnion, â†exists_and_distrib_left,
  â†exists_and_distrib_right]

lemma edge_finset_bUnion (s : finset Î¹) (t : finset Îº) (f : Î¹ â†’ finset Î±) (g : Îº â†’ finset Î²) :
  edge_finset r (s.bUnion f) (t.bUnion g) =
    (s.product t).bUnion (Î» ab, edge_finset r (f ab.1) (g ab.2)) :=
by simp_rw [product_bUnion, edge_finset_bUnion_left, edge_finset_bUnion_right]

end decidable_eq

lemma edge_count_le_mul (s : finset Î±) (t : finset Î²) : edge_count r s t â‰¤ s.card * t.card :=
begin
  rw [edge_count, edge_finset, â†finset.card_product],
  exact finset.card_filter_le _ _,
end

lemma edge_density_nonneg (s : finset Î±) (t : finset Î²) : 0 â‰¤ edge_density r s t :=
by { apply div_nonneg; exact_mod_cast nat.zero_le _ }

lemma edge_density_le_one (s : finset Î±) (t : finset Î²) : edge_density r s t â‰¤ 1 :=
div_le_one_of_le (by exact_mod_cast (edge_count_le_mul _ _ _)) (by exact_mod_cast (nat.zero_le _))

lemma edge_density_compl (hs : s.nonempty) (ht : t.nonempty) :
  edge_density r s t + edge_density (Î» x y, Â¬r x y) s t = 1 :=
begin
  rw [edge_density, edge_density, div_add_div_same, div_eq_one_iff_eq],
  { exact_mod_cast card_edge_finset_compl r s t },
  { exact_mod_cast (mul_pos hs.card_pos ht.card_pos).ne' },
end

@[simp] lemma edge_density_empty_left (t : finset Î²) : edge_density r âˆ… t = 0 :=
by rw [edge_density, finset.card_empty, nat.cast_zero, zero_mul, div_zero]

@[simp] lemma edge_density_empty_right (s : finset Î±) : edge_density r s âˆ… = 0 :=
by rw [edge_density, finset.card_empty, nat.cast_zero, mul_zero, div_zero]

end asymmetric

section symmetric
variables (r : Î± â†’ Î± â†’ Prop) [decidable_rel r] {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ : finset Î±} {a b : Î±}
variables {r} (hr : symmetric r)
include hr

@[simp] lemma swap_mem_edge_finset_iff {x : Î± Ã— Î±} :
  x.swap âˆˆ edge_finset r s t â†” x âˆˆ edge_finset r t s :=
by { rw [mem_edge_finset_iff, mem_edge_finset_iff, hr.iff], exact and.left_comm }

lemma mk_mem_edge_finset_comm : (a, b) âˆˆ edge_finset r s t â†” (b, a) âˆˆ edge_finset r t s :=
@swap_mem_edge_finset_iff _ _ _ _ _ hr (b, a)

lemma edge_count_comm (s t : finset Î±) : edge_count r s t = edge_count r t s :=
finset.card_congr (Î» (x : Î± Ã— Î±) _, x.swap) (Î» x, (swap_mem_edge_finset_iff hr).2)
  (Î» _ _ _ _ h, prod.swap_injective h)
  (Î» x h, âŸ¨x.swap, (swap_mem_edge_finset_iff hr).2 h, x.swap_swapâŸ©)

lemma edge_density_comm (s t : finset Î±) : edge_density r s t = edge_density r t s :=
by rw [edge_density, mul_comm, edge_count_comm hr, edge_density]

end symmetric
end relation

open relation

/-! ### Density of a graph -/

namespace simple_graph
variables (G : simple_graph Î±) [decidable_rel G.adj]

/-- Number of edges of a graph between two finsets of vertices. -/
def edge_count (s t : finset Î±) : â„• := edge_count G.adj s t

/-- Density of edges of a graph between two finsets of vertices. -/
def edge_density : finset Î± â†’ finset Î± â†’ â„š := edge_density G.adj

lemma edge_density_eq_edge_count_div_card (s t : finset Î±) :
  G.edge_density s t = G.edge_count s t/(s.card * t.card) := rfl

lemma edge_density_comm (s t : finset Î±) : G.edge_density s t = G.edge_density t s :=
edge_density_comm G.symm s t

lemma edge_density_nonneg (s t : finset Î±) : 0 â‰¤ G.edge_density s t := edge_density_nonneg _ s t
lemma edge_density_le_one (s t : finset Î±) : G.edge_density s t â‰¤ 1 := edge_density_le_one _ s t

/-! ###  Uniform graphs -/

variables [linear_ordered_field ğ•œ] (Îµ : ğ•œ) {s t : finset Î±} {a b : Î±}

/-- A pair of finsets of vertices is `Îµ`-uniform iff their edge density is close to the density of
any big enough pair of subsets. Intuitively, the edges between them are random-like. -/
def is_uniform (s t : finset Î±) : Prop :=
âˆ€ â¦ƒs'â¦„, s' âŠ† s â†’ âˆ€ â¦ƒt'â¦„, t' âŠ† t â†’ (s.card : ğ•œ) * Îµ â‰¤ s'.card â†’ (t.card : ğ•œ) * Îµ â‰¤ t'.card â†’
  |(edge_density G s' t' : ğ•œ) - (edge_density G s t : ğ•œ)| < Îµ

variables {G Îµ}

/-- If the pair `(s, t)` is `Îµ`-uniform and `Îµ â‰¤ Îµ'`, then it is `Îµ'`-uniform. -/
lemma is_uniform.mono {Îµ' : ğ•œ} (hÎµ : is_uniform G Îµ s t) (h : Îµ â‰¤ Îµ') : is_uniform G Îµ' s t :=
Î» s' hs' t' ht' hs ht, by refine (hÎµ hs' ht' (le_trans _ hs) (le_trans _ ht)).trans_le h;
  exact mul_le_mul_of_nonneg_left h (nat.cast_nonneg _)

lemma is_uniform.symm : symmetric (is_uniform G Îµ) :=
Î» s t h t' ht' s' hs' ht hs,
  by { rw [edge_density_comm _ t', edge_density_comm _ t], exact h hs' ht' hs ht }

variables (G)

lemma is_uniform_comm : is_uniform G Îµ s t â†” is_uniform G Îµ t s := âŸ¨Î» h, h.symm, Î» h, h.symmâŸ©

lemma is_uniform_singleton (hÎµ : 0 < Îµ) : G.is_uniform Îµ {a} {b} :=
begin
  intros s' hs' t' ht' hs ht,
  rw [card_singleton, nat.cast_one, one_mul] at hs ht,
  obtain rfl | rfl := finset.subset_singleton_iff.1 hs',
  { exact (hÎµ.not_le hs).elim },
  obtain rfl | rfl := finset.subset_singleton_iff.1 ht',
  { exact (hÎµ.not_le ht).elim },
  { rwa [sub_self, abs_zero] }
end

lemma not_is_uniform_zero : Â¬ G.is_uniform (0 : ğ•œ) s t :=
Î» h, (abs_nonneg _).not_lt $ h (empty_subset _) (empty_subset _) (by simp) (by simp)

lemma is_uniform_of_one_le (hÎµ : 1 â‰¤ Îµ) : G.is_uniform Îµ s t :=
begin
  intros s' hs' t' ht' hs ht,
  have h : âˆ€ n : â„•, (n : ğ•œ) â‰¤ n * Îµ := Î» n, le_mul_of_one_le_right n.cast_nonneg hÎµ,
  rw [eq_of_subset_of_card_le hs' (nat.cast_le.1 ((h _).trans hs)),
    eq_of_subset_of_card_le ht' (nat.cast_le.1 ((h _).trans ht)), sub_self, abs_zero],
  exact zero_lt_one.trans_le hÎµ,
end

end simple_graph
