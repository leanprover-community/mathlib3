/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.density
import set_theory.ordinal.basic

/-!
# Graph uniformity and uniform partitions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define uniformity of a pair of vertices in a graph and uniformity of a partition of
vertices of a graph. Both are also known as Îµ-regularity.

Finsets of vertices `s` and `t` are `Îµ`-uniform in a graph `G` if their edge density is at most
`Îµ`-far from the density of any big enough `s'` and `t'` where `s' âŠ† s`, `t' âŠ† t`.
The definition is pretty technical, but it amounts to the edges between `s` and `t` being "random"
The literature contains several definitions which are equivalent up to scaling `Îµ` by some constant
when the partition is equitable.

A partition `P` of the vertices is `Îµ`-uniform if the proportion of non `Îµ`-uniform pairs of parts
is less than `Îµ`.

## Main declarations

* `simple_graph.is_uniform`: Graph uniformity of a pair of finsets of vertices.
* `simple_graph.nonuniform_witness`: `G.nonuniform_witness Îµ s t` and `G.nonuniform_witness Îµ t s`
  together witness the non-uniformity of `s` and `t`.
* `finpartition.non_uniforms`: Non uniform pairs of parts of a partition.
* `finpartition.is_uniform`: Uniformity of a partition.
* `finpartition.nonuniform_witnesses`: For each non-uniform pair of parts of a partition, pick
  witnesses of non-uniformity and dump them all together.
-/

open finset

variables {Î± ğ•œ : Type*} [linear_ordered_field ğ•œ]

/-! ###  Graph uniformity -/

namespace simple_graph
variables (G : simple_graph Î±) [decidable_rel G.adj] (Îµ : ğ•œ) {s t : finset Î±} {a b : Î±}

/-- A pair of finsets of vertices is `Îµ`-uniform (aka `Îµ`-regular) iff their edge density is close
to the density of any big enough pair of subsets. Intuitively, the edges between them are
random-like. -/
def is_uniform (s t : finset Î±) : Prop :=
âˆ€ â¦ƒs'â¦„, s' âŠ† s â†’ âˆ€ â¦ƒt'â¦„, t' âŠ† t â†’ (s.card : ğ•œ) * Îµ â‰¤ s'.card â†’ (t.card : ğ•œ) * Îµ â‰¤ t'.card â†’
  |(G.edge_density s' t' : ğ•œ) - (G.edge_density s t : ğ•œ)| < Îµ

variables {G Îµ}

lemma is_uniform.mono {Îµ' : ğ•œ} (h : Îµ â‰¤ Îµ') (hÎµ : is_uniform G Îµ s t) : is_uniform G Îµ' s t :=
Î» s' hs' t' ht' hs ht, by refine (hÎµ hs' ht' (le_trans _ hs) (le_trans _ ht)).trans_le h;
  exact mul_le_mul_of_nonneg_left h (nat.cast_nonneg _)

lemma is_uniform.symm : symmetric (is_uniform G Îµ) :=
Î» s t h t' ht' s' hs' ht hs,
  by { rw [edge_density_comm _ t', edge_density_comm _ t], exact h hs' ht' hs ht }

variables (G)

lemma is_uniform_comm : is_uniform G Îµ s t â†” is_uniform G Îµ t s := âŸ¨Î» h, h.symm, Î» h, h.symmâŸ©

lemma is_uniform_singleton (hÎµ : 0 < Îµ) : G.is_uniform Îµ {a} {b} :=
begin
  intros s' hs' t' ht' hs ht,
  rw [card_singleton, nat.cast_one, one_mul] at hs ht,
  obtain rfl | rfl := finset.subset_singleton_iff.1 hs',
  { replace hs : Îµ â‰¤ 0 := by simpa using hs,
    exact (hÎµ.not_le hs).elim },
  obtain rfl | rfl := finset.subset_singleton_iff.1 ht',
  { replace ht : Îµ â‰¤ 0 := by simpa using ht,
    exact (hÎµ.not_le ht).elim },
  { rwa [sub_self, abs_zero] }
end

lemma not_is_uniform_zero : Â¬ G.is_uniform (0 : ğ•œ) s t :=
Î» h, (abs_nonneg _).not_lt $ h (empty_subset _) (empty_subset _) (by simp) (by simp)

lemma is_uniform_one : G.is_uniform (1 : ğ•œ) s t :=
begin
  intros s' hs' t' ht' hs ht,
  rw mul_one at hs ht,
  rw [eq_of_subset_of_card_le hs' (nat.cast_le.1 hs),
    eq_of_subset_of_card_le ht' (nat.cast_le.1 ht), sub_self, abs_zero],
  exact zero_lt_one,
end

variables {G}

lemma not_is_uniform_iff :
  Â¬ G.is_uniform Îµ s t â†” âˆƒ s', s' âŠ† s âˆ§ âˆƒ t', t' âŠ† t âˆ§ â†‘s.card * Îµ â‰¤ s'.card âˆ§
    â†‘t.card * Îµ â‰¤ t'.card âˆ§  Îµ â‰¤ |G.edge_density s' t' - G.edge_density s t| :=
by { unfold is_uniform, simp only [not_forall, not_lt, exists_prop] }

open_locale classical
variables (G)

/-- An arbitrary pair of subsets witnessing the non-uniformity of `(s, t)`. If `(s, t)` is uniform,
returns `(s, t)`. Witnesses for `(s, t)` and `(t, s)` don't necessarily match. See
`simple_graph.nonuniform_witness`. -/
noncomputable def nonuniform_witnesses (Îµ : ğ•œ) (s t : finset Î±) : finset Î± Ã— finset Î± :=
if h : Â¬ G.is_uniform Îµ s t
  then ((not_is_uniform_iff.1 h).some, (not_is_uniform_iff.1 h).some_spec.2.some)
  else (s, t)

lemma left_nonuniform_witnesses_subset (h : Â¬ G.is_uniform Îµ s t) :
  (G.nonuniform_witnesses Îµ s t).1 âŠ† s :=
by { rw [nonuniform_witnesses, dif_pos h], exact (not_is_uniform_iff.1 h).some_spec.1 }

lemma left_nonuniform_witnesses_card (h : Â¬ G.is_uniform Îµ s t) :
  (s.card : ğ•œ) * Îµ â‰¤ (G.nonuniform_witnesses Îµ s t).1.card :=
by { rw [nonuniform_witnesses, dif_pos h],
  exact (not_is_uniform_iff.1 h).some_spec.2.some_spec.2.1 }

lemma right_nonuniform_witnesses_subset (h : Â¬ G.is_uniform Îµ s t) :
  (G.nonuniform_witnesses Îµ s t).2 âŠ† t :=
by { rw [nonuniform_witnesses, dif_pos h], exact (not_is_uniform_iff.1 h).some_spec.2.some_spec.1 }

lemma right_nonuniform_witnesses_card (h : Â¬ G.is_uniform Îµ s t) :
  (t.card : ğ•œ) * Îµ â‰¤ (G.nonuniform_witnesses Îµ s t).2.card :=
by { rw [nonuniform_witnesses, dif_pos h],
  exact (not_is_uniform_iff.1 h).some_spec.2.some_spec.2.2.1 }

lemma nonuniform_witnesses_spec (h : Â¬ G.is_uniform Îµ s t) :
  Îµ â‰¤ |G.edge_density (G.nonuniform_witnesses Îµ s t).1 (G.nonuniform_witnesses Îµ s t).2
    - G.edge_density s t| :=
by { rw [nonuniform_witnesses, dif_pos h],
  exact (not_is_uniform_iff.1 h).some_spec.2.some_spec.2.2.2 }

/-- Arbitrary witness of non-uniformity. `G.nonuniform_witness Îµ s t` and
`G.nonuniform_witness Îµ t s` form a pair of subsets witnessing the non-uniformity of `(s, t)`. If
`(s, t)` is uniform, returns `s`. -/
noncomputable def nonuniform_witness (Îµ : ğ•œ) (s t : finset Î±) : finset Î± :=
if well_ordering_rel s t then (G.nonuniform_witnesses Îµ s t).1 else (G.nonuniform_witnesses Îµ t s).2

lemma nonuniform_witness_subset (h : Â¬ G.is_uniform Îµ s t) : G.nonuniform_witness Îµ s t âŠ† s :=
begin
  unfold nonuniform_witness,
  split_ifs,
  { exact G.left_nonuniform_witnesses_subset h },
  { exact G.right_nonuniform_witnesses_subset (Î» i, h i.symm) }
end

lemma nonuniform_witness_card_le (h : Â¬ G.is_uniform Îµ s t) :
  (s.card : ğ•œ) * Îµ â‰¤ (G.nonuniform_witness Îµ s t).card :=
begin
  unfold nonuniform_witness,
  split_ifs,
  { exact G.left_nonuniform_witnesses_card h },
  { exact G.right_nonuniform_witnesses_card (Î» i, h i.symm) }
end

lemma nonuniform_witness_spec (hâ‚ : s â‰  t) (hâ‚‚ : Â¬ G.is_uniform Îµ s t) :
  Îµ â‰¤ |G.edge_density (G.nonuniform_witness Îµ s t) (G.nonuniform_witness Îµ t s)
    - G.edge_density s t| :=
begin
  unfold nonuniform_witness,
  rcases trichotomous_of well_ordering_rel s t with lt | rfl | gt,
  { rw [if_pos lt, if_neg (asymm lt)],
    exact G.nonuniform_witnesses_spec hâ‚‚ },
  { cases hâ‚ rfl },
  { rw [if_neg (asymm gt), if_pos gt, edge_density_comm, edge_density_comm _ s],
    apply G.nonuniform_witnesses_spec (Î» i, hâ‚‚ i.symm) }
end

end simple_graph

/-! ### Uniform partitions -/

variables [decidable_eq Î±] {A : finset Î±} (P : finpartition A) (G : simple_graph Î±)
  [decidable_rel G.adj] {Îµ : ğ•œ}

namespace finpartition
open_locale classical

/-- The pairs of parts of a partition `P` which are not `Îµ`-uniform in a graph `G`. Note that we
dismiss the diagonal. We do not care whether `s` is `Îµ`-uniform with itself. -/
noncomputable def non_uniforms (Îµ : ğ•œ) : finset (finset Î± Ã— finset Î±) :=
P.parts.off_diag.filter $ Î» uv, Â¬G.is_uniform Îµ uv.1 uv.2

lemma mk_mem_non_uniforms_iff (u v : finset Î±) (Îµ : ğ•œ) :
  (u, v) âˆˆ P.non_uniforms G Îµ â†” u âˆˆ P.parts âˆ§ v âˆˆ P.parts âˆ§ u â‰  v âˆ§ Â¬G.is_uniform Îµ u v :=
by rw [non_uniforms, mem_filter, mem_off_diag, and_assoc, and_assoc]

lemma non_uniforms_mono {Îµ Îµ' : ğ•œ} (h : Îµ â‰¤ Îµ') : P.non_uniforms G Îµ' âŠ† P.non_uniforms G Îµ :=
monotone_filter_right _ $ Î» uv, mt $ simple_graph.is_uniform.mono h

lemma non_uniforms_bot (hÎµ : 0 < Îµ) : (âŠ¥ : finpartition A).non_uniforms G Îµ = âˆ… :=
begin
  rw eq_empty_iff_forall_not_mem,
  rintro âŸ¨u, vâŸ©,
  simp only [finpartition.mk_mem_non_uniforms_iff, finpartition.parts_bot, mem_map, not_and,
    not_not, exists_imp_distrib],
  rintro x hx rfl y hy rfl h,
  exact G.is_uniform_singleton hÎµ,
end

/-- A finpartition of a graph's vertex set is `Îµ`-uniform (aka `Îµ`-regular) iff the proportion of
its pairs of parts that are not `Îµ`-uniform is at most `Îµ`. -/
def is_uniform (Îµ : ğ•œ) : Prop :=
((P.non_uniforms G Îµ).card : ğ•œ) â‰¤ (P.parts.card * (P.parts.card - 1) : â„•) * Îµ

lemma bot_is_uniform (hÎµ : 0 < Îµ) : (âŠ¥ : finpartition A).is_uniform G Îµ :=
begin
  rw [finpartition.is_uniform, finpartition.card_bot, non_uniforms_bot _ hÎµ,
    finset.card_empty, nat.cast_zero],
  exact mul_nonneg (nat.cast_nonneg _) hÎµ.le,
end

lemma is_uniform_one : P.is_uniform G (1 : ğ•œ) :=
begin
  rw [is_uniform, mul_one, nat.cast_le],
  refine (card_filter_le _ _).trans _,
  rw [off_diag_card, nat.mul_sub_left_distrib, mul_one],
end

variables {P G}

lemma is_uniform.mono {Îµ Îµ' : ğ•œ} (hP : P.is_uniform G Îµ) (h : Îµ â‰¤ Îµ') : P.is_uniform G Îµ' :=
((nat.cast_le.2 $ card_le_of_subset $ P.non_uniforms_mono G h).trans hP).trans $
  mul_le_mul_of_nonneg_left h $ nat.cast_nonneg _

lemma is_uniform_of_empty (hP : P.parts = âˆ…) : P.is_uniform G Îµ :=
by simp [is_uniform, hP, non_uniforms]

lemma nonempty_of_not_uniform (h : Â¬ P.is_uniform G Îµ) : P.parts.nonempty :=
nonempty_of_ne_empty $ Î» hâ‚, h $ is_uniform_of_empty hâ‚

variables (P G Îµ) (s : finset Î±)

/-- A choice of witnesses of non-uniformity among the parts of a finpartition. -/
noncomputable def nonuniform_witnesses : finset (finset Î±) :=
(P.parts.filter $ Î» t, s â‰  t âˆ§ Â¬ G.is_uniform Îµ s t).image (G.nonuniform_witness Îµ s)

variables {P G Îµ s} {t : finset Î±}

lemma nonuniform_witness_mem_nonuniform_witnesses (h : Â¬ G.is_uniform Îµ s t) (ht : t âˆˆ P.parts)
  (hst : s â‰  t) :
  G.nonuniform_witness Îµ s t âˆˆ P.nonuniform_witnesses G Îµ s :=
mem_image_of_mem _ $ mem_filter.2 âŸ¨ht, hst, hâŸ©

end finpartition
