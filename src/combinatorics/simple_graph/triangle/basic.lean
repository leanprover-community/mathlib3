/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.clique

/-!
# Triangles in graphs

A *triangle* in a simple graph is a `3`-clique, namely a set of three vertices that are
pairwise adjacent.

This module defines and proves properties about triangles in simple graphs.

## Main declarations

* `simple_graph.triangle_free_far`: Predicate for a graph to have enough triangles that, to remove
  all of them, one must one must remove a lot of edges. This is the crux of the Triangle Removal
  lemma.

## TODO

* Generalise `triangle_free_far` to other graphs, to state and prove the Graph Removal Lemma.
* Find a better name for `triangle_free_far`. (Added 4/26/2022. Remove this TODO if it gets old.)
-/

open finset fintype
open_locale classical

namespace simple_graph
variables {Î± ğ•œ : Type*} [fintype Î±] [linear_ordered_field ğ•œ] {G H : simple_graph Î±} {Îµ Î´ : ğ•œ}
  {n : â„•} {s : finset Î±}

/-- A simple graph is *`Îµ`-triangle-free far* if one must remove at least `Îµ * (card Î±)^2` edges to
make it triangle-free. -/
def triangle_free_far (G : simple_graph Î±) (Îµ : ğ•œ) : Prop :=
âˆ€ â¦ƒHâ¦„, H â‰¤ G â†’ H.clique_free 3 â†’ Îµ * (card Î±^2 : â„•) â‰¤ G.edge_finset.card - H.edge_finset.card

lemma triangle_free_far_iff (G : simple_graph Î±) (Îµ : ğ•œ) :
  G.triangle_free_far Îµ â†”
  âˆ€ (S âŠ† G.edge_finset), (G.delete_edges S).clique_free 3 â†’ Îµ * (card Î± ^ 2 : â„•) â‰¤ S.card :=
begin
  split,
  { intros h S hS htf,
    simpa [finset.card_sdiff, hS, edge_finset_delete_edges, finset.card_le_of_subset]
      using h (G.delete_edges_le S) htf },
  { intros h H hH htf,
    have hs : â†‘(G.edge_finset \ H.edge_finset) = G.edge_set \ H.edge_set,
    { ext e, simp },
    simpa [hs, delete_edges_sdiff_eq_of_le, hH, htf, edge_finset_mono hH, finset.card_sdiff,
      finset.card_le_of_subset, nat.cast_pow]
      using h (G.edge_finset \ H.edge_finset) (finset.sdiff_subset _ _), }
end

lemma triangle_free_far.mono (hÎµ : G.triangle_free_far Îµ) (h : Î´ â‰¤ Îµ) : G.triangle_free_far Î´ :=
Î» I hIG hI, (mul_le_mul_of_nonneg_right h $ nat.cast_nonneg _).trans $ hÎµ hIG hI

lemma triangle_free_far.clique_finset_nonempty' (hH : H â‰¤ G) (hG : G.triangle_free_far Îµ)
  (hcard : (G.edge_finset.card - H.edge_finset.card : ğ•œ) < Îµ * (card Î± ^ 2 : â„•)) :
  (H.clique_finset 3).nonempty :=
nonempty_of_ne_empty $ H.clique_finset_eq_empty_iff.not.2 $ Î» hH', (hG hH hH').not_lt hcard

variables [nonempty Î±]

lemma triangle_free_far.nonpos (hâ‚€ : G.triangle_free_far Îµ) (hâ‚ : G.clique_free 3) : Îµ â‰¤ 0 :=
begin
  have := hâ‚€ le_rfl hâ‚,
  rw sub_self at this,
  exact nonpos_of_mul_nonpos_right this (nat.cast_pos.2 $ sq_pos_of_pos fintype.card_pos),
end

lemma clique_free.not_triangle_free_far (hG : G.clique_free 3) (hÎµ : 0 < Îµ) :
  Â¬ G.triangle_free_far Îµ :=
Î» h, (h.nonpos hG).not_lt hÎµ

lemma triangle_free_far.not_clique_free (hG : G.triangle_free_far Îµ) (hÎµ : 0 < Îµ) :
  Â¬ G.clique_free 3 :=
Î» h, (hG.nonpos h).not_lt hÎµ

lemma triangle_free_far.clique_finset_nonempty (hG : G.triangle_free_far Îµ) (hÎµ : 0 < Îµ) :
  (G.clique_finset 3).nonempty :=
nonempty_of_ne_empty $ G.clique_finset_eq_empty_iff.not.2 $ hG.not_clique_free hÎµ

end simple_graph
