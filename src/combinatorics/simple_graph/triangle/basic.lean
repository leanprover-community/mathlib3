/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.clique

/-!
# Triangles in graphs

This file defines triangles in simple graphs.

A triangle is a `3`-clique, that is a set of three vertices which are all connected.

## Main declarations

* `simple_graph.triangle_finset`: Finset of triangles of a graph.
* `simple_graph.triangle_free`: Predicate for a graph to have no triangle.
* `simple_graph.triangle_free_far`: Predicate for a graph to have enough triangles that one must
  remove a lot of edges to remove all of them. This is the crux of the Triangle Removal lemma.
-/

open finset fintype

namespace simple_graph
variables {Î± ğ•œ : Type*} [linear_ordered_field ğ•œ] (G H : simple_graph Î±) {n : â„•} {s : finset Î±}

section triangle_free

/-- `G.triangle_free` means that `G` has no triangles. -/
def triangle_free : Prop := âˆ€ t, Â¬ G.is_n_clique 3 t

variables {G H}

lemma triangle_free_bot : (âŠ¥ : simple_graph Î±).triangle_free :=
begin
  rintro t âŸ¨htâ‚, htâ‚‚âŸ©,
  have : 1 < t.card,
  { rw htâ‚,
    norm_num },
  rw finset.one_lt_card at this,
  obtain âŸ¨a, ha, b, hb, habâŸ© := this,
  exact htâ‚‚ ha hb hab,
end

lemma triangle_free.le (h : G â‰¤ H) : H.triangle_free â†’ G.triangle_free :=
forall_imp $ Î» s, mt $ is_n_clique.le h

end triangle_free

section triangle_finset
variables (G) [fintype Î±] [decidable_eq Î±] [decidable_rel G.adj] {a b c : Î±}

/-- The triangles in a graph as a finset. -/
def triangle_finset : finset (finset Î±) := (powerset_len 3 univ).filter $ G.is_n_clique 3

lemma mem_triangle_finset_iff (s : finset Î±) : s âˆˆ G.triangle_finset â†” G.is_n_clique 3 s :=
by rw [triangle_finset, mem_filter, mem_powerset_len, and_iff_right (subset_univ _),
  and_iff_right_of_imp is_n_clique.card_eq]

lemma triple_mem_triangle_finset_iff :
  {a, b, c} âˆˆ G.triangle_finset â†” G.adj a b âˆ§ G.adj a c âˆ§ G.adj b c :=
begin
  rw [mem_triangle_finset_iff, is_n_clique_iff],
  simp only [coe_insert, coe_singleton, set.pairwise_insert_of_symmetric G.symm,
    set.pairwise_singleton, true_and, set.mem_insert_iff, set.mem_singleton_iff,
    forall_eq_or_imp, forall_eq, ne.def],
  split,
  { rintro âŸ¨h, hbc, hab, hacâŸ©,
    refine âŸ¨hab _, hac _, hbc _âŸ©;
    { rintro rfl,
      simp only [insert_idem, insert_singleton_comm, insert_singleton_self_eq] at h,
      exact h.not_lt (nat.lt_succ_iff.2 $ card_insert_le _ _) } },
  { rintro âŸ¨hab, hac, hbcâŸ©,
    refine âŸ¨_, Î» _, hbc, Î» _, hab, Î» _, hacâŸ©,
    rw card_eq_three,
    exact âŸ¨_, _, _, G.ne_of_adj hab, G.ne_of_adj hac, G.ne_of_adj hbc, rflâŸ© }
end

lemma mem_triangle_finset_iff' :
  s âˆˆ G.triangle_finset â†” âˆƒ a b c, G.adj a b âˆ§ G.adj a c âˆ§ G.adj b c âˆ§ s = {a, b, c} :=
begin
  refine âŸ¨Î» h, _, _âŸ©,
  { obtain âŸ¨a, b, c, -, -, -, rflâŸ© := card_eq_three.1 ((G.mem_triangle_finset_iff s).1 h).1,
    refine âŸ¨a, b, c, _âŸ©,
    rw triple_mem_triangle_finset_iff at h,
    tauto },
  { rintro âŸ¨a, b, c, hab, hbc, hca, rflâŸ©,
    exact G.triple_mem_triangle_finset_iff.2 âŸ¨hab, hbc, hcaâŸ© }
end

@[simp] lemma triangle_finset_eq_empty_iff : G.triangle_finset = âˆ… â†” G.triangle_free :=
by simp_rw [triangle_free, eq_empty_iff_forall_not_mem, mem_triangle_finset_iff]

variables {G} [decidable_rel H.adj]

lemma triangle_finset_mono (h : G â‰¤ H) : G.triangle_finset âŠ† H.triangle_finset :=
monotone_filter_right _ $ Î» _, is_n_clique.le h

end triangle_finset

section triangle_free_far
variables [fintype Î±] {G H} {Îµ Î´ : ğ•œ}

open_locale classical

/-- A simple graph is `Îµ`-triangle-free far if one must remove a density of `Îµ` edges to make it
triangle-free. -/
def triangle_free_far (G : simple_graph Î±) (Îµ : ğ•œ) : Prop :=
âˆ€ â¦ƒHâ¦„, H â‰¤ G â†’ H.triangle_free â†’ Îµ * (card Î±^2 : â„•) â‰¤ G.edge_finset.card - H.edge_finset.card

lemma triangle_free_far.mono (hÎµ : G.triangle_free_far Îµ) (h : Î´ â‰¤ Îµ) : G.triangle_free_far Î´ :=
Î» I hIG hI, (mul_le_mul_of_nonneg_right h $ nat.cast_nonneg _).trans $ hÎµ hIG hI

lemma triangle_free_far.triangle_finset_nonempty' (hH : H â‰¤ G) (hG : G.triangle_free_far Îµ)
  (hcard : (G.edge_finset.card - H.edge_finset.card : ğ•œ) < Îµ * (card Î± ^ 2 : â„•)) :
  H.triangle_finset.nonempty :=
begin
  apply nonempty_of_ne_empty,
  rw [ne.def, triangle_finset_eq_empty_iff],
  exact Î» hH', (hG hH hH').not_lt hcard ,
end

variables [nonempty Î±]

lemma triangle_free_far.nonpos (hâ‚€ : G.triangle_free_far Îµ) (hâ‚ : G.triangle_free) : Îµ â‰¤ 0 :=
begin
  have := hâ‚€ le_rfl hâ‚,
  rw sub_self at this,
  exact nonpos_of_mul_nonpos_right this (nat.cast_pos.2 $ sq_pos_of_pos fintype.card_pos),
end

lemma triangle_free.not_triangle_free_far (hG : G.triangle_free) (hÎµ : 0 < Îµ) :
  Â¬ G.triangle_free_far Îµ :=
Î» h, (h.nonpos hG).not_lt hÎµ

lemma triangle_free_far.not_triangle_free (hG : G.triangle_free_far Îµ) (hÎµ : 0 < Îµ) :
  Â¬ G.triangle_free :=
Î» h, (hG.nonpos h).not_lt hÎµ

lemma triangle_free_far.triangle_finset_nonempty (hG : G.triangle_free_far Îµ) (hÎµ : 0 < Îµ) :
  G.triangle_finset.nonempty :=
nonempty_of_ne_empty $ G.triangle_finset_eq_empty_iff.not.2 $ hG.not_triangle_free hÎµ

end triangle_free_far
end simple_graph
