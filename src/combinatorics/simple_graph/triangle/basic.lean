/-
Copyright (c) 2022 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.clique

/-!
# Triangles in graphs

This file defines triangles in simple graphs.

A triangle is a `3`-clique, that is a set of three vertices which are all connected.

## Main declarations

* `simple_graph.triangle_finset`: Finset of triangles of a graph.
* `simple_graph.triangle_free`: Predicate for a graph to have no triangle.
* `simple_graph.triangle_free_far`: Predicate for a graph to have enough triangles that one must
  remove a lot of edges to remove all of them. This is the crux of the Triangle Removal lemma.
-/

open finset fintype

namespace simple_graph
variables {Œ± ùïú : Type*} [linear_ordered_field ùïú] (G H : simple_graph Œ±) {n : ‚Ñï} {s : finset Œ±}

section triangle_free

/-- `G.triangle_free` means that `G` has no triangles. -/
def triangle_free : Prop := ‚àÄ t, ¬¨ G.is_n_clique 3 t

variables {G H}

lemma triangle_free_bot : (‚ä• : simple_graph Œ±).triangle_free :=
begin
  rintro t ht,
  rw is_n_clique_bot_iff at ht,
  norm_num at ht,
end

lemma triangle_free.mono (h : G ‚â§ H) : H.triangle_free ‚Üí G.triangle_free :=
forall_imp $ Œª s, mt $ is_n_clique.mono h

end triangle_free

section triangle_finset
variables (G) [fintype Œ±] [decidable_eq Œ±] [decidable_rel G.adj] {a b c : Œ±}

/-- The triangles in a graph as a finset. -/
def triangle_finset : finset (finset Œ±) := (powerset_len 3 univ).filter $ G.is_n_clique 3

lemma mem_triangle_finset_iff (s : finset Œ±) : s ‚àà G.triangle_finset ‚Üî G.is_n_clique 3 s :=
by rw [triangle_finset, mem_filter, mem_powerset_len, and_iff_right (subset_univ _),
  and_iff_right_of_imp is_n_clique.card_eq]

lemma triple_mem_triangle_finset_iff :
  {a, b, c} ‚àà G.triangle_finset ‚Üî G.adj a b ‚àß G.adj a c ‚àß G.adj b c :=
begin
  rw [mem_triangle_finset_iff, is_n_clique_iff, is_clique_iff],
  simp only [coe_insert, coe_singleton, set.pairwise_insert_of_symmetric G.symm,
    set.pairwise_singleton, true_and, set.mem_insert_iff, set.mem_singleton_iff,
    forall_eq_or_imp, forall_eq, ne.def],
  split,
  { rintro ‚ü®‚ü®hbc, hab, hac‚ü©, h‚ü©,
    refine ‚ü®hab _, hac _, hbc _‚ü©;
    { rintro rfl,
      simp only [insert_idem, insert_singleton_comm, insert_singleton_self_eq] at h,
      exact h.not_lt (nat.lt_succ_iff.2 $ card_insert_le _ _) } },
  { rintro ‚ü®hab, hac, hbc‚ü©,
    refine ‚ü®‚ü®Œª _, hbc, Œª _, hab, Œª _, hac‚ü©, _‚ü©,
    rw card_eq_three,
    exact ‚ü®_, _, _, G.ne_of_adj hab, G.ne_of_adj hac, G.ne_of_adj hbc, rfl‚ü© }
end

lemma mem_triangle_finset_iff' :
  s ‚àà G.triangle_finset ‚Üî ‚àÉ a b c, G.adj a b ‚àß G.adj a c ‚àß G.adj b c ‚àß s = {a, b, c} :=
begin
  refine ‚ü®Œª h, _, _‚ü©,
  { obtain ‚ü®a, b, c, -, -, -, rfl‚ü© := card_eq_three.1 ((G.mem_triangle_finset_iff s).1 h).2,
    refine ‚ü®a, b, c, _‚ü©,
    rw triple_mem_triangle_finset_iff at h,
    tauto },
  { rintro ‚ü®a, b, c, hab, hbc, hca, rfl‚ü©,
    exact G.triple_mem_triangle_finset_iff.2 ‚ü®hab, hbc, hca‚ü© }
end

@[simp] lemma triangle_finset_eq_empty_iff : G.triangle_finset = ‚àÖ ‚Üî G.triangle_free :=
by simp_rw [triangle_free, eq_empty_iff_forall_not_mem, mem_triangle_finset_iff]

alias triangle_finset_eq_empty_iff ‚Üî _ simple_graph.triangle_free.triangle_finset

attribute [protected] triangle_free.triangle_finset

variables {G} [decidable_rel H.adj]

lemma triangle_finset_mono (h : G ‚â§ H) : G.triangle_finset ‚äÜ H.triangle_finset :=
monotone_filter_right _ $ Œª _, is_n_clique.mono h

end triangle_finset

section triangle_free_far
variables [fintype Œ±] {G H} {Œµ Œ¥ : ùïú}

open_locale classical

/-- A simple graph is `Œµ`-triangle-free far if one must remove a density of `Œµ` edges to make it
triangle-free. -/
def triangle_free_far (G : simple_graph Œ±) (Œµ : ùïú) : Prop :=
‚àÄ ‚¶ÉH‚¶Ñ, H ‚â§ G ‚Üí H.triangle_free ‚Üí Œµ * (card Œ±^2 : ‚Ñï) ‚â§ G.edge_finset.card - H.edge_finset.card

lemma triangle_free_far.mono (hŒµ : G.triangle_free_far Œµ) (h : Œ¥ ‚â§ Œµ) : G.triangle_free_far Œ¥ :=
Œª I hIG hI, (mul_le_mul_of_nonneg_right h $ nat.cast_nonneg _).trans $ hŒµ hIG hI

lemma triangle_free_far.triangle_finset_nonempty' (hH : H ‚â§ G) (hG : G.triangle_free_far Œµ)
  (hcard : (G.edge_finset.card - H.edge_finset.card : ùïú) < Œµ * (card Œ± ^ 2 : ‚Ñï)) :
  H.triangle_finset.nonempty :=
begin
  apply nonempty_of_ne_empty,
  rw [ne.def, triangle_finset_eq_empty_iff],
  exact Œª hH', (hG hH hH').not_lt hcard,
end

variables [nonempty Œ±]

lemma triangle_free_far.nonpos (h‚ÇÄ : G.triangle_free_far Œµ) (h‚ÇÅ : G.triangle_free) : Œµ ‚â§ 0 :=
begin
  have := h‚ÇÄ le_rfl h‚ÇÅ,
  rw sub_self at this,
  exact nonpos_of_mul_nonpos_right this (nat.cast_pos.2 $ sq_pos_of_pos fintype.card_pos),
end

lemma triangle_free.not_triangle_free_far (hG : G.triangle_free) (hŒµ : 0 < Œµ) :
  ¬¨ G.triangle_free_far Œµ :=
Œª h, (h.nonpos hG).not_lt hŒµ

lemma triangle_free_far.not_triangle_free (hG : G.triangle_free_far Œµ) (hŒµ : 0 < Œµ) :
  ¬¨ G.triangle_free :=
Œª h, (hG.nonpos h).not_lt hŒµ

lemma triangle_free_far.triangle_finset_nonempty (hG : G.triangle_free_far Œµ) (hŒµ : 0 < Œµ) :
  G.triangle_finset.nonempty :=
nonempty_of_ne_empty $ G.triangle_finset_eq_empty_iff.not.2 $ hG.not_triangle_free hŒµ

end triangle_free_far
end simple_graph
