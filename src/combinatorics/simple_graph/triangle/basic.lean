/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.clique
import data.nat.parity
import data.sym.card

/-!
# Triangles in graphs

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

A *triangle* in a simple graph is a `3`-clique, namely a set of three vertices that are
pairwise adjacent.

This module defines and proves properties about triangles in simple graphs.

## Main declarations

* `simple_graph.far_from_triangle_free`: Predicate for a graph to have enough triangles that, to
  remove all of them, one must one must remove a lot of edges. This is the crux of the Triangle
  Removal lemma.

## TODO

* Generalise `far_from_triangle_free` to other graphs, to state and prove the Graph Removal Lemma.
* Find a better name for `far_from_triangle_free`. Added 4/26/2022. Remove this TODO if it gets old.
-/

namespace simple_graph
variables {V : Type*}

@[simp] lemma edge_set_top : (âŠ¤ : simple_graph V).edge_set = {e : sym2 V | Â¬ e.is_diag} :=
by ext x; induction x using sym2.ind; simp

@[simp] lemma edge_finset_top [fintype V] [decidable_eq V] [fintype (âŠ¤ : simple_graph V).edge_set] :
  (âŠ¤ : simple_graph V).edge_finset = finset.univ.filter (Î» e, Â¬ e.is_diag) :=
by ext x; induction x using sym2.ind; simp

end simple_graph

open finset fintype nat
open_locale classical

namespace simple_graph
variables {Î± ğ•œ : Type*} [fintype Î±] [linear_ordered_field ğ•œ] {G H : simple_graph Î±} {Îµ Î´ : ğ•œ}
  {n : â„•} {s : finset Î±}

/-- A simple graph is *`Îµ`-triangle-free far* if one must remove at least `Îµ * (card Î±)^2` edges to
make it triangle-free. -/
def far_from_triangle_free (G : simple_graph Î±) (Îµ : ğ•œ) : Prop :=
G.delete_far (Î» H, H.clique_free 3) $ Îµ * (card Î±^2 : â„•)

lemma far_from_triangle_free_iff :
  G.far_from_triangle_free Îµ â†”
    âˆ€ â¦ƒHâ¦„, H â‰¤ G â†’ H.clique_free 3 â†’ Îµ * (card Î±^2 : â„•) â‰¤ G.edge_finset.card - H.edge_finset.card :=
delete_far_iff

alias far_from_triangle_free_iff â†” far_from_triangle_free.le_card_sub_card _

lemma far_from_triangle_free.mono (hÎµ : G.far_from_triangle_free Îµ) (h : Î´ â‰¤ Îµ) :
  G.far_from_triangle_free Î´ :=
hÎµ.mono $ mul_le_mul_of_nonneg_right h $ cast_nonneg _

lemma far_from_triangle_free.clique_finset_nonempty' (hH : H â‰¤ G) (hG : G.far_from_triangle_free Îµ)
  (hcard : (G.edge_finset.card - H.edge_finset.card : ğ•œ) < Îµ * (card Î± ^ 2 : â„•)) :
  (H.clique_finset 3).nonempty :=
nonempty_of_ne_empty $ H.clique_finset_eq_empty_iff.not.2 $ Î» hH',
  (hG.le_card_sub_card hH hH').not_lt hcard

variables [nonempty Î±]

lemma far_from_triangle_free.lt_half (hG : G.far_from_triangle_free Îµ) : Îµ < 2â»Â¹ :=
begin
  by_contra' hÎµ,
  have := hG.le_card_sub_card bot_le (clique_free_bot $ by norm_num),
  simp only [set.to_finset_card (edge_set âŠ¥), card_of_finset, edge_set_bot, cast_zero,
    finset.card_empty, tsub_zero] at this,
  have hÎµâ‚€ : 0 < Îµ := hÎµ.trans_lt' (by norm_num),
  rw inv_pos_le_iff_one_le_mul (zero_lt_two' ğ•œ) at hÎµ,
  refine (this.trans $ le_mul_of_one_le_left (by positivity) hÎµ).not_lt _,
  rw [mul_assoc, mul_lt_mul_left hÎµâ‚€],
  norm_cast,
  refine (mul_le_mul_left' (card_mono $ edge_finset_mono le_top) _).trans_lt _,
  rw [edge_finset_top, filter_not, card_sdiff (subset_univ _), card_univ, sym2.card],
  simp_rw [sym2.is_diag_iff_mem_range_diag, univ_filter_mem_range, mul_tsub,
    nat.mul_div_cancel' (card Î±).even_mul_succ_self.two_dvd],
  rw [card_image_of_injective _ sym2.diag_injective, card_univ, mul_add_one, two_mul, sq,
    add_tsub_add_eq_tsub_right],
  exact tsub_lt_self (mul_pos fintype.card_pos fintype.card_pos) fintype.card_pos,
end

lemma far_from_triangle_free.lt_one (hG : G.far_from_triangle_free Îµ) : Îµ < 1 :=
hG.lt_half.trans $ by norm_num

lemma far_from_triangle_free.nonpos (hâ‚€ : G.far_from_triangle_free Îµ) (hâ‚ : G.clique_free 3) :
  Îµ â‰¤ 0 :=
begin
  have := hâ‚€ (empty_subset _),
  rw [coe_empty, finset.card_empty, cast_zero, delete_edges_empty_eq] at this,
  exact nonpos_of_mul_nonpos_left (this hâ‚) (cast_pos.2 $ sq_pos_of_pos fintype.card_pos),
end

lemma clique_free.not_far_from_triangle_free (hG : G.clique_free 3) (hÎµ : 0 < Îµ) :
  Â¬ G.far_from_triangle_free Îµ :=
Î» h, (h.nonpos hG).not_lt hÎµ

lemma far_from_triangle_free.not_clique_free (hG : G.far_from_triangle_free Îµ) (hÎµ : 0 < Îµ) :
  Â¬ G.clique_free 3 :=
Î» h, (hG.nonpos h).not_lt hÎµ

lemma far_from_triangle_free.clique_finset_nonempty (hG : G.far_from_triangle_free Îµ) (hÎµ : 0 < Îµ) :
  (G.clique_finset 3).nonempty :=
nonempty_of_ne_empty $ G.clique_finset_eq_empty_iff.not.2 $ hG.not_clique_free hÎµ

variables {G H} {tris : finset (finset Î±)}

private lemma far_from_triangle_free_of_disjoint_triangles_aux (htris : tris âŠ† G.clique_finset 3)
  (pd : (tris : set (finset Î±)).pairwise (Î» x y, (x âˆ© y).card â‰¤ 1)) (hHG : H â‰¤ G)
  (hH : H.clique_free 3) : tris.card â‰¤ G.edge_finset.card - H.edge_finset.card :=
begin
  rw [â†card_sdiff (edge_finset_mono hHG), â†card_attach],
  by_contra' hG,
  have : âˆ€ t, t âˆˆ tris â†’ âˆƒ x y, x âˆˆ t âˆ§ y âˆˆ t âˆ§ x â‰  y âˆ§ âŸ¦(x, y)âŸ§ âˆˆ G.edge_finset \ H.edge_finset,
  { intros t ht,
    by_contra' h,
    refine hH t _,
    simp only [not_and, mem_sdiff, not_not, mem_edge_finset, mem_edge_set] at h,
    obtain âŸ¨x, y, z, xy, xz, yz, rflâŸ© := is_3_clique_iff.1 (G.mem_clique_finset_iff.1 $ htris ht),
    rw is_3_clique_triple_iff,
    refine âŸ¨h _ _ _ _ xy.ne xy, h _ _ _ _ xz.ne xz, h _ _ _ _ yz.ne yzâŸ©; simp },
  choose fx fy hfx hfy hfne fmem using this,
  let f : {x // x âˆˆ tris} â†’ sym2 Î± := Î» t, âŸ¦(fx _ t.2, fy _ t.2)âŸ§,
  have hf : âˆ€ x, x âˆˆ tris.attach â†’ f x âˆˆ G.edge_finset \ H.edge_finset := Î» x hx, fmem _ _,
  obtain âŸ¨âŸ¨tâ‚, htâ‚âŸ©, -, âŸ¨tâ‚‚, htâ‚‚âŸ©, -, tne, t : âŸ¦_âŸ§ = âŸ¦_âŸ§âŸ© :=
    exists_ne_map_eq_of_card_lt_of_maps_to hG hf,
  dsimp at t,
  have i := pd htâ‚ htâ‚‚ (subtype.val_injective.ne tne),
  simp only [finset.card_le_one_iff, mem_inter, and_imp] at i,
  rw sym2.eq_iff at t,
  cases t,
  { exact hfne _ _ (i (hfx tâ‚ htâ‚) (t.1.symm â–¸ hfx tâ‚‚ htâ‚‚) (hfy tâ‚ htâ‚) $ t.2.symm â–¸ hfy tâ‚‚ htâ‚‚) },
  { exact hfne _ _ (i (hfx tâ‚ htâ‚) (t.1.symm â–¸ hfy tâ‚‚ htâ‚‚) (hfy tâ‚ htâ‚) $ t.2.symm â–¸ hfx tâ‚‚ htâ‚‚) }
end

/-- If there are `Îµ * (card Î±)^2` disjoint triangles, then the graph is `Îµ`-far from being
triangle-free. -/
lemma far_from_triangle_free_of_disjoint_triangles (tris : finset (finset Î±))
  (htris : tris âŠ† G.clique_finset 3)
  (pd : (tris : set (finset Î±)).pairwise (Î» x y, (x âˆ© y).card â‰¤ 1))
  (tris_big : Îµ * (card Î± ^ 2 : â„•) â‰¤ tris.card) :
  G.far_from_triangle_free Îµ :=
begin
  refine far_from_triangle_free_iff.2 (Î» H hG hH, _),
  rw â†nat.cast_sub (card_le_of_subset $ edge_finset_mono hG),
  exact tris_big.trans
    (nat.cast_le.2 $ far_from_triangle_free_of_disjoint_triangles_aux htris pd hG hH),
end

end simple_graph
