/-
Copyright (c) 2023 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.triangle.basic

/-!
# Construct a tripartite graph from its triangles

This file contains the construction of a simple graph on `Œ± ‚äï Œ≤ ‚äï Œ≥` from a list of triangles
`(a, b, c)` (with `a` in the first summand, `b` in the second, `c` in the third).

We call
* `t : finset (Œ± √ó Œ≤ √ó Œ≥)` the set of *triangle indices* (its elements are not triangles within the
  graph but instead index them).
* *explicit* a triangle of the constructed graph coming from a triangle index.
* *accidental* a triangle of the constructed graph not coming from a triangle.

The two important properties of this construction are:
* `simple_graph.tripartite_from_triangles.explicit_disjoint`: Whether the explicit triangles are
  edge-disjoint.
* `simple_graph.tripartite_from_triangles.no_accidental`: Whether all triangles are explicit.

This construction shows up unrelatingly twice in the theory of Roth numbers:
* The lower bound of the Ruzsa-Szemer√©di problem: From a Salem-Spencer set `s` on a commutative ring
  `Œ±` (in which `2` is invertible), we build a locally linear graph on `Œ± ‚äï Œ± ‚äï Œ±`. The triangle
  indices are `(x, x + a, x + 2 * a)` for `x` any element and `a ‚àà s`. The explicit triangles are
  edge-disjoint and there is no accidental triangle.
* The proof of the corners theorem from the triangle removal lemma. For a subset `s` of the `n √ó n`
  grid, we construct a tripartite graph whose vertices are the horizontal, vertical and diagonal
  lines in the grid. The explicit triangles are `(h, v, d)` where `h`, `v`, `d` are horizontal,
  vertical, diagonal lines that intersect in an element of `s`. The explicit triangles are
  edge-disjoint. However, there are accidental triangles (and this is what the argument wants to
  prove).
-/

open finset function sum3

variables {Œ± Œ≤ Œ≥ ùïú : Type*} [linear_ordered_field ùïú] {t : finset (Œ± √ó Œ≤ √ó Œ≥)} {a a' : Œ±} {b b' : Œ≤}
  {c c' : Œ≥} {x : Œ± √ó Œ≤ √ó Œ≥} {Œµ : ùïú}

namespace simple_graph
namespace tripartite_from_triangles

/-- The underlying relation of the tripartite-from-triangles graph. Two vertices are related iff
there exists a triangle index containing them both. -/
@[mk_iff] inductive rel (t : finset (Œ± √ó Œ≤ √ó Œ≥)) : Œ± ‚äï Œ≤ ‚äï Œ≥ ‚Üí Œ± ‚äï Œ≤ ‚äï Œ≥ ‚Üí Prop
| in‚ÇÄ‚ÇÅ ‚¶Éa b c‚¶Ñ : (a, b, c) ‚àà t ‚Üí rel (in‚ÇÄ a) (in‚ÇÅ b)
| in‚ÇÅ‚ÇÄ ‚¶Éa b c‚¶Ñ : (a, b, c) ‚àà t ‚Üí rel (in‚ÇÅ b) (in‚ÇÄ a)
| in‚ÇÄ‚ÇÇ ‚¶Éa b c‚¶Ñ : (a, b, c) ‚àà t ‚Üí rel (in‚ÇÄ a) (in‚ÇÇ c)
| in‚ÇÇ‚ÇÄ ‚¶Éa b c‚¶Ñ : (a, b, c) ‚àà t ‚Üí rel (in‚ÇÇ c) (in‚ÇÄ a)
| in‚ÇÅ‚ÇÇ ‚¶Éa b c‚¶Ñ : (a, b, c) ‚àà t ‚Üí rel (in‚ÇÅ b) (in‚ÇÇ c)
| in‚ÇÇ‚ÇÅ ‚¶Éa b c‚¶Ñ : (a, b, c) ‚àà t ‚Üí rel (in‚ÇÇ c) (in‚ÇÅ b)

open rel

lemma rel_irrefl : ‚àÄ x, ¬¨ rel t x x.
lemma rel_symm : symmetric (rel t) := Œª x y h, by cases h; constructor; assumption

/-- The tripartite-from-triangles graph. Two vertices are related iff there exists a triangle index
containing them both. -/
def graph (t : finset (Œ± √ó Œ≤ √ó Œ≥)) : simple_graph (Œ± ‚äï Œ≤ ‚äï Œ≥) := ‚ü®rel t, rel_symm, rel_irrefl‚ü©

namespace graph

@[simp] lemma not_in‚ÇÄ‚ÇÄ : ¬¨ (graph t).adj (in‚ÇÄ a) (in‚ÇÄ a') .
@[simp] lemma not_in‚ÇÅ‚ÇÅ : ¬¨ (graph t).adj (in‚ÇÅ b) (in‚ÇÅ b') .
@[simp] lemma not_in‚ÇÇ‚ÇÇ : ¬¨ (graph t).adj (in‚ÇÇ c) (in‚ÇÇ c') .

@[simp] lemma in‚ÇÄ‚ÇÅ_iff : (graph t).adj (in‚ÇÄ a) (in‚ÇÅ b) ‚Üî ‚àÉ c, (a, b, c) ‚àà t :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫‚ü© }, Œª ‚ü®_, h‚ü©, in‚ÇÄ‚ÇÅ h‚ü©
@[simp] lemma in‚ÇÅ‚ÇÄ_iff : (graph t).adj (in‚ÇÅ b) (in‚ÇÄ a) ‚Üî ‚àÉ c, (a, b, c) ‚àà t :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫‚ü© }, Œª ‚ü®_, h‚ü©, in‚ÇÅ‚ÇÄ h‚ü©
@[simp] lemma in‚ÇÄ‚ÇÇ_iff : (graph t).adj (in‚ÇÄ a) (in‚ÇÇ c) ‚Üî ‚àÉ b, (a, b, c) ‚àà t :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫‚ü© }, Œª ‚ü®_, h‚ü©, in‚ÇÄ‚ÇÇ h‚ü©
@[simp] lemma in‚ÇÇ‚ÇÄ_iff : (graph t).adj (in‚ÇÇ c) (in‚ÇÄ a) ‚Üî ‚àÉ b, (a, b, c) ‚àà t :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫‚ü© }, Œª ‚ü®_, h‚ü©, in‚ÇÇ‚ÇÄ h‚ü©
@[simp] lemma in‚ÇÅ‚ÇÇ_iff : (graph t).adj (in‚ÇÅ b) (in‚ÇÇ c) ‚Üî ‚àÉ a, (a, b, c) ‚àà t :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫‚ü© }, Œª ‚ü®_, h‚ü©, in‚ÇÅ‚ÇÇ h‚ü©
@[simp] lemma in‚ÇÇ‚ÇÅ_iff : (graph t).adj (in‚ÇÇ c) (in‚ÇÅ b) ‚Üî ‚àÉ a, (a, b, c) ‚àà t :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫‚ü© }, Œª ‚ü®_, h‚ü©, in‚ÇÇ‚ÇÅ h‚ü©

lemma in‚ÇÄ‚ÇÅ_iff' :
  (graph t).adj (in‚ÇÄ a) (in‚ÇÅ b) ‚Üî ‚àÉ (x : Œ± √ó Œ≤ √ó Œ≥) (hx : x ‚àà t), x.1 = a ‚àß x.2.1 = b :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫, rfl, rfl‚ü© },
  by { rintro ‚ü®‚ü®a, b, c‚ü©, h, rfl, rfl‚ü©, constructor, assumption }‚ü©
lemma in‚ÇÅ‚ÇÄ_iff' :
  (graph t).adj (in‚ÇÅ b) (in‚ÇÄ a) ‚Üî ‚àÉ (x : Œ± √ó Œ≤ √ó Œ≥) (hx : x ‚àà t), x.2.1 = b ‚àß x.1 = a :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫, rfl, rfl‚ü© },
  by { rintro ‚ü®‚ü®a, b, c‚ü©, h, rfl, rfl‚ü©, constructor, assumption }‚ü©
lemma in‚ÇÄ‚ÇÇ_iff' :
  (graph t).adj (in‚ÇÄ a) (in‚ÇÇ c) ‚Üî ‚àÉ (x : Œ± √ó Œ≤ √ó Œ≥) (hx : x ‚àà t), x.1 = a ‚àß x.2.2 = c :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫, rfl, rfl‚ü© },
  by { rintro ‚ü®‚ü®a, b, c‚ü©, h, rfl, rfl‚ü©, constructor, assumption }‚ü©
lemma in‚ÇÇ‚ÇÄ_iff' :
  (graph t).adj (in‚ÇÇ c) (in‚ÇÄ a) ‚Üî ‚àÉ (x : Œ± √ó Œ≤ √ó Œ≥) (hx : x ‚àà t), x.2.2 = c ‚àß x.1 = a :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫, rfl, rfl‚ü© },
  by { rintro ‚ü®‚ü®a, b, c‚ü©, h, rfl, rfl‚ü©, constructor, assumption }‚ü©
lemma in‚ÇÅ‚ÇÇ_iff' :
  (graph t).adj (in‚ÇÅ b) (in‚ÇÇ c) ‚Üî ‚àÉ (x : Œ± √ó Œ≤ √ó Œ≥) (hx : x ‚àà t), x.2.1 = b ‚àß x.2.2 = c :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫, rfl, rfl‚ü© },
  by { rintro ‚ü®‚ü®a, b, c‚ü©, h, rfl, rfl‚ü©, constructor, assumption }‚ü©
lemma in‚ÇÇ‚ÇÅ_iff' :
  (graph t).adj (in‚ÇÇ c) (in‚ÇÅ b) ‚Üî ‚àÉ (x : Œ± √ó Œ≤ √ó Œ≥) (hx : x ‚àà t), x.2.2 = c ‚àß x.2.1 = b :=
‚ü®by { rintro ‚ü®‚ü©, exact ‚ü®_, ‚Äπ_‚Ä∫, rfl, rfl‚ü© },
  by { rintro ‚ü®‚ü®a, b, c‚ü©, h, rfl, rfl‚ü©, constructor, assumption }‚ü©

end graph

open graph

/-- Predicate on the triangle indices for the explicit triangles to be edge-disjoint. -/
class explicit_disjoint (t : finset (Œ± √ó Œ≤ √ó Œ≥)) : Prop :=
(inj‚ÇÄ : ‚àÄ ‚¶Éa b c a'‚¶Ñ, (a, b, c) ‚àà t ‚Üí (a', b, c) ‚àà t ‚Üí a = a')
(inj‚ÇÅ : ‚àÄ ‚¶Éa b c b'‚¶Ñ, (a, b, c) ‚àà t ‚Üí (a, b', c) ‚àà t ‚Üí b = b')
(inj‚ÇÇ : ‚àÄ ‚¶Éa b c c'‚¶Ñ, (a, b, c) ‚àà t ‚Üí (a, b, c') ‚àà t ‚Üí c = c')

/-- Predicate on the triangle indices for there to be no accidental triangle.

Note that we cheat a bit, since the exact translation of this informal description would have
`(a', b', c') ‚àà s` as a conclusion rather than `a = a' ‚à® b = b' ‚à® c = c'`. Those conditions are
equivalent when the explicit triangles are edge-disjoint (which is the case we care about). -/
class no_accidental (t : finset (Œ± √ó Œ≤ √ó Œ≥)) : Prop :=
(wow : ‚àÄ ‚¶Éa a' b b' c c'‚¶Ñ, (a', b, c) ‚àà t ‚Üí (a, b', c) ‚àà t ‚Üí (a, b, c') ‚àà t ‚Üí
  a = a' ‚à® b = b' ‚à® c = c')

section decidable_eq
variables [decidable_eq Œ±] [decidable_eq Œ≤] [decidable_eq Œ≥]

instance : decidable_rel (graph t).adj
| (in‚ÇÄ a) (in‚ÇÄ a') := decidable.is_false not_in‚ÇÄ‚ÇÄ
| (in‚ÇÄ a) (in‚ÇÅ b') := decidable_of_iff' _ in‚ÇÄ‚ÇÅ_iff'
| (in‚ÇÄ a) (in‚ÇÇ c') := decidable_of_iff' _ in‚ÇÄ‚ÇÇ_iff'
| (in‚ÇÅ b) (in‚ÇÄ a') := decidable_of_iff' _ in‚ÇÅ‚ÇÄ_iff'
| (in‚ÇÅ b) (in‚ÇÅ b') := decidable.is_false not_in‚ÇÅ‚ÇÅ
| (in‚ÇÅ b) (in‚ÇÇ b') := decidable_of_iff' _ in‚ÇÅ‚ÇÇ_iff'
| (in‚ÇÇ c) (in‚ÇÄ a') := decidable_of_iff' _ in‚ÇÇ‚ÇÄ_iff'
| (in‚ÇÇ c) (in‚ÇÅ b') := decidable_of_iff' _ in‚ÇÇ‚ÇÅ_iff'
| (in‚ÇÇ c) (in‚ÇÇ b') := decidable.is_false not_in‚ÇÇ‚ÇÇ

/-- This lemma reorders the elements of a triangle in the tripartite graph. It turns a triangle
`{x, y, z}` into a triangle `{a, b, c}` where `a : Œ± `, `b : Œ≤`, `c : Œ≥`. -/
 lemma graph_triple ‚¶Éx y z‚¶Ñ :
  (graph t).adj x y ‚Üí (graph t).adj x z ‚Üí (graph t).adj y z ‚Üí ‚àÉ a b c,
    ({in‚ÇÄ a, in‚ÇÅ b, in‚ÇÇ c} : finset (Œ± ‚äï Œ≤ ‚äï Œ≥)) = {x, y, z} ‚àß (graph t).adj (in‚ÇÄ a) (in‚ÇÅ b) ‚àß
      (graph t).adj (in‚ÇÄ a) (in‚ÇÇ c) ‚àß (graph t).adj (in‚ÇÅ b) (in‚ÇÇ c) :=
by rintro (_ | _ | _) (_ | _ | _) (_ | _ | _); refine ‚ü®_, _, _, by ext; simp only
  [finset.mem_insert, finset.mem_singleton]; try { tauto }, _, _, _‚ü©; constructor; assumption

/-- The map that turns a triangle index into an explicit triangle. -/
@[simps] def to_triangle : Œ± √ó Œ≤ √ó Œ≥ ‚Ü™ finset (Œ± ‚äï Œ≤ ‚äï Œ≥) :=
{ to_fun := Œª x, {in‚ÇÄ x.1, in‚ÇÅ x.2.1, in‚ÇÇ x.2.2},
  inj' := Œª ‚ü®a, b, c‚ü© ‚ü®a', b', c'‚ü©, by simpa only [finset.subset.antisymm_iff, finset.subset_iff,
    mem_insert, mem_singleton, forall_eq_or_imp, forall_eq, prod.mk.inj_iff, or_false, false_or,
    in‚ÇÄ, in‚ÇÅ, in‚ÇÇ, sum.inl.inj_eq, sum.inr.inj_eq] using and.left }

lemma to_triangle_is_3_clique (hx : x ‚àà t) : (graph t).is_n_clique 3 (to_triangle x) :=
begin
  rcases x with ‚ü®a, b, c‚ü©,
  simp only [to_triangle_apply, is_3_clique_triple_iff, in‚ÇÄ‚ÇÅ_iff, in‚ÇÄ‚ÇÇ_iff, in‚ÇÅ‚ÇÇ_iff],
  exact ‚ü®‚ü®_, hx‚ü©, ‚ü®_, hx‚ü©, _, hx‚ü©,
end

lemma exists_mem_to_triangle {x y : Œ± ‚äï Œ≤ ‚äï Œ≥} (hxy : (graph t).adj x y) :
  ‚àÉ z ‚àà t, x ‚àà to_triangle z ‚àß y ‚àà to_triangle z :=
by cases hxy; exact ‚ü®_, ‚Äπ_‚Ä∫, by simp‚ü©

lemma is_3_clique_iff [no_accidental t] {s : finset (Œ± ‚äï Œ≤ ‚äï Œ≥)} :
  (graph t).is_n_clique 3 s ‚Üî ‚àÉ x, x ‚àà t ‚àß to_triangle x = s :=
begin
  refine ‚ü®Œª h, _, _‚ü©,
  { rw is_3_clique_iff at h,
    obtain ‚ü®x, y, z, hxy, hxz, hyz, rfl‚ü© := h,
    obtain ‚ü®a, b, c, habc, hab, hac, hbc‚ü© := graph_triple hxy hxz hyz,
    refine ‚ü®(a, b, c), _, habc‚ü©,
    obtain ‚ü®c', hc'‚ü© := in‚ÇÄ‚ÇÅ_iff.1 hab,
    obtain ‚ü®b', hb'‚ü© := in‚ÇÄ‚ÇÇ_iff.1 hac,
    obtain ‚ü®a', ha'‚ü© := in‚ÇÅ‚ÇÇ_iff.1 hbc,
    obtain (rfl | rfl | rfl) := no_accidental.wow ha' hb' hc'; assumption },
  { rintro ‚ü®x, hx, rfl‚ü©,
    exact to_triangle_is_3_clique hx }
end

lemma to_triangle_surj_on [no_accidental t] :
  (t : set (Œ± √ó Œ≤ √ó Œ≥)).surj_on to_triangle ((graph t).clique_set 3) :=
Œª s, is_3_clique_iff.1

variables (t)

lemma map_to_triangle_disjoint [explicit_disjoint t] :
  (t.map to_triangle : set (finset (Œ± ‚äï Œ≤ ‚äï Œ≥))).pairwise $
    Œª x y, (x ‚à© y : set (Œ± ‚äï Œ≤ ‚äï Œ≥)).subsingleton :=
begin
  intro,
  simp only [finset.coe_map, set.mem_image, finset.mem_coe, prod.exists, ne.def,
    forall_exists_index, and_imp],
  rintro a b c habc rfl e x y z hxyz rfl h',
  have := ne_of_apply_ne _ h',
  simp only [ne.def, prod.mk.inj_iff, not_and] at this,
  simp only [to_triangle_apply, in‚ÇÄ, in‚ÇÅ, in‚ÇÇ, set.mem_inter_iff, mem_insert, mem_singleton,
    mem_coe, and_imp, sum.forall, or_false, forall_eq, false_or, eq_self_iff_true, implies_true_iff,
    true_and, and_true, set.subsingleton],
  suffices : ¬¨ (a = x ‚àß b = y) ‚àß ¬¨ (a = x ‚àß c = z) ‚àß ¬¨ (b = y ‚àß c = z),
  { tauto },
  refine ‚ü®_, _, _‚ü©,
  { rintro ‚ü®rfl, rfl‚ü©,
    exact this rfl rfl (explicit_disjoint.inj‚ÇÇ habc hxyz) },
  { rintro ‚ü®rfl, rfl‚ü©,
    exact this rfl (explicit_disjoint.inj‚ÇÅ habc hxyz) rfl },
  { rintro ‚ü®rfl, rfl‚ü©,
    exact this (explicit_disjoint.inj‚ÇÄ habc hxyz) rfl rfl }
end

lemma clique_set_eq_image [no_accidental t] : (graph t).clique_set 3 = to_triangle '' t :=
by ext; exact is_3_clique_iff

section fintype
variables [fintype Œ±] [fintype Œ≤] [fintype Œ≥]

lemma clique_finset_eq_image [no_accidental t] : (graph t).clique_finset 3 = t.image to_triangle :=
coe_injective $ by { push_cast, exact clique_set_eq_image _ }

lemma clique_finset_eq_map [no_accidental t] : (graph t).clique_finset 3 = t.map to_triangle :=
by simp [clique_finset_eq_image, map_eq_image]

@[simp] lemma card_triangles [no_accidental t] : ((graph t).clique_finset 3).card = t.card :=
by rw [clique_finset_eq_map, card_map]

lemma far_from_triangle_free [explicit_disjoint t] {Œµ : ùïú}
  (ht : Œµ * ((fintype.card Œ± + fintype.card Œ≤ + fintype.card Œ≥) ^ 2 : ‚Ñï) ‚â§ t.card) :
  (graph t).far_from_triangle_free Œµ :=
far_from_triangle_free_of_disjoint_triangles (t.map to_triangle)
  (map_subset_iff_subset_preimage.2 $ Œª x hx, by simpa using to_triangle_is_3_clique hx)
  (map_to_triangle_disjoint t) $  by simpa [add_assoc] using ht

end fintype
end decidable_eq

variables (t)

lemma locally_linear [explicit_disjoint t] [no_accidental t] : (graph t).locally_linear :=
begin
  classical,
  refine ‚ü®_, Œª x y hxy, _‚ü©,
  { unfold edge_disjoint_triangles,
    convert map_to_triangle_disjoint t,
    rw [clique_set_eq_image, coe_map] },
  { obtain ‚ü®z, hz, hxy‚ü© := exists_mem_to_triangle hxy,
    exact ‚ü®_, to_triangle_is_3_clique hz, hxy‚ü© }
end

end tripartite_from_triangles
end simple_graph
