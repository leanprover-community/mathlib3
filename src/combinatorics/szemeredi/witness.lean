/-
Copyright (c) 2022 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import combinatorics.simple_graph.regularity.uniform
import set_theory.ordinal.basic

/-!
# Witnesses of non uniformity

This file defines witnesses of non-uniformity of a graph and of a partition.

## Main declarations

* `simple_graph.witness`
* `finpartition.witnesses`
-/

namespace finpartition
open finset

variables {Î± ğ•œ : Type*} [linear_ordered_field ğ•œ] [decidable_eq Î±] {s : finset Î±}
  (P : finpartition s) {G : simple_graph Î±} [decidable_rel G.adj]

open_locale classical

lemma non_uniforms_mono {Îµ Îµ' : ğ•œ} (h : Îµ â‰¤ Îµ') : P.non_uniforms G Îµ' âŠ† P.non_uniforms G Îµ :=
monotone_filter_right _ $ Î» uv, mt $ simple_graph.is_uniform.mono h

variables {P}

lemma is_uniform.mono {Îµ Îµ' : ğ•œ} (hP : P.is_uniform G Îµ) (h : Îµ â‰¤ Îµ') : P.is_uniform G Îµ' :=
((nat.cast_le.2 $ card_le_of_subset $ P.non_uniforms_mono h).trans hP).trans $
  mul_le_mul_of_nonneg_left h $ nat.cast_nonneg _

end finpartition

open finset
open_locale classical

variables {Î± ğ•œ : Type*} [linear_ordered_field ğ•œ]

namespace simple_graph
variables (G : simple_graph Î±)

lemma not_witness_prop {G : simple_graph Î±} {Îµ : ğ•œ} {U V : finset Î±} (h : Â¬G.is_uniform Îµ U V) :
  âˆƒ U', U' âŠ† U âˆ§ âˆƒ V', V' âŠ† V âˆ§ â†‘U.card * Îµ â‰¤ U'.card âˆ§ â†‘V.card * Îµ â‰¤ V'.card âˆ§
    Îµ â‰¤ |G.edge_density U' V' - G.edge_density U V| :=
by { rw [is_uniform] at h, push_neg at h, exact h }

/-- Extracts a witness of the non-uniformity of `(U, V)`. Witnesses for `(U, V)` and `(V, U)` don't
necessarily match. Hence the motivation to define `witness_aux`. -/
noncomputable def witness_aux (Îµ : ğ•œ) (U V : finset Î±) : finset Î± Ã— finset Î± :=
if h : Â¬G.is_uniform Îµ U V
  then ((not_witness_prop h).some, (not_witness_prop h).some_spec.2.some)
  else (U, V)

/-- Extracts a witness of the non-uniformity of `(U, V)`. It uses an arbitrary ordering of
`finset Î±` (`well_ordering_rel`) to ensure that the witnesses of `(U, V)` and `(V, U)` are related
(the existentials don't ensure we would take the same from `Â¬G.is_uniform Îµ U V` and
`Â¬G.is_uniform Îµ V U`). -/
noncomputable def witness (Îµ : ğ•œ) (U V : finset Î±) : finset Î± :=
ite (well_ordering_rel U V) (G.witness_aux Îµ U V).1 (G.witness_aux Îµ V U).2

variables (G) {Îµ : ğ•œ} {U V : finset Î±}

lemma left_witness_aux_subset (h : Â¬G.is_uniform Îµ U V) : (G.witness_aux Îµ U V).1 âŠ† U :=
by { rw [witness_aux, dif_pos h], apply (not_witness_prop h).some_spec.1 }

lemma left_witness_aux_card (h : Â¬ G.is_uniform Îµ U V) :
  (U.card : ğ•œ) * Îµ â‰¤ (G.witness_aux Îµ U V).1.card :=
by { rw [witness_aux, dif_pos h], apply (not_witness_prop h).some_spec.2.some_spec.2.1 }

lemma right_witness_aux_subset (h : Â¬ G.is_uniform Îµ U V) : (G.witness_aux Îµ U V).2 âŠ† V :=
by { rw [witness_aux, dif_pos h], apply (not_witness_prop h).some_spec.2.some_spec.1 }

lemma right_witness_aux_card (h : Â¬ G.is_uniform Îµ U V) :
  (V.card : ğ•œ) * Îµ â‰¤ (G.witness_aux Îµ U V).2.card :=
by { rw [witness_aux, dif_pos h], apply (not_witness_prop h).some_spec.2.some_spec.2.2.1 }

lemma witness_aux_pair_spec (h : Â¬ G.is_uniform Îµ U V) :
  Îµ â‰¤ |G.edge_density (G.witness_aux Îµ U V).1 (G.witness_aux Îµ U V).2 - G.edge_density U V| :=
by { rw [witness_aux, dif_pos h], apply (not_witness_prop h).some_spec.2.some_spec.2.2.2 }

lemma witness_subset (h : Â¬ G.is_uniform Îµ U V) : G.witness Îµ U V âŠ† U :=
begin
  dsimp [witness],
  split_ifs,
  { apply G.left_witness_aux_subset h },
  { apply G.right_witness_aux_subset (Î» i, h i.symm) },
end

lemma witness_card (h : Â¬ G.is_uniform Îµ U V) : (U.card : ğ•œ) * Îµ â‰¤ (G.witness Îµ U V).card :=
begin
  dsimp [witness],
  split_ifs,
  { apply G.left_witness_aux_card h },
  { apply G.right_witness_aux_card (Î» i, h i.symm) }
end

lemma witness_pair_spec (hâ‚ : U â‰  V) (hâ‚‚ : Â¬ G.is_uniform Îµ U V) :
  Îµ â‰¤ |G.edge_density (G.witness Îµ U V) (G.witness Îµ V U) - G.edge_density U V| :=
begin
  unfold witness,
  rcases trichotomous_of well_ordering_rel U V with lt | rfl | gt,
  { rw [if_pos lt, if_neg (asymm lt)],
    exact G.witness_aux_pair_spec hâ‚‚ },
  { cases hâ‚ rfl },
  { rw [if_neg (asymm gt), if_pos gt, edge_density_comm, edge_density_comm _ U],
    apply G.witness_aux_pair_spec (Î» i, hâ‚‚ i.symm) }
end

end simple_graph

/-- The witnesses of non uniformity among the parts of a finpartition. -/
noncomputable def finpartition.witnesses {s : finset Î±} (P : finpartition s) (G : simple_graph Î±)
  (Îµ : ğ•œ) (U : finset Î±) :=
(P.parts.filter (Î» V, U â‰  V âˆ§ Â¬G.is_uniform Îµ U V)).image (G.witness Îµ U)
