/-
Copyright (c) 2017 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro

Computational realization of topological spaces (experimental).
-/
import topology.bases data.analysis.filter
open set
open filter (hiding realizer)
open_locale topological_space

/-- A `ctop Î± Ïƒ` is a realization of a topology (basis) on `Î±`,
  represented by a type `Ïƒ` together with operations for the top element and
  the intersection operation. -/
structure ctop (Î± Ïƒ : Type*) :=
(f : Ïƒ â†’ set Î±)
(top : Î± â†’ Ïƒ)
(top_mem : âˆ€ x : Î±, x âˆˆ f (top x))
(inter : Î  a b (x : Î±), x âˆˆ f a âˆ© f b â†’ Ïƒ)
(inter_mem : âˆ€ a b x h, x âˆˆ f (inter a b x h))
(inter_sub : âˆ€ a b x h, f (inter a b x h) âŠ† f a âˆ© f b)

variables {Î± : Type*} {Î² : Type*} {Ïƒ : Type*} {Ï„ : Type*}

namespace ctop
section
variables (F : ctop Î± Ïƒ)

instance : has_coe_to_fun (ctop Î± Ïƒ) := âŸ¨_, ctop.fâŸ©

@[simp] theorem coe_mk (f T hâ‚ I hâ‚‚ hâ‚ƒ a) : (@ctop.mk Î± Ïƒ f T hâ‚ I hâ‚‚ hâ‚ƒ) a = f a := rfl

/-- Map a ctop to an equivalent representation type. -/
def of_equiv (E : Ïƒ â‰ƒ Ï„) : ctop Î± Ïƒ â†’ ctop Î± Ï„
| âŸ¨f, T, hâ‚, I, hâ‚‚, hâ‚ƒâŸ© :=
  { f         := Î» a, f (E.symm a),
    top       := Î» x, E (T x),
    top_mem   := Î» x, by simpa using hâ‚ x,
    inter     := Î» a b x h, E (I (E.symm a) (E.symm b) x h),
    inter_mem := Î» a b x h, by simpa using hâ‚‚ (E.symm a) (E.symm b) x h,
    inter_sub := Î» a b x h, by simpa using hâ‚ƒ (E.symm a) (E.symm b) x h }

@[simp] theorem of_equiv_val (E : Ïƒ â‰ƒ Ï„) (F : ctop Î± Ïƒ) (a : Ï„) :
  F.of_equiv E a = F (E.symm a) := by cases F; refl

end

instance to_topsp (F : ctop Î± Ïƒ) : topological_space Î± :=
topological_space.generate_from (set.range F.f)

theorem to_topsp_is_topological_basis (F : ctop Î± Ïƒ) :
  @topological_space.is_topological_basis _ F.to_topsp (set.range F.f) :=
âŸ¨Î» u âŸ¨a, eâ‚âŸ© v âŸ¨b, eâ‚‚âŸ©, eâ‚ â–¸ eâ‚‚ â–¸
   Î» x h, âŸ¨_, âŸ¨_, rflâŸ©, F.inter_mem a b x h, F.inter_sub a b x hâŸ©,
eq_univ_iff_forall.2 $ Î» x, âŸ¨_, âŸ¨_, rflâŸ©, F.top_mem xâŸ©, rflâŸ©

@[simp] theorem mem_nhds_to_topsp (F : ctop Î± Ïƒ) {s : set Î±} {a : Î±} :
  s âˆˆ @nhds _ F.to_topsp a â†” âˆƒ b, a âˆˆ F b âˆ§ F b âŠ† s :=
(@topological_space.mem_nhds_of_is_topological_basis
  _ F.to_topsp _ _ _ F.to_topsp_is_topological_basis).trans $
âŸ¨Î» âŸ¨_, âŸ¨x, rflâŸ©, hâŸ©, âŸ¨x, hâŸ©, Î» âŸ¨x, hâŸ©, âŸ¨_, âŸ¨x, rflâŸ©, hâŸ©âŸ©

end ctop

/-- A `ctop` realizer for the topological space `T` is a `ctop`
  which generates `T`. -/
structure ctop.realizer (Î±) [T : topological_space Î±] :=
(Ïƒ : Type*)
(F : ctop Î± Ïƒ)
(eq : F.to_topsp = T)
open ctop

protected def ctop.to_realizer (F : ctop Î± Ïƒ) : @ctop.realizer _ F.to_topsp :=
@ctop.realizer.mk _ F.to_topsp Ïƒ F rfl

namespace ctop.realizer

protected theorem is_basis [T : topological_space Î±] (F : realizer Î±) :
  topological_space.is_topological_basis (set.range F.F.f) :=
by have := to_topsp_is_topological_basis F.F; rwa F.eq at this

protected theorem mem_nhds [T : topological_space Î±] (F : realizer Î±) {s : set Î±} {a : Î±} :
  s âˆˆ ğ“ a â†” âˆƒ b, a âˆˆ F.F b âˆ§ F.F b âŠ† s :=
by have := mem_nhds_to_topsp F.F; rwa F.eq at this

theorem is_open_iff [topological_space Î±] (F : realizer Î±) {s : set Î±} :
  is_open s â†” âˆ€ a âˆˆ s, âˆƒ b, a âˆˆ F.F b âˆ§ F.F b âŠ† s :=
is_open_iff_mem_nhds.trans $ ball_congr $ Î» a h, F.mem_nhds

theorem is_closed_iff [topological_space Î±] (F : realizer Î±) {s : set Î±} :
  is_closed s â†” âˆ€ a, (âˆ€ b, a âˆˆ F.F b â†’ âˆƒ z, z âˆˆ F.F b âˆ© s) â†’ a âˆˆ s :=
F.is_open_iff.trans $ forall_congr $ Î» a,
show (a âˆ‰ s â†’ (âˆƒ (b : F.Ïƒ), a âˆˆ F.F b âˆ§ âˆ€ z âˆˆ F.F b, z âˆ‰ s)) â†” _,
by haveI := classical.prop_decidable; rw [not_imp_comm];
   simp [not_exists, not_and, not_forall, and_comm]

theorem mem_interior_iff [topological_space Î±] (F : realizer Î±) {s : set Î±} {a : Î±} :
  a âˆˆ interior s â†” âˆƒ b, a âˆˆ F.F b âˆ§ F.F b âŠ† s :=
mem_interior_iff_mem_nhds.trans F.mem_nhds

protected theorem is_open [topological_space Î±] (F : realizer Î±) (s : F.Ïƒ) : is_open (F.F s) :=
is_open_iff_nhds.2 $ Î» a m, by simpa using F.mem_nhds.2 âŸ¨s, m, subset.refl _âŸ©

theorem ext' [T : topological_space Î±] {Ïƒ : Type*} {F : ctop Î± Ïƒ}
  (H : âˆ€ a s, s âˆˆ ğ“ a â†” âˆƒ b, a âˆˆ F b âˆ§ F b âŠ† s) :
  F.to_topsp = T :=
topological_space_eq $ funext $ Î» s, begin
  have : âˆ€ T s, @topological_space.is_open _ T s â†” _ := @is_open_iff_mem_nhds Î±,
  rw [this, this],
  apply congr_arg (Î» f : Î± â†’ filter Î±, âˆ€ a âˆˆ s, s âˆˆ f a),
  funext a, apply filter_eq, apply set.ext, intro x,
  rw [mem_nhds_to_topsp, H]
end

theorem ext [T : topological_space Î±] {Ïƒ : Type*} {F : ctop Î± Ïƒ}
  (Hâ‚ : âˆ€ a, is_open (F a))
  (Hâ‚‚ : âˆ€ a s, s âˆˆ ğ“ a â†’ âˆƒ b, a âˆˆ F b âˆ§ F b âŠ† s) :
  F.to_topsp = T :=
ext' $ Î» a s, âŸ¨Hâ‚‚ a s, Î» âŸ¨b, hâ‚, hâ‚‚âŸ©, mem_nhds_sets_iff.2 âŸ¨_, hâ‚‚, Hâ‚ _, hâ‚âŸ©âŸ©

variable [topological_space Î±]

protected def id : realizer Î± := âŸ¨{x:set Î± // is_open x},
{ f            := subtype.val,
  top          := Î» _, âŸ¨univ, is_open_univâŸ©,
  top_mem      := mem_univ,
  inter        := Î» âŸ¨x, hâ‚âŸ© âŸ¨y, hâ‚‚âŸ© a hâ‚ƒ, âŸ¨_, is_open_inter hâ‚ hâ‚‚âŸ©,
  inter_mem    := Î» âŸ¨x, hâ‚âŸ© âŸ¨y, hâ‚‚âŸ© a, id,
  inter_sub    := Î» âŸ¨x, hâ‚âŸ© âŸ¨y, hâ‚‚âŸ© a hâ‚ƒ, subset.refl _ },
ext subtype.property $ Î» x s h,
  let âŸ¨t, h, o, mâŸ© := mem_nhds_sets_iff.1 h in âŸ¨âŸ¨t, oâŸ©, m, hâŸ©âŸ©

def of_equiv (F : realizer Î±) (E : F.Ïƒ â‰ƒ Ï„) : realizer Î± :=
âŸ¨Ï„, F.F.of_equiv E, ext' (Î» a s, F.mem_nhds.trans $
 âŸ¨Î» âŸ¨s, hâŸ©, âŸ¨E s, by simpa using hâŸ©, Î» âŸ¨t, hâŸ©, âŸ¨E.symm t, by simpa using hâŸ©âŸ©)âŸ©

@[simp] theorem of_equiv_Ïƒ (F : realizer Î±) (E : F.Ïƒ â‰ƒ Ï„) : (F.of_equiv E).Ïƒ = Ï„ := rfl
@[simp] theorem of_equiv_F (F : realizer Î±) (E : F.Ïƒ â‰ƒ Ï„) (s : Ï„) :
  (F.of_equiv E).F s = F.F (E.symm s) := by delta of_equiv; simp

protected def nhds (F : realizer Î±) (a : Î±) : (ğ“ a).realizer :=
âŸ¨{s : F.Ïƒ // a âˆˆ F.F s},
{ f            := Î» s, F.F s.1,
  pt           := âŸ¨_, F.F.top_mem aâŸ©,
  inf          := Î» âŸ¨x, hâ‚âŸ© âŸ¨y, hâ‚‚âŸ©, âŸ¨_, F.F.inter_mem x y a âŸ¨hâ‚, hâ‚‚âŸ©âŸ©,
  inf_le_left  := Î» âŸ¨x, hâ‚âŸ© âŸ¨y, hâ‚‚âŸ© z h, (F.F.inter_sub x y a âŸ¨hâ‚, hâ‚‚âŸ© h).1,
  inf_le_right := Î» âŸ¨x, hâ‚âŸ© âŸ¨y, hâ‚‚âŸ© z h, (F.F.inter_sub x y a âŸ¨hâ‚, hâ‚‚âŸ© h).2 },
filter_eq $ set.ext $ Î» x,
âŸ¨Î» âŸ¨âŸ¨s, asâŸ©, hâŸ©, mem_nhds_sets_iff.2 âŸ¨_, h, F.is_open _, asâŸ©,
 Î» h, let âŸ¨s, h, asâŸ© := F.mem_nhds.1 h in âŸ¨âŸ¨s, hâŸ©, asâŸ©âŸ©âŸ©

@[simp] theorem nhds_Ïƒ (m : Î± â†’ Î²) (F : realizer Î±) (a : Î±) :
  (F.nhds a).Ïƒ = {s : F.Ïƒ // a âˆˆ F.F s} := rfl
@[simp] theorem nhds_F (m : Î± â†’ Î²) (F : realizer Î±) (a : Î±) (s) :
  (F.nhds a).F s = F.F s.1 := rfl

theorem tendsto_nhds_iff {m : Î² â†’ Î±} {f : filter Î²} (F : f.realizer) (R : realizer Î±) {a : Î±} :
  tendsto m f (ğ“ a) â†” âˆ€ t, a âˆˆ R.F t â†’ âˆƒ s, âˆ€ x âˆˆ F.F s, m x âˆˆ R.F t :=
(F.tendsto_iff _ (R.nhds a)).trans subtype.forall

end ctop.realizer

structure locally_finite.realizer [topological_space Î±] (F : realizer Î±) (f : Î² â†’ set Î±) :=
(bas : âˆ€ a, {s // a âˆˆ F.F s})
(sets : âˆ€ x:Î±, fintype {i | f i âˆ© F.F (bas x) â‰  âˆ…})

theorem locally_finite.realizer.to_locally_finite [topological_space Î±]
  {F : realizer Î±} {f : Î² â†’ set Î±} (R : locally_finite.realizer F f) :
  locally_finite f :=
Î» a, âŸ¨_, F.mem_nhds.2
  âŸ¨(R.bas a).1, (R.bas a).2, subset.refl _âŸ©, âŸ¨R.sets aâŸ©âŸ©

theorem locally_finite_iff_exists_realizer [topological_space Î±]
  (F : realizer Î±) {f : Î² â†’ set Î±} : locally_finite f â†” nonempty (locally_finite.realizer F f) :=
âŸ¨Î» h, let âŸ¨g, hâ‚âŸ© := classical.axiom_of_choice h,
    âŸ¨gâ‚‚, hâ‚‚âŸ© := classical.axiom_of_choice (Î» x,
       show âˆƒ (b : F.Ïƒ), x âˆˆ (F.F) b âˆ§ (F.F) b âŠ† g x, from
       let âŸ¨h, h'âŸ© := hâ‚ x in F.mem_nhds.1 h) in
  âŸ¨âŸ¨Î» x, âŸ¨gâ‚‚ x, (hâ‚‚ x).1âŸ©, Î» x, finite.fintype $
    let âŸ¨h, h'âŸ© := hâ‚ x in finite_subset h' $ Î» i,
    subset_ne_empty (inter_subset_inter_right _ (hâ‚‚ x).2)âŸ©âŸ©,
 Î» âŸ¨RâŸ©, R.to_locally_finiteâŸ©

def compact.realizer [topological_space Î±] (R : realizer Î±) (s : set Î±) :=
âˆ€ {f : filter Î±} (F : f.realizer) (x : F.Ïƒ), f â‰  âŠ¥ â†’
  F.F x âŠ† s â†’ {a // aâˆˆs âˆ§ ğ“ a âŠ“ f â‰  âŠ¥}
