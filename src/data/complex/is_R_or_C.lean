/-
Copyright (c) 2020 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import data.real.sqrt
import field_theory.tower
import analysis.normed_space.finite_dimension

/-!
# `is_R_or_C`: a typeclass for ‚Ñù or ‚ÑÇ

This file defines the typeclass `is_R_or_C` intended to have only two instances:
‚Ñù and ‚ÑÇ. It is meant for definitions and theorems which hold for both the real and the complex case,
and in particular when the real case follows directly from the complex case by setting `re` to `id`,
`im` to zero and so on. Its API follows closely that of ‚ÑÇ.

Possible applications include defining inner products and Hilbert spaces for both the real and
complex case. One would produce the definitions and proof for an arbitrary field of this
typeclass, which basically amounts to doing the complex case, and the two cases then fall out
immediately from the two instances of the class.

The instance for `‚Ñù` is registered in this file.
The instance for `‚ÑÇ` is declared in `analysis.complex.basic`.

## Implementation notes

The coercion from reals into an `is_R_or_C` field is done by registering `algebra_map ‚Ñù K` as
a `has_coe_t`. For this to work, we must proceed carefully to avoid problems involving circular
coercions in the case `K=‚Ñù`; in particular, we cannot use the plain `has_coe` and must set
priorities carefully. This problem was already solved for `‚Ñï`, and we copy the solution detailed
in `data/nat/cast`. See also Note [coercion into rings] for more details.

In addition, several lemmas need to be set at priority 900 to make sure that they do not override
their counterparts in `complex.lean` (which causes linter errors).
-/

open_locale big_operators

section

local notation `ùìö` := algebra_map ‚Ñù _
open_locale complex_conjugate

/--
This typeclass captures properties shared by ‚Ñù and ‚ÑÇ, with an API that closely matches that of ‚ÑÇ.
-/
class is_R_or_C (K : Type*)
  extends nondiscrete_normed_field K, star_ring K, normed_algebra ‚Ñù K, complete_space K :=
(re : K ‚Üí+ ‚Ñù)
(im : K ‚Üí+ ‚Ñù)
(I : K)                 -- Meant to be set to 0 for K=‚Ñù
(I_re_ax : re I = 0)
(I_mul_I_ax : I = 0 ‚à® I * I = -1)
(re_add_im_ax : ‚àÄ (z : K), ùìö (re z) + ùìö (im z) * I = z)
(of_real_re_ax : ‚àÄ r : ‚Ñù, re (ùìö r) = r)
(of_real_im_ax : ‚àÄ r : ‚Ñù, im (ùìö r) = 0)
(mul_re_ax : ‚àÄ z w : K, re (z * w) = re z * re w - im z * im w)
(mul_im_ax : ‚àÄ z w : K, im (z * w) = re z * im w + im z * re w)
(conj_re_ax : ‚àÄ z : K, re (conj z) = re z)
(conj_im_ax : ‚àÄ z : K, im (conj z) = -(im z))
(conj_I_ax : conj I = -I)
(norm_sq_eq_def_ax : ‚àÄ (z : K), ‚à•z‚à•^2 = (re z) * (re z) + (im z) * (im z))
(mul_im_I_ax : ‚àÄ (z : K), (im z) * im I = im z)
(inv_def_ax : ‚àÄ (z : K), z‚Åª¬π = conj z * ùìö ((‚à•z‚à•^2)‚Åª¬π))
(div_I_ax : ‚àÄ (z : K), z / I = -(z * I))

end

namespace is_R_or_C
variables {K : Type*} [is_R_or_C K]

open_locale complex_conjugate

/- The priority must be set at 900 to ensure that coercions are tried in the right order.
See Note [coercion into rings], or `data/nat/cast.lean` for more details. -/
@[priority 900] noncomputable instance algebra_map_coe : has_coe_t ‚Ñù K := ‚ü®algebra_map ‚Ñù K‚ü©

lemma of_real_alg (x : ‚Ñù) : (x : K) = x ‚Ä¢ (1 : K) :=
algebra.algebra_map_eq_smul_one x

lemma algebra_map_eq_of_real : ‚áë(algebra_map ‚Ñù K) = coe := rfl

@[simp] lemma re_add_im (z : K) : ((re z) : K) + (im z) * I = z := is_R_or_C.re_add_im_ax z
@[simp, norm_cast] lemma of_real_re : ‚àÄ r : ‚Ñù, re (r : K) = r := is_R_or_C.of_real_re_ax
@[simp, norm_cast] lemma of_real_im : ‚àÄ r : ‚Ñù, im (r : K) = 0 := is_R_or_C.of_real_im_ax
@[simp] lemma mul_re : ‚àÄ z w : K, re (z * w) = re z * re w - im z * im w :=
is_R_or_C.mul_re_ax
@[simp] lemma mul_im : ‚àÄ z w : K, im (z * w) = re z * im w + im z * re w :=
is_R_or_C.mul_im_ax

theorem inv_def (z : K) : z‚Åª¬π = conj z * ((‚à•z‚à•^2)‚Åª¬π:‚Ñù) :=
is_R_or_C.inv_def_ax z

theorem ext_iff : ‚àÄ {z w : K}, z = w ‚Üî re z = re w ‚àß im z = im w :=
Œª z w, { mp := by { rintro rfl, cc },
         mpr := by { rintro ‚ü®h‚ÇÅ,h‚ÇÇ‚ü©, rw [‚Üêre_add_im z, ‚Üêre_add_im w, h‚ÇÅ, h‚ÇÇ] } }

theorem ext : ‚àÄ {z w : K}, re z = re w ‚Üí im z = im w ‚Üí z = w :=
by { simp_rw ext_iff, cc }


@[simp, norm_cast, priority 900] lemma of_real_zero : ((0 : ‚Ñù) : K) = 0 :=
by rw [of_real_alg, zero_smul]

@[simp] lemma zero_re' : re (0 : K) = (0 : ‚Ñù) := re.map_zero

@[simp, norm_cast, priority 900] lemma of_real_one : ((1 : ‚Ñù) : K) = 1 :=
by rw [of_real_alg, one_smul]
@[simp] lemma one_re : re (1 : K) = 1 := by rw [‚Üêof_real_one, of_real_re]
@[simp] lemma one_im : im (1 : K) = 0 := by rw [‚Üêof_real_one, of_real_im]

@[simp, norm_cast, priority 900] theorem of_real_inj {z w : ‚Ñù} : (z : K) = (w : K) ‚Üî z = w :=
{ mp := Œª h, by { convert congr_arg re h; simp only [of_real_re] },
  mpr := Œª h, by rw h }

@[simp] lemma bit0_re (z : K) : re (bit0 z) = bit0 (re z) := by simp [bit0]
@[simp] lemma bit1_re (z : K) : re (bit1 z) = bit1 (re z) :=
by simp only [bit1, add_monoid_hom.map_add, bit0_re, add_right_inj, one_re]
@[simp] lemma bit0_im (z : K) : im (bit0 z) = bit0 (im z) := by simp [bit0]
@[simp] lemma bit1_im (z : K) : im (bit1 z) = bit0 (im z) :=
by simp only [bit1, add_right_eq_self, add_monoid_hom.map_add, bit0_im, one_im]

@[simp, priority 900] theorem of_real_eq_zero {z : ‚Ñù} : (z : K) = 0 ‚Üî z = 0 :=
by rw [‚Üêof_real_zero]; exact of_real_inj

@[simp, norm_cast, priority 900] lemma of_real_add ‚¶Ér s : ‚Ñù‚¶Ñ : ((r + s : ‚Ñù) : K) = r + s :=
by { apply (@is_R_or_C.ext_iff K _ ((r + s : ‚Ñù) : K) (r + s)).mpr, simp }

@[simp, norm_cast, priority 900] lemma of_real_bit0 (r : ‚Ñù) : ((bit0 r : ‚Ñù) : K) = bit0 (r : K) :=
ext_iff.2 $ by simp [bit0]

@[simp, norm_cast, priority 900] lemma of_real_bit1 (r : ‚Ñù) : ((bit1 r : ‚Ñù) : K) = bit1 (r : K) :=
ext_iff.2 $ by simp [bit1]

/- Note: This can be proven by `norm_num` once K is proven to be of characteristic zero below. -/
lemma two_ne_zero : (2 : K) ‚â† 0 :=
begin
  intro h, rw [(show (2 : K) = ((2 : ‚Ñù) : K), by norm_num), ‚Üêof_real_zero, of_real_inj] at h,
  linarith,
end

@[simp, norm_cast, priority 900] lemma of_real_neg (r : ‚Ñù) : ((-r : ‚Ñù) : K) = -r :=
ext_iff.2 $ by simp
@[simp, norm_cast, priority 900] lemma of_real_mul (r s : ‚Ñù) : ((r * s : ‚Ñù) : K) = r * s :=
ext_iff.2 $ by simp
@[simp, norm_cast] lemma of_real_smul (r x : ‚Ñù) : r ‚Ä¢ (x : K) = (r : K) * (x : K) :=
by { simp_rw [‚Üê smul_eq_mul, of_real_alg r], simp, }

lemma of_real_mul_re (r : ‚Ñù) (z : K) : re (‚Üër * z) = r * re z :=
by simp only [mul_re, of_real_im, zero_mul, of_real_re, sub_zero]
lemma of_real_mul_im (r : ‚Ñù) (z : K) : im (‚Üër * z) = r * (im z) :=
by simp only [add_zero, of_real_im, zero_mul, of_real_re, mul_im]

lemma smul_re : ‚àÄ (r : ‚Ñù) (z : K), re (r ‚Ä¢ z) = r * (re z) :=
Œª r z, by { rw algebra.smul_def, apply of_real_mul_re }
lemma smul_im : ‚àÄ (r : ‚Ñù) (z : K), im (r ‚Ä¢ z) = r * (im z) :=
Œª r z, by { rw algebra.smul_def, apply of_real_mul_im }

/-! ### The imaginary unit, `I` -/

/-- The imaginary unit. -/
@[simp] lemma I_re : re (I : K) = 0 := I_re_ax
@[simp] lemma I_im (z : K) : im z * im (I : K) = im z := mul_im_I_ax z
@[simp] lemma I_im' (z : K) : im (I : K) * im z = im z :=
by rw [mul_comm, I_im _]

lemma I_mul_re (z : K) : re (I * z) = - im z :=
by simp only [I_re, zero_sub, I_im', zero_mul, mul_re]

lemma I_mul_I : (I : K) = 0 ‚à® (I : K) * I = -1 := I_mul_I_ax

@[simp] lemma conj_re (z : K) : re (conj z) = re z := is_R_or_C.conj_re_ax z
@[simp] lemma conj_im (z : K) : im (conj z) = -(im z) := is_R_or_C.conj_im_ax z
@[simp] lemma conj_I : conj (I : K) = -I := is_R_or_C.conj_I_ax
@[simp] lemma conj_of_real (r : ‚Ñù) : conj (r : K) = (r : K) :=
by { rw ext_iff, simp only [of_real_im, conj_im, eq_self_iff_true, conj_re, and_self, neg_zero] }


@[simp] lemma conj_bit0 (z : K) : conj (bit0 z) = bit0 (conj z) := by simp [bit0, ext_iff]
@[simp] lemma conj_bit1 (z : K) : conj (bit1 z) = bit1 (conj z) := by simp [bit0, ext_iff]

@[simp] lemma conj_neg_I : conj (-I) = (I : K) := by simp [ext_iff]

lemma conj_eq_re_sub_im (z : K) : conj z = re z - (im z) * I := by { rw ext_iff, simp, }

lemma conj_smul (r : ‚Ñù) (z : K) : conj (r ‚Ä¢ z) = r ‚Ä¢ conj z :=
begin
  simp_rw conj_eq_re_sub_im,
  simp only [smul_re, smul_im, of_real_mul],
  rw smul_sub,
  simp_rw of_real_alg,
  simp,
end

lemma eq_conj_iff_real {z : K} : conj z = z ‚Üî ‚àÉ r : ‚Ñù, z = (r : K) :=
begin
  split,
  { intro h,
    suffices : im z = 0,
    { use (re z),
      rw ‚Üê add_zero (coe _),
      convert (re_add_im z).symm, simp [this] },
    contrapose! h,
    rw ‚Üê re_add_im z,
    simp only [conj_of_real, ring_equiv.map_add, ring_equiv.map_mul, conj_I_ax],
    rw [add_left_cancel_iff, ext_iff],
    simpa [neg_eq_iff_add_eq_zero, add_self_eq_zero] },
  { rintros ‚ü®r, rfl‚ü©, apply conj_of_real }
end

variables (K)
/-- Conjugation as a ring equivalence. This is used to convert the inner product into a
sesquilinear product. -/
abbreviation conj_to_ring_equiv : K ‚âÉ+* K·µí·µñ := star_ring_equiv

variables {K}

lemma eq_conj_iff_re {z : K} : conj z = z ‚Üî ((re z) : K) = z :=
eq_conj_iff_real.trans ‚ü®by rintro ‚ü®r, rfl‚ü©; simp, Œª h, ‚ü®_, h.symm‚ü©‚ü©

/-- The norm squared function. -/
def norm_sq : monoid_with_zero_hom K ‚Ñù :=
{ to_fun := Œª z, re z * re z + im z * im z,
  map_zero' := by simp,
  map_one' := by simp,
  map_mul' := Œª z w, by { simp, ring } }

lemma norm_sq_eq_def {z : K} : ‚à•z‚à•^2 = (re z) * (re z) + (im z) * (im z) := norm_sq_eq_def_ax z
lemma norm_sq_eq_def' (z : K) : norm_sq z = ‚à•z‚à•^2 := by { rw norm_sq_eq_def, refl }

@[simp] lemma norm_sq_of_real (r : ‚Ñù) : ‚à•(r : K)‚à•^2 = r * r :=
by simp [norm_sq_eq_def]

lemma norm_sq_zero : norm_sq (0 : K) = 0 := norm_sq.map_zero
lemma norm_sq_one : norm_sq (1 : K) = 1 := norm_sq.map_one

lemma norm_sq_nonneg (z : K) : 0 ‚â§ norm_sq z :=
add_nonneg (mul_self_nonneg _) (mul_self_nonneg _)

@[simp] lemma norm_sq_eq_zero {z : K} : norm_sq z = 0 ‚Üî z = 0 :=
by { rw [norm_sq_eq_def'], simp [sq] }

@[simp] lemma norm_sq_pos {z : K} : 0 < norm_sq z ‚Üî z ‚â† 0 :=
by rw [lt_iff_le_and_ne, ne, eq_comm]; simp [norm_sq_nonneg]

@[simp] lemma norm_sq_neg (z : K) : norm_sq (-z) = norm_sq z :=
by simp [norm_sq_eq_def']

@[simp] lemma norm_sq_conj (z : K) : norm_sq (conj z) = norm_sq z := by simp [norm_sq]

@[simp] lemma norm_sq_mul (z w : K) : norm_sq (z * w) = norm_sq z * norm_sq w :=
norm_sq.map_mul z w

lemma norm_sq_add (z w : K) :
  norm_sq (z + w) = norm_sq z + norm_sq w + 2 * (re (z * conj w)) :=
by simp [norm_sq, sq]; ring

lemma re_sq_le_norm_sq (z : K) : re z * re z ‚â§ norm_sq z :=
le_add_of_nonneg_right (mul_self_nonneg _)

lemma im_sq_le_norm_sq (z : K) : im z * im z ‚â§ norm_sq z :=
le_add_of_nonneg_left (mul_self_nonneg _)

theorem mul_conj (z : K) : z * conj z = ((norm_sq z) : K) :=
by simp [ext_iff, norm_sq, mul_comm, sub_eq_neg_add, add_comm]

theorem add_conj (z : K) : z + conj z = 2 * (re z) :=
by simp [ext_iff, two_mul]

/-- The pseudo-coercion `of_real` as a `ring_hom`. -/
noncomputable def of_real_hom : ‚Ñù ‚Üí+* K := algebra_map ‚Ñù K

/-- The coercion from reals as a `ring_hom`. -/
noncomputable def coe_hom : ‚Ñù ‚Üí+* K := ‚ü®coe, of_real_one, of_real_mul, of_real_zero, of_real_add‚ü©

@[simp, norm_cast, priority 900] lemma of_real_sub (r s : ‚Ñù) : ((r - s : ‚Ñù) : K) = r - s :=
ext_iff.2 $ by simp
@[simp, norm_cast, priority 900] lemma of_real_pow (r : ‚Ñù) (n : ‚Ñï) : ((r ^ n : ‚Ñù) : K) = r ^ n :=
by induction n; simp [*, of_real_mul, pow_succ]

theorem sub_conj (z : K) : z - conj z = (2 * im z) * I :=
by simp [ext_iff, two_mul, sub_eq_add_neg, add_mul, mul_im_I_ax]

lemma norm_sq_sub (z w : K) : norm_sq (z - w) =
  norm_sq z + norm_sq w - 2 * re (z * conj w) :=
by simp [-mul_re, norm_sq_add, add_comm, add_left_comm, sub_eq_add_neg]

lemma sqrt_norm_sq_eq_norm {z : K} : real.sqrt (norm_sq z) = ‚à•z‚à• :=
begin
  have h‚ÇÇ : ‚à•z‚à• = real.sqrt (‚à•z‚à•^2) := (real.sqrt_sq (norm_nonneg z)).symm,
  rw [h‚ÇÇ],
  exact congr_arg real.sqrt (norm_sq_eq_def' z)
end

/-! ### Inversion -/

@[simp] lemma inv_re (z : K) : re (z‚Åª¬π) = re z / norm_sq z :=
by simp [inv_def, norm_sq_eq_def, norm_sq, division_def]
@[simp] lemma inv_im (z : K) : im (z‚Åª¬π) = im (-z) / norm_sq z :=
by simp [inv_def, norm_sq_eq_def, norm_sq, division_def]

@[simp, norm_cast, priority 900] lemma of_real_inv (r : ‚Ñù) : ((r‚Åª¬π : ‚Ñù) : K) = r‚Åª¬π :=
begin
  rw ext_iff, by_cases r = 0, { simp [h] },
  { simp; field_simp [h, norm_sq] },
end

protected lemma inv_zero : (0‚Åª¬π : K) = 0 :=
by rw [‚Üê of_real_zero, ‚Üê of_real_inv, inv_zero]

protected theorem mul_inv_cancel {z : K} (h : z ‚â† 0) : z * z‚Åª¬π = 1 :=
by rw [inv_def, ‚Üêmul_assoc, mul_conj, ‚Üêof_real_mul, ‚Üênorm_sq_eq_def',
      mul_inv_cancel (mt norm_sq_eq_zero.1 h), of_real_one]

lemma div_re (z w : K) : re (z / w) = re z * re w / norm_sq w + im z * im w / norm_sq w :=
by simp [div_eq_mul_inv, mul_assoc, sub_eq_add_neg]
lemma div_im (z w : K) : im (z / w) = im z * re w / norm_sq w - re z * im w / norm_sq w :=
by simp [div_eq_mul_inv, mul_assoc, sub_eq_add_neg, add_comm]

@[simp, norm_cast, priority 900] lemma of_real_div (r s : ‚Ñù) : ((r / s : ‚Ñù) : K) = r / s :=
(@is_R_or_C.coe_hom K _).map_div r s

lemma div_re_of_real {z : K} {r : ‚Ñù} : re (z / r) = re z / r :=
begin
  by_cases h : r = 0,
  { simp [h, of_real_zero] },
  { change r ‚â† 0 at h,
    rw [div_eq_mul_inv, ‚Üêof_real_inv, div_eq_mul_inv],
    simp [norm_sq, div_mul_eq_div_mul_one_div, div_self h] }
end

@[simp, norm_cast, priority 900] lemma of_real_fpow (r : ‚Ñù) (n : ‚Ñ§) : ((r ^ n : ‚Ñù) : K) = r ^ n :=
(@is_R_or_C.coe_hom K _).map_fpow r n

lemma I_mul_I_of_nonzero : (I : K) ‚â† 0 ‚Üí (I : K) * I = -1 :=
by { have := I_mul_I_ax, tauto }

@[simp] lemma div_I (z : K) : z / I = -(z * I) :=
begin
  by_cases h : (I : K) = 0,
  { simp [h] },
  { field_simp [mul_assoc, I_mul_I_of_nonzero h] }
end

@[simp] lemma inv_I : (I : K)‚Åª¬π = -I :=
by { by_cases h : (I : K) = 0; field_simp [h] }

@[simp] lemma norm_sq_inv (z : K) : norm_sq z‚Åª¬π = (norm_sq z)‚Åª¬π :=
(@norm_sq K _).map_inv z

@[simp] lemma norm_sq_div (z w : K) : norm_sq (z / w) = norm_sq z / norm_sq w :=
(@norm_sq K _).map_div z w

lemma norm_conj {z : K} : ‚à•conj z‚à• = ‚à•z‚à• :=
by simp only [‚Üêsqrt_norm_sq_eq_norm, norm_sq_conj]

/-! ### Cast lemmas -/

@[simp, norm_cast, priority 900] theorem of_real_nat_cast (n : ‚Ñï) : ((n : ‚Ñù) : K) = n :=
of_real_hom.map_nat_cast n

@[simp, norm_cast] lemma nat_cast_re (n : ‚Ñï) : re (n : K) = n :=
by rw [‚Üê of_real_nat_cast, of_real_re]

@[simp, norm_cast] lemma nat_cast_im (n : ‚Ñï) : im (n : K) = 0 :=
by rw [‚Üê of_real_nat_cast, of_real_im]

@[simp, norm_cast, priority 900] theorem of_real_int_cast (n : ‚Ñ§) : ((n : ‚Ñù) : K) = n :=
of_real_hom.map_int_cast n

@[simp, norm_cast] lemma int_cast_re (n : ‚Ñ§) : re (n : K) = n :=
by rw [‚Üê of_real_int_cast, of_real_re]

@[simp, norm_cast] lemma int_cast_im (n : ‚Ñ§) : im (n : K) = 0 :=
by rw [‚Üê of_real_int_cast, of_real_im]

@[simp, norm_cast, priority 900] theorem of_real_rat_cast (n : ‚Ñö) : ((n : ‚Ñù) : K) = n :=
(@is_R_or_C.of_real_hom K _).map_rat_cast n

@[simp, norm_cast] lemma rat_cast_re (q : ‚Ñö) : re (q : K) = q :=
by rw [‚Üê of_real_rat_cast, of_real_re]

@[simp, norm_cast] lemma rat_cast_im (q : ‚Ñö) : im (q : K) = 0 :=
by rw [‚Üê of_real_rat_cast, of_real_im]

/-! ### Characteristic zero -/

-- TODO: I think this can be instance, because it is a `Prop`

/--
‚Ñù and ‚ÑÇ are both of characteristic zero.

Note: This is not registered as an instance to avoid having multiple instances on ‚Ñù and ‚ÑÇ.
-/
lemma char_zero_R_or_C : char_zero K :=
char_zero_of_inj_zero $ Œª n h,
by rwa [‚Üê of_real_nat_cast, of_real_eq_zero, nat.cast_eq_zero] at h

theorem re_eq_add_conj (z : K) : ‚Üë(re z) = (z + conj z) / 2 :=
begin
  haveI : char_zero K := char_zero_R_or_C,
  rw [add_conj, mul_div_cancel_left ((re z):K) two_ne_zero'],
end

theorem im_eq_conj_sub (z : K) : ‚Üë(im z) = I * (conj z - z) / 2 :=
begin
  rw [‚Üê neg_inj, ‚Üê of_real_neg, ‚Üê I_mul_re, re_eq_add_conj],
  simp [mul_add, sub_eq_add_neg, neg_div']
end

/-! ### Absolute value -/

/-- The complex absolute value function, defined as the square root of the norm squared. -/
@[pp_nodot] noncomputable def abs (z : K) : ‚Ñù := (norm_sq z).sqrt

local notation `abs'` := has_abs.abs
local notation `absK` := @abs K _

@[simp, norm_cast] lemma abs_of_real (r : ‚Ñù) : absK r = abs' r :=
by simp [abs, norm_sq, norm_sq_of_real, real.sqrt_mul_self_eq_abs]

lemma norm_eq_abs (z : K) : ‚à•z‚à• = absK z := by simp [abs, norm_sq_eq_def']

lemma abs_of_nonneg {r : ‚Ñù} (h : 0 ‚â§ r) : absK r = r :=
(abs_of_real _).trans (abs_of_nonneg h)

lemma abs_of_nat (n : ‚Ñï) : absK n = n :=
by { rw [‚Üê of_real_nat_cast], exact abs_of_nonneg (nat.cast_nonneg n) }

lemma mul_self_abs (z : K) : abs z * abs z = norm_sq z :=
real.mul_self_sqrt (norm_sq_nonneg _)

@[simp] lemma abs_zero : absK 0 = 0 := by simp [abs]
@[simp] lemma abs_one : absK 1 = 1 := by simp [abs]

@[simp] lemma abs_two : absK 2 = 2 :=
calc absK 2 = absK (2 : ‚Ñù) : by rw [of_real_bit0, of_real_one]
... = (2 : ‚Ñù) : abs_of_nonneg (by norm_num)

lemma abs_nonneg (z : K) : 0 ‚â§ absK z :=
real.sqrt_nonneg _

@[simp] lemma abs_eq_zero {z : K} : absK z = 0 ‚Üî z = 0 :=
(real.sqrt_eq_zero $ norm_sq_nonneg _).trans norm_sq_eq_zero

lemma abs_ne_zero {z : K} : abs z ‚â† 0 ‚Üî z ‚â† 0 :=
not_congr abs_eq_zero

@[simp] lemma abs_conj (z : K) : abs (conj z) = abs z :=
by simp [abs]

@[simp] lemma abs_mul (z w : K) : abs (z * w) = abs z * abs w :=
by rw [abs, norm_sq_mul, real.sqrt_mul (norm_sq_nonneg _)]; refl

lemma abs_re_le_abs (z : K) : abs' (re z) ‚â§ abs z :=
by rw [mul_self_le_mul_self_iff (_root_.abs_nonneg (re z)) (abs_nonneg _),
       abs_mul_abs_self, mul_self_abs];
   apply re_sq_le_norm_sq

lemma abs_im_le_abs (z : K) : abs' (im z) ‚â§ abs z :=
by rw [mul_self_le_mul_self_iff (_root_.abs_nonneg (im z)) (abs_nonneg _),
       abs_mul_abs_self, mul_self_abs];
   apply im_sq_le_norm_sq

lemma norm_re_le_norm (z : K) : ‚à•re z‚à• ‚â§ ‚à•z‚à• :=
by { rw [is_R_or_C.norm_eq_abs, real.norm_eq_abs], exact is_R_or_C.abs_re_le_abs _, }

lemma norm_im_le_norm (z : K) : ‚à•im z‚à• ‚â§ ‚à•z‚à• :=
by { rw [is_R_or_C.norm_eq_abs, real.norm_eq_abs], exact is_R_or_C.abs_im_le_abs _, }

lemma re_le_abs (z : K) : re z ‚â§ abs z :=
(abs_le.1 (abs_re_le_abs _)).2

lemma im_le_abs (z : K) : im z ‚â§ abs z :=
(abs_le.1 (abs_im_le_abs _)).2

lemma im_eq_zero_of_le {a : K} (h : abs a ‚â§ re a) : im a = 0 :=
begin
  rw ‚Üê zero_eq_mul_self,
  have : re a * re a = re a * re a + im a * im a,
  { convert is_R_or_C.mul_self_abs a;
    linarith [re_le_abs a] },
  linarith
end

lemma re_eq_self_of_le {a : K} (h : abs a ‚â§ re a) : (re a : K) = a :=
by { rw ‚Üê re_add_im a, simp [im_eq_zero_of_le h] }

lemma abs_add (z w : K) : abs (z + w) ‚â§ abs z + abs w :=
(mul_self_le_mul_self_iff (abs_nonneg _)
  (add_nonneg (abs_nonneg _) (abs_nonneg _))).2 $
begin
  rw [mul_self_abs, add_mul_self_eq, mul_self_abs, mul_self_abs,
      add_right_comm, norm_sq_add, add_le_add_iff_left,
      mul_assoc, mul_le_mul_left (@zero_lt_two ‚Ñù _ _)],
  simpa [-mul_re] using re_le_abs (z * conj w)
end

instance : is_absolute_value absK :=
{ abv_nonneg  := abs_nonneg,
  abv_eq_zero := Œª _, abs_eq_zero,
  abv_add     := abs_add,
  abv_mul     := abs_mul }
open is_absolute_value

@[simp] lemma abs_abs (z : K) : abs' (abs z) = abs z :=
_root_.abs_of_nonneg (abs_nonneg _)

@[simp] lemma abs_pos {z : K} : 0 < abs z ‚Üî z ‚â† 0 := abv_pos abs
@[simp] lemma abs_neg : ‚àÄ z : K, abs (-z) = abs z := abv_neg abs
lemma abs_sub : ‚àÄ z w : K, abs (z - w) = abs (w - z) := abv_sub abs
lemma abs_sub_le : ‚àÄ a b c : K, abs (a - c) ‚â§ abs (a - b) + abs (b - c) := abv_sub_le abs
@[simp] theorem abs_inv : ‚àÄ z : K, abs z‚Åª¬π = (abs z)‚Åª¬π := abv_inv abs
@[simp] theorem abs_div : ‚àÄ z w : K, abs (z / w) = abs z / abs w := abv_div abs

lemma abs_abs_sub_le_abs_sub : ‚àÄ z w : K, abs' (abs z - abs w) ‚â§ abs (z - w) :=
abs_abv_sub_le_abv_sub abs

lemma abs_re_div_abs_le_one (z : K) : abs' (re z / abs z) ‚â§ 1 :=
begin
  by_cases hz : z = 0,
  { simp [hz, zero_le_one] },
  { simp_rw [_root_.abs_div, abs_abs, div_le_iff (abs_pos.2 hz), one_mul, abs_re_le_abs] }
end

lemma abs_im_div_abs_le_one (z : K) : abs' (im z / abs z) ‚â§ 1 :=
begin
  by_cases hz : z = 0,
  { simp [hz, zero_le_one] },
  { simp_rw [_root_.abs_div, abs_abs, div_le_iff (abs_pos.2 hz), one_mul, abs_im_le_abs] }
end

@[simp, norm_cast] lemma abs_cast_nat (n : ‚Ñï) : abs (n : K) = n :=
by rw [‚Üê of_real_nat_cast, abs_of_nonneg (nat.cast_nonneg n)]

lemma norm_sq_eq_abs (x : K) : norm_sq x = abs x ^ 2 :=
by rw [abs, sq, real.mul_self_sqrt (norm_sq_nonneg _)]

lemma re_eq_abs_of_mul_conj (x : K) : re (x * (conj x)) = abs (x * (conj x)) :=
by rw [mul_conj, of_real_re, abs_of_real, norm_sq_eq_abs, sq, _root_.abs_mul, abs_abs]

lemma abs_sq_re_add_conj (x : K) : (abs (x + conj x))^2 = (re (x + conj x))^2 :=
by simp [sq, ‚Üênorm_sq_eq_abs, norm_sq]

lemma abs_sq_re_add_conj' (x : K) : (abs (conj x + x))^2 = (re (conj x + x))^2 :=
by simp [sq, ‚Üênorm_sq_eq_abs, norm_sq]

lemma conj_mul_eq_norm_sq_left (x : K) : conj x * x = ((norm_sq x) : K) :=
begin
  rw ext_iff,
  refine ‚ü®by simp [of_real_re, mul_re, conj_re, conj_im, norm_sq],_‚ü©,
  simp [of_real_im, mul_im, conj_im, conj_re, mul_comm],
end

/-! ### Cauchy sequences -/

theorem is_cau_seq_re (f : cau_seq K abs) : is_cau_seq abs' (Œª n, re (f n)) :=
Œª Œµ Œµ0, (f.cauchy Œµ0).imp $ Œª i H j ij,
lt_of_le_of_lt (by simpa using abs_re_le_abs (f j - f i)) (H _ ij)

theorem is_cau_seq_im (f : cau_seq K abs) : is_cau_seq abs' (Œª n, im (f n)) :=
Œª Œµ Œµ0, (f.cauchy Œµ0).imp $ Œª i H j ij,
lt_of_le_of_lt (by simpa using abs_im_le_abs (f j - f i)) (H _ ij)

/-- The real part of a K Cauchy sequence, as a real Cauchy sequence. -/
noncomputable def cau_seq_re (f : cau_seq K abs) : cau_seq ‚Ñù abs' :=
‚ü®_, is_cau_seq_re f‚ü©

/-- The imaginary part of a K Cauchy sequence, as a real Cauchy sequence. -/
noncomputable def cau_seq_im (f : cau_seq K abs) : cau_seq ‚Ñù abs' :=
‚ü®_, is_cau_seq_im f‚ü©

lemma is_cau_seq_abs {f : ‚Ñï ‚Üí K} (hf : is_cau_seq abs f) :
  is_cau_seq abs' (abs ‚àò f) :=
Œª Œµ Œµ0, let ‚ü®i, hi‚ü© := hf Œµ Œµ0 in
‚ü®i, Œª j hj, lt_of_le_of_lt (abs_abs_sub_le_abs_sub _ _) (hi j hj)‚ü©

@[simp, norm_cast, priority 900] lemma of_real_prod {Œ± : Type*} (s : finset Œ±) (f : Œ± ‚Üí ‚Ñù) :
  ((‚àè i in s, f i : ‚Ñù) : K) = ‚àè i in s, (f i : K) :=
ring_hom.map_prod _ _ _

@[simp, norm_cast, priority 900] lemma of_real_sum {Œ± : Type*} (s : finset Œ±) (f : Œ± ‚Üí ‚Ñù) :
  ((‚àë i in s, f i : ‚Ñù) : K) = ‚àë i in s, (f i : K) :=
ring_hom.map_sum _ _ _

@[simp, norm_cast] lemma of_real_finsupp_sum
  {Œ± M : Type*} [has_zero M] (f : Œ± ‚Üí‚ÇÄ M) (g : Œ± ‚Üí M ‚Üí ‚Ñù) :
  ((f.sum (Œª a b, g a b) : ‚Ñù) : K) = f.sum (Œª a b, ((g a b) : K)) :=
ring_hom.map_finsupp_sum _ f g

@[simp, norm_cast] lemma of_real_finsupp_prod
  {Œ± M : Type*} [has_zero M] (f : Œ± ‚Üí‚ÇÄ M) (g : Œ± ‚Üí M ‚Üí ‚Ñù) :
  ((f.prod (Œª a b, g a b) : ‚Ñù) : K) = f.prod (Œª a b, ((g a b) : K)) :=
ring_hom.map_finsupp_prod _ f g

end is_R_or_C

namespace finite_dimensional
variables {K : Type*} [is_R_or_C K]

open_locale classical
open is_R_or_C

/-- This instance generates a type-class problem with a metavariable `?m` that should satisfy
`is_R_or_C ?m`. Since this can only be satisfied by `‚Ñù` or `‚ÑÇ`, this does not cause problems. -/
library_note "is_R_or_C instance"

/-- An `is_R_or_C` field is finite-dimensional over `‚Ñù`, since it is spanned by `{1, I}`. -/
@[nolint dangerous_instance] instance is_R_or_C_to_real : finite_dimensional ‚Ñù K :=
is_noetherian.iff_fg.mpr ‚ü®‚ü®{1, I},
  begin
    rw eq_top_iff,
    intros a _,
    rw [finset.coe_insert, finset.coe_singleton, submodule.mem_span_insert],
    refine ‚ü®re a, (im a) ‚Ä¢ I, _, _‚ü©,
    { rw submodule.mem_span_singleton,
      use im a },
    simp [re_add_im a, algebra.smul_def, algebra_map_eq_of_real]
  end‚ü©‚ü©

/-- Over an `is_R_or_C` field, we can register the properness of finite-dimensional normed spaces as
an instance. -/
@[priority 900, nolint dangerous_instance] instance proper_is_R_or_C -- note [is_R_or_C instance]
  {E : Type*} [normed_group E] [normed_space K E] [finite_dimensional K E] :
  proper_space E :=
begin
  letI : normed_space ‚Ñù E := restrict_scalars.normed_space ‚Ñù K E,
  letI : is_scalar_tower ‚Ñù K E := restrict_scalars.is_scalar_tower _ _ _,
  letI : finite_dimensional ‚Ñù E := finite_dimensional.trans ‚Ñù K E,
  apply_instance
end

end finite_dimensional

section instances

noncomputable instance real.is_R_or_C : is_R_or_C ‚Ñù :=
{ re := add_monoid_hom.id ‚Ñù,
  im := 0,
  I := 0,
  I_re_ax := by simp only [add_monoid_hom.map_zero],
  I_mul_I_ax := or.intro_left _ rfl,
  re_add_im_ax := Œª z, by unfold_coes; simp [add_zero, id.def, mul_zero],
  of_real_re_ax := Œª r, by simp only [add_monoid_hom.id_apply, algebra.id.map_eq_self],
  of_real_im_ax := Œª r, by simp only [add_monoid_hom.zero_apply],
  mul_re_ax := Œª z w,
    by simp only [sub_zero, mul_zero, add_monoid_hom.zero_apply, add_monoid_hom.id_apply],
  mul_im_ax := Œª z w, by simp only [add_zero, zero_mul, mul_zero, add_monoid_hom.zero_apply],
  conj_re_ax := Œª z, by simp only [star_ring_aut_apply, star_id_of_comm],
  conj_im_ax := Œª z, by simp only [neg_zero, add_monoid_hom.zero_apply],
  conj_I_ax := by simp only [ring_equiv.map_zero, neg_zero],
  norm_sq_eq_def_ax := Œª z, by simp only [sq, norm, ‚Üêabs_mul, abs_mul_self z, add_zero,
    mul_zero, add_monoid_hom.zero_apply, add_monoid_hom.id_apply],
  mul_im_I_ax := Œª z, by simp only [mul_zero, add_monoid_hom.zero_apply],
  inv_def_ax := Œª z, by simp only [star_ring_aut_apply, star, sq, real.norm_eq_abs,
    abs_mul_abs_self, ‚Üêdiv_eq_mul_inv, algebra.id.map_eq_id, id.def, ring_hom.id_apply,
    div_self_mul_self'],
  div_I_ax := Œª z, by simp only [div_zero, mul_zero, neg_zero]}

end instances

namespace is_R_or_C

open_locale complex_conjugate

section cleanup_lemmas

local notation `reR` := @is_R_or_C.re ‚Ñù _
local notation `imR` := @is_R_or_C.im ‚Ñù _
local notation `IR` := @is_R_or_C.I ‚Ñù _
local notation `absR` := @is_R_or_C.abs ‚Ñù _
local notation `norm_sqR` := @is_R_or_C.norm_sq ‚Ñù _

@[simp] lemma re_to_real {x : ‚Ñù} : reR x = x := rfl
@[simp] lemma im_to_real {x : ‚Ñù} : imR x = 0 := rfl
@[simp] lemma conj_to_real {x : ‚Ñù} : conj x = x := rfl
@[simp] lemma I_to_real : IR = 0 := rfl
@[simp] lemma norm_sq_to_real {x : ‚Ñù} : norm_sq x = x*x := by simp [is_R_or_C.norm_sq]
@[simp] lemma abs_to_real {x : ‚Ñù} : absR x = has_abs.abs x :=
by simp [is_R_or_C.abs, abs, real.sqrt_mul_self_eq_abs]

@[simp] lemma coe_real_eq_id : @coe ‚Ñù ‚Ñù _ = id := rfl

end cleanup_lemmas

section linear_maps

variables {K : Type*} [is_R_or_C K]

/-- The real part in a `is_R_or_C` field, as a linear map. -/
noncomputable def re_lm : K ‚Üí‚Çó[‚Ñù] ‚Ñù :=
{ map_smul' := smul_re,  .. re }

@[simp] lemma re_lm_coe : (re_lm : K ‚Üí ‚Ñù) = re := rfl

/-- The real part in a `is_R_or_C` field, as a continuous linear map. -/
noncomputable def re_clm : K ‚ÜíL[‚Ñù] ‚Ñù :=
linear_map.mk_continuous re_lm 1 $ by
{ simp only [norm_eq_abs, re_lm_coe, one_mul, abs_to_real], exact abs_re_le_abs, }

@[simp] lemma re_clm_norm : ‚à•(re_clm : K ‚ÜíL[‚Ñù] ‚Ñù)‚à• = 1 :=
begin
  apply le_antisymm (linear_map.mk_continuous_norm_le _ zero_le_one _),
  convert continuous_linear_map.ratio_le_op_norm _ (1 : K),
  simp,
end

@[simp, norm_cast] lemma re_clm_coe : ((re_clm : K ‚ÜíL[‚Ñù] ‚Ñù) : K ‚Üí‚Çó[‚Ñù] ‚Ñù) = re_lm := rfl

@[simp] lemma re_clm_apply : ((re_clm : K ‚ÜíL[‚Ñù] ‚Ñù) : K ‚Üí ‚Ñù) = re := rfl

@[continuity] lemma continuous_re : continuous (re : K ‚Üí ‚Ñù) := re_clm.continuous

/-- The imaginary part in a `is_R_or_C` field, as a linear map. -/
noncomputable def im_lm : K ‚Üí‚Çó[‚Ñù] ‚Ñù :=
{ map_smul' := smul_im,  .. im }

@[simp] lemma im_lm_coe : (im_lm : K ‚Üí ‚Ñù) = im := rfl

/-- The imaginary part in a `is_R_or_C` field, as a continuous linear map. -/
noncomputable def im_clm : K ‚ÜíL[‚Ñù] ‚Ñù :=
linear_map.mk_continuous im_lm 1 $ by
{ simp only [norm_eq_abs, re_lm_coe, one_mul, abs_to_real], exact abs_im_le_abs, }

@[simp, norm_cast] lemma im_clm_coe : ((im_clm : K ‚ÜíL[‚Ñù] ‚Ñù) : K ‚Üí‚Çó[‚Ñù] ‚Ñù) = im_lm := rfl

@[simp] lemma im_clm_apply : ((im_clm : K ‚ÜíL[‚Ñù] ‚Ñù) : K ‚Üí ‚Ñù) = im := rfl

@[continuity] lemma continuous_im : continuous (im : K ‚Üí ‚Ñù) := im_clm.continuous

/-- Conjugate as an `‚Ñù`-algebra equivalence -/
noncomputable def conj_ae : K ‚âÉ‚Çê[‚Ñù] K :=
{ commutes' := conj_of_real,
  .. star_ring_aut }

@[simp] lemma conj_ae_coe : (conj_ae : K ‚Üí K) = conj := rfl

/-- Conjugate as a linear isometry -/
noncomputable def conj_lie : K ‚âÉ‚Çó·µ¢[‚Ñù] K := ‚ü®conj_ae.to_linear_equiv, Œª z, by simp [norm_eq_abs]‚ü©

@[simp] lemma conj_lie_apply : (conj_lie : K ‚Üí K) = conj := rfl

/-- Conjugate as a continuous linear equivalence -/
noncomputable def conj_cle : K ‚âÉL[‚Ñù] K := @conj_lie K _

@[simp] lemma conj_cle_coe : (@conj_cle K _).to_linear_equiv = conj_ae.to_linear_equiv := rfl

@[simp] lemma conj_cle_apply : (conj_cle : K ‚Üí K) = conj := rfl

@[simp] lemma conj_cle_norm : ‚à•(@conj_cle K _ : K ‚ÜíL[‚Ñù] K)‚à• = 1 :=
(@conj_lie K _).to_linear_isometry.norm_to_continuous_linear_map

@[continuity] lemma continuous_conj : continuous (conj : K ‚Üí K) := conj_lie.continuous

/-- The `‚Ñù ‚Üí K` coercion, as a linear map -/
noncomputable def of_real_am : ‚Ñù ‚Üí‚Çê[‚Ñù] K := algebra.of_id ‚Ñù K

@[simp] lemma of_real_am_coe : (of_real_am : ‚Ñù ‚Üí K) = coe := rfl

/-- The ‚Ñù ‚Üí K coercion, as a linear isometry -/
noncomputable def of_real_li : ‚Ñù ‚Üí‚Çó·µ¢[‚Ñù] K :=
{ to_linear_map := of_real_am.to_linear_map, norm_map' := by simp [norm_eq_abs] }

@[simp] lemma of_real_li_apply : (of_real_li : ‚Ñù ‚Üí K) = coe := rfl

/-- The `‚Ñù ‚Üí K` coercion, as a continuous linear map -/
noncomputable def of_real_clm : ‚Ñù ‚ÜíL[‚Ñù] K := of_real_li.to_continuous_linear_map

@[simp] lemma of_real_clm_coe : ((@of_real_clm K _) : ‚Ñù ‚Üí‚Çó[‚Ñù] K) = of_real_am.to_linear_map := rfl

@[simp] lemma of_real_clm_apply : (of_real_clm : ‚Ñù ‚Üí K) = coe := rfl

@[simp] lemma of_real_clm_norm : ‚à•(of_real_clm : ‚Ñù ‚ÜíL[‚Ñù] K)‚à• = 1 :=
linear_isometry.norm_to_continuous_linear_map of_real_li

@[continuity] lemma continuous_of_real : continuous (coe : ‚Ñù ‚Üí K) := of_real_li.continuous

end linear_maps

end is_R_or_C

section normalization
variables {K : Type*} [is_R_or_C K]
variables {E : Type*} [normed_group E] [normed_space K E]

open is_R_or_C

/- Note: one might think the following lemma belongs in `analysis.normed_space.basic`.  But it
can't be placed there, because that file is an import of `data.complex.is_R_or_C`! -/

/-- Lemma to normalize a vector in a normed space `E` over either `‚ÑÇ` or `‚Ñù` to unit length. -/
@[simp] lemma norm_smul_inv_norm {x : E} (hx : x ‚â† 0) : ‚à•(‚à•x‚à•‚Åª¬π : K) ‚Ä¢ x‚à• = 1 :=
begin
  have h : ‚à•(‚à•x‚à• : K)‚à• = ‚à•x‚à•,
  { rw norm_eq_abs,
    exact abs_of_nonneg (norm_nonneg _) },
  have : ‚à•x‚à• ‚â† 0 := by simp [hx],
  field_simp [norm_smul, h]
end

end normalization
