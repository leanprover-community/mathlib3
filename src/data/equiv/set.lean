/-
Copyright (c) 2015 Microsoft Corporation. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Leonardo de Moura, Mario Carneiro
-/
import data.equiv.basic
import data.set.function

/-!
# Equivalences and sets

In this file we provide lemmas linking equivalences to sets.

Some notable definitions are:

* `equiv.of_injective`: an injective function is (noncomputably) equivalent to its range.
* `equiv.set_congr`: two equal sets are equivalent as types.
* `equiv.set.union`: a disjoint union of sets is equivalent to their `sum`.

This file is separate from `equiv/basic` such that we do not require the full lattice structure
on sets before defining what an equivalence is.
-/

open function

universes u v w z
variables {Î± : Sort u} {Î² : Sort v} {Î³ : Sort w}

namespace equiv

@[simp] lemma range_eq_univ {Î± : Type*} {Î² : Type*} (e : Î± â‰ƒ Î²) : set.range e = set.univ :=
set.eq_univ_of_forall e.surjective

protected lemma image_eq_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î±) : e '' s = e.symm â»Â¹' s :=
set.ext $ assume x, set.mem_image_iff_of_inverse e.left_inv e.right_inv

lemma _root_.set.mem_image_equiv {Î± Î²} {S : set Î±} {f : Î± â‰ƒ Î²} {x : Î²} :
  x âˆˆ f '' S â†” f.symm x âˆˆ S :=
set.ext_iff.mp (f.image_eq_preimage S) x

/-- Alias for `equiv.image_eq_preimage` -/
lemma _root_.set.image_equiv_eq_preimage_symm {Î± Î²} (S : set Î±) (f : Î± â‰ƒ Î²) :
  f '' S = f.symm â»Â¹' S :=
f.image_eq_preimage S

/-- Alias for `equiv.image_eq_preimage` -/
lemma _root_.set.preimage_equiv_eq_image_symm {Î± Î²} (S : set Î±) (f : Î² â‰ƒ Î±) :
  f â»Â¹' S = f.symm '' S :=
(f.symm.image_eq_preimage S).symm

@[simp] protected lemma subset_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î±) (t : set Î²) :
  e.symm '' t âŠ† s â†” t âŠ† e '' s :=
by rw [set.image_subset_iff, e.image_eq_preimage]

@[simp] protected lemma subset_image' {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î±) (t : set Î²) :
  s âŠ† e.symm '' t â†” e '' s âŠ† t :=
calc s âŠ† e.symm '' t â†” e.symm.symm '' s âŠ† t : by rw e.symm.subset_image
                 ... â†” e '' s âŠ† t : by rw e.symm_symm

@[simp] lemma symm_image_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î±) : e.symm '' (e '' s) = s :=
e.left_inverse_symm.image_image s

lemma eq_image_iff_symm_image_eq {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î±) (t : set Î²) :
  t = e '' s â†” e.symm '' t = s :=
(e.symm.injective.image_injective.eq_iff' (e.symm_image_image s)).symm

@[simp] lemma image_symm_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î²) : e '' (e.symm '' s) = s :=
e.symm.symm_image_image s

@[simp] lemma image_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î²) : e '' (e â»Â¹' s) = s :=
e.surjective.image_preimage s

@[simp] lemma preimage_image {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î±) : e â»Â¹' (e '' s) = s :=
e.injective.preimage_image s

protected lemma image_compl {Î± Î²} (f : equiv Î± Î²) (s : set Î±) :
  f '' sá¶œ = (f '' s)á¶œ :=
set.image_compl_eq f.bijective

@[simp] lemma symm_preimage_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î²) :
  e.symm â»Â¹' (e â»Â¹' s) = s :=
e.right_inverse_symm.preimage_preimage s

@[simp] lemma preimage_symm_preimage {Î± Î²} (e : Î± â‰ƒ Î²) (s : set Î±) :
  e â»Â¹' (e.symm â»Â¹' s) = s :=
e.left_inverse_symm.preimage_preimage s

@[simp] lemma preimage_subset {Î± Î²} (e : Î± â‰ƒ Î²) (s t : set Î²) : e â»Â¹' s âŠ† e â»Â¹' t â†” s âŠ† t :=
e.surjective.preimage_subset_preimage_iff

@[simp] lemma image_subset {Î± Î²} (e : Î± â‰ƒ Î²) (s t : set Î±) : e '' s âŠ† e '' t â†” s âŠ† t :=
set.image_subset_image_iff e.injective

@[simp] lemma image_eq_iff_eq {Î± Î²} (e : Î± â‰ƒ Î²) (s t : set Î±) : e '' s = e '' t â†” s = t :=
set.image_eq_image e.injective

lemma preimage_eq_iff_eq_image {Î± Î²} (e : Î± â‰ƒ Î²) (s t) : e â»Â¹' s = t â†” s = e '' t :=
set.preimage_eq_iff_eq_image e.bijective

lemma eq_preimage_iff_image_eq {Î± Î²} (e : Î± â‰ƒ Î²) (s t) : s = e â»Â¹' t â†” e '' s = t :=
set.eq_preimage_iff_image_eq e.bijective

lemma prod_assoc_preimage {Î± Î² Î³} {s : set Î±} {t : set Î²} {u : set Î³} :
  equiv.prod_assoc Î± Î² Î³ â»Â¹' s.prod (t.prod u) = (s.prod t).prod u :=
by { ext, simp [and_assoc] }

/-- A set `s` in `Î± Ã— Î²` is equivalent to the sigma-type `Î£ x, {y | (x, y) âˆˆ s}`. -/
def set_prod_equiv_sigma {Î± Î² : Type*} (s : set (Î± Ã— Î²)) :
  s â‰ƒ Î£ x : Î±, {y | (x, y) âˆˆ s} :=
{ to_fun := Î» x, âŸ¨x.1.1, x.1.2, by simpâŸ©,
  inv_fun := Î» x, âŸ¨(x.1, x.2.1), x.2.2âŸ©,
  left_inv := Î» âŸ¨âŸ¨x, yâŸ©, hâŸ©, rfl,
  right_inv := Î» âŸ¨x, y, hâŸ©, rfl }

/-- The subtypes corresponding to equal sets are equivalent. -/
@[simps apply]
def set_congr {Î± : Type*} {s t : set Î±} (h : s = t) : s â‰ƒ t :=
subtype_equiv_prop h

/--
A set is equivalent to its image under an equivalence.
-/
-- We could construct this using `equiv.set.image e s e.injective`,
-- but this definition provides an explicit inverse.
@[simps]
def image {Î± Î² : Type*} (e : Î± â‰ƒ Î²) (s : set Î±) : s â‰ƒ e '' s :=
{ to_fun := Î» x, âŸ¨e x.1, by simpâŸ©,
  inv_fun := Î» y, âŸ¨e.symm y.1, by { rcases y with âŸ¨-, âŸ¨a, âŸ¨m, rflâŸ©âŸ©âŸ©, simpa using m, }âŸ©,
  left_inv := Î» x, by simp,
  right_inv := Î» y, by simp, }.

open set

namespace set

/-- `univ Î±` is equivalent to `Î±`. -/
@[simps apply symm_apply]
protected def univ (Î±) : @univ Î± â‰ƒ Î± :=
âŸ¨coe, Î» a, âŸ¨a, trivialâŸ©, Î» âŸ¨a, _âŸ©, rfl, Î» a, rflâŸ©

/-- An empty set is equivalent to the `empty` type. -/
protected def empty (Î±) : (âˆ… : set Î±) â‰ƒ empty :=
equiv_empty _

/-- An empty set is equivalent to a `pempty` type. -/
protected def pempty (Î±) : (âˆ… : set Î±) â‰ƒ pempty :=
equiv_pempty _

/-- If sets `s` and `t` are separated by a decidable predicate, then `s âˆª t` is equivalent to
`s âŠ• t`. -/
protected def union' {Î±} {s t : set Î±}
  (p : Î± â†’ Prop) [decidable_pred p]
  (hs : âˆ€ x âˆˆ s, p x)
  (ht : âˆ€ x âˆˆ t, Â¬ p x) : (s âˆª t : set Î±) â‰ƒ s âŠ• t :=
{ to_fun := Î» x, if hp : p x
    then sum.inl âŸ¨_, x.2.resolve_right (Î» xt, ht _ xt hp)âŸ©
    else sum.inr âŸ¨_, x.2.resolve_left (Î» xs, hp (hs _ xs))âŸ©,
  inv_fun := Î» o, match o with
    | (sum.inl x) := âŸ¨x, or.inl x.2âŸ©
    | (sum.inr x) := âŸ¨x, or.inr x.2âŸ©
  end,
  left_inv := Î» âŸ¨x, h'âŸ©, by by_cases p x; simp [union'._match_1, h]; congr,
  right_inv := Î» o, begin
    rcases o with âŸ¨x, hâŸ© | âŸ¨x, hâŸ©;
    dsimp [union'._match_1];
    [simp [hs _ h], simp [ht _ h]]
  end }

/-- If sets `s` and `t` are disjoint, then `s âˆª t` is equivalent to `s âŠ• t`. -/
protected def union {Î±} {s t : set Î±} [decidable_pred (Î» x, x âˆˆ s)] (H : s âˆ© t âŠ† âˆ…) :
  (s âˆª t : set Î±) â‰ƒ s âŠ• t :=
set.union' (Î» x, x âˆˆ s) (Î» _, id) (Î» x xt xs, H âŸ¨xs, xtâŸ©)

lemma union_apply_left {Î±} {s t : set Î±} [decidable_pred (Î» x, x âˆˆ s)] (H : s âˆ© t âŠ† âˆ…)
  {a : (s âˆª t : set Î±)} (ha : â†‘a âˆˆ s) : equiv.set.union H a = sum.inl âŸ¨a, haâŸ© :=
dif_pos ha

lemma union_apply_right {Î±} {s t : set Î±} [decidable_pred (Î» x, x âˆˆ s)] (H : s âˆ© t âŠ† âˆ…)
  {a : (s âˆª t : set Î±)} (ha : â†‘a âˆˆ t) : equiv.set.union H a = sum.inr âŸ¨a, haâŸ© :=
dif_neg $ Î» h, H âŸ¨h, haâŸ©

@[simp] lemma union_symm_apply_left {Î±} {s t : set Î±} [decidable_pred (Î» x, x âˆˆ s)] (H : s âˆ© t âŠ† âˆ…)
  (a : s) : (equiv.set.union H).symm (sum.inl a) = âŸ¨a, subset_union_left _ _ a.2âŸ© :=
rfl

@[simp] lemma union_symm_apply_right {Î±} {s t : set Î±} [decidable_pred (Î» x, x âˆˆ s)] (H : s âˆ© t âŠ† âˆ…)
  (a : t) : (equiv.set.union H).symm (sum.inr a) = âŸ¨a, subset_union_right _ _ a.2âŸ© :=
rfl

/-- A singleton set is equivalent to a `punit` type. -/
protected def singleton {Î±} (a : Î±) : ({a} : set Î±) â‰ƒ punit.{u} :=
âŸ¨Î» _, punit.star, Î» _, âŸ¨a, mem_singleton _âŸ©,
 Î» âŸ¨x, hâŸ©, by { simp at h, subst x },
 Î» âŸ¨âŸ©, rflâŸ©

/-- Equal sets are equivalent. -/
@[simps apply symm_apply]
protected def of_eq {Î± : Type u} {s t : set Î±} (h : s = t) : s â‰ƒ t :=
{ to_fun := Î» x, âŸ¨x, h â–¸ x.2âŸ©,
  inv_fun := Î» x, âŸ¨x, h.symm â–¸ x.2âŸ©,
  left_inv := Î» _, subtype.eq rfl,
  right_inv := Î» _, subtype.eq rfl }

/-- If `a âˆ‰ s`, then `insert a s` is equivalent to `s âŠ• punit`. -/
protected def insert {Î±} {s : set.{u} Î±} [decidable_pred (âˆˆ s)] {a : Î±} (H : a âˆ‰ s) :
  (insert a s : set Î±) â‰ƒ s âŠ• punit.{u+1} :=
calc (insert a s : set Î±) â‰ƒ â†¥(s âˆª {a}) : equiv.set.of_eq (by simp)
... â‰ƒ s âŠ• ({a} : set Î±) : equiv.set.union (by finish [set.subset_def])
... â‰ƒ s âŠ• punit.{u+1} : sum_congr (equiv.refl _) (equiv.set.singleton _)

@[simp] lemma insert_symm_apply_inl {Î±} {s : set.{u} Î±} [decidable_pred (âˆˆ s)] {a : Î±} (H : a âˆ‰ s)
  (b : s) : (equiv.set.insert H).symm (sum.inl b) = âŸ¨b, or.inr b.2âŸ© :=
rfl

@[simp] lemma insert_symm_apply_inr {Î±} {s : set.{u} Î±} [decidable_pred (âˆˆ s)] {a : Î±} (H : a âˆ‰ s)
  (b : punit.{u+1}) : (equiv.set.insert H).symm (sum.inr b) = âŸ¨a, or.inl rflâŸ© :=
rfl

@[simp] lemma insert_apply_left {Î±} {s : set.{u} Î±} [decidable_pred (âˆˆ s)] {a : Î±} (H : a âˆ‰ s) :
  equiv.set.insert H âŸ¨a, or.inl rflâŸ© = sum.inr punit.star :=
(equiv.set.insert H).apply_eq_iff_eq_symm_apply.2 rfl

@[simp] lemma insert_apply_right {Î±} {s : set.{u} Î±} [decidable_pred (âˆˆ s)] {a : Î±} (H : a âˆ‰ s)
  (b : s) : equiv.set.insert H âŸ¨b, or.inr b.2âŸ© = sum.inl b :=
(equiv.set.insert H).apply_eq_iff_eq_symm_apply.2 rfl

/-- If `s : set Î±` is a set with decidable membership, then `s âŠ• sá¶œ` is equivalent to `Î±`. -/
protected def sum_compl {Î±} (s : set Î±) [decidable_pred (âˆˆ s)] : s âŠ• (sá¶œ : set Î±) â‰ƒ Î± :=
calc s âŠ• (sá¶œ : set Î±) â‰ƒ â†¥(s âˆª sá¶œ) : (equiv.set.union (by simp [set.ext_iff])).symm
... â‰ƒ @univ Î± : equiv.set.of_eq (by simp)
... â‰ƒ Î± : equiv.set.univ _

@[simp] lemma sum_compl_apply_inl {Î± : Type u} (s : set Î±) [decidable_pred (âˆˆ s)] (x : s) :
  equiv.set.sum_compl s (sum.inl x) = x := rfl

@[simp] lemma sum_compl_apply_inr {Î± : Type u} (s : set Î±) [decidable_pred (âˆˆ s)] (x : sá¶œ) :
  equiv.set.sum_compl s (sum.inr x) = x := rfl

lemma sum_compl_symm_apply_of_mem {Î± : Type u} {s : set Î±} [decidable_pred (âˆˆ s)] {x : Î±}
  (hx : x âˆˆ s) : (equiv.set.sum_compl s).symm x = sum.inl âŸ¨x, hxâŸ© :=
have â†‘(âŸ¨x, or.inl hxâŸ© : (s âˆª sá¶œ : set Î±)) âˆˆ s, from hx,
by { rw [equiv.set.sum_compl], simpa using set.union_apply_left _ this }

lemma sum_compl_symm_apply_of_not_mem {Î± : Type u} {s : set Î±} [decidable_pred (âˆˆ s)] {x : Î±}
  (hx : x âˆ‰ s) : (equiv.set.sum_compl s).symm x = sum.inr âŸ¨x, hxâŸ© :=
have â†‘(âŸ¨x, or.inr hxâŸ© : (s âˆª sá¶œ : set Î±)) âˆˆ sá¶œ, from hx,
by { rw [equiv.set.sum_compl], simpa using set.union_apply_right _ this }

@[simp] lemma sum_compl_symm_apply {Î± : Type*} {s : set Î±} [decidable_pred (âˆˆ s)] {x : s} :
  (equiv.set.sum_compl s).symm x = sum.inl x :=
by cases x with x hx; exact set.sum_compl_symm_apply_of_mem hx

@[simp] lemma sum_compl_symm_apply_compl {Î± : Type*} {s : set Î±}
  [decidable_pred (âˆˆ s)] {x : sá¶œ} : (equiv.set.sum_compl s).symm x = sum.inr x :=
by cases x with x hx; exact set.sum_compl_symm_apply_of_not_mem hx

/-- `sum_diff_subset s t` is the natural equivalence between
`s âŠ• (t \ s)` and `t`, where `s` and `t` are two sets. -/
protected def sum_diff_subset {Î±} {s t : set Î±} (h : s âŠ† t) [decidable_pred (âˆˆ s)] :
  s âŠ• (t \ s : set Î±) â‰ƒ t :=
calc s âŠ• (t \ s : set Î±) â‰ƒ (s âˆª (t \ s) : set Î±) :
  (equiv.set.union (by simp [inter_diff_self])).symm
... â‰ƒ t : equiv.set.of_eq (by { simp [union_diff_self, union_eq_self_of_subset_left h] })

@[simp] lemma sum_diff_subset_apply_inl
  {Î±} {s t : set Î±} (h : s âŠ† t) [decidable_pred (âˆˆ s)] (x : s) :
  equiv.set.sum_diff_subset h (sum.inl x) = inclusion h x := rfl

@[simp] lemma sum_diff_subset_apply_inr
  {Î±} {s t : set Î±} (h : s âŠ† t) [decidable_pred (âˆˆ s)] (x : t \ s) :
  equiv.set.sum_diff_subset h (sum.inr x) = inclusion (diff_subset t s) x := rfl

lemma sum_diff_subset_symm_apply_of_mem
  {Î±} {s t : set Î±} (h : s âŠ† t) [decidable_pred (âˆˆ s)] {x : t} (hx : x.1 âˆˆ s) :
  (equiv.set.sum_diff_subset h).symm x = sum.inl âŸ¨x, hxâŸ© :=
begin
  apply (equiv.set.sum_diff_subset h).injective,
  simp only [apply_symm_apply, sum_diff_subset_apply_inl],
  exact subtype.eq rfl,
end

lemma sum_diff_subset_symm_apply_of_not_mem
  {Î±} {s t : set Î±} (h : s âŠ† t) [decidable_pred (âˆˆ s)] {x : t} (hx : x.1 âˆ‰ s) :
  (equiv.set.sum_diff_subset h).symm x = sum.inr âŸ¨x, âŸ¨x.2, hxâŸ©âŸ©  :=
begin
  apply (equiv.set.sum_diff_subset h).injective,
  simp only [apply_symm_apply, sum_diff_subset_apply_inr],
  exact subtype.eq rfl,
end

/-- If `s` is a set with decidable membership, then the sum of `s âˆª t` and `s âˆ© t` is equivalent
to `s âŠ• t`. -/
protected def union_sum_inter {Î± : Type u} (s t : set Î±) [decidable_pred (âˆˆ s)] :
  (s âˆª t : set Î±) âŠ• (s âˆ© t : set Î±) â‰ƒ s âŠ• t :=
calc  (s âˆª t : set Î±) âŠ• (s âˆ© t : set Î±)
    â‰ƒ (s âˆª t \ s : set Î±) âŠ• (s âˆ© t : set Î±) : by rw [union_diff_self]
... â‰ƒ (s âŠ• (t \ s : set Î±)) âŠ• (s âˆ© t : set Î±) :
  sum_congr (set.union $ subset_empty_iff.2 (inter_diff_self _ _)) (equiv.refl _)
... â‰ƒ s âŠ• (t \ s : set Î±) âŠ• (s âˆ© t : set Î±) : sum_assoc _ _ _
... â‰ƒ s âŠ• (t \ s âˆª s âˆ© t : set Î±) : sum_congr (equiv.refl _) begin
    refine (set.union' (âˆ‰ s) _ _).symm,
    exacts [Î» x hx, hx.2, Î» x hx, not_not_intro hx.1]
  end
... â‰ƒ s âŠ• t : by { rw (_ : t \ s âˆª s âˆ© t = t), rw [union_comm, inter_comm, inter_union_diff] }

/-- Given an equivalence `eâ‚€` between sets `s : set Î±` and `t : set Î²`, the set of equivalences
`e : Î± â‰ƒ Î²` such that `e â†‘x = â†‘(eâ‚€ x)` for each `x : s` is equivalent to the set of equivalences
between `sá¶œ` and `tá¶œ`. -/
protected def compl {Î± : Type u} {Î² : Type v} {s : set Î±} {t : set Î²} [decidable_pred (âˆˆ s)]
  [decidable_pred (âˆˆ t)] (eâ‚€ : s â‰ƒ t) :
  {e : Î± â‰ƒ Î² // âˆ€ x : s, e x = eâ‚€ x} â‰ƒ ((sá¶œ : set Î±) â‰ƒ (tá¶œ : set Î²)) :=
{ to_fun := Î» e, subtype_equiv e
    (Î» a, not_congr $ iff.symm $ maps_to.mem_iff
      (maps_to_iff_exists_map_subtype.2 âŸ¨eâ‚€, e.2âŸ©)
      (surj_on.maps_to_compl (surj_on_iff_exists_map_subtype.2
        âŸ¨t, eâ‚€, subset.refl t, eâ‚€.surjective, e.2âŸ©) e.1.injective)),
  inv_fun := Î» eâ‚,
    subtype.mk
      (calc Î± â‰ƒ s âŠ• (sá¶œ : set Î±) : (set.sum_compl s).symm
          ... â‰ƒ t âŠ• (tá¶œ : set Î²) : eâ‚€.sum_congr eâ‚
          ... â‰ƒ Î² : set.sum_compl t)
      (Î» x, by simp only [sum.map_inl, trans_apply, sum_congr_apply,
        set.sum_compl_apply_inl, set.sum_compl_symm_apply]),
  left_inv := Î» e,
    begin
      ext x,
      by_cases hx : x âˆˆ s,
      { simp only [set.sum_compl_symm_apply_of_mem hx, â†e.prop âŸ¨x, hxâŸ©,
          sum.map_inl, sum_congr_apply, trans_apply,
          subtype.coe_mk, set.sum_compl_apply_inl] },
      { simp only [set.sum_compl_symm_apply_of_not_mem hx, sum.map_inr,
          subtype_equiv_apply, set.sum_compl_apply_inr, trans_apply,
          sum_congr_apply, subtype.coe_mk] },
    end,
  right_inv := Î» e, equiv.ext $ Î» x, by simp only [sum.map_inr, subtype_equiv_apply,
    set.sum_compl_apply_inr, function.comp_app, sum_congr_apply, equiv.coe_trans,
    subtype.coe_eta, subtype.coe_mk, set.sum_compl_symm_apply_compl] }

/-- The set product of two sets is equivalent to the type product of their coercions to types. -/
protected def prod {Î± Î²} (s : set Î±) (t : set Î²) :
  s.prod t â‰ƒ s Ã— t :=
@subtype_prod_equiv_prod Î± Î² s t

/-- If a function `f` is injective on a set `s`, then `s` is equivalent to `f '' s`. -/
protected noncomputable def image_of_inj_on {Î± Î²} (f : Î± â†’ Î²) (s : set Î±) (H : inj_on f s) :
  s â‰ƒ (f '' s) :=
âŸ¨Î» p, âŸ¨f p, mem_image_of_mem f p.2âŸ©,
 Î» p, âŸ¨classical.some p.2, (classical.some_spec p.2).1âŸ©,
 Î» âŸ¨x, hâŸ©, subtype.eq (H (classical.some_spec (mem_image_of_mem f h)).1 h
   (classical.some_spec (mem_image_of_mem f h)).2),
 Î» âŸ¨y, hâŸ©, subtype.eq (classical.some_spec h).2âŸ©

/-- If `f` is an injective function, then `s` is equivalent to `f '' s`. -/
@[simps apply]
protected noncomputable def image {Î± Î²} (f : Î± â†’ Î²) (s : set Î±) (H : injective f) : s â‰ƒ (f '' s) :=
equiv.set.image_of_inj_on f s (H.inj_on s)

@[simp] protected lemma image_symm_apply {Î± Î²} (f : Î± â†’ Î²) (s : set Î±) (H : injective f)
  (x : Î±) (h : x âˆˆ s) :
  (set.image f s H).symm âŸ¨f x, âŸ¨x, âŸ¨h, rflâŸ©âŸ©âŸ© = âŸ¨x, hâŸ© :=
begin
  apply (set.image f s H).injective,
  simp [(set.image f s H).apply_symm_apply],
end

lemma image_symm_preimage {Î± Î²} {f : Î± â†’ Î²} (hf : injective f) (u s : set Î±) :
  (Î» x, (set.image f s hf).symm x : f '' s â†’ Î±) â»Â¹' u = coe â»Â¹' (f '' u) :=
begin
  ext âŸ¨b, a, has, rflâŸ©,
  have : âˆ€(h : âˆƒa', a' âˆˆ s âˆ§ a' = a), classical.some h = a := Î» h, (classical.some_spec h).2,
  simp [equiv.set.image, equiv.set.image_of_inj_on, hf.eq_iff, this],
end

/-- If `Î±` is equivalent to `Î²`, then `set Î±` is equivalent to `set Î²`. -/
@[simps]
protected def congr {Î± Î² : Type*} (e : Î± â‰ƒ Î²) : set Î± â‰ƒ set Î² :=
âŸ¨Î» s, e '' s, Î» t, e.symm '' t, symm_image_image e, symm_image_image e.symmâŸ©

/-- The set `{x âˆˆ s | t x}` is equivalent to the set of `x : s` such that `t x`. -/
protected def sep {Î± : Type u} (s : set Î±) (t : Î± â†’ Prop) :
  ({ x âˆˆ s | t x } : set Î±) â‰ƒ { x : s | t x } :=
(equiv.subtype_subtype_equiv_subtype_inter s t).symm

/-- The set `ğ’« S := {x | x âŠ† S}` is equivalent to the type `set S`. -/
protected def powerset {Î±} (S : set Î±) : ğ’« S â‰ƒ set S :=
{ to_fun := Î» x : ğ’« S, coe â»Â¹' (x : set Î±),
  inv_fun := Î» x : set S, âŸ¨coe '' x, by rintro _ âŸ¨a : S, _, rflâŸ©; exact a.2âŸ©,
  left_inv := Î» x, by ext y; exact âŸ¨Î» âŸ¨âŸ¨_, _âŸ©, h, rflâŸ©, h, Î» h, âŸ¨âŸ¨_, x.2 hâŸ©, h, rflâŸ©âŸ©,
  right_inv := Î» x, by ext; simp }

/--
If `s` is a set in `range f`,
then its image under `range_splitting f` is in bijection (via `f`) with `s`.
-/
@[simps]
noncomputable def range_splitting_image_equiv {Î± Î² : Type*} (f : Î± â†’ Î²) (s : set (range f)) :
  range_splitting f '' s â‰ƒ s :=
{ to_fun := Î» x, âŸ¨âŸ¨f x, by simpâŸ©,
    (by { rcases x with âŸ¨x, âŸ¨y, âŸ¨m, rflâŸ©âŸ©âŸ©, simpa [apply_range_splitting f] using m, })âŸ©,
  inv_fun := Î» x, âŸ¨range_splitting f x, âŸ¨x, âŸ¨x.2, rflâŸ©âŸ©âŸ©,
  left_inv := Î» x, by { rcases x with âŸ¨x, âŸ¨y, âŸ¨m, rflâŸ©âŸ©âŸ©, simp [apply_range_splitting f] },
  right_inv := Î» x, by simp [apply_range_splitting f], }

end set


/-- If `f : Î± â†’ Î²` has a left-inverse when `Î±` is nonempty, then `Î±` is computably equivalent to the
range of `f`.

While awkward, the `nonempty Î±` hypothesis on `f_inv` and `hf` allows this to be used when `Î±` is
empty too. This hypothesis is absent on analogous definitions on stronger `equiv`s like
`linear_equiv.of_left_inverse` and `ring_equiv.of_left_inverse` as their typeclass assumptions
are already sufficient to ensure non-emptiness. -/
@[simps]
def of_left_inverse {Î± Î² : Sort*}
  (f : Î± â†’ Î²) (f_inv : nonempty Î± â†’ Î² â†’ Î±) (hf : Î  h : nonempty Î±, left_inverse (f_inv h) f) :
  Î± â‰ƒ set.range f :=
{ to_fun := Î» a, âŸ¨f a, a, rflâŸ©,
  inv_fun := Î» b, f_inv (nonempty_of_exists b.2) b,
  left_inv := Î» a, hf âŸ¨aâŸ© a,
  right_inv := Î» âŸ¨b, a, haâŸ©, subtype.eq $ show f (f_inv âŸ¨aâŸ© b) = b,
    from eq.trans (congr_arg f $ by exact ha â–¸ (hf _ a)) ha }

/-- If `f : Î± â†’ Î²` has a left-inverse, then `Î±` is computably equivalent to the range of `f`.

Note that if `Î±` is empty, no such `f_inv` exists and so this definition can't be used, unlike
the stronger but less convenient `of_left_inverse`. -/
abbreviation of_left_inverse' {Î± Î² : Sort*}
  (f : Î± â†’ Î²) (f_inv : Î² â†’ Î±) (hf : left_inverse f_inv f) :
  Î± â‰ƒ set.range f :=
of_left_inverse f (Î» _, f_inv) (Î» _, hf)

/-- If `f : Î± â†’ Î²` is an injective function, then domain `Î±` is equivalent to the range of `f`. -/
@[simps apply]
noncomputable def of_injective {Î± Î²} (f : Î± â†’ Î²) (hf : injective f) : Î± â‰ƒ set.range f :=
equiv.of_left_inverse f
  (Î» h, by exactI function.inv_fun f) (Î» h, by exactI function.left_inverse_inv_fun hf)

theorem apply_of_injective_symm {Î± Î²} (f : Î± â†’ Î²) (hf : injective f) (b : set.range f) :
  f ((of_injective f hf).symm b) = b :=
subtype.ext_iff.1 $ (of_injective f hf).apply_symm_apply b

@[simp] theorem of_injective_symm_apply {Î± Î²} (f : Î± â†’ Î²) (hf : injective f) (a : Î±) :
  (of_injective f hf).symm âŸ¨f a, âŸ¨a, rflâŸ©âŸ© = a :=
begin
  apply (of_injective f hf).injective,
  simp [apply_of_injective_symm f hf],
end

@[simp] lemma self_comp_of_injective_symm {Î± Î²} (f : Î± â†’ Î²) (hf : injective f) :
  f âˆ˜ ((of_injective f hf).symm) = coe :=
funext (Î» x, apply_of_injective_symm f hf x)

lemma of_left_inverse_eq_of_injective {Î± Î² : Type*}
  (f : Î± â†’ Î²) (f_inv : nonempty Î± â†’ Î² â†’ Î±) (hf : Î  h : nonempty Î±, left_inverse (f_inv h) f) :
  of_left_inverse f f_inv hf = of_injective f
    ((em (nonempty Î±)).elim (Î» h, (hf h).injective) (Î» h _ _ _, by {
      haveI : subsingleton Î± := subsingleton_of_not_nonempty h, simp })) :=
by { ext, simp }

lemma of_left_inverse'_eq_of_injective {Î± Î² : Type*}
  (f : Î± â†’ Î²) (f_inv : Î² â†’ Î±) (hf : left_inverse f_inv f) :
  of_left_inverse' f f_inv hf = of_injective f hf.injective :=
by { ext, simp }

protected lemma set_forall_iff {Î± Î²} (e : Î± â‰ƒ Î²) {p : set Î± â†’ Prop} :
  (âˆ€ a, p a) â†” (âˆ€ a, p (e â»Â¹' a)) :=
by simpa [equiv.image_eq_preimage] using (equiv.set.congr e).forall_congr_left'

protected lemma preimage_sUnion {Î± Î²} (f : Î± â‰ƒ Î²) {s : set (set Î²)} :
  f â»Â¹' (â‹ƒâ‚€ s) = â‹ƒâ‚€ (_root_.set.image f â»Â¹' s) :=
by { ext x, simp [(equiv.set.congr f).symm.exists_congr_left] }

end equiv

/-- If a function is a bijection between two sets `s` and `t`, then it induces an
equivalence between the types `â†¥s` and ``â†¥t`. -/
noncomputable def set.bij_on.equiv {Î± : Type*} {Î² : Type*} {s : set Î±} {t : set Î²} (f : Î± â†’ Î²)
  (h : set.bij_on f s t) : s â‰ƒ t :=
equiv.of_bijective _ h.bijective

/-- The composition of an updated function with an equiv on a subset can be expressed as an
updated function. -/
lemma dite_comp_equiv_update {Î± : Type*} {Î² : Sort*} {Î³ : Sort*} {s : set Î±} (e : Î² â‰ƒ s)
  (v : Î² â†’ Î³) (w : Î± â†’ Î³) (j : Î²) (x : Î³) [decidable_eq Î²] [decidable_eq Î±]
  [âˆ€ j, decidable (j âˆˆ s)] :
  (Î» (i : Î±), if h : i âˆˆ s then (function.update v j x) (e.symm âŸ¨i, hâŸ©) else w i) =
  function.update (Î» (i : Î±), if h : i âˆˆ s then v (e.symm âŸ¨i, hâŸ©) else w i) (e j) x :=
begin
  ext i,
  by_cases h : i âˆˆ s,
  { rw [dif_pos h,
        function.update_apply_equiv_apply, equiv.symm_symm, function.comp,
        function.update_apply, function.update_apply,
        dif_pos h],
    have h_coe : (âŸ¨i, hâŸ© : s) = e j â†” i = e j := subtype.ext_iff.trans (by rw subtype.coe_mk),
    simp_rw h_coe,
    congr, },
  { have : i â‰  e j,
      by { contrapose! h, have : (e j : Î±) âˆˆ s := (e j).2, rwa â† h at this },
    simp [h, this] }
end
