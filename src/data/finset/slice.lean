/-
Copyright (c) 2021 Bhavik Mehta, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import algebra.big_operators.basic
import data.nat.interval
import order.antichain

/-!
# `r`-sets and slice

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the `r`-th slice of a set family and provides a way to say that a set family is
made of `r`-sets.

An `r`-set is a finset of cardinality `r` (aka of *size* `r`). The `r`-th slice of a set family is
the set family made of its `r`-sets.

## Main declarations

* `set.sized`: `A.sized r` means that `A` only contains `r`-sets.
* `finset.slice`: `A.slice r` is the set of `r`-sets in `A`.

## Notation

`A # r` is notation for `A.slice r` in locale `finset_family`.
-/

open finset nat
open_locale big_operators

variables {Î± : Type*} {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*}

namespace set
variables {A B : set (finset Î±)} {r : â„•}

/-! ### Families of `r`-sets -/

/-- `sized r A` means that every finset in `A` has size `r`. -/
def sized (r : â„•) (A : set (finset Î±)) : Prop := âˆ€ â¦ƒxâ¦„, x âˆˆ A â†’ card x = r

lemma sized.mono (h : A âŠ† B) (hB : B.sized r) : A.sized r := Î» x hx, hB $ h hx

lemma sized_union : (A âˆª B).sized r â†” A.sized r âˆ§ B.sized r :=
âŸ¨Î» hA, âŸ¨hA.mono $ subset_union_left _ _, hA.mono $ subset_union_right _ _âŸ©,
  Î» hA x hx, hx.elim (Î» h, hA.1 h) $ Î» h, hA.2 hâŸ©

alias sized_union â†” _ sized.union

--TODO: A `forall_Union` lemma would be handy here.
@[simp] lemma sized_Union {f : Î¹ â†’ set (finset Î±)} : (â‹ƒ i, f i).sized r â†” âˆ€ i, (f i).sized r :=
by { simp_rw [set.sized, set.mem_Union, forall_exists_index], exact forall_swap }

@[simp] lemma sized_Unionâ‚‚ {f : Î  i, Îº i â†’ set (finset Î±)} :
  (â‹ƒ i j, f i j).sized r â†” âˆ€ i j, (f i j).sized r :=
by simp_rw sized_Union

protected lemma sized.is_antichain (hA : A.sized r) : is_antichain (âŠ†) A :=
Î» s hs t ht h hst, h $ finset.eq_of_subset_of_card_le hst ((hA ht).trans (hA hs).symm).le

protected lemma sized.subsingleton (hA : A.sized 0) : A.subsingleton :=
subsingleton_of_forall_eq âˆ… $ Î» s hs, card_eq_zero.1 $ hA hs

lemma sized.subsingleton' [fintype Î±] (hA : A.sized (fintype.card Î±)) : A.subsingleton :=
subsingleton_of_forall_eq finset.univ $ Î» s hs, s.card_eq_iff_eq_univ.1 $ hA hs

lemma sized.empty_mem_iff (hA : A.sized r) : âˆ… âˆˆ A â†” A = {âˆ…} := hA.is_antichain.bot_mem_iff

lemma sized.univ_mem_iff [fintype Î±] (hA : A.sized r) : finset.univ âˆˆ A â†” A = {finset.univ} :=
hA.is_antichain.top_mem_iff

lemma sized_powerset_len (s : finset Î±) (r : â„•) : (powerset_len r s : set (finset Î±)).sized r :=
Î» t ht, (mem_powerset_len.1 ht).2

end set

namespace finset
section sized
variables [fintype Î±] {ğ’œ : finset (finset Î±)} {s : finset Î±} {r : â„•}

lemma subset_powerset_len_univ_iff : ğ’œ âŠ† powerset_len r univ â†” (ğ’œ : set (finset Î±)).sized r :=
forall_congr $ Î» A, by rw [mem_powerset_len_univ_iff, mem_coe]

alias subset_powerset_len_univ_iff  â†” _ _root_.set.sized.subset_powerset_len_univ

lemma _root_.set.sized.card_le (hğ’œ : (ğ’œ : set (finset Î±)).sized r) :
  card ğ’œ â‰¤ (fintype.card Î±).choose r :=
begin
  rw [fintype.card, â†card_powerset_len],
  exact card_le_of_subset hğ’œ.subset_powerset_len_univ,
end

end sized

/-! ### Slices -/

section slice
variables {ğ’œ : finset (finset Î±)} {A Aâ‚ Aâ‚‚ : finset Î±} {r râ‚ râ‚‚ : â„•}

/-- The `r`-th slice of a set family is the subset of its elements which have cardinality `r`. -/
def slice (ğ’œ : finset (finset Î±)) (r : â„•) : finset (finset Î±) := ğ’œ.filter (Î» i, i.card = r)

localized "infix (name := finset.slice) ` # `:90 := finset.slice" in finset_family

/-- `A` is in the `r`-th slice of `ğ’œ` iff it's in `ğ’œ` and has cardinality `r`. -/
lemma mem_slice : A âˆˆ ğ’œ # r â†” A âˆˆ ğ’œ âˆ§ A.card = r := mem_filter

/-- The `r`-th slice of `ğ’œ` is a subset of `ğ’œ`. -/
lemma slice_subset : ğ’œ # r âŠ† ğ’œ := filter_subset _ _

/-- Everything in the `r`-th slice of `ğ’œ` has size `r`. -/
lemma sized_slice : (ğ’œ # r : set (finset Î±)).sized r := Î» _, and.right âˆ˜ mem_slice.mp

lemma eq_of_mem_slice (hâ‚ : A âˆˆ ğ’œ # râ‚) (hâ‚‚ : A âˆˆ ğ’œ # râ‚‚) : râ‚ = râ‚‚ :=
(sized_slice hâ‚).symm.trans $ sized_slice hâ‚‚

/-- Elements in distinct slices must be distinct. -/
lemma ne_of_mem_slice (hâ‚ : Aâ‚ âˆˆ ğ’œ # râ‚) (hâ‚‚ : Aâ‚‚ âˆˆ ğ’œ # râ‚‚) : râ‚ â‰  râ‚‚ â†’ Aâ‚ â‰  Aâ‚‚ :=
mt $ Î» h, (sized_slice hâ‚).symm.trans ((congr_arg card h).trans (sized_slice hâ‚‚))

lemma pairwise_disjoint_slice : (set.univ : set â„•).pairwise_disjoint (slice ğ’œ) :=
Î» m _ n _ hmn, disjoint_filter.2 $ Î» s hs hm hn, hmn $ hm.symm.trans hn

variables [fintype Î±] (ğ’œ)

@[simp] lemma bUnion_slice [decidable_eq Î±] : (Iic $ fintype.card Î±).bUnion ğ’œ.slice = ğ’œ :=
subset.antisymm (bUnion_subset.2 $ Î» r _, slice_subset) $ Î» s hs,
  mem_bUnion.2 âŸ¨s.card, mem_Iic.2 $ s.card_le_univ, mem_slice.2 $ âŸ¨hs, rflâŸ©âŸ©

@[simp] lemma sum_card_slice : âˆ‘ r in Iic (fintype.card Î±), (ğ’œ # r).card = ğ’œ.card :=
begin
  letI := classical.dec_eq Î±,
  rw [â†card_bUnion, bUnion_slice],
  exact finset.pairwise_disjoint_slice.subset (set.subset_univ _),
end

end slice
end finset
