/-
Copyright (c) 2021 Bhavik Mehta, YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, Alena Gusakov, YaÃ«l Dillies
-/
import data.fintype.basic
import order.antichain
import order.polytope.grade

/-!
# `n`-sets and slice

This file defines the `n`-th slice of a set family and provides a way to say that a set family is
made of `n`-sets.

An `n`-set is a finset of cardinality `n` (aka of *size* `n`). The `n`-th slice of a set family is
the set family made of its `n`-sets.

## Main declarations

* `set.sized`: `s.sized n` means that `s` only contains `n`-sets.
* `finset.slice`: `s.slice n` is the set of `n`-sets in `s`.

## Notation

`s # n` is notation for `s.slice n` in locale `finset_family`.
-/

open finset nat

variables {Î± : Type*}

namespace set
section preorder
variables [preorder Î±] [order_bot Î±] [grade_order Î±] {s t : set Î±} {n : â„•}

/-! ### Families of `n`-sets -/

/-- `sized n s` means that every element in `s` has grade `n`. -/
def sized (n : â„•) (s : set Î±) : Prop := âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ grade a = n

lemma sized.mono (h : s âŠ† t) (ht : t.sized n) : s.sized n := Î» a ha, ht $ h ha

lemma sized_union : (s âˆª t).sized n â†” s.sized n âˆ§ t.sized n :=
âŸ¨Î» hs, âŸ¨hs.mono $ subset_union_left _ _, hs.mono $ subset_union_right _ _âŸ©,
  Î» hs a ha, ha.elim (Î» h, hs.1 h) $ Î» h, hs.2 hâŸ©

alias sized_union â†” _ set.sized.union

attribute [protected] set.sized.union

end preorder

section partial_order
variables [partial_order Î±] [order_bot Î±] [grade_order Î±] {s t : set Î±} {n : â„•}

protected lemma sized.is_antichain (hs : s.sized n) : is_antichain (â‰¤) s :=
Î» a ha b hb h hab, (grade_strict_mono $ hab.lt_of_ne h).ne $ (hs ha).trans (hs hb).symm

end partial_order
end set

namespace finset
section sized
variables [fintype Î±] {ğ’œ : finset (finset Î±)} {s : finset Î±} {n : â„•}

lemma subset_powerset_len_univ_iff : ğ’œ âŠ† powerset_len n univ â†” (ğ’œ : set (finset Î±)).sized n :=
forall_congr $ Î» s, by rw [mem_powerset_len_univ_iff, finset.grade, mem_coe]

alias subset_powerset_len_univ_iff  â†” _ set.sized.subset_powerset_len_univ

lemma _root_.set.sized.card_le (hğ’œ : (ğ’œ : set (finset Î±)).sized n) :
  card ğ’œ â‰¤ (fintype.card Î±).choose n :=
begin
  rw [fintype.card, â†card_powerset_len],
  exact card_le_of_subset hğ’œ.subset_powerset_len_univ,
end

end sized

/-! ### Slices -/

section slice
variables [preorder Î±] [order_bot Î±] [grade_order Î±] {s : finset Î±} {a b c : Î±} {m n : â„•}

/-- The `n`-th slice of a set family is the subset of its elements which have cardinality `n`. -/
def slice (s : finset Î±) (n : â„•) : finset Î± := s.filter (Î» a, grade a = n)

localized "infix ` # `:90 := finset.slice" in finset_family

/-- `a` is in the `n`-th slice of `s` iff it's in `s` and has grade `n`. -/
lemma mem_slice : a âˆˆ s # n â†” a âˆˆ s âˆ§ grade a = n := mem_filter

/-- The `n`-th slice of `s` is a subset of `s`. -/
lemma slice_subset : s # n âŠ† s := filter_subset _ _

/-- Everything in the `n`-th slice of `s` has size `n`. -/
lemma sized_slice : (s # n : set Î±).sized n := Î» _, and.right âˆ˜ mem_slice.mp

lemma eq_of_mem_slice (hâ‚ : a âˆˆ s # m) (hâ‚‚ : a âˆˆ s # n) : m = n :=
(sized_slice hâ‚).symm.trans $ sized_slice hâ‚‚

/-- Elements in distinct slices must be distinct. -/
lemma ne_of_mem_slice (ha : a âˆˆ s # m) (hb : b âˆˆ s # n) : m â‰  n â†’ a â‰  b :=
mt $ Î» h, (sized_slice ha).symm.trans ((congr_arg grade h).trans (sized_slice hb))

variables [decidable_eq Î±]

lemma pairwise_disjoint_slice : (set.univ : set â„•).pairwise_disjoint (slice s) :=
Î» m _ n _ hmn, disjoint_filter.2 $ Î» s hs hm hn, hmn $ hm.symm.trans hn

end slice
end finset
