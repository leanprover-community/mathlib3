/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import set_theory.continuum
import data.stream.basic
import data.setoid.basic
import order.pilex
import order.conditionally_complete_lattice

/-- A binary fraction is a sequence of zeros and ones. We use a map `â„• â†’ bool` to represent it, with
`ff` encoding zero and `tt` encoding one. -/
def binary_fraction : Type := â„• â†’ bool

variables {x y z : binary_fraction}

open_locale cardinal
open cardinal function set bool

namespace binary_fraction

instance : inhabited binary_fraction := pi.inhabited _

noncomputable instance : linear_order binary_fraction := pilex.linear_order nat.lt_wf

/-- Two binary fractions `x` and `y` are related by `binary_fraction.tail_rel` if they have the form
`x = xâ‚€...xâ‚™ ff tt tt tt tt ...` and `y = xâ‚€...xâ‚™ tt ff ff ff ff ...`. Two fractions related by
`tail_rel` define the same real number. -/
def tail_rel (x y : binary_fraction) :=
âˆƒ k, (âˆ€ i < k, x i = y i) âˆ§ x k = ff âˆ§ y k = tt âˆ§ (âˆ€ i > k, x i = tt) âˆ§ (âˆ€ i > k, y i = ff)

namespace tail_rel
protected lemma lt (h : tail_rel x y) : x < y :=
h.imp $ Î» K hK, âŸ¨hK.1, bool.lt_iff.2 âŸ¨hK.2.1, hK.2.2.1âŸ©âŸ©

lemma comp_bnot_symm (h : tail_rel x y) : tail_rel (bnot âˆ˜ y) (bnot âˆ˜ x) :=
begin
  rcases h with âŸ¨K, hlt, hxk, hyk, hgtx, hgtyâŸ©,
  refine âŸ¨K, Î» i hi, (congr_arg bnot (hlt i hi)).symm, by simp [hyk], by simp [hxk],
    Î» i hi, by simp [hgty i hi], Î» i hi, by simp [hgtx i hi]âŸ©
end

/-- The same fraction can not have a tail of `ff`s and a tail of `tt`s at the same time. -/
lemma strong_asymm (h : tail_rel x y) (z : binary_fraction) : Â¬ tail_rel y z :=
begin
  rcases h with âŸ¨k, -, -, -, -, hkâŸ©, rintro âŸ¨l, -, -, -, hl, -âŸ©,
  specialize hk (max k l + 1) ((le_max_left k l).trans_lt $ nat.lt_succ_self _),
  specialize hl (max k l + 1) ((le_max_right k l).trans_lt $ nat.lt_succ_self _),
  exact ff_ne_tt (hk.symm.trans hl)
end

lemma right_le_of_left_lt (hâ‚ : tail_rel x y) (hâ‚‚ : x < z) : y â‰¤ z :=
begin
  rcases hâ‚ with âŸ¨K, hlt, hxk, hyk, hxgt, hygtâŸ©,
  rcases hâ‚‚ with âŸ¨N, hltN, hNâŸ©,
  rcases lt_trichotomy K N with (hKN|rfl|hNK),
  { rw hxgt N hKN at hN, exact (le_tt.not_lt hN).elim },
  { refine pilex.le_of_forall_le nat.lt_wf (Î» i, _),
    rcases lt_trichotomy i K with (hiK|rfl|hKi),
    exacts [hlt i hiK â–¸ (hltN _ hiK).le, (bool.lt_iff.1 hN).2.symm â–¸ le_tt,
      (hygt _ hKi).symm â–¸ ff_le] },
  { exact le_of_lt âŸ¨N, Î» j hj, hlt j (hj.trans hNK) â–¸ hltN _ hj, hlt N hNK â–¸ hNâŸ© }
end

lemma le_left_of_lt_right (hâ‚ : tail_rel y z) (hâ‚‚ : x < z) : x â‰¤ y :=
le_of_not_lt $ Î» h, (hâ‚.right_le_of_left_lt h).not_lt hâ‚‚

protected lemma left_unique (hâ‚ : tail_rel x z) (hâ‚‚ : tail_rel y z) : x = y :=
le_antisymm (hâ‚‚.le_left_of_lt_right hâ‚.lt) (hâ‚.le_left_of_lt_right hâ‚‚.lt)

protected lemma right_unique (hâ‚ : tail_rel x y) (hâ‚‚ : tail_rel x z) : y = z :=
le_antisymm (hâ‚.right_le_of_left_lt hâ‚‚.lt) (hâ‚‚.right_le_of_left_lt hâ‚.lt)

end tail_rel

instance : setoid binary_fraction :=
{ r := Î» x y, x = y âˆ¨ tail_rel x y âˆ¨ tail_rel y x,
  iseqv :=
    begin
      refine âŸ¨Î» x, or.inl rfl, Î» x y, or.imp eq.symm or.symm, _âŸ©,
      rintro x y z (rfl|hxy|hyx),
      { exact id },
      { rintro (rfl|hyz|hzy),
        exacts [or.inr (or.inl hxy), (hxy.strong_asymm _ hyz).elim, or.inl (hxy.left_unique hzy)] },
      { rintro (rfl|hyz|hzy),
        exacts [or.inr (or.inr hyx), or.inl $ hyx.right_unique hyz, (hzy.strong_asymm _ hyx).elim] }
    end }

lemma tail_rel.equiv (h : tail_rel x y) : x â‰ˆ y := or.inr (or.inl h)
lemma tail_rel.equiv' (h : tail_rel x y) : y â‰ˆ x := or.inr (or.inr h)

lemma lt_of_lt_of_equiv_not_equiv {a a' b b' : binary_fraction} (hlt : a < b) (ha : a â‰ˆ a')
  (hb : b â‰ˆ b') (hne : Â¬ a â‰ˆ b) :
  a' < b' :=
begin
  rcases âŸ¨ha, hbâŸ© with âŸ¨(rfl|ha|ha), (rfl|hb|hb)âŸ©,
  { exact hlt },
  { exact hlt.trans hb.lt },
  { exact (hb.le_left_of_lt_right hlt).lt_of_ne (Î» hab', hne $ hab'.symm â–¸ hb.equiv) },
  { exact (ha.right_le_of_left_lt hlt).lt_of_ne (Î» ha'b, hne $ ha'b â–¸ ha.equiv) },
  { exact (ha.right_le_of_left_lt hlt).trans_lt hb.lt },
  { refine (ha.right_le_of_left_lt ((hb.le_left_of_lt_right hlt).lt_of_ne $ _)).lt_of_ne _,
    { rintro rfl, obtain rfl : a' = b := ha.right_unique hb, exact hne hb.equiv },
    { rintro rfl, exact ha.strong_asymm _ hb } },
  { exact ha.lt.trans hlt },
  { exact ha.lt.trans (hlt.trans hb.lt) },
  { exact ha.lt.trans_le (hb.le_left_of_lt_right hlt) }
end

lemma not_tail_rel_interleave : Â¬tail_rel (x â‹ˆ z) (y â‹ˆ z) :=
begin
  rintro âŸ¨K, -, -, -, hKâ‚, hKâ‚‚âŸ©,
  have : K < 2 * K + 1, by simp [two_mul, add_assoc],
  refine ff_ne_tt _,
  calc ff = (y â‹ˆ z).nth (2 * K + 1) : (hKâ‚‚ _ this).symm
  ... = z K : stream.nth_interleave_right _ _ _
  ... = (x â‹ˆ z).nth (2 * K + 1) : (stream.nth_interleave_right _ _ _).symm
  ... = tt : hKâ‚ _ this
end

lemma eq_of_eqv_interleave (h : binary_fraction.setoid.rel (x â‹ˆ z) (y â‹ˆ z)) : x = y :=
begin
  rcases h with (eq|h|h),
  { rw [â† stream.even_interleave x z, eq, stream.even_interleave] },
  exacts [(not_tail_rel_interleave h).elim, (not_tail_rel_interleave h).elim] 
end

noncomputable instance : linear_order (quotient binary_fraction.setoid) :=
linear_order.lift quotient.out $ (Î» _ _, quotient.out_inj.1)

lemma mkq_lt_mkq : âŸ¦xâŸ§ < âŸ¦yâŸ§ â†” x < y âˆ§ Â¬ x â‰ˆ y :=
begin
  refine âŸ¨Î» hlt, _, Î» hlt, _âŸ©,
  { refine âŸ¨lt_of_lt_of_equiv_not_equiv hlt (quotient.mk_out _) (quotient.mk_out _) (Î» h, _), _âŸ©,
    exacts [hlt.ne (quotient.out_equiv_out.1 h), Î» h, hlt.ne (quotient.sound h)] },
  { exact lt_of_lt_of_equiv_not_equiv hlt.1 (setoid.symm $ quotient.mk_out _)
      (setoid.symm $ quotient.mk_out _) hlt.2 }
end

lemma mkq_le_mkq : âŸ¦xâŸ§ â‰¤ âŸ¦yâŸ§ â†” x â‰¤ y âˆ¨ x â‰ˆ y :=
begin
  simp only [â† not_lt, mkq_lt_mkq, not_and_distrib, not_not],
  exact or_congr iff.rfl âŸ¨setoid.symm, setoid.symmâŸ©
end

variables {Î± : Type*}

section preorder

variable [preorder Î±]

variable (choice : Î  I : nontrivial_interval Î±, I.Ioo)

/-- Given a choice function `choice : Î  I : nontrivial_interval Î±, I.Ioo`, a boolean value `b`, and
a nontrivial interval `I`, returns either `[I.left, choice I]` (if `b = ff`), or
`[choice I, I.right]` (if `b = tt`). -/
def next_interval (b : bool) (I : nontrivial_interval Î±) : nontrivial_interval Î± :=
cond b âŸ¨choice I, I.right, (choice I).2.2âŸ© âŸ¨I.left, choice I, (choice I).2.1âŸ©

lemma next_interval_lt (I : nontrivial_interval Î±) :
  âˆ€ b, next_interval choice b I < I
| ff := âŸ¨âŸ¨le_rfl, (choice I).2.2.leâŸ©, Î» h, h.2.not_lt (choice I).2.2âŸ©
| tt := âŸ¨âŸ¨(choice I).2.1.le, le_rflâŸ©, Î» h, h.1.not_lt (choice I).2.1âŸ©

/-- Given a choice function `choice : Î  I : nontrivial_interval Î±, I.Ioo`, a binary fraction `x`
and an initial interval `I`, returns the strictly antitone sequence of nontrivial intervals given by
`nth_interval choice x I 0 = I` and
`nth_interval choice x I (n + 1) = next_interval choice (x n) (nth_interval choice x I n)`. -/
def nth_interval (x : binary_fraction) (I : nontrivial_interval Î±) : â„• â†’ nontrivial_interval Î±
| 0 := I
| (n + 1) := next_interval choice (x n) (nth_interval n)

@[simp] lemma nth_interval_zero (x : binary_fraction) (I : nontrivial_interval Î±) :
  nth_interval choice x I 0 = I := rfl

lemma nth_interval_succ (x : binary_fraction) (I : nontrivial_interval Î±) (n : â„•) :
  nth_interval choice x I (n + 1) = next_interval choice (x n) (nth_interval choice x I n) :=
rfl

lemma nth_interval_succ' (x : binary_fraction) (I : nontrivial_interval Î±) (n : â„•) :
  nth_interval choice x I (n + 1) =
    nth_interval choice (stream.tail x) (next_interval choice (x 0) I) n :=
begin
  induction n with n ihn generalizing I, { refl },
  rw [nth_interval_succ, nat.succ_eq_add_one, ihn, nth_interval_succ],
  refl
end

lemma strict_anti_nth_interval (x : binary_fraction) (I : nontrivial_interval Î±) :
  strict_anti (x.nth_interval choice I) :=
strict_anti_nat_of_succ_lt $ Î» n, next_interval_lt _ _ _

lemma antitone_nth_interval (x : binary_fraction) (I : nontrivial_interval Î±) :
  antitone (x.nth_interval choice I) :=
(strict_anti_nth_interval choice x I).antitone

lemma nth_interval_congr (n : â„•) (h : âˆ€ k < n, x k = y k) (I : nontrivial_interval Î±) :
  x.nth_interval choice I n = y.nth_interval choice I n :=
begin
  induction n with n ihn, { refl },
  rw [nth_interval, nth_interval, ihn, h n n.lt_succ_self],
  exact Î» k hk, h k (hk.trans n.lt_succ_self)
end

end preorder

variables [conditionally_complete_lattice Î±] (choice : Î  I : nontrivial_interval Î±, I.Ioo)

/-- â€œDecodeâ€ an element of a conditionally complete lattice `Î±` encoded by `x : binary_fraction`
given a choice function and an initial interval. In the case of real numbers,
`I = âŸ¨0, 1, zero_lt_oneâŸ©`, and `(choice J : â„) = (J.left + J.right) / 2`, this corresponds to the
classical binary representation of a real number. -/
def decode (I : nontrivial_interval Î±) (x : binary_fraction) : Î± :=
â¨† n, (x.nth_interval choice I n).left

lemma decode_mem_Inter_Icc (I : nontrivial_interval Î±) (x : binary_fraction) :
  x.decode choice I âˆˆ â‹‚ n, (x.nth_interval choice I n).Icc :=
csupr_mem_Inter_Icc_of_antitone_nontrivial_interval (x.antitone_nth_interval choice I)

lemma decode_mem_Icc (I : nontrivial_interval Î±) (x : binary_fraction) (n : â„•) :
  x.decode choice I âˆˆ (x.nth_interval choice I n).Icc :=
by convert mem_Inter.1 (x.decode_mem_Inter_Icc choice I) n

lemma decode_lt_of_lt_not_equiv (I : nontrivial_interval Î±) (hâ‚ : x < y) (hâ‚‚ : Â¬ x â‰ˆ y) :
  x.decode choice I < y.decode choice I :=
begin
  rcases hâ‚ with âŸ¨N, lt_N, xy_NâŸ©,
  rw bool.lt_iff at xy_N,
  rcases em (âˆƒ n > N, x n = ff) with âŸ¨n, hNn, hxnâŸ©|Hx,
  { calc x.decode choice I â‰¤ (x.nth_interval choice I (n + 1)).right :
      (x.decode_mem_Icc choice I _).2
    ... < (x.nth_interval choice I n).right :
      by { rw [nth_interval, hxn], exact (choice _).2.2 }
    ... â‰¤ (x.nth_interval choice I (N + 1)).right :
      nontrivial_interval.monotone_right $ antitone_nth_interval _ _ _ hNn
    ... = (y.nth_interval choice I (N + 1)).left :
      by { rw [nth_interval, nth_interval, xy_N.1, xy_N.2, nth_interval_congr _ _ lt_N], refl }
    ... â‰¤ y.decode choice I : (y.decode_mem_Icc choice I _).1 },
  { rcases em (âˆƒ n > N, y n = tt) with âŸ¨n, hNn, hynâŸ©|Hy,
    { calc x.decode choice I â‰¤ (x.nth_interval choice I (N + 1)).right :
        (x.decode_mem_Icc choice I _).2
      ... = (y.nth_interval choice I (N + 1)).left :
        by { rw [nth_interval, nth_interval, xy_N.1, xy_N.2, nth_interval_congr _ _ lt_N], refl }
      ... â‰¤ (y.nth_interval choice I n).left :
        nontrivial_interval.antitone_left $ antitone_nth_interval _ _ _ hNn
      ... < (y.nth_interval choice I (n + 1)).left :
        by { rw [nth_interval, hyn], exact (choice _).2.1 }
      ... â‰¤ y.decode choice I : (y.decode_mem_Icc choice I _).1 },
    suffices : tail_rel x y, from (hâ‚‚ this.equiv).elim,
    push_neg at Hx Hy,
    exact âŸ¨N, lt_N, xy_N.1, xy_N.2, Î» i hi, eq_tt_of_ne_ff (Hx i hi),
      Î» i hi, eq_ff_of_ne_tt (Hy i hi)âŸ© }
end

lemma strict_mono_decode_out (I : nontrivial_interval Î±) :
  strict_mono (Î» x : quotient binary_fraction.setoid, x.out.decode choice I) :=
Î» x y h, decode_lt_of_lt_not_equiv _ _ h $ Î» H, h.ne $ quotient.out_equiv_out.1 H

instance : has_card_continuum binary_fraction := pi.has_card_continuum _ _

instance : has_card_continuum (quotient binary_fraction.setoid) :=
âŸ¨begin
  rw â† mk_eq_continuum binary_fraction,
  refine mk_quotient_le.antisymm _,
  set f : binary_fraction â†’ quotient binary_fraction.setoid :=
    Î» x, quotient.mk (x â‹ˆ (Î» _, ff)),
  have inj : injective f := Î» x y h, eq_of_eqv_interleave (quotient.exact h),
  exact mk_le_of_injective inj
endâŸ©

end binary_fraction

namespace cardinal

open binary_fraction

universe u

variables {Î± : Type u} [conditionally_complete_lattice Î±] [densely_ordered Î±]

section

variables {a b : Î±}

lemma continuum_le_mk_Icc (h : a < b) : ð”  â‰¤ #(Icc a b) :=
begin
  set c : Î  I : nontrivial_interval Î±, I.Ioo :=
    Î» I, classical.indefinite_description _ I.nonempty_Ioo,
  set f : quotient binary_fraction.setoid â†’ Icc a b :=
    Î» x, âŸ¨x.out.decode c âŸ¨a, b, hâŸ©, x.out.decode_mem_Icc _ _ 0âŸ©,
  have hf : strict_mono f := strict_mono_decode_out c _,
  simpa using lift_mk_le'.2 âŸ¨âŸ¨f, hf.injectiveâŸ©âŸ©,
end

lemma continuum_le_mk_Ioo (h : a < b) : ð”  â‰¤ #(Ioo a b) :=
begin
  rcases exists_between h with âŸ¨aâ‚, ha, hltâŸ©, rcases exists_between hlt with âŸ¨bâ‚, hab, hbâŸ©,
  calc ð”  â‰¤ #(Icc aâ‚ bâ‚) : continuum_le_mk_Icc hab
  ... â‰¤ #(Ioo a b) : mk_le_mk_of_subset (Icc_subset_Ioo ha hb)
end

lemma continuum_le_mk_Ico (h : a < b) : ð”  â‰¤ #(Ico a b) :=
(continuum_le_mk_Ioo h).trans (mk_le_mk_of_subset Ioo_subset_Ico_self)

lemma continuum_le_mk_Ioc (h : a < b) : ð”  â‰¤ #(Ioc a b) :=
(continuum_le_mk_Ioo h).trans (mk_le_mk_of_subset Ioo_subset_Ioc_self)

lemma continuum_le_mk_Ioi' (h : (Ioi a).nonempty) : ð”  â‰¤ #(Ioi a) :=
exists.elim h $ Î» b hb, (continuum_le_mk_Ioo hb).trans $ mk_le_mk_of_subset Ioo_subset_Ioi_self

lemma continuum_le_mk_Ioi [no_top_order Î±] (a : Î±) : ð”  â‰¤ #(Ioi a) :=
continuum_le_mk_Ioi' (no_top a)

lemma continuum_le_mk_Ici' (h : (Ioi a).nonempty) : ð”  â‰¤ #(Ici a) :=
(continuum_le_mk_Ioi' h).trans $ mk_le_mk_of_subset Ioi_subset_Ici_self

lemma continuum_le_mk_Ici [no_top_order Î±] (a : Î±) : ð”  â‰¤ #(Ici a) :=
continuum_le_mk_Ici' (no_top a)

lemma continuum_le_mk_Iio' (h : (Iio a).nonempty) : ð”  â‰¤ #(Iio a) :=
@continuum_le_mk_Ioi' (order_dual Î±) _ _ a h

lemma continuum_le_mk_Iio [no_bot_order Î±] (a : Î±) : ð”  â‰¤ #(Iio a) :=
@continuum_le_mk_Ioi (order_dual Î±) _ _ _ a

lemma continuum_le_mk_Iic' (h : (Iio a).nonempty) : ð”  â‰¤ #(Iic a) :=
@continuum_le_mk_Ici' (order_dual Î±) _ _ a h

lemma continuum_le_mk_Iic [no_bot_order Î±] (a : Î±) : ð”  â‰¤ #(Iic a) :=
@continuum_le_mk_Ici (order_dual Î±) _ _ _ a

variable (Î±)

lemma continuum_le_mk [nontrivial Î±] : ð”  â‰¤ #Î± :=
let âŸ¨a, b, hâŸ© := exists_lt_of_inf Î± in (continuum_le_mk_Icc h).trans $ mk_set_le _

end

variables [has_card_continuum Î±] {a b : Î±}

@[simp] lemma mk_Icc_eq_continuum (h : a < b) : #(Icc a b) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Icc h)

@[simp] lemma mk_Ico_eq_continuum (h : a < b) : #(Ico a b) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Ico h)

@[simp] lemma mk_Ioc_eq_continuum (h : a < b) : #(Ioc a b) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Ioc h)

@[simp] lemma mk_Ioo_eq_continuum (h : a < b) : #(Ioo a b) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Ioo h)

lemma mk_Ici_eq_continuum' (h : (Ioi a).nonempty) : #(Ici a) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Ici' h)

@[simp] lemma mk_Ici_eq_continuum [no_top_order Î±] (a : Î±) : #(Ici a) = ð”  :=
mk_Ici_eq_continuum' (no_top a)

lemma mk_Ioi_eq_continuum' (h : (Ioi a).nonempty) : #(Ioi a) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Ioi' h)

@[simp] lemma mk_Ioi_eq_continuum [no_top_order Î±] (a : Î±) : #(Ioi a) = ð”  :=
mk_Ioi_eq_continuum' (no_top a)

lemma mk_Iic_eq_continuum' (h : (Iio a).nonempty) : #(Iic a) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Iic' h)

@[simp] lemma mk_Iic_eq_continuum [no_bot_order Î±] (a : Î±) : #(Iic a) = ð”  :=
mk_Iic_eq_continuum' (no_bot a)

lemma mk_Iio_eq_continuum' (h : (Iio a).nonempty) : #(Iio a) = ð”  :=
le_antisymm ((mk_set_le _).trans_eq $ mk_eq_continuum Î±) (continuum_le_mk_Iio' h)

@[simp] lemma mk_Iio_eq_continuum [no_bot_order Î±] (a : Î±) : #(Iio a) = ð”  :=
mk_Iio_eq_continuum' (no_bot a)

end cardinal
