/-
Copyright (c) 2019 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import set_theory.continuum
import analysis.specific_limits
import data.rat.denumerable
import data.set.intervals.image_preimage

/-!
# The cardinality of the reals

This file shows that the real numbers have cardinality continuum, i.e. `#‚Ñù = ùî†`.

We show that `#‚Ñù ‚â§ ùî†` by noting that every real number is determined by a Cauchy-sequence of the
form `‚Ñï ‚Üí ‚Ñö`, which has cardinality `ùî†`. To show that `#‚Ñù ‚â• ùî†` we define an injection from
`{0, 1} ^ ‚Ñï` to `‚Ñù` with `f ‚Ü¶ Œ£ n, f n * (1 / 3) ^ n`.

We conclude that all intervals with distinct endpoints have cardinality continuum.

## Main definitions

* `cardinal.cantor_function` is the function that sends `f` in `{0, 1} ^ ‚Ñï` to `‚Ñù` by
  `f ‚Ü¶ Œ£' n, f n * (1 / 3) ^ n`

## Main statements

* `cardinal.mk_real : #‚Ñù = ùî†`: the reals have cardinality continuum.
* `cardinal.not_countable_real`: the universal set of real numbers is not countable.
  We can use this same proof to show that all the other sets in this file are not countable.
* 8 lemmas of the form `mk_Ixy_real` for `x,y ‚àà {i,o,c}` state that intervals on the reals
  have cardinality continuum.

## Notation

* `ùî†` : notation for `cardinal.continuum` in locale `cardinal`, defined in `set_theory.continuum`.

## Tags
continuum, cardinality, reals, cardinality of the reals
-/

open nat set
open_locale cardinal
noncomputable theory

namespace cardinal

variables {c : ‚Ñù} {f g : ‚Ñï ‚Üí bool} {n : ‚Ñï}

/-- The body of the sum in `cantor_function`.
`cantor_function_aux c f n = c ^ n` if `f n = tt`;
`cantor_function_aux c f n = 0` if `f n = ff`. -/
def cantor_function_aux (c : ‚Ñù) (f : ‚Ñï ‚Üí bool) (n : ‚Ñï) : ‚Ñù := cond (f n) (c ^ n) 0

@[simp] lemma cantor_function_aux_tt (h : f n = tt) : cantor_function_aux c f n = c ^ n :=
by simp [cantor_function_aux, h]

@[simp] lemma cantor_function_aux_ff (h : f n = ff) : cantor_function_aux c f n = 0 :=
by simp [cantor_function_aux, h]

lemma cantor_function_aux_nonneg (h : 0 ‚â§ c) : 0 ‚â§ cantor_function_aux c f n :=
by { cases h' : f n; simp [h'], apply pow_nonneg h }

lemma cantor_function_aux_eq (h : f n = g n) :
  cantor_function_aux c f n = cantor_function_aux c g n :=
by simp [cantor_function_aux, h]

lemma cantor_function_aux_succ (f : ‚Ñï ‚Üí bool) :
  (Œª n, cantor_function_aux c f (n + 1)) = Œª n, c * cantor_function_aux c (Œª n, f (n + 1)) n :=
by { ext n, cases h : f (n + 1); simp [h, pow_succ] }

lemma summable_cantor_function (f : ‚Ñï ‚Üí bool) (h1 : 0 ‚â§ c) (h2 : c < 1) :
  summable (cantor_function_aux c f) :=
begin
  apply (summable_geometric_of_lt_1 h1 h2).summable_of_eq_zero_or_self,
  intro n, cases h : f n; simp [h]
end

/-- `cantor_function c (f : ‚Ñï ‚Üí bool)` is `Œ£ n, f n * c ^ n`, where `tt` is interpreted as `1` and
`ff` is interpreted as `0`. It is implemented using `cantor_function_aux`. -/
def cantor_function (c : ‚Ñù) (f : ‚Ñï ‚Üí bool) : ‚Ñù := ‚àë' n, cantor_function_aux c f n

lemma cantor_function_le (h1 : 0 ‚â§ c) (h2 : c < 1) (h3 : ‚àÄ n, f n ‚Üí g n) :
  cantor_function c f ‚â§ cantor_function c g :=
begin
  apply tsum_le_tsum _ (summable_cantor_function f h1 h2) (summable_cantor_function g h1 h2),
  intro n, cases h : f n, simp [h, cantor_function_aux_nonneg h1],
  replace h3 : g n = tt := h3 n h, simp [h, h3]
end

lemma cantor_function_succ (f : ‚Ñï ‚Üí bool) (h1 : 0 ‚â§ c) (h2 : c < 1) :
  cantor_function c f = cond (f 0) 1 0 + c * cantor_function c (Œª n, f (n+1)) :=
begin
  rw [cantor_function, tsum_eq_zero_add (summable_cantor_function f h1 h2)],
  rw [cantor_function_aux_succ, tsum_mul_left, cantor_function_aux, pow_zero],
  refl
end

/-- `cantor_function c` is strictly increasing with if `0 < c < 1/2`, if we endow `‚Ñï ‚Üí bool` with a
lexicographic order. The lexicographic order doesn't exist for these infinitary products, so we
explicitly write out what it means. -/
lemma increasing_cantor_function (h1 : 0 < c) (h2 : c < 1 / 2) {n : ‚Ñï} {f g : ‚Ñï ‚Üí bool}
  (hn : ‚àÄ(k < n), f k = g k) (fn : f n = ff) (gn : g n = tt) :
  cantor_function c f < cantor_function c g :=
begin
  have h3 : c < 1, { apply h2.trans, norm_num },
  induction n with n ih generalizing f g,
  { let f_max : ‚Ñï ‚Üí bool := Œª n, nat.rec ff (Œª _ _, tt) n,
    have hf_max : ‚àÄn, f n ‚Üí f_max n,
    { intros n hn, cases n, rw [fn] at hn, contradiction, apply rfl },
    let g_min : ‚Ñï ‚Üí bool := Œª n, nat.rec tt (Œª _ _, ff) n,
    have hg_min : ‚àÄn, g_min n ‚Üí g n,
    { intros n hn, cases n, rw [gn], apply rfl, contradiction },
    apply (cantor_function_le (le_of_lt h1) h3 hf_max).trans_lt,
    refine lt_of_lt_of_le _ (cantor_function_le (le_of_lt h1) h3 hg_min),
    have : c / (1 - c) < 1,
    { rw [div_lt_one, lt_sub_iff_add_lt],
      { convert add_lt_add h2 h2, norm_num },
      rwa sub_pos },
    convert this,
    { rw [cantor_function_succ _ (le_of_lt h1) h3, div_eq_mul_inv,
          ‚Üêtsum_geometric_of_lt_1 (le_of_lt h1) h3],
      apply zero_add },
    { convert tsum_eq_single 0 _,
      { apply_instance },
      { intros n hn, cases n, contradiction, refl } } },
  rw [cantor_function_succ f (le_of_lt h1) h3, cantor_function_succ g (le_of_lt h1) h3],
  rw [hn 0 $ zero_lt_succ n],
  apply add_lt_add_left, rw mul_lt_mul_left h1, exact ih (Œª k hk, hn _ $ succ_lt_succ hk) fn gn
end

/-- `cantor_function c` is injective if `0 < c < 1/2`. -/
lemma cantor_function_injective (h1 : 0 < c) (h2 : c < 1 / 2) :
  function.injective (cantor_function c) :=
begin
  intros f g hfg, classical, by_contra h, revert hfg,
  have : ‚àÉn, f n ‚â† g n,
  { rw [‚Üênot_forall], intro h', apply h, ext, apply h' },
  let n := nat.find this,
  have hn : ‚àÄ (k : ‚Ñï), k < n ‚Üí f k = g k,
  { intros k hk, apply of_not_not, exact nat.find_min this hk },
  cases fn : f n,
  { apply ne_of_lt, refine increasing_cantor_function h1 h2 hn fn _,
    apply eq_tt_of_not_eq_ff, rw [‚Üêfn], apply ne.symm, exact nat.find_spec this },
  { apply ne_of_gt, refine increasing_cantor_function h1 h2 (Œª k hk, (hn k hk).symm) _ fn,
    apply eq_ff_of_not_eq_tt, rw [‚Üêfn], apply ne.symm, exact nat.find_spec this }
end

/-- The cardinality of the reals, as a type. -/
lemma mk_real : #‚Ñù = ùî† :=
begin
  apply le_antisymm,
  { rw real.equiv_Cauchy.cardinal_eq,
    apply mk_quotient_le.trans, apply (mk_subtype_le _).trans_eq,
    rw [‚Üê power_def, mk_nat, mk_rat, omega_power_omega] },
  { convert mk_le_of_injective (cantor_function_injective _ _),
    rw [‚Üêpower_def, mk_bool, mk_nat, two_power_omega], exact 1 / 3, norm_num, norm_num }
end

/-- The cardinality of the reals, as a set. -/
lemma mk_univ_real : #(set.univ : set ‚Ñù) = ùî† :=
by rw [mk_univ, mk_real]

/-- **Non-Denumerability of the Continuum**: The reals are not countable. -/
lemma not_countable_real : ¬¨ countable (set.univ : set ‚Ñù) :=
by { rw [‚Üê mk_set_le_omega, not_le, mk_univ_real], apply cantor }

/-- The cardinality of the interval (a, ‚àû). -/
lemma mk_Ioi_real (a : ‚Ñù) : #(Ioi a) = ùî† :=
begin
  refine le_antisymm (mk_real ‚ñ∏ mk_set_le _) _,
  rw [‚Üê not_lt], intro h,
  refine ne_of_lt _ mk_univ_real,
  have hu : Iio a ‚à™ {a} ‚à™ Ioi a = set.univ,
  { convert Iic_union_Ioi, exact Iio_union_right },
  rw ‚Üê hu,
  refine lt_of_le_of_lt (mk_union_le _ _) _,
  refine lt_of_le_of_lt (add_le_add_right (mk_union_le _ _) _) _,
  have h2 : (Œª x, a + a - x) '' Ioi a = Iio a,
  { convert image_const_sub_Ioi _ _, simp },
  rw ‚Üê h2,
  refine add_lt_of_lt (cantor _).le _ h,
  refine add_lt_of_lt (cantor _).le (mk_image_le.trans_lt h) _,
  rw mk_singleton,
  exact one_lt_omega.trans (cantor _)
end

/-- The cardinality of the interval [a, ‚àû). -/
lemma mk_Ici_real (a : ‚Ñù) : #(Ici a) = ùî† :=
le_antisymm (mk_real ‚ñ∏ mk_set_le _) (mk_Ioi_real a ‚ñ∏ mk_le_mk_of_subset Ioi_subset_Ici_self)

/-- The cardinality of the interval (-‚àû, a). -/
lemma mk_Iio_real (a : ‚Ñù) : #(Iio a) = ùî† :=
begin
  refine le_antisymm (mk_real ‚ñ∏ mk_set_le _) _,
  have h2 : (Œª x, a + a - x) '' Iio a = Ioi a,
  { convert image_const_sub_Iio _ _, simp },
  exact mk_Ioi_real a ‚ñ∏ h2 ‚ñ∏ mk_image_le
end

/-- The cardinality of the interval (-‚àû, a]. -/
lemma mk_Iic_real (a : ‚Ñù) : #(Iic a) = ùî† :=
le_antisymm (mk_real ‚ñ∏ mk_set_le _) (mk_Iio_real a ‚ñ∏ mk_le_mk_of_subset Iio_subset_Iic_self)

/-- The cardinality of the interval (a, b). -/
lemma mk_Ioo_real {a b : ‚Ñù} (h : a < b) : #(Ioo a b) = ùî† :=
begin
  refine le_antisymm (mk_real ‚ñ∏ mk_set_le _) _,
  have h1 : #((Œª x, x - a) '' Ioo a b) ‚â§ #(Ioo a b) := mk_image_le,
  refine le_trans _ h1,
  rw [image_sub_const_Ioo, sub_self],
  replace h := sub_pos_of_lt h,
  have h2 : #(has_inv.inv '' Ioo 0 (b - a)) ‚â§ #(Ioo 0 (b - a)) := mk_image_le,
  refine le_trans _ h2,
  rw [image_inv_Ioo_0_left h, mk_Ioi_real]
end

/-- The cardinality of the interval [a, b). -/
lemma mk_Ico_real {a b : ‚Ñù} (h : a < b) : #(Ico a b) = ùî† :=
le_antisymm (mk_real ‚ñ∏ mk_set_le _) (mk_Ioo_real h ‚ñ∏ mk_le_mk_of_subset Ioo_subset_Ico_self)

/-- The cardinality of the interval [a, b]. -/
lemma mk_Icc_real {a b : ‚Ñù} (h : a < b) : #(Icc a b) = ùî† :=
le_antisymm (mk_real ‚ñ∏ mk_set_le _) (mk_Ioo_real h ‚ñ∏ mk_le_mk_of_subset Ioo_subset_Icc_self)

/-- The cardinality of the interval (a, b]. -/
lemma mk_Ioc_real {a b : ‚Ñù} (h : a < b) : #(Ioc a b) = ùî† :=
le_antisymm (mk_real ‚ñ∏ mk_set_le _) (mk_Ioo_real h ‚ñ∏ mk_le_mk_of_subset Ioo_subset_Ioc_self)

end cardinal
