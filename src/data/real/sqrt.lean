/-
Copyright (c) 2020 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mario Carneiro, Floris van Doorn, Yury Kudryashov
-/
import topology.instances.nnreal

/-!
# Square root of a real number

In this file we define

* `nnreal.sqrt` to be the square root of a nonnegative real number.
* `real.sqrt` to be the square root of a real number, defined to be zero on negative numbers.

Then we prove some basic properties of these functions.

## Implementation notes

We define `nnreal.sqrt` as the noncomputable inverse to the function `x â†¦ x * x`. We use general
theory of inverses of strictly monotone functions to prove that `nnreal.sqrt x` exists. As a side
effect, `nnreal.sqrt` is a bundled `order_iso`, so for `nnreal` numbers we get continuity as well as
theorems like `sqrt x â‰¤ y â†” x * x â‰¤ y` for free.

Then we define `real.sqrt x` to be `nnreal.sqrt (real.to_nnreal x)`. We also define a Cauchy
sequence `real.sqrt_aux (f : cau_seq â„š abs)` which converges to `sqrt (mk f)` but do not prove (yet)
that this sequence actually converges to `sqrt (mk f)`.

## Tags

square root
-/

open set filter
open_locale filter nnreal topological_space

namespace nnreal

variables {x y : â„â‰¥0}

/-- Square root of a nonnegative real number. -/
@[pp_nodot] noncomputable def sqrt : â„â‰¥0 â‰ƒo â„â‰¥0 :=
order_iso.symm $ strict_mono.order_iso_of_surjective (Î» x, x * x)
  (Î» x y h, mul_self_lt_mul_self x.2 h) $
  (continuous_id.mul continuous_id).surjective tendsto_mul_self_at_top $
    by simp [order_bot.at_bot_eq]

lemma sqrt_eq_iff_sq_eq : sqrt x = y â†” y * y = x :=
sqrt.to_equiv.apply_eq_iff_eq_symm_apply.trans eq_comm

lemma sqrt_le_iff : sqrt x â‰¤ y â†” x â‰¤ y * y :=
sqrt.to_galois_connection _ _

lemma le_sqrt_iff : x â‰¤ sqrt y â†” x * x â‰¤ y :=
(sqrt.symm.to_galois_connection _ _).symm

@[simp] lemma sqrt_eq_zero : sqrt x = 0 â†” x = 0 :=
sqrt_eq_iff_sq_eq.trans $ by rw [eq_comm, zero_mul]

@[simp] lemma sqrt_zero : sqrt 0 = 0 := sqrt_eq_zero.2 rfl

@[simp] lemma sqrt_one : sqrt 1 = 1 := sqrt_eq_iff_sq_eq.2 $ mul_one 1

@[simp] lemma mul_sqrt_self (x : â„â‰¥0) : sqrt x * sqrt x = x :=
sqrt.symm_apply_apply x

@[simp] lemma sqrt_mul_self (x : â„â‰¥0) : sqrt (x * x) = x := sqrt.apply_symm_apply x

lemma sqrt_mul (x y : â„â‰¥0) : sqrt (x * y) = sqrt x * sqrt y :=
by rw [sqrt_eq_iff_sq_eq, mul_mul_mul_comm, mul_sqrt_self, mul_sqrt_self]

/-- `nnreal.sqrt` as a `monoid_with_zero_hom`. -/
noncomputable def sqrt_hom : monoid_with_zero_hom â„â‰¥0 â„â‰¥0 := âŸ¨sqrt, sqrt_zero, sqrt_one, sqrt_mulâŸ©

lemma sqrt_inv (x : â„â‰¥0) : sqrt (xâ»Â¹) = (sqrt x)â»Â¹ := sqrt_hom.map_inv' x

lemma sqrt_div (x y : â„â‰¥0) : sqrt (x / y) = sqrt x / sqrt y := sqrt_hom.map_div x y

lemma continuous_sqrt : continuous sqrt := sqrt.continuous

end nnreal

namespace real

/-- An auxiliary sequence of rational numbers that converges to `real.sqrt (mk f)`.
Currently this sequence is not used in `mathlib`.  -/
def sqrt_aux (f : cau_seq â„š abs) : â„• â†’ â„š
| 0       := rat.mk_nat (f 0).num.to_nat.sqrt (f 0).denom.sqrt
| (n + 1) := let s := sqrt_aux n in max 0 $ (s + f (n+1) / s) / 2

theorem sqrt_aux_nonneg (f : cau_seq â„š abs) : âˆ€ i : â„•, 0 â‰¤ sqrt_aux f i
| 0       := by rw [sqrt_aux, rat.mk_nat_eq, rat.mk_eq_div];
  apply div_nonneg; exact int.cast_nonneg.2 (int.of_nat_nonneg _)
| (n + 1) := le_max_left _ _

/- TODO(Mario): finish the proof
theorem sqrt_aux_converges (f : cau_seq â„š abs) : âˆƒ h x, 0 â‰¤ x âˆ§ x * x = max 0 (mk f) âˆ§
  mk âŸ¨sqrt_aux f, hâŸ© = x :=
begin
  rcases sqrt_exists (le_max_left 0 (mk f)) with âŸ¨x, x0, hxâŸ©,
  suffices : âˆƒ h, mk âŸ¨sqrt_aux f, hâŸ© = x,
  { exact this.imp (Î» h e, âŸ¨x, x0, hx, eâŸ©) },
  apply of_near,

  suffices : âˆƒ Î´ > 0, âˆ€ i, abs (â†‘(sqrt_aux f i) - x) < Î´ / 2 ^ i,
  { rcases this with âŸ¨Î´, Î´0, hÎ´âŸ©,
    intros }
end -/

/-- The square root of a real number. This returns 0 for negative inputs. -/
@[pp_nodot] noncomputable def sqrt (x : â„) : â„ :=
nnreal.sqrt (real.to_nnreal x)
/-quotient.lift_on x
  (Î» f, mk âŸ¨sqrt_aux f, (sqrt_aux_converges f).fstâŸ©)
  (Î» f g e, begin
    rcases sqrt_aux_converges f with âŸ¨hf, x, x0, xf, xsâŸ©,
    rcases sqrt_aux_converges g with âŸ¨hg, y, y0, yg, ysâŸ©,
    refine xs.trans (eq.trans _ ys.symm),
    rw [â† @mul_self_inj_of_nonneg â„ _ x y x0 y0, xf, yg],
    congr' 1, exact quotient.sound e
  end)-/

variables {x y : â„}

@[continuity]
lemma continuous_sqrt : continuous sqrt :=
nnreal.continuous_coe.comp $ nnreal.sqrt.continuous.comp nnreal.continuous_of_real

theorem sqrt_eq_zero_of_nonpos (h : x â‰¤ 0) : sqrt x = 0 :=
by simp [sqrt, real.to_nnreal_eq_zero.2 h]

theorem sqrt_nonneg (x : â„) : 0 â‰¤ sqrt x := nnreal.coe_nonneg _

@[simp] theorem mul_self_sqrt (h : 0 â‰¤ x) : sqrt x * sqrt x = x :=
by simp [sqrt, â† nnreal.coe_mul, real.coe_to_nnreal _ h]

@[simp] theorem sqrt_mul_self (h : 0 â‰¤ x) : sqrt (x * x) = x :=
(mul_self_inj_of_nonneg (sqrt_nonneg _) h).1 (mul_self_sqrt (mul_self_nonneg _))

theorem sqrt_eq_iff_mul_self_eq (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) :
  sqrt x = y â†” y * y = x :=
âŸ¨Î» h, by rw [â† h, mul_self_sqrt hx], Î» h, by rw [â† h, sqrt_mul_self hy]âŸ©

@[simp] theorem sq_sqrt (h : 0 â‰¤ x) : sqrt x ^ 2 = x :=
by rw [sq, mul_self_sqrt h]

@[simp] theorem sqrt_sq (h : 0 â‰¤ x) : sqrt (x ^ 2) = x :=
by rw [sq, sqrt_mul_self h]

theorem sqrt_eq_iff_sq_eq (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) :
  sqrt x = y â†” y ^ 2 = x :=
by rw [sq, sqrt_eq_iff_mul_self_eq hx hy]

theorem sqrt_mul_self_eq_abs (x : â„) : sqrt (x * x) = abs x :=
by rw [â† abs_mul_abs_self x, sqrt_mul_self (abs_nonneg _)]

theorem sqrt_sq_eq_abs (x : â„) : sqrt (x ^ 2) = abs x :=
by rw [sq, sqrt_mul_self_eq_abs]

@[simp] theorem sqrt_zero : sqrt 0 = 0 := by simp [sqrt]

@[simp] theorem sqrt_one : sqrt 1 = 1 := by simp [sqrt]

@[simp] theorem sqrt_le (hy : 0 â‰¤ y) : sqrt x â‰¤ sqrt y â†” x â‰¤ y :=
by simp [sqrt, real.to_nnreal_le_to_nnreal_iff, *]

@[simp] theorem sqrt_lt (hx : 0 â‰¤ x) : sqrt x < sqrt y â†” x < y :=
lt_iff_lt_of_le_iff_le (sqrt_le hx)

theorem sqrt_le_sqrt (h : x â‰¤ y) : sqrt x â‰¤ sqrt y :=
by simp [sqrt, real.to_nnreal_le_to_nnreal h]

theorem sqrt_le_left (hy : 0 â‰¤ y) : sqrt x â‰¤ y â†” x â‰¤ y ^ 2 :=
by rw [sqrt, â† real.le_to_nnreal_iff_coe_le hy, nnreal.sqrt_le_iff, â† real.to_nnreal_mul hy,
  real.to_nnreal_le_to_nnreal_iff (mul_self_nonneg y), sq]

theorem sqrt_le_iff : sqrt x â‰¤ y â†” 0 â‰¤ y âˆ§ x â‰¤ y ^ 2 :=
begin
  rw [â† and_iff_right_of_imp (Î» h, (sqrt_nonneg x).trans h), and.congr_right_iff],
  exact sqrt_le_left
end

/- note: if you want to conclude `x â‰¤ sqrt y`, then use `le_sqrt_of_sq_le`.
   if you have `x > 0`, consider using `le_sqrt'` -/
theorem le_sqrt (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) : x â‰¤ sqrt y â†” x ^ 2 â‰¤ y :=
by rw [mul_self_le_mul_self_iff hx (sqrt_nonneg _), sq, mul_self_sqrt hy]

theorem le_sqrt' (hx : 0 < x) : x â‰¤ sqrt y â†” x ^ 2 â‰¤ y :=
by { rw [sqrt, â† nnreal.coe_mk x hx.le, nnreal.coe_le_coe, nnreal.le_sqrt_iff,
  real.le_to_nnreal_iff_coe_le', sq, nnreal.coe_mul], exact mul_pos hx hx }

theorem abs_le_sqrt (h : x^2 â‰¤ y) : abs x â‰¤ sqrt y :=
by rw â† sqrt_sq_eq_abs; exact sqrt_le_sqrt h

theorem sq_le (h : 0 â‰¤ y) : x^2 â‰¤ y â†” -sqrt y â‰¤ x âˆ§ x â‰¤ sqrt y :=
begin
  split,
  { simpa only [abs_le] using abs_le_sqrt },
  { rw [â† abs_le, â† sq_abs],
    exact (le_sqrt (abs_nonneg x) h).mp },
end

theorem neg_sqrt_le_of_sq_le (h : x^2 â‰¤ y) : -sqrt y â‰¤ x :=
((sq_le ((sq_nonneg x).trans h)).mp h).1

theorem le_sqrt_of_sq_le (h : x^2 â‰¤ y) : x â‰¤ sqrt y :=
((sq_le ((sq_nonneg x).trans h)).mp h).2

@[simp] theorem sqrt_inj (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) : sqrt x = sqrt y â†” x = y :=
by simp [le_antisymm_iff, hx, hy]

@[simp] theorem sqrt_eq_zero (h : 0 â‰¤ x) : sqrt x = 0 â†” x = 0 :=
by simpa using sqrt_inj h (le_refl _)

theorem sqrt_eq_zero' : sqrt x = 0 â†” x â‰¤ 0 :=
by rw [sqrt, nnreal.coe_eq_zero, nnreal.sqrt_eq_zero, real.to_nnreal_eq_zero]

theorem sqrt_ne_zero (h : 0 â‰¤ x) : sqrt x â‰  0 â†” x â‰  0 :=
by rw [not_iff_not, sqrt_eq_zero h]

theorem sqrt_ne_zero' : sqrt x â‰  0 â†” 0 < x :=
by rw [â† not_le, not_iff_not, sqrt_eq_zero']

@[simp] theorem sqrt_pos : 0 < sqrt x â†” 0 < x :=
lt_iff_lt_of_le_iff_le (iff.trans
  (by simp [le_antisymm_iff, sqrt_nonneg]) sqrt_eq_zero')

@[simp] theorem sqrt_mul (hx : 0 â‰¤ x) (y : â„) : sqrt (x * y) = sqrt x * sqrt y :=
by simp_rw [sqrt, â† nnreal.coe_mul, nnreal.coe_eq, real.to_nnreal_mul hx, nnreal.sqrt_mul]

@[simp] theorem sqrt_mul' (x) {y : â„} (hy : 0 â‰¤ y) : sqrt (x * y) = sqrt x * sqrt y :=
by rw [mul_comm, sqrt_mul hy, mul_comm]

@[simp] theorem sqrt_inv (x : â„) : sqrt xâ»Â¹ = (sqrt x)â»Â¹ :=
by rw [sqrt, real.to_nnreal_inv, nnreal.sqrt_inv, nnreal.coe_inv, sqrt]

@[simp] theorem sqrt_div (hx : 0 â‰¤ x) (y : â„) : sqrt (x / y) = sqrt x / sqrt y :=
by rw [division_def, sqrt_mul hx, sqrt_inv, division_def]

@[simp] theorem div_sqrt : x / sqrt x = sqrt x :=
begin
  cases le_or_lt x 0,
  { rw [sqrt_eq_zero'.mpr h, div_zero] },
  { rw [div_eq_iff (sqrt_ne_zero'.mpr h), mul_self_sqrt h.le] },
end

theorem lt_sqrt (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) : x < sqrt y â†” x ^ 2 < y :=
by rw [mul_self_lt_mul_self_iff hx (sqrt_nonneg y), sq, mul_self_sqrt hy]

theorem sq_lt : x^2 < y â†” -sqrt y < x âˆ§ x < sqrt y :=
begin
  split,
  { simpa only [â† sqrt_lt (sq_nonneg x), sqrt_sq_eq_abs] using abs_lt.mp },
  { rw [â† abs_lt, â† sq_abs],
    exact Î» h, (lt_sqrt (abs_nonneg x) (sqrt_pos.mp (lt_of_le_of_lt (abs_nonneg x) h)).le).mp h },
end

theorem neg_sqrt_lt_of_sq_lt (h : x^2 < y) : -sqrt y < x := (sq_lt.mp h).1

theorem lt_sqrt_of_sq_lt (h : x^2 < y) : x < sqrt y := (sq_lt.mp h).2

end real

open real

variables {Î± : Type*}

lemma filter.tendsto.sqrt {f : Î± â†’ â„} {l : filter Î±} {x : â„} (h : tendsto f l (ð“ x)) :
  tendsto (Î» x, sqrt (f x)) l (ð“ (sqrt x)) :=
(continuous_sqrt.tendsto _).comp h

variables [topological_space Î±] {f : Î± â†’ â„} {s : set Î±} {x : Î±}

lemma continuous_within_at.sqrt (h : continuous_within_at f s x) :
  continuous_within_at (Î» x, sqrt (f x)) s x :=
h.sqrt

lemma continuous_at.sqrt (h : continuous_at f x) : continuous_at (Î» x, sqrt (f x)) x := h.sqrt

lemma continuous_on.sqrt (h : continuous_on f s) : continuous_on (Î» x, sqrt (f x)) s :=
Î» x hx, (h x hx).sqrt

@[continuity]
lemma continuous.sqrt (h : continuous f) : continuous (Î» x, sqrt (f x)) := continuous_sqrt.comp h
