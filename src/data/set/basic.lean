/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura
-/
import order.symm_diff
import logic.function.iterate
import data.set.n_ary

/-!
# Basic properties of sets

Sets in Lean are homogeneous; all their elements have the same type. Sets whose elements
have type `X` are thus defined as `set X := X â†’ Prop`. Note that this function need not
be decidable. The definition is in the core library.

This file provides some basic definitions related to sets and functions not present in the core
library, as well as extra lemmas for functions in the core library (empty set, univ, union,
intersection, insert, singleton, set-theoretic difference, complement, and powerset).

Note that a set is a term, not a type. There is a coercion from `set Î±` to `Type*` sending
`s` to the corresponding subtype `â†¥s`.

See also the file `set_theory/zfc.lean`, which contains an encoding of ZFC set theory in Lean.

## Main definitions

Notation used here:

-  `f : Î± â†’ Î²` is a function,

-  `s : set Î±` and `sâ‚ sâ‚‚ : set Î±` are subsets of `Î±`

-  `t : set Î²` is a subset of `Î²`.

Definitions in the file:

* `nonempty s : Prop` : the predicate `s â‰  âˆ…`. Note that this is the preferred way to express the
  fact that `s` has an element (see the Implementation Notes).

* `preimage f t : set Î±` : the preimage fâ»Â¹(t) (written `f â»Â¹' t` in Lean) of a subset of Î².

* `subsingleton s : Prop` : the predicate saying that `s` has at most one element.

* `nontrivial s : Prop` : the predicate saying that `s` has at least two distinct elements.

* `range f : set Î²` : the image of `univ` under `f`.
  Also works for `{p : Prop} (f : p â†’ Î±)` (unlike `image`)

* `inclusion sâ‚ sâ‚‚ : â†¥sâ‚ â†’ â†¥sâ‚‚` : the map `â†¥sâ‚ â†’ â†¥sâ‚‚` induced by an inclusion `sâ‚ âŠ† sâ‚‚`.

## Notation

* `f â»Â¹' t` for `preimage f t`

* `f '' s` for `image f s`

* `sá¶œ` for the complement of `s`

## Implementation notes

* `s.nonempty` is to be preferred to `s â‰  âˆ…` or `âˆƒ x, x âˆˆ s`. It has the advantage that
the `s.nonempty` dot notation can be used.

* For `s : set Î±`, do not use `subtype s`. Instead use `â†¥s` or `(s : Type*)` or `s`.

## Tags

set, sets, subset, subsets, image, preimage, pre-image, range, union, intersection, insert,
singleton, complement, powerset

-/

/-! ### Set coercion to a type -/

open function

universes u v w x

namespace set

variables {Î± : Type*} {s t : set Î±}

instance {Î± : Type*} : boolean_algebra (set Î±) :=
{ sup := Î» s t, {x | x âˆˆ s âˆ¨ x âˆˆ t},
  le  := (â‰¤),
  lt  := Î» s t, s âŠ† t âˆ§ Â¬t âŠ† s,
  inf := Î» s t, {x | x âˆˆ s âˆ§ x âˆˆ t},
  bot := âˆ…,
  compl := Î» s, {x | x âˆ‰ s},
  top := univ,
  sdiff := Î» s t, {x | x âˆˆ s âˆ§ x âˆ‰ t},
  .. (infer_instance : boolean_algebra (Î± â†’ Prop)) }

@[simp] lemma top_eq_univ : (âŠ¤ : set Î±) = univ := rfl
@[simp] lemma bot_eq_empty : (âŠ¥ : set Î±) = âˆ… := rfl
@[simp] lemma sup_eq_union : ((âŠ”) : set Î± â†’ set Î± â†’ set Î±) = (âˆª) := rfl
@[simp] lemma inf_eq_inter : ((âŠ“) : set Î± â†’ set Î± â†’ set Î±) = (âˆ©) := rfl

end set

/-- Duplicate of `eq.subset'`, which currently has elaboration problems. -/
lemma eq.subset {Î±} {s t : set Î±} : s = t â†’ s âŠ† t := eq.subset'

namespace set

variables {Î± : Type u} {Î² : Type v} {Î³ : Type w} {Î¹ : Sort x} {a b : Î±} {s t u : set Î±}

/-! ### Lemmas about union -/

@[simp] theorem union_eq_left_iff_subset {s t : set Î±} : s âˆª t = s â†” t âŠ† s :=
sup_eq_left

@[simp] theorem union_eq_right_iff_subset {s t : set Î±} : s âˆª t = t â†” s âŠ† t :=
sup_eq_right

theorem union_eq_self_of_subset_left {s t : set Î±} (h : s âŠ† t) : s âˆª t = t :=
union_eq_right_iff_subset.mpr h

theorem union_eq_self_of_subset_right {s t : set Î±} (h : t âŠ† s) : s âˆª t = s :=
union_eq_left_iff_subset.mpr h

lemma union_congr_left (ht : t âŠ† s âˆª u) (hu : u âŠ† s âˆª t) : s âˆª t = s âŠ” u := sup_congr_left ht hu
lemma union_congr_right (hs : s âŠ† t âˆª u) (ht : t âŠ† s âˆª u) : s âˆª u = t âˆª u := sup_congr_right hs ht

lemma union_eq_union_iff_left : s âˆª t = s âˆª u â†” t âŠ† s âˆª u âˆ§ u âŠ† s âˆª t := sup_eq_sup_iff_left
lemma union_eq_union_iff_right : s âˆª u = t âˆª u â†” s âŠ† t âˆª u âˆ§ t âŠ† s âˆª u := sup_eq_sup_iff_right

@[simp] lemma union_univ {s : set Î±} : s âˆª univ = univ := sup_top_eq

@[simp] lemma univ_union {s : set Î±} : univ âˆª s = univ := top_sup_eq

/-! ### Lemmas about intersection -/

@[simp] theorem inter_eq_left_iff_subset {s t : set Î±} : s âˆ© t = s â†” s âŠ† t :=
inf_eq_left

@[simp] theorem inter_eq_right_iff_subset {s t : set Î±} : s âˆ© t = t â†” t âŠ† s :=
inf_eq_right

theorem inter_eq_self_of_subset_left {s t : set Î±} : s âŠ† t â†’ s âˆ© t = s :=
inter_eq_left_iff_subset.mpr

theorem inter_eq_self_of_subset_right {s t : set Î±} : t âŠ† s â†’ s âˆ© t = t :=
inter_eq_right_iff_subset.mpr

lemma inter_congr_left (ht : s âˆ© u âŠ† t) (hu : s âˆ© t âŠ† u) : s âˆ© t = s âˆ© u := inf_congr_left ht hu
lemma inter_congr_right (hs : t âˆ© u âŠ† s) (ht : s âˆ© u âŠ† t) : s âˆ© u = t âˆ© u := inf_congr_right hs ht

lemma inter_eq_inter_iff_left : s âˆ© t = s âˆ© u â†” s âˆ© u âŠ† t âˆ§ s âˆ© t âŠ† u := inf_eq_inf_iff_left
lemma inter_eq_inter_iff_right : s âˆ© u = t âˆ© u â†” t âˆ© u âŠ† s âˆ§ s âˆ© u âŠ† t := inf_eq_inf_iff_right

theorem union_inter_cancel_left {s t : set Î±} : (s âˆª t) âˆ© s = s :=
inter_eq_self_of_subset_right $ subset_union_left _ _

theorem union_inter_cancel_right {s t : set Î±} : (s âˆª t) âˆ© t = t :=
inter_eq_self_of_subset_right $ subset_union_right _ _

/-! ### Distributivity laws -/

theorem inter_distrib_left (s t u : set Î±) : s âˆ© (t âˆª u) = (s âˆ© t) âˆª (s âˆ© u) :=
inf_sup_left
theorem inter_union_distrib_left {s t u : set Î±} : s âˆ© (t âˆª u) = (s âˆ© t) âˆª (s âˆ© u) :=
inf_sup_left

theorem inter_distrib_right (s t u : set Î±) : (s âˆª t) âˆ© u = (s âˆ© u) âˆª (t âˆ© u) :=
inf_sup_right
theorem union_inter_distrib_right {s t u : set Î±} : (s âˆª t) âˆ© u = (s âˆ© u) âˆª (t âˆ© u) :=
inf_sup_right

theorem union_distrib_left (s t u : set Î±) : s âˆª (t âˆ© u) = (s âˆª t) âˆ© (s âˆª u) :=
sup_inf_left
theorem union_inter_distrib_left {s t u : set Î±} : s âˆª (t âˆ© u) = (s âˆª t) âˆ© (s âˆª u) :=
sup_inf_left

theorem union_distrib_right (s t u : set Î±) : (s âˆ© t) âˆª u = (s âˆª u) âˆ© (t âˆª u) :=
sup_inf_right
theorem inter_union_distrib_right {s t u : set Î±} : (s âˆ© t) âˆª u = (s âˆª u) âˆ© (t âˆª u) :=
sup_inf_right

lemma union_union_distrib_left (s t u : set Î±) : s âˆª (t âˆª u) = (s âˆª t) âˆª (s âˆª u) :=
sup_sup_distrib_left _ _ _

lemma union_union_distrib_right (s t u : set Î±) : (s âˆª t) âˆª u = (s âˆª u) âˆª (t âˆª u) :=
sup_sup_distrib_right _ _ _

lemma inter_inter_distrib_left (s t u : set Î±) : s âˆ© (t âˆ© u) = (s âˆ© t) âˆ© (s âˆ© u) :=
inf_inf_distrib_left _ _ _

lemma inter_inter_distrib_right (s t u : set Î±) : (s âˆ© t) âˆ© u = (s âˆ© u) âˆ© (t âˆ© u) :=
inf_inf_distrib_right _ _ _

lemma union_union_union_comm (s t u v : set Î±) : (s âˆª t) âˆª (u âˆª v) = (s âˆª u) âˆª (t âˆª v) :=
sup_sup_sup_comm _ _ _ _

lemma inter_inter_inter_comm (s t u v : set Î±) : (s âˆ© t) âˆ© (u âˆ© v) = (s âˆ© u) âˆ© (t âˆ© v) :=
inf_inf_inf_comm _ _ _ _

/-! ### Lemmas about sets defined as `{x âˆˆ s | p x}`. -/

section sep
variables {p q : Î± â†’ Prop} {x : Î±}

theorem sep_eq_of_subset (h : s âŠ† t) : {x âˆˆ t | x âˆˆ s} = s :=
inter_eq_self_of_subset_right h

@[simp] lemma sep_union : {x âˆˆ s âˆª t | p x} = {x âˆˆ s | p x} âˆª {x âˆˆ t | p x} :=
union_inter_distrib_right

@[simp] lemma sep_inter : {x âˆˆ s âˆ© t | p x} = {x âˆˆ s | p x} âˆ© {x âˆˆ t | p x} :=
inter_inter_distrib_right s t p

@[simp] lemma sep_and : {x âˆˆ s | p x âˆ§ q x} = {x âˆˆ s | p x} âˆ© {x âˆˆ s | q x} :=
inter_inter_distrib_left s p q

@[simp] lemma sep_or : {x âˆˆ s | p x âˆ¨ q x} = {x âˆˆ s | p x} âˆª {x âˆˆ s | q x} :=
inter_union_distrib_left

end sep

/-! ### Disjointness -/

protected theorem disjoint_iff : disjoint s t â†” s âˆ© t âŠ† âˆ… := disjoint_iff_inf_le

theorem disjoint_iff_inter_eq_empty : disjoint s t â†” s âˆ© t = âˆ… :=
disjoint_iff

lemma _root_.disjoint.inter_eq : disjoint s t â†’ s âˆ© t = âˆ… := disjoint.eq_bot

lemma disjoint_left : disjoint s t â†” âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ a âˆ‰ t :=
disjoint_iff_inf_le.trans $ forall_congr $ Î» _, not_and
lemma disjoint_right : disjoint s t â†” âˆ€ â¦ƒaâ¦„, a âˆˆ t â†’ a âˆ‰ s := by rw [disjoint.comm, disjoint_left]

/-! ### Lemmas about complement -/

@[simp] theorem inter_compl_self (s : set Î±) : s âˆ© sá¶œ = âˆ… := inf_compl_eq_bot

@[simp] theorem compl_inter_self (s : set Î±) : sá¶œ âˆ© s = âˆ… := compl_inf_eq_bot

@[simp] theorem compl_empty : (âˆ… : set Î±)á¶œ = univ := compl_bot

@[simp] theorem compl_union (s t : set Î±) : (s âˆª t)á¶œ = sá¶œ âˆ© tá¶œ := compl_sup

theorem compl_inter (s t : set Î±) : (s âˆ© t)á¶œ = sá¶œ âˆª tá¶œ := compl_inf

@[simp] theorem compl_univ : (univ : set Î±)á¶œ = âˆ… := compl_top

@[simp] lemma compl_empty_iff {s : set Î±} : sá¶œ = âˆ… â†” s = univ := compl_eq_bot

@[simp] lemma compl_univ_iff {s : set Î±} : sá¶œ = univ â†” s = âˆ… := compl_eq_top

lemma compl_ne_univ : sá¶œ â‰  univ â†” s.nonempty := compl_univ_iff.not.trans nonempty_iff_ne_empty.symm

@[simp] lemma compl_ne_eq_singleton (a : Î±) : ({x | x â‰  a} : set Î±)á¶œ = {a} := compl_compl _

lemma compl_subset_comm : sá¶œ âŠ† t â†” tá¶œ âŠ† s := @compl_le_iff_compl_le _ s _ _
lemma subset_compl_comm : s âŠ† tá¶œ â†” t âŠ† sá¶œ := @le_compl_iff_le_compl _ _ _ t

@[simp] lemma compl_subset_compl : sá¶œ âŠ† tá¶œ â†” t âŠ† s := @compl_le_compl_iff_le (set Î±) _ _ _

lemma subset_compl_iff_disjoint_left : s âŠ† tá¶œ â†” disjoint t s :=
@le_compl_iff_disjoint_left (set Î±) _ _ _

lemma subset_compl_iff_disjoint_right : s âŠ† tá¶œ â†” disjoint s t :=
@le_compl_iff_disjoint_right (set Î±) _ _ _

lemma disjoint_compl_left_iff_subset : disjoint sá¶œ t â†” t âŠ† s := disjoint_compl_left_iff
lemma disjoint_compl_right_iff_subset : disjoint s tá¶œ â†” s âŠ† t := disjoint_compl_right_iff

alias subset_compl_iff_disjoint_right â†” _ _root_.disjoint.subset_compl_right
alias subset_compl_iff_disjoint_left â†” _ _root_.disjoint.subset_compl_left
alias disjoint_compl_left_iff_subset â†” _ _root_.has_subset.subset.disjoint_compl_left
alias disjoint_compl_right_iff_subset â†” _ _root_.has_subset.subset.disjoint_compl_right

theorem subset_union_compl_iff_inter_subset {s t u : set Î±} : s âŠ† t âˆª uá¶œ â†” s âˆ© u âŠ† t :=
(@is_compl_compl _ u _).le_sup_right_iff_inf_left_le

@[simp] lemma subset_compl_singleton_iff {a : Î±} {s : set Î±} : s âŠ† {a}á¶œ â†” a âˆ‰ s :=
subset_compl_comm.trans singleton_subset_iff

/-! ### Lemmas about set difference -/

theorem diff_eq (s t : set Î±) : s \ t = s âˆ© tá¶œ := rfl

@[simp] theorem mem_diff {s t : set Î±} (x : Î±) : x âˆˆ s \ t â†” x âˆˆ s âˆ§ x âˆ‰ t := iff.rfl

theorem mem_diff_of_mem {s t : set Î±} {x : Î±} (h1 : x âˆˆ s) (h2 : x âˆ‰ t) : x âˆˆ s \ t :=
âŸ¨h1, h2âŸ©

lemma not_mem_diff_of_mem {s t : set Î±} {x : Î±} (hx : x âˆˆ t) : x âˆ‰ s \ t :=
Î» h, h.2 hx

theorem mem_of_mem_diff {s t : set Î±} {x : Î±} (h : x âˆˆ s \ t) : x âˆˆ s :=
h.left

theorem not_mem_of_mem_diff {s t : set Î±} {x : Î±} (h : x âˆˆ s \ t) : x âˆ‰ t :=
h.right

theorem diff_eq_compl_inter {s t : set Î±} : s \ t = tá¶œ âˆ© s :=
by rw [diff_eq, inter_comm]

theorem nonempty_diff {s t : set Î±} : (s \ t).nonempty â†” Â¬ (s âŠ† t) := inter_compl_nonempty_iff

theorem diff_subset (s t : set Î±) : s \ t âŠ† s := show s \ t â‰¤ s, from sdiff_le

theorem union_diff_cancel' {s t u : set Î±} (hâ‚ : s âŠ† t) (hâ‚‚ : t âŠ† u) : t âˆª (u \ s) = u :=
sup_sdiff_cancel' hâ‚ hâ‚‚

theorem union_diff_cancel {s t : set Î±} (h : s âŠ† t) : s âˆª (t \ s) = t :=
sup_sdiff_cancel_right h

theorem union_diff_cancel_left {s t : set Î±} (h : s âˆ© t âŠ† âˆ…) : (s âˆª t) \ s = t :=
disjoint.sup_sdiff_cancel_left $ disjoint_iff_inf_le.2 h

theorem union_diff_cancel_right {s t : set Î±} (h : s âˆ© t âŠ† âˆ…) : (s âˆª t) \ t = s :=
disjoint.sup_sdiff_cancel_right $ disjoint_iff_inf_le.2 h

@[simp] theorem union_diff_left {s t : set Î±} : (s âˆª t) \ s = t \ s :=
sup_sdiff_left_self

@[simp] theorem union_diff_right {s t : set Î±} : (s âˆª t) \ t = s \ t :=
sup_sdiff_right_self

theorem union_diff_distrib {s t u : set Î±} : (s âˆª t) \ u = s \ u âˆª t \ u :=
sup_sdiff

theorem inter_diff_assoc (a b c : set Î±) : (a âˆ© b) \ c = a âˆ© (b \ c) :=
inf_sdiff_assoc

@[simp] theorem inter_diff_self (a b : set Î±) : a âˆ© (b \ a) = âˆ… :=
inf_sdiff_self_right

@[simp] theorem inter_union_diff (s t : set Î±) : (s âˆ© t) âˆª (s \ t) = s :=
sup_inf_sdiff s t

@[simp] lemma diff_union_inter (s t : set Î±) : (s \ t) âˆª (s âˆ© t) = s :=
by { rw union_comm, exact sup_inf_sdiff _ _ }

@[simp] theorem inter_union_compl (s t : set Î±) : (s âˆ© t) âˆª (s âˆ© tá¶œ) = s := inter_union_diff _ _

theorem diff_subset_diff {sâ‚ sâ‚‚ tâ‚ tâ‚‚ : set Î±} : sâ‚ âŠ† sâ‚‚ â†’ tâ‚‚ âŠ† tâ‚ â†’ sâ‚ \ tâ‚ âŠ† sâ‚‚ \ tâ‚‚ :=
show sâ‚ â‰¤ sâ‚‚ â†’ tâ‚‚ â‰¤ tâ‚ â†’ sâ‚ \ tâ‚ â‰¤ sâ‚‚ \ tâ‚‚, from sdiff_le_sdiff

theorem diff_subset_diff_left {sâ‚ sâ‚‚ t : set Î±} (h : sâ‚ âŠ† sâ‚‚) : sâ‚ \ t âŠ† sâ‚‚ \ t :=
sdiff_le_sdiff_right â€¹sâ‚ â‰¤ sâ‚‚â€º

theorem diff_subset_diff_right {s t u : set Î±} (h : t âŠ† u) : s \ u âŠ† s \ t :=
sdiff_le_sdiff_left â€¹t â‰¤ uâ€º

theorem compl_eq_univ_diff (s : set Î±) : sá¶œ = univ \ s :=
top_sdiff.symm

@[simp] lemma empty_diff (s : set Î±) : (âˆ… \ s : set Î±) = âˆ… :=
bot_sdiff

theorem diff_eq_empty {s t : set Î±} : s \ t = âˆ… â†” s âŠ† t :=
sdiff_eq_bot_iff

@[simp] theorem diff_empty {s : set Î±} : s \ âˆ… = s :=
sdiff_bot

@[simp] lemma diff_univ (s : set Î±) : s \ univ = âˆ… := diff_eq_empty.2 (subset_univ s)

theorem diff_diff {u : set Î±} : s \ t \ u = s \ (t âˆª u) :=
sdiff_sdiff_left

-- the following statement contains parentheses to help the reader
lemma diff_diff_comm {s t u : set Î±} : (s \ t) \ u = (s \ u) \ t :=
sdiff_sdiff_comm

lemma diff_subset_iff {s t u : set Î±} : s \ t âŠ† u â†” s âŠ† t âˆª u :=
show s \ t â‰¤ u â†” s â‰¤ t âˆª u, from sdiff_le_iff

lemma subset_diff_union (s t : set Î±) : s âŠ† (s \ t) âˆª t :=
show s â‰¤ (s \ t) âˆª t, from le_sdiff_sup

lemma diff_union_of_subset {s t : set Î±} (h : t âŠ† s) :
  (s \ t) âˆª t = s :=
subset.antisymm (union_subset (diff_subset _ _) h) (subset_diff_union _ _)

@[simp] lemma diff_singleton_subset_iff {x : Î±} {s t : set Î±} : s \ {x} âŠ† t â†” s âŠ† insert x t :=
by { rw [â†union_singleton, union_comm], apply diff_subset_iff }

lemma subset_diff_singleton {x : Î±} {s t : set Î±} (h : s âŠ† t) (hx : x âˆ‰ s) : s âŠ† t \ {x} :=
subset_inter h $ subset_compl_comm.1 $ singleton_subset_iff.2 hx

lemma subset_insert_diff_singleton (x : Î±) (s : set Î±) : s âŠ† insert x (s \ {x}) :=
by rw [â†diff_singleton_subset_iff]

lemma diff_subset_comm {s t u : set Î±} : s \ t âŠ† u â†” s \ u âŠ† t :=
show s \ t â‰¤ u â†” s \ u â‰¤ t, from sdiff_le_comm

lemma diff_inter {s t u : set Î±} : s \ (t âˆ© u) = (s \ t) âˆª (s \ u) :=
sdiff_inf

lemma diff_inter_diff {s t u : set Î±} : s \ t âˆ© (s \ u) = s \ (t âˆª u) :=
sdiff_sup.symm

lemma diff_compl : s \ tá¶œ = s âˆ© t := sdiff_compl

lemma diff_diff_right {s t u : set Î±} : s \ (t \ u) = (s \ t) âˆª (s âˆ© u) :=
sdiff_sdiff_right'

@[simp] theorem insert_diff_of_mem (s) (h : a âˆˆ t) : insert a s \ t = s \ t :=
by { ext, split; simp [or_imp_distrib, h] {contextual := tt} }

theorem insert_diff_of_not_mem (s) (h : a âˆ‰ t) : insert a s \ t = insert a (s \ t) :=
begin
  classical,
  ext x,
  by_cases h' : x âˆˆ t,
  { have : x â‰  a,
    { assume H,
      rw H at h',
      exact h h' },
    simp [h, h', this] },
  { simp [h, h'] }
end

lemma insert_diff_self_of_not_mem {a : Î±} {s : set Î±} (h : a âˆ‰ s) :
  insert a s \ {a} = s :=
by { ext, simp [and_iff_left_of_imp (Î» hx : x âˆˆ s, show x â‰  a, from Î» hxa, h $ hxa â–¸ hx)] }

@[simp] lemma insert_diff_eq_singleton {a : Î±} {s : set Î±} (h : a âˆ‰ s) :
  insert a s \ s = {a} :=
begin
  ext,
  rw [set.mem_diff, set.mem_insert_iff, set.mem_singleton_iff, or_and_distrib_right,
    and_not_self, or_false, and_iff_left_iff_imp],
  rintro rfl,
  exact h,
end

lemma inter_insert_of_mem (h : a âˆˆ s) : s âˆ© insert a t = insert a (s âˆ© t) :=
by rw [insert_inter_distrib, insert_eq_of_mem h]

lemma insert_inter_of_mem (h : a âˆˆ t) : insert a s âˆ© t = insert a (s âˆ© t) :=
by rw [insert_inter_distrib, insert_eq_of_mem h]

lemma inter_insert_of_not_mem (h : a âˆ‰ s) : s âˆ© insert a t = s âˆ© t :=
ext $ Î» x, and_congr_right $ Î» hx, or_iff_right $ ne_of_mem_of_not_mem hx h

lemma insert_inter_of_not_mem (h : a âˆ‰ t) : insert a s âˆ© t = s âˆ© t :=
ext $ Î» x, and_congr_left $ Î» hx, or_iff_right $ ne_of_mem_of_not_mem hx h

@[simp] lemma union_diff_self {s t : set Î±} : s âˆª (t \ s) = s âˆª t := sup_sdiff_self _ _
@[simp] lemma diff_union_self {s t : set Î±} : (s \ t) âˆª t = s âˆª t := sdiff_sup_self _ _

@[simp] theorem diff_inter_self {a b : set Î±} : (b \ a) âˆ© a = âˆ… :=
inf_sdiff_self_left

@[simp] theorem diff_inter_self_eq_diff {s t : set Î±} : s \ (t âˆ© s) = s \ t :=
sdiff_inf_self_right _ _

@[simp] theorem diff_self_inter {s t : set Î±} : s \ (s âˆ© t) = s \ t := sdiff_inf_self_left _ _

@[simp] theorem diff_eq_self {s t : set Î±} : s \ t = s â†” t âˆ© s âŠ† âˆ… :=
show s \ t = s â†” t âŠ“ s â‰¤ âŠ¥, from sdiff_eq_self_iff_disjoint.trans disjoint_iff_inf_le

@[simp] theorem diff_singleton_eq_self {a : Î±} {s : set Î±} (h : a âˆ‰ s) : s \ {a} = s :=
diff_eq_self.2 $ by simp [singleton_inter_eq_empty.2 h]

@[simp] theorem insert_diff_singleton {a : Î±} {s : set Î±} :
  insert a (s \ {a}) = insert a s :=
by simp [insert_eq, union_diff_self, -union_singleton, -singleton_union]

@[simp] lemma diff_self {s : set Î±} : s \ s = âˆ… := sdiff_self

lemma diff_diff_right_self (s t : set Î±)  : s \ (s \ t) = s âˆ© t := sdiff_sdiff_right_self

lemma diff_diff_cancel_left {s t : set Î±} (h : s âŠ† t) : t \ (t \ s) = s :=
sdiff_sdiff_eq_self h

lemma mem_diff_singleton {x y : Î±} {s : set Î±} : x âˆˆ s \ {y} â†” (x âˆˆ s âˆ§ x â‰  y) :=
iff.rfl

lemma mem_diff_singleton_empty {t : set (set Î±)} : s âˆˆ t \ {âˆ…} â†” s âˆˆ t âˆ§ s.nonempty :=
mem_diff_singleton.trans $ and_congr_right' nonempty_iff_ne_empty.symm

lemma union_eq_diff_union_diff_union_inter (s t : set Î±) :
  s âˆª t = (s \ t) âˆª (t \ s) âˆª (s âˆ© t) :=
sup_eq_sdiff_sup_sdiff_sup_inf

/-! ### Symmetric difference -/

lemma mem_symm_diff : a âˆˆ s âˆ† t â†” a âˆˆ s âˆ§ a âˆ‰ t âˆ¨ a âˆˆ t âˆ§ a âˆ‰ s := iff.rfl

protected lemma symm_diff_def (s t : set Î±) : s âˆ† t = s \ t âˆª t \ s := rfl

lemma symm_diff_subset_union : s âˆ† t âŠ† s âˆª t := @symm_diff_le_sup (set Î±) _ _ _

@[simp] lemma symm_diff_eq_empty : s âˆ† t = âˆ… â†” s = t := symm_diff_eq_bot

@[simp] lemma symm_diff_nonempty : (s âˆ† t).nonempty â†” s â‰  t :=
nonempty_iff_ne_empty.trans symm_diff_eq_empty.not

lemma inter_symm_diff_distrib_left (s t u : set Î±) : s âˆ© t âˆ† u = (s âˆ© t) âˆ† (s âˆ© u) :=
inf_symm_diff_distrib_left _ _ _

lemma inter_symm_diff_distrib_right (s t u : set Î±) : s âˆ† t âˆ© u = (s âˆ© u) âˆ† (t âˆ© u) :=
inf_symm_diff_distrib_right _ _ _

lemma subset_symm_diff_union_symm_diff_left (h : disjoint s t) : u âŠ† s âˆ† u âˆª t âˆ† u :=
h.le_symm_diff_sup_symm_diff_left

lemma subset_symm_diff_union_symm_diff_right (h : disjoint t u) : s âŠ† s âˆ† t âˆª s âˆ† u :=
h.le_symm_diff_sup_symm_diff_right

/-! ### Powerset -/

theorem monotone_powerset : monotone (powerset : set Î± â†’ set (set Î±)) :=
Î» s t, powerset_mono.2

/-! ### If-then-else for sets -/

/-- `ite` for sets: `set.ite t s s' âˆ© t = s âˆ© t`, `set.ite t s s' âˆ© tá¶œ = s' âˆ© tá¶œ`.
Defined as `s âˆ© t âˆª s' \ t`. -/
protected def ite (t s s' : set Î±) : set Î± := s âˆ© t âˆª s' \ t

@[simp] lemma ite_inter_self (t s s' : set Î±) : t.ite s s' âˆ© t = s âˆ© t :=
by rw [set.ite, union_inter_distrib_right, diff_inter_self, inter_assoc, inter_self, union_empty]

@[simp] lemma ite_compl (t s s' : set Î±) : tá¶œ.ite s s' = t.ite s' s :=
by rw [set.ite, set.ite, diff_compl, union_comm, diff_eq]

@[simp] lemma ite_inter_compl_self (t s s' : set Î±) : t.ite s s' âˆ© tá¶œ = s' âˆ© tá¶œ :=
by rw [â† ite_compl, ite_inter_self]

@[simp] lemma ite_diff_self (t s s' : set Î±) : t.ite s s' \ t = s' \ t :=
ite_inter_compl_self t s s'

@[simp] lemma ite_same (t s : set Î±) : t.ite s s = s := inter_union_diff _ _

@[simp] lemma ite_left (s t : set Î±) : s.ite s t = s âˆª t := by simp [set.ite]

@[simp] lemma ite_right (s t : set Î±) : s.ite t s = t âˆ© s := by simp [set.ite]

@[simp] lemma ite_empty (s s' : set Î±) : set.ite âˆ… s s' = s' :=
by simp [set.ite]

@[simp] lemma ite_univ (s s' : set Î±) : set.ite univ s s' = s :=
by simp [set.ite]

@[simp] lemma ite_empty_left (t s : set Î±) : t.ite âˆ… s = s \ t :=
by simp [set.ite]

@[simp] lemma ite_empty_right (t s : set Î±) : t.ite s âˆ… = s âˆ© t :=
by simp [set.ite]

lemma ite_mono (t : set Î±) {sâ‚ sâ‚' sâ‚‚ sâ‚‚' : set Î±} (h : sâ‚ âŠ† sâ‚‚) (h' : sâ‚' âŠ† sâ‚‚') :
  t.ite sâ‚ sâ‚' âŠ† t.ite sâ‚‚ sâ‚‚' :=
union_subset_union (inter_subset_inter_left _ h) (inter_subset_inter_left _ h')

lemma ite_subset_union (t s s' : set Î±) : t.ite s s' âŠ† s âˆª s' :=
union_subset_union (inter_subset_left _ _) (diff_subset _ _)

lemma inter_subset_ite (t s s' : set Î±) : s âˆ© s' âŠ† t.ite s s' :=
ite_same t (s âˆ© s') â–¸ ite_mono _ (inter_subset_left _ _) (inter_subset_right _ _)

lemma ite_inter_inter (t sâ‚ sâ‚‚ sâ‚' sâ‚‚' : set Î±) :
  t.ite (sâ‚ âˆ© sâ‚‚) (sâ‚' âˆ© sâ‚‚') = t.ite sâ‚ sâ‚' âˆ© t.ite sâ‚‚ sâ‚‚' :=
by { ext x, simp only [set.ite, set.mem_inter_iff, set.mem_diff, set.mem_union], itauto }

lemma ite_inter (t sâ‚ sâ‚‚ s : set Î±) :
  t.ite (sâ‚ âˆ© s) (sâ‚‚ âˆ© s) = t.ite sâ‚ sâ‚‚ âˆ© s :=
by rw [ite_inter_inter, ite_same]

lemma ite_inter_of_inter_eq (t : set Î±) {sâ‚ sâ‚‚ s : set Î±} (h : sâ‚ âˆ© s = sâ‚‚ âˆ© s) :
  t.ite sâ‚ sâ‚‚ âˆ© s = sâ‚ âˆ© s :=
by rw [â† ite_inter, â† h, ite_same]

lemma subset_ite {t s s' u : set Î±} : u âŠ† t.ite s s' â†” u âˆ© t âŠ† s âˆ§ u \ t âŠ† s' :=
begin
  simp only [subset_def, â† forall_and_distrib],
  refine forall_congr (Î» x, _),
  by_cases hx : x âˆˆ t; simp [*, set.ite]
end

/-! ### Inverse image -/

section preimage
variables {f : Î± â†’ Î²} {g : Î² â†’ Î³}

@[simp] theorem preimage_diff (f : Î± â†’ Î²) (s t : set Î²) :
  f â»Â¹' (s \ t) = f â»Â¹' s \ f â»Â¹' t := rfl

@[simp] theorem preimage_ite (f : Î± â†’ Î²) (s tâ‚ tâ‚‚ : set Î²) :
  f â»Â¹' (s.ite tâ‚ tâ‚‚) = (f â»Â¹' s).ite (f â»Â¹' tâ‚) (f â»Â¹' tâ‚‚) :=
rfl

end preimage

/-! ### Image of a set under a function -/

section image
variables {f : Î± â†’ Î²}

lemma preimage_compl_eq_image_compl [boolean_algebra Î±] (S : set Î±) :
  compl â»Â¹' S = compl '' S :=
set.ext (Î» x, âŸ¨Î» h, âŸ¨xá¶œ,h, compl_compl xâŸ©,
  Î» h, exists.elim h (Î» y hy, (compl_eq_comm.mp hy.2).symm.subst hy.1)âŸ©)

theorem mem_compl_image [boolean_algebra Î±] (t : Î±) (S : set Î±) :
  t âˆˆ compl '' S â†” tá¶œ âˆˆ S :=
by simp [â†preimage_compl_eq_image_compl]

theorem compl_compl_image [boolean_algebra Î±] (S : set Î±) :
  compl '' (compl '' S) = S :=
by rw [â†image_comp, compl_comp_compl, image_id]

theorem image_compl_subset {f : Î± â†’ Î²} {s : set Î±} (H : injective f) : f '' sá¶œ âŠ† (f '' s)á¶œ :=
disjoint.subset_compl_left $ by simp [disjoint_iff_inf_le, image_inter H]

theorem image_compl_eq {f : Î± â†’ Î²} {s : set Î±} (H : bijective f) : f '' sá¶œ = (f '' s)á¶œ :=
subset.antisymm (image_compl_subset H.1) (subset_image_compl H.2)

theorem subset_image_diff (f : Î± â†’ Î²) (s t : set Î±) :
  f '' s \ f '' t âŠ† f '' (s \ t) :=
begin
  rw [diff_subset_iff, â† image_union, union_diff_self],
  exact image_subset f (subset_union_right t s)
end

lemma subset_image_symm_diff : (f '' s) âˆ† (f '' t) âŠ† f '' s âˆ† t :=
(union_subset_union (subset_image_diff _ _ _) $ subset_image_diff _ _ _).trans
  (image_union _ _ _).superset

theorem image_diff {f : Î± â†’ Î²} (hf : injective f) (s t : set Î±) :
  f '' (s \ t) = f '' s \ f '' t :=
subset.antisymm
  (subset.trans (image_inter_subset _ _ _) $ inter_subset_inter_right _ $ image_compl_subset hf)
  (subset_image_diff f s t)

lemma image_symm_diff (hf : injective f) (s t : set Î±) : f '' (s âˆ† t) = (f '' s) âˆ† (f '' t) :=
by simp_rw [set.symm_diff_def, image_union, image_diff hf]

lemma image_diff_preimage {f : Î± â†’ Î²} {s : set Î±} {t : set Î²} : f '' (s \ f â»Â¹' t) = f '' s \ t :=
by simp_rw [diff_eq, â† preimage_compl, image_inter_preimage]

theorem compl_image : image (compl : set Î± â†’ set Î±) = preimage compl :=
image_eq_preimage_of_inverse compl_compl compl_compl

theorem compl_image_set_of {p : set Î± â†’ Prop} :
  compl '' {s | p s} = {s | p sá¶œ} :=
congr_fun compl_image p

end image

/-! ### Subsingleton -/

/-- The image of a subsingleton is a subsingleton. -/
lemma subsingleton.image (hs : s.subsingleton) (f : Î± â†’ Î²) : (f '' s).subsingleton :=
Î» _ âŸ¨x, hx, HxâŸ© _ âŸ¨y, hy, HyâŸ©, Hx â–¸ Hy â–¸ congr_arg f (hs hx hy)

/-- The preimage of a subsingleton under an injective map is a subsingleton. -/
theorem subsingleton.preimage {s : set Î²} (hs : s.subsingleton) {f : Î± â†’ Î²}
  (hf : function.injective f) : (f â»Â¹' s).subsingleton := Î» a ha b hb, hf $ hs ha hb

/-- If the image of a set under an injective map is a subsingleton, the set is a subsingleton. -/
theorem subsingleton_of_image {Î± Î² : Type*} {f : Î± â†’ Î²} (hf : function.injective f)
  (s : set Î±) (hs : (f '' s).subsingleton) : s.subsingleton :=
(hs.preimage hf).anti $ subset_preimage_image _ _

/-- If the preimage of a set under an surjective map is a subsingleton,
the set is a subsingleton. -/
theorem subsingleton_of_preimage {Î± Î² : Type*} {f : Î± â†’ Î²} (hf : function.surjective f)
  (s : set Î²) (hs : (f â»Â¹' s).subsingleton) : s.subsingleton :=
Î» fx hx fy hy, by { rcases âŸ¨hf fx, hf fyâŸ© with âŸ¨âŸ¨x, rflâŸ©, âŸ¨y, rflâŸ©âŸ©, exact congr_arg f (hs hx hy) }

/-! ### Nontrivial -/

/-- If the image of a set is nontrivial, the set is nontrivial. -/
lemma nontrivial_of_image (f : Î± â†’ Î²) (s : set Î±) (hs : (f '' s).nontrivial) : s.nontrivial :=
let âŸ¨_, âŸ¨x, hx, rflâŸ©, _, âŸ¨y, hy, rflâŸ©, hxyâŸ© := hs in âŸ¨x, hx, y, hy, mt (congr_arg f) hxyâŸ©

/-- If the preimage of a set under an injective map is nontrivial, the set is nontrivial. -/
lemma nontrivial_of_preimage {f : Î± â†’ Î²} (hf : function.injective f) (s : set Î²)
  (hs : (f â»Â¹' s).nontrivial) : s.nontrivial :=
(hs.image hf).mono $ image_preimage_subset _ _

/-! ### Lemmas about range of a function. -/
section range
variables {f : Î¹ â†’ Î±}
open function

lemma image_preimage_eq_of_subset {f : Î± â†’ Î²} {s : set Î²} (hs : s âŠ† range f) :
  f '' (f â»Â¹' s) = s :=
by rw [image_preimage_eq_inter_range, inter_eq_self_of_subset_left hs]

lemma image_preimage_eq_iff {f : Î± â†’ Î²} {s : set Î²} : f '' (f â»Â¹' s) = s â†” s âŠ† range f :=
âŸ¨by { intro h, rw [â† h], apply image_subset_range }, image_preimage_eq_of_subsetâŸ©

lemma subset_range_iff_exists_image_eq {f : Î± â†’ Î²} {s : set Î²} :
  s âŠ† range f â†” âˆƒ t, f '' t = s :=
âŸ¨Î» h, âŸ¨_, image_preimage_eq_iff.2 hâŸ©, Î» âŸ¨t, htâŸ©, ht â–¸ image_subset_range _ _âŸ©

@[simp] lemma exists_subset_range_and_iff {f : Î± â†’ Î²} {p : set Î² â†’ Prop} :
  (âˆƒ s, s âŠ† range f âˆ§ p s) â†” âˆƒ s, p (f '' s) :=
âŸ¨Î» âŸ¨s, hsf, hpsâŸ©, âŸ¨f â»Â¹' s, (image_preimage_eq_of_subset hsf).symm â–¸ hpsâŸ©,
  Î» âŸ¨s, hsâŸ©, âŸ¨f '' s, image_subset_range _ _, hsâŸ©âŸ©

lemma exists_subset_range_iff {f : Î± â†’ Î²} {p : set Î² â†’ Prop} :
  (âˆƒ s âŠ† range f, p s) â†” âˆƒ s, p (f '' s) :=
by simp only [exists_prop, exists_subset_range_and_iff]

lemma range_image (f : Î± â†’ Î²) : range (image f) = ğ’« (range f) :=
ext $ Î» s, subset_range_iff_exists_image_eq.symm

theorem is_compl_range_inl_range_inr : is_compl (range $ @sum.inl Î± Î²) (range sum.inr) :=
is_compl.of_le
  (by { rintro y âŸ¨âŸ¨xâ‚, rflâŸ©, âŸ¨xâ‚‚, _âŸ©âŸ©, cc })
  (by { rintro (x|y) -; [left, right]; exact mem_range_self _ })

@[simp] theorem range_inl_union_range_inr : range (sum.inl : Î± â†’ Î± âŠ• Î²) âˆª range sum.inr = univ :=
is_compl.sup_eq_top $ by exact is_compl_range_inl_range_inr

@[simp] theorem range_inl_inter_range_inr : range (sum.inl : Î± â†’ Î± âŠ• Î²) âˆ© range sum.inr = âˆ… :=
is_compl.inf_eq_bot $ by exact is_compl_range_inl_range_inr

@[simp] theorem range_inr_union_range_inl : range (sum.inr : Î² â†’ Î± âŠ• Î²) âˆª range sum.inl = univ :=
is_compl.sup_eq_top $ by exact is_compl_range_inl_range_inr.symm

@[simp] theorem range_inr_inter_range_inl : range (sum.inr : Î² â†’ Î± âŠ• Î²) âˆ© range sum.inl = âˆ… :=
is_compl.inf_eq_bot $ by exact is_compl_range_inl_range_inr.symm

@[simp] lemma compl_range_inl : (range (sum.inl : Î± â†’ Î± âŠ• Î²))á¶œ = range (sum.inr : Î² â†’ Î± âŠ• Î²) :=
is_compl.compl_eq is_compl_range_inl_range_inr

@[simp] lemma compl_range_inr : (range (sum.inr : Î² â†’ Î± âŠ• Î²))á¶œ = range (sum.inl : Î± â†’ Î± âŠ• Î²) :=
is_compl.compl_eq is_compl_range_inl_range_inr.symm

theorem image_preimage_inl_union_image_preimage_inr (s : set (Î± âŠ• Î²)) :
  sum.inl '' (sum.inl â»Â¹' s) âˆª sum.inr '' (sum.inr â»Â¹' s) = s :=
by rw [image_preimage_eq_inter_range, image_preimage_eq_inter_range, â† inter_distrib_left,
  range_inl_union_range_inr, inter_univ]

instance can_lift (c) (p) [can_lift Î± Î² c p] :
  can_lift (set Î±) (set Î²) (('') c) (Î» s, âˆ€ x âˆˆ s, p x) :=
{ prf := Î» s hs, subset_range_iff_exists_image_eq.mp (Î» x hx, can_lift.prf _ (hs x hx)) }

lemma image_compl_preimage {f : Î± â†’ Î²} {s : set Î²} : f '' ((f â»Â¹' s)á¶œ) = range f \ s :=
by rw [compl_eq_univ_diff, image_diff_preimage, image_univ]

lemma _root_.sum.range_eq (f : Î± âŠ• Î² â†’ Î³) : range f = range (f âˆ˜ sum.inl) âˆª range (f âˆ˜ sum.inr) :=
ext $ Î» x, sum.exists

@[simp] lemma sum.elim_range (f : Î± â†’ Î³) (g : Î² â†’ Î³) : range (sum.elim f g) = range f âˆª range g :=
sum.range_eq _

lemma range_diff_image_subset (f : Î± â†’ Î²) (s : set Î±) :
  range f \ f '' s âŠ† f '' sá¶œ :=
Î» y âŸ¨âŸ¨x, hâ‚âŸ©, hâ‚‚âŸ©, âŸ¨x, Î» h, hâ‚‚ âŸ¨x, h, hâ‚âŸ©, hâ‚âŸ©

lemma range_diff_image {f : Î± â†’ Î²} (H : injective f) (s : set Î±) :
  range f \ f '' s = f '' sá¶œ :=
subset.antisymm (range_diff_image_subset f s) $ Î» y âŸ¨x, hx, hyâŸ©, hy â–¸
  âŸ¨mem_range_self _, Î» âŸ¨x', hx', eqâŸ©, hx $ H eq â–¸ hx'âŸ©

lemma is_compl_range_some_none (Î± : Type*) :
  is_compl (range (some : Î± â†’ option Î±)) {none} :=
is_compl.of_le
  (Î» x âŸ¨âŸ¨a, haâŸ©, (hn : x = none)âŸ©, option.some_ne_none _ (ha.trans hn))
  (Î» x hx, option.cases_on x (or.inr rfl) (Î» x, or.inl $ mem_range_self _))

@[simp] lemma compl_range_some (Î± : Type*) :
  (range (some : Î± â†’ option Î±))á¶œ = {none} :=
(is_compl_range_some_none Î±).compl_eq

@[simp] lemma range_some_inter_none (Î± : Type*) : range (some : Î± â†’ option Î±) âˆ© {none} = âˆ… :=
(is_compl_range_some_none Î±).inf_eq_bot

@[simp] lemma range_some_union_none (Î± : Type*) : range (some : Î± â†’ option Î±) âˆª {none} = univ :=
(is_compl_range_some_none Î±).sup_eq_top

@[simp] lemma insert_none_range_some (Î± : Type*) :
  insert none (range (some : Î± â†’ option Î±)) = univ :=
(is_compl_range_some_none Î±).symm.sup_eq_top

end range
end set

open set

namespace function

variables {Î¹ : Sort*} {Î± : Type*} {Î² : Type*} {f : Î± â†’ Î²}

lemma injective.subsingleton_image_iff (hf : injective f) {s : set Î±} :
  (f '' s).subsingleton â†” s.subsingleton :=
âŸ¨subsingleton_of_image hf s, Î» h, h.image fâŸ©

end function
open function

namespace option

lemma injective_iff {Î± Î²} {f : option Î± â†’ Î²} :
  injective f â†” injective (f âˆ˜ some) âˆ§ f none âˆ‰ range (f âˆ˜ some) :=
begin
  simp only [mem_range, not_exists, (âˆ˜)],
  refine âŸ¨Î» hf, âŸ¨hf.comp (option.some_injective _), Î» x, hf.ne $ option.some_ne_none _âŸ©, _âŸ©,
  rintro âŸ¨h_some, h_noneâŸ© (_|a) (_|b) hab,
  exacts [rfl, (h_none _ hab.symm).elim, (h_none _ hab).elim, congr_arg some (h_some hab)]
end

lemma range_eq {Î± Î²} (f : option Î± â†’ Î²) : range f = insert (f none) (range (f âˆ˜ some)) :=
set.ext $ Î» y, option.exists.trans $ eq_comm.or iff.rfl

end option

lemma with_bot.range_eq {Î± Î²} (f : with_bot Î± â†’ Î²) :
  range f = insert (f âŠ¥) (range (f âˆ˜ coe : Î± â†’ Î²)) :=
option.range_eq f

lemma with_top.range_eq {Î± Î²} (f : with_top Î± â†’ Î²) :
  range f = insert (f âŠ¤) (range (f âˆ˜ coe : Î± â†’ Î²)) :=
option.range_eq f

/-! ### Image and preimage on subtypes -/

namespace subtype

variable {Î± : Type*}


lemma exists_set_subtype {t : set Î±} (p : set Î± â†’ Prop) :
  (âˆƒ(s : set t), p (coe '' s)) â†” âˆƒ(s : set Î±), s âŠ† t âˆ§ p s :=
begin
  split,
  { rintro âŸ¨s, hsâŸ©, refine âŸ¨coe '' s, _, hsâŸ©,
    convert image_subset_range _ _, rw [range_coe] },
  rintro âŸ¨s, hsâ‚, hsâ‚‚âŸ©, refine âŸ¨coe â»Â¹' s, _âŸ©,
  rw [image_preimage_eq_of_subset], exact hsâ‚‚, rw [range_coe], exact hsâ‚
end

@[simp] lemma preimage_coe_compl (s : set Î±) : (coe : s â†’ Î±) â»Â¹' sá¶œ = âˆ… :=
preimage_coe_eq_empty.2 (inter_compl_self s)

@[simp] lemma preimage_coe_compl' (s : set Î±) : (coe : sá¶œ â†’ Î±) â»Â¹' s = âˆ… :=
preimage_coe_eq_empty.2 (compl_inter_self s)

end subtype

namespace set

/-! ### Lemmas about `inclusion`, the injection of subtypes induced by `âŠ†` -/

section inclusion
variables {Î± : Type*} {s t u : set Î±}

/-- `inclusion` is the "identity" function between two subsets `s` and `t`, where `s âŠ† t` -/
def inclusion (h : s âŠ† t) : s â†’ t :=
Î» x : s, (âŸ¨x, h x.2âŸ© : t)

@[simp] lemma inclusion_self (x : s) : inclusion subset.rfl x = x := by { cases x, refl }

lemma inclusion_eq_id (h : s âŠ† s) : inclusion h = id := funext inclusion_self

@[simp] lemma inclusion_mk {h : s âŠ† t} (a : Î±) (ha : a âˆˆ s) : inclusion h âŸ¨a, haâŸ© = âŸ¨a, h haâŸ© := rfl

lemma inclusion_right (h : s âŠ† t) (x : t) (m : (x : Î±) âˆˆ s) : inclusion h âŸ¨x, mâŸ© = x :=
by { cases x, refl }

@[simp] lemma inclusion_inclusion (hst : s âŠ† t) (htu : t âŠ† u) (x : s) :
  inclusion htu (inclusion hst x) = inclusion (hst.trans htu) x :=
by { cases x, refl }

@[simp] lemma inclusion_comp_inclusion {Î±} {s t u : set Î±} (hst : s âŠ† t) (htu : t âŠ† u) :
  inclusion htu âˆ˜ inclusion hst = inclusion (hst.trans htu) :=
funext (inclusion_inclusion hst htu)

@[simp] lemma coe_inclusion (h : s âŠ† t) (x : s) : (inclusion h x : Î±) = (x : Î±) := rfl

lemma inclusion_injective (h : s âŠ† t) : injective (inclusion h)
| âŸ¨_, _âŸ© âŸ¨_, _âŸ© := subtype.ext_iff_val.2 âˆ˜ subtype.ext_iff_val.1

@[simp] lemma range_inclusion (h : s âŠ† t) : range (inclusion h) = {x : t | (x:Î±) âˆˆ s} :=
by { ext âŸ¨x, hxâŸ©, simp [inclusion] }

lemma eq_of_inclusion_surjective {s t : set Î±} {h : s âŠ† t}
  (h_surj : function.surjective (inclusion h)) : s = t :=
begin
  rw [â† range_iff_surjective, range_inclusion, eq_univ_iff_forall] at h_surj,
  exact set.subset.antisymm h (Î» x hx, h_surj âŸ¨x, hxâŸ©)
end

end inclusion

/-!
### Images of binary and ternary functions

This section is very similar to `order.filter.n_ary`, `data.finset.n_ary`, `data.option.n_ary`.
Please keep them in sync.
-/

end set

/-! ### Decidability instances for sets -/

namespace set
variables {Î± : Type u} (s t : set Î±) (a : Î±)

instance decidable_sdiff [decidable (a âˆˆ s)] [decidable (a âˆˆ t)] : decidable (a âˆˆ s \ t) :=
(by apply_instance : decidable (a âˆˆ s âˆ§ a âˆ‰ t))

end set

/-! ### Indicator function valued in bool -/

open bool

namespace set
variables {Î± : Type*} (s : set Î±)

/-- `bool_indicator` maps `x` to `tt` if `x âˆˆ s`, else to `ff` -/
noncomputable def bool_indicator (x : Î±) :=
@ite _ (x âˆˆ s) (classical.prop_decidable _) tt ff

lemma mem_iff_bool_indicator (x : Î±) : x âˆˆ s â†” s.bool_indicator x = tt :=
by { unfold bool_indicator, split_ifs ; tauto }

lemma not_mem_iff_bool_indicator (x : Î±) : x âˆ‰ s â†” s.bool_indicator x = ff :=
by { unfold bool_indicator, split_ifs ; tauto }

lemma preimage_bool_indicator_tt : s.bool_indicator â»Â¹' {tt} = s :=
ext (Î» x, (s.mem_iff_bool_indicator x).symm)

lemma preimage_bool_indicator_ff : s.bool_indicator â»Â¹' {ff} = sá¶œ :=
ext (Î» x, (s.not_mem_iff_bool_indicator x).symm)

open_locale classical

lemma preimage_bool_indicator_eq_union (t : set bool) :
  s.bool_indicator â»Â¹' t = (if tt âˆˆ t then s else âˆ…) âˆª (if ff âˆˆ t then sá¶œ else âˆ…) :=
begin
  ext x,
  dsimp [bool_indicator],
  split_ifs ; tauto
end

lemma preimage_bool_indicator (t : set bool) :
  s.bool_indicator â»Â¹' t = univ âˆ¨ s.bool_indicator â»Â¹' t = s âˆ¨
  s.bool_indicator â»Â¹' t = sá¶œ âˆ¨ s.bool_indicator â»Â¹' t = âˆ… :=
begin
  simp only [preimage_bool_indicator_eq_union],
  split_ifs ; simp [s.union_compl_self]
end

end set
