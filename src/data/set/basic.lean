/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura
-/
import logic.unique
import order.boolean_algebra

/-!
# Basic properties of sets

Sets in Lean are homogeneous; all their elements have the same type. Sets whose elements
have type `X` are thus defined as `set X := X ‚Üí Prop`. Note that this function need not
be decidable. The definition is in the core library.

This file provides some basic definitions related to sets and functions not present in the core
library, as well as extra lemmas for functions in the core library (empty set, univ, union,
intersection, insert, singleton, set-theoretic difference, complement, and powerset).

Note that a set is a term, not a type. There is a coersion from `set Œ±` to `Type*` sending
`s` to the corresponding subtype `‚Ü•s`.

See also the file `set_theory/zfc.lean`, which contains an encoding of ZFC set theory in Lean.

## Main definitions

Notation used here:

-  `f : Œ± ‚Üí Œ≤` is a function,

-  `s : set Œ±` and `s‚ÇÅ s‚ÇÇ : set Œ±` are subsets of `Œ±`

-  `t : set Œ≤` is a subset of `Œ≤`.

Definitions in the file:

* `strict_subset s‚ÇÅ s‚ÇÇ : Prop` : the predicate `s‚ÇÅ ‚äÜ s‚ÇÇ` but `s‚ÇÅ ‚â† s‚ÇÇ`.

* `nonempty s : Prop` : the predicate `s ‚â† ‚àÖ`. Note that this is the preferred way to express the
  fact that `s` has an element (see the Implementation Notes).

* `preimage f t : set Œ±` : the preimage f‚Åª¬π(t) (written `f ‚Åª¬π' t` in Lean) of a subset of Œ≤.

* `subsingleton s : Prop` : the predicate saying that `s` has at most one element.

* `range f : set Œ≤` : the image of `univ` under `f`.
  Also works for `{p : Prop} (f : p ‚Üí Œ±)` (unlike `image`)

* `s.prod t : set (Œ± √ó Œ≤)` : the subset `s √ó t`.

* `inclusion s‚ÇÅ s‚ÇÇ : ‚Ü•s‚ÇÅ ‚Üí ‚Ü•s‚ÇÇ` : the map `‚Ü•s‚ÇÅ ‚Üí ‚Ü•s‚ÇÇ` induced by an inclusion `s‚ÇÅ ‚äÜ s‚ÇÇ`.

## Notation

* `f ‚Åª¬π' t` for `preimage f t`

* `f '' s` for `image f s`

* `s·∂ú` for the complement of `s`

## Implementation notes

* `s.nonempty` is to be preferred to `s ‚â† ‚àÖ` or `‚àÉ x, x ‚àà s`. It has the advantage that
the `s.nonempty` dot notation can be used.

* For `s : set Œ±`, do not use `subtype s`. Instead use `‚Ü•s` or `(s : Type*)` or `s`.

## Tags

set, sets, subset, subsets, image, preimage, pre-image, range, union, intersection, insert,
singleton, complement, powerset

-/

/-! ### Set coercion to a type -/

open function

universe variables u v w x

run_cmd do e ‚Üê tactic.get_env,
  tactic.set_env $ e.mk_protected `set.compl

namespace set

variable {Œ± : Type*}

instance : has_le (set Œ±) := ‚ü®(‚äÜ)‚ü©
instance : has_lt (set Œ±) := ‚ü®Œª s t, s ‚â§ t ‚àß ¬¨t ‚â§ s‚ü©  -- `‚äÇ` is not defined until further down

instance {Œ± : Type*} : boolean_algebra (set Œ±) :=
{ sup := (‚à™),
  le  := (‚â§),
  lt  := (<),
  inf := (‚à©),
  bot := ‚àÖ,
  compl := set.compl,
  top := univ,
  sdiff := (\),
  .. (infer_instance : boolean_algebra (Œ± ‚Üí Prop)) }

@[simp] lemma top_eq_univ : (‚ä§ : set Œ±) = univ := rfl
@[simp] lemma bot_eq_empty : (‚ä• : set Œ±) = ‚àÖ := rfl
@[simp] lemma sup_eq_union (s t : set Œ±) : s ‚äî t = s ‚à™ t := rfl
@[simp] lemma inf_eq_inter (s t : set Œ±) : s ‚äì t = s ‚à© t := rfl
@[simp] lemma le_eq_subset (s t : set Œ±) : s ‚â§ t = (s ‚äÜ t) := rfl
/-! `set.lt_eq_ssubset` is defined further down -/

/-- Coercion from a set to the corresponding subtype. -/
instance {Œ± : Type*} : has_coe_to_sort (set Œ±) := ‚ü®_, Œª s, {x // x ‚àà s}‚ü©

instance pi_set_coe.can_lift (Œπ : Type u) (Œ± : Œ† i : Œπ, Type v) [ne : Œ† i, nonempty (Œ± i)]
  (s : set Œπ) :
  can_lift (Œ† i : s, Œ± i) (Œ† i, Œ± i) :=
{ coe := Œª f i, f i,
  .. pi_subtype.can_lift Œπ Œ± s }

instance pi_set_coe.can_lift' (Œπ : Type u) (Œ± : Type v) [ne : nonempty Œ±] (s : set Œπ) :
  can_lift (s ‚Üí Œ±) (Œπ ‚Üí Œ±) :=
pi_set_coe.can_lift Œπ (Œª _, Œ±) s

end set

section set_coe

variables {Œ± : Type u}

theorem set.set_coe_eq_subtype (s : set Œ±) :
  coe_sort.{(u+1) (u+2)} s = {x // x ‚àà s} := rfl

@[simp] theorem set_coe.forall {s : set Œ±} {p : s ‚Üí Prop} :
  (‚àÄ x : s, p x) ‚Üî (‚àÄ x (h : x ‚àà s), p ‚ü®x, h‚ü©) :=
subtype.forall

@[simp] theorem set_coe.exists {s : set Œ±} {p : s ‚Üí Prop} :
  (‚àÉ x : s, p x) ‚Üî (‚àÉ x (h : x ‚àà s), p ‚ü®x, h‚ü©) :=
subtype.exists

theorem set_coe.exists' {s : set Œ±} {p : Œ† x, x ‚àà s ‚Üí Prop} :
  (‚àÉ x (h : x ‚àà s), p x h) ‚Üî (‚àÉ x : s, p x x.2)  :=
(@set_coe.exists _ _ $ Œª x, p x.1 x.2).symm

theorem set_coe.forall' {s : set Œ±} {p : Œ† x, x ‚àà s ‚Üí Prop} :
  (‚àÄ x (h : x ‚àà s), p x h) ‚Üî (‚àÄ x : s, p x x.2)  :=
(@set_coe.forall _ _ $ Œª x, p x.1 x.2).symm

@[simp] theorem set_coe_cast : ‚àÄ {s t : set Œ±} (H' : s = t) (H : @eq (Type u) s t) (x : s),
  cast H x = ‚ü®x.1, H' ‚ñ∏ x.2‚ü©
| s _ rfl _ ‚ü®x, h‚ü© := rfl

theorem set_coe.ext {s : set Œ±} {a b : s} : (‚Üëa : Œ±) = ‚Üëb ‚Üí a = b :=
subtype.eq

theorem set_coe.ext_iff {s : set Œ±} {a b : s} : (‚Üëa : Œ±) = ‚Üëb ‚Üî a = b :=
iff.intro set_coe.ext (assume h, h ‚ñ∏ rfl)

end set_coe

/-- See also `subtype.prop` -/
lemma subtype.mem {Œ± : Type*} {s : set Œ±} (p : s) : (p : Œ±) ‚àà s := p.prop

lemma eq.subset {Œ±} {s t : set Œ±} : s = t ‚Üí s ‚äÜ t :=
by { rintro rfl x hx, exact hx }

namespace set

variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} {Œπ : Sort x} {a : Œ±} {s t : set Œ±}

instance : inhabited (set Œ±) := ‚ü®‚àÖ‚ü©

@[ext]
theorem ext {a b : set Œ±} (h : ‚àÄ x, x ‚àà a ‚Üî x ‚àà b) : a = b :=
funext (assume x, propext (h x))

theorem ext_iff {s t : set Œ±} : s = t ‚Üî ‚àÄ x, x ‚àà s ‚Üî x ‚àà t :=
‚ü®Œª h x, by rw h, ext‚ü©

@[trans] theorem mem_of_mem_of_subset {x : Œ±} {s t : set Œ±}
  (hx : x ‚àà s) (h : s ‚äÜ t) : x ‚àà t := h hx

/-! ### Lemmas about `mem` and `set_of` -/

@[simp] theorem mem_set_of_eq {a : Œ±} {p : Œ± ‚Üí Prop} : a ‚àà {a | p a} = p a := rfl

theorem nmem_set_of_eq {a : Œ±} {P : Œ± ‚Üí Prop} : a ‚àâ {a : Œ± | P a} = ¬¨ P a := rfl

@[simp] theorem set_of_mem_eq {s : set Œ±} : {x | x ‚àà s} = s := rfl

theorem set_of_set {s : set Œ±} : set_of s = s := rfl

lemma set_of_app_iff {p : Œ± ‚Üí Prop} {x : Œ±} : { x | p x } x ‚Üî p x := iff.rfl

theorem mem_def {a : Œ±} {s : set Œ±} : a ‚àà s ‚Üî s a := iff.rfl

instance decidable_mem (s : set Œ±) [H : decidable_pred s] : ‚àÄ a, decidable (a ‚àà s) := H

instance decidable_set_of (p : Œ± ‚Üí Prop) [H : decidable_pred p] : decidable_pred {a | p a} := H

@[simp] theorem set_of_subset_set_of {p q : Œ± ‚Üí Prop} :
  {a | p a} ‚äÜ {a | q a} ‚Üî (‚àÄa, p a ‚Üí q a) := iff.rfl

@[simp] lemma sep_set_of {p q : Œ± ‚Üí Prop} : {a ‚àà {a | p a } | q a} = {a | p a ‚àß q a} := rfl

lemma set_of_and {p q : Œ± ‚Üí Prop} : {a | p a ‚àß q a} = {a | p a} ‚à© {a | q a} := rfl

lemma set_of_or {p q : Œ± ‚Üí Prop} : {a | p a ‚à® q a} = {a | p a} ‚à™ {a | q a} := rfl

/-! ### Lemmas about subsets -/

-- TODO(Jeremy): write a tactic to unfold specific instances of generic notation?
theorem subset_def {s t : set Œ±} : (s ‚äÜ t) = ‚àÄ x, x ‚àà s ‚Üí x ‚àà t := rfl

@[refl] theorem subset.refl (a : set Œ±) : a ‚äÜ a := assume x, id
theorem subset.rfl {s : set Œ±} : s ‚äÜ s := subset.refl s

@[trans] theorem subset.trans {a b c : set Œ±} (ab : a ‚äÜ b) (bc : b ‚äÜ c) : a ‚äÜ c :=
assume x h, bc (ab h)

@[trans] theorem mem_of_eq_of_mem {x y : Œ±} {s : set Œ±} (hx : x = y) (h : y ‚àà s) : x ‚àà s :=
hx.symm ‚ñ∏ h

theorem subset.antisymm {a b : set Œ±} (h‚ÇÅ : a ‚äÜ b) (h‚ÇÇ : b ‚äÜ a) : a = b :=
set.ext $ Œª x, ‚ü®@h‚ÇÅ _, @h‚ÇÇ _‚ü©

theorem subset.antisymm_iff {a b : set Œ±} : a = b ‚Üî a ‚äÜ b ‚àß b ‚äÜ a :=
‚ü®Œª e, ‚ü®e.subset, e.symm.subset‚ü©, Œª ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, subset.antisymm h‚ÇÅ h‚ÇÇ‚ü©

-- an alternative name
theorem eq_of_subset_of_subset {a b : set Œ±} : a ‚äÜ b ‚Üí b ‚äÜ a ‚Üí a = b := subset.antisymm

theorem mem_of_subset_of_mem {s‚ÇÅ s‚ÇÇ : set Œ±} {a : Œ±} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : a ‚àà s‚ÇÅ ‚Üí a ‚àà s‚ÇÇ := @h _

theorem not_mem_subset (h : s ‚äÜ t) : a ‚àâ t ‚Üí a ‚àâ s :=
mt $ mem_of_subset_of_mem h

theorem not_subset : (¬¨ s ‚äÜ t) ‚Üî ‚àÉa ‚àà s, a ‚àâ t := by simp only [subset_def, not_forall]

/-! ### Definition of strict subsets `s ‚äÇ t` and basic properties. -/

instance : has_ssubset (set Œ±) := ‚ü®(<)‚ü©

@[simp] lemma lt_eq_ssubset (s t : set Œ±) : s < t = (s ‚äÇ t) := rfl

theorem ssubset_def : (s ‚äÇ t) = (s ‚äÜ t ‚àß ¬¨ (t ‚äÜ s)) := rfl

theorem eq_or_ssubset_of_subset (h : s ‚äÜ t) : s = t ‚à® s ‚äÇ t :=
eq_or_lt_of_le h

lemma exists_of_ssubset {s t : set Œ±} (h : s ‚äÇ t) : (‚àÉx‚ààt, x ‚àâ s) :=
not_subset.1 h.2

lemma ssubset_iff_subset_ne {s t : set Œ±} : s ‚äÇ t ‚Üî s ‚äÜ t ‚àß s ‚â† t :=
@lt_iff_le_and_ne (set Œ±) _ s t

lemma ssubset_iff_of_subset {s t : set Œ±} (h : s ‚äÜ t) : s ‚äÇ t ‚Üî ‚àÉ x ‚àà t, x ‚àâ s :=
‚ü®exists_of_ssubset, Œª ‚ü®x, hxt, hxs‚ü©, ‚ü®h, Œª h, hxs $ h hxt‚ü©‚ü©

theorem not_mem_empty (x : Œ±) : ¬¨ (x ‚àà (‚àÖ : set Œ±)) := id

@[simp] theorem not_not_mem : ¬¨ (a ‚àâ s) ‚Üî a ‚àà s := not_not

/-! ### Non-empty sets -/

/-- The property `s.nonempty` expresses the fact that the set `s` is not empty. It should be used
in theorem assumptions instead of `‚àÉ x, x ‚àà s` or `s ‚â† ‚àÖ` as it gives access to a nice API thanks
to the dot notation. -/
protected def nonempty (s : set Œ±) : Prop := ‚àÉ x, x ‚àà s

lemma nonempty_def : s.nonempty ‚Üî ‚àÉ x, x ‚àà s := iff.rfl

lemma nonempty_of_mem {x} (h : x ‚àà s) : s.nonempty := ‚ü®x, h‚ü©

theorem nonempty.not_subset_empty : s.nonempty ‚Üí ¬¨(s ‚äÜ ‚àÖ)
| ‚ü®x, hx‚ü© hs := hs hx

theorem nonempty.ne_empty : ‚àÄ {s : set Œ±}, s.nonempty ‚Üí s ‚â† ‚àÖ
| _ ‚ü®x, hx‚ü© rfl := hx

@[simp] theorem not_nonempty_empty : ¬¨(‚àÖ : set Œ±).nonempty :=
Œª h, h.ne_empty rfl

/-- Extract a witness from `s.nonempty`. This function might be used instead of case analysis
on the argument. Note that it makes a proof depend on the `classical.choice` axiom. -/
protected noncomputable def nonempty.some (h : s.nonempty) : Œ± := classical.some h

protected lemma nonempty.some_mem (h : s.nonempty) : h.some ‚àà s := classical.some_spec h

lemma nonempty.mono (ht : s ‚äÜ t) (hs : s.nonempty) : t.nonempty := hs.imp ht

lemma nonempty_of_not_subset (h : ¬¨s ‚äÜ t) : (s \ t).nonempty :=
let ‚ü®x, xs, xt‚ü© := not_subset.1 h in ‚ü®x, xs, xt‚ü©

lemma nonempty_of_ssubset (ht : s ‚äÇ t) : (t \ s).nonempty :=
nonempty_of_not_subset ht.2

lemma nonempty.of_diff (h : (s \ t).nonempty) : s.nonempty := h.imp $ Œª _, and.left

lemma nonempty_of_ssubset' (ht : s ‚äÇ t) : t.nonempty := (nonempty_of_ssubset ht).of_diff

lemma nonempty.inl (hs : s.nonempty) : (s ‚à™ t).nonempty := hs.imp $ Œª _, or.inl

lemma nonempty.inr (ht : t.nonempty) : (s ‚à™ t).nonempty := ht.imp $ Œª _, or.inr

@[simp] lemma union_nonempty : (s ‚à™ t).nonempty ‚Üî s.nonempty ‚à® t.nonempty := exists_or_distrib

lemma nonempty.left (h : (s ‚à© t).nonempty) : s.nonempty := h.imp $ Œª _, and.left

lemma nonempty.right (h : (s ‚à© t).nonempty) : t.nonempty := h.imp $ Œª _, and.right

lemma nonempty_inter_iff_exists_right : (s ‚à© t).nonempty ‚Üî ‚àÉ x : t, ‚Üëx ‚àà s :=
‚ü®Œª ‚ü®x, xs, xt‚ü©, ‚ü®‚ü®x, xt‚ü©, xs‚ü©, Œª ‚ü®‚ü®x, xt‚ü©, xs‚ü©, ‚ü®x, xs, xt‚ü©‚ü©

lemma nonempty_inter_iff_exists_left : (s ‚à© t).nonempty ‚Üî ‚àÉ x : s, ‚Üëx ‚àà t :=
‚ü®Œª ‚ü®x, xs, xt‚ü©, ‚ü®‚ü®x, xs‚ü©, xt‚ü©, Œª ‚ü®‚ü®x, xt‚ü©, xs‚ü©, ‚ü®x, xt, xs‚ü©‚ü©

lemma nonempty_iff_univ_nonempty : nonempty Œ± ‚Üî (univ : set Œ±).nonempty :=
‚ü®Œª ‚ü®x‚ü©, ‚ü®x, trivial‚ü©, Œª ‚ü®x, _‚ü©, ‚ü®x‚ü©‚ü©

@[simp] lemma univ_nonempty : ‚àÄ [h : nonempty Œ±], (univ : set Œ±).nonempty
| ‚ü®x‚ü© := ‚ü®x, trivial‚ü©

lemma nonempty.to_subtype (h : s.nonempty) : nonempty s :=
nonempty_subtype.2 h

instance [nonempty Œ±] : nonempty (set.univ : set Œ±) := set.univ_nonempty.to_subtype

@[simp] lemma nonempty_insert (a : Œ±) (s : set Œ±) : (insert a s).nonempty := ‚ü®a, or.inl rfl‚ü©

lemma nonempty_of_nonempty_subtype [nonempty s] : s.nonempty :=
nonempty_subtype.mp ‚Äπ_‚Ä∫

/-! ### Lemmas about the empty set -/

theorem empty_def : (‚àÖ : set Œ±) = {x | false} := rfl

@[simp] theorem mem_empty_eq (x : Œ±) : x ‚àà (‚àÖ : set Œ±) = false := rfl

@[simp] theorem set_of_false : {a : Œ± | false} = ‚àÖ := rfl

@[simp] theorem empty_subset (s : set Œ±) : ‚àÖ ‚äÜ s.

theorem subset_empty_iff {s : set Œ±} : s ‚äÜ ‚àÖ ‚Üî s = ‚àÖ :=
(subset.antisymm_iff.trans $ and_iff_left (empty_subset _)).symm

theorem eq_empty_iff_forall_not_mem {s : set Œ±} : s = ‚àÖ ‚Üî ‚àÄ x, x ‚àâ s := subset_empty_iff.symm

theorem eq_empty_of_subset_empty {s : set Œ±} : s ‚äÜ ‚àÖ ‚Üí s = ‚àÖ := subset_empty_iff.1

theorem eq_empty_of_not_nonempty (h : ¬¨nonempty Œ±) (s : set Œ±) : s = ‚àÖ :=
eq_empty_of_subset_empty $ Œª x hx, h ‚ü®x‚ü©

lemma not_nonempty_iff_eq_empty {s : set Œ±} : ¬¨s.nonempty ‚Üî s = ‚àÖ :=
by simp only [set.nonempty, eq_empty_iff_forall_not_mem, not_exists]

lemma empty_not_nonempty : ¬¨(‚àÖ : set Œ±).nonempty := Œª h, h.ne_empty rfl

theorem ne_empty_iff_nonempty : s ‚â† ‚àÖ ‚Üî s.nonempty := not_iff_comm.1 not_nonempty_iff_eq_empty

lemma eq_empty_or_nonempty (s : set Œ±) : s = ‚àÖ ‚à® s.nonempty :=
or_iff_not_imp_left.2 ne_empty_iff_nonempty.1

theorem subset_eq_empty {s t : set Œ±} (h : t ‚äÜ s) (e : s = ‚àÖ) : t = ‚àÖ :=
subset_empty_iff.1 $ e ‚ñ∏ h

theorem ball_empty_iff {p : Œ± ‚Üí Prop} : (‚àÄ x ‚àà (‚àÖ : set Œ±), p x) ‚Üî true :=
iff_true_intro $ Œª x, false.elim

/-!

### Universal set.

In Lean `@univ Œ±` (or `univ : set Œ±`) is the set that contains all elements of type `Œ±`.
Mathematically it is the same as `Œ±` but it has a different type.

-/

@[simp] theorem set_of_true : {x : Œ± | true} = univ := rfl

@[simp] theorem mem_univ (x : Œ±) : x ‚àà @univ Œ± := trivial

@[simp] lemma univ_eq_empty_iff : (univ : set Œ±) = ‚àÖ ‚Üî ¬¨ nonempty Œ± :=
eq_empty_iff_forall_not_mem.trans ‚ü®Œª H ‚ü®x‚ü©, H x trivial, Œª H x _, H ‚ü®x‚ü©‚ü©

theorem empty_ne_univ [h : nonempty Œ±] : (‚àÖ : set Œ±) ‚â† univ :=
Œª e, univ_eq_empty_iff.1 e.symm h

@[simp] theorem subset_univ (s : set Œ±) : s ‚äÜ univ := Œª x H, trivial

theorem univ_subset_iff {s : set Œ±} : univ ‚äÜ s ‚Üî s = univ :=
(subset.antisymm_iff.trans $ and_iff_right (subset_univ _)).symm

theorem eq_univ_of_univ_subset {s : set Œ±} : univ ‚äÜ s ‚Üí s = univ := univ_subset_iff.1

theorem eq_univ_iff_forall {s : set Œ±} : s = univ ‚Üî ‚àÄ x, x ‚àà s :=
univ_subset_iff.symm.trans $ forall_congr $ Œª x, imp_iff_right ‚ü®‚ü©

theorem eq_univ_of_forall {s : set Œ±} : (‚àÄ x, x ‚àà s) ‚Üí s = univ := eq_univ_iff_forall.2

lemma eq_univ_of_subset {s t : set Œ±} (h : s ‚äÜ t) (hs : s = univ) : t = univ :=
eq_univ_of_univ_subset $ hs ‚ñ∏ h

lemma exists_mem_of_nonempty (Œ±) : ‚àÄ [nonempty Œ±], ‚àÉx:Œ±, x ‚àà (univ : set Œ±)
| ‚ü®x‚ü© := ‚ü®x, trivial‚ü©

instance univ_decidable : decidable_pred (@set.univ Œ±) :=
Œª x, is_true trivial

/-- `diagonal Œ±` is the subset of `Œ± √ó Œ±` consisting of all pairs of the form `(a, a)`. -/
def diagonal (Œ± : Type*) : set (Œ± √ó Œ±) := {p | p.1 = p.2}

@[simp]
lemma mem_diagonal {Œ± : Type*} (x : Œ±) : (x, x) ‚àà diagonal Œ± :=
by simp [diagonal]

/-! ### Lemmas about union -/

theorem union_def {s‚ÇÅ s‚ÇÇ : set Œ±} : s‚ÇÅ ‚à™ s‚ÇÇ = {a | a ‚àà s‚ÇÅ ‚à® a ‚àà s‚ÇÇ} := rfl

theorem mem_union_left {x : Œ±} {a : set Œ±} (b : set Œ±) : x ‚àà a ‚Üí x ‚àà a ‚à™ b := or.inl

theorem mem_union_right {x : Œ±} {b : set Œ±} (a : set Œ±) : x ‚àà b ‚Üí x ‚àà a ‚à™ b := or.inr

theorem mem_or_mem_of_mem_union {x : Œ±} {a b : set Œ±} (H : x ‚àà a ‚à™ b) : x ‚àà a ‚à® x ‚àà b := H

theorem mem_union.elim {x : Œ±} {a b : set Œ±} {P : Prop}
    (H‚ÇÅ : x ‚àà a ‚à™ b) (H‚ÇÇ : x ‚àà a ‚Üí P) (H‚ÇÉ : x ‚àà b ‚Üí P) : P :=
or.elim H‚ÇÅ H‚ÇÇ H‚ÇÉ

theorem mem_union (x : Œ±) (a b : set Œ±) : x ‚àà a ‚à™ b ‚Üî x ‚àà a ‚à® x ‚àà b := iff.rfl

@[simp] theorem mem_union_eq (x : Œ±) (a b : set Œ±) : x ‚àà a ‚à™ b = (x ‚àà a ‚à® x ‚àà b) := rfl

@[simp] theorem union_self (a : set Œ±) : a ‚à™ a = a := ext $ Œª x, or_self _

@[simp] theorem union_empty (a : set Œ±) : a ‚à™ ‚àÖ = a := ext $ Œª x, or_false _

@[simp] theorem empty_union (a : set Œ±) : ‚àÖ ‚à™ a = a := ext $ Œª x, false_or _

theorem union_comm (a b : set Œ±) : a ‚à™ b = b ‚à™ a := ext $ Œª x, or.comm

theorem union_assoc (a b c : set Œ±) : (a ‚à™ b) ‚à™ c = a ‚à™ (b ‚à™ c) := ext $ Œª x, or.assoc

instance union_is_assoc : is_associative (set Œ±) (‚à™) := ‚ü®union_assoc‚ü©

instance union_is_comm : is_commutative (set Œ±) (‚à™) := ‚ü®union_comm‚ü©

theorem union_left_comm (s‚ÇÅ s‚ÇÇ s‚ÇÉ : set Œ±) : s‚ÇÅ ‚à™ (s‚ÇÇ ‚à™ s‚ÇÉ) = s‚ÇÇ ‚à™ (s‚ÇÅ ‚à™ s‚ÇÉ) :=
ext $ Œª x, or.left_comm

theorem union_right_comm (s‚ÇÅ s‚ÇÇ s‚ÇÉ : set Œ±) : (s‚ÇÅ ‚à™ s‚ÇÇ) ‚à™ s‚ÇÉ = (s‚ÇÅ ‚à™ s‚ÇÉ) ‚à™ s‚ÇÇ :=
ext $ Œª x, or.right_comm

theorem union_eq_self_of_subset_left {s t : set Œ±} (h : s ‚äÜ t) : s ‚à™ t = t :=
ext $ Œª x, or_iff_right_of_imp $ @h _

theorem union_eq_self_of_subset_right {s t : set Œ±} (h : t ‚äÜ s) : s ‚à™ t = s :=
ext $ Œª x, or_iff_left_of_imp $ @h _

@[simp] theorem subset_union_left (s t : set Œ±) : s ‚äÜ s ‚à™ t := Œª x, or.inl

@[simp] theorem subset_union_right (s t : set Œ±) : t ‚äÜ s ‚à™ t := Œª x, or.inr

theorem union_subset {s t r : set Œ±} (sr : s ‚äÜ r) (tr : t ‚äÜ r) : s ‚à™ t ‚äÜ r :=
Œª x, or.rec (@sr _) (@tr _)

@[simp] theorem union_subset_iff {s t u : set Œ±} : s ‚à™ t ‚äÜ u ‚Üî s ‚äÜ u ‚àß t ‚äÜ u :=
(forall_congr (by exact Œª x, or_imp_distrib)).trans forall_and_distrib

theorem union_subset_union {s‚ÇÅ s‚ÇÇ t‚ÇÅ t‚ÇÇ : set Œ±}
  (h‚ÇÅ : s‚ÇÅ ‚äÜ s‚ÇÇ) (h‚ÇÇ : t‚ÇÅ ‚äÜ t‚ÇÇ) : s‚ÇÅ ‚à™ t‚ÇÅ ‚äÜ s‚ÇÇ ‚à™ t‚ÇÇ := Œª x, or.imp (@h‚ÇÅ _) (@h‚ÇÇ _)

theorem union_subset_union_left {s‚ÇÅ s‚ÇÇ : set Œ±} (t) (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : s‚ÇÅ ‚à™ t ‚äÜ s‚ÇÇ ‚à™ t :=
union_subset_union h subset.rfl

theorem union_subset_union_right (s) {t‚ÇÅ t‚ÇÇ : set Œ±} (h : t‚ÇÅ ‚äÜ t‚ÇÇ) : s ‚à™ t‚ÇÅ ‚äÜ s ‚à™ t‚ÇÇ :=
union_subset_union subset.rfl h

lemma subset_union_of_subset_left {s t : set Œ±} (h : s ‚äÜ t) (u : set Œ±) : s ‚äÜ t ‚à™ u :=
subset.trans h (subset_union_left t u)

lemma subset_union_of_subset_right {s u : set Œ±} (h : s ‚äÜ u) (t : set Œ±) : s ‚äÜ t ‚à™ u :=
subset.trans h (subset_union_right t u)

@[simp] theorem union_empty_iff {s t : set Œ±} : s ‚à™ t = ‚àÖ ‚Üî s = ‚àÖ ‚àß t = ‚àÖ :=
by simp only [‚Üê subset_empty_iff]; exact union_subset_iff

@[simp] lemma union_univ {s : set Œ±} : s ‚à™ univ = univ := sup_top_eq

@[simp] lemma univ_union {s : set Œ±} : univ ‚à™ s = univ := top_sup_eq

/-! ### Lemmas about intersection -/

theorem inter_def {s‚ÇÅ s‚ÇÇ : set Œ±} : s‚ÇÅ ‚à© s‚ÇÇ = {a | a ‚àà s‚ÇÅ ‚àß a ‚àà s‚ÇÇ} := rfl

theorem mem_inter_iff (x : Œ±) (a b : set Œ±) : x ‚àà a ‚à© b ‚Üî x ‚àà a ‚àß x ‚àà b := iff.rfl

@[simp] theorem mem_inter_eq (x : Œ±) (a b : set Œ±) : x ‚àà a ‚à© b = (x ‚àà a ‚àß x ‚àà b) := rfl

theorem mem_inter {x : Œ±} {a b : set Œ±} (ha : x ‚àà a) (hb : x ‚àà b) : x ‚àà a ‚à© b := ‚ü®ha, hb‚ü©

theorem mem_of_mem_inter_left {x : Œ±} {a b : set Œ±} (h : x ‚àà a ‚à© b) : x ‚àà a := h.left

theorem mem_of_mem_inter_right {x : Œ±} {a b : set Œ±} (h : x ‚àà a ‚à© b) : x ‚àà b := h.right

@[simp] theorem inter_self (a : set Œ±) : a ‚à© a = a := ext $ Œª x, and_self _

@[simp] theorem inter_empty (a : set Œ±) : a ‚à© ‚àÖ = ‚àÖ := ext $ Œª x, and_false _

@[simp] theorem empty_inter (a : set Œ±) : ‚àÖ ‚à© a = ‚àÖ := ext $ Œª x, false_and _

theorem inter_comm (a b : set Œ±) : a ‚à© b = b ‚à© a := ext $ Œª x, and.comm

theorem inter_assoc (a b c : set Œ±) : (a ‚à© b) ‚à© c = a ‚à© (b ‚à© c) := ext $ Œª x, and.assoc

instance inter_is_assoc : is_associative (set Œ±) (‚à©) := ‚ü®inter_assoc‚ü©

instance inter_is_comm : is_commutative (set Œ±) (‚à©) := ‚ü®inter_comm‚ü©

theorem inter_left_comm (s‚ÇÅ s‚ÇÇ s‚ÇÉ : set Œ±) : s‚ÇÅ ‚à© (s‚ÇÇ ‚à© s‚ÇÉ) = s‚ÇÇ ‚à© (s‚ÇÅ ‚à© s‚ÇÉ) :=
ext $ Œª x, and.left_comm

theorem inter_right_comm (s‚ÇÅ s‚ÇÇ s‚ÇÉ : set Œ±) : (s‚ÇÅ ‚à© s‚ÇÇ) ‚à© s‚ÇÉ = (s‚ÇÅ ‚à© s‚ÇÉ) ‚à© s‚ÇÇ :=
ext $ Œª x, and.right_comm

@[simp] theorem inter_subset_left (s t : set Œ±) : s ‚à© t ‚äÜ s := Œª x, and.left

@[simp] theorem inter_subset_right (s t : set Œ±) : s ‚à© t ‚äÜ t := Œª x, and.right

theorem subset_inter {s t r : set Œ±} (rs : r ‚äÜ s) (rt : r ‚äÜ t) : r ‚äÜ s ‚à© t := Œª x h, ‚ü®rs h, rt h‚ü©

@[simp] theorem subset_inter_iff {s t r : set Œ±} : r ‚äÜ s ‚à© t ‚Üî r ‚äÜ s ‚àß r ‚äÜ t :=
(forall_congr (by exact Œª x, imp_and_distrib)).trans forall_and_distrib

theorem subset_iff_inter_eq_left {s t : set Œ±} : s ‚äÜ t ‚Üî s ‚à© t = s :=
(ext_iff.trans $ forall_congr $ Œª x, and_iff_left_iff_imp).symm

theorem subset_iff_inter_eq_right {s t : set Œ±} : t ‚äÜ s ‚Üî s ‚à© t = t :=
(ext_iff.trans $ forall_congr $ Œª x, and_iff_right_iff_imp).symm

theorem inter_eq_self_of_subset_left {s t : set Œ±} : s ‚äÜ t ‚Üí s ‚à© t = s :=
subset_iff_inter_eq_left.1

theorem inter_eq_self_of_subset_right {s t : set Œ±} : t ‚äÜ s ‚Üí s ‚à© t = t :=
subset_iff_inter_eq_right.1

@[simp] theorem inter_univ (a : set Œ±) : a ‚à© univ = a :=
inter_eq_self_of_subset_left $ subset_univ _

@[simp] theorem univ_inter (a : set Œ±) : univ ‚à© a = a :=
inter_eq_self_of_subset_right $ subset_univ _

theorem inter_subset_inter {s‚ÇÅ s‚ÇÇ t‚ÇÅ t‚ÇÇ : set Œ±}
  (h‚ÇÅ : s‚ÇÅ ‚äÜ t‚ÇÅ) (h‚ÇÇ : s‚ÇÇ ‚äÜ t‚ÇÇ) : s‚ÇÅ ‚à© s‚ÇÇ ‚äÜ t‚ÇÅ ‚à© t‚ÇÇ := Œª x, and.imp (@h‚ÇÅ _) (@h‚ÇÇ _)

theorem inter_subset_inter_left {s t : set Œ±} (u : set Œ±) (H : s ‚äÜ t) : s ‚à© u ‚äÜ t ‚à© u :=
inter_subset_inter H subset.rfl

theorem inter_subset_inter_right {s t : set Œ±} (u : set Œ±) (H : s ‚äÜ t) : u ‚à© s ‚äÜ u ‚à© t :=
inter_subset_inter subset.rfl H

theorem union_inter_cancel_left {s t : set Œ±} : (s ‚à™ t) ‚à© s = s :=
subset_iff_inter_eq_right.1 $ subset_union_left _ _

theorem union_inter_cancel_right {s t : set Œ±} : (s ‚à™ t) ‚à© t = t :=
subset_iff_inter_eq_right.1 $ subset_union_right _ _

/-! ### Distributivity laws -/

theorem inter_distrib_left (s t u : set Œ±) : s ‚à© (t ‚à™ u) = (s ‚à© t) ‚à™ (s ‚à© u) :=
ext $ Œª x, and_or_distrib_left

theorem inter_distrib_right (s t u : set Œ±) : (s ‚à™ t) ‚à© u = (s ‚à© u) ‚à™ (t ‚à© u) :=
ext $ Œª x, or_and_distrib_right

theorem union_distrib_left (s t u : set Œ±) : s ‚à™ (t ‚à© u) = (s ‚à™ t) ‚à© (s ‚à™ u) :=
ext $ Œª x, or_and_distrib_left

theorem union_distrib_right (s t u : set Œ±) : (s ‚à© t) ‚à™ u = (s ‚à™ u) ‚à© (t ‚à™ u) :=
ext $ Œª x, and_or_distrib_right

/-!
### Lemmas about `insert`

`insert Œ± s` is the set `{Œ±} ‚à™ s`.
-/

theorem insert_def (x : Œ±) (s : set Œ±) : insert x s = { y | y = x ‚à® y ‚àà s } := rfl

@[simp] theorem subset_insert (x : Œ±) (s : set Œ±) : s ‚äÜ insert x s := Œª y, or.inr

theorem mem_insert (x : Œ±) (s : set Œ±) : x ‚àà insert x s := or.inl rfl

theorem mem_insert_of_mem {x : Œ±} {s : set Œ±} (y : Œ±) : x ‚àà s ‚Üí x ‚àà insert y s := or.inr

theorem eq_or_mem_of_mem_insert {x a : Œ±} {s : set Œ±} : x ‚àà insert a s ‚Üí x = a ‚à® x ‚àà s := id

theorem mem_of_mem_insert_of_ne {x a : Œ±} {s : set Œ±} : x ‚àà insert a s ‚Üí x ‚â† a ‚Üí x ‚àà s :=
or.resolve_left

@[simp] theorem mem_insert_iff {x a : Œ±} {s : set Œ±} : x ‚àà insert a s ‚Üî x = a ‚à® x ‚àà s := iff.rfl

@[simp] theorem insert_eq_of_mem {a : Œ±} {s : set Œ±} (h : a ‚àà s) : insert a s = s :=
ext $ Œª x, or_iff_right_of_imp $ Œª e, e.symm ‚ñ∏ h

lemma ne_insert_of_not_mem {s : set Œ±} (t : set Œ±) {a : Œ±} : a ‚àâ s ‚Üí s ‚â† insert a t :=
mt $ Œª e, e.symm ‚ñ∏ mem_insert _ _

theorem insert_subset : insert a s ‚äÜ t ‚Üî (a ‚àà t ‚àß s ‚äÜ t) :=
by simp only [subset_def, or_imp_distrib, forall_and_distrib, forall_eq, mem_insert_iff]

theorem insert_subset_insert (h : s ‚äÜ t) : insert a s ‚äÜ insert a t := Œª x, or.imp_right (@h _)

theorem ssubset_iff_insert {s t : set Œ±} : s ‚äÇ t ‚Üî ‚àÉ a ‚àâ s, insert a s ‚äÜ t :=
begin
  simp only [insert_subset, exists_and_distrib_right, ssubset_def, not_subset],
  simp only [exists_prop, and_comm]
end

theorem ssubset_insert {s : set Œ±} {a : Œ±} (h : a ‚àâ s) : s ‚äÇ insert a s :=
ssubset_iff_insert.2 ‚ü®a, h, subset.refl _‚ü©

theorem insert_comm (a b : Œ±) (s : set Œ±) : insert a (insert b s) = insert b (insert a s) :=
ext $ Œª x, or.left_comm

theorem insert_union : insert a s ‚à™ t = insert a (s ‚à™ t) := ext $ Œª x, or.assoc

@[simp] theorem union_insert : s ‚à™ insert a t = insert a (s ‚à™ t) := ext $ Œª x, or.left_comm

theorem insert_nonempty (a : Œ±) (s : set Œ±) : (insert a s).nonempty := ‚ü®a, mem_insert a s‚ü©

instance (a : Œ±) (s : set Œ±) : nonempty (insert a s : set Œ±) := (insert_nonempty a s).to_subtype

lemma insert_inter (x : Œ±) (s t : set Œ±) : insert x (s ‚à© t) = insert x s ‚à© insert x t :=
ext $ Œª y, or_and_distrib_left

-- useful in proofs by induction
theorem forall_of_forall_insert {P : Œ± ‚Üí Prop} {a : Œ±} {s : set Œ±}
  (H : ‚àÄ x, x ‚àà insert a s ‚Üí P x) (x) (h : x ‚àà s) : P x := H _ (or.inr h)

theorem forall_insert_of_forall {P : Œ± ‚Üí Prop} {a : Œ±} {s : set Œ±}
  (H : ‚àÄ x, x ‚àà s ‚Üí P x) (ha : P a) (x) (h : x ‚àà insert a s) : P x :=
h.elim (Œª e, e.symm ‚ñ∏ ha) (H _)

theorem bex_insert_iff {P : Œ± ‚Üí Prop} {a : Œ±} {s : set Œ±} :
  (‚àÉ x ‚àà insert a s, P x) ‚Üî P a ‚à® (‚àÉ x ‚àà s, P x) :=
bex_or_left_distrib.trans $ or_congr_left bex_eq_left

theorem ball_insert_iff {P : Œ± ‚Üí Prop} {a : Œ±} {s : set Œ±} :
  (‚àÄ x ‚àà insert a s, P x) ‚Üî P a ‚àß (‚àÄx ‚àà s, P x) :=
ball_or_left_distrib.trans $ and_congr_left' forall_eq

/-! ### Lemmas about singletons -/

theorem singleton_def (a : Œ±) : ({a} : set Œ±) = insert a ‚àÖ := (insert_emptyc_eq _).symm

@[simp] theorem mem_singleton_iff {a b : Œ±} : a ‚àà ({b} : set Œ±) ‚Üî a = b := iff.rfl

@[simp]
lemma set_of_eq_eq_singleton {a : Œ±} : {n | n = a} = {a} :=
ext $ Œª n, (set.mem_singleton_iff).symm

-- TODO: again, annotation needed
@[simp] theorem mem_singleton (a : Œ±) : a ‚àà ({a} : set Œ±) := @rfl _ _

theorem eq_of_mem_singleton {x y : Œ±} (h : x ‚àà ({y} : set Œ±)) : x = y := h

@[simp] theorem singleton_eq_singleton_iff {x y : Œ±} : {x} = ({y} : set Œ±) ‚Üî x = y :=
ext_iff.trans eq_iff_eq_cancel_left

theorem mem_singleton_of_eq {x y : Œ±} (H : x = y) : x ‚àà ({y} : set Œ±) := H

theorem insert_eq (x : Œ±) (s : set Œ±) : insert x s = ({x} : set Œ±) ‚à™ s := rfl

@[simp] theorem pair_eq_singleton (a : Œ±) : ({a, a} : set Œ±) = {a} := union_self _

theorem pair_comm (a b : Œ±) : ({a, b} : set Œ±) = {b, a} := union_comm _ _

@[simp] theorem singleton_nonempty (a : Œ±) : ({a} : set Œ±).nonempty :=
‚ü®a, rfl‚ü©

@[simp] theorem singleton_subset_iff {a : Œ±} {s : set Œ±} : {a} ‚äÜ s ‚Üî a ‚àà s := forall_eq

theorem set_compr_eq_eq_singleton {a : Œ±} : {b | b = a} = {a} := rfl

@[simp] theorem singleton_union : {a} ‚à™ s = insert a s := rfl

@[simp] theorem union_singleton : s ‚à™ {a} = insert a s := union_comm _ _

@[simp] theorem singleton_inter_nonempty : ({a} ‚à© s).nonempty ‚Üî a ‚àà s :=
by simp only [set.nonempty, mem_inter_eq, mem_singleton_iff, exists_eq_left]

@[simp] theorem inter_singleton_nonempty : (s ‚à© {a}).nonempty ‚Üî a ‚àà s :=
by rw [inter_comm, singleton_inter_nonempty]

@[simp] theorem singleton_inter_eq_empty : {a} ‚à© s = ‚àÖ ‚Üî a ‚àâ s :=
not_nonempty_iff_eq_empty.symm.trans $ not_congr singleton_inter_nonempty

@[simp] theorem inter_singleton_eq_empty : s ‚à© {a} = ‚àÖ ‚Üî a ‚àâ s :=
by rw [inter_comm, singleton_inter_eq_empty]

lemma nmem_singleton_empty {s : set Œ±} : s ‚àâ ({‚àÖ} : set (set Œ±)) ‚Üî s.nonempty :=
ne_empty_iff_nonempty

instance unique_singleton (a : Œ±) : unique ‚Ü•({a} : set Œ±) :=
‚ü®‚ü®‚ü®a, mem_singleton a‚ü©‚ü©, Œª ‚ü®x, h‚ü©, subtype.eq h‚ü©

lemma eq_singleton_iff_unique_mem {s : set Œ±} {a : Œ±} : s = {a} ‚Üî a ‚àà s ‚àß ‚àÄ x ‚àà s, x = a :=
subset.antisymm_iff.trans $ and.comm.trans $ and_congr_left' singleton_subset_iff

lemma eq_singleton_iff_nonempty_unique_mem {s : set Œ±} {a : Œ±} :
  s = {a} ‚Üî s.nonempty ‚àß ‚àÄ x ‚àà s, x = a :=
eq_singleton_iff_unique_mem.trans $ and_congr_left $ Œª H, ‚ü®Œª h', ‚ü®_, h'‚ü©, Œª ‚ü®x, h‚ü©, H x h ‚ñ∏ h‚ü©

/-! ### Lemmas about sets defined as `{x ‚àà s | p x}`. -/

theorem mem_sep {s : set Œ±} {p : Œ± ‚Üí Prop} {x : Œ±} (xs : x ‚àà s) (px : p x) : x ‚àà {x ‚àà s | p x} :=
‚ü®xs, px‚ü©

@[simp] theorem sep_mem_eq {s t : set Œ±} : {x ‚àà s | x ‚àà t} = s ‚à© t := rfl

@[simp] theorem mem_sep_eq {s : set Œ±} {p : Œ± ‚Üí Prop} {x : Œ±} :
  x ‚àà {x ‚àà s | p x} = (x ‚àà s ‚àß p x) := rfl

theorem mem_sep_iff {s : set Œ±} {p : Œ± ‚Üí Prop} {x : Œ±} : x ‚àà {x ‚àà s | p x} ‚Üî x ‚àà s ‚àß p x :=
iff.rfl

theorem eq_sep_of_subset {s t : set Œ±} (h : s ‚äÜ t) : s = {x ‚àà t | x ‚àà s} :=
(subset_iff_inter_eq_right.1 h).symm

theorem sep_subset (s : set Œ±) (p : Œ± ‚Üí Prop) : {x ‚àà s | p x} ‚äÜ s := Œª x, and.left

theorem forall_not_of_sep_empty {s : set Œ±} {p : Œ± ‚Üí Prop} (H : {x ‚àà s | p x} = ‚àÖ)
  (x) : x ‚àà s ‚Üí ¬¨ p x := not_and.1 (eq_empty_iff_forall_not_mem.1 H x : _)

@[simp] lemma sep_univ {Œ±} {p : Œ± ‚Üí Prop} : {a ‚àà (univ : set Œ±) | p a} = {a | p a} := univ_inter _

@[simp] lemma subset_singleton_iff {Œ± : Type*} {s : set Œ±} {x : Œ±} : s ‚äÜ {x} ‚Üî ‚àÄ y ‚àà s, y = x :=
iff.rfl

/-! ### Lemmas about complement -/

theorem mem_compl {s : set Œ±} {x : Œ±} (h : x ‚àâ s) : x ‚àà s·∂ú := h

lemma compl_set_of {Œ±} (p : Œ± ‚Üí Prop) : {a | p a}·∂ú = { a | ¬¨ p a } := rfl

theorem not_mem_of_mem_compl {s : set Œ±} {x : Œ±} (h : x ‚àà s·∂ú) : x ‚àâ s := h

@[simp] theorem mem_compl_eq (s : set Œ±) (x : Œ±) : x ‚àà s·∂ú = (x ‚àâ s) := rfl

theorem mem_compl_iff (s : set Œ±) (x : Œ±) : x ‚àà s·∂ú ‚Üî x ‚àâ s := iff.rfl

@[simp] theorem inter_compl_self (s : set Œ±) : s ‚à© s·∂ú = ‚àÖ := inf_compl_eq_bot

@[simp] theorem compl_inter_self (s : set Œ±) : s·∂ú ‚à© s = ‚àÖ := compl_inf_eq_bot

@[simp] theorem compl_empty : (‚àÖ : set Œ±)·∂ú = univ := compl_bot

@[simp] theorem compl_union (s t : set Œ±) : (s ‚à™ t)·∂ú = s·∂ú ‚à© t·∂ú := compl_sup

theorem compl_inter (s t : set Œ±) : (s ‚à© t)·∂ú = s·∂ú ‚à™ t·∂ú := compl_inf

@[simp] theorem compl_univ : (univ : set Œ±)·∂ú = ‚àÖ := compl_top

@[simp] lemma compl_empty_iff {s : set Œ±} : s·∂ú = ‚àÖ ‚Üî s = univ := compl_eq_bot

@[simp] lemma compl_univ_iff {s : set Œ±} : s·∂ú = univ ‚Üî s = ‚àÖ := compl_eq_top

lemma nonempty_compl {s : set Œ±} : s·∂ú.nonempty ‚Üî s ‚â† univ :=
ne_empty_iff_nonempty.symm.trans $ not_congr $ compl_empty_iff

lemma mem_compl_singleton_iff {a x : Œ±} : x ‚àà ({a} : set Œ±)·∂ú ‚Üî x ‚â† a :=
not_congr mem_singleton_iff

lemma compl_singleton_eq (a : Œ±) : ({a} : set Œ±)·∂ú = {x | x ‚â† a} :=
ext $ Œª x, mem_compl_singleton_iff

@[simp]
lemma compl_ne_eq_singleton (a : Œ±) : ({x | x ‚â† a} : set Œ±)·∂ú = {a} :=
by { ext, simp, }

theorem union_eq_compl_compl_inter_compl (s t : set Œ±) : s ‚à™ t = (s·∂ú ‚à© t·∂ú)·∂ú :=
ext $ Œª x, or_iff_not_and_not

theorem inter_eq_compl_compl_union_compl (s t : set Œ±) : s ‚à© t = (s·∂ú ‚à™ t·∂ú)·∂ú :=
ext $ Œª x, and_iff_not_or_not

@[simp] theorem union_compl_self (s : set Œ±) : s ‚à™ s·∂ú = univ := eq_univ_iff_forall.2 $ Œª x, em _

@[simp] theorem compl_union_self (s : set Œ±) : s·∂ú ‚à™ s = univ := by rw [union_comm, union_compl_self]

theorem compl_comp_compl : compl ‚àò compl = @id (set Œ±) := funext compl_compl

theorem compl_subset_comm {s t : set Œ±} : s·∂ú ‚äÜ t ‚Üî t·∂ú ‚äÜ s := @compl_le_iff_compl_le _ s t _

@[simp] lemma compl_subset_compl {s t : set Œ±} : s·∂ú ‚äÜ t·∂ú ‚Üî t ‚äÜ s := @compl_le_compl_iff_le _ t s _

theorem compl_subset_iff_union {s t : set Œ±} : s·∂ú ‚äÜ t ‚Üî s ‚à™ t = univ :=
iff.symm $ eq_univ_iff_forall.trans $ forall_congr $ Œª a, or_iff_not_imp_left

theorem subset_compl_comm {s t : set Œ±} : s ‚äÜ t·∂ú ‚Üî t ‚äÜ s·∂ú :=
forall_congr $ Œª a, imp_not_comm

theorem subset_compl_iff_disjoint {s t : set Œ±} : s ‚äÜ t·∂ú ‚Üî s ‚à© t = ‚àÖ :=
iff.trans (forall_congr $ Œª a, and_imp.symm) subset_empty_iff

lemma subset_compl_singleton_iff {a : Œ±} {s : set Œ±} : s ‚äÜ {a}·∂ú ‚Üî a ‚àâ s :=
subset_compl_comm.trans singleton_subset_iff

theorem inter_subset (a b c : set Œ±) : a ‚à© b ‚äÜ c ‚Üî a ‚äÜ b·∂ú ‚à™ c :=
forall_congr $ Œª x, and_imp.trans $ imp_congr_right $ Œª _, imp_iff_not_or

lemma inter_compl_nonempty_iff {s t : set Œ±} : (s ‚à© t·∂ú).nonempty ‚Üî ¬¨ s ‚äÜ t :=
(not_subset.trans $ exists_congr $ by exact Œª x, by simp [mem_compl]).symm

/-! ### Lemmas about set difference -/

theorem diff_eq (s t : set Œ±) : s \ t = s ‚à© t·∂ú := rfl

@[simp] theorem mem_diff {s t : set Œ±} (x : Œ±) : x ‚àà s \ t ‚Üî x ‚àà s ‚àß x ‚àâ t := iff.rfl

theorem mem_diff_of_mem {s t : set Œ±} {x : Œ±} (h1 : x ‚àà s) (h2 : x ‚àâ t) : x ‚àà s \ t :=
‚ü®h1, h2‚ü©

theorem mem_of_mem_diff {s t : set Œ±} {x : Œ±} (h : x ‚àà s \ t) : x ‚àà s :=
h.left

theorem not_mem_of_mem_diff {s t : set Œ±} {x : Œ±} (h : x ‚àà s \ t) : x ‚àâ t :=
h.right

theorem diff_eq_compl_inter {s t : set Œ±} : s \ t = t·∂ú ‚à© s :=
by rw [diff_eq, inter_comm]

theorem nonempty_diff {s t : set Œ±} : (s \ t).nonempty ‚Üî ¬¨ (s ‚äÜ t) := inter_compl_nonempty_iff

theorem diff_subset (s t : set Œ±) : s \ t ‚äÜ s := inter_subset_left _ _

theorem union_diff_cancel' {s t u : set Œ±} (h‚ÇÅ : s ‚äÜ t) (h‚ÇÇ : t ‚äÜ u) : t ‚à™ (u \ s) = u :=
by finish [ext_iff, iff_def, subset_def]

theorem union_diff_cancel {s t : set Œ±} (h : s ‚äÜ t) : s ‚à™ (t \ s) = t :=
union_diff_cancel' (subset.refl s) h

theorem union_diff_cancel_left {s t : set Œ±} (h : s ‚à© t ‚äÜ ‚àÖ) : (s ‚à™ t) \ s = t :=
by finish [ext_iff, iff_def, subset_def]

theorem union_diff_cancel_right {s t : set Œ±} (h : s ‚à© t ‚äÜ ‚àÖ) : (s ‚à™ t) \ t = s :=
by finish [ext_iff, iff_def, subset_def]

@[simp] theorem union_diff_left {s t : set Œ±} : (s ‚à™ t) \ s = t \ s :=
by finish [ext_iff, iff_def]

@[simp] theorem union_diff_right {s t : set Œ±} : (s ‚à™ t) \ t = s \ t :=
by finish [ext_iff, iff_def]

theorem union_diff_distrib {s t u : set Œ±} : (s ‚à™ t) \ u = s \ u ‚à™ t \ u :=
inter_distrib_right _ _ _

theorem inter_union_distrib_left {s t u : set Œ±} : s ‚à© (t ‚à™ u) = (s ‚à© t) ‚à™ (s ‚à© u) :=
ext $ Œª _, and_or_distrib_left

theorem inter_union_distrib_right {s t u : set Œ±} : (s ‚à© t) ‚à™ u = (s ‚à™ u) ‚à© (t ‚à™ u) :=
ext $ Œª _, and_or_distrib_right

theorem union_inter_distrib_left {s t u : set Œ±} : s ‚à™ (t ‚à© u) = (s ‚à™ t) ‚à© (s ‚à™ u) :=
ext $ Œª _, or_and_distrib_left

theorem union_inter_distrib_right {s t u : set Œ±} : (s ‚à™ t) ‚à© u = (s ‚à© u) ‚à™ (t ‚à© u) :=
ext $ Œª _, or_and_distrib_right

theorem inter_diff_assoc (a b c : set Œ±) : (a ‚à© b) \ c = a ‚à© (b \ c) :=
inter_assoc _ _ _

@[simp] theorem inter_diff_self (a b : set Œ±) : a ‚à© (b \ a) = ‚àÖ :=
by finish [ext_iff]

@[simp] theorem inter_union_diff (s t : set Œ±) : (s ‚à© t) ‚à™ (s \ t) = s :=
by finish [ext_iff, iff_def]

@[simp] theorem inter_union_compl (s t : set Œ±) : (s ‚à© t) ‚à™ (s ‚à© t·∂ú) = s := inter_union_diff _ _

theorem diff_subset_diff {s‚ÇÅ s‚ÇÇ t‚ÇÅ t‚ÇÇ : set Œ±} : s‚ÇÅ ‚äÜ s‚ÇÇ ‚Üí t‚ÇÇ ‚äÜ t‚ÇÅ ‚Üí s‚ÇÅ \ t‚ÇÅ ‚äÜ s‚ÇÇ \ t‚ÇÇ :=
by finish [subset_def]

theorem diff_subset_diff_left {s‚ÇÅ s‚ÇÇ t : set Œ±} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) : s‚ÇÅ \ t ‚äÜ s‚ÇÇ \ t :=
diff_subset_diff h (by refl)

theorem diff_subset_diff_right {s t u : set Œ±} (h : t ‚äÜ u) : s \ u ‚äÜ s \ t :=
diff_subset_diff (subset.refl s) h

theorem compl_eq_univ_diff (s : set Œ±) : s·∂ú = univ \ s :=
by finish [ext_iff]

@[simp] lemma empty_diff (s : set Œ±) : (‚àÖ \ s : set Œ±) = ‚àÖ :=
eq_empty_of_subset_empty $ assume x ‚ü®hx, _‚ü©, hx

theorem diff_eq_empty {s t : set Œ±} : s \ t = ‚àÖ ‚Üî s ‚äÜ t :=
‚ü®assume h x hx, by_contradiction $ assume : x ‚àâ t, show x ‚àà (‚àÖ : set Œ±), from h ‚ñ∏ ‚ü®hx, this‚ü©,
  assume h, eq_empty_of_subset_empty $ assume x ‚ü®hx, hnx‚ü©, hnx $ h hx‚ü©

@[simp] theorem diff_empty {s : set Œ±} : s \ ‚àÖ = s :=
ext $ assume x, ‚ü®assume ‚ü®hx, _‚ü©, hx, assume h, ‚ü®h, not_false‚ü©‚ü©

@[simp] lemma diff_univ (s : set Œ±) : s \ univ = ‚àÖ := diff_eq_empty.2 (subset_univ s)

theorem diff_diff {u : set Œ±} : s \ t \ u = s \ (t ‚à™ u) :=
ext $ by simp [not_or_distrib, and.comm, and.left_comm]

-- the following statement contains parentheses to help the reader
lemma diff_diff_comm {s t u : set Œ±} : (s \ t) \ u = (s \ u) \ t :=
by simp_rw [diff_diff, union_comm]

lemma diff_subset_iff {s t u : set Œ±} : s \ t ‚äÜ u ‚Üî s ‚äÜ t ‚à™ u :=
‚ü®assume h x xs, classical.by_cases or.inl (assume nxt, or.inr (h ‚ü®xs, nxt‚ü©)),
 assume h x ‚ü®xs, nxt‚ü©, or.resolve_left (h xs) nxt‚ü©

lemma subset_diff_union (s t : set Œ±) : s ‚äÜ (s \ t) ‚à™ t :=
by rw [union_comm, ‚Üêdiff_subset_iff]

@[simp] lemma diff_singleton_subset_iff {x : Œ±} {s t : set Œ±} : s \ {x} ‚äÜ t ‚Üî s ‚äÜ insert x t :=
by { rw [‚Üêunion_singleton, union_comm], apply diff_subset_iff }

lemma subset_diff_singleton {x : Œ±} {s t : set Œ±} (h : s ‚äÜ t) (hx : x ‚àâ s) : s ‚äÜ t \ {x} :=
subset_inter h $ subset_compl_comm.1 $ singleton_subset_iff.2 hx

lemma subset_insert_diff_singleton (x : Œ±) (s : set Œ±) : s ‚äÜ insert x (s \ {x}) :=
by rw [‚Üêdiff_singleton_subset_iff]

lemma diff_subset_comm {s t u : set Œ±} : s \ t ‚äÜ u ‚Üî s \ u ‚äÜ t :=
by rw [diff_subset_iff, diff_subset_iff, union_comm]

lemma diff_inter {s t u : set Œ±} : s \ (t ‚à© u) = (s \ t) ‚à™ (s \ u) :=
ext $ Œª x, by simp [not_and_distrib, and_or_distrib_left]

lemma diff_inter_diff {s t u : set Œ±} : s \ t ‚à© (s \ u) = s \ (t ‚à™ u) :=
by { ext x, simp only [mem_inter_eq, mem_union_eq, mem_diff, not_or_distrib, and.left_comm,
  and.assoc, and_self_left] }

lemma diff_compl : s \ t·∂ú = s ‚à© t := by rw [diff_eq, compl_compl]

lemma diff_diff_right {s t u : set Œ±} : s \ (t \ u) = (s \ t) ‚à™ (s ‚à© u) :=
by rw [diff_eq t u, diff_inter, diff_compl]

@[simp] theorem insert_diff_of_mem (s) (h : a ‚àà t) : insert a s \ t = s \ t :=
by { ext, split; simp [or_imp_distrib, h] {contextual := tt} }

theorem insert_diff_of_not_mem (s) (h : a ‚àâ t) : insert a s \ t = insert a (s \ t) :=
begin
  classical,
  ext x,
  by_cases h' : x ‚àà t,
  { have : x ‚â† a,
    { assume H,
      rw H at h',
      exact h h' },
    simp [h, h', this] },
  { simp [h, h'] }
end

lemma insert_diff_self_of_not_mem {a : Œ±} {s : set Œ±} (h : a ‚àâ s) :
  insert a s \ {a} = s :=
by { ext, simp [and_iff_left_of_imp (Œª hx : x ‚àà s, show x ‚â† a, from Œª hxa, h $ hxa ‚ñ∏ hx)] }

theorem union_diff_self {s t : set Œ±} : s ‚à™ (t \ s) = s ‚à™ t :=
by finish [ext_iff, iff_def]

theorem diff_union_self {s t : set Œ±} : (s \ t) ‚à™ t = s ‚à™ t :=
by rw [union_comm, union_diff_self, union_comm]

theorem diff_inter_self {a b : set Œ±} : (b \ a) ‚à© a = ‚àÖ :=
by { ext, by simp [iff_def] {contextual:=tt} }

theorem diff_inter_self_eq_diff {s t : set Œ±} : s \ (t ‚à© s) = s \ t :=
by { ext, simp [iff_def] {contextual := tt} }

theorem diff_self_inter {s t : set Œ±} : s \ (s ‚à© t) = s \ t :=
by rw [inter_comm, diff_inter_self_eq_diff]

theorem diff_eq_self {s t : set Œ±} : s \ t = s ‚Üî t ‚à© s ‚äÜ ‚àÖ :=
by finish [ext_iff, iff_def, subset_def]

@[simp] theorem diff_singleton_eq_self {a : Œ±} {s : set Œ±} (h : a ‚àâ s) : s \ {a} = s :=
diff_eq_self.2 $ by simp [singleton_inter_eq_empty.2 h]

@[simp] theorem insert_diff_singleton {a : Œ±} {s : set Œ±} :
  insert a (s \ {a}) = insert a s :=
by simp [insert_eq, union_diff_self, -union_singleton, -singleton_union]

@[simp] lemma diff_self {s : set Œ±} : s \ s = ‚àÖ := by { ext, simp }

lemma diff_diff_cancel_left {s t : set Œ±} (h : s ‚äÜ t) : t \ (t \ s) = s :=
by simp only [diff_diff_right, diff_self, inter_eq_self_of_subset_right h, empty_union]

lemma mem_diff_singleton {x y : Œ±} {s : set Œ±} : x ‚àà s \ {y} ‚Üî (x ‚àà s ‚àß x ‚â† y) :=
iff.rfl

lemma mem_diff_singleton_empty {s : set Œ±} {t : set (set Œ±)} :
  s ‚àà t \ {‚àÖ} ‚Üî (s ‚àà t ‚àß s.nonempty) :=
mem_diff_singleton.trans $ and_congr iff.rfl ne_empty_iff_nonempty

/-! ### Powerset -/

theorem mem_powerset {x s : set Œ±} (h : x ‚äÜ s) : x ‚àà powerset s := h

theorem subset_of_mem_powerset {x s : set Œ±} (h : x ‚àà powerset s) : x ‚äÜ s := h

@[simp] theorem mem_powerset_iff (x s : set Œ±) : x ‚àà powerset s ‚Üî x ‚äÜ s := iff.rfl

theorem powerset_inter (s t : set Œ±) : ùí´ (s ‚à© t) = ùí´ s ‚à© ùí´ t :=
ext $ Œª u, subset_inter_iff

@[simp] theorem powerset_mono : ùí´ s ‚äÜ ùí´ t ‚Üî s ‚äÜ t :=
‚ü®Œª h, h (subset.refl s), Œª h u hu, subset.trans hu h‚ü©

theorem monotone_powerset : monotone (powerset : set Œ± ‚Üí set (set Œ±)) :=
Œª s t, powerset_mono.2

@[simp] theorem powerset_nonempty : (ùí´ s).nonempty :=
‚ü®‚àÖ, empty_subset s‚ü©

@[simp] theorem powerset_empty : ùí´ (‚àÖ : set Œ±) = {‚àÖ} :=
ext $ Œª s, subset_empty_iff

@[simp] theorem powerset_univ : ùí´ (univ : set Œ±) = univ :=
eq_univ_of_forall subset_univ

/-! ### If-then-else for sets -/

/-- `ite` for sets: `set.ite t s s' ‚à© t = s ‚à© t`, `set.ite t s s' ‚à© t·∂ú = s' ‚à© t·∂ú`.
Defined as `s ‚à© t ‚à™ s' \ t`. -/
protected def ite (t s s' : set Œ±) : set Œ± := s ‚à© t ‚à™ s' \ t

@[simp] lemma ite_inter_self (t s s' : set Œ±) : t.ite s s' ‚à© t = s ‚à© t :=
by rw [set.ite, union_inter_distrib_right, diff_inter_self, inter_assoc, inter_self, union_empty]

@[simp] lemma ite_compl (t s s' : set Œ±) : t·∂ú.ite s s' = t.ite s' s :=
by rw [set.ite, set.ite, diff_compl, union_comm, diff_eq]

@[simp] lemma ite_inter_compl_self (t s s' : set Œ±) : t.ite s s' ‚à© t·∂ú = s' ‚à© t·∂ú :=
by rw [‚Üê ite_compl, ite_inter_self]

@[simp] lemma ite_diff_self (t s s' : set Œ±) : t.ite s s' \ t = s' \ t :=
ite_inter_compl_self t s s'

@[simp] lemma ite_same (t s : set Œ±) : t.ite s s = s := inter_union_diff _ _

@[simp] lemma ite_empty (s s' : set Œ±) : set.ite ‚àÖ s s' = s' :=
by simp [set.ite]

@[simp] lemma ite_univ (s s' : set Œ±) : set.ite univ s s' = s :=
by simp [set.ite]

@[simp] lemma ite_empty_left (t s : set Œ±) : t.ite ‚àÖ s = s \ t :=
by simp [set.ite]

@[simp] lemma ite_empty_right (t s : set Œ±) : t.ite s ‚àÖ = s ‚à© t :=
by simp [set.ite]

lemma ite_mono (t : set Œ±) {s‚ÇÅ s‚ÇÅ' s‚ÇÇ s‚ÇÇ' : set Œ±} (h : s‚ÇÅ ‚äÜ s‚ÇÇ) (h' : s‚ÇÅ' ‚äÜ s‚ÇÇ') :
  t.ite s‚ÇÅ s‚ÇÅ' ‚äÜ t.ite s‚ÇÇ s‚ÇÇ' :=
union_subset_union (inter_subset_inter_left _ h) (inter_subset_inter_left _ h')

lemma ite_subset_union (t s s' : set Œ±) : t.ite s s' ‚äÜ s ‚à™ s' :=
union_subset_union (inter_subset_left _ _) (diff_subset _ _)

lemma inter_subset_ite (t s s' : set Œ±) : s ‚à© s' ‚äÜ t.ite s s' :=
ite_same t (s ‚à© s') ‚ñ∏ ite_mono _ (inter_subset_left _ _) (inter_subset_right _ _)

lemma ite_inter_inter (t s‚ÇÅ s‚ÇÇ s‚ÇÅ' s‚ÇÇ' : set Œ±) :
  t.ite (s‚ÇÅ ‚à© s‚ÇÇ) (s‚ÇÅ' ‚à© s‚ÇÇ') = t.ite s‚ÇÅ s‚ÇÅ' ‚à© t.ite s‚ÇÇ s‚ÇÇ' :=
by { ext x, finish [set.ite, iff_def] }

lemma ite_inter (t s‚ÇÅ s‚ÇÇ s : set Œ±) :
  t.ite (s‚ÇÅ ‚à© s) (s‚ÇÇ ‚à© s) = t.ite s‚ÇÅ s‚ÇÇ ‚à© s :=
by rw [ite_inter_inter, ite_same]

/-! ### Inverse image -/

/-- The preimage of `s : set Œ≤` by `f : Œ± ‚Üí Œ≤`, written `f ‚Åª¬π' s`,
  is the set of `x : Œ±` such that `f x ‚àà s`. -/
def preimage {Œ± : Type u} {Œ≤ : Type v} (f : Œ± ‚Üí Œ≤) (s : set Œ≤) : set Œ± := {x | f x ‚àà s}

infix ` ‚Åª¬π' `:80 := preimage

section preimage
variables {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ≥}

@[simp] theorem preimage_empty : f ‚Åª¬π' ‚àÖ = ‚àÖ := rfl

@[simp] theorem mem_preimage {s : set Œ≤} {a : Œ±} : (a ‚àà f ‚Åª¬π' s) ‚Üî (f a ‚àà s) := iff.rfl

lemma preimage_congr {f g : Œ± ‚Üí Œ≤} {s : set Œ≤} (h : ‚àÄ (x : Œ±), f x = g x) : f ‚Åª¬π' s = g ‚Åª¬π' s :=
by { congr' with x, apply_assumption }

theorem preimage_mono {s t : set Œ≤} (h : s ‚äÜ t) : f ‚Åª¬π' s ‚äÜ f ‚Åª¬π' t :=
assume x hx, h hx

@[simp] theorem preimage_univ : f ‚Åª¬π' univ = univ := rfl

theorem subset_preimage_univ {s : set Œ±} : s ‚äÜ f ‚Åª¬π' univ := subset_univ _

@[simp] theorem preimage_inter {s t : set Œ≤} : f ‚Åª¬π' (s ‚à© t) = f ‚Åª¬π' s ‚à© f ‚Åª¬π' t := rfl

@[simp] theorem preimage_union {s t : set Œ≤} : f ‚Åª¬π' (s ‚à™ t) = f ‚Åª¬π' s ‚à™ f ‚Åª¬π' t := rfl

@[simp] theorem preimage_compl {s : set Œ≤} : f ‚Åª¬π' s·∂ú = (f ‚Åª¬π' s)·∂ú := rfl

@[simp] theorem preimage_diff (f : Œ± ‚Üí Œ≤) (s t : set Œ≤) :
  f ‚Åª¬π' (s \ t) = f ‚Åª¬π' s \ f ‚Åª¬π' t := rfl

@[simp] theorem preimage_set_of_eq {p : Œ± ‚Üí Prop} {f : Œ≤ ‚Üí Œ±} : f ‚Åª¬π' {a | p a} = {a | p (f a)} :=
rfl

@[simp] theorem preimage_id {s : set Œ±} : id ‚Åª¬π' s = s := rfl

@[simp] theorem preimage_id' {s : set Œ±} : (Œª x, x) ‚Åª¬π' s = s := rfl

theorem preimage_const_of_mem {b : Œ≤} {s : set Œ≤} (h : b ‚àà s) :
  (Œª (x : Œ±), b) ‚Åª¬π' s = univ :=
eq_univ_of_forall $ Œª x, h

theorem preimage_const_of_not_mem {b : Œ≤} {s : set Œ≤} (h : b ‚àâ s) :
  (Œª (x : Œ±), b) ‚Åª¬π' s = ‚àÖ :=
eq_empty_of_subset_empty $ Œª x hx, h hx

theorem preimage_const (b : Œ≤) (s : set Œ≤) [decidable (b ‚àà s)] :
  (Œª (x : Œ±), b) ‚Åª¬π' s = if b ‚àà s then univ else ‚àÖ :=
by { split_ifs with hb hb, exacts [preimage_const_of_mem hb, preimage_const_of_not_mem hb] }

theorem preimage_comp {s : set Œ≥} : (g ‚àò f) ‚Åª¬π' s = f ‚Åª¬π' (g ‚Åª¬π' s) := rfl

lemma preimage_preimage {g : Œ≤ ‚Üí Œ≥} {f : Œ± ‚Üí Œ≤} {s : set Œ≥} :
  f ‚Åª¬π' (g ‚Åª¬π' s) = (Œª x, g (f x)) ‚Åª¬π' s :=
preimage_comp.symm

theorem eq_preimage_subtype_val_iff {p : Œ± ‚Üí Prop} {s : set (subtype p)} {t : set Œ±} :
  s = subtype.val ‚Åª¬π' t ‚Üî (‚àÄx (h : p x), (‚ü®x, h‚ü© : subtype p) ‚àà s ‚Üî x ‚àà t) :=
‚ü®assume s_eq x h, by { rw [s_eq], simp },
 assume h, ext $ Œª ‚ü®x, hx‚ü©, by simp [h]‚ü©

lemma preimage_coe_coe_diagonal {Œ± : Type*} (s : set Œ±) :
  (prod.map coe coe) ‚Åª¬π' (diagonal Œ±) = diagonal s :=
begin
  ext ‚ü®‚ü®x, x_in‚ü©, ‚ü®y, y_in‚ü©‚ü©,
  simp [set.diagonal],
end

end preimage

/-! ### Image of a set under a function -/

section image

infix ` '' `:80 := image

theorem mem_image_iff_bex {f : Œ± ‚Üí Œ≤} {s : set Œ±} {y : Œ≤} :
  y ‚àà f '' s ‚Üî ‚àÉ x (_ : x ‚àà s), f x = y := bex_def.symm

theorem mem_image_eq (f : Œ± ‚Üí Œ≤) (s : set Œ±) (y: Œ≤) : y ‚àà f '' s = ‚àÉ x, x ‚àà s ‚àß f x = y := rfl

@[simp] theorem mem_image (f : Œ± ‚Üí Œ≤) (s : set Œ±) (y : Œ≤) :
  y ‚àà f '' s ‚Üî ‚àÉ x, x ‚àà s ‚àß f x = y := iff.rfl

lemma image_eta (f : Œ± ‚Üí Œ≤) : f '' s = (Œª x, f x) '' s := rfl

theorem mem_image_of_mem (f : Œ± ‚Üí Œ≤) {x : Œ±} {a : set Œ±} (h : x ‚àà a) : f x ‚àà f '' a :=
‚ü®_, h, rfl‚ü©

theorem mem_image_of_injective {f : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} (hf : injective f) :
  f a ‚àà f '' s ‚Üî a ‚àà s :=
iff.intro
  (assume ‚ü®b, hb, eq‚ü©, (hf eq) ‚ñ∏ hb)
  (assume h, mem_image_of_mem _ h)

theorem ball_image_iff {f : Œ± ‚Üí Œ≤} {s : set Œ±} {p : Œ≤ ‚Üí Prop} :
  (‚àÄ y ‚àà f '' s, p y) ‚Üî (‚àÄ x ‚àà s, p (f x)) :=
by simp

theorem ball_image_of_ball {f : Œ± ‚Üí Œ≤} {s : set Œ±} {p : Œ≤ ‚Üí Prop}
  (h : ‚àÄ x ‚àà s, p (f x)) : ‚àÄ y ‚àà f '' s, p y :=
ball_image_iff.2 h

theorem bex_image_iff {f : Œ± ‚Üí Œ≤} {s : set Œ±} {p : Œ≤ ‚Üí Prop} :
  (‚àÉ y ‚àà f '' s, p y) ‚Üî (‚àÉ x ‚àà s, p (f x)) :=
by simp

theorem mem_image_elim {f : Œ± ‚Üí Œ≤} {s : set Œ±} {C : Œ≤ ‚Üí Prop} (h : ‚àÄ (x : Œ±), x ‚àà s ‚Üí C (f x)) :
 ‚àÄ{y : Œ≤}, y ‚àà f '' s ‚Üí C y
| ._ ‚ü®a, a_in, rfl‚ü© := h a a_in

theorem mem_image_elim_on {f : Œ± ‚Üí Œ≤} {s : set Œ±} {C : Œ≤ ‚Üí Prop} {y : Œ≤} (h_y : y ‚àà f '' s)
  (h : ‚àÄ (x : Œ±), x ‚àà s ‚Üí C (f x)) : C y :=
mem_image_elim h h_y

@[congr] lemma image_congr {f g : Œ± ‚Üí Œ≤} {s : set Œ±}
  (h : ‚àÄa‚ààs, f a = g a) : f '' s = g '' s :=
by safe [ext_iff, iff_def]

/-- A common special case of `image_congr` -/
lemma image_congr' {f g : Œ± ‚Üí Œ≤} {s : set Œ±} (h : ‚àÄ (x : Œ±), f x = g x) : f '' s = g '' s :=
image_congr (Œªx _, h x)

theorem image_comp (f : Œ≤ ‚Üí Œ≥) (g : Œ± ‚Üí Œ≤) (a : set Œ±) : (f ‚àò g) '' a = f '' (g '' a) :=
subset.antisymm
  (ball_image_of_ball $ assume a ha, mem_image_of_mem _ $ mem_image_of_mem _ ha)
  (ball_image_of_ball $ ball_image_of_ball $ assume a ha, mem_image_of_mem _ ha)

/-- A variant of `image_comp`, useful for rewriting -/
lemma image_image (g : Œ≤ ‚Üí Œ≥) (f : Œ± ‚Üí Œ≤) (s : set Œ±) : g '' (f '' s) = (Œª x, g (f x)) '' s :=
(image_comp g f s).symm

/-- Image is monotone with respect to `‚äÜ`. See `set.monotone_image` for the statement in
terms of `‚â§`. -/
theorem image_subset {a b : set Œ±} (f : Œ± ‚Üí Œ≤) (h : a ‚äÜ b) : f '' a ‚äÜ f '' b :=
by finish [subset_def, mem_image_eq]

theorem image_union (f : Œ± ‚Üí Œ≤) (s t : set Œ±) :
  f '' (s ‚à™ t) = f '' s ‚à™ f '' t :=
by finish [ext_iff, iff_def, mem_image_eq]

@[simp] theorem image_empty (f : Œ± ‚Üí Œ≤) : f '' ‚àÖ = ‚àÖ := by { ext, simp }

lemma image_inter_subset (f : Œ± ‚Üí Œ≤) (s t : set Œ±) :
  f '' (s ‚à© t) ‚äÜ f '' s ‚à© f '' t :=
subset_inter (image_subset _ $ inter_subset_left _ _) (image_subset _ $ inter_subset_right _ _)

theorem image_inter_on {f : Œ± ‚Üí Œ≤} {s t : set Œ±} (h : ‚àÄx‚ààt, ‚àÄy‚ààs, f x = f y ‚Üí x = y) :
  f '' s ‚à© f '' t = f '' (s ‚à© t) :=
subset.antisymm
  (assume b ‚ü®‚ü®a‚ÇÅ, ha‚ÇÅ, h‚ÇÅ‚ü©, ‚ü®a‚ÇÇ, ha‚ÇÇ, h‚ÇÇ‚ü©‚ü©,
    have a‚ÇÇ = a‚ÇÅ, from h _ ha‚ÇÇ _ ha‚ÇÅ (by simp *),
    ‚ü®a‚ÇÅ, ‚ü®ha‚ÇÅ, this ‚ñ∏ ha‚ÇÇ‚ü©, h‚ÇÅ‚ü©)
  (image_inter_subset _ _ _)

theorem image_inter {f : Œ± ‚Üí Œ≤} {s t : set Œ±} (H : injective f) :
  f '' s ‚à© f '' t = f '' (s ‚à© t) :=
image_inter_on (assume x _ y _ h, H h)

theorem image_univ_of_surjective {Œπ : Type*} {f : Œπ ‚Üí Œ≤} (H : surjective f) : f '' univ = univ :=
eq_univ_of_forall $ by { simpa [image] }

@[simp] theorem image_singleton {f : Œ± ‚Üí Œ≤} {a : Œ±} : f '' {a} = {f a} :=
by { ext, simp [image, eq_comm] }

@[simp] theorem nonempty.image_const {s : set Œ±} (hs : s.nonempty) (a : Œ≤) : (Œª _, a) '' s = {a} :=
ext $ Œª x, ‚ü®Œª ‚ü®y, _, h‚ü©, h ‚ñ∏ mem_singleton _,
  Œª h, (eq_of_mem_singleton h).symm ‚ñ∏ hs.imp (Œª y hy, ‚ü®hy, rfl‚ü©)‚ü©

@[simp] lemma image_eq_empty {Œ± Œ≤} {f : Œ± ‚Üí Œ≤} {s : set Œ±} : f '' s = ‚àÖ ‚Üî s = ‚àÖ :=
by { simp only [eq_empty_iff_forall_not_mem],
     exact ‚ü®Œª H a ha, H _ ‚ü®_, ha, rfl‚ü©, Œª H b ‚ü®_, ha, _‚ü©, H _ ha‚ü© }

-- TODO(Jeremy): there is an issue with - t unfolding to compl t
theorem mem_compl_image (t : set Œ±) (S : set (set Œ±)) :
  t ‚àà compl '' S ‚Üî t·∂ú ‚àà S :=
begin
  suffices : ‚àÄ x, x·∂ú = t ‚Üî t·∂ú = x, { simp [this] },
  intro x, split; { intro e, subst e, simp }
end

/-- A variant of `image_id` -/
@[simp] lemma image_id' (s : set Œ±) : (Œªx, x) '' s = s := by { ext, simp }

theorem image_id (s : set Œ±) : id '' s = s := by simp

theorem compl_compl_image (S : set (set Œ±)) :
  compl '' (compl '' S) = S :=
by rw [‚Üê image_comp, compl_comp_compl, image_id]

theorem image_insert_eq {f : Œ± ‚Üí Œ≤} {a : Œ±} {s : set Œ±} :
  f '' (insert a s) = insert (f a) (f '' s) :=
by { ext, simp [and_or_distrib_left, exists_or_distrib, eq_comm, or_comm, and_comm] }

theorem image_pair (f : Œ± ‚Üí Œ≤) (a b : Œ±) : f '' {a, b} = {f a, f b} :=
by simp only [image_insert_eq, image_singleton]

theorem image_subset_preimage_of_inverse {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}
  (I : left_inverse g f) (s : set Œ±) : f '' s ‚äÜ g ‚Åª¬π' s :=
Œª b ‚ü®a, h, e‚ü©, e ‚ñ∏ ((I a).symm ‚ñ∏ h : g (f a) ‚àà s)

theorem preimage_subset_image_of_inverse {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}
  (I : left_inverse g f) (s : set Œ≤) : f ‚Åª¬π' s ‚äÜ g '' s :=
Œª b h, ‚ü®f b, h, I b‚ü©

theorem image_eq_preimage_of_inverse {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±}
  (h‚ÇÅ : left_inverse g f) (h‚ÇÇ : right_inverse g f) :
  image f = preimage g :=
funext $ Œª s, subset.antisymm
  (image_subset_preimage_of_inverse h‚ÇÅ s)
  (preimage_subset_image_of_inverse h‚ÇÇ s)

theorem mem_image_iff_of_inverse {f : Œ± ‚Üí Œ≤} {g : Œ≤ ‚Üí Œ±} {b : Œ≤} {s : set Œ±}
  (h‚ÇÅ : left_inverse g f) (h‚ÇÇ : right_inverse g f) :
  b ‚àà f '' s ‚Üî g b ‚àà s :=
by rw image_eq_preimage_of_inverse h‚ÇÅ h‚ÇÇ; refl

theorem image_compl_subset {f : Œ± ‚Üí Œ≤} {s : set Œ±} (H : injective f) : f '' s·∂ú ‚äÜ (f '' s)·∂ú :=
subset_compl_iff_disjoint.2 $ by simp [image_inter H]

theorem subset_image_compl {f : Œ± ‚Üí Œ≤} {s : set Œ±} (H : surjective f) : (f '' s)·∂ú ‚äÜ f '' s·∂ú :=
compl_subset_iff_union.2 $
by { rw ‚Üê image_union, simp [image_univ_of_surjective H] }

theorem image_compl_eq {f : Œ± ‚Üí Œ≤} {s : set Œ±} (H : bijective f) : f '' s·∂ú = (f '' s)·∂ú :=
subset.antisymm (image_compl_subset H.1) (subset_image_compl H.2)

theorem subset_image_diff (f : Œ± ‚Üí Œ≤) (s t : set Œ±) :
  f '' s \ f '' t ‚äÜ f '' (s \ t) :=
begin
  rw [diff_subset_iff, ‚Üê image_union, union_diff_self],
  exact image_subset f (subset_union_right t s)
end

theorem image_diff {f : Œ± ‚Üí Œ≤} (hf : injective f) (s t : set Œ±) :
  f '' (s \ t) = f '' s \ f '' t :=
subset.antisymm
  (subset.trans (image_inter_subset _ _ _) $ inter_subset_inter_right _ $ image_compl_subset hf)
  (subset_image_diff f s t)

lemma nonempty.image (f : Œ± ‚Üí Œ≤) {s : set Œ±} : s.nonempty ‚Üí (f '' s).nonempty
| ‚ü®x, hx‚ü© := ‚ü®f x, mem_image_of_mem f hx‚ü©

lemma nonempty.of_image {f : Œ± ‚Üí Œ≤} {s : set Œ±} : (f '' s).nonempty ‚Üí s.nonempty
| ‚ü®y, x, hx, _‚ü© := ‚ü®x, hx‚ü©

@[simp] lemma nonempty_image_iff {f : Œ± ‚Üí Œ≤} {s : set Œ±} :
  (f '' s).nonempty ‚Üî s.nonempty :=
‚ü®nonempty.of_image, Œª h, h.image f‚ü©

lemma nonempty.preimage {s : set Œ≤} (hs : s.nonempty) {f : Œ± ‚Üí Œ≤} (hf : surjective f) :
  (f ‚Åª¬π' s).nonempty :=
let ‚ü®y, hy‚ü© := hs, ‚ü®x, hx‚ü© := hf y in ‚ü®x, mem_preimage.2 $ hx.symm ‚ñ∏ hy‚ü©

instance (f : Œ± ‚Üí Œ≤) (s : set Œ±) [nonempty s] : nonempty (f '' s) :=
(set.nonempty.image f nonempty_of_nonempty_subtype).to_subtype

/-- image and preimage are a Galois connection -/
@[simp] theorem image_subset_iff {s : set Œ±} {t : set Œ≤} {f : Œ± ‚Üí Œ≤} :
  f '' s ‚äÜ t ‚Üî s ‚äÜ f ‚Åª¬π' t :=
ball_image_iff

theorem image_preimage_subset (f : Œ± ‚Üí Œ≤) (s : set Œ≤) :
  f '' (f ‚Åª¬π' s) ‚äÜ s :=
image_subset_iff.2 (subset.refl _)

theorem subset_preimage_image (f : Œ± ‚Üí Œ≤) (s : set Œ±) :
  s ‚äÜ f ‚Åª¬π' (f '' s) :=
Œª x, mem_image_of_mem f

theorem preimage_image_eq {f : Œ± ‚Üí Œ≤} (s : set Œ±) (h : injective f) : f ‚Åª¬π' (f '' s) = s :=
subset.antisymm
  (Œª x ‚ü®y, hy, e‚ü©, h e ‚ñ∏ hy)
  (subset_preimage_image f s)

theorem image_preimage_eq {f : Œ± ‚Üí Œ≤} (s : set Œ≤) (h : surjective f) : f '' (f ‚Åª¬π' s) = s :=
subset.antisymm
  (image_preimage_subset f s)
  (Œª x hx, let ‚ü®y, e‚ü© := h x in ‚ü®y, (e.symm ‚ñ∏ hx : f y ‚àà s), e‚ü©)

lemma preimage_eq_preimage {f : Œ≤ ‚Üí Œ±} (hf : surjective f) : f ‚Åª¬π' s = f ‚Åª¬π' t ‚Üî s = t :=
iff.intro
  (assume eq, by rw [‚Üê image_preimage_eq s hf, ‚Üê image_preimage_eq t hf, eq])
  (assume eq, eq ‚ñ∏ rfl)

lemma image_inter_preimage (f : Œ± ‚Üí Œ≤) (s : set Œ±) (t : set Œ≤) :
  f '' (s ‚à© f ‚Åª¬π' t) = f '' s ‚à© t :=
begin
  apply subset.antisymm,
  { calc f '' (s ‚à© f ‚Åª¬π' t) ‚äÜ f '' s ‚à© (f '' (f‚Åª¬π' t)) : image_inter_subset _ _ _
  ... ‚äÜ f '' s ‚à© t : inter_subset_inter_right _ (image_preimage_subset f t) },
  { rintros _ ‚ü®‚ü®x, h', rfl‚ü©, h‚ü©,
    exact ‚ü®x, ‚ü®h', h‚ü©, rfl‚ü© }
end

lemma image_preimage_inter (f : Œ± ‚Üí Œ≤) (s : set Œ±) (t : set Œ≤) :
  f '' (f ‚Åª¬π' t ‚à© s) = t ‚à© f '' s :=
by simp only [inter_comm, image_inter_preimage]

@[simp] lemma image_inter_nonempty_iff {f : Œ± ‚Üí Œ≤} {s : set Œ±} {t : set Œ≤} :
  (f '' s ‚à© t).nonempty ‚Üî (s ‚à© f ‚Åª¬π' t).nonempty :=
by rw [‚Üêimage_inter_preimage, nonempty_image_iff]

lemma image_diff_preimage {f : Œ± ‚Üí Œ≤} {s : set Œ±} {t : set Œ≤} : f '' (s \ f ‚Åª¬π' t) = f '' s \ t :=
by simp_rw [diff_eq, ‚Üê preimage_compl, image_inter_preimage]

theorem compl_image : image (compl : set Œ± ‚Üí set Œ±) = preimage compl :=
image_eq_preimage_of_inverse compl_compl compl_compl

theorem compl_image_set_of {p : set Œ± ‚Üí Prop} :
  compl '' {s | p s} = {s | p s·∂ú} :=
congr_fun compl_image p

theorem inter_preimage_subset (s : set Œ±) (t : set Œ≤) (f : Œ± ‚Üí Œ≤) :
  s ‚à© f ‚Åª¬π' t ‚äÜ f ‚Åª¬π' (f '' s ‚à© t) :=
Œª x h, ‚ü®mem_image_of_mem _ h.left, h.right‚ü©

theorem union_preimage_subset (s : set Œ±) (t : set Œ≤) (f : Œ± ‚Üí Œ≤) :
  s ‚à™ f ‚Åª¬π' t ‚äÜ f ‚Åª¬π' (f '' s ‚à™ t) :=
Œª x h, or.elim h (Œª l, or.inl $ mem_image_of_mem _ l) (Œª r, or.inr r)

theorem subset_image_union (f : Œ± ‚Üí Œ≤) (s : set Œ±) (t : set Œ≤) :
  f '' (s ‚à™ f ‚Åª¬π' t) ‚äÜ f '' s ‚à™ t :=
image_subset_iff.2 (union_preimage_subset _ _ _)

lemma preimage_subset_iff {A : set Œ±} {B : set Œ≤} {f : Œ± ‚Üí Œ≤} :
  f‚Åª¬π' B ‚äÜ A ‚Üî (‚àÄ a : Œ±, f a ‚àà B ‚Üí a ‚àà A) := iff.rfl

lemma image_eq_image {f : Œ± ‚Üí Œ≤} (hf : injective f) : f '' s = f '' t ‚Üî s = t :=
iff.symm $ iff.intro (assume eq, eq ‚ñ∏ rfl) $ assume eq,
  by rw [‚Üê preimage_image_eq s hf, ‚Üê preimage_image_eq t hf, eq]

lemma image_subset_image_iff {f : Œ± ‚Üí Œ≤} (hf : injective f) : f '' s ‚äÜ f '' t ‚Üî s ‚äÜ t :=
begin
  refine (iff.symm $ iff.intro (image_subset f) $ assume h, _),
  rw [‚Üê preimage_image_eq s hf, ‚Üê preimage_image_eq t hf],
  exact preimage_mono h
end

lemma prod_quotient_preimage_eq_image [s : setoid Œ±] (g : quotient s ‚Üí Œ≤) {h : Œ± ‚Üí Œ≤}
  (Hh : h = g ‚àò quotient.mk) (r : set (Œ≤ √ó Œ≤)) :
  {x : quotient s √ó quotient s | (g x.1, g x.2) ‚àà r} =
  (Œª a : Œ± √ó Œ±, (‚ü¶a.1‚üß, ‚ü¶a.2‚üß)) '' ((Œª a : Œ± √ó Œ±, (h a.1, h a.2)) ‚Åª¬π' r) :=
Hh.symm ‚ñ∏ set.ext (Œª ‚ü®a‚ÇÅ, a‚ÇÇ‚ü©, ‚ü®quotient.induction_on‚ÇÇ a‚ÇÅ a‚ÇÇ
  (Œª a‚ÇÅ a‚ÇÇ h, ‚ü®(a‚ÇÅ, a‚ÇÇ), h, rfl‚ü©),
  Œª ‚ü®‚ü®b‚ÇÅ, b‚ÇÇ‚ü©, h‚ÇÅ, h‚ÇÇ‚ü©, show (g a‚ÇÅ, g a‚ÇÇ) ‚àà r, from
  have h‚ÇÉ : ‚ü¶b‚ÇÅ‚üß = a‚ÇÅ ‚àß ‚ü¶b‚ÇÇ‚üß = a‚ÇÇ := prod.ext_iff.1 h‚ÇÇ,
    h‚ÇÉ.1 ‚ñ∏ h‚ÇÉ.2 ‚ñ∏ h‚ÇÅ‚ü©)

/-- Restriction of `f` to `s` factors through `s.image_factorization f : s ‚Üí f '' s`. -/
def image_factorization (f : Œ± ‚Üí Œ≤) (s : set Œ±) : s ‚Üí f '' s :=
Œª p, ‚ü®f p.1, mem_image_of_mem f p.2‚ü©

lemma image_factorization_eq {f : Œ± ‚Üí Œ≤} {s : set Œ±} :
  subtype.val ‚àò image_factorization f s = f ‚àò subtype.val :=
funext $ Œª p, rfl

lemma surjective_onto_image {f : Œ± ‚Üí Œ≤} {s : set Œ±} :
  surjective (image_factorization f s) :=
Œª ‚ü®_, ‚ü®a, ha, rfl‚ü©‚ü©, ‚ü®‚ü®a, ha‚ü©, rfl‚ü©

end image

/-! ### Subsingleton -/

/-- A set `s` is a `subsingleton`, if it has at most one element. -/
protected def subsingleton (s : set Œ±) : Prop :=
‚àÄ ‚¶Éx‚¶Ñ (hx : x ‚àà s) ‚¶Éy‚¶Ñ (hy : y ‚àà s), x = y

lemma subsingleton.mono (ht : t.subsingleton) (hst : s ‚äÜ t) : s.subsingleton :=
Œª x hx y hy, ht (hst hx) (hst hy)

lemma subsingleton.image (hs : s.subsingleton) (f : Œ± ‚Üí Œ≤) : (f '' s).subsingleton :=
Œª _ ‚ü®x, hx, Hx‚ü© _ ‚ü®y, hy, Hy‚ü©, Hx ‚ñ∏ Hy ‚ñ∏ congr_arg f (hs hx hy)

lemma subsingleton.eq_singleton_of_mem (hs : s.subsingleton) {x:Œ±} (hx : x ‚àà s) :
  s = {x} :=
ext $ Œª y, ‚ü®Œª hy, (hs hx hy) ‚ñ∏ mem_singleton _, Œª hy, (eq_of_mem_singleton hy).symm ‚ñ∏ hx‚ü©

@[simp] lemma subsingleton_empty : (‚àÖ : set Œ±).subsingleton := Œª x, false.elim

@[simp] lemma subsingleton_singleton {a} : ({a} : set Œ±).subsingleton :=
Œª x hx y hy, (eq_of_mem_singleton hx).symm ‚ñ∏ (eq_of_mem_singleton hy).symm ‚ñ∏ rfl

lemma subsingleton_iff_singleton {x} (hx : x ‚àà s) : s.subsingleton ‚Üî s = {x} :=
‚ü®Œª h, h.eq_singleton_of_mem hx, Œª h,h.symm ‚ñ∏ subsingleton_singleton‚ü©

lemma subsingleton.eq_empty_or_singleton (hs : s.subsingleton) :
  s = ‚àÖ ‚à® ‚àÉ x, s = {x} :=
s.eq_empty_or_nonempty.elim or.inl (Œª ‚ü®x, hx‚ü©, or.inr ‚ü®x, hs.eq_singleton_of_mem hx‚ü©)

lemma subsingleton.induction_on {p : set Œ± ‚Üí Prop} (hs : s.subsingleton) (he : p ‚àÖ)
  (h‚ÇÅ : ‚àÄ x, p {x}) : p s :=
by { rcases hs.eq_empty_or_singleton with rfl|‚ü®x, rfl‚ü©, exacts [he, h‚ÇÅ _] }

lemma subsingleton_univ [subsingleton Œ±] : (univ : set Œ±).subsingleton :=
Œª x hx y hy, subsingleton.elim x y

/-- `s`, coerced to a type, is a subsingleton type if and only if `s`
is a subsingleton set. -/
@[simp, norm_cast] lemma subsingleton_coe (s : set Œ±) : subsingleton s ‚Üî s.subsingleton :=
begin
  split,
  { refine Œª h, (Œª a ha b hb, _),
    exact set_coe.ext_iff.2 (@subsingleton.elim s h ‚ü®a, ha‚ü© ‚ü®b, hb‚ü©) },
  { exact Œª h, subsingleton.intro (Œª a b, set_coe.ext (h a.property b.property)) }
end

/-- `s` is a subsingleton, if its image of an injective function is. -/
theorem subsingleton_of_image {Œ± Œ≤ : Type*} {f : Œ± ‚Üí Œ≤} (hf : function.injective f)
  (s : set Œ±) (hs : (f '' s).subsingleton) : s.subsingleton :=
Œª a ha b hb, hf $ hs (mem_image_of_mem _ ha) (mem_image_of_mem _ hb)

theorem univ_eq_true_false : univ = ({true, false} : set Prop) :=
eq.symm $ eq_univ_of_forall $ classical.cases (by simp) (by simp)

/-! ### Lemmas about range of a function. -/
section range
variables {f : Œπ ‚Üí Œ±}
open function

/-- Range of a function.

This function is more flexible than `f '' univ`, as the image requires that the domain is in Type
and not an arbitrary Sort. -/
def range (f : Œπ ‚Üí Œ±) : set Œ± := {x | ‚àÉy, f y = x}

@[simp] theorem mem_range {x : Œ±} : x ‚àà range f ‚Üî ‚àÉ y, f y = x := iff.rfl

@[simp] theorem mem_range_self (i : Œπ) : f i ‚àà range f := ‚ü®i, rfl‚ü©

theorem forall_range_iff {p : Œ± ‚Üí Prop} : (‚àÄ a ‚àà range f, p a) ‚Üî (‚àÄ i, p (f i)) :=
by simp

theorem exists_range_iff {p : Œ± ‚Üí Prop} : (‚àÉ a ‚àà range f, p a) ‚Üî (‚àÉ i, p (f i)) :=
by simp

lemma exists_range_iff' {p : Œ± ‚Üí Prop} :
  (‚àÉ a, a ‚àà range f ‚àß p a) ‚Üî ‚àÉ i, p (f i) :=
by simpa only [exists_prop] using exists_range_iff

theorem range_iff_surjective : range f = univ ‚Üî surjective f :=
eq_univ_iff_forall

alias range_iff_surjective ‚Üî _ function.surjective.range_eq

@[simp] theorem range_id : range (@id Œ±) = univ := range_iff_surjective.2 surjective_id

theorem is_compl_range_inl_range_inr : is_compl (range $ @sum.inl Œ± Œ≤) (range sum.inr) :=
‚ü®by { rintro y ‚ü®‚ü®x‚ÇÅ, rfl‚ü©, ‚ü®x‚ÇÇ, _‚ü©‚ü©, cc },
  by { rintro (x|y) -; [left, right]; exact mem_range_self _ }‚ü©

@[simp] theorem range_inl_union_range_inr : range (sum.inl : Œ± ‚Üí Œ± ‚äï Œ≤) ‚à™ range sum.inr = univ :=
is_compl_range_inl_range_inr.sup_eq_top

@[simp] theorem range_inl_inter_range_inr : range (sum.inl : Œ± ‚Üí Œ± ‚äï Œ≤) ‚à© range sum.inr = ‚àÖ :=
is_compl_range_inl_range_inr.inf_eq_bot

@[simp] theorem range_inr_union_range_inl : range (sum.inr : Œ≤ ‚Üí Œ± ‚äï Œ≤) ‚à™ range sum.inl = univ :=
is_compl_range_inl_range_inr.symm.sup_eq_top

@[simp] theorem range_inr_inter_range_inl : range (sum.inr : Œ≤ ‚Üí Œ± ‚äï Œ≤) ‚à© range sum.inl = ‚àÖ :=
is_compl_range_inl_range_inr.symm.inf_eq_bot

@[simp] theorem preimage_inl_range_inr : sum.inl ‚Åª¬π' range (sum.inr : Œ≤ ‚Üí Œ± ‚äï Œ≤) = ‚àÖ :=
by { ext, simp }

@[simp] theorem preimage_inr_range_inl : sum.inr ‚Åª¬π' range (sum.inl : Œ± ‚Üí Œ± ‚äï Œ≤) = ‚àÖ :=
by { ext, simp }

@[simp] theorem range_quot_mk (r : Œ± ‚Üí Œ± ‚Üí Prop) : range (quot.mk r) = univ :=
(surjective_quot_mk r).range_eq

@[simp] theorem image_univ {Œπ : Type*} {f : Œπ ‚Üí Œ≤} : f '' univ = range f :=
by { ext, simp [image, range] }

theorem image_subset_range {Œπ : Type*} (f : Œπ ‚Üí Œ≤) (s : set Œπ) : f '' s ‚äÜ range f :=
by rw ‚Üê image_univ; exact image_subset _ (subset_univ _)

theorem range_comp (g : Œ± ‚Üí Œ≤) (f : Œπ ‚Üí Œ±) : range (g ‚àò f) = g '' range f :=
subset.antisymm
  (forall_range_iff.mpr $ assume i, mem_image_of_mem g (mem_range_self _))
  (ball_image_iff.mpr $ forall_range_iff.mpr mem_range_self)

theorem range_subset_iff {s : set Œ±} : range f ‚äÜ s ‚Üî ‚àÄ y, f y ‚àà s :=
forall_range_iff

lemma range_comp_subset_range (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) : range (g ‚àò f) ‚äÜ range g :=
by rw range_comp; apply image_subset_range

lemma range_nonempty_iff_nonempty : (range f).nonempty ‚Üî nonempty Œπ :=
‚ü®Œª ‚ü®y, x, hxy‚ü©, ‚ü®x‚ü©, Œª ‚ü®x‚ü©, ‚ü®f x, mem_range_self x‚ü©‚ü©

lemma range_nonempty [h : nonempty Œπ] (f : Œπ ‚Üí Œ±) : (range f).nonempty :=
range_nonempty_iff_nonempty.2 h

@[simp] lemma range_eq_empty {f : Œπ ‚Üí Œ±} : range f = ‚àÖ ‚Üî ¬¨ nonempty Œπ :=
not_nonempty_iff_eq_empty.symm.trans $ not_congr range_nonempty_iff_nonempty

instance [nonempty Œπ] (f : Œπ ‚Üí Œ±) : nonempty (range f) := (range_nonempty f).to_subtype

@[simp] lemma image_union_image_compl_eq_range (f : Œ± ‚Üí Œ≤) :
  (f '' s) ‚à™ (f '' s·∂ú) = range f :=
by rw [‚Üê image_union, ‚Üê image_univ, ‚Üê union_compl_self]

theorem image_preimage_eq_inter_range {f : Œ± ‚Üí Œ≤} {t : set Œ≤} :
  f '' (f ‚Åª¬π' t) = t ‚à© range f :=
ext $ assume x, ‚ü®assume ‚ü®x, hx, heq‚ü©, heq ‚ñ∏ ‚ü®hx, mem_range_self _‚ü©,
  assume ‚ü®hx, ‚ü®y, h_eq‚ü©‚ü©, h_eq ‚ñ∏ mem_image_of_mem f $
    show y ‚àà f ‚Åª¬π' t, by simp [preimage, h_eq, hx]‚ü©

lemma image_preimage_eq_of_subset {f : Œ± ‚Üí Œ≤} {s : set Œ≤} (hs : s ‚äÜ range f) :
  f '' (f ‚Åª¬π' s) = s :=
by rw [image_preimage_eq_inter_range, inter_eq_self_of_subset_left hs]

lemma image_preimage_eq_iff {f : Œ± ‚Üí Œ≤} {s : set Œ≤} : f '' (f ‚Åª¬π' s) = s ‚Üî s ‚äÜ range f :=
‚ü®by { intro h, rw [‚Üê h], apply image_subset_range }, image_preimage_eq_of_subset‚ü©

lemma preimage_subset_preimage_iff {s t : set Œ±} {f : Œ≤ ‚Üí Œ±} (hs : s ‚äÜ range f) :
  f ‚Åª¬π' s ‚äÜ f ‚Åª¬π' t ‚Üî s ‚äÜ t :=
begin
  split,
  { intros h x hx, rcases hs hx with ‚ü®y, rfl‚ü©, exact h hx },
  intros h x, apply h
end

lemma preimage_eq_preimage' {s t : set Œ±} {f : Œ≤ ‚Üí Œ±} (hs : s ‚äÜ range f) (ht : t ‚äÜ range f) :
  f ‚Åª¬π' s = f ‚Åª¬π' t ‚Üî s = t :=
begin
  split,
  { intro h, apply subset.antisymm, rw [‚Üêpreimage_subset_preimage_iff hs, h],
    rw [‚Üêpreimage_subset_preimage_iff ht, h] },
  rintro rfl, refl
end

@[simp] theorem preimage_inter_range {f : Œ± ‚Üí Œ≤} {s : set Œ≤} : f ‚Åª¬π' (s ‚à© range f) = f ‚Åª¬π' s :=
set.ext $ Œª x, and_iff_left ‚ü®x, rfl‚ü©

@[simp] theorem preimage_range_inter {f : Œ± ‚Üí Œ≤} {s : set Œ≤} : f ‚Åª¬π' (range f ‚à© s) = f ‚Åª¬π' s :=
by rw [inter_comm, preimage_inter_range]

theorem preimage_image_preimage {f : Œ± ‚Üí Œ≤} {s : set Œ≤} :
  f ‚Åª¬π' (f '' (f ‚Åª¬π' s)) = f ‚Åª¬π' s :=
by rw [image_preimage_eq_inter_range, preimage_inter_range]

@[simp] theorem quot_mk_range_eq [setoid Œ±] : range (Œªx : Œ±, ‚ü¶x‚üß) = univ :=
range_iff_surjective.2 quot.exists_rep

lemma range_const_subset {c : Œ±} : range (Œªx:Œπ, c) ‚äÜ {c} :=
range_subset_iff.2 $ Œª x, rfl

@[simp] lemma range_const : ‚àÄ [nonempty Œπ] {c : Œ±}, range (Œªx:Œπ, c) = {c}
| ‚ü®x‚ü© c := subset.antisymm range_const_subset $
  assume y hy, (mem_singleton_iff.1 hy).symm ‚ñ∏ mem_range_self x

lemma diagonal_eq_range {Œ±  : Type*} : diagonal Œ± = range (Œª x, (x, x)) :=
by { ext ‚ü®x, y‚ü©, simp [diagonal, eq_comm] }

theorem preimage_singleton_nonempty {f : Œ± ‚Üí Œ≤} {y : Œ≤} :
  (f ‚Åª¬π' {y}).nonempty ‚Üî y ‚àà range f :=
iff.rfl

theorem preimage_singleton_eq_empty {f : Œ± ‚Üí Œ≤} {y : Œ≤} :
  f ‚Åª¬π' {y} = ‚àÖ ‚Üî y ‚àâ range f :=
not_nonempty_iff_eq_empty.symm.trans $ not_congr preimage_singleton_nonempty

lemma range_subset_singleton {f : Œπ ‚Üí Œ±} {x : Œ±} : range f ‚äÜ {x} ‚Üî f = const Œπ x :=
by simp [range_subset_iff, funext_iff, mem_singleton]

lemma image_compl_preimage {f : Œ± ‚Üí Œ≤} {s : set Œ≤} : f '' ((f ‚Åª¬π' s)·∂ú) = range f \ s :=
by rw [compl_eq_univ_diff, image_diff_preimage, image_univ]

@[simp] theorem range_sigma_mk {Œ≤ : Œ± ‚Üí Type*} (a : Œ±) :
  range (sigma.mk a : Œ≤ a ‚Üí Œ£ a, Œ≤ a) = sigma.fst ‚Åª¬π' {a} :=
begin
  apply subset.antisymm,
  { rintros _ ‚ü®b, rfl‚ü©, simp },
  { rintros ‚ü®x, y‚ü© (rfl|_),
    exact mem_range_self y }
end

/-- Any map `f : Œπ ‚Üí Œ≤` factors through a map `range_factorization f : Œπ ‚Üí range f`. -/
def range_factorization (f : Œπ ‚Üí Œ≤) : Œπ ‚Üí range f :=
Œª i, ‚ü®f i, mem_range_self i‚ü©

lemma range_factorization_eq {f : Œπ ‚Üí Œ≤} :
  subtype.val ‚àò range_factorization f = f :=
funext $ Œª i, rfl

lemma surjective_onto_range : surjective (range_factorization f) :=
Œª ‚ü®_, ‚ü®i, rfl‚ü©‚ü©, ‚ü®i, rfl‚ü©

lemma image_eq_range (f : Œ± ‚Üí Œ≤) (s : set Œ±) : f '' s = range (Œª(x : s), f x) :=
by { ext, split, rintro ‚ü®x, h1, h2‚ü©, exact ‚ü®‚ü®x, h1‚ü©, h2‚ü©, rintro ‚ü®‚ü®x, h1‚ü©, h2‚ü©, exact ‚ü®x, h1, h2‚ü© }

@[simp] lemma sum.elim_range {Œ± Œ≤ Œ≥ : Type*} (f : Œ± ‚Üí Œ≥) (g : Œ≤ ‚Üí Œ≥) :
  range (sum.elim f g) = range f ‚à™ range g :=
by simp [set.ext_iff, mem_range]

lemma range_ite_subset' {p : Prop} [decidable p] {f g : Œ± ‚Üí Œ≤} :
  range (if p then f else g) ‚äÜ range f ‚à™ range g :=
begin
  by_cases h : p, {rw if_pos h, exact subset_union_left _ _},
  {rw if_neg h, exact subset_union_right _ _}
end

lemma range_ite_subset {p : Œ± ‚Üí Prop} [decidable_pred p] {f g : Œ± ‚Üí Œ≤} :
  range (Œª x, if p x then f x else g x) ‚äÜ range f ‚à™ range g :=
begin
  rw range_subset_iff, intro x, by_cases h : p x,
  simp [if_pos h, mem_union, mem_range_self],
  simp [if_neg h, mem_union, mem_range_self]
end

@[simp] lemma preimage_range (f : Œ± ‚Üí Œ≤) : f ‚Åª¬π' (range f) = univ :=
eq_univ_of_forall mem_range_self

/-- The range of a function from a `unique` type contains just the
function applied to its single value. -/
lemma range_unique [h : unique Œπ] : range f = {f $ default Œπ} :=
begin
  ext x,
  rw mem_range,
  split,
  { rintros ‚ü®i, hi‚ü©,
    rw h.uniq i at hi,
    exact hi ‚ñ∏ mem_singleton _ },
  { exact Œª h, ‚ü®default Œπ, h.symm‚ü© }
end

lemma range_diff_image_subset (f : Œ± ‚Üí Œ≤) (s : set Œ±) :
  range f \ f '' s ‚äÜ f '' s·∂ú :=
Œª y ‚ü®‚ü®x, h‚ÇÅ‚ü©, h‚ÇÇ‚ü©, ‚ü®x, Œª h, h‚ÇÇ ‚ü®x, h, h‚ÇÅ‚ü©, h‚ÇÅ‚ü©

lemma range_diff_image {f : Œ± ‚Üí Œ≤} (H : injective f) (s : set Œ±) :
  range f \ f '' s = f '' s·∂ú :=
subset.antisymm (range_diff_image_subset f s) $ Œª y ‚ü®x, hx, hy‚ü©, hy ‚ñ∏
  ‚ü®mem_range_self _, Œª ‚ü®x', hx', eq‚ü©, hx $ H eq ‚ñ∏ hx'‚ü©

end range

/-- The set `s` is pairwise `r` if `r x y` for all *distinct* `x y ‚àà s`. -/
def pairwise_on (s : set Œ±) (r : Œ± ‚Üí Œ± ‚Üí Prop) := ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, x ‚â† y ‚Üí r x y

theorem pairwise_on.mono {s t : set Œ±} {r}
  (h : t ‚äÜ s) (hp : pairwise_on s r) : pairwise_on t r :=
Œª x xt y yt, hp x (h xt) y (h yt)

theorem pairwise_on.mono' {s : set Œ±} {r r' : Œ± ‚Üí Œ± ‚Üí Prop}
  (H : ‚àÄ a b, r a b ‚Üí r' a b) (hp : pairwise_on s r) : pairwise_on s r' :=
Œª x xs y ys h, H _ _ (hp x xs y ys h)

/-- If and only if `f` takes pairwise equal values on `s`, there is
some value it takes everywhere on `s`. -/
lemma pairwise_on_eq_iff_exists_eq [nonempty Œ≤] (s : set Œ±) (f : Œ± ‚Üí Œ≤) :
  (pairwise_on s (Œª x y, f x = f y)) ‚Üî ‚àÉ z, ‚àÄ x ‚àà s, f x = z :=
begin
  split,
  { intro h,
    rcases eq_empty_or_nonempty s with rfl | ‚ü®x, hx‚ü©,
    { exact ‚ü®classical.arbitrary Œ≤, Œª x hx, false.elim hx‚ü© },
    { use f x,
      intros y hy,
      by_cases hyx : y = x,
      { rw hyx },
      { exact h y hy x hx hyx } } },
  { rintros ‚ü®z, hz‚ü© x hx y hy hne,
    rw [hz x hx, hz y hy] }
end

@[simp] lemma pairwise_on_empty {Œ±} (r : Œ± ‚Üí Œ± ‚Üí Prop) :
  (‚àÖ : set Œ±).pairwise_on r :=
Œª _, by simp

lemma pairwise_on_insert_of_symmetric {Œ±} {s : set Œ±} {a : Œ±} {r : Œ± ‚Üí Œ± ‚Üí Prop}
  (hr : symmetric r) :
  (insert a s).pairwise_on r ‚Üî s.pairwise_on r ‚àß ‚àÄ b ‚àà s, a ‚â† b ‚Üí r a b :=
begin
  refine ‚ü®Œª h, ‚ü®_, _‚ü©, Œª h, _‚ü©,
  { exact h.mono (s.subset_insert a) },
  { intros b hb hn,
    exact h a (s.mem_insert _) b (set.mem_insert_of_mem _ hb) hn },
  { intros b hb c hc hn,
    rw [mem_insert_iff] at hb hc,
    rcases hb with (rfl | hb);
    rcases hc with (rfl | hc),
    { exact absurd rfl hn },
    { exact h.right _ hc hn },
    { exact hr (h.right _ hb hn.symm) },
    { exact h.left _ hb _ hc hn } }
end

end set

open set

namespace function

variables {Œπ : Sort*} {Œ± : Type*} {Œ≤ : Type*} {f : Œ± ‚Üí Œ≤}

lemma surjective.preimage_injective (hf : surjective f) : injective (preimage f) :=
assume s t, (preimage_eq_preimage hf).1

lemma injective.preimage_image (hf : injective f) (s : set Œ±) : f ‚Åª¬π' (f '' s) = s :=
preimage_image_eq s hf

lemma injective.preimage_surjective (hf : injective f) : surjective (preimage f) :=
by { intro s, use f '' s, rw hf.preimage_image }

lemma injective.subsingleton_image_iff (hf : injective f) {s : set Œ±} :
  (f '' s).subsingleton ‚Üî s.subsingleton :=
‚ü®subsingleton_of_image hf s, Œª h, h.image f‚ü©

lemma surjective.image_preimage (hf : surjective f) (s : set Œ≤) : f '' (f ‚Åª¬π' s) = s :=
image_preimage_eq s hf

lemma surjective.image_surjective (hf : surjective f) : surjective (image f) :=
by { intro s, use f ‚Åª¬π' s, rw hf.image_preimage }

lemma injective.image_injective (hf : injective f) : injective (image f) :=
by { intros s t h, rw [‚Üêpreimage_image_eq s hf, ‚Üêpreimage_image_eq t hf, h] }

lemma surjective.preimage_subset_preimage_iff {s t : set Œ≤} (hf : surjective f) :
  f ‚Åª¬π' s ‚äÜ f ‚Åª¬π' t ‚Üî s ‚äÜ t :=
by { apply preimage_subset_preimage_iff, rw [hf.range_eq], apply subset_univ }

lemma surjective.range_comp {Œπ' : Sort*} {f : Œπ ‚Üí Œπ'} (hf : surjective f) (g : Œπ' ‚Üí Œ±) :
  range (g ‚àò f) = range g :=
ext $ Œª y, (@surjective.exists _ _ _ hf (Œª x, g x = y)).symm

lemma injective.nonempty_apply_iff {f : set Œ± ‚Üí set Œ≤} (hf : injective f)
  (h2 : f ‚àÖ = ‚àÖ) {s : set Œ±} : (f s).nonempty ‚Üî s.nonempty :=
by rw [‚Üê ne_empty_iff_nonempty, ‚Üê h2, ‚Üê ne_empty_iff_nonempty, hf.ne_iff]

lemma injective.mem_range_iff_exists_unique (hf : injective f) {b : Œ≤} :
  b ‚àà range f ‚Üî ‚àÉ! a, f a = b :=
‚ü®Œª ‚ü®a, h‚ü©, ‚ü®a, h, Œª a' ha, hf (ha.trans h.symm)‚ü©, exists_unique.exists‚ü©

lemma injective.exists_unique_of_mem_range (hf : injective f) {b : Œ≤} (hb : b ‚àà range f) :
  ‚àÉ! a, f a = b :=
hf.mem_range_iff_exists_unique.mp hb

end function
open function

/-! ### Image and preimage on subtypes -/

namespace subtype

variable {Œ± : Type*}

lemma coe_image {p : Œ± ‚Üí Prop} {s : set (subtype p)} :
  coe '' s = {x | ‚àÉh : p x, (‚ü®x, h‚ü© : subtype p) ‚àà s} :=
set.ext $ assume a,
‚ü®assume ‚ü®‚ü®a', ha'‚ü©, in_s, h_eq‚ü©, h_eq ‚ñ∏ ‚ü®ha', in_s‚ü©,
  assume ‚ü®ha, in_s‚ü©, ‚ü®‚ü®a, ha‚ü©, in_s, rfl‚ü©‚ü©

lemma range_coe {s : set Œ±} :
  range (coe : s ‚Üí Œ±) = s :=
by { rw ‚Üê set.image_univ, simp [-set.image_univ, coe_image] }

/-- A variant of `range_coe`. Try to use `range_coe` if possible.
  This version is useful when defining a new type that is defined as the subtype of something.
  In that case, the coercion doesn't fire anymore. -/
lemma range_val {s : set Œ±} :
  range (subtype.val : s ‚Üí Œ±) = s :=
range_coe

/-- We make this the simp lemma instead of `range_coe`. The reason is that if we write
  for `s : set Œ±` the function `coe : s ‚Üí Œ±`, then the inferred implicit arguments of `coe` are
  `coe Œ± (Œª x, x ‚àà s)`. -/
@[simp] lemma range_coe_subtype {p : Œ± ‚Üí Prop} :
  range (coe : subtype p ‚Üí Œ±) = {x | p x} :=
range_coe

@[simp] lemma coe_preimage_self (s : set Œ±) : (coe : s ‚Üí Œ±) ‚Åª¬π' s = univ :=
by rw [‚Üê preimage_range (coe : s ‚Üí Œ±), range_coe]

lemma range_val_subtype {p : Œ± ‚Üí Prop} :
  range (subtype.val : subtype p ‚Üí Œ±) = {x | p x} :=
range_coe

theorem coe_image_subset (s : set Œ±) (t : set s) : coe '' t ‚äÜ s :=
Œª x ‚ü®y, yt, yvaleq‚ü©, by rw ‚Üêyvaleq; exact y.property

theorem coe_image_univ (s : set Œ±) : (coe : s ‚Üí Œ±) '' set.univ = s :=
image_univ.trans range_coe

@[simp] theorem image_preimage_coe (s t : set Œ±) :
  (coe : s ‚Üí Œ±) '' (coe ‚Åª¬π' t) = t ‚à© s :=
image_preimage_eq_inter_range.trans $ congr_arg _ range_coe

theorem image_preimage_val (s t : set Œ±) :
  (subtype.val : s ‚Üí Œ±) '' (subtype.val ‚Åª¬π' t) = t ‚à© s :=
image_preimage_coe s t

theorem preimage_coe_eq_preimage_coe_iff {s t u : set Œ±} :
  ((coe : s ‚Üí Œ±) ‚Åª¬π' t = coe ‚Åª¬π' u) ‚Üî t ‚à© s = u ‚à© s :=
begin
  rw [‚Üêimage_preimage_coe, ‚Üêimage_preimage_coe],
  split, { intro h, rw h },
  intro h, exact coe_injective.image_injective h
end

theorem preimage_val_eq_preimage_val_iff (s t u : set Œ±) :
  ((subtype.val : s ‚Üí Œ±) ‚Åª¬π' t = subtype.val ‚Åª¬π' u) ‚Üî (t ‚à© s = u ‚à© s) :=
preimage_coe_eq_preimage_coe_iff

lemma exists_set_subtype {t : set Œ±} (p : set Œ± ‚Üí Prop) :
  (‚àÉ(s : set t), p (coe '' s)) ‚Üî ‚àÉ(s : set Œ±), s ‚äÜ t ‚àß p s :=
begin
  split,
  { rintro ‚ü®s, hs‚ü©, refine ‚ü®coe '' s, _, hs‚ü©,
    convert image_subset_range _ _, rw [range_coe] },
  rintro ‚ü®s, hs‚ÇÅ, hs‚ÇÇ‚ü©, refine ‚ü®coe ‚Åª¬π' s, _‚ü©,
  rw [image_preimage_eq_of_subset], exact hs‚ÇÇ, rw [range_coe], exact hs‚ÇÅ
end

lemma preimage_coe_nonempty {s t : set Œ±} : ((coe : s ‚Üí Œ±) ‚Åª¬π' t).nonempty ‚Üî (s ‚à© t).nonempty :=
by rw [inter_comm, ‚Üê image_preimage_coe, nonempty_image_iff]

lemma preimage_coe_eq_empty {s t : set Œ±} : (coe : s ‚Üí Œ±) ‚Åª¬π' t = ‚àÖ ‚Üî s ‚à© t = ‚àÖ :=
by simp only [‚Üê not_nonempty_iff_eq_empty, preimage_coe_nonempty]

@[simp] lemma preimage_coe_compl (s : set Œ±) : (coe : s ‚Üí Œ±) ‚Åª¬π' s·∂ú = ‚àÖ :=
preimage_coe_eq_empty.2 (inter_compl_self s)

@[simp] lemma preimage_coe_compl' (s : set Œ±) : (coe : s·∂ú ‚Üí Œ±) ‚Åª¬π' s = ‚àÖ :=
preimage_coe_eq_empty.2 (compl_inter_self s)

end subtype

namespace set

/-! ### Lemmas about cartesian product of sets -/

section prod

variables {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œ¥ : Type*}
variables {s s‚ÇÅ s‚ÇÇ : set Œ±} {t t‚ÇÅ t‚ÇÇ : set Œ≤}

/-- The cartesian product `prod s t` is the set of `(a, b)`
  such that `a ‚àà s` and `b ‚àà t`. -/
protected def prod (s : set Œ±) (t : set Œ≤) : set (Œ± √ó Œ≤) :=
{p | p.1 ‚àà s ‚àß p.2 ‚àà t}

lemma prod_eq (s : set Œ±) (t : set Œ≤) : s.prod t = prod.fst ‚Åª¬π' s ‚à© prod.snd ‚Åª¬π' t := rfl

theorem mem_prod_eq {p : Œ± √ó Œ≤} : p ‚àà s.prod t = (p.1 ‚àà s ‚àß p.2 ‚àà t) := rfl

@[simp] theorem mem_prod {p : Œ± √ó Œ≤} : p ‚àà s.prod t ‚Üî p.1 ‚àà s ‚àß p.2 ‚àà t := iff.rfl

@[simp] theorem prod_mk_mem_set_prod_eq {a : Œ±} {b : Œ≤} :
  (a, b) ‚àà s.prod t = (a ‚àà s ‚àß b ‚àà t) := rfl

lemma mk_mem_prod {a : Œ±} {b : Œ≤} (a_in : a ‚àà s) (b_in : b ‚àà t) : (a, b) ‚àà s.prod t :=
‚ü®a_in, b_in‚ü©

theorem prod_mono {s‚ÇÅ s‚ÇÇ : set Œ±} {t‚ÇÅ t‚ÇÇ : set Œ≤} (hs : s‚ÇÅ ‚äÜ s‚ÇÇ) (ht : t‚ÇÅ ‚äÜ t‚ÇÇ) :
  s‚ÇÅ.prod t‚ÇÅ ‚äÜ s‚ÇÇ.prod t‚ÇÇ :=
assume x ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, ‚ü®hs h‚ÇÅ, ht h‚ÇÇ‚ü©

lemma prod_subset_iff {P : set (Œ± √ó Œ≤)} :
  (s.prod t ‚äÜ P) ‚Üî ‚àÄ (x ‚àà s) (y ‚àà t), (x, y) ‚àà P :=
‚ü®Œª h _ xin _ yin, h (mk_mem_prod xin yin), Œª h ‚ü®_, _‚ü© pin, h _ pin.1 _ pin.2‚ü©

lemma forall_prod_set {p : Œ± √ó Œ≤ ‚Üí Prop} :
  (‚àÄ x ‚àà s.prod t, p x) ‚Üî ‚àÄ (x ‚àà s) (y ‚àà t), p (x, y) :=
prod_subset_iff

lemma exists_prod_set {p : Œ± √ó Œ≤ ‚Üí Prop} :
  (‚àÉ x ‚àà s.prod t, p x) ‚Üî ‚àÉ (x ‚àà s) (y ‚àà t), p (x, y) :=
by simp [and_assoc]

@[simp] theorem prod_empty : s.prod ‚àÖ = (‚àÖ : set (Œ± √ó Œ≤)) :=
by { ext, simp }

@[simp] theorem empty_prod : set.prod ‚àÖ t = (‚àÖ : set (Œ± √ó Œ≤)) :=
by { ext, simp }

@[simp] theorem univ_prod_univ : (@univ Œ±).prod (@univ Œ≤) = univ :=
by { ext ‚ü®x, y‚ü©, simp }

lemma univ_prod {t : set Œ≤} : set.prod (univ : set Œ±) t = prod.snd ‚Åª¬π' t :=
by simp [prod_eq]

lemma prod_univ {s : set Œ±} : set.prod s (univ : set Œ≤) = prod.fst ‚Åª¬π' s :=
by simp [prod_eq]

@[simp] theorem singleton_prod {a : Œ±} : set.prod {a} t = prod.mk a '' t :=
by { ext ‚ü®x, y‚ü©, simp [and.left_comm, eq_comm] }

@[simp] theorem prod_singleton {b : Œ≤} : s.prod {b} = (Œª a, (a, b)) '' s :=
by { ext ‚ü®x, y‚ü©, simp [and.left_comm, eq_comm] }

theorem singleton_prod_singleton {a : Œ±} {b : Œ≤} : set.prod {a} {b} = ({(a, b)} : set (Œ± √ó Œ≤)) :=
by simp

@[simp] theorem union_prod : (s‚ÇÅ ‚à™ s‚ÇÇ).prod t = s‚ÇÅ.prod t ‚à™ s‚ÇÇ.prod t :=
by { ext ‚ü®x, y‚ü©, simp [or_and_distrib_right] }

@[simp] theorem prod_union : s.prod (t‚ÇÅ ‚à™ t‚ÇÇ) = s.prod t‚ÇÅ ‚à™ s.prod t‚ÇÇ :=
by { ext ‚ü®x, y‚ü©, simp [and_or_distrib_left] }

theorem prod_inter_prod : s‚ÇÅ.prod t‚ÇÅ ‚à© s‚ÇÇ.prod t‚ÇÇ = (s‚ÇÅ ‚à© s‚ÇÇ).prod (t‚ÇÅ ‚à© t‚ÇÇ) :=
by { ext ‚ü®x, y‚ü©, simp [and_assoc, and.left_comm] }

theorem insert_prod {a : Œ±} : (insert a s).prod t = (prod.mk a '' t) ‚à™ s.prod t :=
by { ext ‚ü®x, y‚ü©, simp [image, iff_def, or_imp_distrib, imp.swap] {contextual := tt} }

theorem prod_insert {b : Œ≤} : s.prod (insert b t) = ((Œªa, (a, b)) '' s) ‚à™ s.prod t :=
by { ext ‚ü®x, y‚ü©, simp [image, iff_def, or_imp_distrib, imp.swap] {contextual := tt} }

theorem prod_preimage_eq {f : Œ≥ ‚Üí Œ±} {g : Œ¥ ‚Üí Œ≤} :
  (f ‚Åª¬π' s).prod (g ‚Åª¬π' t) = (Œª p, (f p.1, g p.2)) ‚Åª¬π' s.prod t := rfl

lemma prod_preimage_left {f : Œ≥ ‚Üí Œ±} : (f ‚Åª¬π' s).prod t = (Œªp, (f p.1, p.2)) ‚Åª¬π' (s.prod t) := rfl

lemma prod_preimage_right {g : Œ¥ ‚Üí Œ≤} : s.prod (g ‚Åª¬π' t) = (Œªp, (p.1, g p.2)) ‚Åª¬π' (s.prod t) := rfl

lemma preimage_prod_map_prod (f : Œ± ‚Üí Œ≤) (g : Œ≥ ‚Üí Œ¥) (s : set Œ≤) (t : set Œ¥) :
  prod.map f g ‚Åª¬π' (s.prod t) = (f ‚Åª¬π' s).prod (g ‚Åª¬π' t) :=
rfl

lemma mk_preimage_prod (f : Œ≥ ‚Üí Œ±) (g : Œ≥ ‚Üí Œ≤) :
  (Œª x, (f x, g x)) ‚Åª¬π' s.prod t = f ‚Åª¬π' s ‚à© g ‚Åª¬π' t := rfl

@[simp] lemma mk_preimage_prod_left {y : Œ≤} (h : y ‚àà t) : (Œª x, (x, y)) ‚Åª¬π' s.prod t = s :=
by { ext x, simp [h] }

@[simp] lemma mk_preimage_prod_right {x : Œ±} (h : x ‚àà s) : prod.mk x ‚Åª¬π' s.prod t = t :=
by { ext y, simp [h] }

@[simp] lemma mk_preimage_prod_left_eq_empty {y : Œ≤} (hy : y ‚àâ t) :
  (Œª x, (x, y)) ‚Åª¬π' s.prod t = ‚àÖ :=
by { ext z, simp [hy] }

@[simp] lemma mk_preimage_prod_right_eq_empty {x : Œ±} (hx : x ‚àâ s) :
  prod.mk x ‚Åª¬π' s.prod t = ‚àÖ :=
by { ext z, simp [hx] }

lemma mk_preimage_prod_left_eq_if {y : Œ≤} [decidable_pred (‚àà t)] :
  (Œª x, (x, y)) ‚Åª¬π' s.prod t = if y ‚àà t then s else ‚àÖ :=
by { split_ifs; simp [h] }

lemma mk_preimage_prod_right_eq_if {x : Œ±} [decidable_pred (‚àà s)] :
  prod.mk x ‚Åª¬π' s.prod t = if x ‚àà s then t else ‚àÖ :=
by { split_ifs; simp [h] }

lemma mk_preimage_prod_left_fn_eq_if {y : Œ≤} [decidable_pred (‚àà t)] (f : Œ≥ ‚Üí Œ±) :
  (Œª x, (f x, y)) ‚Åª¬π' s.prod t = if y ‚àà t then f ‚Åª¬π' s else ‚àÖ :=
by rw [‚Üê mk_preimage_prod_left_eq_if, prod_preimage_left, preimage_preimage]

lemma mk_preimage_prod_right_fn_eq_if {x : Œ±} [decidable_pred (‚àà s)] (g : Œ¥ ‚Üí Œ≤) :
  (Œª y, (x, g y)) ‚Åª¬π' s.prod t = if x ‚àà s then g ‚Åª¬π' t else ‚àÖ :=
by rw [‚Üê mk_preimage_prod_right_eq_if, prod_preimage_right, preimage_preimage]

theorem image_swap_eq_preimage_swap : image (@prod.swap Œ± Œ≤) = preimage prod.swap :=
image_eq_preimage_of_inverse prod.swap_left_inverse prod.swap_right_inverse

theorem preimage_swap_prod {s : set Œ±} {t : set Œ≤} : prod.swap ‚Åª¬π' t.prod s = s.prod t :=
by { ext ‚ü®x, y‚ü©, simp [and_comm] }

theorem image_swap_prod : prod.swap '' t.prod s = s.prod t :=
by rw [image_swap_eq_preimage_swap, preimage_swap_prod]

theorem prod_image_image_eq {m‚ÇÅ : Œ± ‚Üí Œ≥} {m‚ÇÇ : Œ≤ ‚Üí Œ¥} :
  (m‚ÇÅ '' s).prod (m‚ÇÇ '' t) = image (Œªp:Œ±√óŒ≤, (m‚ÇÅ p.1, m‚ÇÇ p.2)) (s.prod t) :=
ext $ by simp [-exists_and_distrib_right, exists_and_distrib_right.symm, and.left_comm,
  and.assoc, and.comm]

theorem prod_range_range_eq {Œ± Œ≤ Œ≥ Œ¥} {m‚ÇÅ : Œ± ‚Üí Œ≥} {m‚ÇÇ : Œ≤ ‚Üí Œ¥} :
  (range m‚ÇÅ).prod (range m‚ÇÇ) = range (Œªp:Œ±√óŒ≤, (m‚ÇÅ p.1, m‚ÇÇ p.2)) :=
ext $ by simp [range]

@[simp] theorem range_prod_map {Œ± Œ≤ Œ≥ Œ¥} {m‚ÇÅ : Œ± ‚Üí Œ≥} {m‚ÇÇ : Œ≤ ‚Üí Œ¥} :
  range (prod.map m‚ÇÅ m‚ÇÇ) = (range m‚ÇÅ).prod (range m‚ÇÇ) :=
prod_range_range_eq.symm

theorem prod_range_univ_eq {Œ± Œ≤ Œ≥} {m‚ÇÅ : Œ± ‚Üí Œ≥} :
  (range m‚ÇÅ).prod (univ : set Œ≤) = range (Œªp:Œ±√óŒ≤, (m‚ÇÅ p.1, p.2)) :=
ext $ by simp [range]

theorem prod_univ_range_eq {Œ± Œ≤ Œ¥} {m‚ÇÇ : Œ≤ ‚Üí Œ¥} :
  (univ : set Œ±).prod (range m‚ÇÇ) = range (Œªp:Œ±√óŒ≤, (p.1, m‚ÇÇ p.2)) :=
ext $ by simp [range]

theorem nonempty.prod : s.nonempty ‚Üí t.nonempty ‚Üí (s.prod t).nonempty
| ‚ü®x, hx‚ü© ‚ü®y, hy‚ü© := ‚ü®(x, y), ‚ü®hx, hy‚ü©‚ü©

theorem nonempty.fst : (s.prod t).nonempty ‚Üí s.nonempty
| ‚ü®p, hp‚ü© := ‚ü®p.1, hp.1‚ü©

theorem nonempty.snd : (s.prod t).nonempty ‚Üí t.nonempty
| ‚ü®p, hp‚ü© := ‚ü®p.2, hp.2‚ü©

theorem prod_nonempty_iff : (s.prod t).nonempty ‚Üî s.nonempty ‚àß t.nonempty :=
‚ü®Œª h, ‚ü®h.fst, h.snd‚ü©, Œª h, nonempty.prod h.1 h.2‚ü©

theorem prod_eq_empty_iff :
  s.prod t = ‚àÖ ‚Üî (s = ‚àÖ ‚à® t = ‚àÖ) :=
by simp only [not_nonempty_iff_eq_empty.symm, prod_nonempty_iff, not_and_distrib]

lemma prod_sub_preimage_iff {W : set Œ≥} {f : Œ± √ó Œ≤ ‚Üí Œ≥} :
  s.prod t ‚äÜ f ‚Åª¬π' W ‚Üî ‚àÄ a b, a ‚àà s ‚Üí b ‚àà t ‚Üí f (a, b) ‚àà W :=
by simp [subset_def]

lemma fst_image_prod_subset (s : set Œ±) (t : set Œ≤) :
  prod.fst '' (s.prod t) ‚äÜ s :=
Œª _ h, let ‚ü®_, ‚ü®h‚ÇÇ, _‚ü©, h‚ÇÅ‚ü© := (set.mem_image _ _ _).1 h in h‚ÇÅ ‚ñ∏ h‚ÇÇ

lemma prod_subset_preimage_fst (s : set Œ±) (t : set Œ≤) :
  s.prod t ‚äÜ prod.fst ‚Åª¬π' s :=
image_subset_iff.1 (fst_image_prod_subset s t)

lemma fst_image_prod (s : set Œ≤) {t : set Œ±} (ht : t.nonempty) :
  prod.fst '' (s.prod t) = s :=
set.subset.antisymm (fst_image_prod_subset _ _)
  $ Œª y y_in, let ‚ü®x, x_in‚ü© := ht in
    ‚ü®(y, x), ‚ü®y_in, x_in‚ü©, rfl‚ü©

lemma snd_image_prod_subset (s : set Œ±) (t : set Œ≤) :
  prod.snd '' (s.prod t) ‚äÜ t :=
Œª _ h, let ‚ü®_, ‚ü®_, h‚ÇÇ‚ü©, h‚ÇÅ‚ü© := (set.mem_image _ _ _).1 h in h‚ÇÅ ‚ñ∏ h‚ÇÇ

lemma prod_subset_preimage_snd (s : set Œ±) (t : set Œ≤) :
  s.prod t ‚äÜ prod.snd ‚Åª¬π' t :=
image_subset_iff.1 (snd_image_prod_subset s t)

lemma snd_image_prod {s : set Œ±} (hs : s.nonempty) (t : set Œ≤) :
  prod.snd '' (s.prod t) = t :=
set.subset.antisymm (snd_image_prod_subset _ _)
  $ Œª y y_in, let ‚ü®x, x_in‚ü© := hs in
    ‚ü®(x, y), ‚ü®x_in, y_in‚ü©, rfl‚ü©

lemma prod_diff_prod : s.prod t \ s‚ÇÅ.prod t‚ÇÅ = s.prod (t \ t‚ÇÅ) ‚à™ (s \ s‚ÇÅ).prod t :=
by { ext x, by_cases h‚ÇÅ : x.1 ‚àà s‚ÇÅ; by_cases h‚ÇÇ : x.2 ‚àà t‚ÇÅ; simp * }

/-- A product set is included in a product set if and only factors are included, or a factor of the
first set is empty. -/
lemma prod_subset_prod_iff :
  (s.prod t ‚äÜ s‚ÇÅ.prod t‚ÇÅ) ‚Üî (s ‚äÜ s‚ÇÅ ‚àß t ‚äÜ t‚ÇÅ) ‚à® (s = ‚àÖ) ‚à® (t = ‚àÖ) :=
begin
  classical,
  cases (s.prod t).eq_empty_or_nonempty with h h,
  { simp [h, prod_eq_empty_iff.1 h] },
  { have st : s.nonempty ‚àß t.nonempty, by rwa [prod_nonempty_iff] at h,
    split,
    { assume H : s.prod t ‚äÜ s‚ÇÅ.prod t‚ÇÅ,
      have h' : s‚ÇÅ.nonempty ‚àß t‚ÇÅ.nonempty := prod_nonempty_iff.1 (h.mono H),
      refine or.inl ‚ü®_, _‚ü©,
      show s ‚äÜ s‚ÇÅ,
      { have := image_subset (prod.fst : Œ± √ó Œ≤ ‚Üí Œ±) H,
        rwa [fst_image_prod _ st.2, fst_image_prod _ h'.2] at this },
      show t ‚äÜ t‚ÇÅ,
      { have := image_subset (prod.snd : Œ± √ó Œ≤ ‚Üí Œ≤) H,
        rwa [snd_image_prod st.1, snd_image_prod h'.1] at this } },
    { assume H,
      simp only [st.1.ne_empty, st.2.ne_empty, or_false] at H,
      exact prod_mono H.1 H.2 } }
end

end prod

/-! ### Lemmas about set-indexed products of sets -/

section pi
variables {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} {s s‚ÇÅ : set Œπ} {t t‚ÇÅ t‚ÇÇ : Œ† i, set (Œ± i)}

/-- Given an index set `i` and a family of sets `s : Œ† i, set (Œ± i)`, `pi i s`
is the set of dependent functions `f : Œ†a, œÄ a` such that `f a` belongs to `s a`
whenever `a ‚àà i`. -/
def pi (s : set Œπ) (t : Œ† i, set (Œ± i)) : set (Œ† i, Œ± i) := { f | ‚àÄi ‚àà s, f i ‚àà t i }

@[simp] lemma mem_pi {f : Œ† i, Œ± i} : f ‚àà s.pi t ‚Üî ‚àÄ i ‚àà s, f i ‚àà t i :=
by refl

@[simp] lemma mem_univ_pi {f : Œ† i, Œ± i} : f ‚àà pi univ t ‚Üî ‚àÄ i, f i ‚àà t i :=
by simp

@[simp] lemma empty_pi (s : Œ† i, set (Œ± i)) : pi ‚àÖ s = univ := by { ext, simp [pi] }

@[simp] lemma pi_univ (s : set Œπ) : pi s (Œª i, (univ : set (Œ± i))) = univ :=
eq_univ_of_forall $ Œª f i hi, mem_univ _

lemma pi_mono (h : ‚àÄ i ‚àà s, t‚ÇÅ i ‚äÜ t‚ÇÇ i) : pi s t‚ÇÅ ‚äÜ pi s t‚ÇÇ :=
Œª x hx i hi, (h i hi $ hx i hi)

lemma pi_inter_distrib : s.pi (Œª i, t i ‚à© t‚ÇÅ i) = s.pi t ‚à© s.pi t‚ÇÅ :=
ext $ Œª x, by simp only [forall_and_distrib, mem_pi, mem_inter_eq]

lemma pi_congr (h : s = s‚ÇÅ) (h' : ‚àÄ i ‚àà s, t i = t‚ÇÅ i) : pi s t = pi s‚ÇÅ t‚ÇÅ :=
h ‚ñ∏ (ext $ Œª x, forall_congr $ Œª i, forall_congr $ Œª hi, h' i hi ‚ñ∏ iff.rfl)

lemma pi_eq_empty {i : Œπ} (hs : i ‚àà s) (ht : t i = ‚àÖ) : s.pi t = ‚àÖ :=
by { ext f, simp only [mem_empty_eq, not_forall, iff_false, mem_pi, not_imp],
     exact ‚ü®i, hs, by simp [ht]‚ü© }

lemma univ_pi_eq_empty {i : Œπ} (ht : t i = ‚àÖ) : pi univ t = ‚àÖ :=
pi_eq_empty (mem_univ i) ht

lemma pi_nonempty_iff : (s.pi t).nonempty ‚Üî ‚àÄ i, ‚àÉ x, i ‚àà s ‚Üí x ‚àà t i :=
by simp [classical.skolem, set.nonempty]

lemma univ_pi_nonempty_iff : (pi univ t).nonempty ‚Üî ‚àÄ i, (t i).nonempty :=
by simp [classical.skolem, set.nonempty]

lemma pi_eq_empty_iff : s.pi t = ‚àÖ ‚Üî ‚àÉ i, (Œ± i ‚Üí false) ‚à® (i ‚àà s ‚àß t i = ‚àÖ) :=
begin
  rw [‚Üê not_nonempty_iff_eq_empty, pi_nonempty_iff], push_neg, apply exists_congr, intro i,
  split,
  { intro h, by_cases hŒ± : nonempty (Œ± i),
    { cases hŒ± with x, refine or.inr ‚ü®(h x).1, by simp [eq_empty_iff_forall_not_mem, h]‚ü© },
    { exact or.inl (Œª x, hŒ± ‚ü®x‚ü©) }},
  { rintro (h|h) x, exfalso, exact h x, simp [h] }
end

lemma univ_pi_eq_empty_iff : pi univ t = ‚àÖ ‚Üî ‚àÉ i, t i = ‚àÖ :=
by simp [‚Üê not_nonempty_iff_eq_empty, univ_pi_nonempty_iff]

@[simp] lemma range_dcomp {Œ≤ : Œπ ‚Üí Type*} (f : Œ† i, Œ± i ‚Üí Œ≤ i) :
  range (Œª (g : Œ† i, Œ± i), (Œª i, f i (g i))) = pi univ (Œª i, range (f i)) :=
begin
  apply subset.antisymm,
  { rintro _ ‚ü®x, rfl‚ü© i -,
    exact ‚ü®x i, rfl‚ü© },
  { intros x hx,
    choose y hy using hx,
    exact ‚ü®Œª i, y i trivial, funext $ Œª i, hy i trivial‚ü© }
end

@[simp] lemma insert_pi (i : Œπ) (s : set Œπ) (t : Œ† i, set (Œ± i)) :
  pi (insert i s) t = (eval i ‚Åª¬π' t i) ‚à© pi s t :=
by { ext, simp [pi, or_imp_distrib, forall_and_distrib] }

@[simp] lemma singleton_pi (i : Œπ) (t : Œ† i, set (Œ± i)) :
  pi {i} t = (eval i ‚Åª¬π' t i) :=
by { ext, simp [pi] }

lemma singleton_pi' (i : Œπ) (t : Œ† i, set (Œ± i)) : pi {i} t = {x | x i ‚àà t i} :=
singleton_pi i t

lemma pi_if {p : Œπ ‚Üí Prop} [h : decidable_pred p] (s : set Œπ) (t‚ÇÅ t‚ÇÇ : Œ† i, set (Œ± i)) :
  pi s (Œª i, if p i then t‚ÇÅ i else t‚ÇÇ i) = pi {i ‚àà s | p i} t‚ÇÅ ‚à© pi {i ‚àà s | ¬¨ p i} t‚ÇÇ :=
begin
  ext f,
  split,
  { assume h, split; { rintros i ‚ü®his, hpi‚ü©, simpa [*] using h i } },
  { rintros ‚ü®ht‚ÇÅ, ht‚ÇÇ‚ü© i his,
    by_cases p i; simp * at * }
end

lemma union_pi : (s ‚à™ s‚ÇÅ).pi t = s.pi t ‚à© s‚ÇÅ.pi t :=
by simp [pi, or_imp_distrib, forall_and_distrib, set_of_and]

@[simp] lemma pi_inter_compl (s : set Œπ) : pi s t ‚à© pi s·∂ú t = pi univ t :=
by rw [‚Üê union_pi, union_compl_self]

lemma pi_update_of_not_mem [decidable_eq Œπ] {Œ≤ : Œ† i, Type*} {i : Œπ} (hi : i ‚àâ s) (f : Œ† j, Œ± j)
  (a : Œ± i) (t : Œ† j, Œ± j ‚Üí set (Œ≤ j)) :
  s.pi (Œª j, t j (update f i a j)) = s.pi (Œª j, t j (f j)) :=
pi_congr rfl $ Œª j hj, by { rw update_noteq, exact Œª h, hi (h ‚ñ∏ hj) }

lemma pi_update_of_mem [decidable_eq Œπ] {Œ≤ : Œ† i, Type*} {i : Œπ} (hi : i ‚àà s) (f : Œ† j, Œ± j)
  (a : Œ± i) (t : Œ† j, Œ± j ‚Üí set (Œ≤ j)) :
  s.pi (Œª j, t j (update f i a j)) = {x | x i ‚àà t i a} ‚à© (s \ {i}).pi (Œª j, t j (f j)) :=
calc s.pi (Œª j, t j (update f i a j)) = ({i} ‚à™ s \ {i}).pi (Œª j, t j (update f i a j)) :
  by rw [union_diff_self, union_eq_self_of_subset_left (singleton_subset_iff.2 hi)]
... = {x | x i ‚àà t i a} ‚à© (s \ {i}).pi (Œª j, t j (f j)) :
  by { rw [union_pi, singleton_pi', update_same, pi_update_of_not_mem], simp }

lemma univ_pi_update [decidable_eq Œπ] {Œ≤ : Œ† i, Type*} (i : Œπ) (f : Œ† j, Œ± j)
  (a : Œ± i) (t : Œ† j, Œ± j ‚Üí set (Œ≤ j)) :
  pi univ (Œª j, t j (update f i a j)) = {x | x i ‚àà t i a} ‚à© pi {i}·∂ú (Œª j, t j (f j)) :=
by rw [compl_eq_univ_diff, ‚Üê pi_update_of_mem (mem_univ _)]

lemma univ_pi_update_univ [decidable_eq Œπ] (i : Œπ) (s : set (Œ± i)) :
  pi univ (update (Œª j : Œπ, (univ : set (Œ± j))) i s) = eval i ‚Åª¬π' s :=
by rw [univ_pi_update i (Œª j, (univ : set (Œ± j))) s (Œª j t, t), pi_univ, inter_univ, preimage]

open_locale classical

lemma eval_image_pi {i : Œπ} (hs : i ‚àà s) (ht : (s.pi t).nonempty) : eval i '' s.pi t = t i :=
begin
  ext x, rcases ht with ‚ü®f, hf‚ü©, split,
  { rintro ‚ü®g, hg, rfl‚ü©, exact hg i hs },
  { intro hg, refine ‚ü®update f i x, _, by simp‚ü©,
    intros j hj, by_cases hji : j = i,
    { subst hji, simp [hg] },
    { rw [mem_pi] at hf, simp [hji, hf, hj] }},
end

@[simp] lemma eval_image_univ_pi {i : Œπ} (ht : (pi univ t).nonempty) :
  (Œª f : Œ† i, Œ± i, f i) '' pi univ t = t i :=
eval_image_pi (mem_univ i) ht

lemma eval_preimage {Œπ} {Œ± : Œπ ‚Üí Type*} {i : Œπ} {s : set (Œ± i)} :
  eval i ‚Åª¬π' s = pi univ (update (Œª i, univ) i s) :=
by { ext x, simp [@forall_update_iff _ (Œª i, set (Œ± i)) _ _ _ _ (Œª i' y, x i' ‚àà y)] }

lemma eval_preimage' {Œπ} {Œ± : Œπ ‚Üí Type*} {i : Œπ} {s : set (Œ± i)} :
  eval i ‚Åª¬π' s = pi {i} (update (Œª i, univ) i s) :=
by { ext, simp }

lemma update_preimage_pi {i : Œπ} {f : Œ† i, Œ± i} (hi : i ‚àà s)
  (hf : ‚àÄ j ‚àà s, j ‚â† i ‚Üí f j ‚àà t j) : (update f i) ‚Åª¬π' s.pi t = t i :=
begin
  ext x, split,
  { intro h, convert h i hi, simp },
  { intros hx j hj, by_cases h : j = i,
    { cases h, simpa },
    { rw [update_noteq h], exact hf j hj h }}
end

lemma update_preimage_univ_pi {i : Œπ} {f : Œ† i, Œ± i} (hf : ‚àÄ j ‚â† i, f j ‚àà t j) :
  (update f i) ‚Åª¬π' pi univ t = t i :=
update_preimage_pi (mem_univ i) (Œª j _, hf j)

lemma subset_pi_eval_image (s : set Œπ) (u : set (Œ† i, Œ± i)) : u ‚äÜ pi s (Œª i, eval i '' u) :=
Œª f hf i hi, ‚ü®f, hf, rfl‚ü©

lemma univ_pi_ite (s : set Œπ) (t : Œ† i, set (Œ± i)) :
  pi univ (Œª i, if i ‚àà s then t i else univ) = s.pi t :=
by { ext, simp_rw [mem_univ_pi], apply forall_congr, intro i, split_ifs; simp [h] }

end pi

/-! ### Lemmas about `inclusion`, the injection of subtypes induced by `‚äÜ` -/

section inclusion
variable {Œ± : Type*}

/-- `inclusion` is the "identity" function between two subsets `s` and `t`, where `s ‚äÜ t` -/
def inclusion {s t : set Œ±} (h : s ‚äÜ t) : s ‚Üí t :=
Œª x : s, (‚ü®x, h x.2‚ü© : t)

@[simp] lemma inclusion_self {s : set Œ±} (x : s) :
  inclusion (set.subset.refl _) x = x :=
by { cases x, refl }

@[simp] lemma inclusion_right {s t : set Œ±} (h : s ‚äÜ t) (x : t) (m : (x : Œ±) ‚àà s) :
  inclusion h ‚ü®x, m‚ü© = x :=
by { cases x, refl }

@[simp] lemma inclusion_inclusion {s t u : set Œ±} (hst : s ‚äÜ t) (htu : t ‚äÜ u)
  (x : s) : inclusion htu (inclusion hst x) = inclusion (set.subset.trans hst htu) x :=
by { cases x, refl }

@[simp] lemma coe_inclusion {s t : set Œ±} (h : s ‚äÜ t) (x : s) :
  (inclusion h x : Œ±) = (x : Œ±) := rfl

lemma inclusion_injective {s t : set Œ±} (h : s ‚äÜ t) :
  function.injective (inclusion h)
| ‚ü®_, _‚ü© ‚ü®_, _‚ü© := subtype.ext_iff_val.2 ‚àò subtype.ext_iff_val.1

@[simp] lemma range_inclusion {s t : set Œ±} (h : s ‚äÜ t) :
  range (inclusion h) = {x : t | (x:Œ±) ‚àà s} :=
by { ext ‚ü®x, hx‚ü©, simp [inclusion] }

lemma eq_of_inclusion_surjective {s t : set Œ±} {h : s ‚äÜ t}
  (h_surj : function.surjective (inclusion h)) : s = t :=
begin
  rw [‚Üê range_iff_surjective, range_inclusion, eq_univ_iff_forall] at h_surj,
  exact set.subset.antisymm h (Œª x hx, h_surj ‚ü®x, hx‚ü©)
end

end inclusion

/-! ### Injectivity and surjectivity lemmas for image and preimage -/
section image_preimage
variables {Œ± : Type u} {Œ≤ : Type v} {f : Œ± ‚Üí Œ≤}
@[simp]
lemma preimage_injective : injective (preimage f) ‚Üî surjective f :=
begin
  refine ‚ü®Œª h y, _, surjective.preimage_injective‚ü©,
  obtain ‚ü®x, hx‚ü© : (f ‚Åª¬π' {y}).nonempty,
  { rw [h.nonempty_apply_iff preimage_empty], apply singleton_nonempty },
  exact ‚ü®x, hx‚ü©
end

@[simp]
lemma preimage_surjective : surjective (preimage f) ‚Üî injective f :=
begin
  refine ‚ü®Œª h x x' hx, _, injective.preimage_surjective‚ü©,
  cases h {x} with s hs, have := mem_singleton x,
  rwa [‚Üê hs, mem_preimage, hx, ‚Üê mem_preimage, hs, mem_singleton_iff, eq_comm] at this
end

@[simp] lemma image_surjective : surjective (image f) ‚Üî surjective f :=
begin
  refine ‚ü®Œª h y, _, surjective.image_surjective‚ü©,
  cases h {y} with s hs,
  have := mem_singleton y, rw [‚Üê hs] at this, rcases this with ‚ü®x, h1x, h2x‚ü©,
  exact ‚ü®x, h2x‚ü©
end

@[simp] lemma image_injective : injective (image f) ‚Üî injective f :=
begin
  refine ‚ü®Œª h x x' hx, _, injective.image_injective‚ü©,
  rw [‚Üê singleton_eq_singleton_iff], apply h,
  rw [image_singleton, image_singleton, hx]
end

lemma preimage_eq_iff_eq_image {f : Œ± ‚Üí Œ≤} (hf : bijective f) {s t} :
  f ‚Åª¬π' s = t ‚Üî s = f '' t :=
by rw [‚Üê image_eq_image hf.1, hf.2.image_preimage]

lemma eq_preimage_iff_image_eq {f : Œ± ‚Üí Œ≤} (hf : bijective f) {s t} :
  s = f ‚Åª¬π' t ‚Üî f '' s = t :=
by rw [‚Üê image_eq_image hf.1, hf.2.image_preimage]

end image_preimage

/-! ### Lemmas about images of binary and ternary functions -/

section n_ary_image

variables {Œ± Œ≤ Œ≥ Œ¥ Œµ : Type*} {f f' : Œ± ‚Üí Œ≤ ‚Üí Œ≥} {g g' : Œ± ‚Üí Œ≤ ‚Üí Œ≥ ‚Üí Œ¥}
variables {s s' : set Œ±} {t t' : set Œ≤} {u u' : set Œ≥} {a a' : Œ±} {b b' : Œ≤} {c c' : Œ≥} {d d' : Œ¥}


/-- The image of a binary function `f : Œ± ‚Üí Œ≤ ‚Üí Œ≥` as a function `set Œ± ‚Üí set Œ≤ ‚Üí set Œ≥`.
  Mathematically this should be thought of as the image of the corresponding function `Œ± √ó Œ≤ ‚Üí Œ≥`.
-/
def image2 (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) (s : set Œ±) (t : set Œ≤) : set Œ≥ :=
{c | ‚àÉ a b, a ‚àà s ‚àß b ‚àà t ‚àß f a b = c }

@[simp] lemma mem_image2_eq : c ‚àà image2 f s t = ‚àÉ a b, a ‚àà s ‚àß b ‚àà t ‚àß f a b = c := rfl

lemma mem_image2 : c ‚àà image2 f s t ‚Üî ‚àÉ a b, a ‚àà s ‚àß b ‚àà t ‚àß f a b = c := iff.rfl

lemma mem_image2_of_mem (h1 : a ‚àà s) (h2 : b ‚àà t) : f a b ‚àà image2 f s t :=
‚ü®a, b, h1, h2, rfl‚ü©

lemma mem_image2_iff (hf : injective2 f) : f a b ‚àà image2 f s t ‚Üî a ‚àà s ‚àß b ‚àà t :=
‚ü® by { rintro ‚ü®a', b', ha', hb', h‚ü©, rcases hf h with ‚ü®rfl, rfl‚ü©, exact ‚ü®ha', hb'‚ü© },
  Œª ‚ü®ha, hb‚ü©, mem_image2_of_mem ha hb‚ü©

/-- image2 is monotone with respect to `‚äÜ`. -/
lemma image2_subset (hs : s ‚äÜ s') (ht : t ‚äÜ t') : image2 f s t ‚äÜ image2 f s' t' :=
by { rintro _ ‚ü®a, b, ha, hb, rfl‚ü©, exact mem_image2_of_mem (hs ha) (ht hb) }

lemma forall_image2_iff {p : Œ≥ ‚Üí Prop} :
  (‚àÄ z ‚àà image2 f s t, p z) ‚Üî ‚àÄ (x ‚àà s) (y ‚àà t), p (f x y) :=
‚ü®Œª h x hx y hy, h _ ‚ü®x, y, hx, hy, rfl‚ü©, Œª h z ‚ü®x, y, hx, hy, hz‚ü©, hz ‚ñ∏ h x hx y hy‚ü©

@[simp] lemma image2_subset_iff {u : set Œ≥} :
  image2 f s t ‚äÜ u ‚Üî ‚àÄ (x ‚àà s) (y ‚àà t), f x y ‚àà u :=
forall_image2_iff

lemma image2_union_left : image2 f (s ‚à™ s') t = image2 f s t ‚à™ image2 f s' t :=
begin
  ext c, split,
  { rintros ‚ü®a, b, h1a|h2a, hb, rfl‚ü©;[left, right]; exact ‚ü®_, _, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, rfl‚ü© },
  { rintro (‚ü®_, _, _, _, rfl‚ü©|‚ü®_, _, _, _, rfl‚ü©); refine ‚ü®_, _, _, ‚Äπ_‚Ä∫, rfl‚ü©; simp [mem_union, *] }
end

lemma image2_union_right : image2 f s (t ‚à™ t') = image2 f s t ‚à™ image2 f s t' :=
begin
  ext c, split,
  { rintros ‚ü®a, b, ha, h1b|h2b, rfl‚ü©;[left, right]; exact ‚ü®_, _, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, rfl‚ü© },
  { rintro (‚ü®_, _, _, _, rfl‚ü©|‚ü®_, _, _, _, rfl‚ü©); refine ‚ü®_, _, ‚Äπ_‚Ä∫, _, rfl‚ü©; simp [mem_union, *] }
end

@[simp] lemma image2_empty_left : image2 f ‚àÖ t = ‚àÖ := ext $ by simp
@[simp] lemma image2_empty_right : image2 f s ‚àÖ = ‚àÖ := ext $ by simp

lemma image2_inter_subset_left : image2 f (s ‚à© s') t ‚äÜ image2 f s t ‚à© image2 f s' t :=
by { rintro _ ‚ü®a, b, ‚ü®h1a, h2a‚ü©, hb, rfl‚ü©, split; exact ‚ü®_, _, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, rfl‚ü© }

lemma image2_inter_subset_right : image2 f s (t ‚à© t') ‚äÜ image2 f s t ‚à© image2 f s t' :=
by { rintro _ ‚ü®a, b, ha, ‚ü®h1b, h2b‚ü©, rfl‚ü©, split; exact ‚ü®_, _, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, rfl‚ü© }

@[simp] lemma image2_singleton_left : image2 f {a} t = f a '' t :=
ext $ Œª x, by simp

@[simp] lemma image2_singleton_right : image2 f s {b} = (Œª a, f a b) '' s :=
ext $ Œª x, by simp

lemma image2_singleton : image2 f {a} {b} = {f a b} := by simp

@[congr] lemma image2_congr (h : ‚àÄ (a ‚àà s) (b ‚àà t), f a b = f' a b) :
  image2 f s t = image2 f' s t :=
by { ext, split; rintro ‚ü®a, b, ha, hb, rfl‚ü©; refine ‚ü®a, b, ha, hb, by rw h a ha b hb‚ü© }

/-- A common special case of `image2_congr` -/
lemma image2_congr' (h : ‚àÄ a b, f a b = f' a b) : image2 f s t = image2 f' s t :=
image2_congr (Œª a _ b _, h a b)

/-- The image of a ternary function `f : Œ± ‚Üí Œ≤ ‚Üí Œ≥ ‚Üí Œ¥` as a function
  `set Œ± ‚Üí set Œ≤ ‚Üí set Œ≥ ‚Üí set Œ¥`. Mathematically this should be thought of as the image of the
  corresponding function `Œ± √ó Œ≤ √ó Œ≥ ‚Üí Œ¥`.
-/
def image3 (g : Œ± ‚Üí Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (s : set Œ±) (t : set Œ≤) (u : set Œ≥) : set Œ¥ :=
{d | ‚àÉ a b c, a ‚àà s ‚àß b ‚àà t ‚àß c ‚àà u ‚àß g a b c = d }

@[simp] lemma mem_image3 : d ‚àà image3 g s t u ‚Üî ‚àÉ a b c, a ‚àà s ‚àß b ‚àà t ‚àß c ‚àà u ‚àß g a b c = d :=
iff.rfl

@[congr] lemma image3_congr (h : ‚àÄ (a ‚àà s) (b ‚àà t) (c ‚àà u), g a b c = g' a b c) :
  image3 g s t u = image3 g' s t u :=
by { ext x,
     split; rintro ‚ü®a, b, c, ha, hb, hc, rfl‚ü©; exact ‚ü®a, b, c, ha, hb, hc, by rw h a ha b hb c hc‚ü© }

/-- A common special case of `image3_congr` -/
lemma image3_congr' (h : ‚àÄ a b c, g a b c = g' a b c) : image3 g s t u = image3 g' s t u :=
image3_congr (Œª a _ b _ c _, h a b c)

lemma image2_image2_left (f : Œ¥ ‚Üí Œ≥ ‚Üí Œµ) (g : Œ± ‚Üí Œ≤ ‚Üí Œ¥) :
  image2 f (image2 g s t) u = image3 (Œª a b c, f (g a b) c) s t u :=
begin
  ext, split,
  { rintro ‚ü®_, c, ‚ü®a, b, ha, hb, rfl‚ü©, hc, rfl‚ü©, refine ‚ü®a, b, c, ha, hb, hc, rfl‚ü© },
  { rintro ‚ü®a, b, c, ha, hb, hc, rfl‚ü©, refine ‚ü®_, c, ‚ü®a, b, ha, hb, rfl‚ü©, hc, rfl‚ü© }
end

lemma image2_image2_right (f : Œ± ‚Üí Œ¥ ‚Üí Œµ) (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) :
  image2 f s (image2 g t u) = image3 (Œª a b c, f a (g b c)) s t u :=
begin
  ext, split,
  { rintro ‚ü®a, _, ha, ‚ü®b, c, hb, hc, rfl‚ü©, rfl‚ü©, refine ‚ü®a, b, c, ha, hb, hc, rfl‚ü© },
  { rintro ‚ü®a, b, c, ha, hb, hc, rfl‚ü©, refine ‚ü®a, _, ha, ‚ü®b, c, hb, hc, rfl‚ü©, rfl‚ü© }
end

lemma image2_assoc {Œµ'} {f : Œ¥ ‚Üí Œ≥ ‚Üí Œµ} {g : Œ± ‚Üí Œ≤ ‚Üí Œ¥} {f' : Œ± ‚Üí Œµ' ‚Üí Œµ} {g' : Œ≤ ‚Üí Œ≥ ‚Üí Œµ'}
  (h_assoc : ‚àÄ a b c, f (g a b) c = f' a (g' b c)) :
  image2 f (image2 g s t) u = image2 f' s (image2 g' t u) :=
by simp only [image2_image2_left, image2_image2_right, h_assoc]

lemma image_image2 (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) (g : Œ≥ ‚Üí Œ¥) :
  g '' image2 f s t = image2 (Œª a b, g (f a b)) s t :=
begin
  ext, split,
  { rintro ‚ü®_, ‚ü®a, b, ha, hb, rfl‚ü©, rfl‚ü©, refine ‚ü®a, b, ha, hb, rfl‚ü© },
  { rintro ‚ü®a, b, ha, hb, rfl‚ü©, refine ‚ü®_, ‚ü®a, b, ha, hb, rfl‚ü©, rfl‚ü© }
end

lemma image2_image_left (f : Œ≥ ‚Üí Œ≤ ‚Üí Œ¥) (g : Œ± ‚Üí Œ≥) :
  image2 f (g '' s) t = image2 (Œª a b, f (g a) b) s t :=
begin
  ext, split,
  { rintro ‚ü®_, b, ‚ü®a, ha, rfl‚ü©, hb, rfl‚ü©, refine ‚ü®a, b, ha, hb, rfl‚ü© },
  { rintro ‚ü®a, b, ha, hb, rfl‚ü©, refine ‚ü®_, b, ‚ü®a, ha, rfl‚ü©, hb, rfl‚ü© }
end

lemma image2_image_right (f : Œ± ‚Üí Œ≥ ‚Üí Œ¥) (g : Œ≤ ‚Üí Œ≥) :
  image2 f s (g '' t) = image2 (Œª a b, f a (g b)) s t :=
begin
  ext, split,
  { rintro ‚ü®a, _, ha, ‚ü®b, hb, rfl‚ü©, rfl‚ü©, refine ‚ü®a, b, ha, hb, rfl‚ü© },
  { rintro ‚ü®a, b, ha, hb, rfl‚ü©, refine ‚ü®a, _, ha, ‚ü®b, hb, rfl‚ü©, rfl‚ü© }
end

lemma image2_swap (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) (s : set Œ±) (t : set Œ≤) :
  image2 f s t = image2 (Œª a b, f b a) t s :=
by { ext, split; rintro ‚ü®a, b, ha, hb, rfl‚ü©; refine ‚ü®b, a, hb, ha, rfl‚ü© }

@[simp] lemma image2_left (h : t.nonempty) : image2 (Œª x y, x) s t = s :=
by simp [nonempty_def.mp h, ext_iff]

@[simp] lemma image2_right (h : s.nonempty) : image2 (Œª x y, y) s t = t :=
by simp [nonempty_def.mp h, ext_iff]

@[simp] lemma image_prod (f : Œ± ‚Üí Œ≤ ‚Üí Œ≥) : (Œª x : Œ± √ó Œ≤, f x.1 x.2) '' s.prod t = image2 f s t :=
set.ext $ Œª a,
‚ü® by { rintros ‚ü®_, _, rfl‚ü©, exact ‚ü®_, _, (mem_prod.mp ‚Äπ_‚Ä∫).1, (mem_prod.mp ‚Äπ_‚Ä∫).2, rfl‚ü© },
  by { rintros ‚ü®_, _, _, _, rfl‚ü©, exact ‚ü®(_, _), mem_prod.mpr ‚ü®‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫‚ü©, rfl‚ü© }‚ü©

lemma nonempty.image2 (hs : s.nonempty) (ht : t.nonempty) : (image2 f s t).nonempty :=
by { cases hs with a ha, cases ht with b hb, exact ‚ü®f a b, ‚ü®a, b, ha, hb, rfl‚ü©‚ü© }

end n_ary_image

end set

namespace subsingleton

variables {Œ± : Type*} [subsingleton Œ±]

lemma eq_univ_of_nonempty {s : set Œ±} : s.nonempty ‚Üí s = univ :=
Œª ‚ü®x, hx‚ü©, eq_univ_of_forall $ Œª y, subsingleton.elim x y ‚ñ∏ hx

@[elab_as_eliminator]
lemma set_cases {p : set Œ± ‚Üí Prop} (h0 : p ‚àÖ) (h1 : p univ) (s) : p s :=
s.eq_empty_or_nonempty.elim (Œª h, h.symm ‚ñ∏ h0) $ Œª h, (eq_univ_of_nonempty h).symm ‚ñ∏ h1

end subsingleton
