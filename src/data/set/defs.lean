/-
Copyright (c) 2014 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Leonardo de Moura
-/
import data.subtype
import logic.function.iterate
import logic.unique
import order.bounded_order
import order.rel_classes
import tactic.alias
import tactic.lift

/-!
# Basic properties of sets

Sets in Lean are homogeneous; all their elements have the same type. Sets whose elements
have type `X` are thus defined as `set X := X â†’ Prop`. Note that this function need not
be decidable. The definition is in the core library.

This file provides some basic definitions related to sets and functions not present in the core
library, as well as extra lemmas for functions in the core library (empty set, univ, union,
intersection, insert, singleton, set-theoretic difference, complement, and powerset).

Note that a set is a term, not a type. There is a coercion from `set Î±` to `Type*` sending
`s` to the corresponding subtype `â†¥s`.

See also the file `set_theory/zfc.lean`, which contains an encoding of ZFC set theory in Lean.

## Main definitions

Notation used here:

-  `f : Î± â†’ Î²` is a function,

-  `s : set Î±` and `sâ‚ sâ‚‚ : set Î±` are subsets of `Î±`

-  `t : set Î²` is a subset of `Î²`.

Definitions in the file:

* `nonempty s : Prop` : the predicate `s â‰  âˆ…`. Note that this is the preferred way to express the
  fact that `s` has an element (see the Implementation Notes).

* `preimage f t : set Î±` : the preimage fâ»Â¹(t) (written `f â»Â¹' t` in Lean) of a subset of Î².

* `subsingleton s : Prop` : the predicate saying that `s` has at most one element.

* `nontrivial s : Prop` : the predicate saying that `s` has at least two distinct elements.

* `range f : set Î²` : the image of `univ` under `f`.
  Also works for `{p : Prop} (f : p â†’ Î±)` (unlike `image`)

* `inclusion sâ‚ sâ‚‚ : â†¥sâ‚ â†’ â†¥sâ‚‚` : the map `â†¥sâ‚ â†’ â†¥sâ‚‚` induced by an inclusion `sâ‚ âŠ† sâ‚‚`.

## Notation

* `f â»Â¹' t` for `preimage f t`

* `f '' s` for `image f s`

* `sá¶œ` for the complement of `s`

## Implementation notes

* `s.nonempty` is to be preferred to `s â‰  âˆ…` or `âˆƒ x, x âˆˆ s`. It has the advantage that
the `s.nonempty` dot notation can be used.

* For `s : set Î±`, do not use `subtype s`. Instead use `â†¥s` or `(s : Type*)` or `s`.

## Tags

set, sets, subset, subsets, image, preimage, pre-image, range, union, intersection, insert,
singleton, complement, powerset

-/

open function

universes u v w x
variables {Î± : Type*} {s t u : set Î±} {p q : Î± â†’ Prop} {a b x : Î±}

namespace set

/-! ### Set coercion to a type -/

/-- Coercion from a set to the corresponding subtype. -/
instance : has_coe_to_sort (set Î±) Type* := âŸ¨Î» s, {x // x âˆˆ s}âŸ©

lemma coe_eq_subtype (s : set Î±) : â†¥s = {x // x âˆˆ s} := rfl
@[simp] lemma coe_set_of (p : Î± â†’ Prop) : â†¥{x | p x} = {x // p x} := rfl

instance pi_set_coe.can_lift (Î¹ : Type u) (Î± : Î  i : Î¹, Type v) [Î  i, nonempty (Î± i)] (s : set Î¹) :
  can_lift (Î  i : s, Î± i) (Î  i, Î± i) (Î» f i, f i) (Î» _, true) :=
pi_subtype.can_lift Î¹ Î± s

instance pi_set_coe.can_lift' (Î¹ : Type u) (Î± : Type v) [nonempty Î±] (s : set Î¹) :
  can_lift (s â†’ Î±) (Î¹ â†’ Î±) (Î» f i, f i) (Î» _, true) :=
pi_set_coe.can_lift Î¹ (Î» _, Î±) s

end set

namespace set_coe

@[simp] lemma Â«forallÂ» {p : s â†’ Prop} : (âˆ€ x : s, p x) â†” âˆ€ x h, p âŸ¨x, hâŸ© := subtype.forall
@[simp] lemma Â«existsÂ» {p : s â†’ Prop} : (âˆƒ x : s, p x) â†” âˆƒ x h, p âŸ¨x, hâŸ© := subtype.exists

lemma exists' {p : Î  x, x âˆˆ s â†’ Prop} : (âˆƒ x (h : x âˆˆ s), p x h) â†” âˆƒ x : s, p x x.2 :=
(@set_coe.exists _ _ $ Î» x, p x.1 x.2).symm

lemma forall' {p : Î  x, x âˆˆ s â†’ Prop} : (âˆ€ x (h : x âˆˆ s), p x h) â†” âˆ€ x : s, p x x.2 :=
(@set_coe.forall _ _ $ Î» x, p x.1 x.2).symm

@[simp] lemma cast : âˆ€ {s t : set Î±} (H' : s = t) (H : â†¥s = â†¥t) (x : s), cast H x = âŸ¨x.1, H' â–¸ x.2âŸ©
| s _ rfl _ âŸ¨x, hâŸ© := rfl

lemma ext {a b : s} : (â†‘a : Î±) = â†‘b â†’ a = b := subtype.eq
lemma ext_iff {a b : s} : (â†‘a : Î±) = â†‘b â†” a = b := subtype.coe_inj

end set_coe

/-- See also `subtype.prop` -/
lemma subtype.mem (p : s) : (p : Î±) âˆˆ s := p.prop

namespace set

/-! ### Order -/

instance : has_le (set Î±) := âŸ¨Î» s t, âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ x âˆˆ tâŸ©
instance : has_lt (set Î±) := âŸ¨Î» s t, s â‰¤ t âˆ§ Â¬ t â‰¤ sâŸ©
instance : has_subset (set Î±) := âŸ¨(â‰¤)âŸ©
instance : has_ssubset (set Î±) := âŸ¨(<)âŸ©
instance : has_union (set Î±) := âŸ¨Î» s t, {x | x âˆˆ s âˆ¨ x âˆˆ t}âŸ©
instance : has_inter (set Î±) := âŸ¨Î» s t, {x | x âˆˆ s âˆ§ x âˆˆ t}âŸ©
instance : has_compl (set Î±) := âŸ¨Î» s, {x | x âˆ‰ s}âŸ©

@[simp] lemma le_eq_subset : ((â‰¤) : set Î± â†’ set Î± â†’ Prop) = (âŠ†) := rfl
@[simp] lemma lt_eq_ssubset : ((<) : set Î± â†’ set Î± â†’ Prop) = (âŠ‚) := rfl

lemma le_iff_subset : s â‰¤ t â†” s âŠ† t := iff.rfl
lemma lt_iff_ssubset : s < t â†” s âŠ‚ t := iff.rfl

alias le_iff_subset â†” _root_.has_le.le.subset _root_.has_subset.subset.le
alias lt_iff_ssubset â†” _root_.has_lt.lt.ssubset _root_.has_ssubset.ssubset.lt

instance : partial_order (set Î±) :=
{ le := (â‰¤),
  lt := (<),
  ..(infer_instance : partial_order (Î± â†’ Prop)) }

instance : bounded_order (set Î±) :=
{ bot := âˆ…,
  top := univ,
  ..(infer_instance : bounded_order (Î± â†’ Prop)) }

instance : is_refl (set Î±) (âŠ†) := has_le.le.is_refl
instance : is_trans (set Î±) (âŠ†) := has_le.le.is_trans
instance : is_antisymm (set Î±) (âŠ†) := has_le.le.is_antisymm
instance : is_irrefl (set Î±) (âŠ‚) := has_lt.lt.is_irrefl
instance : is_trans (set Î±) (âŠ‚) := has_lt.lt.is_trans
instance : is_asymm (set Î±) (âŠ‚) := has_lt.lt.is_asymm
instance : is_nonstrict_strict_order (set Î±) (âŠ†) (âŠ‚) := âŸ¨Î» _ _, iff.rflâŸ©

-- TODO(Jeremy): write a tactic to unfold specific instances of generic notation?
lemma subset_def : (s âŠ† t) = âˆ€ x, x âˆˆ s â†’ x âˆˆ t := rfl
lemma ssubset_def : s âŠ‚ t = (s âŠ† t âˆ§ Â¬ t âŠ† s) := rfl

@[refl] lemma subset.refl (a : set Î±) : a âŠ† a := assume x, id
lemma subset.rfl : s âŠ† s := subset.refl s

@[trans] lemma subset.trans : s âŠ† t â†’ t âŠ† u â†’ s âŠ† u := has_subset.subset.trans

@[trans] theorem mem_of_eq_of_mem {x y : Î±} {s : set Î±} (hx : x = y) (h : y âˆˆ s) : x âˆˆ s :=
hx.symm â–¸ h

@[ext] lemma ext (h : âˆ€ x, x âˆˆ s â†” x âˆˆ t) : s = t := funext $ Î» x, propext $ h x

lemma ext_iff : s = t â†” âˆ€ x, x âˆˆ s â†” x âˆˆ t := âŸ¨Î» h x, by rw h, extâŸ©

lemma subset.antisymm (hâ‚ : s âŠ† t) (hâ‚‚ : t âŠ† s) : s = t := le_antisymm hâ‚ hâ‚‚
lemma subset.antisymm_iff : s = t â†” s âŠ† t âˆ§ t âŠ† s := le_antisymm_iff

alias subset.antisymm â† eq_of_subset_of_subset

lemma mem_of_subset_of_mem (h : s âŠ† t) : a âˆˆ s â†’ a âˆˆ t := @h _

lemma not_mem_subset (h : s âŠ† t) : a âˆ‰ t â†’ a âˆ‰ s := mt $ mem_of_subset_of_mem h

lemma not_subset : Â¬ s âŠ† t â†” âˆƒ a âˆˆ s, a âˆ‰ t := by simp only [subset_def, not_forall]

/-! ### Definition of strict subsets `s âŠ‚ t` and basic properties. -/

protected lemma eq_or_ssubset_of_subset (h : s âŠ† t) : s = t âˆ¨ s âŠ‚ t := eq_or_lt_of_le h
protected lemma ssubset_iff_subset_ne : s âŠ‚ t â†” s âŠ† t âˆ§ s â‰  t := @lt_iff_le_and_ne (set Î±) _ s t
protected lemma ssubset_of_ssubset_of_subset : s âŠ‚ t â†’ t âŠ† u â†’ s âŠ‚ u := @lt_of_lt_of_le _ _ s _ _
protected lemma ssubset_of_subset_of_ssubset : s âŠ† t â†’ t âŠ‚ u â†’ s âŠ‚ u := @lt_of_le_of_lt _ _ s _ _

lemma exists_of_ssubset (h : s âŠ‚ t) : âˆƒ x âˆˆ t, x âˆ‰ s := not_subset.1 h.2

lemma ssubset_iff_of_subset (h : s âŠ† t) : s âŠ‚ t â†” âˆƒ x âˆˆ t, x âˆ‰ s :=
âŸ¨exists_of_ssubset, Î» âŸ¨x, hxt, hxsâŸ©, âŸ¨h, Î» h, hxs $ h hxtâŸ©âŸ©

end set

namespace set

variables {Î² : Type v} {Î³ : Type w} {Î¹ : Sort x}

instance : inhabited (set Î±) := âŸ¨âˆ…âŸ©

@[trans] lemma mem_of_mem_of_subset {x : Î±} {s t : set Î±} (ha : a âˆˆ s) (h : s âŠ† t) : a âˆˆ t := h ha

lemma forall_in_swap {p : Î± â†’ Î² â†’ Prop} : (âˆ€ (a âˆˆ s) b, p a b) â†” âˆ€ b (a âˆˆ s), p a b :=
âŸ¨Î» h b a ha, h _ ha _, Î» h a ha b, h _ _ haâŸ©

/-! ### Lemmas about `mem` and `set_of` -/

lemma mem_set_of : a âˆˆ {x | p x} â†” p a := iff.rfl
lemma nmem_set_of_iff : a âˆ‰ {x | p x} â†” Â¬ p a := iff.rfl

/-- If `h : a âˆˆ {x | p x}` then `h.out : p x`. These are definitionally equal, but this can
nevertheless be useful for various reasons, e.g. to apply further projection notation or in an
argument to `simp`. -/
lemma _root_.has_mem.mem.out {p : Î± â†’ Prop} (h : a âˆˆ {x | p x}) : p a := h

@[simp] lemma set_of_mem_eq : {x | x âˆˆ s} = s := rfl

lemma set_of_set : set_of s = s := rfl

lemma set_of_app_iff : {x | p x} a â†” p a := iff.rfl

lemma mem_def : a âˆˆ s â†” s a := iff.rfl

lemma set_of_bijective : bijective (set_of : (Î± â†’ Prop) â†’ set Î±) := bijective_id

@[simp] lemma set_of_subset_set_of : {a | p a} âŠ† {a | q a} â†” âˆ€ a, p a â†’ q a := iff.rfl

lemma set_of_and {p q : Î± â†’ Prop} : {a | p a âˆ§ q a} = {a | p a} âˆ© {a | q a} := rfl

lemma set_of_or {p q : Î± â†’ Prop} : {a | p a âˆ¨ q a} = {a | p a} âˆª {a | q a} := rfl

/-! ### Subset and strict subset relations -/

lemma not_mem_empty (x : Î±) : x âˆ‰ (âˆ… : set Î±) := id

@[simp] lemma not_not_mem : Â¬ a âˆ‰ s â†” a âˆˆ s := not_not

/-! ### Non-empty sets -/

/-- The property `s.nonempty` expresses the fact that the set `s` is not empty. It should be used
in lemma assumptions instead of `âˆƒ x, x âˆˆ s` or `s â‰  âˆ…` as it gives access to a nice API thanks to
the dot notation. -/
protected def nonempty (s : set Î±) : Prop := âˆƒ x, x âˆˆ s

lemma nonempty_def : s.nonempty â†” âˆƒ x, x âˆˆ s := iff.rfl

lemma nonempty_of_mem (h : a âˆˆ s) : s.nonempty := âŸ¨a, hâŸ©

lemma nonempty.not_subset_empty : s.nonempty â†’ Â¬ s âŠ† âˆ… := Î» âŸ¨x, hxâŸ© hs, hs hx

lemma nonempty.mono (ht : s âŠ† t) (hs : s.nonempty) : t.nonempty := hs.imp ht

lemma nonempty_iff_univ_nonempty : nonempty Î± â†” (univ : set Î±).nonempty :=
âŸ¨Î» âŸ¨xâŸ©, âŸ¨x, trivialâŸ©, Î» âŸ¨x, _âŸ©, âŸ¨xâŸ©âŸ©

@[simp] lemma univ_nonempty : âˆ€ [h : nonempty Î±], (univ : set Î±).nonempty
| âŸ¨xâŸ© := âŸ¨x, trivialâŸ©

@[simp] lemma nonempty_coe_sort : nonempty s â†” s.nonempty := nonempty_subtype
lemma nonempty_of_nonempty_subtype [nonempty s] : s.nonempty := nonempty_coe_sort.1 â€¹_â€º

alias nonempty_coe_sort â†” _ nonempty.coe_sort
alias nonempty.coe_sort â† nonempty.to_subtype

lemma nonempty.to_type : s.nonempty â†’ nonempty Î± := Î» âŸ¨x, hxâŸ©, âŸ¨xâŸ©

instance [nonempty Î±] : nonempty (set.univ : set Î±) := set.univ_nonempty.to_subtype

/-- Extract a witness from `s.nonempty`. This function might be used instead of case analysis
on the argument. Note that it makes a proof depend on the `classical.choice` axiom. -/
protected noncomputable def nonempty.some : s.nonempty â†’ Î± := classical.some

protected lemma nonempty.some_mem (h : s.nonempty) : h.some âˆˆ s := classical.some_spec h

/-! ### Empty set -/

lemma empty_def : (âˆ… : set Î±) = {x | false} := rfl

@[simp] lemma mem_empty_iff_false (x : Î±) : x âˆˆ (âˆ… : set Î±) â†” false := iff.rfl

@[simp] lemma set_of_false : {a : Î± | false} = âˆ… := rfl

@[simp] lemma empty_subset (s : set Î±) : âˆ… âŠ† s.

lemma subset_empty_iff : s âŠ† âˆ… â†” s = âˆ… :=
(subset.antisymm_iff.trans $ and_iff_left (empty_subset _)).symm

lemma eq_empty_iff_forall_not_mem : s = âˆ… â†” âˆ€ x, x âˆ‰ s := subset_empty_iff.symm

lemma eq_empty_of_forall_not_mem (h : âˆ€ x, x âˆ‰ s) : s = âˆ… := subset_empty_iff.1 h

lemma eq_empty_of_subset_empty : s âŠ† âˆ… â†’ s = âˆ… := subset_empty_iff.1

lemma eq_empty_of_is_empty [is_empty Î±] (s : set Î±) : s = âˆ… :=
eq_empty_of_subset_empty $ Î» x hx, is_empty_elim x

/-- There is exactly one set of a type that is empty. -/
instance unique_empty [is_empty Î±] : unique (set Î±) :=
{ default := âˆ…, uniq := eq_empty_of_is_empty }

/-- See also `set.nonempty_iff_ne_empty`. -/
lemma not_nonempty_iff_eq_empty : Â¬s.nonempty â†” s = âˆ… :=
by simp only [set.nonempty, eq_empty_iff_forall_not_mem, not_exists]

/-- See also `set.not_nonempty_iff_eq_empty`. -/
lemma nonempty_iff_ne_empty : s.nonempty â†” s â‰  âˆ… := not_nonempty_iff_eq_empty.not_right

alias nonempty_iff_ne_empty â†” nonempty.ne_empty _

@[simp] lemma not_nonempty_empty : Â¬(âˆ… : set Î±).nonempty := Î» âŸ¨x, hxâŸ©, hx

@[simp] lemma is_empty_coe_sort : is_empty s â†” s = âˆ… :=
not_iff_not.1 $ by simpa using nonempty_iff_ne_empty

lemma eq_empty_or_nonempty (s : set Î±) : s = âˆ… âˆ¨ s.nonempty :=
or_iff_not_imp_left.2 nonempty_iff_ne_empty.2

lemma subset_eq_empty (h : t âŠ† s) (e : s = âˆ…) : t = âˆ… :=
subset_empty_iff.1 $ e â–¸ h

lemma ball_empty_iff {p : Î± â†’ Prop} : (âˆ€ x âˆˆ (âˆ… : set Î±), p x) â†” true :=
iff_true_intro $ Î» x, false.elim

instance (Î± : Type u) : is_empty.{u+1} (âˆ… : set Î±) := âŸ¨Î» x, x.2âŸ©

@[simp] lemma empty_ssubset : âˆ… âŠ‚ s â†” s.nonempty :=
(@bot_lt_iff_ne_bot (set Î±) _ _ _).trans nonempty_iff_ne_empty.symm

alias empty_ssubset â†” _ nonempty.empty_ssubset

/-!
### Universal set

In Lean `@univ Î±` (or `univ : set Î±`) is the set that contains all elements of type `Î±`.
Mathematically it is the same as `Î±` but it has a different type.
-/

@[simp] lemma set_of_true : {x : Î± | true} = univ := rfl

@[simp] lemma mem_univ (x : Î±) : x âˆˆ @univ Î± := trivial

@[simp] lemma univ_eq_empty_iff : (univ : set Î±) = âˆ… â†” is_empty Î± :=
eq_empty_iff_forall_not_mem.trans âŸ¨Î» H, âŸ¨Î» x, H x trivialâŸ©, Î» H x _, @is_empty.false Î± H xâŸ©

lemma empty_ne_univ [nonempty Î±] : (âˆ… : set Î±) â‰  univ :=
Î» e, not_is_empty_of_nonempty Î± $ univ_eq_empty_iff.1 e.symm

@[simp] lemma subset_univ (s : set Î±) : s âŠ† univ := Î» x H, trivial

lemma univ_subset_iff : univ âŠ† s â†” s = univ := @top_le_iff _ _ _ s

alias univ_subset_iff â†” eq_univ_of_univ_subset _

lemma eq_univ_iff_forall : s = univ â†” âˆ€ x, x âˆˆ s :=
univ_subset_iff.symm.trans $ forall_congr $ Î» x, imp_iff_right trivial

alias eq_univ_iff_forall â†” _ eq_univ_of_forall

lemma nonempty.eq_univ [subsingleton Î±] : s.nonempty â†’ s = univ :=
by { rintro âŸ¨x, hxâŸ©, refine eq_univ_of_forall (Î» y, by rwa subsingleton.elim y x) }

lemma eq_univ_of_subset (h : s âŠ† t) (hs : s = univ) : t = univ :=
eq_univ_of_univ_subset $ hs â–¸ h

lemma exists_mem_of_nonempty (Î±) : âˆ€ [nonempty Î±], âˆƒx:Î±, x âˆˆ (univ : set Î±)
| âŸ¨xâŸ© := âŸ¨x, trivialâŸ©

lemma ne_univ_iff_exists_not_mem (s : set Î±) : s â‰  univ â†” âˆƒ a, a âˆ‰ s :=
by rw [â†not_forall, â†eq_univ_iff_forall]

lemma not_subset_iff_exists_mem_not_mem : Â¬ s âŠ† t â†” âˆƒ x, x âˆˆ s âˆ§ x âˆ‰ t :=
by simp [subset_def]

lemma univ_unique [unique Î±] : @set.univ Î± = {default} :=
set.ext $ Î» x, iff_of_true trivial $ subsingleton.elim x default

lemma ssubset_univ_iff : s âŠ‚ univ â†” s â‰  univ := @lt_top_iff_ne_top _ _ _ s

instance nontrivial_of_nonempty [nonempty Î±] : nontrivial (set Î±) := âŸ¨âŸ¨âˆ…, univ, empty_ne_univâŸ©âŸ©

/-! ### Lemmas about union -/

theorem union_def {sâ‚ sâ‚‚ : set Î±} : sâ‚ âˆª sâ‚‚ = {a | a âˆˆ sâ‚ âˆ¨ a âˆˆ sâ‚‚} := rfl

theorem mem_union_left {x : Î±} {a : set Î±} (b : set Î±) : x âˆˆ a â†’ x âˆˆ a âˆª b := or.inl

theorem mem_union_right {x : Î±} {b : set Î±} (a : set Î±) : x âˆˆ b â†’ x âˆˆ a âˆª b := or.inr

theorem mem_or_mem_of_mem_union {x : Î±} {a b : set Î±} (H : x âˆˆ a âˆª b) : x âˆˆ a âˆ¨ x âˆˆ b := H

theorem mem_union.elim {x : Î±} {a b : set Î±} {P : Prop}
    (Hâ‚ : x âˆˆ a âˆª b) (Hâ‚‚ : x âˆˆ a â†’ P) (Hâ‚ƒ : x âˆˆ b â†’ P) : P :=
or.elim Hâ‚ Hâ‚‚ Hâ‚ƒ

@[simp] theorem mem_union (x : Î±) (a b : set Î±) : x âˆˆ a âˆª b â†” (x âˆˆ a âˆ¨ x âˆˆ b) := iff.rfl

@[simp] theorem union_self (a : set Î±) : a âˆª a = a := ext $ Î» x, or_self _

@[simp] theorem union_empty (a : set Î±) : a âˆª âˆ… = a := ext $ Î» x, or_false _

@[simp] theorem empty_union (a : set Î±) : âˆ… âˆª a = a := ext $ Î» x, false_or _

theorem union_comm (a b : set Î±) : a âˆª b = b âˆª a := ext $ Î» x, or.comm

theorem union_assoc (a b c : set Î±) : (a âˆª b) âˆª c = a âˆª (b âˆª c) := ext $ Î» x, or.assoc

instance union_is_assoc : is_associative (set Î±) (âˆª) := âŸ¨union_assocâŸ©

instance union_is_comm : is_commutative (set Î±) (âˆª) := âŸ¨union_commâŸ©

theorem union_left_comm (sâ‚ sâ‚‚ sâ‚ƒ : set Î±) : sâ‚ âˆª (sâ‚‚ âˆª sâ‚ƒ) = sâ‚‚ âˆª (sâ‚ âˆª sâ‚ƒ) :=
ext $ Î» x, or.left_comm

theorem union_right_comm (sâ‚ sâ‚‚ sâ‚ƒ : set Î±) : (sâ‚ âˆª sâ‚‚) âˆª sâ‚ƒ = (sâ‚ âˆª sâ‚ƒ) âˆª sâ‚‚ :=
ext $ Î» x, or.right_comm

@[simp] theorem subset_union_left (s t : set Î±) : s âŠ† s âˆª t := Î» x, or.inl

@[simp] theorem subset_union_right (s t : set Î±) : t âŠ† s âˆª t := Î» x, or.inr

theorem union_subset {s t r : set Î±} (sr : s âŠ† r) (tr : t âŠ† r) : s âˆª t âŠ† r :=
Î» x, or.rec (@sr _) (@tr _)

@[simp] theorem union_subset_iff {s t u : set Î±} : s âˆª t âŠ† u â†” s âŠ† u âˆ§ t âŠ† u :=
(forall_congr (by exact Î» x, or_imp_distrib)).trans forall_and_distrib

theorem union_subset_union {sâ‚ sâ‚‚ tâ‚ tâ‚‚ : set Î±}
  (hâ‚ : sâ‚ âŠ† sâ‚‚) (hâ‚‚ : tâ‚ âŠ† tâ‚‚) : sâ‚ âˆª tâ‚ âŠ† sâ‚‚ âˆª tâ‚‚ := Î» x, or.imp (@hâ‚ _) (@hâ‚‚ _)

theorem union_subset_union_left {sâ‚ sâ‚‚ : set Î±} (t) (h : sâ‚ âŠ† sâ‚‚) : sâ‚ âˆª t âŠ† sâ‚‚ âˆª t :=
union_subset_union h subset.rfl

theorem union_subset_union_right (s) {tâ‚ tâ‚‚ : set Î±} (h : tâ‚ âŠ† tâ‚‚) : s âˆª tâ‚ âŠ† s âˆª tâ‚‚ :=
union_subset_union subset.rfl h

lemma subset_union_of_subset_left {s t : set Î±} (h : s âŠ† t) (u : set Î±) : s âŠ† t âˆª u :=
subset.trans h (subset_union_left t u)

lemma subset_union_of_subset_right {s u : set Î±} (h : s âŠ† u) (t : set Î±) : s âŠ† t âˆª u :=
subset.trans h (subset_union_right t u)

@[simp] theorem union_empty_iff {s t : set Î±} : s âˆª t = âˆ… â†” s = âˆ… âˆ§ t = âˆ… :=
by simp only [â† subset_empty_iff]; exact union_subset_iff

/-! ### Lemmas about intersection -/

theorem inter_def {sâ‚ sâ‚‚ : set Î±} : sâ‚ âˆ© sâ‚‚ = {a | a âˆˆ sâ‚ âˆ§ a âˆˆ sâ‚‚} := rfl

@[simp] theorem mem_inter_iff (x : Î±) (a b : set Î±) : x âˆˆ a âˆ© b â†” (x âˆˆ a âˆ§ x âˆˆ b) := iff.rfl

theorem mem_inter {x : Î±} {a b : set Î±} (ha : x âˆˆ a) (hb : x âˆˆ b) : x âˆˆ a âˆ© b := âŸ¨ha, hbâŸ©

theorem mem_of_mem_inter_left {x : Î±} {a b : set Î±} (h : x âˆˆ a âˆ© b) : x âˆˆ a := h.left

theorem mem_of_mem_inter_right {x : Î±} {a b : set Î±} (h : x âˆˆ a âˆ© b) : x âˆˆ b := h.right

@[simp] theorem inter_self (a : set Î±) : a âˆ© a = a := ext $ Î» x, and_self _

@[simp] theorem inter_empty (a : set Î±) : a âˆ© âˆ… = âˆ… := ext $ Î» x, and_false _

@[simp] theorem empty_inter (a : set Î±) : âˆ… âˆ© a = âˆ… := ext $ Î» x, false_and _

theorem inter_comm (a b : set Î±) : a âˆ© b = b âˆ© a := ext $ Î» x, and.comm

theorem inter_assoc (a b c : set Î±) : (a âˆ© b) âˆ© c = a âˆ© (b âˆ© c) := ext $ Î» x, and.assoc

instance inter_is_assoc : is_associative (set Î±) (âˆ©) := âŸ¨inter_assocâŸ©

instance inter_is_comm : is_commutative (set Î±) (âˆ©) := âŸ¨inter_commâŸ©

theorem inter_left_comm (sâ‚ sâ‚‚ sâ‚ƒ : set Î±) : sâ‚ âˆ© (sâ‚‚ âˆ© sâ‚ƒ) = sâ‚‚ âˆ© (sâ‚ âˆ© sâ‚ƒ) :=
ext $ Î» x, and.left_comm

theorem inter_right_comm (sâ‚ sâ‚‚ sâ‚ƒ : set Î±) : (sâ‚ âˆ© sâ‚‚) âˆ© sâ‚ƒ = (sâ‚ âˆ© sâ‚ƒ) âˆ© sâ‚‚ :=
ext $ Î» x, and.right_comm

@[simp] theorem inter_subset_left (s t : set Î±) : s âˆ© t âŠ† s := Î» x, and.left

@[simp] theorem inter_subset_right (s t : set Î±) : s âˆ© t âŠ† t := Î» x, and.right

theorem subset_inter {s t r : set Î±} (rs : r âŠ† s) (rt : r âŠ† t) : r âŠ† s âˆ© t := Î» x h, âŸ¨rs h, rt hâŸ©

@[simp] theorem subset_inter_iff {s t r : set Î±} : r âŠ† s âˆ© t â†” r âŠ† s âˆ§ r âŠ† t :=
(forall_congr (by exact Î» x, imp_and_distrib)).trans forall_and_distrib


@[simp] theorem inter_univ (a : set Î±) : a âˆ© univ = a := ext $ Î» a, and_true _

@[simp] theorem univ_inter (a : set Î±) : univ âˆ© a = a := ext $ Î» a, true_and _

theorem inter_subset_inter {sâ‚ sâ‚‚ tâ‚ tâ‚‚ : set Î±}
  (hâ‚ : sâ‚ âŠ† tâ‚) (hâ‚‚ : sâ‚‚ âŠ† tâ‚‚) : sâ‚ âˆ© sâ‚‚ âŠ† tâ‚ âˆ© tâ‚‚ := Î» x, and.imp (@hâ‚ _) (@hâ‚‚ _)

theorem inter_subset_inter_left {s t : set Î±} (u : set Î±) (H : s âŠ† t) : s âˆ© u âŠ† t âˆ© u :=
inter_subset_inter H subset.rfl

theorem inter_subset_inter_right {s t : set Î±} (u : set Î±) (H : s âŠ† t) : u âˆ© s âŠ† u âˆ© t :=
inter_subset_inter subset.rfl H

/-!
### Lemmas about `insert`

`insert Î± s` is the set `{Î±} âˆª s`.
-/

lemma insert_def (x : Î±) (s : set Î±) : insert x s = { y | y = x âˆ¨ y âˆˆ s } := rfl

@[simp] lemma subset_insert (x : Î±) (s : set Î±) : s âŠ† insert x s := Î» y, or.inr

lemma mem_insert (x : Î±) (s : set Î±) : x âˆˆ insert x s := or.inl rfl

lemma mem_insert_of_mem (y : Î±) : a âˆˆ s â†’ a âˆˆ insert y s := or.inr

lemma eq_or_mem_of_mem_insert {x a : Î±} : x âˆˆ insert a s â†’ x = a âˆ¨ x âˆˆ s := id

lemma mem_of_mem_insert_of_ne : b âˆˆ insert a s â†’ b â‰  a â†’ b âˆˆ s := or.resolve_left
lemma eq_of_not_mem_of_mem_insert : b âˆˆ insert a s â†’ b âˆ‰ s â†’ b = a := or.resolve_right

@[simp] lemma mem_insert_iff {x a : Î±} : x âˆˆ insert a s â†” x = a âˆ¨ x âˆˆ s := iff.rfl

@[simp] lemma insert_eq_of_mem (h : a âˆˆ s) : insert a s = s :=
ext $ Î» x, or_iff_right_of_imp $ Î» e, e.symm â–¸ h

lemma ne_insert_of_not_mem (t : set Î±) : a âˆ‰ s â†’ s â‰  insert a t :=
mt $ Î» e, e.symm â–¸ mem_insert _ _

@[simp] lemma insert_eq_self : insert a s = s â†” a âˆˆ s := âŸ¨Î» h, h â–¸ mem_insert _ _, insert_eq_of_memâŸ©

lemma insert_ne_self : insert a s â‰  s â†” a âˆ‰ s := insert_eq_self.not

lemma insert_subset : insert a s âŠ† t â†” (a âˆˆ t âˆ§ s âŠ† t) :=
by simp only [subset_def, or_imp_distrib, forall_and_distrib, forall_eq, mem_insert_iff]

lemma insert_subset_insert (h : s âŠ† t) : insert a s âŠ† insert a t := Î» x, or.imp_right (@h _)

lemma insert_subset_insert_iff (ha : a âˆ‰ s) : insert a s âŠ† insert a t â†” s âŠ† t :=
begin
  refine âŸ¨Î» h x hx, _, insert_subset_insertâŸ©,
  rcases h (subset_insert _ _ hx) with (rfl|hxt),
  exacts [(ha hx).elim, hxt]
end

lemma ssubset_iff_insert : s âŠ‚ t â†” âˆƒ a âˆ‰ s, insert a s âŠ† t :=
begin
  simp only [insert_subset, exists_and_distrib_right, ssubset_def, not_subset],
  simp only [exists_prop, and_comm]
end

lemma ssubset_insert (h : a âˆ‰ s) : s âŠ‚ insert a s := ssubset_iff_insert.2 âŸ¨a, h, subset.rflâŸ©

lemma insert_comm (a b : Î±) (s : set Î±) : insert a (insert b s) = insert b (insert a s) :=
ext $ Î» x, or.left_comm

@[simp] lemma insert_idem (a : Î±) (s : set Î±) : insert a (insert a s) = insert a s :=
insert_eq_of_mem $ mem_insert _ _

lemma insert_union : insert a s âˆª t = insert a (s âˆª t) := ext $ Î» x, or.assoc

@[simp] lemma union_insert : s âˆª insert a t = insert a (s âˆª t) := ext $ Î» x, or.left_comm

@[simp] lemma insert_nonempty (a : Î±) (s : set Î±) : (insert a s).nonempty := âŸ¨a, mem_insert a sâŸ©

instance (a : Î±) (s : set Î±) : nonempty (insert a s : set Î±) := (insert_nonempty a s).to_subtype

lemma insert_inter_distrib (a : Î±) (s t : set Î±) : insert a (s âˆ© t) = insert a s âˆ© insert a t :=
ext $ Î» y, or_and_distrib_left

lemma insert_union_distrib (a : Î±) (s t : set Î±) : insert a (s âˆª t) = insert a s âˆª insert a t :=
ext $ Î» _, or_or_distrib_left _ _ _

lemma insert_inj (ha : a âˆ‰ s) : insert a s = insert b s â†” a = b :=
âŸ¨Î» h, eq_of_not_mem_of_mem_insert (h.subst $ mem_insert a s) ha, congr_arg _âŸ©

-- useful in proofs by induction
lemma forall_of_forall_insert {P : Î± â†’ Prop} {s : set Î±}
  (H : âˆ€ x, x âˆˆ insert a s â†’ P x) (x) (h : x âˆˆ s) : P x := H _ (or.inr h)

lemma forall_insert_of_forall {P : Î± â†’ Prop} {s : set Î±}
  (H : âˆ€ x, x âˆˆ s â†’ P x) (ha : P a) (x) (h : x âˆˆ insert a s) : P x :=
h.elim (Î» e, e.symm â–¸ ha) (H _)

lemma bex_insert_iff {P : Î± â†’ Prop} : (âˆƒ x âˆˆ insert a s, P x) â†” P a âˆ¨ âˆƒ x âˆˆ s, P x :=
bex_or_left_distrib.trans $ or_congr_left' bex_eq_left

lemma ball_insert_iff {P : Î± â†’ Prop} : (âˆ€ x âˆˆ insert a s, P x) â†” P a âˆ§ âˆ€ x âˆˆ s, P x :=
ball_or_left_distrib.trans $ and_congr_left' forall_eq

/-! ### Lemmas about singletons -/

lemma singleton_def (a : Î±) : ({a} : set Î±) = insert a âˆ… := (insert_emptyc_eq _).symm

@[simp] lemma mem_singleton_iff {a b : Î±} : a âˆˆ ({b} : set Î±) â†” a = b := iff.rfl

alias mem_singleton_iff â†” eq_of_mem_singleton mem_singleton_of_eq

@[simp] lemma set_of_eq_eq_singleton : {n | n = a} = {a} := rfl
@[simp] lemma set_of_eq_eq_singleton' : {x | a = x} = {a} := ext $ Î» x, eq_comm

-- TODO: again, annotation needed
@[simp] lemma mem_singleton (a : Î±) : a âˆˆ ({a} : set Î±) := @rfl _ _

@[simp] lemma singleton_eq_singleton_iff : {a} = ({b} : set Î±) â†” a = b :=
ext_iff.trans eq_iff_eq_cancel_left

lemma singleton_injective : injective (singleton : Î± â†’ set Î±) := Î» _ _, singleton_eq_singleton_iff.1

lemma insert_eq (x : Î±) (s : set Î±) : insert x s = ({x} : set Î±) âˆª s := rfl

@[simp] lemma singleton_nonempty (a : Î±) : ({a} : set Î±).nonempty := âŸ¨a, rflâŸ©
@[simp] lemma singleton_ne_empty (a : Î±) : ({a} : set Î±) â‰  âˆ… := (singleton_nonempty _).ne_empty

@[simp] lemma empty_ssubset_singleton : (âˆ… : set Î±) âŠ‚ {a} := (singleton_nonempty _).empty_ssubset

@[simp] lemma singleton_subset_iff : {a} âŠ† s â†” a âˆˆ s := forall_eq

lemma set_compr_eq_eq_singleton : {b | b = a} = {a} := rfl

@[simp] lemma singleton_union : {a} âˆª s = insert a s := rfl

@[simp] lemma union_singleton : s âˆª {a} = insert a s := union_comm _ _

@[simp] lemma singleton_inter_nonempty : ({a} âˆ© s).nonempty â†” a âˆˆ s :=
by simp only [set.nonempty, mem_inter_iff, mem_singleton_iff, exists_eq_left]

@[simp] lemma inter_singleton_nonempty : (s âˆ© {a}).nonempty â†” a âˆˆ s :=
by rw [inter_comm, singleton_inter_nonempty]

@[simp] lemma singleton_inter_eq_empty : {a} âˆ© s = âˆ… â†” a âˆ‰ s :=
not_nonempty_iff_eq_empty.symm.trans singleton_inter_nonempty.not

@[simp] lemma inter_singleton_eq_empty : s âˆ© {a} = âˆ… â†” a âˆ‰ s :=
by rw [inter_comm, singleton_inter_eq_empty]

lemma nmem_singleton_empty : s âˆ‰ ({âˆ…} : set (set Î±)) â†” s.nonempty :=
nonempty_iff_ne_empty.symm

instance unique_singleton (a : Î±) : unique â†¥({a} : set Î±) :=
âŸ¨âŸ¨âŸ¨a, mem_singleton aâŸ©âŸ©, Î» âŸ¨x, hâŸ©, subtype.eq hâŸ©

lemma eq_singleton_iff_unique_mem : s = {a} â†” a âˆˆ s âˆ§ âˆ€ x âˆˆ s, x = a :=
subset.antisymm_iff.trans $ and.comm.trans $ and_congr_left' singleton_subset_iff

lemma eq_singleton_iff_nonempty_unique_mem : s = {a} â†” s.nonempty âˆ§ âˆ€ x âˆˆ s, x = a :=
eq_singleton_iff_unique_mem.trans $ and_congr_left $ Î» H, âŸ¨Î» h', âŸ¨_, h'âŸ©, Î» âŸ¨x, hâŸ©, H x h â–¸ hâŸ©

-- while `simp` is capable of proving this, it is not capable of turning the LHS into the RHS.
@[simp] lemma default_coe_singleton (x : Î±) : (default : ({x} : set Î±)) = âŸ¨x, rflâŸ© := rfl

/-! ### Lemmas about pairs -/

@[simp] lemma pair_eq_singleton (a : Î±) : ({a, a} : set Î±) = {a} := union_self _

lemma pair_comm (a b : Î±) : ({a, b} : set Î±) = {b, a} := union_comm _ _

lemma pair_eq_pair_iff {x y z w : Î±} :
  ({x, y} : set Î±) = {z, w} â†” x = z âˆ§ y = w âˆ¨ x = w âˆ§ y = z :=
begin
  simp only [set.subset.antisymm_iff, set.insert_subset, set.mem_insert_iff, set.mem_singleton_iff,
    set.singleton_subset_iff],
  split,
  { tauto! },
  { rintro (âŸ¨rfl,rflâŸ©|âŸ¨rfl,rflâŸ©); simp }
end

/-! ### Lemmas about sets defined as `{x âˆˆ s | p x}`. -/

section sep

lemma mem_sep (xs : x âˆˆ s) (px : p x) : x âˆˆ {x âˆˆ s | p x} := âŸ¨xs, pxâŸ©

@[simp] lemma sep_mem_eq : {x âˆˆ s | x âˆˆ t} = s âˆ© t := rfl

@[simp] lemma mem_sep_iff : x âˆˆ {x âˆˆ s | p x} â†” x âˆˆ s âˆ§ p x := iff.rfl

lemma sep_ext_iff : {x âˆˆ s | p x} = {x âˆˆ s | q x} â†” âˆ€ x âˆˆ s, (p x â†” q x) :=
by simp_rw [ext_iff, mem_sep_iff, and.congr_right_iff]

@[simp] lemma sep_subset (s : set Î±) (p : Î± â†’ Prop) : {x âˆˆ s | p x} âŠ† s := Î» x, and.left

@[simp] lemma sep_eq_self_iff_mem_true : {x âˆˆ s | p x} = s â†” âˆ€ x âˆˆ s, p x :=
by simp_rw [ext_iff, mem_sep_iff, and_iff_left_iff_imp]

@[simp] lemma sep_eq_empty_iff_mem_false : {x âˆˆ s | p x} = âˆ… â†” âˆ€ x âˆˆ s, Â¬ p x :=
by simp_rw [ext_iff, mem_sep_iff, mem_empty_iff_false, iff_false, not_and]

@[simp] lemma sep_true : {x âˆˆ s | true} = s := inter_univ s

@[simp] lemma sep_false : {x âˆˆ s | false} = âˆ… := inter_empty s

@[simp] lemma sep_empty (p : Î± â†’ Prop) : {x âˆˆ (âˆ… : set Î±) | p x} = âˆ… := empty_inter p

@[simp] lemma sep_univ : {x âˆˆ (univ : set Î±) | p x} = {x | p x} := univ_inter p

@[simp] lemma sep_set_of : {x âˆˆ {y | p y} | q x} = {x | p x âˆ§ q x} := rfl

end sep

@[simp] lemma subset_singleton_iff : s âŠ† {a} â†” âˆ€ b âˆˆ s, b = a := iff.rfl

lemma subset_singleton_iff_eq : s âŠ† {a} â†” s = âˆ… âˆ¨ s = {a} :=
begin
  obtain rfl | hs := s.eq_empty_or_nonempty,
  { exact âŸ¨Î» _, or.inl rfl, Î» _, empty_subset _âŸ© },
  { simp [eq_singleton_iff_nonempty_unique_mem, hs, hs.ne_empty] }
end

lemma nonempty.subset_singleton_iff (h : s.nonempty) : s âŠ† {a} â†” s = {a} :=
subset_singleton_iff_eq.trans $ or_iff_right h.ne_empty

lemma ssubset_singleton_iff : s âŠ‚ {a} â†” s = âˆ… :=
begin
  rw [ssubset_iff_subset_ne, subset_singleton_iff_eq, or_and_distrib_right, and_not_self, or_false,
    and_iff_left_iff_imp],
  exact Î» h, ne_of_eq_of_ne h (singleton_ne_empty _).symm,
end

alias ssubset_singleton_iff â†” eq_empty_of_ssubset_singleton _

/-! ### Lemmas about complement -/

lemma compl_def (s : set Î±) : sá¶œ = {x | x âˆ‰ s} := rfl

lemma mem_compl (h : a âˆ‰ s) : a âˆˆ sá¶œ := h

lemma compl_set_of (p : Î± â†’ Prop) : {a | p a}á¶œ = { a | Â¬ p a } := rfl

lemma not_mem_of_mem_compl (h : x âˆˆ sá¶œ) : x âˆ‰ s := h

@[simp] lemma mem_compl_iff (s : set Î±) (x : Î±) : x âˆˆ sá¶œ â†” (x âˆ‰ s) := iff.rfl

lemma not_mem_compl_iff : x âˆ‰ sá¶œ â†” x âˆˆ s := not_not

lemma nonempty_compl : sá¶œ.nonempty â†” s â‰  univ := (ne_univ_iff_exists_not_mem s).symm

lemma mem_compl_singleton_iff {a x : Î±} : x âˆˆ ({a} : set Î±)á¶œ â†” x â‰  a := iff.rfl

lemma compl_singleton_eq (a : Î±) : ({a} : set Î±)á¶œ = {x | x â‰  a} := rfl

lemma union_eq_compl_compl_inter_compl (s t : set Î±) : s âˆª t = (sá¶œ âˆ© tá¶œ)á¶œ :=
ext $ Î» x, or_iff_not_and_not

lemma inter_eq_compl_compl_union_compl (s t : set Î±) : s âˆ© t = (sá¶œ âˆª tá¶œ)á¶œ :=
ext $ Î» x, and_iff_not_or_not

@[simp] lemma union_compl_self (s : set Î±) : s âˆª sá¶œ = univ := eq_univ_iff_forall.2 $ Î» x, em _

@[simp] lemma compl_union_self (s : set Î±) : sá¶œ âˆª s = univ := by rw [union_comm, union_compl_self]

lemma compl_subset_iff_union : sá¶œ âŠ† t â†” s âˆª t = univ :=
iff.symm $ eq_univ_iff_forall.trans $ forall_congr $ Î» a, or_iff_not_imp_left

lemma inter_subset (a b c : set Î±) : a âˆ© b âŠ† c â†” a âŠ† bá¶œ âˆª c :=
forall_congr $ Î» x, and_imp.trans $ imp_congr_right $ Î» _, imp_iff_not_or

lemma inter_compl_nonempty_iff : (s âˆ© tá¶œ).nonempty â†” Â¬ s âŠ† t :=
(not_subset.trans $ exists_congr $ by exact Î» x, by simp [mem_compl]).symm


/-! ### Powerset -/

/-- `ğ’« s = set.powerset s` is the set of all subsets of `s`. -/
def powerset (s : set Î±) : set (set Î±) := {t | t âŠ† s}

prefix `ğ’«`:100 := powerset

lemma mem_powerset {x s : set Î±} (h : x âŠ† s) : x âˆˆ ğ’« s := h

lemma subset_of_mem_powerset {x s : set Î±} (h : x âˆˆ ğ’« s) : x âŠ† s := h

@[simp] lemma mem_powerset_iff (x s : set Î±) : x âˆˆ ğ’« s â†” x âŠ† s := iff.rfl

lemma powerset_inter (s t : set Î±) : ğ’« (s âˆ© t) = ğ’« s âˆ© ğ’« t :=
ext $ Î» u, subset_inter_iff

@[simp] lemma powerset_mono : ğ’« s âŠ† ğ’« t â†” s âŠ† t :=
âŸ¨Î» h, h (subset.refl s), Î» h u hu, subset.trans hu hâŸ©

@[simp] lemma powerset_nonempty : (ğ’« s).nonempty :=
âŸ¨âˆ…, empty_subset sâŸ©

@[simp] lemma powerset_empty : ğ’« (âˆ… : set Î±) = {âˆ…} :=
ext $ Î» s, subset_empty_iff

@[simp] lemma powerset_univ : ğ’« (univ : set Î±) = univ :=
eq_univ_of_forall subset_univ

/-! ### Sets defined as an if-then-else -/

lemma mem_dite_univ_right (p : Prop) [decidable p] (t : p â†’ set Î±) (x : Î±) :
  (x âˆˆ if h : p then t h else univ) â†” (âˆ€ h : p, x âˆˆ t h) :=
by split_ifs; simp [h]

@[simp] lemma mem_ite_univ_right (p : Prop) [decidable p] (t : set Î±) (x : Î±) :
  x âˆˆ ite p t set.univ â†” (p â†’ x âˆˆ t) :=
mem_dite_univ_right p (Î» _, t) x

lemma mem_dite_univ_left (p : Prop) [decidable p] (t : Â¬ p â†’ set Î±) (x : Î±) :
  (x âˆˆ if h : p then univ else t h) â†” (âˆ€ h : Â¬ p, x âˆˆ t h)  :=
by split_ifs; simp [h]

@[simp] lemma mem_ite_univ_left (p : Prop) [decidable p] (t : set Î±) (x : Î±) :
  x âˆˆ ite p set.univ t â†” (Â¬ p â†’ x âˆˆ t) :=
mem_dite_univ_left p (Î» _, t) x

lemma mem_dite_empty_right (p : Prop) [decidable p] (t : p â†’ set Î±) (x : Î±) :
  (x âˆˆ if h : p then t h else âˆ…) â†” (âˆƒ h : p, x âˆˆ t h) :=
by split_ifs; simp [h]

@[simp] lemma mem_ite_empty_right (p : Prop) [decidable p] (t : set Î±) (x : Î±) :
  x âˆˆ ite p t âˆ… â†” p âˆ§ x âˆˆ t :=
by split_ifs; simp [h]

lemma mem_dite_empty_left (p : Prop) [decidable p] (t : Â¬ p â†’ set Î±) (x : Î±) :
  (x âˆˆ if h : p then âˆ… else t h) â†” (âˆƒ h : Â¬ p, x âˆˆ t h) :=
by split_ifs; simp [h]

@[simp] lemma mem_ite_empty_left (p : Prop) [decidable p] (t : set Î±) (x : Î±) :
  x âˆˆ ite p âˆ… t â†” Â¬ p âˆ§ x âˆˆ t :=
by split_ifs; simp [h]

/-! ### Subsingleton -/

/-- A set `s` is a `subsingleton` if it has at most one element. -/
protected def subsingleton (s : set Î±) : Prop :=
âˆ€ â¦ƒxâ¦„ (hx : x âˆˆ s) â¦ƒyâ¦„ (hy : y âˆˆ s), x = y

lemma subsingleton.anti (ht : t.subsingleton) (hst : s âŠ† t) : s.subsingleton :=
Î» x hx y hy, ht (hst hx) (hst hy)

lemma subsingleton.eq_singleton_of_mem (hs : s.subsingleton) {x:Î±} (hx : x âˆˆ s) : s = {x} :=
ext $ Î» y, âŸ¨Î» hy, (hs hx hy) â–¸ mem_singleton _, Î» hy, (eq_of_mem_singleton hy).symm â–¸ hxâŸ©

@[simp] lemma subsingleton_empty : (âˆ… : set Î±).subsingleton := Î» x, false.elim

@[simp] lemma subsingleton_singleton {a} : ({a} : set Î±).subsingleton :=
Î» x hx y hy, (eq_of_mem_singleton hx).symm â–¸ (eq_of_mem_singleton hy).symm â–¸ rfl

lemma subsingleton_of_subset_singleton (h : s âŠ† {a}) : s.subsingleton :=
subsingleton_singleton.anti h

lemma subsingleton_of_forall_eq (a : Î±) (h : âˆ€ b âˆˆ s, b = a) : s.subsingleton :=
Î» b hb c hc, (h _ hb).trans (h _ hc).symm

lemma subsingleton_iff_singleton {x} (hx : x âˆˆ s) : s.subsingleton â†” s = {x} :=
âŸ¨Î» h, h.eq_singleton_of_mem hx, Î» h,h.symm â–¸ subsingleton_singletonâŸ©

lemma subsingleton.eq_empty_or_singleton (hs : s.subsingleton) :
  s = âˆ… âˆ¨ âˆƒ x, s = {x} :=
s.eq_empty_or_nonempty.elim or.inl (Î» âŸ¨x, hxâŸ©, or.inr âŸ¨x, hs.eq_singleton_of_mem hxâŸ©)

lemma subsingleton.induction_on {p : set Î± â†’ Prop} (hs : s.subsingleton) (he : p âˆ…)
  (hâ‚ : âˆ€ x, p {x}) : p s :=
by { rcases hs.eq_empty_or_singleton with rfl|âŸ¨x, rflâŸ©, exacts [he, hâ‚ _] }

lemma subsingleton_univ [subsingleton Î±] : (univ : set Î±).subsingleton :=
Î» x hx y hy, subsingleton.elim x y

lemma subsingleton_of_univ_subsingleton (h : (univ : set Î±).subsingleton) : subsingleton Î± :=
âŸ¨Î» a b, h (mem_univ a) (mem_univ b)âŸ©

@[simp] lemma subsingleton_univ_iff : (univ : set Î±).subsingleton â†” subsingleton Î± :=
âŸ¨subsingleton_of_univ_subsingleton, Î» h, @subsingleton_univ _ hâŸ©

lemma subsingleton_of_subsingleton [subsingleton Î±] : set.subsingleton s :=
subsingleton_univ.anti (subset_univ s)

lemma subsingleton_is_top (Î± : Type*) [partial_order Î±] : set.subsingleton {x : Î± | is_top x} :=
Î» x hx y hy, hx.is_max.eq_of_le (hy x)

lemma subsingleton_is_bot (Î± : Type*) [partial_order Î±] : set.subsingleton {x : Î± | is_bot x} :=
Î» x hx y hy, hx.is_min.eq_of_ge (hy x)

lemma exists_eq_singleton_iff_nonempty_subsingleton :
  (âˆƒ a : Î±, s = {a}) â†” s.nonempty âˆ§ s.subsingleton :=
begin
  refine âŸ¨_, Î» h, _âŸ©,
  { rintros âŸ¨a, rflâŸ©,
    exact âŸ¨singleton_nonempty a, subsingleton_singletonâŸ© },
  { exact h.2.eq_empty_or_singleton.resolve_left h.1.ne_empty },
end

/-- `s`, coerced to a type, is a subsingleton type if and only if `s` is a subsingleton set. -/
@[simp, norm_cast] lemma subsingleton_coe (s : set Î±) : subsingleton s â†” s.subsingleton :=
begin
  split,
  { refine Î» h, (Î» a ha b hb, _),
    exact set_coe.ext_iff.2 (@subsingleton.elim s h âŸ¨a, haâŸ© âŸ¨b, hbâŸ©) },
  { exact Î» h, subsingleton.intro (Î» a b, set_coe.ext (h a.property b.property)) }
end

lemma subsingleton.coe_sort : s.subsingleton â†’ subsingleton s := s.subsingleton_coe.2

/-- The `coe_sort` of a set `s` in a subsingleton type is a subsingleton.
For the corresponding result for `subtype`, see `subtype.subsingleton`. -/
instance subsingleton_coe_of_subsingleton [subsingleton Î±] : subsingleton s :=
by { rw [s.subsingleton_coe], exact subsingleton_of_subsingleton }

/-! ### Nontrivial -/

/-- A set `s` is `nontrivial` if it has at least two distinct elements. -/
protected def nontrivial (s : set Î±) : Prop := âˆƒ x y âˆˆ s, x â‰  y

lemma nontrivial_of_mem_mem_ne {x y} (hx : x âˆˆ s) (hy : y âˆˆ s) (hxy : x â‰  y) : s.nontrivial :=
âŸ¨x, hx, y, hy, hxyâŸ©

/-- Extract witnesses from s.nontrivial. This function might be used instead of case analysis on the
argument. Note that it makes a proof depend on the classical.choice axiom. -/
protected noncomputable def nontrivial.some (hs : s.nontrivial) : Î± Ã— Î± :=
(hs.some, hs.some_spec.some_spec.some)

protected lemma nontrivial.some_fst_mem (hs : s.nontrivial) : hs.some.fst âˆˆ s := hs.some_spec.some

protected lemma nontrivial.some_snd_mem (hs : s.nontrivial) : hs.some.snd âˆˆ s :=
hs.some_spec.some_spec.some_spec.some

protected lemma nontrivial.some_fst_ne_some_snd (hs : s.nontrivial) : hs.some.fst â‰  hs.some.snd :=
hs.some_spec.some_spec.some_spec.some_spec

lemma nontrivial.mono (hs : s.nontrivial) (hst : s âŠ† t) : t.nontrivial :=
let âŸ¨x, hx, y, hy, hxyâŸ© := hs in âŸ¨x, hst hx, y, hst hy, hxyâŸ©

lemma nontrivial_pair {x y} (hxy : x â‰  y) : ({x, y} : set Î±).nontrivial :=
âŸ¨x, mem_insert _ _, y, mem_insert_of_mem _ (mem_singleton _), hxyâŸ©

lemma nontrivial_of_pair_subset {x y} (hxy : x â‰  y) (h : {x, y} âŠ† s) : s.nontrivial :=
(nontrivial_pair hxy).mono h

lemma nontrivial.pair_subset (hs : s.nontrivial) : âˆƒ x y (hab : x â‰  y), {x, y} âŠ† s :=
let âŸ¨x, hx, y, hy, hxyâŸ© := hs in âŸ¨x, y, hxy, insert_subset.2 âŸ¨hx, (singleton_subset_iff.2 hy)âŸ©âŸ©

lemma nontrivial_iff_pair_subset : s.nontrivial â†” âˆƒ x y (hxy : x â‰  y), {x, y} âŠ† s :=
âŸ¨nontrivial.pair_subset, Î» H, let âŸ¨x, y, hxy, hâŸ© := H in nontrivial_of_pair_subset hxy hâŸ©

lemma nontrivial_of_exists_ne {x} (hx : x âˆˆ s) (h : âˆƒ y âˆˆ s, y â‰  x) : s.nontrivial :=
let âŸ¨y, hy, hyxâŸ© := h in âŸ¨y, hy, x, hx, hyxâŸ©

lemma nontrivial.exists_ne (hs : s.nontrivial) (z) : âˆƒ x âˆˆ s, x â‰  z :=
begin
  by_contra H, push_neg at H,
  rcases hs with âŸ¨x, hx, y, hy, hxyâŸ©,
  rw [H x hx, H y hy] at hxy,
  exact hxy rfl
end

lemma nontrivial_iff_exists_ne {x} (hx : x âˆˆ s) : s.nontrivial â†” âˆƒ y âˆˆ s, y â‰  x :=
âŸ¨Î» H, H.exists_ne _, nontrivial_of_exists_ne hxâŸ©

lemma nontrivial_of_lt [preorder Î±] {x y} (hx : x âˆˆ s) (hy : y âˆˆ s) (hxy : x < y) : s.nontrivial :=
âŸ¨x, hx, y, hy, ne_of_lt hxyâŸ©

lemma nontrivial_of_exists_lt [preorder Î±] (H : âˆƒ x y âˆˆ s, x < y) : s.nontrivial :=
let âŸ¨x, hx, y, hy, hxyâŸ© := H in nontrivial_of_lt hx hy hxy

lemma nontrivial.exists_lt [linear_order Î±] (hs : s.nontrivial) : âˆƒ x y âˆˆ s, x < y :=
let âŸ¨x, hx, y, hy, hxyâŸ© := hs in
or.elim (lt_or_gt_of_ne hxy) (Î» H, âŸ¨x, hx, y, hy, HâŸ©) (Î» H, âŸ¨y, hy, x, hx, HâŸ©)

lemma nontrivial_iff_exists_lt [linear_order Î±] : s.nontrivial â†” âˆƒ x y âˆˆ s, x < y :=
âŸ¨nontrivial.exists_lt, nontrivial_of_exists_ltâŸ©

protected lemma nontrivial.nonempty (hs : s.nontrivial) : s.nonempty :=
let âŸ¨x, hx, _âŸ© := hs in âŸ¨x, hxâŸ©

protected lemma nontrivial.ne_empty (hs : s.nontrivial) : s â‰  âˆ… := hs.nonempty.ne_empty

lemma nontrivial.not_subset_empty (hs : s.nontrivial) : Â¬ s âŠ† âˆ… := hs.nonempty.not_subset_empty

@[simp] lemma not_nontrivial_empty : Â¬ (âˆ… : set Î±).nontrivial := Î» h, h.ne_empty rfl

@[simp] lemma not_nontrivial_singleton {x} : Â¬ ({x} : set Î±).nontrivial :=
Î» H, begin
  rw nontrivial_iff_exists_ne (mem_singleton x) at H,
  exact let âŸ¨y, hy, hyaâŸ© := H in hya (mem_singleton_iff.1 hy)
end

lemma nontrivial.ne_singleton {x} (hs : s.nontrivial) : s â‰  {x} :=
Î» H, by { rw H at hs, exact not_nontrivial_singleton hs }

lemma nontrivial.not_subset_singleton {x} (hs : s.nontrivial) : Â¬ s âŠ† {x} :=
(not_congr subset_singleton_iff_eq).2 (not_or hs.ne_empty hs.ne_singleton)

lemma nontrivial_univ [nontrivial Î±] : (univ : set Î±).nontrivial :=
let âŸ¨x, y, hxyâŸ© := exists_pair_ne Î± in âŸ¨x, mem_univ _, y, mem_univ _, hxyâŸ©

lemma nontrivial_of_univ_nontrivial (h : (univ : set Î±).nontrivial) : nontrivial Î± :=
let âŸ¨x, _, y, _, hxyâŸ© := h in âŸ¨âŸ¨x, y, hxyâŸ©âŸ©

@[simp] lemma nontrivial_univ_iff : (univ : set Î±).nontrivial â†” nontrivial Î± :=
âŸ¨nontrivial_of_univ_nontrivial, Î» h, @nontrivial_univ _ hâŸ©

lemma nontrivial_of_nontrivial (hs : s.nontrivial) : nontrivial Î± :=
let âŸ¨x, _, y, _, hxyâŸ© := hs in âŸ¨âŸ¨x, y, hxyâŸ©âŸ©

/-- `s`, coerced to a type, is a nontrivial type if and only if `s` is a nontrivial set. -/
@[simp, norm_cast] lemma nontrivial_coe_sort : nontrivial s â†” s.nontrivial :=
by simp_rw [â† nontrivial_univ_iff, set.nontrivial, mem_univ,
            exists_true_left, set_coe.exists, subtype.mk_eq_mk]

alias nontrivial_coe_sort â†” _ nontrivial.coe_sort

/-- A type with a set `s` whose `coe_sort` is a nontrivial type is nontrivial.
For the corresponding result for `subtype`, see `subtype.nontrivial_iff_exists_ne`. -/
lemma nontrivial_of_nontrivial_coe (hs : nontrivial s) : nontrivial Î± :=
nontrivial_of_nontrivial $ nontrivial_coe_sort.1 hs

lemma nontrivial_mono (hst : s âŠ† t) (hs : nontrivial s) :
  nontrivial t := nontrivial.coe_sort $ (nontrivial_coe_sort.1 hs).mono hst

@[simp] lemma not_subsingleton_iff : Â¬ s.subsingleton â†” s.nontrivial :=
by simp_rw [set.subsingleton, set.nontrivial, not_forall]

@[simp] lemma not_nontrivial_iff : Â¬ s.nontrivial â†” s.subsingleton :=
iff.not_left not_subsingleton_iff.symm

alias not_nontrivial_iff â†” _ subsingleton.not_nontrivial
alias not_subsingleton_iff â†” _ nontrivial.not_subsingleton

lemma univ_eq_true_false : univ = ({true, false} : set Prop) :=
eq.symm $ eq_univ_of_forall $ classical.cases (by simp) (by simp)

end set

open set function

namespace subsingleton
variables [subsingleton Î±]

lemma eq_univ_of_nonempty : s.nonempty â†’ s = univ :=
Î» âŸ¨x, hxâŸ©, eq_univ_of_forall $ Î» y, subsingleton.elim x y â–¸ hx

@[elab_as_eliminator]
lemma set_cases {p : set Î± â†’ Prop} (h0 : p âˆ…) (h1 : p univ) (s) : p s :=
s.eq_empty_or_nonempty.elim (Î» h, h.symm â–¸ h0) $ Î» h, (eq_univ_of_nonempty h).symm â–¸ h1

lemma mem_iff_nonempty [subsingleton Î±] :
  x âˆˆ s â†” s.nonempty :=
âŸ¨Î» hx, âŸ¨x, hxâŸ©, Î» âŸ¨y, hyâŸ©, subsingleton.elim y x â–¸ hyâŸ©

end subsingleton

/-! ### Decidability instances for sets -/

namespace set
variables (s t a)

instance decidable_inter [decidable (a âˆˆ s)] [decidable (a âˆˆ t)] : decidable (a âˆˆ s âˆ© t) :=
(by apply_instance : decidable (a âˆˆ s âˆ§ a âˆˆ t))

instance decidable_union [decidable (a âˆˆ s)] [decidable (a âˆˆ t)] : decidable (a âˆˆ s âˆª t) :=
(by apply_instance : decidable (a âˆˆ s âˆ¨ a âˆˆ t))

instance decidable_compl [decidable (a âˆˆ s)] : decidable (a âˆˆ sá¶œ) :=
(by apply_instance : decidable (a âˆ‰ s))

instance decidable_emptyset : decidable_pred (âˆˆ (âˆ… : set Î±)) :=
Î» _, decidable.is_false (by simp)

instance decidable_univ : decidable_pred (âˆˆ (set.univ : set Î±)) :=
Î» _, decidable.is_true (by simp)

instance decidable_set_of (p : Î± â†’ Prop) [decidable (p a)] : decidable (a âˆˆ {a | p a}) :=
by assumption

end set
