/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import measure_theory.group.add_circle
import dynamics.ergodic.ergodic
import measure_theory.covering.density_theorem

/-!
# Ergodic maps of the additive circle

This file contains proofs of ergodicity for maps of the additive circle.

# Main definitions:

 * `add_circle.ergodic_zsmul`: given `n : ‚Ñ§` such that `1 < |n|`, the self map `y ‚Ü¶ n ‚Ä¢ y` on
   the additive circle is ergodic (wrt the Haar measure).

# TODO

 * The map `y ‚Ü¶ n ‚Ä¢ y + x` is ergodic for any `x` (and `1 < |n|`).

-/

open set function measure_theory measure_theory.measure filter metric
open_locale measure_theory nnreal ennreal topological_space pointwise

namespace add_circle

variables {T : ‚Ñù}

lemma vadd_eq_self_of_preimage_zsmul_eq_self
  {j : ‚Ñï} {n : ‚Ñ§} {s : set $ add_circle T} (hs : (Œª y, n ‚Ä¢ y)‚Åª¬π' s = s)
  {u : add_circle T} (hu : n^j ‚Ä¢ u = 0) : u +·µ• s = s :=
begin
  suffices : ‚àÄ {v : add_circle T} (hv : n^j ‚Ä¢ v = 0), v +·µ• s ‚äÜ s,
  { refine le_antisymm (this hu) _,
    conv_lhs { rw ‚Üê vadd_neg_vadd u s, },
    replace hu : n^j ‚Ä¢ (-u) = 0, { rwa [zsmul_neg', neg_smul, neg_eq_zero], },
    simpa only [le_eq_subset, set_vadd_subset_set_vadd_iff] using this hu, },
  set f : add_circle T ‚Üí add_circle T := Œª y, n ‚Ä¢ y,
  rw (is_fixed_pt.preimage_iterate hs j : (f^[j])‚Åª¬π' s = s).symm,
  rintros v hv - ‚ü®y, hy : (f^[j]) y ‚àà s, rfl‚ü©,
  change (f^[j]) (v + y) ‚àà s,
  simpa only [f, smul_iterate, smul_add, add_left_eq_self, hv, zero_add] using hy,
end

variables [hT : fact (0 < T)]
include hT

lemma pre_ergodic_zsmul {n : ‚Ñ§} (hn : 1 < |n|) :
  pre_ergodic (Œª (y : add_circle T), n ‚Ä¢ y) :=
‚ü®begin
  intros s hs hs',
  have hT‚ÇÄ : 0 < T := hT.out,
  have hT‚ÇÅ : ennreal.of_real T ‚â† 0 := by simpa,
  replace hn : 1 < n.nat_abs, { rwa [int.abs_eq_nat_abs, nat.one_lt_cast] at hn, },
  replace hs : null_measurable_set s volume := hs.null_measurable_set,
  rw [ae_eq_empty, ae_eq_univ_iff_measure_eq hs, add_circle.measure_univ],
  cases (eq_or_ne (volume s) 0) with h h, { exact or.inl h, },
  right,
  obtain ‚ü®d, -, hd‚ü© := exists_mem_of_measure_ne_zero_of_ae h
    (is_doubling_measure.ae_tendsto_measure_inter_div (volume : measure $ add_circle T) s 1),
  let I : ‚Ñï ‚Üí set (add_circle T) := Œª j, closed_ball d (T / (2 * ‚Üë(n.nat_abs^j))),
  replace hd : tendsto (Œª j, volume (s ‚à© I j) / volume (I j)) at_top (ùìù 1),
  { let Œ¥ : ‚Ñï ‚Üí ‚Ñù := Œª j, T / (2 * ‚Üë(n.nat_abs^j)),
    have hŒ¥‚ÇÄ : ‚àÄ j, 0 < Œ¥ j := Œª j, div_pos hT‚ÇÄ (by positivity),
    have hŒ¥‚ÇÅ : tendsto Œ¥ at_top (ùìù[>] 0),
    { simp_rw [Œ¥, div_eq_mul_inv, mul_inv, ‚Üê mul_assoc],
      rw ‚Üê mul_zero (T * 2‚Åª¬π),
      apply tendsto_nhds_within_pos.const_mul (by positivity : 0 < T * 2‚Åª¬π),
      simp_rw [nat.cast_pow, ‚Üê inv_pow],
      exact tendsto_pow_at_top_nhds_within_0_of_lt_1
        (by positivity) (inv_lt_one $ nat.one_lt_cast.mpr hn), },
    have hw : ‚àÄ·∂† j in at_top, d ‚àà closed_ball d (1 * Œ¥ j) :=
      eventually_of_forall (Œª j, by simp [(hŒ¥‚ÇÄ j).le]),
    exact hd _ Œ¥ hŒ¥‚ÇÅ hw, },
  suffices : ‚àÄ j, volume (s ‚à© I j) / volume (I j) = volume s / ennreal.of_real T,
  { simp_rw [this, tendsto_const_nhds_iff, ennreal.div_eq_one_iff hT‚ÇÅ ennreal.of_real_ne_top] at hd,
    exact hd, },
  intros j,
  have hnj : 0 < n.nat_abs^j, { positivity, },
  have hnj' : 1 ‚â§ (‚Üë(n.nat_abs ^ j) : ‚Ñù), { norm_cast, exact nat.succ_le_iff.mpr hnj, },
  let u : add_circle T := ‚Üë(((‚Üë1 : ‚Ñù) / ‚Üë(n.nat_abs^j)) * T),
  have hu‚ÇÄ : add_order_of u = n.nat_abs^j,
  { exact add_order_of_div_of_gcd_eq_one hnj (gcd_one_right _), },
  have hu‚ÇÅ : is_of_fin_add_order u, { rwa [‚Üê add_order_of_pos_iff, hu‚ÇÄ], },
  have hu‚ÇÇ : n^j ‚Ä¢ u = 0,
  { rw [‚Üê add_order_of_dvd_iff_zsmul_eq_zero, hu‚ÇÄ, int.coe_nat_pow, ‚Üê int.abs_eq_nat_abs, ‚Üê abs_pow,
      abs_dvd], },
  have hI‚ÇÄ : volume (I j) ‚â† 0 := (measure_closed_ball_pos _ d $ by positivity).ne.symm,
  have hI‚ÇÅ : volume (I j) ‚â† ‚ä§ := measure_ne_top _ _,
  have hI‚ÇÇ : volume (I j) * ‚Üë(n.nat_abs ^ j) = ennreal.of_real T,
  { rw [volume_closed_ball, mul_div, mul_div_mul_left T _ two_ne_zero,
      min_eq_right (div_le_self hT‚ÇÄ.le hnj'), mul_comm, ‚Üê nsmul_eq_mul, ennreal.nsmul_of_real,
      nsmul_eq_mul, mul_div_cancel'],
    exact nat.cast_ne_zero.mpr hnj.ne.symm, },
  have hus : u +·µ• s = s := vadd_eq_self_of_preimage_zsmul_eq_self hs' hu‚ÇÇ,
  rw [ennreal.div_eq_div_iff hT‚ÇÅ ennreal.of_real_ne_top hI‚ÇÄ hI‚ÇÅ,
    volume_of_add_preimage_eq s _ u d hu‚ÇÅ hus closed_ball_ae_eq_ball, hu‚ÇÄ, nsmul_eq_mul,
    ‚Üê mul_assoc, hI‚ÇÇ],
end‚ü©

lemma ergodic_zsmul {n : ‚Ñ§} (hn : 1 < |n|) : ergodic (Œª (y : add_circle T), n ‚Ä¢ y) :=
{ .. measure_preserving_zsmul volume (abs_pos.mp $ lt_trans zero_lt_one hn),
  .. pre_ergodic_zsmul hn, }

end add_circle
