/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import measure_theory.group.add_circle
import dynamics.ergodic.ergodic
import measure_theory.covering.density_theorem
import data.set.pointwise.iterate

/-!
# Ergodic maps of the additive circle

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains proofs of ergodicity for maps of the additive circle.

## Main definitions:

 * `add_circle.ergodic_zsmul`: given `n : â„¤` such that `1 < |n|`, the self map `y â†¦ n â€¢ y` on
   the additive circle is ergodic (wrt the Haar measure).
 * `add_circle.ergodic_nsmul`: given `n : â„•` such that `1 < n`, the self map `y â†¦ n â€¢ y` on
   the additive circle is ergodic (wrt the Haar measure).
 * `add_circle.ergodic_zsmul_add`: given `n : â„¤` such that `1 < |n|` and `x : add_circle T`, the
   self map `y â†¦ n â€¢ y + x` on the additive circle is ergodic (wrt the Haar measure).
 * `add_circle.ergodic_nsmul_add`: given `n : â„•` such that `1 < n` and `x : add_circle T`, the
   self map `y â†¦ n â€¢ y + x` on the additive circle is ergodic (wrt the Haar measure).

-/

open set function measure_theory measure_theory.measure filter metric
open_locale measure_theory nnreal ennreal topology pointwise

namespace add_circle

variables {T : â„} [hT : fact (0 < T)]
include hT

/-- If a null-measurable subset of the circle is almost invariant under rotation by a family of
rational angles with denominators tending to infinity, then it must be almost empty or almost full.
-/
lemma ae_empty_or_univ_of_forall_vadd_ae_eq_self
  {s : set $ add_circle T} (hs : null_measurable_set s volume)
  {Î¹ : Type*} {l : filter Î¹} [l.ne_bot] {u : Î¹ â†’ add_circle T}
  (huâ‚ : âˆ€ i, ((u i) +áµ¥ s : set _) =áµ[volume] s) (huâ‚‚ : tendsto (add_order_of âˆ˜ u) l at_top) :
  s =áµ[volume] (âˆ… : set $ add_circle T) âˆ¨ s =áµ[volume] univ :=
begin
  /- Sketch of proof:
  Assume `T = 1` for simplicity and let `Î¼` be the Haar measure. We may assume `s` has positive
  measure since otherwise there is nothing to prove. In this case, by Lebesgue's density theorem,
  there exists a point `d` of positive density. Let `Iâ±¼` be the sequence of closed balls about `d`
  of diameter `1 / nâ±¼` where `nâ±¼` is the additive order of `uâ±¼`. Since `d` has positive density we
  must have `Î¼ (s âˆ© Iâ±¼) / Î¼ Iâ±¼ â†’ 1` along `l`. However since `s` is invariant under the action of
  `uâ±¼` and since `Iâ±¼` is a fundamental domain for this action, we must have
  `Î¼ (s âˆ© Iâ±¼) = nâ±¼ * Î¼ s = (Î¼ Iâ±¼) * Î¼ s`. We thus have `Î¼ s â†’ 1` and thus `Î¼ s = 1`. -/
  set Î¼ := (volume : measure $ add_circle T),
  set n : Î¹ â†’ â„• := add_order_of âˆ˜ u,
  have hTâ‚€ : 0 < T := hT.out,
  have hTâ‚ : ennreal.of_real T â‰  0 := by simpa,
  rw [ae_eq_empty, ae_eq_univ_iff_measure_eq hs, add_circle.measure_univ],
  cases (eq_or_ne (Î¼ s) 0) with h h, { exact or.inl h, },
  right,
  obtain âŸ¨d, -, hdâŸ© : âˆƒ d, d âˆˆ s âˆ§ âˆ€ {Î¹'} {l : filter Î¹'} (w : Î¹' â†’ add_circle T) (Î´ : Î¹' â†’ â„),
    tendsto Î´ l (ğ“[>] 0) â†’ (âˆ€á¶  j in l, d âˆˆ closed_ball (w j) (1 * Î´ j)) â†’
      tendsto (Î» j, Î¼ (s âˆ© closed_ball (w j) (Î´ j)) / Î¼ (closed_ball (w j) (Î´ j))) l (ğ“ 1) :=
    exists_mem_of_measure_ne_zero_of_ae h
      (is_unif_loc_doubling_measure.ae_tendsto_measure_inter_div Î¼ s 1),
  let I : Î¹ â†’ set (add_circle T) := Î» j, closed_ball d (T / (2 * â†‘(n j))),
  replace hd : tendsto (Î» j, Î¼ (s âˆ© I j) / Î¼ (I j)) l (ğ“ 1),
  { let Î´ : Î¹ â†’ â„ := Î» j, T / (2 * â†‘(n j)),
    have hÎ´â‚€ : âˆ€á¶  j in l, 0 < Î´ j :=
      (huâ‚‚.eventually_gt_at_top 0).mono (Î» j hj, div_pos hTâ‚€ $ by positivity),
    have hÎ´â‚ : tendsto Î´ l (ğ“[>] 0),
    { refine tendsto_nhds_within_iff.mpr âŸ¨_, hÎ´â‚€âŸ©,
      replace huâ‚‚ : tendsto (Î» j, (Tâ»Â¹ * 2) * n j) l at_top :=
        (tendsto_coe_nat_at_top_iff.mpr huâ‚‚).const_mul_at_top (by positivity : 0 < Tâ»Â¹ * 2),
      convert huâ‚‚.inv_tendsto_at_top,
      ext j,
      simp only [Î´, pi.inv_apply, mul_inv_rev, inv_inv, div_eq_inv_mul, â† mul_assoc], },
    have hw : âˆ€á¶  j in l, d âˆˆ closed_ball d (1 * Î´ j) := hÎ´â‚€.mono (Î» j hj, by simp [hj.le]),
    exact hd _ Î´ hÎ´â‚ hw, },
  suffices : âˆ€á¶  j in l, Î¼ (s âˆ© I j) / Î¼ (I j) = Î¼ s / ennreal.of_real T,
  { replace hd := hd.congr' this,
    rwa [tendsto_const_nhds_iff, ennreal.div_eq_one_iff hTâ‚ ennreal.of_real_ne_top] at hd, },
  refine (huâ‚‚.eventually_gt_at_top 0).mono (Î» j hj, _),
  have huj : is_of_fin_add_order (u j) := add_order_of_pos_iff.mp hj,
  have huj' : 1 â‰¤ (â†‘(n j) : â„), { norm_cast, exact nat.succ_le_iff.mpr hj, },
  have hIâ‚€ : Î¼ (I j) â‰  0 := (measure_closed_ball_pos _ d $ by positivity).ne.symm,
  have hIâ‚ : Î¼ (I j) â‰  âŠ¤ := measure_ne_top _ _,
  have hIâ‚‚ : Î¼ (I j) * â†‘(n j) = ennreal.of_real T,
  { rw [volume_closed_ball, mul_div, mul_div_mul_left T _ two_ne_zero,
      min_eq_right (div_le_self hTâ‚€.le huj'), mul_comm, â† nsmul_eq_mul, â† ennreal.of_real_nsmul,
      nsmul_eq_mul, mul_div_cancel'],
    exact nat.cast_ne_zero.mpr hj.ne', },
  rw [ennreal.div_eq_div_iff hTâ‚ ennreal.of_real_ne_top hIâ‚€ hIâ‚,
    volume_of_add_preimage_eq s _ (u j) d huj (huâ‚ j) closed_ball_ae_eq_ball, nsmul_eq_mul,
    â† mul_assoc, hIâ‚‚],
end

lemma ergodic_zsmul {n : â„¤} (hn : 1 < |n|) : ergodic (Î» (y : add_circle T), n â€¢ y) :=
{ ae_empty_or_univ := Î» s hs hs',
  begin
    let u : â„• â†’ add_circle T := Î» j, â†‘(((â†‘1 : â„) / â†‘(n.nat_abs^j)) * T),
    replace hn : 1 < n.nat_abs, { rwa [int.abs_eq_nat_abs, nat.one_lt_cast] at hn, },
    have huâ‚€ : âˆ€ j, add_order_of (u j) = n.nat_abs^j,
    { exact Î» j, add_order_of_div_of_gcd_eq_one (pow_pos (pos_of_gt hn) j) (gcd_one_left _), },
    have hnu : âˆ€ j, n^j â€¢ (u j) = 0 := Î» j, by rw [â† add_order_of_dvd_iff_zsmul_eq_zero, huâ‚€,
      int.coe_nat_pow, int.coe_nat_abs, â† abs_pow, abs_dvd],
    have huâ‚ : âˆ€ j, ((u j) +áµ¥ s : set _) =áµ[volume] s :=
      Î» j, by rw vadd_eq_self_of_preimage_zsmul_eq_self hs' (hnu j),
    have huâ‚‚ : tendsto (Î» j, add_order_of $ u j) at_top at_top,
    { simp_rw huâ‚€, exact nat.tendsto_pow_at_top_at_top_of_one_lt hn, },
    exact ae_empty_or_univ_of_forall_vadd_ae_eq_self hs.null_measurable_set huâ‚ huâ‚‚,
  end,
  .. measure_preserving_zsmul volume (abs_pos.mp $ lt_trans zero_lt_one hn), }

lemma ergodic_nsmul {n : â„•} (hn : 1 < n) : ergodic (Î» (y : add_circle T), n â€¢ y) :=
ergodic_zsmul (by simp [hn] : 1 < |(n : â„¤)|)

lemma ergodic_zsmul_add (x : add_circle T) {n : â„¤} (h : 1 < |n|) : ergodic $ Î» y, n â€¢ y + x :=
begin
  set f : add_circle T â†’ add_circle T := Î» y, n â€¢ y + x,
  let e : add_circle T â‰ƒáµ add_circle T := measurable_equiv.add_left (divisible_by.div x $ n - 1),
  have he : measure_preserving e volume volume := measure_preserving_add_left volume _,
  suffices : e âˆ˜ f âˆ˜ e.symm = Î» y, n â€¢ y,
  { rw [â† he.ergodic_conjugate_iff, this], exact ergodic_zsmul h, },
  replace h : n - 1 â‰  0, { rw â†abs_one at h, rw sub_ne_zero, exact ne_of_apply_ne _ (ne_of_gt h), },
  have hnx : n â€¢ divisible_by.div x (n - 1) = x + divisible_by.div x (n - 1),
  { conv_rhs { congr, rw â†divisible_by.div_cancel x h }, rw [sub_smul, one_smul, sub_add_cancel], },
  ext y,
  simp only [f, hnx, measurable_equiv.coe_add_left, measurable_equiv.symm_add_left, comp_app,
    smul_add, zsmul_neg', neg_smul, neg_add_rev],
  abel,
end

lemma ergodic_nsmul_add (x : add_circle T) {n : â„•} (h : 1 < n) : ergodic $ Î» y, n â€¢ y + x :=
ergodic_zsmul_add x (by simp [h] : 1 < |(n : â„¤)|)

end add_circle
