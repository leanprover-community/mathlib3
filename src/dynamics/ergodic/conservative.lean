/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import dynamics.ergodic.measure_preserving

/-!
# Conservative systems

In this file we define `f : Î± â†’ Î±` to be a *conservative* system w.r.t a measure `Î¼` if `f` is
non-singular (`measure_theory.quasi_measure_preserving`) and for every measurable set `s` of
positive measure at least one point `x âˆˆ s` returns back to `s` after some number of iterations of
`f`. There are several properties that look like they are stronger than this one but actually follow from it:

* `measure_theory.conservative.frequently_measure_inter_ne_zero`,
  `measure_theory.conservative.exists_gt_measure_inter_ne_zero`: if `Î¼ s â‰  0`, then for infinitely
  many `n`, the measure of `s âˆ© (f^[n]) â»Â¹' s` is positive.

* `measure_theory.conservative.measure_mem_forall_ge_image_not_mem_eq_zero`,
  `measure_theory.conservative.ae_mem_imp_frequently_image_mem`: a.e. every point of `s` visits `s`
  infinitely many times (PoincarÃ© recurrence theorem).

We also prove the topological PoincarÃ© recurrence theorem
`measure_theory.conservative.ae_frequently_mem_of_mem_nhds`. Let `f : Î± â†’ Î±` be a conservative
dynamical system on a topological space with second countable topology and measurable open
sets. Then almost every point `x : Î±` is recurrent: it visits every neighborhood `s âˆˆ ğ“ x`
infinitely many times.

## Tags

conservative dynamical system, Poincare recurrence theorem
-/

noncomputable theory

open classical set filter measure_theory finset function topological_space
open_locale classical topological_space

variables {Î¹ : Type*} {Î± : Type*} [measurable_space Î±] {f : Î± â†’ Î±} {s : set Î±} {Î¼ : measure Î±}

namespace measure_theory

open measure

/-- We say that a non-singular (`measure_theory.quasi_measure_preserving`) self-map is
*conservative* if for any measurable set `s` of positive measure there exists `x âˆˆ s` such that `x`
returns back to `s` under some iteration of `f`. -/
structure conservative (f : Î± â†’ Î±) (Î¼ : measure Î± . volume_tac)
  extends quasi_measure_preserving f Î¼ Î¼ : Prop :=
(exists_mem_image_mem : âˆ€ â¦ƒsâ¦„, measurable_set s â†’ Î¼ s â‰  0 â†’ âˆƒ (x âˆˆ s) (m â‰  0), f^[m] x âˆˆ s)

/-- A self-map preserving a finite measure is conservative. -/
protected lemma measure_preserving.conservative [finite_measure Î¼] (h : measure_preserving f Î¼ Î¼) :
  conservative f Î¼ :=
âŸ¨h.quasi_measure_preserving, Î» s hsm h0, h.exists_mem_image_mem hsm h0âŸ©

namespace conservative

/-- If `f` is a conservative map and `s` is a measurable set of nonzero measure, then
for infinitely many values of `m` a positive measure of points `x âˆˆ s` returns back to `s`
after `m` iterations of `f`. -/
lemma frequently_measure_inter_ne_zero (hf : conservative f Î¼) (hs : measurable_set s)
  (h0 : Î¼ s â‰  0) :
  âˆƒá¶  m in at_top, Î¼ (s âˆ© (f^[m]) â»Â¹' s) â‰  0 :=
begin
  by_contra H, simp only [not_frequently, eventually_at_top, ne.def, not_not] at H,
  rcases H with âŸ¨N, hNâŸ©,
  induction N with N ihN,
  { apply h0, simpa using hN 0 le_rfl },
  rw [imp_false] at ihN, push_neg at ihN,
  rcases ihN with âŸ¨n, hn, hÎ¼nâŸ©,
  set T := s âˆ© â‹ƒ n â‰¥ N + 1, (f^[n]) â»Â¹' s,
  have hT : measurable_set T,
    from hs.inter (measurable_set.bUnion (countable_encodable _)
      (Î» _ _, hf.measurable.iterate _ hs)),
  have hÎ¼T : Î¼ T = 0,
  { convert (measure_bUnion_null_iff $ countable_encodable _).2 hN,
    rw â† set.inter_bUnion, refl },
  have : Î¼ ((s âˆ© (f^[n]) â»Â¹' s) \ T) â‰  0, by rwa [measure_diff_null hÎ¼T],
  rcases hf.exists_mem_image_mem ((hs.inter (hf.measurable.iterate n hs)).diff hT) this
    with âŸ¨x, âŸ¨âŸ¨hxs, hxnâŸ©, hxTâŸ©, m, hm0, âŸ¨hxms, hxmâŸ©, hxxâŸ©,
  refine hxT âŸ¨hxs, mem_bUnion_iff.2 âŸ¨n + m, _, _âŸ©âŸ©,
  { exact add_le_add hn (nat.one_le_of_lt $ pos_iff_ne_zero.2 hm0) },
  { rwa [set.mem_preimage, â† iterate_add_apply] at hxm }
end

/-- If `f` is a conservative map and `s` is a measurable set of nonzero measure, then
for an arbitrafily large `m` a positive measure of points `x âˆˆ s` returns back to `s`
after `m` iterations of `f`. -/
lemma exists_gt_measure_inter_ne_zero (hf : conservative f Î¼) (hs : measurable_set s) (h0 : Î¼ s â‰  0)
  (N : â„•) :
  âˆƒ m > N, Î¼ (s âˆ© (f^[m]) â»Â¹' s) â‰  0 :=
let âŸ¨m, hm, hmNâŸ© :=
  ((hf.frequently_measure_inter_ne_zero hs h0).and_eventually (eventually_gt_at_top N)).exists
in âŸ¨m, hmN, hmâŸ©

/-- PoincarÃ© recurrence theorem: given a volume preserving map `f` and a measurable set `s`, the set
of points `x âˆˆ s` such that `x` does not return to `s` after `â‰¥ n` iterations has measure zero. -/
lemma measure_mem_forall_ge_image_not_mem_eq_zero (hf : conservative f Î¼) (hs : measurable_set s)
  (n : â„•) :
  Î¼ {x âˆˆ s | âˆ€ m â‰¥ n, f^[m] x âˆ‰ s} = 0 :=
begin
  by_contradiction H,
  have : measurable_set (s âˆ© {x | âˆ€ m â‰¥ n, f^[m] x âˆ‰ s}),
  { simp only [set_of_forall, â† compl_set_of],
    exact hs.inter (measurable_set.bInter (countable_encodable _)
      (Î» m _, hf.measurable.iterate m hs.compl)) },
  rcases (hf.exists_gt_measure_inter_ne_zero this H) n with âŸ¨m, hmn, hmâŸ©,
  rcases nonempty_of_measure_ne_zero hm with âŸ¨x, âŸ¨hxs, hxnâŸ©, hxm, -âŸ©,
  exact hxn m hmn.lt.le hxm
end

/-- PoincarÃ© recurrence theorem: given a volume preserving map `f` and a measurable set `s`,
almost every point `x âˆˆ s` returns back to `s` infinitely many times. -/
lemma ae_mem_imp_frequently_image_mem (hf : conservative f Î¼) (hs : measurable_set s) :
  âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆƒá¶  n in at_top, (f^[n] x) âˆˆ s :=
begin
  simp only [frequently_at_top, @forall_swap (_ âˆˆ s), ae_all_iff],
  intro n,
  filter_upwards [measure_zero_iff_ae_nmem.1 (hf.measure_mem_forall_ge_image_not_mem_eq_zero hs n)],
  simp
end

/-- PoincarÃ© recurrence theorem. Let `f : Î± â†’ Î±` be a conservative dynamical system on a topological
space with second countable topology and measurable open sets. Then almost every point `x : Î±`
is recurrent: it visits every neighborhood `s âˆˆ ğ“ x` infinitely many times. -/
lemma ae_frequently_mem_of_mem_nhds [topological_space Î±] [second_countable_topology Î±]
  [opens_measurable_space Î±] {f : Î± â†’ Î±} {Î¼ : measure Î±} (h : conservative f Î¼) :
  âˆ€áµ x âˆ‚Î¼, âˆ€ s âˆˆ ğ“ x, âˆƒá¶  n in at_top, f^[n] x âˆˆ s :=
begin
  rcases is_open_generated_countable_inter Î± with âŸ¨S, hSc, he, hSbâŸ©,
  have : âˆ€ s âˆˆ S, âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆƒá¶  n in at_top, (f^[n] x) âˆˆ s,
    from Î» s hs, h.ae_mem_imp_frequently_image_mem
      (is_open_of_is_topological_basis hSb hs).measurable_set,
  refine ((ae_ball_iff hSc).2 this).mono (Î» x hx s hs, _),
  rcases (mem_nhds_of_is_topological_basis hSb).1 hs with âŸ¨o, hoS, hxo, hosâŸ©,
  exact (hx o hoS hxo).mono (Î» n hn, hos hn)
end

end conservative

end measure_theory
