/-
Copyright (c) 2020 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/

import field_theory.splitting_field
import field_theory.perfect_closure
import field_theory.separable

/-!
# Algebraically Closed Field

In this file we define the typeclass for algebraically closed fields and algebraic closures,
and prove some of their properties.

## Main Definitions

- `is_alg_closed k` is the typeclass saying `k` is an algebraically closed field, i.e. every
polynomial in `k` splits.

- `is_alg_closure R K` is the typeclass saying `K` is an algebraic closure of `R`, where `R` is a
  commutative ring. This means that the map from `R` to `K` is injective, and `K` is
  algebraically closed and algebraic over `R`

- `is_alg_closed.lift` is a map from an algebraic extension `L` of `R`, into any algebraically
  closed extension of `R`.

- `is_alg_closure.equiv` is a proof that any two algebraic closures of the
  same field are isomorphic.

## Tags

algebraic closure, algebraically closed

-/
universes u v w

open_locale classical big_operators polynomial
open polynomial

variables (k : Type u) [field k]

/-- Typeclass for algebraically closed fields.

To show `polynomial.splits p f` for an arbitrary ring homomorphism `f`,
see `is_alg_closed.splits_codomain` and `is_alg_closed.splits_domain`.
-/
class is_alg_closed : Prop :=
(splits : âˆ€ p : k[X], p.splits $ ring_hom.id k)

/-- Every polynomial splits in the field extension `f : K â†’+* k` if `k` is algebraically closed.

See also `is_alg_closed.splits_domain` for the case where `K` is algebraically closed.
-/
theorem is_alg_closed.splits_codomain {k K : Type*} [field k] [is_alg_closed k] [field K]
  {f : K â†’+* k} (p : K[X]) : p.splits f :=
by { convert is_alg_closed.splits (p.map f), simp [splits_map_iff] }

/-- Every polynomial splits in the field extension `f : K â†’+* k` if `K` is algebraically closed.

See also `is_alg_closed.splits_codomain` for the case where `k` is algebraically closed.
-/
theorem is_alg_closed.splits_domain {k K : Type*} [field k] [is_alg_closed k] [field K]
  {f : k â†’+* K} (p : k[X]) : p.splits f :=
polynomial.splits_of_splits_id _ $ is_alg_closed.splits _

namespace is_alg_closed

variables {k}

theorem exists_root [is_alg_closed k] (p : k[X]) (hp : p.degree â‰  0) : âˆƒ x, is_root p x :=
exists_root_of_splits _ (is_alg_closed.splits p) hp

lemma exists_pow_nat_eq [is_alg_closed k] (x : k) {n : â„•} (hn : 0 < n) : âˆƒ z, z ^ n = x :=
begin
  rcases exists_root (X ^ n - C x) _ with âŸ¨z, hzâŸ©, swap,
  { rw degree_X_pow_sub_C hn x,
    exact ne_of_gt (with_bot.coe_lt_coe.2 hn) },
  use z,
  simp only [eval_C, eval_X, eval_pow, eval_sub, is_root.def] at hz,
  exact sub_eq_zero.1 hz
end

lemma exists_eq_mul_self [is_alg_closed k] (x : k) : âˆƒ z, x = z * z :=
begin
  rcases exists_pow_nat_eq x zero_lt_two with âŸ¨z, rflâŸ©,
  exact âŸ¨z, sq zâŸ©
end

lemma roots_eq_zero_iff [is_alg_closed k] {p : k[X]} :
  p.roots = 0 â†” p = polynomial.C (p.coeff 0) :=
begin
  refine âŸ¨Î» h, _, Î» hp, by rw [hp, roots_C]âŸ©,
  cases (le_or_lt (degree p) 0) with hd hd,
  { exact eq_C_of_degree_le_zero hd },
  { obtain âŸ¨z, hzâŸ© := is_alg_closed.exists_root p hd.ne',
    rw [â†mem_roots (ne_zero_of_degree_gt hd), h] at hz,
    simpa using hz }
end

theorem exists_evalâ‚‚_eq_zero_of_injective {R : Type*} [ring R] [is_alg_closed k] (f : R â†’+* k)
  (hf : function.injective f) (p : R[X]) (hp : p.degree â‰  0) : âˆƒ x, p.evalâ‚‚ f x = 0 :=
let âŸ¨x, hxâŸ© := exists_root (p.map f) (by rwa [degree_map_eq_of_injective hf]) in
âŸ¨x, by rwa [evalâ‚‚_eq_eval_map, â† is_root]âŸ©

theorem exists_evalâ‚‚_eq_zero {R : Type*} [field R] [is_alg_closed k] (f : R â†’+* k)
  (p : R[X]) (hp : p.degree â‰  0) : âˆƒ x, p.evalâ‚‚ f x = 0 :=
exists_evalâ‚‚_eq_zero_of_injective f f.injective p hp

variables (k)

theorem exists_aeval_eq_zero_of_injective {R : Type*} [comm_ring R] [is_alg_closed k] [algebra R k]
  (hinj : function.injective (algebra_map R k)) (p : R[X]) (hp : p.degree â‰  0) :
  âˆƒ x : k, aeval x p = 0 :=
exists_evalâ‚‚_eq_zero_of_injective (algebra_map R k) hinj p hp

theorem exists_aeval_eq_zero {R : Type*} [field R] [is_alg_closed k] [algebra R k]
  (p : R[X]) (hp : p.degree â‰  0) : âˆƒ x : k, aeval x p = 0 :=
exists_evalâ‚‚_eq_zero (algebra_map R k) p hp

theorem of_exists_root (H : âˆ€ p : k[X], p.monic â†’ irreducible p â†’ âˆƒ x, p.eval x = 0) :
  is_alg_closed k :=
âŸ¨Î» p, or.inr $ Î» q hq hqp,
 have irreducible (q * C (leading_coeff q)â»Â¹),
   by { rw â† coe_norm_unit_of_ne_zero hq.ne_zero,
        exact (associated_normalize _).irreducible hq },
 let âŸ¨x, hxâŸ© := H (q * C (leading_coeff q)â»Â¹) (monic_mul_leading_coeff_inv hq.ne_zero) this in
 degree_mul_leading_coeff_inv q hq.ne_zero â–¸ degree_eq_one_of_irreducible_of_root this hxâŸ©

lemma degree_eq_one_of_irreducible [is_alg_closed k] {p : k[X]}
  (hp : irreducible p) :
  p.degree = 1 :=
degree_eq_one_of_irreducible_of_splits hp (is_alg_closed.splits_codomain _)

lemma algebra_map_surjective_of_is_integral {k K : Type*} [field k] [ring K] [is_domain K]
  [hk : is_alg_closed k] [algebra k K] (hf : algebra.is_integral k K) :
  function.surjective (algebra_map k K) :=
begin
  refine Î» x, âŸ¨-((minpoly k x).coeff 0), _âŸ©,
  have hq : (minpoly k x).leading_coeff = 1 := minpoly.monic (hf x),
  have h : (minpoly k x).degree = 1 := degree_eq_one_of_irreducible k
    (minpoly.irreducible (hf x)),
  have : (aeval x (minpoly k x)) = 0 := minpoly.aeval k x,
  rw [eq_X_add_C_of_degree_eq_one h, hq, C_1, one_mul,
    aeval_add, aeval_X, aeval_C, add_eq_zero_iff_eq_neg] at this,
  exact (ring_hom.map_neg (algebra_map k K) ((minpoly k x).coeff 0)).symm â–¸ this.symm,
end

lemma algebra_map_surjective_of_is_integral'
  {k K : Type*} [field k] [comm_ring K] [is_domain K]
  [hk : is_alg_closed k] (f : k â†’+* K) (hf : f.is_integral) : function.surjective f :=
@algebra_map_surjective_of_is_integral k K _ _ _ _ f.to_algebra hf

lemma algebra_map_surjective_of_is_algebraic {k K : Type*} [field k] [ring K] [is_domain K]
  [hk : is_alg_closed k] [algebra k K] (hf : algebra.is_algebraic k K) :
  function.surjective (algebra_map k K) :=
algebra_map_surjective_of_is_integral (algebra.is_algebraic_iff_is_integral.mp hf)

end is_alg_closed

/-- Typeclass for an extension being an algebraic closure. -/
class is_alg_closure (R : Type u) (K : Type v) [comm_ring R]
  [field K] [algebra R K] [no_zero_smul_divisors R K] : Prop :=
(alg_closed : is_alg_closed K)
(algebraic : algebra.is_algebraic R K)

theorem is_alg_closure_iff (K : Type v) [field K] [algebra k K] :
  is_alg_closure k K â†” is_alg_closed K âˆ§ algebra.is_algebraic k K :=
âŸ¨Î» h, âŸ¨h.1, h.2âŸ©, Î» h, âŸ¨h.1, h.2âŸ©âŸ©

/--
Every element `f` in a nontrivial finite-dimensional algebra `A`
over an algebraically closed field `K`
has non-empty spectrum:
that is, there is some `c : K` so `f - c â€¢ 1` is not invertible.
-/
-- We will use this both to show eigenvalues exist, and to prove Schur's lemma.
lemma exists_spectrum_of_is_alg_closed_of_finite_dimensional (ð•œ : Type*) [field ð•œ] [is_alg_closed ð•œ]
  {A : Type*} [nontrivial A] [ring A] [algebra ð•œ A] [I : finite_dimensional ð•œ A] (f : A) :
  âˆƒ c : ð•œ, Â¬ is_unit (f - algebra_map ð•œ A c) :=
begin
  obtain âŸ¨p, âŸ¨h_mon, h_eval_pâŸ©âŸ© := is_integral_of_noetherian (is_noetherian.iff_fg.2 I) f,
  have nu : Â¬ is_unit (aeval f p), { rw [â†aeval_def] at h_eval_p, rw h_eval_p, simp, },
  rw [eq_prod_roots_of_monic_of_splits_id h_mon (is_alg_closed.splits p),
    â†multiset.prod_to_list, list.map_prod] at nu,
  replace nu := mt list.prod_is_unit nu,
  simp only [not_forall, exists_prop, aeval_C, multiset.mem_to_list,
    list.mem_map, aeval_X, exists_exists_and_eq_and, multiset.mem_map, alg_hom.map_sub] at nu,
  exact âŸ¨nu.some, nu.some_spec.2âŸ©,
end

namespace lift

/- In this section, the homomorphism from any algebraic extension into an algebraically
  closed extension is proven to exist. The assumption that M is algebraically closed could probably
  easily be switched to an assumption that M contains all the roots of polynomials in K -/
variables {K : Type u} {L : Type v} {M : Type w} [field K] [field L] [algebra K L]
  [field M] [algebra K M] [is_alg_closed M] (hL : algebra.is_algebraic K L)

variables (K L M)
include hL
open subalgebra alg_hom function

/-- This structure is used to prove the existence of a homomorphism from any algebraic extension
into an algebraic closure -/
structure subfield_with_hom :=
(carrier : subalgebra K L)
(emb : carrier â†’â‚[K] M)

variables {K L M hL}

namespace subfield_with_hom
variables {Eâ‚ Eâ‚‚ Eâ‚ƒ : subfield_with_hom K L M hL}

instance : has_le (subfield_with_hom K L M hL) :=
{ le := Î» Eâ‚ Eâ‚‚, âˆƒ h : Eâ‚.carrier â‰¤ Eâ‚‚.carrier, âˆ€ x, Eâ‚‚.emb (inclusion h x) = Eâ‚.emb x }

noncomputable instance : inhabited (subfield_with_hom K L M hL) :=
âŸ¨{ carrier := âŠ¥,
   emb := (algebra.of_id K M).comp (algebra.bot_equiv K L).to_alg_hom }âŸ©

lemma le_def : Eâ‚ â‰¤ Eâ‚‚ â†” âˆƒ h : Eâ‚.carrier â‰¤ Eâ‚‚.carrier, âˆ€ x, Eâ‚‚.emb (inclusion h x) = Eâ‚.emb x :=
iff.rfl

lemma compat (h : Eâ‚ â‰¤ Eâ‚‚) : âˆ€ x, Eâ‚‚.emb (inclusion h.fst x) = Eâ‚.emb x :=
by { rw le_def at h, cases h, assumption }

instance : preorder (subfield_with_hom K L M hL) :=
{ le := (â‰¤),
  le_refl := Î» E, âŸ¨le_rfl, by simpâŸ©,
  le_trans := Î» Eâ‚ Eâ‚‚ Eâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ,
    âŸ¨le_trans hâ‚â‚‚.fst hâ‚‚â‚ƒ.fst,
    Î» _, by erw [â† inclusion_inclusion hâ‚â‚‚.fst hâ‚‚â‚ƒ.fst, compat, compat]âŸ© }

open lattice

lemma maximal_subfield_with_hom_chain_bounded (c : set (subfield_with_hom K L M hL))
  (hc : is_chain (â‰¤) c) :
  âˆƒ ub : subfield_with_hom K L M hL, âˆ€ N, N âˆˆ c â†’ N â‰¤ ub :=
if hcn : c.nonempty then
let ub : subfield_with_hom K L M hL :=
by haveI : nonempty c := set.nonempty.to_subtype hcn; exact
{ carrier := â¨† i : c, (i : subfield_with_hom K L M hL).carrier,
  emb := subalgebra.supr_lift
    (Î» i : c, (i : subfield_with_hom K L M hL).carrier)
    (Î» i j, let âŸ¨k, hik, hjkâŸ© := directed_on_iff_directed.1 hc.directed_on i j in
      âŸ¨k, hik.fst, hjk.fstâŸ©)
    (Î» i, (i : subfield_with_hom K L M hL).emb)
    begin
      assume i j h,
      ext x,
      cases hc.total i.prop j.prop with hij hji,
      { simp [â† hij.snd x] },
      { erw [alg_hom.comp_apply, â† hji.snd (inclusion h x),
          inclusion_inclusion, inclusion_self, alg_hom.id_apply x] }
    end _ rfl } in
âŸ¨ub, Î» N hN, âŸ¨(le_supr (Î» i : c, (i : subfield_with_hom K L M hL).carrier) âŸ¨N, hNâŸ© : _),
  begin
    intro x,
    simp [ub],
    refl
  endâŸ©âŸ©
else by { rw [set.not_nonempty_iff_eq_empty] at hcn, simp [hcn], }

variables (hL M)

lemma exists_maximal_subfield_with_hom : âˆƒ E : subfield_with_hom K L M hL,
  âˆ€ N, E â‰¤ N â†’ N â‰¤ E :=
exists_maximal_of_chains_bounded
  maximal_subfield_with_hom_chain_bounded (Î» _ _ _, le_trans)

/-- The maximal `subfield_with_hom`. We later prove that this is equal to `âŠ¤`. -/
noncomputable def maximal_subfield_with_hom : subfield_with_hom K L M hL :=
classical.some (exists_maximal_subfield_with_hom M hL)

lemma maximal_subfield_with_hom_is_maximal :
  âˆ€ (N : subfield_with_hom K L M hL),
    (maximal_subfield_with_hom M hL) â‰¤ N â†’ N â‰¤ (maximal_subfield_with_hom M hL) :=
classical.some_spec (exists_maximal_subfield_with_hom M hL)

lemma maximal_subfield_with_hom_eq_top :
  (maximal_subfield_with_hom M hL).carrier = âŠ¤ :=
begin
  rw [eq_top_iff],
  intros x _,
  let p := minpoly K x,
  let N : subalgebra K L := (maximal_subfield_with_hom M hL).carrier,
  letI : field N := (subalgebra.is_field_of_algebraic N hL).to_field,
  letI : algebra N M := (maximal_subfield_with_hom M hL).emb.to_ring_hom.to_algebra,
  cases is_alg_closed.exists_aeval_eq_zero M (minpoly N x)
    (ne_of_gt (minpoly.degree_pos
      (is_algebraic_iff_is_integral.1
        (algebra.is_algebraic_of_larger_base _ _ hL x)))) with y hy,
  let O : subalgebra N L := algebra.adjoin N {(x : L)},
  let larger_emb := ((adjoin_root.lift_hom (minpoly N x) y hy).comp
     (alg_equiv.adjoin_singleton_equiv_adjoin_root_minpoly N x).to_alg_hom),
  have hNO : N â‰¤ O.restrict_scalars K,
  { intros z hz,
    show algebra_map N L âŸ¨z, hzâŸ© âˆˆ O,
    exact O.algebra_map_mem _ },
  let O' : subfield_with_hom K L M hL :=
  { carrier := O.restrict_scalars K,
    emb := larger_emb.restrict_scalars K },
  have hO' : maximal_subfield_with_hom M hL â‰¤ O',
  { refine âŸ¨hNO, _âŸ©,
    intros z,
    show O'.emb (algebra_map N O z) = algebra_map N M z,
    simp only [O', restrict_scalars_apply, alg_hom.commutes] },
  refine (maximal_subfield_with_hom_is_maximal M hL O' hO').fst _,
  exact algebra.subset_adjoin (set.mem_singleton x),
end

end subfield_with_hom
end lift

namespace is_alg_closed

variables {K : Type u} [field K] {L : Type v} {M : Type w} [field L] [algebra K L]
  [field M] [algebra K M] [is_alg_closed M] (hL : algebra.is_algebraic K L)

variables (K L M)
include hL

/-- Less general version of `lift`. -/
@[irreducible] private noncomputable def lift_aux : L â†’â‚[K] M :=
(lift.subfield_with_hom.maximal_subfield_with_hom M hL).emb.comp $
  eq.rec_on (lift.subfield_with_hom.maximal_subfield_with_hom_eq_top M hL).symm algebra.to_top

omit hL

variables {R : Type u} [comm_ring R]
variables {S : Type v} [comm_ring S] [is_domain S] [algebra R S]
  [algebra R M] [no_zero_smul_divisors R S]
  [no_zero_smul_divisors R M]
  (hS : algebra.is_algebraic R S)
variables {M}

include hS

/-- A (random) homomorphism from an algebraic extension of R into an algebraically
  closed extension of R. -/

@[irreducible] noncomputable def lift : S â†’â‚[R] M :=
begin
  letI : is_domain R := (no_zero_smul_divisors.algebra_map_injective R S).is_domain _,
  have hfRfS : algebra.is_algebraic (fraction_ring R) (fraction_ring S),
    from Î» x, (is_fraction_ring.is_algebraic_iff R (fraction_ring R) (fraction_ring S)).1
      ((is_fraction_ring.is_algebraic_iff' R S (fraction_ring S)).1 hS x),
  let f : fraction_ring S â†’â‚[fraction_ring R] M :=
    lift_aux (fraction_ring R) (fraction_ring S) M hfRfS,
  exact (f.restrict_scalars R).comp ((algebra.of_id S (fraction_ring S)).restrict_scalars R),
end

omit hS
@[priority 100]
noncomputable instance perfect_ring (p : â„•) [fact p.prime] [char_p k p]
  [is_alg_closed k] : perfect_ring k p :=
perfect_ring.of_surjective k p $ Î» x, is_alg_closed.exists_pow_nat_eq _ $ fact.out _

/-- Algebraically closed fields are infinite since `Xâ¿âºÂ¹ - 1` is separable when `#K = n` -/
@[priority 500]
instance {K : Type*} [field K] [is_alg_closed K] : infinite K :=
begin
  apply infinite.mk,
  introsI hfin,
  set n := fintype.card K with hn,
  set f := (X : K[X]) ^ (n + 1) - 1 with hf,
  have hfsep : separable f := separable_X_pow_sub_C 1 (by simp) one_ne_zero,
  apply nat.not_succ_le_self (fintype.card K),
  have hroot : n.succ = fintype.card (f.root_set K),
  { erw [card_root_set_eq_nat_degree hfsep (is_alg_closed.splits_domain _),
         nat_degree_X_pow_sub_C] },
  rw hroot,
  exact fintype.card_le_of_injective coe subtype.coe_injective,
end

end is_alg_closed

namespace is_alg_closure

variables (K : Type*) (J : Type*) (R : Type u) (S : Type*) [field K] [field J] [comm_ring R]
  (L : Type v) (M : Type w) [field L] [field M] [algebra R M] [no_zero_smul_divisors R M]
  [is_alg_closure R M] [algebra K M] [is_alg_closure K M]
  [comm_ring S] [algebra S L] [no_zero_smul_divisors S L] [is_alg_closure S L]

local attribute [instance] is_alg_closure.alg_closed

section
variables [algebra R L] [no_zero_smul_divisors R L] [is_alg_closure R L]

/-- A (random) isomorphism between two algebraic closures of `R`. -/
noncomputable def equiv : L â‰ƒâ‚[R] M :=
let f : L â†’â‚[R] M := is_alg_closed.lift is_alg_closure.algebraic in
alg_equiv.of_bijective f
  âŸ¨ring_hom.injective f.to_ring_hom,
    begin
      letI : algebra L M := ring_hom.to_algebra f,
      letI : is_scalar_tower R L M :=
        is_scalar_tower.of_algebra_map_eq (by simp [ring_hom.algebra_map_to_algebra]),
      show function.surjective (algebra_map L M),
      exact is_alg_closed.algebra_map_surjective_of_is_algebraic
        (algebra.is_algebraic_of_larger_base_of_injective
          (no_zero_smul_divisors.algebra_map_injective R _) is_alg_closure.algebraic),
    endâŸ©

end

section equiv_of_algebraic

variables [algebra R S] [algebra R L] [is_scalar_tower R S L]
variables [algebra K J] [algebra J L] [is_alg_closure J L] [algebra K L]
  [is_scalar_tower K J L]


/-- A (random) isomorphism between an algebraic closure of `R` and an algebraic closure of
  an algebraic extension of `R` -/
noncomputable def equiv_of_algebraic' [nontrivial S] [no_zero_smul_divisors R S]
  (hRL : algebra.is_algebraic R L) : L â‰ƒâ‚[R] M :=
begin
  letI : no_zero_smul_divisors R L :=
    no_zero_smul_divisors.of_algebra_map_injective begin
      rw [is_scalar_tower.algebra_map_eq R S L],
      exact function.injective.comp
        (no_zero_smul_divisors.algebra_map_injective _ _)
        (no_zero_smul_divisors.algebra_map_injective _ _)
    end,
  letI : is_alg_closure R L :=
  { alg_closed := by apply_instance,
    algebraic := hRL },
  exact is_alg_closure.equiv _ _ _
end

/-- A (random) isomorphism between an algebraic closure of `K` and an algebraic closure
  of an algebraic extension of `K` -/
noncomputable def equiv_of_algebraic (hKJ : algebra.is_algebraic K J) : L â‰ƒâ‚[K] M :=
equiv_of_algebraic' K J _ _ (algebra.is_algebraic_trans hKJ is_alg_closure.algebraic)

end equiv_of_algebraic

section equiv_of_equiv

variables {R S}

/-- Used in the definition of `equiv_of_equiv` -/
noncomputable def equiv_of_equiv_aux (hSR : S â‰ƒ+* R) :
  { e : L â‰ƒ+* M // e.to_ring_hom.comp (algebra_map S L) =
    (algebra_map R M).comp hSR.to_ring_hom }:=
begin
  letI : algebra R S := ring_hom.to_algebra hSR.symm.to_ring_hom,
  letI : algebra S R := ring_hom.to_algebra hSR.to_ring_hom,
  letI : is_domain R := (no_zero_smul_divisors.algebra_map_injective R M).is_domain _,
  letI : is_domain S := (no_zero_smul_divisors.algebra_map_injective S L).is_domain _,
  have : algebra.is_algebraic R S,
    from Î» x, begin
      rw [â† ring_equiv.symm_apply_apply hSR x],
      exact is_algebraic_algebra_map _
    end,
  letI : algebra R L := ring_hom.to_algebra ((algebra_map S L).comp (algebra_map R S)),
  haveI : is_scalar_tower R S L := is_scalar_tower.of_algebra_map_eq (Î» _, rfl),
  haveI : is_scalar_tower S R L := is_scalar_tower.of_algebra_map_eq
    (by simp [ring_hom.algebra_map_to_algebra]),
  haveI : no_zero_smul_divisors R S :=
    no_zero_smul_divisors.of_algebra_map_injective hSR.symm.injective,
  refine âŸ¨equiv_of_algebraic' R S L M (algebra.is_algebraic_of_larger_base_of_injective
      (show function.injective (algebra_map S R), from hSR.injective)
      is_alg_closure.algebraic) , _âŸ©,
  ext,
  simp only [ring_equiv.to_ring_hom_eq_coe, function.comp_app, ring_hom.coe_comp,
    alg_equiv.coe_ring_equiv, ring_equiv.coe_to_ring_hom],
  conv_lhs { rw [â† hSR.symm_apply_apply x] },
  show equiv_of_algebraic' R S L M _ (algebra_map R L (hSR x)) = _,
  rw [alg_equiv.commutes]
end

/-- Algebraic closure of isomorphic fields are isomorphic -/
noncomputable def equiv_of_equiv (hSR : S â‰ƒ+* R) : L â‰ƒ+* M :=
equiv_of_equiv_aux L M hSR

@[simp] lemma equiv_of_equiv_comp_algebra_map (hSR : S â‰ƒ+* R) :
  (â†‘(equiv_of_equiv L M hSR) : L â†’+* M).comp (algebra_map S L) =
  (algebra_map R M).comp hSR :=
(equiv_of_equiv_aux L M hSR).2

@[simp] lemma equiv_of_equiv_algebra_map (hSR : S â‰ƒ+* R) (s : S):
  equiv_of_equiv L M hSR (algebra_map S L s) =
  algebra_map R M (hSR s) :=
ring_hom.ext_iff.1 (equiv_of_equiv_comp_algebra_map L M hSR) s

@[simp] lemma equiv_of_equiv_symm_algebra_map (hSR : S â‰ƒ+* R) (r : R):
  (equiv_of_equiv L M hSR).symm (algebra_map R M r) =
  algebra_map S L (hSR.symm r) :=
(equiv_of_equiv L M hSR).injective (by simp)

@[simp] lemma equiv_of_equiv_symm_comp_algebra_map (hSR : S â‰ƒ+* R) :
  ((equiv_of_equiv L M hSR).symm : M â†’+* L).comp (algebra_map R M) =
  (algebra_map S L).comp hSR.symm :=
ring_hom.ext_iff.2 (equiv_of_equiv_symm_algebra_map L M hSR)

end equiv_of_equiv

end is_alg_closure
