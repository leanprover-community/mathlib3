/-
Copyright (c) 2022 Sebastian Monnet. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sebastian Monnet
-/

import field_theory.galois
import topology.algebra.filter_basis
import algebra.algebra.subalgebra

/-!
# Krull topology

We define the Krull topology on `L ‚âÉ‚Çê[K] L` for an arbitrary field extension `L/K`. In order to do
this, we first define a `group_filter_basis` on `L ‚âÉ‚Çê[K] L`, whose sets are `E.fixing_subgroup` for
all intermediate fields `E` with `E/K` finite dimensional.

## Main Definitions

- `finite_exts K L`. Given a field extension `L/K`, this is the set of intermediate fields that are
  finite-dimensional over `K`.

- `fixed_by_finite K L`. Given a field extension `L/K`, `fixed_by_finite K L` is the set of
  subsets `Gal(L/E)` of `Gal(L/K)`, where `E/K` is finite

- `gal_basis K L`. Given a field extension `L/K`, this is the filter basis on `L ‚âÉ‚Çê[K] L` whose
  sets are `Gal(L/E)` for intermediate fields `E` with `E/K` finite.

- `gal_group_basis K L`. This is the same as `gal_basis K L`, but with the added structure
  that it is a group filter basis on `L ‚âÉ‚Çê[K] L`, rather than just a filter basis.

- `krull_topology K L`. Given a field extension `L/K`, this is the topology on `L ‚âÉ‚Çê[K] L`, induced
  by the group filter basis `gal_group_basis K L`.

## Main Results

- `krull_topology_t2 K L h_int`. For an integral field extension `L/K` (one that satisfies
  `h_int : algebra.is_integral K L`), the Krull topology on `L ‚âÉ‚Çê[K] L`, `krull_topology K L`,
  is Hausdorff.

## Notations

- In docstrings, we will write `Gal(L/E)` to denote the fixing subgroup of an intermediate field
  `E`. That is, `Gal(L/E)` is the subgroup of `L ‚âÉ‚Çê[K] L` consisting of automorphisms that fix
  every element of `E`. In particular, we distinguish between `L ‚âÉ‚Çê[E] L` and `Gal(L/E)`, since the
  former is defined to be a subgroup of `L ‚âÉ‚Çê[K] L`, while the latter is a group in its own right.

## Implementation Notes

- `krull_topology K L` is defined as an instance for type class inference.
-/

open_locale classical

/-- Mapping intermediate fields along algebra equivalences preserves the partial order -/
lemma intermediate_field.map_mono {K L M : Type*} [field K] [field L] [field M]
  [algebra K L] [algebra K M] {E1 E2 : intermediate_field K L}
  (e : L ‚âÉ‚Çê[K] M) (h12 : E1 ‚â§ E2) : E1.map e.to_alg_hom ‚â§ E2.map e.to_alg_hom :=
set.image_subset e h12

/-- Mapping intermediate fields along the identity does not change them -/
lemma intermediate_field.map_id {K L : Type*} [field K] [field L] [algebra K L]
  (E : intermediate_field K L) :
  E.map (alg_hom.id K L) = E :=
set_like.coe_injective $ set.image_id _

/-- Mapping a finite dimensional intermediate field along an algebra equivalence gives
a finite-dimensional intermediate field. -/
instance im_finite_dimensional {K L : Type*} [field K] [field L] [algebra K L]
  {E : intermediate_field K L} (œÉ : L ‚âÉ‚Çê[K] L) [finite_dimensional K E]:
  finite_dimensional K (E.map œÉ.to_alg_hom) :=
linear_equiv.finite_dimensional (intermediate_field.intermediate_field_map œÉ E).to_linear_equiv

/-- Given a field extension `L/K`, `finite_exts K L` is the set of
intermediate field extensions `L/E/K` such that `E/K` is finite -/
def finite_exts (K : Type*) [field K] (L : Type*) [field L] [algebra K L] :
  set (intermediate_field K L) :=
{E | finite_dimensional K E}

/-- Given a field extension `L/K`, `fixed_by_finite K L` is the set of
subsets `Gal(L/E)` of `L ‚âÉ‚Çê[K] L`, where `E/K` is finite -/
def fixed_by_finite (K L : Type*) [field K] [field L] [algebra K L]: set (subgroup (L ‚âÉ‚Çê[K] L)) :=
intermediate_field.fixing_subgroup '' (finite_exts K L)

/-- For an field extension `L/K`, the intermediate field `K` is finite-dimensional over `K` -/
lemma intermediate_field.finite_dimensional_bot (K L : Type*) [field K]
  [field L] [algebra K L] : finite_dimensional K (‚ä• : intermediate_field K L) :=
finite_dimensional_of_dim_eq_one intermediate_field.dim_bot

/-- This lemma says that `Gal(L/K) = L ‚âÉ‚Çê[K] L` -/
lemma intermediate_field.fixing_subgroup.bot {K L : Type*} [field K]
  [field L] [algebra K L] :
  intermediate_field.fixing_subgroup (‚ä• : intermediate_field K L) = ‚ä§ :=
begin
  ext f,
  refine ‚ü®Œª _, subgroup.mem_top _, Œª _, _‚ü©,
  rintro ‚ü®x, hx : x ‚àà (‚ä• : intermediate_field K L)‚ü©,
  rw intermediate_field.mem_bot at hx,
  rcases hx with ‚ü®y, rfl‚ü©,
  exact f.commutes y,
end

/-- If `L/K` is a field extension, then we have `Gal(L/K) ‚àà fixed_by_finite K L` -/
lemma top_fixed_by_finite {K L : Type*} [field K] [field L] [algebra K L] :
  ‚ä§ ‚àà fixed_by_finite K L :=
‚ü®‚ä•, intermediate_field.finite_dimensional_bot K L, intermediate_field.fixing_subgroup.bot‚ü©

/-- If `E1` and `E2` are finite-dimensional intermediate fields, then so is their compositum.
This rephrases a result already in mathlib so that it is compatible with our type classes -/
lemma finite_dimensional_sup {K L: Type*} [field K] [field L] [algebra K L]
  (E1 E2 : intermediate_field K L) (h1 : finite_dimensional K E1)
  (h2 : finite_dimensional K E2) : finite_dimensional K ‚Ü•(E1 ‚äî E2) :=
by exactI intermediate_field.finite_dimensional_sup E1 E2

/-- An element of `L ‚âÉ‚Çê[K] L` is in `Gal(L/E)` if and only if it fixes every element of `E`-/
lemma mem_fixing_subgroup_iff {K L : Type*} [field K] [field L] [algebra K L]
  (E : intermediate_field K L) (œÉ : (L ‚âÉ‚Çê[K] L)) :
  œÉ ‚àà E.fixing_subgroup ‚Üî‚àÄ (x : L), x ‚àà E ‚Üí œÉ x = x :=
‚ü®Œª hœÉ x hx, hœÉ ‚ü®x, hx‚ü©, Œª h ‚ü®x, hx‚ü©, h x hx‚ü©

/-- The map `E ‚Ü¶ Gal(L/E)` is inclusion-reversing -/
lemma intermediate_field.fixing_subgroup.antimono {K L : Type*} [field K] [field L] [algebra K L]
  {E1 E2 : intermediate_field K L} (h12 : E1 ‚â§ E2) : E2.fixing_subgroup ‚â§ E1.fixing_subgroup :=
begin
  rintro œÉ hœÉ ‚ü®x, hx‚ü©,
  exact hœÉ ‚ü®x, h12 hx‚ü©,
end

/-- Given a field extension `L/K`, `gal_basis K L` is the filter basis on `L ‚âÉ‚Çê[K] L` whose sets
are `Gal(L/E)` for intermediate fields `E` with `E/K` finite dimensional -/
def gal_basis (K L : Type*) [field K] [field L] [algebra K L] : filter_basis (L ‚âÉ‚Çê[K] L) :=
{ sets := subgroup.carrier '' (fixed_by_finite K L),
  nonempty := ‚ü®‚ä§, ‚ä§, top_fixed_by_finite, rfl‚ü©,
  inter_sets :=
  begin
    rintros X Y ‚ü®H1, ‚ü®E1, h_E1, rfl‚ü©, rfl‚ü© ‚ü®H2, ‚ü®E2, h_E2, rfl‚ü©, rfl‚ü©,
    use (intermediate_field.fixing_subgroup (E1 ‚äî E2)).carrier,
    refine ‚ü®‚ü®_, ‚ü®_, finite_dimensional_sup E1 E2 h_E1 h_E2, rfl‚ü©, rfl‚ü©, _‚ü©,
    rw set.subset_inter_iff,
    exact ‚ü®intermediate_field.fixing_subgroup.antimono le_sup_left,
      intermediate_field.fixing_subgroup.antimono le_sup_right‚ü©,
  end }

/-- A subset of `L ‚âÉ‚Çê[K] L` is a member of `gal_basis K L` if and only if it is the underlying set
of `Gal(L/E)` for some finite subextension `E/K`-/
lemma mem_gal_basis_iff (K L : Type*) [field K] [field L] [algebra K L]
  (U : set (L ‚âÉ‚Çê[K] L)) : U ‚àà gal_basis K L ‚Üî U ‚àà subgroup.carrier '' (fixed_by_finite K L) :=
iff.rfl

/-- For a field extension `L/K`, `gal_group_basis K L` is the group filter basis on `L ‚âÉ‚Çê[K] L`
whose sets are `Gal(L/E)` for finite subextensions `E/K` -/
def gal_group_basis (K L : Type*) [field K] [field L] [algebra K L] :
  group_filter_basis (L ‚âÉ‚Çê[K] L) :=
{ to_filter_basis := gal_basis K L,
  one' := Œª U ‚ü®H, hH, h2‚ü©, h2 ‚ñ∏ H.one_mem,
  mul' := Œª U hU, ‚ü®U, hU, begin
    rcases hU with ‚ü®H, hH, rfl‚ü©,
    rintros x ‚ü®a, b, haH, hbH, rfl‚ü©,
    exact H.mul_mem haH hbH,
  end‚ü©,
  inv' := Œª U hU, ‚ü®U, hU, begin
    rcases hU with ‚ü®H, hH, rfl‚ü©,
    exact H.inv_mem',
  end‚ü©,
  conj' :=
  begin
    rintros œÉ U ‚ü®H, ‚ü®E, hE, rfl‚ü©, rfl‚ü©,
    let F : intermediate_field K L := E.map (œÉ.symm.to_alg_hom),
    refine ‚ü®F.fixing_subgroup.carrier, ‚ü®‚ü®F.fixing_subgroup, ‚ü®F,
      _, rfl‚ü©, rfl‚ü©, Œª g hg, _‚ü©‚ü©,
    { apply im_finite_dimensional œÉ.symm,
      exact hE },
    change œÉ * g * œÉ‚Åª¬π ‚àà E.fixing_subgroup,
    rw mem_fixing_subgroup_iff,
    intros x hx,
    change œÉ(g(œÉ‚Åª¬π x)) = x,
    have h_in_F : œÉ‚Åª¬π x ‚àà F := ‚ü®x, hx, by {dsimp, rw ‚Üê alg_equiv.inv_fun_eq_symm, refl }‚ü©,
    have h_g_fix : g (œÉ‚Åª¬π x) = (œÉ‚Åª¬π x),
    { rw [subgroup.mem_carrier, mem_fixing_subgroup_iff F g] at hg,
      exact hg (œÉ‚Åª¬π x) h_in_F },
    rw h_g_fix,
    change œÉ(œÉ‚Åª¬π x) = x,
    exact alg_equiv.apply_symm_apply œÉ x,
  end }

/-- For a field extension `L/K`, `krull_topology K L` is the topological space structure on
`L ‚âÉ‚Çê[K] L` induced by the group filter basis `gal_group_basis K L` -/
instance krull_topology (K L : Type*) [field K] [field L] [algebra K L] :
  topological_space (L ‚âÉ‚Çê[K] L) :=
group_filter_basis.topology (gal_group_basis K L)

/-- For a field extension `L/K`, the Krull topology on `L ‚âÉ‚Çê[K] L` makes it a topological group. -/
instance (K L : Type*) [field K] [field L] [algebra K L] :
  topological_group (L ‚âÉ‚Çê[K] L) :=
group_filter_basis.is_topological_group (gal_group_basis K L)

section krull_t2

open_locale topological_space filter

/-- If a subgroup of a topological group has `1` in its interior, then it is open. -/
lemma subgroup.is_open_of_one_mem_interior {G : Type*} [group G] [topological_space G]
  [topological_group G] {H : subgroup G} (h_1_int : (1 : G) ‚àà interior (H : set G)) :
  is_open (H : set G) :=
begin
  have h : ùìù 1 ‚â§ ùìü (H : set G) :=
    nhds_le_of_le h_1_int (is_open_interior) (filter.principal_mono.2 interior_subset),
  rw is_open_iff_nhds,
  intros g hg,
  rw (show ùìù g = filter.map ‚áë(homeomorph.mul_left g) (ùìù 1), by simp),
  convert filter.map_mono h,
  simp only [homeomorph.coe_mul_left, filter.map_principal, set.image_mul_left,
  filter.principal_eq_iff_eq],
  ext,
  simp [H.mul_mem_cancel_left (H.inv_mem hg)],
end

/-- Let `L/E/K` be a tower of fields with `E/K` finite. Then `Gal(L/E)` is an open subgroup of
  `L ‚âÉ‚Çê[K] L`. -/
lemma intermediate_field.fixing_subgroup_is_open {K L : Type*} [field K] [field L] [algebra K L]
  (E : intermediate_field K L) [finite_dimensional K E] :
  is_open (E.fixing_subgroup : set (L ‚âÉ‚Çê[K] L)) :=
begin
  have h_basis : E.fixing_subgroup.carrier ‚àà (gal_group_basis K L) :=
   ‚ü®E.fixing_subgroup, ‚ü®E, _inst_4, rfl‚ü©, rfl‚ü©,
  have h_nhd := group_filter_basis.mem_nhds_one (gal_group_basis K L) h_basis,
  rw mem_nhds_iff at h_nhd,
  rcases h_nhd with ‚ü®U, hU_le, hU_open, h1U‚ü©,
  exact subgroup.is_open_of_one_mem_interior ‚ü®U, ‚ü®hU_open, hU_le‚ü©, h1U‚ü©,
end

/-- If `L/K` is an algebraic extension, then the Krull topology on `L ‚âÉ‚Çê[K] L` is Hausdorff. -/
lemma krull_topology_t2 (K L : Type*) [field K] [field L] [algebra K L]
  (h_int : algebra.is_integral K L) : t2_space (L ‚âÉ‚Çê[K] L) :=
{ t2 := Œª f g hfg,
  begin
    let œÜ := f‚Åª¬π * g,
    cases (fun_like.exists_ne hfg) with x hx,
    have hœÜx : œÜ x ‚â† x,
    { apply ne_of_apply_ne f,
      change f (f.symm (g x)) ‚â† f x,
      rw [alg_equiv.apply_symm_apply f (g x), ne_comm],
      exact hx },
    let E : intermediate_field K L := intermediate_field.adjoin K {x},
    let h_findim : finite_dimensional K E :=
      intermediate_field.adjoin.finite_dimensional (h_int x),
    let H := E.fixing_subgroup,
    have h_basis : (H : set (L ‚âÉ‚Çê[K] L)) ‚àà gal_group_basis K L := ‚ü®H, ‚ü®E, ‚ü®h_findim, rfl‚ü©‚ü©, rfl‚ü©,
    have h_nhd := group_filter_basis.mem_nhds_one (gal_group_basis K L) h_basis,
    rw mem_nhds_iff at h_nhd,
    rcases h_nhd with ‚ü®W, hWH, hW_open, hW_1‚ü©,
    refine ‚ü®left_coset f W, left_coset g W,
      ‚ü®hW_open.left_coset f, hW_open.left_coset g, ‚ü®1, hW_1, mul_one _‚ü©, ‚ü®1, hW_1, mul_one _‚ü©, _‚ü©‚ü©,
    by_contra h_nonempty,
    change left_coset f W ‚à© left_coset g W ‚â† ‚àÖ at h_nonempty,
    rw set.ne_empty_iff_nonempty at h_nonempty,
    rcases h_nonempty with ‚ü®œÉ, ‚ü®‚ü®w1, hw1, hfw1‚ü©, ‚ü®w2, hw2, hgw2‚ü©‚ü©‚ü©,
    rw ‚Üê hgw2 at hfw1,
    rename hfw1 h,
    rw [eq_inv_mul_iff_mul_eq.symm, ‚Üê mul_assoc, mul_inv_eq_iff_eq_mul.symm] at h,
    have h_in_H : w1 * w2‚Åª¬π ‚àà H := H.mul_mem (hWH hw1) (H.inv_mem (hWH hw2)),
    rw h at h_in_H,
    change œÜ ‚àà E.fixing_subgroup at h_in_H,
    rw mem_fixing_subgroup_iff at h_in_H,
    specialize h_in_H x,
    have hxE : x ‚àà E,
    { apply intermediate_field.subset_adjoin,
      apply set.mem_singleton },
    exact hœÜx (h_in_H hxE),
  end }

end krull_t2
