/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: NicolÃ² Cavalleri.
-/

import geometry.manifold.times_cont_mdiff

noncomputable theory

/-!
# Lie groups

We define Lie groups.

## Main definitions and statements

* `lie_add_group I G` : a Lie additive group where `G` is a manifold on the model with corners `I`.
* `lie_group I G`     : a Lie multiplicative group where `G` is a manifold on the model with
                        corners `I`.

## Implementation notes
A priori, a Lie group here is a manifold with corner.
-/

section lie_group

set_option default_priority 100

/-- A Lie (additive) group is a group and a smooth manifold at the same time in which
the addition and negation operations are smooth. -/
class lie_add_group {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {H : Type*} [topological_space H]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] (I : model_with_corners ğ•œ E H)
  (G : Type*) [add_group G] [topological_space G] [topological_add_group G] [charted_space H G]
  extends smooth_manifold_with_corners I G : Prop :=
(smooth_add : smooth (I.prod I) I (Î» p : GÃ—G, p.1 + p.2))
(smooth_neg : smooth I I (Î» a:G, -a))

/-- A Lie group is a group and a smooth manifold at the same time in which
the multiplication and inverse operations are smooth. -/
@[to_additive lie_add_group]
class lie_group {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {H : Type*} [topological_space H]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] (I : model_with_corners ğ•œ E H)
  (G : Type*) [group G] [topological_space G] [topological_group G] [charted_space H G]
  extends smooth_manifold_with_corners I G : Prop :=
(smooth_mul : smooth (I.prod I) I (Î» p : GÃ—G, p.1 * p.2))
(smooth_inv : smooth I I (Î» a:G, aâ»Â¹))

section lie_group

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{H : Type*} [topological_space H]
{E : Type*} [normed_group E] [normed_space ğ•œ E] {I : model_with_corners ğ•œ E H}
{F : Type*} [normed_group F] [normed_space ğ•œ F] {J : model_with_corners ğ•œ F F}
{G : Type*} [topological_space G] [charted_space H G] [group G]
[topological_group G] [lie_group I G]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M : Type*} [topological_space M] [charted_space H' M] [smooth_manifold_with_corners I' M]
{E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M' : Type*} [topological_space M'] [charted_space H'' M'] [smooth_manifold_with_corners I'' M']

@[to_additive]
lemma smooth_mul : smooth (I.prod I) I (Î» p : GÃ—G, p.1 * p.2) :=
lie_group.smooth_mul

@[to_additive]
lemma smooth.mul {f : M â†’ G} {g : M â†’ G} (hf : smooth I' I f) (hg : smooth I' I g) :
  smooth I' I (f * g) :=
smooth_mul.comp (hf.prod_mk hg)

namespace lie_group

/-- `L g` denotes left multiplication by `g` -/
@[nolint unused_arguments, to_additive L_add "`L_add g` denotes left addition by `g`"]
def L : G â†’ G â†’ G := Î» g : G, Î» x : G, g * x

attribute [nolint unused_arguments] lie_add_group.L_add

/-- `R g` denotes right multiplication by `g` -/
@[nolint unused_arguments, to_additive R_add "`R_add g` denotes right addition by `g`"]
def R : G â†’ G â†’ G := Î» g : G, Î» x : G, x * g

attribute [nolint unused_arguments] lie_add_group.R_add

end lie_group

@[to_additive]
lemma smooth_mul_left {a : G} : smooth I I (Î» b : G, a * b) :=
smooth_mul.comp (smooth_const.prod_mk smooth_id)

@[to_additive]
lemma smooth_mul_right {a : G} : smooth I I (Î» b : G, b * a) :=
smooth_mul.comp (smooth_id.prod_mk smooth_const)

@[to_additive]
lemma smooth_on.mul {f : M â†’ G} {g : M â†’ G} {s : set M}
  (hf : smooth_on I' I f s) (hg : smooth_on I' I g s) :
  smooth_on I' I (f * g) s :=
(smooth_mul.comp_smooth_on (hf.prod_mk hg) : _)

lemma smooth_pow : âˆ€ n : â„•, smooth I I (Î» a : G, a ^ n)
| 0 := by { simp only [pow_zero], exact smooth_const }
| (k+1) := show smooth I I (Î» (a : G), a * a ^ k), from smooth_id.mul (smooth_pow _)

@[to_additive]
lemma smooth_inv : smooth I I (Î» x : G, xâ»Â¹) :=
lie_group.smooth_inv

@[to_additive]
lemma smooth.inv {f : M â†’ G}
  (hf : smooth I' I f) : smooth I' I (Î»x, (f x)â»Â¹) :=
smooth_inv.comp hf

@[to_additive]
lemma smooth_on.inv {f : M â†’ G} {s : set M}
  (hf : smooth_on I' I f s) : smooth_on I' I (Î»x, (f x)â»Â¹) s :=
smooth_inv.comp_smooth_on hf

end lie_group

section prod_lie_group

/- Instance of product group -/
@[to_additive lie_add_group]
instance {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] {H : Type*} [topological_space H]
{E : Type*} [normed_group E] [normed_space ğ•œ E]  {I : model_with_corners ğ•œ E H}
{G : Type*} [topological_space G] [charted_space H G] [group G] [topological_group G]
[h : lie_group I G] {E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{G' : Type*} [topological_space G'] [charted_space H' G']
[group G'] [topological_group G'] [h' : lie_group I' G'] : lie_group (I.prod I') (GÃ—G') :=
{ smooth_mul := ((smooth_fst.comp smooth_fst).smooth.mul (smooth_fst.comp smooth_snd)).prod_mk
    ((smooth_snd.comp smooth_fst).smooth.mul (smooth_snd.comp smooth_snd)),
  smooth_inv := smooth_fst.inv.prod_mk smooth_snd.inv, }

end prod_lie_group

section lie_add_group_morphism

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']

/-- Morphism of additive Lie groups. -/
structure lie_add_group_morphism (I : model_with_corners ğ•œ E E) (I' : model_with_corners ğ•œ E' E')
(G : Type*) [topological_space G] [charted_space E G] [smooth_manifold_with_corners I G]
[add_group G] [topological_add_group G] [lie_add_group I G]
(G' : Type*) [topological_space G'] [charted_space E' G'] [smooth_manifold_with_corners I' G']
[add_group G'] [topological_add_group G'] [lie_add_group I' G'] extends add_monoid_hom G G' :=
  (smooth_to_fun : smooth I I' to_fun)

/-- Morphism of Lie groups. -/
@[to_additive lie_add_group_morphism]
structure lie_group_morphism (I : model_with_corners ğ•œ E E) (I' : model_with_corners ğ•œ E' E')
(G : Type*) [topological_space G] [charted_space E G] [smooth_manifold_with_corners I G] [group G]
[topological_group G] [lie_group I G]
(G' : Type*) [topological_space G'] [charted_space E' G'] [smooth_manifold_with_corners I' G']
[group G'] [topological_group G'] [lie_group I' G'] extends monoid_hom G G' :=
  (smooth_to_fun : smooth I I' to_fun)

variables {I : model_with_corners ğ•œ E E} {I' : model_with_corners ğ•œ E' E'}
{G : Type*} [topological_space G] [charted_space E G] [smooth_manifold_with_corners I G]
[group G] [topological_group G] [lie_group I G]
{G' : Type*} [topological_space G'] [charted_space E' G'] [smooth_manifold_with_corners I' G']
[group G'] [topological_group G'] [lie_group I' G']

@[to_additive]
instance : has_one (lie_group_morphism I I' G G') := âŸ¨âŸ¨1, smooth_constâŸ©âŸ©

@[to_additive]
instance : inhabited (lie_group_morphism I I' G G') := âŸ¨1âŸ©

@[to_additive]
instance : has_coe_to_fun (lie_group_morphism I I' G G') := âŸ¨_, Î» a, a.to_funâŸ©

end lie_add_group_morphism

end lie_group

section lie_group_core

/-- Sometimes one might want to define a Lie additive group `G` without having proved previously
that `G` is a topological additive group. In such case it is possible to use `lie_add_group_core`
that does not require such instance, and then get a Lie group by invoking `to_Lie_add_group`. -/
structure lie_add_group_core {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E]
  [normed_space ğ•œ E] (I : model_with_corners ğ•œ E E)
  (G : Type*) [add_group G] [topological_space G]
  [charted_space E G] [smooth_manifold_with_corners I G] : Prop :=
  (smooth_add : smooth (I.prod I) I (Î» p : GÃ—G, p.1 + p.2))
  (smooth_neg : smooth I I (Î» a:G, -a))

/-- Sometimes one might want to define a Lie group `G` without having proved previously that `G` is
a topological group. In such case it is possible to use `lie_group_core` that does not require such
instance, and then get a Lie group by invoking `to_lie_group` defined below. -/
@[to_additive lie_add_group_core]
structure lie_group_core {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E]
  [normed_space ğ•œ E] (I : model_with_corners ğ•œ E E)
  (G : Type*) [group G] [topological_space G]
  [charted_space E G] [smooth_manifold_with_corners I G] : Prop :=
  (smooth_mul : smooth (I.prod I) I (Î» p : GÃ—G, p.1 * p.2))
  (smooth_inv : smooth I I (Î» a:G, aâ»Â¹))

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E] {I : model_with_corners ğ•œ E E}
{F : Type*} [normed_group F] [normed_space ğ•œ F] {J : model_with_corners ğ•œ F F}
{G : Type*} [topological_space G] [charted_space E G] [smooth_manifold_with_corners I G] [group G]

namespace lie_group_core

variables (c : lie_group_core I G)

@[to_additive]
protected lemma to_topological_group : topological_group G :=
{ continuous_mul := c.smooth_mul.continuous,
  continuous_inv := c.smooth_inv.continuous, }

@[to_additive]
protected lemma to_lie_group : @lie_group ğ•œ _ _ _ E _ _ I G _ _ c.to_topological_group _ :=
{ smooth_mul := c.smooth_mul,
  smooth_inv := c.smooth_inv, }

end lie_group_core

end lie_group_core

/-! ### Real numbers are a Lie group -/

section real_numbers_lie_group

instance normed_group_lie_group {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E] :
lie_add_group (model_with_corners_self ğ•œ E) E :=
{ smooth_add :=
  begin
    rw smooth_iff,
    refine âŸ¨continuous_add, Î» x y, _âŸ©,
    simp only [prod.mk.eta] with mfld_simps,
    rw times_cont_diff_on_univ,
    exact times_cont_diff_add,
  end,
  smooth_neg :=
  begin
    rw smooth_iff,
    refine âŸ¨continuous_neg, Î» x y, _âŸ©,
    simp only [prod.mk.eta] with mfld_simps,
    rw times_cont_diff_on_univ,
    exact times_cont_diff_neg,
  end }

instance reals_lie_group : lie_add_group (model_with_corners_self â„ â„) â„ := by apply_instance

end real_numbers_lie_group
