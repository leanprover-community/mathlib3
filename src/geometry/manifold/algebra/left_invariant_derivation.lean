/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/

import geometry.manifold.derivation_bundle

/-!

# Left invariant derivations

In this file we define the concept of left invariant derivation for a Lie group. The concept is
analogous to the more classical concept of left invariant vector fields, and it holds that the
derivation associated to a vector field is left invariant iff the field is.

Moreover we prove that `left_invariant_derivation I G` has the structure of a Lie algebra, hence
implementing one of the possible definitions of the Lie algebra attached to a Lie group.

-/

noncomputable theory

open_locale lie_group manifold derivation

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
(G : Type*) [topological_space G] [charted_space H G] [monoid G] [has_smooth_mul I G] (g h : G)

-- Generate trivial has_sizeof instance. It prevents weird type class inference timeout problems
local attribute [nolint instance_priority, instance, priority 10000]
private def disable_has_sizeof {Î±} : has_sizeof Î± := âŸ¨Î» _, 0âŸ©

/--
Left-invariant global derivations.

A global derivation is left-invariant if it is equal to its pullback along left multiplication by
an arbitrary element of `G`.
-/
structure left_invariant_derivation extends derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯ :=
(left_invariant'' : âˆ€ g, ğ’…â‚•(smooth_left_mul_one I g) (derivation.eval_at 1 to_derivation) =
  derivation.eval_at g to_derivation)

variables {I G}

namespace left_invariant_derivation

instance : has_coe (left_invariant_derivation I G) (derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) :=
âŸ¨Î» X, X.to_derivationâŸ©

instance : has_coe_to_fun (left_invariant_derivation I G) (Î» _, C^âˆâŸ®I, G; ğ•œâŸ¯ â†’ C^âˆâŸ®I, G; ğ•œâŸ¯) :=
âŸ¨Î» X, X.to_derivation.to_funâŸ©

variables
{M : Type*} [topological_space M] [charted_space H M] {x : M} {r : ğ•œ}
{X Y : left_invariant_derivation I G} {f f' : C^âˆâŸ®I, G; ğ•œâŸ¯}

lemma to_fun_eq_coe : X.to_fun = â‡‘X := rfl
lemma coe_to_linear_map : â‡‘(X : C^âˆâŸ®I, G; ğ•œâŸ¯ â†’â‚—[ğ•œ] C^âˆâŸ®I, G; ğ•œâŸ¯) = X := rfl
@[simp] lemma to_derivation_eq_coe : X.to_derivation = X := rfl

lemma coe_injective :
  @function.injective (left_invariant_derivation I G) (_ â†’ C^âŠ¤âŸ®I, G; ğ•œâŸ¯) coe_fn :=
Î» X Y h, by { cases X, cases Y, congr', exact derivation.coe_injective h }

@[ext] theorem ext (h : âˆ€ f, X f = Y f) : X = Y :=
coe_injective $ funext h

variables (X Y f)

lemma coe_derivation :
  â‡‘(X : derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) = (X : C^âˆâŸ®I, G; ğ•œâŸ¯ â†’ C^âˆâŸ®I, G; ğ•œâŸ¯) := rfl

lemma coe_derivation_injective : function.injective
  (coe : left_invariant_derivation I G â†’ derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) :=
Î» X Y h, by { cases X, cases Y, congr, exact h }

/-- Premature version of the lemma. Prefer using `left_invariant` instead. -/
lemma left_invariant' :
  ğ’…â‚• (smooth_left_mul_one I g) (derivation.eval_at (1 : G) â†‘X) = derivation.eval_at g â†‘X :=
left_invariant'' X g

@[simp] lemma map_add : X (f + f') = X f + X f' := derivation.map_add X f f'
@[simp] lemma map_zero : X 0 = 0 := derivation.map_zero X
@[simp] lemma map_neg : X (-f) = -X f := derivation.map_neg X f
@[simp] lemma map_sub : X (f - f') = X f - X f' := derivation.map_sub X f f'
@[simp] lemma map_smul : X (r â€¢ f) = r â€¢ X f := derivation.map_smul X r f
@[simp] lemma leibniz : X (f * f') = f â€¢ X f' + f' â€¢ X f := X.leibniz' _ _

instance : has_zero (left_invariant_derivation I G) :=
âŸ¨âŸ¨0, Î» g, by simp only [linear_map.map_zero, derivation.coe_zero]âŸ©âŸ©

instance : inhabited (left_invariant_derivation I G) := âŸ¨0âŸ©

instance : has_add (left_invariant_derivation I G) :=
{ add := Î» X Y, âŸ¨X + Y, Î» g, by simp only [linear_map.map_add, derivation.coe_add,
    left_invariant', pi.add_apply]âŸ© }

instance : has_neg (left_invariant_derivation I G) :=
{ neg := Î» X, âŸ¨-X, Î» g, by simp [left_invariant']âŸ© }

instance : has_sub (left_invariant_derivation I G) :=
{ sub := Î» X Y, âŸ¨X - Y, Î» g, by simp [left_invariant']âŸ© }

@[simp] lemma coe_add : â‡‘(X + Y) = X + Y := rfl
@[simp] lemma coe_zero : â‡‘(0 : left_invariant_derivation I G) = 0 := rfl
@[simp] lemma coe_neg : â‡‘(-X) = -X := rfl
@[simp] lemma coe_sub : â‡‘(X - Y) = X - Y := rfl
@[simp, norm_cast] lemma lift_add :
  (â†‘(X + Y) : derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) = X + Y := rfl
@[simp, norm_cast] lemma lift_zero :
  (â†‘(0 : left_invariant_derivation I G) : derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) = 0 := rfl

instance : add_comm_group (left_invariant_derivation I G) :=
coe_injective.add_comm_group _ coe_zero coe_add coe_neg coe_sub

instance : has_scalar ğ•œ (left_invariant_derivation I G) :=
{ smul := Î» r X, âŸ¨r â€¢ X, Î» g, by simp only [derivation.smul_apply, smul_eq_mul,
            mul_eq_mul_left_iff, linear_map.map_smul, left_invariant']âŸ© }

variables (r X)

@[simp] lemma coe_smul : â‡‘(r â€¢ X) = r â€¢ X := rfl
@[simp] lemma lift_smul (k : ğ•œ) : (â†‘(k â€¢ X) : derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) = k â€¢ X := rfl

variables (I G)

/-- The coercion to function is a monoid homomorphism. -/
@[simps] def coe_fn_add_monoid_hom :
  (left_invariant_derivation I G) â†’+ (C^âˆâŸ®I, G; ğ•œâŸ¯ â†’ C^âˆâŸ®I, G; ğ•œâŸ¯) :=
âŸ¨Î» X, X.to_derivation.to_fun, coe_zero, coe_addâŸ©

variables {I G}

instance : module ğ•œ (left_invariant_derivation I G) :=
coe_injective.module _ (coe_fn_add_monoid_hom I G) coe_smul

/-- Evaluation at a point for left invariant derivation. Same thing as for generic global
derivations (`derivation.eval_at`). -/
def eval_at : (left_invariant_derivation I G) â†’â‚—[ğ•œ] (point_derivation I g) :=
{ to_fun := Î» X, derivation.eval_at g â†‘X,
  map_add' := Î» X Y, rfl,
  map_smul' := Î» k X, rfl }

lemma eval_at_apply : eval_at g X f = (X f) g := rfl

@[simp] lemma eval_at_coe : derivation.eval_at g â†‘X = eval_at g X := rfl

lemma left_invariant : ğ’…â‚•(smooth_left_mul_one I g) (eval_at (1 : G) X) = eval_at g X :=
(X.left_invariant'' g)

lemma eval_at_mul : eval_at (g * h) X = ğ’…â‚•(L_apply I g h) (eval_at h X) :=
by { ext f, rw [â†left_invariant, apply_hfdifferential, apply_hfdifferential, L_mul,
  fdifferential_comp, apply_fdifferential, linear_map.comp_apply, apply_fdifferential,
  â†apply_hfdifferential, left_invariant] }

lemma comp_L : (X f).comp (ğ‘³ I g) = X (f.comp (ğ‘³ I g)) :=
by ext h; rw [times_cont_mdiff_map.comp_apply, L_apply, â†eval_at_apply, eval_at_mul,
  apply_hfdifferential, apply_fdifferential, eval_at_apply]

instance : has_bracket (left_invariant_derivation I G) (left_invariant_derivation I G) :=
{ bracket := Î» X Y, âŸ¨â…(X : derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯), Yâ†, Î» g, begin
    ext f,
    have hX := derivation.congr_fun (left_invariant' g X) (Y f),
    have hY := derivation.congr_fun (left_invariant' g Y) (X f),
    rw [apply_hfdifferential, apply_fdifferential, derivation.eval_at_apply] at hX hY âŠ¢,
    rw comp_L at hX hY,
    rw [derivation.commutator_apply, smooth_map.coe_sub, pi.sub_apply, coe_derivation],
    rw coe_derivation at hX hY âŠ¢,
    rw [hX, hY],
    refl
  endâŸ© }

@[simp] lemma commutator_coe_derivation :
  â‡‘â…X, Yâ† = (â…(X : derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯), Yâ† :
  derivation ğ•œ C^âˆâŸ®I, G; ğ•œâŸ¯ C^âˆâŸ®I, G; ğ•œâŸ¯) := rfl

lemma commutator_apply : â…X, Yâ† f = X (Y f) - Y (X f) := rfl

instance : lie_ring (left_invariant_derivation I G) :=
{ add_lie := Î» X Y Z, by { ext1, simp only [commutator_apply, coe_add, pi.add_apply,
              linear_map.map_add, left_invariant_derivation.map_add], ring },
  lie_add := Î» X Y Z, by { ext1, simp only [commutator_apply, coe_add, pi.add_apply,
              linear_map.map_add, left_invariant_derivation.map_add], ring },
  lie_self := Î» X, by { ext1, simp only [commutator_apply, sub_self], refl },
  leibniz_lie := Î» X Y Z, by { ext1, simp only [commutator_apply, coe_add, coe_sub, map_sub,
              pi.add_apply], ring, } }

instance : lie_algebra ğ•œ (left_invariant_derivation I G) :=
{ lie_smul := Î» r Y Z, by { ext1, simp only [commutator_apply, map_smul, smul_sub, coe_smul,
              pi.smul_apply] } }

end left_invariant_derivation
