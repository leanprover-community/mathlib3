/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/

import geometry.manifold.algebra.structures
import geometry.manifold.smooth_map

/-!
# Algebraic structures over smooth functions

In this file we define instances of algebraic sturctures over smooth functions.
-/

noncomputable theory

open_locale manifold

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{N : Type*} [topological_space N] [charted_space H N] [smooth_manifold_with_corners I N]
{E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{N' : Type*} [topological_space N'] [charted_space H'' N'] [smooth_manifold_with_corners I'' N']

namespace smooth_map

@[to_additive]
instance has_mul {G : Type*} [has_mul G] [topological_space G] [has_continuous_mul G]
  [charted_space H' G] [smooth_manifold_with_corners I' G] [has_smooth_mul I' G] :
  has_mul Câˆ(I, N; I', G) :=
âŸ¨Î» f g, âŸ¨f * g, smooth_mul.comp (f.smooth.prod_mk g.smooth)âŸ©âŸ©

@[simp, to_additive] lemma mul_apply {G : Type*} [has_mul G] [topological_space G]
  [has_continuous_mul G] [charted_space H' G] [smooth_manifold_with_corners I' G]
  [has_smooth_mul I' G] (f g : Câˆ(I, N; I', G)) (x : N) :
(f * g) x = f x * g x := rfl

@[simp, to_additive] lemma mul_comp {G : Type*} [has_mul G] [topological_space G]
  [has_continuous_mul G] [charted_space H' G] [smooth_manifold_with_corners I' G]
  [has_smooth_mul I' G] (f g : Câˆ(I'', N'; I', G)) (h : Câˆ(I, N; I'', N')) (x : N) :
(f * g).comp h = (f.comp h) * (g.comp h) :=
by ext; simp only [comp_apply, mul_apply]

@[to_additive]
instance {G : Type*} [monoid G] [topological_space G]
  [charted_space H' G] [smooth_manifold_with_corners I' G] :
  has_one Câˆ(I, N; I', G) :=
âŸ¨const (1 : G)âŸ©

end smooth_map

section group_structure

/-!
### Group stucture

In this section we show that smooth functions valued in a Lie group inherit a structure of group.
-/

@[to_additive]
instance smooth_map_semigroup {G : Type*} [semigroup G] [topological_space G] [has_continuous_mul G]
  [charted_space H' G] [smooth_manifold_with_corners I' G] [has_smooth_mul I' G]
 : semigroup Câˆ(I, N; I', G) :=
{ mul_assoc := Î» a b c, by ext; exact mul_assoc _ _ _,
  ..smooth_map.has_mul}

@[to_additive]
instance smooth_map_monoid {G : Type*} [monoid G] [topological_space G] [has_continuous_mul G]
  [charted_space H' G] [smooth_manifold_with_corners I' G] [has_smooth_mul I' G] :
  monoid Câˆ(I, N; I', G) :=
{ one_mul := Î» a, by ext; exact one_mul _,
  mul_one := Î» a, by ext; exact mul_one _,
  ..smooth_map_semigroup,
  ..smooth_map.has_one }

@[to_additive]
instance smooth_map_comm_monoid {G : Type*} [comm_monoid G] [topological_space G]
  [has_continuous_mul G] [charted_space H' G]
  [smooth_manifold_with_corners I' G] [has_smooth_mul I' G] :
  comm_monoid Câˆ(I, N; I', G) :=
{ one_mul := Î» a, by ext; exact one_mul _,
  mul_one := Î» a, by ext; exact mul_one _,
  mul_comm := Î» a b, by ext; exact mul_comm _ _,
  ..smooth_map_semigroup,
  ..smooth_map.has_one }

@[to_additive]
instance smooth_map_group {G : Type*} [group G] [topological_space G] [topological_group G]
  [charted_space H' G] [smooth_manifold_with_corners I' G] [lie_group I' G] :
  group Câˆ(I, N; I', G) :=
{ inv := Î» f, âŸ¨Î» x, (f x)â»Â¹, smooth_inv.comp f.smoothâŸ©,
  mul_left_inv := Î» a, by ext; exact mul_left_inv _,
  ..smooth_map_monoid }

@[simp] lemma smooth_map.sub_apply {G : Type*} [add_group G] [topological_space G] [topological_add_group G]
  [charted_space H' G] [smooth_manifold_with_corners I' G] [lie_add_group I' G] (f g : Câˆ(I, N; I', G))
  (x : N) : (f - g) x = f x - g x := rfl

@[to_additive]
instance smooth_map_comm_group {G : Type*} [comm_group G] [topological_space G]
  [topological_group G] [charted_space H' G] [smooth_manifold_with_corners I' G] [lie_group I' G] :
  comm_group Câˆ(I, N; I', G) :=
{ ..smooth_map_group,
  ..smooth_map_comm_monoid }

end group_structure

section ring_structure

/-!
### Ring stucture

In this section we show that smooth functions valued in a smooth ring `R` inherit a structure of
ring.
-/

instance smooth_map_semiring {R : Type*} [semiring R] [topological_space R] [topological_semiring R]
  [charted_space H' R] [smooth_manifold_with_corners I' R] [smooth_semiring I' R] :
  semiring Câˆ(I, N; I', R) :=
{ left_distrib := Î» a b c, by ext; exact left_distrib _ _ _,
  right_distrib := Î» a b c, by ext; exact right_distrib _ _ _,
  zero_mul := Î» a, by ext; exact zero_mul _,
  mul_zero := Î» a, by ext; exact mul_zero _,
  ..smooth_map_add_comm_monoid,
  ..smooth_map_monoid }

instance smooth_map_ring {R : Type*} [ring R] [topological_space R] [topological_ring R]
  [charted_space H' R] [smooth_manifold_with_corners I' R] [smooth_ring I' R] :
  ring Câˆ(I, N; I', R) :=
{ ..smooth_map_semiring,
  ..smooth_map_add_comm_group, }

instance smooth_map_comm_ring {R : Type*} [comm_ring R] [topological_space R] [topological_ring R]
  [charted_space H' R] [smooth_manifold_with_corners I' R] [smooth_ring I' R] :
  comm_ring Câˆ(I, N; I', R) :=
{ ..smooth_map_semiring,
  ..smooth_map_add_comm_group,
  ..smooth_map_comm_monoid,}

end ring_structure

section semimodule_structure

/-!
### Semiodule stucture

In this section we show that smooth functions valued in a smooth vector space `M` over a normed
field `R` that is a normed space over `ğ•œ` inherit a structure of vector space.

From now on, whenever a scalar multiplication is involved, we cannot consider `R` to be a generic
semiring with a manifold structure, because of dangerous instances problems (the model with corners
on which `R` is a manifold gets forgotten). Hence we do everything in the case of `R` being a field
which is a normed space over `ğ•œ`, which is slightly more general that the case `ğ•œ = R` as it takes
into account the case `ğ•œ = â„`, `R = â„‚`.
-/

instance smooth_map_has_scalar
  {V : Type*} [normed_group V] [normed_space ğ•œ V] :
  has_scalar ğ•œ Câˆ(I, N; Isf(ğ•œ, V), V) :=
âŸ¨Î» r f, âŸ¨r â€¢ f, smooth_const.smul f.smoothâŸ©âŸ©

instance smooth_map_semimodule
  {V : Type*} [normed_group V] [normed_space ğ•œ V] :
  vector_space ğ•œ Câˆ(I, N; Isf(ğ•œ, V), V) :=
semimodule.of_core $
{ smul     := (â€¢),
  smul_add := Î» c f g, by ext x; exact smul_add c (f x) (g x),
  add_smul := Î» câ‚ câ‚‚ f, by ext x; exact add_smul câ‚ câ‚‚ (f x),
  mul_smul := Î» câ‚ câ‚‚ f, by ext x; exact mul_smul câ‚ câ‚‚ (f x),
  one_smul := Î» f, by ext x; exact one_smul ğ•œ (f x), }

end semimodule_structure

section algebra_structure

/-!
### Algebra structure

In this section we show that smooth functions valued in a smooth algebra `A` over a normed field
`R` that is a normed space over `ğ•œ` inherit a structure of algebra. Note that the hypothesis that
`A` is a smooth algebra is obtained by requiring that `A` be both a `smooth_vector_space` and a
`smooth_ring`.

To see why `R` cannot be a generic semiring with a manifold structure see the note of "Semiodule
stucture".
-/

variables {A : Type*} [normed_ring A] [normed_algebra ğ•œ A] [topological_ring A]
[smooth_ring Isf(ğ•œ, A) A]

/-- Smooth constant functions as a `ring_hom`. -/
def smooth_map.C : ğ•œ â†’+* Câˆ(I, N; Isf(ğ•œ, A), A) :=
{ to_fun    := Î» c : ğ•œ, âŸ¨Î» x, ((algebra_map ğ•œ A) c), smooth_constâŸ©,
  map_one'  := by ext x; exact (algebra_map ğ•œ A).map_one,
  map_mul'  := Î» câ‚ câ‚‚, by ext x; exact (algebra_map ğ•œ A).map_mul _ _,
  map_zero' := by ext x; exact (algebra_map ğ•œ A).map_zero,
  map_add'  := Î» câ‚ câ‚‚, by ext x; exact (algebra_map ğ•œ A).map_add _ _ }

instance : algebra ğ•œ Câˆ(I, N; Isf(ğ•œ, A), A) :=
{ smul := Î» r f,
  âŸ¨r â€¢ f, smooth_const.smul f.smoothâŸ©,
  to_ring_hom := smooth_map.C,
  commutes' := Î» c f, by ext x; exact algebra.commutes' _ _,
  smul_def' := Î» c f, by ext x; exact algebra.smul_def' _ _,
  ..smooth_map_semiring }

end algebra_structure

section module_over_continuous_functions

/-!
### Structure as module over scalar functions

If `M` is a module over `R`, then we show that the space of smooth functions from `Î±` to `M`
is naturally a module over the ring of smooth functions from `Î±` to `M`. -/

instance smooth_map_has_scalar'
  {V : Type*} [normed_group V] [normed_space ğ•œ V] :
  has_scalar Câˆ(I, N; Isf(ğ•œ), ğ•œ) Câˆ(I, N; Isf(ğ•œ, V), V) :=
âŸ¨Î» f g, âŸ¨Î» x, (f x) â€¢ (g x), (smooth.smul f.2 g.2)âŸ©âŸ©

instance smooth_map_module'
  {V : Type*} [normed_group V] [normed_space ğ•œ V]
  : semimodule Câˆ(I, N; Isf(ğ•œ), ğ•œ) Câˆ(I, N; Isf(ğ•œ, V), V) :=
{ smul     := (â€¢),
  smul_add := Î» c f g, by ext x; exact smul_add (c x) (f x) (g x),
  add_smul := Î» câ‚ câ‚‚ f, by ext x; exact add_smul (câ‚ x) (câ‚‚ x) (f x),
  mul_smul := Î» câ‚ câ‚‚ f, by ext x; exact mul_smul (câ‚ x) (câ‚‚ x) (f x),
  one_smul := Î» f, by ext x; exact one_smul ğ•œ (f x),
  zero_smul := Î» f, by ext x; exact zero_smul _ _,
  smul_zero := Î» r, by ext x; exact smul_zero _, }

end module_over_continuous_functions

instance field_valued_smooth_maps_ring : ring Câˆ(I, N; ğ•œ) := by apply_instance

instance field_valued_smooth_maps_algebra : algebra ğ•œ Câˆ(I, N; ğ•œ) := by apply_instance
