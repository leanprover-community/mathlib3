/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.calculus.bump_function_findim
import geometry.manifold.cont_mdiff

/-!
# Smooth bump functions on a smooth manifold

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

In this file we define `smooth_bump_function I c` to be a bundled smooth "bump" function centered at
`c`. It is a structure that consists of two real numbers `0 < r < R` with small enough `R`. We
define a coercion to function for this type, and for `f : smooth_bump_function I c`, the function
`â‡‘f` written in the extended chart at `c` has the following properties:

* `f x = 1` in the closed ball of radius `f.r` centered at `c`;
* `f x = 0` outside of the ball of radius `f.R` centered at `c`;
* `0 â‰¤ f x â‰¤ 1` for all `x`.

The actual statements involve (pre)images under `ext_chart_at I f` and are given as lemmas in the
`smooth_bump_function` namespace.

## Tags

manifold, smooth bump function
-/

universes uE uF uH uM
variables
{E : Type uE} [normed_add_comm_group E] [normed_space â„ E] [finite_dimensional â„ E]
{H : Type uH} [topological_space H] (I : model_with_corners â„ E H)
{M : Type uM} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]

open function filter finite_dimensional set metric
open_locale topology manifold classical filter big_operators

noncomputable theory

/-!
### Smooth bump function

In this section we define a structure for a bundled smooth bump function and prove its properties.
-/

/-- Given a smooth manifold modelled on a finite dimensional space `E`,
`f : smooth_bump_function I M` is a smooth function on `M` such that in the extended chart `e` at
`f.c`:

* `f x = 1` in the closed ball of radius `f.r` centered at `f.c`;
* `f x = 0` outside of the ball of radius `f.R` centered at `f.c`;
* `0 â‰¤ f x â‰¤ 1` for all `x`.

The structure contains data required to construct a function with these properties. The function is
available as `â‡‘f` or `f x`. Formal statements of the properties listed above involve some
(pre)images under `ext_chart_at I f.c` and are given as lemmas in the `smooth_bump_function`
namespace. -/
structure smooth_bump_function (c : M) extends cont_diff_bump (ext_chart_at I c c) :=
(closed_ball_subset : (closed_ball (ext_chart_at I c c) R) âˆ© range I âŠ† (ext_chart_at I c).target)

variable {M}

namespace smooth_bump_function

variables {c : M} (f : smooth_bump_function I c) {x : M} {I}

/-- The function defined by `f : smooth_bump_function c`. Use automatic coercion to function
instead. -/
def to_fun : M â†’ â„ :=
indicator (chart_at H c).source (f.to_cont_diff_bump âˆ˜ ext_chart_at I c)

instance : has_coe_to_fun (smooth_bump_function I c) (Î» _, M â†’ â„) := âŸ¨to_funâŸ©

lemma coe_def :
  â‡‘f = indicator (chart_at H c).source (f.to_cont_diff_bump âˆ˜ ext_chart_at I c) :=
rfl

lemma R_pos : 0 < f.R := f.to_cont_diff_bump.R_pos

lemma ball_subset :
  ball (ext_chart_at I c c) f.R âˆ© range I âŠ† (ext_chart_at I c).target :=
subset.trans (inter_subset_inter_left _ ball_subset_closed_ball) f.closed_ball_subset

lemma eq_on_source :
  eq_on f (f.to_cont_diff_bump âˆ˜ ext_chart_at I c) (chart_at H c).source :=
eq_on_indicator

lemma eventually_eq_of_mem_source (hx : x âˆˆ (chart_at H c).source) :
  f =á¶ [ð“ x] f.to_cont_diff_bump âˆ˜ ext_chart_at I c :=
f.eq_on_source.eventually_eq_of_mem $ is_open.mem_nhds (chart_at H c).open_source hx

lemma one_of_dist_le (hs : x âˆˆ (chart_at H c).source)
  (hd : dist (ext_chart_at I c x) (ext_chart_at I c c) â‰¤ f.r) :
  f x = 1 :=
by simp only [f.eq_on_source hs, (âˆ˜), f.to_cont_diff_bump.one_of_mem_closed_ball hd]

lemma support_eq_inter_preimage :
  support f =
    (chart_at H c).source âˆ© (ext_chart_at I c â»Â¹' ball (ext_chart_at I c c) f.R) :=
by rw [coe_def, support_indicator, (âˆ˜), support_comp_eq_preimage, â† ext_chart_at_source I,
  â† (ext_chart_at I c).symm_image_target_inter_eq',
  â† (ext_chart_at I c).symm_image_target_inter_eq', f.to_cont_diff_bump.support_eq]

lemma is_open_support : is_open (support f) :=
by { rw support_eq_inter_preimage, exact is_open_ext_chart_at_preimage I c is_open_ball }

lemma support_eq_symm_image :
  support f = (ext_chart_at I c).symm '' (ball (ext_chart_at I c c) f.R âˆ© range I) :=
begin
  rw [f.support_eq_inter_preimage, â† ext_chart_at_source I,
    â† (ext_chart_at I c).symm_image_target_inter_eq', inter_comm],
  congr' 1 with y,
  exact and.congr_right_iff.2
    (Î» hy, âŸ¨Î» h, ext_chart_at_target_subset_range _ _ h, Î» h, f.ball_subset âŸ¨hy, hâŸ©âŸ©)
end

lemma support_subset_source : support f âŠ† (chart_at H c).source :=
by { rw [f.support_eq_inter_preimage, â† ext_chart_at_source I], exact inter_subset_left _ _ }

lemma image_eq_inter_preimage_of_subset_support {s : set M} (hs : s âŠ† support f) :
  ext_chart_at I c '' s =
    closed_ball (ext_chart_at I c c) f.R âˆ© range I âˆ© (ext_chart_at I c).symm â»Â¹' s :=
begin
  rw [support_eq_inter_preimage, subset_inter_iff, â† ext_chart_at_source I,
    â† image_subset_iff] at hs,
  cases hs with hse hsf,
  apply subset.antisymm,
  { refine subset_inter (subset_inter (subset.trans hsf ball_subset_closed_ball) _) _,
    { rintro _ âŸ¨x, -, rflâŸ©, exact mem_range_self _ },
    { rw [(ext_chart_at I c).image_eq_target_inter_inv_preimage hse],
      exact inter_subset_right _ _ } },
  { refine subset.trans (inter_subset_inter_left _ f.closed_ball_subset) _,
    rw [(ext_chart_at I c).image_eq_target_inter_inv_preimage hse] }
end

lemma mem_Icc : f x âˆˆ Icc (0 : â„) 1 :=
begin
  have : f x = 0 âˆ¨ f x = _, from indicator_eq_zero_or_self _ _ _,
  cases this; rw this,
  exacts [left_mem_Icc.2 zero_le_one,
    âŸ¨f.to_cont_diff_bump.nonneg, f.to_cont_diff_bump.le_oneâŸ©]
end

lemma nonneg : 0 â‰¤ f x := f.mem_Icc.1

lemma le_one : f x â‰¤ 1 := f.mem_Icc.2

lemma eventually_eq_one_of_dist_lt (hs : x âˆˆ (chart_at H c).source)
  (hd : dist (ext_chart_at I c x) (ext_chart_at I c c) < f.r) :
  f =á¶ [ð“ x] 1 :=
begin
  filter_upwards [is_open.mem_nhds (is_open_ext_chart_at_preimage I c is_open_ball) âŸ¨hs, hdâŸ©],
  rintro z âŸ¨hzs, hzd : _ < _âŸ©,
  exact f.one_of_dist_le hzs hzd.le
end

lemma eventually_eq_one : f =á¶ [ð“ c] 1 :=
f.eventually_eq_one_of_dist_lt (mem_chart_source _ _) $
by { rw [dist_self], exact f.r_pos }

@[simp] lemma eq_one : f c = 1 := f.eventually_eq_one.eq_of_nhds

lemma support_mem_nhds : support f âˆˆ ð“ c :=
f.eventually_eq_one.mono $ Î» x hx, by { rw hx, exact one_ne_zero }

lemma tsupport_mem_nhds : tsupport f âˆˆ ð“ c :=
mem_of_superset f.support_mem_nhds subset_closure

lemma c_mem_support : c âˆˆ support f := mem_of_mem_nhds f.support_mem_nhds

lemma nonempty_support : (support f).nonempty := âŸ¨c, f.c_mem_supportâŸ©

lemma is_compact_symm_image_closed_ball :
  is_compact ((ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R âˆ© range I)) :=
((is_compact_closed_ball _ _).inter_right I.closed_range).image_of_continuous_on $
  (continuous_on_ext_chart_at_symm _ _).mono f.closed_ball_subset

/-- Given a smooth bump function `f : smooth_bump_function I c`, the closed ball of radius `f.R` is
known to include the support of `f`. These closed balls (in the model normed space `E`) intersected
with `set.range I` form a basis of `ð“[range I] (ext_chart_at I c c)`. -/
lemma nhds_within_range_basis :
  (ð“[range I] (ext_chart_at I c c)).has_basis (Î» f : smooth_bump_function I c, true)
    (Î» f, closed_ball (ext_chart_at I c c) f.R âˆ© range I) :=
begin
  refine ((nhds_within_has_basis nhds_basis_closed_ball _).restrict_subset
      (ext_chart_at_target_mem_nhds_within _ _)).to_has_basis' _ _,
  { rintro R âŸ¨hR0, hsubâŸ©,
    exact âŸ¨âŸ¨âŸ¨R / 2, R, half_pos hR0, half_lt_self hR0âŸ©, hsubâŸ©, trivial, subset.rflâŸ© },
  { exact Î» f _, inter_mem (mem_nhds_within_of_mem_nhds $ closed_ball_mem_nhds _ f.R_pos)
      self_mem_nhds_within }
end

lemma is_closed_image_of_is_closed {s : set M} (hsc : is_closed s) (hs : s âŠ† support f) :
  is_closed (ext_chart_at I c '' s) :=
begin
  rw f.image_eq_inter_preimage_of_subset_support hs,
  refine continuous_on.preimage_closed_of_closed
    ((continuous_on_ext_chart_at_symm _ _).mono f.closed_ball_subset) _ hsc,
  exact is_closed.inter is_closed_ball I.closed_range
end

/-- If `f` is a smooth bump function and `s` closed subset of the support of `f` (i.e., of the open
ball of radius `f.R`), then there exists `0 < r < f.R` such that `s` is a subset of the open ball of
radius `r`. Formally, `s âŠ† e.source âˆ© e â»Â¹' (ball (e c) r)`, where `e = ext_chart_at I c`. -/
lemma exists_r_pos_lt_subset_ball {s : set M} (hsc : is_closed s) (hs : s âŠ† support f) :
  âˆƒ r (hr : r âˆˆ Ioo 0 f.R), s âŠ†
    (chart_at H c).source âˆ© ext_chart_at I c â»Â¹' (ball (ext_chart_at I c c) r) :=
begin
  set e := ext_chart_at I c,
  have : is_closed (e '' s) := f.is_closed_image_of_is_closed hsc hs,
  rw [support_eq_inter_preimage, subset_inter_iff, â† image_subset_iff] at hs,
  rcases exists_pos_lt_subset_ball f.R_pos this hs.2 with âŸ¨r, hrR, hrâŸ©,
  exact âŸ¨r, hrR, subset_inter hs.1 (image_subset_iff.1 hr)âŸ©
end

/-- Replace `r` with another value in the interval `(0, f.R)`. -/
def update_r (r : â„) (hr : r âˆˆ Ioo 0 f.R) : smooth_bump_function I c :=
âŸ¨âŸ¨r, f.R, hr.1, hr.2âŸ©, f.closed_ball_subsetâŸ©

@[simp] lemma update_r_R {r : â„} (hr : r âˆˆ Ioo 0 f.R) : (f.update_r r hr).R = f.R := rfl

@[simp] lemma update_r_r {r : â„} (hr : r âˆˆ Ioo 0 f.R) : (f.update_r r hr).r = r := rfl

@[simp] lemma support_update_r {r : â„} (hr : r âˆˆ Ioo 0 f.R) :
  support (f.update_r r hr) = support f :=
by simp only [support_eq_inter_preimage, update_r_R]

instance : inhabited (smooth_bump_function I c) :=
classical.inhabited_of_nonempty nhds_within_range_basis.nonempty

variables [t2_space M]

lemma is_closed_symm_image_closed_ball :
  is_closed ((ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R âˆ© range I)) :=
f.is_compact_symm_image_closed_ball.is_closed

lemma tsupport_subset_symm_image_closed_ball :
  tsupport f âŠ† (ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R âˆ© range I) :=
begin
  rw [tsupport, support_eq_symm_image],
  exact closure_minimal (image_subset _ $ inter_subset_inter_left _ ball_subset_closed_ball)
    f.is_closed_symm_image_closed_ball
end

lemma tsupport_subset_ext_chart_at_source :
  tsupport f âŠ† (ext_chart_at I c).source :=
calc tsupport f
    âŠ† (ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R âˆ© range I) :
  f.tsupport_subset_symm_image_closed_ball
... âŠ† (ext_chart_at I c).symm '' (ext_chart_at I c).target :
  image_subset _ f.closed_ball_subset
... = (ext_chart_at I c).source :
  (ext_chart_at I c).symm_image_target_eq_source

lemma tsupport_subset_chart_at_source :
  tsupport f âŠ† (chart_at H c).source :=
by simpa only [ext_chart_at_source] using f.tsupport_subset_ext_chart_at_source

protected lemma has_compact_support : has_compact_support f :=
is_compact_of_is_closed_subset f.is_compact_symm_image_closed_ball is_closed_closure
 f.tsupport_subset_symm_image_closed_ball

variables (I c)

/-- The closures of supports of smooth bump functions centered at `c` form a basis of `ð“ c`.
In other words, each of these closures is a neighborhood of `c` and each neighborhood of `c`
includes `tsupport f` for some `f : smooth_bump_function I c`. -/
lemma nhds_basis_tsupport :
  (ð“ c).has_basis (Î» f : smooth_bump_function I c, true) (Î» f, tsupport f) :=
begin
  have : (ð“ c).has_basis (Î» f : smooth_bump_function I c, true)
    (Î» f, (ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R âˆ© range I)),
  { rw [â† map_ext_chart_at_symm_nhds_within_range I c],
    exact nhds_within_range_basis.map _ },
  refine this.to_has_basis' (Î» f hf, âŸ¨f, trivial, f.tsupport_subset_symm_image_closed_ballâŸ©)
    (Î» f _, f.tsupport_mem_nhds),
end

variable {c}

/-- Given `s âˆˆ ð“ c`, the supports of smooth bump functions `f : smooth_bump_function I c` such that
`tsupport f âŠ† s` form a basis of `ð“ c`.  In other words, each of these supports is a
neighborhood of `c` and each neighborhood of `c` includes `support f` for some `f :
smooth_bump_function I c` such that `tsupport f âŠ† s`. -/
lemma nhds_basis_support {s : set M} (hs : s âˆˆ ð“ c) :
  (ð“ c).has_basis (Î» f : smooth_bump_function I c, tsupport f âŠ† s) (Î» f, support f) :=
((nhds_basis_tsupport I c).restrict_subset hs).to_has_basis'
  (Î» f hf, âŸ¨f, hf.2, subset_closureâŸ©) (Î» f hf, f.support_mem_nhds)

variables [smooth_manifold_with_corners I M] {I}

/-- A smooth bump function is infinitely smooth. -/
protected lemma smooth : smooth I ð“˜(â„) f :=
begin
  refine cont_mdiff_of_support (Î» x hx, _),
  have : x âˆˆ (chart_at H c).source := f.tsupport_subset_chart_at_source hx,
  refine cont_mdiff_at.congr_of_eventually_eq _
    (f.eq_on_source.eventually_eq_of_mem $ is_open.mem_nhds (chart_at _ _).open_source this),
  exact f.to_cont_diff_bump.cont_diff_at.cont_mdiff_at.comp _
    (cont_mdiff_at_ext_chart_at' this)
end

protected lemma smooth_at {x} : smooth_at I ð“˜(â„) f x := f.smooth.smooth_at

protected lemma continuous : continuous f := f.smooth.continuous

/-- If `f : smooth_bump_function I c` is a smooth bump function and `g : M â†’ G` is a function smooth
on the source of the chart at `c`, then `f â€¢ g` is smooth on the whole manifold. -/
lemma smooth_smul {G} [normed_add_comm_group G] [normed_space â„ G]
  {g : M â†’ G} (hg : smooth_on I ð“˜(â„, G) g (chart_at H c).source) :
  smooth I ð“˜(â„, G) (Î» x, f x â€¢ g x) :=
begin
  apply cont_mdiff_of_support (Î» x hx, _),
  have : x âˆˆ (chart_at H c).source,
  calc x âˆˆ tsupport (Î» x, f x â€¢ g x) : hx
     ... âŠ† tsupport f : tsupport_smul_subset_left _ _
     ... âŠ† (chart_at _ c).source : f.tsupport_subset_chart_at_source,
  exact f.smooth_at.smul ((hg _ this).cont_mdiff_at $
    is_open.mem_nhds (chart_at _ _).open_source this)
end

end smooth_bump_function
