/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.calculus.specific_functions
import geometry.manifold.diffeomorph
import geometry.manifold.instances.real

/-!
# Smooth bump functions on a smooth manifold

In this file we define `smooth_bump_function I c` to be a bundled smooth "bump" function centered at
`c`. It is a structure that consists of two real numbers `0 < r < R` with small enough `R`. We
define a coercion to function for this type, and for `f : smooth_bump_function I c`, the function
`‚áëf` written in the extended chart at `f.c` has the following properties:

* `f x = 1` in the closed euclidean ball of radius `f.r` centered at `f.c`;
* `f x = 0` outside of the euclidean ball of radius `f.R` centered at `f.c`;
* `0 ‚â§ f x ‚â§ 1` for all `x`.

The actual statements involve (pre)images under `ext_chart_at I f.c` and are given as lemmas in the
`smooth_bump_function` namespace.

We also define `smooth_bump_covering` of a set `s : set M` to be a collection of
`smooth_bump_function`s such that their supports is a locally finite family of sets, and for each
point `x ‚àà s` there exists a bump function `f i` in the collection such that `f i =·∂†[ùìù x] 1`. This
structure is the main building block in the construction of a smooth partition of unity (see TODO),
and can be used instead of a partition of unity in some proofs.

We say that `f : smooth_bump_covering I s` is *subordinate* to a map `U : M ‚Üí set M` if for each
index `i`, we have `closure (support (f i)) ‚äÜ U (f i).c`. This notion is a bit more general than
being subordinate to an open covering of `M`, because we make no assumption about the way `U x`
depends on `x`.

We prove that on a smooth finitely dimensional real manifold with `œÉ`-compact Hausdorff topology,
for any `U : M ‚Üí set M` such that `‚àÄ x ‚àà s, U x ‚àà ùìù x` there exists a `smooth_bump_covering I s`
subordinate to `U`. Then we use this fact to prove a version of the Whitney embedding theorem: any
compact real manifold can be embedded into `‚Ñù^n` for large enough `n`.

## TODO

* Prove the weak Whitney embedding theorem: any `œÉ`-compact smooth `m`-dimensional manifold can be
  embedded into `‚Ñù^(2m+1)`. This requires a version of Sard's theorem: for a locally Lipschitz
  continuous map `f : ‚Ñù^m ‚Üí ‚Ñù^n`, `m < n`, the range has Hausdorff dimension at most `m`, hence it
  has measure zero.

* Construct a smooth partition of unity. While we can do it now, the formulas will be much nicer if
  we wait for `finprod` and `finsum` coming in #6355.

* Deduce some corollaries from existence of a smooth partition of unity.

  - Prove that for any disjoint closed sets `s`, `t` there exists a smooth function `f` suth that
  `f` equals zero on `s` and `f` equals one on `t`.

  - Build a framework for to transfer local definitions to global using partition of unity and use
    it to define, e.g., the integral of a differential form over a manifold.

## Tags

manifold, smooth bump function, partition of unity, Whitney theorem
-/

universes uE uF uH uM
variables
{E : Type uE} [normed_group E] [normed_space ‚Ñù E] [finite_dimensional ‚Ñù E]
{H : Type uH} [topological_space H] (I : model_with_corners ‚Ñù E H)
{M : Type uM} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]

open function filter finite_dimensional set
open_locale topological_space manifold classical filter big_operators

noncomputable theory

/-!
### Smooth bump function

In this section we define a structure for a bundled smooth bump function and prove its properties.
-/

/-- Given a smooth manifold modelled on a finite dimensional space `E`,
`f : smooth_bump_function I M` is a smooth function on `M` such that in the extended chart `e` at
`f.c`:

* `f x = 1` in the closed euclidean ball of radius `f.r` centered at `f.c`;
* `f x = 0` outside of the euclidean ball of radius `f.R` centered at `f.c`;
* `0 ‚â§ f x ‚â§ 1` for all `x`.

The structure contains data required to construct a function with these properties. The function is
available as `‚áëf` or `f x`. Formal statements of the properties listed above involve some
(pre)images under `ext_chart_at I f.c` and are given as lemmas in the `msmooth_bump_function`
namespace. -/
structure smooth_bump_function (c : M) extends times_cont_diff_bump (ext_chart_at I c c) :=
(closed_ball_subset :
  (euclidean.closed_ball (ext_chart_at I c c) R) ‚à© range I ‚äÜ (ext_chart_at I c).target)

variable {M}

namespace smooth_bump_function

open euclidean (renaming dist -> eudist)

variables {c : M} (f : smooth_bump_function I c) {x : M} {I}

/-- The function defined by `f : smooth_bump_function c`. Use automatic coercion to function
instead. -/
def to_fun : M ‚Üí ‚Ñù :=
indicator (chart_at H c).source (f.to_times_cont_diff_bump ‚àò ext_chart_at I c)

instance : has_coe_to_fun (smooth_bump_function I c) := ‚ü®_, to_fun‚ü©

lemma coe_def :
  ‚áëf = indicator (chart_at H c).source (f.to_times_cont_diff_bump ‚àò ext_chart_at I c) :=
rfl

lemma R_pos : 0 < f.R := f.to_times_cont_diff_bump.R_pos

lemma ball_subset :
  ball (ext_chart_at I c c) f.R ‚à© range I ‚äÜ (ext_chart_at I c).target :=
subset.trans (inter_subset_inter_left _ ball_subset_closed_ball) f.closed_ball_subset

lemma eq_on_source :
  eq_on f (f.to_times_cont_diff_bump ‚àò ext_chart_at I c) (chart_at H c).source :=
eq_on_indicator

lemma eventually_eq_of_mem_source (hx : x ‚àà (chart_at H c).source) :
  f =·∂†[ùìù x] f.to_times_cont_diff_bump ‚àò ext_chart_at I c :=
f.eq_on_source.eventually_eq_of_mem $ mem_nhds_sets (chart_at H c).open_source hx

lemma one_of_dist_le (hs : x ‚àà (chart_at H c).source)
  (hd : eudist (ext_chart_at I c x) (ext_chart_at I c c) ‚â§ f.r) :
  f x = 1 :=
by simp only [f.eq_on_source hs, (‚àò), f.to_times_cont_diff_bump.one_of_mem_closed_ball hd]

lemma support_eq_inter_preimage :
  support f =
    (chart_at H c).source ‚à© (ext_chart_at I c ‚Åª¬π' ball (ext_chart_at I c c) f.R) :=
by rw [coe_def, support_indicator, (‚àò), support_comp_eq_preimage, ‚Üê ext_chart_at_source I,
  ‚Üê (ext_chart_at I c).symm_image_target_inter_eq',
  ‚Üê (ext_chart_at I c).symm_image_target_inter_eq', f.to_times_cont_diff_bump.support_eq]

lemma open_support : is_open (support f) :=
by { rw support_eq_inter_preimage, exact ext_chart_preimage_open_of_open I c is_open_ball }

lemma support_eq_symm_image :
  support f = (ext_chart_at I c).symm '' (ball (ext_chart_at I c c) f.R ‚à© range I) :=
begin
  rw [f.support_eq_inter_preimage, ‚Üê ext_chart_at_source I,
    ‚Üê (ext_chart_at I c).symm_image_target_inter_eq', inter_comm],
  congr' 1 with y,
  exact and.congr_right_iff.2
    (Œª hy, ‚ü®Œª h, ext_chart_at_target_subset_range _ _ h, Œª h, f.ball_subset ‚ü®hy, h‚ü©‚ü©)
end

lemma support_subset_source : support f ‚äÜ (chart_at H c).source :=
by { rw [f.support_eq_inter_preimage, ‚Üê ext_chart_at_source I], exact inter_subset_left _ _ }

lemma image_eq_inter_preimage_of_subset_support {s : set M} (hs : s ‚äÜ support f) :
  ext_chart_at I c '' s =
    closed_ball (ext_chart_at I c c) f.R ‚à© range I ‚à© (ext_chart_at I c).symm ‚Åª¬π' s :=
begin
  rw [support_eq_inter_preimage, subset_inter_iff, ‚Üê ext_chart_at_source I,
    ‚Üê image_subset_iff] at hs,
  cases hs with hse hsf,
  apply subset.antisymm,
  { refine subset_inter (subset_inter (subset.trans hsf ball_subset_closed_ball) _) _,
    { rintro _ ‚ü®x, -, rfl‚ü©, exact mem_range_self _ },
    { rw [(ext_chart_at I c).image_eq_target_inter_inv_preimage hse],
      exact inter_subset_right _ _ } },
  { refine subset.trans (inter_subset_inter_left _ f.closed_ball_subset) _,
    rw [(ext_chart_at I c).image_eq_target_inter_inv_preimage hse] }
end

lemma mem_Icc : f x ‚àà Icc (0 : ‚Ñù) 1 :=
begin
  have : f x = 0 ‚à® f x = _, from indicator_eq_zero_or_self _ _ _,
  cases this; rw this,
  exacts [left_mem_Icc.2 zero_le_one,
    ‚ü®f.to_times_cont_diff_bump.nonneg, f.to_times_cont_diff_bump.le_one‚ü©]
end

lemma nonneg : 0 ‚â§ f x := f.mem_Icc.1

lemma le_one : f x ‚â§ 1 := f.mem_Icc.2

lemma eventually_eq_one_of_dist_lt (hs : x ‚àà (chart_at H c).source)
  (hd : eudist (ext_chart_at I c x) (ext_chart_at I c c) < f.r) :
  f =·∂†[ùìù x] 1 :=
begin
  filter_upwards [mem_nhds_sets (ext_chart_preimage_open_of_open I c is_open_ball) ‚ü®hs, hd‚ü©],
  rintro z ‚ü®hzs, hzd : _ < _‚ü©,
  exact f.one_of_dist_le hzs hzd.le
end

lemma eventually_eq_one : f =·∂†[ùìù c] 1 :=
f.eventually_eq_one_of_dist_lt (mem_chart_source _ _) $
by { rw [euclidean.dist, dist_self], exact f.r_pos }

@[simp] lemma eq_one : f c = 1 := f.eventually_eq_one.eq_of_nhds

lemma support_mem_nhds : support f ‚àà ùìù c :=
f.eventually_eq_one.mono $ Œª x hx, by { rw hx, exact one_ne_zero }

lemma closure_support_mem_nhds : closure (support f) ‚àà ùìù c :=
mem_sets_of_superset f.support_mem_nhds subset_closure

lemma c_mem_support : c ‚àà support f := mem_of_nhds f.support_mem_nhds

lemma nonempty_support : (support f).nonempty := ‚ü®c, f.c_mem_support‚ü©

lemma compact_symm_image_closed_ball :
  is_compact ((ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R ‚à© range I)) :=
(compact_ball.inter_right I.closed_range).image_of_continuous_on $
  (ext_chart_at_continuous_on_symm _ _).mono f.closed_ball_subset

/-- Given a smooth bump function `f : smooth_bump_function I c`, the closed ball of radius `f.R` is
known to include the support of `f`. These closed balls (in the model normed space `E`) intersected
with `set.range I` form a basis of `ùìù[range I] (ext_chart_at I c c)`. -/
lemma nhds_within_range_basis :
  (ùìù[range I] (ext_chart_at I c c)).has_basis (Œª f : smooth_bump_function I c, true)
    (Œª f, closed_ball (ext_chart_at I c c) f.R ‚à© range I) :=
begin
  refine ((nhds_within_has_basis euclidean.nhds_basis_closed_ball _).restrict_subset
      (ext_chart_at_target_mem_nhds_within _ _)).to_has_basis' _ _,
  { rintro R ‚ü®hR0, hsub‚ü©,
    exact ‚ü®‚ü®‚ü®‚ü®R / 2, R, half_pos hR0, half_lt_self hR0‚ü©‚ü©, hsub‚ü©, trivial, subset.rfl‚ü© },
  { exact Œª f _, inter_mem_sets (mem_nhds_within_of_mem_nhds $ closed_ball_mem_nhds f.R_pos)
      self_mem_nhds_within }
end

lemma closed_image_of_closed {s : set M} (hsc : is_closed s) (hs : s ‚äÜ support f) :
  is_closed (ext_chart_at I c '' s) :=
begin
  rw f.image_eq_inter_preimage_of_subset_support hs,
  refine continuous_on.preimage_closed_of_closed
    ((ext_chart_continuous_on_symm _ _).mono f.closed_ball_subset) _ hsc,
  exact is_closed_inter is_closed_closed_ball I.closed_range
end

/-- If `f` is a smooth bump function and `s` closed subset of the support of `f` (i.e., of the open
ball of radius `f.R`), then there exists `0 < r < f.R` such that `s` is a subset of the open ball of
radius `r`. Formally, `s ‚äÜ e.source ‚à© e ‚Åª¬π' (ball (e c) r)`, where `e = ext_chart_at I c`. -/
lemma exists_r_pos_lt_subset_ball {s : set M} (hsc : is_closed s) (hs : s ‚äÜ support f) :
  ‚àÉ r (hr : r ‚àà Ioo 0 f.R), s ‚äÜ
    (chart_at H c).source ‚à© ext_chart_at I c ‚Åª¬π' (ball (ext_chart_at I c c) r) :=
begin
  set e := ext_chart_at I c,
  have : is_closed (e '' s) := f.closed_image_of_closed hsc hs,
  rw [support_eq_inter_preimage, subset_inter_iff, ‚Üê image_subset_iff] at hs,
  rcases euclidean.exists_pos_lt_subset_ball f.R_pos this hs.2 with ‚ü®r, hrR, hr‚ü©,
  exact ‚ü®r, hrR, subset_inter hs.1 (image_subset_iff.1 hr)‚ü©
end

/-- Replace `r` with another value in the interval `(0, f.R)`. -/
def update_r (r : ‚Ñù) (hr : r ‚àà Ioo 0 f.R) : smooth_bump_function I c :=
‚ü®‚ü®‚ü®r, f.R, hr.1, hr.2‚ü©‚ü©, f.closed_ball_subset‚ü©

@[simp] lemma update_r_R {r : ‚Ñù} (hr : r ‚àà Ioo 0 f.R) : (f.update_r r hr).R = f.R := rfl

@[simp] lemma update_r_r {r : ‚Ñù} (hr : r ‚àà Ioo 0 f.R) : (f.update_r r hr).r = r := rfl

@[simp] lemma support_update_r {r : ‚Ñù} (hr : r ‚àà Ioo 0 f.R) :
  support (f.update_r r hr) = support f :=
by simp only [support_eq_inter_preimage, update_r_R]

instance : inhabited (smooth_bump_function I c) :=
classical.inhabited_of_nonempty nhds_within_range_basis.nonempty

variables [t2_space M]

lemma closed_symm_image_closed_ball :
  is_closed ((ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R ‚à© range I)) :=
f.compact_symm_image_closed_ball.is_closed

lemma closure_support_subset_symm_image_closed_ball :
  closure (support f) ‚äÜ
    (ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R ‚à© range I) :=
begin
  rw support_eq_symm_image,
  exact closure_minimal (image_subset _ $ inter_subset_inter_left _ ball_subset_closed_ball)
    f.closed_symm_image_closed_ball
end

lemma closure_support_subset_ext_chart_at_source :
  closure (support f) ‚äÜ (ext_chart_at I c).source :=
calc closure (support f)
    ‚äÜ (ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R ‚à© range I) :
  f.closure_support_subset_symm_image_closed_ball
... ‚äÜ (ext_chart_at I c).symm '' (ext_chart_at I c).target :
  image_subset _ f.closed_ball_subset
... = (ext_chart_at I c).source :
  (ext_chart_at I c).symm_image_target_eq_source

lemma closure_support_subset_chart_at_source :
  closure (support f) ‚äÜ (chart_at H c).source :=
by simpa only [ext_chart_at_source] using f.closure_support_subset_ext_chart_at_source

lemma compact_closure_support : is_compact (closure $ support f) :=
compact_of_is_closed_subset f.compact_symm_image_closed_ball is_closed_closure
 f.closure_support_subset_symm_image_closed_ball

variables (I c)

/-- The closures of supports of smooth bump functions centered at `c` form a basis of `ùìù c`.
In other words, each of these closures is a neighborhood of `c` and each neighborhood of `c`
includes `closure (support f)` for some `f : smooth_bump_function I c`. -/
lemma nhds_basis_closure_support :
  (ùìù c).has_basis (Œª f : smooth_bump_function I c, true) (Œª f, closure $ support f) :=
begin
  have : (ùìù c).has_basis (Œª f : smooth_bump_function I c, true)
    (Œª f, (ext_chart_at I c).symm '' (closed_ball (ext_chart_at I c c) f.R ‚à© range I)),
  { rw [‚Üê ext_chart_at_symm_map_nhds_within_range I c],
    exact nhds_within_range_basis.map _ },
  refine this.to_has_basis' (Œª f hf, ‚ü®f, trivial, f.closure_support_subset_symm_image_closed_ball‚ü©)
    (Œª f _, f.closure_support_mem_nhds),
end

variable {c}

/-- Given `s ‚àà ùìù c`, the supports of smooth bump functions `f : smooth_bump_function I c` such that
`closure (support f) ‚äÜ s` form a basis of `ùìù c`.  In other words, each of these supports is a
neighborhood of `c` and each neighborhood of `c` includes `support f` for some `f :
smooth_bump_function I c` such that `closure (support f) ‚äÜ s`. -/
lemma nhds_basis_support {s : set M} (hs : s ‚àà ùìù c) :
  (ùìù c).has_basis (Œª f : smooth_bump_function I c, closure (support f) ‚äÜ s) (Œª f, support f) :=
((nhds_basis_closure_support I c).restrict_subset hs).to_has_basis'
  (Œª f hf, ‚ü®f, hf.2, subset_closure‚ü©) (Œª f hf, f.support_mem_nhds)

variables [smooth_manifold_with_corners I M] {I}

/-- A smooth bump function is infinitely smooth. -/
protected lemma smooth : smooth I ùìò(‚Ñù) f :=
begin
  refine times_cont_mdiff_of_support (Œª x hx, _),
  have : x ‚àà (chart_at H c).source := f.closure_support_subset_chart_at_source hx,
  refine times_cont_mdiff_at.congr_of_eventually_eq _
    (f.eq_on_source.eventually_eq_of_mem $ mem_nhds_sets (chart_at _ _).open_source this),
  exact f.to_times_cont_diff_bump.times_cont_diff_at.times_cont_mdiff_at.comp _
    (times_cont_mdiff_at_ext_chart_at' this)
end

protected lemma smooth_at {x} : smooth_at I ùìò(‚Ñù) f x := f.smooth.smooth_at

/-- If `f : smooth_bump_function I c` is a smooth bump function and `g : M ‚Üí G` is a function smooth
on the source of the chart at `c`, then `f ‚Ä¢ g` is smooth on the whole manifold. -/
lemma smooth_smul {G} [normed_group G] [normed_space ‚Ñù G]
  {g : M ‚Üí G} (hg : smooth_on I ùìò(‚Ñù, G) g (chart_at H c).source) :
  smooth I ùìò(‚Ñù, G) (Œª x, f x ‚Ä¢ g x) :=
begin
  apply times_cont_mdiff_of_support (Œª x hx, _),
  have : x ‚àà (chart_at H c).source,
  calc x ‚àà closure (support (Œª x, f x ‚Ä¢ g x)) : hx
     ... ‚äÜ closure (support f) : closure_mono (support_smul_subset_left _ _)
     ... ‚äÜ (chart_at _ c).source : f.closure_support_subset_chart_at_source,
  exact f.smooth_at.smul ((hg _ this).times_cont_mdiff_at $
    mem_nhds_sets (chart_at _ _).open_source this)
end

end smooth_bump_function

/-!
### Covering by supports of smooth bump functions

In this section we define `smooth_bump_covering I s` to be a collection of `smooth_bump_function`s
such that their supports is a locally finite family of sets and for each `x ‚àà s` some function `f i`
from the collection is equal to `1` in a neighborhood of `x`. A covering of this type is useful to
construct a smooth partition of unity and can be used instead of a partition of unity in some
proofs.

We prove that on a smooth finitely dimensional real manifold with `Œ£`-countable Hausdorff topology,
for any `U : M ‚Üí set M` such that `‚àÄ x ‚àà s, U x ‚àà ùìù x` there exists a `smooth_bump_covering I s`
subordinate to `U`. Then we use this fact to prove a version of the Whitney embedding theorem: any
compact real manifold can be embedded into `‚Ñù^n` for large enough `n`.
-/

/-- We say that a collection of `smooth_bump_function`s is a `smooth_bump_covering` of a set `s` if

* `(f i).c ‚àà s` for all `i`;
* the family `Œª i, support (f i)` is locally finite;
* for each point `x ‚àà s` there exists `i` such that `f i =·∂†[ùìù x] 1`;
  in other words, `x` belongs to the interior of `{y | f i y = 1}`;

If `M` is a finite dimensional real manifold which is a sigma-compact Hausdorff topological space,
then a choice of `smooth_bump_covering` is available as `smooth_bump_covering.choice_set`, see also
`smooth_bump_covering.choice` for the case `s = univ` and
`smooth_bump_covering.exists_is_subordinate` for a lemma providing a covering subordinate to a given
`U : M ‚Üí set M`.

This covering can be used, e.g., to construct a partition of unity and to prove the weak
Whitney embedding theorem. -/
structure smooth_bump_covering (s : set M) :=
(Œπ : Type uM)
(c : Œπ ‚Üí M)
(to_fun : Œ† i, smooth_bump_function I (c i))
(c_mem' : ‚àÄ i, c i ‚àà s)
(locally_finite' : locally_finite (Œª i, support (to_fun i)))
(eventually_eq_one' : ‚àÄ x ‚àà s, ‚àÉ i, to_fun i =·∂†[ùìù x] 1)

namespace smooth_bump_covering

variables {s : set M} {U : M ‚Üí set M} (f : smooth_bump_covering I s) {I}

instance : has_coe_to_fun (smooth_bump_covering I s) := ‚ü®_, to_fun‚ü©

@[simp] lemma coe_mk (Œπ : Type uM) (c : Œπ ‚Üí M) (to_fun : Œ† i, smooth_bump_function I (c i))
  (h‚ÇÅ h‚ÇÇ h‚ÇÉ) : ‚áë(mk Œπ c to_fun h‚ÇÅ h‚ÇÇ h‚ÇÉ : smooth_bump_covering I s) = to_fun :=
rfl

/-- 
We say that `f : smooth_bump_covering I s` is *subordinate* to a map `U : M ‚Üí set M` if for each
index `i`, we have `closure (support (f i)) ‚äÜ U (f i).c`. This notion is a bit more general than
being subordinate to an open covering of `M`, because we make no assumption about the way `U x`
depends on `x`.
-/
def is_subordinate {s : set M} (f : smooth_bump_covering I s) (U : M ‚Üí set M) :=
‚àÄ i, closure (support $ f i) ‚äÜ U (f.c i)

variable (I)

/-- Let `M` be a smooth manifold with corners modelled on a finite dimensional real vector space.
Suppose also that `M` is a Hausdorff `Œ£`-compact topological space. Let `s` be a closed set
in `M` and `U : M ‚Üí set M` be a collection of sets such that `U x ‚àà ùìù x` for every `x ‚àà s`.
Then there exists a smooth bump covering of `s` that is subordinate to `U`. -/
lemma exists_is_subordinate [t2_space M] [sigma_compact_space M] (hs : is_closed s)
  (hU : ‚àÄ x ‚àà s, U x ‚àà ùìù x) :
  ‚àÉ f : smooth_bump_covering I s, f.is_subordinate U :=
begin
  -- First we deduce some missing instances
  haveI : locally_compact_space H := I.locally_compact,
  haveI : locally_compact_space M := charted_space.locally_compact H,
  haveI : normal_space M := normal_of_paracompact_t2,
  -- Next we choose a covering by supports of smooth bump functions
  have hB := Œª x hx, smooth_bump_function.nhds_basis_support I (hU x hx),
  rcases refinement_of_locally_compact_sigma_compact_of_nhds_basis_set hs hB
    with ‚ü®Œπ, c, f, hf, hsub', hfin‚ü©, choose hcs hfU using hf,
  /- Then we use the shrinking lemma to get a covering by smaller open -/
  rcases exists_subset_Union_closed_subset hs (Œª i, (f i).open_support)
    (Œª x hx, hfin.point_finite x) hsub' with ‚ü®V, hsV, hVc, hVf‚ü©,
  choose r hrR hr using Œª i, (f i).exists_r_pos_lt_subset_ball (hVc i) (hVf i),
  refine ‚ü®‚ü®Œπ, c, Œª i, (f i).update_r (r i) (hrR i), hcs, _, Œª x hx, _‚ü©, Œª i, _‚ü©,
  { simpa only [smooth_bump_function.support_update_r] },
  { refine (mem_Union.1 $ hsV hx).imp (Œª i hi, _),
    exact ((f i).update_r _ _).eventually_eq_one_of_dist_lt
      ((f i).support_subset_source $ hVf _ hi) (hr i hi).2 },
  { simpa only [coe_mk, smooth_bump_function.support_update_r] using hfU i }
end

/-- Choice of a covering of a closed set `s` by supports of smooth bump functions. -/
def choice_set [t2_space M] [sigma_compact_space M] (s : set M) (hs : is_closed s) :
  smooth_bump_covering I s :=
(exists_is_subordinate I hs (Œª x hx, univ_mem_sets)).some

instance [t2_space M] [sigma_compact_space M] {s : set M} [is_closed s] :
  inhabited (smooth_bump_covering I s) :=
‚ü®choice_set I s ‚Äπ_‚Ä∫‚ü©

variable (M)

/-- Choice of a covering of a manifold by supports of smooth bump functions. -/
def choice [t2_space M] [sigma_compact_space M] :
  smooth_bump_covering I (univ : set M) :=
choice_set I univ is_closed_univ

variables {I M}

protected lemma locally_finite : locally_finite (Œª i, support (f i)) := f.locally_finite'

protected lemma point_finite (x : M) : {i | f i x ‚â† 0}.finite :=
f.locally_finite.point_finite x

lemma mem_chart_at_source_of_eq_one {i : f.Œπ} {x : M} (h : f i x = 1) :
  x ‚àà (chart_at H (f.c i)).source :=
(f i).support_subset_source $ by simp [h]

lemma mem_ext_chart_at_source_of_eq_one {i : f.Œπ} {x : M} (h : f i x = 1) :
  x ‚àà (ext_chart_at I (f.c i)).source :=
by { rw ext_chart_at_source, exact f.mem_chart_at_source_of_eq_one h }

/-- Index of a bump function such that `f i =·∂†[ùìù x] 1`. -/
def ind (x : M) (hx : x ‚àà s) : f.Œπ := (f.eventually_eq_one' x hx).some

lemma eventually_eq_one (x : M) (hx : x ‚àà s) : f (f.ind x hx) =·∂†[ùìù x] 1 :=
(f.eventually_eq_one' x hx).some_spec

lemma apply_ind (x : M) (hx : x ‚àà s) : f (f.ind x hx) x = 1 :=
(f.eventually_eq_one x hx).eq_of_nhds

lemma mem_support_ind (x : M) (hx : x ‚àà s) : x ‚àà support (f $ f.ind x hx) :=
by simp [f.apply_ind x hx]

lemma mem_chart_at_ind_source (x : M) (hx : x ‚àà s) :
  x ‚àà (chart_at H (f.c (f.ind x hx))).source :=
f.mem_chart_at_source_of_eq_one (f.apply_ind x hx)

lemma mem_ext_chart_at_ind_source (x : M) (hx : x ‚àà s) :
  x ‚àà (ext_chart_at I (f.c (f.ind x hx))).source :=
f.mem_ext_chart_at_source_of_eq_one (f.apply_ind x hx)

section embedding

/-!
### Whitney embedding theorem

In this section we prove a version of the Whitney embedding theorem: for any compact real manifold
`M`, for sufficiently large `n` there exists a smooth embedding `M ‚Üí ‚Ñù^n`.
-/

instance fintype_Œπ_of_compact [compact_space M] : fintype f.Œπ :=
f.locally_finite.fintype_of_compact $ Œª i, (f i).nonempty_support

variables [t2_space M] [fintype f.Œπ]

/-- Smooth embedding of `M` into `(E √ó ‚Ñù) ^ f.Œπ`. -/
def embedding_pi_tangent : C^‚àû‚üÆI, M; ùìò(‚Ñù, f.Œπ ‚Üí (E √ó ‚Ñù)), f.Œπ ‚Üí (E √ó ‚Ñù)‚üØ :=
{ to_fun := Œª x i, (f i x ‚Ä¢ ext_chart_at I (f.c i) x, f i x),
  times_cont_mdiff_to_fun := times_cont_mdiff_pi_space.2 $ Œª i,
    ((f i).smooth_smul times_cont_mdiff_on_ext_chart_at).prod_mk_space ((f i).smooth) }

local attribute [simp] lemma embedding_pi_tangent_coe :
  ‚áëf.embedding_pi_tangent = Œª x i, (f i x ‚Ä¢ ext_chart_at I (f.c i) x, f i x) :=
rfl

lemma embedding_pi_tangent_inj_on : inj_on f.embedding_pi_tangent s :=
begin
  intros x hx y hy h,
  simp only [embedding_pi_tangent_coe, funext_iff] at h,
  obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := prod.mk.inj_iff.1 (h (f.ind x hx)),
  rw [f.apply_ind x hx] at h‚ÇÇ,
  rw [‚Üê h‚ÇÇ, f.apply_ind x hx, one_smul, one_smul] at h‚ÇÅ,
  have := f.mem_ext_chart_at_source_of_eq_one h‚ÇÇ.symm,
  exact (ext_chart_at I (f.c _)).inj_on (f.mem_ext_chart_at_ind_source x hx) this h‚ÇÅ
end

lemma embedding_pi_tangent_injective (f : smooth_bump_covering I (univ : set M))
  [fintype f.Œπ] :
  injective f.embedding_pi_tangent :=
injective_iff_inj_on_univ.2 f.embedding_pi_tangent_inj_on

lemma comp_embedding_pi_tangent_mfderiv (x : M) (hx : x ‚àà s) :
  ((continuous_linear_map.fst ‚Ñù E ‚Ñù).comp
    (@continuous_linear_map.proj ‚Ñù _ f.Œπ (Œª _, E √ó ‚Ñù) _ _ (Œª _, infer_instance) (f.ind x hx))).comp
      (mfderiv I ùìò(‚Ñù, f.Œπ ‚Üí (E √ó ‚Ñù)) f.embedding_pi_tangent x) =
  mfderiv I I (chart_at H (f.c (f.ind x hx))) x :=
begin
  set L := ((continuous_linear_map.fst ‚Ñù E ‚Ñù).comp
    (@continuous_linear_map.proj ‚Ñù _ f.Œπ (Œª _, E √ó ‚Ñù) _ _ (Œª _, infer_instance) (f.ind x hx))),
  have := (L.has_mfderiv_at.comp x (f.embedding_pi_tangent.mdifferentiable_at.has_mfderiv_at)),
  convert has_mfderiv_at_unique this _,
  refine (has_mfderiv_at_ext_chart_at I (f.mem_chart_at_ind_source x hx)).congr_of_eventually_eq _,
  refine (f.eventually_eq_one x hx).mono (Œª y hy, _),
  simp only [embedding_pi_tangent_coe, continuous_linear_map.coe_comp', (‚àò),
    continuous_linear_map.coe_fst', continuous_linear_map.proj_apply],
  rw [hy, pi.one_apply, one_smul]
end

lemma embedding_pi_tangent_ker_mfderiv (x : M) (hx : x ‚àà s) :
  (mfderiv I ùìò(‚Ñù, f.Œπ ‚Üí (E √ó ‚Ñù)) f.embedding_pi_tangent x).ker = ‚ä• :=
begin
  apply bot_unique,
  rw [‚Üê (mdifferentiable_chart I (f.c (f.ind x hx))).ker_mfderiv_eq_bot
    (f.mem_chart_at_ind_source x hx), ‚Üê comp_embedding_pi_tangent_mfderiv],
  exact linear_map.ker_le_ker_comp _ _
end

lemma embedding_pi_tangent_injective_mfderiv (x : M) (hx : x ‚àà s) :
  injective (mfderiv I ùìò(‚Ñù, f.Œπ ‚Üí (E √ó ‚Ñù)) f.embedding_pi_tangent x) :=
linear_map.ker_eq_bot.1 (f.embedding_pi_tangent_ker_mfderiv x hx)

end embedding

/-- Baby version of the Whitney weak embedding theorem: if `M` admits a finite covering by
supports of bump functions, then for some `n` it can be embedded into the `n`-dimensional
Euclidean space. -/
lemma exists_embedding_findim [t2_space M] (f : smooth_bump_covering I (univ : set M))
  [fintype f.Œπ] :
  ‚àÉ (n : ‚Ñï) (e : M ‚Üí euclidean_space ‚Ñù (fin n)), smooth I (ùì° n) e ‚àß
    injective e ‚àß ‚àÄ x : M, injective (mfderiv I (ùì° n) e x) :=
begin
  set F := euclidean_space ‚Ñù (fin $ findim ‚Ñù (f.Œπ ‚Üí (E √ó ‚Ñù))),
  letI : finite_dimensional ‚Ñù (E √ó ‚Ñù) := by apply_instance,
  set eEF : (f.Œπ ‚Üí (E √ó ‚Ñù)) ‚âÉL[‚Ñù] F :=
    continuous_linear_equiv.of_findim_eq findim_euclidean_space_fin.symm,
  refine ‚ü®_, eEF ‚àò f.embedding_pi_tangent,
    eEF.to_diffeomorph.smooth.comp f.embedding_pi_tangent.smooth,
    eEF.injective.comp f.embedding_pi_tangent_injective, Œª x, _‚ü©,
  rw [mfderiv_comp _ eEF.differentiable_at.mdifferentiable_at
    f.embedding_pi_tangent.mdifferentiable_at, eEF.mfderiv_eq],
  exact eEF.injective.comp (f.embedding_pi_tangent_injective_mfderiv _ trivial)
end

end smooth_bump_covering

/-- Baby version of the Whitney weak embedding theorem: if `M` admits a finite covering by
supports of bump functions, then for some `n` it can be embedded into the `n`-dimensional
Euclidean space. -/
lemma exists_embedding_findim_of_compact [t2_space M] [compact_space M] :
  ‚àÉ (n : ‚Ñï) (e : M ‚Üí euclidean_space ‚Ñù (fin n)), smooth I (ùì° n) e ‚àß
    injective e ‚àß ‚àÄ x : M, injective (mfderiv I (ùì° n) e x) :=
(smooth_bump_covering.choice I M).exists_embedding_findim
