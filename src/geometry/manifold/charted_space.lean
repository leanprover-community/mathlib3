/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import topology.local_homeomorph

/-!
# Charted spaces

A smooth manifold is a topological space `M` locally modelled on a euclidean space (or a euclidean
half-space for manifolds with boundaries, or an infinite dimensional vector space for more general
notions of manifolds), i.e., the manifold is covered by open subsets on which there are local
homeomorphisms (the charts) going to a model space `H`, and the changes of charts should be smooth
maps.

In this file, we introduce a general framework describing these notions, where the model space is an
arbitrary topological space. We avoid the word *manifold*, which should be reserved for the
situation where the model space is a (subset of a) vector space, and use the terminology
*charted space* instead.

If the changes of charts satisfy some additional property (for instance if they are smooth), then
`M` inherits additional structure (it makes sense to talk about smooth manifolds). There are
therefore two different ingredients in a charted space:
* the set of charts, which is data
* the fact that changes of charts belong to some group (in fact groupoid), which is additional Prop.

We separate these two parts in the definition: the charted space structure is just the set of
charts, and then the different smoothness requirements (smooth manifold, orientable manifold,
contact manifold, and so on) are additional properties of these charts. These properties are
formalized through the notion of structure groupoid, i.e., a set of local homeomorphisms stable
under composition and inverse, to which the change of coordinates should belong.

## Main definitions

* `structure_groupoid H` : a subset of local homeomorphisms of `H` stable under composition,
  inverse and restriction (ex: local diffeos).
* `continuous_groupoid H` : the groupoid of all local homeomorphisms of `H`
* `charted_space H M` : charted space structure on `M` modelled on `H`, given by an atlas of
  local homeomorphisms from `M` to `H` whose sources cover `M`. This is a type class.
* `has_groupoid M G` : when `G` is a structure groupoid on `H` and `M` is a charted space
  modelled on `H`, require that all coordinate changes belong to `G`. This is a type class.
* `atlas H M` : when `M` is a charted space modelled on `H`, the atlas of this charted
  space structure, i.e., the set of charts.
* `G.maximal_atlas M` : when `M` is a charted space modelled on `H` and admitting `G` as a
  structure groupoid, one can consider all the local homeomorphisms from `M` to `H` such that
  changing coordinate from any chart to them belongs to `G`. This is a larger atlas, called the
  maximal atlas (for the groupoid `G`).
* `structomorph G M M'` : the type of diffeomorphisms between the charted spaces `M` and `M'` for
  the groupoid `G`. We avoid the word diffeomorphism, keeping it for the smooth category.

As a basic example, we give the instance
`instance charted_space_model_space (H : Type*) [topological_space H] : charted_space H H`
saying that a topological space is a charted space over itself, with the identity as unique chart.
This charted space structure is compatible with any groupoid.

Additional useful definitions:

* `pregroupoid H` : a subset of local mas of `H` stable under composition and
  restriction, but not inverse (ex: smooth maps)
* `groupoid_of_pregroupoid` : construct a groupoid from a pregroupoid, by requiring that a map and
  its inverse both belong to the pregroupoid (ex: construct diffeos from smooth maps)
* `chart_at H x` is a preferred chart at `x : M` when `M` has a charted space structure modelled on
  `H`.
* `G.compatible he he'` states that, for any two charts `e` and `e'` in the atlas, the composition
  of `e.symm` and `e'` belongs to the groupoid `G` when `M` admits `G` as a structure groupoid.
* `G.compatible_of_mem_maximal_atlas he he'` states that, for any two charts `e` and `e'` in the
  maximal atlas associated to the groupoid `G`, the composition of `e.symm` and `e'` belongs to the
  `G` if `M` admits `G` as a structure groupoid.
* `charted_space_core.to_charted_space`: consider a space without a topology, but endowed with a set
  of charts (which are local equivs) for which the change of coordinates are local homeos. Then
  one can construct a topology on the space for which the charts become local homeos, defining
  a genuine charted space structure.

## Implementation notes

The atlas in a charted space is *not* a maximal atlas in general: the notion of maximality depends
on the groupoid one considers, and changing groupoids changes the maximal atlas. With the current
formalization, it makes sense first to choose the atlas, and then to ask whether this precise atlas
defines a smooth manifold, an orientable manifold, and so on. A consequence is that structomorphisms
between `M` and `M'` do *not* induce a bijection between the atlases of `M` and `M'`: the
definition is only that, read in charts, the structomorphism locally belongs to the groupoid under
consideration. (This is equivalent to inducing a bijection between elements of the maximal atlas).
A consequence is that the invariance under structomorphisms of properties defined in terms of the
atlas is not obvious in general, and could require some work in theory (amounting to the fact
that these properties only depend on the maximal atlas, for instance). In practice, this does not
create any real difficulty.

We use the letter `H` for the model space thinking of the case of manifolds with boundary, where the
model space is a half space.

Manifolds are sometimes defined as topological spaces with an atlas of local diffeomorphisms, and
sometimes as spaces with an atlas from which a topology is deduced. We use the former approach:
otherwise, there would be an instance from manifolds to topological spaces, which means that any
instance search for topological spaces would try to find manifold structures involving a yet
unknown model space, leading to problems. However, we also introduce the latter approach,
through a structure `charted_space_core` making it possible to construct a topology out of a set of
local equivs with compatibility conditions (but we do not register it as an instance).

In the definition of a charted space, the model space is written as an explicit parameter as there
can be several model spaces for a given topological space. For instance, a complex manifold
(modelled over `‚ÑÇ^n`) will also be seen sometimes as a real manifold modelled over `‚Ñù^(2n)`.

## Notations

In the locale `manifold`, we denote the composition of local homeomorphisms with `‚â´‚Çï`, and the
composition of local equivs with `‚â´`.
-/

noncomputable theory
open_locale classical topological_space
open filter
universes u

variables {H : Type u} {H' : Type*} {M : Type*} {M' : Type*} {M'' : Type*}

/- Notational shortcut for the composition of local homeomorphisms and local equivs, i.e.,
`local_homeomorph.trans` and `local_equiv.trans`.
Note that, as is usual for equivs, the composition is from left to right, hence the direction of
the arrow. -/
localized "infixr  ` ‚â´‚Çï `:100 := local_homeomorph.trans" in manifold
localized "infixr  ` ‚â´ `:100 := local_equiv.trans" in manifold

/- `simp` looks for subsingleton instances at every call. This turns out to be very
inefficient, especially in `simp`-heavy parts of the library such as the manifold code.
Disable two such instances to speed up things.
NB: this is just a hack. TODO: fix `simp` properly. -/
localized "attribute [-instance] unique.subsingleton pi.subsingleton" in manifold

open set local_homeomorph

/-! ### Structure groupoids-/

section groupoid

/-! One could add to the definition of a structure groupoid the fact that the restriction of an
element of the groupoid to any open set still belongs to the groupoid.
(This is in Kobayashi-Nomizu.)
I am not sure I want this, for instance on `H √ó E` where `E` is a vector space, and the groupoid is
made of functions respecting the fibers and linear in the fibers (so that a charted space over this
groupoid is naturally a vector bundle) I prefer that the members of the groupoid are always
defined on sets of the form `s √ó E`.  There is a typeclass `closed_under_restriction` for groupoids
which have the restriction property.

The only nontrivial requirement is locality: if a local homeomorphism belongs to the groupoid
around each point in its domain of definition, then it belongs to the groupoid. Without this
requirement, the composition of structomorphisms does not have to be a structomorphism. Note that
this implies that a local homeomorphism with empty source belongs to any structure groupoid, as
it trivially satisfies this condition.

There is also a technical point, related to the fact that a local homeomorphism is by definition a
global map which is a homeomorphism when restricted to its source subset (and its values outside
of the source are not relevant). Therefore, we also require that being a member of the groupoid only
depends on the values on the source.

We use primes in the structure names as we will reformulate them below (without primes) using a
`has_mem` instance, writing `e ‚àà G` instead of `e ‚àà G.members`.
-/
/-- A structure groupoid is a set of local homeomorphisms of a topological space stable under
composition and inverse. They appear in the definition of the smoothness class of a manifold. -/
structure structure_groupoid (H : Type u) [topological_space H] :=
(members       : set (local_homeomorph H H))
(trans'        : ‚àÄe e' : local_homeomorph H H, e ‚àà members ‚Üí e' ‚àà members ‚Üí e ‚â´‚Çï e' ‚àà members)
(symm'         : ‚àÄe : local_homeomorph H H, e ‚àà members ‚Üí e.symm ‚àà members)
(id_mem'       : local_homeomorph.refl H ‚àà members)
(locality'     : ‚àÄe : local_homeomorph H H, (‚àÄx ‚àà e.source, ‚àÉs, is_open s ‚àß
                  x ‚àà s ‚àß e.restr s ‚àà members) ‚Üí e ‚àà members)
(eq_on_source' : ‚àÄ e e' : local_homeomorph H H, e ‚àà members ‚Üí e' ‚âà e ‚Üí e' ‚àà members)

variable [topological_space H]

instance : has_mem (local_homeomorph H H) (structure_groupoid H) :=
‚ü®Œª(e : local_homeomorph H H) (G : structure_groupoid H), e ‚àà G.members‚ü©

lemma structure_groupoid.trans (G : structure_groupoid H) {e e' : local_homeomorph H H}
  (he : e ‚àà G) (he' : e' ‚àà G) : e ‚â´‚Çï e' ‚àà G :=
G.trans' e e' he he'

lemma structure_groupoid.symm (G : structure_groupoid H) {e : local_homeomorph H H} (he : e ‚àà G) :
  e.symm ‚àà G :=
G.symm' e he

lemma structure_groupoid.id_mem (G : structure_groupoid H) :
  local_homeomorph.refl H ‚àà G :=
G.id_mem'

lemma structure_groupoid.locality (G : structure_groupoid H) {e : local_homeomorph H H}
  (h : ‚àÄx ‚àà e.source, ‚àÉs, is_open s ‚àß x ‚àà s ‚àß e.restr s ‚àà G) :
  e ‚àà G :=
G.locality' e h

lemma structure_groupoid.eq_on_source (G : structure_groupoid H) {e e' : local_homeomorph H H}
  (he : e ‚àà G) (h : e' ‚âà e) : e' ‚àà G :=
G.eq_on_source' e e' he h

/-- Partial order on the set of groupoids, given by inclusion of the members of the groupoid -/
instance structure_groupoid.partial_order : partial_order (structure_groupoid H) :=
partial_order.lift structure_groupoid.members
(Œªa b h, by { cases a, cases b, dsimp at h, induction h, refl })

lemma structure_groupoid.le_iff {G‚ÇÅ G‚ÇÇ : structure_groupoid H} :
  G‚ÇÅ ‚â§ G‚ÇÇ ‚Üî ‚àÄ e, e ‚àà G‚ÇÅ ‚Üí e ‚àà G‚ÇÇ :=
iff.rfl

/-- The trivial groupoid, containing only the identity (and maps with empty source, as this is
necessary from the definition) -/
def id_groupoid (H : Type u) [topological_space H] : structure_groupoid H :=
{ members := {local_homeomorph.refl H} ‚à™ {e : local_homeomorph H H | e.source = ‚àÖ},
  trans' := Œªe e' he he', begin
    cases he; simp at he he',
    { simpa only [he, refl_trans]},
    { have : (e ‚â´‚Çï e').source ‚äÜ e.source := sep_subset _ _,
      rw he at this,
      have : (e ‚â´‚Çï e') ‚àà {e : local_homeomorph H H | e.source = ‚àÖ} := disjoint_iff.1 this,
      exact (mem_union _ _ _).2 (or.inr this) },
  end,
  symm' := Œªe he, begin
    cases (mem_union _ _ _).1 he with E E,
    { finish },
    { right,
      simpa only [e.to_local_equiv.image_source_eq_target.symm] with mfld_simps using E},
  end,
  id_mem' := mem_union_left _ rfl,
  locality' := Œªe he, begin
    cases e.source.eq_empty_or_nonempty with h h,
    { right, exact h },
    { left,
      rcases h with ‚ü®x, hx‚ü©,
      rcases he x hx with ‚ü®s, open_s, xs, hs‚ü©,
      have x's : x ‚àà (e.restr s).source,
      { rw [restr_source, open_s.interior_eq],
        exact ‚ü®hx, xs‚ü© },
      cases hs,
      { replace hs : local_homeomorph.restr e s = local_homeomorph.refl H,
          by simpa only using hs,
        have : (e.restr s).source = univ, by { rw hs, simp },
        change (e.to_local_equiv).source ‚à© interior s = univ at this,
        have : univ ‚äÜ interior s, by { rw ‚Üê this, exact inter_subset_right _ _ },
        have : s = univ, by rwa [open_s.interior_eq, univ_subset_iff] at this,
        simpa only [this, restr_univ] using hs },
      { exfalso,
        rw mem_set_of_eq at hs,
        rwa hs at x's } },
  end,
  eq_on_source' := Œªe e' he he'e, begin
    cases he,
    { left,
      have : e = e',
      { refine eq_of_eq_on_source_univ (setoid.symm he'e) _ _;
        rw set.mem_singleton_iff.1 he ; refl },
      rwa ‚Üê this },
    { right,
      change (e.to_local_equiv).source = ‚àÖ at he,
      rwa [set.mem_set_of_eq, he'e.source_eq] }
  end }

/-- Every structure groupoid contains the identity groupoid -/
instance : order_bot (structure_groupoid H) :=
{ bot    := id_groupoid H,
  bot_le := begin
    assume u f hf,
    change f ‚àà {local_homeomorph.refl H} ‚à™ {e : local_homeomorph H H | e.source = ‚àÖ} at hf,
    simp only [singleton_union, mem_set_of_eq, mem_insert_iff] at hf,
    cases hf,
    { rw hf,
      apply u.id_mem },
    { apply u.locality,
      assume x hx,
      rw [hf, mem_empty_eq] at hx,
      exact hx.elim }
  end,
  ..structure_groupoid.partial_order }

instance (H : Type u) [topological_space H] : inhabited (structure_groupoid H) :=
‚ü®id_groupoid H‚ü©

/-- To construct a groupoid, one may consider classes of local homeos such that both the function
and its inverse have some property. If this property is stable under composition,
one gets a groupoid. `pregroupoid` bundles the properties needed for this construction, with the
groupoid of smooth functions with smooth inverses as an application. -/
structure pregroupoid (H : Type*) [topological_space H] :=
(property : (H ‚Üí H) ‚Üí (set H) ‚Üí Prop)
(comp     : ‚àÄ{f g u v}, property f u ‚Üí property g v ‚Üí is_open u ‚Üí is_open v ‚Üí is_open (u ‚à© f ‚Åª¬π' v)
              ‚Üí property (g ‚àò f) (u ‚à© f ‚Åª¬π' v))
(id_mem   : property id univ)
(locality : ‚àÄ{f u}, is_open u ‚Üí (‚àÄx‚ààu, ‚àÉv, is_open v ‚àß x ‚àà v ‚àß property f (u ‚à© v)) ‚Üí property f u)
(congr    : ‚àÄ{f g : H ‚Üí H} {u}, is_open u ‚Üí (‚àÄx‚ààu, g x = f x) ‚Üí property f u ‚Üí property g u)

/-- Construct a groupoid of local homeos for which the map and its inverse have some property,
from a pregroupoid asserting that this property is stable under composition. -/
def pregroupoid.groupoid (PG : pregroupoid H) : structure_groupoid H :=
{ members   := {e : local_homeomorph H H | PG.property e e.source ‚àß PG.property e.symm e.target},
  trans'     := Œªe e' he he', begin
    split,
    { apply PG.comp he.1 he'.1 e.open_source e'.open_source,
      apply e.continuous_to_fun.preimage_open_of_open e.open_source e'.open_source },
    { apply PG.comp he'.2 he.2 e'.open_target e.open_target,
      apply e'.continuous_inv_fun.preimage_open_of_open e'.open_target e.open_target }
  end,
  symm'     := Œªe he, ‚ü®he.2, he.1‚ü©,
  id_mem'   := ‚ü®PG.id_mem, PG.id_mem‚ü©,
  locality' := Œªe he, begin
    split,
    { apply PG.locality e.open_source (Œªx xu, _),
      rcases he x xu with ‚ü®s, s_open, xs, hs‚ü©,
      refine ‚ü®s, s_open, xs, _‚ü©,
      convert hs.1 using 1,
      dsimp [local_homeomorph.restr], rw s_open.interior_eq },
    { apply PG.locality e.open_target (Œªx xu, _),
      rcases he (e.symm x) (e.map_target xu) with ‚ü®s, s_open, xs, hs‚ü©,
      refine ‚ü®e.target ‚à© e.symm ‚Åª¬π' s, _, ‚ü®xu, xs‚ü©, _‚ü©,
      { exact continuous_on.preimage_open_of_open e.continuous_inv_fun e.open_target s_open },
      { rw [‚Üê inter_assoc, inter_self],
        convert hs.2 using 1,
        dsimp [local_homeomorph.restr], rw s_open.interior_eq } },
  end,
  eq_on_source' := Œªe e' he ee', begin
    split,
    { apply PG.congr e'.open_source ee'.2,
      simp only [ee'.1, he.1] },
    { have A := ee'.symm',
      apply PG.congr e'.symm.open_source A.2,
      convert he.2,
      rw A.1,
      refl }
  end }

lemma mem_groupoid_of_pregroupoid {PG : pregroupoid H} {e : local_homeomorph H H} :
  e ‚àà PG.groupoid ‚Üî PG.property e e.source ‚àß PG.property e.symm e.target :=
iff.rfl

lemma groupoid_of_pregroupoid_le (PG‚ÇÅ PG‚ÇÇ : pregroupoid H)
  (h : ‚àÄf s, PG‚ÇÅ.property f s ‚Üí PG‚ÇÇ.property f s) : PG‚ÇÅ.groupoid ‚â§ PG‚ÇÇ.groupoid :=
begin
  refine structure_groupoid.le_iff.2 (Œª e he, _),
  rw mem_groupoid_of_pregroupoid at he ‚ä¢,
  exact ‚ü®h _ _ he.1, h _ _ he.2‚ü©
end

lemma mem_pregroupoid_of_eq_on_source (PG : pregroupoid H) {e e' : local_homeomorph H H}
  (he' : e ‚âà e') (he : PG.property e e.source) : PG.property e' e'.source :=
begin
  rw ‚Üê he'.1,
  exact PG.congr e.open_source he'.eq_on.symm he,
end

/-- The pregroupoid of all local maps on a topological space `H` -/
@[reducible] def continuous_pregroupoid (H : Type*) [topological_space H] : pregroupoid H :=
{ property := Œªf s, true,
  comp     := Œªf g u v hf hg hu hv huv, trivial,
  id_mem   := trivial,
  locality := Œªf u u_open h, trivial,
  congr    := Œªf g u u_open hcongr hf, trivial }

instance (H : Type*) [topological_space H] : inhabited (pregroupoid H) :=
‚ü®continuous_pregroupoid H‚ü©

/-- The groupoid of all local homeomorphisms on a topological space `H` -/
def continuous_groupoid (H : Type*) [topological_space H] : structure_groupoid H :=
pregroupoid.groupoid (continuous_pregroupoid H)

/-- Every structure groupoid is contained in the groupoid of all local homeomorphisms -/
instance : order_top (structure_groupoid H) :=
{ top    := continuous_groupoid H,
  le_top := Œª u f hf, by { split; exact dec_trivial },
  ..structure_groupoid.partial_order }

/-- A groupoid is closed under restriction if it contains all restrictions of its element local
homeomorphisms to open subsets of the source. -/
class closed_under_restriction (G : structure_groupoid H) : Prop :=
(closed_under_restriction : ‚àÄ {e : local_homeomorph H H}, e ‚àà G ‚Üí ‚àÄ (s : set H), is_open s ‚Üí
  e.restr s ‚àà G)

lemma closed_under_restriction' {G : structure_groupoid H} [closed_under_restriction G]
  {e : local_homeomorph H H} (he : e ‚àà G) {s : set H} (hs : is_open s) :
  e.restr s ‚àà G :=
closed_under_restriction.closed_under_restriction he s hs

/-- The trivial restriction-closed groupoid, containing only local homeomorphisms equivalent to the
restriction of the identity to the various open subsets. -/
def id_restr_groupoid : structure_groupoid H :=
{ members := {e | ‚àÉ {s : set H} (h : is_open s), e ‚âà local_homeomorph.of_set s h},
  trans' := begin
    rintros e e' ‚ü®s, hs, hse‚ü© ‚ü®s', hs', hse'‚ü©,
    refine ‚ü®s ‚à© s', is_open.inter hs hs', _‚ü©,
    have := local_homeomorph.eq_on_source.trans' hse hse',
    rwa local_homeomorph.of_set_trans_of_set at this,
  end,
  symm' := begin
    rintros e ‚ü®s, hs, hse‚ü©,
    refine ‚ü®s, hs, _‚ü©,
    rw [‚Üê of_set_symm],
    exact local_homeomorph.eq_on_source.symm' hse,
  end,
  id_mem' := ‚ü®univ, is_open_univ, by simp only with mfld_simps‚ü©,
  locality' := begin
    intros e h,
    refine ‚ü®e.source, e.open_source, by simp only with mfld_simps, _‚ü©,
    intros x hx,
    rcases h x hx with ‚ü®s, hs, hxs, s', hs', hes'‚ü©,
    have hes : x ‚àà (e.restr s).source,
    { rw e.restr_source, refine ‚ü®hx, _‚ü©,
      rw hs.interior_eq, exact hxs },
    simpa only with mfld_simps using local_homeomorph.eq_on_source.eq_on hes' hes,
  end,
  eq_on_source' := begin
    rintros e e' ‚ü®s, hs, hse‚ü© hee',
    exact ‚ü®s, hs, setoid.trans hee' hse‚ü©,
  end
}

lemma id_restr_groupoid_mem {s : set H} (hs : is_open s) :
  of_set s hs ‚àà @id_restr_groupoid H _ := ‚ü®s, hs, by refl‚ü©

/-- The trivial restriction-closed groupoid is indeed `closed_under_restriction`. -/
instance closed_under_restriction_id_restr_groupoid :
  closed_under_restriction (@id_restr_groupoid H _) :=
‚ü® begin
    rintros e ‚ü®s', hs', he‚ü© s hs,
    use [s' ‚à© s, is_open.inter hs' hs],
    refine setoid.trans (local_homeomorph.eq_on_source.restr he s) _,
    exact ‚ü®by simp only [hs.interior_eq] with mfld_simps, by simp only with mfld_simps‚ü©,
  end ‚ü©

/-- A groupoid is closed under restriction if and only if it contains the trivial restriction-closed
groupoid. -/
lemma closed_under_restriction_iff_id_le (G : structure_groupoid H) :
  closed_under_restriction G ‚Üî id_restr_groupoid ‚â§ G :=
begin
  split,
  { introsI _i,
    apply structure_groupoid.le_iff.mpr,
    rintros e ‚ü®s, hs, hes‚ü©,
    refine G.eq_on_source _ hes,
    convert closed_under_restriction' G.id_mem hs,
    change s = _ ‚à© _,
    rw hs.interior_eq,
    simp only with mfld_simps },
  { intros h,
    split,
    intros e he s hs,
    rw ‚Üê of_set_trans (e : local_homeomorph H H) hs,
    refine G.trans _ he,
    apply structure_groupoid.le_iff.mp h,
    exact id_restr_groupoid_mem hs },
end

/-- The groupoid of all local homeomorphisms on a topological space `H` is closed under restriction.
-/
instance : closed_under_restriction (continuous_groupoid H) :=
(closed_under_restriction_iff_id_le _).mpr (by convert le_top)

end groupoid


/-! ### Charted spaces -/
/-- A charted space is a topological space endowed with an atlas, i.e., a set of local
homeomorphisms taking value in a model space `H`, called charts, such that the domains of the charts
cover the whole space. We express the covering property by chosing for each `x` a member
`chart_at H x` of the atlas containing `x` in its source: in the smooth case, this is convenient to
construct the tangent bundle in an efficient way.
The model space is written as an explicit parameter as there can be several model spaces for a
given topological space. For instance, a complex manifold (modelled over `‚ÑÇ^n`) will also be seen
sometimes as a real manifold over `‚Ñù^(2n)`.
-/
class charted_space (H : Type*) [topological_space H] (M : Type*) [topological_space M] :=
(atlas []            : set (local_homeomorph M H))
(chart_at []         : M ‚Üí local_homeomorph M H)
(mem_chart_source [] : ‚àÄx, x ‚àà (chart_at x).source)
(chart_mem_atlas []  : ‚àÄx, chart_at x ‚àà atlas)

export charted_space
attribute [simp, mfld_simps] mem_chart_source chart_mem_atlas

section charted_space

/-- Any space is a charted_space modelled over itself, by just using the identity chart -/
instance charted_space_self (H : Type*) [topological_space H] : charted_space H H :=
{ atlas            := {local_homeomorph.refl H},
  chart_at         := Œªx, local_homeomorph.refl H,
  mem_chart_source := Œªx, mem_univ x,
  chart_mem_atlas  := Œªx, mem_singleton _ }

/-- In the trivial charted_space structure of a space modelled over itself through the identity, the
atlas members are just the identity -/
@[simp, mfld_simps] lemma charted_space_self_atlas
  {H : Type*} [topological_space H] {e : local_homeomorph H H} :
  e ‚àà atlas H H ‚Üî e = local_homeomorph.refl H :=
by simp [atlas, charted_space.atlas]

/-- In the model space, chart_at is always the identity -/
lemma chart_at_self_eq {H : Type*} [topological_space H] {x : H} :
  chart_at H x = local_homeomorph.refl H :=
by simpa using chart_mem_atlas H x

section

variables (H) [topological_space H] [topological_space M] [charted_space H M]

lemma mem_chart_target (x : M) : chart_at H x x ‚àà (chart_at H x).target :=
(chart_at H x).map_source (mem_chart_source _ _)

/-- If a topological space admits an atlas with locally compact charts, then the space itself
is locally compact. -/
lemma charted_space.locally_compact [locally_compact_space H] : locally_compact_space M :=
begin
  have : ‚àÄ (x : M), (ùìù x).has_basis
      (Œª s, s ‚àà ùìù (chart_at H x x) ‚àß is_compact s ‚àß s ‚äÜ (chart_at H x).target)
      (Œª s, (chart_at H x).symm '' s),
  { intro x,
    rw [‚Üê (chart_at H x).symm_map_nhds_eq (mem_chart_source H x)],
    exact ((compact_basis_nhds (chart_at H x x)).has_basis_self_subset
      (is_open.mem_nhds (chart_at H x).open_target (mem_chart_target H x))).map _ },
  refine locally_compact_space_of_has_basis this _,
  rintro x s ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©,
  exact h‚ÇÇ.image_of_continuous_on ((chart_at H x).continuous_on_symm.mono h‚ÇÉ)
end

open topological_space

lemma charted_space.second_countable_of_countable_cover [second_countable_topology H]
  {s : set M} (hs : (‚ãÉ x (hx : x ‚àà s), (chart_at H x).source) = univ)
  (hsc : countable s) :
  second_countable_topology M :=
begin
  haveI : ‚àÄ x : M, second_countable_topology (chart_at H x).source :=
    Œª x, (chart_at H x).second_countable_topology_source,
  haveI := hsc.to_encodable,
  rw bUnion_eq_Union at hs,
  exact second_countable_topology_of_countable_cover (Œª x : s, (chart_at H (x : M)).open_source) hs
end

lemma charted_space.second_countable_of_sigma_compact [second_countable_topology H]
  [sigma_compact_space M] :
  second_countable_topology M :=
begin
  obtain ‚ü®s, hsc, hsU‚ü© : ‚àÉ s, countable s ‚àß (‚ãÉ x (hx : x ‚àà s), (chart_at H x).source) = univ :=
    countable_cover_nhds_of_sigma_compact
      (Œª x : M, is_open.mem_nhds (chart_at H x).open_source (mem_chart_source H x)),
  exact charted_space.second_countable_of_countable_cover H hsU hsc
end

end

/-- For technical reasons we introduce two type tags:

* `model_prod H H'` is the same as `H √ó H'`;
* `model_pi H` is the same as `Œ† i, H i`, where `H : Œπ ‚Üí Type*` and `Œπ` is a finite type.

In both cases the reason is the same, so we explain it only in the case of the product. A charted
space `M` with model `H` is a set of local charts from `M` to `H` covering the space. Every space is
registered as a charted space over itself, using the only chart `id`, in `manifold_model_space`. You
can also define a product of charted space `M` and `M'` (with model space `H √ó H'`) by taking the
products of the charts. Now, on `H √ó H'`, there are two charted space structures with model space
`H √ó H'` itself, the one coming from `manifold_model_space`, and the one coming from the product of
the two `manifold_model_space` on each component. They are equal, but not defeq (because the product
of `id` and `id` is not defeq to `id`), which is bad as we know. This expedient of renaming `H √ó H'`
solves this problem. -/
library_note "Manifold type tags"

/-- Same thing as `H √ó H'` We introduce it for technical reasons,
see note [Manifold type tags]. -/
def model_prod (H : Type*) (H' : Type*) := H √ó H'

/-- Same thing as `Œ† i, H i` We introduce it for technical reasons,
see note [Manifold type tags]. -/
def model_pi {Œπ : Type*} (H : Œπ ‚Üí Type*) := Œ† i, H i

section
local attribute [reducible] model_prod

instance model_prod_inhabited [inhabited H] [inhabited H'] :
  inhabited (model_prod H H') :=
prod.inhabited

instance (H : Type*) [topological_space H] (H' : Type*) [topological_space H'] :
  topological_space (model_prod H H') :=
prod.topological_space

/- Next lemma shows up often when dealing with derivatives, register it as simp. -/
@[simp, mfld_simps] lemma model_prod_range_prod_id
  {H : Type*} {H' : Type*} {Œ± : Type*} (f : H ‚Üí Œ±) :
  range (Œª (p : model_prod H H'), (f p.1, p.2)) = set.prod (range f) univ :=
by rw prod_range_univ_eq

end

section

variables {Œπ : Type*} {Hi : Œπ ‚Üí Type*}

instance model_pi_inhabited [Œ† i, inhabited (Hi i)] :
  inhabited (model_pi Hi) :=
pi.inhabited _

instance [Œ† i, topological_space (Hi i)] :
  topological_space (model_pi Hi) :=
Pi.topological_space

end

/-- The product of two charted spaces is naturally a charted space, with the canonical
construction of the atlas of product maps. -/
instance prod_charted_space (H : Type*) [topological_space H]
  (M : Type*) [topological_space M] [charted_space H M]
  (H' : Type*) [topological_space H']
  (M' : Type*) [topological_space M'] [charted_space H' M'] :
  charted_space (model_prod H H') (M √ó M') :=
{ atlas            := image2 local_homeomorph.prod (atlas H M) (atlas H' M'),
  chart_at         := Œª x : M √ó M', (chart_at H x.1).prod (chart_at H' x.2),
  mem_chart_source := Œª x, ‚ü®mem_chart_source _ _, mem_chart_source _ _‚ü©,
  chart_mem_atlas  := Œª x, mem_image2_of_mem (chart_mem_atlas _ _) (chart_mem_atlas _ _) }

section prod_charted_space

variables [topological_space H] [topological_space M] [charted_space H M]
[topological_space H'] [topological_space M'] [charted_space H' M'] {x : M√óM'}

@[simp, mfld_simps] lemma prod_charted_space_chart_at :
  (chart_at (model_prod H H') x) = (chart_at H x.fst).prod (chart_at H' x.snd) := rfl

end prod_charted_space

/-- The product of a finite family of charted spaces is naturally a charted space, with the
canonical construction of the atlas of finite product maps. -/
instance pi_charted_space {Œπ : Type*} [fintype Œπ] (H : Œπ ‚Üí Type*) [Œ† i, topological_space (H i)]
  (M : Œπ ‚Üí Type*) [Œ† i, topological_space (M i)] [Œ† i, charted_space (H i) (M i)] :
  charted_space (model_pi H) (Œ† i, M i) :=
{ atlas := local_homeomorph.pi '' (set.pi univ $ Œª i, atlas (H i) (M i)),
  chart_at := Œª f, local_homeomorph.pi $ Œª i, chart_at (H i) (f i),
  mem_chart_source := Œª f i hi, mem_chart_source (H i) (f i),
  chart_mem_atlas := Œª f, mem_image_of_mem _ $ Œª i hi, chart_mem_atlas (H i) (f i) }

@[simp, mfld_simps] lemma pi_charted_space_chart_at {Œπ : Type*} [fintype Œπ] (H : Œπ ‚Üí Type*)
  [Œ† i, topological_space (H i)] (M : Œπ ‚Üí Type*) [Œ† i, topological_space (M i)]
  [Œ† i, charted_space (H i) (M i)] (f : Œ† i, M i) :
  chart_at (model_pi H) f = local_homeomorph.pi (Œª i, chart_at (H i) (f i)) := rfl

end charted_space

/-! ### Constructing a topology from an atlas -/

/-- Sometimes, one may want to construct a charted space structure on a space which does not yet
have a topological structure, where the topology would come from the charts. For this, one needs
charts that are only local equivs, and continuity properties for their composition.
This is formalised in `charted_space_core`. -/
@[nolint has_inhabited_instance]
structure charted_space_core (H : Type*) [topological_space H] (M : Type*) :=
(atlas            : set (local_equiv M H))
(chart_at         : M ‚Üí local_equiv M H)
(mem_chart_source : ‚àÄx, x ‚àà (chart_at x).source)
(chart_mem_atlas  : ‚àÄx, chart_at x ‚àà atlas)
(open_source : ‚àÄe e' : local_equiv M H, e ‚àà atlas ‚Üí e' ‚àà atlas ‚Üí is_open (e.symm.trans e').source)
(continuous_to_fun : ‚àÄe e' : local_equiv M H, e ‚àà atlas ‚Üí e' ‚àà atlas ‚Üí
                       continuous_on (e.symm.trans e') (e.symm.trans e').source)

namespace charted_space_core

variables [topological_space H] (c : charted_space_core H M) {e : local_equiv M H}

/-- Topology generated by a set of charts on a Type. -/
protected def to_topological_space : topological_space M :=
topological_space.generate_from $ ‚ãÉ (e : local_equiv M H) (he : e ‚àà c.atlas)
  (s : set H) (s_open : is_open s), {e ‚Åª¬π' s ‚à© e.source}

lemma open_source' (he : e ‚àà c.atlas) : @is_open M c.to_topological_space e.source :=
begin
  apply topological_space.generate_open.basic,
  simp only [exists_prop, mem_Union, mem_singleton_iff],
  refine ‚ü®e, he, univ, is_open_univ, _‚ü©,
  simp only [set.univ_inter, set.preimage_univ]
end

lemma open_target (he : e ‚àà c.atlas) : is_open e.target :=
begin
  have E : e.target ‚à© e.symm ‚Åª¬π' e.source = e.target :=
  subset.antisymm (inter_subset_left _ _) (Œªx hx, ‚ü®hx,
    local_equiv.target_subset_preimage_source _ hx‚ü©),
  simpa [local_equiv.trans_source, E] using c.open_source e e he he
end

/-- An element of the atlas in a charted space without topology becomes a local homeomorphism
for the topology constructed from this atlas. The `local_homeomorph` version is given in this
definition. -/
protected def local_homeomorph (e : local_equiv M H) (he : e ‚àà c.atlas) :
  @local_homeomorph M H c.to_topological_space _ :=
{ open_source := by convert c.open_source' he,
  open_target := by convert c.open_target he,
  continuous_to_fun := begin
    letI : topological_space M := c.to_topological_space,
    rw continuous_on_open_iff (c.open_source' he),
    assume s s_open,
    rw inter_comm,
    apply topological_space.generate_open.basic,
    simp only [exists_prop, mem_Union, mem_singleton_iff],
    exact ‚ü®e, he, ‚ü®s, s_open, rfl‚ü©‚ü©
  end,
  continuous_inv_fun := begin
    letI : topological_space M := c.to_topological_space,
    apply continuous_on_open_of_generate_from (c.open_target he),
    assume t ht,
    simp only [exists_prop, mem_Union, mem_singleton_iff] at ht,
    rcases ht with ‚ü®e', e'_atlas, s, s_open, ts‚ü©,
    rw ts,
    let f := e.symm.trans e',
    have : is_open (f ‚Åª¬π' s ‚à© f.source),
      by simpa [inter_comm] using (continuous_on_open_iff (c.open_source e e' he e'_atlas)).1
        (c.continuous_to_fun e e' he e'_atlas) s s_open,
    have A : e' ‚àò e.symm ‚Åª¬π' s ‚à© (e.target ‚à© e.symm ‚Åª¬π' e'.source) =
             e.target ‚à© (e' ‚àò e.symm ‚Åª¬π' s ‚à© e.symm ‚Åª¬π' e'.source),
      by { rw [‚Üê inter_assoc, ‚Üê inter_assoc], congr' 1, exact inter_comm _ _ },
    simpa [local_equiv.trans_source, preimage_inter, preimage_comp.symm, A] using this
  end,
  ..e }

/-- Given a charted space without topology, endow it with a genuine charted space structure with
respect to the topology constructed from the atlas. -/
def to_charted_space : @charted_space H _ M c.to_topological_space :=
{ atlas := ‚ãÉ (e : local_equiv M H) (he : e ‚àà c.atlas), {c.local_homeomorph e he},
  chart_at := Œªx, c.local_homeomorph (c.chart_at x) (c.chart_mem_atlas x),
  mem_chart_source := Œªx, c.mem_chart_source x,
  chart_mem_atlas := Œªx, begin
    simp only [mem_Union, mem_singleton_iff],
    exact ‚ü®c.chart_at x, c.chart_mem_atlas x, rfl‚ü©,
  end }

end charted_space_core


/-! ### Charted space with a given structure groupoid -/

section has_groupoid
variables [topological_space H] [topological_space M] [charted_space H M]

/-- A charted space has an atlas in a groupoid `G` if the change of coordinates belong to the
groupoid -/
class has_groupoid {H : Type*} [topological_space H] (M : Type*) [topological_space M]
  [charted_space H M] (G : structure_groupoid H) : Prop :=
(compatible [] : ‚àÄ{e e' : local_homeomorph M H}, e ‚àà atlas H M ‚Üí e' ‚àà atlas H M ‚Üí e.symm ‚â´‚Çï e' ‚àà G)

/-- Reformulate in the `structure_groupoid` namespace the compatibility condition of charts in a
charted space admitting a structure groupoid, to make it more easily accessible with dot
notation. -/
lemma structure_groupoid.compatible {H : Type*} [topological_space H] (G : structure_groupoid H)
  {M : Type*} [topological_space M] [charted_space H M] [has_groupoid M G]
  {e e' : local_homeomorph M H} (he : e ‚àà atlas H M) (he' : e' ‚àà atlas H M) :
  e.symm ‚â´‚Çï e' ‚àà G :=
has_groupoid.compatible G he he'

lemma has_groupoid_of_le {G‚ÇÅ G‚ÇÇ : structure_groupoid H} (h : has_groupoid M G‚ÇÅ) (hle : G‚ÇÅ ‚â§ G‚ÇÇ) :
  has_groupoid M G‚ÇÇ :=
‚ü® Œª e e' he he', hle ((h.compatible : _) he he') ‚ü©

lemma has_groupoid_of_pregroupoid (PG : pregroupoid H)
  (h : ‚àÄ{e e' : local_homeomorph M H}, e ‚àà atlas H M ‚Üí e' ‚àà atlas H M
    ‚Üí PG.property (e.symm ‚â´‚Çï e') (e.symm ‚â´‚Çï e').source) :
  has_groupoid M (PG.groupoid) :=
‚ü®assume e e' he he', mem_groupoid_of_pregroupoid.mpr ‚ü®h he he', h he' he‚ü©‚ü©

/-- The trivial charted space structure on the model space is compatible with any groupoid -/
instance has_groupoid_model_space (H : Type*) [topological_space H] (G : structure_groupoid H) :
  has_groupoid H G :=
{ compatible := Œªe e' he he', begin
    replace he : e ‚àà atlas H H := he,
    replace he' : e' ‚àà atlas H H := he',
    rw charted_space_self_atlas at he he',
    simp [he, he', structure_groupoid.id_mem]
  end }

/-- Any charted space structure is compatible with the groupoid of all local homeomorphisms -/
instance has_groupoid_continuous_groupoid : has_groupoid M (continuous_groupoid H) :=
‚ü®begin
  assume e e' he he',
  rw [continuous_groupoid, mem_groupoid_of_pregroupoid],
  simp only [and_self]
end‚ü©

section maximal_atlas

variables (M) (G : structure_groupoid H)

/-- Given a charted space admitting a structure groupoid, the maximal atlas associated to this
structure groupoid is the set of all local charts that are compatible with the atlas, i.e., such
that changing coordinates with an atlas member gives an element of the groupoid. -/
def structure_groupoid.maximal_atlas : set (local_homeomorph M H) :=
{e | ‚àÄ e' ‚àà atlas H M, e.symm ‚â´‚Çï e' ‚àà G ‚àß e'.symm ‚â´‚Çï e ‚àà G}

variable {M}

/-- The elements of the atlas belong to the maximal atlas for any structure groupoid -/
lemma structure_groupoid.mem_maximal_atlas_of_mem_atlas [has_groupoid M G]
  {e : local_homeomorph M H} (he : e ‚àà atlas H M) : e ‚àà G.maximal_atlas M :=
Œª e' he', ‚ü®G.compatible he he', G.compatible he' he‚ü©

lemma structure_groupoid.chart_mem_maximal_atlas [has_groupoid M G]
  (x : M) : chart_at H x ‚àà G.maximal_atlas M :=
G.mem_maximal_atlas_of_mem_atlas (chart_mem_atlas H x)

variable {G}

lemma mem_maximal_atlas_iff {e : local_homeomorph M H} :
  e ‚àà G.maximal_atlas M ‚Üî ‚àÄ e' ‚àà atlas H M, e.symm ‚â´‚Çï e' ‚àà G ‚àß e'.symm ‚â´‚Çï e ‚àà G :=
iff.rfl

/-- Changing coordinates between two elements of the maximal atlas gives rise to an element
of the structure groupoid. -/
lemma structure_groupoid.compatible_of_mem_maximal_atlas {e e' : local_homeomorph M H}
  (he : e ‚àà G.maximal_atlas M) (he' : e' ‚àà G.maximal_atlas M) : e.symm ‚â´‚Çï e' ‚àà G :=
begin
  apply G.locality (Œª x hx, _),
  set f := chart_at H (e.symm x) with hf,
  let s := e.target ‚à© (e.symm ‚Åª¬π' f.source),
  have hs : is_open s,
  { apply e.symm.continuous_to_fun.preimage_open_of_open; apply open_source },
  have xs : x ‚àà s, by { dsimp at hx, simp [s, hx] },
  refine ‚ü®s, hs, xs, _‚ü©,
  have A : e.symm ‚â´‚Çï f ‚àà G := (mem_maximal_atlas_iff.1 he f (chart_mem_atlas _ _)).1,
  have B : f.symm ‚â´‚Çï e' ‚àà G := (mem_maximal_atlas_iff.1 he' f (chart_mem_atlas _ _)).2,
  have C : (e.symm ‚â´‚Çï f) ‚â´‚Çï (f.symm ‚â´‚Çï e') ‚àà G := G.trans A B,
  have D : (e.symm ‚â´‚Çï f) ‚â´‚Çï (f.symm ‚â´‚Çï e') ‚âà (e.symm ‚â´‚Çï e').restr s := calc
    (e.symm ‚â´‚Çï f) ‚â´‚Çï (f.symm ‚â´‚Çï e') = e.symm ‚â´‚Çï (f ‚â´‚Çï f.symm) ‚â´‚Çï e' : by simp [trans_assoc]
    ... ‚âà e.symm ‚â´‚Çï (of_set f.source f.open_source) ‚â´‚Çï e' :
      by simp [eq_on_source.trans', trans_self_symm]
    ... ‚âà (e.symm ‚â´‚Çï (of_set f.source f.open_source)) ‚â´‚Çï e' : by simp [trans_assoc]
    ... ‚âà (e.symm.restr s) ‚â´‚Çï e' : by simp [s, trans_of_set']
    ... ‚âà (e.symm ‚â´‚Çï e').restr s : by simp [restr_trans],
  exact G.eq_on_source C (setoid.symm D),
end

variable (G)

/-- In the model space, the identity is in any maximal atlas. -/
lemma structure_groupoid.id_mem_maximal_atlas : local_homeomorph.refl H ‚àà G.maximal_atlas H :=
G.mem_maximal_atlas_of_mem_atlas (by simp)

end maximal_atlas

section singleton
variables {Œ± : Type*} [topological_space Œ±]

namespace local_homeomorph

variable (e : local_homeomorph Œ± H)

/-- If a single local homeomorphism `e` from a space `Œ±` into `H` has source covering the whole
space `Œ±`, then that local homeomorphism induces an `H`-charted space structure on `Œ±`.
(This condition is equivalent to `e` being an open embedding of `Œ±` into `H`; see
`open_embedding.singleton_charted_space`.) -/
def singleton_charted_space (h : e.source = set.univ) : charted_space H Œ± :=
{ atlas := {e},
  chart_at := Œª _, e,
  mem_chart_source := Œª _, by simp only [h] with mfld_simps,
  chart_mem_atlas := Œª _, by tauto }

@[simp, mfld_simps] lemma singleton_charted_space_chart_at_eq (h : e.source = set.univ) {x : Œ±} :
  @chart_at H _ Œ± _ (e.singleton_charted_space h) x = e := rfl

lemma singleton_charted_space_chart_at_source
  (h : e.source = set.univ) {x : Œ±} :
  (@chart_at H _ Œ± _ (e.singleton_charted_space h) x).source = set.univ := h

lemma singleton_charted_space_mem_atlas_eq (h : e.source = set.univ)
  (e' : local_homeomorph Œ± H) (h' : e' ‚àà (e.singleton_charted_space h).atlas) : e' = e := h'

/-- Given a local homeomorphism `e` from a space `Œ±` into `H`, if its source covers the whole
space `Œ±`, then the induced charted space structure on `Œ±` is `has_groupoid G` for any structure
groupoid `G` which is closed under restrictions. -/
lemma singleton_has_groupoid (h : e.source = set.univ) (G : structure_groupoid H)
  [closed_under_restriction G] : @has_groupoid _ _ _ _ (e.singleton_charted_space h) G :=
{ compatible := begin
    intros e' e'' he' he'',
    rw e.singleton_charted_space_mem_atlas_eq h e' he',
    rw e.singleton_charted_space_mem_atlas_eq h e'' he'',
    refine G.eq_on_source _ e.trans_symm_self,
    have hle : id_restr_groupoid ‚â§ G := (closed_under_restriction_iff_id_le G).mp (by assumption),
    exact structure_groupoid.le_iff.mp hle _ (id_restr_groupoid_mem _),
  end }

end local_homeomorph

namespace open_embedding

variable [nonempty Œ±]

/-- An open embedding of `Œ±` into `H` induces an `H`-charted space structure on `Œ±`.
See `local_homeomorph.singleton_charted_space` -/
def singleton_charted_space {f : Œ± ‚Üí H} (h : open_embedding f) :
  charted_space H Œ± := (h.to_local_homeomorph f).singleton_charted_space (by simp)

lemma singleton_charted_space_chart_at_eq {f : Œ± ‚Üí H} (h : open_embedding f) {x : Œ±} :
  ‚áë(@chart_at H _ Œ± _ (h.singleton_charted_space) x) = f := rfl

lemma singleton_has_groupoid {f : Œ± ‚Üí H} (h : open_embedding f)
  (G : structure_groupoid H) [closed_under_restriction G] :
  @has_groupoid _ _ _ _ h.singleton_charted_space G :=
(h.to_local_homeomorph f).singleton_has_groupoid (by simp) G

end open_embedding

end singleton

namespace topological_space.opens

open topological_space
variables (G : structure_groupoid H) [has_groupoid M G]
variables (s : opens M)

/-- An open subset of a charted space is naturally a charted space. -/
instance : charted_space H s :=
{ atlas := ‚ãÉ (x : s), {@local_homeomorph.subtype_restr _ _ _ _ (chart_at H x.1) s ‚ü®x‚ü©},
  chart_at := Œª x, @local_homeomorph.subtype_restr _ _ _ _ (chart_at H x.1) s ‚ü®x‚ü©,
  mem_chart_source := Œª x, by { simp only with mfld_simps, exact (mem_chart_source H x.1) },
  chart_mem_atlas := Œª x, by { simp only [mem_Union, mem_singleton_iff], use x } }

/-- If a groupoid `G` is `closed_under_restriction`, then an open subset of a space which is
`has_groupoid G` is naturally `has_groupoid G`. -/
instance [closed_under_restriction G] : has_groupoid s G :=
{ compatible := begin
    rintros e e' ‚ü®_, ‚ü®x, hc‚ü©, he‚ü© ‚ü®_, ‚ü®x', hc'‚ü©, he'‚ü©,
    haveI : nonempty s := ‚ü®x‚ü©,
    simp only [hc.symm, mem_singleton_iff, subtype.val_eq_coe] at he,
    simp only [hc'.symm, mem_singleton_iff, subtype.val_eq_coe] at he',
    rw [he, he'],
    convert G.eq_on_source _
      (subtype_restr_symm_trans_subtype_restr s (chart_at H x) (chart_at H x')),
    apply closed_under_restriction',
    { exact G.compatible (chart_mem_atlas H x) (chart_mem_atlas H x') },
    { exact preimage_open_of_open_symm (chart_at H x) s.2 },
  end }

end topological_space.opens

/-! ### Structomorphisms -/

/-- A `G`-diffeomorphism between two charted spaces is a homeomorphism which, when read in the
charts, belongs to `G`. We avoid the word diffeomorph as it is too related to the smooth category,
and use structomorph instead. -/
@[nolint has_inhabited_instance]
structure structomorph (G : structure_groupoid H) (M : Type*) (M' : Type*)
  [topological_space M] [topological_space M'] [charted_space H M] [charted_space H M']
  extends homeomorph M M' :=
(mem_groupoid : ‚àÄc : local_homeomorph M H, ‚àÄc' : local_homeomorph M' H,
  c ‚àà atlas H M ‚Üí c' ‚àà atlas H M' ‚Üí c.symm ‚â´‚Çï to_homeomorph.to_local_homeomorph ‚â´‚Çï c' ‚àà G)

variables [topological_space M'] [topological_space M'']
{G : structure_groupoid H} [charted_space H M'] [charted_space H M'']

/-- The identity is a diffeomorphism of any charted space, for any groupoid. -/
def structomorph.refl (M : Type*) [topological_space M] [charted_space H M]
  [has_groupoid M G] : structomorph G M M :=
{ mem_groupoid := Œªc c' hc hc', begin
    change (local_homeomorph.symm c) ‚â´‚Çï (local_homeomorph.refl M) ‚â´‚Çï c' ‚àà G,
    rw local_homeomorph.refl_trans,
    exact has_groupoid.compatible G hc hc'
  end,
  ..homeomorph.refl M }

/-- The inverse of a structomorphism is a structomorphism -/
def structomorph.symm (e : structomorph G M M') : structomorph G M' M :=
{ mem_groupoid := begin
    assume c c' hc hc',
    have : (c'.symm ‚â´‚Çï e.to_homeomorph.to_local_homeomorph ‚â´‚Çï c).symm ‚àà G :=
      G.symm (e.mem_groupoid c' c hc' hc),
    rwa [trans_symm_eq_symm_trans_symm, trans_symm_eq_symm_trans_symm, symm_symm, trans_assoc]
      at this,
  end,
  ..e.to_homeomorph.symm}

/-- The composition of structomorphisms is a structomorphism -/
def structomorph.trans (e : structomorph G M M') (e' : structomorph G M' M'') :
  structomorph G M M'' :=
{ mem_groupoid := begin
    /- Let c and c' be two charts in M and M''. We want to show that e' ‚àò e is smooth in these
    charts, around any point x. For this, let y = e (c‚Åª¬π x), and consider a chart g around y.
    Then g ‚àò e ‚àò c‚Åª¬π and c' ‚àò e' ‚àò g‚Åª¬π are both smooth as e and e' are structomorphisms, so
    their composition is smooth, and it coincides with c' ‚àò e' ‚àò e ‚àò c‚Åª¬π around x. -/
    assume c c' hc hc',
    refine G.locality (Œªx hx, _),
    let f‚ÇÅ := e.to_homeomorph.to_local_homeomorph,
    let f‚ÇÇ := e'.to_homeomorph.to_local_homeomorph,
    let f  := (e.to_homeomorph.trans e'.to_homeomorph).to_local_homeomorph,
    have feq : f = f‚ÇÅ ‚â´‚Çï f‚ÇÇ := homeomorph.trans_to_local_homeomorph _ _,
    -- define the atlas g around y
    let y := (c.symm ‚â´‚Çï f‚ÇÅ) x,
    let g := chart_at H y,
    have hg‚ÇÅ := chart_mem_atlas H y,
    have hg‚ÇÇ := mem_chart_source H y,
    let s := (c.symm ‚â´‚Çï f‚ÇÅ).source ‚à© (c.symm ‚â´‚Çï f‚ÇÅ) ‚Åª¬π' g.source,
    have open_s : is_open s,
      by apply (c.symm ‚â´‚Çï f‚ÇÅ).continuous_to_fun.preimage_open_of_open; apply open_source,
    have : x ‚àà s,
    { split,
      { simp only [trans_source, preimage_univ, inter_univ, homeomorph.to_local_homeomorph_source],
        rw trans_source at hx,
        exact hx.1 },
      { exact hg‚ÇÇ } },
    refine ‚ü®s, open_s, this, _‚ü©,
    let F‚ÇÅ := (c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï g) ‚â´‚Çï (g.symm ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c'),
    have A : F‚ÇÅ ‚àà G := G.trans (e.mem_groupoid c g hc hg‚ÇÅ) (e'.mem_groupoid g c' hg‚ÇÅ hc'),
    let F‚ÇÇ := (c.symm ‚â´‚Çï f ‚â´‚Çï c').restr s,
    have : F‚ÇÅ ‚âà F‚ÇÇ := calc
      F‚ÇÅ ‚âà c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï (g ‚â´‚Çï g.symm) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' : by simp [F‚ÇÅ, trans_assoc]
      ... ‚âà c.symm ‚â´‚Çï f‚ÇÅ ‚â´‚Çï (of_set g.source g.open_source) ‚â´‚Çï f‚ÇÇ ‚â´‚Çï c' :
        by simp [eq_on_source.trans', trans_self_symm g]
      ... ‚âà ((c.symm ‚â´‚Çï f‚ÇÅ) ‚â´‚Çï (of_set g.source g.open_source)) ‚â´‚Çï (f‚ÇÇ ‚â´‚Çï c') :
        by simp [trans_assoc]
      ... ‚âà ((c.symm ‚â´‚Çï f‚ÇÅ).restr s) ‚â´‚Çï (f‚ÇÇ ‚â´‚Çï c') : by simp [s, trans_of_set']
      ... ‚âà ((c.symm ‚â´‚Çï f‚ÇÅ) ‚â´‚Çï (f‚ÇÇ ‚â´‚Çï c')).restr s : by simp [restr_trans]
      ... ‚âà (c.symm ‚â´‚Çï (f‚ÇÅ ‚â´‚Çï f‚ÇÇ) ‚â´‚Çï c').restr s : by simp [eq_on_source.restr, trans_assoc]
      ... ‚âà F‚ÇÇ : by simp [F‚ÇÇ, feq],
    have : F‚ÇÇ ‚àà G := G.eq_on_source A (setoid.symm this),
    exact this
  end,
  ..homeomorph.trans e.to_homeomorph e'.to_homeomorph }

end has_groupoid
