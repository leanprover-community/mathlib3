/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import analysis.complex.abs_max
import geometry.manifold.mfderiv

/-! # Holomorphic functions on complex manifolds

Thanks to the rigidity of complex-differentiability compared to real-differentiability, there are
many results about complex manifolds with no analogue for manifolds a general normed field.  For
now, this file contains just one such result:

## Main results

* `mdifferentiable.const_of_compact_space_of_connected_space`: A complex-differentiable function on
  a compact preconnected complex manifold is constant.

## TODO

There is a whole theory to develop here, but an obvious next step would be to generalize the above
result to possibly disconnected compact complex manifolds, where the result should be that a
complex-differentiable function is `is_locally_constant`.  There are some gaps in the
`is_locally_constant` API that need to be filled for this.
-/

open_locale manifold topological_space
open complex

variables {E : Type*} [normed_add_comm_group E] [normed_space â„‚ E]
variables {F : Type*} [normed_add_comm_group F] [normed_space â„‚ F] [strict_convex_space â„ F]

variables {M : Type*} [topological_space M] [charted_space E M]
  [smooth_manifold_with_corners ğ“˜(â„‚, E) M]

/-- A holomorphic function on a complex manifold is constant on every compact, preconnected, clopen
subset. -/
lemma mdifferentiable.const_of_is_preconnected {s : set M} (hsâ‚ : is_compact s)
  (hsâ‚‚ : is_preconnected s) (hsâ‚ƒ : is_clopen s)
  {f : M â†’ F} (hf : mdifferentiable ğ“˜(â„‚, E) ğ“˜(â„‚, F) f) {a b : M} (ha : a âˆˆ s) (hb : b âˆˆ s) :
  f a = f b :=
begin
  -- for an empty set this fact is trivial
  rcases s.eq_empty_or_nonempty with rfl | hs',
  { exact false.rec _ ha },
  -- otherwise, let `pâ‚€` be a point where the value of `f` has maximal norm
  obtain âŸ¨pâ‚€, hpâ‚€s, hpâ‚€âŸ© := hsâ‚.exists_forall_ge hs' hf.continuous.norm.continuous_on,
  -- we will show `f` agrees everywhere with `f pâ‚€`
  suffices : s âŠ† {r : M | f r = f pâ‚€} âˆ© s,
  { exact (this ha).1.trans (this hb).1.symm }, clear ha hb a b,
  refine hsâ‚‚.subset_clopen _ âŸ¨pâ‚€, hpâ‚€s, âŸ¨rfl, hpâ‚€sâŸ©âŸ©,
  -- closedness of the set of points sent to `f pâ‚€`
  refine âŸ¨_, (is_closed_singleton.preimage hf.continuous).inter hsâ‚ƒ.2âŸ©,
  -- we will show this set is open by showing it is a neighbourhood of each of its members
  rw is_open_iff_mem_nhds,
  rintros p âŸ¨hp : f p = _, hpsâŸ©, -- let `p` be  in this set
  have hps' : s âˆˆ ğ“ p := hsâ‚ƒ.1.mem_nhds hps,
  have keyâ‚ : (chart_at E p).symm â»Â¹' s âˆˆ ğ“ (chart_at E p p),
  { rw [â† filter.mem_map, (chart_at E p).symm_map_nhds_eq (mem_chart_source E p)],
    exact hps' },
  have keyâ‚‚ : (chart_at E p).target âˆˆ ğ“ (chart_at E p p) :=
    (local_homeomorph.open_target _).mem_nhds (mem_chart_target E p),
  -- `f` pulled back by the chart at `p` is differentiable around `chart_at E p p`
  have hf' : âˆ€á¶  (z : E) in ğ“ (chart_at E p p), differentiable_at â„‚ (f âˆ˜ (chart_at E p).symm) z,
  { apply filter.eventually_of_mem keyâ‚‚,
    intros z hz,
    have Hâ‚ : (chart_at E p).symm z âˆˆ (chart_at E p).source := (chart_at E p).map_target hz,
    have Hâ‚‚ : f ((chart_at E p).symm z) âˆˆ (chart_at F (0:F)).source := trivial,
    have H := hf ((chart_at E p).symm z),
    rw mdifferentiable_at_iff_of_mem_source Hâ‚ Hâ‚‚ at H,
    simp only [differentiable_within_at_univ] with mfld_simps at H,
    simpa [local_homeomorph.right_inv _ hz] using H.2,
    apply_instance,
    apply_instance },
  -- `f` pulled back by the chart at `p` has a local max at `chart_at E p p`
  have hf'' : is_local_max (norm âˆ˜ f âˆ˜ (chart_at E p).symm) (chart_at E p p),
  { apply filter.eventually_of_mem keyâ‚,
    intros z hz,
    refine (hpâ‚€ ((chart_at E p).symm z) hz).trans (_ : âˆ¥f pâ‚€âˆ¥ â‰¤ âˆ¥f _âˆ¥),
    rw [â† hp, local_homeomorph.left_inv _ (mem_chart_source E p)] },
  -- so by the maximum principle `f` is equal to `f p` near `p`
  obtain âŸ¨U, hU, hUfâŸ© := (complex.eventually_eq_of_is_local_max_norm hf' hf'').exists_mem,
  have Hâ‚ : (chart_at E p) â»Â¹' U âˆˆ ğ“ p := (chart_at E p).continuous_at (mem_chart_source E p) hU,
  have Hâ‚‚ : (chart_at E p).source âˆˆ ğ“ p :=
    (local_homeomorph.open_source _).mem_nhds (mem_chart_source E p),
  apply filter.mem_of_superset (filter.inter_mem hps' (filter.inter_mem Hâ‚ Hâ‚‚)),
  rintros q âŸ¨hqs, hq : chart_at E p q âˆˆ _, hq'âŸ©,
  refine âŸ¨_, hqsâŸ©,
  simpa [local_homeomorph.left_inv _ hq', hp, -norm_eq_abs] using hUf (chart_at E p q) hq,
end

/-- A holomorphic function on a compact connected complex manifold is constant. -/
lemma mdifferentiable.const_of_compact_space_of_preconnected_space [compact_space M]
  [preconnected_space M] {f : M â†’ F} (hf : mdifferentiable ğ“˜(â„‚, E) ğ“˜(â„‚, F) f) (a b : M) :
  f a = f b :=
hf.const_of_is_preconnected compact_univ is_preconnected_univ is_clopen_univ (set.mem_univ _)
  (set.mem_univ _)
