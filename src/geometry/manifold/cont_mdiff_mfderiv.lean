/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import geometry.manifold.cont_mdiff
import geometry.manifold.mfderiv

/-!
### Interactions between differentiability, smoothness and manifold derivatives

We give the relation between `mdifferentiable`, `cont_mdiff`, `mfderiv`, `tangent_map`
and related notions.

## Main statements

* `cont_mdiff_on.cont_mdiff_on_tangent_map_within` states that the bundled derivative
  of a `Câ¿` function in a domain is `Cáµ` when `m + 1 â‰¤ n`.
* `cont_mdiff.cont_mdiff_tangent_map` states that the bundled derivative
  of a `Câ¿` function is `Cáµ` when `m + 1 â‰¤ n`.
-/

open set function filter charted_space smooth_manifold_with_corners
open_locale topology manifold

/-! ### Definition of smooth functions between manifolds -/

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
-- declare a smooth manifold `M` over the pair `(E, H)`.
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
{M : Type*} [topological_space M] [charted_space H M] [Is : smooth_manifold_with_corners I M]
-- declare a smooth manifold `M'` over the pair `(E', H')`.
{E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M'] [I's : smooth_manifold_with_corners I' M']
-- declare a smooth manifold `N` over the pair `(F, G)`.
{F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
{G : Type*} [topological_space G] {J : model_with_corners ğ•œ F G}
{N : Type*} [topological_space N] [charted_space G N] [Js : smooth_manifold_with_corners J N]
-- declare a smooth manifold `N'` over the pair `(F', G')`.
{F' : Type*} [normed_add_comm_group F'] [normed_space ğ•œ F']
{G' : Type*} [topological_space G'] {J' : model_with_corners ğ•œ F' G'}
{N' : Type*} [topological_space N'] [charted_space G' N'] [J's : smooth_manifold_with_corners J' N']
-- declare functions, sets, points and smoothness indices
{f fâ‚ : M â†’ M'} {s sâ‚ t : set M} {x : M} {m n : â„•âˆ}

/-! ### Deducing differentiability from smoothness -/

lemma cont_mdiff_within_at.mdifferentiable_within_at
  (hf : cont_mdiff_within_at I I' n f s x) (hn : 1 â‰¤ n) :
  mdifferentiable_within_at I I' f s x :=
begin
  suffices h : mdifferentiable_within_at I I' f (s âˆ© (f â»Â¹' (ext_chart_at I' (f x)).source)) x,
  { rwa mdifferentiable_within_at_inter' at h,
    apply (hf.1).preimage_mem_nhds_within,
    exact ext_chart_at_source_mem_nhds I' (f x) },
  rw mdifferentiable_within_at_iff,
  exact âŸ¨hf.1.mono (inter_subset_left _ _),
    (hf.2.differentiable_within_at hn).mono (by mfld_set_tac)âŸ©,
end

lemma cont_mdiff_at.mdifferentiable_at (hf : cont_mdiff_at I I' n f x) (hn : 1 â‰¤ n) :
  mdifferentiable_at I I' f x :=
mdifferentiable_within_at_univ.1 $ cont_mdiff_within_at.mdifferentiable_within_at hf hn

lemma cont_mdiff_on.mdifferentiable_on (hf : cont_mdiff_on I I' n f s) (hn : 1 â‰¤ n) :
  mdifferentiable_on I I' f s :=
Î» x hx, (hf x hx).mdifferentiable_within_at hn

lemma cont_mdiff.mdifferentiable (hf : cont_mdiff I I' n f) (hn : 1 â‰¤ n) :
  mdifferentiable I I' f :=
Î» x, (hf x).mdifferentiable_at hn

lemma smooth_within_at.mdifferentiable_within_at
  (hf : smooth_within_at I I' f s x) : mdifferentiable_within_at I I' f s x :=
hf.mdifferentiable_within_at le_top

lemma smooth_at.mdifferentiable_at (hf : smooth_at I I' f x) : mdifferentiable_at I I' f x :=
hf.mdifferentiable_at le_top

lemma smooth_on.mdifferentiable_on (hf : smooth_on I I' f s) : mdifferentiable_on I I' f s :=
hf.mdifferentiable_on le_top

lemma smooth.mdifferentiable (hf : smooth I I' f) : mdifferentiable I I' f :=
cont_mdiff.mdifferentiable hf le_top

lemma smooth.mdifferentiable_at (hf : smooth I I' f) : mdifferentiable_at I I' f x :=
hf.mdifferentiable x

lemma smooth.mdifferentiable_within_at (hf : smooth I I' f) :
  mdifferentiable_within_at I I' f s x :=
hf.mdifferentiable_at.mdifferentiable_within_at


/-! ### The tangent map of a smooth function is smooth -/

section tangent_map

/-- If a function is `C^n` with `1 â‰¤ n` on a domain with unique derivatives, then its bundled
derivative is continuous. In this auxiliary lemma, we prove this fact when the source and target
space are model spaces in models with corners. The general fact is proved in
`cont_mdiff_on.continuous_on_tangent_map_within`-/
lemma cont_mdiff_on.continuous_on_tangent_map_within_aux
  {f : H â†’ H'} {s : set H}
  (hf : cont_mdiff_on I I' n f s) (hn : 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  continuous_on (tangent_map_within I I' f s) ((tangent_bundle.proj I H) â»Â¹' s) :=
begin
  suffices h : continuous_on (Î» (p : H Ã— E), (f p.fst,
    (fderiv_within ğ•œ (written_in_ext_chart_at I I' p.fst f) (I.symm â»Â¹' s âˆ© range I)
      ((ext_chart_at I p.fst) p.fst) : E â†’L[ğ•œ] E') p.snd)) (prod.fst â»Â¹' s),
  { have A := (tangent_bundle_model_space_homeomorph H I).continuous,
    rw continuous_iff_continuous_on_univ at A,
    have B := ((tangent_bundle_model_space_homeomorph H' I').symm.continuous.comp_continuous_on h)
      .comp' A,
    have : (univ âˆ© â‡‘(tangent_bundle_model_space_homeomorph H I) â»Â¹' (prod.fst â»Â¹' s)) =
      tangent_bundle.proj I H â»Â¹' s,
      by { ext âŸ¨x, vâŸ©, simp only with mfld_simps },
    rw this at B,
    apply B.congr,
    rintros âŸ¨x, vâŸ© hx,
    dsimp [tangent_map_within],
    ext, { refl },
    simp only with mfld_simps,
    apply congr_fun,
    apply congr_arg,
    rw mdifferentiable_within_at.mfderiv_within (hf.mdifferentiable_on hn x hx),
    refl },
  suffices h : continuous_on (Î» (p : H Ã— E), (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm)
    (I.symm â»Â¹' s âˆ© range I) (I p.fst) : E â†’L[ğ•œ] E') p.snd) (prod.fst â»Â¹' s),
  { dsimp [written_in_ext_chart_at, ext_chart_at],
    apply continuous_on.prod
      (continuous_on.comp hf.continuous_on continuous_fst.continuous_on (subset.refl _)),
    apply h.congr,
    assume p hp,
    refl },
  suffices h : continuous_on (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm)
                     (I.symm â»Â¹' s âˆ© range I)) (I '' s),
  { have C := continuous_on.comp h I.continuous_to_fun.continuous_on (subset.refl _),
    have A : continuous (Î»q : (E â†’L[ğ•œ] E') Ã— E, q.1 q.2) :=
      is_bounded_bilinear_map_apply.continuous,
    have B : continuous_on (Î»p : H Ã— E,
      (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I)
                       (I p.1), p.2)) (prod.fst â»Â¹' s),
    { apply continuous_on.prod _ continuous_snd.continuous_on,
      refine (continuous_on.comp C continuous_fst.continuous_on _ : _),
      exact preimage_mono (subset_preimage_image _ _) },
    exact A.comp_continuous_on B },
  rw cont_mdiff_on_iff at hf,
  let x : H := I.symm (0 : E),
  let y : H' := I'.symm (0 : E'),
  have A := hf.2 x y,
  simp only [I.image_eq, inter_comm] with mfld_simps at A âŠ¢,
  apply A.continuous_on_fderiv_within _ hn,
  convert hs.unique_diff_on_target_inter x using 1,
  simp only [inter_comm] with mfld_simps
end

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative is
`C^m` when `m+1 â‰¤ n`. In this auxiliary lemma, we prove this fact when the source and target space
are model spaces in models with corners. The general fact is proved in
`cont_mdiff_on.cont_mdiff_on_tangent_map_within` -/
lemma cont_mdiff_on.cont_mdiff_on_tangent_map_within_aux
  {f : H â†’ H'} {s : set H}
  (hf : cont_mdiff_on I I' n f s) (hmn : m + 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s)
    ((tangent_bundle.proj I H) â»Â¹' s) :=
begin
  have m_le_n : m â‰¤ n,
  { apply le_trans _ hmn,
    have : m + 0 â‰¤ m + 1 := add_le_add_left (zero_le _) _,
    simpa only [add_zero] using this },
  have one_le_n : 1 â‰¤ n,
  { apply le_trans _ hmn,
    change 0 + 1 â‰¤ m + 1,
    exact add_le_add_right (zero_le _) _ },
  have U': unique_diff_on ğ•œ (range I âˆ© I.symm â»Â¹' s),
  { assume y hy,
    simpa only [unique_mdiff_on, unique_mdiff_within_at, hy.1, inter_comm] with mfld_simps
      using hs (I.symm y) hy.2 },
  rw cont_mdiff_on_iff,
  refine âŸ¨hf.continuous_on_tangent_map_within_aux one_le_n hs, Î»p q, _âŸ©,
  have A : range I Ã—Ë¢ univ âˆ©
      ((equiv.sigma_equiv_prod H E).symm âˆ˜ Î» (p : E Ã— E), ((I.symm) p.fst, p.snd)) â»Â¹'
        (tangent_bundle.proj I H â»Â¹' s)
      = (range I âˆ© I.symm â»Â¹' s) Ã—Ë¢ univ,
    by { ext âŸ¨x, vâŸ©, simp only with mfld_simps },
  suffices h : cont_diff_on ğ•œ m (((Î» (p : H' Ã— E'), (I' p.fst, p.snd)) âˆ˜
      (equiv.sigma_equiv_prod H' E')) âˆ˜ tangent_map_within I I' f s âˆ˜
      ((equiv.sigma_equiv_prod H E).symm) âˆ˜ Î» (p : E Ã— E), (I.symm p.fst, p.snd))
    ((range â‡‘I âˆ© â‡‘(I.symm) â»Â¹' s) Ã—Ë¢ univ),
    by simpa [A] using h,
  change cont_diff_on ğ•œ m (Î» (p : E Ã— E),
    ((I' (f (I.symm p.fst)), ((mfderiv_within I I' f s (I.symm p.fst)) : E â†’ E') p.snd) : E' Ã— E'))
    ((range I âˆ© I.symm â»Â¹' s) Ã—Ë¢ univ),
  -- check that all bits in this formula are `C^n`
  have hf' := cont_mdiff_on_iff.1 hf,
  have A : cont_diff_on ğ•œ m (I' âˆ˜ f âˆ˜ I.symm) (range I âˆ© I.symm â»Â¹' s) :=
    by simpa only with mfld_simps using (hf'.2 (I.symm 0) (I'.symm 0)).of_le m_le_n,
  have B : cont_diff_on ğ•œ m ((I' âˆ˜ f âˆ˜ I.symm) âˆ˜ prod.fst)
           ((range I âˆ© I.symm â»Â¹' s) Ã—Ë¢ univ) :=
    A.comp (cont_diff_fst.cont_diff_on) (prod_subset_preimage_fst _ _),
  suffices C : cont_diff_on ğ•œ m (Î» (p : E Ã— E),
    ((fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) p.1 : _) p.2))
    ((range I âˆ© I.symm â»Â¹' s) Ã—Ë¢ univ),
  { apply cont_diff_on.prod B _,
    apply C.congr (Î»p hp, _),
    simp only with mfld_simps at hp,
    simp only [mfderiv_within, hf.mdifferentiable_on one_le_n _ hp.2, hp.1, if_pos]
      with mfld_simps },
  have D : cont_diff_on ğ•œ m (Î» x,
    (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) x))
    (range I âˆ© I.symm â»Â¹' s),
  { have : cont_diff_on ğ•œ n (I' âˆ˜ f âˆ˜ I.symm) (range I âˆ© I.symm â»Â¹' s) :=
      by simpa only with mfld_simps using (hf'.2 (I.symm 0) (I'.symm 0)),
    simpa only [inter_comm] using this.fderiv_within U' hmn },
  have := D.comp (cont_diff_fst.cont_diff_on) (prod_subset_preimage_fst _ _),
  have := cont_diff_on.prod this (cont_diff_snd.cont_diff_on),
  exact is_bounded_bilinear_map_apply.cont_diff.comp_cont_diff_on this,
end

include Is I's

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative
is `C^m` when `m+1 â‰¤ n`. -/
theorem cont_mdiff_on.cont_mdiff_on_tangent_map_within
  (hf : cont_mdiff_on I I' n f s) (hmn : m + 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s)
  ((tangent_bundle.proj I M) â»Â¹' s) :=
begin
  /- The strategy of the proof is to avoid unfolding the definitions, and reduce by functoriality
  to the case of functions on the model spaces, where we have already proved the result.
  Let `l` and `r` be the charts to the left and to the right, so that we have
  ```
     l^{-1}      f       r
  H --------> M ---> M' ---> H'
  ```
  Then the tangent map `T(r âˆ˜ f âˆ˜ l)` is smooth by a previous result. Consider the composition
  ```
      Tl        T(r âˆ˜ f âˆ˜ l^{-1})         Tr^{-1}
  TM -----> TH -------------------> TH' ---------> TM'
  ```
  where `Tr^{-1}` and `Tl` are the tangent maps of `r^{-1}` and `l`. Writing `Tl` and `Tr^{-1}` as
  composition of charts (called `Dl` and `il` for `l` and `Dr` and `ir` in the proof below), it
  follows that they are smooth. The composition of all these maps is `Tf`, and is therefore smooth
  as a composition of smooth maps.
  -/
  have m_le_n : m â‰¤ n,
  { apply le_trans _ hmn,
    have : m + 0 â‰¤ m + 1 := add_le_add_left (zero_le _) _,
    simpa only [add_zero] },
  have one_le_n : 1 â‰¤ n,
  { apply le_trans _ hmn,
    change 0 + 1 â‰¤ m + 1,
    exact add_le_add_right (zero_le _) _ },
  /- First step: local reduction on the space, to a set `s'` which is contained in chart domains. -/
  refine cont_mdiff_on_of_locally_cont_mdiff_on (Î»p hp, _),
  have hf' := cont_mdiff_on_iff.1 hf,
  simp [tangent_bundle.proj] at hp,
  let l  := chart_at H p.1,
  set Dl := chart_at (model_prod H E) p with hDl,
  let r  := chart_at H' (f p.1),
  let Dr := chart_at (model_prod H' E') (tangent_map_within I I' f s p),
  let il := chart_at (model_prod H E) (tangent_map I I l p),
  let ir := chart_at (model_prod H' E') (tangent_map I I' (r âˆ˜ f) p),
  let s' := f â»Â¹' r.source âˆ© s âˆ© l.source,
  let s'_lift := (tangent_bundle.proj I M)â»Â¹' s',
  let s'l := l.target âˆ© l.symm â»Â¹' s',
  let s'l_lift := (tangent_bundle.proj I H) â»Â¹' s'l,
  rcases continuous_on_iff'.1 hf'.1 r.source r.open_source with âŸ¨o, o_open, hoâŸ©,
  suffices h : cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s) s'_lift,
  { refine âŸ¨(tangent_bundle.proj I M)â»Â¹' (o âˆ© l.source), _, _, _âŸ©,
    show is_open ((tangent_bundle.proj I M)â»Â¹' (o âˆ© l.source)), from
      (is_open.inter o_open l.open_source).preimage (tangent_bundle_proj_continuous _ _) ,
    show p âˆˆ tangent_bundle.proj I M â»Â¹' (o âˆ© l.source),
    { simp [tangent_bundle.proj] at âŠ¢,
      have : p.1 âˆˆ f â»Â¹' r.source âˆ© s, by simp [hp],
      rw ho at this,
      exact this.1 },
    { have : tangent_bundle.proj I M â»Â¹' s âˆ© tangent_bundle.proj I M â»Â¹' (o âˆ© l.source) = s'_lift,
      { dsimp only [s'_lift, s'], rw [ho], mfld_set_tac },
      rw this,
      exact h } },
  /- Second step: check that all functions are smooth, and use the chain rule to write the bundled
  derivative as a composition of a function between model spaces and of charts.
  Convention: statements about the differentiability of `a âˆ˜ b âˆ˜ c` are named `diff_abc`. Statements
  about differentiability in the bundle have a `_lift` suffix. -/
  have U' : unique_mdiff_on I s',
  { apply unique_mdiff_on.inter _ l.open_source,
    rw [ho, inter_comm],
    exact hs.inter o_open },
  have U'l : unique_mdiff_on I s'l :=
    U'.unique_mdiff_on_preimage (mdifferentiable_chart _ _),
  have diff_f : cont_mdiff_on I I' n f s' :=
    hf.mono (by mfld_set_tac),
  have diff_r : cont_mdiff_on I' I' n r r.source :=
    cont_mdiff_on_chart,
  have diff_rf : cont_mdiff_on I I' n (r âˆ˜ f) s',
  { apply cont_mdiff_on.comp diff_r diff_f (Î»x hx, _),
    simp only [s'] with mfld_simps at hx, simp only [hx] with mfld_simps },
  have diff_l : cont_mdiff_on I I n l.symm s'l,
  { have A : cont_mdiff_on I I n l.symm l.target :=
      cont_mdiff_on_chart_symm,
    exact A.mono (by mfld_set_tac) },
  have diff_rfl : cont_mdiff_on I I' n (r âˆ˜ f âˆ˜ l.symm) s'l,
  { apply cont_mdiff_on.comp diff_rf diff_l,
    mfld_set_tac },
  have diff_rfl_lift : cont_mdiff_on I.tangent I'.tangent m
      (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l) s'l_lift :=
    diff_rfl.cont_mdiff_on_tangent_map_within_aux hmn U'l,
  have diff_irrfl_lift : cont_mdiff_on I.tangent I'.tangent m
      (ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l)) s'l_lift,
  { have A : cont_mdiff_on I'.tangent I'.tangent m ir ir.source := cont_mdiff_on_chart,
    exact cont_mdiff_on.comp A diff_rfl_lift (Î»p hp, by simp only [ir] with mfld_simps) },
  have diff_Drirrfl_lift : cont_mdiff_on I.tangent I'.tangent m
    (Dr.symm âˆ˜ (ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l))) s'l_lift,
  { have A : cont_mdiff_on I'.tangent I'.tangent m Dr.symm Dr.target :=
      cont_mdiff_on_chart_symm,
    apply cont_mdiff_on.comp A diff_irrfl_lift (Î»p hp, _),
    simp only [s'l_lift, tangent_bundle.proj] with mfld_simps at hp,
    simp only [ir, @local_equiv.refl_coe (model_prod H' E'), hp] with mfld_simps },
  -- conclusion of this step: the composition of all the maps above is smooth
  have diff_DrirrflilDl : cont_mdiff_on I.tangent I'.tangent m
    (Dr.symm âˆ˜ (ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l)) âˆ˜
      (il.symm âˆ˜ Dl)) s'_lift,
  { have A : cont_mdiff_on I.tangent I.tangent m Dl Dl.source := cont_mdiff_on_chart,
    have A' : cont_mdiff_on I.tangent I.tangent m Dl s'_lift,
    { apply A.mono (Î»p hp, _),
      simp only [s'_lift, tangent_bundle.proj] with mfld_simps at hp,
      simp only [Dl, hp] with mfld_simps },
    have B : cont_mdiff_on I.tangent I.tangent m il.symm il.target :=
      cont_mdiff_on_chart_symm,
    have C : cont_mdiff_on I.tangent I.tangent m (il.symm âˆ˜ Dl) s'_lift :=
      cont_mdiff_on.comp B A' (Î»p hp, by simp only [il] with mfld_simps),
    apply cont_mdiff_on.comp diff_Drirrfl_lift C (Î»p hp, _),
    simp only [s'_lift, tangent_bundle.proj] with mfld_simps at hp,
    simp only [il, s'l_lift, hp, tangent_bundle.proj] with mfld_simps },
  /- Third step: check that the composition of all the maps indeed coincides with the derivative we
  are looking for -/
  have eq_comp : âˆ€q âˆˆ s'_lift, tangent_map_within I I' f s q =
      (Dr.symm âˆ˜ ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l) âˆ˜
      (il.symm âˆ˜ Dl)) q,
  { assume q hq,
    simp only [s'_lift, tangent_bundle.proj] with mfld_simps at hq,
    have U'q : unique_mdiff_within_at I s' q.1,
      by { apply U', simp only [hq, s'] with mfld_simps },
    have U'lq : unique_mdiff_within_at I s'l (Dl q).1,
      by { apply U'l, simp only [hq, s'l] with mfld_simps },
    have A : tangent_map_within I I' ((r âˆ˜ f) âˆ˜ l.symm) s'l (il.symm (Dl q)) =
      tangent_map_within I I' (r âˆ˜ f) s' (tangent_map_within I I l.symm s'l (il.symm (Dl q))),
    { refine tangent_map_within_comp_at (il.symm (Dl q)) _ _ (Î»p hp, _) U'lq,
      { apply diff_rf.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { apply diff_l.mdifferentiable_on one_le_n,
        simp only [s'l, hq] with mfld_simps },
      { simp only with mfld_simps at hp, simp only [hp] with mfld_simps } },
    have B : tangent_map_within I I l.symm s'l (il.symm (Dl q)) = q,
    { have : tangent_map_within I I l.symm s'l (il.symm (Dl q))
        = tangent_map I I l.symm (il.symm (Dl q)),
      { refine tangent_map_within_eq_tangent_map U'lq _,
        refine mdifferentiable_at_atlas_symm _ (chart_mem_atlas _ _) _,
        simp only [hq] with mfld_simps },
      rw [this, tangent_map_chart_symm, hDl],
      { simp only [hq] with mfld_simps,
        have : q âˆˆ (chart_at (model_prod H E) p).source, by simp only [hq] with mfld_simps,
        exact (chart_at (model_prod H E) p).left_inv this },
      { simp only [hq] with mfld_simps } },
    have C : tangent_map_within I I' (r âˆ˜ f) s' q
      = tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q),
    { refine tangent_map_within_comp_at q _ _ (Î»r hr, _) U'q,
      { apply diff_r.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { apply diff_f.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { simp only [s'] with mfld_simps at hr,
        simp only [hr] with mfld_simps } },
    have D : Dr.symm (ir (tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q)))
      = tangent_map_within I I' f s' q,
    { have A : tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q) =
             tangent_map I' I' r (tangent_map_within I I' f s' q),
      { apply tangent_map_within_eq_tangent_map,
        { apply is_open.unique_mdiff_within_at _ r.open_source, simp [hq] },
        { refine mdifferentiable_at_atlas _ (chart_mem_atlas _ _) _,
          simp only [hq] with mfld_simps } },
      have : f p.1 = (tangent_map_within I I' f s p).1 := rfl,
      rw [A],
      dsimp [r, Dr],
      rw [this, tangent_map_chart],
      { simp only [hq] with mfld_simps,
        have : tangent_map_within I I' f s' q âˆˆ
          (chart_at (model_prod H' E') (tangent_map_within I I' f s p)).source,
            by simp only [hq] with mfld_simps,
        exact (chart_at (model_prod H' E') (tangent_map_within I I' f s p)).left_inv this },
      { simp only [hq] with mfld_simps } },
    have E : tangent_map_within I I' f s' q = tangent_map_within I I' f s q,
    { refine tangent_map_within_subset (by mfld_set_tac) U'q _,
      apply hf.mdifferentiable_on one_le_n,
      simp only [hq] with mfld_simps },
    simp only [(âˆ˜), A, B, C, D, E.symm] },
  exact diff_DrirrflilDl.congr eq_comp,
end

/-- If a function is `C^n` on a domain with unique derivatives, with `1 â‰¤ n`, then its bundled
derivative is continuous there. -/
theorem cont_mdiff_on.continuous_on_tangent_map_within
  (hf : cont_mdiff_on I I' n f s) (hmn : 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  continuous_on (tangent_map_within I I' f s) ((tangent_bundle.proj I M) â»Â¹' s) :=
begin
  have : cont_mdiff_on I.tangent I'.tangent 0 (tangent_map_within I I' f s)
         ((tangent_bundle.proj I M) â»Â¹' s) :=
    hf.cont_mdiff_on_tangent_map_within hmn hs,
  exact this.continuous_on
end

/-- If a function is `C^n`, then its bundled derivative is `C^m` when `m+1 â‰¤ n`. -/
theorem cont_mdiff.cont_mdiff_tangent_map
  (hf : cont_mdiff I I' n f) (hmn : m + 1 â‰¤ n) :
  cont_mdiff I.tangent I'.tangent m (tangent_map I I' f) :=
begin
  rw â† cont_mdiff_on_univ at hf âŠ¢,
  convert hf.cont_mdiff_on_tangent_map_within hmn unique_mdiff_on_univ,
  rw tangent_map_within_univ
end

/-- If a function is `C^n`, with `1 â‰¤ n`, then its bundled derivative is continuous. -/
theorem cont_mdiff.continuous_tangent_map
  (hf : cont_mdiff I I' n f) (hmn : 1 â‰¤ n) :
  continuous (tangent_map I I' f) :=
begin
  rw â† cont_mdiff_on_univ at hf,
  rw continuous_iff_continuous_on_univ,
  convert hf.continuous_on_tangent_map_within hmn unique_mdiff_on_univ,
  rw tangent_map_within_univ
end

end tangent_map

/-! ### Smoothness of the projection in a basic smooth bundle -/

namespace basic_smooth_vector_bundle_core

variables (Z : basic_smooth_vector_bundle_core I M E')

/-- A version of `cont_mdiff_at_iff_target` when the codomain is the total space of
  a `basic_smooth_vector_bundle_core`. The continuity condition in the RHS is weaker. -/
lemma cont_mdiff_at_iff_target {f : N â†’ Z.to_vector_bundle_core.total_space}
  {x : N} {n : â„•âˆ} :
  cont_mdiff_at J (I.prod ğ“˜(ğ•œ, E')) n f x â†” continuous_at (bundle.total_space.proj âˆ˜ f) x âˆ§
    cont_mdiff_at J ğ“˜(ğ•œ, E Ã— E') n (ext_chart_at (I.prod ğ“˜(ğ•œ, E')) (f x) âˆ˜ f) x :=
begin
  let Z' := Z.to_vector_bundle_core,
  rw [cont_mdiff_at_iff_target, and.congr_left_iff],
  refine Î» hf, âŸ¨Î» h, Z'.continuous_proj.continuous_at.comp h, Î» h, _âŸ©,
  exact (Z'.local_triv âŸ¨chart_at _ (f x).1, chart_mem_atlas _ _âŸ©)
    .continuous_at_of_comp_left h (mem_chart_source _ _) (h.prod hf.continuous_at.snd)
end

lemma smooth_iff_target {f : N â†’ Z.to_vector_bundle_core.total_space} :
  smooth J (I.prod ğ“˜(ğ•œ, E')) f â†” continuous (bundle.total_space.proj âˆ˜ f) âˆ§
  âˆ€ x, smooth_at J ğ“˜(ğ•œ, E Ã— E') (ext_chart_at (I.prod ğ“˜(ğ•œ, E')) (f x) âˆ˜ f) x :=
by simp_rw [smooth, smooth_at, cont_mdiff, Z.cont_mdiff_at_iff_target, forall_and_distrib,
  continuous_iff_continuous_at]

lemma cont_mdiff_proj :
  cont_mdiff (I.prod ğ“˜(ğ•œ, E')) I n Z.to_vector_bundle_core.proj :=
begin
  assume x,
  rw [cont_mdiff_at, cont_mdiff_within_at_iff'],
  refine âŸ¨Z.to_vector_bundle_core.continuous_proj.continuous_within_at, _âŸ©,
  simp only [(âˆ˜), chart_at, chart] with mfld_simps,
  apply cont_diff_within_at_fst.congr,
  { rintros âŸ¨a, bâŸ© hab,
    simp only with mfld_simps at hab,
    simp only [hab] with mfld_simps },
  { simp only with mfld_simps }
end

lemma smooth_proj :
  smooth (I.prod ğ“˜(ğ•œ, E')) I Z.to_vector_bundle_core.proj :=
cont_mdiff_proj Z

lemma cont_mdiff_on_proj {s : set (Z.to_vector_bundle_core.total_space)} :
  cont_mdiff_on (I.prod ğ“˜(ğ•œ, E')) I n
    Z.to_vector_bundle_core.proj s :=
Z.cont_mdiff_proj.cont_mdiff_on

lemma smooth_on_proj {s : set (Z.to_vector_bundle_core.total_space)} :
  smooth_on (I.prod ğ“˜(ğ•œ, E')) I Z.to_vector_bundle_core.proj s :=
cont_mdiff_on_proj Z

lemma cont_mdiff_at_proj {p : Z.to_vector_bundle_core.total_space} :
  cont_mdiff_at (I.prod ğ“˜(ğ•œ, E')) I n
    Z.to_vector_bundle_core.proj p :=
Z.cont_mdiff_proj.cont_mdiff_at

lemma smooth_at_proj {p : Z.to_vector_bundle_core.total_space} :
  smooth_at (I.prod ğ“˜(ğ•œ, E')) I Z.to_vector_bundle_core.proj p :=
Z.cont_mdiff_at_proj

lemma cont_mdiff_within_at_proj
  {s : set (Z.to_vector_bundle_core.total_space)}
  {p : Z.to_vector_bundle_core.total_space} :
  cont_mdiff_within_at (I.prod ğ“˜(ğ•œ, E')) I n
    Z.to_vector_bundle_core.proj s p :=
Z.cont_mdiff_at_proj.cont_mdiff_within_at

lemma smooth_within_at_proj
  {s : set (Z.to_vector_bundle_core.total_space)}
  {p : Z.to_vector_bundle_core.total_space} :
  smooth_within_at (I.prod ğ“˜(ğ•œ, E')) I
    Z.to_vector_bundle_core.proj s p :=
Z.cont_mdiff_within_at_proj

/-- If an element of `E'` is invariant under all coordinate changes, then one can define a
corresponding section of the fiber bundle, which is smooth. This applies in particular to the
zero section of a vector bundle. Another example (not yet defined) would be the identity
section of the endomorphism bundle of a vector bundle. -/
lemma smooth_const_section (v : E')
  (h : âˆ€ (i j : atlas H M), âˆ€ x âˆˆ i.1.source âˆ© j.1.source, Z.coord_change i j (i.1 x) v = v) :
  smooth I (I.prod ğ“˜(ğ•œ, E'))
    (show M â†’ Z.to_vector_bundle_core.total_space, from Î» x, âŸ¨x, vâŸ©) :=
begin
  assume x,
  rw [cont_mdiff_at, cont_mdiff_within_at_iff'],
  split,
  { apply continuous.continuous_within_at,
    apply fiber_bundle_core.continuous_const_section,
    assume i j y hy,
    exact h _ _ _ hy },
  { have : cont_diff ğ•œ âŠ¤ (Î» (y : E), (y, v)) := cont_diff_id.prod cont_diff_const,
    apply this.cont_diff_within_at.congr,
    { assume y hy,
      simp only with mfld_simps at hy,
      simp only [chart, hy, chart_at, prod.mk.inj_iff, to_vector_bundle_core]
        with mfld_simps,
      apply h,
      simp only [hy, subtype.val_eq_coe] with mfld_simps },
    { simp only [chart, chart_at, prod.mk.inj_iff, to_vector_bundle_core]
        with mfld_simps,
      apply h,
      simp only [subtype.val_eq_coe] with mfld_simps } }
end

end basic_smooth_vector_bundle_core

/-! ### Smoothness of the tangent bundle projection -/

namespace tangent_bundle

include Is

lemma cont_mdiff_proj :
  cont_mdiff I.tangent I n (proj I M) :=
basic_smooth_vector_bundle_core.cont_mdiff_proj _

lemma smooth_proj : smooth I.tangent I (proj I M) :=
basic_smooth_vector_bundle_core.smooth_proj _

lemma cont_mdiff_on_proj {s : set (tangent_bundle I M)} :
  cont_mdiff_on I.tangent I n (proj I M) s :=
basic_smooth_vector_bundle_core.cont_mdiff_on_proj _

lemma smooth_on_proj {s : set (tangent_bundle I M)} :
  smooth_on I.tangent I (proj I M) s :=
basic_smooth_vector_bundle_core.smooth_on_proj _

lemma cont_mdiff_at_proj {p : tangent_bundle I M} :
  cont_mdiff_at I.tangent I n
    (proj I M) p :=
basic_smooth_vector_bundle_core.cont_mdiff_at_proj _

lemma smooth_at_proj {p : tangent_bundle I M} :
  smooth_at I.tangent I (proj I M) p :=
basic_smooth_vector_bundle_core.smooth_at_proj _

lemma cont_mdiff_within_at_proj
  {s : set (tangent_bundle I M)} {p : tangent_bundle I M} :
  cont_mdiff_within_at I.tangent I n
    (proj I M) s p :=
basic_smooth_vector_bundle_core.cont_mdiff_within_at_proj _

lemma smooth_within_at_proj
  {s : set (tangent_bundle I M)} {p : tangent_bundle I M} :
  smooth_within_at I.tangent I
    (proj I M) s p :=
basic_smooth_vector_bundle_core.smooth_within_at_proj _

variables (I M)
/-- The zero section of the tangent bundle -/
def zero_section : M â†’ tangent_bundle I M := Î» x, âŸ¨x, 0âŸ©
variables {I M}

lemma smooth_zero_section : smooth I I.tangent (zero_section I M) :=
begin
  apply basic_smooth_vector_bundle_core.smooth_const_section (tangent_bundle_core I M) 0,
  assume i j x hx,
  simp only [tangent_bundle_core, continuous_linear_map.map_zero, continuous_linear_map.coe_coe]
    with mfld_simps,
end

open bundle

/-- The derivative of the zero section of the tangent bundle maps `âŸ¨x, vâŸ©` to `âŸ¨âŸ¨x, 0âŸ©, âŸ¨v, 0âŸ©âŸ©`.

Note that, as currently framed, this is a statement in coordinates, thus reliant on the choice
of the coordinate system we use on the tangent bundle.

However, the result itself is coordinate-dependent only to the extent that the coordinates
determine a splitting of the tangent bundle.  Moreover, there is a canonical splitting at each
point of the zero section (since there is a canonical horizontal space there, the tangent space
to the zero section, in addition to the canonical vertical space which is the kernel of the
derivative of the projection), and this canonical splitting is also the one that comes from the
coordinates on the tangent bundle in our definitions. So this statement is not as crazy as it
may seem.

TODO define splittings of vector bundles; state this result invariantly. -/
lemma tangent_map_tangent_bundle_pure (p : tangent_bundle I M) :
  tangent_map I I.tangent (tangent_bundle.zero_section I M) p = âŸ¨âŸ¨p.1, 0âŸ©, âŸ¨p.2, 0âŸ©âŸ© :=
begin
  rcases p with âŸ¨x, vâŸ©,
  have N : I.symm â»Â¹' (chart_at H x).target âˆˆ ğ“ (I ((chart_at H x) x)),
  { apply is_open.mem_nhds,
    apply (local_homeomorph.open_target _).preimage I.continuous_inv_fun,
    simp only with mfld_simps },
  have A : mdifferentiable_at I I.tangent (Î» x, @total_space_mk M (tangent_space I) x 0) x :=
    tangent_bundle.smooth_zero_section.mdifferentiable_at,
  have B : fderiv_within ğ•œ (Î» (x_1 : E), (x_1, (0 : E))) (set.range â‡‘I) (I ((chart_at H x) x)) v
    = (v, 0),
  { rw [fderiv_within_eq_fderiv, differentiable_at.fderiv_prod],
    { simp },
    { exact differentiable_at_id' },
    { exact differentiable_at_const _ },
    { exact model_with_corners.unique_diff_at_image I },
    { exact differentiable_at_id'.prod (differentiable_at_const _) } },
  simp only [tangent_bundle.zero_section, tangent_map, mfderiv,
    A, if_pos, chart_at, basic_smooth_vector_bundle_core.chart,
    basic_smooth_vector_bundle_core.to_vector_bundle_core, tangent_bundle_core,
    function.comp, continuous_linear_map.map_zero] with mfld_simps,
  rw â† fderiv_within_inter N (I.unique_diff (I ((chart_at H x) x)) (set.mem_range_self _)) at B,
  rw [â† fderiv_within_inter N (I.unique_diff (I ((chart_at H x) x)) (set.mem_range_self _)), â† B],
  congr' 2,
  apply fderiv_within_congr _ (Î» y hy, _),
  { simp only [prod.mk.inj_iff] with mfld_simps },
  { apply unique_diff_within_at.inter (I.unique_diff _ _) N,
    simp only with mfld_simps },
  { simp only with mfld_simps at hy,
    simp only [hy, prod.mk.inj_iff] with mfld_simps },
end

end tangent_bundle
