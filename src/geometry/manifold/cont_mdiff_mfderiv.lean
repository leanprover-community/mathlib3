/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Floris van Doorn
-/
import geometry.manifold.mfderiv

/-!
### Interactions between differentiability, smoothness and manifold derivatives

We give the relation between `mdifferentiable`, `cont_mdiff`, `mfderiv`, `tangent_map`
and related notions.

## Main statements

* `cont_mdiff_on.cont_mdiff_on_tangent_map_within` states that the bundled derivative
  of a `C‚Åø` function in a domain is `C·µê` when `m + 1 ‚â§ n`.
* `cont_mdiff.cont_mdiff_tangent_map` states that the bundled derivative
  of a `C‚Åø` function is `C·µê` when `m + 1 ‚â§ n`.
-/

open set function filter charted_space smooth_manifold_with_corners bundle
open_locale topology manifold bundle

/-! ### Definition of smooth functions between manifolds -/

variables {ùïú : Type*} [nontrivially_normed_field ùïú]
-- declare a smooth manifold `M` over the pair `(E, H)`.
{E : Type*} [normed_add_comm_group E] [normed_space ùïú E]
{H : Type*} [topological_space H] {I : model_with_corners ùïú E H}
{M : Type*} [topological_space M] [charted_space H M] [Is : smooth_manifold_with_corners I M]
-- declare a smooth manifold `M'` over the pair `(E', H')`.
{E' : Type*} [normed_add_comm_group E'] [normed_space ùïú E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ùïú E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M'] [I's : smooth_manifold_with_corners I' M']
-- declare a smooth manifold `N` over the pair `(F, G)`.
{F : Type*} [normed_add_comm_group F] [normed_space ùïú F]
{G : Type*} [topological_space G] {J : model_with_corners ùïú F G}
{N : Type*} [topological_space N] [charted_space G N] [Js : smooth_manifold_with_corners J N]
-- declare a smooth manifold `N'` over the pair `(F', G')`.
{F' : Type*} [normed_add_comm_group F'] [normed_space ùïú F']
{G' : Type*} [topological_space G'] {J' : model_with_corners ùïú F' G'}
{N' : Type*} [topological_space N'] [charted_space G' N'] [J's : smooth_manifold_with_corners J' N']
-- declare some additional normed spaces, used for fibers of vector bundles
{F‚ÇÅ : Type*} [normed_add_comm_group F‚ÇÅ] [normed_space ùïú F‚ÇÅ]
{F‚ÇÇ : Type*} [normed_add_comm_group F‚ÇÇ] [normed_space ùïú F‚ÇÇ]
-- declare functions, sets, points and smoothness indices
{f f‚ÇÅ : M ‚Üí M'} {s s‚ÇÅ t : set M} {x : M} {m n : ‚Ñï‚àû}

/-! ### Deducing differentiability from smoothness -/

lemma cont_mdiff_within_at.mdifferentiable_within_at
  (hf : cont_mdiff_within_at I I' n f s x) (hn : 1 ‚â§ n) :
  mdifferentiable_within_at I I' f s x :=
begin
  suffices h : mdifferentiable_within_at I I' f (s ‚à© (f ‚Åª¬π' (ext_chart_at I' (f x)).source)) x,
  { rwa mdifferentiable_within_at_inter' at h,
    apply (hf.1).preimage_mem_nhds_within,
    exact ext_chart_at_source_mem_nhds I' (f x) },
  rw mdifferentiable_within_at_iff,
  exact ‚ü®hf.1.mono (inter_subset_left _ _),
    (hf.2.differentiable_within_at hn).mono (by mfld_set_tac)‚ü©,
end

lemma cont_mdiff_at.mdifferentiable_at (hf : cont_mdiff_at I I' n f x) (hn : 1 ‚â§ n) :
  mdifferentiable_at I I' f x :=
mdifferentiable_within_at_univ.1 $ cont_mdiff_within_at.mdifferentiable_within_at hf hn

lemma cont_mdiff_on.mdifferentiable_on (hf : cont_mdiff_on I I' n f s) (hn : 1 ‚â§ n) :
  mdifferentiable_on I I' f s :=
Œª x hx, (hf x hx).mdifferentiable_within_at hn

lemma cont_mdiff.mdifferentiable (hf : cont_mdiff I I' n f) (hn : 1 ‚â§ n) :
  mdifferentiable I I' f :=
Œª x, (hf x).mdifferentiable_at hn

lemma smooth_within_at.mdifferentiable_within_at
  (hf : smooth_within_at I I' f s x) : mdifferentiable_within_at I I' f s x :=
hf.mdifferentiable_within_at le_top

lemma smooth_at.mdifferentiable_at (hf : smooth_at I I' f x) : mdifferentiable_at I I' f x :=
hf.mdifferentiable_at le_top

lemma smooth_on.mdifferentiable_on (hf : smooth_on I I' f s) : mdifferentiable_on I I' f s :=
hf.mdifferentiable_on le_top

lemma smooth.mdifferentiable (hf : smooth I I' f) : mdifferentiable I I' f :=
cont_mdiff.mdifferentiable hf le_top

lemma smooth.mdifferentiable_at (hf : smooth I I' f) : mdifferentiable_at I I' f x :=
hf.mdifferentiable x

lemma smooth.mdifferentiable_within_at (hf : smooth I I' f) :
  mdifferentiable_within_at I I' f s x :=
hf.mdifferentiable_at.mdifferentiable_within_at

/-! ### The derivative of a smooth function is smooth -/

section mfderiv

include Is I's Js

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` is `C^n` at `x‚ÇÄ`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(x‚ÇÄ, g(x‚ÇÄ))` and `g` is `C^n` at `x‚ÇÄ`.
We have to insert a coordinate change from `x‚ÇÄ` to `x` to make the derivative sensible.
`cont_mdiff_at.mfderiv_id` and `cont_mdiff_at.mfderiv_const` are special cases of this.

This lemma should be generalized to a `cont_mdiff_within_at` for `mfderiv_within`. If we do that, we
can deduce `cont_mdiff_on.cont_mdiff_on_tangent_map_within` from this.
-/
lemma cont_mdiff_at.mfderiv {x‚ÇÄ : N} (f : N ‚Üí M ‚Üí M') (g : N ‚Üí M)
  (hf : cont_mdiff_at (J.prod I) I' n (function.uncurry f) (x‚ÇÄ, g x‚ÇÄ))
  (hg : cont_mdiff_at J I m g x‚ÇÄ) (hmn : m + 1 ‚â§ n) :
  cont_mdiff_at J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
    (in_tangent_coordinates I I' g (Œª x, f x (g x)) (Œª x, mfderiv I I' (f x) (g x)) x‚ÇÄ) x‚ÇÄ :=
begin
  have h4f : continuous_at (Œª x, f x (g x)) x‚ÇÄ,
  { apply continuous_at.comp (by apply hf.continuous_at) (continuous_at_id.prod hg.continuous_at) },
  have h4f := h4f.preimage_mem_nhds (ext_chart_at_source_mem_nhds I' (f x‚ÇÄ (g x‚ÇÄ))),
  have h3f := cont_mdiff_at_iff_cont_mdiff_at_nhds.mp (hf.of_le $ (self_le_add_left 1 m).trans hmn),
  have h2f : ‚àÄ·∂† x‚ÇÇ in ùìù x‚ÇÄ, cont_mdiff_at I I' 1 (f x‚ÇÇ) (g x‚ÇÇ),
  { refine ((continuous_at_id.prod hg.continuous_at).tendsto.eventually h3f).mono (Œª x hx, _),
    exact hx.comp (g x) (cont_mdiff_at_const.prod_mk cont_mdiff_at_id) },
  have h2g := hg.continuous_at.preimage_mem_nhds (ext_chart_at_source_mem_nhds I (g x‚ÇÄ)),
  have : cont_diff_within_at ùïú m (Œª x, fderiv_within ùïú
    (ext_chart_at I' (f x‚ÇÄ (g x‚ÇÄ)) ‚àò f ((ext_chart_at J x‚ÇÄ).symm x) ‚àò (ext_chart_at I (g x‚ÇÄ)).symm)
    (range I) (ext_chart_at I (g x‚ÇÄ) (g ((ext_chart_at J x‚ÇÄ).symm x))))
    (range J) (ext_chart_at J x‚ÇÄ x‚ÇÄ),
  { rw [cont_mdiff_at_iff] at hf hg,
    simp_rw [function.comp, uncurry, ext_chart_at_prod, local_equiv.prod_coe_symm,
      model_with_corners.range_prod] at hf ‚ä¢,
    refine cont_diff_within_at.fderiv_within _ hg.2 I.unique_diff hmn (mem_range_self _) _,
    { simp_rw [ext_chart_at_to_inv], exact hf.2 },
    { rw [‚Üê image_subset_iff],
      rintros _ ‚ü®x, hx, rfl‚ü©,
      exact mem_range_self _ } },
  have : cont_mdiff_at J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
    (Œª x, fderiv_within ùïú (ext_chart_at I' (f x‚ÇÄ (g x‚ÇÄ)) ‚àò f x ‚àò (ext_chart_at I (g x‚ÇÄ)).symm)
    (range I) (ext_chart_at I (g x‚ÇÄ) (g x))) x‚ÇÄ,
  { simp_rw [cont_mdiff_at_iff_source_of_mem_source (mem_chart_source G x‚ÇÄ),
      cont_mdiff_within_at_iff_cont_diff_within_at, function.comp],
    exact this },
  have : cont_mdiff_at J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
    (Œª x, fderiv_within ùïú (ext_chart_at I' (f x‚ÇÄ (g x‚ÇÄ)) ‚àò (ext_chart_at I' (f x (g x))).symm ‚àò
      written_in_ext_chart_at I I' (g x) (f x) ‚àò ext_chart_at I (g x) ‚àò
      (ext_chart_at I (g x‚ÇÄ)).symm) (range I) (ext_chart_at I (g x‚ÇÄ) (g x))) x‚ÇÄ,
  { refine this.congr_of_eventually_eq _,
    filter_upwards [h2g, h2f],
    intros x‚ÇÇ hx‚ÇÇ h2x‚ÇÇ,
    have : ‚àÄ x ‚àà (ext_chart_at I (g x‚ÇÄ)).symm ‚Åª¬π' (ext_chart_at I (g x‚ÇÇ)).source ‚à©
        (ext_chart_at I (g x‚ÇÄ)).symm ‚Åª¬π' (f x‚ÇÇ ‚Åª¬π' (ext_chart_at I' (f x‚ÇÇ (g x‚ÇÇ))).source),
      (ext_chart_at I' (f x‚ÇÄ (g x‚ÇÄ)) ‚àò (ext_chart_at I' (f x‚ÇÇ (g x‚ÇÇ))).symm ‚àò
      written_in_ext_chart_at I I' (g x‚ÇÇ) (f x‚ÇÇ) ‚àò ext_chart_at I (g x‚ÇÇ) ‚àò
      (ext_chart_at I (g x‚ÇÄ)).symm) x =
      ext_chart_at I' (f x‚ÇÄ (g x‚ÇÄ)) (f x‚ÇÇ ((ext_chart_at I (g x‚ÇÄ)).symm x)),
    { rintro x ‚ü®hx, h2x‚ü©,
      simp_rw [written_in_ext_chart_at, function.comp_apply],
      rw [(ext_chart_at I (g x‚ÇÇ)).left_inv hx, (ext_chart_at I' (f x‚ÇÇ (g x‚ÇÇ))).left_inv h2x] },
    refine filter.eventually_eq.fderiv_within_eq_nhds (I.unique_diff _ $ mem_range_self _) _,
    refine eventually_of_mem (inter_mem _ _) this,
    { exact ext_chart_at_preimage_mem_nhds' _ _ hx‚ÇÇ (ext_chart_at_source_mem_nhds I (g x‚ÇÇ)) },
    refine ext_chart_at_preimage_mem_nhds' _ _ hx‚ÇÇ _,
    exact (h2x‚ÇÇ.continuous_at).preimage_mem_nhds (ext_chart_at_source_mem_nhds _ _) },
  /- The conclusion is equal to the following, when unfolding coord_change of
    `tangent_bundle_core` -/
  have : cont_mdiff_at J ùìò(ùïú, E ‚ÜíL[ùïú] E') m
    (Œª x, (fderiv_within ùïú (ext_chart_at I' (f x‚ÇÄ (g x‚ÇÄ)) ‚àò (ext_chart_at I' (f x (g x))).symm)
        (range I') (ext_chart_at I' (f x (g x)) (f x (g x)))).comp
        ((mfderiv I I' (f x) (g x)).comp (fderiv_within ùïú (ext_chart_at I (g x) ‚àò
        (ext_chart_at I (g x‚ÇÄ)).symm) (range I) (ext_chart_at I (g x‚ÇÄ) (g x))))) x‚ÇÄ,
  { refine this.congr_of_eventually_eq _,
    filter_upwards [h2g, h2f, h4f],
    intros x‚ÇÇ hx‚ÇÇ h2x‚ÇÇ h3x‚ÇÇ,
    symmetry,
    rw [(h2x‚ÇÇ.mdifferentiable_at le_rfl).mfderiv],
    have hI := (cont_diff_within_at_ext_coord_change I (g x‚ÇÇ) (g x‚ÇÄ) $
      local_equiv.mem_symm_trans_source _ hx‚ÇÇ $ mem_ext_chart_source I (g x‚ÇÇ))
      .differentiable_within_at le_top,
    have hI' := (cont_diff_within_at_ext_coord_change I' (f x‚ÇÄ (g x‚ÇÄ)) (f x‚ÇÇ (g x‚ÇÇ)) $
      local_equiv.mem_symm_trans_source _
      (mem_ext_chart_source I' (f x‚ÇÇ (g x‚ÇÇ))) h3x‚ÇÇ).differentiable_within_at le_top,
    have h3f := (h2x‚ÇÇ.mdifferentiable_at le_rfl).2,
    refine fderiv_within.comp‚ÇÉ _ hI' h3f hI _ _ _ _ (I.unique_diff _ $ mem_range_self _),
    { exact Œª x _, mem_range_self _ },
    { exact Œª x _, mem_range_self _ },
    { simp_rw [written_in_ext_chart_at, function.comp_apply,
        (ext_chart_at I (g x‚ÇÇ)).left_inv (mem_ext_chart_source I (g x‚ÇÇ))] },
    { simp_rw [function.comp_apply, (ext_chart_at I (g x‚ÇÄ)).left_inv hx‚ÇÇ] } },
  refine this.congr_of_eventually_eq _,
  filter_upwards [h2g, h4f] with x hx h2x,
  rw [in_tangent_coordinates_eq],
  { refl },
  { rwa [ext_chart_at_source] at hx },
  { rwa [ext_chart_at_source] at h2x },
end

omit Js

/-- The function `x ‚Ü¶ D_yf(x,y)` is `C^n` at `x‚ÇÄ`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^(n+1)` at `(x‚ÇÄ, x‚ÇÄ)`.
We have to insert a coordinate change from `x‚ÇÄ` to `x` to make the derivative sensible.
This is a special case of `cont_mdiff_at.mfderiv` (with `g = id`),
and `cont_mdiff_at.mfderiv_const` is a special case of this.
-/
lemma cont_mdiff_at.mfderiv_id {x‚ÇÄ : M} (f : M ‚Üí M ‚Üí M')
  (hf : cont_mdiff_at (I.prod I) I' n (function.uncurry f) (x‚ÇÄ, x‚ÇÄ)) (hmn : m + 1 ‚â§ n) :
  cont_mdiff_at I ùìò(ùïú, E ‚ÜíL[ùïú] E') m
    (in_tangent_coordinates I I' id (Œª x, f x x) (Œª x, mfderiv I I' (f x) x) x‚ÇÄ) x‚ÇÄ :=
hf.mfderiv f id cont_mdiff_at_id hmn

/-- The derivative `D_yf(y)` is `C^n` at `x‚ÇÄ`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^(n+1)` at `x‚ÇÄ`.
We have to insert a coordinate change from `x‚ÇÄ` to `x` to make the derivative sensible.
This is a special case of See `cont_mdiff_at.mfderiv_id` where `f` does not contain any parameters.
-/
lemma cont_mdiff_at.mfderiv_const {x‚ÇÄ : M} {f : M ‚Üí M'}
  (hf : cont_mdiff_at I I' n f x‚ÇÄ) (hmn : m + 1 ‚â§ n) :
  cont_mdiff_at I ùìò(ùïú, E ‚ÜíL[ùïú] E') m (in_tangent_coordinates I I' id f (mfderiv I I' f) x‚ÇÄ) x‚ÇÄ :=
begin
  have : cont_mdiff_at (I.prod I) I' n (Œª x : M √ó M, f x.2) (x‚ÇÄ, x‚ÇÄ) :=
  cont_mdiff_at.comp (x‚ÇÄ, x‚ÇÄ) hf cont_mdiff_at_snd,
  apply cont_mdiff_at.mfderiv_id (Œª x, f) this hmn,
end

end mfderiv

/-! ### The tangent map of a smooth function is smooth -/

section tangent_map

/-- If a function is `C^n` with `1 ‚â§ n` on a domain with unique derivatives, then its bundled
derivative is continuous. In this auxiliary lemma, we prove this fact when the source and target
space are model spaces in models with corners. The general fact is proved in
`cont_mdiff_on.continuous_on_tangent_map_within`-/
lemma cont_mdiff_on.continuous_on_tangent_map_within_aux
  {f : H ‚Üí H'} {s : set H}
  (hf : cont_mdiff_on I I' n f s) (hn : 1 ‚â§ n) (hs : unique_mdiff_on I s) :
  continuous_on (tangent_map_within I I' f s) (œÄ (tangent_space I) ‚Åª¬π' s) :=
begin
  suffices h : continuous_on (Œª (p : H √ó E), (f p.fst,
    (fderiv_within ùïú (written_in_ext_chart_at I I' p.fst f) (I.symm ‚Åª¬π' s ‚à© range I)
      ((ext_chart_at I p.fst) p.fst) : E ‚ÜíL[ùïú] E') p.snd)) (prod.fst ‚Åª¬π' s),
  { have A := (tangent_bundle_model_space_homeomorph H I).continuous,
    rw continuous_iff_continuous_on_univ at A,
    have B := ((tangent_bundle_model_space_homeomorph H' I').symm.continuous.comp_continuous_on h)
      .comp' A,
    have : (univ ‚à© ‚áë(tangent_bundle_model_space_homeomorph H I) ‚Åª¬π' (prod.fst ‚Åª¬π' s)) =
      œÄ (tangent_space I) ‚Åª¬π' s,
      by { ext ‚ü®x, v‚ü©, simp only with mfld_simps },
    rw this at B,
    apply B.congr,
    rintros ‚ü®x, v‚ü© hx,
    dsimp [tangent_map_within],
    ext, { refl },
    simp only with mfld_simps,
    apply congr_fun,
    apply congr_arg,
    rw mdifferentiable_within_at.mfderiv_within (hf.mdifferentiable_on hn x hx),
    refl },
  suffices h : continuous_on (Œª (p : H √ó E), (fderiv_within ùïú (I' ‚àò f ‚àò I.symm)
    (I.symm ‚Åª¬π' s ‚à© range I) (I p.fst) : E ‚ÜíL[ùïú] E') p.snd) (prod.fst ‚Åª¬π' s),
  { dsimp [written_in_ext_chart_at, ext_chart_at],
    apply continuous_on.prod
      (continuous_on.comp hf.continuous_on continuous_fst.continuous_on (subset.refl _)),
    apply h.congr,
    assume p hp,
    refl },
  suffices h : continuous_on (fderiv_within ùïú (I' ‚àò f ‚àò I.symm)
                     (I.symm ‚Åª¬π' s ‚à© range I)) (I '' s),
  { have C := continuous_on.comp h I.continuous_to_fun.continuous_on (subset.refl _),
    have A : continuous (Œªq : (E ‚ÜíL[ùïú] E') √ó E, q.1 q.2) :=
      is_bounded_bilinear_map_apply.continuous,
    have B : continuous_on (Œªp : H √ó E,
      (fderiv_within ùïú (I' ‚àò f ‚àò I.symm) (I.symm ‚Åª¬π' s ‚à© range I)
                       (I p.1), p.2)) (prod.fst ‚Åª¬π' s),
    { apply continuous_on.prod _ continuous_snd.continuous_on,
      refine (continuous_on.comp C continuous_fst.continuous_on _ : _),
      exact preimage_mono (subset_preimage_image _ _) },
    exact A.comp_continuous_on B },
  rw cont_mdiff_on_iff at hf,
  let x : H := I.symm (0 : E),
  let y : H' := I'.symm (0 : E'),
  have A := hf.2 x y,
  simp only [I.image_eq, inter_comm] with mfld_simps at A ‚ä¢,
  apply A.continuous_on_fderiv_within _ hn,
  convert hs.unique_diff_on_target_inter x using 1,
  simp only [inter_comm] with mfld_simps
end

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative is
`C^m` when `m+1 ‚â§ n`. In this auxiliary lemma, we prove this fact when the source and target space
are model spaces in models with corners. The general fact is proved in
`cont_mdiff_on.cont_mdiff_on_tangent_map_within` -/
lemma cont_mdiff_on.cont_mdiff_on_tangent_map_within_aux
  {f : H ‚Üí H'} {s : set H}
  (hf : cont_mdiff_on I I' n f s) (hmn : m + 1 ‚â§ n) (hs : unique_mdiff_on I s) :
  cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s) (œÄ (tangent_space I) ‚Åª¬π' s) :=
begin
  have m_le_n : m ‚â§ n,
  { apply le_trans _ hmn,
    have : m + 0 ‚â§ m + 1 := add_le_add_left (zero_le _) _,
    simpa only [add_zero] using this },
  have one_le_n : 1 ‚â§ n,
  { apply le_trans _ hmn,
    change 0 + 1 ‚â§ m + 1,
    exact add_le_add_right (zero_le _) _ },
  have U': unique_diff_on ùïú (range I ‚à© I.symm ‚Åª¬π' s),
  { assume y hy,
    simpa only [unique_mdiff_on, unique_mdiff_within_at, hy.1, inter_comm] with mfld_simps
      using hs (I.symm y) hy.2 },
  rw cont_mdiff_on_iff,
  refine ‚ü®hf.continuous_on_tangent_map_within_aux one_le_n hs, Œªp q, _‚ü©,
  have A : range I √óÀ¢ univ ‚à©
      ((equiv.sigma_equiv_prod H E).symm ‚àò Œª (p : E √ó E), ((I.symm) p.fst, p.snd)) ‚Åª¬π'
        (œÄ (tangent_space I) ‚Åª¬π' s)
      = (range I ‚à© I.symm ‚Åª¬π' s) √óÀ¢ univ,
    by { ext ‚ü®x, v‚ü©, simp only with mfld_simps },
  suffices h : cont_diff_on ùïú m (((Œª (p : H' √ó E'), (I' p.fst, p.snd)) ‚àò
      (equiv.sigma_equiv_prod H' E')) ‚àò tangent_map_within I I' f s ‚àò
      ((equiv.sigma_equiv_prod H E).symm) ‚àò Œª (p : E √ó E), (I.symm p.fst, p.snd))
    ((range ‚áëI ‚à© ‚áë(I.symm) ‚Åª¬π' s) √óÀ¢ univ),
    by simpa [A] using h,
  change cont_diff_on ùïú m (Œª (p : E √ó E),
    ((I' (f (I.symm p.fst)), ((mfderiv_within I I' f s (I.symm p.fst)) : E ‚Üí E') p.snd) : E' √ó E'))
    ((range I ‚à© I.symm ‚Åª¬π' s) √óÀ¢ univ),
  -- check that all bits in this formula are `C^n`
  have hf' := cont_mdiff_on_iff.1 hf,
  have A : cont_diff_on ùïú m (I' ‚àò f ‚àò I.symm) (range I ‚à© I.symm ‚Åª¬π' s) :=
    by simpa only with mfld_simps using (hf'.2 (I.symm 0) (I'.symm 0)).of_le m_le_n,
  have B : cont_diff_on ùïú m ((I' ‚àò f ‚àò I.symm) ‚àò prod.fst)
           ((range I ‚à© I.symm ‚Åª¬π' s) √óÀ¢ univ) :=
    A.comp (cont_diff_fst.cont_diff_on) (prod_subset_preimage_fst _ _),
  suffices C : cont_diff_on ùïú m (Œª (p : E √ó E),
    ((fderiv_within ùïú (I' ‚àò f ‚àò I.symm) (I.symm ‚Åª¬π' s ‚à© range I) p.1 : _) p.2))
    ((range I ‚à© I.symm ‚Åª¬π' s) √óÀ¢ univ),
  { apply cont_diff_on.prod B _,
    apply C.congr (Œªp hp, _),
    simp only with mfld_simps at hp,
    simp only [mfderiv_within, hf.mdifferentiable_on one_le_n _ hp.2, hp.1, if_pos]
      with mfld_simps },
  have D : cont_diff_on ùïú m (Œª x,
    (fderiv_within ùïú (I' ‚àò f ‚àò I.symm) (I.symm ‚Åª¬π' s ‚à© range I) x))
    (range I ‚à© I.symm ‚Åª¬π' s),
  { have : cont_diff_on ùïú n (I' ‚àò f ‚àò I.symm) (range I ‚à© I.symm ‚Åª¬π' s) :=
      by simpa only with mfld_simps using (hf'.2 (I.symm 0) (I'.symm 0)),
    simpa only [inter_comm] using this.fderiv_within U' hmn },
  have := D.comp (cont_diff_fst.cont_diff_on) (prod_subset_preimage_fst _ _),
  have := cont_diff_on.prod this (cont_diff_snd.cont_diff_on),
  exact is_bounded_bilinear_map_apply.cont_diff.comp_cont_diff_on this,
end

include Is I's

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative
is `C^m` when `m+1 ‚â§ n`. -/
theorem cont_mdiff_on.cont_mdiff_on_tangent_map_within
  (hf : cont_mdiff_on I I' n f s) (hmn : m + 1 ‚â§ n) (hs : unique_mdiff_on I s) :
  cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s)
  (œÄ (tangent_space I) ‚Åª¬π' s) :=
begin
  /- The strategy of the proof is to avoid unfolding the definitions, and reduce by functoriality
  to the case of functions on the model spaces, where we have already proved the result.
  Let `l` and `r` be the charts to the left and to the right, so that we have
  ```
     l^{-1}      f       r
  H --------> M ---> M' ---> H'
  ```
  Then the tangent map `T(r ‚àò f ‚àò l)` is smooth by a previous result. Consider the composition
  ```
      Tl        T(r ‚àò f ‚àò l^{-1})         Tr^{-1}
  TM -----> TH -------------------> TH' ---------> TM'
  ```
  where `Tr^{-1}` and `Tl` are the tangent maps of `r^{-1}` and `l`. Writing `Tl` and `Tr^{-1}` as
  composition of charts (called `Dl` and `il` for `l` and `Dr` and `ir` in the proof below), it
  follows that they are smooth. The composition of all these maps is `Tf`, and is therefore smooth
  as a composition of smooth maps.
  -/
  have m_le_n : m ‚â§ n,
  { apply le_trans _ hmn,
    have : m + 0 ‚â§ m + 1 := add_le_add_left (zero_le _) _,
    simpa only [add_zero] },
  have one_le_n : 1 ‚â§ n,
  { apply le_trans _ hmn,
    change 0 + 1 ‚â§ m + 1,
    exact add_le_add_right (zero_le _) _ },
  /- First step: local reduction on the space, to a set `s'` which is contained in chart domains. -/
  refine cont_mdiff_on_of_locally_cont_mdiff_on (Œªp hp, _),
  have hf' := cont_mdiff_on_iff.1 hf,
  simp only with mfld_simps at hp,
  let l  := chart_at H p.proj,
  set Dl := chart_at (model_prod H E) p with hDl,
  let r  := chart_at H' (f p.proj),
  let Dr := chart_at (model_prod H' E') (tangent_map_within I I' f s p),
  let il := chart_at (model_prod H E) (tangent_map I I l p),
  let ir := chart_at (model_prod H' E') (tangent_map I I' (r ‚àò f) p),
  let s' := f ‚Åª¬π' r.source ‚à© s ‚à© l.source,
  let s'_lift := œÄ (tangent_space I) ‚Åª¬π' s',
  let s'l := l.target ‚à© l.symm ‚Åª¬π' s',
  let s'l_lift := œÄ (tangent_space I) ‚Åª¬π' s'l,
  rcases continuous_on_iff'.1 hf'.1 r.source r.open_source with ‚ü®o, o_open, ho‚ü©,
  suffices h : cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s) s'_lift,
  { refine ‚ü®œÄ (tangent_space I) ‚Åª¬π' (o ‚à© l.source), _, _, _‚ü©,
    show is_open (œÄ (tangent_space I) ‚Åª¬π' (o ‚à© l.source)), from
      (is_open.inter o_open l.open_source).preimage (continuous_proj E _) ,
    show p ‚àà œÄ (tangent_space I) ‚Åª¬π' (o ‚à© l.source),
    { simp,
      have : p.proj ‚àà f ‚Åª¬π' r.source ‚à© s, by simp [hp],
      rw ho at this,
      exact this.1 },
    { have : œÄ (tangent_space I) ‚Åª¬π' s ‚à© œÄ (tangent_space I) ‚Åª¬π' (o ‚à© l.source) = s'_lift,
      { dsimp only [s'_lift, s'], rw [ho], mfld_set_tac },
      rw this,
      exact h } },
  /- Second step: check that all functions are smooth, and use the chain rule to write the bundled
  derivative as a composition of a function between model spaces and of charts.
  Convention: statements about the differentiability of `a ‚àò b ‚àò c` are named `diff_abc`. Statements
  about differentiability in the bundle have a `_lift` suffix. -/
  have U' : unique_mdiff_on I s',
  { apply unique_mdiff_on.inter _ l.open_source,
    rw [ho, inter_comm],
    exact hs.inter o_open },
  have U'l : unique_mdiff_on I s'l :=
    U'.unique_mdiff_on_preimage (mdifferentiable_chart _ _),
  have diff_f : cont_mdiff_on I I' n f s' :=
    hf.mono (by mfld_set_tac),
  have diff_r : cont_mdiff_on I' I' n r r.source :=
    cont_mdiff_on_chart,
  have diff_rf : cont_mdiff_on I I' n (r ‚àò f) s',
  { apply cont_mdiff_on.comp diff_r diff_f (Œªx hx, _),
    simp only [s'] with mfld_simps at hx, simp only [hx] with mfld_simps },
  have diff_l : cont_mdiff_on I I n l.symm s'l,
  { have A : cont_mdiff_on I I n l.symm l.target :=
      cont_mdiff_on_chart_symm,
    exact A.mono (by mfld_set_tac) },
  have diff_rfl : cont_mdiff_on I I' n (r ‚àò f ‚àò l.symm) s'l,
  { apply cont_mdiff_on.comp diff_rf diff_l,
    mfld_set_tac },
  have diff_rfl_lift : cont_mdiff_on I.tangent I'.tangent m
      (tangent_map_within I I' (r ‚àò f ‚àò l.symm) s'l) s'l_lift :=
    diff_rfl.cont_mdiff_on_tangent_map_within_aux hmn U'l,
  have diff_irrfl_lift : cont_mdiff_on I.tangent I'.tangent m
      (ir ‚àò (tangent_map_within I I' (r ‚àò f ‚àò l.symm) s'l)) s'l_lift,
  { have A : cont_mdiff_on I'.tangent I'.tangent m ir ir.source := cont_mdiff_on_chart,
    exact cont_mdiff_on.comp A diff_rfl_lift (Œªp hp, by simp only [ir] with mfld_simps) },
  have diff_Drirrfl_lift : cont_mdiff_on I.tangent I'.tangent m
    (Dr.symm ‚àò (ir ‚àò (tangent_map_within I I' (r ‚àò f ‚àò l.symm) s'l))) s'l_lift,
  { have A : cont_mdiff_on I'.tangent I'.tangent m Dr.symm Dr.target :=
      cont_mdiff_on_chart_symm,
    apply cont_mdiff_on.comp A diff_irrfl_lift (Œªp hp, _),
    simp only [s'l_lift] with mfld_simps at hp,
    simp only [ir, hp] with mfld_simps },
  -- conclusion of this step: the composition of all the maps above is smooth
  have diff_DrirrflilDl : cont_mdiff_on I.tangent I'.tangent m
    (Dr.symm ‚àò (ir ‚àò (tangent_map_within I I' (r ‚àò f ‚àò l.symm) s'l)) ‚àò
      (il.symm ‚àò Dl)) s'_lift,
  { have A : cont_mdiff_on I.tangent I.tangent m Dl Dl.source := cont_mdiff_on_chart,
    have A' : cont_mdiff_on I.tangent I.tangent m Dl s'_lift,
    { apply A.mono (Œªp hp, _),
      simp only [s'_lift] with mfld_simps at hp,
      simp only [Dl, hp] with mfld_simps },
    have B : cont_mdiff_on I.tangent I.tangent m il.symm il.target :=
      cont_mdiff_on_chart_symm,
    have C : cont_mdiff_on I.tangent I.tangent m (il.symm ‚àò Dl) s'_lift :=
      cont_mdiff_on.comp B A' (Œªp hp, by simp only [il] with mfld_simps),
    apply cont_mdiff_on.comp diff_Drirrfl_lift C (Œªp hp, _),
    simp only [s'_lift] with mfld_simps at hp,
    simp only [il, s'l_lift, hp, total_space.proj] with mfld_simps },
  /- Third step: check that the composition of all the maps indeed coincides with the derivative we
  are looking for -/
  have eq_comp : ‚àÄq ‚àà s'_lift, tangent_map_within I I' f s q =
      (Dr.symm ‚àò ir ‚àò (tangent_map_within I I' (r ‚àò f ‚àò l.symm) s'l) ‚àò
      (il.symm ‚àò Dl)) q,
  { assume q hq,
    simp only [s'_lift] with mfld_simps at hq,
    have U'q : unique_mdiff_within_at I s' q.1,
      by { apply U', simp only [hq, s'] with mfld_simps },
    have U'lq : unique_mdiff_within_at I s'l (Dl q).1,
      by { apply U'l, simp only [hq, s'l] with mfld_simps },
    have A : tangent_map_within I I' ((r ‚àò f) ‚àò l.symm) s'l (il.symm (Dl q)) =
      tangent_map_within I I' (r ‚àò f) s' (tangent_map_within I I l.symm s'l (il.symm (Dl q))),
    { refine tangent_map_within_comp_at (il.symm (Dl q)) _ _ (Œªp hp, _) U'lq,
      { apply diff_rf.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { apply diff_l.mdifferentiable_on one_le_n,
        simp only [s'l, hq] with mfld_simps },
      { simp only with mfld_simps at hp, simp only [hp] with mfld_simps } },
    have B : tangent_map_within I I l.symm s'l (il.symm (Dl q)) = q,
    { have : tangent_map_within I I l.symm s'l (il.symm (Dl q))
        = tangent_map I I l.symm (il.symm (Dl q)),
      { refine tangent_map_within_eq_tangent_map U'lq _,
        refine mdifferentiable_at_atlas_symm _ (chart_mem_atlas _ _) _,
        simp only [hq] with mfld_simps },
      rw [this, tangent_map_chart_symm, hDl],
      { simp only [hq] with mfld_simps,
        have : q ‚àà (chart_at (model_prod H E) p).source, { simp only [hq] with mfld_simps },
        exact (chart_at (model_prod H E) p).left_inv this },
      { simp only [hq] with mfld_simps } },
    have C : tangent_map_within I I' (r ‚àò f) s' q
      = tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q),
    { refine tangent_map_within_comp_at q _ _ (Œªr hr, _) U'q,
      { apply diff_r.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { apply diff_f.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { simp only [s'] with mfld_simps at hr,
        simp only [hr] with mfld_simps } },
    have D : Dr.symm (ir (tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q)))
      = tangent_map_within I I' f s' q,
    { have A : tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q) =
             tangent_map I' I' r (tangent_map_within I I' f s' q),
      { apply tangent_map_within_eq_tangent_map,
        { apply is_open.unique_mdiff_within_at _ r.open_source, simp [hq] },
        { refine mdifferentiable_at_atlas _ (chart_mem_atlas _ _) _,
          simp only [hq] with mfld_simps } },
      have : f p.proj = (tangent_map_within I I' f s p).1 := rfl,
      rw [A],
      dsimp [r, Dr],
      rw [this, tangent_map_chart],
      { simp only [hq] with mfld_simps,
        have : tangent_map_within I I' f s' q ‚àà
          (chart_at (model_prod H' E') (tangent_map_within I I' f s p)).source,
            by { simp only [hq] with mfld_simps },
        exact (chart_at (model_prod H' E') (tangent_map_within I I' f s p)).left_inv this },
      { simp only [hq] with mfld_simps } },
    have E : tangent_map_within I I' f s' q = tangent_map_within I I' f s q,
    { refine tangent_map_within_subset (by mfld_set_tac) U'q _,
      apply hf.mdifferentiable_on one_le_n,
      simp only [hq] with mfld_simps },
    simp only [(‚àò), A, B, C, D, E.symm] },
  exact diff_DrirrflilDl.congr eq_comp,
end

/-- If a function is `C^n` on a domain with unique derivatives, with `1 ‚â§ n`, then its bundled
derivative is continuous there. -/
theorem cont_mdiff_on.continuous_on_tangent_map_within
  (hf : cont_mdiff_on I I' n f s) (hmn : 1 ‚â§ n) (hs : unique_mdiff_on I s) :
  continuous_on (tangent_map_within I I' f s) (œÄ (tangent_space I) ‚Åª¬π' s) :=
begin
  have : cont_mdiff_on I.tangent I'.tangent 0 (tangent_map_within I I' f s)
         (œÄ (tangent_space I) ‚Åª¬π' s) :=
    hf.cont_mdiff_on_tangent_map_within hmn hs,
  exact this.continuous_on
end

/-- If a function is `C^n`, then its bundled derivative is `C^m` when `m+1 ‚â§ n`. -/
theorem cont_mdiff.cont_mdiff_tangent_map
  (hf : cont_mdiff I I' n f) (hmn : m + 1 ‚â§ n) :
  cont_mdiff I.tangent I'.tangent m (tangent_map I I' f) :=
begin
  rw ‚Üê cont_mdiff_on_univ at hf ‚ä¢,
  convert hf.cont_mdiff_on_tangent_map_within hmn unique_mdiff_on_univ,
  rw tangent_map_within_univ
end

/-- If a function is `C^n`, with `1 ‚â§ n`, then its bundled derivative is continuous. -/
theorem cont_mdiff.continuous_tangent_map
  (hf : cont_mdiff I I' n f) (hmn : 1 ‚â§ n) :
  continuous (tangent_map I I' f) :=
begin
  rw ‚Üê cont_mdiff_on_univ at hf,
  rw continuous_iff_continuous_on_univ,
  convert hf.continuous_on_tangent_map_within hmn unique_mdiff_on_univ,
  rw tangent_map_within_univ
end

end tangent_map

namespace tangent_bundle

include Is
variables (I M)
open bundle

/-- The derivative of the zero section of the tangent bundle maps `‚ü®x, v‚ü©` to `‚ü®‚ü®x, 0‚ü©, ‚ü®v, 0‚ü©‚ü©`.

Note that, as currently framed, this is a statement in coordinates, thus reliant on the choice
of the coordinate system we use on the tangent bundle.

However, the result itself is coordinate-dependent only to the extent that the coordinates
determine a splitting of the tangent bundle.  Moreover, there is a canonical splitting at each
point of the zero section (since there is a canonical horizontal space there, the tangent space
to the zero section, in addition to the canonical vertical space which is the kernel of the
derivative of the projection), and this canonical splitting is also the one that comes from the
coordinates on the tangent bundle in our definitions. So this statement is not as crazy as it
may seem.

TODO define splittings of vector bundles; state this result invariantly. -/
lemma tangent_map_tangent_bundle_pure (p : tangent_bundle I M) :
  tangent_map I I.tangent (zero_section (tangent_space I)) p = ‚ü®‚ü®p.proj, 0‚ü©, ‚ü®p.2, 0‚ü©‚ü© :=
begin
  rcases p with ‚ü®x, v‚ü©,
  have N : I.symm ‚Åª¬π' (chart_at H x).target ‚àà ùìù (I ((chart_at H x) x)),
  { apply is_open.mem_nhds,
    apply (local_homeomorph.open_target _).preimage I.continuous_inv_fun,
    simp only with mfld_simps },
  have A : mdifferentiable_at I I.tangent (Œª x, @total_space_mk M (tangent_space I) x 0) x,
  { have : smooth I (I.prod ùìò(ùïú, E)) (zero_section (tangent_space I : M ‚Üí Type*)) :=
    bundle.smooth_zero_section ùïú (tangent_space I : M ‚Üí Type*),
    exact this.mdifferentiable_at },
  have B : fderiv_within ùïú (Œª (x' : E), (x', (0 : E))) (set.range ‚áëI) (I ((chart_at H x) x)) v
    = (v, 0),
  { rw [fderiv_within_eq_fderiv, differentiable_at.fderiv_prod],
    { simp },
    { exact differentiable_at_id' },
    { exact differentiable_at_const _ },
    { exact model_with_corners.unique_diff_at_image I },
    { exact differentiable_at_id'.prod (differentiable_at_const _) } },
  simp only [bundle.zero_section, tangent_map, mfderiv, total_space.proj_mk, A,
    if_pos, chart_at, fiber_bundle.charted_space_chart_at, tangent_bundle.trivialization_at_apply,
    tangent_bundle_core, function.comp, continuous_linear_map.map_zero] with mfld_simps,
  rw ‚Üê fderiv_within_inter N (I.unique_diff (I ((chart_at H x) x)) (set.mem_range_self _)) at B,
  rw [‚Üê fderiv_within_inter N (I.unique_diff (I ((chart_at H x) x)) (set.mem_range_self _)), ‚Üê B],
  congr' 2,
  apply fderiv_within_congr _ (Œª y hy, _),
  { simp only [prod.mk.inj_iff] with mfld_simps },
  { apply unique_diff_within_at.inter (I.unique_diff _ _) N,
    simp only with mfld_simps },
  { simp only with mfld_simps at hy,
    simp only [hy, prod.mk.inj_iff] with mfld_simps },
end

end tangent_bundle
