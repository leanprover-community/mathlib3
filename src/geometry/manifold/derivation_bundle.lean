/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/

import geometry.manifold.algebra.smooth_functions
import ring_theory.derivation

/-!

# Derivation bundle

In this file we define the derivations at a point of a manifold on the algebra of smooth fuctions.
Moreover we define the total bundle of derivations (although at the moment it has not been given a
topology). Finally we define the differential of a function in terms of derivations.

The content of this file is not meant to be regarded as an alternative definition to the current
tangent bundle but rather as a purely algebraic theory that provides a purely algebraic definition
of the Lie algebra for a Lie group.

-/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
(M : Type*) [topological_space M] [charted_space H M]

open_locale manifold

namespace point_derivation

instance smooth_functions_algebra : algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ := by apply_instance
instance smooth_functions_tower : is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯ := by apply_instance

/-- The scalar multiplication depends on the point `x : M`. -/
def has_scalar (x : M) : has_scalar C^âˆâŸ®I, M; ğ•œâŸ¯ ğ•œ :=
{ smul := Î» f k, f x * k }

lemma scalar_def {x : M} {f : C^âˆâŸ®I, M; ğ•œâŸ¯} {k : ğ•œ} :
  @has_scalar.smul C^âˆâŸ®I, M; ğ•œâŸ¯ ğ•œ (has_scalar I M x) f k = f x * k := rfl

/-- The scalar multiplication defined above gives rise to a module structure. -/
def module (x : M) : module C^âˆâŸ®I, M; ğ•œâŸ¯ ğ•œ :=
{ one_smul := Î» k, one_mul k,
  mul_smul := Î» f g k, mul_assoc _ _ _,
  smul_add := Î» f g k, mul_add _ _ _,
  smul_zero := Î» f, mul_zero _,
  add_smul := Î» f g k, add_mul _ _ _,
  zero_smul := Î» f, zero_mul _,
  ..point_derivation.has_scalar I M x }

lemma is_scalar_tower (x : M) :
  @is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ ğ•œ _ (has_scalar I M x) _ :=
{ smul_assoc := Î» k f h, by { simp only [scalar_def, algebra.id.smul_eq_mul,
    smooth_map.coe_smul, pi.smul_apply, mul_assoc]} }

end point_derivation

/-- The derivations at a point of a manifold. Some regard this as a possible definition of the
tangent space -/
@[reducible] def point_derivation (x : M) :=
  @derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ _ _ _ ğ•œ _ (point_derivation.module I M x) _
    (point_derivation.is_scalar_tower I M x)

/-- The total bundle of point derivations. -/
def derivation_bundle := Î£ x : M, point_derivation I M x

variables {I M}

/-- The inclusion map of derivations at a point into the total bundle. -/
def derivation_inclusion {x : M} (v : point_derivation I M x) : derivation_bundle I M :=
sigma.mk x v

instance [inhabited M] : inhabited (derivation_bundle I M) :=
âŸ¨derivation_inclusion (0 : point_derivation I M (default M))âŸ©

section

/- Why do I need to rewrite extensionality rules for reducible defs? -/
namespace point_derivation

variables {I} {M} {x y : M} {v w : point_derivation I M x} (f g : C^âˆâŸ®I, M; ğ•œâŸ¯) (r : ğ•œ)

lemma coe_injective (h : â‡‘v = w) : v = w :=
@derivation.coe_injective ğ•œ _ C^âˆâŸ®I, M; ğ•œâŸ¯ _ _ ğ•œ _ (point_derivation.module I M x) _
  (point_derivation.is_scalar_tower I M x) v w h

@[ext] theorem ext (h : âˆ€ f, v f = w f) : v = w :=
coe_injective $ funext h

theorem hext {u : point_derivation I M y} (h1 : x = y) (h2 : âˆ€ f, v f = u f) : v == u :=
by { cases h1, rw heq_iff_eq, ext, exact h2 f }

end point_derivation

end

section

variables {I} {M} (X Y : derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯)
  (f g : C^âˆâŸ®I, M; ğ•œâŸ¯) (r : ğ•œ)

namespace derivation

/-- Evaluation of a global derivation at a point, giving a point derivation in the most natural
possible way. -/
def eval_map (x : M) : point_derivation I M x :=
{ to_fun := Î» f, (X f) x,
  map_add' := Î» f g, by { rw derivation.map_add, refl },
  map_smul' := Î» f g, by { rw [derivation.map_smul, algebra.id.smul_eq_mul], refl },
  leibniz' := Î» h k, by { dsimp only [], rw [derivation.leibniz, algebra.id.smul_eq_mul], refl } }

/-- The evaluation is a linear map. -/
def eval_at (x : M) : (derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âŠ¤âŸ®I, M; ğ•œâŸ¯) â†’â‚—[ğ•œ] point_derivation I M x :=
{ to_fun := Î» X, X.eval_map x,
  map_add' := Î» X Y, rfl,
  map_smul' := Î» k X, rfl }

lemma eval_apply (x : M) : eval_at x X f = (X f) x := rfl

end derivation

variables {E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M']

/-- The differential of a function interpreted in the context of derivations. -/
def fdifferential_map (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) (v : point_derivation I M x) :
  (point_derivation I' M' (f x)) :=
{ to_fun := Î» g, v (g.comp f),
  map_add' := Î» g h, by rw [smooth_map.add_comp, derivation.map_add],
  map_smul' := Î» k g, by rw [smooth_map.smul_comp, derivation.map_smul],
  leibniz' := Î» g h, by {simp only [derivation.leibniz, smooth_map.mul_comp], refl} }

/-- The differential is a linear map. -/
def fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) : (point_derivation I M x) â†’â‚—[ğ•œ]
  (point_derivation I' M' (f x)) :=
{ to_fun := fdifferential_map f x,
  map_smul' := Î» k v, rfl,
  map_add' := Î» v w, rfl }

/- Standard notion for the differential. The abbreviation is `MId`. -/
localized "notation `ğ’…` := fdifferential" in manifold

lemma apply_fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) (v : point_derivation I M x)
  (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) :
  ğ’…f x v g = v (g.comp f) := rfl

variables {E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']

@[simp] lemma fdifferential_comp (g : C^âˆâŸ®I', M'; I'', M''âŸ¯) (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
  (ğ’…g (f x)).comp (ğ’…f x) = ğ’…(g.comp f) x :=
by { ext, simp only [apply_fdifferential], refl }

end
