/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/

import geometry.manifold.algebra.smooth_functions
import ring_theory.derivation

/-!

# Derivation bundle

In this file we define the derivations at a point of a manifold on the algebra of smooth fuctions.
Moreover, we define the differential of a function in terms of derivations.

The content of this file is not meant to be regarded as an alternative definition to the current
tangent bundle but rather as a purely algebraic theory that provides a purely algebraic definition
of the Lie algebra for a Lie group.

-/

variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
(M : Type*) [topological_space M] [charted_space H M] (n : with_top â„•)

open_locale manifold

-- the following two instances prevent poorly understood type class inference timeout problems
instance smooth_functions_algebra : algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ := by apply_instance
instance smooth_functions_tower : is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯ := by apply_instance

/-- Type synonym, introduced to put a different `has_scalar` action on `C^nâŸ®I, M; ğ•œâŸ¯`
which is defined as `f â€¢ r = f(x) * r`. -/
@[nolint unused_arguments] def pointed_smooth_map (x : M) := C^nâŸ®I, M; ğ•œâŸ¯

localized "notation `C^` n `âŸ®` I `,` M `;` ğ•œ `âŸ¯âŸ¨` x `âŸ©` :=
  pointed_smooth_map ğ•œ I M n x" in derivation

variables {ğ•œ M}

namespace pointed_smooth_map

instance {x : M} : has_coe_to_fun C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© (Î» _, M â†’ ğ•œ) :=
times_cont_mdiff_map.has_coe_to_fun
instance {x : M} : comm_ring C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© := smooth_map.comm_ring
instance {x : M} : algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© := smooth_map.algebra
instance {x : M} : inhabited C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© := âŸ¨0âŸ©
instance {x : M} : algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ := algebra.id C^âˆâŸ®I, M; ğ•œâŸ¯
instance {x : M} : is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ := is_scalar_tower.right

variable {I}

/-- `smooth_map.eval_ring_hom` gives rise to an algebra structure of `C^âˆâŸ®I, M; ğ•œâŸ¯` on `ğ•œ`. -/
instance eval_algebra {x : M} : algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ :=
(smooth_map.eval_ring_hom x : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© â†’+* ğ•œ).to_algebra

/-- With the `eval_algebra` algebra structure evaluation is actually an algebra morphism. -/
def eval (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
algebra.of_id C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ

lemma smul_def (x : M) (f : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©) (k : ğ•œ) : f â€¢ k = f x * k := rfl

instance (x : M) : is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ :=
{ smul_assoc := Î» k f h, by { simp only [smul_def, algebra.id.smul_eq_mul, smooth_map.coe_smul,
  pi.smul_apply, mul_assoc]} }

end pointed_smooth_map

open_locale derivation

/-- The derivations at a point of a manifold. Some regard this as a possible definition of the
tangent space -/
@[reducible] def point_derivation (x : M) := derivation ğ•œ (C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©) ğ•œ

section

variables (I) {M} (X Y : derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯) (f g : C^âˆâŸ®I, M; ğ•œâŸ¯) (r : ğ•œ)

/-- Evaluation at a point gives rise to a `C^âˆâŸ®I, M; ğ•œâŸ¯`-linear map between `C^âˆâŸ®I, M; ğ•œâŸ¯` and `ğ•œ`.
 -/
def smooth_function.eval_at (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚—[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
(pointed_smooth_map.eval x).to_linear_map

namespace derivation

variable {I}

/-- The evaluation at a point as a linear map. -/
def eval_at (x : M) : (derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯) â†’â‚—[ğ•œ] point_derivation I x :=
(smooth_function.eval_at I x).comp_der

lemma eval_at_apply (x : M) : eval_at x X f = (X f) x := rfl

end derivation

variables {I} {E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M']

/-- The heterogeneous differential as a linear map. Instead of taking a function as an argument this
differential takes `h : f x = y`. It is particularly handy to deal with situations where the points
on where it has to be evaluated are equal but not definitionally equal. -/
def hfdifferential {f : C^âˆâŸ®I, M; I', M'âŸ¯} {x : M} {y : M'} (h : f x = y) :
  point_derivation I x â†’â‚—[ğ•œ] point_derivation I' y :=
{ to_fun := Î» v, { to_linear_map :=
    { to_fun := Î» g, v (g.comp f),
      map_add' := Î» g g', by rw [smooth_map.add_comp, derivation.map_add],
      map_smul' := Î» k g,
        by simp only [smooth_map.smul_comp, derivation.map_smul, ring_hom.id_apply], },
    leibniz' := Î» g g', by simp only [derivation.leibniz, smooth_map.mul_comp,
      pointed_smooth_map.smul_def, times_cont_mdiff_map.comp_apply, h] },
  map_smul' := Î» k v, rfl,
  map_add' := Î» v w, rfl }

/-- The homogeneous differential as a linear map. -/
def fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
  point_derivation I x â†’â‚—[ğ•œ] point_derivation I' (f x) :=
hfdifferential (rfl : f x = f x)

/- Standard notation for the differential. The abbreviation is `MId`. -/
localized "notation `ğ’…` := fdifferential" in manifold

/- Standard notation for the differential. The abbreviation is `MId`. -/
localized "notation `ğ’…â‚•` := hfdifferential" in manifold

@[simp] lemma apply_fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) {x : M} (v : point_derivation I x)
  (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) : ğ’…f x v g = v (g.comp f) := rfl

@[simp] lemma apply_hfdifferential {f : C^âˆâŸ®I, M; I', M'âŸ¯} {x : M} {y : M'} (h : f x = y)
  (v : point_derivation I x) (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) : ğ’…â‚•h v g = ğ’…f x v g := rfl

variables {E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']

@[simp] lemma fdifferential_comp (g : C^âˆâŸ®I', M'; I'', M''âŸ¯) (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
  ğ’…(g.comp f) x = (ğ’…g (f x)).comp (ğ’…f x) := rfl

end
