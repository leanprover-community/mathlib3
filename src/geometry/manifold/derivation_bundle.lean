/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri
-/

import geometry.manifold.algebra.smooth_functions
import ring_theory.derivation

/-!

# Derivation bundle

In this file we define the derivations at a point of a manifold on the algebra of smooth fuctions.
Moreover we define the total bundle of derivations (although at the moment it has not been given a
topology). Finally we define the differential of a function in terms of derivations.

The content of this file is not meant to be regarded as an alternative definition to the current
tangent bundle but rather as a purely algebraic theory that provides a purely algebraic definition
of the Lie algebra for a Lie group.

-/

variables (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
(M : Type*) [topological_space M] [charted_space H M] (n : with_top â„•)

open_locale manifold

instance smooth_functions_algebra : algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ := by apply_instance
instance smooth_functions_tower : is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯ := by apply_instance

/-- Type synonym, introduced to put a different `has_scalar` action on `C^nâŸ®I, M; ğ•œâŸ¯`
which is defined as `f â€¢ r = f(x) * r`. -/
@[nolint unused_arguments] def pointed_smooth_map (x : M) := C^nâŸ®I, M; ğ•œâŸ¯

localized "notation `C^` n `âŸ®` I `,` M `;` ğ•œ `âŸ¯âŸ¨` x `âŸ©` :=
  pointed_smooth_map ğ•œ I M n x" in derivation

variables {ğ•œ M}

namespace pointed_smooth_map

instance {x : M} : has_coe_to_fun C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© := by { rw pointed_smooth_map, apply_instance }
instance {x : M} : comm_ring C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© := by { rw pointed_smooth_map, apply_instance }
instance {x : M} : algebra ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© := by { dunfold pointed_smooth_map, apply_instance }
instance {x : M} : inhabited C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© := âŸ¨0âŸ©

instance {x : M} : algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ :=
by { dunfold pointed_smooth_map, apply_instance }

instance {x : M} : is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© C^âˆâŸ®I, M; ğ•œâŸ¯ :=
by { dunfold pointed_smooth_map, apply_instance }

/-- Evaluation at a point is a ring homomorphism. Same thing as writing manually
`to_fun := Î» f, f x`.-/
def eval' (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© â†’+* ğ•œ :=
(pi.eval_ring_hom _ x : (M â†’ ğ•œ) â†’+* ğ•œ).comp smooth_map.coe_fn_ring_hom

variable {I}

/-- The above evaluation gives rise to an algebra structure of `C^âˆâŸ®I, M; ğ•œâŸ¯` on `ğ•œ`. -/
instance eval_algebra {x : M} : algebra C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ := (eval' I x).to_algebra

/-- With the above algebra structure evaluation is actually an algebra morphism. -/
def eval (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
{ commutes' := Î» k, rfl, ..eval' I x }

lemma scalar_def (x : M) (f : C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©) (k : ğ•œ) : f â€¢ k = f x * k := rfl

instance (x : M) : is_scalar_tower ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ© ğ•œ :=
{ smul_assoc := Î» k f h, by { simp only [scalar_def, algebra.id.smul_eq_mul, smooth_map.coe_smul,
  pi.smul_apply, mul_assoc]} }

end pointed_smooth_map

open_locale derivation

/-- The derivations at a point of a manifold. Some regard this as a possible definition of the
tangent space -/
@[reducible] def point_derivation (x : M) := derivation ğ•œ (C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©) ğ•œ

variable (M)

/-- The total bundle of point derivations. -/
def derivation_bundle := Î£ x : M, point_derivation I x

variables {I M}

/-- The inclusion map of derivations at a point into the total bundle. -/
def derivation_inclusion {x : M} (v : point_derivation I x) : derivation_bundle I M :=
sigma.mk x v

instance [inhabited M] : inhabited (derivation_bundle I M) :=
âŸ¨derivation_inclusion (0 : point_derivation I (default M))âŸ©

section

variables (I) {M} (X Y : derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯) (f g : C^âˆâŸ®I, M; ğ•œâŸ¯) (r : ğ•œ)

/-- Evaluation at a point gives rise to a `C^âˆâŸ®I, M; ğ•œâŸ¯`-linear map between `C^âˆâŸ®I, M; ğ•œâŸ¯` and `ğ•œ`.
 -/
def smooth_function.eval_at (x : M) : C^âˆâŸ®I, M; ğ•œâŸ¯ â†’â‚—[C^âˆâŸ®I, M; ğ•œâŸ¯âŸ¨xâŸ©] ğ•œ :=
(pointed_smooth_map.eval x).to_linear_map

namespace derivation

variable {I}

/-- The evaluation at a point as a linear map. -/
def eval_at (x : M) : (derivation ğ•œ C^âˆâŸ®I, M; ğ•œâŸ¯ C^âˆâŸ®I, M; ğ•œâŸ¯) â†’â‚—[ğ•œ] point_derivation I x :=
(smooth_function.eval_at I x).comp_der

lemma eval_apply (x : M) : eval_at x X f = (X f) x := rfl

end derivation

variables {I} {E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M']

/-- The differential of a function interpreted in the context of derivations. -/
def fdifferential_map (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) (v : point_derivation I x) :
  (point_derivation I' (f x)) :=
{ to_fun := Î» g, v (g.comp f),
  map_add' := Î» g h, by rw [smooth_map.add_comp, derivation.map_add],
  map_smul' := Î» k g, by rw [smooth_map.smul_comp, derivation.map_smul],
  leibniz' := Î» g h, by { simp only [derivation.leibniz, smooth_map.mul_comp], refl} }

/-- The differential is a linear map. -/
def fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) : (point_derivation I x) â†’â‚—[ğ•œ]
  (point_derivation I' (f x)) :=
{ to_fun := fdifferential_map f x,
  map_smul' := Î» k v, rfl,
  map_add' := Î» v w, rfl }

/- Standard notion for the differential. The abbreviation is `MId`. -/
localized "notation `ğ’…` := fdifferential" in manifold

lemma apply_fdifferential (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) (v : point_derivation I x)
  (g : C^âˆâŸ®I', M'; ğ•œâŸ¯) :
  ğ’…f x v g = v (g.comp f) := rfl

variables {E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']

@[simp] lemma fdifferential_comp (g : C^âˆâŸ®I', M'; I'', M''âŸ¯) (f : C^âˆâŸ®I, M; I', M'âŸ¯) (x : M) :
  ğ’…(g.comp f) x = (ğ’…g (f x)).comp (ğ’…f x) := rfl

end
