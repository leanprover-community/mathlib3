/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: NicolÃ² Cavalleri.
-/

import geometry.manifold.times_cont_mdiff_map

/-!
# Diffeomorphisms
This file implements diffeomorphisms.

## Definitions

* `times_diffeomorph I I' M M' n`:  `n`-times continuously differentiable diffeomorphism between
                                    `M` and `M'` with respect to I and I'
* `diffeomorph  I I' M M'` : smooth diffeomorphism between `M` and `M'` with respect to I and I'

## Notations

* `M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M'`  := `times_diffeomorph I J M N n`
* `M â‰ƒâ‚˜âŸ®I, I'âŸ¯ M'`    := `times_diffeomorph I J M N âŠ¤`

## Implementation notes

This notion of diffeomorphism is needed although there is already a notion of structomorphism
because structomorphisms do not allow the model spaces `H` and `H'` of the two manifolds to be
different, i.e. for a structomorphism one has to impose `H = H'` which is often not the case in
practice.

-/

open_locale manifold

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{F : Type*} [normed_group F] [normed_space ğ•œ F]
{H : Type*} [topological_space H]
{H' : Type*} [topological_space H']
{G : Type*} [topological_space G]
(I : model_with_corners ğ•œ E H) (I' : model_with_corners ğ•œ E' H')
(J : model_with_corners ğ•œ F G)

section diffeomorph

variables (M : Type*) [topological_space M] [charted_space H M]
(M' : Type*) [topological_space M'] [charted_space H' M']
(N : Type*) [topological_space N] [charted_space G N]
(n : with_top â„•)

/--
`n`-times continuously differentiable diffeomorphism between `M` and `M'` with respect to I and I'
-/
@[protect_proj, nolint has_inhabited_instance]
structure times_diffeomorph extends M â‰ƒ M' :=
(times_cont_mdiff_to_fun  : times_cont_mdiff I I' n to_fun)
(times_cont_mdiff_inv_fun : times_cont_mdiff I' I n inv_fun)

/-- A `diffeomorph` is just a smooth `times_diffeomorph`. -/
@[reducible] def diffeomorph := times_diffeomorph I I' M M' âŠ¤

infix ` â‰ƒâ‚˜ `:50 := times_diffeomorph _ _
localized "notation M ` â‰ƒâ‚˜^ `n `âŸ®` I `,` J `âŸ¯` N := times_diffeomorph I J M N n" in manifold
localized "notation M ` â‰ƒâ‚˜âŸ®` I `,` J `âŸ¯` N := times_diffeomorph I J M N âŠ¤" in manifold

namespace times_diffeomorph
instance : has_coe_to_fun (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') := âŸ¨Î» _, M â†’ M', Î»e, e.to_equivâŸ©

instance : has_coe (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') C^nâŸ®I, M; I', M'âŸ¯ := âŸ¨Î» Î¦, âŸ¨Î¦, Î¦.times_cont_mdiff_to_funâŸ©âŸ©

protected lemma continuous (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : continuous h :=
h.times_cont_mdiff_to_fun.continuous
protected lemma times_cont_mdiff (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : times_cont_mdiff I I' n h :=
h.times_cont_mdiff_to_fun
protected lemma smooth (h : M â‰ƒâ‚˜âŸ®I, I'âŸ¯ M') : smooth I I' h := h.times_cont_mdiff_to_fun


lemma coe_eq_to_equiv (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (x : M) : h x = h.to_equiv x := rfl

/-- Identity map as a diffeomorphism. -/
protected def refl : M â‰ƒâ‚˜^nâŸ®I, IâŸ¯ M :=
{ times_cont_mdiff_to_fun := times_cont_mdiff_id,
  times_cont_mdiff_inv_fun := times_cont_mdiff_id,
  ..equiv.refl M }

/-- Composition of two diffeomorphisms. -/
protected def trans (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : M' â‰ƒâ‚˜^nâŸ®I', JâŸ¯ N) :
  M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N :=
{ times_cont_mdiff_to_fun  := hâ‚‚.times_cont_mdiff_to_fun.comp hâ‚.times_cont_mdiff_to_fun,
  times_cont_mdiff_inv_fun := hâ‚.times_cont_mdiff_inv_fun.comp hâ‚‚.times_cont_mdiff_inv_fun,
  .. equiv.trans hâ‚.to_equiv hâ‚‚.to_equiv }

/-- Inverse of a diffeomorphism. -/
protected def symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : N â‰ƒâ‚˜^nâŸ®J, IâŸ¯ M :=
{ times_cont_mdiff_to_fun  := h.times_cont_mdiff_inv_fun,
  times_cont_mdiff_inv_fun := h.times_cont_mdiff_to_fun,
  .. h.to_equiv.symm }

end times_diffeomorph

end diffeomorph
