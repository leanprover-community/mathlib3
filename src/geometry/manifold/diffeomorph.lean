/-
Copyright Â© 2020 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Yury Kudryashov
-/

import geometry.manifold.times_cont_mdiff_map

/-!
# Diffeomorphisms
This file implements diffeomorphisms.

## Definitions

* `diffeomorph I I' M M' n`:  `n`-times continuously differentiable diffeomorphism between
  `M` and `M'` with respect to I and I'; we do not introduce a separate definition for the case
  `n = âˆ`; we use notation instead.
* `diffeomorph.to_homeomorph`: reinterpret a diffeomorphism as a homeomorphism.
* `continuous_linear_equiv.to_diffeomorph`: reinterpret a continuous equivalence as
  a diffeomorphism.
* `model_with_corners.trans_diffeomorph`: compose a given `model_with_corners` with a diffeomorphism
  between the old and the new target spaces. Useful, e.g, to turn any finite dimensional manifold
  into a manifold modelled on a Euclidean space.
* `diffeomorph.to_trans_diffeomorph`: the identity diffeomorphism between `M` with model `I` and `M`
  with model `I.trans_diffeomorph e`.

## Notations

* `M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M'`  := `diffeomorph I J M N n`
* `M â‰ƒâ‚˜âŸ®I, I'âŸ¯ M'`    := `diffeomorph I J M N âŠ¤`
* `E â‰ƒâ‚˜^n[ğ•œ] E'`      := `E â‰ƒâ‚˜^nâŸ®ğ“˜(ğ•œ, E), ğ“˜(ğ•œ, E')âŸ¯ E'`
* `E â‰ƒâ‚˜[ğ•œ] E'`        := `E â‰ƒâ‚˜âŸ®ğ“˜(ğ•œ, E), ğ“˜(ğ•œ, E')âŸ¯ E'`

## Implementation notes

This notion of diffeomorphism is needed although there is already a notion of structomorphism
because structomorphisms do not allow the model spaces `H` and `H'` of the two manifolds to be
different, i.e. for a structomorphism one has to impose `H = H'` which is often not the case in
practice.

## Keywords

diffeomorphism, manifold
-/

open_locale manifold topological_space
open function set

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{F : Type*} [normed_group F] [normed_space ğ•œ F]
{H : Type*} [topological_space H]
{H' : Type*} [topological_space H']
{G : Type*} [topological_space G]
{I : model_with_corners ğ•œ E H} {I' : model_with_corners ğ•œ E' H'}
{J : model_with_corners ğ•œ F G}

variables {M : Type*} [topological_space M] [charted_space H M]
{M' : Type*} [topological_space M'] [charted_space H' M']
{N : Type*} [topological_space N] [charted_space G N]
{n : with_top â„•}

section defs

variables (I I' M M' n)

/--
`n`-times continuously differentiable diffeomorphism between `M` and `M'` with respect to I and I'
-/
@[protect_proj, nolint has_inhabited_instance]
structure diffeomorph extends M â‰ƒ M' :=
(times_cont_mdiff_to_fun  : times_cont_mdiff I I' n to_equiv)
(times_cont_mdiff_inv_fun : times_cont_mdiff I' I n to_equiv.symm)

end defs

localized "notation M ` â‰ƒâ‚˜^` n:1000 `âŸ®`:50 I `,` J `âŸ¯ ` N := diffeomorph I J M N n" in manifold
localized "notation M ` â‰ƒâ‚˜âŸ®` I `,` J `âŸ¯ ` N := diffeomorph I J M N âŠ¤" in manifold
localized "notation E ` â‰ƒâ‚˜^` n:1000 `[`:50 ğ•œ `] ` E' :=
  diffeomorph (model_with_corners_self ğ•œ E) (model_with_corners_self ğ•œ E') E E' n" in manifold
localized "notation E ` â‰ƒâ‚˜[` ğ•œ `] ` E' :=
  diffeomorph (model_with_corners_self ğ•œ E) (model_with_corners_self ğ•œ E') E E' âŠ¤" in manifold

namespace diffeomorph
instance : has_coe_to_fun (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (Î» _, M â†’ M') := âŸ¨Î»e, e.to_equivâŸ©

instance : has_coe (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') C^nâŸ®I, M; I', M'âŸ¯ := âŸ¨Î» Î¦, âŸ¨Î¦, Î¦.times_cont_mdiff_to_funâŸ©âŸ©

@[continuity] protected lemma continuous (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : continuous h :=
h.times_cont_mdiff_to_fun.continuous
protected lemma times_cont_mdiff (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : times_cont_mdiff I I' n h :=
h.times_cont_mdiff_to_fun
protected lemma times_cont_mdiff_at (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') {x} : times_cont_mdiff_at I I' n h x :=
h.times_cont_mdiff.times_cont_mdiff_at
protected lemma times_cont_mdiff_within_at (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') {s x} :
  times_cont_mdiff_within_at I I' n h s x :=
h.times_cont_mdiff_at.times_cont_mdiff_within_at
protected lemma times_cont_diff (h : E â‰ƒâ‚˜^n[ğ•œ] E') : times_cont_diff ğ•œ n h :=
h.times_cont_mdiff.times_cont_diff
protected lemma smooth (h : M â‰ƒâ‚˜âŸ®I, I'âŸ¯ M') : smooth I I' h := h.times_cont_mdiff_to_fun
protected lemma mdifferentiable (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hn : 1 â‰¤ n) : mdifferentiable I I' h :=
h.times_cont_mdiff.mdifferentiable hn
protected lemma mdifferentiable_on (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (s : set M) (hn : 1 â‰¤ n) :
  mdifferentiable_on I I' h s :=
(h.mdifferentiable hn).mdifferentiable_on

@[simp] lemma coe_to_equiv (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : â‡‘h.to_equiv = h := rfl
@[simp, norm_cast] lemma coe_coe (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : â‡‘(h : C^nâŸ®I, M; I', M'âŸ¯) = h := rfl

lemma to_equiv_injective : injective (diffeomorph.to_equiv : (M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') â†’ (M â‰ƒ M'))
| âŸ¨e, _, _âŸ© âŸ¨e', _, _âŸ© rfl := rfl

@[simp] lemma to_equiv_inj {h h' : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M'} : h.to_equiv = h'.to_equiv â†” h = h' :=
to_equiv_injective.eq_iff

/-- Coercion to function `Î» h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M', (h : M â†’ M')` is injective. -/
lemma coe_fn_injective : injective (Î» (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (x : M), h x) :=
equiv.coe_fn_injective.comp to_equiv_injective

@[ext] lemma ext {h h' : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M'} (Heq : âˆ€ x, h x = h' x) : h = h' :=
coe_fn_injective $ funext Heq

section

variables (M I n)

/-- Identity map as a diffeomorphism. -/
protected def refl : M â‰ƒâ‚˜^nâŸ®I, IâŸ¯ M :=
{ times_cont_mdiff_to_fun := times_cont_mdiff_id,
  times_cont_mdiff_inv_fun := times_cont_mdiff_id,
  to_equiv := equiv.refl M }

@[simp] lemma refl_to_equiv : (diffeomorph.refl I M n).to_equiv = equiv.refl _ := rfl
@[simp] lemma coe_refl : â‡‘(diffeomorph.refl I M n) = id := rfl

end

/-- Composition of two diffeomorphisms. -/
protected def trans (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : M' â‰ƒâ‚˜^nâŸ®I', JâŸ¯ N) :
  M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N :=
{ times_cont_mdiff_to_fun  := hâ‚‚.times_cont_mdiff_to_fun.comp hâ‚.times_cont_mdiff_to_fun,
  times_cont_mdiff_inv_fun := hâ‚.times_cont_mdiff_inv_fun.comp hâ‚‚.times_cont_mdiff_inv_fun,
  to_equiv := hâ‚.to_equiv.trans hâ‚‚.to_equiv }

@[simp] lemma trans_refl (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : h.trans (diffeomorph.refl I' M' n) = h :=
ext $ Î» _, rfl
@[simp] lemma refl_trans (h : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') : (diffeomorph.refl I M n).trans h = h :=
ext $ Î» _, rfl
@[simp] lemma coe_trans (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : M' â‰ƒâ‚˜^nâŸ®I', JâŸ¯ N) :
  â‡‘(hâ‚.trans hâ‚‚) = hâ‚‚ âˆ˜ hâ‚ := rfl

/-- Inverse of a diffeomorphism. -/
protected def symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : N â‰ƒâ‚˜^nâŸ®J, IâŸ¯ M :=
{ times_cont_mdiff_to_fun  := h.times_cont_mdiff_inv_fun,
  times_cont_mdiff_inv_fun := h.times_cont_mdiff_to_fun,
  to_equiv := h.to_equiv.symm }

@[simp] lemma apply_symm_apply (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (x : N) : h (h.symm x) = x :=
h.to_equiv.apply_symm_apply x
@[simp] lemma symm_apply_apply (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (x : M) : h.symm (h x) = x :=
h.to_equiv.symm_apply_apply x

@[simp] lemma symm_refl : (diffeomorph.refl I M n).symm = diffeomorph.refl I M n :=
ext $ Î» _, rfl
@[simp] lemma trans_symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.trans h.symm = diffeomorph.refl I M n :=
ext h.symm_apply_apply
@[simp] lemma symm_trans (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.symm.trans h = diffeomorph.refl J N n :=
ext h.apply_symm_apply
@[simp] lemma symm_trans' (hâ‚ : M â‰ƒâ‚˜^nâŸ®I, I'âŸ¯ M') (hâ‚‚ : M' â‰ƒâ‚˜^nâŸ®I', JâŸ¯ N) :
  (hâ‚.trans hâ‚‚).symm = hâ‚‚.symm.trans hâ‚.symm := rfl
@[simp] lemma symm_to_equiv (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : h.symm.to_equiv = h.to_equiv.symm := rfl
@[simp, mfld_simps] lemma to_equiv_coe_symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : â‡‘h.to_equiv.symm = h.symm := rfl

lemma image_eq_preimage (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : set M) : h '' s = h.symm â»Â¹' s :=
h.to_equiv.image_eq_preimage s
lemma symm_image_eq_preimage (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : set N) : h.symm '' s = h â»Â¹' s :=
h.symm.image_eq_preimage s

@[simp, mfld_simps] lemma range_comp {Î±} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (f : Î± â†’ M) :
  range (h âˆ˜ f) = h.symm â»Â¹' (range f) :=
by rw [range_comp, image_eq_preimage]

@[simp] lemma image_symm_image (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : set N) : h '' (h.symm '' s) = s :=
h.to_equiv.image_symm_image s
@[simp] lemma symm_image_image (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (s : set M) : h.symm '' (h '' s) = s :=
h.to_equiv.symm_image_image s

/-- A diffeomorphism is a homeomorphism. -/
def to_homeomorph (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : M â‰ƒâ‚œ N :=
âŸ¨h.to_equiv, h.continuous, h.symm.continuousâŸ©

@[simp] lemma to_homeomorph_to_equiv (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) :
  h.to_homeomorph.to_equiv = h.to_equiv :=
rfl
@[simp] lemma symm_to_homeomorph (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) :
  h.symm.to_homeomorph = h.to_homeomorph.symm :=
rfl

@[simp] lemma coe_to_homeomorph (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) : â‡‘h.to_homeomorph = h := rfl
@[simp] lemma coe_to_homeomorph_symm (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) :
  â‡‘h.to_homeomorph.symm = h.symm := rfl

@[simp] lemma times_cont_mdiff_within_at_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'}
  {s x} (hm : m â‰¤ n) :
  times_cont_mdiff_within_at I I' m (f âˆ˜ h) s x â†”
    times_cont_mdiff_within_at J I' m f (h.symm â»Â¹' s) (h x) :=
begin
  split,
  { intro Hfh,
    rw [â† h.symm_apply_apply x] at Hfh,
    simpa only [(âˆ˜), h.apply_symm_apply]
      using Hfh.comp (h x) (h.symm.times_cont_mdiff_within_at.of_le hm) (maps_to_preimage _ _) },
  { rw â† h.image_eq_preimage,
    exact Î» hf, hf.comp x (h.times_cont_mdiff_within_at.of_le hm) (maps_to_image _ _) }
end

@[simp] lemma times_cont_mdiff_on_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'}
  {s} (hm : m â‰¤ n) :
  times_cont_mdiff_on I I' m (f âˆ˜ h) s â†” times_cont_mdiff_on J I' m f (h.symm â»Â¹' s) :=
h.to_equiv.forall_congr $ Î» x, by simp only [hm, coe_to_equiv, symm_apply_apply,
  times_cont_mdiff_within_at_comp_diffeomorph_iff, mem_preimage]

@[simp] lemma times_cont_mdiff_at_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'} {x}
  (hm : m â‰¤ n) :
  times_cont_mdiff_at I I' m (f âˆ˜ h) x â†” times_cont_mdiff_at J I' m f (h x) :=
h.times_cont_mdiff_within_at_comp_diffeomorph_iff hm

@[simp] lemma times_cont_mdiff_comp_diffeomorph_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : N â†’ M'}
  (hm : m â‰¤ n) :
  times_cont_mdiff I I' m (f âˆ˜ h) â†” times_cont_mdiff J I' m f :=
h.to_equiv.forall_congr $ Î» x, (h.times_cont_mdiff_at_comp_diffeomorph_iff hm)

@[simp] lemma times_cont_mdiff_within_at_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M}
  (hm : m â‰¤ n) {s x} :
  times_cont_mdiff_within_at I' J m (h âˆ˜ f) s x â†” times_cont_mdiff_within_at I' I m f s x :=
âŸ¨Î» Hhf, by simpa only [(âˆ˜), h.symm_apply_apply]
  using (h.symm.times_cont_mdiff_at.of_le hm).comp_times_cont_mdiff_within_at _ Hhf,
  Î» Hf, (h.times_cont_mdiff_at.of_le hm).comp_times_cont_mdiff_within_at _ HfâŸ©

@[simp] lemma times_cont_mdiff_at_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M}
  (hm : m â‰¤ n) {x} :
  times_cont_mdiff_at I' J m (h âˆ˜ f) x â†” times_cont_mdiff_at I' I m f x :=
h.times_cont_mdiff_within_at_diffeomorph_comp_iff hm

@[simp] lemma times_cont_mdiff_on_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M}
  (hm : m â‰¤ n) {s} :
  times_cont_mdiff_on I' J m (h âˆ˜ f) s â†” times_cont_mdiff_on I' I m f s :=
forall_congr $ Î» x, forall_congr $ Î» hx, h.times_cont_mdiff_within_at_diffeomorph_comp_iff hm

@[simp] lemma times_cont_mdiff_diffeomorph_comp_iff {m} (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) {f : M' â†’ M}
  (hm : m â‰¤ n) :
  times_cont_mdiff I' J m (h âˆ˜ f) â†” times_cont_mdiff I' I m f :=
forall_congr $ Î» x, h.times_cont_mdiff_within_at_diffeomorph_comp_iff hm

lemma to_local_homeomorph_mdifferentiable (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n) :
  h.to_homeomorph.to_local_homeomorph.mdifferentiable I J :=
âŸ¨h.mdifferentiable_on _ hn, h.symm.mdifferentiable_on _ hnâŸ©

variables [smooth_manifold_with_corners I M] [smooth_manifold_with_corners J N]

lemma unique_mdiff_on_image_aux (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n)
  {s : set M} (hs : unique_mdiff_on I s) :
  unique_mdiff_on J (h '' s) :=
begin
  convert hs.unique_mdiff_on_preimage (h.to_local_homeomorph_mdifferentiable hn),
  simp [h.image_eq_preimage]
end

@[simp] lemma unique_mdiff_on_image (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n) {s : set M} :
  unique_mdiff_on J (h '' s) â†” unique_mdiff_on I s :=
âŸ¨Î» hs, h.symm_image_image s â–¸ h.symm.unique_mdiff_on_image_aux hn hs,
  h.unique_mdiff_on_image_aux hnâŸ©

@[simp] lemma unique_mdiff_on_preimage (h : M â‰ƒâ‚˜^nâŸ®I, JâŸ¯ N) (hn : 1 â‰¤ n) {s : set N} :
  unique_mdiff_on I (h â»Â¹' s) â†” unique_mdiff_on J s :=
h.symm_image_eq_preimage s â–¸ h.symm.unique_mdiff_on_image hn

@[simp] lemma unique_diff_on_image (h : E â‰ƒâ‚˜^n[ğ•œ] F) (hn : 1 â‰¤ n) {s : set E} :
  unique_diff_on ğ•œ (h '' s) â†” unique_diff_on ğ•œ s :=
by simp only [â† unique_mdiff_on_iff_unique_diff_on, unique_mdiff_on_image, hn]

@[simp] lemma unique_diff_on_preimage (h : E â‰ƒâ‚˜^n[ğ•œ] F) (hn : 1 â‰¤ n) {s : set F} :
  unique_diff_on ğ•œ (h â»Â¹' s) â†” unique_diff_on ğ•œ s :=
h.symm_image_eq_preimage s â–¸ h.symm.unique_diff_on_image hn

end diffeomorph

namespace continuous_linear_equiv

variable (e : E â‰ƒL[ğ•œ] E')

/-- A continuous linear equivalence between normed spaces is a diffeomorphism. -/
def to_diffeomorph : E â‰ƒâ‚˜[ğ•œ] E' :=
{ times_cont_mdiff_to_fun := e.times_cont_diff.times_cont_mdiff,
  times_cont_mdiff_inv_fun := e.symm.times_cont_diff.times_cont_mdiff,
  to_equiv := e.to_linear_equiv.to_equiv }

@[simp] lemma coe_to_diffeomorph : â‡‘e.to_diffeomorph = e := rfl
@[simp] lemma symm_to_diffeomorph : e.symm.to_diffeomorph = e.to_diffeomorph.symm := rfl
@[simp] lemma coe_to_diffeomorph_symm : â‡‘e.to_diffeomorph.symm = e.symm := rfl

end continuous_linear_equiv

namespace model_with_corners

variables (I) (e : E â‰ƒâ‚˜[ğ•œ] E')

/-- Apply a diffeomorphism (e.g., a continuous linear equivalence) to the model vector space. -/
def trans_diffeomorph (I : model_with_corners ğ•œ E H) (e : E â‰ƒâ‚˜[ğ•œ] E') :
  model_with_corners ğ•œ E' H :=
{ to_local_equiv := I.to_local_equiv.trans e.to_equiv.to_local_equiv,
  source_eq := by simp,
  unique_diff' := by simp [range_comp e, I.unique_diff],
  continuous_to_fun := e.continuous.comp I.continuous,
  continuous_inv_fun := I.continuous_symm.comp e.symm.continuous }

@[simp, mfld_simps] lemma coe_trans_diffeomorph : â‡‘(I.trans_diffeomorph e) = e âˆ˜ I := rfl
@[simp, mfld_simps] lemma coe_trans_diffeomorph_symm :
  â‡‘(I.trans_diffeomorph e).symm = I.symm âˆ˜ e.symm := rfl

lemma trans_diffeomorph_range : range (I.trans_diffeomorph e) = e '' (range I) :=
range_comp e I

lemma coe_ext_chart_at_trans_diffeomorph (x : M) :
  â‡‘(ext_chart_at (I.trans_diffeomorph e) x) = e âˆ˜ ext_chart_at I x := rfl

lemma coe_ext_chart_at_trans_diffeomorph_symm (x : M) :
  â‡‘(ext_chart_at (I.trans_diffeomorph e) x).symm = (ext_chart_at I x).symm âˆ˜ e.symm := rfl

lemma ext_chart_at_trans_diffeomorph_target (x : M) :
  (ext_chart_at (I.trans_diffeomorph e) x).target = e.symm â»Â¹' (ext_chart_at I x).target :=
by simp only [range_comp e, e.image_eq_preimage, preimage_preimage] with mfld_simps

end model_with_corners

namespace diffeomorph

variables (e : E â‰ƒâ‚˜[ğ•œ] F)

instance smooth_manifold_with_corners_trans_diffeomorph [smooth_manifold_with_corners I M] :
  smooth_manifold_with_corners (I.trans_diffeomorph e) M :=
begin
  refine smooth_manifold_with_corners_of_times_cont_diff_on  _ _ (Î» eâ‚ eâ‚‚ hâ‚ hâ‚‚, _),
  refine e.times_cont_diff.comp_times_cont_diff_on
    (((times_cont_diff_groupoid âŠ¤ I).compatible hâ‚ hâ‚‚).1.comp
      e.symm.times_cont_diff.times_cont_diff_on _),
  mfld_set_tac
end

variables (I M)

/-- The identity diffeomorphism between a manifold with model `I` and the same manifold
with model `I.trans_diffeomorph e`. -/
def to_trans_diffeomorph (e : E â‰ƒâ‚˜[ğ•œ] F) : M â‰ƒâ‚˜âŸ®I, I.trans_diffeomorph eâŸ¯ M :=
{ to_equiv := equiv.refl M,
  times_cont_mdiff_to_fun := Î» x,
    begin
      refine times_cont_mdiff_within_at_iff.2 âŸ¨continuous_within_at_id, _âŸ©,
      refine e.times_cont_diff.times_cont_diff_within_at.congr' (Î» y hy, _) _,
      { simp only [equiv.coe_refl, id, (âˆ˜), I.coe_ext_chart_at_trans_diffeomorph,
          (ext_chart_at I x).right_inv hy.1] },
      exact âŸ¨(ext_chart_at I x).map_source (mem_ext_chart_source I x), trivial,
        by simp only with mfld_simpsâŸ©
    end,
  times_cont_mdiff_inv_fun := Î» x,
    begin
      refine times_cont_mdiff_within_at_iff.2 âŸ¨continuous_within_at_id, _âŸ©,
      refine e.symm.times_cont_diff.times_cont_diff_within_at.congr' (Î» y hy, _) _,
      { simp only [mem_inter_eq, I.ext_chart_at_trans_diffeomorph_target] at hy,
        simp only [equiv.coe_refl, equiv.refl_symm, id, (âˆ˜),
          I.coe_ext_chart_at_trans_diffeomorph_symm, (ext_chart_at I x).right_inv hy.1] },
      exact âŸ¨(ext_chart_at _ x).map_source (mem_ext_chart_source _ x), trivial,
        by simp only [e.symm_apply_apply, equiv.refl_symm, equiv.coe_refl] with mfld_simpsâŸ©
    end }

variables {I M}

@[simp] lemma times_cont_mdiff_within_at_trans_diffeomorph_right {f : M' â†’ M} {x s} :
  times_cont_mdiff_within_at I' (I.trans_diffeomorph e) n f s x â†”
    times_cont_mdiff_within_at I' I n f s x :=
(to_trans_diffeomorph I M e).times_cont_mdiff_within_at_diffeomorph_comp_iff le_top

@[simp] lemma times_cont_mdiff_at_trans_diffeomorph_right {f : M' â†’ M} {x} :
  times_cont_mdiff_at I' (I.trans_diffeomorph e) n f x â†” times_cont_mdiff_at I' I n f x :=
(to_trans_diffeomorph I M e).times_cont_mdiff_at_diffeomorph_comp_iff le_top

@[simp] lemma times_cont_mdiff_on_trans_diffeomorph_right {f : M' â†’ M} {s} :
  times_cont_mdiff_on I' (I.trans_diffeomorph e) n f s â†” times_cont_mdiff_on I' I n f s :=
(to_trans_diffeomorph I M e).times_cont_mdiff_on_diffeomorph_comp_iff le_top

@[simp] lemma times_cont_mdiff_trans_diffeomorph_right {f : M' â†’ M} :
  times_cont_mdiff I' (I.trans_diffeomorph e) n f â†” times_cont_mdiff I' I n f :=
(to_trans_diffeomorph I M e).times_cont_mdiff_diffeomorph_comp_iff le_top

@[simp] lemma smooth_trans_diffeomorph_right {f : M' â†’ M} :
  smooth I' (I.trans_diffeomorph e) f â†” smooth I' I f :=
times_cont_mdiff_trans_diffeomorph_right e

@[simp] lemma times_cont_mdiff_within_at_trans_diffeomorph_left {f : M â†’ M'} {x s} :
  times_cont_mdiff_within_at (I.trans_diffeomorph e) I' n f s x â†”
    times_cont_mdiff_within_at I I' n f s x :=
((to_trans_diffeomorph I M e).times_cont_mdiff_within_at_comp_diffeomorph_iff le_top).symm

@[simp] lemma times_cont_mdiff_at_trans_diffeomorph_left {f : M â†’ M'} {x} :
  times_cont_mdiff_at (I.trans_diffeomorph e) I' n f x â†” times_cont_mdiff_at I I' n f x :=
((to_trans_diffeomorph I M e).times_cont_mdiff_at_comp_diffeomorph_iff le_top).symm

@[simp] lemma times_cont_mdiff_on_trans_diffeomorph_left {f : M â†’ M'} {s} :
  times_cont_mdiff_on (I.trans_diffeomorph e) I' n f s â†” times_cont_mdiff_on I I' n f s :=
((to_trans_diffeomorph I M e).times_cont_mdiff_on_comp_diffeomorph_iff le_top).symm

@[simp] lemma times_cont_mdiff_trans_diffeomorph_left {f : M â†’ M'} :
  times_cont_mdiff (I.trans_diffeomorph e) I' n f â†” times_cont_mdiff I I' n f :=
((to_trans_diffeomorph I M e).times_cont_mdiff_comp_diffeomorph_iff le_top).symm

@[simp] lemma smooth_trans_diffeomorph_left {f : M â†’ M'} :
  smooth (I.trans_diffeomorph e) I' f â†” smooth I I' f :=
e.times_cont_mdiff_trans_diffeomorph_left

end diffeomorph
