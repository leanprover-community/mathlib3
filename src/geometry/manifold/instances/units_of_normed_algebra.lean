/-
Copyright Â© 2021 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Heather Macbeth
-/

import geometry.manifold.smooth_manifold_with_corners
import geometry.manifold.algebra.lie_group
import analysis.normed_space.units

/-!
# Units of a normed algebra

This file is a stub, containing a construction of the charted space structure on the group of units
of a complete normed ring `R`, and of the smooth manifold structure on the group of units of a
complete normed `ğ•œ`-algebra `R`.

This manifold is actually a Lie group, which eventually should be the main result of this file.

An important special case of this construction is the general linear group.  For a normed space `V`
over a field `ğ•œ`, the `ğ•œ`-linear endomorphisms of `V` are a normed `ğ•œ`-algebra (see
`continuous_linear_map.to_normed_algebra`), so this construction provides a Lie group structure on
its group of units, the general linear group GL(`ğ•œ`, `V`).

## TODO

The Lie group instance requires the following fields:
```
instance : lie_group ğ“˜(ğ•œ, R) RË£ :=
{ smooth_mul := sorry,
  smooth_inv := sorry,
  ..units.smooth_manifold_with_corners }
```

The ingredients needed for the construction are
* smoothness of multiplication and inversion in the charts, i.e. as functions on the normed
  `ğ•œ`-space `R`:  see `cont_diff_at_ring_inverse` for the inversion result, and
  `cont_diff_mul` (needs to be generalized from field to algebra) for the multiplication
  result
* for an open embedding `f`, whose domain is equipped with the induced manifold structure
  `f.singleton_smooth_manifold_with_corners`, characterization of smoothness of functions to/from
  this manifold in terms of smoothness in the target space.  See the pair of lemmas
  `cont_mdiff_coe_sphere` and `cont_mdiff.cod_restrict_sphere` for a model.
None of this should be particularly difficult.

-/

noncomputable theory

open_locale manifold

lemma open_embedding.to_local_homeomorph_left_inv
 {Î± : Type*} {Î² : Type*} [topological_space Î±] [topological_space Î²]
 (f : Î± â†’ Î²) (h : open_embedding f) [nonempty Î±] {x : Î±} :
 (open_embedding.to_local_homeomorph f h).symm (f x) = x :=
begin
  rw â†congr_fun (open_embedding.to_local_homeomorph_apply f h),
  rw local_homeomorph.left_inv,
  rw open_embedding.to_local_homeomorph_source,
  apply set.mem_univ
end

lemma open_embedding.to_local_homeomorph_right_inv
 {Î± : Type*} {Î² : Type*} [topological_space Î±] [topological_space Î²]
 (f : Î± â†’ Î²) (h : open_embedding f) [nonempty Î±] {x : Î²} (hx : x âˆˆ set.range f) :
 f ((open_embedding.to_local_homeomorph f h).symm x) = x :=
begin
  rw â†congr_fun (open_embedding.to_local_homeomorph_apply f h),
  rw local_homeomorph.right_inv,
  rw open_embedding.to_local_homeomorph_target,
  exact hx
end

lemma cont_mdiff_open_embedding
  {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
  {M : Type*} [topological_space M] [nonempty M]
  {e : M â†’ H} (h : open_embedding e) {n : with_top â„•} :
  @cont_mdiff _ _ _ _ _ _ _ I _ _ h.singleton_charted_space _ _ _ _ _ I _ _ _ n e :=
begin
  haveI := h.singleton_smooth_manifold_with_corners I,
  rw cont_mdiff_iff,
  split,
  { apply h.continuous },
  intros, -- show the function is actually the identity on the range of I âˆ˜ e
  apply cont_diff_on.congr cont_diff_on_id,
  intros z hz, -- factorise into the chart (=e) and the model (=id)
  rw [ext_chart_at_coe, ext_chart_at_coe_symm, chart_at_self_eq],
  repeat {rw function.comp_app},
  rw [local_homeomorph.refl_apply, id.def, local_homeomorph.singleton_charted_space_chart_at_eq,
    open_embedding.to_local_homeomorph_right_inv e],
  { rw model_with_corners.right_inv,
    { refl },
    apply set.mem_of_subset_of_mem _ hz.1,
    apply ext_chart_at_target_subset_range },
  rw model_with_corners.symm, -- show hz implies z is in range of I âˆ˜ e
  have := hz.1,
  rw [ext_chart_at, local_equiv.trans_target] at this,
  have := this.2,
  rw [set.mem_preimage, local_homeomorph.singleton_charted_space_chart_at_eq,
    open_embedding.to_local_homeomorph_target] at this,
  exact this
end

lemma cont_mdiff_on_open_embedding_symm
  {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {M : Type*} [topological_space M] [nonempty M]
  {e : M â†’ H} (h : open_embedding e) {n : with_top â„•} :
  @cont_mdiff_on _ _ _ _ _ _ _ I _ _ _ _ _ _ _ _ I _ _ h.singleton_charted_space
    n (open_embedding.to_local_homeomorph e h).symm (set.range e) :=
begin
  haveI := h.singleton_smooth_manifold_with_corners I,
  rw cont_mdiff_on_iff,
  split,
  { rw â†open_embedding.to_local_homeomorph_target,
    apply local_homeomorph.continuous_on_symm (open_embedding.to_local_homeomorph e h) },
  intros, -- show the function is actually the identity on the range of I âˆ˜ e
  apply cont_diff_on.congr cont_diff_on_id,
  intros z hz, -- factorise into the chart (=e) and the model (=id)
  rw [ext_chart_at_coe, ext_chart_at_coe_symm, chart_at_self_eq],
  repeat {rw function.comp_app},
  rw [local_homeomorph.refl_symm, local_homeomorph.refl_apply, id.def,
    local_homeomorph.singleton_charted_space_chart_at_eq, local_homeomorph.right_inv],
  { rw model_with_corners.right_inv,
    { refl },
    apply set.mem_of_subset_of_mem _ hz.1,
    apply ext_chart_at_target_subset_range }, -- show hz implies z is in range of I âˆ˜ e
  rw [open_embedding.to_local_homeomorph_target, model_with_corners.symm, â†set.mem_preimage],
  have := hz.2,
  rw [set.preimage_inter] at this,
  exact this.1
end

lemma cont_mdiff.of_comp_open_embedding
  {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {M : Type*} [topological_space M] [charted_space H M]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
  {M' : Type*} [topological_space M'] [nonempty M']
  {e' : M' â†’ H'} (h : open_embedding e') {n : with_top â„•}
  {f : M â†’ M'} (hf : cont_mdiff I I' n (e' âˆ˜ f)) :
  @cont_mdiff _ _ _ _ _ _ _ I _ _ _ _ _ _ _ _ I' _ _ h.singleton_charted_space n f :=
begin
  have : f = (open_embedding.to_local_homeomorph e' h).symm âˆ˜ e' âˆ˜ f,
  { ext,
    rw [function.comp_app, function.comp_app, open_embedding.to_local_homeomorph_left_inv] },
  rw this,
  apply cont_mdiff_on.comp_cont_mdiff _ hf,
  show set H',
  { exact set.range e' },
  { intros,
    simp },
  exact cont_mdiff_on_open_embedding_symm h
end

namespace units

variables {R : Type*} [normed_ring R] [complete_space R]

instance : charted_space R RË£ := open_embedding_coe.singleton_charted_space

lemma chart_at_apply {a : RË£} {b : RË£} : chart_at R a b = b := rfl
lemma chart_at_source {a : RË£} : (chart_at R a).source = set.univ := rfl

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ] [normed_algebra ğ•œ R]

instance : smooth_manifold_with_corners ğ“˜(ğ•œ, R) RË£ :=
open_embedding_coe.singleton_smooth_manifold_with_corners ğ“˜(ğ•œ, R)

lemma cont_mdiff_coe {m : with_top â„•} : cont_mdiff ğ“˜(ğ•œ, R) ğ“˜(ğ•œ, R) m (coe : RË£ â†’ R) :=
cont_mdiff_open_embedding ğ“˜(ğ•œ, R) units.open_embedding_coe

/-- Multiplication of units of a complete normed ring is a smooth map between manifolds.

It suffices to show that `coe âˆ˜ mul : RË£ Ã— RË£ â†’ R` is smooth. This function is equal to the usual
ring multiplication composed with the embedding from `RË£ Ã— RË£` to `R Ã— R`, and we know each of these
factors is smooth. -/
lemma smooth_mul :
  smooth (ğ“˜(ğ•œ, R).prod ğ“˜(ğ•œ, R)) ğ“˜(ğ•œ, R) (Î» (p : RË£ Ã— RË£), p.fst * p.snd) :=
begin
  apply cont_mdiff.of_comp_open_embedding,
  have : (coe : RË£ â†’ R) âˆ˜ (Î» x : RË£ Ã— RË£, x.1 * x.2) =
    (Î» x : R Ã— R, x.1 * x.2) âˆ˜ (Î» x : RË£ Ã— RË£, (x.1, x.2)),
  { ext, simp },
  rw this,
  have : cont_mdiff (ğ“˜(ğ•œ, R).prod ğ“˜(ğ•œ, R)) (ğ“˜(ğ•œ, R Ã— R))
    âŠ¤ (Î» x : RË£ Ã— RË£, ((x.1 : R), (x.2 : R))) :=
    cont_mdiff.prod_mk_space
      (cont_mdiff.comp cont_mdiff_coe cont_mdiff_fst)
      (cont_mdiff.comp cont_mdiff_coe cont_mdiff_snd),
  apply cont_mdiff.comp _ this,
  rw cont_mdiff_iff_cont_diff,
  apply cont_diff_mul
end

/-- Inversion of units of a complete normed ring is a smooth map between manifolds.

It suffices to show that `coe âˆ˜ inv : RË£ â†’ R` is smooth. This function is equal to the composition
`ring.inverse âˆ˜ coe`, and we know each of these factors is smooth. -/
lemma smooth_inv :
  smooth ğ“˜(ğ•œ, R) ğ“˜(ğ•œ, R) (Î» (a : RË£), aâ»Â¹) :=
begin
  apply cont_mdiff.of_comp_open_embedding,
  have : (coe : RË£ â†’ R) âˆ˜ (Î» x : RË£, xâ»Â¹) = ring.inverse âˆ˜ coe,
  { ext, simp },
  rw [this, cont_mdiff],
  intro,
  have : cont_mdiff ğ“˜(ğ•œ, R) ğ“˜(ğ•œ, R) âŠ¤ (coe : RË£ â†’ R) := cont_mdiff_coe,
  rw cont_mdiff at this,
  apply cont_mdiff_at.comp x _ (this x),
  rw cont_mdiff_at_iff_cont_diff_at,
  apply cont_diff_at_ring_inverse
end

/-- The units of a complete normed ring form a Lie group. -/
instance : lie_group ğ“˜(ğ•œ, R) RË£ :=
{ smooth_mul := smooth_mul,
  smooth_inv := smooth_inv }

end units
