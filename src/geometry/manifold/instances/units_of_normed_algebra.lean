/-
Copyright Â© 2021 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Heather Macbeth, Winston Yin
-/

import geometry.manifold.smooth_manifold_with_corners
import geometry.manifold.algebra.lie_group
import analysis.normed_space.units

/-!
# Units of a normed algebra

We construct the Lie group structure on the group of units of a complete normed `ğ•œ`-algebra `R`. The
group of units `RË£` has a natural smooth manifold structure modelled on `R` given by its embedding
into `R`. Together with the smoothness of the multiplication and inverse of its elements, `RË£` forms
a Lie group.

An important special case of this construction is the general linear group.  For a normed space `V`
over a field `ğ•œ`, the `ğ•œ`-linear endomorphisms of `V` are a normed `ğ•œ`-algebra (see
`continuous_linear_map.to_normed_algebra`), so this construction provides a Lie group structure on
its group of units, the general linear group GL(`ğ•œ`, `V`), as demonstrated by:
```
example {V : Type*} [normed_add_comm_group V] [normed_space ğ•œ V] [complete_space V] [nontrivial V] :
  lie_group ğ“˜(ğ•œ, V â†’L[ğ•œ] V) (V â†’L[ğ•œ] V)Ë£ :=
by apply_instance
```
-/

noncomputable theory

open_locale manifold

/-- Let `M'` be a manifold whose chart structure is given by an open embedding `e'` into its model
space `H'`. Then the smoothness of `e' âˆ˜ f : M â†’ H'` implies the smoothness of `f`.

This is useful, for example, when `e' âˆ˜ f = g âˆ˜ e` for smooth maps `e : M â†’ X` and `g : X â†’ H'`. -/
lemma cont_mdiff.of_comp_open_embedding
  {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {M : Type*} [topological_space M] [charted_space H M]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
  {M' : Type*} [topological_space M'] [nonempty M']
  {e' : M' â†’ H'} (h : open_embedding e') {n : with_top â„•}
  {f : M â†’ M'} (hf : cont_mdiff I I' n (e' âˆ˜ f)) :
  @cont_mdiff _ _ _ _ _ _ _ I _ _ _ _ _ _ _ _ I' _ _ h.singleton_charted_space n f :=
begin
  have : f = (open_embedding.to_local_homeomorph e' h).symm âˆ˜ e' âˆ˜ f,
  { ext,
    rw [function.comp_app, function.comp_app, open_embedding.to_local_homeomorph_left_inv] },
  rw this,
  apply cont_mdiff_on.comp_cont_mdiff _ hf,
  show set H',
  { exact set.range e' },
  { intros,
    simp },
  exact cont_mdiff_on_open_embedding_symm h
end

namespace units

variables {R : Type*} [normed_ring R] [complete_space R]

instance : charted_space R RË£ := open_embedding_coe.singleton_charted_space

lemma chart_at_apply {a : RË£} {b : RË£} : chart_at R a b = b := rfl
lemma chart_at_source {a : RË£} : (chart_at R a).source = set.univ := rfl

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ] [normed_algebra ğ•œ R]

instance : smooth_manifold_with_corners ğ“˜(ğ•œ, R) RË£ :=
open_embedding_coe.singleton_smooth_manifold_with_corners ğ“˜(ğ•œ, R)

lemma cont_mdiff_coe {m : with_top â„•} : cont_mdiff ğ“˜(ğ•œ, R) ğ“˜(ğ•œ, R) m (coe : RË£ â†’ R) :=
cont_mdiff_open_embedding ğ“˜(ğ•œ, R) units.open_embedding_coe

/-- For any map `f` from a manifold `M` to the units `RË£` of a complete normed ring `R`, the
smoothness of `coe âˆ˜ f`, where `coe : RË£ â†’ R` is the embedding, implies the smoothness of `f`.

This can be used to show that ring multiplication `RË£ Ã— RË£ â†’ RË£` and inverse `RË£ â†’ RË£` are
smooth. -/
lemma cont_mdiff.of_comp_units_coe
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {M : Type*} [topological_space M] [charted_space H M]
  {n : with_top â„•}
  {f : M â†’ RË£} (hf : cont_mdiff I ğ“˜(ğ•œ, R) n ((coe : RË£ â†’ R) âˆ˜ f)) :
  cont_mdiff I ğ“˜(ğ•œ, R) n f :=
cont_mdiff.of_comp_open_embedding units.open_embedding_coe hf

/-- The units of a complete normed ring form a Lie group. -/
instance : lie_group ğ“˜(ğ•œ, R) RË£ :=
{ smooth_mul :=
  begin
    apply cont_mdiff.of_comp_units_coe,
    have : (coe : RË£ â†’ R) âˆ˜ (Î» x : RË£ Ã— RË£, x.1 * x.2) =
      (Î» x : R Ã— R, x.1 * x.2) âˆ˜ (Î» x : RË£ Ã— RË£, (x.1, x.2)),
    { ext, simp },
    rw this,
    have : cont_mdiff (ğ“˜(ğ•œ, R).prod ğ“˜(ğ•œ, R)) (ğ“˜(ğ•œ, R Ã— R))
      âˆ (Î» x : RË£ Ã— RË£, ((x.1 : R), (x.2 : R))) :=
      cont_mdiff.prod_mk_space
        (cont_mdiff.comp cont_mdiff_coe cont_mdiff_fst)
        (cont_mdiff.comp cont_mdiff_coe cont_mdiff_snd),
    refine cont_mdiff.comp _ this,
    rw cont_mdiff_iff_cont_diff,
    apply cont_diff_mul
  end,
  smooth_inv :=
  begin
    apply cont_mdiff.of_comp_units_coe,
    have : (coe : RË£ â†’ R) âˆ˜ (Î» x : RË£, xâ»Â¹) = ring.inverse âˆ˜ coe,
    { ext, simp },
    rw [this, cont_mdiff],
    intro x,
    refine cont_mdiff_at.comp x _ (cont_mdiff_coe x),
    rw cont_mdiff_at_iff_cont_diff_at,
    apply cont_diff_at_ring_inverse
  end }

end units
