/-
Copyright Â© 2021 NicolÃ² Cavalleri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: NicolÃ² Cavalleri, Heather Macbeth
-/

import geometry.manifold.smooth_manifold_with_corners
import geometry.manifold.algebra.lie_group
import analysis.normed_space.units

/-!
# Units of a normed algebra

This file is a stub, containing a construction of the charted space structure on the group of units
of a complete normed ring `R`, and of the smooth manifold structure on the group of units of a
complete normed `ğ•œ`-algebra `R`.

This manifold is actually a Lie group, which eventually should be the main result of this file.

An important special case of this construction is the general linear group.  For a normed space `V`
over a field `ğ•œ`, the `ğ•œ`-linear endomorphisms of `V` are a normed `ğ•œ`-algebra (see
`continuous_linear_map.to_normed_algebra`), so this construction provides a Lie group structure on
its group of units, the general linear group GL(`ğ•œ`, `V`).

## TODO

The Lie group instance requires the following fields:
```
instance : lie_group ğ“˜(ğ•œ, R) RË£ :=
{ smooth_mul := sorry,
  smooth_inv := sorry,
  ..units.smooth_manifold_with_corners }
```

The ingredients needed for the construction are
* smoothness of multiplication and inversion in the charts, i.e. as functions on the normed
  `ğ•œ`-space `R`:  see `cont_diff_at_ring_inverse` for the inversion result, and
  `cont_diff_mul` (needs to be generalized from field to algebra) for the multiplication
  result
* for an open embedding `f`, whose domain is equipped with the induced manifold structure
  `f.singleton_smooth_manifold_with_corners`, characterization of smoothness of functions to/from
  this manifold in terms of smoothness in the target space.  See the pair of lemmas
  `cont_mdiff_coe_sphere` and `cont_mdiff.cod_restrict_sphere` for a model.
None of this should be particularly difficult.

-/

noncomputable theory

open_locale manifold

/-- Continuous differentiability of a function between manifolds can be stated in terms of the
continuous differentiability of the corresponding function on the model vector space. This requires
that the extended charts on the manifolds coincide with an open embedding of the manifold into the
model vector space.

TODO: Restructure proof and rearrange variables -/
lemma open_embedding_cont_diff_on_cont_mdiff'
  {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {M : Type*} [topological_space M] [charted_space H M]
  [smooth_manifold_with_corners I M]
  (e : M â†’ E) {he : open_embedding e} (hce : âˆ€ x y, (ext_chart_at I x) y = e y)
  (htarg : âˆ€ x : M, (ext_chart_at I x).target = set.range e)
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
  {M' : Type*} [topological_space M'] [charted_space H' M']
  [smooth_manifold_with_corners I' M']
  (e' : M' â†’ E') {he' : open_embedding e'} (hce' : âˆ€ x y, (ext_chart_at I' x) y = e' y)
  {n : with_top â„•} {f : E â†’ E'} (H : cont_diff_on ğ•œ n f (set.range e))
  {g : M â†’ M'} (hfg : e' âˆ˜ g = f âˆ˜ e) :
  cont_mdiff I I' n g :=
begin
  rw cont_mdiff_iff,
  split,
  { rw continuous_def,
    intros s hs,
    rw â†set.preimage_image_eq s he'.inj,
    rw â†set.preimage_comp,
    rw hfg,
    have hcont : continuous (f âˆ˜ e),
    { apply continuous_on.comp_continuous
        (cont_diff_on.continuous_on H)
        (open_embedding.continuous he),
      exact Î» y, âŸ¨y, rflâŸ© },
    apply continuous.is_open_preimage hcont,
    exact he'.open_iff_image_open.mp hs },
    { intros,
      apply cont_diff_on.congr_mono,
      swap 4,
      exact set.range e,
      swap 4,
      exact f,
      swap 3,
      rw set.subset_def,
      intros a ha,
      cases ha with ha ha',
      rw htarg at ha,
      exact ha,

      exact H,

      intros a ha,
      rw [function.comp_app, hce', â†function.comp_app e' g, hfg, function.comp_app, â†hce x,
        local_equiv.right_inv],
      exact ha.1 }
end

/-- A weaker version of `units.open_embedding_cont_diff_on_cont_mdiff` in which the model space H
coincides with the model vector space E via `model_with_corners_self` and the chart is given by the
open embedding itself. -/
lemma open_embedding_cont_diff_on_cont_mdiff
  {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {M : Type*} [topological_space M] [nonempty M]
  (e : M â†’ E) {he : open_embedding e}
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {M' : Type*} [topological_space M'] [nonempty M']
  (e' : M' â†’ E') {he' : open_embedding e'}
  {n : with_top â„•} {f : E â†’ E'} (H : cont_diff_on ğ•œ n f (set.range e))
  {g : M â†’ M'} (hfg : e' âˆ˜ g = f âˆ˜ e) :
  @cont_mdiff _ _ _ _ _ _ _ ğ“˜(ğ•œ, E) _ _ he.singleton_charted_space
    _ _ _ _ _ ğ“˜(ğ•œ, E') _ _ he'.singleton_charted_space n g :=
begin
  apply @open_embedding_cont_diff_on_cont_mdiff' ğ•œ _
    E _ _ E _ ğ“˜(ğ•œ, E) M _ he.singleton_charted_space
    (he.singleton_smooth_manifold_with_corners ğ“˜(ğ•œ, E)) e he (_) (_)
    E' _ _ E' _ ğ“˜(ğ•œ, E') M' _ he'.singleton_charted_space
    (he'.singleton_smooth_manifold_with_corners ğ“˜(ğ•œ, E')) e' he' (_)
    n f H _ hfg;
  simp
end

namespace units

variables {R : Type*} [normed_ring R] [complete_space R]

instance : charted_space R RË£ := open_embedding_coe.singleton_charted_space

lemma chart_at_apply {a : RË£} {b : RË£} : chart_at R a b = b := rfl
lemma chart_at_source {a : RË£} : (chart_at R a).source = set.univ := rfl

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ] [normed_algebra ğ•œ R]

instance : smooth_manifold_with_corners ğ“˜(ğ•œ, R) RË£ :=
open_embedding_coe.singleton_smooth_manifold_with_corners ğ“˜(ğ•œ, R)

lemma smooth_mul :
  smooth (ğ“˜(ğ•œ, R).prod ğ“˜(ğ•œ, R)) ğ“˜(ğ•œ, R) (Î» (p : RË£ Ã— RË£), p.fst * p.snd) :=
begin
  apply @open_embedding_cont_diff_on_cont_mdiff' ğ•œ _
    (R Ã— R) _ _ (model_prod R R) _ _ (RË£ Ã— RË£) _ _ _ (Î» x, (x.1, x.2))
    (by {apply open_embedding.prod open_embedding_coe open_embedding_coe; apply_instance}) _ _
    R _ _ R _ _ RË£ _ _ _ coe open_embedding_coe _ _ âŠ¤ (Î» x, x.1 * x.2),
  { exact cont_diff.cont_diff_on cont_diff_mul },
  { ext, simp },
  { apply_instance },
  { simp },
  { intro,
    ext x',
    cases x' with x1 x2,
    split;
    { simp,
      intros y1 hy1 y2 hy2,
      exact âŸ¨âŸ¨y1, hy1âŸ©, âŸ¨y2, hy2âŸ©âŸ© } },
  { apply_instance },
  { intros, simp },
  { intro, simp }
end

lemma smooth_inv :
  smooth ğ“˜(ğ•œ, R) ğ“˜(ğ•œ, R) (Î» (a : RË£), aâ»Â¹) :=
begin
  apply open_embedding_cont_diff_on_cont_mdiff,
  intros x hx,
  apply cont_diff_at.cont_diff_within_at,
  rw set.mem_range at hx,
  cases hx with y hy,
  rw â†hy,
  apply cont_diff_at_ring_inverse,

  ext,
  simp
end

instance : lie_group ğ“˜(ğ•œ, R) RË£ :=
{ smooth_mul := smooth_mul,
  smooth_inv := smooth_inv }

end units
