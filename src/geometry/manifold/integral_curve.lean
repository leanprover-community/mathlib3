/-
Copyright (c) 2022 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import analysis.ODE.picard_lindelof
import geometry.manifold.cont_mdiff
import geometry.manifold.mfderiv

/-!
# Integral curves of vector fields on a manifold

For any continuously differentiable vector field on a manifold `M` and any chosen non-boundary point
`xâ‚€ : M`, an integral curve `Î³ : â„ â†’ M` exists such that `Î³ tâ‚€ = xâ‚€` and the tangent vector of `Î³`
at `t` coincides with the vector field at `Î³ t` for all `t` within an open interval around `tâ‚€`.

As a corollary, such an integral curve exists for any starting point `xâ‚€` if `M` is a manifold
without boundary.

## Implementation details

Since there is already an ODE solution existence theorem
`ODE_solution_exists.at_ball_of_cont_diff_on_nhds`, the bulk of this file is to convert statements
about manifolds to statements about the model space. This comes in a few steps:
1. Express the smoothness of the vector field `v` in a single fixed chart around the starting point
`xâ‚€`.
2. Use the ODE solution existence theorem to obtain a curve `Î³ : â„ â†’ M` whose derivative coincides
with the vector field (stated in the local chart around `xâ‚€`).
3. Same as 2 but now stated in the local chart around `Î³ t`, which is how `cont_mdiff` is defined.

## Tags

integral curve, vector field
-/

localized "notation (name := ext_chart_at) `ğ“”(` I `, ` x `)` :=
  ext_chart_at I x" in manifold

open_locale manifold

section

variables
  {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {M : Type*} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
  {M' : Type*} [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']

/-- Express cont_mdiff_at in a fixed chosen local chart.

TODO: cont_mdiff_within_at, cont_mdiff_on versions -/
lemma cont_mdiff_at_indep_ext_chart
  {n : â„•âˆ} {f : M â†’ M'} (xâ‚€ : M) {x : M}
  (hx : x âˆˆ ğ“”(I, xâ‚€).source) (hfx : f x âˆˆ ğ“”(I', f xâ‚€).source) :
  cont_mdiff_at I I' n f x â†” continuous_at f x âˆ§
    cont_diff_within_at ğ•œ n (written_in_ext_chart_at I I' xâ‚€ f) (set.range I) (ğ“”(I, xâ‚€) x) :=
begin
  rw [cont_mdiff_at, cont_mdiff_within_at],
  rw ext_chart_at_source at hx hfx,
  rw (cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_indep_chart
    (smooth_manifold_with_corners.chart_mem_maximal_atlas I xâ‚€) hx
    (smooth_manifold_with_corners.chart_mem_maximal_atlas I' (f xâ‚€)) hfx,
  apply and_congr (continuous_within_at_iff_continuous_at
    (is_open.mem_nhds is_open_univ (set.mem_univ x))),
  rw [cont_diff_within_at_prop, written_in_ext_chart_at, ext_chart_at_coe, ext_chart_at_coe,
    ext_chart_at_coe_symm, set.inter_comm _ (set.range I)],
  refine cont_diff_within_at_inter ((I.continuous_at_symm).preimage_mem_nhds _),
  rw I.left_inv,
  apply (local_homeomorph.continuous_at_symm _
    (local_homeomorph.map_source _ hx)).preimage_mem_nhds,
  exact is_open.mem_nhds is_open_univ (set.mem_univ _)
end

lemma vector_field_cont_mdiff_at_indep_ext_chart
  {n : â„•âˆ} {v : M â†’ tangent_bundle I M} (hv : âˆ€ x, (v x).1 = x) (xâ‚€ : M) {x : M}
  (hx : x âˆˆ ğ“”(I, xâ‚€).source) :
  cont_mdiff_at I I.tangent n v x â†” continuous_at v x âˆ§
    cont_diff_within_at ğ•œ n (written_in_ext_chart_at I I.tangent xâ‚€ v) (set.range I) (ğ“”(I, xâ‚€) x) :=
begin
  refine cont_mdiff_at_indep_ext_chart xâ‚€ hx _,
  rw [ext_chart_at_source, basic_smooth_vector_bundle_core.mem_chart_source_iff, hv, hv,
    â†ext_chart_at_source I],
  exact hx
end

/-- If `v` is a smooth vector field, then the partial function `E â†’ E` defined by its trivialisation
on a chosen local chart is smooth. -/
lemma vector_field_cont_diff_on_snd_of_cont_mdiff
  {n : â„•âˆ} {v : M â†’ tangent_bundle I M} (hâ‚ : âˆ€ x, (v x).1 = x)
  (hâ‚‚ : cont_mdiff I I.tangent n v) (xâ‚€ : M) :
  cont_diff_on ğ•œ n (Î» (y : E), (written_in_ext_chart_at I I.tangent xâ‚€ v y).2) ğ“”(I, xâ‚€).target :=
begin
  intros y hy,
  apply cont_diff_within_at.mono _ (ext_chart_at_target_subset_range _ _),
  rw â†local_equiv.right_inv _ hy,
  refine cont_diff_at.comp_cont_diff_within_at _ cont_diff_at_snd _,
  apply ((vector_field_cont_mdiff_at_indep_ext_chart hâ‚ _ _).mp hâ‚‚.cont_mdiff_at).2,
  exact local_equiv.map_target _ hy
end

/-- Express the change of coordinates in the tangent bundle in terms of the change of
  coordinates in the base space. -/
lemma tangent_bundle_core_coord_change_triv
  (v v' : tangent_bundle I M) :
  (ğ“”(I.tangent, v') v).2 =
    (fderiv_within ğ•œ (ğ“”(I, v'.1) âˆ˜ ğ“”(I, v.1).symm) (set.range I) (ğ“”(I, v.1) v.1)) v.2 := rfl

lemma tangent_bundle_core_coord_change_triv'
  (v v' : tangent_bundle I M) (hv : v.1 âˆˆ ğ“”(I, v'.1).source) :
  (ğ“”(I.tangent, v) v).2 =
    (fderiv_within ğ•œ (ğ“”(I, v.1) âˆ˜ ğ“”(I, v'.1).symm) (set.range I) (ğ“”(I, v'.1) v.1))
      (ğ“”(I.tangent, v') v).2 :=
begin
  rw [ext_chart_at_coe, function.comp_apply, model_with_corners.prod_apply,
    basic_smooth_vector_bundle_core.to_charted_space_chart_at,
    basic_smooth_vector_bundle_core.chart_apply],
  have hi := mem_achart_source H v.1,
  have hj : v.1 âˆˆ (achart H v'.1).val.to_local_equiv.source,
  { rw ext_chart_at_source at hv,
    exact hv },
  rw â†basic_smooth_vector_bundle_core.coord_change_comp' _ hi hj hi,
  refl
end

end

variables
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
  {H : Type*} [topological_space H] (I : model_with_corners â„ E H)
  (M : Type*) [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  (v : M â†’ tangent_bundle I M)

/-- We apply the ODE existence theorem to a continuously differentiable vector field written in the
  preferred chart around the base point. We require that the base point not be on the boundary.
  Several useful properties of the solution are proven here, to be used in
  `exists_integral_curve_of_cont_mdiff_tangent_vector_field`. -/
lemma exists_integral_curve_of_cont_mdiff_tangent_vector_field_aux [proper_space E]
  (hv : âˆ€ x, (v x).1 = x) (hcd : cont_mdiff I I.tangent 1 v) (tâ‚€ : â„) (xâ‚€ : M)
  (hx : I.is_interior_point xâ‚€) :
  âˆƒ (Îµ > (0 : â„)) (Î³ : â„ â†’ M), Î³ tâ‚€ = xâ‚€ âˆ§ âˆ€ (t : â„), t âˆˆ set.Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ) â†’
    (Î³ t) âˆˆ ğ“”(I, xâ‚€).source âˆ§
    ğ“”(I, xâ‚€) (Î³ t) âˆˆ interior ğ“”(I, xâ‚€).target âˆ§
    continuous_at Î³ t âˆ§
    has_deriv_at (ğ“”(I, xâ‚€) âˆ˜ Î³) (ğ“”(I.tangent, v xâ‚€) (v (Î³ t))).2 t :=
begin
  have hx1 := (vector_field_cont_diff_on_snd_of_cont_mdiff hv hcd xâ‚€).mono interior_subset,
  have hx2 := is_open.mem_nhds (is_open_interior) hx,
  obtain âŸ¨Îµ, hÎµ, f, hf1, hf2âŸ© :=
    exists_forall_deriv_at_Ioo_eq_of_cont_diff_on_nhds tâ‚€ (ğ“”(I, xâ‚€) xâ‚€) hx1 hx2,
  have hf1' : (ğ“”(I, xâ‚€).symm âˆ˜ f) tâ‚€ = xâ‚€,
  { rw function.comp_apply,
    rw hf1,
    exact ext_chart_at_to_inv I xâ‚€ },
  refine âŸ¨Îµ, hÎµ, ğ“”(I, xâ‚€).symm âˆ˜ f, hf1', _âŸ©,
  intros t ht,
  obtain âŸ¨hf3, hf4âŸ© := hf2 t ht,
  refine âŸ¨_, _, _, _âŸ©,
  { rw [function.comp_apply, â†set.mem_preimage],
    apply set.mem_of_mem_of_subset _ (local_equiv.target_subset_preimage_source _),
    apply set.mem_of_mem_of_subset _
      (interior_subset : interior ğ“”(I, xâ‚€).target âŠ† ğ“”(I, xâ‚€).target),
    rw â†set.mem_preimage,
    exact hf3 },
  { rw [function.comp_apply, â†set.mem_preimage, â†set.mem_preimage],
    apply set.mem_of_mem_of_subset _ (set.inter_subset_right ğ“”(I, xâ‚€).target _),
    rw [local_equiv.target_inter_inv_preimage_preimage,
      set.inter_eq_self_of_subset_right interior_subset],
    exact hf3 },
  { refine continuous_at.comp _ hf4.continuous_at,
    apply ext_chart_continuous_at_symm'',
    exact set.mem_of_mem_of_subset hf3 interior_subset },
  { rw [function.comp_apply, â†function.comp_apply v,
    â†function.comp_apply ğ“”(I.tangent, v xâ‚€), â†written_in_ext_chart_at],
    apply has_deriv_at.congr_of_eventually_eq hf4,
    rw filter.eventually_eq_iff_exists_mem,
    refine âŸ¨set.Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ), is_open.mem_nhds is_open_Ioo ht, _âŸ©,
    intros t' ht',
    rw [function.comp_apply, function.comp_apply],
    apply local_equiv.right_inv,
    exact set.mem_of_mem_of_subset (hf2 t' ht').1 interior_subset }
end

-- how to generalise / simplify?
/-- The derivative of a curve on a manifold is independent of the chosen extended chart.

Note: The hypothesis `hÎ³â‚‚` is equivalent to `I.is_boundary_point (Î³ t)`, but the equivalence has
not been implemented in mathlib yet. -/
lemma curve_has_deriv_at_coord_change
  (hv : âˆ€ x, (v x).1 = x) (xâ‚€ : M) (Î³ : â„ â†’ M) (t : â„)
  (hÎ³â‚ : (Î³ t) âˆˆ ğ“”(I, xâ‚€).source) (hÎ³â‚‚ : ğ“”(I, xâ‚€) (Î³ t) âˆˆ interior ğ“”(I, xâ‚€).target)
  (hd : has_deriv_at (ğ“”(I, xâ‚€) âˆ˜ Î³) (ğ“”(I.tangent, v xâ‚€) (v (Î³ t))).2 t) :
  has_deriv_at ((ğ“”(I, Î³ t) âˆ˜ ğ“”(I, xâ‚€).symm) âˆ˜ (ğ“”(I, xâ‚€) âˆ˜ Î³))
    (ğ“”(I.tangent, v (Î³ t)) (v (Î³ t))).2 t :=
begin
  have : (v (Î³ t)).fst âˆˆ ğ“”(I, (v xâ‚€).1).source,
  { rw [hv, hv],
    exact hÎ³â‚ },
  rw tangent_bundle_core_coord_change_triv' (v (Î³ t)) (v xâ‚€) this,
  apply has_fderiv_at.comp_has_deriv_at _ _ hd,
  rw [hv, hv, function.comp_apply],
  have : set.range I âˆˆ nhds (ğ“”(I, xâ‚€) (Î³ t)),
  { rw mem_nhds_iff,
    refine âŸ¨interior ğ“”(I, xâ‚€).target, _, is_open_interior, hÎ³â‚‚âŸ©,
    refine set.subset.trans interior_subset _,
    rw ext_chart_at_target,
    exact set.inter_subset_right _ _ },
  apply has_fderiv_within_at.has_fderiv_at _ this,
  apply differentiable_within_at.has_fderiv_within_at,
  apply cont_diff_within_at.differentiable_within_at _ le_top,
  apply cont_diff_within_at_ext_coord_change,
  apply local_equiv.mem_symm_trans_source _ hÎ³â‚,
  exact mem_ext_chart_source _ _
end

/-- For any continuously differentiable vector field and any chosen non-boundary point `xâ‚€` on the
  manifold, an integral curve `Î³ : â„ â†’ M` exists such that `Î³ tâ‚€ = xâ‚€` and the tangent vector of `Î³`
  at `t` coincides with the vector field at `Î³ t` for all `t` within an open interval around `tâ‚€`.-/
theorem exists_integral_curve_of_cont_mdiff_tangent_vector_field [proper_space E]
  (hv : âˆ€ x, (v x).1 = x) (hcd : cont_mdiff I I.tangent 1 v)
  (tâ‚€ : â„) (xâ‚€ : M) (hx : I.is_interior_point xâ‚€) :
  âˆƒ (Îµ > (0 : â„)) (Î³ : â„ â†’ M), Î³ tâ‚€ = xâ‚€ âˆ§ âˆ€ (t : â„), t âˆˆ set.Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ) â†’
    has_mfderiv_at ğ“˜(â„, â„) I Î³ t ((1 : â„ â†’L[â„] â„).smul_right (ğ“”(I.tangent, v(Î³ t)) (v (Î³ t))).2) :=
begin
  obtain âŸ¨Îµ, hÎµ, Î³, hf1, hf2âŸ© :=
    exists_integral_curve_of_cont_mdiff_tangent_vector_field_aux I M v hv hcd tâ‚€ xâ‚€ hx,
  refine âŸ¨Îµ, hÎµ, Î³, hf1, _âŸ©,
  intros t ht,
  rw has_mfderiv_at,
  obtain âŸ¨hf3, hf4, hf5, hf6âŸ© := hf2 t ht,
  use hf5,
  rw [ext_chart_model_space_apply, written_in_ext_chart_at, ext_chart_model_space_eq_id,
    local_equiv.refl_symm, local_equiv.refl_coe, function.comp.right_id],
  apply has_deriv_within_at.has_fderiv_within_at,
  apply has_deriv_at.has_deriv_within_at,
  have hd := curve_has_deriv_at_coord_change I M v hv xâ‚€ Î³ t hf3 hf4 hf6,
  apply has_deriv_at.congr_of_eventually_eq hd,
  rw filter.eventually_eq_iff_exists_mem,
  refine âŸ¨set.Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ), is_open.mem_nhds is_open_Ioo ht, _âŸ©,
  intros t' ht',
  rw [function.comp_apply, function.comp_apply, function.comp_apply, local_equiv.left_inv],
  exact (hf2 t' ht').1
end

/-- For any continuously differentiable vector field defined on a manifold without boundary and any
  chosen starting point `xâ‚€ : M`, an integral curve `Î³ : â„ â†’ M` exists such that `Î³ tâ‚€ = xâ‚€` and the
  tangent vector of `Î³` at `t` coincides with the vector field at `Î³ t` for all `t` within an open
  interval around `tâ‚€`. -/
lemma exists_integral_curve_of_cont_mdiff_tangent_vector_field_of_boundaryless
  [proper_space E] [I.boundaryless]
  (hâ‚ : âˆ€ x, (v x).1 = x) (hâ‚‚ : cont_mdiff I I.tangent 1 v) (tâ‚€ : â„) (xâ‚€ : M) :
  âˆƒ (Îµ > (0 : â„)) (Î³ : â„ â†’ M), Î³ tâ‚€ = xâ‚€ âˆ§ âˆ€ (t : â„), t âˆˆ set.Ioo (tâ‚€ - Îµ) (tâ‚€ + Îµ) â†’
    has_mfderiv_at ğ“˜(â„, â„) I Î³ t ((1 : â„ â†’L[â„] â„).smul_right (ğ“”(I.tangent, v(Î³ t)) (v (Î³ t))).2) :=
exists_integral_curve_of_cont_mdiff_tangent_vector_field I M v hâ‚ hâ‚‚ tâ‚€ xâ‚€
    (model_with_corners.boundaryless.is_interior_point I)
