/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import geometry.manifold.charted_space

/-!
# Local properties invariant under a groupoid

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

We study properties of a triple `(g, s, x)` where `g` is a function between two spaces `H` and `H'`,
`s` is a subset of `H` and `x` is a point of `H`. Our goal is to register how such a property
should behave to make sense in charted spaces modelled on `H` and `H'`.

The main examples we have in mind are the properties "`g` is differentiable at `x` within `s`", or
"`g` is smooth at `x` within `s`". We want to develop general results that, when applied in these
specific situations, say that the notion of smooth function in a manifold behaves well under
restriction, intersection, is local, and so on.

## Main definitions

* `local_invariant_prop G G' P` says that a property `P` of a triple `(g, s, x)` is local, and
  invariant under composition by elements of the groupoids `G` and `G'` of `H` and `H'`
  respectively.
* `charted_space.lift_prop_within_at` (resp. `lift_prop_at`, `lift_prop_on` and `lift_prop`):
  given a property `P` of `(g, s, x)` where `g : H â†’ H'`, define the corresponding property
  for functions `M â†’ M'` where `M` and `M'` are charted spaces modelled respectively on `H` and
  `H'`. We define these properties within a set at a point, or at a point, or on a set, or in the
  whole space. This lifting process (obtained by restricting to suitable chart domains) can always
  be done, but it only behaves well under locality and invariance assumptions.

Given `hG : local_invariant_prop G G' P`, we deduce many properties of the lifted property on the
charted spaces. For instance, `hG.lift_prop_within_at_inter` says that `P g s x` is equivalent to
`P g (s âˆ© t) x` whenever `t` is a neighborhood of `x`.

## Implementation notes

We do not use dot notation for properties of the lifted property. For instance, we have
`hG.lift_prop_within_at_congr` saying that if `lift_prop_within_at P g s x` holds, and `g` and `g'`
coincide on `s`, then `lift_prop_within_at P g' s x` holds. We can't call it
`lift_prop_within_at.congr` as it is in the namespace associated to `local_invariant_prop`, not
in the one for `lift_prop_within_at`.
-/

noncomputable theory
open_locale classical manifold topology

open set filter

variables {H M H' M' X : Type*}
variables [topological_space H] [topological_space M] [charted_space H M]
variables [topological_space H'] [topological_space M'] [charted_space H' M']
variables [topological_space X]

namespace structure_groupoid

variables (G : structure_groupoid H) (G' : structure_groupoid H')

/-- Structure recording good behavior of a property of a triple `(f, s, x)` where `f` is a function,
`s` a set and `x` a point. Good behavior here means locality and invariance under given groupoids
(both in the source and in the target). Given such a good behavior, the lift of this property
to charted spaces admitting these groupoids will inherit the good behavior. -/
structure local_invariant_prop (P : (H â†’ H') â†’ (set H) â†’ H â†’ Prop) : Prop :=
(is_local : âˆ€ {s x u} {f : H â†’ H'}, is_open u â†’ x âˆˆ u â†’ (P f s x â†” P f (s âˆ© u) x))
(right_invariance' : âˆ€ {s x f} {e : local_homeomorph H H}, e âˆˆ G â†’ x âˆˆ e.source â†’ P f s x â†’
                      P (f âˆ˜ e.symm) (e.symm â»Â¹' s) (e x))
(congr_of_forall : âˆ€ {s x} {f g : H â†’ H'}, (âˆ€ y âˆˆ s, f y = g y) â†’ f x = g x â†’ P f s x â†’ P g s x)
(left_invariance' : âˆ€ {s x f} {e' : local_homeomorph H' H'}, e' âˆˆ G' â†’ s âŠ† f â»Â¹' e'.source â†’
                     f x âˆˆ e'.source â†’ P f s x â†’ P (e' âˆ˜ f) s x)

variables {G G'} {P : (H â†’ H') â†’ (set H) â†’ H â†’ Prop} {s t u : set H} {x : H}

variable (hG : G.local_invariant_prop G' P)
include hG

namespace local_invariant_prop

lemma congr_set {s t : set H} {x : H} {f : H â†’ H'} (hu : s =á¶ [ğ“ x] t) :
  P f s x â†” P f t x :=
begin
  obtain âŸ¨o, host, ho, hxoâŸ© := mem_nhds_iff.mp hu.mem_iff,
  simp_rw [subset_def, mem_set_of, â† and.congr_left_iff, â† mem_inter_iff, â† set.ext_iff] at host,
  rw [hG.is_local ho hxo, host, â† hG.is_local ho hxo]
end

lemma is_local_nhds {s u : set H} {x : H} {f : H â†’ H'} (hu : u âˆˆ ğ“[s] x) :
  P f s x â†” P f (s âˆ© u) x :=
hG.congr_set $ mem_nhds_within_iff_eventually_eq.mp hu

lemma congr_iff_nhds_within {s : set H} {x : H} {f g : H â†’ H'} (h1 : f =á¶ [ğ“[s] x] g)
  (h2 : f x = g x) : P f s x â†” P g s x :=
by { simp_rw [hG.is_local_nhds h1],
  exact âŸ¨hG.congr_of_forall (Î» y hy, hy.2) h2, hG.congr_of_forall (Î» y hy, hy.2.symm) h2.symmâŸ© }

lemma congr_nhds_within {s : set H} {x : H} {f g : H â†’ H'} (h1 : f =á¶ [ğ“[s] x] g) (h2 : f x = g x)
  (hP : P f s x) : P g s x :=
(hG.congr_iff_nhds_within h1 h2).mp hP

lemma congr_nhds_within' {s : set H} {x : H} {f g : H â†’ H'} (h1 : f =á¶ [ğ“[s] x] g) (h2 : f x = g x)
  (hP : P g s x) : P f s x :=
(hG.congr_iff_nhds_within h1 h2).mpr hP

lemma congr_iff {s : set H} {x : H} {f g : H â†’ H'} (h : f =á¶ [ğ“ x] g) : P f s x â†” P g s x :=
hG.congr_iff_nhds_within (mem_nhds_within_of_mem_nhds h) (mem_of_mem_nhds h : _)

lemma congr {s : set H} {x : H} {f g : H â†’ H'} (h : f =á¶ [ğ“ x] g) (hP : P f s x) : P g s x :=
(hG.congr_iff h).mp hP

lemma congr' {s : set H} {x : H} {f g : H â†’ H'} (h : f =á¶ [ğ“ x] g) (hP : P g s x) : P f s x :=
hG.congr h.symm hP

lemma left_invariance {s : set H} {x : H} {f : H â†’ H'} {e' : local_homeomorph H' H'}
  (he' : e' âˆˆ G') (hfs : continuous_within_at f s x) (hxe' : f x âˆˆ e'.source) :
  P (e' âˆ˜ f) s x â†” P f s x :=
begin
  have h2f := hfs.preimage_mem_nhds_within (e'.open_source.mem_nhds hxe'),
  have h3f := (((e'.continuous_at hxe').comp_continuous_within_at hfs).preimage_mem_nhds_within $
    e'.symm.open_source.mem_nhds $ e'.maps_to hxe'),
  split,
  { intro h,
    rw [hG.is_local_nhds h3f] at h,
    have h2 := hG.left_invariance' (G'.symm he') (inter_subset_right _ _)
      (by exact e'.maps_to hxe') h,
    rw [â† hG.is_local_nhds h3f] at h2,
    refine hG.congr_nhds_within _ (e'.left_inv hxe') h2,
    exact eventually_of_mem h2f (Î» x', e'.left_inv) },
  { simp_rw [hG.is_local_nhds h2f],
    exact hG.left_invariance' he' (inter_subset_right _ _) hxe' }
end

lemma right_invariance {s : set H} {x : H} {f : H â†’ H'} {e : local_homeomorph H H}
  (he : e âˆˆ G) (hxe : x âˆˆ e.source) : P (f âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) â†” P f s x :=
begin
  refine âŸ¨Î» h, _, hG.right_invariance' he hxeâŸ©,
  have := hG.right_invariance' (G.symm he) (e.maps_to hxe) h,
  rw [e.symm_symm, e.left_inv hxe] at this,
  refine hG.congr _ ((hG.congr_set _).mp this),
  { refine eventually_of_mem (e.open_source.mem_nhds hxe) (Î» x' hx', _),
    simp_rw [function.comp_apply, e.left_inv hx'] },
  { rw [eventually_eq_set],
    refine eventually_of_mem (e.open_source.mem_nhds hxe) (Î» x' hx', _),
    simp_rw [mem_preimage, e.left_inv hx'] },
end

end local_invariant_prop
end structure_groupoid

namespace charted_space

/-- Given a property of germs of functions and sets in the model space, then one defines
a corresponding property in a charted space, by requiring that it holds at the preferred chart at
this point. (When the property is local and invariant, it will in fact hold using any chart, see
`lift_prop_within_at_indep_chart`). We require continuity in the lifted property, as otherwise one
single chart might fail to capture the behavior of the function.
-/
def lift_prop_within_at (P : (H â†’ H') â†’ set H â†’ H â†’ Prop)
  (f : M â†’ M') (s : set M) (x : M) : Prop :=
continuous_within_at f s x âˆ§
P (chart_at H' (f x) âˆ˜ f âˆ˜ (chart_at H x).symm) ((chart_at H x).symm â»Â¹' s) (chart_at H x x)

/-- Given a property of germs of functions and sets in the model space, then one defines
a corresponding property of functions on sets in a charted space, by requiring that it holds
around each point of the set, in the preferred charts. -/
def lift_prop_on (P : (H â†’ H') â†’ set H â†’ H â†’ Prop) (f : M â†’ M') (s : set M) :=
âˆ€ x âˆˆ s, lift_prop_within_at P f s x

/-- Given a property of germs of functions and sets in the model space, then one defines
a corresponding property of a function at a point in a charted space, by requiring that it holds
in the preferred chart. -/
def lift_prop_at (P : (H â†’ H') â†’ set H â†’ H â†’ Prop) (f : M â†’ M') (x : M) :=
lift_prop_within_at P f univ x

lemma lift_prop_at_iff {P : (H â†’ H') â†’ set H â†’ H â†’ Prop} {f : M â†’ M'} {x : M} :
  lift_prop_at P f x â†” continuous_at f x âˆ§
  P (chart_at H' (f x) âˆ˜ f âˆ˜ (chart_at H x).symm) univ (chart_at H x x) :=
by rw [lift_prop_at, lift_prop_within_at, continuous_within_at_univ, preimage_univ]

/-- Given a property of germs of functions and sets in the model space, then one defines
a corresponding property of a function in a charted space, by requiring that it holds
in the preferred chart around every point. -/
def lift_prop (P : (H â†’ H') â†’ set H â†’ H â†’ Prop) (f : M â†’ M') :=
âˆ€ x, lift_prop_at P f x

lemma lift_prop_iff {P : (H â†’ H') â†’ set H â†’ H â†’ Prop} {f : M â†’ M'} :
  lift_prop P f â†” continuous f âˆ§
  âˆ€ x, P (chart_at H' (f x) âˆ˜ f âˆ˜ (chart_at H x).symm) univ (chart_at H x x) :=
by simp_rw [lift_prop, lift_prop_at_iff, forall_and_distrib, continuous_iff_continuous_at]

end charted_space
open charted_space

namespace structure_groupoid

variables {G : structure_groupoid H} {G' : structure_groupoid H'}
{e e' : local_homeomorph M H} {f f' : local_homeomorph M' H'}
{P : (H â†’ H') â†’ set H â†’ H â†’ Prop} {g g' : M â†’ M'} {s t : set M} {x : M}
{Q : (H â†’ H) â†’ set H â†’ H â†’ Prop}

lemma lift_prop_within_at_univ : lift_prop_within_at P g univ x â†” lift_prop_at P g x :=
iff.rfl

lemma lift_prop_on_univ : lift_prop_on P g univ â†” lift_prop P g :=
by simp [lift_prop_on, lift_prop, lift_prop_at]

lemma lift_prop_within_at_self {f : H â†’ H'} {s : set H} {x : H} :
  lift_prop_within_at P f s x â†” continuous_within_at f s x âˆ§ P f s x :=
iff.rfl

lemma lift_prop_within_at_self_source {f : H â†’ M'} {s : set H} {x : H} :
  lift_prop_within_at P f s x â†” continuous_within_at f s x âˆ§ P (chart_at H' (f x) âˆ˜ f) s x :=
iff.rfl

lemma lift_prop_within_at_self_target {f : M â†’ H'} :
  lift_prop_within_at P f s x â†”
    continuous_within_at f s x âˆ§
    P (f âˆ˜ (chart_at H x).symm) ((chart_at H x).symm â»Â¹' s) (chart_at H x x) :=
iff.rfl

namespace local_invariant_prop

variable (hG : G.local_invariant_prop G' P)
include hG

/-- `lift_prop_within_at P f s x` is equivalent to a definition where we restrict the set we are
  considering to the domain of the charts at `x` and `f x`. -/
lemma lift_prop_within_at_iff {f : M â†’ M'} :
  lift_prop_within_at P f s x â†”
  continuous_within_at f s x âˆ§ P ((chart_at H' (f x)) âˆ˜ f âˆ˜ (chart_at H x).symm)
  ((chart_at H x).target âˆ© (chart_at H x).symm â»Â¹' (s âˆ© f â»Â¹' (chart_at H' (f x)).source))
  (chart_at H x x) :=
begin
  refine and_congr_right (Î» hf, hG.congr_set _),
  exact local_homeomorph.preimage_eventually_eq_target_inter_preimage_inter hf
    (mem_chart_source H x) (chart_source_mem_nhds H' (f x))
end

lemma lift_prop_within_at_indep_chart_source_aux (g : M â†’ H')
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source)
  (he' : e' âˆˆ G.maximal_atlas M) (xe' : x âˆˆ e'.source) :
  P (g âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) â†” P (g âˆ˜ e'.symm) (e'.symm â»Â¹' s) (e' x) :=
begin
  rw [â† hG.right_invariance (compatible_of_mem_maximal_atlas he he')],
  swap, { simp only [xe, xe'] with mfld_simps },
  simp_rw [local_homeomorph.trans_apply, e.left_inv xe],
  rw [hG.congr_iff],
  { refine hG.congr_set _,
    refine (eventually_of_mem _ $ Î» y (hy : y âˆˆ e'.symm â»Â¹' e.source), _).set_eq,
    { refine (e'.symm.continuous_at $ e'.maps_to xe').preimage_mem_nhds (e.open_source.mem_nhds _),
      simp_rw [e'.left_inv xe', xe] },
    simp_rw [mem_preimage, local_homeomorph.coe_trans_symm, local_homeomorph.symm_symm,
      function.comp_apply, e.left_inv hy] },
  { refine ((e'.eventually_nhds' _ xe').mpr $ e.eventually_left_inverse xe).mono (Î» y hy, _),
    simp only with mfld_simps,
    rw [hy] },
end

lemma lift_prop_within_at_indep_chart_target_aux2 (g : H â†’ M') {x : H} {s : set H}
  (hf : f âˆˆ G'.maximal_atlas M') (xf : g x âˆˆ f.source)
  (hf' : f' âˆˆ G'.maximal_atlas M') (xf' : g x âˆˆ f'.source)
  (hgs : continuous_within_at g s x) :
  P (f âˆ˜ g) s x â†” P (f' âˆ˜ g) s x :=
begin
  have hcont : continuous_within_at (f âˆ˜ g) s x :=
    (f.continuous_at xf).comp_continuous_within_at hgs,
  rw [â† hG.left_invariance (compatible_of_mem_maximal_atlas hf hf') hcont
      (by simp only [xf, xf'] with mfld_simps)],
  refine hG.congr_iff_nhds_within _ (by simp only [xf] with mfld_simps),
  exact (hgs.eventually $ f.eventually_left_inverse xf).mono (Î» y, congr_arg f')
end

lemma lift_prop_within_at_indep_chart_target_aux {g : X â†’ M'} {e : local_homeomorph X H} {x : X}
  {s : set X} (xe : x âˆˆ e.source)
  (hf : f âˆˆ G'.maximal_atlas M') (xf : g x âˆˆ f.source)
  (hf' : f' âˆˆ G'.maximal_atlas M') (xf' : g x âˆˆ f'.source)
  (hgs : continuous_within_at g s x) :
  P (f âˆ˜ g âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) â†” P (f' âˆ˜ g âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) :=
begin
  rw [â† e.left_inv xe] at xf xf' hgs,
  refine hG.lift_prop_within_at_indep_chart_target_aux2 (g âˆ˜ e.symm) hf xf hf' xf' _,
  exact hgs.comp (e.symm.continuous_at $ e.maps_to xe).continuous_within_at subset.rfl
end

/-- If a property of a germ of function `g` on a pointed set `(s, x)` is invariant under the
structure groupoid (by composition in the source space and in the target space), then
expressing it in charted spaces does not depend on the element of the maximal atlas one uses
both in the source and in the target manifolds, provided they are defined around `x` and `g x`
respectively, and provided `g` is continuous within `s` at `x` (otherwise, the local behavior
of `g` at `x` can not be captured with a chart in the target). -/
lemma lift_prop_within_at_indep_chart_aux
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source)
  (he' : e' âˆˆ G.maximal_atlas M) (xe' : x âˆˆ e'.source)
  (hf : f âˆˆ G'.maximal_atlas M') (xf : g x âˆˆ f.source)
  (hf' : f' âˆˆ G'.maximal_atlas M') (xf' : g x âˆˆ f'.source)
  (hgs : continuous_within_at g s x) :
  P (f âˆ˜ g âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) â†” P (f' âˆ˜ g âˆ˜ e'.symm) (e'.symm â»Â¹' s) (e' x) :=
by rw [hG.lift_prop_within_at_indep_chart_source_aux (f âˆ˜ g) he xe he' xe',
    hG.lift_prop_within_at_indep_chart_target_aux xe' hf xf hf' xf' hgs]

lemma lift_prop_within_at_indep_chart [has_groupoid M G] [has_groupoid M' G']
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source)
  (hf : f âˆˆ G'.maximal_atlas M') (xf : g x âˆˆ f.source) :
  lift_prop_within_at P g s x â†”
    continuous_within_at g s x âˆ§ P (f âˆ˜ g âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) :=
and_congr_right $ hG.lift_prop_within_at_indep_chart_aux (chart_mem_maximal_atlas _ _)
  (mem_chart_source _ _) he xe (chart_mem_maximal_atlas _ _) (mem_chart_source _ _) hf xf

/-- A version of `lift_prop_within_at_indep_chart`, only for the source. -/
lemma lift_prop_within_at_indep_chart_source [has_groupoid M G]
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source) :
  lift_prop_within_at P g s x â†” lift_prop_within_at P (g âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) :=
begin
  have := e.symm.continuous_within_at_iff_continuous_within_at_comp_right xe,
  rw [e.symm_symm] at this,
  rw [lift_prop_within_at_self_source, lift_prop_within_at, â† this],
  simp_rw [function.comp_app, e.left_inv xe],
  refine and_congr iff.rfl _,
  rw hG.lift_prop_within_at_indep_chart_source_aux (chart_at H' (g x) âˆ˜ g)
    (chart_mem_maximal_atlas G x) (mem_chart_source H x) he xe,
end

/-- A version of `lift_prop_within_at_indep_chart`, only for the target. -/
lemma lift_prop_within_at_indep_chart_target [has_groupoid M' G']
  (hf : f âˆˆ G'.maximal_atlas M') (xf : g x âˆˆ f.source) :
  lift_prop_within_at P g s x â†” continuous_within_at g s x âˆ§ lift_prop_within_at P (f âˆ˜ g) s x :=
begin
  rw [lift_prop_within_at_self_target, lift_prop_within_at, and.congr_right_iff],
  intro hg,
  simp_rw [(f.continuous_at xf).comp_continuous_within_at hg, true_and],
  exact hG.lift_prop_within_at_indep_chart_target_aux (mem_chart_source _ _)
    (chart_mem_maximal_atlas _ _) (mem_chart_source _ _) hf xf hg
end

/-- A version of `lift_prop_within_at_indep_chart`, that uses `lift_prop_within_at` on both sides.
-/
lemma lift_prop_within_at_indep_chart' [has_groupoid M G] [has_groupoid M' G']
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source)
  (hf : f âˆˆ G'.maximal_atlas M') (xf : g x âˆˆ f.source) :
  lift_prop_within_at P g s x â†”
    continuous_within_at g s x âˆ§ lift_prop_within_at P (f âˆ˜ g âˆ˜ e.symm) (e.symm â»Â¹' s) (e x) :=
begin
  rw [hG.lift_prop_within_at_indep_chart he xe hf xf, lift_prop_within_at_self, and.left_comm,
    iff.comm, and_iff_right_iff_imp],
  intro h,
  have h1 := (e.symm.continuous_within_at_iff_continuous_within_at_comp_right xe).mp h.1,
  have : continuous_at f ((g âˆ˜ e.symm) (e x)),
  { simp_rw [function.comp, e.left_inv xe, f.continuous_at xf] },
  exact this.comp_continuous_within_at h1,
end

lemma lift_prop_on_indep_chart [has_groupoid M G] [has_groupoid M' G']
  (he : e âˆˆ G.maximal_atlas M) (hf : f âˆˆ G'.maximal_atlas M') (h : lift_prop_on P g s)
  {y : H} (hy : y âˆˆ e.target âˆ© e.symm â»Â¹'  (s âˆ© g â»Â¹' f.source)) :
  P (f âˆ˜ g âˆ˜ e.symm) (e.symm â»Â¹' s) y :=
begin
  convert ((hG.lift_prop_within_at_indep_chart he (e.symm_maps_to hy.1) hf hy.2.2).1
    (h _ hy.2.1)).2,
  rw [e.right_inv hy.1],
end

lemma lift_prop_within_at_inter' (ht : t âˆˆ ğ“[s] x) :
  lift_prop_within_at P g (s âˆ© t) x â†” lift_prop_within_at P g s x :=
begin
  rw [lift_prop_within_at, lift_prop_within_at, continuous_within_at_inter' ht, hG.congr_set],
  simp_rw [eventually_eq_set, mem_preimage,
    (chart_at H x).eventually_nhds' (Î» x, x âˆˆ s âˆ© t â†” x âˆˆ s) (mem_chart_source H x)],
  exact (mem_nhds_within_iff_eventually_eq.mp ht).symm.mem_iff
end

lemma lift_prop_within_at_inter (ht : t âˆˆ ğ“ x) :
  lift_prop_within_at P g (s âˆ© t) x â†” lift_prop_within_at P g s x :=
hG.lift_prop_within_at_inter' (mem_nhds_within_of_mem_nhds ht)

lemma lift_prop_at_of_lift_prop_within_at (h : lift_prop_within_at P g s x) (hs : s âˆˆ ğ“ x) :
  lift_prop_at P g x :=
by rwa [â† univ_inter s, hG.lift_prop_within_at_inter hs] at h

lemma lift_prop_within_at_of_lift_prop_at_of_mem_nhds (h : lift_prop_at P g x) (hs : s âˆˆ ğ“ x) :
  lift_prop_within_at P g s x :=
by rwa [â† univ_inter s, hG.lift_prop_within_at_inter hs]

lemma lift_prop_on_of_locally_lift_prop_on
  (h : âˆ€ x âˆˆ s, âˆƒ u, is_open u âˆ§ x âˆˆ u âˆ§ lift_prop_on P g (s âˆ© u)) :
  lift_prop_on P g s :=
begin
  assume x hx,
  rcases h x hx with âŸ¨u, u_open, xu, huâŸ©,
  have := hu x âŸ¨hx, xuâŸ©,
  rwa hG.lift_prop_within_at_inter at this,
  exact is_open.mem_nhds u_open xu,
end

lemma lift_prop_of_locally_lift_prop_on (h : âˆ€ x, âˆƒ u, is_open u âˆ§ x âˆˆ u âˆ§ lift_prop_on P g u) :
  lift_prop P g :=
begin
  rw â† lift_prop_on_univ,
  apply hG.lift_prop_on_of_locally_lift_prop_on (Î» x hx, _),
  simp [h x],
end

lemma lift_prop_within_at_congr_of_eventually_eq
  (h : lift_prop_within_at P g s x) (hâ‚ : g' =á¶ [ğ“[s] x] g) (hx : g' x = g x) :
  lift_prop_within_at P g' s x :=
begin
  refine âŸ¨h.1.congr_of_eventually_eq hâ‚ hx, _âŸ©,
  refine hG.congr_nhds_within' _ (by simp_rw [function.comp_apply,
    (chart_at H x).left_inv (mem_chart_source H x), hx]) h.2,
  simp_rw [eventually_eq, function.comp_app, (chart_at H x).eventually_nhds_within'
    (Î» y, chart_at H' (g' x) (g' y) = chart_at H' (g x) (g y))
    (mem_chart_source H x)],
  exact hâ‚.mono (Î» y hy, by rw [hx, hy])
end

lemma lift_prop_within_at_congr_iff_of_eventually_eq (hâ‚ :  g' =á¶ [ğ“[s] x] g) (hx : g' x = g x) :
  lift_prop_within_at P g' s x â†” lift_prop_within_at P g s x :=
âŸ¨Î» h, hG.lift_prop_within_at_congr_of_eventually_eq h hâ‚.symm hx.symm,
 Î» h, hG.lift_prop_within_at_congr_of_eventually_eq h hâ‚ hxâŸ©

lemma lift_prop_within_at_congr_iff
  (hâ‚ : âˆ€ y âˆˆ s, g' y = g y) (hx : g' x = g x) :
  lift_prop_within_at P g' s x â†” lift_prop_within_at P g s x :=
hG.lift_prop_within_at_congr_iff_of_eventually_eq (eventually_nhds_within_of_forall hâ‚) hx

lemma lift_prop_within_at_congr
  (h : lift_prop_within_at P g s x) (hâ‚ : âˆ€ y âˆˆ s, g' y = g y) (hx : g' x = g x) :
  lift_prop_within_at P g' s x :=
(hG.lift_prop_within_at_congr_iff hâ‚ hx).mpr h

lemma lift_prop_at_congr_iff_of_eventually_eq
  (hâ‚ : g' =á¶ [ğ“ x] g) : lift_prop_at P g' x â†” lift_prop_at P g x :=
hG.lift_prop_within_at_congr_iff_of_eventually_eq (by simp_rw [nhds_within_univ, hâ‚]) hâ‚.eq_of_nhds

lemma lift_prop_at_congr_of_eventually_eq (h : lift_prop_at P g x) (hâ‚ : g' =á¶ [ğ“ x] g) :
  lift_prop_at P g' x :=
(hG.lift_prop_at_congr_iff_of_eventually_eq hâ‚).mpr h

lemma lift_prop_on_congr (h : lift_prop_on P g s) (hâ‚ : âˆ€ y âˆˆ s, g' y = g y) :
  lift_prop_on P g' s :=
Î» x hx, hG.lift_prop_within_at_congr (h x hx) hâ‚ (hâ‚ x hx)

lemma lift_prop_on_congr_iff (hâ‚ : âˆ€ y âˆˆ s, g' y = g y) :
  lift_prop_on P g' s â†” lift_prop_on P g s :=
âŸ¨Î» h, hG.lift_prop_on_congr h (Î» y hy, (hâ‚ y hy).symm), Î» h, hG.lift_prop_on_congr h hâ‚âŸ©

omit hG

lemma lift_prop_within_at_mono_of_mem
  (mono_of_mem : âˆ€ â¦ƒs x tâ¦„ â¦ƒf : H â†’ H'â¦„, s âˆˆ ğ“[t] x â†’ P f s x â†’ P f t x)
  (h : lift_prop_within_at P g s x) (hst : s âˆˆ ğ“[t] x) :
  lift_prop_within_at P g t x :=
begin
  refine âŸ¨h.1.mono_of_mem hst, mono_of_mem _ h.2âŸ©,
  simp_rw [â† mem_map, (chart_at H x).symm.map_nhds_within_preimage_eq (mem_chart_target H x),
    (chart_at H x).left_inv (mem_chart_source H x), hst]
end

lemma lift_prop_within_at_mono
  (mono : âˆ€ â¦ƒs x tâ¦„ â¦ƒf : H â†’ H'â¦„, t âŠ† s â†’ P f s x â†’ P f t x)
  (h : lift_prop_within_at P g s x) (hts : t âŠ† s) :
  lift_prop_within_at P g t x :=
begin
  refine âŸ¨h.1.mono hts, _âŸ©,
  apply mono (Î» y hy, _) h.2,
  simp only with mfld_simps at hy,
  simp only [hy, hts _] with mfld_simps,
end

lemma lift_prop_within_at_of_lift_prop_at
  (mono : âˆ€ â¦ƒs x tâ¦„ â¦ƒf : H â†’ H'â¦„, t âŠ† s â†’ P f s x â†’ P f t x) (h : lift_prop_at P g x) :
  lift_prop_within_at P g s x :=
begin
  rw â† lift_prop_within_at_univ at h,
  exact lift_prop_within_at_mono mono h (subset_univ _),
end

lemma lift_prop_on_mono (mono : âˆ€ â¦ƒs x tâ¦„ â¦ƒf : H â†’ H'â¦„, t âŠ† s â†’ P f s x â†’ P f t x)
  (h : lift_prop_on P g t) (hst : s âŠ† t) :
  lift_prop_on P g s :=
Î» x hx, lift_prop_within_at_mono mono (h x (hst hx)) hst

lemma lift_prop_on_of_lift_prop
  (mono : âˆ€ â¦ƒs x tâ¦„ â¦ƒf : H â†’ H'â¦„, t âŠ† s â†’ P f s x â†’ P f t x) (h : lift_prop P g) :
  lift_prop_on P g s :=
begin
  rw â† lift_prop_on_univ at h,
  exact lift_prop_on_mono mono h (subset_univ _)
end

lemma lift_prop_at_of_mem_maximal_atlas [has_groupoid M G]
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y)
  (he : e âˆˆ maximal_atlas M G) (hx : x âˆˆ e.source) : lift_prop_at Q e x :=
begin
  simp_rw [lift_prop_at,
    hG.lift_prop_within_at_indep_chart he hx G.id_mem_maximal_atlas (mem_univ _),
    (e.continuous_at hx).continuous_within_at, true_and],
  exact hG.congr' (e.eventually_right_inverse' hx) (hQ _)
end

lemma lift_prop_on_of_mem_maximal_atlas [has_groupoid M G]
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y) (he : e âˆˆ maximal_atlas M G) :
  lift_prop_on Q e e.source :=
begin
  assume x hx,
  apply hG.lift_prop_within_at_of_lift_prop_at_of_mem_nhds
    (hG.lift_prop_at_of_mem_maximal_atlas hQ he hx),
  exact is_open.mem_nhds e.open_source hx,
end

lemma lift_prop_at_symm_of_mem_maximal_atlas [has_groupoid M G] {x : H}
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y)
  (he : e âˆˆ maximal_atlas M G) (hx : x âˆˆ e.target) : lift_prop_at Q e.symm x :=
begin
  suffices h : Q (e âˆ˜ e.symm) univ x,
  { have A : e.symm â»Â¹' e.source âˆ© e.target = e.target,
      by mfld_set_tac,
    have : e.symm x âˆˆ e.source, by simp only [hx] with mfld_simps,
    rw [lift_prop_at,
      hG.lift_prop_within_at_indep_chart G.id_mem_maximal_atlas (mem_univ _) he this],
    refine âŸ¨(e.symm.continuous_at hx).continuous_within_at, _âŸ©,
    simp only [h] with mfld_simps },
  exact hG.congr' (e.eventually_right_inverse hx) (hQ x)
end

lemma lift_prop_on_symm_of_mem_maximal_atlas [has_groupoid M G]
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y) (he : e âˆˆ maximal_atlas M G) :
  lift_prop_on Q e.symm e.target :=
begin
  assume x hx,
  apply hG.lift_prop_within_at_of_lift_prop_at_of_mem_nhds
    (hG.lift_prop_at_symm_of_mem_maximal_atlas hQ he hx),
  exact is_open.mem_nhds e.open_target hx,
end

lemma lift_prop_at_chart [has_groupoid M G]
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y) : lift_prop_at Q (chart_at H x) x :=
hG.lift_prop_at_of_mem_maximal_atlas hQ (chart_mem_maximal_atlas G x) (mem_chart_source H x)

lemma lift_prop_on_chart [has_groupoid M G]
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y) :
  lift_prop_on Q (chart_at H x) (chart_at H x).source :=
hG.lift_prop_on_of_mem_maximal_atlas hQ (chart_mem_maximal_atlas G x)

lemma lift_prop_at_chart_symm [has_groupoid M G]
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y) :
  lift_prop_at Q (chart_at H x).symm ((chart_at H x) x) :=
hG.lift_prop_at_symm_of_mem_maximal_atlas hQ (chart_mem_maximal_atlas G x) (by simp)

lemma lift_prop_on_chart_symm [has_groupoid M G]
  (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y) :
  lift_prop_on Q (chart_at H x).symm (chart_at H x).target :=
hG.lift_prop_on_symm_of_mem_maximal_atlas hQ (chart_mem_maximal_atlas G x)

lemma lift_prop_at_of_mem_groupoid (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y)
  {f : local_homeomorph H H} (hf : f âˆˆ G) {x : H} (hx : x âˆˆ f.source) :
  lift_prop_at Q f x :=
lift_prop_at_of_mem_maximal_atlas hG hQ (G.mem_maximal_atlas_of_mem_groupoid hf) hx

lemma lift_prop_on_of_mem_groupoid (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y)
  {f : local_homeomorph H H} (hf : f âˆˆ G) :
  lift_prop_on Q f f.source :=
lift_prop_on_of_mem_maximal_atlas hG hQ (G.mem_maximal_atlas_of_mem_groupoid hf)

lemma lift_prop_id (hG : G.local_invariant_prop G Q) (hQ : âˆ€ y, Q id univ y) :
  lift_prop Q (id : M â†’ M) :=
begin
  simp_rw [lift_prop_iff, continuous_id, true_and],
  exact Î» x, hG.congr' ((chart_at H x).eventually_right_inverse $ mem_chart_target H x) (hQ _)
end

end local_invariant_prop

section local_structomorph

variables (G)
open local_homeomorph

/-- A function from a model space `H` to itself is a local structomorphism, with respect to a
structure groupoid `G` for `H`, relative to a set `s` in `H`, if for all points `x` in the set, the
function agrees with a `G`-structomorphism on `s` in a neighbourhood of `x`. -/
def is_local_structomorph_within_at (f : H â†’ H) (s : set H) (x : H) : Prop :=
x âˆˆ s â†’ âˆƒ (e : local_homeomorph H H), e âˆˆ G âˆ§ eq_on f e.to_fun (s âˆ© e.source) âˆ§ x âˆˆ e.source

/-- For a groupoid `G` which is `closed_under_restriction`, being a local structomorphism is a local
invariant property. -/
lemma is_local_structomorph_within_at_local_invariant_prop [closed_under_restriction G] :
  local_invariant_prop G G (is_local_structomorph_within_at G) :=
{ is_local := begin
    intros s x u f hu hux,
    split,
    { rintros h hx,
      rcases h hx.1 with âŸ¨e, heG, hef, hexâŸ©,
      have : s âˆ© u âˆ© e.source âŠ† s âˆ© e.source := by mfld_set_tac,
      exact âŸ¨e, heG, hef.mono this, hexâŸ© },
    { rintros h hx,
      rcases h âŸ¨hx, huxâŸ© with âŸ¨e, heG, hef, hexâŸ©,
      refine âŸ¨e.restr (interior u), _, _, _âŸ©,
      { exact closed_under_restriction' heG (is_open_interior) },
      { have : s âˆ© u âˆ© e.source = s âˆ© (e.source âˆ© u) := by mfld_set_tac,
        simpa only [this, interior_interior, hu.interior_eq] with mfld_simps using hef },
      { simp only [*, interior_interior, hu.interior_eq] with mfld_simps } }
  end,
  right_invariance' := begin
    intros s x f e' he'G he'x h hx,
    have hxs : x âˆˆ s := by simpa only [e'.left_inv he'x] with mfld_simps using hx,
    rcases h hxs with âŸ¨e, heG, hef, hexâŸ©,
    refine âŸ¨e'.symm.trans e, G.trans (G.symm he'G) heG, _, _âŸ©,
    { intros y hy,
      simp only with mfld_simps at hy,
      simp only [hef âŸ¨hy.1, hy.2.2âŸ©] with mfld_simps },
    { simp only [hex, he'x] with mfld_simps }
  end,
  congr_of_forall := begin
    intros s x f g hfgs hfg' h hx,
    rcases h hx with âŸ¨e, heG, hef, hexâŸ©,
    refine âŸ¨e, heG, _, hexâŸ©,
    intros y hy,
    rw [â† hef hy, hfgs y hy.1]
  end,
  left_invariance' := begin
    intros s x f e' he'G he' hfx h hx,
    rcases h hx with âŸ¨e, heG, hef, hexâŸ©,
    refine âŸ¨e.trans e', G.trans heG he'G, _, _âŸ©,
    { intros y hy,
      simp only with mfld_simps at hy,
      simp only [hef âŸ¨hy.1, hy.2.1âŸ©] with mfld_simps },
    { simpa only [hex, hef âŸ¨hx, hexâŸ©] with mfld_simps using hfx }
  end }

/-- A slight reformulation of `is_local_structomorph_within_at` when `f` is a local homeomorph.
  This gives us an `e` that is defined on a subset of `f.source`. -/
lemma _root_.local_homeomorph.is_local_structomorph_within_at_iff {G : structure_groupoid H}
  [closed_under_restriction G]
  (f : local_homeomorph H H) {s : set H} {x : H} (hx : x âˆˆ f.source âˆª sá¶œ) :
  G.is_local_structomorph_within_at â‡‘f s x â†”
  x âˆˆ s â†’ âˆƒ (e : local_homeomorph H H), e âˆˆ G âˆ§ e.source âŠ† f.source âˆ§
  eq_on f â‡‘e (s âˆ© e.source) âˆ§ x âˆˆ e.source :=
begin
  split,
  { intros hf h2x,
    obtain âŸ¨e, he, hfe, hxeâŸ© := hf h2x,
    refine âŸ¨e.restr f.source, closed_under_restriction' he f.open_source, _, _, hxe, _âŸ©,
    { simp_rw [local_homeomorph.restr_source],
      refine (inter_subset_right _ _).trans interior_subset },
    { intros x' hx', exact hfe âŸ¨hx'.1, hx'.2.1âŸ© },
    { rw [f.open_source.interior_eq], exact or.resolve_right hx (not_not.mpr h2x) } },
  { intros hf hx, obtain âŸ¨e, he, h2e, hfe, hxeâŸ© := hf hx, exact âŸ¨e, he, hfe, hxeâŸ© }
end

/-- A slight reformulation of `is_local_structomorph_within_at` when `f` is a local homeomorph and
  the set we're considering is a superset of `f.source`. -/
lemma _root_.local_homeomorph.is_local_structomorph_within_at_iff' {G : structure_groupoid H}
  [closed_under_restriction G]
  (f : local_homeomorph H H) {s : set H} {x : H} (hs : f.source âŠ† s) (hx : x âˆˆ f.source âˆª sá¶œ) :
  G.is_local_structomorph_within_at â‡‘f s x â†”
  x âˆˆ s â†’ âˆƒ (e : local_homeomorph H H), e âˆˆ G âˆ§ e.source âŠ† f.source âˆ§
  eq_on f â‡‘e e.source âˆ§ x âˆˆ e.source :=
begin
  simp_rw [f.is_local_structomorph_within_at_iff hx],
  refine imp_congr_right (Î» hx, exists_congr $ Î» e, and_congr_right $ Î» he, _),
  refine and_congr_right (Î» h2e, _),
  rw [inter_eq_right_iff_subset.mpr (h2e.trans hs)],
end

/-- A slight reformulation of `is_local_structomorph_within_at` when `f` is a local homeomorph and
  the set we're considering is `f.source`. -/
lemma _root_.local_homeomorph.is_local_structomorph_within_at_source_iff {G : structure_groupoid H}
  [closed_under_restriction G]
  (f : local_homeomorph H H) {x : H} :
  G.is_local_structomorph_within_at â‡‘f f.source x â†”
  x âˆˆ f.source â†’ âˆƒ (e : local_homeomorph H H), e âˆˆ G âˆ§ e.source âŠ† f.source âˆ§
  eq_on f â‡‘e e.source âˆ§ x âˆˆ e.source :=
begin
  have : x âˆˆ f.source âˆª f.sourceá¶œ, { simp_rw [union_compl_self] },
  exact f.is_local_structomorph_within_at_iff' subset.rfl this,
end

variables {Hâ‚ : Type*} [topological_space Hâ‚] {Hâ‚‚ : Type*} [topological_space Hâ‚‚]
   {Hâ‚ƒ : Type*} [topological_space Hâ‚ƒ] [charted_space Hâ‚ Hâ‚‚] [charted_space Hâ‚‚ Hâ‚ƒ]
   {Gâ‚ : structure_groupoid Hâ‚} [has_groupoid Hâ‚‚ Gâ‚] [closed_under_restriction Gâ‚]
   (Gâ‚‚ : structure_groupoid Hâ‚‚) [has_groupoid Hâ‚ƒ Gâ‚‚]

variables (Gâ‚‚)
lemma has_groupoid.comp
  (H : âˆ€ e âˆˆ Gâ‚‚, lift_prop_on (is_local_structomorph_within_at Gâ‚) (e : Hâ‚‚ â†’ Hâ‚‚) e.source) :
  @has_groupoid Hâ‚ _ Hâ‚ƒ _ (charted_space.comp Hâ‚ Hâ‚‚ Hâ‚ƒ) Gâ‚ :=
{ compatible := begin
    rintros _ _ âŸ¨e, f, he, hf, rflâŸ© âŸ¨e', f', he', hf', rflâŸ©,
    apply Gâ‚.locality,
    intros x hx,
    simp only with mfld_simps at hx,
    have hxs : x âˆˆ f.symm â»Â¹' (e.symm â‰«â‚• e').source,
    { simp only [hx] with mfld_simps },
    have hxs' : x âˆˆ f.target âˆ© (f.symm) â»Â¹' ((e.symm â‰«â‚• e').source âˆ© (e.symm â‰«â‚• e') â»Â¹' f'.source),
    { simp only [hx] with mfld_simps },
    obtain âŸ¨Ï†, hÏ†Gâ‚, hÏ†, hÏ†_domâŸ© := local_invariant_prop.lift_prop_on_indep_chart
      (is_local_structomorph_within_at_local_invariant_prop Gâ‚) (Gâ‚.subset_maximal_atlas hf)
      (Gâ‚.subset_maximal_atlas hf') (H _ (Gâ‚‚.compatible he he')) hxs' hxs,
    simp_rw [â† local_homeomorph.coe_trans, local_homeomorph.trans_assoc] at hÏ†,
    simp_rw [local_homeomorph.trans_symm_eq_symm_trans_symm, local_homeomorph.trans_assoc],
    have hs : is_open (f.symm â‰«â‚• e.symm â‰«â‚• e' â‰«â‚• f').source :=
      (f.symm â‰«â‚• e.symm â‰«â‚• e' â‰«â‚• f').open_source,
    refine âŸ¨_, hs.inter Ï†.open_source, _, _âŸ©,
    { simp only [hx, hÏ†_dom] with mfld_simps, },
    { refine Gâ‚.eq_on_source (closed_under_restriction' hÏ†Gâ‚ hs) _,
      rw local_homeomorph.restr_source_inter,
      refine (hÏ†.mono _).restr_eq_on_source,
      mfld_set_tac },
  end }

end local_structomorph

end structure_groupoid
