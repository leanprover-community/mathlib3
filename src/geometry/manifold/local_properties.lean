/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import geometry.manifold.charted_space


noncomputable theory
open_locale classical manifold topological_space
universes u

variables {H : Type u} {M : Type*} [topological_space H] [topological_space M] [charted_space H M]
{H' : Type*} {M' : Type*} [topological_space H'] [topological_space M'] [charted_space H' M']

namespace structure_groupoid

variables (G : structure_groupoid H) (G' : structure_groupoid H')

structure invariant_prop_set_pt (P : set H â†’ H â†’ Prop) : Prop :=
(is_local   : âˆ€ {s x u}, is_open u â†’ x âˆˆ u â†’ (P s x â†” P (s âˆ© u) x))
(invariance : âˆ€ s x (e : local_homeomorph H H), e âˆˆ G â†’ P s x â†’
                P (e.target âˆ© e.symm â»Â¹' s) (e x))

structure invariant_prop_set (P : set H â†’ Prop) : Prop :=
(is_local   : âˆ€ s, (âˆ€ x âˆˆ s, âˆƒ u, is_open u âˆ§ x âˆˆ u âˆ§ P (s âˆ© u)) â†’ P s)
(mono       : âˆ€ s u, P s â†’ is_open u â†’ P (s âˆ© u))
(invariance : âˆ€ s (e : local_homeomorph H H), e âˆˆ G â†’ P s â†’
                P (e.target âˆ© e.symm â»Â¹' s))

lemma invariant_prop_set_pt.invariant_prop_set {P : set H â†’ H â†’ Prop}
  (h : G.invariant_prop_set_pt P) : G.invariant_prop_set (Î» s, (âˆ€ x âˆˆ s, P s x)) :=
begin
  split,
  { assume s hs x hx,
    rcases hs x hx with âŸ¨u, âŸ¨u_open, xu, huâŸ©âŸ©,
    rw h.is_local u_open xu,
    exact hu x âŸ¨hx, xuâŸ© },
  { assume s u hs hu x hx,
    exact (h.is_local hu hx.2).1 (hs x hx.1) },
  { assume s e eG hP x hx,
    set y := e.symm x with hy,
    have : P (e.target âˆ© e.symm â»Â¹' s) (e y) :=
      h.invariance s y e eG (hP y hx.2),
    rwa [hy, e.right_inv hx.1] at this }
end

structure invariant_prop_fun_set_pt (P : (H â†’ H') â†’ (set H) â†’ H â†’ Prop) : Prop :=
(is_local : âˆ€ {s x u} {f : H â†’ H'}, is_open u â†’ x âˆˆ u â†’ (P f s x â†” P f (s âˆ© u) x))
(right_invariance : âˆ€ {s x f} {e : local_homeomorph H H}, e âˆˆ G â†’ P f s x â†’
                      P (f âˆ˜ e.symm) (e.target âˆ© e.symm â»Â¹' s) (e x))
(congr : âˆ€ {s x} {f g : H â†’ H'}, (âˆ€ y âˆˆ s, f y = g y) â†’ P f s x â†’ P g s x)
(left_invariance : âˆ€ {s x f} {e' : local_homeomorph H' H'}, e' âˆˆ G' â†’ s âŠ† f â»Â¹' (e'.source) â†’
                     P f s x â†’ P (e' âˆ˜ f) s x)

end structure_groupoid


/-- If one can define a property of pointed sets in the model space, then one define a
corresponding property in the manifold, using the preferred chart at the point. -/
def charted_space.lift_prop_set_pt (P : set H â†’ H â†’ Prop) (s : set M) (x : M) : Prop :=
P ((chart_at H x).target âˆ© (chart_at H x).symm â»Â¹' s) (chart_at H x x)

/-- If one can define a property of sets in the model space, then one define a
corresponding property in the manifold, by requiring that it holds for all preferred charts. -/
def charted_space.lift_prop_set (P : set H â†’ Prop) (s : set M) : Prop :=
âˆ€ x, P ((chart_at H x).target âˆ© (chart_at H x).symm â»Â¹' s)

/-- If one can define a property of germs of functions and sets in the model space, then one define
a corresponding property in the manifold, by requiring that it holds for all preferred charts. -/
def charted_space.lift_fun_prop_set (P : (H â†’ H') â†’ set H â†’ H â†’ Prop)
  (f : M â†’ M') (s : set M) (x : M) : Prop :=
continuous_within_at f s x âˆ§
P ((chart_at H' (f x)) âˆ˜ f âˆ˜ (chart_at H x).symm)
  ((chart_at H x).target âˆ© (chart_at H x).symm â»Â¹' (s âˆ© f â»Â¹' (chart_at H' (f x)).source))
  (chart_at H x x)

namespace structure_groupoid

variables {G : structure_groupoid H} {G' : structure_groupoid H'}
{e e' : local_homeomorph M H} {f f' : local_homeomorph M' H'}

/-- If a property of a pointed set is invariant under the structure groupoid, then expressing it in
the charted space does not depend on the element of the maximal atlas one uses provided it
contains the point in its source. -/
lemma invariant_prop_set_pt.indep_chart {P : set H â†’ H â†’ Prop}
  (hG : G.invariant_prop_set_pt P) (x : M)
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source)
  (he' : e' âˆˆ G.maximal_atlas M) (xe' : x âˆˆ e'.source)
  {s : set M} (h : P (e.target âˆ© e.symm â»Â¹' s) (e x)) :
  P (e'.target âˆ© e'.symm â»Â¹' s) (e' x) :=
begin
  set c := e.symm â‰«â‚• e' with hc,
  have cG : c âˆˆ G := compatible_of_mem_maximal_atlas he he',
  suffices A : P ((e'.target âˆ© e'.symm â»Â¹' s) âˆ© c.target) (e' x),
  { apply (hG.is_local c.open_target _).2 A,
    simp [c, local_equiv.trans_target, xe, xe'] },
  set t := e.target âˆ© e.symm â»Â¹' s with ht,
  have B : (e'.target âˆ© e'.symm â»Â¹' s) âˆ© c.target =
           c.target âˆ© c.symm â»Â¹' t,
  { ext y,
    simp [c, local_equiv.trans_source, local_equiv.trans_target],
    split,
    { assume hy,
      simp [hy] },
    { assume hy,
      simp [hy],
      simpa [hy] using hy } },
  have : P (c.target âˆ© c.symm â»Â¹' t) (c (e x)) :=
    hG.invariance _ _ _ cG h,
  convert this using 1,
  { exact B },
  { simp [c, xe, xe'] }
end

/-- If a property of a pointed set is invariant under the structure groupoid, then it is equivalent
to express it in the charted space using the preferred chart at the point, or any maximal atlas
member containing the point in its source. -/
lemma invariant_prop_set_pt.lift_prop_set_pt_iff [has_groupoid M G]
  {P : set H â†’ H â†’ Prop} (hG : G.invariant_prop_set_pt P) {e : local_homeomorph M H} (x : M)
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source) (s : set M) :
  charted_space.lift_prop_set_pt P s x â†” P (e.target âˆ© e.symm â»Â¹' s) (e x) :=
âŸ¨hG.indep_chart x (G.chart_mem_maximal_atlas x) (mem_chart_source H x) he xe,
hG.indep_chart x he xe (G.chart_mem_maximal_atlas x) (mem_chart_source H x)âŸ©


/-- If a property of a germ of function `g` on a pointed set `(s, x)` is invariant under the
structure groupoid (in the source space and in the target space), then expressing it in charted
spaces does not depend on the element of the maximal atlas one uses both in the source
and in the target manifolds, provided they are defined around `x` and `g x` respectively, and
provided `g` is continuous within `s` at `x` (otherwise, the local behavior of `g` at `x` can not
be captured with a chart in the target). -/
lemma invariant_prop_fun_set_pt.indep_chart {P : (H â†’ H') â†’ set H â†’ H â†’ Prop}
  (hG : G.invariant_prop_fun_set_pt G' P) (g : M â†’ M') (x : M)
  (he : e âˆˆ G.maximal_atlas M) (xe : x âˆˆ e.source)
  (he' : e' âˆˆ G.maximal_atlas M) (xe' : x âˆˆ e'.source)
  (hf : f âˆˆ G'.maximal_atlas M') (xf : g x âˆˆ f.source)
  (hf' : f' âˆˆ G'.maximal_atlas M') (xf' : g x âˆˆ f'.source)
  {s : set M} (hgs : continuous_within_at g s x)
  (h : P (f âˆ˜ g âˆ˜ e.symm) (e.target âˆ© e.symm â»Â¹' (s âˆ© gâ»Â¹' f.source)) (e x)) :
  P (f' âˆ˜ g âˆ˜ e'.symm) (e'.target âˆ© e'.symm â»Â¹' (s âˆ© gâ»Â¹' f'.source)) (e' x) :=
begin
  obtain âŸ¨o, o_open, xo, oe, oe', of, of'âŸ© :
    âˆƒ (o : set M), is_open o âˆ§ x âˆˆ o âˆ§ o âŠ† e.source âˆ§ o âŠ† e'.source âˆ§
      o âˆ© s âŠ† g â»Â¹' f.source âˆ§ o âˆ© s âŠ†  gâ»Â¹' f'.to_local_equiv.source,
  { have : f.source âˆ© f'.source âˆˆ ğ“ (g x) :=
      mem_nhds_sets (is_open_inter f.open_source f'.open_source) âŸ¨xf, xf'âŸ©,
    rcases mem_nhds_within.1 (hgs.preimage_mem_nhds_within this) with âŸ¨u, u_open, xu, huâŸ©,
    refine âŸ¨u âˆ© e.source âˆ© e'.source, _, âŸ¨âŸ¨xu, xeâŸ©, xe'âŸ©, _, _, _, _âŸ©,
    { exact is_open_inter (is_open_inter u_open e.open_source) e'.open_source },
    { assume x hx, exact hx.1.2 },
    { assume x hx, exact hx.2 },
    { assume x hx, exact (hu âŸ¨hx.1.1.1, hx.2âŸ©).1 },
    { assume x hx, exact (hu âŸ¨hx.1.1.1, hx.2âŸ©).2 } },
  have A : P (f âˆ˜ g âˆ˜ e.symm)
             (e.target âˆ© e.symm â»Â¹' (s âˆ© gâ»Â¹' f.source) âˆ© (e.target âˆ© e.symm â»Â¹' o)) (e x),
  { apply (hG.is_local _ _).1 h,
    { exact e.continuous_on_symm.preimage_open_of_open e.open_target o_open },
    { simp [xe, xo] } },
  have B : P ((f.symm â‰«â‚• f') âˆ˜ (f âˆ˜ g âˆ˜ e.symm))
             (e.target âˆ© e.symm â»Â¹' (s âˆ© gâ»Â¹' f.source) âˆ© (e.target âˆ© e.symm â»Â¹' o)) (e x),
  { apply hG.left_invariance (compatible_of_mem_maximal_atlas hf hf') _ A,
    assume y hy,
    simp at hy,
    have : e.symm y âˆˆ o âˆ© s, by simp [hy],
    simpa [hy] using of' this },
  have C : P (f' âˆ˜ g âˆ˜ e.symm)
             (e.target âˆ© e.symm â»Â¹' (s âˆ© gâ»Â¹' f.source) âˆ© (e.target âˆ© e.symm â»Â¹' o)) (e x),
  { apply hG.congr _ B,
    assume y hy,
    simp only [local_homeomorph.coe_trans, function.comp_app],
    rw f.left_inv,
    apply of,
    simp at hy,
    simp [hy] },
  let w := e.symm â‰«â‚• e',
  let ow := w.target âˆ© w.symm â»Â¹'
    (e.target âˆ© e.symm â»Â¹' (s âˆ© gâ»Â¹' f.source) âˆ© (e.target âˆ© e.symm â»Â¹' o)),
  have wG : w âˆˆ G := compatible_of_mem_maximal_atlas he he',
  have D : P ((f' âˆ˜ g âˆ˜ e.symm) âˆ˜ w.symm) ow (w (e x)) := hG.right_invariance wG C,
  have E : P (f' âˆ˜ g âˆ˜ e'.symm) ow (w (e x)),
  { apply hG.congr _ D,
    assume y hy,
    simp only [local_homeomorph.coe_trans_symm, function.comp_app, local_homeomorph.symm_symm],
    rw e.left_inv,
    simp at hy,
    simp [hy] },
  have : w (e x) = e' x, by simp [w, xe],
  rw this at E,
  have : ow = (e'.target âˆ© e'.symm â»Â¹' (s âˆ© gâ»Â¹' f'.source))
               âˆ© (w.target âˆ© (e'.target âˆ© e'.symm â»Â¹' o)),
  { ext y,
    split,
    { assume hy,
      have : e.symm (e ((e'.symm) y)) = e'.symm y, by { simp at hy, simp [hy] },
      simp [this] at hy,
      have : g (e'.symm y) âˆˆ f'.source, by { apply of', simp [hy] },
      simp [hy, this] },
    { assume hy,
      simp at hy,
      have : g (e'.symm y) âˆˆ f.source, by { apply of, simp [hy] },
      simp [this, hy] } },
  rw this at E,
  apply (hG.is_local _ _).2 E,
  { exact is_open_inter w.open_target
      (e'.continuous_on_symm.preimage_open_of_open e'.open_target o_open) },
  { simp [xe', xe, xo] }
end

end structure_groupoid
