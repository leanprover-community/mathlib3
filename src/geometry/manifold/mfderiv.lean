/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import geometry.manifold.basic_smooth_bundle

/-!
# The derivative of functions between smooth manifolds

Let `M` and `M'` be two smooth manifolds with corners over a field `ğ•œ` (with respective models with
corners `I` on `(E, H)` and `I'` on `(E', H')`), and let `f : M â†’ M'`. We define the
derivative of the function at a point, within a set or along the whole space, mimicking the API
for (FrÃ©chet) derivatives. It is denoted by `mfderiv I I' f x`, where "m" stands for "manifold" and
"f" for "FrÃ©chet" (as in the usual derivative `fderiv ğ•œ f x`).

## Main definitions

* `unique_mdiff_on I s` : predicate saying that, at each point of the set `s`, a function can have
  at most one derivative. This technical condition is important when we define
  `mfderiv_within` below, as otherwise there is an arbitrary choice in the derivative,
  and many properties will fail (for instance the chain rule). This is analogous to
  `unique_diff_on ğ•œ s` in a vector space.

Let `f` be a map between smooth manifolds. The following definitions follow the `fderiv` API.

* `mfderiv I I' f x` : the derivative of `f` at `x`, as a continuous linear map from the tangent
  space at `x` to the tangent space at `f x`. If the map is not differentiable, this is `0`.
* `mfderiv_within I I' f s x` : the derivative of `f` at `x` within `s`, as a continuous linear map
  from the tangent space at `x` to the tangent space at `f x`. If the map is not differentiable
  within `s`, this is `0`.
* `mdifferentiable_at I I' f x` : Prop expressing whether `f` is differentiable at `x`.
* `mdifferentiable_within_at ğ•œ f s x` : Prop expressing whether `f` is differentiable within `s`
  at `x`.
* `has_mfderiv_at I I' f s x f'` : Prop expressing whether `f` has `f'` as a derivative at `x`.
* `has_mfderiv_within_at I I' f s x f'` : Prop expressing whether `f` has `f'` as a derivative
  within `s` at `x`.
* `mdifferentiable_on I I' f s` : Prop expressing that `f` is differentiable on the set `s`.
* `mdifferentiable I I' f` : Prop expressing that `f` is differentiable everywhere.
* `tangent_map I I' f` : the derivative of `f`, as a map from the tangent bundle of `M` to the
  tangent bundle of `M'`.

We also establish results on the differential of the identity, constant functions, charts, extended
charts. For functions between vector spaces, we show that the usual notions and the manifold notions
coincide.

## Implementation notes

The tangent bundle is constructed using the machinery of topological fiber bundles, for which one
can define bundled morphisms and construct canonically maps from the total space of one bundle to
the total space of another one. One could use this mechanism to construct directly the derivative
of a smooth map. However, we want to define the derivative of any map (and let it be zero if the map
is not differentiable) to avoid proof arguments everywhere. This means we have to go back to the
details of the definition of the total space of a fiber bundle constructed from core, to cook up a
suitable definition of the derivative. It is the following: at each point, we have a preferred chart
(used to identify the fiber above the point with the model vector space in fiber bundles). Then one
should read the function using these preferred charts at `x` and `f x`, and take the derivative
of `f` in these charts.

Due to the fact that we are working in a model with corners, with an additional embedding `I` of the
model space `H` in the model vector space `E`, the charts taking values in `E` are not the original
charts of the manifold, but those ones composed with `I`, called extended charts. We define
`written_in_ext_chart I I' x f` for the function `f` written in the preferred extended charts.  Then
the manifold derivative of `f`, at `x`, is just the usual derivative of `written_in_ext_chart I I' x
f`, at the point `(ext_chart_at I x) x`.

There is a subtelty with respect to continuity: if the function is not continuous, then the image
of a small open set around `x` will not be contained in the source of the preferred chart around
`f x`, which means that when reading `f` in the chart one is losing some information. To avoid this,
we include continuity in the definition of differentiablity (which is reasonable since with any
definition, differentiability implies continuity).

*Warning*: the derivative (even within a subset) is a linear map on the whole tangent space. Suppose
that one is given a smooth submanifold `N`, and a function which is smooth on `N` (i.e., its
restriction to the subtype  `N` is smooth). Then, in the whole manifold `M`, the property
`mdifferentiable_on I I' f N` holds. However, `mfderiv_within I I' f N` is not uniquely defined
(what values would one choose for vectors that are transverse to `N`?), which can create issues down
the road. The problem here is that knowing the value of `f` along `N` does not determine the
differential of `f` in all directions. This is in contrast to the case where `N` would be an open
subset, or a submanifold with boundary of maximal dimension, where this issue does not appear.
The predicate `unique_mdiff_on I N` indicates that the derivative along `N` is unique if it exists,
and is an assumption in most statements requiring a form of uniqueness.

On a vector space, the manifold derivative and the usual derivative are equal. This means in
particular that they live on the same space, i.e., the tangent space is defeq to the original vector
space. To get this property is a motivation for our definition of the tangent space as a single
copy of the vector space, instead of more usual definitions such as the space of derivations, or
the space of equivalence classes of smooth curves in the manifold.

## Tags
Derivative, manifold
-/

noncomputable theory
open_locale classical topological_space manifold

open set

universe u

section derivatives_definitions
/-!
### Derivative of maps between manifolds

The derivative of a smooth map `f` between smooth manifold `M` and `M'` at `x` is a bounded linear
map from the tangent space to `M` at `x`, to the tangent space to `M'` at `f x`. Since we defined
the tangent space using one specific chart, the formula for the derivative is written in terms of
this specific chart.

We use the names `mdifferentiable` and `mfderiv`, where the prefix letter `m` means "manifold".
-/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
{M : Type*} [topological_space M] [charted_space H M]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] (I' : model_with_corners ğ•œ E' H')
{M' : Type*} [topological_space M'] [charted_space H' M']

/-- Predicate ensuring that, at a point and within a set, a function can have at most one
derivative. This is expressed using the preferred chart at the considered point. -/
def unique_mdiff_within_at (s : set M) (x : M) :=
unique_diff_within_at ğ•œ ((ext_chart_at I x).symm â»Â¹' s âˆ© range I) ((ext_chart_at I x) x)

/-- Predicate ensuring that, at all points of a set, a function can have at most one derivative. -/
def unique_mdiff_on (s : set M) :=
âˆ€xâˆˆs, unique_mdiff_within_at I s x

/-- Conjugating a function to write it in the preferred charts around `x`. The manifold derivative
of `f` will just be the derivative of this conjugated function. -/
@[simp, mfld_simps] def written_in_ext_chart_at (x : M) (f : M â†’ M') : E â†’ E' :=
(ext_chart_at I' (f x)) âˆ˜ f âˆ˜ (ext_chart_at I x).symm

/-- `mdifferentiable_within_at I I' f s x` indicates that the function `f` between manifolds
has a derivative at the point `x` within the set `s`.
This is a generalization of `differentiable_within_at` to manifolds.

We require continuity in the definition, as otherwise points close to `x` in `s` could be sent by
`f` outside of the chart domain around `f x`. Then the chart could do anything to the image points,
and in particular by coincidence `written_in_ext_chart_at I I' x f` could be differentiable, while
this would not mean anything relevant. -/
def mdifferentiable_within_at (f : M â†’ M') (s : set M) (x : M) :=
continuous_within_at f s x âˆ§
differentiable_within_at ğ•œ (written_in_ext_chart_at I I' x f)
  ((ext_chart_at I x).symm â»Â¹' s âˆ© range I) ((ext_chart_at I x) x)

/-- `mdifferentiable_at I I' f x` indicates that the function `f` between manifolds
has a derivative at the point `x`.
This is a generalization of `differentiable_at` to manifolds.

We require continuity in the definition, as otherwise points close to `x` could be sent by
`f` outside of the chart domain around `f x`. Then the chart could do anything to the image points,
and in particular by coincidence `written_in_ext_chart_at I I' x f` could be differentiable, while
this would not mean anything relevant. -/
def mdifferentiable_at (f : M â†’ M') (x : M) :=
continuous_at f x âˆ§
differentiable_within_at ğ•œ (written_in_ext_chart_at I I' x f) (range I)
  ((ext_chart_at I x) x)

/-- `mdifferentiable_on I I' f s` indicates that the function `f` between manifolds
has a derivative within `s` at all points of `s`.
This is a generalization of `differentiable_on` to manifolds. -/
def mdifferentiable_on (f : M â†’ M') (s : set M) :=
âˆ€x âˆˆ s, mdifferentiable_within_at I I' f s x

/-- `mdifferentiable I I' f` indicates that the function `f` between manifolds
has a derivative everywhere.
This is a generalization of `differentiable` to manifolds. -/
def mdifferentiable (f : M â†’ M') :=
âˆ€x, mdifferentiable_at I I' f x

/-- Prop registering if a local homeomorphism is a local diffeomorphism on its source -/
def local_homeomorph.mdifferentiable (f : local_homeomorph M M') :=
(mdifferentiable_on I I' f f.source) âˆ§ (mdifferentiable_on I' I f.symm f.target)

variables [smooth_manifold_with_corners I M] [smooth_manifold_with_corners I' M']

/-- `has_mfderiv_within_at I I' f s x f'` indicates that the function `f` between manifolds
has, at the point `x` and within the set `s`, the derivative `f'`. Here, `f'` is a continuous linear
map from the tangent space at `x` to the tangent space at `f x`.

This is a generalization of `has_fderiv_within_at` to manifolds (as indicated by the prefix `m`).
The order of arguments is changed as the type of the derivative `f'` depends on the choice of `x`.

We require continuity in the definition, as otherwise points close to `x` in `s` could be sent by
`f` outside of the chart domain around `f x`. Then the chart could do anything to the image points,
and in particular by coincidence `written_in_ext_chart_at I I' x f` could be differentiable, while
this would not mean anything relevant. -/
def has_mfderiv_within_at (f : M â†’ M') (s : set M) (x : M)
  (f' : tangent_space I x â†’L[ğ•œ] tangent_space I' (f x)) :=
continuous_within_at f s x âˆ§
has_fderiv_within_at (written_in_ext_chart_at I I' x f : E â†’ E') f'
  ((ext_chart_at I x).symm â»Â¹' s âˆ© range I) ((ext_chart_at I x) x)

/-- `has_mfderiv_at I I' f x f'` indicates that the function `f` between manifolds
has, at the point `x`, the derivative `f'`. Here, `f'` is a continuous linear
map from the tangent space at `x` to the tangent space at `f x`.

We require continuity in the definition, as otherwise points close to `x` `s` could be sent by
`f` outside of the chart domain around `f x`. Then the chart could do anything to the image points,
and in particular by coincidence `written_in_ext_chart_at I I' x f` could be differentiable, while
this would not mean anything relevant. -/
def has_mfderiv_at (f : M â†’ M') (x : M)
  (f' : tangent_space I x â†’L[ğ•œ] tangent_space I' (f x)) :=
continuous_at f x âˆ§
has_fderiv_within_at (written_in_ext_chart_at I I' x f : E â†’ E') f' (range I)
  ((ext_chart_at I x) x)

/-- Let `f` be a function between two smooth manifolds. Then `mfderiv_within I I' f s x` is the
derivative of `f` at `x` within `s`, as a continuous linear map from the tangent space at `x` to the
tangent space at `f x`. -/
def mfderiv_within (f : M â†’ M') (s : set M) (x : M) :
  tangent_space I x â†’L[ğ•œ] tangent_space I' (f x) :=
if h : mdifferentiable_within_at I I' f s x then
(fderiv_within ğ•œ (written_in_ext_chart_at I I' x f) ((ext_chart_at I x).symm â»Â¹' s âˆ© range I)
  ((ext_chart_at I x) x) : _)
else 0

/-- Let `f` be a function between two smooth manifolds. Then `mfderiv I I' f x` is the derivative of
`f` at `x`, as a continuous linear map from the tangent space at `x` to the tangent space at
`f x`. -/
def mfderiv (f : M â†’ M') (x : M) : tangent_space I x â†’L[ğ•œ] tangent_space I' (f x) :=
if h : mdifferentiable_at I I' f x then
(fderiv_within ğ•œ (written_in_ext_chart_at I I' x f : E â†’ E') (range I)
  ((ext_chart_at I x) x) : _)
else 0

/-- The derivative within a set, as a map between the tangent bundles -/
def tangent_map_within (f : M â†’ M') (s : set M) : tangent_bundle I M â†’ tangent_bundle I' M' :=
Î»p, âŸ¨f p.1, (mfderiv_within I I' f s p.1 : tangent_space I p.1 â†’ tangent_space I' (f p.1)) p.2âŸ©

/-- The derivative, as a map between the tangent bundles -/
def tangent_map (f : M â†’ M') : tangent_bundle I M â†’ tangent_bundle I' M' :=
Î»p, âŸ¨f p.1, (mfderiv I I' f p.1 : tangent_space I p.1 â†’ tangent_space I' (f p.1)) p.2âŸ©

end derivatives_definitions

section derivatives_properties
/-! ### Unique differentiability sets in manifolds -/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
{M : Type*} [topological_space M] [charted_space H M] --
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M']
{E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']
{f fâ‚€ fâ‚ : M â†’ M'}
{x : M}
{s t : set M}
{g : M' â†’ M''}
{u : set M'}

lemma unique_mdiff_within_at_univ : unique_mdiff_within_at I univ x :=
begin
  unfold unique_mdiff_within_at,
  simp only [preimage_univ, univ_inter],
  exact I.unique_diff _ (mem_range_self _)
end
variable {I}

lemma unique_mdiff_within_at_iff {s : set M} {x : M} :
  unique_mdiff_within_at I s x â†”
  unique_diff_within_at ğ•œ ((ext_chart_at I x).symm â»Â¹' s âˆ© (ext_chart_at I x).target)
  ((ext_chart_at I x) x) :=
begin
  apply unique_diff_within_at_congr,
  rw [nhds_within_inter, nhds_within_inter, nhds_within_ext_chart_target_eq]
end

lemma unique_mdiff_within_at.mono (h : unique_mdiff_within_at I s x) (st : s âŠ† t) :
  unique_mdiff_within_at I t x :=
unique_diff_within_at.mono h $ inter_subset_inter (preimage_mono st) (subset.refl _)

lemma unique_mdiff_within_at.inter' (hs : unique_mdiff_within_at I s x) (ht : t âˆˆ ğ“[s] x) :
  unique_mdiff_within_at I (s âˆ© t) x :=
begin
  rw [unique_mdiff_within_at, ext_chart_preimage_inter_eq],
  exact unique_diff_within_at.inter' hs (ext_chart_preimage_mem_nhds_within I x ht)
end

lemma unique_mdiff_within_at.inter (hs : unique_mdiff_within_at I s x) (ht : t âˆˆ ğ“ x) :
  unique_mdiff_within_at I (s âˆ© t) x :=
begin
  rw [unique_mdiff_within_at, ext_chart_preimage_inter_eq],
  exact unique_diff_within_at.inter hs (ext_chart_preimage_mem_nhds I x ht)
end

lemma is_open.unique_mdiff_within_at (xs : x âˆˆ s) (hs : is_open s) : unique_mdiff_within_at I s x :=
begin
  have := unique_mdiff_within_at.inter (unique_mdiff_within_at_univ I) (is_open.mem_nhds hs xs),
  rwa univ_inter at this
end

lemma unique_mdiff_on.inter (hs : unique_mdiff_on I s) (ht : is_open t) :
  unique_mdiff_on I (s âˆ© t) :=
Î»x hx, unique_mdiff_within_at.inter (hs _ hx.1) (is_open.mem_nhds ht hx.2)

lemma is_open.unique_mdiff_on (hs : is_open s) : unique_mdiff_on I s :=
Î»x hx, is_open.unique_mdiff_within_at hx hs

lemma unique_mdiff_on_univ : unique_mdiff_on I (univ : set M) :=
is_open_univ.unique_mdiff_on

/- We name the typeclass variables related to `smooth_manifold_with_corners` structure as they are
necessary in lemmas mentioning the derivative, but not in lemmas about differentiability, so we
want to include them or omit them when necessary. -/
variables [Is : smooth_manifold_with_corners I M] [I's : smooth_manifold_with_corners I' M']
[I''s : smooth_manifold_with_corners I'' M'']
{f' fâ‚€' fâ‚' : tangent_space I x â†’L[ğ•œ] tangent_space I' (f x)}
{g' : tangent_space I' (f x) â†’L[ğ•œ] tangent_space I'' (g (f x))}

/-- `unique_mdiff_within_at` achieves its goal: it implies the uniqueness of the derivative. -/
theorem unique_mdiff_within_at.eq (U : unique_mdiff_within_at I s x)
  (h : has_mfderiv_within_at I I' f s x f') (hâ‚ : has_mfderiv_within_at I I' f s x fâ‚') :
  f' = fâ‚' :=
U.eq h.2 hâ‚.2

theorem unique_mdiff_on.eq (U : unique_mdiff_on I s) (hx : x âˆˆ s)
  (h : has_mfderiv_within_at I I' f s x f') (hâ‚ : has_mfderiv_within_at I I' f s x fâ‚') :
  f' = fâ‚' :=
unique_mdiff_within_at.eq (U _ hx) h hâ‚


/-!
### General lemmas on derivatives of functions between manifolds

We mimick the API for functions between vector spaces
-/

lemma mdifferentiable_within_at_iff {f : M â†’ M'} {s : set M} {x : M} :
  mdifferentiable_within_at I I' f s x â†”
  continuous_within_at f s x âˆ§
  differentiable_within_at ğ•œ (written_in_ext_chart_at I I' x f)
    ((ext_chart_at I x).target âˆ© (ext_chart_at I x).symm â»Â¹' s) ((ext_chart_at I x) x) :=
begin
  refine and_congr iff.rfl (exists_congr $ Î» f', _),
  rw [inter_comm],
  simp only [has_fderiv_within_at, nhds_within_inter, nhds_within_ext_chart_target_eq]
end

include Is I's

lemma mfderiv_within_zero_of_not_mdifferentiable_within_at
  (h : Â¬ mdifferentiable_within_at I I' f s x) : mfderiv_within I I' f s x = 0 :=
by simp only [mfderiv_within, h, dif_neg, not_false_iff]

lemma mfderiv_zero_of_not_mdifferentiable_at
  (h : Â¬ mdifferentiable_at I I' f x) : mfderiv I I' f x = 0 :=
by simp only [mfderiv, h, dif_neg, not_false_iff]

theorem has_mfderiv_within_at.mono (h : has_mfderiv_within_at I I' f t x f') (hst : s âŠ† t) :
  has_mfderiv_within_at I I' f s x f' :=
âŸ¨ continuous_within_at.mono h.1 hst,
  has_fderiv_within_at.mono h.2 (inter_subset_inter (preimage_mono hst) (subset.refl _)) âŸ©

theorem has_mfderiv_at.has_mfderiv_within_at
  (h : has_mfderiv_at I I' f x f') : has_mfderiv_within_at I I' f s x f' :=
âŸ¨ continuous_at.continuous_within_at h.1, has_fderiv_within_at.mono h.2 (inter_subset_right _ _) âŸ©

lemma has_mfderiv_within_at.mdifferentiable_within_at (h : has_mfderiv_within_at I I' f s x f') :
  mdifferentiable_within_at I I' f s x :=
âŸ¨h.1, âŸ¨f', h.2âŸ©âŸ©

lemma has_mfderiv_at.mdifferentiable_at (h : has_mfderiv_at I I' f x f') :
  mdifferentiable_at I I' f x :=
âŸ¨h.1, âŸ¨f', h.2âŸ©âŸ©

@[simp, mfld_simps] lemma has_mfderiv_within_at_univ :
  has_mfderiv_within_at I I' f univ x f' â†” has_mfderiv_at I I' f x f' :=
by simp only [has_mfderiv_within_at, has_mfderiv_at, continuous_within_at_univ] with mfld_simps

theorem has_mfderiv_at_unique
  (hâ‚€ : has_mfderiv_at I I' f x fâ‚€') (hâ‚ : has_mfderiv_at I I' f x fâ‚') : fâ‚€' = fâ‚' :=
begin
  rw â† has_mfderiv_within_at_univ at hâ‚€ hâ‚,
  exact (unique_mdiff_within_at_univ I).eq hâ‚€ hâ‚
end

lemma has_mfderiv_within_at_inter' (h : t âˆˆ ğ“[s] x) :
  has_mfderiv_within_at I I' f (s âˆ© t) x f' â†” has_mfderiv_within_at I I' f s x f' :=
begin
  rw [has_mfderiv_within_at, has_mfderiv_within_at, ext_chart_preimage_inter_eq,
      has_fderiv_within_at_inter', continuous_within_at_inter' h],
  exact ext_chart_preimage_mem_nhds_within I x h,
end

lemma has_mfderiv_within_at_inter (h : t âˆˆ ğ“ x) :
  has_mfderiv_within_at I I' f (s âˆ© t) x f' â†” has_mfderiv_within_at I I' f s x f' :=
begin
  rw [has_mfderiv_within_at, has_mfderiv_within_at, ext_chart_preimage_inter_eq,
      has_fderiv_within_at_inter, continuous_within_at_inter h],
  exact ext_chart_preimage_mem_nhds I x h,
end

lemma has_mfderiv_within_at.union
  (hs : has_mfderiv_within_at I I' f s x f') (ht : has_mfderiv_within_at I I' f t x f') :
  has_mfderiv_within_at I I' f (s âˆª t) x f' :=
begin
  split,
  { exact continuous_within_at.union hs.1 ht.1 },
  { convert has_fderiv_within_at.union hs.2 ht.2,
    simp only [union_inter_distrib_right, preimage_union] }
end

lemma has_mfderiv_within_at.nhds_within (h : has_mfderiv_within_at I I' f s x f')
  (ht : s âˆˆ ğ“[t] x) : has_mfderiv_within_at I I' f t x f' :=
(has_mfderiv_within_at_inter' ht).1 (h.mono (inter_subset_right _ _))

lemma has_mfderiv_within_at.has_mfderiv_at (h : has_mfderiv_within_at I I' f s x f')
  (hs : s âˆˆ ğ“ x) :
  has_mfderiv_at I I' f x f' :=
by rwa [â† univ_inter s, has_mfderiv_within_at_inter hs, has_mfderiv_within_at_univ] at h

lemma mdifferentiable_within_at.has_mfderiv_within_at (h : mdifferentiable_within_at I I' f s x) :
  has_mfderiv_within_at I I' f s x (mfderiv_within I I' f s x) :=
begin
  refine âŸ¨h.1, _âŸ©,
  simp only [mfderiv_within, h, dif_pos] with mfld_simps,
  exact differentiable_within_at.has_fderiv_within_at h.2
end

lemma mdifferentiable_within_at.mfderiv_within (h : mdifferentiable_within_at I I' f s x) :
  (mfderiv_within I I' f s x) =
  fderiv_within ğ•œ (written_in_ext_chart_at I I' x f : _) ((ext_chart_at I x).symm â»Â¹' s âˆ© range I)
  ((ext_chart_at I x) x) :=
by simp only [mfderiv_within, h, dif_pos]

lemma mdifferentiable_at.has_mfderiv_at (h : mdifferentiable_at I I' f x) :
  has_mfderiv_at I I' f x (mfderiv I I' f x) :=
begin
  refine âŸ¨h.1, _âŸ©,
  simp only [mfderiv, h, dif_pos] with mfld_simps,
  exact differentiable_within_at.has_fderiv_within_at h.2
end

lemma mdifferentiable_at.mfderiv (h : mdifferentiable_at I I' f x) :
  (mfderiv I I' f x) =
  fderiv_within ğ•œ (written_in_ext_chart_at I I' x f : _) (range I) ((ext_chart_at I x) x) :=
by simp only [mfderiv, h, dif_pos]

lemma has_mfderiv_at.mfderiv (h : has_mfderiv_at I I' f x f') :
  mfderiv I I' f x = f' :=
(has_mfderiv_at_unique h h.mdifferentiable_at.has_mfderiv_at).symm

lemma has_mfderiv_within_at.mfderiv_within
  (h : has_mfderiv_within_at I I' f s x f') (hxs : unique_mdiff_within_at I s x) :
  mfderiv_within I I' f s x = f' :=
by { ext, rw hxs.eq h h.mdifferentiable_within_at.has_mfderiv_within_at }

lemma mdifferentiable.mfderiv_within
  (h : mdifferentiable_at I I' f x) (hxs : unique_mdiff_within_at I s x) :
  mfderiv_within I I' f s x = mfderiv I I' f x :=
begin
  apply has_mfderiv_within_at.mfderiv_within _ hxs,
  exact h.has_mfderiv_at.has_mfderiv_within_at
end

lemma mfderiv_within_subset (st : s âŠ† t) (hs : unique_mdiff_within_at I s x)
  (h : mdifferentiable_within_at I I' f t x) :
  mfderiv_within I I' f s x = mfderiv_within I I' f t x :=
((mdifferentiable_within_at.has_mfderiv_within_at h).mono st).mfderiv_within hs

omit Is I's

lemma mdifferentiable_within_at.mono (hst : s âŠ† t)
  (h : mdifferentiable_within_at I I' f t x) : mdifferentiable_within_at I I' f s x :=
âŸ¨ continuous_within_at.mono h.1 hst,
  differentiable_within_at.mono h.2 (inter_subset_inter (preimage_mono hst) (subset.refl _)) âŸ©

lemma mdifferentiable_within_at_univ :
  mdifferentiable_within_at I I' f univ x â†” mdifferentiable_at I I' f x :=
by simp only [mdifferentiable_within_at, mdifferentiable_at, continuous_within_at_univ]
  with mfld_simps

lemma mdifferentiable_within_at_inter (ht : t âˆˆ ğ“ x) :
  mdifferentiable_within_at I I' f (s âˆ© t) x â†” mdifferentiable_within_at I I' f s x :=
begin
  rw [mdifferentiable_within_at, mdifferentiable_within_at, ext_chart_preimage_inter_eq,
      differentiable_within_at_inter, continuous_within_at_inter ht],
  exact ext_chart_preimage_mem_nhds I x ht
end

lemma mdifferentiable_within_at_inter' (ht : t âˆˆ ğ“[s] x) :
  mdifferentiable_within_at I I' f (s âˆ© t) x â†” mdifferentiable_within_at I I' f s x :=
begin
  rw [mdifferentiable_within_at, mdifferentiable_within_at, ext_chart_preimage_inter_eq,
      differentiable_within_at_inter', continuous_within_at_inter' ht],
  exact ext_chart_preimage_mem_nhds_within I x ht
end

lemma mdifferentiable_at.mdifferentiable_within_at
  (h : mdifferentiable_at I I' f x) : mdifferentiable_within_at I I' f s x :=
mdifferentiable_within_at.mono (subset_univ _) (mdifferentiable_within_at_univ.2 h)

lemma mdifferentiable_within_at.mdifferentiable_at
  (h : mdifferentiable_within_at I I' f s x) (hs : s âˆˆ ğ“ x) : mdifferentiable_at I I' f x :=
begin
  have : s = univ âˆ© s, by rw univ_inter,
  rwa [this, mdifferentiable_within_at_inter hs, mdifferentiable_within_at_univ] at h,
end

lemma mdifferentiable_on.mono
  (h : mdifferentiable_on I I' f t) (st : s âŠ† t) : mdifferentiable_on I I' f s :=
Î»x hx, (h x (st hx)).mono st

lemma mdifferentiable_on_univ :
  mdifferentiable_on I I' f univ â†” mdifferentiable I I' f :=
by { simp only [mdifferentiable_on, mdifferentiable_within_at_univ] with mfld_simps, refl }

lemma mdifferentiable.mdifferentiable_on
  (h : mdifferentiable I I' f) : mdifferentiable_on I I' f s :=
(mdifferentiable_on_univ.2 h).mono (subset_univ _)

lemma mdifferentiable_on_of_locally_mdifferentiable_on
  (h : âˆ€xâˆˆs, âˆƒu, is_open u âˆ§ x âˆˆ u âˆ§ mdifferentiable_on I I' f (s âˆ© u)) :
  mdifferentiable_on I I' f s :=
begin
  assume x xs,
  rcases h x xs with âŸ¨t, t_open, xt, htâŸ©,
  exact (mdifferentiable_within_at_inter (is_open.mem_nhds t_open xt)).1 (ht x âŸ¨xs, xtâŸ©)
end

include Is I's
@[simp, mfld_simps] lemma mfderiv_within_univ : mfderiv_within I I' f univ = mfderiv I I' f :=
begin
  ext x : 1,
  simp only [mfderiv_within, mfderiv] with mfld_simps,
  rw mdifferentiable_within_at_univ
end

lemma mfderiv_within_inter (ht : t âˆˆ ğ“ x) (hs : unique_mdiff_within_at I s x) :
  mfderiv_within I I' f (s âˆ© t) x = mfderiv_within I I' f s x :=
by rw [mfderiv_within, mfderiv_within, ext_chart_preimage_inter_eq,
  mdifferentiable_within_at_inter ht, fderiv_within_inter (ext_chart_preimage_mem_nhds I x ht) hs]

omit Is I's

/-! ### Deriving continuity from differentiability on manifolds -/

theorem has_mfderiv_within_at.continuous_within_at
  (h : has_mfderiv_within_at I I' f s x f') : continuous_within_at f s x :=
h.1

theorem has_mfderiv_at.continuous_at (h : has_mfderiv_at I I' f x f') :
  continuous_at f x :=
h.1

lemma mdifferentiable_within_at.continuous_within_at (h : mdifferentiable_within_at I I' f s x) :
  continuous_within_at f s x :=
h.1

lemma mdifferentiable_at.continuous_at (h : mdifferentiable_at I I' f x) : continuous_at f x :=
h.1

lemma mdifferentiable_on.continuous_on (h : mdifferentiable_on I I' f s) : continuous_on f s :=
Î»x hx, (h x hx).continuous_within_at

lemma mdifferentiable.continuous (h : mdifferentiable I I' f) : continuous f :=
continuous_iff_continuous_at.2 $ Î»x, (h x).continuous_at

include Is I's
lemma tangent_map_within_subset {p : tangent_bundle I M}
  (st : s âŠ† t) (hs : unique_mdiff_within_at I s p.1) (h : mdifferentiable_within_at I I' f t p.1) :
  tangent_map_within I I' f s p = tangent_map_within I I' f t p :=
begin
  simp only [tangent_map_within] with mfld_simps,
  rw mfderiv_within_subset st hs h,
end

lemma tangent_map_within_univ :
  tangent_map_within I I' f univ = tangent_map I I' f :=
by { ext p : 1, simp only [tangent_map_within, tangent_map] with mfld_simps }

lemma tangent_map_within_eq_tangent_map {p : tangent_bundle I M}
  (hs : unique_mdiff_within_at I s p.1) (h : mdifferentiable_at I I' f p.1) :
  tangent_map_within I I' f s p = tangent_map I I' f p :=
begin
  rw â† mdifferentiable_within_at_univ at h,
  rw â† tangent_map_within_univ,
  exact tangent_map_within_subset (subset_univ _) hs h,
end

@[simp, mfld_simps] lemma tangent_map_within_tangent_bundle_proj {p : tangent_bundle I M} :
  tangent_bundle.proj I' M' (tangent_map_within I I' f s p) = f (tangent_bundle.proj I M p) := rfl

@[simp, mfld_simps] lemma tangent_map_within_proj {p : tangent_bundle I M} :
  (tangent_map_within I I' f s p).1 = f p.1 := rfl

@[simp, mfld_simps] lemma tangent_map_tangent_bundle_proj {p : tangent_bundle I M} :
  tangent_bundle.proj I' M' (tangent_map I I' f p) = f (tangent_bundle.proj I M p) := rfl

@[simp, mfld_simps] lemma tangent_map_proj {p : tangent_bundle I M} :
  (tangent_map I I' f p).1 = f p.1 := rfl

omit Is I's

/-! ### Congruence lemmas for derivatives on manifolds -/

lemma has_mfderiv_within_at.congr_of_eventually_eq (h : has_mfderiv_within_at I I' f s x f')
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : has_mfderiv_within_at I I' fâ‚ s x f' :=
begin
  refine âŸ¨continuous_within_at.congr_of_eventually_eq h.1 hâ‚ hx, _âŸ©,
  apply has_fderiv_within_at.congr_of_eventually_eq h.2,
  { have : (ext_chart_at I x).symm â»Â¹' {y | fâ‚ y = f y} âˆˆ
      ğ“[(ext_chart_at I x).symm â»Â¹' s âˆ© range I] ((ext_chart_at I x) x)  :=
      ext_chart_preimage_mem_nhds_within I x hâ‚,
    apply filter.mem_of_superset this (Î»y, _),
    simp only [hx] with mfld_simps {contextual := tt} },
  { simp only [hx] with mfld_simps },
end

lemma has_mfderiv_within_at.congr_mono (h : has_mfderiv_within_at I I' f s x f')
  (ht : âˆ€x âˆˆ t, fâ‚ x = f x) (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) :
  has_mfderiv_within_at I I' fâ‚ t x f' :=
(h.mono hâ‚).congr_of_eventually_eq (filter.mem_inf_of_right ht) hx

lemma has_mfderiv_at.congr_of_eventually_eq (h : has_mfderiv_at I I' f x f')
  (hâ‚ : fâ‚ =á¶ [ğ“ x] f) : has_mfderiv_at I I' fâ‚ x f' :=
begin
  rw â† has_mfderiv_within_at_univ at âŠ¢ h,
  apply h.congr_of_eventually_eq _ (mem_of_mem_nhds hâ‚ : _),
  rwa nhds_within_univ
end

include Is I's

lemma mdifferentiable_within_at.congr_of_eventually_eq
  (h : mdifferentiable_within_at I I' f s x) (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f)
  (hx : fâ‚ x = f x) : mdifferentiable_within_at I I' fâ‚ s x :=
(h.has_mfderiv_within_at.congr_of_eventually_eq hâ‚ hx).mdifferentiable_within_at

variables (I I')
lemma filter.eventually_eq.mdifferentiable_within_at_iff
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  mdifferentiable_within_at I I' f s x â†” mdifferentiable_within_at I I' fâ‚ s x :=
begin
  split,
  { assume h,
    apply h.congr_of_eventually_eq hâ‚ hx },
  { assume h,
    apply h.congr_of_eventually_eq _ hx.symm,
    apply hâ‚.mono,
    intro y,
    apply eq.symm }
end
variables {I I'}

lemma mdifferentiable_within_at.congr_mono (h : mdifferentiable_within_at I I' f s x)
  (ht : âˆ€x âˆˆ t, fâ‚ x = f x) (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) :
  mdifferentiable_within_at I I' fâ‚ t x :=
(has_mfderiv_within_at.congr_mono h.has_mfderiv_within_at ht hx hâ‚).mdifferentiable_within_at

lemma mdifferentiable_within_at.congr (h : mdifferentiable_within_at I I' f s x)
  (ht : âˆ€x âˆˆ s, fâ‚ x = f x) (hx : fâ‚ x = f x) : mdifferentiable_within_at I I' fâ‚ s x :=
(has_mfderiv_within_at.congr_mono h.has_mfderiv_within_at ht hx
  (subset.refl _)).mdifferentiable_within_at

lemma mdifferentiable_on.congr_mono (h : mdifferentiable_on I I' f s) (h' : âˆ€x âˆˆ t, fâ‚ x = f x)
  (hâ‚ : t âŠ† s) : mdifferentiable_on I I' fâ‚ t :=
Î» x hx, (h x (hâ‚ hx)).congr_mono h' (h' x hx) hâ‚

lemma mdifferentiable_at.congr_of_eventually_eq (h : mdifferentiable_at I I' f x)
  (hL : fâ‚ =á¶ [ğ“ x] f) : mdifferentiable_at I I' fâ‚ x :=
((h.has_mfderiv_at).congr_of_eventually_eq hL).mdifferentiable_at

lemma mdifferentiable_within_at.mfderiv_within_congr_mono (h : mdifferentiable_within_at I I' f s x)
  (hs : âˆ€x âˆˆ t, fâ‚ x = f x) (hx : fâ‚ x = f x) (hxt : unique_mdiff_within_at I t x) (hâ‚ : t âŠ† s) :
  mfderiv_within I I' fâ‚ t x = (mfderiv_within I I' f s x : _) :=
(has_mfderiv_within_at.congr_mono h.has_mfderiv_within_at hs hx hâ‚).mfderiv_within hxt

lemma filter.eventually_eq.mfderiv_within_eq (hs : unique_mdiff_within_at I s x)
  (hL : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  mfderiv_within I I' fâ‚ s x = (mfderiv_within I I' f s x : _) :=
begin
  by_cases h : mdifferentiable_within_at I I' f s x,
  { exact ((h.has_mfderiv_within_at).congr_of_eventually_eq hL hx).mfderiv_within hs },
  { unfold mfderiv_within,
    rw [dif_neg h, dif_neg],
    rwa â† hL.mdifferentiable_within_at_iff I I' hx }
end

lemma mfderiv_within_congr (hs : unique_mdiff_within_at I s x)
  (hL : âˆ€ x âˆˆ s, fâ‚ x = f x) (hx : fâ‚ x = f x) :
  mfderiv_within I I' fâ‚ s x = (mfderiv_within I I' f s x : _) :=
filter.eventually_eq.mfderiv_within_eq hs (filter.eventually_eq_of_mem (self_mem_nhds_within) hL) hx

lemma tangent_map_within_congr (h : âˆ€ x âˆˆ s, f x = fâ‚ x)
  (p : tangent_bundle I M) (hp : p.1 âˆˆ s) (hs : unique_mdiff_within_at I s p.1) :
  tangent_map_within I I' f s p = tangent_map_within I I' fâ‚ s p :=
begin
  simp only [tangent_map_within, h p.fst hp, true_and, eq_self_iff_true, heq_iff_eq,
    sigma.mk.inj_iff],
  congr' 1,
  exact mfderiv_within_congr hs h (h _ hp)
end

lemma filter.eventually_eq.mfderiv_eq (hL : fâ‚ =á¶ [ğ“ x] f) :
  mfderiv I I' fâ‚ x = (mfderiv I I' f x : _) :=
begin
  have A : fâ‚ x = f x := (mem_of_mem_nhds hL : _),
  rw [â† mfderiv_within_univ, â† mfderiv_within_univ],
  rw â† nhds_within_univ at hL,
  exact hL.mfderiv_within_eq (unique_mdiff_within_at_univ I) A
end

/-! ### Composition lemmas -/

omit Is I's

lemma written_in_ext_chart_comp (h : continuous_within_at f s x) :
  {y | written_in_ext_chart_at I I'' x (g âˆ˜ f) y
       = ((written_in_ext_chart_at I' I'' (f x) g) âˆ˜ (written_in_ext_chart_at I I' x f)) y}
  âˆˆ ğ“[(ext_chart_at I x).symm â»Â¹' s âˆ© range I] ((ext_chart_at I x) x)  :=
begin
  apply @filter.mem_of_superset _ _
    ((f âˆ˜ (ext_chart_at I x).symm)â»Â¹' (ext_chart_at I' (f x)).source) _
    (ext_chart_preimage_mem_nhds_within I x
      (h.preimage_mem_nhds_within (ext_chart_at_source_mem_nhds _ _))),
  mfld_set_tac,
end

variable (x)
include Is I's I''s

theorem has_mfderiv_within_at.comp
  (hg : has_mfderiv_within_at I' I'' g u (f x) g') (hf : has_mfderiv_within_at I I' f s x f')
  (hst : s âŠ† f â»Â¹' u) :
  has_mfderiv_within_at I I'' (g âˆ˜ f) s x (g'.comp f') :=
begin
  refine âŸ¨continuous_within_at.comp hg.1 hf.1 hst, _âŸ©,
  have A : has_fderiv_within_at ((written_in_ext_chart_at I' I'' (f x) g) âˆ˜
       (written_in_ext_chart_at I I' x f))
    (continuous_linear_map.comp g' f' : E â†’L[ğ•œ] E'')
    ((ext_chart_at I x).symm â»Â¹' s âˆ© range (I))
    ((ext_chart_at I x) x),
  { have : (ext_chart_at I x).symm â»Â¹' (f â»Â¹' (ext_chart_at I' (f x)).source)
    âˆˆ ğ“[(ext_chart_at I x).symm â»Â¹' s âˆ© range I] ((ext_chart_at I x) x)  :=
      (ext_chart_preimage_mem_nhds_within I x
        (hf.1.preimage_mem_nhds_within (ext_chart_at_source_mem_nhds _ _))),
    unfold has_mfderiv_within_at at *,
    rw [â† has_fderiv_within_at_inter' this, â† ext_chart_preimage_inter_eq] at hf âŠ¢,
    have : written_in_ext_chart_at I I' x f ((ext_chart_at I x) x)
        = (ext_chart_at I' (f x)) (f x),
      by simp only with mfld_simps,
    rw â† this at hg,
    apply has_fderiv_within_at.comp ((ext_chart_at I x) x) hg.2 hf.2 _,
    assume y hy,
    simp only with mfld_simps at hy,
    have : f (((chart_at H x).symm : H â†’ M) (I.symm y)) âˆˆ u := hst hy.1.1,
    simp only [hy, this] with mfld_simps },
  apply A.congr_of_eventually_eq (written_in_ext_chart_comp hf.1),
  simp only with mfld_simps
end

/-- The chain rule. -/
theorem has_mfderiv_at.comp
  (hg : has_mfderiv_at I' I'' g (f x) g') (hf : has_mfderiv_at I I' f x f') :
  has_mfderiv_at I I'' (g âˆ˜ f) x (g'.comp f') :=
begin
  rw â† has_mfderiv_within_at_univ at *,
  exact has_mfderiv_within_at.comp x (hg.mono (subset_univ _)) hf subset_preimage_univ
end

theorem has_mfderiv_at.comp_has_mfderiv_within_at
  (hg : has_mfderiv_at I' I'' g (f x) g') (hf : has_mfderiv_within_at I I' f s x f') :
  has_mfderiv_within_at I I'' (g âˆ˜ f) s x (g'.comp f') :=
begin
  rw â† has_mfderiv_within_at_univ at *,
  exact has_mfderiv_within_at.comp x (hg.mono (subset_univ _)) hf subset_preimage_univ
end

lemma mdifferentiable_within_at.comp
  (hg : mdifferentiable_within_at I' I'' g u (f x)) (hf : mdifferentiable_within_at I I' f s x)
  (h : s âŠ† f â»Â¹' u) : mdifferentiable_within_at I I'' (g âˆ˜ f) s x :=
begin
  rcases hf.2 with âŸ¨f', hf'âŸ©,
  have F : has_mfderiv_within_at I I' f s x f' := âŸ¨hf.1, hf'âŸ©,
  rcases hg.2 with âŸ¨g', hg'âŸ©,
  have G : has_mfderiv_within_at I' I'' g u (f x) g' := âŸ¨hg.1, hg'âŸ©,
  exact (has_mfderiv_within_at.comp x G F h).mdifferentiable_within_at
end

lemma mdifferentiable_at.comp
  (hg : mdifferentiable_at I' I'' g (f x)) (hf : mdifferentiable_at I I' f x) :
  mdifferentiable_at I I'' (g âˆ˜ f) x :=
(hg.has_mfderiv_at.comp x hf.has_mfderiv_at).mdifferentiable_at

lemma mfderiv_within_comp
  (hg : mdifferentiable_within_at I' I'' g u (f x)) (hf : mdifferentiable_within_at I I' f s x)
  (h : s âŠ† f â»Â¹' u) (hxs : unique_mdiff_within_at I s x) :
  mfderiv_within I I'' (g âˆ˜ f) s x =
    (mfderiv_within I' I'' g u (f x)).comp (mfderiv_within I I' f s x) :=
begin
  apply has_mfderiv_within_at.mfderiv_within _ hxs,
  exact has_mfderiv_within_at.comp x hg.has_mfderiv_within_at hf.has_mfderiv_within_at h
end

lemma mfderiv_comp
  (hg : mdifferentiable_at I' I'' g (f x)) (hf : mdifferentiable_at I I' f x) :
  mfderiv I I'' (g âˆ˜ f) x = (mfderiv I' I'' g (f x)).comp (mfderiv I I' f x) :=
begin
  apply has_mfderiv_at.mfderiv,
  exact has_mfderiv_at.comp x hg.has_mfderiv_at hf.has_mfderiv_at
end

lemma mdifferentiable_on.comp
  (hg : mdifferentiable_on I' I'' g u) (hf : mdifferentiable_on I I' f s) (st : s âŠ† f â»Â¹' u) :
  mdifferentiable_on I I'' (g âˆ˜ f) s :=
Î»x hx, mdifferentiable_within_at.comp x (hg (f x) (st hx)) (hf x hx) st

lemma mdifferentiable.comp
  (hg : mdifferentiable I' I'' g) (hf : mdifferentiable I I' f) : mdifferentiable I I'' (g âˆ˜ f) :=
Î»x, mdifferentiable_at.comp x (hg (f x)) (hf x)

lemma tangent_map_within_comp_at (p : tangent_bundle I M)
  (hg : mdifferentiable_within_at I' I'' g u (f p.1)) (hf : mdifferentiable_within_at I I' f s p.1)
  (h : s âŠ† f â»Â¹' u)  (hps : unique_mdiff_within_at I s p.1) :
  tangent_map_within I I'' (g âˆ˜ f) s p =
  tangent_map_within I' I'' g u (tangent_map_within I I' f s p) :=
begin
  simp only [tangent_map_within] with mfld_simps,
  rw mfderiv_within_comp p.1 hg hf h hps,
  refl
end

lemma tangent_map_comp_at (p : tangent_bundle I M)
  (hg : mdifferentiable_at I' I'' g (f p.1)) (hf : mdifferentiable_at I I' f p.1) :
  tangent_map I I'' (g âˆ˜ f) p = tangent_map I' I'' g (tangent_map I I' f p) :=
begin
  simp only [tangent_map] with mfld_simps,
  rw mfderiv_comp p.1 hg hf,
  refl
end

lemma tangent_map_comp (hg : mdifferentiable I' I'' g) (hf : mdifferentiable I I' f) :
  tangent_map I I'' (g âˆ˜ f) = (tangent_map I' I'' g) âˆ˜ (tangent_map I I' f) :=
by { ext p : 1, exact tangent_map_comp_at _ (hg _) (hf _) }

end derivatives_properties

section mfderiv_fderiv

/-!
### Relations between vector space derivative and manifold derivative

The manifold derivative `mfderiv`, when considered on the model vector space with its trivial
manifold structure, coincides with the usual Frechet derivative `fderiv`. In this section, we prove
this and related statements.
-/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{f : E â†’ E'} {s : set E} {x : E}

lemma unique_mdiff_within_at_iff_unique_diff_within_at :
  unique_mdiff_within_at (ğ“˜(ğ•œ, E)) s x â†” unique_diff_within_at ğ•œ s x :=
by simp only [unique_mdiff_within_at] with mfld_simps

alias unique_mdiff_within_at_iff_unique_diff_within_at â†”
  unique_mdiff_within_at.unique_diff_within_at unique_diff_within_at.unique_mdiff_within_at

lemma unique_mdiff_on_iff_unique_diff_on :
  unique_mdiff_on (ğ“˜(ğ•œ, E)) s â†” unique_diff_on ğ•œ s :=
by simp [unique_mdiff_on, unique_diff_on, unique_mdiff_within_at_iff_unique_diff_within_at]

alias unique_mdiff_on_iff_unique_diff_on â†”
  unique_mdiff_on.unique_diff_on unique_diff_on.unique_mdiff_on

@[simp, mfld_simps] lemma written_in_ext_chart_model_space :
  written_in_ext_chart_at (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) x f = f :=
rfl

lemma has_mfderiv_within_at_iff_has_fderiv_within_at {f'} :
  has_mfderiv_within_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x f' â†”
    has_fderiv_within_at f f' s x :=
by simpa only [has_mfderiv_within_at, and_iff_right_iff_imp] with mfld_simps
  using has_fderiv_within_at.continuous_within_at

alias has_mfderiv_within_at_iff_has_fderiv_within_at â†”
  has_mfderiv_within_at.has_fderiv_within_at has_fderiv_within_at.has_mfderiv_within_at

lemma has_mfderiv_at_iff_has_fderiv_at {f'} :
  has_mfderiv_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x f' â†” has_fderiv_at f f' x :=
by rw [â† has_mfderiv_within_at_univ, has_mfderiv_within_at_iff_has_fderiv_within_at,
  has_fderiv_within_at_univ]

alias has_mfderiv_at_iff_has_fderiv_at â†” has_mfderiv_at.has_fderiv_at has_fderiv_at.has_mfderiv_at

/-- For maps between vector spaces, `mdifferentiable_within_at` and `fdifferentiable_within_at`
coincide -/
theorem mdifferentiable_within_at_iff_differentiable_within_at :
  mdifferentiable_within_at (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) f s x
  â†” differentiable_within_at ğ•œ f s x :=
begin
  simp only [mdifferentiable_within_at] with mfld_simps,
  exact âŸ¨Î»H, H.2, Î»H, âŸ¨H.continuous_within_at, HâŸ©âŸ©
end

alias mdifferentiable_within_at_iff_differentiable_within_at â†”
  mdifferentiable_within_at.differentiable_within_at
  differentiable_within_at.mdifferentiable_within_at

/-- For maps between vector spaces, `mdifferentiable_at` and `differentiable_at` coincide -/
theorem mdifferentiable_at_iff_differentiable_at :
  mdifferentiable_at (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) f x â†” differentiable_at ğ•œ f x :=
begin
  simp only [mdifferentiable_at, differentiable_within_at_univ] with mfld_simps,
  exact âŸ¨Î»H, H.2, Î»H, âŸ¨H.continuous_at, HâŸ©âŸ©
end

alias mdifferentiable_at_iff_differentiable_at â†”
  mdifferentiable_at.differentiable_at differentiable_at.mdifferentiable_at

/-- For maps between vector spaces, `mdifferentiable_on` and `differentiable_on` coincide -/
theorem mdifferentiable_on_iff_differentiable_on :
  mdifferentiable_on (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) f s â†” differentiable_on ğ•œ f s :=
by simp only [mdifferentiable_on, differentiable_on,
              mdifferentiable_within_at_iff_differentiable_within_at]

alias mdifferentiable_on_iff_differentiable_on â†”
  mdifferentiable_on.differentiable_on differentiable_on.mdifferentiable_on

/-- For maps between vector spaces, `mdifferentiable` and `differentiable` coincide -/
theorem mdifferentiable_iff_differentiable :
  mdifferentiable (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) f â†” differentiable ğ•œ f :=
by simp only [mdifferentiable, differentiable, mdifferentiable_at_iff_differentiable_at]

alias mdifferentiable_iff_differentiable â†”
  mdifferentiable.differentiable differentiable.mdifferentiable

/-- For maps between vector spaces, `mfderiv_within` and `fderiv_within` coincide -/
@[simp] theorem mfderiv_within_eq_fderiv_within :
  mfderiv_within (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) f s x = fderiv_within ğ•œ f s x :=
begin
  by_cases h : mdifferentiable_within_at (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) f s x,
  { simp only [mfderiv_within, h, dif_pos] with mfld_simps },
  { simp only [mfderiv_within, h, dif_neg, not_false_iff],
    rw [mdifferentiable_within_at_iff_differentiable_within_at] at h,
    exact (fderiv_within_zero_of_not_differentiable_within_at h).symm }
end

/-- For maps between vector spaces, `mfderiv` and `fderiv` coincide -/
@[simp] theorem mfderiv_eq_fderiv :
  mfderiv (ğ“˜(ğ•œ, E)) (ğ“˜(ğ•œ, E')) f x = fderiv ğ•œ f x :=
begin
  rw [â† mfderiv_within_univ, â† fderiv_within_univ],
  exact mfderiv_within_eq_fderiv_within
end

end mfderiv_fderiv

section specific_functions

/-! ### Differentiability of specific functions -/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
{M : Type*} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] (I' : model_with_corners ğ•œ E' H')
{M' : Type*} [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']

namespace continuous_linear_map

variables (f : E â†’L[ğ•œ] E') {s : set E} {x : E}

protected lemma has_mfderiv_within_at : has_mfderiv_within_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x f :=
f.has_fderiv_within_at.has_mfderiv_within_at

protected lemma has_mfderiv_at : has_mfderiv_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x f :=
f.has_fderiv_at.has_mfderiv_at

protected lemma mdifferentiable_within_at : mdifferentiable_within_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x :=
f.differentiable_within_at.mdifferentiable_within_at

protected lemma mdifferentiable_on : mdifferentiable_on ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s :=
f.differentiable_on.mdifferentiable_on

protected lemma mdifferentiable_at : mdifferentiable_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x :=
f.differentiable_at.mdifferentiable_at

protected lemma mdifferentiable : mdifferentiable ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f :=
f.differentiable.mdifferentiable

lemma mfderiv_eq : mfderiv ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x = f :=
f.has_mfderiv_at.mfderiv

lemma mfderiv_within_eq (hs : unique_mdiff_within_at ğ“˜(ğ•œ, E) s x)  :
  mfderiv_within ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x = f :=
f.has_mfderiv_within_at.mfderiv_within hs

end continuous_linear_map

namespace continuous_linear_equiv

variables (f : E â‰ƒL[ğ•œ] E') {s : set E} {x : E}

protected lemma has_mfderiv_within_at :
  has_mfderiv_within_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x (f : E â†’L[ğ•œ] E') :=
f.has_fderiv_within_at.has_mfderiv_within_at

protected lemma has_mfderiv_at : has_mfderiv_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x (f : E â†’L[ğ•œ] E') :=
f.has_fderiv_at.has_mfderiv_at

protected lemma mdifferentiable_within_at : mdifferentiable_within_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x :=
f.differentiable_within_at.mdifferentiable_within_at

protected lemma mdifferentiable_on : mdifferentiable_on ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s :=
f.differentiable_on.mdifferentiable_on

protected lemma mdifferentiable_at : mdifferentiable_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x :=
f.differentiable_at.mdifferentiable_at

protected lemma mdifferentiable : mdifferentiable ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f :=
f.differentiable.mdifferentiable

lemma mfderiv_eq : mfderiv ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f x = (f : E â†’L[ğ•œ] E') :=
f.has_mfderiv_at.mfderiv

lemma mfderiv_within_eq (hs : unique_mdiff_within_at ğ“˜(ğ•œ, E) s x)  :
  mfderiv_within ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') f s x = (f : E â†’L[ğ•œ] E') :=
f.has_mfderiv_within_at.mfderiv_within hs

end continuous_linear_equiv

variables {s : set M} {x : M}

section id
/-! #### Identity -/

lemma has_mfderiv_at_id (x : M) :
  has_mfderiv_at I I (@_root_.id M) x (continuous_linear_map.id ğ•œ (tangent_space I x)) :=
begin
  refine âŸ¨continuous_id.continuous_at, _âŸ©,
  have : âˆ€á¶  y in ğ“[range I] ((ext_chart_at I x) x),
    ((ext_chart_at I x) âˆ˜ (ext_chart_at I x).symm) y = id y,
  { apply filter.mem_of_superset (ext_chart_at_target_mem_nhds_within I x),
    mfld_set_tac },
  apply has_fderiv_within_at.congr_of_eventually_eq (has_fderiv_within_at_id _ _) this,
  simp only with mfld_simps
end

theorem has_mfderiv_within_at_id (s : set M) (x : M) :
  has_mfderiv_within_at I I (@_root_.id M) s x (continuous_linear_map.id ğ•œ (tangent_space I x)) :=
(has_mfderiv_at_id I x).has_mfderiv_within_at

lemma mdifferentiable_at_id : mdifferentiable_at I I (@_root_.id M) x :=
(has_mfderiv_at_id I x).mdifferentiable_at

lemma mdifferentiable_within_at_id : mdifferentiable_within_at I I (@_root_.id M) s x :=
(mdifferentiable_at_id I).mdifferentiable_within_at

lemma mdifferentiable_id : mdifferentiable I I (@_root_.id M) :=
Î»x, mdifferentiable_at_id I

lemma mdifferentiable_on_id : mdifferentiable_on I I (@_root_.id M) s :=
(mdifferentiable_id I).mdifferentiable_on

@[simp, mfld_simps] lemma mfderiv_id :
  mfderiv I I (@_root_.id M) x = (continuous_linear_map.id ğ•œ (tangent_space I x)) :=
has_mfderiv_at.mfderiv (has_mfderiv_at_id I x)

lemma mfderiv_within_id (hxs : unique_mdiff_within_at I s x) :
  mfderiv_within I I (@_root_.id M) s x = (continuous_linear_map.id ğ•œ (tangent_space I x)) :=
begin
  rw mdifferentiable.mfderiv_within (mdifferentiable_at_id I) hxs,
  exact mfderiv_id I
end

@[simp, mfld_simps] lemma tangent_map_id : tangent_map I I (id : M â†’ M) = id :=
by { ext1 âŸ¨x, vâŸ©, simp [tangent_map] }

lemma tangent_map_within_id {p : tangent_bundle I M}
  (hs : unique_mdiff_within_at I s (tangent_bundle.proj I M p)) :
  tangent_map_within I I (id : M â†’ M) s p = p :=
begin
  simp only [tangent_map_within, id.def],
  rw mfderiv_within_id,
  { rcases p, refl },
  { exact hs }
end

end id

section const
/-! #### Constants -/

variables {c : M'}

lemma has_mfderiv_at_const (c : M') (x : M) :
  has_mfderiv_at I I' (Î»y : M, c) x
  (0 : tangent_space I x â†’L[ğ•œ] tangent_space I' c) :=
begin
  refine âŸ¨continuous_const.continuous_at, _âŸ©,
  simp only [written_in_ext_chart_at, (âˆ˜), has_fderiv_within_at_const]
end

theorem has_mfderiv_within_at_const (c : M') (s : set M) (x : M) :
  has_mfderiv_within_at I I' (Î»y : M, c) s x
  (0 : tangent_space I x â†’L[ğ•œ] tangent_space I' c) :=
(has_mfderiv_at_const I I' c x).has_mfderiv_within_at

lemma mdifferentiable_at_const : mdifferentiable_at I I' (Î»y : M, c) x :=
(has_mfderiv_at_const I I' c x).mdifferentiable_at

lemma mdifferentiable_within_at_const : mdifferentiable_within_at I I' (Î»y : M, c) s x :=
(mdifferentiable_at_const I I').mdifferentiable_within_at

lemma mdifferentiable_const : mdifferentiable I I' (Î»y : M, c) :=
Î»x, mdifferentiable_at_const I I'

lemma mdifferentiable_on_const : mdifferentiable_on I I' (Î»y : M, c) s :=
(mdifferentiable_const I I').mdifferentiable_on

@[simp, mfld_simps] lemma mfderiv_const : mfderiv I I' (Î»y : M, c) x =
  (0 : tangent_space I x â†’L[ğ•œ] tangent_space I' c) :=
has_mfderiv_at.mfderiv (has_mfderiv_at_const I I' c x)

lemma mfderiv_within_const (hxs : unique_mdiff_within_at I s x) :
  mfderiv_within I I' (Î»y : M, c) s x =
  (0 : tangent_space I x â†’L[ğ•œ] tangent_space I' c) :=
(has_mfderiv_within_at_const _ _ _ _ _).mfderiv_within hxs

end const

namespace model_with_corners
/-! #### Model with corners -/

protected lemma has_mfderiv_at {x} :
  has_mfderiv_at I ğ“˜(ğ•œ, E) I x (continuous_linear_map.id _ _) :=
âŸ¨I.continuous_at, (has_fderiv_within_at_id _ _).congr' I.right_inv_on (mem_range_self _)âŸ©

protected lemma has_mfderiv_within_at {s x} :
  has_mfderiv_within_at I ğ“˜(ğ•œ, E) I s x (continuous_linear_map.id _ _) :=
I.has_mfderiv_at.has_mfderiv_within_at

protected lemma mdifferentiable_within_at {s x} :
  mdifferentiable_within_at I ğ“˜(ğ•œ, E) I s x :=
I.has_mfderiv_within_at.mdifferentiable_within_at

protected lemma mdifferentiable_at {x} :
  mdifferentiable_at I ğ“˜(ğ•œ, E) I x :=
I.has_mfderiv_at.mdifferentiable_at

protected lemma mdifferentiable_on {s} :
  mdifferentiable_on I ğ“˜(ğ•œ, E) I s :=
Î» x hx, I.mdifferentiable_within_at

protected lemma mdifferentiable :
  mdifferentiable I (ğ“˜(ğ•œ, E)) I :=
Î» x, I.mdifferentiable_at

lemma has_mfderiv_within_at_symm {x} (hx : x âˆˆ range I) :
  has_mfderiv_within_at ğ“˜(ğ•œ, E) I I.symm (range I) x (continuous_linear_map.id _ _) :=
âŸ¨I.continuous_within_at_symm, (has_fderiv_within_at_id _ _).congr'
  (Î» y hy, I.right_inv_on hy.1) âŸ¨hx, mem_range_self _âŸ©âŸ©

lemma mdifferentiable_on_symm :
  mdifferentiable_on (ğ“˜(ğ•œ, E)) I I.symm (range I) :=
Î» x hx, (I.has_mfderiv_within_at_symm hx).mdifferentiable_within_at

end model_with_corners

section charts

variable {e : local_homeomorph M H}

lemma mdifferentiable_at_atlas (h : e âˆˆ atlas H M) {x : M} (hx : x âˆˆ e.source) :
  mdifferentiable_at I I e x :=
begin
  refine âŸ¨(e.continuous_on x hx).continuous_at (is_open.mem_nhds e.open_source hx), _âŸ©,
  have mem : I ((chart_at H x : M â†’ H) x) âˆˆ
    I.symm â»Â¹' ((chart_at H x).symm â‰«â‚• e).source âˆ© range I,
    by simp only [hx] with mfld_simps,
  have : (chart_at H x).symm.trans e âˆˆ times_cont_diff_groupoid âˆ I :=
    has_groupoid.compatible _ (chart_mem_atlas H x) h,
  have A : times_cont_diff_on ğ•œ âˆ
    (I âˆ˜ ((chart_at H x).symm.trans e) âˆ˜ I.symm)
    (I.symm â»Â¹' ((chart_at H x).symm.trans e).source âˆ© range I) :=
    this.1,
  have B := A.differentiable_on le_top (I ((chart_at H x : M â†’ H) x)) mem,
  simp only with mfld_simps at B,
  rw [inter_comm, differentiable_within_at_inter] at B,
  { simpa only with mfld_simps },
  { apply is_open.mem_nhds ((local_homeomorph.open_source _).preimage I.continuous_symm) mem.1 }
end

lemma mdifferentiable_on_atlas (h : e âˆˆ atlas H M) :
  mdifferentiable_on I I e e.source :=
Î»x hx, (mdifferentiable_at_atlas I h hx).mdifferentiable_within_at

lemma mdifferentiable_at_atlas_symm (h : e âˆˆ atlas H M) {x : H} (hx : x âˆˆ e.target) :
  mdifferentiable_at I I e.symm x :=
begin
  refine âŸ¨(e.continuous_on_symm x hx).continuous_at (is_open.mem_nhds e.open_target hx), _âŸ©,
  have mem : I x âˆˆ I.symm â»Â¹' (e.symm â‰«â‚• chart_at H (e.symm x)).source âˆ© range (I),
    by simp only [hx] with mfld_simps,
  have : e.symm.trans (chart_at H (e.symm x)) âˆˆ times_cont_diff_groupoid âˆ I :=
    has_groupoid.compatible _ h (chart_mem_atlas H _),
  have A : times_cont_diff_on ğ•œ âˆ
    (I âˆ˜ (e.symm.trans (chart_at H (e.symm x))) âˆ˜ I.symm)
    (I.symm â»Â¹' (e.symm.trans (chart_at H (e.symm x))).source âˆ© range I) :=
    this.1,
  have B := A.differentiable_on le_top (I x) mem,
  simp only with mfld_simps at B,
  rw [inter_comm, differentiable_within_at_inter] at B,
  { simpa only with mfld_simps },
  { apply (is_open.mem_nhds ((local_homeomorph.open_source _).preimage I.continuous_symm) mem.1) }
end

lemma mdifferentiable_on_atlas_symm (h : e âˆˆ atlas H M) :
  mdifferentiable_on I I e.symm e.target :=
Î»x hx, (mdifferentiable_at_atlas_symm I h hx).mdifferentiable_within_at

lemma mdifferentiable_of_mem_atlas (h : e âˆˆ atlas H M) : e.mdifferentiable I I :=
âŸ¨mdifferentiable_on_atlas I h, mdifferentiable_on_atlas_symm I hâŸ©

lemma mdifferentiable_chart (x : M) : (chart_at H x).mdifferentiable I I :=
mdifferentiable_of_mem_atlas _ (chart_mem_atlas _ _)

/-- The derivative of the chart at a base point is the chart of the tangent bundle, composed with
the identification between the tangent bundle of the model space and the product space. -/
lemma tangent_map_chart {p q : tangent_bundle I M} (h : q.1 âˆˆ (chart_at H p.1).source) :
  tangent_map I I (chart_at H p.1) q =
  (equiv.sigma_equiv_prod _ _).symm
    ((chart_at (model_prod H E) p : tangent_bundle I M â†’ model_prod H E) q) :=
begin
  dsimp [tangent_map],
  rw mdifferentiable_at.mfderiv,
  { refl },
  { exact mdifferentiable_at_atlas _ (chart_mem_atlas _ _) h }
end

/-- The derivative of the inverse of the chart at a base point is the inverse of the chart of the
tangent bundle, composed with the identification between the tangent bundle of the model space and
the product space. -/
lemma tangent_map_chart_symm {p : tangent_bundle I M} {q : tangent_bundle I H}
  (h : q.1 âˆˆ (chart_at H p.1).target) :
  tangent_map I I (chart_at H p.1).symm q =
  ((chart_at (model_prod H E) p).symm : model_prod H E â†’ tangent_bundle I M)
    ((equiv.sigma_equiv_prod H E) q) :=
begin
  dsimp only [tangent_map],
  rw mdifferentiable_at.mfderiv (mdifferentiable_at_atlas_symm _ (chart_mem_atlas _ _) h),
  -- a trivial instance is needed after the rewrite, handle it right now.
  rotate, { apply_instance },
  simp only [chart_at, basic_smooth_bundle_core.chart, subtype.coe_mk, tangent_bundle_core, h,
    basic_smooth_bundle_core.to_topological_fiber_bundle_core, equiv.sigma_equiv_prod_apply]
    with mfld_simps,
end

end charts

end specific_functions

/-! ### Differentiable local homeomorphisms -/
namespace local_homeomorph.mdifferentiable

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
{M : Type*} [topological_space M] [charted_space H M]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M']
{E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']
{e : local_homeomorph M M'} (he : e.mdifferentiable I I')
{e' : local_homeomorph M' M''}
include he

lemma symm : e.symm.mdifferentiable I' I :=
âŸ¨he.2, he.1âŸ©

protected lemma mdifferentiable_at {x : M} (hx : x âˆˆ e.source) :
  mdifferentiable_at I I' e x :=
(he.1 x hx).mdifferentiable_at (is_open.mem_nhds e.open_source hx)

lemma mdifferentiable_at_symm {x : M'} (hx : x âˆˆ e.target) :
  mdifferentiable_at I' I e.symm x :=
(he.2 x hx).mdifferentiable_at (is_open.mem_nhds e.open_target hx)

variables [smooth_manifold_with_corners I M] [smooth_manifold_with_corners I' M']
[smooth_manifold_with_corners I'' M'']

lemma symm_comp_deriv {x : M} (hx : x âˆˆ e.source) :
  (mfderiv I' I e.symm (e x)).comp (mfderiv I I' e x) =
    continuous_linear_map.id ğ•œ (tangent_space I x) :=
begin
  have : (mfderiv I I (e.symm âˆ˜ e) x) =
         (mfderiv I' I e.symm (e x)).comp (mfderiv I I' e x) :=
    mfderiv_comp x (he.mdifferentiable_at_symm (e.map_source hx)) (he.mdifferentiable_at hx),
  rw â† this,
  have : mfderiv I I (_root_.id : M â†’ M) x = continuous_linear_map.id _ _ := mfderiv_id I,
  rw â† this,
  apply filter.eventually_eq.mfderiv_eq,
  have : e.source âˆˆ ğ“ x := is_open.mem_nhds e.open_source hx,
  exact filter.mem_of_superset this (by mfld_set_tac)
end

lemma comp_symm_deriv {x : M'} (hx : x âˆˆ e.target) :
  (mfderiv I I' e (e.symm x)).comp (mfderiv I' I e.symm x) =
    continuous_linear_map.id ğ•œ (tangent_space I' x) :=
he.symm.symm_comp_deriv hx

/-- The derivative of a differentiable local homeomorphism, as a continuous linear equivalence
between the tangent spaces at `x` and `e x`. -/
protected def mfderiv {x : M} (hx : x âˆˆ e.source) :
  tangent_space I x â‰ƒL[ğ•œ] tangent_space I' (e x) :=
{ inv_fun := (mfderiv I' I e.symm (e x)),
  continuous_to_fun := (mfderiv I I' e x).cont,
  continuous_inv_fun := (mfderiv I' I e.symm (e x)).cont,
  left_inv := Î»y, begin
    have : (continuous_linear_map.id _ _ : tangent_space I x â†’L[ğ•œ] tangent_space I x) y = y := rfl,
    conv_rhs { rw [â† this, â† he.symm_comp_deriv hx] },
    refl
  end,
  right_inv := Î»y, begin
    have : (continuous_linear_map.id ğ•œ _ :
      tangent_space I' (e x) â†’L[ğ•œ] tangent_space I' (e x)) y = y := rfl,
    conv_rhs { rw [â† this, â† he.comp_symm_deriv (e.map_source hx)] },
    rw e.left_inv hx,
    refl
  end,
  .. mfderiv I I' e x }

lemma mfderiv_bijective {x : M} (hx : x âˆˆ e.source) :
  function.bijective (mfderiv I I' e x) :=
(he.mfderiv hx).bijective

lemma mfderiv_injective {x : M} (hx : x âˆˆ e.source) :
  function.injective (mfderiv I I' e x) :=
(he.mfderiv hx).injective

lemma mfderiv_surjective {x : M} (hx : x âˆˆ e.source) :
  function.surjective (mfderiv I I' e x) :=
(he.mfderiv hx).surjective

lemma ker_mfderiv_eq_bot {x : M} (hx : x âˆˆ e.source) :
  (mfderiv I I' e x).ker = âŠ¥ :=
(he.mfderiv hx).to_linear_equiv.ker

lemma range_mfderiv_eq_top {x : M} (hx : x âˆˆ e.source) :
  (mfderiv I I' e x).range = âŠ¤ :=
(he.mfderiv hx).to_linear_equiv.range

lemma range_mfderiv_eq_univ {x : M} (hx : x âˆˆ e.source) :
  range (mfderiv I I' e x) = univ :=
(he.mfderiv_surjective hx).range_eq

lemma trans (he': e'.mdifferentiable I' I'') : (e.trans e').mdifferentiable I I'' :=
begin
  split,
  { assume x hx,
    simp only with mfld_simps at hx,
    exact ((he'.mdifferentiable_at hx.2).comp _
           (he.mdifferentiable_at hx.1)).mdifferentiable_within_at },
  { assume x hx,
    simp only with mfld_simps at hx,
    exact ((he.symm.mdifferentiable_at hx.2).comp _
           (he'.symm.mdifferentiable_at hx.1)).mdifferentiable_within_at }
end

end local_homeomorph.mdifferentiable

/-! ### Differentiability of `ext_chart_at` -/

section ext_chart_at

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
{M : Type*} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
{s : set M} {x y : M}

lemma has_mfderiv_at_ext_chart_at (h : y âˆˆ (chart_at H x).source) :
  has_mfderiv_at I ğ“˜(ğ•œ, E) (ext_chart_at I x) y (mfderiv I I (chart_at H x) y : _) :=
I.has_mfderiv_at.comp y ((mdifferentiable_chart I x).mdifferentiable_at h).has_mfderiv_at

lemma has_mfderiv_within_at_ext_chart_at (h : y âˆˆ (chart_at H x).source) :
  has_mfderiv_within_at I ğ“˜(ğ•œ, E) (ext_chart_at I x) s y (mfderiv I I (chart_at H x) y : _) :=
(has_mfderiv_at_ext_chart_at I h).has_mfderiv_within_at

lemma mdifferentiable_at_ext_chart_at (h : y âˆˆ (chart_at H x).source) :
  mdifferentiable_at I ğ“˜(ğ•œ, E) (ext_chart_at I x) y :=
(has_mfderiv_at_ext_chart_at I h).mdifferentiable_at

lemma mdifferentiable_on_ext_chart_at :
  mdifferentiable_on I ğ“˜(ğ•œ, E) (ext_chart_at I x) (chart_at H x).source :=
Î» y hy, (has_mfderiv_within_at_ext_chart_at I hy).mdifferentiable_within_at

end ext_chart_at

/-! ### Unique derivative sets in manifolds -/
section unique_mdiff

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
{M : Type*} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
{M' : Type*} [topological_space M'] [charted_space H' M']
{s : set M}

/-- If a set has the unique differential property, then its image under a local
diffeomorphism also has the unique differential property. -/
lemma unique_mdiff_on.unique_mdiff_on_preimage [smooth_manifold_with_corners I' M']
  (hs : unique_mdiff_on I s) {e : local_homeomorph M M'} (he : e.mdifferentiable I I') :
  unique_mdiff_on I' (e.target âˆ© e.symm â»Â¹' s) :=
begin
  /- Start from a point `x` in the image, and let `z` be its preimage. Then the unique
  derivative property at `x` is expressed through `ext_chart_at I' x`, and the unique
  derivative property at `z` is expressed through `ext_chart_at I z`. We will argue that
  the composition of these two charts with `e` is a local diffeomorphism in vector spaces,
  and therefore preserves the unique differential property thanks to lemma
  `has_fderiv_within_at.unique_diff_within_at`, saying that a differentiable function with onto
  derivative preserves the unique derivative property.-/
  assume x hx,
  let z := e.symm x,
  have z_source : z âˆˆ e.source, by simp only [hx.1] with mfld_simps,
  have zx : e z = x, by simp only [z, hx.1] with mfld_simps,
  let F := ext_chart_at I z,
  -- the unique derivative property at `z` is expressed through its preferred chart,
  -- that we call `F`.
  have B : unique_diff_within_at ğ•œ
    (F.symm â»Â¹' (s âˆ© (e.source âˆ© e â»Â¹' ((ext_chart_at I' x).source))) âˆ© F.target) (F z),
  { have : unique_mdiff_within_at I s z := hs _ hx.2,
    have S : e.source âˆ© e â»Â¹' ((ext_chart_at I' x).source) âˆˆ ğ“ z,
    { apply is_open.mem_nhds,
      apply e.continuous_on.preimage_open_of_open e.open_source (ext_chart_at_open_source I' x),
      simp only [z_source, zx] with mfld_simps },
    have := this.inter S,
    rw [unique_mdiff_within_at_iff] at this,
    exact this },
  -- denote by `G` the change of coordinate, i.e., the composition of the two extended charts and
  -- of `e`
  let G := F.symm â‰« e.to_local_equiv â‰« (ext_chart_at I' x),
  -- `G` is differentiable
  have Diff : ((chart_at H z).symm â‰«â‚• e â‰«â‚• (chart_at H' x)).mdifferentiable I I',
  { have A := mdifferentiable_of_mem_atlas I (chart_mem_atlas H z),
    have B := mdifferentiable_of_mem_atlas I' (chart_mem_atlas H' x),
    exact A.symm.trans (he.trans B) },
  have Mmem : (chart_at H z : M â†’ H) z âˆˆ ((chart_at H z).symm â‰«â‚• e â‰«â‚• (chart_at H' x)).source,
    by simp only [z_source, zx] with mfld_simps,
  have A : differentiable_within_at ğ•œ G (range I) (F z),
  { refine (Diff.mdifferentiable_at Mmem).2.congr (Î»p hp, _) _;
    simp only [G, F] with mfld_simps },
  -- let `G'` be its derivative
  let G' := fderiv_within ğ•œ G (range I) (F z),
  have Dâ‚ : has_fderiv_within_at G G' (range I) (F z) :=
    A.has_fderiv_within_at,
  have Dâ‚‚ : has_fderiv_within_at G G'
      (F.symm â»Â¹' (s âˆ© (e.source âˆ© e â»Â¹' ((ext_chart_at I' x).source))) âˆ© F.target) (F z) :=
    Dâ‚.mono (by mfld_set_tac),
  -- The derivative `G'` is onto, as it is the derivative of a local diffeomorphism, the composition
  -- of the two charts and of `e`.
  have C : dense_range (G' : E â†’ E'),
  { have : G' = mfderiv I I' ((chart_at H z).symm â‰«â‚• e â‰«â‚• (chart_at H' x))
      ((chart_at H z : M â†’ H) z),
      by { rw (Diff.mdifferentiable_at Mmem).mfderiv, refl },
    rw this,
    exact (Diff.mfderiv_surjective Mmem).dense_range },
  -- key step: thanks to what we have proved about it, `G` preserves the unique derivative property
  have key : unique_diff_within_at ğ•œ
    (G '' (F.symm â»Â¹' (s âˆ© (e.source âˆ© e â»Â¹' ((ext_chart_at I' x).source))) âˆ© F.target))
    (G (F z)) := Dâ‚‚.unique_diff_within_at B C,
  have : G (F z) = (ext_chart_at I' x) x, by { dsimp [G, F], simp only [hx.1] with mfld_simps },
  rw this at key,
  apply key.mono,
  show G '' (F.symm â»Â¹' (s âˆ© (e.source âˆ© e â»Â¹' ((ext_chart_at I' x).source))) âˆ© F.target) âŠ†
    (ext_chart_at I' x).symm â»Â¹' e.target âˆ© (ext_chart_at I' x).symm â»Â¹' (e.symm â»Â¹' s) âˆ©
      range (I'),
  rw image_subset_iff,
  mfld_set_tac
end

/-- If a set in a manifold has the unique derivative property, then its pullback by any extended
chart, in the vector space, also has the unique derivative property. -/
lemma unique_mdiff_on.unique_diff_on_target_inter (hs : unique_mdiff_on I s) (x : M) :
  unique_diff_on ğ•œ ((ext_chart_at I x).target âˆ© ((ext_chart_at I x).symm â»Â¹' s)) :=
begin
  -- this is just a reformulation of `unique_mdiff_on.unique_mdiff_on_preimage`, using as `e`
  -- the local chart at `x`.
  assume z hz,
  simp only with mfld_simps at hz,
  have : (chart_at H x).mdifferentiable I I := mdifferentiable_chart _ _,
  have T := (hs.unique_mdiff_on_preimage this) (I.symm z),
  simp only [hz.left.left, hz.left.right, hz.right, unique_mdiff_within_at] with mfld_simps at âŠ¢ T,
  convert T using 1,
  rw @preimage_comp _ _ _ _ (chart_at H x).symm,
  mfld_set_tac
end

/-- When considering functions between manifolds, this statement shows up often. It entails
the unique differential of the pullback in extended charts of the set where the function can
be read in the charts. -/
lemma unique_mdiff_on.unique_diff_on_inter_preimage (hs : unique_mdiff_on I s) (x : M) (y : M')
  {f : M â†’ M'} (hf : continuous_on f s) :
  unique_diff_on ğ•œ ((ext_chart_at I x).target
    âˆ© ((ext_chart_at I x).symm â»Â¹' (s âˆ© fâ»Â¹' (ext_chart_at I' y).source))) :=
begin
  have : unique_mdiff_on I (s âˆ© f â»Â¹' (ext_chart_at I' y).source),
  { assume z hz,
    apply (hs z hz.1).inter',
    apply (hf z hz.1).preimage_mem_nhds_within,
    exact is_open.mem_nhds (ext_chart_at_open_source I' y) hz.2 },
  exact this.unique_diff_on_target_inter _
end

variables {F : Type*} [normed_group F] [normed_space ğ•œ F]
(Z : basic_smooth_bundle_core I M F)

/-- In a smooth fiber bundle constructed from core, the preimage under the projection of a set with
unique differential in the basis also has unique differential. -/
lemma unique_mdiff_on.smooth_bundle_preimage (hs : unique_mdiff_on I s) :
  unique_mdiff_on (I.prod (ğ“˜(ğ•œ, F))) (Z.to_topological_fiber_bundle_core.proj â»Â¹' s) :=
begin
  /- Using a chart (and the fact that unique differentiability is invariant under charts), we
  reduce the situation to the model space, where we can use the fact that products respect
  unique differentiability. -/
  assume p hp,
  replace hp : p.fst âˆˆ s, by simpa only with mfld_simps using hp,
  let eâ‚€ := chart_at H p.1,
  let e := chart_at (model_prod H F) p,
  -- It suffices to prove unique differentiability in a chart
  suffices h : unique_mdiff_on (I.prod (ğ“˜(ğ•œ, F)))
    (e.target âˆ© e.symmâ»Â¹' (Z.to_topological_fiber_bundle_core.proj â»Â¹' s)),
  { have A : unique_mdiff_on (I.prod (ğ“˜(ğ•œ, F))) (e.symm.target âˆ©
      e.symm.symm â»Â¹' (e.target âˆ© e.symmâ»Â¹' (Z.to_topological_fiber_bundle_core.proj â»Â¹' s))),
    { apply h.unique_mdiff_on_preimage,
      exact (mdifferentiable_of_mem_atlas _ (chart_mem_atlas _ _)).symm,
      apply_instance },
    have : p âˆˆ e.symm.target âˆ©
      e.symm.symm â»Â¹' (e.target âˆ© e.symmâ»Â¹' (Z.to_topological_fiber_bundle_core.proj â»Â¹' s)),
        by simp only [e, hp] with mfld_simps,
    apply (A _ this).mono,
    assume q hq,
    simp only [e, local_homeomorph.left_inv _ hq.1] with mfld_simps at hq,
    simp only [hq] with mfld_simps },
  -- rewrite the relevant set in the chart as a direct product
  have : (Î» (p : E Ã— F), (I.symm p.1, p.snd)) â»Â¹' e.target âˆ©
         (Î» (p : E Ã— F), (I.symm p.1, p.snd)) â»Â¹' (e.symm â»Â¹' (sigma.fst â»Â¹' s)) âˆ©
         ((range I).prod univ)
        = set.prod (I.symm â»Â¹' (eâ‚€.target âˆ© eâ‚€.symmâ»Â¹' s) âˆ© range I) univ,
    by mfld_set_tac,
  assume q hq,
  replace hq : q.1 âˆˆ (chart_at H p.1).target âˆ§ ((chart_at H p.1).symm : H â†’ M) q.1 âˆˆ s,
    by simpa only with mfld_simps using hq,
  simp only [unique_mdiff_within_at, model_with_corners.prod, preimage_inter, this] with mfld_simps,
  -- apply unique differentiability of products to conclude
  apply unique_diff_on.prod _ unique_diff_on_univ,
  { simp only [hq] with mfld_simps },
  { assume x hx,
    have A : unique_mdiff_on I (eâ‚€.target âˆ© eâ‚€.symmâ»Â¹' s),
    { apply hs.unique_mdiff_on_preimage,
      exact (mdifferentiable_of_mem_atlas _ (chart_mem_atlas _ _)),
      apply_instance },
    simp only [unique_mdiff_on, unique_mdiff_within_at, preimage_inter] with mfld_simps at A,
    have B := A (I.symm x) hx.1.1 hx.1.2,
    rwa [â† preimage_inter, model_with_corners.right_inv _ hx.2] at B }
end

lemma unique_mdiff_on.tangent_bundle_proj_preimage (hs : unique_mdiff_on I s):
  unique_mdiff_on I.tangent ((tangent_bundle.proj I M) â»Â¹' s) :=
hs.smooth_bundle_preimage _

end unique_mdiff
