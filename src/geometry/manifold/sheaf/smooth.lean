/-
Copyright ¬© 2023 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import geometry.manifold.sheaf.basic
import geometry.manifold.algebra.smooth_functions
import category_theory.sites.whiskering

/-! # The sheaf of smooth functions on a manifold -/

noncomputable theory
open_locale manifold
open topological_space opposite

universe u

variables {ùïú : Type*} [nontrivially_normed_field ùïú]
{EM : Type*} [normed_add_comm_group EM] [normed_space ùïú EM]
{HM : Type*} [topological_space HM] (IM : model_with_corners ùïú EM HM)
variables {E : Type*} [normed_add_comm_group E] [normed_space ùïú E]
{H : Type*} [topological_space H] (I : model_with_corners ùïú E H)
{H' : Type*} [topological_space H'] (I' : model_with_corners ùïú E H')
(M : Type u) [topological_space M] [charted_space HM M] [smooth_manifold_with_corners IM M]
(N G A A' R : Type u) [topological_space N] [charted_space H N]
[topological_space G] [charted_space H G] [topological_space A] [charted_space H A]
[topological_space A'] [charted_space H' A'] [topological_space R] [charted_space H R]

section type
variables [smooth_manifold_with_corners I N]

/-- The sheaf of smooth functions from `M` to `N`, as a sheaf of types. -/
def smooth_sheaf : Top.sheaf (Type u) (Top.of M) :=
(cont_diff_within_at_local_invariant_prop IM I ‚ä§).sheaf M N

instance smooth_sheaf.has_coe_to_fun (U : (opens (Top.of M))·µí·µñ) :
  has_coe_to_fun ((smooth_sheaf IM I M N).val.obj U) (Œª _, unop U ‚Üí N) :=
(cont_diff_within_at_local_invariant_prop IM I ‚ä§).sheaf_has_coe_to_fun _ _ _

/-- The object of `smooth_sheaf IM I M N` for the open set `U` in `M` is
`C^‚àû‚üÆIM, (unop U : opens M); I, N‚üØ`, the `(IM, I)`-smooth functions from `U` to `N`.  This is not
just a "moral" equality but a literal and definitional equality! -/
lemma smooth_sheaf.obj_eq (U : (opens (Top.of M))·µí·µñ) :
  (smooth_sheaf IM I M N).val.obj U = C^‚àû‚üÆIM, (unop U : opens M); I, N‚üØ := rfl

lemma smooth_sheaf.section_spec (U : (opens (Top.of M))·µí·µñ) (f : (smooth_sheaf IM I M N).val.obj U) :
  smooth IM I f :=
(cont_diff_within_at_local_invariant_prop IM I ‚ä§).section_spec _ _ _ _

variables {IM I M N}

lemma smooth_section {U : (opens (Top.of M))·µí·µñ} (f : (smooth_sheaf IM I M N).val.obj U) :
  smooth IM I f :=
(cont_diff_within_at_local_invariant_prop IM I ‚ä§).section_spec _ _ _ _

instance Top.of.smooth_manifold_with_corners : smooth_manifold_with_corners IM (Top.of M) :=
(infer_instance : smooth_manifold_with_corners IM M)

end type

section lie_group
variables [group G] [lie_group I G]

@[to_additive]
instance (U : (opens (Top.of M))·µí·µñ) : group ((smooth_sheaf IM I M G).val.obj U) := smooth_map.group

/-- The presheaf of smooth functions from `M` to `G`, for `G` a Lie group, as a presheaf
of groups. -/
@[to_additive smooth_presheaf_AddGroup]
def smooth_presheaf_Group : Top.presheaf Group.{u} (Top.of M) :=
{ obj := Œª U, Group.of ((smooth_sheaf IM I M G).val.obj U),
  map := Œª U V h, Group.of_hom $
    smooth_map.restrict_monoid_hom IM I G $ category_theory.le_of_hom h.unop,
  map_id' := begin
    intro U,
    ext ‚ü®_, _‚ü© ‚ü®_, _‚ü©,
    refl,
  end,
  map_comp' := Œª U V W f g, rfl }

/-- The sheaf of smooth functions from `M` to `G`, for `G` a Lie group, as a sheaf of
groups. -/
@[to_additive smooth_sheaf_AddGroup]
def smooth_sheaf_Group : Top.sheaf Group.{u} (Top.of M) :=
{ val := smooth_presheaf_Group IM I M G,
  cond := begin
    change category_theory.presheaf.is_sheaf _ _,
    rw category_theory.presheaf.is_sheaf_iff_is_sheaf_forget _ _ (category_theory.forget Group),
    { exact category_theory.Sheaf.cond (smooth_sheaf IM I M G) },
    { apply_instance },
  end }

end lie_group

section comm_lie_group
variables [comm_group A] [comm_group A'] [lie_group I A] [lie_group I' A']

@[to_additive]
instance (U : (opens (Top.of M))·µí·µñ) : comm_group ((smooth_sheaf IM I M A).val.obj U) :=
smooth_map.comm_group

/-- The presheaf of smooth functions from `M` to `A`, for `A` an abelian Lie group, as a
presheaf of abelian groups. -/
@[to_additive smooth_presheaf_AddCommGroup "The presheaf of smooth functions from
`M` to `A`, for `A` an abelian additive Lie group, as a presheaf of abelian additive groups."]
def smooth_presheaf_CommGroup : Top.presheaf CommGroup.{u} (Top.of M) :=
{ obj := Œª U, CommGroup.of ((smooth_sheaf IM I M A).val.obj U),
  map := Œª U V h, CommGroup.of_hom $
    smooth_map.restrict_monoid_hom IM I A $ category_theory.le_of_hom h.unop,
  map_id' := begin
    intro U,
    ext ‚ü®_, _‚ü© ‚ü®_, _‚ü©,
    refl,
  end,
  map_comp' := Œª U V W f g, rfl }

/-- The sheaf of smooth functions from `M` to `A`, for `A` an abelian Lie group, as a
sheaf of abelian groups. -/
@[to_additive smooth_sheaf_AddCommGroup "The sheaf of smooth functions from `M` to
`A`, for `A` an abelian additive Lie group, as a sheaf of abelian additive groups."]
def smooth_sheaf_CommGroup : Top.sheaf CommGroup.{u} (Top.of M) :=
{ val := smooth_presheaf_CommGroup IM I M A,
  cond := begin
    change category_theory.presheaf.is_sheaf _ _,
    rw category_theory.presheaf.is_sheaf_iff_is_sheaf_forget _ _ (category_theory.forget CommGroup),
    { exact category_theory.Sheaf.cond (smooth_sheaf IM I M A) },
    { apply_instance },
  end }

@[to_additive] def smooth_sheaf_CommGroup.comp_right (œÜ : A ‚Üí* A') (hœÜ : smooth I I' œÜ) :
  smooth_sheaf_CommGroup IM I M A ‚ü∂ smooth_sheaf_CommGroup IM I' M A' :=
category_theory.Sheaf.hom.mk $
{ app := Œª U, CommGroup.of_hom $ smooth_map.comp_right_monoid_hom _ _ œÜ hœÜ,
  naturality' := Œª U V f, rfl }

end comm_lie_group

section smooth_ring
variables [ring R] [smooth_ring I R]

instance (U : (opens (Top.of M))·µí·µñ) : ring ((smooth_sheaf IM I M R).val.obj U) := smooth_map.ring

/-- The presheaf of smooth functions from `M` to `R`, for `R` a smooth ring, as a presheaf
of rings. -/
def smooth_presheaf_Ring : Top.presheaf Ring.{u} (Top.of M) :=
{ obj := Œª U, Ring.of ((smooth_sheaf IM I M R).val.obj U),
  map := Œª U V h, Ring.of_hom $
    smooth_map.restrict_ring_hom IM I R $ category_theory.le_of_hom h.unop,
  map_id' := begin
    intro U,
    ext ‚ü®_, _‚ü© ‚ü®_, _‚ü©,
    refl,
  end,
  map_comp' := Œª U V W f g, rfl }

/-- The sheaf of smooth functions from `M` to `R`, for `R` a smooth ring, as a sheaf of
rings. -/
def smooth_sheaf_Ring : Top.sheaf Ring.{u} (Top.of M) :=
{ val := smooth_presheaf_Ring IM I M R,
  cond := begin
    change category_theory.presheaf.is_sheaf _ _,
    rw category_theory.presheaf.is_sheaf_iff_is_sheaf_forget _ _ (category_theory.forget Ring),
    { exact category_theory.Sheaf.cond (smooth_sheaf IM I M R) },
    { apply_instance },
  end }

-- sanity check: applying the `Ring`-to-`Type` forgetful functor to the sheaf-of-rings of smooth
-- functions gives the sheaf-of-types of smooth functions.
example : (category_theory.Sheaf_compose _ (category_theory.forget Ring)).obj
  (smooth_sheaf_Ring.{u} IM I M R) =
  (smooth_sheaf IM I M R) := rfl

end smooth_ring
