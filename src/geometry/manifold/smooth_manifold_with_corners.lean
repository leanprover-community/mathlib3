/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/

import analysis.calculus.times_cont_diff geometry.manifold.manifold linear_algebra.finite_dimensional

/-!
# Smooth manifolds (possibly with boundary or corners)

A smooth manifold is a manifold modelled on a normed vector space, or a subset like a
half-space (to get manifolds with boundaries) for which the change of coordinates are smooth maps.
We define the groupoid of smooth maps (with regularity n : with_top â„•), and then define smooth
manifolds as manifolds for which the changes of coordinates are C^âˆ.

## Main definitions

`model_with_corners ğ•œ E H` :
          a structure containing informations on the way a space H embeds in a
          model vector space E over the field ğ•œ. This is all that is needed to
          define a smooth manifold with model space H, and model vector space E.
`model_with_corners_self ğ•œ E` :
          trivial model with corners structure on the space E embedded in itself by the identity.
`times_cont_diff_groupoid n I` :
          when I is a model with corners on (ğ•œ, E, H), this is the groupoid of local homeos of H
          which are of class C^n over the normed field ğ•œ, when read in E.
`smooth_manifold_with_corners I M` :
          a type class saying that the manifold M, modelled on the space H, has C^âˆ changes of
          coordinates with respect to the model with corners I on (ğ•œ, E, H). This type class is just
          a shortcut for `has_groupoid M (times_cont_diff_groupoid âŠ¤ I)`

As a specific example of a model with corners, we define
`model_with_corners â„ (euclidean_space n) (euclidean_half_space n)` for the model space used
to define `n`-dimensional real manifolds with boundary.

To invoke an `n`-dimensional real manifold one could use

  `variables {n : â„•} {M : Type*} [topological_space M] [manifold (euclidean_space n)]
   [smooth_manifold_with_corners (model_with_corners_self â„ (euclidean_space n)) M]`.

However, this is not the recommended way: a theorem proved using this assumption would not apply
for instance to the tangent space of such a manifold, which is modelled on
`(euclidean_space n) Ã— (euclidean_space n)` and not on `euclidean_space (2 * n)`! In the same way,
it would not apply to product manifolds, modelled on `(euclidean_space n) Ã— (euclidean_space m)`.
The right invocation does not focus on one specific construction, but on all constructions sharing
the right properties, like

  `variables {E : Type*} [normed_group E] [normed_space â„ E] [finite_dimensional â„ E]
  {I : model_with_corners â„ E E} [I.boundaryless]
  {M : Type*} [topological_space M] [manifold E M] [smooth_manifold_with_corners I M]`

Here, `I.boundaryless` is a typeclass property ensuring that there is no boundary (this is for
instance the case for model_with_corners_self, or products of these). Note that one could consider
as a natural assumption to only use the trivial model with corners `model_with_corners_self â„ E`,
but again in product manifolds the natural model with corners will not be this one but the product
one (and they are not defeq as (Î»p : E Ã— F, (p.1, p.2)) is not defeq to the identity). So, it is
important to use the above incantation to maximize the applicability of theorems.

## Implementation notes

We want to talk about manifolds modelled on a vector space, but also on manifolds with
boundary, modelled on a half space (or even manifolds with corners). For the latter examples,
we still want to define smooth functions, tangent bundles, and so on. As smooth functions are
well defined on vector spaces or subsets of these, one could take for model space a subtype of a
vector space. With the drawback that the whole vector space itself (which is the most basic
example) is not directly a subtype of itself: the inclusion of `univ : set E` in `set E` would
show up in the definition, instead of `id`.

A good abstraction covering both cases it to have a vector
space E (with basic example the Euclidean space), a model space H (with basic example the upper half
space), and an embedding of H into E (which can be the identity for H = E, or
subtype.val for manifolds with corners). We say that the pair (E, H) with their embedding is a model
with corners, and we encompass all the relevant properties (in particular the fact that the image of
H in E should have unique differentials) in the definition of `model_with_corners`.

We concentrate on C^âˆ manifolds: all the definitions work equally well for C^n manifolds, but later
on it is a pain to carry all over the smoothness parameter, especially when one wants to deal with
C^k functions as there would be additional conditions k â‰¤ n everywhere. Since one deals almost all
the time with C^âˆ (or analytic) manifolds, this seems to be a reasonable choice that one could
revisit later if needed. C^k manifolds are still available, but they should be called using
`has_groupoid M (times_cont_diff_groupoid k I)` where `I` is the model with corners.

I have considered using the model with corners `I` as a typeclass argument, possibly out_param, to
get lighter notations later on, but it did not turn out right, as on `E Ã— F` there are two natural
model with corners, the trivial (identity) one, and the product one, and they are not defeq and one
needs to indicate to Lean which one we want to use.
This means that when talking on objects on manifolds one will most often need to specify the model
with corners one is using. For instance, the tangent bundle will be `tangent_bundle I M` and the
derivative will be `mfderiv I I' f`, instead of the more natural notations `tangent_bundle ğ•œ M` and
`mfderiv ğ•œ f` (the field has to be explicit anyway, as some manifolds could be considered both as
real and complex manifolds).
-/

noncomputable theory
local attribute [instance, priority 10] classical.decidable_inhabited classical.prop_decidable

universes u v w u' v' w'

open set

section model_with_corners

/-- A structure containing informations on the way a space H embeds in a
model vector space E over the field ğ•œ. This is all what is needed to
define a smooth manifold with model space H, and model vector space E.
We register it as a type class-/
structure model_with_corners (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  (E : Type*) [normed_group E] [normed_space ğ•œ E] (H : Type*) [topological_space H]
  extends local_equiv H E :=
(source_eq          : source = univ)
(unique_diff        : unique_diff_on ğ•œ (range to_fun))
(continuous_to_fun  : continuous to_fun)
(continuous_inv_fun : continuous inv_fun)

attribute [simp] model_with_corners.source_eq

/-- A vector space is a model with corners. -/
def model_with_corners_self (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  (E : Type*) [normed_group E] [normed_space ğ•œ E] : model_with_corners ğ•œ E E :=
{ to_fun     := id,
  inv_fun    := id,
  source     := univ,
  target     := univ,
  source_eq  := rfl,
  map_source := Î»_ _, mem_univ _,
  map_target := Î»_ _, mem_univ _,
  left_inv   := Î»_ _, rfl,
  right_inv  := Î»_ _, rfl,
  unique_diff := by { rw range_id, exact is_open_univ.unique_diff_on },
  continuous_to_fun  := continuous_id,
  continuous_inv_fun := continuous_id }

/-- In the trivial model with corners, the associated local equiv is the identity. -/
@[simp] lemma model_with_corners_self_local_equiv (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  (E : Type*) [normed_group E] [normed_space ğ•œ E] :
  (model_with_corners_self ğ•œ E).to_local_equiv = local_equiv.refl E := rfl

section
/- Basic properties of models with corners. -/
variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {H : Type*} [topological_space H]
  (I : model_with_corners ğ•œ E H)

@[simp] lemma model_with_corners_target : I.target = range I.to_fun :=
by rw [â† image_univ, â† local_equiv.image_source_eq_target, I.source_eq]

@[simp] lemma model_with_corners_left_inv (x : H) : I.inv_fun (I.to_fun x) = x :=
by simp [I.left_inv, I.source_eq]

@[simp] lemma model_with_corners_inv_fun_comp : I.inv_fun âˆ˜ I.to_fun = id :=
by { ext x, exact model_with_corners_left_inv _ _ }

@[simp] lemma model_with_corners_right_inv {x : E} (hx : x âˆˆ range I.to_fun) :
  I.to_fun (I.inv_fun x) = x :=
begin
  apply I.right_inv,
  simp [hx]
end

lemma model_with_corners.image (s : set H) :
  I.to_fun '' s = I.inv_fun â»Â¹' s âˆ© range I.to_fun :=
begin
  ext x,
  simp only [mem_image, mem_inter_eq, mem_range, mem_preimage],
  split,
  { rintros âŸ¨y, âŸ¨ys, hyâŸ©âŸ©,
    rw â† hy,
    simp [ys],
    exact âŸ¨y, rflâŸ© },
  { rintros âŸ¨xs, âŸ¨y, yxâŸ©âŸ©,
    rw â† yx at xs,
    simp at xs,
    exact âŸ¨y, âŸ¨xs, yxâŸ©âŸ© }
end

end

/-- Given two model_with_corners I on (E, H) and I' on (E', H'), we define the model with corners
I.prod I' on (E Ã— E', H Ã— H'). This appears in particular for the manifold structure on the tangent
bundle to a manifold modelled on (E, H): it will be modelled on (E Ã— E, H Ã— E). -/
def model_with_corners.prod
  {ğ•œ : Type u} [nondiscrete_normed_field ğ•œ]
  {E : Type v} [normed_group E] [normed_space ğ•œ E] {H : Type w} [topological_space H]
  (I : model_with_corners ğ•œ E H)
  {E' : Type v'} [normed_group E'] [normed_space ğ•œ E'] {H' : Type w'} [topological_space H']
  (I' : model_with_corners ğ•œ E' H') : model_with_corners ğ•œ (E Ã— E') (H Ã— H') :=
{ to_fun      := Î»p, (I.to_fun p.1, I'.to_fun p.2),
  inv_fun     := Î»p, (I.inv_fun p.1, I'.inv_fun p.2),
  source      := (univ : set (H Ã— H')),
  target      := set.prod (range I.to_fun) (range I'.to_fun),
  map_source  := Î» âŸ¨x, x'âŸ© _, by simp [-mem_range, mem_range_self],
  map_target  := Î» âŸ¨x, x'âŸ© _, mem_univ _,
  left_inv    := Î» âŸ¨x, x'âŸ© _, by simp,
  right_inv   := Î» âŸ¨x, x'âŸ© âŸ¨hx, hx'âŸ©, by rw [I.right_inv, I'.right_inv]; rwa model_with_corners_target,
  source_eq   := rfl,
  unique_diff := begin
    have : range (Î»(p : H Ã— H'), (I.to_fun p.1, I'.to_fun p.2)) = set.prod (range I.to_fun) (range I'.to_fun),
      by { rw â† prod_range_range_eq },
    rw this,
    exact unique_diff_on.prod I.unique_diff I'.unique_diff,
  end,
  continuous_to_fun := (continuous.comp I.continuous_to_fun continuous_fst).prod_mk
    (continuous.comp I'.continuous_to_fun continuous_snd),
  continuous_inv_fun := (continuous.comp I.continuous_inv_fun continuous_fst).prod_mk
    (continuous.comp I'.continuous_inv_fun continuous_snd) }

/-- Special case of product model with corners, which is trivial on the second factor. This shows up
as the model to tangent bundles. -/
@[reducible] def model_with_corners.tangent
  {ğ•œ : Type u} [nondiscrete_normed_field ğ•œ]
  {E : Type v} [normed_group E] [normed_space ğ•œ E] {H : Type w} [topological_space H]
  (I : model_with_corners ğ•œ E H) : model_with_corners ğ•œ (E Ã— E) (H Ã— E) :=
 I.prod (model_with_corners_self ğ•œ E)

section boundaryless

/-- Property ensuring that the model with corners I defines manifolds without boundary. -/
class model_with_corners.boundaryless {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {H : Type*} [topological_space H]
  (I : model_with_corners ğ•œ E H) : Prop :=
(range_eq_univ : range I.to_fun = univ)

/-- The trivial model with corners has no boundary -/
instance model_with_corners_self_range (ğ•œ : Type*) [nondiscrete_normed_field ğ•œ]
  (E : Type*) [normed_group E] [normed_space ğ•œ E] : (model_with_corners_self ğ•œ E).boundaryless :=
âŸ¨ by simp âŸ©

/-- If two model with corners are boundaryless, their product also is -/
instance model_with_corners.range_eq_univ_prod {ğ•œ : Type u} [nondiscrete_normed_field ğ•œ]
  {E : Type v} [normed_group E] [normed_space ğ•œ E] {H : Type w} [topological_space H]
  (I : model_with_corners ğ•œ E H) [I.boundaryless]
  {E' : Type v'} [normed_group E'] [normed_space ğ•œ E'] {H' : Type w'} [topological_space H']
  (I' : model_with_corners ğ•œ E' H') [I'.boundaryless] :
  (I.prod I').boundaryless :=
begin
  split,
  dsimp [model_with_corners.prod],
  rw [â† prod_range_range_eq, model_with_corners.boundaryless.range_eq_univ,
      model_with_corners.boundaryless.range_eq_univ, univ_prod_univ]
end

end boundaryless

section times_cont_diff_groupoid

/-- Given a model with corners (E, H), we define the groupoid of C^n transformations of H as the
maps that are C^n when read in E through I. -/
def times_cont_diff_groupoid (n : with_top â„•) {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {H : Type*} [topological_space H]
  (I : model_with_corners ğ•œ E H) :
  structure_groupoid H :=
groupoid_of_pregroupoid
{ property := Î»f s, times_cont_diff_on ğ•œ n (I.to_fun âˆ˜ f âˆ˜ I.inv_fun) (I.inv_fun â»Â¹' s âˆ© range I.to_fun),
  comp     := Î»f g u v hf hg huv, begin
    have A : unique_diff_on ğ•œ (I.inv_fun â»Â¹' (u âˆ© (f â»Â¹' v)) âˆ© range (I.to_fun)),
      by { rw inter_comm, exact I.unique_diff.inter (I.continuous_inv_fun _ huv) },
    have : I.to_fun âˆ˜ (g âˆ˜ f) âˆ˜ I.inv_fun = (I.to_fun âˆ˜ g âˆ˜ I.inv_fun) âˆ˜ (I.to_fun âˆ˜ f âˆ˜ I.inv_fun),
      by { ext x, simp },
    rw this,
    apply times_cont_diff_on.comp hg _ A,
    { change I.inv_fun â»Â¹' (u âˆ© (f â»Â¹' v)) âˆ© (range (I.to_fun)) âŠ†
        I.to_fun âˆ˜ f âˆ˜ I.inv_fun â»Â¹' ((I.inv_fun â»Â¹' v) âˆ© (range (I.to_fun))),
      rintros x âŸ¨hx1, hx2âŸ©,
      simp at âŠ¢ hx1,
      exact âŸ¨hx1.2, (f (I.inv_fun x)), rflâŸ© },
    { refine hf.mono _ A,
      rintros x âŸ¨hx1, hx2âŸ©,
      exact âŸ¨hx1.1, hx2âŸ© }
  end,
  id_mem   := begin
    have A : unique_diff_on ğ•œ ((I.inv_fun â»Â¹' univ) âˆ© (range I.to_fun)),
      by simp [I.unique_diff],
    apply times_cont_diff_on.congr (times_cont_diff_id.times_cont_diff_on A) A _,
    rintros x âŸ¨hx1, hx2âŸ©,
    rcases mem_range.1 hx2 with âŸ¨y, hyâŸ©,
    rw â† hy,
    simp,
  end,
  locality := Î»f u hu H, begin
    apply times_cont_diff_on_of_locally_times_cont_diff_on,
    show unique_diff_on ğ•œ ((I.inv_fun â»Â¹' u) âˆ© (range (I.to_fun))),
      by { rw inter_comm, exact I.unique_diff.inter (I.continuous_inv_fun _ hu) },
    rintros y âŸ¨hy1, hy2âŸ©,
    rcases mem_range.1 hy2 with âŸ¨x, hxâŸ©,
    rw â† hx at âŠ¢ hy1,
    simp at âŠ¢ hy1,
    rcases H x hy1 with âŸ¨v, v_open, xv, hvâŸ©,
    have : (set.inter (I.inv_fun â»Â¹' set.inter u v) (range (I.to_fun)))
      = (set.inter (I.inv_fun â»Â¹' u) (range (I.to_fun)) âˆ© I.inv_fun â»Â¹' v),
    { change ((I.inv_fun â»Â¹' (u âˆ© v)) âˆ© (range (I.to_fun)))
        = ((I.inv_fun â»Â¹' u) âˆ© (range (I.to_fun)) âˆ© I.inv_fun â»Â¹' v),
      rw [preimage_inter, inter_assoc, inter_assoc],
      congr' 1,
      rw inter_comm },
    rw this at hv,
    exact âŸ¨I.inv_fun â»Â¹' v, I.continuous_inv_fun _ v_open, by simpa, hvâŸ©
  end,
  congr    := Î»f g u hu fg hf, begin
    apply hf.congr,
    show unique_diff_on ğ•œ ((I.inv_fun â»Â¹' u) âˆ© (range (I.to_fun))),
      by { rw inter_comm, exact I.unique_diff.inter (I.continuous_inv_fun _ hu) },
    rintros y âŸ¨hy1, hy2âŸ©,
    rcases mem_range.1 hy2 with âŸ¨x, hxâŸ©,
    rw â† hx at âŠ¢ hy1,
    simp at âŠ¢ hy1,
    rw fg _ hy1
  end }

/-- Inclusion of the groupoid of C^n local diffeos in the groupoid of C^m local diffeos when m â‰¤ n -/
lemma times_cont_diff_groupoid_le {n m : with_top â„•} {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {H : Type*} [topological_space H]
  (I : model_with_corners ğ•œ E H) (h : m â‰¤ n) :
  times_cont_diff_groupoid n I â‰¤ times_cont_diff_groupoid m I :=
begin
  rw [times_cont_diff_groupoid, times_cont_diff_groupoid],
  apply groupoid_of_pregroupoid_le,
  assume f s hfs,
  exact times_cont_diff_on.of_le hfs h
end

/-- The groupoid of 0-times continuously differentiable maps is just the groupoid of all
local homeomorphisms -/
lemma times_cont_diff_groupoid_zero_eq {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {H : Type*} [topological_space H]
  (I : model_with_corners ğ•œ E H) :
  times_cont_diff_groupoid 0 I = continuous_groupoid H :=
begin
  apply le_antisymm lattice.le_top,
  assume u hu,
  change u âˆˆ times_cont_diff_groupoid 0 I,
  rw [times_cont_diff_groupoid, mem_groupoid_of_pregroupoid],
  simp only [times_cont_diff_on_zero],
  split,
  { apply continuous_on.comp (@continuous.continuous_on _ _ _ _ _ univ I.continuous_to_fun)
      _ (subset_univ _),
    apply continuous_on.comp u.continuous_to_fun I.continuous_inv_fun.continuous_on
      (inter_subset_left _ _) },
  { apply continuous_on.comp (@continuous.continuous_on _ _ _ _ _ univ I.continuous_to_fun)
      _ (subset_univ _),
    apply continuous_on.comp u.continuous_inv_fun I.continuous_inv_fun.continuous_on
      (inter_subset_left _ _) },
end

end times_cont_diff_groupoid

end model_with_corners

/- Typeclass defining smooth manifolds with corners with respect to a model with corners, over a
field ğ•œ and with infinite smoothness to simplify typeclass search and statements later on. -/
class smooth_manifold_with_corners {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
  (M : Type*) [topological_space M] [manifold H M] extends
  has_groupoid M (times_cont_diff_groupoid âŠ¤ I) : Prop

/-- For any model with corners, the model space is a smooth manifold -/
instance model_space_smooth {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] {H : Type*} [topological_space H]
  {I : model_with_corners ğ•œ E H} :
  smooth_manifold_with_corners I H := {}

/-- the space â„^n. Note that the name is slightly misleading, as we only need a normed space
structure on â„^n, but the one we use here is the sup norm and not the euclidean one -- this is not
a problem for the manifold applications, but should probably be refactored at some point. -/
def euclidean_space (n : â„•) : Type := (fin n â†’ â„)
/-- the half-space in â„^n, used to model manifolds with boundary. The definition is a little bit
convoluted as one can not require `0 â‰¤ x 0`: if n=0, then there is no 0 in `fin n`...-/
def euclidean_half_space (n : â„•) : Type := {x : euclidean_space n // âˆ€i:fin n, i.val = 0 â†’ 0 â‰¤ x i}

section
/- Register class instances for euclidean space and half-space -/
local attribute [reducible] euclidean_space euclidean_half_space
variable {n : â„•}

instance : vector_space â„ (euclidean_space n) := by apply_instance
instance : normed_group (euclidean_space n) := by apply_instance
instance : normed_space â„ (euclidean_space n) := by apply_instance
instance : topological_space (euclidean_half_space n) := by apply_instance

end

lemma range_half_space (n : â„•) :
  range (Î»x : euclidean_half_space n, x.val) = {y | âˆ€i:fin n, i.val = 0 â†’ 0 â‰¤ y i } :=
by simp [euclidean_half_space]

/-- Definition of the model with corners (euclidean_space n, euclidean_half_space n), used as a
model for manifolds with boundary -/
def model_with_corners_euclidean_half_space (n : â„•) :
  model_with_corners â„ (euclidean_space n) (euclidean_half_space n) :=
{ to_fun     := Î»x, x.val,
  inv_fun    := Î»x, âŸ¨Î»i, if h : i.val = 0 then max (x i) 0 else x i,
                    Î»i, by { by_cases h : i.val = 0; simp [h, le_refl] } âŸ©,
  source     := univ,
  target     := range (Î»x : euclidean_half_space n, x.val),
  map_source := Î»x hx, by simpa [-mem_range, mem_range_self] using x.property,
  map_target := Î»x hx, mem_univ _,
  left_inv   := Î»âŸ¨xval, xpropâŸ© hx, begin
    rw subtype.mk_eq_mk,
    ext1 i,
    by_cases hi : i.val = 0,
    simp [hi, xprop i hi],
    simp [hi],
  end,
  right_inv := Î»x hx, begin
    rw range_half_space at hx,
    ext1 i,
    by_cases hi : i.val = 0,
    { simp [hi, hx i hi] },
    { simp [hi] }
  end,
  source_eq   := rfl,
  unique_diff := begin
    /- To check that the half-space has the unique differentiability property, we use the criterion
    `unique_diff_on_convex`: it suffices to check that it is convex and with nonempty interior. -/
    rw range_half_space,
    apply unique_diff_on_convex,
    show convex {y : euclidean_space n | âˆ€ (i : fin n), i.val = 0 â†’ 0 â‰¤ y i},
    { assume x y a b hx hy ha hb hab i hi,
      simpa using add_le_add (mul_nonneg' ha (hx i hi)) (mul_nonneg' hb (hy i hi)) },
    show interior {y : euclidean_space n | âˆ€ (i : fin n), i.val = 0 â†’ 0 â‰¤ y i} â‰  âˆ…,
    { rw ne_empty_iff_exists_mem,
      use (Î»i, 1),
      rw mem_interior,
      refine âŸ¨(pi (univ : set (fin n)) (Î»i, (Ioi 0 : set â„))), _,
        is_open_set_pi finite_univ (Î»a ha, is_open_Ioi), _âŸ©,
      { assume x hx i hi,
        simp [pi] at hx,
        exact le_of_lt (hx i) },
      { simp [pi],
        assume i,
        exact zero_lt_one } }
  end,
  continuous_to_fun  := continuous_subtype_val,
  continuous_inv_fun := begin
    apply continuous_subtype_mk,
    apply continuous_pi,
    assume i,
    by_cases h : i.val = 0,
    { simp only [h, dif_pos],
      have : continuous (Î»x:â„, max x 0) := continuous_max continuous_id continuous_const,
      exact this.comp (continuous_apply i) },
    { simp [h],
      exact continuous_apply i }
  end }
