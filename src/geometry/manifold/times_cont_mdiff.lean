/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/

import geometry.manifold.mfderiv
import geometry.manifold.local_invariant_properties

/-!
# Smooth functions between smooth manifolds

We define `Câ¿` functions between smooth manifolds, as functions which are `Câ¿` in charts, and prove
basic properties of these notions.

## Main definitions and statements

Let `M ` and `M'` be two smooth manifolds, with respect to model with corners `I` and `I'`. Let
`f : M â†’ M'`.

* `times_cont_mdiff_within_at I I' n f s x` states that the function `f` is `Câ¿` within the set `s`
  around the point `x`.
* `times_cont_mdiff_at I I' n f x` states that the function `f` is `Câ¿` around `x`.
* `times_cont_mdiff_on I I' n f s` states that the function `f` is `Câ¿` on the set `s`
* `times_cont_mdiff I I' n f` states that the function `f` is `Câ¿`.
* `times_cont_mdiff_on.comp` gives the invariance of the `Câ¿` property under composition
* `times_cont_mdiff_on.times_cont_mdiff_on_tangent_map_within` states that the bundled derivative
  of a `Câ¿` function in a domain is `Cáµ` when `m + 1 â‰¤ n`.
* `times_cont_mdiff.times_cont_mdiff_tangent_map` states that the bundled derivative
  of a `Câ¿` function is `Cáµ` when `m + 1 â‰¤ n`.
* `times_cont_mdiff_iff_times_cont_diff` states that, for functions between vector spaces,
  manifold-smoothness is equivalent to usual smoothness.

We also give many basic properties of smooth functions between manifolds, following the API of
smooth functions between vector spaces.

## Implementation details

Many properties follow for free from the corresponding properties of functions in vector spaces,
as being `Câ¿` is a local property invariant under the smooth groupoid. We take advantage of the
general machinery developed in `local_invariant_properties.lean` to get these properties
automatically. For instance, the fact that being `Câ¿` does not depend on the chart one considers
is given by `lift_prop_within_at_indep_chart`.

For this to work, the definition of `times_cont_mdiff_within_at` and friends has to
follow definitionally the setup of local invariant properties. Still, we recast the definition
in terms of extended charts in `times_cont_mdiff_on_iff` and `times_cont_mdiff_iff`.
-/

open set function filter charted_space smooth_manifold_with_corners
open_locale topological_space manifold

/-! ### Definition of smooth functions between manifolds -/

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
-- declare a smooth manifold `M` over the pair `(E, H)`.
{E : Type*} [normed_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
{M : Type*} [topological_space M] [charted_space H M] [Is : smooth_manifold_with_corners I M]
-- declare a smooth manifold `M'` over the pair `(E', H')`.
{E' : Type*} [normed_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] (I' : model_with_corners ğ•œ E' H')
{M' : Type*} [topological_space M'] [charted_space H' M'] [I's : smooth_manifold_with_corners I' M']
-- declare a smooth manifold `N` over the pair `(F, G)`.
{F : Type*} [normed_group F] [normed_space ğ•œ F]
{G : Type*} [topological_space G] {J : model_with_corners ğ•œ F G}
{N : Type*} [topological_space N] [charted_space G N] [Js : smooth_manifold_with_corners J N]
-- declare a smooth manifold `N'` over the pair `(F', G')`.
{F' : Type*} [normed_group F'] [normed_space ğ•œ F']
{G' : Type*} [topological_space G'] {J' : model_with_corners ğ•œ F' G'}
{N' : Type*} [topological_space N'] [charted_space G' N'] [J's : smooth_manifold_with_corners J' N']
-- declare functions, sets, points and smoothness indices
{f fâ‚ : M â†’ M'} {s sâ‚ t : set M} {x : M} {m n : with_top â„•}

/-- Property in the model space of a model with corners of being `C^n` within at set at a point,
when read in the model vector space. This property will be lifted to manifolds to define smooth
functions between manifolds. -/
def times_cont_diff_within_at_prop (n : with_top â„•) (f s x) : Prop :=
times_cont_diff_within_at ğ•œ n (I' âˆ˜ f âˆ˜ I.symm) (range I âˆ© I.symm â»Â¹' s) (I x)

/-- Being `Câ¿` in the model space is a local property, invariant under smooth maps. Therefore,
it will lift nicely to manifolds. -/
lemma times_cont_diff_within_at_local_invariant_prop (n : with_top â„•) :
  (times_cont_diff_groupoid âˆ I).local_invariant_prop (times_cont_diff_groupoid âˆ I')
  (times_cont_diff_within_at_prop I I' n) :=
{ is_local :=
  begin
    assume s x u f u_open xu,
    have : range I âˆ© I.symm â»Â¹' (s âˆ© u) = (range I âˆ© I.symm â»Â¹' s) âˆ© I.symm â»Â¹' u,
      by simp only [inter_assoc, preimage_inter],
    rw [times_cont_diff_within_at_prop, times_cont_diff_within_at_prop, this],
    symmetry,
    apply times_cont_diff_within_at_inter,
    have : u âˆˆ ğ“ (I.symm (I x)),
      by { rw [model_with_corners.left_inv], exact is_open.mem_nhds u_open xu },
    apply continuous_at.preimage_mem_nhds I.continuous_symm.continuous_at this,
  end,
  right_invariance :=
  begin
    assume s x f e he hx h,
    rw times_cont_diff_within_at_prop at h âŠ¢,
    have : I x = (I âˆ˜ e.symm âˆ˜ I.symm) (I (e x)), by simp only [hx] with mfld_simps,
    rw this at h,
    have : I (e x) âˆˆ (I.symm) â»Â¹' e.target âˆ© range â‡‘I, by simp only [hx] with mfld_simps,
    have := ((mem_groupoid_of_pregroupoid.2 he).2.times_cont_diff_within_at this).of_le le_top,
    convert h.comp' _ this using 1,
    { ext y, simp only with mfld_simps },
    { mfld_set_tac }
  end,
  congr :=
  begin
    assume s x f g h hx hf,
    apply hf.congr,
    { assume y hy,
      simp only with mfld_simps at hy,
      simp only [h, hy] with mfld_simps },
    { simp only [hx] with mfld_simps }
  end,
  left_invariance :=
  begin
    assume s x f e' he' hs hx h,
    rw times_cont_diff_within_at_prop at h âŠ¢,
    have A : (I' âˆ˜ f âˆ˜ I.symm) (I x) âˆˆ (I'.symm â»Â¹' e'.source âˆ© range I'),
      by simp only [hx] with mfld_simps,
    have := ((mem_groupoid_of_pregroupoid.2 he').1.times_cont_diff_within_at A).of_le le_top,
    convert this.comp _ h _,
    { ext y, simp only with mfld_simps },
    { assume y hy, simp only with mfld_simps at hy, simpa only [hy] with mfld_simps using hs hy.2 }
  end }

lemma times_cont_diff_within_at_local_invariant_prop_mono (n : with_top â„•)
  â¦ƒs x tâ¦„ â¦ƒf : H â†’ H'â¦„ (hts : t âŠ† s) (h : times_cont_diff_within_at_prop I I' n f s x) :
  times_cont_diff_within_at_prop I I' n f t x :=
begin
  apply h.mono (Î» y hy, _),
  simp only with mfld_simps at hy,
  simp only [hy, hts _] with mfld_simps
end

lemma times_cont_diff_within_at_local_invariant_prop_id (x : H) :
  times_cont_diff_within_at_prop I I âˆ id univ x :=
begin
  simp [times_cont_diff_within_at_prop],
  have : times_cont_diff_within_at ğ•œ âˆ id (range I) (I x) :=
    times_cont_diff_id.times_cont_diff_at.times_cont_diff_within_at,
  apply this.congr (Î» y hy, _),
  { simp only with mfld_simps },
  { simp only [model_with_corners.right_inv I hy] with mfld_simps }
end

/-- A function is `n` times continuously differentiable within a set at a point in a manifold if
it is continuous and it is `n` times continuously differentiable in this set around this point, when
read in the preferred chart at this point. -/
def times_cont_mdiff_within_at (n : with_top â„•) (f : M â†’ M') (s : set M) (x : M) :=
lift_prop_within_at (times_cont_diff_within_at_prop I I' n) f s x

/-- Abbreviation for `times_cont_mdiff_within_at I I' âŠ¤ f s x`. See also documentation for `smooth`.
-/
@[reducible] def smooth_within_at (f : M â†’ M') (s : set M) (x : M) :=
  times_cont_mdiff_within_at I I' âŠ¤ f s x

/-- A function is `n` times continuously differentiable at a point in a manifold if
it is continuous and it is `n` times continuously differentiable around this point, when
read in the preferred chart at this point. -/
def times_cont_mdiff_at (n : with_top â„•) (f : M â†’ M') (x : M) :=
times_cont_mdiff_within_at I I' n f univ x

/-- Abbreviation for `times_cont_mdiff_at I I' âŠ¤ f x`. See also documentation for `smooth`. -/
@[reducible] def smooth_at (f : M â†’ M') (x : M) := times_cont_mdiff_at I I' âŠ¤ f x

/-- A function is `n` times continuously differentiable in a set of a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable on this set in the charts
around these points. -/
def times_cont_mdiff_on (n : with_top â„•) (f : M â†’ M') (s : set M) :=
âˆ€ x âˆˆ s, times_cont_mdiff_within_at I I' n f s x

/-- Abbreviation for `times_cont_mdiff_on I I' âŠ¤ f s`. See also documentation for `smooth`. -/
@[reducible] def smooth_on (f : M â†’ M') (s : set M) := times_cont_mdiff_on I I' âŠ¤ f s

/-- A function is `n` times continuously differentiable in a manifold if it is continuous
and, for any pair of points, it is `n` times continuously differentiable in the charts
around these points. -/
def times_cont_mdiff (n : with_top â„•) (f : M â†’ M') :=
âˆ€ x, times_cont_mdiff_at I I' n f x

/-- Abbreviation for `times_cont_mdiff I I' âŠ¤ f`.
Short note to work with these abbreviations: a lemma of the form `times_cont_mdiff_foo.bar` will
apply fine to an assumption `smooth_foo` using dot notation or normal notation.
If the consequence `bar` of the lemma involves `times_cont_diff`, it is still better to restate
the lemma replacing `times_cont_diff` with `smooth` both in the assumption and in the conclusion,
to make it possible to use `smooth` consistently.
This also applies to `smooth_at`, `smooth_on` and `smooth_within_at`.-/
@[reducible] def smooth (f : M â†’ M') := times_cont_mdiff I I' âŠ¤ f

/-! ### Basic properties of smooth functions between manifolds -/

variables {I I'}

lemma times_cont_mdiff.smooth (h : times_cont_mdiff I I' âŠ¤ f) : smooth I I' f := h

lemma smooth.times_cont_mdiff (h : smooth I I' f) : times_cont_mdiff I I' âŠ¤ f := h

lemma times_cont_mdiff_on.smooth_on (h : times_cont_mdiff_on I I' âŠ¤ f s) : smooth_on I I' f s := h

lemma smooth_on.times_cont_mdiff_on (h : smooth_on I I' f s) : times_cont_mdiff_on I I' âŠ¤ f s := h

lemma times_cont_mdiff_at.smooth_at (h : times_cont_mdiff_at I I' âŠ¤ f x) : smooth_at I I' f x := h

lemma smooth_at.times_cont_mdiff_at (h : smooth_at I I' f x) : times_cont_mdiff_at I I' âŠ¤ f x := h

lemma times_cont_mdiff_within_at.smooth_within_at (h : times_cont_mdiff_within_at I I' âŠ¤ f s x) :
  smooth_within_at I I' f s x := h

lemma smooth_within_at.times_cont_mdiff_within_at (h : smooth_within_at I I' f s x) :
times_cont_mdiff_within_at I I' âŠ¤ f s x := h

lemma times_cont_mdiff.times_cont_mdiff_at (h : times_cont_mdiff I I' n f) :
  times_cont_mdiff_at I I' n f x :=
h x

lemma smooth.smooth_at (h : smooth I I' f) :
  smooth_at I I' f x := times_cont_mdiff.times_cont_mdiff_at h

lemma times_cont_mdiff_within_at_univ :
  times_cont_mdiff_within_at I I' n f univ x â†” times_cont_mdiff_at I I' n f x :=
iff.rfl

lemma smooth_at_univ :
 smooth_within_at I I' f univ x â†” smooth_at I I' f x := times_cont_mdiff_within_at_univ

lemma times_cont_mdiff_on_univ :
  times_cont_mdiff_on I I' n f univ â†” times_cont_mdiff I I' n f :=
by simp only [times_cont_mdiff_on, times_cont_mdiff, times_cont_mdiff_within_at_univ,
  forall_prop_of_true, mem_univ]

lemma smooth_on_univ :
  smooth_on I I' f univ â†” smooth I I' f := times_cont_mdiff_on_univ

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in the corresponding extended chart. -/
lemma times_cont_mdiff_within_at_iff :
  times_cont_mdiff_within_at I I' n f s x â†” continuous_within_at f s x âˆ§
    times_cont_diff_within_at ğ•œ n ((ext_chart_at I' (f x)) âˆ˜ f âˆ˜ (ext_chart_at I x).symm)
    ((ext_chart_at I x).target âˆ©
      (ext_chart_at I x).symm â»Â¹' (s âˆ© f â»Â¹' (ext_chart_at I' (f x)).source))
    (ext_chart_at I x x) :=
begin
  rw [times_cont_mdiff_within_at, lift_prop_within_at, times_cont_diff_within_at_prop],
  congr' 3,
  mfld_set_tac
end

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in the corresponding extended chart. This form states smoothness of `f`
written in the `ext_chart_at`s within the set `(ext_chart_at I x).symm â»Â¹' s âˆ© range I`. This set
is larger than the set
`(ext_chart_at I x).target âˆ© (ext_chart_at I x).symm â»Â¹' (s âˆ© f â»Â¹' (ext_chart_at I' (f x)).source)`
used in `times_cont_mdiff_within_at_iff` but their germs at `ext_chart_at I x x` are equal. It may
be useful to rewrite using `times_cont_mdiff_within_at_iff''` in the *assumptions* of a lemma and
using `times_cont_mdiff_within_at_iff` in the goal. -/
lemma times_cont_mdiff_within_at_iff'' :
  times_cont_mdiff_within_at I I' n f s x â†” continuous_within_at f s x âˆ§
    times_cont_diff_within_at ğ•œ n (written_in_ext_chart_at I I' x f)
      ((ext_chart_at I x).symm â»Â¹' s âˆ© range I) (ext_chart_at I x x) :=
begin
  rw [times_cont_mdiff_within_at_iff, and.congr_right_iff],
  set e := ext_chart_at I x, set e' := ext_chart_at I' (f x),
  refine Î» hc, times_cont_diff_within_at_congr_nhds _,
  rw [â† e.image_source_inter_eq', â† ext_chart_at_map_nhds_within_eq_image,
      â† ext_chart_at_map_nhds_within, inter_comm, nhds_within_inter_of_mem],
  exact hc (ext_chart_at_source_mem_nhds _ _)
end

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in the corresponding extended chart in the target. -/
lemma times_cont_mdiff_within_at_iff_target :
  times_cont_mdiff_within_at I I' n f s x â†” continuous_within_at f s x âˆ§
    times_cont_mdiff_within_at I ğ“˜(ğ•œ, E') n ((ext_chart_at I' (f x)) âˆ˜ f)
    (s âˆ© f â»Â¹' (ext_chart_at I' (f x)).source) x :=
begin
  rw [times_cont_mdiff_within_at, times_cont_mdiff_within_at, lift_prop_within_at,
    lift_prop_within_at, â† and_assoc],
  have cont : (continuous_within_at f s x âˆ§
      continuous_within_at ((I' âˆ˜ (chart_at H' (f x))) âˆ˜ f)
      (s âˆ© f â»Â¹' (chart_at H' (f x)).to_local_equiv.source) x) â†”
      continuous_within_at f s x,
  { refine âŸ¨Î» h, h.1, Î» h, âŸ¨h, _âŸ©âŸ©,
    have hâ‚ : continuous_within_at _ univ ((chart_at H' (f x)) (f x)),
    { exact (model_with_corners.continuous I').continuous_within_at },
    have hâ‚‚ := (chart_at H' (f x)).continuous_to_fun.continuous_within_at (mem_chart_source _ _),
    convert (hâ‚.comp' hâ‚‚).comp' h,
    simp },
  simp [cont, times_cont_diff_within_at_prop]
end

lemma smooth_within_at_iff :
  smooth_within_at I I' f s x â†” continuous_within_at f s x âˆ§
    times_cont_diff_within_at ğ•œ âˆ ((ext_chart_at I' (f x)) âˆ˜ f âˆ˜ (ext_chart_at I x).symm)
    ((ext_chart_at I x).target âˆ©
      (ext_chart_at I x).symm â»Â¹' (s âˆ© f â»Â¹' (ext_chart_at I' (f x)).source))
    (ext_chart_at I x x) :=
times_cont_mdiff_within_at_iff

lemma smooth_within_at_iff_target :
  smooth_within_at I I' f s x â†” continuous_within_at f s x âˆ§
    smooth_within_at I ğ“˜(ğ•œ, E') ((ext_chart_at I' (f x)) âˆ˜ f)
    (s âˆ© f â»Â¹' (ext_chart_at I' (f x)).source) x :=
times_cont_mdiff_within_at_iff_target

lemma times_cont_mdiff_at_ext_chart_at :
  times_cont_mdiff_at I ğ“˜(ğ•œ, E) n (ext_chart_at I x) x :=
begin
  rw [times_cont_mdiff_at, times_cont_mdiff_within_at_iff],
  refine âŸ¨(ext_chart_at_continuous_at _ _).continuous_within_at, _âŸ©,
  refine times_cont_diff_within_at_id.congr _ _;
    simp only with mfld_simps { contextual := tt }
end

include Is I's

/-- One can reformulate smoothness within a set at a point as continuity within this set at this
point, and smoothness in the corresponding extended chart. -/
lemma times_cont_mdiff_within_at_iff' {x' : M} {y : M'} (hx : x' âˆˆ (chart_at H x).source)
  (hy : f x' âˆˆ (chart_at H' y).source) :
  times_cont_mdiff_within_at I I' n f s x' â†” continuous_within_at f s x' âˆ§
    times_cont_diff_within_at ğ•œ n ((ext_chart_at I' y) âˆ˜ f âˆ˜ (ext_chart_at I x).symm)
    ((ext_chart_at I x).target âˆ© (ext_chart_at I x).symm â»Â¹' (s âˆ© f â»Â¹' (ext_chart_at I' y).source))
    (ext_chart_at I x x') :=
begin
  refine ((times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_indep_chart
    (structure_groupoid.chart_mem_maximal_atlas _ x) hx
    (structure_groupoid.chart_mem_maximal_atlas _ y) hy).trans _,
  rw [times_cont_diff_within_at_prop, iff_eq_eq],
  congr' 2,
  mfld_set_tac
end

omit I's

lemma times_cont_mdiff_at_ext_chart_at' {x' : M} (h : x' âˆˆ (chart_at H x).source) :
  times_cont_mdiff_at I ğ“˜(ğ•œ, E) n (ext_chart_at I x) x' :=
begin
  refine (times_cont_mdiff_within_at_iff' h (mem_chart_source _ _)).2 _,
  refine âŸ¨(ext_chart_at_continuous_at' _ _ _).continuous_within_at, _âŸ©,
  { rwa ext_chart_at_source },
  refine times_cont_diff_within_at_id.congr' _ _;
    simp only [h] with mfld_simps { contextual := tt }
end

include I's

/-- One can reformulate smoothness on a set as continuity on this set, and smoothness in any
extended chart. -/
lemma times_cont_mdiff_on_iff :
  times_cont_mdiff_on I I' n f s â†” continuous_on f s âˆ§
    âˆ€ (x : M) (y : M'), times_cont_diff_on ğ•œ n ((ext_chart_at I' y) âˆ˜ f âˆ˜ (ext_chart_at I x).symm)
    ((ext_chart_at I x).target âˆ©
      (ext_chart_at I x).symm â»Â¹' (s âˆ© f â»Â¹' (ext_chart_at I' y).source)) :=
begin
  split,
  { assume h,
    refine âŸ¨Î» x hx, (h x hx).1, Î» x y z hz, _âŸ©,
    simp only with mfld_simps at hz,
    let w := (ext_chart_at I x).symm z,
    have : w âˆˆ s, by simp only [w, hz] with mfld_simps,
    specialize h w this,
    have w1 : w âˆˆ (chart_at H x).source, by simp only [w, hz] with mfld_simps,
    have w2 : f w âˆˆ (chart_at H' y).source, by simp only [w, hz] with mfld_simps,
    convert
      (((times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_indep_chart
        (structure_groupoid.chart_mem_maximal_atlas _ x) w1
        (structure_groupoid.chart_mem_maximal_atlas _ y) w2).1 h).2 using 1,
    { mfld_set_tac },
    { simp only [w, hz] with mfld_simps } },
  { rintros âŸ¨hcont, hdiffâŸ© x hx,
    refine âŸ¨hcont x hx, _âŸ©,
    have Z := hdiff x (f x) (ext_chart_at I x x) (by simp only [hx] with mfld_simps),
    dsimp [times_cont_diff_within_at_prop],
    convert Z using 1,
    mfld_set_tac }
end

/-- One can reformulate smoothness on a set as continuity on this set, and smoothness in any
extended chart in the target. -/
lemma times_cont_mdiff_on_iff_target :
  times_cont_mdiff_on I I' n f s â†” continuous_on f s âˆ§ âˆ€ (y : M'),
    times_cont_mdiff_on I ğ“˜(ğ•œ, E') n ((ext_chart_at I' y) âˆ˜ f)
    (s âˆ© f â»Â¹' (ext_chart_at I' y).source) :=
begin
  inhabit E',
  simp only [times_cont_mdiff_on_iff, model_with_corners.source_eq, chart_at_self_eq,
    local_homeomorph.refl_local_equiv, local_equiv.refl_trans, ext_chart_at.equations._eqn_1,
    set.preimage_univ, set.inter_univ, and.congr_right_iff],
  intros h,
  split,
  { refine Î» h' y, âŸ¨_, Î» x _, h' x yâŸ©,
    have h'' : continuous_on _ univ := (model_with_corners.continuous I').continuous_on,
    convert (h''.comp' (chart_at H' y).continuous_to_fun).comp' h,
    simp },
  { exact Î» h' x y, (h' y).2 x (default E') }
end

lemma smooth_on_iff :
  smooth_on I I' f s â†” continuous_on f s âˆ§
    âˆ€ (x : M) (y : M'), times_cont_diff_on ğ•œ âŠ¤ ((ext_chart_at I' y) âˆ˜ f âˆ˜ (ext_chart_at I x).symm)
    ((ext_chart_at I x).target âˆ©
      (ext_chart_at I x).symm â»Â¹' (s âˆ© f â»Â¹' (ext_chart_at I' y).source)) :=
times_cont_mdiff_on_iff

lemma smooth_on_iff_target :
  smooth_on I I' f s â†” continuous_on f s âˆ§ âˆ€ (y : M'),
    smooth_on I ğ“˜(ğ•œ, E') ((ext_chart_at I' y) âˆ˜ f)
    (s âˆ© f â»Â¹' (ext_chart_at I' y).source) :=
times_cont_mdiff_on_iff_target

/-- One can reformulate smoothness as continuity and smoothness in any extended chart. -/
lemma times_cont_mdiff_iff :
  times_cont_mdiff I I' n f â†” continuous f âˆ§
    âˆ€ (x : M) (y : M'), times_cont_diff_on ğ•œ n ((ext_chart_at I' y) âˆ˜ f âˆ˜ (ext_chart_at I x).symm)
    ((ext_chart_at I x).target âˆ© (ext_chart_at I x).symm â»Â¹' (f â»Â¹' (ext_chart_at I' y).source)) :=
by simp [â† times_cont_mdiff_on_univ, times_cont_mdiff_on_iff, continuous_iff_continuous_on_univ]

/-- One can reformulate smoothness as continuity and smoothness in any extended chart in the
target. -/
lemma times_cont_mdiff_iff_target :
  times_cont_mdiff I I' n f â†” continuous f âˆ§
    âˆ€ (y : M'), times_cont_mdiff_on I ğ“˜(ğ•œ, E') n ((ext_chart_at I' y) âˆ˜ f)
    (f â»Â¹' (ext_chart_at I' y).source) :=
begin
  rw [â† times_cont_mdiff_on_univ, times_cont_mdiff_on_iff_target],
  simp [continuous_iff_continuous_on_univ]
end

lemma smooth_iff :
  smooth I I' f â†” continuous f âˆ§
    âˆ€ (x : M) (y : M'), times_cont_diff_on ğ•œ âŠ¤ ((ext_chart_at I' y) âˆ˜ f âˆ˜ (ext_chart_at I x).symm)
    ((ext_chart_at I x).target âˆ© (ext_chart_at I x).symm â»Â¹' (f â»Â¹' (ext_chart_at I' y).source)) :=
times_cont_mdiff_iff

lemma smooth_iff_target :
  smooth I I' f â†” continuous f âˆ§ âˆ€ (y : M'), smooth_on I ğ“˜(ğ•œ, E') ((ext_chart_at I' y) âˆ˜ f)
    (f â»Â¹' (ext_chart_at I' y).source) :=
times_cont_mdiff_iff_target

omit Is I's

/-! ### Deducing smoothness from higher smoothness -/

lemma times_cont_mdiff_within_at.of_le (hf : times_cont_mdiff_within_at I I' n f s x) (le : m â‰¤ n) :
  times_cont_mdiff_within_at I I' m f s x :=
âŸ¨hf.1, hf.2.of_le leâŸ©

lemma times_cont_mdiff_at.of_le (hf : times_cont_mdiff_at I I' n f x) (le : m â‰¤ n) :
  times_cont_mdiff_at I I' m f x :=
times_cont_mdiff_within_at.of_le hf le

lemma times_cont_mdiff_on.of_le (hf : times_cont_mdiff_on I I' n f s) (le : m â‰¤ n) :
  times_cont_mdiff_on I I' m f s :=
Î» x hx, (hf x hx).of_le le

lemma times_cont_mdiff.of_le (hf : times_cont_mdiff I I' n f) (le : m â‰¤ n) :
  times_cont_mdiff I I' m f :=
Î» x, (hf x).of_le le

/-! ### Deducing smoothness from smoothness one step beyond -/

lemma times_cont_mdiff_within_at.of_succ {n : â„•}
  (h : times_cont_mdiff_within_at I I' n.succ f s x) :
  times_cont_mdiff_within_at I I' n f s x :=
h.of_le (with_top.coe_le_coe.2 (nat.le_succ n))

lemma times_cont_mdiff_at.of_succ {n : â„•} (h : times_cont_mdiff_at I I' n.succ f x) :
  times_cont_mdiff_at I I' n f x :=
times_cont_mdiff_within_at.of_succ h

lemma times_cont_mdiff_on.of_succ {n : â„•} (h : times_cont_mdiff_on I I' n.succ f s) :
  times_cont_mdiff_on I I' n f s :=
Î» x hx, (h x hx).of_succ

lemma times_cont_mdiff.of_succ {n : â„•} (h : times_cont_mdiff I I' n.succ f) :
  times_cont_mdiff I I' n f :=
Î» x, (h x).of_succ

/-! ### Deducing continuity from smoothness-/

lemma times_cont_mdiff_within_at.continuous_within_at
  (hf : times_cont_mdiff_within_at I I' n f s x) : continuous_within_at f s x :=
hf.1

lemma times_cont_mdiff_at.continuous_at
  (hf : times_cont_mdiff_at I I' n f x) : continuous_at f x :=
(continuous_within_at_univ _ _ ).1 $ times_cont_mdiff_within_at.continuous_within_at hf

lemma times_cont_mdiff_on.continuous_on
  (hf : times_cont_mdiff_on I I' n f s) : continuous_on f s :=
Î» x hx, (hf x hx).continuous_within_at

lemma times_cont_mdiff.continuous (hf : times_cont_mdiff I I' n f) :
  continuous f :=
continuous_iff_continuous_at.2 $ Î» x, (hf x).continuous_at

/-! ### Deducing differentiability from smoothness -/

lemma times_cont_mdiff_within_at.mdifferentiable_within_at
  (hf : times_cont_mdiff_within_at I I' n f s x) (hn : 1 â‰¤ n) :
  mdifferentiable_within_at I I' f s x :=
begin
  suffices h : mdifferentiable_within_at I I' f (s âˆ© (f â»Â¹' (ext_chart_at I' (f x)).source)) x,
  { rwa mdifferentiable_within_at_inter' at h,
    apply (hf.1).preimage_mem_nhds_within,
    exact is_open.mem_nhds (ext_chart_at_open_source I' (f x)) (mem_ext_chart_source I' (f x)) },
  rw mdifferentiable_within_at_iff,
  exact âŸ¨hf.1.mono (inter_subset_left _ _),
    (hf.2.differentiable_within_at hn).mono (by mfld_set_tac)âŸ©,
end

lemma times_cont_mdiff_at.mdifferentiable_at (hf : times_cont_mdiff_at I I' n f x) (hn : 1 â‰¤ n) :
  mdifferentiable_at I I' f x :=
mdifferentiable_within_at_univ.1 $ times_cont_mdiff_within_at.mdifferentiable_within_at hf hn

lemma times_cont_mdiff_on.mdifferentiable_on (hf : times_cont_mdiff_on I I' n f s) (hn : 1 â‰¤ n) :
  mdifferentiable_on I I' f s :=
Î» x hx, (hf x hx).mdifferentiable_within_at hn

lemma times_cont_mdiff.mdifferentiable (hf : times_cont_mdiff I I' n f) (hn : 1 â‰¤ n) :
  mdifferentiable I I' f :=
Î» x, (hf x).mdifferentiable_at hn

lemma smooth.mdifferentiable (hf : smooth I I' f) : mdifferentiable I I' f :=
times_cont_mdiff.mdifferentiable hf le_top

lemma smooth.mdifferentiable_at (hf : smooth I I' f) : mdifferentiable_at I I' f x :=
hf.mdifferentiable x

lemma smooth.mdifferentiable_within_at (hf : smooth I I' f) :
  mdifferentiable_within_at I I' f s x :=
hf.mdifferentiable_at.mdifferentiable_within_at

/-! ### `C^âˆ` smoothness -/

lemma times_cont_mdiff_within_at_top :
  smooth_within_at I I' f s x â†” (âˆ€n:â„•, times_cont_mdiff_within_at I I' n f s x) :=
âŸ¨Î» h n, âŸ¨h.1, times_cont_diff_within_at_top.1 h.2 nâŸ©,
 Î» H, âŸ¨(H 0).1, times_cont_diff_within_at_top.2 (Î» n, (H n).2)âŸ©âŸ©

lemma times_cont_mdiff_at_top :
  smooth_at I I' f x â†” (âˆ€n:â„•, times_cont_mdiff_at I I' n f x) :=
times_cont_mdiff_within_at_top

lemma times_cont_mdiff_on_top :
  smooth_on I I' f s â†” (âˆ€n:â„•, times_cont_mdiff_on I I' n f s) :=
âŸ¨Î» h n, h.of_le le_top, Î» h x hx, times_cont_mdiff_within_at_top.2 (Î» n, h n x hx)âŸ©

lemma times_cont_mdiff_top :
  smooth I I' f â†” (âˆ€n:â„•, times_cont_mdiff I I' n f) :=
âŸ¨Î» h n, h.of_le le_top, Î» h x, times_cont_mdiff_within_at_top.2 (Î» n, h n x)âŸ©

lemma times_cont_mdiff_within_at_iff_nat :
  times_cont_mdiff_within_at I I' n f s x â†”
  (âˆ€m:â„•, (m : with_top â„•) â‰¤ n â†’ times_cont_mdiff_within_at I I' m f s x) :=
begin
  refine âŸ¨Î» h m hm, h.of_le hm, Î» h, _âŸ©,
  cases n,
  { exact times_cont_mdiff_within_at_top.2 (Î» n, h n le_top) },
  { exact h n (le_refl _) }
end

/-! ### Restriction to a smaller set -/

lemma times_cont_mdiff_within_at.mono (hf : times_cont_mdiff_within_at I I' n f s x) (hts : t âŠ† s) :
  times_cont_mdiff_within_at I I' n f t x :=
structure_groupoid.local_invariant_prop.lift_prop_within_at_mono
  (times_cont_diff_within_at_local_invariant_prop_mono I I' n) hf hts

lemma times_cont_mdiff_at.times_cont_mdiff_within_at (hf : times_cont_mdiff_at I I' n f x) :
  times_cont_mdiff_within_at I I' n f s x :=
times_cont_mdiff_within_at.mono hf (subset_univ _)

lemma smooth_at.smooth_within_at (hf : smooth_at I I' f x) :
  smooth_within_at I I' f s x :=
times_cont_mdiff_at.times_cont_mdiff_within_at hf

lemma times_cont_mdiff_on.mono (hf : times_cont_mdiff_on I I' n f s) (hts : t âŠ† s) :
  times_cont_mdiff_on I I' n f t :=
Î» x hx, (hf x (hts hx)).mono hts

lemma times_cont_mdiff.times_cont_mdiff_on (hf : times_cont_mdiff I I' n f) :
  times_cont_mdiff_on I I' n f s :=
Î» x hx, (hf x).times_cont_mdiff_within_at

lemma smooth.smooth_on (hf : smooth I I' f) :
  smooth_on I I' f s :=
times_cont_mdiff.times_cont_mdiff_on hf

lemma times_cont_mdiff_within_at_inter' (ht : t âˆˆ ğ“[s] x) :
  times_cont_mdiff_within_at I I' n f (s âˆ© t) x â†” times_cont_mdiff_within_at I I' n f s x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_inter' ht

lemma times_cont_mdiff_within_at_inter (ht : t âˆˆ ğ“ x) :
  times_cont_mdiff_within_at I I' n f (s âˆ© t) x â†” times_cont_mdiff_within_at I I' n f s x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_inter ht

lemma times_cont_mdiff_within_at.times_cont_mdiff_at
  (h : times_cont_mdiff_within_at I I' n f s x) (ht : s âˆˆ ğ“ x) :
  times_cont_mdiff_at I I' n f x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_at_of_lift_prop_within_at h ht

lemma smooth_within_at.smooth_at
  (h : smooth_within_at I I' f s x) (ht : s âˆˆ ğ“ x) :
  smooth_at I I' f x :=
times_cont_mdiff_within_at.times_cont_mdiff_at h ht

include Is

lemma times_cont_mdiff_on_ext_chart_at :
  times_cont_mdiff_on I ğ“˜(ğ•œ, E) n (ext_chart_at I x) (chart_at H x).source :=
Î» x' hx', (times_cont_mdiff_at_ext_chart_at' hx').times_cont_mdiff_within_at

include I's

/-- A function is `C^n` within a set at a point, for `n : â„•`, if and only if it is `C^n` on
a neighborhood of this point. -/
lemma times_cont_mdiff_within_at_iff_times_cont_mdiff_on_nhds {n : â„•} :
  times_cont_mdiff_within_at I I' n f s x â†”
  âˆƒ u âˆˆ ğ“[insert x s] x, times_cont_mdiff_on I I' n f u :=
begin
  split,
  { assume h,
    -- the property is true in charts. We will pull such a good neighborhood in the chart to the
    -- manifold. For this, we need to restrict to a small enough set where everything makes sense
    obtain âŸ¨o, o_open, xo, ho, h'oâŸ© : âˆƒ (o : set M),
      is_open o âˆ§ x âˆˆ o âˆ§ o âŠ† (chart_at H x).source âˆ§ o âˆ© s âŠ† f â»Â¹' (chart_at H' (f x)).source,
    { have : (chart_at H' (f x)).source âˆˆ ğ“ (f x) :=
        is_open.mem_nhds (local_homeomorph.open_source _) (mem_chart_source H' (f x)),
      rcases mem_nhds_within.1 (h.1.preimage_mem_nhds_within this) with âŸ¨u, u_open, xu, huâŸ©,
      refine âŸ¨u âˆ© (chart_at H x).source, _, âŸ¨xu, mem_chart_source _ _âŸ©, _, _âŸ©,
      { exact is_open.inter u_open (local_homeomorph.open_source _) },
      { assume y hy, exact hy.2 },
      { assume y hy, exact hu âŸ¨hy.1.1, hy.2âŸ© } },
    have h' : times_cont_mdiff_within_at I I' n f (s âˆ© o) x := h.mono (inter_subset_left _ _),
    simp only [times_cont_mdiff_within_at, lift_prop_within_at, times_cont_diff_within_at_prop]
      at h',
    -- let `u` be a good neighborhood in the chart where the function is smooth
    rcases h.2.times_cont_diff_on (le_refl _) with âŸ¨u, u_nhds, u_subset, huâŸ©,
    -- pull it back to the manifold, and intersect with a suitable neighborhood of `x`, to get the
    -- desired good neighborhood `v`.
    let v := ((insert x s) âˆ© o) âˆ© (ext_chart_at I x) â»Â¹' u,
    have v_incl : v âŠ† (chart_at H x).source := Î» y hy, ho hy.1.2,
    have v_incl' : âˆ€ y âˆˆ v, f y âˆˆ (chart_at H' (f x)).source,
    { assume y hy,
      rcases hy.1.1 with rfl|h',
      { simp only with mfld_simps },
      { apply h'o âŸ¨hy.1.2, h'âŸ© } },
    refine âŸ¨v, _, _âŸ©,
    show v âˆˆ ğ“[insert x s] x,
    { rw nhds_within_restrict _ xo o_open,
      refine filter.inter_mem self_mem_nhds_within _,
      suffices : u âˆˆ ğ“[(ext_chart_at I x) '' (insert x s âˆ© o)] (ext_chart_at I x x),
        from (ext_chart_at_continuous_at I x).continuous_within_at.preimage_mem_nhds_within' this,
      apply nhds_within_mono _ _ u_nhds,
      rw image_subset_iff,
      assume y hy,
      rcases hy.1 with rfl|h',
      { simp only [mem_insert_iff] with mfld_simps },
      { simp only [mem_insert_iff, ho hy.2, h', h'o âŸ¨hy.2, h'âŸ©] with mfld_simps } },
    show times_cont_mdiff_on I I' n f v,
    { assume y hy,
      apply
        (((times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_indep_chart
        (structure_groupoid.chart_mem_maximal_atlas _ x) (v_incl hy)
        (structure_groupoid.chart_mem_maximal_atlas _ (f x)) (v_incl' y hy))).2,
      split,
      { apply (((ext_chart_at_continuous_on_symm I' (f x) _ _).comp'
          (hu _ hy.2).continuous_within_at).comp' (ext_chart_at_continuous_on I x _ _)).congr_mono,
        { assume z hz,
          simp only [v_incl hz, v_incl' z hz] with mfld_simps },
        { assume z hz,
          simp only [v_incl hz, v_incl' z hz] with mfld_simps,
          exact hz.2 },
        { simp only [v_incl hy, v_incl' y hy] with mfld_simps },
        { simp only [v_incl hy, v_incl' y hy] with mfld_simps },
        { simp only [v_incl hy] with mfld_simps } },
      { apply hu.mono,
        { assume z hz,
          simp only [v] with mfld_simps at hz,
          have : I ((chart_at H x) (((chart_at H x).symm) (I.symm z))) âˆˆ u, by simp only [hz],
          simpa only [hz] with mfld_simps using this },
        { have exty : I (chart_at H x y) âˆˆ u := hy.2,
          simp only [v_incl hy, v_incl' y hy, exty, hy.1.1, hy.1.2] with mfld_simps } } } },
  { rintros âŸ¨u, u_nhds, huâŸ©,
    have : times_cont_mdiff_within_at I I' â†‘n f (insert x s âˆ© u) x,
    { have : x âˆˆ insert x s := mem_insert x s,
      exact hu.mono (inter_subset_right _ _) _ âŸ¨this, mem_of_mem_nhds_within this u_nhdsâŸ© },
    rw times_cont_mdiff_within_at_inter' u_nhds at this,
    exact this.mono (subset_insert x s) }
end

/-- A function is `C^n` at a point, for `n : â„•`, if and only if it is `C^n` on
a neighborhood of this point. -/
lemma times_cont_mdiff_at_iff_times_cont_mdiff_on_nhds {n : â„•} :
  times_cont_mdiff_at I I' n f x â†” âˆƒ u âˆˆ ğ“ x, times_cont_mdiff_on I I' n f u :=
by simp [â† times_cont_mdiff_within_at_univ, times_cont_mdiff_within_at_iff_times_cont_mdiff_on_nhds,
  nhds_within_univ]

omit Is I's

/-! ### Congruence lemmas -/

lemma times_cont_mdiff_within_at.congr
  (h : times_cont_mdiff_within_at I I' n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
  (hx : fâ‚ x = f x) : times_cont_mdiff_within_at I I' n fâ‚ s x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_congr h hâ‚ hx

lemma times_cont_mdiff_within_at_congr (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : fâ‚ x = f x) :
  times_cont_mdiff_within_at I I' n fâ‚ s x â†” times_cont_mdiff_within_at I I' n f s x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_congr_iff hâ‚ hx

lemma times_cont_mdiff_within_at.congr_of_eventually_eq
  (h : times_cont_mdiff_within_at I I' n f s x) (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f)
  (hx : fâ‚ x = f x) : times_cont_mdiff_within_at I I' n fâ‚ s x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_within_at_congr_of_eventually_eq
  h hâ‚ hx

lemma filter.eventually_eq.times_cont_mdiff_within_at_iff
  (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
  times_cont_mdiff_within_at I I' n fâ‚ s x â†” times_cont_mdiff_within_at I I' n f s x :=
(times_cont_diff_within_at_local_invariant_prop I I' n)
  .lift_prop_within_at_congr_iff_of_eventually_eq hâ‚ hx

lemma times_cont_mdiff_at.congr_of_eventually_eq
  (h : times_cont_mdiff_at I I' n f x) (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
  times_cont_mdiff_at I I' n fâ‚ x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_at_congr_of_eventually_eq h hâ‚

lemma filter.eventually_eq.times_cont_mdiff_at_iff (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
  times_cont_mdiff_at I I' n fâ‚ x â†” times_cont_mdiff_at I I' n f x :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_at_congr_iff_of_eventually_eq hâ‚

lemma times_cont_mdiff_on.congr (h : times_cont_mdiff_on I I' n f s) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) :
  times_cont_mdiff_on I I' n fâ‚ s :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_on_congr h hâ‚

lemma times_cont_mdiff_on_congr (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) :
  times_cont_mdiff_on I I' n fâ‚ s â†” times_cont_mdiff_on I I' n f s :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_on_congr_iff hâ‚

/-! ### Locality -/

/-- Being `C^n` is a local property. -/
lemma times_cont_mdiff_on_of_locally_times_cont_mdiff_on
  (h : âˆ€xâˆˆs, âˆƒu, is_open u âˆ§ x âˆˆ u âˆ§ times_cont_mdiff_on I I' n f (s âˆ© u)) :
  times_cont_mdiff_on I I' n f s :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_on_of_locally_lift_prop_on h

lemma times_cont_mdiff_of_locally_times_cont_mdiff_on
  (h : âˆ€x, âˆƒu, is_open u âˆ§ x âˆˆ u âˆ§ times_cont_mdiff_on I I' n f u) :
  times_cont_mdiff I I' n f :=
(times_cont_diff_within_at_local_invariant_prop I I' n).lift_prop_of_locally_lift_prop_on h

/-! ### Smoothness of the composition of smooth functions between manifolds -/

section composition

variables {E'' : Type*} [normed_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']

/-- The composition of `C^n` functions within domains at points is `C^n`. -/
lemma times_cont_mdiff_within_at.comp {t : set M'} {g : M' â†’ M''} (x : M)
  (hg : times_cont_mdiff_within_at I' I'' n g t (f x))
  (hf : times_cont_mdiff_within_at I I' n f s x)
  (st : maps_to f s t) : times_cont_mdiff_within_at I I'' n (g âˆ˜ f) s x :=
begin
  rw times_cont_mdiff_within_at_iff'' at hg hf âŠ¢,
  refine âŸ¨hg.1.comp hf.1 st, _âŸ©,
  set e := ext_chart_at I x,
  set e' := ext_chart_at I' (f x),
  set e'' := ext_chart_at I'' (g (f x)),
  have : e' (f x) = (written_in_ext_chart_at I I' x f) (e x),
    by simp only [e, e'] with mfld_simps,
  rw this at hg,
  have A : âˆ€á¶  y in ğ“[e.symm â»Â¹' s âˆ© range I] e x,
    y âˆˆ e.target âˆ§ f (e.symm y) âˆˆ t âˆ§ f (e.symm y) âˆˆ e'.source âˆ§ g (f (e.symm y)) âˆˆ e''.source,
  { simp only [â† ext_chart_at_map_nhds_within, eventually_map],
    filter_upwards [hf.1.tendsto (ext_chart_at_source_mem_nhds I' (f x)),
      (hg.1.comp hf.1 st).tendsto (ext_chart_at_source_mem_nhds I'' (g (f x))),
      (inter_mem_nhds_within s (ext_chart_at_source_mem_nhds I x))],
    rintros x' (hfx' : f x' âˆˆ _) (hgfx' : g (f x') âˆˆ _) âŸ¨hx's, hx'âŸ©,
    simp only [e.map_source hx', true_and, e.left_inv hx', st hx's, *] },
  refine ((hg.2.comp _ (hf.2.mono (inter_subset_right _ _)) (inter_subset_left _ _)).mono_of_mem
    (inter_mem _ self_mem_nhds_within)).congr_of_eventually_eq _ _,
  { filter_upwards [A],
    rintro x' âŸ¨hx', ht, hfx', hgfx'âŸ©,
    simp only [*, mem_preimage, written_in_ext_chart_at, (âˆ˜), mem_inter_eq, e'.left_inv, true_and],
    exact mem_range_self _ },
  { filter_upwards [A],
    rintro x' âŸ¨hx', ht, hfx', hgfx'âŸ©,
    simp only [*, (âˆ˜), written_in_ext_chart_at, e'.left_inv] },
  { simp only [written_in_ext_chart_at, (âˆ˜), mem_ext_chart_source, e.left_inv, e'.left_inv] }
end

/-- The composition of `C^n` functions on domains is `C^n`. -/
lemma times_cont_mdiff_on.comp {t : set M'} {g : M' â†’ M''}
  (hg : times_cont_mdiff_on I' I'' n g t) (hf : times_cont_mdiff_on I I' n f s)
  (st : s âŠ† f â»Â¹' t) : times_cont_mdiff_on I I'' n (g âˆ˜ f) s :=
Î» x hx, (hg _ (st hx)).comp x (hf x hx) st

/-- The composition of `C^n` functions on domains is `C^n`. -/
lemma times_cont_mdiff_on.comp' {t : set M'} {g : M' â†’ M''}
  (hg : times_cont_mdiff_on I' I'' n g t) (hf : times_cont_mdiff_on I I' n f s) :
  times_cont_mdiff_on I I'' n (g âˆ˜ f) (s âˆ© f â»Â¹' t) :=
hg.comp (hf.mono (inter_subset_left _ _)) (inter_subset_right _ _)

/-- The composition of `C^n` functions is `C^n`. -/
lemma times_cont_mdiff.comp {g : M' â†’ M''}
  (hg : times_cont_mdiff I' I'' n g) (hf : times_cont_mdiff I I' n f) :
  times_cont_mdiff I I'' n (g âˆ˜ f) :=
begin
  rw â† times_cont_mdiff_on_univ at hf hg âŠ¢,
  exact hg.comp hf subset_preimage_univ,
end

/-- The composition of `C^n` functions within domains at points is `C^n`. -/
lemma times_cont_mdiff_within_at.comp' {t : set M'} {g : M' â†’ M''} (x : M)
  (hg : times_cont_mdiff_within_at I' I'' n g t (f x))
  (hf : times_cont_mdiff_within_at I I' n f s x) :
  times_cont_mdiff_within_at I I'' n (g âˆ˜ f) (s âˆ© fâ»Â¹' t) x :=
hg.comp x (hf.mono (inter_subset_left _ _)) (inter_subset_right _ _)

/-- `g âˆ˜ f` is `C^n` within `s` at `x` if `g` is `C^n` at `f x` and
`f` is `C^n` within `s` at `x`. -/
lemma times_cont_mdiff_at.comp_times_cont_mdiff_within_at {g : M' â†’ M''} (x : M)
  (hg : times_cont_mdiff_at I' I'' n g (f x)) (hf : times_cont_mdiff_within_at I I' n f s x) :
  times_cont_mdiff_within_at I I'' n (g âˆ˜ f) s x :=
hg.comp x hf (maps_to_univ _ _)

/-- The composition of `C^n` functions at points is `C^n`. -/
lemma times_cont_mdiff_at.comp {g : M' â†’ M''} (x : M)
  (hg : times_cont_mdiff_at I' I'' n g (f x)) (hf : times_cont_mdiff_at I I' n f x) :
  times_cont_mdiff_at I I'' n (g âˆ˜ f) x :=
hg.comp x hf (maps_to_univ _ _)

lemma times_cont_mdiff.comp_times_cont_mdiff_on {f : M â†’ M'} {g : M' â†’ M''} {s : set M}
  (hg : times_cont_mdiff I' I'' n g) (hf : times_cont_mdiff_on I I' n f s) :
  times_cont_mdiff_on I I'' n (g âˆ˜ f) s :=
hg.times_cont_mdiff_on.comp hf set.subset_preimage_univ

lemma smooth.comp_smooth_on {f : M â†’ M'} {g : M' â†’ M''} {s : set M}
  (hg : smooth I' I'' g) (hf : smooth_on I I' f s) :
  smooth_on I I'' (g âˆ˜ f) s :=
hg.smooth_on.comp hf set.subset_preimage_univ

end composition

/-! ### Atlas members are smooth -/
section atlas

variables {e : local_homeomorph M H}
include Is

/-- An atlas member is `C^n` for any `n`. -/
lemma times_cont_mdiff_on_of_mem_maximal_atlas
  (h : e âˆˆ maximal_atlas I M) : times_cont_mdiff_on I I n e e.source :=
times_cont_mdiff_on.of_le
  ((times_cont_diff_within_at_local_invariant_prop I I âˆ).lift_prop_on_of_mem_maximal_atlas
    (times_cont_diff_within_at_local_invariant_prop_id I) h) le_top

/-- The inverse of an atlas member is `C^n` for any `n`. -/
lemma times_cont_mdiff_on_symm_of_mem_maximal_atlas
  (h : e âˆˆ maximal_atlas I M) : times_cont_mdiff_on I I n e.symm e.target :=
times_cont_mdiff_on.of_le
  ((times_cont_diff_within_at_local_invariant_prop I I âˆ).lift_prop_on_symm_of_mem_maximal_atlas
    (times_cont_diff_within_at_local_invariant_prop_id I) h) le_top

lemma times_cont_mdiff_on_chart :
  times_cont_mdiff_on I I n (chart_at H x) (chart_at H x).source :=
times_cont_mdiff_on_of_mem_maximal_atlas
  ((times_cont_diff_groupoid âŠ¤ I).chart_mem_maximal_atlas x)

lemma times_cont_mdiff_on_chart_symm :
  times_cont_mdiff_on I I n (chart_at H x).symm (chart_at H x).target :=
times_cont_mdiff_on_symm_of_mem_maximal_atlas
  ((times_cont_diff_groupoid âŠ¤ I).chart_mem_maximal_atlas x)

end atlas

/-! ### The identity is smooth -/
section id

lemma times_cont_mdiff_id : times_cont_mdiff I I n (id : M â†’ M) :=
times_cont_mdiff.of_le ((times_cont_diff_within_at_local_invariant_prop I I âˆ).lift_prop_id
  (times_cont_diff_within_at_local_invariant_prop_id I)) le_top

lemma smooth_id : smooth I I (id : M â†’ M) := times_cont_mdiff_id

lemma times_cont_mdiff_on_id : times_cont_mdiff_on I I n (id : M â†’ M) s :=
times_cont_mdiff_id.times_cont_mdiff_on

lemma smooth_on_id : smooth_on I I (id : M â†’ M) s := times_cont_mdiff_on_id

lemma times_cont_mdiff_at_id : times_cont_mdiff_at I I n (id : M â†’ M) x :=
times_cont_mdiff_id.times_cont_mdiff_at

lemma smooth_at_id : smooth_at I I (id : M â†’ M) x := times_cont_mdiff_at_id

lemma times_cont_mdiff_within_at_id : times_cont_mdiff_within_at I I n (id : M â†’ M) s x :=
times_cont_mdiff_at_id.times_cont_mdiff_within_at

lemma smooth_within_at_id : smooth_within_at I I (id : M â†’ M) s x := times_cont_mdiff_within_at_id

end id

/-! ### Constants are smooth -/
section id

variable {c : M'}

lemma times_cont_mdiff_const : times_cont_mdiff I I' n (Î» (x : M), c) :=
begin
  assume x,
  refine âŸ¨continuous_within_at_const, _âŸ©,
  simp only [times_cont_diff_within_at_prop, (âˆ˜)],
  exact times_cont_diff_within_at_const,
end

@[to_additive]
lemma times_cont_mdiff_one [has_one M'] : times_cont_mdiff I I' n (1 : M â†’ M') :=
by simp only [pi.one_def, times_cont_mdiff_const]

lemma smooth_const : smooth I I' (Î» (x : M), c) := times_cont_mdiff_const

@[to_additive]
lemma smooth_one [has_one M'] : smooth I I' (1 : M â†’ M') :=
by simp only [pi.one_def, smooth_const]

lemma times_cont_mdiff_on_const : times_cont_mdiff_on I I' n (Î» (x : M), c) s :=
times_cont_mdiff_const.times_cont_mdiff_on

@[to_additive]
lemma times_cont_mdiff_on_one [has_one M'] : times_cont_mdiff_on I I' n (1 : M â†’ M') s :=
times_cont_mdiff_one.times_cont_mdiff_on

lemma smooth_on_const : smooth_on I I' (Î» (x : M), c) s :=
times_cont_mdiff_on_const

@[to_additive]
lemma smooth_on_one [has_one M'] : smooth_on I I' (1 : M â†’ M') s :=
times_cont_mdiff_on_one

lemma times_cont_mdiff_at_const : times_cont_mdiff_at I I' n (Î» (x : M), c) x :=
times_cont_mdiff_const.times_cont_mdiff_at

@[to_additive]
lemma times_cont_mdiff_at_one [has_one M'] : times_cont_mdiff_at I I' n (1 : M â†’ M') x :=
times_cont_mdiff_one.times_cont_mdiff_at

lemma smooth_at_const : smooth_at I I' (Î» (x : M), c) x :=
times_cont_mdiff_at_const

@[to_additive]
lemma smooth_at_one [has_one M'] : smooth_at I I' (1 : M â†’ M') x :=
times_cont_mdiff_at_one

lemma times_cont_mdiff_within_at_const : times_cont_mdiff_within_at I I' n (Î» (x : M), c) s x :=
times_cont_mdiff_at_const.times_cont_mdiff_within_at

@[to_additive]
lemma times_cont_mdiff_within_at_one [has_one M'] :
  times_cont_mdiff_within_at I I' n (1 : M â†’ M') s x :=
times_cont_mdiff_at_const.times_cont_mdiff_within_at

lemma smooth_within_at_const : smooth_within_at I I' (Î» (x : M), c) s x :=
times_cont_mdiff_within_at_const

@[to_additive]
lemma smooth_within_at_one [has_one M'] : smooth_within_at I I' (1 : M â†’ M') s x :=
times_cont_mdiff_within_at_one

end id

lemma times_cont_mdiff_of_support {f : M â†’ F}
  (hf : âˆ€ x âˆˆ closure (support f), times_cont_mdiff_at I ğ“˜(ğ•œ, F) n f x) :
  times_cont_mdiff I ğ“˜(ğ•œ, F) n f :=
begin
  intro x,
  by_cases hx : x âˆˆ closure (support f),
  { exact hf x hx },
  { refine times_cont_mdiff_at.congr_of_eventually_eq _ (eventually_eq_zero_nhds.2 hx),
    exact times_cont_mdiff_at_const }
end

/-! ### Equivalence with the basic definition for functions between vector spaces -/

section module

lemma times_cont_mdiff_within_at_iff_times_cont_diff_within_at {f : E â†’ E'} {s : set E} {x : E} :
  times_cont_mdiff_within_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') n f s x
  â†” times_cont_diff_within_at ğ•œ n f s x :=
begin
  simp only [times_cont_mdiff_within_at, lift_prop_within_at, times_cont_diff_within_at_prop,
    iff_def] with mfld_simps {contextual := tt},
  exact times_cont_diff_within_at.continuous_within_at
end

alias times_cont_mdiff_within_at_iff_times_cont_diff_within_at â†”
  times_cont_mdiff_within_at.times_cont_diff_within_at
  times_cont_diff_within_at.times_cont_mdiff_within_at

lemma times_cont_mdiff_at_iff_times_cont_diff_at {f : E â†’ E'} {x : E} :
  times_cont_mdiff_at ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') n f x
  â†” times_cont_diff_at ğ•œ n f x :=
by rw [â† times_cont_mdiff_within_at_univ,
  times_cont_mdiff_within_at_iff_times_cont_diff_within_at, times_cont_diff_within_at_univ]

alias times_cont_mdiff_at_iff_times_cont_diff_at â†”
  times_cont_mdiff_at.times_cont_diff_at times_cont_diff_at.times_cont_mdiff_at

lemma times_cont_mdiff_on_iff_times_cont_diff_on {f : E â†’ E'} {s : set E} :
  times_cont_mdiff_on ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') n f s
  â†” times_cont_diff_on ğ•œ n f s :=
forall_congr $ by simp [times_cont_mdiff_within_at_iff_times_cont_diff_within_at]

alias times_cont_mdiff_on_iff_times_cont_diff_on â†”
  times_cont_mdiff_on.times_cont_diff_on times_cont_diff_on.times_cont_mdiff_on

lemma times_cont_mdiff_iff_times_cont_diff {f : E â†’ E'} :
  times_cont_mdiff ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, E') n f
  â†” times_cont_diff ğ•œ n f :=
by rw [â† times_cont_diff_on_univ, â† times_cont_mdiff_on_univ,
  times_cont_mdiff_on_iff_times_cont_diff_on]

alias times_cont_mdiff_iff_times_cont_diff â†”
  times_cont_mdiff.times_cont_diff times_cont_diff.times_cont_mdiff

end module

/-! ### The tangent map of a smooth function is smooth -/

section tangent_map

/-- If a function is `C^n` with `1 â‰¤ n` on a domain with unique derivatives, then its bundled
derivative is continuous. In this auxiliary lemma, we prove this fact when the source and target
space are model spaces in models with corners. The general fact is proved in
`times_cont_mdiff_on.continuous_on_tangent_map_within`-/
lemma times_cont_mdiff_on.continuous_on_tangent_map_within_aux
  {f : H â†’ H'} {s : set H}
  (hf : times_cont_mdiff_on I I' n f s) (hn : 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  continuous_on (tangent_map_within I I' f s) ((tangent_bundle.proj I H) â»Â¹' s) :=
begin
  suffices h : continuous_on (Î» (p : H Ã— E), (f p.fst,
    (fderiv_within ğ•œ (written_in_ext_chart_at I I' p.fst f) (I.symm â»Â¹' s âˆ© range I)
      ((ext_chart_at I p.fst) p.fst) : E â†’L[ğ•œ] E') p.snd)) (prod.fst â»Â¹' s),
  { have A := (tangent_bundle_model_space_homeomorph H I).continuous,
    rw continuous_iff_continuous_on_univ at A,
    have B := ((tangent_bundle_model_space_homeomorph H' I').symm.continuous.comp_continuous_on h)
      .comp' A,
    have : (univ âˆ© â‡‘(tangent_bundle_model_space_homeomorph H I) â»Â¹' (prod.fst â»Â¹' s)) =
      tangent_bundle.proj I H â»Â¹' s,
      by { ext âŸ¨x, vâŸ©, simp only with mfld_simps },
    rw this at B,
    apply B.congr,
    rintros âŸ¨x, vâŸ© hx,
    dsimp [tangent_map_within],
    ext, { refl },
    simp only with mfld_simps,
    apply congr_fun,
    apply congr_arg,
    rw mdifferentiable_within_at.mfderiv_within (hf.mdifferentiable_on hn x hx),
    refl },
  suffices h : continuous_on (Î» (p : H Ã— E), (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm)
    (I.symm â»Â¹' s âˆ© range I) (I p.fst) : E â†’L[ğ•œ] E') p.snd) (prod.fst â»Â¹' s),
  { dsimp [written_in_ext_chart_at, ext_chart_at],
    apply continuous_on.prod
      (continuous_on.comp hf.continuous_on continuous_fst.continuous_on (subset.refl _)),
    apply h.congr,
    assume p hp,
    refl },
  suffices h : continuous_on (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm)
                     (I.symm â»Â¹' s âˆ© range I)) (I '' s),
  { have C := continuous_on.comp h I.continuous_to_fun.continuous_on (subset.refl _),
    have A : continuous (Î»q : (E â†’L[ğ•œ] E') Ã— E, q.1 q.2) :=
      is_bounded_bilinear_map_apply.continuous,
    have B : continuous_on (Î»p : H Ã— E,
      (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I)
                       (I p.1), p.2)) (prod.fst â»Â¹' s),
    { apply continuous_on.prod _ continuous_snd.continuous_on,
      refine (continuous_on.comp C continuous_fst.continuous_on _ : _),
      exact preimage_mono (subset_preimage_image _ _) },
    exact A.comp_continuous_on B },
  rw times_cont_mdiff_on_iff at hf,
  let x : H := I.symm (0 : E),
  let y : H' := I'.symm (0 : E'),
  have A := hf.2 x y,
  simp only [I.image_eq, inter_comm] with mfld_simps at A âŠ¢,
  apply A.continuous_on_fderiv_within _ hn,
  convert hs.unique_diff_on_target_inter x using 1,
  simp only [inter_comm] with mfld_simps
end

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative is
`C^m` when `m+1 â‰¤ n`. In this auxiliary lemma, we prove this fact when the source and target space
are model spaces in models with corners. The general fact is proved in
`times_cont_mdiff_on.times_cont_mdiff_on_tangent_map_within` -/
lemma times_cont_mdiff_on.times_cont_mdiff_on_tangent_map_within_aux
  {f : H â†’ H'} {s : set H}
  (hf : times_cont_mdiff_on I I' n f s) (hmn : m + 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  times_cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s)
    ((tangent_bundle.proj I H) â»Â¹' s) :=
begin
  have m_le_n : m â‰¤ n,
  { apply le_trans _ hmn,
    have : m + 0 â‰¤ m + 1 := add_le_add_left (zero_le _) _,
    simpa only [add_zero] using this },
  have one_le_n : 1 â‰¤ n,
  { apply le_trans _ hmn,
    change 0 + 1 â‰¤ m + 1,
    exact add_le_add_right (zero_le _) _ },
  have U': unique_diff_on ğ•œ (range I âˆ© I.symm â»Â¹' s),
  { assume y hy,
    simpa only [unique_mdiff_on, unique_mdiff_within_at, hy.1, inter_comm] with mfld_simps
      using hs (I.symm y) hy.2 },
  have U : unique_diff_on ğ•œ (set.prod (range I âˆ© I.symm â»Â¹' s) (univ : set E)) :=
    U'.prod unique_diff_on_univ,
  rw times_cont_mdiff_on_iff,
  refine âŸ¨hf.continuous_on_tangent_map_within_aux one_le_n hs, Î»p q, _âŸ©,
  have A : (range I).prod univ âˆ©
      ((equiv.sigma_equiv_prod H E).symm âˆ˜ Î» (p : E Ã— E), ((I.symm) p.fst, p.snd)) â»Â¹'
        (tangent_bundle.proj I H â»Â¹' s)
      = set.prod (range I âˆ© I.symm â»Â¹' s) univ,
    by { ext âŸ¨x, vâŸ©, simp only with mfld_simps },
  suffices h : times_cont_diff_on ğ•œ m (((Î» (p : H' Ã— E'), (I' p.fst, p.snd)) âˆ˜
      (equiv.sigma_equiv_prod H' E')) âˆ˜ tangent_map_within I I' f s âˆ˜
      ((equiv.sigma_equiv_prod H E).symm) âˆ˜ Î» (p : E Ã— E), (I.symm p.fst, p.snd))
    ((range â‡‘I âˆ© â‡‘(I.symm) â»Â¹' s).prod univ),
    by simpa [A] using h,
  change times_cont_diff_on ğ•œ m (Î» (p : E Ã— E),
    ((I' (f (I.symm p.fst)), ((mfderiv_within I I' f s (I.symm p.fst)) : E â†’ E') p.snd) : E' Ã— E'))
    (set.prod (range I âˆ© I.symm â»Â¹' s) univ),
  -- check that all bits in this formula are `C^n`
  have hf' := times_cont_mdiff_on_iff.1 hf,
  have A : times_cont_diff_on ğ•œ m (I' âˆ˜ f âˆ˜ I.symm) (range I âˆ© I.symm â»Â¹' s) :=
    by simpa only with mfld_simps using (hf'.2 (I.symm 0) (I'.symm 0)).of_le m_le_n,
  have B : times_cont_diff_on ğ•œ m ((I' âˆ˜ f âˆ˜ I.symm) âˆ˜ prod.fst)
           (set.prod (range I âˆ© I.symm â»Â¹' s) (univ : set E)) :=
    A.comp (times_cont_diff_fst.times_cont_diff_on) (prod_subset_preimage_fst _ _),
  suffices C : times_cont_diff_on ğ•œ m (Î» (p : E Ã— E),
    ((fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) p.1 : _) p.2))
    (set.prod (range I âˆ© I.symm â»Â¹' s) univ),
  { apply times_cont_diff_on.prod B _,
    apply C.congr (Î»p hp, _),
    simp only with mfld_simps at hp,
    simp only [mfderiv_within, hf.mdifferentiable_on one_le_n _ hp.2, hp.1, dif_pos]
      with mfld_simps },
  have D : times_cont_diff_on ğ•œ m (Î» x,
    (fderiv_within ğ•œ (I' âˆ˜ f âˆ˜ I.symm) (I.symm â»Â¹' s âˆ© range I) x))
    (range I âˆ© I.symm â»Â¹' s),
  { have : times_cont_diff_on ğ•œ n (I' âˆ˜ f âˆ˜ I.symm) (range I âˆ© I.symm â»Â¹' s) :=
      by simpa only with mfld_simps using (hf'.2 (I.symm 0) (I'.symm 0)),
    simpa only [inter_comm] using this.fderiv_within U' hmn },
  have := D.comp (times_cont_diff_fst.times_cont_diff_on) (prod_subset_preimage_fst _ _),
  have := times_cont_diff_on.prod this (times_cont_diff_snd.times_cont_diff_on),
  exact is_bounded_bilinear_map_apply.times_cont_diff.comp_times_cont_diff_on this,
end

include Is I's

/-- If a function is `C^n` on a domain with unique derivatives, then its bundled derivative
is `C^m` when `m+1 â‰¤ n`. -/
theorem times_cont_mdiff_on.times_cont_mdiff_on_tangent_map_within
  (hf : times_cont_mdiff_on I I' n f s) (hmn : m + 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  times_cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s)
  ((tangent_bundle.proj I M) â»Â¹' s) :=
begin
  /- The strategy of the proof is to avoid unfolding the definitions, and reduce by functoriality
  to the case of functions on the model spaces, where we have already proved the result.
  Let `l` and `r` be the charts to the left and to the right, so that we have
  ```
     l^{-1}      f       r
  H --------> M ---> M' ---> H'
  ```
  Then the tangent map `T(r âˆ˜ f âˆ˜ l)` is smooth by a previous result. Consider the composition
  ```
      Tl        T(r âˆ˜ f âˆ˜ l^{-1})         Tr^{-1}
  TM -----> TH -------------------> TH' ---------> TM'
  ```
  where `Tr^{-1}` and `Tl` are the tangent maps of `r^{-1}` and `l`. Writing `Tl` and `Tr^{-1}` as
  composition of charts (called `Dl` and `il` for `l` and `Dr` and `ir` in the proof below), it
  follows that they are smooth. The composition of all these maps is `Tf`, and is therefore smooth
  as a composition of smooth maps.
  -/
  have m_le_n : m â‰¤ n,
  { apply le_trans _ hmn,
    have : m + 0 â‰¤ m + 1 := add_le_add_left (zero_le _) _,
    simpa only [add_zero] },
  have one_le_n : 1 â‰¤ n,
  { apply le_trans _ hmn,
    change 0 + 1 â‰¤ m + 1,
    exact add_le_add_right (zero_le _) _ },
  /- First step: local reduction on the space, to a set `s'` which is contained in chart domains. -/
  refine times_cont_mdiff_on_of_locally_times_cont_mdiff_on (Î»p hp, _),
  have hf' := times_cont_mdiff_on_iff.1 hf,
  simp [tangent_bundle.proj] at hp,
  let l  := chart_at H p.1,
  set Dl := chart_at (model_prod H E) p with hDl,
  let r  := chart_at H' (f p.1),
  let Dr := chart_at (model_prod H' E') (tangent_map_within I I' f s p),
  let il := chart_at (model_prod H E) (tangent_map I I l p),
  let ir := chart_at (model_prod H' E') (tangent_map I I' (r âˆ˜ f) p),
  let s' := f â»Â¹' r.source âˆ© s âˆ© l.source,
  let s'_lift := (tangent_bundle.proj I M)â»Â¹' s',
  let s'l := l.target âˆ© l.symm â»Â¹' s',
  let s'l_lift := (tangent_bundle.proj I H) â»Â¹' s'l,
  rcases continuous_on_iff'.1 hf'.1 r.source r.open_source with âŸ¨o, o_open, hoâŸ©,
  suffices h : times_cont_mdiff_on I.tangent I'.tangent m (tangent_map_within I I' f s) s'_lift,
  { refine âŸ¨(tangent_bundle.proj I M)â»Â¹' (o âˆ© l.source), _, _, _âŸ©,
    show is_open ((tangent_bundle.proj I M)â»Â¹' (o âˆ© l.source)), from
      (is_open.inter o_open l.open_source).preimage (tangent_bundle_proj_continuous _ _) ,
    show p âˆˆ tangent_bundle.proj I M â»Â¹' (o âˆ© l.source),
    { simp [tangent_bundle.proj] at âŠ¢,
      have : p.1 âˆˆ f â»Â¹' r.source âˆ© s, by simp [hp],
      rw ho at this,
      exact this.1 },
    { have : tangent_bundle.proj I M â»Â¹' s âˆ© tangent_bundle.proj I M â»Â¹' (o âˆ© l.source) = s'_lift,
      { dsimp only [s'_lift, s'], rw [ho], mfld_set_tac },
      rw this,
      exact h } },
  /- Second step: check that all functions are smooth, and use the chain rule to write the bundled
  derivative as a composition of a function between model spaces and of charts.
  Convention: statements about the differentiability of `a âˆ˜ b âˆ˜ c` are named `diff_abc`. Statements
  about differentiability in the bundle have a `_lift` suffix. -/
  have U' : unique_mdiff_on I s',
  { apply unique_mdiff_on.inter _ l.open_source,
    rw [ho, inter_comm],
    exact hs.inter o_open },
  have U'l : unique_mdiff_on I s'l :=
    U'.unique_mdiff_on_preimage (mdifferentiable_chart _ _),
  have diff_f : times_cont_mdiff_on I I' n f s' :=
    hf.mono (by mfld_set_tac),
  have diff_r : times_cont_mdiff_on I' I' n r r.source :=
    times_cont_mdiff_on_chart,
  have diff_rf : times_cont_mdiff_on I I' n (r âˆ˜ f) s',
  { apply times_cont_mdiff_on.comp diff_r diff_f (Î»x hx, _),
    simp only [s'] with mfld_simps at hx, simp only [hx] with mfld_simps },
  have diff_l : times_cont_mdiff_on I I n l.symm s'l,
  { have A : times_cont_mdiff_on I I n l.symm l.target :=
      times_cont_mdiff_on_chart_symm,
    exact A.mono (by mfld_set_tac) },
  have diff_rfl : times_cont_mdiff_on I I' n (r âˆ˜ f âˆ˜ l.symm) s'l,
  { apply times_cont_mdiff_on.comp diff_rf diff_l,
    mfld_set_tac },
  have diff_rfl_lift : times_cont_mdiff_on I.tangent I'.tangent m
      (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l) s'l_lift :=
    diff_rfl.times_cont_mdiff_on_tangent_map_within_aux hmn U'l,
  have diff_irrfl_lift : times_cont_mdiff_on I.tangent I'.tangent m
      (ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l)) s'l_lift,
  { have A : times_cont_mdiff_on I'.tangent I'.tangent m ir ir.source := times_cont_mdiff_on_chart,
    exact times_cont_mdiff_on.comp A diff_rfl_lift (Î»p hp, by simp only [ir] with mfld_simps) },
  have diff_Drirrfl_lift : times_cont_mdiff_on I.tangent I'.tangent m
    (Dr.symm âˆ˜ (ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l))) s'l_lift,
  { have A : times_cont_mdiff_on I'.tangent I'.tangent m Dr.symm Dr.target :=
      times_cont_mdiff_on_chart_symm,
    apply times_cont_mdiff_on.comp A diff_irrfl_lift (Î»p hp, _),
    simp only [s'l_lift, tangent_bundle.proj] with mfld_simps at hp,
    simp only [ir, @local_equiv.refl_coe (model_prod H' E'), hp] with mfld_simps },
  -- conclusion of this step: the composition of all the maps above is smooth
  have diff_DrirrflilDl : times_cont_mdiff_on I.tangent I'.tangent m
    (Dr.symm âˆ˜ (ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l)) âˆ˜
      (il.symm âˆ˜ Dl)) s'_lift,
  { have A : times_cont_mdiff_on I.tangent I.tangent m Dl Dl.source := times_cont_mdiff_on_chart,
    have A' : times_cont_mdiff_on I.tangent I.tangent m Dl s'_lift,
    { apply A.mono (Î»p hp, _),
      simp only [s'_lift, tangent_bundle.proj] with mfld_simps at hp,
      simp only [Dl, hp] with mfld_simps },
    have B : times_cont_mdiff_on I.tangent I.tangent m il.symm il.target :=
      times_cont_mdiff_on_chart_symm,
    have C : times_cont_mdiff_on I.tangent I.tangent m (il.symm âˆ˜ Dl) s'_lift :=
      times_cont_mdiff_on.comp B A' (Î»p hp, by simp only [il] with mfld_simps),
    apply times_cont_mdiff_on.comp diff_Drirrfl_lift C (Î»p hp, _),
    simp only [s'_lift, tangent_bundle.proj] with mfld_simps at hp,
    simp only [il, s'l_lift, hp, tangent_bundle.proj] with mfld_simps },
  /- Third step: check that the composition of all the maps indeed coincides with the derivative we
  are looking for -/
  have eq_comp : âˆ€q âˆˆ s'_lift, tangent_map_within I I' f s q =
      (Dr.symm âˆ˜ ir âˆ˜ (tangent_map_within I I' (r âˆ˜ f âˆ˜ l.symm) s'l) âˆ˜
      (il.symm âˆ˜ Dl)) q,
  { assume q hq,
    simp only [s'_lift, tangent_bundle.proj] with mfld_simps at hq,
    have U'q : unique_mdiff_within_at I s' q.1,
      by { apply U', simp only [hq, s'] with mfld_simps },
    have U'lq : unique_mdiff_within_at I s'l (Dl q).1,
      by { apply U'l, simp only [hq, s'l] with mfld_simps },
    have A : tangent_map_within I I' ((r âˆ˜ f) âˆ˜ l.symm) s'l (il.symm (Dl q)) =
      tangent_map_within I I' (r âˆ˜ f) s' (tangent_map_within I I l.symm s'l (il.symm (Dl q))),
    { refine tangent_map_within_comp_at (il.symm (Dl q)) _ _ (Î»p hp, _) U'lq,
      { apply diff_rf.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { apply diff_l.mdifferentiable_on one_le_n,
        simp only [s'l, hq] with mfld_simps },
      { simp only with mfld_simps at hp, simp only [hp] with mfld_simps } },
    have B : tangent_map_within I I l.symm s'l (il.symm (Dl q)) = q,
    { have : tangent_map_within I I l.symm s'l (il.symm (Dl q))
        = tangent_map I I l.symm (il.symm (Dl q)),
      { refine tangent_map_within_eq_tangent_map U'lq _,
        refine mdifferentiable_at_atlas_symm _ (chart_mem_atlas _ _) _,
        simp only [hq] with mfld_simps },
      rw [this, tangent_map_chart_symm, hDl],
      { simp only [hq] with mfld_simps,
        have : q âˆˆ (chart_at (model_prod H E) p).source, by simp only [hq] with mfld_simps,
        exact (chart_at (model_prod H E) p).left_inv this },
      { simp only [hq] with mfld_simps } },
    have C : tangent_map_within I I' (r âˆ˜ f) s' q
      = tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q),
    { refine tangent_map_within_comp_at q _ _ (Î»r hr, _) U'q,
      { apply diff_r.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { apply diff_f.mdifferentiable_on one_le_n,
        simp only [hq] with mfld_simps },
      { simp only [s'] with mfld_simps at hr,
        simp only [hr] with mfld_simps } },
    have D : Dr.symm (ir (tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q)))
      = tangent_map_within I I' f s' q,
    { have A : tangent_map_within I' I' r r.source (tangent_map_within I I' f s' q) =
             tangent_map I' I' r (tangent_map_within I I' f s' q),
      { apply tangent_map_within_eq_tangent_map,
        { apply is_open.unique_mdiff_within_at _ r.open_source, simp [hq] },
        { refine mdifferentiable_at_atlas _ (chart_mem_atlas _ _) _,
          simp only [hq] with mfld_simps } },
      have : f p.1 = (tangent_map_within I I' f s p).1 := rfl,
      rw [A],
      dsimp [r, Dr],
      rw [this, tangent_map_chart],
      { simp only [hq] with mfld_simps,
        have : tangent_map_within I I' f s' q âˆˆ
          (chart_at (model_prod H' E') (tangent_map_within I I' f s p)).source,
            by simp only [hq] with mfld_simps,
        exact (chart_at (model_prod H' E') (tangent_map_within I I' f s p)).left_inv this },
      { simp only [hq] with mfld_simps } },
    have E : tangent_map_within I I' f s' q = tangent_map_within I I' f s q,
    { refine tangent_map_within_subset (by mfld_set_tac) U'q _,
      apply hf.mdifferentiable_on one_le_n,
      simp only [hq] with mfld_simps },
    simp only [(âˆ˜), A, B, C, D, E.symm] },
  exact diff_DrirrflilDl.congr eq_comp,
end

/-- If a function is `C^n` on a domain with unique derivatives, with `1 â‰¤ n`, then its bundled
derivative is continuous there. -/
theorem times_cont_mdiff_on.continuous_on_tangent_map_within
  (hf : times_cont_mdiff_on I I' n f s) (hmn : 1 â‰¤ n) (hs : unique_mdiff_on I s) :
  continuous_on (tangent_map_within I I' f s) ((tangent_bundle.proj I M) â»Â¹' s) :=
begin
  have : times_cont_mdiff_on I.tangent I'.tangent 0 (tangent_map_within I I' f s)
         ((tangent_bundle.proj I M) â»Â¹' s) :=
    hf.times_cont_mdiff_on_tangent_map_within hmn hs,
  exact this.continuous_on
end

/-- If a function is `C^n`, then its bundled derivative is `C^m` when `m+1 â‰¤ n`. -/
theorem times_cont_mdiff.times_cont_mdiff_tangent_map
  (hf : times_cont_mdiff I I' n f) (hmn : m + 1 â‰¤ n) :
  times_cont_mdiff I.tangent I'.tangent m (tangent_map I I' f) :=
begin
  rw â† times_cont_mdiff_on_univ at hf âŠ¢,
  convert hf.times_cont_mdiff_on_tangent_map_within hmn unique_mdiff_on_univ,
  rw tangent_map_within_univ
end

/-- If a function is `C^n`, with `1 â‰¤ n`, then its bundled derivative is continuous. -/
theorem times_cont_mdiff.continuous_tangent_map
  (hf : times_cont_mdiff I I' n f) (hmn : 1 â‰¤ n) :
  continuous (tangent_map I I' f) :=
begin
  rw â† times_cont_mdiff_on_univ at hf,
  rw continuous_iff_continuous_on_univ,
  convert hf.continuous_on_tangent_map_within hmn unique_mdiff_on_univ,
  rw tangent_map_within_univ
end

end tangent_map

/-! ### Smoothness of the projection in a basic smooth bundle -/

namespace basic_smooth_bundle_core

variables (Z : basic_smooth_bundle_core I M E')

lemma times_cont_mdiff_proj :
  times_cont_mdiff ((I.prod ğ“˜(ğ•œ, E'))) I n
    Z.to_topological_fiber_bundle_core.proj :=
begin
  assume x,
  rw [times_cont_mdiff_at, times_cont_mdiff_within_at_iff],
  refine âŸ¨Z.to_topological_fiber_bundle_core.continuous_proj.continuous_at.continuous_within_at, _âŸ©,
  simp only [(âˆ˜), chart_at, chart] with mfld_simps,
  apply times_cont_diff_within_at_fst.congr,
  { rintros âŸ¨a, bâŸ© hab,
    simp only with mfld_simps at hab,
    simp only [hab] with mfld_simps },
  { simp only with mfld_simps }
end

lemma smooth_proj :
  smooth ((I.prod ğ“˜(ğ•œ, E'))) I Z.to_topological_fiber_bundle_core.proj :=
times_cont_mdiff_proj Z

lemma times_cont_mdiff_on_proj {s : set (Z.to_topological_fiber_bundle_core.total_space)} :
  times_cont_mdiff_on ((I.prod ğ“˜(ğ•œ, E'))) I n
    Z.to_topological_fiber_bundle_core.proj s :=
Z.times_cont_mdiff_proj.times_cont_mdiff_on

lemma smooth_on_proj {s : set (Z.to_topological_fiber_bundle_core.total_space)} :
  smooth_on ((I.prod ğ“˜(ğ•œ, E'))) I Z.to_topological_fiber_bundle_core.proj s :=
times_cont_mdiff_on_proj Z

lemma times_cont_mdiff_at_proj {p : Z.to_topological_fiber_bundle_core.total_space} :
  times_cont_mdiff_at ((I.prod ğ“˜(ğ•œ, E'))) I n
    Z.to_topological_fiber_bundle_core.proj p :=
Z.times_cont_mdiff_proj.times_cont_mdiff_at

lemma smooth_at_proj {p : Z.to_topological_fiber_bundle_core.total_space} :
  smooth_at ((I.prod ğ“˜(ğ•œ, E'))) I Z.to_topological_fiber_bundle_core.proj p :=
Z.times_cont_mdiff_at_proj

lemma times_cont_mdiff_within_at_proj
  {s : set (Z.to_topological_fiber_bundle_core.total_space)}
  {p : Z.to_topological_fiber_bundle_core.total_space} :
  times_cont_mdiff_within_at ((I.prod ğ“˜(ğ•œ, E'))) I n
    Z.to_topological_fiber_bundle_core.proj s p :=
Z.times_cont_mdiff_at_proj.times_cont_mdiff_within_at

lemma smooth_within_at_proj
  {s : set (Z.to_topological_fiber_bundle_core.total_space)}
  {p : Z.to_topological_fiber_bundle_core.total_space} :
  smooth_within_at ((I.prod ğ“˜(ğ•œ, E'))) I
    Z.to_topological_fiber_bundle_core.proj s p :=
Z.times_cont_mdiff_within_at_proj

/-- If an element of `E'` is invariant under all coordinate changes, then one can define a
corresponding section of the fiber bundle, which is smooth. This applies in particular to the
zero section of a vector bundle. Another example (not yet defined) would be the identity
section of the endomorphism bundle of a vector bundle. -/
lemma smooth_const_section (v : E')
  (h : âˆ€ (i j : atlas H M), âˆ€ x âˆˆ i.1.source âˆ© j.1.source, Z.coord_change i j (i.1 x) v = v) :
  smooth I ((I.prod ğ“˜(ğ•œ, E')))
    (show M â†’ Z.to_topological_fiber_bundle_core.total_space, from Î» x, âŸ¨x, vâŸ©) :=
begin
  assume x,
  rw [times_cont_mdiff_at, times_cont_mdiff_within_at_iff],
  split,
  { apply continuous.continuous_within_at,
    apply topological_fiber_bundle_core.continuous_const_section,
    assume i j y hy,
    exact h _ _ _ hy },
  { have : times_cont_diff ğ•œ âŠ¤ (Î» (y : E), (y, v)) := times_cont_diff_id.prod times_cont_diff_const,
    apply this.times_cont_diff_within_at.congr,
    { assume y hy,
      simp only with mfld_simps at hy,
      simp only [chart, hy, chart_at, prod.mk.inj_iff, to_topological_fiber_bundle_core]
        with mfld_simps,
      apply h,
      simp only [hy] with mfld_simps },
    { simp only [chart, chart_at, prod.mk.inj_iff, to_topological_fiber_bundle_core]
        with mfld_simps,
      apply h,
      simp only with mfld_simps } }
end

end basic_smooth_bundle_core

/-! ### Smoothness of the tangent bundle projection -/

namespace tangent_bundle

include Is

lemma times_cont_mdiff_proj :
  times_cont_mdiff I.tangent I n (proj I M) :=
basic_smooth_bundle_core.times_cont_mdiff_proj _

lemma smooth_proj : smooth I.tangent I (proj I M) :=
basic_smooth_bundle_core.smooth_proj _

lemma times_cont_mdiff_on_proj {s : set (tangent_bundle I M)} :
  times_cont_mdiff_on I.tangent I n (proj I M) s :=
basic_smooth_bundle_core.times_cont_mdiff_on_proj _

lemma smooth_on_proj {s : set (tangent_bundle I M)} :
  smooth_on I.tangent I (proj I M) s :=
basic_smooth_bundle_core.smooth_on_proj _

lemma times_cont_mdiff_at_proj {p : tangent_bundle I M} :
  times_cont_mdiff_at I.tangent I n
    (proj I M) p :=
basic_smooth_bundle_core.times_cont_mdiff_at_proj _

lemma smooth_at_proj {p : tangent_bundle I M} :
  smooth_at I.tangent I (proj I M) p :=
basic_smooth_bundle_core.smooth_at_proj _

lemma times_cont_mdiff_within_at_proj
  {s : set (tangent_bundle I M)} {p : tangent_bundle I M} :
  times_cont_mdiff_within_at I.tangent I n
    (proj I M) s p :=
basic_smooth_bundle_core.times_cont_mdiff_within_at_proj _

lemma smooth_within_at_proj
  {s : set (tangent_bundle I M)} {p : tangent_bundle I M} :
  smooth_within_at I.tangent I
    (proj I M) s p :=
basic_smooth_bundle_core.smooth_within_at_proj _

variables (I M)
/-- The zero section of the tangent bundle -/
def zero_section : M â†’ tangent_bundle I M := Î» x, âŸ¨x, 0âŸ©
variables {I M}

lemma smooth_zero_section : smooth I I.tangent (zero_section I M) :=
begin
  apply basic_smooth_bundle_core.smooth_const_section (tangent_bundle_core I M) 0,
  assume i j x hx,
  simp only [tangent_bundle_core, continuous_linear_map.map_zero] with mfld_simps
end

/-- The derivative of the zero section of the tangent bundle maps `âŸ¨x, vâŸ©` to `âŸ¨âŸ¨x, 0âŸ©, âŸ¨v, 0âŸ©âŸ©`.

Note that, as currently framed, this is a statement in coordinates, thus reliant on the choice
of the coordinate system we use on the tangent bundle.

However, the result itself is coordinate-dependent only to the extent that the coordinates
determine a splitting of the tangent bundle.  Moreover, there is a canonical splitting at each
point of the zero section (since there is a canonical horizontal space there, the tangent space
to the zero section, in addition to the canonical vertical space which is the kernel of the
derivative of the projection), and this canonical splitting is also the one that comes from the
coordinates on the tangent bundle in our definitions. So this statement is not as crazy as it
may seem.

TODO define splittings of vector bundles; state this result invariantly. -/
lemma tangent_map_tangent_bundle_pure (p : tangent_bundle I M) :
  tangent_map I I.tangent (tangent_bundle.zero_section I M) p = âŸ¨âŸ¨p.1, 0âŸ©, âŸ¨p.2, 0âŸ©âŸ© :=
begin
  rcases p with âŸ¨x, vâŸ©,
  have N : I.symm â»Â¹' (chart_at H x).target âˆˆ ğ“ (I ((chart_at H x) x)),
  { apply is_open.mem_nhds,
    apply (local_homeomorph.open_target _).preimage I.continuous_inv_fun,
    simp only with mfld_simps },
  have A : mdifferentiable_at I I.tangent (Î» (x : M), (âŸ¨x, 0âŸ© : tangent_bundle I M)) x :=
    tangent_bundle.smooth_zero_section.mdifferentiable_at,
  have B : fderiv_within ğ•œ (Î» (x_1 : E), (x_1, (0 : E))) (set.range â‡‘I) (I ((chart_at H x) x)) v
    = (v, 0),
  { rw [fderiv_within_eq_fderiv, differentiable_at.fderiv_prod],
    { simp },
    { exact differentiable_at_id' },
    { exact differentiable_at_const _ },
    { exact model_with_corners.unique_diff_at_image I },
    { exact differentiable_at_id'.prod (differentiable_at_const _) } },
  simp only [tangent_bundle.zero_section, tangent_map, mfderiv,
    A, dif_pos, chart_at, basic_smooth_bundle_core.chart,
    basic_smooth_bundle_core.to_topological_fiber_bundle_core, tangent_bundle_core,
    function.comp, continuous_linear_map.map_zero] with mfld_simps,
  rw â† fderiv_within_inter N (I.unique_diff (I ((chart_at H x) x)) (set.mem_range_self _)) at B,
  rw [â† fderiv_within_inter N (I.unique_diff (I ((chart_at H x) x)) (set.mem_range_self _)), â† B],
  congr' 1,
  apply fderiv_within_congr _ (Î» y hy, _),
  { simp only with mfld_simps, },
  { apply unique_diff_within_at.inter (I.unique_diff _ _) N,
    simp only with mfld_simps },
  { simp only with mfld_simps at hy,
    simp only [hy] with mfld_simps },
end

end tangent_bundle

/-! ### Smoothness of standard maps associated to the product of manifolds -/

section prod_mk

lemma times_cont_mdiff_within_at.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : times_cont_mdiff_within_at I I' n f s x) (hg : times_cont_mdiff_within_at I J' n g s x) :
  times_cont_mdiff_within_at I (I'.prod J') n (Î» x, (f x, g x)) s x :=
begin
  rw times_cont_mdiff_within_at_iff'' at *,
  exact âŸ¨hf.1.prod hg.1, hf.2.prod hg.2âŸ©,
end

lemma times_cont_mdiff_within_at.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : times_cont_mdiff_within_at I ğ“˜(ğ•œ, E') n f s x)
  (hg : times_cont_mdiff_within_at I ğ“˜(ğ•œ, F') n g s x) :
  times_cont_mdiff_within_at I ğ“˜(ğ•œ, E' Ã— F') n (Î» x, (f x, g x)) s x :=
begin
  rw times_cont_mdiff_within_at_iff'' at *,
  exact âŸ¨hf.1.prod hg.1, hf.2.prod hg.2âŸ©,
end

lemma times_cont_mdiff_at.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : times_cont_mdiff_at I I' n f x) (hg : times_cont_mdiff_at I J' n g x) :
  times_cont_mdiff_at I (I'.prod J') n (Î» x, (f x, g x)) x :=
hf.prod_mk hg

lemma times_cont_mdiff_at.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : times_cont_mdiff_at I ğ“˜(ğ•œ, E') n f x) (hg : times_cont_mdiff_at I ğ“˜(ğ•œ, F') n g x) :
  times_cont_mdiff_at I ğ“˜(ğ•œ, E' Ã— F') n (Î» x, (f x, g x)) x :=
hf.prod_mk_space hg

lemma times_cont_mdiff_on.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : times_cont_mdiff_on I I' n f s) (hg : times_cont_mdiff_on I J' n g s) :
  times_cont_mdiff_on I (I'.prod J') n (Î» x, (f x, g x)) s :=
Î» x hx, (hf x hx).prod_mk (hg x hx)

lemma times_cont_mdiff_on.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : times_cont_mdiff_on I ğ“˜(ğ•œ, E') n f s) (hg : times_cont_mdiff_on I ğ“˜(ğ•œ, F') n g s) :
  times_cont_mdiff_on I ğ“˜(ğ•œ, E' Ã— F') n (Î» x, (f x, g x)) s :=
Î» x hx, (hf x hx).prod_mk_space (hg x hx)

lemma times_cont_mdiff.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : times_cont_mdiff I I' n f) (hg : times_cont_mdiff I J' n g) :
  times_cont_mdiff I (I'.prod J') n (Î» x, (f x, g x)) :=
Î» x, (hf x).prod_mk (hg x)

lemma times_cont_mdiff.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : times_cont_mdiff I ğ“˜(ğ•œ, E') n f) (hg : times_cont_mdiff I ğ“˜(ğ•œ, F') n g) :
  times_cont_mdiff I ğ“˜(ğ•œ, E' Ã— F') n (Î» x, (f x, g x)) :=
Î» x, (hf x).prod_mk_space (hg x)

lemma smooth_within_at.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : smooth_within_at I I' f s x) (hg : smooth_within_at I J' g s x) :
  smooth_within_at I (I'.prod J') (Î» x, (f x, g x)) s x :=
hf.prod_mk hg

lemma smooth_within_at.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : smooth_within_at I ğ“˜(ğ•œ, E') f s x) (hg : smooth_within_at I ğ“˜(ğ•œ, F') g s x) :
  smooth_within_at I ğ“˜(ğ•œ, E' Ã— F') (Î» x, (f x, g x)) s x :=
hf.prod_mk_space hg

lemma smooth_at.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : smooth_at I I' f x) (hg : smooth_at I J' g x) :
  smooth_at I (I'.prod J') (Î» x, (f x, g x)) x :=
hf.prod_mk hg

lemma smooth_at.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : smooth_at I ğ“˜(ğ•œ, E') f x) (hg : smooth_at I ğ“˜(ğ•œ, F') g x) :
  smooth_at I ğ“˜(ğ•œ, E' Ã— F') (Î» x, (f x, g x)) x :=
hf.prod_mk_space hg

lemma smooth_on.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : smooth_on I I' f s) (hg : smooth_on I J' g s) :
  smooth_on I (I'.prod J') (Î» x, (f x, g x)) s :=
hf.prod_mk hg

lemma smooth_on.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : smooth_on I ğ“˜(ğ•œ, E') f s) (hg : smooth_on I ğ“˜(ğ•œ, F') g s) :
  smooth_on I ğ“˜(ğ•œ, E' Ã— F') (Î» x, (f x, g x)) s :=
hf.prod_mk_space hg

lemma smooth.prod_mk {f : M â†’ M'} {g : M â†’ N'}
  (hf : smooth I I' f) (hg : smooth I J' g) :
  smooth I (I'.prod J') (Î» x, (f x, g x)) :=
hf.prod_mk hg

lemma smooth.prod_mk_space {f : M â†’ E'} {g : M â†’ F'}
  (hf : smooth I ğ“˜(ğ•œ, E') f) (hg : smooth I ğ“˜(ğ•œ, F') g) :
  smooth I ğ“˜(ğ•œ, E' Ã— F') (Î» x, (f x, g x)) :=
hf.prod_mk_space hg

end prod_mk

section projections

lemma times_cont_mdiff_within_at_fst {s : set (M Ã— N)} {p : M Ã— N} :
  times_cont_mdiff_within_at (I.prod J) I n prod.fst s p :=
begin
  rw times_cont_mdiff_within_at_iff,
  refine âŸ¨continuous_within_at_fst, _âŸ©,
  refine times_cont_diff_within_at_fst.congr (Î» y hy, _) _,
  { simp only with mfld_simps at hy,
    simp only [hy] with mfld_simps },
  { simp only with mfld_simps }
end

lemma times_cont_mdiff_at_fst {p : M Ã— N} :
  times_cont_mdiff_at (I.prod J) I n prod.fst p :=
times_cont_mdiff_within_at_fst

lemma times_cont_mdiff_on_fst {s : set (M Ã— N)} :
  times_cont_mdiff_on (I.prod J) I n prod.fst s :=
Î» x hx, times_cont_mdiff_within_at_fst

lemma times_cont_mdiff_fst :
  times_cont_mdiff (I.prod J) I n (@prod.fst M N) :=
Î» x, times_cont_mdiff_at_fst

lemma smooth_within_at_fst {s : set (M Ã— N)} {p : M Ã— N} :
  smooth_within_at (I.prod J) I prod.fst s p :=
times_cont_mdiff_within_at_fst

lemma smooth_at_fst {p : M Ã— N} :
  smooth_at (I.prod J) I prod.fst p :=
times_cont_mdiff_at_fst

lemma smooth_on_fst {s : set (M Ã— N)} :
  smooth_on (I.prod J) I prod.fst s :=
times_cont_mdiff_on_fst

lemma smooth_fst :
  smooth (I.prod J) I (@prod.fst M N) :=
times_cont_mdiff_fst

lemma times_cont_mdiff_within_at_snd {s : set (M Ã— N)} {p : M Ã— N} :
  times_cont_mdiff_within_at (I.prod J) J n prod.snd s p :=
begin
  rw times_cont_mdiff_within_at_iff,
  refine âŸ¨continuous_within_at_snd, _âŸ©,
  refine times_cont_diff_within_at_snd.congr (Î» y hy, _) _,
  { simp only with mfld_simps at hy,
    simp only [hy] with mfld_simps },
  { simp only with mfld_simps }
end

lemma times_cont_mdiff_at_snd {p : M Ã— N} :
  times_cont_mdiff_at (I.prod J) J n prod.snd p :=
times_cont_mdiff_within_at_snd

lemma times_cont_mdiff_on_snd {s : set (M Ã— N)} :
  times_cont_mdiff_on (I.prod J) J n prod.snd s :=
Î» x hx, times_cont_mdiff_within_at_snd

lemma times_cont_mdiff_snd :
  times_cont_mdiff (I.prod J) J n (@prod.snd M N) :=
Î» x, times_cont_mdiff_at_snd

lemma smooth_within_at_snd {s : set (M Ã— N)} {p : M Ã— N} :
  smooth_within_at (I.prod J) J prod.snd s p :=
times_cont_mdiff_within_at_snd

lemma smooth_at_snd {p : M Ã— N} :
  smooth_at (I.prod J) J prod.snd p :=
times_cont_mdiff_at_snd

lemma smooth_on_snd {s : set (M Ã— N)} :
  smooth_on (I.prod J) J prod.snd s :=
times_cont_mdiff_on_snd

lemma smooth_snd :
  smooth (I.prod J) J (@prod.snd M N) :=
times_cont_mdiff_snd

lemma smooth_iff_proj_smooth {f : M â†’ M' Ã— N'} :
  (smooth I (I'.prod J') f) â†” (smooth I I' (prod.fst âˆ˜ f)) âˆ§ (smooth I J' (prod.snd âˆ˜ f)) :=
begin
  split,
  { intro h, exact âŸ¨smooth_fst.comp h, smooth_snd.comp hâŸ© },
  { rintro âŸ¨h_fst, h_sndâŸ©, simpa only [prod.mk.eta] using h_fst.prod_mk h_snd, }
end

end projections

section prod_map

variables {g : N â†’ N'} {r : set N} {y : N}

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
lemma times_cont_mdiff_within_at.prod_map' {p : M Ã— N}
  (hf : times_cont_mdiff_within_at I I' n f s p.1)
  (hg : times_cont_mdiff_within_at J J' n g r p.2) :
  times_cont_mdiff_within_at (I.prod J) (I'.prod J') n (prod.map f g) (s.prod r) p :=
(hf.comp p times_cont_mdiff_within_at_fst (prod_subset_preimage_fst _ _)).prod_mk $
hg.comp p times_cont_mdiff_within_at_snd (prod_subset_preimage_snd _ _)

lemma times_cont_mdiff_within_at.prod_map
  (hf : times_cont_mdiff_within_at I I' n f s x) (hg : times_cont_mdiff_within_at J J' n g r y) :
  times_cont_mdiff_within_at (I.prod J) (I'.prod J') n (prod.map f g) (s.prod r) (x, y) :=
times_cont_mdiff_within_at.prod_map' hf hg

lemma times_cont_mdiff_at.prod_map
  (hf : times_cont_mdiff_at I I' n f x) (hg : times_cont_mdiff_at J J' n g y) :
  times_cont_mdiff_at (I.prod J) (I'.prod J') n (prod.map f g) (x, y) :=
begin
  rw â† times_cont_mdiff_within_at_univ at *,
  convert hf.prod_map hg,
  exact univ_prod_univ.symm
end

lemma times_cont_mdiff_at.prod_map' {p : M Ã— N}
  (hf : times_cont_mdiff_at I I' n f p.1) (hg : times_cont_mdiff_at J J' n g p.2) :
  times_cont_mdiff_at (I.prod J) (I'.prod J') n (prod.map f g) p :=
begin
  rcases p,
  exact hf.prod_map hg
end

lemma times_cont_mdiff_on.prod_map
  (hf : times_cont_mdiff_on I I' n f s) (hg : times_cont_mdiff_on J J' n g r) :
  times_cont_mdiff_on (I.prod J) (I'.prod J') n (prod.map f g) (s.prod r) :=
(hf.comp times_cont_mdiff_on_fst (prod_subset_preimage_fst _ _)).prod_mk $
hg.comp (times_cont_mdiff_on_snd) (prod_subset_preimage_snd _ _)

lemma times_cont_mdiff.prod_map
  (hf : times_cont_mdiff I I' n f) (hg : times_cont_mdiff J J' n g) :
  times_cont_mdiff (I.prod J) (I'.prod J') n (prod.map f g) :=
begin
  assume p,
  exact (hf p.1).prod_map' (hg p.2)
end

lemma smooth_within_at.prod_map
  (hf : smooth_within_at I I' f s x) (hg : smooth_within_at J J' g r y) :
  smooth_within_at (I.prod J) (I'.prod J') (prod.map f g) (s.prod r) (x, y) :=
hf.prod_map hg

lemma smooth_at.prod_map
  (hf : smooth_at I I' f x) (hg : smooth_at J J' g y) :
  smooth_at (I.prod J) (I'.prod J') (prod.map f g) (x, y) :=
hf.prod_map hg

lemma smooth_on.prod_map
  (hf : smooth_on I I' f s) (hg : smooth_on J J' g r) :
  smooth_on (I.prod J) (I'.prod J') (prod.map f g) (s.prod r) :=
hf.prod_map hg

lemma smooth.prod_map
  (hf : smooth I I' f) (hg : smooth J J' g) :
  smooth (I.prod J) (I'.prod J') (prod.map f g) :=
hf.prod_map hg

end prod_map

section pi_space

/-!
### Smoothness of functions with codomain `Î  i, F i`

We have no `model_with_corners.pi` yet, so we prove lemmas about functions `f : M â†’ Î  i, F i` and
use `ğ“˜(ğ•œ, Î  i, F i)` as the model space.
-/

variables {Î¹ : Type*} [fintype Î¹] {Fi : Î¹ â†’ Type*} [Î  i, normed_group (Fi i)]
  [Î  i, normed_space ğ•œ (Fi i)] {Ï† : M â†’ Î  i, Fi i}

lemma times_cont_mdiff_within_at_pi_space :
  times_cont_mdiff_within_at I (ğ“˜(ğ•œ, Î  i, Fi i)) n Ï† s x â†”
    âˆ€ i, times_cont_mdiff_within_at I (ğ“˜(ğ•œ, Fi i)) n (Î» x, Ï† x i) s x :=
by simp only [times_cont_mdiff_within_at_iff'', continuous_within_at_pi,
  times_cont_diff_within_at_pi, forall_and_distrib, written_in_ext_chart_at,
  ext_chart_model_space_eq_id, (âˆ˜), local_equiv.refl_coe, id]

lemma times_cont_mdiff_on_pi_space :
  times_cont_mdiff_on I (ğ“˜(ğ•œ, Î  i, Fi i)) n Ï† s â†”
    âˆ€ i, times_cont_mdiff_on I (ğ“˜(ğ•œ, Fi i)) n (Î» x, Ï† x i) s :=
âŸ¨Î» h i x hx, times_cont_mdiff_within_at_pi_space.1 (h x hx) i,
  Î» h x hx, times_cont_mdiff_within_at_pi_space.2 (Î» i, h i x hx)âŸ©

lemma times_cont_mdiff_at_pi_space :
  times_cont_mdiff_at I (ğ“˜(ğ•œ, Î  i, Fi i)) n Ï† x â†”
    âˆ€ i, times_cont_mdiff_at I (ğ“˜(ğ•œ, Fi i)) n (Î» x, Ï† x i) x :=
times_cont_mdiff_within_at_pi_space

lemma times_cont_mdiff_pi_space :
  times_cont_mdiff I (ğ“˜(ğ•œ, Î  i, Fi i)) n Ï† â†”
    âˆ€ i, times_cont_mdiff I (ğ“˜(ğ•œ, Fi i)) n (Î» x, Ï† x i) :=
âŸ¨Î» h i x, times_cont_mdiff_at_pi_space.1 (h x) i,
  Î» h x, times_cont_mdiff_at_pi_space.2 (Î» i, h i x)âŸ©

lemma smooth_within_at_pi_space :
  smooth_within_at I (ğ“˜(ğ•œ, Î  i, Fi i)) Ï† s x â†”
    âˆ€ i, smooth_within_at I (ğ“˜(ğ•œ, Fi i)) (Î» x, Ï† x i) s x :=
times_cont_mdiff_within_at_pi_space

lemma smooth_on_pi_space :
  smooth_on I (ğ“˜(ğ•œ, Î  i, Fi i)) Ï† s â†” âˆ€ i, smooth_on I (ğ“˜(ğ•œ, Fi i)) (Î» x, Ï† x i) s :=
times_cont_mdiff_on_pi_space

lemma smooth_at_pi_space :
  smooth_at I (ğ“˜(ğ•œ, Î  i, Fi i)) Ï† x â†” âˆ€ i, smooth_at I (ğ“˜(ğ•œ, Fi i)) (Î» x, Ï† x i) x :=
times_cont_mdiff_at_pi_space

lemma smooth_pi_space :
  smooth I (ğ“˜(ğ•œ, Î  i, Fi i)) Ï† â†” âˆ€ i, smooth I (ğ“˜(ğ•œ, Fi i)) (Î» x, Ï† x i) :=
times_cont_mdiff_pi_space

end pi_space

/-! ### Linear maps between normed spaces are smooth -/

lemma continuous_linear_map.times_cont_mdiff (L : E â†’L[ğ•œ] F) :
  times_cont_mdiff ğ“˜(ğ•œ, E) ğ“˜(ğ•œ, F) n L :=
L.times_cont_diff.times_cont_mdiff

/-! ### Smoothness of standard operations -/

variables {V : Type*} [normed_group V] [normed_space ğ•œ V]

/-- On any vector space, multiplication by a scalar is a smooth operation. -/
lemma smooth_smul : smooth (ğ“˜(ğ•œ).prod ğ“˜(ğ•œ, V)) ğ“˜(ğ•œ, V) (Î»p : ğ•œ Ã— V, p.1 â€¢ p.2) :=
smooth_iff.2 âŸ¨continuous_smul, Î» x y, times_cont_diff_smul.times_cont_diff_onâŸ©

lemma smooth.smul {N : Type*} [topological_space N] [charted_space H N]
  {f : N â†’ ğ•œ} {g : N â†’ V} (hf : smooth I ğ“˜(ğ•œ) f) (hg : smooth I ğ“˜(ğ•œ, V) g) :
  smooth I ğ“˜(ğ•œ, V) (Î» p, f p â€¢ g p) :=
smooth_smul.comp (hf.prod_mk hg)

lemma smooth_on.smul {N : Type*} [topological_space N] [charted_space H N]
  {f : N â†’ ğ•œ} {g : N â†’ V} {s : set N} (hf : smooth_on I ğ“˜(ğ•œ) f s) (hg : smooth_on I ğ“˜(ğ•œ, V) g s) :
  smooth_on I ğ“˜(ğ•œ, V) (Î» p, f p â€¢ g p) s :=
smooth_smul.comp_smooth_on (hf.prod_mk hg)

lemma smooth_at.smul {N : Type*} [topological_space N] [charted_space H N]
  {f : N â†’ ğ•œ} {g : N â†’ V} {x : N} (hf : smooth_at I ğ“˜(ğ•œ) f x) (hg : smooth_at I ğ“˜(ğ•œ, V) g x) :
  smooth_at I ğ“˜(ğ•œ, V) (Î» p, f p â€¢ g p) x :=
smooth_smul.smooth_at.comp _ (hf.prod_mk hg)
