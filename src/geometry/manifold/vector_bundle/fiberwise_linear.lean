/-
Copyright (c) 2022 Floris van Doorn, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import geometry.manifold.cont_mdiff

/-! # The groupoid of smooth, fiberwise-linear maps

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains preliminaries for the definition of a smooth vector bundle: an associated
`structure_groupoid`, the groupoid of `smooth_fiberwise_linear` functions.
-/

noncomputable theory

open set topological_space
open_locale manifold topology

/-! ### The groupoid of smooth, fiberwise-linear maps -/

variables {ğ•œ B F : Type*} [topological_space B]
variables [nontrivially_normed_field ğ•œ] [normed_add_comm_group F] [normed_space ğ•œ F]

namespace fiberwise_linear

variables {Ï† Ï†' : B â†’ F â‰ƒL[ğ•œ] F} {U U' : set B}

/-- For `B` a topological space and `F` a `ğ•œ`-normed space, a map from `U : set B` to `F â‰ƒL[ğ•œ] F`
determines a local homeomorphism from `B Ã— F` to itself by its action fiberwise. -/
def local_homeomorph (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (hU : is_open U)
  (hÏ† : continuous_on (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : continuous_on (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U) :
  local_homeomorph (B Ã— F) (B Ã— F) :=
{ to_fun := Î» x, (x.1, Ï† x.1 x.2),
  inv_fun := Î» x, (x.1, (Ï† x.1).symm x.2),
  source := U Ã—Ë¢ univ,
  target := U Ã—Ë¢ univ,
  map_source' := Î» x hx, mk_mem_prod hx.1 (mem_univ _),
  map_target' := Î» x hx, mk_mem_prod hx.1 (mem_univ _),
  left_inv' := Î» x _, prod.ext rfl (continuous_linear_equiv.symm_apply_apply _ _),
  right_inv' := Î» x _, prod.ext rfl (continuous_linear_equiv.apply_symm_apply _ _),
  open_source := hU.prod is_open_univ,
  open_target := hU.prod is_open_univ,
  continuous_to_fun := begin
    have : continuous_on (Î» p : B Ã— F, ((Ï† p.1 : F â†’L[ğ•œ] F), p.2)) (U Ã—Ë¢ univ),
    { exact hÏ†.prod_map continuous_on_id },
    exact continuous_on_fst.prod (is_bounded_bilinear_map_apply.continuous.comp_continuous_on this),
  end,
  continuous_inv_fun := begin
    have : continuous_on (Î» p : B Ã— F, (((Ï† p.1).symm : F â†’L[ğ•œ] F), p.2)) (U Ã—Ë¢ univ),
    { exact h2Ï†.prod_map continuous_on_id },
    exact continuous_on_fst.prod (is_bounded_bilinear_map_apply.continuous.comp_continuous_on this),
  end, }

/-- Compute the composition of two local homeomorphisms induced by fiberwise linear
equivalences. -/
lemma trans_local_homeomorph_apply
  (hU : is_open U)
  (hÏ† : continuous_on (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : continuous_on (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U)
  (hU' : is_open U')
  (hÏ†' : continuous_on (Î» x, Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
  (h2Ï†' : continuous_on (Î» x, (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U')
  (b : B) (v : F) :
  (fiberwise_linear.local_homeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
    fiberwise_linear.local_homeomorph Ï†' hU' hÏ†' h2Ï†') âŸ¨b, vâŸ© = âŸ¨b, Ï†' b (Ï† b v)âŸ© :=
rfl

/-- Compute the source of the composition of two local homeomorphisms induced by fiberwise linear
equivalences. -/
lemma source_trans_local_homeomorph
  (hU : is_open U)
  (hÏ† : continuous_on (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : continuous_on (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U)
  (hU' : is_open U')
  (hÏ†' : continuous_on (Î» x, Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
  (h2Ï†' : continuous_on (Î» x, (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U') :
  (fiberwise_linear.local_homeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
    fiberwise_linear.local_homeomorph Ï†' hU' hÏ†' h2Ï†').source = (U âˆ© U') Ã—Ë¢ univ :=
by { dsimp only [fiberwise_linear.local_homeomorph], mfld_set_tac }

/-- Compute the target of the composition of two local homeomorphisms induced by fiberwise linear
equivalences. -/
lemma target_trans_local_homeomorph
  (hU : is_open U)
  (hÏ† : continuous_on (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : continuous_on (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U)
  (hU' : is_open U')
  (hÏ†' : continuous_on (Î» x, Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
  (h2Ï†' : continuous_on (Î» x, (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U') :
  (fiberwise_linear.local_homeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
    fiberwise_linear.local_homeomorph Ï†' hU' hÏ†' h2Ï†').target = (U âˆ© U') Ã—Ë¢ univ :=
by { dsimp only [fiberwise_linear.local_homeomorph], mfld_set_tac }

end fiberwise_linear

variables {EB : Type*} [normed_add_comm_group EB] [normed_space ğ•œ EB]
  {HB : Type*} [topological_space HB] [charted_space HB B] {IB : model_with_corners ğ•œ EB HB}

/-- Let `e` be a local homeomorphism of `B Ã— F`.  Suppose that at every point `p` in the source of
`e`, there is some neighbourhood `s` of `p` on which `e` is equal to a bi-smooth fiberwise linear
local homeomorphism.
Then the source of `e` is of the form `U Ã—Ë¢ univ`, for some set `U` in `B`, and, at any point `x` in
`U`, admits a neighbourhood `u` of `x` such that `e` is equal on `u Ã—Ë¢ univ` to some bi-smooth
fiberwise linear local homeomorphism. -/
lemma smooth_fiberwise_linear.locality_auxâ‚ (e : local_homeomorph (B Ã— F) (B Ã— F))
  (h : âˆ€ p âˆˆ e.source, âˆƒ s : set (B Ã— F), is_open s âˆ§ p âˆˆ s âˆ§
    âˆƒ (Ï† : B â†’ (F â‰ƒL[ğ•œ] F)) (u : set B) (hu : is_open u)
      (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x : F â†’L[ğ•œ] F)) u)
      (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
      (e.restr s).eq_on_source
        (fiberwise_linear.local_homeomorph Ï† hu hÏ†.continuous_on h2Ï†.continuous_on)) :
  âˆƒ (U : set B) (hU : e.source = U Ã—Ë¢ univ),
  âˆ€ x âˆˆ U, âˆƒ (Ï† : B â†’ (F â‰ƒL[ğ•œ] F)) (u : set B) (hu : is_open u) (huU : u âŠ† U) (hux : x âˆˆ u)
    (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x : F â†’L[ğ•œ] F)) u)
    (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
    (e.restr (u Ã—Ë¢ univ)).eq_on_source
      (fiberwise_linear.local_homeomorph Ï† hu hÏ†.continuous_on h2Ï†.continuous_on) :=
begin
  rw [set_coe.forall'] at h,
  -- choose s hs hsp Ï† u hu hÏ† h2Ï† heÏ† using h,
  -- the following 2 lines should be `choose s hs hsp Ï† u hu hÏ† h2Ï† heÏ† using h,`
  -- `choose` produces a proof term that takes a long time to type-check by the kernel (it seems)
  -- porting note: todo: try using `choose` again in Lean 4
  simp only [classical.skolem, â† exists_prop] at h,
  rcases h with âŸ¨s, hs, hsp, Ï†, u, hu, hÏ†, h2Ï†, heÏ†âŸ©,
  have hesu : âˆ€ p : e.source, e.source âˆ© s p = u p Ã—Ë¢ univ,
  { intros p,
    rw â† e.restr_source' (s _) (hs _),
    exact (heÏ† p).1 },
  have hu' : âˆ€ p : e.source, (p : B Ã— F).fst âˆˆ u p,
  { intros p,
    have : (p : B Ã— F) âˆˆ e.source âˆ© s p := âŸ¨p.prop, hsp pâŸ©,
    simpa only [hesu, mem_prod, mem_univ, and_true] using this },
  have heu : âˆ€ p : e.source, âˆ€ q : B Ã— F, q.fst âˆˆ u p â†’ q âˆˆ e.source,
  { intros p q hq,
    have : q âˆˆ u p Ã—Ë¢ (univ : set F) := âŸ¨hq, trivialâŸ©,
    rw â† hesu p at this,
    exact this.1 },
  have he : e.source = (prod.fst '' e.source) Ã—Ë¢ (univ : set F),
  { apply has_subset.subset.antisymm,
    { intros p hp,
      exact âŸ¨âŸ¨p, hp, rflâŸ©, trivialâŸ© },
    { rintros âŸ¨x, vâŸ© âŸ¨âŸ¨p, hp, rfl : p.fst = xâŸ©, -âŸ©,
      exact heu âŸ¨p, hpâŸ© (p.fst, v) (hu' âŸ¨p, hpâŸ©) } },
  refine âŸ¨prod.fst '' e.source, he, _âŸ©,
  rintros x âŸ¨p, hp, rflâŸ©,
  refine âŸ¨Ï† âŸ¨p, hpâŸ©, u âŸ¨p, hpâŸ©, hu âŸ¨p, hpâŸ©, _, hu' _, hÏ† âŸ¨p, hpâŸ©, h2Ï† âŸ¨p, hpâŸ©, _âŸ©,
  { intros y hy, refine âŸ¨(y, 0), heu âŸ¨p, hpâŸ© âŸ¨_, _âŸ© hy, rflâŸ© },
  { rw [â† hesu, e.restr_source_inter], exact heÏ† âŸ¨p, hpâŸ© },
end

/-- Let `e` be a local homeomorphism of `B Ã— F` whose source is `U Ã—Ë¢ univ`, for some set `U` in
`B`, and which, at any point `x` in `U`, admits a neighbourhood `u` of `x` such that `e` is equal on
`u Ã—Ë¢ univ` to some bi-smooth fiberwise linear local homeomorphism.  Then `e` itself is equal to
some bi-smooth fiberwise linear local homeomorphism.

This is the key mathematical point of the `locality` condition in the construction of the
`structure_groupoid` of bi-smooth fiberwise linear local homeomorphisms.  The proof is by gluing
together the various bi-smooth fiberwise linear local homeomorphism which exist locally.

The `U` in the conclusion is the same `U` as in the hypothesis. We state it like this, because this
is exactly what we need for `smooth_fiberwise_linear`. -/
lemma smooth_fiberwise_linear.locality_auxâ‚‚ (e : local_homeomorph (B Ã— F) (B Ã— F))
  (U : set B) (hU : e.source = U Ã—Ë¢ univ)
  (h : âˆ€ x âˆˆ U, âˆƒ (Ï† : B â†’ (F â‰ƒL[ğ•œ] F)) (u : set B) (hu : is_open u) (hUu : u âŠ† U) (hux : x âˆˆ u)
    (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x : F â†’L[ğ•œ] F)) u)
    (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
    (e.restr (u Ã—Ë¢ univ)).eq_on_source
      (fiberwise_linear.local_homeomorph Ï† hu hÏ†.continuous_on h2Ï†.continuous_on)) :
  âˆƒ (Î¦ : B â†’ (F â‰ƒL[ğ•œ] F)) (U : set B) (hUâ‚€ : is_open U)
    (hÎ¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Î¦ x : F â†’L[ğ•œ] F)) U)
    (h2Î¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Î¦ x).symm : F â†’L[ğ•œ] F)) U),
    e.eq_on_source (fiberwise_linear.local_homeomorph Î¦ hUâ‚€ hÎ¦.continuous_on h2Î¦.continuous_on) :=
begin
  classical,
  rw set_coe.forall' at h,
  choose! Ï† u hu hUu hux hÏ† h2Ï† heÏ† using h,
  have heuÏ† : âˆ€ x : U, eq_on e (Î» q, (q.1, Ï† x q.1 q.2)) (u x Ã—Ë¢ univ),
  { intros x p hp,
    refine (heÏ† x).2 _,
    rw (heÏ† x).1,
    exact hp },
  have huÏ† : âˆ€ (x x' : U) (y : B) (hyx : y âˆˆ u x) (hyx' : y âˆˆ u x'), Ï† x y = Ï† x' y,
  { intros p p' y hyp hyp',
    ext v,
    have h1 : e (y, v) = (y, Ï† p y v) := heuÏ† _ âŸ¨(id hyp : (y, v).fst âˆˆ u p), trivialâŸ©,
    have h2 : e (y, v) = (y, Ï† p' y v) := heuÏ† _ âŸ¨(id hyp' : (y, v).fst âˆˆ u p'), trivialâŸ©,
    exact congr_arg prod.snd (h1.symm.trans h2) },
  have hUu' : U = â‹ƒ i, u i,
  { ext x,
    rw mem_Union,
    refine âŸ¨Î» h, âŸ¨âŸ¨x, hâŸ©, hux _âŸ©, _âŸ©,
    rintros âŸ¨x, hxâŸ©,
    exact hUu x hx },
  have hU' : is_open U,
  { rw hUu',
    apply is_open_Union hu },
  let Î¦â‚€ : U â†’ F â‰ƒL[ğ•œ] F := Union_lift u (Î» x, (Ï† x) âˆ˜ coe) huÏ† U hUu'.le,
  let Î¦ : B â†’ F â‰ƒL[ğ•œ] F := Î» y, if hy : y âˆˆ U then Î¦â‚€ âŸ¨y, hyâŸ© else continuous_linear_equiv.refl ğ•œ F,
  have hÎ¦ : âˆ€ (y) (hy : y âˆˆ U), Î¦ y = Î¦â‚€ âŸ¨y, hyâŸ© := Î» y hy, dif_pos hy,
  have hÎ¦Ï† : âˆ€ x : U, âˆ€ y âˆˆ u x, Î¦ y = Ï† x y,
  { intros x y hyu,
    refine (hÎ¦ y (hUu x hyu)).trans _,
    exact Union_lift_mk âŸ¨y, hyuâŸ© _ },
  have hÎ¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» y, (Î¦ y : F â†’L[ğ•œ] F)) U,
  { apply cont_mdiff_on_of_locally_cont_mdiff_on,
    intros x hx,
    refine âŸ¨u âŸ¨x, hxâŸ©, hu âŸ¨x, hxâŸ©, hux _, _âŸ©,
    refine (cont_mdiff_on.congr (hÏ† âŸ¨x, hxâŸ©) _).mono (inter_subset_right _ _),
    intros y hy,
    rw hÎ¦Ï† âŸ¨x, hxâŸ© y hy },
  have h2Î¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» y, ((Î¦ y).symm : F â†’L[ğ•œ] F)) U,
  { apply cont_mdiff_on_of_locally_cont_mdiff_on,
    intros x hx,
    refine âŸ¨u âŸ¨x, hxâŸ©, hu âŸ¨x, hxâŸ©, hux _, _âŸ©,
    refine (cont_mdiff_on.congr (h2Ï† âŸ¨x, hxâŸ©) _).mono (inter_subset_right _ _),
    intros y hy,
    rw hÎ¦Ï† âŸ¨x, hxâŸ© y hy },
  refine âŸ¨Î¦, U, hU', hÎ¦, h2Î¦, hU, Î» p hp, _âŸ©,
  rw [hU] at hp,
  -- using rw on the next line seems to cause a timeout in kernel type-checking
  refine (heuÏ† âŸ¨p.fst, hp.1âŸ© âŸ¨hux _, hp.2âŸ©).trans _,
  congrm (_, _),
  rw hÎ¦Ï†,
  apply hux
end

variables (F B IB)
/-- For `B` a manifold and `F` a normed space, the groupoid on `B Ã— F` consisting of local
homeomorphisms which are bi-smooth and fiberwise linear, and induce the identity on `B`.
When a (topological) vector bundle is smooth, then the composition of charts associated
to the vector bundle belong to this groupoid. -/
def smooth_fiberwise_linear : structure_groupoid (B Ã— F) :=
{ members := â‹ƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (U : set B) (hU : is_open U)
  (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U),
  {e | e.eq_on_source (fiberwise_linear.local_homeomorph Ï† hU hÏ†.continuous_on h2Ï†.continuous_on)},
  trans' := begin
    simp_rw [mem_Union],
    rintros e e' âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ© âŸ¨Ï†', U', hU', hÏ†', h2Ï†', heÏ†'âŸ©,
    refine âŸ¨Î» b, (Ï† b).trans (Ï†' b), _, hU.inter hU', _, _, setoid.trans (heÏ†.trans' heÏ†') âŸ¨_, _âŸ©âŸ©,
    { show smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F)
        (Î» (x : B), (Ï†' x).to_continuous_linear_map âˆ˜L (Ï† x).to_continuous_linear_map) (U âˆ© U'),
      exact (hÏ†'.mono $ inter_subset_right _ _).clm_comp (hÏ†.mono $ inter_subset_left _ _) },
    { show smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» (x : B),
        (Ï† x).symm.to_continuous_linear_map âˆ˜L (Ï†' x).symm.to_continuous_linear_map) (U âˆ© U'),
      exact (h2Ï†.mono $ inter_subset_left _ _).clm_comp (h2Ï†'.mono $ inter_subset_right _ _) },
    { apply fiberwise_linear.source_trans_local_homeomorph },
    { rintros âŸ¨b, vâŸ© hb, apply fiberwise_linear.trans_local_homeomorph_apply }
  end,
  symm' := begin
    simp_rw [mem_Union],
    rintros e âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ©,
    refine âŸ¨Î» b, (Ï† b).symm, U, hU, h2Ï†, _, heÏ†.symm'âŸ©,
    simp_rw continuous_linear_equiv.symm_symm,
    exact hÏ†
  end,
  id_mem' := begin
    simp_rw [mem_Union],
    refine âŸ¨Î» b, continuous_linear_equiv.refl ğ•œ F, univ, is_open_univ, _, _, âŸ¨_, Î» b hb, _âŸ©âŸ©,
    { apply cont_mdiff_on_const },
    { apply cont_mdiff_on_const },
    { simp only [fiberwise_linear.local_homeomorph, local_homeomorph.refl_local_equiv,
        local_equiv.refl_source, univ_prod_univ] },
    { simp only [fiberwise_linear.local_homeomorph, local_homeomorph.refl_apply, prod.mk.eta,
        id.def, continuous_linear_equiv.coe_refl', local_homeomorph.mk_coe, local_equiv.coe_mk] },
  end,
  locality' := begin -- the hard work has been extracted to `locality_auxâ‚` and `locality_auxâ‚‚`
    simp_rw [mem_Union],
    intros e he,
    obtain âŸ¨U, hU, hâŸ© := smooth_fiberwise_linear.locality_auxâ‚ e he,
    exact smooth_fiberwise_linear.locality_auxâ‚‚ e U hU h,
  end,
  eq_on_source' := begin
    simp_rw [mem_Union],
    rintros e e' âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ© hee',
    exact âŸ¨Ï†, U, hU, hÏ†, h2Ï†, setoid.trans hee' heÏ†âŸ©,
  end }

@[simp] lemma mem_smooth_fiberwise_linear_iff (e : local_homeomorph (B Ã— F) (B Ã— F)) :
  e âˆˆ smooth_fiberwise_linear B F IB â†”
  âˆƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (U : set B) (hU : is_open U)
  (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U),
  e.eq_on_source (fiberwise_linear.local_homeomorph Ï† hU hÏ†.continuous_on h2Ï†.continuous_on) :=
show e âˆˆ set.Union _ â†” _, by { simp only [mem_Union], refl }
