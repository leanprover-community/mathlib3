/-
Copyright (c) 2022 Floris van Doorn, Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn, Heather Macbeth
-/
import geometry.manifold.cont_mdiff

/-! # The groupoid of smooth, fibrewise-linear maps

This file contains preliminaries for the definition of a smooth vector bundle: an associated
`structure_groupoid`, the groupoid of `smooth_fibrewise_linear` functions.
-/

noncomputable theory

open set topological_space
open_locale manifold topological_space


variables {ğ•œ B F : Type*} [topological_space B]
variables [nontrivially_normed_field ğ•œ] [normed_add_comm_group F] [normed_space ğ•œ F]

/-- For `B` a topological space and `F` a `ğ•œ`-normed space, a map from `U : set B` to `F â‰ƒL[ğ•œ] F`
determines a local homeomorphism from `B Ã— F` to itself by its action fibrewise. -/
def fiberwise_linear.local_homeomorph (Ï† : B â†’ F â‰ƒL[ğ•œ] F) {U : set B} (hU : is_open U)
  (hÏ† : continuous_on (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : continuous_on (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U) :
  local_homeomorph (B Ã— F) (B Ã— F) :=
{ to_fun := Î» x, (x.1, Ï† x.1 x.2),
  inv_fun := Î» x, (x.1, (Ï† x.1).symm x.2),
  source := U Ã—Ë¢ univ,
  target := U Ã—Ë¢ univ,
  map_source' := Î» x hx, mk_mem_prod hx.1 (mem_univ _),
  map_target' := Î» x hx, mk_mem_prod hx.1 (mem_univ _),
  left_inv' := Î» x _, prod.ext rfl (continuous_linear_equiv.symm_apply_apply _ _),
  right_inv' := Î» x _, prod.ext rfl (continuous_linear_equiv.apply_symm_apply _ _),
  open_source := hU.prod is_open_univ,
  open_target := hU.prod is_open_univ,
  continuous_to_fun := begin
    have : continuous_on (Î» p : B Ã— F, ((Ï† p.1 : F â†’L[ğ•œ] F), p.2)) (U Ã—Ë¢ univ),
    { exact hÏ†.prod_map continuous_on_id },
    exact continuous_on_fst.prod (is_bounded_bilinear_map_apply.continuous.comp_continuous_on this),
  end,
  continuous_inv_fun := begin
    have : continuous_on (Î» p : B Ã— F, (((Ï† p.1).symm : F â†’L[ğ•œ] F), p.2)) (U Ã—Ë¢ univ),
    { exact h2Ï†.prod_map continuous_on_id },
    exact continuous_on_fst.prod (is_bounded_bilinear_map_apply.continuous.comp_continuous_on this),
  end, }


lemma fiberwise_linear.source_trans_local_homeomorph {Ï† : B â†’ (F â‰ƒL[ğ•œ] F)}
  {U : set B}
  (hU : is_open U)
  (hÏ† : continuous_on (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : continuous_on (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U)
  {Ï†' : B â†’ (F â‰ƒL[ğ•œ] F)}
  {U' : set B}
  (hU' : is_open U')
  (hÏ†' : continuous_on (Î» x, Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
  (h2Ï†' : continuous_on (Î» x, (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U') :
  (fiberwise_linear.local_homeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
      fiberwise_linear.local_homeomorph Ï†' hU' hÏ†' h2Ï†').source = (U âˆ© U') Ã—Ë¢ univ :=
begin
  dsimp [fiberwise_linear.local_homeomorph],
  mfld_set_tac,
end

lemma fiberwise_linear.trans_local_homeomorph_apply {Ï† : B â†’ (F â‰ƒL[ğ•œ] F)}
  {U : set B}
  (hU : is_open U)
  (hÏ† : continuous_on (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : continuous_on (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U)
  {Ï†' : B â†’ (F â‰ƒL[ğ•œ] F)}
  {U' : set B}
  (hU' : is_open U')
  (hÏ†' : continuous_on (Î» x, Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
  (h2Ï†' : continuous_on (Î» x, (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U')
  {b : B}
  (v : F) :
  (fiberwise_linear.local_homeomorph Ï† hU hÏ† h2Ï† â‰«â‚•
      fiberwise_linear.local_homeomorph Ï†' hU' hÏ†' h2Ï†') âŸ¨b, vâŸ© = âŸ¨b, Ï†' b (Ï† b v)âŸ© :=
rfl

variables {EB : Type*} [normed_add_comm_group EB] [normed_space ğ•œ EB]
  {HB : Type*} [topological_space HB] [charted_space HB B] {IB : model_with_corners ğ•œ EB HB}

lemma smooth_fibrewise_linear.locality_auxâ‚‚ {e : local_homeomorph (B Ã— F) (B Ã— F)}
  {U : set B}
  (h : âˆ€ x âˆˆ U, âˆƒ (Ï† : B â†’ (F â‰ƒL[ğ•œ] F)) (u : set B) (hu : is_open u) (hUu : u âŠ† U) (hux : x âˆˆ u)
    (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x : F â†’L[ğ•œ] F)) u)
    (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
    (e.restr (u Ã—Ë¢ univ)).eq_on_source
      (fiberwise_linear.local_homeomorph Ï† hu hÏ†.continuous_on h2Ï†.continuous_on)) :
  âˆƒ (Ï† : U â†’ B â†’ (F â‰ƒL[ğ•œ] F))
  (u : U â†’ set B)
  (hu : âˆ€ x, is_open (u x))
  (hÏ† : âˆ€ x, smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» y, (Ï† x y : F â†’L[ğ•œ] F)) (u x))
  (h2Ï† : âˆ€ x, smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» (y : B), ((Ï† x y).symm : F â†’L[ğ•œ] F)) (u x)),
  (âˆ€ x, u x âŠ† U) âˆ§
  (âˆ€ x, â†‘x âˆˆ u x) âˆ§
  (âˆ€ x, eq_on e (Î» q, (q.1, Ï† x q.1 q.2)) (u x Ã—Ë¢ univ)) :=
begin
  rw set_coe.forall' at h,
  choose! Ï† u hu hUu hux hÏ† h2Ï† heÏ† using h,
  have heuÏ† : âˆ€ x : U, eq_on e (Î» q, (q.1, Ï† x q.1 q.2)) (u x Ã—Ë¢ univ),
  { intros x p hp,
    refine (heÏ† x).2 _,
    rw (heÏ† x).1,
    exact hp },
  exact âŸ¨Ï†, u, hu, hÏ†, h2Ï†, hUu, hux, heuÏ†âŸ©
end

lemma smooth_fibrewise_linear.locality_auxâ‚ƒ {e : local_homeomorph (B Ã— F) (B Ã— F)}
  {U : set B}
  (hU : e.source = U Ã—Ë¢ univ)
  {Ï† : U â†’ B â†’ (F â‰ƒL[ğ•œ] F)}
  {u : U â†’ set B}
  (hUu : âˆ€ x, u x âŠ† U)
  (hux : âˆ€ x, â†‘x âˆˆ u x)
  (heuÏ† : âˆ€ x, eq_on e (Î» q, (q.1, Ï† x q.1 q.2)) (u x Ã—Ë¢ univ)) :
  (U = â‹ƒ i, u i) âˆ§
  âˆƒ (Î¦ : B â†’ (F â‰ƒL[ğ•œ] F)),
    (âˆ€ x : U, âˆ€ y âˆˆ u x, Î¦ y = Ï† x y) âˆ§
    eq_on e (Î» x, (x.1, Î¦ x.1 x.2)) e.source :=
begin
  classical,
  have huÏ† : âˆ€ (x x' : U) (y : B) (hyx : y âˆˆ u x) (hyx' : y âˆˆ u x'), Ï† x y = Ï† x' y,
  { intros p p' y hyp hyp',
    ext v,
    have h1 : e (y, v) = (y, Ï† p y v) := heuÏ† _ âŸ¨(id hyp : (y, v).fst âˆˆ u p), trivialâŸ©,
    have h2 : e (y, v) = (y, Ï† p' y v) := heuÏ† _ âŸ¨(id hyp' : (y, v).fst âˆˆ u p'), trivialâŸ©,
    exact congr_arg prod.snd (h1.symm.trans h2) },
  have hUu' : U = â‹ƒ i, u i,
  { ext x,
    rw mem_Union,
    refine âŸ¨Î» h, âŸ¨âŸ¨x, hâŸ©, hux _âŸ©, _âŸ©,
    rintros âŸ¨x, hxâŸ©,
    exact hUu x hx },
  let Î¦â‚€ : U â†’ F â‰ƒL[ğ•œ] F := Union_lift u (Î» x, (Ï† x) âˆ˜ coe) huÏ† U hUu'.le,
  let Î¦ : B â†’ F â‰ƒL[ğ•œ] F := Î» y, if hy : y âˆˆ U then Î¦â‚€ âŸ¨y, hyâŸ© else continuous_linear_equiv.refl ğ•œ F,
  have hÎ¦ : âˆ€ (y) (hy : y âˆˆ U), Î¦ y = Î¦â‚€ âŸ¨y, hyâŸ© := Î» y hy, dif_pos hy,
  have hÎ¦Ï† : âˆ€ x : U, âˆ€ y âˆˆ u x, Î¦ y = Ï† x y,
  { intros x y hyu,
    refine (hÎ¦ y (hUu x hyu)).trans _,
    exact Union_lift_mk âŸ¨y, hyuâŸ© _ },
  refine âŸ¨hUu', âŸ¨Î¦, hÎ¦Ï†, _âŸ©âŸ©,
  intros p hp,
  rw [hU] at hp,
  rw heuÏ† âŸ¨p.fst, hp.1âŸ© âŸ¨hux _, hp.2âŸ©,
  congrm (_, _),
  rw hÎ¦Ï†,
  exact hux _
end

/-- Let `e` be a local homeomorphism of `B Ã— F` whose source is `U Ã—Ë¢ univ`, for some set `U` in
`B`, and which, at any point `x` in `U`, admits a neighbourhood `u` of `x` such that `e` is equal on
`u Ã—Ë¢ univ` to the application fibrewise of a function `Ï† : B â†’ (F â‰ƒL[ğ•œ] F)` which is smooth and has
smooth inverse.

This is the key mathematical point of the `locality` condition in the construction of the
`structure_groupoid` of bi-smooth fibrewise linear local homeomorphisms.  The proof is by gluing
together the various bi-smooth fibrewise linear local homeomorphism which exist locally.

This proof is broken into four parts to fight timeouts. -/
lemma smooth_fibrewise_linear.locality_auxâ‚„ {e : local_homeomorph (B Ã— F) (B Ã— F)}
  {U : set B}
  (hU : e.source = U Ã—Ë¢ univ)
  {Ï† : U â†’ B â†’ (F â‰ƒL[ğ•œ] F)}
  {u : U â†’ set B}
  (hUu : âˆ€ x, u x âŠ† U)
  (hux : âˆ€ x, â†‘x âˆˆ u x)
  (heuÏ† : âˆ€ x, eq_on e (Î» q, (q.1, Ï† x q.1 q.2)) (u x Ã—Ë¢ univ))
  (hu : âˆ€ x, is_open (u x))
  (hÏ† : âˆ€ x, smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» y, (Ï† x y : F â†’L[ğ•œ] F)) (u x))
  (h2Ï† : âˆ€ x, smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» (y : B), ((Ï† x y).symm : F â†’L[ğ•œ] F)) (u x)) :
  âˆƒ (hU' : is_open U) (Î¦ : B â†’ (F â‰ƒL[ğ•œ] F))
    (hÎ¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Î¦ x : F â†’L[ğ•œ] F)) U)
    (h2Î¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Î¦ x).symm : F â†’L[ğ•œ] F)) U),
    e.eq_on_source (fiberwise_linear.local_homeomorph Î¦ hU' hÎ¦.continuous_on h2Î¦.continuous_on) :=
begin
  obtain âŸ¨hUu', âŸ¨Î¦, hÎ¦Ï†, HâŸ©âŸ© := smooth_fibrewise_linear.locality_auxâ‚ƒ hU hUu hux heuÏ†,
  have hU' : is_open U,
  { rw hUu',
    apply is_open_Union hu },
  have hÎ¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» y, (Î¦ y : F â†’L[ğ•œ] F)) U,
  { apply cont_mdiff_on_of_locally_cont_mdiff_on,
    intros x hx,
    refine âŸ¨u âŸ¨x, hxâŸ©, hu âŸ¨x, hxâŸ©, hux _, _âŸ©,
    refine (cont_mdiff_on.congr (hÏ† âŸ¨x, hxâŸ©) _).mono (inter_subset_right _ _),
    intros y hy,
    rw hÎ¦Ï† âŸ¨x, hxâŸ© y hy },
  have h2Î¦ : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» y, ((Î¦ y).symm : F â†’L[ğ•œ] F)) U,
  { apply cont_mdiff_on_of_locally_cont_mdiff_on,
    intros x hx,
    refine âŸ¨u âŸ¨x, hxâŸ©, hu âŸ¨x, hxâŸ©, hux _, _âŸ©,
    refine (cont_mdiff_on.congr (h2Ï† âŸ¨x, hxâŸ©) _).mono (inter_subset_right _ _),
    intros y hy,
    rw hÎ¦Ï† âŸ¨x, hxâŸ© y hy },
  exact âŸ¨hU', Î¦, hÎ¦, h2Î¦, âŸ¨hU, HâŸ©âŸ©,
end

/-- Let `e` be a local homeomorphism of `B Ã— F`.  Suppose that at every point `p` in the source of
`e`, there is some neighbourhood `s` of `p` on which `e` is equal to a bi-smooth fibrewise linear
local homeomorphism.

Then the source of `e` is of the form `U Ã—Ë¢ univ`, for some set `U` in `B`, and, at any point `x` in
`U`, admits a neighbourhood `u` of `x` such that `e` is equal on `u Ã—Ë¢ univ` to some bi-smooth
fibrewise linear local homeomorphism. -/
lemma smooth_fibrewise_linear.locality_auxâ‚ (e : local_homeomorph (B Ã— F) (B Ã— F))
  (h : âˆ€ p âˆˆ e.source, âˆƒ s : set (B Ã— F), is_open s âˆ§ p âˆˆ s âˆ§
    âˆƒ (Ï† : B â†’ (F â‰ƒL[ğ•œ] F)) (u : set B) (hu : is_open u)
      (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x : F â†’L[ğ•œ] F)) u)
      (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
      (e.restr s).eq_on_source
            (fiberwise_linear.local_homeomorph Ï† hu hÏ†.continuous_on h2Ï†.continuous_on)) :
  âˆƒ (U : set B) (hU : e.source = U Ã—Ë¢ univ),
  âˆ€ x âˆˆ U, âˆƒ (Ï† : B â†’ (F â‰ƒL[ğ•œ] F)) (u : set B) (hu : is_open u) (huU : u âŠ† U) (hux : x âˆˆ u)
    (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x : F â†’L[ğ•œ] F)) u)
    (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, ((Ï† x).symm : F â†’L[ğ•œ] F)) u),
    (e.restr (u Ã—Ë¢ univ)).eq_on_source
      (fiberwise_linear.local_homeomorph Ï† hu hÏ†.continuous_on h2Ï†.continuous_on) :=
begin
  rw set_coe.forall' at h,
  choose! s hs hsp Ï† u hu hÏ† h2Ï† heÏ† using h,
  have hesu : âˆ€ p : e.source, e.source âˆ© s p = u p Ã—Ë¢ univ,
  { intros p,
    rw â† e.restr_source' (s _) (hs _),
    exact (heÏ† p).1 },
  have hu' : âˆ€ p : e.source, (p : B Ã— F).fst âˆˆ u p,
  { intros p,
    have : (p : B Ã— F) âˆˆ e.source âˆ© s p := âŸ¨p.prop, hsp pâŸ©,
    simpa only [hesu, mem_prod, mem_univ, and_true] using this },
  have heu : âˆ€ p : e.source, âˆ€ q : B Ã— F, q.fst âˆˆ u p â†’ q âˆˆ e.source,
  { intros p q hq,
    have : q âˆˆ u p Ã—Ë¢ (univ : set F) := âŸ¨hq, trivialâŸ©,
    rw â† hesu p at this,
    exact this.1 },
  have he : e.source = (prod.fst '' e.source) Ã—Ë¢ (univ : set F),
  { ext p,
    simp_rw [mem_prod, mem_image, mem_univ, and_true],
    split,
    { intros hp,
      exact âŸ¨p, hp, rflâŸ© },
    { rintros âŸ¨q, hq, hpqâŸ©,
      apply heu,
      rw â† hpq,
      exact hu' âŸ¨q, hqâŸ© } },
  refine âŸ¨prod.fst '' e.source, he, _âŸ©,
  rintros x âŸ¨p, hp, rflâŸ©,
  let q : e.to_local_equiv.source := âŸ¨p, hpâŸ©,
  refine âŸ¨Ï† q, u q, hu q, _, hu' _, hÏ† q, h2Ï† q, _âŸ©,
  { intros y hy,
    refine âŸ¨(y, 0), heu q _ _, rflâŸ©,
    exact hy },
  { rw [â† hesu, e.restr_source_inter],
    exact heÏ† q },
end

lemma smooth_fibrewise_linear.trans_aux
  {e e' : local_homeomorph (B Ã— F) (B Ã— F)}
  {Ï† : B â†’ (F â‰ƒL[ğ•œ] F)}
  {U : set B}
  (hU : is_open U)
  (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U)
  {Ï†' : B â†’ (F â‰ƒL[ğ•œ] F)}
  {U' : set B}
  (hU' : is_open U')
  (hÏ†' : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, Ï†' x : B â†’ F â†’L[ğ•œ] F) U')
  (h2Ï†' : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï†' x).symm : B â†’ F â†’L[ğ•œ] F) U')
  (heÏ† : e.eq_on_source
    (fiberwise_linear.local_homeomorph Ï† hU hÏ†.continuous_on h2Ï†.continuous_on))
  (heÏ†' : e'.eq_on_source
    (fiberwise_linear.local_homeomorph Ï†' hU' hÏ†'.continuous_on h2Ï†'.continuous_on)) :
  âˆƒ (Ï†'' : B â†’ (F â‰ƒL[ğ•œ] F)) (U'' : set B) (hU'' : is_open U'')
  (hÏ†'' : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, Ï†'' x : B â†’ F â†’L[ğ•œ] F) U'')
  (h2Ï†'' : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï†'' x).symm : B â†’ F â†’L[ğ•œ] F) U''),
    (e â‰«â‚• e').eq_on_source
    (fiberwise_linear.local_homeomorph Ï†'' hU'' hÏ†''.continuous_on h2Ï†''.continuous_on) :=
begin
  refine âŸ¨Î» b, (Ï† b).trans (Ï†' b), _, hU.inter hU', _, _, setoid.trans (heÏ†.trans' heÏ†') âŸ¨_, _âŸ©âŸ©,
  { have : smooth_on IB ğ“˜(ğ•œ, (F â†’L[ğ•œ] F) Ã— (F â†’L[ğ•œ] F))
      (Î» x, ((Ï†' x : F â†’L[ğ•œ] F), (Ï† x : F â†’L[ğ•œ] F))) (U âˆ© U'),
    { exact (hÏ†'.mono (inter_subset_right _ _)).prod_mk (hÏ†.mono (inter_subset_left _ _)) },
    exact is_bounded_bilinear_map_comp.cont_diff.cont_mdiff.comp_cont_mdiff_on this },
  { have : smooth_on IB ğ“˜(ğ•œ, (F â†’L[ğ•œ] F) Ã— (F â†’L[ğ•œ] F))
      (Î» x, (((Ï† x).symm : F â†’L[ğ•œ] F), ((Ï†' x).symm : F â†’L[ğ•œ] F))) (U âˆ© U'),
    { exact (h2Ï†.mono (inter_subset_left _ _)).prod_mk (h2Ï†'.mono (inter_subset_right _ _)) },
    exact is_bounded_bilinear_map_comp.cont_diff.cont_mdiff.comp_cont_mdiff_on this },
  { apply fiberwise_linear.source_trans_local_homeomorph },
  { rintros âŸ¨b, vâŸ© hb,
    apply fiberwise_linear.trans_local_homeomorph_apply }
end

variables (F B IB)

/-- For `B` a manifold and `F` a normed space, the groupoid on `B Ã— F` consisting of local
homeomorphisms which are bi-smooth and fibrewise linear. -/
def smooth_fiberwise_linear : structure_groupoid (B Ã— F) :=
{ members := â‹ƒ (Ï† : B â†’ F â‰ƒL[ğ•œ] F) (U : set B) (hU : is_open U)
  (hÏ† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, Ï† x : B â†’ F â†’L[ğ•œ] F) U)
  (h2Ï† : smooth_on IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» x, (Ï† x).symm : B â†’ F â†’L[ğ•œ] F) U),
  {e | e.eq_on_source (fiberwise_linear.local_homeomorph Ï† hU hÏ†.continuous_on h2Ï†.continuous_on)},
  trans' := begin -- the hard work has been extracted to `trans_aux`
    simp_rw [mem_Union],
    rintros e e' âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ© âŸ¨Ï†', U', hU', hÏ†', h2Ï†', heÏ†'âŸ©,
    exact smooth_fibrewise_linear.trans_aux hU hÏ† h2Ï† hU' hÏ†' h2Ï†' heÏ† heÏ†',
  end,
  symm' := begin
    simp_rw [mem_Union],
    rintros e âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ©,
    refine âŸ¨Î» b, (Ï† b).symm, U, hU, h2Ï†, _, heÏ†.symm'âŸ©,
    simp_rw continuous_linear_equiv.symm_symm,
    exact hÏ†
  end,
  id_mem' := begin
    simp_rw [mem_Union],
    refine âŸ¨Î» b, continuous_linear_equiv.refl ğ•œ F, univ, is_open_univ, _, _, âŸ¨_, Î» b hb, _âŸ©âŸ©,
    { apply cont_mdiff_on_const },
    { apply cont_mdiff_on_const },
    { simp only [fiberwise_linear.local_homeomorph] with mfld_simps },
    { simp only [fiberwise_linear.local_homeomorph, continuous_linear_equiv.coe_refl', prod.mk.eta]
        with mfld_simps, },
  end,
  locality' := begin -- the hard work has been extracted to `locality_auxâ‚` thru `locality_auxâ‚„`
    simp_rw [mem_Union],
    intros e he,
    obtain âŸ¨U, hU, hâŸ© := smooth_fibrewise_linear.locality_auxâ‚ e he,
    obtain âŸ¨Ï†, u, hu, hÏ†, h2Ï†, hUu, hux, heuÏ†âŸ© := smooth_fibrewise_linear.locality_auxâ‚‚ h,
    obtain âŸ¨hU', Î¦, hÎ¦âŸ© := smooth_fibrewise_linear.locality_auxâ‚„ hU hUu hux heuÏ† hu hÏ† h2Ï†,
    exact âŸ¨Î¦, U, hU', hÎ¦âŸ©,
  end,
  eq_on_source' := begin
    simp_rw [mem_Union],
    rintros e e' âŸ¨Ï†, U, hU, hÏ†, h2Ï†, heÏ†âŸ© hee',
    exact âŸ¨Ï†, U, hU, hÏ†, h2Ï†, setoid.trans hee' heÏ†âŸ©,
  end }
