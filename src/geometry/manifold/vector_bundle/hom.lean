/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import geometry.manifold.vector_bundle.basic
import topology.vector_bundle.hom

/-! # Homs of smooth vector bundles over the same base space

Here we show that `bundle.continuous_linear_map` is a smooth vector bundle.

Note that we only do this for of bundles linear maps, not for bundles of arbitrary semilinear maps.
To do it for semilinear maps, we would need to generalize `continuous_linear_map.cont_mdiff`
(and `continuous_linear_map.cont_diff`) to semilinear maps.
-/

noncomputable theory

open bundle set local_homeomorph continuous_linear_map pretrivialization
open_locale manifold bundle

variables {ğ•œ B F Fâ‚ Fâ‚‚ M Mâ‚ Mâ‚‚ : Type*}
  {E : B â†’ Type*} {Eâ‚ : B â†’ Type*} {Eâ‚‚ : B â†’ Type*}
  [nontrivially_normed_field ğ•œ]
  [âˆ€ x, add_comm_monoid (E x)] [âˆ€ x, module ğ•œ (E x)]
  [normed_add_comm_group F] [normed_space ğ•œ F]
  [topological_space (total_space E)] [âˆ€ x, topological_space (E x)]
  [âˆ€ x, add_comm_monoid (Eâ‚ x)] [âˆ€ x, module ğ•œ (Eâ‚ x)]
  [normed_add_comm_group Fâ‚] [normed_space ğ•œ Fâ‚]
  [topological_space (total_space Eâ‚)] [âˆ€ x, topological_space (Eâ‚ x)]
  [âˆ€ x, add_comm_monoid (Eâ‚‚ x)] [âˆ€ x, module ğ•œ (Eâ‚‚ x)]
  [normed_add_comm_group Fâ‚‚] [normed_space ğ•œ Fâ‚‚]
  [topological_space (total_space Eâ‚‚)] [âˆ€ x, topological_space (Eâ‚‚ x)]

  {EB : Type*} [normed_add_comm_group EB] [normed_space ğ•œ EB]
  {HB : Type*} [topological_space HB] (IB : model_with_corners ğ•œ EB HB)
  [topological_space B] [charted_space HB B]
  {EM : Type*} [normed_add_comm_group EM] [normed_space ğ•œ EM]
  {HM : Type*} [topological_space HM] {IM : model_with_corners ğ•œ EM HM}
  [topological_space M] [charted_space HM M] [Is : smooth_manifold_with_corners IM M]
  {n : â„•âˆ}
  [fiber_bundle Fâ‚ Eâ‚] [vector_bundle ğ•œ Fâ‚ Eâ‚]
  [fiber_bundle Fâ‚‚ Eâ‚‚] [vector_bundle ğ•œ Fâ‚‚ Eâ‚‚]
  {eâ‚ eâ‚' : trivialization Fâ‚ (Ï€ Eâ‚)} {eâ‚‚ eâ‚‚' : trivialization Fâ‚‚ (Ï€ Eâ‚‚)}

local notation `LEâ‚Eâ‚‚` := total_space (bundle.continuous_linear_map (ring_hom.id ğ•œ) Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)

/- This proof is slow, especially the `simp only` and the elaboration of `hâ‚‚`. -/
lemma smooth_on_continuous_linear_map_coord_change
  [smooth_manifold_with_corners IB B]
  [smooth_vector_bundle Fâ‚ Eâ‚ IB] [smooth_vector_bundle Fâ‚‚ Eâ‚‚ IB]
  [mem_trivialization_atlas eâ‚] [mem_trivialization_atlas eâ‚']
  [mem_trivialization_atlas eâ‚‚] [mem_trivialization_atlas eâ‚‚'] :
  smooth_on IB ğ“˜(ğ•œ, ((Fâ‚ â†’L[ğ•œ] Fâ‚‚) â†’L[ğ•œ] (Fâ‚ â†’L[ğ•œ] Fâ‚‚)))
    (continuous_linear_map_coord_change (ring_hom.id ğ•œ) eâ‚ eâ‚' eâ‚‚ eâ‚‚')
    ((eâ‚.base_set âˆ© eâ‚‚.base_set) âˆ© (eâ‚'.base_set âˆ© eâ‚‚'.base_set)) :=
begin
  let Lâ‚ := compL ğ•œ Fâ‚ Fâ‚‚ Fâ‚‚,
  have hâ‚ : smooth _ _ _ := Lâ‚.cont_mdiff,
  have hâ‚‚ : smooth _ _ _ := (continuous_linear_map.flip (compL ğ•œ Fâ‚ Fâ‚ Fâ‚‚)).cont_mdiff,
  have hâ‚ƒ : smooth_on IB _ _ _ := smooth_on_coord_change eâ‚' eâ‚,
  have hâ‚„ : smooth_on IB _ _ _ := smooth_on_coord_change eâ‚‚ eâ‚‚',
  refine ((hâ‚.comp_smooth_on (hâ‚„.mono _)).clm_comp (hâ‚‚.comp_smooth_on (hâ‚ƒ.mono _))).congr _,
  { mfld_set_tac },
  { mfld_set_tac },
  { intros b hb, ext L v,
    simp only [continuous_linear_map_coord_change, continuous_linear_equiv.coe_coe,
      continuous_linear_equiv.arrow_congrSL_apply, comp_apply, function.comp, compL_apply,
      flip_apply, continuous_linear_equiv.symm_symm] },
end

variables [âˆ€ x, has_continuous_add (Eâ‚‚ x)] [âˆ€ x, has_continuous_smul ğ•œ (Eâ‚‚ x)]

lemma hom_chart (xâ‚€ x : LEâ‚Eâ‚‚) :
  chart_at (model_prod HB (Fâ‚ â†’L[ğ•œ] Fâ‚‚)) xâ‚€ x =
  (chart_at HB xâ‚€.1 x.1, in_coordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ xâ‚€.1 x.1 xâ‚€.1 x.1 x.2) :=
by simp_rw [fiber_bundle.charted_space_chart_at, trans_apply, local_homeomorph.prod_apply,
  trivialization.coe_coe, local_homeomorph.refl_apply, function.id_def, hom_trivialization_at_apply]

variables {IB}

lemma cont_mdiff_at_hom_bundle (f : M â†’ LEâ‚Eâ‚‚) {xâ‚€ : M} {n : â„•âˆ} :
  cont_mdiff_at IM (IB.prod ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)) n f xâ‚€ â†”
  cont_mdiff_at IM IB n (Î» x, (f x).1) xâ‚€ âˆ§
  cont_mdiff_at IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚) n
  (Î» x, in_coordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (f xâ‚€).1 (f x).1 (f xâ‚€).1 (f x).1 (f x).2) xâ‚€ :=
by  apply cont_mdiff_at_total_space

lemma smooth_at_hom_bundle (f : M â†’ LEâ‚Eâ‚‚) {xâ‚€ : M} :
  smooth_at IM (IB.prod ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)) f xâ‚€ â†”
  smooth_at IM IB (Î» x, (f x).1) xâ‚€ âˆ§
  smooth_at IM ğ“˜(ğ•œ, Fâ‚ â†’L[ğ•œ] Fâ‚‚)
  (Î» x, in_coordinates Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ (f xâ‚€).1 (f x).1 (f xâ‚€).1 (f x).1 (f x).2) xâ‚€ :=
cont_mdiff_at_hom_bundle f

variables [smooth_manifold_with_corners IB B]
  [smooth_vector_bundle Fâ‚ Eâ‚ IB] [smooth_vector_bundle Fâ‚‚ Eâ‚‚ IB]

instance bundle.continuous_linear_map.vector_prebundle.is_smooth :
  (bundle.continuous_linear_map.vector_prebundle (ring_hom.id ğ•œ) Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).is_smooth IB :=
{ exists_smooth_coord_change := begin
    rintro _ âŸ¨eâ‚, eâ‚‚, heâ‚, heâ‚‚, rflâŸ© _ âŸ¨eâ‚', eâ‚‚', heâ‚', heâ‚‚', rflâŸ©,
    resetI,
    refine âŸ¨continuous_linear_map_coord_change (ring_hom.id ğ•œ) eâ‚ eâ‚' eâ‚‚ eâ‚‚',
      smooth_on_continuous_linear_map_coord_change IB,
      continuous_linear_map_coord_change_apply (ring_hom.id ğ•œ) eâ‚ eâ‚' eâ‚‚ eâ‚‚'âŸ©
  end }

/-- Todo: remove this definition. It is probably needed because of the type-class pi bug
https://leanprover.zulipchat.com/#narrow/stream/116395-maths/topic/vector.20bundles.20--.20typeclass.20inference.20issue
-/
@[reducible]
def smooth_vector_bundle.continuous_linear_map.aux (x) :
  topological_space (bundle.continuous_linear_map (ring_hom.id ğ•œ) Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚ x) :=
by apply_instance
local attribute [instance, priority 1] smooth_vector_bundle.continuous_linear_map.aux

instance smooth_vector_bundle.continuous_linear_map :
  smooth_vector_bundle (Fâ‚ â†’L[ğ•œ] Fâ‚‚) (bundle.continuous_linear_map (ring_hom.id ğ•œ) Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚)
    IB :=
(bundle.continuous_linear_map.vector_prebundle (ring_hom.id ğ•œ) Fâ‚ Eâ‚ Fâ‚‚ Eâ‚‚).smooth_vector_bundle IB
