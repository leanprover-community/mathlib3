/-
Copyright Â© 2023 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Floris van Doorn
-/

import geometry.manifold.cont_mdiff_mfderiv
import topology.continuous_function.basic
import geometry.manifold.algebra.lie_group

/-!
# Smooth sections

In this file we define the type `cont_mdiff_section` of `n` times continuously differentiable
sections of a smooth vector bundle over a manifold `M` and prove that it's a module.
-/
open bundle filter function
open_locale manifold

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
{H : Type*} [topological_space H]
{H' : Type*} [topological_space H']
(I : model_with_corners ğ•œ E H) (I' : model_with_corners ğ•œ E' H')
{M : Type*} [topological_space M] [charted_space H M]
{M' : Type*} [topological_space M'] [charted_space H' M']
{E'' : Type*} [normed_add_comm_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H'']
{I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']
[smooth_manifold_with_corners I M]


variables (F : Type*) [normed_add_comm_group F] [normed_space ğ•œ F] -- `F` model fiber
  (n : â„•âˆ)
  (V : M â†’ Type*) [topological_space (total_space V)] -- `V` vector bundle
  [Î  x, add_comm_group (V x)] [Î  x, module ğ•œ (V x)]
variables [Î  x : M, topological_space (V x)]
  [fiber_bundle F V]
  [vector_bundle ğ•œ F V]
  [smooth_vector_bundle F V I]

/-- Bundled `n` times continuously differentiable maps. -/
@[protect_proj]
structure cont_mdiff_section :=
(to_fun            : Î  x, V x)
(cont_mdiff_to_fun : cont_mdiff I (I.prod ğ“˜(ğ•œ, F)) n (Î» x, total_space_mk x (to_fun x)))

/-- Bundled smooth maps. -/
@[reducible] def smooth_section := cont_mdiff_section I F âŠ¤ V

localized "notation (name := cont_mdiff_section) `Câ‚›^` n `âŸ®` I `; ` F `, ` V `âŸ¯` :=
  cont_mdiff_section I F n V" in manifold

namespace cont_mdiff_section

variables {I} {I'} {n} {F} {V}

instance : has_coe_to_fun Câ‚›^nâŸ®I; F, VâŸ¯ (Î» s, Î  x, V x) := âŸ¨cont_mdiff_section.to_funâŸ©

variables {s t : Câ‚›^nâŸ®I; F, VâŸ¯}

@[simp] lemma coe_fn_mk (s : Î  x, V x)
  (hs : cont_mdiff I (I.prod ğ“˜(ğ•œ, F)) n (Î» x, total_space_mk x (s x))) :
  (mk s hs : Î  x, V x) = s :=
rfl

protected lemma cont_mdiff (s : Câ‚›^nâŸ®I; F, VâŸ¯) :
  cont_mdiff I (I.prod ğ“˜(ğ•œ, F)) n (Î» x, total_space_mk x (s x : V x)) := s.cont_mdiff_to_fun

protected lemma smooth (s : Câ‚›^âˆâŸ®I; F, VâŸ¯) :
  smooth I (I.prod ğ“˜(ğ•œ, F)) (Î» x, total_space_mk x (s x : V x)) := s.cont_mdiff_to_fun

protected lemma mdifferentiable' (s : Câ‚›^nâŸ®I; F, VâŸ¯) (hn : 1 â‰¤ n) :
  mdifferentiable I (I.prod ğ“˜(ğ•œ, F)) (Î» x, total_space_mk x (s x : V x)) :=
s.cont_mdiff.mdifferentiable hn

protected lemma mdifferentiable (s : Câ‚›^âˆâŸ®I; F, VâŸ¯) :
  mdifferentiable I (I.prod ğ“˜(ğ•œ, F)) (Î» x, total_space_mk x (s x : V x)) :=
s.cont_mdiff.mdifferentiable le_top

protected lemma mdifferentiable_at (s : Câ‚›^âˆâŸ®I; F, VâŸ¯) {x} :
  mdifferentiable_at I (I.prod ğ“˜(ğ•œ, F)) (Î» x, total_space_mk x (s x : V x)) x :=
s.mdifferentiable x

lemma coe_inj â¦ƒs t : Câ‚›^nâŸ®I; F, VâŸ¯â¦„ (h : (s : Î  x, V x) = t) : s = t :=
by cases s; cases t; cases h; refl

lemma coe_injective : injective (coe_fn : Câ‚›^nâŸ®I; F, VâŸ¯ â†’ Î  x, V x) :=
coe_inj

@[ext] theorem ext (h : âˆ€ x, s x = t x) : s = t :=
by cases s; cases t; congr'; exact funext h

-- duped with open PR #18877
instance has_smooth_add_self : has_smooth_add ğ“˜(ğ•œ, E) E :=
âŸ¨by { convert cont_diff_add.cont_mdiff, exact model_with_corners_self_prod.symm,
  exact charted_space_self_prod }âŸ©

instance has_add : has_add Câ‚›^nâŸ®I; F, VâŸ¯ :=
begin
  refine âŸ¨Î» s t, âŸ¨s + t, _âŸ©âŸ©,
  intro xâ‚€,
  have hs := s.cont_mdiff xâ‚€,
  have ht := t.cont_mdiff xâ‚€,
  rw [cont_mdiff_at_section] at hs ht âŠ¢,
  set e := trivialization_at F V xâ‚€,
  refine (hs.add ht).congr_of_eventually_eq _,
  refine eventually_of_mem (e.open_base_set.mem_nhds $ mem_base_set_trivialization_at F V xâ‚€) _,
  intros x hx,
  apply (e.linear ğ•œ hx).1,
end

@[simp]
lemma coe_add (s t : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(s + t) = s + t := rfl

instance has_sub : has_sub Câ‚›^nâŸ®I; F, VâŸ¯ :=
begin
  refine âŸ¨Î» s t, âŸ¨s - t, _âŸ©âŸ©,
  intro xâ‚€,
  have hs := s.cont_mdiff xâ‚€,
  have ht := t.cont_mdiff xâ‚€,
  rw [cont_mdiff_at_section] at hs ht âŠ¢,
  set e := trivialization_at F V xâ‚€,
  refine (hs.sub ht).congr_of_eventually_eq _,
  refine eventually_of_mem (e.open_base_set.mem_nhds $ mem_base_set_trivialization_at F V xâ‚€) _,
  intros x hx,
  apply (e.linear ğ•œ hx).map_sub,
end

@[simp]
lemma coe_sub (s t : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(s - t) = s - t := rfl

instance has_zero : has_zero Câ‚›^nâŸ®I; F, VâŸ¯ :=
âŸ¨âŸ¨Î» x, 0, (smooth_zero_section ğ•œ V).of_le le_topâŸ©âŸ©

@[simp]
lemma coe_zero : â‡‘(0 : Câ‚›^nâŸ®I; F, VâŸ¯) = 0 := rfl

instance has_smul : has_smul ğ•œ Câ‚›^nâŸ®I; F, VâŸ¯ :=
begin
  refine âŸ¨Î» c s, âŸ¨c â€¢ s, _âŸ©âŸ©,
  intro xâ‚€,
  have hs := s.cont_mdiff xâ‚€,
  rw [cont_mdiff_at_section] at hs âŠ¢,
  set e := trivialization_at F V xâ‚€,
  refine (cont_mdiff_at_const.smul hs).congr_of_eventually_eq _, -- todo: hs.const_smul
  { exact c },
  refine eventually_of_mem (e.open_base_set.mem_nhds $ mem_base_set_trivialization_at F V xâ‚€) _,
  intros x hx,
  apply (e.linear ğ•œ hx).2,
end

@[simp]
lemma coe_smul (r : ğ•œ) (s : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(r â€¢ s : Câ‚›^nâŸ®I; F, VâŸ¯) = r â€¢ s := rfl

instance has_neg : has_neg Câ‚›^nâŸ®I; F, VâŸ¯ :=
begin
  refine âŸ¨Î» s, âŸ¨- s, _âŸ©âŸ©,
  intro xâ‚€,
  have hs := s.cont_mdiff xâ‚€,
  rw [cont_mdiff_at_section] at hs âŠ¢,
  set e := trivialization_at F V xâ‚€,
  refine hs.neg.congr_of_eventually_eq _,
  refine eventually_of_mem (e.open_base_set.mem_nhds $ mem_base_set_trivialization_at F V xâ‚€) _,
  intros x hx,
  apply (e.linear ğ•œ hx).map_neg
end

@[simp]
lemma coe_neg (s : Câ‚›^nâŸ®I; F, VâŸ¯) : â‡‘(- s : Câ‚›^nâŸ®I; F, VâŸ¯) = - s := rfl

instance has_nsmul : has_smul â„•  Câ‚›^nâŸ®I; F, VâŸ¯ :=
âŸ¨nsmul_recâŸ©

@[simp]
lemma coe_nsmul (s : Câ‚›^nâŸ®I; F, VâŸ¯) (k : â„•) : â‡‘(k â€¢ s : Câ‚›^nâŸ®I; F, VâŸ¯) = k â€¢ s :=
begin
  induction k with k ih,
  { simp_rw [zero_smul], refl },
  simp_rw [succ_nsmul, â† ih], refl,
end

instance has_zsmul : has_smul â„¤ Câ‚›^nâŸ®I; F, VâŸ¯ :=
âŸ¨zsmul_recâŸ©

@[simp]
lemma coe_zsmul (s : Câ‚›^nâŸ®I; F, VâŸ¯) (z : â„¤) : â‡‘(z â€¢ s : Câ‚›^nâŸ®I; F, VâŸ¯) = z â€¢ s :=
begin
  cases z with n n,
  refine (coe_nsmul s n).trans _,
  simp only [int.of_nat_eq_coe, coe_nat_zsmul],
  refine (congr_arg has_neg.neg (coe_nsmul s (n+1))).trans _,
  simp only [zsmul_neg_succ_of_nat, neg_inj]
end

instance add_comm_group : add_comm_group Câ‚›^nâŸ®I; F, VâŸ¯ :=
coe_injective.add_comm_group _ coe_zero coe_add coe_neg coe_sub coe_nsmul coe_zsmul

variables (I F V n)
def coe_add_hom : Câ‚›^nâŸ®I; F, VâŸ¯ â†’+ Î  x, V x :=
{ to_fun := coe_fn,
  map_zero' := coe_zero,
  map_add' := coe_add }

variables {I F V n}

instance module : module ğ•œ Câ‚›^nâŸ®I; F, VâŸ¯ :=
coe_injective.module ğ•œ (coe_add_hom I F n V) coe_smul

end cont_mdiff_section
