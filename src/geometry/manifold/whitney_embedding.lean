/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import geometry.manifold.partition_of_unity

/-!
# Whitney embedding theorem

In this file we prove a version of the Whitney embedding theorem: for any compact real manifold `M`,
for sufficiently large `n` there exists a smooth embedding `M ‚Üí ‚Ñù^n`.

## TODO

* Prove the weak Whitney embedding theorem: any `œÉ`-compact smooth `m`-dimensional manifold can be
  embedded into `‚Ñù^(2m+1)`. This requires a version of Sard's theorem: for a locally Lipschitz
  continuous map `f : ‚Ñù^m ‚Üí ‚Ñù^n`, `m < n`, the range has Hausdorff dimension at most `m`, hence it
  has measure zero.

## Tags

partition of unity, smooth bump function, whitney theorem
-/

universes uŒπ uE uH uM
variables {Œπ : Type uŒπ}
{E : Type uE} [normed_group E] [normed_space ‚Ñù E] [finite_dimensional ‚Ñù E]
{H : Type uH} [topological_space H] {I : model_with_corners ‚Ñù E H}
{M : Type uM} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]

open function filter finite_dimensional set
open_locale topological_space manifold classical filter big_operators

noncomputable theory

namespace smooth_bump_covering

/-!
### Whitney embedding theorem

In this section we prove a version of the Whitney embedding theorem: for any compact real manifold
`M`, for sufficiently large `n` there exists a smooth embedding `M ‚Üí ‚Ñù^n`.
-/

variables [t2_space M] [fintype Œπ] {s : set M} (f : smooth_bump_covering Œπ I M s)

/-- Smooth embedding of `M` into `(E √ó ‚Ñù) ^ Œπ`. -/
def embedding_pi_tangent : C^‚àû‚üÆI, M; ùìò(‚Ñù, Œπ ‚Üí (E √ó ‚Ñù)), Œπ ‚Üí (E √ó ‚Ñù)‚üØ :=
{ to_fun := Œª x i, (f i x ‚Ä¢ ext_chart_at I (f.c i) x, f i x),
  times_cont_mdiff_to_fun := times_cont_mdiff_pi_space.2 $ Œª i,
    ((f i).smooth_smul times_cont_mdiff_on_ext_chart_at).prod_mk_space ((f i).smooth) }

local attribute [simp] lemma embedding_pi_tangent_coe :
  ‚áëf.embedding_pi_tangent = Œª x i, (f i x ‚Ä¢ ext_chart_at I (f.c i) x, f i x) :=
rfl

lemma embedding_pi_tangent_inj_on : inj_on f.embedding_pi_tangent s :=
begin
  intros x hx y hy h,
  simp only [embedding_pi_tangent_coe, funext_iff] at h,
  obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := prod.mk.inj_iff.1 (h (f.ind x hx)),
  rw [f.apply_ind x hx] at h‚ÇÇ,
  rw [‚Üê h‚ÇÇ, f.apply_ind x hx, one_smul, one_smul] at h‚ÇÅ,
  have := f.mem_ext_chart_at_source_of_eq_one h‚ÇÇ.symm,
  exact (ext_chart_at I (f.c _)).inj_on (f.mem_ext_chart_at_ind_source x hx) this h‚ÇÅ
end

lemma embedding_pi_tangent_injective (f : smooth_bump_covering Œπ I M) :
  injective f.embedding_pi_tangent :=
injective_iff_inj_on_univ.2 f.embedding_pi_tangent_inj_on

lemma comp_embedding_pi_tangent_mfderiv (x : M) (hx : x ‚àà s) :
  ((continuous_linear_map.fst ‚Ñù E ‚Ñù).comp
    (@continuous_linear_map.proj ‚Ñù _ Œπ (Œª _, E √ó ‚Ñù) _ _
      (Œª _, infer_instance) (f.ind x hx))).comp
      (mfderiv I ùìò(‚Ñù, Œπ ‚Üí (E √ó ‚Ñù)) f.embedding_pi_tangent x) =
  mfderiv I I (chart_at H (f.c (f.ind x hx))) x :=
begin
  set L := ((continuous_linear_map.fst ‚Ñù E ‚Ñù).comp
    (@continuous_linear_map.proj ‚Ñù _ Œπ (Œª _, E √ó ‚Ñù) _ _ (Œª _, infer_instance) (f.ind x hx))),
  have := L.has_mfderiv_at.comp x f.embedding_pi_tangent.mdifferentiable_at.has_mfderiv_at,
  convert has_mfderiv_at_unique this _,
  refine (has_mfderiv_at_ext_chart_at I (f.mem_chart_at_ind_source x hx)).congr_of_eventually_eq _,
  refine (f.eventually_eq_one x hx).mono (Œª y hy, _),
  simp only [embedding_pi_tangent_coe, continuous_linear_map.coe_comp', (‚àò),
    continuous_linear_map.coe_fst', continuous_linear_map.proj_apply],
  rw [hy, pi.one_apply, one_smul]
end

lemma embedding_pi_tangent_ker_mfderiv (x : M) (hx : x ‚àà s) :
  (mfderiv I ùìò(‚Ñù, Œπ ‚Üí (E √ó ‚Ñù)) f.embedding_pi_tangent x).ker = ‚ä• :=
begin
  apply bot_unique,
  rw [‚Üê (mdifferentiable_chart I (f.c (f.ind x hx))).ker_mfderiv_eq_bot
    (f.mem_chart_at_ind_source x hx), ‚Üê comp_embedding_pi_tangent_mfderiv],
  exact linear_map.ker_le_ker_comp _ _
end

lemma embedding_pi_tangent_injective_mfderiv (x : M) (hx : x ‚àà s) :
  injective (mfderiv I ùìò(‚Ñù, Œπ ‚Üí (E √ó ‚Ñù)) f.embedding_pi_tangent x) :=
linear_map.ker_eq_bot.1 (f.embedding_pi_tangent_ker_mfderiv x hx)

/-- Baby version of the Whitney weak embedding theorem: if `M` admits a finite covering by
supports of bump functions, then for some `n` it can be immersed into the `n`-dimensional
Euclidean space. -/
lemma exists_immersion_euclidean (f : smooth_bump_covering Œπ I M) :
  ‚àÉ (n : ‚Ñï) (e : M ‚Üí euclidean_space ‚Ñù (fin n)), smooth I (ùì° n) e ‚àß
    injective e ‚àß ‚àÄ x : M, injective (mfderiv I (ùì° n) e x) :=
begin
  set F := euclidean_space ‚Ñù (fin $ finrank ‚Ñù (Œπ ‚Üí (E √ó ‚Ñù))),
  letI : finite_dimensional ‚Ñù (E √ó ‚Ñù) := by apply_instance,
  set eEF : (Œπ ‚Üí (E √ó ‚Ñù)) ‚âÉL[‚Ñù] F :=
    continuous_linear_equiv.of_finrank_eq finrank_euclidean_space_fin.symm,
  refine ‚ü®_, eEF ‚àò f.embedding_pi_tangent,
    eEF.to_diffeomorph.smooth.comp f.embedding_pi_tangent.smooth,
    eEF.injective.comp f.embedding_pi_tangent_injective, Œª x, _‚ü©,
  rw [mfderiv_comp _ eEF.differentiable_at.mdifferentiable_at
    f.embedding_pi_tangent.mdifferentiable_at, eEF.mfderiv_eq],
  exact eEF.injective.comp (f.embedding_pi_tangent_injective_mfderiv _ trivial)
end

end smooth_bump_covering

/-- Baby version of the Whitney weak embedding theorem: if `M` admits a finite covering by
supports of bump functions, then for some `n` it can be embedded into the `n`-dimensional
Euclidean space. -/
lemma exists_embedding_euclidean_of_compact [t2_space M] [compact_space M] :
  ‚àÉ (n : ‚Ñï) (e : M ‚Üí euclidean_space ‚Ñù (fin n)), smooth I (ùì° n) e ‚àß
    closed_embedding e ‚àß ‚àÄ x : M, injective (mfderiv I (ùì° n) e x) :=
begin
  rcases smooth_bump_covering.exists_is_subordinate I is_closed_univ (Œª (x : M) _, univ_mem)
    with ‚ü®Œπ, f, -‚ü©,
  haveI := f.fintype,
  rcases f.exists_immersion_euclidean with ‚ü®n, e, hsmooth, hinj, hinj_mfderiv‚ü©,
  exact ‚ü®n, e, hsmooth, hsmooth.continuous.closed_embedding hinj, hinj_mfderiv‚ü©
end
