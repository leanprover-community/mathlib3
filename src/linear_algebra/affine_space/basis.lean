/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import linear_algebra.affine_space.independent
import linear_algebra.basis

/-!
# Affine bases and barycentric coordinates

Suppose `P` is an affine space modelled on the module `V` over the ring `k`, and `p : Œπ ‚Üí P` is an
affine-independent family of points spanning `P`. Given this data, each point `q : P` may be written
uniquely as an affine combination: `q = w‚ÇÄ p‚ÇÄ + w‚ÇÅ p‚ÇÅ + ‚ãØ` for some (finitely-supported) weights
`w·µ¢`. For each `i : Œπ`, we thus have an affine map `P ‚Üí·µÉ[k] k`, namely `q ‚Ü¶ w·µ¢`. This family of
maps is known as the family of barycentric coordinates. It is defined in this file.

## The construction

Fixing `i : Œπ`, and allowing `j : Œπ` to range over the values `j ‚â† i`, we obtain a basis `b·µ¢` of `V`
defined by `b·µ¢ j = p j -·µ• p i`. Let `f·µ¢ j : V ‚Üí‚Çó[k] k` be the corresponding dual basis and let
`f·µ¢ = ‚àë j, f·µ¢ j : V ‚Üí‚Çó[k] k` be the corresponding "sum of all coordinates" form. Then the `i`th
barycentric coordinate of `q : P` is `1 - f·µ¢ (q -·µ• p i)`.

## Main definitions

 * `affine_basis`: a structure representing an affine basis of an affine space.
 * `affine_basis.coord`: the map `P ‚Üí·µÉ[k] k` corresponding to `i : Œπ`.
 * `affine_basis.coord_apply_eq`: the behaviour of `affine_basis.coord i` on `p i`.
 * `affine_basis.coord_apply_ne`: the behaviour of `affine_basis.coord i` on `p j` when `j ‚â† i`.
 * `affine_basis.coord_apply`: the behaviour of `affine_basis.coord i` on `p j` for general `j`.
 * `affine_basis.coord_apply_combination`: the characterisation of `affine_basis.coord i` in terms
    of affine combinations, i.e., `affine_basis.coord i (w‚ÇÄ p‚ÇÄ + w‚ÇÅ p‚ÇÅ + ‚ãØ) = w·µ¢`.

## TODO

 * Construct the affine equivalence between `P` and `{ f : Œπ ‚Üí‚ÇÄ k | f.sum = 1 }`.

-/

namespace set
variables {Œ± Œ≤ Œ≥ : Type*} (f : Œ≤ ‚Üí Œ≥) (e : Œ± ‚âÉ Œ≤)

@[simp] lemma range_comp_equiv : range (f ‚àò e) = range f := e.surjective.range_comp _

end set

namespace basis
variables {ùïú V Œπ Œπ' : Type*} [semiring ùïú] [add_comm_monoid V] [module ùïú V] [fintype Œπ] [fintype Œπ']

@[simp] lemma sum_coords_reindex (b : basis Œπ ùïú V) (e : Œπ ‚âÉ Œπ') :
  (b.reindex e).sum_coords = b.sum_coords :=
begin
  ext x,
  simp only [coe_sum_coords_of_fintype, fintype.sum_apply, basis.coord_apply, reindex_repr],
  exact e.symm.sum_comp _,
end

end basis

open_locale affine big_operators
open set

universes u‚ÇÅ u‚ÇÇ u‚ÇÉ u‚ÇÑ

/-- An affine basis is a family of affine-independent points whose span is the top subspace. -/
@[protect_proj]
structure affine_basis (Œπ : Type u‚ÇÅ) (k : Type u‚ÇÇ) {V : Type u‚ÇÉ} (P : Type u‚ÇÑ)
  [add_comm_group V] [affine_space V P] [ring k] [module k V] :=
(to_fun : Œπ ‚Üí P)
(ind' : affine_independent k to_fun)
(tot' : affine_span k (range to_fun) = ‚ä§)

variables {Œπ Œπ' k V P : Type*} [add_comm_group V] [affine_space V P]

namespace affine_basis

section ring

variables [ring k] [module k V] (b : affine_basis Œπ k P) {s : finset Œπ} {i j : Œπ} (e : Œπ ‚âÉ Œπ')
include V

instance fun_like : fun_like (affine_basis Œπ k P) Œπ (Œª _, P) :=
{ coe := affine_basis.to_fun,
  coe_injective' := Œª f g h, by cases f; cases g; congr' }

@[ext]
lemma ext {b‚ÇÅ b‚ÇÇ : affine_basis Œπ k P} (h : (b‚ÇÅ : Œπ ‚Üí P) = b‚ÇÇ) : b‚ÇÅ = b‚ÇÇ := fun_like.coe_injective h

lemma ind : affine_independent k b := b.ind'
lemma tot : affine_span k (range b) = ‚ä§ := b.tot'

/-- The unique point in a single-point space is the simplest example of an affine basis. -/
instance : inhabited (affine_basis punit k punit) :=
‚ü®‚ü®id, affine_independent_of_subsingleton k id, by simp‚ü©‚ü©

include b

protected lemma nonempty : nonempty Œπ :=
not_is_empty_iff.mp $ Œª hŒπ,
  by simpa only [@range_eq_empty _ _ hŒπ, affine_subspace.span_empty, bot_ne_top] using b.tot

/-- Composition of an affine basis and an equivalence of index types. -/
def reindex (e : Œπ ‚âÉ Œπ') : affine_basis Œπ' k P :=
‚ü®b ‚àò e.symm, b.ind.comp_embedding e.symm.to_embedding,
  by { rw [e.symm.surjective.range_comp], exact b.3 }‚ü©

@[simp] lemma reindex_apply (i' : Œπ') : b.reindex e i' = b (e.symm i') := rfl

@[simp, norm_cast] lemma coe_reindex : ‚áë(b.reindex e) = b ‚àò e.symm := rfl

@[simp] lemma reindex_refl : b.reindex (equiv.refl _) = b := ext rfl

/-- Given an affine basis for an affine space `P`, if we single out one member of the family, we
obtain a linear basis for the model space `V`.

The linear basis corresponding to the singled-out member `i : Œπ` is indexed by `{j : Œπ // j ‚â† i}`
and its `j`th element is `b j -·µ• b i`. (See `basis_of_apply`.) -/
noncomputable def basis_of (i : Œπ) : basis {j : Œπ // j ‚â† i} k V :=
basis.mk ((affine_independent_iff_linear_independent_vsub k b i).mp b.ind)
begin
  suffices : submodule.span k (range (Œª (j : {x // x ‚â† i}), b ‚Üëj -·µ• b i)) =
             vector_span k (range b),
  { rw [this, ‚Üê direction_affine_span, b.tot, affine_subspace.direction_top], exact le_rfl },
  conv_rhs { rw ‚Üê image_univ, },
  rw vector_span_image_eq_span_vsub_set_right_ne k b (mem_univ i),
  congr,
  ext v,
  simp,
end

@[simp] lemma basis_of_apply (i : Œπ) (j : {j : Œπ // j ‚â† i}) :
  b.basis_of i j = b ‚Üëj -·µ• b i :=
by simp [basis_of]

@[simp] lemma basis_of_reindex (i : Œπ') :
  (b.reindex e).basis_of i =
    (b.basis_of $ e.symm i).reindex (e.subtype_equiv $ Œª _, e.eq_symm_apply.not) :=
by { ext j, simp }

/-- The `i`th barycentric coordinate of a point. -/
noncomputable def coord (i : Œπ) : P ‚Üí·µÉ[k] k :=
{ to_fun    := Œª q, 1 - (b.basis_of i).sum_coords (q -·µ• b i),
  linear    := -(b.basis_of i).sum_coords,
  map_vadd' := Œª q v, by rw [vadd_vsub_assoc, linear_map.map_add, vadd_eq_add, linear_map.neg_apply,
    sub_add_eq_sub_sub_swap, add_comm, sub_eq_add_neg], }

@[simp] lemma linear_eq_sum_coords (i : Œπ) :
  (b.coord i).linear = -(b.basis_of i).sum_coords :=
rfl

@[simp] lemma coord_reindex [fintype Œπ] [fintype Œπ'] (i : Œπ') :
  (b.reindex e).coord i = b.coord (e.symm i) :=
by { ext, classical, simp [affine_basis.coord] }

@[simp] lemma coord_apply_eq (i : Œπ) : b.coord i (b i) = 1 :=
by simp only [coord, basis.coe_sum_coords, linear_equiv.map_zero, linear_equiv.coe_coe,
  sub_zero, affine_map.coe_mk, finsupp.sum_zero_index, vsub_self]

@[simp] lemma coord_apply_ne (h : i ‚â† j) : b.coord i (b j) = 0 :=
by rw [coord, affine_map.coe_mk, ‚Üê subtype.coe_mk j h.symm, ‚Üê b.basis_of_apply,
  basis.sum_coords_self_apply, sub_self]

lemma coord_apply [decidable_eq Œπ] (i j : Œπ) : b.coord i (b j) = if i = j then 1 else 0 :=
by cases eq_or_ne i j; simp [h]

@[simp] lemma coord_apply_combination_of_mem (hi : i ‚àà s) {w : Œπ ‚Üí k} (hw : s.sum w = 1) :
  b.coord i (s.affine_combination b w) = w i :=
begin
  classical,
  simp only [coord_apply, hi, finset.affine_combination_eq_linear_combination, if_true, mul_boole,
    hw, function.comp_app, smul_eq_mul, s.sum_ite_eq, s.map_affine_combination b w hw],
end

@[simp] lemma coord_apply_combination_of_not_mem (hi : i ‚àâ s) {w : Œπ ‚Üí k} (hw : s.sum w = 1) :
  b.coord i (s.affine_combination b w) = 0 :=
begin
  classical,
  simp only [coord_apply, hi, finset.affine_combination_eq_linear_combination, if_false, mul_boole,
    hw, function.comp_app, smul_eq_mul, s.sum_ite_eq, s.map_affine_combination b w hw],
end

@[simp] lemma sum_coord_apply_eq_one [fintype Œπ] (q : P) :
  ‚àë i, b.coord i q = 1 :=
begin
  have hq : q ‚àà affine_span k (range b), { rw b.tot, exact affine_subspace.mem_top k V q, },
  obtain ‚ü®w, hw, rfl‚ü© := eq_affine_combination_of_mem_affine_span_of_fintype hq,
  convert hw,
  ext i,
  exact b.coord_apply_combination_of_mem (finset.mem_univ i) hw,
end

@[simp] lemma affine_combination_coord_eq_self [fintype Œπ] (q : P) :
  finset.univ.affine_combination b (Œª i, b.coord i q) = q :=
begin
  have hq : q ‚àà affine_span k (range b), { rw b.tot, exact affine_subspace.mem_top k V q, },
  obtain ‚ü®w, hw, rfl‚ü© := eq_affine_combination_of_mem_affine_span_of_fintype hq,
  congr,
  ext i,
  exact b.coord_apply_combination_of_mem (finset.mem_univ i) hw,
end

/-- A variant of `affine_basis.affine_combination_coord_eq_self` for the special case when the
affine space is a module so we can talk about linear combinations. -/
@[simp] lemma linear_combination_coord_eq_self [fintype Œπ] (b : affine_basis Œπ k V) (v : V) :
  ‚àë i, b.coord i v ‚Ä¢ b i = v :=
begin
  have hb := b.affine_combination_coord_eq_self v,
  rwa finset.univ.affine_combination_eq_linear_combination _ _ (b.sum_coord_apply_eq_one v) at hb,
end

lemma ext_elem [fintype Œπ] {q‚ÇÅ q‚ÇÇ : P} (h : ‚àÄ i, b.coord i q‚ÇÅ = b.coord i q‚ÇÇ) : q‚ÇÅ = q‚ÇÇ :=
begin
  rw [‚Üê b.affine_combination_coord_eq_self q‚ÇÅ, ‚Üê b.affine_combination_coord_eq_self q‚ÇÇ],
  simp only [h],
end

@[simp] lemma coe_coord_of_subsingleton_eq_one [subsingleton Œπ] (i : Œπ) :
  (b.coord i : P ‚Üí k) = 1 :=
begin
  ext q,
  have hp : (range b).subsingleton,
  { rw ‚Üê image_univ,
    apply subsingleton.image,
    apply subsingleton_of_subsingleton, },
  haveI := affine_subspace.subsingleton_of_subsingleton_span_eq_top hp b.tot,
  let s : finset Œπ := {i},
  have hi : i ‚àà s, { simp, },
  have hw : s.sum (function.const Œπ (1 : k)) = 1, { simp, },
  have hq : q = s.affine_combination b (function.const Œπ (1 : k)), { simp, },
  rw [pi.one_apply, hq, b.coord_apply_combination_of_mem hi hw],
end

lemma surjective_coord [nontrivial Œπ] (i : Œπ) :
  function.surjective $ b.coord i :=
begin
  classical,
  intros x,
  obtain ‚ü®j, hij‚ü© := exists_ne i,
  let s : finset Œπ := {i, j},
  have hi : i ‚àà s, { simp, },
  have hj : j ‚àà s, { simp, },
  let w : Œπ ‚Üí k := Œª j', if j' = i then x else 1-x,
  have hw : s.sum w = 1, { simp [hij, finset.sum_ite, finset.filter_insert, finset.filter_eq'], },
  use s.affine_combination b w,
  simp [b.coord_apply_combination_of_mem hi hw],
end

/-- Barycentric coordinates as an affine map. -/
noncomputable def coords : P ‚Üí·µÉ[k] Œπ ‚Üí k :=
{ to_fun    := Œª q i, b.coord i q,
  linear    :=
  { to_fun    := Œª v i, -(b.basis_of i).sum_coords v,
    map_add'  := Œª v w, by { ext i, simp only [linear_map.map_add, pi.add_apply, neg_add], },
    map_smul' := Œª t v, by { ext i, simpa only [linear_map.map_smul, pi.smul_apply, smul_neg] } },
  map_vadd' := Œª p v, by
    { ext i,
      simp only [linear_eq_sum_coords, linear_map.coe_mk, linear_map.neg_apply, pi.vadd_apply',
        affine_map.map_vadd], }, }

@[simp] lemma coords_apply (q : P) (i : Œπ) :
  b.coords q i = b.coord i q :=
rfl

end ring

section division_ring

variables [division_ring k] [module k V]
include V

@[simp] lemma coord_apply_centroid [char_zero k] (b : affine_basis Œπ k P) {s : finset Œπ} {i : Œπ}
  (hi : i ‚àà s) :
  b.coord i (s.centroid k b) = (s.card : k) ‚Åª¬π :=
by rw [finset.centroid, b.coord_apply_combination_of_mem hi
  (s.sum_centroid_weights_eq_one_of_nonempty _ ‚ü®i, hi‚ü©), finset.centroid_weights]

lemma exists_affine_subbasis {t : set P} (ht : affine_span k t = ‚ä§) :
  ‚àÉ (s ‚äÜ t) (b : affine_basis ‚Ü•s k P), ‚áëb = coe :=
begin
  obtain ‚ü®s, hst, h_tot, h_ind‚ü© := exists_affine_independent k V t,
  refine ‚ü®s, hst, ‚ü®coe, h_ind, _‚ü©, rfl‚ü©,
  rw [subtype.range_coe, h_tot, ht]
end

variables (k V P)

lemma exists_affine_basis : ‚àÉ (s : set P) (b : affine_basis ‚Ü•s k P), ‚áëb = coe :=
let ‚ü®s, _, hs‚ü© := exists_affine_subbasis (affine_subspace.span_univ k V P) in ‚ü®s, hs‚ü©

end division_ring

end affine_basis
