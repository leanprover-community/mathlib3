/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Mario Carneiro, Alexander Bentkamp
-/

import linear_algebra.basic linear_algebra.finsupp order.zorn

/-!

# Linear independence and bases

This file defines linear independence and bases in a module or vector space.

It is inspired by Isabelle/HOL's linear algebra, and hence indirectly by HOL Light.

## Main definitions

All definitions are given for families of vectors, i.e. `v : Î¹ â†’ M` where M is the module or
vectorspace and `Î¹ : Type*` is an arbitrary indexing type.

* `linear_independent R v` states that the elements of the family `v` are linear independent

* `linear_independent.repr hv x` returns the linear combination representing `x : span R (range v)`
  on the linear independent vectors `v`, given `hv : linear_independent R v`
  (using classical choice). `linear_independent.repr hv` is provided as a linear map.

* `is_basis R v` states that the vector family `v` is a basis, i.e. it is linear independent and
  spans the entire space

* `is_basis.repr hv x` is the basis version of `linear_independent.repr hv x`. It returns the
  linear combination representing `x : M` on a basis `v` of `M` (using classical choice).
  The argument `hv` must be a proof that `is_basis R v`. `is_basis.repr hv` is given as a linear
  map as well.

* `is_basis.constr hv f` constructs a linear map `Mâ‚ â†’â‚—[R] Mâ‚‚` given the values `f : Î¹ â†’ Mâ‚‚` at the
  basis `v : Î¹ â†’ Mâ‚`, given `hv : is_basis R v`.

## Main statements

* `is_basis.ext` states that two linear maps are equal if they coincide on a basis.

* `exists_is_basis` states that every vector space has a basis.

## Implementation notes

We use families instead of sets because it allows us to say that two identical vectors are linearly
dependent. For bases, this is useful as well because we can easily derive ordered bases by using an
ordered index type Î¹.

If you want to use sets, use the family `(Î» x, x : s â†’ M)` given a set `s : set M`. The lemmas
`linear_independent.to_subtype_range` and `linear_independent.of_subtype_range` connect those two
worlds.

## Tags

linearly dependent, linear dependence, linearly independent, linear independence, basis

-/

noncomputable theory

open function lattice set submodule
open_locale classical

variables {Î¹ : Type*} {Î¹' : Type*} {R : Type*} {ğ•œ : Type*}
          {M : Type*} {M' : Type*} {V : Type*} {V' : Type*}

section module
variables {v : Î¹ â†’ M}
variables [ring R] [add_comm_group M] [add_comm_group M']
variables [module R M] [module R M']
variables {a b : R} {x y : M}
include R

variables (R) (v)
/-- Linearly independent family of vectors -/
def linear_independent : Prop := (finsupp.total Î¹ M R v).ker = âŠ¥
variables {R} {v}

theorem linear_independent_iff : linear_independent R v â†”
  âˆ€l, finsupp.total Î¹ M R v l = 0 â†’ l = 0 :=
by simp [linear_independent, linear_map.ker_eq_bot']

lemma linear_independent_empty_type (h : Â¬ nonempty Î¹) : linear_independent R v :=
begin
 rw [linear_independent_iff],
 intros,
 ext i,
 exact false.elim (not_nonempty_iff_imp_false.1 h i)
end

lemma ne_zero_of_linear_independent
  {i : Î¹} (ne : 0 â‰  (1:R)) (hv : linear_independent R v) : v i â‰  0 :=
Î» h, ne $ eq.symm begin
  suffices : (finsupp.single i 1 : Î¹ â†’â‚€ R) i = 0, {simpa},
  rw linear_independent_iff.1 hv (finsupp.single i 1),
  {simp},
  {simp [h]}
end

lemma linear_independent.comp
  (h : linear_independent R v) (f : Î¹' â†’ Î¹) (hf : injective f) : linear_independent R (v âˆ˜ f) :=
begin
  rw [linear_independent_iff, finsupp.total_comp],
  intros l hl,
  have h_map_domain : âˆ€ x, (finsupp.map_domain f l) (f x) = 0,
    by rw linear_independent_iff.1 h (finsupp.map_domain f l) hl; simp,
  ext,
  convert h_map_domain a,
  simp only [finsupp.map_domain_apply hf],
end

lemma linear_independent_of_zero_eq_one (zero_eq_one : (0 : R) = 1) : linear_independent R v :=
linear_independent_iff.2 (Î» l hl, finsupp.eq_zero_of_zero_eq_one zero_eq_one _)

lemma linear_independent.unique (hv : linear_independent R v) {lâ‚ lâ‚‚ : Î¹ â†’â‚€ R} :
  finsupp.total Î¹ M R v lâ‚ = finsupp.total Î¹ M R v lâ‚‚ â†’ lâ‚ = lâ‚‚ :=
by apply linear_map.ker_eq_bot.1 hv

lemma linear_independent.injective (zero_ne_one : (0 : R) â‰  1) (hv : linear_independent R v) :
  injective v :=
begin
  intros i j hij,
  let l : Î¹ â†’â‚€ R := finsupp.single i (1 : R) - finsupp.single j 1,
  have h_total : finsupp.total Î¹ M R v l = 0,
  { rw finsupp.total_apply,
    rw finsupp.sum_sub_index,
    { simp [finsupp.sum_single_index, hij] },
    { intros, apply sub_smul } },
  have h_single_eq : finsupp.single i (1 : R) = finsupp.single j 1,
  { rw linear_independent_iff at hv,
    simp [eq_add_of_sub_eq' (hv l h_total)] },
  show i = j,
  { apply or.elim ((finsupp.single_eq_single_iff _ _ _ _).1 h_single_eq),
    simp,
    exact Î» h, false.elim (zero_ne_one.symm h.1) }
end

lemma linear_independent_span (hs : linear_independent R v) :
  @linear_independent Î¹ R (span R (range v))
      (Î» i : Î¹, âŸ¨v i, subset_span (mem_range_self i)âŸ©) _ _ _ :=
begin
  rw linear_independent_iff at *,
  intros l hl,
  apply hs l,
  have := congr_arg (submodule.subtype (span R (range v))) hl,
  convert this,
  rw [finsupp.total_apply, finsupp.total_apply],
  unfold finsupp.sum,
  rw linear_map.map_sum (submodule.subtype (span R (range v))),
  simp
end

section subtype
/- The following lemmas use the subtype defined by a set in M as the index set Î¹. -/

theorem linear_independent_comp_subtype {s : set Î¹} :
  linear_independent R (v âˆ˜ subtype.val : s â†’ M) â†”
  âˆ€ l âˆˆ (finsupp.supported R R s), (finsupp.total Î¹ M R v) l = 0 â†’ l = 0 :=
begin
  rw [linear_independent_iff, finsupp.total_comp],
  simp only [linear_map.comp_apply],
  split,
  { intros h l hlâ‚ hlâ‚‚,
    have h_bij : bij_on subtype.val (subtype.val â»Â¹' l.support.to_set : set s) l.support.to_set,
    { apply bij_on.mk,
      { unfold maps_to },
      { apply set.inj_on_of_injective _ subtype.val_injective },
      intros i hi,
      rw mem_image,
      use subtype.mk i (((finsupp.mem_supported _ _).1 hlâ‚ : â†‘(l.support) âŠ† s) hi),
      rw mem_preimage,
      exact âŸ¨hi, rflâŸ© },
    show l = 0,
    { apply finsupp.eq_zero_of_comap_domain_eq_zero (subtype.val : s â†’ Î¹) _ h_bij,
      apply h,
      convert hlâ‚‚,
      rw [finsupp.lmap_domain_apply, finsupp.map_domain_comap_domain],
      apply subtype.val_injective,
      rw subtype.range_val,
      exact (finsupp.mem_supported _ _).1 hlâ‚ } },
  { intros h l hl,
    have hl' : finsupp.total Î¹ M R v (finsupp.emb_domain âŸ¨subtype.val, subtype.val_injectiveâŸ© l) = 0,
    { rw finsupp.emb_domain_eq_map_domain âŸ¨subtype.val, subtype.val_injectiveâŸ© l,
      apply hl },
    apply finsupp.emb_domain_inj.1,
    rw [h (finsupp.emb_domain âŸ¨subtype.val, subtype.val_injectiveâŸ© l) _ hl',
        finsupp.emb_domain_zero],
    rw [finsupp.mem_supported, finsupp.support_emb_domain],
    intros x hx,
    rw [finset.mem_coe, finset.mem_map] at hx,
    rcases hx with âŸ¨i, x', hx'âŸ©,
    rw â†hx',
    simp }
end

theorem linear_independent_subtype {s : set M} :
  linear_independent R (Î» x, x : s â†’ M) â†”
  âˆ€ l âˆˆ (finsupp.supported R R s), (finsupp.total M M R id) l = 0 â†’ l = 0 :=
by apply @linear_independent_comp_subtype _ _ _ id

theorem linear_independent_comp_subtype_disjoint {s : set Î¹} :
  linear_independent R (v âˆ˜ subtype.val : s â†’ M) â†”
  disjoint (finsupp.supported R R s) (finsupp.total Î¹ M R v).ker :=
by rw [linear_independent_comp_subtype, linear_map.disjoint_ker]

theorem linear_independent_subtype_disjoint {s : set M} :
  linear_independent R (Î» x, x : s â†’ M) â†”
  disjoint (finsupp.supported R R s) (finsupp.total M M R id).ker :=
by apply @linear_independent_comp_subtype_disjoint _ _ _ id

theorem linear_independent_iff_total_on {s : set M} :
  linear_independent R (Î» x, x : s â†’ M) â†” (finsupp.total_on M M R id s).ker = âŠ¥ :=
by rw [finsupp.total_on, linear_map.ker, linear_map.comap_cod_restrict, map_bot, comap_bot,
  linear_map.ker_comp, linear_independent_subtype_disjoint, disjoint, â† map_comap_subtype,
  map_le_iff_le_comap, comap_bot, ker_subtype, le_bot_iff]

lemma linear_independent.to_subtype_range
  (hv : linear_independent R v) : linear_independent R (Î» x, x : range v â†’ M) :=
begin
  by_cases zero_eq_one : (0 : R) = 1,
  { apply linear_independent_of_zero_eq_one zero_eq_one },
  rw linear_independent_subtype,
  intros l hlâ‚ hlâ‚‚,
  have h_bij : bij_on v (v â»Â¹' finset.to_set (l.support)) (finset.to_set (l.support)),
  { apply bij_on.mk,
    { unfold maps_to },
    { apply set.inj_on_of_injective _ (linear_independent.injective zero_eq_one hv) },
    intros x hx,
    rcases mem_range.1 (((finsupp.mem_supported _ _).1 hlâ‚ : â†‘(l.support) âŠ† range v) hx) with âŸ¨i, hiâŸ©,
    rw mem_image,
    use i,
    rw [mem_preimage, hi],
    exact âŸ¨hx, rflâŸ© },
  apply finsupp.eq_zero_of_comap_domain_eq_zero v l,
  apply linear_independent_iff.1 hv,
  rw [finsupp.total_comap_domain, finset.sum_preimage v l.support h_bij (Î» (x : M), l x â€¢ x)],
  rw [finsupp.total_apply, finsupp.sum] at hlâ‚‚,
  apply hlâ‚‚
end

lemma linear_independent.of_subtype_range (hv : injective v)
  (h : linear_independent R (Î» x, x : range v â†’ M)) : linear_independent R v :=
begin
  rw linear_independent_iff,
  intros l hl,
  apply finsupp.injective_map_domain hv,
  apply linear_independent_subtype.1 h (l.map_domain v),
  { rw finsupp.mem_supported,
    intros x hx,
    have := finset.mem_coe.2 (finsupp.map_domain_support hx),
    rw finset.coe_image at this,
    apply set.image_subset_range _ _ this, },
  { rwa [finsupp.total_map_domain _ _ hv, left_id] }
end

lemma linear_independent.restrict_of_comp_subtype {s : set Î¹}
  (hs : linear_independent R (v âˆ˜ subtype.val : s â†’ M)) :
  linear_independent R (function.restrict v s) :=
begin
  have h_restrict : restrict v s = v âˆ˜ (Î» x, x.val) := rfl,
  rw [linear_independent_iff, h_restrict, finsupp.total_comp],
  intros l hl,
  have h_map_domain_subtype_eq_0 : l.map_domain subtype.val = 0,
  { rw linear_independent_comp_subtype at hs,
    apply hs (finsupp.lmap_domain R R (Î» x : subtype s, x.val) l) _ hl,
    rw finsupp.mem_supported,
    simp,
    intros x hx,
    have := finset.mem_coe.2 (finsupp.map_domain_support (finset.mem_coe.1 hx)),
    rw finset.coe_image at this,
    exact subtype.val_image_subset _ _ this },
  apply @finsupp.injective_map_domain _ (subtype s) Î¹,
  { apply subtype.val_injective },
  { simpa },
end

lemma linear_independent_empty : linear_independent R (Î» x, x : (âˆ… : set M) â†’ M) :=
by simp [linear_independent_subtype_disjoint]

lemma linear_independent.mono {t s : set M} (h : t âŠ† s) :
  linear_independent R (Î» x, x : s â†’ M) â†’ linear_independent R (Î» x, x : t â†’ M) :=
begin
 simp only [linear_independent_subtype_disjoint],
 exact (disjoint_mono_left (finsupp.supported_mono h))
end

lemma linear_independent_union {s t : set M}
  (hs : linear_independent R (Î» x, x : s â†’ M)) (ht : linear_independent R (Î» x, x : t â†’ M))
  (hst : disjoint (span R s) (span R t)) :
  linear_independent R (Î» x, x : (s âˆª t) â†’ M) :=
begin
  rw [linear_independent_subtype_disjoint, disjoint_def, finsupp.supported_union],
  intros l hâ‚ hâ‚‚, rw mem_sup at hâ‚,
  rcases hâ‚ with âŸ¨ls, hls, lt, hlt, rflâŸ©,
  have h_ls_mem_t : finsupp.total M M R id ls âˆˆ span R t,
  { rw [â† image_id t, finsupp.span_eq_map_total],
    apply (add_mem_iff_left (map _ _) (mem_image_of_mem _ hlt)).1,
    rw [â† linear_map.map_add, linear_map.mem_ker.1 hâ‚‚],
    apply zero_mem },
  have h_lt_mem_s : finsupp.total M M R id lt âˆˆ span R s,
  { rw [â† image_id s, finsupp.span_eq_map_total],
    apply (add_mem_iff_left (map _ _) (mem_image_of_mem _ hls)).1,
    rw [â† linear_map.map_add, add_comm, linear_map.mem_ker.1 hâ‚‚],
    apply zero_mem },
  have h_ls_mem_s : (finsupp.total M M R id) ls âˆˆ span R s,
  { rw â† image_id s,
    apply (finsupp.mem_span_iff_total _).2 âŸ¨ls, hls, rflâŸ© },
  have h_lt_mem_t : (finsupp.total M M R id) lt âˆˆ span R t,
  { rw â† image_id t,
    apply (finsupp.mem_span_iff_total _).2 âŸ¨lt, hlt, rflâŸ© },
  have h_ls_0 : ls = 0 :=
    disjoint_def.1 (linear_independent_subtype_disjoint.1 hs) _ hls
    (linear_map.mem_ker.2 $ disjoint_def.1 hst (finsupp.total M M R id ls) h_ls_mem_s h_ls_mem_t),
  have h_lt_0 : lt = 0 :=
    disjoint_def.1 (linear_independent_subtype_disjoint.1 ht) _ hlt
    (linear_map.mem_ker.2 $ disjoint_def.1 hst (finsupp.total M M R id lt) h_lt_mem_s h_lt_mem_t),
  show ls + lt = 0,
    by simp [h_ls_0, h_lt_0],
end

lemma linear_independent_of_finite (s : set M)
  (H : âˆ€ t âŠ† s, finite t â†’ linear_independent R (Î» x, x : t â†’ M)) :
  linear_independent R (Î» x, x : s â†’ M) :=
linear_independent_subtype.2 $
  Î» l hl, linear_independent_subtype.1 (H _ hl (finset.finite_to_set _)) l (subset.refl _)

lemma linear_independent_Union_of_directed {Î· : Type*}
  {s : Î· â†’ set M} (hs : directed (âŠ†) s)
  (h : âˆ€ i, linear_independent R (Î» x, x : s i â†’ M)) :
  linear_independent R (Î» x, x : (â‹ƒ i, s i) â†’ M) :=
begin
  haveI := classical.dec (nonempty Î·),
  by_cases hÎ· : nonempty Î·,
  { refine linear_independent_of_finite (â‹ƒ i, s i) (Î» t ht ft, _),
    rcases finite_subset_Union ft ht with âŸ¨I, fi, hIâŸ©,
    rcases hs.finset_le hÎ· fi.to_finset with âŸ¨i, hiâŸ©,
    exact (h i).mono (subset.trans hI $ bUnion_subset $
      Î» j hj, hi j (finite.mem_to_finset.2 hj)) },
  { refine linear_independent_empty.mono _,
    rintro _ âŸ¨_, âŸ¨i, _âŸ©, _âŸ©, exact hÎ· âŸ¨iâŸ© }
end

lemma linear_independent_sUnion_of_directed {s : set (set M)}
  (hs : directed_on (âŠ†) s)
  (h : âˆ€ a âˆˆ s, linear_independent R (Î» x, x : (a : set M) â†’ M)) :
  linear_independent R (Î» x, x : (â‹ƒâ‚€ s) â†’ M) :=
by rw sUnion_eq_Union; exact
linear_independent_Union_of_directed
  ((directed_on_iff_directed _).1 hs) (by simpa using h)

lemma linear_independent_bUnion_of_directed {Î·} {s : set Î·} {t : Î· â†’ set M}
  (hs : directed_on (t â»Â¹'o (âŠ†)) s) (h : âˆ€aâˆˆs, linear_independent R (Î» x, x : t a â†’ M)) :
  linear_independent R (Î» x, x : (â‹ƒaâˆˆs, t a) â†’ M) :=
by rw bUnion_eq_Union; exact
linear_independent_Union_of_directed
  ((directed_comp _ _ _).2 $ (directed_on_iff_directed _).1 hs)
  (by simpa using h)

lemma linear_independent_Union_finite_subtype {Î¹ : Type*} {f : Î¹ â†’ set M}
  (hl : âˆ€i, linear_independent R (Î» x, x : f i â†’ M))
  (hd : âˆ€i, âˆ€t:set Î¹, finite t â†’ i âˆ‰ t â†’ disjoint (span R (f i)) (â¨†iâˆˆt, span R (f i))) :
  linear_independent R (Î» x, x : (â‹ƒi, f i) â†’ M) :=
begin
  rw [Union_eq_Union_finset f],
  apply linear_independent_Union_of_directed,
  apply directed_of_sup,
  exact (assume tâ‚ tâ‚‚ ht, Union_subset_Union $ assume i, Union_subset_Union_const $ assume h, ht h),
  assume t, rw [set.Union, â† finset.sup_eq_supr],
  refine t.induction_on _ _,
  { rw finset.sup_empty,
    apply linear_independent_empty_type (not_nonempty_iff_imp_false.2 _),
    exact Î» x, set.not_mem_empty x (subtype.mem x) },
  { rintros âŸ¨iâŸ© s his ih,
    rw [finset.sup_insert],
    apply linear_independent_union,
    { apply hl },
    { apply ih },
    rw [finset.sup_eq_supr],
    refine disjoint_mono (le_refl _) _ (hd i _ _ his),
    { simp only [(span_Union _).symm],
      refine span_mono (@supr_le_supr2 (set M) _ _ _ _ _ _),
      rintros âŸ¨iâŸ©, exact âŸ¨i, le_refl _âŸ© },
    { change finite (plift.up â»Â¹' s.to_set),
      exact finite_preimage (inj_on_of_injective _ (assume i j, plift.up.inj))
        s.finite_to_set } }
end

lemma linear_independent_Union_finite {Î· : Type*} {Î¹s : Î· â†’ Type*}
  {f : Î  j : Î·, Î¹s j â†’ M}
  (hindep : âˆ€j, linear_independent R (f j))
  (hd : âˆ€i, âˆ€t:set Î·, finite t â†’ i âˆ‰ t â†’
      disjoint (span R (range (f i))) (â¨†iâˆˆt, span R (range (f i)))) :
  linear_independent R (Î» ji : Î£ j, Î¹s j, f ji.1 ji.2) :=
begin
  by_cases zero_eq_one : (0 : R) = 1,
  { apply linear_independent_of_zero_eq_one zero_eq_one },
  apply linear_independent.of_subtype_range,
  { rintros âŸ¨xâ‚, xâ‚‚âŸ© âŸ¨yâ‚, yâ‚‚âŸ© hxy,
    by_cases h_cases : xâ‚ = yâ‚,
    subst h_cases,
    { apply sigma.eq,
      rw linear_independent.injective zero_eq_one (hindep _) hxy,
      refl },
    { have h0 : f xâ‚ xâ‚‚ = 0,
      { apply disjoint_def.1 (hd xâ‚ {yâ‚} (finite_singleton yâ‚)
          (Î» h, h_cases (eq_of_mem_singleton h))) (f xâ‚ xâ‚‚) (subset_span (mem_range_self _)),
        rw supr_singleton,
        simp only [] at hxy,
        rw hxy,
        exact (subset_span (mem_range_self yâ‚‚)) },
      exact false.elim (ne_zero_of_linear_independent zero_eq_one (hindep xâ‚) h0) } },
  rw range_sigma_eq_Union_range,
  apply linear_independent_Union_finite_subtype (Î» j, (hindep j).to_subtype_range) hd,
end

end subtype

section repr
variables (hv : linear_independent R v)

/-- Canonical isomorphism between linear combinations and the span of linearly independent vectors. -/
def linear_independent.total_equiv (hv : linear_independent R v) : (Î¹ â†’â‚€ R) â‰ƒâ‚—[R] span R (range v) :=
begin
apply linear_equiv.of_bijective (linear_map.cod_restrict (span R (range v)) (finsupp.total Î¹ M R v) _),
{ rw linear_map.ker_cod_restrict,
  apply hv },
{ rw [linear_map.range, linear_map.map_cod_restrict, â† linear_map.range_le_iff_comap,
  range_subtype, map_top],
  rw finsupp.range_total,
  apply le_refl (span R (range v)) },
{ intro l,
  rw â† finsupp.range_total,
  rw linear_map.mem_range,
  apply mem_range_self l }
end

/-- Linear combination representing a vector in the span of linearly independent vectors.

   Given a family of linearly independent vectors, we can represent any vector in their span as
   a linear combination of these vectors. These are provided by this linear map.
   It is simply one direction of `linear_independent.total_equiv` -/
def linear_independent.repr (hv : linear_independent R v) :
  span R (range v) â†’â‚—[R] Î¹ â†’â‚€ R := hv.total_equiv.symm

lemma linear_independent.total_repr (x) : finsupp.total Î¹ M R v (hv.repr x) = x :=
subtype.coe_ext.1 (linear_equiv.apply_symm_apply hv.total_equiv x)

lemma linear_independent.total_comp_repr : (finsupp.total Î¹ M R v).comp hv.repr = submodule.subtype _ :=
linear_map.ext $ hv.total_repr

lemma linear_independent.repr_ker : hv.repr.ker = âŠ¥ :=
by rw [linear_independent.repr, linear_equiv.ker]

lemma linear_independent.repr_range : hv.repr.range = âŠ¤ :=
by rw [linear_independent.repr, linear_equiv.range]

lemma linear_independent.repr_eq
  {l : Î¹ â†’â‚€ R} {x} (eq : finsupp.total Î¹ M R v l = â†‘x) :
  hv.repr x = l :=
begin
  have : â†‘((linear_independent.total_equiv hv : (Î¹ â†’â‚€ R) â†’â‚—[R] span R (range v)) l)
      = finsupp.total Î¹ M R v l := rfl,
  have : (linear_independent.total_equiv hv : (Î¹ â†’â‚€ R) â†’â‚—[R] span R (range v)) l = x,
  { rw eq at this,
    exact subtype.coe_ext.2 this },
  rw â†linear_equiv.symm_apply_apply hv.total_equiv l,
  rw â†this,
  refl,
end

lemma linear_independent.repr_eq_single (i) (x) (hx : â†‘x = v i) :
  hv.repr x = finsupp.single i 1 :=
begin
  apply hv.repr_eq,
  simp [finsupp.total_single, hx]
end

lemma linear_independent_iff_not_smul_mem_span :
  linear_independent R v â†” (âˆ€ (i : Î¹) (a : R), a â€¢ (v i) âˆˆ span R (v '' (univ \ {i})) â†’ a = 0) :=
âŸ¨ Î» hv i a ha, begin
  rw [finsupp.span_eq_map_total, mem_map] at ha,
  rcases ha with âŸ¨l, hl, eâŸ©,
  rw sub_eq_zero.1 (linear_independent_iff.1 hv (l - finsupp.single i a) (by simp [e])) at hl,
  by_contra hn,
  exact (not_mem_of_mem_diff (hl $ by simp [hn])) (mem_singleton _),
end, Î» H, linear_independent_iff.2 $ Î» l hl, begin
  ext i, simp,
  by_contra hn,
  refine hn (H i _ _),
  refine (finsupp.mem_span_iff_total _).2 âŸ¨finsupp.single i (l i) - l, _, _âŸ©,
  { rw finsupp.mem_supported',
    intros j hj,
    have hij : j = i :=
      classical.not_not.1
          (Î» hij : j â‰  i, hj ((mem_diff _).2 âŸ¨mem_univ _, Î» h, hij (eq_of_mem_singleton h)âŸ©)),
    simp [hij] },
  { simp [hl] }
endâŸ©

end repr

lemma surjective_of_linear_independent_of_span
  (hv : linear_independent R v) (f : Î¹' â†ª Î¹)
  (hss : range v âŠ† span R (range (v âˆ˜ f))) (zero_ne_one : 0 â‰  (1 : R)):
  surjective f :=
begin
  intros i,
  let repr : (span R (range (v âˆ˜ f)) : Type*) â†’ Î¹' â†’â‚€ R := (hv.comp f f.inj).repr,
  let l := (repr âŸ¨v i, hss (mem_range_self i)âŸ©).map_domain f,
  have h_total_l : finsupp.total Î¹ M R v l = v i,
  { dsimp only [l],
    rw finsupp.total_map_domain,
    rw (hv.comp f f.inj).total_repr,
    { refl },
    { exact f.inj } },
  have h_total_eq : (finsupp.total Î¹ M R v) l = (finsupp.total Î¹ M R v) (finsupp.single i 1),
    by rw [h_total_l, finsupp.total_single, one_smul],
  have l_eq : l = _ := linear_map.ker_eq_bot.1 hv h_total_eq,
  dsimp only [l] at l_eq,
  rw â†finsupp.emb_domain_eq_map_domain at l_eq,
  rcases finsupp.single_of_emb_domain_single (repr âŸ¨v i, _âŸ©) f i (1 : R) zero_ne_one.symm l_eq with âŸ¨i', hi'âŸ©,
  use i',
  exact hi'.2
end

lemma eq_of_linear_independent_of_span_subtype {s t : set M} (zero_ne_one : (0 : R) â‰  1)
  (hs : linear_independent R (Î» x, x : s â†’ M)) (h : t âŠ† s) (hst : s âŠ† span R t) : s = t :=
begin
  let f : t â†ª s := âŸ¨Î» x, âŸ¨x.1, h x.2âŸ©, Î» a b hab, subtype.val_injective (subtype.mk.inj hab)âŸ©,
  have h_surj : surjective f,
  { apply surjective_of_linear_independent_of_span hs f _ zero_ne_one,
    convert hst; simp [f, comp], },
  show s = t,
  { apply subset.antisymm _ h,
    intros x hx,
    rcases h_surj âŸ¨x, hxâŸ© with âŸ¨y, hyâŸ©,
    convert y.mem,
    rw â† subtype.mk.inj hy,
    refl }
end

open linear_map

lemma linear_independent.image (hv : linear_independent R v) {f : M â†’â‚— M'}
  (hf_inj : disjoint (span R (range v)) f.ker) : linear_independent R (f âˆ˜ v) :=
begin
  rw [disjoint, â† set.image_univ, finsupp.span_eq_map_total, map_inf_eq_map_inf_comap,
    map_le_iff_le_comap, comap_bot, finsupp.supported_univ, top_inf_eq] at hf_inj,
  unfold linear_independent at hv,
  rw hv at hf_inj,
  haveI : inhabited M := âŸ¨0âŸ©,
  rw [linear_independent, finsupp.total_comp],
  rw [@finsupp.lmap_domain_total _ _ R _ _ _ _ _ _ _ _ _ _ f, ker_comp, eq_bot_iff],
  apply hf_inj,
  exact Î» _, rfl,
end

lemma linear_independent.image_subtype {s : set M} {f : M â†’â‚— M'} (hs : linear_independent R (Î» x, x : s â†’ M))
  (hf_inj : disjoint (span R s) f.ker) : linear_independent R (Î» x, x : f '' s â†’ M') :=
begin
  rw [disjoint, â† set.image_id s, finsupp.span_eq_map_total, map_inf_eq_map_inf_comap,
    map_le_iff_le_comap, comap_bot] at hf_inj,
  haveI : inhabited M := âŸ¨0âŸ©,
  rw [linear_independent_subtype_disjoint, disjoint, â† finsupp.lmap_domain_supported _ _ f, map_inf_eq_map_inf_comap,
      map_le_iff_le_comap, â† ker_comp],
  rw [@finsupp.lmap_domain_total _ _ R _ _ _, ker_comp],
  { exact le_trans (le_inf inf_le_left hf_inj) (le_trans (linear_independent_subtype_disjoint.1 hs) bot_le) },
  { simp }
end

lemma linear_independent_inl_union_inr {s : set M} {t : set M'}
  (hs : linear_independent R (Î» x, x : s â†’ M))
  (ht : linear_independent R (Î» x, x : t â†’ M')) :
  linear_independent R (Î» x, x : inl R M M' '' s âˆª inr R M M' '' t â†’ M Ã— M') :=
begin
  apply linear_independent_union,
  exact (hs.image_subtype $ by simp),
  exact (ht.image_subtype $ by simp),
  rw [span_image, span_image];
    simp [disjoint_iff, prod_inf_prod]
end

lemma linear_independent_inl_union_inr' {v : Î¹ â†’ M} {v' : Î¹' â†’ M'}
  (hv : linear_independent R v) (hv' : linear_independent R v') :
  linear_independent R (sum.elim (inl R M M' âˆ˜ v) (inr R M M' âˆ˜ v')) :=
begin
  by_cases zero_eq_one : (0 : R) = 1,
  { apply linear_independent_of_zero_eq_one zero_eq_one },
  have inj_v : injective v := (linear_independent.injective zero_eq_one hv),
  have inj_v' : injective v' := (linear_independent.injective zero_eq_one hv'),
  apply linear_independent.of_subtype_range,
  { apply sum.elim_injective,
    { exact injective_comp prod.injective_inl inj_v },
    { exact injective_comp prod.injective_inr inj_v' },
    { intros, simp [ne_zero_of_linear_independent zero_eq_one hv] } },
  { rw sum.elim_range,
    apply linear_independent_union,
    { apply linear_independent.to_subtype_range,
      apply linear_independent.image hv,
      simp [ker_inl] },
    { apply linear_independent.to_subtype_range,
      apply linear_independent.image hv',
      simp [ker_inr] },
    { apply disjoint_mono _ _ disjoint_inl_inr,
      { rw [set.range_comp, span_image],
        apply linear_map.map_le_range },
      { rw [set.range_comp, span_image],
        apply linear_map.map_le_range } } }
end

lemma le_of_span_le_span {s t u: set M} (zero_ne_one : (0 : R) â‰  1)
  (hl : linear_independent R (subtype.val : u â†’ M )) (hsu : s âŠ† u) (htu : t âŠ† u)
  (hst : span R s â‰¤ span R t) : s âŠ† t :=
begin
  have := eq_of_linear_independent_of_span_subtype zero_ne_one
    (hl.mono (set.union_subset hsu htu))
    (set.subset_union_right _ _)
    (set.union_subset (set.subset.trans subset_span hst) subset_span),
  rw â† this, apply set.subset_union_left
end

lemma span_le_span_iff {s t u: set M} (zero_ne_one : (0 : R) â‰  1)
  (hl : linear_independent R (subtype.val : u â†’ M )) (hsu : s âŠ† u) (htu : t âŠ† u) :
  span R s â‰¤ span R t â†” s âŠ† t :=
âŸ¨le_of_span_le_span zero_ne_one hl hsu htu, span_monoâŸ©

variables (R) (v)
/-- A family of vectors is a basis if it is linearly independent and all vectors are in the span. -/
def is_basis := linear_independent R v âˆ§ span R (range v) = âŠ¤
variables {R} {v}

section is_basis
variables {s t : set M} (hv : is_basis R v)

lemma is_basis.mem_span (hv : is_basis R v) : âˆ€ x, x âˆˆ span R (range v) := eq_top_iff'.1 hv.2

lemma is_basis.comp (hv : is_basis R v) (f : Î¹' â†’ Î¹) (hf : bijective f) :
  is_basis R (v âˆ˜ f) :=
begin
  split,
  { apply hv.1.comp f hf.1 },
  { rw[set.range_comp, range_iff_surjective.2 hf.2, image_univ, hv.2] }
end

lemma is_basis.injective (hv : is_basis R v) (zero_ne_one : (0 : R) â‰  1) : injective v :=
  Î» x y h, linear_independent.injective zero_ne_one hv.1 h

/- Given a basis, any vector can be written as a linear combination of the basis vectors. They are
   given by this linear map. This is one direction of `module_equiv_finsupp` -/
def is_basis.repr : M â†’â‚— (Î¹ â†’â‚€ R) :=
(hv.1.repr).comp (linear_map.id.cod_restrict _ hv.mem_span)

lemma is_basis.total_repr (x) : finsupp.total Î¹ M R v (hv.repr x) = x :=
hv.1.total_repr âŸ¨x, _âŸ©

lemma is_basis.total_comp_repr : (finsupp.total Î¹ M R v).comp hv.repr = linear_map.id :=
linear_map.ext hv.total_repr

lemma is_basis.repr_ker : hv.repr.ker = âŠ¥ :=
linear_map.ker_eq_bot.2 $ injective_of_left_inverse hv.total_repr

lemma is_basis.repr_range : hv.repr.range = finsupp.supported R R univ :=
by rw [is_basis.repr, linear_map.range, submodule.map_comp,
  linear_map.map_cod_restrict, submodule.map_id, comap_top, map_top, hv.1.repr_range,
  finsupp.supported_univ]

lemma is_basis.repr_total (x : Î¹ â†’â‚€ R) (hx : x âˆˆ finsupp.supported R R (univ : set Î¹)) :
  hv.repr (finsupp.total Î¹ M R v x) = x :=
begin
  rw [â† hv.repr_range, linear_map.mem_range] at hx,
  cases hx with w hw,
  rw [â† hw, hv.total_repr],
end

lemma is_basis.repr_eq_single {i} : hv.repr (v i) = finsupp.single i 1 :=
by apply hv.1.repr_eq_single; simp

/-- Construct a linear map given the value at the basis. -/
def is_basis.constr (f : Î¹ â†’ M') : M â†’â‚—[R] M' :=
(finsupp.total M' M' R id).comp $ (finsupp.lmap_domain R R f).comp hv.repr

theorem is_basis.constr_apply (f : Î¹ â†’ M') (x : M) :
  (hv.constr f : M â†’ M') x = (hv.repr x).sum (Î»b a, a â€¢ f b) :=
by dsimp [is_basis.constr];
   rw [finsupp.total_apply, finsupp.sum_map_domain_index]; simp [add_smul]

lemma is_basis.ext {f g : M â†’â‚—[R] M'} (hv : is_basis R v) (h : âˆ€i, f (v i) = g (v i)) : f = g :=
begin
  apply linear_map.ext (Î» x, linear_eq_on (range v) _ (hv.mem_span x)),
  exact (Î» y hy, exists.elim (set.mem_range.1 hy) (Î» i hi, by rw â†hi; exact h i))
end

lemma constr_basis {f : Î¹ â†’ M'} {i : Î¹} (hv : is_basis R v) :
  (hv.constr f : M â†’ M') (v i) = f i :=
by simp [is_basis.constr_apply, hv.repr_eq_single, finsupp.sum_single_index]

lemma constr_eq {g : Î¹ â†’ M'} {f : M â†’â‚—[R] M'} (hv : is_basis R v)
  (h : âˆ€i, g i = f (v i)) : hv.constr g = f :=
hv.ext $ Î» i, (constr_basis hv).trans (h i)

lemma constr_self (f : M â†’â‚—[R] M') : hv.constr (Î» i, f (v i)) = f :=
constr_eq hv $ Î» x, rfl

lemma constr_zero (hv : is_basis R v) : hv.constr (Î»i, (0 : M')) = 0 :=
constr_eq hv $ Î» x, rfl

lemma constr_add {g f : Î¹ â†’ M'} (hv : is_basis R v) :
  hv.constr (Î»i, f i + g i) = hv.constr f + hv.constr g :=
constr_eq hv $ by simp [constr_basis hv] {contextual := tt}

lemma constr_neg {f : Î¹ â†’ M'} (hv : is_basis R v) : hv.constr (Î»i, - f i) = - hv.constr f :=
constr_eq hv $ by simp [constr_basis hv] {contextual := tt}

lemma constr_sub {g f : Î¹ â†’ M'} (hs : is_basis R v) :
  hv.constr (Î»i, f i - g i) = hs.constr f - hs.constr g :=
by simp [constr_add, constr_neg]

-- this only works on functions if `R` is a commutative ring
lemma constr_smul {Î¹ R M M'} [comm_ring R]
  [add_comm_group M] [add_comm_group M'] [module R M] [module R M']
  {v : Î¹ â†’ R} {f : Î¹ â†’ M'} {a : R} (hv : is_basis R v) {b : M} :
  hv.constr (Î»b, a â€¢ f b) = a â€¢ hv.constr f :=
constr_eq hv $ by simp [constr_basis hv] {contextual := tt}

lemma constr_range [inhabited Î¹] (hv : is_basis R v) {f : Î¹  â†’ M'} :
  (hv.constr f).range = span R (range f) :=
by rw [is_basis.constr, linear_map.range_comp, linear_map.range_comp, is_basis.repr_range,
    finsupp.lmap_domain_supported, â†set.image_univ, â†finsupp.span_eq_map_total, image_id]

/-- Canonical equivalence between a module and the linear combinations of basis vectors. -/
def module_equiv_finsupp (hv : is_basis R v) : M â‰ƒâ‚—[R] Î¹ â†’â‚€ R :=
(hv.1.total_equiv.trans (linear_equiv.of_top _ hv.2)).symm

/-- Isomorphism between the two modules, given two modules M and M' with respective bases v and v'
   and a bijection between the two bases. -/
def equiv_of_is_basis {v : Î¹ â†’ M} {v' : Î¹' â†’ M'} {f : M â†’ M'} {g : M' â†’ M}
  (hv : is_basis R v) (hv' : is_basis R v') (hf : âˆ€i, f (v i) âˆˆ range v') (hg : âˆ€i, g (v' i) âˆˆ range v)
  (hgf : âˆ€i, g (f (v i)) = v i) (hfg : âˆ€i, f (g (v' i)) = v' i) :
  M â‰ƒâ‚— M' :=
{ inv_fun := hv'.constr (g âˆ˜ v'),
  left_inv :=
    have (hv'.constr (g âˆ˜ v')).comp (hv.constr (f âˆ˜ v)) = linear_map.id,
    from hv.ext $ Î» i, exists.elim (hf i) (Î» i' hi', by simp [constr_basis, hi'.symm]; rw [hi', hgf]),
    Î» x, congr_arg (Î» h:M â†’â‚—[R] M, h x) this,
  right_inv :=
    have (hv.constr (f âˆ˜ v)).comp (hv'.constr (g âˆ˜ v')) = linear_map.id,
    from hv'.ext $ Î» i', exists.elim (hg i') (Î» i hi, by simp [constr_basis, hi.symm]; rw [hi, hfg]),
    Î» y, congr_arg (Î» h:M' â†’â‚—[R] M', h y) this,
  ..hv.constr (f âˆ˜ v) }

lemma is_basis_inl_union_inr {v : Î¹ â†’ M} {v' : Î¹' â†’ M'}
  (hv : is_basis R v) (hv' : is_basis R v') : is_basis R (sum.elim (inl R M M' âˆ˜ v) (inr R M M' âˆ˜ v')) :=
begin
  split,
  apply linear_independent_inl_union_inr' hv.1 hv'.1,
  rw [sum.elim_range, span_union,
      set.range_comp, span_image (inl R M M'), hv.2,  map_top,
      set.range_comp, span_image (inr R M M'), hv'.2, map_top],
  exact linear_map.sup_range_inl_inr
end

end is_basis

lemma is_basis_singleton_one (R : Type*) [unique Î¹] [ring R] :
  is_basis R (Î» (_ : Î¹), (1 : R)) :=
begin
  split,
  { refine linear_independent_iff.2 (Î» l, _),
    rw [finsupp.unique_single l, finsupp.total_single, smul_eq_mul, mul_one],
    intro hi,
    simp [hi] },
  { refine top_unique (Î» _ _, _),
    simp [submodule.mem_span_singleton] }
end

lemma linear_equiv.is_basis (hs : is_basis R v)
  (f : M â‰ƒâ‚—[R] M') : is_basis R (f âˆ˜ v) :=
begin
  split,
  { apply @linear_independent.image _ _ _ _ _ _ _ _ _ _ hs.1 (f : M â†’â‚—[R] M'),
    simp [linear_equiv.ker f] },
  { rw set.range_comp,
    have : span R ((f : M â†’â‚—[R] M') '' range v) = âŠ¤,
    { rw [span_image (f : M â†’â‚—[R] M'), hs.2],
      simp },
    exact this }
end

lemma is_basis_span (hs : linear_independent R v) :
  @is_basis Î¹ R (span R (range v)) (Î» i : Î¹, âŸ¨v i, subset_span (mem_range_self _)âŸ©) _ _ _ :=
begin
split,
{ apply linear_independent_span hs },
{ rw eq_top_iff',
  intro x,
  have hâ‚ : subtype.val '' set.range (Î» i, subtype.mk (v i) _) = range v,
    by rw â†set.range_comp,
  have hâ‚‚ : map (submodule.subtype _) (span R (set.range (Î» i, subtype.mk (v i) _)))
              = span R (range v),
    by rw [â†span_image, submodule.subtype_eq_val, hâ‚],
  have hâ‚ƒ : (x : M) âˆˆ map (submodule.subtype _) (span R (set.range (Î» i, subtype.mk (v i) _))),
    by rw hâ‚‚; apply subtype.mem x,
  rcases mem_map.1 hâ‚ƒ with âŸ¨y, hyâ‚, hyâ‚‚âŸ©,
  have h_x_eq_y : x = y,
    by rw [subtype.coe_ext, â† hyâ‚‚]; simp,
  rw h_x_eq_y,
  exact hyâ‚ }
end

lemma is_basis_empty (h_empty : Â¬ nonempty Î¹) (h : âˆ€x:M, x = 0) : is_basis R (Î» x : Î¹, (0 : M)) :=
âŸ¨ linear_independent_empty_type h_empty,
  eq_top_iff'.2 $ assume x, (h x).symm â–¸ submodule.zero_mem _ âŸ©

lemma is_basis_empty_bot (h_empty : Â¬ nonempty Î¹) : is_basis R (Î» _ : Î¹, (0 : (âŠ¥ : submodule R M))) :=
begin
  apply is_basis_empty h_empty,
  intro x,
  apply subtype.ext.2,
  exact (submodule.mem_bot R).1 (subtype.mem x),
end

open fintype
variables [fintype Î¹] (h : is_basis R v)

/-- A module over R with a finite basis is linearly equivalent to functions from its basis to R. -/
def equiv_fun_basis  : M â‰ƒâ‚—[R] (Î¹ â†’ R) :=
linear_equiv.trans (module_equiv_finsupp h)
  { to_fun := finsupp.to_fun,
    add := Î» x y, by ext; exact finsupp.add_apply,
    smul := Î» x y, by ext; exact finsupp.smul_apply,
    ..finsupp.equiv_fun_on_fintype }

theorem module.card_fintype [fintype R] [fintype M] :
  card M = (card R) ^ (card Î¹) :=
calc card M = card (Î¹ â†’ R)    : card_congr (equiv_fun_basis h).to_equiv
        ... = card R ^ card Î¹ : card_fun

end module

section vector_space
variables
  {v : Î¹ â†’ V}
  [discrete_field ğ•œ] [add_comm_group V] [add_comm_group V']
  [vector_space ğ•œ V] [vector_space ğ•œ V']
  {s t : set V} {x y z : V}
include ğ•œ
open submodule

/- TODO: some of the following proofs can generalized with a zero_ne_one predicate type class
   (instead of a data containing type class) -/

section
set_option class.instance_max_depth 36

lemma mem_span_insert_exchange : x âˆˆ span ğ•œ (insert y s) â†’ x âˆ‰ span ğ•œ s â†’ y âˆˆ span ğ•œ (insert x s) :=
begin
  simp [mem_span_insert],
  rintro a z hz rfl h,
  refine âŸ¨aâ»Â¹, -aâ»Â¹ â€¢ z, smul_mem _ _ hz, _âŸ©,
  have a0 : a â‰  0, {rintro rfl, simp * at *},
  simp [a0, smul_add, smul_smul]
end

end

lemma linear_independent_iff_not_mem_span : linear_independent ğ•œ v â†” (âˆ€i, v i âˆ‰ span ğ•œ (v '' (univ \ {i}))) :=
begin
  apply linear_independent_iff_not_smul_mem_span.trans,
  split,
  { intros h i h_in_span,
    apply one_ne_zero (h i 1 (by simp [h_in_span])) },
  { intros h i a ha,
    by_contradiction ha',
    exact false.elim (h _ ((smul_mem_iff _ ha').1 ha)) }
end

lemma linear_independent_unique [unique Î¹] (h : v (default Î¹) â‰  0): linear_independent ğ•œ v :=
begin
  rw linear_independent_iff,
  intros l hl,
  ext i,
  rw [unique.eq_default i, finsupp.zero_apply],
  by_contra hc,
  have := smul_smul _ (l (default Î¹))â»Â¹ (l (default Î¹)) (v (default Î¹)),
  rw [finsupp.unique_single l, finsupp.total_single] at hl,
  rw [hl, inv_mul_cancel hc, smul_zero, one_smul] at this,
  exact h this.symm
end

lemma linear_independent_singleton {x : V} (hx : x â‰  0) : linear_independent ğ•œ (Î» x, x : ({x} : set V) â†’ V) :=
begin
  apply @linear_independent_unique _ _ _ _ _ _ _ _ _,
  apply set.unique_singleton,
  apply hx,
end

lemma disjoint_span_singleton {p : submodule ğ•œ V} {x : V} (x0 : x â‰  0) :
  disjoint p (span ğ•œ {x}) â†” x âˆ‰ p :=
âŸ¨Î» H xp, x0 (disjoint_def.1 H _ xp (singleton_subset_iff.1 subset_span:_)),
begin
  simp [disjoint_def, mem_span_singleton],
  rintro xp y yp a rfl,
  by_cases a0 : a = 0, {simp [a0]},
  exact xp.elim ((smul_mem_iff p a0).1 yp),
endâŸ©

lemma linear_independent.insert (hs : linear_independent ğ•œ (Î» b, b : s â†’ V)) (hx : x âˆ‰ span ğ•œ s) :
  linear_independent ğ•œ (Î» b, b : insert x s â†’ V) :=
begin
  rw â† union_singleton,
  have x0 : x â‰  0 := mt (by rintro rfl; apply zero_mem _) hx,
  apply linear_independent_union hs (linear_independent_singleton x0),
  rwa [disjoint_span_singleton x0]
end

lemma exists_linear_independent (hs : linear_independent ğ•œ (Î» x, x : s â†’ V)) (hst : s âŠ† t) :
  âˆƒbâŠ†t, s âŠ† b âˆ§ t âŠ† span ğ•œ b âˆ§ linear_independent ğ•œ (Î» x, x : b â†’ V) :=
begin
  rcases zorn.zorn_subsetâ‚€ {b | b âŠ† t âˆ§ linear_independent ğ•œ (Î» x, x : b â†’ V)} _ _
    âŸ¨hst, hsâŸ© with âŸ¨b, âŸ¨bt, biâŸ©, sb, hâŸ©,
  { refine âŸ¨b, bt, sb, Î» x xt, _, biâŸ©,
    haveI := classical.dec (x âˆˆ span ğ•œ b),
    by_contra hn,
    apply hn,
    rw â† h _ âŸ¨insert_subset.2 âŸ¨xt, btâŸ©, bi.insert hnâŸ© (subset_insert _ _),
    exact subset_span (mem_insert _ _) },
  { refine Î» c hc cc c0, âŸ¨â‹ƒâ‚€ c, âŸ¨_, _âŸ©, Î» x, _âŸ©,
    { exact sUnion_subset (Î» x xc, (hc xc).1) },
    { exact linear_independent_sUnion_of_directed cc.directed_on (Î» x xc, (hc xc).2) },
    { exact subset_sUnion_of_mem } }
end

lemma exists_subset_is_basis (hs : linear_independent ğ•œ (Î» x, x : s â†’ V)) :
  âˆƒb, s âŠ† b âˆ§ is_basis ğ•œ (Î» i : b, i.val) :=
let âŸ¨b, hbâ‚€, hx, hbâ‚‚, hbâ‚ƒâŸ© := exists_linear_independent hs (@subset_univ _ _) in
âŸ¨ b, hx,
  @linear_independent.restrict_of_comp_subtype _ _ _ id _ _ _ _ hbâ‚ƒ,
  by simp; exact eq_top_iff.2 hbâ‚‚âŸ©

variables (ğ•œ V)
lemma exists_is_basis : âˆƒb : set V, is_basis ğ•œ (Î» i : b, i.val) :=
let âŸ¨b, _, hbâŸ© := exists_subset_is_basis linear_independent_empty in âŸ¨b, hbâŸ©

variables {ğ•œ V}

-- TODO(Mario): rewrite?
lemma exists_of_linear_independent_of_finite_span {t : finset V}
  (hs : linear_independent ğ•œ (Î» x, x : s â†’ V)) (hst : s âŠ† (span ğ•œ â†‘t : submodule ğ•œ V)) :
  âˆƒt':finset V, â†‘t' âŠ† s âˆª â†‘t âˆ§ s âŠ† â†‘t' âˆ§ t'.card = t.card :=
have âˆ€t, âˆ€(s' : finset V), â†‘s' âŠ† s â†’ s âˆ© â†‘t = âˆ… â†’ s âŠ† (span ğ•œ â†‘(s' âˆª t) : submodule ğ•œ V) â†’
  âˆƒt':finset V, â†‘t' âŠ† s âˆª â†‘t âˆ§ s âŠ† â†‘t' âˆ§ t'.card = (s' âˆª t).card :=
assume t, finset.induction_on t
  (assume s' hs' _ hss',
    have s = â†‘s',
      from eq_of_linear_independent_of_span_subtype (@zero_ne_one ğ•œ _) hs hs' $
          by simpa using hss',
    âŸ¨s', by simp [this]âŸ©)
  (assume bâ‚ t hbâ‚t ih s' hs' hst hss',
    have hbâ‚s : bâ‚ âˆ‰ s,
      from assume h,
      have bâ‚ âˆˆ s âˆ© â†‘(insert bâ‚ t), from âŸ¨h, finset.mem_insert_self _ _âŸ©,
      by rwa [hst] at this,
    have hbâ‚s' : bâ‚ âˆ‰ s', from assume h, hbâ‚s $ hs' h,
    have hst : s âˆ© â†‘t = âˆ…,
      from eq_empty_of_subset_empty $ subset.trans
        (by simp [inter_subset_inter, subset.refl]) (le_of_eq hst),
    classical.by_cases
      (assume : s âŠ† (span ğ•œ â†‘(s' âˆª t) : submodule ğ•œ V),
        let âŸ¨u, hust, hsu, eqâŸ© := ih _ hs' hst this in
        have hbâ‚u : bâ‚ âˆ‰ u, from assume h, (hust h).elim hbâ‚s hbâ‚t,
        âŸ¨insert bâ‚ u, by simp [insert_subset_insert hust],
          subset.trans hsu (by simp), by simp [eq, hbâ‚t, hbâ‚s', hbâ‚u]âŸ©)
      (assume : Â¬ s âŠ† (span ğ•œ â†‘(s' âˆª t) : submodule ğ•œ V),
        let âŸ¨bâ‚‚, hbâ‚‚s, hbâ‚‚tâŸ© := not_subset.mp this in
        have hbâ‚‚t' : bâ‚‚ âˆ‰ s' âˆª t, from assume h, hbâ‚‚t $ subset_span h,
        have s âŠ† (span ğ•œ â†‘(insert bâ‚‚ s' âˆª t) : submodule ğ•œ V), from
          assume bâ‚ƒ hbâ‚ƒ,
          have â†‘(s' âˆª insert bâ‚ t) âŠ† insert bâ‚ (insert bâ‚‚ â†‘(s' âˆª t) : set V),
            by simp [insert_eq, -singleton_union, -union_singleton, union_subset_union, subset.refl, subset_union_right],
          have hbâ‚ƒ : bâ‚ƒ âˆˆ span ğ•œ (insert bâ‚ (insert bâ‚‚ â†‘(s' âˆª t) : set V)),
            from span_mono this (hss' hbâ‚ƒ),
          have s âŠ† (span ğ•œ (insert bâ‚ â†‘(s' âˆª t)) : submodule ğ•œ V),
            by simpa [insert_eq, -singleton_union, -union_singleton] using hss',
          have hbâ‚ : bâ‚ âˆˆ span ğ•œ (insert bâ‚‚ â†‘(s' âˆª t)),
            from mem_span_insert_exchange (this hbâ‚‚s) hbâ‚‚t,
          by rw [span_insert_eq_span hbâ‚] at hbâ‚ƒ; simpa using hbâ‚ƒ,
        let âŸ¨u, hust, hsu, eqâŸ© := ih _ (by simp [insert_subset, hbâ‚‚s, hs']) hst this in
        âŸ¨u, subset.trans hust $ union_subset_union (subset.refl _) (by simp [subset_insert]),
          hsu, by rw [finset.union_comm] at hbâ‚‚t'; simp [eq, hbâ‚‚t', hbâ‚t, hbâ‚s']âŸ©)),
begin
  letI := classical.dec_pred (Î»x, x âˆˆ s),
  have eq : t.filter (Î»x, x âˆˆ s) âˆª t.filter (Î»x, x âˆ‰ s) = t,
  { apply finset.ext.mpr,
    intro x,
    by_cases x âˆˆ s; simp *, finish },
  apply exists.elim (this (t.filter (Î»x, x âˆ‰ s)) (t.filter (Î»x, x âˆˆ s))
    (by simp [set.subset_def]) (by simp [set.ext_iff] {contextual := tt}) (by rwa [eq])),
  intros u h,
  exact âŸ¨u, subset.trans h.1 (by simp [subset_def, and_imp, or_imp_distrib] {contextual:=tt}),
    h.2.1, by simp only [h.2.2, eq]âŸ©
end

lemma exists_finite_card_le_of_finite_of_linear_independent_of_span
  (ht : finite t) (hs : linear_independent ğ•œ (Î» x, x : s â†’ V)) (hst : s âŠ† span ğ•œ t) :
  âˆƒh : finite s, h.to_finset.card â‰¤ ht.to_finset.card :=
have s âŠ† (span ğ•œ â†‘(ht.to_finset) : submodule ğ•œ V), by simp; assumption,
let âŸ¨u, hust, hsu, eqâŸ© := exists_of_linear_independent_of_finite_span hs this in
have finite s, from finite_subset u.finite_to_set hsu,
âŸ¨this, by rw [â†eq]; exact (finset.card_le_of_subset $ finset.coe_subset.mp $ by simp [hsu])âŸ©

lemma exists_left_inverse_linear_map_of_injective {f : V â†’â‚—[ğ•œ] V'}
  (hf_inj : f.ker = âŠ¥) : âˆƒg:V' â†’â‚— V, g.comp f = linear_map.id :=
begin
  rcases exists_is_basis ğ•œ V with âŸ¨B, hBâŸ©,
  have hBâ‚€ : _ := hB.1.to_subtype_range,
  have : linear_independent ğ•œ (Î» x, x : f '' B â†’ V'),
  { have hâ‚ := hBâ‚€.image_subtype (show disjoint (span ğ•œ (range (Î» i : B, i.val))) (linear_map.ker f), by simp [hf_inj]),
    have hâ‚‚ : range (Î» (i : B), i.val) = B := subtype.range_val B,
    rwa hâ‚‚ at hâ‚ },
  rcases exists_subset_is_basis this with âŸ¨C, BC, hCâŸ©,
  haveI : inhabited V := âŸ¨0âŸ©,
  use hC.constr (function.restrict (inv_fun f) C : C â†’ V),
  apply @is_basis.ext _ _ _ _ _ _ _ _ _ _ _ _ hB,
  intros b,
  rw image_subset_iff at BC,
  simp,
  have := BC (subtype.mem b),
  rw mem_preimage at this,
  have : f (b.val) = (subtype.mk (f â†‘b) (begin rw â†mem_preimage, exact BC (subtype.mem b) end) : C).val,
    by simp; unfold_coes,
  rw this,
  rw [constr_basis hC],
  exact left_inverse_inv_fun (linear_map.ker_eq_bot.1 hf_inj) _,
end

lemma exists_right_inverse_linear_map_of_surjective {f : V â†’â‚—[ğ•œ] V'}
  (hf_surj : f.range = âŠ¤) : âˆƒg:V' â†’â‚— V, f.comp g = linear_map.id :=
begin
  rcases exists_is_basis ğ•œ V' with âŸ¨C, hCâŸ©,
  haveI : inhabited V := âŸ¨0âŸ©,
  use hC.constr (function.restrict (inv_fun f) C : C â†’ V),
  apply @is_basis.ext _ _ _ _ _ _ _ _ _ _ _ _ hC,
  intros c,
  simp [constr_basis hC],
  exact right_inverse_inv_fun (linear_map.range_eq_top.1 hf_surj) _
end

set_option class.instance_max_depth 49
open submodule linear_map
theorem quotient_prod_linear_equiv (p : submodule ğ•œ V) :
  nonempty ((p.quotient Ã— p) â‰ƒâ‚—[ğ•œ] V) :=
begin
  haveI := classical.dec_eq (quotient p),
  rcases exists_right_inverse_linear_map_of_surjective p.range_mkq with âŸ¨f, hfâŸ©,
  have mkf : âˆ€ x, submodule.quotient.mk (f x) = x := linear_map.ext_iff.1 hf,
  have fp : âˆ€ x, x - f (p.mkq x) âˆˆ p :=
    Î» x, (submodule.quotient.eq p).1 (mkf (p.mkq x)).symm,
  refine âŸ¨linear_equiv.of_linear (f.copair p.subtype)
    (p.mkq.pair (cod_restrict p (linear_map.id - f.comp p.mkq) fp))
    (by ext; simp) _âŸ©,
  ext âŸ¨âŸ¨xâŸ©, y, hyâŸ©; simp,
  { apply (submodule.quotient.eq p).2,
    simpa using sub_mem p hy (fp x) },
  { refine subtype.coe_ext.2 _,
    simp [mkf, (submodule.quotient.mk_eq_zero p).2 hy] }
end

open fintype

theorem vector_space.card_fintype [fintype ğ•œ] [fintype V] :
  âˆƒ n : â„•, card V = (card ğ•œ) ^ n :=
begin
  apply exists.elim (exists_is_basis ğ•œ V),
  intros b hb,
  haveI := classical.dec_pred (Î» x, x âˆˆ b),
  use card b,
  exact module.card_fintype hb,
end

end vector_space

namespace pi
open set linear_map

section module
variables {Î· : Type*} {Î¹s : Î· â†’ Type*} {Ms : Î· â†’ Type*}
variables [ring R] [âˆ€i, add_comm_group (Ms i)] [âˆ€i, module R (Ms i)] [fintype Î·]

lemma linear_independent_std_basis
  (v : Î j, Î¹s j â†’ (Ms j)) (hs : âˆ€i, linear_independent R (v i)) :
  linear_independent R (Î» (ji : Î£ j, Î¹s j), std_basis R Ms ji.1 (v ji.1 ji.2)) :=
begin
  have hs' : âˆ€j : Î·, linear_independent R (Î» i : Î¹s j, std_basis R Ms j (v j i)),
  { intro j,
    apply linear_independent.image (hs j),
    simp [ker_std_basis] },
  apply linear_independent_Union_finite hs',
  { assume j J _ hiJ,
    simp [(set.Union.equations._eqn_1 _).symm, submodule.span_image, submodule.span_Union],
    have hâ‚€ : âˆ€ j, span R (range (Î» (i : Î¹s j), std_basis R Ms j (v j i)))
        â‰¤ range (std_basis R Ms j),
    { intro j,
      rw [span_le, linear_map.range_coe],
      apply range_comp_subset_range },
    have hâ‚ : span R (range (Î» (i : Î¹s j), std_basis R Ms j (v j i)))
        â‰¤ â¨† i âˆˆ {j}, range (std_basis R Ms i),
    { rw @supr_singleton _ _ _ (Î» i, linear_map.range (std_basis R (Î» (j : Î·), Ms j) i)),
      apply hâ‚€ },
    have hâ‚‚ : (â¨† j âˆˆ J, span R (range (Î» (i : Î¹s j), std_basis R Ms j (v j i)))) â‰¤
               â¨† j âˆˆ J, range (std_basis R (Î» (j : Î·), Ms j) j) :=
      supr_le_supr (Î» i, supr_le_supr (Î» H, hâ‚€ i)),
    have hâ‚ƒ : disjoint (Î» (i : Î·), i âˆˆ {j}) J,
    { convert set.disjoint_singleton_left.2 hiJ,
      rw â†@set_of_mem_eq _ {j},
      refl },
    refine disjoint_mono hâ‚ hâ‚‚
      (disjoint_std_basis_std_basis _ _ _ _ hâ‚ƒ), }
end

lemma is_basis_std_basis (s : Î j, Î¹s j â†’ (Ms j)) (hs : âˆ€j, is_basis R (s j)) :
  is_basis R (Î» (ji : Î£ j, Î¹s j), std_basis R Ms ji.1 (s ji.1 ji.2)) :=
begin
  split,
  { apply linear_independent_std_basis _ (assume i, (hs i).1) },
  have hâ‚ : Union (Î» j, set.range (std_basis R Ms j âˆ˜ s j))
    âŠ† range (Î» (ji : Î£ (j : Î·), Î¹s j), (std_basis R Ms (ji.fst)) (s (ji.fst) (ji.snd))),
  { apply Union_subset, intro i,
    apply range_comp_subset_range (Î» x : Î¹s i, (âŸ¨i, xâŸ© : Î£ (j : Î·), Î¹s j))
        (Î» (ji : Î£ (j : Î·), Î¹s j), std_basis R Ms (ji.fst) (s (ji.fst) (ji.snd))) },
  have hâ‚‚ : âˆ€ i, span R (range (std_basis R Ms i âˆ˜ s i)) = range (std_basis R Ms i),
  { intro i,
    rw [set.range_comp, submodule.span_image, (assume i, (hs i).2), submodule.map_top] },
  apply eq_top_mono,
  apply span_mono hâ‚,
  rw span_Union,
  simp only [hâ‚‚],
  apply supr_range_std_basis
end

section
variables (R Î¹)

lemma is_basis_funâ‚€ : is_basis R
    (Î» (ji : Î£ (j : Î·), (Î» _, unit) j),
       (std_basis R (Î» (i : Î·), R) (ji.fst)) 1) :=
begin
  haveI := classical.dec_eq,
  apply @is_basis_std_basis R Î· (Î»i:Î·, unit) (Î»i:Î·, R) _ _ _ _ (Î» _ _, (1 : R))
      (assume i, @is_basis_singleton_one _ _ _ _),
end

lemma is_basis_fun : is_basis R (Î» i, std_basis R (Î»i:Î·, R) i 1) :=
begin
  apply is_basis.comp (is_basis_funâ‚€ R) (Î» i, âŸ¨i, punit.starâŸ©) ,
  { apply bijective_iff_has_inverse.2,
    use (Î» x, x.1),
    simp [function.left_inverse, function.right_inverse],
    intros _ b,
    rw [unique.eq_default b, unique.eq_default punit.star] },
  apply_instance
end

end

end module

end pi
