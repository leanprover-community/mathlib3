/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import algebra.quaternion_basis
import data.complex.module
import linear_algebra.clifford_algebra.conjugation
import algebra.dual_number

/-!
# Other constructions isomorphic to Clifford Algebras

This file contains isomorphisms showing that other types are equivalent to some `clifford_algebra`.

## Rings

* `clifford_algebra_ring.equiv`: any ring is equivalent to a `clifford_algebra` over a
  zero-dimensional vector space.

## Complex numbers

* `clifford_algebra_complex.equiv`: the `complex` numbers are equivalent as an `‚Ñù`-algebra to a
  `clifford_algebra` over a one-dimensional vector space with a quadratic form that satisfies
  `Q (Œπ Q 1) = -1`.
* `clifford_algebra_complex.to_complex`: the forward direction of this equiv
* `clifford_algebra_complex.of_complex`: the reverse direction of this equiv

We show additionally that this equivalence sends `complex.conj` to `clifford_algebra.involute` and
vice-versa:

* `clifford_algebra_complex.to_complex_involute`
* `clifford_algebra_complex.of_complex_conj`

Note that in this algebra `clifford_algebra.reverse` is the identity and so the clifford conjugate
is the same as `clifford_algebra.involute`.

## Quaternion algebras

* `clifford_algebra_quaternion.equiv`: a `quaternion_algebra` over `R` is equivalent as an
  `R`-algebra to a clifford algebra over `R √ó R`, sending `i` to `(0, 1)` and `j` to `(1, 0)`.
* `clifford_algebra_quaternion.to_quaternion`: the forward direction of this equiv
* `clifford_algebra_quaternion.of_quaternion`: the reverse direction of this equiv

We show additionally that this equivalence sends `quaternion_algebra.conj` to the clifford conjugate
and vice-versa:

* `clifford_algebra_quaternion.to_quaternion_involute_reverse`
* `clifford_algebra_quaternion.of_quaternion_conj`

## Dual numbers

* `clifford_algebra_dual_number.equiv`: `ùîª[R]` is is equivalent as an `R`-algebra to a clifford
  algebra over `R` where `Q = 0`.

-/

open clifford_algebra

/-! ### The clifford algebra isomorphic to a ring -/
namespace clifford_algebra_ring
open_locale complex_conjugate

variables {R : Type*} [comm_ring R]

@[simp]
lemma Œπ_eq_zero : Œπ (0 : quadratic_form R unit) = 0 :=
subsingleton.elim _ _

/-- Since the vector space is empty the ring is commutative. -/
instance : comm_ring (clifford_algebra (0 : quadratic_form R unit)) :=
{ mul_comm := Œª x y, begin
    induction x using clifford_algebra.induction,
    case h_grade0 : r { apply algebra.commutes, },
    case h_grade1 : x { simp, },
    case h_add : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ { rw [mul_add, add_mul, hx‚ÇÅ, hx‚ÇÇ], },
    case h_mul : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ { rw [mul_assoc, hx‚ÇÇ, ‚Üêmul_assoc, hx‚ÇÅ, ‚Üêmul_assoc], },
  end,
  ..clifford_algebra.ring _ }

lemma reverse_apply (x : clifford_algebra (0 : quadratic_form R unit)) : x.reverse = x :=
begin
  induction x using clifford_algebra.induction,
  case h_grade0 : r { exact reverse.commutes _},
  case h_grade1 : x { rw [Œπ_eq_zero, linear_map.zero_apply, reverse.map_zero] },
  case h_mul : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ { rw [reverse.map_mul, mul_comm, hx‚ÇÅ, hx‚ÇÇ] },
  case h_add : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ { rw [reverse.map_add, hx‚ÇÅ, hx‚ÇÇ] },
end

@[simp] lemma reverse_eq_id :
  (reverse : clifford_algebra (0 : quadratic_form R unit) ‚Üí‚Çó[R] _) = linear_map.id :=
linear_map.ext reverse_apply

@[simp] lemma involute_eq_id :
  (involute : clifford_algebra (0 : quadratic_form R unit) ‚Üí‚Çê[R] _) = alg_hom.id R _ :=
by { ext, simp }

/-- The clifford algebra over a 0-dimensional vector space is isomorphic to its scalars. -/
protected def equiv : clifford_algebra (0 : quadratic_form R unit) ‚âÉ‚Çê[R] R :=
alg_equiv.of_alg_hom
  (clifford_algebra.lift (0 : quadratic_form R unit) $
    ‚ü®0, Œª m : unit, (zero_mul (0 : R)).trans (algebra_map R _).map_zero.symm‚ü©)
  (algebra.of_id R _)
  (by { ext x, exact (alg_hom.commutes _ x), })
  (by { ext : 1, rw [Œπ_eq_zero, linear_map.comp_zero, linear_map.comp_zero], })

end clifford_algebra_ring

/-! ### The clifford algebra isomorphic to the complex numbers -/
namespace clifford_algebra_complex
open_locale complex_conjugate

/-- The quadratic form sending elements to the negation of their square. -/
def Q : quadratic_form ‚Ñù ‚Ñù := -quadratic_form.lin_mul_lin linear_map.id linear_map.id

@[simp]
lemma Q_apply (r : ‚Ñù) : Q r = - (r * r) := rfl

/-- Intermediate result for `clifford_algebra_complex.equiv`: clifford algebras over
`clifford_algebra_complex.Q` above can be converted to `‚ÑÇ`. -/
def to_complex : clifford_algebra Q ‚Üí‚Çê[‚Ñù] ‚ÑÇ :=
clifford_algebra.lift Q ‚ü®linear_map.to_span_singleton _ _ complex.I, Œª r, begin
  dsimp [linear_map.to_span_singleton, linear_map.id],
  rw mul_mul_mul_comm,
  simp,
end‚ü©

@[simp]
lemma to_complex_Œπ (r : ‚Ñù) : to_complex (Œπ Q r) = r ‚Ä¢ complex.I :=
clifford_algebra.lift_Œπ_apply _ _ r

/-- `clifford_algebra.involute` is analogous to `complex.conj`. -/
@[simp] lemma to_complex_involute (c : clifford_algebra Q) :
  to_complex (c.involute) = conj (to_complex c) :=
begin
  have : to_complex (involute (Œπ Q 1)) = conj (to_complex (Œπ Q 1)),
  { simp only [involute_Œπ, to_complex_Œπ, alg_hom.map_neg, one_smul, complex.conj_I] },
  suffices : to_complex.comp involute = complex.conj_ae.to_alg_hom.comp to_complex,
  { exact alg_hom.congr_fun this c },
  ext : 2,
  exact this
end

/-- Intermediate result for `clifford_algebra_complex.equiv`: `‚ÑÇ` can be converted to
`clifford_algebra_complex.Q` above can be converted to. -/
def of_complex : ‚ÑÇ ‚Üí‚Çê[‚Ñù] clifford_algebra Q :=
complex.lift ‚ü®
  clifford_algebra.Œπ Q 1,
  by rw [clifford_algebra.Œπ_sq_scalar, Q_apply, one_mul, ring_hom.map_neg, ring_hom.map_one]‚ü©

@[simp]
lemma of_complex_I : of_complex complex.I = Œπ Q 1 :=
complex.lift_aux_apply_I _ _

@[simp] lemma to_complex_comp_of_complex : to_complex.comp of_complex = alg_hom.id ‚Ñù ‚ÑÇ :=
begin
  ext1,
  dsimp only [alg_hom.comp_apply, subtype.coe_mk, alg_hom.id_apply],
  rw [of_complex_I, to_complex_Œπ, one_smul],
end

@[simp] lemma to_complex_of_complex (c : ‚ÑÇ) : to_complex (of_complex c) = c :=
alg_hom.congr_fun to_complex_comp_of_complex c

@[simp] lemma of_complex_comp_to_complex :
  of_complex.comp to_complex = alg_hom.id ‚Ñù (clifford_algebra Q) :=
begin
  ext,
  dsimp only [linear_map.comp_apply, subtype.coe_mk, alg_hom.id_apply,
    alg_hom.to_linear_map_apply, alg_hom.comp_apply],
  rw [to_complex_Œπ, one_smul, of_complex_I],
end

@[simp] lemma of_complex_to_complex (c : clifford_algebra Q) : of_complex (to_complex c) = c :=
alg_hom.congr_fun of_complex_comp_to_complex c

/-- The clifford algebras over `clifford_algebra_complex.Q` is isomorphic as an `‚Ñù`-algebra to
`‚ÑÇ`. -/
@[simps]
protected def equiv : clifford_algebra Q ‚âÉ‚Çê[‚Ñù] ‚ÑÇ :=
alg_equiv.of_alg_hom to_complex of_complex to_complex_comp_of_complex of_complex_comp_to_complex

/-- The clifford algebra is commutative since it is isomorphic to the complex numbers.

TODO: prove this is true for all `clifford_algebra`s over a 1-dimensional vector space. -/
instance : comm_ring (clifford_algebra Q) :=
{ mul_comm := Œª x y, clifford_algebra_complex.equiv.injective $
    by rw [alg_equiv.map_mul, mul_comm, alg_equiv.map_mul],
  .. clifford_algebra.ring _ }

/-- `reverse` is a no-op over `clifford_algebra_complex.Q`. -/
lemma reverse_apply (x : clifford_algebra Q) : x.reverse = x :=
begin
  induction x using clifford_algebra.induction,
  case h_grade0 : r { exact reverse.commutes _},
  case h_grade1 : x { rw [reverse_Œπ] },
  case h_mul : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ { rw [reverse.map_mul, mul_comm, hx‚ÇÅ, hx‚ÇÇ] },
  case h_add : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ { rw [reverse.map_add, hx‚ÇÅ, hx‚ÇÇ] },
end

@[simp]
lemma reverse_eq_id : (reverse : clifford_algebra Q ‚Üí‚Çó[‚Ñù] _) = linear_map.id :=
linear_map.ext reverse_apply

/-- `complex.conj` is analogous to `clifford_algebra.involute`. -/
@[simp] lemma of_complex_conj (c : ‚ÑÇ) : of_complex (conj c) = (of_complex c).involute :=
clifford_algebra_complex.equiv.injective $
  by rw [equiv_apply, equiv_apply, to_complex_involute, to_complex_of_complex,
    to_complex_of_complex]

-- this name is too short for us to want it visible after `open clifford_algebra_complex`
attribute [protected] Q

end clifford_algebra_complex

/-! ### The clifford algebra isomorphic to the quaternions -/
namespace clifford_algebra_quaternion

open_locale quaternion
open quaternion_algebra

variables {R : Type*} [comm_ring R] (c‚ÇÅ c‚ÇÇ : R)

/-- `Q c‚ÇÅ c‚ÇÇ` is a quadratic form over `R √ó R` such that `clifford_algebra (Q c‚ÇÅ c‚ÇÇ)` is isomorphic
as an `R`-algebra to `‚Ñç[R,c‚ÇÅ,c‚ÇÇ]`. -/
def Q : quadratic_form R (R √ó R) :=
c‚ÇÅ ‚Ä¢ quadratic_form.lin_mul_lin (linear_map.fst _ _ _) (linear_map.fst _ _ _) +
c‚ÇÇ ‚Ä¢ quadratic_form.lin_mul_lin (linear_map.snd _ _ _) (linear_map.snd _ _ _)

@[simp]
lemma Q_apply (v : R √ó R) : Q c‚ÇÅ c‚ÇÇ v = c‚ÇÅ * (v.1 * v.1) + c‚ÇÇ * (v.2 * v.2) := rfl

/-- The quaternion basis vectors within the algebra. -/
@[simps i j k]
def quaternion_basis : quaternion_algebra.basis (clifford_algebra (Q c‚ÇÅ c‚ÇÇ)) c‚ÇÅ c‚ÇÇ :=
{ i := Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0),
  j := Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1),
  k := Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0) * Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1),
  i_mul_i := begin
    rw [Œπ_sq_scalar, Q_apply, ‚Üêalgebra.algebra_map_eq_smul_one],
    simp,
  end,
  j_mul_j := begin
    rw [Œπ_sq_scalar, Q_apply, ‚Üêalgebra.algebra_map_eq_smul_one],
    simp,
  end,
  i_mul_j := rfl,
  j_mul_i := begin
    rw [eq_neg_iff_add_eq_zero, Œπ_mul_Œπ_add_swap, quadratic_form.polar],
    simp,
  end }

variables {c‚ÇÅ c‚ÇÇ}

/-- Intermediate result of `clifford_algebra_quaternion.equiv`: clifford algebras over
`clifford_algebra_quaternion.Q` can be converted to `‚Ñç[R,c‚ÇÅ,c‚ÇÇ]`. -/
def to_quaternion : clifford_algebra (Q c‚ÇÅ c‚ÇÇ) ‚Üí‚Çê[R] ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] :=
clifford_algebra.lift (Q c‚ÇÅ c‚ÇÇ) ‚ü®
  { to_fun := Œª v, (‚ü®0, v.1, v.2, 0‚ü© : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]),
    map_add' := Œª v‚ÇÅ v‚ÇÇ, by simp,
    map_smul' := Œª r v, by ext; simp },
  Œª v, begin
    dsimp,
    ext,
    all_goals {dsimp, ring},
  end‚ü©

@[simp]
lemma to_quaternion_Œπ (v : R √ó R) :
  to_quaternion (Œπ (Q c‚ÇÅ c‚ÇÇ) v) = (‚ü®0, v.1, v.2, 0‚ü© : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) :=
clifford_algebra.lift_Œπ_apply _ _ v

/-- The "clifford conjugate" (aka `involute ‚àò reverse = reverse ‚àò involute`) maps to the quaternion
conjugate. -/
lemma to_quaternion_involute_reverse (c : clifford_algebra (Q c‚ÇÅ c‚ÇÇ)) :
  to_quaternion (involute (reverse c)) = quaternion_algebra.conj (to_quaternion c) :=
begin
  induction c using clifford_algebra.induction,
  case h_grade0 : r
  { simp only [reverse.commutes, alg_hom.commutes, quaternion_algebra.coe_algebra_map,
      quaternion_algebra.conj_coe], },
  case h_grade1 : x
  { rw [reverse_Œπ, involute_Œπ, to_quaternion_Œπ, alg_hom.map_neg, to_quaternion_Œπ,
      quaternion_algebra.neg_mk, conj_mk, neg_zero], },
  case h_mul : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ
  { simp only [reverse.map_mul, alg_hom.map_mul, hx‚ÇÅ, hx‚ÇÇ, quaternion_algebra.conj_mul] },
  case h_add : x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ
  { simp only [reverse.map_add, alg_hom.map_add, hx‚ÇÅ, hx‚ÇÇ, quaternion_algebra.conj_add] },
end

/-- Map a quaternion into the clifford algebra. -/
def of_quaternion : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] ‚Üí‚Çê[R] clifford_algebra (Q c‚ÇÅ c‚ÇÇ) :=
(quaternion_basis c‚ÇÅ c‚ÇÇ).lift_hom

@[simp] lemma of_quaternion_mk (a‚ÇÅ a‚ÇÇ a‚ÇÉ a‚ÇÑ : R) :
  of_quaternion (‚ü®a‚ÇÅ, a‚ÇÇ, a‚ÇÉ, a‚ÇÑ‚ü© : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ])
    = algebra_map R _ a‚ÇÅ
    + a‚ÇÇ ‚Ä¢ Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0)
    + a‚ÇÉ ‚Ä¢ Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1)
    + a‚ÇÑ ‚Ä¢ (Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0) * Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1)) := rfl

@[simp]
lemma of_quaternion_comp_to_quaternion :
  of_quaternion.comp to_quaternion = alg_hom.id R (clifford_algebra (Q c‚ÇÅ c‚ÇÇ)) :=
begin
  ext : 1,
  dsimp, -- before we end up with two goals and have to do this twice
  ext,
  all_goals
  { dsimp,
    rw to_quaternion_Œπ,
    dsimp,
    simp only [to_quaternion_Œπ, zero_smul, one_smul, zero_add, add_zero, ring_hom.map_zero], },
end

@[simp] lemma of_quaternion_to_quaternion (c : clifford_algebra (Q c‚ÇÅ c‚ÇÇ)) :
  of_quaternion (to_quaternion c) = c :=
alg_hom.congr_fun
  (of_quaternion_comp_to_quaternion : _ = alg_hom.id R (clifford_algebra (Q c‚ÇÅ c‚ÇÇ))) c

@[simp]
lemma to_quaternion_comp_of_quaternion :
  to_quaternion.comp of_quaternion = alg_hom.id R ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] :=
begin
  apply quaternion_algebra.lift.symm.injective,
  ext1; dsimp [quaternion_algebra.basis.lift]; simp,
end

@[simp] lemma to_quaternion_of_quaternion (q : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) : to_quaternion (of_quaternion q) = q :=
alg_hom.congr_fun (to_quaternion_comp_of_quaternion : _ = alg_hom.id R ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) q

/-- The clifford algebra over `clifford_algebra_quaternion.Q c‚ÇÅ c‚ÇÇ` is isomorphic as an `R`-algebra
to `‚Ñç[R,c‚ÇÅ,c‚ÇÇ]`. -/
@[simps]
protected def equiv : clifford_algebra (Q c‚ÇÅ c‚ÇÇ) ‚âÉ‚Çê[R] ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] :=
alg_equiv.of_alg_hom to_quaternion of_quaternion
  to_quaternion_comp_of_quaternion
  of_quaternion_comp_to_quaternion

/-- The quaternion conjugate maps to the "clifford conjugate" (aka
`involute ‚àò reverse = reverse ‚àò involute`). -/
@[simp] lemma of_quaternion_conj (q : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) :
  of_quaternion (q.conj) = (of_quaternion q).reverse.involute :=
clifford_algebra_quaternion.equiv.injective $
  by rw [equiv_apply, equiv_apply, to_quaternion_involute_reverse, to_quaternion_of_quaternion,
    to_quaternion_of_quaternion]

-- this name is too short for us to want it visible after `open clifford_algebra_quaternion`
attribute [protected] Q

end clifford_algebra_quaternion

/-! ### The clifford algebra isomorphic to the dual numbers -/
section clifford_algebra_dual_number
open_locale dual_number
open dual_number triv_sq_zero_ext

variables {R M : Type*} [comm_ring R] [add_comm_group M] [module R M]

local notation `‚Üë‚Çê[` R `]` := ‚áë(algebra_map R _)

lemma Œπ_mul_Œπ (r‚ÇÅ r‚ÇÇ) : Œπ (0 : quadratic_form R R) r‚ÇÅ * Œπ (0 : quadratic_form R R) r‚ÇÇ = 0 :=
by rw [‚Üêmul_one r‚ÇÅ, ‚Üêmul_one r‚ÇÇ, ‚Üêsmul_eq_mul R, ‚Üêsmul_eq_mul R, linear_map.map_smul,
       linear_map.map_smul, smul_mul_smul, Œπ_sq_scalar, quadratic_form.zero_apply,
       ring_hom.map_zero, smul_zero]

/-- The clifford algebra over a 1-dimensional vector space with 0 quadratic form is isomorphic to
the dual numbers. -/
protected def equiv : clifford_algebra (0 : quadratic_form R R) ‚âÉ‚Çê[R] ùîª[R] :=
alg_equiv.of_alg_hom
  (clifford_algebra.lift (0 : quadratic_form R R) ‚ü®inr_hom, Œª m, inr_mul_inr _ m m‚ü©)
  (dual_number.lift_aux (Œπ _ (1 : R)) (Œπ_mul_Œπ _ _))
  (by { ext x : 1, dsimp, rw [lift_aux_apply_eps, lift_Œπ_apply, inr_hom_apply, eps] })
  (by { ext : 2, dsimp, rw [lift_Œπ_apply, inr_hom_apply, ‚Üêeps, lift_aux_apply_eps] })

end clifford_algebra_dual_number
