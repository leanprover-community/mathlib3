/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao
-/
import group_theory.group_action.conj_act
import algebra.star.unitary
import linear_algebra.clifford_algebra.star
import linear_algebra.clifford_algebra.even

/-!
# The Pin group and the Spin group

In this file we define `lipschitz`, `pin_group` and `spin_group` and show they form a group.

## Main definitions

* `lipschitz`: the Lipschitz group with a quadratic form.
* `pin_group`: the Pin group defined as the infimum of `lipschitz` and `unitary`.
* `spin_group`: the Spin group defined as the infimum of `pin_group` and `clifford.even`.

## Implementation Notes

Here are some discussion about the latent ambiguity of definition :
https://mathoverflow.net/q/427881/172242 and https://mathoverflow.net/q/251288/172242

The definition of the Lipschitz group `{ğ‘¥ âˆˆ ğ¶ğ‘™(ğ‘‰,ğ‘) â”‚ ğ‘¥ ğ‘–ğ‘  ğ‘–ğ‘›ğ‘£ğ‘’ğ‘Ÿğ‘¡ğ‘–ğ‘ğ‘™ğ‘’ ğ‘ğ‘›ğ‘‘ ğ‘¥ğ‘£ğ‘¥â»Â¹âˆˆ ğ‘‰}` is given by:
â€¢ Fulton, W. and Harris, J., 2004. Representation theory. New York: Springer, p.chapter 20.
â€¢ https://en.wikipedia.org/wiki/Clifford_algebra#Lipschitz_group
But they presumably form a group only in finite dimensions. So we define `lipschitz` with closure of
all the elements in the form of `Î¹ Q m`, and we show this definition is at least as large as the
other definition (See `mem_lipschitz_conj_act_le` and `mem_lipschitz_involute_le`). The reverse
statement presumably being true only in finite dimensions.

## TODO

Try to show the reverse statement is true in finite dimensions.
-/

variables {R : Type*} [comm_ring R]
variables {M : Type*} [add_comm_group M] [module R M]
variables {Q : quadratic_form R M}

section pin
open clifford_algebra mul_action
open_locale pointwise

/-- `lipschitz` is the subgroup closure of all the elements in the form of `Î¹ Q m` where `Î¹`
is the canonical linear map `M â†’â‚—[R] clifford_algebra Q`. -/
def lipschitz (Q : quadratic_form R M) :=
subgroup.closure (coe â»Â¹' set.range (Î¹ Q) : set (clifford_algebra Q)Ë£)

/-- If x is in `lipschitz Q`, then `(Î¹ Q).range` is closed under twisted conjugation. The reverse
statement presumably being true only in finite dimensions.-/
lemma mem_lipschitz_conj_act_le {x : (clifford_algebra Q)Ë£} [invertible (2 : R)]
  (hx : x âˆˆ lipschitz Q) : conj_act.to_conj_act x â€¢ (Î¹ Q).range â‰¤ (Î¹ Q).range :=
begin
  refine @subgroup.closure_induction'' _ _ _ _ _ hx _ _ _ _,
  { rintros x âŸ¨z, hzâŸ© y âŸ¨a, haâŸ©,
    simp only [has_smul.smul, set_like.mem_coe, linear_map.mem_range,
      distrib_mul_action.to_linear_map_apply, conj_act.of_conj_act_to_conj_act] at ha,
    rcases ha with âŸ¨âŸ¨b, hbâŸ©, ha1âŸ©,
    subst hb,
    letI := x.invertible,
    letI : invertible (Î¹ Q z) := by rwa hz,
    rw [linear_map.mem_range, â† ha1, â† inv_of_units x],
    suffices : âˆƒ (y : M), (Î¹ Q) y = (Î¹ Q z) * (Î¹ Q) b * â…Ÿ (Î¹ Q z),
    { convert this,
      ext1,
      congr';
      simp only [hz.symm, subsingleton.helim (congr_arg invertible hz.symm)], },
    letI := invertible_of_invertible_Î¹ Q z,
    refine âŸ¨(â…Ÿ(Q z) * quadratic_form.polar Q z b) â€¢ z - b, (Î¹_mul_Î¹_mul_inv_of_Î¹ Q z b).symmâŸ©, },
  { rintros x âŸ¨z, hz1âŸ© y âŸ¨a, âŸ¨b, hbâŸ©, ha2âŸ©,
    simp only [conj_act.to_conj_act_inv, distrib_mul_action.to_linear_map_apply,
      has_smul.smul, conj_act.of_conj_act_inv, conj_act.of_conj_act_to_conj_act,
        inv_inv] at ha2,
    subst hb,
    subst ha2,
    letI := x.invertible,
    letI : invertible (Î¹ Q z) := by rwa hz1,
    rw [linear_map.mem_range, â† inv_of_units x],
    suffices : âˆƒ (y : M), (Î¹ Q) y = â…Ÿ (Î¹ Q z) * (Î¹ Q) b * (Î¹ Q z),
    { convert this,
      ext1,
      congr';
      simp only [hz1.symm, subsingleton.helim (congr_arg invertible hz1.symm)], },
    letI := invertible_of_invertible_Î¹ Q z,
    refine âŸ¨(â…Ÿ(Q z) * quadratic_form.polar Q z b) â€¢ z - b, (inv_of_Î¹_mul_Î¹_mul_Î¹ Q z b).symmâŸ©, },
  { simp only [conj_act.to_conj_act_one, one_smul, le_refl], },
  { intros x y hx1 hy1 z hz1,
    simp only [conj_act.to_conj_act_mul] at hz1,
    suffices : (conj_act.to_conj_act x * conj_act.to_conj_act y) â€¢ (Î¹ Q).range â‰¤ (Î¹ Q).range,
    { exact this hz1, },
    { rintros m âŸ¨a, âŸ¨b, hbâŸ©, haâŸ©,
      simp only [distrib_mul_action.to_linear_map_apply, has_smul.smul, conj_act.of_conj_act_mul,
       conj_act.of_conj_act_to_conj_act, units.coe_mul, mul_inv_rev] at ha,
      subst hb,
      have hb : â†‘x * (â†‘y * (Î¹ Q) b * â†‘yâ»Â¹) * â†‘xâ»Â¹ = m := by simp_rw [â† ha, mul_assoc],
      have hy2 : â†‘y * (Î¹ Q) b * â†‘yâ»Â¹ âˆˆ conj_act.to_conj_act y â€¢ (Î¹ Q).range := by simp only
        [has_smul.smul, exists_exists_eq_and, exists_apply_eq_apply, submodule.mem_map,
          linear_map.mem_range, distrib_mul_action.to_linear_map_apply,
            conj_act.of_conj_act_to_conj_act],
      specialize hy1 hy2,
      have hx2 : â†‘x * (â†‘y * (Î¹ Q) b * â†‘yâ»Â¹) * â†‘xâ»Â¹ âˆˆ conj_act.to_conj_act x â€¢ (Î¹ Q).range,
      { simp only [has_smul.smul, units.mul_left_inj, units.mul_right_inj, exists_exists_eq_and,
          submodule.mem_map, linear_map.mem_range, distrib_mul_action.to_linear_map_apply,
            conj_act.of_conj_act_to_conj_act],
        exact hy1, },
      specialize hx1 hx2,
      rwa hb at hx1, }, },
end

/-- This is another version of `mem_lipschitz_conj_act_le` which uses `involute`.-/
lemma mem_lipschitz_involute_le {x : (clifford_algebra Q)Ë£} [invertible (2 : R)]
  (hx : x âˆˆ lipschitz Q) (y : M) : involute â†‘x * (Î¹ Q y) * â†‘xâ»Â¹ âˆˆ (Î¹ Q).range :=
begin
  revert y,
  refine @subgroup.closure_induction'' _ _ _ _ _ hx _ _ _ _,
  { rintros x âŸ¨z, hzâŸ© y,
    letI := x.invertible,
    letI : invertible (Î¹ Q z) := by rwa hz,
    rw [linear_map.mem_range, â† inv_of_units x],
    suffices : âˆƒ (y_1 : M), (Î¹ Q) y_1 = -(Î¹ Q z) * (Î¹ Q) y * â…Ÿ (Î¹ Q z),
    { convert this,
      ext1,
      congr',
      { rw [â† hz, involute_Î¹], },
      { exact hz.symm, },
      { exact subsingleton.helim (congr_arg invertible hz.symm) _ _, }, },
    letI := invertible_of_invertible_Î¹ Q z,
    refine âŸ¨-((â…Ÿ(Q z) * quadratic_form.polar Q z y) â€¢ z - y), by simp only
      [map_neg, neg_mul, Î¹_mul_Î¹_mul_inv_of_Î¹ Q z y]âŸ©, },
  { rintros x âŸ¨z, hzâŸ© y,
    letI := x.invertible,
    letI : invertible (Î¹ Q z) := by rwa hz,
    letI := invertible_neg (Î¹ Q z),
    letI := invertible.map (involute : clifford_algebra Q â†’â‚[R] clifford_algebra Q) â†‘x,
    rw [inv_inv, linear_map.mem_range, â† inv_of_units x, map_inv_of],
    suffices : âˆƒ (y_1 : M), (Î¹ Q) y_1 = -â…Ÿ (Î¹ Q z) * (Î¹ Q) y * (Î¹ Q z),
    { convert this,
      ext1,
      congr',
      { rw â† inv_of_neg,
        apply invertible_unique,
        rw [â† hz, involute_Î¹], },
      { exact hz.symm, }, },
    letI := invertible_of_invertible_Î¹ Q z,
    refine âŸ¨-((â…Ÿ(Q z) * quadratic_form.polar Q z y) â€¢ z - y), by simp only
      [map_neg, neg_mul, inv_of_Î¹_mul_Î¹_mul_Î¹ Q z y]âŸ©, },
  { simp only [units.coe_one, map_one, one_mul, inv_one, mul_one,
      linear_map.mem_range, exists_apply_eq_apply, forall_const], },
  { intros a b ha hb y,
    simp only [units.coe_mul, map_mul, mul_inv_rev, linear_map.mem_range],
    cases hb y with c hc,
    suffices : âˆƒ (y_1 : M), (Î¹ Q) y_1 = involute â†‘a * (involute â†‘b * (Î¹ Q) y * â†‘bâ»Â¹) * â†‘aâ»Â¹,
    { cases this with p hp,
      refine âŸ¨p, by simp only [hp, mul_assoc]âŸ©, },
    rw â† hc,
    exact ha c, },
end

lemma coe_mem_lipschitz_iff_mem {x : (clifford_algebra Q)Ë£} :
  â†‘x âˆˆ (lipschitz Q).to_submonoid.map (units.coe_hom $ clifford_algebra Q) â†” x âˆˆ lipschitz Q :=
begin
  simp only [submonoid.mem_map, subgroup.mem_to_submonoid, units.coe_hom_apply, exists_prop],
  norm_cast,
  exact exists_eq_right,
end

/-- `pin_group Q` is defined as the infimum of `lipschitz Q` and `unitary (clifford_algebra Q)`.
See `mem_iff`. -/
def pin_group (Q : quadratic_form R M) : submonoid (clifford_algebra Q) :=
(lipschitz Q).to_submonoid.map (units.coe_hom $ clifford_algebra Q) âŠ“ unitary _

namespace pin_group

/-- An element is in `pin_group Q` if and only if it is in `lipschitz Q` and `unitary`. -/
lemma mem_iff {x : clifford_algebra Q} :
  x âˆˆ pin_group Q â†” x âˆˆ (lipschitz Q).to_submonoid.map (units.coe_hom $ clifford_algebra Q) âˆ§
    x âˆˆ unitary (clifford_algebra Q) := iff.rfl
lemma mem_lipschitz {x : clifford_algebra Q} (hx : x âˆˆ pin_group Q) :
  x âˆˆ (lipschitz Q).to_submonoid.map (units.coe_hom $ clifford_algebra Q) := hx.1
lemma mem_unitary {x : clifford_algebra Q} (hx : x âˆˆ pin_group Q) :
  x âˆˆ unitary (clifford_algebra Q) := hx.2

lemma units_mem_iff {x : (clifford_algebra Q)Ë£} :
  â†‘x âˆˆ pin_group Q â†” x âˆˆ lipschitz Q âˆ§ â†‘x âˆˆ unitary (clifford_algebra Q) :=
by rw [mem_iff, coe_mem_lipschitz_iff_mem]
lemma units_mem_lipschitz {x : (clifford_algebra Q)Ë£} (hx : â†‘x âˆˆ pin_group Q) :
  x âˆˆ lipschitz Q := ((units_mem_iff).1 hx).1

/-- If x is in `pin_group Q`, then `(Î¹ Q).range` is closed under twisted conjugation. The reverse
statement presumably being true only in finite dimensions.-/
lemma units_mem_conj_act_le {x : (clifford_algebra Q)Ë£} (hx : â†‘x âˆˆ pin_group Q)
  [invertible (2 : R)] : conj_act.to_conj_act x â€¢ (Î¹ Q).range â‰¤ (Î¹ Q).range :=
mem_lipschitz_conj_act_le (units_mem_lipschitz hx)

/-- This is another version of `units_mem_conj_act_le` which uses `involute`. -/
lemma units_mem_involute_act_le {x : (clifford_algebra Q)Ë£} (hx : â†‘x âˆˆ pin_group Q)
  [invertible (2 : R)] (y : M) : involute â†‘x * (Î¹ Q y) * â†‘xâ»Â¹ âˆˆ (Î¹ Q).range :=
mem_lipschitz_involute_le (units_mem_lipschitz hx) y

@[simp] lemma star_mul_self_of_mem {x : clifford_algebra Q} (hx : x âˆˆ pin_group Q) :
  star x * x = 1 := (hx.2).1
@[simp] lemma mul_star_self_of_mem {x : clifford_algebra Q} (hx : x âˆˆ pin_group Q) :
  x * star x = 1 := (hx.2).2

/-- See `star_mem_iff` for both directions. -/
lemma star_mem {x : clifford_algebra Q} (hx : x âˆˆ pin_group Q) : star x âˆˆ pin_group Q :=
begin
  rw mem_iff at hx âŠ¢,
  refine âŸ¨_, unitary.star_mem hx.2âŸ©,
  rcases hx with âŸ¨âŸ¨y, hyâ‚, hyâ‚‚âŸ©, hxâ‚‚, hxâ‚ƒâŸ©,
  simp only [subgroup.coe_to_submonoid, set_like.mem_coe] at hyâ‚,
  simp only [units.coe_hom_apply] at hyâ‚‚,
  simp only [submonoid.mem_map, subgroup.mem_to_submonoid, units.coe_hom_apply, exists_prop],
  refine âŸ¨star y, _, by simp only [hyâ‚‚, units.coe_star]âŸ©,
  rw â† hyâ‚‚ at hxâ‚ƒ,
  have hyâ‚ƒ : y * star y = 1,
  { rw â† units.eq_iff,
    simp only [hxâ‚ƒ, units.coe_mul, units.coe_star, units.coe_one], },
  apply_fun (Î» x, yâ»Â¹ * x) at hyâ‚ƒ,
  simp only [inv_mul_cancel_left, mul_one] at hyâ‚ƒ,
  simp only [hyâ‚ƒ, hyâ‚, inv_mem_iff],
end

/-- An element is in `pin_group Q` if and only if `star x` is in `pin_group Q`.
See `star_mem` for only one direction. -/
@[simp] lemma star_mem_iff {x : clifford_algebra Q} : star x âˆˆ pin_group Q â†” x âˆˆ pin_group Q :=
begin
  refine âŸ¨_, star_memâŸ©,
  intro hx,
  convert star_mem hx,
  exact (star_star x).symm,
end

instance : has_star (pin_group Q) := âŸ¨Î» x, âŸ¨star x, star_mem x.propâŸ©âŸ©

@[simp, norm_cast] lemma coe_star {x : pin_group Q} :
  â†‘(star x) = (star x : clifford_algebra Q) := rfl

lemma coe_star_mul_self (x : pin_group Q) :
  (star x : clifford_algebra Q) * x = 1 := star_mul_self_of_mem x.prop
lemma coe_mul_star_self (x : pin_group Q) :
  (x : clifford_algebra Q) * star x = 1 := mul_star_self_of_mem x.prop

@[simp] lemma star_mul_self (x : pin_group Q) : star x * x = 1 := subtype.ext $ coe_star_mul_self x
@[simp] lemma mul_star_self (x : pin_group Q) : x * star x = 1 := subtype.ext $ coe_mul_star_self x

/-- `pin_group Q` forms a group where the inverse is `star`. -/
instance : group (pin_group Q) :=
{ inv := star,
  mul_left_inv := star_mul_self,
  ..submonoid.to_monoid _ }

instance : has_involutive_star (pin_group Q) := âŸ¨Î» _, by { ext, simp only [coe_star, star_star] }âŸ©

instance : star_semigroup (pin_group Q) :=
âŸ¨Î» _ _, by { ext, simp only [coe_star, submonoid.coe_mul, star_mul] }âŸ©

instance : inhabited (pin_group Q) := âŸ¨1âŸ©

lemma star_eq_inv (x : pin_group Q) : star x = xâ»Â¹ := rfl

lemma star_eq_inv' : (star : pin_group Q â†’ pin_group Q) = has_inv.inv := rfl

/-- The elements in `pin_group Q` embed into (clifford_algebra Q)Ë£. -/
@[simps]
def to_units : pin_group Q â†’* (clifford_algebra Q)Ë£ :=
{ to_fun := Î» x, âŸ¨x, â†‘(xâ»Â¹), coe_mul_star_self x, coe_star_mul_self xâŸ©,
  map_one' := units.ext rfl,
  map_mul' := Î» x y, units.ext rfl }

lemma to_units_injective : function.injective (to_units : pin_group Q â†’ (clifford_algebra Q)Ë£) :=
Î» x y h, subtype.ext $ units.ext_iff.mp h

end pin_group
end pin

section spin
open clifford_algebra mul_action
open_locale pointwise

/-- `spin_group Q` is defined as the infimum of `pin_group Q` and `clifford_algebra.even Q`.
See `mem_iff`. -/
def spin_group (Q : quadratic_form R M) :=
pin_group Q âŠ“ (clifford_algebra.even Q).to_subring.to_submonoid

namespace spin_group

/-- An element is in `spin_group Q` if and only if it is in `pin_group Q` and `even Q`. -/
lemma mem_iff {x : clifford_algebra Q} : x âˆˆ spin_group Q â†” x âˆˆ pin_group Q âˆ§ x âˆˆ even Q := iff.rfl
lemma mem_pin {x : clifford_algebra Q} (hx : x âˆˆ spin_group Q) : x âˆˆ pin_group Q := hx.1
lemma mem_even {x : clifford_algebra Q} (hx : x âˆˆ spin_group Q) : x âˆˆ even Q := hx.2

lemma units_mem_lipschitz {x : (clifford_algebra Q)Ë£} (hx : â†‘x âˆˆ spin_group Q) :
  x âˆˆ lipschitz Q := pin_group.units_mem_lipschitz (mem_pin hx)

/-- If x is in `spin_group Q`, then `involute x` is equal to x.-/
lemma mem_involute_eq {x : clifford_algebra Q} (hx : x âˆˆ spin_group Q) : involute x = x :=
involute_eq_of_mem_even (mem_even hx)

lemma units_involute_act_eq_conj_act {x : (clifford_algebra Q)Ë£} (hx : â†‘x âˆˆ spin_group Q)
  [invertible (2 : R)] (y : M) : involute â†‘x * (Î¹ Q y) * â†‘xâ»Â¹ = conj_act.to_conj_act x â€¢ (Î¹ Q y) :=
by simp_rw [has_smul.smul, conj_act.of_conj_act_to_conj_act, units.mul_left_inj,
  mem_involute_eq hx]

/-- If x is in `spin_group Q`, then `(Î¹ Q).range` is closed under twisted conjugation. The reverse
statement presumably being true only in finite dimensions.-/
lemma units_mem_conj_act_le {x : (clifford_algebra Q)Ë£} (hx : â†‘x âˆˆ spin_group Q)
  [invertible (2 : R)] : conj_act.to_conj_act x â€¢ (Î¹ Q).range â‰¤ (Î¹ Q).range :=
mem_lipschitz_conj_act_le (units_mem_lipschitz hx)

/-- This is another version of `units_mem_conj_act_le` which uses `involute`.-/
lemma units_mem_involute_act_le {x : (clifford_algebra Q)Ë£} (hx : â†‘x âˆˆ spin_group Q)
  [invertible (2 : R)] (y : M) : involute â†‘x * (Î¹ Q y) * â†‘xâ»Â¹ âˆˆ (Î¹ Q).range :=
mem_lipschitz_involute_le (units_mem_lipschitz hx) y

@[simp] lemma star_mul_self_of_mem {x : clifford_algebra Q} (hx : x âˆˆ spin_group Q) :
  star x * x = 1 := ((hx.1).2).1
@[simp] lemma mul_star_self_of_mem {x : clifford_algebra Q} (hx : x âˆˆ spin_group Q) :
  x * star x = 1 := ((hx.1).2).2

/-- See `star_mem_iff` for both directions. -/
lemma star_mem {x : clifford_algebra Q} (hx : x âˆˆ spin_group Q) : star x âˆˆ spin_group Q :=
begin
  rw mem_iff at hx âŠ¢,
  cases hx with hxâ‚ hxâ‚‚,
  refine âŸ¨pin_group.star_mem hxâ‚, _âŸ©,
  dsimp only [clifford_algebra.even] at hxâ‚‚ âŠ¢,
  simp only [submodule.mem_to_subalgebra] at hxâ‚‚ âŠ¢,
  simp only [star_def, reverse_mem_even_odd_iff, involute_mem_even_odd_iff, hxâ‚‚],
end

/--
An element is in `spin_group Q` if and only if `star x` is in `spin_group Q`.
See `star_mem` for only one direction.
-/
@[simp] lemma star_mem_iff {x : clifford_algebra Q} : star x âˆˆ spin_group Q â†” x âˆˆ spin_group Q :=
begin
  refine âŸ¨_, star_memâŸ©,
  intro hx,
  convert star_mem hx,
  exact (star_star x).symm,
end

instance : has_star (spin_group Q) := âŸ¨Î» x, âŸ¨star x, star_mem x.propâŸ©âŸ©

@[simp, norm_cast] lemma coe_star {x : spin_group Q} :
  â†‘(star x) = (star x : clifford_algebra Q) := rfl

lemma coe_star_mul_self (x : spin_group Q) :
  (star x : clifford_algebra Q) * x = 1 := star_mul_self_of_mem x.prop
lemma coe_mul_star_self (x : spin_group Q) :
  (x : clifford_algebra Q) * star x = 1 := mul_star_self_of_mem x.prop

@[simp] lemma star_mul_self (x : spin_group Q) : star x * x = 1 := subtype.ext $ coe_star_mul_self x
@[simp] lemma mul_star_self (x : spin_group Q) : x * star x = 1 := subtype.ext $ coe_mul_star_self x

/-- `spin_group Q` forms a group where the inverse is `star`. -/
instance : group (spin_group Q) :=
{ inv := star,
  mul_left_inv := star_mul_self,
  ..submonoid.to_monoid _ }

instance : has_involutive_star (spin_group Q) := âŸ¨Î» _, by { ext, simp only [coe_star, star_star] }âŸ©

instance : star_semigroup (spin_group Q) :=
âŸ¨Î» _ _, by { ext, simp only [coe_star, submonoid.coe_mul, star_mul] }âŸ©

instance : inhabited (spin_group Q) := âŸ¨1âŸ©

lemma star_eq_inv (x : spin_group Q) : star x = xâ»Â¹ := rfl

lemma star_eq_inv' : (star : spin_group Q â†’ spin_group Q) = has_inv.inv := rfl

/-- The elements in `spin_group Q` embed into (clifford_algebra Q)Ë£. -/
@[simps]
def to_units : spin_group Q â†’* (clifford_algebra Q)Ë£ :=
{ to_fun := Î» x, âŸ¨x, â†‘(xâ»Â¹), coe_mul_star_self x, coe_star_mul_self xâŸ©,
  map_one' := units.ext rfl,
  map_mul' := Î» x y, units.ext rfl }

lemma to_units_injective : function.injective (to_units : spin_group Q â†’ (clifford_algebra Q)Ë£) :=
Î» x y h, subtype.ext $ units.ext_iff.mp h

end spin_group
end spin
