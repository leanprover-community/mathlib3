/-
Copyright (c) 2019 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot, Casper Putz, Anne Baanen
-/
import linear_algebra.finite_dimensional
import linear_algebra.general_linear_group
import linear_algebra.matrix.reindex
import tactic.field_simp
import linear_algebra.matrix.nonsingular_inverse
import linear_algebra.matrix.basis

/-!
# Determinant of families of vectors

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file defines the determinant of an endomorphism, and of a family of vectors
with respect to some basis. For the determinant of a matrix, see the file
`linear_algebra.matrix.determinant`.

## Main definitions

In the list below, and in all this file, `R` is a commutative ring (semiring
is sometimes enough), `M` and its variations are `R`-modules, `Î¹`, `Îº`, `n` and `m` are finite
types used for indexing.

 * `basis.det`: the determinant of a family of vectors with respect to a basis,
   as a multilinear map
 * `linear_map.det`: the determinant of an endomorphism `f : End R M` as a
   multiplicative homomorphism (if `M` does not have a finite `R`-basis, the
   result is `1` instead)
 * `linear_equiv.det`: the determinant of an isomorphism `f : M â‰ƒâ‚—[R] M` as a
   multiplicative homomorphism (if `M` does not have a finite `R`-basis, the
   result is `1` instead)

## Tags

basis, det, determinant
-/

noncomputable theory

open_locale big_operators
open_locale matrix

open linear_map
open submodule

universes u v w

open linear_map matrix set function

variables {R : Type*} [comm_ring R]
variables {M : Type*} [add_comm_group M] [module R M]
variables {M' : Type*} [add_comm_group M'] [module R M']
variables {Î¹ : Type*} [decidable_eq Î¹] [fintype Î¹]
variables (e : basis Î¹ R M)

section conjugate

variables {A : Type*} [comm_ring A]
variables {m n : Type*} [fintype m] [fintype n]

/-- If `R^m` and `R^n` are linearly equivalent, then `m` and `n` are also equivalent. -/
def equiv_of_pi_lequiv_pi {R : Type*} [comm_ring R] [nontrivial R]
  (e : (m â†’ R) â‰ƒâ‚—[R] (n â†’ R)) : m â‰ƒ n :=
basis.index_equiv (basis.of_equiv_fun e.symm) (pi.basis_fun _ _)

namespace matrix

/-- If `M` and `M'` are each other's inverse matrices, they are square matrices up to
equivalence of types. -/
def index_equiv_of_inv [nontrivial A] [decidable_eq m] [decidable_eq n]
  {M : matrix m n A} {M' : matrix n m A}
  (hMM' : M â¬ M' = 1) (hM'M : M' â¬ M = 1) :
  m â‰ƒ n :=
equiv_of_pi_lequiv_pi (to_lin'_of_inv hMM' hM'M)

lemma det_comm [decidable_eq n] (M N : matrix n n A) : det (M â¬ N) = det (N â¬ M) :=
by rw [det_mul, det_mul, mul_comm]

/-- If there exists a two-sided inverse `M'` for `M` (indexed differently),
then `det (N â¬ M) = det (M â¬ N)`. -/
lemma det_comm' [decidable_eq m] [decidable_eq n]
  {M : matrix n m A} {N : matrix m n A} {M' : matrix m n A}
  (hMM' : M â¬ M' = 1) (hM'M : M' â¬ M = 1) :
  det (M â¬ N) = det (N â¬ M) :=
begin
  nontriviality A,
  -- Although `m` and `n` are different a priori, we will show they have the same cardinality.
  -- This turns the problem into one for square matrices, which is easy.
  let e := index_equiv_of_inv hMM' hM'M,
  rw [â† det_submatrix_equiv_self e, â† submatrix_mul_equiv _ _ _ (equiv.refl n) _, det_comm,
    submatrix_mul_equiv, equiv.coe_refl, submatrix_id_id]
end

/-- If `M'` is a two-sided inverse for `M` (indexed differently), `det (M â¬ N â¬ M') = det N`.

See `matrix.det_conj` and `matrix.det_conj'` for the case when `M' = Mâ»Â¹` or vice versa. -/
lemma det_conj_of_mul_eq_one [decidable_eq m] [decidable_eq n]
  {M : matrix m n A} {M' : matrix n m A} {N : matrix n n A}
  (hMM' : M â¬ M' = 1) (hM'M : M' â¬ M = 1) :
  det (M â¬ N â¬ M') = det N :=
by rw [â† det_comm' hM'M hMM', â† matrix.mul_assoc, hM'M, matrix.one_mul]

end matrix

end conjugate

namespace linear_map

/-! ### Determinant of a linear map -/

variables {A : Type*} [comm_ring A] [module A M]
variables {Îº : Type*} [fintype Îº]

/-- The determinant of `linear_map.to_matrix` does not depend on the choice of basis. -/
lemma det_to_matrix_eq_det_to_matrix [decidable_eq Îº]
  (b : basis Î¹ A M) (c : basis Îº A M) (f : M â†’â‚—[A] M) :
  det (linear_map.to_matrix b b f) = det (linear_map.to_matrix c c f) :=
by rw [â† linear_map_to_matrix_mul_basis_to_matrix c b c,
       â† basis_to_matrix_mul_linear_map_to_matrix b c b,
       matrix.det_conj_of_mul_eq_one]; rw [basis.to_matrix_mul_to_matrix, basis.to_matrix_self]

/-- The determinant of an endomorphism given a basis.

See `linear_map.det` for a version that populates the basis non-computably.

Although the `trunc (basis Î¹ A M)` parameter makes it slightly more convenient to switch bases,
there is no good way to generalize over universe parameters, so we can't fully state in `det_aux`'s
type that it does not depend on the choice of basis. Instead you can use the `det_aux_def'` lemma,
or avoid mentioning a basis at all using `linear_map.det`.
-/
@[irreducible] def det_aux : trunc (basis Î¹ A M) â†’ (M â†’â‚—[A] M) â†’* A :=
trunc.lift
  (Î» b : basis Î¹ A M,
    (det_monoid_hom).comp (to_matrix_alg_equiv b : (M â†’â‚—[A] M) â†’* matrix Î¹ Î¹ A))
  (Î» b c, monoid_hom.ext $ det_to_matrix_eq_det_to_matrix b c)

/-- Unfold lemma for `det_aux`.

See also `det_aux_def'` which allows you to vary the basis.
-/
lemma det_aux_def (b : basis Î¹ A M) (f : M â†’â‚—[A] M) :
  linear_map.det_aux (trunc.mk b) f = matrix.det (linear_map.to_matrix b b f) :=
by  { rw [det_aux], refl }

lemma det_aux_def' {Î¹' : Type*} [fintype Î¹'] [decidable_eq Î¹']
  (tb : trunc $ basis Î¹ A M) (b' : basis Î¹' A M) (f : M â†’â‚—[A] M) :
  linear_map.det_aux tb f = matrix.det (linear_map.to_matrix b' b' f) :=
by { apply trunc.induction_on tb, intro b, rw [det_aux_def, det_to_matrix_eq_det_to_matrix b b'] }

@[simp]
lemma det_aux_id (b : trunc $ basis Î¹ A M) : linear_map.det_aux b (linear_map.id) = 1 :=
(linear_map.det_aux b).map_one

@[simp]
lemma det_aux_comp (b : trunc $ basis Î¹ A M) (f g : M â†’â‚—[A] M) :
  linear_map.det_aux b (f.comp g) = linear_map.det_aux b f * linear_map.det_aux b g :=
(linear_map.det_aux b).map_mul f g

section
open_locale classical

-- Discourage the elaborator from unfolding `det` and producing a huge term by marking it
-- as irreducible.
/-- The determinant of an endomorphism independent of basis.

If there is no finite basis on `M`, the result is `1` instead.
-/
@[irreducible] protected def det : (M â†’â‚—[A] M) â†’* A :=
if H : âˆƒ (s : finset M), nonempty (basis s A M)
then linear_map.det_aux (trunc.mk H.some_spec.some)
else 1

lemma coe_det [decidable_eq M] : â‡‘(linear_map.det : (M â†’â‚—[A] M) â†’* A) =
  if H : âˆƒ (s : finset M), nonempty (basis s A M)
  then linear_map.det_aux (trunc.mk H.some_spec.some)
  else 1 :=
by { ext, unfold linear_map.det,
     split_ifs,
     { congr }, -- use the correct `decidable_eq` instance
     refl }

end

-- Auxiliary lemma, the `simp` normal form goes in the other direction
-- (using `linear_map.det_to_matrix`)
lemma det_eq_det_to_matrix_of_finset [decidable_eq M]
  {s : finset M} (b : basis s A M) (f : M â†’â‚—[A] M) :
  f.det = matrix.det (linear_map.to_matrix b b f) :=
have âˆƒ (s : finset M), nonempty (basis s A M),
from âŸ¨s, âŸ¨bâŸ©âŸ©,
by rw [linear_map.coe_det, dif_pos, det_aux_def' _ b]; assumption

@[simp] lemma det_to_matrix
  (b : basis Î¹ A M) (f : M â†’â‚—[A] M) :
  matrix.det (to_matrix b b f) = f.det :=
by { haveI := classical.dec_eq M,
     rw [det_eq_det_to_matrix_of_finset b.reindex_finset_range, det_to_matrix_eq_det_to_matrix b] }

@[simp] lemma det_to_matrix' {Î¹ : Type*} [fintype Î¹] [decidable_eq Î¹]
  (f : (Î¹ â†’ A) â†’â‚—[A] (Î¹ â†’ A)) :
  det f.to_matrix' = f.det :=
by simp [â† to_matrix_eq_to_matrix']

@[simp] lemma det_to_lin (b : basis Î¹ R M) (f : matrix Î¹ Î¹ R) :
  linear_map.det (matrix.to_lin b b f) = f.det :=
by rw [â† linear_map.det_to_matrix b, linear_map.to_matrix_to_lin]

@[simp] lemma det_to_lin' (f : matrix Î¹ Î¹ R) :
  linear_map.det (f.to_lin') = f.det :=
by simp only [â† to_lin_eq_to_lin', det_to_lin]

/-- To show `P f.det` it suffices to consider `P (to_matrix _ _ f).det` and `P 1`. -/
@[elab_as_eliminator]
lemma det_cases [decidable_eq M] {P : A â†’ Prop} (f : M â†’â‚—[A] M)
  (hb : âˆ€ (s : finset M) (b : basis s A M), P (to_matrix b b f).det) (h1 : P 1) :
  P f.det :=
begin
  unfold linear_map.det,
  split_ifs with h,
  { convert hb _ h.some_spec.some,
    apply det_aux_def' },
  { exact h1 }
end

@[simp]
lemma det_comp (f g : M â†’â‚—[A] M) : (f.comp g).det = f.det * g.det :=
linear_map.det.map_mul f g

@[simp]
lemma det_id : (linear_map.id : M â†’â‚—[A] M).det = 1 :=
linear_map.det.map_one

/-- Multiplying a map by a scalar `c` multiplies its determinant by `c ^ dim M`. -/
@[simp] lemma det_smul {ğ•œ : Type*} [field ğ•œ] {M : Type*} [add_comm_group M] [module ğ•œ M]
  (c : ğ•œ) (f : M â†’â‚—[ğ•œ] M) :
  linear_map.det (c â€¢ f) = c ^ (finite_dimensional.finrank ğ•œ M) * linear_map.det f :=
begin
  by_cases H : âˆƒ (s : finset M), nonempty (basis s ğ•œ M),
  { haveI : finite_dimensional ğ•œ M,
    { rcases H with âŸ¨s, âŸ¨hsâŸ©âŸ©, exact finite_dimensional.of_fintype_basis hs },
    simp only [â† det_to_matrix (finite_dimensional.fin_basis ğ•œ M), linear_equiv.map_smul,
              fintype.card_fin, det_smul] },
  { classical,
    have : finite_dimensional.finrank ğ•œ M = 0 := finrank_eq_zero_of_not_exists_basis H,
    simp [coe_det, H, this] }
end

lemma det_zero' {Î¹ : Type*} [finite Î¹] [nonempty Î¹] (b : basis Î¹ A M) :
  linear_map.det (0 : M â†’â‚—[A] M) = 0 :=
by { haveI := classical.dec_eq Î¹, casesI nonempty_fintype Î¹,
     rwa [â† det_to_matrix b, linear_equiv.map_zero, det_zero] }

/-- In a finite-dimensional vector space, the zero map has determinant `1` in dimension `0`,
and `0` otherwise. We give a formula that also works in infinite dimension, where we define
the determinant to be `1`. -/
@[simp] lemma det_zero {ğ•œ : Type*} [field ğ•œ] {M : Type*} [add_comm_group M] [module ğ•œ M] :
  linear_map.det (0 : M â†’â‚—[ğ•œ] M) = (0 : ğ•œ) ^ (finite_dimensional.finrank ğ•œ M) :=
by simp only [â† zero_smul ğ•œ (1 : M â†’â‚—[ğ•œ] M), det_smul, mul_one, monoid_hom.map_one]

lemma det_eq_one_of_subsingleton [subsingleton M] (f : M â†’â‚—[R] M) : (f : M â†’â‚—[R] M).det = 1 :=
begin
  have b : basis (fin 0) R M := basis.empty M,
  rw â† f.det_to_matrix b,
  exact matrix.det_is_empty,
end

lemma det_eq_one_of_finrank_eq_zero {ğ•œ : Type*} [field ğ•œ] {M : Type*} [add_comm_group M]
  [module ğ•œ M] (h : finite_dimensional.finrank ğ•œ M = 0) (f : M â†’â‚—[ğ•œ] M) :
  (f : M â†’â‚—[ğ•œ] M).det = 1 :=
begin
  classical,
  refine @linear_map.det_cases M  _ ğ•œ _ _ _ (Î» t, t = 1) f _ rfl,
  intros s b,
  haveI : is_empty s,
  { rw â† fintype.card_eq_zero_iff,
    exact (finite_dimensional.finrank_eq_card_basis b).symm.trans h },
  exact matrix.det_is_empty
end

/-- Conjugating a linear map by a linear equiv does not change its determinant. -/
@[simp] lemma det_conj {N : Type*} [add_comm_group N] [module A N]
  (f : M â†’â‚—[A] M) (e : M â‰ƒâ‚—[A] N) :
  linear_map.det ((e : M â†’â‚—[A] N) âˆ˜â‚— (f âˆ˜â‚— (e.symm : N â†’â‚—[A] M))) = linear_map.det f :=
begin
  classical,
  by_cases H : âˆƒ (s : finset M), nonempty (basis s A M),
  { rcases H with âŸ¨s, âŸ¨bâŸ©âŸ©,
    rw [â† det_to_matrix b f, â† det_to_matrix (b.map e), to_matrix_comp (b.map e) b (b.map e),
        to_matrix_comp (b.map e) b b, â† matrix.mul_assoc, matrix.det_conj_of_mul_eq_one],
    { rw [â† to_matrix_comp, linear_equiv.comp_coe, e.symm_trans_self,
          linear_equiv.refl_to_linear_map, to_matrix_id] },
    { rw [â† to_matrix_comp, linear_equiv.comp_coe, e.self_trans_symm,
          linear_equiv.refl_to_linear_map, to_matrix_id] } },
  { have H' : Â¬ (âˆƒ (t : finset N), nonempty (basis t A N)),
    { contrapose! H,
      rcases H with âŸ¨s, âŸ¨bâŸ©âŸ©,
      exact âŸ¨_, âŸ¨(b.map e.symm).reindex_finset_rangeâŸ©âŸ© },
    simp only [coe_det, H, H', pi.one_apply, dif_neg, not_false_iff] }
end

/-- If a linear map is invertible, so is its determinant. -/
lemma is_unit_det {A : Type*} [comm_ring A] [module A M]
  (f : M â†’â‚—[A] M) (hf : is_unit f) : is_unit f.det :=
begin
  obtain âŸ¨g, hgâŸ© : âˆƒ g, f.comp g = 1 := hf.exists_right_inv,
  have : linear_map.det f * linear_map.det g = 1,
    by simp only [â† linear_map.det_comp, hg, monoid_hom.map_one],
  exact is_unit_of_mul_eq_one _ _ this,
end

/-- If a linear map has determinant different from `1`, then the space is finite-dimensional. -/
lemma finite_dimensional_of_det_ne_one {ğ•œ : Type*} [field ğ•œ] [module ğ•œ M]
  (f : M â†’â‚—[ğ•œ] M) (hf : f.det â‰  1) : finite_dimensional ğ•œ M :=
begin
  by_cases H : âˆƒ (s : finset M), nonempty (basis s ğ•œ M),
  { rcases H with âŸ¨s, âŸ¨hsâŸ©âŸ©, exact finite_dimensional.of_fintype_basis hs },
  { classical,
    simp [linear_map.coe_det, H] at hf,
    exact hf.elim }
end

/-- If the determinant of a map vanishes, then the map is not onto. -/
lemma range_lt_top_of_det_eq_zero {ğ•œ : Type*} [field ğ•œ] [module ğ•œ M]
  {f : M â†’â‚—[ğ•œ] M} (hf : f.det = 0) : f.range < âŠ¤ :=
begin
  haveI : finite_dimensional ğ•œ M, by simp [f.finite_dimensional_of_det_ne_one, hf],
  contrapose hf,
  simp only [lt_top_iff_ne_top, not_not, â† is_unit_iff_range_eq_top] at hf,
  exact is_unit_iff_ne_zero.1 (f.is_unit_det hf)
end

/-- If the determinant of a map vanishes, then the map is not injective. -/
lemma bot_lt_ker_of_det_eq_zero {ğ•œ : Type*} [field ğ•œ] [module ğ•œ M]
  {f : M â†’â‚—[ğ•œ] M} (hf : f.det = 0) : âŠ¥ < f.ker :=
begin
  haveI : finite_dimensional ğ•œ M, by simp [f.finite_dimensional_of_det_ne_one, hf],
  contrapose hf,
  simp only [bot_lt_iff_ne_bot, not_not, â† is_unit_iff_ker_eq_bot] at hf,
  exact is_unit_iff_ne_zero.1 (f.is_unit_det hf)
end

end linear_map

namespace linear_equiv

/-- On a `linear_equiv`, the domain of `linear_map.det` can be promoted to `RË£`. -/
protected def det : (M â‰ƒâ‚—[R] M) â†’* RË£ :=
(units.map (linear_map.det : (M â†’â‚—[R] M) â†’* R)).comp
  (linear_map.general_linear_group.general_linear_equiv R M).symm.to_monoid_hom

@[simp] lemma coe_det (f : M â‰ƒâ‚—[R] M) : â†‘f.det = linear_map.det (f : M â†’â‚—[R] M) := rfl
@[simp] lemma coe_inv_det (f : M â‰ƒâ‚—[R] M) : â†‘(f.detâ»Â¹) = linear_map.det (f.symm : M â†’â‚—[R] M) := rfl

@[simp] lemma det_refl : (linear_equiv.refl R M).det = 1 := units.ext $ linear_map.det_id

@[simp] lemma det_trans (f g : M â‰ƒâ‚—[R] M) : (f.trans g).det = g.det * f.det := map_mul _ g f

@[simp] lemma det_symm (f : M â‰ƒâ‚—[R] M) : f.symm.det = f.detâ»Â¹ := map_inv _ f

/-- Conjugating a linear equiv by a linear equiv does not change its determinant. -/
@[simp] lemma det_conj (f : M â‰ƒâ‚—[R] M) (e : M â‰ƒâ‚—[R] M') :
  ((e.symm.trans f).trans e).det = f.det :=
by rw [â†units.eq_iff, coe_det, coe_det, â†comp_coe, â†comp_coe, linear_map.det_conj]

end linear_equiv

/-- The determinants of a `linear_equiv` and its inverse multiply to 1. -/
@[simp] lemma linear_equiv.det_mul_det_symm {A : Type*} [comm_ring A] [module A M]
  (f : M â‰ƒâ‚—[A] M) : (f : M â†’â‚—[A] M).det * (f.symm : M â†’â‚—[A] M).det = 1 :=
by simp [â†linear_map.det_comp]

/-- The determinants of a `linear_equiv` and its inverse multiply to 1. -/
@[simp] lemma linear_equiv.det_symm_mul_det {A : Type*} [comm_ring A] [module A M]
  (f : M â‰ƒâ‚—[A] M) : (f.symm : M â†’â‚—[A] M).det * (f : M â†’â‚—[A] M).det = 1 :=
by simp [â†linear_map.det_comp]

-- Cannot be stated using `linear_map.det` because `f` is not an endomorphism.
lemma linear_equiv.is_unit_det (f : M â‰ƒâ‚—[R] M') (v : basis Î¹ R M) (v' : basis Î¹ R M') :
  is_unit (linear_map.to_matrix v v' f).det :=
begin
  apply is_unit_det_of_left_inverse,
  simpa using (linear_map.to_matrix_comp v v' v f.symm f).symm
end

/-- Specialization of `linear_equiv.is_unit_det` -/
lemma linear_equiv.is_unit_det' {A : Type*} [comm_ring A] [module A M]
  (f : M â‰ƒâ‚—[A] M) : is_unit (linear_map.det (f : M â†’â‚—[A] M)) :=
is_unit_of_mul_eq_one _ _ f.det_mul_det_symm

/-- The determinant of `f.symm` is the inverse of that of `f` when `f` is a linear equiv. -/
lemma linear_equiv.det_coe_symm {ğ•œ : Type*} [field ğ•œ] [module ğ•œ M]
  (f : M â‰ƒâ‚—[ğ•œ] M) : (f.symm : M â†’â‚—[ğ•œ] M).det = (f : M â†’â‚—[ğ•œ] M).det â»Â¹ :=
by field_simp [is_unit.ne_zero f.is_unit_det']

/-- Builds a linear equivalence from a linear map whose determinant in some bases is a unit. -/
@[simps]
def linear_equiv.of_is_unit_det {f : M â†’â‚—[R] M'} {v : basis Î¹ R M} {v' : basis Î¹ R M'}
  (h : is_unit (linear_map.to_matrix v v' f).det) : M â‰ƒâ‚—[R] M' :=
{ to_fun := f,
  map_add' := f.map_add,
  map_smul' := f.map_smul,
  inv_fun := to_lin v' v (to_matrix v v' f)â»Â¹,
  left_inv := Î» x,
    calc to_lin v' v (to_matrix v v' f)â»Â¹ (f x)
        = to_lin v v ((to_matrix v v' f)â»Â¹ â¬ to_matrix v v' f) x :
      by { rw [to_lin_mul v v' v, to_lin_to_matrix, linear_map.comp_apply] }
    ... = x : by simp [h],
  right_inv := Î» x,
    calc f (to_lin v' v (to_matrix v v' f)â»Â¹ x)
        = to_lin v' v' (to_matrix v v' f â¬ (to_matrix v v' f)â»Â¹) x :
      by { rw [to_lin_mul v' v v', linear_map.comp_apply, to_lin_to_matrix v v'] }
    ... = x : by simp [h] }

@[simp] lemma linear_equiv.coe_of_is_unit_det {f : M â†’â‚—[R] M'} {v : basis Î¹ R M} {v' : basis Î¹ R M'}
  (h : is_unit (linear_map.to_matrix v v' f).det) :
  (linear_equiv.of_is_unit_det h : M â†’â‚—[R] M') = f :=
by { ext x, refl }

/-- Builds a linear equivalence from a linear map on a finite-dimensional vector space whose
determinant is nonzero. -/
@[reducible] def linear_map.equiv_of_det_ne_zero
  {ğ•œ : Type*} [field ğ•œ] {M : Type*} [add_comm_group M] [module ğ•œ M]
  [finite_dimensional ğ•œ M] (f : M â†’â‚—[ğ•œ] M) (hf : linear_map.det f â‰  0) :
  M â‰ƒâ‚—[ğ•œ] M :=
have is_unit (linear_map.to_matrix (finite_dimensional.fin_basis ğ•œ M)
  (finite_dimensional.fin_basis ğ•œ M) f).det :=
    by simp only [linear_map.det_to_matrix, is_unit_iff_ne_zero.2 hf],
linear_equiv.of_is_unit_det this

lemma linear_map.associated_det_of_eq_comp (e : M â‰ƒâ‚—[R] M) (f f' : M â†’â‚—[R] M)
  (h : âˆ€ x, f x = f' (e x)) : associated f.det f'.det :=
begin
  suffices : associated (f' âˆ˜â‚— â†‘e).det f'.det,
  { convert this using 2, ext x, exact h x },
  rw [â† mul_one f'.det, linear_map.det_comp],
  exact associated.mul_left _ (associated_one_iff_is_unit.mpr e.is_unit_det')
end

lemma linear_map.associated_det_comp_equiv {N : Type*} [add_comm_group N] [module R N]
  (f : N â†’â‚—[R] M) (e e' : M â‰ƒâ‚—[R] N) :
  associated (f âˆ˜â‚— â†‘e).det (f âˆ˜â‚— â†‘e').det :=
begin
  refine linear_map.associated_det_of_eq_comp (e.trans e'.symm) _ _ _,
  intro x,
  simp only [linear_map.comp_apply, linear_equiv.coe_coe, linear_equiv.trans_apply,
             linear_equiv.apply_symm_apply],
end

/-- The determinant of a family of vectors with respect to some basis, as an alternating
multilinear map. -/
def basis.det : alternating_map R M R Î¹ :=
{ to_fun := Î» v, det (e.to_matrix v),
  map_add' := begin
    intros inst v i x y,
    cases subsingleton.elim inst â€¹_â€º,
    simp only [e.to_matrix_update, linear_equiv.map_add, finsupp.coe_add],
    exact det_update_column_add _ _ _ _,
  end,
  map_smul' := begin
    intros inst u i c x,
    cases subsingleton.elim inst â€¹_â€º,
    simp only [e.to_matrix_update, algebra.id.smul_eq_mul, linear_equiv.map_smul],
    apply det_update_column_smul
  end,
  map_eq_zero_of_eq' := begin
    intros v i j h hij,
    rw [â†function.update_eq_self i v, h, â†det_transpose, e.to_matrix_update,
        â†update_row_transpose, â†e.to_matrix_transpose_apply],
    apply det_zero_of_row_eq hij,
    rw [update_row_ne hij.symm, update_row_self],
  end }

lemma basis.det_apply (v : Î¹ â†’ M) : e.det v = det (e.to_matrix v) := rfl

lemma basis.det_self : e.det e = 1 :=
by simp [e.det_apply]

@[simp] lemma basis.det_is_empty [is_empty Î¹] : e.det = alternating_map.const_of_is_empty R M 1 :=
begin
  ext v,
  exact matrix.det_is_empty,
end

/-- `basis.det` is not the zero map. -/
lemma basis.det_ne_zero [nontrivial R] : e.det â‰  0 :=
Î» h, by simpa [h] using e.det_self

lemma is_basis_iff_det {v : Î¹ â†’ M} :
  linear_independent R v âˆ§ span R (set.range v) = âŠ¤ â†” is_unit (e.det v) :=
begin
  split,
  { rintro âŸ¨hli, hspanâŸ©,
    set v' := basis.mk hli hspan.ge with v'_eq,
    rw e.det_apply,
    convert linear_equiv.is_unit_det (linear_equiv.refl _ _) v' e using 2,
    ext i j,
    simp },
  { intro h,
    rw [basis.det_apply, basis.to_matrix_eq_to_matrix_constr] at h,
    set v' := basis.map e (linear_equiv.of_is_unit_det h) with v'_def,
    have : â‡‘ v' = v,
    { ext i, rw [v'_def, basis.map_apply, linear_equiv.of_is_unit_det_apply, e.constr_basis] },
    rw â† this,
    exact âŸ¨v'.linear_independent, v'.span_eqâŸ© },
end

lemma basis.is_unit_det (e' : basis Î¹ R M) : is_unit (e.det e') :=
(is_basis_iff_det e).mp âŸ¨e'.linear_independent, e'.span_eqâŸ©

/-- Any alternating map to `R` where `Î¹` has the cardinality of a basis equals the determinant
map with respect to that basis, multiplied by the value of that alternating map on that basis. -/
lemma alternating_map.eq_smul_basis_det (f : alternating_map R M R Î¹) : f = f e â€¢ e.det :=
begin
  refine basis.ext_alternating e (Î» i h, _),
  let Ïƒ : equiv.perm Î¹ := equiv.of_bijective i (finite.injective_iff_bijective.1 h),
  change f (e âˆ˜ Ïƒ) = (f e â€¢ e.det) (e âˆ˜ Ïƒ),
  simp [alternating_map.map_perm, basis.det_self]
end

@[simp] lemma alternating_map.map_basis_eq_zero_iff {Î¹ : Type*} [finite Î¹]
  (e : basis Î¹ R M) (f : alternating_map R M R Î¹) :
  f e = 0 â†” f = 0 :=
âŸ¨Î» h, begin
  casesI nonempty_fintype Î¹,
  letI := classical.dec_eq Î¹,
  simpa [h] using f.eq_smul_basis_det e
end, Î» h, h.symm â–¸ alternating_map.zero_apply _âŸ©

lemma alternating_map.map_basis_ne_zero_iff {Î¹ : Type*} [finite Î¹]
  (e : basis Î¹ R M) (f : alternating_map R M R Î¹) :
  f e â‰  0 â†” f â‰  0 :=
not_congr $ f.map_basis_eq_zero_iff e

variables {A : Type*} [comm_ring A] [module A M]

@[simp] lemma basis.det_comp (e : basis Î¹ A M) (f : M â†’â‚—[A] M) (v : Î¹ â†’ M) :
  e.det (f âˆ˜ v) = f.det * e.det v :=
by { rw [basis.det_apply, basis.det_apply, â† f.det_to_matrix e, â† matrix.det_mul,
         e.to_matrix_eq_to_matrix_constr (f âˆ˜ v), e.to_matrix_eq_to_matrix_constr v,
         â† to_matrix_comp, e.constr_comp] }

@[simp] lemma basis.det_comp_basis [module A M']
  (b : basis Î¹ A M) (b' : basis Î¹ A M') (f : M â†’â‚—[A] M') :
  b'.det (f âˆ˜ b) = linear_map.det (f âˆ˜â‚— (b'.equiv b (equiv.refl Î¹) : M' â†’â‚—[A] M)) :=
begin
  rw [basis.det_apply, â† linear_map.det_to_matrix b', linear_map.to_matrix_comp _ b,
      matrix.det_mul, linear_map.to_matrix_basis_equiv, matrix.det_one, mul_one],
  congr' 1, ext i j,
  rw [basis.to_matrix_apply, linear_map.to_matrix_apply]
end

lemma basis.det_reindex {Î¹' : Type*} [fintype Î¹'] [decidable_eq Î¹']
  (b : basis Î¹ R M) (v : Î¹' â†’ M) (e : Î¹ â‰ƒ Î¹') :
  (b.reindex e).det v = b.det (v âˆ˜ e) :=
by rw [basis.det_apply, basis.to_matrix_reindex', det_reindex_alg_equiv, basis.det_apply]

lemma basis.det_reindex_symm {Î¹' : Type*} [fintype Î¹'] [decidable_eq Î¹']
  (b : basis Î¹ R M) (v : Î¹ â†’ M) (e : Î¹' â‰ƒ Î¹) :
  (b.reindex e.symm).det (v âˆ˜ e) = b.det v :=
by rw [basis.det_reindex, function.comp.assoc, e.self_comp_symm, function.comp.right_id]

@[simp]
lemma basis.det_map (b : basis Î¹ R M) (f : M â‰ƒâ‚—[R] M') (v : Î¹ â†’ M') :
  (b.map f).det v = b.det (f.symm âˆ˜ v) :=
by { rw [basis.det_apply, basis.to_matrix_map, basis.det_apply] }

lemma basis.det_map' (b : basis Î¹ R M) (f : M â‰ƒâ‚—[R] M') :
  (b.map f).det = b.det.comp_linear_map f.symm :=
alternating_map.ext $ b.det_map f

@[simp] lemma pi.basis_fun_det : (pi.basis_fun R Î¹).det = matrix.det_row_alternating :=
begin
  ext M,
  rw [basis.det_apply, basis.coe_pi_basis_fun.to_matrix_eq_transpose, det_transpose],
end

/-- If we fix a background basis `e`, then for any other basis `v`, we can characterise the
coordinates provided by `v` in terms of determinants relative to `e`. -/
lemma basis.det_smul_mk_coord_eq_det_update {v : Î¹ â†’ M}
  (hli : linear_independent R v) (hsp : âŠ¤ â‰¤ span R (range v)) (i : Î¹) :
  (e.det v) â€¢ (basis.mk hli hsp).coord i = e.det.to_multilinear_map.to_linear_map v i :=
begin
  apply (basis.mk hli hsp).ext,
  intros k,
  rcases eq_or_ne k i with rfl | hik;
  simp only [algebra.id.smul_eq_mul, basis.coe_mk, linear_map.smul_apply, linear_map.coe_mk,
    multilinear_map.to_linear_map_apply],
  { rw [basis.mk_coord_apply_eq, mul_one, update_eq_self], congr, },
  { rw [basis.mk_coord_apply_ne hik, mul_zero, eq_comm],
    exact e.det.map_eq_zero_of_eq _ (by simp [hik, function.update_apply]) hik, },
end

/-- If a basis is multiplied columnwise by scalars `w : Î¹ â†’ RË£`, then the determinant with respect
to this basis is multiplied by the product of the inverse of these scalars. -/
lemma basis.det_units_smul (e : basis Î¹ R M) (w : Î¹ â†’ RË£) :
  (e.units_smul w).det = (â†‘(âˆ i, w i)â»Â¹ : R) â€¢ e.det :=
begin
  ext f,
  change matrix.det (Î» i j, (e.units_smul w).repr (f j) i)
    = (â†‘(âˆ i, w i)â»Â¹ : R) â€¢ matrix.det (Î» i j, e.repr (f j) i),
  simp only [e.repr_units_smul],
  convert matrix.det_mul_column (Î» i, (â†‘((w i)â»Â¹) : R)) (Î» i j, e.repr (f j) i),
  simp [â† finset.prod_inv_distrib]
end

/-- The determinant of a basis constructed by `units_smul` is the product of the given units. -/
@[simp] lemma basis.det_units_smul_self (w : Î¹ â†’ RË£) : e.det (e.units_smul w) = âˆ i, w i :=
by simp [basis.det_apply]

/-- The determinant of a basis constructed by `is_unit_smul` is the product of the given units. -/
@[simp] lemma basis.det_is_unit_smul {w : Î¹ â†’ R} (hw : âˆ€ i, is_unit (w i)) :
  e.det (e.is_unit_smul hw) = âˆ i, w i :=
e.det_units_smul_self _
