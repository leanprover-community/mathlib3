/-
Copyright (c) 2018 Mario Carneiro. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Mario Carneiro, Johannes HÃ¶lzl, Sander Dahmen

Dimension of modules and vector spaces.
-/
import linear_algebra.basis
import set_theory.ordinal
noncomputable theory

universes u u' u'' v v' w w'

variables {ğ•œ : Type u} {V Vâ‚‚ Vâ‚ƒ Vâ‚„ : Type v}
variables {Î¹ : Type w} {Î¹' : Type w'} {Î· : Type u''} {Ï† : Î· â†’ Type u'}
-- TODO: relax these universe constraints

open_locale classical

section vector_space
variables [discrete_field ğ•œ] [add_comm_group V] [vector_space ğ•œ V]
include ğ•œ
open submodule lattice function set

variables (ğ•œ V)
def vector_space.dim : cardinal :=
cardinal.min
  (nonempty_subtype.2 (@exists_is_basis ğ•œ V _ _ _))
  (Î» b, cardinal.mk b.1)
variables {ğ•œ V}

open vector_space

section
set_option class.instance_max_depth 50
theorem is_basis.le_span (zero_ne_one : (0 : ğ•œ) â‰  1) {v : Î¹ â†’ V} {J : set V} (hv : is_basis ğ•œ v)
   (hJ : span ğ•œ J = âŠ¤) : cardinal.mk (range v) â‰¤ cardinal.mk J :=
begin
  cases le_or_lt cardinal.omega (cardinal.mk J) with oJ oJ,
  { have := cardinal.mk_range_eq_of_inj  (linear_independent.injective zero_ne_one hv.1),
    let S : J â†’ set Î¹ := Î» j, (is_basis.repr hv j).support.to_set,
    let S' : J â†’ set V := Î» j, v '' S j,
    have hs : range v âŠ† â‹ƒ j, S' j,
    { intros b hb,
      rcases mem_range.1 hb with âŸ¨i, hiâŸ©,
      have : span ğ•œ J â‰¤ comap hv.repr (finsupp.supported ğ•œ ğ•œ (â‹ƒ j, S j)) :=
        span_le.2 (Î» j hj x hx, âŸ¨_, âŸ¨âŸ¨j, hjâŸ©, rflâŸ©, hxâŸ©),
      rw hJ at this,
      replace : hv.repr (v i) âˆˆ (finsupp.supported ğ•œ ğ•œ (â‹ƒ j, S j)) := this trivial,
      rw [hv.repr_eq_single, finsupp.mem_supported, finsupp.support_single_ne_zero zero_ne_one.symm] at this,
      rw â† hi,
      apply mem_Union.2,
      rcases mem_Union.1 (this (mem_singleton _)) with âŸ¨j, hjâŸ©,
      use j,
      rw mem_image,
      use i,
      exact âŸ¨hj, rflâŸ© },
    refine le_of_not_lt (Î» IJ, _),
    suffices : cardinal.mk (â‹ƒ j, S' j) < cardinal.mk (range v),
    { exact not_le_of_lt this âŸ¨set.embedding_of_subset hsâŸ© },
    refine lt_of_le_of_lt (le_trans cardinal.mk_Union_le_sum_mk
      (cardinal.sum_le_sum _ (Î» _, cardinal.omega) _)) _,
    { exact Î» j, le_of_lt (cardinal.lt_omega_iff_finite.2 $ finite_image _ (finset.finite_to_set _)) },
    { rwa [cardinal.sum_const, cardinal.mul_eq_max oJ (le_refl _), max_eq_left oJ] } },
  { rcases exists_finite_card_le_of_finite_of_linear_independent_of_span
      (cardinal.lt_omega_iff_finite.1 oJ) hv.1.to_subtype_range _ with âŸ¨fI, hiâŸ©,
    { rwa [â† cardinal.nat_cast_le, cardinal.finset_card, finset.coe_to_finset,
        cardinal.finset_card, finset.coe_to_finset] at hi, },
    { rw hJ, apply set.subset_univ } },
end
end

/-- dimension theorem -/
theorem mk_eq_mk_of_basis {v : Î¹ â†’ V} {v' : Î¹' â†’ V}
  (hv : is_basis ğ•œ v) (hv' : is_basis ğ•œ v') :
  cardinal.lift.{w w'} (cardinal.mk Î¹) = cardinal.lift.{w' w} (cardinal.mk Î¹') :=
begin
  rw â†cardinal.lift_inj.{(max w w') v},
  rw [cardinal.lift_lift, cardinal.lift_lift],
  apply le_antisymm,
  { convert cardinal.lift_le.{v (max w w')}.2 (hv.le_span zero_ne_one hv'.2),
    { rw cardinal.lift_max.{w v w'},
      apply (cardinal.mk_range_eq_of_inj (hv.injective zero_ne_one)).symm, },
    { rw cardinal.lift_max.{w' v w},
      apply (cardinal.mk_range_eq_of_inj (hv'.injective zero_ne_one)).symm, }, },
  { convert cardinal.lift_le.{v (max w w')}.2 (hv'.le_span zero_ne_one hv.2),
    { rw cardinal.lift_max.{w' v w},
      apply (cardinal.mk_range_eq_of_inj (hv'.injective zero_ne_one)).symm, },
    { rw cardinal.lift_max.{w v w'},
      apply (cardinal.mk_range_eq_of_inj (hv.injective zero_ne_one)).symm, }, }
end

theorem is_basis.mk_range_eq_dim {v : Î¹ â†’ V} (h : is_basis ğ•œ v) :
  cardinal.mk (range v) = dim ğ•œ V :=
begin
  have := show âˆƒ v', dim ğ•œ V = _, from cardinal.min_eq _ _,
  rcases this with âŸ¨v', eâŸ©,
  rw e,
  apply cardinal.lift_inj.1,
  rw cardinal.mk_range_eq_of_inj (h.injective zero_ne_one),
  convert @mk_eq_mk_of_basis _ _ _ _ _ _ _ _ _ h v'.property
end

theorem is_basis.mk_eq_dim {v : Î¹ â†’ V} (h : is_basis ğ•œ v) :
  cardinal.lift.{w v} (cardinal.mk Î¹) = cardinal.lift.{v w} (dim ğ•œ V) :=
by rw [â†h.mk_range_eq_dim, cardinal.mk_range_eq_of_inj (h.injective zero_ne_one)]

variables [add_comm_group Vâ‚‚] [vector_space ğ•œ Vâ‚‚]

theorem linear_equiv.dim_eq (f : V â‰ƒâ‚—[ğ•œ] Vâ‚‚) :
  dim ğ•œ V = dim ğ•œ Vâ‚‚ :=
by letI := classical.dec_eq V;
letI := classical.dec_eq Vâ‚‚; exact
let âŸ¨b, hbâŸ© := exists_is_basis ğ•œ V in
cardinal.lift_inj.1 $ hb.mk_eq_dim.symm.trans (f.is_basis hb).mk_eq_dim

@[simp] lemma dim_bot : dim ğ•œ (âŠ¥ : submodule ğ•œ V) = 0 :=
by letI := classical.dec_eq V;
  rw [â† cardinal.lift_inj, â† (@is_basis_empty_bot pempty ğ•œ V _ _ _ not_nonempty_pempty).mk_eq_dim,
    cardinal.mk_pempty]

@[simp] lemma dim_top : dim ğ•œ (âŠ¤ : submodule ğ•œ V) = dim ğ•œ V :=
linear_equiv.dim_eq (linear_equiv.of_top _ rfl)

lemma dim_of_field (ğ•œ : Type*) [discrete_field ğ•œ] : dim ğ•œ ğ•œ = 1 :=
by rw [â†cardinal.lift_inj, â† (@is_basis_singleton_one punit ğ•œ _ _).mk_eq_dim, cardinal.mk_punit]

lemma dim_span {v : Î¹ â†’ V} (hv : linear_independent ğ•œ v) :
  dim ğ•œ â†¥(span ğ•œ (range v)) = cardinal.mk (range v) :=
by rw [â†cardinal.lift_inj, â† (is_basis_span hv).mk_eq_dim,
    cardinal.mk_range_eq_of_inj (@linear_independent.injective Î¹ ğ•œ V v _ _ _ zero_ne_one hv)]

lemma dim_span_set {s : set V} (hs : linear_independent ğ•œ (Î» x, x : s â†’ V)) :
  dim ğ•œ â†¥(span ğ•œ s) = cardinal.mk s :=
by rw [â† @set_of_mem_eq _ s, â† subtype.val_range]; exact dim_span hs

lemma dim_span_le (s : set V) : dim ğ•œ (span ğ•œ s) â‰¤ cardinal.mk s :=
begin
  classical,
  rcases
    exists_linear_independent linear_independent_empty (set.empty_subset s)
    with âŸ¨b, hb, _, hsb, hlibâŸ©,
  have hsab : span ğ•œ s = span ğ•œ b,
    from span_eq_of_le _ hsb (span_le.2 (Î» x hx, subset_span (hb hx))),
  convert cardinal.mk_le_mk_of_subset hb,
  rw [hsab, dim_span_set hlib]
end

lemma dim_span_of_finset (s : finset V) :
  dim ğ•œ (span ğ•œ (â†‘s : set V)) < cardinal.omega :=
calc dim ğ•œ (span ğ•œ (â†‘s : set V)) â‰¤ cardinal.mk (â†‘s : set V) : dim_span_le â†‘s
                             ... = s.card : by rw â†cardinal.finset_card
                             ... < cardinal.omega : cardinal.nat_lt_omega _

theorem dim_prod : dim ğ•œ (V Ã— Vâ‚‚) = dim ğ•œ V + dim ğ•œ Vâ‚‚ :=
begin
  letI := classical.dec_eq V,
  letI := classical.dec_eq Vâ‚‚,
  rcases exists_is_basis ğ•œ V with âŸ¨b, hbâŸ©,
  rcases exists_is_basis ğ•œ Vâ‚‚ with âŸ¨c, hcâŸ©,
  rw [â† cardinal.lift_inj,
      â† @is_basis.mk_eq_dim ğ•œ (V Ã— Vâ‚‚) _ _ _ _ _ (is_basis_inl_union_inr hb hc),
      cardinal.lift_add, cardinal.lift_mk,
      â† hb.mk_eq_dim, â† hc.mk_eq_dim,
      cardinal.lift_mk, cardinal.lift_mk,
      cardinal.add_def (ulift b) (ulift c)],
  exact cardinal.lift_inj.1 (cardinal.lift_mk_eq.2
      âŸ¨equiv.ulift.trans (equiv.sum_congr (@equiv.ulift b) (@equiv.ulift c)).symm âŸ©),
end

theorem dim_quotient (p : submodule ğ•œ V) :
  dim ğ•œ p.quotient + dim ğ•œ p = dim ğ•œ V :=
by classical; exact let âŸ¨fâŸ© := quotient_prod_linear_equiv p in dim_prod.symm.trans f.dim_eq

/-- rank-nullity theorem -/
theorem dim_range_add_dim_ker (f : V â†’â‚—[ğ•œ] Vâ‚‚) : dim ğ•œ f.range + dim ğ•œ f.ker = dim ğ•œ V :=
begin
  haveI := Î» (p : submodule ğ•œ V), classical.dec_eq p.quotient,
  rw [â† f.quot_ker_equiv_range.dim_eq, dim_quotient]
end

lemma dim_range_le (f : V â†’â‚—[ğ•œ] Vâ‚‚) : dim ğ•œ f.range â‰¤ dim ğ•œ V :=
by rw â† dim_range_add_dim_ker f; exact le_add_right (le_refl _)

lemma dim_map_le (f : V â†’â‚— Vâ‚‚) (p : submodule ğ•œ V) : dim ğ•œ (p.map f) â‰¤ dim ğ•œ p :=
begin
  have h := dim_range_le (f.comp (submodule.subtype p)),
  rwa [linear_map.range_comp, range_subtype] at h,
end

lemma dim_range_of_surjective (f : V â†’â‚—[ğ•œ] Vâ‚‚) (h : surjective f) : dim ğ•œ f.range = dim ğ•œ Vâ‚‚ :=
begin
  refine linear_equiv.dim_eq (linear_equiv.of_bijective (submodule.subtype _) _ _),
  exact linear_map.ker_eq_bot.2 subtype.val_injective,
  rwa [range_subtype, linear_map.range_eq_top]
end

lemma dim_eq_surjective (f : V â†’â‚—[ğ•œ] Vâ‚‚) (h : surjective f) : dim ğ•œ V = dim ğ•œ Vâ‚‚ + dim ğ•œ f.ker :=
by rw [â† dim_range_add_dim_ker f, â† dim_range_of_surjective f h]

lemma dim_le_surjective (f : V â†’â‚—[ğ•œ] Vâ‚‚) (h : surjective f) : dim ğ•œ Vâ‚‚ â‰¤ dim ğ•œ V :=
by rw [dim_eq_surjective f h]; refine le_add_right (le_refl _)

lemma dim_eq_injective (f : V â†’â‚—[ğ•œ] Vâ‚‚) (h : injective f) : dim ğ•œ V = dim ğ•œ f.range :=
by rw [â† dim_range_add_dim_ker f, linear_map.ker_eq_bot.2 h]; simp [dim_bot]

set_option class.instance_max_depth 37
lemma dim_submodule_le (s : submodule ğ•œ V) : dim ğ•œ s â‰¤ dim ğ•œ V :=
begin
  letI := classical.dec_eq V,
  rcases exists_is_basis ğ•œ s with âŸ¨bs, hbsâŸ©,
  have : linear_independent ğ•œ (Î» (i : bs), submodule.subtype s i.val) :=
    (linear_independent.image hbs.1) (linear_map.disjoint_ker'.2
        (Î» _ _ _ _ h, subtype.val_injective h)),
  rcases exists_subset_is_basis (this.to_subtype_range) with âŸ¨b, hbs_b, hbâŸ©,
  rw [â†cardinal.lift_le, â† is_basis.mk_eq_dim hbs, â† is_basis.mk_eq_dim hb, cardinal.lift_le],
  have : subtype.val '' bs âŠ† b,
  { convert hbs_b,
    rw [@range_comp _ _ _ (Î» (i : bs), (i.val)) (submodule.subtype s),
      â†image_univ, submodule.subtype],
    simp only [subtype.val_image_univ],
    refl },
  calc cardinal.mk â†¥bs = cardinal.mk ((subtype.val : s â†’ V) '' bs) :
      (cardinal.mk_image_eq $ subtype.val_injective).symm
    ... â‰¤ cardinal.mk â†¥b :
      nonempty.intro (embedding_of_subset this),
end
set_option class.instance_max_depth 32

lemma dim_le_injective (f : V â†’â‚—[ğ•œ] Vâ‚‚) (h : injective f) :
  dim ğ•œ V â‰¤ dim ğ•œ Vâ‚‚ :=
by rw [dim_eq_injective f h]; exact dim_submodule_le _

lemma dim_le_of_submodule (s t : submodule ğ•œ V) (h : s â‰¤ t) : dim ğ•œ s â‰¤ dim ğ•œ t :=
dim_le_injective (of_le h) $ assume âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© eq,
  subtype.eq $ show x = y, from subtype.ext.1 eq

section
variables [add_comm_group Vâ‚ƒ] [vector_space ğ•œ Vâ‚ƒ]
variables [add_comm_group Vâ‚„] [vector_space ğ•œ Vâ‚„]
set_option class.instance_max_depth 70
open linear_map

/-- This is mostly an auxiliary lemma for `dim_sup_add_dim_inf_eq` -/
lemma dim_add_dim_split
  (db : Vâ‚ƒ â†’â‚—[ğ•œ] V) (eb : Vâ‚„ â†’â‚—[ğ•œ] V) (cd : Vâ‚‚ â†’â‚—[ğ•œ] Vâ‚ƒ) (ce : Vâ‚‚ â†’â‚—[ğ•œ] Vâ‚„)
  (hde : âŠ¤ â‰¤ db.range âŠ” eb.range)
  (hgd : ker cd = âŠ¥)
  (eq : db.comp cd = eb.comp ce)
  (eqâ‚‚ : âˆ€d e, db d = eb e â†’ (âˆƒc, cd c = d âˆ§ ce c = e)) :
  dim ğ•œ V + dim ğ•œ Vâ‚‚ = dim ğ•œ Vâ‚ƒ + dim ğ•œ Vâ‚„ :=
have hf : surjective (copair db eb),
begin
  refine (range_eq_top.1 $ top_unique $ _),
  rwa [â† map_top, â† prod_top, map_copair_prod]
end,
begin
  conv {to_rhs, rw [â† dim_prod, dim_eq_surjective _ hf] },
  congr' 1,
  apply linear_equiv.dim_eq,
  fapply linear_equiv.of_bijective,
  { refine cod_restrict _ (pair cd (- ce)) _,
    { assume c,
      simp [add_eq_zero_iff_eq_neg],
      exact linear_map.ext_iff.1 eq c } },
  { rw [ker_cod_restrict, ker_pair, hgd, bot_inf_eq] },
  { rw [eq_top_iff, range_cod_restrict, â† map_le_iff_le_comap, map_top, range_subtype],
    rintros âŸ¨d, eâŸ©,
    have h := eqâ‚‚ d (-e),
    simp [add_eq_zero_iff_eq_neg] at âŠ¢ h,
    assume hde,
    rcases h hde with âŸ¨c, hâ‚, hâ‚‚âŸ©,
    refine âŸ¨c, hâ‚, _âŸ©,
    rw [hâ‚‚, _root_.neg_neg] }
end

lemma dim_sup_add_dim_inf_eq (s t : submodule ğ•œ V) :
  dim ğ•œ (s âŠ” t : submodule ğ•œ V) + dim ğ•œ (s âŠ“ t : submodule ğ•œ V) = dim ğ•œ s + dim ğ•œ t :=
dim_add_dim_split (of_le le_sup_left) (of_le le_sup_right) (of_le inf_le_left) (of_le inf_le_right)
  begin
    rw [â† map_le_map_iff (ker_subtype $ s âŠ” t), map_sup, map_top,
      â† linear_map.range_comp, â† linear_map.range_comp, subtype_comp_of_le, subtype_comp_of_le,
      range_subtype, range_subtype, range_subtype],
    exact le_refl _
  end
  (ker_of_le _ _ _)
  begin ext âŸ¨x, hxâŸ©, refl end
  begin
    rintros âŸ¨bâ‚, hbâ‚âŸ© âŸ¨bâ‚‚, hbâ‚‚âŸ© eq,
    have : bâ‚ = bâ‚‚ := congr_arg subtype.val eq,
    subst this,
    exact âŸ¨âŸ¨bâ‚, hbâ‚, hbâ‚‚âŸ©, rfl, rflâŸ©
  end

lemma dim_add_le_dim_add_dim (s t : submodule ğ•œ V) :
  dim ğ•œ (s âŠ” t : submodule ğ•œ V) â‰¤ dim ğ•œ s + dim ğ•œ t :=
by rw [â† dim_sup_add_dim_inf_eq]; exact le_add_right (le_refl _)

end

section fintype
variable [fintype Î·]
variables [âˆ€i, add_comm_group (Ï† i)] [âˆ€i, vector_space ğ•œ (Ï† i)]

open linear_map

lemma dim_pi : vector_space.dim ğ•œ (Î i, Ï† i) = cardinal.sum (Î»i, vector_space.dim ğ•œ (Ï† i)) :=
begin
  choose b hb using assume i, exists_is_basis ğ•œ (Ï† i),
  have : is_basis ğ•œ (Î» (ji : Î£ j, b j), std_basis ğ•œ (Î» j, Ï† j) ji.fst ji.snd.val),
    by apply pi.is_basis_std_basis _ hb,
  rw [â†cardinal.lift_inj, â† this.mk_eq_dim],
  simp [Î» i, (hb i).mk_range_eq_dim.symm, cardinal.sum_mk]
end

lemma dim_fun {V Î· : Type u} [fintype Î·] [add_comm_group V] [vector_space ğ•œ V] :
  vector_space.dim ğ•œ (Î· â†’ V) = fintype.card Î· * vector_space.dim ğ•œ V :=
by rw [dim_pi, cardinal.sum_const, cardinal.fintype_card]

lemma dim_fun_eq_lift_mul :
  vector_space.dim ğ•œ (Î· â†’ V) = (fintype.card Î· : cardinal.{max u'' v}) *
    cardinal.lift.{v u''} (vector_space.dim ğ•œ V) :=
by rw [dim_pi, cardinal.sum_const_eq_lift_mul, cardinal.fintype_card, cardinal.lift_nat_cast]

lemma dim_fun' : vector_space.dim ğ•œ (Î· â†’ ğ•œ) = fintype.card Î· :=
by rw [dim_fun_eq_lift_mul, dim_of_field ğ•œ, cardinal.lift_one, mul_one, cardinal.nat_cast_inj]

lemma dim_fin_fun (n : â„•) : dim ğ•œ (fin n â†’ ğ•œ) = n :=
by simp [dim_fun']

end fintype

lemma exists_mem_ne_zero_of_ne_bot {s : submodule ğ•œ V} (h : s â‰  âŠ¥) : âˆƒ b : V, b âˆˆ s âˆ§ b â‰  0 :=
begin
  classical,
  by_contradiction hex,
  have : âˆ€xâˆˆs, (x:V) = 0, { simpa only [not_exists, not_and, not_not, ne.def] using hex },
  exact (h $ bot_unique $ assume s hs, (submodule.mem_bot ğ•œ).2 $ this s hs)
end

lemma exists_mem_ne_zero_of_dim_pos {s : submodule ğ•œ V} (h : vector_space.dim ğ•œ s > 0) :
  âˆƒ b : V, b âˆˆ s âˆ§ b â‰  0 :=
exists_mem_ne_zero_of_ne_bot $ assume eq, by rw [(>), eq, dim_bot] at h; exact lt_irrefl _ h

lemma exists_is_basis_fintype (h : dim ğ•œ V < cardinal.omega) :
  âˆƒ s : (set V), (is_basis ğ•œ (subtype.val : s â†’ V)) âˆ§ nonempty (fintype s) :=
begin
  cases exists_is_basis ğ•œ V with s hs,
  rw [â†cardinal.lift_lt, â† is_basis.mk_eq_dim hs, cardinal.lift_lt,
      cardinal.lt_omega_iff_fintype] at h,
  exact âŸ¨s, hs, hâŸ©
end

section rank

def rank (f : V â†’â‚—[ğ•œ] Vâ‚‚) : cardinal := dim ğ•œ f.range

lemma rank_le_domain (f : V â†’â‚—[ğ•œ] Vâ‚‚) : rank f â‰¤ dim ğ•œ V :=
by rw [â† dim_range_add_dim_ker f]; exact le_add_right (le_refl _)

lemma rank_le_range (f : V â†’â‚—[ğ•œ] Vâ‚‚) : rank f â‰¤ dim ğ•œ Vâ‚‚ :=
dim_submodule_le _

lemma rank_add_le (f g : V â†’â‚—[ğ•œ] Vâ‚‚) : rank (f + g) â‰¤ rank f + rank g :=
calc rank (f + g) â‰¤ dim ğ•œ (f.range âŠ” g.range : submodule ğ•œ Vâ‚‚) :
  begin
    refine dim_le_of_submodule _ _ _,
    exact (linear_map.range_le_iff_comap.2 $ eq_top_iff'.2 $
      assume x, show f x + g x âˆˆ (f.range âŠ” g.range : submodule ğ•œ Vâ‚‚), from
        mem_sup.2 âŸ¨_, mem_image_of_mem _ (mem_univ _), _, mem_image_of_mem _ (mem_univ _), rflâŸ©)
  end
  ... â‰¤ rank f + rank g : dim_add_le_dim_add_dim _ _

@[simp] lemma rank_zero : rank (0 : V â†’â‚—[ğ•œ] Vâ‚‚) = 0 :=
by rw [rank, linear_map.range_zero, dim_bot]

lemma rank_finset_sum_le {Î·} (s : finset Î·) (f : Î· â†’ V â†’â‚—[ğ•œ] Vâ‚‚) :
  rank (s.sum f) â‰¤ s.sum (Î» d, rank (f d)) :=
@finset.sum_hom_rel _ _ _ _ _ (Î»a b, rank a â‰¤ b) f (Î» d, rank (f d)) s (le_of_eq rank_zero)
      (Î» i g c h, le_trans (rank_add_le _ _) (add_le_add_left' h))

variables [add_comm_group Vâ‚ƒ] [vector_space ğ•œ Vâ‚ƒ]

lemma rank_comp_le1 (g : V â†’â‚—[ğ•œ] Vâ‚‚) (f : Vâ‚‚ â†’â‚—[ğ•œ] Vâ‚ƒ) : rank (f.comp g) â‰¤ rank f :=
begin
  refine dim_le_of_submodule _ _ _,
  rw [linear_map.range_comp],
  exact image_subset _ (subset_univ _)
end

lemma rank_comp_le2 (g : V â†’â‚—[ğ•œ] Vâ‚‚) (f : Vâ‚‚ â†’â‚— Vâ‚ƒ) : rank (f.comp g) â‰¤ rank g :=
by rw [rank, rank, linear_map.range_comp]; exact dim_map_le _ _

end rank

end vector_space

section unconstrained_universes

variables {E : Type v'}
variables [discrete_field ğ•œ] [add_comm_group V] [vector_space ğ•œ V]
          [add_comm_group E] [vector_space ğ•œ E]
open vector_space

/-- Version of linear_equiv.dim_eq without universe constraints. -/
theorem linear_equiv.dim_eq_lift (f : V â‰ƒâ‚—[ğ•œ] E) :
  cardinal.lift.{v v'} (dim ğ•œ V) = cardinal.lift.{v' v} (dim ğ•œ E) :=
begin
  cases exists_is_basis ğ•œ V with b hb,
  rw [â† cardinal.lift_inj.1 hb.mk_eq_dim, â† (f.is_basis hb).mk_eq_dim, cardinal.lift_mk],
end

end unconstrained_universes
