/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao, Kevin Buzzard
-/

import tactic.basic
import algebra.big_operators.basic
import analysis.inner_product_space.basic
import analysis.normed_space.is_R_or_C

/-!
# Gram-Schmidt Orthogonalization and Orthonormalization

In this file we introduce Gram-Schmidt Orthogonalization and Orthonormalization

## Main results

- `proj` : projection between two vectors in the inner product space
- `GS`   : Gram-Schmidt Process
- `GS_Orthogonal` : the proof that "GS" produces an orthogonal system of vectors
- `GS_unit` : Normalized "Gram-Schmidt" (i.e each vector in this system has unit length)
- `GS_Orthornormal` : the proof that "GS_unit" produces an orthornormal system of vectors
-/

open_locale big_operators

variables (ğ•œ : Type*) (E : Type*) [is_R_or_C ğ•œ] [inner_product_space ğ•œ E]

/-- projection in the inner product space -/
def proj (u v : E) : E := ((inner u v) / (inner u u) : ğ•œ) â€¢ u

/-- Definition of Gram-Schmidt Process -/
def GS (f : â„• â†’ E) : â„• â†’ E
| n := f n - âˆ‘ i in finset.range(n),
  if h1 : i < n then proj ğ•œ E (GS i) (f n) else f 37

/-- 'GS_n_1' helps us to get rid of 'ite' in the definition of GS -/
@[simp] lemma GS_n_1 (f : â„• â†’ E) (n : â„•) :
GS ğ•œ E f (n + 1) = f (n + 1) - âˆ‘ i in finset.range(n + 1), proj ğ•œ E (GS ğ•œ E f i) (f (n + 1)) :=
begin
  rw [GS, sub_right_inj],
  apply finset.sum_congr rfl,
  intros x hx,
  rw finset.mem_range at hx,
  rw if_pos hx,
end

/-- # Gram-Schmidt Orthogonalisation -/
theorem GS_orthogonal (f : â„• â†’ E) (a b : â„•) (hâ‚€ : a < b) :
(inner (GS ğ•œ E f a) (GS ğ•œ E f b) : ğ•œ) = 0 :=
begin
  have hc : âˆƒ c, b â‰¤ c := by refine âŸ¨b+1, by linarithâŸ©,
  cases hc with c hâ‚,
  induction c with c hc generalizing a b,
  { simp at hâ‚, simp [hâ‚] at hâ‚€, contradiction },
  { rw nat.le_add_one_iff at hâ‚, cases hâ‚ with hbâ‚ hbâ‚‚,
    { exact hc _ _ hâ‚€ hbâ‚ },
    { simp only [GS_n_1, hbâ‚‚, inner_sub_right, inner_sum],
      have hâ‚‚ : âˆ€ x âˆˆ finset.range(c + 1), x â‰  a
      â†’ (inner (GS ğ•œ E f a) (proj ğ•œ E (GS ğ•œ E f x) (f (c + 1))) : ğ•œ) = 0,
      { intros x hxâ‚ hxâ‚‚, simp [proj], rw inner_smul_right,
        cases hxâ‚‚.lt_or_lt with hxaâ‚ hxaâ‚‚,
        { have haâ‚‚ : a â‰¤ c,
          { rw hbâ‚‚ at hâ‚€, exact nat.lt_succ_iff.mp hâ‚€ },
          specialize hc x a, simp [hxaâ‚, haâ‚‚] at hc,
          simp only [mul_eq_zero, div_eq_zero_iff, inner_self_eq_zero], right,
          rwa inner_eq_zero_sym at hc },
        { rw [finset.mem_range, nat.lt_succ_iff] at hxâ‚,
          specialize hc a x, simp [hxaâ‚‚, hxâ‚] at hc,
          simp only [mul_eq_zero, div_eq_zero_iff, inner_self_eq_zero], right,
          exact hc }},
      rw hbâ‚‚ at hâ‚€,
      have haâ‚‚ : a âˆˆ finset.range(c+1) := finset.mem_range.mpr hâ‚€,
      rw finset.sum_eq_single_of_mem a haâ‚‚ hâ‚‚, clear hâ‚‚,
      simp [proj], rw inner_smul_right,
      by_cases inner (GS ğ•œ E f a) (GS ğ•œ E f a) = (0 : ğ•œ),
      { simp [inner_self_eq_zero] at h,
        repeat {rw h}, simp only [inner_zero_left, mul_zero, sub_zero] },
      { simp [h] }}}
end

/-- Generalised Gram-Schmidt Orthorgonalization -/
theorem GS_orthogonal' (f : â„• â†’ E) (a b : â„•) (hâ‚€ : a â‰  b) :
(inner (GS ğ•œ E f a) (GS ğ•œ E f b) : ğ•œ) = 0 :=
begin
  cases hâ‚€.lt_or_lt with ha hb,
  { exact GS_orthogonal ğ•œ E f a b ha },
  { rw inner_eq_zero_sym,
    exact GS_orthogonal ğ•œ E f b a hb }
end

/-- Normalized Gram-Schmidt Process -/
noncomputable def GS_unit (f : â„• â†’ E) (n : â„•) : E :=
(âˆ¥ GS ğ•œ E f n âˆ¥ : ğ•œ)â»Â¹ â€¢ (GS ğ•œ E f n)

lemma GS_unit_length (f : â„• â†’ E) (n : â„•) (hf : GS ğ•œ E f n â‰  0) :
âˆ¥ GS_unit ğ•œ E f n âˆ¥ = 1 := by simp only [GS_unit, norm_smul_inv_norm hf]

/-- # Gram-Schmidt Orthonormalization -/
theorem GS_Orthonormal (f : â„• â†’ E) (h : âˆ€ n, GS ğ•œ E f n â‰  0) :
orthonormal ğ•œ (GS_unit ğ•œ E f) :=
begin
  simp [orthonormal], split,
  { simp [GS_unit_length, h] },
  { intros i j hij,
    simp [GS_unit, inner_smul_left, inner_smul_right], repeat {right},
    exact GS_orthogonal' ğ•œ E f i j hij }
end
