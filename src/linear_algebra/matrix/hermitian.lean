/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import analysis.inner_product_space.spectrum

/-! # Hermitian matrices

This file defines hermitian matrices and some basic results about them.

## Main definition

 * `matrix.is_hermitian `: a matrix `A : matrix n n ğ•œ` is hermitian if `Aá´´ = A`.

## Tags

self-adjoint matrix, hermitian matrix

-/

namespace matrix

variables {ğ•œ ğ•œ' : Type*} [is_R_or_C ğ•œ] [is_R_or_C ğ•œ'] {m n : Type*} {A : matrix n n ğ•œ}

open_locale matrix

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ (pi_Lp 2 (Î» (_ : n), ğ•œ)) _ x y

/-- A matrix is hermitian if it is equal to its conjugate transpose. On the reals, this definition
captures symmetric matrices. -/
def is_hermitian (A : matrix n n ğ•œ) : Prop := Aá´´ = A

lemma is_hermitian.eq {A : matrix n n ğ•œ} (h : A.is_hermitian) : Aá´´ = A := h

@[ext]
lemma is_hermitian.ext {A : matrix n n ğ•œ} : (âˆ€ i j, star (A j i) = A i j) â†’ A.is_hermitian :=
by { intros h, ext i j, exact h i j }

lemma is_hermitian.apply {A : matrix n n ğ•œ} (h : A.is_hermitian) (i j : n) : star (A j i) = A i j :=
by { unfold is_hermitian at h, rw [â† h, conj_transpose_apply, star_star, h] }

lemma is_hermitian.ext_iff {A : matrix n n ğ•œ} : A.is_hermitian â†” âˆ€ i j, star (A j i) = A i j :=
âŸ¨is_hermitian.apply, is_hermitian.extâŸ©

lemma is_hermitian_mul_conj_transpose_self [fintype n] (A : matrix n n ğ•œ) :
  (A â¬ Aá´´).is_hermitian :=
by rw [is_hermitian, conj_transpose_mul, conj_transpose_conj_transpose]

lemma is_hermitian_transpose_mul_self [fintype n] (A : matrix n n ğ•œ) :
  (Aá´´ â¬ A).is_hermitian :=
by rw [is_hermitian, conj_transpose_mul, conj_transpose_conj_transpose]

lemma is_hermitian_add_transpose_self (A : matrix n n ğ•œ) :
  (A + Aá´´).is_hermitian :=
by simp [is_hermitian, add_comm]

lemma is_hermitian_transpose_add_self (A : matrix n n ğ•œ) :
  (Aá´´ + A).is_hermitian :=
by simp [is_hermitian, add_comm]

@[simp] lemma is_hermitian_zero :
  (0 : matrix n n ğ•œ).is_hermitian :=
conj_transpose_zero

@[simp] lemma is_hermitian_one [decidable_eq n] :
  (1 : matrix n n ğ•œ).is_hermitian :=
conj_transpose_one

-- TODO: move
lemma conj_transpose_map {A : matrix n n ğ•œ} (f : ğ•œ â†’ ğ•œ') (hf : f âˆ˜ star = star âˆ˜ f) :
  Aá´´.map f = (A.map f)á´´ :=
by rw [conj_transpose, conj_transpose, â†transpose_map, map_map, map_map, hf]

@[simp] lemma is_hermitian.map {A : matrix n n ğ•œ} (h : A.is_hermitian) (f : ğ•œ â†’ ğ•œ')
    (hf : f âˆ˜ star = star âˆ˜ f) :
  (A.map f).is_hermitian :=
by {refine (conj_transpose_map f hf).symm.trans _, rw h.eq }

@[simp] lemma is_hermitian.transpose {A : matrix n n ğ•œ} (h : A.is_hermitian) :
  Aáµ€.is_hermitian :=
by { rw [is_hermitian, conj_transpose, transpose_map], congr, exact h }

@[simp] lemma is_hermitian.conj_transpose {A : matrix n n ğ•œ} (h : A.is_hermitian) :
  Aá´´.is_hermitian :=
h.transpose.map _ rfl

@[simp] lemma is_hermitian.neg {A : matrix n n ğ•œ} (h : A.is_hermitian) :
  (-A).is_hermitian :=
(conj_transpose_neg _).trans (congr_arg _ h)

@[simp] lemma is_hermitian.add {A B : matrix n n ğ•œ} (hA : A.is_hermitian) (hB : B.is_hermitian) :
  (A + B).is_hermitian :=
(conj_transpose_add _ _).trans (hA.symm â–¸ hB.symm â–¸ rfl)

@[simp] lemma is_hermitian.sub {A B : matrix n n ğ•œ} (hA : A.is_hermitian) (hB : B.is_hermitian) :
  (A - B).is_hermitian :=
(conj_transpose_sub _ _).trans (hA.symm â–¸ hB.symm â–¸ rfl)

@[simp] lemma is_hermitian.minor {A : matrix n n ğ•œ} (h : A.is_hermitian) (f : m â†’ n) :
  (A.minor f f).is_hermitian :=
(conj_transpose_minor _ _ _).trans (h.symm â–¸ rfl)

/-- The real diagonal matrix `diagonal v` is hermitian. -/
@[simp] lemma is_hermitian_diagonal [decidable_eq n] (v : n â†’ â„) :
  (diagonal v).is_hermitian :=
diagonal_conj_transpose _

/-- A block matrix `A.from_blocks B C D` is hermitian,
    if `A` and `D` are hermitian and `Bá´´ = C`. -/
lemma is_hermitian.from_blocks
  {A : matrix m m ğ•œ} {B : matrix m n ğ•œ} {C : matrix n m ğ•œ} {D : matrix n n ğ•œ}
  (hA : A.is_hermitian) (hBC : Bá´´ = C) (hD : D.is_hermitian) :
  (A.from_blocks B C D).is_hermitian :=
begin
  have hCB : Cá´´ = B, {rw â† hBC, simp},
  unfold matrix.is_hermitian,
  rw from_blocks_conj_transpose,
  congr;
  assumption
end

/-- This is the `iff` version of `matrix.is_hermitian.from_blocks`. -/
lemma is_hermitian_from_blocks_iff
  {A : matrix m m ğ•œ} {B : matrix m n ğ•œ} {C : matrix n m ğ•œ} {D : matrix n n ğ•œ} :
  (A.from_blocks B C D).is_hermitian â†” A.is_hermitian âˆ§ Bá´´ = C âˆ§ Cá´´ = B âˆ§ D.is_hermitian :=
âŸ¨Î» h, âŸ¨congr_arg to_blocksâ‚â‚ h, congr_arg to_blocksâ‚‚â‚ h,
       congr_arg to_blocksâ‚â‚‚ h, congr_arg to_blocksâ‚‚â‚‚ hâŸ©,
 Î» âŸ¨hA, hBC, hCB, hDâŸ©, is_hermitian.from_blocks hA hBC hDâŸ©

/-- A matrix is hermitian iff the corresponding linear map is self adjoint. -/
lemma is_hermitian_iff_is_self_adjoint [fintype n] [decidable_eq n] {A : matrix n n ğ•œ} :
  is_hermitian A â†” inner_product_space.is_self_adjoint
    ((pi_Lp.linear_equiv ğ•œ (Î» _ : n, ğ•œ)).symm.conj A.to_lin' : module.End ğ•œ (pi_Lp 2 _)) :=
begin
  rw [inner_product_space.is_self_adjoint, (pi_Lp.equiv 2 (Î» _ : n, ğ•œ)).symm.surjective.forallâ‚‚],
  simp only [linear_equiv.conj_apply, linear_map.comp_apply, linear_equiv.coe_coe,
    pi_Lp.linear_equiv_apply, pi_Lp.linear_equiv_symm_apply, linear_equiv.symm_symm],
  simp_rw [euclidean_space.inner_eq_star_dot_product, equiv.apply_symm_apply, to_lin'_apply,
    star_mul_vec, dot_product_mul_vec],
  split,
  { rintro (h : Aá´´ = A) x y,
    rw h },
  { intro h,
    ext i j,
    simpa only [matrix.star_single, map_one, vec_mul_single, one_smul,
      dot_product_single, mul_one, star_one] using h (pi.single i 1) (pi.single j 1) }
end

end matrix
