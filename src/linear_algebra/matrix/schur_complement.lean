/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Eric Wieser, Jeremy Avigad, Johan Commelin
-/
import linear_algebra.matrix.nonsingular_inverse
import linear_algebra.matrix.pos_def

/-! # 2Ã—2 block matrices and the Schur complement

This file proves properties of 2Ã—2 block matrices `[A B; C D]` that relate to the Schur complement
`D - Câ¬Aâ»Â¹â¬B`.

## Main results

 * `matrix.det_from_blocksâ‚â‚`, `matrix.det_from_blocksâ‚‚â‚‚`: determinant of a block matrix in terms of
   the Schur complement.
 * `matrix.inv_of_from_blocks_zeroâ‚‚â‚_eq`, `matrix.inv_of_from_blocks_zeroâ‚â‚‚_eq`: the inverse of a
   block triangular matrix
 * `matrix.det_one_add_mul_comm`: the **Weinsteinâ€“Aronszajn identity**.
 * `matrix.schur_complement_pos_semidef_iff` : If a matrix `A` is positive definite, then
  `[A B; Bá´´ D]` is postive semidefinite if and only if `D - Bá´´ Aâ»Â¹ B` is postive semidefinite.

-/

variables {l m n Î± : Type*}

namespace matrix
open_locale matrix

section comm_ring
variables [fintype l] [fintype m] [fintype n]
variables [decidable_eq l] [decidable_eq m] [decidable_eq n]
variables [comm_ring Î±]

/-- LDU decomposition of a block matrix with an invertible top-left corner, using the
Schur complement. -/
lemma from_blocks_eq_of_invertibleâ‚â‚
  (A : matrix m m Î±) (B : matrix m n Î±) (C : matrix l m Î±) (D : matrix l n Î±) [invertible A] :
  from_blocks A B C D =
    from_blocks 1 0 (Câ¬â…ŸA) 1 â¬ from_blocks A 0 0 (D - Câ¬(â…ŸA)â¬B) â¬ from_blocks 1 (â…ŸAâ¬B) 0 1 :=
by simp only [from_blocks_multiply, matrix.mul_zero, matrix.zero_mul, add_zero, zero_add,
      matrix.one_mul, matrix.mul_one, matrix.inv_of_mul_self, matrix.mul_inv_of_self_assoc,
        matrix.mul_inv_of_mul_self_cancel, matrix.mul_assoc, add_sub_cancel'_right]

/-- LDU decomposition of a block matrix with an invertible bottom-right corner, using the
Schur complement. -/
lemma from_blocks_eq_of_invertibleâ‚‚â‚‚
  (A : matrix l m Î±) (B : matrix l n Î±) (C : matrix n m Î±) (D : matrix n n Î±) [invertible D] :
  from_blocks A B C D =
    from_blocks 1 (Bâ¬â…ŸD) 0 1 â¬ from_blocks (A - Bâ¬â…ŸDâ¬C) 0 0 D â¬ from_blocks 1 0 (â…ŸD â¬ C) 1 :=
(matrix.reindex (equiv.sum_comm _ _) (equiv.sum_comm _ _)).injective $ by
  simpa [reindex_apply, equiv.sum_comm_symm,
    â†submatrix_mul_equiv _ _ _ (equiv.sum_comm n m),
    â†submatrix_mul_equiv _ _ _ (equiv.sum_comm n l),
    equiv.sum_comm_apply, from_blocks_submatrix_sum_swap_sum_swap]
    using from_blocks_eq_of_invertibleâ‚â‚ D C B A

section triangular

/-! #### Block triangular matrices -/

/-- An upper-block-triangular matrix is invertible if its diagonal is. -/
def from_blocks_zeroâ‚‚â‚_invertible (A : matrix m m Î±) (B : matrix m n Î±) (D : matrix n n Î±)
  [invertible A] [invertible D] : invertible (from_blocks A B 0 D) :=
invertible_of_left_inverse _ (from_blocks (â…ŸA) (-(â…ŸAâ¬Bâ¬â…ŸD)) 0 (â…ŸD)) $
  by simp_rw [from_blocks_multiply, matrix.mul_zero, matrix.zero_mul, zero_add, add_zero,
    matrix.neg_mul, matrix.inv_of_mul_self, matrix.mul_inv_of_mul_self_cancel, add_right_neg,
    from_blocks_one]

/-- A lower-block-triangular matrix is invertible if its diagonal is. -/
def from_blocks_zeroâ‚â‚‚_invertible (A : matrix m m Î±) (C : matrix n m Î±) (D : matrix n n Î±)
  [invertible A] [invertible D] : invertible (from_blocks A 0 C D) :=
invertible_of_left_inverse _ (from_blocks (â…ŸA) 0 (-(â…ŸDâ¬Câ¬â…ŸA)) (â…ŸD)) $
  -- a symmetry argument is more work than just copying the proof
  by simp_rw [from_blocks_multiply, matrix.mul_zero, matrix.zero_mul, zero_add, add_zero,
    matrix.neg_mul, matrix.inv_of_mul_self, matrix.mul_inv_of_mul_self_cancel, add_left_neg,
    from_blocks_one]

lemma inv_of_from_blocks_zeroâ‚‚â‚_eq
  (A : matrix m m Î±) (B : matrix m n Î±) (D : matrix n n Î±)
  [invertible A] [invertible D] [invertible (from_blocks A B 0 D)] :
  â…Ÿ(from_blocks A B 0 D) = from_blocks (â…ŸA) (-(â…ŸAâ¬Bâ¬â…ŸD)) 0 (â…ŸD) :=
begin
  letI := from_blocks_zeroâ‚‚â‚_invertible A B D,
  haveI := invertible.subsingleton (from_blocks A B 0 D),
  convert (rfl : â…Ÿ(from_blocks A B 0 D) = _),
end

lemma inv_of_from_blocks_zeroâ‚â‚‚_eq
  (A : matrix m m Î±) (C : matrix n m Î±) (D : matrix n n Î±)
  [invertible A] [invertible D] [invertible (from_blocks A 0 C D)] :
  â…Ÿ(from_blocks A 0 C D) = from_blocks (â…ŸA) 0 (-(â…ŸDâ¬Câ¬â…ŸA)) (â…ŸD) :=
begin
  letI := from_blocks_zeroâ‚â‚‚_invertible A C D,
  haveI := invertible.subsingleton (from_blocks A 0 C D),
  convert (rfl : â…Ÿ(from_blocks A 0 C D) = _),
end

/-- Both diagonal entries of an invertible upper-block-triangular matrix are invertible (by reading
off the diagonal entries of the inverse). -/
def invertible_of_from_blocks_zeroâ‚‚â‚_invertible
  (A : matrix m m Î±) (B : matrix m n Î±) (D : matrix n n Î±)
  [invertible (from_blocks A B 0 D)] : invertible A Ã— invertible D :=
{ fst := invertible_of_left_inverse _ (â…Ÿ(from_blocks A B 0 D)).to_blocksâ‚â‚ $ begin
    have := matrix.inv_of_mul_self (from_blocks A B 0 D),
    rw [â†from_blocks_to_blocks (â…Ÿ(from_blocks A B 0 D)), from_blocks_multiply] at this,
    replace := congr_arg matrix.to_blocksâ‚â‚ this,
    simpa only [matrix.to_blocks_from_blocksâ‚â‚, matrix.mul_zero, add_zero, â†from_blocks_one]
      using this,
  end,
  snd := invertible_of_right_inverse _ (â…Ÿ(from_blocks A B 0 D)).to_blocksâ‚‚â‚‚ $ begin
    have := matrix.mul_inv_of_self (from_blocks A B 0 D),
    rw [â†from_blocks_to_blocks (â…Ÿ(from_blocks A B 0 D)), from_blocks_multiply] at this,
    replace := congr_arg matrix.to_blocksâ‚‚â‚‚ this,
    simpa only [matrix.to_blocks_from_blocksâ‚‚â‚‚, matrix.zero_mul, zero_add, â†from_blocks_one]
      using this,
  end }

/-- Both diagonal entries of an invertible lower-block-triangular matrix are invertible (by reading
off the diagonal entries of the inverse). -/
def invertible_of_from_blocks_zeroâ‚â‚‚_invertible
  (A : matrix m m Î±) (C : matrix n m Î±) (D : matrix n n Î±)
  [invertible (from_blocks A 0 C D)] : invertible A Ã— invertible D :=
{ fst := invertible_of_right_inverse _ (â…Ÿ(from_blocks A 0 C D)).to_blocksâ‚â‚ $ begin
    have := matrix.mul_inv_of_self (from_blocks A 0 C  D),
    rw [â†from_blocks_to_blocks (â…Ÿ(from_blocks A 0 C  D)), from_blocks_multiply] at this,
    replace := congr_arg matrix.to_blocksâ‚â‚ this,
    simpa only [matrix.to_blocks_from_blocksâ‚â‚, matrix.zero_mul, add_zero, â†from_blocks_one]
      using this,
  end,
  snd := invertible_of_left_inverse _ (â…Ÿ(from_blocks A 0 C D)).to_blocksâ‚‚â‚‚ $ begin
    have := matrix.inv_of_mul_self (from_blocks A 0 C  D),
    rw [â†from_blocks_to_blocks (â…Ÿ(from_blocks A 0 C D)), from_blocks_multiply] at this,
    replace := congr_arg matrix.to_blocksâ‚‚â‚‚ this,
    simpa only [matrix.to_blocks_from_blocksâ‚‚â‚‚, matrix.mul_zero, zero_add, â†from_blocks_one]
      using this,
  end }

/-- `invertible_of_from_blocks_zeroâ‚‚â‚_invertible` and `from_blocks_zeroâ‚‚â‚_invertible` form
an equivalence. -/
def from_blocks_zeroâ‚‚â‚_invertible_equiv (A : matrix m m Î±) (B : matrix m n Î±) (D : matrix n n Î±) :
  invertible (from_blocks A B 0 D) â‰ƒ invertible A Ã— invertible D :=
{ to_fun := Î» _, by exactI invertible_of_from_blocks_zeroâ‚‚â‚_invertible A B D,
  inv_fun := Î» i, by letI := i.1; letI := i.2; exact from_blocks_zeroâ‚‚â‚_invertible A B D,
  left_inv := Î» _, subsingleton.elim _ _,
  right_inv := Î» _, subsingleton.elim _ _ }

/-- `invertible_of_from_blocks_zeroâ‚â‚‚_invertible` and `from_blocks_zeroâ‚â‚‚_invertible` form
an equivalence. -/
def from_blocks_zeroâ‚â‚‚_invertible_equiv (A : matrix m m Î±) (C : matrix n m Î±) (D : matrix n n Î±) :
  invertible (from_blocks A 0 C D) â‰ƒ invertible A Ã— invertible D :=
{ to_fun := Î» _, by exactI invertible_of_from_blocks_zeroâ‚â‚‚_invertible A C D,
  inv_fun := Î» i, by letI := i.1; letI := i.2; exact from_blocks_zeroâ‚â‚‚_invertible A C D,
  left_inv := Î» _, subsingleton.elim _ _,
  right_inv := Î» _, subsingleton.elim _ _ }

/-- When lowered to a prop, `matrix.from_blocks_zeroâ‚‚â‚_invertible_equiv` forms an `iff`. -/
@[simp] lemma is_unit_from_blocks_zeroâ‚‚â‚ {A : matrix m m Î±} {B : matrix m n Î±} {D : matrix n n Î±} :
  is_unit (from_blocks A B 0 D) â†” is_unit A âˆ§ is_unit D :=
by simp only [â† nonempty_invertible_iff_is_unit, â†nonempty_prod,
  (from_blocks_zeroâ‚‚â‚_invertible_equiv _ _ _).nonempty_congr]

/-- When lowered to a prop, `matrix.from_blocks_zeroâ‚â‚‚_invertible_equiv` forms an `iff`. -/
@[simp] lemma is_unit_from_blocks_zeroâ‚â‚‚ {A : matrix m m Î±} {C : matrix n m Î±} {D : matrix n n Î±} :
  is_unit (from_blocks A 0 C D) â†” is_unit A âˆ§ is_unit D :=
by simp only [â† nonempty_invertible_iff_is_unit, â†nonempty_prod,
  (from_blocks_zeroâ‚â‚‚_invertible_equiv _ _ _).nonempty_congr]

lemma inv_from_blocks_zeroâ‚‚â‚_of_is_unit_iff
  (A : matrix m m Î±) (B : matrix m n Î±) (D : matrix n n Î±)
  (hAD : is_unit A â†” is_unit D) :
  (from_blocks A B 0 D)â»Â¹ = from_blocks Aâ»Â¹ (-(Aâ»Â¹â¬Bâ¬Dâ»Â¹)) 0 Dâ»Â¹ :=
begin
  by_cases hA : is_unit A,
  { have hD := hAD.mp hA,
    casesI hA.nonempty_invertible,
    casesI hD.nonempty_invertible,
    letI := from_blocks_zeroâ‚‚â‚_invertible A B D,
    simp_rw [â†inv_of_eq_nonsing_inv, inv_of_from_blocks_zeroâ‚‚â‚_eq] },
  { have hD := hAD.not.mp hA,
    have : Â¬is_unit (from_blocks A B 0 D) :=
      is_unit_from_blocks_zeroâ‚‚â‚.not.mpr (not_and'.mpr $ Î» _, hA),
    simp_rw [nonsing_inv_eq_ring_inverse,
      ring.inverse_non_unit _ hA, ring.inverse_non_unit _ hD, ring.inverse_non_unit _ this,
      matrix.zero_mul, neg_zero, from_blocks_zero] }
end

lemma inv_from_blocks_zeroâ‚â‚‚_of_is_unit_iff
  (A : matrix m m Î±) (C : matrix n m Î±) (D : matrix n n Î±)
  (hAD : is_unit A â†” is_unit D) :
  (from_blocks A 0 C D)â»Â¹ = from_blocks Aâ»Â¹ 0 (-(Dâ»Â¹â¬Câ¬Aâ»Â¹)) Dâ»Â¹ :=
begin
  by_cases hA : is_unit A,
  { have hD := hAD.mp hA,
    casesI hA.nonempty_invertible,
    casesI hD.nonempty_invertible,
    letI := from_blocks_zeroâ‚â‚‚_invertible A C D,
    simp_rw [â†inv_of_eq_nonsing_inv, inv_of_from_blocks_zeroâ‚â‚‚_eq] },
  { have hD := hAD.not.mp hA,
    have : Â¬is_unit (from_blocks A 0 C D) :=
      is_unit_from_blocks_zeroâ‚â‚‚.not.mpr (not_and'.mpr $ Î» _, hA),
    simp_rw [nonsing_inv_eq_ring_inverse,
      ring.inverse_non_unit _ hA, ring.inverse_non_unit _ hD, ring.inverse_non_unit _ this,
      matrix.zero_mul, neg_zero, from_blocks_zero] }
end

end triangular

/-! ### Lemmas about `matrix.det` -/

section det

/-- Determinant of a 2Ã—2 block matrix, expanded around an invertible top left element in terms of
the Schur complement. -/
lemma det_from_blocksâ‚â‚ (A : matrix m m Î±) (B : matrix m n Î±) (C : matrix n m Î±) (D : matrix n n Î±)
  [invertible A] : (matrix.from_blocks A B C D).det = det A * det (D - C â¬ (â…ŸA) â¬ B) :=
by rw [from_blocks_eq_of_invertibleâ‚â‚, det_mul, det_mul, det_from_blocks_zeroâ‚‚â‚,
  det_from_blocks_zeroâ‚‚â‚, det_from_blocks_zeroâ‚â‚‚, det_one, det_one, one_mul, one_mul, mul_one]

@[simp] lemma det_from_blocks_oneâ‚â‚ (B : matrix m n Î±) (C : matrix n m Î±) (D : matrix n n Î±) :
  (matrix.from_blocks 1 B C D).det = det (D - C â¬ B) :=
begin
  haveI : invertible (1 : matrix m m Î±) := invertible_one,
  rw [det_from_blocksâ‚â‚, inv_of_one, matrix.mul_one, det_one, one_mul],
end

/-- Determinant of a 2Ã—2 block matrix, expanded around an invertible bottom right element in terms
of the Schur complement. -/
lemma det_from_blocksâ‚‚â‚‚ (A : matrix m m Î±) (B : matrix m n Î±) (C : matrix n m Î±) (D : matrix n n Î±)
  [invertible D] : (matrix.from_blocks A B C D).det = det D * det (A - B â¬ (â…ŸD) â¬ C) :=
begin
  have : from_blocks A B C D
    = (from_blocks D C B A).submatrix (equiv.sum_comm _ _) (equiv.sum_comm _ _),
  { ext i j,
    cases i; cases j; refl },
  rw [this, det_submatrix_equiv_self, det_from_blocksâ‚â‚],
end

@[simp] lemma det_from_blocks_oneâ‚‚â‚‚ (A : matrix m m Î±) (B : matrix m n Î±) (C : matrix n m Î±) :
  (matrix.from_blocks A B C 1).det = det (A - B â¬ C) :=
begin
  haveI : invertible (1 : matrix n n Î±) := invertible_one,
  rw [det_from_blocksâ‚‚â‚‚, inv_of_one, matrix.mul_one, det_one, one_mul],
end

/-- The **Weinsteinâ€“Aronszajn identity**. Note the `1` on the LHS is of shape mÃ—m, while the `1` on
the RHS is of shape nÃ—n. -/
lemma det_one_add_mul_comm (A : matrix m n Î±) (B : matrix n m Î±) :
  det (1 + A â¬ B) = det (1 + B â¬ A) :=
calc  det (1 + A â¬ B)
    = det (from_blocks 1 (-A) B 1) : by rw [det_from_blocks_oneâ‚‚â‚‚, matrix.neg_mul, sub_neg_eq_add]
... = det (1 + B â¬ A)              : by rw [det_from_blocks_oneâ‚â‚, matrix.mul_neg, sub_neg_eq_add]

/-- Alternate statement of the **Weinsteinâ€“Aronszajn identity** -/
lemma det_mul_add_one_comm (A : matrix m n Î±) (B : matrix n m Î±) :
  det (A â¬ B + 1) = det (B â¬ A + 1) :=
by rw [add_comm, det_one_add_mul_comm, add_comm]

lemma det_one_sub_mul_comm (A : matrix m n Î±) (B : matrix n m Î±) :
  det (1 - A â¬ B) = det (1 - B â¬ A) :=
by rw [sub_eq_add_neg, â†matrix.neg_mul, det_one_add_mul_comm, matrix.mul_neg, â†sub_eq_add_neg]

/-- A special case of the **Matrix determinant lemma** for when `A = I`.

TODO: show this more generally. -/
lemma det_one_add_col_mul_row (u v : m â†’ Î±) : det (1 + col u â¬ row v) = 1 + v â¬áµ¥ u :=
by rw [det_one_add_mul_comm, det_unique, pi.add_apply, pi.add_apply, matrix.one_apply_eq,
       matrix.row_mul_col_apply]

end det

end comm_ring

/-! ### Lemmas about `â„` and `â„‚`-/

section is_R_or_C

open_locale matrix
variables {ğ•œ : Type*} [is_R_or_C ğ•œ]

localized "infix ` âŠ•áµ¥ `:65 := sum.elim" in matrix

lemma schur_complement_eqâ‚â‚ [fintype m] [decidable_eq m] [fintype n]
  {A : matrix m m ğ•œ} (B : matrix m n ğ•œ) (D : matrix n n ğ•œ) (x : m â†’ ğ•œ) (y : n â†’ ğ•œ)
  [invertible A] (hA : A.is_hermitian) :
vec_mul (star (x âŠ•áµ¥ y)) (from_blocks A B Bá´´ D) â¬áµ¥ (x âŠ•áµ¥ y) =
  vec_mul (star (x + (Aâ»Â¹ â¬ B).mul_vec y)) A â¬áµ¥ (x + (Aâ»Â¹ â¬ B).mul_vec y) +
    vec_mul (star y) (D - Bá´´ â¬ Aâ»Â¹ â¬ B) â¬áµ¥ y :=
begin
  simp [function.star_sum_elim, from_blocks_mul_vec, vec_mul_from_blocks, add_vec_mul,
    dot_product_mul_vec, vec_mul_sub, matrix.mul_assoc, vec_mul_mul_vec, hA.eq,
    conj_transpose_nonsing_inv, star_mul_vec],
  abel
end

lemma schur_complement_eqâ‚‚â‚‚ [fintype m] [fintype n] [decidable_eq n]
  (A : matrix m m ğ•œ) (B : matrix m n ğ•œ) {D : matrix n n ğ•œ} (x : m â†’ ğ•œ) (y : n â†’ ğ•œ)
  [invertible D] (hD : D.is_hermitian) :
vec_mul (star (x âŠ•áµ¥ y)) (from_blocks A B Bá´´ D) â¬áµ¥ (x âŠ•áµ¥ y) =
  vec_mul (star ((Dâ»Â¹ â¬ Bá´´).mul_vec x + y)) D â¬áµ¥ ((Dâ»Â¹ â¬ Bá´´).mul_vec x + y) +
    vec_mul (star x) (A - B â¬ Dâ»Â¹ â¬ Bá´´) â¬áµ¥ x :=
begin
  simp [function.star_sum_elim, from_blocks_mul_vec, vec_mul_from_blocks, add_vec_mul,
    dot_product_mul_vec, vec_mul_sub, matrix.mul_assoc, vec_mul_mul_vec, hD.eq,
    conj_transpose_nonsing_inv, star_mul_vec],
  abel
end

lemma is_hermitian.from_blocksâ‚â‚ [fintype m] [decidable_eq m]
  {A : matrix m m ğ•œ} (B : matrix m n ğ•œ) (D : matrix n n ğ•œ)
  (hA : A.is_hermitian) :
  (from_blocks A B Bá´´ D).is_hermitian â†” (D - Bá´´ â¬ Aâ»Â¹ â¬ B).is_hermitian :=
begin
  have hBAB : (Bá´´ â¬ Aâ»Â¹ â¬ B).is_hermitian,
  { apply is_hermitian_conj_transpose_mul_mul,
    apply hA.inv },
  rw [is_hermitian_from_blocks_iff],
  split,
  { intro h,
    apply is_hermitian.sub h.2.2.2 hBAB },
  { intro h,
    refine âŸ¨hA, rfl, conj_transpose_conj_transpose B, _âŸ©,
    rw â† sub_add_cancel D,
    apply is_hermitian.add h hBAB }
end

lemma is_hermitian.from_blocksâ‚‚â‚‚ [fintype n] [decidable_eq n]
  (A : matrix m m ğ•œ) (B : matrix m n ğ•œ) {D : matrix n n ğ•œ}
  (hD : D.is_hermitian) :
  (from_blocks A B Bá´´ D).is_hermitian â†” (A - B â¬ Dâ»Â¹ â¬ Bá´´).is_hermitian :=
begin
  rw [â†is_hermitian_submatrix_equiv (equiv.sum_comm n m), equiv.sum_comm_apply,
    from_blocks_submatrix_sum_swap_sum_swap],
  convert is_hermitian.from_blocksâ‚â‚ _ _ hD; simp
end

lemma pos_semidef.from_blocksâ‚â‚ [fintype m] [decidable_eq m] [fintype n]
  {A : matrix m m ğ•œ} (B : matrix m n ğ•œ) (D : matrix n n ğ•œ)
  (hA : A.pos_def) [invertible A] :
  (from_blocks A B Bá´´ D).pos_semidef â†” (D - Bá´´ â¬ Aâ»Â¹ â¬ B).pos_semidef :=
begin
  rw [pos_semidef, is_hermitian.from_blocksâ‚â‚ _ _ hA.1],
  split,
  { refine Î» h, âŸ¨h.1, Î» x, _âŸ©,
    have := h.2 (- ((Aâ»Â¹ â¬ B).mul_vec x) âŠ•áµ¥ x),
    rw [dot_product_mul_vec, schur_complement_eqâ‚â‚ B D _ _ hA.1, neg_add_self,
      dot_product_zero, zero_add] at this,
    rw [dot_product_mul_vec], exact this },
  { refine Î» h, âŸ¨h.1, Î» x, _âŸ©,
    rw [dot_product_mul_vec, â† sum.elim_comp_inl_inr x, schur_complement_eqâ‚â‚ B D _ _ hA.1,
      map_add],
    apply le_add_of_nonneg_of_le,
    { rw â† dot_product_mul_vec,
      apply hA.pos_semidef.2, },
    { rw â† dot_product_mul_vec, apply h.2 } }
end

lemma pos_semidef.from_blocksâ‚‚â‚‚ [fintype m] [fintype n] [decidable_eq n]
  (A : matrix m m ğ•œ) (B : matrix m n ğ•œ) {D : matrix n n ğ•œ}
  (hD : D.pos_def) [invertible D] :
  (from_blocks A B Bá´´ D).pos_semidef â†” (A - B â¬ Dâ»Â¹ â¬ Bá´´).pos_semidef :=
begin
  rw [â†pos_semidef_submatrix_equiv (equiv.sum_comm n m), equiv.sum_comm_apply,
    from_blocks_submatrix_sum_swap_sum_swap],
  convert pos_semidef.from_blocksâ‚â‚ _ _ hD; apply_instance <|> simp
end

end is_R_or_C

end matrix
