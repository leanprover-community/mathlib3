/-
Copyright (c) 2019 Johannes H√∂lzl, Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Zhouhang Zhou
-/
import measure_theory.integration
import order.filter.germ

/-!

# Almost everywhere equal functions

Two measurable functions are treated as identical if they are almost everywhere equal. We form the
set of equivalence classes under the relation of being almost everywhere equal, which is sometimes
known as the `L‚Å∞` space.

See `l1_space.lean` for `L¬π` space.

## Notation

* `Œ± ‚Üí‚Çò[Œº] Œ≤` is the type of `L‚Å∞` space, where `Œ±` and `Œ≤` are measurable spaces and `Œº`
  is a measure on `Œ±`. `f : Œ± ‚Üí‚Çò Œ≤` is a "function" in `L‚Å∞`. In comments, `[f]` is also used
  to denote an `L‚Å∞` function.

  `‚Çò` can be typed as `\_m`. Sometimes it is shown as a box if font is missing.


## Main statements

* The linear structure of `L‚Å∞` :
    Addition and scalar multiplication are defined on `L‚Å∞` in the natural way, i.e.,
    `[f] + [g] := [f + g]`, `c ‚Ä¢ [f] := [c ‚Ä¢ f]`. So defined, `Œ± ‚Üí‚Çò Œ≤` inherits the linear structure
    of `Œ≤`. For example, if `Œ≤` is a module, then `Œ± ‚Üí‚Çò Œ≤` is a module over the same ring.

    See `mk_add_mk`,  `neg_mk`,     `mk_sub_mk`,  `smul_mk`,
        `add_to_fun`, `neg_to_fun`, `sub_to_fun`, `smul_to_fun`

* The order structure of `L‚Å∞` :
    `‚â§` can be defined in a similar way: `[f] ‚â§ [g]` if `f a ‚â§ g a` for almost all `a` in domain.
    And `Œ± ‚Üí‚Çò Œ≤` inherits the preorder and partial order of `Œ≤`.

    TODO: Define `sup` and `inf` on `L‚Å∞` so that it forms a lattice. It seems that `Œ≤` must be a
    linear order, since otherwise `f ‚äî g` may not be a measurable function.

* Emetric on `L‚Å∞` :
    If `Œ≤` is an `emetric_space`, then `L‚Å∞` can be made into an `emetric_space`, where
    `edist [f] [g]` is defined to be `‚à´‚Åª a, edist (f a) (g a)`.

    The integral used here is `lintegral : (Œ± ‚Üí ennreal) ‚Üí ennreal`, which is defined in the file
    `integration.lean`.

    See `edist_mk_mk` and `edist_to_fun`.

## Implementation notes

* `f.to_fun`     : To find a representative of `f : Œ± ‚Üí‚Çò Œ≤`, use `f.to_fun`.
                 For each operation `op` in `L‚Å∞`, there is a lemma called `op_to_fun`, characterizing,
                 say, `(f op g).to_fun`.
* `ae_eq_fun.mk` : To constructs an `L‚Å∞` function `Œ± ‚Üí‚Çò Œ≤` from a measurable function `f : Œ± ‚Üí Œ≤`,
                 use `ae_eq_fun.mk`
* `comp`         : Use `comp g f` to get `[g ‚àò f]` from `g : Œ≤ ‚Üí Œ≥` and `[f] : Œ± ‚Üí‚Çò Œ≥`
* `comp‚ÇÇ`        : Use `comp‚ÇÇ g f‚ÇÅ f‚ÇÇ to get `[Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)]`.
                 For example, `[f + g]` is `comp‚ÇÇ (+)`


## Tags

function space, almost everywhere equal, `L‚Å∞`, ae_eq_fun

-/

noncomputable theory
open_locale classical

namespace measure_theory
open set filter topological_space function

variables {Œ± Œ≤ Œ≥ Œ¥ : Type*} [measurable_space Œ±]

section measurable_space
variables [measurable_space Œ≤]

variable (Œ≤)

/-- The equivalence relation of being almost everywhere equal -/
def measure.ae_eq_setoid (Œº : measure Œ±) : setoid { f : Œ± ‚Üí Œ≤ // measurable f } :=
‚ü®Œªf g, (f : Œ± ‚Üí Œ≤) =·µê[Œº] g, Œª f, ae_eq_refl f, Œª f g, ae_eq_symm, Œª f g h, ae_eq_trans‚ü©

variable (Œ±)

/-- The space of equivalence classes of measurable functions, where two measurable functions are
    equivalent if they agree almost everywhere, i.e., they differ on a set of measure `0`.  -/
def ae_eq_fun (Œº : measure Œ±) : Type* := quotient (Œº.ae_eq_setoid Œ≤)

variables {Œ± Œ≤}

notation Œ± ` ‚Üí‚Çò[`:25 Œº `] ` Œ≤ := ae_eq_fun Œ± Œ≤ Œº

end measurable_space

namespace ae_eq_fun
variables [measurable_space Œ≤] [measurable_space Œ≥] [measurable_space Œ¥] {Œº : measure Œ±}

/-- Construct the equivalence class `[f]` of a measurable function `f`, based on the equivalence
    relation of being almost everywhere equal. -/
def mk (f : Œ± ‚Üí Œ≤) (hf : measurable f) : Œ± ‚Üí‚Çò[Œº] Œ≤ := quotient.mk' ‚ü®f, hf‚ü©

/-- A representative of an `ae_eq_fun` [f] -/
instance : has_coe_to_fun (Œ± ‚Üí‚Çò[Œº] Œ≤) :=
‚ü®_, Œªf, ((quotient.out' f : {f : Œ± ‚Üí Œ≤ // measurable f}) : Œ± ‚Üí Œ≤)‚ü©

protected lemma measurable (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : measurable f :=
(quotient.out' f).2

@[simp] lemma quot_mk_eq_mk (f : Œ± ‚Üí Œ≤) (hf) :
  (quot.mk (@setoid.r _ $ Œº.ae_eq_setoid Œ≤) ‚ü®f, hf‚ü© : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk f hf :=
rfl

@[simp] lemma quotient_out'_eq_coe_fn (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
  quotient.out' f = ‚ü®f, f.measurable‚ü© :=
subtype.eq rfl

@[simp] lemma mk_eq_mk {f g : Œ± ‚Üí Œ≤} {hf hg} :
  (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk g hg ‚Üî f =·µê[Œº] g :=
quotient.eq'

@[simp] lemma mk_coe_fn (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : mk f f.measurable = f :=
by simpa using quotient.out_eq' f

@[ext] lemma ext {f g : Œ± ‚Üí‚Çò[Œº] Œ≤} (h : f =·µê[Œº] g) : f = g :=
by rwa [‚Üê f.mk_coe_fn, ‚Üê g.mk_coe_fn, mk_eq_mk]

lemma coe_fn_mk (f : Œ± ‚Üí Œ≤) (hf) : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) =·µê[Œº] f :=
@quotient.mk_out' _ (Œº.ae_eq_setoid Œ≤) (‚ü®f, hf‚ü© : {f // measurable f})

@[elab_as_eliminator]
lemma induction_on (f : Œ± ‚Üí‚Çò[Œº] Œ≤) {p : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí Prop} (H : ‚àÄ f hf, p (mk f hf)) : p f :=
quotient.induction_on' f $ subtype.forall.2 H

@[elab_as_eliminator]
lemma induction_on‚ÇÇ {Œ±' Œ≤' : Type*} [measurable_space Œ±'] [measurable_space Œ≤'] {Œº' : measure Œ±'}
  (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (f' : Œ±' ‚Üí‚Çò[Œº'] Œ≤') {p : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí (Œ±' ‚Üí‚Çò[Œº'] Œ≤') ‚Üí Prop}
  (H : ‚àÄ f hf f' hf', p (mk f hf) (mk f' hf')) :
  p f f' :=
induction_on f $ Œª f hf, induction_on f' $ H f hf

@[elab_as_eliminator]
lemma induction_on‚ÇÉ {Œ±' Œ≤' : Type*} [measurable_space Œ±'] [measurable_space Œ≤'] {Œº' : measure Œ±'}
  {Œ±'' Œ≤'' : Type*} [measurable_space Œ±''] [measurable_space Œ≤''] {Œº'' : measure Œ±''}
  (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (f' : Œ±' ‚Üí‚Çò[Œº'] Œ≤') (f'' : Œ±'' ‚Üí‚Çò[Œº''] Œ≤'')
  {p : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí (Œ±' ‚Üí‚Çò[Œº'] Œ≤') ‚Üí (Œ±'' ‚Üí‚Çò[Œº''] Œ≤'') ‚Üí Prop}
  (H : ‚àÄ f hf f' hf' f'' hf'', p (mk f hf) (mk f' hf') (mk f'' hf'')) :
  p f f' f'' :=
induction_on f $ Œª f hf, induction_on‚ÇÇ f' f'' $ H f hf

/-- Given a measurable function `g : Œ≤ ‚Üí Œ≥`, and an almost everywhere equal function `[f] : Œ± ‚Üí‚Çò Œ≤`,
    return the equivalence class of `g ‚àò f`, i.e., the almost everywhere equal function
    `[g ‚àò f] : Œ± ‚Üí‚Çò Œ≥`. -/
def comp (g : Œ≤ ‚Üí Œ≥) (hg : measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Œ± ‚Üí‚Çò[Œº] Œ≥ :=
quotient.lift_on' f (Œª f, mk (g ‚àò (f : Œ± ‚Üí Œ≤)) (hg.comp f.2)) $ Œª f f' H, mk_eq_mk.2 $ H.fun_comp g

@[simp] lemma comp_mk (g : Œ≤ ‚Üí Œ≥) (hg : measurable g)
  (f : Œ± ‚Üí Œ≤) (hf) :
  comp g hg (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk (g ‚àò f) (hg.comp hf) :=
rfl

lemma comp_eq_mk (g : Œ≤ ‚Üí Œ≥) (hg : measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
  comp g hg f = mk (g ‚àò f) (hg.comp f.measurable) :=
by rw [‚Üê comp_mk g hg f f.measurable, mk_coe_fn]

lemma coe_fn_comp (g : Œ≤ ‚Üí Œ≥) (hg : measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
  comp g hg f =·µê[Œº] g ‚àò f :=
by { rw [comp_eq_mk], apply coe_fn_mk }

/-- The class of `x ‚Ü¶ (f x, g x)`. -/
def pair (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) : Œ± ‚Üí‚Çò[Œº] Œ≤ √ó Œ≥ :=
quotient.lift_on‚ÇÇ' f g (Œª f g, mk (Œª x, (f.1 x, g.1 x)) (f.2.prod_mk g.2)) $
  Œª f g f' g' Hf Hg, mk_eq_mk.2 $ Hf.prod_mk Hg

@[simp] lemma pair_mk_mk (f : Œ± ‚Üí Œ≤) (hf) (g : Œ± ‚Üí Œ≥) (hg) :
  (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤).pair (mk g hg) = mk (Œª x, (f x, g x)) (hf.prod_mk hg) :=
rfl

lemma pair_eq_mk (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  f.pair g = mk (Œª x, (f x, g x)) (f.measurable.prod_mk g.measurable) :=
by simp only [‚Üê pair_mk_mk, mk_coe_fn]

lemma coe_fn_pair (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  f.pair g =·µê[Œº] (Œª x, (f x, g x)) :=
by { rw pair_eq_mk, apply coe_fn_mk }

/-- Given a measurable function `g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥`, and almost everywhere equal functions
    `[f‚ÇÅ] : Œ± ‚Üí‚Çò Œ≤` and `[f‚ÇÇ] : Œ± ‚Üí‚Çò Œ≥`, return the equivalence class of the function
    `Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)`, i.e., the almost everywhere equal function
    `[Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)] : Œ± ‚Üí‚Çò Œ≥` -/
def comp‚ÇÇ {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥] (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥)
  (hg : measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) : Œ± ‚Üí‚Çò[Œº] Œ¥ :=
comp _ hg (f‚ÇÅ.pair f‚ÇÇ)

@[simp] lemma comp‚ÇÇ_mk_mk {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí Œ≤) (f‚ÇÇ : Œ± ‚Üí Œ≥) (hf‚ÇÅ hf‚ÇÇ) :
  comp‚ÇÇ g hg (mk f‚ÇÅ hf‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (mk f‚ÇÇ hf‚ÇÇ) =
    mk (Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)) (hg.comp (hf‚ÇÅ.prod_mk hf‚ÇÇ)) :=
rfl

lemma comp‚ÇÇ_eq_pair {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ = comp _ hg (f‚ÇÅ.pair f‚ÇÇ) :=
rfl

lemma comp‚ÇÇ_eq_mk {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ = mk (Œª a, g (f‚ÇÅ a) (f‚ÇÇ a))
    (hg.comp (f‚ÇÅ.measurable.prod_mk f‚ÇÇ.measurable)) :=
by rw [comp‚ÇÇ_eq_pair, pair_eq_mk, comp_mk]; refl

lemma coe_fn_comp‚ÇÇ {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (uncurry g)) (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ =·µê[Œº] Œª a, g (f‚ÇÅ a) (f‚ÇÇ a) :=
by { rw comp‚ÇÇ_eq_mk, apply coe_fn_mk }

/-- Interpret `f : Œ± ‚Üí‚Çò[Œº] Œ≤` as a germ at `Œº.ae` forgetting that `f` is measurable. -/
def to_germ (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : germ Œº.ae Œ≤ :=
quotient.lift_on' f (Œª f, ((f : Œ± ‚Üí Œ≤) : germ Œº.ae Œ≤)) $ Œª f g H, germ.coe_eq.2 H

@[simp] lemma mk_to_germ (f : Œ± ‚Üí Œ≤) (hf) : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤).to_germ = f := rfl

lemma to_germ_eq (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : f.to_germ = (f : Œ± ‚Üí Œ≤) :=
by rw [‚Üê mk_to_germ, mk_coe_fn]

lemma to_germ_injective : injective (to_germ : (Œ± ‚Üí‚Çò[Œº] Œ≤) ‚Üí germ Œº.ae Œ≤) :=
Œª f g H, ext $ germ.coe_eq.1 $ by rwa [‚Üê to_germ_eq, ‚Üê to_germ_eq]

lemma comp_to_germ (g : Œ≤ ‚Üí Œ≥) (hg : measurable g) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) :
  (comp g hg f).to_germ = f.to_germ.map g :=
induction_on f $ Œª f hf, by simp

lemma comp‚ÇÇ_to_germ (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (uncurry g))
  (f‚ÇÅ : Œ± ‚Üí‚Çò[Œº] Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  (comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ).to_germ = f‚ÇÅ.to_germ.map‚ÇÇ g f‚ÇÇ.to_germ :=
induction_on‚ÇÇ f‚ÇÅ f‚ÇÇ $ Œª f‚ÇÅ hf‚ÇÅ f‚ÇÇ hf‚ÇÇ, by simp

/-- Given a predicate `p` and an equivalence class `[f]`, return true if `p` holds of `f a`
    for almost all `a` -/
def lift_pred (p : Œ≤ ‚Üí Prop) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) : Prop := f.to_germ.lift_pred p

/-- Given a relation `r` and equivalence class `[f]` and `[g]`, return true if `r` holds of
    `(f a, g a)` for almost all `a` -/
def lift_rel (r : Œ≤ ‚Üí Œ≥ ‚Üí Prop) (f : Œ± ‚Üí‚Çò[Œº] Œ≤) (g : Œ± ‚Üí‚Çò[Œº] Œ≥) : Prop :=
f.to_germ.lift_rel r g.to_germ

lemma lift_rel_mk_mk {r : Œ≤ ‚Üí Œ≥ ‚Üí Prop} {f : Œ± ‚Üí Œ≤} {g : Œ± ‚Üí Œ≥} {hf hg} :
  lift_rel r (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) (mk g hg) ‚Üî ‚àÄ·µê a ‚àÇŒº, r (f a) (g a) :=
iff.rfl

lemma lift_rel_iff_coe_fn {r : Œ≤ ‚Üí Œ≥ ‚Üí Prop} {f : Œ± ‚Üí‚Çò[Œº] Œ≤} {g : Œ± ‚Üí‚Çò[Œº] Œ≥} :
  lift_rel r f g ‚Üî ‚àÄ·µê a ‚àÇŒº, r (f a) (g a) :=
by rw [‚Üê lift_rel_mk_mk, mk_coe_fn, mk_coe_fn]

section order

instance [preorder Œ≤] : preorder (Œ± ‚Üí‚Çò[Œº] Œ≤) := preorder.lift to_germ

@[simp] lemma mk_le_mk [preorder Œ≤] {f g : Œ± ‚Üí Œ≤} (hf hg) :
  (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≤) ‚â§ mk g hg ‚Üî f ‚â§·µê[Œº] g :=
iff.rfl

@[simp, norm_cast] lemma coe_fn_le [preorder Œ≤] {f g : Œ± ‚Üí‚Çò[Œº] Œ≤} :
  (f : Œ± ‚Üí Œ≤) ‚â§·µê[Œº] g ‚Üî f ‚â§ g :=
lift_rel_iff_coe_fn.symm

instance [partial_order Œ≤] : partial_order (Œ± ‚Üí‚Çò[Œº] Œ≤) :=
partial_order.lift to_germ to_germ_injective

/- TODO: Prove `L‚Å∞` space is a lattice if Œ≤ is linear order.
         What if Œ≤ is only a lattice? -/

-- instance [linear_order Œ≤] : semilattice_sup (Œ± ‚Üí‚Çò Œ≤) :=
-- { sup := comp‚ÇÇ (‚äî) (_),
--    .. ae_eq_fun.partial_order }

end order

variable (Œ±)
/-- The equivalence class of a constant function: `[Œªa:Œ±, b]`, based on the equivalence relation of
    being almost everywhere equal -/
def const (b : Œ≤) : Œ± ‚Üí‚Çò[Œº] Œ≤ := mk (Œªa:Œ±, b) measurable_const

lemma coe_fn_const (b : Œ≤) : (const Œ± b : Œ± ‚Üí‚Çò[Œº] Œ≤) =·µê[Œº] function.const Œ± b :=
coe_fn_mk _ _

variable {Œ±}

instance [inhabited Œ≤] : inhabited (Œ± ‚Üí‚Çò[Œº] Œ≤) := ‚ü®const Œ± (default Œ≤)‚ü©

@[to_additive] instance [has_one Œ≤] : has_one (Œ± ‚Üí‚Çò[Œº] Œ≤) := ‚ü®const Œ± 1‚ü©
@[to_additive] lemma one_def [has_one Œ≤] : (1 : Œ± ‚Üí‚Çò[Œº] Œ≤) = mk (Œªa:Œ±, 1) measurable_const := rfl
@[to_additive] lemma coe_fn_one [has_one Œ≤] : ‚áë(1 : Œ± ‚Üí‚Çò[Œº] Œ≤) =·µê[Œº] 1 := coe_fn_const _ _
@[simp, to_additive] lemma one_to_germ [has_one Œ≤] : (1 : Œ± ‚Üí‚Çò[Œº] Œ≤).to_germ = 1 := rfl

section monoid
variables
  [topological_space Œ≥] [second_countable_topology Œ≥] [borel_space Œ≥]
  [monoid Œ≥] [has_continuous_mul Œ≥]

@[to_additive]
instance : has_mul (Œ± ‚Üí‚Çò[Œº] Œ≥) := ‚ü®comp‚ÇÇ (*) measurable_mul‚ü©

@[simp, to_additive] lemma mk_mul_mk (f g : Œ± ‚Üí Œ≥) (hf hg) :
  (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) * (mk g hg) = mk (f * g) (hf.mul hg) :=
rfl

@[to_additive] lemma coe_fn_mul (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(f * g) =·µê[Œº] f * g := coe_fn_comp‚ÇÇ _ _ _ _

@[simp, to_additive] lemma mul_to_germ (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  (f * g).to_germ = f.to_germ * g.to_germ :=
comp‚ÇÇ_to_germ _ _ _ _

@[to_additive add_monoid]
instance : monoid (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
to_germ_injective.monoid to_germ one_to_germ mul_to_germ

end monoid

@[to_additive add_comm_monoid]
instance comm_monoid [topological_space Œ≥] [second_countable_topology Œ≥] [borel_space Œ≥]
  [comm_monoid Œ≥] [has_continuous_mul Œ≥] : comm_monoid (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
to_germ_injective.comm_monoid to_germ one_to_germ mul_to_germ

section group

variables [topological_space Œ≥] [borel_space Œ≥] [group Œ≥] [topological_group Œ≥]

@[to_additive] instance : has_inv (Œ± ‚Üí‚Çò[Œº] Œ≥) := ‚ü®comp has_inv.inv measurable_inv‚ü©

@[simp, to_additive] lemma inv_mk (f : Œ± ‚Üí Œ≥) (hf) : (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥)‚Åª¬π = mk f‚Åª¬π hf.inv := rfl

@[to_additive] lemma coe_fn_inv (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(f‚Åª¬π) =·µê[Œº] f‚Åª¬π := coe_fn_comp _ _ _

@[to_additive] lemma inv_to_germ (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : (f‚Åª¬π).to_germ = f.to_germ‚Åª¬π := comp_to_germ _ _ _

variables [second_countable_topology Œ≥]
@[to_additive add_group]
instance : group (Œ± ‚Üí‚Çò[Œº] Œ≥) := to_germ_injective.group _ one_to_germ mul_to_germ inv_to_germ

end group

section add_group

variables [topological_space Œ≥] [borel_space Œ≥] [add_group Œ≥] [topological_add_group Œ≥]
  [second_countable_topology Œ≥]

@[simp] lemma mk_sub (f g : Œ± ‚Üí Œ≥) (hf hg) :
  mk (f - g) (measurable.sub hf hg) = (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) - (mk g hg) :=
rfl

lemma coe_fn_sub (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(f - g) =·µê[Œº] f - g :=
(coe_fn_add f (-g)).trans $ (coe_fn_neg g).mono $ Œª x hx, congr_arg ((+) (f x)) hx

end add_group

@[to_additive add_comm_group]
instance [topological_space Œ≥] [borel_space Œ≥] [comm_group Œ≥] [topological_group Œ≥]
  [second_countable_topology Œ≥] : comm_group (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
{ .. ae_eq_fun.group, .. ae_eq_fun.comm_monoid }

section semimodule

variables {ùïú : Type*} [semiring ùïú] [topological_space ùïú]
variables [topological_space Œ≥] [borel_space Œ≥] [add_comm_monoid Œ≥] [semimodule ùïú Œ≥]
  [topological_semimodule ùïú Œ≥]

instance : has_scalar ùïú (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
‚ü®Œª c f, comp ((‚Ä¢) c) (measurable_id.const_smul c) f‚ü©

@[simp] lemma smul_mk (c : ùïú) (f : Œ± ‚Üí Œ≥) (hf) :
  c ‚Ä¢ (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) = mk (c ‚Ä¢ f) (hf.const_smul _) :=
rfl

lemma coe_fn_smul (c : ùïú) (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(c ‚Ä¢ f) =·µê[Œº] c ‚Ä¢ f := coe_fn_comp _ _ _

lemma smul_to_germ (c : ùïú) (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : (c ‚Ä¢ f).to_germ = c ‚Ä¢ f.to_germ :=
comp_to_germ _ _ _

variables [second_countable_topology Œ≥] [has_continuous_add Œ≥]

instance : semimodule ùïú (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
to_germ_injective.semimodule ùïú ‚ü®@to_germ Œ± Œ≥ _ _ Œº, zero_to_germ, add_to_germ‚ü© smul_to_germ

end semimodule

/- TODO : Prove that `L‚Å∞` is a complete space if the codomain is complete. -/

open ennreal

/-- For `f : Œ± ‚Üí ennreal`, define `‚à´ [f]` to be `‚à´ f` -/
def lintegral (f : Œ± ‚Üí‚Çò[Œº] ennreal) : ennreal :=
quotient.lift_on' f (Œªf, ‚à´‚Åª a, (f : Œ± ‚Üí ennreal) a ‚àÇŒº) (assume f g, lintegral_congr_ae)

@[simp] lemma lintegral_mk (f : Œ± ‚Üí ennreal) (hf) :
  (mk f hf : Œ± ‚Üí‚Çò[Œº] ennreal).lintegral = ‚à´‚Åª a, f a ‚àÇŒº := rfl

lemma lintegral_coe_fn (f : Œ± ‚Üí‚Çò[Œº] ennreal) : ‚à´‚Åª a, f a ‚àÇŒº = f.lintegral :=
by rw [‚Üê lintegral_mk, mk_coe_fn]

@[simp] lemma lintegral_zero : lintegral (0 : Œ± ‚Üí‚Çò[Œº] ennreal) = 0 := lintegral_zero

@[simp] lemma lintegral_eq_zero_iff {f : Œ± ‚Üí‚Çò[Œº] ennreal} : lintegral f = 0 ‚Üî f = 0 :=
induction_on f $ Œª f hf, (lintegral_eq_zero_iff hf).trans mk_eq_mk.symm

lemma lintegral_add (f g : Œ± ‚Üí‚Çò[Œº] ennreal) : lintegral (f + g) = lintegral f + lintegral g :=
induction_on‚ÇÇ f g $ Œª f hf g hg, by simp [lintegral_add hf hg]

lemma lintegral_mono {f g : Œ± ‚Üí‚Çò[Œº] ennreal} : f ‚â§ g ‚Üí lintegral f ‚â§ lintegral g :=
induction_on‚ÇÇ f g $ Œª f hf g hg hfg, lintegral_mono_ae hfg

section
variables [emetric_space Œ≥] [second_countable_topology Œ≥] [opens_measurable_space Œ≥]

/-- `comp_edist [f] [g] a` will return `edist (f a) (g a) -/
protected def edist (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : Œ± ‚Üí‚Çò[Œº] ennreal := comp‚ÇÇ edist measurable_edist f g

protected lemma edist_comm (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : f.edist g = g.edist f :=
induction_on‚ÇÇ f g $ Œª f hf g hg, mk_eq_mk.2 $ eventually_of_forall $ Œª x, edist_comm (f x) (g x)

lemma coe_fn_edist (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(f.edist g) =·µê[Œº] Œª a, edist (f a) (g a) :=
coe_fn_comp‚ÇÇ _ _ _ _

protected lemma edist_self (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : f.edist f = 0 :=
induction_on f $ Œª f hf, mk_eq_mk.2 $ eventually_of_forall $ Œª x, edist_self (f x)

/-- Almost everywhere equal functions form an `emetric_space`, with the emetric defined as
  `edist f g = ‚à´‚Åª a, edist (f a) (g a)`. -/
instance : emetric_space (Œ± ‚Üí‚Çò[Œº] Œ≥) :=
{ edist               := Œªf g, lintegral (f.edist g),
  edist_self          := assume f, lintegral_eq_zero_iff.2 f.edist_self,
  edist_comm          := Œª f g, congr_arg lintegral $ f.edist_comm g,
  edist_triangle      := Œª f g h, induction_on‚ÇÉ f g h $ Œª f hf g hg h hh,
    calc ‚à´‚Åª a, edist (f a) (h a) ‚àÇŒº ‚â§ ‚à´‚Åª a, edist (f a) (g a) + edist (g a) (h a) ‚àÇŒº :
      measure_theory.lintegral_mono (Œª a, edist_triangle (f a) (g a) (h a))
    ... = ‚à´‚Åª a, edist (f a) (g a) ‚àÇŒº + ‚à´‚Åª a, edist (g a) (h a) ‚àÇŒº :
      measure_theory.lintegral_add (hf.edist hg) (hg.edist hh),
  eq_of_edist_eq_zero := Œª f g, induction_on‚ÇÇ f g $ Œª f hf g hg H, mk_eq_mk.2 $
    ((measure_theory.lintegral_eq_zero_iff (hf.edist hg)).1 H).mono $ Œª x, eq_of_edist_eq_zero }

lemma edist_mk_mk {f g : Œ± ‚Üí Œ≥} (hf hg) :
  edist (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) (mk g hg) = ‚à´‚Åª x, edist (f x) (g x) ‚àÇŒº :=
rfl

lemma edist_eq_coe (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : edist f g = ‚à´‚Åª x, edist (f x) (g x) ‚àÇŒº :=
by rw [‚Üê edist_mk_mk, mk_coe_fn, mk_coe_fn]

lemma edist_zero_eq_coe [has_zero Œ≥] (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : edist f 0 = ‚à´‚Åª x, edist (f x) 0 ‚àÇŒº :=
by rw [‚Üê edist_mk_mk, mk_coe_fn, zero_def]

end

section metric
variables [metric_space Œ≥] [second_countable_topology Œ≥] [opens_measurable_space Œ≥]

lemma edist_mk_mk' {f g : Œ± ‚Üí Œ≥} (hf hg) :
  edist (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) (mk g hg) = ‚à´‚Åª x, nndist (f x) (g x) ‚àÇŒº :=
by simp only [edist_mk_mk, edist_nndist]

lemma edist_eq_coe' (f g : Œ± ‚Üí‚Çò[Œº] Œ≥) : edist f g = ‚à´‚Åª x, nndist (f x) (g x) ‚àÇŒº :=
by simp only [edist_eq_coe, edist_nndist]

end metric

lemma edist_add_right [normed_group Œ≥] [second_countable_topology Œ≥] [borel_space Œ≥]
  (f g h : Œ± ‚Üí‚Çò[Œº] Œ≥) :
  edist (f + h) (g + h) = edist f g :=
induction_on‚ÇÉ f g h $ Œª f hf g hg h hh, by simp [edist_mk_mk, edist_dist, dist_add_right]

section normed_space

variables {ùïú : Type*} [normed_field ùïú]
variables [normed_group Œ≥] [second_countable_topology Œ≥] [normed_space ùïú Œ≥] [borel_space Œ≥]

lemma edist_smul (c : ùïú) (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : edist (c ‚Ä¢ f) 0 = (ennreal.of_real ‚à•c‚à•) * edist f 0 :=
induction_on f $ Œª f hf, by simp [edist_mk_mk, zero_def, smul_mk, edist_dist, norm_smul,
  ennreal.of_real_mul, lintegral_const_mul']

end normed_space

section pos_part

variables [topological_space Œ≥] [decidable_linear_order Œ≥] [order_closed_topology Œ≥]
  [second_countable_topology Œ≥] [has_zero Œ≥] [opens_measurable_space Œ≥]

/-- Positive part of an `ae_eq_fun`. -/
def pos_part (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : Œ± ‚Üí‚Çò[Œº] Œ≥ :=
comp (Œª x, max x 0) (measurable_id.max measurable_const) f

@[simp] lemma pos_part_mk (f : Œ± ‚Üí Œ≥) (hf) :
  pos_part (mk f hf : Œ± ‚Üí‚Çò[Œº] Œ≥) = mk (Œª x, max (f x) 0) (hf.max measurable_const) :=
rfl

lemma coe_fn_pos_part (f : Œ± ‚Üí‚Çò[Œº] Œ≥) : ‚áë(pos_part f) =·µê[Œº] (Œª a, max (f a) 0) :=
coe_fn_comp _ _ _

end pos_part

end ae_eq_fun

end measure_theory
