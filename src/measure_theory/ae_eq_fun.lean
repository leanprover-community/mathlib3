/-
Copyright (c) 2019 Johannes HÃ¶lzl, Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Zhouhang Zhou
-/
import measure_theory.integration
import order.filter.germ

/-!

# Almost everywhere equal functions

Two measurable functions are treated as identical if they are almost everywhere equal. We form the
set of equivalence classes under the relation of being almost everywhere equal, which is sometimes
known as the `Lâ°` space.

See `l1_space.lean` for `LÂ¹` space.

## Notation

* `Î± â†’â‚˜[Î¼] Î²` is the type of `Lâ°` space, where `Î±` and `Î²` are measurable spaces and `Î¼`
  is a measure on `Î±`. `f : Î± â†’â‚˜ Î²` is a "function" in `Lâ°`. In comments, `[f]` is also used
  to denote an `Lâ°` function.

  `â‚˜` can be typed as `\_m`. Sometimes it is shown as a box if font is missing.


## Main statements

* The linear structure of `Lâ°` :
    Addition and scalar multiplication are defined on `Lâ°` in the natural way, i.e.,
    `[f] + [g] := [f + g]`, `c â€¢ [f] := [c â€¢ f]`. So defined, `Î± â†’â‚˜ Î²` inherits the linear structure
    of `Î²`. For example, if `Î²` is a module, then `Î± â†’â‚˜ Î²` is a module over the same ring.

    See `mk_add_mk`,  `neg_mk`,     `mk_sub_mk`,  `smul_mk`,
        `add_to_fun`, `neg_to_fun`, `sub_to_fun`, `smul_to_fun`

* The order structure of `Lâ°` :
    `â‰¤` can be defined in a similar way: `[f] â‰¤ [g]` if `f a â‰¤ g a` for almost all `a` in domain.
    And `Î± â†’â‚˜ Î²` inherits the preorder and partial order of `Î²`.

    TODO: Define `sup` and `inf` on `Lâ°` so that it forms a lattice. It seems that `Î²` must be a
    linear order, since otherwise `f âŠ” g` may not be a measurable function.

* Emetric on `Lâ°` :
    If `Î²` is an `emetric_space`, then `Lâ°` can be made into an `emetric_space`, where
    `edist [f] [g]` is defined to be `âˆ«â» a, edist (f a) (g a)`.

    The integral used here is `lintegral : (Î± â†’ ennreal) â†’ ennreal`, which is defined in the file
    `integration.lean`.

    See `edist_mk_mk` and `edist_to_fun`.

## Implementation notes

* `f.to_fun`     : To find a representative of `f : Î± â†’â‚˜ Î²`, use `f.to_fun`.
                 For each operation `op` in `Lâ°`, there is a lemma called `op_to_fun`,
                 characterizing, say, `(f op g).to_fun`.
* `ae_eq_fun.mk` : To constructs an `Lâ°` function `Î± â†’â‚˜ Î²` from a measurable function `f : Î± â†’ Î²`,
                 use `ae_eq_fun.mk`
* `comp`         : Use `comp g f` to get `[g âˆ˜ f]` from `g : Î² â†’ Î³` and `[f] : Î± â†’â‚˜ Î³`
* `compâ‚‚`        : Use `compâ‚‚ g fâ‚ fâ‚‚ to get `[Î»a, g (fâ‚ a) (fâ‚‚ a)]`.
                 For example, `[f + g]` is `compâ‚‚ (+)`


## Tags

function space, almost everywhere equal, `Lâ°`, ae_eq_fun

-/

noncomputable theory
open_locale classical

open set filter topological_space ennreal emetric measure_theory function
variables {Î± Î² Î³ Î´ : Type*} [measurable_space Î±] {Î¼ Î½ : measure Î±}


namespace function

lemma preimage_inv_fun_of_mem [n : nonempty Î±] {f : Î± â†’ Î²} (hf : injective f) {s : set Î±}
  (h : classical.choice n âˆˆ s) : inv_fun f â»Â¹' s = f '' s âˆª (f '' univ)á¶œ :=
begin
  apply subset.antisymm,
  { assume x hx,
    by_cases H : âˆƒ a, f a = x,
    { simp only [inv_fun, inv_fun_on, H, true_and, dif_pos, mem_univ, mem_preimage] at hx,
      left,
      simp only [mem_image],
      exact âŸ¨_, hx, classical.some_spec HâŸ© },
    { push_neg at H,
      simp [H] } },
  { assume x hx,
    cases hx,
    { rcases hx with âŸ¨y, ys, fyâŸ©,
      simp only [â†fy, left_inverse_inv_fun hf y, ys, mem_preimage] },
    { have : Â¬ (âˆƒ a, f a = x), by simpa using hx,
      simp only [inv_fun, inv_fun_on, this, h, true_and, mem_univ, mem_preimage, dif_neg,
        not_false_iff] } }
end

lemma preimage_inv_fun_of_not_mem [n : nonempty Î±] {f : Î± â†’ Î²} (hf : injective f)
  {s : set Î±} (h : classical.choice n âˆ‰ s) : inv_fun f â»Â¹' s = f '' s :=
begin
  apply subset.antisymm,
  { assume x hx,
    by_cases H : âˆƒ a, f a = x,
    { simp only [inv_fun, inv_fun_on, H, true_and, dif_pos, mem_univ, mem_preimage] at hx,
      simp only [mem_image],
      exact âŸ¨_, hx, classical.some_spec HâŸ© },
    { simp [inv_fun, inv_fun_on, H] at hx,
      exact false.elim (h hx) } },
  { assume x hx,
    rcases hx with âŸ¨y, ys, fyâŸ©,
    simp only [â†fy, left_inverse_inv_fun hf y, ys, mem_preimage] },
end


end function

section ae_measurable

variables [measurable_space Î²] {f g : Î± â†’ Î²}

/-- A function is almost everywhere measurable if there exists a measurable function which
coincides with it almost everywhere. -/
def ae_measurable (f : Î± â†’ Î²) (Î¼ : measure Î± . volume_tac) : Prop :=
âˆƒ g : Î± â†’ Î², measurable g âˆ§ f =áµ[Î¼] g

/-- Given an almost everywhere measurable function `f`, associate to it a measurable function
which coincides with it almost everywhere. -/
def ae_measurable.mk (f : Î± â†’ Î²) (h : ae_measurable f Î¼) : Î± â†’ Î² := classical.some h

lemma ae_measurable.measurable_mk (h : ae_measurable f Î¼) : measurable (h.mk f) :=
(classical.some_spec h).1

lemma ae_measurable.ae_eq_mk (h : ae_measurable f Î¼) : f =áµ[Î¼] (h.mk f) :=
(classical.some_spec h).2

lemma measurable.ae_measurable (h : measurable f) :
  ae_measurable f Î¼ :=
âŸ¨f, h, ae_eq_refl fâŸ©

lemma ae_measurable.congr (hf : ae_measurable f Î¼) (h : f =áµ[Î¼] g) : ae_measurable g Î¼ :=
âŸ¨hf.mk f, hf.measurable_mk, h.symm.trans hf.ae_eq_mkâŸ©

lemma ae_measurable_congr (h : f =áµ[Î¼] g) :
  ae_measurable f Î¼ â†” ae_measurable g Î¼ :=
âŸ¨Î» hf, ae_measurable.congr hf h, Î» hg, ae_measurable.congr hg h.symmâŸ©

lemma ae_measurable.mono_measure (h : ae_measurable f Î¼) (h' : Î½ â‰¤ Î¼) : ae_measurable f Î½ :=
begin
  exact âŸ¨h.mk f, h.measurable_mk, eventually.filter_mono (ae_mono h') h.ae_eq_mkâŸ©
end

lemma ae_measurable.add_measure {f : Î± â†’ Î²} (hÎ¼ : ae_measurable f Î¼) (hÎ½ : ae_measurable f Î½) :
  ae_measurable f (Î¼ + Î½) :=
begin
  let s := {x | f x â‰  hÎ¼.mk f x},
  have : Î¼ s = 0 := hÎ¼.ae_eq_mk,
  obtain âŸ¨t, st, t_meas, Î¼tâŸ© : âˆƒ t, s âŠ† t âˆ§ is_measurable t âˆ§ Î¼ t = 0 :=
    exists_is_measurable_superset_of_measure_eq_zero this,
  let g : Î± â†’ Î² := t.piecewise (hÎ½.mk f) (hÎ¼.mk f),
  refine âŸ¨g, measurable.piecewise t_meas hÎ½.measurable_mk hÎ¼.measurable_mk, _âŸ©,
  change Î¼ {x | f x â‰  g x} + Î½ {x | f x â‰  g x} = 0,
  suffices : Î¼ {x | f x â‰  g x} = 0 âˆ§ Î½ {x | f x â‰  g x} = 0, by simp [this.1, this.2],
  have ht : {x | f x â‰  g x} âŠ† t,
  { assume x hx,
    by_contra h,
    simp only [g, h, mem_set_of_eq, ne.def, not_false_iff, piecewise_eq_of_not_mem] at hx,
    exact h (st hx) },
  split,
  { have : Î¼ {x | f x â‰  g x} â‰¤ Î¼ t := measure_mono ht,
    rw Î¼t at this,
    exact le_antisymm this bot_le },
  { have : {x | f x â‰  g x} âŠ† {x | f x â‰  hÎ½.mk f x},
    { assume x hx,
      simpa [ht hx, g] using hx },
    apply le_antisymm _ bot_le,
    calc Î½ {x | f x â‰  g x} â‰¤ Î½ {x | f x â‰  hÎ½.mk f x} : measure_mono this
    ... = 0 : hÎ½.ae_eq_mk }
end

@[simp] lemma ae_measurable_add_measure_iff :
  ae_measurable f (Î¼ + Î½) â†” ae_measurable f Î¼ âˆ§ ae_measurable f Î½ :=
âŸ¨Î» h, âŸ¨h.mono_measure (measure.le_add_right (le_refl _)),
         h.mono_measure (measure.le_add_left (le_refl _))âŸ©,
  Î» h, h.1.add_measure h.2âŸ©

lemma ae_measurable.smul_measure (h : ae_measurable f Î¼) (c : ennreal) :
  ae_measurable f (c â€¢ Î¼) :=
begin
  exact âŸ¨h.mk f, h.measurable_mk, ae_smul_measure h.ae_eq_mk câŸ©
end

lemma ae_eq_comp {f : Î± â†’ Î²} {g g' : Î² â†’ Î´} (hf : measurable f)
  (h : g =áµ[measure.map f Î¼] g') : g âˆ˜ f =áµ[Î¼] g' âˆ˜ f :=
begin
  rcases exists_is_measurable_superset_of_measure_eq_zero h with âŸ¨t, ht, tmeas, tzeroâŸ©,
  refine le_antisymm _ bot_le,
  calc Î¼ {x | g (f x) â‰  g' (f x)} â‰¤ Î¼ (fâ»Â¹' t) : measure_mono (Î» x hx, ht hx)
  ... = 0 : by rwa â† measure.map_apply hf tmeas
end

lemma ae_measurable.comp_measurable [measurable_space Î´] {f : Î± â†’ Î´} {g : Î´ â†’ Î²}
  (hg : ae_measurable g (measure.map f Î¼)) (hf : measurable f) : ae_measurable (g âˆ˜ f) Î¼ :=
âŸ¨(hg.mk g) âˆ˜ f, hg.measurable_mk.comp hf, ae_eq_comp hf hg.ae_eq_mkâŸ©

lemma measurable.comp_ae_measurable [measurable_space Î´] {f : Î± â†’ Î´} {g : Î´ â†’ Î²}
  (hg : measurable g) (hf : ae_measurable f Î¼) : ae_measurable (g âˆ˜ f) Î¼ :=
âŸ¨g âˆ˜ hf.mk f, hg.comp hf.measurable_mk, eventually_eq.fun_comp hf.ae_eq_mk _âŸ©

lemma lintegral_map' {f : Î² â†’ ennreal} {g : Î± â†’ Î²}
  (hf : ae_measurable f (measure.map g Î¼)) (hg : measurable g) :
  âˆ«â» a, f a âˆ‚(measure.map g Î¼) = âˆ«â» a, f (g a) âˆ‚Î¼ :=
calc âˆ«â» a, f a âˆ‚(measure.map g Î¼) = âˆ«â» a, hf.mk f a âˆ‚(measure.map g Î¼) :
  lintegral_congr_ae hf.ae_eq_mk
... = âˆ«â» a, hf.mk f (g a) âˆ‚Î¼ : lintegral_map hf.measurable_mk hg
... = âˆ«â» a, f (g a) âˆ‚Î¼ : lintegral_congr_ae (ae_eq_comp hg hf.ae_eq_mk.symm)

lemma lintegral_add' {f g : Î± â†’ ennreal} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  (âˆ«â» a, f a + g a âˆ‚Î¼) = (âˆ«â» a, f a âˆ‚Î¼) + (âˆ«â» a, g a âˆ‚Î¼) :=
calc (âˆ«â» a, f a + g a âˆ‚Î¼) = (âˆ«â» a, hf.mk f a + hg.mk g a âˆ‚Î¼) :
  lintegral_congr_ae (eventually_eq.add hf.ae_eq_mk hg.ae_eq_mk)
... = (âˆ«â» a, hf.mk f a âˆ‚Î¼) + (âˆ«â» a, hg.mk g a âˆ‚Î¼) : lintegral_add hf.measurable_mk hg.measurable_mk
... = (âˆ«â» a, f a âˆ‚Î¼) + (âˆ«â» a, g a âˆ‚Î¼) : begin
  congr' 1,
  { exact lintegral_congr_ae hf.ae_eq_mk.symm },
  { exact lintegral_congr_ae hg.ae_eq_mk.symm },
end

@[simp] lemma lintegral_eq_zero_iff' {f : Î± â†’ ennreal} (hf : ae_measurable f Î¼) :
  âˆ«â» a, f a âˆ‚Î¼ = 0 â†” (f =áµ[Î¼] 0) :=
begin
  have : âˆ«â» a, f a âˆ‚Î¼ = âˆ«â» a, hf.mk f a âˆ‚Î¼ := lintegral_congr_ae hf.ae_eq_mk,
  rw [this, lintegral_eq_zero_iff hf.measurable_mk],
  exact âŸ¨Î» H, hf.ae_eq_mk.trans H, Î» H, hf.ae_eq_mk.symm.trans HâŸ©
end

lemma ae_measurable.norm [normed_group Î²] [opens_measurable_space Î²] (hf : ae_measurable f Î¼) :
  ae_measurable (Î» a, norm (f a)) Î¼ :=
âŸ¨Î» a, norm (hf.mk f a), hf.measurable_mk.norm, hf.ae_eq_mk.fun_comp _âŸ©

lemma ae_measurable.ennnorm [normed_group Î²] [opens_measurable_space Î²] (hf : ae_measurable f Î¼) :
  ae_measurable (Î» a, (nnnorm (f a) : ennreal)) Î¼ :=
âŸ¨(Î» a, (nnnorm (hf.mk f a) : ennreal)), hf.measurable_mk.ennnorm,
  hf.ae_eq_mk.fun_comp (Î» a : Î², (nnnorm a : ennreal))âŸ©

lemma ae_measurable.edist [emetric_space Î²] [second_countable_topology Î²] [opens_measurable_space Î²]
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) : ae_measurable (Î» a, edist (f a) (g a)) Î¼ :=
âŸ¨Î» a, edist (hf.mk f a) (hg.mk g a), hf.measurable_mk.edist hg.measurable_mk,
  eventually_eq.compâ‚‚ hf.ae_eq_mk _ hg.ae_eq_mkâŸ©

lemma ae_measurable.prod_mk [measurable_space Î³] {f : Î± â†’ Î²} {g : Î± â†’ Î³}
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) : ae_measurable (Î» x, (f x, g x)) Î¼ :=
âŸ¨Î» a, (hf.mk f a, hg.mk g a), hf.measurable_mk.prod_mk hg.measurable_mk,
  eventually_eq.prod_mk hf.ae_eq_mk hg.ae_eq_mk,âŸ©

lemma ae_measurable.max [linear_order Î²] [topological_space Î²] [order_closed_topology Î²]
  [second_countable_topology Î²] [opens_measurable_space Î²]
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  ae_measurable (Î» a, max (f a) (g a)) Î¼ :=
âŸ¨Î» a, max (hf.mk f a) (hg.mk g a), hf.measurable_mk.max hg.measurable_mk,
  eventually_eq.compâ‚‚ hf.ae_eq_mk _ hg.ae_eq_mkâŸ©

lemma ae_measurable.min [linear_order Î²] [topological_space Î²] [order_closed_topology Î²]
  [second_countable_topology Î²] [opens_measurable_space Î²]
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  ae_measurable (Î» a, min (f a) (g a)) Î¼ :=
âŸ¨Î» a, min (hf.mk f a) (hg.mk g a), hf.measurable_mk.min hg.measurable_mk,
  eventually_eq.compâ‚‚ hf.ae_eq_mk _ hg.ae_eq_mkâŸ©

lemma ae_measurable.smul [semiring Î²] [topological_space Î²] [second_countable_topology Î²]
  [opens_measurable_space Î²] [add_comm_monoid Î³] [topological_space Î³] [second_countable_topology Î³]
  [semimodule Î² Î³] [topological_semimodule Î² Î³] [measurable_space Î³] [borel_space Î³]
  {g : Î± â†’ Î³} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  ae_measurable (Î» c, f c â€¢ g c) Î¼ :=
âŸ¨Î» a, hf.mk f a â€¢ hg.mk g a, hf.measurable_mk.smul hg.measurable_mk,
  eventually_eq.compâ‚‚ hf.ae_eq_mk _ hg.ae_eq_mkâŸ©

@[to_additive]
lemma ae_measurable.mul [topological_space Î²] [borel_space Î²] [has_mul Î²] [has_continuous_mul Î²]
  [second_countable_topology Î²]
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) : ae_measurable (Î» a, f a * g a) Î¼ :=
âŸ¨Î» a, hf.mk f a * hg.mk g a, hf.measurable_mk.mul hg.measurable_mk,
  eventually_eq.compâ‚‚ hf.ae_eq_mk _ hg.ae_eq_mkâŸ©

@[to_additive]
lemma ae_measurable.inv [topological_space Î²] [group Î²] [topological_group Î²] [borel_space Î²]
  (hf : ae_measurable f Î¼) : ae_measurable (Î» a, (f a)â»Â¹) Î¼ :=
âŸ¨Î» a, (hf.mk f a)â»Â¹, hf.measurable_mk.inv, eventually_eq.fun_comp hf.ae_eq_mk _âŸ©

lemma ae_measurable.sub [add_group Î²] [topological_space Î²] [topological_add_group Î²]
  [second_countable_topology Î²] [borel_space Î²]
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) : ae_measurable (Î» x, f x - g x) Î¼ :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

lemma ae_measurable.const_smul {R M : Type*} [topological_space R] [semiring R]
  [add_comm_monoid M] [semimodule R M] [topological_space M] [topological_semimodule R M]
  [measurable_space M] [borel_space M]
  {f : Î± â†’ M} (hf : ae_measurable f Î¼) (c : R) :
  ae_measurable (Î» x, c â€¢ f x) Î¼ :=
âŸ¨Î» a, c â€¢ hf.mk f a, hf.measurable_mk.const_smul _, eventually_eq.fun_comp hf.ae_eq_mk _âŸ©

lemma ae_measurable_const_smul_iff {R M : Type*} [topological_space R] [division_ring R]
  [add_comm_monoid M] [semimodule R M] [topological_space M] [topological_semimodule R M]
  [measurable_space M] [borel_space M]
  {f : Î± â†’ M} {c : R} (hc : c â‰  0) :
  ae_measurable (Î» x, c â€¢ f x) Î¼ â†” ae_measurable f Î¼ :=
âŸ¨Î» h, by simpa only [smul_smul, inv_mul_cancel hc, one_smul] using h.const_smul câ»Â¹,
  Î» h, h.const_smul câŸ©

lemma closed_embedding.measurable_inv_fun [topological_space Î²] [borel_space Î²] [n : nonempty Î²]
  [measurable_space Î³] [topological_space Î³] [borel_space Î³] {g : Î² â†’ Î³} (hg : closed_embedding g) :
  measurable (function.inv_fun g) :=
begin
  refine measurable_of_is_closed (Î» s hs, _),
  let o := classical.choice n,
  by_cases h : o âˆˆ s,
  { have : function.inv_fun g â»Â¹' s = g '' s âˆª (g '' univ)á¶œ :=
      function.preimage_inv_fun_of_mem hg.to_embedding.inj h,
    rw this,
    apply is_measurable.union,
    { exact ((closed_embedding.closed_iff_image_closed hg).mp hs).is_measurable },
    { exact ((closed_embedding.closed_iff_image_closed hg).mp is_closed_univ).is_measurable.compl } },
  { have : function.inv_fun g â»Â¹' s = g '' s :=
      function.preimage_inv_fun_of_not_mem hg.to_embedding.inj h,
    rw this,
    exact ((closed_embedding.closed_iff_image_closed hg).mp hs).is_measurable }
end

lemma measurable_of_not_nonempty  (h : Â¬ nonempty Î±) (f : Î± â†’ Î²) : measurable f :=
begin
  assume s hs,
  convert is_measurable.empty,
  exact eq_empty_of_not_nonempty h _,
end

lemma ae_measurable_comp_iff_of_closed_embedding
  [topological_space Î²] [borel_space Î²] [topological_space Î³]
  [measurable_space Î³] [borel_space Î³] (g : Î² â†’ Î³) (hg : closed_embedding g) :
  ae_measurable (g âˆ˜ f) Î¼ â†” ae_measurable f Î¼ :=
begin
  by_cases h : nonempty Î²,
  { resetI,
    refine âŸ¨Î» hf, _, Î» hf, hg.continuous.measurable.comp_ae_measurable hfâŸ©,
    convert hg.measurable_inv_fun.comp_ae_measurable hf,
    ext x,
    exact (function.left_inverse_inv_fun hg.to_embedding.inj (f x)).symm },
  { have H : Â¬ nonempty Î±, by { contrapose! h, exact nonempty.map f h },
    simp [(measurable_of_not_nonempty H (g âˆ˜ f)).ae_measurable,
          (measurable_of_not_nonempty H f).ae_measurable] }
end

lemma ae_measurable_smul_const
  {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] [complete_space ğ•œ] [measurable_space ğ•œ] [borel_space ğ•œ]
  {E : Type*} [normed_group E] [normed_space ğ•œ E] [measurable_space E] [borel_space E]
  {f : Î± â†’ ğ•œ} {c : E} (hc : c â‰  0) :
  ae_measurable (Î» x, f x â€¢ c) Î¼ â†” ae_measurable f Î¼ :=
ae_measurable_comp_iff_of_closed_embedding (Î» y : ğ•œ, y â€¢ c) (closed_embedding_smul_left hc)

@[simp] lemma ae_measurable_const {b : Î²} : ae_measurable (Î» a : Î±, b) Î¼ :=
measurable_const.ae_measurable

end ae_measurable

/-

/-- Given an almost everywhere measurable function `f`, associate its class of almost everywhere
defined functions. -/
def ae_measurable.mk_ae (f : Î± â†’ Î²) (h : ae_measurable f Î¼) : Î± â†’â‚˜[Î¼] Î² :=
ae_eq_fun.mk (h.mk f) h.measurable_mk

@[simp] lemma ae_measurable.mk_ae_eq_mk_ae_iff (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  hf.mk_ae f = hg.mk_ae g â†” f =áµ[Î¼] g :=
begin
  simp only [ae_measurable.mk_ae, ae_eq_fun.mk_eq_mk],
  exact âŸ¨Î» H, (hf.ae_eq_mk.trans H).trans hg.ae_eq_mk.symm,
    Î» H, (hf.ae_eq_mk.symm.trans H).trans hg.ae_eq_mkâŸ©
end

lemma ae_measurable.mk_ae_eq_mk (hf : measurable f) :
  (hf.ae_measurable.mk_ae f : Î± â†’â‚˜[Î¼] Î²) = ae_eq_fun.mk f hf :=
begin
  rw [ae_measurable.mk_ae, ae_eq_fun.mk_eq_mk],
  exact hf.ae_measurable.ae_eq_mk.symm
end

@[simp] lemma coe_fn_mk_ae (f : Î± â†’â‚˜[Î¼] Î²) :
  ae_measurable.mk_ae f f.measurable.ae_measurable = f :=
begin
  conv_rhs { rw â† ae_eq_fun.mk_coe_fn f },
  rw [ae_measurable.mk_ae, ae_eq_fun.mk_eq_mk],
  exact (ae_measurable.ae_eq_mk _).symm
end

lemma edist_mk_ae_mk_ae'
  [metric_space Î²] [second_countable_topology Î²] [opens_measurable_space Î²]
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  edist (hf.mk_ae f) (hg.mk_ae g) = âˆ«â» x, nndist (f x) (g x) âˆ‚Î¼ :=
calc
edist (hf.mk_ae f) (hg.mk_ae g) = âˆ«â» x, nndist (hf.mk f x) (hg.mk g x) âˆ‚Î¼ :
  ae_eq_fun.edist_mk_mk' hf.measurable_mk hg.measurable_mk
... = âˆ«â» x, nndist (f x) (g x) âˆ‚Î¼ :
begin
  apply lintegral_congr_ae,
  apply eventually_eq.compâ‚‚ hf.ae_eq_mk.symm (Î» x y, (nndist x y : ennreal)) hg.ae_eq_mk.symm,
end
-/

namespace measure_theory

section measurable_space
variables [measurable_space Î²]

variable (Î²)

/-- The equivalence relation of being almost everywhere equal -/
def measure.ae_eq_setoid (Î¼ : measure Î±) : setoid { f : Î± â†’ Î² // ae_measurable f Î¼ } :=
âŸ¨Î»f g, (f : Î± â†’ Î²) =áµ[Î¼] g, Î» f, ae_eq_refl f, Î» f g, ae_eq_symm, Î» f g h, ae_eq_transâŸ©

variable (Î±)

/-- The space of equivalence classes of measurable functions, where two measurable functions are
    equivalent if they agree almost everywhere, i.e., they differ on a set of measure `0`.  -/
def ae_eq_fun (Î¼ : measure Î±) : Type* := quotient (Î¼.ae_eq_setoid Î²)

variables {Î± Î²}

notation Î± ` â†’â‚˜[`:25 Î¼ `] ` Î² := ae_eq_fun Î± Î² Î¼

end measurable_space

namespace ae_eq_fun
variables [measurable_space Î²] [measurable_space Î³] [measurable_space Î´]

/-- Construct the equivalence class `[f]` of a measurable function `f`, based on the equivalence
    relation of being almost everywhere equal. -/
def mk (f : Î± â†’ Î²) (hf : ae_measurable f Î¼) : Î± â†’â‚˜[Î¼] Î² := quotient.mk' âŸ¨f, hfâŸ©

/-- A representative of an `ae_eq_fun` [f] -/
instance : has_coe_to_fun (Î± â†’â‚˜[Î¼] Î²) :=
âŸ¨_, Î»f, ae_measurable.mk _ (quotient.out' f : {f : Î± â†’ Î² // ae_measurable f Î¼}).2âŸ©

protected lemma measurable (f : Î± â†’â‚˜[Î¼] Î²) : measurable f :=
ae_measurable.measurable_mk _

protected lemma ae_measurable (f : Î± â†’â‚˜[Î¼] Î²) : ae_measurable f Î¼ :=
f.measurable.ae_measurable

@[simp] lemma quot_mk_eq_mk (f : Î± â†’ Î²) (hf) :
  (quot.mk (@setoid.r _ $ Î¼.ae_eq_setoid Î²) âŸ¨f, hfâŸ© : Î± â†’â‚˜[Î¼] Î²) = mk f hf :=
rfl

@[simp] lemma mk_eq_mk {f g : Î± â†’ Î²} {hf hg} :
  (mk f hf : Î± â†’â‚˜[Î¼] Î²) = mk g hg â†” f =áµ[Î¼] g :=
quotient.eq'

@[simp] lemma mk_coe_fn (f : Î± â†’â‚˜[Î¼] Î²) : mk f f.ae_measurable = f :=
begin
  conv_rhs { rw â† quotient.out_eq' f },
  set g : {f : Î± â†’ Î² // ae_measurable f Î¼} := quotient.out' f with hg,
  have : g = âŸ¨g.1, g.2âŸ© := subtype.eq rfl,
  rw [this, â† mk, mk_eq_mk],
  exact (ae_measurable.ae_eq_mk _).symm,
end

@[ext] lemma ext {f g : Î± â†’â‚˜[Î¼] Î²} (h : f =áµ[Î¼] g) : f = g :=
by rwa [â† f.mk_coe_fn, â† g.mk_coe_fn, mk_eq_mk]

lemma coe_fn_mk (f : Î± â†’ Î²) (hf) : (mk f hf : Î± â†’â‚˜[Î¼] Î²) =áµ[Î¼] f :=
begin
   apply (ae_measurable.ae_eq_mk _).symm.trans,
   exact @quotient.mk_out' _ (Î¼.ae_eq_setoid Î²) (âŸ¨f, hfâŸ© : {f // ae_measurable f Î¼})
end

@[elab_as_eliminator]
lemma induction_on (f : Î± â†’â‚˜[Î¼] Î²) {p : (Î± â†’â‚˜[Î¼] Î²) â†’ Prop} (H : âˆ€ f hf, p (mk f hf)) : p f :=
quotient.induction_on' f $ subtype.forall.2 H

@[elab_as_eliminator]
lemma induction_onâ‚‚ {Î±' Î²' : Type*} [measurable_space Î±'] [measurable_space Î²'] {Î¼' : measure Î±'}
  (f : Î± â†’â‚˜[Î¼] Î²) (f' : Î±' â†’â‚˜[Î¼'] Î²') {p : (Î± â†’â‚˜[Î¼] Î²) â†’ (Î±' â†’â‚˜[Î¼'] Î²') â†’ Prop}
  (H : âˆ€ f hf f' hf', p (mk f hf) (mk f' hf')) :
  p f f' :=
induction_on f $ Î» f hf, induction_on f' $ H f hf

@[elab_as_eliminator]
lemma induction_onâ‚ƒ {Î±' Î²' : Type*} [measurable_space Î±'] [measurable_space Î²'] {Î¼' : measure Î±'}
  {Î±'' Î²'' : Type*} [measurable_space Î±''] [measurable_space Î²''] {Î¼'' : measure Î±''}
  (f : Î± â†’â‚˜[Î¼] Î²) (f' : Î±' â†’â‚˜[Î¼'] Î²') (f'' : Î±'' â†’â‚˜[Î¼''] Î²'')
  {p : (Î± â†’â‚˜[Î¼] Î²) â†’ (Î±' â†’â‚˜[Î¼'] Î²') â†’ (Î±'' â†’â‚˜[Î¼''] Î²'') â†’ Prop}
  (H : âˆ€ f hf f' hf' f'' hf'', p (mk f hf) (mk f' hf') (mk f'' hf'')) :
  p f f' f'' :=
induction_on f $ Î» f hf, induction_onâ‚‚ f' f'' $ H f hf

/-- Given a measurable function `g : Î² â†’ Î³`, and an almost everywhere equal function `[f] : Î± â†’â‚˜ Î²`,
    return the equivalence class of `g âˆ˜ f`, i.e., the almost everywhere equal function
    `[g âˆ˜ f] : Î± â†’â‚˜ Î³`. -/
def comp (g : Î² â†’ Î³) (hg : measurable g) (f : Î± â†’â‚˜[Î¼] Î²) : Î± â†’â‚˜[Î¼] Î³ :=
quotient.lift_on' f (Î» f, mk (g âˆ˜ (f : Î± â†’ Î²)) (hg.comp_ae_measurable f.2)) $
  Î» f f' H, mk_eq_mk.2 $ H.fun_comp g

@[simp] lemma comp_mk (g : Î² â†’ Î³) (hg : measurable g)
  (f : Î± â†’ Î²) (hf) :
  comp g hg (mk f hf : Î± â†’â‚˜[Î¼] Î²) = mk (g âˆ˜ f) (hg.comp_ae_measurable hf) :=
rfl

lemma comp_eq_mk (g : Î² â†’ Î³) (hg : measurable g) (f : Î± â†’â‚˜[Î¼] Î²) :
  comp g hg f = mk (g âˆ˜ f) (hg.comp_ae_measurable f.ae_measurable) :=
by rw [â† comp_mk g hg f f.ae_measurable, mk_coe_fn]

lemma coe_fn_comp (g : Î² â†’ Î³) (hg : measurable g) (f : Î± â†’â‚˜[Î¼] Î²) :
  comp g hg f =áµ[Î¼] g âˆ˜ f :=
by { rw [comp_eq_mk], apply coe_fn_mk }

/-- The class of `x â†¦ (f x, g x)`. -/
def pair (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î³) : Î± â†’â‚˜[Î¼] Î² Ã— Î³ :=
quotient.lift_onâ‚‚' f g (Î» f g, mk (Î» x, (f.1 x, g.1 x)) (f.2.prod_mk g.2)) $
  Î» f g f' g' Hf Hg, mk_eq_mk.2 $ Hf.prod_mk Hg

@[simp] lemma pair_mk_mk (f : Î± â†’ Î²) (hf) (g : Î± â†’ Î³) (hg) :
  (mk f hf : Î± â†’â‚˜[Î¼] Î²).pair (mk g hg) = mk (Î» x, (f x, g x)) (hf.prod_mk hg) :=
rfl

lemma pair_eq_mk (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î³) :
  f.pair g = mk (Î» x, (f x, g x)) (f.ae_measurable.prod_mk g.ae_measurable) :=
by simp only [â† pair_mk_mk, mk_coe_fn]

lemma coe_fn_pair (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î³) :
  f.pair g =áµ[Î¼] (Î» x, (f x, g x)) :=
by { rw pair_eq_mk, apply coe_fn_mk }

/-- Given a measurable function `g : Î² â†’ Î³ â†’ Î´`, and almost everywhere equal functions
    `[fâ‚] : Î± â†’â‚˜ Î²` and `[fâ‚‚] : Î± â†’â‚˜ Î³`, return the equivalence class of the function
    `Î»a, g (fâ‚ a) (fâ‚‚ a)`, i.e., the almost everywhere equal function
    `[Î»a, g (fâ‚ a) (fâ‚‚ a)] : Î± â†’â‚˜ Î³` -/
def compâ‚‚ {Î³ Î´ : Type*} [measurable_space Î³] [measurable_space Î´] (g : Î² â†’ Î³ â†’ Î´)
  (hg : measurable (uncurry g)) (fâ‚ : Î± â†’â‚˜[Î¼] Î²) (fâ‚‚ : Î± â†’â‚˜[Î¼] Î³) : Î± â†’â‚˜[Î¼] Î´ :=
comp _ hg (fâ‚.pair fâ‚‚)

@[simp] lemma compâ‚‚_mk_mk {Î³ Î´ : Type*} [measurable_space Î³] [measurable_space Î´]
  (g : Î² â†’ Î³ â†’ Î´) (hg : measurable (uncurry g)) (fâ‚ : Î± â†’ Î²) (fâ‚‚ : Î± â†’ Î³) (hfâ‚ hfâ‚‚) :
  compâ‚‚ g hg (mk fâ‚ hfâ‚ : Î± â†’â‚˜[Î¼] Î²) (mk fâ‚‚ hfâ‚‚) =
    mk (Î»a, g (fâ‚ a) (fâ‚‚ a)) (hg.comp_ae_measurable (hfâ‚.prod_mk hfâ‚‚)) :=
rfl

lemma compâ‚‚_eq_pair {Î³ Î´ : Type*} [measurable_space Î³] [measurable_space Î´]
  (g : Î² â†’ Î³ â†’ Î´) (hg : measurable (uncurry g)) (fâ‚ : Î± â†’â‚˜[Î¼] Î²) (fâ‚‚ : Î± â†’â‚˜[Î¼] Î³) :
  compâ‚‚ g hg fâ‚ fâ‚‚ = comp _ hg (fâ‚.pair fâ‚‚) :=
rfl

lemma compâ‚‚_eq_mk {Î³ Î´ : Type*} [measurable_space Î³] [measurable_space Î´]
  (g : Î² â†’ Î³ â†’ Î´) (hg : measurable (uncurry g)) (fâ‚ : Î± â†’â‚˜[Î¼] Î²) (fâ‚‚ : Î± â†’â‚˜[Î¼] Î³) :
  compâ‚‚ g hg fâ‚ fâ‚‚ = mk (Î» a, g (fâ‚ a) (fâ‚‚ a))
    (hg.comp_ae_measurable (fâ‚.ae_measurable.prod_mk fâ‚‚.ae_measurable)) :=
by rw [compâ‚‚_eq_pair, pair_eq_mk, comp_mk]; refl

lemma coe_fn_compâ‚‚ {Î³ Î´ : Type*} [measurable_space Î³] [measurable_space Î´]
  (g : Î² â†’ Î³ â†’ Î´) (hg : measurable (uncurry g)) (fâ‚ : Î± â†’â‚˜[Î¼] Î²) (fâ‚‚ : Î± â†’â‚˜[Î¼] Î³) :
  compâ‚‚ g hg fâ‚ fâ‚‚ =áµ[Î¼] Î» a, g (fâ‚ a) (fâ‚‚ a) :=
by { rw compâ‚‚_eq_mk, apply coe_fn_mk }

/-- Interpret `f : Î± â†’â‚˜[Î¼] Î²` as a germ at `Î¼.ae` forgetting that `f` is measurable. -/
def to_germ (f : Î± â†’â‚˜[Î¼] Î²) : germ Î¼.ae Î² :=
quotient.lift_on' f (Î» f, ((f : Î± â†’ Î²) : germ Î¼.ae Î²)) $ Î» f g H, germ.coe_eq.2 H

@[simp] lemma mk_to_germ (f : Î± â†’ Î²) (hf) : (mk f hf : Î± â†’â‚˜[Î¼] Î²).to_germ = f := rfl

lemma to_germ_eq (f : Î± â†’â‚˜[Î¼] Î²) : f.to_germ = (f : Î± â†’ Î²) :=
by rw [â† mk_to_germ, mk_coe_fn]

lemma to_germ_injective : injective (to_germ : (Î± â†’â‚˜[Î¼] Î²) â†’ germ Î¼.ae Î²) :=
Î» f g H, ext $ germ.coe_eq.1 $ by rwa [â† to_germ_eq, â† to_germ_eq]

lemma comp_to_germ (g : Î² â†’ Î³) (hg : measurable g) (f : Î± â†’â‚˜[Î¼] Î²) :
  (comp g hg f).to_germ = f.to_germ.map g :=
induction_on f $ Î» f hf, by simp

lemma compâ‚‚_to_germ (g : Î² â†’ Î³ â†’ Î´) (hg : measurable (uncurry g))
  (fâ‚ : Î± â†’â‚˜[Î¼] Î²) (fâ‚‚ : Î± â†’â‚˜[Î¼] Î³) :
  (compâ‚‚ g hg fâ‚ fâ‚‚).to_germ = fâ‚.to_germ.mapâ‚‚ g fâ‚‚.to_germ :=
induction_onâ‚‚ fâ‚ fâ‚‚ $ Î» fâ‚ hfâ‚ fâ‚‚ hfâ‚‚, by simp

/-- Given a predicate `p` and an equivalence class `[f]`, return true if `p` holds of `f a`
    for almost all `a` -/
def lift_pred (p : Î² â†’ Prop) (f : Î± â†’â‚˜[Î¼] Î²) : Prop := f.to_germ.lift_pred p

/-- Given a relation `r` and equivalence class `[f]` and `[g]`, return true if `r` holds of
    `(f a, g a)` for almost all `a` -/
def lift_rel (r : Î² â†’ Î³ â†’ Prop) (f : Î± â†’â‚˜[Î¼] Î²) (g : Î± â†’â‚˜[Î¼] Î³) : Prop :=
f.to_germ.lift_rel r g.to_germ

lemma lift_rel_mk_mk {r : Î² â†’ Î³ â†’ Prop} {f : Î± â†’ Î²} {g : Î± â†’ Î³} {hf hg} :
  lift_rel r (mk f hf : Î± â†’â‚˜[Î¼] Î²) (mk g hg) â†” âˆ€áµ a âˆ‚Î¼, r (f a) (g a) :=
iff.rfl

lemma lift_rel_iff_coe_fn {r : Î² â†’ Î³ â†’ Prop} {f : Î± â†’â‚˜[Î¼] Î²} {g : Î± â†’â‚˜[Î¼] Î³} :
  lift_rel r f g â†” âˆ€áµ a âˆ‚Î¼, r (f a) (g a) :=
by rw [â† lift_rel_mk_mk, mk_coe_fn, mk_coe_fn]

section order

instance [preorder Î²] : preorder (Î± â†’â‚˜[Î¼] Î²) := preorder.lift to_germ

@[simp] lemma mk_le_mk [preorder Î²] {f g : Î± â†’ Î²} (hf hg) :
  (mk f hf : Î± â†’â‚˜[Î¼] Î²) â‰¤ mk g hg â†” f â‰¤áµ[Î¼] g :=
iff.rfl

@[simp, norm_cast] lemma coe_fn_le [preorder Î²] {f g : Î± â†’â‚˜[Î¼] Î²} :
  (f : Î± â†’ Î²) â‰¤áµ[Î¼] g â†” f â‰¤ g :=
lift_rel_iff_coe_fn.symm

instance [partial_order Î²] : partial_order (Î± â†’â‚˜[Î¼] Î²) :=
partial_order.lift to_germ to_germ_injective

/- TODO: Prove `Lâ°` space is a lattice if Î² is linear order.
         What if Î² is only a lattice? -/

-- instance [linear_order Î²] : semilattice_sup (Î± â†’â‚˜ Î²) :=
-- { sup := compâ‚‚ (âŠ”) (_),
--    .. ae_eq_fun.partial_order }

end order

variable (Î±)
/-- The equivalence class of a constant function: `[Î»a:Î±, b]`, based on the equivalence relation of
    being almost everywhere equal -/
def const (b : Î²) : Î± â†’â‚˜[Î¼] Î² := mk (Î»a:Î±, b) ae_measurable_const

lemma coe_fn_const (b : Î²) : (const Î± b : Î± â†’â‚˜[Î¼] Î²) =áµ[Î¼] function.const Î± b :=
coe_fn_mk _ _

variable {Î±}

instance [inhabited Î²] : inhabited (Î± â†’â‚˜[Î¼] Î²) := âŸ¨const Î± (default Î²)âŸ©

@[to_additive] instance [has_one Î²] : has_one (Î± â†’â‚˜[Î¼] Î²) := âŸ¨const Î± 1âŸ©
@[to_additive] lemma one_def [has_one Î²] :
  (1 : Î± â†’â‚˜[Î¼] Î²) = mk (Î»a:Î±, 1) ae_measurable_const := rfl
@[to_additive] lemma coe_fn_one [has_one Î²] : â‡‘(1 : Î± â†’â‚˜[Î¼] Î²) =áµ[Î¼] 1 := coe_fn_const _ _
@[simp, to_additive] lemma one_to_germ [has_one Î²] : (1 : Î± â†’â‚˜[Î¼] Î²).to_germ = 1 := rfl

section monoid
variables
  [topological_space Î³] [second_countable_topology Î³] [borel_space Î³]
  [monoid Î³] [has_continuous_mul Î³]

@[to_additive]
instance : has_mul (Î± â†’â‚˜[Î¼] Î³) := âŸ¨compâ‚‚ (*) measurable_mulâŸ©

@[simp, to_additive] lemma mk_mul_mk (f g : Î± â†’ Î³) (hf hg) :
  (mk f hf : Î± â†’â‚˜[Î¼] Î³) * (mk g hg) = mk (f * g) (hf.mul hg) :=
rfl

@[to_additive] lemma coe_fn_mul (f g : Î± â†’â‚˜[Î¼] Î³) : â‡‘(f * g) =áµ[Î¼] f * g := coe_fn_compâ‚‚ _ _ _ _

@[simp, to_additive] lemma mul_to_germ (f g : Î± â†’â‚˜[Î¼] Î³) :
  (f * g).to_germ = f.to_germ * g.to_germ :=
compâ‚‚_to_germ _ _ _ _

@[to_additive]
instance : monoid (Î± â†’â‚˜[Î¼] Î³) :=
to_germ_injective.monoid to_germ one_to_germ mul_to_germ

end monoid

@[to_additive]
instance comm_monoid [topological_space Î³] [second_countable_topology Î³] [borel_space Î³]
  [comm_monoid Î³] [has_continuous_mul Î³] : comm_monoid (Î± â†’â‚˜[Î¼] Î³) :=
to_germ_injective.comm_monoid to_germ one_to_germ mul_to_germ

section group

variables [topological_space Î³] [borel_space Î³] [group Î³] [topological_group Î³]

@[to_additive] instance : has_inv (Î± â†’â‚˜[Î¼] Î³) := âŸ¨comp has_inv.inv measurable_invâŸ©

@[simp, to_additive] lemma inv_mk (f : Î± â†’ Î³) (hf) : (mk f hf : Î± â†’â‚˜[Î¼] Î³)â»Â¹ = mk fâ»Â¹ hf.inv := rfl

@[to_additive] lemma coe_fn_inv (f : Î± â†’â‚˜[Î¼] Î³) : â‡‘(fâ»Â¹) =áµ[Î¼] fâ»Â¹ := coe_fn_comp _ _ _

@[to_additive] lemma inv_to_germ (f : Î± â†’â‚˜[Î¼] Î³) : (fâ»Â¹).to_germ = f.to_germâ»Â¹ := comp_to_germ _ _ _

variables [second_countable_topology Î³]
@[to_additive]
instance : group (Î± â†’â‚˜[Î¼] Î³) := to_germ_injective.group _ one_to_germ mul_to_germ inv_to_germ

end group

section add_group

variables [topological_space Î³] [borel_space Î³] [add_group Î³] [topological_add_group Î³]
  [second_countable_topology Î³]

@[simp] lemma mk_sub (f g : Î± â†’ Î³) (hf hg) :
  mk (f - g) (ae_measurable.sub hf hg) = (mk f hf : Î± â†’â‚˜[Î¼] Î³) - (mk g hg) :=
by simp [sub_eq_add_neg]

lemma coe_fn_sub (f g : Î± â†’â‚˜[Î¼] Î³) : â‡‘(f - g) =áµ[Î¼] f - g :=
by { simp only [sub_eq_add_neg],
     exact ((coe_fn_add f (-g)).trans $ (coe_fn_neg g).mono $ Î» x hx, congr_arg ((+) (f x)) hx) }

end add_group

@[to_additive]
instance [topological_space Î³] [borel_space Î³] [comm_group Î³] [topological_group Î³]
  [second_countable_topology Î³] : comm_group (Î± â†’â‚˜[Î¼] Î³) :=
{ .. ae_eq_fun.group, .. ae_eq_fun.comm_monoid }

section semimodule

variables {ğ•œ : Type*} [semiring ğ•œ] [topological_space ğ•œ]
variables [topological_space Î³] [borel_space Î³] [add_comm_monoid Î³] [semimodule ğ•œ Î³]
  [topological_semimodule ğ•œ Î³]

instance : has_scalar ğ•œ (Î± â†’â‚˜[Î¼] Î³) :=
âŸ¨Î» c f, comp ((â€¢) c) (measurable_id.const_smul c) fâŸ©

@[simp] lemma smul_mk (c : ğ•œ) (f : Î± â†’ Î³) (hf) :
  c â€¢ (mk f hf : Î± â†’â‚˜[Î¼] Î³) = mk (c â€¢ f) (hf.const_smul _) :=
rfl

lemma coe_fn_smul (c : ğ•œ) (f : Î± â†’â‚˜[Î¼] Î³) : â‡‘(c â€¢ f) =áµ[Î¼] c â€¢ f := coe_fn_comp _ _ _

lemma smul_to_germ (c : ğ•œ) (f : Î± â†’â‚˜[Î¼] Î³) : (c â€¢ f).to_germ = c â€¢ f.to_germ :=
comp_to_germ _ _ _

variables [second_countable_topology Î³] [has_continuous_add Î³]

instance : semimodule ğ•œ (Î± â†’â‚˜[Î¼] Î³) :=
to_germ_injective.semimodule ğ•œ âŸ¨@to_germ Î± Î³ _ Î¼ _, zero_to_germ, add_to_germâŸ© smul_to_germ

end semimodule

/- TODO : Prove that `Lâ°` is a complete space if the codomain is complete. -/

open ennreal

/-- For `f : Î± â†’ ennreal`, define `âˆ« [f]` to be `âˆ« f` -/
def lintegral (f : Î± â†’â‚˜[Î¼] ennreal) : ennreal :=
quotient.lift_on' f (Î»f, âˆ«â» a, (f : Î± â†’ ennreal) a âˆ‚Î¼) (assume f g, lintegral_congr_ae)

@[simp] lemma lintegral_mk (f : Î± â†’ ennreal) (hf) :
  (mk f hf : Î± â†’â‚˜[Î¼] ennreal).lintegral = âˆ«â» a, f a âˆ‚Î¼ := rfl

lemma lintegral_coe_fn (f : Î± â†’â‚˜[Î¼] ennreal) : âˆ«â» a, f a âˆ‚Î¼ = f.lintegral :=
by rw [â† lintegral_mk, mk_coe_fn]

@[simp] lemma lintegral_zero : lintegral (0 : Î± â†’â‚˜[Î¼] ennreal) = 0 := lintegral_zero

@[simp] lemma lintegral_eq_zero_iff {f : Î± â†’â‚˜[Î¼] ennreal} : lintegral f = 0 â†” f = 0 :=
induction_on f $ Î» f hf, (lintegral_eq_zero_iff' hf).trans mk_eq_mk.symm

lemma lintegral_add (f g : Î± â†’â‚˜[Î¼] ennreal) : lintegral (f + g) = lintegral f + lintegral g :=
induction_onâ‚‚ f g $ Î» f hf g hg, by simp [lintegral_add' hf hg]

lemma lintegral_mono {f g : Î± â†’â‚˜[Î¼] ennreal} : f â‰¤ g â†’ lintegral f â‰¤ lintegral g :=
induction_onâ‚‚ f g $ Î» f hf g hg hfg, lintegral_mono_ae hfg

section
variables [emetric_space Î³] [second_countable_topology Î³] [opens_measurable_space Î³]

/-- `comp_edist [f] [g] a` will return `edist (f a) (g a) -/
protected def edist (f g : Î± â†’â‚˜[Î¼] Î³) : Î± â†’â‚˜[Î¼] ennreal := compâ‚‚ edist measurable_edist f g

protected lemma edist_comm (f g : Î± â†’â‚˜[Î¼] Î³) : f.edist g = g.edist f :=
induction_onâ‚‚ f g $ Î» f hf g hg, mk_eq_mk.2 $ eventually_of_forall $ Î» x, edist_comm (f x) (g x)

lemma coe_fn_edist (f g : Î± â†’â‚˜[Î¼] Î³) : â‡‘(f.edist g) =áµ[Î¼] Î» a, edist (f a) (g a) :=
coe_fn_compâ‚‚ _ _ _ _

protected lemma edist_self (f : Î± â†’â‚˜[Î¼] Î³) : f.edist f = 0 :=
induction_on f $ Î» f hf, mk_eq_mk.2 $ eventually_of_forall $ Î» x, edist_self (f x)

/-- Almost everywhere equal functions form an `emetric_space`, with the emetric defined as
  `edist f g = âˆ«â» a, edist (f a) (g a)`. -/
instance : emetric_space (Î± â†’â‚˜[Î¼] Î³) :=
{ edist               := Î»f g, lintegral (f.edist g),
  edist_self          := assume f, lintegral_eq_zero_iff.2 f.edist_self,
  edist_comm          := Î» f g, congr_arg lintegral $ f.edist_comm g,
  edist_triangle      := Î» f g h, induction_onâ‚ƒ f g h $ Î» f hf g hg h hh,
    calc âˆ«â» a, edist (f a) (h a) âˆ‚Î¼ â‰¤ âˆ«â» a, edist (f a) (g a) + edist (g a) (h a) âˆ‚Î¼ :
      measure_theory.lintegral_mono (Î» a, edist_triangle (f a) (g a) (h a))
    ... = âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ + âˆ«â» a, edist (g a) (h a) âˆ‚Î¼ :
      lintegral_add' (hf.edist hg) (hg.edist hh),
  eq_of_edist_eq_zero := Î» f g, induction_onâ‚‚ f g $ Î» f hf g hg H, mk_eq_mk.2 $
    ((lintegral_eq_zero_iff' (hf.edist hg)).1 H).mono $ Î» x, eq_of_edist_eq_zero }

lemma edist_mk_mk {f g : Î± â†’ Î³} (hf hg) :
  edist (mk f hf : Î± â†’â‚˜[Î¼] Î³) (mk g hg) = âˆ«â» x, edist (f x) (g x) âˆ‚Î¼ :=
rfl

lemma edist_eq_coe (f g : Î± â†’â‚˜[Î¼] Î³) : edist f g = âˆ«â» x, edist (f x) (g x) âˆ‚Î¼ :=
by rw [â† edist_mk_mk, mk_coe_fn, mk_coe_fn]

lemma edist_zero_eq_coe [has_zero Î³] (f : Î± â†’â‚˜[Î¼] Î³) : edist f 0 = âˆ«â» x, edist (f x) 0 âˆ‚Î¼ :=
by rw [â† edist_mk_mk, mk_coe_fn, zero_def]

end

section metric
variables [metric_space Î³] [second_countable_topology Î³] [opens_measurable_space Î³]

lemma edist_mk_mk' {f g : Î± â†’ Î³} (hf hg) :
  edist (mk f hf : Î± â†’â‚˜[Î¼] Î³) (mk g hg) = âˆ«â» x, nndist (f x) (g x) âˆ‚Î¼ :=
by simp only [edist_mk_mk, edist_nndist]

lemma edist_eq_coe' (f g : Î± â†’â‚˜[Î¼] Î³) : edist f g = âˆ«â» x, nndist (f x) (g x) âˆ‚Î¼ :=
by simp only [edist_eq_coe, edist_nndist]

end metric

lemma edist_add_right [normed_group Î³] [second_countable_topology Î³] [borel_space Î³]
  (f g h : Î± â†’â‚˜[Î¼] Î³) :
  edist (f + h) (g + h) = edist f g :=
induction_onâ‚ƒ f g h $ Î» f hf g hg h hh, by simp [edist_mk_mk, edist_dist, dist_add_right]

section normed_space

variables {ğ•œ : Type*} [normed_field ğ•œ]
variables [normed_group Î³] [second_countable_topology Î³] [normed_space ğ•œ Î³] [borel_space Î³]

lemma edist_smul (c : ğ•œ) (f : Î± â†’â‚˜[Î¼] Î³) : edist (c â€¢ f) 0 = (ennreal.of_real âˆ¥câˆ¥) * edist f 0 :=
induction_on f $ Î» f hf, by simp [edist_mk_mk, zero_def, smul_mk, edist_dist, norm_smul,
  ennreal.of_real_mul, lintegral_const_mul']

end normed_space

section pos_part

variables [topological_space Î³] [linear_order Î³] [order_closed_topology Î³]
  [second_countable_topology Î³] [has_zero Î³] [opens_measurable_space Î³]

/-- Positive part of an `ae_eq_fun`. -/
def pos_part (f : Î± â†’â‚˜[Î¼] Î³) : Î± â†’â‚˜[Î¼] Î³ :=
comp (Î» x, max x 0) (measurable_id.max measurable_const) f

@[simp] lemma pos_part_mk (f : Î± â†’ Î³) (hf) :
  pos_part (mk f hf : Î± â†’â‚˜[Î¼] Î³) = mk (Î» x, max (f x) 0) (hf.max ae_measurable_const) :=
rfl

lemma coe_fn_pos_part (f : Î± â†’â‚˜[Î¼] Î³) : â‡‘(pos_part f) =áµ[Î¼] (Î» a, max (f a) 0) :=
coe_fn_comp _ _ _

end pos_part

end ae_eq_fun

end measure_theory
