/-
Copyright (c) 2019 Johannes H√∂lzl, Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Zhouhang Zhou
-/
import measure_theory.integration

/-!

# Almost everywhere equal functions

Two measurable functions are treated as identical if they are almost everywhere equal. We form the
set of equivalence classes under the relation of being almost everywhere equal, which is sometimes
known as the `L‚Å∞` space.

See `l1_space.lean` for `L¬π` space.


## Notation

* `Œ± ‚Üí‚Çò Œ≤` is the type of `L‚Å∞` space, where `Œ±` is a measure space and `Œ≤` is a measurable space.
  `f : Œ± ‚Üí‚Çò Œ≤` is a "function" in `L‚Å∞`. In comments, `[f]` is also used to denote an `L‚Å∞` function.

  `‚Çò` can be typed as `\_m`. Sometimes it is shown as a box if font is missing.


## Main statements

* The linear structure of `L‚Å∞` :
    Addition and scalar multiplication are defined on `L‚Å∞` in the natural way, i.e.,
    `[f] + [g] := [f + g]`, `c ‚Ä¢ [f] := [c ‚Ä¢ f]`. So defined, `Œ± ‚Üí‚Çò Œ≤` inherits the linear structure
    of `Œ≤`. For example, if `Œ≤` is a module, then `Œ± ‚Üí‚Çò Œ≤` is a module over the same ring.

    See `mk_add_mk`,  `neg_mk`,     `mk_sub_mk`,  `smul_mk`,
        `add_to_fun`, `neg_to_fun`, `sub_to_fun`, `smul_to_fun`

* The order structure of `L‚Å∞` :
    `‚â§` can be defined in a similar way: `[f] ‚â§ [g]` if `f a ‚â§ g a` for almost all `a` in domain.
    And `Œ± ‚Üí‚Çò Œ≤` inherits the preorder and partial order of `Œ≤`.

    TODO: Define `sup` and `inf` on `L‚Å∞` so that it forms a lattice. It seems that `Œ≤` must be a
    linear order, since otherwise `f ‚äî g` may not be a measurable function.

* Emetric on `L‚Å∞` :
    If `Œ≤` is an `emetric_space`, then `L‚Å∞` can be made into an `emetric_space`, where
    `edist [f] [g]` is defined to be `‚à´‚Åª a, edist (f a) (g a)`.

    The integral used here is `lintegral : (Œ± ‚Üí ennreal) ‚Üí ennreal`, which is defined in the file
    `integration.lean`.

    See `edist_mk_mk` and `edist_to_fun`.


## Implementation notes

* `f.to_fun`     : To find a representative of `f : Œ± ‚Üí‚Çò Œ≤`, use `f.to_fun`.
                 For each operation `op` in `L‚Å∞`, there is a lemma called `op_to_fun`, characterizing,
                 say, `(f op g).to_fun`.
* `ae_eq_fun.mk` : To constructs an `L‚Å∞` function `Œ± ‚Üí‚Çò Œ≤` from a measurable function `f : Œ± ‚Üí Œ≤`,
                 use `ae_eq_fun.mk`
* `comp`         : Use `comp g f` to get `[g ‚àò f]` from `g : Œ≤ ‚Üí Œ≥` and `[f] : Œ± ‚Üí‚Çò Œ≥`
* `comp‚ÇÇ`        : Use `comp‚ÇÇ g f‚ÇÅ f‚ÇÇ to get `[Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)]`.
                 For example, `[f + g]` is `comp‚ÇÇ (+)`


## Tags

function space, almost everywhere equal, `L‚Å∞`, ae_eq_fun

-/

noncomputable theory
open_locale classical

namespace measure_theory
open set filter topological_space

universes u v
variables {Œ± : Type u} {Œ≤ : Type v} [measure_space Œ±]

section measurable_space
variables [measurable_space Œ≤]

variables (Œ± Œ≤)

/-- The equivalence relation of being almost everywhere equal -/
instance ae_eq_fun.setoid : setoid { f : Œ± ‚Üí Œ≤ // measurable f } :=
‚ü®Œªf g, ‚àÄ‚Çò a, f.1 a = g.1 a, Œª f, ae_eq_refl f, Œª f g, ae_eq_symm, Œª f g h, ae_eq_trans‚ü©

/-- The space of equivalence classes of measurable functions, where two measurable functions are
    equivalent if they agree almost everywhere, i.e., they differ on a set of measure `0`.  -/
def ae_eq_fun : Type (max u v) := quotient (ae_eq_fun.setoid Œ± Œ≤)

variables {Œ± Œ≤}

infixr ` ‚Üí‚Çò `:25 := ae_eq_fun

end measurable_space

namespace ae_eq_fun
variables [measurable_space Œ≤]

/-- Construct the equivalence class `[f]` of a measurable function `f`, based on the equivalence
    relation of being almost everywhere equal. -/
def mk (f : Œ± ‚Üí Œ≤) (hf : measurable f) : Œ± ‚Üí‚Çò Œ≤ := quotient.mk ‚ü®f, hf‚ü©

/-- A representative of an `ae_eq_fun` [f] -/
protected def to_fun (f : Œ± ‚Üí‚Çò Œ≤) : Œ± ‚Üí Œ≤ := @quotient.out _ (ae_eq_fun.setoid Œ± Œ≤) f

protected lemma measurable (f : Œ± ‚Üí‚Çò Œ≤) : measurable f.to_fun :=
(@quotient.out _ (ae_eq_fun.setoid Œ± Œ≤) f).2

instance : has_coe (Œ± ‚Üí‚Çò Œ≤) (Œ± ‚Üí Œ≤) := ‚ü®Œªf, f.to_fun‚ü©

@[simp] lemma quot_mk_eq_mk (f : {f : Œ± ‚Üí Œ≤ // measurable f}) : quot.mk setoid.r f = mk f.1 f.2 :=
by cases f; refl

@[simp] lemma mk_eq_mk (f g : Œ± ‚Üí Œ≤) (hf hg) :
  mk f hf = mk g hg ‚Üî (‚àÄ‚Çò a, f a = g a) :=
‚ü®quotient.exact, assume h, quotient.sound h‚ü©

@[ext] lemma ext (f g : Œ± ‚Üí‚Çò Œ≤) (f' g' : Œ± ‚Üí Œ≤) (hf' hg') (hf : mk f' hf' = f)
  (hg : mk g' hg' = g) (h : ‚àÄ‚Çò a, f' a = g' a) : f = g :=
by { rw [‚Üê hf, ‚Üê hg], rw mk_eq_mk, assumption }

lemma self_eq_mk (f : Œ± ‚Üí‚Çò Œ≤) : f = mk (f.to_fun) f.measurable :=
by simp [mk, ae_eq_fun.to_fun]

lemma all_ae_mk_to_fun (f : Œ± ‚Üí Œ≤) (hf) : ‚àÄ‚Çò a, (mk f hf).to_fun a = f a :=
by rw [‚Üê mk_eq_mk _ f _ hf, ‚Üê self_eq_mk (mk f hf)]

/-- Given a measurable function `g : Œ≤ ‚Üí Œ≥`, and an almost everywhere equal function `[f] : Œ± ‚Üí‚Çò Œ≤`,
    return the equivalence class of `g ‚àò f`, i.e., the almost everywhere equal function
    `[g ‚àò f] : Œ± ‚Üí‚Çò Œ≥`. -/
def comp {Œ≥ : Type*} [measurable_space Œ≥] (g : Œ≤ ‚Üí Œ≥) (hg : measurable g) (f : Œ± ‚Üí‚Çò Œ≤) : Œ± ‚Üí‚Çò Œ≥ :=
quotient.lift_on f (Œªf, mk (g ‚àò f.1)  (measurable.comp hg f.2)) $ assume f‚ÇÅ f‚ÇÇ eq,
  by refine quotient.sound _; filter_upwards [eq] assume a, congr_arg g

@[simp] lemma comp_mk {Œ≥ : Type*} [measurable_space Œ≥] (g : Œ≤ ‚Üí Œ≥) (hg : measurable g)
  (f : Œ± ‚Üí Œ≤) (hf) : comp g hg (mk f hf) = mk (g ‚àò f) (measurable.comp hg hf) :=
rfl

lemma comp_eq_mk_to_fun {Œ≥ : Type*} [measurable_space Œ≥] (g : Œ≤ ‚Üí Œ≥) (hg : measurable g) (f : Œ± ‚Üí‚Çò Œ≤) :
  comp g hg f = mk (g ‚àò f.to_fun) (hg.comp f.measurable) :=
by conv_lhs { rw [self_eq_mk f, comp_mk] }

lemma comp_to_fun {Œ≥ : Type*} [measurable_space Œ≥] (g : Œ≤ ‚Üí Œ≥) (hg : measurable g) (f : Œ± ‚Üí‚Çò Œ≤) :
  ‚àÄ‚Çò a, (comp g hg f).to_fun a = (g ‚àò f.to_fun) a :=
by { rw comp_eq_mk_to_fun, apply all_ae_mk_to_fun }

/-- Given a measurable function `g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥`, and almost everywhere equal functions
    `[f‚ÇÅ] : Œ± ‚Üí‚Çò Œ≤` and `[f‚ÇÇ] : Œ± ‚Üí‚Çò Œ≥`, return the equivalence class of the function
    `Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)`, i.e., the almost everywhere equal function
    `[Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)] : Œ± ‚Üí‚Çò Œ≥` -/
def comp‚ÇÇ {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (Œªp:Œ≤√óŒ≥, g p.1 p.2)) (f‚ÇÅ : Œ± ‚Üí‚Çò Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò Œ≥) : Œ± ‚Üí‚Çò Œ¥ :=
begin
  refine quotient.lift_on‚ÇÇ f‚ÇÅ f‚ÇÇ (Œªf‚ÇÅ f‚ÇÇ, mk (Œªa, g (f‚ÇÅ.1 a) (f‚ÇÇ.1 a)) $ _) _,
  { exact measurable.comp hg (measurable.prod_mk f‚ÇÅ.2 f‚ÇÇ.2) },
  { rintros ‚ü®f‚ÇÅ, hf‚ÇÅ‚ü© ‚ü®f‚ÇÇ, hf‚ÇÇ‚ü© ‚ü®g‚ÇÅ, hg‚ÇÅ‚ü© ‚ü®g‚ÇÇ, hg‚ÇÇ‚ü© h‚ÇÅ h‚ÇÇ,
    refine quotient.sound _,
    filter_upwards [h‚ÇÅ, h‚ÇÇ],
    simp {contextual := tt} }
end

@[simp] lemma comp‚ÇÇ_mk_mk {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (Œªp:Œ≤√óŒ≥, g p.1 p.2)) (f‚ÇÅ : Œ± ‚Üí Œ≤) (f‚ÇÇ : Œ± ‚Üí Œ≥) (hf‚ÇÅ hf‚ÇÇ) :
  comp‚ÇÇ g hg (mk f‚ÇÅ hf‚ÇÅ) (mk f‚ÇÇ hf‚ÇÇ) =
    mk (Œªa, g (f‚ÇÅ a) (f‚ÇÇ a)) (measurable.comp hg (measurable.prod_mk hf‚ÇÅ hf‚ÇÇ)) :=
rfl

lemma comp‚ÇÇ_eq_mk_to_fun {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (Œªp:Œ≤√óŒ≥, g p.1 p.2)) (f‚ÇÅ : Œ± ‚Üí‚Çò Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò Œ≥) :
  comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ = mk (Œªa, g (f‚ÇÅ.to_fun a) (f‚ÇÇ.to_fun a))
    (hg.comp (measurable.prod_mk f‚ÇÅ.measurable f‚ÇÇ.measurable)) :=
by conv_lhs { rw [self_eq_mk f‚ÇÅ, self_eq_mk f‚ÇÇ, comp‚ÇÇ_mk_mk] }

lemma comp‚ÇÇ_to_fun {Œ≥ Œ¥ : Type*} [measurable_space Œ≥] [measurable_space Œ¥]
  (g : Œ≤ ‚Üí Œ≥ ‚Üí Œ¥) (hg : measurable (Œªp:Œ≤√óŒ≥, g p.1 p.2)) (f‚ÇÅ : Œ± ‚Üí‚Çò Œ≤) (f‚ÇÇ : Œ± ‚Üí‚Çò Œ≥) :
  ‚àÄ‚Çò a, (comp‚ÇÇ g hg f‚ÇÅ f‚ÇÇ).to_fun a = g (f‚ÇÅ.to_fun a) (f‚ÇÇ.to_fun a) :=
by { rw comp‚ÇÇ_eq_mk_to_fun, apply all_ae_mk_to_fun }

/-- Given a predicate `p` and an equivalence class `[f]`, return true if `p` holds of `f a`
    for almost all `a` -/
def lift_pred (p : Œ≤ ‚Üí Prop) (f : Œ± ‚Üí‚Çò Œ≤) : Prop :=
quotient.lift_on f (Œªf, ‚àÄ‚Çò a, p (f.1 a))
begin
  assume f g h, dsimp, refine propext (eventually_congr _),
  filter_upwards [h], simp {contextual := tt}
end

/-- Given a relation `r` and equivalence class `[f]` and `[g]`, return true if `r` holds of
    `(f a, g a)` for almost all `a` -/
def lift_rel {Œ≥ : Type*} [measurable_space Œ≥] (r : Œ≤ ‚Üí Œ≥ ‚Üí Prop) (f : Œ± ‚Üí‚Çò Œ≤) (g : Œ± ‚Üí‚Çò Œ≥) : Prop :=
lift_pred (Œªp:Œ≤√óŒ≥, r p.1 p.2)
  (comp‚ÇÇ prod.mk (measurable.prod_mk
    (measurable.fst measurable_id) (measurable.snd measurable_id)) f g)

lemma lift_rel_mk_mk {Œ≥ : Type*} [measurable_space Œ≥] (r : Œ≤ ‚Üí Œ≥ ‚Üí Prop)
  (f : Œ± ‚Üí Œ≤) (g : Œ± ‚Üí Œ≥) (hf hg) : lift_rel r (mk f hf) (mk g hg) ‚Üî ‚àÄ‚Çò a, r (f a) (g a) :=
iff.rfl

lemma lift_rel_iff_to_fun {Œ≥ : Type*} [measurable_space Œ≥] (r : Œ≤ ‚Üí Œ≥ ‚Üí Prop) (f : Œ± ‚Üí‚Çò Œ≤)
  (g : Œ± ‚Üí‚Çò Œ≥) : lift_rel r f g ‚Üî ‚àÄ‚Çò a, r (f.to_fun a) (g.to_fun a) :=
by conv_lhs { rw [self_eq_mk f, self_eq_mk g, lift_rel_mk_mk] }

section order

instance [preorder Œ≤] : preorder (Œ± ‚Üí‚Çò Œ≤) :=
{ le          := lift_rel (‚â§),
  le_refl     := by rintros ‚ü®‚ü®f, hf‚ü©‚ü©; exact univ_mem_sets' (assume a, le_refl _),
  le_trans    :=
  begin
    rintros ‚ü®‚ü®f, hf‚ü©‚ü© ‚ü®‚ü®g, hg‚ü©‚ü© ‚ü®‚ü®h, hh‚ü©‚ü© hfg hgh,
    filter_upwards [hfg, hgh] assume a, le_trans
  end }

lemma mk_le_mk [preorder Œ≤] {f g : Œ± ‚Üí Œ≤} (hf hg) : mk f hf ‚â§ mk g hg ‚Üî ‚àÄ‚Çò a, f a ‚â§ g a :=
iff.rfl

lemma le_iff_to_fun_le [preorder Œ≤] {f g : Œ± ‚Üí‚Çò Œ≤} : f ‚â§ g ‚Üî ‚àÄ‚Çò a, f.to_fun a ‚â§ g.to_fun a :=
lift_rel_iff_to_fun _ _ _

instance [partial_order Œ≤] : partial_order (Œ± ‚Üí‚Çò Œ≤) :=
{ le_antisymm :=
  begin
    rintros ‚ü®‚ü®f, hf‚ü©‚ü© ‚ü®‚ü®g, hg‚ü©‚ü© hfg hgf,
    refine quotient.sound _,
    filter_upwards [hfg, hgf] assume a, le_antisymm
  end,
  .. ae_eq_fun.preorder }

/- TODO: Prove `L‚Å∞` space is a lattice if Œ≤ is linear order.
         What if Œ≤ is only a lattice? -/

-- instance [linear_order Œ≤] : semilattice_sup (Œ± ‚Üí‚Çò Œ≤) :=
-- { sup := comp‚ÇÇ (‚äî) (_),
--    .. ae_eq_fun.partial_order }

end order

variable (Œ±)
/-- The equivalence class of a constant function: `[Œªa:Œ±, b]`, based on the equivalence relation of
    being almost everywhere equal -/
def const (b : Œ≤) : Œ± ‚Üí‚Çò Œ≤ := mk (Œªa:Œ±, b) measurable_const

lemma const_to_fun (b : Œ≤) : ‚àÄ‚Çò a, (const Œ± b).to_fun a = b := all_ae_mk_to_fun _ _
variable {Œ±}

instance [inhabited Œ≤] : inhabited (Œ± ‚Üí‚Çò Œ≤) := ‚ü®const _ (default _)‚ü©

instance [has_zero Œ≤] : has_zero (Œ± ‚Üí‚Çò Œ≤) := ‚ü®const Œ± 0‚ü©
lemma zero_def [has_zero Œ≤] : (0 : Œ± ‚Üí‚Çò Œ≤) = mk (Œªa:Œ±, 0) measurable_const := rfl
lemma zero_to_fun [has_zero Œ≤] : ‚àÄ‚Çò a, (0 : Œ± ‚Üí‚Çò Œ≤).to_fun a = 0 := const_to_fun _ _

instance [has_one Œ≤] : has_one (Œ± ‚Üí‚Çò Œ≤) := ‚ü®const Œ± 1‚ü©
lemma one_def [has_one Œ≤] : (1 : Œ± ‚Üí‚Çò Œ≤) = mk (Œªa:Œ±, 1) measurable_const := rfl
lemma one_to_fun [has_one Œ≤] : ‚àÄ‚Çò a, (1 : Œ± ‚Üí‚Çò Œ≤).to_fun a = 1 := const_to_fun _ _

section add_monoid
variables {Œ≥ : Type*}
  [topological_space Œ≥] [second_countable_topology Œ≥] [measurable_space Œ≥] [borel_space Œ≥]
  [add_monoid Œ≥] [topological_add_monoid Œ≥]

instance : has_add (Œ± ‚Üí‚Çò Œ≥) := ‚ü®comp‚ÇÇ (+) measurable_add‚ü©

@[simp] lemma mk_add_mk (f g : Œ± ‚Üí Œ≥) (hf hg) :
   (mk f hf) + (mk g hg) = mk (f + g) (measurable.add hf hg) := rfl

lemma add_to_fun (f g : Œ± ‚Üí‚Çò Œ≥) : ‚àÄ‚Çò a, (f + g).to_fun a = f.to_fun a + g.to_fun a :=
comp‚ÇÇ_to_fun _ _ _ _

instance : add_monoid (Œ± ‚Üí‚Çò Œ≥) :=
{ zero      := 0,
  add       := (+),
  add_zero  := by rintros ‚ü®a‚ü©; exact quotient.sound (ae_of_all _ $ assume a, add_zero _),
  zero_add  := by rintros ‚ü®a‚ü©; exact quotient.sound (ae_of_all _ $ assume a, zero_add _),
  add_assoc :=
    by rintros ‚ü®a‚ü© ‚ü®b‚ü© ‚ü®c‚ü©; exact quotient.sound (ae_of_all _ $ assume a, add_assoc _ _ _) }

end add_monoid

section add_comm_monoid
variables {Œ≥ : Type*}
  [topological_space Œ≥] [second_countable_topology Œ≥] [measurable_space Œ≥] [borel_space Œ≥]
  [add_comm_monoid Œ≥] [topological_add_monoid Œ≥]

instance add_comm_monoid : add_comm_monoid (Œ± ‚Üí‚Çò Œ≥) :=
{ add_comm := by rintros ‚ü®a‚ü© ‚ü®b‚ü©; exact quotient.sound (univ_mem_sets' $ assume a, add_comm _ _),
  .. ae_eq_fun.add_monoid }

end add_comm_monoid

section add_group

variables {Œ≥ : Type*} [topological_space Œ≥] [measurable_space Œ≥] [borel_space Œ≥]
  [add_group Œ≥] [topological_add_group Œ≥]

instance : has_neg (Œ± ‚Üí‚Çò Œ≥) := ‚ü®comp has_neg.neg measurable_id.neg‚ü©

@[simp] lemma neg_mk (f : Œ± ‚Üí Œ≥) (hf) : -(mk f hf) = mk (-f) (measurable.neg hf) := rfl

lemma neg_to_fun (f : Œ± ‚Üí‚Çò Œ≥) : ‚àÄ‚Çò a, (-f).to_fun a = - f.to_fun a := comp_to_fun _ _ _

variables [second_countable_topology Œ≥]
instance : add_group (Œ± ‚Üí‚Çò Œ≥) :=
{ neg          := has_neg.neg,
  add_left_neg := by rintros ‚ü®a‚ü©; exact quotient.sound (ae_of_all _ $ assume a, add_left_neg _),
  .. ae_eq_fun.add_monoid }

@[simp] lemma mk_sub_mk (f g : Œ± ‚Üí Œ≥) (hf hg) :
   (mk f hf) - (mk g hg) = mk (Œªa, (f a) - (g a)) (measurable.sub hf hg) := rfl

lemma sub_to_fun (f g : Œ± ‚Üí‚Çò Œ≥) : ‚àÄ‚Çò a, (f - g).to_fun a = f.to_fun a - g.to_fun a :=
begin
  rw sub_eq_add_neg,
  filter_upwards [add_to_fun f (-g), neg_to_fun g],
  assume a,
  simp only [mem_set_of_eq],
  repeat {assume h, rw h},
  refl
end

end add_group

section add_comm_group

variables {Œ≥ : Type*}
  [topological_space Œ≥] [second_countable_topology Œ≥] [measurable_space Œ≥] [borel_space Œ≥]
  [add_comm_group Œ≥] [topological_add_group Œ≥]

instance : add_comm_group (Œ± ‚Üí‚Çò Œ≥) :=
{ .. ae_eq_fun.add_group, .. ae_eq_fun.add_comm_monoid }

end add_comm_group

section semimodule

variables {ùïú : Type*} [semiring ùïú] [topological_space ùïú]
variables {Œ≥ : Type*} [topological_space Œ≥] [measurable_space Œ≥] [borel_space Œ≥]
          [add_comm_monoid Œ≥] [semimodule ùïú Œ≥] [topological_semimodule ùïú Œ≥]

instance : has_scalar ùïú (Œ± ‚Üí‚Çò Œ≥) :=
‚ü®Œª c f, comp (has_scalar.smul c) (measurable_id.const_smul _) f‚ü©

@[simp] lemma smul_mk (c : ùïú) (f : Œ± ‚Üí Œ≥) (hf) :
  c ‚Ä¢ (mk f hf) = mk (c ‚Ä¢ f) (hf.const_smul _) :=
rfl

lemma smul_to_fun (c : ùïú) (f : Œ± ‚Üí‚Çò Œ≥) : ‚àÄ‚Çò a, (c ‚Ä¢ f).to_fun a = c ‚Ä¢ f.to_fun a :=
comp_to_fun _ _ _

variables [second_countable_topology Œ≥] [topological_add_monoid Œ≥]

instance : semimodule ùïú (Œ± ‚Üí‚Çò Œ≥) :=
{ one_smul  := by { rintros ‚ü®f, hf‚ü©, simp only [quot_mk_eq_mk, smul_mk, one_smul] },
  mul_smul  :=
    by { rintros x y ‚ü®f, hf‚ü©, simp only [quot_mk_eq_mk, smul_mk, mul_action.mul_smul x y f], refl },
  smul_add  :=
  begin
    rintros x ‚ü®f, hf‚ü© ‚ü®g, hg‚ü©, simp only [quot_mk_eq_mk, smul_mk, mk_add_mk],
    congr, exact smul_add x f g
  end,
  smul_zero := by { intro x, simp only [zero_def, smul_mk], congr, exact smul_zero x },
  add_smul  :=
  begin
    intros x y, rintro ‚ü®f, hf‚ü©, simp only [quot_mk_eq_mk, smul_mk, mk_add_mk], congr,
    exact add_smul x y f
  end,
  zero_smul :=
    by { rintro ‚ü®f, hf‚ü©, simp only [quot_mk_eq_mk, smul_mk, zero_def], congr, exact zero_smul ùïú f }}

instance : mul_action ùïú (Œ± ‚Üí‚Çò Œ≥) := by apply_instance

end semimodule

/- TODO : Prove that `L‚Å∞` is a complete space if the codomain is complete. -/
/- TODO : Multiplicative structure of `L‚Å∞` if useful -/

open ennreal

/-- For `f : Œ± ‚Üí ennreal`, Define `‚à´ [f]` to be `‚à´ f` -/
def eintegral (f : Œ± ‚Üí‚Çò ennreal) : ennreal :=
quotient.lift_on f (Œªf, lintegral f.1) (assume ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© eq, lintegral_congr_ae eq)

@[simp] lemma eintegral_mk (f : Œ± ‚Üí ennreal) (hf) : eintegral (mk f hf) = lintegral f := rfl

lemma eintegral_to_fun (f : Œ± ‚Üí‚Çò ennreal) : eintegral f = lintegral (f.to_fun) :=
by conv_lhs { rw [self_eq_mk f, eintegral_mk] }

@[simp] lemma eintegral_zero : eintegral (0 : Œ± ‚Üí‚Çò ennreal) = 0 := lintegral_zero

@[simp] lemma eintegral_eq_zero_iff (f : Œ± ‚Üí‚Çò ennreal) : eintegral f = 0 ‚Üî f = 0 :=
begin
  rcases f with ‚ü®f, hf‚ü©,
  refine iff.trans (lintegral_eq_zero_iff hf) ‚ü®_, _‚ü©,
  { assume h, exact quotient.sound h },
  { assume h, exact quotient.exact h }
end

lemma eintegral_add : ‚àÄ(f g : Œ± ‚Üí‚Çò ennreal), eintegral (f + g) = eintegral f + eintegral g :=
by { rintros ‚ü®f‚ü© ‚ü®g‚ü©, simp only [quot_mk_eq_mk, mk_add_mk, eintegral_mk], exact lintegral_add f.2 g.2 }

lemma eintegral_le_eintegral {f g : Œ± ‚Üí‚Çò ennreal} (h : f ‚â§ g) : eintegral f ‚â§ eintegral g :=
begin
  rcases f with ‚ü®f, hf‚ü©, rcases g with ‚ü®g, hg‚ü©,
  simp only [quot_mk_eq_mk, eintegral_mk, mk_le_mk] at *,
  refine lintegral_le_lintegral_ae _,
  filter_upwards [h], simp
end

section
variables {Œ≥ : Type*} [emetric_space Œ≥] [second_countable_topology Œ≥] [measurable_space Œ≥]
  [opens_measurable_space Œ≥]

/-- `comp_edist [f] [g] a` will return `edist (f a) (g a) -/
def comp_edist (f g : Œ± ‚Üí‚Çò Œ≥) : Œ± ‚Üí‚Çò ennreal := comp‚ÇÇ edist measurable_edist f g

lemma comp_edist_to_fun (f g : Œ± ‚Üí‚Çò Œ≥) :
  ‚àÄ‚Çò a, (comp_edist f g).to_fun a = edist (f.to_fun a) (g.to_fun a) :=
comp‚ÇÇ_to_fun _ _ _ _

lemma comp_edist_self : ‚àÄ (f : Œ± ‚Üí‚Çò Œ≥), comp_edist f f = 0 :=
by rintro ‚ü®f‚ü©; refine quotient.sound _; simp only [edist_self]

/-- Almost everywhere equal functions form an `emetric_space`, with the emetric defined as
  `edist f g = ‚à´‚Åª a, edist (f a) (g a)`. -/
instance : emetric_space (Œ± ‚Üí‚Çò Œ≥) :=
{ edist               := Œªf g, eintegral (comp_edist f g),
  edist_self          := assume f, (eintegral_eq_zero_iff _).2 (comp_edist_self _),
  edist_comm          :=
    by rintros ‚ü®f‚ü© ‚ü®g‚ü©; simp only [comp_edist, quot_mk_eq_mk, comp‚ÇÇ_mk_mk, edist_comm],
  edist_triangle      :=
  begin
    rintros ‚ü®f‚ü© ‚ü®g‚ü© ‚ü®h‚ü©,
    simp only [comp_edist, quot_mk_eq_mk, comp‚ÇÇ_mk_mk, (eintegral_add _ _).symm],
    exact lintegral_mono (assume a, edist_triangle _ _ _)
  end,
  eq_of_edist_eq_zero :=
  begin
    rintros ‚ü®f‚ü© ‚ü®g‚ü©,
    simp only [edist, comp_edist, quot_mk_eq_mk, comp‚ÇÇ_mk_mk, eintegral_eq_zero_iff],
    simp only [zero_def, mk_eq_mk, edist_eq_zero],
    assume h, assumption
  end }

lemma edist_mk_mk {f g : Œ± ‚Üí Œ≥} (hf hg) : edist (mk f hf) (mk g hg) = ‚à´‚Åª x, edist (f x) (g x) := rfl

lemma edist_to_fun (f g : Œ± ‚Üí‚Çò Œ≥) : edist f g = ‚à´‚Åª x, edist (f.to_fun x) (g.to_fun x) :=
by conv_lhs { rw [self_eq_mk f, self_eq_mk g, edist_mk_mk] }

lemma edist_zero_to_fun [has_zero Œ≥] (f : Œ± ‚Üí‚Çò Œ≥) : edist f 0 = ‚à´‚Åª x, edist (f.to_fun x) 0 :=
begin
  rw edist_to_fun,
  apply lintegral_congr_ae,
  have : ‚àÄ‚Çò a:Œ±, (0 : Œ± ‚Üí‚Çò Œ≥).to_fun a = 0 := zero_to_fun,
  filter_upwards [this],
  assume a h,
  simp only [mem_set_of_eq] at *,
  rw h
end

end

section metric
variables {Œ≥ : Type*} [metric_space Œ≥] [second_countable_topology Œ≥] [measurable_space Œ≥]
  [opens_measurable_space Œ≥]

lemma edist_mk_mk' {f g : Œ± ‚Üí Œ≥} (hf hg) :
  edist (mk f hf) (mk g hg) = ‚à´‚Åª x, nndist (f x) (g x) :=
show  (‚à´‚Åª x, edist (f x) (g x)) =  ‚à´‚Åª x, nndist (f x) (g x), from
lintegral_congr_ae $ ae_of_all _ $ assume a, edist_nndist _ _

lemma edist_to_fun' (f g : Œ± ‚Üí‚Çò Œ≥) : edist f g = ‚à´‚Åª x, nndist (f.to_fun x) (g.to_fun x) :=
by conv_lhs { rw [self_eq_mk f, self_eq_mk g, edist_mk_mk'] }

end metric

section normed_group

variables {Œ≥ : Type*} [normed_group Œ≥] [second_countable_topology Œ≥] [measurable_space Œ≥]
  [borel_space Œ≥]

lemma edist_eq_add_add : ‚àÄ {f g h : Œ± ‚Üí‚Çò Œ≥}, edist f g = edist (f + h) (g + h) :=
begin
  rintros ‚ü®f‚ü© ‚ü®g‚ü© ‚ü®h‚ü©,
  simp only [quot_mk_eq_mk, mk_add_mk, edist_mk_mk'],
  apply lintegral_congr_ae,
  filter_upwards [], simp [nndist_eq_nnnorm]
end

end normed_group

section normed_space


variables {ùïú : Type*} [normed_field ùïú]
variables {Œ≥ : Type*} [normed_group Œ≥] [second_countable_topology Œ≥] [normed_space ùïú Œ≥]
  [measurable_space Œ≥] [borel_space Œ≥]

lemma edist_smul (x : ùïú) : ‚àÄ f : Œ± ‚Üí‚Çò Œ≥, edist (x ‚Ä¢ f) 0 = (ennreal.of_real ‚à•x‚à•) * edist f 0 :=
begin
  rintros ‚ü®f, hf‚ü©, simp only [zero_def, edist_mk_mk', quot_mk_eq_mk, smul_mk],
  exact calc
    (‚à´‚Åª (a : Œ±), nndist (x ‚Ä¢ f a) 0) = (‚à´‚Åª (a : Œ±), (nnnorm x) * nnnorm (f a)) :
      lintegral_congr_ae $ by { filter_upwards [], assume a, simp [nndist_eq_nnnorm, nnnorm_smul] }
    ... = _ : lintegral_const_mul _ hf.ennnorm
    ... = _ :
    begin
      convert rfl,
      { rw ‚Üê coe_nnnorm, rw [ennreal.of_real], congr, exact nnreal.of_real_coe },
      { funext, simp [nndist_eq_nnnorm] }
    end,
end

end normed_space

section pos_part

variables {Œ≥ : Type*} [topological_space Œ≥] [decidable_linear_order Œ≥] [order_closed_topology Œ≥]
  [second_countable_topology Œ≥] [has_zero Œ≥] [measurable_space Œ≥] [opens_measurable_space Œ≥]

/-- Positive part of an `ae_eq_fun`. -/
def pos_part (f : Œ± ‚Üí‚Çò Œ≥) : Œ± ‚Üí‚Çò Œ≥ :=
comp‚ÇÇ max (measurable_id.fst.max measurable_id.snd) f 0

lemma pos_part_to_fun (f : Œ± ‚Üí‚Çò Œ≥) : ‚àÄ‚Çò a, (pos_part f).to_fun a = max (f.to_fun a) (0:Œ≥) :=
begin
  filter_upwards [comp‚ÇÇ_to_fun max (measurable_id.fst.max measurable_id.snd) f 0,
    @ae_eq_fun.zero_to_fun Œ± Œ≥],
  simp only [mem_set_of_eq],
  assume a h‚ÇÅ h‚ÇÇ,
  rw [pos_part, h‚ÇÅ, h‚ÇÇ]
end

end pos_part

end ae_eq_fun

end measure_theory
