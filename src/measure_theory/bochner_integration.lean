/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/

import measure_theory.simple_func_dense
import analysis.normed_space.bounded_linear_maps

/-!
# Bochner integral

To be added in the next PR.

## Main definitions

`simple_func.bintegral` :
    bochner integral of simple functions.

`l1.simple_func` :
    subspace of L1 consisting of equivalence classes of an integrable simple function.

`l1.simple_func.integral` :
    bochner integral of `l1.simple_func`; basically the same thing as `simple_func.bintegral`.

`l1.simple_func.integral_clm` :
    `l1.simple_func.integral` as a continuous linear map.

## Main statements

`section bintegral` : basic properties of `simple_func.bintegral` proved.

`section instances` : `l1.simple_func` forms a `normed_space`.

`section coe_to_l1` : `coe` from `l1.simple_func` to `l1` is a uniform and dense embedding.

`section simple_func_integral` : basic properties of `l1.simple_func.integral` proved.
-/

noncomputable theory
open_locale classical

set_option class.instance_max_depth 100

namespace measure_theory
open set lattice filter topological_space ennreal emetric

universes u v w
variables {Î± : Type u} [measure_space Î±]{Î² : Type v} {Î³ : Type w}

local infixr ` â†’â‚› `:25 := simple_func

namespace simple_func

section bintegral
open finset

variables [normed_group Î²] [normed_group Î³]

lemma integrable_iff_integral_lt_top {f : Î± â†’â‚› Î²} :
  integrable f â†” integral (f.map (coe âˆ˜ nnnorm)) < âŠ¤ :=
by { rw [integrable, â† lintegral_eq_integral, lintegral_map] }

lemma fin_vol_supp_of_integrable {f : Î± â†’â‚› Î²} (hf : integrable f) : f.fin_vol_supp :=
begin
  rw [integrable_iff_integral_lt_top] at hf,
  have hf := fin_vol_supp_of_integral_lt_top hf,
  refine fin_vol_supp_of_fin_vol_supp_map f hf _,
  assume b, simp [nnnorm_eq_zero]
end

lemma integrable_of_fin_vol_supp {f : Î± â†’â‚› Î²} (h : f.fin_vol_supp) : integrable f :=
by { rw [integrable_iff_integral_lt_top], exact integral_map_coe_lt_top h nnnorm_zero }

/-- For simple functions with a `normed_group` as codomain, being integrable is the same as having
    finite volume support. -/
lemma integrable_iff_fin_vol_supp (f : Î± â†’â‚› Î²) : integrable f â†” f.fin_vol_supp :=
iff.intro fin_vol_supp_of_integrable integrable_of_fin_vol_supp

lemma integrable_pair {f : Î± â†’â‚› Î²} {g : Î± â†’â‚› Î³} (hf : integrable f) (hg : integrable g) :
  integrable (pair f g) :=
by { rw integrable_iff_fin_vol_supp at *, apply fin_vol_supp_pair; assumption }

variables [normed_space â„ Î³]

/-- Bochner integral of simple functions whose codomain is a real `normed_space`.
    The name `simple_func.integral` has been taken in the file `integration.lean`, which calculates
    the integral of a simple function with type `Î± â†’ ennreal`.
    The name `bintegral` stands for bochner integral. -/
def bintegral [normed_space â„ Î²] (f : Î± â†’â‚› Î²) : Î² :=
f.range.sum (Î» x, (ennreal.to_real (volume (f â»Â¹' {x}))) â€¢ x)

/-- Calculate the integral of `g âˆ˜ f : Î± â†’â‚› Î³`, where `f` is an integrable function from `Î±` to `Î²`
    and `g` is a function from `Î²` to `Î³`. We require `g 0 = 0` so that `g âˆ˜ f` is integrable. -/
lemma map_bintegral (f : Î± â†’â‚› Î²) (g : Î² â†’ Î³) (hf : integrable f) (hg : g 0 = 0) :
  (f.map g).bintegral = f.range.sum (Î» x, (ennreal.to_real (volume (f â»Â¹' {x}))) â€¢ (g x)) :=
begin
  /- Just a complicated calculation with `finset.sum`. Real work is done by
     `map_preimage_singleton`, `simple_func.volume_bUnion_preimage` and `ennreal.to_real_sum`  -/
  rw integrable_iff_fin_vol_supp at hf,
  simp only [bintegral, range_map],
  refine finset.sum_image' _ (assume b hb, _),
  rcases mem_range.1 hb with âŸ¨a, rflâŸ©,
  let s' := f.range.filter (Î»b, g b = g (f a)),
  calc (ennreal.to_real (volume ((f.map g) â»Â¹' {g (f a)}))) â€¢ (g (f a)) =
      (ennreal.to_real (volume (â‹ƒbâˆˆs', f â»Â¹' {b}))) â€¢ (g (f a)) : by rw map_preimage_singleton
  ... = (ennreal.to_real (s'.sum (Î»b, volume (f â»Â¹' {b})))) â€¢ (g (f a)) :
    by rw volume_bUnion_preimage
  ... = (s'.sum (Î»b, ennreal.to_real (volume (f â»Â¹' {b})))) â€¢ (g (f a)) :
  begin
    by_cases h : g (f a) = 0,
    { rw [h, smul_zero, smul_zero] },
    { rw ennreal.to_real_sum,
      simp only [mem_filter],
      rintros b âŸ¨_, hbâŸ©,
      have : b â‰  0, { assume hb', rw [â† hb, hb'] at h, contradiction },
      apply hf,
      assumption }
  end
  ... = s'.sum (Î»b, (ennreal.to_real (volume (f â»Â¹' {b}))) â€¢ (g (f a))) : by rw [finset.smul_sum']
  ... = s'.sum (Î»b, (ennreal.to_real (volume (f â»Â¹' {b}))) â€¢ (g b)) :
    finset.sum_congr rfl $ by { assume x, simp only [mem_filter], rintro âŸ¨_, hâŸ©, rw h }
end

/-- `simple_func.bintegral` and `simple_func.integral` agree when the integrand has type
    `Î± â†’â‚› ennreal`. But since `ennreal` is not a `normed_space`, we need some form of coercion.
    See `bintegral_eq_integral'` for a simpler version. -/
lemma bintegral_eq_integral {f : Î± â†’â‚› Î²} {g : Î² â†’ ennreal} (hf : integrable f) (hg0 : g 0 = 0)
  (hgt : âˆ€b, g b < âŠ¤):
  (f.map (ennreal.to_real âˆ˜ g)).bintegral = ennreal.to_real (f.map g).integral :=
begin
  have hf' : f.fin_vol_supp, { rwa integrable_iff_fin_vol_supp at hf },
  rw [map_bintegral f _ hf, map_integral, ennreal.to_real_sum],
  { refine finset.sum_congr rfl (Î»b hb, _),
    rw [smul_eq_mul],
    rw [to_real_mul_to_real, mul_comm] },
  { assume a ha,
    by_cases a0 : a = 0,
    { rw [a0, hg0, zero_mul], exact with_top.zero_lt_top },
    apply mul_lt_top (hgt a) (hf' _ a0) },
  { simp [hg0] }
end

/-- `simple_func.bintegral` and `lintegral : (Î± â†’ ennreal) â†’ ennreal` are the same when the
    integrand has type `Î± â†’â‚› ennreal`. But since `ennreal` is not a `normed_space`, we need some
    form of coercion.
    See `bintegral_eq_lintegral'` for a simpler version. -/
lemma bintegral_eq_lintegral (f : Î± â†’â‚› Î²) (g : Î² â†’ ennreal) (hf : integrable f) (hg0 : g 0 = 0)
  (hgt : âˆ€b, g b < âŠ¤):
  (f.map (ennreal.to_real âˆ˜ g)).bintegral = ennreal.to_real (âˆ«â» a, g (f a)) :=
by { rw [bintegral_eq_integral hf hg0 hgt, â† lintegral_eq_integral], refl }

variables [normed_space â„ Î²]

lemma bintegral_congr {f g : Î± â†’â‚› Î²} (hf : integrable f) (hg : integrable g) (h : âˆ€â‚˜ a, f a = g a):
  bintegral f = bintegral g :=
show ((pair f g).map prod.fst).bintegral = ((pair f g).map prod.snd).bintegral, from
begin
  have inte := integrable_pair hf hg,
  rw [map_bintegral (pair f g) _ inte prod.fst_zero, map_bintegral (pair f g) _ inte prod.snd_zero],
  refine finset.sum_congr rfl (assume p hp, _),
  rcases mem_range.1 hp with âŸ¨a, rflâŸ©,
  by_cases eq : f a = g a,
  { dsimp only [pair_apply], rw eq },
  { have : volume ((pair f g) â»Â¹' {(f a, g a)}) = 0,
    { refine volume_mono_null (assume a' ha', _) h,
      simp only [set.mem_preimage, mem_singleton_iff, pair_apply, prod.mk.inj_iff] at ha',
      show f a' â‰  g a',
      rwa [ha'.1, ha'.2] },
    simp only [this, pair_apply, zero_smul, ennreal.zero_to_real] },
end

/-- `simple_func.bintegral` and `simple_func.integral` agree when the integrand has type
    `Î± â†’â‚› ennreal`. But since `ennreal` is not a `normed_space`, we need some form of coercion. -/
lemma bintegral_eq_integral' {f : Î± â†’â‚› â„} (hf : integrable f) (h_pos : âˆ€â‚˜ a, 0 â‰¤ f a) :
  f.bintegral = ennreal.to_real (f.map ennreal.of_real).integral :=
begin
  have : âˆ€â‚˜ a, f a = (f.map (ennreal.to_real âˆ˜ ennreal.of_real)) a,
  { filter_upwards [h_pos],
    assume a,
    simp only [mem_set_of_eq, map_apply, function.comp_apply],
    assume h,
    exact (ennreal.to_real_of_real h).symm },
  rw â† bintegral_eq_integral hf,
  { refine bintegral_congr hf _ this, exact integrable_of_ae_eq hf this },
  { exact ennreal.of_real_zero },
  { assume b, rw ennreal.lt_top_iff_ne_top, exact ennreal.of_real_ne_top  }
end

/-- `simple_func.bintegral` and `lintegral : (Î± â†’ ennreal) â†’ ennreal` agree when the integrand has
    type `Î± â†’â‚› ennreal`. But since `ennreal` is not a `normed_space`, we need some form of coercion. -/
lemma bintegral_eq_lintegral' {f : Î± â†’â‚› â„} (hf : integrable f) (h_pos : âˆ€â‚˜ a, 0 â‰¤ f a) :
  f.bintegral = ennreal.to_real (âˆ«â» a, (f.map ennreal.of_real a)) :=
by rw [bintegral_eq_integral' hf h_pos, â† lintegral_eq_integral]

lemma bintegral_add {f g : Î± â†’â‚› Î²} (hf : integrable f) (hg : integrable g) :
  bintegral (f + g) = bintegral f + bintegral g :=
calc bintegral (f + g) = sum (pair f g).range
       (Î»x, ennreal.to_real (volume ((pair f g) â»Â¹' {x})) â€¢ (x.fst + x.snd)) :
begin
  rw [add_eq_mapâ‚‚, map_bintegral (pair f g)],
  { exact integrable_pair hf hg },
  { simp only [add_zero, prod.fst_zero, prod.snd_zero] }
end
... = sum (pair f g).range
        (Î»x, ennreal.to_real (volume ((pair f g) â»Â¹' {x})) â€¢ x.fst +
             ennreal.to_real (volume ((pair f g) â»Â¹' {x})) â€¢ x.snd) :
  finset.sum_congr rfl $ assume a ha, smul_add _ _ _
... = sum (simple_func.range (pair f g))
        (Î» (x : Î² Ã— Î²), ennreal.to_real (volume ((pair f g) â»Â¹' {x})) â€¢ x.fst) +
      sum (simple_func.range (pair f g))
        (Î» (x : Î² Ã— Î²), ennreal.to_real (volume ((pair f g) â»Â¹' {x})) â€¢ x.snd) :
  by rw finset.sum_add_distrib
... = ((pair f g).map prod.fst).bintegral + ((pair f g).map prod.snd).bintegral :
begin
  rw [map_bintegral (pair f g), map_bintegral (pair f g)],
  { exact integrable_pair hf hg }, { refl },
  { exact integrable_pair hf hg }, { refl }
end
... = bintegral f + bintegral g : rfl

lemma bintegral_smul (r : â„) {f : Î± â†’â‚› Î²} (hf : integrable f) :
  bintegral (r â€¢ f) = r â€¢ bintegral f :=
calc bintegral (r â€¢ f) = sum f.range (Î»x, ennreal.to_real (volume (f â»Â¹' {x})) â€¢ r â€¢ x) :
  by rw [smul_eq_map r f, map_bintegral f _ hf (smul_zero _)]
... = (f.range).sum (Î» (x : Î²), ((ennreal.to_real (volume (f â»Â¹' {x}))) * r) â€¢ x) :
  finset.sum_congr rfl $ Î»b hb, by apply smul_smul
... = r â€¢ bintegral f :
begin
  rw [bintegral, smul_sum],
  refine finset.sum_congr rfl (Î»b hb, _),
  rw [smul_smul, mul_comm]
end

lemma norm_bintegral_le_bintegral_norm (f : Î± â†’â‚› Î²) (hf : integrable f) :
  âˆ¥f.bintegralâˆ¥ â‰¤ (f.map norm).bintegral :=
begin
  rw map_bintegral f norm hf norm_zero,
  rw bintegral,
  calc âˆ¥sum f.range (Î»x, ennreal.to_real (volume (f â»Â¹' {x})) â€¢ x)âˆ¥ â‰¤
       sum f.range (Î»x, âˆ¥ennreal.to_real (volume (f â»Â¹' {x})) â€¢ xâˆ¥) :
    norm_triangle_sum _ _
    ... = sum f.range (Î»x, ennreal.to_real (volume (f â»Â¹' {x})) â€¢ âˆ¥xâˆ¥) :
    begin
      refine finset.sum_congr rfl (Î»b hb, _),
      rw [norm_smul, smul_eq_mul, real.norm_eq_abs, abs_of_nonneg to_real_nonneg]
    end
end

end bintegral

end simple_func

namespace l1

open ae_eq_fun

variables [normed_group Î²] [second_countable_topology Î²]
          [normed_group Î³] [second_countable_topology Î³]

variables (Î± Î²)
/-- `l1.simple_func` is a subspace of L1 consisting of equivalence classes of an integrable simple
    function. -/
def simple_func : Type* :=
{ f : Î± â†’â‚ Î² // âˆƒ (s : Î± â†’â‚› Î²),  integrable s âˆ§ ae_eq_fun.mk s s.measurable = f}

variables {Î± Î²}

infixr ` â†’â‚â‚› `:25 := measure_theory.l1.simple_func

namespace simple_func

section instances

instance : has_coe (Î± â†’â‚â‚› Î²) (Î± â†’â‚ Î²) := âŸ¨subtype.valâŸ©

protected lemma eq {f g : Î± â†’â‚â‚› Î²} : (f : Î± â†’â‚ Î²) = (g : Î± â†’â‚ Î²) â†’ f = g := subtype.eq
protected lemma eq' {f g : Î± â†’â‚â‚› Î²} : (f : Î± â†’â‚˜ Î²) = (g : Î± â†’â‚˜ Î²) â†’ f = g := subtype.eq âˆ˜ subtype.eq

@[elim_cast] protected lemma eq_iff {f g : Î± â†’â‚â‚› Î²} : (f : Î± â†’â‚ Î²) = (g : Î± â†’â‚ Î²) â†” f = g :=
iff.intro (subtype.eq) (congr_arg coe)

@[elim_cast] protected lemma eq_iff' {f g : Î± â†’â‚â‚› Î²} : (f : Î± â†’â‚˜ Î²) = (g : Î± â†’â‚˜ Î²) â†” f = g :=
iff.intro (simple_func.eq') (congr_arg _)

@[nolint] protected def emetric_space  : emetric_space (Î± â†’â‚â‚› Î²) := subtype.emetric_space

@[nolint] protected def metric_space : metric_space (Î± â†’â‚â‚› Î²) := subtype.metric_space

local attribute [instance] protected lemma is_add_subgroup : is_add_subgroup
  (Î»f:Î± â†’â‚ Î², âˆƒ (s : Î± â†’â‚› Î²), integrable s âˆ§ ae_eq_fun.mk s s.measurable = f) :=
{ zero_mem := by { use 0, split, { exact integrable_zero }, { refl } },
  add_mem :=
  begin
    rintros f g âŸ¨s, hsi, hsâŸ© âŸ¨t, hti, htâŸ©,
    use s + t, split,
    { exact integrable_add s.measurable t.measurable hsi hti },
    { rw [coe_add, â† hs, â† ht], refl }
  end,
  neg_mem :=
  begin
    rintros f âŸ¨s, hsi, hsâŸ©,
    use -s, split,
    { exact integrable_neg hsi },
    { rw [coe_neg, â† hs], refl }
  end }

@[nolint] protected def add_comm_group : add_comm_group (Î± â†’â‚â‚› Î²) := subtype.add_comm_group

local attribute [instance] simple_func.add_comm_group simple_func.metric_space
  simple_func.emetric_space

@[simp] lemma coe_zero : ((0 : Î± â†’â‚â‚› Î²) : Î± â†’â‚ Î²) = 0 := rfl
@[simp] lemma coe_add (f g : Î± â†’â‚â‚› Î²) : ((f + g : Î± â†’â‚â‚› Î²) : Î± â†’â‚ Î²) = f + g := rfl
@[simp] lemma coe_neg (f : Î± â†’â‚â‚› Î²) : ((-f : Î± â†’â‚â‚› Î²) : Î± â†’â‚ Î²) = -f := rfl
@[simp] lemma coe_sub (f g : Î± â†’â‚â‚› Î²) : ((f - g : Î± â†’â‚â‚› Î²) : Î± â†’â‚ Î²) = f - g := rfl
@[simp] lemma edist_eq (f g : Î± â†’â‚â‚› Î²) : edist f g = edist (f : Î± â†’â‚ Î²) (g : Î± â†’â‚ Î²) := rfl
@[simp] lemma dist_eq (f g : Î± â†’â‚â‚› Î²) : dist f g = dist (f : Î± â†’â‚ Î²) (g : Î± â†’â‚ Î²) := rfl

@[nolint] protected def has_norm : has_norm (Î± â†’â‚â‚› Î²) := âŸ¨Î»f, âˆ¥(f : Î± â†’â‚ Î²)âˆ¥âŸ©

local attribute [instance] simple_func.has_norm

lemma norm_eq (f : Î± â†’â‚â‚› Î²) : âˆ¥fâˆ¥ = âˆ¥(f : Î± â†’â‚ Î²)âˆ¥ := rfl
lemma norm_eq' (f : Î± â†’â‚â‚› Î²) : âˆ¥fâˆ¥ = ennreal.to_real (edist (f : Î± â†’â‚˜ Î²) 0) := rfl

@[nolint] protected def normed_group : normed_group (Î± â†’â‚â‚› Î²) :=
normed_group.of_add_dist (Î» x, rfl) $ by
  { intros, simp only [dist_eq, coe_add, l1.dist_eq, l1.coe_add], rw edist_eq_add_add }

variables {ð•œ : Type*} [normed_field ð•œ] [normed_space ð•œ Î²]

@[nolint] protected def has_scalar : has_scalar ð•œ (Î± â†’â‚â‚› Î²) := âŸ¨Î»k f, âŸ¨k â€¢ f,
begin
  rcases f with âŸ¨f, âŸ¨s, hsi, hsâŸ©âŸ©,
  use k â€¢ s, split,
  { exact integrable_smul _ hsi },
  { rw [coe_smul, subtype.coe_mk, â† hs], refl }
end âŸ©âŸ©

local attribute [instance] simple_func.has_scalar

@[simp] lemma coe_smul (c : ð•œ) (f : Î± â†’â‚â‚› Î²) : ((c â€¢ f : Î± â†’â‚â‚› Î²) : Î± â†’â‚ Î²) = c â€¢ (f : Î± â†’â‚ Î²) := rfl

@[nolint] protected def semimodule : semimodule ð•œ (Î± â†’â‚â‚› Î²) :=
{ one_smul  := Î»f, simple_func.eq (by { simp only [coe_smul], exact one_smul _ _ }),
  mul_smul  := Î»x y f, simple_func.eq (by { simp only [coe_smul], exact mul_smul _ _ _ }),
  smul_add  := Î»x f g, simple_func.eq (by { simp only [coe_smul, coe_add], exact smul_add _ _ _ }),
  smul_zero := Î»x, simple_func.eq (by { simp only [coe_zero, coe_smul], exact smul_zero _ }),
  add_smul  := Î»x y f, simple_func.eq (by { simp only [coe_smul], exact add_smul _ _ _ }),
  zero_smul := Î»f, simple_func.eq (by { simp only [coe_smul], exact zero_smul _ _ }) }

@[nolint] protected def module : module ð•œ (Î± â†’â‚â‚› Î²) :=
{ .. simple_func.semimodule }

@[nolint] protected def vector_space : vector_space ð•œ (Î± â†’â‚â‚› Î²) :=
{ .. simple_func.semimodule }

local attribute [instance] simple_func.vector_space simple_func.normed_group

@[nolint] protected def normed_space : normed_space ð•œ (Î± â†’â‚â‚› Î²) :=
âŸ¨ Î»c f, by { rw [norm_eq, norm_eq, coe_smul, norm_smul] } âŸ©

end instances

local attribute [instance] simple_func.normed_group simple_func.normed_space

section of_simple_func

/-- Construct the equivalence class `[f]` of an integrable simple function `f`. -/
@[reducible] def of_simple_func (f : Î± â†’â‚› Î²) (hf : integrable f) : (Î± â†’â‚â‚› Î²) :=
  âŸ¨l1.of_fun f f.measurable hf, âŸ¨f, âŸ¨hf, rflâŸ©âŸ©âŸ©

lemma of_simple_func_eq_of_fun (f : Î± â†’â‚› Î²) (hf : integrable f) :
  (of_simple_func f hf : Î± â†’â‚ Î²) = l1.of_fun f f.measurable hf := rfl

lemma of_simple_func_eq_mk (f : Î± â†’â‚› Î²) (hf : integrable f) :
  (of_simple_func f hf : Î± â†’â‚˜ Î²) = ae_eq_fun.mk f f.measurable := rfl

lemma of_simple_func_zero : of_simple_func (0 : Î± â†’â‚› Î²) integrable_zero = 0 := rfl

lemma of_simple_func_add (f g : Î± â†’â‚› Î²) (hf hg) :
  of_simple_func (f + g) (integrable_add f.measurable g.measurable hf hg) = of_simple_func f hf +
    of_simple_func g hg := rfl

lemma of_simple_func_neg (f : Î± â†’â‚› Î²) (hf) :
  of_simple_func (-f) (integrable_neg hf) = -of_simple_func f hf := rfl

lemma of_simple_func_sub (f g : Î± â†’â‚› Î²) (hf hg) :
  of_simple_func (f - g) (integrable_sub f.measurable g.measurable hf hg) = of_simple_func f hf -
    of_simple_func g hg := rfl

variables {ð•œ : Type*} [normed_field ð•œ] [normed_space ð•œ Î²]

lemma of_simple_func_smul (f : Î± â†’â‚› Î²) (hf) (c : ð•œ) :
  of_simple_func (c â€¢ f) (integrable_smul _ hf) = c â€¢ of_simple_func f hf := rfl

lemma norm_of_simple_func (f : Î± â†’â‚› Î²) (hf) : âˆ¥of_simple_func f hfâˆ¥ = ennreal.to_real (âˆ«â» a, edist (f a) 0) :=
rfl

end of_simple_func

section to_simple_func

/-- Find a representative of a `l1.simple_func`. -/
def to_simple_func (f : Î± â†’â‚â‚› Î²) : Î± â†’â‚› Î² := classical.some f.2

/-- `f.to_simple_func` is measurable. -/
protected lemma measurable (f : Î± â†’â‚â‚› Î²) : measurable f.to_simple_func := f.to_simple_func.measurable

/-- `f.to_simple_func` is integrable. -/
protected lemma integrable (f : Î± â†’â‚â‚› Î²) : integrable f.to_simple_func :=
let âŸ¨h, _âŸ© := classical.some_spec f.2 in h

lemma of_simple_func_to_simple_func (f : Î± â†’â‚â‚› Î²) :
  of_simple_func (f.to_simple_func) f.integrable = f :=
by { rw â† simple_func.eq_iff', exact (classical.some_spec f.2).2 }

lemma to_simple_func_of_simple_func (f : Î± â†’â‚› Î²) (hfi) :
  âˆ€â‚˜ a, (of_simple_func f hfi).to_simple_func a = f a :=
by { rw â† mk_eq_mk, exact (classical.some_spec (of_simple_func f hfi).2).2 }

lemma to_simple_func_eq_to_fun (f : Î± â†’â‚â‚› Î²) : âˆ€â‚˜ a, (f.to_simple_func) a = (f : Î± â†’â‚ Î²).to_fun a :=
begin
  rw [â† of_fun_eq_of_fun (f.to_simple_func) (f : Î± â†’â‚ Î²).to_fun f.measurable f.integrable
    (f:Î±â†’â‚Î²).measurable (f:Î±â†’â‚Î²).integrable, â† l1.eq_iff],
  simp only [of_fun_eq_mk],
  rcases classical.some_spec f.2 with âŸ¨_, hâŸ©, convert h, rw mk_to_fun, refl
end

variables (Î± Î²)
lemma zero_to_simple_func : âˆ€â‚˜ a, (0 : Î± â†’â‚â‚› Î²).to_simple_func a = 0 :=
begin
  filter_upwards [to_simple_func_eq_to_fun (0 : Î± â†’â‚â‚› Î²), l1.zero_to_fun Î± Î²],
  assume a,
  simp only [mem_set_of_eq],
  assume h,
  rw h,
  assume h,
  exact h
end
variables {Î± Î²}

lemma add_to_simple_func (f g : Î± â†’â‚â‚› Î²) :
  âˆ€â‚˜ a, (f + g).to_simple_func a = f.to_simple_func a + g.to_simple_func a :=
begin
  filter_upwards [to_simple_func_eq_to_fun (f + g), to_simple_func_eq_to_fun f,
    to_simple_func_eq_to_fun g, l1.add_to_fun (f:Î±â†’â‚Î²) g],
  assume a,
  simp only [mem_set_of_eq],
  repeat { assume h, rw h },
  assume h,
  rw â† h,
  refl
end

lemma neg_to_simple_func (f : Î± â†’â‚â‚› Î²) : âˆ€â‚˜ a, (-f).to_simple_func a = - f.to_simple_func a :=
begin
  filter_upwards [to_simple_func_eq_to_fun (-f), to_simple_func_eq_to_fun f, l1.neg_to_fun (f:Î±â†’â‚Î²)],
  assume a,
  simp only [mem_set_of_eq],
  repeat { assume h, rw h },
  assume h,
  rw â† h,
  refl
end

lemma sub_to_simple_func (f g : Î± â†’â‚â‚› Î²) :
  âˆ€â‚˜ a, (f - g).to_simple_func a = f.to_simple_func a - g.to_simple_func a :=
begin
  filter_upwards [to_simple_func_eq_to_fun (f - g), to_simple_func_eq_to_fun f,
    to_simple_func_eq_to_fun g, l1.sub_to_fun (f:Î±â†’â‚Î²) g],
  assume a,
  simp only [mem_set_of_eq],
  repeat { assume h, rw h },
  assume h,
  rw â† h,
  refl
end

variables {ð•œ : Type*} [normed_field ð•œ] [normed_space ð•œ Î²]

lemma smul_to_simple_func (k : ð•œ) (f : Î± â†’â‚â‚› Î²) :
  âˆ€â‚˜ a, (k â€¢ f).to_simple_func a = k â€¢ f.to_simple_func a :=
begin
  filter_upwards [to_simple_func_eq_to_fun (k â€¢ f), to_simple_func_eq_to_fun f,
    l1.smul_to_fun k (f:Î±â†’â‚Î²)],
  assume a,
  simp only [mem_set_of_eq],
  repeat { assume h, rw h },
  assume h,
  rw â† h,
  refl
end

lemma lintegral_edist_to_simple_func_lt_top (f g : Î± â†’â‚â‚› Î²) :
  (âˆ«â» (x : Î±), edist ((to_simple_func f) x) ((to_simple_func g) x)) < âŠ¤ :=
begin
  rw lintegral_rwâ‚‚ (to_simple_func_eq_to_fun f) (to_simple_func_eq_to_fun g),
  exact lintegral_edist_to_fun_lt_top _ _
end

lemma dist_to_simple_func (f g : Î± â†’â‚â‚› Î²) : dist f g =
  ennreal.to_real (âˆ«â» x, edist (f.to_simple_func x) (g.to_simple_func x)) :=
begin
  rw [dist_eq, l1.dist_to_fun, ennreal.to_real_eq_to_real],
  { rw lintegral_rwâ‚‚, repeat { exact all_ae_eq_symm (to_simple_func_eq_to_fun _) } },
  { exact l1.lintegral_edist_to_fun_lt_top _ _ },
  { exact lintegral_edist_to_simple_func_lt_top _ _ }
end

lemma norm_to_simple_func (f : Î± â†’â‚â‚› Î²) :
  âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» (a : Î±), nnnorm ((to_simple_func f) a)) :=
calc âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â»x, edist (f.to_simple_func x) ((0 : Î± â†’â‚â‚› Î²).to_simple_func x)) :
begin
  rw [â† dist_zero_right, dist_to_simple_func]
end
... = ennreal.to_real (âˆ«â» (x : Î±), (coe âˆ˜ nnnorm) (f.to_simple_func x)) :
begin
  rw lintegral_nnnorm_eq_lintegral_edist,
  have : (âˆ«â» (x : Î±), edist ((to_simple_func f) x) ((to_simple_func (0:Î±â†’â‚â‚›Î²)) x)) =
            âˆ«â» (x : Î±), edist ((to_simple_func f) x) 0,
  { apply lintegral_congr_ae, filter_upwards [zero_to_simple_func Î± Î²],
    assume a,
    simp only [mem_set_of_eq],
    assume h,
    rw h },
  rw [ennreal.to_real_eq_to_real],
  { exact this },
  { exact lintegral_edist_to_simple_func_lt_top _ _ },
  { rw â† this, exact lintegral_edist_to_simple_func_lt_top _ _ }
end

lemma norm_eq_bintegral (f : Î± â†’â‚â‚› Î²) : âˆ¥fâˆ¥ = (f.to_simple_func.map norm).bintegral :=
calc âˆ¥fâˆ¥ = ennreal.to_real (âˆ«â» (x : Î±), (coe âˆ˜ nnnorm) (f.to_simple_func x)) :
  by { rw norm_to_simple_func }
... = (f.to_simple_func.map norm).bintegral :
begin
  rw â† f.to_simple_func.bintegral_eq_lintegral (coe âˆ˜ nnnorm) f.integrable,
  { congr },
  { simp only [nnnorm_zero, function.comp_app, ennreal.coe_zero] },
  { assume b, exact coe_lt_top }
end

end to_simple_func

section coe_to_l1

lemma exists_simple_func_near (f : Î± â†’â‚ Î²) {Îµ : â„} (Îµ0 : 0 < Îµ) :
  âˆƒ s : Î± â†’â‚â‚› Î², dist f s < Îµ :=
begin
  rcases f with âŸ¨âŸ¨f, hfmâŸ©, hfiâŸ©,
  simp only [integrable_mk, quot_mk_eq_mk] at hfi,
  rcases simple_func_sequence_tendsto' hfm hfi with âŸ¨F, âŸ¨hâ‚, hâ‚‚âŸ©âŸ©,
  rw ennreal.tendsto_at_top at hâ‚‚,
  rcases hâ‚‚ (ennreal.of_real (Îµ/2)) (of_real_pos.2 $ half_pos Îµ0) with âŸ¨N, hNâŸ©,
  have : (âˆ«â» (x : Î±), nndist (F N x) (f x)) < ennreal.of_real Îµ :=
    calc (âˆ«â» (x : Î±), nndist (F N x) (f x)) â‰¤ 0 + ennreal.of_real (Îµ/2) : (hN N (le_refl _)).2
    ... < ennreal.of_real Îµ :
      by { simp only [zero_add, of_real_lt_of_real_iff Îµ0], exact half_lt_self Îµ0 },
  { refine âŸ¨of_simple_func (F N) (hâ‚ N), _âŸ©, rw dist_comm,
    rw lt_of_real_iff_to_real_lt _ at this,
    { simpa [edist_mk_mk', of_simple_func, l1.of_fun, l1.dist_eq] },
    rw â† lt_top_iff_ne_top, exact lt_trans this (by simp [lt_top_iff_ne_top, of_real_ne_top]) },
  { exact zero_ne_top }
end

protected lemma uniform_continuous : uniform_continuous (coe : (Î± â†’â‚â‚› Î²) â†’ (Î± â†’â‚ Î²)) :=
uniform_continuous_comap

protected lemma uniform_embedding : uniform_embedding (coe : (Î± â†’â‚â‚› Î²) â†’ (Î± â†’â‚ Î²)) :=
uniform_embedding_comap subtype.val_injective

protected lemma uniform_inducing : uniform_inducing (coe : (Î± â†’â‚â‚› Î²) â†’ (Î± â†’â‚ Î²)) :=
l1.simple_func.uniform_embedding.to_uniform_inducing

protected lemma dense_embedding : dense_embedding (coe : (Î± â†’â‚â‚› Î²) â†’ (Î± â†’â‚ Î²)) :=
l1.simple_func.uniform_embedding.dense_embedding $
Î» f, mem_closure_iff_nhds.2 $ Î» t ht,
let âŸ¨Îµ,Îµ0, hÎµâŸ© := metric.mem_nhds_iff.1 ht in
let âŸ¨s, hâŸ© := exists_simple_func_near f Îµ0 in
ne_empty_iff_exists_mem.2 âŸ¨_, hÎµ (metric.mem_ball'.2 h), s, rflâŸ©

protected lemma dense_inducing : dense_inducing (coe : (Î± â†’â‚â‚› Î²) â†’ (Î± â†’â‚ Î²)) :=
l1.simple_func.dense_embedding.to_dense_inducing

protected lemma closure_range : closure (range (coe : (Î± â†’â‚â‚› Î²) â†’ (Î± â†’â‚ Î²))) = univ :=
l1.simple_func.dense_embedding.to_dense_inducing.closure_range

variables (ð•œ : Type*) [normed_field ð•œ] [normed_space ð•œ Î²]

variables (Î± Î²)

/-- The uniform and dense embedding of L1 simple functions into L1 functions. -/
def coe_to_l1 : (Î± â†’â‚â‚› Î²) â†’L[ð•œ] (Î± â†’â‚ Î²) :=
{ to_fun := (coe : (Î± â†’â‚â‚› Î²) â†’ (Î± â†’â‚ Î²)),
  add := Î»f g, rfl,
  smul := Î»k f, rfl,
  cont := l1.simple_func.uniform_continuous.continuous, }

variables {Î± Î² ð•œ}

end coe_to_l1

section simple_func_integral

variables [normed_space â„ Î²]

/-- Bochner integration over simple functions in l1 space. -/
def integral (f : Î± â†’â‚â‚› Î²) : Î² := simple_func.bintegral (f.to_simple_func)

lemma integral_eq_lintegral {f : Î± â†’â‚â‚› â„} (h_pos : âˆ€â‚˜ a, 0 â‰¤ f.to_simple_func a) :
  integral f = ennreal.to_real (âˆ«â» a, ennreal.of_real (f.to_simple_func a)) :=
by { rw [integral, simple_func.bintegral_eq_lintegral' f.integrable h_pos], refl }

lemma integral_congr (f g : Î± â†’â‚â‚› Î²) (h : âˆ€â‚˜ a, f.to_simple_func a = g.to_simple_func a) :
  integral f = integral g :=
by { simp only [integral], apply simple_func.bintegral_congr f.integrable g.integrable, exact h }

lemma integral_add (f g : Î± â†’â‚â‚› Î²) : integral (f + g) = integral f + integral g :=
begin
  simp only [integral],
  rw â† simple_func.bintegral_add f.integrable g.integrable,
  apply simple_func.bintegral_congr (f + g).integrable,
    { exact integrable_add f.measurable g.measurable f.integrable g.integrable },
    { apply add_to_simple_func },
end

lemma integral_smul (r : â„) (f : Î± â†’â‚â‚› Î²) : integral (r â€¢ f) = r â€¢ integral f :=
begin
  simp only [integral],
  rw â† simple_func.bintegral_smul _ f.integrable,
  apply simple_func.bintegral_congr (r â€¢ f).integrable,
    { exact integrable_smul _ f.integrable },
    { apply smul_to_simple_func }
end

lemma norm_integral_le_norm (f : Î± â†’â‚â‚› Î²) : âˆ¥ integral f âˆ¥ â‰¤ âˆ¥fâˆ¥ :=
begin
  rw [integral, norm_eq_bintegral],
  exact f.to_simple_func.norm_bintegral_le_bintegral_norm f.integrable
end

/-- Bochner integration over simple functions in l1 space as a continuous linear map. -/
def integral_clm : (Î± â†’â‚â‚› Î²) â†’L[â„] Î² :=
linear_map.with_bound âŸ¨integral, integral_add, integral_smulâŸ©
  âŸ¨1, (Î»f, le_trans (norm_integral_le_norm _) $ by rw one_mul)âŸ©

local notation `Integral` := @integral_clm Î± _ Î² _ _ _

open continuous_linear_map

lemma norm_Integral_le_one : âˆ¥Integralâˆ¥ â‰¤ 1 :=
begin
  apply op_norm_le_bound,
  { exact zero_le_one },
  assume f,
  rw [one_mul],
  exact norm_integral_le_norm _
end

end simple_func_integral

end simple_func

end l1

end measure_theory
