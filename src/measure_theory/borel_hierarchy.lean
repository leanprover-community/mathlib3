/-
Copyright (c) 2022 Pedro S√°nchez Terraf. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Pedro S√°nchez Terraf
-/
import set_theory.cardinal.continuum
import measure_theory.measurable_space_def
import set_theory.cardinal.cofinality
import tactic.induction

/-!
# The Borel hierarchy

In this file we define recursively define the classical Borel hierarchy of sets.
It is developed as a purely set-theoretic definition here.

## Mathematical overview

For a second countable topological space $X$, the Borel hierarchy is defined
recursively as follows (viz., Sect. 11.B in [kechris1995]):
$$
\begin{align}
  \mathbf{\Sigma}^0_1(X)     &= \{U \subseteq X: U \text { is open }\} \\
  \mathbf{\Pi}^0_{\xi}(X)    &= \{ X \setminus Q : Q \in \mathbf{\Sigma}^0_{\xi}(X)\} \\
  \mathbf{\Sigma}^0_{\xi}(X) &= \left\{\textstyle\bigcup_n A_n: A_n \in
    \mathbf{\Pi}^0_{\xi_n}(X), \xi_n<\xi, n \in \mathbb{N}\right\}, \text { if } \xi>1,
\end{align}
$$
where $\xi$ and $\xi_n$ are ordinals between $1$ and $\omega_1$.

This is a streamlined version of the notation for classes of *definable* subsets
of the space $X$ (aka, **pointclasses**): $\mathbf{\Pi}^0_1(X)$ are the closed sets; $\mathbf{\Sigma}^0_2(X)$
are the $F_\sigma$ sets; $\mathbf{\Pi}^0_2(X)$, the $G_\delta$; and so forth.
This is specially useful for infinite indices $\xi$.

In this file, we provide an inductive definition of the above hierarchy of subsets
of an arbitrary type, without assuming a topology but using an extra paramater `s`
which is intended to be a countable base of a topology.

## Main definitions

- `sigma0_pi0_rec`: Recursive definition of the hierarchy, not intended for direct use.
- `sigma0`, `pi0` : The classical pointclasses, obtained from `sigma0_pi0_rec`
using its `bool` argument.

## Implementation notes

Traditionally, pointclasses $\Sigma^0_\alpha$ and $\Pi^0_\alpha$ are defined for positive
values of the ordinal $\alpha$. Here the definition is extended in such a way that
$\Sigma^0_0 = \emptyset$ (which results in more generality for some lemmas) and
$\Pi^0_0$ coincides with the set of generators.

The `inductive` definition of `sigma0_pi0_rec` was suggested by Junyan Xu.

## References

The definition of the hierarchy was extracted from [kechris1995].
-/

universe u

namespace pointclasses

section sigma0_pi0

/-!
### Œ£ and Œ† pointclasses

This section includes basic definitions and API.
-/
open set

variables {Œ± : Type u} (s : set (set Œ±)) (i k : ordinal.{u})

/--
Simultaneous recursive definition of Œ£‚Å∞·µ¢ and Œ†‚Å∞·µ¢ pointclasses by recursion
on ordinals (a variant of 11.B in Kechris, [Classical Descriptive Set Theory][kechris1995]).

The definition is encoded as a single inductive predicate, where the `bool` argument
determines if we are defining Œ£‚Å∞·µ¢ (for `ff`) or Œ†‚Å∞·µ¢ (for `tt`). The parameter
`s : set (set Œ±)` is the generating family.

The main difference is that the hierarchy starts at level 0: Œ†‚Å∞‚ÇÄ coincides with `s`
augmented with `‚àÖ` and `univ` (intended to be a countable basis of a topology) and
Œ£‚Å∞‚ÇÄ is the empty family.
-/
inductive sigma0_pi0_rec {Œ± : Type u} (s : set (set Œ±)) :
  ordinal.{u} ‚Üí bool ‚Üí set Œ± ‚Üí Prop
| basic (x ‚àà s) : sigma0_pi0_rec 0 tt x
| empty         : sigma0_pi0_rec 0 tt ‚àÖ
| univ          : sigma0_pi0_rec 0 tt univ
| compl {i x}   : sigma0_pi0_rec i ff x ‚Üí sigma0_pi0_rec i tt x·∂ú
| union {i} (f : ‚Ñï ‚Üí set Œ±) (g : ‚Ñï ‚Üí ordinal.{u}) :
    (‚àÄ n, g n < i) ‚Üí (‚àÄ n, sigma0_pi0_rec (g n) tt (f n)) ‚Üí sigma0_pi0_rec i ff (‚ãÉ n, f n)

/--
The family of (boldface) Œ£‚Å∞·µ¢ pointsets, which are countable unions of Œ†‚Å∞‚±º sets
(given by the function `pointclasses.pi0` below) of smaller index. The parameter
`s : set (set Œ±)` is the family of sets from which the generation begins.
-/
def sigma0 : set (set Œ±) := sigma0_pi0_rec s i ff

/--
The family of (boldface) Œ†‚Å∞·µ¢ pointsets, which are the complements of Œ†‚Å∞·µ¢ sets
(given by the function `pointclasses.sigma0` above). The parameter
`s : set (set Œ±)` is the family of sets from which the generation begins.

When the ordinal argument is `0`, `pi0` returns the generating family `s`.
-/
def pi0 : set (set Œ±) := sigma0_pi0_rec s i tt

lemma sigma0_pi0_rec_def' {b : bool} :
  sigma0_pi0_rec s i b = if b then pi0 s i else sigma0 s i :=
by { unfold pi0 sigma0, cases b; refl }

@[simp] lemma sigma0_zero : sigma0 s 0 = ‚àÖ :=
begin
  unfold sigma0,
  ext x, rw [mem_empty_iff_false, iff_false],
  intro hx,
  induction' hx with _ _ _ _ _ _ _ f g glt hf IH,
  exact ordinal.not_lt_zero (g 0) (glt 0)
 end

lemma sigma0_eq_Union_pi0:
  sigma0 s i = set.range (Œª (f : ‚Ñï ‚Üí ‚ãÉ j (hij : j < i), pi0 s j), ‚ãÉ n, (f n).1) :=
begin
  rcases classical.em (i=0) with rfl | hi; unfold sigma0, rw sigma0_pi0_rec_def',
  { apply eq.symm, simp [range_eq_empty, ordinal.not_lt_zero] },
  { ext x, split; intro hx,
    { induction' hx with _ _ _ _ _ _ i f g glt hf,
      existsi Œªn, (‚ü®f n, _‚ü© : ‚Ü•‚ãÉ j < i, pi0 s j),
      { simp only [eq_self_iff_true] },
      { rw mem_Union,
      use g n,
      rw mem_Union,
      exact ‚ü®glt n, hf n‚ü© } },
    { cases hx with f hf,
      rw ‚Üê hf,
      dsimp only at hf,
      choose g hg using Œªn, (mem_Union.mp (f n).property),
      simp only [mem_Union,exists_prop] at hg,
      apply sigma0_pi0_rec.union _ g,
      exact Œª n, (hg n).1,
      unfold pi0 at hg,
      exact Œª n, (hg n).2 } }
end

lemma pi0_subset_sigma0 (hik : i < k) :
  pi0 s i ‚äÜ sigma0 s k :=
begin
  simp only [sigma0_eq_Union_pi0,hik],
  intros x hx,
  apply mem_range.mpr,
  have hxU : x ‚àà ‚ãÉ j < k, pi0 s j,
  { simp only [mem_Union,exists_prop],
    use i,
    exact ‚ü®hik,hx‚ü© },
  existsi (Œªn : ‚Ñï, (‚ü®x,hxU‚ü© : ‚ãÉ (j < k), pi0 s j)),
  exact Union_const x
end

lemma pi0_eq_compl_sigma0 (hi : ¬¨i = 0):
  pi0 s i = compl '' sigma0 s i :=
begin
  unfold sigma0 pi0,
  ext x, split; intro hx; induction' hx with hcomp IH c d IH,
  any_goals { contradiction },
  { simp only [mem_image], use d, tauto },
  { have := sigma0_pi0_rec.compl IH.1, rwa ‚Üê IH.2 }
end

lemma pi0_zero :
  pi0 s 0 = s ‚à™ {‚àÖ,univ} :=
begin
  unfold pi0, ext,
  simp only [mem_insert_iff,union_insert,union_singleton],
  split; intro hx,
  { induction' hx with _ hx _ v hv,
    any_goals { tauto },
    simp only [sigma0_pi0_rec_def'] at hv,
    have : sigma0 s 0 v,
    { exact hv },
    exfalso,
    rw sigma0_zero at this,
    exact this },
  { rcases hx with rfl | rfl | x_in_s,
    exacts [sigma0_pi0_rec.empty, sigma0_pi0_rec.univ, sigma0_pi0_rec.basic x x_in_s] }
end

lemma sigma0_one :
  sigma0 s 1 = set.range (Œª (f : ‚Ñï ‚Üí s ‚à™ {‚àÖ,univ}), ‚ãÉ n, (f n).1) :=
begin
  unfold sigma0,
  change sigma0_pi0_rec s 1 ff = range (Œª (f : ‚Ñï ‚Üí ‚Ü•(s ‚à™ {‚àÖ, univ})), ‚ãÉ (n : ‚Ñï), ‚Üë(f n)),
  ext z,
  change sigma0_pi0_rec s 1 ff z ‚Üî ‚àÉ (y : ‚Ñï ‚Üí ‚Ü•(s ‚à™ {‚àÖ, univ})), (‚ãÉ (n : ‚Ñï), ‚Üë(y n)) = z,
  refine ‚ü®Œª h, _, Œª h, _‚ü©,
  { -- Subgoal solved by Junyan Xu.
    induction' h with _ _ _ _ _ _ _ f g glt1 IH f_Union,
    simp_rw ordinal.lt_one_iff_zero at glt1,
    simp_rw glt1 at IH,
    change ‚àÄ n, f n ‚àà pi0 s 0 at IH,
    simp_rw pi0_zero at IH,
    use Œª n, ‚ü®f n, IH n‚ü©,
    exact rfl },
  { rcases h with ‚ü®f,rfl‚ü©,
    apply sigma0_pi0_rec.union (Œª n, (f n).val) (Œª n, 0),
    { simp },
    { change ‚àÄ (n : ‚Ñï), sigma0_pi0_rec s 0 tt ‚Üë(f n),
      intro n,
      have hfn := (f n).property,
      simp only [subtype.val_eq_coe,mem_insert_iff,union_insert,union_singleton] at hfn,
      rcases hfn with nul | uni | bas,
      { rw nul, exact sigma0_pi0_rec.empty },
      { rw uni, exact sigma0_pi0_rec.univ },
      { exact sigma0_pi0_rec.basic (f n) bas } } }
end

lemma sigma0_subset_sigma0 (hik : i ‚â§ k) :
  sigma0 s i ‚äÜ sigma0 s k :=
begin
  rw le_iff_lt_or_eq at hik,
  cases hik,
  -- Take care of the trivial case `i = k` first,
  swap,
  { simp only [hik, subset_rfl] },
  -- Now the interesting `i < k`:
  repeat { rw sigma0_eq_Union_pi0 },
  intros x hx,
  cases hx with f hf,
  simp only at hf,
  have hfUn : ‚àÄ (n : ‚Ñï), ‚Üë(f n) ‚àà (‚ãÉ j < k, pi0 s j),
  { intro n,
    apply mem_Union.mpr,
    -- Awful proof ahead :(
    have : ‚àÉ j (hjk : j < k), (f n).val ‚àà pi0 s j :=
      (let (Exists.intro j q) := (mem_Union.mp (f n).property) in
        let (Exists.intro l r) := mem_Union.mp q in
        Exists.intro j (Exists.intro (trans l hik) r)),
    cases this with j hj,
    use j,
    exact mem_Union.mpr hj },
  apply mem_range.mpr,
  existsi (Œªn : ‚Ñï, (‚ü®f n, hfUn n‚ü© : ‚ãÉ j < k, (pi0 s j))),
  tauto,
end

/--
The sequence of `pi0` families is nondecreasing.

The hypothesis `¬¨i = 0` is required in case elements of the generating set are
not closed. If the underlying space is zero-dimensional, one can take a basis
of clopen sets and the inclusion will hold unconditionally.
-/
lemma pi0_subset_pi0 (hi : ¬¨i = 0) (hik : i ‚â§ k) :
  pi0 s i ‚äÜ pi0 s k :=
begin
  rw [pi0_eq_compl_sigma0,pi0_eq_compl_sigma0],
  exacts [image_subset _ (sigma0_subset_sigma0 s i k hik),
    ordinal.one_le_iff_ne_zero.mp (trans (ordinal.one_le_iff_ne_zero.mpr hi) hik),
    hi]
end

lemma Union_of_sigma0_sequence {g : ‚Ñï ‚Üí sigma0 s i} :
  (‚ãÉ n, (g n).val) ‚àà sigma0 s i :=
begin
  have hg : ‚àÄ n : ‚Ñï, (g n).val ‚àà sigma0 s i := Œª n, (g n).property,
  simp only [subtype.val_eq_coe,sigma0_eq_Union_pi0] at *,
  choose o ho using hg,
  have : ‚Ñï √ó ‚Ñï ‚âÉ ‚Ñï,
  { exact denumerable.eqv (‚Ñï √ó ‚Ñï) },
  cases this with tup untup htup huntup,
  use Œª n, let p := (untup n) in o p.1 p.2,
  ext x, split; intro hx; simp only [mem_Union] at hx ‚ä¢;
  cases hx with j hxin,
  { let n := (untup j).fst,
    use n,
    specialize ho n,
    rw [‚Üê ho, mem_Union],
    use (untup j).snd,
    assumption },
  { simp only [mem_Union,‚Üê ho j] at hxin,
    cases hxin with k hk,
    existsi tup ‚ü®j,k‚ü©,
    have fstj : (untup (tup ‚ü®j,k‚ü©)).fst = j,
    { exact (congr_arg prod.fst (htup (j, k))).trans rfl },
    have sndk : (untup (tup ‚ü®j,k‚ü©)).snd = k,
    { exact (congr_arg prod.snd (htup (j, k))).trans rfl },
    rw [fstj,sndk],
    exact hk }
end

lemma Union_of_mem_sigma0 {f : ‚Ñï ‚Üí set Œ±} (hf : ‚àÄ n, f n ‚àà sigma0 s i):
  (‚ãÉ n, f n) ‚àà sigma0 s i :=
by exact @Union_of_sigma0_sequence _ s i (Œªn, {val := f n, property := hf n} : ‚Ñï ‚Üí sigma0 s i)

lemma self_subset_sigma0 (hi : ¬¨i = 0) :
  s ‚äÜ sigma0 s i :=
begin
  calc
  s   ‚äÜ s ‚à™ {‚àÖ,univ} : subset_union_left _ _
  ... = pi0 s 0      : eq.symm (pi0_zero s)
  ... ‚äÜ sigma0 s i   : pi0_subset_sigma0 s 0 i (ordinal.pos_iff_ne_zero.mpr hi),
end

theorem empty_mem_sigma0 (hi : ¬¨i = 0) :
  ‚àÖ ‚àà sigma0 s i :=
begin
  have : ‚àÖ ‚àà s ‚à™ {‚àÖ,univ} := by { apply mem_union_right, simp },
  have that : s ‚à™ {‚àÖ,univ} = pi0 s 0 := eq.symm (pi0_zero s),
  rw that at this,
  calc
  ‚àÖ   ‚àà pi0 s 0      : this
  ... ‚äÜ sigma0 s i   : pi0_subset_sigma0 s 0 i (ordinal.pos_iff_ne_zero.mpr hi)
end

end sigma0_pi0

section gen_measurable
/-!
### Generated œÉ-algebra by recursion
-/
variables {Œ± : Type u} (s : set (set Œ±)) (i k : ordinal.{u})

open set ordinal cardinal
open_locale ordinal

/--
The œÉ-algebra recursively generated by the family `s`. It is obtained at the
`œâ‚ÇÅ`th level of the `sigma0` hierarchy.
-/
def gen_measurable := sigma0 s œâ‚ÇÅ

lemma gen_measurable_eq_Union_sigma0 :
  gen_measurable s = ‚ãÉ (j < œâ‚ÇÅ), sigma0 s j :=
begin
  unfold gen_measurable,
  apply subset_antisymm,
  { rw sigma0_eq_Union_pi0,
    intros x hx,
    simp only [mem_Union,exists_prop] at *,
    cases hx with f hf,
    let g := Œª n, (f n).property,
    simp only [mem_Union,exists_prop] at g,
    choose o ho using g,
    use order.succ(sup o),
    split,
    { exact is_limit.succ_lt is_limit_omega_1 (sup_sequence_lt_omega_1 o (Œª n, (ho n).left)) },
    rw sigma0_eq_Union_pi0,
    rw mem_range,
    have typf : ‚àÄ n, ‚Üë(f n) ‚àà ‚ãÉ (j < order.succ (sup o)), pi0 s j,
    { intro n, apply mem_Union.2,
      specialize ho n,
      use o n,
      exact mem_Union.2 ‚ü®lt_of_le_of_lt (le_sup o n) (order.lt_succ (sup o)), ho.2‚ü© },
    use Œª n, (‚ü®f n, typf n‚ü© : ‚ãÉ (j < order.succ (sup o)), pi0 s j),
    tauto },
  { simp only [Union_subset_iff],
    exact Œª _ hi, sigma0_subset_sigma0 s _ _ (le_of_lt hi) }
end

theorem compl_mem_gen_measurable (t : set Œ±) (ht : t ‚àà gen_measurable s) :
  t·∂ú ‚àà gen_measurable s :=
begin
  rw gen_measurable_eq_Union_sigma0 at ht,
  simp only [mem_Union,exists_prop] at ht,
  cases ht with o ho,
  rcases classical.em (o=0) with rfl | onon,
  { finish },
  calc
  t·∂ú  ‚àà pi0 s o          : by { rw pi0_eq_compl_sigma0,
    simp only [mem_image,compl_inj_iff,exists_eq_right], exacts [ho.2,onon] }
  ... ‚äÜ gen_measurable s : pi0_subset_sigma0 s o œâ‚ÇÅ ho.1,
end

theorem Union_mem_gen_measurable {f : ‚Ñï ‚Üí set Œ±} (hf : ‚àÄ n, f n ‚àà gen_measurable s) :
  (‚ãÉ n, f n) ‚àà gen_measurable s:=
by { unfold gen_measurable at *, exact Union_of_mem_sigma0 s œâ‚ÇÅ hf }

open measurable_space

lemma generate_measurable_of_mem_sigma0 (t) (ht : t ‚àà sigma0 s i) :
  generate_measurable s t :=
begin
  induction i using ordinal.induction with i IH generalizing t,
  rw [sigma0_eq_Union_pi0,mem_range] at ht,
  rcases ht with ‚ü®f,hf‚ü©,
  have typf : ‚àÄ n : ‚Ñï, generate_measurable s (f n),
  { intro n,
    have fn_in : (f n).val ‚àà ‚ãÉ (j : ordinal) (hij : j < i), pi0 s j := (f n).property,
    simp only [subtype.val_eq_coe,mem_Union,exists_prop] at fn_in,
    rcases fn_in with ‚ü®o,‚ü®o_lt_i,fn_in‚ü©‚ü©,
    -- Case `(f n).val ‚àà pi0 s 0`.
    rcases classical.em (o=0) with rfl | honz,
    { rw pi0_zero at fn_in,
      rcases fn_in with  fn_in | fn_emp | fn_in,
      { exact generate_measurable.basic _ fn_in },
      { rw fn_emp, exact generate_measurable.empty },
      { rw mem_singleton_iff at fn_in, rw [fn_in,‚Üêcompl_empty],
        exact generate_measurable.compl _ generate_measurable.empty } },
    -- Case `(f n).val ‚àà pi0 s o` with `o ‚â† 0`.
    simp only at IH,
    rw pi0_eq_compl_sigma0 s o honz at fn_in,
    rw ‚Üê compl_compl ‚Üë(f n),
    apply generate_measurable.compl,
    cases fn_in with x hx,
    rw [‚Üêhx.2, compl_compl],
    exact IH o o_lt_i x hx.1 },
  rw ‚Üê hf,
  exact generate_measurable.union (Œªn, (f n).val) typf
end

theorem generate_measurable_eq_gen_measurable :
  {t | generate_measurable s t} = gen_measurable s :=
begin
  ext t, refine ‚ü®Œª ht, _, Œª ht, _‚ü©,
  { have om1_nonz : œâ‚ÇÅ ‚â† 0,
    { unfold omega_1, exact ne_zero_of_out_nonempty _,},
    induction ht with u hu u hu IH f hf IH,
    exacts
    [ self_subset_sigma0 s œâ‚ÇÅ om1_nonz hu,
      empty_mem_sigma0 s œâ‚ÇÅ om1_nonz,
      compl_mem_gen_measurable s u IH,
      Union_mem_gen_measurable s IH ] },
  { exact generate_measurable_of_mem_sigma0 s œâ‚ÇÅ t ht }
end

end gen_measurable

section card_gen_measurable

/-!
### Cardinality of sigma-algebras
-/

variables {Œ± : Type u} (s : set (set Œ±)) (i k : ordinal.{u})

open set measurable_space cardinal ordinal
open_locale cardinal ordinal

/- The result holds for arbitrary `i`, but it is easier to prove
this way -/
lemma cardinal_sigma0_le (hi : i ‚â§ œâ‚ÇÅ):
  #(sigma0 s i) ‚â§ (max (#s) 2) ^ aleph_0.{u} :=
begin
  induction i using ordinal.induction with i IH,
  have Upi0sub : (‚ãÉ j < i, pi0 s j) ‚äÜ s ‚à™ {‚àÖ, univ} ‚à™ ‚ãÉ j < i, compl '' sigma0 s j,
  { simp only [mem_singleton_iff,union_insert,union_singleton,mem_insert_iff,Union_subset_iff],
    intros j hj x hx,
    rcases classical.em (j=0) with rfl | hjnz,
    { simp only [mem_singleton_iff,union_insert,union_singleton,mem_insert_iff,pi0_zero] at hx,
      exact mem_union_left _ hx },
    rw pi0_eq_compl_sigma0 s j hjnz at hx,
    apply mem_union_right _ (mem_Union.mpr _),
    use j,
    apply mem_Union.mpr,
    use hj,
    exact hx },
  have cardcompl : ‚àÄ j, #(sigma0 s j) = #(compl '' sigma0 s j) :=
    Œª j, cardinal.eq.mpr (‚ü®equiv.set.image _ _  compl_injective‚ü©),
  have A := aleph_0_le_aleph 1,
  have B : aleph 1 ‚â§ (max (#s) 2) ^ aleph_0.{u} :=
    aleph_one_le_continuum.trans (power_le_power_right (le_max_right _ _)),
  have C : ‚Ñµ‚ÇÄ ‚â§ (max (#s) 2) ^ aleph_0.{u} := A.trans B,
  have L : #(‚Ü•(s ‚à™ {‚àÖ, univ})) ‚â§ (max (#s) 2) ^ aleph_0.{u},
  { apply_rules [(mk_union_le _ _).trans, add_le_of_le C, mk_image_le.trans],
    { exact (le_max_left _ _).trans (self_le_power _ one_lt_aleph_0.le) },
    repeat { simp only [mk_fintype,fintype.card_unique,nat.cast_one,mk_singleton],
      exact one_lt_aleph_0.le.trans C } },
  have K : #(‚Ü•‚ãÉ j < i, compl '' sigma0 s j) ‚â§ (max (#s) 2) ^ aleph_0.{u},
  { apply mk_Union_ordinal_le_of_le,
    exact (hi.trans $ ord_le_ord.mpr B),
    exact C,
    intros j hj,
    rw ‚Üê cardcompl,
    apply IH j hj,
    exact (le_of_lt $ lt_of_lt_of_le hj hi) },
  have J : #(‚Ü•(s ‚à™ {‚àÖ, univ} ‚à™ ‚ãÉ j < i, compl '' sigma0 s j)) ‚â§ (max (#s) 2) ^ aleph_0.{u},
    { calc
      #(‚Ü•(s ‚à™ {‚àÖ, univ} ‚à™ ‚ãÉ j < i, compl '' sigma0 s j)) ‚â§
        #(‚Ü•(s ‚à™ {‚àÖ, univ})) + #(‚Ü•‚ãÉ j < i, compl '' sigma0 s j) : mk_union_le _ _
      ... ‚â§ (max (#s) 2) ^ aleph_0.{u} + (max (#s) 2) ^ aleph_0.{u} :
        (add_le_add (le_refl _) K).trans (add_le_add L (le_refl _))
      ... = (max (#s) 2) ^ aleph_0.{u} :
        (add_eq_max C).trans (max_eq_right (le_refl _)) },
  -- The main calculation:
  calc
  #‚Ü•(sigma0 s i) =
    #‚Ü•(range (Œª (f : ‚Ñï ‚Üí (‚Ü•‚ãÉ j < i, pi0 s j)), ‚ãÉ n, ‚Üë(f n))) :
    by { rw sigma0_eq_Union_pi0, simp }
  ... ‚â§ #(‚Ñï ‚Üí (‚Ü•‚ãÉ j < i, pi0 s j))                  : mk_range_le
  ... = prod (Œª n : ‚Ñï, #(‚Ü•‚ãÉ j < i, pi0 s j))        : mk_pi _
  ... = #(‚Ü•‚ãÉ j < i, pi0 s j) ^ aleph_0.{u}          : by { simp [prod_const] }
  ... ‚â§ #(‚Ü•(s ‚à™ {‚àÖ, univ} ‚à™ ‚ãÉ j < i, compl '' sigma0 s j)) ^ aleph_0.{u} :
    power_le_power_right (mk_le_mk_of_subset Upi0sub)
  ... ‚â§ (max (# ‚Ü•s) 2 ^ aleph_0.{u}) ^ aleph_0.{u}  : power_le_power_right J
  ... ‚â§ (max (# ‚Ü•s) 2 ^ aleph_0.{u})                :
    by { rwa [‚Üê power_mul, aleph_0_mul_aleph_0] }
end

theorem cardinal_gen_measurable_le :
  #(gen_measurable s) ‚â§ (max (#s) 2) ^ aleph_0.{u} := cardinal_sigma0_le _ _ (le_refl _)

theorem cardinal_generate_measurable_le :
  #{t | generate_measurable s t} ‚â§ (max (#s) 2) ^ aleph_0.{u} :=
begin
  rw generate_measurable_eq_gen_measurable,
  exact cardinal_gen_measurable_le s,
end

theorem cardinal_measurable_set_le' :
  #{t | @measurable_set Œ± (generate_from s) t} ‚â§ (max (#s) 2) ^ aleph_0.{u} :=
cardinal_generate_measurable_le _

theorem cardinal_generate_measurable_le_continuum (hs : #s ‚â§ ùî†) :
  #{t | generate_measurable s t} ‚â§ ùî† :=
(cardinal_generate_measurable_le _).trans begin
  rw ‚Üêcontinuum_power_aleph_0,
  exact_mod_cast power_le_power_right (max_le hs (nat_lt_continuum 2).le)
end

theorem cardinal_measurable_set_le_continuum :
  #s ‚â§ ùî† ‚Üí #{t | @measurable_set Œ± (generate_from s) t} ‚â§ ùî† :=
cardinal_generate_measurable_le_continuum _

end card_gen_measurable

end pointclasses
