/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Yury Kudryashov
-/
import measure_theory.measurable_space
import topology.instances.ennreal
import analysis.normed_space.basic

/-!
# Borel (measurable) space

## Main definitions

* `borel Œ±` : the least `œÉ`-algebra that contains all open sets;
* `class borel_space` : a space with `topological_space` and `measurable_space` structures
  such that `‚Äπmeasurable_space Œ±‚Ä∫ = borel Œ±`;
* `class opens_measurable_space` : a space with `topological_space` and `measurable_space`
  structures such that all open sets are measurable; equivalently, `borel Œ± ‚â§ ‚Äπmeasurable_space Œ±‚Ä∫`.
* `borel_space` instances on `empty`, `unit`, `bool`, `nat`, `int`, `rat`;
* `measurable` and `borel_space` instances on `‚Ñù`, `‚Ñù‚â•0`, `ennreal`.

## Main statements

* `is_open.is_measurable`, `is_closed.is_measurable`: open and closed sets are measurable;
* `continuous.measurable` : a continuous function is measurable;
* `continuous.measurable2` : if `f : Œ± ‚Üí Œ≤` and `g : Œ± ‚Üí Œ≥` are measurable and `op : Œ≤ √ó Œ≥ ‚Üí Œ¥`
  is continuous, then `Œª x, op (f x, g y)` is measurable;
* `measurable.add` etc : dot notation for arithmetic operations on `measurable` predicates,
  and similarly for `dist` and `edist`;
* `measurable.ennreal*` : special cases for arithmetic operations on `ennreal`s.
-/

noncomputable theory

open classical set
open_locale classical big_operators topological_space

universes u v w x y
variables {Œ± : Type u} {Œ≤ : Type v} {Œ≥ : Type w} {Œ¥ : Type x} {Œπ : Sort y} {s t u : set Œ±}

open measurable_space topological_space

/-- `measurable_space` structure generated by `topological_space`. -/
def borel (Œ± : Type u) [topological_space Œ±] : measurable_space Œ± :=
generate_from {s : set Œ± | is_open s}

lemma borel_eq_top_of_discrete [topological_space Œ±] [discrete_topology Œ±] :
  borel Œ± = ‚ä§ :=
top_le_iff.1 $ Œª s hs, generate_measurable.basic s (is_open_discrete s)

lemma borel_eq_top_of_encodable [topological_space Œ±] [t1_space Œ±] [encodable Œ±] :
  borel Œ± = ‚ä§ :=
begin
  refine (top_le_iff.1 $ Œª s hs, bUnion_of_singleton s ‚ñ∏ _),
  apply is_measurable.bUnion s.countable_encodable,
  intros x hx,
  apply is_measurable.of_compl,
  apply generate_measurable.basic,
  exact is_closed_singleton
end

lemma borel_eq_generate_from_of_subbasis {s : set (set Œ±)}
  [t : topological_space Œ±] [second_countable_topology Œ±] (hs : t = generate_from s) :
  borel Œ± = generate_from s :=
le_antisymm
  (generate_from_le $ assume u (hu : t.is_open u),
    begin
      rw [hs] at hu,
      induction hu,
      case generate_open.basic : u hu
      { exact generate_measurable.basic u hu },
      case generate_open.univ
      { exact @is_measurable.univ Œ± (generate_from s) },
      case generate_open.inter : s‚ÇÅ s‚ÇÇ _ _ hs‚ÇÅ hs‚ÇÇ
      { exact @is_measurable.inter Œ± (generate_from s) _ _ hs‚ÇÅ hs‚ÇÇ },
      case generate_open.sUnion : f hf ih {
        rcases is_open_sUnion_countable f (by rwa hs) with ‚ü®v, hv, vf, vu‚ü©,
        rw ‚Üê vu,
        exact @is_measurable.sUnion Œ± (generate_from s) _ hv
          (Œª x xv, ih _ (vf xv)) }
    end)
  (generate_from_le $ assume u hu, generate_measurable.basic _ $
    show t.is_open u, by rw [hs]; exact generate_open.basic _ hu)

lemma borel_eq_generate_Iio (Œ±)
  [topological_space Œ±] [second_countable_topology Œ±]
  [linear_order Œ±] [order_topology Œ±] :
  borel Œ± = generate_from (range Iio) :=
begin
  refine le_antisymm _ (generate_from_le _),
  { rw borel_eq_generate_from_of_subbasis (@order_topology.topology_eq_generate_intervals Œ± _ _ _),
    have H : ‚àÄ a:Œ±, is_measurable (measurable_space.generate_from (range Iio)) (Iio a) :=
      Œª a, generate_measurable.basic _ ‚ü®_, rfl‚ü©,
    refine generate_from_le _, rintro _ ‚ü®a, rfl | rfl‚ü©; [skip, apply H],
    by_cases h : ‚àÉ a', ‚àÄ b, a < b ‚Üî a' ‚â§ b,
    { rcases h with ‚ü®a', ha'‚ü©,
      rw (_ : Ioi a = (Iio a')·∂ú), {exact (H _).compl _},
      simp [set.ext_iff, ha'] },
    { rcases is_open_Union_countable
        (Œª a' : {a' : Œ± // a < a'}, {b | a'.1 < b})
        (Œª a', is_open_lt' _) with ‚ü®v, ‚ü®hv‚ü©, vu‚ü©,
      simp [set.ext_iff] at vu,
      have : Ioi a = ‚ãÉ x : v, (Iio x.1.1)·∂ú,
      { simp [set.ext_iff],
        refine Œª x, ‚ü®Œª ax, _, Œª ‚ü®a', ‚ü®h, av‚ü©, ax‚ü©, lt_of_lt_of_le h ax‚ü©,
        rcases (vu x).2 _ with ‚ü®a', h‚ÇÅ, h‚ÇÇ‚ü©,
        { exact ‚ü®a', h‚ÇÅ, le_of_lt h‚ÇÇ‚ü© },
        refine not_imp_comm.1 (Œª h, _) h,
        exact ‚ü®x, Œª b, ‚ü®Œª ab, le_of_not_lt (Œª h', h ‚ü®b, ab, h'‚ü©),
          lt_of_lt_of_le ax‚ü©‚ü© },
      rw this, resetI,
      apply is_measurable.Union,
      exact Œª _, (H _).compl _ } },
  { simp, rintro _ a rfl,
    exact generate_measurable.basic _ is_open_Iio }
end

lemma borel_eq_generate_Ioi (Œ±)
  [topological_space Œ±] [second_countable_topology Œ±]
  [linear_order Œ±] [order_topology Œ±] :
  borel Œ± = generate_from (range Ioi) :=
begin
  refine le_antisymm _ (generate_from_le _),
  { rw borel_eq_generate_from_of_subbasis (@order_topology.topology_eq_generate_intervals Œ± _ _ _),
    have H : ‚àÄ a:Œ±, is_measurable (measurable_space.generate_from (range (Œª a, {x | a < x})))
      {x | a < x} := Œª a, generate_measurable.basic _ ‚ü®_, rfl‚ü©,
    refine generate_from_le _, rintro _ ‚ü®a, rfl | rfl‚ü©, {apply H},
    by_cases h : ‚àÉ a', ‚àÄ b, b < a ‚Üî b ‚â§ a',
    { rcases h with ‚ü®a', ha'‚ü©,
      rw (_ : Iio a = (Ioi a')·∂ú), {exact (H _).compl _},
      simp [set.ext_iff, ha'] },
    { rcases is_open_Union_countable
        (Œª a' : {a' : Œ± // a' < a}, {b | b < a'.1})
        (Œª a', is_open_gt' _) with ‚ü®v, ‚ü®hv‚ü©, vu‚ü©,
      simp [set.ext_iff] at vu,
      have : Iio a = ‚ãÉ x : v, (Ioi x.1.1)·∂ú,
      { simp [set.ext_iff],
        refine Œª x, ‚ü®Œª ax, _, Œª ‚ü®a', ‚ü®h, av‚ü©, ax‚ü©, lt_of_le_of_lt ax h‚ü©,
        rcases (vu x).2 _ with ‚ü®a', h‚ÇÅ, h‚ÇÇ‚ü©,
        { exact ‚ü®a', h‚ÇÅ, le_of_lt h‚ÇÇ‚ü© },
        refine not_imp_comm.1 (Œª h, _) h,
        exact ‚ü®x, Œª b, ‚ü®Œª ab, le_of_not_lt (Œª h', h ‚ü®b, ab, h'‚ü©),
          Œª h, lt_of_le_of_lt h ax‚ü©‚ü© },
      rw this, resetI,
      apply is_measurable.Union,
      exact Œª _, (H _).compl _ } },
  { simp, rintro _ a rfl,
    exact generate_measurable.basic _ (is_open_lt' _) }
end

lemma borel_comap {f : Œ± ‚Üí Œ≤} {t : topological_space Œ≤} :
  @borel Œ± (t.induced f) = (@borel Œ≤ t).comap f :=
comap_generate_from.symm

lemma continuous.borel_measurable [topological_space Œ±] [topological_space Œ≤]
  {f : Œ± ‚Üí Œ≤} (hf : continuous f) :
  @measurable Œ± Œ≤ (borel Œ±) (borel Œ≤) f :=
measurable.of_le_map $ generate_from_le $
  Œª s hs, generate_measurable.basic (f ‚Åª¬π' s) (hf s hs)

/-- A space with `measurable_space` and `topological_space` structures such that
all open sets are measurable. -/
class opens_measurable_space (Œ± : Type*) [topological_space Œ±] [h : measurable_space Œ±] : Prop :=
(borel_le : borel Œ± ‚â§ h)

/-- A space with `measurable_space` and `topological_space` structures such that
the `œÉ`-algebra of measurable sets is exactly the `œÉ`-algebra generated by open sets. -/
class borel_space (Œ± : Type*) [topological_space Œ±] [measurable_space Œ±] : Prop :=
(measurable_eq : ‚Äπmeasurable_space Œ±‚Ä∫ = borel Œ±)

/-- In a `borel_space` all open sets are measurable. -/
@[priority 100]
instance borel_space.opens_measurable {Œ± : Type*} [topological_space Œ±] [measurable_space Œ±]
  [borel_space Œ±] : opens_measurable_space Œ± :=
‚ü®ge_of_eq $ borel_space.measurable_eq‚ü©

instance subtype.borel_space {Œ± : Type*} [topological_space Œ±] [measurable_space Œ±]
  [hŒ± : borel_space Œ±] (s : set Œ±) :
  borel_space s :=
‚ü®by { rw [hŒ±.1, subtype.measurable_space, ‚Üê borel_comap], refl }‚ü©

instance subtype.opens_measurable_space {Œ± : Type*} [topological_space Œ±] [measurable_space Œ±]
  [h : opens_measurable_space Œ±] (s : set Œ±) :
  opens_measurable_space s :=
‚ü®by { rw [borel_comap], exact comap_mono h.1 }‚ü©

section
variables [topological_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±]
   [topological_space Œ≤] [measurable_space Œ≤] [opens_measurable_space Œ≤]
   [topological_space Œ≥] [measurable_space Œ≥] [borel_space Œ≥]
   [measurable_space Œ¥]

lemma is_open.is_measurable (h : is_open s) : is_measurable s :=
opens_measurable_space.borel_le _ $ generate_measurable.basic _ h

lemma is_measurable_interior : is_measurable (interior s) := is_open_interior.is_measurable

lemma is_closed.is_measurable (h : is_closed s) : is_measurable s :=
is_measurable.compl_iff.1 $ h.is_measurable

lemma is_compact.is_measurable [t2_space Œ±] (h : is_compact s) : is_measurable s :=
h.is_closed.is_measurable

lemma is_measurable_closure : is_measurable (closure s) :=
is_closed_closure.is_measurable

instance nhds_is_measurably_generated (a : Œ±) : (ùìù a).is_measurably_generated :=
begin
  rw [nhds, infi_subtype'],
  refine @filter.infi_is_measurably_generated _ _ _ _ (Œª i, _),
  exact i.2.2.is_measurable.principal_is_measurably_generated
end

/-- If `s` is a measurable set, then `nhds_within a s` is a measurably generated filter for
each `a`. This cannot be an `instance` because it depends on a non-instance `hs : is_measurable s`.
-/
lemma is_measurable.nhds_within_is_measurably_generated {s : set Œ±} (hs : is_measurable s) (a : Œ±) :
  (nhds_within a s).is_measurably_generated :=
by haveI := hs.principal_is_measurably_generated; exact filter.inf_is_measurably_generated _ _

@[priority 100] -- see Note [lower instance priority]
instance opens_measurable_space.to_measurable_singleton_class [t1_space Œ±] :
  measurable_singleton_class Œ± :=
‚ü®Œª x, is_closed_singleton.is_measurable‚ü©

section order_closed_topology
variables [preorder Œ±] [order_closed_topology Œ±] {a b : Œ±}

lemma is_measurable_Ici : is_measurable (Ici a) := is_closed_Ici.is_measurable
lemma is_measurable_Iic : is_measurable (Iic a) := is_closed_Iic.is_measurable
lemma is_measurable_Icc : is_measurable (Icc a b) := is_closed_Icc.is_measurable

instance at_top_is_measurably_generated : (filter.at_top : filter Œ±).is_measurably_generated :=
@filter.infi_is_measurably_generated _ _ _ _ $
  Œª a, (is_measurable_Ici : is_measurable (Ici a)).principal_is_measurably_generated

instance at_bot_is_measurably_generated : (filter.at_bot : filter Œ±).is_measurably_generated :=
@filter.infi_is_measurably_generated _ _ _ _ $
  Œª a, (is_measurable_Iic : is_measurable (Iic a)).principal_is_measurably_generated

end order_closed_topology

section order_closed_topology
variables [linear_order Œ±] [order_closed_topology Œ±] {a b : Œ±}

lemma is_measurable_Iio : is_measurable (Iio a) := is_open_Iio.is_measurable
lemma is_measurable_Ioi : is_measurable (Ioi a) := is_open_Ioi.is_measurable
lemma is_measurable_Ioo : is_measurable (Ioo a b) := is_open_Ioo.is_measurable
lemma is_measurable_Ioc : is_measurable (Ioc a b) := is_measurable_Ioi.inter is_measurable_Iic
lemma is_measurable_Ico : is_measurable (Ico a b) := is_measurable_Ici.inter is_measurable_Iio

end order_closed_topology

lemma is_measurable_interval [decidable_linear_order Œ±] [order_closed_topology Œ±] {a b : Œ±} :
  is_measurable (interval a b) :=
is_measurable_Icc

instance prod.opens_measurable_space [second_countable_topology Œ±] [second_countable_topology Œ≤] :
  opens_measurable_space (Œ± √ó Œ≤) :=
begin
  refine ‚ü®_‚ü©,
  rcases is_open_generated_countable_inter Œ± with ‚ü®a, ha‚ÇÅ, ha‚ÇÇ, ha‚ÇÉ, ha‚ÇÑ, ha‚ÇÖ‚ü©,
  rcases is_open_generated_countable_inter Œ≤ with ‚ü®b, hb‚ÇÅ, hb‚ÇÇ, hb‚ÇÉ, hb‚ÇÑ, hb‚ÇÖ‚ü©,
  have : prod.topological_space = generate_from {g | ‚àÉu‚ààa, ‚àÉv‚ààb, g = set.prod u v},
  { rw [ha‚ÇÖ, hb‚ÇÖ], exact prod_generate_from_generate_from_eq ha‚ÇÑ hb‚ÇÑ },
  rw [borel_eq_generate_from_of_subbasis this],
  apply generate_from_le,
  rintros _ ‚ü®u, hu, v, hv, rfl‚ü©,
  have hu : is_open u, by { rw [ha‚ÇÖ], exact generate_open.basic _ hu },
  have hv : is_open v, by { rw [hb‚ÇÖ], exact generate_open.basic _ hv },
  exact hu.is_measurable.prod hv.is_measurable
end

/-- A continuous function from an `opens_measurable_space` to a `borel_space`
is measurable. -/
lemma continuous.measurable {f : Œ± ‚Üí Œ≥} (hf : continuous f) :
  measurable f :=
hf.borel_measurable.mono opens_measurable_space.borel_le
  (le_of_eq $ borel_space.measurable_eq)

/-- A homeomorphism between two Borel spaces is a measurable equivalence.-/
def homeomorph.to_measurable_equiv {Œ± : Type*} {Œ≤ : Type*} [topological_space Œ±]
  [measurable_space Œ±] [borel_space Œ±] [topological_space Œ≤] [measurable_space Œ≤]
  [borel_space Œ≤] (h : Œ± ‚âÉ‚Çú Œ≤) :
  measurable_equiv Œ± Œ≤ :=
{ measurable_to_fun := h.continuous_to_fun.measurable,
  measurable_inv_fun := h.continuous_inv_fun.measurable,
  .. h }

lemma measurable_of_continuous_on_compl_singleton [t1_space Œ±] {f : Œ± ‚Üí Œ≥} (a : Œ±)
  (hf : continuous_on f {x | x ‚â† a}) :
  measurable f :=
measurable_of_measurable_on_compl_singleton a
  (continuous_on_iff_continuous_restrict.1 hf).measurable

lemma continuous.measurable2 [second_countable_topology Œ±] [second_countable_topology Œ≤]
  {f : Œ¥ ‚Üí Œ±} {g : Œ¥ ‚Üí Œ≤} {c : Œ± ‚Üí Œ≤ ‚Üí Œ≥}
  (h : continuous (Œªp:Œ±√óŒ≤, c p.1 p.2)) (hf : measurable f) (hg : measurable g) :
  measurable (Œªa, c (f a) (g a)) :=
h.measurable.comp (hf.prod_mk hg)

lemma measurable.smul [semiring Œ±] [second_countable_topology Œ±]
  [add_comm_monoid Œ≥] [second_countable_topology Œ≥]
  [semimodule Œ± Œ≥] [topological_semimodule Œ± Œ≥]
  {f : Œ¥ ‚Üí Œ±} {g : Œ¥ ‚Üí Œ≥} (hf : measurable f) (hg : measurable g) :
  measurable (Œª c, f c ‚Ä¢ g c) :=
continuous_smul.measurable2 hf hg

lemma measurable.const_smul {Œ± : Type*} [topological_space Œ±] [semiring Œ±]
  [add_comm_monoid Œ≥] [semimodule Œ± Œ≥] [topological_semimodule Œ± Œ≥]
  {f : Œ¥ ‚Üí Œ≥} (hf : measurable f) (c : Œ±) :
  measurable (Œª x, c ‚Ä¢ f x) :=
(continuous_const.smul continuous_id).measurable.comp hf

lemma measurable_const_smul_iff {Œ± : Type*} [topological_space Œ±]
  [division_ring Œ±] [add_comm_monoid Œ≥]
  [semimodule Œ± Œ≥] [topological_semimodule Œ± Œ≥]
  {f : Œ¥ ‚Üí Œ≥} {c : Œ±} (hc : c ‚â† 0) :
  measurable (Œª x, c ‚Ä¢ f x) ‚Üî measurable f :=
‚ü®Œª h, by simpa only [smul_smul, inv_mul_cancel hc, one_smul] using h.const_smul c‚Åª¬π,
  Œª h, h.const_smul c‚ü©

lemma is_measurable_le' [partial_order Œ±] [order_closed_topology Œ±] [second_countable_topology Œ±] :
  is_measurable {p : Œ± √ó Œ± | p.1 ‚â§ p.2} :=
order_closed_topology.is_closed_le'.is_measurable

lemma is_measurable_le [partial_order Œ±] [order_closed_topology Œ±] [second_countable_topology Œ±]
  {f g : Œ¥ ‚Üí Œ±} (hf : measurable f) (hg : measurable g) :
  is_measurable {a | f a ‚â§ g a} :=
hf.prod_mk hg is_measurable_le'

lemma measurable.max [decidable_linear_order Œ±] [order_closed_topology Œ±] [second_countable_topology Œ±]
  {f g : Œ¥ ‚Üí Œ±} (hf : measurable f) (hg : measurable g) :
  measurable (Œªa, max (f a) (g a)) :=
hg.piecewise (is_measurable_le hf hg) hf

lemma measurable.min [decidable_linear_order Œ±] [order_closed_topology Œ±] [second_countable_topology Œ±]
  {f g : Œ¥ ‚Üí Œ±} (hf : measurable f) (hg : measurable g) :
  measurable (Œªa, min (f a) (g a)) :=
hf.piecewise (is_measurable_le hf hg) hg

end

section borel_space
variables [topological_space Œ±] [measurable_space Œ±] [borel_space Œ±]
  [topological_space Œ≤] [measurable_space Œ≤] [borel_space Œ≤]
  [topological_space Œ≥] [measurable_space Œ≥] [borel_space Œ≥]
  [measurable_space Œ¥]

lemma prod_le_borel_prod : prod.measurable_space ‚â§ borel (Œ± √ó Œ≤) :=
begin
  rw [‚Äπborel_space Œ±‚Ä∫.measurable_eq, ‚Äπborel_space Œ≤‚Ä∫.measurable_eq],
  refine sup_le _ _,
  { exact comap_le_iff_le_map.mpr continuous_fst.borel_measurable },
  { exact comap_le_iff_le_map.mpr continuous_snd.borel_measurable }
end

instance prod.borel_space [second_countable_topology Œ±] [second_countable_topology Œ≤] :
  borel_space (Œ± √ó Œ≤) :=
‚ü®le_antisymm prod_le_borel_prod opens_measurable_space.borel_le‚ü©

@[to_additive]
lemma measurable_mul [has_mul Œ±] [has_continuous_mul Œ±] [second_countable_topology Œ±] :
  measurable (Œª p : Œ± √ó Œ±, p.1 * p.2) :=
continuous_mul.measurable

@[to_additive]
lemma measurable.mul [has_mul Œ±] [has_continuous_mul Œ±] [second_countable_topology Œ±]
  {f : Œ¥ ‚Üí Œ±} {g : Œ¥ ‚Üí Œ±} : measurable f ‚Üí measurable g ‚Üí measurable (Œªa, f a * g a) :=
continuous_mul.measurable2

@[to_additive]
lemma measurable_mul_left [has_mul Œ±] [has_continuous_mul Œ±] (x : Œ±) :
  measurable (Œª y : Œ±, x * y) :=
continuous.measurable $ continuous_const.mul continuous_id

@[to_additive]
lemma measurable_mul_right [has_mul Œ±] [has_continuous_mul Œ±] (x : Œ±) :
  measurable (Œª y : Œ±, y * x) :=
continuous.measurable $ continuous_id.mul continuous_const

@[to_additive]
lemma finset.measurable_prod {Œπ : Type*} [comm_monoid Œ±] [has_continuous_mul Œ±]
  [second_countable_topology Œ±] {f : Œπ ‚Üí Œ¥ ‚Üí Œ±} (s : finset Œπ) (hf : ‚àÄi, measurable (f i)) :
  measurable (Œªa, ‚àè i in s, f i a) :=
finset.induction_on s
  (by simp only [finset.prod_empty, measurable_const])
  (assume i s his ih, by simpa [his] using (hf i).mul ih)

@[to_additive]
lemma measurable_inv [group Œ±] [topological_group Œ±] : measurable (has_inv.inv : Œ± ‚Üí Œ±) :=
continuous_inv.measurable

@[to_additive]
lemma measurable.inv [group Œ±] [topological_group Œ±] {f : Œ¥ ‚Üí Œ±} (hf : measurable f) :
  measurable (Œªa, (f a)‚Åª¬π) :=
measurable_inv.comp hf

lemma measurable_inv' {Œ± : Type*} [normed_field Œ±] [measurable_space Œ±] [borel_space Œ±] :
  measurable (has_inv.inv : Œ± ‚Üí Œ±) :=
measurable_of_continuous_on_compl_singleton 0 normed_field.continuous_on_inv

lemma measurable.inv' {Œ± : Type*} [normed_field Œ±] [measurable_space Œ±] [borel_space Œ±]
  {f : Œ¥ ‚Üí Œ±} (hf : measurable f) :
  measurable (Œªa, (f a)‚Åª¬π) :=
measurable_inv'.comp hf

@[to_additive]
lemma measurable.of_inv [group Œ±] [topological_group Œ±] {f : Œ¥ ‚Üí Œ±}
  (hf : measurable (Œª a, (f a)‚Åª¬π)) : measurable f :=
by simpa only [inv_inv] using hf.inv

@[simp, to_additive]
lemma measurable_inv_iff [group Œ±] [topological_group Œ±] {f : Œ¥ ‚Üí Œ±} :
  measurable (Œª a, (f a)‚Åª¬π) ‚Üî measurable f :=
‚ü®measurable.of_inv, measurable.inv‚ü©

lemma measurable.sub [add_group Œ±] [topological_add_group Œ±] [second_countable_topology Œ±]
  {f g : Œ¥ ‚Üí Œ±} (hf : measurable f) (hg : measurable g) :
  measurable (Œª x, f x - g x) :=
hf.add hg.neg

lemma measurable.is_lub [linear_order Œ±] [order_topology Œ±] [second_countable_topology Œ±]
  {Œπ} [encodable Œπ] {f : Œπ ‚Üí Œ¥ ‚Üí Œ±} {g : Œ¥ ‚Üí Œ±} (hf : ‚àÄ i, measurable (f i))
  (hg : ‚àÄ b, is_lub {a | ‚àÉ i, f i b = a} (g b)) :
  measurable g :=
begin
  change ‚àÄ b, is_lub (range $ Œª i, f i b) (g b) at hg,
  rw [‚Äπborel_space Œ±‚Ä∫.measurable_eq, borel_eq_generate_Ioi Œ±],
  apply measurable_generate_from,
  rintro _ ‚ü®a, rfl‚ü©,
  simp only [set.preimage, mem_Ioi, lt_is_lub_iff (hg _), exists_range_iff, set_of_exists],
  exact is_measurable.Union (Œª i, hf i (is_open_lt' _).is_measurable)
end

lemma measurable.is_glb [linear_order Œ±] [order_topology Œ±] [second_countable_topology Œ±]
  {Œπ} [encodable Œπ] {f : Œπ ‚Üí Œ¥ ‚Üí Œ±} {g : Œ¥ ‚Üí Œ±} (hf : ‚àÄ i, measurable (f i))
  (hg : ‚àÄ b, is_glb {a | ‚àÉ i, f i b = a} (g b)) :
  measurable g :=
begin
  change ‚àÄ b, is_glb (range $ Œª i, f i b) (g b) at hg,
  rw [‚Äπborel_space Œ±‚Ä∫.measurable_eq, borel_eq_generate_Iio Œ±],
  apply measurable_generate_from,
  rintro _ ‚ü®a, rfl‚ü©,
  simp only [set.preimage, mem_Iio, is_glb_lt_iff (hg _), exists_range_iff, set_of_exists],
  exact is_measurable.Union (Œª i, hf i (is_open_gt' _).is_measurable)
end

lemma measurable_supr [complete_linear_order Œ±] [order_topology Œ±] [second_countable_topology Œ±]
  {Œπ} [encodable Œπ] {f : Œπ ‚Üí Œ¥ ‚Üí Œ±} (hf : ‚àÄ i, measurable (f i)) :
  measurable (Œª b, ‚®Ü i, f i b) :=
measurable.is_lub hf $ Œª b, is_lub_supr

lemma measurable_infi [complete_linear_order Œ±] [order_topology Œ±] [second_countable_topology Œ±]
  {Œπ} [encodable Œπ] {f : Œπ ‚Üí Œ¥ ‚Üí Œ±} (hf : ‚àÄ i, measurable (f i)) :
  measurable (Œª b, ‚®Ö i, f i b) :=
measurable.is_glb hf $ Œª b, is_glb_infi

lemma measurable.supr_Prop {Œ±} [measurable_space Œ±] [complete_lattice Œ±]
  (p : Prop) {f : Œ¥ ‚Üí Œ±} (hf : measurable f) :
  measurable (Œª b, ‚®Ü h : p, f b) :=
classical.by_cases
  (assume h : p, begin convert hf, funext, exact supr_pos h end)
  (assume h : ¬¨p, begin convert measurable_const, funext, exact supr_neg h end)

lemma measurable.infi_Prop {Œ±} [measurable_space Œ±] [complete_lattice Œ±]
  (p : Prop) {f : Œ¥ ‚Üí Œ±} (hf : measurable f) :
  measurable (Œª b, ‚®Ö h : p, f b) :=
classical.by_cases
  (assume h : p, begin convert hf, funext, exact infi_pos h end )
  (assume h : ¬¨p, begin convert measurable_const, funext, exact infi_neg h end)

lemma measurable_bsupr [complete_linear_order Œ±] [order_topology Œ±] [second_countable_topology Œ±]
  {Œπ} [encodable Œπ] (p : Œπ ‚Üí Prop) {f : Œπ ‚Üí Œ¥ ‚Üí Œ±} (hf : ‚àÄ i, measurable (f i)) :
  measurable (Œª b, ‚®Ü i (hi : p i), f i b) :=
measurable_supr $ Œª i, (hf i).supr_Prop (p i)

lemma measurable_binfi [complete_linear_order Œ±] [order_topology Œ±] [second_countable_topology Œ±]
  {Œπ} [encodable Œπ] (p : Œπ ‚Üí Prop) {f : Œπ ‚Üí Œ¥ ‚Üí Œ±} (hf : ‚àÄ i, measurable (f i)) :
  measurable (Œª b, ‚®Ö i (hi : p i), f i b) :=
measurable_infi $ Œª i, (hf i).infi_Prop (p i)

/-- Convert a `homeomorph` to a `measurable_equiv`. -/
def homemorph.to_measurable_equiv (h : Œ± ‚âÉ‚Çú Œ≤) :
  measurable_equiv Œ± Œ≤ :=
{ to_equiv := h.to_equiv,
  measurable_to_fun := h.continuous_to_fun.measurable,
  measurable_inv_fun := h.continuous_inv_fun.measurable }

end borel_space

instance empty.borel_space : borel_space empty := ‚ü®borel_eq_top_of_discrete.symm‚ü©
instance unit.borel_space : borel_space unit := ‚ü®borel_eq_top_of_discrete.symm‚ü©
instance bool.borel_space : borel_space bool := ‚ü®borel_eq_top_of_discrete.symm‚ü©
instance nat.borel_space : borel_space ‚Ñï := ‚ü®borel_eq_top_of_discrete.symm‚ü©
instance int.borel_space : borel_space ‚Ñ§ := ‚ü®borel_eq_top_of_discrete.symm‚ü©
instance rat.borel_space : borel_space ‚Ñö := ‚ü®borel_eq_top_of_encodable.symm‚ü©

instance real.measurable_space : measurable_space ‚Ñù := borel ‚Ñù
instance real.borel_space : borel_space ‚Ñù := ‚ü®rfl‚ü©

instance nnreal.measurable_space : measurable_space nnreal := borel nnreal
instance nnreal.borel_space : borel_space nnreal := ‚ü®rfl‚ü©

instance ennreal.measurable_space : measurable_space ennreal := borel ennreal
instance ennreal.borel_space : borel_space ennreal := ‚ü®rfl‚ü©

section metric_space

variables [metric_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±] {x : Œ±} {Œµ : ‚Ñù}

lemma is_measurable_ball : is_measurable (metric.ball x Œµ) :=
metric.is_open_ball.is_measurable

lemma is_measurable_closed_ball : is_measurable (metric.closed_ball x Œµ) :=
metric.is_closed_ball.is_measurable

lemma measurable_dist [second_countable_topology Œ±] :
  measurable (Œªp:Œ±√óŒ±, dist p.1 p.2) :=
continuous_dist.measurable

lemma measurable.dist [second_countable_topology Œ±] [measurable_space Œ≤] {f g : Œ≤ ‚Üí Œ±}
  (hf : measurable f) (hg : measurable g) : measurable (Œª b, dist (f b) (g b)) :=
continuous_dist.measurable2 hf hg

lemma measurable_nndist [second_countable_topology Œ±] : measurable (Œªp:Œ±√óŒ±, nndist p.1 p.2) :=
continuous_nndist.measurable

lemma measurable.nndist [second_countable_topology Œ±] [measurable_space Œ≤] {f g : Œ≤ ‚Üí Œ±} :
  measurable f ‚Üí measurable g ‚Üí measurable (Œª b, nndist (f b) (g b)) :=
continuous_nndist.measurable2

end metric_space

section emetric_space

variables [emetric_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±] {x : Œ±} {Œµ : ennreal}

lemma is_measurable_eball : is_measurable (emetric.ball x Œµ) :=
emetric.is_open_ball.is_measurable

lemma measurable_edist [second_countable_topology Œ±] :
  measurable (Œªp:Œ±√óŒ±, edist p.1 p.2) :=
continuous_edist.measurable

lemma measurable.edist [second_countable_topology Œ±] [measurable_space Œ≤] {f g : Œ≤ ‚Üí Œ±} :
  measurable f ‚Üí measurable g ‚Üí measurable (Œª b, edist (f b) (g b)) :=
continuous_edist.measurable2

end emetric_space

namespace real
open measurable_space

lemma borel_eq_generate_from_Ioo_rat :
  borel ‚Ñù = generate_from (‚ãÉ(a b : ‚Ñö) (h : a < b), {Ioo a b}) :=
borel_eq_generate_from_of_subbasis is_topological_basis_Ioo_rat.2.2

lemma borel_eq_generate_from_Iio_rat :
  borel ‚Ñù = generate_from (‚ãÉa:‚Ñö, {Iio a}) :=
begin
  let g, swap,
  apply le_antisymm (_ : _ ‚â§ g) (measurable_space.generate_from_le (Œª t, _)),
  { rw borel_eq_generate_from_Ioo_rat,
    refine generate_from_le (Œª t, _),
    simp only [mem_Union], rintro ‚ü®a, b, h, H‚ü©,
    rw [mem_singleton_iff.1 H],
    rw (set.ext (Œª x, _) : Ioo (a:‚Ñù) b = (‚ãÉc>a, (Iio c)·∂ú) ‚à© Iio b),
    { have hg : ‚àÄq:‚Ñö, g.is_measurable (Iio q) :=
        Œª q, generate_measurable.basic _ (by simp; exact ‚ü®_, rfl‚ü©),
      refine @is_measurable.inter _ g _ _ _ (hg _),
      refine @is_measurable.bUnion _ _ g _ _ (countable_encodable _) (Œª c h, _),
      exact @is_measurable.compl _ _ g (hg _) },
    { simp [Ioo, Iio],
      refine and_congr _ iff.rfl,
      exact ‚ü®Œª h,
        let ‚ü®c, ac, cx‚ü© := exists_rat_btwn h in
        ‚ü®c, rat.cast_lt.1 ac, le_of_lt cx‚ü©,
       Œª ‚ü®c, ac, cx‚ü©, lt_of_lt_of_le (rat.cast_lt.2 ac) cx‚ü© } },
  { simp, rintro r rfl,
    exact is_open_Iio.is_measurable }
end

end real

lemma measurable.sub_nnreal [measurable_space Œ±] {f g : Œ± ‚Üí nnreal} :
  measurable f ‚Üí measurable g ‚Üí measurable (Œª a, f a - g a) :=
nnreal.continuous_sub.measurable2

lemma measurable.nnreal_of_real [measurable_space Œ±] {f : Œ± ‚Üí ‚Ñù} (hf : measurable f) :
  measurable (Œª x, nnreal.of_real (f x)) :=
nnreal.continuous_of_real.measurable.comp hf

lemma measurable.nnreal_coe [measurable_space Œ±] {f : Œ± ‚Üí nnreal} (hf : measurable f) :
  measurable (Œª x, (f x : ‚Ñù)) :=
nnreal.continuous_coe.measurable.comp hf

lemma measurable.ennreal_coe [measurable_space Œ±] {f : Œ± ‚Üí nnreal} (hf : measurable f) :
  measurable (Œª x, (f x : ennreal)) :=
(ennreal.continuous_coe.2 continuous_id).measurable.comp hf

lemma measurable.ennreal_of_real [measurable_space Œ±] {f : Œ± ‚Üí ‚Ñù} (hf : measurable f) :
  measurable (Œª x, ennreal.of_real (f x)) :=
ennreal.continuous_of_real.measurable.comp hf

/-- The set of finite `ennreal` numbers is `measurable_equiv` to `nnreal`. -/
def measurable_equiv.ennreal_equiv_nnreal : measurable_equiv {r : ennreal | r ‚â† ‚ä§} nnreal :=
ennreal.ne_top_homeomorph_nnreal.to_measurable_equiv

namespace ennreal
open filter

lemma measurable_coe : measurable (coe : nnreal ‚Üí ennreal) :=
measurable_id.ennreal_coe

lemma measurable_of_measurable_nnreal [measurable_space Œ±] {f : ennreal ‚Üí Œ±}
  (h : measurable (Œªp:nnreal, f p)) : measurable f :=
measurable_of_measurable_on_compl_singleton ‚ä§
  (measurable_equiv.ennreal_equiv_nnreal.symm.measurable_coe_iff.1 h)

/-- `ennreal` is `measurable_equiv` to `nnreal ‚äï unit`. -/
def ennreal_equiv_sum :
  measurable_equiv ennreal (nnreal ‚äï unit) :=
{ measurable_to_fun  := measurable_of_measurable_nnreal measurable_inl,
  measurable_inv_fun := measurable_sum measurable_coe (@measurable_const ennreal unit _ _ ‚ä§),
  .. equiv.option_equiv_sum_punit nnreal }

lemma measurable_of_measurable_nnreal_nnreal [measurable_space Œ±] [measurable_space Œ≤]
  (f : ennreal ‚Üí ennreal ‚Üí Œ≤) {g : Œ± ‚Üí ennreal} {h : Œ± ‚Üí ennreal}
  (h‚ÇÅ : measurable (Œªp:nnreal √ó nnreal, f p.1 p.2))
  (h‚ÇÇ : measurable (Œªr:nnreal, f ‚ä§ r))
  (h‚ÇÉ : measurable (Œªr:nnreal, f r ‚ä§))
  (hg : measurable g) (hh : measurable h) : measurable (Œªa, f (g a) (h a)) :=
let e : measurable_equiv (ennreal √ó ennreal)
  (((nnreal √ó nnreal) ‚äï (nnreal √ó unit)) ‚äï ((unit √ó nnreal) ‚äï (unit √ó unit))) :=
  (measurable_equiv.prod_congr ennreal_equiv_sum ennreal_equiv_sum).trans
    (measurable_equiv.sum_prod_sum _ _ _ _) in
have measurable (Œªp:ennreal√óennreal, f p.1 p.2),
begin
  refine e.symm.measurable_coe_iff.1 (measurable_sum (measurable_sum _ _) (measurable_sum _ _)),
  { show measurable (Œªp:nnreal √ó nnreal, f p.1 p.2),
    exact h‚ÇÅ },
  { show measurable (Œªp:nnreal √ó unit, f p.1 ‚ä§),
    exact h‚ÇÉ.comp (measurable.fst measurable_id) },
  { show measurable ((Œªp:nnreal, f ‚ä§ p) ‚àò (Œªp:unit √ó nnreal, p.2)),
    exact h‚ÇÇ.comp (measurable.snd measurable_id) },
  { show measurable (Œªp:unit √ó unit, f ‚ä§ ‚ä§),
    exact measurable_const }
end,
this.comp (measurable.prod_mk hg hh)

lemma measurable_of_real : measurable ennreal.of_real :=
ennreal.continuous_of_real.measurable

end ennreal

lemma measurable.ennreal_mul {Œ± : Type*} [measurable_space Œ±] {f g : Œ± ‚Üí ennreal} :
  measurable f ‚Üí measurable g ‚Üí measurable (Œªa, f a * g a) :=
begin
  refine ennreal.measurable_of_measurable_nnreal_nnreal (*) _ _ _,
  { simp only [ennreal.coe_mul.symm],
    exact ennreal.measurable_coe.comp measurable_mul },
  { simp [ennreal.top_mul],
    exact measurable_const.piecewise
      (is_closed_eq continuous_id continuous_const).is_measurable
      measurable_const },
  { simp [ennreal.mul_top],
    exact measurable_const.piecewise
      (is_closed_eq continuous_id continuous_const).is_measurable
      measurable_const }
end

lemma measurable.ennreal_add {Œ± : Type*} [measurable_space Œ±] {f g : Œ± ‚Üí ennreal} :
  measurable f ‚Üí measurable g ‚Üí measurable (Œªa, f a + g a) :=
begin
  refine ennreal.measurable_of_measurable_nnreal_nnreal (+) _ _ _,
  { simp only [ennreal.coe_add.symm],
    exact ennreal.measurable_coe.comp measurable_add },
  { simp [measurable_const] },
  { simp [measurable_const] }
end

lemma measurable.ennreal_sub {Œ± : Type*} [measurable_space Œ±] {f g : Œ± ‚Üí ennreal} :
  measurable f ‚Üí measurable g ‚Üí measurable (Œªa, f a - g a) :=
begin
  refine ennreal.measurable_of_measurable_nnreal_nnreal (has_sub.sub) _ _ _,
  { simp only [ennreal.coe_sub.symm],
    exact ennreal.measurable_coe.comp nnreal.continuous_sub.measurable },
  { simp [measurable_const] },
  { simp [measurable_const] }
end

section normed_group

variables [measurable_space Œ±] [normed_group Œ±] [opens_measurable_space Œ±] [measurable_space Œ≤]

lemma measurable_norm : measurable (norm : Œ± ‚Üí ‚Ñù) :=
continuous_norm.measurable

lemma measurable.norm {f : Œ≤ ‚Üí Œ±} (hf : measurable f) : measurable (Œªa, norm (f a)) :=
measurable_norm.comp hf

lemma measurable_nnnorm : measurable (nnnorm : Œ± ‚Üí nnreal) :=
continuous_nnnorm.measurable

lemma measurable.nnnorm {f : Œ≤ ‚Üí Œ±} (hf : measurable f) : measurable (Œªa, nnnorm (f a)) :=
measurable_nnnorm.comp hf

lemma measurable.ennnorm {f : Œ≤ ‚Üí Œ±} (hf : measurable f) :
  measurable (Œªa, (nnnorm (f a) : ennreal)) :=
hf.nnnorm.ennreal_coe

end normed_group
