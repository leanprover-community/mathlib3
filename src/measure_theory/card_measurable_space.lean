/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Violeta HernÃ¡ndez Palacios
-/
import measure_theory.measurable_space_def
import set_theory.continuum
import set_theory.cofinality

/-!
# Cardinal of sigma-algebras

If a sigma-algebra is generated by a set of sets `s`, then the cardinality of the sigma-algebra is
bounded by `(max (#s) 2) ^ Ï‰`. This is stated in `measurable_space.cardinal_generate_measurable_le`
and `measurable_space.cardinal_measurable_set_le`.

In particular, if `#s â‰¤ ğ” `, then the generated sigma-algebra has cardinality at most `ğ” `, see
`measurable_space.cardinal_measurable_set_le_continuum`.

For the proof, we rely on an explicit inductive construction of the sigma-algebra generated by
`s` (instead of the inductive predicate `generate_measurable`). This transfinite inductive
construction is parameterized by an ordinal `< Ï‰â‚`, and the cardinality bound is preserved along
each step of the construction.
-/

universe u
variables {Î± : Type u}

open_locale cardinal
open cardinal set

local notation `Ï‰â‚`:= (aleph 1 : cardinal.{u}).ord.out.Î±

namespace measurable_space

/-- Transfinite induction construction of the sigma-algebra generated by a set of sets `s`. At each
step, we add all elements of `s`, the empty set, the complements of already constructed sets, and
countable unions of already constructed sets. We index this construction by an ordinal `< Ï‰â‚`, as
this will be enough to generate all sets in the sigma-algebra. -/
def generate_measurable_rec (s : set (set Î±)) : Ï‰â‚ â†’ set (set Î±)
| i := let S := â‹ƒ j : {j // j < i}, generate_measurable_rec j.1 in
    s âˆª {âˆ…} âˆª compl '' S âˆª (set.range (Î» (f : â„• â†’ S), â‹ƒ n, (f n).1))
using_well_founded {dec_tac := `[exact j.2]}

/-- At each step of the inductive construction, the cardinality bound `â‰¤ (max (#s) 2) ^ Ï‰`
holds. -/
lemma cardinal_generate_measurable_rec_le (s : set (set Î±)) (i : Ï‰â‚) :
  #(generate_measurable_rec s i) â‰¤ (max (#s) 2) ^ omega.{u} :=
begin
  apply (aleph 1).ord.out.wo.wf.induction i,
  assume i IH,
  have A := omega_le_aleph 1,
  have B : aleph 1 â‰¤ (max (#s) 2) ^ omega.{u} :=
    aleph_one_le_continuum.trans (power_le_power_right (le_max_right _ _)),
  have C : omega.{u} â‰¤ (max (#s) 2) ^ omega.{u} := A.trans B,
  have J : #(â‹ƒ (j : {j // j < i}), generate_measurable_rec s j.1) â‰¤ (max (#s) 2) ^ omega.{u},
  { apply (mk_Union_le _).trans,
    have D : # {j // j < i} â‰¤ aleph 1 := (mk_subtype_le _).trans (le_of_eq (aleph 1).mk_ord_out),
    have E : cardinal.sup.{u u}
      (Î» (j : {j // j < i}), #(generate_measurable_rec s j.1)) â‰¤ (max (#s) 2) ^ omega.{u} :=
    cardinal.sup_le.2 (Î» âŸ¨j, hjâŸ©, IH j hj),
    apply (mul_le_mul' D E).trans,
    rw mul_eq_max A C,
    exact max_le B le_rfl },
  rw [generate_measurable_rec],
  apply_rules [(mk_union_le _ _).trans, add_le_of_le C, mk_image_le.trans],
  { exact (le_max_left _ _).trans (self_le_power _ one_lt_omega.le) },
  { rw [mk_singleton],
    exact one_lt_omega.le.trans C },
  { apply mk_range_le.trans,
    simp only [mk_pi, subtype.val_eq_coe, prod_const, lift_uzero, mk_denumerable, lift_omega],
    have := @power_le_power_right _ _ omega.{u} J,
    rwa [â† power_mul, omega_mul_omega] at this }
end

lemma cardinal_Union_generate_measurable_rec_le (s : set (set Î±)) :
  #(â‹ƒ i, generate_measurable_rec s i) â‰¤ (max (#s) 2) ^ omega.{u} :=
begin
  apply (mk_Union_le _).trans,
  rw [(aleph 1).mk_ord_out],
  refine le_trans (mul_le_mul' aleph_one_le_continuum
    (cardinal.sup_le.2 (Î» i, cardinal_generate_measurable_rec_le s i))) _,
  have := power_le_power_right (le_max_right (#s) 2),
  rw mul_eq_max omega_le_continuum (omega_le_continuum.trans this),
  exact max_le this le_rfl
end

/-- A set in the sigma-algebra generated by a set of sets `s` is constructed at some step of the
transfinite induction defining `generate_measurable_rec`.
The other inclusion is also true, but not proved here as it is not needed for the cardinality
bounds.-/
theorem generate_measurable_subset_rec (s : set (set Î±)) â¦ƒt : set Î±â¦„
  (ht : generate_measurable s t) :
  t âˆˆ â‹ƒ i, generate_measurable_rec s i :=
begin
  haveI : nonempty Ï‰â‚, by simp [â† mk_ne_zero_iff, ne_of_gt, (aleph 1).mk_ord_out, aleph_pos 1],
  inhabit Ï‰â‚,
  induction ht with u hu u hu IH f hf IH,
  { refine mem_Union.2 âŸ¨default, _âŸ©,
    rw generate_measurable_rec,
    simp only [hu, mem_union_eq, true_or] },
  { refine mem_Union.2 âŸ¨default, _âŸ©,
    rw generate_measurable_rec,
    simp only [union_singleton, mem_union_eq, mem_insert_iff, eq_self_iff_true, true_or] },
  { rcases mem_Union.1 IH with âŸ¨i, hiâŸ©,
    obtain âŸ¨j, hjâŸ© : âˆƒ j, i < j := ordinal.has_succ_of_is_limit
      (by { rw ordinal.type_lt, exact ord_aleph_is_limit 1 }) _,
    apply mem_Union.2 âŸ¨j, _âŸ©,
    rw generate_measurable_rec,
    have : âˆƒ a, (a < j) âˆ§ u âˆˆ generate_measurable_rec s a := âŸ¨i, hj, hiâŸ©,
    simp [this] },
  { have : âˆ€ n, âˆƒ i, f n âˆˆ generate_measurable_rec s i := Î» n, by simpa using IH n,
    choose I hI using this,
    obtain âŸ¨j, hjâŸ© : âˆƒ j, âˆ€ k, k âˆˆ range I â†’ (k < j),
    { apply ordinal.lt_cof_type,
      simp only [is_regular_aleph_one.2, mk_singleton, ordinal.type_lt],
      have : #(range I) = lift.{0} (#(range I)), by simp only [lift_uzero],
      rw this,
      apply mk_range_le_lift.trans_lt _,
      simp [omega_lt_aleph_one] },
    apply mem_Union.2 âŸ¨j, _âŸ©,
    rw generate_measurable_rec,
    have : âˆƒ (g : â„• â†’ (â†¥â‹ƒ (i : {i // i < j}), generate_measurable_rec s i.1)),
      (â‹ƒ (n : â„•), â†‘(g n)) = (â‹ƒ n, f n),
    { refine âŸ¨Î» n, âŸ¨f n, _âŸ©, rflâŸ©,
      exact mem_Union.2 âŸ¨âŸ¨I n, hj (I n) (mem_range_self _)âŸ©, hI nâŸ© },
    simp [this] }
end

/-- If a sigma-algebra is generated by a set of sets `s`, then the sigma
algebra has cardinality at most `(max (#s) 2) ^ Ï‰`. -/
theorem cardinal_generate_measurable_le (s : set (set Î±)) :
  #{t | generate_measurable s t} â‰¤ (max (#s) 2) ^ omega.{u} :=
(mk_subtype_le_of_subset (generate_measurable_subset_rec s)).trans
  (cardinal_Union_generate_measurable_rec_le s)

/-- If a sigma-algebra is generated by a set of sets `s`, then the sigma
algebra has cardinality at most `(max (#s) 2) ^ Ï‰`. -/
theorem cardinal_measurable_set_le (s : set (set Î±)) :
  #{t | @measurable_set Î± (generate_from s) t} â‰¤ (max (#s) 2) ^ omega.{u} :=
cardinal_generate_measurable_le s

/-- If a sigma-algebra is generated by a set of sets `s` with cardinality at most the continuum,
then the sigma algebra has the same cardinality bound. -/
theorem cardinal_generate_measurable_le_continuum {s : set (set Î±)} (hs : #s â‰¤ ğ” ) :
  #{t | generate_measurable s t} â‰¤ ğ”  :=
calc
#{t | generate_measurable s t}
    â‰¤ (max (#s) 2) ^ omega.{u} : cardinal_generate_measurable_le s
... â‰¤ ğ”  ^ omega.{u} :
  by exact_mod_cast power_le_power_right (max_le hs (nat_lt_continuum 2).le)
... = ğ”  : continuum_power_omega

/-- If a sigma-algebra is generated by a set of sets `s` with cardinality at most the continuum,
then the sigma algebra has the same cardinality bound. -/
theorem cardinal_measurable_set_le_continuum {s : set (set Î±)} (hs : #s â‰¤ ğ” ) :
  #{t | @measurable_set Î± (generate_from s) t} â‰¤ ğ”  :=
cardinal_generate_measurable_le_continuum hs

end measurable_space
