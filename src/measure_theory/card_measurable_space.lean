/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Violeta HernÃ¡ndez Palacios
-/
import measure_theory.measurable_space_def
import set_theory.cardinal.cofinality
import set_theory.cardinal.continuum

/-!
# Cardinal of sigma-algebras

If a sigma-algebra is generated by a set of sets `s`, then the cardinality of the sigma-algebra is
bounded by `(max (#s) 2) ^ â„µâ‚€`. This is stated in `measurable_space.cardinal_generate_measurable_le`
and `measurable_space.cardinal_measurable_set_le`.

In particular, if `#s â‰¤ ğ” `, then the generated sigma-algebra has cardinality at most `ğ” `, see
`measurable_space.cardinal_measurable_set_le_continuum`.

For the proof, we rely on an explicit inductive construction of the sigma-algebra generated by
`s` (instead of the inductive predicate `generate_measurable`). This transfinite inductive
construction is parameterized by an ordinal `< Ï‰â‚`, and the cardinality bound is preserved along
each step of the construction. We show in `measurable_space.generate_measurable_eq_rec` that this
indeed generates this sigma-algebra.
-/

universe u
variables {Î± : Type u}

open_locale cardinal
open cardinal set

local notation `Ï‰â‚` := (aleph 1 : cardinal.{u}).ord.out.Î±

namespace measurable_space

/-- Transfinite induction construction of the sigma-algebra generated by a set of sets `s`. At each
step, we add all elements of `s`, the empty set, the complements of already constructed sets, and
countable unions of already constructed sets. We index this construction by an ordinal `< Ï‰â‚`, as
this will be enough to generate all sets in the sigma-algebra.

This construction is very similar to that of the Borel hierarchy. -/
def generate_measurable_rec (s : set (set Î±)) : Ï‰â‚ â†’ set (set Î±)
| i := let S := â‹ƒ j : Iio i, generate_measurable_rec j.1 in
    s âˆª {âˆ…} âˆª compl '' S âˆª set.range (Î» (f : â„• â†’ S), â‹ƒ n, (f n).1)
using_well_founded {dec_tac := `[exact j.2]}

theorem self_subset_generate_measurable_rec (s : set (set Î±)) (i : Ï‰â‚) :
  s âŠ† generate_measurable_rec s i :=
begin
  unfold generate_measurable_rec,
  apply_rules [subset_union_of_subset_left],
  exact subset_rfl
end

theorem empty_mem_generate_measurable_rec (s : set (set Î±)) (i : Ï‰â‚) :
  âˆ… âˆˆ generate_measurable_rec s i :=
begin
  unfold generate_measurable_rec,
  exact mem_union_left _ (mem_union_left _ (mem_union_right _ (mem_singleton âˆ…)))
end

theorem compl_mem_generate_measurable_rec {s : set (set Î±)} {i j : Ï‰â‚} (h : j < i) {t : set Î±}
  (ht : t âˆˆ generate_measurable_rec s j) : tá¶œ âˆˆ generate_measurable_rec s i :=
begin
  unfold generate_measurable_rec,
  exact mem_union_left _ (mem_union_right _ âŸ¨t, mem_Union.2 âŸ¨âŸ¨j, hâŸ©, htâŸ©, rflâŸ©)
end

theorem Union_mem_generate_measurable_rec {s : set (set Î±)} {i : Ï‰â‚}
  {f : â„• â†’ set Î±} (hf : âˆ€ n, âˆƒ j < i, f n âˆˆ generate_measurable_rec s j) :
  (â‹ƒ n, f n) âˆˆ generate_measurable_rec s i :=
begin
  unfold generate_measurable_rec,
  exact mem_union_right _ âŸ¨Î» n, âŸ¨f n, let âŸ¨j, hj, hfâŸ© := hf n in mem_Union.2 âŸ¨âŸ¨j, hjâŸ©, hfâŸ©âŸ©, rflâŸ©
end

theorem generate_measurable_rec_subset (s : set (set Î±)) {i j : Ï‰â‚} (h : i â‰¤ j) :
  generate_measurable_rec s i âŠ† generate_measurable_rec s j :=
Î» x hx, begin
  rcases eq_or_lt_of_le h with rfl | h,
  { exact hx },
  { convert Union_mem_generate_measurable_rec (Î» n, âŸ¨i, h, hxâŸ©),
    exact (Union_const x).symm }
end

/-- At each step of the inductive construction, the cardinality bound `â‰¤ (max (#s) 2) ^ â„µâ‚€` holds.
-/
lemma cardinal_generate_measurable_rec_le (s : set (set Î±)) (i : Ï‰â‚) :
  #(generate_measurable_rec s i) â‰¤ (max (#s) 2) ^ aleph_0.{u} :=
begin
  apply (aleph 1).ord.out.wo.wf.induction i,
  assume i IH,
  have A := aleph_0_le_aleph 1,
  have B : aleph 1 â‰¤ (max (#s) 2) ^ aleph_0.{u} :=
    aleph_one_le_continuum.trans (power_le_power_right (le_max_right _ _)),
  have C : â„µâ‚€ â‰¤ (max (#s) 2) ^ aleph_0.{u} := A.trans B,
  have J : #(â‹ƒ j : Iio i, generate_measurable_rec s j.1) â‰¤ (max (#s) 2) ^ aleph_0.{u},
  { apply (mk_Union_le _).trans,
    have D : (â¨† j : Iio i, #(generate_measurable_rec s j)) â‰¤ _ := csupr_le' (Î» âŸ¨j, hjâŸ©, IH j hj),
    apply (mul_le_mul' ((mk_subtype_le _).trans (aleph 1).mk_ord_out.le) D).trans,
    rw mul_eq_max A C,
    exact max_le B le_rfl },
  rw [generate_measurable_rec],
  apply_rules [(mk_union_le _ _).trans, add_le_of_le C, mk_image_le.trans],
  { exact (le_max_left _ _).trans (self_le_power _ one_lt_aleph_0.le) },
  { rw [mk_singleton],
    exact one_lt_aleph_0.le.trans C },
  { apply mk_range_le.trans,
    simp only [mk_pi, subtype.val_eq_coe, prod_const, lift_uzero, mk_denumerable, lift_aleph_0],
    have := @power_le_power_right _ _ â„µâ‚€ J,
    rwa [â† power_mul, aleph_0_mul_aleph_0] at this }
end

/-- `generate_measurable_rec s` generates precisely the smallest sigma-algebra containing `s`. -/
theorem generate_measurable_eq_rec (s : set (set Î±)) :
  {t | generate_measurable s t} = â‹ƒ i, generate_measurable_rec s i :=
begin
  ext t, refine âŸ¨Î» ht, _, Î» ht, _âŸ©,
  { inhabit Ï‰â‚,
    induction ht with u hu u hu IH f hf IH,
    { exact mem_Union.2 âŸ¨default, self_subset_generate_measurable_rec s _ huâŸ© },
    { exact mem_Union.2 âŸ¨default, empty_mem_generate_measurable_rec s _âŸ© },
    { rcases mem_Union.1 IH with âŸ¨i, hiâŸ©,
      obtain âŸ¨j, hjâŸ© := exists_gt i,
      exact mem_Union.2 âŸ¨j, compl_mem_generate_measurable_rec hj hiâŸ© },
    { have : âˆ€ n, âˆƒ i, f n âˆˆ generate_measurable_rec s i := Î» n, by simpa using IH n,
      choose I hI using this,
      refine mem_Union.2 âŸ¨ordinal.enum (<) (ordinal.lsub (Î» n, ordinal.typein.{u} (<) (I n))) _,
        Union_mem_generate_measurable_rec (Î» n, âŸ¨I n, _, hI nâŸ©)âŸ©,
      { rw ordinal.type_lt,
        refine ordinal.lsub_lt_ord_lift _ (Î» i, ordinal.typein_lt_self _),
        rw [mk_denumerable, lift_aleph_0, is_regular_aleph_one.cof_eq],
        exact aleph_0_lt_aleph_one },
      { rw [â†ordinal.typein_lt_typein (<), ordinal.typein_enum],
        apply ordinal.lt_lsub (Î» n : â„•, _) } } },
  { rcases ht with âŸ¨t, âŸ¨i, rflâŸ©, hxâŸ©,
    revert t,
    apply (aleph 1).ord.out.wo.wf.induction i,
    intros j H t ht,
    unfold generate_measurable_rec at ht,
    rcases ht with (((h | h) | âŸ¨u, âŸ¨-, âŸ¨âŸ¨k, hkâŸ©, rflâŸ©, huâŸ©, rflâŸ©) | âŸ¨f, rflâŸ©),
    { exact generate_measurable.basic t h },
    { convert generate_measurable.empty },
    { exact generate_measurable.compl u (H k hk u hu) },
    { apply generate_measurable.union _ (Î» n, _),
      obtain âŸ¨-, âŸ¨âŸ¨k, hkâŸ©, rflâŸ©, hfâŸ© := (f n).prop,
      exact H k hk _ hf } }
end

/-- If a sigma-algebra is generated by a set of sets `s`, then the sigma-algebra has cardinality at
most `(max (#s) 2) ^ â„µâ‚€`. -/
theorem cardinal_generate_measurable_le (s : set (set Î±)) :
  #{t | generate_measurable s t} â‰¤ (max (#s) 2) ^ aleph_0.{u} :=
begin
  rw generate_measurable_eq_rec,
  apply (mk_Union_le _).trans,
  rw (aleph 1).mk_ord_out,
  refine le_trans (mul_le_mul' aleph_one_le_continuum
    (csupr_le' (Î» i, cardinal_generate_measurable_rec_le s i))) _,
  have := power_le_power_right (le_max_right (#s) 2),
  rw mul_eq_max aleph_0_le_continuum (aleph_0_le_continuum.trans this),
  exact max_le this le_rfl
end

/-- If a sigma-algebra is generated by a set of sets `s`, then the sigma
algebra has cardinality at most `(max (#s) 2) ^ â„µâ‚€`. -/
theorem cardinal_measurable_set_le (s : set (set Î±)) :
  #{t | @measurable_set Î± (generate_from s) t} â‰¤ (max (#s) 2) ^ aleph_0.{u} :=
cardinal_generate_measurable_le s

/-- If a sigma-algebra is generated by a set of sets `s` with cardinality at most the continuum,
then the sigma algebra has the same cardinality bound. -/
theorem cardinal_generate_measurable_le_continuum {s : set (set Î±)} (hs : #s â‰¤ ğ” ) :
  #{t | generate_measurable s t} â‰¤ ğ”  :=
(cardinal_generate_measurable_le s).trans begin
  rw â†continuum_power_aleph_0,
  exact_mod_cast power_le_power_right (max_le hs (nat_lt_continuum 2).le)
end

/-- If a sigma-algebra is generated by a set of sets `s` with cardinality at most the continuum,
then the sigma algebra has the same cardinality bound. -/
theorem cardinal_measurable_set_le_continuum {s : set (set Î±)} :
  #s â‰¤ ğ”  â†’ #{t | @measurable_set Î± (generate_from s) t} â‰¤ ğ”  :=
cardinal_generate_measurable_le_continuum

end measurable_space
