/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/

import measure_theory.l2_space

/-! # Conditional expectation

The conditional expectation will be defined for functions in `LÂ²` by an orthogonal projection into
a complete subspace of `LÂ²`. It will then be extended to `LÂ¹`.

For now, this file contains only the definition of the subspace of `Láµ–` containing functions which
are measurable with respect to a sub-Ïƒ-algebra, as well as a proof that it is complete.

-/

noncomputable theory
open topological_space measure_theory.Lp filter
open_locale nnreal ennreal topological_space big_operators measure_theory

namespace measure_theory

/-- A function `f` verifies `ae_measurable' m f Î¼` if it is `Î¼`-a.e. equal to an `m`-measurable
function. This is similar to `ae_measurable`, but the `measurable_space` structures used for the
measurability statement and for the measure are different. -/
def ae_measurable' {Î± Î²} [measurable_space Î²] (m : measurable_space Î±) {m0 : measurable_space Î±}
  (f : Î± â†’ Î²) (Î¼ : measure Î±) : Prop :=
âˆƒ g : Î± â†’ Î², @measurable Î± Î² m _ g âˆ§ f =áµ[Î¼] g

namespace ae_measurable'

variables {Î± Î² ğ•œ : Type*} {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  [measurable_space Î²] [measurable_space ğ•œ] {f g : Î± â†’ Î²}

lemma congr (hf : ae_measurable' m f Î¼) (hfg : f =áµ[Î¼] g) : ae_measurable' m g Î¼ :=
by { obtain âŸ¨f', hf'_meas, hff'âŸ© := hf, exact âŸ¨f', hf'_meas, hfg.symm.trans hff'âŸ©, }

lemma add [has_add Î²] [has_measurable_addâ‚‚ Î²] (hf : ae_measurable' m f Î¼)
  (hg : ae_measurable' m g Î¼) :
  ae_measurable' m (f+g) Î¼ :=
begin
  rcases hf with âŸ¨f', h_f'_meas, hff'âŸ©,
  rcases hg with âŸ¨g', h_g'_meas, hgg'âŸ©,
  exact âŸ¨f' + g', @measurable.add Î± m _ _ _ _ f' g' h_f'_meas h_g'_meas, hff'.add hgg'âŸ©,
end

lemma const_smul [has_scalar ğ•œ Î²] [has_measurable_smul ğ•œ Î²] (c : ğ•œ) (hf : ae_measurable' m f Î¼) :
  ae_measurable' m (c â€¢ f) Î¼ :=
begin
  rcases hf with âŸ¨f', h_f'_meas, hff'âŸ©,
  refine âŸ¨c â€¢ f', @measurable.const_smul Î± m _ _ _ _ _ _ f' h_f'_meas c, _âŸ©,
  exact eventually_eq.fun_comp hff' (Î» x, c â€¢ x),
end

end ae_measurable'

lemma ae_measurable'_of_ae_measurable'_trim {Î± Î²} {m m0 m0' : measurable_space Î±}
  [measurable_space Î²] (hm0 : m0 â‰¤ m0') {Î¼ : measure Î±} {f : Î± â†’ Î²}
  (hf : ae_measurable' m f (Î¼.trim hm0)) :
  ae_measurable' m f Î¼ :=
by { obtain âŸ¨g, hg_meas, hfgâŸ© := hf, exact âŸ¨g, hg_meas, ae_eq_of_ae_eq_trim hfgâŸ©, }

variables {Î± Î² Î³ E E' F F' G G' H ğ•œ : Type*} {p : â„â‰¥0âˆ}
  [is_R_or_C ğ•œ] [measurable_space ğ•œ] -- ğ•œ for â„ or â„‚, together with a measurable_space
  [measurable_space Î²] -- Î² for a generic measurable space
  -- E for an inner product space
  [inner_product_space ğ•œ E] [measurable_space E] [borel_space E] [second_countable_topology E]
  -- E' for an inner product space on which we compute integrals
  [inner_product_space ğ•œ E'] [measurable_space E'] [borel_space E'] [second_countable_topology E']
  [complete_space E'] [normed_space â„ E']
  -- F for a Lp submodule
  [normed_group F] [normed_space ğ•œ F] [measurable_space F] [borel_space F]
  [second_countable_topology F]
  -- F' for integrals on a Lp submodule
  [normed_group F'] [normed_space ğ•œ F'] [measurable_space F'] [borel_space F']
  [second_countable_topology F'] [normed_space â„ F'] [complete_space F']
  -- G for a Lp add_subgroup
  [normed_group G] [measurable_space G] [borel_space G] [second_countable_topology G]
  -- G' for integrals on a Lp add_subgroup
  [normed_group G'] [measurable_space G'] [borel_space G'] [second_countable_topology G']
  [normed_space â„ G'] [complete_space G']
  -- H for measurable space and normed group (hypotheses of mem_â„’p)
  [measurable_space H] [normed_group H]

section Lp_meas

variables (F ğ•œ)
/-- `Lp_meas F ğ•œ m p Î¼` is the subspace of `Lp F p Î¼` containing functions `f` verifying
`ae_measurable' m f Î¼`, i.e. functions which are `Î¼`-a.e. equal to an `m`-measurable function. -/
def Lp_meas [opens_measurable_space ğ•œ] (m : measurable_space Î±) [measurable_space Î±] (p : â„â‰¥0âˆ)
  (Î¼ : measure Î±) :
  submodule ğ•œ (Lp F p Î¼) :=
{ carrier   := {f : (Lp F p Î¼) | ae_measurable' m f Î¼} ,
  zero_mem' := âŸ¨(0 : Î± â†’ F), @measurable_zero _ Î± _ m _, Lp.coe_fn_zero _ _ _âŸ©,
  add_mem'  := Î» f g hf hg, (hf.add hg).congr (Lp.coe_fn_add f g).symm,
  smul_mem' := Î» c f hf, (hf.const_smul c).congr (Lp.coe_fn_smul c f).symm, }
variables {F ğ•œ}

variables [opens_measurable_space ğ•œ]

lemma mem_Lp_meas_iff_ae_measurable' {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f : Lp F p Î¼} :
  f âˆˆ Lp_meas F ğ•œ m p Î¼ â†” ae_measurable' m f Î¼ :=
by simp_rw [â† set_like.mem_coe, â† submodule.mem_carrier, Lp_meas, set.mem_set_of_eq]

lemma Lp_meas.ae_measurable' {m m0 : measurable_space Î±} {Î¼ : measure Î±} (f : Lp_meas F ğ•œ m p Î¼) :
  ae_measurable' m f Î¼ :=
mem_Lp_meas_iff_ae_measurable'.mp f.mem

lemma mem_Lp_meas_self {m0 : measurable_space Î±} (Î¼ : measure Î±) (f : Lp F p Î¼) :
  f âˆˆ Lp_meas F ğ•œ m0 p Î¼ :=
mem_Lp_meas_iff_ae_measurable'.mpr (Lp.ae_measurable f)

lemma Lp_meas_coe {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f : Lp_meas F ğ•œ m p Î¼} :
  â‡‘f = (f : Lp F p Î¼) :=
coe_fn_coe_base f

lemma mem_Lp_meas_indicator_const_Lp {m m0 : measurable_space Î±} (hm : m â‰¤ m0)
  {Î¼ : measure Î±} {s : set Î±} (hs : @measurable_set Î± m s) {hÎ¼s : Î¼ s â‰  âˆ} {c : F} :
  indicator_const_Lp p (hm s hs) hÎ¼s c âˆˆ Lp_meas F ğ•œ m p Î¼ :=
âŸ¨s.indicator (Î» x : Î±, c),
  @measurable.indicator Î± _ m _ _ s (Î» x, c) (@measurable_const _ Î± _ m _) hs,
  indicator_const_Lp_coe_fnâŸ©

section complete_subspace

/-! ## The subspace `Lp_meas` is complete.

We define a `linear_isometry_equiv` between `Lp_meas` and the `Lp` space corresponding to the
measure `Î¼.trim hm`. As a consequence, the completeness of `Lp` implies completeness of
`Lp_meas`. -/

variables {Î¹ : Type*} {m m0 : measurable_space Î±} {Î¼ : measure Î±}

/-- If `f` belongs to `Lp_meas F ğ•œ m p Î¼`, then the measurable function it is almost everywhere
equal to (given by `ae_measurable.mk`) belongs to `â„’p` for the measure `Î¼.trim hm`. -/
lemma mem_â„’p_trim_of_mem_Lp_meas (hm : m â‰¤ m0) (f : Lp F p Î¼) (hf_meas : f âˆˆ Lp_meas F ğ•œ m p Î¼) :
  @mem_â„’p Î± F m _ _ (mem_Lp_meas_iff_ae_measurable'.mp hf_meas).some p (Î¼.trim hm) :=
begin
  have hf : ae_measurable' m f Î¼, from (mem_Lp_meas_iff_ae_measurable'.mp hf_meas),
  let g := hf.some,
  obtain âŸ¨hg, hfgâŸ© := hf.some_spec,
  change @mem_â„’p Î± F m _ _ g p (Î¼.trim hm),
  refine âŸ¨@measurable.ae_measurable _ _ m _ g (Î¼.trim hm) hg, _âŸ©,
  have h_snorm_fg : @snorm Î± _ m _ g p (Î¼.trim hm) = snorm f p Î¼,
    by { rw snorm_trim hm hg, exact snorm_congr_ae hfg.symm, },
  rw h_snorm_fg,
  exact Lp.snorm_lt_top f,
end

/-- If `f` belongs to `Lp` for the measure `Î¼.trim hm`, then it belongs to the subspace
`Lp_meas F ğ•œ m p Î¼`. -/
lemma mem_Lp_meas_to_Lp_of_trim (hm : m â‰¤ m0) (f : @Lp Î± F m _ _ _ _ p (Î¼.trim hm)) :
  (mem_â„’p_of_mem_â„’p_trim hm (@Lp.mem_â„’p _ _ m _ _ _ _ _ _ f)).to_Lp f âˆˆ Lp_meas F ğ•œ m p Î¼ :=
begin
  let hf_mem_â„’p := mem_â„’p_of_mem_â„’p_trim hm (@Lp.mem_â„’p _ _ m _ _ _ _ _ _ f),
  rw mem_Lp_meas_iff_ae_measurable',
  refine ae_measurable'.congr _ (mem_â„’p.coe_fn_to_Lp hf_mem_â„’p).symm,
  refine ae_measurable'_of_ae_measurable'_trim hm _,
  exact (@Lp.ae_measurable _ _ m _ _ _ _ _ _ f),
end

variables (F ğ•œ p Î¼)
/-- Map from `Lp_meas` to `Lp F p (Î¼.trim hm)`. -/
def Lp_meas_to_Lp_trim (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) : @Lp Î± F m _ _ _ _ p (Î¼.trim hm) :=
@mem_â„’p.to_Lp _ _ m p (Î¼.trim hm) _ _ _ _ (mem_Lp_meas_iff_ae_measurable'.mp f.mem).some
  (mem_â„’p_trim_of_mem_Lp_meas hm f f.mem)

/-- Map from `Lp F p (Î¼.trim hm)` to `Lp_meas`, inverse of `Lp_meas_to_Lp_trim`. -/
def Lp_trim_to_Lp_meas (hm : m â‰¤ m0) (f : @Lp Î± F m _ _ _ _ p (Î¼.trim hm)) :
  Lp_meas F ğ•œ m p Î¼ :=
âŸ¨(mem_â„’p_of_mem_â„’p_trim hm (@Lp.mem_â„’p _ _ m _ _ _ _ _ _ f)).to_Lp f,
  mem_Lp_meas_to_Lp_of_trim hm fâŸ©

variables {F ğ•œ p Î¼}

lemma Lp_meas_to_Lp_trim_ae_eq (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f =áµ[Î¼] f :=
(ae_eq_of_ae_eq_trim
    (@mem_â„’p.coe_fn_to_Lp _ _ m _ _ _ _ _ _ _ (mem_â„’p_trim_of_mem_Lp_meas hm â†‘f f.mem))).trans
  (mem_Lp_meas_iff_ae_measurable'.mp f.mem).some_spec.2.symm

lemma Lp_trim_to_Lp_meas_ae_eq (hm : m â‰¤ m0) (f : @Lp Î± F m _ _ _ _ p (Î¼.trim hm)) :
  Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm f =áµ[Î¼] f :=
mem_â„’p.coe_fn_to_Lp _

/-- `Lp_trim_to_Lp_meas` is a right inverse of `Lp_meas_to_Lp_trim`. -/
lemma Lp_meas_to_Lp_trim_right_inv (hm : m â‰¤ m0) :
  function.right_inverse (Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm) (Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm) :=
begin
  intro f,
  ext1,
  refine ae_eq_trim_of_measurable hm _ _ _,
  { exact @Lp.measurable _ _ m _ _ _ _ _ _ _, },
  { exact @Lp.measurable _ _ m _ _ _ _ _ _ _, },
  { exact (Lp_meas_to_Lp_trim_ae_eq hm _).trans (Lp_trim_to_Lp_meas_ae_eq hm _), },
end

/-- `Lp_trim_to_Lp_meas` is a left inverse of `Lp_meas_to_Lp_trim`. -/
lemma Lp_meas_to_Lp_trim_left_inv (hm : m â‰¤ m0) :
  function.left_inverse (Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm) (Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm) :=
begin
  intro f,
  ext1,
  ext1,
  rw â† Lp_meas_coe,
  exact (Lp_trim_to_Lp_meas_ae_eq hm _).trans (Lp_meas_to_Lp_trim_ae_eq hm _),
end

lemma Lp_meas_to_Lp_trim_add (hm : m â‰¤ m0) (f g : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm (f + g)
    = Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f + Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm g :=
begin
  ext1,
  refine eventually_eq.trans _ (@Lp.coe_fn_add _ _ m _ _ _ _ _ _ _ _).symm,
  refine ae_eq_trim_of_measurable hm _ _ _,
  { exact @Lp.measurable _ _ m _ _ _ _ _ _ _, },
  { exact @measurable.add _ m _ _ _ _ _ _ (@Lp.measurable _ _ m _ _ _ _ _ _ _)
      (@Lp.measurable _ _ m _ _ _ _ _ _ _), },
  refine (Lp_meas_to_Lp_trim_ae_eq hm _).trans _,
  refine eventually_eq.trans _
    (eventually_eq.add (Lp_meas_to_Lp_trim_ae_eq hm f).symm (Lp_meas_to_Lp_trim_ae_eq hm g).symm),
  refine (Lp.coe_fn_add _ _).trans _,
  simp_rw Lp_meas_coe,
  refine eventually_of_forall (Î» x, _),
  refl,
end

lemma Lp_meas_to_Lp_trim_smul (hm : m â‰¤ m0) (c : ğ•œ) (f : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm (c â€¢ f) = c â€¢ Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f :=
begin
  ext1,
  refine eventually_eq.trans _ (@Lp.coe_fn_smul _ _ m _ _ _ _ _ _ _ _ _ _ _ _ _).symm,
  refine ae_eq_trim_of_measurable hm _ _ _,
  { exact @Lp.measurable _ _ m _ _ _ _ _ _ _, },
  { exact @measurable.const_smul _ m _ _ _ _ _ _ _ (@Lp.measurable _ _ m _ _ _ _ _ _ _) c, },
  refine (Lp_meas_to_Lp_trim_ae_eq hm _).trans _,
  refine (Lp.coe_fn_smul c _).trans _,
  refine (Lp_meas_to_Lp_trim_ae_eq hm f).mono (Î» x hx, _),
  rw [pi.smul_apply, pi.smul_apply, hx, Lp_meas_coe],
  refl,
end

/-- `Lp_meas_to_Lp_trim` preserves the norm. -/
lemma Lp_meas_to_Lp_trim_norm_map [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) :
  âˆ¥Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm fâˆ¥ = âˆ¥fâˆ¥ :=
begin
  rw [norm_def, snorm_trim hm (@Lp.measurable _ _ m _ _ _ _ _ _ _)],
  swap, { apply_instance, },
  rw [snorm_congr_ae (Lp_meas_to_Lp_trim_ae_eq hm _), Lp_meas_coe, â† norm_def],
  congr,
end

variables (F ğ•œ p Î¼)
/-- A linear isometry equivalence between `Lp_meas` and `Lp F p (Î¼.trim hm)`. -/
def Lp_meas_to_Lp_trim_lie [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0) :
  Lp_meas F ğ•œ m p Î¼ â‰ƒâ‚—áµ¢[ğ•œ] @Lp Î± F m _ _ _ _ p (Î¼.trim hm) :=
{ to_fun    := Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm,
  map_add'  := Lp_meas_to_Lp_trim_add hm,
  map_smul' := Lp_meas_to_Lp_trim_smul hm,
  inv_fun   := Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm,
  left_inv  := Lp_meas_to_Lp_trim_left_inv hm,
  right_inv := Lp_meas_to_Lp_trim_right_inv hm,
  norm_map' := Lp_meas_to_Lp_trim_norm_map hm, }
variables {F ğ•œ p Î¼}

instance [hm : fact (m â‰¤ m0)] [complete_space F] [hp : fact (1 â‰¤ p)] :
  complete_space (Lp_meas F ğ•œ m p Î¼) :=
by { rw (Lp_meas_to_Lp_trim_lie F ğ•œ p Î¼ hm.elim).to_isometric.complete_space_iff, apply_instance, }

end complete_subspace

end Lp_meas

lemma ennreal.one_le_two : (1 : â„â‰¥0âˆ) â‰¤ 2 := ennreal.coe_le_coe.2 (show (1 : â„â‰¥0) â‰¤ 2, by norm_num)

section condexp_L2_clm

variables [borel_space ğ•œ] {m m0 : measurable_space Î±} {Î¼ : measure Î±}

lemma mem_â„’p.mem_â„’p_restrict_of_le_of_measure_finite {p q : â„â‰¥0âˆ} (hpq : p â‰¤ q) {f : Î± â†’ G}
  (hf : mem_â„’p f q Î¼) {s : set Î±} (hÎ¼s : Î¼ s â‰  âˆ) :
  mem_â„’p f p (Î¼.restrict s) :=
begin
  have hf_meas_restrict : ae_measurable f (Î¼.restrict s), from (hf.restrict s).ae_measurable,
  by_cases hp_zero : p = 0,
  { rwa [hp_zero, mem_â„’p_zero_iff_ae_measurable], },
  by_cases hq_zero : q = 0,
  { rw hq_zero at hpq,
    exact absurd (le_antisymm hpq (zero_le _)) hp_zero, },
  refine âŸ¨hf_meas_restrict, _âŸ©,
  refine (snorm_le_snorm_mul_rpow_measure_univ hpq hf_meas_restrict).trans_lt _,
  refine ennreal.mul_lt_top (hf.restrict s).snorm_lt_top (ennreal.rpow_lt_top_of_nonneg _ _),
  { by_cases hq_top : q = âˆ,
    { simp [hq_top], },
    by_cases hp_top : p = âˆ,
    { rw hp_top at hpq,
      exact absurd (le_antisymm le_top hpq) hq_top, },
    rw [sub_nonneg, one_div_le_one_div],
    { rwa ennreal.to_real_le_to_real hp_top hq_top, },
    { exact ennreal.to_real_pos_iff.mpr âŸ¨(zero_le _).lt_of_ne (ne.symm hq_zero), hq_topâŸ©, },
    { exact ennreal.to_real_pos_iff.mpr âŸ¨(zero_le _).lt_of_ne (ne.symm hp_zero), hp_topâŸ©, }, },
  { simp only [set.univ_inter, measurable_set.univ, ne.def, measure.restrict_apply],
    exact hÎ¼s, },
end

lemma integrable_on_Lp_of_measure_finite (f : Lp G p Î¼) (hp : 1 â‰¤ p) {s : set Î±} (hÎ¼s : Î¼ s â‰  âˆ) :
  integrable_on f s Î¼ :=
mem_â„’p_one_iff_integrable.mp $ mem_â„’p.mem_â„’p_restrict_of_le_of_measure_finite hp (Lp.mem_â„’p _) hÎ¼s

variables (ğ•œ)
/-- Conditional expectation of a function in L2 with respect to a sigma-algebra -/
def condexp_L2_clm [complete_space E] (hm : m â‰¤ m0) :
  (Î± â†’â‚‚[Î¼] E) â†’L[ğ•œ] (Lp_meas E ğ•œ m 2 Î¼) :=
@orthogonal_projection ğ•œ (Î± â†’â‚‚[Î¼] E) _ _ (Lp_meas E ğ•œ m 2 Î¼)
  (by { haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©, exact infer_instance, })
variables {ğ•œ}

lemma inner_condexp_L2_left_eq_right (hm : m â‰¤ m0) {f g : Lp E' 2 Î¼} :
  @inner ğ•œ _ _ (condexp_L2_clm ğ•œ hm f : Lp E' 2 Î¼) g
    = inner f (condexp_L2_clm ğ•œ hm g : Lp E' 2 Î¼) :=
begin
  haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©,
  haveI : fact ((1 : â„â‰¥0âˆ) â‰¤ 2) := âŸ¨ennreal.one_le_twoâŸ©,
  exact inner_orthogonal_projection_left_eq_right _ f g,
end

lemma norm_condexp_L2_le_one (hm : m â‰¤ m0) :
  âˆ¥@condexp_L2_clm Î± E' ğ•œ _ _ _ _ _ _ _ _ _ Î¼ _ hmâˆ¥ â‰¤ 1 :=
begin
  haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©,
  haveI : fact ((1 : â„â‰¥0âˆ) â‰¤ 2) := âŸ¨ennreal.one_le_twoâŸ©,
  exact orthogonal_projection_norm_le _,
end

lemma norm_condexp_L2_apply_le (hm : m â‰¤ m0) (f : Lp E' 2 Î¼) : âˆ¥condexp_L2_clm ğ•œ hm fâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
begin
  refine ((@condexp_L2_clm Î± E' ğ•œ _ _ _ _ _ _ _ _ _ Î¼ _ hm).le_op_norm _).trans _,
  nth_rewrite 1 â† one_mul (âˆ¥fâˆ¥),
  exact mul_le_mul (norm_condexp_L2_le_one hm) le_rfl (norm_nonneg _) zero_le_one,
end

lemma snorm_condexp_L2_le (hm : m â‰¤ m0) (f : Lp E' 2 Î¼) :
  snorm (condexp_L2_clm ğ•œ hm f) 2 Î¼ â‰¤ snorm f 2 Î¼ :=
begin
  rw [Lp_meas_coe, â† ennreal.to_real_le_to_real (Lp.snorm_ne_top _) (Lp.snorm_ne_top _), â† norm_def,
    â† norm_def, submodule.norm_coe],
  exact norm_condexp_L2_apply_le hm f,
end

lemma norm_condexp_L2_coe_le (hm : m â‰¤ m0) (f : Lp E' 2 Î¼) :
  âˆ¥(condexp_L2_clm ğ•œ hm f : Lp E' 2 Î¼)âˆ¥ â‰¤ âˆ¥fâˆ¥ :=
begin
  rw [norm_def, norm_def, â† Lp_meas_coe],
  refine (ennreal.to_real_le_to_real _ (Lp.snorm_ne_top _)).mpr (snorm_condexp_L2_le hm f),
  exact Lp.snorm_ne_top _,
end

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y
local notation `âŸª`x`, `y`âŸ«'` := @inner ğ•œ E' _ x y

lemma integral_inner [is_scalar_tower â„ ğ•œ E'] {f : Î± â†’ E'} (hf : integrable f Î¼) (c : E') :
  âˆ« x, âŸªc, f xâŸ«' âˆ‚Î¼ = âŸªc, âˆ« x, f x âˆ‚Î¼âŸ«' :=
((@inner_right ğ•œ E' _ _ c).restrict_scalars â„).integral_comp_comm hf

lemma integral_zero_of_forall_integral_inner_zero [is_scalar_tower â„ ğ•œ E'] (f : Î± â†’ E')
  (hf : integrable f Î¼) (hf_int : âˆ€ (c : E'), âˆ« x, âŸªc, f xâŸ«' âˆ‚Î¼ = 0) :
  âˆ« x, f x âˆ‚Î¼ = 0 :=
by { specialize hf_int (âˆ« x, f x âˆ‚Î¼), rwa [integral_inner hf, inner_self_eq_zero] at hf_int }

lemma inner_indicator_const_Lp (f : Lp E 2 Î¼) {s : set Î±} (hs : measurable_set s) (c : E)
  (hÎ¼s : Î¼ s â‰  âˆ) :
  inner (indicator_const_Lp 2 hs hÎ¼s c) f = âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼ :=
begin
  simp_rw L2.inner_def,
  rw â† integral_add_compl hs (L2.integrable_inner _ f),
  have h_left : âˆ« x in s, âŸª(indicator_const_Lp 2 hs hÎ¼s c) x, f xâŸ« âˆ‚Î¼ = âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼,
  { suffices h_ae_eq : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âŸªindicator_const_Lp 2 hs hÎ¼s c x, f xâŸ« = âŸªc, f xâŸ«,
      from set_integral_congr_ae hs h_ae_eq,
    have h_indicator : âˆ€áµ (x : Î±) âˆ‚Î¼, x âˆˆ s â†’ (indicator_const_Lp 2 hs hÎ¼s c x) = c,
      from indicator_const_Lp_coe_fn_mem,
    refine h_indicator.mono (Î» x hx hxs, _),
    congr,
    exact hx hxs, },
  have h_right : âˆ« x in sá¶œ, âŸª(indicator_const_Lp 2 hs hÎ¼s c) x, f xâŸ« âˆ‚Î¼ = 0,
  { suffices h_ae_eq : âˆ€áµ x âˆ‚Î¼, x âˆ‰ s â†’ âŸªindicator_const_Lp 2 hs hÎ¼s c x, f xâŸ« = 0,
    { simp_rw â† set.mem_compl_iff at h_ae_eq,
      suffices h_int_zero : âˆ« x in sá¶œ, inner (indicator_const_Lp 2 hs hÎ¼s c x) (f x) âˆ‚Î¼
        = âˆ« x in sá¶œ, (0 : ğ•œ) âˆ‚Î¼,
      { rw h_int_zero,
        simp, },
      exact set_integral_congr_ae hs.compl h_ae_eq, },
    have h_indicator : âˆ€áµ (x : Î±) âˆ‚Î¼, x âˆ‰ s â†’ (indicator_const_Lp 2 hs hÎ¼s c x) = 0,
      from indicator_const_Lp_coe_fn_nmem,
    refine h_indicator.mono (Î» x hx hxs, _),
    rw hx hxs,
    exact inner_zero_left, },
  rw [h_left, h_right, add_zero],
end

variables (ğ•œ)
lemma inner_set_integral_eq_inner_indicator [is_scalar_tower â„ ğ•œ E'] {s : set Î±}
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E') (f : Lp E' 2 Î¼) :
  @inner ğ•œ _ _ c (âˆ« x in s, f x âˆ‚Î¼) = inner (indicator_const_Lp 2 hs hÎ¼s c) f :=
begin
  rw â† integral_inner (integrable_on_Lp_of_measure_finite f ennreal.one_le_two hÎ¼s),
  simp_rw inner,
  rw â† integral_indicator hs,
  refine integral_congr_ae _,
  refine (@indicator_const_Lp_coe_fn _ _ _ 2 _ _ _ s hs hÎ¼s c _ _).mono (Î» x hx, _),
  dsimp only,
  rw hx,
  simp_rw set.indicator_apply,
  by_cases hx_mem : x âˆˆ s; simp [hx_mem],
end
variables {ğ•œ}

lemma set_integral_eq_inner_indicator (hm : m â‰¤ m0) {s : set Î±} (hs : @measurable_set Î± m s)
  (hÎ¼s : Î¼ s â‰  âˆ) (f : Lp â„ 2 Î¼) :
  âˆ« x in s, f x âˆ‚Î¼ = inner (indicator_const_Lp 2 (hm s hs) hÎ¼s (1 : â„)) f :=
begin
  rw â† inner_set_integral_eq_inner_indicator â„ (hm s hs) hÎ¼s (1 : â„) f,
  simp only [is_R_or_C.inner_apply, is_R_or_C.conj_to_real, one_mul],
end

section fin_meas_sets

variables (hm : m â‰¤ m0) {s t : set Î±} {f : Lp â„ 2 Î¼}

lemma condexp_L2_indicator_of_measurable (hs : @measurable_set _ m s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E') :
  (condexp_L2_clm ğ•œ hm (indicator_const_Lp 2 (hm s hs) hÎ¼s c) : Lp E' 2 Î¼)
    = indicator_const_Lp 2 (hm s hs) hÎ¼s c :=
begin
  rw condexp_L2_clm,
  haveI : fact(m â‰¤ m0) := âŸ¨hmâŸ©,
  have h_mem : indicator_const_Lp 2 (hm s hs) hÎ¼s c âˆˆ Lp_meas E' ğ•œ m 2 Î¼,
    from mem_Lp_meas_indicator_const_Lp hm hs,
  let ind := (âŸ¨indicator_const_Lp 2 (hm s hs) hÎ¼s c, h_memâŸ© : Lp_meas E' ğ•œ m 2 Î¼),
  have h_coe_ind : (ind : Lp E' 2 Î¼) = indicator_const_Lp 2 (hm s hs) hÎ¼s c, by refl,
  have h_orth_mem := orthogonal_projection_mem_subspace_eq_self ind,
  rw [â† h_coe_ind, h_orth_mem],
end

lemma inner_condexp_L2_eq_inner_fun (f g : Lp E' 2 Î¼) (hg : ae_measurable' m g Î¼) :
  @inner ğ•œ _ _ (â†‘(condexp_L2_clm ğ•œ hm f) : Lp E' 2 Î¼) g = inner f g :=
begin
  symmetry,
  rw [â† sub_eq_zero, â† inner_sub_left, condexp_L2_clm],
  simp only [mem_Lp_meas_iff_ae_measurable'.mpr hg, orthogonal_projection_inner_eq_zero],
end

lemma integrable_on_condexp_L2_of_measure_finite (hÎ¼s : Î¼ s â‰  âˆ) (f : Lp E' 2 Î¼) :
  integrable_on (condexp_L2_clm ğ•œ hm f) s Î¼ :=
integrable_on_Lp_of_measure_finite ((condexp_L2_clm ğ•œ hm f) : Lp E' 2 Î¼) ennreal.one_le_two hÎ¼s

lemma integrable_condexp_L2_of_finite_measure [finite_measure Î¼] {f : Lp E' 2 Î¼} :
  integrable (condexp_L2_clm ğ•œ hm f) Î¼ :=
integrable_on_univ.mp $ integrable_on_condexp_L2_of_measure_finite hm (measure_ne_top _ _) f

end fin_meas_sets

end condexp_L2_clm

end measure_theory
