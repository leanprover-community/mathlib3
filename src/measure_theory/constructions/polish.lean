/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.metric_space.polish
import measure_theory.constructions.borel_space

/-!
# The Borel sigma-algebra on Polish spaces

We discuss several results pertaining to the relationship between the topology and the Borel
structure on Polish spaces.

## Main definitions and results

First, we define the class of analytic sets and establish its basic properties.

* `measure_theory.analytic_set s`: a set in a topological space is analytic if it is the continuous
  image of a Polish space. Equivalently, it is empty, or the image of `â„• â†’ â„•`.
* `measure_theory.analytic_set.image_of_continuous`: a continuous image of an analytic set is
  analytic.
* `measurable_set.analytic_set`: in a Polish space, any Borel-measurable set is analytic.

Then, we show Lusin's theorem that two disjoint analytic sets can be separated by Borel sets.

* `measurably_separable s t` states that there exists a measurable set containing `s` and disjoint
  from `t`.
* `analytic_set.measurably_separable` shows that two disjoint analytic sets are separated by a
  Borel set.

Finally, we prove the Lusin-Souslin theorem that a continuous injective image of a Borel subset of
a Polish space is Borel. The proof of this nontrivial result relies on the above results on
analytic sets.

* `measurable_set.image_of_continuous_on_inj_on` asserts that, if `s` is a Borel measurable set in
  a Polish space, then the image of `s` under a continuous injective map is still Borel measurable.
* `continuous.measurable_embedding` states that a continuous injective map on a Polish space
  is a measurable embedding for the Borel sigma-algebra.
* `continuous_on.measurable_embedding` is the same result for a map restricted to a measurable set
  on which it is continuous.
* `measurable.measurable_embedding` states that a measurable injective map from a Polish space
  to a second-countable topological space is a measurable embedding.
* `is_clopenable_iff_measurable_set`: in a Polish space, a set is clopenable (i.e., it can be made
  open and closed by using a finer Polish topology) if and only if it is Borel-measurable.
-/

open set function polish_space pi_nat topological_space metric filter
open_locale topology measure_theory filter

variables {Î± : Type*} [topological_space Î±] {Î¹ : Type*}

namespace measure_theory

/-! ### Analytic sets -/

/-- An analytic set is a set which is the continuous image of some Polish space. There are several
equivalent characterizations of this definition. For the definition, we pick one that avoids
universe issues: a set is analytic if and only if it is a continuous image of `â„• â†’ â„•` (or if it
is empty). The above more usual characterization is given
in `analytic_set_iff_exists_polish_space_range`.

Warning: these are analytic sets in the context of descriptive set theory (which is why they are
registered in the namespace `measure_theory`). They have nothing to do with analytic sets in the
context of complex analysis. -/
@[irreducible] def analytic_set (s : set Î±) : Prop :=
s = âˆ… âˆ¨ âˆƒ (f : (â„• â†’ â„•) â†’ Î±), continuous f âˆ§ range f = s

lemma analytic_set_empty : analytic_set (âˆ… : set Î±) :=
begin
  rw analytic_set,
  exact or.inl rfl
end

lemma analytic_set_range_of_polish_space
  {Î² : Type*} [topological_space Î²] [polish_space Î²] {f : Î² â†’ Î±} (f_cont : continuous f) :
  analytic_set (range f) :=
begin
  casesI is_empty_or_nonempty Î²,
  { rw range_eq_empty,
    exact analytic_set_empty },
  { rw analytic_set,
    obtain âŸ¨g, g_cont, hgâŸ© : âˆƒ (g : (â„• â†’ â„•) â†’ Î²), continuous g âˆ§ surjective g :=
      exists_nat_nat_continuous_surjective Î²,
    refine or.inr âŸ¨f âˆ˜ g, f_cont.comp g_cont, _âŸ©,
    rwa hg.range_comp }
end

/-- The image of an open set under a continuous map is analytic. -/
lemma _root_.is_open.analytic_set_image {Î² : Type*} [topological_space Î²] [polish_space Î²]
  {s : set Î²} (hs : is_open s) {f : Î² â†’ Î±} (f_cont : continuous f) :
  analytic_set (f '' s) :=
begin
  rw image_eq_range,
  haveI : polish_space s := hs.polish_space,
  exact analytic_set_range_of_polish_space (f_cont.comp continuous_subtype_coe),
end

/-- A set is analytic if and only if it is the continuous image of some Polish space. -/
theorem analytic_set_iff_exists_polish_space_range {s : set Î±} :
  analytic_set s â†” âˆƒ (Î² : Type) (h : topological_space Î²) (h' : @polish_space Î² h) (f : Î² â†’ Î±),
    @continuous _ _ h _ f âˆ§ range f = s :=
begin
  split,
  { assume h,
    rw analytic_set at h,
    cases h,
    { refine âŸ¨empty, by apply_instance, by apply_instance, empty.elim, continuous_bot, _âŸ©,
      rw h,
      exact range_eq_empty _ },
    { exact âŸ¨â„• â†’ â„•, by apply_instance, by apply_instance, hâŸ© } },
  { rintros âŸ¨Î², h, h', f, f_cont, f_rangeâŸ©,
    resetI,
    rw â† f_range,
    exact analytic_set_range_of_polish_space f_cont }
end

/-- The continuous image of an analytic set is analytic -/
lemma analytic_set.image_of_continuous_on {Î² : Type*} [topological_space Î²]
  {s : set Î±} (hs : analytic_set s) {f : Î± â†’ Î²} (hf : continuous_on f s) :
  analytic_set (f '' s) :=
begin
  rcases analytic_set_iff_exists_polish_space_range.1 hs with âŸ¨Î³, Î³top, Î³polish, g, g_cont, gsâŸ©,
  resetI,
  have : f '' s = range (f âˆ˜ g), by rw [range_comp, gs],
  rw this,
  apply analytic_set_range_of_polish_space,
  apply hf.comp_continuous g_cont (Î» x, _),
  rw â† gs,
  exact mem_range_self _
end

lemma analytic_set.image_of_continuous {Î² : Type*} [topological_space Î²]
  {s : set Î±} (hs : analytic_set s) {f : Î± â†’ Î²} (hf : continuous f) :
  analytic_set (f '' s) :=
hs.image_of_continuous_on hf.continuous_on

/-- A countable intersection of analytic sets is analytic. -/
theorem analytic_set.Inter [hÎ¹ : nonempty Î¹] [countable Î¹] [t2_space Î±]
  {s : Î¹ â†’ set Î±} (hs : âˆ€ n, analytic_set (s n)) :
  analytic_set (â‹‚ n, s n) :=
begin
  unfreezingI { rcases hÎ¹ with âŸ¨iâ‚€âŸ© },
  /- For the proof, write each `s n` as the continuous image under a map `f n` of a
  Polish space `Î² n`. The product space `Î³ = Î  n, Î² n` is also Polish, and so is the subset
  `t` of sequences `x n` for which `f n (x n)` is independent of `n`. The set `t` is Polish, and the
  range of `x â†¦ f 0 (x 0)` on `t` is exactly `â‹‚ n, s n`, so this set is analytic. -/
  choose Î² hÎ² h'Î² f f_cont f_range using Î» n, analytic_set_iff_exists_polish_space_range.1 (hs n),
  resetI,
  let Î³ := Î  n, Î² n,
  let t : set Î³ := â‹‚ n, {x | f n (x n) = f iâ‚€ (x iâ‚€)},
  have t_closed : is_closed t,
  { apply is_closed_Inter,
    assume n,
    exact is_closed_eq ((f_cont n).comp (continuous_apply n))
      ((f_cont iâ‚€).comp (continuous_apply iâ‚€)) },
  haveI : polish_space t := t_closed.polish_space,
  let F : t â†’ Î± := Î» x, f iâ‚€ ((x : Î³) iâ‚€),
  have F_cont : continuous F :=
    (f_cont iâ‚€).comp ((continuous_apply iâ‚€).comp continuous_subtype_coe),
  have F_range : range F = â‹‚ (n : Î¹), s n,
  { apply subset.antisymm,
    { rintros y âŸ¨x, rflâŸ©,
      apply mem_Inter.2 (Î» n, _),
      have : f n ((x : Î³) n) = F x := (mem_Inter.1 x.2 n : _),
      rw [â† this, â† f_range n],
      exact mem_range_self _ },
    { assume y hy,
      have A : âˆ€ n, âˆƒ (x : Î² n), f n x = y,
      { assume n,
        rw [â† mem_range, f_range n],
        exact mem_Inter.1 hy n },
      choose x hx using A,
      have xt : x âˆˆ t,
      { apply mem_Inter.2 (Î» n, _),
        simp [hx] },
        refine âŸ¨âŸ¨x, xtâŸ©, _âŸ©,
        exact hx iâ‚€ } },
  rw â† F_range,
  exact analytic_set_range_of_polish_space F_cont,
end

/-- A countable union of analytic sets is analytic. -/
theorem analytic_set.Union [countable Î¹] {s : Î¹ â†’ set Î±} (hs : âˆ€ n, analytic_set (s n)) :
  analytic_set (â‹ƒ n, s n) :=
begin
  /- For the proof, write each `s n` as the continuous image under a map `f n` of a
  Polish space `Î² n`. The union space `Î³ = Î£ n, Î² n` is also Polish, and the map `F : Î³ â†’ Î±` which
  coincides with `f n` on `Î² n` sends it to `â‹ƒ n, s n`. -/
  choose Î² hÎ² h'Î² f f_cont f_range using Î» n, analytic_set_iff_exists_polish_space_range.1 (hs n),
  resetI,
  let Î³ := Î£ n, Î² n,
  let F : Î³ â†’ Î± := by { rintros âŸ¨n, xâŸ©, exact f n x },
  have F_cont : continuous F := continuous_sigma f_cont,
  have F_range : range F = â‹ƒ n, s n,
  { rw [range_sigma_eq_Union_range],
    congr,
    ext1 n,
    rw â† f_range n },
  rw â† F_range,
  exact analytic_set_range_of_polish_space F_cont,
end

theorem _root_.is_closed.analytic_set [polish_space Î±] {s : set Î±} (hs : is_closed s) :
  analytic_set s :=
begin
  haveI : polish_space s := hs.polish_space,
  rw â† @subtype.range_val Î± s,
  exact analytic_set_range_of_polish_space continuous_subtype_coe,
end

/-- Given a Borel-measurable set in a Polish space, there exists a finer Polish topology making
it clopen. This is in fact an equivalence, see `is_clopenable_iff_measurable_set`. -/
lemma _root_.measurable_set.is_clopenable [polish_space Î±] [measurable_space Î±] [borel_space Î±]
  {s : set Î±} (hs : measurable_set s) :
  is_clopenable s :=
begin
  revert s,
  apply measurable_set.induction_on_open,
  { exact Î» u hu, hu.is_clopenable },
  { exact Î» u hu h'u, h'u.compl },
  { exact Î» f f_disj f_meas hf, is_clopenable.Union hf }
end

theorem _root_.measurable_set.analytic_set
  {Î± : Type*} [t : topological_space Î±] [polish_space Î±] [measurable_space Î±] [borel_space Î±]
  {s : set Î±} (hs : measurable_set s) :
  analytic_set s :=
begin
  /- For a short proof (avoiding measurable induction), one sees `s` as a closed set for a finer
  topology `t'`. It is analytic for this topology. As the identity from `t'` to `t` is continuous
  and the image of an analytic set is analytic, it follows that `s` is also analytic for `t`. -/
  obtain âŸ¨t', t't, t'_polish, s_closed, s_openâŸ© :
    âˆƒ t' : topological_space Î±, t' â‰¤ t âˆ§ @polish_space Î± t' âˆ§ is_closed[t'] s âˆ§ is_open[t'] s :=
    hs.is_clopenable,
  have A := @is_closed.analytic_set Î± t' t'_polish s s_closed,
  convert @analytic_set.image_of_continuous Î± t' Î± t s A id (continuous_id_of_le t't),
  simp only [id.def, image_id'],
end

/-- Given a Borel-measurable function from a Polish space to a second-countable space, there exists
a finer Polish topology on the source space for which the function is continuous. -/
lemma _root_.measurable.exists_continuous {Î± Î² : Type*}
  [t : topological_space Î±] [polish_space Î±] [measurable_space Î±] [borel_space Î±]
  [tÎ² : topological_space Î²] [second_countable_topology Î²] [measurable_space Î²] [borel_space Î²]
  {f : Î± â†’ Î²} (hf : measurable f) :
  âˆƒ (t' : topological_space Î±), t' â‰¤ t âˆ§ @continuous Î± Î² t' tÎ² f âˆ§ @polish_space Î± t' :=
begin
  obtain âŸ¨b, b_count, -, hbâŸ© : âˆƒb : set (set Î²), b.countable âˆ§ âˆ… âˆ‰ b âˆ§ is_topological_basis b :=
    exists_countable_basis Î²,
  haveI : encodable b := b_count.to_encodable,
  have : âˆ€ (s : b), is_clopenable (f â»Â¹' s),
  { assume s,
    apply measurable_set.is_clopenable,
    exact hf (hb.is_open s.2).measurable_set },
  choose T Tt Tpolish Tclosed Topen using this,
  obtain âŸ¨t', t'T, t't, t'_polishâŸ© :
    âˆƒ (t' : topological_space Î±), (âˆ€ i, t' â‰¤ T i) âˆ§ (t' â‰¤ t) âˆ§ @polish_space Î± t' :=
      exists_polish_space_forall_le T Tt Tpolish,
  refine âŸ¨t', t't, _, t'_polishâŸ©,
  apply hb.continuous _ (Î» s hs, _),
  exact t'T âŸ¨s, hsâŸ© _ (Topen âŸ¨s, hsâŸ©),
end

/-! ### Separating sets with measurable sets -/

/-- Two sets `u` and `v` in a measurable space are measurably separable if there
exists a measurable set containing `u` and disjoint from `v`.
This is mostly interesting for Borel-separable sets. -/
def measurably_separable {Î± : Type*} [measurable_space Î±] (s t : set Î±) : Prop :=
âˆƒ u, s âŠ† u âˆ§ disjoint t u âˆ§ measurable_set u

lemma measurably_separable.Union [countable Î¹]
  {Î± : Type*} [measurable_space Î±] {s t : Î¹ â†’ set Î±}
  (h : âˆ€ m n, measurably_separable (s m) (t n)) :
  measurably_separable (â‹ƒ n, s n) (â‹ƒ m, t m) :=
begin
  choose u hsu htu hu using h,
  refine âŸ¨â‹ƒ m, (â‹‚ n, u m n), _, _, _âŸ©,
  { refine Union_subset (Î» m, subset_Union_of_subset m _),
    exact subset_Inter (Î» n, hsu m n) },
  { simp_rw [disjoint_Union_left, disjoint_Union_right],
    assume n m,
    apply disjoint.mono_right _ (htu m n),
    apply Inter_subset },
  { refine measurable_set.Union (Î» m, _),
    exact measurable_set.Inter (Î» n, hu m n) }
end

/-- The hard part of the Lusin separation theorem saying that two disjoint analytic sets are
contained in disjoint Borel sets (see the full statement in `analytic_set.measurably_separable`).
Here, we prove this when our analytic sets are the ranges of functions from `â„• â†’ â„•`.
-/
lemma measurably_separable_range_of_disjoint [t2_space Î±] [measurable_space Î±] [borel_space Î±]
  {f g : (â„• â†’ â„•) â†’ Î±} (hf : continuous f) (hg : continuous g) (h : disjoint (range f) (range g)) :
  measurably_separable (range f) (range g) :=
begin
  /- We follow [Kechris, *Classical Descriptive Set Theory* (Theorem 14.7)][kechris1995].
  If the ranges are not Borel-separated, then one can find two cylinders of length one whose images
  are not Borel-separated, and then two smaller cylinders of length two whose images are not
  Borel-separated, and so on. One thus gets two sequences of cylinders, that decrease to two
  points `x` and `y`. Their images are different by the disjointness assumption, hence contained
  in two disjoint open sets by the T2 property. By continuity, long enough cylinders around `x`
  and `y` have images which are separated by these two disjoint open sets, a contradiction.
  -/
  by_contra hfg,
  have I : âˆ€ n x y, (Â¬(measurably_separable (f '' (cylinder x n)) (g '' (cylinder y n))))
    â†’ âˆƒ x' y', x' âˆˆ cylinder x n âˆ§ y' âˆˆ cylinder y n âˆ§
                Â¬(measurably_separable (f '' (cylinder x' (n+1))) (g '' (cylinder y' (n+1)))),
  { assume n x y,
    contrapose!,
    assume H,
    rw [â† Union_cylinder_update x n, â† Union_cylinder_update y n, image_Union, image_Union],
    refine measurably_separable.Union (Î» i j, _),
    exact H _ _ (update_mem_cylinder _ _ _) (update_mem_cylinder _ _ _) },
  -- consider the set of pairs of cylinders of some length whose images are not Borel-separated
  let A := {p : â„• Ã— (â„• â†’ â„•) Ã— (â„• â†’ â„•) //
              Â¬(measurably_separable (f '' (cylinder p.2.1 p.1)) (g '' (cylinder p.2.2 p.1)))},
  -- for each such pair, one can find longer cylinders whose images are not Borel-separated either
  have : âˆ€ (p : A), âˆƒ (q : A), q.1.1 = p.1.1 + 1 âˆ§ q.1.2.1 âˆˆ cylinder p.1.2.1 p.1.1
    âˆ§ q.1.2.2 âˆˆ cylinder p.1.2.2 p.1.1,
  { rintros âŸ¨âŸ¨n, x, yâŸ©, hpâŸ©,
    rcases I n x y hp with âŸ¨x', y', hx', hy', h'âŸ©,
    exact âŸ¨âŸ¨âŸ¨n+1, x', y'âŸ©, h'âŸ©, rfl, hx', hy'âŸ© },
  choose F hFn hFx hFy using this,
  let p0 : A := âŸ¨âŸ¨0, Î» n, 0, Î» n, 0âŸ©, by simp [hfg]âŸ©,
  -- construct inductively decreasing sequences of cylinders whose images are not separated
  let p : â„• â†’ A := Î» n, F^[n] p0,
  have prec : âˆ€ n, p (n+1) = F (p n) := Î» n, by simp only [p, iterate_succ'],
  -- check that at the `n`-th step we deal with cylinders of length `n`
  have pn_fst : âˆ€ n, (p n).1.1 = n,
  { assume n,
    induction n with n IH,
    { refl },
    { simp only [prec, hFn, IH] } },
  -- check that the cylinders we construct are indeed decreasing, by checking that the coordinates
  -- are stationary.
  have Ix : âˆ€ m n, m + 1 â‰¤ n â†’ (p n).1.2.1 m = (p (m+1)).1.2.1 m,
  { assume m,
    apply nat.le_induction,
    { refl },
    assume n hmn IH,
    have I : (F (p n)).val.snd.fst m = (p n).val.snd.fst m,
    { apply hFx (p n) m,
      rw pn_fst,
      exact hmn },
    rw [prec, I, IH] },
  have Iy : âˆ€ m n, m + 1 â‰¤ n â†’ (p n).1.2.2 m = (p (m+1)).1.2.2 m,
  { assume m,
    apply nat.le_induction,
    { refl },
    assume n hmn IH,
    have I : (F (p n)).val.snd.snd m = (p n).val.snd.snd m,
    { apply hFy (p n) m,
      rw pn_fst,
      exact hmn },
    rw [prec, I, IH] },
  -- denote by `x` and `y` the limit points of these two sequences of cylinders.
  set x : â„• â†’ â„• := Î» n, (p (n+1)).1.2.1 n with hx,
  set y : â„• â†’ â„• := Î» n, (p (n+1)).1.2.2 n with hy,
  -- by design, the cylinders around these points have images which are not Borel-separable.
  have M : âˆ€ n, Â¬(measurably_separable (f '' (cylinder x n)) (g '' (cylinder y n))),
  { assume n,
    convert (p n).2 using 3,
    { rw [pn_fst, â† mem_cylinder_iff_eq, mem_cylinder_iff],
      assume i hi,
      rw hx,
      exact (Ix i n hi).symm },
    { rw [pn_fst, â† mem_cylinder_iff_eq, mem_cylinder_iff],
      assume i hi,
      rw hy,
      exact (Iy i n hi).symm } },
  -- consider two open sets separating `f x` and `g y`.
  obtain âŸ¨u, v, u_open, v_open, xu, yv, huvâŸ© :
    âˆƒ u v : set Î±, is_open u âˆ§ is_open v âˆ§ f x âˆˆ u âˆ§ g y âˆˆ v âˆ§ disjoint u v,
  { apply t2_separation,
    exact disjoint_iff_forall_ne.1 h _ (mem_range_self _) _ (mem_range_self _) },
  letI : metric_space (â„• â†’ â„•) := metric_space_nat_nat,
  obtain âŸ¨Îµx, Îµxpos, hÎµxâŸ© : âˆƒ (Îµx : â„) (H : Îµx > 0), metric.ball x Îµx âŠ† f â»Â¹' u,
  { apply metric.mem_nhds_iff.1,
    exact hf.continuous_at.preimage_mem_nhds (u_open.mem_nhds xu) },
  obtain âŸ¨Îµy, Îµypos, hÎµyâŸ© : âˆƒ (Îµy : â„) (H : Îµy > 0), metric.ball y Îµy âŠ† g â»Â¹' v,
  { apply metric.mem_nhds_iff.1,
    exact hg.continuous_at.preimage_mem_nhds (v_open.mem_nhds yv) },
  obtain âŸ¨n, hnâŸ© : âˆƒ (n : â„•), (1/2 : â„)^n < min Îµx Îµy :=
    exists_pow_lt_of_lt_one (lt_min Îµxpos Îµypos) (by norm_num),
  -- for large enough `n`, these open sets separate the images of long cylinders around `x` and `y`
  have B : measurably_separable (f '' (cylinder x n)) (g '' (cylinder y n)),
  { refine âŸ¨u, _, _, u_open.measurable_setâŸ©,
    { rw image_subset_iff,
      apply subset.trans _ hÎµx,
      assume z hz,
      rw mem_cylinder_iff_dist_le at hz,
      exact hz.trans_lt (hn.trans_le (min_le_left _ _)) },
    { refine disjoint.mono_left _ huv.symm,
      change g '' cylinder y n âŠ† v,
      rw image_subset_iff,
      apply subset.trans _ hÎµy,
      assume z hz,
      rw mem_cylinder_iff_dist_le at hz,
      exact hz.trans_lt (hn.trans_le (min_le_right _ _)) } },
  -- this is a contradiction.
  exact M n B
end

/-- The Lusin separation theorem: if two analytic sets are disjoint, then they are contained in
disjoint Borel sets. -/
theorem analytic_set.measurably_separable [t2_space Î±] [measurable_space Î±] [borel_space Î±]
  {s t : set Î±} (hs : analytic_set s) (ht : analytic_set t) (h : disjoint s t) :
  measurably_separable s t :=
begin
  rw analytic_set at hs ht,
  rcases hs with rfl|âŸ¨f, f_cont, rflâŸ©,
  { refine âŸ¨âˆ…, subset.refl _, by simp, measurable_set.emptyâŸ© },
  rcases ht with rfl|âŸ¨g, g_cont, rflâŸ©,
  { exact âŸ¨univ, subset_univ _, by simp, measurable_set.univâŸ© },
  exact measurably_separable_range_of_disjoint f_cont g_cont h,
end

/-! ### Injective images of Borel sets -/

variables {Î³ : Type*} [tÎ³ : topological_space Î³] [polish_space Î³]
include tÎ³

/-- The Lusin-Souslin theorem: the range of a continuous injective function defined on a Polish
space is Borel-measurable. -/
theorem measurable_set_range_of_continuous_injective {Î² : Type*}
  [topological_space Î²] [t2_space Î²] [measurable_space Î²] [borel_space Î²]
  {f : Î³ â†’ Î²} (f_cont : continuous f) (f_inj : injective f) :
  measurable_set (range f) :=
begin
  /- We follow [Fremlin, *Measure Theory* (volume 4, 423I)][fremlin_vol4].
  Let `b = {s i}` be a countable basis for `Î±`. When `s i` and `s j` are disjoint, their images are
  disjoint analytic sets, hence by the separation theorem one can find a Borel-measurable set
  `q i j` separating them.
  Let `E i = closure (f '' s i) âˆ© â‹‚ j, q i j \ q j i`. It contains `f '' (s i)` and it is
  measurable. Let `F n = â‹ƒ E i`, where the union is taken over those `i` for which `diam (s i)`
  is bounded by some number `u n` tending to `0` with `n`.
  We claim that `range f = â‹‚ F n`, from which the measurability is obvious. The inclusion `âŠ†` is
  straightforward. To show `âŠ‡`, consider a point `x` in the intersection. For each `n`, it belongs
  to some `E i` with `diam (s i) â‰¤ u n`. Pick a point `y i âˆˆ s i`. We claim that for such `i`
  and `j`, the intersection `s i âˆ© s j` is nonempty: if it were empty, then thanks to the
  separating set `q i j` in the definition of `E i` one could not have `x âˆˆ E i âˆ© E j`.
  Since these two sets have small diameter, it follows that `y i` and `y j` are close.
  Thus, `y` is a Cauchy sequence, converging to a limit `z`. We claim that `f z = x`, completing
  the proof.
  Otherwise, one could find open sets `v` and `w` separating `f z` from `x`. Then, for large `n`,
  the image `f '' (s i)` would be included in `v` by continuity of `f`, so its closure would be
  contained in the closure of `v`, and therefore it would be disjoint from `w`. This is a
  contradiction since `x` belongs both to this closure and to `w`. -/
  letI := upgrade_polish_space Î³,
  obtain âŸ¨b, b_count, b_nonempty, hbâŸ© :
    âˆƒ b : set (set Î³), b.countable âˆ§ âˆ… âˆ‰ b âˆ§ is_topological_basis b := exists_countable_basis Î³,
  haveI : encodable b := b_count.to_encodable,
  let A := {p : b Ã— b // disjoint (p.1 : set Î³) p.2},
  -- for each pair of disjoint sets in the topological basis `b`, consider Borel sets separating
  -- their images, by injectivity of `f` and the Lusin separation theorem.
  have : âˆ€ (p : A), âˆƒ (q : set Î²), f '' (p.1.1 : set Î³) âŠ† q âˆ§ disjoint (f '' (p.1.2 : set Î³)) q
    âˆ§ measurable_set q,
  { assume p,
    apply analytic_set.measurably_separable ((hb.is_open p.1.1.2).analytic_set_image f_cont)
      ((hb.is_open p.1.2.2).analytic_set_image f_cont),
    exact disjoint.image p.2 (f_inj.inj_on univ) (subset_univ _) (subset_univ _) },
  choose q hq1 hq2 q_meas using this,
  -- define sets `E i` and `F n` as in the proof sketch above
  let E : b â†’ set Î² := Î» s, closure (f '' s) âˆ©
    (â‹‚ (t : b) (ht : disjoint s.1 t.1), q âŸ¨(s, t), htâŸ© \ q âŸ¨(t, s), ht.symmâŸ©),
  obtain âŸ¨u, u_anti, u_pos, u_limâŸ© :
    âˆƒ (u : â„• â†’ â„), strict_anti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ tendsto u at_top (ğ“ 0) :=
      exists_seq_strict_anti_tendsto (0 : â„),
  let F : â„• â†’ set Î² := Î» n, â‹ƒ (s : b) (hs : bounded s.1 âˆ§ diam s.1 â‰¤ u n), E s,
  -- it is enough to show that `range f = â‹‚ F n`, as the latter set is obviously measurable.
  suffices : range f = â‹‚ n, F n,
  { have E_meas : âˆ€ (s : b), measurable_set (E s),
    { assume b,
      refine is_closed_closure.measurable_set.inter _,
      refine measurable_set.Inter (Î» s, _),
      exact measurable_set.Inter (Î» hs, (q_meas _).diff (q_meas _)) },
    have F_meas : âˆ€ n, measurable_set (F n),
    { assume n,
      refine measurable_set.Union (Î» s, _),
      exact measurable_set.Union (Î» hs, E_meas _) },
    rw this,
    exact measurable_set.Inter (Î» n, F_meas n) },
  -- we check both inclusions.
  apply subset.antisymm,
  -- we start with the easy inclusion `range f âŠ† â‹‚ F n`. One just needs to unfold the definitions.
  { rintros x âŸ¨y, rflâŸ©,
    apply mem_Inter.2 (Î» n, _),
    obtain âŸ¨s, sb, ys, hsâŸ© : âˆƒ (s : set Î³) (H : s âˆˆ b), y âˆˆ s âˆ§ s âŠ† ball y (u n / 2),
    { apply hb.mem_nhds_iff.1,
      exact ball_mem_nhds _ (half_pos (u_pos n)) },
    have diam_s : diam s â‰¤ u n,
    { apply (diam_mono hs bounded_ball).trans,
      convert diam_ball (half_pos (u_pos n)).le,
      ring },
    refine mem_Union.2 âŸ¨âŸ¨s, sbâŸ©, _âŸ©,
    refine mem_Union.2 âŸ¨âŸ¨metric.bounded.mono hs bounded_ball, diam_sâŸ©, _âŸ©,
    apply mem_inter (subset_closure (mem_image_of_mem _ ys)),
    refine mem_Inter.2 (Î» t, mem_Inter.2 (Î» ht, âŸ¨_, _âŸ©)),
    { apply hq1,
      exact mem_image_of_mem _ ys },
    { apply disjoint_left.1 (hq2 âŸ¨(t, âŸ¨s, sbâŸ©), ht.symmâŸ©),
      exact mem_image_of_mem _ ys } },
  -- Now, let us prove the harder inclusion `â‹‚ F n âŠ† range f`.
  { assume x hx,
    -- pick for each `n` a good set `s n` of small diameter for which `x âˆˆ E (s n)`.
    have C1 : âˆ€ n, âˆƒ (s : b) (hs : bounded s.1 âˆ§ diam s.1 â‰¤ u n), x âˆˆ E s :=
      Î» n, by simpa only [mem_Union] using mem_Inter.1 hx n,
    choose s hs hxs using C1,
    have C2 : âˆ€ n, (s n).1.nonempty,
    { assume n,
      rw nonempty_iff_ne_empty,
      assume hn,
      have := (s n).2,
      rw hn at this,
      exact b_nonempty this },
    -- choose a point `y n âˆˆ s n`.
    choose y hy using C2,
    have I : âˆ€ m n, ((s m).1 âˆ© (s n).1).nonempty,
    { assume m n,
      rw â† not_disjoint_iff_nonempty_inter,
      by_contra' h,
      have A : x âˆˆ q âŸ¨(s m, s n), hâŸ© \ q âŸ¨(s n, s m), h.symmâŸ©,
      { have := mem_Inter.1 (hxs m).2 (s n), exact (mem_Inter.1 this h : _) },
      have B : x âˆˆ q âŸ¨(s n, s m), h.symmâŸ© \ q âŸ¨(s m, s n), hâŸ©,
      { have := mem_Inter.1 (hxs n).2 (s m), exact (mem_Inter.1 this h.symm : _) },
      exact A.2 B.1 },
    -- the points `y n` are nearby, and therefore they form a Cauchy sequence.
    have cauchy_y : cauchy_seq y,
    { have : tendsto (Î» n, 2 * u n) at_top (ğ“ 0), by simpa only [mul_zero] using u_lim.const_mul 2,
      apply cauchy_seq_of_le_tendsto_0' (Î» n, 2 * u n) (Î» m n hmn, _) this,
      rcases I m n with âŸ¨z, zsm, zsnâŸ©,
      calc dist (y m) (y n) â‰¤ dist (y m) z + dist z (y n) : dist_triangle _ _ _
      ... â‰¤ u m + u n :
        add_le_add ((dist_le_diam_of_mem (hs m).1 (hy m) zsm).trans (hs m).2)
                   ((dist_le_diam_of_mem (hs n).1 zsn (hy n)).trans (hs n).2)
      ... â‰¤ 2 * u m : by linarith [u_anti.antitone hmn] },
    haveI : nonempty Î³ := âŸ¨y 0âŸ©,
    -- let `z` be its limit.
    let z := lim at_top y,
    have y_lim : tendsto y at_top (ğ“ z) := cauchy_y.tendsto_lim,
    suffices : f z = x, by { rw â† this, exact mem_range_self _ },
    -- assume for a contradiction that `f z â‰  x`.
    by_contra' hne,
    -- introduce disjoint open sets `v` and `w` separating `f z` from `x`.
    obtain âŸ¨v, w, v_open, w_open, fzv, xw, hvwâŸ© := t2_separation hne,
    obtain âŸ¨Î´, Î´pos, hÎ´âŸ© : âˆƒ Î´ > (0 : â„), ball z Î´ âŠ† f â»Â¹' v,
    { apply metric.mem_nhds_iff.1,
      exact f_cont.continuous_at.preimage_mem_nhds (v_open.mem_nhds fzv) },
    obtain âŸ¨n, hnâŸ© : âˆƒ n, u n + dist (y n) z < Î´,
    { have : tendsto (Î» n, u n + dist (y n) z) at_top (ğ“ 0),
        by simpa only [add_zero] using u_lim.add (tendsto_iff_dist_tendsto_zero.1 y_lim),
      exact ((tendsto_order.1 this).2 _ Î´pos).exists },
    -- for large enough `n`, the image of `s n` is contained in `v`, by continuity of `f`.
    have fsnv : f '' (s n) âŠ† v,
    { rw image_subset_iff,
      apply subset.trans _ hÎ´,
      assume a ha,
      calc dist a z â‰¤ dist a (y n) + dist (y n) z : dist_triangle _ _ _
      ... â‰¤ u n + dist (y n) z :
        add_le_add_right ((dist_le_diam_of_mem (hs n).1 ha (hy n)).trans (hs n).2) _
      ... < Î´ : hn },
    -- as `x` belongs to the closure of `f '' (s n)`, it belongs to the closure of `v`.
    have : x âˆˆ closure v := closure_mono fsnv (hxs n).1,
    -- this is a contradiction, as `x` is supposed to belong to `w`, which is disjoint from
    -- the closure of `v`.
    exact disjoint_left.1 (hvw.closure_left w_open) this xw }
end

theorem _root_.is_closed.measurable_set_image_of_continuous_on_inj_on
  {Î² : Type*} [topological_space Î²] [t2_space Î²] [measurable_space Î²] [borel_space Î²]
  {s : set Î³} (hs : is_closed s) {f : Î³ â†’ Î²} (f_cont : continuous_on f s) (f_inj : inj_on f s) :
  measurable_set (f '' s) :=
begin
  rw image_eq_range,
  haveI : polish_space s := is_closed.polish_space hs,
  apply measurable_set_range_of_continuous_injective,
  { rwa continuous_on_iff_continuous_restrict at f_cont },
  { rwa inj_on_iff_injective at f_inj }
end


variables [measurable_space Î³] [hÎ³b : borel_space Î³]
{Î² : Type*} [tÎ² : topological_space Î²] [t2_space Î²] [measurable_space Î²] [borel_space Î²]
{s : set Î³} {f : Î³ â†’ Î²}
include tÎ² hÎ³b

/-- The Lusin-Souslin theorem: if `s` is Borel-measurable in a Polish space, then its image under
a continuous injective map is also Borel-measurable. -/
theorem _root_.measurable_set.image_of_continuous_on_inj_on
  (hs : measurable_set s) (f_cont : continuous_on f s) (f_inj : inj_on f s) :
  measurable_set (f '' s) :=
begin
  obtain âŸ¨t', t't, t'_polish, s_closed, s_openâŸ© :
    âˆƒ (t' : topological_space Î³), t' â‰¤ tÎ³ âˆ§ @polish_space Î³ t' âˆ§ is_closed[t'] s âˆ§
      is_open[t'] s := hs.is_clopenable,
  exact @is_closed.measurable_set_image_of_continuous_on_inj_on Î³ t' t'_polish Î² _ _ _ _ s
    s_closed f (f_cont.mono_dom t't) f_inj,
end

/-- The Lusin-Souslin theorem: if `s` is Borel-measurable in a Polish space, then its image under
a measurable injective map taking values in a second-countable topological space
is also Borel-measurable. -/
theorem _root_.measurable_set.image_of_measurable_inj_on [second_countable_topology Î²]
  (hs : measurable_set s) (f_meas : measurable f) (f_inj : inj_on f s) :
  measurable_set (f '' s) :=
begin
  -- for a finer Polish topology, `f` is continuous. Therefore, one may apply the corresponding
  -- result for continuous maps.
  obtain âŸ¨t', t't, f_cont, t'_polishâŸ© :
    âˆƒ (t' : topological_space Î³), t' â‰¤ tÎ³ âˆ§ @continuous Î³ Î² t' tÎ² f âˆ§ @polish_space Î³ t' :=
      f_meas.exists_continuous,
  have M : measurable_set[@borel Î³ t'] s :=
    @continuous.measurable Î³ Î³ t' (@borel Î³ t')
      (@borel_space.opens_measurable Î³ t' (@borel Î³ t') (by { constructor, refl }))
      tÎ³ _ _ _ (continuous_id_of_le t't) s hs,
  exact @measurable_set.image_of_continuous_on_inj_on Î³ t' t'_polish
    (@borel Î³ t') (by { constructor, refl }) Î² _ _ _ _ s f M
    (@continuous.continuous_on Î³ Î² t' tÎ² f s f_cont) f_inj,
end

/-- An injective continuous function on a Polish space is a measurable embedding. -/
theorem _root_.continuous.measurable_embedding (f_cont : continuous f) (f_inj : injective f) :
  measurable_embedding f :=
{ injective := f_inj,
  measurable := f_cont.measurable,
  measurable_set_image' := Î» u hu,
    hu.image_of_continuous_on_inj_on f_cont.continuous_on (f_inj.inj_on _) }

/-- If `s` is Borel-measurable in a Polish space and `f` is continuous injective on `s`, then
the restriction of `f` to `s` is a measurable embedding. -/
theorem _root_.continuous_on.measurable_embedding (hs : measurable_set s)
  (f_cont : continuous_on f s) (f_inj : inj_on f s) :
  measurable_embedding (s.restrict f) :=
{ injective := inj_on_iff_injective.1 f_inj,
  measurable := (continuous_on_iff_continuous_restrict.1 f_cont).measurable,
  measurable_set_image' :=
  begin
    assume u hu,
    have A : measurable_set ((coe : s â†’ Î³) '' u) :=
      (measurable_embedding.subtype_coe hs).measurable_set_image.2 hu,
    have B : measurable_set (f '' ((coe : s â†’ Î³) '' u)) :=
      A.image_of_continuous_on_inj_on (f_cont.mono (subtype.coe_image_subset s u))
        (f_inj.mono ((subtype.coe_image_subset s u))),
    rwa â† image_comp at B,
  end }

/-- An injective measurable function from a Polish space to a second-countable topological space
is a measurable embedding. -/
theorem _root_.measurable.measurable_embedding [second_countable_topology Î²]
  (f_meas : measurable f) (f_inj : injective f) :
  measurable_embedding f :=
{ injective := f_inj,
  measurable := f_meas,
  measurable_set_image' := Î» u hu, hu.image_of_measurable_inj_on f_meas (f_inj.inj_on _) }

omit tÎ²

/-- In a Polish space, a set is clopenable if and only if it is Borel-measurable. -/
lemma is_clopenable_iff_measurable_set :
  is_clopenable s â†” measurable_set s :=
begin
  -- we already know that a measurable set is clopenable. Conversely, assume that `s` is clopenable.
  refine âŸ¨Î» hs, _, Î» hs, hs.is_clopenableâŸ©,
  -- consider a finer topology `t'` in which `s` is open and closed.
  obtain âŸ¨t', t't, t'_polish, s_closed, s_openâŸ© :
    âˆƒ (t' : topological_space Î³), t' â‰¤ tÎ³ âˆ§ @polish_space Î³ t' âˆ§ is_closed[t'] s âˆ§
      is_open[t'] s := hs,
  -- the identity is continuous from `t'` to `tÎ³`.
  have C : @continuous Î³ Î³ t' tÎ³ id := continuous_id_of_le t't,
  -- therefore, it is also a measurable embedding, by the Lusin-Souslin theorem
  have E := @continuous.measurable_embedding Î³ t' t'_polish (@borel Î³ t') (by { constructor, refl })
    Î³ tÎ³ (polish_space.t2_space Î³) _ _ id C injective_id,
  -- the set `s` is measurable for `t'` as it is closed.
  have M : @measurable_set Î³ (@borel Î³ t') s :=
    @is_closed.measurable_set Î³ s t' (@borel Î³ t')
      (@borel_space.opens_measurable Î³ t' (@borel Î³ t') (by { constructor, refl })) s_closed,
  -- therefore, its image under the measurable embedding `id` is also measurable for `tÎ³`.
  convert E.measurable_set_image.2 M,
  simp only [id.def, image_id'],
end

omit hÎ³b

/-- The set of points for which a measurable sequence of functions converges is measurable. -/
@[measurability] lemma measurable_set_exists_tendsto
  [hÎ³ : opens_measurable_space Î³] [countable Î¹] {l : filter Î¹}
  [l.is_countably_generated] {f : Î¹ â†’ Î² â†’ Î³} (hf : âˆ€ i, measurable (f i)) :
  measurable_set {x | âˆƒ c, tendsto (Î» n, f n x) l (ğ“ c)} :=
begin
  by_cases hl : l.ne_bot,
  swap, { rw not_ne_bot at hl, simp [hl] },
  letI := upgrade_polish_space Î³,
  rcases l.exists_antitone_basis with âŸ¨u, huâŸ©,
  simp_rw â† cauchy_map_iff_exists_tendsto,
  change measurable_set {x | _ âˆ§ _},
  have : âˆ€ x, ((map (Î» i, f i x) l) Ã—á¶  (map (Î» i, f i x) l)).has_antitone_basis
    (Î» n, ((Î» i, f i x) '' u n) Ã—Ë¢ ((Î» i, f i x) '' u n)) := Î» x, hu.map.prod hu.map,
  simp_rw [and_iff_right (hl.map _), filter.has_basis.le_basis_iff (this _).to_has_basis
    metric.uniformity_basis_dist_inv_nat_succ, set.set_of_forall],
  refine measurable_set.bInter set.countable_univ (Î» K _, _),
  simp_rw set.set_of_exists,
  refine measurable_set.bUnion set.countable_univ (Î» N hN, _),
  simp_rw [prod_image_image_eq, image_subset_iff, prod_subset_iff, set.set_of_forall],
  exact measurable_set.bInter (to_countable (u N)) (Î» i _,
    measurable_set.bInter (to_countable (u N)) (Î» j _,
    measurable_set_lt (measurable.dist (hf i) (hf j)) measurable_const)),
end

end measure_theory
