/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import measure_theory.measure.giry_monad
import measure_theory.integral.set_integral

/-!
# The product measure

In this file we define and prove properties about the binary product measure. If `Œ±` and `Œ≤` have
œÉ-finite measures `Œº` resp. `ŒΩ` then `Œ± √ó Œ≤` can be equipped with a œÉ-finite measure `Œº.prod ŒΩ` that
satisfies `(Œº.prod ŒΩ) s = ‚à´‚Åª x, ŒΩ {y | (x, y) ‚àà s} ‚àÇŒº`.
We also have `(Œº.prod ŒΩ) (s.prod t) = Œº s * ŒΩ t`, i.e. the measure of a rectangle is the product of
the measures of the sides.

We also prove Tonelli's theorem and Fubini's theorem.

## Main definition

* `measure_theory.measure.prod`: The product of two measures.

## Main results

* `measure_theory.measure.prod_apply` states `Œº.prod ŒΩ s = ‚à´‚Åª x, ŒΩ {y | (x, y) ‚àà s} ‚àÇŒº`
  for measurable `s`. `measure_theory.measure.prod_apply_symm` is the reversed version.
* `measure_theory.measure.prod_prod` states `Œº.prod ŒΩ (s.prod t) = Œº s * ŒΩ t` for measurable sets
  `s` and `t`.
* `measure_theory.lintegral_prod`: Tonelli's theorem. It states that for a measurable function
  `Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû` we have `‚à´‚Åª z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´‚Åª x, ‚à´‚Åª y, f (x, y) ‚àÇŒΩ ‚àÇŒº`. The version
  for functions `Œ± ‚Üí Œ≤ ‚Üí ‚Ñù‚â•0‚àû` is reversed, and called `lintegral_lintegral`. Both versions have
  a variant with `_symm` appended, where the order of integration is reversed.
  The lemma `measurable.lintegral_prod_right'` states that the inner integral of the right-hand side
  is measurable.
* `measure_theory.integrable_prod_iff` states that a binary function is integrable iff both
  * `y ‚Ü¶ f (x, y)` is integrable for almost every `x`, and
  * the function `x ‚Ü¶ ‚à´ ‚à•f (x, y)‚à• dy` is integrable.
* `measure_theory.integral_prod`: Fubini's theorem. It states that for a integrable function
  `Œ± √ó Œ≤ ‚Üí E` (where `E` is a second countable Banach space) we have
  `‚à´ z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº`. This theorem has the same variants as
  Tonelli's theorem. The lemma `measure_theory.integrable.integral_prod_right` states that the
  inner integral of the right-hand side is integrable.

## Implementation Notes

Many results are proven twice, once for functions in curried form (`Œ± ‚Üí Œ≤ ‚Üí Œ≥`) and one for
functions in uncurried form (`Œ± √ó Œ≤ ‚Üí Œ≥`). The former often has an assumption
`measurable (uncurry f)`, which could be inconvenient to discharge, but for the latter it is more
common that the function has to be given explicitly, since Lean cannot synthesize the function by
itself. We name the lemmas about the uncurried form with a prime.
Tonelli's theorem and Fubini's theorem have a different naming scheme, since the version for the
uncurried version is reversed.

## Tags

product measure, Fubini's theorem, Tonelli's theorem, Fubini-Tonelli theorem
-/

noncomputable theory
open_locale classical topological_space ennreal measure_theory
open set function real ennreal
open measure_theory measurable_space measure_theory.measure
open topological_space (hiding generate_from)
open filter (hiding prod_eq map)

variables {Œ± Œ±' Œ≤ Œ≤' Œ≥ E : Type*}

/-- Rectangles formed by œÄ-systems form a œÄ-system. -/
lemma is_pi_system.prod {C : set (set Œ±)} {D : set (set Œ≤)} (hC : is_pi_system C)
  (hD : is_pi_system D) : is_pi_system (image2 set.prod C D) :=
begin
  rintro _ _ ‚ü®s‚ÇÅ, t‚ÇÅ, hs‚ÇÅ, ht‚ÇÅ, rfl‚ü© ‚ü®s‚ÇÇ, t‚ÇÇ, hs‚ÇÇ, ht‚ÇÇ, rfl‚ü© hst,
  rw [prod_inter_prod] at hst ‚ä¢, rw [prod_nonempty_iff] at hst,
  exact mem_image2_of_mem (hC _ _ hs‚ÇÅ hs‚ÇÇ hst.1) (hD _ _ ht‚ÇÅ ht‚ÇÇ hst.2)
end

/-- Rectangles of countably spanning sets are countably spanning. -/
lemma is_countably_spanning.prod {C : set (set Œ±)} {D : set (set Œ≤)}
  (hC : is_countably_spanning C) (hD : is_countably_spanning D) :
  is_countably_spanning (image2 set.prod C D) :=
begin
  rcases ‚ü®hC, hD‚ü© with ‚ü®‚ü®s, h1s, h2s‚ü©, t, h1t, h2t‚ü©,
  refine ‚ü®Œª n, (s n.unpair.1).prod (t n.unpair.2), Œª n, mem_image2_of_mem (h1s _) (h1t _), _‚ü©,
  rw [Union_unpair_prod, h2s, h2t, univ_prod_univ]
end

variables [measurable_space Œ±] [measurable_space Œ±'] [measurable_space Œ≤] [measurable_space Œ≤']
variables [measurable_space Œ≥]
variables {Œº : measure Œ±} {ŒΩ : measure Œ≤} {œÑ : measure Œ≥}
variables [normed_group E] [measurable_space E]

/-! ### Measurability

Before we define the product measure, we can talk about the measurability of operations on binary
functions. We show that if `f` is a binary measurable function, then the function that integrates
along one of the variables (using either the Lebesgue or Bochner integral) is measurable.
-/

/-- The product of generated œÉ-algebras is the one generated by rectangles, if both generating sets
  are countably spanning. -/
lemma generate_from_prod_eq {Œ± Œ≤} {C : set (set Œ±)} {D : set (set Œ≤)}
  (hC : is_countably_spanning C) (hD : is_countably_spanning D) :
  @prod.measurable_space _ _ (generate_from C) (generate_from D) =
    generate_from (image2 set.prod C D) :=
begin
  apply le_antisymm,
  { refine sup_le _ _; rw [comap_generate_from];
      apply generate_from_le; rintro _ ‚ü®s, hs, rfl‚ü©,
    { rcases hD with ‚ü®t, h1t, h2t‚ü©,
      rw [‚Üê prod_univ, ‚Üê h2t, prod_Union],
      apply measurable_set.Union,
      intro n, apply measurable_set_generate_from,
      exact ‚ü®s, t n, hs, h1t n, rfl‚ü© },
    { rcases hC with ‚ü®t, h1t, h2t‚ü©,
      rw [‚Üê univ_prod, ‚Üê h2t, Union_prod_const],
      apply measurable_set.Union,
      rintro n, apply measurable_set_generate_from,
      exact mem_image2_of_mem (h1t n) hs } },
  { apply generate_from_le, rintro _ ‚ü®s, t, hs, ht, rfl‚ü©, rw [prod_eq],
    apply (measurable_fst _).inter (measurable_snd _),
    { exact measurable_set_generate_from hs },
    { exact measurable_set_generate_from ht } }
end

/-- If `C` and `D` generate the œÉ-algebras on `Œ±` resp. `Œ≤`, then rectangles formed by `C` and `D`
  generate the œÉ-algebra on `Œ± √ó Œ≤`. -/
lemma generate_from_eq_prod {C : set (set Œ±)} {D : set (set Œ≤)} (hC : generate_from C = ‚Äπ_‚Ä∫)
  (hD : generate_from D = ‚Äπ_‚Ä∫) (h2C : is_countably_spanning C) (h2D : is_countably_spanning D) :
    generate_from (image2 set.prod C D) = prod.measurable_space :=
by rw [‚Üê hC, ‚Üê hD, generate_from_prod_eq h2C h2D]

/-- The product œÉ-algebra is generated from boxes, i.e. `s.prod t` for sets `s : set Œ±` and
  `t : set Œ≤`. -/
lemma generate_from_prod :
  generate_from (image2 set.prod {s : set Œ± | measurable_set s} {t : set Œ≤ | measurable_set t}) =
  prod.measurable_space :=
generate_from_eq_prod generate_from_measurable_set generate_from_measurable_set
  is_countably_spanning_measurable_set is_countably_spanning_measurable_set

/-- Rectangles form a œÄ-system. -/
lemma is_pi_system_prod :
  is_pi_system (image2 set.prod {s : set Œ± | measurable_set s} {t : set Œ≤ | measurable_set t}) :=
is_pi_system_measurable_set.prod is_pi_system_measurable_set

/-- If `ŒΩ` is a finite measure, and `s ‚äÜ Œ± √ó Œ≤` is measurable, then `x ‚Ü¶ ŒΩ { y | (x, y) ‚àà s }` is
  a measurable function. `measurable_measure_prod_mk_left` is strictly more general. -/
lemma measurable_measure_prod_mk_left_finite [is_finite_measure ŒΩ] {s : set (Œ± √ó Œ≤)}
  (hs : measurable_set s) : measurable (Œª x, ŒΩ (prod.mk x ‚Åª¬π' s)) :=
begin
  refine induction_on_inter generate_from_prod.symm is_pi_system_prod _ _ _ _ hs,
  { simp [measurable_zero, const_def] },
  { rintro _ ‚ü®s, t, hs, ht, rfl‚ü©, simp only [mk_preimage_prod_right_eq_if, measure_if],
    exact measurable_const.indicator hs },
  { intros t ht h2t,
    simp_rw [preimage_compl, measure_compl (measurable_prod_mk_left ht) (measure_ne_top ŒΩ _)],
    exact h2t.const_sub _ },
  { intros f h1f h2f h3f, simp_rw [preimage_Union],
    have : ‚àÄ b, ŒΩ (‚ãÉ i, prod.mk b ‚Åª¬π' f i) = ‚àë' i, ŒΩ (prod.mk b ‚Åª¬π' f i) :=
      Œª b, measure_Union (Œª i j hij, disjoint.preimage _ (h1f i j hij))
        (Œª i, measurable_prod_mk_left (h2f i)),
    simp_rw [this], apply measurable.ennreal_tsum h3f },
end

/-- If `ŒΩ` is a œÉ-finite measure, and `s ‚äÜ Œ± √ó Œ≤` is measurable, then `x ‚Ü¶ ŒΩ { y | (x, y) ‚àà s }` is
  a measurable function. -/
lemma measurable_measure_prod_mk_left [sigma_finite ŒΩ] {s : set (Œ± √ó Œ≤)}
  (hs : measurable_set s) : measurable (Œª x, ŒΩ (prod.mk x ‚Åª¬π' s)) :=
begin
  have : ‚àÄ x, measurable_set (prod.mk x ‚Åª¬π' s) := Œª x, measurable_prod_mk_left hs,
  simp only [‚Üê @supr_restrict_spanning_sets _ _ ŒΩ, this],
  apply measurable_supr, intro i,
  haveI := fact.mk (measure_spanning_sets_lt_top ŒΩ i),
  exact measurable_measure_prod_mk_left_finite hs
end

/-- If `Œº` is a œÉ-finite measure, and `s ‚äÜ Œ± √ó Œ≤` is measurable, then `y ‚Ü¶ Œº { x | (x, y) ‚àà s }` is
  a measurable function. -/
lemma measurable_measure_prod_mk_right {Œº : measure Œ±} [sigma_finite Œº] {s : set (Œ± √ó Œ≤)}
  (hs : measurable_set s) : measurable (Œª y, Œº ((Œª x, (x, y)) ‚Åª¬π' s)) :=
measurable_measure_prod_mk_left (measurable_set_swap_iff.mpr hs)

lemma measurable.map_prod_mk_left [sigma_finite ŒΩ] : measurable (Œª x : Œ±, map (prod.mk x) ŒΩ) :=
begin
  apply measurable_of_measurable_coe, intros s hs,
  simp_rw [map_apply measurable_prod_mk_left hs],
  exact measurable_measure_prod_mk_left hs
end

lemma measurable.map_prod_mk_right {Œº : measure Œ±} [sigma_finite Œº] :
  measurable (Œª y : Œ≤, map (Œª x : Œ±, (x, y)) Œº) :=
begin
  apply measurable_of_measurable_coe, intros s hs,
  simp_rw [map_apply measurable_prod_mk_right hs],
  exact measurable_measure_prod_mk_right hs
end

/-- The Lebesgue integral is measurable. This shows that the integrand of (the right-hand-side of)
  Tonelli's theorem is measurable. -/
lemma measurable.lintegral_prod_right' [sigma_finite ŒΩ] :
  ‚àÄ {f : Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû} (hf : measurable f), measurable (Œª x, ‚à´‚Åª y, f (x, y) ‚àÇŒΩ) :=
begin
  have m := @measurable_prod_mk_left,
  refine measurable.ennreal_induction _ _ _,
  { intros c s hs, simp only [‚Üê indicator_comp_right],
    suffices : measurable (Œª x, c * ŒΩ (prod.mk x ‚Åª¬π' s)),
    { simpa [lintegral_indicator _ (m hs)] },
    exact (measurable_measure_prod_mk_left hs).const_mul _ },
  { rintro f g - hf hg h2f h2g, simp_rw [pi.add_apply, lintegral_add (hf.comp m) (hg.comp m)],
    exact h2f.add h2g },
  { intros f hf h2f h3f,
    have := measurable_supr h3f,
    have : ‚àÄ x, monotone (Œª n y, f n (x, y)) := Œª x i j hij y, h2f hij (x, y),
    simpa [lintegral_supr (Œª n, (hf n).comp m), this] }
end

/-- The Lebesgue integral is measurable. This shows that the integrand of (the right-hand-side of)
  Tonelli's theorem is measurable.
  This version has the argument `f` in curried form. -/
lemma measurable.lintegral_prod_right [sigma_finite ŒΩ] {f : Œ± ‚Üí Œ≤ ‚Üí ‚Ñù‚â•0‚àû}
  (hf : measurable (uncurry f)) : measurable (Œª x, ‚à´‚Åª y, f x y ‚àÇŒΩ) :=
hf.lintegral_prod_right'

/-- The Lebesgue integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Tonelli's theorem is measurable. -/
lemma measurable.lintegral_prod_left' [sigma_finite Œº] {f : Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû}
  (hf : measurable f) : measurable (Œª y, ‚à´‚Åª x, f (x, y) ‚àÇŒº) :=
(measurable_swap_iff.mpr hf).lintegral_prod_right'

/-- The Lebesgue integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Tonelli's theorem is measurable.
  This version has the argument `f` in curried form. -/
lemma measurable.lintegral_prod_left [sigma_finite Œº] {f : Œ± ‚Üí Œ≤ ‚Üí ‚Ñù‚â•0‚àû}
  (hf : measurable (uncurry f)) : measurable (Œª y, ‚à´‚Åª x, f x y ‚àÇŒº) :=
hf.lintegral_prod_left'

lemma measurable_set_integrable [sigma_finite ŒΩ] [opens_measurable_space E] ‚¶Éf : Œ± ‚Üí Œ≤ ‚Üí E‚¶Ñ
  (hf : measurable (uncurry f)) : measurable_set { x | integrable (f x) ŒΩ } :=
begin
  simp_rw [integrable, hf.of_uncurry_left.ae_measurable, true_and],
  exact measurable_set_lt (measurable.lintegral_prod_right hf.ennnorm) measurable_const
end

section
variables [second_countable_topology E] [normed_space ‚Ñù E]
  [complete_space E] [borel_space E]

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable.
  This version has `f` in curried form. -/
lemma measurable.integral_prod_right [sigma_finite ŒΩ] ‚¶Éf : Œ± ‚Üí Œ≤ ‚Üí E‚¶Ñ
  (hf : measurable (uncurry f)) : measurable (Œª x, ‚à´ y, f x y ‚àÇŒΩ) :=
begin
  let s : ‚Ñï ‚Üí simple_func (Œ± √ó Œ≤) E := simple_func.approx_on _ hf univ _ (mem_univ 0),
  let s' : ‚Ñï ‚Üí Œ± ‚Üí simple_func Œ≤ E := Œª n x, (s n).comp (prod.mk x) measurable_prod_mk_left,
  let f' : ‚Ñï ‚Üí Œ± ‚Üí E := Œª n, {x | integrable (f x) ŒΩ}.indicator
    (Œª x, (s' n x).integral ŒΩ),
  have hf' : ‚àÄ n, measurable (f' n),
  { intro n, refine measurable.indicator _ (measurable_set_integrable hf),
    have : ‚àÄ x, (s' n x).range.filter (Œª x, x ‚â† 0) ‚äÜ (s n).range,
    { intros x, refine finset.subset.trans (finset.filter_subset _ _) _, intro y,
      simp_rw [simple_func.mem_range], rintro ‚ü®z, rfl‚ü©, exact ‚ü®(x, z), rfl‚ü© },
    simp only [simple_func.integral_eq_sum_of_subset (this _)],
    refine finset.measurable_sum _ (Œª x _, _),
    refine (measurable.ennreal_to_real _).smul_const _,
    simp only [simple_func.coe_comp, preimage_comp] {single_pass := tt},
    apply measurable_measure_prod_mk_left,
    exact (s n).measurable_set_fiber x },
  have h2f' : tendsto f' at_top (ùìù (Œª (x : Œ±), ‚à´ (y : Œ≤), f x y ‚àÇŒΩ)),
  { rw [tendsto_pi], intro x,
    by_cases hfx : integrable (f x) ŒΩ,
    { have : ‚àÄ n, integrable (s' n x) ŒΩ,
      { intro n, apply (hfx.norm.add hfx.norm).mono' (s' n x).measurable.ae_measurable,
        apply eventually_of_forall, intro y,
        simp_rw [s', simple_func.coe_comp], exact simple_func.norm_approx_on_zero_le _ _ (x, y) n },
      simp only [f', hfx, simple_func.integral_eq_integral _ (this _), indicator_of_mem,
        mem_set_of_eq],
      refine tendsto_integral_of_dominated_convergence (Œª y, ‚à•f x y‚à• + ‚à•f x y‚à•)
        (Œª n, (s' n x).ae_measurable) hf.of_uncurry_left.ae_measurable (hfx.norm.add hfx.norm) _ _,
      { exact Œª n, eventually_of_forall (Œª y, simple_func.norm_approx_on_zero_le _ _ (x, y) n) },
      { exact eventually_of_forall (Œª y, simple_func.tendsto_approx_on _ _ (by simp)) } },
    { simpa [f', hfx, integral_undef] using @tendsto_const_nhds _ _ _ (0 : E) _, } },
  exact measurable_of_tendsto_metric hf' h2f'
end

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable. -/
lemma measurable.integral_prod_right' [sigma_finite ŒΩ] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : measurable f) : measurable (Œª x, ‚à´ y, f (x, y) ‚àÇŒΩ) :=
by { rw [‚Üê uncurry_curry f] at hf, exact hf.integral_prod_right }

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable.
  This version has `f` in curried form. -/
lemma measurable.integral_prod_left [sigma_finite Œº] ‚¶Éf : Œ± ‚Üí Œ≤ ‚Üí E‚¶Ñ
  (hf : measurable (uncurry f)) : measurable (Œª y, ‚à´ x, f x y ‚àÇŒº) :=
(hf.comp measurable_swap).integral_prod_right'

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable. -/
lemma measurable.integral_prod_left' [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : measurable f) : measurable (Œª y, ‚à´ x, f (x, y) ‚àÇŒº) :=
(hf.comp measurable_swap).integral_prod_right'

end

/-! ### The product measure -/

namespace measure_theory

namespace measure

/-- The binary product of measures. They are defined for arbitrary measures, but we basically
  prove all properties under the assumption that at least one of them is œÉ-finite. -/
@[irreducible] protected def prod (Œº : measure Œ±) (ŒΩ : measure Œ≤) : measure (Œ± √ó Œ≤) :=
bind Œº $ Œª x : Œ±, map (prod.mk x) ŒΩ

instance prod.measure_space {Œ± Œ≤} [measure_space Œ±] [measure_space Œ≤] : measure_space (Œ± √ó Œ≤) :=
{ volume := volume.prod volume }

variables {Œº ŒΩ} [sigma_finite ŒΩ]

lemma prod_apply {s : set (Œ± √ó Œ≤)} (hs : measurable_set s) :
  Œº.prod ŒΩ s = ‚à´‚Åª x, ŒΩ (prod.mk x ‚Åª¬π' s) ‚àÇŒº :=
by simp_rw [measure.prod, bind_apply hs measurable.map_prod_mk_left,
  map_apply measurable_prod_mk_left hs]

@[simp] lemma prod_prod {s : set Œ±} {t : set Œ≤}
  (hs : measurable_set s) (ht : measurable_set t) : Œº.prod ŒΩ (s.prod t) = Œº s * ŒΩ t :=
by simp_rw [prod_apply (hs.prod ht), mk_preimage_prod_right_eq_if, measure_if,
  lintegral_indicator _ hs, lintegral_const, restrict_apply measurable_set.univ,
  univ_inter, mul_comm]

local attribute [instance] nonempty_measurable_superset
/-- If we don't assume measurability of `s` and `t`, we can bound the measure of their product. -/
lemma prod_prod_le (s : set Œ±) (t : set Œ≤) : Œº.prod ŒΩ (s.prod t) ‚â§ Œº s * ŒΩ t :=
begin
  by_cases hs0 : Œº s = 0,
  { rcases (exists_measurable_superset_of_null hs0) with ‚ü®s', hs', h2s', h3s'‚ü©,
    convert measure_mono (prod_mono hs' (subset_univ _)),
    simp_rw [hs0, prod_prod h2s' measurable_set.univ, h3s', zero_mul] },
  by_cases hti : ŒΩ t = ‚àû,
  { convert le_top, simp_rw [hti, ennreal.mul_top, hs0, if_false] },
  rw [measure_eq_infi' Œº],
  simp_rw [ennreal.infi_mul hti],
  refine le_infi _,
  rintro ‚ü®s', h1s', h2s'‚ü©,
  rw [subtype.coe_mk],
  by_cases ht0 : ŒΩ t = 0,
  { rcases (exists_measurable_superset_of_null ht0) with ‚ü®t', ht', h2t', h3t'‚ü©,
    convert measure_mono (prod_mono (subset_univ _) ht'),
    simp_rw [ht0, prod_prod measurable_set.univ h2t', h3t', mul_zero] },
  by_cases hsi : Œº s' = ‚àû,
  { convert le_top, simp_rw [hsi, ennreal.top_mul, ht0, if_false] },
  rw [measure_eq_infi' ŒΩ],
  simp_rw [ennreal.mul_infi hsi],
  refine le_infi _,
  rintro ‚ü®t', h1t', h2t'‚ü©,
  convert measure_mono (prod_mono h1s' h1t'),
  simp [prod_prod h2s' h2t'],
end

lemma ae_measure_lt_top {s : set (Œ± √ó Œ≤)} (hs : measurable_set s)
  (h2s : (Œº.prod ŒΩ) s ‚â† ‚àû) : ‚àÄ·µê x ‚àÇŒº, ŒΩ (prod.mk x ‚Åª¬π' s) < ‚àû :=
by { simp_rw [prod_apply hs] at h2s, refine ae_lt_top (measurable_measure_prod_mk_left hs) h2s }

lemma integrable_measure_prod_mk_left {s : set (Œ± √ó Œ≤)}
  (hs : measurable_set s) (h2s : (Œº.prod ŒΩ) s ‚â† ‚àû) :
  integrable (Œª x, (ŒΩ (prod.mk x ‚Åª¬π' s)).to_real) Œº :=
begin
  refine ‚ü®(measurable_measure_prod_mk_left hs).ennreal_to_real.ae_measurable, _‚ü©,
  simp_rw [has_finite_integral, ennnorm_eq_of_real to_real_nonneg],
  convert h2s.lt_top using 1, simp_rw [prod_apply hs], apply lintegral_congr_ae,
  refine (ae_measure_lt_top hs h2s).mp _, apply eventually_of_forall, intros x hx,
  rw [lt_top_iff_ne_top] at hx, simp [of_real_to_real, hx],
end

/-- Note: the assumption `hs` cannot be dropped. For a counterexample, see
  Walter Rudin *Real and Complex Analysis*, example (c) in section 8.9. -/
lemma measure_prod_null {s : set (Œ± √ó Œ≤)}
  (hs : measurable_set s) : Œº.prod ŒΩ s = 0 ‚Üî (Œª x, ŒΩ (prod.mk x ‚Åª¬π' s)) =·µê[Œº] 0 :=
by simp_rw [prod_apply hs, lintegral_eq_zero_iff (measurable_measure_prod_mk_left hs)]

/-- Note: the converse is not true without assuming that `s` is measurable. For a counterexample,
  see Walter Rudin *Real and Complex Analysis*, example (c) in section 8.9. -/
lemma measure_ae_null_of_prod_null {s : set (Œ± √ó Œ≤)}
  (h : Œº.prod ŒΩ s = 0) : (Œª x, ŒΩ (prod.mk x ‚Åª¬π' s)) =·µê[Œº] 0 :=
begin
  obtain ‚ü®t, hst, mt, ht‚ü© := exists_measurable_superset_of_null h,
  simp_rw [measure_prod_null mt] at ht,
  rw [eventually_le_antisymm_iff],
  exact ‚ü®eventually_le.trans_eq
    (eventually_of_forall $ Œª x, (measure_mono (preimage_mono hst) : _)) ht,
    eventually_of_forall $ Œª x, zero_le _‚ü©
end

/-- Note: the converse is not true. For a counterexample, see
  Walter Rudin *Real and Complex Analysis*, example (c) in section 8.9. -/
lemma ae_ae_of_ae_prod {p : Œ± √ó Œ≤ ‚Üí Prop} (h : ‚àÄ·µê z ‚àÇŒº.prod ŒΩ, p z) :
  ‚àÄ·µê x ‚àÇ Œº, ‚àÄ·µê y ‚àÇ ŒΩ, p (x, y) :=
measure_ae_null_of_prod_null h

/-- `Œº.prod ŒΩ` has finite spanning sets in rectangles of finite spanning sets. -/
def finite_spanning_sets_in.prod {ŒΩ : measure Œ≤} {C : set (set Œ±)} {D : set (set Œ≤)}
  (hŒº : Œº.finite_spanning_sets_in C) (hŒΩ : ŒΩ.finite_spanning_sets_in D)
  (hC : ‚àÄ s ‚àà C, measurable_set s) (hD : ‚àÄ t ‚àà D, measurable_set t) :
  (Œº.prod ŒΩ).finite_spanning_sets_in (image2 set.prod C D) :=
begin
  haveI := hŒΩ.sigma_finite,
  refine ‚ü®Œª n, (hŒº.set n.unpair.1).prod (hŒΩ.set n.unpair.2),
    Œª n, mem_image2_of_mem (hŒº.set_mem _) (hŒΩ.set_mem _), Œª n, _, _‚ü©,
  { simp_rw [prod_prod (hC _ (hŒº.set_mem _)) (hD _ (hŒΩ.set_mem _))],
    exact mul_lt_top (hŒº.finite _).ne (hŒΩ.finite _).ne },
  { simp_rw [Union_unpair_prod, hŒº.spanning, hŒΩ.spanning, univ_prod_univ] }
end

lemma prod_fst_absolutely_continuous : map prod.fst (Œº.prod ŒΩ) ‚â™ Œº :=
begin
  refine absolutely_continuous.mk (Œª s hs h2s, _),
  simp_rw [map_apply measurable_fst hs, ‚Üê prod_univ, prod_prod hs measurable_set.univ],
  rw [h2s, zero_mul] -- for some reason `simp_rw [h2s]` doesn't work
end

lemma prod_snd_absolutely_continuous : map prod.snd (Œº.prod ŒΩ) ‚â™ ŒΩ :=
begin
  refine absolutely_continuous.mk (Œª s hs h2s, _),
  simp_rw [map_apply measurable_snd hs, ‚Üê univ_prod, prod_prod measurable_set.univ hs],
  rw [h2s, mul_zero] -- for some reason `simp_rw [h2s]` doesn't work
end

variables [sigma_finite Œº]

instance prod.sigma_finite : sigma_finite (Œº.prod ŒΩ) :=
(Œº.to_finite_spanning_sets_in.prod ŒΩ.to_finite_spanning_sets_in (Œª _, id) (Œª _, id)).sigma_finite

/-- A measure on a product space equals the product measure if they are equal on rectangles
  with as sides sets that generate the corresponding œÉ-algebras. -/
lemma prod_eq_generate_from {Œº : measure Œ±} {ŒΩ : measure Œ≤} {C : set (set Œ±)}
  {D : set (set Œ≤)} (hC : generate_from C = ‚Äπ_‚Ä∫)
  (hD : generate_from D = ‚Äπ_‚Ä∫) (h2C : is_pi_system C) (h2D : is_pi_system D)
  (h3C : Œº.finite_spanning_sets_in C) (h3D : ŒΩ.finite_spanning_sets_in D)
  {ŒºŒΩ : measure (Œ± √ó Œ≤)}
  (h‚ÇÅ : ‚àÄ (s ‚àà C) (t ‚àà D), ŒºŒΩ (set.prod s t) = Œº s * ŒΩ t) : Œº.prod ŒΩ = ŒºŒΩ :=
begin
  have h4C : ‚àÄ (s : set Œ±), s ‚àà C ‚Üí measurable_set s,
  { intros s hs, rw [‚Üê hC], exact measurable_set_generate_from hs },
  have h4D : ‚àÄ (t : set Œ≤), t ‚àà D ‚Üí measurable_set t,
  { intros t ht, rw [‚Üê hD], exact measurable_set_generate_from ht },
  refine (h3C.prod h3D h4C h4D).ext
    (generate_from_eq_prod hC hD h3C.is_countably_spanning h3D.is_countably_spanning).symm
    (h2C.prod h2D) _,
  { rintro _ ‚ü®s, t, hs, ht, rfl‚ü©, haveI := h3D.sigma_finite,
    simp_rw [h‚ÇÅ s hs t ht, prod_prod (h4C s hs) (h4D t ht)] }
end

/-- A measure on a product space equals the product measure if they are equal on rectangles. -/
lemma prod_eq {ŒºŒΩ : measure (Œ± √ó Œ≤)}
  (h : ‚àÄ s t, measurable_set s ‚Üí measurable_set t ‚Üí ŒºŒΩ (s.prod t) = Œº s * ŒΩ t) : Œº.prod ŒΩ = ŒºŒΩ :=
prod_eq_generate_from generate_from_measurable_set generate_from_measurable_set
  is_pi_system_measurable_set is_pi_system_measurable_set
  Œº.to_finite_spanning_sets_in ŒΩ.to_finite_spanning_sets_in (Œª s hs t ht, h s t hs ht)

lemma prod_swap : map prod.swap (Œº.prod ŒΩ) = ŒΩ.prod Œº :=
begin
  refine (prod_eq _).symm,
  intros s t hs ht,
  simp_rw [map_apply measurable_swap (hs.prod ht), preimage_swap_prod, prod_prod ht hs, mul_comm]
end

lemma prod_apply_symm {s : set (Œ± √ó Œ≤)} (hs : measurable_set s) :
  Œº.prod ŒΩ s = ‚à´‚Åª y, Œº ((Œª x, (x, y)) ‚Åª¬π' s) ‚àÇŒΩ :=
by { rw [‚Üê prod_swap, map_apply measurable_swap hs],
     simp only [prod_apply (measurable_swap hs)], refl }

lemma prod_assoc_prod [sigma_finite œÑ] :
  map measurable_equiv.prod_assoc ((Œº.prod ŒΩ).prod œÑ) = Œº.prod (ŒΩ.prod œÑ) :=
begin
  refine (prod_eq_generate_from generate_from_measurable_set generate_from_prod
    is_pi_system_measurable_set is_pi_system_prod Œº.to_finite_spanning_sets_in
    (ŒΩ.to_finite_spanning_sets_in.prod œÑ.to_finite_spanning_sets_in (Œª _, id) (Œª _, id)) _).symm,
  rintro s hs _ ‚ü®t, u, ht, hu, rfl‚ü©, rw [mem_set_of_eq] at hs ht hu,
  simp_rw [map_apply (measurable_equiv.measurable _) (hs.prod (ht.prod hu)), prod_prod ht hu,
    measurable_equiv.prod_assoc, measurable_equiv.coe_eq, equiv.prod_assoc_preimage,
    prod_prod (hs.prod ht) hu, prod_prod hs ht, mul_assoc]
end

/-! ### The product of specific measures -/

lemma prod_restrict {s : set Œ±} {t : set Œ≤} (hs : measurable_set s) (ht : measurable_set t) :
  (Œº.restrict s).prod (ŒΩ.restrict t) = (Œº.prod ŒΩ).restrict (s.prod t) :=
begin
  refine prod_eq (Œª s' t' hs' ht', _),
  simp_rw [restrict_apply (hs'.prod ht'), prod_inter_prod, prod_prod (hs'.inter hs) (ht'.inter ht),
    restrict_apply hs', restrict_apply ht']
end

lemma restrict_prod_eq_prod_univ {s : set Œ±} (hs : measurable_set s) :
  (Œº.restrict s).prod ŒΩ = (Œº.prod ŒΩ).restrict (s.prod univ) :=
begin
  have : ŒΩ = ŒΩ.restrict set.univ := measure.restrict_univ.symm,
  rwa [this, measure.prod_restrict, ‚Üê this],
  exact measurable_set.univ,
end

lemma prod_dirac (y : Œ≤) : Œº.prod (dirac y) = map (Œª x, (x, y)) Œº :=
begin
  refine prod_eq (Œª s t hs ht, _),
  simp_rw [map_apply measurable_prod_mk_right (hs.prod ht), mk_preimage_prod_left_eq_if, measure_if,
    dirac_apply' _ ht, ‚Üê indicator_mul_right _ (Œª x, Œº s), pi.one_apply, mul_one]
end

lemma dirac_prod (x : Œ±) : (dirac x).prod ŒΩ = map (prod.mk x) ŒΩ :=
begin
  refine prod_eq (Œª s t hs ht, _),
  simp_rw [map_apply measurable_prod_mk_left (hs.prod ht), mk_preimage_prod_right_eq_if, measure_if,
    dirac_apply' _ hs, ‚Üê indicator_mul_left _ _ (Œª x, ŒΩ t), pi.one_apply, one_mul]
end

lemma dirac_prod_dirac {x : Œ±} {y : Œ≤} : (dirac x).prod (dirac y) = dirac (x, y) :=
by rw [prod_dirac, map_dirac measurable_prod_mk_right]

lemma prod_sum {Œπ : Type*} [fintype Œπ] (ŒΩ : Œπ ‚Üí measure Œ≤) [‚àÄ i, sigma_finite (ŒΩ i)] :
  Œº.prod (sum ŒΩ) = sum (Œª i, Œº.prod (ŒΩ i)) :=
begin
  refine prod_eq (Œª s t hs ht, _),
  simp_rw [sum_apply _ (hs.prod ht), sum_apply _ ht, prod_prod hs ht, tsum_fintype, finset.mul_sum]
end

lemma sum_prod {Œπ : Type*} [fintype Œπ] (Œº : Œπ ‚Üí measure Œ±) [‚àÄ i, sigma_finite (Œº i)] :
  (sum Œº).prod ŒΩ = sum (Œª i, (Œº i).prod ŒΩ) :=
begin
  refine prod_eq (Œª s t hs ht, _),
  simp_rw [sum_apply _ (hs.prod ht), sum_apply _ hs, prod_prod hs ht, tsum_fintype, finset.sum_mul]
end

lemma prod_add (ŒΩ' : measure Œ≤) [sigma_finite ŒΩ'] : Œº.prod (ŒΩ + ŒΩ') = Œº.prod ŒΩ + Œº.prod ŒΩ' :=
by { refine prod_eq (Œª s t hs ht, _), simp_rw [add_apply, prod_prod hs ht, left_distrib] }

lemma add_prod (Œº' : measure Œ±) [sigma_finite Œº'] : (Œº + Œº').prod ŒΩ = Œº.prod ŒΩ + Œº'.prod ŒΩ :=
by { refine prod_eq (Œª s t hs ht, _), simp_rw [add_apply, prod_prod hs ht, right_distrib] }

@[simp] lemma zero_prod (ŒΩ : measure Œ≤) : (0 : measure Œ±).prod ŒΩ = 0 :=
by { rw measure.prod, exact bind_zero_left _ }

@[simp] lemma prod_zero (Œº : measure Œ±) : Œº.prod (0 : measure Œ≤) = 0 :=
by simp [measure.prod]

lemma map_prod_map {Œ¥} [measurable_space Œ¥] {f : Œ± ‚Üí Œ≤} {g : Œ≥ ‚Üí Œ¥}
  {Œºa : measure Œ±} {Œºc : measure Œ≥} (hfa : sigma_finite (map f Œºa))
  (hgc : sigma_finite (map g Œºc)) (hf : measurable f) (hg : measurable g) :
  (map f Œºa).prod (map g Œºc) = map (prod.map f g) (Œºa.prod Œºc) :=
begin
  haveI := hgc.of_map Œºc hg,
  refine prod_eq (Œª s t hs ht, _),
  rw [map_apply (hf.prod_map hg) (hs.prod ht), map_apply hf hs, map_apply hg ht],
  exact prod_prod (hf hs) (hg ht)
end

end measure
end measure_theory

open measure_theory.measure

section

lemma ae_measurable.prod_swap [sigma_finite Œº] [sigma_finite ŒΩ] {f : Œ≤ √ó Œ± ‚Üí Œ≥}
  (hf : ae_measurable f (ŒΩ.prod Œº)) : ae_measurable (Œª (z : Œ± √ó Œ≤), f z.swap) (Œº.prod ŒΩ) :=
by { rw ‚Üê prod_swap at hf, exact hf.comp_measurable measurable_swap }

lemma ae_measurable.fst [sigma_finite ŒΩ] {f : Œ± ‚Üí Œ≥}
  (hf : ae_measurable f Œº) : ae_measurable (Œª (z : Œ± √ó Œ≤), f z.1) (Œº.prod ŒΩ) :=
hf.comp_measurable' measurable_fst prod_fst_absolutely_continuous

lemma ae_measurable.snd [sigma_finite ŒΩ] {f : Œ≤ ‚Üí Œ≥}
  (hf : ae_measurable f ŒΩ) : ae_measurable (Œª (z : Œ± √ó Œ≤), f z.2) (Œº.prod ŒΩ) :=
hf.comp_measurable' measurable_snd prod_snd_absolutely_continuous

/-- The Bochner integral is a.e.-measurable.
  This shows that the integrand of (the right-hand-side of) Fubini's theorem is a.e.-measurable. -/
lemma ae_measurable.integral_prod_right' [sigma_finite ŒΩ]
  [second_countable_topology E] [normed_space ‚Ñù E] [borel_space E] [complete_space E]
  ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ (hf : ae_measurable f (Œº.prod ŒΩ)) : ae_measurable (Œª x, ‚à´ y, f (x, y) ‚àÇŒΩ) Œº :=
‚ü®Œª x, ‚à´ y, hf.mk f (x, y) ‚àÇŒΩ, hf.measurable_mk.integral_prod_right', begin
  filter_upwards [ae_ae_of_ae_prod hf.ae_eq_mk],
  assume x hx,
  exact integral_congr_ae hx
end‚ü©

lemma ae_measurable.prod_mk_left [sigma_finite ŒΩ] {f : Œ± √ó Œ≤ ‚Üí Œ≥}
  (hf : ae_measurable f (Œº.prod ŒΩ)) : ‚àÄ·µê x ‚àÇŒº, ae_measurable (Œª y, f (x, y)) ŒΩ :=
begin
  filter_upwards [ae_ae_of_ae_prod hf.ae_eq_mk],
  intros x hx,
  exact ‚ü®Œª y, hf.mk f (x, y), hf.measurable_mk.comp measurable_prod_mk_left, hx‚ü©
end

end

namespace measure_theory

/-! ### The Lebesgue integral on a product -/

variables [sigma_finite ŒΩ]

lemma lintegral_prod_swap [sigma_finite Œº] (f : Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû)
  (hf : ae_measurable f (Œº.prod ŒΩ)) : ‚à´‚Åª z, f z.swap ‚àÇ(ŒΩ.prod Œº) = ‚à´‚Åª z, f z ‚àÇ(Œº.prod ŒΩ) :=
by { rw ‚Üê prod_swap at hf, rw [‚Üê lintegral_map' hf measurable_swap, prod_swap] }

/-- **Tonelli's Theorem**: For `‚Ñù‚â•0‚àû`-valued measurable functions on `Œ± √ó Œ≤`,
  the integral of `f` is equal to the iterated integral. -/
lemma lintegral_prod_of_measurable :
  ‚àÄ (f : Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû) (hf : measurable f), ‚à´‚Åª z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´‚Åª x, ‚à´‚Åª y, f (x, y) ‚àÇŒΩ ‚àÇŒº :=
begin
  have m := @measurable_prod_mk_left,
  refine measurable.ennreal_induction _ _ _,
  { intros c s hs, simp only [‚Üê indicator_comp_right],
    simp [lintegral_indicator, m hs, hs, lintegral_const_mul, measurable_measure_prod_mk_left hs,
      prod_apply] },
  { rintro f g - hf hg h2f h2g,
    simp [lintegral_add, measurable.lintegral_prod_right', hf.comp m, hg.comp m,
      hf, hg, h2f, h2g] },
  { intros f hf h2f h3f,
    have kf : ‚àÄ x n, measurable (Œª y, f n (x, y)) := Œª x n, (hf n).comp m,
    have k2f : ‚àÄ x, monotone (Œª n y, f n (x, y)) := Œª x i j hij y, h2f hij (x, y),
    have lf : ‚àÄ n, measurable (Œª x, ‚à´‚Åª y, f n (x, y) ‚àÇŒΩ) := Œª n, (hf n).lintegral_prod_right',
    have l2f : monotone (Œª n x, ‚à´‚Åª y, f n (x, y) ‚àÇŒΩ) := Œª i j hij x, lintegral_mono (k2f x hij),
    simp only [lintegral_supr hf h2f, lintegral_supr (kf _), k2f, lintegral_supr lf l2f, h3f] },
end

/-- **Tonelli's Theorem**: For `‚Ñù‚â•0‚àû`-valued almost everywhere measurable functions on `Œ± √ó Œ≤`,
  the integral of `f` is equal to the iterated integral. -/
lemma lintegral_prod (f : Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû) (hf : ae_measurable f (Œº.prod ŒΩ)) :
  ‚à´‚Åª z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´‚Åª x, ‚à´‚Åª y, f (x, y) ‚àÇŒΩ ‚àÇŒº :=
begin
  have A : ‚à´‚Åª z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´‚Åª z, hf.mk f z ‚àÇ(Œº.prod ŒΩ) :=
    lintegral_congr_ae hf.ae_eq_mk,
  have B : ‚à´‚Åª x, ‚à´‚Åª y, f (x, y) ‚àÇŒΩ ‚àÇŒº = ‚à´‚Åª x, ‚à´‚Åª y, hf.mk f (x, y) ‚àÇŒΩ ‚àÇŒº,
  { apply lintegral_congr_ae,
    filter_upwards [ae_ae_of_ae_prod hf.ae_eq_mk],
    assume a ha,
    exact lintegral_congr_ae ha },
  rw [A, B, lintegral_prod_of_measurable _ hf.measurable_mk],
  apply_instance
end

/-- The symmetric verion of Tonelli's Theorem: For `‚Ñù‚â•0‚àû`-valued almost everywhere measurable
functions on `Œ± √ó Œ≤`,  the integral of `f` is equal to the iterated integral, in reverse order. -/
lemma lintegral_prod_symm' [sigma_finite Œº] (f : Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû)
  (hf : ae_measurable f (Œº.prod ŒΩ)) : ‚à´‚Åª z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´‚Åª y, ‚à´‚Åª x, f (x, y) ‚àÇŒº ‚àÇŒΩ :=
by { simp_rw [‚Üê lintegral_prod_swap f hf], exact lintegral_prod _ hf.prod_swap }

/-- The symmetric verion of Tonelli's Theorem: For `‚Ñù‚â•0‚àû`-valued measurable
functions on `Œ± √ó Œ≤`,  the integral of `f` is equal to the iterated integral, in reverse order. -/
lemma lintegral_prod_symm [sigma_finite Œº] (f : Œ± √ó Œ≤ ‚Üí ‚Ñù‚â•0‚àû)
  (hf : ae_measurable f (Œº.prod ŒΩ)) : ‚à´‚Åª z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´‚Åª y, ‚à´‚Åª x, f (x, y) ‚àÇŒº ‚àÇŒΩ :=
lintegral_prod_symm' f hf

/-- The reversed version of **Tonelli's Theorem**. In this version `f` is in curried form, which
makes it easier for the elaborator to figure out `f` automatically. -/
lemma lintegral_lintegral ‚¶Éf : Œ± ‚Üí Œ≤ ‚Üí ‚Ñù‚â•0‚àû‚¶Ñ
  (hf : ae_measurable (uncurry f) (Œº.prod ŒΩ)) :
  ‚à´‚Åª x, ‚à´‚Åª y, f x y ‚àÇŒΩ ‚àÇŒº = ‚à´‚Åª z, f z.1 z.2 ‚àÇ(Œº.prod ŒΩ) :=
(lintegral_prod _ hf).symm

/-- The reversed version of **Tonelli's Theorem** (symmetric version). In this version `f` is in
curried form, which makes it easier for the elaborator to figure out `f` automatically. -/
lemma lintegral_lintegral_symm [sigma_finite Œº] ‚¶Éf : Œ± ‚Üí Œ≤ ‚Üí ‚Ñù‚â•0‚àû‚¶Ñ
  (hf : ae_measurable (uncurry f) (Œº.prod ŒΩ)) :
  ‚à´‚Åª x, ‚à´‚Åª y, f x y ‚àÇŒΩ ‚àÇŒº = ‚à´‚Åª z, f z.2 z.1 ‚àÇ(ŒΩ.prod Œº) :=
(lintegral_prod_symm _ hf.prod_swap).symm

/-- Change the order of Lebesgue integration. -/
lemma lintegral_lintegral_swap [sigma_finite Œº] ‚¶Éf : Œ± ‚Üí Œ≤ ‚Üí ‚Ñù‚â•0‚àû‚¶Ñ
  (hf : ae_measurable (uncurry f) (Œº.prod ŒΩ)) :
  ‚à´‚Åª x, ‚à´‚Åª y, f x y ‚àÇŒΩ ‚àÇŒº = ‚à´‚Åª y, ‚à´‚Åª x, f x y ‚àÇŒº ‚àÇŒΩ :=
(lintegral_lintegral hf).trans (lintegral_prod_symm _ hf)

lemma lintegral_prod_mul {f : Œ± ‚Üí ‚Ñù‚â•0‚àû} {g : Œ≤ ‚Üí ‚Ñù‚â•0‚àû}
  (hf : ae_measurable f Œº) (hg : ae_measurable g ŒΩ) :
  ‚à´‚Åª z, f z.1 * g z.2 ‚àÇ(Œº.prod ŒΩ) = ‚à´‚Åª x, f x ‚àÇŒº * ‚à´‚Åª y, g y ‚àÇŒΩ :=
by simp [lintegral_prod _ (hf.fst.mul hg.snd), lintegral_lintegral_mul hf hg]

/-! ### Integrability on a product -/
section

variables [opens_measurable_space E]

lemma integrable.swap [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) : integrable (f ‚àò prod.swap) (ŒΩ.prod Œº) :=
‚ü®hf.ae_measurable.prod_swap,
  (lintegral_prod_swap _ hf.ae_measurable.ennnorm : _).le.trans_lt hf.has_finite_integral‚ü©

lemma integrable_swap_iff [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ :
  integrable (f ‚àò prod.swap) (ŒΩ.prod Œº) ‚Üî integrable f (Œº.prod ŒΩ) :=
‚ü®Œª hf, by { convert hf.swap, ext ‚ü®x, y‚ü©, refl }, Œª hf, hf.swap‚ü©

lemma has_finite_integral_prod_iff ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ (h1f : measurable f) :
  has_finite_integral f (Œº.prod ŒΩ) ‚Üî (‚àÄ·µê x ‚àÇ Œº, has_finite_integral (Œª y, f (x, y)) ŒΩ) ‚àß
    has_finite_integral (Œª x, ‚à´ y, ‚à•f (x, y)‚à• ‚àÇŒΩ) Œº :=
begin
  simp only [has_finite_integral, lintegral_prod_of_measurable _ h1f.ennnorm],
  have : ‚àÄ x, ‚àÄ·µê y ‚àÇŒΩ, 0 ‚â§ ‚à•f (x, y)‚à• := Œª x, eventually_of_forall (Œª y, norm_nonneg _),
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _)
    (h1f.norm.comp measurable_prod_mk_left).ae_measurable,
    ennnorm_eq_of_real to_real_nonneg, of_real_norm_eq_coe_nnnorm],
  -- this fact is probably too specialized to be its own lemma
  have : ‚àÄ {p q r : Prop} (h1 : r ‚Üí p), (r ‚Üî p ‚àß q) ‚Üî (p ‚Üí (r ‚Üî q)) :=
  Œª p q r h1, by rw [‚Üê and.congr_right_iff, and_iff_right_of_imp h1],
  rw [this],
  { intro h2f, rw lintegral_congr_ae,
    refine h2f.mp _, apply eventually_of_forall, intros x hx, dsimp only,
    rw [of_real_to_real], rw [‚Üê lt_top_iff_ne_top], exact hx },
  { intro h2f, refine ae_lt_top _ h2f.ne, exact h1f.ennnorm.lintegral_prod_right' },
end

lemma has_finite_integral_prod_iff' ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ (h1f : ae_measurable f (Œº.prod ŒΩ)) :
  has_finite_integral f (Œº.prod ŒΩ) ‚Üî (‚àÄ·µê x ‚àÇ Œº, has_finite_integral (Œª y, f (x, y)) ŒΩ) ‚àß
    has_finite_integral (Œª x, ‚à´ y, ‚à•f (x, y)‚à• ‚àÇŒΩ) Œº :=
begin
  rw [has_finite_integral_congr h1f.ae_eq_mk, has_finite_integral_prod_iff h1f.measurable_mk],
  apply and_congr,
  { apply eventually_congr,
    filter_upwards [ae_ae_of_ae_prod h1f.ae_eq_mk.symm],
    assume x hx,
    exact has_finite_integral_congr hx },
  { apply has_finite_integral_congr,
    filter_upwards [ae_ae_of_ae_prod h1f.ae_eq_mk.symm],
    assume x hx,
    exact integral_congr_ae (eventually_eq.fun_comp hx _) },
  { apply_instance }
end


/-- A binary function is integrable if the function `y ‚Ü¶ f (x, y)` is integrable for almost every
  `x` and the function `x ‚Ü¶ ‚à´ ‚à•f (x, y)‚à• dy` is integrable. -/
lemma integrable_prod_iff ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ (h1f : ae_measurable f (Œº.prod ŒΩ)) :
  integrable f (Œº.prod ŒΩ) ‚Üî
    (‚àÄ·µê x ‚àÇ Œº, integrable (Œª y, f (x, y)) ŒΩ) ‚àß integrable (Œª x, ‚à´ y, ‚à•f (x, y)‚à• ‚àÇŒΩ) Œº :=
by simp [integrable, h1f, has_finite_integral_prod_iff', h1f.norm.integral_prod_right',
         h1f.prod_mk_left]

/-- A binary function is integrable if the function `x ‚Ü¶ f (x, y)` is integrable for almost every
  `y` and the function `y ‚Ü¶ ‚à´ ‚à•f (x, y)‚à• dx` is integrable. -/
lemma integrable_prod_iff' [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ (h1f : ae_measurable f (Œº.prod ŒΩ)) :
  integrable f (Œº.prod ŒΩ) ‚Üî
    (‚àÄ·µê y ‚àÇ ŒΩ, integrable (Œª x, f (x, y)) Œº) ‚àß integrable (Œª y, ‚à´ x, ‚à•f (x, y)‚à• ‚àÇŒº) ŒΩ :=
by { convert integrable_prod_iff (h1f.prod_swap) using 1, rw [integrable_swap_iff] }

lemma integrable.prod_left_ae [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) : ‚àÄ·µê y ‚àÇ ŒΩ, integrable (Œª x, f (x, y)) Œº :=
((integrable_prod_iff' hf.ae_measurable).mp hf).1

lemma integrable.prod_right_ae [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) : ‚àÄ·µê x ‚àÇ Œº, integrable (Œª y, f (x, y)) ŒΩ :=
hf.swap.prod_left_ae

lemma integrable.integral_norm_prod_left ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) : integrable (Œª x, ‚à´ y, ‚à•f (x, y)‚à• ‚àÇŒΩ) Œº :=
((integrable_prod_iff hf.ae_measurable).mp hf).2

lemma integrable.integral_norm_prod_right [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) : integrable (Œª y, ‚à´ x, ‚à•f (x, y)‚à• ‚àÇŒº) ŒΩ :=
hf.swap.integral_norm_prod_left

end

variables [second_countable_topology E] [normed_space ‚Ñù E]
  [complete_space E] [borel_space E]

lemma integrable.integral_prod_left ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) : integrable (Œª x, ‚à´ y, f (x, y) ‚àÇŒΩ) Œº :=
integrable.mono hf.integral_norm_prod_left hf.ae_measurable.integral_prod_right' $
  eventually_of_forall $ Œª x, (norm_integral_le_integral_norm _).trans_eq $
  (norm_of_nonneg $ integral_nonneg_of_ae $ eventually_of_forall $
  Œª y, (norm_nonneg (f (x, y)) : _)).symm

lemma integrable.integral_prod_right [sigma_finite Œº] ‚¶Éf : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) : integrable (Œª y, ‚à´ x, f (x, y) ‚àÇŒº) ŒΩ :=
hf.swap.integral_prod_left

/-! ### The Bochner integral on a product -/

variables [sigma_finite Œº]

lemma integral_prod_swap (f : Œ± √ó Œ≤ ‚Üí E)
  (hf : ae_measurable f (Œº.prod ŒΩ)) : ‚à´ z, f z.swap ‚àÇ(ŒΩ.prod Œº) = ‚à´ z, f z ‚àÇ(Œº.prod ŒΩ) :=
begin
  rw ‚Üê prod_swap at hf,
  rw [‚Üê integral_map measurable_swap hf, prod_swap]
end

variables {E' : Type*} [measurable_space E'] [normed_group E'] [borel_space E'] [complete_space E']
  [normed_space ‚Ñù E'] [second_countable_topology E']

/-! Some rules about the sum/difference of double integrals. They follow from `integral_add`, but
  we separate them out as separate lemmas, because they involve quite some steps. -/

/-- Integrals commute with addition inside another integral. `F` can be any function. -/
lemma integral_fn_integral_add ‚¶Éf g : Œ± √ó Œ≤ ‚Üí E‚¶Ñ (F : E ‚Üí E')
  (hf : integrable f (Œº.prod ŒΩ)) (hg : integrable g (Œº.prod ŒΩ)) :
  ‚à´ x, F (‚à´ y, f (x, y) + g (x, y) ‚àÇŒΩ) ‚àÇŒº = ‚à´ x, F (‚à´ y, f (x, y) ‚àÇŒΩ + ‚à´ y, g (x, y) ‚àÇŒΩ) ‚àÇŒº :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae],
  intros x h2f h2g, simp [integral_add h2f h2g],
end

/-- Integrals commute with subtraction inside another integral.
  `F` can be any measurable function. -/
lemma integral_fn_integral_sub ‚¶Éf g : Œ± √ó Œ≤ ‚Üí E‚¶Ñ (F : E ‚Üí E')
  (hf : integrable f (Œº.prod ŒΩ)) (hg : integrable g (Œº.prod ŒΩ)) :
  ‚à´ x, F (‚à´ y, f (x, y) - g (x, y) ‚àÇŒΩ) ‚àÇŒº = ‚à´ x, F (‚à´ y, f (x, y) ‚àÇŒΩ - ‚à´ y, g (x, y) ‚àÇŒΩ) ‚àÇŒº :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae],
  intros x h2f h2g, simp [integral_sub h2f h2g]
end

/-- Integrals commute with subtraction inside a lower Lebesgue integral.
  `F` can be any function. -/
lemma lintegral_fn_integral_sub ‚¶Éf g : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (F : E ‚Üí ‚Ñù‚â•0‚àû) (hf : integrable f (Œº.prod ŒΩ)) (hg : integrable g (Œº.prod ŒΩ)) :
  ‚à´‚Åª x, F (‚à´ y, f (x, y) - g (x, y) ‚àÇŒΩ) ‚àÇŒº = ‚à´‚Åª x, F (‚à´ y, f (x, y) ‚àÇŒΩ - ‚à´ y, g (x, y) ‚àÇŒΩ) ‚àÇŒº :=
begin
  refine lintegral_congr_ae _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae],
  intros x h2f h2g, simp [integral_sub h2f h2g]
end

/-- Double integrals commute with addition. -/
lemma integral_integral_add ‚¶Éf g : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) (hg : integrable g (Œº.prod ŒΩ)) :
  ‚à´ x, ‚à´ y, f (x, y) + g (x, y) ‚àÇŒΩ ‚àÇŒº = ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº + ‚à´ x, ‚à´ y, g (x, y) ‚àÇŒΩ ‚àÇŒº :=
(integral_fn_integral_add id hf hg).trans $
  integral_add hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with addition. This is the version with `(f + g) (x, y)`
  (instead of `f (x, y) + g (x, y)`) in the LHS. -/
lemma integral_integral_add' ‚¶Éf g : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) (hg : integrable g (Œº.prod ŒΩ)) :
  ‚à´ x, ‚à´ y, (f + g) (x, y) ‚àÇŒΩ ‚àÇŒº = ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº + ‚à´ x, ‚à´ y, g (x, y) ‚àÇŒΩ ‚àÇŒº :=
integral_integral_add hf hg

/-- Double integrals commute with subtraction. -/
lemma integral_integral_sub ‚¶Éf g : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) (hg : integrable g (Œº.prod ŒΩ)) :
  ‚à´ x, ‚à´ y, f (x, y) - g (x, y) ‚àÇŒΩ ‚àÇŒº = ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº - ‚à´ x, ‚à´ y, g (x, y) ‚àÇŒΩ ‚àÇŒº :=
(integral_fn_integral_sub id hf hg).trans $
  integral_sub hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with subtraction. This is the version with `(f - g) (x, y)`
  (instead of `f (x, y) - g (x, y)`) in the LHS. -/
lemma integral_integral_sub' ‚¶Éf g : Œ± √ó Œ≤ ‚Üí E‚¶Ñ
  (hf : integrable f (Œº.prod ŒΩ)) (hg : integrable g (Œº.prod ŒΩ)) :
  ‚à´ x, ‚à´ y, (f - g) (x, y) ‚àÇŒΩ ‚àÇŒº = ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº - ‚à´ x, ‚à´ y, g (x, y) ‚àÇŒΩ ‚àÇŒº :=
integral_integral_sub hf hg

/-- The map that sends an L¬π-function `f : Œ± √ó Œ≤ ‚Üí E` to `‚à´‚à´f` is continuous. -/
lemma continuous_integral_integral :
  continuous (Œª (f : Œ± √ó Œ≤ ‚Üí‚ÇÅ[Œº.prod ŒΩ] E), ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº) :=
begin
  rw [continuous_iff_continuous_at], intro g,
  refine tendsto_integral_of_L1 _ (L1.integrable_coe_fn g).integral_prod_left
    (eventually_of_forall $ Œª h, (L1.integrable_coe_fn h).integral_prod_left) _,
  simp_rw [‚Üê lintegral_fn_integral_sub (Œª x, (nnnorm x : ‚Ñù‚â•0‚àû)) (L1.integrable_coe_fn _)
    (L1.integrable_coe_fn g)],
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (Œª i, zero_le _) _,
  { exact Œª i, ‚à´‚Åª x, ‚à´‚Åª y, nnnorm (i (x, y) - g (x, y)) ‚àÇŒΩ ‚àÇŒº },
  swap, { exact Œª i, lintegral_mono (Œª x, ennnorm_integral_le_lintegral_ennnorm _) },
  show tendsto (Œª (i : Œ± √ó Œ≤ ‚Üí‚ÇÅ[Œº.prod ŒΩ] E),
    ‚à´‚Åª x, ‚à´‚Åª (y : Œ≤), nnnorm (i (x, y) - g (x, y)) ‚àÇŒΩ ‚àÇŒº) (ùìù g) (ùìù 0),
  have : ‚àÄ (i : Œ± √ó Œ≤ ‚Üí‚ÇÅ[Œº.prod ŒΩ] E), measurable (Œª z, (nnnorm (i z - g z) : ‚Ñù‚â•0‚àû)) :=
  Œª i, ((Lp.measurable i).sub (Lp.measurable g)).ennnorm,
  simp_rw [‚Üê lintegral_prod_of_measurable _ (this _), ‚Üê L1.of_real_norm_sub_eq_lintegral,
    ‚Üê of_real_zero],
  refine (continuous_of_real.tendsto 0).comp _,
  rw [‚Üê tendsto_iff_norm_tendsto_zero], exact tendsto_id
end

/-- **Fubini's Theorem**: For integrable functions on `Œ± √ó Œ≤`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  `integrable_prod_iff` can be useful to show that the function in question in integrable.
  `measure_theory.integrable.integral_prod_right` is useful to show that the inner integral
  of the right-hand side is integrable. -/
lemma integral_prod : ‚àÄ (f : Œ± √ó Œ≤ ‚Üí E) (hf : integrable f (Œº.prod ŒΩ)),
  ‚à´ z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´ x, ‚à´ y, f (x, y) ‚àÇŒΩ ‚àÇŒº :=
begin
  apply integrable.induction,
  { intros c s hs h2s,
    simp_rw [integral_indicator hs, ‚Üê indicator_comp_right,
      function.comp, integral_indicator (measurable_prod_mk_left hs),
      set_integral_const, integral_smul_const,
      integral_to_real (measurable_measure_prod_mk_left hs).ae_measurable
      (ae_measure_lt_top hs h2s.ne), prod_apply hs] },
  { intros f g hfg i_f i_g hf hg,
    simp_rw [integral_add' i_f i_g, integral_integral_add' i_f i_g, hf, hg] },
  { exact is_closed_eq continuous_integral continuous_integral_integral },
  { intros f g hfg i_f hf, convert hf using 1,
    { exact integral_congr_ae hfg.symm },
    { refine integral_congr_ae _,
      refine (ae_ae_of_ae_prod hfg).mp _,
      apply eventually_of_forall, intros x hfgx,
      exact integral_congr_ae (ae_eq_symm hfgx) } }
end

/-- Symmetric version of **Fubini's Theorem**: For integrable functions on `Œ± √ó Œ≤`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  This version has the integrals on the right-hand side in the other order. -/
lemma integral_prod_symm (f : Œ± √ó Œ≤ ‚Üí E) (hf : integrable f (Œº.prod ŒΩ)) :
  ‚à´ z, f z ‚àÇ(Œº.prod ŒΩ) = ‚à´ y, ‚à´ x, f (x, y) ‚àÇŒº ‚àÇŒΩ :=
by { simp_rw [‚Üê integral_prod_swap f hf.ae_measurable], exact integral_prod _ hf.swap }

/-- Reversed version of **Fubini's Theorem**. -/
lemma integral_integral {f : Œ± ‚Üí Œ≤ ‚Üí E} (hf : integrable (uncurry f) (Œº.prod ŒΩ)) :
  ‚à´ x, ‚à´ y, f x y ‚àÇŒΩ ‚àÇŒº = ‚à´ z, f z.1 z.2 ‚àÇ(Œº.prod ŒΩ) :=
(integral_prod _ hf).symm

/-- Reversed version of **Fubini's Theorem** (symmetric version). -/
lemma integral_integral_symm {f : Œ± ‚Üí Œ≤ ‚Üí E} (hf : integrable (uncurry f) (Œº.prod ŒΩ)) :
  ‚à´ x, ‚à´ y, f x y ‚àÇŒΩ ‚àÇŒº = ‚à´ z, f z.2 z.1 ‚àÇ(ŒΩ.prod Œº) :=
(integral_prod_symm _ hf.swap).symm

/-- Change the order of Bochner integration. -/
lemma integral_integral_swap ‚¶Éf : Œ± ‚Üí Œ≤ ‚Üí E‚¶Ñ (hf : integrable (uncurry f) (Œº.prod ŒΩ)) :
  ‚à´ x, ‚à´ y, f x y ‚àÇŒΩ ‚àÇŒº = ‚à´ y, ‚à´ x, f x y ‚àÇŒº ‚àÇŒΩ :=
(integral_integral hf).trans (integral_prod_symm _ hf)

end measure_theory
