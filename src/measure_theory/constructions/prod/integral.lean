/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import measure_theory.constructions.prod.basic
import measure_theory.integral.set_integral

/-!
# Integration with respect to the product measure

In this file we prove Fubini's theorem.

## Main results

* `measure_theory.integrable_prod_iff` states that a binary function is integrable iff both
  * `y â†¦ f (x, y)` is integrable for almost every `x`, and
  * the function `x â†¦ âˆ« â€–f (x, y)â€– dy` is integrable.
* `measure_theory.integral_prod`: Fubini's theorem. It states that for a integrable function
  `Î± Ã— Î² â†’ E` (where `E` is a second countable Banach space) we have
  `âˆ« z, f z âˆ‚(Î¼.prod Î½) = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼`. This theorem has the same variants as
  Tonelli's theorem (see `measure_theory.lintegral_prod`). The lemma
  `measure_theory.integrable.integral_prod_right` states that the inner integral of the right-hand
  side is integrable.

## Tags

product measure, Fubini's theorem, Fubini-Tonelli theorem
-/

noncomputable theory
open_locale classical topology ennreal measure_theory
open set function real ennreal
open measure_theory measurable_space measure_theory.measure
open topological_space
open filter (hiding prod_eq map)

variables {Î± Î±' Î² Î²' Î³ E : Type*}

variables [measurable_space Î±] [measurable_space Î±'] [measurable_space Î²] [measurable_space Î²']
variables [measurable_space Î³]
variables {Î¼ Î¼' : measure Î±} {Î½ Î½' : measure Î²} {Ï„ : measure Î³}
variables [normed_add_comm_group E]

/-! ### Measurability

Before we define the product measure, we can talk about the measurability of operations on binary
functions. We show that if `f` is a binary measurable function, then the function that integrates
along one of the variables (using either the Lebesgue or Bochner integral) is measurable.
-/

lemma measurable_set_integrable [sigma_finite Î½] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
  (hf : strongly_measurable (uncurry f)) : measurable_set {x | integrable (f x) Î½} :=
begin
  simp_rw [integrable, hf.of_uncurry_left.ae_strongly_measurable, true_and],
  exact measurable_set_lt (measurable.lintegral_prod_right hf.ennnorm) measurable_const
end

section
variables [normed_space â„ E] [complete_space E]

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable.
  This version has `f` in curried form. -/
lemma measure_theory.strongly_measurable.integral_prod_right [sigma_finite Î½] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
  (hf : strongly_measurable (uncurry f)) : strongly_measurable (Î» x, âˆ« y, f x y âˆ‚Î½) :=
begin
  borelize E,
  haveI : separable_space (range (uncurry f) âˆª {0} : set E) :=
    hf.separable_space_range_union_singleton,
  let s : â„• â†’ simple_func (Î± Ã— Î²) E := simple_func.approx_on _ hf.measurable
    (range (uncurry f) âˆª {0}) 0 (by simp),
  let s' : â„• â†’ Î± â†’ simple_func Î² E := Î» n x, (s n).comp (prod.mk x) measurable_prod_mk_left,
  let f' : â„• â†’ Î± â†’ E := Î» n, {x | integrable (f x) Î½}.indicator
    (Î» x, (s' n x).integral Î½),
  have hf' : âˆ€ n, strongly_measurable (f' n),
  { intro n, refine strongly_measurable.indicator _ (measurable_set_integrable hf),
    have : âˆ€ x, (s' n x).range.filter (Î» x, x â‰  0) âŠ† (s n).range,
    { intros x, refine finset.subset.trans (finset.filter_subset _ _) _, intro y,
      simp_rw [simple_func.mem_range], rintro âŸ¨z, rflâŸ©, exact âŸ¨(x, z), rflâŸ© },
    simp only [simple_func.integral_eq_sum_of_subset (this _)],
    refine finset.strongly_measurable_sum _ (Î» x _, _),
    refine (measurable.ennreal_to_real _).strongly_measurable.smul_const _,
    simp only [simple_func.coe_comp, preimage_comp] {single_pass := tt},
    apply measurable_measure_prod_mk_left,
    exact (s n).measurable_set_fiber x },
  have h2f' : tendsto f' at_top (ğ“ (Î» (x : Î±), âˆ« (y : Î²), f x y âˆ‚Î½)),
  { rw [tendsto_pi_nhds], intro x,
    by_cases hfx : integrable (f x) Î½,
    { have : âˆ€ n, integrable (s' n x) Î½,
      { intro n, apply (hfx.norm.add hfx.norm).mono' (s' n x).ae_strongly_measurable,
        apply eventually_of_forall, intro y,
        simp_rw [s', simple_func.coe_comp], exact simple_func.norm_approx_on_zero_le _ _ (x, y) n },
      simp only [f', hfx, simple_func.integral_eq_integral _ (this _), indicator_of_mem,
        mem_set_of_eq],
      refine tendsto_integral_of_dominated_convergence (Î» y, â€–f x yâ€– + â€–f x yâ€–)
        (Î» n, (s' n x).ae_strongly_measurable) (hfx.norm.add hfx.norm) _ _,
      { exact Î» n, eventually_of_forall (Î» y, simple_func.norm_approx_on_zero_le _ _ (x, y) n) },
      { refine eventually_of_forall (Î» y, simple_func.tendsto_approx_on _ _ _),
        apply subset_closure,
        simp [-uncurry_apply_pair], } },
    { simp [f', hfx, integral_undef], } },
  exact strongly_measurable_of_tendsto _ hf' h2f'
end

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  Fubini's theorem is measurable. -/
lemma measure_theory.strongly_measurable.integral_prod_right' [sigma_finite Î½] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : strongly_measurable f) : strongly_measurable (Î» x, âˆ« y, f (x, y) âˆ‚Î½) :=
by { rw [â† uncurry_curry f] at hf, exact hf.integral_prod_right }

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable.
  This version has `f` in curried form. -/
lemma measure_theory.strongly_measurable.integral_prod_left [sigma_finite Î¼] â¦ƒf : Î± â†’ Î² â†’ Eâ¦„
  (hf : strongly_measurable (uncurry f)) : strongly_measurable (Î» y, âˆ« x, f x y âˆ‚Î¼) :=
(hf.comp_measurable measurable_swap).integral_prod_right'

/-- The Bochner integral is measurable. This shows that the integrand of (the right-hand-side of)
  the symmetric version of Fubini's theorem is measurable. -/
lemma measure_theory.strongly_measurable.integral_prod_left' [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : strongly_measurable f) : strongly_measurable (Î» y, âˆ« x, f (x, y) âˆ‚Î¼) :=
(hf.comp_measurable measurable_swap).integral_prod_right'

end

/-! ### The product measure -/

namespace measure_theory

namespace measure

variables [sigma_finite Î½]

lemma integrable_measure_prod_mk_left {s : set (Î± Ã— Î²)}
  (hs : measurable_set s) (h2s : (Î¼.prod Î½) s â‰  âˆ) :
  integrable (Î» x, (Î½ (prod.mk x â»Â¹' s)).to_real) Î¼ :=
begin
  refine âŸ¨(measurable_measure_prod_mk_left hs).ennreal_to_real.ae_measurable.ae_strongly_measurable,
    _âŸ©,
  simp_rw [has_finite_integral, ennnorm_eq_of_real to_real_nonneg],
  convert h2s.lt_top using 1, simp_rw [prod_apply hs], apply lintegral_congr_ae,
  refine (ae_measure_lt_top hs h2s).mp _, apply eventually_of_forall, intros x hx,
  rw [lt_top_iff_ne_top] at hx, simp [of_real_to_real, hx],
end

end measure

open measure

end measure_theory

open measure_theory.measure

section

lemma measure_theory.ae_strongly_measurable.prod_swap
  {Î³ : Type*} [topological_space Î³] [sigma_finite Î¼] [sigma_finite Î½] {f : Î² Ã— Î± â†’ Î³}
  (hf : ae_strongly_measurable f (Î½.prod Î¼)) :
  ae_strongly_measurable (Î» (z : Î± Ã— Î²), f z.swap) (Î¼.prod Î½) :=
by { rw â† prod_swap at hf, exact hf.comp_measurable measurable_swap }

lemma measure_theory.ae_strongly_measurable.fst {Î³} [topological_space Î³] [sigma_finite Î½]
  {f : Î± â†’ Î³} (hf : ae_strongly_measurable f Î¼) :
  ae_strongly_measurable (Î» (z : Î± Ã— Î²), f z.1) (Î¼.prod Î½) :=
hf.comp_quasi_measure_preserving quasi_measure_preserving_fst

lemma measure_theory.ae_strongly_measurable.snd {Î³} [topological_space Î³] [sigma_finite Î½]
  {f : Î² â†’ Î³} (hf : ae_strongly_measurable f Î½) :
  ae_strongly_measurable (Î» (z : Î± Ã— Î²), f z.2) (Î¼.prod Î½) :=
hf.comp_quasi_measure_preserving quasi_measure_preserving_snd

/-- The Bochner integral is a.e.-measurable.
  This shows that the integrand of (the right-hand-side of) Fubini's theorem is a.e.-measurable. -/
lemma measure_theory.ae_strongly_measurable.integral_prod_right' [sigma_finite Î½]
  [normed_space â„ E] [complete_space E]
  â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (hf : ae_strongly_measurable f (Î¼.prod Î½)) :
  ae_strongly_measurable (Î» x, âˆ« y, f (x, y) âˆ‚Î½) Î¼ :=
âŸ¨Î» x, âˆ« y, hf.mk f (x, y) âˆ‚Î½, hf.strongly_measurable_mk.integral_prod_right',
  by { filter_upwards [ae_ae_of_ae_prod hf.ae_eq_mk] with _ hx using integral_congr_ae hx }âŸ©

lemma measure_theory.ae_strongly_measurable.prod_mk_left
  {Î³ : Type*} [sigma_finite Î½] [topological_space Î³] {f : Î± Ã— Î² â†’ Î³}
  (hf : ae_strongly_measurable f (Î¼.prod Î½)) : âˆ€áµ x âˆ‚Î¼, ae_strongly_measurable (Î» y, f (x, y)) Î½ :=
begin
  filter_upwards [ae_ae_of_ae_prod hf.ae_eq_mk] with x hx,
  exact âŸ¨Î» y, hf.mk f (x, y), hf.strongly_measurable_mk.comp_measurable measurable_prod_mk_left, hxâŸ©
end

end

namespace measure_theory

variables [sigma_finite Î½]

/-! ### Integrability on a product -/
section

lemma integrable.swap [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (f âˆ˜ prod.swap) (Î½.prod Î¼) :=
âŸ¨hf.ae_strongly_measurable.prod_swap,
  (lintegral_prod_swap _ hf.ae_strongly_measurable.ennnorm : _).le.trans_lt hf.has_finite_integralâŸ©

lemma integrable_swap_iff [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ :
  integrable (f âˆ˜ prod.swap) (Î½.prod Î¼) â†” integrable f (Î¼.prod Î½) :=
âŸ¨Î» hf, by { convert hf.swap, ext âŸ¨x, yâŸ©, refl }, Î» hf, hf.swapâŸ©

lemma has_finite_integral_prod_iff â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : strongly_measurable f) :
  has_finite_integral f (Î¼.prod Î½) â†” (âˆ€áµ x âˆ‚ Î¼, has_finite_integral (Î» y, f (x, y)) Î½) âˆ§
    has_finite_integral (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ :=
begin
  simp only [has_finite_integral, lintegral_prod_of_measurable _ h1f.ennnorm],
  have : âˆ€ x, âˆ€áµ y âˆ‚Î½, 0 â‰¤ â€–f (x, y)â€– := Î» x, eventually_of_forall (Î» y, norm_nonneg _),
  simp_rw [integral_eq_lintegral_of_nonneg_ae (this _)
    (h1f.norm.comp_measurable measurable_prod_mk_left).ae_strongly_measurable,
    ennnorm_eq_of_real to_real_nonneg, of_real_norm_eq_coe_nnnorm],
  -- this fact is probably too specialized to be its own lemma
  have : âˆ€ {p q r : Prop} (h1 : r â†’ p), (r â†” p âˆ§ q) â†” (p â†’ (r â†” q)) :=
  Î» p q r h1, by rw [â† and.congr_right_iff, and_iff_right_of_imp h1],
  rw [this],
  { intro h2f, rw lintegral_congr_ae,
    refine h2f.mp _, apply eventually_of_forall, intros x hx, dsimp only,
    rw [of_real_to_real], rw [â† lt_top_iff_ne_top], exact hx },
  { intro h2f, refine ae_lt_top _ h2f.ne, exact h1f.ennnorm.lintegral_prod_right' },
end

lemma has_finite_integral_prod_iff' â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : ae_strongly_measurable f (Î¼.prod Î½)) :
  has_finite_integral f (Î¼.prod Î½) â†” (âˆ€áµ x âˆ‚ Î¼, has_finite_integral (Î» y, f (x, y)) Î½) âˆ§
    has_finite_integral (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ :=
begin
  rw [has_finite_integral_congr h1f.ae_eq_mk,
    has_finite_integral_prod_iff h1f.strongly_measurable_mk],
  apply and_congr,
  { apply eventually_congr,
    filter_upwards [ae_ae_of_ae_prod h1f.ae_eq_mk.symm],
    assume x hx,
    exact has_finite_integral_congr hx },
  { apply has_finite_integral_congr,
    filter_upwards [ae_ae_of_ae_prod h1f.ae_eq_mk.symm] with _ hx
      using integral_congr_ae (eventually_eq.fun_comp hx _), },
  { apply_instance, },
end

/-- A binary function is integrable if the function `y â†¦ f (x, y)` is integrable for almost every
  `x` and the function `x â†¦ âˆ« â€–f (x, y)â€– dy` is integrable. -/
lemma integrable_prod_iff â¦ƒf : Î± Ã— Î² â†’ Eâ¦„ (h1f : ae_strongly_measurable f (Î¼.prod Î½)) :
  integrable f (Î¼.prod Î½) â†”
    (âˆ€áµ x âˆ‚ Î¼, integrable (Î» y, f (x, y)) Î½) âˆ§ integrable (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ :=
by simp [integrable, h1f, has_finite_integral_prod_iff', h1f.norm.integral_prod_right',
         h1f.prod_mk_left]

/-- A binary function is integrable if the function `x â†¦ f (x, y)` is integrable for almost every
  `y` and the function `y â†¦ âˆ« â€–f (x, y)â€– dx` is integrable. -/
lemma integrable_prod_iff' [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (h1f : ae_strongly_measurable f (Î¼.prod Î½)) :
  integrable f (Î¼.prod Î½) â†”
    (âˆ€áµ y âˆ‚ Î½, integrable (Î» x, f (x, y)) Î¼) âˆ§ integrable (Î» y, âˆ« x, â€–f (x, y)â€– âˆ‚Î¼) Î½ :=
by { convert integrable_prod_iff (h1f.prod_swap) using 1, rw [integrable_swap_iff] }

lemma integrable.prod_left_ae [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : âˆ€áµ y âˆ‚ Î½, integrable (Î» x, f (x, y)) Î¼ :=
((integrable_prod_iff' hf.ae_strongly_measurable).mp hf).1

lemma integrable.prod_right_ae [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : âˆ€áµ x âˆ‚ Î¼, integrable (Î» y, f (x, y)) Î½ :=
hf.swap.prod_left_ae

lemma integrable.integral_norm_prod_left â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» x, âˆ« y, â€–f (x, y)â€– âˆ‚Î½) Î¼ :=
((integrable_prod_iff hf.ae_strongly_measurable).mp hf).2

lemma integrable.integral_norm_prod_right [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» y, âˆ« x, â€–f (x, y)â€– âˆ‚Î¼) Î½ :=
hf.swap.integral_norm_prod_left

lemma integrable_prod_mul {L : Type*} [is_R_or_C L]
  {f : Î± â†’ L} {g : Î² â†’ L} (hf : integrable f Î¼) (hg : integrable g Î½) :
  integrable (Î» (z : Î± Ã— Î²), f z.1 * g z.2) (Î¼.prod Î½) :=
begin
  refine (integrable_prod_iff _).2 âŸ¨_, _âŸ©,
  { exact hf.1.fst.mul hg.1.snd },
  { exact eventually_of_forall (Î» x, hg.const_mul (f x)) },
  { simpa only [norm_mul, integral_mul_left] using hf.norm.mul_const _ }
end

end

variables [normed_space â„ E] [complete_space E]

lemma integrable.integral_prod_left â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» x, âˆ« y, f (x, y) âˆ‚Î½) Î¼ :=
integrable.mono hf.integral_norm_prod_left hf.ae_strongly_measurable.integral_prod_right' $
  eventually_of_forall $ Î» x, (norm_integral_le_integral_norm _).trans_eq $
  (norm_of_nonneg $ integral_nonneg_of_ae $ eventually_of_forall $
  Î» y, (norm_nonneg (f (x, y)) : _)).symm

lemma integrable.integral_prod_right [sigma_finite Î¼] â¦ƒf : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) : integrable (Î» y, âˆ« x, f (x, y) âˆ‚Î¼) Î½ :=
hf.swap.integral_prod_left

/-! ### The Bochner integral on a product -/

variables [sigma_finite Î¼]

lemma integral_prod_swap (f : Î± Ã— Î² â†’ E)
  (hf : ae_strongly_measurable f (Î¼.prod Î½)) : âˆ« z, f z.swap âˆ‚(Î½.prod Î¼) = âˆ« z, f z âˆ‚(Î¼.prod Î½) :=
begin
  rw â† prod_swap at hf,
  rw [â† integral_map measurable_swap.ae_measurable hf, prod_swap]
end

variables {E' : Type*} [normed_add_comm_group E'] [complete_space E'] [normed_space â„ E']

/-! Some rules about the sum/difference of double integrals. They follow from `integral_add`, but
  we separate them out as separate lemmas, because they involve quite some steps. -/

/-- Integrals commute with addition inside another integral. `F` can be any function. -/
lemma integral_fn_integral_add â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (F : E â†’ E')
  (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, F (âˆ« y, f (x, y) + g (x, y) âˆ‚Î½) âˆ‚Î¼ = âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î½ + âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae] with _ h2f h2g,
  simp [integral_add h2f h2g],
end

/-- Integrals commute with subtraction inside another integral.
  `F` can be any measurable function. -/
lemma integral_fn_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„ (F : E â†’ E')
  (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î½) âˆ‚Î¼ = âˆ« x, F (âˆ« y, f (x, y) âˆ‚Î½ - âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ :=
begin
  refine integral_congr_ae _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae] with _ h2f h2g,
  simp [integral_sub h2f h2g],
end

/-- Integrals commute with subtraction inside a lower Lebesgue integral.
  `F` can be any function. -/
lemma lintegral_fn_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (F : E â†’ â„â‰¥0âˆ) (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ«â» x, F (âˆ« y, f (x, y) - g (x, y) âˆ‚Î½) âˆ‚Î¼ = âˆ«â» x, F (âˆ« y, f (x, y) âˆ‚Î½ - âˆ« y, g (x, y) âˆ‚Î½) âˆ‚Î¼ :=
begin
  refine lintegral_congr_ae _,
  filter_upwards [hf.prod_right_ae, hg.prod_right_ae] with _ h2f h2g,
  simp [integral_sub h2f h2g],
end

/-- Double integrals commute with addition. -/
lemma integral_integral_add â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f (x, y) + g (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ + âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
(integral_fn_integral_add id hf hg).trans $
  integral_add hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with addition. This is the version with `(f + g) (x, y)`
  (instead of `f (x, y) + g (x, y)`) in the LHS. -/
lemma integral_integral_add' â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, (f + g) (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ + âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
integral_integral_add hf hg

/-- Double integrals commute with subtraction. -/
lemma integral_integral_sub â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f (x, y) - g (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ - âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
(integral_fn_integral_sub id hf hg).trans $
  integral_sub hf.integral_prod_left hg.integral_prod_left

/-- Double integrals commute with subtraction. This is the version with `(f - g) (x, y)`
  (instead of `f (x, y) - g (x, y)`) in the LHS. -/
lemma integral_integral_sub' â¦ƒf g : Î± Ã— Î² â†’ Eâ¦„
  (hf : integrable f (Î¼.prod Î½)) (hg : integrable g (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, (f - g) (x, y) âˆ‚Î½ âˆ‚Î¼ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ - âˆ« x, âˆ« y, g (x, y) âˆ‚Î½ âˆ‚Î¼ :=
integral_integral_sub hf hg

/-- The map that sends an LÂ¹-function `f : Î± Ã— Î² â†’ E` to `âˆ«âˆ«f` is continuous. -/
lemma continuous_integral_integral :
  continuous (Î» (f : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E), âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼) :=
begin
  rw [continuous_iff_continuous_at], intro g,
  refine tendsto_integral_of_L1 _ (L1.integrable_coe_fn g).integral_prod_left
    (eventually_of_forall $ Î» h, (L1.integrable_coe_fn h).integral_prod_left) _,
  simp_rw [â† lintegral_fn_integral_sub (Î» x, (â€–xâ€–â‚Š : â„â‰¥0âˆ)) (L1.integrable_coe_fn _)
    (L1.integrable_coe_fn g)],
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds _ (Î» i, zero_le _) _,
  { exact Î» i, âˆ«â» x, âˆ«â» y, â€–i (x, y) - g (x, y)â€–â‚Š âˆ‚Î½ âˆ‚Î¼ },
  swap, { exact Î» i, lintegral_mono (Î» x, ennnorm_integral_le_lintegral_ennnorm _) },
  show tendsto (Î» (i : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E),
    âˆ«â» x, âˆ«â» (y : Î²), â€–i (x, y) - g (x, y)â€–â‚Š âˆ‚Î½ âˆ‚Î¼) (ğ“ g) (ğ“ 0),
  have : âˆ€ (i : Î± Ã— Î² â†’â‚[Î¼.prod Î½] E), measurable (Î» z, (â€–i z - g zâ€–â‚Š : â„â‰¥0âˆ)) :=
  Î» i, ((Lp.strongly_measurable i).sub (Lp.strongly_measurable g)).ennnorm,
  simp_rw [â† lintegral_prod_of_measurable _ (this _), â† L1.of_real_norm_sub_eq_lintegral,
    â† of_real_zero],
  refine (continuous_of_real.tendsto 0).comp _,
  rw [â† tendsto_iff_norm_tendsto_zero], exact tendsto_id
end

/-- **Fubini's Theorem**: For integrable functions on `Î± Ã— Î²`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  `integrable_prod_iff` can be useful to show that the function in question in integrable.
  `measure_theory.integrable.integral_prod_right` is useful to show that the inner integral
  of the right-hand side is integrable. -/
lemma integral_prod : âˆ€ (f : Î± Ã— Î² â†’ E) (hf : integrable f (Î¼.prod Î½)),
  âˆ« z, f z âˆ‚(Î¼.prod Î½) = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ :=
begin
  apply integrable.induction,
  { intros c s hs h2s,
    simp_rw [integral_indicator hs, â† indicator_comp_right,
      function.comp, integral_indicator (measurable_prod_mk_left hs),
      set_integral_const, integral_smul_const,
      integral_to_real (measurable_measure_prod_mk_left hs).ae_measurable
      (ae_measure_lt_top hs h2s.ne), prod_apply hs] },
  { intros f g hfg i_f i_g hf hg,
    simp_rw [integral_add' i_f i_g, integral_integral_add' i_f i_g, hf, hg] },
  { exact is_closed_eq continuous_integral continuous_integral_integral },
  { intros f g hfg i_f hf, convert hf using 1,
    { exact integral_congr_ae hfg.symm },
    { refine integral_congr_ae _,
      refine (ae_ae_of_ae_prod hfg).mp _,
      apply eventually_of_forall, intros x hfgx,
      exact integral_congr_ae (ae_eq_symm hfgx) } }
end

/-- Symmetric version of **Fubini's Theorem**: For integrable functions on `Î± Ã— Î²`,
  the Bochner integral of `f` is equal to the iterated Bochner integral.
  This version has the integrals on the right-hand side in the other order. -/
lemma integral_prod_symm (f : Î± Ã— Î² â†’ E) (hf : integrable f (Î¼.prod Î½)) :
  âˆ« z, f z âˆ‚(Î¼.prod Î½) = âˆ« y, âˆ« x, f (x, y) âˆ‚Î¼ âˆ‚Î½ :=
by { simp_rw [â† integral_prod_swap f hf.ae_strongly_measurable], exact integral_prod _ hf.swap }

/-- Reversed version of **Fubini's Theorem**. -/
lemma integral_integral {f : Î± â†’ Î² â†’ E} (hf : integrable (uncurry f) (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« z, f z.1 z.2 âˆ‚(Î¼.prod Î½) :=
(integral_prod _ hf).symm

/-- Reversed version of **Fubini's Theorem** (symmetric version). -/
lemma integral_integral_symm {f : Î± â†’ Î² â†’ E} (hf : integrable (uncurry f) (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« z, f z.2 z.1 âˆ‚(Î½.prod Î¼) :=
(integral_prod_symm _ hf.swap).symm

/-- Change the order of Bochner integration. -/
lemma integral_integral_swap â¦ƒf : Î± â†’ Î² â†’ Eâ¦„ (hf : integrable (uncurry f) (Î¼.prod Î½)) :
  âˆ« x, âˆ« y, f x y âˆ‚Î½ âˆ‚Î¼ = âˆ« y, âˆ« x, f x y âˆ‚Î¼ âˆ‚Î½ :=
(integral_integral hf).trans (integral_prod_symm _ hf)

/-- **Fubini's Theorem** for set integrals. -/
lemma set_integral_prod (f : Î± Ã— Î² â†’ E) {s : set Î±} {t : set Î²}
  (hf : integrable_on f (s Ã—Ë¢ t) (Î¼.prod Î½)) :
  âˆ« z in s Ã—Ë¢ t, f z âˆ‚(Î¼.prod Î½) = âˆ« x in s, âˆ« y in t, f (x, y) âˆ‚Î½ âˆ‚Î¼ :=
begin
  simp only [â† measure.prod_restrict s t, integrable_on] at hf âŠ¢,
  exact integral_prod f hf
end

lemma integral_prod_mul {L : Type*} [is_R_or_C L] (f : Î± â†’ L) (g : Î² â†’ L) :
  âˆ« z, f z.1 * g z.2 âˆ‚(Î¼.prod Î½) = (âˆ« x, f x âˆ‚Î¼) * (âˆ« y, g y âˆ‚Î½) :=
begin
  by_cases h : integrable (Î» (z : Î± Ã— Î²), f z.1 * g z.2) (Î¼.prod Î½),
  { rw integral_prod _ h,
    simp_rw [integral_mul_left, integral_mul_right] },
  have H : Â¬(integrable f Î¼) âˆ¨ Â¬(integrable g Î½),
  { contrapose! h,
    exact integrable_prod_mul h.1 h.2 },
  cases H;
  simp [integral_undef h, integral_undef H],
end

lemma set_integral_prod_mul {L : Type*} [is_R_or_C L]
  (f : Î± â†’ L) (g : Î² â†’ L) (s : set Î±) (t : set Î²) :
  âˆ« z in s Ã—Ë¢ t, f z.1 * g z.2 âˆ‚(Î¼.prod Î½) = (âˆ« x in s, f x âˆ‚Î¼) * (âˆ« y in t, g y âˆ‚Î½) :=
by simp only [â† measure.prod_restrict s t, integrable_on, integral_prod_mul]

end measure_theory
