/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import measure_theory.covering.vitali
import measure_theory.covering.differentiation

/-!
# Doubling measures and Lebesgue's density theorem

A doubling measure `Î¼` on a metric space is a measure for which there exists a constant `C` such
that for all sufficiently small radii `Îµ`, and for any centre, the measure of a ball of radius
`2 * Îµ` is bounded by `C` times the measure of the concentric ball of radius `Îµ`.

Lebesgue's density theorem states that given a set `S` in a proper metric space with locally-finite
doubling measure `Î¼` then for almost all points `x` in `S`, for any sequence of closed balls
`Bâ‚€, Bâ‚, Bâ‚‚, ...` containing `x`, the limit `Î¼ (S âˆ© Bâ±¼) / Î¼ (Bâ±¼) â†’ 1` as `j â†’ âˆ`.

In this file we combine general results about existence of Vitali families for doubling measures
with results about differentiation along a Vitali family to obtain an explicit form of Lebesgue's
density theorem.

## Main results

  * `is_doubling_measure`: the definition of a doubling measure (as a typeclass).
  * `is_doubling_measure.doubling_constant`: a function yielding the doubling constant `C` appearing
  in the definition of a doubling measure.
  * `is_doubling_measure.ae_tendsto_measure_inter_div`: a version of Lebesgue's density theorem for
  sequences of balls converging on a point but whose centres are not required to be fixed.

-/

noncomputable theory

open set filter metric measure_theory
open_locale nnreal topological_space

/-- A measure `Î¼` is said to be a doubling measure if there exists a constant `C` such that for
all sufficiently small radii `Îµ`, and for any centre, the measure of a ball of radius `2 * Îµ` is
bounded by `C` times the measure of the concentric ball of radius `Îµ`. -/
class is_doubling_measure {Î± : Type*} [metric_space Î±] [measurable_space Î±] (Î¼ : measure Î±) :=
(exists_measure_closed_ball_le_mul [] :
  âˆƒ (C : â„â‰¥0), âˆ€á¶  Îµ in ğ“[>] 0, âˆ€ x, Î¼ (closed_ball x (2 * Îµ)) â‰¤ C * Î¼ (closed_ball x Îµ))

namespace is_doubling_measure

variables {Î± : Type*} [metric_space Î±] [measurable_space Î±] (Î¼ : measure Î±) [is_doubling_measure Î¼]

/-- The doubling constant of a doubling measure. -/
def doubling_constant : â„â‰¥0 := classical.some $ exists_measure_closed_ball_le_mul Î¼

lemma exists_measure_closed_ball_le_mul' :
  âˆ€á¶  Îµ in ğ“[>] 0, âˆ€ x, Î¼ (closed_ball x (2 * Îµ)) â‰¤ (doubling_constant Î¼) * Î¼ (closed_ball x Îµ) :=
classical.some_spec $ exists_measure_closed_ball_le_mul Î¼

lemma exists_measure_closed_ball_le_mul_of_mem_Ioc (t : â„) (ht : t âˆˆ Ioc (0 : â„) 8) :
  âˆ€á¶  Îµ in ğ“[>] 0, âˆ€ x,
    Î¼ (closed_ball x (t * Îµ)) â‰¤ â†‘((doubling_constant Î¼)^3) * Î¼ (closed_ball x Îµ) :=
begin
  let C := doubling_constant Î¼,
  let h := exists_measure_closed_ball_le_mul' Î¼,
  let hp : 0 < (2 : â„) := two_pos,
  apply ((eventually_nhds_within_pos_mul_left hp $ eventually_nhds_within_pos_mul_left hp h).and $
    (eventually_nhds_within_pos_mul_left hp h).and h).mono,
  rintros Îµ âŸ¨hâ‚, hâ‚‚, hâ‚ƒâŸ© x,
  calc Î¼ (closed_ball x (t * Îµ))
        â‰¤ Î¼ (closed_ball x (8 * Îµ)) : measure_mono _
    ... = Î¼ (closed_ball x (2 * (2 * (2 * Îµ)))) : by { simp only [â† mul_assoc], norm_num, }
    ... â‰¤ C * Î¼ (closed_ball x (2 * (2 * Îµ))) : hâ‚ x
    ... â‰¤ C * (C * Î¼ (closed_ball x (2 * Îµ))) : ennreal.mul_left_mono $ hâ‚‚ x
    ... â‰¤ C * (C * (C*Î¼ (closed_ball x Îµ))) : ennreal.mul_left_mono $ ennreal.mul_left_mono $ hâ‚ƒ x
    ... = â†‘(C^3) * Î¼ (closed_ball x Îµ) : by simp only [â† mul_assoc, â† pow_three, ennreal.coe_pow],
  cases le_or_gt 0 Îµ with hÎµ hÎµ,
  { exact closed_ball_subset_closed_ball (mul_le_mul_of_nonneg_right ht.2 hÎµ), },
  { rw [closed_ball_eq_empty.mpr (mul_neg_of_pos_of_neg ht.1 hÎµ),
        closed_ball_eq_empty.mpr (by linarith : 8 * Îµ < 0)], },
end

variables [proper_space Î±] [borel_space Î±] [is_locally_finite_measure Î¼]

/-- The Vitali family of a doubling measure. -/
def vitali_family : vitali_family Î¼ :=
vitali.vitali_family Î¼ ((doubling_constant Î¼)^3) $ Î» x Îµ hÎµ,
begin
  have h := forall_eventually_of_eventually_forall
    (exists_measure_closed_ball_le_mul_of_mem_Ioc Î¼ 6 $ by norm_num),
  simpa only [exists_prop] using ((eventually_nhds_within_pos_mem_Ioc hÎµ).and (h x)).exists,
end

/-- A version of *Lebesgue's density theorem* for a sequence of closed balls whose centres are
not required to be fixed.

See also `besicovitch.ae_tendsto_measure_inter_div`. -/
lemma ae_tendsto_measure_inter_div (S : set Î±) (K : â„) (hK : K âˆˆ unit_interval) :
  âˆ€áµ x âˆ‚Î¼.restrict S, âˆ€ {Î¹ : Type*} {l : filter Î¹} (w : Î¹ â†’ Î±) (Î´ : Î¹ â†’ â„)
    (Î´lim : tendsto Î´ l (ğ“[>] 0))
    (xmem : âˆ€á¶  j in l, x âˆˆ closed_ball (w j) (K * Î´ j)),
    tendsto (Î» j, Î¼ (S âˆ© closed_ball (w j) (Î´ j)) / Î¼ (closed_ball (w j) (Î´ j))) l (ğ“ 1) :=
begin
  let v := is_doubling_measure.vitali_family Î¼,
  filter_upwards [v.ae_tendsto_measure_inter_div S] with x hx Î¹ l w Î´ Î´lim xmem,
  have : tendsto (Î» j, closed_ball (w j) (Î´ j)) l (v.filter_at x),
  { refine v.tendsto_filter_at_iff.mpr âŸ¨_, (Î» Îµ hÎµ, _)âŸ©,
    { simp only [v, vitali.vitali_family],
      have Î´pos : âˆ€á¶  j in l, 0 < Î´ j := eventually_mem_of_tendsto_nhds_within Î´lim,
      replace xmem : âˆ€á¶  (j : Î¹) in l, x âˆˆ closed_ball (w j) (Î´ j) := (Î´pos.and xmem).mono
        (Î» j hj, closed_ball_subset_closed_ball (by nlinarith [hj.1, hK.2]) hj.2),
      apply ((Î´lim.eventually
        (exists_measure_closed_ball_le_mul_of_mem_Ioc Î¼ 7 $ by norm_num)).and (xmem.and Î´pos)).mono,
      rintros j âŸ¨hjC, hjx, hjÎ´âŸ©,
      have hdiam : 3 * diam (closed_ball (w j) (Î´ j)) â‰¤ 6 * Î´ j,
      { linarith [@diam_closed_ball _ _ (w j) _ hjÎ´.le], },
      refine âŸ¨hjx, is_closed_ball, (nonempty_ball.mpr hjÎ´).mono ball_subset_interior_closed_ball,
        (measure_mono (closed_ball_subset_closed_ball hdiam)).trans _âŸ©,
      suffices : closed_ball x (6 * Î´ j) âŠ† closed_ball (w j) (7 * Î´ j),
      { exact (measure_mono this).trans ((hjC _).trans (le_refl _)), },
      intros y hy,
      simp only [mem_closed_ball] at hjx hy âŠ¢,
      rw dist_comm at hjx,
      linarith [dist_triangle_right y (w j) x], },
    { have Î´pos := eventually_mem_of_tendsto_nhds_within Î´lim,
      replace Î´lim := tendsto_nhds_of_tendsto_nhds_within Î´lim,
      replace hK : 0 < K + 1 := by linarith [hK.1],
      apply (((metric.tendsto_nhds.mp Î´lim _ (div_pos hÎµ hK)).and Î´pos).and xmem).mono,
      rintros j âŸ¨âŸ¨hjÎµ, hjâ‚€ : 0 < Î´ jâŸ©, hxâŸ© y hy,
      replace hjÎµ : (K + 1) * Î´ j < Îµ :=
        by simpa [abs_eq_self.mpr hjâ‚€.le] using (lt_div_iff' hK).mp hjÎµ,
      simp only [mem_closed_ball] at hx hy âŠ¢,
      linarith [dist_triangle_right y x (w j)], } },
  exact hx.comp this,
end

end is_doubling_measure
