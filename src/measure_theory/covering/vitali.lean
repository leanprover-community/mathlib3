/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import topology.metric_space.basic
import measure_theory.constructions.borel_space

/-!
# Vitali covering theorems

The topological Vitali covering theorem, in its most classical version, states the following.
Consider a family of balls `(B (x_i, r_i))_{i âˆˆ I}` in a metric space, with uniformly bounded
radii. Then one can extract a disjoint subfamily indexed by `J âŠ† I`, such that any `B (x_i, r_i)`
is included in a ball `B (x_j, 5 r_j)`.

We prove this theorem in `vitali.exists_disjoint_subfamily_covering_enlargment_closed_ball`.
It is deduced from a more general version, called
`vitali.exists_disjoint_subfamily_covering_enlargment`, which applies to any family of sets
together with a size function `Î´` (think "radius" or "diameter").

We deduce the measurable Vitali covering theorem. Assume one is given a family `t` of closed sets
with nonempty interior, such that each `a âˆˆ t` is included in a ball `B (x, r)` and covers a
definite proportion of the ball `B (x, 6 r)` for a given measure `Î¼` (think of the situation
where `Î¼` is a doubling measure and `t` is a family of balls). Consider a set `s` at which the
family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`. Then one
can extract from `t` a disjoint subfamily that covers almost all `s`. It is proved in
`vitali.exists_disjoint_covering_ae`.
-/

variables {Î± : Type*}

open set metric measure_theory topological_space filter
open_locale nnreal classical ennreal topological_space

namespace vitali

/-- Vitali covering theorem: given a set `t` of subsets of a type, one may extract a disjoint
subfamily `u` such that the `Ï„`-enlargment of this family covers all elements of `t`, where `Ï„ > 1`
is any fixed number.

When `t` is a family of balls, the `Ï„`-enlargment of `ball x r` is `ball x ((1+2Ï„) r)`. In general,
it is expressed in terms of a function `Î´` (think "radius" or "diameter"), positive and bounded on
all elements of `t`. The condition is that every element `a` of `t` should intersect an
element `b` of `u` of size larger than that of `a` up to `Ï„`, i.e., `Î´ b â‰¥ Î´ a / Ï„`.
-/
theorem exists_disjoint_subfamily_covering_enlargment
  (t : set (set Î±)) (Î´ : set Î± â†’ â„) (Ï„ : â„) (hÏ„ : 1 < Ï„) (Î´nonneg : âˆ€ a âˆˆ t, 0 â‰¤ Î´ a)
  (R : â„) (Î´le : âˆ€ a âˆˆ t, Î´ a â‰¤ R) (hne : âˆ€ a âˆˆ t, set.nonempty a) :
  âˆƒ u âŠ† t, u.pairwise disjoint âˆ§
    âˆ€ a âˆˆ t, âˆƒ b âˆˆ u, set.nonempty (a âˆ© b) âˆ§ Î´ a â‰¤ Ï„ * Î´ b :=
begin
  /- The proof could be formulated as a transfinite induction. First pick an element of `t` with `Î´`
  as large as possible (up to a factor of `Ï„`). Then among the remaining elements not intersecting
  the already chosen one, pick another element with large `Î´`. Go on forever (transfinitely) until
  there is nothing left.

  Instead, we give a direct Zorn-based argument. Consider a maximal family `u` of disjoint sets
  with the following property: if an element `a` of `t` intersects some element `b` of `u`, then it
  intersects some `b' âˆˆ u` with `Î´ b' â‰¥ Î´ a / Ï„`. Such a maximal family exists by Zorn. If this
  family did not intersect some element `a âˆˆ t`, then take an element `a' âˆˆ t` which does not
  intersect any element of `u`, with `Î´ a'` almost as large as possible. One checks easily
  that `u âˆª {a'}` still has this property, contradicting the maximality. Therefore, `u`
  intersects all elements of `t`, and by definition it satisfies all the desired properties.
  -/
  let T : set (set (set Î±)) := {u | u âŠ† t âˆ§ u.pairwise disjoint
    âˆ§ âˆ€ a âˆˆ t, âˆ€ b âˆˆ u, set.nonempty (a âˆ© b) â†’ âˆƒ c âˆˆ u, (a âˆ© c).nonempty âˆ§ Î´ a â‰¤ Ï„ * Î´ c},
  -- By Zorn, choose a maximal family in the good set `T` of disjoint families.
  obtain âŸ¨u, uT, huâŸ© : âˆƒ u âˆˆ T, âˆ€ v âˆˆ T, u âŠ† v â†’ v = u,
  { refine zorn.zorn_subset _ (Î» U UT hU, _),
    refine âŸ¨â‹ƒâ‚€ U, _, Î» s hs, subset_sUnion_of_mem hsâŸ©,
    simp only [set.sUnion_subset_iff, and_imp, exists_prop, forall_exists_index,
                set.mem_set_of_eq],
    refine âŸ¨Î» u hu, (UT hu).1, _, Î» a hat b u uU hbu hab, _âŸ©,
    { rw [pairwise_sUnion hU.directed_on],
      assume u hu,
      exact (UT hu).2.1 },
    { obtain âŸ¨c, cu, ac, hcâŸ© : âˆƒ (c : set Î±) (H : c âˆˆ u), (a âˆ© c).nonempty âˆ§ Î´ a â‰¤ Ï„ * Î´ c :=
        (UT uU).2.2 a hat b hbu hab,
      exact âŸ¨c, âŸ¨u, uU, cuâŸ©, ac, hcâŸ© } },
  -- the only nontrivial bit is to check that every `a âˆˆ t` intersects an element `b âˆˆ u` with
  -- comparatively large `Î´ b`. Assume this is not the case, then we will contradict the maximality.
  refine âŸ¨u, uT.1, uT.2.1, Î» a hat, _âŸ©,
  contrapose! hu,
  have a_disj : âˆ€ c âˆˆ u, disjoint a c,
  { assume c hc,
    by_contra,
    rw not_disjoint_iff_nonempty_inter at h,
    obtain âŸ¨d, du, ad, hdâŸ© : âˆƒ (d : set Î±) (H : d âˆˆ u), (a âˆ© d).nonempty âˆ§ Î´ a â‰¤ Ï„ * Î´ d :=
      uT.2.2 a hat c hc h,
    exact lt_irrefl _ ((hu d du ad).trans_le hd) },
  -- Let `A` be all the elements of `t` which do not intersect the family `u`. It is nonempty as it
  -- contains `a`. We will pick an element `a'` of `A` with `Î´ a'` almost as large as possible.
  let A := {a' | a' âˆˆ t âˆ§ âˆ€ c âˆˆ u, disjoint a' c},
  have Anonempty : A.nonempty := âŸ¨a, hat, a_disjâŸ©,
  let m := Sup (Î´ '' A),
  have bddA : bdd_above (Î´ '' A),
  { refine âŸ¨R, Î» x xA, _âŸ©,
    rcases (mem_image _ _ _).1 xA with âŸ¨a', ha', rflâŸ©,
    exact Î´le a' ha'.1 },
  obtain âŸ¨a', a'A, ha'âŸ© : âˆƒ a' âˆˆ A, m / Ï„ â‰¤ Î´ a',
  { have : 0 â‰¤ m := (Î´nonneg a hat).trans (le_cSup bddA (mem_image_of_mem _ âŸ¨hat, a_disjâŸ©)),
    rcases eq_or_lt_of_le this with mzero|mpos,
    { refine âŸ¨a, âŸ¨hat, a_disjâŸ©, _âŸ©,
      simpa only [â† mzero, zero_div] using Î´nonneg a hat },
    { have I : m / Ï„ < m,
      { rw div_lt_iff (zero_lt_one.trans hÏ„),
        conv_lhs { rw â† mul_one m },
        exact (mul_lt_mul_left mpos).2 hÏ„ },
      rcases exists_lt_of_lt_cSup (nonempty_image_iff.2 Anonempty) I with âŸ¨x, xA, hxâŸ©,
      rcases (mem_image _ _ _).1 xA with âŸ¨a', ha', rflâŸ©,
      exact âŸ¨a', ha', hx.leâŸ©, } },
  clear hat hu a_disj a,
  have a'_ne_u : a' âˆ‰ u := Î» H, (hne _ a'A.1).ne_empty (disjoint_self.1 (a'A.2 _ H)),
  -- we claim that `u âˆª {a'}` still belongs to `T`, contradicting the maximality of `u`.
  refine âŸ¨insert a' u, âŸ¨_, _, _âŸ©, subset_insert _ _, (ne_insert_of_not_mem _ a'_ne_u).symmâŸ©,
  -- check that `u âˆª {a'}` is made of elements of `t`.
  { rw insert_subset,
    exact âŸ¨a'A.1, uT.1âŸ© },
  -- check that `u âˆª {a'}` is a disjoint family. This follows from the fact that `a'` does not
  -- intersect `u`.
  { rw pairwise_insert_of_symmetric, swap,
    { simp only [function.on_fun, symmetric, disjoint.comm, imp_self, forall_const] },
    exact âŸ¨uT.2.1, Î» b bu ba', a'A.2 b buâŸ© },
  -- check that every element `c` of `t` intersecting `u âˆª {a'}` intersects an element of this
  -- family with large `Î´`.
  { assume c ct b ba'u hcb,
    -- if `c` already intersects an element of `u`, then it intersects an element of `u` with
    -- large `Î´` by the assumption on `u`, and there is nothing left to do.
    by_cases H : âˆƒ d âˆˆ u, set.nonempty (c âˆ© d),
    { rcases H with âŸ¨d, du, hdâŸ©,
      rcases uT.2.2 c ct d du hd with âŸ¨d', d'u, hd'âŸ©,
      exact âŸ¨d', mem_insert_of_mem _ d'u, hd'âŸ© },
    -- otherwise, `c` belongs to `A`. The element of `u âˆª {a'}` that it intersects has to be `a'`.
    -- moreover, `Î´ c` is smaller than the maximum `m` of `Î´` over `A`, which is `â‰¤ Î´ a' / Ï„`
    -- thanks to the good choice of `a'`. This is the desired inequality.
    { push_neg at H,
      simp only [â† not_disjoint_iff_nonempty_inter, not_not] at H,
      rcases mem_insert_iff.1 ba'u with rfl|H',
      { refine âŸ¨b, mem_insert _ _, hcb, _âŸ©,
        calc Î´ c â‰¤ m : le_cSup bddA (mem_image_of_mem _ âŸ¨ct, HâŸ©)
        ... = Ï„ * (m / Ï„) : by { field_simp [(zero_lt_one.trans hÏ„).ne'], ring }
        ... â‰¤ Ï„ * Î´ b : mul_le_mul_of_nonneg_left ha' (zero_le_one.trans hÏ„.le) },
      { rw â† not_disjoint_iff_nonempty_inter at hcb,
        exact (hcb (H _ H')).elim } } }
end

/-- Vitali covering theorem, closed balls version: given a family `t` of closed balls, one can
extract a disjoint subfamily `u âŠ† t` so that all balls in `t` are covered by the 5-times
dilations of balls in `u`. -/
theorem exists_disjoint_subfamily_covering_enlargment_closed_ball [metric_space Î±]
  (t : set (set Î±)) (R : â„) (ht : âˆ€ s âˆˆ t, âˆƒ x r, s = closed_ball x r âˆ§ r â‰¤ R) :
  âˆƒ u âŠ† t, u.pairwise disjoint âˆ§
    âˆ€ a âˆˆ t, âˆƒ x r, closed_ball x r âˆˆ u âˆ§ a âŠ† closed_ball x (5 * r) :=
begin
  rcases eq_empty_or_nonempty t with rfl|tnonempty,
  { exact âŸ¨âˆ…, subset.refl _, by simpâŸ© },
  haveI : inhabited Î±,
  { choose s hst using tnonempty,
    choose x r hxr using ht s hst,
    exact âŸ¨xâŸ© },
  -- Exclude the trivial case where `t` is reduced to the empty set.
  rcases eq_or_ne t {âˆ…} with rfl|t_ne_empty,
  { refine âŸ¨{âˆ…}, subset.refl _, _âŸ©,
    simp only [true_and, closed_ball_eq_empty, mem_singleton_iff, and_true, empty_subset, forall_eq,
      pairwise_singleton, exists_const],
    exact âŸ¨-1, by simp only [right.neg_neg_iff, zero_lt_one]âŸ© },
  -- The real proof starts now. Since the center or the radius of a ball is not uniquely defined
  -- in a general metric space, we just choose one for definiteness.
  choose! x r hxr using ht,
  have r_nonneg : âˆ€ (a : set Î±), a âˆˆ t â†’ a.nonempty â†’ 0 â‰¤ r a,
  { assume a hat a_nonempty,
    rw (hxr a hat).1 at a_nonempty,
    simpa only [nonempty_closed_ball] using a_nonempty },
  -- The difference with the generic version is that we are not excluding empty sets in our family
  -- (which would correspond to `r a < 0`). To compensate for this, we apply the generic version
  -- to the subfamily `t'` made of nonempty sets, and we use `Î´ = r` there. This gives a disjointed
  -- subfamily `u'`.
  let t' := {a âˆˆ t | 0 â‰¤ r a},
  obtain âŸ¨u', u't', u'_disj, hu'âŸ© : âˆƒ u' âŠ† t', u'.pairwise disjoint âˆ§
    âˆ€ a âˆˆ t', âˆƒ b âˆˆ u', set.nonempty (a âˆ© b) âˆ§ r a â‰¤ 2 * r b,
  { refine exists_disjoint_subfamily_covering_enlargment t' r 2 one_lt_two
      (Î» a ha, ha.2) R (Î» a ha, (hxr a ha.1).2) (Î» a ha, _),
    rw [(hxr a ha.1).1],
    simp only [ha.2, nonempty_closed_ball] },
  -- this subfamily is nonempty, as we have excluded the situation `t = {âˆ…}`.
  have u'_nonempty : u'.nonempty,
  { have : âˆƒ a âˆˆ t, a â‰  âˆ…,
    { contrapose! t_ne_empty,
      apply subset.antisymm,
      { simpa only using t_ne_empty },
      { rcases tnonempty with âŸ¨a, hatâŸ©,
        have := t_ne_empty a hat,
        simpa only [this, singleton_subset_iff] using hat } },
    rcases this with âŸ¨a, hat, a_nonemptyâŸ©,
    have ranonneg : 0 â‰¤ r a := r_nonneg a hat (ne_empty_iff_nonempty.1 a_nonempty),
    rcases hu' a âŸ¨hat, ranonnegâŸ© with âŸ¨b, bu', hbâŸ©,
    exact âŸ¨b, bu'âŸ© },
  -- check that the family `u'` gives the desired disjoint covering.
  refine âŸ¨u', Î» a ha, (u't' ha).1, u'_disj, Î» a hat, _âŸ©,
  -- it remains to check that any ball in `t` is contained in the 5-dilation of a ball
  -- in `u'`. This depends on whether the ball is empty of not.
  rcases eq_empty_or_nonempty a with rfl|a_nonempty,
  -- if the ball is empty, use any element of `u'` (since we know that `u'` is nonempty).
  { rcases u'_nonempty with âŸ¨b, hbâŸ©,
    refine âŸ¨x b, r b, _, empty_subset _âŸ©,
    rwa â† (hxr b (u't' hb).1).1 },
  -- if the ball is not empty, it belongs to `t'`. Then it intersects a ball `a'` in `u'` with
  -- controlled radius, by definition of `u'`. It is straightforward to check that this ball
  -- satisfies all the desired properties.
  { have hat' : a âˆˆ t' := âŸ¨hat, r_nonneg a hat a_nonemptyâŸ©,
    obtain âŸ¨a', a'u', aa', raa'âŸ© :
      (âˆƒ (a' : set Î±) (H : a' âˆˆ u'), (a âˆ© a').nonempty âˆ§ r a â‰¤ 2 * r a') := hu' a hat',
    refine âŸ¨x a', r a', _, _âŸ©,
    { convert a'u',
      exact (hxr a' (u't' a'u').1).1.symm },
    { rw (hxr a hat'.1).1 at aa' âŠ¢,
      rw (hxr a' (u't' a'u').1).1 at aa',
      have : dist (x a) (x a') â‰¤ r a + r a' :=
        dist_le_add_of_nonempty_closed_ball_inter_closed_ball aa',
      apply closed_ball_subset_closed_ball',
      linarith } }
end

/-- The measurable Vitali covering theorem. Assume one is given a family `t` of closed sets with
nonempty interior, such that each `a âˆˆ t` is included in a ball `B (x, r)` and covers a definite
proportion of the ball `B (x, 6 r)` for a given measure `Î¼` (think of the situation where `Î¼` is
a doubling measure and `t` is a family of balls). Consider a (possible non-measurable) set `s`
at which the family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`.
Then one can extract from `t` a disjoint subfamily that covers almost all `s`. -/
theorem exists_disjoint_covering_ae [metric_space Î±] [measurable_space Î±] [opens_measurable_space Î±]
  [second_countable_topology Î±]
  (Î¼ : measure Î±) [is_locally_finite_measure Î¼] (s : set Î±)
  (t : set (set Î±)) (hf : âˆ€ x âˆˆ s, âˆ€ (Îµ > (0 : â„)), âˆƒ a âˆˆ t, x âˆˆ a âˆ§ a âŠ† closed_ball x Îµ)
  (ht : âˆ€ a âˆˆ t, (interior a).nonempty) (h't : âˆ€ a âˆˆ t, is_closed a)
  (C : â„â‰¥0) (h : âˆ€ a âˆˆ t, âˆƒ x âˆˆ a, Î¼ (closed_ball x (3 * diam a)) â‰¤ C * Î¼ a) :
  âˆƒ u âŠ† t, countable u âˆ§ u.pairwise disjoint âˆ§ Î¼ (s \ â‹ƒ (a âˆˆ u), a) = 0 :=
begin
  /- The idea of the proof is the following. Assume for simplicity that `Î¼` is finite. Applying the
  abstract Vitali covering theorem with `Î´ = diam`, one obtains a disjoint subfamily `u`, such
  that any element of `t` intersects an element of `u` with comparable diameter. Fix `Îµ > 0`.
  Since the elements of `u` have summable measure, one can remove finitely elements `w_1, ..., w_n`.
  so that the measure of the remaining elements is `< Îµ`. Consider now a point `z` not
  in the `w_i`. There is a small ball around `z` not intersecting the `w_i` (as they are closed),
  an element `a âˆˆ t` contained in this small ball (as the family `t` is fine at `z`) and an element
  `b âˆˆ u` intersecting `a`, with comparable diameter (by definition of `u`). Then `z` belongs to the
  enlargement of `b`. This shows that `s \ (w_1 âˆª ... âˆª w_n)` is contained in
  `â‹ƒ (b âˆˆ u \ {w_1, ... w_n}) (enlargement of b)`. The measure of the latter set is bounded by
  `âˆ‘ (b âˆˆ u \ {w_1, ... w_n}) C * Î¼ b` (by the doubling property of the measure), which is at most
  `C Îµ`. Letting `Îµ` tend to `0` shows that `s` is almost everywhere covered by the family `u`.

  For the real argument, the measure is only locally finite. Therefore, we implement the same
  strategy, but locally restricted to balls on which the measure is finite. For this, we do not
  use the whole family `t`, but a subfamily `t'` supported on small balls (which is possible since
  the family is assumed to be fine at every point of `s`).
  -/
  rcases eq_empty_or_nonempty s with rfl|nonempty,
  { refine âŸ¨âˆ…, empty_subset _, countable_empty, by simp only [pairwise_empty],
      by simp only [measure_empty, Union_false, Union_empty, diff_self]âŸ© },
  haveI : inhabited Î±,
  { choose x hx using nonempty,
    exact âŸ¨xâŸ© },
  -- choose around each `x` a small ball on which the measure is finite
  have : âˆ€ x, âˆƒ r, 0 < r âˆ§ r â‰¤ 1 âˆ§ Î¼ (closed_ball x (20 * r)) < âˆ,
  { assume x,
    obtain âŸ¨R, Rpos, Î¼RâŸ© : âˆƒ (R : â„) (hR : 0 < R), Î¼ (closed_ball x R) < âˆ :=
      (Î¼.finite_at_nhds x).exists_mem_basis nhds_basis_closed_ball,
    refine âŸ¨min 1 (R/20), _, min_le_left _ _, _âŸ©,
    { simp only [true_and, lt_min_iff, zero_lt_one],
      linarith },
    { apply lt_of_le_of_lt (measure_mono _) Î¼R,
      apply closed_ball_subset_closed_ball,
      calc 20 * min 1 (R / 20) â‰¤ 20 * (R/20) :
        mul_le_mul_of_nonneg_left (min_le_right _ _) (by norm_num)
      ... = R : by ring } },
  choose r hr using this,
  -- we restrict to a subfamily `t'` of `t`, made of elements small enough to ensure that
  -- they only see a finite part of the measure.
  let t' := {a âˆˆ t | âˆƒ x, a âŠ† closed_ball x (r x)},
  -- extract a disjoint subfamily `u` of `t'` thanks to the abstract Vitali covering theorem.
  obtain âŸ¨u, ut', u_disj, huâŸ© : âˆƒ u âŠ† t', u.pairwise disjoint âˆ§
    âˆ€ a âˆˆ t', âˆƒ b âˆˆ u, set.nonempty (a âˆ© b) âˆ§ diam a â‰¤ 2 * diam b,
  { have A : âˆ€ (a : set Î±), a âˆˆ t' â†’ diam a â‰¤ 2,
    { rintros a âŸ¨hat, âŸ¨x, haxâŸ©âŸ©,
      calc diam a â‰¤ diam (closed_ball x (r x)) : diam_mono hax bounded_closed_ball
      ... â‰¤ 2 * r x : diam_closed_ball (hr x).1.le
      ... â‰¤ 2 * 1 : mul_le_mul_of_nonneg_left (hr x).2.1 zero_le_two
      ... = 2 : by norm_num },
    have B : âˆ€ (a : set Î±), a âˆˆ t' â†’ a.nonempty :=
      Î» a hat', set.nonempty.mono interior_subset (ht a hat'.1),
    exact exists_disjoint_subfamily_covering_enlargment t' diam 2 one_lt_two
      (Î» a ha, diam_nonneg) 2 A B },
  have ut : u âŠ† t := Î» a hau, (ut' hau).1,
  -- As the space is second countable, the family is countable since all its sets have nonempty
  -- interior.
  have u_count : countable u :=
    countable_of_nonempty_interior_of_disjoint id (Î» a ha, ht a (ut ha)) u_disj,
  -- the family `u` will be the desired family
  refine âŸ¨u, Î» a hat', (ut' hat').1, u_count, u_disj, _âŸ©,
  -- it suffices to show that it covers almost all `s` locally around each point `x`.
  refine null_of_locally_null _ (Î» x hx, _),
  -- let `v` be the subfamily of `u` made of those sets intersecting the small ball `ball x (r x)`
  let v := {a âˆˆ u | (a âˆ© ball x (r x)).nonempty },
  have vu : v âŠ† u := Î» a ha, ha.1,
  -- they are all contained in a fixed ball of finite measure, thanks to our choice of `t'`
  obtain âŸ¨R, Î¼R, hRâŸ© : âˆƒ R, Î¼ (closed_ball x R) < âˆ âˆ§
                          âˆ€ a âˆˆ u, (a âˆ© ball x (r x)).nonempty â†’ a âŠ† closed_ball x R,
  { have : âˆ€ a âˆˆ u, âˆƒ y, a âŠ† closed_ball y (r y) := Î» a hau, (ut' hau).2,
    choose! y hy using this,
    have Idist_v : âˆ€ a âˆˆ v, dist (y a) x â‰¤ r (y a) + r x,
    { assume a hav,
      apply dist_le_add_of_nonempty_closed_ball_inter_closed_ball,
      exact hav.2.mono (inter_subset_inter (hy a hav.1) ball_subset_closed_ball) },
    set R0 := Sup ((Î» a, r (y a)) '' v) with hR0,
    have R0_bdd : bdd_above ((Î» a, r (y a)) '' v),
    { refine âŸ¨1, Î» r' hr', _âŸ©,
      rcases (mem_image _ _ _).1 hr' with âŸ¨b, hb, rflâŸ©,
      exact (hr _).2.1 },
    rcases le_total R0 (r x) with H|H,
    { refine âŸ¨20 * r x, (hr x).2.2, Î» a au hax, _âŸ©,
      refine (hy a au).trans _,
      apply closed_ball_subset_closed_ball',
      have : r (y a) â‰¤ R0 := le_cSup R0_bdd (mem_image_of_mem _ âŸ¨au, haxâŸ©),
      linarith [(hr (y a)).1.le, (hr x).1.le, Idist_v a âŸ¨au, haxâŸ©] },
    { have R0pos : 0 < R0 := (hr x).1.trans_le H,
      have vnonempty : v.nonempty,
      { by_contra,
        rw [â† ne_empty_iff_nonempty, not_not] at h,
        simp only [h, real.Sup_empty, image_empty] at hR0,
        exact lt_irrefl _ (R0pos.trans_le (le_of_eq hR0)) },
      obtain âŸ¨a, hav, R0aâŸ© : âˆƒ a âˆˆ v, R0/2 < r (y a),
      { obtain âŸ¨r', r'mem, hr'âŸ© : âˆƒ r' âˆˆ ((Î» a, r (y a)) '' v), R0 / 2 < r' :=
          exists_lt_of_lt_cSup (nonempty_image_iff.2 vnonempty) (half_lt_self R0pos),
        rcases (mem_image _ _ _).1 r'mem with âŸ¨a, hav, rflâŸ©,
        exact âŸ¨a, hav, hr'âŸ© },
      refine âŸ¨8 * R0, _, _âŸ©,
      { apply lt_of_le_of_lt (measure_mono _) (hr (y a)).2.2,
        apply closed_ball_subset_closed_ball',
        rw dist_comm,
        linarith [Idist_v a hav] },
      { assume b bu hbx,
        refine (hy b bu).trans _,
        apply closed_ball_subset_closed_ball',
        have : r (y b) â‰¤ R0 := le_cSup R0_bdd (mem_image_of_mem _ âŸ¨bu, hbxâŸ©),
        linarith [Idist_v b âŸ¨bu, hbxâŸ©] } } },
  -- we will show that, in `ball x (r x)`, almost all `s` is covered by the family `u`.
  refine âŸ¨ball x (r x), _, le_antisymm (le_of_forall_le_of_dense (Î» Îµ Îµpos, _)) bot_leâŸ©,
  { apply mem_nhds_within_of_mem_nhds (is_open_ball.mem_nhds _),
    simp only [(hr x).left, mem_ball, dist_self] },
  -- the elements of `v` are disjoint and all contained in a finite volume ball, hence the sum
  -- of their measures is finite.
  have I : âˆ‘' (a : v), Î¼ a < âˆ,
  { calc âˆ‘' (a : v), Î¼ a = Î¼ (â‹ƒ (a âˆˆ v), a) : begin
      rw measure_bUnion (u_count.mono vu) _ (Î» a ha, (h't _ (vu.trans ut ha)).measurable_set),
      exact u_disj.mono vu
    end
    ... â‰¤ Î¼ (closed_ball x R) : measure_mono (bUnion_subset (Î» a ha, hR a (vu ha) ha.2))
    ... < âˆ : Î¼R },
  -- we can obtain a finite subfamily of `v`, such that the measures of the remaining elements
  -- add up to an arbitrarily small number, say `Îµ / C`.
  obtain âŸ¨w, hwâŸ© : âˆƒ (w : finset â†¥v), âˆ‘' (a : {a // a âˆ‰ w}), Î¼ a < Îµ / C,
  { haveI : ne_bot (at_top : filter (finset v)) := at_top_ne_bot,
    have : 0 < Îµ / C, by simp only [ennreal.div_pos_iff, Îµpos.ne', ennreal.coe_ne_top, ne.def,
                                    not_false_iff, and_self],
    exact ((tendsto_order.1 (ennreal.tendsto_tsum_compl_at_top_zero I.ne)).2 _ this).exists },
  choose! y hy using h,
  -- main property: the points `z` of `s` which are not covered by `u` are contained in the
  -- enlargements of the elements not in `w`.
  have M : (s \ â‹ƒ (a : set Î±) (H : a âˆˆ u), a) âˆ© ball x (r x)
    âŠ† â‹ƒ (a : {a // a âˆ‰ w}), closed_ball (y a) (3 * diam (a : set Î±)),
  { assume z hz,
    set k := â‹ƒ (a : v) (ha : a âˆˆ w), (a : set Î±) with hk,
    have k_closed : is_closed k :=
      is_closed_bUnion w.finite_to_set (Î» i hi, h't _ (ut (vu i.2))),
    have z_notmem_k : z âˆ‰ k,
    { simp only [not_exists, exists_prop, mem_Union, mem_sep_eq, forall_exists_index,
        set_coe.exists, not_and, exists_and_distrib_right, subtype.coe_mk],
      assume b hbv h'b h'z,
      have : z âˆˆ (s \ â‹ƒ (a : set Î±) (H : a âˆˆ u), a) âˆ© (â‹ƒ (a : set Î±) (H : a âˆˆ u), a) :=
        mem_inter (mem_of_mem_inter_left hz) (mem_bUnion (vu hbv) h'z),
      simpa only [diff_inter_self] },
    -- since the elements of `w` are closed and finitely many, one can find a small ball around `z`
    -- not intersecting them
    have : ball x (r x) \ k âˆˆ ğ“ z,
    { apply is_open.mem_nhds (is_open_ball.sdiff k_closed) _,
      exact (mem_diff _).2 âŸ¨mem_of_mem_inter_right hz, z_notmem_kâŸ© },
    obtain âŸ¨d, dpos, hdâŸ© : âˆƒ (d : â„) (dpos : 0 < d), closed_ball z d âŠ† ball x (r x) \ k :=
      nhds_basis_closed_ball.mem_iff.1 this,
    -- choose an element `a` of the family `t` contained in this small ball
    obtain âŸ¨a, hat, za, adâŸ© : âˆƒ a âˆˆ t, z âˆˆ a âˆ§ a âŠ† closed_ball z d :=
      hf z ((mem_diff _).1 (mem_of_mem_inter_left hz)).1 d dpos,
    have ax : a âŠ† ball x (r x) := ad.trans (hd.trans (diff_subset (ball x (r x)) k)),
    -- it intersects an element `b` of `u` with comparable diameter, by definition of `u`
    obtain âŸ¨b, bu, ab, bdiamâŸ© : âˆƒ (b : set Î±) (H : b âˆˆ u), (a âˆ© b).nonempty âˆ§ diam a â‰¤ 2 * diam b :=
      hu a âŸ¨hat, âŸ¨x, ax.trans ball_subset_closed_ballâŸ©âŸ©,
    have bv : b âˆˆ v,
    { refine âŸ¨bu, ab.mono _âŸ©,
      rw inter_comm,
      exact inter_subset_inter_right _ ax },
    let b' : v := âŸ¨b, bvâŸ©,
    -- `b` can not belong to `w`, as the elements of `w` do not intersect `closed_ball z d`,
    -- contrary to `b`
    have b'_notmem_w : b' âˆ‰ w,
    { assume b'w,
      have b'k : (b' : set Î±) âŠ† k := finset.subset_set_bUnion_of_mem b'w,
      have : ((ball x (r x) \ k) âˆ© k).nonempty := ab.mono (inter_subset_inter (ad.trans hd) b'k),
      simpa only [diff_inter_self, not_nonempty_empty] },
    let b'' : {a // a âˆ‰ w} := âŸ¨b', b'_notmem_wâŸ©,
    -- since `a` and `b` have comparable diameters, it follows that `z` belongs to the
    -- enlargement of `b`
    have zb : z âˆˆ closed_ball (y b) (3 * diam b),
    { rcases ab with âŸ¨e, âŸ¨ea, ebâŸ©âŸ©,
      have A : dist z e â‰¤ diam a := dist_le_diam_of_mem (bounded_closed_ball.mono ad) za ea,
      have B : dist e (y b) â‰¤ diam b,
      { rcases (ut' bu).2 with âŸ¨c, hcâŸ©,
        apply dist_le_diam_of_mem (bounded_closed_ball.mono hc) eb (hy b (ut bu)).1 },
      simp only [mem_closed_ball],
      linarith [dist_triangle z e (y b)] },
    suffices H : closed_ball (y (b'' : set Î±)) (3 * diam (b'' : set Î±))
      âŠ† â‹ƒ (a : {a // a âˆ‰ w}), closed_ball (y (a : set Î±)) (3 * diam (a : set Î±)), from H zb,
    exact subset_Union (Î» (a : {a // a âˆ‰ w}), closed_ball (y a) (3 * diam (a : set Î±))) b'' },
  -- now that we have proved our main inclusion, we can use it to estimate the measure of the points
  -- in `ball x (r x)` not covered by `u`.
  haveI : encodable v := (u_count.mono vu).to_encodable,
  calc Î¼ ((s \ â‹ƒ (a : set Î±) (H : a âˆˆ u), a) âˆ© ball x (r x))
      â‰¤ Î¼ (â‹ƒ (a : {a // a âˆ‰ w}), closed_ball (y a) (3 * diam (a : set Î±))) : measure_mono M
  ... â‰¤ âˆ‘' (a : {a // a âˆ‰ w}), Î¼ (closed_ball (y a) (3 * diam (a : set Î±))) : measure_Union_le _
  ... â‰¤ âˆ‘' (a : {a // a âˆ‰ w}), C * Î¼ a : ennreal.tsum_le_tsum (Î» a, (hy a (ut (vu a.1.2))).2)
  ... = C * âˆ‘' (a : {a // a âˆ‰ w}), Î¼ a : ennreal.tsum_mul_left
  ... â‰¤ C * (Îµ / C) : ennreal.mul_le_mul le_rfl hw.le
  ... â‰¤ Îµ : ennreal.mul_div_le,
end

end vitali
