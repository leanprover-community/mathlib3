/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import topology.metric_space.basic
import measure_theory.constructions.borel_space
import measure_theory.covering.vitali_family

/-!
# Vitali covering theorems

The topological Vitali covering theorem, in its most classical version, states the following.
Consider a family of balls `(B (x_i, r_i))_{i ‚àà I}` in a metric space, with uniformly bounded
radii. Then one can extract a disjoint subfamily indexed by `J ‚äÜ I`, such that any `B (x_i, r_i)`
is included in a ball `B (x_j, 5 r_j)`.

We prove this theorem in `vitali.exists_disjoint_subfamily_covering_enlargment_closed_ball`.
It is deduced from a more general version, called
`vitali.exists_disjoint_subfamily_covering_enlargment`, which applies to any family of sets
together with a size function `Œ¥` (think "radius" or "diameter").

We deduce the measurable Vitali covering theorem. Assume one is given a family `t` of closed sets
with nonempty interior, such that each `a ‚àà t` is included in a ball `B (x, r)` and covers a
definite proportion of the ball `B (x, 6 r)` for a given measure `Œº` (think of the situation
where `Œº` is a doubling measure and `t` is a family of balls). Consider a set `s` at which the
family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`. Then one
can extract from `t` a disjoint subfamily that covers almost all `s`. It is proved in
`vitali.exists_disjoint_covering_ae`.

A way to restate this theorem is to say that the set of closed sets `a` with nonempty interior
covering a fixed proportion `1/C` of the ball `closed_ball x (3 * diam a)` forms a Vitali family.
This version is given in `vitali.vitali_family`.
-/

variables {Œ± Œπ : Type*}

open set metric measure_theory topological_space filter
open_locale nnreal classical ennreal topology

namespace vitali

/-- Vitali covering theorem: given a set `t` of subsets of a type, one may extract a disjoint
subfamily `u` such that the `œÑ`-enlargment of this family covers all elements of `t`, where `œÑ > 1`
is any fixed number.

When `t` is a family of balls, the `œÑ`-enlargment of `ball x r` is `ball x ((1+2œÑ) r)`. In general,
it is expressed in terms of a function `Œ¥` (think "radius" or "diameter"), positive and bounded on
all elements of `t`. The condition is that every element `a` of `t` should intersect an
element `b` of `u` of size larger than that of `a` up to `œÑ`, i.e., `Œ¥ b ‚â• Œ¥ a / œÑ`.

We state the lemma slightly more generally, with an indexed family of sets `B a` for `a ‚àà t`, for
wider applicability.
-/
theorem exists_disjoint_subfamily_covering_enlargment
  (B : Œπ ‚Üí set Œ±) (t : set Œπ) (Œ¥ : Œπ ‚Üí ‚Ñù) (œÑ : ‚Ñù) (hœÑ : 1 < œÑ) (Œ¥nonneg : ‚àÄ a ‚àà t, 0 ‚â§ Œ¥ a)
  (R : ‚Ñù) (Œ¥le : ‚àÄ a ‚àà t, Œ¥ a ‚â§ R) (hne : ‚àÄ a ‚àà t, (B a).nonempty) :
  ‚àÉ u ‚äÜ t, u.pairwise_disjoint B ‚àß
    ‚àÄ a ‚àà t, ‚àÉ b ‚àà u, (B a ‚à© B b).nonempty ‚àß Œ¥ a ‚â§ œÑ * Œ¥ b :=
begin
  /- The proof could be formulated as a transfinite induction. First pick an element of `t` with `Œ¥`
  as large as possible (up to a factor of `œÑ`). Then among the remaining elements not intersecting
  the already chosen one, pick another element with large `Œ¥`. Go on forever (transfinitely) until
  there is nothing left.

  Instead, we give a direct Zorn-based argument. Consider a maximal family `u` of disjoint sets
  with the following property: if an element `a` of `t` intersects some element `b` of `u`, then it
  intersects some `b' ‚àà u` with `Œ¥ b' ‚â• Œ¥ a / œÑ`. Such a maximal family exists by Zorn. If this
  family did not intersect some element `a ‚àà t`, then take an element `a' ‚àà t` which does not
  intersect any element of `u`, with `Œ¥ a'` almost as large as possible. One checks easily
  that `u ‚à™ {a'}` still has this property, contradicting the maximality. Therefore, `u`
  intersects all elements of `t`, and by definition it satisfies all the desired properties.
  -/
  let T : set (set Œπ) := {u | u ‚äÜ t ‚àß u.pairwise_disjoint B
    ‚àß ‚àÄ a ‚àà t, ‚àÄ b ‚àà u, (B a ‚à© B b).nonempty ‚Üí ‚àÉ c ‚àà u, (B a ‚à© B c).nonempty ‚àß Œ¥ a ‚â§ œÑ * Œ¥ c},
  -- By Zorn, choose a maximal family in the good set `T` of disjoint families.
  obtain ‚ü®u, uT, hu‚ü© : ‚àÉ u ‚àà T, ‚àÄ v ‚àà T, u ‚äÜ v ‚Üí v = u,
  { refine zorn_subset _ (Œª U UT hU, _),
    refine ‚ü®‚ãÉ‚ÇÄ U, _, Œª s hs, subset_sUnion_of_mem hs‚ü©,
    simp only [set.sUnion_subset_iff, and_imp, exists_prop, forall_exists_index, mem_sUnion,
                set.mem_set_of_eq],
    refine ‚ü®Œª u hu, (UT hu).1, (pairwise_disjoint_sUnion hU.directed_on).2 (Œª u hu, (UT hu).2.1),
      Œª a hat b u uU hbu hab, _‚ü©,
    obtain ‚ü®c, cu, ac, hc‚ü© : ‚àÉ (c : Œπ) (H : c ‚àà u), (B a ‚à© B c).nonempty ‚àß Œ¥ a ‚â§ œÑ * Œ¥ c :=
      (UT uU).2.2 a hat b hbu hab,
    exact ‚ü®c, ‚ü®u, uU, cu‚ü©, ac, hc‚ü© },
  -- the only nontrivial bit is to check that every `a ‚àà t` intersects an element `b ‚àà u` with
  -- comparatively large `Œ¥ b`. Assume this is not the case, then we will contradict the maximality.
  refine ‚ü®u, uT.1, uT.2.1, Œª a hat, _‚ü©,
  contrapose! hu,
  have a_disj : ‚àÄ c ‚àà u, disjoint (B a) (B c),
  { assume c hc,
    by_contra,
    rw not_disjoint_iff_nonempty_inter at h,
    obtain ‚ü®d, du, ad, hd‚ü© : ‚àÉ (d : Œπ) (H : d ‚àà u), (B a ‚à© B d).nonempty ‚àß Œ¥ a ‚â§ œÑ * Œ¥ d :=
      uT.2.2 a hat c hc h,
    exact lt_irrefl _ ((hu d du ad).trans_le hd) },
  -- Let `A` be all the elements of `t` which do not intersect the family `u`. It is nonempty as it
  -- contains `a`. We will pick an element `a'` of `A` with `Œ¥ a'` almost as large as possible.
  let A := {a' | a' ‚àà t ‚àß ‚àÄ c ‚àà u, disjoint (B a') (B c)},
  have Anonempty : A.nonempty := ‚ü®a, hat, a_disj‚ü©,
  let m := Sup (Œ¥ '' A),
  have bddA : bdd_above (Œ¥ '' A),
  { refine ‚ü®R, Œª x xA, _‚ü©,
    rcases (mem_image _ _ _).1 xA with ‚ü®a', ha', rfl‚ü©,
    exact Œ¥le a' ha'.1 },
  obtain ‚ü®a', a'A, ha'‚ü© : ‚àÉ a' ‚àà A, m / œÑ ‚â§ Œ¥ a',
  { have : 0 ‚â§ m := (Œ¥nonneg a hat).trans (le_cSup bddA (mem_image_of_mem _ ‚ü®hat, a_disj‚ü©)),
    rcases eq_or_lt_of_le this with mzero|mpos,
    { refine ‚ü®a, ‚ü®hat, a_disj‚ü©, _‚ü©,
      simpa only [‚Üê mzero, zero_div] using Œ¥nonneg a hat },
    { have I : m / œÑ < m,
      { rw div_lt_iff (zero_lt_one.trans hœÑ),
        conv_lhs { rw ‚Üê mul_one m },
        exact (mul_lt_mul_left mpos).2 hœÑ },
      rcases exists_lt_of_lt_cSup (nonempty_image_iff.2 Anonempty) I with ‚ü®x, xA, hx‚ü©,
      rcases (mem_image _ _ _).1 xA with ‚ü®a', ha', rfl‚ü©,
      exact ‚ü®a', ha', hx.le‚ü©, } },
  clear hat hu a_disj a,
  have a'_ne_u : a' ‚àâ u := Œª H, (hne _ a'A.1).ne_empty (disjoint_self.1 (a'A.2 _ H)),
  -- we claim that `u ‚à™ {a'}` still belongs to `T`, contradicting the maximality of `u`.
  refine ‚ü®insert a' u, ‚ü®_, _, _‚ü©, subset_insert _ _, (ne_insert_of_not_mem _ a'_ne_u).symm‚ü©,
  -- check that `u ‚à™ {a'}` is made of elements of `t`.
  { rw insert_subset,
    exact ‚ü®a'A.1, uT.1‚ü© },
  -- check that `u ‚à™ {a'}` is a disjoint family. This follows from the fact that `a'` does not
  -- intersect `u`.
  { exact uT.2.1.insert (Œª b bu ba', a'A.2 b bu) },
  -- check that every element `c` of `t` intersecting `u ‚à™ {a'}` intersects an element of this
  -- family with large `Œ¥`.
  { assume c ct b ba'u hcb,
    -- if `c` already intersects an element of `u`, then it intersects an element of `u` with
    -- large `Œ¥` by the assumption on `u`, and there is nothing left to do.
    by_cases H : ‚àÉ d ‚àà u, (B c ‚à© B d).nonempty,
    { rcases H with ‚ü®d, du, hd‚ü©,
      rcases uT.2.2 c ct d du hd with ‚ü®d', d'u, hd'‚ü©,
      exact ‚ü®d', mem_insert_of_mem _ d'u, hd'‚ü© },
    -- otherwise, `c` belongs to `A`. The element of `u ‚à™ {a'}` that it intersects has to be `a'`.
    -- moreover, `Œ¥ c` is smaller than the maximum `m` of `Œ¥` over `A`, which is `‚â§ Œ¥ a' / œÑ`
    -- thanks to the good choice of `a'`. This is the desired inequality.
    { push_neg at H,
      simp only [‚Üê not_disjoint_iff_nonempty_inter, not_not] at H,
      rcases mem_insert_iff.1 ba'u with rfl|H',
      { refine ‚ü®b, mem_insert _ _, hcb, _‚ü©,
        calc Œ¥ c ‚â§ m : le_cSup bddA (mem_image_of_mem _ ‚ü®ct, H‚ü©)
        ... = œÑ * (m / œÑ) : by { field_simp [(zero_lt_one.trans hœÑ).ne'], ring }
        ... ‚â§ œÑ * Œ¥ b : mul_le_mul_of_nonneg_left ha' (zero_le_one.trans hœÑ.le) },
      { rw ‚Üê not_disjoint_iff_nonempty_inter at hcb,
        exact (hcb (H _ H')).elim } } }
end

/-- Vitali covering theorem, closed balls version: given a family `t` of closed balls, one can
extract a disjoint subfamily `u ‚äÜ t` so that all balls in `t` are covered by the 5-times
dilations of balls in `u`. -/
theorem exists_disjoint_subfamily_covering_enlargment_closed_ball [metric_space Œ±]
  (t : set Œπ) (x : Œπ ‚Üí Œ±) (r : Œπ ‚Üí ‚Ñù) (R : ‚Ñù) (hr : ‚àÄ a ‚àà t, r a ‚â§ R) :
  ‚àÉ u ‚äÜ t, u.pairwise_disjoint (Œª a, closed_ball (x a) (r a)) ‚àß
    ‚àÄ a ‚àà t, ‚àÉ b ‚àà u, closed_ball (x a) (r a) ‚äÜ closed_ball (x b) (5 * r b) :=
begin
  rcases eq_empty_or_nonempty t with rfl|tnonempty,
  { exact ‚ü®‚àÖ, subset.refl _, pairwise_disjoint_empty, by simp‚ü© },
  by_cases ht : ‚àÄ a ‚àà t, r a < 0,
  { exact ‚ü®t, subset.rfl, Œª a ha b hb hab,
      by simp only [function.on_fun, closed_ball_eq_empty.2 (ht a ha), empty_disjoint],
      Œª a ha, ‚ü®a, ha, by simp only [closed_ball_eq_empty.2 (ht a ha), empty_subset]‚ü©‚ü© },
  push_neg at ht,
  let t' := {a ‚àà t | 0 ‚â§ r a},
  rcases exists_disjoint_subfamily_covering_enlargment (Œª a, closed_ball (x a) (r a)) t' r
    2 one_lt_two (Œª a ha, ha.2) R (Œª a ha, hr a ha.1) (Œª a ha, ‚ü®x a, mem_closed_ball_self ha.2‚ü©)
    with ‚ü®u, ut', u_disj, hu‚ü©,
  have A : ‚àÄ a ‚àà t', ‚àÉ b ‚àà u, closed_ball (x a) (r a) ‚äÜ closed_ball (x b) (5 * r b),
  { assume a ha,
    rcases hu a ha with ‚ü®b, bu, hb, rb‚ü©,
    refine ‚ü®b, bu, _‚ü©,
    have : dist (x a) (x b) ‚â§ r a + r b :=
      dist_le_add_of_nonempty_closed_ball_inter_closed_ball hb,
    apply closed_ball_subset_closed_ball',
    linarith },
  refine ‚ü®u, ut'.trans (Œª a ha, ha.1), u_disj, Œª a ha, _‚ü©,
  rcases le_or_lt 0 (r a) with h'a|h'a,
  { exact A a ‚ü®ha, h'a‚ü© },
  { rcases ht with ‚ü®b, rb‚ü©,
    rcases A b ‚ü®rb.1, rb.2‚ü© with ‚ü®c, cu, hc‚ü©,
    refine ‚ü®c, cu, by simp only [closed_ball_eq_empty.2 h'a, empty_subset]‚ü© },
end


/-- The measurable Vitali covering theorem. Assume one is given a family `t` of closed sets with
nonempty interior, such that each `a ‚àà t` is included in a ball `B (x, r)` and covers a definite
proportion of the ball `B (x, 3 r)` for a given measure `Œº` (think of the situation where `Œº` is
a doubling measure and `t` is a family of balls). Consider a (possibly non-measurable) set `s`
at which the family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`.
Then one can extract from `t` a disjoint subfamily that covers almost all `s`.

For more flexibility, we give a statement with a parameterized family of sets.
-/
theorem exists_disjoint_covering_ae [metric_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±]
  [second_countable_topology Œ±]
  (Œº : measure Œ±) [is_locally_finite_measure Œº] (s : set Œ±)
  (t : set Œπ) (C : ‚Ñù‚â•0) (r : Œπ ‚Üí ‚Ñù) (c : Œπ ‚Üí Œ±) (B : Œπ ‚Üí set Œ±)
  (hB : ‚àÄ a ‚àà t, B a ‚äÜ closed_ball (c a) (r a))
  (ŒºB : ‚àÄ a ‚àà t, Œº (closed_ball (c a) (3 * r a)) ‚â§ C * Œº (B a))
  (ht : ‚àÄ a ‚àà t, (interior (B a)).nonempty) (h't : ‚àÄ a ‚àà t, is_closed (B a))
  (hf : ‚àÄ x ‚àà s, ‚àÄ (Œµ > (0 : ‚Ñù)), ‚àÉ a ‚àà t, r a ‚â§ Œµ ‚àß c a = x) :
  ‚àÉ u ‚äÜ t, u.countable ‚àß u.pairwise_disjoint B ‚àß Œº (s \ ‚ãÉ a ‚àà u, B a) = 0 :=
begin
  /- The idea of the proof is the following. Assume for simplicity that `Œº` is finite. Applying the
  abstract Vitali covering theorem with `Œ¥ = r` given by `hf`, one obtains a disjoint subfamily `u`,
  such that any element of `t` intersects an element of `u` with comparable radius. Fix `Œµ > 0`.
  Since the elements of `u` have summable measure, one can remove finitely elements `w_1, ..., w_n`.
  so that the measure of the remaining elements is `< Œµ`. Consider now a point `z` not
  in the `w_i`. There is a small ball around `z` not intersecting the `w_i` (as they are closed),
  an element `a ‚àà t` contained in this small ball (as the family `t` is fine at `z`) and an element
  `b ‚àà u` intersecting `a`, with comparable radius (by definition of `u`). Then `z` belongs to the
  enlargement of `b`. This shows that `s \ (w_1 ‚à™ ... ‚à™ w_n)` is contained in
  `‚ãÉ (b ‚àà u \ {w_1, ... w_n}) (enlargement of b)`. The measure of the latter set is bounded by
  `‚àë (b ‚àà u \ {w_1, ... w_n}) C * Œº b` (by the doubling property of the measure), which is at most
  `C Œµ`. Letting `Œµ` tend to `0` shows that `s` is almost everywhere covered by the family `u`.

  For the real argument, the measure is only locally finite. Therefore, we implement the same
  strategy, but locally restricted to balls on which the measure is finite. For this, we do not
  use the whole family `t`, but a subfamily `t'` supported on small balls (which is possible since
  the family is assumed to be fine at every point of `s`).
  -/
  -- choose around each `x` a small ball on which the measure is finite
  have : ‚àÄ x, ‚àÉ R, 0 < R ‚àß R ‚â§ 1 ‚àß Œº (closed_ball x (20 * R)) < ‚àû,
  { assume x,
    obtain ‚ü®R, Rpos, ŒºR‚ü© : ‚àÉ (R : ‚Ñù) (hR : 0 < R), Œº (closed_ball x R) < ‚àû :=
      (Œº.finite_at_nhds x).exists_mem_basis nhds_basis_closed_ball,
    refine ‚ü®min 1 (R/20), _, min_le_left _ _, _‚ü©,
    { simp only [true_and, lt_min_iff, zero_lt_one],
      linarith },
    { apply lt_of_le_of_lt (measure_mono _) ŒºR,
      apply closed_ball_subset_closed_ball,
      calc 20 * min 1 (R / 20) ‚â§ 20 * (R/20) :
        mul_le_mul_of_nonneg_left (min_le_right _ _) (by norm_num)
      ... = R : by ring } },
  choose R hR0 hR1 hRŒº,
  -- we restrict to a subfamily `t'` of `t`, made of elements small enough to ensure that
  -- they only see a finite part of the measure, and with a doubling property
  let t' := {a ‚àà t | r a ‚â§ R (c a)},
  -- extract a disjoint subfamily `u` of `t'` thanks to the abstract Vitali covering theorem.
  obtain ‚ü®u, ut', u_disj, hu‚ü© : ‚àÉ u ‚äÜ t', u.pairwise_disjoint B ‚àß
    ‚àÄ a ‚àà t', ‚àÉ b ‚àà u, (B a ‚à© B b).nonempty ‚àß r a ‚â§ 2 * r b,
  { have A : ‚àÄ a ‚àà t', r a ‚â§ 1,
    { assume a ha,
      apply ha.2.trans (hR1 (c a)), },
    have A' : ‚àÄ a ‚àà t', (B a).nonempty :=
      Œª a hat', set.nonempty.mono interior_subset (ht a hat'.1),
    refine exists_disjoint_subfamily_covering_enlargment B t' r 2 one_lt_two
      (Œª a ha, _) 1 A A',
    exact nonempty_closed_ball.1 ((A' a ha).mono (hB a ha.1)) },
  have ut : u ‚äÜ t := Œª a hau, (ut' hau).1,
  -- As the space is second countable, the family is countable since all its sets have nonempty
  -- interior.
  have u_count : u.countable := u_disj.countable_of_nonempty_interior (Œª a ha, ht a (ut ha)),
  -- the family `u` will be the desired family
  refine ‚ü®u, Œª a hat', (ut' hat').1, u_count, u_disj, _‚ü©,
  -- it suffices to show that it covers almost all `s` locally around each point `x`.
  refine null_of_locally_null _ (Œª x hx, _),
  -- let `v` be the subfamily of `u` made of those sets intersecting the small ball `ball x (r x)`
  let v := {a ‚àà u | (B a ‚à© ball x (R x)).nonempty },
  have vu : v ‚äÜ u := Œª a ha, ha.1,
  -- they are all contained in a fixed ball of finite measure, thanks to our choice of `t'`
  obtain ‚ü®K, ŒºK, hK‚ü© : ‚àÉ K, Œº (closed_ball x K) < ‚àû ‚àß
                          ‚àÄ a ‚àà u, (B a ‚à© ball x (R x)).nonempty ‚Üí B a ‚äÜ closed_ball x K,
  { have Idist_v : ‚àÄ a ‚àà v, dist (c a) x ‚â§ r a + R x,
    { assume a hav,
      apply dist_le_add_of_nonempty_closed_ball_inter_closed_ball,
      refine hav.2.mono _,
      apply inter_subset_inter _ ball_subset_closed_ball,
      exact hB a (ut (vu hav)) },
    set R0 := Sup (r '' v) with R0_def,
    have R0_bdd : bdd_above (r '' v),
    { refine ‚ü®1, Œª r' hr', _‚ü©,
      rcases (mem_image _ _ _).1 hr' with ‚ü®b, hb, rfl‚ü©,
      exact le_trans (ut' (vu hb)).2 (hR1 (c b)) },
    rcases le_total R0 (R x) with H|H,
    { refine ‚ü®20 * R x, hRŒº x, Œª a au hax, _‚ü©,
      refine (hB a (ut au)).trans _,
      apply closed_ball_subset_closed_ball',
      have : r a ‚â§ R0 := le_cSup R0_bdd (mem_image_of_mem _ ‚ü®au, hax‚ü©),
      linarith [Idist_v a ‚ü®au, hax‚ü©, hR0 x] },
    { have R0pos : 0 < R0 := (hR0 x).trans_le H,
      have vnonempty : v.nonempty,
      { by_contra,
        rw [nonempty_iff_ne_empty, not_not] at h,
        simp only [h, real.Sup_empty, image_empty] at R0_def,
        exact lt_irrefl _ (R0pos.trans_le (le_of_eq R0_def)) },
      obtain ‚ü®a, hav, R0a‚ü© : ‚àÉ a ‚àà v, R0/2 < r a,
      { obtain ‚ü®r', r'mem, hr'‚ü© : ‚àÉ r' ‚àà r '' v, R0 / 2 < r' :=
          exists_lt_of_lt_cSup (nonempty_image_iff.2 vnonempty) (half_lt_self R0pos),
        rcases (mem_image _ _ _).1 r'mem with ‚ü®a, hav, rfl‚ü©,
        exact ‚ü®a, hav, hr'‚ü© },
      refine ‚ü®8 * R0, _, _‚ü©,
      { apply lt_of_le_of_lt (measure_mono _) (hRŒº (c a)),
        apply closed_ball_subset_closed_ball',
        rw dist_comm,
        linarith [Idist_v a hav, (ut' (vu hav)).2] },
      { assume b bu hbx,
        refine (hB b (ut bu)).trans _,
        apply closed_ball_subset_closed_ball',
        have : r b ‚â§ R0 := le_cSup R0_bdd (mem_image_of_mem _ ‚ü®bu, hbx‚ü©),
        linarith [Idist_v b ‚ü®bu, hbx‚ü©] } } },
  -- we will show that, in `ball x (R x)`, almost all `s` is covered by the family `u`.
  refine ‚ü®_ ‚à© ball x (R x), inter_mem_nhds_within _ (ball_mem_nhds _ (hR0 _)),
    nonpos_iff_eq_zero.mp (le_of_forall_le_of_dense (Œª Œµ Œµpos, _))‚ü©,
  -- the elements of `v` are disjoint and all contained in a finite volume ball, hence the sum
  -- of their measures is finite.
  have I : ‚àë' (a : v), Œº (B a) < ‚àû,
  { calc ‚àë' (a : v), Œº (B a) = Œº (‚ãÉ (a ‚àà v), B a) : begin
      rw measure_bUnion (u_count.mono vu) _ (Œª a ha, (h't _ (vu.trans ut ha)).measurable_set),
      exact u_disj.subset vu
    end
    ... ‚â§ Œº (closed_ball x K) : measure_mono (Union‚ÇÇ_subset (Œª a ha, hK a (vu ha) ha.2))
    ... < ‚àû : ŒºK },
  -- we can obtain a finite subfamily of `v`, such that the measures of the remaining elements
  -- add up to an arbitrarily small number, say `Œµ / C`.
  obtain ‚ü®w, hw‚ü© : ‚àÉ (w : finset ‚Ü•v), ‚àë' (a : {a // a ‚àâ w}), Œº (B a) < Œµ / C,
  { have : 0 < Œµ / C, by simp only [ennreal.div_pos_iff, Œµpos.ne', ennreal.coe_ne_top, ne.def,
                                    not_false_iff, and_self],
    exact ((tendsto_order.1 (ennreal.tendsto_tsum_compl_at_top_zero I.ne)).2 _ this).exists },
  -- main property: the points `z` of `s` which are not covered by `u` are contained in the
  -- enlargements of the elements not in `w`.
  have M : (s \ ‚ãÉ a ‚àà u, B a) ‚à© ball x (R x)
    ‚äÜ ‚ãÉ (a : {a // a ‚àâ w}), closed_ball (c a) (3 * r a),
  { assume z hz,
    set k := ‚ãÉ (a : v) (ha : a ‚àà w), B a with hk,
    have k_closed : is_closed k :=
      is_closed_bUnion w.finite_to_set (Œª i hi, h't _ (ut (vu i.2))),
    have z_notmem_k : z ‚àâ k,
    { simp only [not_exists, exists_prop, mem_Union, mem_sep_iff, forall_exists_index,
        set_coe.exists, not_and, exists_and_distrib_right, subtype.coe_mk],
      assume b hbv h'b h'z,
      have : z ‚àà (s \ ‚ãÉ a ‚àà u, B a) ‚à© (‚ãÉ a ‚àà u, B a) :=
        mem_inter (mem_of_mem_inter_left hz) (mem_bUnion (vu hbv) h'z),
      simpa only [diff_inter_self] },
    -- since the elements of `w` are closed and finitely many, one can find a small ball around `z`
    -- not intersecting them
    have : ball x (R x) \ k ‚àà ùìù z,
    { apply is_open.mem_nhds (is_open_ball.sdiff k_closed) _,
      exact (mem_diff _).2 ‚ü®mem_of_mem_inter_right hz, z_notmem_k‚ü© },
    obtain ‚ü®d, dpos, hd‚ü© : ‚àÉ (d : ‚Ñù) (dpos : 0 < d), closed_ball z d ‚äÜ ball x (R x) \ k :=
      nhds_basis_closed_ball.mem_iff.1 this,
    -- choose an element `a` of the family `t` contained in this small ball
    obtain ‚ü®a, hat, ad, rfl‚ü© : ‚àÉ a ‚àà t, r a ‚â§ min d (R z) ‚àß c a = z,
      from hf z ((mem_diff _).1 (mem_of_mem_inter_left hz)).1 (min d (R z)) (lt_min dpos (hR0 z)),
    have ax : B a ‚äÜ ball x (R x),
    { refine (hB a hat).trans _,
      refine subset.trans _ (hd.trans (diff_subset (ball x (R x)) k)),
      exact closed_ball_subset_closed_ball (ad.trans (min_le_left _ _)), },
    -- it intersects an element `b` of `u` with comparable diameter, by definition of `u`
    obtain ‚ü®b, bu, ab, bdiam‚ü© : ‚àÉ b ‚àà u, (B a ‚à© B b).nonempty ‚àß r a ‚â§ 2 * r b,
      from hu a ‚ü®hat, ad.trans (min_le_right _ _)‚ü©,
    have bv : b ‚àà v,
    { refine ‚ü®bu, ab.mono _‚ü©,
      rw inter_comm,
      exact inter_subset_inter_right _ ax },
    let b' : v := ‚ü®b, bv‚ü©,
    -- `b` can not belong to `w`, as the elements of `w` do not intersect `closed_ball z d`,
    -- contrary to `b`
    have b'_notmem_w : b' ‚àâ w,
    { assume b'w,
      have b'k : B b' ‚äÜ k, from @finset.subset_set_bUnion_of_mem _ _ _ (Œª (y : v), B y) _ b'w,
      have : ((ball x (R x) \ k) ‚à© k).nonempty,
      { apply ab.mono (inter_subset_inter _ b'k),
        refine ((hB _ hat).trans _).trans hd,
        exact (closed_ball_subset_closed_ball (ad.trans (min_le_left _ _))) },
      simpa only [diff_inter_self, not_nonempty_empty] },
    let b'' : {a // a ‚àâ w} := ‚ü®b', b'_notmem_w‚ü©,
    -- since `a` and `b` have comparable diameters, it follows that `z` belongs to the
    -- enlargement of `b`
    have zb : c a ‚àà closed_ball (c b) (3 * r b),
    { rcases ab with ‚ü®e, ‚ü®ea, eb‚ü©‚ü©,
      have A : dist (c a) e ‚â§ r a, from mem_closed_ball'.1 (hB a hat ea),
      have B : dist e (c b) ‚â§ r b, from mem_closed_ball.1 (hB b (ut bu) eb),
      simp only [mem_closed_ball],
      linarith [dist_triangle (c a) e (c b)] },
    suffices H : closed_ball (c b'') (3 * r b'')
      ‚äÜ ‚ãÉ (a : {a // a ‚àâ w}), closed_ball (c a) (3 * r a), from H zb,
    exact subset_Union (Œª (a : {a // a ‚àâ w}), closed_ball (c a) (3 * r a)) b'' },
  -- now that we have proved our main inclusion, we can use it to estimate the measure of the points
  -- in `ball x (r x)` not covered by `u`.
  haveI : encodable v := (u_count.mono vu).to_encodable,
  calc Œº ((s \ ‚ãÉ a ‚àà u, B a) ‚à© ball x (R x))
      ‚â§ Œº (‚ãÉ (a : {a // a ‚àâ w}), closed_ball (c a) (3 * r a)) : measure_mono M
  ... ‚â§ ‚àë' (a : {a // a ‚àâ w}), Œº (closed_ball (c a) (3 * r a)) :
    measure_Union_le _
  ... ‚â§ ‚àë' (a : {a // a ‚àâ w}), C * Œº (B a) : ennreal.tsum_le_tsum (Œª a, ŒºB a (ut (vu a.1.2)))
  ... = C * ‚àë' (a : {a // a ‚àâ w}), Œº (B a) : ennreal.tsum_mul_left
  ... ‚â§ C * (Œµ / C) : mul_le_mul_left' hw.le _
  ... ‚â§ Œµ : ennreal.mul_div_le
end

/-- Assume that around every point there are arbitrarily small scales at which the measure is
doubling. Then the set of closed sets `a` with nonempty interior contained in `closed_ball x r` and
covering a fixed proportion `1/C` of the ball `closed_ball x (3 * r)` forms a Vitali family.
This is essentially a restatement of the measurable Vitali theorem. -/
protected def vitali_family [metric_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±]
  [second_countable_topology Œ±] (Œº : measure Œ±) [is_locally_finite_measure Œº] (C : ‚Ñù‚â•0)
  (h : ‚àÄ x, ‚àÉ·∂† r in ùìù[>] 0, Œº (closed_ball x (3 * r)) ‚â§ C * Œº (closed_ball x r)) :
  vitali_family Œº :=
{ sets_at := Œª x, {a | is_closed a ‚àß (interior a).nonempty ‚àß ‚àÉ r, (a ‚äÜ closed_ball x r ‚àß
                      Œº (closed_ball x (3 * r)) ‚â§ C * Œº a)},
  measurable_set' := Œª x a ha, ha.1.measurable_set,
  nonempty_interior := Œª x a ha, ha.2.1,
  nontrivial := Œª x Œµ Œµpos, begin
    obtain ‚ü®r, Œºr, rpos, rŒµ‚ü© : ‚àÉ r,
      Œº (closed_ball x (3 * r)) ‚â§ C * Œº (closed_ball x r) ‚àß r ‚àà Ioc (0 : ‚Ñù) Œµ :=
      ((h x).and_eventually (Ioc_mem_nhds_within_Ioi ‚ü®le_rfl, Œµpos‚ü©)).exists,
    refine ‚ü®closed_ball x r, ‚ü®is_closed_ball, _, ‚ü®r, subset.rfl, Œºr‚ü©‚ü©,
      closed_ball_subset_closed_ball rŒµ‚ü©,
    exact (nonempty_ball.2 rpos).mono (ball_subset_interior_closed_ball)
  end,
  covering := begin
    assume s f fsubset ffine,
    let t : set (‚Ñù √ó Œ± √ó set Œ±) :=
      {p | p.2.2 ‚äÜ closed_ball p.2.1 p.1 ‚àß Œº (closed_ball p.2.1 (3 * p.1)) ‚â§ C * Œº p.2.2
      ‚àß (interior p.2.2).nonempty ‚àß is_closed p.2.2 ‚àß p.2.2 ‚àà f p.2.1 ‚àß p.2.1 ‚àà s},
    have A : ‚àÄ x ‚àà s, ‚àÄ (Œµ : ‚Ñù), Œµ > 0 ‚Üí (‚àÉ (p : ‚Ñù √ó Œ± √ó set Œ±) (Hp : p ‚àà t), p.1 ‚â§ Œµ ‚àß p.2.1 = x),
    { assume x xs Œµ Œµpos,
      rcases ffine x xs Œµ Œµpos with ‚ü®a, ha, h'a‚ü©,
      rcases fsubset x xs ha with ‚ü®a_closed, a_int, ‚ü®r, ar, Œºr‚ü©‚ü©,
      refine ‚ü®‚ü®min r Œµ, x, a‚ü©, ‚ü®_, _, a_int, a_closed, ha, xs‚ü©, min_le_right _ _, rfl‚ü©,
      { rcases min_cases r Œµ with h'|h'; rwa h'.1 },
      { apply le_trans (measure_mono (closed_ball_subset_closed_ball _)) Œºr,
        exact mul_le_mul_of_nonneg_left (min_le_left _ _) zero_le_three } },
    rcases exists_disjoint_covering_ae Œº s t C (Œª p, p.1) (Œª p, p.2.1) (Œª p, p.2.2) (Œª p hp, hp.1)
      (Œª p hp, hp.2.1) (Œª p hp, hp.2.2.1) (Œª p hp, hp.2.2.2.1) A
      with ‚ü®t', t't, t'_count, t'_disj, Œºt'‚ü©,
    refine ‚ü®(Œª (p : ‚Ñù √ó Œ± √ó set Œ±), p.2) '' t', _, _, _, _‚ü©,
    { rintros - ‚ü®q, hq, rfl‚ü©,
      exact (t't hq).2.2.2.2.2 },
    { rintros p ‚ü®q, hq, rfl‚ü© p' ‚ü®q', hq', rfl‚ü© hqq',
      exact t'_disj hq hq' (ne_of_apply_ne _ hqq') },
    { rintros - ‚ü®q, hq, rfl‚ü©,
      exact (t't hq).2.2.2.2.1 },
    { convert Œºt' using 3,
      rw bUnion_image }
  end }

end vitali
