/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import topology.metric_space.basic
import measure_theory.constructions.borel_space
import measure_theory.covering.vitali_family

/-!
# Vitali covering theorems

The topological Vitali covering theorem, in its most classical version, states the following.
Consider a family of balls `(B (x_i, r_i))_{i ‚àà I}` in a metric space, with uniformly bounded
radii. Then one can extract a disjoint subfamily indexed by `J ‚äÜ I`, such that any `B (x_i, r_i)`
is included in a ball `B (x_j, 5 r_j)`.

We prove this theorem in `vitali.exists_disjoint_subfamily_covering_enlargment_closed_ball`.
It is deduced from a more general version, called
`vitali.exists_disjoint_subfamily_covering_enlargment`, which applies to any family of sets
together with a size function `Œ¥` (think "radius" or "diameter").

We deduce the measurable Vitali covering theorem. Assume one is given a family `t` of closed sets
with nonempty interior, such that each `a ‚àà t` is included in a ball `B (x, r)` and covers a
definite proportion of the ball `B (x, 6 r)` for a given measure `Œº` (think of the situation
where `Œº` is a doubling measure and `t` is a family of balls). Consider a set `s` at which the
family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`. Then one
can extract from `t` a disjoint subfamily that covers almost all `s`. It is proved in
`vitali.exists_disjoint_covering_ae`.

A way to restate this theorem is to say that the set of closed sets `a` with nonempty interior
covering a fixed proportion `1/C` of the ball `closed_ball x (3 * diam a)` forms a Vitali family.
This version is given in `vitali.vitali_family`.
-/

variables {Œ± : Type*}

open set metric measure_theory topological_space filter
open_locale nnreal classical ennreal topological_space

namespace vitali

/-- Vitali covering theorem: given a set `t` of subsets of a type, one may extract a disjoint
subfamily `u` such that the `œÑ`-enlargment of this family covers all elements of `t`, where `œÑ > 1`
is any fixed number.

When `t` is a family of balls, the `œÑ`-enlargment of `ball x r` is `ball x ((1+2œÑ) r)`. In general,
it is expressed in terms of a function `Œ¥` (think "radius" or "diameter"), positive and bounded on
all elements of `t`. The condition is that every element `a` of `t` should intersect an
element `b` of `u` of size larger than that of `a` up to `œÑ`, i.e., `Œ¥ b ‚â• Œ¥ a / œÑ`.
-/
theorem exists_disjoint_subfamily_covering_enlargment
  (t : set (set Œ±)) (Œ¥ : set Œ± ‚Üí ‚Ñù) (œÑ : ‚Ñù) (hœÑ : 1 < œÑ) (Œ¥nonneg : ‚àÄ a ‚àà t, 0 ‚â§ Œ¥ a)
  (R : ‚Ñù) (Œ¥le : ‚àÄ a ‚àà t, Œ¥ a ‚â§ R) (hne : ‚àÄ a ‚àà t, set.nonempty a) :
  ‚àÉ u ‚äÜ t, u.pairwise_on disjoint ‚àß
    ‚àÄ a ‚àà t, ‚àÉ b ‚àà u, set.nonempty (a ‚à© b) ‚àß Œ¥ a ‚â§ œÑ * Œ¥ b :=
begin
  /- The proof could be formulated as a transfinite induction. First pick an element of `t` with `Œ¥`
  as large as possible (up to a factor of `œÑ`). Then among the remaining elements not intersecting
  the already chosen one, pick another element with large `Œ¥`. Go on forever (transfinitely) until
  there is nothing left.

  Instead, we give a direct Zorn-based argument. Consider a maximal family `u` of disjoint sets
  with the following property: if an element `a` of `t` intersects some element `b` of `u`, then it
  intersects some `b' ‚àà u` with `Œ¥ b' ‚â• Œ¥ a / œÑ`. Such a maximal family exists by Zorn. If this
  family did not intersect some element `a ‚àà t`, then take an element `a' ‚àà t` which does not
  intersect any element of `u`, with `Œ¥ a'` almost as large as possible. One checks easily
  that `u ‚à™ {a'}` still has this property, contradicting the maximality. Therefore, `u`
  intersects all elements of `t`, and by definition it satisfies all the desired properties.
  -/
  let T : set (set (set Œ±)) := {u | u ‚äÜ t ‚àß u.pairwise_on disjoint
    ‚àß ‚àÄ a ‚àà t, ‚àÄ b ‚àà u, set.nonempty (a ‚à© b) ‚Üí ‚àÉ c ‚àà u, (a ‚à© c).nonempty ‚àß Œ¥ a ‚â§ œÑ * Œ¥ c},
  -- By Zorn, choose a maximal family in the good set `T` of disjoint families.
  obtain ‚ü®u, uT, hu‚ü© : ‚àÉ u ‚àà T, ‚àÄ v ‚àà T, u ‚äÜ v ‚Üí v = u,
  { refine zorn.zorn_subset _ (Œª U UT hU, _),
    refine ‚ü®‚ãÉ‚ÇÄ U, _, Œª s hs, subset_sUnion_of_mem hs‚ü©,
    simp only [set.sUnion_subset_iff, and_imp, exists_prop, forall_exists_index,
                set.mem_set_of_eq],
    refine ‚ü®Œª u hu, (UT hu).1, _, Œª a hat b u uU hbu hab, _‚ü©,
    { rw [pairwise_on_sUnion hU.directed_on],
      assume u hu,
      exact (UT hu).2.1 },
    { obtain ‚ü®c, cu, ac, hc‚ü© : ‚àÉ (c : set Œ±) (H : c ‚àà u), (a ‚à© c).nonempty ‚àß Œ¥ a ‚â§ œÑ * Œ¥ c :=
        (UT uU).2.2 a hat b hbu hab,
      exact ‚ü®c, ‚ü®u, uU, cu‚ü©, ac, hc‚ü© } },
  -- the only nontrivial bit is to check that every `a ‚àà t` intersects an element `b ‚àà u` with
  -- comparatively large `Œ¥ b`. Assume this is not the case, then we will contradict the maximality.
  refine ‚ü®u, uT.1, uT.2.1, Œª a hat, _‚ü©,
  contrapose! hu,
  have a_disj : ‚àÄ c ‚àà u, disjoint a c,
  { assume c hc,
    by_contra,
    rw not_disjoint_iff_nonempty_inter at h,
    obtain ‚ü®d, du, ad, hd‚ü© : ‚àÉ (d : set Œ±) (H : d ‚àà u), (a ‚à© d).nonempty ‚àß Œ¥ a ‚â§ œÑ * Œ¥ d :=
      uT.2.2 a hat c hc h,
    exact lt_irrefl _ ((hu d du ad).trans_le hd) },
  -- Let `A` be all the elements of `t` which do not intersect the family `u`. It is nonempty as it
  -- contains `a`. We will pick an element `a'` of `A` with `Œ¥ a'` almost as large as possible.
  let A := {a' | a' ‚àà t ‚àß ‚àÄ c ‚àà u, disjoint a' c},
  have Anonempty : A.nonempty := ‚ü®a, hat, a_disj‚ü©,
  let m := Sup (Œ¥ '' A),
  have bddA : bdd_above (Œ¥ '' A),
  { refine ‚ü®R, Œª x xA, _‚ü©,
    rcases (mem_image _ _ _).1 xA with ‚ü®a', ha', rfl‚ü©,
    exact Œ¥le a' ha'.1 },
  obtain ‚ü®a', a'A, ha'‚ü© : ‚àÉ a' ‚àà A, m / œÑ ‚â§ Œ¥ a',
  { have : 0 ‚â§ m := (Œ¥nonneg a hat).trans (le_cSup bddA (mem_image_of_mem _ ‚ü®hat, a_disj‚ü©)),
    rcases eq_or_lt_of_le this with mzero|mpos,
    { refine ‚ü®a, ‚ü®hat, a_disj‚ü©, _‚ü©,
      simpa only [‚Üê mzero, zero_div] using Œ¥nonneg a hat },
    { have I : m / œÑ < m,
      { rw div_lt_iff (zero_lt_one.trans hœÑ),
        conv_lhs { rw ‚Üê mul_one m },
        exact (mul_lt_mul_left mpos).2 hœÑ },
      rcases exists_lt_of_lt_cSup (nonempty_image_iff.2 Anonempty) I with ‚ü®x, xA, hx‚ü©,
      rcases (mem_image _ _ _).1 xA with ‚ü®a', ha', rfl‚ü©,
      exact ‚ü®a', ha', hx.le‚ü©, } },
  clear hat hu a_disj a,
  have a'_ne_u : a' ‚àâ u := Œª H, (hne _ a'A.1).ne_empty (disjoint_self.1 (a'A.2 _ H)),
  -- we claim that `u ‚à™ {a'}` still belongs to `T`, contradicting the maximality of `u`.
  refine ‚ü®insert a' u, ‚ü®_, _, _‚ü©, subset_insert _ _, (ne_insert_of_not_mem _ a'_ne_u).symm‚ü©,
  -- check that `u ‚à™ {a'}` is made of elements of `t`.
  { rw insert_subset,
    exact ‚ü®a'A.1, uT.1‚ü© },
  -- check that `u ‚à™ {a'}` is a disjoint family. This follows from the fact that `a'` does not
  -- intersect `u`.
  { rw pairwise_on_insert_of_symmetric, swap,
    { simp only [function.on_fun, symmetric, disjoint.comm, imp_self, forall_const] },
    exact ‚ü®uT.2.1, Œª b bu ba', a'A.2 b bu‚ü© },
  -- check that every element `c` of `t` intersecting `u ‚à™ {a'}` intersects an element of this
  -- family with large `Œ¥`.
  { assume c ct b ba'u hcb,
    -- if `c` already intersects an element of `u`, then it intersects an element of `u` with
    -- large `Œ¥` by the assumption on `u`, and there is nothing left to do.
    by_cases H : ‚àÉ d ‚àà u, set.nonempty (c ‚à© d),
    { rcases H with ‚ü®d, du, hd‚ü©,
      rcases uT.2.2 c ct d du hd with ‚ü®d', d'u, hd'‚ü©,
      exact ‚ü®d', mem_insert_of_mem _ d'u, hd'‚ü© },
    -- otherwise, `c` belongs to `A`. The element of `u ‚à™ {a'}` that it intersects has to be `a'`.
    -- moreover, `Œ¥ c` is smaller than the maximum `m` of `Œ¥` over `A`, which is `‚â§ Œ¥ a' / œÑ`
    -- thanks to the good choice of `a'`. This is the desired inequality.
    { push_neg at H,
      simp only [‚Üê not_disjoint_iff_nonempty_inter, not_not] at H,
      rcases mem_insert_iff.1 ba'u with rfl|H',
      { refine ‚ü®b, mem_insert _ _, hcb, _‚ü©,
        calc Œ¥ c ‚â§ m : le_cSup bddA (mem_image_of_mem _ ‚ü®ct, H‚ü©)
        ... = œÑ * (m / œÑ) : by { field_simp [(zero_lt_one.trans hœÑ).ne'], ring }
        ... ‚â§ œÑ * Œ¥ b : mul_le_mul_of_nonneg_left ha' (zero_le_one.trans hœÑ.le) },
      { rw ‚Üê not_disjoint_iff_nonempty_inter at hcb,
        exact (hcb (H _ H')).elim } } }
end

/-- Vitali covering theorem, closed balls version: given a family `t` of closed balls, one can
extract a disjoint subfamily `u ‚äÜ t` so that all balls in `t` are covered by the 5-times
dilations of balls in `u`. -/
theorem exists_disjoint_subfamily_covering_enlargment_closed_ball [metric_space Œ±]
  (t : set (set Œ±)) (R : ‚Ñù) (ht : ‚àÄ s ‚àà t, ‚àÉ x r, s = closed_ball x r ‚àß r ‚â§ R) :
  ‚àÉ u ‚äÜ t, u.pairwise_on disjoint ‚àß
    ‚àÄ a ‚àà t, ‚àÉ x r, closed_ball x r ‚àà u ‚àß a ‚äÜ closed_ball x (5 * r) :=
begin
  rcases eq_empty_or_nonempty t with rfl|tnonempty,
  { exact ‚ü®‚àÖ, subset.refl _, by simp‚ü© },
  haveI : inhabited Œ±,
  { choose s hst using tnonempty,
    choose x r hxr using ht s hst,
    exact ‚ü®x‚ü© },
  -- Exclude the trivial case where `t` is reduced to the empty set.
  rcases eq_or_ne t {‚àÖ} with rfl|t_ne_empty,
  { refine ‚ü®{‚àÖ}, subset.refl _, _‚ü©,
    simp only [true_and, closed_ball_eq_empty, mem_singleton_iff, and_true, empty_subset, forall_eq,
      pairwise_on_singleton, exists_const],
    exact ‚ü®-1, by simp only [right.neg_neg_iff, zero_lt_one]‚ü© },
  -- The real proof starts now. Since the center or the radius of a ball is not uniquely defined
  -- in a general metric space, we just choose one for definiteness.
  choose! x r hxr using ht,
  have r_nonneg : ‚àÄ (a : set Œ±), a ‚àà t ‚Üí a.nonempty ‚Üí 0 ‚â§ r a,
  { assume a hat a_nonempty,
    rw (hxr a hat).1 at a_nonempty,
    simpa only [nonempty_closed_ball] using a_nonempty },
  -- The difference with the generic version is that we are not excluding empty sets in our family
  -- (which would correspond to `r a < 0`). To compensate for this, we apply the generic version
  -- to the subfamily `t'` made of nonempty sets, and we use `Œ¥ = r` there. This gives a disjointed
  -- subfamily `u'`.
  let t' := {a ‚àà t | 0 ‚â§ r a},
  obtain ‚ü®u', u't', u'_disj, hu'‚ü© : ‚àÉ u' ‚äÜ t', u'.pairwise_on disjoint ‚àß
    ‚àÄ a ‚àà t', ‚àÉ b ‚àà u', set.nonempty (a ‚à© b) ‚àß r a ‚â§ 2 * r b,
  { refine exists_disjoint_subfamily_covering_enlargment t' r 2 one_lt_two
      (Œª a ha, ha.2) R (Œª a ha, (hxr a ha.1).2) (Œª a ha, _),
    rw [(hxr a ha.1).1],
    simp only [ha.2, nonempty_closed_ball] },
  -- this subfamily is nonempty, as we have excluded the situation `t = {‚àÖ}`.
  have u'_nonempty : u'.nonempty,
  { have : ‚àÉ a ‚àà t, a ‚â† ‚àÖ,
    { contrapose! t_ne_empty,
      apply subset.antisymm,
      { simpa only using t_ne_empty },
      { rcases tnonempty with ‚ü®a, hat‚ü©,
        have := t_ne_empty a hat,
        simpa only [this, singleton_subset_iff] using hat } },
    rcases this with ‚ü®a, hat, a_nonempty‚ü©,
    have ranonneg : 0 ‚â§ r a := r_nonneg a hat (ne_empty_iff_nonempty.1 a_nonempty),
    rcases hu' a ‚ü®hat, ranonneg‚ü© with ‚ü®b, bu', hb‚ü©,
    exact ‚ü®b, bu'‚ü© },
  -- check that the family `u'` gives the desired disjoint covering.
  refine ‚ü®u', Œª a ha, (u't' ha).1, u'_disj, Œª a hat, _‚ü©,
  -- it remains to check that any ball in `t` is contained in the 5-dilation of a ball
  -- in `u'`. This depends on whether the ball is empty of not.
  rcases eq_empty_or_nonempty a with rfl|a_nonempty,
  -- if the ball is empty, use any element of `u'` (since we know that `u'` is nonempty).
  { rcases u'_nonempty with ‚ü®b, hb‚ü©,
    refine ‚ü®x b, r b, _, empty_subset _‚ü©,
    rwa ‚Üê (hxr b (u't' hb).1).1 },
  -- if the ball is not empty, it belongs to `t'`. Then it intersects a ball `a'` in `u'` with
  -- controlled radius, by definition of `u'`. It is straightforward to check that this ball
  -- satisfies all the desired properties.
  { have hat' : a ‚àà t' := ‚ü®hat, r_nonneg a hat a_nonempty‚ü©,
    obtain ‚ü®a', a'u', aa', raa'‚ü© :
      (‚àÉ (a' : set Œ±) (H : a' ‚àà u'), (a ‚à© a').nonempty ‚àß r a ‚â§ 2 * r a') := hu' a hat',
    refine ‚ü®x a', r a', _, _‚ü©,
    { convert a'u',
      exact (hxr a' (u't' a'u').1).1.symm },
    { rw (hxr a hat'.1).1 at aa' ‚ä¢,
      rw (hxr a' (u't' a'u').1).1 at aa',
      have : dist (x a) (x a') ‚â§ r a + r a' :=
        dist_le_add_of_nonempty_closed_ball_inter_closed_ball aa',
      apply closed_ball_subset_closed_ball',
      linarith } }
end

/-- The measurable Vitali covering theorem. Assume one is given a family `t` of closed sets with
nonempty interior, such that each `a ‚àà t` is included in a ball `B (x, r)` and covers a definite
proportion of the ball `B (x, 6 r)` for a given measure `Œº` (think of the situation where `Œº` is
a doubling measure and `t` is a family of balls). Consider a (possible non-measurable) set `s`
at which the family is fine, i.e., every point of `s` belongs to arbitrarily small elements of `t`.
Then one can extract from `t` a disjoint subfamily that covers almost all `s`. -/
theorem exists_disjoint_covering_ae [metric_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±]
  [second_countable_topology Œ±]
  (Œº : measure Œ±) [is_locally_finite_measure Œº] (s : set Œ±)
  (t : set (set Œ±)) (hf : ‚àÄ x ‚àà s, ‚àÄ (Œµ > (0 : ‚Ñù)), ‚àÉ a ‚àà t, x ‚àà a ‚àß a ‚äÜ closed_ball x Œµ)
  (ht : ‚àÄ a ‚àà t, (interior a).nonempty) (h't : ‚àÄ a ‚àà t, is_closed a)
  (C : ‚Ñù‚â•0) (h : ‚àÄ a ‚àà t, ‚àÉ x ‚àà a, Œº (closed_ball x (3 * diam a)) ‚â§ C * Œº a) :
  ‚àÉ u ‚äÜ t, countable u ‚àß u.pairwise_on disjoint ‚àß Œº (s \ ‚ãÉ (a ‚àà u), a) = 0 :=
begin
  /- The idea of the proof is the following. Assume for simplicity that `Œº` is finite. Applying the
  abstract Vitali covering theorem with `Œ¥ = diam`, one obtains a disjoint subfamily `u`, such
  that any element of `t` intersects an element of `u` with comparable diameter. Fix `Œµ > 0`.
  Since the elements of `u` have summable measure, one can remove finitely elements `w_1, ..., w_n`.
  so that the measure of the remaining elements is `< Œµ`. Consider now a point `z` not
  in the `w_i`. There is a small ball around `z` not intersecting the `w_i` (as they are closed),
  an element `a ‚àà t` contained in this small ball (as the family `t` is fine at `z`) and an element
  `b ‚àà u` intersecting `a`, with comparable diameter (by definition of `u`). Then `z` belongs to the
  enlargement of `b`. This shows that `s \ (w_1 ‚à™ ... ‚à™ w_n)` is contained in
  `‚ãÉ (b ‚àà u \ {w_1, ... w_n}) (enlargement of b)`. The measure of the latter set is bounded by
  `‚àë (b ‚àà u \ {w_1, ... w_n}) C * Œº b` (by the doubling property of the measure), which is at most
  `C Œµ`. Letting `Œµ` tend to `0` shows that `s` is almost everywhere covered by the family `u`.

  For the real argument, the measure is only locally finite. Therefore, we implement the same
  strategy, but locally restricted to balls on which the measure is finite. For this, we do not
  use the whole family `t`, but a subfamily `t'` supported on small balls (which is possible since
  the family is assumed to be fine at every point of `s`).
  -/
  rcases eq_empty_or_nonempty s with rfl|nonempty,
  { refine ‚ü®‚àÖ, empty_subset _, countable_empty, by simp only [pairwise_on_empty],
      by simp only [measure_empty, Union_false, Union_empty, diff_self]‚ü© },
  haveI : inhabited Œ±,
  { choose x hx using nonempty,
    exact ‚ü®x‚ü© },
  -- choose around each `x` a small ball on which the measure is finite
  have : ‚àÄ x, ‚àÉ r, 0 < r ‚àß r ‚â§ 1 ‚àß Œº (closed_ball x (20 * r)) < ‚àû,
  { assume x,
    obtain ‚ü®R, Rpos, ŒºR‚ü© : ‚àÉ (R : ‚Ñù) (hR : 0 < R), Œº (closed_ball x R) < ‚àû :=
      (Œº.finite_at_nhds x).exists_mem_basis nhds_basis_closed_ball,
    refine ‚ü®min 1 (R/20), _, min_le_left _ _, _‚ü©,
    { simp only [true_and, lt_min_iff, zero_lt_one],
      linarith },
    { apply lt_of_le_of_lt (measure_mono _) ŒºR,
      apply closed_ball_subset_closed_ball,
      calc 20 * min 1 (R / 20) ‚â§ 20 * (R/20) :
        mul_le_mul_of_nonneg_left (min_le_right _ _) (by norm_num)
      ... = R : by ring } },
  choose r hr using this,
  -- we restrict to a subfamily `t'` of `t`, made of elements small enough to ensure that
  -- they only see a finite part of the measure.
  let t' := {a ‚àà t | ‚àÉ x, a ‚äÜ closed_ball x (r x)},
  -- extract a disjoint subfamily `u` of `t'` thanks to the abstract Vitali covering theorem.
  obtain ‚ü®u, ut', u_disj, hu‚ü© : ‚àÉ u ‚äÜ t', u.pairwise_on disjoint ‚àß
    ‚àÄ a ‚àà t', ‚àÉ b ‚àà u, set.nonempty (a ‚à© b) ‚àß diam a ‚â§ 2 * diam b,
  { have A : ‚àÄ (a : set Œ±), a ‚àà t' ‚Üí diam a ‚â§ 2,
    { rintros a ‚ü®hat, ‚ü®x, hax‚ü©‚ü©,
      calc diam a ‚â§ diam (closed_ball x (r x)) : diam_mono hax bounded_closed_ball
      ... ‚â§ 2 * r x : diam_closed_ball (hr x).1.le
      ... ‚â§ 2 * 1 : mul_le_mul_of_nonneg_left (hr x).2.1 zero_le_two
      ... = 2 : by norm_num },
    have B : ‚àÄ (a : set Œ±), a ‚àà t' ‚Üí a.nonempty :=
      Œª a hat', set.nonempty.mono interior_subset (ht a hat'.1),
    exact exists_disjoint_subfamily_covering_enlargment t' diam 2 one_lt_two
      (Œª a ha, diam_nonneg) 2 A B },
  have ut : u ‚äÜ t := Œª a hau, (ut' hau).1,
  -- As the space is second countable, the family is countable since all its sets have nonempty
  -- interior.
  have u_count : countable u :=
    countable_of_nonempty_interior_of_disjoint id (Œª a ha, ht a (ut ha)) u_disj,
  -- the family `u` will be the desired family
  refine ‚ü®u, Œª a hat', (ut' hat').1, u_count, u_disj, _‚ü©,
  -- it suffices to show that it covers almost all `s` locally around each point `x`.
  refine null_of_locally_null _ (Œª x hx, _),
  -- let `v` be the subfamily of `u` made of those sets intersecting the small ball `ball x (r x)`
  let v := {a ‚àà u | (a ‚à© ball x (r x)).nonempty },
  have vu : v ‚äÜ u := Œª a ha, ha.1,
  -- they are all contained in a fixed ball of finite measure, thanks to our choice of `t'`
  obtain ‚ü®R, ŒºR, hR‚ü© : ‚àÉ R, Œº (closed_ball x R) < ‚àû ‚àß
                          ‚àÄ a ‚àà u, (a ‚à© ball x (r x)).nonempty ‚Üí a ‚äÜ closed_ball x R,
  { have : ‚àÄ a ‚àà u, ‚àÉ y, a ‚äÜ closed_ball y (r y) := Œª a hau, (ut' hau).2,
    choose! y hy using this,
    have Idist_v : ‚àÄ a ‚àà v, dist (y a) x ‚â§ r (y a) + r x,
    { assume a hav,
      apply dist_le_add_of_nonempty_closed_ball_inter_closed_ball,
      exact hav.2.mono (inter_subset_inter (hy a hav.1) ball_subset_closed_ball) },
    set R0 := Sup ((Œª a, r (y a)) '' v) with hR0,
    have R0_bdd : bdd_above ((Œª a, r (y a)) '' v),
    { refine ‚ü®1, Œª r' hr', _‚ü©,
      rcases (mem_image _ _ _).1 hr' with ‚ü®b, hb, rfl‚ü©,
      exact (hr _).2.1 },
    rcases le_total R0 (r x) with H|H,
    { refine ‚ü®20 * r x, (hr x).2.2, Œª a au hax, _‚ü©,
      refine (hy a au).trans _,
      apply closed_ball_subset_closed_ball',
      have : r (y a) ‚â§ R0 := le_cSup R0_bdd (mem_image_of_mem _ ‚ü®au, hax‚ü©),
      linarith [(hr (y a)).1.le, (hr x).1.le, Idist_v a ‚ü®au, hax‚ü©] },
    { have R0pos : 0 < R0 := (hr x).1.trans_le H,
      have vnonempty : v.nonempty,
      { by_contra,
        rw [‚Üê ne_empty_iff_nonempty, not_not] at h,
        simp only [h, real.Sup_empty, image_empty] at hR0,
        exact lt_irrefl _ (R0pos.trans_le (le_of_eq hR0)) },
      obtain ‚ü®a, hav, R0a‚ü© : ‚àÉ a ‚àà v, R0/2 < r (y a),
      { obtain ‚ü®r', r'mem, hr'‚ü© : ‚àÉ r' ‚àà ((Œª a, r (y a)) '' v), R0 / 2 < r' :=
          exists_lt_of_lt_cSup (nonempty_image_iff.2 vnonempty) (half_lt_self R0pos),
        rcases (mem_image _ _ _).1 r'mem with ‚ü®a, hav, rfl‚ü©,
        exact ‚ü®a, hav, hr'‚ü© },
      refine ‚ü®8 * R0, _, _‚ü©,
      { apply lt_of_le_of_lt (measure_mono _) (hr (y a)).2.2,
        apply closed_ball_subset_closed_ball',
        rw dist_comm,
        linarith [Idist_v a hav] },
      { assume b bu hbx,
        refine (hy b bu).trans _,
        apply closed_ball_subset_closed_ball',
        have : r (y b) ‚â§ R0 := le_cSup R0_bdd (mem_image_of_mem _ ‚ü®bu, hbx‚ü©),
        linarith [Idist_v b ‚ü®bu, hbx‚ü©] } } },
  -- we will show that, in `ball x (r x)`, almost all `s` is covered by the family `u`.
  refine ‚ü®ball x (r x), _, le_antisymm (le_of_forall_le_of_dense (Œª Œµ Œµpos, _)) bot_le‚ü©,
  { apply mem_nhds_within_of_mem_nhds (is_open_ball.mem_nhds _),
    simp only [(hr x).left, mem_ball, dist_self] },
  -- the elements of `v` are disjoint and all contained in a finite volume ball, hence the sum
  -- of their measures is finite.
  have I : ‚àë' (a : v), Œº a < ‚àû,
  { calc ‚àë' (a : v), Œº a = Œº (‚ãÉ (a ‚àà v), a) : begin
      rw measure_bUnion (u_count.mono vu) _ (Œª a ha, (h't _ (vu.trans ut ha)).measurable_set),
      exact u_disj.mono vu
    end
    ... ‚â§ Œº (closed_ball x R) : measure_mono (bUnion_subset (Œª a ha, hR a (vu ha) ha.2))
    ... < ‚àû : ŒºR },
  -- we can obtain a finite subfamily of `v`, such that the measures of the remaining elements
  -- add up to an arbitrarily small number, say `Œµ / C`.
  obtain ‚ü®w, hw‚ü© : ‚àÉ (w : finset ‚Ü•v), ‚àë' (a : {a // a ‚àâ w}), Œº a < Œµ / C,
  { haveI : ne_bot (at_top : filter (finset v)) := at_top_ne_bot,
    have : 0 < Œµ / C, by simp only [ennreal.div_pos_iff, Œµpos.ne', ennreal.coe_ne_top, ne.def,
                                    not_false_iff, and_self],
    exact ((tendsto_order.1 (ennreal.tendsto_tsum_compl_at_top_zero I.ne)).2 _ this).exists },
  choose! y hy using h,
  -- main property: the points `z` of `s` which are not covered by `u` are contained in the
  -- enlargements of the elements not in `w`.
  have M : (s \ ‚ãÉ (a : set Œ±) (H : a ‚àà u), a) ‚à© ball x (r x)
    ‚äÜ ‚ãÉ (a : {a // a ‚àâ w}), closed_ball (y a) (3 * diam (a : set Œ±)),
  { assume z hz,
    set k := ‚ãÉ (a : v) (ha : a ‚àà w), (a : set Œ±) with hk,
    have k_closed : is_closed k :=
      is_closed_bUnion w.finite_to_set (Œª i hi, h't _ (ut (vu i.2))),
    have z_notmem_k : z ‚àâ k,
    { simp only [not_exists, exists_prop, mem_Union, mem_sep_eq, forall_exists_index,
        set_coe.exists, not_and, exists_and_distrib_right, subtype.coe_mk],
      assume b hbv h'b h'z,
      have : z ‚àà (s \ ‚ãÉ (a : set Œ±) (H : a ‚àà u), a) ‚à© (‚ãÉ (a : set Œ±) (H : a ‚àà u), a) :=
        mem_inter (mem_of_mem_inter_left hz) (mem_bUnion (vu hbv) h'z),
      simpa only [diff_inter_self] },
    -- since the elements of `w` are closed and finitely many, one can find a small ball around `z`
    -- not intersecting them
    have : ball x (r x) \ k ‚àà ùìù z,
    { apply is_open.mem_nhds (is_open_ball.sdiff k_closed) _,
      exact (mem_diff _).2 ‚ü®mem_of_mem_inter_right hz, z_notmem_k‚ü© },
    obtain ‚ü®d, dpos, hd‚ü© : ‚àÉ (d : ‚Ñù) (dpos : 0 < d), closed_ball z d ‚äÜ ball x (r x) \ k :=
      nhds_basis_closed_ball.mem_iff.1 this,
    -- choose an element `a` of the family `t` contained in this small ball
    obtain ‚ü®a, hat, za, ad‚ü© : ‚àÉ a ‚àà t, z ‚àà a ‚àß a ‚äÜ closed_ball z d :=
      hf z ((mem_diff _).1 (mem_of_mem_inter_left hz)).1 d dpos,
    have ax : a ‚äÜ ball x (r x) := ad.trans (hd.trans (diff_subset (ball x (r x)) k)),
    -- it intersects an element `b` of `u` with comparable diameter, by definition of `u`
    obtain ‚ü®b, bu, ab, bdiam‚ü© : ‚àÉ (b : set Œ±) (H : b ‚àà u), (a ‚à© b).nonempty ‚àß diam a ‚â§ 2 * diam b :=
      hu a ‚ü®hat, ‚ü®x, ax.trans ball_subset_closed_ball‚ü©‚ü©,
    have bv : b ‚àà v,
    { refine ‚ü®bu, ab.mono _‚ü©,
      rw inter_comm,
      exact inter_subset_inter_right _ ax },
    let b' : v := ‚ü®b, bv‚ü©,
    -- `b` can not belong to `w`, as the elements of `w` do not intersect `closed_ball z d`,
    -- contrary to `b`
    have b'_notmem_w : b' ‚àâ w,
    { assume b'w,
      have b'k : (b' : set Œ±) ‚äÜ k := finset.subset_set_bUnion_of_mem b'w,
      have : ((ball x (r x) \ k) ‚à© k).nonempty := ab.mono (inter_subset_inter (ad.trans hd) b'k),
      simpa only [diff_inter_self, not_nonempty_empty] },
    let b'' : {a // a ‚àâ w} := ‚ü®b', b'_notmem_w‚ü©,
    -- since `a` and `b` have comparable diameters, it follows that `z` belongs to the
    -- enlargement of `b`
    have zb : z ‚àà closed_ball (y b) (3 * diam b),
    { rcases ab with ‚ü®e, ‚ü®ea, eb‚ü©‚ü©,
      have A : dist z e ‚â§ diam a := dist_le_diam_of_mem (bounded_closed_ball.mono ad) za ea,
      have B : dist e (y b) ‚â§ diam b,
      { rcases (ut' bu).2 with ‚ü®c, hc‚ü©,
        apply dist_le_diam_of_mem (bounded_closed_ball.mono hc) eb (hy b (ut bu)).1 },
      simp only [mem_closed_ball],
      linarith [dist_triangle z e (y b)] },
    suffices H : closed_ball (y (b'' : set Œ±)) (3 * diam (b'' : set Œ±))
      ‚äÜ ‚ãÉ (a : {a // a ‚àâ w}), closed_ball (y (a : set Œ±)) (3 * diam (a : set Œ±)), from H zb,
    exact subset_Union (Œª (a : {a // a ‚àâ w}), closed_ball (y a) (3 * diam (a : set Œ±))) b'' },
  -- now that we have proved our main inclusion, we can use it to estimate the measure of the points
  -- in `ball x (r x)` not covered by `u`.
  haveI : encodable v := (u_count.mono vu).to_encodable,
  calc Œº ((s \ ‚ãÉ (a : set Œ±) (H : a ‚àà u), a) ‚à© ball x (r x))
      ‚â§ Œº (‚ãÉ (a : {a // a ‚àâ w}), closed_ball (y a) (3 * diam (a : set Œ±))) : measure_mono M
  ... ‚â§ ‚àë' (a : {a // a ‚àâ w}), Œº (closed_ball (y a) (3 * diam (a : set Œ±))) : measure_Union_le _
  ... ‚â§ ‚àë' (a : {a // a ‚àâ w}), C * Œº a : ennreal.tsum_le_tsum (Œª a, (hy a (ut (vu a.1.2))).2)
  ... = C * ‚àë' (a : {a // a ‚àâ w}), Œº a : ennreal.tsum_mul_left
  ... ‚â§ C * (Œµ / C) : ennreal.mul_le_mul le_rfl hw.le
  ... ‚â§ Œµ : ennreal.mul_div_le,
end

/-- Assume that around every point there are arbitrarily small scales at which the measure is
doubling. Then the set of closed sets `a` with nonempty interior covering a fixed proportion `1/C`
of the ball `closed_ball x (3 * diam a)` forms a Vitali family. This is essentially a restatement
of the measurable Vitali theorem. -/
protected def vitali_family [metric_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±]
  [second_countable_topology Œ±] (Œº : measure Œ±) [is_locally_finite_measure Œº] (C : ‚Ñù‚â•0)
  (h : ‚àÄ x (Œµ > 0), ‚àÉ r ‚àà Ioc (0 : ‚Ñù) Œµ, Œº (closed_ball x (6 * r)) ‚â§ C * Œº (closed_ball x r)) :
  vitali_family Œº :=
{ sets_at := Œª x, {a | x ‚àà a ‚àß is_closed a ‚àß (interior a).nonempty ‚àß
                      Œº (closed_ball x (3 * diam a)) ‚â§ C * Œº a},
  measurable_set' := Œª x a ha, ha.2.1.measurable_set,
  nonempty_interior := Œª x a ha, ha.2.2.1,
  nontrivial := Œª x Œµ Œµpos, begin
    obtain ‚ü®r, ‚ü®rpos, rŒµ‚ü©, Œºr‚ü© : ‚àÉ r ‚àà Ioc (0 : ‚Ñù) Œµ,
      Œº (closed_ball x (6 * r)) ‚â§ C * Œº (closed_ball x r) := h x Œµ Œµpos,
    refine ‚ü®closed_ball x r, ‚ü®_, is_closed_ball, _, _‚ü©, closed_ball_subset_closed_ball rŒµ‚ü©,
    { simp only [rpos.le, mem_closed_ball, dist_self] },
    { exact (nonempty_ball.2 rpos).mono (ball_subset_interior_closed_ball) },
    { apply le_trans (measure_mono (closed_ball_subset_closed_ball _)) Œºr,
      have : diam (closed_ball x r) ‚â§ 2 * r := diam_closed_ball rpos.le,
      linarith }
  end,
  covering := begin
    assume s f fsubset ffine,
    rcases eq_empty_or_nonempty s with rfl|H,
    { refine ‚ü®‚àÖ, Œª _, ‚àÖ, by simp, by simp‚ü© },
    haveI : inhabited Œ±, { choose x hx using H, exact ‚ü®x‚ü© },
    let t := ‚ãÉ (x ‚àà s), f x,
    have A‚ÇÅ : ‚àÄ x ‚àà s, ‚àÄ (Œµ : ‚Ñù), 0 < Œµ ‚Üí (‚àÉ a ‚àà t, x ‚àà a ‚àß a ‚äÜ closed_ball x Œµ),
    { assume x xs Œµ Œµpos,
      rcases ffine x xs Œµ Œµpos with ‚ü®a, xa, hax‚ü©,
      exact ‚ü®a, mem_bUnion xs xa, (fsubset x xs xa).1, hax‚ü© },
    have A‚ÇÇ : ‚àÄ a ‚àà t, (interior a).nonempty,
    { rintros a ha,
      rcases mem_bUnion_iff.1 ha with ‚ü®x, xs, xa‚ü©,
      exact (fsubset x xs xa).2.2.1 },
    have A‚ÇÉ : ‚àÄ a ‚àà t, is_closed a,
    { rintros a ha,
      rcases mem_bUnion_iff.1 ha with ‚ü®x, xs, xa‚ü©,
      exact (fsubset x xs xa).2.1 },
    have A‚ÇÑ : ‚àÄ a ‚àà t, ‚àÉ x ‚àà a, Œº (closed_ball x (3 * diam a)) ‚â§ C * Œº a,
    { rintros a ha,
      rcases mem_bUnion_iff.1 ha with ‚ü®x, xs, xa‚ü©,
      exact ‚ü®x, (fsubset x xs xa).1, (fsubset x xs xa).2.2.2‚ü© },
    obtain ‚ü®u, ut, u_count, u_disj, Œºu‚ü© :
      ‚àÉ u ‚äÜ t, u.countable ‚àß u.pairwise_on (disjoint on id) ‚àß Œº (s \ ‚ãÉ a ‚àà u, a) = 0 :=
        exists_disjoint_covering_ae Œº s t A‚ÇÅ A‚ÇÇ A‚ÇÉ C A‚ÇÑ,
    have : ‚àÄ a ‚àà u, ‚àÉ x ‚àà s, a ‚àà f x := Œª a ha, mem_bUnion_iff.1 (ut ha),
    choose! x hx using this,
    have inj_on_x : inj_on x u,
    { assume a ha b hb hab,
      have A : (a ‚à© b).nonempty,
      { refine ‚ü®x a, mem_inter ((fsubset _ (hx a ha).1 (hx a ha).2).1) _‚ü©,
        rw hab,
        exact (fsubset _ (hx b hb).1 (hx b hb).2).1 },
      contrapose A,
      have : disjoint a b := u_disj a ha b hb A,
      simpa only [‚Üê not_disjoint_iff_nonempty_inter] },
    refine ‚ü®x '' u, function.inv_fun_on x u, _, _, _, _‚ü©,
    { assume y hy,
      rcases (mem_image _ _ _).1 hy with ‚ü®a, au, rfl‚ü©,
      exact (hx a au).1 },
    { rw [inj_on_x.pairwise_on_image],
      assume a ha b hb hab,
      simp only [function.on_fun, function.inv_fun_on_eq' inj_on_x, ha, hb],
      exact u_disj a ha b hb hab },
    { assume y hy,
      rcases (mem_image _ _ _).1 hy with ‚ü®a, ha, rfl‚ü©,
      rw function.inv_fun_on_eq' inj_on_x ha,
      exact (hx a ha).2 },
    { rw [bUnion_image],
      convert Œºu using 3,
      exact bUnion_congr (Œª a ha, function.inv_fun_on_eq' inj_on_x ha) }
  end }

end vitali
