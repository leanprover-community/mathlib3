/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import measure_theory.measure.measure_space

/-!
# Vitali families

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

On a metric space `X` with a measure `Î¼`, consider for each `x : X` a family of measurable sets with
nonempty interiors, called `sets_at x`. This family is a Vitali family if it satisfies the following
property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `sets_at x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.

This file gives the basic definition of Vitali families. More interesting developments of this
notion are deferred to other files:
* constructions of specific Vitali families are provided by the Besicovitch covering theorem, in
`besicovitch.vitali_family`, and by the Vitali covering theorem, in `vitali.vitali_family`.
* The main theorem on differentiation of measures along a Vitali family is proved in
`vitali_family.ae_tendsto_rn_deriv`.

## Main definitions

* `vitali_family Î¼` is a structure made, for each `x : X`, of a family of sets around `x`, such that
one can extract an almost everywhere disjoint covering from any subfamily containing sets of
arbitrarily small diameters.

Let `v` be such a Vitali family.
* `v.fine_subfamily_on` describes the subfamilies of `v` from which one can extract almost
everywhere disjoint coverings. This property, called
`v.fine_subfamily_on.exists_disjoint_covering_ae`, is essentially a restatement of the definition
of a Vitali family. We also provide an API to use efficiently such a disjoint covering.
* `v.filter_at x` is a filter on sets of `X`, such that convergence with respect to this filter
means convergence when sets in the Vitali family shrink towards `x`.

## References

* [Herbert Federer, Geometric Measure Theory, Chapter 2.8][Federer1996] (Vitali families are called
Vitali relations there)
-/

open measure_theory metric set filter topological_space measure_theory.measure
open_locale filter measure_theory topology

variables {Î± : Type*} [metric_space Î±]

/-- On a metric space `X` with a measure `Î¼`, consider for each `x : X` a family of measurable sets
with nonempty interiors, called `sets_at x`. This family is a Vitali family if it satisfies the
following property: consider a (possibly non-measurable) set `s`, and for any `x` in `s` a
subfamily `f x` of `sets_at x` containing sets of arbitrarily small diameter. Then one can extract
a disjoint subfamily covering almost all `s`.

Vitali families are provided by covering theorems such as the Besicovitch covering theorem or the
Vitali covering theorem. They make it possible to formulate general versions of theorems on
differentiations of measure that apply in both contexts.
-/
@[nolint has_nonempty_instance]
structure vitali_family {m : measurable_space Î±} (Î¼ : measure Î±) :=
(sets_at : Î  (x : Î±), set (set Î±))
(measurable_set' : âˆ€ (x : Î±), âˆ€ (a : set Î±), a âˆˆ sets_at x â†’ measurable_set a)
(nonempty_interior : âˆ€ (x : Î±), âˆ€ (y : set Î±), y âˆˆ sets_at x â†’ (interior y).nonempty)
(nontrivial : âˆ€ (x : Î±) (Îµ > (0 : â„)), âˆƒ y âˆˆ sets_at x, y âŠ† closed_ball x Îµ)
(covering : âˆ€ (s : set Î±) (f : Î  (x : Î±), set (set Î±)), (âˆ€ x âˆˆ s, f x âŠ† sets_at x) â†’
  (âˆ€ (x âˆˆ s) (Îµ > (0 : â„)), âˆƒ a âˆˆ f x, a âŠ† closed_ball x Îµ) â†’ âˆƒ (t : set (Î± Ã— set Î±)),
    (âˆ€ (p : Î± Ã— set Î±), p âˆˆ t â†’ p.1 âˆˆ s) âˆ§ t.pairwise_disjoint (Î» p, p.2) âˆ§
    (âˆ€ (p : Î± Ã— set Î±), p âˆˆ t â†’ p.2 âˆˆ f p.1) âˆ§ Î¼ (s \ â‹ƒ (p : Î± Ã— set Î±) (hp : p âˆˆ t), p.2) = 0)

namespace vitali_family

variables {m0 : measurable_space Î±} {Î¼ : measure Î±}
include Î¼

/-- A Vitali family for a measure `Î¼` is also a Vitali family for any measure absolutely continuous
with respect to `Î¼`. -/
def mono (v : vitali_family Î¼) (Î½ : measure Î±) (hÎ½ : Î½ â‰ª Î¼) :
  vitali_family Î½ :=
{ sets_at := v.sets_at,
  measurable_set' := v.measurable_set',
  nonempty_interior := v.nonempty_interior,
  nontrivial := v.nontrivial,
  covering := Î» s f h h', begin
    rcases v.covering s f h h' with âŸ¨t, ts, disj, mem_f, hÎ¼âŸ©,
    exact âŸ¨t, ts, disj, mem_f, hÎ½ hÎ¼âŸ©
  end }

/-- Given a Vitali family `v` for a measure `Î¼`, a family `f` is a fine subfamily on a set `s` if
every point `x` in `s` belongs to arbitrarily small sets in `v.sets_at x âˆ© f x`. This is precisely
the subfamilies for which the Vitali family definition ensures that one can extract a disjoint
covering of almost all `s`. -/
def fine_subfamily_on (v : vitali_family Î¼) (f : Î± â†’ set (set Î±)) (s : set Î±) : Prop :=
âˆ€ x âˆˆ s, âˆ€ (Îµ > 0), âˆƒ a âˆˆ v.sets_at x âˆ© f x, a âŠ† closed_ball x Îµ

namespace fine_subfamily_on

variables {v : vitali_family Î¼} {f : Î± â†’ set (set Î±)} {s : set Î±} (h : v.fine_subfamily_on f s)
include h

theorem exists_disjoint_covering_ae :
  âˆƒ (t : set (Î± Ã— set Î±)), (âˆ€ (p : Î± Ã— set Î±), p âˆˆ t â†’ p.1 âˆˆ s) âˆ§
  t.pairwise_disjoint (Î» p, p.2) âˆ§
  (âˆ€ (p : Î± Ã— set Î±), p âˆˆ t â†’ p.2 âˆˆ v.sets_at p.1 âˆ© f p.1)
  âˆ§ Î¼ (s \ â‹ƒ (p : Î± Ã— set Î±) (hp : p âˆˆ t), p.2) = 0 :=
v.covering s (Î» x, v.sets_at x âˆ© f x) (Î» x hx, inter_subset_left _ _) h

/-- Given `h : v.fine_subfamily_on f s`, then `h.index` is a set parametrizing a disjoint
covering of almost every `s`. -/
protected def index : set (Î± Ã— set Î±) :=
h.exists_disjoint_covering_ae.some

/-- Given `h : v.fine_subfamily_on f s`, then `h.covering p` is a set in the family,
for `p âˆˆ h.index`, such that these sets form a disjoint covering of almost every `s`. -/
@[nolint unused_arguments] protected def covering : Î± Ã— set Î± â†’ set Î± :=
Î» p, p.2

lemma index_subset : âˆ€ (p : Î± Ã— set Î±), p âˆˆ h.index â†’ p.1 âˆˆ s :=
h.exists_disjoint_covering_ae.some_spec.1

lemma covering_disjoint : h.index.pairwise_disjoint h.covering :=
h.exists_disjoint_covering_ae.some_spec.2.1

lemma covering_disjoint_subtype : pairwise (disjoint on (Î» x : h.index, h.covering x)) :=
(pairwise_subtype_iff_pairwise_set _ _).2 h.covering_disjoint

lemma covering_mem {p : Î± Ã— set Î±} (hp : p âˆˆ h.index) : h.covering p âˆˆ f p.1 :=
(h.exists_disjoint_covering_ae.some_spec.2.2.1 p hp).2

lemma covering_mem_family {p : Î± Ã— set Î±} (hp : p âˆˆ h.index) : h.covering p âˆˆ v.sets_at p.1 :=
(h.exists_disjoint_covering_ae.some_spec.2.2.1 p hp).1

lemma measure_diff_bUnion : Î¼ (s \ â‹ƒ p âˆˆ h.index, h.covering p) = 0 :=
h.exists_disjoint_covering_ae.some_spec.2.2.2

lemma index_countable [second_countable_topology Î±] : h.index.countable :=
h.covering_disjoint.countable_of_nonempty_interior
  (Î» x hx, v.nonempty_interior _ _ (h.covering_mem_family hx))

protected lemma measurable_set_u {p : Î± Ã— set Î±} (hp : p âˆˆ h.index) :
  measurable_set (h.covering p) :=
v.measurable_set' p.1 _ (h.covering_mem_family hp)

lemma measure_le_tsum_of_absolutely_continuous [second_countable_topology Î±]
  {Ï : measure Î±} (hÏ : Ï â‰ª Î¼) :
  Ï s â‰¤ âˆ‘' (p : h.index), Ï (h.covering p) :=
calc Ï s â‰¤ Ï ((s \ â‹ƒ (p âˆˆ h.index), h.covering p) âˆª (â‹ƒ (p âˆˆ h.index), h.covering p)) :
    measure_mono (by simp only [subset_union_left, diff_union_self])
  ... â‰¤ Ï (s \ â‹ƒ (p âˆˆ h.index), h.covering p) + Ï (â‹ƒ (p âˆˆ h.index), h.covering p) :
    measure_union_le _ _
  ... = âˆ‘' (p : h.index), Ï (h.covering p) : by rw [hÏ h.measure_diff_bUnion,
    measure_bUnion h.index_countable h.covering_disjoint (Î» x hx, h.measurable_set_u hx),
    zero_add]

lemma measure_le_tsum [second_countable_topology Î±] :
  Î¼ s â‰¤ âˆ‘' (x : h.index), Î¼ (h.covering x) :=
h.measure_le_tsum_of_absolutely_continuous measure.absolutely_continuous.rfl

end fine_subfamily_on

/-- One can enlarge a Vitali family by adding to the sets `f x` at `x` all sets which are not
contained in a `Î´`-neighborhood on `x`. This does not change the local filter at a point, but it
can be convenient to get a nicer global behavior. -/
def enlarge (v : vitali_family Î¼) (Î´ : â„) (Î´pos : 0 < Î´) : vitali_family Î¼ :=
{ sets_at := Î» x, v.sets_at x âˆª
                {a | measurable_set a âˆ§ (interior a).nonempty âˆ§ Â¬(a âŠ† closed_ball x Î´)},
  measurable_set' := Î» x a ha, by { cases ha, exacts [v.measurable_set' _ _ ha, ha.1] },
  nonempty_interior := Î» x a ha, by { cases ha, exacts [v.nonempty_interior _ _ ha, ha.2.1] },
  nontrivial := begin
    assume x Îµ Îµpos,
    rcases v.nontrivial x Îµ Îµpos with âŸ¨a, ha, h'aâŸ©,
    exact âŸ¨a, mem_union_left _ ha, h'aâŸ©,
  end,
  covering := begin
    assume s f fset ffine,
    let g : Î± â†’ set (set Î±) := Î» x, f x âˆ© v.sets_at x,
    have : âˆ€ x âˆˆ s, âˆ€ (Îµ : â„), Îµ > 0 â†’ (âˆƒ (a : set Î±) (H : a âˆˆ g x), a âŠ† closed_ball x Îµ),
    { assume x hx Îµ Îµpos,
      obtain âŸ¨a, af, haâŸ© : âˆƒ a âˆˆ f x, a âŠ† closed_ball x (min Îµ Î´),
        from ffine x hx (min Îµ Î´) (lt_min Îµpos Î´pos),
      rcases fset x hx af with h'a|h'a,
      { exact âŸ¨a, âŸ¨af, h'aâŸ©, ha.trans (closed_ball_subset_closed_ball (min_le_left _ _))âŸ© },
      { refine false.elim (h'a.2.2 _),
        exact ha.trans (closed_ball_subset_closed_ball (min_le_right _ _)) } },
    rcases v.covering s g (Î» x hx, inter_subset_right _ _) this with âŸ¨t, ts, tdisj, tg, Î¼tâŸ©,
    exact âŸ¨t, ts, tdisj, Î» p hp, (tg p hp).1, Î¼tâŸ©,
  end }

variable (v : vitali_family Î¼)
include v

/-- Given a vitali family `v`, then `v.filter_at x` is the filter on `set Î±` made of those families
that contain all sets of `v.sets_at x` of a sufficiently small diameter. This filter makes it
possible to express limiting behavior when sets in `v.sets_at x` shrink to `x`. -/
def filter_at (x : Î±) : filter (set Î±) :=
â¨… (Îµ âˆˆ Ioi (0 : â„)), ğ“Ÿ {a âˆˆ v.sets_at x | a âŠ† closed_ball x Îµ}

lemma mem_filter_at_iff {x : Î±} {s : set (set Î±)} :
  (s âˆˆ v.filter_at x) â†” âˆƒ (Îµ > (0 : â„)), âˆ€ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ â†’ a âˆˆ s :=
begin
  simp only [filter_at, exists_prop, gt_iff_lt],
  rw mem_binfi_of_directed,
  { simp only [subset_def, and_imp, exists_prop, mem_sep_iff, mem_Ioi, mem_principal] },
  { simp only [directed_on, exists_prop, ge_iff_le, le_principal_iff, mem_Ioi, order.preimage,
      mem_principal],
    assume x hx y hy,
    refine âŸ¨min x y, lt_min hx hy,
      Î» a ha, âŸ¨ha.1, ha.2.trans (closed_ball_subset_closed_ball (min_le_left _ _))âŸ©,
      Î» a ha, âŸ¨ha.1, ha.2.trans (closed_ball_subset_closed_ball (min_le_right _ _))âŸ©âŸ© },
  { exact âŸ¨(1 : â„), mem_Ioi.2 zero_lt_oneâŸ© }
end

instance filter_at_ne_bot (x : Î±) : (v.filter_at x).ne_bot :=
begin
  simp only [ne_bot_iff, â†empty_mem_iff_bot, mem_filter_at_iff, not_exists, exists_prop,
    mem_empty_iff_false, and_true, gt_iff_lt, not_and, ne.def, not_false_iff, not_forall],
  assume Îµ Îµpos,
  obtain âŸ¨w, w_sets, hwâŸ© : âˆƒ (w âˆˆ v.sets_at x), w âŠ† closed_ball x Îµ := v.nontrivial x Îµ Îµpos,
  exact âŸ¨w, w_sets, hwâŸ©
end

lemma eventually_filter_at_iff {x : Î±} {P : set Î± â†’ Prop} :
  (âˆ€á¶  a in v.filter_at x, P a) â†” âˆƒ (Îµ > (0 : â„)), âˆ€ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ â†’ P a :=
v.mem_filter_at_iff

lemma eventually_filter_at_mem_sets (x : Î±) :
  âˆ€á¶  a in v.filter_at x, a âˆˆ v.sets_at x :=
begin
  simp only [eventually_filter_at_iff, exists_prop, and_true, gt_iff_lt,
             implies_true_iff] {contextual := tt},
  exact âŸ¨1, zero_lt_oneâŸ©
end

lemma eventually_filter_at_subset_closed_ball (x : Î±) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆ€á¶  (a : set Î±) in v.filter_at x, a âŠ† closed_ball x Îµ :=
begin
  simp only [v.eventually_filter_at_iff],
  exact âŸ¨Îµ, hÎµ, Î» a ha ha', ha'âŸ©,
end

lemma tendsto_filter_at_iff {Î¹ : Type*} {l : filter Î¹} {f : Î¹ â†’ set Î±} {x : Î±} :
  tendsto f l (v.filter_at x) â†”
  (âˆ€á¶  i in l, f i âˆˆ v.sets_at x) âˆ§ (âˆ€ (Îµ > (0 : â„)), âˆ€á¶  i in l, f i âŠ† closed_ball x Îµ) :=
begin
  refine âŸ¨Î» H,
    âŸ¨H.eventually $ v.eventually_filter_at_mem_sets x,
     Î» Îµ hÎµ, H.eventually $ v.eventually_filter_at_subset_closed_ball x hÎµâŸ©,
    Î» H s hs, (_ : âˆ€á¶  i in l, f i âˆˆ s)âŸ©,
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© := v.mem_filter_at_iff.mp hs,
  filter_upwards [H.1, H.2 Îµ Îµpos] with i hi hiÎµ using hÎµ _ hi hiÎµ,
end

lemma eventually_filter_at_measurable_set (x : Î±) :
  âˆ€á¶  a in v.filter_at x, measurable_set a :=
by { filter_upwards [v.eventually_filter_at_mem_sets x] with _ ha using v.measurable_set' _ _ ha }

lemma frequently_filter_at_iff {x : Î±} {P : set Î± â†’ Prop} :
  (âˆƒá¶  a in v.filter_at x, P a) â†” âˆ€ (Îµ > (0 : â„)), âˆƒ a âˆˆ v.sets_at x, a âŠ† closed_ball x Îµ âˆ§ P a :=
by simp only [filter.frequently, eventually_filter_at_iff, not_exists, exists_prop, not_and,
  not_not, not_forall]

lemma eventually_filter_at_subset_of_nhds {x : Î±} {o : set Î±} (hx : o âˆˆ ğ“ x) :
  âˆ€á¶  a in v.filter_at x, a âŠ† o :=
begin
  rw eventually_filter_at_iff,
  rcases metric.mem_nhds_iff.1 hx with âŸ¨Îµ, Îµpos, hÎµâŸ©,
  exact âŸ¨Îµ/2, half_pos Îµpos,
    Î» a av ha, ha.trans ((closed_ball_subset_ball (half_lt_self Îµpos)).trans hÎµ)âŸ©
end

lemma fine_subfamily_on_of_frequently (v : vitali_family Î¼) (f : Î± â†’ set (set Î±)) (s : set Î±)
  (h : âˆ€ x âˆˆ s, âˆƒá¶  a in v.filter_at x, a âˆˆ f x) :
  v.fine_subfamily_on f s :=
begin
  assume x hx Îµ Îµpos,
  obtain âŸ¨a, av, ha, afâŸ© : âˆƒ (a : set Î±) (H : a âˆˆ v.sets_at x), a âŠ† closed_ball x Îµ âˆ§ a âˆˆ f x :=
    v.frequently_filter_at_iff.1 (h x hx) Îµ Îµpos,
  exact âŸ¨a, âŸ¨av, afâŸ©, haâŸ©,
end

end vitali_family
