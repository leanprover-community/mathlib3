/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.vector_measure
import order.symm_diff

/-!
# Hahn decomposition

This file defines positive and negative sets with respect to a vector measure and
prove the Hahn decomposition theorem (signed version).

## Main definitions

* `vector_measure.positive` : a set `i` is positive with respect to a vector measure `v` if `v`
  restricted on `i` is non-negative, i.e. `0 â‰¤ v.restrict i`.
* `vector_measure.negative` : a set `i` is negative with respect to a vector measure `v` if `v`
  restricted on `i` is non-positive, i.e. `v.restrict i â‰¤ 0`.

## Main results

* `signed_measure.exists_disjoint_positive_negative_union_eq` : the Hahn decomposition theorem.
* `signed_measure.exists_negative_set` : A measurable set of negative measure contains at least
  one negative subset.

## Tags

positive, negative, Hahn decomposition theorem
-/

noncomputable theory
open_locale classical big_operators nnreal ennreal

section lemmas

variables {Î± : Type*}

open_locale topological_space

open set filter topological_space

-- from #8266
lemma strict_mono_of_strict_mono_nat [preorder Î±] {f : â„• â†’ Î±} (hf : âˆ€n, f n < f (n + 1)) :
  strict_mono f | n m h :=
begin
  induction h,
  { exact hf _ },
  { transitivity, assumption, exact hf _ }
end

variables [conditionally_complete_linear_order Î±] [topological_space Î±] [order_topology Î±]
  [first_countable_topology Î±]

lemma exists_seq_tendsto_Sup {S : set Î±} (hS : S.nonempty) (hS' : bdd_above S) :
  âˆƒ (u : â„• â†’ Î±) (hu : âˆ€ n, u n âˆˆ S), tendsto u at_top (ð“ (Sup S)) :=
begin
  by_cases hSup : Sup S âˆˆ S,
  { exact âŸ¨Î» n, Sup S, Î» _, hSup, tendsto_const_nhdsâŸ© },
  { have hlt : âˆ€ s âˆˆ S, s < Sup S,
    { exact Î» s hs, lt_of_le_of_ne (le_cSup hS' hs) (Î» h, hSup (h â–¸ hs)) },
    obtain âŸ¨s, hsâŸ© : âˆƒ s : â„• â†’ set Î±, (ð“ (Sup S)).has_basis (Î» (_x : â„•), true) s :=
      let âŸ¨s, hsâŸ© := (is_countably_generated_nhds (Sup S)).exists_antimono_basis in
        âŸ¨s, hs.to_has_basisâŸ©,
    have : âˆ€ n k, k âˆˆ S â†’ âˆƒ y âˆˆ S, Ico y (Sup S) âŠ† s n âˆ§ k < y,
    { intros n k hk,
      obtain âŸ¨L, hL, hâŸ© : âˆƒ (L : Î±) (hL : L âˆˆ Ico k (Sup S)), Ioc L (Sup S) âŠ† s n :=
        exists_Ioc_subset_of_mem_nhds' (hs.mem_of_mem trivial) (hlt _ hk),
      obtain âŸ¨y, hyâŸ© : âˆƒ y âˆˆ S, L < y := exists_lt_of_lt_cSup hS hL.2,
      exact âŸ¨y, (exists_prop.1 hy).1, Î» z hz, h âŸ¨(exists_prop.1 hy).2.trans_le hz.1,
              le_of_lt hz.2âŸ©, hL.1.trans_lt (exists_prop.1 hy).2âŸ© },
    choose! f hf using this,
    obtain âŸ¨t, htâŸ© := hS,
    let u : â„• â†’ Î± := Î» n, nat.rec_on n (f 0 t) (Î» n h, f n.succ h),
    have I : âˆ€ n, u n âˆˆ S,
    { intro n,
      induction n with n IH,
      { exact (hf 0 t ht).1 },
      { exact (hf n.succ _ IH).1 } },
    have hmono : strict_mono u := strict_mono_of_strict_mono_nat (Î» n, (hf n.succ _ (I n)).2.2),
    refine âŸ¨u, I, hs.tendsto_right_iff.2 (Î» n _, _)âŸ©,
    simp only [ge_iff_le, eventually_at_top],
    refine âŸ¨n, Î» p hp, _âŸ©,
    have up : u p âˆˆ Icc (u n) (Sup S) := âŸ¨hmono.monotone hp, le_cSup hS' (I p)âŸ©,
    have : Ico (u n) (Sup S) âŠ† s n,
      by { cases n, { exact (hf 0 t ht).2.1 }, { exact (hf n.succ (u n) (I n)).2.1 } },
    exact this âŸ¨up.1, lt_of_le_of_ne up.2 (Î» h, hSup (h â–¸ I p))âŸ© }
end

lemma exists_seq_tendsto_Inf {S : set Î±} (hS : S.nonempty) (hS' : bdd_below S) :
  âˆƒ (u : â„• â†’ Î±) (hu : âˆ€ n, u n âˆˆ S), tendsto u at_top (ð“ (Inf S)) :=
begin
  haveI : first_countable_topology (order_dual Î±) := sorry,
  exact @exists_seq_tendsto_Sup (order_dual Î±) _ _ _ _ _ hS hS',
end

end lemmas

variables {Î± Î² : Type*} [measurable_space Î±]
variables {M : Type*} [add_comm_monoid M] [topological_space M] [ordered_add_comm_monoid M]

namespace measure_theory

namespace vector_measure

/-- A set `i` is positive with respect to a vector measure `v` if `v` restricted
on`i` is non-negative, i.e. `0 â‰¤ v.restrict i`. -/
def positive (v : vector_measure Î± M) (i : set Î±) : Prop :=
0 â‰¤ v.restrict i

lemma positive_iff {v : vector_measure Î± M} {i : set Î±} (hi : measurable_set i) :
  v.positive i â†” âˆ€ â¦ƒjâ¦„, measurable_set j â†’ j âŠ† i â†’ 0 â‰¤ v j :=
begin
  convert restrict_le_restrict_iff 0 v hi,
  rw restrict_zero, refl,
end

/-- A set `i` is negative with respect to a vector measure `v` if `v` restricted
on`i` is non-positive, i.e. `v.restrict i â‰¤ 0`. -/
def negative (v : vector_measure Î± M) (i : set Î±) : Prop :=
v.restrict i â‰¤ 0

lemma negative_iff {v : vector_measure Î± M} {i : set Î±} (hi : measurable_set i) :
  v.negative i â†” âˆ€ â¦ƒjâ¦„, measurable_set j â†’ j âŠ† i â†’ v j â‰¤ 0 :=
begin
  convert restrict_le_restrict_iff v 0 hi,
  rw restrict_zero, refl,
end

variables {v : signed_measure Î±} {i j : set Î±}

lemma empty_positive : v.positive âˆ… :=
begin
  intros j hj,
  rw restrict_empty,
end

lemma empty_negative : v.negative âˆ… :=
begin
  intros j hj,
  rw restrict_empty,
end

lemma positive_nonneg_measure (hiâ‚‚ : v.positive i) :
  0 â‰¤ v i :=
begin
  by_cases hiâ‚ : measurable_set i,
  { exact (positive_iff hiâ‚).1 hiâ‚‚ hiâ‚ set.subset.rfl },
  { rw v.not_measurable hiâ‚ },
end

lemma negative_nonpos_measure (hiâ‚‚ : v.negative i) :
  v i â‰¤ 0 :=
begin
  by_cases hiâ‚ : measurable_set i,
  { exact (negative_iff hiâ‚).1 hiâ‚‚ hiâ‚ set.subset.rfl },
  { rw v.not_measurable hiâ‚ }
end

lemma positive_of_not_measurable (hi : Â¬ measurable_set i) :
  v.positive i :=
begin
  rw [positive, restrict, dif_neg hi],
  exact le_refl _,
end

lemma negative_of_not_measurable (hi : Â¬ measurable_set i) :
  v.negative i :=
begin
  rw [negative, restrict, dif_neg hi],
  exact le_refl _,
end

/-- This lemma is similar to the forward direction of `vector_measure.positive_iff` except it
does not have a measurability condition on `j`.

This is useful when we are using the property that a set is positive. -/
lemma subset_nonneg_of_positive {i : set Î±}
  (hi : measurable_set i) (hiâ‚‚ : v.positive i) {j : set Î±} (hj : j âŠ† i) : 0 â‰¤ v j :=
begin
  rw â† zero_apply j,
  refine subset_le_of_restrict_le_restrict 0 v hi _ hj,
  rwa restrict_zero
end

/-- This lemma is similar to the backward direction of `vector_measure.positive_iff` except it
does not require `i` to be measurable.

This is useful when we are proving a set is positive. -/
lemma positive_of_subset_nonneg {i : set Î±}
  (h : âˆ€ â¦ƒjâ¦„, measurable_set j â†’ j âŠ† i â†’ 0 â‰¤ v j) : v.positive i :=
Î» j hj, (@restrict_zero Î± _ â„ _ _ i) â–¸ restrict_le_restrict_of_subset_le 0 v h _ hj

/-- This lemma is similar to the forward direction of `vector_measure.negative_iff` except it
does not have a measurability condition on `j`.

This is useful when we are using the property that a set is negative. -/
lemma subset_nonpos_of_negative {i : set Î±}
  (hi : measurable_set i) (hiâ‚‚ : v.negative i) {j : set Î±} (hj : j âŠ† i) : v j â‰¤ 0 :=
begin
  rw â† zero_apply j,
  refine subset_le_of_restrict_le_restrict v 0 hi _ hj,
  rwa restrict_zero
end

/-- This lemma is similar to the backward direction of `vector_measure.negative_iff` except it
does not require `i` to be measurable.

This is useful when we are proving a set is negative. -/
lemma negative_of_subset_nonpos {i : set Î±}
  (h : âˆ€ â¦ƒjâ¦„, measurable_set j â†’ j âŠ† i â†’ v j â‰¤ 0) : v.negative i :=
Î» j hj, (@restrict_zero Î± _ â„ _ _ i) â–¸ restrict_le_restrict_of_subset_le v 0 h _ hj

lemma measurable_of_not_positive (hi : Â¬ v.positive i) : measurable_set i :=
not.imp_symm positive_of_not_measurable hi

lemma measurable_of_not_negative (hi : Â¬ v.negative i) : measurable_set i :=
not.imp_symm negative_of_not_measurable hi

lemma positive_subset_positive (hiâ‚ : measurable_set i) (hiâ‚‚ : v.positive i) (hij : j âŠ† i) :
  v.positive j :=
positive_of_subset_nonneg (Î» k hkâ‚ hkâ‚‚, (positive_iff hiâ‚).1 hiâ‚‚ hkâ‚ (set.subset.trans hkâ‚‚ hij))

lemma negative_subset_negative (hiâ‚ : measurable_set i) (hiâ‚‚ : v.negative i) (hij : j âŠ† i) :
  v.negative j :=
negative_of_subset_nonpos (Î» k hkâ‚ hkâ‚‚, (negative_iff hiâ‚).1 hiâ‚‚ hkâ‚ (set.subset.trans hkâ‚‚ hij))

lemma not_positive_subset (hi : Â¬ v.positive i) (h : i âŠ† j) (hj : measurable_set j) :
  Â¬ v.positive j :=
Î» h', hi $ positive_subset_positive hj h' h

lemma not_negative_subset (hi : Â¬ v.negative i) (h : i âŠ† j) (hj : measurable_set j) :
  Â¬ v.negative j :=
Î» h', hi $ negative_subset_negative hj h' h

lemma positive_union_positive
  (hiâ‚ : measurable_set i) (hiâ‚‚ : v.positive i)
  (hjâ‚ : measurable_set j) (hjâ‚‚ : v.positive j) : v.positive (i âˆª j) :=
begin
  refine positive_of_subset_nonneg (Î» a haâ‚ haâ‚‚, _),
  have hâ‚ := haâ‚.inter hiâ‚,
  have : a âˆ© i âˆª a âˆ© j \ (a âˆ© i) = a,
    { rwa [set.union_diff_self, â† set.inter_union_distrib_left, set.inter_eq_left_iff_subset] },
  rw [â† this, of_union disjoint_sdiff_self_right hâ‚ ((haâ‚.inter hjâ‚).diff hâ‚)],
  refine add_nonneg ((positive_iff hiâ‚).1 hiâ‚‚ hâ‚ (a.inter_subset_right i)) _,
  exact (positive_iff hjâ‚).1 hjâ‚‚ ((haâ‚.inter hjâ‚).diff hâ‚)
    (set.subset.trans ((a âˆ© j).diff_subset (a âˆ© i)) (a.inter_subset_right j)),
  apply_instance
end

lemma positive_Union_positive {f : â„• â†’ set Î±}
  (hfâ‚ : âˆ€ n, measurable_set (f n)) (hfâ‚‚ : âˆ€ n, v.positive (f n)) :
  v.positive â‹ƒ n, f n :=
begin
  convert restrict_le_restrict_Union 0 v hfâ‚ _,
  { rw restrict_zero, refl },
  { intro n,
    rw restrict_zero,
    exact hfâ‚‚ n }
end

lemma negative_union_negative
  (hiâ‚ : measurable_set i) (hiâ‚‚ : v.negative i)
  (hjâ‚ : measurable_set j) (hjâ‚‚ : v.negative j) : v.negative (i âˆª j) :=
begin
  refine negative_of_subset_nonpos (Î» a haâ‚ haâ‚‚, _),
  have hâ‚ := haâ‚.inter hiâ‚,
  have : a âˆ© i âˆª a âˆ© j \ (a âˆ© i) = a,
    { rwa [set.union_diff_self, â† set.inter_union_distrib_left, set.inter_eq_left_iff_subset] },
  rw [â† this, of_union disjoint_sdiff_self_right hâ‚ ((haâ‚.inter hjâ‚).diff hâ‚)],
  refine add_nonpos ((negative_iff hiâ‚).1 hiâ‚‚ hâ‚ (a.inter_subset_right i)) _,
  exact (negative_iff hjâ‚).1 hjâ‚‚ ((haâ‚.inter hjâ‚).diff hâ‚)
    (set.subset.trans ((a âˆ© j).diff_subset (a âˆ© i)) (a.inter_subset_right j)),
  apply_instance
end

lemma negative_Union_negative {f : â„• â†’ set Î±}
  (hfâ‚ : âˆ€ n, measurable_set (f n)) (hfâ‚‚ : âˆ€ n, v.negative (f n)) :
  v.negative â‹ƒ n, f n :=
begin
  convert restrict_le_restrict_Union v 0 hfâ‚ _,
  { rw restrict_zero, refl },
  { intro n,
    rw restrict_zero,
    exact hfâ‚‚ n }
end

lemma exists_pos_measure_of_not_negative (hi : Â¬ v.negative i) :
  âˆƒ (j : set Î±) (hjâ‚ : measurable_set j) (hjâ‚‚ : j âŠ† i) , 0 < v j :=
begin
  have hiâ‚ : measurable_set i := measurable_of_not_negative hi,
  rw [negative_iff hiâ‚] at hi,
  push_neg at hi,
  obtain âŸ¨j, hjâ‚, hjâ‚‚, hjâŸ© := hi,
  exact âŸ¨j, hjâ‚, hjâ‚‚, hjâŸ©,
end

end vector_measure

namespace signed_measure

open filter vector_measure

variables {s : signed_measure Î±} {i j : set Î±}

section exists_negative_set

private def p (s : signed_measure Î±) (i j : set Î±) (n : â„•) : Prop :=
âˆƒ (k : set Î±) (hjâ‚ : k âŠ† i \ j) (hjâ‚‚ : measurable_set k), (1 / (n + 1) : â„) < s k

private lemma exists_nat_one_div_lt_measure_of_not_negative (hi : Â¬ s.negative (i \ j)) :
  âˆƒ (n : â„•), p s i j n :=
let âŸ¨k, hjâ‚, hjâ‚‚, hjâŸ© := exists_pos_measure_of_not_negative hi in
let âŸ¨n, hnâŸ© := exists_nat_one_div_lt hj in âŸ¨n, k, hjâ‚‚, hjâ‚, hnâŸ©

private def auxâ‚€ (s : signed_measure Î±) (i j : set Î±) : â„• :=
if hi : Â¬ s.negative (i \ j) then nat.find (exists_nat_one_div_lt_measure_of_not_negative hi) else 0

private lemma auxâ‚€_spec (hi : Â¬ s.negative (i \ j)) : p s i j (auxâ‚€ s i j) :=
begin
  rw [auxâ‚€, dif_pos hi],
  convert nat.find_spec _,
end

private lemma auxâ‚€_min (hi : Â¬ s.negative (i \ j)) {m : â„•} (hm : m < auxâ‚€ s i j) :
  Â¬ p s i j m :=
begin
  rw [auxâ‚€, dif_pos hi] at hm,
  exact nat.find_min _ hm
end

private def auxâ‚ (s : signed_measure Î±) (i j : set Î±) : set Î± :=
if hi : Â¬ s.negative (i \ j) then classical.some (auxâ‚€_spec hi) else âˆ…

private lemma auxâ‚_spec (hi : Â¬ s.negative (i \ j)) :
  âˆƒ (hjâ‚ : (auxâ‚ s i j) âŠ† i \ j) (hjâ‚‚ : measurable_set (auxâ‚ s i j)),
  (1 / (auxâ‚€ s i j + 1) : â„) < s (auxâ‚ s i j) :=
begin
  rw [auxâ‚, dif_pos hi],
  exact classical.some_spec (auxâ‚€_spec hi),
end

private lemma auxâ‚_subset : auxâ‚ s i j âŠ† i \ j :=
begin
  by_cases hi : Â¬ s.negative (i \ j),
  { exact let âŸ¨h, _âŸ© := auxâ‚_spec hi in h },
  { rw [auxâ‚, dif_neg hi],
    exact set.empty_subset _ },
end

private lemma auxâ‚_subset' : auxâ‚ s i j âŠ† i :=
set.subset.trans auxâ‚_subset (set.diff_subset _ _)

private lemma auxâ‚_measurable_set : measurable_set (auxâ‚ s i j) :=
begin
  by_cases hi : Â¬ s.negative (i \ j),
  { exact let âŸ¨_, h, _âŸ© := auxâ‚_spec hi in h },
  { rw [auxâ‚, dif_neg hi],
    exact measurable_set.empty }
end

private lemma auxâ‚_lt (hi : Â¬ s.negative (i \ j)) :
  (1 / (auxâ‚€ s i j + 1) : â„) < s (auxâ‚ s i j) :=
let âŸ¨_, _, hâŸ© := auxâ‚_spec hi in h

private def aux (s : signed_measure Î±) (i : set Î±) : â„• â†’ set Î±
| 0 := auxâ‚ s i âˆ…
| (n + 1) := auxâ‚ s i â‹ƒ k â‰¤ n,
  have k < n + 1 := nat.lt_succ_iff.mpr H,
  aux k

private lemma aux_succ (n : â„•) : aux s i n.succ = auxâ‚ s i â‹ƒ k â‰¤ n, aux s i k :=
by rw aux

private lemma aux_subset (n : â„•) :
  aux s i n âŠ† i :=
begin
  cases n;
  { rw aux, exact auxâ‚_subset' }
end

private lemma aux_lt (n : â„•) (hn :Â¬ s.negative (i \ â‹ƒ l â‰¤ n, aux s i l)) :
  (1 / (auxâ‚€ s i (â‹ƒ k â‰¤ n, aux s i k) + 1) : â„) < s (aux s i n.succ) :=
begin
  rw aux_succ,
  apply auxâ‚_lt hn,
end

private lemma measure_of_aux (hiâ‚‚ : Â¬ s.negative i)
  (n : â„•) (hn : Â¬ s.negative (i \ â‹ƒ k < n, aux s i k)) :
  0 < s (aux s i n) :=
begin
  cases n,
  { rw aux, rw â† @set.diff_empty _ i at hiâ‚‚,
    rcases auxâ‚_spec hiâ‚‚ with âŸ¨_, _, hâŸ©,
    exact (lt_trans nat.one_div_pos_of_nat h) },
  { rw aux_succ,
    have hâ‚ : Â¬ s.negative (i \ â‹ƒ (k : â„•) (H : k â‰¤ n), aux s i k),
    { apply not_negative_subset hn,
      { apply set.diff_subset_diff_right,
        intros x,
        simp_rw [set.mem_Union],
        rintro âŸ¨n, hnâ‚, hnâ‚‚âŸ©,
        exact âŸ¨n, nat.lt_succ_iff.mpr hnâ‚, hnâ‚‚âŸ© },
      { convert measurable_of_not_negative hn,
        exact funext (Î» x, by rw nat.lt_succ_iff) } },
    rcases auxâ‚_spec hâ‚ with âŸ¨_, _, hâŸ©,
    exact (lt_trans nat.one_div_pos_of_nat h) }
end

private lemma aux_measurable_set (n : â„•) : measurable_set (aux s i n) :=
begin
  cases n,
  { rw aux,
    exact auxâ‚_measurable_set },
  { rw aux,
    exact auxâ‚_measurable_set }
end

private lemma aux_disjoint' {n m : â„•} (h : n < m) : aux s i n âˆ© aux s i m = âˆ… :=
begin
  rw set.eq_empty_iff_forall_not_mem,
  rintro x âŸ¨hxâ‚, hxâ‚‚âŸ©,
  cases m, linarith,
  { rw aux at hxâ‚‚,
    exact (auxâ‚_subset hxâ‚‚).2
      (set.mem_Union.2 âŸ¨n, set.mem_Union.2 âŸ¨nat.lt_succ_iff.mp h, hxâ‚âŸ©âŸ©) }
end

private lemma aux_disjoint : pairwise (disjoint on (aux s i)) :=
begin
  intros n m h,
  rcases lt_or_gt_of_ne h with (h | h),
  { intro x,
    rw [set.inf_eq_inter, aux_disjoint' h],
    exact id },
  { intro x,
    rw [set.inf_eq_inter, set.inter_comm, aux_disjoint' h],
    exact id }
end

private lemma exists_negative_set' (hiâ‚ : measurable_set i) (hiâ‚‚ : s i < 0)
  (hn : Â¬ âˆ€ n : â„•, Â¬ s.negative (i \ â‹ƒ l < n, aux s i l)) :
  âˆƒ (j : set Î±) (hjâ‚ : measurable_set j) (hjâ‚‚ : j âŠ† i), s.negative j âˆ§ s j < 0 :=
begin
  by_cases s.negative i,
  { exact âŸ¨i, hiâ‚, set.subset.refl _, h, hiâ‚‚âŸ© },
  { push_neg at hn,
    set k := nat.find hn with hkâ‚,
    have hkâ‚‚ : s.negative (i \ â‹ƒ l < k, aux s i l) := nat.find_spec hn,
    have hmeas : measurable_set (â‹ƒ (l : â„•) (H : l < k), aux s i l) :=
      (measurable_set.Union $ Î» _, measurable_set.Union_Prop (Î» _, aux_measurable_set _)),
    refine âŸ¨i \ â‹ƒ l < k, aux s i l, hiâ‚.diff hmeas, set.diff_subset _ _, hkâ‚‚, _âŸ©,
    rw [of_diff hmeas hiâ‚, s.of_disjoint_Union_nat],
    { have hâ‚ : âˆ€ l < k, 0 â‰¤ s (aux s i l),
      { intros l hl,
        refine le_of_lt (measure_of_aux h _ _),
        refine (not_negative_subset (nat.find_min hn hl) (set.subset.refl _)) (hiâ‚.diff _),
        exact (measurable_set.Union $ Î» _, measurable_set.Union_Prop (Î» _, aux_measurable_set _)) },
      suffices : 0 â‰¤ âˆ‘' (l : â„•), s (â‹ƒ (H : l < k), aux s i l),
        linarith,
      refine tsum_nonneg _,
      intro l, by_cases l < k,
      { convert hâ‚ _ h,
        ext x,
        rw [set.mem_Union, exists_prop, and_iff_right_iff_imp],
        exact Î» _, h },
      { convert le_of_eq s.empty.symm,
        ext, simp only [exists_prop, set.mem_empty_eq, set.mem_Union, not_and, iff_false],
        exact Î» h', false.elim (h h') } },
    { intro, exact measurable_set.Union_Prop (Î» _, aux_measurable_set _) },
    { intros a b hab x hx,
      simp only [exists_prop, set.mem_Union, set.mem_inter_eq, set.inf_eq_inter] at hx,
      exact let âŸ¨âŸ¨_, hxâ‚âŸ©, _, hxâ‚‚âŸ© := hx in aux_disjoint a b hab âŸ¨hxâ‚, hxâ‚‚âŸ© },
    { apply set.Union_subset,
      intros a x,
      simp only [and_imp, exists_prop, set.mem_Union],
      intros _ hx,
      exact aux_subset _ hx },
    { apply_instance } }
end .

/-- A measurable set of negative measure has a negative subset of negative measure. -/
theorem exists_negative_set (hiâ‚ : measurable_set i) (hiâ‚‚ : s i < 0) :
  âˆƒ (j : set Î±) (hjâ‚ : measurable_set j) (hjâ‚‚ : j âŠ† i), s.negative j âˆ§ s j < 0 :=
begin
  by_cases s.negative i,
  { exact âŸ¨i, hiâ‚, set.subset.refl _, h, hiâ‚‚âŸ© },
  { by_cases hn : âˆ€ n : â„•, Â¬ s.negative (i \ â‹ƒ l < n, aux s i l),
    { set A := i \ â‹ƒ l, aux s i l with hA,
      set bdd : â„• â†’ â„• := Î» n, auxâ‚€ s i (â‹ƒ k â‰¤ n, aux s i k) with hbdd,

      have hn' : âˆ€ n : â„•, Â¬ s.negative (i \ â‹ƒ l â‰¤ n, aux s i l),
      { intro n,
        convert hn (n + 1), ext l,
        simp only [exists_prop, set.mem_Union, and.congr_left_iff],
        exact Î» _, nat.lt_succ_iff.symm },
      have hâ‚ : s i = s A + âˆ‘' l, s (aux s i l),
      { rw [hA, â† s.of_disjoint_Union_nat, add_comm, of_add_of_diff],
        exact measurable_set.Union (Î» _, aux_measurable_set _),
        exacts [hiâ‚, set.Union_subset (Î» _, aux_subset _), Î» _,
                aux_measurable_set _, aux_disjoint] },
      have hâ‚‚ : s A â‰¤ s i,
      { rw hâ‚,
        apply le_add_of_nonneg_right,
        exact tsum_nonneg (Î» n, le_of_lt (measure_of_aux h _ (hn n))) },
      have hâ‚ƒ' : summable (Î» n, (1 / (bdd n + 1) : â„)),
      { have : summable (Î» l, s (aux s i l)),
        { exact has_sum.summable (s.m_Union (Î» _, aux_measurable_set _) aux_disjoint) },
        refine summable_of_nonneg_of_le _ _ (summable.comp_injective this nat.succ_injective),
        { intro _, exact le_of_lt nat.one_div_pos_of_nat },
        { intro n, exact le_of_lt (aux_lt n (hn' n)) } },
      have hâ‚ƒ : tendsto (Î» n, (bdd n : â„) + 1) at_top at_top,
      { simp only [one_div] at hâ‚ƒ',
        exact summable.tendsto_top_of_pos hâ‚ƒ' (Î» n, nat.cast_add_one_pos (bdd n)) },
      have hâ‚„ : tendsto (Î» n, (bdd n : â„)) at_top at_top,
      { convert at_top.tendsto_at_top_add_const_right (-1) hâ‚ƒ, simp },

      refine âŸ¨A, _, set.diff_subset _ _, _, _âŸ©,
      { exact hiâ‚.diff (measurable_set.Union (Î» _, aux_measurable_set _)) },
      { by_contra hnn,
        have hAâ‚ : measurable_set A := measurable_of_not_negative hnn,
        rw negative_iff hAâ‚ at hnn, push_neg at hnn,
        obtain âŸ¨E, hEâ‚, hEâ‚‚, hEâ‚ƒâŸ© := hnn,
        have : âˆƒ k, 1 â‰¤ bdd k âˆ§ 1 / (bdd k : â„) < s E,
        { rw tendsto_at_top_at_top at hâ‚„,
          obtain âŸ¨k, hkâŸ© := hâ‚„ (max (1 / s E + 1) 1),
          refine âŸ¨k, _, _âŸ©,
          { have hle := le_of_max_le_right (hk k le_rfl),
            norm_cast at hle,
            exact hle },
          { have : 1 / s E < bdd k, linarith [le_of_max_le_left (hk k le_rfl)],
            rw one_div at this âŠ¢,
            rwa inv_lt (lt_trans (inv_pos.2 hEâ‚ƒ) this) hEâ‚ƒ } },
        obtain âŸ¨k, hkâ‚, hkâ‚‚âŸ© := this,
        have hA' : A âŠ† i \ â‹ƒ l â‰¤ k, aux s i l,
        { rw hA,
          apply set.diff_subset_diff_right,
          intro x, simp only [set.mem_Union],
          rintro âŸ¨n, _, hnâ‚‚âŸ©,
          exact âŸ¨n, hnâ‚‚âŸ© },
        refine auxâ‚€_min (hn' k) (buffer.lt_aux_2 hkâ‚) âŸ¨E, set.subset.trans hEâ‚‚ hA', hEâ‚, _âŸ©,
        convert hkâ‚‚, norm_cast,
        exact nat.sub_add_cancel hkâ‚ },
      { exact lt_of_le_of_lt hâ‚‚ hiâ‚‚ } },
    { exact exists_negative_set' hiâ‚ hiâ‚‚ hn } }
end .

end exists_negative_set

/-- The set of measures of the set of measurable negative sets. -/
def measure_of_negatives (s : signed_measure Î±) : set â„ :=
  s '' { B | âˆƒ (hBâ‚ : measurable_set B), s.negative B }

lemma zero_mem_measure_of_negatives : (0 : â„) âˆˆ s.measure_of_negatives :=
âŸ¨âˆ…, âŸ¨measurable_set.empty, empty_negativeâŸ©, s.emptyâŸ©

lemma measure_of_negatives_bdd_below :
  âˆƒ x, âˆ€ y âˆˆ s.measure_of_negatives, x â‰¤ y :=
begin
  by_contra, push_neg at h,
  have h' : âˆ€ n : â„•, âˆƒ y : â„, y âˆˆ s.measure_of_negatives âˆ§ y < -n := Î» n, h (-n),
  choose f hf using h',
  have hf' : âˆ€ n : â„•, âˆƒ B âˆˆ { B | âˆƒ (hBâ‚ : measurable_set B), s.negative B }, s B < -n,
  { intro n,
    rcases hf n with âŸ¨âŸ¨B, hBâ‚, hBâ‚‚âŸ©, hltâŸ©,
    exact âŸ¨B, hBâ‚, hBâ‚‚.symm â–¸ hltâŸ© },
  choose B hB using hf',
  have hmeas : âˆ€ n, measurable_set (B n) := Î» n, let âŸ¨h, _âŸ© := (hB n).1 in h,
  set A := â‹ƒ n, B n with hA,
  have hfalse : âˆ€ n : â„•, s A â‰¤ -n,
  { intro n,
    refine le_trans _ (le_of_lt (hB n).2),
    rw [hA, â† set.diff_union_of_subset (set.subset_Union _ n),
        of_union (disjoint.comm.1 set.disjoint_diff) _ (hmeas n)],
    { refine add_le_of_nonpos_left _,
      have : s.negative A := negative_Union_negative hmeas (Î» m, let âŸ¨_, hâŸ© := (hB m).1 in h),
      refine negative_nonpos_measure (negative_subset_negative _ this (set.diff_subset _ _)),
      exact measurable_set.Union hmeas },
    { apply_instance },
    { exact (measurable_set.Union hmeas).diff (hmeas n) } },
  suffices : Â¬ âˆ€ n : â„•, s A â‰¤ -n,
  { exact this hfalse },
  push_neg,
  rcases exists_nat_gt (-(s A)) with âŸ¨n, hnâŸ©,
  exact âŸ¨n, neg_lt.1 hnâŸ©,
end

/-- **The Hahn decomposition thoerem**: Given a signed measure `s`, there exist
disjoint measurable sets `i`, `j` such that `i` is positive, `j` is negative
and `i âˆª j = set.univ`.  -/
theorem exists_disjoint_positive_negative_union_eq (s : signed_measure Î±) :
  âˆƒ (i j : set Î±) (hiâ‚ : measurable_set i) (hiâ‚‚ : s.positive i)
                  (hjâ‚ : measurable_set j) (hjâ‚‚ : s.negative j),
  disjoint i j âˆ§ i âˆª j = set.univ :=
begin
  obtain âŸ¨f, hfâ‚, hfâ‚‚âŸ© := exists_seq_tendsto_Inf
    âŸ¨0, @zero_mem_measure_of_negatives _ _ sâŸ© measure_of_negatives_bdd_below,

  choose B hB using hfâ‚,
  have hBâ‚ : âˆ€ n, measurable_set (B n) := Î» n, let âŸ¨h, _âŸ© := (hB n).1 in h,
  have hBâ‚‚ : âˆ€ n, s.negative (B n) := Î» n, let âŸ¨_, hâŸ© := (hB n).1 in h,

  set A := â‹ƒ n, B n with hA,
  have hAâ‚ : measurable_set A := measurable_set.Union hBâ‚,
  have hAâ‚‚ : s.negative A := negative_Union_negative hBâ‚ hBâ‚‚,
  have hAâ‚ƒ : s A = Inf s.measure_of_negatives,
  { apply has_le.le.antisymm,
    { refine le_of_tendsto_of_tendsto tendsto_const_nhds hfâ‚‚ (eventually_of_forall _),
      intro n,
      rw [â† (hB n).2, hA, â† set.diff_union_of_subset (set.subset_Union _ n),
      of_union (disjoint.comm.1 set.disjoint_diff) _ (hBâ‚ n)],
      { refine add_le_of_nonpos_left _,
        have : s.negative A := negative_Union_negative hBâ‚ (Î» m, let âŸ¨_, hâŸ© := (hB m).1 in h),
        refine negative_nonpos_measure (negative_subset_negative _ this (set.diff_subset _ _)),
        exact measurable_set.Union hBâ‚ },
      { apply_instance },
      { exact (measurable_set.Union hBâ‚).diff (hBâ‚ n) } },
    { exact real.Inf_le _ measure_of_negatives_bdd_below âŸ¨A, âŸ¨hAâ‚, hAâ‚‚âŸ©, rflâŸ© } },

  refine âŸ¨Aá¶œ, A, hAâ‚.compl, _, hAâ‚, hAâ‚‚,
          disjoint_compl_left, (set.union_comm A Aá¶œ) â–¸ set.union_compl_self AâŸ©,
  rw positive_iff hAâ‚.compl,
  intros C hC hCâ‚,
  by_contra hCâ‚‚, push_neg at hCâ‚‚,
  rcases exists_negative_set hC hCâ‚‚ with âŸ¨D, hDâ‚, hD, hDâ‚‚, hDâ‚ƒâŸ©,

  have : s (A âˆª D) < Inf s.measure_of_negatives,
  { rw [â† hAâ‚ƒ, of_union (set.disjoint_of_subset_right (set.subset.trans hD hCâ‚)
        disjoint_compl_right) hAâ‚ hDâ‚],
    linarith, apply_instance },
  refine not_le.2 this _,
  refine real.Inf_le _ measure_of_negatives_bdd_below âŸ¨A âˆª D, âŸ¨_, _âŸ©, rflâŸ©,
  { exact hAâ‚.union hDâ‚ },
  { exact negative_union_negative hAâ‚ hAâ‚‚ hDâ‚ hDâ‚‚ }
end

/-- Alternative formulation of `exists_disjoint_positive_negative_union_eq` using complements. -/
lemma exists_compl_positive_negative (s : signed_measure Î±) :
  âˆƒ (i : set Î±) (hiâ‚ : measurable_set i), s.positive i âˆ§ s.negative iá¶œ :=
begin
  obtain âŸ¨i, j, hiâ‚, hiâ‚‚, _, hjâ‚‚, hdisj, hunivâŸ© :=
    s.exists_disjoint_positive_negative_union_eq,
  refine âŸ¨i, hiâ‚, hiâ‚‚, _âŸ©,
  rw [set.compl_eq_univ_diff, â† huniv,
      set.union_diff_cancel_left (set.disjoint_iff.mp hdisj)],
  exact hjâ‚‚,
end

/-- The symmetric difference of two Hahn decompositions have measure zero. -/
lemma of_symm_diff_compl_positive_negative {s : signed_measure Î±}
  {i j : set Î±} (hi : measurable_set i) (hj : measurable_set j)
  (hi' : s.positive i âˆ§ s.negative iá¶œ) (hj' : s.positive j âˆ§ s.negative já¶œ) :
  s (i Î” j) = 0 âˆ§ s (iá¶œ Î” já¶œ) = 0 :=
begin
  rw [positive_iff, negative_iff] at hi' hj',
  split,
  { rw [symm_diff_def, set.diff_eq_compl_inter, set.diff_eq_compl_inter,
        set.sup_eq_union, of_union,
        le_antisymm (hi'.2 (hi.compl.inter hj) (set.inter_subset_left _ _))
          (hj'.1 (hi.compl.inter hj) (set.inter_subset_right _ _)),
        le_antisymm (hj'.2 (hj.compl.inter hi) (set.inter_subset_left _ _))
          (hi'.1 (hj.compl.inter hi) (set.inter_subset_right _ _)), add_zero],
    { exact set.disjoint_of_subset_left (set.inter_subset_left _ _)
        (set.disjoint_of_subset_right (set.inter_subset_right _ _)
        (disjoint.comm.1 (is_compl.disjoint is_compl_compl))) },
    { exact hj.compl.inter hi },
    { exact hi.compl.inter hj } },
  { rw [symm_diff_def, set.diff_eq_compl_inter, set.diff_eq_compl_inter,
        compl_compl, compl_compl, set.sup_eq_union, of_union,
        le_antisymm (hi'.2 (hj.inter hi.compl) (set.inter_subset_right _ _))
          (hj'.1 (hj.inter hi.compl) (set.inter_subset_left _ _)),
        le_antisymm (hj'.2 (hi.inter hj.compl) (set.inter_subset_right _ _))
          (hi'.1 (hi.inter hj.compl) (set.inter_subset_left _ _)), add_zero],
    { exact set.disjoint_of_subset_left (set.inter_subset_left _ _)
        (set.disjoint_of_subset_right (set.inter_subset_right _ _)
        (is_compl.disjoint is_compl_compl)) },
    { exact hj.inter hi.compl },
    { exact hi.inter hj.compl } },
  all_goals { measurability },
end

end signed_measure

end measure_theory
