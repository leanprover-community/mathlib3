/-
Copyright (c) 2019 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl
-/
import measure_theory.measure.measure_space

/-!
# Unsigned Hahn decomposition theorem

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file proves the unsigned version of the Hahn decomposition theorem.

## Main statements

* `hahn_decomposition` : Given two finite measures `Î¼` and `Î½`, there exists a measurable set `s`
    such that any measurable set `t` included in `s` satisfies `Î½ t â‰¤ Î¼ t`, and any
    measurable set `u` included in the complement of `s` satisfies `Î¼ u â‰¤ Î½ u`.

## Tags

Hahn decomposition
-/

open set filter
open_locale classical topology ennreal

namespace measure_theory

variables {Î± : Type*} [measurable_space Î±] {Î¼ Î½ : measure Î±}

/-- **Hahn decomposition theorem** -/
lemma hahn_decomposition [is_finite_measure Î¼] [is_finite_measure Î½] :
  âˆƒs, measurable_set s âˆ§
    (âˆ€t, measurable_set t â†’ t âŠ† s â†’ Î½ t â‰¤ Î¼ t) âˆ§
    (âˆ€t, measurable_set t â†’ t âŠ† sá¶œ â†’ Î¼ t â‰¤ Î½ t) :=
begin
  let d : set Î± â†’ â„ := Î»s, ((Î¼ s).to_nnreal : â„) - (Î½ s).to_nnreal,
  let c : set â„ := d '' {s | measurable_set s },
  let Î³ : â„ := Sup c,

  have hÎ¼ : âˆ€ s, Î¼ s â‰  âˆ := measure_ne_top Î¼,
  have hÎ½ : âˆ€ s, Î½ s â‰  âˆ := measure_ne_top Î½,
  have to_nnreal_Î¼ : âˆ€s, ((Î¼ s).to_nnreal : â„â‰¥0âˆ) = Î¼ s :=
    (assume s, ennreal.coe_to_nnreal $ hÎ¼ _),
  have to_nnreal_Î½ : âˆ€s, ((Î½ s).to_nnreal : â„â‰¥0âˆ) = Î½ s :=
    (assume s, ennreal.coe_to_nnreal $ hÎ½ _),

  have d_empty : d âˆ… = 0,
  { change _ - _ = _, rw [measure_empty, measure_empty, sub_self] },

  have d_split : âˆ€s t, measurable_set s â†’ measurable_set t â†’
    d s = d (s \ t) + d (s âˆ© t),
  { assume s t hs ht,
    simp only [d],
    rw [â† measure_inter_add_diff s ht, â† measure_inter_add_diff s ht,
      ennreal.to_nnreal_add (hÎ¼ _) (hÎ¼ _), ennreal.to_nnreal_add (hÎ½ _) (hÎ½ _),
      nnreal.coe_add, nnreal.coe_add],
    simp only [sub_eq_add_neg, neg_add],
    abel },

  have d_Union : âˆ€(s : â„• â†’ set Î±), monotone s â†’
    tendsto (Î»n, d (s n)) at_top (ğ“ (d (â‹ƒn, s n))),
  { assume s hm,
    refine tendsto.sub _ _;
      refine (nnreal.tendsto_coe.2 $ (ennreal.tendsto_to_nnreal _).comp $
        tendsto_measure_Union hm),
    exact hÎ¼ _,
    exact hÎ½ _ },

  have d_Inter : âˆ€(s : â„• â†’ set Î±), (âˆ€n, measurable_set (s n)) â†’ (âˆ€n m, n â‰¤ m â†’ s m âŠ† s n) â†’
    tendsto (Î»n, d (s n)) at_top (ğ“ (d (â‹‚n, s n))),
  { assume s hs hm,
    refine tendsto.sub _ _;
      refine (nnreal.tendsto_coe.2 $ (ennreal.tendsto_to_nnreal $ _).comp $
        tendsto_measure_Inter hs hm _),
    exacts [hÎ¼ _, âŸ¨0, hÎ¼ _âŸ©, hÎ½ _, âŸ¨0, hÎ½ _âŸ©] },

  have bdd_c : bdd_above c,
  { use (Î¼ univ).to_nnreal,
    rintros r âŸ¨s, hs, rflâŸ©,
    refine le_trans (sub_le_self _ $ nnreal.coe_nonneg _) _,
    rw [nnreal.coe_le_coe, â† ennreal.coe_le_coe, to_nnreal_Î¼, to_nnreal_Î¼],
    exact measure_mono (subset_univ _) },

  have c_nonempty : c.nonempty := nonempty.image _ âŸ¨_, measurable_set.emptyâŸ©,

  have d_le_Î³ : âˆ€s, measurable_set s â†’ d s â‰¤ Î³ := assume s hs, le_cSup bdd_c âŸ¨s, hs, rflâŸ©,

  have : âˆ€n:â„•, âˆƒs : set Î±, measurable_set s âˆ§ Î³ - (1/2)^n < d s,
  { assume n,
    have : Î³ - (1/2)^n < Î³ := sub_lt_self Î³ (pow_pos (half_pos zero_lt_one) n),
    rcases exists_lt_of_lt_cSup c_nonempty this with âŸ¨r, âŸ¨s, hs, rflâŸ©, hltâŸ©,
    exact âŸ¨s, hs, hltâŸ© },
  rcases classical.axiom_of_choice this with âŸ¨e, heâŸ©,
  change â„• â†’ set Î± at e,
  have heâ‚ : âˆ€n, measurable_set (e n) := assume n, (he n).1,
  have heâ‚‚ : âˆ€n, Î³ - (1/2)^n < d (e n) := assume n, (he n).2,

  let f : â„• â†’ â„• â†’ set Î± := Î»n m, (finset.Ico n (m + 1)).inf e,

  have hf : âˆ€n m, measurable_set (f n m),
  { assume n m,
    simp only [f, finset.inf_eq_infi],
    exact measurable_set.bInter (to_countable _) (assume i _, heâ‚ _) },

  have f_subset_f : âˆ€{a b c d}, a â‰¤ b â†’ c â‰¤ d â†’ f a d âŠ† f b c,
  { assume a b c d hab hcd,
    dsimp only [f],
    rw [finset.inf_eq_infi, finset.inf_eq_infi],
    exact bInter_subset_bInter_left (finset.Ico_subset_Ico hab $ nat.succ_le_succ hcd) },

  have f_succ : âˆ€n m, n â‰¤ m â†’ f n (m + 1) = f n m âˆ© e (m + 1),
  { assume n m hnm,
    have : n â‰¤ m + 1 := le_of_lt (nat.succ_le_succ hnm),
    simp only [f],
    rw [nat.Ico_succ_right_eq_insert_Ico this, finset.inf_insert, set.inter_comm],
    refl },

  have le_d_f : âˆ€n m, m â‰¤ n â†’ Î³ - 2 * ((1 / 2) ^ m) + (1 / 2) ^ n â‰¤ d (f m n),
  { assume n m h,
    refine nat.le_induction _ _ n h,
    { have := heâ‚‚ m,
      simp only [f],
      rw [nat.Ico_succ_singleton, finset.inf_singleton],
      linarith },
    { assume n (hmn : m â‰¤ n) ih,
      have : Î³ + (Î³ - 2 * (1 / 2)^m + (1 / 2) ^ (n + 1)) â‰¤ Î³ + d (f m (n + 1)),
      { calc Î³ + (Î³ - 2 * (1 / 2)^m + (1 / 2) ^ (n+1)) â‰¤
            Î³ + (Î³ - 2 * (1 / 2)^m + ((1 / 2) ^ n - (1/2)^(n+1))) :
          begin
            refine add_le_add_left (add_le_add_left _ _) Î³,
            simp only [pow_add, pow_one, le_sub_iff_add_le],
            linarith
          end
          ... = (Î³ - (1 / 2)^(n+1)) + (Î³ - 2 * (1 / 2)^m + (1 / 2)^n) :
            by simp only [sub_eq_add_neg]; abel
          ... â‰¤ d (e (n + 1)) + d (f m n) : add_le_add (le_of_lt $ heâ‚‚ _) ih
          ... â‰¤ d (e (n + 1)) + d (f m n \ e (n + 1)) + d (f m (n + 1)) :
            by rw [f_succ _ _ hmn, d_split (f m n) (e (n + 1)) (hf _ _) (heâ‚ _), add_assoc]
          ... = d (e (n + 1) âˆª f m n) + d (f m (n + 1)) :
            begin
              rw [d_split (e (n + 1) âˆª f m n) (e (n + 1)),
                union_diff_left, union_inter_cancel_left],
              abel,
              exact (heâ‚ _).union (hf _ _),
              exact (heâ‚ _)
            end
          ... â‰¤ Î³ + d (f m (n + 1)) :
            add_le_add_right (d_le_Î³ _ $ (heâ‚ _).union (hf _ _)) _ },
      exact (add_le_add_iff_left Î³).1 this } },

  let s := â‹ƒ m, â‹‚n, f m n,
  have Î³_le_d_s : Î³ â‰¤ d s,
  { have hÎ³ : tendsto (Î»m:â„•, Î³ - 2 * (1/2)^m) at_top (ğ“ Î³),
    { suffices : tendsto (Î»m:â„•, Î³ - 2 * (1/2)^m) at_top (ğ“ (Î³ - 2 * 0)),
      { simpa only [mul_zero, tsub_zero] },
      exact (tendsto_const_nhds.sub $ tendsto_const_nhds.mul $
        tendsto_pow_at_top_nhds_0_of_lt_1
          (le_of_lt $ half_pos $ zero_lt_one) (half_lt_self zero_lt_one)) },
    have hd : tendsto (Î»m, d (â‹‚n, f m n)) at_top (ğ“ (d (â‹ƒ m, â‹‚ n, f m n))),
    { refine d_Union _ _,
      exact assume n m hnm, subset_Inter
        (assume i, subset.trans (Inter_subset (f n) i) $ f_subset_f hnm $ le_rfl) },
    refine le_of_tendsto_of_tendsto' hÎ³ hd (assume m, _),
    have : tendsto (Î»n, d (f m n)) at_top (ğ“ (d (â‹‚ n, f m n))),
    { refine d_Inter _ _ _,
      { assume n, exact hf _ _ },
      { assume n m hnm, exact f_subset_f le_rfl hnm } },
    refine ge_of_tendsto this (eventually_at_top.2 âŸ¨m, assume n hmn, _âŸ©),
    change Î³ - 2 * (1 / 2) ^ m â‰¤ d (f m n),
    refine le_trans _ (le_d_f _ _ hmn),
    exact le_add_of_le_of_nonneg le_rfl (pow_nonneg (le_of_lt $ half_pos $ zero_lt_one) _) },

  have hs : measurable_set s :=
    measurable_set.Union (assume n, measurable_set.Inter (assume m, hf _ _)),
  refine âŸ¨s, hs, _, _âŸ©,
  { assume t ht hts,
    have : 0 â‰¤ d t := ((add_le_add_iff_left Î³).1 $
      calc Î³ + 0 â‰¤ d s : by rw [add_zero]; exact Î³_le_d_s
        ... = d (s \ t) + d t : by rw [d_split _ _ hs ht, inter_eq_self_of_subset_right hts]
        ... â‰¤ Î³ + d t : add_le_add (d_le_Î³ _ (hs.diff ht)) le_rfl),
    rw [â† to_nnreal_Î¼, â† to_nnreal_Î½, ennreal.coe_le_coe, â† nnreal.coe_le_coe],
    simpa only [d, le_sub_iff_add_le, zero_add] using this },
  { assume t ht hts,
    have : d t â‰¤ 0,
    exact ((add_le_add_iff_left Î³).1 $
      calc Î³ + d t â‰¤ d s + d t : add_le_add Î³_le_d_s le_rfl
        ... = d (s âˆª t) :
          by rw [d_split _ _ (hs.union ht) ht, union_diff_right, union_inter_cancel_right,
            (subset_compl_iff_disjoint_left.1 hts).sdiff_eq_left]
        ... â‰¤ Î³ + 0 : by rw [add_zero]; exact d_le_Î³ _ (hs.union ht)),
    rw [â† to_nnreal_Î¼, â† to_nnreal_Î½, ennreal.coe_le_coe, â† nnreal.coe_le_coe],
    simpa only [d, sub_le_iff_le_add, zero_add] using this }
end

end measure_theory
