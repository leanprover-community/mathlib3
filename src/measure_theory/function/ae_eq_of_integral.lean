/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/

import analysis.normed_space.dual
import measure_theory.function.strongly_measurable
import measure_theory.integral.set_integral

/-! # From equality of integrals to equality of functions

This file provides various statements of the general form "if two functions have the same integral
on all sets, then they are equal almost everywhere".
The different lemmas use various hypotheses on the class of functions, on the target space or on the
possible finiteness of the measure.

## Main statements

All results listed below apply to two functions `f, g`, together with two main hypotheses,
* `f` and `g` are integrable on all measurable sets with finite measure,
* for all measurable sets `s` with finite measure, `‚à´ x in s, f x ‚àÇŒº = ‚à´ x in s, g x ‚àÇŒº`.
The conclusion is then `f =·µê[Œº] g`. The main lemmas are:
* `ae_eq_of_forall_set_integral_eq_of_sigma_finite`: case of a sigma-finite measure.
* `ae_fin_strongly_measurable.ae_eq_of_forall_set_integral_eq`: for functions which are
  `ae_fin_strongly_measurable`.
* `Lp.ae_eq_of_forall_set_integral_eq`: for elements of `Lp`, for `0 < p < ‚àû`.
* `integrable.ae_eq_of_forall_set_integral_eq`: for integrable functions.

For each of these results, we also provide a lemma about the equality of one function and 0. For
example, `Lp.ae_eq_zero_of_forall_set_integral_eq_zero`.

We also register the corresponding lemma for integrals of `‚Ñù‚â•0‚àû`-valued functions, in
`ae_eq_of_forall_set_lintegral_eq_of_sigma_finite`.

Generally useful lemmas which are not related to integrals:
* `ae_eq_zero_of_forall_inner`: if for all constants `c`, `Œª x, inner c (f x) =·µê[Œº] 0` then
  `f =·µê[Œº] 0`.
* `ae_eq_zero_of_forall_dual`: if for all constants `c` in the dual space, `Œª x, c (f x) =·µê[Œº] 0`
  then `f =·µê[Œº] 0`.

-/

open measure_theory topological_space normed_space filter

open_locale ennreal nnreal measure_theory

namespace measure_theory


section ae_eq_of_forall

variables {Œ± E ùïú : Type*} {m : measurable_space Œ±} {Œº : measure Œ±} [is_R_or_C ùïú]

lemma ae_eq_zero_of_forall_inner [inner_product_space ùïú E] [second_countable_topology E]
  {f : Œ± ‚Üí E} (hf : ‚àÄ c : E, (Œª x, (inner c (f x) : ùïú)) =·µê[Œº] 0) :
  f =·µê[Œº] 0 :=
begin
  let s := dense_seq E,
  have hs : dense_range s := dense_range_dense_seq E,
  have hf' : ‚àÄ·µê x ‚àÇŒº, ‚àÄ n : ‚Ñï, inner (s n) (f x) = (0 : ùïú), from ae_all_iff.mpr (Œª n, hf (s n)),
  refine hf'.mono (Œª x hx, _),
  rw [pi.zero_apply, ‚Üê inner_self_eq_zero],
  have h_closed : is_closed {c : E | inner c (f x) = (0 : ùïú)},
    from is_closed_eq (continuous_id.inner continuous_const) continuous_const,
  exact @is_closed_property ‚Ñï E _ s (Œª c, inner c (f x) = (0 : ùïú)) hs h_closed (Œª n, hx n) _,
end

local notation `‚ü™`x`, `y`‚ü´` := y x

variables (ùïú)

lemma ae_eq_zero_of_forall_dual [normed_group E] [normed_space ùïú E]
  [second_countable_topology E]
  {f : Œ± ‚Üí E} (hf : ‚àÄ c : dual ùïú E, (Œª x, ‚ü™f x, c‚ü´) =·µê[Œº] 0) :
  f =·µê[Œº] 0 :=
begin
  let u := dense_seq E,
  have hu : dense_range u := dense_range_dense_seq _,
  have : ‚àÄ n, ‚àÉ g : E ‚ÜíL[ùïú] ùïú, ‚à•g‚à• ‚â§ 1 ‚àß g (u n) = norm' ùïú (u n) :=
    Œª n, exists_dual_vector'' ùïú (u n),
  choose s hs using this,
  have A : ‚àÄ (a : E), (‚àÄ n, ‚ü™a, s n‚ü´ = (0 : ùïú)) ‚Üí a = 0,
  { assume a ha,
    contrapose! ha,
    have a_pos : 0 < ‚à•a‚à•, by simp only [ha, norm_pos_iff, ne.def, not_false_iff],
    have a_mem : a ‚àà closure (set.range u), by simp [hu.closure_range],
    obtain ‚ü®n, hn‚ü© : ‚àÉ (n : ‚Ñï), dist a (u n) < ‚à•a‚à• / 2 :=
      metric.mem_closure_range_iff.1 a_mem (‚à•a‚à•/2) (half_pos a_pos),
    use n,
    have I : ‚à•a‚à•/2 < ‚à•u n‚à•,
    { have : ‚à•a‚à• ‚â§ ‚à•u n‚à• + ‚à•a - u n‚à• := norm_le_insert' _ _,
      have : ‚à•a - u n‚à• < ‚à•a‚à•/2, by rwa dist_eq_norm at hn,
      linarith },
    assume h,
    apply lt_irrefl (‚à•s n (u n)‚à•),
    calc ‚à•s n (u n)‚à• = ‚à•s n (u n - a)‚à• : by simp only [h, sub_zero, continuous_linear_map.map_sub]
    ... ‚â§ 1 * ‚à•u n - a‚à• : continuous_linear_map.le_of_op_norm_le _ (hs n).1 _
    ... < ‚à•a‚à• / 2 : by { rw [one_mul], rwa dist_eq_norm' at hn }
    ... < ‚à•u n‚à• : I
    ... = ‚à•s n (u n)‚à• : by rw [(hs n).2, norm_norm'] },
  have hfs : ‚àÄ n : ‚Ñï, ‚àÄ·µê x ‚àÇŒº, ‚ü™f x, s n‚ü´ = (0 : ùïú), from Œª n, hf (s n),
  have hf' : ‚àÄ·µê x ‚àÇŒº, ‚àÄ n : ‚Ñï, ‚ü™f x, s n‚ü´ = (0 : ùïú), by rwa ae_all_iff,
  exact hf'.mono (Œª x hx, A (f x) hx),
end

variables {ùïú}

end ae_eq_of_forall


variables {Œ± E : Type*}
  {m m0 : measurable_space Œ±} {Œº : measure Œ±} {s t : set Œ±}
  [normed_group E] [normed_space ‚Ñù E]
  [measurable_space E] [borel_space E] [second_countable_topology E]
  [complete_space E]
  {p : ‚Ñù‚â•0‚àû}

section ae_eq_of_forall_set_integral_eq

lemma ae_const_le_iff_forall_lt_measure_zero {Œ≤} [linear_order Œ≤] [topological_space Œ≤]
  [order_topology Œ≤] [first_countable_topology Œ≤] (f : Œ± ‚Üí Œ≤) (c : Œ≤) :
  (‚àÄ·µê x ‚àÇŒº, c ‚â§ f x) ‚Üî ‚àÄ b < c, Œº {x | f x ‚â§ b} = 0 :=
begin
  rw ae_iff,
  push_neg,
  split,
  { assume h b hb,
    exact measure_mono_null (Œª y hy, (lt_of_le_of_lt hy hb : _)) h },
  assume hc,
  by_cases h : ‚àÄ b, c ‚â§ b,
  { have : {a : Œ± | f a < c} = ‚àÖ,
    { apply set.eq_empty_iff_forall_not_mem.2 (Œª x hx, _),
      exact (lt_irrefl _ (lt_of_lt_of_le hx (h (f x)))).elim },
    simp [this] },
  by_cases H : ¬¨ (is_lub (set.Iio c) c),
  { have : c ‚àà upper_bounds (set.Iio c) := Œª y hy, le_of_lt hy,
    obtain ‚ü®b, b_up, bc‚ü© : ‚àÉ (b : Œ≤), b ‚àà upper_bounds (set.Iio c) ‚àß b < c,
      by simpa [is_lub, is_least, this, lower_bounds] using H,
    exact measure_mono_null (Œª x hx, b_up hx) (hc b bc) },
  push_neg at H h,
  obtain ‚ü®u, u_mono, u_lt, u_lim, -‚ü© : ‚àÉ (u : ‚Ñï ‚Üí Œ≤), strict_mono u ‚àß (‚àÄ (n : ‚Ñï), u n < c)
      ‚àß tendsto u at_top (nhds c) ‚àß ‚àÄ (n : ‚Ñï), u n ‚àà set.Iio c :=
    H.exists_seq_strict_mono_tendsto_of_not_mem (lt_irrefl c) h,
  have h_Union : {x | f x < c} = ‚ãÉ (n : ‚Ñï), {x | f x ‚â§ u n},
  { ext1 x,
    simp_rw [set.mem_Union, set.mem_set_of_eq],
    split; intro h,
    { obtain ‚ü®n, hn‚ü© := ((tendsto_order.1 u_lim).1 _ h).exists, exact ‚ü®n, hn.le‚ü© },
    { obtain ‚ü®n, hn‚ü© := h, exact hn.trans_lt (u_lt _), }, },
  rw [h_Union, measure_Union_null_iff],
  assume n,
  exact hc _ (u_lt n),
end

section ennreal

open_locale topological_space

lemma ae_le_of_forall_set_lintegral_le_of_sigma_finite [sigma_finite Œº]
  {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : measurable f) (hg : measurable g)
  (h : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´‚Åª x in s, f x ‚àÇŒº ‚â§ ‚à´‚Åª x in s, g x ‚àÇŒº) :
  f ‚â§·µê[Œº] g :=
begin
  have A : ‚àÄ (Œµ N : ‚Ñù‚â•0) (p : ‚Ñï), 0 < Œµ ‚Üí
    Œº ({x | g x + Œµ ‚â§ f x ‚àß g x ‚â§ N} ‚à© spanning_sets Œº p) = 0,
  { assume Œµ N p Œµpos,
    let s := {x | g x + Œµ ‚â§ f x ‚àß g x ‚â§ N} ‚à© spanning_sets Œº p,
    have s_meas : measurable_set s,
    { have A : measurable_set {x | g x + Œµ ‚â§ f x} := measurable_set_le (hg.add measurable_const) hf,
      have B : measurable_set {x | g x ‚â§ N} := measurable_set_le hg measurable_const,
      exact (A.inter B).inter (measurable_spanning_sets Œº p) },
    have s_lt_top : Œº s < ‚àû :=
      (measure_mono (set.inter_subset_right _ _)).trans_lt (measure_spanning_sets_lt_top Œº p),
    have A : ‚à´‚Åª x in s, g x ‚àÇŒº + Œµ * Œº s ‚â§ ‚à´‚Åª x in s, g x ‚àÇŒº + 0 := calc
      ‚à´‚Åª x in s, g x ‚àÇŒº + Œµ * Œº s = ‚à´‚Åª x in s, g x ‚àÇŒº + ‚à´‚Åª x in s, Œµ ‚àÇŒº :
        by simp only [lintegral_const, set.univ_inter, measurable_set.univ, measure.restrict_apply]
      ... = ‚à´‚Åª x in s, (g x + Œµ) ‚àÇŒº : (lintegral_add hg measurable_const).symm
      ... ‚â§ ‚à´‚Åª x in s, f x ‚àÇŒº : set_lintegral_mono (hg.add measurable_const) hf (Œª x hx, hx.1.1)
      ... ‚â§ ‚à´‚Åª x in s, g x ‚àÇŒº + 0 : by { rw [add_zero], exact h s s_meas s_lt_top },
    have B : ‚à´‚Åª x in s, g x ‚àÇŒº ‚â† ‚àû,
    { apply ne_of_lt,
      calc ‚à´‚Åª x in s, g x ‚àÇŒº ‚â§ ‚à´‚Åª x in s, N ‚àÇŒº :
        set_lintegral_mono hg measurable_const (Œª x hx, hx.1.2)
      ... = N * Œº s :
        by simp only [lintegral_const, set.univ_inter, measurable_set.univ, measure.restrict_apply]
      ... < ‚àû : by simp only [lt_top_iff_ne_top, s_lt_top.ne, and_false,
        ennreal.coe_ne_top, with_top.mul_eq_top_iff, ne.def, not_false_iff, false_and, or_self] },
    have : (Œµ : ‚Ñù‚â•0‚àû) * Œº s ‚â§ 0 := ennreal.le_of_add_le_add_left B A,
    simpa only [ennreal.coe_eq_zero, nonpos_iff_eq_zero, mul_eq_zero, Œµpos.ne', false_or] },
  obtain ‚ü®u, u_mono, u_pos, u_lim‚ü© : ‚àÉ (u : ‚Ñï ‚Üí ‚Ñù‚â•0), strict_anti u ‚àß (‚àÄ n, 0 < u n) ‚àß
    tendsto u at_top (nhds 0) := exists_seq_strict_anti_tendsto (0 : ‚Ñù‚â•0),
  let s := Œª (n : ‚Ñï), {x | g x + u n ‚â§ f x ‚àß g x ‚â§ (n : ‚Ñù‚â•0)} ‚à© spanning_sets Œº n,
  have Œºs : ‚àÄ n, Œº (s n) = 0 := Œª n, A _ _ _ (u_pos n),
  have B : {x | f x ‚â§ g x}·∂ú ‚äÜ ‚ãÉ n, s n,
  { assume x hx,
    simp at hx,
    have L1 : ‚àÄ·∂† n in at_top, g x + u n ‚â§ f x,
    { have : tendsto (Œª n, g x + u n) at_top (ùìù (g x + (0 : ‚Ñù‚â•0))) :=
        tendsto_const_nhds.add (ennreal.tendsto_coe.2 u_lim),
      simp at this,
      exact eventually_le_of_tendsto_lt hx this },
    have L2 : ‚àÄ·∂† (n : ‚Ñï) in (at_top : filter ‚Ñï), g x ‚â§ (n : ‚Ñù‚â•0),
    { have : tendsto (Œª (n : ‚Ñï), ((n : ‚Ñù‚â•0) : ‚Ñù‚â•0‚àû)) at_top (ùìù ‚àû),
      { simp only [ennreal.coe_nat],
        exact ennreal.tendsto_nat_nhds_top },
      exact eventually_ge_of_tendsto_gt (hx.trans_le le_top) this },
    apply set.mem_Union.2,
    exact ((L1.and L2).and (eventually_mem_spanning_sets Œº x)).exists },
  refine le_antisymm _ bot_le,
  calc Œº {x : Œ± | (Œª (x : Œ±), f x ‚â§ g x) x}·∂ú ‚â§ Œº (‚ãÉ n, s n) : measure_mono B
  ... ‚â§ ‚àë' n, Œº (s n) : measure_Union_le _
  ... = 0 : by simp only [Œºs, tsum_zero]
end

lemma ae_eq_of_forall_set_lintegral_eq_of_sigma_finite [sigma_finite Œº]
  {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû} (hf : measurable f) (hg : measurable g)
  (h : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´‚Åª x in s, f x ‚àÇŒº = ‚à´‚Åª x in s, g x ‚àÇŒº) :
  f =·µê[Œº] g :=
begin
  have A : f ‚â§·µê[Œº] g :=
    ae_le_of_forall_set_lintegral_le_of_sigma_finite hf hg (Œª s hs h's, le_of_eq (h s hs h's)),
  have B : g ‚â§·µê[Œº] f :=
    ae_le_of_forall_set_lintegral_le_of_sigma_finite hg hf (Œª s hs h's, ge_of_eq (h s hs h's)),
  filter_upwards [A, B],
  exact Œª x, le_antisymm
end

end ennreal

section real

section real_finite_measure

variables [is_finite_measure Œº] {f : Œ± ‚Üí ‚Ñù}

/-- Don't use this lemma. Use `ae_nonneg_of_forall_set_integral_nonneg_of_finite_measure`. -/
lemma ae_nonneg_of_forall_set_integral_nonneg_of_finite_measure_of_measurable (hfm : measurable f)
  (hf : integrable f Œº) (hf_zero : ‚àÄ s, measurable_set s ‚Üí 0 ‚â§ ‚à´ x in s, f x ‚àÇŒº) :
  0 ‚â§·µê[Œº] f :=
begin
  simp_rw [eventually_le, pi.zero_apply],
  rw ae_const_le_iff_forall_lt_measure_zero,
  intros b hb_neg,
  let s := {x | f x ‚â§ b},
  have hs : measurable_set s, from measurable_set_le hfm measurable_const,
  have h_int_gt : ‚à´ x in s, f x ‚àÇŒº ‚â§ b * (Œº s).to_real,
  { have h_const_le : ‚à´ x in s, f x ‚àÇŒº ‚â§ ‚à´ x in s, b ‚àÇŒº,
    { refine set_integral_mono_ae_restrict hf.integrable_on
        (integrable_on_const.mpr (or.inr (measure_lt_top Œº s))) _,
      rw [eventually_le, ae_restrict_iff hs],
      exact eventually_of_forall (Œª x hxs, hxs), },
    rwa [set_integral_const, smul_eq_mul, mul_comm] at h_const_le, },
  by_contra,
  refine (lt_self_iff_false (‚à´ x in s, f x ‚àÇŒº)).mp (h_int_gt.trans_lt _),
  refine (mul_neg_iff.mpr (or.inr ‚ü®hb_neg, _‚ü©)).trans_le _,
  swap, { simp_rw measure.restrict_restrict hs, exact hf_zero s hs, },
  refine (ennreal.to_real_nonneg).lt_of_ne (Œª h_eq, h _),
  cases (ennreal.to_real_eq_zero_iff _).mp h_eq.symm with hŒºs_eq_zero hŒºs_eq_top,
  { exact hŒºs_eq_zero, },
  { exact absurd hŒºs_eq_top (measure_lt_top Œº s).ne, },
end

lemma ae_nonneg_of_forall_set_integral_nonneg_of_finite_measure (hf : integrable f Œº)
  (hf_zero : ‚àÄ s, measurable_set s ‚Üí 0 ‚â§ ‚à´ x in s, f x ‚àÇŒº) :
  0 ‚â§·µê[Œº] f :=
begin
  rcases hf.1 with ‚ü®f', hf'_meas, hf_ae‚ü©,
  have hf'_integrable : integrable f' Œº, from integrable.congr hf hf_ae,
  have hf'_zero : ‚àÄ s, measurable_set s ‚Üí 0 ‚â§ ‚à´ x in s, f' x ‚àÇŒº,
  { intros s hs,
    rw set_integral_congr_ae hs (hf_ae.mono (Œª x hx hxs, hx.symm)),
    exact hf_zero s hs, },
  exact (ae_nonneg_of_forall_set_integral_nonneg_of_finite_measure_of_measurable hf'_meas
    hf'_integrable hf'_zero).trans hf_ae.symm.le,
end

end real_finite_measure

lemma ae_nonneg_restrict_of_forall_set_integral_nonneg_inter {f : Œ± ‚Üí ‚Ñù} {t : set Œ±} (hŒºt : Œº t ‚â† ‚àû)
  (hf : integrable_on f t Œº) (hf_zero : ‚àÄ s, measurable_set s ‚Üí 0 ‚â§ ‚à´ x in (s ‚à© t), f x ‚àÇŒº) :
  0 ‚â§·µê[Œº.restrict t] f :=
begin
  haveI : fact (Œº t < ‚àû) := ‚ü®lt_top_iff_ne_top.mpr hŒºt‚ü©,
  refine ae_nonneg_of_forall_set_integral_nonneg_of_finite_measure hf (Œª s hs, _),
  simp_rw measure.restrict_restrict hs,
  exact hf_zero s hs,
end

lemma ae_nonneg_of_forall_set_integral_nonneg_of_sigma_finite [sigma_finite Œº] {f : Œ± ‚Üí ‚Ñù}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí 0 ‚â§ ‚à´ x in s, f x ‚àÇŒº) :
  0 ‚â§·µê[Œº] f :=
begin
  apply ae_of_forall_measure_lt_top_ae_restrict,
  assume t t_meas t_lt_top,
  apply ae_nonneg_restrict_of_forall_set_integral_nonneg_inter t_lt_top.ne
    (hf_int_finite t t_meas t_lt_top),
  assume s s_meas,
  exact hf_zero _ (s_meas.inter t_meas)
    (lt_of_le_of_lt (measure_mono (set.inter_subset_right _ _)) t_lt_top)
end

lemma ae_fin_strongly_measurable.ae_nonneg_of_forall_set_integral_nonneg {f : Œ± ‚Üí ‚Ñù}
  (hf : ae_fin_strongly_measurable f Œº)
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí 0 ‚â§ ‚à´ x in s, f x ‚àÇŒº) :
  0 ‚â§·µê[Œº] f :=
begin
  let t := hf.sigma_finite_set,
  suffices : 0 ‚â§·µê[Œº.restrict t] f,
    from ae_of_ae_restrict_of_ae_restrict_compl this hf.ae_eq_zero_compl.symm.le,
  haveI : sigma_finite (Œº.restrict t) := hf.sigma_finite_restrict,
  refine ae_nonneg_of_forall_set_integral_nonneg_of_sigma_finite (Œª s hs hŒºts, _)
    (Œª s hs hŒºts, _),
  { rw [integrable_on, measure.restrict_restrict hs],
    rw measure.restrict_apply hs at hŒºts,
    exact hf_int_finite (s ‚à© t) (hs.inter hf.measurable_set) hŒºts, },
  { rw measure.restrict_restrict hs,
    rw measure.restrict_apply hs at hŒºts,
    exact hf_zero (s ‚à© t) (hs.inter hf.measurable_set) hŒºts, },
end

lemma integrable.ae_nonneg_of_forall_set_integral_nonneg {f : Œ± ‚Üí ‚Ñù} (hf : integrable f Œº)
  (hf_zero : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí 0 ‚â§ ‚à´ x in s, f x ‚àÇŒº) :
  0 ‚â§·µê[Œº] f :=
ae_fin_strongly_measurable.ae_nonneg_of_forall_set_integral_nonneg hf.ae_fin_strongly_measurable
  (Œª s hs hŒºs, hf.integrable_on) hf_zero

lemma ae_nonneg_restrict_of_forall_set_integral_nonneg {f : Œ± ‚Üí ‚Ñù}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí 0 ‚â§ ‚à´ x in s, f x ‚àÇŒº)
  {t : set Œ±} (ht : measurable_set t) (hŒºt : Œº t ‚â† ‚àû) :
  0 ‚â§·µê[Œº.restrict t] f :=
begin
  refine ae_nonneg_restrict_of_forall_set_integral_nonneg_inter hŒºt
    (hf_int_finite t ht (lt_top_iff_ne_top.mpr hŒºt)) (Œª s hs, _),
  refine (hf_zero (s ‚à© t) (hs.inter ht) _),
  exact (measure_mono (set.inter_subset_right s t)).trans_lt (lt_top_iff_ne_top.mpr hŒºt),
end

lemma ae_eq_zero_restrict_of_forall_set_integral_eq_zero_real {f : Œ± ‚Üí ‚Ñù}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = 0)
  {t : set Œ±} (ht : measurable_set t) (hŒºt : Œº t ‚â† ‚àû) :
  f =·µê[Œº.restrict t] 0 :=
begin
  suffices h_and : f ‚â§·µê[Œº.restrict t] 0 ‚àß 0 ‚â§·µê[Œº.restrict t] f,
    from h_and.1.mp (h_and.2.mono (Œª x hx1 hx2, le_antisymm hx2 hx1)),
  refine ‚ü®_, ae_nonneg_restrict_of_forall_set_integral_nonneg hf_int_finite
    (Œª s hs hŒºs, (hf_zero s hs hŒºs).symm.le) ht hŒºt‚ü©,
  suffices h_neg : 0 ‚â§·µê[Œº.restrict t] -f,
  { refine h_neg.mono (Œª x hx, _),
    rw pi.neg_apply at hx,
    simpa using hx, },
  refine ae_nonneg_restrict_of_forall_set_integral_nonneg
    (Œª s hs hŒºs, (hf_int_finite s hs hŒºs).neg) (Œª s hs hŒºs, _) ht hŒºt,
  simp_rw pi.neg_apply,
  rw [integral_neg, neg_nonneg],
  exact (hf_zero s hs hŒºs).le,
end

end real

lemma ae_eq_zero_restrict_of_forall_set_integral_eq_zero {f : Œ± ‚Üí E}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = 0)
  {t : set Œ±} (ht : measurable_set t) (hŒºt : Œº t ‚â† ‚àû) :
  f =·µê[Œº.restrict t] 0 :=
begin
  refine ae_eq_zero_of_forall_dual ‚Ñù (Œª c, _),
  refine ae_eq_zero_restrict_of_forall_set_integral_eq_zero_real _ _ ht hŒºt,
  { assume s hs hŒºs,
    exact continuous_linear_map.integrable_comp c (hf_int_finite s hs hŒºs) },
  { assume s hs hŒºs,
    rw [continuous_linear_map.integral_comp_comm c (hf_int_finite s hs hŒºs), hf_zero s hs hŒºs],
    exact continuous_linear_map.map_zero _ }
end

lemma ae_eq_restrict_of_forall_set_integral_eq {f g : Œ± ‚Üí E}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hg_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on g s Œº)
  (hfg_zero : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = ‚à´ x in s, g x ‚àÇŒº)
  {t : set Œ±} (ht : measurable_set t) (hŒºt : Œº t ‚â† ‚àû) :
  f =·µê[Œº.restrict t] g :=
begin
  rw ‚Üê sub_ae_eq_zero,
  have hfg' : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, (f - g) x ‚àÇŒº = 0,
  { intros s hs hŒºs,
    rw integral_sub' (hf_int_finite s hs hŒºs) (hg_int_finite s hs hŒºs),
    exact sub_eq_zero.mpr (hfg_zero s hs hŒºs), },
  have hfg_int : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on (f-g) s Œº,
    from Œª s hs hŒºs, (hf_int_finite s hs hŒºs).sub (hg_int_finite s hs hŒºs),
  exact ae_eq_zero_restrict_of_forall_set_integral_eq_zero hfg_int hfg' ht hŒºt,
end

lemma ae_eq_zero_of_forall_set_integral_eq_of_sigma_finite [sigma_finite Œº] {f : Œ± ‚Üí E}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = 0) :
  f =·µê[Œº] 0 :=
begin
  let S := spanning_sets Œº,
  rw [‚Üê @measure.restrict_univ _ _ Œº, ‚Üê Union_spanning_sets Œº, eventually_eq, ae_iff,
    measure.restrict_apply' (measurable_set.Union (measurable_spanning_sets Œº))],
  rw [set.inter_Union, measure_Union_null_iff],
  intro n,
  have h_meas_n : measurable_set (S n), from (measurable_spanning_sets Œº n),
  have hŒºn : Œº (S n) < ‚àû, from measure_spanning_sets_lt_top Œº n,
  rw ‚Üê measure.restrict_apply' h_meas_n,
  exact ae_eq_zero_restrict_of_forall_set_integral_eq_zero hf_int_finite hf_zero h_meas_n hŒºn.ne,
end

lemma ae_eq_of_forall_set_integral_eq_of_sigma_finite [sigma_finite Œº] {f g : Œ± ‚Üí E}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hg_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on g s Œº)
  (hfg_eq : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = ‚à´ x in s, g x ‚àÇŒº) :
  f =·µê[Œº] g :=
begin
  rw ‚Üê sub_ae_eq_zero,
  have hfg : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, (f - g) x ‚àÇŒº = 0,
  { intros s hs hŒºs,
    rw [integral_sub' (hf_int_finite s hs hŒºs) (hg_int_finite s hs hŒºs),
      sub_eq_zero.mpr (hfg_eq s hs hŒºs)], },
  have hfg_int : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on (f-g) s Œº,
    from Œª s hs hŒºs, (hf_int_finite s hs hŒºs).sub (hg_int_finite s hs hŒºs),
  exact ae_eq_zero_of_forall_set_integral_eq_of_sigma_finite hfg_int hfg,
end

lemma ae_fin_strongly_measurable.ae_eq_zero_of_forall_set_integral_eq_zero {f : Œ± ‚Üí E}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = 0)
  (hf : ae_fin_strongly_measurable f Œº) :
  f =·µê[Œº] 0 :=
begin
  let t := hf.sigma_finite_set,
  suffices : f =·µê[Œº.restrict t] 0,
    from ae_of_ae_restrict_of_ae_restrict_compl this hf.ae_eq_zero_compl,
  haveI : sigma_finite (Œº.restrict t) := hf.sigma_finite_restrict,
  refine ae_eq_zero_of_forall_set_integral_eq_of_sigma_finite _ _,
  { intros s hs hŒºs,
    rw [integrable_on, measure.restrict_restrict hs],
    rw [measure.restrict_apply hs] at hŒºs,
    exact hf_int_finite _ (hs.inter hf.measurable_set) hŒºs, },
  { intros s hs hŒºs,
    rw [measure.restrict_restrict hs],
    rw [measure.restrict_apply hs] at hŒºs,
    exact hf_zero _ (hs.inter hf.measurable_set) hŒºs, },
end

lemma ae_fin_strongly_measurable.ae_eq_of_forall_set_integral_eq {f g : Œ± ‚Üí E}
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hg_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on g s Œº)
  (hfg_eq : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = ‚à´ x in s, g x ‚àÇŒº)
  (hf : ae_fin_strongly_measurable f Œº) (hg : ae_fin_strongly_measurable g Œº) :
  f =·µê[Œº] g :=
begin
  rw ‚Üê sub_ae_eq_zero,
  have hfg : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, (f - g) x ‚àÇŒº = 0,
  { intros s hs hŒºs,
    rw [integral_sub' (hf_int_finite s hs hŒºs) (hg_int_finite s hs hŒºs),
      sub_eq_zero.mpr (hfg_eq s hs hŒºs)], },
  have hfg_int : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on (f-g) s Œº,
    from Œª s hs hŒºs, (hf_int_finite s hs hŒºs).sub (hg_int_finite s hs hŒºs),
  exact (hf.sub hg).ae_eq_zero_of_forall_set_integral_eq_zero hfg_int hfg,
end

lemma Lp.ae_eq_zero_of_forall_set_integral_eq_zero
  (f : Lp E p Œº) (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû)
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = 0) :
  f =·µê[Œº] 0 :=
ae_fin_strongly_measurable.ae_eq_zero_of_forall_set_integral_eq_zero hf_int_finite hf_zero
  (Lp.fin_strongly_measurable _ hp_ne_zero hp_ne_top).ae_fin_strongly_measurable

lemma Lp.ae_eq_of_forall_set_integral_eq (f g : Lp E p Œº) (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû)
  (hf_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hg_int_finite : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí integrable_on g s Œº)
  (hfg : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = ‚à´ x in s, g x ‚àÇŒº) :
  f =·µê[Œº] g :=
ae_fin_strongly_measurable.ae_eq_of_forall_set_integral_eq hf_int_finite hg_int_finite hfg
  (Lp.fin_strongly_measurable _ hp_ne_zero hp_ne_top).ae_fin_strongly_measurable
  (Lp.fin_strongly_measurable _ hp_ne_zero hp_ne_top).ae_fin_strongly_measurable

lemma ae_eq_zero_of_forall_set_integral_eq_of_fin_strongly_measurable_trim (hm : m ‚â§ m0)
  {f : Œ± ‚Üí E} (hf_int_finite : ‚àÄ s, measurable_set[m] s ‚Üí Œº s < ‚àû ‚Üí integrable_on f s Œº)
  (hf_zero : ‚àÄ s : set Œ±, measurable_set[m] s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = 0)
  (hf : fin_strongly_measurable f (Œº.trim hm)) :
  f =·µê[Œº] 0 :=
begin
  obtain ‚ü®t, ht_meas, htf_zero, htŒº‚ü© := hf.exists_set_sigma_finite,
  haveI : sigma_finite ((Œº.restrict t).trim hm) := by rwa restrict_trim hm Œº ht_meas at htŒº,
  have htf_zero : f =·µê[Œº.restrict t·∂ú] 0,
  { rw [eventually_eq, ae_restrict_iff' (measurable_set.compl (hm _ ht_meas))],
    exact eventually_of_forall htf_zero, },
  have hf_meas_m : @measurable _ _ m _ f, from hf.measurable,
  suffices : f =·µê[Œº.restrict t] 0,
    from ae_of_ae_restrict_of_ae_restrict_compl this htf_zero,
  refine measure_eq_zero_of_trim_eq_zero hm _,
  refine ae_eq_zero_of_forall_set_integral_eq_of_sigma_finite _ _,
  { intros s hs hŒºs,
    rw [integrable_on, restrict_trim hm (Œº.restrict t) hs, measure.restrict_restrict (hm s hs)],
    rw [‚Üê restrict_trim hm Œº ht_meas, measure.restrict_apply hs,
      trim_measurable_set_eq hm (@measurable_set.inter _ m _ _ hs ht_meas)] at hŒºs,
    refine integrable.trim hm _ hf_meas_m,
    exact hf_int_finite _ (@measurable_set.inter _ m _ _ hs ht_meas) hŒºs, },
  { intros s hs hŒºs,
    rw [restrict_trim hm (Œº.restrict t) hs, measure.restrict_restrict (hm s hs)],
    rw [‚Üê restrict_trim hm Œº ht_meas, measure.restrict_apply hs,
      trim_measurable_set_eq hm (@measurable_set.inter _ m _ _ hs ht_meas)] at hŒºs,
    rw ‚Üê integral_trim hm hf_meas_m,
    exact hf_zero _ (@measurable_set.inter _ m _ _ hs ht_meas) hŒºs, },
end

lemma integrable.ae_eq_zero_of_forall_set_integral_eq_zero {f : Œ± ‚Üí E} (hf : integrable f Œº)
  (hf_zero : ‚àÄ s, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = 0) :
  f =·µê[Œº] 0 :=
begin
  have hf_Lp : mem_‚Ñíp f 1 Œº, from mem_‚Ñíp_one_iff_integrable.mpr hf,
  let f_Lp := hf_Lp.to_Lp f,
  have hf_f_Lp : f =·µê[Œº] f_Lp, from (mem_‚Ñíp.coe_fn_to_Lp hf_Lp).symm,
  refine hf_f_Lp.trans _,
  refine Lp.ae_eq_zero_of_forall_set_integral_eq_zero f_Lp one_ne_zero ennreal.coe_ne_top _ _,
  { exact Œª s hs hŒºs, integrable.integrable_on (L1.integrable_coe_fn _), },
  { intros s hs hŒºs,
    rw integral_congr_ae (ae_restrict_of_ae hf_f_Lp.symm),
    exact hf_zero s hs hŒºs, },
end

lemma integrable.ae_eq_of_forall_set_integral_eq (f g : Œ± ‚Üí E)
  (hf : integrable f Œº) (hg : integrable g Œº)
  (hfg : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, f x ‚àÇŒº = ‚à´ x in s, g x ‚àÇŒº) :
  f =·µê[Œº] g :=
begin
  rw ‚Üê sub_ae_eq_zero,
  have hfg' : ‚àÄ s : set Œ±, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, (f - g) x ‚àÇŒº = 0,
  { intros s hs hŒºs,
    rw integral_sub' hf.integrable_on hg.integrable_on,
    exact sub_eq_zero.mpr (hfg s hs hŒºs), },
  exact integrable.ae_eq_zero_of_forall_set_integral_eq_zero (hf.sub hg) hfg',
end

end ae_eq_of_forall_set_integral_eq

section lintegral

lemma ae_measurable.ae_eq_of_forall_set_lintegral_eq {f g : Œ± ‚Üí ‚Ñù‚â•0‚àû}
  (hf : ae_measurable f Œº) (hg : ae_measurable g Œº)
  (hfi : ‚à´‚Åª x, f x ‚àÇŒº ‚â† ‚àû) (hgi : ‚à´‚Åª x, g x ‚àÇŒº ‚â† ‚àû)
  (hfg : ‚àÄ ‚¶És‚¶Ñ, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚à´‚Åª x in s, f x ‚àÇŒº = ‚à´‚Åª x in s, g x ‚àÇŒº) :
  f =·µê[Œº] g :=
begin
  refine ennreal.eventually_eq_of_to_real_eventually_eq
    (ae_lt_top' hf hfi).ne_of_lt (ae_lt_top' hg hgi).ne_of_lt
    (integrable.ae_eq_of_forall_set_integral_eq _ _
      (integrable_to_real_of_lintegral_ne_top hf hfi)
      (integrable_to_real_of_lintegral_ne_top hg hgi) (Œª s hs hs', _)),
  rw [integral_eq_lintegral_of_nonneg_ae, integral_eq_lintegral_of_nonneg_ae],
  { congr' 1,
    rw [lintegral_congr_ae (of_real_to_real_ae_eq _),
        lintegral_congr_ae (of_real_to_real_ae_eq _)],
    { exact hfg hs hs' },
    { refine (ae_lt_top' hg.restrict (ne_of_lt (lt_of_le_of_lt _ hgi.lt_top))),
      exact @set_lintegral_univ Œ± _ Œº g ‚ñ∏ lintegral_mono_set (set.subset_univ _) },
    { refine (ae_lt_top' hf.restrict (ne_of_lt (lt_of_le_of_lt _ hfi.lt_top))),
      exact @set_lintegral_univ Œ± _ Œº f ‚ñ∏ lintegral_mono_set (set.subset_univ _) } },
  -- putting the proofs where they are used is extremely slow
  exacts [ae_of_all _ (Œª x, ennreal.to_real_nonneg), hg.ennreal_to_real.restrict,
          ae_of_all _ (Œª x, ennreal.to_real_nonneg), hf.ennreal_to_real.restrict]
end

end lintegral

end measure_theory
