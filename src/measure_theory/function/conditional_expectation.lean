/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/

import measure_theory.function.l2_space
import measure_theory.function.ae_eq_of_integral

/-! # Conditional expectation

The conditional expectation will be defined for functions in `LÂ²` by an orthogonal projection into
a complete subspace of `LÂ²`. It will then be extended to `LÂ¹`.

For now, this file contains only the definition of the subspace of `Láµ–` containing functions which
are measurable with respect to a sub-Ïƒ-algebra, as well as a proof that it is complete.

-/

noncomputable theory
open topological_space measure_theory.Lp filter
open_locale nnreal ennreal topological_space big_operators measure_theory

namespace measure_theory

/-- A function `f` verifies `ae_measurable' m f Î¼` if it is `Î¼`-a.e. equal to an `m`-measurable
function. This is similar to `ae_measurable`, but the `measurable_space` structures used for the
measurability statement and for the measure are different. -/
def ae_measurable' {Î± Î²} [measurable_space Î²] (m : measurable_space Î±) {m0 : measurable_space Î±}
  (f : Î± â†’ Î²) (Î¼ : measure Î±) : Prop :=
âˆƒ g : Î± â†’ Î², @measurable Î± Î² m _ g âˆ§ f =áµ[Î¼] g

namespace ae_measurable'

variables {Î± Î² ğ•œ : Type*} {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  [measurable_space Î²] [measurable_space ğ•œ] {f g : Î± â†’ Î²}

lemma congr (hf : ae_measurable' m f Î¼) (hfg : f =áµ[Î¼] g) : ae_measurable' m g Î¼ :=
by { obtain âŸ¨f', hf'_meas, hff'âŸ© := hf, exact âŸ¨f', hf'_meas, hfg.symm.trans hff'âŸ©, }

lemma add [has_add Î²] [has_measurable_addâ‚‚ Î²] (hf : ae_measurable' m f Î¼)
  (hg : ae_measurable' m g Î¼) :
  ae_measurable' m (f+g) Î¼ :=
begin
  rcases hf with âŸ¨f', h_f'_meas, hff'âŸ©,
  rcases hg with âŸ¨g', h_g'_meas, hgg'âŸ©,
  exact âŸ¨f' + g', @measurable.add _ _ _ _ m _ f' g' h_f'_meas h_g'_meas, hff'.add hgg'âŸ©,
end

lemma neg [has_neg Î²] [has_measurable_neg Î²] {f : Î± â†’ Î²} (hfm : ae_measurable' m f Î¼) :
  ae_measurable' m (-f) Î¼ :=
begin
  rcases hfm with âŸ¨f', hf'_meas, hf_aeâŸ©,
  refine âŸ¨-f', @measurable.neg _ _ _ _ _ m _ hf'_meas, hf_ae.mono (Î» x hx, _)âŸ©,
  simp_rw pi.neg_apply,
  rw hx,
end

lemma sub [has_sub Î²] [has_measurable_subâ‚‚ Î²] {f g : Î± â†’ Î²}
  (hfm : ae_measurable' m f Î¼) (hgm : ae_measurable' m g Î¼) :
  ae_measurable' m (f - g) Î¼ :=
begin
  rcases hfm with âŸ¨f', hf'_meas, hf_aeâŸ©,
  rcases hgm with âŸ¨g', hg'_meas, hg_aeâŸ©,
  refine âŸ¨f'-g', @measurable.sub _ _ _ _ m _ _ _ hf'_meas hg'_meas,
    hf_ae.mp (hg_ae.mono (Î» x hx1 hx2, _))âŸ©,
  simp_rw pi.sub_apply,
  rw [hx1, hx2],
end

lemma const_smul [has_scalar ğ•œ Î²] [has_measurable_smul ğ•œ Î²] (c : ğ•œ) (hf : ae_measurable' m f Î¼) :
  ae_measurable' m (c â€¢ f) Î¼ :=
begin
  rcases hf with âŸ¨f', h_f'_meas, hff'âŸ©,
  refine âŸ¨c â€¢ f', @measurable.const_smul _ _ _ _ _ _ m _ f' h_f'_meas c, _âŸ©,
  exact eventually_eq.fun_comp hff' (Î» x, c â€¢ x),
end

/-- A m-measurable function almost everywhere equal to `f`. -/
def mk (f : Î± â†’ Î²) (hfm : ae_measurable' m f Î¼) : Î± â†’ Î² := hfm.some

lemma measurable_mk {f : Î± â†’ Î²} (hfm : ae_measurable' m f Î¼) : @measurable _ _ m _ (hfm.mk f) :=
hfm.some_spec.1

lemma ae_eq_mk {f : Î± â†’ Î²} (hfm : ae_measurable' m f Î¼) : f =áµ[Î¼] hfm.mk f :=
hfm.some_spec.2

end ae_measurable'

lemma ae_measurable'_of_ae_measurable'_trim {Î± Î²} {m m0 m0' : measurable_space Î±}
  [measurable_space Î²] (hm0 : m0 â‰¤ m0') {Î¼ : measure Î±} {f : Î± â†’ Î²}
  (hf : ae_measurable' m f (Î¼.trim hm0)) :
  ae_measurable' m f Î¼ :=
by { obtain âŸ¨g, hg_meas, hfgâŸ© := hf, exact âŸ¨g, hg_meas, ae_eq_of_ae_eq_trim hfgâŸ©, }

lemma measurable.ae_measurable' {Î± Î²} {m m0 : measurable_space Î±} [measurable_space Î²]
  {Î¼ : measure Î±} {f : Î± â†’ Î²} (hf : @measurable _ _ m _ f) :
  ae_measurable' m f Î¼ :=
âŸ¨f, hf, ae_eq_refl _âŸ©

lemma ae_eq_trim_iff_of_ae_measurable' {Î± Î²} [add_group Î²] [measurable_space Î²]
  [measurable_singleton_class Î²] [has_measurable_subâ‚‚ Î²]
  {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f g : Î± â†’ Î²}
  (hm : m â‰¤ m0) (hfm : ae_measurable' m f Î¼) (hgm : ae_measurable' m g Î¼) :
  hfm.mk f =áµ[Î¼.trim hm] hgm.mk g â†” f =áµ[Î¼] g :=
(ae_eq_trim_iff hm hfm.measurable_mk hgm.measurable_mk).trans
âŸ¨Î» h, hfm.ae_eq_mk.trans (h.trans hgm.ae_eq_mk.symm),
  Î» h, hfm.ae_eq_mk.symm.trans (h.trans hgm.ae_eq_mk)âŸ©


variables {Î± Î² Î³ E E' F F' G G' H ğ•œ : Type*} {p : â„â‰¥0âˆ}
  [is_R_or_C ğ•œ] [measurable_space ğ•œ] -- ğ•œ for â„ or â„‚, together with a measurable_space
  [measurable_space Î²] -- Î² for a generic measurable space
  -- E for an inner product space
  [inner_product_space ğ•œ E] [measurable_space E] [borel_space E] [second_countable_topology E]
  -- E' for an inner product space on which we compute integrals
  [inner_product_space ğ•œ E'] [measurable_space E'] [borel_space E'] [second_countable_topology E']
  [complete_space E'] [normed_space â„ E']
  -- F for a Lp submodule
  [normed_group F] [normed_space ğ•œ F] [measurable_space F] [borel_space F]
  [second_countable_topology F]
  -- F' for integrals on a Lp submodule
  [normed_group F'] [normed_space ğ•œ F'] [measurable_space F'] [borel_space F']
  [second_countable_topology F'] [normed_space â„ F'] [complete_space F']
  -- G for a Lp add_subgroup
  [normed_group G] [measurable_space G] [borel_space G] [second_countable_topology G]
  -- G' for integrals on a Lp add_subgroup
  [normed_group G'] [measurable_space G'] [borel_space G'] [second_countable_topology G']
  [normed_space â„ G'] [complete_space G']
  -- H for measurable space and normed group (hypotheses of mem_â„’p)
  [measurable_space H] [normed_group H]

section Lp_meas

variables (F ğ•œ)
/-- `Lp_meas F ğ•œ m p Î¼` is the subspace of `Lp F p Î¼` containing functions `f` verifying
`ae_measurable' m f Î¼`, i.e. functions which are `Î¼`-a.e. equal to an `m`-measurable function. -/
def Lp_meas [opens_measurable_space ğ•œ] (m : measurable_space Î±) [measurable_space Î±] (p : â„â‰¥0âˆ)
  (Î¼ : measure Î±) :
  submodule ğ•œ (Lp F p Î¼) :=
{ carrier   := {f : (Lp F p Î¼) | ae_measurable' m f Î¼} ,
  zero_mem' := âŸ¨(0 : Î± â†’ F), @measurable_zero _ Î± _ m _, Lp.coe_fn_zero _ _ _âŸ©,
  add_mem'  := Î» f g hf hg, (hf.add hg).congr (Lp.coe_fn_add f g).symm,
  smul_mem' := Î» c f hf, (hf.const_smul c).congr (Lp.coe_fn_smul c f).symm, }
variables {F ğ•œ}

variables [opens_measurable_space ğ•œ]

lemma mem_Lp_meas_iff_ae_measurable' {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f : Lp F p Î¼} :
  f âˆˆ Lp_meas F ğ•œ m p Î¼ â†” ae_measurable' m f Î¼ :=
by simp_rw [â† set_like.mem_coe, â† submodule.mem_carrier, Lp_meas, set.mem_set_of_eq]

lemma Lp_meas.ae_measurable' {m m0 : measurable_space Î±} {Î¼ : measure Î±} (f : Lp_meas F ğ•œ m p Î¼) :
  ae_measurable' m f Î¼ :=
mem_Lp_meas_iff_ae_measurable'.mp f.mem

lemma mem_Lp_meas_self {m0 : measurable_space Î±} (Î¼ : measure Î±) (f : Lp F p Î¼) :
  f âˆˆ Lp_meas F ğ•œ m0 p Î¼ :=
mem_Lp_meas_iff_ae_measurable'.mpr (Lp.ae_measurable f)

lemma Lp_meas_coe {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f : Lp_meas F ğ•œ m p Î¼} :
  â‡‘f = (f : Lp F p Î¼) :=
coe_fn_coe_base f

lemma mem_Lp_meas_indicator_const_Lp {m m0 : measurable_space Î±} (hm : m â‰¤ m0)
  {Î¼ : measure Î±} {s : set Î±} (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ) {c : F} :
  indicator_const_Lp p (hm s hs) hÎ¼s c âˆˆ Lp_meas F ğ•œ m p Î¼ :=
âŸ¨s.indicator (Î» x : Î±, c),
  @measurable.indicator Î± _ m _ _ s (Î» x, c) (@measurable_const _ Î± _ m _) hs,
  indicator_const_Lp_coe_fnâŸ©

section complete_subspace

/-! ## The subspace `Lp_meas` is complete.

We define a `linear_isometry_equiv` between `Lp_meas` and the `Lp` space corresponding to the
measure `Î¼.trim hm`. As a consequence, the completeness of `Lp` implies completeness of
`Lp_meas`. -/

variables {Î¹ : Type*} {m m0 : measurable_space Î±} {Î¼ : measure Î±}

/-- If `f` belongs to `Lp_meas F ğ•œ m p Î¼`, then the measurable function it is almost everywhere
equal to (given by `ae_measurable.mk`) belongs to `â„’p` for the measure `Î¼.trim hm`. -/
lemma mem_â„’p_trim_of_mem_Lp_meas (hm : m â‰¤ m0) (f : Lp F p Î¼) (hf_meas : f âˆˆ Lp_meas F ğ•œ m p Î¼) :
  mem_â„’p (mem_Lp_meas_iff_ae_measurable'.mp hf_meas).some p (Î¼.trim hm) :=
begin
  have hf : ae_measurable' m f Î¼, from (mem_Lp_meas_iff_ae_measurable'.mp hf_meas),
  let g := hf.some,
  obtain âŸ¨hg, hfgâŸ© := hf.some_spec,
  change mem_â„’p g p (Î¼.trim hm),
  refine âŸ¨hg.ae_measurable, _âŸ©,
  have h_snorm_fg : snorm g p (Î¼.trim hm) = snorm f p Î¼,
    by { rw snorm_trim hm hg, exact snorm_congr_ae hfg.symm, },
  rw h_snorm_fg,
  exact Lp.snorm_lt_top f,
end

/-- If `f` belongs to `Lp` for the measure `Î¼.trim hm`, then it belongs to the subspace
`Lp_meas F ğ•œ m p Î¼`. -/
lemma mem_Lp_meas_to_Lp_of_trim (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) :
  (mem_â„’p_of_mem_â„’p_trim hm (Lp.mem_â„’p f)).to_Lp f âˆˆ Lp_meas F ğ•œ m p Î¼ :=
begin
  let hf_mem_â„’p := mem_â„’p_of_mem_â„’p_trim hm (Lp.mem_â„’p f),
  rw mem_Lp_meas_iff_ae_measurable',
  refine ae_measurable'.congr _ (mem_â„’p.coe_fn_to_Lp hf_mem_â„’p).symm,
  refine ae_measurable'_of_ae_measurable'_trim hm _,
  exact (Lp.ae_measurable f),
end

variables (F ğ•œ p Î¼)
/-- Map from `Lp_meas` to `Lp F p (Î¼.trim hm)`. -/
def Lp_meas_to_Lp_trim (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) : Lp F p (Î¼.trim hm) :=
mem_â„’p.to_Lp (mem_Lp_meas_iff_ae_measurable'.mp f.mem).some (mem_â„’p_trim_of_mem_Lp_meas hm f f.mem)

/-- Map from `Lp F p (Î¼.trim hm)` to `Lp_meas`, inverse of `Lp_meas_to_Lp_trim`. -/
def Lp_trim_to_Lp_meas (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) : Lp_meas F ğ•œ m p Î¼ :=
âŸ¨(mem_â„’p_of_mem_â„’p_trim hm (Lp.mem_â„’p f)).to_Lp f, mem_Lp_meas_to_Lp_of_trim hm fâŸ©

variables {F ğ•œ p Î¼}

lemma Lp_meas_to_Lp_trim_ae_eq (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f =áµ[Î¼] f :=
(ae_eq_of_ae_eq_trim (mem_â„’p.coe_fn_to_Lp (mem_â„’p_trim_of_mem_Lp_meas hm â†‘f f.mem))).trans
  (mem_Lp_meas_iff_ae_measurable'.mp f.mem).some_spec.2.symm

lemma Lp_trim_to_Lp_meas_ae_eq (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) :
  Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm f =áµ[Î¼] f :=
mem_â„’p.coe_fn_to_Lp _

/-- `Lp_trim_to_Lp_meas` is a right inverse of `Lp_meas_to_Lp_trim`. -/
lemma Lp_meas_to_Lp_trim_right_inv (hm : m â‰¤ m0) :
  function.right_inverse (Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm) (Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm) :=
begin
  intro f,
  ext1,
  refine ae_eq_trim_of_measurable hm (Lp.measurable _) (Lp.measurable _) _,
  exact (Lp_meas_to_Lp_trim_ae_eq hm _).trans (Lp_trim_to_Lp_meas_ae_eq hm _),
end

/-- `Lp_trim_to_Lp_meas` is a left inverse of `Lp_meas_to_Lp_trim`. -/
lemma Lp_meas_to_Lp_trim_left_inv (hm : m â‰¤ m0) :
  function.left_inverse (Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm) (Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm) :=
begin
  intro f,
  ext1,
  ext1,
  rw â† Lp_meas_coe,
  exact (Lp_trim_to_Lp_meas_ae_eq hm _).trans (Lp_meas_to_Lp_trim_ae_eq hm _),
end

lemma Lp_meas_to_Lp_trim_add (hm : m â‰¤ m0) (f g : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm (f + g)
    = Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f + Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm g :=
begin
  ext1,
  refine eventually_eq.trans _ (Lp.coe_fn_add _ _).symm,
  refine ae_eq_trim_of_measurable hm (Lp.measurable _) _ _,
  { exact @measurable.add _ _ _ _ m _ _ _ (Lp.measurable _) (Lp.measurable _), },
  refine (Lp_meas_to_Lp_trim_ae_eq hm _).trans _,
  refine eventually_eq.trans _
    (eventually_eq.add (Lp_meas_to_Lp_trim_ae_eq hm f).symm (Lp_meas_to_Lp_trim_ae_eq hm g).symm),
  refine (Lp.coe_fn_add _ _).trans _,
  simp_rw Lp_meas_coe,
  exact eventually_of_forall (Î» x, by refl),
end

lemma Lp_meas_to_Lp_trim_smul (hm : m â‰¤ m0) (c : ğ•œ) (f : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm (c â€¢ f) = c â€¢ Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f :=
begin
  ext1,
  refine eventually_eq.trans _ (Lp.coe_fn_smul _ _).symm,
  refine ae_eq_trim_of_measurable hm (Lp.measurable _) _ _,
  { exact @measurable.const_smul _ _ _ _ _ _ m _ _ (Lp.measurable _) c, },
  refine (Lp_meas_to_Lp_trim_ae_eq hm _).trans _,
  refine (Lp.coe_fn_smul c _).trans _,
  refine (Lp_meas_to_Lp_trim_ae_eq hm f).mono (Î» x hx, _),
  rw [pi.smul_apply, pi.smul_apply, hx, Lp_meas_coe],
  refl,
end

/-- `Lp_meas_to_Lp_trim` preserves the norm. -/
lemma Lp_meas_to_Lp_trim_norm_map [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) :
  âˆ¥Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm fâˆ¥ = âˆ¥fâˆ¥ :=
begin
  rw [norm_def, snorm_trim hm (Lp.measurable _)],
  swap, { apply_instance, },
  rw [snorm_congr_ae (Lp_meas_to_Lp_trim_ae_eq hm _), Lp_meas_coe, â† norm_def],
  congr,
end

variables (F ğ•œ p Î¼)
/-- A linear isometry equivalence between `Lp_meas` and `Lp F p (Î¼.trim hm)`. -/
def Lp_meas_to_Lp_trim_lie [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0) :
  Lp_meas F ğ•œ m p Î¼ â‰ƒâ‚—áµ¢[ğ•œ] Lp F p (Î¼.trim hm) :=
{ to_fun    := Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm,
  map_add'  := Lp_meas_to_Lp_trim_add hm,
  map_smul' := Lp_meas_to_Lp_trim_smul hm,
  inv_fun   := Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm,
  left_inv  := Lp_meas_to_Lp_trim_left_inv hm,
  right_inv := Lp_meas_to_Lp_trim_right_inv hm,
  norm_map' := Lp_meas_to_Lp_trim_norm_map hm, }
variables {F ğ•œ p Î¼}

instance [hm : fact (m â‰¤ m0)] [complete_space F] [hp : fact (1 â‰¤ p)] :
  complete_space (Lp_meas F ğ•œ m p Î¼) :=
by { rw (Lp_meas_to_Lp_trim_lie F ğ•œ p Î¼ hm.elim).to_isometric.complete_space_iff, apply_instance, }

end complete_subspace

section strongly_measurable

variables {m m0 : measurable_space Î±} {Î¼ : measure Î±}

/-- We do not get `ae_fin_strongly_measurable f (Î¼.trim hm)`, since we don't have
`f =áµ[Î¼.trim hm] Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f` but only the weaker
`f =áµ[Î¼] Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f`. -/
lemma Lp_meas.ae_fin_strongly_measurable' (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) (hp_ne_zero : p â‰  0)
  (hp_ne_top : p â‰  âˆ) :
  âˆƒ g, fin_strongly_measurable g (Î¼.trim hm) âˆ§ f =áµ[Î¼] g :=
âŸ¨Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f, Lp.fin_strongly_measurable _ hp_ne_zero hp_ne_top,
  (Lp_meas_to_Lp_trim_ae_eq hm f).symmâŸ©

end strongly_measurable

end Lp_meas


section uniqueness_of_conditional_expectation

/-! ## Uniqueness of the conditional expectation -/

variables {m m0 : measurable_space Î±} {Î¼ : measure Î±} [borel_space ğ•œ]

lemma Lp_meas.ae_eq_zero_of_forall_set_integral_eq_zero
  (hm : m â‰¤ m0) (f : Lp_meas E' ğ•œ m p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
  (hf_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼ s < âˆ â†’ integrable_on f s Î¼)
  (hf_zero : âˆ€ s : set Î±, measurable_set[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0) :
  f =áµ[Î¼] 0 :=
begin
  obtain âŸ¨g, hg_sm, hfgâŸ© := Lp_meas.ae_fin_strongly_measurable' hm f hp_ne_zero hp_ne_top,
  refine hfg.trans _,
  refine ae_eq_zero_of_forall_set_integral_eq_of_fin_strongly_measurable_trim hm _ _ hg_sm,
  { intros s hs hÎ¼s,
    have hfg_restrict : f =áµ[Î¼.restrict s] g, from ae_restrict_of_ae hfg,
    rw [integrable_on, integrable_congr hfg_restrict.symm],
    exact hf_int_finite s hs hÎ¼s, },
  { intros s hs hÎ¼s,
    have hfg_restrict : f =áµ[Î¼.restrict s] g, from ae_restrict_of_ae hfg,
    rw integral_congr_ae hfg_restrict.symm,
    exact hf_zero s hs hÎ¼s, },
end

include ğ•œ

lemma Lp.ae_eq_zero_of_forall_set_integral_eq_zero'
  (hm : m â‰¤ m0) (f : Lp E' p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
  (hf_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼ s < âˆ â†’ integrable_on f s Î¼)
  (hf_zero : âˆ€ s : set Î±, measurable_set[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = 0)
  (hf_meas : ae_measurable' m f Î¼) :
  f =áµ[Î¼] 0 :=
begin
  let f_meas : Lp_meas E' ğ•œ m p Î¼ := âŸ¨f, hf_measâŸ©,
  have hf_f_meas : f =áµ[Î¼] f_meas, by simp only [coe_fn_coe_base, subtype.coe_mk],
  refine hf_f_meas.trans _,
  refine Lp_meas.ae_eq_zero_of_forall_set_integral_eq_zero hm f_meas hp_ne_zero hp_ne_top _ _,
  { intros s hs hÎ¼s,
    have hfg_restrict : f =áµ[Î¼.restrict s] f_meas, from ae_restrict_of_ae hf_f_meas,
    rw [integrable_on, integrable_congr hfg_restrict.symm],
    exact hf_int_finite s hs hÎ¼s, },
  { intros s hs hÎ¼s,
    have hfg_restrict : f =áµ[Î¼.restrict s] f_meas, from ae_restrict_of_ae hf_f_meas,
    rw integral_congr_ae hfg_restrict.symm,
    exact hf_zero s hs hÎ¼s, },
end

/-- **Uniqueness of the conditional expectation** -/
lemma Lp.ae_eq_of_forall_set_integral_eq'
  (hm : m â‰¤ m0) (f g : Lp E' p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆ)
  (hf_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼ s < âˆ â†’ integrable_on f s Î¼)
  (hg_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼ s < âˆ â†’ integrable_on g s Î¼)
  (hfg : âˆ€ s : set Î±, measurable_set[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼)
  (hf_meas : ae_measurable' m f Î¼) (hg_meas : ae_measurable' m g Î¼) :
  f =áµ[Î¼] g :=
begin
  suffices h_sub : â‡‘(f-g) =áµ[Î¼] 0,
    by { rw â† sub_ae_eq_zero, exact (Lp.coe_fn_sub f g).symm.trans h_sub, },
  have hfg' : âˆ€ s : set Î±, measurable_set[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, (f - g) x âˆ‚Î¼ = 0,
  { intros s hs hÎ¼s,
    rw integral_congr_ae (ae_restrict_of_ae (Lp.coe_fn_sub f g)),
    rw integral_sub' (hf_int_finite s hs hÎ¼s) (hg_int_finite s hs hÎ¼s),
    exact sub_eq_zero.mpr (hfg s hs hÎ¼s), },
  have hfg_int : âˆ€ s, measurable_set[m] s â†’ Î¼ s < âˆ â†’ integrable_on â‡‘(f-g) s Î¼,
  { intros s hs hÎ¼s,
    rw [integrable_on, integrable_congr (ae_restrict_of_ae (Lp.coe_fn_sub f g))],
    exact (hf_int_finite s hs hÎ¼s).sub (hg_int_finite s hs hÎ¼s), },
  have hfg_meas : ae_measurable' m â‡‘(f - g) Î¼,
    from ae_measurable'.congr (hf_meas.sub hg_meas) (Lp.coe_fn_sub f g).symm,
  exact Lp.ae_eq_zero_of_forall_set_integral_eq_zero' hm (f-g) hp_ne_zero hp_ne_top hfg_int hfg'
    hfg_meas,
end

omit ğ•œ

lemma ae_eq_of_forall_set_integral_eq_of_sigma_finite' (hm : m â‰¤ m0) [sigma_finite (Î¼.trim hm)]
  {f g : Î± â†’ F'}
  (hf_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼ s < âˆ â†’ integrable_on f s Î¼)
  (hg_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼ s < âˆ â†’ integrable_on g s Î¼)
  (hfg_eq : âˆ€ s : set Î±, measurable_set[m] s â†’ Î¼ s < âˆ â†’ âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼)
  (hfm : ae_measurable' m f Î¼) (hgm : ae_measurable' m g Î¼) :
  f =áµ[Î¼] g :=
begin
  rw â† ae_eq_trim_iff_of_ae_measurable' hm hfm hgm,
  have hf_mk_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼.trim hm s < âˆ â†’
    @integrable_on _ _ m _ _ (hfm.mk f) s (Î¼.trim hm),
  { intros s hs hÎ¼s,
    rw trim_measurable_set_eq hm hs at hÎ¼s,
    rw [integrable_on, restrict_trim hm _ hs],
    refine integrable.trim hm _ hfm.measurable_mk,
    exact integrable.congr (hf_int_finite s hs hÎ¼s) (ae_restrict_of_ae hfm.ae_eq_mk), },
  have hg_mk_int_finite : âˆ€ s, measurable_set[m] s â†’ Î¼.trim hm s < âˆ â†’
    @integrable_on _ _ m _ _ (hgm.mk g) s (Î¼.trim hm),
  { intros s hs hÎ¼s,
    rw trim_measurable_set_eq hm hs at hÎ¼s,
    rw [integrable_on, restrict_trim hm _ hs],
    refine integrable.trim hm _ hgm.measurable_mk,
    exact integrable.congr (hg_int_finite s hs hÎ¼s) (ae_restrict_of_ae hgm.ae_eq_mk), },
  have hfg_mk_eq : âˆ€ s : set Î±, measurable_set[m] s â†’ Î¼.trim hm s < âˆ â†’
    âˆ« x in s, (hfm.mk f x) âˆ‚(Î¼.trim hm) = âˆ« x in s, (hgm.mk g x) âˆ‚(Î¼.trim hm),
  { intros s hs hÎ¼s,
    rw trim_measurable_set_eq hm hs at hÎ¼s,
    rw [restrict_trim hm _ hs, â† integral_trim hm hfm.measurable_mk,
      â† integral_trim hm hgm.measurable_mk, integral_congr_ae (ae_restrict_of_ae hfm.ae_eq_mk.symm),
      integral_congr_ae (ae_restrict_of_ae hgm.ae_eq_mk.symm)],
    exact hfg_eq s hs hÎ¼s, },
  exact ae_eq_of_forall_set_integral_eq_of_sigma_finite hf_mk_int_finite hg_mk_int_finite hfg_mk_eq,
end

end uniqueness_of_conditional_expectation

/-! ## Conditional expectation in L2

We define a conditional expectation in `L2`: it is the orthogonal projection on the subspace
`Lp_meas`. -/

section condexp_L2

local attribute [instance] fact_one_le_two_ennreal

variables [complete_space E] [borel_space ğ•œ] {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  {s t : set Î±}

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y
local notation `âŸª`x`, `y`âŸ«â‚‚` := @inner ğ•œ (Î± â†’â‚‚[Î¼] E) _ x y

variables (ğ•œ)
/-- Conditional expectation of a function in L2 with respect to a sigma-algebra -/
def condexp_L2 (hm : m â‰¤ m0) : (Î± â†’â‚‚[Î¼] E) â†’L[ğ•œ] (Lp_meas E ğ•œ m 2 Î¼) :=
@orthogonal_projection ğ•œ (Î± â†’â‚‚[Î¼] E) _ _ (Lp_meas E ğ•œ m 2 Î¼)
  (by { haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©, exact infer_instance, })
variables {ğ•œ}

lemma integrable_on_condexp_L2_of_measure_ne_top (hm : m â‰¤ m0) (hÎ¼s : Î¼ s â‰  âˆ) (f : Î± â†’â‚‚[Î¼] E) :
  integrable_on (condexp_L2 ğ•œ hm f) s Î¼ :=
integrable_on_Lp_of_measure_ne_top ((condexp_L2 ğ•œ hm f) : Î± â†’â‚‚[Î¼] E)
  fact_one_le_two_ennreal.elim hÎ¼s

lemma integrable_condexp_L2_of_finite_measure (hm : m â‰¤ m0) [finite_measure Î¼] {f : Î± â†’â‚‚[Î¼] E} :
  integrable (condexp_L2 ğ•œ hm f) Î¼ :=
integrable_on_univ.mp $ integrable_on_condexp_L2_of_measure_ne_top hm (measure_ne_top _ _) f

lemma norm_condexp_L2_le_one (hm : m â‰¤ m0) : âˆ¥@condexp_L2 Î± E ğ•œ _ _ _ _ _ _ _ _ _ _ Î¼ hmâˆ¥ â‰¤ 1 :=
by { haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©, exact orthogonal_projection_norm_le _, }

lemma norm_condexp_L2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) : âˆ¥condexp_L2 ğ•œ hm fâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
((@condexp_L2 _ E ğ•œ _ _ _ _ _ _ _ _ _ _ Î¼ hm).le_op_norm f).trans
  (mul_le_of_le_one_left (norm_nonneg _) (norm_condexp_L2_le_one hm))

lemma snorm_condexp_L2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
  snorm (condexp_L2 ğ•œ hm f) 2 Î¼ â‰¤ snorm f 2 Î¼ :=
begin
  rw [Lp_meas_coe, â† ennreal.to_real_le_to_real (Lp.snorm_ne_top _) (Lp.snorm_ne_top _), â† norm_def,
    â† norm_def, submodule.norm_coe],
  exact norm_condexp_L2_le hm f,
end

lemma norm_condexp_L2_coe_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
  âˆ¥(condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E)âˆ¥ â‰¤ âˆ¥fâˆ¥ :=
begin
  rw [norm_def, norm_def, â† Lp_meas_coe],
  refine (ennreal.to_real_le_to_real _ (Lp.snorm_ne_top _)).mpr (snorm_condexp_L2_le hm f),
  exact Lp.snorm_ne_top _,
end

lemma inner_condexp_L2_left_eq_right (hm : m â‰¤ m0) {f g : Î± â†’â‚‚[Î¼] E} :
  âŸª(condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ«â‚‚ = âŸªf, (condexp_L2 ğ•œ hm g : Î± â†’â‚‚[Î¼] E)âŸ«â‚‚ :=
by { haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©, exact inner_orthogonal_projection_left_eq_right _ f g, }

lemma condexp_L2_indicator_of_measurable (hm : m â‰¤ m0)
  (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) :
  (condexp_L2 ğ•œ hm (indicator_const_Lp 2 (hm s hs) hÎ¼s c) : Î± â†’â‚‚[Î¼] E)
    = indicator_const_Lp 2 (hm s hs) hÎ¼s c :=
begin
  rw condexp_L2,
  haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©,
  have h_mem : indicator_const_Lp 2 (hm s hs) hÎ¼s c âˆˆ Lp_meas E ğ•œ m 2 Î¼,
    from mem_Lp_meas_indicator_const_Lp hm hs hÎ¼s,
  let ind := (âŸ¨indicator_const_Lp 2 (hm s hs) hÎ¼s c, h_memâŸ© : Lp_meas E ğ•œ m 2 Î¼),
  have h_coe_ind : (ind : Î± â†’â‚‚[Î¼] E) = indicator_const_Lp 2 (hm s hs) hÎ¼s c, by refl,
  have h_orth_mem := orthogonal_projection_mem_subspace_eq_self ind,
  rw [â† h_coe_ind, h_orth_mem],
end

lemma inner_condexp_L2_eq_inner_fun (hm : m â‰¤ m0) (f g : Î± â†’â‚‚[Î¼] E) (hg : ae_measurable' m g Î¼) :
  âŸª(condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ«â‚‚ = âŸªf, gâŸ«â‚‚ :=
begin
  symmetry,
  rw [â† sub_eq_zero, â† inner_sub_left, condexp_L2],
  simp only [mem_Lp_meas_iff_ae_measurable'.mpr hg, orthogonal_projection_inner_eq_zero],
end

end condexp_L2

end measure_theory
