/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import measure_theory.function.l2_space
import measure_theory.function.strongly_measurable.lp

/-! # Functions a.e. measurable with respect to a sub-Ïƒ-algebra

A function `f` verifies `ae_strongly_measurable' m f Î¼` if it is `Î¼`-a.e. equal to
an `m`-strongly measurable function. This is similar to `ae_strongly_measurable`, but the
`measurable_space` structures used for the measurability statement and for the measure are
different.

We define `Lp_meas F ğ•œ m p Î¼`, the subspace of `Lp F p Î¼` containing functions `f` verifying
`ae_strongly_measurable' m f Î¼`, i.e. functions which are `Î¼`-a.e. equal to an `m`-strongly
measurable function.

## Main statements

We define an `isometry_equiv` between `Lp_meas_subgroup` and the `Lp` space corresponding to the
measure `Î¼.trim hm`. As a consequence, the completeness of `Lp` implies completeness of `Lp_meas`.

`Lp.induction_strongly_measurable` (see also `mem_â„’p.induction_strongly_measurable`):
To prove something for an `Lp` function a.e. strongly measurable with respect to a
sub-Ïƒ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in `Lp` strongly measurable w.r.t. `m` for which the property holds is
  closed.

-/

open topological_space filter
open_locale ennreal measure_theory

namespace measure_theory

/-- A function `f` verifies `ae_strongly_measurable' m f Î¼` if it is `Î¼`-a.e. equal to
an `m`-strongly measurable function. This is similar to `ae_strongly_measurable`, but the
`measurable_space` structures used for the measurability statement and for the measure are
different. -/
def ae_strongly_measurable' {Î± Î²} [topological_space Î²]
  (m : measurable_space Î±) {m0 : measurable_space Î±}
  (f : Î± â†’ Î²) (Î¼ : measure Î±) : Prop :=
âˆƒ g : Î± â†’ Î², strongly_measurable[m] g âˆ§ f =áµ[Î¼] g

namespace ae_strongly_measurable'

variables {Î± Î² ğ•œ : Type*} {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  [topological_space Î²] {f g : Î± â†’ Î²}

lemma congr (hf : ae_strongly_measurable' m f Î¼) (hfg : f =áµ[Î¼] g) :
  ae_strongly_measurable' m g Î¼ :=
by { obtain âŸ¨f', hf'_meas, hff'âŸ© := hf, exact âŸ¨f', hf'_meas, hfg.symm.trans hff'âŸ©, }

lemma add [has_add Î²] [has_continuous_add Î²] (hf : ae_strongly_measurable' m f Î¼)
  (hg : ae_strongly_measurable' m g Î¼) :
  ae_strongly_measurable' m (f+g) Î¼ :=
begin
  rcases hf with âŸ¨f', h_f'_meas, hff'âŸ©,
  rcases hg with âŸ¨g', h_g'_meas, hgg'âŸ©,
  exact âŸ¨f' + g', h_f'_meas.add h_g'_meas, hff'.add hgg'âŸ©,
end

lemma neg [add_group Î²] [topological_add_group Î²]
  {f : Î± â†’ Î²} (hfm : ae_strongly_measurable' m f Î¼) :
  ae_strongly_measurable' m (-f) Î¼ :=
begin
  rcases hfm with âŸ¨f', hf'_meas, hf_aeâŸ©,
  refine âŸ¨-f', hf'_meas.neg, hf_ae.mono (Î» x hx, _)âŸ©,
  simp_rw pi.neg_apply,
  rw hx,
end

lemma sub [add_group Î²] [topological_add_group Î²] {f g : Î± â†’ Î²}
  (hfm : ae_strongly_measurable' m f Î¼) (hgm : ae_strongly_measurable' m g Î¼) :
  ae_strongly_measurable' m (f - g) Î¼ :=
begin
  rcases hfm with âŸ¨f', hf'_meas, hf_aeâŸ©,
  rcases hgm with âŸ¨g', hg'_meas, hg_aeâŸ©,
  refine âŸ¨f'-g', hf'_meas.sub hg'_meas, hf_ae.mp (hg_ae.mono (Î» x hx1 hx2, _))âŸ©,
  simp_rw pi.sub_apply,
  rw [hx1, hx2],
end

lemma const_smul [has_smul ğ•œ Î²] [has_continuous_const_smul ğ•œ Î²]
  (c : ğ•œ) (hf : ae_strongly_measurable' m f Î¼) :
  ae_strongly_measurable' m (c â€¢ f) Î¼ :=
begin
  rcases hf with âŸ¨f', h_f'_meas, hff'âŸ©,
  refine âŸ¨c â€¢ f', h_f'_meas.const_smul c, _âŸ©,
  exact eventually_eq.fun_comp hff' (Î» x, c â€¢ x),
end

lemma const_inner {ğ•œ Î²} [is_R_or_C ğ•œ] [normed_add_comm_group Î²] [inner_product_space ğ•œ Î²]
  {f : Î± â†’ Î²} (hfm : ae_strongly_measurable' m f Î¼) (c : Î²) :
  ae_strongly_measurable' m (Î» x, (inner c (f x) : ğ•œ)) Î¼ :=
begin
  rcases hfm with âŸ¨f', hf'_meas, hf_aeâŸ©,
  refine âŸ¨Î» x, (inner c (f' x) : ğ•œ), (@strongly_measurable_const _ _ m _ _).inner hf'_meas,
    hf_ae.mono (Î» x hx, _)âŸ©,
  dsimp only,
  rw hx,
end

/-- An `m`-strongly measurable function almost everywhere equal to `f`. -/
noncomputable
def mk (f : Î± â†’ Î²) (hfm : ae_strongly_measurable' m f Î¼) : Î± â†’ Î² := hfm.some

lemma strongly_measurable_mk {f : Î± â†’ Î²} (hfm : ae_strongly_measurable' m f Î¼) :
  strongly_measurable[m] (hfm.mk f) :=
hfm.some_spec.1

lemma ae_eq_mk {f : Î± â†’ Î²} (hfm : ae_strongly_measurable' m f Î¼) : f =áµ[Î¼] hfm.mk f :=
hfm.some_spec.2

lemma continuous_comp {Î³} [topological_space Î³] {f : Î± â†’ Î²} {g : Î² â†’ Î³}
  (hg : continuous g) (hf : ae_strongly_measurable' m f Î¼) :
  ae_strongly_measurable' m (g âˆ˜ f) Î¼ :=
âŸ¨Î» x, g (hf.mk _ x),
  @continuous.comp_strongly_measurable _ _ _ m _ _ _ _ hg hf.strongly_measurable_mk,
  hf.ae_eq_mk.mono (Î» x hx, by rw [function.comp_apply, hx])âŸ©

end ae_strongly_measurable'

lemma ae_strongly_measurable'_of_ae_strongly_measurable'_trim {Î± Î²} {m m0 m0' : measurable_space Î±}
  [topological_space Î²] (hm0 : m0 â‰¤ m0') {Î¼ : measure Î±} {f : Î± â†’ Î²}
  (hf : ae_strongly_measurable' m f (Î¼.trim hm0)) :
  ae_strongly_measurable' m f Î¼ :=
by { obtain âŸ¨g, hg_meas, hfgâŸ© := hf, exact âŸ¨g, hg_meas, ae_eq_of_ae_eq_trim hfgâŸ©, }

lemma strongly_measurable.ae_strongly_measurable'
  {Î± Î²} {m m0 : measurable_space Î±} [topological_space Î²]
  {Î¼ : measure Î±} {f : Î± â†’ Î²} (hf : strongly_measurable[m] f) :
  ae_strongly_measurable' m f Î¼ :=
âŸ¨f, hf, ae_eq_refl _âŸ©

lemma ae_eq_trim_iff_of_ae_strongly_measurable' {Î± Î²} [topological_space Î²] [metrizable_space Î²]
  {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f g : Î± â†’ Î²}
  (hm : m â‰¤ m0) (hfm : ae_strongly_measurable' m f Î¼) (hgm : ae_strongly_measurable' m g Î¼) :
  hfm.mk f =áµ[Î¼.trim hm] hgm.mk g â†” f =áµ[Î¼] g :=
(ae_eq_trim_iff hm hfm.strongly_measurable_mk hgm.strongly_measurable_mk).trans
âŸ¨Î» h, hfm.ae_eq_mk.trans (h.trans hgm.ae_eq_mk.symm),
  Î» h, hfm.ae_eq_mk.symm.trans (h.trans hgm.ae_eq_mk)âŸ©

lemma ae_strongly_measurable.comp_ae_measurable'
  {Î± Î² Î³ : Type*} [topological_space Î²] {mÎ± : measurable_space Î±} {mÎ³ : measurable_space Î³}
  {f : Î± â†’ Î²} {Î¼ : measure Î³} {g : Î³ â†’ Î±}
  (hf : ae_strongly_measurable f (Î¼.map g)) (hg : ae_measurable g Î¼) :
  ae_strongly_measurable' (mÎ±.comap g) (f âˆ˜ g) Î¼ :=
âŸ¨(hf.mk f) âˆ˜ g, hf.strongly_measurable_mk.comp_measurable (measurable_iff_comap_le.mpr le_rfl),
  ae_eq_comp hg hf.ae_eq_mkâŸ©

/-- If the restriction to a set `s` of a Ïƒ-algebra `m` is included in the restriction to `s` of
another Ïƒ-algebra `mâ‚‚` (hypothesis `hs`), the set `s` is `m` measurable and a function `f` almost
everywhere supported on `s` is `m`-ae-strongly-measurable, then `f` is also
`mâ‚‚`-ae-strongly-measurable. -/
lemma ae_strongly_measurable'.ae_strongly_measurable'_of_measurable_space_le_on
  {Î± E} {m mâ‚‚ m0 : measurable_space Î±} {Î¼ : measure Î±}
  [topological_space E] [has_zero E] (hm : m â‰¤ m0) {s : set Î±} {f : Î± â†’ E}
  (hs_m : measurable_set[m] s) (hs : âˆ€ t, measurable_set[m] (s âˆ© t) â†’ measurable_set[mâ‚‚] (s âˆ© t))
  (hf : ae_strongly_measurable' m f Î¼) (hf_zero : f =áµ[Î¼.restrict sá¶œ] 0) :
  ae_strongly_measurable' mâ‚‚ f Î¼ :=
begin
  let f' := hf.mk f,
  have h_ind_eq : s.indicator (hf.mk f) =áµ[Î¼] f,
  { refine filter.eventually_eq.trans _
      (indicator_ae_eq_of_restrict_compl_ae_eq_zero (hm _ hs_m) hf_zero),
    filter_upwards [hf.ae_eq_mk] with x hx,
    by_cases hxs : x âˆˆ s,
    { simp [hxs, hx], },
    { simp [hxs], }, },
  suffices : strongly_measurable[mâ‚‚] (s.indicator (hf.mk f)),
    from ae_strongly_measurable'.congr this.ae_strongly_measurable' h_ind_eq,
  have hf_ind : strongly_measurable[m] (s.indicator (hf.mk f)),
    from hf.strongly_measurable_mk.indicator hs_m,
  exact hf_ind.strongly_measurable_of_measurable_space_le_on hs_m hs
    (Î» x hxs, set.indicator_of_not_mem hxs _),
end

variables {Î± E' F F' ğ•œ : Type*} {p : â„â‰¥0âˆ}
  [is_R_or_C ğ•œ] -- ğ•œ for â„ or â„‚
  -- E' for an inner product space on which we compute integrals
  [normed_add_comm_group E'] [inner_product_space ğ•œ E']
  [complete_space E'] [normed_space â„ E']
  -- F for a Lp submodule
  [normed_add_comm_group F] [normed_space ğ•œ F]
  -- F' for integrals on a Lp submodule
  [normed_add_comm_group F'] [normed_space ğ•œ F'] [normed_space â„ F'] [complete_space F']

section Lp_meas

/-! ## The subset `Lp_meas` of `Lp` functions a.e. measurable with respect to a sub-sigma-algebra -/

variables (F)

/-- `Lp_meas_subgroup F m p Î¼` is the subspace of `Lp F p Î¼` containing functions `f` verifying
`ae_strongly_measurable' m f Î¼`, i.e. functions which are `Î¼`-a.e. equal to
an `m`-strongly measurable function. -/
def Lp_meas_subgroup (m : measurable_space Î±) [measurable_space Î±] (p : â„â‰¥0âˆ) (Î¼ : measure Î±) :
  add_subgroup (Lp F p Î¼) :=
{ carrier   := {f : (Lp F p Î¼) | ae_strongly_measurable' m f Î¼} ,
  zero_mem' := âŸ¨(0 : Î± â†’ F), @strongly_measurable_zero _ _ m _ _, Lp.coe_fn_zero _ _ _âŸ©,
  add_mem'  := Î» f g hf hg, (hf.add hg).congr (Lp.coe_fn_add f g).symm,
  neg_mem' := Î» f hf, ae_strongly_measurable'.congr hf.neg (Lp.coe_fn_neg f).symm, }

variables (ğ•œ)
/-- `Lp_meas F ğ•œ m p Î¼` is the subspace of `Lp F p Î¼` containing functions `f` verifying
`ae_strongly_measurable' m f Î¼`, i.e. functions which are `Î¼`-a.e. equal to
an `m`-strongly measurable function. -/
def Lp_meas (m : measurable_space Î±) [measurable_space Î±] (p : â„â‰¥0âˆ)
  (Î¼ : measure Î±) :
  submodule ğ•œ (Lp F p Î¼) :=
{ carrier   := {f : (Lp F p Î¼) | ae_strongly_measurable' m f Î¼} ,
  zero_mem' := âŸ¨(0 : Î± â†’ F), @strongly_measurable_zero _ _ m _ _, Lp.coe_fn_zero _ _ _âŸ©,
  add_mem'  := Î» f g hf hg, (hf.add hg).congr (Lp.coe_fn_add f g).symm,
  smul_mem' := Î» c f hf, (hf.const_smul c).congr (Lp.coe_fn_smul c f).symm, }
variables {F ğ•œ}

variables

lemma mem_Lp_meas_subgroup_iff_ae_strongly_measurable' {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  {f : Lp F p Î¼} :
  f âˆˆ Lp_meas_subgroup F m p Î¼ â†” ae_strongly_measurable' m f Î¼ :=
by rw [â† add_subgroup.mem_carrier, Lp_meas_subgroup, set.mem_set_of_eq]

lemma mem_Lp_meas_iff_ae_strongly_measurable'
  {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f : Lp F p Î¼} :
  f âˆˆ Lp_meas F ğ•œ m p Î¼ â†” ae_strongly_measurable' m f Î¼ :=
by rw [â† set_like.mem_coe, â† submodule.mem_carrier, Lp_meas, set.mem_set_of_eq]

lemma Lp_meas.ae_strongly_measurable'
  {m m0 : measurable_space Î±} {Î¼ : measure Î±} (f : Lp_meas F ğ•œ m p Î¼) :
  ae_strongly_measurable' m f Î¼ :=
mem_Lp_meas_iff_ae_strongly_measurable'.mp f.mem

lemma mem_Lp_meas_self
  {m0 : measurable_space Î±} (Î¼ : measure Î±) (f : Lp F p Î¼) :
  f âˆˆ Lp_meas F ğ•œ m0 p Î¼ :=
mem_Lp_meas_iff_ae_strongly_measurable'.mpr (Lp.ae_strongly_measurable f)

lemma Lp_meas_subgroup_coe {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  {f : Lp_meas_subgroup F m p Î¼} :
  â‡‘f = (f : Lp F p Î¼) :=
coe_fn_coe_base f

lemma Lp_meas_coe {m m0 : measurable_space Î±} {Î¼ : measure Î±} {f : Lp_meas F ğ•œ m p Î¼} :
  â‡‘f = (f : Lp F p Î¼) :=
coe_fn_coe_base f

lemma mem_Lp_meas_indicator_const_Lp {m m0 : measurable_space Î±} (hm : m â‰¤ m0)
  {Î¼ : measure Î±} {s : set Î±} (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ) {c : F} :
  indicator_const_Lp p (hm s hs) hÎ¼s c âˆˆ Lp_meas F ğ•œ m p Î¼ :=
âŸ¨s.indicator (Î» x : Î±, c), (@strongly_measurable_const _ _ m _ _).indicator hs,
  indicator_const_Lp_coe_fnâŸ©

section complete_subspace

/-! ## The subspace `Lp_meas` is complete.

We define an `isometry_equiv` between `Lp_meas_subgroup` and the `Lp` space corresponding to the
measure `Î¼.trim hm`. As a consequence, the completeness of `Lp` implies completeness of
`Lp_meas_subgroup` (and `Lp_meas`). -/

variables {Î¹ : Type*} {m m0 : measurable_space Î±} {Î¼ : measure Î±}

/-- If `f` belongs to `Lp_meas_subgroup F m p Î¼`, then the measurable function it is almost
everywhere equal to (given by `ae_measurable.mk`) belongs to `â„’p` for the measure `Î¼.trim hm`. -/
lemma mem_â„’p_trim_of_mem_Lp_meas_subgroup (hm : m â‰¤ m0) (f : Lp F p Î¼)
  (hf_meas : f âˆˆ Lp_meas_subgroup F m p Î¼) :
  mem_â„’p (mem_Lp_meas_subgroup_iff_ae_strongly_measurable'.mp hf_meas).some p (Î¼.trim hm) :=
begin
  have hf : ae_strongly_measurable' m f Î¼,
    from (mem_Lp_meas_subgroup_iff_ae_strongly_measurable'.mp hf_meas),
  let g := hf.some,
  obtain âŸ¨hg, hfgâŸ© := hf.some_spec,
  change mem_â„’p g p (Î¼.trim hm),
  refine âŸ¨hg.ae_strongly_measurable, _âŸ©,
  have h_snorm_fg : snorm g p (Î¼.trim hm) = snorm f p Î¼,
    by { rw snorm_trim hm hg, exact snorm_congr_ae hfg.symm, },
  rw h_snorm_fg,
  exact Lp.snorm_lt_top f,
end

/-- If `f` belongs to `Lp` for the measure `Î¼.trim hm`, then it belongs to the subgroup
`Lp_meas_subgroup F m p Î¼`. -/
lemma mem_Lp_meas_subgroup_to_Lp_of_trim (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) :
  (mem_â„’p_of_mem_â„’p_trim hm (Lp.mem_â„’p f)).to_Lp f âˆˆ Lp_meas_subgroup F m p Î¼ :=
begin
  let hf_mem_â„’p := mem_â„’p_of_mem_â„’p_trim hm (Lp.mem_â„’p f),
  rw mem_Lp_meas_subgroup_iff_ae_strongly_measurable',
  refine ae_strongly_measurable'.congr _ (mem_â„’p.coe_fn_to_Lp hf_mem_â„’p).symm,
  refine ae_strongly_measurable'_of_ae_strongly_measurable'_trim hm _,
  exact Lp.ae_strongly_measurable f,
end

variables (F p Î¼)
/-- Map from `Lp_meas_subgroup` to `Lp F p (Î¼.trim hm)`. -/
noncomputable
def Lp_meas_subgroup_to_Lp_trim (hm : m â‰¤ m0) (f : Lp_meas_subgroup F m p Î¼) : Lp F p (Î¼.trim hm) :=
mem_â„’p.to_Lp (mem_Lp_meas_subgroup_iff_ae_strongly_measurable'.mp f.mem).some
  (mem_â„’p_trim_of_mem_Lp_meas_subgroup hm f f.mem)

variables (ğ•œ)
/-- Map from `Lp_meas` to `Lp F p (Î¼.trim hm)`. -/
noncomputable
def Lp_meas_to_Lp_trim (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) : Lp F p (Î¼.trim hm) :=
mem_â„’p.to_Lp (mem_Lp_meas_iff_ae_strongly_measurable'.mp f.mem).some
  (mem_â„’p_trim_of_mem_Lp_meas_subgroup hm f f.mem)
variables {ğ•œ}

/-- Map from `Lp F p (Î¼.trim hm)` to `Lp_meas_subgroup`, inverse of
`Lp_meas_subgroup_to_Lp_trim`. -/
noncomputable
def Lp_trim_to_Lp_meas_subgroup (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) : Lp_meas_subgroup F m p Î¼ :=
âŸ¨(mem_â„’p_of_mem_â„’p_trim hm (Lp.mem_â„’p f)).to_Lp f, mem_Lp_meas_subgroup_to_Lp_of_trim hm fâŸ©

variables (ğ•œ)
/-- Map from `Lp F p (Î¼.trim hm)` to `Lp_meas`, inverse of `Lp_meas_to_Lp_trim`. -/
noncomputable
def Lp_trim_to_Lp_meas (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) : Lp_meas F ğ•œ m p Î¼ :=
âŸ¨(mem_â„’p_of_mem_â„’p_trim hm (Lp.mem_â„’p f)).to_Lp f, mem_Lp_meas_subgroup_to_Lp_of_trim hm fâŸ©

variables {F ğ•œ p Î¼}

lemma Lp_meas_subgroup_to_Lp_trim_ae_eq (hm : m â‰¤ m0) (f : Lp_meas_subgroup F m p Î¼) :
  Lp_meas_subgroup_to_Lp_trim F p Î¼ hm f =áµ[Î¼] f :=
(ae_eq_of_ae_eq_trim (mem_â„’p.coe_fn_to_Lp (mem_â„’p_trim_of_mem_Lp_meas_subgroup hm â†‘f f.mem))).trans
  (mem_Lp_meas_subgroup_iff_ae_strongly_measurable'.mp f.mem).some_spec.2.symm

lemma Lp_trim_to_Lp_meas_subgroup_ae_eq (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) :
  Lp_trim_to_Lp_meas_subgroup F p Î¼ hm f =áµ[Î¼] f :=
mem_â„’p.coe_fn_to_Lp _

lemma Lp_meas_to_Lp_trim_ae_eq (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f =áµ[Î¼] f :=
(ae_eq_of_ae_eq_trim (mem_â„’p.coe_fn_to_Lp (mem_â„’p_trim_of_mem_Lp_meas_subgroup hm â†‘f f.mem))).trans
  (mem_Lp_meas_subgroup_iff_ae_strongly_measurable'.mp f.mem).some_spec.2.symm

lemma Lp_trim_to_Lp_meas_ae_eq (hm : m â‰¤ m0) (f : Lp F p (Î¼.trim hm)) :
  Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm f =áµ[Î¼] f :=
mem_â„’p.coe_fn_to_Lp _

/-- `Lp_trim_to_Lp_meas_subgroup` is a right inverse of `Lp_meas_subgroup_to_Lp_trim`. -/
lemma Lp_meas_subgroup_to_Lp_trim_right_inv (hm : m â‰¤ m0) :
  function.right_inverse (Lp_trim_to_Lp_meas_subgroup F p Î¼ hm)
    (Lp_meas_subgroup_to_Lp_trim F p Î¼ hm) :=
begin
  intro f,
  ext1,
  refine ae_eq_trim_of_strongly_measurable hm
    (Lp.strongly_measurable _) (Lp.strongly_measurable _) _,
  exact (Lp_meas_subgroup_to_Lp_trim_ae_eq hm _).trans (Lp_trim_to_Lp_meas_subgroup_ae_eq hm _),
end

/-- `Lp_trim_to_Lp_meas_subgroup` is a left inverse of `Lp_meas_subgroup_to_Lp_trim`. -/
lemma Lp_meas_subgroup_to_Lp_trim_left_inv (hm : m â‰¤ m0) :
  function.left_inverse (Lp_trim_to_Lp_meas_subgroup F p Î¼ hm)
    (Lp_meas_subgroup_to_Lp_trim F p Î¼ hm) :=
begin
  intro f,
  ext1,
  ext1,
  rw â† Lp_meas_subgroup_coe,
  exact (Lp_trim_to_Lp_meas_subgroup_ae_eq hm _).trans (Lp_meas_subgroup_to_Lp_trim_ae_eq hm _),
end

lemma Lp_meas_subgroup_to_Lp_trim_add (hm : m â‰¤ m0) (f g : Lp_meas_subgroup F m p Î¼) :
  Lp_meas_subgroup_to_Lp_trim F p Î¼ hm (f + g)
    = Lp_meas_subgroup_to_Lp_trim F p Î¼ hm f + Lp_meas_subgroup_to_Lp_trim F p Î¼ hm g :=
begin
  ext1,
  refine eventually_eq.trans _ (Lp.coe_fn_add _ _).symm,
  refine ae_eq_trim_of_strongly_measurable hm (Lp.strongly_measurable _) _ _,
  { exact (Lp.strongly_measurable _).add (Lp.strongly_measurable _), },
  refine (Lp_meas_subgroup_to_Lp_trim_ae_eq hm _).trans _,
  refine eventually_eq.trans _
    (eventually_eq.add (Lp_meas_subgroup_to_Lp_trim_ae_eq hm f).symm
      (Lp_meas_subgroup_to_Lp_trim_ae_eq hm g).symm),
  refine (Lp.coe_fn_add _ _).trans _,
  simp_rw Lp_meas_subgroup_coe,
  exact eventually_of_forall (Î» x, by refl),
end

lemma Lp_meas_subgroup_to_Lp_trim_neg (hm : m â‰¤ m0) (f : Lp_meas_subgroup F m p Î¼) :
  Lp_meas_subgroup_to_Lp_trim F p Î¼ hm (-f)
    = -Lp_meas_subgroup_to_Lp_trim F p Î¼ hm f :=
begin
  ext1,
  refine eventually_eq.trans _ (Lp.coe_fn_neg _).symm,
  refine ae_eq_trim_of_strongly_measurable hm (Lp.strongly_measurable _) _ _,
  { exact @strongly_measurable.neg _ _ _ m _ _ _ (Lp.strongly_measurable _), },
  refine (Lp_meas_subgroup_to_Lp_trim_ae_eq hm _).trans _,
  refine eventually_eq.trans _
    (eventually_eq.neg (Lp_meas_subgroup_to_Lp_trim_ae_eq hm f).symm),
  refine (Lp.coe_fn_neg _).trans _,
  simp_rw Lp_meas_subgroup_coe,
  exact eventually_of_forall (Î» x, by refl),
end

lemma Lp_meas_subgroup_to_Lp_trim_sub (hm : m â‰¤ m0) (f g : Lp_meas_subgroup F m p Î¼) :
  Lp_meas_subgroup_to_Lp_trim F p Î¼ hm (f - g)
    = Lp_meas_subgroup_to_Lp_trim F p Î¼ hm f - Lp_meas_subgroup_to_Lp_trim F p Î¼ hm g :=
by rw [sub_eq_add_neg, sub_eq_add_neg, Lp_meas_subgroup_to_Lp_trim_add,
  Lp_meas_subgroup_to_Lp_trim_neg]

lemma Lp_meas_to_Lp_trim_smul (hm : m â‰¤ m0) (c : ğ•œ) (f : Lp_meas F ğ•œ m p Î¼) :
  Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm (c â€¢ f) = c â€¢ Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f :=
begin
  ext1,
  refine eventually_eq.trans _ (Lp.coe_fn_smul _ _).symm,
  refine ae_eq_trim_of_strongly_measurable hm (Lp.strongly_measurable _) _ _,
  { exact (Lp.strongly_measurable _).const_smul c, },
  refine (Lp_meas_to_Lp_trim_ae_eq hm _).trans _,
  refine (Lp.coe_fn_smul _ _).trans _,
  refine (Lp_meas_to_Lp_trim_ae_eq hm f).mono (Î» x hx, _),
  rw [pi.smul_apply, pi.smul_apply, hx],
  refl,
end

/-- `Lp_meas_subgroup_to_Lp_trim` preserves the norm. -/
lemma Lp_meas_subgroup_to_Lp_trim_norm_map [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0)
  (f : Lp_meas_subgroup F m p Î¼) :
  â€–Lp_meas_subgroup_to_Lp_trim F p Î¼ hm fâ€– = â€–fâ€– :=
begin
  rw [Lp.norm_def, snorm_trim hm (Lp.strongly_measurable _),
    snorm_congr_ae (Lp_meas_subgroup_to_Lp_trim_ae_eq hm _), Lp_meas_subgroup_coe, â† Lp.norm_def],
  congr,
end

lemma isometry_Lp_meas_subgroup_to_Lp_trim [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0) :
  isometry (Lp_meas_subgroup_to_Lp_trim F p Î¼ hm) :=
isometry.of_dist_eq $ Î» f g, by rw [dist_eq_norm, â† Lp_meas_subgroup_to_Lp_trim_sub,
  Lp_meas_subgroup_to_Lp_trim_norm_map, dist_eq_norm]

variables (F p Î¼)
/-- `Lp_meas_subgroup` and `Lp F p (Î¼.trim hm)` are isometric. -/
noncomputable
def Lp_meas_subgroup_to_Lp_trim_iso [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0) :
  Lp_meas_subgroup F m p Î¼ â‰ƒáµ¢ Lp F p (Î¼.trim hm) :=
{ to_fun    := Lp_meas_subgroup_to_Lp_trim F p Î¼ hm,
  inv_fun   := Lp_trim_to_Lp_meas_subgroup F p Î¼ hm,
  left_inv  := Lp_meas_subgroup_to_Lp_trim_left_inv hm,
  right_inv := Lp_meas_subgroup_to_Lp_trim_right_inv hm,
  isometry_to_fun := isometry_Lp_meas_subgroup_to_Lp_trim hm, }

variables (ğ•œ)
/-- `Lp_meas_subgroup` and `Lp_meas` are isometric. -/
noncomputable
def Lp_meas_subgroup_to_Lp_meas_iso [hp : fact (1 â‰¤ p)] :
  Lp_meas_subgroup F m p Î¼ â‰ƒáµ¢ Lp_meas F ğ•œ m p Î¼ :=
isometry_equiv.refl (Lp_meas_subgroup F m p Î¼)

/-- `Lp_meas` and `Lp F p (Î¼.trim hm)` are isometric, with a linear equivalence. -/
noncomputable
def Lp_meas_to_Lp_trim_lie [hp : fact (1 â‰¤ p)] (hm : m â‰¤ m0) :
  Lp_meas F ğ•œ m p Î¼ â‰ƒâ‚—áµ¢[ğ•œ] Lp F p (Î¼.trim hm) :=
{ to_fun    := Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm,
  inv_fun   := Lp_trim_to_Lp_meas F ğ•œ p Î¼ hm,
  left_inv  := Lp_meas_subgroup_to_Lp_trim_left_inv hm,
  right_inv := Lp_meas_subgroup_to_Lp_trim_right_inv hm,
  map_add'  := Lp_meas_subgroup_to_Lp_trim_add hm,
  map_smul' := Lp_meas_to_Lp_trim_smul hm,
  norm_map' := Lp_meas_subgroup_to_Lp_trim_norm_map hm, }
variables {F ğ•œ p Î¼}

instance [hm : fact (m â‰¤ m0)] [complete_space F] [hp : fact (1 â‰¤ p)] :
  complete_space (Lp_meas_subgroup F m p Î¼) :=
by { rw (Lp_meas_subgroup_to_Lp_trim_iso F p Î¼ hm.elim).complete_space_iff, apply_instance, }

-- For now just no-lint this; lean4's tree-based logging will make this easier to debug.
-- One possible change might be to generalize `ğ•œ` from `is_R_or_C` to `normed_field`, as this
-- result may well hold there.
@[nolint fails_quickly]
instance [hm : fact (m â‰¤ m0)] [complete_space F] [hp : fact (1 â‰¤ p)] :
  complete_space (Lp_meas F ğ•œ m p Î¼) :=
by { rw (Lp_meas_subgroup_to_Lp_meas_iso F ğ•œ p Î¼).symm.complete_space_iff, apply_instance, }

lemma is_complete_ae_strongly_measurable' [hp : fact (1 â‰¤ p)] [complete_space F] (hm : m â‰¤ m0) :
  is_complete {f : Lp F p Î¼ | ae_strongly_measurable' m f Î¼} :=
begin
  rw â† complete_space_coe_iff_is_complete,
  haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©,
  change complete_space (Lp_meas_subgroup F m p Î¼),
  apply_instance,
end

lemma is_closed_ae_strongly_measurable' [hp : fact (1 â‰¤ p)] [complete_space F] (hm : m â‰¤ m0) :
  is_closed {f : Lp F p Î¼ | ae_strongly_measurable' m f Î¼} :=
is_complete.is_closed (is_complete_ae_strongly_measurable' hm)

end complete_subspace

section strongly_measurable

variables {m m0 : measurable_space Î±} {Î¼ : measure Î±}

/-- We do not get `ae_fin_strongly_measurable f (Î¼.trim hm)`, since we don't have
`f =áµ[Î¼.trim hm] Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f` but only the weaker
`f =áµ[Î¼] Lp_meas_to_Lp_trim F ğ•œ p Î¼ hm f`. -/
lemma Lp_meas.ae_fin_strongly_measurable' (hm : m â‰¤ m0) (f : Lp_meas F ğ•œ m p Î¼) (hp_ne_zero : p â‰  0)
  (hp_ne_top : p â‰  âˆ) :
  âˆƒ g, fin_strongly_measurable g (Î¼.trim hm) âˆ§ f =áµ[Î¼] g :=
âŸ¨Lp_meas_subgroup_to_Lp_trim F p Î¼ hm f, Lp.fin_strongly_measurable _ hp_ne_zero hp_ne_top,
  (Lp_meas_subgroup_to_Lp_trim_ae_eq hm f).symmâŸ©

/-- When applying the inverse of `Lp_meas_to_Lp_trim_lie` (which takes a function in the Lp space of
the sub-sigma algebra and returns its version in the larger Lp space) to an indicator of the
sub-sigma-algebra, we obtain an indicator in the Lp space of the larger sigma-algebra. -/
lemma Lp_meas_to_Lp_trim_lie_symm_indicator [one_le_p : fact (1 â‰¤ p)] [normed_space â„ F]
  {hm : m â‰¤ m0} {s : set Î±} {Î¼ : measure Î±}
  (hs : measurable_set[m] s) (hÎ¼s : Î¼.trim hm s â‰  âˆ) (c : F) :
  ((Lp_meas_to_Lp_trim_lie F â„ p Î¼ hm).symm
      (indicator_const_Lp p hs hÎ¼s c) : Lp F p Î¼)
    = indicator_const_Lp p (hm s hs) ((le_trim hm).trans_lt hÎ¼s.lt_top).ne c :=
begin
  ext1,
  rw â† Lp_meas_coe,
  change Lp_trim_to_Lp_meas F â„ p Î¼ hm (indicator_const_Lp p hs hÎ¼s c)
    =áµ[Î¼] (indicator_const_Lp p _ _ c : Î± â†’ F),
  refine (Lp_trim_to_Lp_meas_ae_eq hm _).trans _,
  exact (ae_eq_of_ae_eq_trim indicator_const_Lp_coe_fn).trans indicator_const_Lp_coe_fn.symm,
end

lemma Lp_meas_to_Lp_trim_lie_symm_to_Lp [one_le_p : fact (1 â‰¤ p)] [normed_space â„ F]
  (hm : m â‰¤ m0) (f : Î± â†’ F) (hf : mem_â„’p f p (Î¼.trim hm)) :
  ((Lp_meas_to_Lp_trim_lie F â„ p Î¼ hm).symm (hf.to_Lp f) : Lp F p Î¼)
    = (mem_â„’p_of_mem_â„’p_trim hm hf).to_Lp f :=
begin
  ext1,
  rw â† Lp_meas_coe,
  refine (Lp_trim_to_Lp_meas_ae_eq hm _).trans _,
  exact (ae_eq_of_ae_eq_trim (mem_â„’p.coe_fn_to_Lp hf)).trans (mem_â„’p.coe_fn_to_Lp _).symm,
end

end strongly_measurable

end Lp_meas


section induction

variables {m m0 : measurable_space Î±} {Î¼ : measure Î±} [fact (1 â‰¤ p)] [normed_space â„ F]

/-- Auxiliary lemma for `Lp.induction_strongly_measurable`. -/
@[elab_as_eliminator]
lemma Lp.induction_strongly_measurable_aux (hm : m â‰¤ m0) (hp_ne_top : p â‰  âˆ) (P : Lp F p Î¼ â†’ Prop)
  (h_ind : âˆ€ (c : F) {s : set Î±} (hs : measurable_set[m] s) (hÎ¼s : Î¼ s < âˆ),
      P (Lp.simple_func.indicator_const p (hm s hs) hÎ¼s.ne c))
  (h_add : âˆ€ â¦ƒf gâ¦„, âˆ€ hf : mem_â„’p f p Î¼, âˆ€ hg : mem_â„’p g p Î¼,
    âˆ€ hfm : ae_strongly_measurable' m f Î¼, âˆ€ hgm : ae_strongly_measurable' m g Î¼,
    disjoint (function.support f) (function.support g) â†’
    P (hf.to_Lp f) â†’ P (hg.to_Lp g) â†’ P ((hf.to_Lp f) + (hg.to_Lp g)))
  (h_closed : is_closed {f : Lp_meas F â„ m p Î¼ | P f}) :
  âˆ€ f : Lp F p Î¼, ae_strongly_measurable' m f Î¼ â†’ P f :=
begin
  intros f hf,
  let f' := (âŸ¨f, hfâŸ© : Lp_meas F â„ m p Î¼),
  let g := Lp_meas_to_Lp_trim_lie F â„ p Î¼ hm f',
  have hfg : f' = (Lp_meas_to_Lp_trim_lie F â„ p Î¼ hm).symm g,
    by simp only [linear_isometry_equiv.symm_apply_apply],
  change P â†‘f',
  rw hfg,
  refine @Lp.induction Î± F m _ p (Î¼.trim hm) _ hp_ne_top
    (Î» g, P ((Lp_meas_to_Lp_trim_lie F â„ p Î¼ hm).symm g)) _ _ _ g,
  { intros b t ht hÎ¼t,
    rw [Lp.simple_func.coe_indicator_const,
      Lp_meas_to_Lp_trim_lie_symm_indicator ht hÎ¼t.ne b],
      have hÎ¼t' : Î¼ t < âˆ, from (le_trim hm).trans_lt hÎ¼t,
    specialize h_ind b ht hÎ¼t',
    rwa Lp.simple_func.coe_indicator_const at h_ind, },
  { intros f g hf hg h_disj hfP hgP,
    rw linear_isometry_equiv.map_add,
    push_cast,
    have h_eq : âˆ€ (f : Î± â†’ F) (hf : mem_â„’p f p (Î¼.trim hm)),
      ((Lp_meas_to_Lp_trim_lie F â„ p Î¼ hm).symm (mem_â„’p.to_Lp f hf) : Lp F p Î¼)
        = (mem_â„’p_of_mem_â„’p_trim hm hf).to_Lp f,
      from Lp_meas_to_Lp_trim_lie_symm_to_Lp hm,
    rw h_eq f hf at hfP âŠ¢,
    rw h_eq g hg at hgP âŠ¢,
    exact h_add (mem_â„’p_of_mem_â„’p_trim hm hf) (mem_â„’p_of_mem_â„’p_trim hm hg)
      (ae_strongly_measurable'_of_ae_strongly_measurable'_trim hm hf.ae_strongly_measurable)
      (ae_strongly_measurable'_of_ae_strongly_measurable'_trim hm hg.ae_strongly_measurable)
      h_disj hfP hgP, },
  { change is_closed ((Lp_meas_to_Lp_trim_lie F â„ p Î¼ hm).symm â»Â¹' {g : Lp_meas F â„ m p Î¼ | P â†‘g}),
    exact is_closed.preimage (linear_isometry_equiv.continuous _) h_closed, },
end

/-- To prove something for an `Lp` function a.e. strongly measurable with respect to a
sub-Ïƒ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in `Lp` strongly measurable w.r.t. `m` for which the property holds is
  closed.
-/
@[elab_as_eliminator]
lemma Lp.induction_strongly_measurable (hm : m â‰¤ m0) (hp_ne_top : p â‰  âˆ) (P : Lp F p Î¼ â†’ Prop)
  (h_ind : âˆ€ (c : F) {s : set Î±} (hs : measurable_set[m] s) (hÎ¼s : Î¼ s < âˆ),
      P (Lp.simple_func.indicator_const p (hm s hs) hÎ¼s.ne c))
  (h_add : âˆ€ â¦ƒf gâ¦„, âˆ€ hf : mem_â„’p f p Î¼, âˆ€ hg : mem_â„’p g p Î¼,
    âˆ€ hfm : strongly_measurable[m] f, âˆ€ hgm : strongly_measurable[m] g,
    disjoint (function.support f) (function.support g) â†’
    P (hf.to_Lp f) â†’ P (hg.to_Lp g) â†’ P ((hf.to_Lp f) + (hg.to_Lp g)))
  (h_closed : is_closed {f : Lp_meas F â„ m p Î¼ | P f}) :
  âˆ€ f : Lp F p Î¼, ae_strongly_measurable' m f Î¼ â†’ P f :=
begin
  intros f hf,
  suffices h_add_ae : âˆ€ â¦ƒf gâ¦„, âˆ€ hf : mem_â„’p f p Î¼, âˆ€ hg : mem_â„’p g p Î¼,
      âˆ€ hfm : ae_strongly_measurable' m f Î¼, âˆ€ hgm : ae_strongly_measurable' m g Î¼,
      disjoint (function.support f) (function.support g) â†’
      P (hf.to_Lp f) â†’ P (hg.to_Lp g) â†’ P ((hf.to_Lp f) + (hg.to_Lp g)),
    from Lp.induction_strongly_measurable_aux hm hp_ne_top P h_ind h_add_ae h_closed f hf,
  intros f g hf hg hfm hgm h_disj hPf hPg,
  let s_f : set Î± := function.support (hfm.mk f),
  have hs_f : measurable_set[m] s_f := hfm.strongly_measurable_mk.measurable_set_support,
  have hs_f_eq : s_f =áµ[Î¼] function.support f := hfm.ae_eq_mk.symm.support,
  let s_g : set Î± := function.support (hgm.mk g),
  have hs_g : measurable_set[m] s_g := hgm.strongly_measurable_mk.measurable_set_support,
  have hs_g_eq : s_g =áµ[Î¼] function.support g := hgm.ae_eq_mk.symm.support,
  have h_inter_empty : ((s_f âˆ© s_g) : set Î±) =áµ[Î¼] (âˆ… : set Î±),
  { refine (hs_f_eq.inter hs_g_eq).trans _,
    suffices : function.support f âˆ© function.support g = âˆ…, by rw this,
    exact set.disjoint_iff_inter_eq_empty.mp h_disj, },
  let f' := (s_f \ s_g).indicator (hfm.mk f),
  have hff' : f =áµ[Î¼] f',
  { have : s_f \ s_g =áµ[Î¼] s_f,
    { rw [â† set.diff_inter_self_eq_diff, set.inter_comm],
      refine ((ae_eq_refl s_f).diff h_inter_empty).trans _,
      rw set.diff_empty, },
    refine ((indicator_ae_eq_of_ae_eq_set this).trans _).symm,
    rw set.indicator_support,
    exact hfm.ae_eq_mk.symm, },
  have hf'_meas : strongly_measurable[m] f',
    from hfm.strongly_measurable_mk.indicator (hs_f.diff hs_g),
  have hf'_Lp : mem_â„’p f' p Î¼ := hf.ae_eq hff',
  let g' := (s_g \ s_f).indicator (hgm.mk g),
  have hgg' : g =áµ[Î¼] g',
  { have : s_g \ s_f =áµ[Î¼] s_g,
    { rw [â† set.diff_inter_self_eq_diff],
      refine ((ae_eq_refl s_g).diff h_inter_empty).trans _,
      rw set.diff_empty, },
    refine ((indicator_ae_eq_of_ae_eq_set this).trans _).symm,
    rw set.indicator_support,
    exact hgm.ae_eq_mk.symm, },
  have hg'_meas : strongly_measurable[m] g',
    from hgm.strongly_measurable_mk.indicator (hs_g.diff hs_f),
  have hg'_Lp : mem_â„’p g' p Î¼ := hg.ae_eq hgg',
  have h_disj : disjoint (function.support f') (function.support g'),
  { have : disjoint (s_f \ s_g) (s_g \ s_f) := disjoint_sdiff_sdiff,
    exact this.mono set.support_indicator_subset set.support_indicator_subset, },
  rw â† mem_â„’p.to_Lp_congr hf'_Lp hf hff'.symm at âŠ¢ hPf,
  rw â† mem_â„’p.to_Lp_congr hg'_Lp hg hgg'.symm at âŠ¢ hPg,
  exact h_add hf'_Lp hg'_Lp hf'_meas hg'_meas h_disj hPf hPg,
end

/-- To prove something for an arbitrary `mem_â„’p` function a.e. strongly measurable with respect
to a sub-Ïƒ-algebra `m` in a normed space, it suffices to show that
* the property holds for (multiples of) characteristic functions which are measurable w.r.t. `m`;
* is closed under addition;
* the set of functions in the `Láµ–` space strongly measurable w.r.t. `m` for which the property
  holds is closed.
* the property is closed under the almost-everywhere equal relation.
-/
@[elab_as_eliminator]
lemma mem_â„’p.induction_strongly_measurable (hm : m â‰¤ m0) (hp_ne_top : p â‰  âˆ)
  (P : (Î± â†’ F) â†’ Prop)
  (h_ind : âˆ€ (c : F) â¦ƒsâ¦„, measurable_set[m] s â†’ Î¼ s < âˆ â†’ P (s.indicator (Î» _, c)))
  (h_add : âˆ€ â¦ƒf g : Î± â†’ Fâ¦„, disjoint (function.support f) (function.support g)
    â†’ mem_â„’p f p Î¼ â†’ mem_â„’p g p Î¼ â†’ strongly_measurable[m] f â†’ strongly_measurable[m] g â†’
    P f â†’ P g â†’ P (f + g))
  (h_closed : is_closed {f : Lp_meas F â„ m p Î¼ | P f} )
  (h_ae : âˆ€ â¦ƒf gâ¦„, f =áµ[Î¼] g â†’ mem_â„’p f p Î¼ â†’ P f â†’ P g) :
  âˆ€ â¦ƒf : Î± â†’ Fâ¦„ (hf : mem_â„’p f p Î¼) (hfm : ae_strongly_measurable' m f Î¼), P f :=
begin
  intros f hf hfm,
  let f_Lp := hf.to_Lp f,
  have hfm_Lp : ae_strongly_measurable' m f_Lp Î¼, from hfm.congr hf.coe_fn_to_Lp.symm,
  refine h_ae (hf.coe_fn_to_Lp) (Lp.mem_â„’p _) _,
  change P f_Lp,
  refine Lp.induction_strongly_measurable hm hp_ne_top (Î» f, P â‡‘f) _ _ h_closed f_Lp hfm_Lp,
  { intros c s hs hÎ¼s,
    rw Lp.simple_func.coe_indicator_const,
    refine h_ae (indicator_const_Lp_coe_fn).symm _ (h_ind c hs hÎ¼s),
    exact mem_â„’p_indicator_const p (hm s hs) c (or.inr hÎ¼s.ne), },
  { intros f g hf_mem hg_mem hfm hgm h_disj hfP hgP,
    have hfP' : P f := h_ae (hf_mem.coe_fn_to_Lp) (Lp.mem_â„’p _) hfP,
    have hgP' : P g := h_ae (hg_mem.coe_fn_to_Lp) (Lp.mem_â„’p _) hgP,
    specialize h_add h_disj hf_mem hg_mem hfm hgm hfP' hgP',
    refine h_ae _ (hf_mem.add hg_mem) h_add,
    exact ((hf_mem.coe_fn_to_Lp).symm.add (hg_mem.coe_fn_to_Lp).symm).trans
      (Lp.coe_fn_add _ _).symm, },
end

end induction

end measure_theory
