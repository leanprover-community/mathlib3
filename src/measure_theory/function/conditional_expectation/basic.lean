/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import measure_theory.function.conditional_expectation.condexp_L1

/-! # Conditional expectation

We build the conditional expectation of an integrable function `f` with value in a Banach space
with respect to a measure `Œº` (defined on a measurable space structure `m0`) and a measurable space
structure `m` with `hm : m ‚â§ m0` (a sub-sigma-algebra). This is an `m`-strongly measurable
function `Œº[f|hm]` which is integrable and verifies `‚à´ x in s, Œº[f|hm] x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº`
for all `m`-measurable sets `s`. It is unique as an element of `L¬π`.

The construction is done in four steps:
* Define the conditional expectation of an `L¬≤` function, as an element of `L¬≤`. This is the
  orthogonal projection on the subspace of almost everywhere `m`-measurable functions.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `set Œ± ‚Üí (E ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E))` which to a set associates a linear
  map. That linear map sends `x ‚àà E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condexp_L1_clm : (Œ± ‚Üí‚ÇÅ[Œº] E) ‚ÜíL[‚Ñù] (Œ± ‚Üí‚ÇÅ[Œº] E)`. This is done using the same
  construction as the Bochner integral (see the file `measure_theory/integral/set_to_L1`).
* Define the conditional expectation of a function `f : Œ± ‚Üí E`, which is an integrable function
  `Œ± ‚Üí E` equal to 0 if `f` is not integrable, and equal to an `m`-measurable representative of
  `condexp_L1_clm` applied to `[f]`, the equivalence class of `f` in `L¬π`.

The first step is done in `measure_theory.function.conditional_expectation.condexp_L2`, the two
next steps in `measure_theory.function.conditional_expectation.condexp_L1` and the final step is
performed in this file.

## Main results

The conditional expectation and its properties

* `condexp (m : measurable_space Œ±) (Œº : measure Œ±) (f : Œ± ‚Üí E)`: conditional expectation of `f`
  with respect to `m`.
* `integrable_condexp` : `condexp` is integrable.
* `strongly_measurable_condexp` : `condexp` is `m`-strongly-measurable.
* `set_integral_condexp (hf : integrable f Œº) (hs : measurable_set[m] s)` : if `m ‚â§ m0` (the
  œÉ-algebra over which the measure is defined), then the conditional expectation verifies
  `‚à´ x in s, condexp m Œº f x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº` for any `m`-measurable set `s`.

While `condexp` is function-valued, we also define `condexp_L1` with value in `L1` and a continuous
linear map `condexp_L1_clm` from `L1` to `L1`. `condexp` should be used in most cases.

Uniqueness of the conditional expectation

* `ae_eq_condexp_of_forall_set_integral_eq`: an a.e. `m`-measurable function which verifies the
  equality of integrals is a.e. equal to `condexp`.

## Notations

For a measure `Œº` defined on a measurable space structure `m0`, another measurable space structure
`m` with `hm : m ‚â§ m0` (a sub-œÉ-algebra) and a function `f`, we define the notation
* `Œº[f|m] = condexp m Œº f`.

## Tags

conditional expectation, conditional expected value

-/

open topological_space measure_theory.Lp filter
open_locale ennreal topology big_operators measure_theory

namespace measure_theory

variables {Œ± F F' ùïú : Type*} {p : ‚Ñù‚â•0‚àû}
  [is_R_or_C ùïú] -- ùïú for ‚Ñù or ‚ÑÇ
  -- F for a Lp submodule
  [normed_add_comm_group F] [normed_space ùïú F]
  -- F' for integrals on a Lp submodule
  [normed_add_comm_group F'] [normed_space ùïú F'] [normed_space ‚Ñù F'] [complete_space F']

open_locale classical

variables {ùïú} {m m0 : measurable_space Œ±} {Œº : measure Œ±} {f g : Œ± ‚Üí F'} {s : set Œ±}

/-- Conditional expectation of a function. It is defined as 0 if any one of the following conditions
is true:
- `m` is not a sub-œÉ-algebra of `m0`,
- `Œº` is not œÉ-finite with respect to `m`,
- `f` is not integrable. -/
@[irreducible]
noncomputable
def condexp (m : measurable_space Œ±) {m0 : measurable_space Œ±} (Œº : measure Œ±) (f : Œ± ‚Üí F') :
  Œ± ‚Üí F' :=
if hm : m ‚â§ m0
  then if h : sigma_finite (Œº.trim hm) ‚àß integrable f Œº
    then if strongly_measurable[m] f
      then f
      else (@ae_strongly_measurable'_condexp_L1 _ _ _ _ _ m m0 Œº hm h.1 _).mk
        (@condexp_L1 _ _ _ _ _ _ _ hm Œº h.1 f)
    else 0
  else 0

-- We define notation `Œº[f|m]` for the conditional expectation of `f` with respect to `m`.
localized "notation (name := measure_theory.condexp)
  Œº `[` f `|` m `]` := measure_theory.condexp m Œº f" in measure_theory

lemma condexp_of_not_le (hm_not : ¬¨ m ‚â§ m0) : Œº[f|m] = 0 := by rw [condexp, dif_neg hm_not]

lemma condexp_of_not_sigma_finite (hm : m ‚â§ m0) (hŒºm_not : ¬¨ sigma_finite (Œº.trim hm)) :
  Œº[f|m] = 0 :=
by { rw [condexp, dif_pos hm, dif_neg], push_neg, exact Œª h, absurd h hŒºm_not, }

lemma condexp_of_sigma_finite (hm : m ‚â§ m0) [hŒºm : sigma_finite (Œº.trim hm)] :
  Œº[f|m] =
  if integrable f Œº
    then if strongly_measurable[m] f
      then f else ae_strongly_measurable'_condexp_L1.mk (condexp_L1 hm Œº f)
    else 0 :=
begin
  rw [condexp, dif_pos hm],
  simp only [hŒºm, ne.def, true_and],
  by_cases hf : integrable f Œº,
  { rw [dif_pos hf, if_pos hf], },
  { rw [dif_neg hf, if_neg hf], },
end

lemma condexp_of_strongly_measurable (hm : m ‚â§ m0) [hŒºm : sigma_finite (Œº.trim hm)]
  {f : Œ± ‚Üí F'} (hf : strongly_measurable[m] f) (hfi : integrable f Œº) :
  Œº[f|m] = f :=
by { rw [condexp_of_sigma_finite hm, if_pos hfi, if_pos hf], apply_instance, }

lemma condexp_const (hm : m ‚â§ m0) (c : F') [is_finite_measure Œº] : Œº[(Œª x : Œ±, c)|m] = Œª _, c :=
condexp_of_strongly_measurable hm (@strongly_measurable_const _ _ m _ _) (integrable_const c)

lemma condexp_ae_eq_condexp_L1 (hm : m ‚â§ m0) [hŒºm : sigma_finite (Œº.trim hm)]
  (f : Œ± ‚Üí F') : Œº[f|m] =·µê[Œº] condexp_L1 hm Œº f :=
begin
  rw condexp_of_sigma_finite hm,
  by_cases hfi : integrable f Œº,
  { rw if_pos hfi,
    by_cases hfm : strongly_measurable[m] f,
    { rw if_pos hfm,
      exact (condexp_L1_of_ae_strongly_measurable'
        (strongly_measurable.ae_strongly_measurable' hfm) hfi).symm, },
    { rw if_neg hfm,
      exact (ae_strongly_measurable'.ae_eq_mk ae_strongly_measurable'_condexp_L1).symm, }, },
  rw [if_neg hfi, condexp_L1_undef hfi],
  exact (coe_fn_zero _ _ _).symm,
end

lemma condexp_ae_eq_condexp_L1_clm (hm : m ‚â§ m0) [sigma_finite (Œº.trim hm)] (hf : integrable f Œº) :
  Œº[f|m] =·µê[Œº] condexp_L1_clm hm Œº (hf.to_L1 f) :=
begin
  refine (condexp_ae_eq_condexp_L1 hm f).trans (eventually_of_forall (Œª x, _)),
  rw condexp_L1_eq hf,
end

lemma condexp_undef (hf : ¬¨ integrable f Œº) : Œº[f|m] = 0 :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { rw condexp_of_not_le hm, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { rw condexp_of_not_sigma_finite hm hŒºm, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  rw [condexp_of_sigma_finite, if_neg hf],
end

@[simp] lemma condexp_zero : Œº[(0 : Œ± ‚Üí F')|m] = 0 :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { rw condexp_of_not_le hm, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { rw condexp_of_not_sigma_finite hm hŒºm, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  exact condexp_of_strongly_measurable hm (@strongly_measurable_zero _ _ m _ _)
    (integrable_zero _ _ _),
end

lemma strongly_measurable_condexp : strongly_measurable[m] (Œº[f|m]) :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { rw condexp_of_not_le hm, exact strongly_measurable_zero, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { rw condexp_of_not_sigma_finite hm hŒºm, exact strongly_measurable_zero, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  rw condexp_of_sigma_finite hm,
  swap, { apply_instance, },
  split_ifs with hfi hfm,
  { exact hfm, },
  { exact ae_strongly_measurable'.strongly_measurable_mk _, },
  { exact strongly_measurable_zero, },
end

lemma condexp_congr_ae (h : f =·µê[Œº] g) : Œº[f | m] =·µê[Œº] Œº[g | m] :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { simp_rw condexp_of_not_le hm, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { simp_rw condexp_of_not_sigma_finite hm hŒºm, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  exact (condexp_ae_eq_condexp_L1 hm f).trans
    (filter.eventually_eq.trans (by rw condexp_L1_congr_ae hm h)
    (condexp_ae_eq_condexp_L1 hm g).symm),
end

lemma condexp_of_ae_strongly_measurable' (hm : m ‚â§ m0) [hŒºm : sigma_finite (Œº.trim hm)]
  {f : Œ± ‚Üí F'} (hf : ae_strongly_measurable' m f Œº) (hfi : integrable f Œº) :
  Œº[f|m] =·µê[Œº] f :=
begin
  refine ((condexp_congr_ae hf.ae_eq_mk).trans _).trans hf.ae_eq_mk.symm,
  rw condexp_of_strongly_measurable hm hf.strongly_measurable_mk
    ((integrable_congr hf.ae_eq_mk).mp hfi),
end

lemma integrable_condexp : integrable (Œº[f|m]) Œº :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { rw condexp_of_not_le hm, exact integrable_zero _ _ _, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { rw condexp_of_not_sigma_finite hm hŒºm, exact integrable_zero _ _ _, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  exact (integrable_condexp_L1 f).congr (condexp_ae_eq_condexp_L1 hm f).symm,
end

/-- The integral of the conditional expectation `Œº[f|hm]` over an `m`-measurable set is equal to
the integral of `f` on that set. -/
lemma set_integral_condexp (hm : m ‚â§ m0) [sigma_finite (Œº.trim hm)]
  (hf : integrable f Œº) (hs : measurable_set[m] s) :
  ‚à´ x in s, Œº[f|m] x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº :=
begin
  rw set_integral_congr_ae (hm s hs) ((condexp_ae_eq_condexp_L1 hm f).mono (Œª x hx _, hx)),
  exact set_integral_condexp_L1 hf hs,
end

lemma integral_condexp (hm : m ‚â§ m0) [hŒºm : sigma_finite (Œº.trim hm)]
  (hf : integrable f Œº) : ‚à´ x, Œº[f|m] x ‚àÇŒº = ‚à´ x, f x ‚àÇŒº :=
begin
  suffices : ‚à´ x in set.univ, Œº[f|m] x ‚àÇŒº = ‚à´ x in set.univ, f x ‚àÇŒº,
    by { simp_rw integral_univ at this, exact this, },
  exact set_integral_condexp hm hf (@measurable_set.univ _ m),
end

/-- **Uniqueness of the conditional expectation**
If a function is a.e. `m`-measurable, verifies an integrability condition and has same integral
as `f` on all `m`-measurable sets, then it is a.e. equal to `Œº[f|hm]`. -/
lemma ae_eq_condexp_of_forall_set_integral_eq (hm : m ‚â§ m0) [sigma_finite (Œº.trim hm)]
  {f g : Œ± ‚Üí F'} (hf : integrable f Œº)
  (hg_int_finite : ‚àÄ s, measurable_set[m] s ‚Üí Œº s < ‚àû ‚Üí integrable_on g s Œº)
  (hg_eq : ‚àÄ s : set Œ±, measurable_set[m] s ‚Üí Œº s < ‚àû ‚Üí ‚à´ x in s, g x ‚àÇŒº = ‚à´ x in s, f x ‚àÇŒº)
  (hgm : ae_strongly_measurable' m g Œº) :
  g =·µê[Œº] Œº[f|m] :=
begin
  refine ae_eq_of_forall_set_integral_eq_of_sigma_finite' hm hg_int_finite
    (Œª s hs hŒºs, integrable_condexp.integrable_on) (Œª s hs hŒºs, _) hgm
    (strongly_measurable.ae_strongly_measurable' strongly_measurable_condexp),
  rw [hg_eq s hs hŒºs, set_integral_condexp hm hf hs],
end

lemma condexp_bot' [hŒº : Œº.ae.ne_bot] (f : Œ± ‚Üí F') :
  Œº[f|‚ä•] = Œª _, (Œº set.univ).to_real‚Åª¬π ‚Ä¢ ‚à´ x, f x ‚àÇŒº :=
begin
  by_cases hŒº_finite : is_finite_measure Œº,
  swap,
  { have h : ¬¨ sigma_finite (Œº.trim bot_le),
    { rwa sigma_finite_trim_bot_iff, },
    rw not_is_finite_measure_iff at hŒº_finite,
    rw [condexp_of_not_sigma_finite bot_le h],
    simp only [hŒº_finite, ennreal.top_to_real, inv_zero, zero_smul],
    refl, },
  haveI : is_finite_measure Œº := hŒº_finite,
  by_cases hf : integrable f Œº,
  swap, { rw [integral_undef hf, smul_zero, condexp_undef hf], refl, },
  have h_meas : strongly_measurable[‚ä•] (Œº[f|‚ä•]) := strongly_measurable_condexp,
  obtain ‚ü®c, h_eq‚ü© := strongly_measurable_bot_iff.mp h_meas,
  rw h_eq,
  have h_integral : ‚à´ x, Œº[f|‚ä•] x ‚àÇŒº = ‚à´ x, f x ‚àÇŒº := integral_condexp bot_le hf,
  simp_rw [h_eq, integral_const] at h_integral,
  rw [‚Üê h_integral, ‚Üê smul_assoc, smul_eq_mul, inv_mul_cancel, one_smul],
  rw [ne.def, ennreal.to_real_eq_zero_iff, auto.not_or_eq, measure.measure_univ_eq_zero,
    ‚Üê ae_eq_bot, ‚Üê ne.def, ‚Üê ne_bot_iff],
  exact ‚ü®hŒº, measure_ne_top Œº set.univ‚ü©,
end

lemma condexp_bot_ae_eq (f : Œ± ‚Üí F') :
  Œº[f|‚ä•] =·µê[Œº] Œª _, (Œº set.univ).to_real‚Åª¬π ‚Ä¢ ‚à´ x, f x ‚àÇŒº :=
begin
  by_cases Œº.ae.ne_bot,
  { refine eventually_of_forall (Œª x, _),
    rw condexp_bot' f,
    exact h, },
  { rw [ne_bot_iff, not_not, ae_eq_bot] at h,
    simp only [h, ae_zero], },
end

lemma condexp_bot [is_probability_measure Œº] (f : Œ± ‚Üí F') :
  Œº[f|‚ä•] = Œª _, ‚à´ x, f x ‚àÇŒº :=
by { refine (condexp_bot' f).trans _, rw [measure_univ, ennreal.one_to_real, inv_one, one_smul], }

lemma condexp_add (hf : integrable f Œº) (hg : integrable g Œº) :
  Œº[f + g | m] =·µê[Œº] Œº[f|m] + Œº[g|m] :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { simp_rw condexp_of_not_le hm, simp, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { simp_rw condexp_of_not_sigma_finite hm hŒºm, simp, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  refine (condexp_ae_eq_condexp_L1 hm _).trans _,
  rw condexp_L1_add hf hg,
  exact (coe_fn_add _ _).trans
    ((condexp_ae_eq_condexp_L1 hm _).symm.add (condexp_ae_eq_condexp_L1 hm _).symm),
end

lemma condexp_finset_sum {Œπ : Type*} {s : finset Œπ} {f : Œπ ‚Üí Œ± ‚Üí F'}
  (hf : ‚àÄ i ‚àà s, integrable (f i) Œº) :
  Œº[‚àë i in s, f i | m] =·µê[Œº] ‚àë i in s, Œº[f i | m] :=
begin
  induction s using finset.induction_on with i s his heq hf,
  { rw [finset.sum_empty, finset.sum_empty, condexp_zero] },
  { rw [finset.sum_insert his, finset.sum_insert his],
    exact (condexp_add (hf i $ finset.mem_insert_self i s) $ integrable_finset_sum' _
      (Œª j hmem, hf j $ finset.mem_insert_of_mem hmem)).trans
      ((eventually_eq.refl _ _).add (heq $ Œª j hmem, hf j $ finset.mem_insert_of_mem hmem)) }
end

lemma condexp_smul (c : ùïú) (f : Œ± ‚Üí F') : Œº[c ‚Ä¢ f | m] =·µê[Œº] c ‚Ä¢ Œº[f|m] :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { simp_rw condexp_of_not_le hm, simp, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { simp_rw condexp_of_not_sigma_finite hm hŒºm, simp, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  refine (condexp_ae_eq_condexp_L1 hm _).trans _,
  rw condexp_L1_smul c f,
  refine (@condexp_ae_eq_condexp_L1 _ _ _ _ _ m _ _ hm _ f).mp _,
  refine (coe_fn_smul c (condexp_L1 hm Œº f)).mono (Œª x hx1 hx2, _),
  rw [hx1, pi.smul_apply, pi.smul_apply, hx2],
end

lemma condexp_neg (f : Œ± ‚Üí F') : Œº[-f|m] =·µê[Œº] - Œº[f|m] :=
by letI : module ‚Ñù (Œ± ‚Üí F') := @pi.module Œ± (Œª _, F') ‚Ñù _ _ (Œª _, infer_instance);
calc Œº[-f|m] = Œº[(-1 : ‚Ñù) ‚Ä¢ f|m] : by rw neg_one_smul ‚Ñù f
... =·µê[Œº] (-1 : ‚Ñù) ‚Ä¢ Œº[f|m] : condexp_smul (-1) f
... = -Œº[f|m] : neg_one_smul ‚Ñù (Œº[f|m])

lemma condexp_sub (hf : integrable f Œº) (hg : integrable g Œº) :
  Œº[f - g | m] =·µê[Œº] Œº[f|m] - Œº[g|m] :=
begin
  simp_rw sub_eq_add_neg,
  exact (condexp_add hf hg.neg).trans (eventually_eq.rfl.add (condexp_neg g)),
end

lemma condexp_condexp_of_le {m‚ÇÅ m‚ÇÇ m0 : measurable_space Œ±} {Œº : measure Œ±} (hm‚ÇÅ‚ÇÇ : m‚ÇÅ ‚â§ m‚ÇÇ)
  (hm‚ÇÇ : m‚ÇÇ ‚â§ m0) [sigma_finite (Œº.trim hm‚ÇÇ)] :
  Œº[ Œº[f|m‚ÇÇ] | m‚ÇÅ] =·µê[Œº] Œº[f | m‚ÇÅ] :=
begin
  by_cases hŒºm‚ÇÅ : sigma_finite (Œº.trim (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ)),
  swap, { simp_rw condexp_of_not_sigma_finite (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ) hŒºm‚ÇÅ, },
  haveI : sigma_finite (Œº.trim (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ)) := hŒºm‚ÇÅ,
  by_cases hf : integrable f Œº,
  swap, { simp_rw [condexp_undef hf, condexp_zero], },
  refine ae_eq_of_forall_set_integral_eq_of_sigma_finite' (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ)
    (Œª s hs hŒºs, integrable_condexp.integrable_on) (Œª s hs hŒºs, integrable_condexp.integrable_on)
    _ (strongly_measurable.ae_strongly_measurable' strongly_measurable_condexp)
      (strongly_measurable.ae_strongly_measurable' strongly_measurable_condexp),
  intros s hs hŒºs,
  rw set_integral_condexp (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ) integrable_condexp hs,
  swap, { apply_instance, },
  rw [set_integral_condexp (hm‚ÇÅ‚ÇÇ.trans hm‚ÇÇ) hf hs, set_integral_condexp hm‚ÇÇ hf (hm‚ÇÅ‚ÇÇ s hs)],
end

lemma condexp_mono {E} [normed_lattice_add_comm_group E] [complete_space E] [normed_space ‚Ñù E]
  [ordered_smul ‚Ñù E] {f g : Œ± ‚Üí E} (hf : integrable f Œº) (hg : integrable g Œº) (hfg : f ‚â§·µê[Œº] g) :
  Œº[f | m] ‚â§·µê[Œº] Œº[g | m] :=
begin
  by_cases hm : m ‚â§ m0,
  swap, { simp_rw condexp_of_not_le hm, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { simp_rw condexp_of_not_sigma_finite hm hŒºm, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  exact (condexp_ae_eq_condexp_L1 hm _).trans_le
    ((condexp_L1_mono hf hg hfg).trans_eq (condexp_ae_eq_condexp_L1 hm _).symm),
end

lemma condexp_nonneg {E} [normed_lattice_add_comm_group E] [complete_space E] [normed_space ‚Ñù E]
  [ordered_smul ‚Ñù E] {f : Œ± ‚Üí E} (hf : 0 ‚â§·µê[Œº] f) :
  0 ‚â§·µê[Œº] Œº[f | m] :=
begin
  by_cases hfint : integrable f Œº,
  { rw (condexp_zero.symm : (0 : Œ± ‚Üí E) = Œº[0 | m]),
    exact condexp_mono (integrable_zero _ _ _) hfint hf },
  { rw condexp_undef hfint, }
end

lemma condexp_nonpos {E} [normed_lattice_add_comm_group E] [complete_space E] [normed_space ‚Ñù E]
  [ordered_smul ‚Ñù E] {f : Œ± ‚Üí E} (hf : f ‚â§·µê[Œº] 0) :
  Œº[f | m] ‚â§·µê[Œº] 0 :=
begin
  by_cases hfint : integrable f Œº,
  { rw (condexp_zero.symm : (0 : Œ± ‚Üí E) = Œº[0 | m]),
    exact condexp_mono hfint (integrable_zero _ _ _) hf },
  { rw condexp_undef hfint, }
end

/-- **Lebesgue dominated convergence theorem**: sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their image by
  `condexp_L1`. -/
lemma tendsto_condexp_L1_of_dominated_convergence (hm : m ‚â§ m0) [sigma_finite (Œº.trim hm)]
  {fs : ‚Ñï ‚Üí Œ± ‚Üí F'} {f : Œ± ‚Üí F'} (bound_fs : Œ± ‚Üí ‚Ñù)
  (hfs_meas : ‚àÄ n, ae_strongly_measurable (fs n) Œº) (h_int_bound_fs : integrable bound_fs Œº)
  (hfs_bound : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, ‚Äñfs n x‚Äñ ‚â§ bound_fs x)
  (hfs : ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, fs n x) at_top (ùìù (f x))) :
  tendsto (Œª n, condexp_L1 hm Œº (fs n)) at_top (ùìù (condexp_L1 hm Œº f)) :=
tendsto_set_to_fun_of_dominated_convergence _ bound_fs hfs_meas h_int_bound_fs hfs_bound hfs

/-- If two sequences of functions have a.e. equal conditional expectations at each step, converge
and verify dominated convergence hypotheses, then the conditional expectations of their limits are
a.e. equal. -/
lemma tendsto_condexp_unique (fs gs : ‚Ñï ‚Üí Œ± ‚Üí F') (f g : Œ± ‚Üí F')
  (hfs_int : ‚àÄ n, integrable (fs n) Œº) (hgs_int : ‚àÄ n, integrable (gs n) Œº)
  (hfs : ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, fs n x) at_top (ùìù (f x)))
  (hgs : ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, gs n x) at_top (ùìù (g x)))
  (bound_fs : Œ± ‚Üí ‚Ñù) (h_int_bound_fs : integrable bound_fs Œº)
  (bound_gs : Œ± ‚Üí ‚Ñù) (h_int_bound_gs : integrable bound_gs Œº)
  (hfs_bound : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, ‚Äñfs n x‚Äñ ‚â§ bound_fs x)
  (hgs_bound : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, ‚Äñgs n x‚Äñ ‚â§ bound_gs x)
  (hfg : ‚àÄ n, Œº[fs n | m] =·µê[Œº] Œº[gs n | m]) :
  Œº[f | m] =·µê[Œº] Œº[g | m] :=
begin
  by_cases hm : m ‚â§ m0, swap, { simp_rw condexp_of_not_le hm, },
  by_cases hŒºm : sigma_finite (Œº.trim hm), swap, { simp_rw condexp_of_not_sigma_finite hm hŒºm, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  refine (condexp_ae_eq_condexp_L1 hm f).trans ((condexp_ae_eq_condexp_L1 hm g).trans _).symm,
  rw ‚Üê Lp.ext_iff,
  have hn_eq : ‚àÄ n, condexp_L1 hm Œº (gs n) = condexp_L1 hm Œº (fs n),
  { intros n,
    ext1,
    refine (condexp_ae_eq_condexp_L1 hm (gs n)).symm.trans ((hfg n).symm.trans _),
    exact (condexp_ae_eq_condexp_L1 hm (fs n)), },
  have hcond_fs : tendsto (Œª n, condexp_L1 hm Œº (fs n)) at_top (ùìù (condexp_L1 hm Œº f)),
    from tendsto_condexp_L1_of_dominated_convergence hm _ (Œª n, (hfs_int n).1) h_int_bound_fs
       hfs_bound hfs,
  have hcond_gs : tendsto (Œª n, condexp_L1 hm Œº (gs n)) at_top (ùìù (condexp_L1 hm Œº g)),
    from tendsto_condexp_L1_of_dominated_convergence hm _ (Œª n, (hgs_int n).1) h_int_bound_gs
       hgs_bound hgs,
  exact tendsto_nhds_unique_of_eventually_eq hcond_gs hcond_fs (eventually_of_forall hn_eq),
end

end measure_theory
