/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import measure_theory.function.conditional_expectation.condexp_L2

/-! # Conditional expectation in L1

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains two more steps of the construction of the conditional expectation, which is
completed in `measure_theory.function.conditional_expectation.basic`. See that file for a
description of the full process.

The contitional expectation of an `LÂ²` function is defined in
`measure_theory.function.conditional_expectation.condexp_L2`. In this file, we perform two steps.
* Show that the conditional expectation of the indicator of a measurable set with finite measure
  is integrable and define a map `set Î± â†’ (E â†’L[â„] (Î± â†’â‚[Î¼] E))` which to a set associates a linear
  map. That linear map sends `x âˆˆ E` to the conditional expectation of the indicator of the set
  with value `x`.
* Extend that map to `condexp_L1_clm : (Î± â†’â‚[Î¼] E) â†’L[â„] (Î± â†’â‚[Î¼] E)`. This is done using the same
  construction as the Bochner integral (see the file `measure_theory/integral/set_to_L1`).

## Main definitions

* `condexp_L1`: Conditional expectation of a function as a linear map from `L1` to itself.

-/

noncomputable theory
open topological_space measure_theory.Lp filter continuous_linear_map
open_locale nnreal ennreal topology big_operators measure_theory

namespace measure_theory

variables {Î± Î² F F' G G' ğ•œ : Type*} {p : â„â‰¥0âˆ}
  [is_R_or_C ğ•œ] -- ğ•œ for â„ or â„‚
  -- F for a Lp submodule
  [normed_add_comm_group F] [normed_space ğ•œ F]
  -- F' for integrals on a Lp submodule
  [normed_add_comm_group F'] [normed_space ğ•œ F'] [normed_space â„ F'] [complete_space F']
  -- G for a Lp add_subgroup
  [normed_add_comm_group G]
  -- G' for integrals on a Lp add_subgroup
  [normed_add_comm_group G'] [normed_space â„ G'] [complete_space G']

section condexp_ind

/-! ## Conditional expectation of an indicator as a continuous linear map.

The goal of this section is to build
`condexp_ind (hm : m â‰¤ m0) (Î¼ : measure Î±) (s : set s) : G â†’L[â„] Î± â†’â‚[Î¼] G`, which
takes `x : G` to the conditional expectation of the indicator of the set `s` with value `x`,
seen as an element of `Î± â†’â‚[Î¼] G`.
-/

variables {m m0 : measurable_space Î±} {Î¼ : measure Î±} {s t : set Î±} [normed_space â„ G]

section condexp_ind_L1_fin

/-- Conditional expectation of the indicator of a measurable set with finite measure,
as a function in L1. -/
def condexp_ind_L1_fin (hm : m â‰¤ m0) [sigma_finite (Î¼.trim hm)] (hs : measurable_set s)
  (hÎ¼s : Î¼ s â‰  âˆ) (x : G) : Î± â†’â‚[Î¼] G :=
(integrable_condexp_ind_smul hm hs hÎ¼s x).to_L1 _

lemma condexp_ind_L1_fin_ae_eq_condexp_ind_smul (hm : m â‰¤ m0) [sigma_finite (Î¼.trim hm)]
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
  condexp_ind_L1_fin hm hs hÎ¼s x =áµ[Î¼] condexp_ind_smul hm hs hÎ¼s x :=
(integrable_condexp_ind_smul hm hs hÎ¼s x).coe_fn_to_L1

variables {hm : m â‰¤ m0} [sigma_finite (Î¼.trim hm)]

lemma condexp_ind_L1_fin_add (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x y : G) :
  condexp_ind_L1_fin hm hs hÎ¼s (x + y)
    = condexp_ind_L1_fin hm hs hÎ¼s x + condexp_ind_L1_fin hm hs hÎ¼s y :=
begin
  ext1,
  refine (mem_â„’p.coe_fn_to_Lp _).trans _,
  refine eventually_eq.trans _ (Lp.coe_fn_add _ _).symm,
  refine eventually_eq.trans _
    (eventually_eq.add (mem_â„’p.coe_fn_to_Lp _).symm (mem_â„’p.coe_fn_to_Lp _).symm),
  rw condexp_ind_smul_add,
  refine (Lp.coe_fn_add _ _).trans (eventually_of_forall (Î» a, _)),
  refl,
end

lemma condexp_ind_L1_fin_smul (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (c : â„) (x : G) :
  condexp_ind_L1_fin hm hs hÎ¼s (c â€¢ x) = c â€¢ condexp_ind_L1_fin hm hs hÎ¼s x :=
begin
  ext1,
  refine (mem_â„’p.coe_fn_to_Lp _).trans _,
  refine eventually_eq.trans _ (Lp.coe_fn_smul _ _).symm,
  rw condexp_ind_smul_smul hs hÎ¼s c x,
  refine (Lp.coe_fn_smul _ _).trans _,
  refine (condexp_ind_L1_fin_ae_eq_condexp_ind_smul hm hs hÎ¼s x).mono (Î» y hy, _),
  rw [pi.smul_apply, pi.smul_apply, hy],
end

lemma condexp_ind_L1_fin_smul' [normed_space â„ F] [smul_comm_class â„ ğ•œ F]
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (c : ğ•œ) (x : F) :
  condexp_ind_L1_fin hm hs hÎ¼s (c â€¢ x) = c â€¢ condexp_ind_L1_fin hm hs hÎ¼s x :=
begin
  ext1,
  refine (mem_â„’p.coe_fn_to_Lp _).trans _,
  refine eventually_eq.trans _ (Lp.coe_fn_smul _ _).symm,
  rw condexp_ind_smul_smul' hs hÎ¼s c x,
  refine (Lp.coe_fn_smul _ _).trans _,
  refine (condexp_ind_L1_fin_ae_eq_condexp_ind_smul hm hs hÎ¼s x).mono (Î» y hy, _),
  rw [pi.smul_apply, pi.smul_apply, hy],
end

lemma norm_condexp_ind_L1_fin_le (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
  â€–condexp_ind_L1_fin hm hs hÎ¼s xâ€– â‰¤ (Î¼ s).to_real * â€–xâ€– :=
begin
  have : 0 â‰¤ âˆ« (a : Î±), â€–condexp_ind_L1_fin hm hs hÎ¼s x aâ€– âˆ‚Î¼,
    from integral_nonneg (Î» a, norm_nonneg _),
  rw [L1.norm_eq_integral_norm, â† ennreal.to_real_of_real (norm_nonneg x), â† ennreal.to_real_mul,
    â† ennreal.to_real_of_real this, ennreal.to_real_le_to_real ennreal.of_real_ne_top
      (ennreal.mul_ne_top hÎ¼s ennreal.of_real_ne_top),
    of_real_integral_norm_eq_lintegral_nnnorm],
  swap, { rw [â† mem_â„’p_one_iff_integrable], exact Lp.mem_â„’p _, },
  have h_eq : âˆ«â» a, â€–condexp_ind_L1_fin hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼
    = âˆ«â» a, â€–condexp_ind_smul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼,
  { refine lintegral_congr_ae _,
    refine (condexp_ind_L1_fin_ae_eq_condexp_ind_smul hm hs hÎ¼s x).mono (Î» z hz, _),
    dsimp only,
    rw hz, },
  rw [h_eq, of_real_norm_eq_coe_nnnorm],
  exact lintegral_nnnorm_condexp_ind_smul_le hm hs hÎ¼s x,
end

lemma condexp_ind_L1_fin_disjoint_union (hs : measurable_set s) (ht : measurable_set t)
  (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (hst : s âˆ© t = âˆ…) (x : G) :
  condexp_ind_L1_fin hm (hs.union ht) ((measure_union_le s t).trans_lt
    (lt_top_iff_ne_top.mpr (ennreal.add_ne_top.mpr âŸ¨hÎ¼s, hÎ¼tâŸ©))).ne x
  = condexp_ind_L1_fin hm hs hÎ¼s x + condexp_ind_L1_fin hm ht hÎ¼t x :=
begin
  ext1,
  have hÎ¼st := ((measure_union_le s t).trans_lt
    (lt_top_iff_ne_top.mpr (ennreal.add_ne_top.mpr âŸ¨hÎ¼s, hÎ¼tâŸ©))).ne,
  refine (condexp_ind_L1_fin_ae_eq_condexp_ind_smul hm (hs.union ht) hÎ¼st x).trans _,
  refine eventually_eq.trans _ (Lp.coe_fn_add _ _).symm,
  have hs_eq := condexp_ind_L1_fin_ae_eq_condexp_ind_smul hm hs hÎ¼s x,
  have ht_eq := condexp_ind_L1_fin_ae_eq_condexp_ind_smul hm ht hÎ¼t x,
  refine eventually_eq.trans _ (eventually_eq.add hs_eq.symm ht_eq.symm),
  rw condexp_ind_smul,
  rw indicator_const_Lp_disjoint_union hs ht hÎ¼s hÎ¼t hst (1 : â„),
  rw (condexp_L2 â„ hm).map_add,
  push_cast,
  rw ((to_span_singleton â„ x).comp_LpL 2 Î¼).map_add,
  refine (Lp.coe_fn_add _ _).trans _,
  refine eventually_of_forall (Î» y, _),
  refl,
end

end condexp_ind_L1_fin

open_locale classical

section condexp_ind_L1

/-- Conditional expectation of the indicator of a set, as a function in L1. Its value for sets
which are not both measurable and of finite measure is not used: we set it to 0. -/
def condexp_ind_L1 {m m0 : measurable_space Î±} (hm : m â‰¤ m0) (Î¼ : measure Î±) (s : set Î±)
  [sigma_finite (Î¼.trim hm)] (x : G) :
  Î± â†’â‚[Î¼] G :=
if hs : measurable_set s âˆ§ Î¼ s â‰  âˆ then condexp_ind_L1_fin hm hs.1 hs.2 x else 0

variables {hm : m â‰¤ m0} [sigma_finite (Î¼.trim hm)]

lemma condexp_ind_L1_of_measurable_set_of_measure_ne_top (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ)
  (x : G) :
  condexp_ind_L1 hm Î¼ s x = condexp_ind_L1_fin hm hs hÎ¼s x :=
by simp only [condexp_ind_L1, and.intro hs hÎ¼s, dif_pos, ne.def, not_false_iff, and_self]

lemma condexp_ind_L1_of_measure_eq_top (hÎ¼s : Î¼ s = âˆ) (x : G) :
  condexp_ind_L1 hm Î¼ s x = 0 :=
by simp only [condexp_ind_L1, hÎ¼s, eq_self_iff_true, not_true, ne.def, dif_neg, not_false_iff,
  and_false]

lemma condexp_ind_L1_of_not_measurable_set (hs : Â¬ measurable_set s) (x : G) :
  condexp_ind_L1 hm Î¼ s x = 0 :=
by simp only [condexp_ind_L1, hs, dif_neg, not_false_iff, false_and]

lemma condexp_ind_L1_add (x y : G) :
  condexp_ind_L1 hm Î¼ s (x + y) = condexp_ind_L1 hm Î¼ s x + condexp_ind_L1 hm Î¼ s y :=
begin
  by_cases hs : measurable_set s,
  swap, {simp_rw condexp_ind_L1_of_not_measurable_set hs, rw zero_add, },
  by_cases hÎ¼s : Î¼ s = âˆ,
  { simp_rw condexp_ind_L1_of_measure_eq_top hÎ¼s, rw zero_add, },
  { simp_rw condexp_ind_L1_of_measurable_set_of_measure_ne_top hs hÎ¼s,
    exact condexp_ind_L1_fin_add hs hÎ¼s x y, },
end

lemma condexp_ind_L1_smul (c : â„) (x : G) :
  condexp_ind_L1 hm Î¼ s (c â€¢ x) = c â€¢ condexp_ind_L1 hm Î¼ s x :=
begin
  by_cases hs : measurable_set s,
  swap, {simp_rw condexp_ind_L1_of_not_measurable_set hs, rw smul_zero, },
  by_cases hÎ¼s : Î¼ s = âˆ,
  { simp_rw condexp_ind_L1_of_measure_eq_top hÎ¼s, rw smul_zero, },
  { simp_rw condexp_ind_L1_of_measurable_set_of_measure_ne_top hs hÎ¼s,
    exact condexp_ind_L1_fin_smul hs hÎ¼s c x, },
end

lemma condexp_ind_L1_smul' [normed_space â„ F] [smul_comm_class â„ ğ•œ F] (c : ğ•œ) (x : F) :
  condexp_ind_L1 hm Î¼ s (c â€¢ x) = c â€¢ condexp_ind_L1 hm Î¼ s x :=
begin
  by_cases hs : measurable_set s,
  swap, {simp_rw condexp_ind_L1_of_not_measurable_set hs, rw smul_zero, },
  by_cases hÎ¼s : Î¼ s = âˆ,
  { simp_rw condexp_ind_L1_of_measure_eq_top hÎ¼s, rw smul_zero, },
  { simp_rw condexp_ind_L1_of_measurable_set_of_measure_ne_top hs hÎ¼s,
    exact condexp_ind_L1_fin_smul' hs hÎ¼s c x, },
end

lemma norm_condexp_ind_L1_le (x : G) :
  â€–condexp_ind_L1 hm Î¼ s xâ€– â‰¤ (Î¼ s).to_real * â€–xâ€– :=
begin
  by_cases hs : measurable_set s,
  swap, {simp_rw condexp_ind_L1_of_not_measurable_set hs, rw Lp.norm_zero,
    exact mul_nonneg ennreal.to_real_nonneg (norm_nonneg _), },
  by_cases hÎ¼s : Î¼ s = âˆ,
  { rw [condexp_ind_L1_of_measure_eq_top hÎ¼s x, Lp.norm_zero],
    exact mul_nonneg ennreal.to_real_nonneg (norm_nonneg _), },
  { rw condexp_ind_L1_of_measurable_set_of_measure_ne_top hs hÎ¼s x,
    exact norm_condexp_ind_L1_fin_le hs hÎ¼s x, },
end

lemma continuous_condexp_ind_L1 : continuous (Î» x : G, condexp_ind_L1 hm Î¼ s x) :=
continuous_of_linear_of_bound condexp_ind_L1_add condexp_ind_L1_smul norm_condexp_ind_L1_le

lemma condexp_ind_L1_disjoint_union (hs : measurable_set s) (ht : measurable_set t)
  (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (hst : s âˆ© t = âˆ…) (x : G) :
  condexp_ind_L1 hm Î¼ (s âˆª t) x = condexp_ind_L1 hm Î¼ s x + condexp_ind_L1 hm Î¼ t x :=
begin
  have hÎ¼st : Î¼ (s âˆª t) â‰  âˆ, from ((measure_union_le s t).trans_lt
    (lt_top_iff_ne_top.mpr (ennreal.add_ne_top.mpr âŸ¨hÎ¼s, hÎ¼tâŸ©))).ne,
  rw [condexp_ind_L1_of_measurable_set_of_measure_ne_top hs hÎ¼s x,
    condexp_ind_L1_of_measurable_set_of_measure_ne_top ht hÎ¼t x,
    condexp_ind_L1_of_measurable_set_of_measure_ne_top (hs.union ht) hÎ¼st x],
  exact condexp_ind_L1_fin_disjoint_union hs ht hÎ¼s hÎ¼t hst x,
end

end condexp_ind_L1

/-- Conditional expectation of the indicator of a set, as a linear map from `G` to L1. -/
def condexp_ind {m m0 : measurable_space Î±} (hm : m â‰¤ m0) (Î¼ : measure Î±) [sigma_finite (Î¼.trim hm)]
  (s : set Î±) : G â†’L[â„] Î± â†’â‚[Î¼] G :=
{ to_fun    := condexp_ind_L1 hm Î¼ s,
  map_add'  := condexp_ind_L1_add,
  map_smul' := condexp_ind_L1_smul,
  cont      := continuous_condexp_ind_L1, }

lemma condexp_ind_ae_eq_condexp_ind_smul (hm : m â‰¤ m0) [sigma_finite (Î¼.trim hm)]
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
  condexp_ind hm Î¼ s x =áµ[Î¼] condexp_ind_smul hm hs hÎ¼s x :=
begin
  refine eventually_eq.trans _ (condexp_ind_L1_fin_ae_eq_condexp_ind_smul hm hs hÎ¼s x),
  simp [condexp_ind, condexp_ind_L1, hs, hÎ¼s],
end

variables {hm : m â‰¤ m0} [sigma_finite (Î¼.trim hm)]

lemma ae_strongly_measurable'_condexp_ind (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
  ae_strongly_measurable' m (condexp_ind hm Î¼ s x) Î¼ :=
ae_strongly_measurable'.congr (ae_strongly_measurable'_condexp_ind_smul hm hs hÎ¼s x)
  (condexp_ind_ae_eq_condexp_ind_smul hm hs hÎ¼s x).symm

@[simp] lemma condexp_ind_empty : condexp_ind hm Î¼ âˆ… = (0 : G â†’L[â„] Î± â†’â‚[Î¼] G) :=
begin
  ext1,
  ext1,
  refine (condexp_ind_ae_eq_condexp_ind_smul hm measurable_set.empty (by simp) x).trans _,
  rw condexp_ind_smul_empty,
  refine (Lp.coe_fn_zero G 2 Î¼).trans _,
  refine eventually_eq.trans _ (Lp.coe_fn_zero G 1 Î¼).symm,
  refl,
end

lemma condexp_ind_smul' [normed_space â„ F] [smul_comm_class â„ ğ•œ F] (c : ğ•œ) (x : F) :
  condexp_ind hm Î¼ s (c â€¢ x) = c â€¢ condexp_ind hm Î¼ s x :=
condexp_ind_L1_smul' c x

lemma norm_condexp_ind_apply_le (x : G) : â€–condexp_ind hm Î¼ s xâ€– â‰¤ (Î¼ s).to_real * â€–xâ€– :=
norm_condexp_ind_L1_le x

lemma norm_condexp_ind_le : â€–(condexp_ind hm Î¼ s : G â†’L[â„] Î± â†’â‚[Î¼] G)â€– â‰¤ (Î¼ s).to_real :=
continuous_linear_map.op_norm_le_bound _ ennreal.to_real_nonneg norm_condexp_ind_apply_le

lemma condexp_ind_disjoint_union_apply (hs : measurable_set s) (ht : measurable_set t)
  (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (hst : s âˆ© t = âˆ…) (x : G) :
  condexp_ind hm Î¼ (s âˆª t) x = condexp_ind hm Î¼ s x + condexp_ind hm Î¼ t x :=
condexp_ind_L1_disjoint_union hs ht hÎ¼s hÎ¼t hst x

lemma condexp_ind_disjoint_union (hs : measurable_set s) (ht : measurable_set t) (hÎ¼s : Î¼ s â‰  âˆ)
  (hÎ¼t : Î¼ t â‰  âˆ) (hst : s âˆ© t = âˆ…) :
  (condexp_ind hm Î¼ (s âˆª t) : G â†’L[â„] Î± â†’â‚[Î¼] G) = condexp_ind hm Î¼ s + condexp_ind hm Î¼ t :=
by { ext1, push_cast, exact condexp_ind_disjoint_union_apply hs ht hÎ¼s hÎ¼t hst x, }

variables (G)

lemma dominated_fin_meas_additive_condexp_ind (hm : m â‰¤ m0) (Î¼ : measure Î±)
  [sigma_finite (Î¼.trim hm)] :
  dominated_fin_meas_additive Î¼ (condexp_ind hm Î¼ : set Î± â†’ G â†’L[â„] Î± â†’â‚[Î¼] G) 1 :=
âŸ¨Î» s t, condexp_ind_disjoint_union, Î» s _ _, norm_condexp_ind_le.trans (one_mul _).symm.leâŸ©

variables {G}

lemma set_integral_condexp_ind (hs : measurable_set[m] s) (ht : measurable_set t) (hÎ¼s : Î¼ s â‰  âˆ)
  (hÎ¼t : Î¼ t â‰  âˆ) (x : G') :
  âˆ« a in s, condexp_ind hm Î¼ t x a âˆ‚Î¼ = (Î¼ (t âˆ© s)).to_real â€¢ x :=
calc
âˆ« a in s, condexp_ind hm Î¼ t x a âˆ‚Î¼ = âˆ« a in s, condexp_ind_smul hm ht hÎ¼t x a âˆ‚Î¼ :
  set_integral_congr_ae (hm s hs)
    ((condexp_ind_ae_eq_condexp_ind_smul hm ht hÎ¼t x).mono (Î» x hx hxs, hx))
... = (Î¼ (t âˆ© s)).to_real â€¢ x : set_integral_condexp_ind_smul hs ht hÎ¼s hÎ¼t x

lemma condexp_ind_of_measurable (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ) (c : G) :
  condexp_ind hm Î¼ s c = indicator_const_Lp 1 (hm s hs) hÎ¼s c :=
begin
  ext1,
  refine eventually_eq.trans _ indicator_const_Lp_coe_fn.symm,
  refine (condexp_ind_ae_eq_condexp_ind_smul hm (hm s hs) hÎ¼s c).trans _,
  refine (condexp_ind_smul_ae_eq_smul hm (hm s hs) hÎ¼s c).trans _,
  rw [Lp_meas_coe, condexp_L2_indicator_of_measurable hm hs hÎ¼s (1 : â„)],
  refine (@indicator_const_Lp_coe_fn Î± _ _ 2 Î¼ _ s (hm s hs) hÎ¼s (1 : â„)).mono (Î» x hx, _),
  dsimp only,
  rw hx,
  by_cases hx_mem : x âˆˆ s; simp [hx_mem],
end

lemma condexp_ind_nonneg {E} [normed_lattice_add_comm_group E] [normed_space â„ E] [ordered_smul â„ E]
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) (hx : 0 â‰¤ x) :
  0 â‰¤ condexp_ind hm Î¼ s x :=
begin
  rw â† coe_fn_le,
  refine eventually_le.trans_eq _ (condexp_ind_ae_eq_condexp_ind_smul hm hs hÎ¼s x).symm,
  exact (coe_fn_zero E 1 Î¼).trans_le (condexp_ind_smul_nonneg hs hÎ¼s x hx),
end

end condexp_ind

section condexp_L1

variables {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  {hm : m â‰¤ m0} [sigma_finite (Î¼.trim hm)] {f g : Î± â†’ F'} {s : set Î±}

/-- Conditional expectation of a function as a linear map from `Î± â†’â‚[Î¼] F'` to itself. -/
def condexp_L1_clm (hm : m â‰¤ m0) (Î¼ : measure Î±) [sigma_finite (Î¼.trim hm)] :
  (Î± â†’â‚[Î¼] F') â†’L[â„] Î± â†’â‚[Î¼] F' :=
L1.set_to_L1 (dominated_fin_meas_additive_condexp_ind F' hm Î¼)

lemma condexp_L1_clm_smul (c : ğ•œ) (f : Î± â†’â‚[Î¼] F') :
  condexp_L1_clm hm Î¼ (c â€¢ f) = c â€¢ condexp_L1_clm hm Î¼ f :=
L1.set_to_L1_smul (dominated_fin_meas_additive_condexp_ind F' hm Î¼)
  (Î» c s x, condexp_ind_smul' c x) c f

lemma condexp_L1_clm_indicator_const_Lp (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : F') :
  (condexp_L1_clm hm Î¼) (indicator_const_Lp 1 hs hÎ¼s x) = condexp_ind hm Î¼ s x :=
L1.set_to_L1_indicator_const_Lp (dominated_fin_meas_additive_condexp_ind F' hm Î¼) hs hÎ¼s x

lemma condexp_L1_clm_indicator_const (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : F') :
  (condexp_L1_clm hm Î¼) â†‘(simple_func.indicator_const 1 hs hÎ¼s x) = condexp_ind hm Î¼ s x :=
by { rw Lp.simple_func.coe_indicator_const, exact condexp_L1_clm_indicator_const_Lp hs hÎ¼s x, }

/-- Auxiliary lemma used in the proof of `set_integral_condexp_L1_clm`. -/
lemma set_integral_condexp_L1_clm_of_measure_ne_top (f : Î± â†’â‚[Î¼] F') (hs : measurable_set[m] s)
  (hÎ¼s : Î¼ s â‰  âˆ) :
  âˆ« x in s, condexp_L1_clm hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
begin
  refine Lp.induction ennreal.one_ne_top
    (Î» f : Î± â†’â‚[Î¼] F', âˆ« x in s, condexp_L1_clm hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼)
  _ _ (is_closed_eq _ _) f,
  { intros x t ht hÎ¼t,
    simp_rw condexp_L1_clm_indicator_const ht hÎ¼t.ne x,
    rw [Lp.simple_func.coe_indicator_const, set_integral_indicator_const_Lp (hm _ hs)],
    exact set_integral_condexp_ind hs ht hÎ¼s hÎ¼t.ne x, },
  { intros f g hf_Lp hg_Lp hfg_disj hf hg,
    simp_rw (condexp_L1_clm hm Î¼).map_add,
    rw set_integral_congr_ae (hm s hs) ((Lp.coe_fn_add (condexp_L1_clm hm Î¼ (hf_Lp.to_Lp f))
      (condexp_L1_clm hm Î¼ (hg_Lp.to_Lp g))).mono (Î» x hx hxs, hx)),
    rw set_integral_congr_ae (hm s hs) ((Lp.coe_fn_add (hf_Lp.to_Lp f) (hg_Lp.to_Lp g)).mono
      (Î» x hx hxs, hx)),
    simp_rw pi.add_apply,
    rw [integral_add (L1.integrable_coe_fn _).integrable_on (L1.integrable_coe_fn _).integrable_on,
      integral_add (L1.integrable_coe_fn _).integrable_on (L1.integrable_coe_fn _).integrable_on,
      hf, hg], },
  { exact (continuous_set_integral s).comp (condexp_L1_clm hm Î¼).continuous, },
  { exact continuous_set_integral s, },
end

/-- The integral of the conditional expectation `condexp_L1_clm` over an `m`-measurable set is equal
to the integral of `f` on that set. See also `set_integral_condexp`, the similar statement for
`condexp`. -/
lemma set_integral_condexp_L1_clm (f : Î± â†’â‚[Î¼] F') (hs : measurable_set[m] s) :
  âˆ« x in s, condexp_L1_clm hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
begin
  let S := spanning_sets (Î¼.trim hm),
  have hS_meas : âˆ€ i, measurable_set[m] (S i) := measurable_spanning_sets (Î¼.trim hm),
  have hS_meas0 : âˆ€ i, measurable_set (S i) := Î» i, hm _ (hS_meas i),
  have hs_eq : s = â‹ƒ i, S i âˆ© s,
  { simp_rw set.inter_comm,
    rw [â† set.inter_Union, (Union_spanning_sets (Î¼.trim hm)), set.inter_univ], },
  have hS_finite : âˆ€ i, Î¼ (S i âˆ© s) < âˆ,
  { refine Î» i, (measure_mono (set.inter_subset_left _ _)).trans_lt _,
    have hS_finite_trim := measure_spanning_sets_lt_top (Î¼.trim hm) i,
    rwa trim_measurable_set_eq hm (hS_meas i) at hS_finite_trim, },
  have h_mono : monotone (Î» i, (S i) âˆ© s),
  { intros i j hij x,
    simp_rw set.mem_inter_iff,
    exact Î» h, âŸ¨monotone_spanning_sets (Î¼.trim hm) hij h.1, h.2âŸ©, },
  have h_eq_forall : (Î» i, âˆ« x in (S i) âˆ© s, condexp_L1_clm hm Î¼ f x âˆ‚Î¼)
      = Î» i, âˆ« x in (S i) âˆ© s, f x âˆ‚Î¼,
    from funext (Î» i, set_integral_condexp_L1_clm_of_measure_ne_top f
      (@measurable_set.inter Î± m _ _ (hS_meas i) hs) (hS_finite i).ne),
  have h_right : tendsto (Î» i, âˆ« x in (S i) âˆ© s, f x âˆ‚Î¼) at_top (ğ“ (âˆ« x in s, f x âˆ‚Î¼)),
  { have h := tendsto_set_integral_of_monotone (Î» i, (hS_meas0 i).inter (hm s hs)) h_mono
      (L1.integrable_coe_fn f).integrable_on,
    rwa â† hs_eq at h, },
  have h_left : tendsto (Î» i, âˆ« x in (S i) âˆ© s, condexp_L1_clm hm Î¼ f x âˆ‚Î¼) at_top
    (ğ“ (âˆ« x in s, condexp_L1_clm hm Î¼ f x âˆ‚Î¼)),
  { have h := tendsto_set_integral_of_monotone (Î» i, (hS_meas0 i).inter (hm s hs))
      h_mono (L1.integrable_coe_fn (condexp_L1_clm hm Î¼ f)).integrable_on,
    rwa â† hs_eq at h, },
  rw h_eq_forall at h_left,
  exact tendsto_nhds_unique h_left h_right,
end

lemma ae_strongly_measurable'_condexp_L1_clm (f : Î± â†’â‚[Î¼] F') :
  ae_strongly_measurable' m (condexp_L1_clm hm Î¼ f) Î¼ :=
begin
  refine Lp.induction ennreal.one_ne_top
    (Î» f : Î± â†’â‚[Î¼] F', ae_strongly_measurable' m (condexp_L1_clm hm Î¼ f) Î¼)
    _ _ _ f,
  { intros c s hs hÎ¼s,
    rw condexp_L1_clm_indicator_const hs hÎ¼s.ne c,
    exact ae_strongly_measurable'_condexp_ind hs hÎ¼s.ne c, },
  { intros f g hf hg h_disj hfm hgm,
    rw (condexp_L1_clm hm Î¼).map_add,
    refine ae_strongly_measurable'.congr _ (coe_fn_add _ _).symm,
    exact ae_strongly_measurable'.add hfm hgm, },
  { have : {f : Lp F' 1 Î¼ | ae_strongly_measurable' m (condexp_L1_clm hm Î¼ f) Î¼}
        = (condexp_L1_clm hm Î¼) â»Â¹' {f | ae_strongly_measurable' m f Î¼},
      by refl,
    rw this,
    refine is_closed.preimage (condexp_L1_clm hm Î¼).continuous _,
    exact is_closed_ae_strongly_measurable' hm, },
end

lemma condexp_L1_clm_Lp_meas (f : Lp_meas F' â„ m 1 Î¼) :
  condexp_L1_clm hm Î¼ (f : Î± â†’â‚[Î¼] F') = â†‘f :=
begin
  let g := Lp_meas_to_Lp_trim_lie F' â„ 1 Î¼ hm f,
  have hfg : f = (Lp_meas_to_Lp_trim_lie F' â„ 1 Î¼ hm).symm g,
    by simp only [linear_isometry_equiv.symm_apply_apply],
  rw hfg,
  refine @Lp.induction Î± F' m _ 1 (Î¼.trim hm) _ ennreal.coe_ne_top
    (Î» g : Î± â†’â‚[Î¼.trim hm] F',
      condexp_L1_clm hm Î¼ ((Lp_meas_to_Lp_trim_lie F' â„ 1 Î¼ hm).symm g : Î± â†’â‚[Î¼] F')
        = â†‘((Lp_meas_to_Lp_trim_lie F' â„ 1 Î¼ hm).symm g)) _ _ _ g,
  { intros c s hs hÎ¼s,
    rw [Lp.simple_func.coe_indicator_const, Lp_meas_to_Lp_trim_lie_symm_indicator hs hÎ¼s.ne c,
      condexp_L1_clm_indicator_const_Lp],
    exact condexp_ind_of_measurable hs ((le_trim hm).trans_lt hÎ¼s).ne c, },
  { intros f g hf hg hfg_disj hf_eq hg_eq,
    rw linear_isometry_equiv.map_add,
    push_cast,
    rw [map_add, hf_eq, hg_eq], },
  { refine is_closed_eq _ _,
    { refine (condexp_L1_clm hm Î¼).continuous.comp (continuous_induced_dom.comp _),
      exact linear_isometry_equiv.continuous _, },
    { refine continuous_induced_dom.comp _,
      exact linear_isometry_equiv.continuous _, }, },
end

lemma condexp_L1_clm_of_ae_strongly_measurable'
  (f : Î± â†’â‚[Î¼] F') (hfm : ae_strongly_measurable' m f Î¼) :
  condexp_L1_clm hm Î¼ f = f :=
condexp_L1_clm_Lp_meas (âŸ¨f, hfmâŸ© : Lp_meas F' â„ m 1 Î¼)

/-- Conditional expectation of a function, in L1. Its value is 0 if the function is not
integrable. The function-valued `condexp` should be used instead in most cases. -/
def condexp_L1 (hm : m â‰¤ m0) (Î¼ : measure Î±) [sigma_finite (Î¼.trim hm)] (f : Î± â†’ F') : Î± â†’â‚[Î¼] F' :=
set_to_fun Î¼ (condexp_ind hm Î¼) (dominated_fin_meas_additive_condexp_ind F' hm Î¼) f

lemma condexp_L1_undef (hf : Â¬ integrable f Î¼) : condexp_L1 hm Î¼ f = 0 :=
set_to_fun_undef (dominated_fin_meas_additive_condexp_ind F' hm Î¼) hf

lemma condexp_L1_eq (hf : integrable f Î¼) :
  condexp_L1 hm Î¼ f = condexp_L1_clm hm Î¼ (hf.to_L1 f) :=
set_to_fun_eq (dominated_fin_meas_additive_condexp_ind F' hm Î¼) hf

@[simp] lemma condexp_L1_zero : condexp_L1 hm Î¼ (0 : Î± â†’ F') = 0 :=
set_to_fun_zero _

@[simp] lemma condexp_L1_measure_zero (hm : m â‰¤ m0) : condexp_L1 hm (0 : measure Î±) f = 0 :=
set_to_fun_measure_zero _ rfl

lemma ae_strongly_measurable'_condexp_L1 {f : Î± â†’ F'} :
  ae_strongly_measurable' m (condexp_L1 hm Î¼ f) Î¼ :=
begin
  by_cases hf : integrable f Î¼,
  { rw condexp_L1_eq hf,
    exact ae_strongly_measurable'_condexp_L1_clm _, },
  { rw condexp_L1_undef hf,
    refine ae_strongly_measurable'.congr _ (coe_fn_zero _ _ _).symm,
    exact strongly_measurable.ae_strongly_measurable' (@strongly_measurable_zero _ _ m _ _), },
end

lemma condexp_L1_congr_ae (hm : m â‰¤ m0) [sigma_finite (Î¼.trim hm)] (h : f =áµ[Î¼] g) :
  condexp_L1 hm Î¼ f = condexp_L1 hm Î¼ g :=
set_to_fun_congr_ae _ h

lemma integrable_condexp_L1 (f : Î± â†’ F') : integrable (condexp_L1 hm Î¼ f) Î¼ :=
L1.integrable_coe_fn _

/-- The integral of the conditional expectation `condexp_L1` over an `m`-measurable set is equal to
the integral of `f` on that set. See also `set_integral_condexp`, the similar statement for
`condexp`. -/
lemma set_integral_condexp_L1 (hf : integrable f Î¼) (hs : measurable_set[m] s) :
  âˆ« x in s, condexp_L1 hm Î¼ f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
begin
  simp_rw condexp_L1_eq hf,
  rw set_integral_condexp_L1_clm (hf.to_L1 f) hs,
  exact set_integral_congr_ae (hm s hs) ((hf.coe_fn_to_L1).mono (Î» x hx hxs, hx)),
end

lemma condexp_L1_add (hf : integrable f Î¼) (hg : integrable g Î¼) :
  condexp_L1 hm Î¼ (f + g) = condexp_L1 hm Î¼ f + condexp_L1 hm Î¼ g :=
set_to_fun_add _ hf hg

lemma condexp_L1_neg (f : Î± â†’ F') : condexp_L1 hm Î¼ (-f) = - condexp_L1 hm Î¼ f :=
set_to_fun_neg _ f

lemma condexp_L1_smul (c : ğ•œ) (f : Î± â†’ F') : condexp_L1 hm Î¼ (c â€¢ f) = c â€¢ condexp_L1 hm Î¼ f :=
set_to_fun_smul _ (Î» c _ x, condexp_ind_smul' c x) c f

lemma condexp_L1_sub (hf : integrable f Î¼) (hg : integrable g Î¼) :
  condexp_L1 hm Î¼ (f - g) = condexp_L1 hm Î¼ f - condexp_L1 hm Î¼ g :=
set_to_fun_sub _ hf hg

lemma condexp_L1_of_ae_strongly_measurable'
  (hfm : ae_strongly_measurable' m f Î¼) (hfi : integrable f Î¼) :
  condexp_L1 hm Î¼ f =áµ[Î¼] f :=
begin
  rw condexp_L1_eq hfi,
  refine eventually_eq.trans _ (integrable.coe_fn_to_L1 hfi),
  rw condexp_L1_clm_of_ae_strongly_measurable',
  exact ae_strongly_measurable'.congr hfm (integrable.coe_fn_to_L1 hfi).symm,
end

lemma condexp_L1_mono {E} [normed_lattice_add_comm_group E] [complete_space E] [normed_space â„ E]
  [ordered_smul â„ E] {f g : Î± â†’ E}
  (hf : integrable f Î¼) (hg : integrable g Î¼) (hfg : f â‰¤áµ[Î¼] g) :
  condexp_L1 hm Î¼ f â‰¤áµ[Î¼] condexp_L1 hm Î¼ g :=
begin
  rw coe_fn_le,
  have h_nonneg : âˆ€ s, measurable_set s â†’ Î¼ s < âˆ â†’ âˆ€ x : E, 0 â‰¤ x â†’ 0 â‰¤ condexp_ind hm Î¼ s x,
    from Î» s hs hÎ¼s x hx, condexp_ind_nonneg hs hÎ¼s.ne x hx,
  exact set_to_fun_mono (dominated_fin_meas_additive_condexp_ind E hm Î¼) h_nonneg hf hg hfg,
end

end condexp_L1

end measure_theory
