/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import analysis.inner_product_space.projection
import measure_theory.function.conditional_expectation.unique

/-! # Conditional expectation in L2

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file contains one step of the construction of the conditional expectation, which is completed
in `measure_theory.function.conditional_expectation.basic`. See that file for a description of the
full process.

We build the conditional expectation of an `LÂ²` function, as an element of `LÂ²`. This is the
orthogonal projection on the subspace of almost everywhere `m`-measurable functions.

## Main definitions

* `condexp_L2`: Conditional expectation of a function in L2 with respect to a sigma-algebra: it is
the orthogonal projection on the subspace `Lp_meas`.

## Implementation notes

Most of the results in this file are valid for a complete real normed space `F`.
However, some lemmas also use `ğ•œ : is_R_or_C`:
* `condexp_L2` is defined only for an `inner_product_space` for now, and we use `ğ•œ` for its field.
* results about scalar multiplication are stated not only for `â„` but also for `ğ•œ` if we happen to
  have `normed_space ğ•œ F`.

-/

open topological_space filter continuous_linear_map
open_locale ennreal topology measure_theory

namespace measure_theory

variables {Î± E E' F G G' ğ•œ : Type*} {p : â„â‰¥0âˆ}
  [is_R_or_C ğ•œ] -- ğ•œ for â„ or â„‚
  -- E for an inner product space
  [normed_add_comm_group E] [inner_product_space ğ•œ E] [complete_space E]
  -- E' for an inner product space on which we compute integrals
  [normed_add_comm_group E'] [inner_product_space ğ•œ E']
  [complete_space E'] [normed_space â„ E']
  -- F for a Lp submodule
  [normed_add_comm_group F] [normed_space ğ•œ F]
  -- G for a Lp add_subgroup
  [normed_add_comm_group G]
  -- G' for integrals on a Lp add_subgroup
  [normed_add_comm_group G'] [normed_space â„ G'] [complete_space G']

variables {m m0 : measurable_space Î±} {Î¼ : measure Î±} {s t : set Î±}

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y
local notation `âŸª`x`, `y`âŸ«â‚‚` := @inner ğ•œ (Î± â†’â‚‚[Î¼] E) _ x y

variables (ğ•œ)
/-- Conditional expectation of a function in L2 with respect to a sigma-algebra -/
noncomputable
def condexp_L2 (hm : m â‰¤ m0) : (Î± â†’â‚‚[Î¼] E) â†’L[ğ•œ] (Lp_meas E ğ•œ m 2 Î¼) :=
@orthogonal_projection ğ•œ (Î± â†’â‚‚[Î¼] E) _ _ _ (Lp_meas E ğ•œ m 2 Î¼)
  (by { haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©, exact infer_instance, })
variables {ğ•œ}

lemma ae_strongly_measurable'_condexp_L2 (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
  ae_strongly_measurable' m (condexp_L2 ğ•œ hm f) Î¼ :=
Lp_meas.ae_strongly_measurable' _

lemma integrable_on_condexp_L2_of_measure_ne_top (hm : m â‰¤ m0) (hÎ¼s : Î¼ s â‰  âˆ) (f : Î± â†’â‚‚[Î¼] E) :
  integrable_on (condexp_L2 ğ•œ hm f) s Î¼ :=
integrable_on_Lp_of_measure_ne_top ((condexp_L2 ğ•œ hm f) : Î± â†’â‚‚[Î¼] E)
  fact_one_le_two_ennreal.elim hÎ¼s

lemma integrable_condexp_L2_of_is_finite_measure (hm : m â‰¤ m0) [is_finite_measure Î¼]
  {f : Î± â†’â‚‚[Î¼] E} :
  integrable (condexp_L2 ğ•œ hm f) Î¼ :=
integrable_on_univ.mp $ integrable_on_condexp_L2_of_measure_ne_top hm (measure_ne_top _ _) f

lemma norm_condexp_L2_le_one (hm : m â‰¤ m0) : â€–@condexp_L2 Î± E ğ•œ _ _ _ _ _ _ Î¼ hmâ€– â‰¤ 1 :=
by { haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©, exact orthogonal_projection_norm_le _, }

lemma norm_condexp_L2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) : â€–condexp_L2 ğ•œ hm fâ€– â‰¤ â€–fâ€– :=
((@condexp_L2 _ E ğ•œ _ _ _ _ _ _ Î¼ hm).le_op_norm f).trans
  (mul_le_of_le_one_left (norm_nonneg _) (norm_condexp_L2_le_one hm))

lemma snorm_condexp_L2_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
  snorm (condexp_L2 ğ•œ hm f) 2 Î¼ â‰¤ snorm f 2 Î¼ :=
begin
  rw [Lp_meas_coe, â† ennreal.to_real_le_to_real (Lp.snorm_ne_top _) (Lp.snorm_ne_top _),
    â† Lp.norm_def, â† Lp.norm_def, submodule.norm_coe],
  exact norm_condexp_L2_le hm f,
end

lemma norm_condexp_L2_coe_le (hm : m â‰¤ m0) (f : Î± â†’â‚‚[Î¼] E) :
  â€–(condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E)â€– â‰¤ â€–fâ€– :=
begin
  rw [Lp.norm_def, Lp.norm_def, â† Lp_meas_coe],
  refine (ennreal.to_real_le_to_real _ (Lp.snorm_ne_top _)).mpr (snorm_condexp_L2_le hm f),
  exact Lp.snorm_ne_top _,
end

lemma inner_condexp_L2_left_eq_right (hm : m â‰¤ m0) {f g : Î± â†’â‚‚[Î¼] E} :
  âŸª(condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ«â‚‚ = âŸªf, (condexp_L2 ğ•œ hm g : Î± â†’â‚‚[Î¼] E)âŸ«â‚‚ :=
by { haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©, exact inner_orthogonal_projection_left_eq_right _ f g, }

lemma condexp_L2_indicator_of_measurable (hm : m â‰¤ m0)
  (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) :
  (condexp_L2 ğ•œ hm (indicator_const_Lp 2 (hm s hs) hÎ¼s c) : Î± â†’â‚‚[Î¼] E)
    = indicator_const_Lp 2 (hm s hs) hÎ¼s c :=
begin
  rw condexp_L2,
  haveI : fact (m â‰¤ m0) := âŸ¨hmâŸ©,
  have h_mem : indicator_const_Lp 2 (hm s hs) hÎ¼s c âˆˆ Lp_meas E ğ•œ m 2 Î¼,
    from mem_Lp_meas_indicator_const_Lp hm hs hÎ¼s,
  let ind := (âŸ¨indicator_const_Lp 2 (hm s hs) hÎ¼s c, h_memâŸ© : Lp_meas E ğ•œ m 2 Î¼),
  have h_coe_ind : (ind : Î± â†’â‚‚[Î¼] E) = indicator_const_Lp 2 (hm s hs) hÎ¼s c, by refl,
  have h_orth_mem := orthogonal_projection_mem_subspace_eq_self ind,
  rw [â† h_coe_ind, h_orth_mem],
end

lemma inner_condexp_L2_eq_inner_fun (hm : m â‰¤ m0) (f g : Î± â†’â‚‚[Î¼] E)
  (hg : ae_strongly_measurable' m g Î¼) :
  âŸª(condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E), gâŸ«â‚‚ = âŸªf, gâŸ«â‚‚ :=
begin
  symmetry,
  rw [â† sub_eq_zero, â† inner_sub_left, condexp_L2],
  simp only [mem_Lp_meas_iff_ae_strongly_measurable'.mpr hg, orthogonal_projection_inner_eq_zero],
end

section real

variables {hm : m â‰¤ m0}

lemma integral_condexp_L2_eq_of_fin_meas_real (f : Lp ğ•œ 2 Î¼) (hs : measurable_set[m] s)
  (hÎ¼s : Î¼ s â‰  âˆ) :
  âˆ« x in s, condexp_L2 ğ•œ hm f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
begin
  rw â† L2.inner_indicator_const_Lp_one (hm s hs) hÎ¼s,
  have h_eq_inner : âˆ« x in s, condexp_L2 ğ•œ hm f x âˆ‚Î¼
    = inner (indicator_const_Lp 2 (hm s hs) hÎ¼s (1 : ğ•œ)) (condexp_L2 ğ•œ hm f),
  { rw L2.inner_indicator_const_Lp_one (hm s hs) hÎ¼s,
    congr, },
  rw [h_eq_inner, â† inner_condexp_L2_left_eq_right, condexp_L2_indicator_of_measurable hm hs hÎ¼s],
end

lemma lintegral_nnnorm_condexp_L2_le (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ) (f : Lp â„ 2 Î¼) :
  âˆ«â» x in s, â€–condexp_L2 â„ hm f xâ€–â‚Š âˆ‚Î¼ â‰¤ âˆ«â» x in s, â€–f xâ€–â‚Š âˆ‚Î¼ :=
begin
  let h_meas := Lp_meas.ae_strongly_measurable' (condexp_L2 â„ hm f),
  let g := h_meas.some,
  have hg_meas : strongly_measurable[m] g, from h_meas.some_spec.1,
  have hg_eq : g =áµ[Î¼] condexp_L2 â„ hm f, from h_meas.some_spec.2.symm,
  have hg_eq_restrict : g =áµ[Î¼.restrict s] condexp_L2 â„ hm f, from ae_restrict_of_ae hg_eq,
  have hg_nnnorm_eq : (Î» x, (â€–g xâ€–â‚Š : â„â‰¥0âˆ))
    =áµ[Î¼.restrict s] (Î» x, (â€–condexp_L2 â„ hm f xâ€–â‚Š : â„â‰¥0âˆ)),
  { refine hg_eq_restrict.mono (Î» x hx, _),
    dsimp only,
    rw hx, },
  rw lintegral_congr_ae hg_nnnorm_eq.symm,
  refine lintegral_nnnorm_le_of_forall_fin_meas_integral_eq hm
    (Lp.strongly_measurable f) _ _ _ _ hs hÎ¼s,
  { exact integrable_on_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s, },
  { exact hg_meas, },
  { rw [integrable_on, integrable_congr hg_eq_restrict],
    exact integrable_on_condexp_L2_of_measure_ne_top hm hÎ¼s f, },
  { intros t ht hÎ¼t,
    rw â† integral_condexp_L2_eq_of_fin_meas_real f ht hÎ¼t.ne,
    exact set_integral_congr_ae (hm t ht) (hg_eq.mono (Î» x hx _, hx)), },
end

lemma condexp_L2_ae_eq_zero_of_ae_eq_zero (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ)
  {f : Lp â„ 2 Î¼} (hf : f =áµ[Î¼.restrict s] 0) :
  condexp_L2 â„ hm f =áµ[Î¼.restrict s] 0 :=
begin
  suffices h_nnnorm_eq_zero : âˆ«â» x in s, â€–condexp_L2 â„ hm f xâ€–â‚Š âˆ‚Î¼ = 0,
  { rw lintegral_eq_zero_iff at h_nnnorm_eq_zero,
    refine h_nnnorm_eq_zero.mono (Î» x hx, _),
    dsimp only at hx,
    rw pi.zero_apply at hx âŠ¢,
    { rwa [ennreal.coe_eq_zero, nnnorm_eq_zero] at hx, },
    { refine measurable.coe_nnreal_ennreal (measurable.nnnorm _),
      rw Lp_meas_coe,
      exact (Lp.strongly_measurable _).measurable }, },
  refine le_antisymm _ (zero_le _),
  refine (lintegral_nnnorm_condexp_L2_le hs hÎ¼s f).trans (le_of_eq _),
  rw lintegral_eq_zero_iff,
  { refine hf.mono (Î» x hx, _),
    dsimp only,
    rw hx,
    simp, },
  { exact (Lp.strongly_measurable _).ennnorm, },
end

lemma lintegral_nnnorm_condexp_L2_indicator_le_real
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (ht : measurable_set[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
  âˆ«â» a in t, â€–condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ (s âˆ© t) :=
begin
  refine (lintegral_nnnorm_condexp_L2_le ht hÎ¼t _).trans (le_of_eq _),
  have h_eq : âˆ«â» x in t, â€–(indicator_const_Lp 2 hs hÎ¼s (1 : â„)) xâ€–â‚Š âˆ‚Î¼
    = âˆ«â» x in t, s.indicator (Î» x, (1 : â„â‰¥0âˆ)) x âˆ‚Î¼,
  { refine lintegral_congr_ae (ae_restrict_of_ae _),
    refine (@indicator_const_Lp_coe_fn _ _ _ 2 _ _ _ hs hÎ¼s (1 : â„)).mono (Î» x hx, _),
    rw hx,
    classical,
    simp_rw set.indicator_apply,
    split_ifs; simp, },
  rw [h_eq, lintegral_indicator _ hs, lintegral_const, measure.restrict_restrict hs],
  simp only [one_mul, set.univ_inter, measurable_set.univ, measure.restrict_apply],
end

end real

/-- `condexp_L2` commutes with taking inner products with constants. See the lemma
`condexp_L2_comp_continuous_linear_map` for a more general result about commuting with continuous
linear maps. -/
lemma condexp_L2_const_inner (hm : m â‰¤ m0) (f : Lp E 2 Î¼) (c : E) :
  condexp_L2 ğ•œ hm (((Lp.mem_â„’p f).const_inner c).to_Lp (Î» a, âŸªc, f aâŸ«))
    =áµ[Î¼] Î» a, âŸªc, condexp_L2 ğ•œ hm f aâŸ« :=
begin
  rw Lp_meas_coe,
  have h_mem_Lp : mem_â„’p (Î» a, âŸªc, condexp_L2 ğ•œ hm f aâŸ«) 2 Î¼,
  { refine mem_â„’p.const_inner _ _, rw Lp_meas_coe, exact Lp.mem_â„’p _, },
  have h_eq : h_mem_Lp.to_Lp _ =áµ[Î¼] Î» a, âŸªc, condexp_L2 ğ•œ hm f aâŸ«, from h_mem_Lp.coe_fn_to_Lp,
  refine eventually_eq.trans _ h_eq,
  refine Lp.ae_eq_of_forall_set_integral_eq' ğ•œ hm _ _ two_ne_zero ennreal.coe_ne_top
    (Î» s hs hÎ¼s, integrable_on_condexp_L2_of_measure_ne_top hm hÎ¼s.ne _) _ _ _ _,
  { intros s hs hÎ¼s,
    rw [integrable_on, integrable_congr (ae_restrict_of_ae h_eq)],
    exact (integrable_on_condexp_L2_of_measure_ne_top hm hÎ¼s.ne _).const_inner _, },
  { intros s hs hÎ¼s,
    rw [â† Lp_meas_coe, integral_condexp_L2_eq_of_fin_meas_real _ hs hÎ¼s.ne,
      integral_congr_ae (ae_restrict_of_ae h_eq), Lp_meas_coe,
      â† L2.inner_indicator_const_Lp_eq_set_integral_inner ğ•œ â†‘(condexp_L2 ğ•œ hm f) (hm s hs) c hÎ¼s.ne,
      â† inner_condexp_L2_left_eq_right, condexp_L2_indicator_of_measurable,
      L2.inner_indicator_const_Lp_eq_set_integral_inner ğ•œ f (hm s hs) c hÎ¼s.ne,
      set_integral_congr_ae (hm s hs)
        ((mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p f).const_inner c)).mono (Î» x hx hxs, hx))], },
  { rw â† Lp_meas_coe, exact Lp_meas.ae_strongly_measurable' _, },
  { refine ae_strongly_measurable'.congr _ h_eq.symm,
    exact (Lp_meas.ae_strongly_measurable' _).const_inner _, },
end

/-- `condexp_L2` verifies the equality of integrals defining the conditional expectation. -/
lemma integral_condexp_L2_eq (hm : m â‰¤ m0)
  (f : Lp E' 2 Î¼) (hs : measurable_set[m] s) (hÎ¼s : Î¼ s â‰  âˆ) :
  âˆ« x in s, condexp_L2 ğ•œ hm f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
begin
  rw [â† sub_eq_zero, Lp_meas_coe, â† integral_sub'
      (integrable_on_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s)
      (integrable_on_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s)],
  refine integral_eq_zero_of_forall_integral_inner_eq_zero ğ•œ _ _ _,
  { rw integrable_congr (ae_restrict_of_ae (Lp.coe_fn_sub â†‘(condexp_L2 ğ•œ hm f) f).symm),
    exact integrable_on_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s, },
  intro c,
  simp_rw [pi.sub_apply, inner_sub_right],
  rw integral_sub
    ((integrable_on_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s).const_inner c)
    ((integrable_on_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s).const_inner c),
  have h_ae_eq_f := mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p f).const_inner c),
  rw [â† Lp_meas_coe, sub_eq_zero,
    â† set_integral_congr_ae (hm s hs) ((condexp_L2_const_inner hm f c).mono (Î» x hx _, hx)),
    â† set_integral_congr_ae (hm s hs) (h_ae_eq_f.mono (Î» x hx _, hx))],
  exact integral_condexp_L2_eq_of_fin_meas_real _ hs hÎ¼s,
end

variables {E'' ğ•œ' : Type*} [is_R_or_C ğ•œ'] [normed_add_comm_group E'']
  [inner_product_space ğ•œ' E''] [complete_space E''] [normed_space â„ E'']

variables (ğ•œ ğ•œ')
lemma condexp_L2_comp_continuous_linear_map (hm : m â‰¤ m0) (T : E' â†’L[â„] E'') (f : Î± â†’â‚‚[Î¼] E') :
  (condexp_L2 ğ•œ' hm (T.comp_Lp f) : Î± â†’â‚‚[Î¼] E'') =áµ[Î¼] T.comp_Lp (condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E') :=
begin
  refine Lp.ae_eq_of_forall_set_integral_eq' ğ•œ' hm _ _ two_ne_zero ennreal.coe_ne_top
    (Î» s hs hÎ¼s, integrable_on_condexp_L2_of_measure_ne_top hm hÎ¼s.ne _)
    (Î» s hs hÎ¼s, integrable_on_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s.ne)
    _ _ _,
  { intros s hs hÎ¼s,
    rw [T.set_integral_comp_Lp _ (hm s hs),
      T.integral_comp_comm
        (integrable_on_Lp_of_measure_ne_top _ fact_one_le_two_ennreal.elim hÎ¼s.ne),
      â† Lp_meas_coe, â† Lp_meas_coe, integral_condexp_L2_eq hm f hs hÎ¼s.ne,
      integral_condexp_L2_eq hm (T.comp_Lp f) hs hÎ¼s.ne, T.set_integral_comp_Lp _ (hm s hs),
      T.integral_comp_comm
        (integrable_on_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s.ne)], },
  { rw â† Lp_meas_coe, exact Lp_meas.ae_strongly_measurable' _, },
  { have h_coe := T.coe_fn_comp_Lp (condexp_L2 ğ•œ hm f : Î± â†’â‚‚[Î¼] E'),
    rw â† eventually_eq at h_coe,
    refine ae_strongly_measurable'.congr _ h_coe.symm,
    exact (Lp_meas.ae_strongly_measurable' (condexp_L2 ğ•œ hm f)).continuous_comp T.continuous, },
end
variables {ğ•œ ğ•œ'}

section condexp_L2_indicator

variables (ğ•œ)
lemma condexp_L2_indicator_ae_eq_smul (hm : m â‰¤ m0) (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ)
  (x : E') :
  condexp_L2 ğ•œ hm (indicator_const_Lp 2 hs hÎ¼s x)
    =áµ[Î¼] Î» a, (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) a) â€¢ x :=
begin
  rw indicator_const_Lp_eq_to_span_singleton_comp_Lp hs hÎ¼s x,
  have h_comp := condexp_L2_comp_continuous_linear_map â„ ğ•œ hm (to_span_singleton â„ x)
    (indicator_const_Lp 2 hs hÎ¼s (1 : â„)),
  rw â† Lp_meas_coe at h_comp,
  refine h_comp.trans _,
  exact (to_span_singleton â„ x).coe_fn_comp_Lp _,
end

lemma condexp_L2_indicator_eq_to_span_singleton_comp (hm : m â‰¤ m0) (hs : measurable_set s)
  (hÎ¼s : Î¼ s â‰  âˆ) (x : E') :
  (condexp_L2 ğ•œ hm (indicator_const_Lp 2 hs hÎ¼s x) : Î± â†’â‚‚[Î¼] E')
    = (to_span_singleton â„ x).comp_Lp (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„))) :=
begin
  ext1,
  rw â† Lp_meas_coe,
  refine (condexp_L2_indicator_ae_eq_smul ğ•œ hm hs hÎ¼s x).trans _,
  have h_comp := (to_span_singleton â„ x).coe_fn_comp_Lp
    (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) : Î± â†’â‚‚[Î¼] â„),
  rw â† eventually_eq at h_comp,
  refine eventually_eq.trans _ h_comp.symm,
  refine eventually_of_forall (Î» y, _),
  refl,
end

variables {ğ•œ}

lemma set_lintegral_nnnorm_condexp_L2_indicator_le (hm : m â‰¤ m0) (hs : measurable_set s)
  (hÎ¼s : Î¼ s â‰  âˆ) (x : E') {t : set Î±} (ht : measurable_set[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
  âˆ«â» a in t, â€–condexp_L2 ğ•œ hm (indicator_const_Lp 2 hs hÎ¼s x) aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
calc âˆ«â» a in t, â€–condexp_L2 ğ•œ hm (indicator_const_Lp 2 hs hÎ¼s x) aâ€–â‚Š âˆ‚Î¼
    = âˆ«â» a in t, â€–(condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) a) â€¢ xâ€–â‚Š âˆ‚Î¼ :
set_lintegral_congr_fun (hm t ht)
  ((condexp_L2_indicator_ae_eq_smul ğ•œ hm hs hÎ¼s x).mono (Î» a ha hat, by rw ha))
... = âˆ«â» a in t, â€–condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) aâ€–â‚Š âˆ‚Î¼ * â€–xâ€–â‚Š :
begin
  simp_rw [nnnorm_smul, ennreal.coe_mul],
  rw [lintegral_mul_const, Lp_meas_coe],
  exact (Lp.strongly_measurable _).ennnorm
end
... â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :
  mul_le_mul_right' (lintegral_nnnorm_condexp_L2_indicator_le_real hs hÎ¼s ht hÎ¼t) _

lemma lintegral_nnnorm_condexp_L2_indicator_le (hm : m â‰¤ m0) (hs : measurable_set s)
  (hÎ¼s : Î¼ s â‰  âˆ) (x : E') [sigma_finite (Î¼.trim hm)] :
  âˆ«â» a, â€–condexp_L2 ğ•œ hm (indicator_const_Lp 2 hs hÎ¼s x) aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ s * â€–xâ€–â‚Š :=
begin
  refine lintegral_le_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š) _ (Î» t ht hÎ¼t, _),
  { rw Lp_meas_coe,
    exact (Lp.ae_strongly_measurable _).ennnorm },
  refine (set_lintegral_nnnorm_condexp_L2_indicator_le hm hs hÎ¼s x ht hÎ¼t).trans _,
  exact mul_le_mul_right' (measure_mono (set.inter_subset_left _ _)) _
end

/-- If the measure `Î¼.trim hm` is sigma-finite, then the conditional expectation of a measurable set
with finite measure is integrable. -/
lemma integrable_condexp_L2_indicator (hm : m â‰¤ m0) [sigma_finite (Î¼.trim hm)]
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E') :
  integrable (condexp_L2 ğ•œ hm (indicator_const_Lp 2 hs hÎ¼s x)) Î¼ :=
begin
  refine integrable_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š)
    (ennreal.mul_lt_top hÎ¼s ennreal.coe_ne_top) _ _,
  { rw Lp_meas_coe, exact Lp.ae_strongly_measurable _, },
  { refine Î» t ht hÎ¼t, (set_lintegral_nnnorm_condexp_L2_indicator_le hm hs hÎ¼s x ht hÎ¼t).trans _,
    exact mul_le_mul_right' (measure_mono (set.inter_subset_left _ _)) _, },
end

end condexp_L2_indicator

section condexp_ind_smul

variables [normed_space â„ G] {hm : m â‰¤ m0}

/-- Conditional expectation of the indicator of a measurable set with finite measure, in L2. -/
noncomputable
def condexp_ind_smul (hm : m â‰¤ m0) (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) : Lp G 2 Î¼ :=
(to_span_singleton â„ x).comp_LpL 2 Î¼ (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)))

lemma ae_strongly_measurable'_condexp_ind_smul
  (hm : m â‰¤ m0) (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
  ae_strongly_measurable' m (condexp_ind_smul hm hs hÎ¼s x) Î¼ :=
begin
  have h : ae_strongly_measurable' m (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„))) Î¼,
    from ae_strongly_measurable'_condexp_L2 _ _,
  rw condexp_ind_smul,
  suffices : ae_strongly_measurable' m
    ((to_span_singleton â„ x) âˆ˜ (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)))) Î¼,
  { refine ae_strongly_measurable'.congr this _,
    refine eventually_eq.trans _ (coe_fn_comp_LpL _ _).symm,
    rw Lp_meas_coe, },
  exact ae_strongly_measurable'.continuous_comp (to_span_singleton â„ x).continuous h,
end

lemma condexp_ind_smul_add (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x y : G) :
  condexp_ind_smul hm hs hÎ¼s (x + y)
    = condexp_ind_smul hm hs hÎ¼s x + condexp_ind_smul hm hs hÎ¼s y :=
by { simp_rw [condexp_ind_smul], rw [to_span_singleton_add, add_comp_LpL, add_apply], }

lemma condexp_ind_smul_smul (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (c : â„) (x : G) :
  condexp_ind_smul hm hs hÎ¼s (c â€¢ x) = c â€¢ condexp_ind_smul hm hs hÎ¼s x :=
by { simp_rw [condexp_ind_smul], rw [to_span_singleton_smul, smul_comp_LpL, smul_apply], }

lemma condexp_ind_smul_smul' [normed_space â„ F] [smul_comm_class â„ ğ•œ F] (hs : measurable_set s)
  (hÎ¼s : Î¼ s â‰  âˆ) (c : ğ•œ) (x : F) :
  condexp_ind_smul hm hs hÎ¼s (c â€¢ x) = c â€¢ condexp_ind_smul hm hs hÎ¼s x :=
by rw [condexp_ind_smul, condexp_ind_smul, to_span_singleton_smul',
  (to_span_singleton â„ x).smul_comp_LpL c, smul_apply]

lemma condexp_ind_smul_ae_eq_smul (hm : m â‰¤ m0) (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
  condexp_ind_smul hm hs hÎ¼s x
    =áµ[Î¼] Î» a, (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) a) â€¢ x :=
(to_span_singleton â„ x).coe_fn_comp_LpL _

lemma set_lintegral_nnnorm_condexp_ind_smul_le (hm : m â‰¤ m0) (hs : measurable_set s)
  (hÎ¼s : Î¼ s â‰  âˆ) (x : G) {t : set Î±} (ht : measurable_set[m] t) (hÎ¼t : Î¼ t â‰  âˆ) :
  âˆ«â» a in t, â€–condexp_ind_smul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :=
calc âˆ«â» a in t, â€–condexp_ind_smul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼
    = âˆ«â» a in t, â€–condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) a â€¢ xâ€–â‚Š âˆ‚Î¼ :
set_lintegral_congr_fun (hm t ht)
  ((condexp_ind_smul_ae_eq_smul hm hs hÎ¼s x).mono (Î» a ha hat, by rw ha ))
... = âˆ«â» a in t, â€–condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) aâ€–â‚Š âˆ‚Î¼ * â€–xâ€–â‚Š :
begin
  simp_rw [nnnorm_smul, ennreal.coe_mul],
  rw [lintegral_mul_const, Lp_meas_coe],
  exact (Lp.strongly_measurable _).ennnorm
end
... â‰¤ Î¼ (s âˆ© t) * â€–xâ€–â‚Š :
  mul_le_mul_right' (lintegral_nnnorm_condexp_L2_indicator_le_real hs hÎ¼s ht hÎ¼t) _

lemma lintegral_nnnorm_condexp_ind_smul_le (hm : m â‰¤ m0) (hs : measurable_set s)
  (hÎ¼s : Î¼ s â‰  âˆ) (x : G) [sigma_finite (Î¼.trim hm)] :
  âˆ«â» a, â€–condexp_ind_smul hm hs hÎ¼s x aâ€–â‚Š âˆ‚Î¼ â‰¤ Î¼ s * â€–xâ€–â‚Š :=
begin
  refine lintegral_le_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š) _ (Î» t ht hÎ¼t, _),
  { exact (Lp.ae_strongly_measurable _).ennnorm },
  refine (set_lintegral_nnnorm_condexp_ind_smul_le hm hs hÎ¼s x ht hÎ¼t).trans _,
  exact mul_le_mul_right' (measure_mono (set.inter_subset_left _ _)) _
end

/-- If the measure `Î¼.trim hm` is sigma-finite, then the conditional expectation of a measurable set
with finite measure is integrable. -/
lemma integrable_condexp_ind_smul (hm : m â‰¤ m0) [sigma_finite (Î¼.trim hm)]
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : G) :
  integrable (condexp_ind_smul hm hs hÎ¼s x) Î¼ :=
begin
  refine integrable_of_forall_fin_meas_le' hm (Î¼ s * â€–xâ€–â‚Š)
    (ennreal.mul_lt_top hÎ¼s ennreal.coe_ne_top) _ _,
  { exact Lp.ae_strongly_measurable _, },
  { refine Î» t ht hÎ¼t, (set_lintegral_nnnorm_condexp_ind_smul_le hm hs hÎ¼s x ht hÎ¼t).trans _,
    exact mul_le_mul_right' (measure_mono (set.inter_subset_left _ _)) _, },
end

lemma condexp_ind_smul_empty {x : G} :
  condexp_ind_smul hm measurable_set.empty
    ((@measure_empty _ _ Î¼).le.trans_lt ennreal.coe_lt_top).ne x = 0 :=
begin
  rw [condexp_ind_smul, indicator_const_empty],
  simp only [coe_fn_coe_base, submodule.coe_zero, continuous_linear_map.map_zero],
end

lemma set_integral_condexp_L2_indicator (hs : measurable_set[m] s) (ht : measurable_set t)
  (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) :
  âˆ« x in s, (condexp_L2 â„ hm (indicator_const_Lp 2 ht hÎ¼t (1 : â„))) x âˆ‚Î¼ = (Î¼ (t âˆ© s)).to_real :=
calc âˆ« x in s, (condexp_L2 â„ hm (indicator_const_Lp 2 ht hÎ¼t (1 : â„))) x âˆ‚Î¼
    = âˆ« x in s, indicator_const_Lp 2 ht hÎ¼t (1 : â„) x âˆ‚Î¼ :
      @integral_condexp_L2_eq
        Î± _ â„ _ _ _ _ _ _ _ _ _ hm (indicator_const_Lp 2 ht hÎ¼t (1 : â„)) hs hÎ¼s
... = (Î¼ (t âˆ© s)).to_real â€¢ 1 : set_integral_indicator_const_Lp (hm s hs) ht hÎ¼t (1 : â„)
... = (Î¼ (t âˆ© s)).to_real : by rw [smul_eq_mul, mul_one]

lemma set_integral_condexp_ind_smul (hs : measurable_set[m] s) (ht : measurable_set t)
  (hÎ¼s : Î¼ s â‰  âˆ) (hÎ¼t : Î¼ t â‰  âˆ) (x : G') :
  âˆ« a in s, (condexp_ind_smul hm ht hÎ¼t x) a âˆ‚Î¼ = (Î¼ (t âˆ© s)).to_real â€¢ x :=
calc âˆ« a in s, (condexp_ind_smul hm ht hÎ¼t x) a âˆ‚Î¼
    = (âˆ« a in s, (condexp_L2 â„ hm (indicator_const_Lp 2 ht hÎ¼t (1 : â„)) a â€¢ x) âˆ‚Î¼) :
  set_integral_congr_ae (hm s hs) ((condexp_ind_smul_ae_eq_smul hm ht hÎ¼t x).mono (Î» x hx hxs, hx))
... = (âˆ« a in s, condexp_L2 â„ hm (indicator_const_Lp 2 ht hÎ¼t (1 : â„)) a âˆ‚Î¼) â€¢ x :
  integral_smul_const _ x
... = (Î¼ (t âˆ© s)).to_real â€¢ x :
  by rw set_integral_condexp_L2_indicator hs ht hÎ¼s hÎ¼t

lemma condexp_L2_indicator_nonneg (hm : m â‰¤ m0) (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ)
  [sigma_finite (Î¼.trim hm)] :
  0 â‰¤áµ[Î¼] condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) :=
begin
  have h : ae_strongly_measurable' m (condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„))) Î¼,
    from ae_strongly_measurable'_condexp_L2 _ _,
  refine eventually_le.trans_eq _ h.ae_eq_mk.symm,
  refine @ae_le_of_ae_le_trim _ _ _ _ _ _ hm _ _ _,
  refine ae_nonneg_of_forall_set_integral_nonneg_of_sigma_finite _ _,
  { intros t ht hÎ¼t,
    refine @integrable.integrable_on _ _ m _ _ _ _ _,
    refine integrable.trim hm _ _,
    { rw integrable_congr h.ae_eq_mk.symm,
      exact integrable_condexp_L2_indicator hm hs hÎ¼s _, },
    { exact h.strongly_measurable_mk, }, },
  { intros t ht hÎ¼t,
    rw â† set_integral_trim hm h.strongly_measurable_mk ht,
    have h_ae : âˆ€áµ x âˆ‚Î¼, x âˆˆ t â†’ h.mk _ x = condexp_L2 â„ hm (indicator_const_Lp 2 hs hÎ¼s (1 : â„)) x,
    { filter_upwards [h.ae_eq_mk] with x hx,
      exact Î» _, hx.symm, },
    rw [set_integral_congr_ae (hm t ht) h_ae,
      set_integral_condexp_L2_indicator ht hs ((le_trim hm).trans_lt hÎ¼t).ne hÎ¼s],
    exact ennreal.to_real_nonneg, },
end

lemma condexp_ind_smul_nonneg {E} [normed_lattice_add_comm_group E] [normed_space â„ E]
  [ordered_smul â„ E] [sigma_finite (Î¼.trim hm)]
  (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (x : E) (hx : 0 â‰¤ x) :
  0 â‰¤áµ[Î¼] condexp_ind_smul hm hs hÎ¼s x :=
begin
  refine eventually_le.trans_eq _ (condexp_ind_smul_ae_eq_smul hm hs hÎ¼s x).symm,
  filter_upwards [condexp_L2_indicator_nonneg hm hs hÎ¼s] with a ha,
  exact smul_nonneg ha hx,
end

end condexp_ind_smul

end measure_theory
