/-
Copyright (c) 2022 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Kexing Ying
-/

import measure_theory.function.conditional_expectation.indicator
import measure_theory.function.uniform_integrable
import measure_theory.decomposition.radon_nikodym

/-!

# Conditional expectation of real-valued functions

> THIS FILE IS SYNCHRONIZED WITH MATHLIB4.
> Any changes to this file require a corresponding PR to mathlib4.

This file proves some results regarding the conditional expectation of real-valued functions.

## Main results

* `measure_theory.rn_deriv_ae_eq_condexp`: the conditional expectation `Œº[f | m]` is equal to the
  Radon-Nikodym derivative of `fŒº` restricted on `m` with respect to `Œº` restricted on `m`.
* `measure_theory.integrable.uniform_integrable_condexp`: the conditional expectation of a function
  form a uniformly integrable class.
* `measure_theory.condexp_strongly_measurable_mul`: the pull-out property of the conditional
  expectation.

-/

noncomputable theory
open topological_space measure_theory.Lp filter continuous_linear_map
open_locale nnreal ennreal topology big_operators measure_theory

namespace measure_theory

variables {Œ± : Type*} {m m0 : measurable_space Œ±} {Œº : measure Œ±}

lemma rn_deriv_ae_eq_condexp {hm : m ‚â§ m0} [hŒºm : sigma_finite (Œº.trim hm)] {f : Œ± ‚Üí ‚Ñù}
  (hf : integrable f Œº) :
  signed_measure.rn_deriv ((Œº.with_density·µ• f).trim hm) (Œº.trim hm) =·µê[Œº] Œº[f | m] :=
begin
  refine ae_eq_condexp_of_forall_set_integral_eq hm hf _ _ _,
  { exact Œª _ _ _, (integrable_of_integrable_trim hm (signed_measure.integrable_rn_deriv
      ((Œº.with_density·µ• f).trim hm) (Œº.trim hm))).integrable_on },
  { intros s hs hlt,
    conv_rhs { rw [‚Üê hf.with_density·µ•_trim_eq_integral hm hs,
      ‚Üê signed_measure.with_density·µ•_rn_deriv_eq ((Œº.with_density·µ• f).trim hm) (Œº.trim hm)
        (hf.with_density·µ•_trim_absolutely_continuous hm)], },
    rw [with_density·µ•_apply
        (signed_measure.integrable_rn_deriv ((Œº.with_density·µ• f).trim hm) (Œº.trim hm)) hs,
      ‚Üê set_integral_trim hm _ hs],
    exact (signed_measure.measurable_rn_deriv _ _).strongly_measurable },
  { exact strongly_measurable.ae_strongly_measurable'
      (signed_measure.measurable_rn_deriv _ _).strongly_measurable },
end

-- TODO: the following couple of lemmas should be generalized and proved using Jensen's inequality
-- for the conditional expectation (not in mathlib yet) .
lemma snorm_one_condexp_le_snorm (f : Œ± ‚Üí ‚Ñù) :
  snorm (Œº[f | m]) 1 Œº ‚â§ snorm f 1 Œº :=
begin
  by_cases hf : integrable f Œº,
  swap, { rw [condexp_undef hf, snorm_zero], exact zero_le _ },
  by_cases hm : m ‚â§ m0,
  swap, { rw [condexp_of_not_le hm, snorm_zero], exact zero_le _ },
  by_cases hsig : sigma_finite (Œº.trim hm),
  swap, { rw [condexp_of_not_sigma_finite hm hsig, snorm_zero], exact zero_le _ },
  calc snorm (Œº[f | m]) 1 Œº ‚â§ snorm (Œº[|f| | m]) 1 Œº :
  begin
    refine snorm_mono_ae _,
    filter_upwards [@condexp_mono _ m m0 _ _ _ _ _ _ _ _ hf hf.abs
        (@ae_of_all _ m0 _ Œº (Œª x, le_abs_self (f x) : ‚àÄ x, f x ‚â§ |f x|)),
      eventually_le.trans (condexp_neg f).symm.le
        (@condexp_mono _ m m0 _ _ _ _ _ _ _  _ hf.neg hf.abs
        (@ae_of_all _ m0 _ Œº (Œª x, neg_le_abs_self (f x) : ‚àÄ x, -f x ‚â§ |f x|)))] with x hx‚ÇÅ hx‚ÇÇ,
    exact abs_le_abs hx‚ÇÅ hx‚ÇÇ,
  end
    ... = snorm f 1 Œº :
  begin
    rw [snorm_one_eq_lintegral_nnnorm, snorm_one_eq_lintegral_nnnorm,
      ‚Üê ennreal.to_real_eq_to_real (ne_of_lt integrable_condexp.2) (ne_of_lt hf.2),
      ‚Üê integral_norm_eq_lintegral_nnnorm
        (strongly_measurable_condexp.mono hm).ae_strongly_measurable,
      ‚Üê integral_norm_eq_lintegral_nnnorm hf.1],
    simp_rw [real.norm_eq_abs],
    rw ‚Üê @integral_condexp _ _ _ _ _ m m0 Œº _ hm hsig hf.abs,
    refine integral_congr_ae _,
    have : 0 ‚â§·µê[Œº] Œº[|f| | m],
    { rw ‚Üê @condexp_zero Œ± ‚Ñù _ _ _ m m0 Œº,
      exact condexp_mono (integrable_zero _ _ _) hf.abs
        (@ae_of_all _ m0 _ Œº (Œª x, abs_nonneg (f x) : ‚àÄ x, 0 ‚â§ |f x|)) },
    filter_upwards [this] with x hx,
    exact abs_eq_self.2 hx
  end
end

lemma integral_abs_condexp_le (f : Œ± ‚Üí ‚Ñù) :
  ‚à´ x, |Œº[f | m] x| ‚àÇŒº ‚â§ ‚à´ x, |f x| ‚àÇŒº :=
begin
  by_cases hm : m ‚â§ m0,
  swap,
  { simp_rw [condexp_of_not_le hm, pi.zero_apply, abs_zero, integral_zero],
    exact integral_nonneg (Œª x, abs_nonneg _) },
  by_cases hfint : integrable f Œº,
  swap,
  { simp only [condexp_undef hfint, pi.zero_apply, abs_zero, integral_const,
      algebra.id.smul_eq_mul, mul_zero],
    exact integral_nonneg (Œª x, abs_nonneg _) },
  rw [integral_eq_lintegral_of_nonneg_ae, integral_eq_lintegral_of_nonneg_ae],
  { rw ennreal.to_real_le_to_real;
    simp_rw [‚Üê real.norm_eq_abs, of_real_norm_eq_coe_nnnorm],
    { rw [‚Üê snorm_one_eq_lintegral_nnnorm, ‚Üê snorm_one_eq_lintegral_nnnorm],
      exact snorm_one_condexp_le_snorm _ },
    { exact ne_of_lt integrable_condexp.2 },
    { exact ne_of_lt hfint.2 } },
  { exact eventually_of_forall (Œª x, abs_nonneg _) },
  { simp_rw ‚Üê real.norm_eq_abs,
    exact hfint.1.norm },
  { exact eventually_of_forall (Œª x, abs_nonneg _) },
  { simp_rw ‚Üê real.norm_eq_abs,
    exact (strongly_measurable_condexp.mono hm).ae_strongly_measurable.norm }
end

lemma set_integral_abs_condexp_le {s : set Œ±} (hs : measurable_set[m] s) (f : Œ± ‚Üí ‚Ñù) :
  ‚à´ x in s, |Œº[f | m] x| ‚àÇŒº ‚â§ ‚à´ x in s, |f x| ‚àÇŒº :=
begin
  by_cases hnm : m ‚â§ m0,
  swap,
  { simp_rw [condexp_of_not_le hnm, pi.zero_apply, abs_zero, integral_zero],
    exact integral_nonneg (Œª x, abs_nonneg _) },
  by_cases hfint : integrable f Œº,
  swap,
  { simp only [condexp_undef hfint, pi.zero_apply, abs_zero, integral_const,
      algebra.id.smul_eq_mul, mul_zero],
    exact integral_nonneg (Œª x, abs_nonneg _) },
  have : ‚à´ x in s, |Œº[f | m] x| ‚àÇŒº = ‚à´ x, |Œº[s.indicator f | m] x| ‚àÇŒº,
  { rw ‚Üê integral_indicator,
    swap, { exact hnm _ hs },
    refine integral_congr_ae _,
    have : (Œª x, |Œº[s.indicator f | m] x|) =·µê[Œº] Œª x, |s.indicator (Œº[f | m]) x| :=
      eventually_eq.fun_comp (condexp_indicator hfint hs) _,
    refine eventually_eq.trans (eventually_of_forall $ Œª x, _) this.symm,
    rw [‚Üê real.norm_eq_abs, norm_indicator_eq_indicator_norm],
    refl },
  rw [this, ‚Üê integral_indicator],
  swap, { exact hnm _ hs },
  refine (integral_abs_condexp_le _).trans (le_of_eq $ integral_congr_ae $
    eventually_of_forall $ Œª x, _),
  rw [‚Üê real.norm_eq_abs, norm_indicator_eq_indicator_norm],
  refl,
end

/-- If the real valued function `f` is bounded almost everywhere by `R`, then so is its conditional
expectation. -/
lemma ae_bdd_condexp_of_ae_bdd {R : ‚Ñù‚â•0} {f : Œ± ‚Üí ‚Ñù}
  (hbdd : ‚àÄ·µê x ‚àÇŒº, |f x| ‚â§ R) :
  ‚àÄ·µê x ‚àÇŒº, |Œº[f | m] x| ‚â§ R :=
begin
  by_cases hnm : m ‚â§ m0,
  swap,
  { simp_rw [condexp_of_not_le hnm, pi.zero_apply, abs_zero],
    refine eventually_of_forall (Œª x, R.coe_nonneg) },
  by_cases hfint : integrable f Œº,
  swap,
  { simp_rw [condexp_undef hfint],
    filter_upwards [hbdd] with x hx,
    rw [pi.zero_apply, abs_zero],
    exact (abs_nonneg _).trans hx },
  by_contra h,
  change Œº _ ‚â† 0 at h,
  simp only [‚Üê zero_lt_iff, set.compl_def, set.mem_set_of_eq, not_le] at h,
  suffices : (Œº {x | ‚ÜëR < |Œº[f | m] x|}).to_real * ‚ÜëR < (Œº {x | ‚ÜëR < |Œº[f | m] x|}).to_real * ‚ÜëR,
  { exact this.ne rfl },
  refine lt_of_lt_of_le (set_integral_gt_gt R.coe_nonneg _ _ h.ne.symm) _,
  { simp_rw [‚Üê real.norm_eq_abs],
    exact (strongly_measurable_condexp.mono hnm).measurable.norm },
  { exact integrable_condexp.abs.integrable_on },
  refine (set_integral_abs_condexp_le _ _).trans _,
  { simp_rw [‚Üê real.norm_eq_abs],
    exact @measurable_set_lt _ _ _ _ _ m _ _ _ _ _ measurable_const
      strongly_measurable_condexp.norm.measurable },
  simp only [‚Üê smul_eq_mul, ‚Üê set_integral_const, nnreal.val_eq_coe,
    is_R_or_C.coe_real_eq_id, id.def],
  refine set_integral_mono_ae hfint.abs.integrable_on _ _,
  { refine ‚ü®ae_strongly_measurable_const, lt_of_le_of_lt _
      (integrable_condexp.integrable_on : integrable_on (Œº[f | m]) {x | ‚ÜëR < |Œº[f | m] x|} Œº).2‚ü©,
    refine set_lintegral_mono (measurable.nnnorm _).coe_nnreal_ennreal
      (strongly_measurable_condexp.mono hnm).measurable.nnnorm.coe_nnreal_ennreal (Œª x hx, _),
    { exact measurable_const },
    { rw [ennreal.coe_le_coe, real.nnnorm_of_nonneg R.coe_nonneg],
      exact subtype.mk_le_mk.2 (le_of_lt hx) } },
  { exact hbdd },
end

/-- Given a integrable function `g`, the conditional expectations of `g` with respect to
a sequence of sub-œÉ-algebras is uniformly integrable. -/
lemma integrable.uniform_integrable_condexp {Œπ : Type*} [is_finite_measure Œº]
  {g : Œ± ‚Üí ‚Ñù} (hint : integrable g Œº) {‚Ñ± : Œπ ‚Üí measurable_space Œ±} (h‚Ñ± : ‚àÄ i, ‚Ñ± i ‚â§ m0) :
  uniform_integrable (Œª i, Œº[g | ‚Ñ± i]) 1 Œº :=
begin
  have hmeas : ‚àÄ n, ‚àÄ C, measurable_set {x | C ‚â§ ‚ÄñŒº[g | ‚Ñ± n] x‚Äñ‚Çä} :=
    Œª n C, measurable_set_le measurable_const
      (strongly_measurable_condexp.mono (h‚Ñ± n)).measurable.nnnorm,
  have hg : mem_‚Ñíp g 1 Œº := mem_‚Ñíp_one_iff_integrable.2 hint,
  refine uniform_integrable_of le_rfl ennreal.one_ne_top
    (Œª n, (strongly_measurable_condexp.mono (h‚Ñ± n)).ae_strongly_measurable) (Œª Œµ hŒµ, _),
  by_cases hne : snorm g 1 Œº = 0,
  { rw snorm_eq_zero_iff hg.1 one_ne_zero at hne,
    refine ‚ü®0, Œª n, (le_of_eq $ (snorm_eq_zero_iff
      ((strongly_measurable_condexp.mono (h‚Ñ± n)).ae_strongly_measurable.indicator (hmeas n 0))
      one_ne_zero).2 _).trans (zero_le _)‚ü©,
    filter_upwards [@condexp_congr_ae _ _ _ _ _ (‚Ñ± n) m0 Œº _ _ hne] with x hx,
    simp only [zero_le', set.set_of_true, set.indicator_univ, pi.zero_apply, hx, condexp_zero] },
  obtain ‚ü®Œ¥, hŒ¥, h‚ü© := hg.snorm_indicator_le Œº le_rfl ennreal.one_ne_top hŒµ,
  set C : ‚Ñù‚â•0 := ‚ü®Œ¥, hŒ¥.le‚ü©‚Åª¬π * (snorm g 1 Œº).to_nnreal with hC,
  have hCpos : 0 < C :=
    mul_pos (inv_pos.2 hŒ¥) (ennreal.to_nnreal_pos hne hg.snorm_lt_top.ne),
  have : ‚àÄ n, Œº {x : Œ± | C ‚â§ ‚ÄñŒº[g | ‚Ñ± n] x‚Äñ‚Çä} ‚â§ ennreal.of_real Œ¥,
  { intro n,
    have := mul_meas_ge_le_pow_snorm' Œº one_ne_zero ennreal.one_ne_top
      ((@strongly_measurable_condexp _ _ _ _ _ (‚Ñ± n) _ Œº g).mono
        (h‚Ñ± n)).ae_strongly_measurable C,
    rw [ennreal.one_to_real, ennreal.rpow_one, ennreal.rpow_one, mul_comm,
      ‚Üê ennreal.le_div_iff_mul_le (or.inl (ennreal.coe_ne_zero.2 hCpos.ne.symm))
        (or.inl ennreal.coe_lt_top.ne)] at this,
    simp_rw [ennreal.coe_le_coe] at this,
    refine this.trans _,
    rw [ennreal.div_le_iff_le_mul (or.inl (ennreal.coe_ne_zero.2 hCpos.ne.symm))
        (or.inl ennreal.coe_lt_top.ne), hC, nonneg.inv_mk, ennreal.coe_mul,
      ennreal.coe_to_nnreal hg.snorm_lt_top.ne, ‚Üê mul_assoc, ‚Üê ennreal.of_real_eq_coe_nnreal,
      ‚Üê ennreal.of_real_mul hŒ¥.le, mul_inv_cancel hŒ¥.ne.symm, ennreal.of_real_one, one_mul],
    exact snorm_one_condexp_le_snorm _ },
  refine ‚ü®C, Œª n, le_trans _ (h {x : Œ± | C ‚â§ ‚ÄñŒº[g | ‚Ñ± n] x‚Äñ‚Çä} (hmeas n C) (this n))‚ü©,
  have hmeas‚Ñ± : measurable_set[‚Ñ± n] {x : Œ± | C ‚â§ ‚ÄñŒº[g | ‚Ñ± n] x‚Äñ‚Çä} :=
    @measurable_set_le _ _ _ _ _ (‚Ñ± n) _ _ _ _ _ measurable_const
      (@measurable.nnnorm _ _ _ _ _ (‚Ñ± n) _ strongly_measurable_condexp.measurable),
  rw ‚Üê snorm_congr_ae (condexp_indicator hint hmeas‚Ñ±),
  exact snorm_one_condexp_le_snorm _,
end

section pull_out
-- TODO: this section could be generalized beyond multiplication, to any bounded bilinear map.

/-- Auxiliary lemma for `condexp_measurable_mul`. -/
lemma condexp_strongly_measurable_simple_func_mul (hm : m ‚â§ m0)
  (f : @simple_func Œ± m ‚Ñù) {g : Œ± ‚Üí ‚Ñù} (hg : integrable g Œº) :
  Œº[f * g | m] =·µê[Œº] f * Œº[g | m] :=
begin
  have : ‚àÄ s c (f : Œ± ‚Üí ‚Ñù), set.indicator s (function.const Œ± c) * f = s.indicator (c ‚Ä¢ f),
  { intros s c f,
    ext1 x,
    by_cases hx : x ‚àà s,
    { simp only [hx, pi.mul_apply, set.indicator_of_mem, pi.smul_apply, algebra.id.smul_eq_mul] },
    { simp only [hx, pi.mul_apply, set.indicator_of_not_mem, not_false_iff, zero_mul], }, },
  refine @simple_func.induction _ _ m _ _ (Œª c s hs, _) (Œª g‚ÇÅ g‚ÇÇ h_disj h_eq‚ÇÅ h_eq‚ÇÇ, _) f,
  { simp only [simple_func.const_zero, simple_func.coe_piecewise, simple_func.coe_const,
      simple_func.coe_zero, set.piecewise_eq_indicator],
    rw [this, this],
    refine (condexp_indicator (hg.smul c) hs).trans _,
    filter_upwards [@condexp_smul Œ± ‚Ñù ‚Ñù _ _ _ _ _ m m0 Œº c g] with x hx,
    classical,
    simp_rw [set.indicator_apply, hx], },
  { have h_add := @simple_func.coe_add _ _ m _ g‚ÇÅ g‚ÇÇ,
    calc Œº[‚áë(g‚ÇÅ + g‚ÇÇ) * g|m] =·µê[Œº] Œº[(‚áëg‚ÇÅ + ‚áëg‚ÇÇ) * g|m] :
      by { refine condexp_congr_ae (eventually_eq.mul _ eventually_eq.rfl), rw h_add, }
    ... =·µê[Œº] Œº[‚áëg‚ÇÅ * g|m] + Œº[‚áëg‚ÇÇ * g|m] :
      by { rw add_mul, exact condexp_add (hg.simple_func_mul' hm _) (hg.simple_func_mul' hm _), }
    ... =·µê[Œº] ‚áëg‚ÇÅ * Œº[g|m] + ‚áëg‚ÇÇ * Œº[g|m] : eventually_eq.add h_eq‚ÇÅ h_eq‚ÇÇ
    ... =·µê[Œº] ‚áë(g‚ÇÅ + g‚ÇÇ) * Œº[g|m] : by rw [h_add, add_mul], },
end

lemma condexp_strongly_measurable_mul_of_bound (hm : m ‚â§ m0) [is_finite_measure Œº]
  {f g : Œ± ‚Üí ‚Ñù} (hf : strongly_measurable[m] f) (hg : integrable g Œº) (c : ‚Ñù)
  (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
  Œº[f * g | m] =·µê[Œº] f * Œº[g | m] :=
begin
  let fs := hf.approx_bounded c,
  have hfs_tendsto : ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, fs n x) at_top (ùìù (f x)),
    from hf.tendsto_approx_bounded_ae hf_bound,
  by_cases hŒº : Œº = 0,
  { simp only [hŒº, ae_zero], },
  haveI : Œº.ae.ne_bot, by simp only [hŒº, ae_ne_bot, ne.def, not_false_iff],
  have hc : 0 ‚â§ c,
  { have h_exists : ‚àÉ x, ‚Äñf x‚Äñ ‚â§ c := eventually.exists hf_bound,
    exact (norm_nonneg _).trans h_exists.some_spec, },
  have hfs_bound : ‚àÄ n x, ‚Äñfs n x‚Äñ ‚â§ c := hf.norm_approx_bounded_le hc,
  have hn_eq : ‚àÄ n, Œº[fs n * g | m] =·µê[Œº] fs n * Œº[g | m],
    from Œª n, condexp_strongly_measurable_simple_func_mul hm _ hg,
  have : Œº[f * Œº[g|m]|m] = f * Œº[g|m],
  { refine condexp_of_strongly_measurable hm (hf.mul strongly_measurable_condexp) _,
    exact integrable_condexp.bdd_mul' ((hf.mono hm).ae_strongly_measurable) hf_bound, },
  rw ‚Üê this,
  refine tendsto_condexp_unique (Œª n x, fs n x * g x) (Œª n x, fs n x * Œº[g|m] x) (f * g)
    (f * Œº[g|m]) _ _ _ _ (Œª x, c * ‚Äñg x‚Äñ) _ (Œª x, c * ‚ÄñŒº[g|m] x‚Äñ) _ _ _ _,
  { exact Œª n, hg.bdd_mul'
      ((simple_func.strongly_measurable (fs n)).mono hm).ae_strongly_measurable
      (eventually_of_forall (hfs_bound n)), },
  { exact Œª n, integrable_condexp.bdd_mul'
      ((simple_func.strongly_measurable (fs n)).mono hm).ae_strongly_measurable
      (eventually_of_forall (hfs_bound n)), },
  { filter_upwards [hfs_tendsto] with x hx,
    rw pi.mul_apply,
    exact tendsto.mul hx tendsto_const_nhds, },
  { filter_upwards [hfs_tendsto] with x hx,
    rw pi.mul_apply,
    exact tendsto.mul hx tendsto_const_nhds, },
  { exact hg.norm.const_mul c, },
  { exact integrable_condexp.norm.const_mul c, },
  { refine Œª n, eventually_of_forall (Œª x, _),
    exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right (hfs_bound n x) (norm_nonneg _)), },
  { refine Œª n, eventually_of_forall (Œª x, _),
    exact (norm_mul_le _ _).trans (mul_le_mul_of_nonneg_right (hfs_bound n x) (norm_nonneg _)), },
  { intros n,
    simp_rw ‚Üê pi.mul_apply,
    refine (condexp_strongly_measurable_simple_func_mul hm _ hg).trans _,
    rw condexp_of_strongly_measurable hm
      ((simple_func.strongly_measurable _).mul strongly_measurable_condexp) _,
    { apply_instance, },
    { apply_instance, },
    exact integrable_condexp.bdd_mul'
      ((simple_func.strongly_measurable (fs n)).mono hm).ae_strongly_measurable
      (eventually_of_forall (hfs_bound n)), },
end

lemma condexp_strongly_measurable_mul_of_bound‚ÇÄ (hm : m ‚â§ m0) [is_finite_measure Œº]
  {f g : Œ± ‚Üí ‚Ñù} (hf : ae_strongly_measurable' m f Œº) (hg : integrable g Œº) (c : ‚Ñù)
  (hf_bound : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c) :
  Œº[f * g | m] =·µê[Œº] f * Œº[g | m] :=
begin
  have : Œº[f * g | m] =·µê[Œº] Œº[hf.mk f * g | m],
    from condexp_congr_ae (eventually_eq.mul hf.ae_eq_mk eventually_eq.rfl),
  refine this.trans _,
  have : f * Œº[g | m] =·µê[Œº] hf.mk f * Œº[g | m] := eventually_eq.mul hf.ae_eq_mk eventually_eq.rfl,
  refine eventually_eq.trans _ this.symm,
  refine condexp_strongly_measurable_mul_of_bound hm hf.strongly_measurable_mk hg c _,
  filter_upwards [hf_bound, hf.ae_eq_mk] with x hxc hx_eq,
  rw ‚Üê hx_eq,
  exact hxc,
end

/-- Pull-out property of the conditional expectation. -/
lemma condexp_strongly_measurable_mul {f g : Œ± ‚Üí ‚Ñù} (hf : strongly_measurable[m] f)
  (hfg : integrable (f * g) Œº) (hg : integrable g Œº) :
  Œº[f * g | m] =·µê[Œº] f * Œº[g | m] :=
begin
  by_cases hm : m ‚â§ m0, swap, { simp_rw condexp_of_not_le hm, rw mul_zero, },
  by_cases hŒºm : sigma_finite (Œº.trim hm),
  swap, { simp_rw condexp_of_not_sigma_finite hm hŒºm, rw mul_zero, },
  haveI : sigma_finite (Œº.trim hm) := hŒºm,
  obtain ‚ü®sets, sets_prop, h_univ‚ü© := hf.exists_spanning_measurable_set_norm_le hm Œº,
  simp_rw forall_and_distrib at sets_prop,
  obtain ‚ü®h_meas, h_finite, h_norm‚ü© := sets_prop,

  suffices : ‚àÄ n, ‚àÄ·µê x ‚àÇŒº, x ‚àà sets n ‚Üí Œº[f * g|m] x = f x * Œº[g|m] x,
  { rw ‚Üê ae_all_iff at this,
    filter_upwards [this] with x hx,
    rw pi.mul_apply,
    obtain ‚ü®i, hi‚ü© : ‚àÉ i, x ‚àà sets i,
    { have h_mem : x ‚àà ‚ãÉ i, sets i,
      { rw h_univ, exact set.mem_univ _, },
      simpa using h_mem, },
    exact hx i hi, },
  refine Œª n, ae_imp_of_ae_restrict _,
  suffices : (Œº.restrict (sets n))[f * g | m]
    =·µê[Œº.restrict (sets n)] f * (Œº.restrict (sets n))[g | m],
  { simp_rw ‚Üê pi.mul_apply,
    refine (condexp_restrict_ae_eq_restrict hm (h_meas n) hfg).symm.trans _,
    exact this.trans (eventually_eq.rfl.mul (condexp_restrict_ae_eq_restrict hm (h_meas n) hg)), },
  suffices : (Œº.restrict (sets n))[((sets n).indicator f) * g | m]
    =·µê[Œº.restrict (sets n)] ((sets n).indicator f) * (Œº.restrict (sets n))[g | m],
  { refine eventually_eq.trans _ (this.trans _),
    { exact condexp_congr_ae
        ((indicator_ae_eq_restrict (hm _ (h_meas n))).symm.mul eventually_eq.rfl), },
    { exact (indicator_ae_eq_restrict (hm _ (h_meas n))).mul eventually_eq.rfl, }, },
  haveI : is_finite_measure (Œº.restrict (sets n)),
  { constructor,
    rw measure.restrict_apply_univ,
    exact h_finite n, },
  refine condexp_strongly_measurable_mul_of_bound hm (hf.indicator (h_meas n)) hg.integrable_on n _,
  refine eventually_of_forall (Œª x, _),
  by_cases hxs : x ‚àà sets n,
  { simp only [hxs, set.indicator_of_mem],
    exact h_norm n x hxs, },
  { simp only [hxs, set.indicator_of_not_mem, not_false_iff, _root_.norm_zero, nat.cast_nonneg], },
end

/-- Pull-out property of the conditional expectation. -/
lemma condexp_strongly_measurable_mul‚ÇÄ {f g : Œ± ‚Üí ‚Ñù} (hf : ae_strongly_measurable' m f Œº)
  (hfg : integrable (f * g) Œº) (hg : integrable g Œº) :
  Œº[f * g | m] =·µê[Œº] f * Œº[g | m] :=
begin
  have : Œº[f * g | m] =·µê[Œº] Œº[hf.mk f * g | m],
    from condexp_congr_ae (eventually_eq.mul hf.ae_eq_mk eventually_eq.rfl),
  refine this.trans _,
  have : f * Œº[g | m] =·µê[Œº] hf.mk f * Œº[g | m] := eventually_eq.mul hf.ae_eq_mk eventually_eq.rfl,
  refine eventually_eq.trans _ this.symm,
  refine condexp_strongly_measurable_mul hf.strongly_measurable_mk _ hg,
  refine (integrable_congr _).mp hfg,
  exact eventually_eq.mul hf.ae_eq_mk eventually_eq.rfl,
end

end pull_out

end measure_theory
