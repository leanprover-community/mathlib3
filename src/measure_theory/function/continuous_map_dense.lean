/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/

import measure_theory.measure.regular
import measure_theory.function.simple_func_dense_lp
import topology.urysohns_lemma
import measure_theory.integral.bochner

/-!
# Approximation in Láµ– by continuous functions

This file proves that bounded continuous functions are dense in `Lp E p Î¼`, for `1 â‰¤ p < âˆž`, if the
domain `Î±` of the functions is a normal topological space and the measure `Î¼` is weakly regular.
It also proves the same results for approximation by continuous functions with compact support
when the space is locally compact and `Î¼` is regular.

The result is presented in several versions. First concrete versions giving an approximation
up to `Îµ` in these various contexts, and then abstract versions stating that the topological
closure of the relevant subgroups of `Lp` are the whole space.

* `mem_â„’p.exists_has_compact_support_snorm_sub_le` states that, in a locally compact space,
  an `â„’p` function can be approximated by continuous functions with compact support,
  in the sense that `snorm (f - g) p Î¼` is small.
* `mem_â„’p.exists_has_compact_support_integral_rpow_sub_le`: same result, but expressed in
  terms of `âˆ« â€–f - gâ€–^p`.

Versions with `integrable` instead of `mem_â„’p` are specialized to the case `p = 1`.
Versions with `bounded_continuous` instead of `has_compact_support` drop the locally
compact assumption and give only approximation by a bounded continuous function.

* `measure_theory.Lp.bounded_continuous_function_dense`: The subgroup
  `measure_theory.Lp.bounded_continuous_function` of `Lp E p Î¼`, the additive subgroup of
  `Lp E p Î¼` consisting of equivalence classes containing a continuous representative, is dense in
  `Lp E p Î¼`.
* `bounded_continuous_function.to_Lp_dense_range`: For finite-measure `Î¼`, the continuous linear
  map `bounded_continuous_function.to_Lp p Î¼ ð•œ` from `Î± â†’áµ‡ E` to `Lp E p Î¼` has dense range.
* `continuous_map.to_Lp_dense_range`: For compact `Î±` and finite-measure `Î¼`, the continuous linear
  map `continuous_map.to_Lp p Î¼ ð•œ` from `C(Î±, E)` to `Lp E p Î¼` has dense range.

Note that for `p = âˆž` this result is not true:  the characteristic function of the set `[0, âˆž)` in
`â„` cannot be continuously approximated in `Lâˆž`.

The proof is in three steps.  First, since simple functions are dense in `Lp`, it suffices to prove
the result for a scalar multiple of a characteristic function of a measurable set `s`. Secondly,
since the measure `Î¼` is weakly regular, the set `s` can be approximated above by an open set and
below by a closed set.  Finally, since the domain `Î±` is normal, we use Urysohn's lemma to find a
continuous function interpolating between these two sets.

## Related results

Are you looking for a result on "directional" approximation (above or below with respect to an
order) of functions whose codomain is `â„â‰¥0âˆž` or `â„`, by semicontinuous functions?  See the
Vitali-CarathÃ©odory theorem, in the file `measure_theory.vitali_caratheodory`.

-/

open_locale ennreal nnreal topology bounded_continuous_function
open measure_theory topological_space continuous_map set

variables {Î± : Type*} [measurable_space Î±] [topological_space Î±] [normal_space Î±] [borel_space Î±]
variables {E : Type*} [normed_add_comm_group E] {Î¼ : measure Î±} {p : â„â‰¥0âˆž}

namespace measure_theory

variables [normed_space â„ E]

/-- A variant of Urysohn's lemma, `â„’^p` version, for an outer regular measure `Î¼`:
consider two sets `s âŠ† u` which are respectively closed and open with `Î¼ s < âˆž`, and a vector `c`.
Then one may find a continuous function `f` equal to `c` on `s` and to `0` outside of `u`,
bounded by `â€–câ€–` everywhere, and such that the `â„’^p` norm of `f - s.indicator (Î» y, c)` is
arbitrarily small. Additionally, this function `f` belongs to `â„’^p`. -/
lemma exists_continuous_snorm_sub_le_of_closed [Î¼.outer_regular]
  (hp : p â‰  âˆž) {s u : set Î±} (s_closed : is_closed s) (u_open : is_open u) (hsu : s âŠ† u)
  (hs : Î¼ s â‰  âˆž) (c : E) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ (f : Î± â†’ E), snorm (Î» x, f x - s.indicator (Î» y, c) x) p Î¼ â‰¤ Îµ âˆ§
    continuous f âˆ§ (âˆ€ x, â€–f xâ€– â‰¤ â€–câ€–) âˆ§ function.support f âŠ† u âˆ§ mem_â„’p f p Î¼ :=
begin
  rcases exists_snorm_indicator_le hp c hÎµ with âŸ¨Î·, Î·_pos, hÎ·âŸ©,
  have Î·pos : (0 : â„â‰¥0âˆž) < Î· := ennreal.coe_lt_coe.2 Î·_pos,
  obtain âŸ¨V, sV, V_open, h'V, hVâŸ© : âˆƒ (V : set Î±) (H : V âŠ‡ s), is_open V âˆ§ Î¼ V < âˆž âˆ§ Î¼ (V \ s) < Î·,
    from s_closed.measurable_set.exists_is_open_diff_lt hs Î·pos.ne',
  let v := u âˆ© V,
  have hsv : s âŠ† v := subset_inter hsu sV,
  have hÎ¼v : Î¼ v < âˆž := (measure_mono (inter_subset_right _ _)).trans_lt h'V,
  obtain âŸ¨g, hgv, hgs, hg_rangeâŸ© := exists_continuous_zero_one_of_closed
    (u_open.inter V_open).is_closed_compl s_closed (disjoint_compl_left_iff.2 hsv),
  -- Multiply this by `c` to get a continuous approximation to the function `f`; the key point is
  -- that this is pointwise bounded by the indicator of the set `v \ s`, which has small measure.
  have g_norm : âˆ€ x, â€–g xâ€– = g x := Î» x, by rw [real.norm_eq_abs, abs_of_nonneg (hg_range x).1],
  have gc_bd0 : âˆ€ x, â€–g x â€¢ câ€– â‰¤ â€–câ€–,
  { assume x,
    simp only [norm_smul, g_norm x],
    apply mul_le_of_le_one_left (norm_nonneg _),
    exact (hg_range x).2 },
  have gc_bd : âˆ€ x, â€–g x â€¢ c - s.indicator (Î» x, c) xâ€– â‰¤ â€–(v \ s).indicator (Î» x, c) xâ€–,
  { intros x,
    by_cases hv : x âˆˆ v,
    { rw â† set.diff_union_of_subset hsv at hv,
      cases hv with hsv hs,
      { simpa only [hsv.2, set.indicator_of_not_mem, not_false_iff, sub_zero, hsv,
          set.indicator_of_mem] using gc_bd0 x},
      { simp [hgs hs, hs] } },
    { simp [hgv hv, (Î» h, hv (hsv h) : x âˆ‰ s)], } },
  have gc_support : function.support (Î» (x : Î±), g x â€¢ c) âŠ† v,
  { refine function.support_subset_iff'.2 (Î» x hx, _),
    simp only [hgv hx, pi.zero_apply, zero_smul] },
  have gc_mem : mem_â„’p (Î» x, g x â€¢ c) p Î¼,
  { apply mem_â„’p.smul_of_top_left (mem_â„’p_top_const _),
    refine âŸ¨g.continuous.ae_strongly_measurable, _âŸ©,
    have : snorm (v.indicator (Î» x, (1 : â„))) p Î¼ < âŠ¤,
    { refine (snorm_indicator_const_le _ _).trans_lt _,
      simp only [lt_top_iff_ne_top, hÎ¼v.ne, nnnorm_one, ennreal.coe_one, one_div, one_mul, ne.def,
        ennreal.rpow_eq_top_iff, inv_lt_zero, false_and, or_false, not_and, not_lt,
        ennreal.to_real_nonneg, implies_true_iff] },
    refine (snorm_mono (Î» x, _)).trans_lt this,
    by_cases hx : x âˆˆ v,
    { simp only [hx, abs_of_nonneg (hg_range x).1, (hg_range x).2, real.norm_eq_abs,
        indicator_of_mem, cstar_ring.norm_one] },
    { simp only [hgv hx, pi.zero_apply, real.norm_eq_abs, abs_zero, abs_nonneg] } },
  refine âŸ¨Î» x, g x â€¢ c, (snorm_mono gc_bd).trans _, g.continuous.smul continuous_const, gc_bd0,
    gc_support.trans (inter_subset_left _ _), gc_memâŸ©,
  exact hÎ· _ ((measure_mono (diff_subset_diff (inter_subset_right _ _) subset.rfl)).trans hV.le),
end

/-- In a locally compact space, any function in `â„’p` can be approximated by compactly supported
continuous functions when `1 â‰¤ p < âˆž`, version in terms of `snorm`. -/
lemma mem_â„’p.exists_has_compact_support_snorm_sub_le
  [locally_compact_space Î±] [Î¼.regular] (hp : p â‰  âˆž) (h'p : 1 â‰¤ p)
  {f : Î± â†’ E} (hf : mem_â„’p f p Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ (g : Î± â†’ E), snorm (f - g) p Î¼ â‰¤ Îµ âˆ§ continuous g âˆ§ mem_â„’p g p Î¼ âˆ§ has_compact_support g :=
begin
  -- It suffices to check that the set of functions we consider approximates characteristic
  -- functions, is stable under addition and consists of ae strongly measurable functions.
  -- First check the latter easy facts.
  apply hf.induction_dense hp h'p _ _ _ _ hÎµ, rotate,
  { rintros f g âŸ¨f_cont, f_mem, hfâŸ© âŸ¨g_cont, g_mem, hgâŸ©,
    exact âŸ¨f_cont.add g_cont, f_mem.add g_mem, hf.add hgâŸ© },
  { rintros f âŸ¨f_cont, f_mem, hfâŸ©,
    exact f_mem.ae_strongly_measurable },
  -- We are left with approximating characteristic functions.
  -- This follows from `exists_continuous_snorm_sub_le_of_closed`.
  assume c t ht htÎ¼ Îµ hÎµ,
  have h'Îµ : Îµ / 2 â‰  0, by simpa using hÎµ,
  rcases exists_snorm_indicator_le hp c h'Îµ with âŸ¨Î·, Î·pos, hÎ·âŸ©,
  have hÎ·_pos' : (0 : â„â‰¥0âˆž) < Î·, from ennreal.coe_pos.2 Î·pos,
  obtain âŸ¨s, st, s_compact, Î¼sâŸ© : âˆƒ s âŠ† t, is_compact s âˆ§ Î¼ (t \ s) < Î·,
    from ht.exists_is_compact_diff_lt htÎ¼.ne hÎ·_pos'.ne',
  have hsÎ¼ : Î¼ s < âˆž, from (measure_mono st).trans_lt htÎ¼,
  have I1 : snorm (s.indicator (Î» y, c) - t.indicator (Î» y, c)) p Î¼ â‰¤ Îµ/2,
  { rw [â† snorm_neg, neg_sub, â† indicator_diff st],
    exact (hÎ· _ Î¼s.le) },
  rcases exists_compact_between s_compact is_open_univ (subset_univ _) with âŸ¨k, k_compact, sk, -âŸ©,
  rcases exists_continuous_snorm_sub_le_of_closed hp s_compact.is_closed is_open_interior sk
    hsÎ¼.ne c h'Îµ with âŸ¨f, I2, f_cont, f_bound, f_support, f_memâŸ©,
  have I3 : snorm (f - t.indicator (Î» y, c)) p Î¼ â‰¤ Îµ, from calc
    snorm (f - t.indicator (Î» y, c)) p Î¼
      = snorm ((f - s.indicator (Î» y, c)) + (s.indicator (Î» y, c) - t.indicator (Î» y, c))) p Î¼ :
    by simp only [sub_add_sub_cancel]
  ... â‰¤ snorm (f - s.indicator (Î» y, c)) p Î¼
        + snorm (s.indicator (Î» y, c) - t.indicator (Î» y, c)) p Î¼ :
    begin
      refine snorm_add_le _ _ h'p,
      { exact f_mem.ae_strongly_measurable.sub
          (ae_strongly_measurable_const.indicator s_compact.measurable_set) },
      { exact (ae_strongly_measurable_const.indicator s_compact.measurable_set).sub
          (ae_strongly_measurable_const.indicator ht) },
    end
  ... â‰¤ Îµ/2 + Îµ/2 : add_le_add I2 I1
  ... = Îµ : ennreal.add_halves _,
  refine âŸ¨f, I3, f_cont, f_mem, has_compact_support.intro k_compact (Î» x hx, _)âŸ©,
  rw â† function.nmem_support,
  contrapose! hx,
  exact interior_subset (f_support hx)
end

/-- In a locally compact space, any function in `â„’p` can be approximated by compactly supported
continuous functions when `1 â‰¤ p < âˆž`, version in terms of `âˆ«`. -/
lemma mem_â„’p.exists_has_compact_support_integral_rpow_sub_le
  [locally_compact_space Î±] [Î¼.regular] {p : â„} (h'p : 1 â‰¤ p)
  {f : Î± â†’ E} (hf : mem_â„’p f (ennreal.of_real p) Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (g : Î± â†’ E), âˆ« x, â€–f x - g xâ€–^p âˆ‚Î¼ â‰¤ Îµ âˆ§ continuous g âˆ§ mem_â„’p g (ennreal.of_real p) Î¼
    âˆ§ has_compact_support g :=
begin
  have I : 0 < Îµ ^ (1/p) := real.rpow_pos_of_pos hÎµ _,
  have A : ennreal.of_real (Îµ ^ (1/p)) â‰  0,
    by simp only [ne.def, ennreal.of_real_eq_zero, not_le, I],
  have B : 1 â‰¤ ennreal.of_real p,
  { convert ennreal.of_real_le_of_real h'p, exact ennreal.of_real_one.symm },
  rcases hf.exists_has_compact_support_snorm_sub_le ennreal.coe_ne_top B A
    with âŸ¨g, hg, g_cont, g_mem, g_supportâŸ©,
  change snorm _ (ennreal.of_real p) _ â‰¤ _ at hg,
  refine âŸ¨g, _, g_cont, g_mem, g_supportâŸ©,
  rwa [(hf.sub g_mem).snorm_eq_integral_rpow_norm (zero_lt_one.trans_le B).ne'
    ennreal.coe_ne_top, ennreal.of_real_le_of_real_iff I.le, one_div,
    ennreal.to_real_of_real (zero_le_one.trans h'p), real.rpow_le_rpow_iff _ hÎµ.le _] at hg,
  { exact integral_nonneg (Î» x, real.rpow_nonneg_of_nonneg (norm_nonneg _) _) },
  { exact inv_pos.2 (zero_lt_one.trans_le h'p) }
end

/-- In a locally compact space, any integrable function can be approximated by compactly supported
continuous functions, version in terms of `âˆ«â»`. -/
lemma integrable.exists_has_compact_support_lintegral_sub_le [locally_compact_space Î±] [Î¼.regular]
  {f : Î± â†’ E} (hf : integrable f Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ (g : Î± â†’ E), âˆ«â» x, â€–f x - g xâ€–â‚Š âˆ‚Î¼ â‰¤ Îµ âˆ§ continuous g âˆ§ integrable g Î¼
    âˆ§ has_compact_support g :=
begin
  simp only [â† mem_â„’p_one_iff_integrable, â† snorm_one_eq_lintegral_nnnorm] at hf âŠ¢,
  exact hf.exists_has_compact_support_snorm_sub_le ennreal.one_ne_top le_rfl hÎµ,
end

/-- In a locally compact space, any integrable function can be approximated by compactly supported
continuous functions, version in terms of `âˆ«`. -/
lemma integrable.exists_has_compact_support_integral_sub_le [locally_compact_space Î±] [Î¼.regular]
  {f : Î± â†’ E} (hf : integrable f Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (g : Î± â†’ E), âˆ« x, â€–f x - g xâ€– âˆ‚Î¼ â‰¤ Îµ âˆ§ continuous g âˆ§ integrable g Î¼
    âˆ§ has_compact_support g :=
begin
  simp only [â† mem_â„’p_one_iff_integrable, â† snorm_one_eq_lintegral_nnnorm,
    â† ennreal.of_real_one] at hf âŠ¢,
  simpa using hf.exists_has_compact_support_integral_rpow_sub_le le_rfl hÎµ,
end

/-- Any function in `â„’p` can be approximated by bounded continuous functions when `1 â‰¤ p < âˆž`,
version in terms of `snorm`. -/
lemma mem_â„’p.exists_bounded_continuous_snorm_sub_le [Î¼.weakly_regular] (hp : p â‰  âˆž) (h'p : 1 â‰¤ p)
  {f : Î± â†’ E} (hf : mem_â„’p f p Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ (g : Î± â†’áµ‡ E), snorm (f - g) p Î¼ â‰¤ Îµ âˆ§ mem_â„’p g p Î¼ :=
begin
  -- It suffices to check that the set of functions we consider approximates characteristic
  -- functions, is stable under addition and made of ae strongly measurable functions.
  -- First check the latter easy facts.
  suffices H : âˆƒ (g : Î± â†’ E), snorm (f - g) p Î¼ â‰¤ Îµ âˆ§ continuous g âˆ§ mem_â„’p g p Î¼ âˆ§
    metric.bounded (range g),
  { rcases H with âŸ¨g, hg, g_cont, g_mem, g_bdâŸ©,
    exact âŸ¨âŸ¨âŸ¨g, g_contâŸ©, metric.bounded_range_iff.1 g_bdâŸ©, hg, g_memâŸ© },
  apply hf.induction_dense hp h'p _ _ _ _ hÎµ, rotate,
  { rintros f g âŸ¨f_cont, f_mem, f_bdâŸ© âŸ¨g_cont, g_mem, g_bdâŸ©,
    refine âŸ¨f_cont.add g_cont, f_mem.add g_mem, _âŸ©,
    let f' : Î± â†’áµ‡ E := âŸ¨âŸ¨f, f_contâŸ©, metric.bounded_range_iff.1 f_bdâŸ©,
    let g' : Î± â†’áµ‡ E := âŸ¨âŸ¨g, g_contâŸ©, metric.bounded_range_iff.1 g_bdâŸ©,
    exact (f' + g').bounded_range },
  { exact Î» f âŸ¨_, h, _âŸ©, h.ae_strongly_measurable },
  -- We are left with approximating characteristic functions.
  -- This follows from `exists_continuous_snorm_sub_le_of_closed`.
  assume c t ht htÎ¼ Îµ hÎµ,
  have h'Îµ : Îµ / 2 â‰  0, by simpa using hÎµ,
  rcases exists_snorm_indicator_le hp c h'Îµ with âŸ¨Î·, Î·pos, hÎ·âŸ©,
  have hÎ·_pos' : (0 : â„â‰¥0âˆž) < Î·, from ennreal.coe_pos.2 Î·pos,
  obtain âŸ¨s, st, s_closed, Î¼sâŸ© : âˆƒ s âŠ† t, is_closed s âˆ§ Î¼ (t \ s) < Î·,
    from ht.exists_is_closed_diff_lt htÎ¼.ne hÎ·_pos'.ne',
  have hsÎ¼ : Î¼ s < âˆž, from (measure_mono st).trans_lt htÎ¼,
  have I1 : snorm (s.indicator (Î» y, c) - t.indicator (Î» y, c)) p Î¼ â‰¤ Îµ/2,
  { rw [â† snorm_neg, neg_sub, â† indicator_diff st],
    exact (hÎ· _ Î¼s.le) },
  rcases exists_continuous_snorm_sub_le_of_closed hp s_closed is_open_univ (subset_univ _)
    hsÎ¼.ne c h'Îµ with âŸ¨f, I2, f_cont, f_bound, -, f_memâŸ©,
  have I3 : snorm (f - t.indicator (Î» y, c)) p Î¼ â‰¤ Îµ, from calc
    snorm (f - t.indicator (Î» y, c)) p Î¼
      = snorm ((f - s.indicator (Î» y, c)) + (s.indicator (Î» y, c) - t.indicator (Î» y, c))) p Î¼ :
    by simp only [sub_add_sub_cancel]
  ... â‰¤ snorm (f - s.indicator (Î» y, c)) p Î¼
        + snorm (s.indicator (Î» y, c) - t.indicator (Î» y, c)) p Î¼ :
    begin
      refine snorm_add_le _ _ h'p,
      { exact f_mem.ae_strongly_measurable.sub
          (ae_strongly_measurable_const.indicator s_closed.measurable_set) },
      { exact (ae_strongly_measurable_const.indicator s_closed.measurable_set).sub
          (ae_strongly_measurable_const.indicator ht) },
    end
  ... â‰¤ Îµ/2 + Îµ/2 : add_le_add I2 I1
  ... = Îµ : ennreal.add_halves _,
  refine âŸ¨f, I3, f_cont, f_mem, _âŸ©,
  exact (bounded_continuous_function.of_normed_add_comm_group f f_cont _ f_bound).bounded_range,
end

/-- Any function in `â„’p` can be approximated by bounded continuous functions when `1 â‰¤ p < âˆž`,
version in terms of `âˆ«`. -/
lemma mem_â„’p.exists_bounded_continuous_integral_rpow_sub_le
  [Î¼.weakly_regular] {p : â„} (h'p : 1 â‰¤ p)
  {f : Î± â†’ E} (hf : mem_â„’p f (ennreal.of_real p) Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (g : Î± â†’áµ‡ E), âˆ« x, â€–f x - g xâ€–^p âˆ‚Î¼ â‰¤ Îµ âˆ§ mem_â„’p g (ennreal.of_real p) Î¼ :=
begin
  have I : 0 < Îµ ^ (1/p) := real.rpow_pos_of_pos hÎµ _,
  have A : ennreal.of_real (Îµ ^ (1/p)) â‰  0,
    by simp only [ne.def, ennreal.of_real_eq_zero, not_le, I],
  have B : 1 â‰¤ ennreal.of_real p,
  { convert ennreal.of_real_le_of_real h'p, exact ennreal.of_real_one.symm },
  rcases hf.exists_bounded_continuous_snorm_sub_le ennreal.coe_ne_top B A
    with âŸ¨g, hg, g_memâŸ©,
  change snorm _ (ennreal.of_real p) _ â‰¤ _ at hg,
  refine âŸ¨g, _, g_memâŸ©,
  rwa [(hf.sub g_mem).snorm_eq_integral_rpow_norm (zero_lt_one.trans_le B).ne'
    ennreal.coe_ne_top, ennreal.of_real_le_of_real_iff I.le, one_div,
    ennreal.to_real_of_real (zero_le_one.trans h'p), real.rpow_le_rpow_iff _ hÎµ.le _] at hg,
  { exact integral_nonneg (Î» x, real.rpow_nonneg_of_nonneg (norm_nonneg _) _) },
  { exact inv_pos.2 (zero_lt_one.trans_le h'p) }
end

/-- Any integrable function can be approximated by bounded continuous functions,
version in terms of `âˆ«â»`. -/
lemma integrable.exists_bounded_continuous_lintegral_sub_le [Î¼.weakly_regular]
  {f : Î± â†’ E} (hf : integrable f Î¼) {Îµ : â„â‰¥0âˆž} (hÎµ : Îµ â‰  0) :
  âˆƒ (g : Î± â†’áµ‡ E), âˆ«â» x, â€–f x - g xâ€–â‚Š âˆ‚Î¼ â‰¤ Îµ âˆ§ integrable g Î¼ :=
begin
  simp only [â† mem_â„’p_one_iff_integrable, â† snorm_one_eq_lintegral_nnnorm] at hf âŠ¢,
  exact hf.exists_bounded_continuous_snorm_sub_le ennreal.one_ne_top le_rfl hÎµ,
end

/-- Any integrable function can be approximated by bounded continuous functions,
version in terms of `âˆ«`. -/
lemma integrable.exists_bounded_continuous_integral_sub_le [Î¼.weakly_regular]
  {f : Î± â†’ E} (hf : integrable f Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (g : Î± â†’áµ‡ E), âˆ« x, â€–f x - g xâ€– âˆ‚Î¼ â‰¤ Îµ âˆ§ integrable g Î¼ :=
begin
  simp only [â† mem_â„’p_one_iff_integrable, â† snorm_one_eq_lintegral_nnnorm,
    â† ennreal.of_real_one] at hf âŠ¢,
  simpa using hf.exists_bounded_continuous_integral_rpow_sub_le le_rfl hÎµ,
end

namespace Lp

variables (E)

/-- A function in `Lp` can be approximated in `Lp` by continuous functions. -/
lemma bounded_continuous_function_dense
  [second_countable_topology_either Î± E] [_i : fact (1 â‰¤ p)] (hp : p â‰  âˆž) [Î¼.weakly_regular] :
  (bounded_continuous_function E p Î¼).topological_closure = âŠ¤ :=
begin
  rw add_subgroup.eq_top_iff',
  assume f,
  refine mem_closure_iff_frequently.mpr _,
  rw metric.nhds_basis_closed_ball.frequently_iff,
  intros Îµ hÎµ,
  have A : ennreal.of_real Îµ â‰  0, by simp only [ne.def, ennreal.of_real_eq_zero, not_le, hÎµ],
  obtain âŸ¨g, hg, g_memâŸ© : âˆƒ (g : Î± â†’áµ‡ E), snorm (f - g) p Î¼ â‰¤ ennreal.of_real Îµ âˆ§ mem_â„’p g p Î¼,
    from (Lp.mem_â„’p f).exists_bounded_continuous_snorm_sub_le hp _i.out A,
  refine âŸ¨g_mem.to_Lp _, _, âŸ¨g, rflâŸ©âŸ©,
  simp only [dist_eq_norm, metric.mem_closed_ball'],
  rw Lp.norm_def,
  convert ennreal.to_real_le_of_le_of_real hÎµ.le hg using 2,
  apply snorm_congr_ae,
  filter_upwards [coe_fn_sub f (g_mem.to_Lp g), g_mem.coe_fn_to_Lp] with x hx h'x,
  simp only [hx, pi.sub_apply, sub_right_inj, h'x],
end

end Lp

end measure_theory

variables [second_countable_topology_either Î± E] [_i : fact (1 â‰¤ p)] (hp : p â‰  âˆž)
variables (ð•œ : Type*) [normed_field ð•œ] [normed_algebra â„ ð•œ] [normed_space ð•œ E]
include _i hp
variables (E) (Î¼)

namespace bounded_continuous_function

lemma to_Lp_dense_range [Î¼.weakly_regular] [is_finite_measure Î¼] :
  dense_range â‡‘(to_Lp p Î¼ ð•œ : (Î± â†’áµ‡ E) â†’L[ð•œ] Lp E p Î¼) :=
begin
  haveI : normed_space â„ E := restrict_scalars.normed_space â„ ð•œ E,
  rw dense_range_iff_closure_range,
  suffices : (linear_map.range (to_Lp p Î¼ ð•œ : _ â†’L[ð•œ] Lp E p Î¼))
    .to_add_subgroup.topological_closure = âŠ¤,
  { exact congr_arg coe this },
  simp [range_to_Lp p Î¼, measure_theory.Lp.bounded_continuous_function_dense E hp],
end

end bounded_continuous_function

namespace continuous_map

lemma to_Lp_dense_range [compact_space Î±] [Î¼.weakly_regular] [is_finite_measure Î¼] :
  dense_range â‡‘(to_Lp p Î¼ ð•œ : C(Î±, E) â†’L[ð•œ] Lp E p Î¼) :=
begin
  haveI : normed_space â„ E := restrict_scalars.normed_space â„ ð•œ E,
  rw dense_range_iff_closure_range,
  suffices : (linear_map.range (to_Lp p Î¼ ð•œ : _ â†’L[ð•œ] Lp E p Î¼))
    .to_add_subgroup.topological_closure = âŠ¤,
  { exact congr_arg coe this },
  simp [range_to_Lp p Î¼, measure_theory.Lp.bounded_continuous_function_dense E hp]
end

end continuous_map
