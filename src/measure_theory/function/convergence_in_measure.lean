/-
Copyright (c) 2022 RÃ©my Degenne, Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Kexing Ying
-/

import measure_theory.function.uniform_integrable

/-!
# Convergence in measure

We define convergence in measure which is one of the many notions of convergence in probability.
Convergence in measure is most notably used in the formulation of the weak law of large numbers
and is also useful in theorems such as the Vitali convergence theorem. This file provides some
basic lemmas for working with convergence in measure and establishes some relations between
convergence in measure and other notions of convergence.

## Main definitions

* `measure_theory.tendsto_in_measure (Î¼ : measure Î±) (f : Î¹ â†’ Î± â†’ E) (g : Î± â†’ E)`: `f` converges
  in `Î¼`-measure to `g`.

## Main results

* `measure_theory.tendsto_in_measure_of_tendsto_ae`: convergence almost everywhere in a finite
  measure space implies convergence in measure.
* `measure_theory.tendsto_in_measure.exists_seq_tendsto_ae`: if `f` is a sequence of functions
  which converges in measure to `g`, then `f` has a subsequence which convergence almost
  everywhere to `g`.
* `measure_theory.tendsto_in_measure_of_tendsto_snorm`: convergence in Lp implies convergence
  in measure.
-/

open topological_space filter
open_locale nnreal ennreal measure_theory topological_space

namespace measure_theory

variables {Î± Î¹ E : Type*} {m : measurable_space Î±} {Î¼ : measure Î±}

/-- A sequence of functions `f` is said to converge in measure to some function `g` if for all
`Îµ > 0`, the measure of the set `{x | Îµ â‰¤ dist (f i x) (g x)}` tends to 0 as `i` tends to
infinity. -/
def tendsto_in_measure [preorder Î¹] [has_dist E] {m : measurable_space Î±}
  (Î¼ : measure Î±) (f : Î¹ â†’ Î± â†’ E) (g : Î± â†’ E) : Prop :=
âˆ€ Îµ (hÎµ : 0 < Îµ), tendsto (Î» i, Î¼ {x | Îµ â‰¤ dist (f i x) (g x)}) at_top (ð“ 0)

lemma tendsto_in_measure_iff_norm [preorder Î¹] [semi_normed_group E] {f : Î¹ â†’ Î± â†’ E} {g : Î± â†’ E} :
  tendsto_in_measure Î¼ f g
    â†” âˆ€ Îµ (hÎµ : 0 < Îµ), tendsto (Î» i, Î¼ {x | Îµ â‰¤ âˆ¥f i x - g xâˆ¥}) at_top (ð“ 0) :=
by simp_rw [tendsto_in_measure, dist_eq_norm]

namespace tendsto_in_measure

variables [preorder Î¹] [has_dist E] {f f' : Î¹ â†’ Î± â†’ E} {g g' : Î± â†’ E}

protected lemma congr (h_left : âˆ€ i, f i =áµ[Î¼] f' i) (h_right : g =áµ[Î¼] g')
  (h_tendsto : tendsto_in_measure Î¼ f g) :
  tendsto_in_measure Î¼ f' g' :=
begin
  intros Îµ hÎµ,
  specialize h_tendsto Îµ hÎµ,
  suffices : (Î» i, Î¼ {x | Îµ â‰¤ dist (f' i x) (g' x)}) = (Î» i, Î¼ {x | Îµ â‰¤ dist (f i x) (g x)}),
    by rwa this,
  refine funext (Î» i, measure_congr _),
  filter_upwards [h_left i, h_right],
  intros x hxf hxg,
  rw eq_iff_iff,
  change Îµ â‰¤ dist (f' i x) (g' x) â†” Îµ â‰¤ dist (f i x) (g x),
  rw [hxg, hxf],
end

lemma congr_left (h : âˆ€ i, f i =áµ[Î¼] f' i) (h_tendsto : tendsto_in_measure Î¼ f g) :
  tendsto_in_measure Î¼ f' g :=
h_tendsto.congr h (eventually_eq.rfl)

lemma congr_right (h : g =áµ[Î¼] g') (h_tendsto : tendsto_in_measure Î¼ f g) :
  tendsto_in_measure Î¼ f g' :=
h_tendsto.congr (Î» i, eventually_eq.rfl) h

end tendsto_in_measure

section

variables [metric_space E]
variables {f : â„• â†’ Î± â†’ E} {g : Î± â†’ E}

/-- Auxiliary lemma for `tendsto_in_measure_of_tendsto_ae`. -/
lemma tendsto_in_measure_of_tendsto_ae_of_measurable
  [measurable_space E] [second_countable_topology E] [borel_space E] [is_finite_measure Î¼]
  (hf : âˆ€ n, measurable (f n)) (hg : measurable g)
  (hfg : âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, f n x) at_top (ð“ (g x))) :
  tendsto_in_measure Î¼ f g :=
begin
  refine Î» Îµ hÎµ, ennreal.tendsto_at_top_zero.mpr (Î» Î´ hÎ´, _),
  by_cases hÎ´i : Î´ = âˆž,
  { simp only [hÎ´i, implies_true_iff, le_top, exists_const], },
  lift Î´ to â„â‰¥0 using hÎ´i,
  rw [gt_iff_lt, ennreal.coe_pos, â† nnreal.coe_pos] at hÎ´,
  obtain âŸ¨t, htm, ht, hunifâŸ© := tendsto_uniformly_on_of_ae_tendsto' hf hg hfg hÎ´,
  rw ennreal.of_real_coe_nnreal at ht,
  rw metric.tendsto_uniformly_on_iff at hunif,
  obtain âŸ¨N, hNâŸ© := eventually_at_top.1 (hunif Îµ hÎµ),
  refine âŸ¨N, Î» n hn, _âŸ©,
  suffices : {x : Î± | Îµ â‰¤ dist (f n x) (g x)} âŠ† t, from (measure_mono this).trans ht,
  rw â† set.compl_subset_compl,
  intros x hx,
  rw [set.mem_compl_eq, set.nmem_set_of_eq, dist_comm, not_le],
  exact hN n hn x hx,
end

/-- Convergence a.e. implies convergence in measure in a finite measure space. -/
lemma tendsto_in_measure_of_tendsto_ae
  [measurable_space E] [second_countable_topology E] [borel_space E] [is_finite_measure Î¼]
  (hf : âˆ€ n, ae_measurable (f n) Î¼) (hg : ae_measurable g Î¼)
  (hfg : âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, f n x) at_top (ð“ (g x))) :
  tendsto_in_measure Î¼ f g :=
begin
  refine tendsto_in_measure.congr (Î» i, (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm _,
  refine tendsto_in_measure_of_tendsto_ae_of_measurable (Î» i, (hf i).measurable_mk)
    hg.measurable_mk _,
  have hf_eq_ae : âˆ€áµ x âˆ‚Î¼, âˆ€ n, (hf n).mk (f n) x = f n x,
    from ae_all_iff.mpr (Î» n, (hf n).ae_eq_mk.symm),
  filter_upwards [hf_eq_ae, hg.ae_eq_mk, hfg],
  intros x hxf hxg hxfg,
  rw [â† hxg, funext (Î» n, hxf n)],
  exact hxfg,
end

/-- If `f` is a sequence of functions which converges in measure to `g`, then there exists a
subsequence of `f` which converges a.e. to `g`. -/
lemma tendsto_in_measure.exists_seq_tendsto_ae
  (hfg : tendsto_in_measure Î¼ f g) :
  âˆƒ ns : â„• â†’ â„•, âˆ€áµ x âˆ‚Î¼, tendsto (Î» i, f (ns i) x) at_top (ð“ (g x)) :=
begin
  have : âˆ€ k : â„•, âˆƒ N, âˆ€ n â‰¥ N, Î¼ {x | 2â»Â¹ ^ k â‰¤ dist (f n x) (g x)} â‰¤ 2â»Â¹ ^ k,
  { intro k,
    specialize hfg (2â»Â¹ ^ k) (by simp only [zero_lt_bit0, pow_pos, zero_lt_one, inv_pos]),
    rw ennreal.tendsto_at_top_zero at hfg,
    exact hfg (2â»Â¹ ^ k) (pos_iff_ne_zero.mpr (Î» h_zero, by simpa using pow_eq_zero h_zero)), },
  have h_lt_Îµ_real : âˆ€ (Îµ : â„) (hÎµ : 0 < Îµ), âˆƒ k : â„•, 2â»Â¹ ^ (k - 1 : â„) < Îµ,
  { intros Îµ hÎµ,
    obtain âŸ¨k, h_kâŸ© : âˆƒ (k : â„•), 2â»Â¹ ^ k < Îµ := exists_pow_lt_of_lt_one hÎµ (by norm_num),
    refine âŸ¨k+1, (le_of_eq _).trans_lt h_kâŸ©,
    rw [nat.cast_add, nat.cast_one, add_tsub_cancel_right, real.rpow_nat_cast] },
  let ns := Î» k, (this k).some,
  use ns,
  let S := Î» k, {x | 2â»Â¹ ^ k â‰¤ dist (f (ns k) x) (g x)},
  have hÎ¼S_le : âˆ€ k, Î¼ (S k) â‰¤ 2â»Â¹ ^ k,
  { have h_ns_k : âˆ€ k, âˆ€ n, n â‰¥ ns k â†’ Î¼ {x | 2â»Â¹ ^ k â‰¤ dist (f n x) (g x)} â‰¤ 2â»Â¹ ^ k,
      from Î» k, (this k).some_spec,
    exact Î» k, h_ns_k k (ns k) (le_rfl) },
  let s := â‹‚ k, â‹ƒ i (hik : k â‰¤ i), S i,
  have hÎ¼s : Î¼ s = 0,
  { have : âˆ€ k, s âŠ† â‹ƒ i (hik : k â‰¤ i), S i := Î» k, infi_le (Î» k, â‹ƒ i (hik : k â‰¤ i), S i) k,
    have hÎ¼s_le : âˆ€ k : â„•, Î¼ s â‰¤ ennreal.of_real (2â»Â¹ ^ ((k : â„) - 1)),
    { refine Î» k, (measure_mono (this k)).trans ((measure_Union_le _).trans _),
      have hÎ¼_if_eq : âˆ€ i, Î¼ (â‹ƒ (hik : k â‰¤ i), S i) = if k â‰¤ i then Î¼ (S i) else 0,
      { intro i, split_ifs; simp only [h, measure_empty, set.Union_true, set.Union_false] },
      rw tsum_congr hÎ¼_if_eq,
      dsimp only,
      have tsum_le_tsum : âˆ‘' i, ite (k â‰¤ i) (Î¼ (S i)) 0
        â‰¤ âˆ‘' i, ite (k â‰¤ i) (2â»Â¹ ^ i) 0,
      { refine tsum_le_tsum (Î» i, _) ennreal.summable ennreal.summable,
        split_ifs; simp only [hÎ¼S_le i, nonpos_iff_eq_zero] },
      refine tsum_le_tsum.trans _,
      have tsum_eq_of_real_tsum : âˆ‘' i, ite (k â‰¤ i) ((2 : â„â‰¥0âˆž)â»Â¹ ^ i) 0
        = ennreal.of_real (âˆ‘' i, ite (k â‰¤ i) (2â»Â¹ ^ i) 0),
      { rw ennreal.of_real_tsum_of_nonneg,
        { refine tsum_congr (Î» i, _),
          split_ifs,
          { rw [ennreal.of_real_pow (inv_nonneg.mpr zero_le_two) i,
            â† ennreal.of_real_inv_of_pos zero_lt_two, ennreal.of_real_bit0 zero_le_one,
            ennreal.of_real_one] },
          { exact ennreal.of_real_zero.symm } },
        { intro n,
          split_ifs,
          { refine pow_nonneg _ _, norm_num },
          { exact le_rfl } },
        { refine summable.summable_of_eq_zero_or_self summable_geometric_two (Î» i, _),
          split_ifs,
          { simp only [one_div, eq_self_iff_true, or_true] },
          { exact or.inl rfl } } },
      rw tsum_eq_of_real_tsum,
      refine ennreal.of_real_le_of_real (le_of_eq $ tsum_geometric_inv_two_ge k) },
    refine le_antisymm (ennreal.le_of_forall_pos_le_add (Î» Îµ hÎµ _, _)) (zero_le _),
    rw zero_add,
    obtain âŸ¨k, hk_lt_ÎµâŸ© := h_lt_Îµ_real Îµ hÎµ,
    exact ((hÎ¼s_le k).trans (ennreal.of_real_le_of_real hk_lt_Îµ.le)).trans
      (ennreal.of_real_coe_nnreal).le },
  have h_tendsto : âˆ€ x âˆˆ sá¶œ, tendsto (Î» i, f (ns i) x) at_top (ð“ (g x)),
  { refine Î» x hx, metric.tendsto_at_top.mpr (Î» Îµ hÎµ, _),
    simp_rw [s, set.compl_Inter, set.compl_Union, set.mem_Union, set.mem_Inter] at hx,
    obtain âŸ¨N, hNxâŸ© := hx,
    obtain âŸ¨k, hk_lt_ÎµâŸ© := h_lt_Îµ_real Îµ hÎµ,
    refine âŸ¨max N (k - 1), Î» n hn_ge, lt_of_le_of_lt _ hk_lt_ÎµâŸ©,
    specialize hNx n ((le_max_left _ _).trans hn_ge),
    have h_inv_n_le_k : (2 : â„)â»Â¹ ^ n â‰¤ 2â»Â¹ ^ ((k : â„) - 1),
    { rw [â† real.rpow_nat_cast],
      refine real.rpow_le_rpow_of_exponent_ge ((one_div (2 : â„)) â–¸ one_half_pos)
        (inv_le_one one_le_two) _,
      rw [sub_le_iff_le_add, â† nat.cast_add_one, nat.cast_le],
      exact (le_tsub_add.trans (add_le_add_right (le_max_right _ _) 1)).trans
        (add_le_add_right hn_ge 1) },
    refine le_trans _ h_inv_n_le_k,
    rw [set.mem_compl_iff, set.nmem_set_of_eq, not_le] at hNx,
    exact hNx.le },
  rw ae_iff,
  refine measure_mono_null (Î» x, _) hÎ¼s,
  rw [set.mem_set_of_eq, â† @not_not (x âˆˆ s), not_imp_not],
  exact h_tendsto x,
end

end

section

variables [measurable_space E] [normed_group E] [borel_space E] [has_measurable_subâ‚‚ E] {p : â„â‰¥0âˆž}
variables {f : â„• â†’ Î± â†’ E} {g : Î± â†’ E}

/-- Auxiliary lemma for `tendsto_in_measure_of_tendsto_snorm`. -/
lemma tendsto_in_measure_of_tendsto_snorm_of_measurable
  (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆž)
  (hf : âˆ€ n, measurable (f n)) (hg : measurable g)
  (hfg : tendsto (Î» n, snorm (f n - g) p Î¼) at_top (ð“ 0)) :
  tendsto_in_measure Î¼ f g :=
begin
  intros Îµ hÎµ,
  replace hfg := ennreal.tendsto.const_mul (tendsto.ennrpow_const p.to_real hfg)
    (or.inr $ @ennreal.of_real_ne_top (1 / Îµ ^ (p.to_real))),
  simp only [mul_zero, ennreal.zero_rpow_of_pos (ennreal.to_real_pos hp_ne_zero hp_ne_top)] at hfg,
  rw ennreal.tendsto_at_top_zero at hfg âŠ¢,
  intros Î´ hÎ´,
  obtain âŸ¨N, hNâŸ© := hfg Î´ hÎ´,
  refine âŸ¨N, Î» n hn, le_trans _ (hN n hn)âŸ©,
  rw [ennreal.of_real_div_of_pos, ennreal.of_real_one, mul_comm, mul_one_div,
      ennreal.le_div_iff_mul_le, mul_comm],
  { convert mul_meas_ge_le_pow_snorm' Î¼ hp_ne_zero hp_ne_top ((hf n).sub hg)
      (ennreal.of_real Îµ),
    { exact (ennreal.of_real_rpow_of_pos hÎµ).symm },
    { ext x,
      rw [dist_eq_norm, â† ennreal.of_real_le_of_real_iff (norm_nonneg _),
          of_real_norm_eq_coe_nnnorm] } },
  { refine or.inl _,
    rw [ne, ennreal.of_real_eq_zero, not_le],
    exact real.rpow_pos_of_pos hÎµ _ },
  { exact or.inl (ennreal.of_real_ne_top) },
  { exact real.rpow_pos_of_pos hÎµ _ }
end

/-- Convergence in Lp implies convergence in measure. -/
lemma tendsto_in_measure_of_tendsto_snorm
  (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆž)
  (hf : âˆ€ n, ae_measurable (f n) Î¼) (hg : ae_measurable g Î¼)
  (hfg : tendsto (Î» n, snorm (f n - g) p Î¼) at_top (ð“ 0)) :
  tendsto_in_measure Î¼ f g :=
begin
  refine tendsto_in_measure.congr (Î» i, (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm _,
  refine tendsto_in_measure_of_tendsto_snorm_of_measurable hp_ne_zero hp_ne_top
    (Î» i, (hf i).measurable_mk) hg.measurable_mk _,
  have hf_eq_ae : âˆ€áµ x âˆ‚Î¼, âˆ€ n, (hf n).mk (f n) x = f n x,
    from ae_all_iff.mpr (Î» n, (hf n).ae_eq_mk.symm),
  have : (Î» n, snorm ((hf n).mk (f n) - ae_measurable.mk g hg) p Î¼) = (Î» n, snorm (f n - g) p Î¼),
  { ext1 n, refine snorm_congr_ae (eventually_eq.sub (hf n).ae_eq_mk.symm hg.ae_eq_mk.symm), },
  rw this,
  exact hfg,
end

/-- Convergence in Lp implies convergence in measure. -/
lemma tendsto_in_measure_of_tendsto_Lp [second_countable_topology E] [hp : fact (1 â‰¤ p)]
  {f : â„• â†’ Lp E p Î¼} {g : Lp E p Î¼} (hp_ne_top : p â‰  âˆž)
  (hfg : tendsto f at_top (ð“ g)) :
  tendsto_in_measure Î¼ (Î» n, f n) g :=
tendsto_in_measure_of_tendsto_snorm (ennreal.zero_lt_one.trans_le hp.elim).ne.symm hp_ne_top
  (Î» n, Lp.ae_measurable _) (Lp.ae_measurable _) ((Lp.tendsto_Lp_iff_tendsto_â„’p' _ _).mp hfg)

end

end measure_theory
