/-
Copyright (c) 2022 R√©my Degenne, Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, Kexing Ying
-/

import measure_theory.function.uniform_integrable

/-!
# Convergence in measure

We define convergence in measure which is one of the many notions of convergence in probability.
A sequence of functions `f` is said to converge in measure to some function `g`
if for all `Œµ > 0`, the measure of the set `{x | Œµ ‚â§ dist (f i x) (g x)}` tends to 0 as `i`
tends to infinity. Convergence in measure is most notably used in the formulation of the weak
law of large numbers and is also useful in theorems such as the Vitali convergence theorem.
This file provides some basic lemmas for working with convergence in measure and establishes
some relations between convergence in measure and other notions of convergence.

## Main definitions

* `measure_theory.tendsto_in_measure (Œº : measure Œ±) (f : Œπ ‚Üí Œ± ‚Üí E) (g : Œ± ‚Üí E)`: `f` converges
  in `Œº`-measure to `g`.

## Main results

* `measure_theory.tendsto_in_measure_of_tendsto_ae`: convergence almost everywhere in a finite
  measure space implies convergence in measure.
* `measure_theory.tendsto_in_measure.exists_seq_tendsto_ae`: if `f` is a sequence of functions
  which converges in measure to `g`, then `f` has a subsequence which convergence almost
  everywhere to `g`.
* `measure_theory.tendsto_in_measure_of_tendsto_snorm`: convergence in Lp implies convergence
  in measure.
-/

open topological_space filter
open_locale nnreal ennreal measure_theory topological_space

namespace measure_theory

variables {Œ± Œπ E : Type*} {m : measurable_space Œ±} {Œº : measure Œ±}

/-- A sequence of functions `f` is said to converge in measure to some function `g` if for all
`Œµ > 0`, the measure of the set `{x | Œµ ‚â§ dist (f i x) (g x)}` tends to 0 as `i` tends to
infinity. -/
def tendsto_in_measure [has_dist E] {m : measurable_space Œ±}
  (Œº : measure Œ±) (f : Œπ ‚Üí Œ± ‚Üí E) (l : filter Œπ) (g : Œ± ‚Üí E) : Prop :=
‚àÄ Œµ (hŒµ : 0 < Œµ), tendsto (Œª i, Œº {x | Œµ ‚â§ dist (f i x) (g x)}) l (ùìù 0)

lemma tendsto_in_measure_iff_norm [semi_normed_group E] {l : filter Œπ}
  {f : Œπ ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E} :
  tendsto_in_measure Œº f l g
  ‚Üî ‚àÄ Œµ (hŒµ : 0 < Œµ), tendsto (Œª i, Œº {x | Œµ ‚â§ ‚à•f i x - g x‚à•}) l (ùìù 0) :=
by simp_rw [tendsto_in_measure, dist_eq_norm]

namespace tendsto_in_measure

variables [has_dist E] {l : filter Œπ} {f f' : Œπ ‚Üí Œ± ‚Üí E} {g g' : Œ± ‚Üí E}

protected lemma congr' (h_left : ‚àÄ·∂† i in l, f i =·µê[Œº] f' i) (h_right : g =·µê[Œº] g')
  (h_tendsto : tendsto_in_measure Œº f l g) :
  tendsto_in_measure Œº f' l g' :=
begin
  intros Œµ hŒµ,
  suffices : (Œª i, Œº {x | Œµ ‚â§ dist (f' i x) (g' x)})
      =·∂†[l] (Œª i, Œº {x | Œµ ‚â§ dist (f i x) (g x)}),
  { rw tendsto_congr' this,
    exact h_tendsto Œµ hŒµ, },
  filter_upwards [h_left],
  intros i h_ae_eq,
  refine measure_congr _,
  filter_upwards [h_ae_eq, h_right],
  intros x hxf hxg,
  rw eq_iff_iff,
  change Œµ ‚â§ dist (f' i x) (g' x) ‚Üî Œµ ‚â§ dist (f i x) (g x),
  rw [hxg, hxf],
end

protected lemma congr (h_left : ‚àÄ i, f i =·µê[Œº] f' i) (h_right : g =·µê[Œº] g')
  (h_tendsto : tendsto_in_measure Œº f l g) :
  tendsto_in_measure Œº f' l g' :=
tendsto_in_measure.congr' (eventually_of_forall h_left) h_right h_tendsto

lemma congr_left (h : ‚àÄ i, f i =·µê[Œº] f' i) (h_tendsto : tendsto_in_measure Œº f l g) :
  tendsto_in_measure Œº f' l g :=
h_tendsto.congr h (eventually_eq.rfl)

lemma congr_right (h : g =·µê[Œº] g') (h_tendsto : tendsto_in_measure Œº f l g) :
  tendsto_in_measure Œº f l g' :=
h_tendsto.congr (Œª i, eventually_eq.rfl) h

end tendsto_in_measure

section exists_seq_tendsto_ae

variables [metric_space E]
variables {f : ‚Ñï ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E}

/-- Auxiliary lemma for `tendsto_in_measure_of_tendsto_ae`. -/
lemma tendsto_in_measure_of_tendsto_ae_of_measurable
  [measurable_space E] [second_countable_topology E] [borel_space E] [is_finite_measure Œº]
  (hf : ‚àÄ n, measurable (f n)) (hg : measurable g)
  (hfg : ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (g x))) :
  tendsto_in_measure Œº f at_top g :=
begin
  refine Œª Œµ hŒµ, ennreal.tendsto_at_top_zero.mpr (Œª Œ¥ hŒ¥, _),
  by_cases hŒ¥i : Œ¥ = ‚àû,
  { simp only [hŒ¥i, implies_true_iff, le_top, exists_const], },
  lift Œ¥ to ‚Ñù‚â•0 using hŒ¥i,
  rw [gt_iff_lt, ennreal.coe_pos, ‚Üê nnreal.coe_pos] at hŒ¥,
  obtain ‚ü®t, htm, ht, hunif‚ü© := tendsto_uniformly_on_of_ae_tendsto' hf hg hfg hŒ¥,
  rw ennreal.of_real_coe_nnreal at ht,
  rw metric.tendsto_uniformly_on_iff at hunif,
  obtain ‚ü®N, hN‚ü© := eventually_at_top.1 (hunif Œµ hŒµ),
  refine ‚ü®N, Œª n hn, _‚ü©,
  suffices : {x : Œ± | Œµ ‚â§ dist (f n x) (g x)} ‚äÜ t, from (measure_mono this).trans ht,
  rw ‚Üê set.compl_subset_compl,
  intros x hx,
  rw [set.mem_compl_eq, set.nmem_set_of_eq, dist_comm, not_le],
  exact hN n hn x hx,
end

/-- Convergence a.e. implies convergence in measure in a finite measure space. -/
lemma tendsto_in_measure_of_tendsto_ae
  [measurable_space E] [second_countable_topology E] [borel_space E] [is_finite_measure Œº]
  (hf : ‚àÄ n, ae_measurable (f n) Œº) (hfg : ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (g x))) :
  tendsto_in_measure Œº f at_top g :=
begin
  have hg : ae_measurable g Œº, from ae_measurable_of_tendsto_metric_ae hf hfg,
  refine tendsto_in_measure.congr (Œª i, (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm _,
  refine tendsto_in_measure_of_tendsto_ae_of_measurable (Œª i, (hf i).measurable_mk)
    hg.measurable_mk _,
  have hf_eq_ae : ‚àÄ·µê x ‚àÇŒº, ‚àÄ n, (hf n).mk (f n) x = f n x,
    from ae_all_iff.mpr (Œª n, (hf n).ae_eq_mk.symm),
  filter_upwards [hf_eq_ae, hg.ae_eq_mk, hfg],
  intros x hxf hxg hxfg,
  rw [‚Üê hxg, funext (Œª n, hxf n)],
  exact hxfg,
end

namespace exists_seq_tendsto_ae

lemma exists_nat_measure_lt_two_inv (hfg : tendsto_in_measure Œº f at_top g) (n : ‚Ñï) :
  ‚àÉ N, ‚àÄ m ‚â• N, Œº {x | 2‚Åª¬π ^ n ‚â§ dist (f m x) (g x)} ‚â§ 2‚Åª¬π ^ n :=
begin
  specialize hfg (2‚Åª¬π ^ n) (by simp only [zero_lt_bit0, pow_pos, zero_lt_one, inv_pos]),
  rw ennreal.tendsto_at_top_zero at hfg,
  exact hfg (2‚Åª¬π ^ n) (pos_iff_ne_zero.mpr (Œª h_zero, by simpa using pow_eq_zero h_zero))
end

/-- Given a sequence of functions `f` which converges in measure to `g`,
`seq_tendsto_ae_seq_aux` is a sequence such that
`‚àÄ m ‚â• seq_tendsto_ae_seq_aux n, Œº {x | 2‚Åª¬π ^ n ‚â§ dist (f m x) (g x)} ‚â§ 2‚Åª¬π ^ n`. -/
noncomputable
def seq_tendsto_ae_seq_aux (hfg : tendsto_in_measure Œº f at_top g) (n : ‚Ñï) :=
classical.some (exists_nat_measure_lt_two_inv hfg n)

/-- Transformation of `seq_tendsto_ae_seq_aux` to makes sure it is strictly monotone. -/
noncomputable
def seq_tendsto_ae_seq (hfg : tendsto_in_measure Œº f at_top g) : ‚Ñï ‚Üí ‚Ñï
| 0 := seq_tendsto_ae_seq_aux hfg 0
| (n + 1) :=  max (seq_tendsto_ae_seq_aux hfg (n + 1))
  (seq_tendsto_ae_seq n + 1)

lemma seq_tendsto_ae_seq_succ (hfg : tendsto_in_measure Œº f at_top g) {n : ‚Ñï} :
  seq_tendsto_ae_seq hfg (n + 1) =
  max (seq_tendsto_ae_seq_aux hfg (n + 1)) (seq_tendsto_ae_seq hfg n + 1) :=
by rw seq_tendsto_ae_seq

lemma seq_tendsto_ae_seq_spec (hfg : tendsto_in_measure Œº f at_top g)
  (n k : ‚Ñï) (hn : seq_tendsto_ae_seq hfg n ‚â§ k) :
  Œº {x | 2‚Åª¬π ^ n ‚â§ dist (f k x) (g x)} ‚â§ 2‚Åª¬π ^ n :=
begin
  cases n,
  { exact classical.some_spec (exists_nat_measure_lt_two_inv hfg 0) k hn },
  { exact classical.some_spec (exists_nat_measure_lt_two_inv hfg _) _
      (le_trans (le_max_left _ _) hn) }
end

lemma seq_tendsto_ae_seq_strict_mono (hfg : tendsto_in_measure Œº f at_top g) :
  strict_mono (seq_tendsto_ae_seq hfg) :=
begin
  refine strict_mono_nat_of_lt_succ (Œª n, _),
  rw seq_tendsto_ae_seq_succ,
  exact lt_of_lt_of_le (lt_add_one $ seq_tendsto_ae_seq hfg n) (le_max_right _ _),
end

end exists_seq_tendsto_ae

/-- If `f` is a sequence of functions which converges in measure to `g`, then there exists a
subsequence of `f` which converges a.e. to `g`. -/
lemma tendsto_in_measure.exists_seq_tendsto_ae
  (hfg : tendsto_in_measure Œº f at_top g) :
  ‚àÉ ns : ‚Ñï ‚Üí ‚Ñï, strict_mono ns ‚àß ‚àÄ·µê x ‚àÇŒº, tendsto (Œª i, f (ns i) x) at_top (ùìù (g x)) :=
begin
  have h_lt_Œµ_real : ‚àÄ (Œµ : ‚Ñù) (hŒµ : 0 < Œµ), ‚àÉ k : ‚Ñï, 2‚Åª¬π ^ (k - 1 : ‚Ñù) < Œµ,
  { intros Œµ hŒµ,
    obtain ‚ü®k, h_k‚ü© : ‚àÉ (k : ‚Ñï), 2‚Åª¬π ^ k < Œµ := exists_pow_lt_of_lt_one hŒµ (by norm_num),
    refine ‚ü®k+1, (le_of_eq _).trans_lt h_k‚ü©,
    rw [nat.cast_add, nat.cast_one, add_tsub_cancel_right, real.rpow_nat_cast] },
  set ns := exists_seq_tendsto_ae.seq_tendsto_ae_seq hfg,
  use ns,
  let S := Œª k, {x | 2‚Åª¬π ^ k ‚â§ dist (f (ns k) x) (g x)},
  have hŒºS_le : ‚àÄ k, Œº (S k) ‚â§ 2‚Åª¬π ^ k :=
    Œª k, exists_seq_tendsto_ae.seq_tendsto_ae_seq_spec hfg k (ns k) (le_rfl),
  let s := ‚ãÇ k, ‚ãÉ i (hik : k ‚â§ i), S i,
  have hŒºs : Œº s = 0,
  { suffices hŒºs_le : ‚àÄ k : ‚Ñï, Œº s ‚â§ ennreal.of_real (2‚Åª¬π ^ ((k : ‚Ñù) - 1)),
    { refine le_antisymm (ennreal.le_of_forall_pos_le_add (Œª Œµ hŒµ _, _)) (zero_le _),
      rw zero_add,
      obtain ‚ü®k, hk_lt_Œµ‚ü© := h_lt_Œµ_real Œµ hŒµ,
      exact ((hŒºs_le k).trans (ennreal.of_real_le_of_real hk_lt_Œµ.le)).trans
        (ennreal.of_real_coe_nnreal).le },
    have : ‚àÄ k, s ‚äÜ ‚ãÉ i (hik : k ‚â§ i), S i := Œª k, infi_le (Œª k, ‚ãÉ i (hik : k ‚â§ i), S i) k,
    refine Œª k, (measure_mono (this k)).trans ((measure_Union_le _).trans _),
    have hŒº_if_eq : ‚àÄ i, Œº (‚ãÉ (hik : k ‚â§ i), S i) = if k ‚â§ i then Œº (S i) else 0,
    { intro i, split_ifs; simp only [h, measure_empty, set.Union_true, set.Union_false] },
    rw tsum_congr hŒº_if_eq,
    have tsum_le_tsum : ‚àë' i, ite (k ‚â§ i) (Œº (S i)) 0 ‚â§ ‚àë' i, ite (k ‚â§ i) (2‚Åª¬π ^ i) 0,
    { refine tsum_le_tsum (Œª i, _) ennreal.summable ennreal.summable,
      split_ifs; simp only [hŒºS_le i, nonpos_iff_eq_zero] },
    refine tsum_le_tsum.trans _,
    suffices tsum_eq_of_real_tsum : ‚àë' i, ite (k ‚â§ i) ((2 : ‚Ñù‚â•0‚àû)‚Åª¬π ^ i) 0
      = ennreal.of_real (‚àë' i, ite (k ‚â§ i) (2‚Åª¬π ^ i) 0),
    { rw tsum_eq_of_real_tsum,
      exact ennreal.of_real_le_of_real (tsum_geometric_inv_two_ge k).le },
    rw ennreal.of_real_tsum_of_nonneg,
    { refine tsum_congr (Œª i, _),
      split_ifs,
      { rw [ennreal.of_real_pow (inv_nonneg.mpr zero_le_two) i,
        ‚Üê ennreal.of_real_inv_of_pos zero_lt_two, ennreal.of_real_bit0 zero_le_one,
        ennreal.of_real_one] },
      { exact ennreal.of_real_zero.symm } },
    { intro n,
      split_ifs,
      { refine pow_nonneg _ _, norm_num },
      { exact le_rfl } },
    { refine summable.summable_of_eq_zero_or_self summable_geometric_two (Œª i, _),
      simp only [one_div, inv_eq_zero, not_le, inv_pow‚ÇÄ, zero_eq_inv],
      exact (ite_eq_or_eq _ _ _).symm, }, },
  have h_tendsto : ‚àÄ x ‚àà s·∂ú, tendsto (Œª i, f (ns i) x) at_top (ùìù (g x)),
  { refine Œª x hx, metric.tendsto_at_top.mpr (Œª Œµ hŒµ, _),
    simp_rw [s, set.compl_Inter, set.compl_Union, set.mem_Union, set.mem_Inter] at hx,
    obtain ‚ü®N, hNx‚ü© := hx,
    obtain ‚ü®k, hk_lt_Œµ‚ü© := h_lt_Œµ_real Œµ hŒµ,
    refine ‚ü®max N (k - 1), Œª n hn_ge, lt_of_le_of_lt _ hk_lt_Œµ‚ü©,
    specialize hNx n ((le_max_left _ _).trans hn_ge),
    have h_inv_n_le_k : (2 : ‚Ñù)‚Åª¬π ^ n ‚â§ 2‚Åª¬π ^ ((k : ‚Ñù) - 1),
    { rw [‚Üê real.rpow_nat_cast],
      refine real.rpow_le_rpow_of_exponent_ge ((one_div (2 : ‚Ñù)) ‚ñ∏ one_half_pos)
        (inv_le_one one_le_two) _,
      rw [sub_le_iff_le_add, ‚Üê nat.cast_add_one, nat.cast_le],
      exact (le_tsub_add.trans (add_le_add_right (le_max_right _ _) 1)).trans
        (add_le_add_right hn_ge 1) },
    refine le_trans _ h_inv_n_le_k,
    rw [set.mem_compl_iff, set.nmem_set_of_eq, not_le] at hNx,
    exact hNx.le },
  rw ae_iff,
  refine ‚ü®exists_seq_tendsto_ae.seq_tendsto_ae_seq_strict_mono hfg,
    measure_mono_null (Œª x, _) hŒºs‚ü©,
  rw [set.mem_set_of_eq, ‚Üê @not_not (x ‚àà s), not_imp_not],
  exact h_tendsto x,
end

end exists_seq_tendsto_ae

section tendsto_in_measure_of

variables [measurable_space E] [normed_group E] [borel_space E] [has_measurable_sub‚ÇÇ E] {p : ‚Ñù‚â•0‚àû}
variables {f : ‚Ñï ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E}

/-- TODO: move this next to ennreal.tendsto_nhds -/
lemma ennreal.tendsto_nhds_zero {f : filter Œ±} {u : Œ± ‚Üí ‚Ñù‚â•0‚àû} :
  tendsto u f (ùìù 0) ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in f, u x ‚â§ Œµ :=
begin
  rw ennreal.tendsto_nhds ennreal.zero_ne_top,
  simp only [true_and, zero_tsub, zero_le, zero_add, set.mem_Icc],
end

/-- This lemma is superceded by `measure_theory.tendsto_in_measure_of_tendsto_snorm` where we
allow `p = ‚àû` and only require `ae_measurable`. -/
lemma tendsto_in_measure_of_tendsto_snorm_of_measurable
  (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû)
  (hf : ‚àÄ n, measurable (f n)) (hg : measurable g) {l : filter ‚Ñï}
  (hfg : tendsto (Œª n, snorm (f n - g) p Œº) l (ùìù 0)) :
  tendsto_in_measure Œº f l g :=
begin
  intros Œµ hŒµ,
  replace hfg := ennreal.tendsto.const_mul (tendsto.ennrpow_const p.to_real hfg)
    (or.inr $ @ennreal.of_real_ne_top (1 / Œµ ^ (p.to_real))),
  simp only [mul_zero, ennreal.zero_rpow_of_pos (ennreal.to_real_pos hp_ne_zero hp_ne_top)] at hfg,
  rw ennreal.tendsto_nhds_zero at hfg ‚ä¢,
  intros Œ¥ hŒ¥,
  refine (hfg Œ¥ hŒ¥).mono (Œª n hn, _),
  refine le_trans _ hn,
  rw [ennreal.of_real_div_of_pos (real.rpow_pos_of_pos hŒµ _), ennreal.of_real_one, mul_comm,
    mul_one_div, ennreal.le_div_iff_mul_le _ (or.inl (ennreal.of_real_ne_top)), mul_comm],
  { convert mul_meas_ge_le_pow_snorm' Œº hp_ne_zero hp_ne_top ((hf n).sub hg) (ennreal.of_real Œµ),
    { exact (ennreal.of_real_rpow_of_pos hŒµ).symm },
    { ext x,
      rw [dist_eq_norm, ‚Üê ennreal.of_real_le_of_real_iff (norm_nonneg _),
          of_real_norm_eq_coe_nnnorm] } },
  { rw [ne, ennreal.of_real_eq_zero, not_le],
    exact or.inl (real.rpow_pos_of_pos hŒµ _) },
end

/-- This lemma is superceded by `measure_theory.tendsto_in_measure_of_tendsto_snorm` where we
allow `p = ‚àû`. -/
lemma tendsto_in_measure_of_tendsto_snorm_of_ne_top
  (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû)
  (hf : ‚àÄ n, ae_measurable (f n) Œº) (hg : ae_measurable g Œº) {l : filter ‚Ñï}
  (hfg : tendsto (Œª n, snorm (f n - g) p Œº) l (ùìù 0)) :
  tendsto_in_measure Œº f l g :=
begin
  refine tendsto_in_measure.congr (Œª i, (hf i).ae_eq_mk.symm) hg.ae_eq_mk.symm _,
  refine tendsto_in_measure_of_tendsto_snorm_of_measurable hp_ne_zero hp_ne_top
    (Œª i, (hf i).measurable_mk) hg.measurable_mk _,
  have hf_eq_ae : ‚àÄ·µê x ‚àÇŒº, ‚àÄ n, (hf n).mk (f n) x = f n x,
    from ae_all_iff.mpr (Œª n, (hf n).ae_eq_mk.symm),
  have : (Œª n, snorm ((hf n).mk (f n) - ae_measurable.mk g hg) p Œº) = (Œª n, snorm (f n - g) p Œº),
  { ext1 n, refine snorm_congr_ae (eventually_eq.sub (hf n).ae_eq_mk.symm hg.ae_eq_mk.symm), },
  rw this,
  exact hfg,
end

/-- See also `measure_theory.tendsto_in_measure_of_tendsto_snorm` which work for general
Lp-convergence for all `p ‚â† 0`. -/
lemma tendsto_in_measure_of_tendsto_snorm_top {E} [normed_group E] {f : ‚Ñï ‚Üí Œ± ‚Üí E} {g : Œ± ‚Üí E}
  {l : filter ‚Ñï} (hfg : tendsto (Œª n, snorm (f n - g) ‚àû Œº) l (ùìù 0)) :
  tendsto_in_measure Œº f l g :=
begin
  intros Œ¥ hŒ¥,
  simp only [snorm_exponent_top, snorm_ess_sup] at hfg,
  rw ennreal.tendsto_nhds_zero at hfg ‚ä¢,
  intros Œµ hŒµ,
  specialize hfg ((ennreal.of_real Œ¥) / 2) (ennreal.div_pos_iff.2
    ‚ü®(ennreal.of_real_pos.2 hŒ¥).ne.symm, ennreal.two_ne_top‚ü©),
  refine hfg.mono (Œª n hn, _),
  simp only [true_and, gt_iff_lt, ge_iff_le, zero_tsub, zero_le, zero_add, set.mem_Icc,
    pi.sub_apply] at *,
  have : ess_sup (Œª (x : Œ±), (‚à•f n x - g x‚à•‚Çä : ‚Ñù‚â•0‚àû)) Œº < ennreal.of_real Œ¥ :=
    lt_of_le_of_lt hn (ennreal.half_lt_self (ennreal.of_real_pos.2 hŒ¥).ne.symm
      ennreal.of_real_lt_top.ne),
  refine ((le_of_eq _).trans (ae_lt_of_ess_sup_lt this).le).trans hŒµ.le,
  congr' with x,
  simp only [ennreal.of_real_le_iff_le_to_real ennreal.coe_lt_top.ne, ennreal.coe_to_real,
    not_lt, coe_nnnorm, set.mem_set_of_eq, set.mem_compl_eq],
  rw ‚Üê dist_eq_norm (f n x) (g x),
  refl
end

/-- Convergence in Lp implies convergence in measure. -/
lemma tendsto_in_measure_of_tendsto_snorm
  (hp_ne_zero : p ‚â† 0) (hf : ‚àÄ n, ae_measurable (f n) Œº) (hg : ae_measurable g Œº) {l : filter ‚Ñï}
  (hfg : tendsto (Œª n, snorm (f n - g) p Œº) l (ùìù 0)) :
  tendsto_in_measure Œº f l g :=
begin
  by_cases hp_ne_top : p = ‚àû,
  { subst hp_ne_top,
    exact tendsto_in_measure_of_tendsto_snorm_top hfg },
  { exact tendsto_in_measure_of_tendsto_snorm_of_ne_top hp_ne_zero hp_ne_top hf hg hfg }
end

/-- Convergence in Lp implies convergence in measure. -/
lemma tendsto_in_measure_of_tendsto_Lp [second_countable_topology E] [hp : fact (1 ‚â§ p)]
  {f : ‚Ñï ‚Üí Lp E p Œº} {g : Lp E p Œº} {l : filter ‚Ñï} (hfg : tendsto f l (ùìù g)) :
  tendsto_in_measure Œº (Œª n, f n) l g :=
tendsto_in_measure_of_tendsto_snorm (ennreal.zero_lt_one.trans_le hp.elim).ne.symm
  (Œª n, Lp.ae_measurable _) (Lp.ae_measurable _) ((Lp.tendsto_Lp_iff_tendsto_‚Ñíp' _ _).mp hfg)

end tendsto_in_measure_of

end measure_theory
