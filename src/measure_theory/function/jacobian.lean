/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import measure_theory.covering.besicovitch_vector_space
import measure_theory.measure.haar_lebesgue
import analysis.normed_space.pointwise
import measure_theory.covering.differentiation

/-!
# Change of variables in higher-dimensional integrals
-/

open measure_theory measure_theory.measure metric filter set finite_dimensional asymptotics
open_locale nnreal ennreal topological_space pointwise

variables {E : Type*} [normed_group E] [normed_space â„ E] [finite_dimensional â„ E]
  [measurable_space E] [borel_space E] (Î¼ : measure E) [is_add_haar_measure Î¼]

/-- Let `f` be a function which is sufficiently close (in the Lipschitz sense) to a given linear
map `A`. Then it expands the volume of any set by at most `m` for any `m > det A`. -/
lemma measure_image_le_mul_of_det_lt
  (A : E â†’L[â„] E) {m : â„â‰¥0} (hm : ennreal.of_real (abs (A : E â†’â‚—[â„] E).det) < m) :
  âˆ€á¶  Î´ in ğ“[>] (0 : â„â‰¥0), âˆ€ (s : set E) (f : E â†’ E) (hf : approximates_linear_on f A s Î´),
  Î¼ (f '' s) â‰¤ m * Î¼ s :=
begin
  apply nhds_within_le_nhds,
  let d := ennreal.of_real (abs (A : E â†’â‚—[â„] E).det),
  -- construct a small neighborhood of `A '' (closed_ball 0 1)` with measure comparable to
  -- the determinant of `A`.
  obtain âŸ¨Îµ, hÎµ, ÎµposâŸ© : âˆƒ (Îµ : â„),
    Î¼ (closed_ball 0 Îµ + A '' (closed_ball 0 1)) < m * Î¼ (closed_ball 0 1) âˆ§ 0 < Îµ,
  { have HC : is_compact (A '' closed_ball 0 1) :=
      (proper_space.is_compact_closed_ball _ _).image A.continuous,
    have L0 : tendsto (Î» Îµ, Î¼ (cthickening Îµ (A '' (closed_ball 0 1))))
      (ğ“[>] 0) (ğ“ (Î¼ (A '' (closed_ball 0 1)))),
    { apply tendsto.mono_left _ nhds_within_le_nhds,
      exact tendsto_measure_cthickening_of_is_compact HC },
    have L1 : tendsto (Î» Îµ, Î¼ (closed_ball 0 Îµ + A '' (closed_ball 0 1)))
      (ğ“[>] 0) (ğ“ (Î¼ (A '' (closed_ball 0 1)))),
    { apply L0.congr' _,
      filter_upwards [self_mem_nhds_within],
      assume r hr,
      rw [HC.cthickening_eq_add_closed_ball (le_of_lt hr), add_comm] },
    have L2 : tendsto (Î» Îµ, Î¼ (closed_ball 0 Îµ + A '' (closed_ball 0 1)))
      (ğ“[>] 0) (ğ“ (d * Î¼ (closed_ball 0 1))),
    { convert L1,
      exact (add_haar_image_continuous_linear_map _ _ _).symm },
    have I : d * Î¼ (closed_ball 0 1) < m * Î¼ (closed_ball 0 1) :=
      (ennreal.mul_lt_mul_right ((add_haar_closed_ball_pos Î¼ _ zero_lt_one).ne')
        measure_closed_ball_lt_top.ne).2 hm,
    have H : âˆ€á¶  (b : â„) in ğ“[>] 0,
      Î¼ (closed_ball 0 b + A '' closed_ball 0 1) < m * Î¼ (closed_ball 0 1) :=
        (tendsto_order.1 L2).2 _ I,
    exact (H.and self_mem_nhds_within).exists },
  have : Iio (âŸ¨Îµ, Îµpos.leâŸ© : â„â‰¥0) âˆˆ ğ“ (0 : â„â‰¥0), { apply Iio_mem_nhds, exact Îµpos },
  filter_upwards [this],
  -- fix a function `f` which is close enough to `A`.
  assume Î´ hÎ´ s f hf,
  -- This function expands the volume of any ball by at most `m`
  have I : âˆ€ x r, x âˆˆ s â†’ 0 â‰¤ r â†’ Î¼ (f '' (s âˆ© closed_ball x r)) â‰¤ m * Î¼ (closed_ball x r),
  { assume x r xs r0,
    have K : f '' (s âˆ© closed_ball x r) âŠ† A '' (closed_ball 0 r) + closed_ball (f x) (Îµ * r),
    { rintros y âŸ¨z, âŸ¨zs, zrâŸ©, rflâŸ©,
      apply set.mem_add.2 âŸ¨A (z - x), f z - f x - A (z - x) + f x, _, _, _âŸ©,
      { apply mem_image_of_mem,
        simpa only [dist_eq_norm, mem_closed_ball, mem_closed_ball_zero_iff] using zr },
      { rw [mem_closed_ball_iff_norm, add_sub_cancel],
        calc âˆ¥f z - f x - A (z - x)âˆ¥
            â‰¤ Î´ * âˆ¥z - xâˆ¥ : hf _ zs _ xs
        ... â‰¤ Îµ * r :
          mul_le_mul (le_of_lt hÎ´) (mem_closed_ball_iff_norm.1 zr) (norm_nonneg _) Îµpos.le },
      { simp only [map_sub, pi.sub_apply],
        abel } },
    have : A '' (closed_ball 0 r) + closed_ball (f x) (Îµ * r)
      = {f x} + r â€¢ (A '' (closed_ball 0 1) + closed_ball 0 Îµ),
      by rw [smul_add_set, â† add_assoc, add_comm ({f x}), add_assoc, smul_closed_ball _ _ Îµpos.le,
        smul_zero, singleton_add_closed_ball_zero, â† A.image_smul_set,
        smul_closed_ball _ _ zero_le_one, smul_zero, real.norm_eq_abs, abs_of_nonneg r0, mul_one,
        mul_comm],
    rw this at K,
    calc Î¼ (f '' (s âˆ© closed_ball x r))
        â‰¤ Î¼ ({f x} + r â€¢ (A '' (closed_ball 0 1) + closed_ball 0 Îµ)) : measure_mono K
    ... = ennreal.of_real (r ^ finrank â„ E) * Î¼ (A '' closed_ball 0 1 + closed_ball 0 Îµ) :
      by simp only [abs_of_nonneg r0, add_haar_smul, image_add_left, add_haar_preimage_add,
                    abs_pow, singleton_add]
    ... â‰¤ ennreal.of_real (r ^ finrank â„ E) * (m * Î¼ (closed_ball 0 1)) :
      by { rw add_comm, exact ennreal.mul_le_mul le_rfl hÎµ.le }
    ... = m * Î¼ (closed_ball x r) :
      by { simp only [add_haar_closed_ball' _ _ r0], ring } },
  -- covering `s` by closed balls with total measure very close to `Î¼ s`, one deduces that the
  -- measure of `f '' s` is at most `m * (Î¼ s + a)` for any positive `a`.
  have J : âˆ€á¶  a in ğ“[>] (0 : â„â‰¥0âˆ), Î¼ (f '' s) â‰¤ m * (Î¼ s + a),
  { filter_upwards [self_mem_nhds_within],
    assume a ha,
    change 0 < a at ha,
    obtain âŸ¨t, r, t_count, ts, rpos, st, Î¼tâŸ© : âˆƒ (t : set E) (r : E â†’ â„), t.countable âˆ§ t âŠ† s
      âˆ§ (âˆ€ (x : E), x âˆˆ t â†’ 0 < r x) âˆ§ (s âŠ† â‹ƒ (x âˆˆ t), closed_ball x (r x))
      âˆ§ âˆ‘' (x : â†¥t), Î¼ (closed_ball â†‘x (r â†‘x)) â‰¤ Î¼ s + a :=
        besicovitch.exists_closed_ball_covering_tsum_measure_le Î¼ ha.ne' (Î» x, Ioi 0) s
        (Î» x xs Î´ Î´pos, âŸ¨Î´/2, by simp [half_pos Î´pos, half_lt_self Î´pos]âŸ©),
    haveI : encodable t := t_count.to_encodable,
    calc Î¼ (f '' s)
        â‰¤ Î¼ (â‹ƒ (x : t), f '' (s âˆ© closed_ball x (r x))) :
      begin
        rw bUnion_eq_Union at st,
        apply measure_mono,
        rw [â† image_Union, â† inter_Union],
        exact image_subset _ (subset_inter (subset.refl _) st)
      end
    ... â‰¤ âˆ‘' (x : t), Î¼ (f '' (s âˆ© closed_ball x (r x))) : measure_Union_le _
    ... â‰¤ âˆ‘' (x : t), m * Î¼ (closed_ball x (r x)) :
      ennreal.tsum_le_tsum (Î» x, I x (r x) (ts x.2) (rpos x x.2).le)
    ... â‰¤ m * (Î¼ s + a) :
      by { rw ennreal.tsum_mul_left, exact ennreal.mul_le_mul le_rfl Î¼t } },
  -- taking the limit in `a`, one obtains the conclusion
  have L : tendsto (Î» a, (m : â„â‰¥0âˆ) * (Î¼ s + a)) (ğ“[>] 0) (ğ“ (m * (Î¼ s + 0))),
  { apply tendsto.mono_left _ nhds_within_le_nhds,
    apply ennreal.tendsto.const_mul (tendsto_const_nhds.add tendsto_id),
    simp only [ennreal.coe_ne_top, ne.def, or_true, not_false_iff] },
  rw add_zero at L,
  exact ge_of_tendsto L J,
end

/-- Let `f` be a function which is sufficiently close (in the Lipschitz sense) to a given linear
map `A`. Then it expands the volume of any set by at least `m` for any `m < det A`. -/
lemma mul_le_measure_image_of_lt_det
  (A : E â†’L[â„] E) {m : â„â‰¥0} (hm : (m : â„â‰¥0âˆ) < ennreal.of_real (abs (A : E â†’â‚—[â„] E).det)) :
  âˆ€á¶  Î´ in ğ“[>] (0 : â„â‰¥0), âˆ€ (s : set E) (f : E â†’ E) (hf : approximates_linear_on f A s Î´),
  (m : â„â‰¥0âˆ) * Î¼ s â‰¤ Î¼ (f '' s) :=
begin
  apply nhds_within_le_nhds,
  -- The assumption `hm` implies that `A` is invertible. If `f` is close enough to `A`, it is also
  -- invertible. One can then pass to the inverses, and deduce the estimate from
  -- `measure_image_le_mul_of_det_lt` applied to `fâ»Â¹` and `Aâ»Â¹`.
  -- exclude first the trivial case where `m = 0`.
  rcases eq_or_lt_of_le (zero_le m) with rfl|mpos,
  { apply eventually_of_forall,
    simp only [forall_const, zero_mul, implies_true_iff, zero_le, ennreal.coe_zero] },
  have hA : (A : E â†’â‚—[â„] E).det â‰  0,
  { assume h, simpa only [h, ennreal.not_lt_zero, ennreal.of_real_zero, abs_zero] using hm },
  -- let `B` be the continuous linear equiv version of `A`.
  let B := ((A : E â†’â‚—[â„] E).equiv_of_det_ne_zero hA).to_continuous_linear_equiv,
  have : (B : E â†’L[â„] E) = A,
  { ext x,
    simp only [linear_equiv.of_is_unit_det_apply, continuous_linear_equiv.coe_coe,
      continuous_linear_map.coe_coe, linear_equiv.coe_to_continuous_linear_equiv'] },
  -- the determinant of `B.symm` is bounded by `mâ»Â¹`
  have I : ennreal.of_real (abs (B.symm : E â†’â‚—[â„] E).det) < (mâ»Â¹ : â„â‰¥0),
  { simp only [linear_equiv.coe_to_continuous_linear_equiv_symm, linear_equiv.det_coe_symm, abs_inv,
               linear_equiv.coe_of_is_unit_det, ennreal.of_real, ennreal.coe_lt_coe,
               real.to_nnreal_inv] at âŠ¢ hm,
    exact nnreal.inv_lt_inv mpos.ne' hm },
  -- therefore, we may apply `measure_image_le_mul_of_det_lt` to `B.symm` and `mâ»Â¹`.
  obtain âŸ¨Î´â‚€, Î´â‚€pos, hÎ´â‚€âŸ© : âˆƒ (Î´ : â„â‰¥0), 0 < Î´ âˆ§ âˆ€ (t : set E) (g : E â†’ E),
    approximates_linear_on g (B.symm : E â†’L[â„] E) t Î´ â†’ Î¼ (g '' t) â‰¤ â†‘mâ»Â¹ * Î¼ t,
  { have : âˆ€á¶  (Î´ : â„â‰¥0) in ğ“[>] 0, âˆ€ (t : set E) (g : E â†’ E),
      approximates_linear_on g (B.symm : E â†’L[â„] E) t Î´ â†’ Î¼ (g '' t) â‰¤ â†‘mâ»Â¹ * Î¼ t :=
        measure_image_le_mul_of_det_lt Î¼ B.symm I,
    rcases (this.and self_mem_nhds_within).exists with âŸ¨Î´â‚€, h, h'âŸ©,
    exact âŸ¨Î´â‚€, h', hâŸ©, },
  -- record smallness conditions for `Î´` that will be needed to apply `hÎ´â‚€` below.
  have L1 : âˆ€á¶  Î´ in ğ“ (0 : â„â‰¥0), subsingleton E âˆ¨ Î´ < âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Šâ»Â¹,
  { by_cases (subsingleton E),
    { simp only [h, true_or, eventually_const] },
    simp only [h, false_or],
    apply Iio_mem_nhds,
    simpa only [h, false_or, nnreal.inv_pos] using B.subsingleton_or_nnnorm_symm_pos },
  have L2 : âˆ€á¶  Î´ in ğ“ (0 : â„â‰¥0),
    âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Š * (âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Šâ»Â¹ - Î´)â»Â¹ * Î´ < Î´â‚€,
  { have : tendsto (Î» Î´, âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Š * (âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Šâ»Â¹ - Î´)â»Â¹ * Î´)
      (ğ“ 0) (ğ“ (âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Š * (âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Šâ»Â¹ - 0)â»Â¹ * 0)),
    { rcases eq_or_ne (âˆ¥(B.symm : E â†’L[â„] E)âˆ¥â‚Š) 0 with H|H,
      { simpa only [H, zero_mul] using tendsto_const_nhds },
      refine tendsto.mul (tendsto_const_nhds.mul _) tendsto_id,
      refine (tendsto.sub tendsto_const_nhds tendsto_id).invâ‚€ _,
      simpa only [tsub_zero, inv_eq_zero, ne.def] using H },
    simp only [mul_zero] at this,
    exact (tendsto_order.1 this).2 Î´â‚€ Î´â‚€pos },
  -- let `Î´` be small enough, and `f` approximated by `B` up to `Î´`.
  filter_upwards [L1, L2],
  assume Î´ h1Î´ h2Î´ s f hf,
  have hf' : approximates_linear_on f (B : E â†’L[â„] E) s Î´, by convert hf,
  let F := hf'.to_local_equiv h1Î´,
  -- the condition to be checked can be reformulated in terms of the inverse maps
  suffices H : Î¼ ((F.symm) '' F.target) â‰¤ (mâ»Â¹ : â„â‰¥0) * Î¼ F.target,
  { change (m : â„â‰¥0âˆ) * Î¼ (F.source) â‰¤ Î¼ (F.target),
    rwa [â† F.symm_image_target_eq_source, mul_comm, â† ennreal.le_div_iff_mul_le, div_eq_mul_inv,
         mul_comm, â† ennreal.coe_inv (mpos.ne')],
    { apply or.inl,
      simpa only [ennreal.coe_eq_zero, ne.def] using mpos.ne'},
    { simp only [ennreal.coe_ne_top, true_or, ne.def, not_false_iff] } },
  -- as `fâ»Â¹` is well approximated by `Bâ»Â¹`, the conclusion follows from `hÎ´â‚€`
  -- and our choice of `Î´`.
  exact hÎ´â‚€ _ _ ((hf'.to_inv h1Î´).mono_num h2Î´.le),
end

/-- Assume that a function `f` has a derivative at every point of a set `s`. Then one may cover `s`
with countably many closed sets `t n` on which `f` is well approximated by linear maps `A n`. -/
lemma exists_closed_cover_approximates_linear_on_of_has_fderiv_within_at
  (f : E â†’ E) (s : set E) (f' : E â†’ E â†’L[â„] E)
  (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x)
  (r : (E â†’L[â„] E) â†’ â„â‰¥0) (rpos : âˆ€ A, r A â‰  0) :
  âˆƒ (t : â„• â†’ set E) (A : â„• â†’ (E â†’L[â„] E)), (âˆ€ n, is_closed (t n)) âˆ§ (s âŠ† â‹ƒ n, t n)
  âˆ§ (âˆ€ n, approximates_linear_on f (A n) (s âˆ© t n) (r (A n)))
  âˆ§ (s.nonempty â†’ âˆ€ n, âˆƒ y âˆˆ s, A n = f' y) :=
begin
  /- Choose countably many linear maps `f' z`. For every such map, if `f` has a derivative at `x`
  close enough to `f' z`, then `f y - f x` is well approximated by `f' z (y - x)` for `y` close
  enough to `x`, say on a ball of radius `r` (or even `u n` for some sequence `u` tending to `0`).
  Let `M n z` be the points where this happends. Then this set is relatively closed inside `s`,
  and moreover in every closed ball of radius `u n / 3` inside it the map is well approximated by
  `f' z`. Using countably many closed balls to split `M n z` into small diameter subsets `K n z p`,
  one obtains the desired sets `t q` after reindexing.
  -/
  -- exclude the trivial case where `s` is empty
  rcases eq_empty_or_nonempty s with rfl|hs,
  { refine âŸ¨Î» n, âˆ…, Î» n, 0, _, _, _, _âŸ©;
    simp },
  -- we will use countably many linear maps. Select these from all the derivatives since the
  -- space of linear maps is second-countable
  obtain âŸ¨T, T_count, hTâŸ© : âˆƒ T : set s, countable T âˆ§
    (â‹ƒ x âˆˆ T, ball (f' (x : E)) (r (f' x))) = â‹ƒ (x : s), ball (f' x) (r (f' x)) :=
    topological_space.is_open_Union_countable _ (Î» x, is_open_ball),
  -- fix a sequence `u` of positive reals tending to zero.
  obtain âŸ¨u, u_anti, u_pos, u_limâŸ© :
    âˆƒ (u : â„• â†’ â„), strict_anti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ tendsto u at_top (ğ“ 0) :=
      exists_seq_strict_anti_tendsto (0 : â„),
  -- `M n z` is the set of points `x` such that `f y - f x` is close to `f' z (y - x)` for `y`
  -- in the ball of radius `u n` around `x`.
  let M : â„• â†’ T â†’ set E := Î» n z, {x | x âˆˆ s âˆ§
    âˆ€ y âˆˆ s âˆ© ball x (u n), âˆ¥f y - f x - f' z (y - x)âˆ¥ â‰¤ r (f' z) * âˆ¥y - xâˆ¥},
  -- As `f` is differentiable everywhere on `s`, the sets `M n z` cover `s` by design.
  have s_subset : âˆ€ x âˆˆ s, âˆƒ (n : â„•) (z : T), x âˆˆ M n z,
  { assume x xs,
    obtain âŸ¨z, zT, hzâŸ© : âˆƒ z âˆˆ T, f' x âˆˆ ball (f' (z : E)) (r (f' z)),
    { have : f' x âˆˆ â‹ƒ (z âˆˆ T), ball (f' (z : E)) (r (f' z)),
      { rw hT,
        refine mem_Union.2 âŸ¨âŸ¨x, xsâŸ©, _âŸ©,
        simpa only [mem_ball, subtype.coe_mk, dist_self] using (rpos (f' x)).bot_lt },
      rwa mem_bUnion_iff at this },
    obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ (Îµ : â„), 0 < Îµ âˆ§ âˆ¥f' x - f' zâˆ¥ + Îµ â‰¤ r (f' z),
    { refine âŸ¨r (f' z) - âˆ¥f' x - f' zâˆ¥, _, le_of_eq (by abel)âŸ©,
      simpa only [sub_pos] using mem_ball_iff_norm.mp hz },
    obtain âŸ¨Î´, Î´pos, hÎ´âŸ© : âˆƒ (Î´ : â„) (H : 0 < Î´),
      ball x Î´ âˆ© s âŠ† {y | âˆ¥f y - f x - (f' x) (y - x)âˆ¥ â‰¤ Îµ * âˆ¥y - xâˆ¥} :=
        metric.mem_nhds_within_iff.1 (is_o.def (hf' x xs) Îµpos),
    obtain âŸ¨n, hnâŸ© : âˆƒ n, u n < Î´ := ((tendsto_order.1 u_lim).2 _ Î´pos).exists,
    refine âŸ¨n, âŸ¨z, zTâŸ©, âŸ¨xs, _âŸ©âŸ©,
    assume y hy,
    calc âˆ¥f y - f x - (f' z) (y - x)âˆ¥
        = âˆ¥(f y - f x - (f' x) (y - x)) + (f' x - f' z) (y - x)âˆ¥ :
      begin
        congr' 1,
        simp only [continuous_linear_map.coe_sub', map_sub, pi.sub_apply],
        abel,
      end
    ... â‰¤ âˆ¥f y - f x - (f' x) (y - x)âˆ¥ + âˆ¥(f' x - f' z) (y - x)âˆ¥ : norm_add_le _ _
    ... â‰¤ Îµ * âˆ¥y - xâˆ¥ + âˆ¥f' x - f' zâˆ¥ * âˆ¥y - xâˆ¥ :
      begin
        refine add_le_add (hÎ´ _) (continuous_linear_map.le_op_norm _ _),
        rw inter_comm,
        exact inter_subset_inter_right _ (ball_subset_ball hn.le) hy,
      end
    ... â‰¤ r (f' z) * âˆ¥y - xâˆ¥ :
      begin
        rw [â† add_mul, add_comm],
        exact mul_le_mul_of_nonneg_right hÎµ (norm_nonneg _),
      end },
  -- the sets `M n z` are relatively closed in `s`, as all the conditions defining it are clearly
  -- closed
  have closure_M_subset : âˆ€ n z, s âˆ© closure (M n z) âŠ† M n z,
  { rintros n z x âŸ¨xs, hxâŸ©,
    refine âŸ¨xs, Î» y hy, _âŸ©,
    obtain âŸ¨a, aM, a_limâŸ© : âˆƒ (a : â„• â†’ E), (âˆ€ k, a k âˆˆ M n z) âˆ§ tendsto a at_top (ğ“ x) :=
      mem_closure_iff_seq_limit.1 hx,
    have L1 : tendsto (Î» (k : â„•), âˆ¥f y - f (a k) - (f' z) (y - a k)âˆ¥) at_top
      (ğ“ âˆ¥f y - f x - (f' z) (y - x)âˆ¥),
    { apply tendsto.norm,
      have L : tendsto (Î» k, f (a k)) at_top (ğ“ (f x)),
      { apply (hf' x xs).continuous_within_at.tendsto.comp,
        apply tendsto_nhds_within_of_tendsto_nhds_of_eventually_within _ a_lim,
        exact eventually_of_forall (Î» k, (aM k).1) },
      apply tendsto.sub (tendsto_const_nhds.sub L),
      exact ((f' z).continuous.tendsto _).comp (tendsto_const_nhds.sub a_lim) },
    have L2 : tendsto (Î» (k : â„•), (r (f' z) : â„) * âˆ¥y - a kâˆ¥) at_top (ğ“ (r (f' z) * âˆ¥y - xâˆ¥)) :=
      (tendsto_const_nhds.sub a_lim).norm.const_mul _,
    have I : âˆ€á¶  k in at_top, âˆ¥f y - f (a k) - (f' z) (y - a k)âˆ¥ â‰¤ r (f' z) * âˆ¥y - a kâˆ¥,
    { have L : tendsto (Î» k, dist y (a k)) at_top (ğ“ (dist y x)) := tendsto_const_nhds.dist a_lim,
      filter_upwards [(tendsto_order.1 L).2 _ hy.2],
      assume k hk,
      exact (aM k).2 y âŸ¨hy.1, hkâŸ© },
    apply le_of_tendsto_of_tendsto L1 L2 I },
  -- choose a dense sequence `d p`
  rcases topological_space.exists_dense_seq E with âŸ¨d, hdâŸ©,
  -- split `M n z` into subsets `K n z p` of small diameters by intersecting with the ball
  -- `closed_ball (d p) (u n / 3)`.
  let K : â„• â†’ T â†’ â„• â†’ set E := Î» n z p, closure (M n z) âˆ© closed_ball (d p) (u n / 3),
  -- on the sets `K n z p`, the map `f` is well approximated by `f' z` by design.
  have K_approx : âˆ€ n (z : T) p, approximates_linear_on f (f' z) (s âˆ© K n z p) (r (f' z)),
  { assume n z p x hx y hy,
    have yM : y âˆˆ M n z := closure_M_subset _ _ âŸ¨hy.1, hy.2.1âŸ©,
    refine yM.2 _ âŸ¨hx.1, _âŸ©,
    calc dist x y â‰¤ dist x (d p) + dist y (d p) : dist_triangle_right _ _ _
    ... â‰¤ u n / 3 + u n / 3 : add_le_add hx.2.2 hy.2.2
    ... < u n : by linarith [u_pos n] },
  -- the sets `K n z p` are also closed, again by design.
  have K_closed : âˆ€ n (z : T) p, is_closed (K n z p) :=
    Î» n z p, is_closed_closure.inter is_closed_ball,
  -- reindex the sets `K n z p`, to let them only depend on an integer parameter `q`.
  obtain âŸ¨F, hFâŸ© : âˆƒ F : â„• â†’ â„• Ã— T Ã— â„•, function.surjective F,
  { haveI : encodable T := T_count.to_encodable,
    haveI : nonempty T,
    { unfreezingI { rcases eq_empty_or_nonempty T with rfl|hT },
      { rcases hs with âŸ¨x, xsâŸ©,
        rcases s_subset x xs with âŸ¨n, z, hnzâŸ©,
        exact false.elim z.2 },
      { exact (nonempty_coe_sort _).2 hT } },
    inhabit (â„• Ã— T Ã— â„•),
    exact âŸ¨_, encodable.surjective_decode_iget _âŸ© },
  -- these sets `t q = K n z p` will do
  refine âŸ¨Î» q, K (F q).1 (F q).2.1 (F q).2.2, Î» q, f' (F q).2.1, Î» n, K_closed _ _ _, Î» x xs, _,
    Î» q, K_approx _ _ _, Î» h's q, âŸ¨(F q).2.1, (F q).2.1.1.2, rflâŸ©âŸ©,
  -- the only fact that needs further checking is that they cover `s`.
  -- we already know that any point `x âˆˆ s` belongs to a set `M n z`.
  obtain âŸ¨n, z, hnzâŸ© : âˆƒ (n : â„•) (z : T), x âˆˆ M n z := s_subset x xs,
  -- by density, it also belongs to a ball `closed_ball (d p) (u n / 3)`.
  obtain âŸ¨p, hpâŸ© : âˆƒ (p : â„•), x âˆˆ closed_ball (d p) (u n / 3),
  { have : set.nonempty (ball x (u n / 3)),
    { simp only [nonempty_ball], linarith [u_pos n] },
    obtain âŸ¨p, hpâŸ© : âˆƒ (p : â„•), d p âˆˆ ball x (u n / 3) := hd.exists_mem_open is_open_ball this,
    exact âŸ¨p, (mem_ball'.1 hp).leâŸ© },
  -- choose `q` for which `t q = K n z p`.
  obtain âŸ¨q, hqâŸ© : âˆƒ q, F q = (n, z, p) := hF _,
  -- then `x` belongs to `t q`.
  apply mem_Union.2 âŸ¨q, _âŸ©,
  simp only [hq, subset_closure hnz, hp, mem_inter_eq, and_self],
end

/-- Assume that a function `f` has a derivative at every point of a set `s`. Then one may
partition `s` into countably many relatively measurable sets `t n` on which `f` is well
approximated by linear maps `A n`. -/
lemma exists_partition_approximates_linear_on_of_has_fderiv_within_at
  (f : E â†’ E) (s : set E) (f' : E â†’ E â†’L[â„] E)
  (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x)
  (r : (E â†’L[â„] E) â†’ â„â‰¥0) (rpos : âˆ€ A, r A â‰  0) :
  âˆƒ (t : â„• â†’ set E) (A : â„• â†’ (E â†’L[â„] E)), pairwise (disjoint on t)
  âˆ§ (âˆ€ n, measurable_set (t n)) âˆ§ (s âŠ† â‹ƒ n, t n)
  âˆ§ (âˆ€ n, approximates_linear_on f (A n) (s âˆ© t n) (r (A n)))
  âˆ§ (s.nonempty â†’ âˆ€ n, âˆƒ y âˆˆ s, A n = f' y) :=
begin
  rcases exists_closed_cover_approximates_linear_on_of_has_fderiv_within_at f s f' hf' r rpos
    with âŸ¨t, A, t_closed, st, t_approx, htâŸ©,
  refine âŸ¨disjointed t, A, disjoint_disjointed _,
          measurable_set.disjointed (Î» n, (t_closed n).measurable_set), _, _, htâŸ©,
  { rw Union_disjointed, exact st },
  { assume n, exact (t_approx n).mono_set (inter_subset_inter_right _ (disjointed_subset _ _)) },
end

/-- A differentiable function maps sets of measure zero to sets of measure zero. -/
lemma add_haar_image_zero_of_differentiable_on_of_add_haar_zero
  (f : E â†’ E) (s : set E) (hf : differentiable_on â„ f s) (hs : Î¼ s = 0) :
  Î¼ (f '' s) = 0 :=
begin
  refine le_antisymm _ (zero_le _),
  have : âˆ€ (A : E â†’L[â„] E), âˆƒ (Î´ : â„â‰¥0), 0 < Î´ âˆ§
    âˆ€ (t : set E) (g : E â†’ E) (hf : approximates_linear_on g A t Î´),
     Î¼ (g '' t) â‰¤ (real.to_nnreal ((abs (A : E â†’â‚—[â„] E).det)) + 1 : â„â‰¥0) * Î¼ t,
  { assume A,
    let m : â„â‰¥0 := real.to_nnreal ((abs (A : E â†’â‚—[â„] E).det)) + 1,
    have I : ennreal.of_real (abs (A : E â†’â‚—[â„] E).det) < m,
      by simp only [ennreal.of_real, m, lt_add_iff_pos_right, zero_lt_one, ennreal.coe_lt_coe],
    rcases ((measure_image_le_mul_of_det_lt Î¼ A I).and self_mem_nhds_within).exists with âŸ¨Î´, h, h'âŸ©,
    exact âŸ¨Î´, h', hâŸ© },
  choose Î´ hÎ´ using this,
  obtain âŸ¨t, A, t_disj, t_meas, t_cover, ht, -âŸ© : âˆƒ (t : â„• â†’ set E) (A : â„• â†’ (E â†’L[â„] E)),
    pairwise (disjoint on t) âˆ§ (âˆ€ (n : â„•), measurable_set (t n)) âˆ§ (s âŠ† â‹ƒ (n : â„•), t n)
    âˆ§ (âˆ€ (n : â„•), approximates_linear_on f (A n) (s âˆ© t n) (Î´ (A n)))
    âˆ§ (s.nonempty â†’ âˆ€ n, âˆƒ y âˆˆ s, A n = fderiv_within â„ f s y) :=
        exists_partition_approximates_linear_on_of_has_fderiv_within_at f s
        (fderiv_within â„ f s) (Î» x xs, (hf x xs).has_fderiv_within_at) Î´ (Î» A, (hÎ´ A).1.ne'),
  calc Î¼ (f '' s)
      â‰¤ Î¼ (â‹ƒ n, f '' (s âˆ© (t n))) :
    begin
      apply measure_mono,
      rw [â† image_Union, â† inter_Union],
      exact image_subset f (subset_inter subset.rfl t_cover)
    end
  ... â‰¤ âˆ‘' n, Î¼ (f '' (s âˆ© (t n))) : measure_Union_le _
  ... â‰¤ âˆ‘' n, (real.to_nnreal ((abs (A n : E â†’â‚—[â„] E).det)) + 1 : â„â‰¥0) * Î¼ (s âˆ© (t n)) :
    begin
      apply ennreal.tsum_le_tsum (Î» n, _),
      apply (hÎ´ (A n)).2,
      exact ht n,
    end
  ... â‰¤ âˆ‘' n, (real.to_nnreal ((abs (A n : E â†’â‚—[â„] E).det)) + 1 : â„â‰¥0) * 0 :
    begin
      refine ennreal.tsum_le_tsum (Î» n, ennreal.mul_le_mul le_rfl _),
      exact le_trans (measure_mono (inter_subset_left _ _)) (le_of_eq hs),
    end
  ... = 0 : by simp only [tsum_zero, mul_zero]
end

/-- A version of Sard lemma in fixed dimension: given a differentiable function from `E` to `E` and
a set where the differential is not invertible, then the image of this set has zero measure.
Here, we give an auxiliary statement towards this result. -/
lemma add_haar_image_zero_of_fderiv_not_onto_aux
  (f : E â†’ E) (s : set E) (f' : E â†’ (E â†’L[â„] E)) (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x)
  (R : â„) (hs : s âŠ† closed_ball 0 R) (Îµ : â„â‰¥0) (Îµpos : 0 < Îµ)
  (h'f' : âˆ€ x âˆˆ s, (f' x : E â†’â‚—[â„] E).det = 0) :
  Î¼ (f '' s) â‰¤ Îµ * Î¼ (closed_ball 0 R) :=
begin
  rcases eq_empty_or_nonempty s with rfl|h's, { simp only [measure_empty, zero_le, image_empty] },
  have : âˆ€ (A : E â†’L[â„] E), âˆƒ (Î´ : â„â‰¥0), 0 < Î´ âˆ§
    âˆ€ (t : set E) (g : E â†’ E) (hf : approximates_linear_on g A t Î´),
     Î¼ (g '' t) â‰¤ (real.to_nnreal ((abs (A : E â†’â‚—[â„] E).det)) + Îµ : â„â‰¥0) * Î¼ t,
  { assume A,
    let m : â„â‰¥0 := real.to_nnreal ((abs (A : E â†’â‚—[â„] E).det)) + Îµ,
    have I : ennreal.of_real (abs (A : E â†’â‚—[â„] E).det) < m,
      by simp only [ennreal.of_real, m, lt_add_iff_pos_right, Îµpos, ennreal.coe_lt_coe],
    rcases ((measure_image_le_mul_of_det_lt Î¼ A I).and self_mem_nhds_within).exists with âŸ¨Î´, h, h'âŸ©,
    exact âŸ¨Î´, h', hâŸ© },
  choose Î´ hÎ´ using this,
  obtain âŸ¨t, A, t_disj, t_meas, t_cover, ht, Af'âŸ© : âˆƒ (t : â„• â†’ set E) (A : â„• â†’ (E â†’L[â„] E)),
    pairwise (disjoint on t) âˆ§ (âˆ€ (n : â„•), measurable_set (t n)) âˆ§ (s âŠ† â‹ƒ (n : â„•), t n)
    âˆ§ (âˆ€ (n : â„•), approximates_linear_on f (A n) (s âˆ© t n) (Î´ (A n)))
    âˆ§  (s.nonempty â†’ âˆ€ n, âˆƒ y âˆˆ s, A n = f' y) :=
      exists_partition_approximates_linear_on_of_has_fderiv_within_at f s
      f' hf' Î´ (Î» A, (hÎ´ A).1.ne'),
  calc Î¼ (f '' s)
      â‰¤ Î¼ (â‹ƒ n, f '' (s âˆ© t n)) :
    begin
      apply measure_mono,
      rw [â† image_Union, â† inter_Union],
      exact image_subset f (subset_inter subset.rfl t_cover)
    end
  ... â‰¤ âˆ‘' n, Î¼ (f '' (s âˆ© t n)) : measure_Union_le _
  ... â‰¤ âˆ‘' n, (real.to_nnreal ((abs (A n : E â†’â‚—[â„] E).det)) + Îµ : â„â‰¥0) * Î¼ (s âˆ© t n) :
    begin
      apply ennreal.tsum_le_tsum (Î» n, _),
      apply (hÎ´ (A n)).2,
      exact ht n,
    end
  ... = âˆ‘' n, Îµ * Î¼ (s âˆ© t n) :
    begin
      congr,
      ext1 n,
      congr,
      rcases Af' h's n with âŸ¨y, ys, hyâŸ©,
      simp only [hy, h'f' y ys, real.to_nnreal_zero, abs_zero, zero_add]
    end
  ... â‰¤ Îµ * âˆ‘' n, Î¼ (closed_ball 0 R âˆ© t n) :
    begin
      rw ennreal.tsum_mul_left,
      refine ennreal.mul_le_mul le_rfl (ennreal.tsum_le_tsum (Î» n, measure_mono _)),
      exact inter_subset_inter_left _ hs,
    end
  ... = Îµ * Î¼ (â‹ƒ n, closed_ball 0 R âˆ© t n) :
    begin
      rw measure_Union,
      { exact pairwise_disjoint.mono t_disj (Î» n, inter_subset_right _ _) },
      { assume n,
        exact measurable_set_closed_ball.inter (t_meas n) }
    end
  ... â‰¤ Îµ * Î¼ (closed_ball 0 R) :
    begin
      rw â† inter_Union,
      exact ennreal.mul_le_mul le_rfl (measure_mono (inter_subset_left _ _)),
    end
end

/-- A version of Sard lemma in fixed dimension: given a differentiable function from `E` to `E` and
a set where the differential is not invertible, then the image of this set has zero measure. -/
lemma add_haar_image_zero_of_fderiv_not_onto
  (f : E â†’ E) (s : set E) (f' : E â†’ (E â†’L[â„] E)) (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x)
  (h'f' : âˆ€ x âˆˆ s, (f' x : E â†’â‚—[â„] E).det = 0) :
  Î¼ (f '' s) = 0 :=
begin
  suffices H : âˆ€ R, Î¼ (f '' (s âˆ© closed_ball 0 R)) = 0,
  { apply le_antisymm _ (zero_le _),
    rw â† Union_inter_closed_ball_nat s 0,
    calc Î¼ (f '' â‹ƒ (n : â„•), s âˆ© closed_ball 0 n) â‰¤ âˆ‘' (n : â„•), Î¼ (f '' (s âˆ© closed_ball 0 n)) :
      by { rw image_Union, exact measure_Union_le _ }
    ... â‰¤ 0 : by simp only [H, tsum_zero, nonpos_iff_eq_zero] },
  assume R,
  have A : âˆ€ (Îµ : â„â‰¥0) (Îµpos : 0 < Îµ), Î¼ (f '' (s âˆ© closed_ball 0 R)) â‰¤ Îµ * Î¼ (closed_ball 0 R) :=
    Î» Îµ Îµpos, add_haar_image_zero_of_fderiv_not_onto_aux Î¼ _ _ f'
      (Î» x hx, (hf' x hx.1).mono (inter_subset_left _ _)) R (inter_subset_right _ _) Îµ Îµpos
      (Î» x hx, h'f' x hx.1),
  have B : tendsto (Î» (Îµ : â„â‰¥0), (Îµ : â„â‰¥0âˆ) * Î¼ (closed_ball 0 R)) (ğ“[>] 0) (ğ“ 0),
  { have : tendsto (Î» (Îµ : â„â‰¥0), (Îµ : â„â‰¥0âˆ) * Î¼ (closed_ball 0 R))
      (ğ“ 0) (ğ“ (((0 : â„â‰¥0) : â„â‰¥0âˆ) * Î¼ (closed_ball 0 R))) :=
        ennreal.tendsto.mul_const (ennreal.tendsto_coe.2 tendsto_id)
          (or.inr ((measure_closed_ball_lt_top).ne)),
    simp only [zero_mul, ennreal.coe_zero] at this,
    exact tendsto.mono_left this nhds_within_le_nhds },
  apply le_antisymm _ (zero_le _),
  apply ge_of_tendsto B,
  filter_upwards [self_mem_nhds_within],
  exact A,
end

/-- If a differentiable function `f` is approximated by a linear map `A` on a set `s`, up to `Î´`,
then at almost every `x` in `s` one has `âˆ¥f' x - Aâˆ¥ â‰¤ Î´`. -/
lemma approximates_linear_on.norm_fderiv_sub_le {f : E â†’ E} {A : E â†’L[â„] E} {s : set E} {Î´ : â„â‰¥0}
  (hf : approximates_linear_on f A s Î´) (hs : measurable_set s)
  (f' : E â†’ E â†’L[â„] E) (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x) :
  âˆ€áµ x âˆ‚ (Î¼.restrict s), âˆ¥f' x - Aâˆ¥â‚Š â‰¤ Î´ :=
begin
  /- The conclusion will hold at the Lebesgue density points of `s` (which have full measure).
  at such a point `x`, for any `z` and any `Îµ > 0` one has for small `r`
  that `{x} + r â€¢ closed_ball z Îµ` intersects `s`. At a point `y` in the intersection,
  `f y - f x` is close both to `f' x (r z)` (by differentiability) and to `A (r z)`
  (by linear approximation), so these two quantities are close, i.e., `(f' x - A) z` is small. -/
  filter_upwards [besicovitch.ae_tendsto_measure_inter_div Î¼ s, ae_restrict_mem hs],
  -- start from a Lebesgue density point `x`, belonging to `s`.
  assume x hx xs,
  -- consider an arbitrary vector `z`.
  apply continuous_linear_map.op_norm_le_bound _ Î´.2 (Î» z, _),
  -- to show that `âˆ¥(f' x - A) zâˆ¥ â‰¤ Î´ âˆ¥zâˆ¥`, it suffices to do it up to some error that vanishes
  -- asymptotically in terms of `Îµ > 0`.
  suffices H : âˆ€ Îµ, 0 < Îµ â†’ âˆ¥(f' x - A) zâˆ¥ â‰¤ (Î´ + Îµ) * (âˆ¥zâˆ¥ + Îµ) + âˆ¥(f' x - A)âˆ¥ * Îµ,
  { have : tendsto (Î» (Îµ : â„), ((Î´ : â„) + Îµ) * (âˆ¥zâˆ¥ + Îµ) + âˆ¥(f' x - A)âˆ¥ * Îµ) (ğ“[>] 0)
      (ğ“ ((Î´ + 0) * (âˆ¥zâˆ¥ + 0) + âˆ¥(f' x - A)âˆ¥ * 0)) :=
        tendsto.mono_left (continuous.tendsto (by continuity) 0) nhds_within_le_nhds,
    simp only [add_zero, mul_zero] at this,
    apply le_of_tendsto_of_tendsto tendsto_const_nhds this,
    filter_upwards [self_mem_nhds_within],
    exact H },
  -- fix a positive `Îµ`.
  assume Îµ Îµpos,
  -- for small enough `r`, the rescaled ball `r â€¢ closed_ball z Îµ` intersects `s`, as `x` is a
  -- density point
  have Bâ‚ : âˆ€á¶  r in ğ“[>] (0 : â„), (s âˆ© ({x} + r â€¢ closed_ball z Îµ)).nonempty :=
    eventually_nonempty_inter_smul_of_density_one Î¼ s x hx
      _ measurable_set_closed_ball (add_haar_closed_ball_pos Î¼ z Îµpos).ne',
  obtain âŸ¨Ï, Ïpos, hÏâŸ© :
    âˆƒ Ï > 0, ball x Ï âˆ© s âŠ† {y : E | âˆ¥f y - f x - (f' x) (y - x)âˆ¥ â‰¤ Îµ * âˆ¥y - xâˆ¥} :=
      mem_nhds_within_iff.1 (is_o.def (hf' x xs) Îµpos),
  -- for small enough `r`, the rescaled ball `r â€¢ closed_ball z Îµ` is included in the set where
  -- `f y - f x` is well approximated by `f' x (y - x)`.
  have Bâ‚‚ : âˆ€á¶  r in ğ“[>] (0 : â„), {x} + r â€¢ closed_ball z Îµ âŠ† ball x Ï := nhds_within_le_nhds
    (eventually_singleton_add_smul_subset bounded_closed_ball (ball_mem_nhds x Ïpos)),
  -- fix a small positive `r` satisfying the above properties, as well as a corresponding `y`.
  obtain âŸ¨r, âŸ¨y, âŸ¨ys, hyâŸ©âŸ©, rÏ, rposâŸ© : âˆƒ (r : â„), (s âˆ© ({x} + r â€¢ closed_ball z Îµ)).nonempty âˆ§
    {x} + r â€¢ closed_ball z Îµ âŠ† ball x Ï âˆ§ 0 < r := (Bâ‚.and (Bâ‚‚.and self_mem_nhds_within)).exists,
  -- write `y = x + r a` with `a âˆˆ closed_ball z Îµ`.
  obtain âŸ¨a, az, yaâŸ© : âˆƒ a, a âˆˆ closed_ball z Îµ âˆ§ y = x + r â€¢ a,
  { simp only [mem_smul_set, image_add_left, mem_preimage, singleton_add] at hy,
    rcases hy with âŸ¨a, az, haâŸ©,
    exact âŸ¨a, az, by simp only [ha, add_neg_cancel_left]âŸ© },
  have norm_a : âˆ¥aâˆ¥ â‰¤ âˆ¥zâˆ¥ + Îµ := calc
    âˆ¥aâˆ¥ = âˆ¥z + (a - z)âˆ¥ : by simp only [add_sub_cancel'_right]
    ... â‰¤ âˆ¥zâˆ¥ + âˆ¥a - zâˆ¥ : norm_add_le _ _
    ... â‰¤ âˆ¥zâˆ¥ + Îµ : add_le_add_left (mem_closed_ball_iff_norm.1 az) _,
  -- use the approximation properties to control `(f' x - A) a`, and then `(f' x - A) z` as `z` is
  -- close to `a`.
  have I : r * âˆ¥(f' x - A) aâˆ¥ â‰¤ r * (Î´ + Îµ) * (âˆ¥zâˆ¥ + Îµ) := calc
    r * âˆ¥(f' x - A) aâˆ¥ = âˆ¥(f' x - A) (r â€¢ a)âˆ¥ :
      by simp only [continuous_linear_map.map_smul, norm_smul, real.norm_eq_abs,
                    abs_of_nonneg rpos.le]
    ... = âˆ¥(f y - f x - A (y - x)) -
            (f y - f x - (f' x) (y - x))âˆ¥ :
      begin
        congr' 1,
        simp only [ya, add_sub_cancel', sub_sub_sub_cancel_left, continuous_linear_map.coe_sub',
          eq_self_iff_true, sub_left_inj, pi.sub_apply, continuous_linear_map.map_smul, smul_sub],
      end
    ... â‰¤ âˆ¥f y - f x - A (y - x)âˆ¥ +
             âˆ¥f y - f x - (f' x) (y - x)âˆ¥ : norm_sub_le _ _
    ... â‰¤ Î´ * âˆ¥y - xâˆ¥ + Îµ * âˆ¥y - xâˆ¥ :
      add_le_add (hf _ ys _ xs) (hÏ âŸ¨rÏ hy, ysâŸ©)
    ... = r * (Î´ + Îµ) * âˆ¥aâˆ¥ :
      by { simp only [ya, add_sub_cancel', norm_smul, real.norm_eq_abs, abs_of_nonneg rpos.le],
           ring }
    ... â‰¤ r * (Î´ + Îµ) * (âˆ¥zâˆ¥ + Îµ) :
      mul_le_mul_of_nonneg_left norm_a (mul_nonneg rpos.le (add_nonneg Î´.2 Îµpos.le)),
  show âˆ¥(f' x - A) zâˆ¥ â‰¤ (Î´ + Îµ) * (âˆ¥zâˆ¥ + Îµ) + âˆ¥(f' x - A)âˆ¥ * Îµ, from calc
    âˆ¥(f' x - A) zâˆ¥ = âˆ¥(f' x - A) a + (f' x - A) (z - a)âˆ¥ :
      begin
        congr' 1,
        simp only [continuous_linear_map.coe_sub', map_sub, pi.sub_apply],
        abel
      end
    ... â‰¤ âˆ¥(f' x - A) aâˆ¥ + âˆ¥(f' x - A) (z - a)âˆ¥ : norm_add_le _ _
    ... â‰¤ (Î´ + Îµ) * (âˆ¥zâˆ¥ + Îµ) + âˆ¥f' x - Aâˆ¥ * âˆ¥z - aâˆ¥ :
      begin
        apply add_le_add,
        { rw mul_assoc at I, exact (mul_le_mul_left rpos).1 I },
        { apply continuous_linear_map.le_op_norm }
      end
    ... â‰¤ (Î´ + Îµ) * (âˆ¥zâˆ¥ + Îµ) + âˆ¥f' x - Aâˆ¥ * Îµ : add_le_add le_rfl
      (mul_le_mul_of_nonneg_left (mem_closed_ball_iff_norm'.1 az) (norm_nonneg _)),
end

/-- The derivative of a function on a measurable set is almost everywhere measurable on this set
with respect to Lebesgue measure. Note that, in general, it is not genuinely measurable there,
as `f'` is not unique (but only on a set of measure `0`, as the argument shows). -/
lemma ae_measurable_fderiv_within
  (f : E â†’ E) (s : set E) (hs : measurable_set s) (f' : E â†’ (E â†’L[â„] E))
  (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x) :
  ae_measurable f' (Î¼.restrict s) :=
begin
  /- It suffices to show that `f'` can be uniformly approximated by a measurable function.
  Fix `Îµ > 0`. Thanks to `exists_partition_approximates_linear_on_of_has_fderiv_within_at`, one
  can find a countable measurable partition of `s` into sets `s âˆ© t n` on which `f` is well
  approximated by linear maps `A n`. On almost all of `s âˆ© t n`, it follows from
  `approximates_linear_on.norm_fderiv_sub_le` that `f'` is uniformly approximated by `A n`, which
  gives the conclusion. -/
  -- fix a precision `Îµ`
  refine ae_measurable_of_unif_approx (Î» Îµ Îµpos, _),
  let Î´ : â„â‰¥0 := âŸ¨Îµ, le_of_lt ÎµposâŸ©,
  have Î´pos : 0 < Î´ := Îµpos,
  -- partition `s` into sets `s âˆ© t n` on which `f` is approximated by linear maps `A n`.
  obtain âŸ¨t, A, t_disj, t_meas, t_cover, ht, Af'âŸ© : âˆƒ (t : â„• â†’ set E) (A : â„• â†’ (E â†’L[â„] E)),
    pairwise (disjoint on t) âˆ§ (âˆ€ (n : â„•), measurable_set (t n)) âˆ§ (s âŠ† â‹ƒ (n : â„•), t n)
    âˆ§ (âˆ€ (n : â„•), approximates_linear_on f (A n) (s âˆ© t n) Î´)
    âˆ§ (s.nonempty â†’ âˆ€ n, âˆƒ y âˆˆ s, A n = f' y) :=
      exists_partition_approximates_linear_on_of_has_fderiv_within_at f s
      f' hf' (Î» A, Î´) (Î» A, Î´pos.ne'),
  -- define a measurable function `g` which coincides with `A n` on `t n`.
  obtain âŸ¨g, g_meas, hgâŸ© : âˆƒ g : E â†’ (E â†’L[â„] E), measurable g âˆ§
    âˆ€ (n : â„•) (x : E), x âˆˆ t n â†’ g x = A n :=
      exists_measurable_piecewise_nat t t_meas t_disj (Î» n x, A n) (Î» n, measurable_const),
  refine âŸ¨g, g_meas.ae_measurable, _âŸ©,
  -- reduce to checking that `f'` and `g` are close on almost all of `s âˆ© t n`, for all `n`.
  suffices H : âˆ€áµ (x : E) âˆ‚(sum (Î» n, Î¼.restrict (s âˆ© t n))), dist (g x) (f' x) â‰¤ Îµ,
  { have : Î¼.restrict s â‰¤ sum (Î» n, Î¼.restrict (s âˆ© t n)),
    { have : s = â‹ƒ n, s âˆ© t n,
      { rw â† inter_Union,
        exact subset.antisymm (subset_inter subset.rfl t_cover) (inter_subset_left _ _) },
      conv_lhs { rw this },
      exact restrict_Union_le },
    exact ae_mono this H },
  -- fix such an `n`.
  refine ae_sum_iff.2 (Î» n, _),
  -- on almost all `s âˆ© t n`, `f' x` is close to `A n` thanks to
  -- `approximates_linear_on.norm_fderiv_sub_le`.
  have Eâ‚ : âˆ€áµ (x : E) âˆ‚Î¼.restrict (s âˆ© t n), âˆ¥f' x - A nâˆ¥â‚Š â‰¤ Î´ :=
    (ht n).norm_fderiv_sub_le Î¼ (hs.inter (t_meas n)) f'
      (Î» x hx, (hf' x hx.1).mono (inter_subset_left _ _)),
  -- moreover, `g x` is equal to `A n` there.
  have Eâ‚‚ : âˆ€áµ (x : E) âˆ‚Î¼.restrict (s âˆ© t n), g x = A n,
  { suffices H : âˆ€áµ (x : E) âˆ‚Î¼.restrict (t n), g x = A n,
      from ae_mono (restrict_mono (inter_subset_right _ _) le_rfl) H,
    filter_upwards [ae_restrict_mem (t_meas n)],
    exact hg n },
  -- putting these two properties together gives the conclusion.
  filter_upwards [Eâ‚, Eâ‚‚],
  assume x hx1 hx2,
  rw â† nndist_eq_nnnorm at hx1,
  rw [hx2, dist_comm],
  exact hx1,
end

lemma add_haar_image_le_of_fderiv (f : E â†’ E) (s : set E) (f' : E â†’ (E â†’L[â„] E))
  (hf' : âˆ€ x âˆˆ s, has_fderiv_within_at f (f' x) s x)
  (R : â„) (hs : s âŠ† closed_ball 0 R) (Îµ : â„â‰¥0) (Îµpos : 0 < Îµ) :
  Î¼ (f '' s) â‰¤ âˆ«â» x in s, ennreal.of_real (abs (f' x : E â†’â‚—[â„] E).det) âˆ‚Î¼
                + Îµ * Î¼ (closed_ball 0 R) :=
begin
  rcases eq_empty_or_nonempty s with rfl|h's, { simp only [measure_empty, zero_le, image_empty] },
  have : âˆ€ (A : E â†’L[â„] E), âˆƒ (Î´ : â„â‰¥0), 0 < Î´ âˆ§
    âˆ€ (t : set E) (g : E â†’ E) (hf : approximates_linear_on g A t Î´),
     Î¼ (g '' t) â‰¤ (real.to_nnreal ((abs (A : E â†’â‚—[â„] E).det)) + Îµ : â„â‰¥0) * Î¼ t,
  { assume A,
    let m : â„â‰¥0 := real.to_nnreal ((abs (A : E â†’â‚—[â„] E).det)) + Îµ,
    have I : ennreal.of_real (abs (A : E â†’â‚—[â„] E).det) < m,
      by simp only [ennreal.of_real, m, lt_add_iff_pos_right, Îµpos, ennreal.coe_lt_coe],
    rcases ((measure_image_le_mul_of_det_lt Î¼ A I).and self_mem_nhds_within).exists with âŸ¨Î´, h, h'âŸ©,
    exact âŸ¨Î´, h', hâŸ© },
  choose Î´ hÎ´ using this,
  obtain âŸ¨t, A, t_disj, t_meas, t_cover, ht, Af'âŸ© : âˆƒ (t : â„• â†’ set E) (A : â„• â†’ (E â†’L[â„] E)),
    pairwise (disjoint on t) âˆ§ (âˆ€ (n : â„•), measurable_set (t n)) âˆ§ (s âŠ† â‹ƒ (n : â„•), t n)
    âˆ§ (âˆ€ (n : â„•), approximates_linear_on f (A n) (s âˆ© t n) (Î´ (A n)))
    âˆ§  (s.nonempty â†’ âˆ€ n, âˆƒ y âˆˆ s, A n = f' y) :=
      exists_partition_approximates_linear_on_of_has_fderiv_within_at f s
      f' hf' Î´ (Î» A, (hÎ´ A).1.ne'),
  calc Î¼ (f '' s)
      â‰¤ Î¼ (â‹ƒ n, f '' (s âˆ© t n)) :
    begin
      apply measure_mono,
      rw [â† image_Union, â† inter_Union],
      exact image_subset f (subset_inter subset.rfl t_cover)
    end
  ... â‰¤ âˆ‘' n, Î¼ (f '' (s âˆ© t n)) : measure_Union_le _
  ... â‰¤ âˆ‘' n, (real.to_nnreal ((abs (A n : E â†’â‚—[â„] E).det)) + Îµ : â„â‰¥0) * Î¼ (s âˆ© t n) :
    begin
      apply ennreal.tsum_le_tsum (Î» n, _),
      apply (hÎ´ (A n)).2,
      exact ht n,
    end
  ... = âˆ‘' n, âˆ«â» x in s âˆ© t n, â†‘(real.to_nnreal ((abs (A n : E â†’â‚—[â„] E).det)) + Îµ : â„â‰¥0) âˆ‚Î¼ :
    by simp only [lintegral_const, measurable_set.univ, measure.restrict_apply, univ_inter]
  ... â‰¤ âˆ‘' n, âˆ«â» x in s âˆ© t n, (ennreal.of_real (abs (f' x : E â†’â‚—[â„] E).det) + 2 * Îµ) âˆ‚Î¼ :
    begin
      apply ennreal.tsum_le_tsum (Î» n, _),
      apply lintegral_mono_ae,
      have Z := (ht n).norm_fderiv_sub_le Î¼ f' (Î» x hx, (hf' x hx.1).mono (inter_subset_left _ _)),
      have : âˆ€áµ (x : E) âˆ‚Î¼.restrict (s âˆ© t n),
        abs ((f' x : E â†’â‚—[â„] E).det - (A n : E â†’â‚—[â„] E).det) â‰¤ Îµ, sorry,
      filter_upwards [this],
      assume x hx,
      rw [ennreal.of_real],
      sorry,


    end
/-  ... â‰¤ Îµ * âˆ‘' n, Î¼ (closed_ball 0 R âˆ© t n) :
    begin
      rw ennreal.tsum_mul_left,
      refine ennreal.mul_le_mul le_rfl (ennreal.tsum_le_tsum (Î» n, measure_mono _)),
      exact inter_subset_inter_left _ hs,
    end
  ... = Îµ * Î¼ (â‹ƒ n, closed_ball 0 R âˆ© t n) :
    begin
      rw measure_Union,
      { rw â† pairwise_univ at âŠ¢ t_disj,
        refine pairwise_disjoint.mono t_disj (Î» n, inter_subset_right _ _) },
      { assume n,
        exact measurable_set_closed_ball.inter (t_meas n) }
    end -/
  ... â‰¤ âˆ«â» x in s, ennreal.of_real (abs (f' x : E â†’â‚—[â„] E).det) âˆ‚Î¼
                + Îµ * Î¼ (closed_ball 0 R) : sorry
end
