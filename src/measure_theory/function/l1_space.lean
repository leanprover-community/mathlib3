/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou
-/
import measure_theory.function.lp_space


/-!
# Integrable functions and `LÂ¹` space

In the first part of this file, the predicate `integrable` is defined and basic properties of
integrable functions are proved.

Such a predicate is already available under the name `mem_â„’p 1`. We give a direct definition which
is easier to use, and show that it is equivalent to `mem_â„’p 1`

In the second part, we establish an API between `integrable` and the space `LÂ¹` of equivalence
classes of integrable functions, already defined as a special case of `L^p` spaces for `p = 1`.

## Notation

* `Î± â†’â‚[Î¼] Î²` is the type of `LÂ¹` space, where `Î±` is a `measure_space` and `Î²` is a `normed_group`
  with a `second_countable_topology`. `f : Î± â†’â‚˜ Î²` is a "function" in `LÂ¹`. In comments, `[f]` is
  also used to denote an `LÂ¹` function.

  `â‚` can be typed as `\1`.

## Main definitions

* Let `f : Î± â†’ Î²` be a function, where `Î±` is a `measure_space` and `Î²` a `normed_group`.
  Then `has_finite_integral f` means `(âˆ«â» a, nnnorm (f a)) < âˆ`.

* If `Î²` is moreover a `measurable_space` then `f` is called `integrable` if
  `f` is `measurable` and `has_finite_integral f` holds.

## Implementation notes

To prove something for an arbitrary integrable function, a useful theorem is
`integrable.induction` in the file `set_integral`.

## Tags

integrable, function space, l1

-/

noncomputable theory
open_locale classical topological_space big_operators ennreal measure_theory nnreal

open set filter topological_space ennreal emetric measure_theory

variables {Î± Î² Î³ Î´ : Type*} {m : measurable_space Î±} {Î¼ Î½ : measure Î±}
variables [normed_group Î²]
variables [normed_group Î³]

namespace measure_theory

/-! ### Some results about the Lebesgue integral involving a normed group -/

lemma lintegral_nnnorm_eq_lintegral_edist (f : Î± â†’ Î²) :
  âˆ«â» a, nnnorm (f a) âˆ‚Î¼ = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ :=
by simp only [edist_eq_coe_nnnorm]

lemma lintegral_norm_eq_lintegral_edist (f : Î± â†’ Î²) :
  âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ :=
by simp only [of_real_norm_eq_coe_nnnorm, edist_eq_coe_nnnorm]

lemma lintegral_edist_triangle [second_countable_topology Î²] [measurable_space Î²]
  [opens_measurable_space Î²] {f g h : Î± â†’ Î²}
  (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) (hh : ae_measurable h Î¼) :
  âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ â‰¤ âˆ«â» a, edist (f a) (h a) âˆ‚Î¼ + âˆ«â» a, edist (g a) (h a) âˆ‚Î¼ :=
begin
  rw â† lintegral_add' (hf.edist hh) (hg.edist hh),
  refine lintegral_mono (Î» a, _),
  apply edist_triangle_right
end

lemma lintegral_nnnorm_zero : âˆ«â» a : Î±, nnnorm (0 : Î²) âˆ‚Î¼ = 0 := by simp

lemma lintegral_nnnorm_add [measurable_space Î²] [opens_measurable_space Î²]
  [measurable_space Î³] [opens_measurable_space Î³]
  {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼) :
  âˆ«â» a, nnnorm (f a) + nnnorm (g a) âˆ‚Î¼ = âˆ«â» a, nnnorm (f a) âˆ‚Î¼ + âˆ«â» a, nnnorm (g a) âˆ‚Î¼ :=
lintegral_add' hf.ennnorm hg.ennnorm

lemma lintegral_nnnorm_neg {f : Î± â†’ Î²} :
  âˆ«â» a, nnnorm ((-f) a) âˆ‚Î¼ = âˆ«â» a, nnnorm (f a) âˆ‚Î¼ :=
by simp only [pi.neg_apply, nnnorm_neg]

/-! ### The predicate `has_finite_integral` -/

/-- `has_finite_integral f Î¼` means that the integral `âˆ«â» a, âˆ¥f aâˆ¥ âˆ‚Î¼` is finite.
  `has_finite_integral f` means `has_finite_integral f volume`. -/
def has_finite_integral {m : measurable_space Î±} (f : Î± â†’ Î²) (Î¼ : measure Î± . volume_tac) : Prop :=
âˆ«â» a, nnnorm (f a) âˆ‚Î¼ < âˆ

lemma has_finite_integral_iff_norm (f : Î± â†’ Î²) :
  has_finite_integral f Î¼ â†” âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ < âˆ :=
by simp only [has_finite_integral, of_real_norm_eq_coe_nnnorm]

lemma has_finite_integral_iff_edist (f : Î± â†’ Î²) :
  has_finite_integral f Î¼ â†” âˆ«â» a, edist (f a) 0 âˆ‚Î¼ < âˆ :=
by simp only [has_finite_integral_iff_norm, edist_dist, dist_zero_right]

lemma has_finite_integral_iff_of_real {f : Î± â†’ â„} (h : 0 â‰¤áµ[Î¼] f) :
  has_finite_integral f Î¼ â†” âˆ«â» a, ennreal.of_real (f a) âˆ‚Î¼ < âˆ :=
have lintegral_eq : âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ = âˆ«â» a, ennreal.of_real (f a) âˆ‚Î¼ :=
begin
  refine lintegral_congr_ae (h.mono $ Î» a h, _),
  rwa [real.norm_eq_abs, abs_of_nonneg]
end,
by rw [has_finite_integral_iff_norm, lintegral_eq]

lemma has_finite_integral_iff_of_nnreal {f : Î± â†’ â„â‰¥0} :
  has_finite_integral (Î» x, (f x : â„)) Î¼ â†” âˆ«â» a, f a âˆ‚Î¼ < âˆ :=
by simp [has_finite_integral_iff_norm]

lemma has_finite_integral.mono {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hg : has_finite_integral g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) : has_finite_integral f Î¼ :=
begin
  simp only [has_finite_integral_iff_norm] at *,
  calc âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ â‰¤ âˆ«â» (a : Î±), (ennreal.of_real âˆ¥g aâˆ¥) âˆ‚Î¼ :
    lintegral_mono_ae (h.mono $ assume a h, of_real_le_of_real h)
    ... < âˆ : hg
end

lemma has_finite_integral.mono' {f : Î± â†’ Î²} {g : Î± â†’ â„} (hg : has_finite_integral g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ g a) : has_finite_integral f Î¼ :=
hg.mono $ h.mono $ Î» x hx, le_trans hx (le_abs_self _)

lemma has_finite_integral.congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : has_finite_integral f Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) :
  has_finite_integral g Î¼ :=
hf.mono $ eventually_eq.le $ eventually_eq.symm h

lemma has_finite_integral_congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) :
  has_finite_integral f Î¼ â†” has_finite_integral g Î¼ :=
âŸ¨Î» hf, hf.congr' h, Î» hg, hg.congr' $ eventually_eq.symm hâŸ©

lemma has_finite_integral.congr {f g : Î± â†’ Î²} (hf : has_finite_integral f Î¼) (h : f =áµ[Î¼] g) :
  has_finite_integral g Î¼ :=
hf.congr' $ h.fun_comp norm

lemma has_finite_integral_congr {f g : Î± â†’ Î²} (h : f =áµ[Î¼] g) :
  has_finite_integral f Î¼ â†” has_finite_integral g Î¼ :=
has_finite_integral_congr' $ h.fun_comp norm

lemma has_finite_integral_const_iff {c : Î²} :
  has_finite_integral (Î» x : Î±, c) Î¼ â†” c = 0 âˆ¨ Î¼ univ < âˆ :=
begin
  simp only [has_finite_integral, lintegral_const],
  by_cases hc : c = 0,
  { simp [hc] },
  { simp only [hc, false_or],
    refine âŸ¨Î» h, _, Î» h, mul_lt_top coe_lt_top hâŸ©,
    replace h := mul_lt_top (@coe_lt_top $ (nnnorm c)â»Â¹) h,
    rwa [â† mul_assoc, â† coe_mul, _root_.inv_mul_cancel, coe_one, one_mul] at h,
    rwa [ne.def, nnnorm_eq_zero] }
end

lemma has_finite_integral_const [is_finite_measure Î¼] (c : Î²) :
  has_finite_integral (Î» x : Î±, c) Î¼ :=
has_finite_integral_const_iff.2 (or.inr $ measure_lt_top _ _)

lemma has_finite_integral_of_bounded [is_finite_measure Î¼] {f : Î± â†’ Î²} {C : â„}
  (hC : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ C) : has_finite_integral f Î¼ :=
(has_finite_integral_const C).mono' hC

lemma has_finite_integral.mono_measure {f : Î± â†’ Î²} (h : has_finite_integral f Î½) (hÎ¼ : Î¼ â‰¤ Î½) :
  has_finite_integral f Î¼ :=
lt_of_le_of_lt (lintegral_mono' hÎ¼ (le_refl _)) h

lemma has_finite_integral.add_measure {f : Î± â†’ Î²} (hÎ¼ : has_finite_integral f Î¼)
  (hÎ½ : has_finite_integral f Î½) : has_finite_integral f (Î¼ + Î½) :=
begin
  simp only [has_finite_integral, lintegral_add_measure] at *,
  exact add_lt_top.2 âŸ¨hÎ¼, hÎ½âŸ©
end

lemma has_finite_integral.left_of_add_measure {f : Î± â†’ Î²} (h : has_finite_integral f (Î¼ + Î½)) :
  has_finite_integral f Î¼ :=
h.mono_measure $ measure.le_add_right $ le_refl _

lemma has_finite_integral.right_of_add_measure {f : Î± â†’ Î²} (h : has_finite_integral f (Î¼ + Î½)) :
  has_finite_integral f Î½ :=
h.mono_measure $ measure.le_add_left $ le_refl _

@[simp] lemma has_finite_integral_add_measure {f : Î± â†’ Î²} :
  has_finite_integral f (Î¼ + Î½) â†” has_finite_integral f Î¼ âˆ§ has_finite_integral f Î½ :=
âŸ¨Î» h, âŸ¨h.left_of_add_measure, h.right_of_add_measureâŸ©, Î» h, h.1.add_measure h.2âŸ©

lemma has_finite_integral.smul_measure {f : Î± â†’ Î²} (h : has_finite_integral f Î¼) {c : â„â‰¥0âˆ}
  (hc : c < âˆ) : has_finite_integral f (c â€¢ Î¼) :=
begin
  simp only [has_finite_integral, lintegral_smul_measure] at *,
  exact mul_lt_top hc h
end

@[simp] lemma has_finite_integral_zero_measure {m : measurable_space Î±} (f : Î± â†’ Î²) :
  has_finite_integral f (0 : measure Î±) :=
by simp only [has_finite_integral, lintegral_zero_measure, with_top.zero_lt_top]

variables (Î± Î² Î¼)
@[simp] lemma has_finite_integral_zero : has_finite_integral (Î»a:Î±, (0:Î²)) Î¼ :=
by simp [has_finite_integral]
variables {Î± Î² Î¼}

lemma has_finite_integral.neg {f : Î± â†’ Î²} (hfi : has_finite_integral f Î¼) :
  has_finite_integral (-f) Î¼ :=
by simpa [has_finite_integral] using hfi

@[simp] lemma has_finite_integral_neg_iff {f : Î± â†’ Î²} :
  has_finite_integral (-f) Î¼ â†” has_finite_integral f Î¼ :=
âŸ¨Î» h, neg_neg f â–¸ h.neg, has_finite_integral.negâŸ©

lemma has_finite_integral.norm {f : Î± â†’ Î²} (hfi : has_finite_integral f Î¼) :
  has_finite_integral (Î»a, âˆ¥f aâˆ¥) Î¼ :=
have eq : (Î»a, (nnnorm âˆ¥f aâˆ¥ : â„â‰¥0âˆ)) = Î»a, (nnnorm (f a) : â„â‰¥0âˆ),
  by { funext, rw nnnorm_norm },
by { rwa [has_finite_integral, eq] }

lemma has_finite_integral_norm_iff (f : Î± â†’ Î²) :
  has_finite_integral (Î»a, âˆ¥f aâˆ¥) Î¼ â†” has_finite_integral f Î¼ :=
has_finite_integral_congr' $ eventually_of_forall $ Î» x, norm_norm (f x)

section dominated_convergence

variables {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}

lemma all_ae_of_real_F_le_bound (h : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a) :
  âˆ€ n, âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥F n aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
Î»n, (h n).mono $ Î» a h, ennreal.of_real_le_of_real h

lemma all_ae_tendsto_of_real_norm (h : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top $ ğ“ $ f a) :
  âˆ€áµ a âˆ‚Î¼, tendsto (Î»n, ennreal.of_real âˆ¥F n aâˆ¥) at_top $ ğ“ $ ennreal.of_real âˆ¥f aâˆ¥ :=
h.mono $
  Î» a h, tendsto_of_real $ tendsto.comp (continuous.tendsto continuous_norm _) h

lemma all_ae_of_real_f_le_bound (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥f aâˆ¥ â‰¤ ennreal.of_real (bound a) :=
begin
  have F_le_bound := all_ae_of_real_F_le_bound h_bound,
  rw â† ae_all_iff at F_le_bound,
  apply F_le_bound.mp ((all_ae_tendsto_of_real_norm h_lim).mono _),
  assume a tendsto_norm F_le_bound,
  exact le_of_tendsto' tendsto_norm (F_le_bound)
end

lemma has_finite_integral_of_dominated_convergence {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (bound_has_finite_integral : has_finite_integral bound Î¼)
  (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  has_finite_integral f Î¼ :=
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `âˆ¥F n aâˆ¥ --> âˆ¥f aâˆ¥` implies `âˆ¥f aâˆ¥ â‰¤ bound a`,
  and so `âˆ« âˆ¥fâˆ¥ â‰¤ âˆ« bound < âˆ` since `bound` is has_finite_integral -/
begin
  rw has_finite_integral_iff_norm,
  calc âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼ â‰¤ âˆ«â» a, ennreal.of_real (bound a) âˆ‚Î¼ :
    lintegral_mono_ae $ all_ae_of_real_f_le_bound h_bound h_lim
    ... < âˆ :
    begin
      rw â† has_finite_integral_iff_of_real,
      { exact bound_has_finite_integral },
      exact (h_bound 0).mono (Î» a h, le_trans (norm_nonneg _) h)
    end
end

lemma tendsto_lintegral_norm_of_dominated_convergence [measurable_space Î²]
  [borel_space Î²] [second_countable_topology Î²]
  {F : â„• â†’ Î± â†’ Î²} {f : Î± â†’ Î²} {bound : Î± â†’ â„}
  (F_measurable : âˆ€ n, ae_measurable (F n) Î¼)
  (f_measurable : ae_measurable f Î¼)
  (bound_has_finite_integral : has_finite_integral bound Î¼)
  (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  tendsto (Î»n, âˆ«â» a, (ennreal.of_real âˆ¥F n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ 0) :=
let b := Î»a, 2 * ennreal.of_real (bound a) in
/- `âˆ¥F n aâˆ¥ â‰¤ bound a` and `F n a --> f a` implies `âˆ¥f aâˆ¥ â‰¤ bound a`, and thus by the
  triangle inequality, have `âˆ¥F n a - f aâˆ¥ â‰¤ 2 * (bound a). -/
have hb : âˆ€ n, âˆ€áµ a âˆ‚Î¼, ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ b a,
begin
  assume n,
  filter_upwards [all_ae_of_real_F_le_bound h_bound n, all_ae_of_real_f_le_bound h_bound h_lim],
  assume a hâ‚ hâ‚‚,
  calc ennreal.of_real âˆ¥F n a - f aâˆ¥ â‰¤ (ennreal.of_real âˆ¥F n aâˆ¥) + (ennreal.of_real âˆ¥f aâˆ¥) :
  begin
    rw [â† ennreal.of_real_add],
    apply of_real_le_of_real,
    { apply norm_sub_le }, { exact norm_nonneg _ }, { exact norm_nonneg _ }
  end
    ... â‰¤ (ennreal.of_real (bound a)) + (ennreal.of_real (bound a)) : add_le_add hâ‚ hâ‚‚
    ... = b a : by rw â† two_mul
end,
/- On the other hand, `F n a --> f a` implies that `âˆ¥F n a - f aâˆ¥ --> 0`  -/
have h : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, ennreal.of_real âˆ¥F n a - f aâˆ¥) at_top (ğ“ 0),
begin
  rw â† ennreal.of_real_zero,
  refine h_lim.mono (Î» a h, (continuous_of_real.tendsto _).comp _),
  rwa â† tendsto_iff_norm_tendsto_zero
end,
/- Therefore, by the dominated convergence theorem for nonnegative integration, have
  ` âˆ« âˆ¥f a - F n aâˆ¥ --> 0 ` -/
begin
  suffices h : tendsto (Î»n, âˆ«â» a, (ennreal.of_real âˆ¥F n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ (âˆ«â» (a:Î±), 0 âˆ‚Î¼)),
  { rwa lintegral_zero at h },
  -- Using the dominated convergence theorem.
  refine tendsto_lintegral_of_dominated_convergence' _ _ hb _ _,
  -- Show `Î»a, âˆ¥f a - F n aâˆ¥` is almost everywhere measurable for all `n`
  { exact Î»n, measurable_of_real.comp_ae_measurable ((F_measurable n).sub f_measurable).norm },
  -- Show `2 * bound` is has_finite_integral
  { rw has_finite_integral_iff_of_real at bound_has_finite_integral,
    { calc âˆ«â» a, b a âˆ‚Î¼ = 2 * âˆ«â» a, ennreal.of_real (bound a) âˆ‚Î¼ :
        by { rw lintegral_const_mul', exact coe_ne_top }
        ... < âˆ : mul_lt_top (coe_lt_top) bound_has_finite_integral },
    filter_upwards [h_bound 0] Î» a h, le_trans (norm_nonneg _) h },
  -- Show `âˆ¥f a - F n aâˆ¥ --> 0`
  { exact h }
end

end dominated_convergence

section pos_part
/-! Lemmas used for defining the positive part of a `LÂ¹` function -/

lemma has_finite_integral.max_zero {f : Î± â†’ â„} (hf : has_finite_integral f Î¼) :
  has_finite_integral (Î»a, max (f a) 0) Î¼ :=
hf.mono $ eventually_of_forall $ Î» x, by simp [real.norm_eq_abs, abs_le, abs_nonneg, le_abs_self]

lemma has_finite_integral.min_zero {f : Î± â†’ â„} (hf : has_finite_integral f Î¼) :
  has_finite_integral (Î»a, min (f a) 0) Î¼ :=
hf.mono $ eventually_of_forall $ Î» x,
  by simp [real.norm_eq_abs, abs_le, abs_nonneg, neg_le, neg_le_abs_self]

end pos_part

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²]

lemma has_finite_integral.smul (c : ğ•œ) {f : Î± â†’ Î²} : has_finite_integral f Î¼ â†’
  has_finite_integral (c â€¢ f) Î¼ :=
begin
  simp only [has_finite_integral], assume hfi,
  calc
    âˆ«â» (a : Î±), nnnorm (c â€¢ f a) âˆ‚Î¼ = âˆ«â» (a : Î±), (nnnorm c) * nnnorm (f a) âˆ‚Î¼ :
      by simp only [nnnorm_smul, ennreal.coe_mul]
    ... < âˆ :
    begin
      rw lintegral_const_mul',
      exacts [mul_lt_top coe_lt_top hfi, coe_ne_top]
    end
end

lemma has_finite_integral_smul_iff {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) :
  has_finite_integral (c â€¢ f) Î¼ â†” has_finite_integral f Î¼ :=
begin
  split,
  { assume h,
    simpa only [smul_smul, inv_mul_cancel hc, one_smul] using h.smul câ»Â¹ },
  exact has_finite_integral.smul _
end

lemma has_finite_integral.const_mul {f : Î± â†’ â„} (h : has_finite_integral f Î¼) (c : â„) :
  has_finite_integral (Î» x, c * f x) Î¼ :=
(has_finite_integral.smul c h : _)

lemma has_finite_integral.mul_const {f : Î± â†’ â„} (h : has_finite_integral f Î¼) (c : â„) :
  has_finite_integral (Î» x, f x * c) Î¼ :=
by simp_rw [mul_comm, h.const_mul _]
end normed_space

/-! ### The predicate `integrable` -/

variables [measurable_space Î²] [measurable_space Î³] [measurable_space Î´]

/-- `integrable f Î¼` means that `f` is measurable and that the integral `âˆ«â» a, âˆ¥f aâˆ¥ âˆ‚Î¼` is finite.
  `integrable f` means `integrable f volume`. -/
def integrable {Î±} {m : measurable_space Î±} (f : Î± â†’ Î²) (Î¼ : measure Î± . volume_tac) : Prop :=
ae_measurable f Î¼ âˆ§ has_finite_integral f Î¼

lemma integrable.ae_measurable {f : Î± â†’ Î²} (hf : integrable f Î¼) : ae_measurable f Î¼ := hf.1
lemma integrable.has_finite_integral {f : Î± â†’ Î²} (hf : integrable f Î¼) : has_finite_integral f Î¼ :=
hf.2

lemma integrable.mono {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hg : integrable g Î¼) (hf : ae_measurable f Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ âˆ¥g aâˆ¥) : integrable f Î¼ :=
âŸ¨hf, hg.has_finite_integral.mono hâŸ©

lemma integrable.mono' {f : Î± â†’ Î²} {g : Î± â†’ â„} (hg : integrable g Î¼) (hf : ae_measurable f Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ â‰¤ g a) : integrable f Î¼ :=
âŸ¨hf, hg.has_finite_integral.mono' hâŸ©

lemma integrable.congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : integrable f Î¼) (hg : ae_measurable g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) : integrable g Î¼ :=
âŸ¨hg, hf.has_finite_integral.congr' hâŸ©

lemma integrable_congr' {f : Î± â†’ Î²} {g : Î± â†’ Î³} (hf : ae_measurable f Î¼) (hg : ae_measurable g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥f aâˆ¥ = âˆ¥g aâˆ¥) : integrable f Î¼ â†” integrable g Î¼ :=
âŸ¨Î» h2f, h2f.congr' hg h, Î» h2g, h2g.congr' hf $ eventually_eq.symm hâŸ©

lemma integrable.congr {f g : Î± â†’ Î²} (hf : integrable f Î¼) (h : f =áµ[Î¼] g) :
  integrable g Î¼ :=
âŸ¨hf.1.congr h, hf.2.congr hâŸ©

lemma integrable_congr {f g : Î± â†’ Î²} (h : f =áµ[Î¼] g) :
  integrable f Î¼ â†” integrable g Î¼ :=
âŸ¨Î» hf, hf.congr h, Î» hg, hg.congr h.symmâŸ©

lemma integrable_const_iff {c : Î²} : integrable (Î» x : Î±, c) Î¼ â†” c = 0 âˆ¨ Î¼ univ < âˆ :=
begin
  have : ae_measurable (Î» (x : Î±), c) Î¼ := measurable_const.ae_measurable,
  rw [integrable, and_iff_right this, has_finite_integral_const_iff]
end

lemma integrable_const [is_finite_measure Î¼] (c : Î²) : integrable (Î» x : Î±, c) Î¼ :=
integrable_const_iff.2 $ or.inr $ measure_lt_top _ _

lemma integrable.mono_measure {f : Î± â†’ Î²} (h : integrable f Î½) (hÎ¼ : Î¼ â‰¤ Î½) : integrable f Î¼ :=
âŸ¨h.ae_measurable.mono_measure hÎ¼, h.has_finite_integral.mono_measure hÎ¼âŸ©

lemma integrable.add_measure {f : Î± â†’ Î²} (hÎ¼ : integrable f Î¼) (hÎ½ : integrable f Î½) :
  integrable f (Î¼ + Î½) :=
âŸ¨hÎ¼.ae_measurable.add_measure hÎ½.ae_measurable,
  hÎ¼.has_finite_integral.add_measure hÎ½.has_finite_integralâŸ©

lemma integrable.left_of_add_measure {f : Î± â†’ Î²} (h : integrable f (Î¼ + Î½)) : integrable f Î¼ :=
h.mono_measure $ measure.le_add_right $ le_refl _

lemma integrable.right_of_add_measure {f : Î± â†’ Î²} (h : integrable f (Î¼ + Î½)) : integrable f Î½ :=
h.mono_measure $ measure.le_add_left $ le_refl _

@[simp] lemma integrable_add_measure {f : Î± â†’ Î²} :
  integrable f (Î¼ + Î½) â†” integrable f Î¼ âˆ§ integrable f Î½ :=
âŸ¨Î» h, âŸ¨h.left_of_add_measure, h.right_of_add_measureâŸ©, Î» h, h.1.add_measure h.2âŸ©

lemma integrable.smul_measure {f : Î± â†’ Î²} (h : integrable f Î¼) {c : â„â‰¥0âˆ} (hc : c < âˆ) :
  integrable f (c â€¢ Î¼) :=
âŸ¨h.ae_measurable.smul_measure c, h.has_finite_integral.smul_measure hcâŸ©

lemma integrable_map_measure [opens_measurable_space Î²] {f : Î± â†’ Î´} {g : Î´ â†’ Î²}
  (hg : ae_measurable g (measure.map f Î¼)) (hf : measurable f) :
  integrable g (measure.map f Î¼) â†” integrable (g âˆ˜ f) Î¼ :=
by simp [integrable, hg, hg.comp_measurable hf, has_finite_integral, lintegral_map' hg.ennnorm hf]

lemma lintegral_edist_lt_top [second_countable_topology Î²] [opens_measurable_space Î²] {f g : Î± â†’ Î²}
  (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ < âˆ :=
lt_of_le_of_lt
  (lintegral_edist_triangle hf.ae_measurable hg.ae_measurable
    (measurable_const.ae_measurable : ae_measurable (Î»a, (0 : Î²)) Î¼))
  (ennreal.add_lt_top.2 $ by { simp_rw â† has_finite_integral_iff_edist,
                               exact âŸ¨hf.has_finite_integral, hg.has_finite_integralâŸ© })

variables (Î± Î² Î¼)
@[simp] lemma integrable_zero : integrable (Î» _, (0 : Î²)) Î¼ :=
by simp [integrable, measurable_const.ae_measurable]
variables {Î± Î² Î¼}

lemma integrable.add' [opens_measurable_space Î²] {f g : Î± â†’ Î²} (hf : integrable f Î¼)
  (hg : integrable g Î¼) :
  has_finite_integral (f + g) Î¼ :=
calc âˆ«â» a, nnnorm (f a + g a) âˆ‚Î¼ â‰¤ âˆ«â» a, nnnorm (f a) + nnnorm (g a) âˆ‚Î¼ :
  lintegral_mono (Î» a, by exact_mod_cast nnnorm_add_le _ _)
... = _ : lintegral_nnnorm_add hf.ae_measurable hg.ae_measurable
... < âˆ : add_lt_top.2 âŸ¨hf.has_finite_integral, hg.has_finite_integralâŸ©

lemma integrable.add [borel_space Î²] [second_countable_topology Î²]
  {f g : Î± â†’ Î²} (hf : integrable f Î¼) (hg : integrable g Î¼) : integrable (f + g) Î¼ :=
âŸ¨hf.ae_measurable.add hg.ae_measurable, hf.add' hgâŸ©

lemma integrable_finset_sum {Î¹} [borel_space Î²] [second_countable_topology Î²] (s : finset Î¹)
  {f : Î¹ â†’ Î± â†’ Î²} (hf : âˆ€ i, integrable (f i) Î¼) : integrable (Î» a, âˆ‘ i in s, f i a) Î¼ :=
begin
  refine finset.induction_on s _ _,
  { simp only [finset.sum_empty, integrable_zero] },
  { assume i s his ih, simp only [his, finset.sum_insert, not_false_iff],
    exact (hf _).add ih }
end

lemma integrable.neg [borel_space Î²] {f : Î± â†’ Î²} (hf : integrable f Î¼) : integrable (-f) Î¼ :=
âŸ¨hf.ae_measurable.neg, hf.has_finite_integral.negâŸ©

@[simp] lemma integrable_neg_iff [borel_space Î²] {f : Î± â†’ Î²} :
  integrable (-f) Î¼ â†” integrable f Î¼ :=
âŸ¨Î» h, neg_neg f â–¸ h.neg, integrable.negâŸ©

lemma integrable.sub' [opens_measurable_space Î²] {f g : Î± â†’ Î²}
  (hf : integrable f Î¼) (hg : integrable g Î¼) : has_finite_integral (f - g) Î¼ :=
calc âˆ«â» a, nnnorm (f a - g a) âˆ‚Î¼ â‰¤ âˆ«â» a, nnnorm (f a) + nnnorm (-g a) âˆ‚Î¼ :
  lintegral_mono (assume a, by { simp only [sub_eq_add_neg], exact_mod_cast nnnorm_add_le _ _ } )
... = _ :
  by { simp only [nnnorm_neg], exact lintegral_nnnorm_add hf.ae_measurable hg.ae_measurable }
... < âˆ : add_lt_top.2 âŸ¨hf.has_finite_integral, hg.has_finite_integralâŸ©

lemma integrable.sub [borel_space Î²] [second_countable_topology Î²] {f g : Î± â†’ Î²}
  (hf : integrable f Î¼) (hg : integrable g Î¼) : integrable (f - g) Î¼ :=
by simpa only [sub_eq_add_neg] using hf.add hg.neg

lemma integrable.norm [opens_measurable_space Î²] {f : Î± â†’ Î²} (hf : integrable f Î¼) :
  integrable (Î»a, âˆ¥f aâˆ¥) Î¼ :=
âŸ¨hf.ae_measurable.norm, hf.has_finite_integral.normâŸ©

lemma integrable_norm_iff [opens_measurable_space Î²] {f : Î± â†’ Î²} (hf : ae_measurable f Î¼) :
  integrable (Î»a, âˆ¥f aâˆ¥) Î¼ â†” integrable f Î¼ :=
by simp_rw [integrable, and_iff_right hf, and_iff_right hf.norm, has_finite_integral_norm_iff]

lemma integrable_of_norm_sub_le [opens_measurable_space Î²] {fâ‚€ fâ‚ : Î± â†’ Î²} {g : Î± â†’ â„}
  (hfâ‚_m : ae_measurable fâ‚ Î¼)
  (hfâ‚€_i : integrable fâ‚€ Î¼)
  (hg_i : integrable g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥fâ‚€ a - fâ‚ aâˆ¥ â‰¤ g a) :
  integrable fâ‚ Î¼ :=
begin
  have : âˆ€áµ a âˆ‚Î¼, âˆ¥fâ‚ aâˆ¥ â‰¤ âˆ¥fâ‚€ aâˆ¥ + g a,
  { apply h.mono,
    intros a ha,
    calc âˆ¥fâ‚ aâˆ¥ â‰¤ âˆ¥fâ‚€ aâˆ¥ + âˆ¥fâ‚€ a - fâ‚ aâˆ¥ : norm_le_insert _ _
    ... â‰¤ âˆ¥fâ‚€ aâˆ¥ + g a : add_le_add_left ha _ },
  exact integrable.mono' (hfâ‚€_i.norm.add hg_i) hfâ‚_m this
end

lemma integrable.prod_mk [opens_measurable_space Î²] [opens_measurable_space Î³] {f : Î± â†’ Î²}
  {g : Î± â†’ Î³} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  integrable (Î» x, (f x, g x)) Î¼ :=
âŸ¨hf.ae_measurable.prod_mk hg.ae_measurable,
  (hf.norm.add' hg.norm).mono $ eventually_of_forall $ Î» x,
  calc max âˆ¥f xâˆ¥ âˆ¥g xâˆ¥ â‰¤ âˆ¥f xâˆ¥ + âˆ¥g xâˆ¥   : max_le_add_of_nonneg (norm_nonneg _) (norm_nonneg _)
                 ... â‰¤ âˆ¥(âˆ¥f xâˆ¥ + âˆ¥g xâˆ¥)âˆ¥ : le_abs_self _âŸ©

lemma mem_â„’p_one_iff_integrable {f : Î± â†’ Î²} : mem_â„’p f 1 Î¼ â†” integrable f Î¼ :=
by simp_rw [integrable, has_finite_integral, mem_â„’p, snorm_one_eq_lintegral_nnnorm]

lemma mem_â„’p.integrable [borel_space Î²] {q : â„â‰¥0âˆ} (hq1 : 1 â‰¤ q) {f : Î± â†’ Î²} [is_finite_measure Î¼]
  (hfq : mem_â„’p f q Î¼) : integrable f Î¼ :=
mem_â„’p_one_iff_integrable.mp (hfq.mem_â„’p_of_exponent_le hq1)

lemma lipschitz_with.integrable_comp_iff_of_antilipschitz [complete_space Î²] [borel_space Î²]
  [borel_space Î³] {K K'} {f : Î± â†’ Î²} {g : Î² â†’ Î³} (hg : lipschitz_with K g)
  (hg' : antilipschitz_with K' g) (g0 : g 0 = 0) :
  integrable (g âˆ˜ f) Î¼ â†” integrable f Î¼ :=
by simp [â† mem_â„’p_one_iff_integrable, hg.mem_â„’p_comp_iff_of_antilipschitz hg' g0]

lemma integrable.real_to_nnreal {f : Î± â†’ â„} (hf : integrable f Î¼) :
  integrable (Î» x, ((f x).to_nnreal : â„)) Î¼ :=
begin
  refine âŸ¨hf.ae_measurable.real_to_nnreal.coe_nnreal_real, _âŸ©,
  rw has_finite_integral_iff_norm,
  refine lt_of_le_of_lt _ ((has_finite_integral_iff_norm _).1 hf.has_finite_integral),
  apply lintegral_mono,
  assume x,
  simp [real.norm_eq_abs, ennreal.of_real_le_of_real, abs_le, abs_nonneg, le_abs_self],
end

section pos_part
/-! ### Lemmas used for defining the positive part of a `LÂ¹` function -/

lemma integrable.max_zero {f : Î± â†’ â„} (hf : integrable f Î¼) : integrable (Î»a, max (f a) 0) Î¼ :=
âŸ¨hf.ae_measurable.max measurable_const.ae_measurable, hf.has_finite_integral.max_zeroâŸ©

lemma integrable.min_zero {f : Î± â†’ â„} (hf : integrable f Î¼) : integrable (Î»a, min (f a) 0) Î¼ :=
âŸ¨hf.ae_measurable.min measurable_const.ae_measurable, hf.has_finite_integral.min_zeroâŸ©

end pos_part

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²] [measurable_space ğ•œ]
  [opens_measurable_space ğ•œ]

lemma integrable.smul [borel_space Î²] (c : ğ•œ) {f : Î± â†’ Î²}
  (hf : integrable f Î¼) : integrable (c â€¢ f) Î¼ :=
âŸ¨hf.ae_measurable.const_smul c, hf.has_finite_integral.smul câŸ©

lemma integrable_smul_iff [borel_space Î²] {c : ğ•œ} (hc : c â‰  0) (f : Î± â†’ Î²) :
  integrable (c â€¢ f) Î¼ â†” integrable f Î¼ :=
and_congr (ae_measurable_const_smul_iff' hc) (has_finite_integral_smul_iff hc f)

lemma integrable.const_mul {f : Î± â†’ â„} (h : integrable f Î¼) (c : â„) :
  integrable (Î» x, c * f x) Î¼ :=
integrable.smul c h

lemma integrable.mul_const {f : Î± â†’ â„} (h : integrable f Î¼) (c : â„) :
  integrable (Î» x, f x * c) Î¼ :=
by simp_rw [mul_comm, h.const_mul _]

end normed_space

section normed_space_over_complete_field
variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ] [complete_space ğ•œ] [measurable_space ğ•œ]
variables [borel_space ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E] [measurable_space E] [borel_space E]

lemma integrable_smul_const {f : Î± â†’ ğ•œ} {c : E} (hc : c â‰  0) :
  integrable (Î» x, f x â€¢ c) Î¼ â†” integrable f Î¼ :=
begin
  simp_rw [integrable, ae_measurable_smul_const hc, and.congr_right_iff, has_finite_integral,
    nnnorm_smul, ennreal.coe_mul],
  intro hf, rw [lintegral_mul_const' _ _ ennreal.coe_ne_top, ennreal.mul_lt_top_iff],
  have : âˆ€ x : â„â‰¥0âˆ, x = 0 â†’ x < âˆ := by simp,
  simp [hc, or_iff_left_of_imp (this _)]
end
end normed_space_over_complete_field

section is_R_or_C
variables {ğ•œ : Type*} [is_R_or_C ğ•œ] [measurable_space ğ•œ] [opens_measurable_space ğ•œ] {f : Î± â†’ ğ•œ}

lemma integrable.re (hf : integrable f Î¼) : integrable (Î» x, is_R_or_C.re (f x)) Î¼ :=
by { rw â† mem_â„’p_one_iff_integrable at hf âŠ¢, exact hf.re, }

lemma integrable.im (hf : integrable f Î¼) : integrable (Î» x, is_R_or_C.im (f x)) Î¼ :=
by { rw â† mem_â„’p_one_iff_integrable at hf âŠ¢, exact hf.im, }

end is_R_or_C

section inner_product
variables {ğ•œ E : Type*} [is_R_or_C ğ•œ] [measurable_space ğ•œ] [borel_space ğ•œ]
  [inner_product_space ğ•œ E]
  [measurable_space E] [opens_measurable_space E] [second_countable_topology E]
  {f : Î± â†’ E}

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y

lemma integrable.const_inner (c : E) (hf : integrable f Î¼) : integrable (Î» x, âŸªc, f xâŸ«) Î¼ :=
by { rw â† mem_â„’p_one_iff_integrable at hf âŠ¢, exact hf.const_inner c, }

lemma integrable.inner_const (hf : integrable f Î¼) (c : E) : integrable (Î» x, âŸªf x, câŸ«) Î¼ :=
by { rw â† mem_â„’p_one_iff_integrable at hf âŠ¢, exact hf.inner_const c, }

end inner_product

section trim

variables {H : Type*} [normed_group H] [measurable_space H] [opens_measurable_space H]
  {m0 : measurable_space Î±} {Î¼' : measure Î±} {f : Î± â†’ H}

lemma integrable.trim (hm : m â‰¤ m0) (hf_int : integrable f Î¼') (hf : @measurable _ _ m _ f) :
  integrable f (Î¼'.trim hm) :=
begin
  refine âŸ¨measurable.ae_measurable hf, _âŸ©,
  rw [has_finite_integral, lintegral_trim hm _],
  { exact hf_int.2, },
  { exact @measurable.coe_nnreal_ennreal Î± m _ (@measurable.nnnorm _ Î± _ _ _ m _ hf), },
end

lemma integrable_of_integrable_trim (hm : m â‰¤ m0) (hf_int : integrable f (Î¼'.trim hm)) :
  integrable f Î¼' :=
begin
  obtain âŸ¨hf_meas_ae, hfâŸ© := hf_int,
  refine âŸ¨ae_measurable_of_ae_measurable_trim hm hf_meas_ae, _âŸ©,
  rw has_finite_integral at hf âŠ¢,
  rwa lintegral_trim_ae hm _ at hf,
  exact @ae_measurable.coe_nnreal_ennreal Î± m _ _
    (@ae_measurable.nnnorm H Î± _ _ _ m _ _ hf_meas_ae),
end

end trim

section sigma_finite

variables {E : Type*} {m0 : measurable_space Î±} [normed_group E] [measurable_space E]
  [opens_measurable_space E]

lemma integrable_of_forall_fin_meas_le' {Î¼ : measure Î±} (hm : m â‰¤ m0)
  [sigma_finite (Î¼.trim hm)] (C : â„â‰¥0âˆ) (hC : C < âˆ) {f : Î± â†’ E} (hf_meas : ae_measurable f Î¼)
  (hf : âˆ€ s, measurable_set[m] s â†’ Î¼ s â‰  âˆ â†’ âˆ«â» x in s, nnnorm (f x) âˆ‚Î¼ â‰¤ C) :
  integrable f Î¼ :=
âŸ¨hf_meas,
  (lintegral_le_of_forall_fin_meas_le' hm C hf_meas.nnnorm.coe_nnreal_ennreal hf).trans_lt hCâŸ©

lemma integrable_of_forall_fin_meas_le [sigma_finite Î¼]
  (C : â„â‰¥0âˆ) (hC : C < âˆ) {f : Î± â†’ E} (hf_meas : ae_measurable f Î¼)
  (hf : âˆ€ s : set Î±, measurable_set s â†’ Î¼ s â‰  âˆ â†’ âˆ«â» x in s, nnnorm (f x) âˆ‚Î¼ â‰¤ C) :
  integrable f Î¼ :=
@integrable_of_forall_fin_meas_le' _ _ _ _ _ _ _ _ le_rfl (by rwa trim_eq_self) C hC _ hf_meas hf

end sigma_finite

/-! ### The predicate `integrable` on measurable functions modulo a.e.-equality -/

namespace ae_eq_fun

section

/-- A class of almost everywhere equal functions is `integrable` if its function representative
is integrable. -/
def integrable (f : Î± â†’â‚˜[Î¼] Î²) : Prop := integrable f Î¼

lemma integrable_mk {f : Î± â†’ Î²} (hf : ae_measurable f Î¼ ) :
  (integrable (mk f hf : Î± â†’â‚˜[Î¼] Î²)) â†” measure_theory.integrable f Î¼ :=
begin
  simp [integrable],
  apply integrable_congr,
  exact coe_fn_mk f hf
end

lemma integrable_coe_fn {f : Î± â†’â‚˜[Î¼] Î²} : (measure_theory.integrable f Î¼) â†” integrable f :=
by rw [â† integrable_mk, mk_coe_fn]

lemma integrable_zero : integrable (0 : Î± â†’â‚˜[Î¼] Î²) :=
(integrable_zero Î± Î² Î¼).congr (coe_fn_mk _ _).symm

end

section

variables [borel_space Î²]

lemma integrable.neg {f : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable (-f) :=
induction_on f $ Î» f hfm hfi, (integrable_mk _).2 ((integrable_mk hfm).1 hfi).neg

section
variable [second_countable_topology Î²]

lemma integrable_iff_mem_L1 {f : Î± â†’â‚˜[Î¼] Î²} : integrable f â†” f âˆˆ (Î± â†’â‚[Î¼] Î²) :=
by rw [â† integrable_coe_fn, â† mem_â„’p_one_iff_integrable, Lp.mem_Lp_iff_mem_â„’p]

lemma integrable.add {f g : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable g â†’ integrable (f + g) :=
begin
  refine induction_onâ‚‚ f g (Î» f hf g hg hfi hgi, _),
  simp only [integrable_mk, mk_add_mk] at hfi hgi âŠ¢,
  exact hfi.add hgi
end

lemma integrable.sub {f g : Î± â†’â‚˜[Î¼] Î²} (hf : integrable f) (hg : integrable g) :
  integrable (f - g) :=
(sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

end

section normed_space
variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²] [measurable_space ğ•œ]
  [opens_measurable_space ğ•œ]

lemma integrable.smul {c : ğ•œ} {f : Î± â†’â‚˜[Î¼] Î²} : integrable f â†’ integrable (c â€¢ f) :=
induction_on f $ Î» f hfm hfi, (integrable_mk _).2 $ ((integrable_mk hfm).1 hfi).smul _

end normed_space

end

end ae_eq_fun

namespace L1
variables [second_countable_topology Î²] [borel_space Î²]

lemma integrable_coe_fn (f : Î± â†’â‚[Î¼] Î²) :
  integrable f Î¼ :=
by { rw â† mem_â„’p_one_iff_integrable, exact Lp.mem_â„’p f }

lemma has_finite_integral_coe_fn (f : Î± â†’â‚[Î¼] Î²) :
  has_finite_integral f Î¼ :=
(integrable_coe_fn f).has_finite_integral

lemma measurable_coe_fn (f : Î± â†’â‚[Î¼] Î²) :
  measurable f := Lp.measurable f

lemma ae_measurable_coe_fn (f : Î± â†’â‚[Î¼] Î²) :
  ae_measurable f Î¼ := Lp.ae_measurable f

lemma edist_def (f g : Î± â†’â‚[Î¼] Î²) :
  edist f g = âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ :=
by { simp [Lp.edist_def, snorm, snorm'], simp [edist_eq_coe_nnnorm_sub] }

lemma dist_def (f g : Î± â†’â‚[Î¼] Î²) :
  dist f g = (âˆ«â» a, edist (f a) (g a) âˆ‚Î¼).to_real :=
by { simp [Lp.dist_def, snorm, snorm'], simp [edist_eq_coe_nnnorm_sub] }

lemma norm_def (f : Î± â†’â‚[Î¼] Î²) :
  âˆ¥fâˆ¥ = (âˆ«â» a, nnnorm (f a) âˆ‚Î¼).to_real :=
by { simp [Lp.norm_def, snorm, snorm'] }

/-- Computing the norm of a difference between two LÂ¹-functions. Note that this is not a
  special case of `norm_def` since `(f - g) x` and `f x - g x` are not equal
  (but only a.e.-equal). -/
lemma norm_sub_eq_lintegral (f g : Î± â†’â‚[Î¼] Î²) :
  âˆ¥f - gâˆ¥ = (âˆ«â» x, (nnnorm (f x - g x) : â„â‰¥0âˆ) âˆ‚Î¼).to_real :=
begin
  rw [norm_def],
  congr' 1,
  rw lintegral_congr_ae,
  filter_upwards [Lp.coe_fn_sub f g],
  assume a ha,
  simp only [ha, pi.sub_apply],
end

lemma of_real_norm_eq_lintegral (f : Î± â†’â‚[Î¼] Î²) :
  ennreal.of_real âˆ¥fâˆ¥ = âˆ«â» x, (nnnorm (f x) : â„â‰¥0âˆ) âˆ‚Î¼ :=
by { rw [norm_def, ennreal.of_real_to_real], rw [â† ennreal.lt_top_iff_ne_top],
  exact has_finite_integral_coe_fn f }

/-- Computing the norm of a difference between two LÂ¹-functions. Note that this is not a
  special case of `of_real_norm_eq_lintegral` since `(f - g) x` and `f x - g x` are not equal
  (but only a.e.-equal). -/
lemma of_real_norm_sub_eq_lintegral (f g : Î± â†’â‚[Î¼] Î²) :
  ennreal.of_real âˆ¥f - gâˆ¥ = âˆ«â» x, (nnnorm (f x - g x) : â„â‰¥0âˆ) âˆ‚Î¼ :=
begin
  simp_rw [of_real_norm_eq_lintegral, â† edist_eq_coe_nnnorm],
  apply lintegral_congr_ae,
  filter_upwards [Lp.coe_fn_sub f g],
  assume a ha,
  simp only [ha, pi.sub_apply],
end

end L1

namespace integrable

variables [second_countable_topology Î²] [borel_space Î²]

/-- Construct the equivalence class `[f]` of an integrable function `f`, as a member of the
space `L1 Î² 1 Î¼`. -/
def to_L1 (f : Î± â†’ Î²) (hf : integrable f Î¼) : Î± â†’â‚[Î¼] Î² :=
(mem_â„’p_one_iff_integrable.2 hf).to_Lp f

@[simp] lemma to_L1_coe_fn (f : Î± â†’â‚[Î¼] Î²) (hf : integrable f Î¼) : hf.to_L1 f = f :=
by simp [integrable.to_L1]

lemma coe_fn_to_L1 {f : Î± â†’ Î²} (hf : integrable f Î¼) : hf.to_L1 f =áµ[Î¼] f :=
ae_eq_fun.coe_fn_mk _ _

@[simp] lemma to_L1_zero (h : integrable (0 : Î± â†’ Î²) Î¼) : h.to_L1 0 = 0 := rfl

@[simp] lemma to_L1_eq_mk (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  (hf.to_L1 f : Î± â†’â‚˜[Î¼] Î²) = ae_eq_fun.mk f hf.ae_measurable :=
rfl

@[simp] lemma to_L1_eq_to_L1_iff (f g : Î± â†’ Î²) (hf : integrable f Î¼) (hg : integrable g Î¼) :
  to_L1 f hf = to_L1 g hg â†” f =áµ[Î¼] g :=
mem_â„’p.to_Lp_eq_to_Lp_iff _ _

lemma to_L1_add (f g : Î± â†’ Î²) (hf : integrable f Î¼) (hg : integrable g Î¼) :
  to_L1 (f + g) (hf.add hg) = to_L1 f hf + to_L1 g hg := rfl

lemma to_L1_neg (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  to_L1 (- f) (integrable.neg hf) = - to_L1 f hf := rfl

lemma to_L1_sub (f g : Î± â†’ Î²) (hf : integrable f Î¼) (hg : integrable g Î¼) :
  to_L1 (f - g) (hf.sub hg) = to_L1 f hf - to_L1 g hg := rfl

lemma norm_to_L1 (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  âˆ¥hf.to_L1 fâˆ¥ = ennreal.to_real (âˆ«â» a, edist (f a) 0 âˆ‚Î¼) :=
by { simp [to_L1, snorm, snorm'], simp [edist_eq_coe_nnnorm] }

lemma norm_to_L1_eq_lintegral_norm (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  âˆ¥hf.to_L1 fâˆ¥ = ennreal.to_real (âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼) :=
by { rw [norm_to_L1, lintegral_norm_eq_lintegral_edist] }

@[simp] lemma edist_to_L1_to_L1 (f g : Î± â†’ Î²) (hf : integrable f Î¼) (hg : integrable g Î¼) :
  edist (hf.to_L1 f) (hg.to_L1 g) = âˆ«â» a, edist (f a) (g a) âˆ‚Î¼ :=
by { simp [integrable.to_L1, snorm, snorm'], simp [edist_eq_coe_nnnorm_sub] }

@[simp] lemma edist_to_L1_zero (f : Î± â†’ Î²) (hf : integrable f Î¼) :
  edist (hf.to_L1 f) 0 = âˆ«â» a, edist (f a) 0 âˆ‚Î¼ :=
by { simp [integrable.to_L1, snorm, snorm'], simp [edist_eq_coe_nnnorm] }

variables {ğ•œ : Type*} [normed_field ğ•œ] [normed_space ğ•œ Î²] [measurable_space ğ•œ]
  [opens_measurable_space ğ•œ]

lemma to_L1_smul (f : Î± â†’ Î²) (hf : integrable f Î¼) (k : ğ•œ) :
  to_L1 (Î»a, k â€¢ f a) (hf.smul k) = k â€¢ to_L1 f hf := rfl

end integrable

end measure_theory

open measure_theory

lemma integrable_zero_measure {m : measurable_space Î±} [measurable_space Î²] {f : Î± â†’ Î²} :
  integrable f (0 : measure Î±) :=
begin
  apply (integrable_zero _ _ _).congr,
  change (0 : measure Î±) {x | 0 â‰  f x} = 0,
  refl,
end

variables {E : Type*} [normed_group E] [measurable_space E] [borel_space E] [normed_space â„ E]
          {H : Type*} [normed_group H] [normed_space â„ H]

lemma measure_theory.integrable.apply_continuous_linear_map {Ï† : Î± â†’ H â†’L[â„] E}
  (Ï†_int : integrable Ï† Î¼) (v : H) : integrable (Î» a, Ï† a v) Î¼ :=
(Ï†_int.norm.mul_const âˆ¥vâˆ¥).mono' (Ï†_int.ae_measurable.apply_continuous_linear_map v)
  (eventually_of_forall $ Î» a, (Ï† a).le_op_norm v)

variables {ğ•œ : Type*} [is_R_or_C ğ•œ]
  {G : Type*} [normed_group G] [normed_space ğ•œ G] [measurable_space G] [borel_space G]
  {F : Type*} [normed_group F] [normed_space ğ•œ F] [measurable_space F] [opens_measurable_space F]

lemma continuous_linear_map.integrable_comp {Ï† : Î± â†’ F} (L : F â†’L[ğ•œ] G)
  (Ï†_int : integrable Ï† Î¼) : integrable (Î» (a : Î±), L (Ï† a)) Î¼ :=
((integrable.norm Ï†_int).const_mul âˆ¥Lâˆ¥).mono' (L.measurable.comp_ae_measurable Ï†_int.ae_measurable)
  (eventually_of_forall $ Î» a, L.le_op_norm (Ï† a))
