/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/
import analysis.normed_space.inner_product
import measure_theory.integral.set_integral

/-! # `L^2` space

If `E` is an inner product space over `ğ•œ` (`â„` or `â„‚`), then `Lp E 2 Î¼` (defined in `lp_space.lean`)
is also an inner product space, with inner product defined as `inner f g = âˆ« a, âŸªf a, g aâŸ« âˆ‚Î¼`.

### Main results

* `mem_L1_inner` : for `f` and `g` in `Lp E 2 Î¼`, the pointwise inner product `Î» x, âŸªf x, g xâŸ«`
  belongs to `Lp ğ•œ 1 Î¼`.
* `integrable_inner` : for `f` and `g` in `Lp E 2 Î¼`, the pointwise inner product `Î» x, âŸªf x, g xâŸ«`
  is integrable.
* `L2.inner_product_space` : `Lp E 2 Î¼` is an inner product space.

-/

noncomputable theory
open topological_space measure_theory measure_theory.Lp
open_locale nnreal ennreal measure_theory

namespace measure_theory
namespace L2

variables {Î± E F ğ•œ : Type*} [is_R_or_C ğ•œ] [measurable_space Î±] {Î¼ : measure Î±}
  [measurable_space E] [inner_product_space ğ•œ E] [borel_space E] [second_countable_topology E]
  [normed_group F] [measurable_space F] [borel_space F] [second_countable_topology F]

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E _ x y

lemma snorm_rpow_two_norm_lt_top (f : Lp F 2 Î¼) : snorm (Î» x, âˆ¥f xâˆ¥ ^ (2 : â„)) 1 Î¼ < âˆ :=
begin
  have h_two : ennreal.of_real (2 : â„) = 2, by simp [zero_le_one],
  rw [snorm_norm_rpow f zero_lt_two, one_mul, h_two],
  exact ennreal.rpow_lt_top_of_nonneg zero_le_two (Lp.snorm_ne_top f),
end

lemma snorm_inner_lt_top (f g : Î± â†’â‚‚[Î¼] E) : snorm (Î» (x : Î±), âŸªf x, g xâŸ«) 1 Î¼ < âˆ :=
begin
  have h : âˆ€ x, is_R_or_C.abs âŸªf x, g xâŸ« â‰¤ âˆ¥f xâˆ¥ * âˆ¥g xâˆ¥, from Î» x, abs_inner_le_norm _ _,
  have h' : âˆ€ x, is_R_or_C.abs âŸªf x, g xâŸ« â‰¤ is_R_or_C.abs (âˆ¥f xâˆ¥^2 + âˆ¥g xâˆ¥^2),
  { refine Î» x, le_trans (h x) _,
    rw [is_R_or_C.abs_to_real, abs_eq_self.mpr],
    swap, { exact add_nonneg (by simp) (by simp), },
    refine le_trans _ (half_le_self (add_nonneg (sq_nonneg _) (sq_nonneg _))),
    refine (le_div_iff (@zero_lt_two â„ _ _)).mpr ((le_of_eq _).trans (two_mul_le_add_sq _ _)),
    ring, },
  simp_rw [â† is_R_or_C.norm_eq_abs, â† real.rpow_nat_cast] at h',
  refine (snorm_mono_ae (ae_of_all _ h')).trans_lt ((snorm_add_le _ _ le_rfl).trans_lt _),
  { exact (Lp.ae_measurable f).norm.pow_const _ },
  { exact (Lp.ae_measurable g).norm.pow_const _ },
  simp only [nat.cast_bit0, ennreal.add_lt_top, nat.cast_one],
  exact âŸ¨snorm_rpow_two_norm_lt_top f, snorm_rpow_two_norm_lt_top gâŸ©,
end

section inner_product_space

variables [measurable_space ğ•œ] [borel_space ğ•œ]

include ğ•œ

instance : has_inner ğ•œ (Î± â†’â‚‚[Î¼] E) := âŸ¨Î» f g, âˆ« a, âŸªf a, g aâŸ« âˆ‚Î¼âŸ©

lemma inner_def (f g : Î± â†’â‚‚[Î¼] E) : inner f g = âˆ« a : Î±, âŸªf a, g aâŸ« âˆ‚Î¼ := rfl

lemma integral_inner_eq_sq_snorm (f : Î± â†’â‚‚[Î¼] E) :
  âˆ« a, âŸªf a, f aâŸ« âˆ‚Î¼ = ennreal.to_real âˆ«â» a, (nnnorm (f a) : â„â‰¥0âˆ) ^ (2:â„) âˆ‚Î¼ :=
begin
  simp_rw inner_self_eq_norm_sq_to_K,
  norm_cast,
  rw integral_eq_lintegral_of_nonneg_ae,
  swap, { exact filter.eventually_of_forall (Î» x, sq_nonneg _), },
  swap, { exact (Lp.ae_measurable f).norm.pow_const _ },
  congr,
  ext1 x,
  have h_two : (2 : â„) = ((2 : â„•) : â„), by simp,
  rw [â† real.rpow_nat_cast _ 2, â† h_two,
    â† ennreal.of_real_rpow_of_nonneg (norm_nonneg _) zero_le_two, of_real_norm_eq_coe_nnnorm],
  norm_cast,
end

private lemma norm_sq_eq_inner' (f : Î± â†’â‚‚[Î¼] E) : âˆ¥fâˆ¥ ^ 2 = is_R_or_C.re (inner f f : ğ•œ) :=
begin
  have h_two : (2 : â„â‰¥0âˆ).to_real = 2 := by simp,
  rw [inner_def, integral_inner_eq_sq_snorm, norm_def, â† ennreal.to_real_pow, is_R_or_C.of_real_re,
    ennreal.to_real_eq_to_real (ennreal.pow_lt_top (Lp.snorm_lt_top f) 2) _],
  { rw [â†ennreal.rpow_nat_cast, snorm_eq_snorm' ennreal.two_ne_zero ennreal.two_ne_top, snorm',
      â† ennreal.rpow_mul, one_div, h_two],
    simp, },
  { refine lintegral_rpow_nnnorm_lt_top_of_snorm'_lt_top zero_lt_two _,
    rw [â† h_two, â† snorm_eq_snorm' ennreal.two_ne_zero ennreal.two_ne_top],
    exact Lp.snorm_lt_top f, },
end

lemma mem_L1_inner (f g : Î± â†’â‚‚[Î¼] E) :
  ae_eq_fun.mk (Î» x, âŸªf x, g xâŸ«) ((Lp.ae_measurable f).inner (Lp.ae_measurable g)) âˆˆ Lp ğ•œ 1 Î¼ :=
by { simp_rw [mem_Lp_iff_snorm_lt_top, snorm_ae_eq_fun], exact snorm_inner_lt_top f g, }

lemma integrable_inner (f g : Î± â†’â‚‚[Î¼] E) : integrable (Î» x : Î±, âŸªf x, g xâŸ«) Î¼ :=
(integrable_congr (ae_eq_fun.coe_fn_mk (Î» x, âŸªf x, g xâŸ«)
    ((Lp.ae_measurable f).inner (Lp.ae_measurable g)))).mp
  (ae_eq_fun.integrable_iff_mem_L1.mpr (mem_L1_inner f g))

private lemma add_left' (f f' g : Î± â†’â‚‚[Î¼] E) : (inner (f + f') g : ğ•œ) = inner f g + inner f' g :=
begin
  simp_rw [inner_def, â† integral_add (integrable_inner f g) (integrable_inner f' g),
    â†inner_add_left],
  refine integral_congr_ae ((coe_fn_add f f').mono (Î» x hx, _)),
  congr,
  rwa pi.add_apply at hx,
end

private lemma smul_left' (f g : Î± â†’â‚‚[Î¼] E) (r : ğ•œ) :
  inner (r â€¢ f) g = is_R_or_C.conj r * inner f g :=
begin
  rw [inner_def, inner_def, â† smul_eq_mul, â† integral_smul],
  refine integral_congr_ae ((coe_fn_smul r f).mono (Î» x hx, _)),
  rw [smul_eq_mul, â† inner_smul_left],
  congr,
  rwa pi.smul_apply at hx,
end

instance inner_product_space : inner_product_space ğ•œ (Î± â†’â‚‚[Î¼] E) :=
{ norm_sq_eq_inner := norm_sq_eq_inner',
  conj_sym := Î» _ _, by simp_rw [inner_def, â† integral_conj, inner_conj_sym],
  add_left := add_left',
  smul_left := smul_left', }

end inner_product_space

section indicator_const_Lp

variables [measurable_space ğ•œ] [borel_space ğ•œ] {s : set Î±}

variables (ğ•œ)

/-- The inner product in `L2` of the indicator of a set `indicator_const_Lp 2 hs hÎ¼s c` and `f` is
equal to the integral of the inner product over `s`: `âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼`. -/
lemma inner_indicator_const_Lp_eq_set_integral_inner (f : Lp E 2 Î¼) (hs : measurable_set s) (c : E)
  (hÎ¼s : Î¼ s â‰  âˆ) :
  inner (indicator_const_Lp 2 hs hÎ¼s c) f = âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼ :=
begin
  rw [inner_def, â† integral_add_compl hs (L2.integrable_inner _ f)],
  have h_left : âˆ« x in s, âŸª(indicator_const_Lp 2 hs hÎ¼s c) x, f xâŸ« âˆ‚Î¼ = âˆ« x in s, âŸªc, f xâŸ« âˆ‚Î¼,
  { suffices h_ae_eq : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âŸªindicator_const_Lp 2 hs hÎ¼s c x, f xâŸ« = âŸªc, f xâŸ«,
      from set_integral_congr_ae hs h_ae_eq,
    have h_indicator : âˆ€áµ (x : Î±) âˆ‚Î¼, x âˆˆ s â†’ (indicator_const_Lp 2 hs hÎ¼s c x) = c,
      from indicator_const_Lp_coe_fn_mem,
    refine h_indicator.mono (Î» x hx hxs, _),
    congr,
    exact hx hxs, },
  have h_right : âˆ« x in sá¶œ, âŸª(indicator_const_Lp 2 hs hÎ¼s c) x, f xâŸ« âˆ‚Î¼ = 0,
  { suffices h_ae_eq : âˆ€áµ x âˆ‚Î¼, x âˆ‰ s â†’ âŸªindicator_const_Lp 2 hs hÎ¼s c x, f xâŸ« = 0,
    { simp_rw â† set.mem_compl_iff at h_ae_eq,
      suffices h_int_zero : âˆ« x in sá¶œ, inner (indicator_const_Lp 2 hs hÎ¼s c x) (f x) âˆ‚Î¼
        = âˆ« x in sá¶œ, (0 : ğ•œ) âˆ‚Î¼,
      { rw h_int_zero,
        simp, },
      exact set_integral_congr_ae hs.compl h_ae_eq, },
    have h_indicator : âˆ€áµ (x : Î±) âˆ‚Î¼, x âˆ‰ s â†’ (indicator_const_Lp 2 hs hÎ¼s c x) = 0,
      from indicator_const_Lp_coe_fn_nmem,
    refine h_indicator.mono (Î» x hx hxs, _),
    rw hx hxs,
    exact inner_zero_left, },
  rw [h_left, h_right, add_zero],
end

/-- The inner product in `L2` of the indicator of a set `indicator_const_Lp 2 hs hÎ¼s c` and `f` is
equal to the inner product of the constant `c` and the integral of `f` over `s`. -/
lemma inner_indicator_const_Lp_eq_inner_set_integral [complete_space E] [normed_space â„ E]
  [is_scalar_tower â„ ğ•œ E] (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (c : E) (f : Lp E 2 Î¼) :
  inner (indicator_const_Lp 2 hs hÎ¼s c) f = âŸªc, âˆ« x in s, f x âˆ‚Î¼âŸ« :=
by rw [â† integral_inner (integrable_on_Lp_of_measure_ne_top f fact_one_le_two_ennreal.elim hÎ¼s),
    L2.inner_indicator_const_Lp_eq_set_integral_inner]

variables {ğ•œ}

/-- The inner product in `L2` of the indicator of a set `indicator_const_Lp 2 hs hÎ¼s (1 : ğ•œ)` and
a real or complex function `f` is equal to the integral of `f` over `s`. -/
lemma inner_indicator_const_Lp_one (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆ) (f : Lp ğ•œ 2 Î¼) :
  inner (indicator_const_Lp 2 hs hÎ¼s (1 : ğ•œ)) f = âˆ« x in s, f x âˆ‚Î¼ :=
by { rw L2.inner_indicator_const_Lp_eq_inner_set_integral ğ•œ hs hÎ¼s (1 : ğ•œ) f, simp, }

end indicator_const_Lp

end L2

section inner_continuous

variables {Î± : Type*} [topological_space Î±] [measure_space Î±] [borel_space Î±] {ğ•œ : Type*}
  [is_R_or_C ğ•œ] [measurable_space ğ•œ] [borel_space ğ•œ]
variables (Î¼ : measure Î±) [finite_measure Î¼]

open_locale bounded_continuous_function

local attribute [instance] fact_one_le_two_ennreal

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ (Î± â†’â‚‚[Î¼] ğ•œ) _ x y

/-- For bounded continuous functions `f`, `g` on a finite-measure topological space `Î±`, the L^2
inner product is the integral of their pointwise inner product. -/
lemma bounded_continuous_function.inner_to_Lp (f g : Î± â†’áµ‡ ğ•œ) :
  âŸªbounded_continuous_function.to_Lp 2 Î¼ ğ•œ f, bounded_continuous_function.to_Lp 2 Î¼ ğ•œ gâŸ«
  = âˆ« x, is_R_or_C.conj (f x) * g x âˆ‚Î¼ :=
begin
  apply integral_congr_ae,
  have hf_ae := f.coe_fn_to_Lp Î¼,
  have hg_ae := g.coe_fn_to_Lp Î¼,
  filter_upwards [hf_ae, hg_ae],
  intros x hf hg,
  rw [hf, hg],
  simp
end

variables [compact_space Î±]

/-- For continuous functions `f`, `g` on a compact, finite-measure topological space `Î±`, the L^2
inner product is the integral of their pointwise inner product. -/
lemma continuous_map.inner_to_Lp (f g : C(Î±, ğ•œ)) :
  âŸªcontinuous_map.to_Lp 2 Î¼ ğ•œ f, continuous_map.to_Lp 2 Î¼ ğ•œ gâŸ«
  = âˆ« x, is_R_or_C.conj (f x) * g x âˆ‚Î¼ :=
begin
  apply integral_congr_ae,
  have hf_ae := f.coe_fn_to_Lp Î¼,
  have hg_ae := g.coe_fn_to_Lp Î¼,
  filter_upwards [hf_ae, hg_ae],
  intros x hf hg,
  rw [hf, hg],
  simp
end

end inner_continuous

end measure_theory
