/-
Copyright (c) 2020 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne, S√©bastien Gou√´zel
-/
import analysis.normed.group.hom
import measure_theory.function.lp_seminorm
import topology.continuous_function.compact

/-!
# ‚Ñíp space and Lp space

This file provides the space `Lp E p Œº` as the subtype of elements of `Œ± ‚Üí‚Çò[Œº] E` (see ae_eq_fun)
such that `snorm f p Œº` is finite. For `1 ‚â§ p`, `snorm` defines a norm and `Lp` is a complete metric space.

## Main definitions

* `Lp E p Œº` : elements of `Œ± ‚Üí‚Çò[Œº] E` (see ae_eq_fun) such that `snorm f p Œº` is finite. Defined
  as an `add_subgroup` of `Œ± ‚Üí‚Çò[Œº] E`.

Lipschitz functions vanishing at zero act by composition on `Lp`. We define this action, and prove
that it is continuous. In particular,
* `continuous_linear_map.comp_Lp` defines the action on `Lp` of a continuous linear map.
* `Lp.pos_part` is the positive part of an `Lp` function.
* `Lp.neg_part` is the negative part of an `Lp` function.

When `Œ±` is a topological space equipped with a finite Borel measure, there is a bounded linear map
from the normed space of bounded continuous functions (`Œ± ‚Üí·µá E`) to `Lp E p Œº`.  We construct this
as `bounded_continuous_function.to_Lp`.

## Notations

* `Œ± ‚Üí‚ÇÅ[Œº] E` : the type `Lp E 1 Œº`.
* `Œ± ‚Üí‚ÇÇ[Œº] E` : the type `Lp E 2 Œº`.

## Implementation

Since `Lp` is defined as an `add_subgroup`, dot notation does not work. Use `Lp.measurable f` to
say that the coercion of `f` to a genuine function is measurable, instead of the non-working
`f.measurable`.

To prove that two `Lp` elements are equal, it suffices to show that their coercions to functions
coincide almost everywhere (this is registered as an `ext` rule). This can often be done using
`filter_upwards`. For instance, a proof from first principles that `f + (g + h) = (f + g) + h`
could read (in the `Lp` namespace)
```
example (f g h : Lp E p Œº) : (f + g) + h = f + (g + h) :=
begin
  ext1,
  filter_upwards [coe_fn_add (f + g) h, coe_fn_add f g, coe_fn_add f (g + h), coe_fn_add g h]
    with _ ha1 ha2 ha3 ha4,
  simp only [ha1, ha2, ha3, ha4, add_assoc],
end
```
The lemma `coe_fn_add` states that the coercion of `f + g` coincides almost everywhere with the sum
of the coercions of `f` and `g`. All such lemmas use `coe_fn` in their name, to distinguish the
function coercion from the coercion to almost everywhere defined functions.
-/

noncomputable theory
open topological_space measure_theory filter
open_locale nnreal ennreal big_operators topology measure_theory

variables {Œ± E F G : Type*} {m m0 : measurable_space Œ±} {p : ‚Ñù‚â•0‚àû} {q : ‚Ñù} {Œº ŒΩ : measure Œ±}
  [normed_add_comm_group E] [normed_add_comm_group F] [normed_add_comm_group G]

namespace measure_theory

/-!
### Lp space

The space of equivalence classes of measurable functions for which `snorm f p Œº < ‚àû`.
-/

@[simp] lemma snorm_ae_eq_fun {Œ± E : Type*} [measurable_space Œ±] {Œº : measure Œ±}
  [normed_add_comm_group E] {p : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí E} (hf : ae_strongly_measurable f Œº) :
  snorm (ae_eq_fun.mk f hf) p Œº = snorm f p Œº :=
snorm_congr_ae (ae_eq_fun.coe_fn_mk _ _)

lemma mem_‚Ñíp.snorm_mk_lt_top {Œ± E : Type*} [measurable_space Œ±] {Œº : measure Œ±}
  [normed_add_comm_group E] {p : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí E} (hfp : mem_‚Ñíp f p Œº) :
  snorm (ae_eq_fun.mk f hfp.1) p Œº < ‚àû :=
by simp [hfp.2]

/-- Lp space -/
def Lp {Œ±} (E : Type*) {m : measurable_space Œ±} [normed_add_comm_group E]
  (p : ‚Ñù‚â•0‚àû) (Œº : measure Œ± . volume_tac) : add_subgroup (Œ± ‚Üí‚Çò[Œº] E) :=
{ carrier := {f | snorm f p Œº < ‚àû},
  zero_mem' := by simp [snorm_congr_ae ae_eq_fun.coe_fn_zero, snorm_zero],
  add_mem' := Œª f g hf hg, by simp [snorm_congr_ae (ae_eq_fun.coe_fn_add _ _),
    snorm_add_lt_top ‚ü®f.ae_strongly_measurable, hf‚ü© ‚ü®g.ae_strongly_measurable, hg‚ü©],
  neg_mem' := Œª f hf,
    by rwa [set.mem_set_of_eq, snorm_congr_ae (ae_eq_fun.coe_fn_neg _), snorm_neg] }

localized "notation (name := measure_theory.L1)
  Œ± ` ‚Üí‚ÇÅ[`:25 Œº `] ` E := measure_theory.Lp E 1 Œº" in measure_theory
localized "notation (name := measure_theory.L2)
  Œ± ` ‚Üí‚ÇÇ[`:25 Œº `] ` E := measure_theory.Lp E 2 Œº" in measure_theory

namespace mem_‚Ñíp

/-- make an element of Lp from a function verifying `mem_‚Ñíp` -/
def to_Lp (f : Œ± ‚Üí E) (h_mem_‚Ñíp : mem_‚Ñíp f p Œº) : Lp E p Œº :=
‚ü®ae_eq_fun.mk f h_mem_‚Ñíp.1, h_mem_‚Ñíp.snorm_mk_lt_top‚ü©

lemma coe_fn_to_Lp {f : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) : hf.to_Lp f =·µê[Œº] f :=
ae_eq_fun.coe_fn_mk _ _

lemma to_Lp_congr {f g : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) (hg : mem_‚Ñíp g p Œº) (hfg : f =·µê[Œº] g) :
  hf.to_Lp f = hg.to_Lp g :=
by simp [to_Lp, hfg]

@[simp] lemma to_Lp_eq_to_Lp_iff {f g : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) (hg : mem_‚Ñíp g p Œº) :
  hf.to_Lp f = hg.to_Lp g ‚Üî f =·µê[Œº] g :=
by simp [to_Lp]

@[simp] lemma to_Lp_zero (h : mem_‚Ñíp (0 : Œ± ‚Üí E) p Œº) : h.to_Lp 0 = 0 := rfl

lemma to_Lp_add {f g : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) (hg : mem_‚Ñíp g p Œº) :
  (hf.add hg).to_Lp (f + g) = hf.to_Lp f + hg.to_Lp g := rfl

lemma to_Lp_neg {f : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) : hf.neg.to_Lp (-f) = - hf.to_Lp f := rfl

lemma to_Lp_sub {f g : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) (hg : mem_‚Ñíp g p Œº) :
  (hf.sub hg).to_Lp (f - g) = hf.to_Lp f - hg.to_Lp g := rfl

end mem_‚Ñíp

namespace Lp

instance : has_coe_to_fun (Lp E p Œº) (Œª _, Œ± ‚Üí E) := ‚ü®Œª f, ((f : Œ± ‚Üí‚Çò[Œº] E) : Œ± ‚Üí E)‚ü©

@[ext] lemma ext {f g : Lp E p Œº} (h : f =·µê[Œº] g) : f = g :=
begin
  cases f,
  cases g,
  simp only [subtype.mk_eq_mk],
  exact ae_eq_fun.ext h
end

lemma ext_iff {f g : Lp E p Œº} : f = g ‚Üî f =·µê[Œº] g :=
‚ü®Œª h, by rw h, Œª h, ext h‚ü©

lemma mem_Lp_iff_snorm_lt_top {f : Œ± ‚Üí‚Çò[Œº] E} : f ‚àà Lp E p Œº ‚Üî snorm f p Œº < ‚àû := iff.refl _

lemma mem_Lp_iff_mem_‚Ñíp {f : Œ± ‚Üí‚Çò[Œº] E} : f ‚àà Lp E p Œº ‚Üî mem_‚Ñíp f p Œº :=
by simp [mem_Lp_iff_snorm_lt_top, mem_‚Ñíp, f.strongly_measurable.ae_strongly_measurable]

protected lemma antitone [is_finite_measure Œº] {p q : ‚Ñù‚â•0‚àû} (hpq : p ‚â§ q) : Lp E q Œº ‚â§ Lp E p Œº :=
Œª f hf, (mem_‚Ñíp.mem_‚Ñíp_of_exponent_le ‚ü®f.ae_strongly_measurable, hf‚ü© hpq).2

@[simp] lemma coe_fn_mk {f : Œ± ‚Üí‚Çò[Œº] E} (hf : snorm f p Œº < ‚àû) :
  ((‚ü®f, hf‚ü© : Lp E p Œº) : Œ± ‚Üí E) = f := rfl

@[simp] lemma coe_mk {f : Œ± ‚Üí‚Çò[Œº] E} (hf : snorm f p Œº < ‚àû) :
  ((‚ü®f, hf‚ü© : Lp E p Œº) : Œ± ‚Üí‚Çò[Œº] E) = f := rfl

@[simp] lemma to_Lp_coe_fn (f : Lp E p Œº) (hf : mem_‚Ñíp f p Œº) : hf.to_Lp f = f :=
by { cases f, simp [mem_‚Ñíp.to_Lp] }

lemma snorm_lt_top (f : Lp E p Œº) : snorm f p Œº < ‚àû := f.prop

lemma snorm_ne_top (f : Lp E p Œº) : snorm f p Œº ‚â† ‚àû := (snorm_lt_top f).ne

@[measurability]
protected lemma strongly_measurable (f : Lp E p Œº) : strongly_measurable f :=
f.val.strongly_measurable

@[measurability]
protected lemma ae_strongly_measurable (f : Lp E p Œº) : ae_strongly_measurable f Œº :=
f.val.ae_strongly_measurable

protected lemma mem_‚Ñíp (f : Lp E p Œº) : mem_‚Ñíp f p Œº := ‚ü®Lp.ae_strongly_measurable f, f.prop‚ü©

variables (E p Œº)
lemma coe_fn_zero : ‚áë(0 : Lp E p Œº) =·µê[Œº] 0 := ae_eq_fun.coe_fn_zero
variables {E p Œº}

lemma coe_fn_neg (f : Lp E p Œº) : ‚áë(-f) =·µê[Œº] -f := ae_eq_fun.coe_fn_neg _

lemma coe_fn_add (f g : Lp E p Œº) : ‚áë(f + g) =·µê[Œº] f + g := ae_eq_fun.coe_fn_add _ _

lemma coe_fn_sub (f g : Lp E p Œº) : ‚áë(f - g) =·µê[Œº] f - g := ae_eq_fun.coe_fn_sub _ _

lemma mem_Lp_const (Œ±) {m : measurable_space Œ±} (Œº : measure Œ±) (c : E) [is_finite_measure Œº] :
  @ae_eq_fun.const Œ± _ _ Œº _ c ‚àà Lp E p Œº :=
(mem_‚Ñíp_const c).snorm_mk_lt_top

instance : has_norm (Lp E p Œº) := { norm := Œª f, ennreal.to_real (snorm f p Œº) }

-- note: we need this to be defeq to the instance from `seminormed_add_group.to_has_nnnorm`, so
-- can't use `ennreal.to_nnreal (snorm f p Œº)`
instance : has_nnnorm (Lp E p Œº) := { nnnorm := Œª f, ‚ü®‚Äñf‚Äñ, ennreal.to_real_nonneg‚ü© }

instance : has_dist (Lp E p Œº) := { dist := Œª f g, ‚Äñf - g‚Äñ}

instance : has_edist (Lp E p Œº) := { edist := Œª f g, snorm (f - g) p Œº }

lemma norm_def (f : Lp E p Œº) : ‚Äñf‚Äñ = ennreal.to_real (snorm f p Œº) := rfl

lemma nnnorm_def (f : Lp E p Œº) : ‚Äñf‚Äñ‚Çä = ennreal.to_nnreal (snorm f p Œº) := subtype.eta _ _

@[simp, norm_cast] protected lemma coe_nnnorm (f : Lp E p Œº) : (‚Äñf‚Äñ‚Çä : ‚Ñù) = ‚Äñf‚Äñ := rfl

@[simp] lemma norm_to_Lp (f : Œ± ‚Üí E) (hf : mem_‚Ñíp f p Œº) :
  ‚Äñhf.to_Lp f‚Äñ = ennreal.to_real (snorm f p Œº) :=
by rw [norm_def, snorm_congr_ae (mem_‚Ñíp.coe_fn_to_Lp hf)]

@[simp] lemma nnnorm_to_Lp (f : Œ± ‚Üí E) (hf : mem_‚Ñíp f p Œº) :
  ‚Äñhf.to_Lp f‚Äñ‚Çä = ennreal.to_nnreal (snorm f p Œº) :=
nnreal.eq $ norm_to_Lp f hf

lemma dist_def (f g : Lp E p Œº) : dist f g = (snorm (f - g) p Œº).to_real :=
begin
  simp_rw [dist, norm_def],
  congr' 1,
  apply snorm_congr_ae (coe_fn_sub _ _),
end

lemma edist_def (f g : Lp E p Œº) : edist f g = snorm (f - g) p Œº :=
rfl

@[simp] lemma edist_to_Lp_to_Lp (f g : Œ± ‚Üí E) (hf : mem_‚Ñíp f p Œº) (hg : mem_‚Ñíp g p Œº) :
  edist (hf.to_Lp f) (hg.to_Lp g) = snorm (f - g) p Œº :=
by { rw edist_def, exact snorm_congr_ae (hf.coe_fn_to_Lp.sub hg.coe_fn_to_Lp) }

@[simp] lemma edist_to_Lp_zero (f : Œ± ‚Üí E) (hf : mem_‚Ñíp f p Œº) :
  edist (hf.to_Lp f) 0 = snorm f p Œº :=
by { convert edist_to_Lp_to_Lp f 0 hf zero_mem_‚Ñíp, simp }

@[simp] lemma nnnorm_zero : ‚Äñ(0 : Lp E p Œº)‚Äñ‚Çä = 0 :=
begin
  rw [nnnorm_def],
  change (snorm ‚áë(0 : Œ± ‚Üí‚Çò[Œº] E) p Œº).to_nnreal = 0,
  simp [snorm_congr_ae ae_eq_fun.coe_fn_zero, snorm_zero]
end

@[simp] lemma norm_zero : ‚Äñ(0 : Lp E p Œº)‚Äñ = 0 :=
congr_arg coe nnnorm_zero

lemma nnnorm_eq_zero_iff {f : Lp E p Œº} (hp : 0 < p) : ‚Äñf‚Äñ‚Çä = 0 ‚Üî f = 0 :=
begin
  refine ‚ü®Œª hf, _, Œª hf, by simp [hf]‚ü©,
  rw [nnnorm_def, ennreal.to_nnreal_eq_zero_iff] at hf,
  cases hf,
  { rw snorm_eq_zero_iff (Lp.ae_strongly_measurable f) hp.ne.symm at hf,
    exact subtype.eq (ae_eq_fun.ext (hf.trans ae_eq_fun.coe_fn_zero.symm)), },
  { exact absurd hf (snorm_ne_top f), },
end

lemma norm_eq_zero_iff {f : Lp E p Œº} (hp : 0 < p) : ‚Äñf‚Äñ = 0 ‚Üî f = 0 :=
iff.symm $ (nnnorm_eq_zero_iff hp).symm.trans $ (nnreal.coe_eq_zero _).symm

lemma eq_zero_iff_ae_eq_zero {f : Lp E p Œº} : f = 0 ‚Üî f =·µê[Œº] 0 :=
begin
  split,
  { assume h,
    rw h,
    exact ae_eq_fun.coe_fn_const _ _ },
  { assume h,
    ext1,
    filter_upwards [h, ae_eq_fun.coe_fn_const Œ± (0 : E)] with _ ha h'a,
    rw ha,
    exact h'a.symm, },
end

@[simp] lemma nnnorm_neg (f : Lp E p Œº) : ‚Äñ-f‚Äñ‚Çä = ‚Äñf‚Äñ‚Çä :=
by rw [nnnorm_def, nnnorm_def, snorm_congr_ae (coe_fn_neg _), snorm_neg]

@[simp] lemma norm_neg (f : Lp E p Œº) : ‚Äñ-f‚Äñ = ‚Äñf‚Äñ :=
(congr_arg (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) (nnnorm_neg f) : _)

lemma nnnorm_le_mul_nnnorm_of_ae_le_mul {c : ‚Ñù‚â•0} {f : Lp E p Œº} {g : Lp F p Œº}
  (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ c * ‚Äñg x‚Äñ‚Çä ) : ‚Äñf‚Äñ‚Çä ‚â§ c * ‚Äñg‚Äñ‚Çä  :=
begin
  simp only [nnnorm_def],
  have := snorm_le_nnreal_smul_snorm_of_ae_le_mul h p,
  rwa [‚Üê ennreal.to_nnreal_le_to_nnreal, ennreal.smul_def, smul_eq_mul, ennreal.to_nnreal_mul,
    ennreal.to_nnreal_coe] at this,
  { exact (Lp.mem_‚Ñíp _).snorm_ne_top },
  { exact ennreal.mul_ne_top ennreal.coe_ne_top (Lp.mem_‚Ñíp _).snorm_ne_top },
end

lemma norm_le_mul_norm_of_ae_le_mul {c : ‚Ñù} {f : Lp E p Œº} {g : Lp F p Œº}
  (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ) : ‚Äñf‚Äñ ‚â§ c * ‚Äñg‚Äñ :=
begin
  cases le_or_lt 0 c with hc hc,
  { lift c to ‚Ñù‚â•0 using hc,
    exact nnreal.coe_le_coe.mpr (nnnorm_le_mul_nnnorm_of_ae_le_mul h) },
  { simp only [norm_def],
    have := snorm_eq_zero_and_zero_of_ae_le_mul_neg h hc p,
    simp [this] }
end

lemma norm_le_norm_of_ae_le {f : Lp E p Œº} {g : Lp F p Œº} (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ ‚Äñg x‚Äñ) :
  ‚Äñf‚Äñ ‚â§ ‚Äñg‚Äñ :=
begin
  rw [norm_def, norm_def, ennreal.to_real_le_to_real (snorm_ne_top _) (snorm_ne_top _)],
  exact snorm_mono_ae h
end

lemma mem_Lp_of_nnnorm_ae_le_mul {c : ‚Ñù‚â•0} {f : Œ± ‚Üí‚Çò[Œº] E} {g : Lp F p Œº}
  (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ c * ‚Äñg x‚Äñ‚Çä) :
  f ‚àà Lp E p Œº :=
mem_Lp_iff_mem_‚Ñíp.2 $ mem_‚Ñíp.of_nnnorm_le_mul (Lp.mem_‚Ñíp g) f.ae_strongly_measurable h

lemma mem_Lp_of_ae_le_mul {c : ‚Ñù} {f : Œ± ‚Üí‚Çò[Œº] E} {g : Lp F p Œº} (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ) :
  f ‚àà Lp E p Œº :=
mem_Lp_iff_mem_‚Ñíp.2 $ mem_‚Ñíp.of_le_mul (Lp.mem_‚Ñíp g) f.ae_strongly_measurable h

lemma mem_Lp_of_nnnorm_ae_le {f : Œ± ‚Üí‚Çò[Œº] E} {g : Lp F p Œº} (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ ‚Äñg x‚Äñ‚Çä) :
  f ‚àà Lp E p Œº :=
mem_Lp_iff_mem_‚Ñíp.2 $ mem_‚Ñíp.of_le (Lp.mem_‚Ñíp g) f.ae_strongly_measurable h

lemma mem_Lp_of_ae_le {f : Œ± ‚Üí‚Çò[Œº] E} {g : Lp F p Œº} (h : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ ‚Äñg x‚Äñ) :
  f ‚àà Lp E p Œº :=
mem_Lp_of_nnnorm_ae_le h

lemma mem_Lp_of_ae_nnnorm_bound [is_finite_measure Œº] {f : Œ± ‚Üí‚Çò[Œº] E} (C : ‚Ñù‚â•0)
  (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ C) :
  f ‚àà Lp E p Œº :=
mem_Lp_iff_mem_‚Ñíp.2 $ mem_‚Ñíp.of_bound f.ae_strongly_measurable _ hfC

lemma mem_Lp_of_ae_bound [is_finite_measure Œº] {f : Œ± ‚Üí‚Çò[Œº] E} (C : ‚Ñù) (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ C) :
  f ‚àà Lp E p Œº :=
mem_Lp_iff_mem_‚Ñíp.2 $ mem_‚Ñíp.of_bound f.ae_strongly_measurable _ hfC

lemma nnnorm_le_of_ae_bound [is_finite_measure Œº] {f : Lp E p Œº} {C : ‚Ñù‚â•0}
  (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ‚Çä ‚â§ C) :
  ‚Äñf‚Äñ‚Çä ‚â§ (measure_univ_nnreal Œº) ^ (p.to_real)‚Åª¬π * C :=
begin
  by_cases hŒº : Œº = 0,
  { by_cases hp : p.to_real‚Åª¬π = 0,
    { simp [hp, hŒº, nnnorm_def] },
    { simp [hŒº, nnnorm_def, real.zero_rpow hp] } },
  rw [‚Üêennreal.coe_le_coe, nnnorm_def, ennreal.coe_to_nnreal (snorm_ne_top _)],
  refine (snorm_le_of_ae_nnnorm_bound hfC).trans_eq _,
  rw [‚Üê coe_measure_univ_nnreal Œº, ennreal.coe_rpow_of_ne_zero (measure_univ_nnreal_pos hŒº).ne',
    ennreal.coe_mul, mul_comm, ennreal.smul_def, smul_eq_mul],
end

lemma norm_le_of_ae_bound [is_finite_measure Œº] {f : Lp E p Œº} {C : ‚Ñù} (hC : 0 ‚â§ C)
  (hfC : ‚àÄ·µê x ‚àÇŒº, ‚Äñf x‚Äñ ‚â§ C) :
  ‚Äñf‚Äñ ‚â§ (measure_univ_nnreal Œº) ^ (p.to_real)‚Åª¬π * C :=
begin
  lift C to ‚Ñù‚â•0 using hC,
  have := nnnorm_le_of_ae_bound hfC,
  rwa [‚Üênnreal.coe_le_coe, nnreal.coe_mul, nnreal.coe_rpow] at this,
end

instance [hp : fact (1 ‚â§ p)] : normed_add_comm_group (Lp E p Œº) :=
{ edist := edist,
  edist_dist := Œª f g, by
    rw [edist_def, dist_def, ‚Üêsnorm_congr_ae (coe_fn_sub _ _),
      ennreal.of_real_to_real (snorm_ne_top (f - g))],
  ..add_group_norm.to_normed_add_comm_group
    { to_fun := (norm : Lp E p Œº ‚Üí ‚Ñù),
      map_zero' := norm_zero,
      neg' := by simp,
      add_le' := Œª f g, begin
        simp only [norm_def],
        rw ‚Üê ennreal.to_real_add (snorm_ne_top f) (snorm_ne_top g),
        suffices h_snorm : snorm ‚áë(f + g) p Œº ‚â§ snorm ‚áëf p Œº + snorm ‚áëg p Œº,
        { rwa ennreal.to_real_le_to_real (snorm_ne_top (f + g)),
          exact ennreal.add_ne_top.mpr ‚ü®snorm_ne_top f, snorm_ne_top g‚ü©, },
        rw [snorm_congr_ae (coe_fn_add _ _)],
        exact snorm_add_le (Lp.ae_strongly_measurable f) (Lp.ae_strongly_measurable g) hp.1,
      end,
      eq_zero_of_map_eq_zero' := Œª f, (norm_eq_zero_iff $ zero_lt_one.trans_le hp.1).1 } }

-- check no diamond is created
example [fact (1 ‚â§ p)] :
  pseudo_emetric_space.to_has_edist = (Lp.has_edist : has_edist (Lp E p Œº)) :=
rfl

example [fact (1 ‚â§ p)] :
  seminormed_add_group.to_has_nnnorm = (Lp.has_nnnorm : has_nnnorm (Lp E p Œº)) :=
rfl

section has_bounded_smul

variables {ùïú ùïú' : Type*}
variables [normed_ring ùïú] [normed_ring ùïú'] [module ùïú E] [module ùïú' E]
variables [has_bounded_smul ùïú E] [has_bounded_smul ùïú' E]

lemma mem_Lp_const_smul (c : ùïú) (f : Lp E p Œº) : c ‚Ä¢ ‚Üëf ‚àà Lp E p Œº :=
begin
  rw [mem_Lp_iff_snorm_lt_top, snorm_congr_ae (ae_eq_fun.coe_fn_smul _ _)],
  refine (snorm_const_smul_le _ _).trans_lt _,
  rw [ennreal.smul_def, smul_eq_mul, ennreal.mul_lt_top_iff],
  exact or.inl ‚ü®ennreal.coe_lt_top, f.prop‚ü©,
end

variables (E p Œº ùïú)

/-- The `ùïú`-submodule of elements of `Œ± ‚Üí‚Çò[Œº] E` whose `Lp` norm is finite.  This is `Lp E p Œº`,
with extra structure. -/
def Lp_submodule : submodule ùïú (Œ± ‚Üí‚Çò[Œº] E) :=
{ smul_mem' := Œª c f hf, by simpa using mem_Lp_const_smul c ‚ü®f, hf‚ü©,
  .. Lp E p Œº }

variables {E p Œº ùïú}

lemma coe_Lp_submodule : (Lp_submodule E p Œº ùïú).to_add_subgroup = Lp E p Œº := rfl

instance : module ùïú (Lp E p Œº) :=
{ .. (Lp_submodule E p Œº ùïú).module }

lemma coe_fn_smul (c : ùïú) (f : Lp E p Œº) : ‚áë(c ‚Ä¢ f) =·µê[Œº] c ‚Ä¢ f := ae_eq_fun.coe_fn_smul _ _

instance [module ùïú·µê·µí·µñ E] [has_bounded_smul ùïú·µê·µí·µñ E] [is_central_scalar ùïú E] :
  is_central_scalar ùïú (Lp E p Œº) :=
{ op_smul_eq_smul := Œª k f, subtype.ext $ op_smul_eq_smul k (f : Œ± ‚Üí‚Çò[Œº] E) }

instance [smul_comm_class ùïú ùïú' E] : smul_comm_class ùïú ùïú' (Lp E p Œº) :=
{ smul_comm := Œª k k' f, subtype.ext $ smul_comm k k' (f : Œ± ‚Üí‚Çò[Œº] E) }

instance [has_smul ùïú ùïú'] [is_scalar_tower ùïú ùïú' E] : is_scalar_tower ùïú ùïú' (Lp E p Œº) :=
{ smul_assoc := Œª k k' f, subtype.ext $ smul_assoc k k' (f : Œ± ‚Üí‚Çò[Œº] E) }

instance [fact (1 ‚â§ p)] : has_bounded_smul ùïú (Lp E p Œº) :=
-- TODO: add `has_bounded_smul.of_nnnorm_smul_le
has_bounded_smul.of_norm_smul_le $ Œª r f, begin
  suffices : (‚Äñr ‚Ä¢ f‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ‚â§ ‚Äñr‚Äñ‚Çä * ‚Äñf‚Äñ‚Çä,
  { exact_mod_cast this },
  rw [nnnorm_def, nnnorm_def, ennreal.coe_to_nnreal (Lp.snorm_ne_top _),
    snorm_congr_ae (coe_fn_smul _ _), ennreal.coe_to_nnreal (Lp.snorm_ne_top _)],
  exact snorm_const_smul_le r f,
end

end has_bounded_smul

section normed_space
variables {ùïú : Type*} [normed_field ùïú] [normed_space ùïú E]

instance [fact (1 ‚â§ p)] : normed_space ùïú (Lp E p Œº) :=
{ norm_smul_le := Œª _ _, norm_smul_le _ _ }

end normed_space

end Lp

namespace mem_‚Ñíp
variables {ùïú : Type*} [normed_ring ùïú] [module ùïú E] [has_bounded_smul ùïú E]

lemma to_Lp_const_smul {f : Œ± ‚Üí E} (c : ùïú) (hf : mem_‚Ñíp f p Œº) :
  (hf.const_smul c).to_Lp (c ‚Ä¢ f) = c ‚Ä¢ hf.to_Lp f := rfl

end mem_‚Ñíp

/-! ### Indicator of a set as an element of L·µñ

For a set `s` with `(hs : measurable_set s)` and `(hŒºs : Œº s < ‚àû)`, we build
`indicator_const_Lp p hs hŒºs c`, the element of `Lp` corresponding to `s.indicator (Œª x, c)`.
-/

section indicator

variables {s : set Œ±} {hs : measurable_set s} {c : E} {f : Œ± ‚Üí E} {hf : ae_strongly_measurable f Œº}

lemma snorm_ess_sup_indicator_le (s : set Œ±) (f : Œ± ‚Üí G) :
  snorm_ess_sup (s.indicator f) Œº ‚â§ snorm_ess_sup f Œº :=
begin
  refine ess_sup_mono_ae (eventually_of_forall (Œª x, _)),
  rw [ennreal.coe_le_coe, nnnorm_indicator_eq_indicator_nnnorm],
  exact set.indicator_le_self s _ x,
end

lemma snorm_ess_sup_indicator_const_le (s : set Œ±) (c : G) :
  snorm_ess_sup (s.indicator (Œª x : Œ± , c)) Œº ‚â§ ‚Äñc‚Äñ‚Çä :=
begin
  by_cases hŒº0 : Œº = 0,
  { rw [hŒº0, snorm_ess_sup_measure_zero],
    exact zero_le _ },
  { exact (snorm_ess_sup_indicator_le s (Œª x, c)).trans (snorm_ess_sup_const c hŒº0).le, },
end

lemma snorm_ess_sup_indicator_const_eq (s : set Œ±) (c : G) (hŒºs : Œº s ‚â† 0) :
  snorm_ess_sup (s.indicator (Œª x : Œ± , c)) Œº = ‚Äñc‚Äñ‚Çä :=
begin
  refine le_antisymm (snorm_ess_sup_indicator_const_le s c) _,
  by_contra' h,
  have h' := ae_iff.mp (ae_lt_of_ess_sup_lt h),
  push_neg at h',
  refine hŒºs (measure_mono_null (Œª x hx_mem, _) h'),
  rw [set.mem_set_of_eq, set.indicator_of_mem hx_mem],
  exact le_rfl,
end

variables (hs)

lemma snorm_indicator_le {E : Type*} [normed_add_comm_group E] (f : Œ± ‚Üí E) :
  snorm (s.indicator f) p Œº ‚â§ snorm f p Œº :=
begin
  refine snorm_mono_ae (eventually_of_forall (Œª x, _)),
  suffices : ‚Äñs.indicator f x‚Äñ‚Çä ‚â§ ‚Äñf x‚Äñ‚Çä,
  { exact nnreal.coe_mono this },
  rw nnnorm_indicator_eq_indicator_nnnorm,
  exact s.indicator_le_self _ x,
end

variables {hs}

lemma snorm_indicator_const {c : G} (hs : measurable_set s) (hp : p ‚â† 0) (hp_top : p ‚â† ‚àû) :
  snorm (s.indicator (Œª x, c)) p Œº = ‚Äñc‚Äñ‚Çä * (Œº s) ^ (1 / p.to_real) :=
begin
  have hp_pos : 0 < p.to_real, from ennreal.to_real_pos hp hp_top,
  rw snorm_eq_lintegral_rpow_nnnorm hp hp_top,
  simp_rw [nnnorm_indicator_eq_indicator_nnnorm, ennreal.coe_indicator],
  have h_indicator_pow : (Œª a : Œ±, s.indicator (Œª (x : Œ±), (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) a ^ p.to_real)
    = s.indicator (Œª (x : Œ±), ‚Üë‚Äñc‚Äñ‚Çä ^ p.to_real),
  { rw set.comp_indicator_const (‚Äñc‚Äñ‚Çä : ‚Ñù‚â•0‚àû) (Œª x, x ^ p.to_real) _,
    simp [hp_pos], },
  rw [h_indicator_pow, lintegral_indicator _ hs, set_lintegral_const, ennreal.mul_rpow_of_nonneg],
  { rw [‚Üê ennreal.rpow_mul, mul_one_div_cancel hp_pos.ne.symm, ennreal.rpow_one], },
  { simp [hp_pos.le], },
end

lemma snorm_indicator_const' {c : G} (hs : measurable_set s) (hŒºs : Œº s ‚â† 0) (hp : p ‚â† 0) :
  snorm (s.indicator (Œª _, c)) p Œº = ‚Äñc‚Äñ‚Çä * (Œº s) ^ (1 / p.to_real) :=
begin
  by_cases hp_top : p = ‚àû,
  { simp [hp_top, snorm_ess_sup_indicator_const_eq s c hŒºs], },
  { exact snorm_indicator_const hs hp hp_top, },
end

lemma snorm_indicator_const_le (c : G) (p : ‚Ñù‚â•0‚àû) :
  snorm (s.indicator (Œª x, c)) p Œº ‚â§ ‚Äñc‚Äñ‚Çä * (Œº s) ^ (1 / p.to_real) :=
begin
  rcases eq_or_ne p 0 with rfl|hp,
  { simp only [snorm_exponent_zero, zero_le'] },
  rcases eq_or_ne p ‚àû with rfl|h'p,
  { simp only [snorm_exponent_top, ennreal.top_to_real, div_zero, ennreal.rpow_zero, mul_one],
    exact snorm_ess_sup_indicator_const_le _ _ },
  let t := to_measurable Œº s,
  calc snorm (s.indicator (Œª x, c)) p Œº
      ‚â§ snorm (t.indicator (Œª x, c)) p Œº :
    snorm_mono (norm_indicator_le_of_subset (subset_to_measurable _ _) _)
  ... = ‚Äñc‚Äñ‚Çä * (Œº t) ^ (1 / p.to_real) :
    snorm_indicator_const (measurable_set_to_measurable _ _) hp h'p
  ... = ‚Äñc‚Äñ‚Çä * (Œº s) ^ (1 / p.to_real) : by rw measure_to_measurable
end

lemma mem_‚Ñíp.indicator (hs : measurable_set s) (hf : mem_‚Ñíp f p Œº) :
  mem_‚Ñíp (s.indicator f) p Œº :=
‚ü®hf.ae_strongly_measurable.indicator hs, lt_of_le_of_lt (snorm_indicator_le f) hf.snorm_lt_top‚ü©

lemma snorm_ess_sup_indicator_eq_snorm_ess_sup_restrict {f : Œ± ‚Üí F} (hs : measurable_set s) :
  snorm_ess_sup (s.indicator f) Œº = snorm_ess_sup f (Œº.restrict s) :=
begin
  simp_rw [snorm_ess_sup, nnnorm_indicator_eq_indicator_nnnorm, ennreal.coe_indicator],
  by_cases hs_null : Œº s = 0,
  { rw measure.restrict_zero_set hs_null,
    simp only [ess_sup_measure_zero, ennreal.ess_sup_eq_zero_iff, ennreal.bot_eq_zero],
    have hs_empty : s =·µê[Œº] (‚àÖ : set Œ±), by { rw ae_eq_set, simpa using hs_null, },
    refine (indicator_ae_eq_of_ae_eq_set hs_empty).trans _,
    rw set.indicator_empty,
    refl, },
  rw ess_sup_indicator_eq_ess_sup_restrict (eventually_of_forall (Œª x, _)) hs hs_null,
  rw pi.zero_apply,
  exact zero_le _,
end

lemma snorm_indicator_eq_snorm_restrict {f : Œ± ‚Üí F} (hs : measurable_set s) :
  snorm (s.indicator f) p Œº = snorm f p (Œº.restrict s) :=
begin
  by_cases hp_zero : p = 0,
  { simp only [hp_zero, snorm_exponent_zero], },
  by_cases hp_top : p = ‚àû,
  { simp_rw [hp_top, snorm_exponent_top],
    exact snorm_ess_sup_indicator_eq_snorm_ess_sup_restrict hs, },
  simp_rw snorm_eq_lintegral_rpow_nnnorm hp_zero hp_top,
  suffices : ‚à´‚Åª x, ‚Äñs.indicator f x‚Äñ‚Çä ^ p.to_real ‚àÇŒº = ‚à´‚Åª x in s, ‚Äñf x‚Äñ‚Çä ^ p.to_real ‚àÇŒº,
    by rw this,
  rw ‚Üê lintegral_indicator _ hs,
  congr,
  simp_rw [nnnorm_indicator_eq_indicator_nnnorm, ennreal.coe_indicator],
  have h_zero : (Œª x, x ^ p.to_real) (0 : ‚Ñù‚â•0‚àû) = 0,
    by simp [ennreal.to_real_pos hp_zero hp_top],
  exact (set.indicator_comp_of_zero h_zero).symm,
end

lemma mem_‚Ñíp_indicator_iff_restrict (hs : measurable_set s) :
  mem_‚Ñíp (s.indicator f) p Œº ‚Üî mem_‚Ñíp f p (Œº.restrict s) :=
by simp [mem_‚Ñíp, ae_strongly_measurable_indicator_iff hs, snorm_indicator_eq_snorm_restrict hs]

lemma mem_‚Ñíp_indicator_const (p : ‚Ñù‚â•0‚àû) (hs : measurable_set s) (c : E) (hŒºsc : c = 0 ‚à® Œº s ‚â† ‚àû) :
  mem_‚Ñíp (s.indicator (Œª _, c)) p Œº :=
begin
  rw mem_‚Ñíp_indicator_iff_restrict hs,
  by_cases hp_zero : p = 0,
  { rw hp_zero, exact mem_‚Ñíp_zero_iff_ae_strongly_measurable.mpr ae_strongly_measurable_const, },
  by_cases hp_top : p = ‚àû,
  { rw hp_top,
    exact mem_‚Ñíp_top_of_bound ae_strongly_measurable_const (‚Äñc‚Äñ)
      (eventually_of_forall (Œª x, le_rfl)), },
  rw [mem_‚Ñíp_const_iff hp_zero hp_top, measure.restrict_apply_univ],
  cases hŒºsc,
  { exact or.inl hŒºsc, },
  { exact or.inr hŒºsc.lt_top, },
end

/-- The `‚Ñí^p` norm of the indicator of a set is uniformly small if the set itself has small measure,
for any `p < ‚àû`. Given here as an existential `‚àÄ Œµ > 0, ‚àÉ Œ∑ > 0, ...` to avoid later
management of `‚Ñù‚â•0‚àû`-arithmetic. -/
lemma exists_snorm_indicator_le (hp : p ‚â† ‚àû) (c : E) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
  ‚àÉ (Œ∑ : ‚Ñù‚â•0), 0 < Œ∑ ‚àß ‚àÄ (s : set Œ±), Œº s ‚â§ Œ∑ ‚Üí snorm (s.indicator (Œª x, c)) p Œº ‚â§ Œµ :=
begin
  rcases eq_or_ne p 0 with rfl|h'p,
  { exact ‚ü®1, zero_lt_one, Œª s hs, by simp‚ü© },
  have hp‚ÇÄ : 0 < p := bot_lt_iff_ne_bot.2 h'p,
  have hp‚ÇÄ' : 0 ‚â§ 1 / p.to_real := div_nonneg zero_le_one ennreal.to_real_nonneg,
  have hp‚ÇÄ'' : 0 < p.to_real,
  { simpa [‚Üê ennreal.to_real_lt_to_real ennreal.zero_ne_top hp] using hp‚ÇÄ },
  obtain ‚ü®Œ∑, hŒ∑_pos, hŒ∑_le‚ü© : ‚àÉ (Œ∑ : ‚Ñù‚â•0), 0 < Œ∑ ‚àß (‚Äñc‚Äñ‚Çä * Œ∑ ^ (1 / p.to_real) : ‚Ñù‚â•0‚àû) ‚â§ Œµ,
  { have : filter.tendsto (Œª x : ‚Ñù‚â•0, ((‚Äñc‚Äñ‚Çä * x ^ (1 / p.to_real) : ‚Ñù‚â•0) : ‚Ñù‚â•0‚àû))
      (ùìù 0) (ùìù (0 : ‚Ñù‚â•0)),
    { rw ennreal.tendsto_coe,
      convert ((nnreal.continuous_at_rpow_const (or.inr hp‚ÇÄ')).tendsto).const_mul _,
      simp [hp‚ÇÄ''.ne'] },
    have hŒµ' : 0 < Œµ := hŒµ.bot_lt,
    obtain ‚ü®Œ¥, hŒ¥, hŒ¥Œµ'‚ü© :=
      nnreal.nhds_zero_basis.eventually_iff.mp (eventually_le_of_tendsto_lt hŒµ' this),
    obtain ‚ü®Œ∑, hŒ∑, hŒ∑Œ¥‚ü© := exists_between hŒ¥,
    refine ‚ü®Œ∑, hŒ∑, _‚ü©,
    rw [ennreal.coe_rpow_of_nonneg _ hp‚ÇÄ', ‚Üê ennreal.coe_mul],
    exact hŒ¥Œµ' hŒ∑Œ¥ },
  refine ‚ü®Œ∑, hŒ∑_pos, Œª s hs, _‚ü©,
  refine (snorm_indicator_const_le _ _).trans (le_trans _ hŒ∑_le),
  exact mul_le_mul_left' (ennreal.rpow_le_rpow hs hp‚ÇÄ') _,
end

end indicator

section indicator_const_Lp

open set function

variables {s : set Œ±} {hs : measurable_set s} {hŒºs : Œº s ‚â† ‚àû} {c : E}

/-- Indicator of a set as an element of `Lp`. -/
def indicator_const_Lp (p : ‚Ñù‚â•0‚àû) (hs : measurable_set s) (hŒºs : Œº s ‚â† ‚àû) (c : E) : Lp E p Œº :=
mem_‚Ñíp.to_Lp (s.indicator (Œª _, c)) (mem_‚Ñíp_indicator_const p hs c (or.inr hŒºs))

lemma indicator_const_Lp_coe_fn : ‚áë(indicator_const_Lp p hs hŒºs c) =·µê[Œº] s.indicator (Œª _, c) :=
mem_‚Ñíp.coe_fn_to_Lp (mem_‚Ñíp_indicator_const p hs c (or.inr hŒºs))

lemma indicator_const_Lp_coe_fn_mem :
  ‚àÄ·µê (x : Œ±) ‚àÇŒº, x ‚àà s ‚Üí indicator_const_Lp p hs hŒºs c x = c :=
indicator_const_Lp_coe_fn.mono (Œª x hx hxs, hx.trans (set.indicator_of_mem hxs _))

lemma indicator_const_Lp_coe_fn_nmem :
  ‚àÄ·µê (x : Œ±) ‚àÇŒº, x ‚àâ s ‚Üí indicator_const_Lp p hs hŒºs c x = 0 :=
indicator_const_Lp_coe_fn.mono (Œª x hx hxs, hx.trans (set.indicator_of_not_mem hxs _))

lemma norm_indicator_const_Lp (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
  ‚Äñindicator_const_Lp p hs hŒºs c‚Äñ = ‚Äñc‚Äñ * (Œº s).to_real ^ (1 / p.to_real) :=
by rw [Lp.norm_def, snorm_congr_ae indicator_const_Lp_coe_fn,
    snorm_indicator_const hs hp_ne_zero hp_ne_top, ennreal.to_real_mul, ennreal.to_real_rpow,
    ennreal.coe_to_real, coe_nnnorm]

lemma norm_indicator_const_Lp_top (hŒºs_ne_zero : Œº s ‚â† 0) : ‚Äñindicator_const_Lp ‚àû hs hŒºs c‚Äñ = ‚Äñc‚Äñ :=
by rw [Lp.norm_def, snorm_congr_ae indicator_const_Lp_coe_fn,
    snorm_indicator_const' hs hŒºs_ne_zero ennreal.top_ne_zero, ennreal.top_to_real, div_zero,
    ennreal.rpow_zero, mul_one, ennreal.coe_to_real, coe_nnnorm]

lemma norm_indicator_const_Lp' (hp_pos : p ‚â† 0) (hŒºs_pos : Œº s ‚â† 0) :
  ‚Äñindicator_const_Lp p hs hŒºs c‚Äñ = ‚Äñc‚Äñ * (Œº s).to_real ^ (1 / p.to_real) :=
begin
  by_cases hp_top : p = ‚àû,
  { rw [hp_top, ennreal.top_to_real, div_zero, real.rpow_zero, mul_one],
    exact norm_indicator_const_Lp_top hŒºs_pos, },
  { exact norm_indicator_const_Lp hp_pos hp_top, },
end

@[simp] lemma indicator_const_empty :
  indicator_const_Lp p measurable_set.empty (by simp : Œº ‚àÖ ‚â† ‚àû) c = 0 :=
begin
  rw Lp.eq_zero_iff_ae_eq_zero,
  convert indicator_const_Lp_coe_fn,
  simp [set.indicator_empty'],
end

lemma mem_‚Ñíp_add_of_disjoint {f g : Œ± ‚Üí E}
  (h : disjoint (support f) (support g)) (hf : strongly_measurable f) (hg : strongly_measurable g) :
  mem_‚Ñíp (f + g) p Œº ‚Üî mem_‚Ñíp f p Œº ‚àß mem_‚Ñíp g p Œº :=
begin
  borelize E,
  refine ‚ü®Œª hfg, ‚ü®_, _‚ü©, Œª h, h.1.add h.2‚ü©,
  { rw ‚Üê indicator_add_eq_left h, exact hfg.indicator (measurable_set_support hf.measurable) },
  { rw ‚Üê indicator_add_eq_right h, exact hfg.indicator (measurable_set_support hg.measurable) }
end

/-- The indicator of a disjoint union of two sets is the sum of the indicators of the sets. -/
lemma indicator_const_Lp_disjoint_union {s t : set Œ±} (hs : measurable_set s)
  (ht : measurable_set t) (hŒºs : Œº s ‚â† ‚àû) (hŒºt : Œº t ‚â† ‚àû) (hst : s ‚à© t = ‚àÖ) (c : E) :
  (indicator_const_Lp p (hs.union ht) ((measure_union_le s t).trans_lt
      (lt_top_iff_ne_top.mpr (ennreal.add_ne_top.mpr ‚ü®hŒºs, hŒºt‚ü©))).ne c)
    = indicator_const_Lp p hs hŒºs c + indicator_const_Lp p ht hŒºt c :=
begin
  ext1,
  refine indicator_const_Lp_coe_fn.trans (eventually_eq.trans _ (Lp.coe_fn_add _ _).symm),
  refine eventually_eq.trans _
    (eventually_eq.add indicator_const_Lp_coe_fn.symm indicator_const_Lp_coe_fn.symm),
  rw set.indicator_union_of_disjoint (set.disjoint_iff_inter_eq_empty.mpr hst) _,
end

end indicator_const_Lp

lemma mem_‚Ñíp.norm_rpow_div {f : Œ± ‚Üí E}
  (hf : mem_‚Ñíp f p Œº) (q : ‚Ñù‚â•0‚àû) :
  mem_‚Ñíp (Œª (x : Œ±), ‚Äñf x‚Äñ ^ q.to_real) (p/q) Œº :=
begin
  refine ‚ü®(hf.1.norm.ae_measurable.pow_const q.to_real).ae_strongly_measurable, _‚ü©,
  by_cases q_top : q = ‚àû, { simp [q_top] },
  by_cases q_zero : q = 0,
  { simp [q_zero],
    by_cases p_zero : p = 0, { simp [p_zero] },
    rw ennreal.div_zero p_zero,
    exact (mem_‚Ñíp_top_const (1 : ‚Ñù)).2 },
  rw snorm_norm_rpow _ (ennreal.to_real_pos q_zero q_top),
  apply ennreal.rpow_lt_top_of_nonneg ennreal.to_real_nonneg,
  rw [ennreal.of_real_to_real q_top, div_eq_mul_inv, mul_assoc,
    ennreal.inv_mul_cancel q_zero q_top, mul_one],
  exact hf.2.ne
end

lemma mem_‚Ñíp_norm_rpow_iff {q : ‚Ñù‚â•0‚àû} {f : Œ± ‚Üí E} (hf : ae_strongly_measurable f Œº)
  (q_zero : q ‚â† 0) (q_top : q ‚â† ‚àû) :
  mem_‚Ñíp (Œª (x : Œ±), ‚Äñf x‚Äñ ^ q.to_real) (p/q) Œº ‚Üî mem_‚Ñíp f p Œº :=
begin
  refine ‚ü®Œª h, _, Œª h, h.norm_rpow_div q‚ü©,
  apply (mem_‚Ñíp_norm_iff hf).1,
  convert h.norm_rpow_div (q‚Åª¬π),
  { ext x,
    rw [real.norm_eq_abs, real.abs_rpow_of_nonneg (norm_nonneg _), ‚Üê real.rpow_mul (abs_nonneg _),
      ennreal.to_real_inv, mul_inv_cancel, abs_of_nonneg (norm_nonneg _), real.rpow_one],
    simp [ennreal.to_real_eq_zero_iff, not_or_distrib, q_zero, q_top] },
  { rw [div_eq_mul_inv, inv_inv, div_eq_mul_inv, mul_assoc, ennreal.inv_mul_cancel q_zero q_top,
    mul_one] }
end

lemma mem_‚Ñíp.norm_rpow {f : Œ± ‚Üí E}
  (hf : mem_‚Ñíp f p Œº) (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
  mem_‚Ñíp (Œª (x : Œ±), ‚Äñf x‚Äñ ^ p.to_real) 1 Œº :=
begin
  convert hf.norm_rpow_div p,
  rw [div_eq_mul_inv, ennreal.mul_inv_cancel hp_ne_zero hp_ne_top],
end

end measure_theory

open measure_theory

/-!
### Composition on `L^p`

We show that Lipschitz functions vanishing at zero act by composition on `L^p`, and specialize
this to the composition with continuous linear maps, and to the definition of the positive
part of an `L^p` function.
-/

section composition

variables {g : E ‚Üí F} {c : ‚Ñù‚â•0}

lemma lipschitz_with.comp_mem_‚Ñíp {Œ± E F} {K} [measurable_space Œ±] {Œº : measure Œ±}
  [normed_add_comm_group E] [normed_add_comm_group F] {f : Œ± ‚Üí E} {g : E ‚Üí F}
  (hg : lipschitz_with K g) (g0 : g 0 = 0) (hL : mem_‚Ñíp f p Œº) : mem_‚Ñíp (g ‚àò f) p Œº  :=
begin
  have : ‚àÄ x, ‚Äñg (f x)‚Äñ ‚â§ K * ‚Äñf x‚Äñ,
  { intro a,
    -- TODO: add `lipschitz_with.nnnorm_sub_le` and `lipschitz_with.nnnorm_le`
    simpa [g0] using hg.norm_sub_le (f a) 0, },
  exact hL.of_le_mul (hg.continuous.comp_ae_strongly_measurable hL.1) (eventually_of_forall this),
end

lemma measure_theory.mem_‚Ñíp.of_comp_antilipschitz_with {Œ± E F} {K'}
  [measurable_space Œ±] {Œº : measure Œ±} [normed_add_comm_group E] [normed_add_comm_group F]
  {f : Œ± ‚Üí E} {g : E ‚Üí F} (hL : mem_‚Ñíp (g ‚àò f) p Œº)
  (hg : uniform_continuous g) (hg' : antilipschitz_with K' g) (g0 : g 0 = 0) : mem_‚Ñíp f p Œº :=
begin
  have A : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ K' * ‚Äñg (f x)‚Äñ,
  { intro x,
    -- TODO: add `antilipschitz_with.le_mul_nnnorm_sub` and `antilipschitz_with.le_mul_norm`
    rw [‚Üê dist_zero_right, ‚Üê dist_zero_right, ‚Üê g0],
    apply hg'.le_mul_dist },
  have B : ae_strongly_measurable f Œº :=
    ((hg'.uniform_embedding hg).embedding.ae_strongly_measurable_comp_iff.1 hL.1),
  exact hL.of_le_mul B (filter.eventually_of_forall A),
end

namespace lipschitz_with

lemma mem_‚Ñíp_comp_iff_of_antilipschitz {Œ± E F} {K K'} [measurable_space Œ±] {Œº : measure Œ±}
  [normed_add_comm_group E] [normed_add_comm_group F]
  {f : Œ± ‚Üí E} {g : E ‚Üí F} (hg : lipschitz_with K g) (hg' : antilipschitz_with K' g) (g0 : g 0 = 0) :
  mem_‚Ñíp (g ‚àò f) p Œº ‚Üî mem_‚Ñíp f p Œº :=
‚ü®Œª h, h.of_comp_antilipschitz_with hg.uniform_continuous hg' g0, Œª h, hg.comp_mem_‚Ñíp g0 h‚ü©

/-- When `g` is a Lipschitz function sending `0` to `0` and `f` is in `Lp`, then `g ‚àò f` is well
defined as an element of `Lp`. -/
def comp_Lp (hg : lipschitz_with c g) (g0 : g 0 = 0) (f : Lp E p Œº) : Lp F p Œº :=
‚ü®ae_eq_fun.comp g hg.continuous (f : Œ± ‚Üí‚Çò[Œº] E),
begin
  suffices : ‚àÄ·µê x ‚àÇŒº, ‚Äñae_eq_fun.comp g hg.continuous (f : Œ± ‚Üí‚Çò[Œº] E) x‚Äñ ‚â§ c * ‚Äñf x‚Äñ,
  { exact Lp.mem_Lp_of_ae_le_mul this },
  filter_upwards [ae_eq_fun.coe_fn_comp g hg.continuous (f : Œ± ‚Üí‚Çò[Œº] E)] with a ha,
  simp only [ha],
  rw [‚Üê dist_zero_right, ‚Üê dist_zero_right, ‚Üê g0],
  exact hg.dist_le_mul (f a) 0,
end‚ü©

lemma coe_fn_comp_Lp (hg : lipschitz_with c g) (g0 : g 0 = 0) (f : Lp E p Œº) :
  hg.comp_Lp g0 f =·µê[Œº] g ‚àò f :=
ae_eq_fun.coe_fn_comp _ _ _

@[simp] lemma comp_Lp_zero (hg : lipschitz_with c g) (g0 : g 0 = 0) :
  hg.comp_Lp g0 (0 : Lp E p Œº) = 0 :=
begin
  rw Lp.eq_zero_iff_ae_eq_zero,
  apply (coe_fn_comp_Lp _ _ _).trans,
  filter_upwards [Lp.coe_fn_zero E p Œº] with _ ha,
  simp [ha, g0],
end

lemma norm_comp_Lp_sub_le (hg : lipschitz_with c g) (g0 : g 0 = 0) (f f' : Lp E p Œº) :
  ‚Äñhg.comp_Lp g0 f - hg.comp_Lp g0 f'‚Äñ ‚â§ c * ‚Äñf - f'‚Äñ :=
begin
  apply Lp.norm_le_mul_norm_of_ae_le_mul,
  filter_upwards [hg.coe_fn_comp_Lp g0 f, hg.coe_fn_comp_Lp g0 f',
    Lp.coe_fn_sub (hg.comp_Lp g0 f) (hg.comp_Lp g0 f'), Lp.coe_fn_sub f f'] with a ha1 ha2 ha3 ha4,
  simp [ha1, ha2, ha3, ha4, ‚Üê dist_eq_norm],
  exact hg.dist_le_mul (f a) (f' a)
end

lemma norm_comp_Lp_le (hg : lipschitz_with c g) (g0 : g 0 = 0) (f : Lp E p Œº) :
  ‚Äñhg.comp_Lp g0 f‚Äñ ‚â§ c * ‚Äñf‚Äñ :=
by simpa using hg.norm_comp_Lp_sub_le g0 f 0

lemma lipschitz_with_comp_Lp [fact (1 ‚â§ p)] (hg : lipschitz_with c g) (g0 : g 0 = 0) :
  lipschitz_with c (hg.comp_Lp g0 : Lp E p Œº ‚Üí Lp F p Œº) :=
lipschitz_with.of_dist_le_mul $ Œª f g, by simp [dist_eq_norm, norm_comp_Lp_sub_le]

lemma continuous_comp_Lp [fact (1 ‚â§ p)] (hg : lipschitz_with c g) (g0 : g 0 = 0) :
  continuous (hg.comp_Lp g0 : Lp E p Œº ‚Üí Lp F p Œº) :=
(lipschitz_with_comp_Lp hg g0).continuous

end lipschitz_with

namespace continuous_linear_map
variables {ùïú : Type*} [nontrivially_normed_field ùïú] [normed_space ùïú E] [normed_space ùïú F]

/-- Composing `f : Lp ` with `L : E ‚ÜíL[ùïú] F`. -/
def comp_Lp (L : E ‚ÜíL[ùïú] F) (f : Lp E p Œº) : Lp F p Œº :=
L.lipschitz.comp_Lp (map_zero L) f

lemma coe_fn_comp_Lp (L : E ‚ÜíL[ùïú] F) (f : Lp E p Œº) :
  ‚àÄ·µê a ‚àÇŒº, (L.comp_Lp f) a = L (f a) :=
lipschitz_with.coe_fn_comp_Lp _ _ _

lemma coe_fn_comp_Lp' (L : E ‚ÜíL[ùïú] F) (f : Lp E p Œº) :
  L.comp_Lp f =·µê[Œº] Œª a, L (f a) :=
L.coe_fn_comp_Lp f

lemma comp_mem_‚Ñíp (L : E ‚ÜíL[ùïú] F) (f : Lp E p Œº) : mem_‚Ñíp (L ‚àò f) p Œº :=
(Lp.mem_‚Ñíp (L.comp_Lp f)).ae_eq (L.coe_fn_comp_Lp' f)

lemma comp_mem_‚Ñíp' (L : E ‚ÜíL[ùïú] F) {f : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) : mem_‚Ñíp (L ‚àò f) p Œº :=
(L.comp_mem_‚Ñíp (hf.to_Lp f)).ae_eq (eventually_eq.fun_comp (hf.coe_fn_to_Lp) _)

section is_R_or_C

variables {K : Type*} [is_R_or_C K]

lemma _root_.measure_theory.mem_‚Ñíp.of_real
  {f : Œ± ‚Üí ‚Ñù} (hf : mem_‚Ñíp f p Œº) : mem_‚Ñíp (Œª x, (f x : K)) p Œº :=
(@is_R_or_C.of_real_clm K _).comp_mem_‚Ñíp' hf

lemma _root_.measure_theory.mem_‚Ñíp_re_im_iff {f : Œ± ‚Üí K} :
  mem_‚Ñíp (Œª x, is_R_or_C.re (f x)) p Œº ‚àß mem_‚Ñíp (Œª x, is_R_or_C.im (f x)) p Œº ‚Üî
  mem_‚Ñíp f p Œº :=
begin
  refine ‚ü®_, Œª hf, ‚ü®hf.re, hf.im‚ü©‚ü©,
  rintro ‚ü®hre, him‚ü©,
  convert hre.of_real.add (him.of_real.const_mul is_R_or_C.I),
  { ext1 x,
    rw [pi.add_apply, mul_comm, is_R_or_C.re_add_im] },
  all_goals { apply_instance }
end

end is_R_or_C

lemma add_comp_Lp (L L' : E ‚ÜíL[ùïú] F) (f : Lp E p Œº) :
  (L + L').comp_Lp f = L.comp_Lp f + L'.comp_Lp f :=
begin
  ext1,
  refine (coe_fn_comp_Lp' (L + L') f).trans _,
  refine eventually_eq.trans _ (Lp.coe_fn_add _ _).symm,
  refine eventually_eq.trans _
    (eventually_eq.add (L.coe_fn_comp_Lp' f).symm (L'.coe_fn_comp_Lp' f).symm),
  refine eventually_of_forall (Œª x, _),
  refl,
end

lemma smul_comp_Lp {ùïú'} [normed_ring ùïú'] [module ùïú' F] [has_bounded_smul ùïú' F]
  [smul_comm_class ùïú ùïú' F]
  (c : ùïú') (L : E ‚ÜíL[ùïú] F) (f : Lp E p Œº) :
  (c ‚Ä¢ L).comp_Lp f = c ‚Ä¢ L.comp_Lp f :=
begin
  ext1,
  refine (coe_fn_comp_Lp' (c ‚Ä¢ L) f).trans _,
  refine eventually_eq.trans _ (Lp.coe_fn_smul _ _).symm,
  refine (L.coe_fn_comp_Lp' f).mono (Œª x hx, _),
  rw [pi.smul_apply, hx],
  refl,
end

lemma norm_comp_Lp_le (L : E ‚ÜíL[ùïú] F) (f : Lp E p Œº)  : ‚ÄñL.comp_Lp f‚Äñ ‚â§ ‚ÄñL‚Äñ * ‚Äñf‚Äñ :=
lipschitz_with.norm_comp_Lp_le _ _ _

variables (Œº p)

/-- Composing `f : Lp E p Œº` with `L : E ‚ÜíL[ùïú] F`, seen as a `ùïú`-linear map on `Lp E p Œº`. -/
def comp_Lp‚Çó (L : E ‚ÜíL[ùïú] F) : (Lp E p Œº) ‚Üí‚Çó[ùïú] (Lp F p Œº) :=
{ to_fun := Œª f, L.comp_Lp f,
  map_add' := begin
    intros f g,
    ext1,
    filter_upwards [Lp.coe_fn_add f g, coe_fn_comp_Lp L (f + g), coe_fn_comp_Lp L f,
      coe_fn_comp_Lp L g, Lp.coe_fn_add (L.comp_Lp f) (L.comp_Lp g)],
    assume a ha1 ha2 ha3 ha4 ha5,
    simp only [ha1, ha2, ha3, ha4, ha5, map_add, pi.add_apply],
  end,
  map_smul' := begin
    intros c f,
    dsimp,
    ext1,
    filter_upwards [Lp.coe_fn_smul c f, coe_fn_comp_Lp L (c ‚Ä¢ f), Lp.coe_fn_smul c (L.comp_Lp f),
      coe_fn_comp_Lp L f] with _ ha1 ha2 ha3 ha4,
    simp only [ha1, ha2, ha3, ha4, smul_hom_class.map_smul, pi.smul_apply],
  end }

/-- Composing `f : Lp E p Œº` with `L : E ‚ÜíL[ùïú] F`, seen as a continuous `ùïú`-linear map on
`Lp E p Œº`. See also the similar
* `linear_map.comp_left` for functions,
* `continuous_linear_map.comp_left_continuous` for continuous functions,
* `continuous_linear_map.comp_left_continuous_bounded` for bounded continuous functions,
* `continuous_linear_map.comp_left_continuous_compact` for continuous functions on compact spaces.
-/
def comp_LpL [fact (1 ‚â§ p)] (L : E ‚ÜíL[ùïú] F) : (Lp E p Œº) ‚ÜíL[ùïú] (Lp F p Œº) :=
linear_map.mk_continuous (L.comp_Lp‚Çó p Œº) ‚ÄñL‚Äñ L.norm_comp_Lp_le

variables {Œº p}

lemma coe_fn_comp_LpL [fact (1 ‚â§ p)] (L : E ‚ÜíL[ùïú] F) (f : Lp E p Œº) :
  L.comp_LpL p Œº f =·µê[Œº] Œª a, L (f a) :=
L.coe_fn_comp_Lp f

lemma add_comp_LpL [fact (1 ‚â§ p)] (L L' : E ‚ÜíL[ùïú] F) :
  (L + L').comp_LpL p Œº = L.comp_LpL p Œº + L'.comp_LpL p Œº :=
by { ext1 f, exact add_comp_Lp L L' f }

lemma smul_comp_LpL [fact (1 ‚â§ p)] {ùïú'} [normed_ring ùïú'] [module ùïú' F]
  [has_bounded_smul ùïú' F] [smul_comm_class ùïú ùïú' F] (c : ùïú') (L : E ‚ÜíL[ùïú] F) :
  (c ‚Ä¢ L).comp_LpL p Œº = c ‚Ä¢ L.comp_LpL p Œº :=
by { ext1 f, exact smul_comp_Lp c L f }

lemma norm_compLpL_le [fact (1 ‚â§ p)] (L : E ‚ÜíL[ùïú] F) :
  ‚ÄñL.comp_LpL p Œº‚Äñ ‚â§ ‚ÄñL‚Äñ :=
linear_map.mk_continuous_norm_le _ (norm_nonneg _) _

end continuous_linear_map

namespace measure_theory

lemma indicator_const_Lp_eq_to_span_singleton_comp_Lp {s : set Œ±} [normed_space ‚Ñù F]
  (hs : measurable_set s) (hŒºs : Œº s ‚â† ‚àû) (x : F) :
  indicator_const_Lp 2 hs hŒºs x =
    (continuous_linear_map.to_span_singleton ‚Ñù x).comp_Lp (indicator_const_Lp 2 hs hŒºs (1 : ‚Ñù)) :=
begin
  ext1,
  refine indicator_const_Lp_coe_fn.trans _,
  have h_comp_Lp := (continuous_linear_map.to_span_singleton ‚Ñù x).coe_fn_comp_Lp
    (indicator_const_Lp 2 hs hŒºs (1 : ‚Ñù)),
  rw ‚Üê eventually_eq at h_comp_Lp,
  refine eventually_eq.trans _ h_comp_Lp.symm,
  refine (@indicator_const_Lp_coe_fn _ _ _ 2 Œº _ s hs hŒºs (1 : ‚Ñù)).mono (Œª y hy, _),
  dsimp only,
  rw hy,
  simp_rw [continuous_linear_map.to_span_singleton_apply],
  by_cases hy_mem : y ‚àà s; simp [hy_mem, continuous_linear_map.lsmul_apply],
end

namespace Lp
section pos_part

lemma lipschitz_with_pos_part : lipschitz_with 1 (Œª (x : ‚Ñù), max x 0) :=
lipschitz_with.of_dist_le_mul $ Œª x y, by simp [real.dist_eq, abs_max_sub_max_le_abs]

lemma _root_.measure_theory.mem_‚Ñíp.pos_part {f : Œ± ‚Üí ‚Ñù} (hf : mem_‚Ñíp f p Œº) :
  mem_‚Ñíp (Œª x, max (f x) 0) p Œº :=
lipschitz_with_pos_part.comp_mem_‚Ñíp  (max_eq_right le_rfl) hf

lemma _root_.measure_theory.mem_‚Ñíp.neg_part {f : Œ± ‚Üí ‚Ñù} (hf : mem_‚Ñíp f p Œº) :
  mem_‚Ñíp (Œª x, max (-f x) 0) p Œº :=
lipschitz_with_pos_part.comp_mem_‚Ñíp (max_eq_right le_rfl) hf.neg

/-- Positive part of a function in `L^p`. -/
def pos_part (f : Lp ‚Ñù p Œº) : Lp ‚Ñù p Œº :=
lipschitz_with_pos_part.comp_Lp (max_eq_right le_rfl) f

/-- Negative part of a function in `L^p`. -/
def neg_part (f : Lp ‚Ñù p Œº) : Lp ‚Ñù p Œº := pos_part (-f)

@[norm_cast]
lemma coe_pos_part (f : Lp ‚Ñù p Œº) : (pos_part f : Œ± ‚Üí‚Çò[Œº] ‚Ñù) = (f : Œ± ‚Üí‚Çò[Œº] ‚Ñù).pos_part := rfl

lemma coe_fn_pos_part (f : Lp ‚Ñù p Œº) : ‚áë(pos_part f) =·µê[Œº] Œª a, max (f a) 0 :=
ae_eq_fun.coe_fn_pos_part _

lemma coe_fn_neg_part_eq_max (f : Lp ‚Ñù p Œº) : ‚àÄ·µê a ‚àÇŒº, neg_part f a = max (- f a) 0 :=
begin
  rw neg_part,
  filter_upwards [coe_fn_pos_part (-f), coe_fn_neg f] with _ h‚ÇÅ h‚ÇÇ,
  rw [h‚ÇÅ, h‚ÇÇ, pi.neg_apply],
end

lemma coe_fn_neg_part (f : Lp ‚Ñù p Œº) : ‚àÄ·µê a ‚àÇŒº, neg_part f a = - min (f a) 0 :=
(coe_fn_neg_part_eq_max f).mono $ assume a h,
by rw [h, ‚Üê max_neg_neg, neg_zero]

lemma continuous_pos_part [fact (1 ‚â§ p)] : continuous (Œªf : Lp ‚Ñù p Œº, pos_part f) :=
lipschitz_with.continuous_comp_Lp _ _

lemma continuous_neg_part [fact (1 ‚â§ p)] : continuous (Œªf : Lp ‚Ñù p Œº, neg_part f) :=
have eq : (Œªf : Lp ‚Ñù p Œº, neg_part f) = (Œªf : Lp ‚Ñù p Œº, pos_part (-f)) := rfl,
by { rw eq, exact continuous_pos_part.comp continuous_neg }

end pos_part
end Lp
end measure_theory

end composition



/-!
## `L^p` is a complete space

We show that `L^p` is a complete space for `1 ‚â§ p`.
-/

section complete_space

namespace measure_theory
namespace Lp

lemma snorm'_lim_eq_lintegral_liminf {Œπ} [nonempty Œπ] [linear_order Œπ] {f : Œπ ‚Üí Œ± ‚Üí G} {p : ‚Ñù}
  (hp_nonneg : 0 ‚â§ p) {f_lim : Œ± ‚Üí G}
  (h_lim : ‚àÄ·µê (x : Œ±) ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (f_lim x))) :
  snorm' f_lim p Œº = (‚à´‚Åª a, at_top.liminf (Œª m, (‚Äñf m a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p) ‚àÇŒº) ^ (1/p) :=
begin
  suffices h_no_pow : (‚à´‚Åª a, ‚Äñf_lim a‚Äñ‚Çä ^ p ‚àÇŒº)
    = (‚à´‚Åª a, at_top.liminf (Œª m, (‚Äñf m a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p) ‚àÇŒº),
  { rw [snorm', h_no_pow], },
  refine lintegral_congr_ae (h_lim.mono (Œª a ha, _)),
  rw tendsto.liminf_eq,
  simp_rw [ennreal.coe_rpow_of_nonneg _ hp_nonneg, ennreal.tendsto_coe],
  refine ((nnreal.continuous_rpow_const hp_nonneg).tendsto (‚Äñf_lim a‚Äñ‚Çä)).comp _,
  exact (continuous_nnnorm.tendsto (f_lim a)).comp ha,
end

lemma snorm'_lim_le_liminf_snorm' {E} [normed_add_comm_group E] {f : ‚Ñï ‚Üí Œ± ‚Üí E} {p : ‚Ñù}
  (hp_pos : 0 < p) (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) {f_lim : Œ± ‚Üí E}
  (h_lim : ‚àÄ·µê (x : Œ±) ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (f_lim x)))  :
  snorm' f_lim p Œº ‚â§ at_top.liminf (Œª n, snorm' (f n) p Œº) :=
begin
  rw snorm'_lim_eq_lintegral_liminf hp_pos.le h_lim,
  rw [‚Üêennreal.le_rpow_one_div_iff (by simp [hp_pos] : 0 < 1 / p), one_div_one_div],
  refine (lintegral_liminf_le' (Œª m, ((hf m).ennnorm.pow_const _))).trans_eq _,
  have h_pow_liminf : at_top.liminf (Œª n, snorm' (f n) p Œº) ^ p
    = at_top.liminf (Œª n, (snorm' (f n) p Œº) ^ p),
  { have h_rpow_mono := ennreal.strict_mono_rpow_of_pos hp_pos,
    have h_rpow_surj := (ennreal.rpow_left_bijective hp_pos.ne.symm).2,
    refine (h_rpow_mono.order_iso_of_surjective _ h_rpow_surj).liminf_apply _ _ _ _,
    all_goals { is_bounded_default }, },
  rw h_pow_liminf,
  simp_rw [snorm', ‚Üê ennreal.rpow_mul, one_div, inv_mul_cancel hp_pos.ne.symm, ennreal.rpow_one],
end

lemma snorm_exponent_top_lim_eq_ess_sup_liminf {Œπ} [nonempty Œπ] [linear_order Œπ] {f : Œπ ‚Üí Œ± ‚Üí G}
  {f_lim : Œ± ‚Üí G}
  (h_lim : ‚àÄ·µê (x : Œ±) ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (f_lim x))) :
  snorm f_lim ‚àû Œº = ess_sup (Œª x, at_top.liminf (Œª m, (‚Äñf m x‚Äñ‚Çä : ‚Ñù‚â•0‚àû))) Œº :=
begin
  rw [snorm_exponent_top, snorm_ess_sup],
  refine ess_sup_congr_ae (h_lim.mono (Œª x hx, _)),
  rw tendsto.liminf_eq,
  rw ennreal.tendsto_coe,
  exact (continuous_nnnorm.tendsto (f_lim x)).comp hx,
end

lemma snorm_exponent_top_lim_le_liminf_snorm_exponent_top {Œπ} [nonempty Œπ] [countable Œπ]
  [linear_order Œπ] {f : Œπ ‚Üí Œ± ‚Üí F} {f_lim : Œ± ‚Üí F}
  (h_lim : ‚àÄ·µê (x : Œ±) ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (f_lim x))) :
  snorm f_lim ‚àû Œº ‚â§ at_top.liminf (Œª n, snorm (f n) ‚àû Œº) :=
begin
  rw snorm_exponent_top_lim_eq_ess_sup_liminf h_lim,
  simp_rw [snorm_exponent_top, snorm_ess_sup],
  exact ennreal.ess_sup_liminf_le (Œª n, (Œª x, (‚Äñf n x‚Äñ‚Çä : ‚Ñù‚â•0‚àû))),
end

lemma snorm_lim_le_liminf_snorm {E} [normed_add_comm_group E]
  {f : ‚Ñï ‚Üí Œ± ‚Üí E} (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) (f_lim : Œ± ‚Üí E)
  (h_lim : ‚àÄ·µê (x : Œ±) ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (f_lim x))) :
  snorm f_lim p Œº ‚â§ at_top.liminf (Œª n, snorm (f n) p Œº) :=
begin
  by_cases hp0 : p = 0,
  { simp [hp0], },
  rw ‚Üê ne.def at hp0,
  by_cases hp_top : p = ‚àû,
  { simp_rw [hp_top],
    exact snorm_exponent_top_lim_le_liminf_snorm_exponent_top h_lim, },
  simp_rw snorm_eq_snorm' hp0 hp_top,
  have hp_pos : 0 < p.to_real, from ennreal.to_real_pos hp0 hp_top,
  exact snorm'_lim_le_liminf_snorm' hp_pos hf h_lim,
end

/-! ### `Lp` is complete iff Cauchy sequences of `‚Ñíp` have limits in `‚Ñíp` -/

lemma tendsto_Lp_iff_tendsto_‚Ñíp' {Œπ} {fi : filter Œπ} [fact (1 ‚â§ p)]
  (f : Œπ ‚Üí Lp E p Œº) (f_lim : Lp E p Œº) :
  fi.tendsto f (ùìù f_lim) ‚Üî fi.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0) :=
begin
  rw tendsto_iff_dist_tendsto_zero,
  simp_rw dist_def,
  rw [‚Üê ennreal.zero_to_real, ennreal.tendsto_to_real_iff (Œª n, _) ennreal.zero_ne_top],
  rw snorm_congr_ae (Lp.coe_fn_sub _ _).symm,
  exact Lp.snorm_ne_top _,
end

lemma tendsto_Lp_iff_tendsto_‚Ñíp {Œπ} {fi : filter Œπ} [fact (1 ‚â§ p)]
  (f : Œπ ‚Üí Lp E p Œº) (f_lim : Œ± ‚Üí E) (f_lim_‚Ñíp : mem_‚Ñíp f_lim p Œº) :
  fi.tendsto f (ùìù (f_lim_‚Ñíp.to_Lp f_lim)) ‚Üî fi.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0) :=
begin
  rw tendsto_Lp_iff_tendsto_‚Ñíp',
  suffices h_eq : (Œª n, snorm (f n - mem_‚Ñíp.to_Lp f_lim f_lim_‚Ñíp) p Œº)
      = (Œª n, snorm (f n - f_lim) p Œº),
    by rw h_eq,
  exact funext (Œª n, snorm_congr_ae (eventually_eq.rfl.sub (mem_‚Ñíp.coe_fn_to_Lp f_lim_‚Ñíp))),
end

lemma tendsto_Lp_iff_tendsto_‚Ñíp'' {Œπ} {fi : filter Œπ} [fact (1 ‚â§ p)]
  (f : Œπ ‚Üí Œ± ‚Üí E) (f_‚Ñíp : ‚àÄ n, mem_‚Ñíp (f n) p Œº) (f_lim : Œ± ‚Üí E) (f_lim_‚Ñíp : mem_‚Ñíp f_lim p Œº) :
  fi.tendsto (Œª n, (f_‚Ñíp n).to_Lp (f n)) (ùìù (f_lim_‚Ñíp.to_Lp f_lim))
    ‚Üî fi.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0) :=
begin
  convert Lp.tendsto_Lp_iff_tendsto_‚Ñíp' _ _,
  ext1 n,
  apply snorm_congr_ae,
  filter_upwards [((f_‚Ñíp n).sub f_lim_‚Ñíp).coe_fn_to_Lp,
    Lp.coe_fn_sub ((f_‚Ñíp n).to_Lp (f n)) (f_lim_‚Ñíp.to_Lp f_lim)] with _ hx‚ÇÅ hx‚ÇÇ,
  rw ‚Üê hx‚ÇÇ,
  exact hx‚ÇÅ.symm,
end

lemma tendsto_Lp_of_tendsto_‚Ñíp {Œπ} {fi : filter Œπ} [hp : fact (1 ‚â§ p)]
  {f : Œπ ‚Üí Lp E p Œº} (f_lim : Œ± ‚Üí E) (f_lim_‚Ñíp : mem_‚Ñíp f_lim p Œº)
  (h_tendsto : fi.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0)) :
  fi.tendsto f (ùìù (f_lim_‚Ñíp.to_Lp f_lim)) :=
(tendsto_Lp_iff_tendsto_‚Ñíp f f_lim f_lim_‚Ñíp).mpr h_tendsto

lemma cauchy_seq_Lp_iff_cauchy_seq_‚Ñíp {Œπ} [nonempty Œπ] [semilattice_sup Œπ] [hp : fact (1 ‚â§ p)]
  (f : Œπ ‚Üí Lp E p Œº) :
  cauchy_seq f ‚Üî tendsto (Œª (n : Œπ √ó Œπ), snorm (f n.fst - f n.snd) p Œº) at_top (ùìù 0) :=
begin
  simp_rw [cauchy_seq_iff_tendsto_dist_at_top_0, dist_def],
  rw [‚Üê ennreal.zero_to_real, ennreal.tendsto_to_real_iff (Œª n, _) ennreal.zero_ne_top],
  rw snorm_congr_ae (Lp.coe_fn_sub _ _).symm,
  exact snorm_ne_top _,
end

lemma complete_space_Lp_of_cauchy_complete_‚Ñíp [hp : fact (1 ‚â§ p)]
  (H : ‚àÄ (f : ‚Ñï ‚Üí Œ± ‚Üí E) (hf : ‚àÄ n, mem_‚Ñíp (f n) p Œº) (B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) (hB : ‚àë' i, B i < ‚àû)
      (h_cau : ‚àÄ (N n m : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí snorm (f n - f m) p Œº < B N),
    ‚àÉ (f_lim : Œ± ‚Üí E) (hf_lim_meas : mem_‚Ñíp f_lim p Œº),
      at_top.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0)) :
  complete_space (Lp E p Œº) :=
begin
  let B := Œª n : ‚Ñï, ((1:‚Ñù) / 2) ^ n,
  have hB_pos : ‚àÄ n, 0 < B n, from Œª n, pow_pos (div_pos zero_lt_one zero_lt_two) n,
  refine metric.complete_of_convergent_controlled_sequences B hB_pos (Œª f hf, _),
  rsuffices ‚ü®f_lim, hf_lim_meas, h_tendsto‚ü© : ‚àÉ (f_lim : Œ± ‚Üí E) (hf_lim_meas : mem_‚Ñíp f_lim p Œº),
    at_top.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0),
  { exact ‚ü®hf_lim_meas.to_Lp f_lim, tendsto_Lp_of_tendsto_‚Ñíp f_lim hf_lim_meas h_tendsto‚ü©, },
  have hB : summable B, from summable_geometric_two,
  cases hB with M hB,
  let B1 := Œª n, ennreal.of_real (B n),
  have hB1_has : has_sum B1 (ennreal.of_real M),
  { have h_tsum_B1 : ‚àë' i, B1 i = (ennreal.of_real M),
    { change (‚àë' (n : ‚Ñï), ennreal.of_real (B n)) = ennreal.of_real M,
      rw ‚ÜêhB.tsum_eq,
      exact (ennreal.of_real_tsum_of_nonneg (Œª n, le_of_lt (hB_pos n)) hB.summable).symm, },
    have h_sum := (@ennreal.summable _ B1).has_sum,
    rwa h_tsum_B1 at h_sum, },
  have hB1 : ‚àë' i, B1 i < ‚àû, by {rw hB1_has.tsum_eq, exact ennreal.of_real_lt_top, },
  let f1 : ‚Ñï ‚Üí Œ± ‚Üí E := Œª n, f n,
  refine H f1 (Œª n, Lp.mem_‚Ñíp (f n)) B1 hB1 (Œª N n m hn hm, _),
  specialize hf N n m hn hm,
  rw dist_def at hf,
  simp_rw [f1, B1],
  rwa ennreal.lt_of_real_iff_to_real_lt,
  rw snorm_congr_ae (Lp.coe_fn_sub _ _).symm,
  exact Lp.snorm_ne_top _,
end

/-! ### Prove that controlled Cauchy sequences of `‚Ñíp` have limits in `‚Ñíp` -/

private lemma snorm'_sum_norm_sub_le_tsum_of_cauchy_snorm' {f : ‚Ñï ‚Üí Œ± ‚Üí E}
  (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) {p : ‚Ñù} (hp1 : 1 ‚â§ p)
  {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} (h_cau : ‚àÄ (N n m : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí snorm' (f n - f m) p Œº < B N) (n : ‚Ñï) :
  snorm' (Œª x, ‚àë i in finset.range (n + 1), ‚Äñf (i + 1) x - f i x‚Äñ) p Œº ‚â§ ‚àë' i, B i :=
begin
  let f_norm_diff := Œª i x, ‚Äñf (i + 1) x - f i x‚Äñ,
  have hgf_norm_diff : ‚àÄ n, (Œª x, ‚àë i in finset.range (n + 1), ‚Äñf (i + 1) x - f i x‚Äñ)
      = ‚àë i in finset.range (n + 1), f_norm_diff i,
    from Œª n, funext (Œª x, by simp [f_norm_diff]),
  rw hgf_norm_diff,
  refine (snorm'_sum_le (Œª i _, ((hf (i+1)).sub (hf i)).norm) hp1).trans _,
  simp_rw [‚Üêpi.sub_apply, snorm'_norm],
  refine (finset.sum_le_sum _).trans (sum_le_tsum _ (Œª m _, zero_le _) ennreal.summable),
  exact Œª m _, (h_cau m (m + 1) m (nat.le_succ m) (le_refl m)).le,
end

private lemma lintegral_rpow_sum_coe_nnnorm_sub_le_rpow_tsum {f : ‚Ñï ‚Üí Œ± ‚Üí E}
  (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) {p : ‚Ñù} (hp1 : 1 ‚â§ p) {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} (n : ‚Ñï)
  (hn : snorm' (Œª x, ‚àë i in finset.range (n + 1), ‚Äñf (i + 1) x - f i x‚Äñ) p Œº ‚â§ ‚àë' i, B i) :
  ‚à´‚Åª a, (‚àë i in finset.range (n + 1), ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº
    ‚â§ (‚àë' i, B i) ^ p :=
begin
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1,
  rw [‚Üêone_div_one_div p, @ennreal.le_rpow_one_div_iff _ _ (1/p) (by simp [hp_pos]),
    one_div_one_div p],
  simp_rw snorm' at hn,
  have h_nnnorm_nonneg :
    (Œª a, (‚Äñ‚àë i in finset.range (n + 1), ‚Äñf (i + 1) a - f i a‚Äñ‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p)
    = Œª a, (‚àë i in finset.range (n + 1), (‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) ^ p,
  { ext1 a,
    congr,
    simp_rw ‚Üêof_real_norm_eq_coe_nnnorm,
    rw ‚Üêennreal.of_real_sum_of_nonneg,
    { rw real.norm_of_nonneg _,
      exact finset.sum_nonneg (Œª x hx, norm_nonneg _), },
    { exact Œª x hx, norm_nonneg _, }, },
  change (‚à´‚Åª a, (Œª x, ‚Üë‚Äñ‚àë i in finset.range (n + 1), ‚Äñf (i+1) x - f i x‚Äñ‚Äñ‚Çä^p) a ‚àÇŒº)^(1/p)
    ‚â§ ‚àë' i, B i at hn,
  rwa h_nnnorm_nonneg at hn,
end

private lemma lintegral_rpow_tsum_coe_nnnorm_sub_le_tsum {f : ‚Ñï ‚Üí Œ± ‚Üí E}
  (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) {p : ‚Ñù} (hp1 : 1 ‚â§ p) {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû}
  (h : ‚àÄ n, ‚à´‚Åª a, (‚àë i in finset.range (n + 1), ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº
    ‚â§ (‚àë' i, B i) ^ p) :
  (‚à´‚Åª a, (‚àë' i, ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº) ^ (1/p) ‚â§ ‚àë' i, B i :=
begin
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1,
  suffices h_pow : ‚à´‚Åª a, (‚àë' i, ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº ‚â§ (‚àë' i, B i) ^ p,
    by rwa [‚Üêennreal.le_rpow_one_div_iff (by simp [hp_pos] : 0 < 1 / p), one_div_one_div],
  have h_tsum_1 : ‚àÄ g : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû,
      ‚àë' i, g i = at_top.liminf (Œª n, ‚àë i in finset.range (n + 1), g i),
    by { intro g, rw [ennreal.tsum_eq_liminf_sum_nat, ‚Üê liminf_nat_add _ 1], },
  simp_rw h_tsum_1 _,
  rw ‚Üê h_tsum_1,
  have h_liminf_pow : ‚à´‚Åª a, at_top.liminf (Œª n, ‚àë i in finset.range (n + 1),
      (‚Äñf (i + 1) a - f i a‚Äñ‚Çä))^p ‚àÇŒº
    = ‚à´‚Åª a, at_top.liminf (Œª n, (‚àë i in finset.range (n + 1), (‚Äñf (i + 1) a - f i a‚Äñ‚Çä))^p) ‚àÇŒº,
  { refine lintegral_congr (Œª x, _),
    have h_rpow_mono := ennreal.strict_mono_rpow_of_pos (zero_lt_one.trans_le hp1),
    have h_rpow_surj := (ennreal.rpow_left_bijective hp_pos.ne.symm).2,
    refine (h_rpow_mono.order_iso_of_surjective _ h_rpow_surj).liminf_apply _ _ _ _,
    all_goals { is_bounded_default }, },
  rw h_liminf_pow,
  refine (lintegral_liminf_le' _).trans _,
  { exact Œª n, (finset.ae_measurable_sum (finset.range (n+1))
      (Œª i _, ((hf (i+1)).sub (hf i)).ennnorm)).pow_const _, },
  { exact liminf_le_of_frequently_le' (frequently_of_forall h), },
end

private lemma tsum_nnnorm_sub_ae_lt_top
  {f : ‚Ñï ‚Üí Œ± ‚Üí E} (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) {p : ‚Ñù} (hp1 : 1 ‚â§ p) {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû}
  (hB : ‚àë' i, B i ‚â† ‚àû)
  (h : (‚à´‚Åª a, (‚àë' i, ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº) ^ (1/p) ‚â§ ‚àë' i, B i) :
  ‚àÄ·µê x ‚àÇŒº, (‚àë' i, ‚Äñf (i + 1) x - f i x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < ‚àû :=
begin
  have hp_pos : 0 < p := zero_lt_one.trans_le hp1,
  have h_integral : ‚à´‚Åª a, (‚àë' i, ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº < ‚àû,
  { have h_tsum_lt_top : (‚àë' i, B i) ^ p < ‚àû,
      from ennreal.rpow_lt_top_of_nonneg hp_pos.le hB,
    refine lt_of_le_of_lt _ h_tsum_lt_top,
    rwa [‚Üêennreal.le_rpow_one_div_iff (by simp [hp_pos] : 0 < 1 / p), one_div_one_div] at h, },
  have rpow_ae_lt_top : ‚àÄ·µê x ‚àÇŒº, (‚àë' i, ‚Äñf (i + 1) x - f i x‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p < ‚àû,
  { refine ae_lt_top' (ae_measurable.pow_const _ _) h_integral.ne,
    exact ae_measurable.ennreal_tsum (Œª n, ((hf (n+1)).sub (hf n)).ennnorm), },
  refine rpow_ae_lt_top.mono (Œª x hx, _),
  rwa [‚Üêennreal.lt_rpow_one_div_iff hp_pos,
    ennreal.top_rpow_of_pos (by simp [hp_pos] : 0 < 1 / p)] at hx,
end

lemma ae_tendsto_of_cauchy_snorm' [complete_space E] {f : ‚Ñï ‚Üí Œ± ‚Üí E} {p : ‚Ñù}
  (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) (hp1 : 1 ‚â§ p) {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} (hB : ‚àë' i, B i ‚â† ‚àû)
  (h_cau : ‚àÄ (N n m : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí snorm' (f n - f m) p Œº < B N) :
  ‚àÄ·µê x ‚àÇŒº, ‚àÉ l : E, at_top.tendsto (Œª n, f n x) (ùìù l) :=
begin
  have h_summable : ‚àÄ·µê x ‚àÇŒº, summable (Œª (i : ‚Ñï), f (i + 1) x - f i x),
  { have h1 : ‚àÄ n, snorm' (Œª x, ‚àë i in finset.range (n + 1), ‚Äñf (i + 1) x - f i x‚Äñ) p Œº
        ‚â§ ‚àë' i, B i,
      from snorm'_sum_norm_sub_le_tsum_of_cauchy_snorm' hf hp1 h_cau,
    have h2 : ‚àÄ n, ‚à´‚Åª a, (‚àë i in finset.range (n + 1), ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº
        ‚â§ (‚àë' i, B i) ^ p,
      from Œª n, lintegral_rpow_sum_coe_nnnorm_sub_le_rpow_tsum hf hp1 n (h1 n),
    have h3 : (‚à´‚Åª a, (‚àë' i, ‚Äñf (i + 1) a - f i a‚Äñ‚Çä : ‚Ñù‚â•0‚àû)^p ‚àÇŒº) ^ (1/p) ‚â§ ‚àë' i, B i,
      from lintegral_rpow_tsum_coe_nnnorm_sub_le_tsum hf hp1 h2,
    have h4 : ‚àÄ·µê x ‚àÇŒº, (‚àë' i, ‚Äñf (i + 1) x - f i x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < ‚àû,
      from tsum_nnnorm_sub_ae_lt_top hf hp1 hB h3,
    exact h4.mono (Œª x hx, summable_of_summable_nnnorm
      (ennreal.tsum_coe_ne_top_iff_summable.mp (lt_top_iff_ne_top.mp hx))), },
  have h : ‚àÄ·µê x ‚àÇŒº, ‚àÉ l : E,
    at_top.tendsto (Œª n, ‚àë i in finset.range n, (f (i + 1) x - f i x)) (ùìù l),
  { refine h_summable.mono (Œª x hx, _),
    let hx_sum := hx.has_sum.tendsto_sum_nat,
    exact ‚ü®‚àë' i, (f (i + 1) x - f i x), hx_sum‚ü©, },
  refine h.mono (Œª x hx, _),
  cases hx with l hx,
  have h_rw_sum : (Œª n, ‚àë i in finset.range n, (f (i + 1) x - f i x)) = Œª n, f n x - f 0 x,
  { ext1 n,
    change ‚àë (i : ‚Ñï) in finset.range n, ((Œª m, f m x) (i + 1) - (Œª m, f m x) i) = f n x - f 0 x,
    rw finset.sum_range_sub, },
  rw h_rw_sum at hx,
  have hf_rw : (Œª n, f n x) = Œª n, f n x - f 0 x + f 0 x, by { ext1 n, abel, },
  rw hf_rw,
  exact ‚ü®l + f 0 x, tendsto.add_const _ hx‚ü©,
end

lemma ae_tendsto_of_cauchy_snorm [complete_space E] {f : ‚Ñï ‚Üí Œ± ‚Üí E}
  (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº) (hp : 1 ‚â§ p) {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} (hB : ‚àë' i, B i ‚â† ‚àû)
  (h_cau : ‚àÄ (N n m : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí snorm (f n - f m) p Œº < B N) :
  ‚àÄ·µê x ‚àÇŒº, ‚àÉ l : E, at_top.tendsto (Œª n, f n x) (ùìù l) :=
begin
  by_cases hp_top : p = ‚àû,
  { simp_rw [hp_top] at *,
    have h_cau_ae : ‚àÄ·µê x ‚àÇŒº, ‚àÄ N n m, N ‚â§ n ‚Üí N ‚â§ m ‚Üí (‚Äñ(f n - f m) x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < B N,
    { simp_rw ae_all_iff,
      exact Œª N n m hnN hmN, ae_lt_of_ess_sup_lt (h_cau N n m hnN hmN), },
    simp_rw [snorm_exponent_top, snorm_ess_sup] at h_cau,
    refine h_cau_ae.mono (Œª x hx, cauchy_seq_tendsto_of_complete _),
    refine cauchy_seq_of_le_tendsto_0 (Œª n, (B n).to_real) _ _,
    { intros n m N hnN hmN,
      specialize hx N n m hnN hmN,
      rw [dist_eq_norm, ‚Üêennreal.to_real_of_real (norm_nonneg _),
        ennreal.to_real_le_to_real ennreal.of_real_ne_top
        (ennreal.ne_top_of_tsum_ne_top hB N)],
      rw ‚Üêof_real_norm_eq_coe_nnnorm at hx,
      exact hx.le, },
    { rw ‚Üê ennreal.zero_to_real,
      exact tendsto.comp (ennreal.tendsto_to_real ennreal.zero_ne_top)
        (ennreal.tendsto_at_top_zero_of_tsum_ne_top hB), }, },
  have hp1 : 1 ‚â§ p.to_real,
  { rw [‚Üê ennreal.of_real_le_iff_le_to_real hp_top, ennreal.of_real_one],
    exact hp, },
  have h_cau' : ‚àÄ (N n m : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí snorm' (f n - f m) (p.to_real) Œº < B N,
  { intros N n m hn hm,
    specialize h_cau N n m hn hm,
    rwa snorm_eq_snorm' (zero_lt_one.trans_le hp).ne.symm hp_top at h_cau, },
  exact ae_tendsto_of_cauchy_snorm' hf hp1 hB h_cau',
end

lemma cauchy_tendsto_of_tendsto {f : ‚Ñï ‚Üí Œ± ‚Üí E} (hf : ‚àÄ n, ae_strongly_measurable (f n) Œº)
  (f_lim : Œ± ‚Üí E) {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû}
  (hB : ‚àë' i, B i ‚â† ‚àû) (h_cau : ‚àÄ (N n m : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí snorm (f n - f m) p Œº < B N)
  (h_lim : ‚àÄ·µê (x : Œ±) ‚àÇŒº, tendsto (Œª n, f n x) at_top (ùìù (f_lim x))) :
  at_top.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0) :=
begin
  rw ennreal.tendsto_at_top_zero,
  intros Œµ hŒµ,
  have h_B : ‚àÉ (N : ‚Ñï), B N ‚â§ Œµ,
  { suffices h_tendsto_zero : ‚àÉ (N : ‚Ñï), ‚àÄ n : ‚Ñï, N ‚â§ n ‚Üí B n ‚â§ Œµ,
      from ‚ü®h_tendsto_zero.some, h_tendsto_zero.some_spec _ le_rfl‚ü©,
    exact (ennreal.tendsto_at_top_zero.mp (ennreal.tendsto_at_top_zero_of_tsum_ne_top hB))
      Œµ hŒµ, },
  cases h_B with N h_B,
  refine ‚ü®N, Œª n hn, _‚ü©,
  have h_sub : snorm (f n - f_lim) p Œº ‚â§ at_top.liminf (Œª m, snorm (f n - f m) p Œº),
  { refine snorm_lim_le_liminf_snorm (Œª m, (hf n).sub (hf m)) (f n - f_lim) _,
    refine h_lim.mono (Œª x hx, _),
    simp_rw sub_eq_add_neg,
    exact tendsto.add tendsto_const_nhds (tendsto.neg hx), },
  refine h_sub.trans _,
  refine liminf_le_of_frequently_le' (frequently_at_top.mpr _),
  refine Œª N1, ‚ü®max N N1, le_max_right _ _, _‚ü©,
  exact (h_cau N n (max N N1) hn (le_max_left _ _)).le.trans h_B,
end

lemma mem_‚Ñíp_of_cauchy_tendsto (hp : 1 ‚â§ p) {f : ‚Ñï ‚Üí Œ± ‚Üí E} (hf : ‚àÄ n, mem_‚Ñíp (f n) p Œº)
  (f_lim : Œ± ‚Üí E) (h_lim_meas : ae_strongly_measurable f_lim Œº)
  (h_tendsto : at_top.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0)) :
  mem_‚Ñíp f_lim p Œº :=
begin
  refine ‚ü®h_lim_meas, _‚ü©,
  rw ennreal.tendsto_at_top_zero at h_tendsto,
  cases (h_tendsto 1 zero_lt_one) with N h_tendsto_1,
  specialize h_tendsto_1 N (le_refl N),
  have h_add : f_lim = f_lim - f N + f N, by abel,
  rw h_add,
  refine lt_of_le_of_lt (snorm_add_le (h_lim_meas.sub (hf N).1) (hf N).1 hp) _,
  rw ennreal.add_lt_top,
  split,
  { refine lt_of_le_of_lt _ ennreal.one_lt_top,
    have h_neg : f_lim - f N = -(f N - f_lim), by simp,
    rwa [h_neg, snorm_neg], },
  { exact (hf N).2, },
end

lemma cauchy_complete_‚Ñíp [complete_space E] (hp : 1 ‚â§ p)
  {f : ‚Ñï ‚Üí Œ± ‚Üí E} (hf : ‚àÄ n, mem_‚Ñíp (f n) p Œº) {B : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} (hB : ‚àë' i, B i ‚â† ‚àû)
  (h_cau : ‚àÄ (N n m : ‚Ñï), N ‚â§ n ‚Üí N ‚â§ m ‚Üí snorm (f n - f m) p Œº < B N) :
  ‚àÉ (f_lim : Œ± ‚Üí E) (hf_lim_meas : mem_‚Ñíp f_lim p Œº),
    at_top.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0) :=
begin
  obtain ‚ü®f_lim, h_f_lim_meas, h_lim‚ü© : ‚àÉ (f_lim : Œ± ‚Üí E) (hf_lim_meas : strongly_measurable f_lim),
      ‚àÄ·µê x ‚àÇŒº, tendsto (Œª n, f n x) at_top (nhds (f_lim x)),
    from exists_strongly_measurable_limit_of_tendsto_ae (Œª n, (hf n).1)
      (ae_tendsto_of_cauchy_snorm (Œª n, (hf n).1) hp hB h_cau),
  have h_tendsto' : at_top.tendsto (Œª n, snorm (f n - f_lim) p Œº) (ùìù 0),
    from cauchy_tendsto_of_tendsto (Œª m, (hf m).1) f_lim hB h_cau h_lim,
  have h_‚Ñíp_lim : mem_‚Ñíp f_lim p Œº,
    from mem_‚Ñíp_of_cauchy_tendsto hp hf f_lim h_f_lim_meas.ae_strongly_measurable h_tendsto',
  exact ‚ü®f_lim, h_‚Ñíp_lim, h_tendsto'‚ü©,
end

/-! ### `Lp` is complete for `1 ‚â§ p` -/

instance [complete_space E] [hp : fact (1 ‚â§ p)] : complete_space (Lp E p Œº) :=
complete_space_Lp_of_cauchy_complete_‚Ñíp $
  Œª f hf B hB h_cau, cauchy_complete_‚Ñíp hp.elim hf hB.ne h_cau

end Lp
end measure_theory

end complete_space

/-! ### Continuous functions in `Lp` -/

open_locale bounded_continuous_function
open bounded_continuous_function

section

variables [topological_space Œ±] [borel_space Œ±] [second_countable_topology_either Œ± E]
variables (E p Œº)

/-- An additive subgroup of `Lp E p Œº`, consisting of the equivalence classes which contain a
bounded continuous representative. -/
def measure_theory.Lp.bounded_continuous_function : add_subgroup (Lp E p Œº) :=
add_subgroup.add_subgroup_of
  ((continuous_map.to_ae_eq_fun_add_hom Œº).comp (to_continuous_map_add_hom Œ± E)).range
  (Lp E p Œº)

variables {E p Œº}

/-- By definition, the elements of `Lp.bounded_continuous_function E p Œº` are the elements of
`Lp E p Œº` which contain a bounded continuous representative. -/
lemma measure_theory.Lp.mem_bounded_continuous_function_iff {f : (Lp E p Œº)} :
  f ‚àà measure_theory.Lp.bounded_continuous_function E p Œº
    ‚Üî ‚àÉ f‚ÇÄ : (Œ± ‚Üí·µá E), f‚ÇÄ.to_continuous_map.to_ae_eq_fun Œº = (f : Œ± ‚Üí‚Çò[Œº] E) :=
add_subgroup.mem_add_subgroup_of

namespace bounded_continuous_function

variables [is_finite_measure Œº]

/-- A bounded continuous function on a finite-measure space is in `Lp`. -/
lemma mem_Lp (f : Œ± ‚Üí·µá E) :
  f.to_continuous_map.to_ae_eq_fun Œº ‚àà Lp E p Œº :=
begin
  refine Lp.mem_Lp_of_ae_bound (‚Äñf‚Äñ) _,
  filter_upwards [f.to_continuous_map.coe_fn_to_ae_eq_fun Œº] with x _,
  convert f.norm_coe_le_norm x
end

/-- The `Lp`-norm of a bounded continuous function is at most a constant (depending on the measure
of the whole space) times its sup-norm. -/
lemma Lp_nnnorm_le (f : Œ± ‚Üí·µá E) :
  ‚Äñ(‚ü®f.to_continuous_map.to_ae_eq_fun Œº, mem_Lp f‚ü© : Lp E p Œº)‚Äñ‚Çä
  ‚â§ (measure_univ_nnreal Œº) ^ (p.to_real)‚Åª¬π * ‚Äñf‚Äñ‚Çä :=
begin
  apply Lp.nnnorm_le_of_ae_bound,
  refine (f.to_continuous_map.coe_fn_to_ae_eq_fun Œº).mono _,
  intros x hx,
  rw [‚Üênnreal.coe_le_coe, coe_nnnorm, coe_nnnorm],
  convert f.norm_coe_le_norm x,
end

/-- The `Lp`-norm of a bounded continuous function is at most a constant (depending on the measure
of the whole space) times its sup-norm. -/
lemma Lp_norm_le (f : Œ± ‚Üí·µá E) :
  ‚Äñ(‚ü®f.to_continuous_map.to_ae_eq_fun Œº, mem_Lp f‚ü© : Lp E p Œº)‚Äñ
  ‚â§ (measure_univ_nnreal Œº) ^ (p.to_real)‚Åª¬π * ‚Äñf‚Äñ :=
Lp_nnnorm_le f

variables (p Œº)

/-- The normed group homomorphism of considering a bounded continuous function on a finite-measure
space as an element of `Lp`. -/
def to_Lp_hom [fact (1 ‚â§ p)] : normed_add_group_hom (Œ± ‚Üí·µá E) (Lp E p Œº) :=
{ bound' := ‚ü®_, Lp_norm_le‚ü©,
  .. add_monoid_hom.cod_restrict
      ((continuous_map.to_ae_eq_fun_add_hom Œº).comp (to_continuous_map_add_hom Œ± E))
      (Lp E p Œº)
      mem_Lp }

lemma range_to_Lp_hom [fact (1 ‚â§ p)] :
  ((to_Lp_hom p Œº).range : add_subgroup (Lp E p Œº))
    = measure_theory.Lp.bounded_continuous_function E p Œº :=
begin
  symmetry,
  convert add_monoid_hom.add_subgroup_of_range_eq_of_le
    ((continuous_map.to_ae_eq_fun_add_hom Œº).comp (to_continuous_map_add_hom Œ± E))
    (by { rintros - ‚ü®f, rfl‚ü©, exact mem_Lp f } : _ ‚â§ Lp E p Œº),
end

variables (ùïú : Type*) [fact (1 ‚â§ p)]

/-- The bounded linear map of considering a bounded continuous function on a finite-measure space
as an element of `Lp`. -/
def to_Lp [normed_field ùïú] [normed_space ùïú E] :
  (Œ± ‚Üí·µá E) ‚ÜíL[ùïú] (Lp E p Œº) :=
linear_map.mk_continuous
  (linear_map.cod_restrict
    (Lp.Lp_submodule E p Œº ùïú)
    ((continuous_map.to_ae_eq_fun_linear_map Œº).comp (to_continuous_map_linear_map Œ± E ùïú))
    mem_Lp)
  _
  Lp_norm_le

lemma coe_fn_to_Lp [normed_field ùïú] [normed_space ùïú E] (f : Œ± ‚Üí·µá E) :
  to_Lp p Œº ùïú f =·µê[Œº] f := ae_eq_fun.coe_fn_mk f _

variables {ùïú}

lemma range_to_Lp [normed_field ùïú] [normed_space ùïú E] :
  ((linear_map.range (to_Lp p Œº ùïú : (Œ± ‚Üí·µá E) ‚ÜíL[ùïú] Lp E p Œº)).to_add_subgroup)
    = measure_theory.Lp.bounded_continuous_function E p Œº :=
range_to_Lp_hom p Œº

variables {p}

lemma to_Lp_norm_le [nontrivially_normed_field ùïú] [normed_space ùïú E]:
  ‚Äñ(to_Lp p Œº ùïú : (Œ± ‚Üí·µá E) ‚ÜíL[ùïú] (Lp E p Œº))‚Äñ ‚â§ (measure_univ_nnreal Œº) ^ (p.to_real)‚Åª¬π :=
linear_map.mk_continuous_norm_le _ ((measure_univ_nnreal Œº) ^ (p.to_real)‚Åª¬π).coe_nonneg _

lemma to_Lp_inj {f g : Œ± ‚Üí·µá E} [Œº.is_open_pos_measure] [normed_field ùïú] [normed_space ùïú E] :
  to_Lp p Œº ùïú f = to_Lp p Œº ùïú g ‚Üî f = g :=
begin
  refine ‚ü®Œª h, _, by tauto‚ü©,
  rw [‚Üêfun_like.coe_fn_eq, ‚Üê(map_continuous f).ae_eq_iff_eq Œº (map_continuous g)],
  refine (coe_fn_to_Lp p Œº ùïú f).symm.trans (eventually_eq.trans _ $ coe_fn_to_Lp p Œº ùïú g),
  rw h,
end

lemma to_Lp_injective [Œº.is_open_pos_measure] [normed_field ùïú] [normed_space ùïú E] :
  function.injective ‚áë(to_Lp p Œº ùïú : (Œ± ‚Üí·µá E) ‚ÜíL[ùïú] (Lp E p Œº)) := Œª f g hfg, (to_Lp_inj Œº).mp hfg

end bounded_continuous_function

namespace continuous_map

variables [compact_space Œ±] [is_finite_measure Œº]
variables (ùïú : Type*) (p Œº) [fact (1 ‚â§ p)]

/-- The bounded linear map of considering a continuous function on a compact finite-measure
space `Œ±` as an element of `Lp`.  By definition, the norm on `C(Œ±, E)` is the sup-norm, transferred
from the space `Œ± ‚Üí·µá E` of bounded continuous functions, so this construction is just a matter of
transferring the structure from `bounded_continuous_function.to_Lp` along the isometry. -/
def to_Lp [normed_field ùïú] [normed_space ùïú E] :
  C(Œ±, E) ‚ÜíL[ùïú] (Lp E p Œº) :=
(bounded_continuous_function.to_Lp p Œº ùïú).comp
  (linear_isometry_bounded_of_compact Œ± E ùïú).to_linear_isometry.to_continuous_linear_map

variables {ùïú}

lemma range_to_Lp [normed_field ùïú] [normed_space ùïú E] :
  (linear_map.range (to_Lp p Œº ùïú : C(Œ±, E) ‚ÜíL[ùïú] Lp E p Œº)).to_add_subgroup
    = measure_theory.Lp.bounded_continuous_function E p Œº :=
begin
  refine set_like.ext' _,
  have := (linear_isometry_bounded_of_compact Œ± E ùïú).surjective,
  convert function.surjective.range_comp this (bounded_continuous_function.to_Lp p Œº ùïú),
  rw ‚Üêbounded_continuous_function.range_to_Lp p Œº,
  refl,
end

variables {p}

lemma coe_fn_to_Lp [normed_field ùïú] [normed_space ùïú E] (f : C(Œ±,  E)) :
  to_Lp p Œº ùïú f =·µê[Œº] f :=
ae_eq_fun.coe_fn_mk f _

lemma to_Lp_def [normed_field ùïú] [normed_space ùïú E] (f : C(Œ±, E)) :
  to_Lp p Œº ùïú f
  = bounded_continuous_function.to_Lp p Œº ùïú (linear_isometry_bounded_of_compact Œ± E ùïú f) :=
rfl

@[simp] lemma to_Lp_comp_to_continuous_map [normed_field ùïú] [normed_space ùïú E] (f : Œ± ‚Üí·µá E) :
  to_Lp p Œº ùïú f.to_continuous_map
  = bounded_continuous_function.to_Lp p Œº ùïú f :=
rfl

@[simp] lemma coe_to_Lp [normed_field ùïú] [normed_space ùïú E] (f : C(Œ±, E)) :
  (to_Lp p Œº ùïú f : Œ± ‚Üí‚Çò[Œº] E) = f.to_ae_eq_fun Œº :=
rfl

lemma to_Lp_injective [Œº.is_open_pos_measure] [normed_field ùïú] [normed_space ùïú E] :
  function.injective ‚áë(to_Lp p Œº ùïú : C(Œ±, E) ‚ÜíL[ùïú] (Lp E p Œº)) :=
(bounded_continuous_function.to_Lp_injective _).comp
  (linear_isometry_bounded_of_compact Œ± E ùïú).injective

lemma to_Lp_inj {f g : C(Œ±, E)} [Œº.is_open_pos_measure] [normed_field ùïú] [normed_space ùïú E] :
  to_Lp p Œº ùïú f = to_Lp p Œº ùïú g ‚Üî f = g :=
(to_Lp_injective Œº).eq_iff

variables {Œº}

/-- If a sum of continuous functions `g n` is convergent, and the same sum converges in `L·µñ` to `h`,
then in fact `g n` converges uniformly to `h`.  -/
lemma has_sum_of_has_sum_Lp {Œ≤ : Type*} [Œº.is_open_pos_measure] [normed_field ùïú] [normed_space ùïú E]
  {g : Œ≤ ‚Üí C(Œ±, E)} {f : C(Œ±, E)} (hg : summable g)
  (hg2 : has_sum (to_Lp p Œº ùïú ‚àò g) (to_Lp p Œº ùïú f)) : has_sum g f :=
begin
  convert summable.has_sum hg,
  exact to_Lp_injective Œº (hg2.unique ((to_Lp p Œº ùïú).has_sum $ summable.has_sum hg)),
end

variables (Œº) [nontrivially_normed_field ùïú] [normed_space ùïú E]

lemma to_Lp_norm_eq_to_Lp_norm_coe :
  ‚Äñ(to_Lp p Œº ùïú : C(Œ±, E) ‚ÜíL[ùïú] (Lp E p Œº))‚Äñ
  = ‚Äñ(bounded_continuous_function.to_Lp p Œº ùïú : (Œ± ‚Üí·µá E) ‚ÜíL[ùïú] (Lp E p Œº))‚Äñ :=
continuous_linear_map.op_norm_comp_linear_isometry_equiv _ _

/-- Bound for the operator norm of `continuous_map.to_Lp`. -/
lemma to_Lp_norm_le :
  ‚Äñ(to_Lp p Œº ùïú : C(Œ±, E) ‚ÜíL[ùïú] (Lp E p Œº))‚Äñ ‚â§ (measure_univ_nnreal Œº) ^ (p.to_real)‚Åª¬π :=
by { rw to_Lp_norm_eq_to_Lp_norm_coe, exact bounded_continuous_function.to_Lp_norm_le Œº }

end continuous_map

end

namespace measure_theory

namespace Lp

lemma pow_mul_meas_ge_le_norm (f : Lp E p Œº)
  (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) (Œµ : ‚Ñù‚â•0‚àû) :
  (Œµ * Œº {x | Œµ ‚â§ ‚Äñf x‚Äñ‚Çä ^ p.to_real}) ^ (1 / p.to_real) ‚â§ (ennreal.of_real ‚Äñf‚Äñ) :=
(ennreal.of_real_to_real (snorm_ne_top f)).symm ‚ñ∏
  pow_mul_meas_ge_le_snorm Œº hp_ne_zero hp_ne_top (Lp.ae_strongly_measurable f) Œµ

lemma mul_meas_ge_le_pow_norm (f : Lp E p Œº)
  (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) (Œµ : ‚Ñù‚â•0‚àû) :
  Œµ * Œº {x | Œµ ‚â§ ‚Äñf x‚Äñ‚Çä ^ p.to_real} ‚â§ (ennreal.of_real ‚Äñf‚Äñ) ^ p.to_real :=
(ennreal.of_real_to_real (snorm_ne_top f)).symm ‚ñ∏
  mul_meas_ge_le_pow_snorm Œº hp_ne_zero hp_ne_top (Lp.ae_strongly_measurable f) Œµ

/-- A version of Markov's inequality with elements of Lp. -/
lemma mul_meas_ge_le_pow_norm' (f : Lp E p Œº)
  (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) (Œµ : ‚Ñù‚â•0‚àû) :
  Œµ ^ p.to_real * Œº {x | Œµ ‚â§ ‚Äñf x‚Äñ‚Çä} ‚â§ (ennreal.of_real ‚Äñf‚Äñ) ^ p.to_real :=
(ennreal.of_real_to_real (snorm_ne_top f)).symm ‚ñ∏
  mul_meas_ge_le_pow_snorm' Œº hp_ne_zero hp_ne_top (Lp.ae_strongly_measurable f) Œµ

lemma meas_ge_le_mul_pow_norm (f : Lp E p Œº)
  (hp_ne_zero : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
  Œº {x | Œµ ‚â§ ‚Äñf x‚Äñ‚Çä} ‚â§ Œµ‚Åª¬π ^ p.to_real * (ennreal.of_real ‚Äñf‚Äñ) ^ p.to_real :=
(ennreal.of_real_to_real (snorm_ne_top f)).symm ‚ñ∏
  meas_ge_le_mul_pow_snorm Œº hp_ne_zero hp_ne_top (Lp.ae_strongly_measurable f) hŒµ

end Lp

end measure_theory
