/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, Heather Macbeth
-/
import measure_theory.function.l1_space

/-!
# Density of simple functions

Show that each Borel measurable function can be approximated pointwise, and each `Láµ–` Borel
measurable function can be approximated in `Láµ–` norm, by a sequence of simple functions.

## Main definitions

* `measure_theory.simple_func.nearest_pt (e : â„• â†’ Î±) (N : â„•) : Î± â†’â‚› â„•`: the `simple_func` sending
  each `x : Î±` to the point `e k` which is the nearest to `x` among `e 0`, ..., `e N`.
* `measure_theory.simple_func.approx_on (f : Î² â†’ Î±) (hf : measurable f) (s : set Î±) (yâ‚€ : Î±)
  (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (n : â„•) : Î² â†’â‚› Î±` : a simple function that takes values in `s`
  and approximates `f`.
* `measure_theory.Lp.simple_func`, the type of `Lp` simple functions
* `coe_to_Lp`, the embedding of `Lp.simple_func E p Î¼` into `Lp E p Î¼`

## Main results

* `tendsto_approx_on` (pointwise convergence): If `f x âˆˆ s`, then the sequence of simple
  approximations `measure_theory.simple_func.approx_on f hf s yâ‚€ hâ‚€ n`, evaluated at `x`,
  tends to `f x` as `n` tends to `âˆž`.
* `tendsto_approx_on_univ_Lp` (Láµ– convergence): If `E` is a `normed_group` and `f` is measurable
  and `mem_â„’p` (for `p < âˆž`), then the simple functions `simple_func.approx_on f hf s 0 hâ‚€ n` may
  be considered as elements of `Lp E p Î¼`, and they tend in Láµ– to `f`.
* `Lp.simple_func.dense_embedding`: the embedding `coe_to_Lp` of the `Lp` simple functions into
  `Lp` is dense.
* `Lp.simple_func.induction`, `Lp.induction`, `mem_â„’p.induction`, `integrable.induction`: to prove
  a predicate for all elements of one of these classes of functions, it suffices to check that it
  behaves correctly on simple functions.

## TODO

For `E` finite-dimensional, simple functions `Î± â†’â‚› E` are dense in L^âˆž -- prove this.

## Notations

* `Î± â†’â‚› Î²` (local notation): the type of simple functions `Î± â†’ Î²`.
* `Î± â†’â‚â‚›[Î¼] E`: the type of `L1` simple functions `Î± â†’ Î²`.
-/

open set function filter topological_space ennreal emetric finset
open_locale classical topological_space ennreal measure_theory big_operators
variables {Î± Î² Î¹ E F ð•œ : Type*}

noncomputable theory

namespace measure_theory

local infixr ` â†’â‚› `:25 := simple_func

namespace simple_func

/-! ### Pointwise approximation by simple functions -/

section pointwise
variables [measurable_space Î±] [emetric_space Î±] [opens_measurable_space Î±]

/-- `nearest_pt_ind e N x` is the index `k` such that `e k` is the nearest point to `x` among the
points `e 0`, ..., `e N`. If more than one point are at the same distance from `x`, then
`nearest_pt_ind e N x` returns the least of their indexes. -/
noncomputable def nearest_pt_ind (e : â„• â†’ Î±) : â„• â†’ Î± â†’â‚› â„•
| 0 := const Î± 0
| (N + 1) := piecewise (â‹‚ k â‰¤ N, {x | edist (e (N + 1)) x < edist (e k) x})
    (measurable_set.Inter $ Î» k, measurable_set.Inter_Prop $ Î» hk,
      measurable_set_lt measurable_edist_right measurable_edist_right)
    (const Î± $ N + 1) (nearest_pt_ind N)

/-- `nearest_pt e N x` is the nearest point to `x` among the points `e 0`, ..., `e N`. If more than
one point are at the same distance from `x`, then `nearest_pt e N x` returns the point with the
least possible index. -/
noncomputable def nearest_pt (e : â„• â†’ Î±) (N : â„•) : Î± â†’â‚› Î± :=
(nearest_pt_ind e N).map e

@[simp] lemma nearest_pt_ind_zero (e : â„• â†’ Î±) : nearest_pt_ind e 0 = const Î± 0 := rfl

@[simp] lemma nearest_pt_zero (e : â„• â†’ Î±) : nearest_pt e 0 = const Î± (e 0) := rfl

lemma nearest_pt_ind_succ (e : â„• â†’ Î±) (N : â„•) (x : Î±) :
  nearest_pt_ind e (N + 1) x =
    if âˆ€ k â‰¤ N, edist (e (N + 1)) x < edist (e k) x
    then N + 1 else nearest_pt_ind e N x :=
by { simp only [nearest_pt_ind, coe_piecewise, set.piecewise], congr, simp }

lemma nearest_pt_ind_le (e : â„• â†’ Î±) (N : â„•) (x : Î±) : nearest_pt_ind e N x â‰¤ N :=
begin
  induction N with N ihN, { simp },
  simp only [nearest_pt_ind_succ],
  split_ifs,
  exacts [le_rfl, ihN.trans N.le_succ]
end

lemma edist_nearest_pt_le (e : â„• â†’ Î±) (x : Î±) {k N : â„•} (hk : k â‰¤ N) :
  edist (nearest_pt e N x) x â‰¤ edist (e k) x :=
begin
  induction N with N ihN generalizing k,
  { simp [nonpos_iff_eq_zero.1 hk, le_refl] },
  { simp only [nearest_pt, nearest_pt_ind_succ, map_apply],
    split_ifs,
    { rcases hk.eq_or_lt with rfl|hk,
      exacts [le_rfl, (h k (nat.lt_succ_iff.1 hk)).le] },
    { push_neg at h,
      rcases h with âŸ¨l, hlN, hxlâŸ©,
      rcases hk.eq_or_lt with rfl|hk,
      exacts [(ihN hlN).trans hxl, ihN (nat.lt_succ_iff.1 hk)] } }
end

lemma tendsto_nearest_pt {e : â„• â†’ Î±} {x : Î±} (hx : x âˆˆ closure (range e)) :
  tendsto (Î» N, nearest_pt e N x) at_top (ð“ x) :=
begin
  refine (at_top_basis.tendsto_iff nhds_basis_eball).2 (Î» Îµ hÎµ, _),
  rcases emetric.mem_closure_iff.1 hx Îµ hÎµ with âŸ¨_, âŸ¨N, rflâŸ©, hNâŸ©,
  rw [edist_comm] at hN,
  exact âŸ¨N, trivial, Î» n hn, (edist_nearest_pt_le e x hn).trans_lt hNâŸ©
end

variables [measurable_space Î²] {f : Î² â†’ Î±}

/-- Approximate a measurable function by a sequence of simple functions `F n` such that
`F n x âˆˆ s`. -/
noncomputable def approx_on (f : Î² â†’ Î±) (hf : measurable f) (s : set Î±) (yâ‚€ : Î±) (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (n : â„•) :
  Î² â†’â‚› Î± :=
by haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©;
  exact comp (nearest_pt (Î» k, nat.cases_on k yâ‚€ (coe âˆ˜ dense_seq s) : â„• â†’ Î±) n) f hf

@[simp] lemma approx_on_zero {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) :
  approx_on f hf s yâ‚€ hâ‚€ 0 x = yâ‚€ :=
rfl

lemma approx_on_mem {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (n : â„•) (x : Î²) :
  approx_on f hf s yâ‚€ hâ‚€ n x âˆˆ s :=
begin
  haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©,
  suffices : âˆ€ n, (nat.cases_on n yâ‚€ (coe âˆ˜ dense_seq s) : Î±) âˆˆ s, { apply this },
  rintro (_|n),
  exacts [hâ‚€, subtype.mem _]
end

@[simp] lemma approx_on_comp {Î³ : Type*} [measurable_space Î³] {f : Î² â†’ Î±} (hf : measurable f)
  {g : Î³ â†’ Î²} (hg : measurable g) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (n : â„•) :
  approx_on (f âˆ˜ g) (hf.comp hg) s yâ‚€ hâ‚€ n = (approx_on f hf s yâ‚€ hâ‚€ n).comp g hg :=
rfl

lemma tendsto_approx_on {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] {x : Î²} (hx : f x âˆˆ closure s) :
  tendsto (Î» n, approx_on f hf s yâ‚€ hâ‚€ n x) at_top (ð“ $ f x) :=
begin
  haveI : nonempty s := âŸ¨âŸ¨yâ‚€, hâ‚€âŸ©âŸ©,
  rw [â† @subtype.range_coe _ s, â† image_univ, â† (dense_range_dense_seq s).closure_eq] at hx,
  simp only [approx_on, coe_comp],
  refine tendsto_nearest_pt (closure_minimal _ is_closed_closure hx),
  simp only [nat.range_cases_on, closure_union, range_comp coe],
  exact subset.trans (image_closure_subset_closure_image continuous_subtype_coe)
    (subset_union_right _ _)
end

lemma edist_approx_on_le {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) (n : â„•) :
  edist (approx_on f hf s yâ‚€ hâ‚€ n x) (f x) â‰¤ edist yâ‚€ (f x) :=
begin
  dsimp only [approx_on, coe_comp, (âˆ˜)],
  exact edist_nearest_pt_le _ _ (zero_le _)
end

lemma edist_approx_on_y0_le {f : Î² â†’ Î±} (hf : measurable f) {s : set Î±} {yâ‚€ : Î±} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (x : Î²) (n : â„•) :
  edist yâ‚€ (approx_on f hf s yâ‚€ hâ‚€ n x) â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) :=
calc edist yâ‚€ (approx_on f hf s yâ‚€ hâ‚€ n x) â‰¤
  edist yâ‚€ (f x) + edist (approx_on f hf s yâ‚€ hâ‚€ n x) (f x) : edist_triangle_right _ _ _
... â‰¤ edist yâ‚€ (f x) + edist yâ‚€ (f x) : add_le_add_left (edist_approx_on_le hf hâ‚€ x n) _

end pointwise

/-! ### Lp approximation by simple functions -/

section Lp
variables [measurable_space Î²]
variables [measurable_space E] [normed_group E] {q : â„} {p : â„â‰¥0âˆž}

lemma nnnorm_approx_on_le [opens_measurable_space E] {f : Î² â†’ E} (hf : measurable f)
  {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (x : Î²) (n : â„•) :
  âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š â‰¤ âˆ¥f x - yâ‚€âˆ¥â‚Š :=
begin
  have := edist_approx_on_le hf hâ‚€ x n,
  rw edist_comm yâ‚€ at this,
  simp only [edist_nndist, nndist_eq_nnnorm] at this,
  exact_mod_cast this
end

lemma norm_approx_on_yâ‚€_le [opens_measurable_space E] {f : Î² â†’ E} (hf : measurable f)
  {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (x : Î²) (n : â„•) :
  âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - yâ‚€âˆ¥ â‰¤ âˆ¥f x - yâ‚€âˆ¥ + âˆ¥f x - yâ‚€âˆ¥ :=
begin
  have := edist_approx_on_y0_le hf hâ‚€ x n,
  repeat { rw [edist_comm yâ‚€, edist_eq_coe_nnnorm_sub] at this },
  exact_mod_cast this,
end

lemma norm_approx_on_zero_le [opens_measurable_space E] {f : Î² â†’ E} (hf : measurable f)
  {s : set E} (hâ‚€ : (0 : E) âˆˆ s) [separable_space s] (x : Î²) (n : â„•) :
  âˆ¥approx_on f hf s 0 hâ‚€ n xâˆ¥ â‰¤ âˆ¥f xâˆ¥ + âˆ¥f xâˆ¥ :=
begin
  have := edist_approx_on_y0_le hf hâ‚€ x n,
  simp [edist_comm (0 : E), edist_eq_coe_nnnorm] at this,
  exact_mod_cast this,
end

lemma tendsto_approx_on_Lp_snorm [opens_measurable_space E]
  {f : Î² â†’ E} (hf : measurable f) {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s]
  (hp_ne_top : p â‰  âˆž) {Î¼ : measure Î²} (hÎ¼ : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closure s)
  (hi : snorm (Î» x, f x - yâ‚€) p Î¼ < âˆž) :
  tendsto (Î» n, snorm (approx_on f hf s yâ‚€ hâ‚€ n - f) p Î¼) at_top (ð“ 0) :=
begin
  by_cases hp_zero : p = 0,
  { simpa only [hp_zero, snorm_exponent_zero] using tendsto_const_nhds },
  have hp : 0 < p.to_real := to_real_pos_iff.mpr âŸ¨bot_lt_iff_ne_bot.mpr hp_zero, hp_ne_topâŸ©,
  suffices : tendsto (Î» n, âˆ«â» x, âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š ^ p.to_real âˆ‚Î¼) at_top (ð“ 0),
  { simp only [snorm_eq_lintegral_rpow_nnnorm hp_zero hp_ne_top],
    convert continuous_rpow_const.continuous_at.tendsto.comp this;
    simp [_root_.inv_pos.mpr hp] },
  -- We simply check the conditions of the Dominated Convergence Theorem:
  -- (1) The function "`p`-th power of distance between `f` and the approximation" is measurable
  have hF_meas : âˆ€ n, measurable (Î» x, (âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š : â„â‰¥0âˆž) ^ p.to_real),
  { simpa only [â† edist_eq_coe_nnnorm_sub] using
      Î» n, (approx_on f hf s yâ‚€ hâ‚€ n).measurable_bind (Î» y x, (edist y (f x)) ^ p.to_real)
      (Î» y, (measurable_edist_right.comp hf).pow_const p.to_real) },
  -- (2) The functions "`p`-th power of distance between `f` and the approximation" are uniformly
  -- bounded, at any given point, by `Î» x, âˆ¥f x - yâ‚€âˆ¥ ^ p.to_real`
  have h_bound : âˆ€ n, (Î» x, (âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š : â„â‰¥0âˆž) ^ p.to_real)
      â‰¤áµ[Î¼] (Î» x, âˆ¥f x - yâ‚€âˆ¥â‚Š ^ p.to_real),
  { exact Î» n, eventually_of_forall
      (Î» x, rpow_le_rpow (coe_mono (nnnorm_approx_on_le hf hâ‚€ x n)) to_real_nonneg) },
  -- (3) The bounding function `Î» x, âˆ¥f x - yâ‚€âˆ¥ ^ p.to_real` has finite integral
  have h_fin :  âˆ«â» (a : Î²), âˆ¥f a - yâ‚€âˆ¥â‚Š ^ p.to_real âˆ‚Î¼ â‰  âŠ¤,
    from (lintegral_rpow_nnnorm_lt_top_of_snorm_lt_top hp_zero hp_ne_top hi).ne,
  -- (4) The functions "`p`-th power of distance between `f` and the approximation" tend pointwise
  -- to zero
  have h_lim : âˆ€áµ (a : Î²) âˆ‚Î¼,
    tendsto (Î» n, (âˆ¥approx_on f hf s yâ‚€ hâ‚€ n a - f aâˆ¥â‚Š : â„â‰¥0âˆž) ^ p.to_real) at_top (ð“ 0),
  { filter_upwards [hÎ¼],
    intros a ha,
    have : tendsto (Î» n, (approx_on f hf s yâ‚€ hâ‚€ n) a - f a) at_top (ð“ (f a - f a)),
    { exact (tendsto_approx_on hf hâ‚€ ha).sub tendsto_const_nhds },
    convert continuous_rpow_const.continuous_at.tendsto.comp (tendsto_coe.mpr this.nnnorm),
    simp [zero_rpow_of_pos hp] },
  -- Then we apply the Dominated Convergence Theorem
  simpa using tendsto_lintegral_of_dominated_convergence _ hF_meas h_bound h_fin h_lim,
end

lemma mem_â„’p_approx_on [borel_space E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : mem_â„’p f p Î¼) {s : set E} {yâ‚€ : E}
  (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s] (hiâ‚€ : mem_â„’p (Î» x, yâ‚€) p Î¼) (n : â„•) :
  mem_â„’p (approx_on f fmeas s yâ‚€ hâ‚€ n) p Î¼ :=
begin
  refine âŸ¨(approx_on f fmeas s yâ‚€ hâ‚€ n).ae_measurable, _âŸ©,
  suffices : snorm (Î» x, approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€) p Î¼ < âŠ¤,
  { have : mem_â„’p (Î» x, approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€) p Î¼ :=
      âŸ¨(approx_on f fmeas s yâ‚€ hâ‚€ n - const Î² yâ‚€).ae_measurable, thisâŸ©,
    convert snorm_add_lt_top this hiâ‚€,
    ext x,
    simp },
  -- We don't necessarily have `mem_â„’p (Î» x, f x - yâ‚€) p Î¼`, because the `ae_measurable` part
  -- requires `ae_measurable.add`, which requires second-countability
  have hf' : mem_â„’p (Î» x, âˆ¥f x - yâ‚€âˆ¥) p Î¼,
  { have h_meas : measurable (Î» x, âˆ¥f x - yâ‚€âˆ¥),
    { simp only [â† dist_eq_norm],
      exact (continuous_id.dist continuous_const).measurable.comp fmeas },
    refine âŸ¨h_meas.ae_measurable, _âŸ©,
    rw snorm_norm,
    convert snorm_add_lt_top hf hiâ‚€.neg,
    ext x,
    simp [sub_eq_add_neg] },
  have : âˆ€áµ x âˆ‚Î¼, âˆ¥approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€âˆ¥ â‰¤ âˆ¥(âˆ¥f x - yâ‚€âˆ¥ + âˆ¥f x - yâ‚€âˆ¥)âˆ¥,
  { refine eventually_of_forall _,
    intros x,
    convert norm_approx_on_yâ‚€_le fmeas hâ‚€ x n,
    rw [real.norm_eq_abs, abs_of_nonneg],
    exact add_nonneg (norm_nonneg _) (norm_nonneg _) },
  calc snorm (Î» x, approx_on f fmeas s yâ‚€ hâ‚€ n x - yâ‚€) p Î¼
      â‰¤ snorm (Î» x, âˆ¥f x - yâ‚€âˆ¥ + âˆ¥f x - yâ‚€âˆ¥) p Î¼ : snorm_mono_ae this
  ... < âŠ¤ : snorm_add_lt_top hf' hf',
end

lemma tendsto_approx_on_univ_Lp_snorm [opens_measurable_space E] [second_countable_topology E]
  {f : Î² â†’ E} (hp_ne_top : p â‰  âˆž) {Î¼ : measure Î²} (fmeas : measurable f) (hf : snorm f p Î¼ < âˆž) :
  tendsto (Î» n, snorm (approx_on f fmeas univ 0 trivial n - f) p Î¼) at_top (ð“ 0) :=
tendsto_approx_on_Lp_snorm fmeas trivial hp_ne_top (by simp) (by simpa using hf)

lemma mem_â„’p_approx_on_univ [borel_space E] [second_countable_topology E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : mem_â„’p f p Î¼) (n : â„•) :
  mem_â„’p (approx_on f fmeas univ 0 trivial n) p Î¼ :=
mem_â„’p_approx_on fmeas hf (mem_univ _) zero_mem_â„’p n

lemma tendsto_approx_on_univ_Lp [borel_space E] [second_countable_topology E]
  {f : Î² â†’ E} [hp : fact (1 â‰¤ p)] (hp_ne_top : p â‰  âˆž) {Î¼ : measure Î²} (fmeas : measurable f)
  (hf : mem_â„’p f p Î¼) :
  tendsto (Î» n, (mem_â„’p_approx_on_univ fmeas hf n).to_Lp (approx_on f fmeas univ 0 trivial n))
    at_top (ð“ (hf.to_Lp f)) :=
by simp [Lp.tendsto_Lp_iff_tendsto_â„’p'', tendsto_approx_on_univ_Lp_snorm hp_ne_top fmeas hf.2]

end Lp

/-! ### L1 approximation by simple functions -/

section integrable
variables [measurable_space Î²]
variables [measurable_space E] [normed_group E]

lemma tendsto_approx_on_L1_nnnorm [opens_measurable_space E]
  {f : Î² â†’ E} (hf : measurable f) {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s) [separable_space s]
  {Î¼ : measure Î²} (hÎ¼ : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closure s) (hi : has_finite_integral (Î» x, f x - yâ‚€) Î¼) :
  tendsto (Î» n, âˆ«â» x, âˆ¥approx_on f hf s yâ‚€ hâ‚€ n x - f xâˆ¥â‚Š âˆ‚Î¼) at_top (ð“ 0) :=
by simpa [snorm_one_eq_lintegral_nnnorm] using tendsto_approx_on_Lp_snorm hf hâ‚€ one_ne_top hÎ¼
  (by simpa [snorm_one_eq_lintegral_nnnorm] using hi)

lemma integrable_approx_on [borel_space E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : integrable f Î¼)
  {s : set E} {yâ‚€ : E} (hâ‚€ : yâ‚€ âˆˆ s)
  [separable_space s] (hiâ‚€ : integrable (Î» x, yâ‚€) Î¼) (n : â„•) :
  integrable (approx_on f fmeas s yâ‚€ hâ‚€ n) Î¼ :=
begin
  rw â† mem_â„’p_one_iff_integrable at hf hiâ‚€ âŠ¢,
  exact mem_â„’p_approx_on fmeas hf hâ‚€ hiâ‚€ n,
end

lemma tendsto_approx_on_univ_L1_nnnorm [opens_measurable_space E] [second_countable_topology E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : integrable f Î¼) :
  tendsto (Î» n, âˆ«â» x, âˆ¥approx_on f fmeas univ 0 trivial n x - f xâˆ¥â‚Š âˆ‚Î¼) at_top (ð“ 0) :=
tendsto_approx_on_L1_nnnorm fmeas trivial (by simp) (by simpa using hf.2)

lemma integrable_approx_on_univ [borel_space E] [second_countable_topology E]
  {f : Î² â†’ E} {Î¼ : measure Î²} (fmeas : measurable f) (hf : integrable f Î¼) (n : â„•) :
  integrable (approx_on f fmeas univ 0 trivial n) Î¼ :=
integrable_approx_on fmeas hf _ (integrable_zero _ _ _) n

end integrable

section simple_func_properties

variables [measurable_space Î±]
variables [normed_group E] [measurable_space E] [normed_group F]
variables {Î¼ : measure Î±} {p : â„â‰¥0âˆž}

/-!
### Properties of simple functions in `Lp` spaces

A simple function `f : Î± â†’â‚› E` into a normed group `E` verifies, for a measure `Î¼`:
- `mem_â„’p f 0 Î¼` and `mem_â„’p f âˆž Î¼`, since `f` is a.e.-measurable and bounded,
- for `0 < p < âˆž`,
  `mem_â„’p f p Î¼ â†” integrable f Î¼ â†” f.fin_meas_supp Î¼ â†” âˆ€ y â‰  0, Î¼ (f â»Â¹' {y}) < âˆž`.
-/

lemma exists_forall_norm_le (f : Î± â†’â‚› F) : âˆƒ C, âˆ€ x, âˆ¥f xâˆ¥ â‰¤ C :=
exists_forall_le (f.map (Î» x, âˆ¥xâˆ¥))

lemma mem_â„’p_zero (f : Î± â†’â‚› E) (Î¼ : measure Î±) : mem_â„’p f 0 Î¼ :=
mem_â„’p_zero_iff_ae_measurable.mpr f.ae_measurable

lemma mem_â„’p_top (f : Î± â†’â‚› E) (Î¼ : measure Î±) : mem_â„’p f âˆž Î¼ :=
let âŸ¨C, hfCâŸ© := f.exists_forall_norm_le in
mem_â„’p_top_of_bound f.ae_measurable C $ eventually_of_forall hfC

protected lemma snorm'_eq {p : â„} (f : Î± â†’â‚› F) (Î¼ : measure Î±) :
  snorm' f p Î¼ = (âˆ‘ y in f.range, (nnnorm y : â„â‰¥0âˆž) ^ p * Î¼ (f â»Â¹' {y})) ^ (1/p) :=
have h_map : (Î» a, (nnnorm (f a) : â„â‰¥0âˆž) ^ p) = f.map (Î» a : F, (nnnorm a : â„â‰¥0âˆž) ^ p), by simp,
by rw [snorm', h_map, lintegral_eq_lintegral, map_lintegral]

lemma measure_preimage_lt_top_of_mem_â„’p  (hp_pos : 0 < p) (hp_ne_top : p â‰  âˆž) (f : Î± â†’â‚› E)
  (hf : mem_â„’p f p Î¼) (y : E) (hy_ne : y â‰  0) :
  Î¼ (f â»Â¹' {y}) < âˆž :=
begin
  have hp_pos_real : 0 < p.to_real, from ennreal.to_real_pos_iff.mpr âŸ¨hp_pos, hp_ne_topâŸ©,
  have hf_snorm := mem_â„’p.snorm_lt_top hf,
  rw [snorm_eq_snorm' hp_pos.ne.symm hp_ne_top, f.snorm'_eq,
    â† @ennreal.lt_rpow_one_div_iff _ _ (1 / p.to_real) (by simp [hp_pos_real]),
    @ennreal.top_rpow_of_pos (1 / (1 / p.to_real)) (by simp [hp_pos_real]),
    ennreal.sum_lt_top_iff] at hf_snorm,
  by_cases hyf : y âˆˆ f.range,
  swap,
  { suffices h_empty : f â»Â¹' {y} = âˆ…,
      by { rw [h_empty, measure_empty], exact ennreal.coe_lt_top, },
    ext1 x,
    rw [set.mem_preimage, set.mem_singleton_iff, mem_empty_eq, iff_false],
    refine Î» hxy, hyf _,
    rw [mem_range, set.mem_range],
    exact âŸ¨x, hxyâŸ©, },
  specialize hf_snorm y hyf,
  rw ennreal.mul_lt_top_iff at hf_snorm,
  cases hf_snorm,
  { exact hf_snorm.2, },
  cases hf_snorm,
  { refine absurd _ hy_ne,
    simpa [hp_pos_real] using hf_snorm, },
  { simp [hf_snorm], },
end

lemma mem_â„’p_of_finite_measure_preimage (p : â„â‰¥0âˆž) {f : Î± â†’â‚› E} (hf : âˆ€ y â‰  0, Î¼ (f â»Â¹' {y}) < âˆž) :
  mem_â„’p f p Î¼ :=
begin
  by_cases hp0 : p = 0,
  { rw [hp0, mem_â„’p_zero_iff_ae_measurable], exact f.ae_measurable, },
  by_cases hp_top : p = âˆž,
  { rw hp_top, exact mem_â„’p_top f Î¼, },
  refine âŸ¨f.ae_measurable, _âŸ©,
  rw [snorm_eq_snorm' hp0 hp_top, f.snorm'_eq],
  refine ennreal.rpow_lt_top_of_nonneg (by simp) (ennreal.sum_lt_top_iff.mpr (Î» y hy, _)).ne,
  by_cases hy0 : y = 0,
  { simp [hy0, ennreal.to_real_pos_iff.mpr âŸ¨lt_of_le_of_ne (zero_le _) (ne.symm hp0), hp_topâŸ©], },
  { refine ennreal.mul_lt_top _ (hf y hy0).ne,
    exact (ennreal.rpow_lt_top_of_nonneg ennreal.to_real_nonneg ennreal.coe_ne_top).ne },
end

lemma mem_â„’p_iff {f : Î± â†’â‚› E} (hp_pos : 0 < p) (hp_ne_top : p â‰  âˆž) :
  mem_â„’p f p Î¼ â†” âˆ€ y â‰  0, Î¼ (f â»Â¹' {y}) < âˆž :=
âŸ¨Î» h, measure_preimage_lt_top_of_mem_â„’p hp_pos hp_ne_top f h,
  Î» h, mem_â„’p_of_finite_measure_preimage p hâŸ©

lemma integrable_iff {f : Î± â†’â‚› E} : integrable f Î¼ â†” âˆ€ y â‰  0, Î¼ (f â»Â¹' {y}) < âˆž :=
mem_â„’p_one_iff_integrable.symm.trans $ mem_â„’p_iff ennreal.zero_lt_one ennreal.coe_ne_top

lemma mem_â„’p_iff_integrable {f : Î± â†’â‚› E} (hp_pos : 0 < p) (hp_ne_top : p â‰  âˆž) :
  mem_â„’p f p Î¼ â†” integrable f Î¼ :=
(mem_â„’p_iff hp_pos hp_ne_top).trans integrable_iff.symm

lemma mem_â„’p_iff_fin_meas_supp {f : Î± â†’â‚› E} (hp_pos : 0 < p) (hp_ne_top : p â‰  âˆž) :
  mem_â„’p f p Î¼ â†” f.fin_meas_supp Î¼ :=
(mem_â„’p_iff hp_pos hp_ne_top).trans fin_meas_supp_iff.symm

lemma integrable_iff_fin_meas_supp {f : Î± â†’â‚› E} : integrable f Î¼ â†” f.fin_meas_supp Î¼ :=
integrable_iff.trans fin_meas_supp_iff.symm

lemma fin_meas_supp.integrable {f : Î± â†’â‚› E} (h : f.fin_meas_supp Î¼) : integrable f Î¼ :=
integrable_iff_fin_meas_supp.2 h

lemma integrable_pair [measurable_space F] {f : Î± â†’â‚› E} {g : Î± â†’â‚› F} :
  integrable f Î¼ â†’ integrable g Î¼ â†’ integrable (pair f g) Î¼ :=
by simpa only [integrable_iff_fin_meas_supp] using fin_meas_supp.pair

lemma mem_â„’p_of_is_finite_measure (f : Î± â†’â‚› E) (p : â„â‰¥0âˆž) (Î¼ : measure Î±) [is_finite_measure Î¼] :
  mem_â„’p f p Î¼ :=
let âŸ¨C, hfCâŸ© := f.exists_forall_norm_le in
mem_â„’p.of_bound f.ae_measurable C $ eventually_of_forall hfC

lemma integrable_of_is_finite_measure [is_finite_measure Î¼] (f : Î± â†’â‚› E) : integrable f Î¼ :=
mem_â„’p_one_iff_integrable.mp (f.mem_â„’p_of_is_finite_measure 1 Î¼)

lemma measure_preimage_lt_top_of_integrable (f : Î± â†’â‚› E) (hf : integrable f Î¼) {x : E}
  (hx : x â‰  0) :
  Î¼ (f â»Â¹' {x}) < âˆž :=
integrable_iff.mp hf x hx

lemma measure_support_lt_top [has_zero Î²] (f : Î± â†’â‚› Î²) (hf : âˆ€ y â‰  0, Î¼ (f â»Â¹' {y}) < âˆž) :
  Î¼ (support f) < âˆž :=
begin
  rw support_eq,
  refine (measure_bUnion_finset_le _ _).trans_lt (ennreal.sum_lt_top_iff.mpr (Î» y hy, _)),
  rw finset.mem_filter at hy,
  exact hf y hy.2,
end

lemma measure_support_lt_top_of_mem_â„’p (f : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) (hp_ne_zero : p â‰  0)
  (hp_ne_top : p â‰  âˆž) :
  Î¼ (support f) < âˆž :=
f.measure_support_lt_top ((mem_â„’p_iff (pos_iff_ne_zero.mpr hp_ne_zero) hp_ne_top).mp hf)

lemma measure_support_lt_top_of_integrable (f : Î± â†’â‚› E) (hf : integrable f Î¼) :
  Î¼ (support f) < âˆž :=
f.measure_support_lt_top (integrable_iff.mp hf)

lemma measure_lt_top_of_mem_â„’p_indicator (hp_pos : 0 < p) (hp_ne_top : p â‰  âˆž) {c : E} (hc : c â‰  0)
  {s : set Î±} (hs : measurable_set s)
  (hcs : mem_â„’p ((const Î± c).piecewise s hs (const Î± 0)) p Î¼) :
  Î¼ s < âŠ¤ :=
begin
  have : function.support (const Î± c) = set.univ := function.support_const hc,
  simpa only [mem_â„’p_iff_fin_meas_supp hp_pos hp_ne_top, fin_meas_supp_iff_support,
    support_indicator, set.inter_univ, this] using hcs
end

end simple_func_properties

end simple_func

/-! Construction of the space of `Lp` simple functions, and its dense embedding into `Lp`. -/
namespace Lp

open ae_eq_fun

variables
  [measurable_space Î±]
  [normed_group E] [second_countable_topology E] [measurable_space E] [borel_space E]
  [normed_group F] [second_countable_topology F] [measurable_space F] [borel_space F]
  (p : â„â‰¥0âˆž) (Î¼ : measure Î±)

variables (E)

/-- `Lp.simple_func` is a subspace of Lp consisting of equivalence classes of an integrable simple
    function. -/
def simple_func : add_subgroup (Lp E p Î¼) :=
{ carrier := {f : Lp E p Î¼ | âˆƒ (s : Î± â†’â‚› E), (ae_eq_fun.mk s s.ae_measurable : Î± â†’â‚˜[Î¼] E) = f},
  zero_mem' := âŸ¨0, rflâŸ©,
  add_mem' := Î» f g âŸ¨s, hsâŸ© âŸ¨t, htâŸ©, âŸ¨s + t,
      by simp only [â†hs, â†ht, mk_add_mk, add_subgroup.coe_add, mk_eq_mk, simple_func.coe_add]âŸ©,
  neg_mem' := Î» f âŸ¨s, hsâŸ©, âŸ¨-s,
      by simp only [â†hs, neg_mk, simple_func.coe_neg, mk_eq_mk, add_subgroup.coe_neg]âŸ© }

variables {E p Î¼}

namespace simple_func

section instances
/-! Simple functions in Lp space form a `normed_space`. -/

@[norm_cast] lemma coe_coe (f : Lp.simple_func E p Î¼) : â‡‘(f : Lp E p Î¼) = f := rfl

protected lemma eq' {f g : Lp.simple_func E p Î¼} : (f : Î± â†’â‚˜[Î¼] E) = (g : Î± â†’â‚˜[Î¼] E) â†’ f = g :=
subtype.eq âˆ˜ subtype.eq

/-! Implementation note:  If `Lp.simple_func E p Î¼` were defined as a `ð•œ`-submodule of `Lp E p Î¼`,
then the next few lemmas, putting a normed `ð•œ`-group structure on `Lp.simple_func E p Î¼`, would be
unnecessary.  But instead, `Lp.simple_func E p Î¼` is defined as an `add_subgroup` of `Lp E p Î¼`,
which does not permit this (but has the advantage of working when `E` itself is a normed group,
i.e. has no scalar action). -/

variables [normed_field ð•œ] [normed_space ð•œ E] [measurable_space ð•œ] [opens_measurable_space ð•œ]

/-- If `E` is a normed space, `Lp.simple_func E p Î¼` is a `has_scalar`. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def has_scalar : has_scalar ð•œ (Lp.simple_func E p Î¼) := âŸ¨Î»k f, âŸ¨k â€¢ f,
begin
  rcases f with âŸ¨f, âŸ¨s, hsâŸ©âŸ©,
  use k â€¢ s,
  apply eq.trans (smul_mk k s s.ae_measurable).symm _,
  rw hs,
  refl,
end âŸ©âŸ©

local attribute [instance] simple_func.has_scalar

@[simp, norm_cast] lemma coe_smul (c : ð•œ) (f : Lp.simple_func E p Î¼) :
  ((c â€¢ f : Lp.simple_func E p Î¼) : Lp E p Î¼) = c â€¢ (f : Lp E p Î¼) := rfl

/-- If `E` is a normed space, `Lp.simple_func E p Î¼` is a module. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def module : module ð•œ (Lp.simple_func E p Î¼) :=
{ one_smul  := Î»f, by { ext1, exact one_smul _ _ },
  mul_smul  := Î»x y f, by { ext1, exact mul_smul _ _ _ },
  smul_add  := Î»x f g, by { ext1, exact smul_add _ _ _ },
  smul_zero := Î»x, by { ext1, exact smul_zero _ },
  add_smul  := Î»x y f, by { ext1, exact add_smul _ _ _ },
  zero_smul := Î»f, by { ext1, exact zero_smul _ _ } }

local attribute [instance] simple_func.module

/-- If `E` is a normed space, `Lp.simple_func E p Î¼` is a normed space. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def normed_space [fact (1 â‰¤ p)] : normed_space ð•œ (Lp.simple_func E p Î¼) :=
âŸ¨ Î»c f, by { rw [coe_norm_subgroup, coe_norm_subgroup, coe_smul, norm_smul] } âŸ©

end instances

local attribute [instance] simple_func.module simple_func.normed_space

section to_Lp

/-- Construct the equivalence class `[f]` of a simple function `f` satisfying `mem_â„’p`. -/
@[reducible] def to_Lp (f : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) : (Lp.simple_func E p Î¼) :=
âŸ¨hf.to_Lp f, âŸ¨f, rflâŸ©âŸ©

lemma to_Lp_eq_to_Lp (f : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) :
  (to_Lp f hf : Lp E p Î¼) = hf.to_Lp f := rfl

lemma to_Lp_eq_mk (f : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) :
  (to_Lp f hf : Î± â†’â‚˜[Î¼] E) = ae_eq_fun.mk f f.ae_measurable := rfl

lemma to_Lp_zero : to_Lp (0 : Î± â†’â‚› E) zero_mem_â„’p = (0 : Lp.simple_func E p Î¼) := rfl

lemma to_Lp_add (f g : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) (hg : mem_â„’p g p Î¼) :
  to_Lp (f + g) (hf.add hg) = to_Lp f hf + to_Lp g hg := rfl

lemma to_Lp_neg (f : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) :
  to_Lp (-f) hf.neg = -to_Lp f hf := rfl

lemma to_Lp_sub (f g : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) (hg : mem_â„’p g p Î¼) :
  to_Lp (f - g) (hf.sub hg) = to_Lp f hf - to_Lp g hg :=
by { simp only [sub_eq_add_neg, â† to_Lp_neg, â† to_Lp_add], refl }

variables [normed_field ð•œ] [normed_space ð•œ E] [measurable_space ð•œ] [opens_measurable_space ð•œ]

lemma to_Lp_smul (f : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) (c : ð•œ) :
  to_Lp (c â€¢ f) (hf.const_smul c) = c â€¢ to_Lp f hf := rfl

lemma norm_to_Lp [fact (1 â‰¤ p)] (f : Î± â†’â‚› E) (hf : mem_â„’p f p Î¼) :
  âˆ¥to_Lp f hfâˆ¥ = ennreal.to_real (snorm f p Î¼) :=
norm_to_Lp f hf

end to_Lp

section to_simple_func

/-- Find a representative of a `Lp.simple_func`. -/
def to_simple_func (f : Lp.simple_func E p Î¼) : Î± â†’â‚› E := classical.some f.2

/-- `(to_simple_func f)` is measurable. -/
@[measurability]
protected lemma measurable (f : Lp.simple_func E p Î¼) : measurable (to_simple_func f) :=
(to_simple_func f).measurable

@[measurability]
protected lemma ae_measurable (f : Lp.simple_func E p Î¼) : ae_measurable (to_simple_func f) Î¼ :=
(simple_func.measurable f).ae_measurable

lemma to_simple_func_eq_to_fun (f : Lp.simple_func E p Î¼) : to_simple_func f =áµ[Î¼] f :=
show â‡‘(to_simple_func f) =áµ[Î¼] â‡‘(f : Î± â†’â‚˜[Î¼] E), by
begin
  convert (ae_eq_fun.coe_fn_mk (to_simple_func f) (simple_func.ae_measurable f)).symm using 2,
  exact (classical.some_spec f.2).symm,
end

/-- `to_simple_func f` satisfies the predicate `mem_â„’p`. -/
protected lemma mem_â„’p (f : Lp.simple_func E p Î¼) : mem_â„’p (to_simple_func f) p Î¼ :=
mem_â„’p.ae_eq (to_simple_func_eq_to_fun f).symm $ mem_Lp_iff_mem_â„’p.mp (f : Lp E p Î¼).2

lemma to_Lp_to_simple_func (f : Lp.simple_func E p Î¼) :
  to_Lp (to_simple_func f) (simple_func.mem_â„’p f) = f :=
simple_func.eq' (classical.some_spec f.2)

lemma to_simple_func_to_Lp (f : Î± â†’â‚› E) (hfi : mem_â„’p f p Î¼) :
  to_simple_func (to_Lp f hfi) =áµ[Î¼] f :=
by { rw â† mk_eq_mk, exact classical.some_spec (to_Lp f hfi).2 }

variables (E Î¼)
lemma zero_to_simple_func : to_simple_func (0 : Lp.simple_func E p Î¼) =áµ[Î¼] 0 :=
begin
  filter_upwards [to_simple_func_eq_to_fun (0 : Lp.simple_func E p Î¼), Lp.coe_fn_zero E 1 Î¼],
  assume a hâ‚ hâ‚‚,
  rwa hâ‚,
end
variables {E Î¼}

lemma add_to_simple_func (f g : Lp.simple_func E p Î¼) :
  to_simple_func (f + g) =áµ[Î¼] to_simple_func f + to_simple_func g :=
begin
  filter_upwards [to_simple_func_eq_to_fun (f + g), to_simple_func_eq_to_fun f,
    to_simple_func_eq_to_fun g, Lp.coe_fn_add (f :  Lp E p Î¼) g],
  assume a,
  simp only [â† coe_coe, add_subgroup.coe_add, pi.add_apply],
  iterate 4 { assume h, rw h }
end

lemma neg_to_simple_func (f : Lp.simple_func E p Î¼) :
  to_simple_func (-f) =áµ[Î¼] - to_simple_func f :=
begin
  filter_upwards [to_simple_func_eq_to_fun (-f), to_simple_func_eq_to_fun f,
    Lp.coe_fn_neg (f : Lp E p Î¼)],
  assume a,
  simp only [pi.neg_apply, add_subgroup.coe_neg, â† coe_coe],
  repeat { assume h, rw h }
end

lemma sub_to_simple_func (f g : Lp.simple_func E p Î¼) :
  to_simple_func (f - g) =áµ[Î¼] to_simple_func f - to_simple_func g :=
begin
  filter_upwards [to_simple_func_eq_to_fun (f - g), to_simple_func_eq_to_fun f,
    to_simple_func_eq_to_fun g, Lp.coe_fn_sub (f : Lp E p Î¼) g],
  assume a,
  simp only [add_subgroup.coe_sub, pi.sub_apply, â† coe_coe],
  repeat { assume h, rw h }
end

variables [normed_field ð•œ] [normed_space ð•œ E] [measurable_space ð•œ] [opens_measurable_space ð•œ]

lemma smul_to_simple_func (k : ð•œ) (f : Lp.simple_func E p Î¼) :
  to_simple_func (k â€¢ f) =áµ[Î¼] k â€¢ to_simple_func f :=
begin
  filter_upwards [to_simple_func_eq_to_fun (k â€¢ f), to_simple_func_eq_to_fun f,
    Lp.coe_fn_smul k (f : Lp E p Î¼)],
  assume a,
  simp only [pi.smul_apply, coe_smul, â† coe_coe],
  repeat { assume h, rw h }
end

lemma norm_to_simple_func [fact (1 â‰¤ p)] (f : Lp.simple_func E p Î¼) :
  âˆ¥fâˆ¥ = ennreal.to_real (snorm (to_simple_func f) p Î¼) :=
by simpa [to_Lp_to_simple_func] using norm_to_Lp (to_simple_func f) (simple_func.mem_â„’p f)

end to_simple_func

section induction

variables (p)

/-- The characteristic function of a finite-measure measurable set `s`, as an `Lp` simple function.
-/
def indicator_const {s : set Î±} (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆž) (c : E) :
  Lp.simple_func E p Î¼ :=
to_Lp ((simple_func.const _ c).piecewise s hs (simple_func.const _ 0))
  (mem_â„’p_indicator_const p hs c (or.inr hÎ¼s))

variables {p}

@[simp] lemma coe_indicator_const {s : set Î±} (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆž) (c : E) :
  (â†‘(indicator_const p hs hÎ¼s c) : Lp E p Î¼) = indicator_const_Lp p hs hÎ¼s c :=
rfl

lemma to_simple_func_indicator_const {s : set Î±} (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆž) (c : E) :
  to_simple_func (indicator_const p hs hÎ¼s c)
    =áµ[Î¼] (simple_func.const _ c).piecewise s hs (simple_func.const _ 0) :=
Lp.simple_func.to_simple_func_to_Lp _ _

/-- To prove something for an arbitrary `Lp` simple function, with `0 < p < âˆž`, it suffices to show
that the property holds for (multiples of) characteristic functions of finite-measure measurable
sets and is closed under addition (of functions with disjoint support). -/
@[elab_as_eliminator]
protected lemma induction (hp_pos : 0 < p) (hp_ne_top : p â‰  âˆž) {P : Lp.simple_func E p Î¼ â†’ Prop}
  (h_ind : âˆ€ (c : E) {s : set Î±} (hs : measurable_set s) (hÎ¼s : Î¼ s < âˆž),
    P (Lp.simple_func.indicator_const p hs hÎ¼s.ne c))
  (h_add : âˆ€ â¦ƒf g : Î± â†’â‚› Eâ¦„, âˆ€ hf : mem_â„’p f p Î¼, âˆ€ hg : mem_â„’p g p Î¼,
    disjoint (support f) (support g) â†’ P (Lp.simple_func.to_Lp f hf)
    â†’ P (Lp.simple_func.to_Lp g hg) â†’ P (Lp.simple_func.to_Lp f hf + Lp.simple_func.to_Lp g hg))
  (f : Lp.simple_func E p Î¼) : P f :=
begin
  suffices : âˆ€ f : Î± â†’â‚› E, âˆ€ hf : mem_â„’p f p Î¼, P (to_Lp f hf),
  { rw â† to_Lp_to_simple_func f,
    apply this }, clear f,
  refine simple_func.induction _ _,
  { intros c s hs hf,
    by_cases hc : c = 0,
    { convert h_ind 0 measurable_set.empty (by simp) using 1,
      ext1,
      simp [hc] },
    exact h_ind c hs (simple_func.measure_lt_top_of_mem_â„’p_indicator hp_pos hp_ne_top hc hs hf) },
  { intros f g hfg hf hg hfg',
    obtain âŸ¨hf', hg'âŸ© : mem_â„’p f p Î¼ âˆ§ mem_â„’p g p Î¼,
    { exact (mem_â„’p_add_of_disjoint hfg f.measurable g.measurable).mp hfg' },
    exact h_add hf' hg' hfg (hf hf') (hg hg') },
end

end induction

section coe_to_Lp

variables [fact (1 â‰¤ p)]

protected lemma uniform_continuous :
  uniform_continuous (coe : (Lp.simple_func E p Î¼) â†’ (Lp E p Î¼)) :=
uniform_continuous_comap

protected lemma uniform_embedding :
  uniform_embedding (coe : (Lp.simple_func E p Î¼) â†’ (Lp E p Î¼)) :=
uniform_embedding_comap subtype.val_injective

protected lemma uniform_inducing : uniform_inducing (coe : (Lp.simple_func E p Î¼) â†’ (Lp E p Î¼)) :=
simple_func.uniform_embedding.to_uniform_inducing

protected lemma dense_embedding (hp_ne_top : p â‰  âˆž) :
  dense_embedding (coe : (Lp.simple_func E p Î¼) â†’ (Lp E p Î¼)) :=
begin
  apply simple_func.uniform_embedding.dense_embedding,
  assume f,
  rw mem_closure_iff_seq_limit,
  have hfi' : mem_â„’p f p Î¼ := Lp.mem_â„’p f,
  refine âŸ¨Î» n, â†‘(to_Lp (simple_func.approx_on f (Lp.measurable f) univ 0 trivial n)
    (simple_func.mem_â„’p_approx_on_univ (Lp.measurable f) hfi' n)), Î» n, mem_range_self _, _âŸ©,
  convert simple_func.tendsto_approx_on_univ_Lp hp_ne_top (Lp.measurable f) hfi',
  rw to_Lp_coe_fn f (Lp.mem_â„’p f)
end

protected lemma dense_inducing (hp_ne_top : p â‰  âˆž) :
  dense_inducing (coe : (Lp.simple_func E p Î¼) â†’ (Lp E p Î¼)) :=
(simple_func.dense_embedding hp_ne_top).to_dense_inducing

protected lemma dense_range (hp_ne_top : p â‰  âˆž) :
  dense_range (coe : (Lp.simple_func E p Î¼) â†’ (Lp E p Î¼)) :=
(simple_func.dense_inducing hp_ne_top).dense

variables [normed_field ð•œ] [normed_space ð•œ E] [measurable_space ð•œ] [opens_measurable_space ð•œ]

variables (Î± E ð•œ)

/-- The embedding of Lp simple functions into Lp functions, as a continuous linear map. -/
def coe_to_Lp : (Lp.simple_func E p Î¼) â†’L[ð•œ] (Lp E p Î¼) :=
{ map_smul' := Î»k f, rfl,
  cont := Lp.simple_func.uniform_continuous.continuous,
  .. add_subgroup.subtype (Lp.simple_func E p Î¼) }

variables {Î± E ð•œ}

end coe_to_Lp

end simple_func

end Lp

variables [measurable_space Î±] [normed_group E] [measurable_space E] [borel_space E]
  [second_countable_topology E] {f : Î± â†’ E} {p : â„â‰¥0âˆž} {Î¼ : measure Î±}

/-- To prove something for an arbitrary `Lp` function in a second countable Borel normed group, it
suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in `Lp` for which the property holds is closed.
-/
@[elab_as_eliminator]
lemma Lp.induction [_i : fact (1 â‰¤ p)] (hp_ne_top : p â‰  âˆž) (P : Lp E p Î¼ â†’ Prop)
  (h_ind : âˆ€ (c : E) {s : set Î±} (hs : measurable_set s) (hÎ¼s : Î¼ s < âˆž),
      P (Lp.simple_func.indicator_const p hs hÎ¼s.ne c))
  (h_add : âˆ€ â¦ƒf gâ¦„, âˆ€ hf : mem_â„’p f p Î¼, âˆ€ hg : mem_â„’p g p Î¼, disjoint (support f) (support g) â†’
    P (hf.to_Lp f) â†’ P (hg.to_Lp g) â†’ P ((hf.to_Lp f) + (hg.to_Lp g)))
  (h_closed : is_closed {f : Lp E p Î¼ | P f}) :
  âˆ€ f : Lp E p Î¼, P f :=
begin
  refine Î» f, (Lp.simple_func.dense_range hp_ne_top).induction_on f h_closed _,
  refine Lp.simple_func.induction (lt_of_lt_of_le ennreal.zero_lt_one _i.elim) hp_ne_top _ _,
  { exact Î» c s, h_ind c },
  { exact Î» f g hf hg, h_add hf hg },
end

/-- To prove something for an arbitrary `mem_â„’p` function in a second countable
Borel normed group, it suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in the `Láµ–` space for which the property holds is closed.
* the property is closed under the almost-everywhere equal relation.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`).
-/
@[elab_as_eliminator]
lemma mem_â„’p.induction [_i : fact (1 â‰¤ p)] (hp_ne_top : p â‰  âˆž) (P : (Î± â†’ E) â†’ Prop)
  (h_ind : âˆ€ (c : E) â¦ƒsâ¦„, measurable_set s â†’ Î¼ s < âˆž â†’ P (s.indicator (Î» _, c)))
  (h_add : âˆ€ â¦ƒf g : Î± â†’ Eâ¦„, disjoint (support f) (support g) â†’ mem_â„’p f p Î¼ â†’ mem_â„’p g p Î¼ â†’
    P f â†’ P g â†’ P (f + g))
  (h_closed : is_closed {f : Lp E p Î¼ | P f} )
  (h_ae : âˆ€ â¦ƒf gâ¦„, f =áµ[Î¼] g â†’ mem_â„’p f p Î¼ â†’ P f â†’ P g) :
  âˆ€ â¦ƒf : Î± â†’ Eâ¦„ (hf : mem_â„’p f p Î¼), P f :=
begin
  have : âˆ€ (f : simple_func Î± E), mem_â„’p f p Î¼ â†’ P f,
  { refine simple_func.induction _ _,
    { intros c s hs h,
      by_cases hc : c = 0,
      { subst hc, convert h_ind 0 measurable_set.empty (by simp) using 1, ext, simp [const] },
      have hp_pos : 0 < p := lt_of_lt_of_le ennreal.zero_lt_one _i.elim,
      exact h_ind c hs (simple_func.measure_lt_top_of_mem_â„’p_indicator hp_pos hp_ne_top hc hs h) },
    { intros f g hfg hf hg int_fg,
      rw [simple_func.coe_add, mem_â„’p_add_of_disjoint hfg f.measurable g.measurable] at int_fg,
      refine h_add hfg int_fg.1 int_fg.2 (hf int_fg.1) (hg int_fg.2) } },
  have : âˆ€ (f : Lp.simple_func E p Î¼), P f,
  { intro f,
    exact h_ae (Lp.simple_func.to_simple_func_eq_to_fun f) (Lp.simple_func.mem_â„’p f)
      (this (Lp.simple_func.to_simple_func f) (Lp.simple_func.mem_â„’p f)) },
  have : âˆ€ (f : Lp E p Î¼), P f :=
    Î» f, (Lp.simple_func.dense_range hp_ne_top).induction_on f h_closed this,
  exact Î» f hf, h_ae hf.coe_fn_to_Lp (Lp.mem_â„’p _) (this (hf.to_Lp f)),
end

section integrable

local attribute [instance] fact_one_le_one_ennreal

notation Î± ` â†’â‚â‚›[`:25 Î¼ `] ` E := @measure_theory.Lp.simple_func Î± E _ _ _ _ _ 1 Î¼

lemma L1.simple_func.to_Lp_one_eq_to_L1 (f : Î± â†’â‚› E) (hf : integrable f Î¼) :
  (Lp.simple_func.to_Lp f (mem_â„’p_one_iff_integrable.2 hf) : Î± â†’â‚[Î¼] E) = hf.to_L1 f :=
rfl

protected lemma L1.simple_func.integrable (f : Î± â†’â‚â‚›[Î¼] E) :
  integrable (Lp.simple_func.to_simple_func f) Î¼ :=
by { rw â† mem_â„’p_one_iff_integrable, exact (Lp.simple_func.mem_â„’p f) }

/-- To prove something for an arbitrary integrable function in a second countable
Borel normed group, it suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in the `LÂ¹` space for which the property holds is closed.
* the property is closed under the almost-everywhere equal relation.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`).
-/
@[elab_as_eliminator]
lemma integrable.induction (P : (Î± â†’ E) â†’ Prop)
  (h_ind : âˆ€ (c : E) â¦ƒsâ¦„, measurable_set s â†’ Î¼ s < âˆž â†’ P (s.indicator (Î» _, c)))
  (h_add : âˆ€ â¦ƒf g : Î± â†’ Eâ¦„, disjoint (support f) (support g) â†’ integrable f Î¼ â†’ integrable g Î¼ â†’
    P f â†’ P g â†’ P (f + g))
  (h_closed : is_closed {f : Î± â†’â‚[Î¼] E | P f} )
  (h_ae : âˆ€ â¦ƒf gâ¦„, f =áµ[Î¼] g â†’ integrable f Î¼ â†’ P f â†’ P g) :
  âˆ€ â¦ƒf : Î± â†’ Eâ¦„ (hf : integrable f Î¼), P f :=
begin
  simp only [â† mem_â„’p_one_iff_integrable] at *,
  exact mem_â„’p.induction one_ne_top P h_ind h_add h_closed h_ae
end

end integrable

end measure_theory
