/-
Copyright (c) 2022 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, Heather Macbeth
-/
import measure_theory.function.l1_space
import measure_theory.function.simple_func_dense

/-!
# Density of simple functions

Show that each `L·µñ` Borel measurable function can be approximated in `L·µñ` norm
by a sequence of simple functions.

## Main definitions

* `measure_theory.Lp.simple_func`, the type of `Lp` simple functions
* `coe_to_Lp`, the embedding of `Lp.simple_func E p Œº` into `Lp E p Œº`

## Main results

* `tendsto_approx_on_univ_Lp` (L·µñ convergence): If `E` is a `normed_add_comm_group` and `f` is
  measurable and `mem_‚Ñíp` (for `p < ‚àû`), then the simple functions
  `simple_func.approx_on f hf s 0 h‚ÇÄ n` may be considered as elements of `Lp E p Œº`, and they tend
  in L·µñ to `f`.
* `Lp.simple_func.dense_embedding`: the embedding `coe_to_Lp` of the `Lp` simple functions into
  `Lp` is dense.
* `Lp.simple_func.induction`, `Lp.induction`, `mem_‚Ñíp.induction`, `integrable.induction`: to prove
  a predicate for all elements of one of these classes of functions, it suffices to check that it
  behaves correctly on simple functions.

## TODO

For `E` finite-dimensional, simple functions `Œ± ‚Üí‚Çõ E` are dense in L^‚àû -- prove this.

## Notations

* `Œ± ‚Üí‚Çõ Œ≤` (local notation): the type of simple functions `Œ± ‚Üí Œ≤`.
* `Œ± ‚Üí‚ÇÅ‚Çõ[Œº] E`: the type of `L1` simple functions `Œ± ‚Üí Œ≤`.
-/

noncomputable theory
open set function filter topological_space ennreal emetric finset
open_locale classical topology ennreal measure_theory big_operators
variables {Œ± Œ≤ Œπ E F ùïú : Type*}

namespace measure_theory

local infixr ` ‚Üí‚Çõ `:25 := simple_func

namespace simple_func

/-! ### Lp approximation by simple functions -/

section Lp
variables [measurable_space Œ≤] [measurable_space E] [normed_add_comm_group E]
  [normed_add_comm_group F] {q : ‚Ñù} {p : ‚Ñù‚â•0‚àû}

lemma nnnorm_approx_on_le [opens_measurable_space E] {f : Œ≤ ‚Üí E} (hf : measurable f)
  {s : set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s] (x : Œ≤) (n : ‚Ñï) :
  ‚Äñapprox_on f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä ‚â§ ‚Äñf x - y‚ÇÄ‚Äñ‚Çä :=
begin
  have := edist_approx_on_le hf h‚ÇÄ x n,
  rw edist_comm y‚ÇÄ at this,
  simp only [edist_nndist, nndist_eq_nnnorm] at this,
  exact_mod_cast this
end

lemma norm_approx_on_y‚ÇÄ_le [opens_measurable_space E] {f : Œ≤ ‚Üí E} (hf : measurable f)
  {s : set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s] (x : Œ≤) (n : ‚Ñï) :
  ‚Äñapprox_on f hf s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ‚Äñ ‚â§ ‚Äñf x - y‚ÇÄ‚Äñ + ‚Äñf x - y‚ÇÄ‚Äñ :=
begin
  have := edist_approx_on_y0_le hf h‚ÇÄ x n,
  repeat { rw [edist_comm y‚ÇÄ, edist_eq_coe_nnnorm_sub] at this },
  exact_mod_cast this,
end

lemma norm_approx_on_zero_le [opens_measurable_space E] {f : Œ≤ ‚Üí E} (hf : measurable f)
  {s : set E} (h‚ÇÄ : (0 : E) ‚àà s) [separable_space s] (x : Œ≤) (n : ‚Ñï) :
  ‚Äñapprox_on f hf s 0 h‚ÇÄ n x‚Äñ ‚â§ ‚Äñf x‚Äñ + ‚Äñf x‚Äñ :=
begin
  have := edist_approx_on_y0_le hf h‚ÇÄ x n,
  simp [edist_comm (0 : E), edist_eq_coe_nnnorm] at this,
  exact_mod_cast this,
end

lemma tendsto_approx_on_Lp_snorm [opens_measurable_space E]
  {f : Œ≤ ‚Üí E} (hf : measurable f) {s : set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s]
  (hp_ne_top : p ‚â† ‚àû) {Œº : measure Œ≤} (hŒº : ‚àÄ·µê x ‚àÇŒº, f x ‚àà closure s)
  (hi : snorm (Œª x, f x - y‚ÇÄ) p Œº < ‚àû) :
  tendsto (Œª n, snorm (approx_on f hf s y‚ÇÄ h‚ÇÄ n - f) p Œº) at_top (ùìù 0) :=
begin
  by_cases hp_zero : p = 0,
  { simpa only [hp_zero, snorm_exponent_zero] using tendsto_const_nhds },
  have hp : 0 < p.to_real := to_real_pos hp_zero hp_ne_top,
  suffices : tendsto (Œª n, ‚à´‚Åª x, ‚Äñapprox_on f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä ^ p.to_real ‚àÇŒº) at_top (ùìù 0),
  { simp only [snorm_eq_lintegral_rpow_nnnorm hp_zero hp_ne_top],
    convert continuous_rpow_const.continuous_at.tendsto.comp this;
    simp [_root_.inv_pos.mpr hp] },
  -- We simply check the conditions of the Dominated Convergence Theorem:
  -- (1) The function "`p`-th power of distance between `f` and the approximation" is measurable
  have hF_meas : ‚àÄ n, measurable (Œª x, (‚Äñapprox_on f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.to_real),
  { simpa only [‚Üê edist_eq_coe_nnnorm_sub] using
      Œª n, (approx_on f hf s y‚ÇÄ h‚ÇÄ n).measurable_bind (Œª y x, (edist y (f x)) ^ p.to_real)
      (Œª y, (measurable_edist_right.comp hf).pow_const p.to_real) },
  -- (2) The functions "`p`-th power of distance between `f` and the approximation" are uniformly
  -- bounded, at any given point, by `Œª x, ‚Äñf x - y‚ÇÄ‚Äñ ^ p.to_real`
  have h_bound : ‚àÄ n, (Œª x, (‚Äñapprox_on f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.to_real)
      ‚â§·µê[Œº] (Œª x, ‚Äñf x - y‚ÇÄ‚Äñ‚Çä ^ p.to_real),
  { exact Œª n, eventually_of_forall
      (Œª x, rpow_le_rpow (coe_mono (nnnorm_approx_on_le hf h‚ÇÄ x n)) to_real_nonneg) },
  -- (3) The bounding function `Œª x, ‚Äñf x - y‚ÇÄ‚Äñ ^ p.to_real` has finite integral
  have h_fin :  ‚à´‚Åª (a : Œ≤), ‚Äñf a - y‚ÇÄ‚Äñ‚Çä ^ p.to_real ‚àÇŒº ‚â† ‚ä§,
    from (lintegral_rpow_nnnorm_lt_top_of_snorm_lt_top hp_zero hp_ne_top hi).ne,
  -- (4) The functions "`p`-th power of distance between `f` and the approximation" tend pointwise
  -- to zero
  have h_lim : ‚àÄ·µê (a : Œ≤) ‚àÇŒº,
    tendsto (Œª n, (‚Äñapprox_on f hf s y‚ÇÄ h‚ÇÄ n a - f a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p.to_real) at_top (ùìù 0),
  { filter_upwards [hŒº] with a ha,
    have : tendsto (Œª n, (approx_on f hf s y‚ÇÄ h‚ÇÄ n) a - f a) at_top (ùìù (f a - f a)),
    { exact (tendsto_approx_on hf h‚ÇÄ ha).sub tendsto_const_nhds },
    convert continuous_rpow_const.continuous_at.tendsto.comp (tendsto_coe.mpr this.nnnorm),
    simp [zero_rpow_of_pos hp] },
  -- Then we apply the Dominated Convergence Theorem
  simpa using tendsto_lintegral_of_dominated_convergence _ hF_meas h_bound h_fin h_lim,
end

lemma mem_‚Ñíp_approx_on [borel_space E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f) (hf : mem_‚Ñíp f p Œº) {s : set E} {y‚ÇÄ : E}
  (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s] (hi‚ÇÄ : mem_‚Ñíp (Œª x, y‚ÇÄ) p Œº) (n : ‚Ñï) :
  mem_‚Ñíp (approx_on f fmeas s y‚ÇÄ h‚ÇÄ n) p Œº :=
begin
  refine ‚ü®(approx_on f fmeas s y‚ÇÄ h‚ÇÄ n).ae_strongly_measurable, _‚ü©,
  suffices : snorm (Œª x, approx_on f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ) p Œº < ‚ä§,
  { have : mem_‚Ñíp (Œª x, approx_on f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ) p Œº :=
      ‚ü®(approx_on f fmeas s y‚ÇÄ h‚ÇÄ n - const Œ≤ y‚ÇÄ).ae_strongly_measurable, this‚ü©,
    convert snorm_add_lt_top this hi‚ÇÄ,
    ext x,
    simp },
  have hf' : mem_‚Ñíp (Œª x, ‚Äñf x - y‚ÇÄ‚Äñ) p Œº,
  { have h_meas : measurable (Œª x, ‚Äñf x - y‚ÇÄ‚Äñ),
    { simp only [‚Üê dist_eq_norm],
      exact (continuous_id.dist continuous_const).measurable.comp fmeas },
    refine ‚ü®h_meas.ae_measurable.ae_strongly_measurable, _‚ü©,
    rw snorm_norm,
    convert snorm_add_lt_top hf hi‚ÇÄ.neg,
    ext x,
    simp [sub_eq_add_neg] },
  have : ‚àÄ·µê x ‚àÇŒº, ‚Äñapprox_on f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ‚Äñ ‚â§ ‚Äñ(‚Äñf x - y‚ÇÄ‚Äñ + ‚Äñf x - y‚ÇÄ‚Äñ)‚Äñ,
  { refine eventually_of_forall _,
    intros x,
    convert norm_approx_on_y‚ÇÄ_le fmeas h‚ÇÄ x n,
    rw [real.norm_eq_abs, abs_of_nonneg],
    exact add_nonneg (norm_nonneg _) (norm_nonneg _) },
  calc snorm (Œª x, approx_on f fmeas s y‚ÇÄ h‚ÇÄ n x - y‚ÇÄ) p Œº
      ‚â§ snorm (Œª x, ‚Äñf x - y‚ÇÄ‚Äñ + ‚Äñf x - y‚ÇÄ‚Äñ) p Œº : snorm_mono_ae this
  ... < ‚ä§ : snorm_add_lt_top hf' hf',
end

lemma tendsto_approx_on_range_Lp_snorm [borel_space E]
  {f : Œ≤ ‚Üí E} (hp_ne_top : p ‚â† ‚àû) {Œº : measure Œ≤} (fmeas : measurable f)
  [separable_space (range f ‚à™ {0} : set E)]
  (hf : snorm f p Œº < ‚àû) :
  tendsto (Œª n, snorm (approx_on f fmeas (range f ‚à™ {0}) 0 (by simp) n - f) p Œº) at_top (ùìù 0) :=
begin
  refine tendsto_approx_on_Lp_snorm fmeas _ hp_ne_top _ _,
  { apply eventually_of_forall,
    assume x,
    apply subset_closure,
    simp },
  { simpa using hf }
end

lemma mem_‚Ñíp_approx_on_range [borel_space E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f) [separable_space (range f ‚à™ {0} : set E)]
  (hf : mem_‚Ñíp f p Œº) (n : ‚Ñï) :
  mem_‚Ñíp (approx_on f fmeas (range f ‚à™ {0}) 0 (by simp) n) p Œº :=
mem_‚Ñíp_approx_on fmeas hf (by simp) zero_mem_‚Ñíp n

lemma tendsto_approx_on_range_Lp [borel_space E]
  {f : Œ≤ ‚Üí E} [hp : fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû) {Œº : measure Œ≤} (fmeas : measurable f)
  [separable_space (range f ‚à™ {0} : set E)] (hf : mem_‚Ñíp f p Œº) :
  tendsto (Œª n, (mem_‚Ñíp_approx_on_range fmeas hf n).to_Lp
    (approx_on f fmeas (range f ‚à™ {0}) 0 (by simp) n))
      at_top (ùìù (hf.to_Lp f)) :=
by simpa only [Lp.tendsto_Lp_iff_tendsto_‚Ñíp'']
  using tendsto_approx_on_range_Lp_snorm hp_ne_top fmeas hf.2

/-- Any function in `‚Ñíp` can be approximated by a simple function if `p < ‚àû`. -/
lemma _root_.measure_theory.mem_‚Ñíp.exists_simple_func_snorm_sub_lt
  {E : Type*} [normed_add_comm_group E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (hf : mem_‚Ñíp f p Œº) (hp_ne_top : p ‚â† ‚àû) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
  ‚àÉ (g : Œ≤ ‚Üí‚Çõ E), snorm (f - g) p Œº < Œµ ‚àß mem_‚Ñíp g p Œº :=
begin
  borelize E,
  let f' := hf.1.mk f,
  suffices H : ‚àÉ (g : Œ≤ ‚Üí‚Çõ E), snorm (f' - g) p Œº < Œµ ‚àß mem_‚Ñíp g p Œº,
  { rcases H with ‚ü®g, hg, g_mem‚ü©,
    refine ‚ü®g, _, g_mem‚ü©,
    convert hg using 1,
    apply snorm_congr_ae,
    filter_upwards [hf.1.ae_eq_mk] with x hx,
    simpa only [pi.sub_apply, sub_left_inj] using hx },
  have hf' : mem_‚Ñíp f' p Œº, from hf.ae_eq hf.1.ae_eq_mk,
  have f'meas : measurable f' := hf.1.measurable_mk,
  haveI : separable_space (range f' ‚à™ {0} : set E),
    from strongly_measurable.separable_space_range_union_singleton hf.1.strongly_measurable_mk,
  rcases ((tendsto_order.1 (tendsto_approx_on_range_Lp_snorm hp_ne_top f'meas hf'.2)).2
    Œµ hŒµ.bot_lt).exists with ‚ü®n, hn‚ü©,
  rw [‚Üê snorm_neg, neg_sub] at hn,
  exact ‚ü®_, hn, mem_‚Ñíp_approx_on_range f'meas hf' _‚ü©,
end

end Lp

/-! ### L1 approximation by simple functions -/

section integrable
variables [measurable_space Œ≤]
variables [measurable_space E] [normed_add_comm_group E]

lemma tendsto_approx_on_L1_nnnorm [opens_measurable_space E]
  {f : Œ≤ ‚Üí E} (hf : measurable f) {s : set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s) [separable_space s]
  {Œº : measure Œ≤} (hŒº : ‚àÄ·µê x ‚àÇŒº, f x ‚àà closure s) (hi : has_finite_integral (Œª x, f x - y‚ÇÄ) Œº) :
  tendsto (Œª n, ‚à´‚Åª x, ‚Äñapprox_on f hf s y‚ÇÄ h‚ÇÄ n x - f x‚Äñ‚Çä ‚àÇŒº) at_top (ùìù 0) :=
by simpa [snorm_one_eq_lintegral_nnnorm] using tendsto_approx_on_Lp_snorm hf h‚ÇÄ one_ne_top hŒº
  (by simpa [snorm_one_eq_lintegral_nnnorm] using hi)

lemma integrable_approx_on [borel_space E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f) (hf : integrable f Œº)
  {s : set E} {y‚ÇÄ : E} (h‚ÇÄ : y‚ÇÄ ‚àà s)
  [separable_space s] (hi‚ÇÄ : integrable (Œª x, y‚ÇÄ) Œº) (n : ‚Ñï) :
  integrable (approx_on f fmeas s y‚ÇÄ h‚ÇÄ n) Œº :=
begin
  rw ‚Üê mem_‚Ñíp_one_iff_integrable at hf hi‚ÇÄ ‚ä¢,
  exact mem_‚Ñíp_approx_on fmeas hf h‚ÇÄ hi‚ÇÄ n,
end

lemma tendsto_approx_on_range_L1_nnnorm [opens_measurable_space E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} [separable_space (range f ‚à™ {0} : set E)]
  (fmeas : measurable f) (hf : integrable f Œº) :
  tendsto (Œª n, ‚à´‚Åª x, ‚Äñapprox_on f fmeas (range f ‚à™ {0}) 0 (by simp) n x - f x‚Äñ‚Çä ‚àÇŒº)
    at_top (ùìù 0) :=
begin
  apply tendsto_approx_on_L1_nnnorm fmeas,
  { apply eventually_of_forall,
    assume x,
    apply subset_closure,
    simp },
  { simpa using hf.2 }
end

lemma integrable_approx_on_range [borel_space E]
  {f : Œ≤ ‚Üí E} {Œº : measure Œ≤} (fmeas : measurable f)
  [separable_space (range f ‚à™ {0} : set E)] (hf : integrable f Œº) (n : ‚Ñï) :
  integrable (approx_on f fmeas (range f ‚à™ {0}) 0 (by simp) n) Œº :=
integrable_approx_on fmeas hf _ (integrable_zero _ _ _) n

end integrable

section simple_func_properties

variables [measurable_space Œ±]
variables [normed_add_comm_group E] [normed_add_comm_group F]
variables {Œº : measure Œ±} {p : ‚Ñù‚â•0‚àû}

/-!
### Properties of simple functions in `Lp` spaces

A simple function `f : Œ± ‚Üí‚Çõ E` into a normed group `E` verifies, for a measure `Œº`:
- `mem_‚Ñíp f 0 Œº` and `mem_‚Ñíp f ‚àû Œº`, since `f` is a.e.-measurable and bounded,
- for `0 < p < ‚àû`,
  `mem_‚Ñíp f p Œº ‚Üî integrable f Œº ‚Üî f.fin_meas_supp Œº ‚Üî ‚àÄ y ‚â† 0, Œº (f ‚Åª¬π' {y}) < ‚àû`.
-/

lemma exists_forall_norm_le (f : Œ± ‚Üí‚Çõ F) : ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C :=
exists_forall_le (f.map (Œª x, ‚Äñx‚Äñ))

lemma mem_‚Ñíp_zero (f : Œ± ‚Üí‚Çõ E) (Œº : measure Œ±) : mem_‚Ñíp f 0 Œº :=
mem_‚Ñíp_zero_iff_ae_strongly_measurable.mpr f.ae_strongly_measurable

lemma mem_‚Ñíp_top (f : Œ± ‚Üí‚Çõ E) (Œº : measure Œ±) : mem_‚Ñíp f ‚àû Œº :=
let ‚ü®C, hfC‚ü© := f.exists_forall_norm_le in
mem_‚Ñíp_top_of_bound f.ae_strongly_measurable C $ eventually_of_forall hfC

protected lemma snorm'_eq {p : ‚Ñù} (f : Œ± ‚Üí‚Çõ F) (Œº : measure Œ±) :
  snorm' f p Œº = (‚àë y in f.range, (‚Äñy‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p * Œº (f ‚Åª¬π' {y})) ^ (1/p) :=
have h_map : (Œª a, (‚Äñf a‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p) = f.map (Œª a : F, (‚Äña‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ^ p), by simp,
by rw [snorm', h_map, lintegral_eq_lintegral, map_lintegral]

lemma measure_preimage_lt_top_of_mem_‚Ñíp (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) (f : Œ± ‚Üí‚Çõ E)
  (hf : mem_‚Ñíp f p Œº) (y : E) (hy_ne : y ‚â† 0) :
  Œº (f ‚Åª¬π' {y}) < ‚àû :=
begin
  have hp_pos_real : 0 < p.to_real, from ennreal.to_real_pos hp_pos hp_ne_top,
  have hf_snorm := mem_‚Ñíp.snorm_lt_top hf,
  rw [snorm_eq_snorm' hp_pos hp_ne_top, f.snorm'_eq,
    ‚Üê @ennreal.lt_rpow_one_div_iff _ _ (1 / p.to_real) (by simp [hp_pos_real]),
    @ennreal.top_rpow_of_pos (1 / (1 / p.to_real)) (by simp [hp_pos_real]),
    ennreal.sum_lt_top_iff] at hf_snorm,
  by_cases hyf : y ‚àà f.range,
  swap,
  { suffices h_empty : f ‚Åª¬π' {y} = ‚àÖ,
      by { rw [h_empty, measure_empty], exact ennreal.coe_lt_top, },
    ext1 x,
    rw [set.mem_preimage, set.mem_singleton_iff, mem_empty_iff_false, iff_false],
    refine Œª hxy, hyf _,
    rw [mem_range, set.mem_range],
    exact ‚ü®x, hxy‚ü©, },
  specialize hf_snorm y hyf,
  rw ennreal.mul_lt_top_iff at hf_snorm,
  cases hf_snorm,
  { exact hf_snorm.2, },
  cases hf_snorm,
  { refine absurd _ hy_ne,
    simpa [hp_pos_real] using hf_snorm, },
  { simp [hf_snorm], },
end

lemma mem_‚Ñíp_of_finite_measure_preimage (p : ‚Ñù‚â•0‚àû) {f : Œ± ‚Üí‚Çõ E} (hf : ‚àÄ y ‚â† 0, Œº (f ‚Åª¬π' {y}) < ‚àû) :
  mem_‚Ñíp f p Œº :=
begin
  by_cases hp0 : p = 0,
  { rw [hp0, mem_‚Ñíp_zero_iff_ae_strongly_measurable], exact f.ae_strongly_measurable, },
  by_cases hp_top : p = ‚àû,
  { rw hp_top, exact mem_‚Ñíp_top f Œº, },
  refine ‚ü®f.ae_strongly_measurable, _‚ü©,
  rw [snorm_eq_snorm' hp0 hp_top, f.snorm'_eq],
  refine ennreal.rpow_lt_top_of_nonneg (by simp) (ennreal.sum_lt_top_iff.mpr (Œª y hy, _)).ne,
  by_cases hy0 : y = 0,
  { simp [hy0, ennreal.to_real_pos hp0 hp_top], },
  { refine ennreal.mul_lt_top _ (hf y hy0).ne,
    exact (ennreal.rpow_lt_top_of_nonneg ennreal.to_real_nonneg ennreal.coe_ne_top).ne },
end

lemma mem_‚Ñíp_iff {f : Œ± ‚Üí‚Çõ E} (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
  mem_‚Ñíp f p Œº ‚Üî ‚àÄ y ‚â† 0, Œº (f ‚Åª¬π' {y}) < ‚àû :=
‚ü®Œª h, measure_preimage_lt_top_of_mem_‚Ñíp hp_pos hp_ne_top f h,
  Œª h, mem_‚Ñíp_of_finite_measure_preimage p h‚ü©

lemma integrable_iff {f : Œ± ‚Üí‚Çõ E} : integrable f Œº ‚Üî ‚àÄ y ‚â† 0, Œº (f ‚Åª¬π' {y}) < ‚àû :=
mem_‚Ñíp_one_iff_integrable.symm.trans $ mem_‚Ñíp_iff one_ne_zero ennreal.coe_ne_top

lemma mem_‚Ñíp_iff_integrable {f : Œ± ‚Üí‚Çõ E} (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
  mem_‚Ñíp f p Œº ‚Üî integrable f Œº :=
(mem_‚Ñíp_iff hp_pos hp_ne_top).trans integrable_iff.symm

lemma mem_‚Ñíp_iff_fin_meas_supp {f : Œ± ‚Üí‚Çõ E} (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) :
  mem_‚Ñíp f p Œº ‚Üî f.fin_meas_supp Œº :=
(mem_‚Ñíp_iff hp_pos hp_ne_top).trans fin_meas_supp_iff.symm

lemma integrable_iff_fin_meas_supp {f : Œ± ‚Üí‚Çõ E} : integrable f Œº ‚Üî f.fin_meas_supp Œº :=
integrable_iff.trans fin_meas_supp_iff.symm

lemma fin_meas_supp.integrable {f : Œ± ‚Üí‚Çõ E} (h : f.fin_meas_supp Œº) : integrable f Œº :=
integrable_iff_fin_meas_supp.2 h

lemma integrable_pair {f : Œ± ‚Üí‚Çõ E} {g : Œ± ‚Üí‚Çõ F} :
  integrable f Œº ‚Üí integrable g Œº ‚Üí integrable (pair f g) Œº :=
by simpa only [integrable_iff_fin_meas_supp] using fin_meas_supp.pair

lemma mem_‚Ñíp_of_is_finite_measure (f : Œ± ‚Üí‚Çõ E) (p : ‚Ñù‚â•0‚àû) (Œº : measure Œ±) [is_finite_measure Œº] :
  mem_‚Ñíp f p Œº :=
let ‚ü®C, hfC‚ü© := f.exists_forall_norm_le in
mem_‚Ñíp.of_bound f.ae_strongly_measurable C $ eventually_of_forall hfC

lemma integrable_of_is_finite_measure [is_finite_measure Œº] (f : Œ± ‚Üí‚Çõ E) : integrable f Œº :=
mem_‚Ñíp_one_iff_integrable.mp (f.mem_‚Ñíp_of_is_finite_measure 1 Œº)

lemma measure_preimage_lt_top_of_integrable (f : Œ± ‚Üí‚Çõ E) (hf : integrable f Œº) {x : E}
  (hx : x ‚â† 0) :
  Œº (f ‚Åª¬π' {x}) < ‚àû :=
integrable_iff.mp hf x hx

lemma measure_support_lt_top [has_zero Œ≤] (f : Œ± ‚Üí‚Çõ Œ≤) (hf : ‚àÄ y ‚â† 0, Œº (f ‚Åª¬π' {y}) < ‚àû) :
  Œº (support f) < ‚àû :=
begin
  rw support_eq,
  refine (measure_bUnion_finset_le _ _).trans_lt (ennreal.sum_lt_top_iff.mpr (Œª y hy, _)),
  rw finset.mem_filter at hy,
  exact hf y hy.2,
end

lemma measure_support_lt_top_of_mem_‚Ñíp (f : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) (hp_ne_zero : p ‚â† 0)
  (hp_ne_top : p ‚â† ‚àû) :
  Œº (support f) < ‚àû :=
f.measure_support_lt_top ((mem_‚Ñíp_iff hp_ne_zero hp_ne_top).mp hf)

lemma measure_support_lt_top_of_integrable (f : Œ± ‚Üí‚Çõ E) (hf : integrable f Œº) :
  Œº (support f) < ‚àû :=
f.measure_support_lt_top (integrable_iff.mp hf)

lemma measure_lt_top_of_mem_‚Ñíp_indicator (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) {c : E} (hc : c ‚â† 0)
  {s : set Œ±} (hs : measurable_set s)
  (hcs : mem_‚Ñíp ((const Œ± c).piecewise s hs (const Œ± 0)) p Œº) :
  Œº s < ‚ä§ :=
begin
  have : function.support (const Œ± c) = set.univ := function.support_const hc,
  simpa only [mem_‚Ñíp_iff_fin_meas_supp hp_pos hp_ne_top, fin_meas_supp_iff_support,
    support_indicator, set.inter_univ, this] using hcs
end

end simple_func_properties

end simple_func

/-! Construction of the space of `Lp` simple functions, and its dense embedding into `Lp`. -/
namespace Lp

open ae_eq_fun

variables [measurable_space Œ±] [normed_add_comm_group E] [normed_add_comm_group F] (p : ‚Ñù‚â•0‚àû)
  (Œº : measure Œ±)

variables (E)

/-- `Lp.simple_func` is a subspace of Lp consisting of equivalence classes of an integrable simple
    function. -/
def simple_func : add_subgroup (Lp E p Œº) :=
{ carrier := {f : Lp E p Œº |
                ‚àÉ (s : Œ± ‚Üí‚Çõ E), (ae_eq_fun.mk s s.ae_strongly_measurable : Œ± ‚Üí‚Çò[Œº] E) = f},
  zero_mem' := ‚ü®0, rfl‚ü©,
  add_mem' := Œª f g ‚ü®s, hs‚ü© ‚ü®t, ht‚ü©, ‚ü®s + t,
      by simp only [‚Üêhs, ‚Üêht, ae_eq_fun.mk_add_mk, add_subgroup.coe_add, ae_eq_fun.mk_eq_mk,
        simple_func.coe_add]‚ü©,
  neg_mem' := Œª f ‚ü®s, hs‚ü©, ‚ü®-s,
      by simp only [‚Üêhs, ae_eq_fun.neg_mk, simple_func.coe_neg, ae_eq_fun.mk_eq_mk,
        add_subgroup.coe_neg]‚ü© }

variables {E p Œº}

namespace simple_func

section instances
/-! Simple functions in Lp space form a `normed_space`. -/

@[norm_cast] lemma coe_coe (f : Lp.simple_func E p Œº) : ‚áë(f : Lp E p Œº) = f := rfl

protected lemma eq' {f g : Lp.simple_func E p Œº} : (f : Œ± ‚Üí‚Çò[Œº] E) = (g : Œ± ‚Üí‚Çò[Œº] E) ‚Üí f = g :=
subtype.eq ‚àò subtype.eq

/-! Implementation note:  If `Lp.simple_func E p Œº` were defined as a `ùïú`-submodule of `Lp E p Œº`,
then the next few lemmas, putting a normed `ùïú`-group structure on `Lp.simple_func E p Œº`, would be
unnecessary.  But instead, `Lp.simple_func E p Œº` is defined as an `add_subgroup` of `Lp E p Œº`,
which does not permit this (but has the advantage of working when `E` itself is a normed group,
i.e. has no scalar action). -/

variables [normed_field ùïú] [normed_space ùïú E]

/-- If `E` is a normed space, `Lp.simple_func E p Œº` is a `has_smul`. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def has_smul : has_smul ùïú (Lp.simple_func E p Œº) := ‚ü®Œª k f, ‚ü®k ‚Ä¢ f,
begin
  rcases f with ‚ü®f, ‚ü®s, hs‚ü©‚ü©,
  use k ‚Ä¢ s,
  apply eq.trans (ae_eq_fun.smul_mk k s s.ae_strongly_measurable).symm _,
  rw hs,
  refl,
end ‚ü©‚ü©

local attribute [instance] simple_func.has_smul

@[simp, norm_cast] lemma coe_smul (c : ùïú) (f : Lp.simple_func E p Œº) :
  ((c ‚Ä¢ f : Lp.simple_func E p Œº) : Lp E p Œº) = c ‚Ä¢ (f : Lp E p Œº) := rfl

/-- If `E` is a normed space, `Lp.simple_func E p Œº` is a module. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def module : module ùïú (Lp.simple_func E p Œº) :=
{ one_smul  := Œªf, by { ext1, exact one_smul _ _ },
  mul_smul  := Œªx y f, by { ext1, exact mul_smul _ _ _ },
  smul_add  := Œªx f g, by { ext1, exact smul_add _ _ _ },
  smul_zero := Œªx, by { ext1, exact smul_zero _ },
  add_smul  := Œªx y f, by { ext1, exact add_smul _ _ _ },
  zero_smul := Œªf, by { ext1, exact zero_smul _ _ } }

local attribute [instance] simple_func.module

/-- If `E` is a normed space, `Lp.simple_func E p Œº` is a normed space. Not declared as an
instance as it is (as of writing) used only in the construction of the Bochner integral. -/
protected def normed_space [fact (1 ‚â§ p)] : normed_space ùïú (Lp.simple_func E p Œº) :=
‚ü® Œªc f, by { rw [add_subgroup.coe_norm, add_subgroup.coe_norm, coe_smul, norm_smul] } ‚ü©

end instances

local attribute [instance] simple_func.module simple_func.normed_space

section to_Lp

/-- Construct the equivalence class `[f]` of a simple function `f` satisfying `mem_‚Ñíp`. -/
@[reducible] def to_Lp (f : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) : (Lp.simple_func E p Œº) :=
‚ü®hf.to_Lp f, ‚ü®f, rfl‚ü©‚ü©

lemma to_Lp_eq_to_Lp (f : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) :
  (to_Lp f hf : Lp E p Œº) = hf.to_Lp f := rfl

lemma to_Lp_eq_mk (f : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) :
  (to_Lp f hf : Œ± ‚Üí‚Çò[Œº] E) = ae_eq_fun.mk f f.ae_strongly_measurable := rfl

lemma to_Lp_zero : to_Lp (0 : Œ± ‚Üí‚Çõ E) zero_mem_‚Ñíp = (0 : Lp.simple_func E p Œº) := rfl

lemma to_Lp_add (f g : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) (hg : mem_‚Ñíp g p Œº) :
  to_Lp (f + g) (hf.add hg) = to_Lp f hf + to_Lp g hg := rfl

lemma to_Lp_neg (f : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) :
  to_Lp (-f) hf.neg = -to_Lp f hf := rfl

lemma to_Lp_sub (f g : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) (hg : mem_‚Ñíp g p Œº) :
  to_Lp (f - g) (hf.sub hg) = to_Lp f hf - to_Lp g hg :=
by { simp only [sub_eq_add_neg, ‚Üê to_Lp_neg, ‚Üê to_Lp_add], refl }

variables [normed_field ùïú] [normed_space ùïú E]

lemma to_Lp_smul (f : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) (c : ùïú) :
  to_Lp (c ‚Ä¢ f) (hf.const_smul c) = c ‚Ä¢ to_Lp f hf := rfl

lemma norm_to_Lp [fact (1 ‚â§ p)] (f : Œ± ‚Üí‚Çõ E) (hf : mem_‚Ñíp f p Œº) :
  ‚Äñto_Lp f hf‚Äñ = ennreal.to_real (snorm f p Œº) :=
norm_to_Lp f hf

end to_Lp

section to_simple_func

/-- Find a representative of a `Lp.simple_func`. -/
def to_simple_func (f : Lp.simple_func E p Œº) : Œ± ‚Üí‚Çõ E := classical.some f.2

/-- `(to_simple_func f)` is measurable. -/
@[measurability]
protected lemma measurable [measurable_space E] (f : Lp.simple_func E p Œº) :
  measurable (to_simple_func f) :=
(to_simple_func f).measurable

protected lemma strongly_measurable (f : Lp.simple_func E p Œº) :
  strongly_measurable (to_simple_func f) :=
(to_simple_func f).strongly_measurable

@[measurability]
protected lemma ae_measurable [measurable_space E] (f : Lp.simple_func E p Œº) :
  ae_measurable (to_simple_func f) Œº :=
(simple_func.measurable f).ae_measurable

protected lemma ae_strongly_measurable (f : Lp.simple_func E p Œº) :
  ae_strongly_measurable (to_simple_func f) Œº :=
(simple_func.strongly_measurable f).ae_strongly_measurable

lemma to_simple_func_eq_to_fun (f : Lp.simple_func E p Œº) : to_simple_func f =·µê[Œº] f :=
show ‚áë(to_simple_func f) =·µê[Œº] ‚áë(f : Œ± ‚Üí‚Çò[Œº] E),
begin
  convert (ae_eq_fun.coe_fn_mk (to_simple_func f) (to_simple_func f).ae_strongly_measurable).symm
    using 2,
  exact (classical.some_spec f.2).symm,
end

/-- `to_simple_func f` satisfies the predicate `mem_‚Ñíp`. -/
protected lemma mem_‚Ñíp (f : Lp.simple_func E p Œº) : mem_‚Ñíp (to_simple_func f) p Œº :=
mem_‚Ñíp.ae_eq (to_simple_func_eq_to_fun f).symm $ mem_Lp_iff_mem_‚Ñíp.mp (f : Lp E p Œº).2

lemma to_Lp_to_simple_func (f : Lp.simple_func E p Œº) :
  to_Lp (to_simple_func f) (simple_func.mem_‚Ñíp f) = f :=
simple_func.eq' (classical.some_spec f.2)

lemma to_simple_func_to_Lp (f : Œ± ‚Üí‚Çõ E) (hfi : mem_‚Ñíp f p Œº) :
  to_simple_func (to_Lp f hfi) =·µê[Œº] f :=
by { rw ‚Üê ae_eq_fun.mk_eq_mk, exact classical.some_spec (to_Lp f hfi).2 }

variables (E Œº)

lemma zero_to_simple_func : to_simple_func (0 : Lp.simple_func E p Œº) =·µê[Œº] 0 :=
begin
  filter_upwards [to_simple_func_eq_to_fun (0 : Lp.simple_func E p Œº), Lp.coe_fn_zero E 1 Œº]
    with _ h‚ÇÅ _,
  rwa h‚ÇÅ,
end

variables {E Œº}

lemma add_to_simple_func (f g : Lp.simple_func E p Œº) :
  to_simple_func (f + g) =·µê[Œº] to_simple_func f + to_simple_func g :=
begin
  filter_upwards [to_simple_func_eq_to_fun (f + g), to_simple_func_eq_to_fun f,
    to_simple_func_eq_to_fun g, Lp.coe_fn_add (f :  Lp E p Œº) g] with _,
  simp only [‚Üê coe_coe, add_subgroup.coe_add, pi.add_apply],
  iterate 4 { assume h, rw h, },
end

lemma neg_to_simple_func (f : Lp.simple_func E p Œº) :
  to_simple_func (-f) =·µê[Œº] - to_simple_func f :=
begin
  filter_upwards [to_simple_func_eq_to_fun (-f), to_simple_func_eq_to_fun f,
    Lp.coe_fn_neg (f : Lp E p Œº)] with _,
  simp only [pi.neg_apply, add_subgroup.coe_neg, ‚Üê coe_coe],
  repeat { assume h, rw h, },
end

lemma sub_to_simple_func (f g : Lp.simple_func E p Œº) :
  to_simple_func (f - g) =·µê[Œº] to_simple_func f - to_simple_func g :=
begin
  filter_upwards [to_simple_func_eq_to_fun (f - g), to_simple_func_eq_to_fun f,
    to_simple_func_eq_to_fun g, Lp.coe_fn_sub (f : Lp E p Œº) g] with _,
  simp only [add_subgroup.coe_sub, pi.sub_apply, ‚Üê coe_coe],
  repeat { assume h, rw h, },
end

variables [normed_field ùïú] [normed_space ùïú E]

lemma smul_to_simple_func (k : ùïú) (f : Lp.simple_func E p Œº) :
  to_simple_func (k ‚Ä¢ f) =·µê[Œº] k ‚Ä¢ to_simple_func f :=
begin
  filter_upwards [to_simple_func_eq_to_fun (k ‚Ä¢ f), to_simple_func_eq_to_fun f,
    Lp.coe_fn_smul k (f : Lp E p Œº)] with _,
  simp only [pi.smul_apply, coe_smul, ‚Üê coe_coe],
  repeat { assume h, rw h, },
end

lemma norm_to_simple_func [fact (1 ‚â§ p)] (f : Lp.simple_func E p Œº) :
  ‚Äñf‚Äñ = ennreal.to_real (snorm (to_simple_func f) p Œº) :=
by simpa [to_Lp_to_simple_func] using norm_to_Lp (to_simple_func f) (simple_func.mem_‚Ñíp f)

end to_simple_func

section induction

variables (p)

/-- The characteristic function of a finite-measure measurable set `s`, as an `Lp` simple function.
-/
def indicator_const {s : set Œ±} (hs : measurable_set s) (hŒºs : Œº s ‚â† ‚àû) (c : E) :
  Lp.simple_func E p Œº :=
to_Lp ((simple_func.const _ c).piecewise s hs (simple_func.const _ 0))
  (mem_‚Ñíp_indicator_const p hs c (or.inr hŒºs))

variables {p}

@[simp] lemma coe_indicator_const {s : set Œ±} (hs : measurable_set s) (hŒºs : Œº s ‚â† ‚àû) (c : E) :
  (‚Üë(indicator_const p hs hŒºs c) : Lp E p Œº) = indicator_const_Lp p hs hŒºs c :=
rfl

lemma to_simple_func_indicator_const {s : set Œ±} (hs : measurable_set s) (hŒºs : Œº s ‚â† ‚àû) (c : E) :
  to_simple_func (indicator_const p hs hŒºs c)
    =·µê[Œº] (simple_func.const _ c).piecewise s hs (simple_func.const _ 0) :=
Lp.simple_func.to_simple_func_to_Lp _ _

/-- To prove something for an arbitrary `Lp` simple function, with `0 < p < ‚àû`, it suffices to show
that the property holds for (multiples of) characteristic functions of finite-measure measurable
sets and is closed under addition (of functions with disjoint support). -/
@[elab_as_eliminator]
protected lemma induction (hp_pos : p ‚â† 0) (hp_ne_top : p ‚â† ‚àû) {P : Lp.simple_func E p Œº ‚Üí Prop}
  (h_ind : ‚àÄ (c : E) {s : set Œ±} (hs : measurable_set s) (hŒºs : Œº s < ‚àû),
    P (Lp.simple_func.indicator_const p hs hŒºs.ne c))
  (h_add : ‚àÄ ‚¶Éf g : Œ± ‚Üí‚Çõ E‚¶Ñ, ‚àÄ hf : mem_‚Ñíp f p Œº, ‚àÄ hg : mem_‚Ñíp g p Œº,
    disjoint (support f) (support g) ‚Üí P (Lp.simple_func.to_Lp f hf)
    ‚Üí P (Lp.simple_func.to_Lp g hg) ‚Üí P (Lp.simple_func.to_Lp f hf + Lp.simple_func.to_Lp g hg))
  (f : Lp.simple_func E p Œº) : P f :=
begin
  suffices : ‚àÄ f : Œ± ‚Üí‚Çõ E, ‚àÄ hf : mem_‚Ñíp f p Œº, P (to_Lp f hf),
  { rw ‚Üê to_Lp_to_simple_func f,
    apply this }, clear f,
  refine simple_func.induction _ _,
  { intros c s hs hf,
    by_cases hc : c = 0,
    { convert h_ind 0 measurable_set.empty (by simp) using 1,
      ext1,
      simp [hc] },
    exact h_ind c hs (simple_func.measure_lt_top_of_mem_‚Ñíp_indicator hp_pos hp_ne_top hc hs hf) },
  { intros f g hfg hf hg hfg',
    obtain ‚ü®hf', hg'‚ü© : mem_‚Ñíp f p Œº ‚àß mem_‚Ñíp g p Œº,
    { exact (mem_‚Ñíp_add_of_disjoint hfg f.strongly_measurable g.strongly_measurable).mp hfg' },
    exact h_add hf' hg' hfg (hf hf') (hg hg') },
end

end induction

section coe_to_Lp

variables [fact (1 ‚â§ p)]

protected lemma uniform_continuous :
  uniform_continuous (coe : (Lp.simple_func E p Œº) ‚Üí (Lp E p Œº)) :=
uniform_continuous_comap

protected lemma uniform_embedding :
  uniform_embedding (coe : (Lp.simple_func E p Œº) ‚Üí (Lp E p Œº)) :=
uniform_embedding_comap subtype.val_injective

protected lemma uniform_inducing : uniform_inducing (coe : (Lp.simple_func E p Œº) ‚Üí (Lp E p Œº)) :=
simple_func.uniform_embedding.to_uniform_inducing

protected lemma dense_embedding (hp_ne_top : p ‚â† ‚àû) :
  dense_embedding (coe : (Lp.simple_func E p Œº) ‚Üí (Lp E p Œº)) :=
begin
  borelize E,
  apply simple_func.uniform_embedding.dense_embedding,
  assume f,
  rw mem_closure_iff_seq_limit,
  have hfi' : mem_‚Ñíp f p Œº := Lp.mem_‚Ñíp f,
  haveI : separable_space (range f ‚à™ {0} : set E) :=
    (Lp.strongly_measurable f).separable_space_range_union_singleton,
  refine ‚ü®Œª n, ‚Üë(to_Lp (simple_func.approx_on f (Lp.strongly_measurable f).measurable
    (range f ‚à™ {0}) 0 (by simp) n)
    (simple_func.mem_‚Ñíp_approx_on_range (Lp.strongly_measurable f).measurable hfi' n)),
    Œª n, mem_range_self _, _‚ü©,
  convert simple_func.tendsto_approx_on_range_Lp hp_ne_top (Lp.strongly_measurable f).measurable
    hfi',
  rw to_Lp_coe_fn f (Lp.mem_‚Ñíp f)
end

protected lemma dense_inducing (hp_ne_top : p ‚â† ‚àû) :
  dense_inducing (coe : (Lp.simple_func E p Œº) ‚Üí (Lp E p Œº)) :=
(simple_func.dense_embedding hp_ne_top).to_dense_inducing

protected lemma dense_range (hp_ne_top : p ‚â† ‚àû) :
  dense_range (coe : (Lp.simple_func E p Œº) ‚Üí (Lp E p Œº)) :=
(simple_func.dense_inducing hp_ne_top).dense

variables [normed_field ùïú] [normed_space ùïú E]
variables (Œ± E ùïú)

/-- The embedding of Lp simple functions into Lp functions, as a continuous linear map. -/
def coe_to_Lp : (Lp.simple_func E p Œº) ‚ÜíL[ùïú] (Lp E p Œº) :=
{ map_smul' := Œªk f, rfl,
  cont := Lp.simple_func.uniform_continuous.continuous,
  .. add_subgroup.subtype (Lp.simple_func E p Œº) }

variables {Œ± E ùïú}

end coe_to_Lp

section order

variables {G : Type*} [normed_lattice_add_comm_group G]

lemma coe_fn_le (f g : Lp.simple_func G p Œº) : f ‚â§·µê[Œº] g ‚Üî f ‚â§ g :=
by rw [‚Üê subtype.coe_le_coe, ‚Üê Lp.coe_fn_le, coe_fn_coe_base', coe_fn_coe_base' g]

instance : covariant_class (Lp.simple_func G p Œº) (Lp.simple_func G p Œº) (+) (‚â§) :=
begin
  refine ‚ü®Œª f g‚ÇÅ g‚ÇÇ hg‚ÇÅ‚ÇÇ, _‚ü©,
  rw ‚Üê Lp.simple_func.coe_fn_le at hg‚ÇÅ‚ÇÇ ‚ä¢,
  have h_add_1 : ‚áë(f + g‚ÇÅ) =·µê[Œº] f + g‚ÇÅ, from Lp.coe_fn_add _ _,
  have h_add_2 : ‚áë(f + g‚ÇÇ) =·µê[Œº] f + g‚ÇÇ, from Lp.coe_fn_add _ _,
  filter_upwards [h_add_1, h_add_2, hg‚ÇÅ‚ÇÇ] with _ h1 h2 h3,
  rw [h1, h2, pi.add_apply, pi.add_apply],
  exact add_le_add le_rfl h3,
end

variables (p Œº G)
lemma coe_fn_zero : (0 : Lp.simple_func G p Œº) =·µê[Œº] (0 : Œ± ‚Üí G) := Lp.coe_fn_zero _ _ _
variables{p Œº G}

lemma coe_fn_nonneg (f : Lp.simple_func G p Œº) : 0 ‚â§·µê[Œº] f ‚Üî 0 ‚â§ f :=
begin
  rw ‚Üê Lp.simple_func.coe_fn_le,
  have h0 : (0 : Lp.simple_func G p Œº) =·µê[Œº] (0 : Œ± ‚Üí G), from Lp.simple_func.coe_fn_zero p Œº G,
  split; intro h; filter_upwards [h, h0] with _ _ h2,
  { rwa h2, },
  { rwa ‚Üê h2, },
end

lemma exists_simple_func_nonneg_ae_eq {f : Lp.simple_func G p Œº} (hf : 0 ‚â§ f) :
  ‚àÉ f' : Œ± ‚Üí‚Çõ G, 0 ‚â§ f' ‚àß f =·µê[Œº] f' :=
begin
  rw ‚Üê Lp.simple_func.coe_fn_nonneg at hf,
  have hf_ae : 0 ‚â§·µê[Œº] (simple_func.to_simple_func f),
    by { filter_upwards [to_simple_func_eq_to_fun f, hf] with _ h1 _, rwa h1 },
  let s := (to_measurable Œº {x | ¬¨ 0 ‚â§ simple_func.to_simple_func f x})·∂ú,
  have hs_zero : Œº s·∂ú = 0,
    by { rw [compl_compl, measure_to_measurable], rwa [eventually_le, ae_iff] at hf_ae, },
  have hfs_nonneg : ‚àÄ x ‚àà s, 0 ‚â§ simple_func.to_simple_func f x,
  { intros x hxs,
    rw mem_compl_iff at hxs,
    have hx' : x ‚àâ {a : Œ± | ¬¨0 ‚â§ simple_func.to_simple_func f a},
      from Œª h, hxs (subset_to_measurable Œº _ h),
    rwa [set.nmem_set_of_iff, not_not] at hx', },
  let f' := simple_func.piecewise s (measurable_set_to_measurable Œº _).compl
    (simple_func.to_simple_func f) (simple_func.const Œ± (0 : G)),
  refine ‚ü®f', Œª x, _, _‚ü©,
  { rw simple_func.piecewise_apply,
    by_cases hxs : x ‚àà s,
    { simp only [hxs, hfs_nonneg x hxs, if_true, pi.zero_apply, simple_func.coe_zero], },
    { simp only [hxs, simple_func.const_zero, if_false], }, },
  { rw simple_func.coe_piecewise,
    have : s =·µê[Œº] univ,
    { rw ae_eq_set,
      simp only [true_and, measure_empty, eq_self_iff_true, diff_univ, ‚Üê compl_eq_univ_diff],
      exact hs_zero, },
    refine eventually_eq.trans (to_simple_func_eq_to_fun f).symm _,
    refine eventually_eq.trans _ (piecewise_ae_eq_of_ae_eq_set this.symm),
    simp only [simple_func.const_zero, indicator_univ, piecewise_eq_indicator,
      simple_func.coe_zero], },
end

variables (p Œº G)
/-- Coercion from nonnegative simple functions of Lp to nonnegative functions of Lp. -/
def coe_simple_func_nonneg_to_Lp_nonneg :
  {g : Lp.simple_func G p Œº // 0 ‚â§ g} ‚Üí {g : Lp G p Œº // 0 ‚â§ g} :=
Œª g, ‚ü®g, g.2‚ü©

lemma dense_range_coe_simple_func_nonneg_to_Lp_nonneg [hp : fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû) :
  dense_range (coe_simple_func_nonneg_to_Lp_nonneg p Œº G) :=
begin
  borelize G,
  assume g,
  rw mem_closure_iff_seq_limit,
  have hg_mem_‚Ñíp : mem_‚Ñíp g p Œº := Lp.mem_‚Ñíp g,
  have zero_mem : (0 : G) ‚àà (range g ‚à™ {0} : set G) ‚à© {y | 0 ‚â§ y}, by simp only [union_singleton,
    mem_inter_iff, mem_insert_iff, eq_self_iff_true, true_or, mem_set_of_eq, le_refl, and_self],
  haveI : separable_space (((range g ‚à™ {0}) ‚à© {y | 0 ‚â§ y}) : set G),
  { apply is_separable.separable_space,
    apply is_separable.mono _ (set.inter_subset_left _ _),
    exact (Lp.strongly_measurable (g : Lp G p Œº)).is_separable_range.union
      (finite_singleton _).is_separable },
  have g_meas : measurable g := (Lp.strongly_measurable (g : Lp G p Œº)).measurable,
  let x := Œª n, simple_func.approx_on g g_meas ((range g ‚à™ {0}) ‚à© {y | 0 ‚â§ y}) 0 zero_mem n,
  have hx_nonneg : ‚àÄ n, 0 ‚â§ x n,
  { assume n a,
    change x n a ‚àà {y : G | 0 ‚â§ y},
    have A : (range g ‚à™ {0} : set G) ‚à© {y | 0 ‚â§ y} ‚äÜ {y | 0 ‚â§ y} := inter_subset_right _ _,
    apply A,
    exact simple_func.approx_on_mem g_meas _ n a },
  have hx_mem_‚Ñíp : ‚àÄ n, mem_‚Ñíp (x n) p Œº,
    from simple_func.mem_‚Ñíp_approx_on _ hg_mem_‚Ñíp _ ‚ü®ae_strongly_measurable_const, by simp‚ü©,
  have h_to_Lp := Œª n, mem_‚Ñíp.coe_fn_to_Lp (hx_mem_‚Ñíp n),
  have hx_nonneg_Lp : ‚àÄ n, 0 ‚â§ to_Lp (x n) (hx_mem_‚Ñíp n),
  { intro n,
    rw [‚Üê Lp.simple_func.coe_fn_le, coe_fn_coe_base' (simple_func.to_Lp (x n) _),
      Lp.simple_func.to_Lp_eq_to_Lp],
    have h0 := Lp.simple_func.coe_fn_zero p Œº G,
    filter_upwards [Lp.simple_func.coe_fn_zero p Œº G, h_to_Lp n] with a ha0 ha_to_Lp,
    rw [ha0, ha_to_Lp],
    exact hx_nonneg n a, },
  have hx_tendsto : tendsto (Œª (n : ‚Ñï), snorm (x n - g) p Œº) at_top (ùìù 0),
  { apply simple_func.tendsto_approx_on_Lp_snorm g_meas zero_mem hp_ne_top,
    { have hg_nonneg : 0 ‚â§·µê[Œº] g, from (Lp.coe_fn_nonneg _).mpr g.2,
      refine hg_nonneg.mono (Œª a ha, subset_closure _),
      simpa using ha, },
    { simp_rw sub_zero, exact hg_mem_‚Ñíp.snorm_lt_top, }, },
  refine ‚ü®Œª n, (coe_simple_func_nonneg_to_Lp_nonneg p Œº G) ‚ü®to_Lp (x n) (hx_mem_‚Ñíp n),
    hx_nonneg_Lp n‚ü©, Œª n, mem_range_self _, _‚ü©,
  suffices : tendsto (Œª (n : ‚Ñï), ‚Üë(to_Lp (x n) (hx_mem_‚Ñíp n))) at_top (ùìù (g : Lp G p Œº)),
  { rw tendsto_iff_dist_tendsto_zero at this ‚ä¢,
    simp_rw subtype.dist_eq,
    convert this, },
  rw Lp.tendsto_Lp_iff_tendsto_‚Ñíp',
  convert hx_tendsto,
  refine funext (Œª n, snorm_congr_ae (eventually_eq.sub _ _)),
  { rw Lp.simple_func.to_Lp_eq_to_Lp,
    exact h_to_Lp n, },
  { rw ‚Üê coe_fn_coe_base, },
end

variables {p Œº G}

end order

end simple_func

end Lp

variables [measurable_space Œ±] [normed_add_comm_group E] {f : Œ± ‚Üí E} {p : ‚Ñù‚â•0‚àû} {Œº : measure Œ±}

/-- To prove something for an arbitrary `Lp` function in a second countable Borel normed group, it
suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in `Lp` for which the property holds is closed.
-/
@[elab_as_eliminator]
lemma Lp.induction [_i : fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû) (P : Lp E p Œº ‚Üí Prop)
  (h_ind : ‚àÄ (c : E) {s : set Œ±} (hs : measurable_set s) (hŒºs : Œº s < ‚àû),
      P (Lp.simple_func.indicator_const p hs hŒºs.ne c))
  (h_add : ‚àÄ ‚¶Éf g‚¶Ñ, ‚àÄ hf : mem_‚Ñíp f p Œº, ‚àÄ hg : mem_‚Ñíp g p Œº, disjoint (support f) (support g) ‚Üí
    P (hf.to_Lp f) ‚Üí P (hg.to_Lp g) ‚Üí P ((hf.to_Lp f) + (hg.to_Lp g)))
  (h_closed : is_closed {f : Lp E p Œº | P f}) :
  ‚àÄ f : Lp E p Œº, P f :=
begin
  refine Œª f, (Lp.simple_func.dense_range hp_ne_top).induction_on f h_closed _,
  refine Lp.simple_func.induction (lt_of_lt_of_le zero_lt_one _i.elim).ne' hp_ne_top _ _,
  { exact Œª c s, h_ind c },
  { exact Œª f g hf hg, h_add hf hg },
end

/-- To prove something for an arbitrary `mem_‚Ñíp` function in a second countable
Borel normed group, it suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in the `L·µñ` space for which the property holds is closed.
* the property is closed under the almost-everywhere equal relation.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`).
-/
@[elab_as_eliminator]
lemma mem_‚Ñíp.induction [_i : fact (1 ‚â§ p)] (hp_ne_top : p ‚â† ‚àû) (P : (Œ± ‚Üí E) ‚Üí Prop)
  (h_ind : ‚àÄ (c : E) ‚¶És‚¶Ñ, measurable_set s ‚Üí Œº s < ‚àû ‚Üí P (s.indicator (Œª _, c)))
  (h_add : ‚àÄ ‚¶Éf g : Œ± ‚Üí E‚¶Ñ, disjoint (support f) (support g) ‚Üí mem_‚Ñíp f p Œº ‚Üí mem_‚Ñíp g p Œº ‚Üí
    P f ‚Üí P g ‚Üí P (f + g))
  (h_closed : is_closed {f : Lp E p Œº | P f} )
  (h_ae : ‚àÄ ‚¶Éf g‚¶Ñ, f =·µê[Œº] g ‚Üí mem_‚Ñíp f p Œº ‚Üí P f ‚Üí P g) :
  ‚àÄ ‚¶Éf : Œ± ‚Üí E‚¶Ñ (hf : mem_‚Ñíp f p Œº), P f :=
begin
  have : ‚àÄ (f : simple_func Œ± E), mem_‚Ñíp f p Œº ‚Üí P f,
  { refine simple_func.induction _ _,
    { intros c s hs h,
      by_cases hc : c = 0,
      { subst hc, convert h_ind 0 measurable_set.empty (by simp) using 1, ext, simp [const] },
      have hp_pos : p ‚â† 0 := (lt_of_lt_of_le zero_lt_one _i.elim).ne',
      exact h_ind c hs (simple_func.measure_lt_top_of_mem_‚Ñíp_indicator hp_pos hp_ne_top hc hs h) },
    { intros f g hfg hf hg int_fg,
      rw [simple_func.coe_add,
        mem_‚Ñíp_add_of_disjoint hfg f.strongly_measurable g.strongly_measurable] at int_fg,
      refine h_add hfg int_fg.1 int_fg.2 (hf int_fg.1) (hg int_fg.2) } },
  have : ‚àÄ (f : Lp.simple_func E p Œº), P f,
  { intro f,
    exact h_ae (Lp.simple_func.to_simple_func_eq_to_fun f) (Lp.simple_func.mem_‚Ñíp f)
      (this (Lp.simple_func.to_simple_func f) (Lp.simple_func.mem_‚Ñíp f)) },
  have : ‚àÄ (f : Lp E p Œº), P f :=
    Œª f, (Lp.simple_func.dense_range hp_ne_top).induction_on f h_closed this,
  exact Œª f hf, h_ae hf.coe_fn_to_Lp (Lp.mem_‚Ñíp _) (this (hf.to_Lp f)),
end

/-- If a set of ae strongly measurable functions is stable under addition and approximates
characteristic functions in `‚Ñíp`, then it is dense in `‚Ñíp`. -/
lemma mem_‚Ñíp.induction_dense (hp_ne_top : p ‚â† ‚àû) (P : (Œ± ‚Üí E) ‚Üí Prop)
  (h0P : ‚àÄ (c : E) ‚¶És : set Œ±‚¶Ñ, measurable_set s ‚Üí Œº s < ‚àû ‚Üí ‚àÄ {Œµ : ‚Ñù‚â•0‚àû}, Œµ ‚â† 0 ‚Üí
    (‚àÉ (g : Œ± ‚Üí E), snorm (g - s.indicator (Œª x, c)) p Œº ‚â§ Œµ ‚àß P g))
  (h1P : ‚àÄ f g, P f ‚Üí P g ‚Üí P (f + g))
  (h2P : ‚àÄ f, P f ‚Üí ae_strongly_measurable f Œº)
  {f : Œ± ‚Üí E} (hf : mem_‚Ñíp f p Œº) {Œµ : ‚Ñù‚â•0‚àû} (hŒµ : Œµ ‚â† 0) :
  ‚àÉ (g : Œ± ‚Üí E), snorm (f - g) p Œº ‚â§ Œµ ‚àß P g :=
begin
  rcases eq_or_ne p 0 with rfl|hp_pos,
  { rcases h0P (0 : E) measurable_set.empty
      (by simp only [measure_empty, with_top.zero_lt_top]) hŒµ with ‚ü®g, hg, Pg‚ü©,
    exact ‚ü®g, by simp only [snorm_exponent_zero, zero_le'], Pg‚ü© },
  have A : ‚àÄ (Œ¥ : ‚Ñù‚â•0‚àû), Œ¥ ‚â† 0 ‚Üí ‚àÉ Œ∑, Œ∑ ‚â† 0 ‚àß Lp_add_const p * (Œ∑ + Œ∑) ‚â§ Œ¥,
  { assume Œ¥ hŒ¥,
    have : tendsto (Œª (Œ∑ : ‚Ñù‚â•0‚àû), Lp_add_const p * (Œ∑ + Œ∑)) (ùìù[>] 0) (ùìù (Lp_add_const p * (0 + 0))),
      from (ennreal.tendsto.const_mul (tendsto_id.add tendsto_id)
        (or.inr (Lp_add_const_lt_top p).ne)).mono_left nhds_within_le_nhds,
    simp only [add_zero, mul_zero] at this,
    rcases (((tendsto_order.1 this).2 Œ¥ hŒ¥.bot_lt).and self_mem_nhds_within).exists
      with ‚ü®Œ∑, hŒ∑, Œ∑pos‚ü©,
    exact ‚ü®Œ∑, Œ∑pos.ne', hŒ∑.le‚ü© },
  suffices H : ‚àÄ (f' : Œ± ‚Üí‚Çõ E) (Œ¥ : ‚Ñù‚â•0‚àû) (hŒ¥ : Œ¥ ‚â† 0), mem_‚Ñíp f' p Œº ‚Üí
    ‚àÉ g, snorm (f' - g) p Œº ‚â§ Œ¥ ‚àß P g,
  { obtain ‚ü®Œ∑, Œ∑pos, hŒ∑‚ü© : ‚àÉ (Œ∑ : ‚Ñù‚â•0‚àû), Œ∑ ‚â† 0 ‚àß Lp_add_const p * (Œ∑ + Œ∑) ‚â§ Œµ := A Œµ hŒµ,
    rcases hf.exists_simple_func_snorm_sub_lt hp_ne_top Œ∑pos with ‚ü®f', hf', f'_mem‚ü©,
    rcases H f' Œ∑ Œ∑pos f'_mem with ‚ü®g, hg, Pg‚ü©,
    refine ‚ü®g, _, Pg‚ü©,
    calc snorm (f - g) p Œº
        = snorm ((f - f') + (f' - g)) p Œº : by { congr, abel }
    ... ‚â§ Lp_add_const p * (snorm (f - f') p Œº + snorm (f' - g) p Œº) :
        snorm_add_le' (hf.ae_strongly_measurable.sub f'.ae_strongly_measurable)
          (f'.ae_strongly_measurable.sub (h2P g Pg)) p
    ... ‚â§ Lp_add_const p * (Œ∑ + Œ∑) :
      mul_le_mul_of_nonneg_left (add_le_add hf'.le hg) bot_le
    ... ‚â§ Œµ : hŒ∑ },
  refine simple_func.induction _ _,
  { intros c s hs Œµ Œµpos Hs,
    rcases eq_or_ne c 0 with rfl|hc,
    { rcases h0P (0 : E) measurable_set.empty
        (by simp only [measure_empty, with_top.zero_lt_top]) Œµpos with ‚ü®g, hg, Pg‚ü©,
      rw [‚Üê snorm_neg, neg_sub] at hg,
      refine ‚ü®g, _, Pg‚ü©,
      convert hg,
      ext x,
      simp only [simple_func.const_zero, simple_func.coe_piecewise, simple_func.coe_zero,
        piecewise_eq_indicator, indicator_zero', pi.zero_apply, indicator_zero] },
    { have : Œº s < ‚àû,
        from (simple_func.measure_lt_top_of_mem_‚Ñíp_indicator hp_pos hp_ne_top hc hs Hs),
      rcases h0P c hs this Œµpos with ‚ü®g, hg, Pg‚ü©,
      rw [‚Üê snorm_neg, neg_sub] at hg,
      exact ‚ü®g, hg, Pg‚ü© } },
  { intros f f' hff' hf hf' Œ¥ Œ¥pos int_ff',
    obtain ‚ü®Œ∑, Œ∑pos, hŒ∑‚ü© : ‚àÉ (Œ∑ : ‚Ñù‚â•0‚àû), Œ∑ ‚â† 0 ‚àß Lp_add_const p * (Œ∑ + Œ∑) ‚â§ Œ¥ := A Œ¥ Œ¥pos,
    rw [simple_func.coe_add,
      mem_‚Ñíp_add_of_disjoint hff' f.strongly_measurable f'.strongly_measurable] at int_ff',
    rcases hf Œ∑ Œ∑pos int_ff'.1 with ‚ü®g, hg, Pg‚ü©,
    rcases hf' Œ∑ Œ∑pos int_ff'.2 with ‚ü®g', hg', Pg'‚ü©,
    refine ‚ü®g + g', _, h1P g g' Pg Pg'‚ü©,
    calc snorm ((f + f') - (g + g')) p Œº
        = snorm ((f - g) + (f' - g')) p Œº : by { congr, abel }
    ... ‚â§ Lp_add_const p * (snorm (f - g) p Œº + snorm (f' - g') p Œº) :
        snorm_add_le' (f.ae_strongly_measurable.sub (h2P g Pg))
          (f'.ae_strongly_measurable.sub (h2P g' Pg')) p
    ... ‚â§ Lp_add_const p * (Œ∑ + Œ∑) :
      mul_le_mul_of_nonneg_left (add_le_add hg hg') bot_le
    ... ‚â§ Œ¥ : hŒ∑ },
end

section integrable

notation Œ± ` ‚Üí‚ÇÅ‚Çõ[`:25 Œº `] ` E := @measure_theory.Lp.simple_func Œ± E _ _ 1 Œº

lemma L1.simple_func.to_Lp_one_eq_to_L1 (f : Œ± ‚Üí‚Çõ E) (hf : integrable f Œº) :
  (Lp.simple_func.to_Lp f (mem_‚Ñíp_one_iff_integrable.2 hf) : Œ± ‚Üí‚ÇÅ[Œº] E) = hf.to_L1 f :=
rfl

protected lemma L1.simple_func.integrable (f : Œ± ‚Üí‚ÇÅ‚Çõ[Œº] E) :
  integrable (Lp.simple_func.to_simple_func f) Œº :=
by { rw ‚Üê mem_‚Ñíp_one_iff_integrable, exact (Lp.simple_func.mem_‚Ñíp f) }

/-- To prove something for an arbitrary integrable function in a normed group,
it suffices to show that
* the property holds for (multiples of) characteristic functions;
* is closed under addition;
* the set of functions in the `L¬π` space for which the property holds is closed.
* the property is closed under the almost-everywhere equal relation.

It is possible to make the hypotheses in the induction steps a bit stronger, and such conditions
can be added once we need them (for example in `h_add` it is only necessary to consider the sum of
a simple function with a multiple of a characteristic function and that the intersection
of their images is a subset of `{0}`).
-/
@[elab_as_eliminator]
lemma integrable.induction (P : (Œ± ‚Üí E) ‚Üí Prop)
  (h_ind : ‚àÄ (c : E) ‚¶És‚¶Ñ, measurable_set s ‚Üí Œº s < ‚àû ‚Üí P (s.indicator (Œª _, c)))
  (h_add : ‚àÄ ‚¶Éf g : Œ± ‚Üí E‚¶Ñ, disjoint (support f) (support g) ‚Üí integrable f Œº ‚Üí integrable g Œº ‚Üí
    P f ‚Üí P g ‚Üí P (f + g))
  (h_closed : is_closed {f : Œ± ‚Üí‚ÇÅ[Œº] E | P f} )
  (h_ae : ‚àÄ ‚¶Éf g‚¶Ñ, f =·µê[Œº] g ‚Üí integrable f Œº ‚Üí P f ‚Üí P g) :
  ‚àÄ ‚¶Éf : Œ± ‚Üí E‚¶Ñ (hf : integrable f Œº), P f :=
begin
  simp only [‚Üê mem_‚Ñíp_one_iff_integrable] at *,
  exact mem_‚Ñíp.induction one_ne_top P h_ind h_add h_closed h_ae
end

end integrable

end measure_theory
