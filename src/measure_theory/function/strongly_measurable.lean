/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne
-/

import measure_theory.function.simple_func_dense

/-!
# Strongly measurable and finitely strongly measurable functions

A function `f` is said to be strongly measurable if `f` is the sequential limit of simple functions.
It is said to be finitely strongly measurable with respect to a measure `Î¼` if the supports
of those simple functions have finite measure.

If the target space has a second countable topology, strongly measurable and measurable are
equivalent.

Functions in `Lp` for `0 < p < âˆž` are finitely strongly measurable.
If the measure is sigma-finite, strongly measurable and finitely strongly measurable are equivalent.

The main property of finitely strongly measurable functions is
`fin_strongly_measurable.exists_set_sigma_finite`: there exists a measurable set `t` such that the
function is supported on `t` and `Î¼.restrict t` is sigma-finite. As a consequence, we can prove some
results for those functions as if the measure was sigma-finite.

## Main definitions

* `strongly_measurable f`: `f : Î± â†’ Î²` is the limit of a sequence `fs : â„• â†’ simple_func Î± Î²`.
* `fin_strongly_measurable f Î¼`: `f : Î± â†’ Î²` is the limit of a sequence `fs : â„• â†’ simple_func Î± Î²`
  such that for all `n âˆˆ â„•`, the measure of the support of `fs n` is finite.
* `ae_fin_strongly_measurable f Î¼`: `f` is almost everywhere equal to a `fin_strongly_measurable`
  function.

* `ae_fin_strongly_measurable.sigma_finite_set`: a measurable set `t` such that
  `f =áµ[Î¼.restrict tá¶œ] 0` and `Î¼.restrict t` is sigma-finite.

## Main statements

* `ae_fin_strongly_measurable.exists_set_sigma_finite`: there exists a measurable set `t` such that
  `f =áµ[Î¼.restrict tá¶œ] 0` and `Î¼.restrict t` is sigma-finite.
* `mem_â„’p.ae_fin_strongly_measurable`: if `mem_â„’p f p Î¼` with `0 < p < âˆž`, then
  `ae_fin_strongly_measurable f Î¼`.
* `Lp.fin_strongly_measurable`: for `0 < p < âˆž`, `Lp` functions are finitely strongly measurable.

## References

* HytÃ¶nen, Tuomas, Jan Van Neerven, Mark Veraar, and Lutz Weis. Analysis in Banach spaces.
  Springer, 2016.

-/

open measure_theory filter topological_space function
open_locale ennreal topological_space measure_theory

namespace measure_theory

local infixr ` â†’â‚› `:25 := simple_func

section definitions
variables {Î± Î² : Type*} [topological_space Î²]

/-- A function is `strongly_measurable` if it is the limit of simple functions. -/
def strongly_measurable [measurable_space Î±] (f : Î± â†’ Î²) : Prop :=
âˆƒ fs : â„• â†’ Î± â†’â‚› Î², âˆ€ x, tendsto (Î» n, fs n x) at_top (ð“ (f x))

/-- A function is `fin_strongly_measurable` with respect to a measure if it is the limit of simple
  functions with support with finite measure. -/
def fin_strongly_measurable [has_zero Î²] {m0 : measurable_space Î±} (f : Î± â†’ Î²) (Î¼ : measure Î±) :
  Prop :=
âˆƒ fs : â„• â†’ Î± â†’â‚› Î², (âˆ€ n, Î¼ (support (fs n)) < âˆž) âˆ§ (âˆ€ x, tendsto (Î» n, fs n x) at_top (ð“ (f x)))

/-- A function is `ae_fin_strongly_measurable` with respect to a measure if it is almost everywhere
equal to the limit of a sequence of simple functions with support with finite measure. -/
def ae_fin_strongly_measurable [has_zero Î²] {m0 : measurable_space Î±} (f : Î± â†’ Î²) (Î¼ : measure Î±) :
  Prop :=
âˆƒ g, fin_strongly_measurable g Î¼ âˆ§ f =áµ[Î¼] g

end definitions

/-! ## Strongly measurable functions -/

lemma subsingleton.strongly_measurable {Î± Î²} [measurable_space Î±] [topological_space Î²]
  [subsingleton Î²] (f : Î± â†’ Î²) :
  strongly_measurable f :=
begin
  let f_sf : Î± â†’â‚› Î² := âŸ¨f, Î» x, _,
    set.subsingleton.finite set.subsingleton_of_subsingletonâŸ©,
  { exact âŸ¨Î» n, f_sf, Î» x, tendsto_const_nhdsâŸ©, },
  { have h_univ : f â»Â¹' {x} = set.univ, by { ext1 y, simp, },
    rw h_univ,
    exact measurable_set.univ, },
end

lemma simple_func.strongly_measurable {Î± Î²} {m : measurable_space Î±} [topological_space Î²]
  (f : Î± â†’â‚› Î²) :
  strongly_measurable f :=
âŸ¨Î» _, f, Î» x, tendsto_const_nhdsâŸ©

lemma strongly_measurable_const {Î± Î²} {m : measurable_space Î±} [topological_space Î²] {b : Î²} :
  strongly_measurable (Î» a : Î±, b) :=
âŸ¨Î» n, simple_func.const Î± b, Î» a, tendsto_const_nhdsâŸ©

namespace strongly_measurable

variables {Î± Î² Î´ : Type*} {f g : Î± â†’ Î²}

section basic_properties_in_any_topological_space
variables [topological_space Î²]

/-- A sequence of simple functions such that `âˆ€ x, tendsto (Î» n, hf.approx n x) at_top (ð“ (f x))`.
That property is given by `strongly_measurable.tendsto_approx`. -/
protected noncomputable
def approx {m : measurable_space Î±} (hf : strongly_measurable f) : â„• â†’ Î± â†’â‚› Î² :=
hf.some

protected lemma tendsto_approx {m : measurable_space Î±} (hf : strongly_measurable f) :
  âˆ€ x, tendsto (Î» n, hf.approx n x) at_top (ð“ (f x)) :=
hf.some_spec

end basic_properties_in_any_topological_space

lemma fin_strongly_measurable_of_set_sigma_finite [topological_space Î²] [has_zero Î²]
  {m : measurable_space Î±} {Î¼ : measure Î±} (hf_meas : strongly_measurable f) {t : set Î±}
  (ht : measurable_set t) (hft_zero : âˆ€ x âˆˆ tá¶œ, f x = 0) (htÎ¼ : sigma_finite (Î¼.restrict t)) :
  fin_strongly_measurable f Î¼ :=
begin
  haveI : sigma_finite (Î¼.restrict t) := htÎ¼,
  let S := spanning_sets (Î¼.restrict t),
  have hS_meas : âˆ€ n, measurable_set (S n), from measurable_spanning_sets (Î¼.restrict t),
  let f_approx := hf_meas.approx,
  let fs := Î» n, simple_func.restrict (f_approx n) (S n âˆ© t),
  have h_fs_t_compl : âˆ€ n, âˆ€ x âˆ‰ t, fs n x = 0,
  { intros n x hxt,
    rw simple_func.restrict_apply _ ((hS_meas n).inter ht),
    refine set.indicator_of_not_mem _ _,
    simp [hxt], },
  refine âŸ¨fs, _, Î» x, _âŸ©,
  { simp_rw simple_func.support_eq,
    refine Î» n, (measure_bUnion_finset_le _ _).trans_lt _,
    refine ennreal.sum_lt_top_iff.mpr (Î» y hy, _),
    rw simple_func.restrict_preimage_singleton _ ((hS_meas n).inter ht),
    swap, { rw finset.mem_filter at hy, exact hy.2, },
    refine (measure_mono (set.inter_subset_left _ _)).trans_lt _,
    have h_lt_top := measure_spanning_sets_lt_top (Î¼.restrict t) n,
    rwa measure.restrict_apply' ht at h_lt_top, },
  { by_cases hxt : x âˆˆ t,
    swap, { rw [funext (Î» n, h_fs_t_compl n x hxt), hft_zero x hxt], exact tendsto_const_nhds, },
    have h : tendsto (Î» n, (f_approx n) x) at_top (ð“ (f x)), from hf_meas.tendsto_approx x,
    obtain âŸ¨nâ‚, hnâ‚âŸ© : âˆƒ n, âˆ€ m, n â‰¤ m â†’ fs m x = f_approx m x,
    { obtain âŸ¨n, hnâŸ© : âˆƒ n, âˆ€ m, n â‰¤ m â†’ x âˆˆ S m âˆ© t,
      { suffices : âˆƒ n, âˆ€ m, n â‰¤ m â†’ x âˆˆ S m,
        { obtain âŸ¨n, hnâŸ© := this,
          exact âŸ¨n, Î» m hnm, set.mem_inter (hn m hnm) hxtâŸ©, },
        suffices : âˆƒ n, x âˆˆ S n,
        { rcases this with âŸ¨n, hnâŸ©,
          exact âŸ¨n, Î» m hnm, monotone_spanning_sets (Î¼.restrict t) hnm hnâŸ©, },
        rw [â† set.mem_Union, Union_spanning_sets (Î¼.restrict t)],
        trivial, },
      refine âŸ¨n, Î» m hnm, _âŸ©,
      simp_rw [fs, simple_func.restrict_apply _ ((hS_meas m).inter ht),
        set.indicator_of_mem (hn m hnm)], },
    rw tendsto_at_top' at h âŠ¢,
    intros s hs,
    obtain âŸ¨nâ‚‚, hnâ‚‚âŸ© := h s hs,
    refine âŸ¨max nâ‚ nâ‚‚, Î» m hm, _âŸ©,
    rw hnâ‚ m ((le_max_left _ _).trans hm.le),
    exact hnâ‚‚ m ((le_max_right _ _).trans hm.le), },
end

/-- If the measure is sigma-finite, all strongly measurable functions are
  `fin_strongly_measurable`. -/
protected lemma fin_strongly_measurable [topological_space Î²] [has_zero Î²] {m0 : measurable_space Î±}
  (hf : strongly_measurable f) (Î¼ : measure Î±) [sigma_finite Î¼] :
  fin_strongly_measurable f Î¼ :=
hf.fin_strongly_measurable_of_set_sigma_finite measurable_set.univ (by simp)
  (by rwa measure.restrict_univ)

/-- A strongly measurable function is measurable. -/
protected lemma measurable [measurable_space Î±] [metric_space Î²] [measurable_space Î²]
  [borel_space Î²] (hf : strongly_measurable f) :
  measurable f :=
measurable_of_tendsto_metric (Î» n, (hf.approx n).measurable) (tendsto_pi_nhds.mpr hf.tendsto_approx)

protected lemma measurable_ennreal [measurable_space Î±] {f : Î± â†’ â„â‰¥0âˆž}
  (hf : strongly_measurable f) :
  measurable f :=
measurable_of_tendsto_ennreal (Î» n, (hf.approx n).measurable)
  (tendsto_pi_nhds.mpr hf.tendsto_approx)

section arithmetic
variables [measurable_space Î±] [topological_space Î²]

@[to_additive]
protected lemma mul [has_mul Î²] [has_continuous_mul Î²]
  (hf : strongly_measurable f) (hg : strongly_measurable g) :
  strongly_measurable (f * g) :=
âŸ¨Î» n, hf.approx n * hg.approx n, Î» x, (hf.tendsto_approx x).mul (hg.tendsto_approx x)âŸ©

@[to_additive]
protected lemma inv [group Î²] [topological_group Î²] (hf : strongly_measurable f) :
  strongly_measurable fâ»Â¹ :=
âŸ¨Î» n, (hf.approx n)â»Â¹, Î» x, (hf.tendsto_approx x).invâŸ©

@[to_additive]
protected lemma div [has_div Î²] [has_continuous_div Î²]
  (hf : strongly_measurable f) (hg : strongly_measurable g) :
  strongly_measurable (f / g) :=
âŸ¨Î» n, hf.approx n / hg.approx n, Î» x, (hf.tendsto_approx x).div' (hg.tendsto_approx x)âŸ©

protected lemma const_smul {ð•œ} [topological_space ð•œ] [has_scalar ð•œ Î²] [has_continuous_smul ð•œ Î²]
  (hf : strongly_measurable f) (c : ð•œ) :
  strongly_measurable (c â€¢ f) :=
âŸ¨Î» n, c â€¢ (hf.approx n), Î» x, (hf.tendsto_approx x).const_smul câŸ©

end arithmetic

protected lemma mono {m m' : measurable_space Î±} [topological_space Î²]
  (hf : @strongly_measurable Î± Î² _ m' f) (h_mono : m' â‰¤ m) :
  @strongly_measurable Î± Î² _ m f :=
begin
  let f_approx : â„• â†’ @simple_func Î± m Î² := Î» n,
  { to_fun := hf.approx n,
    measurable_set_fiber' := Î» x, h_mono _ (simple_func.measurable_set_fiber' _ x),
    finite_range' := simple_func.finite_range (hf.approx n) },
  exact âŸ¨f_approx, hf.tendsto_approxâŸ©,
end

section order
variables [measurable_space Î±] [topological_space Î²]

open filter
open_locale filter

protected lemma sup [has_sup Î²] [has_continuous_sup Î²] (hf : strongly_measurable f)
  (hg : strongly_measurable g) :
  strongly_measurable (f âŠ” g) :=
âŸ¨Î» n, hf.approx n âŠ” hg.approx n, Î» x, (hf.tendsto_approx x).sup_right_nhds (hg.tendsto_approx x)âŸ©

protected lemma inf [has_inf Î²] [has_continuous_inf Î²] (hf : strongly_measurable f)
  (hg : strongly_measurable g) :
  strongly_measurable (f âŠ“ g) :=
âŸ¨Î» n, hf.approx n âŠ“ hg.approx n, Î» x, (hf.tendsto_approx x).inf_right_nhds (hg.tendsto_approx x)âŸ©

end order

end strongly_measurable

section second_countable_strongly_measurable
variables {Î± Î² : Type*} [measurable_space Î±] [measurable_space Î²] {f : Î± â†’ Î²}

/-- In a space with second countable topology, measurable implies strongly measurable. -/
lemma _root_.measurable.strongly_measurable [emetric_space Î²] [opens_measurable_space Î²]
  [second_countable_topology Î²] (hf : measurable f) :
  strongly_measurable f :=
begin
  rcases is_empty_or_nonempty Î²; resetI,
  { exact subsingleton.strongly_measurable f, },
  { inhabit Î²,
    exact âŸ¨simple_func.approx_on f hf set.univ default (set.mem_univ _),
      Î» x, simple_func.tendsto_approx_on hf (set.mem_univ _) (by simp)âŸ©, },
end

lemma strongly_measurable_id [emetric_space Î±] [opens_measurable_space Î±]
  [second_countable_topology Î±] :
  strongly_measurable (id : Î± â†’ Î±) :=
measurable_id.strongly_measurable

/-- In a space with second countable topology, strongly measurable and measurable are equivalent. -/
lemma strongly_measurable_iff_measurable [metric_space Î²] [borel_space Î²]
  [second_countable_topology Î²] :
  strongly_measurable f â†” measurable f :=
âŸ¨Î» h, h.measurable, Î» h, measurable.strongly_measurable hâŸ©

end second_countable_strongly_measurable

/-! ## Finitely strongly measurable functions -/

lemma fin_strongly_measurable_zero {Î± Î²} {m : measurable_space Î±} {Î¼ : measure Î±} [has_zero Î²]
  [topological_space Î²] :
  fin_strongly_measurable (0 : Î± â†’ Î²) Î¼ :=
âŸ¨0, by simp only [pi.zero_apply, simple_func.coe_zero, support_zero', measure_empty,
    with_top.zero_lt_top, forall_const],
  Î» n, tendsto_const_nhdsâŸ©

namespace fin_strongly_measurable

variables {Î± Î² : Type*} {m0 : measurable_space Î±} {Î¼ : measure Î±} {f g : Î± â†’ Î²}

lemma ae_fin_strongly_measurable [has_zero Î²] [topological_space Î²]
  (hf : fin_strongly_measurable f Î¼) :
  ae_fin_strongly_measurable f Î¼ :=
âŸ¨f, hf, ae_eq_refl fâŸ©

section sequence
variables [has_zero Î²] [topological_space Î²] (hf : fin_strongly_measurable f Î¼)

/-- A sequence of simple functions such that `âˆ€ x, tendsto (Î» n, hf.approx n x) at_top (ð“ (f x))`
and `âˆ€ n, Î¼ (support (hf.approx n)) < âˆž`. These properties are given by
`fin_strongly_measurable.tendsto_approx` and `fin_strongly_measurable.fin_support_approx`. -/
protected noncomputable def approx : â„• â†’ Î± â†’â‚› Î² := hf.some

protected lemma fin_support_approx : âˆ€ n, Î¼ (support (hf.approx n)) < âˆž := hf.some_spec.1

protected lemma tendsto_approx : âˆ€ x, tendsto (Î» n, hf.approx n x) at_top (ð“ (f x)) :=
hf.some_spec.2

end sequence

protected lemma strongly_measurable [has_zero Î²] [topological_space Î²]
  (hf : fin_strongly_measurable f Î¼) :
  strongly_measurable f :=
âŸ¨hf.approx, hf.tendsto_approxâŸ©

lemma exists_set_sigma_finite [has_zero Î²] [topological_space Î²] [t2_space Î²]
  (hf : fin_strongly_measurable f Î¼) :
  âˆƒ t, measurable_set t âˆ§ (âˆ€ x âˆˆ tá¶œ, f x = 0) âˆ§ sigma_finite (Î¼.restrict t) :=
begin
  rcases hf with âŸ¨fs, hT_lt_top, h_approxâŸ©,
  let T := Î» n, support (fs n),
  have hT_meas : âˆ€ n, measurable_set (T n), from Î» n, simple_func.measurable_set_support (fs n),
  let t := â‹ƒ n, T n,
  refine âŸ¨t, measurable_set.Union hT_meas, _, _âŸ©,
  { have h_fs_zero : âˆ€ n, âˆ€ x âˆˆ tá¶œ, fs n x = 0,
    { intros n x hxt,
      rw [set.mem_compl_iff, set.mem_Union, not_exists] at hxt,
      simpa using (hxt n), },
    refine Î» x hxt, tendsto_nhds_unique (h_approx x) _,
    rw funext (Î» n, h_fs_zero n x hxt),
    exact tendsto_const_nhds, },
  { refine âŸ¨âŸ¨âŸ¨Î» n, tá¶œ âˆª T n, Î» n, trivial, Î» n, _, _âŸ©âŸ©âŸ©,
    { rw [measure.restrict_apply' (measurable_set.Union hT_meas), set.union_inter_distrib_right,
        set.compl_inter_self t, set.empty_union],
      exact (measure_mono (set.inter_subset_left _ _)).trans_lt (hT_lt_top n), },
    { rw â† set.union_Union tá¶œ T,
      exact set.compl_union_self _ } }
end

/-- A finitely strongly measurable function is measurable. -/
protected lemma measurable [has_zero Î²] [metric_space Î²] [measurable_space Î²] [borel_space Î²]
  (hf : fin_strongly_measurable f Î¼) :
  measurable f :=
hf.strongly_measurable.measurable

protected lemma measurable_ennreal {f : Î± â†’ â„â‰¥0âˆž} (hf : fin_strongly_measurable f Î¼) :
  measurable f :=
hf.strongly_measurable.measurable_ennreal

section arithmetic
variables [topological_space Î²]

protected lemma mul [monoid_with_zero Î²] [has_continuous_mul Î²]
  (hf : fin_strongly_measurable f Î¼) (hg : fin_strongly_measurable g Î¼) :
  fin_strongly_measurable (f * g) Î¼ :=
begin
  refine âŸ¨Î» n, hf.approx n * hg.approx n, _, Î» x, (hf.tendsto_approx x).mul (hg.tendsto_approx x)âŸ©,
  intro n,
  exact (measure_mono (support_mul_subset_left _ _)).trans_lt (hf.fin_support_approx n),
end

protected lemma add [add_monoid Î²] [has_continuous_add Î²]
  (hf : fin_strongly_measurable f Î¼) (hg : fin_strongly_measurable g Î¼) :
  fin_strongly_measurable (f + g) Î¼ :=
âŸ¨Î» n, hf.approx n + hg.approx n,
  Î» n, (measure_mono (function.support_add _ _)).trans_lt ((measure_union_le _ _).trans_lt
    (ennreal.add_lt_top.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©)),
  Î» x, (hf.tendsto_approx x).add (hg.tendsto_approx x)âŸ©

protected lemma neg [add_group Î²] [topological_add_group Î²] (hf : fin_strongly_measurable f Î¼) :
  fin_strongly_measurable (-f) Î¼ :=
begin
  refine âŸ¨Î» n, -hf.approx n, Î» n, _, Î» x, (hf.tendsto_approx x).negâŸ©,
  suffices : Î¼ (function.support (Î» x, - (hf.approx n) x)) < âˆž, by convert this,
  rw function.support_neg (hf.approx n),
  exact hf.fin_support_approx n,
end

protected lemma sub [add_group Î²] [has_continuous_sub Î²]
  (hf : fin_strongly_measurable f Î¼) (hg : fin_strongly_measurable g Î¼) :
  fin_strongly_measurable (f - g) Î¼ :=
âŸ¨Î» n, hf.approx n - hg.approx n,
  Î» n, (measure_mono (function.support_sub _ _)).trans_lt ((measure_union_le _ _).trans_lt
    (ennreal.add_lt_top.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©)),
  Î» x, (hf.tendsto_approx x).sub (hg.tendsto_approx x)âŸ©

protected lemma const_smul {ð•œ} [topological_space ð•œ] [add_monoid Î²] [monoid ð•œ]
  [distrib_mul_action ð•œ Î²] [has_continuous_smul ð•œ Î²]
  (hf : fin_strongly_measurable f Î¼) (c : ð•œ) :
  fin_strongly_measurable (c â€¢ f) Î¼ :=
begin
  refine âŸ¨Î» n, c â€¢ (hf.approx n), Î» n, _, Î» x, (hf.tendsto_approx x).const_smul câŸ©,
  rw simple_func.coe_smul,
  refine (measure_mono (support_smul_subset_right c _)).trans_lt (hf.fin_support_approx n),
end

end arithmetic

section order
variables [topological_space Î²] [has_zero Î²]

protected lemma sup [semilattice_sup Î²] [has_continuous_sup Î²]
  (hf : fin_strongly_measurable f Î¼) (hg : fin_strongly_measurable g Î¼) :
  fin_strongly_measurable (f âŠ” g) Î¼ :=
begin
  refine âŸ¨Î» n, hf.approx n âŠ” hg.approx n, Î» n, _,
    Î» x, (hf.tendsto_approx x).sup_right_nhds (hg.tendsto_approx x)âŸ©,
  refine (measure_mono (support_sup _ _)).trans_lt _,
  exact measure_union_lt_top_iff.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©,
end

protected lemma inf [semilattice_inf Î²] [has_continuous_inf Î²]
  (hf : fin_strongly_measurable f Î¼) (hg : fin_strongly_measurable g Î¼) :
  fin_strongly_measurable (f âŠ“ g) Î¼ :=
begin
  refine âŸ¨Î» n, hf.approx n âŠ“ hg.approx n, Î» n, _,
    Î» x, (hf.tendsto_approx x).inf_right_nhds (hg.tendsto_approx x)âŸ©,
  refine (measure_mono (support_inf _ _)).trans_lt _,
  exact measure_union_lt_top_iff.mpr âŸ¨hf.fin_support_approx n, hg.fin_support_approx nâŸ©,
end

end order

end fin_strongly_measurable

lemma fin_strongly_measurable_iff_strongly_measurable_and_exists_set_sigma_finite {Î± Î²} {f : Î± â†’ Î²}
  [topological_space Î²] [t2_space Î²] [has_zero Î²] {m : measurable_space Î±} {Î¼ : measure Î±} :
  fin_strongly_measurable f Î¼ â†” (strongly_measurable f
    âˆ§ (âˆƒ t, measurable_set t âˆ§ (âˆ€ x âˆˆ tá¶œ, f x = 0) âˆ§ sigma_finite (Î¼.restrict t))) :=
âŸ¨Î» hf, âŸ¨hf.strongly_measurable, hf.exists_set_sigma_finiteâŸ©,
  Î» hf, hf.1.fin_strongly_measurable_of_set_sigma_finite hf.2.some_spec.1 hf.2.some_spec.2.1
    hf.2.some_spec.2.2âŸ©

lemma ae_fin_strongly_measurable_zero {Î± Î²} {m : measurable_space Î±} (Î¼ : measure Î±) [has_zero Î²]
  [topological_space Î²] :
  ae_fin_strongly_measurable (0 : Î± â†’ Î²) Î¼ :=
âŸ¨0, fin_strongly_measurable_zero, eventually_eq.rflâŸ©

namespace ae_fin_strongly_measurable

variables {Î± Î² : Type*} {m : measurable_space Î±} {Î¼ : measure Î±} [topological_space Î²]
  {f g : Î± â†’ Î²}

section mk
variables [has_zero Î²]

/-- A `fin_strongly_measurable` function such that `f =áµ[Î¼] hf.mk f`. See lemmas
`fin_strongly_measurable_mk` and `ae_eq_mk`. -/
protected noncomputable def mk (f : Î± â†’ Î²) (hf : ae_fin_strongly_measurable f Î¼) : Î± â†’ Î² := hf.some

lemma fin_strongly_measurable_mk (hf : ae_fin_strongly_measurable f Î¼) :
  fin_strongly_measurable (hf.mk f) Î¼ :=
hf.some_spec.1

lemma ae_eq_mk (hf : ae_fin_strongly_measurable f Î¼) : f =áµ[Î¼] hf.mk f :=
hf.some_spec.2

protected lemma ae_measurable {Î²} [has_zero Î²] [measurable_space Î²] [metric_space Î²] [borel_space Î²]
  {f : Î± â†’ Î²} (hf : ae_fin_strongly_measurable f Î¼) :
  ae_measurable f Î¼ :=
âŸ¨hf.mk f, hf.fin_strongly_measurable_mk.measurable, hf.ae_eq_mkâŸ©

protected lemma ae_measurable_ennreal {f : Î± â†’ â„â‰¥0âˆž} (hf : ae_fin_strongly_measurable f Î¼) :
  ae_measurable f Î¼ :=
âŸ¨hf.mk f, hf.fin_strongly_measurable_mk.measurable_ennreal, hf.ae_eq_mkâŸ©

end mk

section arithmetic

protected lemma mul [monoid_with_zero Î²] [has_continuous_mul Î²]
  (hf : ae_fin_strongly_measurable f Î¼) (hg : ae_fin_strongly_measurable g Î¼) :
  ae_fin_strongly_measurable (f * g) Î¼ :=
âŸ¨hf.mk f * hg.mk g, hf.fin_strongly_measurable_mk.mul hg.fin_strongly_measurable_mk,
  hf.ae_eq_mk.mul hg.ae_eq_mkâŸ©

protected lemma add [add_monoid Î²] [has_continuous_add Î²]
  (hf : ae_fin_strongly_measurable f Î¼) (hg : ae_fin_strongly_measurable g Î¼) :
  ae_fin_strongly_measurable (f + g) Î¼ :=
âŸ¨hf.mk f + hg.mk g, hf.fin_strongly_measurable_mk.add hg.fin_strongly_measurable_mk,
  hf.ae_eq_mk.add hg.ae_eq_mkâŸ©

protected lemma neg [add_group Î²] [topological_add_group Î²] (hf : ae_fin_strongly_measurable f Î¼) :
  ae_fin_strongly_measurable (-f) Î¼ :=
âŸ¨-hf.mk f, hf.fin_strongly_measurable_mk.neg, hf.ae_eq_mk.negâŸ©

protected lemma sub [add_group Î²] [has_continuous_sub Î²]
  (hf : ae_fin_strongly_measurable f Î¼) (hg : ae_fin_strongly_measurable g Î¼) :
  ae_fin_strongly_measurable (f - g) Î¼ :=
âŸ¨hf.mk f - hg.mk g, hf.fin_strongly_measurable_mk.sub hg.fin_strongly_measurable_mk,
  hf.ae_eq_mk.sub hg.ae_eq_mkâŸ©

protected lemma const_smul {ð•œ} [topological_space ð•œ] [add_monoid Î²] [monoid ð•œ]
  [distrib_mul_action ð•œ Î²] [has_continuous_smul ð•œ Î²]
  (hf : ae_fin_strongly_measurable f Î¼) (c : ð•œ) :
  ae_fin_strongly_measurable (c â€¢ f) Î¼ :=
âŸ¨c â€¢ hf.mk f, hf.fin_strongly_measurable_mk.const_smul c, hf.ae_eq_mk.const_smul câŸ©

end arithmetic

section order
variables [has_zero Î²]

protected lemma sup [semilattice_sup Î²] [has_continuous_sup Î²]
  (hf : ae_fin_strongly_measurable f Î¼) (hg : ae_fin_strongly_measurable g Î¼) :
  ae_fin_strongly_measurable (f âŠ” g) Î¼ :=
âŸ¨hf.mk f âŠ” hg.mk g, hf.fin_strongly_measurable_mk.sup hg.fin_strongly_measurable_mk,
  hf.ae_eq_mk.sup hg.ae_eq_mkâŸ©

protected lemma inf [semilattice_inf Î²] [has_continuous_inf Î²]
  (hf : ae_fin_strongly_measurable f Î¼) (hg : ae_fin_strongly_measurable g Î¼) :
  ae_fin_strongly_measurable (f âŠ“ g) Î¼ :=
âŸ¨hf.mk f âŠ“ hg.mk g, hf.fin_strongly_measurable_mk.inf hg.fin_strongly_measurable_mk,
  hf.ae_eq_mk.inf hg.ae_eq_mkâŸ©

end order

variables [has_zero Î²] [t2_space Î²]

lemma exists_set_sigma_finite (hf : ae_fin_strongly_measurable f Î¼) :
  âˆƒ t, measurable_set t âˆ§ f =áµ[Î¼.restrict tá¶œ] 0 âˆ§ sigma_finite (Î¼.restrict t) :=
begin
  rcases hf with âŸ¨g, hg, hfgâŸ©,
  obtain âŸ¨t, ht, hgt_zero, htÎ¼âŸ© := hg.exists_set_sigma_finite,
  refine âŸ¨t, ht, _, htÎ¼âŸ©,
  refine eventually_eq.trans (ae_restrict_of_ae hfg) _,
  rw [eventually_eq, ae_restrict_iff' ht.compl],
  exact eventually_of_forall hgt_zero,
end

/-- A measurable set `t` such that `f =áµ[Î¼.restrict tá¶œ] 0` and `sigma_finite (Î¼.restrict t)`. -/
def sigma_finite_set (hf : ae_fin_strongly_measurable f Î¼) : set Î± :=
hf.exists_set_sigma_finite.some

protected lemma measurable_set (hf : ae_fin_strongly_measurable f Î¼) :
  measurable_set hf.sigma_finite_set :=
hf.exists_set_sigma_finite.some_spec.1

lemma ae_eq_zero_compl (hf : ae_fin_strongly_measurable f Î¼) :
  f =áµ[Î¼.restrict hf.sigma_finite_setá¶œ] 0 :=
hf.exists_set_sigma_finite.some_spec.2.1

instance sigma_finite_restrict (hf : ae_fin_strongly_measurable f Î¼) :
  sigma_finite (Î¼.restrict hf.sigma_finite_set) :=
hf.exists_set_sigma_finite.some_spec.2.2

end ae_fin_strongly_measurable

section second_countable_topology

variables {Î± G : Type*} {p : â„â‰¥0âˆž} {m m0 : measurable_space Î±} {Î¼ : measure Î±}
  [normed_group G] [measurable_space G] [borel_space G] [second_countable_topology G]
  {f : Î± â†’ G}

/-- In a space with second countable topology and a sigma-finite measure, `fin_strongly_measurable`
  and `measurable` are equivalent. -/
lemma fin_strongly_measurable_iff_measurable {m0 : measurable_space Î±} (Î¼ : measure Î±)
  [sigma_finite Î¼] :
  fin_strongly_measurable f Î¼ â†” measurable f :=
âŸ¨Î» h, h.measurable, Î» h, (measurable.strongly_measurable h).fin_strongly_measurable Î¼âŸ©

/-- In a space with second countable topology and a sigma-finite measure,
  `ae_fin_strongly_measurable` and `ae_measurable` are equivalent. -/
lemma ae_fin_strongly_measurable_iff_ae_measurable {m0 : measurable_space Î±} (Î¼ : measure Î±)
  [sigma_finite Î¼] :
  ae_fin_strongly_measurable f Î¼ â†” ae_measurable f Î¼ :=
by simp_rw [ae_fin_strongly_measurable, ae_measurable, fin_strongly_measurable_iff_measurable]

lemma mem_â„’p.fin_strongly_measurable_of_measurable (hf : mem_â„’p f p Î¼) (hf_meas : measurable f)
  (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆž) :
  fin_strongly_measurable f Î¼ :=
begin
  let fs := simple_func.approx_on f hf_meas set.univ 0 (set.mem_univ _),
  refine âŸ¨fs, _, _âŸ©,
  { have h_fs_Lp : âˆ€ n, mem_â„’p (fs n) p Î¼, from simple_func.mem_â„’p_approx_on_univ hf_meas hf,
    exact Î» n, (fs n).measure_support_lt_top_of_mem_â„’p (h_fs_Lp n) hp_ne_zero hp_ne_top, },
  { exact Î» x, simple_func.tendsto_approx_on hf_meas (set.mem_univ 0) (by simp), },
end

lemma mem_â„’p.ae_fin_strongly_measurable (hf : mem_â„’p f p Î¼) (hp_ne_zero : p â‰  0)
  (hp_ne_top : p â‰  âˆž) :
  ae_fin_strongly_measurable f Î¼ :=
âŸ¨hf.ae_measurable.mk f,
  ((mem_â„’p_congr_ae hf.ae_measurable.ae_eq_mk).mp hf).fin_strongly_measurable_of_measurable
    hf.ae_measurable.measurable_mk hp_ne_zero hp_ne_top,
  hf.ae_measurable.ae_eq_mkâŸ©

lemma integrable.ae_fin_strongly_measurable (hf : integrable f Î¼) :
  ae_fin_strongly_measurable f Î¼ :=
(mem_â„’p_one_iff_integrable.mpr hf).ae_fin_strongly_measurable one_ne_zero ennreal.coe_ne_top

lemma Lp.fin_strongly_measurable (f : Lp G p Î¼) (hp_ne_zero : p â‰  0) (hp_ne_top : p â‰  âˆž) :
  fin_strongly_measurable f Î¼ :=
(Lp.mem_â„’p f).fin_strongly_measurable_of_measurable (Lp.measurable f) hp_ne_zero hp_ne_top

end second_countable_topology

end measure_theory
