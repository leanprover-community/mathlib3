/-
Copyright (c) 2022 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.function.convergence_in_measure

/-!
# Uniform integrability

This file contains the definitions for uniform integrability (both in the measure theory sense
as well as the probability theory sense). This file also contains the Vitali convergence theorem
which estabishes a relation between uniform integrability, convergence in measure and
Lp convergence.

Uniform integrability plays a vital role in the theory of martingales most notably is used to
fomulate the martingale convergence theorem.

## Main definitions

* `measure_theory.unif_integrable`: uniform integrability in the measure theory sense.
  In particular, a sequence of functions `f` is uniformly integrable if for all `Îµ > 0`, there
  exists some `Î´ > 0` such that for all sets `s` of smaller measure than `Î´`, the Lp-norm of
  `f i` restricted `s` is smaller than `Îµ` for all `i`.
* `measure_theory.uniform_integrable`: uniform integrability in the probability theory sense.
  In particular, a sequence of measurable functions `f` is uniformly integrable in the
  probability theory sense if it is uniformly integrable in the measure theory sense and
  has uniformly bounded Lp-norm.

# Main results

* `measure_theory.unif_integrable_fintype`: a finite sequence of Lp functions is uniformly
  integrable.
* `measure_theory.tendsto_Lp_of_tendsto_ae`: a sequence of Lp functions which is uniformly
  integrable converges in Lp if they converge almost everywhere.
* `measure_theory.tendsto_in_measure_iff_tendsto_Lp`: Vitali convergence theorem:
  a sequence of Lp functions converges in Lp if and only if it is uniformly integrable
  and converges in measure.

## Tags
uniform integrable, uniformly absolutely continuous integral, Vitali convergence theorem
-/

noncomputable theory
open_locale classical measure_theory nnreal ennreal topological_space

namespace measure_theory

open set filter topological_space

variables {Î± Î² Î¹ : Type*} {m : measurable_space Î±} {Î¼ : measure Î±} [normed_group Î²]

/-- Uniform integrability in the measure theory sense.

A sequence of functions `f` is said to be uniformly integrable if for all `Îµ > 0`, there exists
some `Î´ > 0` such that for all sets `s` with measure less than `Î´`, the Lp-norm of `f i`
restricted on `s` is less than `Îµ`.

Uniform integrablility is also known as uniformly absolutely continuous integrals. -/
def unif_integrable {m : measurable_space Î±} (f : Î¹ â†’ Î± â†’ Î²) (p : â„â‰¥0âˆ) (Î¼ : measure Î±) : Prop :=
âˆ€ â¦ƒÎµ : â„â¦„ (hÎµ : 0 < Îµ), âˆƒ (Î´ : â„) (hÎ´ : 0 < Î´), âˆ€ i s, measurable_set s â†’ Î¼ s â‰¤ ennreal.of_real Î´ â†’
snorm (s.indicator (f i)) p Î¼ â‰¤ ennreal.of_real Îµ

/-- In probability theory, a family of measurable functions is uniformly integrable if it is
uniformly integrable in the measure theory sense and is uniformly bounded. -/
def uniform_integrable {m : measurable_space Î±}
  (f : Î¹ â†’ Î± â†’ Î²) (p : â„â‰¥0âˆ) (Î¼ : measure Î±) : Prop :=
(âˆ€ i, strongly_measurable (f i)) âˆ§ unif_integrable f p Î¼ âˆ§ âˆƒ C : â„â‰¥0, âˆ€ i, snorm (f i) p Î¼ â‰¤ C

lemma uniform_integrable.strongly_measurable {f : Î¹ â†’ Î± â†’ Î²} {p : â„â‰¥0âˆ}
  (hf : uniform_integrable f p Î¼) (i : Î¹) : strongly_measurable (f i) :=
hf.1 i

lemma uniform_integrable.unif_integrable {f : Î¹ â†’ Î± â†’ Î²} {p : â„â‰¥0âˆ}
  (hf : uniform_integrable f p Î¼) : unif_integrable f p Î¼ :=
hf.2.1

lemma uniform_integrable.mem_â„’p {f : Î¹ â†’ Î± â†’ Î²} {p : â„â‰¥0âˆ}
  (hf : uniform_integrable f p Î¼) (i : Î¹) :
  mem_â„’p (f i) p Î¼ :=
âŸ¨(hf.1 i).ae_strongly_measurable,
let âŸ¨_, _, hCâŸ© := hf.2 in lt_of_le_of_lt (hC i) ennreal.coe_lt_topâŸ©

section unif_integrable

/-! ### `unif_integrable`

This section deals with uniform integrability in the measure theory sense. -/

namespace unif_integrable

variables {f g : Î¹ â†’ Î± â†’ Î²} {p : â„â‰¥0âˆ}

protected lemma add
  (hf : unif_integrable f p Î¼) (hg : unif_integrable g p Î¼) (hp : 1 â‰¤ p)
  (hf_meas : âˆ€ i, ae_strongly_measurable (f i) Î¼) (hg_meas : âˆ€ i, ae_strongly_measurable (g i) Î¼) :
  unif_integrable (f + g) p Î¼ :=
begin
  intros Îµ hÎµ,
  have hÎµ2 : 0 < Îµ / 2 := half_pos hÎµ,
  obtain âŸ¨Î´â‚, hÎ´â‚_pos, hfÎ´â‚âŸ© := hf hÎµ2,
  obtain âŸ¨Î´â‚‚, hÎ´â‚‚_pos, hgÎ´â‚‚âŸ© := hg hÎµ2,
  refine âŸ¨min Î´â‚ Î´â‚‚, lt_min hÎ´â‚_pos hÎ´â‚‚_pos, Î» i s hs hÎ¼s, _âŸ©,
  simp_rw [pi.add_apply, indicator_add'],
  refine (snorm_add_le ((hf_meas i).indicator hs) ((hg_meas i).indicator hs) hp).trans _,
  have hÎµ_halves : ennreal.of_real Îµ = ennreal.of_real (Îµ / 2) + ennreal.of_real (Îµ / 2),
    by rw [â† ennreal.of_real_add hÎµ2.le hÎµ2.le, add_halves],
  rw hÎµ_halves,
  exact add_le_add (hfÎ´â‚ i s hs (hÎ¼s.trans (ennreal.of_real_le_of_real (min_le_left _ _))))
    (hgÎ´â‚‚ i s hs (hÎ¼s.trans (ennreal.of_real_le_of_real (min_le_right _ _)))),
end

protected lemma neg (hf : unif_integrable f p Î¼) : unif_integrable (-f) p Î¼ :=
by { simp_rw [unif_integrable, pi.neg_apply, indicator_neg', snorm_neg], exact hf, }

protected lemma sub
  (hf : unif_integrable f p Î¼) (hg : unif_integrable g p Î¼) (hp : 1 â‰¤ p)
  (hf_meas : âˆ€ i, ae_strongly_measurable (f i) Î¼) (hg_meas : âˆ€ i, ae_strongly_measurable (g i) Î¼) :
  unif_integrable (f - g) p Î¼ :=
by { rw sub_eq_add_neg, exact hf.add hg.neg hp hf_meas (Î» i, (hg_meas i).neg), }

protected lemma ae_eq (hf : unif_integrable f p Î¼) (hfg : âˆ€ n, f n =áµ[Î¼] g n) :
  unif_integrable g p Î¼ :=
begin
  intros Îµ hÎµ,
  obtain âŸ¨Î´, hÎ´_pos, hfÎ´âŸ© := hf hÎµ,
  refine âŸ¨Î´, hÎ´_pos, Î» n s hs hÎ¼s, (le_of_eq $ snorm_congr_ae _).trans (hfÎ´ n s hs hÎ¼s)âŸ©,
  filter_upwards [hfg n] with x hx,
  simp_rw [indicator_apply, hx],
end

end unif_integrable

lemma unif_integrable_zero_meas [measurable_space Î±] {p : â„â‰¥0âˆ} {f : Î¹ â†’ Î± â†’ Î²} :
  unif_integrable f p (0 : measure Î±) :=
Î» Îµ hÎµ, âŸ¨1, one_pos, Î» i s hs hÎ¼s, by simpâŸ©

lemma unif_integrable_congr_ae {p : â„â‰¥0âˆ} {f g : Î¹ â†’ Î± â†’ Î²} (hfg : âˆ€ n, f n =áµ[Î¼] g n) :
  unif_integrable f p Î¼ â†” unif_integrable g p Î¼ :=
âŸ¨Î» hf, hf.ae_eq hfg, Î» hg, hg.ae_eq (Î» n, (hfg n).symm)âŸ©

lemma tendsto_indicator_ge (f : Î± â†’ Î²) (x : Î±):
  tendsto (Î» M : â„•, {x | (M : â„) â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f x) at_top (ğ“ 0) :=
begin
  refine @tendsto_at_top_of_eventually_const _ _ _ _ _ _ _ (nat.ceil (âˆ¥f xâˆ¥â‚Š : â„) + 1) (Î» n hn, _),
  rw indicator_of_not_mem,
  simp only [not_le, mem_set_of_eq],
  refine lt_of_le_of_lt (nat.le_ceil _) _,
  refine lt_of_lt_of_le (lt_add_one _) _,
  norm_cast,
  rwa [ge_iff_le, coe_nnnorm] at hn,
end

variables (Î¼) {p : â„â‰¥0âˆ}

section

variables {f : Î± â†’ Î²}

/-- This lemma is weaker than `measure_theory.mem_â„’p.integral_indicator_norm_ge_nonneg_le`
as the latter provides `0 â‰¤ M` and does not require the measurability of `f`. -/
lemma mem_â„’p.integral_indicator_norm_ge_le
  (hf : mem_â„’p f 1 Î¼) (hmeas : strongly_measurable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ M : â„, âˆ«â» x, âˆ¥{x | M â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f xâˆ¥â‚Š âˆ‚Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  have htendsto : âˆ€áµ x âˆ‚Î¼, tendsto (Î» M : â„•, {x | (M : â„) â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f x) at_top (ğ“ 0) :=
    univ_mem' (id $ Î» x, tendsto_indicator_ge f x),
  have hmeas : âˆ€ M : â„•, ae_strongly_measurable ({x | (M : â„) â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f) Î¼,
  { assume M,
    apply hf.1.indicator,
    apply strongly_measurable.measurable_set_le strongly_measurable_const
      hmeas.nnnorm.measurable.coe_nnreal_real.strongly_measurable },
  have hbound : has_finite_integral (Î» x, âˆ¥f xâˆ¥) Î¼,
  { rw mem_â„’p_one_iff_integrable at hf,
    exact hf.norm.2 },
  have := tendsto_lintegral_norm_of_dominated_convergence hmeas hbound _ htendsto,
  { rw ennreal.tendsto_at_top_zero at this,
    obtain âŸ¨M, hMâŸ© := this (ennreal.of_real Îµ) (ennreal.of_real_pos.2 hÎµ),
    simp only [true_and, ge_iff_le, zero_tsub, zero_le,
              sub_zero, zero_add, coe_nnnorm, mem_Icc] at hM,
    refine âŸ¨M, _âŸ©,
    convert hM M le_rfl,
    ext1 x,
    simp only [coe_nnnorm, ennreal.of_real_eq_coe_nnreal (norm_nonneg _)],
    refl },
  { refine Î» n, univ_mem' (id $ Î» x, _),
    by_cases hx : (n : â„) â‰¤ âˆ¥f xâˆ¥,
    { dsimp,
      rwa indicator_of_mem },
    { dsimp,
      rw [indicator_of_not_mem, norm_zero],
      { exact norm_nonneg _ },
      { assumption } } }
end

/-- This lemma is superceded by `measure_theory.mem_â„’p.integral_indicator_norm_ge_nonneg_le`
which does not require measurability. -/
lemma mem_â„’p.integral_indicator_norm_ge_nonneg_le_of_meas
  (hf : mem_â„’p f 1 Î¼) (hmeas : strongly_measurable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ M : â„, 0 â‰¤ M âˆ§ âˆ«â» x, âˆ¥{x | M â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f xâˆ¥â‚Š âˆ‚Î¼ â‰¤ ennreal.of_real Îµ :=
let âŸ¨M, hMâŸ© := hf.integral_indicator_norm_ge_le Î¼ hmeas hÎµ in âŸ¨max M 0, le_max_right _ _, by simpaâŸ©

lemma mem_â„’p.integral_indicator_norm_ge_nonneg_le
  (hf : mem_â„’p f 1 Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ M : â„, 0 â‰¤ M âˆ§ âˆ«â» x, âˆ¥{x | M â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f xâˆ¥â‚Š âˆ‚Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  have hf_mk : mem_â„’p (hf.1.mk f) 1 Î¼ := (mem_â„’p_congr_ae hf.1.ae_eq_mk).mp hf,
  obtain âŸ¨M, hM_pos, hfMâŸ© := hf_mk.integral_indicator_norm_ge_nonneg_le_of_meas Î¼
    hf.1.strongly_measurable_mk hÎµ,
  refine âŸ¨M, hM_pos, (le_of_eq _).trans hfMâŸ©,
  refine lintegral_congr_ae _,
  filter_upwards [hf.1.ae_eq_mk] with x hx,
  simp only [indicator_apply, coe_nnnorm, mem_set_of_eq, ennreal.coe_eq_coe, hx.symm],
end

lemma mem_â„’p.snorm_ess_sup_indicator_norm_ge_eq_zero
  (hf : mem_â„’p f âˆ Î¼) (hmeas : strongly_measurable f) :
  âˆƒ M : â„, snorm_ess_sup ({x | M â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f) Î¼ = 0 :=
begin
  have hbdd : snorm_ess_sup f Î¼ < âˆ := hf.snorm_lt_top,
  refine âŸ¨(snorm f âˆ Î¼ + 1).to_real, _âŸ©,
  rw snorm_ess_sup_indicator_eq_snorm_ess_sup_restrict,
  have : Î¼.restrict {x : Î± | (snorm f âŠ¤ Î¼ + 1).to_real â‰¤ âˆ¥f xâˆ¥â‚Š} = 0,
  { simp only [coe_nnnorm, snorm_exponent_top, measure.restrict_eq_zero],
    have : {x : Î± | (snorm_ess_sup f Î¼ + 1).to_real â‰¤ âˆ¥f xâˆ¥} âŠ†
      {x : Î± | snorm_ess_sup f Î¼ < âˆ¥f xâˆ¥â‚Š},
    { intros x hx,
      rw [mem_set_of_eq, â† ennreal.to_real_lt_to_real hbdd.ne ennreal.coe_lt_top.ne,
          ennreal.coe_to_real, coe_nnnorm],
      refine lt_of_lt_of_le _ hx,
      rw ennreal.to_real_lt_to_real hbdd.ne,
      { exact ennreal.lt_add_right hbdd.ne one_ne_zero },
      { exact (ennreal.add_lt_top.2 âŸ¨hbdd, ennreal.one_lt_topâŸ©).ne } },
    rw â† nonpos_iff_eq_zero,
    refine (measure_mono this).trans _,
    have hle := coe_nnnorm_ae_le_snorm_ess_sup f Î¼,
    simp_rw [ae_iff, not_le] at hle,
    exact nonpos_iff_eq_zero.2 hle },
  rw [this, snorm_ess_sup_measure_zero],
  exact measurable_set_le measurable_const hmeas.nnnorm.measurable.subtype_coe,
end

/- This lemma is slightly weaker than `measure_theory.mem_â„’p.snorm_indicator_norm_ge_pos_le` as the
latter provides `0 < M`. -/
lemma mem_â„’p.snorm_indicator_norm_ge_le
  (hf : mem_â„’p f p Î¼) (hmeas : strongly_measurable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ M : â„, snorm ({x | M â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f) p Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  by_cases hp_ne_zero : p = 0,
  { refine âŸ¨1, hp_ne_zero.symm â–¸ _âŸ©,
    simp [snorm_exponent_zero] },
  by_cases hp_ne_top : p = âˆ,
  { subst hp_ne_top,
    obtain âŸ¨M, hMâŸ© := hf.snorm_ess_sup_indicator_norm_ge_eq_zero Î¼ hmeas,
    refine âŸ¨M, _âŸ©,
    simp only [snorm_exponent_top, hM, zero_le] },
  obtain âŸ¨M, hM', hMâŸ© := @mem_â„’p.integral_indicator_norm_ge_nonneg_le _ _ _ Î¼ _
    (Î» x, âˆ¥f xâˆ¥^p.to_real) (hf.norm_rpow hp_ne_zero hp_ne_top) _
    (real.rpow_pos_of_pos hÎµ p.to_real),
  refine âŸ¨M ^(1 / p.to_real), _âŸ©,
  rw [snorm_eq_lintegral_rpow_nnnorm hp_ne_zero hp_ne_top,
      â† ennreal.rpow_one (ennreal.of_real Îµ)],
  conv_rhs { rw â† mul_one_div_cancel (ennreal.to_real_pos hp_ne_zero hp_ne_top).ne.symm },
  rw [ennreal.rpow_mul,
      ennreal.rpow_le_rpow_iff (one_div_pos.2 $ ennreal.to_real_pos hp_ne_zero hp_ne_top),
      ennreal.of_real_rpow_of_pos hÎµ],
  convert hM,
  ext1 x,
  rw [ennreal.coe_rpow_of_nonneg _ ennreal.to_real_nonneg,
      nnnorm_indicator_eq_indicator_nnnorm, nnnorm_indicator_eq_indicator_nnnorm],
  have hiff : M ^ (1 / p.to_real) â‰¤ âˆ¥f xâˆ¥â‚Š â†” M â‰¤ âˆ¥âˆ¥f xâˆ¥ ^ p.to_realâˆ¥â‚Š,
  { rw [coe_nnnorm, coe_nnnorm, real.norm_rpow_of_nonneg (norm_nonneg _), norm_norm,
        â† real.rpow_le_rpow_iff hM' (real.rpow_nonneg_of_nonneg (norm_nonneg _) _)
        (one_div_pos.2 $ ennreal.to_real_pos hp_ne_zero hp_ne_top),
        â† real.rpow_mul (norm_nonneg _),
        mul_one_div_cancel (ennreal.to_real_pos hp_ne_zero hp_ne_top).ne.symm, real.rpow_one] },
  by_cases hx : x âˆˆ {x : Î± | M ^ (1 / p.to_real) â‰¤ âˆ¥f xâˆ¥â‚Š},
  { rw [set.indicator_of_mem hx,set.indicator_of_mem, real.nnnorm_of_nonneg], refl,
    change _ â‰¤ _,
    rwa â† hiff },
  { rw [set.indicator_of_not_mem hx, set.indicator_of_not_mem],
    { simp [(ennreal.to_real_pos hp_ne_zero hp_ne_top).ne.symm] },
    { change Â¬ _ â‰¤ _,
      rwa â† hiff } }
end

/-- This lemma implies that a single function is uniformly integrable (in the probability sense). -/
lemma mem_â„’p.snorm_indicator_norm_ge_pos_le
  (hf : mem_â„’p f p Î¼) (hmeas : strongly_measurable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ M : â„, 0 < M âˆ§ snorm ({x | M â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f) p Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  obtain âŸ¨M, hMâŸ© := hf.snorm_indicator_norm_ge_le Î¼ hmeas hÎµ,
  refine âŸ¨max M 1, lt_of_lt_of_le zero_lt_one (le_max_right _ _),
    le_trans (snorm_mono (Î» x, _)) hMâŸ©,
  rw [norm_indicator_eq_indicator_norm, norm_indicator_eq_indicator_norm],
  refine indicator_le_indicator_of_subset (Î» x hx, _) (Î» x, norm_nonneg _) x,
  change max _ _ â‰¤ _ at hx, -- removing the `change` breaks the proof!
  exact (max_le_iff.1 hx).1,
end

end

lemma snorm_indicator_le_of_bound {f : Î± â†’ Î²} (hp_top : p â‰  âˆ)
  {Îµ : â„} (hÎµ : 0 < Îµ) {M : â„} (hf : âˆ€ x, âˆ¥f xâˆ¥ < M) :
  âˆƒ (Î´ : â„) (hÎ´ : 0 < Î´), âˆ€ s, measurable_set s â†’ Î¼ s â‰¤ ennreal.of_real Î´ â†’
  snorm (s.indicator f) p Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  by_cases hM : M â‰¤ 0,
  { refine âŸ¨1, zero_lt_one, Î» s hs hÎ¼, _âŸ©,
    rw (_ : f = 0),
    { simp [hÎµ.le] },
    { ext x,
      rw [pi.zero_apply, â† norm_le_zero_iff],
      exact (lt_of_lt_of_le (hf x) hM).le } },
  rw not_le at hM,
  refine âŸ¨(Îµ / M) ^ p.to_real, real.rpow_pos_of_pos (div_pos hÎµ hM) _, Î» s hs hÎ¼, _âŸ©,
  by_cases hp : p = 0,
  { simp [hp] },
  rw snorm_indicator_eq_snorm_restrict hs,
  have haebdd : âˆ€áµ x âˆ‚Î¼.restrict s, âˆ¥f xâˆ¥ â‰¤ M,
  { filter_upwards,
    exact (Î» x, (hf x).le) },
  refine le_trans (snorm_le_of_ae_bound haebdd) _,
  rw [measure.restrict_apply measurable_set.univ, univ_inter,
    â† ennreal.le_div_iff_mul_le (or.inl _) (or.inl ennreal.of_real_ne_top)],
  { rw [â† one_div, ennreal.rpow_one_div_le_iff (ennreal.to_real_pos hp hp_top)],
    refine le_trans hÎ¼ _,
    rw [â† ennreal.of_real_rpow_of_pos (div_pos hÎµ hM),
      ennreal.rpow_le_rpow_iff (ennreal.to_real_pos hp hp_top), ennreal.of_real_div_of_pos hM],
    exact le_rfl },
  { simpa only [ennreal.of_real_eq_zero, not_le, ne.def] },
end

section

variables {f : Î± â†’ Î²}

/-- Auxiliary lemma for `measure_theory.mem_â„’p.snorm_indicator_le`. -/
lemma mem_â„’p.snorm_indicator_le' (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
  (hf : mem_â„’p f p Î¼) (hmeas : strongly_measurable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (Î´ : â„) (hÎ´ : 0 < Î´), âˆ€ s, measurable_set s â†’ Î¼ s â‰¤ ennreal.of_real Î´ â†’
  snorm (s.indicator f) p Î¼ â‰¤ 2 * ennreal.of_real Îµ :=
begin
  obtain âŸ¨M, hMpos, hMâŸ© := hf.snorm_indicator_norm_ge_pos_le Î¼ hmeas hÎµ,
  obtain âŸ¨Î´, hÎ´pos, hÎ´âŸ© := @snorm_indicator_le_of_bound _ _ _ Î¼ _ _
    ({x | âˆ¥f xâˆ¥ < M}.indicator f) hp_top _ hÎµ M _,
  { refine âŸ¨Î´, hÎ´pos, Î» s hs hÎ¼s, _âŸ©,
    rw (_ : f = {x : Î± | M â‰¤ âˆ¥f xâˆ¥â‚Š}.indicator f + {x : Î± | âˆ¥f xâˆ¥ < M}.indicator f),
    { rw snorm_indicator_eq_snorm_restrict hs,
      refine le_trans (snorm_add_le _ _ hp_one) _,
      { exact strongly_measurable.ae_strongly_measurable (hmeas.indicator
        (measurable_set_le measurable_const hmeas.nnnorm.measurable.subtype_coe)) },
      { exact strongly_measurable.ae_strongly_measurable (hmeas.indicator
        (measurable_set_lt hmeas.nnnorm.measurable.subtype_coe measurable_const)) },
      { rw two_mul,
        refine add_le_add (le_trans (snorm_mono_measure _ measure.restrict_le_self) hM) _,
        rw â† snorm_indicator_eq_snorm_restrict hs,
        exact hÎ´ s hs hÎ¼s } },
    { ext x,
      by_cases hx : M â‰¤ âˆ¥f xâˆ¥,
      { rw [pi.add_apply, indicator_of_mem, indicator_of_not_mem, add_zero];
        simpa },
      { rw [pi.add_apply, indicator_of_not_mem, indicator_of_mem, zero_add];
        simpa using hx } } },
  { intros x,
    rw [norm_indicator_eq_indicator_norm, indicator_apply],
    split_ifs,
    exacts [h, hMpos] }
end

/-- This lemma is superceded by `measure_theory.mem_â„’p.snorm_indicator_le` which does not require
measurability on `f`. -/
lemma mem_â„’p.snorm_indicator_le_of_meas (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
  (hf : mem_â„’p f p Î¼) (hmeas : strongly_measurable f) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (Î´ : â„) (hÎ´ : 0 < Î´), âˆ€ s, measurable_set s â†’ Î¼ s â‰¤ ennreal.of_real Î´ â†’
  snorm (s.indicator f) p Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  obtain âŸ¨Î´, hÎ´pos, hÎ´âŸ© := hf.snorm_indicator_le' Î¼ hp_one hp_top hmeas (half_pos hÎµ),
  refine âŸ¨Î´, hÎ´pos, Î» s hs hÎ¼s, le_trans (hÎ´ s hs hÎ¼s) _âŸ©,
  rw [ennreal.of_real_div_of_pos zero_lt_two, (by norm_num : ennreal.of_real 2 = 2),
    ennreal.mul_div_cancel'];
  norm_num,
end

lemma mem_â„’p.snorm_indicator_le (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
  (hf : mem_â„’p f p Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ (Î´ : â„) (hÎ´ : 0 < Î´), âˆ€ s, measurable_set s â†’ Î¼ s â‰¤ ennreal.of_real Î´ â†’
  snorm (s.indicator f) p Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  have hâ„’p := hf,
  obtain âŸ¨âŸ¨f', hf', heqâŸ©, hnormâŸ© := hf,
  obtain âŸ¨Î´, hÎ´pos, hÎ´âŸ© := (hâ„’p.ae_eq heq).snorm_indicator_le_of_meas Î¼ hp_one hp_top hf' hÎµ,
  refine âŸ¨Î´, hÎ´pos, Î» s hs hÎ¼s, _âŸ©,
  convert hÎ´ s hs hÎ¼s using 1,
  rw [snorm_indicator_eq_snorm_restrict hs, snorm_indicator_eq_snorm_restrict hs],
  refine snorm_congr_ae heq.restrict,
end

/-- A constant function is uniformly integrable. -/
lemma unif_integrable_const {g : Î± â†’ Î²} (hp : 1 â‰¤ p) (hp_ne_top : p â‰  âˆ) (hg : mem_â„’p g p Î¼) :
  unif_integrable (Î» n : Î¹, g) p Î¼ :=
begin
  intros Îµ hÎµ,
  obtain âŸ¨Î´, hÎ´_pos, hgÎ´âŸ© := hg.snorm_indicator_le Î¼ hp hp_ne_top hÎµ,
  exact âŸ¨Î´, hÎ´_pos, Î» i, hgÎ´âŸ©,
end

/-- A single function is uniformly integrable. -/
lemma unif_integrable_subsingleton [subsingleton Î¹]
  (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ) {f : Î¹ â†’ Î± â†’ Î²} (hf : âˆ€ i, mem_â„’p (f i) p Î¼) :
  unif_integrable f p Î¼ :=
begin
  intros Îµ hÎµ,
  by_cases hÎ¹ : nonempty Î¹,
  { cases hÎ¹ with i,
    obtain âŸ¨Î´, hÎ´pos, hÎ´âŸ© := (hf i).snorm_indicator_le Î¼ hp_one hp_top hÎµ,
    refine âŸ¨Î´, hÎ´pos, Î» j s hs hÎ¼s, _âŸ©,
    convert hÎ´ s hs hÎ¼s },
  { exact âŸ¨1, zero_lt_one, Î» i, false.elim $ hÎ¹ $ nonempty.intro iâŸ© }
end

/-- This lemma is less general than `measure_theory.unif_integrable_fintype` which applies to
all sequences indexed by a fintype. -/
lemma unif_integrable_fin (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
  {n : â„•} {f : fin n â†’ Î± â†’ Î²} (hf : âˆ€ i, mem_â„’p (f i) p Î¼) :
  unif_integrable f p Î¼ :=
begin
  revert f,
  induction n with n h,
  { exact (Î» f hf, unif_integrable_subsingleton Î¼ hp_one hp_top hf) },
  intros f hfLp Îµ hÎµ,
  set g : fin n â†’ Î± â†’ Î² := Î» k, f k with hg,
  have hgLp : âˆ€ i, mem_â„’p (g i) p Î¼ := Î» i, hfLp i,
  obtain âŸ¨Î´â‚, hÎ´â‚pos, hÎ´â‚âŸ© := h hgLp hÎµ,
  obtain âŸ¨Î´â‚‚, hÎ´â‚‚pos, hÎ´â‚‚âŸ© := (hfLp n).snorm_indicator_le Î¼ hp_one hp_top hÎµ,
  refine âŸ¨min Î´â‚ Î´â‚‚, lt_min hÎ´â‚pos hÎ´â‚‚pos, Î» i s hs hÎ¼s, _âŸ©,
  by_cases hi : i.val < n,
  { rw (_ : f i = g âŸ¨i.val, hiâŸ©),
    { exact hÎ´â‚ _ s hs (le_trans hÎ¼s $ ennreal.of_real_le_of_real $ min_le_left _ _) },
    { rw hg, simp } },
  { rw (_ : i = n),
    { exact hÎ´â‚‚ _ hs (le_trans hÎ¼s $ ennreal.of_real_le_of_real $ min_le_right _ _) },
    { have hi' := fin.is_lt i,
      rw nat.lt_succ_iff at hi',
      rw not_lt at hi,
      simp [â† le_antisymm hi' hi] } }
end

/-- A finite sequence of Lp functions is uniformly integrable. -/
lemma unif_integrable_fintype [fintype Î¹] (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
  {f : Î¹ â†’ Î± â†’ Î²} (hf : âˆ€ i, mem_â„’p (f i) p Î¼) :
  unif_integrable f p Î¼ :=
begin
  intros Îµ hÎµ,
  set g : fin (fintype.card Î¹) â†’ Î± â†’ Î² := f âˆ˜ (fintype.equiv_fin Î¹).symm,
  have hg : âˆ€ i, mem_â„’p (g i) p Î¼ := Î» _, hf _,
  obtain âŸ¨Î´, hÎ´pos, hÎ´âŸ© := unif_integrable_fin Î¼ hp_one hp_top hg hÎµ,
  exact âŸ¨Î´, hÎ´pos, Î» i s hs hÎ¼s,
    equiv.symm_apply_apply (fintype.equiv_fin Î¹) i â–¸ hÎ´ (fintype.equiv_fin Î¹ i) s hs hÎ¼sâŸ©,
end

end

lemma snorm_sub_le_of_dist_bdd
  {p : â„â‰¥0âˆ} (hp' : p â‰  âˆ) {s : set Î±} (hs : measurable_set[m] s)
  {f g : Î± â†’ Î²} {c : â„} (hc : 0 â‰¤ c) (hf : âˆ€ x âˆˆ s, dist (f x) (g x) â‰¤ c) :
  snorm (s.indicator (f - g)) p Î¼ â‰¤ ennreal.of_real c * Î¼ s ^ (1 / p.to_real) :=
begin
  by_cases hp : p = 0,
  { simp [hp], },
  have : âˆ€ x, âˆ¥s.indicator (f - g) xâˆ¥ â‰¤ âˆ¥s.indicator (Î» x, c) xâˆ¥,
  { intro x,
    by_cases hx : x âˆˆ s,
    { rw [indicator_of_mem hx, indicator_of_mem hx, pi.sub_apply, â† dist_eq_norm,
          real.norm_eq_abs, abs_of_nonneg hc],
      exact hf x hx },
    { simp [indicator_of_not_mem hx] } },
  refine le_trans (snorm_mono this) _,
  rw snorm_indicator_const hs hp hp',
  refine ennreal.mul_le_mul (le_of_eq _) le_rfl,
  rw [â† of_real_norm_eq_coe_nnnorm, real.norm_eq_abs, abs_of_nonneg hc],
end

/-- A sequence of uniformly integrable functions which converges Î¼-a.e. converges in Lp. -/
lemma tendsto_Lp_of_tendsto_ae_of_meas [is_finite_measure Î¼]
  (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²}
  (hf : âˆ€ n, strongly_measurable (f n)) (hg : strongly_measurable g)
  (hg' : mem_â„’p g p Î¼) (hui : unif_integrable f p Î¼)
  (hfg : âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, f n x) at_top (ğ“ (g x))) :
  tendsto (Î» n, snorm (f n - g) p Î¼) at_top (ğ“ 0) :=
begin
  rw ennreal.tendsto_at_top_zero,
  intros Îµ hÎµ,
  by_cases Îµ < âˆ, swap,
  { rw [not_lt, top_le_iff] at h,
    exact âŸ¨0, Î» n hn, by simp [h]âŸ© },
  by_cases hÎ¼ : Î¼ = 0,
  { exact âŸ¨0, Î» n hn, by simp [hÎ¼]âŸ© },
  have hÎµ' : 0 < Îµ.to_real / 3 :=
    div_pos (ennreal.to_real_pos (gt_iff_lt.1 hÎµ).ne.symm h.ne) (by norm_num),
  have hdivp : 0 â‰¤ 1 / p.to_real,
  { refine one_div_nonneg.2 _,
    rw [â† ennreal.zero_to_real, ennreal.to_real_le_to_real ennreal.zero_ne_top hp'],
    exact le_trans ennreal.zero_lt_one.le hp },
  have hpow : 0 < (measure_univ_nnreal Î¼) ^ (1 / p.to_real) :=
    real.rpow_pos_of_pos (measure_univ_nnreal_pos hÎ¼) _,
  obtain âŸ¨Î´â‚, hÎ´â‚, hsnormâ‚âŸ© := hui hÎµ',
  obtain âŸ¨Î´â‚‚, hÎ´â‚‚, hsnormâ‚‚âŸ© := hg'.snorm_indicator_le Î¼ hp hp' hÎµ',
  obtain âŸ¨t, htm, htâ‚, htâ‚‚âŸ© := tendsto_uniformly_on_of_ae_tendsto' hf
    hg hfg (lt_min hÎ´â‚ hÎ´â‚‚),
  rw metric.tendsto_uniformly_on_iff at htâ‚‚,
  specialize htâ‚‚ (Îµ.to_real / (3 * measure_univ_nnreal Î¼ ^ (1 / p.to_real)))
    (div_pos (ennreal.to_real_pos (gt_iff_lt.1 hÎµ).ne.symm h.ne) (mul_pos (by norm_num) hpow)),
  obtain âŸ¨N, hNâŸ© := eventually_at_top.1 htâ‚‚, clear htâ‚‚,
  refine âŸ¨N, Î» n hn, _âŸ©,
  rw [â† t.indicator_self_add_compl (f n - g)],
  refine le_trans (snorm_add_le ((((hf n).sub hg).indicator htm).ae_strongly_measurable)
    (((hf n).sub hg).indicator htm.compl).ae_strongly_measurable hp) _,
  rw [sub_eq_add_neg, indicator_add' t, indicator_neg'],
  refine le_trans (add_le_add_right (snorm_add_le ((hf n).indicator htm).ae_strongly_measurable
    (hg.indicator htm).neg.ae_strongly_measurable hp) _) _,
  have hnf : snorm (t.indicator (f n)) p Î¼ â‰¤ ennreal.of_real (Îµ.to_real / 3),
  { refine hsnormâ‚ n t htm (le_trans htâ‚ _),
    rw ennreal.of_real_le_of_real_iff hÎ´â‚.le,
    exact min_le_left _ _ },
  have hng : snorm (t.indicator g) p Î¼ â‰¤ ennreal.of_real (Îµ.to_real / 3),
  { refine hsnormâ‚‚ t htm (le_trans htâ‚ _),
    rw ennreal.of_real_le_of_real_iff hÎ´â‚‚.le,
    exact min_le_right _ _ },
  have hlt : snorm (tá¶œ.indicator (f n - g)) p Î¼ â‰¤ ennreal.of_real (Îµ.to_real / 3),
  { specialize hN n hn,
    have := snorm_sub_le_of_dist_bdd Î¼
      hp' htm.compl _ (Î» x hx, (dist_comm (g x) (f n x) â–¸ (hN x hx).le :
      dist (f n x) (g x) â‰¤ Îµ.to_real / (3 * measure_univ_nnreal Î¼ ^ (1 / p.to_real)))),
    refine le_trans this _,
    rw [div_mul_eq_div_mul_one_div, â† ennreal.of_real_to_real (measure_lt_top Î¼ tá¶œ).ne,
        ennreal.of_real_rpow_of_nonneg ennreal.to_real_nonneg hdivp, â† ennreal.of_real_mul,
        mul_assoc],
    { refine ennreal.of_real_le_of_real (mul_le_of_le_one_right hÎµ'.le _),
      rw [mul_comm, mul_one_div, div_le_one],
      { refine real.rpow_le_rpow ennreal.to_real_nonneg
          (ennreal.to_real_le_of_le_of_real (measure_univ_nnreal_pos hÎ¼).le _) hdivp,
        rw [ennreal.of_real_coe_nnreal, coe_measure_univ_nnreal],
        exact measure_mono (subset_univ _) },
      { exact real.rpow_pos_of_pos (measure_univ_nnreal_pos hÎ¼) _ } },
    { refine mul_nonneg (hÎµ').le (one_div_nonneg.2 hpow.le) },
    { rw div_mul_eq_div_mul_one_div,
      exact mul_nonneg hÎµ'.le (one_div_nonneg.2 hpow.le) } },
  have : ennreal.of_real (Îµ.to_real / 3) = Îµ / 3,
  { rw [ennreal.of_real_div_of_pos (show (0 : â„) < 3, by norm_num), ennreal.of_real_to_real h.ne],
    simp },
  rw this at hnf hng hlt,
  rw [snorm_neg, â† ennreal.add_thirds Îµ, â† sub_eq_add_neg],
  exact add_le_add_three hnf hng hlt
end

/-- A sequence of uniformly integrable functions which converges Î¼-a.e. converges in Lp. -/
lemma tendsto_Lp_of_tendsto_ae [is_finite_measure Î¼]
  (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²}
  (hf : âˆ€ n, ae_strongly_measurable (f n) Î¼) (hg : mem_â„’p g p Î¼) (hui : unif_integrable f p Î¼)
  (hfg : âˆ€áµ x âˆ‚Î¼, tendsto (Î» n, f n x) at_top (ğ“ (g x))) :
  tendsto (Î» n, snorm (f n - g) p Î¼) at_top (ğ“ 0) :=
begin
  suffices : tendsto (Î» (n : â„•), snorm ((hf n).mk (f n) - (hg.1.mk g)) p Î¼) at_top (ğ“ 0),
  { convert this,
    exact funext (Î» n, snorm_congr_ae ((hf n).ae_eq_mk.sub hg.1.ae_eq_mk)), },
  refine tendsto_Lp_of_tendsto_ae_of_meas Î¼ hp hp' (Î» n, (hf n).strongly_measurable_mk)
    hg.1.strongly_measurable_mk (hg.ae_eq hg.1.ae_eq_mk) (hui.ae_eq (Î» n, (hf n).ae_eq_mk)) _,
  have h_ae_forall_eq : âˆ€áµ x âˆ‚Î¼, âˆ€ n, f n x = (hf n).mk (f n) x,
  { rw ae_all_iff,
    exact Î» n, (hf n).ae_eq_mk, },
  filter_upwards [hfg, h_ae_forall_eq, hg.1.ae_eq_mk] with x hx_tendsto hxf_eq hxg_eq,
  rw â† hxg_eq,
  convert hx_tendsto,
  ext1 n,
  exact (hxf_eq n).symm,
end

variables {f : â„• â†’ Î± â†’ Î²} {g : Î± â†’ Î²}

lemma unif_integrable_of_tendsto_Lp_zero (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
  (hf : âˆ€ n, mem_â„’p (f n) p Î¼) (hf_tendsto : tendsto (Î» n, snorm (f n) p Î¼) at_top (ğ“ 0)) :
  unif_integrable f p Î¼ :=
begin
  intros Îµ hÎµ,
  rw ennreal.tendsto_at_top_zero at hf_tendsto,
  obtain âŸ¨N, hNâŸ© := hf_tendsto (ennreal.of_real Îµ) (by simpa),
  set F : fin N â†’ Î± â†’ Î² := Î» n, f n,
  have hF : âˆ€ n, mem_â„’p (F n) p Î¼ := Î» n, hf n,
  obtain âŸ¨Î´â‚, hÎ´posâ‚, hÎ´â‚âŸ© := unif_integrable_fin Î¼ hp hp' hF hÎµ,
  refine âŸ¨Î´â‚, hÎ´posâ‚, Î» n s hs hÎ¼s, _âŸ©,
  by_cases hn : n < N,
  { exact hÎ´â‚ âŸ¨n, hnâŸ© s hs hÎ¼s },
  { exact (snorm_indicator_le _).trans (hN n (not_lt.1 hn)) },
end

/-- Convergence in Lp implies uniform integrability. -/
lemma unif_integrable_of_tendsto_Lp
  (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) (hf : âˆ€ n, mem_â„’p (f n) p Î¼) (hg : mem_â„’p g p Î¼)
  (hfg : tendsto (Î» n, snorm (f n - g) p Î¼) at_top (ğ“ 0)) :
  unif_integrable f p Î¼ :=
begin
  have : f = (Î» n, g) + Î» n, f n - g, by { ext1 n, simp, },
  rw this,
  refine unif_integrable.add _ _ hp
    (Î» _, hg.ae_strongly_measurable) (Î» n, (hf n).1.sub hg.ae_strongly_measurable),
  { exact unif_integrable_const Î¼ hp hp' hg, },
  { exact unif_integrable_of_tendsto_Lp_zero Î¼ hp hp' (Î» n, (hf n).sub hg) hfg, },
end

/-- Forward direction of Vitali's convergence theorem: if `f` is a sequence of uniformly integrable
functions that converge in measure to some function `g` in a finite measure space, then `f`
converge in Lp to `g`. -/
lemma tendsto_Lp_of_tendsto_in_measure [is_finite_measure Î¼]
  (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) (hf : âˆ€ n, ae_strongly_measurable (f n) Î¼)
  (hg : mem_â„’p g p Î¼) (hui : unif_integrable f p Î¼)
  (hfg : tendsto_in_measure Î¼ f at_top g) :
  tendsto (Î» n, snorm (f n - g) p Î¼) at_top (ğ“ 0) :=
begin
  refine tendsto_of_subseq_tendsto (Î» ns hns, _),
  obtain âŸ¨ms, hms, hms'âŸ© := tendsto_in_measure.exists_seq_tendsto_ae (Î» Îµ hÎµ, (hfg Îµ hÎµ).comp hns),
  exact âŸ¨ms, tendsto_Lp_of_tendsto_ae Î¼ hp hp' (Î» _, hf _) hg
    (Î» Îµ hÎµ, let âŸ¨Î´, hÎ´, hÎ´'âŸ© := hui hÎµ in âŸ¨Î´, hÎ´, Î» i s hs hÎ¼s, hÎ´' _ s hs hÎ¼sâŸ©) hms'âŸ©,
end

/-- **Vitali's convergence theorem**: A sequence of functions `f` converges to `g` in Lp if and
only if it is uniformly integrable and converges to `g` in measure. -/
lemma tendsto_in_measure_iff_tendsto_Lp [is_finite_measure Î¼]
  (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) (hf : âˆ€ n, mem_â„’p (f n) p Î¼) (hg : mem_â„’p g p Î¼) :
  tendsto_in_measure Î¼ f at_top g âˆ§ unif_integrable f p Î¼ â†”
  tendsto (Î» n, snorm (f n - g) p Î¼) at_top (ğ“ 0) :=
âŸ¨Î» h, tendsto_Lp_of_tendsto_in_measure Î¼ hp hp' (Î» n, (hf n).1) hg h.2 h.1,
  Î» h, âŸ¨tendsto_in_measure_of_tendsto_snorm
    (lt_of_lt_of_le ennreal.zero_lt_one hp).ne.symm
    (Î» n, (hf n).ae_strongly_measurable)
    hg.ae_strongly_measurable h, unif_integrable_of_tendsto_Lp Î¼ hp hp' hf hg hâŸ©âŸ©

/-- This lemma is superceded by `unif_integrable_of` which do not require `C` to be positive. -/
lemma unif_integrable_of' (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) {f : Î¹ â†’ Î± â†’ Î²}
  (hf : âˆ€ i, strongly_measurable (f i))
  (h : âˆ€ Îµ : â„, 0 < Îµ â†’ âˆƒ C : â„â‰¥0, 0 < C âˆ§
    âˆ€ i, snorm ({x | C â‰¤ âˆ¥f i xâˆ¥â‚Š}.indicator (f i)) p Î¼ â‰¤ ennreal.of_real Îµ) :
  unif_integrable f p Î¼ :=
begin
  have hpzero := (lt_of_lt_of_le ennreal.zero_lt_one hp).ne.symm,
  by_cases hÎ¼ : Î¼ set.univ = 0,
  { rw measure.measure_univ_eq_zero at hÎ¼,
    exact hÎ¼.symm â–¸ unif_integrable_zero_meas },
  intros Îµ hÎµ,
  obtain âŸ¨C, hCpos, hCâŸ© := h (Îµ / 2) (half_pos hÎµ),
  refine âŸ¨(Îµ / (2 * C)) ^ ennreal.to_real p, real.rpow_pos_of_pos
    (div_pos hÎµ (mul_pos two_pos (nnreal.coe_pos.2 hCpos))) _, Î» i s hs hÎ¼s, _âŸ©,
  by_cases hÎ¼s' : Î¼ s = 0,
  { rw (snorm_eq_zero_iff ((hf i).indicator hs).ae_strongly_measurable hpzero).2
      (indicator_meas_zero hÎ¼s'),
    norm_num },
  calc snorm (indicator s (f i)) p Î¼ â‰¤ snorm (indicator (s âˆ© {x | C â‰¤ âˆ¥f i xâˆ¥â‚Š}) (f i)) p Î¼ +
    snorm (indicator (s âˆ© {x | âˆ¥f i xâˆ¥â‚Š < C}) (f i)) p Î¼ :
    begin
      refine le_trans (eq.le _) (snorm_add_le (strongly_measurable.ae_strongly_measurable
        ((hf i).indicator (hs.inter (strongly_measurable_const.measurable_set_le (hf i).nnnorm))))
        (strongly_measurable.ae_strongly_measurable ((hf i).indicator (hs.inter
        ((hf i).nnnorm.measurable_set_lt strongly_measurable_const)))) hp),
      congr,
      change _ = Î» x, (s âˆ© {x : Î± | C â‰¤ âˆ¥f i xâˆ¥â‚Š}).indicator (f i) x +
        (s âˆ© {x : Î± | âˆ¥f i xâˆ¥â‚Š < C}).indicator (f i) x,
      rw â† set.indicator_union_of_disjoint,
      { congr,
        rw [â† inter_union_distrib_left, (by { ext, simp [le_or_lt] } :
          {x : Î± | C â‰¤ âˆ¥f i xâˆ¥â‚Š} âˆª {x : Î± | âˆ¥f i xâˆ¥â‚Š < C} = set.univ), inter_univ] },
      { refine (disjoint.inf_right' _ _).inf_left' _,
        rintro x âŸ¨hxâ‚ : _ â‰¤ _, hxâ‚‚ : _ < _âŸ©,
        exact false.elim (hxâ‚‚.ne (eq_of_le_of_not_lt hxâ‚ (not_lt.2 hxâ‚‚.le)).symm) }
    end
    ... â‰¤ snorm (indicator ({x | C â‰¤ âˆ¥f i xâˆ¥â‚Š}) (f i)) p Î¼ + C * Î¼ s ^ (1 / ennreal.to_real p) :
    begin
      refine add_le_add (snorm_mono $ Î» x, norm_indicator_le_of_subset
        (inter_subset_right _ _) _ _) _,
      rw â† indicator_indicator,
      rw snorm_indicator_eq_snorm_restrict,
      have : âˆ€áµ x âˆ‚(Î¼.restrict s), âˆ¥({x : Î± | âˆ¥f i xâˆ¥â‚Š < C}).indicator (f i) xâˆ¥ â‰¤ C,
      { refine ae_of_all _ _,
        simp_rw norm_indicator_eq_indicator_norm,
        exact indicator_le' (Î» x (hx : _ < _), hx.le) (Î» _ _, nnreal.coe_nonneg _) },
      refine le_trans (snorm_le_of_ae_bound this) _,
      rw [mul_comm, measure.restrict_apply' hs, univ_inter,
        ennreal.of_real_coe_nnreal, one_div],
      exacts [le_rfl, hs],
    end
    ... â‰¤ ennreal.of_real (Îµ / 2) + C * ennreal.of_real (Îµ / (2 * C)) :
    begin
      refine add_le_add (hC i) (mul_le_mul_left' _ _),
      rwa [ennreal.rpow_one_div_le_iff (ennreal.to_real_pos hpzero hp'),
        ennreal.of_real_rpow_of_pos (div_pos hÎµ (mul_pos two_pos (nnreal.coe_pos.2 hCpos)))]
    end
    ... â‰¤ ennreal.of_real (Îµ / 2) + ennreal.of_real (Îµ / 2) :
    begin
      refine add_le_add_left _ _,
      rw [â† ennreal.of_real_coe_nnreal, â† ennreal.of_real_mul (nnreal.coe_nonneg _),
        â† div_div, mul_div_cancel' _ (nnreal.coe_pos.2 hCpos).ne.symm],
      exact le_rfl,
    end
    ... â‰¤ ennreal.of_real Îµ :
    begin
      rw [â† ennreal.of_real_add (half_pos hÎµ).le (half_pos hÎµ).le, add_halves],
      exact le_rfl,
    end
end

lemma unif_integrable_of (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) {f : Î¹ â†’ Î± â†’ Î²}
  (hf : âˆ€ i, strongly_measurable (f i))
  (h : âˆ€ Îµ : â„, 0 < Îµ â†’ âˆƒ C : â„â‰¥0,
    âˆ€ i, snorm ({x | C â‰¤ âˆ¥f i xâˆ¥â‚Š}.indicator (f i)) p Î¼ â‰¤ ennreal.of_real Îµ) :
  unif_integrable f p Î¼ :=
begin
  refine unif_integrable_of' Î¼ hp hp' hf (Î» Îµ hÎµ, _),
  obtain âŸ¨C, hCâŸ© := h Îµ hÎµ,
  refine âŸ¨max C 1, lt_max_of_lt_right one_pos, Î» i, le_trans (snorm_mono (Î» x, _)) (hC i)âŸ©,
  rw [norm_indicator_eq_indicator_norm, norm_indicator_eq_indicator_norm],
  exact indicator_le_indicator_of_subset
    (Î» x hx, le_trans (le_max_left _ _) hx) (Î» _, norm_nonneg _) _,
end

end unif_integrable

section uniform_integrable

/-! `uniform_integrable`

In probability theory, uniform integrability normally refers to the condition that a sequence
of function `(fâ‚™)` satisfies for all `Îµ > 0`, there exists some `C â‰¥ 0` such that
`âˆ« x in {|fâ‚™| â‰¥ C}, fâ‚™ x âˆ‚Î¼ â‰¤ Îµ` for all `n`.

In this section, we will develope some API for `uniform_integrable` and prove that
`uniform_integrable` is equivalent to this definition of uniform integrability.
-/

variables {p : â„â‰¥0âˆ} {f : Î¹ â†’ Î± â†’ Î²}

lemma uniform_integrable_zero_meas [measurable_space Î±] (hf : âˆ€ i, strongly_measurable (f i)) :
  uniform_integrable f p (0 : measure Î±) :=
âŸ¨hf, unif_integrable_zero_meas, 0, Î» i, snorm_measure_zero.leâŸ©

lemma uniform_integrable.ae_eq {g : Î¹ â†’ Î± â†’ Î²}
  (hf : uniform_integrable f p Î¼) (hg : âˆ€ i, strongly_measurable (g i)) (hfg : âˆ€ n, f n =áµ[Î¼] g n) :
  uniform_integrable g p Î¼ :=
begin
  obtain âŸ¨-, hunif, C, hCâŸ© := hf,
  refine âŸ¨hg, (unif_integrable_congr_ae hfg).1 hunif, C, Î» i, _âŸ©,
  rw â† snorm_congr_ae (hfg i),
  exact hC i
end

lemma uniform_integrable_congr_ae {g : Î¹ â†’ Î± â†’ Î²}
  (hf : âˆ€ i, strongly_measurable (f i)) (hg : âˆ€ i, strongly_measurable (g i))
  (hfg : âˆ€ n, f n =áµ[Î¼] g n) :
  uniform_integrable f p Î¼ â†” uniform_integrable g p Î¼ :=
âŸ¨Î» h, h.ae_eq hg hfg, Î» h, h.ae_eq hf (Î» i, (hfg i).symm)âŸ©

/-- A finite sequence of Lp functions is uniformly integrable in the probability sense. -/
lemma uniform_integrable_fintype [fintype Î¹] (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
  (hf : âˆ€ i, strongly_measurable (f i)) (hf' : âˆ€ i, mem_â„’p (f i) p Î¼) :
  uniform_integrable f p Î¼ :=
begin
  refine âŸ¨hf, unif_integrable_fintype Î¼ hp_one hp_top hf', _âŸ©,
  by_cases hÎ¹ : nonempty Î¹,
  { choose ae_meas hf using hf',
    set C := (finset.univ.image (Î» i : Î¹, snorm (f i) p Î¼)).max'
      âŸ¨snorm (f hÎ¹.some) p Î¼, finset.mem_image.2 âŸ¨hÎ¹.some, finset.mem_univ _, rflâŸ©âŸ©,
    refine âŸ¨C.to_nnreal, Î» i, _âŸ©,
    rw ennreal.coe_to_nnreal,
    { exact finset.le_max' _ _ (finset.mem_image.2 âŸ¨i, finset.mem_univ _, rflâŸ©) },
    { refine ne_of_lt ((finset.max'_lt_iff _ _).2 (Î» y hy, _)),
      rw finset.mem_image at hy,
      obtain âŸ¨i, -, rflâŸ© := hy,
      exact hf i } },
  { exact âŸ¨0, Î» i, false.elim $ hÎ¹ $ nonempty.intro iâŸ© }
end

/-- A single function is uniformly integrable in the probability sense. -/
lemma uniform_integrable_subsingleton [subsingleton Î¹] (hp_one : 1 â‰¤ p) (hp_top : p â‰  âˆ)
  (hf : âˆ€ i, strongly_measurable (f i)) (hf' : âˆ€ i, mem_â„’p (f i) p Î¼) :
  uniform_integrable f p Î¼ :=
uniform_integrable_fintype hp_one hp_top hf hf'

/-- A constant sequence of functions is uniformly integrable in the probability sense. -/
lemma uniform_integrable_const {g : Î± â†’ Î²} (hp : 1 â‰¤ p) (hp_ne_top : p â‰  âˆ)
  (hgm : strongly_measurable g) (hg : mem_â„’p g p Î¼) :
  uniform_integrable (Î» n : Î¹, g) p Î¼ :=
âŸ¨Î» i, hgm, unif_integrable_const Î¼ hp hp_ne_top hg,
  âŸ¨(snorm g p Î¼).to_nnreal, Î» i, le_of_eq (ennreal.coe_to_nnreal hg.2.ne).symmâŸ©âŸ©

/-- A sequene of functions `(fâ‚™)` is uniformly integrable in the probability sense if for all
`Îµ > 0`, there exists some `C` such that `âˆ« x in {|fâ‚™| â‰¥ C}, fâ‚™ x âˆ‚Î¼ â‰¤ Îµ` for all `n`. -/
lemma uniform_integrable_of [is_finite_measure Î¼] (hp : 1 â‰¤ p) (hp' : p â‰  âˆ)
  (hf : âˆ€ i, strongly_measurable (f i))
  (h : âˆ€ Îµ : â„, 0 < Îµ â†’ âˆƒ C : â„â‰¥0,
    âˆ€ i, snorm ({x | C â‰¤ âˆ¥f i xâˆ¥â‚Š}.indicator (f i)) p Î¼ â‰¤ ennreal.of_real Îµ) :
  uniform_integrable f p Î¼ :=
begin
  refine âŸ¨hf, unif_integrable_of Î¼ hp hp' hf h, _âŸ©,
  obtain âŸ¨C, hCâŸ© := h 1 one_pos,
  refine âŸ¨(C * (Î¼ univ ^ (p.to_realâ»Â¹)) + 1 : â„â‰¥0âˆ).to_nnreal, Î» i, _âŸ©,
  calc snorm (f i) p Î¼ â‰¤ snorm ({x : Î± | âˆ¥f i xâˆ¥â‚Š < C}.indicator (f i)) p Î¼ +
    snorm ({x : Î± | C â‰¤ âˆ¥f i xâˆ¥â‚Š}.indicator (f i)) p Î¼ :
  begin
    refine le_trans (snorm_mono (Î» x, _)) (snorm_add_le
      (strongly_measurable.ae_strongly_measurable ((hf i).indicator
      ((hf i).nnnorm.measurable_set_lt strongly_measurable_const)))
      (strongly_measurable.ae_strongly_measurable ((hf i).indicator
      (strongly_measurable_const.measurable_set_le (hf i).nnnorm))) hp),
    { rw [pi.add_apply, indicator_apply],
      split_ifs with hx,
      { rw [indicator_of_not_mem, add_zero],
        simpa using hx },
      { rw [indicator_of_mem, zero_add],
        simpa using hx } }
  end
  ... â‰¤ C * Î¼ univ ^ (p.to_realâ»Â¹) + 1 :
  begin
    have : âˆ€áµ x âˆ‚Î¼, âˆ¥{x : Î± | âˆ¥f i xâˆ¥â‚Š < C}.indicator (f i) xâˆ¥â‚Š â‰¤ C,
    { refine eventually_of_forall _,
      simp_rw nnnorm_indicator_eq_indicator_nnnorm,
      exact indicator_le (Î» x (hx : _ < _), hx.le) },
    refine add_le_add (le_trans (snorm_le_of_ae_bound this) _) (ennreal.of_real_one â–¸ (hC i)),
    rw [ennreal.of_real_coe_nnreal, mul_comm],
    exact le_rfl,
  end
  ... = (C * (Î¼ univ ^ (p.to_realâ»Â¹)) + 1 : â„â‰¥0âˆ).to_nnreal :
  begin
    rw ennreal.coe_to_nnreal,
    exact ennreal.add_ne_top.2 âŸ¨ennreal.mul_ne_top ennreal.coe_ne_top
      (ennreal.rpow_ne_top_of_nonneg (inv_nonneg.2 ennreal.to_real_nonneg)
      (measure_lt_top _ _).ne), ennreal.one_ne_topâŸ©,
  end
end

lemma uniform_integrable.spec (hp : p â‰  0) (hp' : p â‰  âˆ)
  (hfu : uniform_integrable f p Î¼) {Îµ : â„} (hÎµ : 0 < Îµ) :
  âˆƒ C : â„â‰¥0, âˆ€ i, snorm ({x | C â‰¤ âˆ¥f i xâˆ¥â‚Š}.indicator (f i)) p Î¼ â‰¤ ennreal.of_real Îµ :=
begin
  obtain âŸ¨hfâ‚€, hfu, M, hMâŸ© := hfu,
  obtain âŸ¨Î´, hÎ´pos, hÎ´âŸ© := hfu hÎµ,
  obtain âŸ¨C, hCâŸ© : âˆƒ C : â„â‰¥0, âˆ€ i, Î¼ {x | C â‰¤ âˆ¥f i xâˆ¥â‚Š} â‰¤ ennreal.of_real Î´,
  { by_contra hcon, push_neg at hcon,
    choose â„ hâ„ using hcon,
    lift Î´ to â„â‰¥0 using hÎ´pos.le,
    have : âˆ€ C : â„â‰¥0, C â€¢ (Î´ : â„â‰¥0âˆ) ^ (1 / p.to_real) â‰¤ snorm (f (â„ C)) p Î¼,
    { intros C,
      calc C â€¢ (Î´ : â„â‰¥0âˆ) ^ (1 / p.to_real) â‰¤ C â€¢ Î¼ {x | C â‰¤ âˆ¥f (â„ C) xâˆ¥â‚Š} ^ (1 / p.to_real):
      begin
        rw [ennreal.smul_def, ennreal.smul_def, smul_eq_mul, smul_eq_mul],
        simp_rw ennreal.of_real_coe_nnreal at hâ„,
        refine ennreal.mul_le_mul le_rfl (ennreal.rpow_le_rpow (hâ„ C).le
          (one_div_nonneg.2 ennreal.to_real_nonneg)),
      end
      ... â‰¤ snorm ({x | C â‰¤ âˆ¥f (â„ C) xâˆ¥â‚Š}.indicator (f (â„ C))) p Î¼ :
      begin
        refine snorm_indicator_ge_of_bdd_below hp hp' _
          (measurable_set_le measurable_const (hfâ‚€ _).nnnorm.measurable)
          (eventually_of_forall $ Î» x hx, _),
        rwa [nnnorm_indicator_eq_indicator_nnnorm, indicator_of_mem hx],
      end
      ... â‰¤ snorm (f (â„ C)) p Î¼ : snorm_indicator_le _ },
    specialize this ((2 * (max M 1) * (Î´â»Â¹ ^ (1 / p.to_real)))),
    rw [ennreal.coe_rpow_of_nonneg _ (one_div_nonneg.2 ennreal.to_real_nonneg),
      â† ennreal.coe_smul, smul_eq_mul, mul_assoc, nnreal.inv_rpow,
      inv_mul_cancel (nnreal.rpow_pos (nnreal.coe_pos.1 hÎ´pos)).ne.symm, mul_one,
      ennreal.coe_mul, â† nnreal.inv_rpow] at this,
    refine (lt_of_le_of_lt (le_trans (hM $ â„ $ 2 * (max M 1) * (Î´â»Â¹ ^ (1 / p.to_real)))
      (le_max_left M 1)) (lt_of_lt_of_le _ this)).ne rfl,
    rw [â† ennreal.coe_one, â† with_top.coe_max, â† ennreal.coe_mul, ennreal.coe_lt_coe],
    exact lt_two_mul_self (lt_max_of_lt_right one_pos) },
  exact âŸ¨C, Î» i, hÎ´ i _ (measurable_set_le measurable_const (hfâ‚€ i).nnnorm.measurable) (hC i)âŸ©,
end

/-- The definition of uniform integrable in mathlib is equivalent to the definition commonly
found in literature. -/
lemma uniform_integrable_iff [is_finite_measure Î¼] (hp : 1 â‰¤ p) (hp' : p â‰  âˆ) :
  uniform_integrable f p Î¼ â†” (âˆ€ i, strongly_measurable (f i)) âˆ§
  âˆ€ Îµ : â„, 0 < Îµ â†’ âˆƒ C : â„â‰¥0,
    âˆ€ i, snorm ({x | C â‰¤ âˆ¥f i xâˆ¥â‚Š}.indicator (f i)) p Î¼ â‰¤ ennreal.of_real Îµ  :=
âŸ¨Î» h, âŸ¨h.1, Î» Îµ, h.spec (lt_of_lt_of_le ennreal.zero_lt_one hp).ne.symm hp'âŸ©,
 Î» h, uniform_integrable_of hp hp' h.1 h.2âŸ©

end uniform_integrable

end measure_theory
