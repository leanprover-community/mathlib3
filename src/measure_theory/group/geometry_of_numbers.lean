/-
Copyright (c) 2021 Alex J. Best. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex J. Best
-/
import algebra.module.pi
import algebra.module.pointwise_pi
import analysis.convex.measure
import measure_theory.group.fundamental_domain

/-!
# Geometry of numbers

In this file we prove some of the fundamental theorems in the geometry of numbers, as studied by
Hermann Minkowski.

## Main results

- `exists_sub_mem_lattice_of_volume_lt_volume`: Blichfeldt's principle, existence of two points
  within a set whose difference lies in a subgroup when the covolume of the subgroup is larger than
  the set.
- `exists_nonzero_mem_lattice_of_volume_mul_two_pow_card_lt_measure`: Minkowski's theorem, existence
  of a non-zero lattice point inside a convex symmetric domain of large enough covolume.

## TODO

* A finite index subgroup has given fundamental domain and covolume
* Some existence result in a metric space
* Voronoi

See https://arxiv.org/pdf/1405.2119.pdf for some more ideas.
-/

section
variables {E G H : Type*} [group G] [group H] [mul_equiv_class E G H]

@[simp, to_additive]
lemma mul_equiv.coe_to_equiv_symm (e : G â‰ƒ* H) : (e.symm : H â‰ƒ G) = (e : G â‰ƒ H).symm := rfl

namespace subgroup

/-- A subgroup is isomorphic to its image under an isomorphism. If you only have an injective map,
use `subgroup.equiv_map_of_injective`. -/
@[to_additive  "An additive subgroup is isomorphic to its image under an an isomorphism. If you only
have an injective map, use `add_subgroup.equiv_map_of_injective`. "]
def equiv_map (L : subgroup G) (e : E) : L â‰ƒ* L.map (e : G â†’* H) :=
{ map_mul' := Î» _ _, subtype.ext (map_mul e _ _), ..(e : G â‰ƒ H).image L }

@[simp, to_additive]
lemma coe_equiv_map_apply (L : subgroup G) (e : E) (g : L) :
  ((L.equiv_map e g : L.map (e : G â†’* H)) : H) = e g := rfl

@[simp, to_additive]
lemma equiv_map_symm_apply (L : subgroup G) (e : G â‰ƒ* H) (g : L.map (e : G â†’* H)) :
  (L.equiv_map e).symm g = âŸ¨e.symm g, set_like.mem_coe.1 $ set.mem_image_equiv.1 g.2âŸ© := rfl

@[simp, to_additive]
lemma equiv_map_of_injective_equiv (L : subgroup G) (e : E) :
  L.equiv_map_of_injective (e : G â†’* H) (by exact equiv_like.injective e) = L.equiv_map e :=
by { ext, refl }

@[to_additive] instance (L : subgroup G) [countable L] (e : E) : countable (L.map (e : G â†’* H)) :=
(L.equiv_map e).symm.injective.countable

end subgroup
end

section
variables {ğ•œ Î± Î² : Type*} [semiring ğ•œ] [add_comm_group Î±] [add_comm_group Î²] [module ğ•œ Î±]
  [module ğ•œ Î²]

@[simp]
lemma add_subgroup.linear_equiv_map_symm_apply (e : Î± â‰ƒâ‚—[ğ•œ] Î²) {L : add_subgroup Î±}
  {g : L.map (e : Î± â†’+ Î²)} :
  (L.equiv_map e).symm g = âŸ¨e.symm g, set_like.mem_coe.1 $ (@set.mem_image_equiv Î± Î² _ e _).1 g.2âŸ© :=
L.equiv_map_symm_apply (e : Î± â‰ƒ+ Î²) _

end

namespace linear_equiv
variables {ğ•œ Î± Î² : Type*} [semiring ğ•œ] [add_comm_monoid Î±] [add_comm_monoid Î²] [module ğ•œ Î±]
  [module ğ•œ Î²]

@[simp] lemma symm_comp_self (e : Î± â‰ƒâ‚—[ğ•œ] Î²) : e.symm âˆ˜ e = id := e.to_equiv.symm_comp_self
@[simp] lemma self_comp_symm (e : Î± â‰ƒâ‚—[ğ•œ] Î²) : e âˆ˜ e.symm = id := e.to_equiv.self_comp_symm

end linear_equiv

namespace measure_theory
open finite_dimensional fintype function measure set topological_space
open_locale pointwise

namespace measure
variables {E : Type*} [normed_add_comm_group E] [measurable_space E] [normed_space â„ E]
  [finite_dimensional â„ E] [borel_space E] (Î¼ : measure_theory.measure E) [is_add_haar_measure Î¼]

lemma add_haar_smul_of_nonneg {r : â„} (hr : 0 â‰¤ r) (s : set E) :
  Î¼ (r â€¢ s) = ennreal.of_real (r ^ finrank â„ E) * Î¼ s :=
by rw [add_haar_smul, abs_pow, abs_of_nonneg hr]

end measure

section
variables {Î± Î² : Type*} [measurable_space Î±] [measurable_space Î²]

lemma quasi_measure_preserving_map (Î¼ : measure Î±) (e : Î± â‰ƒáµ Î²) :
  quasi_measure_preserving e.symm (map e Î¼) Î¼ :=
{ measurable := e.symm.measurable,
  absolutely_continuous := by rw [map_map, e.symm_comp_self, map_id]; measurability }

end

section
variables {ğ•œ G H : Type*} [nontrivially_normed_field ğ•œ] [complete_space ğ•œ] [measurable_space G]
  [topological_space G] [add_comm_group G] [module ğ•œ G] [finite_dimensional ğ•œ G]
  [has_continuous_smul ğ•œ G] (Î¼ : measure G) [is_add_haar_measure Î¼] [borel_space G] [t2_space G]
  [topological_add_group G] [topological_space H] [add_comm_group H] [module ğ•œ H]
  [finite_dimensional ğ•œ H] [has_continuous_smul ğ•œ H] [measurable_space H] [borel_space H]
  [t2_space H] [topological_add_group H]

instance (e : G â‰ƒâ‚—[ğ•œ] H) : is_add_haar_measure (Î¼.map e) :=
e.to_add_equiv.is_add_haar_measure_map _ (e : G â†’â‚—[ğ•œ] H).continuous_of_finite_dimensional
  (e.symm : H â†’â‚—[ğ•œ] G).continuous_of_finite_dimensional

end

lemma rescale (Î¹ : Type*) [fintype Î¹] {r : â„} (hr : 0 < r) :
  comap ((â€¢) r) (volume : measure (Î¹ â†’ â„)) = ennreal.of_real r ^ card Î¹ â€¢ volume :=
begin
  suffices : (ennreal.of_real r ^ card Î¹)â»Â¹ â€¢ comap ((â€¢) r) (volume : measure (Î¹ â†’ â„)) = volume,
  { conv_rhs { rw â†this },
    rw [ennreal.inv_pow, smul_smul, â†mul_pow, ennreal.mul_inv_cancel (ennreal.of_real_pos.2 hr).ne'
      ennreal.of_real_ne_top, one_pow, one_smul] },
  refine (pi_eq $ Î» s hS, _).symm,
  simp only [smul_eq_mul, measure.coe_smul, pi.smul_apply],
  rw [comap_apply _ (smul_right_injective (Î¹ â†’ â„) hr.ne') (Î» S hS, hS.const_smulâ‚€ r) _
    (measurable_set.univ_pi hS), image_smul, smul_univ_pi, volume_pi_pi],
  simp only [add_haar_smul, finite_dimensional.finrank_self, pow_one, abs_of_pos hr, pi.smul_apply,
    finset.prod_mul_distrib, finset.card_univ, â†mul_assoc, finset.prod_const],
  rw [ennreal.inv_mul_cancel _ (ennreal.pow_ne_top ennreal.of_real_ne_top), one_mul],
  positivity,
end

namespace is_fundamental_domain
variables {G H Î± Î² E : Type*} [group G] [group H]
  [mul_action G Î±] [measurable_space Î±]
  [mul_action H Î²] [measurable_space Î²]
  [normed_add_comm_group E] {s t : set Î±} {Î¼ : measure Î±} {Î½ : measure Î²}

@[to_additive measure_theory.is_add_fundamental_domain.preimage_of_equiv']
lemma preimage_of_equiv' [measurable_space H]
  [has_measurable_smul H Î²] [smul_invariant_measure H Î² Î½]
  {s : set Î²} (h : is_fundamental_domain H s Î½) {f : Î± â†’ Î²}
  (hf : quasi_measure_preserving f Î¼ Î½) {e : H â†’ G} (he : bijective e)
  (hef : âˆ€ g, semiconj f ((â€¢) (e g)) ((â€¢) g)) :
  is_fundamental_domain G (f â»Â¹' s) Î¼ :=
{ null_measurable_set := h.null_measurable_set.preimage hf,
  ae_covers := (hf.ae h.ae_covers).mono $ Î» x âŸ¨g, hgâŸ©, âŸ¨e g, by rwa [mem_preimage, hef g x]âŸ©,
  ae_disjoint := Î» g hg,
    begin
      lift e to H â‰ƒ G using he,
      have : (e.symm gâ»Â¹)â»Â¹ â‰  (e.symm 1)â»Â¹, by simp [hg],
      convert (h.pairwise_ae_disjoint this).preimage hf using 1,
      { simp only [â†preimage_smul_inv, preimage_preimage, â†hef _ _, e.apply_symm_apply, inv_inv] },
      { ext1 x,
        simp only [mem_preimage, â†preimage_smul, â†hef _ _, e.apply_symm_apply, one_smul] }
    end }

@[to_additive measure_theory.is_add_fundamental_domain.image_of_equiv']
lemma image_of_equiv' [measurable_space G] [has_measurable_smul G Î±] [smul_invariant_measure G Î± Î¼]
  (h : is_fundamental_domain G s Î¼)
  (f : Î± â‰ƒ Î²) (hf : quasi_measure_preserving f.symm Î½ Î¼)
  (e : H â‰ƒ G) (hef : âˆ€ g, semiconj f ((â€¢) (e g)) ((â€¢) g)) :
  is_fundamental_domain H (f '' s) Î½ :=
begin
  rw f.image_eq_preimage,
  refine h.preimage_of_equiv' hf e.symm.bijective (Î» g x, _),
  rcases f.surjective x with âŸ¨x, rflâŸ©,
  rw [â†hef, f.symm_apply_apply, f.symm_apply_apply, e.apply_symm_apply]
end

end is_fundamental_domain

namespace is_fundamental_domain
variables {G Î± : Type*} [group G] [mul_action G Î±] [measurable_space G] [measurable_space Î±]
  [has_measurable_mul G] {L : subgroup G}

/- TODO: Prove the version giving `âŒˆvolume S / volume FâŒ‰` points whose difference is in a subgroup.
This needs the `m`-fold version of `exists_nonempty_inter_of_measure_univ_lt_tsum_measure` when
`m * measure < measure`, giving some element in `m` sets. -/
@[to_additive]
lemma exists_ne_div_mem {Î¼ : measure G} [countable L] {s t : set G} (hs : null_measurable_set s Î¼)
 (fund : is_fundamental_domain L t Î¼) (hlt : Î¼ t < Î¼ s)
  [is_mul_left_invariant (Î¼ : measure G)] :
  âˆƒ x y âˆˆ s, x â‰  y âˆ§ y / x âˆˆ L :=
let âŸ¨x, hx, y, hy, g, hg, rflâŸ© := fund.exists_ne_one_smul_eq hs hlt in
  by refine âŸ¨x, hx, _, hy, _, _âŸ©; simp [subgroup.smul_def]; assumption

end is_fundamental_domain

namespace is_add_fundamental_domain
variables {E G : Type*} [normed_add_comm_group E] [normed_add_comm_group G] [normed_space â„ E]
  [normed_space â„ G] [measurable_space E] [measurable_space G] [borel_space E] [borel_space G]
  [finite_dimensional â„ E] {L : add_subgroup E} {F : set E}

lemma map_linear_equiv (Î¼ : measure E) [is_add_haar_measure Î¼]
  (fund : is_add_fundamental_domain L F Î¼) (e : E â‰ƒâ‚—[â„] G) :
  is_add_fundamental_domain (L.map (e : E â†’+ G)) (e '' F) (map e Î¼) :=
begin
  refine fund.image_of_equiv'  e.to_equiv _ (L.equiv_map e).symm.to_equiv (Î» g x, _),
  { convert quasi_measure_preserving_map _
      e.to_continuous_linear_equiv.to_homeomorph.to_measurable_equiv; ext; refl },
  { simp [â†add_equiv.coe_to_equiv_symm, _root_.map_add, add_subgroup.vadd_def, vadd_eq_add] }
end

end is_add_fundamental_domain
end measure_theory

open ennreal finite_dimensional fintype measure_theory measure_theory.measure set topological_space
  topological_space.positive_compacts
open_locale pointwise

namespace measure_theory
variables {Î¹ E : Type*} [fintype Î¹]

-- TODO: The proof shows that there is a point in the interior of T, perhaps we should expose this
private lemma exists_ne_zero_mem_subgroup_of_volume_mul_two_pow_card_lt_measure
  {L : add_subgroup (Î¹ â†’ â„)} [countable L] {F T : set (Î¹ â†’ â„)} (Î¼ : measure (Î¹ â†’ â„))
  [is_add_haar_measure Î¼] (fund : is_add_fundamental_domain L F Î¼) (h : Î¼ F * 2 ^ card Î¹ < Î¼ T)
  (h_symm : âˆ€ x âˆˆ T, -x âˆˆ T) (h_conv : convex â„ T) :
  âˆƒ x : L, x â‰  0 âˆ§ (x : Î¹ â†’ â„) âˆˆ T :=
begin
  rw [add_haar_measure_unique Î¼ (pi_Icc01 Î¹), add_haar_measure_eq_volume_pi] at fund,
  have fund_vol : is_add_fundamental_domain L F volume,
  { refine fund.mono (absolutely_continuous.mk $ Î» s hs h, _),
    rw [measure.smul_apply, smul_eq_zero] at h,
    -- TODO nice lemma for this?
    exact h.resolve_left (measure_pos_of_nonempty_interior _ (pi_Icc01 _).interior_nonempty).ne' },
  rw [add_haar_measure_unique Î¼ (pi_Icc01 Î¹), add_haar_measure_eq_volume_pi, measure.smul_apply,
    measure.smul_apply, smul_mul_assoc, smul_eq_mul, smul_eq_mul] at h,
  rw â†measure_interior_of_null_frontier (h_conv.add_haar_frontier volume) at *,
  set S := interior T,
  have h2 : volume F < volume ((2â»Â¹ : â„) â€¢ S),
  { rw [â†ennreal.mul_lt_mul_right (pow_ne_zero (card Î¹) $ two_ne_zero' _) (pow_ne_top two_ne_top),
      add_haar_smul_of_nonneg],
    simpa [ennreal.of_real_pow, â†inv_pow, â†ennreal.of_real_inv_of_pos zero_lt_two, mul_right_comm,
      â†mul_pow, ennreal.inv_mul_cancel _root_.two_ne_zero] using lt_of_mul_lt_mul_left' h,
    positivity },
  rw [â†one_smul â„ T, â†_root_.add_halves (1 : â„), one_div, h_conv.add_smul (inv_nonneg.2 zero_le_two)
    (inv_nonneg.2 zero_le_two)],
  obtain âŸ¨x, hx, y, hy, hne, hsubâŸ© := fund_vol.exists_ne_sub_mem
    (measurable_set_interior.const_smulâ‚€ _).null_measurable_set h2,
  refine âŸ¨âŸ¨y - x, hsubâŸ©, subtype.ne_of_val_ne $ sub_ne_zero.2 hne.symm, y, -x,
    smul_set_mono interior_subset hy, _, rflâŸ©,
  rw mem_inv_smul_set_iffâ‚€ (two_ne_zero' â„) at âŠ¢ hx,
  rw smul_neg,
  exact h_symm _ (interior_subset hx),
end

lemma exists_ne_zero_mem_lattice_of_measure_mul_two_pow_finrank_lt_measure
  [normed_add_comm_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [finite_dimensional â„ E] (Î¼ : measure E) [is_add_haar_measure Î¼] {L : add_subgroup E}
  [countable L] {F T : set E} (fund : is_add_fundamental_domain L F Î¼)
  (h : Î¼ F * 2 ^ finrank â„ E < Î¼ T) (h_symm : âˆ€ x âˆˆ T, -x âˆˆ T) (h_conv : convex â„ T) :
  âˆƒ x â‰  0, ((x : L) : E) âˆˆ T :=
begin
  let Î¹ := fin (finrank â„ E),
  have : finrank â„ E = finrank â„ (Î¹ â†’ â„), by simp,
  have e : E â‰ƒâ‚—[â„] Î¹ â†’ â„ := linear_equiv.of_finrank_eq E (Î¹ â†’ â„) this,
  obtain âŸ¨x, hx, hxTâŸ© := exists_ne_zero_mem_subgroup_of_volume_mul_two_pow_card_lt_measure (map e Î¼)
      (fund.map_linear_equiv Î¼ e) (_ : map e Î¼ (e '' F) * _ < map e Î¼ (e '' T)) _
      (h_conv.linear_image e.to_linear_map),
  { refine âŸ¨(L.equiv_map e).symm x, (add_equiv_class.map_ne_zero_iff _).2 hx, _âŸ©,
    simp only [add_subgroup.linear_equiv_map_symm_apply, add_subgroup.coe_mk],
    exact (@set.mem_image_equiv E (Î¹ â†’ â„) _ e _).1 hxT },
  { erw [e.to_continuous_linear_equiv.to_homeomorph.to_measurable_equiv.map_apply,
      e.to_continuous_linear_equiv.to_homeomorph.to_measurable_equiv.map_apply,
      preimage_image_eq _ e.injective, preimage_image_eq _ e.injective, card_fin],
    exact h },
  { rintro _ âŸ¨x, hx, rflâŸ©,
    exact âŸ¨-x, h_symm _ hx, map_neg _ _âŸ© }
end

end measure_theory
