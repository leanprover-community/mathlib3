/-
Copyright (c) 2020 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import dynamics.ergodic.measure_preserving
import measure_theory.measure.regular
import measure_theory.group.measurable_equiv
import measure_theory.measure.open_pos
import measure_theory.group.action
import measure_theory.constructions.prod
import topology.continuous_function.cocompact_map

/-!
# Measures on Groups

We develop some properties of measures on (topological) groups

* We define properties on measures: measures that are left or right invariant w.r.t. multiplication.
* We define the measure `Œº.inv : A ‚Ü¶ Œº(A‚Åª¬π)` and show that it is right invariant iff
  `Œº` is left invariant.
* We define a class `is_haar_measure Œº`, requiring that the measure `Œº` is left-invariant, finite
  on compact sets, and positive on open sets.

We also give analogues of all these notions in the additive world.
-/

noncomputable theory

open_locale nnreal ennreal pointwise big_operators topology
open has_inv set function measure_theory.measure filter

variables {ùïú G H : Type*} [measurable_space G] [measurable_space H]

namespace measure_theory
namespace measure

/-- A measure `Œº` on a measurable additive group is left invariant
  if the measure of left translations of a set are equal to the measure of the set itself. -/
class is_add_left_invariant [has_add G] (Œº : measure G) : Prop :=
(map_add_left_eq_self : ‚àÄ g : G, map ((+) g) Œº = Œº)

/-- A measure `Œº` on a measurable group is left invariant
  if the measure of left translations of a set are equal to the measure of the set itself. -/
@[to_additive] class is_mul_left_invariant [has_mul G] (Œº : measure G) : Prop :=
(map_mul_left_eq_self : ‚àÄ g : G, map ((*) g) Œº = Œº)

/-- A measure `Œº` on a measurable additive group is right invariant
  if the measure of right translations of a set are equal to the measure of the set itself. -/
class is_add_right_invariant [has_add G] (Œº : measure G) : Prop :=
(map_add_right_eq_self : ‚àÄ g : G, map (+ g) Œº = Œº)

/-- A measure `Œº` on a measurable group is right invariant
  if the measure of right translations of a set are equal to the measure of the set itself. -/
@[to_additive] class is_mul_right_invariant [has_mul G] (Œº : measure G) : Prop :=
(map_mul_right_eq_self : ‚àÄ g : G, map (* g) Œº = Œº)

end measure
open measure

section mul

variables [has_mul G] {Œº : measure G}

@[to_additive]
lemma map_mul_left_eq_self (Œº : measure G) [is_mul_left_invariant Œº] (g : G) : map ((*) g) Œº = Œº :=
is_mul_left_invariant.map_mul_left_eq_self g

@[to_additive]
lemma map_mul_right_eq_self (Œº : measure G) [is_mul_right_invariant Œº] (g : G) : map (* g) Œº = Œº :=
is_mul_right_invariant.map_mul_right_eq_self g

@[to_additive measure_theory.is_add_left_invariant_smul]
instance is_mul_left_invariant_smul [is_mul_left_invariant Œº] (c : ‚Ñù‚â•0‚àû) :
  is_mul_left_invariant (c ‚Ä¢ Œº) :=
‚ü®Œª g, by rw [measure.map_smul, map_mul_left_eq_self]‚ü©

@[to_additive measure_theory.is_add_right_invariant_smul]
instance is_mul_right_invariant_smul [is_mul_right_invariant Œº] (c : ‚Ñù‚â•0‚àû) :
  is_mul_right_invariant (c ‚Ä¢ Œº) :=
‚ü®Œª g, by rw [measure.map_smul, map_mul_right_eq_self]‚ü©

@[to_additive measure_theory.is_add_left_invariant_smul_nnreal]
instance is_mul_left_invariant_smul_nnreal [is_mul_left_invariant Œº] (c : ‚Ñù‚â•0) :
  is_mul_left_invariant (c ‚Ä¢ Œº) :=
measure_theory.is_mul_left_invariant_smul (c : ‚Ñù‚â•0‚àû)

@[to_additive measure_theory.is_add_right_invariant_smul_nnreal]
instance is_mul_right_invariant_smul_nnreal [is_mul_right_invariant Œº] (c : ‚Ñù‚â•0) :
  is_mul_right_invariant (c ‚Ä¢ Œº) :=
measure_theory.is_mul_right_invariant_smul (c : ‚Ñù‚â•0‚àû)

section has_measurable_mul

variables [has_measurable_mul G]

@[to_additive]
lemma measure_preserving_mul_left (Œº : measure G) [is_mul_left_invariant Œº] (g : G) :
  measure_preserving ((*) g) Œº Œº :=
‚ü®measurable_const_mul g, map_mul_left_eq_self Œº g‚ü©

@[to_additive]
lemma measure_preserving.mul_left (Œº : measure G) [is_mul_left_invariant Œº] (g : G)
  {X : Type*} [measurable_space X] {Œº' : measure X} {f : X ‚Üí G} (hf : measure_preserving f Œº' Œº) :
  measure_preserving (Œª x, g * f x) Œº' Œº :=
(measure_preserving_mul_left Œº g).comp hf

@[to_additive]
lemma measure_preserving_mul_right (Œº : measure G) [is_mul_right_invariant Œº] (g : G) :
  measure_preserving (* g) Œº Œº :=
‚ü®measurable_mul_const g, map_mul_right_eq_self Œº g‚ü©

@[to_additive]
lemma measure_preserving.mul_right (Œº : measure G) [is_mul_right_invariant Œº] (g : G)
  {X : Type*} [measurable_space X] {Œº' : measure X} {f : X ‚Üí G} (hf : measure_preserving f Œº' Œº) :
  measure_preserving (Œª x, f x * g) Œº' Œº :=
(measure_preserving_mul_right Œº g).comp hf

@[to_additive]
instance is_mul_left_invariant.smul_invariant_measure [is_mul_left_invariant Œº] :
  smul_invariant_measure G G Œº :=
‚ü®Œª x s hs, (measure_preserving_mul_left Œº x).measure_preimage hs‚ü©

@[to_additive]
instance is_mul_right_invariant.to_smul_invariant_measure_op [Œº.is_mul_right_invariant] :
  smul_invariant_measure G·µê·µí·µñ G Œº :=
‚ü®Œª x s hs, (measure_preserving_mul_right Œº (mul_opposite.unop x)).measure_preimage hs‚ü©

@[to_additive]
instance subgroup.smul_invariant_measure
  {G Œ± : Type*} [group G] [mul_action G Œ±] [measurable_space Œ±]
  {Œº : measure Œ±} [smul_invariant_measure G Œ± Œº] (H : subgroup G) :
  smul_invariant_measure H Œ± Œº :=
‚ü®Œª y s hs, by convert smul_invariant_measure.measure_preimage_smul Œº (y : G) hs‚ü©

/-- An alternative way to prove that `Œº` is left invariant under multiplication. -/
@[to_additive /-" An alternative way to prove that `Œº` is left invariant under addition. "-/]
lemma forall_measure_preimage_mul_iff (Œº : measure G) :
  (‚àÄ (g : G) (A : set G), measurable_set A ‚Üí Œº ((Œª h, g * h) ‚Åª¬π' A) = Œº A) ‚Üî
  is_mul_left_invariant Œº :=
begin
  transitivity ‚àÄ g, map ((*) g) Œº = Œº,
  { simp_rw [measure.ext_iff],
    refine forall_congr (Œª g, forall_congr $ Œª A, forall_congr $ Œª hA, _),
    rw [map_apply (measurable_const_mul g) hA] },
  exact ‚ü®Œª h, ‚ü®h‚ü©, Œª h, h.1‚ü©
end

/-- An alternative way to prove that `Œº` is right invariant under multiplication. -/
@[to_additive /-" An alternative way to prove that `Œº` is right invariant under addition. "-/]
lemma forall_measure_preimage_mul_right_iff (Œº : measure G) :
  (‚àÄ (g : G) (A : set G), measurable_set A ‚Üí Œº ((Œª h, h * g) ‚Åª¬π' A) = Œº A) ‚Üî
  is_mul_right_invariant Œº :=
begin
  transitivity ‚àÄ g, map (* g) Œº = Œº,
  { simp_rw [measure.ext_iff],
    refine forall_congr (Œª g, forall_congr $ Œª A, forall_congr $ Œª hA, _),
    rw [map_apply (measurable_mul_const g) hA] },
  exact ‚ü®Œª h, ‚ü®h‚ü©, Œª h, h.1‚ü©
end

@[to_additive]
instance [is_mul_left_invariant Œº] [sigma_finite Œº] {H : Type*} [has_mul H]
  {mH : measurable_space H} {ŒΩ : measure H} [has_measurable_mul H]
  [is_mul_left_invariant ŒΩ] [sigma_finite ŒΩ] :
  is_mul_left_invariant (Œº.prod ŒΩ) :=
begin
  constructor,
  rintros ‚ü®g, h‚ü©,
  change map (prod.map ((*) g) ((*) h)) (Œº.prod ŒΩ) = Œº.prod ŒΩ,
  rw [‚Üê map_prod_map _ _ (measurable_const_mul g) (measurable_const_mul h),
    map_mul_left_eq_self Œº g, map_mul_left_eq_self ŒΩ h],
  { rw map_mul_left_eq_self Œº g, apply_instance },
  { rw map_mul_left_eq_self ŒΩ h, apply_instance },
end

@[to_additive]
instance [is_mul_right_invariant Œº] [sigma_finite Œº] {H : Type*} [has_mul H]
  {mH : measurable_space H} {ŒΩ : measure H} [has_measurable_mul H]
  [is_mul_right_invariant ŒΩ] [sigma_finite ŒΩ] :
  is_mul_right_invariant (Œº.prod ŒΩ) :=
begin
  constructor,
  rintros ‚ü®g, h‚ü©,
  change map (prod.map (* g) (* h)) (Œº.prod ŒΩ) = Œº.prod ŒΩ,
  rw [‚Üê map_prod_map _ _ (measurable_mul_const g) (measurable_mul_const h),
    map_mul_right_eq_self Œº g, map_mul_right_eq_self ŒΩ h],
  { rw map_mul_right_eq_self Œº g, apply_instance },
  { rw map_mul_right_eq_self ŒΩ h, apply_instance },
end

@[to_additive]
lemma is_mul_left_invariant_map {H : Type*}
  [measurable_space H] [has_mul H] [has_measurable_mul H]
  [is_mul_left_invariant Œº]
  (f : G ‚Üí‚Çô* H) (hf : measurable f) (h_surj : surjective f) :
  is_mul_left_invariant (measure.map f Œº) :=
begin
  refine ‚ü®Œª h, _‚ü©,
  rw map_map (measurable_const_mul _) hf,
  obtain ‚ü®g, rfl‚ü© := h_surj h,
  conv_rhs { rw ‚Üê map_mul_left_eq_self Œº g },
  rw map_map hf (measurable_const_mul _),
  congr' 2,
  ext y,
  simp only [comp_app, map_mul],
end

end has_measurable_mul

end mul

section div_inv_monoid
variables [div_inv_monoid G]

@[to_additive]
lemma map_div_right_eq_self (Œº : measure G) [is_mul_right_invariant Œº] (g : G) :
  map (/ g) Œº = Œº :=
by simp_rw [div_eq_mul_inv, map_mul_right_eq_self Œº g‚Åª¬π]

end div_inv_monoid

section group
variables [group G] [has_measurable_mul G]

@[to_additive]
lemma measure_preserving_div_right (Œº : measure G) [is_mul_right_invariant Œº]
  (g : G) : measure_preserving (/ g) Œº Œº :=
by simp_rw [div_eq_mul_inv, measure_preserving_mul_right Œº g‚Åª¬π]

/-- We shorten this from `measure_preimage_mul_left`, since left invariant is the preferred option
  for measures in this formalization. -/
@[simp, to_additive "We shorten this from `measure_preimage_add_left`, since left invariant is the
preferred option for measures in this formalization."]
lemma measure_preimage_mul (Œº : measure G) [is_mul_left_invariant Œº] (g : G) (A : set G) :
  Œº ((Œª h, g * h) ‚Åª¬π' A) = Œº A :=
calc Œº ((Œª h, g * h) ‚Åª¬π' A) = map (Œª h, g * h) Œº A :
  ((measurable_equiv.mul_left g).map_apply A).symm
... = Œº A : by rw map_mul_left_eq_self Œº g

@[simp, to_additive]
lemma measure_preimage_mul_right (Œº : measure G) [is_mul_right_invariant Œº] (g : G) (A : set G) :
  Œº ((Œª h, h * g) ‚Åª¬π' A) = Œº A :=
calc Œº ((Œª h, h * g) ‚Åª¬π' A) = map (Œª h, h * g) Œº A :
  ((measurable_equiv.mul_right g).map_apply A).symm
... = Œº A : by rw map_mul_right_eq_self Œº g

@[to_additive]
lemma map_mul_left_ae (Œº : measure G) [is_mul_left_invariant Œº] (x : G) :
  filter.map (Œª h, x * h) Œº.ae = Œº.ae :=
((measurable_equiv.mul_left x).map_ae Œº).trans $ congr_arg ae $ map_mul_left_eq_self Œº x

@[to_additive]
lemma map_mul_right_ae (Œº : measure G) [is_mul_right_invariant Œº] (x : G) :
  filter.map (Œª h, h * x) Œº.ae = Œº.ae :=
((measurable_equiv.mul_right x).map_ae Œº).trans $ congr_arg ae $ map_mul_right_eq_self Œº x

@[to_additive]
lemma map_div_right_ae (Œº : measure G) [is_mul_right_invariant Œº] (x : G) :
  filter.map (Œª t, t / x) Œº.ae = Œº.ae :=
((measurable_equiv.div_right x).map_ae Œº).trans $ congr_arg ae $ map_div_right_eq_self Œº x

@[to_additive]
lemma eventually_mul_left_iff (Œº : measure G) [is_mul_left_invariant Œº] (t : G) {p : G ‚Üí Prop} :
  (‚àÄ·µê x ‚àÇŒº, p (t * x)) ‚Üî ‚àÄ·µê x ‚àÇŒº, p x :=
by { conv_rhs { rw [filter.eventually, ‚Üê map_mul_left_ae Œº t] }, refl }

@[to_additive]
lemma eventually_mul_right_iff (Œº : measure G) [is_mul_right_invariant Œº] (t : G) {p : G ‚Üí Prop} :
  (‚àÄ·µê x ‚àÇŒº, p (x * t)) ‚Üî ‚àÄ·µê x ‚àÇŒº, p x :=
by { conv_rhs { rw [filter.eventually, ‚Üê map_mul_right_ae Œº t] }, refl }

@[to_additive]
lemma eventually_div_right_iff (Œº : measure G) [is_mul_right_invariant Œº] (t : G) {p : G ‚Üí Prop} :
  (‚àÄ·µê x ‚àÇŒº, p (x / t)) ‚Üî ‚àÄ·µê x ‚àÇŒº, p x :=
by { conv_rhs { rw [filter.eventually, ‚Üê map_div_right_ae Œº t] }, refl }

end group

namespace measure

/-- The measure `A ‚Ü¶ Œº (A‚Åª¬π)`, where `A‚Åª¬π` is the pointwise inverse of `A`. -/
@[to_additive "The measure `A ‚Ü¶ Œº (- A)`, where `- A` is the pointwise negation of `A`."]
protected def inv [has_inv G] (Œº : measure G) : measure G :=
measure.map inv Œº

/-- A measure is invariant under negation if `- Œº = Œº`. Equivalently, this means that for all
measurable `A` we have `Œº (- A) = Œº A`, where `- A` is the pointwise negation of `A`. -/
class is_neg_invariant [has_neg G] (Œº : measure G) : Prop :=
(neg_eq_self : Œº.neg = Œº)

/-- A measure is invariant under inversion if `Œº‚Åª¬π = Œº`. Equivalently, this means that for all
measurable `A` we have `Œº (A‚Åª¬π) = Œº A`, where `A‚Åª¬π` is the pointwise inverse of `A`. -/
@[to_additive] class is_inv_invariant [has_inv G] (Œº : measure G) : Prop :=
(inv_eq_self : Œº.inv = Œº)

section inv

variables [has_inv G]

@[simp, to_additive]
lemma inv_eq_self (Œº : measure G) [is_inv_invariant Œº] : Œº.inv = Œº :=
is_inv_invariant.inv_eq_self

@[simp, to_additive]
lemma map_inv_eq_self (Œº : measure G) [is_inv_invariant Œº] : map has_inv.inv Œº = Œº :=
is_inv_invariant.inv_eq_self

variables [has_measurable_inv G]

@[to_additive]
lemma measure_preserving_inv (Œº : measure G) [is_inv_invariant Œº] :
  measure_preserving has_inv.inv Œº Œº :=
‚ü®measurable_inv, map_inv_eq_self Œº‚ü©

end inv

section has_involutive_inv

variables [has_involutive_inv G] [has_measurable_inv G]

@[simp, to_additive]
lemma inv_apply (Œº : measure G) (s : set G) : Œº.inv s = Œº s‚Åª¬π :=
(measurable_equiv.inv G).map_apply s

@[simp, to_additive]
protected lemma inv_inv (Œº : measure G) : Œº.inv.inv = Œº :=
(measurable_equiv.inv G).map_symm_map

@[simp, to_additive]
lemma measure_inv (Œº : measure G) [is_inv_invariant Œº] (A : set G) : Œº A‚Åª¬π = Œº A :=
by rw [‚Üê inv_apply, inv_eq_self]

@[to_additive]
lemma measure_preimage_inv (Œº : measure G) [is_inv_invariant Œº] (A : set G) :
  Œº (has_inv.inv ‚Åª¬π' A) = Œº A :=
Œº.measure_inv A

@[to_additive]
instance (Œº : measure G) [sigma_finite Œº] : sigma_finite Œº.inv :=
(measurable_equiv.inv G).sigma_finite_map ‚Äπ_‚Ä∫

end has_involutive_inv

section division_monoid
variables [division_monoid G] [has_measurable_mul G] [has_measurable_inv G] {Œº : measure G}

@[to_additive]
instance [is_mul_left_invariant Œº] : is_mul_right_invariant Œº.inv :=
begin
  constructor,
  intro g,
  conv_rhs { rw [‚Üê map_mul_left_eq_self Œº g‚Åª¬π] },
  simp_rw [measure.inv, map_map (measurable_mul_const g) measurable_inv,
    map_map measurable_inv (measurable_const_mul g‚Åª¬π), function.comp, mul_inv_rev, inv_inv]
end

@[to_additive]
instance [is_mul_right_invariant Œº] : is_mul_left_invariant Œº.inv :=
begin
  constructor,
  intro g,
  conv_rhs { rw [‚Üê map_mul_right_eq_self Œº g‚Åª¬π] },
  simp_rw [measure.inv, map_map (measurable_const_mul g) measurable_inv,
    map_map measurable_inv (measurable_mul_const g‚Åª¬π), function.comp, mul_inv_rev, inv_inv]
end

@[to_additive]
lemma measure_preserving_div_left (Œº : measure G) [is_inv_invariant Œº] [is_mul_left_invariant Œº]
  (g : G) : measure_preserving (Œª t, g / t) Œº Œº :=
begin
  simp_rw [div_eq_mul_inv],
  exact (measure_preserving_mul_left Œº g).comp (measure_preserving_inv Œº)
end

@[to_additive]
lemma map_div_left_eq_self (Œº : measure G) [is_inv_invariant Œº] [is_mul_left_invariant Œº] (g : G) :
  map (Œª t, g / t) Œº = Œº :=
(measure_preserving_div_left Œº g).map_eq

@[to_additive]
lemma measure_preserving_mul_right_inv (Œº : measure G)
  [is_inv_invariant Œº] [is_mul_left_invariant Œº] (g : G) :
  measure_preserving (Œª t, (g * t)‚Åª¬π) Œº Œº :=
(measure_preserving_inv Œº).comp $ measure_preserving_mul_left Œº g

@[to_additive]
lemma map_mul_right_inv_eq_self (Œº : measure G) [is_inv_invariant Œº] [is_mul_left_invariant Œº]
  (g : G) : map (Œª t, (g * t)‚Åª¬π) Œº = Œº :=
(measure_preserving_mul_right_inv Œº g).map_eq

end division_monoid

section group
variables [group G] [has_measurable_mul G] [has_measurable_inv G] {Œº : measure G}

@[to_additive]
lemma map_div_left_ae (Œº : measure G) [is_mul_left_invariant Œº] [is_inv_invariant Œº] (x : G) :
  filter.map (Œª t, x / t) Œº.ae = Œº.ae :=
((measurable_equiv.div_left x).map_ae Œº).trans $ congr_arg ae $ map_div_left_eq_self Œº x

end group

end measure

section topological_group

variables [topological_space G] [borel_space G] {Œº : measure G} [group G]

@[to_additive]
instance measure.regular.inv [has_continuous_inv G] [t2_space G] [regular Œº] : regular Œº.inv :=
regular.map (homeomorph.inv G)

variables [topological_group G]

@[to_additive]
lemma regular_inv_iff [t2_space G] : Œº.inv.regular ‚Üî Œº.regular :=
begin
  split,
  { introI h, rw ‚Üê Œº.inv_inv, exact measure.regular.inv },
  { introI h, exact measure.regular.inv }
end

variables [is_mul_left_invariant Œº]

/-- If a left-invariant measure gives positive mass to a compact set, then it gives positive mass to
any open set. -/
@[to_additive "If a left-invariant measure gives positive mass to a compact set, then it gives
positive mass to any open set."]
lemma is_open_pos_measure_of_mul_left_invariant_of_compact
  (K : set G) (hK : is_compact K) (h : Œº K ‚â† 0) :
  is_open_pos_measure Œº :=
begin
  refine ‚ü®Œª U hU hne, _‚ü©,
  contrapose! h,
  rw ‚Üê nonpos_iff_eq_zero,
  rw ‚Üê hU.interior_eq at hne,
  obtain ‚ü®t, hKt‚ü© : ‚àÉ (t : finset G), K ‚äÜ ‚ãÉ (g : G) (H : g ‚àà t), (Œª (h : G), g * h) ‚Åª¬π' U :=
    compact_covered_by_mul_left_translates hK hne,
  calc Œº K ‚â§ Œº (‚ãÉ (g : G) (H : g ‚àà t), (Œª (h : G), g * h) ‚Åª¬π' U) : measure_mono hKt
  ... ‚â§ ‚àë g in t, Œº ((Œª (h : G), g * h) ‚Åª¬π' U) : measure_bUnion_finset_le _ _
  ... = 0 : by simp [measure_preimage_mul, h]
end

/-- A nonzero left-invariant regular measure gives positive mass to any open set. -/
@[to_additive "A nonzero left-invariant regular measure gives positive mass to any open set."]
lemma is_open_pos_measure_of_mul_left_invariant_of_regular [regular Œº] (h‚ÇÄ : Œº ‚â† 0) :
  is_open_pos_measure Œº :=
let ‚ü®K, hK, h2K‚ü© := regular.exists_compact_not_null.mpr h‚ÇÄ
in is_open_pos_measure_of_mul_left_invariant_of_compact K hK h2K

@[to_additive]
lemma null_iff_of_is_mul_left_invariant [regular Œº]
  {s : set G} (hs : is_open s) :
  Œº s = 0 ‚Üî s = ‚àÖ ‚à® Œº = 0 :=
begin
  by_cases h3Œº : Œº = 0, { simp [h3Œº] },
  { haveI := is_open_pos_measure_of_mul_left_invariant_of_regular h3Œº,
    simp only [h3Œº, or_false, hs.measure_eq_zero_iff Œº] },
end

@[to_additive]
lemma measure_ne_zero_iff_nonempty_of_is_mul_left_invariant [regular Œº]
  (hŒº : Œº ‚â† 0) {s : set G} (hs : is_open s) :
  Œº s ‚â† 0 ‚Üî s.nonempty :=
by simpa [null_iff_of_is_mul_left_invariant hs, hŒº] using nonempty_iff_ne_empty.symm

@[to_additive]
lemma measure_pos_iff_nonempty_of_is_mul_left_invariant [regular Œº]
  (h3Œº : Œº ‚â† 0) {s : set G} (hs : is_open s) :
  0 < Œº s ‚Üî s.nonempty :=
pos_iff_ne_zero.trans $ measure_ne_zero_iff_nonempty_of_is_mul_left_invariant h3Œº hs

/-- If a left-invariant measure gives finite mass to a nonempty open set, then it gives finite mass
to any compact set. -/
@[to_additive "If a left-invariant measure gives finite mass to a nonempty open set, then it gives
finite mass to any compact set."]
lemma measure_lt_top_of_is_compact_of_is_mul_left_invariant
  (U : set G) (hU : is_open U) (h'U : U.nonempty) (h : Œº U ‚â† ‚àû) {K : set G} (hK : is_compact K) :
  Œº K < ‚àû :=
begin
  rw ‚Üê hU.interior_eq at h'U,
  obtain ‚ü®t, hKt‚ü© : ‚àÉ (t : finset G), K ‚äÜ ‚ãÉ (g : G) (H : g ‚àà t), (Œª (h : G), g * h) ‚Åª¬π' U :=
    compact_covered_by_mul_left_translates hK h'U,
  calc Œº K ‚â§ Œº (‚ãÉ (g : G) (H : g ‚àà t), (Œª (h : G), g * h) ‚Åª¬π' U) : measure_mono hKt
  ... ‚â§ ‚àë g in t, Œº ((Œª (h : G), g * h) ‚Åª¬π' U) : measure_bUnion_finset_le _ _
  ... = finset.card t * Œº U : by simp only [measure_preimage_mul, finset.sum_const, nsmul_eq_mul]
  ... < ‚àû : ennreal.mul_lt_top (ennreal.nat_ne_top _) h
end

/-- If a left-invariant measure gives finite mass to a set with nonempty interior, then
it gives finite mass to any compact set. -/
@[to_additive "If a left-invariant measure gives finite mass to a set with nonempty interior, then
it gives finite mass to any compact set."]
lemma measure_lt_top_of_is_compact_of_is_mul_left_invariant'
  {U : set G} (hU : (interior U).nonempty) (h : Œº U ‚â† ‚àû) {K : set G} (hK : is_compact K) :
  Œº K < ‚àû :=
measure_lt_top_of_is_compact_of_is_mul_left_invariant (interior U) is_open_interior hU
  ((measure_mono (interior_subset)).trans_lt (lt_top_iff_ne_top.2 h)).ne hK

/-- In a noncompact locally compact group, a left-invariant measure which is positive
on open sets has infinite mass. -/
@[simp, to_additive "In a noncompact locally compact additive group, a left-invariant measure which
is positive on open sets has infinite mass."]
lemma measure_univ_of_is_mul_left_invariant [locally_compact_space G] [noncompact_space G]
  (Œº : measure G) [is_open_pos_measure Œº] [Œº.is_mul_left_invariant] :
  Œº univ = ‚àû :=
begin
  /- Consider a closed compact set `K` with nonempty interior. For any compact set `L`, one may
  find `g = g (L)` such that `L` is disjoint from `g ‚Ä¢ K`. Iterating this, one finds
  infinitely many translates of `K` which are disjoint from each other. As they all have the
  same positive mass, it follows that the space has infinite measure. -/
  obtain ‚ü®K, hK, Kclosed, Kint‚ü© : ‚àÉ (K : set G), is_compact K ‚àß is_closed K ‚àß (1 : G) ‚àà interior K,
  { rcases local_is_compact_is_closed_nhds_of_group (is_open_univ.mem_nhds (mem_univ (1 : G)))
      with ‚ü®K, hK‚ü©,
    exact ‚ü®K, hK.1, hK.2.1, hK.2.2.2‚ü©, },
  have K_pos : 0 < Œº K, from measure_pos_of_nonempty_interior _ ‚ü®_, Kint‚ü©,
  have A : ‚àÄ (L : set G), is_compact L ‚Üí ‚àÉ (g : G), disjoint L (g ‚Ä¢ K),
    from Œª L hL, exists_disjoint_smul_of_is_compact hL hK,
  choose! g hg using A,
  set L : ‚Ñï ‚Üí set G := Œª n, (Œª T, T ‚à™ (g T ‚Ä¢ K))^[n] K with hL,
  have Lcompact : ‚àÄ n, is_compact (L n),
  { assume n,
    induction n with n IH,
    { exact hK },
    { simp_rw [hL, iterate_succ'],
      apply is_compact.union IH (hK.smul (g (L n))) } },
  have Lclosed : ‚àÄ n, is_closed (L n),
  { assume n,
    induction n with n IH,
    { exact Kclosed },
    { simp_rw [hL, iterate_succ'],
      apply is_closed.union IH (Kclosed.smul (g (L n))) } },
  have M : ‚àÄ n, Œº (L n) = (n + 1 : ‚Ñï) * Œº K,
  { assume n,
    induction n with n IH,
    { simp only [L, one_mul, algebra_map.coe_one, iterate_zero, id.def] },
    { calc Œº (L (n + 1)) = Œº (L n) + Œº (g (L n) ‚Ä¢ K) :
        begin
          simp_rw [hL, iterate_succ'],
          exact measure_union' (hg _ (Lcompact _)) (Lclosed _).measurable_set
        end
      ... = ((n + 1) + 1 : ‚Ñï) * Œº K :
        by simp only [IH, measure_smul, add_mul, nat.cast_add, algebra_map.coe_one, one_mul] } },
  have N : tendsto (Œª n, Œº (L n)) at_top (ùìù (‚àû * Œº K)),
  { simp_rw [M],
    apply ennreal.tendsto.mul_const _ (or.inl ennreal.top_ne_zero),
    exact ennreal.tendsto_nat_nhds_top.comp (tendsto_add_at_top_nat _) },
  simp only [ennreal.top_mul, K_pos.ne', if_false] at N,
  apply top_le_iff.1,
  exact le_of_tendsto' N (Œª n, measure_mono (subset_univ _)),
end

end topological_group

section comm_semigroup

variables [comm_semigroup G]

/-- In an abelian group every left invariant measure is also right-invariant.
  We don't declare the converse as an instance, since that would loop type-class inference, and
  we use `is_mul_left_invariant` as the default hypothesis in abelian groups. -/
@[priority 100, to_additive is_add_left_invariant.is_add_right_invariant "In an abelian additive
group every left invariant measure is also right-invariant. We don't declare the converse as an
instance, since that would loop type-class inference, and we use `is_add_left_invariant` as the
default hypothesis in abelian groups."]
instance is_mul_left_invariant.is_mul_right_invariant {Œº : measure G} [is_mul_left_invariant Œº] :
  is_mul_right_invariant Œº :=
‚ü®Œª g, by simp_rw [mul_comm, map_mul_left_eq_self]‚ü©


end comm_semigroup

section haar

namespace measure

/-- A measure on an additive group is an additive Haar measure if it is left-invariant, and gives
finite mass to compact sets and positive mass to open sets. -/
class is_add_haar_measure {G : Type*} [add_group G] [topological_space G] [measurable_space G]
  (Œº : measure G)
  extends is_finite_measure_on_compacts Œº, is_add_left_invariant Œº, is_open_pos_measure Œº : Prop

/-- A measure on a group is a Haar measure if it is left-invariant, and gives finite mass to compact
sets and positive mass to open sets. -/
@[to_additive]
class is_haar_measure {G : Type*} [group G] [topological_space G] [measurable_space G]
  (Œº : measure G)
  extends is_finite_measure_on_compacts Œº, is_mul_left_invariant Œº, is_open_pos_measure Œº : Prop

/-- Record that a Haar measure on a locally compact space is locally finite. This is needed as the
fact that a measure which is finite on compacts is locally finite is not registered as an instance,
to avoid an instance loop.

See Note [lower instance priority]. -/

@[priority 100, to_additive "Record that an additive Haar measure on a locally compact space is
locally finite. This is needed as the fact that a measure which is finite on compacts is locally
finite is not registered as an instance, to avoid an instance loop.

See Note [lower instance priority]"]
instance is_locally_finite_measure_of_is_haar_measure {G : Type*}
  [group G] [measurable_space G] [topological_space G] [locally_compact_space G]
  (Œº : measure G) [is_haar_measure Œº] :
  is_locally_finite_measure Œº :=
is_locally_finite_measure_of_is_finite_measure_on_compacts

section

variables [group G] [topological_space G] (Œº : measure G) [is_haar_measure Œº]

@[simp, to_additive]
lemma haar_singleton [topological_group G] [borel_space G] (g : G) :
  Œº {g} = Œº {(1 : G)} :=
begin
  convert measure_preimage_mul Œº (g‚Åª¬π) _,
  simp only [mul_one, preimage_mul_left_singleton, inv_inv],
end

@[to_additive measure_theory.measure.is_add_haar_measure.smul]
lemma is_haar_measure.smul {c : ‚Ñù‚â•0‚àû} (cpos : c ‚â† 0) (ctop : c ‚â† ‚àû) :
  is_haar_measure (c ‚Ä¢ Œº) :=
{ lt_top_of_is_compact := Œª K hK, ennreal.mul_lt_top ctop hK.measure_lt_top.ne,
  to_is_open_pos_measure := is_open_pos_measure_smul Œº cpos }

/-- If a left-invariant measure gives positive mass to some compact set with nonempty interior, then
it is a Haar measure. -/
@[to_additive "If a left-invariant measure gives positive mass to some compact set with nonempty
interior, then it is an additive Haar measure."]
lemma is_haar_measure_of_is_compact_nonempty_interior [topological_group G] [borel_space G]
  (Œº : measure G) [is_mul_left_invariant Œº]
  (K : set G) (hK : is_compact K) (h'K : (interior K).nonempty) (h : Œº K ‚â† 0) (h' : Œº K ‚â† ‚àû) :
  is_haar_measure Œº :=
{ lt_top_of_is_compact :=
    Œª L hL, measure_lt_top_of_is_compact_of_is_mul_left_invariant' h'K h' hL,
  to_is_open_pos_measure := is_open_pos_measure_of_mul_left_invariant_of_compact K hK h }

/-- The image of a Haar measure under a continuous surjective proper group homomorphism is again
a Haar measure. See also `mul_equiv.is_haar_measure_map`. -/
@[to_additive "The image of an additive Haar measure under a continuous surjective proper additive
group homomorphism is again an additive Haar measure. See also
`add_equiv.is_add_haar_measure_map`."]
lemma is_haar_measure_map [borel_space G] [topological_group G] {H : Type*} [group H]
  [topological_space H] [measurable_space H] [borel_space H] [t2_space H] [topological_group H]
  (f : G ‚Üí* H) (hf : continuous f) (h_surj : surjective f)
  (h_prop : tendsto f (cocompact G) (cocompact H)) :
  is_haar_measure (measure.map f Œº) :=
{ to_is_mul_left_invariant := is_mul_left_invariant_map f.to_mul_hom hf.measurable h_surj,
  lt_top_of_is_compact := begin
    assume K hK,
    rw map_apply hf.measurable hK.measurable_set,
    exact is_compact.measure_lt_top
      ((‚ü®‚ü®f, hf‚ü©, h_prop‚ü© : cocompact_map G H).is_compact_preimage hK),
  end,
  to_is_open_pos_measure := hf.is_open_pos_measure_map h_surj }

/-- A convenience wrapper for `measure_theory.measure.is_haar_measure_map`. -/
@[to_additive "A convenience wrapper for `measure_theory.measure.is_add_haar_measure_map`."]
lemma _root_.mul_equiv.is_haar_measure_map
  [borel_space G] [topological_group G] {H : Type*} [group H]
  [topological_space H] [measurable_space H] [borel_space H] [t2_space H] [topological_group H]
  (e : G ‚âÉ* H) (he : continuous e) (hesymm : continuous e.symm) :
  is_haar_measure (measure.map e Œº) :=
is_haar_measure_map Œº (e : G ‚Üí* H) he e.surjective
  ({ .. e } : G ‚âÉ‚Çú H).to_cocompact_map.cocompact_tendsto'

/-- A Haar measure on a œÉ-compact space is œÉ-finite.

See Note [lower instance priority] -/
@[priority 100, to_additive "A Haar measure on a œÉ-compact space is œÉ-finite.

See Note [lower instance priority]"]
instance is_haar_measure.sigma_finite [sigma_compact_space G] : sigma_finite Œº :=
‚ü®‚ü®{ set := compact_covering G,
  set_mem := Œª n, mem_univ _,
  finite := Œª n, is_compact.measure_lt_top $ is_compact_compact_covering G n,
  spanning := Union_compact_covering G }‚ü©‚ü©

@[to_additive]
instance {G : Type*} [group G] [topological_space G] {mG : measurable_space G}
  {H : Type*} [group H] [topological_space H] {mH : measurable_space H}
  (Œº : measure G) (ŒΩ : measure H) [is_haar_measure Œº] [is_haar_measure ŒΩ]
  [sigma_finite Œº] [sigma_finite ŒΩ]
  [has_measurable_mul G] [has_measurable_mul H] :
  is_haar_measure (Œº.prod ŒΩ) := {}

/-- If the neutral element of a group is not isolated, then a Haar measure on this group has
no atoms.

The additive version of this instance applies in particular to show that an additive Haar measure on
a nontrivial finite-dimensional real vector space has no atom. -/
@[priority 100, to_additive "If the zero element of an additive group is not isolated, then an
additive Haar measure on this group has no atoms.

This applies in particular to show that an additive Haar measure on a nontrivial finite-dimensional
real vector space has no atom."]
instance is_haar_measure.has_no_atoms [topological_group G] [borel_space G]
  [t1_space G] [locally_compact_space G] [(ùìù[‚â†] (1 : G)).ne_bot]
  (Œº : measure G) [Œº.is_haar_measure] :
  has_no_atoms Œº :=
begin
  suffices H : Œº {(1 : G)} ‚â§ 0, by { constructor, simp [le_bot_iff.1 H] },
  obtain ‚ü®K, K_compact, K_int‚ü© : ‚àÉ (K : set G), is_compact K ‚àß (1 : G) ‚àà interior K,
  { rcases exists_compact_subset is_open_univ (mem_univ (1 : G)) with ‚ü®K, hK‚ü©,
    exact ‚ü®K, hK.1, hK.2.1‚ü© },
  have K_inf : set.infinite K := infinite_of_mem_nhds (1 : G) (mem_interior_iff_mem_nhds.1 K_int),
  have ŒºKlt : Œº K ‚â† ‚àû := K_compact.measure_lt_top.ne,
  have I : ‚àÄ (n : ‚Ñï), Œº {(1 : G)} ‚â§ Œº K / n,
  { assume n,
    obtain ‚ü®t, tK, tn‚ü© : ‚àÉ (t : finset G), ‚Üët ‚äÜ K ‚àß t.card = n := K_inf.exists_subset_card_eq n,
    have A : Œº t ‚â§ Œº K := measure_mono tK,
    have B : Œº t = n * Œº {(1 : G)},
    { rw ‚Üê bUnion_of_singleton ‚Üët,
      change Œº (‚ãÉ (x ‚àà t), {x}) = n * Œº {1},
      rw @measure_bUnion_finset G G _ Œº t (Œª i, {i}),
      { simp only [tn, finset.sum_const, nsmul_eq_mul, haar_singleton] },
      { assume x hx y hy xy,
        simp only [on_fun, xy.symm, mem_singleton_iff, not_false_iff, disjoint_singleton_right] },
      { assume b hb, exact measurable_set_singleton b } },
    rw B at A,
    rwa [ennreal.le_div_iff_mul_le _ (or.inr ŒºKlt), mul_comm],
    right,
    apply (measure_pos_of_nonempty_interior Œº ‚ü®_, K_int‚ü©).ne' },
  have J : tendsto (Œª (n : ‚Ñï),  Œº K / n) at_top (ùìù (Œº K / ‚àû)) :=
    ennreal.tendsto.const_div ennreal.tendsto_nat_nhds_top (or.inr ŒºKlt),
  simp only [ennreal.div_top] at J,
  exact ge_of_tendsto' J I,
end

/- The above instance applies in particular to show that an additive Haar measure on a nontrivial
finite-dimensional real vector space has no atom. -/
example {E : Type*} [normed_add_comm_group E] [normed_space ‚Ñù E] [nontrivial E]
  [finite_dimensional ‚Ñù E] [measurable_space E] [borel_space E] (Œº : measure E)
  [is_add_haar_measure Œº] :
  has_no_atoms Œº := by apply_instance

end

variables [nontrivially_normed_field ùïú] [topological_space G] [topological_space H]
  [add_comm_group G] [add_comm_group H] [topological_add_group G] [topological_add_group H]
  [module ùïú G] [module ùïú H] (Œº : measure G) [is_add_haar_measure Œº] [borel_space G] [borel_space H]
  [t2_space H]

instance map_continuous_linear_equiv.is_add_haar_measure (e : G ‚âÉL[ùïú] H) :
  is_add_haar_measure (Œº.map e) :=
e.to_add_equiv.is_add_haar_measure_map _ e.continuous e.symm.continuous

variables [complete_space ùïú] [t2_space G] [finite_dimensional ùïú G] [has_continuous_smul ùïú G]
  [has_continuous_smul ùïú H]

instance map_linear_equiv.is_add_haar_measure (e : G ‚âÉ‚Çó[ùïú] H) : is_add_haar_measure (Œº.map e) :=
map_continuous_linear_equiv.is_add_haar_measure _ e.to_continuous_linear_equiv

end measure
end haar

end measure_theory
