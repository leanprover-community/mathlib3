/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury Kudryashov
-/
import topology.metric_space.metric_separated
import measure_theory.borel_space
import analysis.special_functions.pow

/-!
# Hausdorff measure and metric (outer) measures

In this file we define the `d`-dimensional Hausdorff measure on an (extended) metric space `X` and
the Hausdorff dimension of a set in an (extended) metric space. Let `Î¼ d Î´` be the maximal outer
measure such that `Î¼ d Î´ s â‰¤ (emetric.diam s) ^ d` for every set of diameter less than `Î´`. Then
the Hausdorff measure `Î¼H[d] s` of `s` is defined as `â¨† Î´ > 0, Î¼ d Î´ s`. By Caratheodory theorem
`measure_theory.outer_measure.is_metric.borel_le_caratheodory`, this is a Borel measure on `X`.

The value of `Î¼H[d]`, `d > 0`, on a set `s` (measurable or not) is given by
```
Î¼H[d] s = â¨† (r : â„â‰¥0âˆ) (hr : 0 < r), â¨… (t : â„• â†’ set X) (hts : s âŠ† â‹ƒ n, t n)
    (ht : âˆ€ n, emetric.diam (t n) â‰¤ r), âˆ‘' n, emetric.diam (t n) ^ d
```

For every set `s` for any `d < d'` we have either `Î¼H[d] s = âˆ` or `Î¼H[d'] s = 0`, see
`measure_theory.measure.hausdorff_measure_zero_or_top`. The Hausdorff dimension `dimH s : â„â‰¥0âˆ` of a
set `s` is the supremum of `d : â„â‰¥0` such that `Î¼H[d] s = âˆ`. Then `Î¼H[d] s = âˆ` for `d < dimH s`
and `Î¼H[d] s = 0` for `dimH s < d`.

We also define two generalizations of the Hausdorff measure. In one generalization (see
`measure_theory.measure.mk_metric`) we take any function `m (diam s)` instead of `(diam s) ^ d`. In
an even more general definition (see `measure_theory.measure.mk_metric'`) we use any function
of `m : set X â†’ â„â‰¥0âˆ`. Some authors start with a partial function `m` defined only on some sets
`s : set X` (e.g., only on balls or only on measurable sets). This is equivalent to our definition
applied to `measure_theory.extend m`.

We also define a predicate `measure_theory.outer_measure.is_metric` which says that an outer measure
is additive on metric separated pairs of sets: `Î¼ (s âˆª t) = Î¼ s + Î¼ t` provided that
`â¨… (x âˆˆ s) (y âˆˆ t), edist x y â‰  0`. This is the property required for the Caratheodory theorem
`measure_theory.outer_measure.is_metric.borel_le_caratheodory`, so we prove this theorem for any
metric outer measure, then prove that outer measures constructed using `mk_metric'` are metric outer
measures.

## Notations

We use the following notation localized in `measure_theory`.

- `Î¼H[d]` : `measure_theory.measure.hausdorff_measure d`

## Implementation notes

There are a few similar constructions called the `d`-dimensional Hausdorff measure. E.g., some
sources only allow coverings by balls and use `r ^ d` instead of `(diam s) ^ d`. While these
construction lead to different Hausdorff measures, they lead to the same notion of the Hausdorff
dimension.

## References

* [Herbert Federer, Geometric Measure Theory, Chapter 2.10][Federer1996]

## Tags

Hausdorff measure, Hausdorff dimension, dimension, measure, metric measure
-/

open_locale nnreal ennreal topological_space big_operators

open emetric set function filter

noncomputable theory

variables {Î¹ X Y : Type*} [emetric_space X] [emetric_space Y]

namespace measure_theory

namespace outer_measure

/-!
### Metric outer measures

In this section we define metric outer measures and prove Caratheodory theorem: a metric outer
measure has the Caratheodory property.
-/

/-- We say that an outer measure `Î¼` in an (e)metric space is *metric* if `Î¼ (s âˆª t) = Î¼ s + Î¼ t`
for any two metric separated sets `s`, `t`. -/
def is_metric (Î¼ : outer_measure X) : Prop :=
âˆ€ (s t : set X), is_metric_separated s t â†’ Î¼ (s âˆª t) = Î¼ s + Î¼ t

namespace is_metric

variables {Î¼ : outer_measure X}

/-- A metric outer measure is additive on a finite set of pairwise metric separated sets. -/
lemma finset_Union_of_pairwise_separated (hm : is_metric Î¼) {I : finset Î¹} {s : Î¹ â†’ set X}
  (hI : âˆ€ (i âˆˆ I) (j âˆˆ I), i â‰  j â†’ is_metric_separated (s i) (s j)) :
  Î¼ (â‹ƒ i âˆˆ I, s i) = âˆ‘ i in I, Î¼ (s i) :=
begin
  classical,
  induction I using finset.induction_on with i I hiI ihI hI, { simp },
  simp only [finset.mem_insert] at hI,
  rw [finset.set_bUnion_insert, hm, ihI, finset.sum_insert hiI],
  exacts [Î» i hi j hj hij, (hI i (or.inr hi) j (or.inr hj) hij),
    is_metric_separated.finset_Union_right
      (Î» j hj, hI i (or.inl rfl) j (or.inr hj) (ne_of_mem_of_not_mem hj hiI).symm)]
end

/-- Caratheodory theorem. If `m` is a metric outer measure, then every Borel measurable set `t` is
Caratheodory measurable: for any (not necessarily measurable) set `s` we have
`Î¼ (s âˆ© t) + Î¼ (s \ t) = Î¼ s`. -/
lemma borel_le_caratheodory (hm : is_metric Î¼) :
  borel X â‰¤ Î¼.caratheodory :=
begin
  rw [borel_eq_generate_from_is_closed],
  refine measurable_space.generate_from_le (Î» t ht, Î¼.is_caratheodory_iff_le.2 $ Î» s, _),
  set S : â„• â†’ set X := Î» n, {x âˆˆ s | (â†‘n)â»Â¹ â‰¤ inf_edist x t},
  have n0 : âˆ€ {n : â„•}, (nâ»Â¹ : â„â‰¥0âˆ) â‰  0, from Î» n, ennreal.inv_ne_zero.2 ennreal.coe_nat_ne_top,
  have Ssep : âˆ€ n, is_metric_separated (S n) t,
    from Î» n, âŸ¨nâ»Â¹, n0, Î» x hx y hy, hx.2.trans $ inf_edist_le_edist_of_mem hyâŸ©,
  have Ssep' : âˆ€ n, is_metric_separated (S n) (s âˆ© t),
    from Î» n, (Ssep n).mono subset.rfl (inter_subset_right _ _),
  have S_sub : âˆ€ n, S n âŠ† s \ t,
    from Î» n, subset_inter (inter_subset_left _ _) (Ssep n).subset_compl_right,
  have hSs : âˆ€ n, Î¼ (s âˆ© t) + Î¼ (S n) â‰¤ Î¼ s, from Î» n,
  calc Î¼ (s âˆ© t) + Î¼ (S n) = Î¼ (s âˆ© t âˆª S n) :
    eq.symm $ hm _ _ $ (Ssep' n).symm
  ... â‰¤ Î¼ (s âˆ© t âˆª s \ t)  : by { mono*, exact le_rfl }
  ... = Î¼ s : by rw [inter_union_diff],
  have Union_S : (â‹ƒ n, S n) = s \ t,
  { refine subset.antisymm (Union_subset S_sub) _,
    rintro x âŸ¨hxs, hxtâŸ©,
    rw mem_iff_ind_edist_zero_of_closed ht at hxt,
    rcases ennreal.exists_inv_nat_lt hxt with âŸ¨n, hnâŸ©,
    exact mem_Union.2 âŸ¨n, hxs, hn.leâŸ© },
  /- Now we have `âˆ€ n, Î¼ (s âˆ© t) + Î¼ (S n) â‰¤ Î¼ s` and we need to prove
  `Î¼ (s âˆ© t) + Î¼ (â‹ƒ n, S n) â‰¤ Î¼ s`. We can't pass to the limit because
  `Î¼` is only an outer measure. -/
  by_cases htop : Î¼ (s \ t) = âˆ,
  { rw [htop, ennreal.add_top, â† htop],
    exact Î¼.mono (diff_subset _ _) },
  suffices : Î¼ (â‹ƒ n, S n) â‰¤ â¨† n, Î¼ (S n),
  calc Î¼ (s âˆ© t) + Î¼ (s \ t) = Î¼ (s âˆ© t) + Î¼ (â‹ƒ n, S n) :
    by rw Union_S
  ... â‰¤ Î¼ (s âˆ© t) + â¨† n, Î¼ (S n) :
    add_le_add le_rfl this
  ... = â¨† n, Î¼ (s âˆ© t) + Î¼ (S n) : ennreal.add_supr
  ... â‰¤ Î¼ s : supr_le hSs,
  /- It suffices to show that `âˆ‘' k, Î¼ (S (k + 1) \ S k) â‰  âˆ`. Indeed, if we have this,
  then for all `N` we have `Î¼ (â‹ƒ n, S n) â‰¤ Î¼ (S N) + âˆ‘' k, m (S (N + k + 1) \ S (N + k))`
  and the second term tends to zero, see `outer_measure.Union_nat_of_monotone_of_tsum_ne_top`
  for details. -/
  have : âˆ€ n, S n âŠ† S (n + 1), from Î» n x hx,
    âŸ¨hx.1, le_trans (ennreal.inv_le_inv.2 $ ennreal.coe_nat_le_coe_nat.2 n.le_succ) hx.2âŸ©,
  refine (Î¼.Union_nat_of_monotone_of_tsum_ne_top this _).le, clear this,
  /- While the sets `S (k + 1) \ S k` are not pairwise metric separated, the sets in each
  subsequence `S (2 * k + 1) \ S (2 * k)` and `S (2 * k + 2) \ S (2 * k)` are metric separated,
  so `m` is additive on each of those sequences. -/
  rw [â† tsum_even_add_odd ennreal.summable ennreal.summable, ennreal.add_ne_top],
  suffices : âˆ€ a, (âˆ‘' (k : â„•), Î¼ (S (2 * k + 1 + a) \ S (2 * k + a))) â‰  âˆ,
    from âŸ¨by simpa using this 0, by simpa using this 1âŸ©,
  refine Î» r, ne_top_of_le_ne_top htop _,
  rw [â† Union_S, ennreal.tsum_eq_supr_nat, supr_le_iff],
  intro n,
  rw [â† hm.finset_Union_of_pairwise_separated],
  { exact Î¼.mono (Union_subset $ Î» i, Union_subset $ Î» hi x hx, mem_Union.2 âŸ¨_, hx.1âŸ©) },
  suffices : âˆ€ i  j, i < j â†’ is_metric_separated (S (2 * i + 1 + r)) (s \ S (2 * j + r)),
    from Î» i _ j _ hij, hij.lt_or_lt.elim
      (Î» h, (this i j h).mono (inter_subset_left _ _) (Î» x hx, âŸ¨hx.1.1, hx.2âŸ©))
      (Î» h, (this j i h).symm.mono  (Î» x hx, âŸ¨hx.1.1, hx.2âŸ©) (inter_subset_left _ _)),
  intros i j hj,
  have A : ((â†‘(2 * j + r))â»Â¹ : â„â‰¥0âˆ) < (â†‘(2 * i + 1 + r))â»Â¹,
    by { rw [ennreal.inv_lt_inv, ennreal.coe_nat_lt_coe_nat], linarith },
  refine âŸ¨(â†‘(2 * i + 1 + r))â»Â¹ - (â†‘(2 * j + r))â»Â¹, by simpa using A, Î» x hx y hy, _âŸ©,
  have : inf_edist y t < (â†‘(2 * j + r))â»Â¹, from not_le.1 (Î» hle, hy.2 âŸ¨hy.1, hleâŸ©),
  rcases exists_edist_lt_of_inf_edist_lt this with âŸ¨z, hzt, hyzâŸ©,
  have hxz : (â†‘(2 * i + 1 + r))â»Â¹ â‰¤ edist x z, from le_inf_edist.1 hx.2 _ hzt,
  apply ennreal.le_of_add_le_add_right (hyz.trans_le le_top),
  refine le_trans _ (edist_triangle _ _ _),
  refine (add_le_add le_rfl hyz.le).trans (eq.trans_le _ hxz),
  rw [ennreal.sub_add_cancel_of_le A.le]
end

lemma le_caratheodory [measurable_space X] [borel_space X] (hm : is_metric Î¼) :
  â€¹measurable_space Xâ€º â‰¤ Î¼.caratheodory :=
by { rw @borel_space.measurable_eq X _ _, exact hm.borel_le_caratheodory }

end is_metric

/-!
### Constructors of metric outer measures

In this section we provide constructors `measure_theory.outer_measure.mk_metric'` and
`measure_theory.outer_measure.mk_metric` and prove that these outer measures are metric outer
measures. We also prove basic lemmas about `map`/`comap` of these measures.
-/

/-- Auxiliary definition for `outer_measure.mk_metric'`: given a function on sets
`m : set X â†’ â„â‰¥0âˆ`, returns the maximal outer measure `Î¼` such that `Î¼ s â‰¤ m s`
for any set `s` of diameter at most `r`.-/
def mk_metric'.pre (m : set X â†’ â„â‰¥0âˆ) (r : â„â‰¥0âˆ) :
  outer_measure X :=
bounded_by $ extend (Î» s (hs : diam s â‰¤ r), m s)

/-- Given a function `m : set X â†’ â„â‰¥0âˆ`, `mk_metric' m` is the supremum of `mk_metric'.pre m r`
over `r > 0`. Equivalently, it is the limit of `mk_metric'.pre m r` as `r` tends to zero from
the right. -/
def mk_metric' (m : set X â†’ â„â‰¥0âˆ) :
  outer_measure X :=
â¨† r > 0, mk_metric'.pre m r

/-- Given a function `m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ` and `r > 0`, let `Î¼ r` be the maximal outer measure such that
`Î¼ s = 0` on subsingletons and `Î¼ s â‰¤ m (emetric.diam s)` whenever `emetric.diam s < r`. Then
`mk_metric m = â¨† r > 0, Î¼ r`. We add `â¨† (hs : Â¬s.subsingleton)` to ensure that in the case
`m x = x ^ d` the definition gives the expected result for `d = 0`. -/
def mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) : outer_measure X :=
mk_metric' (Î» s, â¨† (hs : Â¬s.subsingleton), m (diam s))

namespace mk_metric'

variables {m : set X â†’ â„â‰¥0âˆ} {r : â„â‰¥0âˆ} {Î¼ : outer_measure X} {s : set X}

lemma le_pre : Î¼ â‰¤ pre m r â†” âˆ€ s : set X, diam s â‰¤ r â†’ Î¼ s â‰¤ m s :=
by simp only [pre, le_bounded_by, extend, le_infi_iff]

lemma pre_le (hs : diam s â‰¤ r) : pre m r s â‰¤ m s :=
(bounded_by_le _).trans $ infi_le _ hs

lemma mono_pre (m : set X â†’ â„â‰¥0âˆ) {r r' : â„â‰¥0âˆ} (h : r â‰¤ r') :
  pre m r' â‰¤ pre m r :=
le_pre.2 $ Î» s hs, pre_le (hs.trans h)

lemma mono_pre_nat (m : set X â†’ â„â‰¥0âˆ) :
  monotone (Î» k : â„•, pre m kâ»Â¹) :=
Î» k l h, le_pre.2 $ Î» s hs, pre_le (hs.trans $ by simpa)

lemma tendsto_pre (m : set X â†’ â„â‰¥0âˆ) (s : set X) :
  tendsto (Î» r, pre m r s) (ğ“[Ioi 0] 0) (ğ“ $ mk_metric' m s) :=
begin
  rw [â† map_coe_Ioi_at_bot, tendsto_map'_iff],
  simp only [mk_metric', outer_measure.supr_apply, supr_subtype'],
  exact tendsto_at_bot_supr (Î» r r' hr, mono_pre _ hr _)
end

lemma tendsto_pre_nat (m : set X â†’ â„â‰¥0âˆ) (s : set X) :
  tendsto (Î» n : â„•, pre m nâ»Â¹ s) at_top (ğ“ $ mk_metric' m s) :=
begin
  refine (tendsto_pre m s).comp (tendsto_inf.2 âŸ¨ennreal.tendsto_inv_nat_nhds_zero, _âŸ©),
  refine tendsto_principal.2 (eventually_of_forall $ Î» n, _),
  simp
end

lemma eq_supr_nat (m : set X â†’ â„â‰¥0âˆ) :
  mk_metric' m = â¨† n : â„•, mk_metric'.pre m nâ»Â¹ :=
begin
  ext1 s,
  rw supr_apply,
  refine tendsto_nhds_unique (mk_metric'.tendsto_pre_nat m s)
    (tendsto_at_top_supr $ Î» k l hkl, mk_metric'.mono_pre_nat m hkl s)
end

lemma trim_pre [measurable_space X] [opens_measurable_space X]
  (m : set X â†’ â„â‰¥0âˆ) (hcl : âˆ€ s, m (closure s) = m s) (r : â„â‰¥0âˆ) :
  (pre m r).trim = pre m r :=
begin
  refine le_antisymm (le_pre.2 $ Î» s hs, _) (le_trim _),
  rw trim_eq_infi,
  refine (infi_le_of_le (closure s) $ infi_le_of_le subset_closure $
    infi_le_of_le measurable_set_closure ((pre_le _).trans_eq (hcl _))),
  rwa diam_closure
end

end mk_metric'

/-- An outer measure constructed using `outer_measure.mk_metric'` is a metric outer measure. -/
lemma mk_metric'_is_metric (m : set X â†’ â„â‰¥0âˆ) :
  (mk_metric' m).is_metric :=
begin
  rintros s t âŸ¨r, r0, hrâŸ©,
  refine tendsto_nhds_unique_of_eventually_eq
    (mk_metric'.tendsto_pre _ _)
    ((mk_metric'.tendsto_pre _ _).add (mk_metric'.tendsto_pre _ _)) _,
  rw [â† pos_iff_ne_zero] at r0,
  filter_upwards [Ioo_mem_nhds_within_Ioi âŸ¨le_rfl, r0âŸ©],
  rintro Îµ âŸ¨Îµ0, ÎµrâŸ©,
  refine bounded_by_union_of_top_of_nonempty_inter _,
  rintro u âŸ¨x, hxs, hxuâŸ© âŸ¨y, hyt, hyuâŸ©,
  have : Îµ < diam u, from Îµr.trans_le ((hr x hxs y hyt).trans $ edist_le_diam_of_mem hxu hyu),
  exact infi_eq_top.2 (Î» h, (this.not_le h).elim)
end

/-- If `c âˆ‰ {0, âˆ}` and `mâ‚ d â‰¤ c * mâ‚‚ d` for `0 < d < Îµ` for some `Îµ > 0`
(we use `â‰¤á¶ [ğ“[Ioi 0]]` to state this), then `mk_metric mâ‚ hmâ‚ â‰¤ c â€¢ mk_metric mâ‚‚ hmâ‚‚`. -/
lemma mk_metric_mono_smul {mâ‚ mâ‚‚ : â„â‰¥0âˆ â†’ â„â‰¥0âˆ} {c : â„â‰¥0âˆ} (hc : c â‰  âˆ) (h0 : c â‰  0)
  (hle : mâ‚ â‰¤á¶ [ğ“[Ioi 0] 0] c â€¢ mâ‚‚) :
  (mk_metric mâ‚ : outer_measure X) â‰¤ c â€¢ mk_metric mâ‚‚ :=
begin
  classical,
  rcases (mem_nhds_within_Ioi_iff_exists_Ioo_subset' ennreal.zero_lt_one).1 hle with âŸ¨r, hr0, hrâŸ©,
  refine Î» s, le_of_tendsto_of_tendsto (mk_metric'.tendsto_pre _ s)
    (ennreal.tendsto.const_mul (mk_metric'.tendsto_pre _ s) (or.inr hc))
    (mem_sets_of_superset (Ioo_mem_nhds_within_Ioi âŸ¨le_rfl, hr0âŸ©) (Î» r' hr', _)),
  simp only [mem_set_of_eq, mk_metric'.pre],
  rw [â† smul_apply, smul_bounded_by hc],
  refine le_bounded_by.2 (Î» t, (bounded_by_le _).trans _) _,
  simp only [smul_eq_mul, pi.smul_apply, extend, infi_eq_if],
  split_ifs with ht ht,
  { refine supr_le (Î» htâ‚, _),
    rw [supr_eq_if, if_pos htâ‚],
    refine hr âŸ¨_, ht.trans_lt hr'.2âŸ©,
    exact pos_iff_ne_zero.2 (mt diam_eq_zero_iff.1 htâ‚) },
  { simp [h0] }
end

/-- If `mâ‚ d â‰¤ mâ‚‚ d` for `0 < d < Îµ` for some `Îµ > 0` (we use `â‰¤á¶ [ğ“[Ioi 0]]` to state this), then
`mk_metric mâ‚ hmâ‚ â‰¤ mk_metric mâ‚‚ hmâ‚‚`-/
lemma mk_metric_mono {mâ‚ mâ‚‚ : â„â‰¥0âˆ â†’ â„â‰¥0âˆ} (hle : mâ‚ â‰¤á¶ [ğ“[Ioi 0] 0] mâ‚‚) :
  (mk_metric mâ‚ : outer_measure X) â‰¤ mk_metric mâ‚‚ :=
by { convert mk_metric_mono_smul ennreal.one_ne_top ennreal.zero_lt_one.ne' _; simp * }

lemma isometry_comap_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) {f : X â†’ Y} (hf : isometry f)
  (H : monotone (Î» d : {d : â„â‰¥0âˆ | d â‰  0}, m d) âˆ¨ surjective f) :
  comap f (mk_metric m) = mk_metric m :=
begin
  simp only [mk_metric, mk_metric', mk_metric'.pre, induced_outer_measure, comap_supr],
  refine supr_congr id surjective_id (Î» Îµ, supr_congr id surjective_id $ Î» hÎµ, _),
  rw comap_bounded_by _ (H.imp (Î» h_mono, _) id),
  { congr' with s : 1,
    apply extend_congr,
    { simp [hf.ediam_image] },
    { intros, simp [hf.injective.subsingleton_image_iff, hf.ediam_image] } },
  { refine Î» s t hst, infi_le_infi2 (Î» ht, âŸ¨(diam_mono hst).trans ht, supr_le $ Î» hs, _âŸ©),
    have ht : Â¬(t : set Y).subsingleton, from Î» ht, hs (ht.mono hst),
    refine (@h_mono âŸ¨_, mt diam_eq_zero_iff.1 hsâŸ© âŸ¨_, mt diam_eq_zero_iff.1 htâŸ©
      (diam_mono hst)).trans _,
    exact le_supr (Î» h : Â¬(t : set Y).subsingleton, m (diam (t : set Y))) ht }
end

lemma isometry_map_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) {f : X â†’ Y} (hf : isometry f)
  (H : monotone (Î» d : {d : â„â‰¥0âˆ | d â‰  0}, m d) âˆ¨ surjective f) :
  map f (mk_metric m) = restrict (range f) (mk_metric m) :=
by rw [â† isometry_comap_mk_metric _ hf H, map_comap]

lemma isometric_comap_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (f : X â‰ƒáµ¢ Y) :
  comap f (mk_metric m) = mk_metric m :=
isometry_comap_mk_metric _ f.isometry (or.inr f.surjective)

lemma isometric_map_mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (f : X â‰ƒáµ¢ Y) :
  map f (mk_metric m) = mk_metric m :=
by rw [â† isometric_comap_mk_metric _ f, map_comap_of_surjective f.surjective]

lemma trim_mk_metric [measurable_space X] [borel_space X] (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) :
  (mk_metric m : outer_measure X).trim = mk_metric m :=
begin
  simp only [mk_metric, mk_metric'.eq_supr_nat, trim_supr],
  congr' 1 with n : 1,
  refine mk_metric'.trim_pre _ (Î» s, _) _,
  simp
end

end outer_measure

/-!
### Metric measures

In this section we use `measure_theory.outer_measure.to_measure` and theorems about
`measure_theory.outer_measure.mk_metric'`/`measure_theory.outer_measure.mk_metric` to define
`measure_theory.measure.mk_metric'`/`measure_theory.measure.mk_metric`. We also restate some lemmas
about metric outer measures for metric measures.
-/

namespace measure

variables [measurable_space X] [borel_space X]

/-- Given a function `m : set X â†’ â„â‰¥0âˆ`, `mk_metric' m` is the supremum of `Î¼ r`
over `r > 0`, where `Î¼ r` is the maximal outer measure `Î¼` such that `Î¼ s â‰¤ m s`
for all `s`. While each `Î¼ r` is an *outer* measure, the supremum is a measure. -/
def mk_metric' (m : set X â†’ â„â‰¥0âˆ) : measure X :=
(outer_measure.mk_metric' m).to_measure (outer_measure.mk_metric'_is_metric _).le_caratheodory

/-- Given a function `m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ`, `mk_metric m` is the supremum of `Î¼ r` over `r > 0`, where
`Î¼ r` is the maximal outer measure `Î¼` such that `Î¼ s â‰¤ m s` for all sets `s` that contain at least
two points. While each `mk_metric'.pre` is an *outer* measure, the supremum is a measure. -/
def mk_metric (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) : measure X :=
(outer_measure.mk_metric m).to_measure (outer_measure.mk_metric'_is_metric _).le_caratheodory

@[simp] lemma mk_metric'_to_outer_measure (m : set X â†’ â„â‰¥0âˆ) :
  (mk_metric' m).to_outer_measure = (outer_measure.mk_metric' m).trim :=
rfl

@[simp] lemma mk_metric_to_outer_measure (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) :
  (mk_metric m : measure X).to_outer_measure = outer_measure.mk_metric m :=
outer_measure.trim_mk_metric m

end measure

lemma outer_measure.coe_mk_metric [measurable_space X] [borel_space X] (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) :
  â‡‘(outer_measure.mk_metric m : outer_measure X) = measure.mk_metric m :=
by rw [â† measure.mk_metric_to_outer_measure, coe_to_outer_measure]

variables [measurable_space X] [borel_space X]

namespace measure

/-- If `c âˆ‰ {0, âˆ}` and `mâ‚ d â‰¤ c * mâ‚‚ d` for `0 < d < Îµ` for some `Îµ > 0`
(we use `â‰¤á¶ [ğ“[Ioi 0]]` to state this), then `mk_metric mâ‚ hmâ‚ â‰¤ c â€¢ mk_metric mâ‚‚ hmâ‚‚`. -/
lemma mk_metric_mono_smul {mâ‚ mâ‚‚ : â„â‰¥0âˆ â†’ â„â‰¥0âˆ} {c : â„â‰¥0âˆ} (hc : c â‰  âˆ) (h0 : c â‰  0)
  (hle : mâ‚ â‰¤á¶ [ğ“[Ioi 0] 0] c â€¢ mâ‚‚) :
  (mk_metric mâ‚ : measure X) â‰¤ c â€¢ mk_metric mâ‚‚ :=
begin
  intros s hs,
  rw [â† outer_measure.coe_mk_metric, coe_smul, â† outer_measure.coe_mk_metric],
  exact outer_measure.mk_metric_mono_smul hc h0 hle s
end

/-- If `mâ‚ d â‰¤ mâ‚‚ d` for `0 < d < Îµ` for some `Îµ > 0` (we use `â‰¤á¶ [ğ“[Ioi 0]]` to state this), then
`mk_metric mâ‚ hmâ‚ â‰¤ mk_metric mâ‚‚ hmâ‚‚`-/
lemma mk_metric_mono {mâ‚ mâ‚‚ : â„â‰¥0âˆ â†’ â„â‰¥0âˆ} (hle : mâ‚ â‰¤á¶ [ğ“[Ioi 0] 0] mâ‚‚) :
  (mk_metric mâ‚ : measure X) â‰¤ mk_metric mâ‚‚ :=
by { convert mk_metric_mono_smul ennreal.one_ne_top ennreal.zero_lt_one.ne' _; simp * }

/-- A formula for `measure_theory.measure.mk_metric`. -/
lemma mk_metric_apply (m : â„â‰¥0âˆ â†’ â„â‰¥0âˆ) (s : set X) :
  mk_metric m s = â¨† (r : â„â‰¥0âˆ) (hr : 0 < r), â¨… (t : â„• â†’ set X) (hts : s âŠ† â‹ƒ n, t n)
    (ht : âˆ€ n, diam (t n) â‰¤ r), âˆ‘' n, â¨† (ht : Â¬(t n).subsingleton), m (diam (t n)) :=
begin
  -- We mostly unfold the definitions but we need to switch the order of `âˆ‘'` and `â¨…`
  -- and merge `(t n).nonempty` with `Â¬subsingleton (t n)`
  classical,
  simp only [â† outer_measure.coe_mk_metric, outer_measure.mk_metric, outer_measure.mk_metric',
    outer_measure.supr_apply, outer_measure.mk_metric'.pre, outer_measure.bounded_by_apply,
    extend],
  refine supr_congr (Î» r, r) surjective_id (Î» r, supr_congr_Prop iff.rfl $ Î» hr,
    infi_congr _ surjective_id $ Î» t, infi_congr_Prop iff.rfl $ Î» ht, _),
  by_cases htr : âˆ€ n, diam (t n) â‰¤ r,
  { rw [infi_eq_if, if_pos htr],
    congr' 1 with n : 1,
    simp only [infi_eq_if, htr n, id, if_true, supr_and'],
    refine supr_congr_Prop (and_iff_right_of_imp $ Î» h, _) (Î» _, rfl),
    contrapose! h,
    rw [not_nonempty_iff_eq_empty.1 h],
    exact subsingleton_empty },
  { rw [infi_eq_if, if_neg htr],
    push_neg at htr, rcases htr with âŸ¨n, hnâŸ©,
    refine ennreal.tsum_eq_top_of_eq_top âŸ¨n, _âŸ©,
    rw [supr_eq_if, if_pos, infi_eq_if, if_neg],
    exact hn.not_le,
    rcases diam_pos_iff.1 ((zero_le r).trans_lt hn) with âŸ¨x, hx, -âŸ©,
    exact âŸ¨x, hxâŸ© }
end

/-!
### Hausdorff measure and Hausdorff dimension
-/

/-- Hausdorff measure on an (e)metric space. -/
def hausdorff_measure (d : â„) : measure X := mk_metric (Î» r, r ^ d)

localized "notation `Î¼H[` d `]` := measure_theory.measure.hausdorff_measure d" in measure_theory

/-- A formula for `Î¼H[d] s` that works for all `d`. In case of a positive `d` a simpler formula
is available as `measure_theory.measure.hausdorff_measure_apply`. -/
lemma hausdorff_measure_apply' (d : â„) (s : set X) :
  Î¼H[d] s = â¨† (r : â„â‰¥0âˆ) (hr : 0 < r), â¨… (t : â„• â†’ set X) (hts : s âŠ† â‹ƒ n, t n)
    (ht : âˆ€ n, diam (t n) â‰¤ r), âˆ‘' n, â¨† (ht : Â¬(t n).subsingleton), (diam (t n)) ^ d :=
mk_metric_apply _ _

/-- A formula for `Î¼H[d] s` that works for all positive `d`. -/
lemma hausdorff_measure_apply {d : â„} (hd : 0 < d) (s : set X) :
  Î¼H[d] s = â¨† (r : â„â‰¥0âˆ) (hr : 0 < r), â¨… (t : â„• â†’ set X) (hts : s âŠ† â‹ƒ n, t n)
    (ht : âˆ€ n, diam (t n) â‰¤ r), âˆ‘' n, diam (t n) ^ d :=
begin
  classical,
  rw hausdorff_measure_apply',
  -- I wish `congr'` was able to generate this
  refine supr_congr id surjective_id (Î» r, supr_congr_Prop iff.rfl $ Î» hr,
    infi_congr id surjective_id $ Î» t, infi_congr_Prop iff.rfl $ Î» hts,
    infi_congr_Prop iff.rfl $ Î» ht, tsum_congr $ Î» n, _),
  rw [supr_eq_if], split_ifs with ht',
  { erw [diam_eq_zero_iff.2 ht', ennreal.zero_rpow_of_pos hd, ennreal.bot_eq_zero] },
  { refl }
end

/-- If `dâ‚ < dâ‚‚`, then for any set `s` we have either `Î¼H[dâ‚‚] s = 0`, or `Î¼H[dâ‚] s = âˆ`. -/
lemma hausdorff_measure_zero_or_top {dâ‚ dâ‚‚ : â„} (h : dâ‚ < dâ‚‚) (s : set X) :
  Î¼H[dâ‚‚] s = 0 âˆ¨ Î¼H[dâ‚] s = âˆ :=
begin
  by_contra H, push_neg at H,
  suffices : âˆ€ (c : â„â‰¥0), c â‰  0 â†’ Î¼H[dâ‚‚] s â‰¤ c * Î¼H[dâ‚] s,
  { rcases ennreal.exists_nnreal_pos_mul_lt H.2 H.1 with âŸ¨c, hc0, hcâŸ©,
    exact hc.not_le (this c (pos_iff_ne_zero.1 hc0)) },
  intros c hc,
  refine le_iff'.1 (mk_metric_mono_smul ennreal.coe_ne_top (by exact_mod_cast hc) _) s,
  have : 0 <  (c ^ (dâ‚‚ - dâ‚)â»Â¹ : â„â‰¥0âˆ),
  { rw [ennreal.coe_rpow_of_ne_zero hc, pos_iff_ne_zero, ne.def, ennreal.coe_eq_zero,
      nnreal.rpow_eq_zero_iff],
    exact mt and.left hc },
  filter_upwards [Ioo_mem_nhds_within_Ioi âŸ¨le_rfl, thisâŸ©],
  rintro r âŸ¨hrâ‚€, hrcâŸ©,
  lift r to â„â‰¥0 using ne_top_of_lt hrc,
  rw [pi.smul_apply, smul_eq_mul, â† ennreal.div_le_iff_le_mul (or.inr ennreal.coe_ne_top)
    (or.inr $ mt ennreal.coe_eq_zero.1 hc), â† ennreal.rpow_sub _ _ hrâ‚€.ne' ennreal.coe_ne_top],
  refine (ennreal.rpow_lt_rpow hrc (sub_pos.2 h)).le.trans _,
  rw [â† ennreal.rpow_mul, inv_mul_cancel (sub_pos.2 h).ne', ennreal.rpow_one],
  exact le_rfl
end

/-- Hausdorff measure `Î¼H[d] s` is monotone in `d`. -/
lemma hausdorff_measure_mono {dâ‚ dâ‚‚ : â„} (h : dâ‚ â‰¤ dâ‚‚) (s : set X) : Î¼H[dâ‚‚] s â‰¤ Î¼H[dâ‚] s :=
begin
  rcases h.eq_or_lt with rfl|h, { exact le_rfl },
  cases hausdorff_measure_zero_or_top h s with hs hs,
  { rw hs, exact zero_le _ },
  { rw hs, exact le_top }
end

end measure

open_locale measure_theory
open measure

/-- Hausdorff dimension of a set in an (e)metric space. -/
def dimH (s : set X) : â„â‰¥0âˆ := â¨† (d : â„â‰¥0) (hd : Î¼H[d] s = âˆ), d

lemma hausdorff_measure_of_lt_dimH {s : set X} {d : â„â‰¥0}
  (h : â†‘d < dimH s) : Î¼H[d] s = âˆ :=
begin
  simp only [dimH, lt_supr_iff] at h,
  rcases h with âŸ¨d', hsd', hdd'âŸ©,
  rw [ennreal.coe_lt_coe, â† nnreal.coe_lt_coe] at hdd',
  refine (hausdorff_measure_zero_or_top hdd' s).resolve_left (Î» h, _),
  exact (ennreal.zero_ne_top $ h.symm.trans hsd').elim
end

lemma hausdorff_measure_of_dimH_lt {s : set X} {d : â„â‰¥0}
  (h : dimH s < d) : Î¼H[d] s = 0 :=
begin
  rcases ennreal.lt_iff_exists_nnreal_btwn.1 h with âŸ¨d', hsd', hd'dâŸ©,
  rw [ennreal.coe_lt_coe, â† nnreal.coe_lt_coe] at hd'd,
  exact (hausdorff_measure_zero_or_top hd'd s).resolve_right
    (Î» h, hsd'.not_le (le_bsupr d' h))
end

lemma measure_zero_of_dimH_lt {Î¼ : measure X} {d : â„â‰¥0}
  (h : Î¼ â‰ª Î¼H[d]) {s : set X} (hd : dimH s < d) :
  Î¼ s = 0 :=
h $ hausdorff_measure_of_dimH_lt hd

end measure_theory
