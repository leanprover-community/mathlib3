/-
Copyright (c) 2021 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/

import measure_theory.l1_space
import analysis.normed_space.indicator_function

/-! # Functions integrable on a set and at a filter

We define `integrable_on f s Œº := integrable f (Œº.restrict s)` and prove theorems like
`integrable_on_union : integrable_on f (s ‚à™ t) Œº ‚Üî integrable_on f s Œº ‚àß integrable_on f t Œº`.

Next we define a predicate `integrable_at_filter (f : Œ± ‚Üí E) (l : filter Œ±) (Œº : measure Œ±)`
saying that `f` is integrable at some set `s ‚àà l` and prove that a measurable function is integrable
at `l` with respect to `Œº` provided that `f` is bounded above at `l ‚äì Œº.ae` and `Œº` is finite
at `l`.

-/

noncomputable theory
open set filter topological_space measure_theory function
open_locale classical topological_space interval big_operators filter ennreal measure_theory

variables {Œ± Œ≤ E F : Type*} [measurable_space Œ±]

section piecewise

variables {Œº : measure Œ±} {s : set Œ±} {f g : Œ± ‚Üí Œ≤}

lemma piecewise_ae_eq_restrict (hs : measurable_set s) : piecewise s f g =·µê[Œº.restrict s] f :=
begin
  rw [ae_restrict_eq hs],
  exact (piecewise_eq_on s f g).eventually_eq.filter_mono inf_le_right
end

lemma piecewise_ae_eq_restrict_compl (hs : measurable_set s) :
  piecewise s f g =·µê[Œº.restrict s·∂ú] g :=
begin
  rw [ae_restrict_eq hs.compl],
  exact (piecewise_eq_on_compl s f g).eventually_eq.filter_mono inf_le_right
end

end piecewise

section indicator_function

variables [has_zero Œ≤] {Œº : measure Œ±} {s : set Œ±} {f : Œ± ‚Üí Œ≤}

lemma indicator_ae_eq_restrict (hs : measurable_set s) : indicator s f =·µê[Œº.restrict s] f :=
piecewise_ae_eq_restrict hs

lemma indicator_ae_eq_restrict_compl (hs : measurable_set s) : indicator s f =·µê[Œº.restrict s·∂ú] 0 :=
piecewise_ae_eq_restrict_compl hs

end indicator_function

section

variables [measurable_space Œ≤] {l l' : filter Œ±} {f g : Œ± ‚Üí Œ≤} {Œº ŒΩ : measure Œ±}

/-- A function `f` is measurable at filter `l` w.r.t. a measure `Œº` if it is ae-measurable
w.r.t. `Œº.restrict s` for some `s ‚àà l`. -/
def measurable_at_filter (f : Œ± ‚Üí Œ≤) (l : filter Œ±) (Œº : measure Œ± . volume_tac) :=
‚àÉ s ‚àà l, ae_measurable f (Œº.restrict s)

@[simp] lemma measurable_at_bot {f : Œ± ‚Üí Œ≤} : measurable_at_filter f ‚ä• Œº :=
‚ü®‚àÖ, mem_bot_sets, by simp‚ü©

protected lemma measurable_at_filter.eventually (h : measurable_at_filter f l Œº) :
  ‚àÄ·∂† s in l.lift' powerset, ae_measurable f (Œº.restrict s) :=
(eventually_lift'_powerset' $ Œª s t, ae_measurable.mono_set).2 h

protected lemma measurable_at_filter.filter_mono (h : measurable_at_filter f l Œº) (h' : l' ‚â§ l) :
  measurable_at_filter f l' Œº :=
let ‚ü®s, hsl, hs‚ü© := h in ‚ü®s, h' hsl, hs‚ü©

protected lemma ae_measurable.measurable_at_filter (h : ae_measurable f Œº) :
  measurable_at_filter f l Œº :=
‚ü®univ, univ_mem_sets, by rwa measure.restrict_univ‚ü©

lemma ae_measurable.measurable_at_filter_of_mem {s} (h : ae_measurable f (Œº.restrict s))
  (hl : s ‚àà l):
  measurable_at_filter f l Œº :=
‚ü®s, hl, h‚ü©

protected lemma measurable.measurable_at_filter (h : measurable f) :
  measurable_at_filter f l Œº :=
h.ae_measurable.measurable_at_filter

lemma ae_measurable_indicator_iff [has_zero Œ≤] {s} (hs : measurable_set s) :
  ae_measurable (indicator s f) Œº ‚Üî ae_measurable f (Œº.restrict s)  :=
begin
  split,
  { assume h,
    exact (h.mono_measure measure.restrict_le_self).congr (indicator_ae_eq_restrict hs) },
  { assume h,
    refine ‚ü®indicator s (h.mk f), h.measurable_mk.indicator hs, _‚ü©,
    have A : s.indicator f =·µê[Œº.restrict s] s.indicator (ae_measurable.mk f h) :=
      (indicator_ae_eq_restrict hs).trans (h.ae_eq_mk.trans $ (indicator_ae_eq_restrict hs).symm),
    have B : s.indicator f =·µê[Œº.restrict s·∂ú] s.indicator (ae_measurable.mk f h) :=
      (indicator_ae_eq_restrict_compl hs).trans (indicator_ae_eq_restrict_compl hs).symm,
    have : s.indicator f =·µê[Œº.restrict s + Œº.restrict s·∂ú] s.indicator (ae_measurable.mk f h) :=
      ae_add_measure_iff.2 ‚ü®A, B‚ü©,
    simpa only [hs, measure.restrict_add_restrict_compl] using this },
end

lemma ae_measurable.restrict (hfm : ae_measurable f Œº) {s} :
  ae_measurable f (Œº.restrict s) :=
‚ü®ae_measurable.mk f hfm, hfm.measurable_mk, ae_restrict_of_ae hfm.ae_eq_mk‚ü©

lemma ae_measurable.indicator [has_zero Œ≤] (hfm : ae_measurable f Œº) {s} (hs : measurable_set s) :
  ae_measurable (s.indicator f) Œº :=
(ae_measurable_indicator_iff hs).mpr hfm.restrict

end

namespace measure_theory

section normed_group

lemma has_finite_integral_restrict_of_bounded [normed_group E] {f : Œ± ‚Üí E} {s : set Œ±}
  {Œº : measure Œ±} {C}  (hs : Œº s < ‚àû) (hf : ‚àÄ·µê x ‚àÇ(Œº.restrict s), ‚à•f x‚à• ‚â§ C) :
  has_finite_integral f (Œº.restrict s) :=
by haveI : finite_measure (Œº.restrict s) := ‚ü®by rwa [measure.restrict_apply_univ]‚ü©;
  exact has_finite_integral_of_bounded hf

variables [normed_group E] [measurable_space E] {f g : Œ± ‚Üí E} {s t : set Œ±} {Œº ŒΩ : measure Œ±}

/-- A function is `integrable_on` a set `s` if it is almost everywhere measurable on `s` and if the
integral of its pointwise norm over `s` is less than infinity. -/
def integrable_on (f : Œ± ‚Üí E) (s : set Œ±) (Œº : measure Œ± . volume_tac) : Prop :=
integrable f (Œº.restrict s)

lemma integrable_on.integrable (h : integrable_on f s Œº) :
  integrable f (Œº.restrict s) := h

@[simp] lemma integrable_on_empty : integrable_on f ‚àÖ Œº :=
by simp [integrable_on, integrable_zero_measure]

@[simp] lemma integrable_on_univ : integrable_on f univ Œº ‚Üî integrable f Œº :=
by rw [integrable_on, measure.restrict_univ]

lemma integrable_on_zero : integrable_on (Œª _, (0:E)) s Œº := integrable_zero _ _ _

lemma integrable_on_const {C : E} : integrable_on (Œª _, C) s Œº ‚Üî C = 0 ‚à® Œº s < ‚àû :=
integrable_const_iff.trans $ by rw [measure.restrict_apply_univ]

lemma integrable_on.mono (h : integrable_on f t ŒΩ) (hs : s ‚äÜ t) (hŒº : Œº ‚â§ ŒΩ) :
  integrable_on f s Œº :=
h.mono_measure $ measure.restrict_mono hs hŒº

lemma integrable_on.mono_set (h : integrable_on f t Œº) (hst : s ‚äÜ t) :
  integrable_on f s Œº :=
h.mono hst (le_refl _)

lemma integrable_on.mono_measure (h : integrable_on f s ŒΩ) (hŒº : Œº ‚â§ ŒΩ) :
  integrable_on f s Œº :=
h.mono (subset.refl _) hŒº

lemma integrable_on.mono_set_ae (h : integrable_on f t Œº) (hst : s ‚â§·µê[Œº] t) :
  integrable_on f s Œº :=
h.integrable.mono_measure $ restrict_mono_ae hst

lemma integrable.integrable_on (h : integrable f Œº) : integrable_on f s Œº :=
h.mono_measure $ measure.restrict_le_self

lemma integrable.integrable_on' (h : integrable f (Œº.restrict s)) : integrable_on f s Œº :=
h

lemma integrable_on.restrict (h : integrable_on f s Œº) (hs : measurable_set s) :
  integrable_on f s (Œº.restrict t) :=
by { rw [integrable_on, measure.restrict_restrict hs], exact h.mono_set (inter_subset_left _ _) }

lemma integrable_on.left_of_union (h : integrable_on f (s ‚à™ t) Œº) : integrable_on f s Œº :=
h.mono_set $ subset_union_left _ _

lemma integrable_on.right_of_union (h : integrable_on f (s ‚à™ t) Œº) : integrable_on f t Œº :=
h.mono_set $ subset_union_right _ _

lemma integrable_on.union (hs : integrable_on f s Œº) (ht : integrable_on f t Œº) :
  integrable_on f (s ‚à™ t) Œº :=
(hs.add_measure ht).mono_measure $ measure.restrict_union_le _ _

@[simp] lemma integrable_on_union :
  integrable_on f (s ‚à™ t) Œº ‚Üî integrable_on f s Œº ‚àß integrable_on f t Œº :=
‚ü®Œª h, ‚ü®h.left_of_union, h.right_of_union‚ü©, Œª h, h.1.union h.2‚ü©

@[simp] lemma integrable_on_singleton_iff {x : Œ±} [measurable_singleton_class Œ±]:
  integrable_on f {x} Œº ‚Üî f x = 0 ‚à® Œº {x} < ‚àû :=
begin
  have : f =·µê[Œº.restrict {x}] (Œª y, f x),
  { filter_upwards [ae_restrict_mem (measurable_set_singleton x)],
    assume a ha,
    simp only [mem_singleton_iff.1 ha] },
  rw [integrable_on, integrable_congr this, integrable_const_iff],
  simp,
end

@[simp] lemma integrable_on_finite_union {s : set Œ≤} (hs : finite s)
  {t : Œ≤ ‚Üí set Œ±} : integrable_on f (‚ãÉ i ‚àà s, t i) Œº ‚Üî ‚àÄ i ‚àà s, integrable_on f (t i) Œº :=
begin
  apply hs.induction_on,
  { simp },
  { intros a s ha hs hf, simp [hf, or_imp_distrib, forall_and_distrib] }
end

@[simp] lemma integrable_on_finset_union  {s : finset Œ≤} {t : Œ≤ ‚Üí set Œ±} :
  integrable_on f (‚ãÉ i ‚àà s, t i) Œº ‚Üî ‚àÄ i ‚àà s, integrable_on f (t i) Œº :=
integrable_on_finite_union s.finite_to_set

lemma integrable_on.add_measure (hŒº : integrable_on f s Œº) (hŒΩ : integrable_on f s ŒΩ) :
  integrable_on f s (Œº + ŒΩ) :=
by { delta integrable_on, rw measure.restrict_add, exact hŒº.integrable.add_measure hŒΩ }

@[simp] lemma integrable_on_add_measure :
  integrable_on f s (Œº + ŒΩ) ‚Üî integrable_on f s Œº ‚àß integrable_on f s ŒΩ :=
‚ü®Œª h, ‚ü®h.mono_measure (measure.le_add_right (le_refl _)),
  h.mono_measure (measure.le_add_left (le_refl _))‚ü©,
  Œª h, h.1.add_measure h.2‚ü©

lemma integrable_indicator_iff (hs : measurable_set s) :
  integrable (indicator s f) Œº ‚Üî integrable_on f s Œº :=
by simp [integrable_on, integrable, has_finite_integral, nnnorm_indicator_eq_indicator_nnnorm,
  ennreal.coe_indicator, lintegral_indicator _ hs, ae_measurable_indicator_iff hs]

lemma integrable_on.indicator (h : integrable_on f s Œº) (hs : measurable_set s) :
  integrable (indicator s f) Œº :=
(integrable_indicator_iff hs).2 h

lemma integrable.indicator (h : integrable f Œº) (hs : measurable_set s) :
  integrable (indicator s f) Œº :=
h.integrable_on.indicator hs

/-- We say that a function `f` is *integrable at filter* `l` if it is integrable on some
set `s ‚àà l`. Equivalently, it is eventually integrable on `s` in `l.lift' powerset`. -/
def integrable_at_filter (f : Œ± ‚Üí E) (l : filter Œ±) (Œº : measure Œ± . volume_tac) :=
‚àÉ s ‚àà l, integrable_on f s Œº

variables {l l' : filter Œ±}

protected lemma integrable_at_filter.eventually (h : integrable_at_filter f l Œº) :
  ‚àÄ·∂† s in l.lift' powerset, integrable_on f s Œº :=
by { refine (eventually_lift'_powerset' $ Œª s t hst ht, _).2 h, exact ht.mono_set hst }

lemma integrable_at_filter.filter_mono (hl : l ‚â§ l') (hl' : integrable_at_filter f l' Œº) :
  integrable_at_filter f l Œº :=
let ‚ü®s, hs, hsf‚ü© := hl' in ‚ü®s, hl hs, hsf‚ü©

lemma integrable_at_filter.inf_of_left (hl : integrable_at_filter f l Œº) :
  integrable_at_filter f (l ‚äì l') Œº :=
hl.filter_mono inf_le_left

lemma integrable_at_filter.inf_of_right (hl : integrable_at_filter f l Œº) :
  integrable_at_filter f (l' ‚äì l) Œº :=
hl.filter_mono inf_le_right

@[simp] lemma integrable_at_filter.inf_ae_iff {l : filter Œ±} :
  integrable_at_filter f (l ‚äì Œº.ae) Œº ‚Üî integrable_at_filter f l Œº :=
begin
  refine ‚ü®_, Œª h, h.filter_mono inf_le_left‚ü©,
  rintros ‚ü®s, ‚ü®t, ht, u, hu, hs‚ü©, hf‚ü©,
  refine ‚ü®t, ht, _‚ü©,
  refine hf.integrable.mono_measure (Œª v hv, _),
  simp only [measure.restrict_apply hv],
  refine measure_mono_ae (mem_sets_of_superset hu $ Œª x hx, _),
  exact Œª ‚ü®hv, ht‚ü©, ‚ü®hv, hs ‚ü®ht, hx‚ü©‚ü©
end

alias integrable_at_filter.inf_ae_iff ‚Üî measure_theory.integrable_at_filter.of_inf_ae _

/-- If `Œº` is a measure finite at filter `l` and `f` is a function such that its norm is bounded
above at `l`, then `f` is integrable at `l`. -/
lemma measure.finite_at_filter.integrable_at_filter {l : filter Œ±} [is_measurably_generated l]
  (hfm : measurable_at_filter f l Œº) (hŒº : Œº.finite_at_filter l)
  (hf : l.is_bounded_under (‚â§) (norm ‚àò f)) :
  integrable_at_filter f l Œº :=
begin
  obtain ‚ü®C, hC‚ü© : ‚àÉ C, ‚àÄ·∂† s in (l.lift' powerset), ‚àÄ x ‚àà s, ‚à•f x‚à• ‚â§ C,
    from hf.imp (Œª C hC, eventually_lift'_powerset.2 ‚ü®_, hC, Œª t, id‚ü©),
  rcases (hfm.eventually.and (hŒº.eventually.and hC)).exists_measurable_mem_of_lift'
    with ‚ü®s, hsl, hsm, hfm, hŒº, hC‚ü©,
  refine ‚ü®s, hsl, ‚ü®hfm, has_finite_integral_restrict_of_bounded hŒº _‚ü©‚ü©,
  exact C,
  rw [ae_restrict_eq hsm, eventually_inf_principal],
  exact eventually_of_forall hC
end

lemma measure.finite_at_filter.integrable_at_filter_of_tendsto_ae
  {l : filter Œ±} [is_measurably_generated l] (hfm : measurable_at_filter f l Œº)
  (hŒº : Œº.finite_at_filter l) {b} (hf : tendsto f (l ‚äì Œº.ae) (ùìù b)) :
  integrable_at_filter f l Œº :=
(hŒº.inf_of_left.integrable_at_filter (hfm.filter_mono inf_le_left)
  hf.norm.is_bounded_under_le).of_inf_ae

alias measure.finite_at_filter.integrable_at_filter_of_tendsto_ae ‚Üê
  filter.tendsto.integrable_at_filter_ae

lemma measure.finite_at_filter.integrable_at_filter_of_tendsto {l : filter Œ±}
  [is_measurably_generated l] (hfm : measurable_at_filter f l Œº) (hŒº : Œº.finite_at_filter l)
  {b} (hf : tendsto f l (ùìù b)) :
  integrable_at_filter f l Œº :=
hŒº.integrable_at_filter hfm hf.norm.is_bounded_under_le

alias measure.finite_at_filter.integrable_at_filter_of_tendsto ‚Üê filter.tendsto.integrable_at_filter

variables [borel_space E] [second_countable_topology E]

lemma integrable_add [opens_measurable_space E] {f g : Œ± ‚Üí E}
  (h : disjoint (support f) (support g)) (hf : measurable f) (hg : measurable g) :
  integrable (f + g) Œº ‚Üî integrable f Œº ‚àß integrable g Œº :=
begin
  refine ‚ü®Œª hfg, ‚ü®_, _‚ü©, Œª h, h.1.add h.2‚ü©,
  { rw ‚Üê indicator_add_eq_left h, exact hfg.indicator (measurable_set_support hf) },
  { rw ‚Üê indicator_add_eq_right h, exact hfg.indicator (measurable_set_support hg) }
end

end normed_group

end measure_theory

open measure_theory asymptotics metric

variables [measurable_space E] [normed_group E]

/-- If a function is integrable at `ùìù[s] x` for each point `x` of a compact set `s`, then it is
integrable on `s`. -/
lemma is_compact.integrable_on_of_nhds_within [topological_space Œ±] {Œº : measure Œ±} {s : set Œ±}
  (hs : is_compact s) {f : Œ± ‚Üí E} (hf : ‚àÄ x ‚àà s, integrable_at_filter f (ùìù[s] x) Œº) :
  integrable_on f s Œº :=
is_compact.induction_on hs integrable_on_empty (Œª s t hst ht, ht.mono_set hst)
  (Œª s t hs ht, hs.union ht) hf

/-- A function which is continuous on a set `s` is almost everywhere measurable with respect to
`Œº.restrict s`. -/
lemma continuous_on.ae_measurable [topological_space Œ±] [opens_measurable_space Œ±] [borel_space E]
  {f : Œ± ‚Üí E} {s : set Œ±} {Œº : measure Œ±} (hf : continuous_on f s) (hs : measurable_set s) :
  ae_measurable f (Œº.restrict s) :=
begin
  refine ‚ü®indicator s f, _, (indicator_ae_eq_restrict hs).symm‚ü©,
  apply measurable_of_is_open,
  assume t ht,
  obtain ‚ü®u, u_open, hu‚ü© : ‚àÉ (u : set Œ±), is_open u ‚àß f ‚Åª¬π' t ‚à© s = u ‚à© s :=
    _root_.continuous_on_iff'.1 hf t ht,
  rw [indicator_preimage, set.ite, hu],
  exact (u_open.measurable_set.inter hs).union ((measurable_zero ht.measurable_set).diff hs)
end

lemma continuous_on.integrable_at_nhds_within
  [topological_space Œ±] [opens_measurable_space Œ±] [borel_space E]
  {Œº : measure Œ±} [locally_finite_measure Œº] {a : Œ±} {t : set Œ±} {f : Œ± ‚Üí E}
  (hft : continuous_on f t) (ht : measurable_set t) (ha : a ‚àà t) :
  integrable_at_filter f (ùìù[t] a) Œº :=
by haveI : (ùìù[t] a).is_measurably_generated := ht.nhds_within_is_measurably_generated _;
exact (hft a ha).integrable_at_filter ‚ü®_, self_mem_nhds_within, hft.ae_measurable ht‚ü©
  (Œº.finite_at_nhds_within _ _)

/-- A function `f` continuous on a compact set `s` is integrable on this set with respect to any
locally finite measure. -/
lemma continuous_on.integrable_on_compact
  [topological_space Œ±] [opens_measurable_space Œ±] [borel_space E]
  [t2_space Œ±] {Œº : measure Œ±} [locally_finite_measure Œº]
  {s : set Œ±} (hs : is_compact s) {f : Œ± ‚Üí E} (hf : continuous_on f s) :
  integrable_on f s Œº :=
hs.integrable_on_of_nhds_within $ Œª x hx, hf.integrable_at_nhds_within hs.measurable_set hx

lemma continuous_on.integrable_on_Icc [borel_space E]
  [conditionally_complete_linear_order Œ≤] [topological_space Œ≤] [order_topology Œ≤]
  [measurable_space Œ≤] [opens_measurable_space Œ≤] {Œº : measure Œ≤} [locally_finite_measure Œº]
  {a b : Œ≤} {f : Œ≤ ‚Üí E} (hf : continuous_on f (Icc a b)) :
  integrable_on f (Icc a b) Œº :=
hf.integrable_on_compact is_compact_Icc

lemma continuous_on.integrable_on_interval [borel_space E]
  [conditionally_complete_linear_order Œ≤] [topological_space Œ≤] [order_topology Œ≤]
  [measurable_space Œ≤] [opens_measurable_space Œ≤] {Œº : measure Œ≤} [locally_finite_measure Œº]
  {a b : Œ≤} {f : Œ≤ ‚Üí E} (hf : continuous_on f (interval a b)) :
  integrable_on f (interval a b) Œº :=
hf.integrable_on_compact is_compact_interval

/-- A continuous function `f` is integrable on any compact set with respect to any locally finite
measure. -/
lemma continuous.integrable_on_compact
  [topological_space Œ±] [opens_measurable_space Œ±] [t2_space Œ±]
  [borel_space E] {Œº : measure Œ±} [locally_finite_measure Œº] {s : set Œ±}
  (hs : is_compact s) {f : Œ± ‚Üí E} (hf : continuous f) :
  integrable_on f s Œº :=
hf.continuous_on.integrable_on_compact hs

lemma continuous.integrable_on_Icc [borel_space E]
  [conditionally_complete_linear_order Œ≤] [topological_space Œ≤] [order_topology Œ≤]
  [measurable_space Œ≤] [opens_measurable_space Œ≤] {Œº : measure Œ≤} [locally_finite_measure Œº]
  {a b : Œ≤} {f : Œ≤ ‚Üí E} (hf : continuous f) :
  integrable_on f (Icc a b) Œº :=
hf.integrable_on_compact is_compact_Icc

lemma continuous.integrable_on_interval [borel_space E]
  [conditionally_complete_linear_order Œ≤] [topological_space Œ≤] [order_topology Œ≤]
  [measurable_space Œ≤] [opens_measurable_space Œ≤] {Œº : measure Œ≤} [locally_finite_measure Œº]
  {a b : Œ≤} {f : Œ≤ ‚Üí E} (hf : continuous f) :
  integrable_on f (interval a b) Œº :=
hf.integrable_on_compact is_compact_interval

/-- A continuous function with compact closure of the support is integrable on the whole space. -/
lemma continuous.integrable_of_compact_closure_support
  [topological_space Œ±] [opens_measurable_space Œ±] [t2_space Œ±] [borel_space E]
  {Œº : measure Œ±} [locally_finite_measure Œº] {f : Œ± ‚Üí E} (hf : continuous f)
  (hfc : is_compact (closure $ support f)) :
  integrable f Œº :=
begin
  rw [‚Üê indicator_eq_self.2 (@subset_closure _ _ (support f)),
    integrable_indicator_iff is_closed_closure.measurable_set],
  { exact hf.integrable_on_compact hfc },
  { apply_instance }
end

lemma measure_theory.integrable_on.mul_continuous_on
  [topological_space Œ±] [opens_measurable_space Œ±] [t2_space Œ±]
  {Œº : measure Œ±} {s : set Œ±} {f g : Œ± ‚Üí ‚Ñù}
  (hf : integrable_on f s Œº) (hg : continuous_on g s) (hs : is_compact s) :
  integrable_on (Œª x, f x * g x) s Œº :=
begin
  rcases is_compact.exists_bound_of_continuous_on hs hg with ‚ü®C, hC‚ü©,
  rw [integrable_on, ‚Üê mem_‚Ñíp_one_iff_integrable] at hf ‚ä¢,
  have : ‚àÄ·µê x ‚àÇ(Œº.restrict s), ‚à•f x * g x‚à• ‚â§ C * ‚à•f x‚à•,
  { filter_upwards [ae_restrict_mem hs.measurable_set],
    assume x hx,
    rw [real.norm_eq_abs, abs_mul, mul_comm, real.norm_eq_abs],
    apply mul_le_mul_of_nonneg_right (hC x hx) (abs_nonneg _) },
  exact mem_‚Ñíp.of_le_mul hf (hf.ae_measurable.mul (hg.ae_measurable hs.measurable_set)) this
end

lemma measure_theory.integrable_on.continuous_on_mul
  [topological_space Œ±] [opens_measurable_space Œ±] [t2_space Œ±]
  {Œº : measure Œ±} {s : set Œ±} {f g : Œ± ‚Üí ‚Ñù}
  (hf : integrable_on f s Œº) (hg : continuous_on g s) (hs : is_compact s) :
  integrable_on (Œª x, g x * f x) s Œº :=
by simpa [mul_comm] using hf.mul_continuous_on hg hs
