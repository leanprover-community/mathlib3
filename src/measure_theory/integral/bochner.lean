/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov, SÃ©bastien GouÃ«zel
-/
import measure_theory.function.simple_func_dense
import measure_theory.function.l1_space
import measure_theory.group.basic
import analysis.normed_space.bounded_linear_maps
import topology.sequences

/-!
# Bochner integral

The Bochner integral extends the definition of the Lebesgue integral to functions that map from a
measure space into a Banach space (complete normed vector space). It is constructed here by
extending the integral on simple functions.

## Main definitions

The Bochner integral is defined following these steps:

1. Define the integral on simple functions of the type `simple_func Î± E` (notation : `Î± â†’â‚› E`)
  where `E` is a real normed space.

  (See `simple_func.bintegral` and section `bintegral` for details. Also see `simple_func.integral`
  for the integral on simple functions of the type `simple_func Î± â„â‰¥0âˆ`.)

2. Transfer this definition to define the integral on `L1.simple_func Î± E` (notation :
  `Î± â†’â‚â‚›[Î¼] E`), see `L1.simple_func.integral`. Show that this integral is a continuous linear
  map from `Î± â†’â‚â‚›[Î¼] E` to `E`.

3. Define the Bochner integral on L1 functions by extending the integral on integrable simple
  functions `Î± â†’â‚â‚›[Î¼] E` using `continuous_linear_map.extend` and the fact that the embedding of
  `Î± â†’â‚â‚›[Î¼] E` into `Î± â†’â‚[Î¼] E` is dense.

4. Define the Bochner integral on functions as the Bochner integral of its equivalence class in L1
  space, if it is in L1, and 0 otherwise.

## Main statements

1. Basic properties of the Bochner integral on functions of type `Î± â†’ E`, where `Î±` is a measure
   space and `E` is a real normed space.

  * `integral_zero`                  : `âˆ« 0 âˆ‚Î¼ = 0`
  * `integral_add`                   : `âˆ« x, f x + g x âˆ‚Î¼ = âˆ« x, f âˆ‚Î¼ + âˆ« x, g x âˆ‚Î¼`
  * `integral_neg`                   : `âˆ« x, - f x âˆ‚Î¼ = - âˆ« x, f x âˆ‚Î¼`
  * `integral_sub`                   : `âˆ« x, f x - g x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ - âˆ« x, g x âˆ‚Î¼`
  * `integral_smul`                  : `âˆ« x, r â€¢ f x âˆ‚Î¼ = r â€¢ âˆ« x, f x âˆ‚Î¼`
  * `integral_congr_ae`              : `f =áµ[Î¼] g â†’ âˆ« x, f x âˆ‚Î¼ = âˆ« x, g x âˆ‚Î¼`
  * `norm_integral_le_integral_norm` : `âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ â‰¤ âˆ« x, âˆ¥f xâˆ¥ âˆ‚Î¼`

2. Basic properties of the Bochner integral on functions of type `Î± â†’ â„`, where `Î±` is a measure
  space.

  * `integral_nonneg_of_ae` : `0 â‰¤áµ[Î¼] f â†’ 0 â‰¤ âˆ« x, f x âˆ‚Î¼`
  * `integral_nonpos_of_ae` : `f â‰¤áµ[Î¼] 0 â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ 0`
  * `integral_mono_ae`      : `f â‰¤áµ[Î¼] g â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ âˆ« x, g x âˆ‚Î¼`
  * `integral_nonneg`       : `0 â‰¤ f â†’ 0 â‰¤ âˆ« x, f x âˆ‚Î¼`
  * `integral_nonpos`       : `f â‰¤ 0 â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ 0`
  * `integral_mono`         : `f â‰¤áµ[Î¼] g â†’ âˆ« x, f x âˆ‚Î¼ â‰¤ âˆ« x, g x âˆ‚Î¼`

3. Propositions connecting the Bochner integral with the integral on `â„â‰¥0âˆ`-valued functions,
   which is called `lintegral` and has the notation `âˆ«â»`.

  * `integral_eq_lintegral_max_sub_lintegral_min` : `âˆ« x, f x âˆ‚Î¼ = âˆ«â» x, fâº x âˆ‚Î¼ - âˆ«â» x, fâ» x âˆ‚Î¼`,
    where `fâº` is the positive part of `f` and `fâ»` is the negative part of `f`.
  * `integral_eq_lintegral_of_nonneg_ae`          : `0 â‰¤áµ[Î¼] f â†’ âˆ« x, f x âˆ‚Î¼ = âˆ«â» x, f x âˆ‚Î¼`

4. `tendsto_integral_of_dominated_convergence` : the Lebesgue dominated convergence theorem

5. (In the file `set_integral`) integration commutes with continuous linear maps.

  * `continuous_linear_map.integral_comp_comm`
  * `linear_isometry.integral_comp_comm`


## Notes

Some tips on how to prove a proposition if the API for the Bochner integral is not enough so that
you need to unfold the definition of the Bochner integral and go back to simple functions.

One method is to use the theorem `integrable.induction` in the file `simple_func_dense`, which
allows you to prove something for an arbitrary measurable + integrable function.

Another method is using the following steps.
See `integral_eq_lintegral_max_sub_lintegral_min` for a complicated example, which proves that
`âˆ« f = âˆ«â» fâº - âˆ«â» fâ»`, with the first integral sign being the Bochner integral of a real-valued
function `f : Î± â†’ â„`, and second and third integral sign being the integral on `â„â‰¥0âˆ`-valued
functions (called `lintegral`). The proof of `integral_eq_lintegral_max_sub_lintegral_min` is
scattered in sections with the name `pos_part`.

Here are the usual steps of proving that a property `p`, say `âˆ« f = âˆ«â» fâº - âˆ«â» fâ»`, holds for all
functions :

1. First go to the `LÂ¹` space.

   For example, if you see `ennreal.to_real (âˆ«â» a, ennreal.of_real $ âˆ¥f aâˆ¥)`, that is the norm of
   `f` in `LÂ¹` space. Rewrite using `L1.norm_of_fun_eq_lintegral_norm`.

2. Show that the set `{f âˆˆ LÂ¹ | âˆ« f = âˆ«â» fâº - âˆ«â» fâ»}` is closed in `LÂ¹` using `is_closed_eq`.

3. Show that the property holds for all simple functions `s` in `LÂ¹` space.

   Typically, you need to convert various notions to their `simple_func` counterpart, using lemmas
   like `L1.integral_coe_eq_integral`.

4. Since simple functions are dense in `LÂ¹`,
```
univ = closure {s simple}
     = closure {s simple | âˆ« s = âˆ«â» sâº - âˆ«â» sâ»} : the property holds for all simple functions
     âŠ† closure {f | âˆ« f = âˆ«â» fâº - âˆ«â» fâ»}
     = {f | âˆ« f = âˆ«â» fâº - âˆ«â» fâ»} : closure of a closed set is itself
```
Use `is_closed_property` or `dense_range.induction_on` for this argument.

## Notations

* `Î± â†’â‚› E`  : simple functions (defined in `measure_theory/integration`)
* `Î± â†’â‚[Î¼] E` : functions in L1 space, i.e., equivalence classes of integrable functions (defined in
                `measure_theory/lp_space`)
* `Î± â†’â‚â‚›[Î¼] E` : simple functions in L1 space, i.e., equivalence classes of integrable simple
                 functions (defined in `measure_theory/simple_func_dense`)
* `âˆ« a, f a âˆ‚Î¼` : integral of `f` with respect to a measure `Î¼`
* `âˆ« a, f a` : integral of `f` with respect to `volume`, the default measure on the
                    ambient type

We also define notations for integral on a set, which are described in the file
`measure_theory/set_integral`.

Note : `â‚›` is typed using `\_s`. Sometimes it shows as a box if font is missing.

## Tags

Bochner integral, simple function, function space, Lebesgue dominated convergence theorem

-/

noncomputable theory
open_locale classical topological_space big_operators nnreal ennreal measure_theory
open set filter topological_space ennreal emetric

local attribute [instance] fact_one_le_one_ennreal

namespace measure_theory

variables {Î± E F ğ•œ : Type*}

local infixr ` â†’â‚› `:25 := simple_func

namespace simple_func

section pos_part
variables [linear_order E] [has_zero E] [measurable_space Î±]

/-- Positive part of a simple function. -/
def pos_part (f : Î± â†’â‚› E) : Î± â†’â‚› E := f.map (Î»b, max b 0)

/-- Negative part of a simple function. -/
def neg_part [has_neg E] (f : Î± â†’â‚› E) : Î± â†’â‚› E := pos_part (-f)

lemma pos_part_map_norm (f : Î± â†’â‚› â„) : (pos_part f).map norm = pos_part f :=
begin
  ext,
  rw [map_apply, real.norm_eq_abs, abs_of_nonneg],
  rw [pos_part, map_apply],
  exact le_max_right _ _
end

lemma neg_part_map_norm (f : Î± â†’â‚› â„) : (neg_part f).map norm = neg_part f :=
by { rw neg_part, exact pos_part_map_norm _ }

lemma pos_part_sub_neg_part (f : Î± â†’â‚› â„) : f.pos_part - f.neg_part = f :=
begin
  simp only [pos_part, neg_part],
  ext a,
  rw coe_sub,
  exact max_zero_sub_eq_self (f a)
end

end pos_part

section integral
/-!
### The Bochner integral of simple functions

Define the Bochner integral of simple functions of the type `Î± â†’â‚› Î²` where `Î²` is a normed group,
and prove basic property of this integral.
-/
open finset

variables [normed_group E] [measurable_space E]
variables [normed_group F] [normed_space â„ F]
variables {m : measurable_space Î±} {Î¼ : measure Î±}

/-- Bochner integral of simple functions whose codomain is a real `normed_space`. -/
def integral {m : measurable_space Î±} (Î¼ : measure Î±) (f : Î± â†’â‚› F) : F :=
âˆ‘ x in f.range, (ennreal.to_real (Î¼ (f â»Â¹' {x}))) â€¢ x

lemma integral_eq_sum_filter {m : measurable_space Î±} (f : Î± â†’â‚› F) (Î¼ : measure Î±) :
  f.integral Î¼ = âˆ‘ x in f.range.filter (Î» x, x â‰  0), (ennreal.to_real (Î¼ (f â»Â¹' {x}))) â€¢ x :=
eq.symm $ sum_filter_of_ne $ Î» x _, mt $ Î» h0, h0.symm â–¸ smul_zero _

/-- The Bochner integral is equal to a sum over any set that includes `f.range` (except `0`). -/
lemma integral_eq_sum_of_subset {f : Î± â†’â‚› F} {s : finset F}
  (hs : f.range.filter (Î» x, x â‰  0) âŠ† s) : f.integral Î¼ = âˆ‘ x in s, (Î¼ (f â»Â¹' {x})).to_real â€¢ x :=
begin
  rw [simple_func.integral_eq_sum_filter, finset.sum_subset hs],
  rintro x - hx, rw [finset.mem_filter, not_and_distrib, ne.def, not_not] at hx,
  rcases hx with hx|rfl; [skip, simp],
  rw [simple_func.mem_range] at hx, rw [preimage_eq_empty]; simp [disjoint_singleton_left, hx]
end

/-- Calculate the integral of `g âˆ˜ f : Î± â†’â‚› F`, where `f` is an integrable function from `Î±` to `E`
    and `g` is a function from `E` to `F`. We require `g 0 = 0` so that `g âˆ˜ f` is integrable. -/
lemma map_integral (f : Î± â†’â‚› E) (g : E â†’ F) (hf : integrable f Î¼) (hg : g 0 = 0) :
  (f.map g).integral Î¼ = âˆ‘ x in f.range, (ennreal.to_real (Î¼ (f â»Â¹' {x}))) â€¢ (g x) :=
begin
  -- We start as in the proof of `map_lintegral`
  simp only [integral, range_map],
  refine finset.sum_image' _ (assume b hb, _),
  rcases mem_range.1 hb with âŸ¨a, rflâŸ©,
  rw [map_preimage_singleton, â† sum_measure_preimage_singleton _
    (Î» _ _, f.measurable_set_preimage _)],
  -- Now we use `hf : integrable f Î¼` to show that `ennreal.to_real` is additive.
  by_cases ha : g (f a) = 0,
  { simp only [ha, smul_zero],
    refine (sum_eq_zero $ Î» x hx, _).symm,
    simp only [mem_filter] at hx,
    simp [hx.2] },
  { rw [to_real_sum, sum_smul],
    { refine sum_congr rfl (Î» x hx, _),
      simp only [mem_filter] at hx,
      rw [hx.2] },
    { intros x hx,
      simp only [mem_filter] at hx,
      refine (integrable_iff_fin_meas_supp.1 hf).meas_preimage_singleton_ne_zero _,
      exact Î» h0, ha (hx.2 â–¸ h0.symm â–¸ hg) } },
end

/-- `simple_func.integral` and `simple_func.lintegral` agree when the integrand has type
    `Î± â†’â‚› â„â‰¥0âˆ`. But since `â„â‰¥0âˆ` is not a `normed_space`, we need some form of coercion.
    See `integral_eq_lintegral` for a simpler version. -/
lemma integral_eq_lintegral' {f : Î± â†’â‚› E} {g : E â†’ â„â‰¥0âˆ} (hf : integrable f Î¼) (hg0 : g 0 = 0)
  (hgt : âˆ€b, g b < âˆ):
  (f.map (ennreal.to_real âˆ˜ g)).integral Î¼ = ennreal.to_real (âˆ«â» a, g (f a) âˆ‚Î¼) :=
begin
  have hf' : f.fin_meas_supp Î¼ := integrable_iff_fin_meas_supp.1 hf,
  simp only [â† map_apply g f, lintegral_eq_lintegral],
  rw [map_integral f _ hf, map_lintegral, ennreal.to_real_sum],
  { refine finset.sum_congr rfl (Î»b hb, _),
    rw [smul_eq_mul, to_real_mul, mul_comm] },
  { assume a ha,
    by_cases a0 : a = 0,
    { rw [a0, hg0, zero_mul], exact with_top.zero_lt_top },
    { apply mul_lt_top (hgt a) (hf'.meas_preimage_singleton_ne_zero a0) } },
  { simp [hg0] }
end

variables [normed_field ğ•œ] [normed_space ğ•œ E] [normed_space â„ E]
  [smul_comm_class â„ ğ•œ E]

lemma integral_congr {f g : Î± â†’â‚› E} (hf : integrable f Î¼) (h : f =áµ[Î¼] g):
  f.integral Î¼ = g.integral Î¼ :=
show ((pair f g).map prod.fst).integral Î¼ = ((pair f g).map prod.snd).integral Î¼, from
begin
  have inte := integrable_pair hf (hf.congr h),
  rw [map_integral (pair f g) _ inte prod.fst_zero, map_integral (pair f g) _ inte prod.snd_zero],
  refine finset.sum_congr rfl (assume p hp, _),
  rcases mem_range.1 hp with âŸ¨a, rflâŸ©,
  by_cases eq : f a = g a,
  { dsimp only [pair_apply], rw eq },
  { have : Î¼ ((pair f g) â»Â¹' {(f a, g a)}) = 0,
    { refine measure_mono_null (assume a' ha', _) h,
      simp only [set.mem_preimage, mem_singleton_iff, pair_apply, prod.mk.inj_iff] at ha',
      show f a' â‰  g a',
      rwa [ha'.1, ha'.2] },
    simp only [this, pair_apply, zero_smul, ennreal.zero_to_real] },
end

/-- `simple_func.bintegral` and `simple_func.integral` agree when the integrand has type
    `Î± â†’â‚› â„â‰¥0âˆ`. But since `â„â‰¥0âˆ` is not a `normed_space`, we need some form of coercion. -/
lemma integral_eq_lintegral {f : Î± â†’â‚› â„} (hf : integrable f Î¼) (h_pos : 0 â‰¤áµ[Î¼] f) :
  f.integral Î¼ = ennreal.to_real (âˆ«â» a, ennreal.of_real (f a) âˆ‚Î¼) :=
begin
  have : f =áµ[Î¼] f.map (ennreal.to_real âˆ˜ ennreal.of_real) :=
    h_pos.mono (Î» a h, (ennreal.to_real_of_real h).symm),
  rw [â† integral_eq_lintegral' hf],
  { exact integral_congr hf this },
  { exact ennreal.of_real_zero },
  { assume b, rw ennreal.lt_top_iff_ne_top, exact ennreal.of_real_ne_top }
end

lemma integral_add {f g : Î± â†’â‚› E} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  integral Î¼ (f + g) = integral Î¼ f + integral Î¼ g :=
calc integral Î¼ (f + g) = âˆ‘ x in (pair f g).range,
       ennreal.to_real (Î¼ ((pair f g) â»Â¹' {x})) â€¢ (x.fst + x.snd) :
begin
  rw [add_eq_mapâ‚‚, map_integral (pair f g)],
  { exact integrable_pair hf hg },
  { simp only [add_zero, prod.fst_zero, prod.snd_zero] }
end
... = âˆ‘ x in (pair f g).range,
        (ennreal.to_real (Î¼ ((pair f g) â»Â¹' {x})) â€¢ x.fst +
         ennreal.to_real (Î¼ ((pair f g) â»Â¹' {x})) â€¢ x.snd) :
  finset.sum_congr rfl $ assume a ha, smul_add _ _ _
... = âˆ‘ x in (pair f g).range,
        ennreal.to_real (Î¼ ((pair f g) â»Â¹' {x})) â€¢ x.fst +
      âˆ‘ x in (pair f g).range,
        ennreal.to_real (Î¼ ((pair f g) â»Â¹' {x})) â€¢ x.snd :
  by rw finset.sum_add_distrib
... = ((pair f g).map prod.fst).integral Î¼ + ((pair f g).map prod.snd).integral Î¼ :
begin
  rw [map_integral (pair f g), map_integral (pair f g)],
  { exact integrable_pair hf hg }, { refl },
  { exact integrable_pair hf hg }, { refl }
end
... = integral Î¼ f + integral Î¼ g : rfl

lemma integral_neg {f : Î± â†’â‚› E} (hf : integrable f Î¼) : integral Î¼ (-f) = - integral Î¼ f :=
calc integral Î¼ (-f) = integral Î¼ (f.map (has_neg.neg)) : rfl
  ... = - integral Î¼ f :
  begin
    rw [map_integral f _ hf neg_zero, integral, â† sum_neg_distrib],
    refine finset.sum_congr rfl (Î»x h, smul_neg _ _),
  end

lemma integral_sub [borel_space E] {f g : Î± â†’â‚› E} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  integral Î¼ (f - g) = integral Î¼ f - integral Î¼ g :=
begin
  rw [sub_eq_add_neg, integral_add hf, integral_neg hg, sub_eq_add_neg],
  exact hg.neg
end

lemma integral_smul (c : ğ•œ) {f : Î± â†’â‚› E} (hf : integrable f Î¼) :
  integral Î¼ (c â€¢ f) = c â€¢ integral Î¼ f :=
calc integral Î¼ (c â€¢ f) = âˆ‘ x in f.range, ennreal.to_real (Î¼ (f â»Â¹' {x})) â€¢ c â€¢ x :
  by rw [smul_eq_map c f, map_integral f _ hf (smul_zero _)]
... = âˆ‘ x in f.range, c â€¢ (ennreal.to_real (Î¼ (f â»Â¹' {x}))) â€¢ x :
  finset.sum_congr rfl $ Î» b hb, by { exact smul_comm _ _ _}
... = c â€¢ integral Î¼ f :
by simp only [integral, smul_sum, smul_smul, mul_comm]

lemma norm_integral_le_integral_norm (f : Î± â†’â‚› E) (hf : integrable f Î¼) :
  âˆ¥f.integral Î¼âˆ¥ â‰¤ (f.map norm).integral Î¼ :=
begin
  rw [map_integral f norm hf norm_zero, integral],
  calc âˆ¥âˆ‘ x in f.range, ennreal.to_real (Î¼ (f â»Â¹' {x})) â€¢ xâˆ¥ â‰¤
       âˆ‘ x in f.range, âˆ¥ennreal.to_real (Î¼ (f â»Â¹' {x})) â€¢ xâˆ¥ :
    norm_sum_le _ _
    ... = âˆ‘ x in f.range, ennreal.to_real (Î¼ (f â»Â¹' {x})) â€¢ âˆ¥xâˆ¥ :
    begin
      refine finset.sum_congr rfl (Î»b hb, _),
      rw [norm_smul, smul_eq_mul, real.norm_eq_abs, abs_of_nonneg to_real_nonneg]
    end
end

lemma integral_add_measure {Î½} (f : Î± â†’â‚› E) (hf : integrable f (Î¼ + Î½)) :
  f.integral (Î¼ + Î½) = f.integral Î¼ + f.integral Î½ :=
begin
  simp only [integral_eq_sum_filter, â† sum_add_distrib, â† add_smul, measure.add_apply],
  refine sum_congr rfl (Î» x hx, _),
  rw [to_real_add];
    refine ne_of_lt ((integrable_iff_fin_meas_supp.1 _).meas_preimage_singleton_ne_zero
      (mem_filter.1 hx).2),
  exacts [hf.left_of_add_measure, hf.right_of_add_measure]
end

end integral

end simple_func

namespace L1

open ae_eq_fun Lp.simple_func Lp

variables
  [normed_group E] [second_countable_topology E] [measurable_space E] [borel_space E]
  [normed_group F] [second_countable_topology F] [measurable_space F] [borel_space F]
  {m : measurable_space Î±} {Î¼ : measure Î±}

variables {Î± E Î¼}

namespace simple_func

lemma norm_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) : âˆ¥fâˆ¥ = ((to_simple_func f).map norm).integral Î¼ :=
begin
  rw [norm_to_simple_func, simple_func.integral_eq_lintegral],
  { simp only [simple_func.map_apply, of_real_norm_eq_coe_nnnorm, snorm_one_eq_lintegral_nnnorm] },
  { exact (simple_func.integrable f).norm },
  { exact eventually_of_forall (Î» x, norm_nonneg _) }
end

section pos_part

/-- Positive part of a simple function in L1 space.  -/
def pos_part (f : Î± â†’â‚â‚›[Î¼] â„) : Î± â†’â‚â‚›[Î¼] â„ := âŸ¨Lp.pos_part (f : Î± â†’â‚[Î¼] â„),
begin
  rcases f with âŸ¨f, s, hsfâŸ©,
  use s.pos_part,
  simp only [subtype.coe_mk, Lp.coe_pos_part, â† hsf, ae_eq_fun.pos_part_mk, simple_func.pos_part,
    simple_func.coe_map]
end âŸ©

/-- Negative part of a simple function in L1 space. -/
def neg_part (f : Î± â†’â‚â‚›[Î¼] â„) : Î± â†’â‚â‚›[Î¼] â„ := pos_part (-f)

@[norm_cast]
lemma coe_pos_part (f : Î± â†’â‚â‚›[Î¼] â„) : (pos_part f : Î± â†’â‚[Î¼] â„) = Lp.pos_part (f : Î± â†’â‚[Î¼] â„) := rfl

@[norm_cast]
lemma coe_neg_part (f : Î± â†’â‚â‚›[Î¼] â„) : (neg_part f : Î± â†’â‚[Î¼] â„) = Lp.neg_part (f : Î± â†’â‚[Î¼] â„) := rfl

end pos_part

section simple_func_integral
/-!
### The Bochner integral of `L1`

Define the Bochner integral on `Î± â†’â‚â‚›[Î¼] E` by extension from the simple functions `Î± â†’â‚â‚›[Î¼] E`,
and prove basic properties of this integral. -/

variables [normed_field ğ•œ] [normed_space ğ•œ E] [normed_space â„ E] [smul_comm_class â„ ğ•œ E]

local attribute [instance] simple_func.normed_space

/-- The Bochner integral over simple functions in L1 space. -/
def integral (f : Î± â†’â‚â‚›[Î¼] E) : E := ((to_simple_func f)).integral Î¼

lemma integral_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) : integral f = ((to_simple_func f)).integral Î¼ := rfl

lemma integral_eq_lintegral {f : Î± â†’â‚â‚›[Î¼] â„} (h_pos : 0 â‰¤áµ[Î¼] (to_simple_func f)) :
  integral f = ennreal.to_real (âˆ«â» a, ennreal.of_real ((to_simple_func f) a) âˆ‚Î¼) :=
by rw [integral, simple_func.integral_eq_lintegral (simple_func.integrable f) h_pos]

lemma integral_congr {f g : Î± â†’â‚â‚›[Î¼] E} (h : to_simple_func f =áµ[Î¼] to_simple_func g) :
  integral f = integral g :=
simple_func.integral_congr (simple_func.integrable f) h

lemma integral_add (f g : Î± â†’â‚â‚›[Î¼] E) : integral (f + g) = integral f + integral g :=
begin
  simp only [integral],
  rw â† simple_func.integral_add (simple_func.integrable f) (simple_func.integrable g),
  apply measure_theory.simple_func.integral_congr (simple_func.integrable (f + g)),
  apply add_to_simple_func
end

lemma integral_smul [measurable_space ğ•œ] [opens_measurable_space ğ•œ] (c : ğ•œ) (f : Î± â†’â‚â‚›[Î¼] E) :
  integral (c â€¢ f) = c â€¢ integral f :=
begin
  simp only [integral],
  rw â† simple_func.integral_smul _ (simple_func.integrable f),
  apply measure_theory.simple_func.integral_congr (simple_func.integrable (c â€¢ f)),
  apply smul_to_simple_func,
  repeat { assumption },
end

lemma norm_integral_le_norm (f : Î± â†’â‚â‚›[Î¼] E) : âˆ¥integral fâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
begin
  rw [integral, norm_eq_integral],
  exact (to_simple_func f).norm_integral_le_integral_norm (simple_func.integrable f)
end

variables (Î± E Î¼ ğ•œ) [measurable_space ğ•œ] [opens_measurable_space ğ•œ]
/-- The Bochner integral over simple functions in L1 space as a continuous linear map. -/
def integral_clm' : (Î± â†’â‚â‚›[Î¼] E) â†’L[ğ•œ] E :=
linear_map.mk_continuous âŸ¨integral, integral_add, integral_smulâŸ©
  1 (Î»f, le_trans (norm_integral_le_norm _) $ by rw one_mul)

/-- The Bochner integral over simple functions in L1 space as a continuous linear map over â„. -/
def integral_clm : (Î± â†’â‚â‚›[Î¼] E) â†’L[â„] E := integral_clm' Î± E â„ Î¼

variables {Î± E Î¼ ğ•œ}

local notation `Integral` := integral_clm Î± E Î¼

open continuous_linear_map

lemma norm_Integral_le_one : âˆ¥Integralâˆ¥ â‰¤ 1 :=
linear_map.mk_continuous_norm_le _ (zero_le_one) _

section pos_part

lemma pos_part_to_simple_func (f : Î± â†’â‚â‚›[Î¼] â„) :
  to_simple_func (pos_part f) =áµ[Î¼] (to_simple_func f).pos_part :=
begin
  have eq : âˆ€ a, (to_simple_func f).pos_part a = max ((to_simple_func f) a) 0 := Î»a, rfl,
  have ae_eq : âˆ€áµ a âˆ‚Î¼, to_simple_func (pos_part f) a = max ((to_simple_func f) a) 0,
  { filter_upwards [to_simple_func_eq_to_fun (pos_part f), Lp.coe_fn_pos_part (f : Î± â†’â‚[Î¼] â„),
      to_simple_func_eq_to_fun f],
    assume a hâ‚ hâ‚‚ hâ‚ƒ,
    convert hâ‚‚ },
  refine ae_eq.mono (assume a h, _),
  rw [h, eq]
end

lemma neg_part_to_simple_func (f : Î± â†’â‚â‚›[Î¼] â„) :
  to_simple_func (neg_part f) =áµ[Î¼] (to_simple_func f).neg_part :=
begin
  rw [simple_func.neg_part, measure_theory.simple_func.neg_part],
  filter_upwards [pos_part_to_simple_func (-f), neg_to_simple_func f],
  assume a hâ‚ hâ‚‚,
  rw hâ‚,
  show max _ _ = max _ _,
  rw hâ‚‚,
  refl
end

lemma integral_eq_norm_pos_part_sub (f : Î± â†’â‚â‚›[Î¼] â„) :
  integral f = âˆ¥pos_part fâˆ¥ - âˆ¥neg_part fâˆ¥ :=
begin
  -- Convert things in `LÂ¹` to their `simple_func` counterpart
  have ae_eqâ‚ : (to_simple_func f).pos_part =áµ[Î¼] (to_simple_func (pos_part f)).map norm,
  { filter_upwards [pos_part_to_simple_func f],
    assume a h,
    rw [simple_func.map_apply, h],
    conv_lhs { rw [â† simple_func.pos_part_map_norm, simple_func.map_apply] } },
  -- Convert things in `LÂ¹` to their `simple_func` counterpart
  have ae_eqâ‚‚ : (to_simple_func f).neg_part =áµ[Î¼] (to_simple_func (neg_part f)).map norm,
  { filter_upwards [neg_part_to_simple_func f],
    assume a h,
    rw [simple_func.map_apply, h],
    conv_lhs { rw [â† simple_func.neg_part_map_norm, simple_func.map_apply] } },
  -- Convert things in `LÂ¹` to their `simple_func` counterpart
  have ae_eq : âˆ€áµ a âˆ‚Î¼, (to_simple_func f).pos_part a - (to_simple_func f).neg_part a =
     (to_simple_func (pos_part f)).map norm a -  (to_simple_func (neg_part f)).map norm a,
  { filter_upwards [ae_eqâ‚, ae_eqâ‚‚],
    assume a hâ‚ hâ‚‚,
    rw [hâ‚, hâ‚‚] },
  rw [integral, norm_eq_integral, norm_eq_integral, â† simple_func.integral_sub],
  { show (to_simple_func f).integral Î¼ =
      ((to_simple_func (pos_part f)).map norm - (to_simple_func (neg_part f)).map norm).integral Î¼,
    apply measure_theory.simple_func.integral_congr (simple_func.integrable f),
    filter_upwards [ae_eqâ‚, ae_eqâ‚‚],
    assume a hâ‚ hâ‚‚, show _ = _ - _,
    rw [â† hâ‚, â† hâ‚‚],
    have := (to_simple_func f).pos_part_sub_neg_part,
    conv_lhs {rw â† this},
    refl },
  { exact (simple_func.integrable f).max_zero.congr ae_eqâ‚ },
  { exact (simple_func.integrable f).neg.max_zero.congr ae_eqâ‚‚ }
end

end pos_part

end simple_func_integral

end simple_func

open simple_func
local notation `Integral` := @integral_clm Î± E _ _ _ _ _ Î¼ _


variables [normed_space â„ E] [nondiscrete_normed_field ğ•œ] [normed_space ğ•œ E]
  [smul_comm_class â„ ğ•œ E] [normed_space â„ F] [complete_space E]

section integration_in_L1

local attribute [instance] simple_func.normed_space

open continuous_linear_map

variables (ğ•œ) [measurable_space ğ•œ] [opens_measurable_space ğ•œ]
/-- The Bochner integral in L1 space as a continuous linear map. -/
def integral_clm' : (Î± â†’â‚[Î¼] E) â†’L[ğ•œ] E :=
(integral_clm' Î± E ğ•œ Î¼).extend
  (coe_to_Lp Î± E ğ•œ) (simple_func.dense_range one_ne_top) simple_func.uniform_inducing

variables {ğ•œ}

/-- The Bochner integral in L1 space as a continuous linear map over â„. -/
def integral_clm : (Î± â†’â‚[Î¼] E) â†’L[â„] E := integral_clm' â„

/-- The Bochner integral in L1 space -/
def integral (f : Î± â†’â‚[Î¼] E) : E := integral_clm f

lemma integral_eq (f : Î± â†’â‚[Î¼] E) : integral f = integral_clm f := rfl

@[norm_cast] lemma simple_func.integral_L1_eq_integral (f : Î± â†’â‚â‚›[Î¼] E) :
  integral (f : Î± â†’â‚[Î¼] E) = (simple_func.integral f) :=
uniformly_extend_of_ind simple_func.uniform_inducing (simple_func.dense_range one_ne_top)
  (simple_func.integral_clm Î± E Î¼).uniform_continuous _

variables (Î± E)
@[simp] lemma integral_zero : integral (0 : Î± â†’â‚[Î¼] E) = 0 :=
map_zero integral_clm
variables {Î± E}

lemma integral_add (f g : Î± â†’â‚[Î¼] E) : integral (f + g) = integral f + integral g :=
map_add integral_clm f g

lemma integral_neg (f : Î± â†’â‚[Î¼] E) : integral (-f) = - integral f :=
map_neg integral_clm f

lemma integral_sub (f g : Î± â†’â‚[Î¼] E) : integral (f - g) = integral f - integral g :=
map_sub integral_clm f g

lemma integral_smul (c : ğ•œ) (f : Î± â†’â‚[Î¼] E) : integral (c â€¢ f) = c â€¢ integral f :=
map_smul (integral_clm' ğ•œ) c f

local notation `Integral` := @integral_clm Î± E _ _ _ _ _ Î¼ _ _
local notation `sIntegral` := @simple_func.integral_clm Î± E _ _ _ _ _ Î¼ _

lemma norm_Integral_le_one : âˆ¥Integralâˆ¥ â‰¤ 1 :=
calc âˆ¥Integralâˆ¥ â‰¤ (1 : â„â‰¥0) * âˆ¥sIntegralâˆ¥ :
  op_norm_extend_le _ _ _ $ Î»s, by {rw [nnreal.coe_one, one_mul], refl}
  ... = âˆ¥sIntegralâˆ¥ : one_mul _
  ... â‰¤ 1 : norm_Integral_le_one

lemma norm_integral_le (f : Î± â†’â‚[Î¼] E) : âˆ¥integral fâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
calc âˆ¥integral fâˆ¥ = âˆ¥Integral fâˆ¥ : rfl
  ... â‰¤ âˆ¥Integralâˆ¥ * âˆ¥fâˆ¥ : le_op_norm _ _
  ... â‰¤ 1 * âˆ¥fâˆ¥ : mul_le_mul_of_nonneg_right norm_Integral_le_one $ norm_nonneg _
  ... = âˆ¥fâˆ¥ : one_mul _

@[continuity]
lemma continuous_integral : continuous (Î» (f : Î± â†’â‚[Î¼] E), integral f) :=
by simp [L1.integral, L1.integral_clm.continuous]

section pos_part

local attribute [instance] fact_one_le_one_ennreal

lemma integral_eq_norm_pos_part_sub (f : Î± â†’â‚[Î¼] â„) :
  integral f = âˆ¥Lp.pos_part fâˆ¥ - âˆ¥Lp.neg_part fâˆ¥ :=
begin
  -- Use `is_closed_property` and `is_closed_eq`
  refine @is_closed_property _ _ _ (coe : (Î± â†’â‚â‚›[Î¼] â„) â†’ (Î± â†’â‚[Î¼] â„))
    (Î» f : Î± â†’â‚[Î¼] â„, integral f = âˆ¥Lp.pos_part fâˆ¥ - âˆ¥Lp.neg_part fâˆ¥)
    (simple_func.dense_range one_ne_top) (is_closed_eq _ _) _ f,
  { exact cont _ },
  { refine continuous.sub (continuous_norm.comp Lp.continuous_pos_part)
      (continuous_norm.comp Lp.continuous_neg_part) },
  -- Show that the property holds for all simple functions in the `LÂ¹` space.
  { assume s,
    norm_cast,
    rw [â† coe_norm_subgroup, â† coe_norm_subgroup],
    exact simple_func.integral_eq_norm_pos_part_sub _ }
end

end pos_part

end integration_in_L1

end L1

/-!
### The Bochner integral on functions

Define the Bochner integral on functions generally to be the `L1` Bochner integral, for integrable
functions, and 0 otherwise; prove its basic properties.

-/

variables [normed_group E] [second_countable_topology E] [normed_space â„ E] [complete_space E]
  [measurable_space E] [borel_space E]
          [nondiscrete_normed_field ğ•œ] [normed_space ğ•œ E] [smul_comm_class â„ ğ•œ E]
          [normed_group F] [second_countable_topology F] [normed_space â„ F] [complete_space F]
  [measurable_space F] [borel_space F]

/-- The Bochner integral -/
def integral {m : measurable_space Î±} (Î¼ : measure Î±) (f : Î± â†’ E) : E :=
if hf : integrable f Î¼ then L1.integral (hf.to_L1 f) else 0

/-! In the notation for integrals, an expression like `âˆ« x, g âˆ¥xâˆ¥ âˆ‚Î¼` will not be parsed correctly,
  and needs parentheses. We do not set the binding power of `r` to `0`, because then
  `âˆ« x, f x = 0` will be parsed incorrectly. -/
notation `âˆ«` binders `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := integral Î¼ r
notation `âˆ«` binders `, ` r:(scoped:60 f, integral volume f) := r
notation `âˆ«` binders ` in ` s `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := integral (measure.restrict Î¼ s) r
notation `âˆ«` binders ` in ` s `, ` r:(scoped:60 f, integral (measure.restrict volume s) f) := r

section properties

open continuous_linear_map measure_theory.simple_func

variables {f g : Î± â†’ E} {m : measurable_space Î±} {Î¼ : measure Î±}

lemma integral_eq (f : Î± â†’ E) (hf : integrable f Î¼) :
  âˆ« a, f a âˆ‚Î¼ = L1.integral (hf.to_L1 f) :=
dif_pos hf

lemma L1.integral_eq_integral (f : Î± â†’â‚[Î¼] E) : L1.integral f = âˆ« a, f a âˆ‚Î¼ :=
by rw [integral_eq _ (L1.integrable_coe_fn f), integrable.to_L1_coe_fn]

lemma integral_undef (h : Â¬ integrable f Î¼) : âˆ« a, f a âˆ‚Î¼ = 0 :=
dif_neg h

lemma integral_non_ae_measurable (h : Â¬ ae_measurable f Î¼) : âˆ« a, f a âˆ‚Î¼ = 0 :=
integral_undef $ not_and_of_not_left _ h

variables (Î± E)

lemma integral_zero : âˆ« a : Î±, (0:E) âˆ‚Î¼ = 0 :=
by { rw [integral_eq _ (integrable_zero Î± E Î¼)], exact L1.integral_zero _ _ }

@[simp] lemma integral_zero' : integral Î¼ (0 : Î± â†’ E) = 0 :=
integral_zero Î± E

variables {Î± E}

lemma integral_add (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ« a, f a + g a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ + âˆ« a, g a âˆ‚Î¼ :=
begin
  rw [integral_eq, integral_eq f hf, integral_eq g hg, â† L1.integral_add],
  { refl },
  { exact hf.add hg }
end

lemma integral_add' (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ« a, (f + g) a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ + âˆ« a, g a âˆ‚Î¼ :=
integral_add hf hg

lemma integral_neg (f : Î± â†’ E) : âˆ« a, -f a âˆ‚Î¼ = - âˆ« a, f a âˆ‚Î¼ :=
begin
  by_cases hf : integrable f Î¼,
  { rw [integral_eq f hf, integral_eq (Î»a, - f a) hf.neg, â† L1.integral_neg],
    refl },
  { rw [integral_undef hf, integral_undef, neg_zero], rwa [â† integrable_neg_iff] at hf }
end

lemma integral_neg' (f : Î± â†’ E) : âˆ« a, (-f) a âˆ‚Î¼ = - âˆ« a, f a âˆ‚Î¼ :=
integral_neg f

lemma integral_sub (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ« a, f a - g a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ - âˆ« a, g a âˆ‚Î¼ :=
by { simp only [sub_eq_add_neg, â† integral_neg], exact integral_add hf hg.neg }

lemma integral_sub' (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ« a, (f - g) a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ - âˆ« a, g a âˆ‚Î¼ :=
integral_sub hf hg

lemma integral_smul [measurable_space ğ•œ] [opens_measurable_space ğ•œ] (c : ğ•œ) (f : Î± â†’ E) :
  âˆ« a, c â€¢ (f a) âˆ‚Î¼ = c â€¢ âˆ« a, f a âˆ‚Î¼ :=
begin
  by_cases hf : integrable f Î¼,
  { rw [integral_eq f hf, integral_eq (Î»a, c â€¢ (f a)), integrable.to_L1_smul, L1.integral_smul], },
  { by_cases hr : c = 0,
    { simp only [hr, measure_theory.integral_zero, zero_smul] },
    have hf' : Â¬ integrable (Î» x, c â€¢ f x) Î¼,
    { change Â¬ integrable (c â€¢ f) Î¼, rwa [integrable_smul_iff hr f] },
    rw [integral_undef hf, integral_undef hf', smul_zero] }
end

lemma integral_mul_left (r : â„) (f : Î± â†’ â„) : âˆ« a, r * (f a) âˆ‚Î¼ = r * âˆ« a, f a âˆ‚Î¼ :=
integral_smul r f

lemma integral_mul_right (r : â„) (f : Î± â†’ â„) : âˆ« a, (f a) * r âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ * r :=
by { simp only [mul_comm], exact integral_mul_left r f }

lemma integral_div (r : â„) (f : Î± â†’ â„) : âˆ« a, (f a) / r âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ / r :=
integral_mul_right râ»Â¹ f

lemma integral_congr_ae (h : f =áµ[Î¼] g) : âˆ« a, f a âˆ‚Î¼ = âˆ« a, g a âˆ‚Î¼ :=
begin
  by_cases hfi : integrable f Î¼,
  { have hgi : integrable g Î¼ := hfi.congr h,
    rw [integral_eq f hfi, integral_eq g hgi, (integrable.to_L1_eq_to_L1_iff f g hfi hgi).2 h] },
  { have hgi : Â¬ integrable g Î¼, { rw integrable_congr h at hfi, exact hfi },
    rw [integral_undef hfi, integral_undef hgi] },
end

@[simp] lemma L1.integral_of_fun_eq_integral {f : Î± â†’ E} (hf : integrable f Î¼) :
  âˆ« a, (hf.to_L1 f) a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ :=
integral_congr_ae $ by simp [integrable.coe_fn_to_L1]

@[continuity]
lemma continuous_integral : continuous (Î» (f : Î± â†’â‚[Î¼] E), âˆ« a, f a âˆ‚Î¼) :=
by { simp only [â† L1.integral_eq_integral], exact L1.continuous_integral }

lemma norm_integral_le_lintegral_norm (f : Î± â†’ E) :
  âˆ¥âˆ« a, f a âˆ‚Î¼âˆ¥ â‰¤ ennreal.to_real (âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼) :=
begin
  by_cases hf : integrable f Î¼,
  { rw [integral_eq f hf, â† integrable.norm_to_L1_eq_lintegral_norm f hf],
    exact L1.norm_integral_le _ },
  { rw [integral_undef hf, norm_zero], exact to_real_nonneg }
end

lemma ennnorm_integral_le_lintegral_ennnorm (f : Î± â†’ E) :
  (nnnorm (âˆ« a, f a âˆ‚Î¼) : â„â‰¥0âˆ) â‰¤ âˆ«â» a, (nnnorm (f a)) âˆ‚Î¼ :=
by { simp_rw [â† of_real_norm_eq_coe_nnnorm], apply ennreal.of_real_le_of_le_to_real,
  exact norm_integral_le_lintegral_norm f }

lemma integral_eq_zero_of_ae {f : Î± â†’ E} (hf : f =áµ[Î¼] 0) : âˆ« a, f a âˆ‚Î¼ = 0 :=
by simp [integral_congr_ae hf, integral_zero]

/-- If `f` has finite integral, then `âˆ« x in s, f x âˆ‚Î¼` is absolutely continuous in `s`: it tends
to zero as `Î¼ s` tends to zero. -/
lemma has_finite_integral.tendsto_set_integral_nhds_zero {Î¹} {f : Î± â†’ E}
  (hf : has_finite_integral f Î¼) {l : filter Î¹} {s : Î¹ â†’ set Î±}
  (hs : tendsto (Î¼ âˆ˜ s) l (ğ“ 0)) :
  tendsto (Î» i, âˆ« x in s i, f x âˆ‚Î¼) l (ğ“ 0) :=
begin
  rw [tendsto_zero_iff_norm_tendsto_zero],
  simp_rw [â† coe_nnnorm, â† nnreal.coe_zero, nnreal.tendsto_coe, â† ennreal.tendsto_coe,
    ennreal.coe_zero],
  exact tendsto_of_tendsto_of_tendsto_of_le_of_le tendsto_const_nhds
    (tendsto_set_lintegral_zero hf hs) (Î» i, zero_le _)
    (Î» i, ennnorm_integral_le_lintegral_ennnorm _)
end

/-- If `f` is integrable, then `âˆ« x in s, f x âˆ‚Î¼` is absolutely continuous in `s`: it tends
to zero as `Î¼ s` tends to zero. -/
lemma integrable.tendsto_set_integral_nhds_zero {Î¹} {f : Î± â†’ E}
  (hf : integrable f Î¼) {l : filter Î¹} {s : Î¹ â†’ set Î±} (hs : tendsto (Î¼ âˆ˜ s) l (ğ“ 0)) :
  tendsto (Î» i, âˆ« x in s i, f x âˆ‚Î¼) l (ğ“ 0) :=
hf.2.tendsto_set_integral_nhds_zero hs

/-- If `F i â†’ f` in `L1`, then `âˆ« x, F i x âˆ‚Î¼ â†’ âˆ« x, f xâˆ‚Î¼`. -/
lemma tendsto_integral_of_L1 {Î¹} (f : Î± â†’ E) (hfi : integrable f Î¼)
  {F : Î¹ â†’ Î± â†’ E} {l : filter Î¹} (hFi : âˆ€á¶  i in l, integrable (F i) Î¼)
  (hF : tendsto (Î» i, âˆ«â» x, âˆ¥F i x - f xâˆ¥â‚Š âˆ‚Î¼) l (ğ“ 0)) :
  tendsto (Î» i, âˆ« x, F i x âˆ‚Î¼) l (ğ“ $ âˆ« x, f x âˆ‚Î¼) :=
begin
  rw [tendsto_iff_norm_tendsto_zero],
  replace hF : tendsto (Î» i, ennreal.to_real $ âˆ«â» x, âˆ¥F i x - f xâˆ¥â‚Š âˆ‚Î¼) l (ğ“ 0) :=
    (ennreal.tendsto_to_real zero_ne_top).comp hF,
  refine squeeze_zero_norm' (hFi.mp $ hFi.mono $ Î» i hFi hFm, _) hF,
  simp only [norm_norm, â† integral_sub hFi hfi],
  convert norm_integral_le_lintegral_norm (Î» x, F i x - f x),
  ext1 x,
  exact coe_nnreal_eq _
end

/-- Lebesgue dominated convergence theorem provides sufficient conditions under which almost
  everywhere convergence of a sequence of functions implies the convergence of their integrals. -/
theorem tendsto_integral_of_dominated_convergence {F : â„• â†’ Î± â†’ E} {f : Î± â†’ E} (bound : Î± â†’ â„)
  (F_measurable : âˆ€ n, ae_measurable (F n) Î¼)
  (f_measurable : ae_measurable f Î¼)
  (bound_integrable : integrable bound Î¼)
  (h_bound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) at_top (ğ“ (f a))) :
  tendsto (Î»n, âˆ« a, F n a âˆ‚Î¼) at_top (ğ“ $ âˆ« a, f a âˆ‚Î¼) :=
begin
  /- To show `(âˆ« a, F n a) --> (âˆ« f)`, suffices to show `âˆ¥âˆ« a, F n a - âˆ« fâˆ¥ --> 0` -/
  rw tendsto_iff_norm_tendsto_zero,
  /- But `0 â‰¤ âˆ¥âˆ« a, F n a - âˆ« fâˆ¥ = âˆ¥âˆ« a, (F n a - f a) âˆ¥ â‰¤ âˆ« a, âˆ¥F n a - f aâˆ¥, and thus we apply the
    sandwich theorem and prove that `âˆ« a, âˆ¥F n a - f aâˆ¥ --> 0` -/
  have lintegral_norm_tendsto_zero :
    tendsto (Î»n, ennreal.to_real $ âˆ«â» a, (ennreal.of_real âˆ¥F n a - f aâˆ¥) âˆ‚Î¼) at_top (ğ“ 0) :=
  (tendsto_to_real zero_ne_top).comp
    (tendsto_lintegral_norm_of_dominated_convergence
      F_measurable f_measurable bound_integrable.has_finite_integral h_bound h_lim),
  -- Use the sandwich theorem
  refine squeeze_zero (Î» n, norm_nonneg _) _ lintegral_norm_tendsto_zero,
  -- Show `âˆ¥âˆ« a, F n a - âˆ« fâˆ¥ â‰¤ âˆ« a, âˆ¥F n a - f aâˆ¥` for all `n`
  { assume n,
    have hâ‚ : integrable (F n) Î¼ := bound_integrable.mono' (F_measurable n) (h_bound _),
    have hâ‚‚ : integrable f Î¼ :=
    âŸ¨f_measurable, has_finite_integral_of_dominated_convergence
      bound_integrable.has_finite_integral h_bound h_limâŸ©,
    rw â† integral_sub hâ‚ hâ‚‚,
    exact norm_integral_le_lintegral_norm _ }
end

/-- Lebesgue dominated convergence theorem for filters with a countable basis -/
lemma tendsto_integral_filter_of_dominated_convergence {Î¹} {l : filter Î¹}
  {F : Î¹ â†’ Î± â†’ E} {f : Î± â†’ E} (bound : Î± â†’ â„)
  (hl_cb : l.is_countably_generated)
  (hF_meas : âˆ€á¶  n in l, ae_measurable (F n) Î¼)
  (f_measurable : ae_measurable f Î¼)
  (h_bound : âˆ€á¶  n in l, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼)
  (h_lim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» n, F n a) l (ğ“ (f a))) :
  tendsto (Î»n, âˆ« a, F n a âˆ‚Î¼) l (ğ“ $ âˆ« a, f a âˆ‚Î¼) :=
begin
  rw hl_cb.tendsto_iff_seq_tendsto,
  { intros x xl,
    have hxl, { rw tendsto_at_top' at xl, exact xl },
    have h := inter_mem hF_meas h_bound,
    replace h := hxl _ h,
    rcases h with âŸ¨k, hâŸ©,
    rw â† tendsto_add_at_top_iff_nat k,
    refine tendsto_integral_of_dominated_convergence _ _ _ _ _ _,
    { exact bound },
    { intro, refine (h _ _).1, exact nat.le_add_left _ _ },
    { assumption },
    { assumption },
    { intro, refine (h _ _).2, exact nat.le_add_left _ _ },
    { filter_upwards [h_lim],
      assume a h_lim,
      apply @tendsto.comp _ _ _ (Î»n, x (n + k)) (Î»n, F n a),
      { assumption },
      rw tendsto_add_at_top_iff_nat,
      assumption } },
end

variables {X : Type*} [topological_space X] [first_countable_topology X]

lemma continuous_at_of_dominated {F : X â†’ Î± â†’ E} {xâ‚€ : X} {bound : Î± â†’ â„}
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) Î¼)
  (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ a âˆ‚Î¼, âˆ¥F x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼) (h_cont : âˆ€áµ a âˆ‚Î¼, continuous_at (Î» x, F x a) xâ‚€) :
  continuous_at (Î» x, âˆ« a, F x a âˆ‚Î¼) xâ‚€ :=
tendsto_integral_filter_of_dominated_convergence bound
  (first_countable_topology.nhds_generated_countable xâ‚€) â€¹_â€º
    (mem_of_mem_nhds hF_meas : _) â€¹_â€º â€¹_â€º â€¹_â€º

lemma continuous_of_dominated {F : X â†’ Î± â†’ E} {bound : Î± â†’ â„}
  (hF_meas : âˆ€ x, ae_measurable (F x) Î¼) (h_bound : âˆ€ x, âˆ€áµ a âˆ‚Î¼, âˆ¥F x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼) (h_cont : âˆ€áµ a âˆ‚Î¼, continuous (Î» x, F x a)) :
  continuous (Î» x, âˆ« a, F x a âˆ‚Î¼) :=
continuous_iff_continuous_at.mpr (Î» xâ‚€, continuous_at_of_dominated (eventually_of_forall hF_meas)
  (eventually_of_forall h_bound) â€¹_â€º $ h_cont.mono $ Î» _, continuous.continuous_at)

/-- The Bochner integral of a real-valued function `f : Î± â†’ â„` is the difference between the
  integral of the positive part of `f` and the integral of the negative part of `f`.  -/
lemma integral_eq_lintegral_pos_part_sub_lintegral_neg_part {f : Î± â†’ â„} (hf : integrable f Î¼) :
  âˆ« a, f a âˆ‚Î¼ =
  ennreal.to_real (âˆ«â» a, (ennreal.of_real $ f a) âˆ‚Î¼) -
  ennreal.to_real (âˆ«â» a, (ennreal.of_real $ - f a) âˆ‚Î¼) :=
let fâ‚ := hf.to_L1 f in
-- Go to the `LÂ¹` space
have eqâ‚ : ennreal.to_real (âˆ«â» a, (ennreal.of_real $ f a) âˆ‚Î¼) = âˆ¥Lp.pos_part fâ‚âˆ¥ :=
begin
  rw L1.norm_def,
  congr' 1,
  apply lintegral_congr_ae,
  filter_upwards [Lp.coe_fn_pos_part fâ‚, hf.coe_fn_to_L1],
  assume a hâ‚ hâ‚‚,
  rw [hâ‚, hâ‚‚, ennreal.of_real],
  congr' 1,
  apply nnreal.eq,
  simp [real.norm_of_nonneg, le_max_right, real.coe_to_nnreal]
end,
-- Go to the `LÂ¹` space
have eqâ‚‚ : ennreal.to_real (âˆ«â» a, (ennreal.of_real $ - f a) âˆ‚Î¼)  = âˆ¥Lp.neg_part fâ‚âˆ¥ :=
begin
  rw L1.norm_def,
  congr' 1,
  apply lintegral_congr_ae,
  filter_upwards [Lp.coe_fn_neg_part fâ‚, hf.coe_fn_to_L1],
  assume a hâ‚ hâ‚‚,
  rw [hâ‚, hâ‚‚, ennreal.of_real],
  congr' 1,
  apply nnreal.eq,
  simp only [real.norm_of_nonneg, min_le_right, neg_nonneg, real.coe_to_nnreal', subtype.coe_mk],
  rw [â† max_neg_neg, coe_nnnorm, neg_zero, real.norm_of_nonneg (le_max_right (-f a) 0)]
end,
begin
  rw [eqâ‚, eqâ‚‚, integral, dif_pos],
  exact L1.integral_eq_norm_pos_part_sub _
end

lemma integral_eq_lintegral_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) (hfm : ae_measurable f Î¼) :
  âˆ« a, f a âˆ‚Î¼ = ennreal.to_real (âˆ«â» a, (ennreal.of_real $ f a) âˆ‚Î¼) :=
begin
  by_cases hfi : integrable f Î¼,
  { rw integral_eq_lintegral_pos_part_sub_lintegral_neg_part hfi,
    have h_min : âˆ«â» a, ennreal.of_real (-f a) âˆ‚Î¼ = 0,
    { rw lintegral_eq_zero_iff',
      { refine hf.mono _,
        simp only [pi.zero_apply],
        assume a h,
        simp only [h, neg_nonpos, of_real_eq_zero], },
      { exact measurable_of_real.comp_ae_measurable hfm.neg } },
    rw [h_min, zero_to_real, _root_.sub_zero] },
  { rw integral_undef hfi,
    simp_rw [integrable, hfm, has_finite_integral_iff_norm, lt_top_iff_ne_top, ne.def, true_and,
      not_not] at hfi,
    have : âˆ«â» (a : Î±), ennreal.of_real (f a) âˆ‚Î¼ = âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼,
    { refine lintegral_congr_ae (hf.mono $ assume a h, _),
      rw [real.norm_eq_abs, abs_of_nonneg h] },
    rw [this, hfi], refl }
end

lemma of_real_integral_norm_eq_lintegral_nnnorm {G} [normed_group G] [measurable_space G]
  [opens_measurable_space G] {f : Î± â†’ G} (hf : integrable f Î¼) :
  ennreal.of_real âˆ« x, âˆ¥f xâˆ¥ âˆ‚Î¼ = âˆ«â» x, âˆ¥f xâˆ¥â‚Š âˆ‚Î¼ :=
begin
  rw integral_eq_lintegral_of_nonneg_ae _ hf.1.norm,
  { simp_rw [of_real_norm_eq_coe_nnnorm, ennreal.of_real_to_real (lt_top_iff_ne_top.mp hf.2)], },
  { refine ae_of_all _ _, simp, },
end

lemma integral_eq_integral_pos_part_sub_integral_neg_part {f : Î± â†’ â„} (hf : integrable f Î¼) :
  âˆ« a, f a âˆ‚Î¼ = (âˆ« a, real.to_nnreal (f a) âˆ‚Î¼) - (âˆ« a, real.to_nnreal (-f a) âˆ‚Î¼) :=
begin
  rw [â† integral_sub hf.real_to_nnreal],
  { simp },
  { exact hf.neg.real_to_nnreal }
end

lemma integral_nonneg_of_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤ âˆ« a, f a âˆ‚Î¼ :=
begin
  by_cases hfm : ae_measurable f Î¼,
  { rw integral_eq_lintegral_of_nonneg_ae hf hfm, exact to_real_nonneg },
  { rw integral_non_ae_measurable hfm }
end

lemma lintegral_coe_eq_integral (f : Î± â†’ â„â‰¥0) (hfi : integrable (Î» x, (f x : â„)) Î¼) :
  âˆ«â» a, f a âˆ‚Î¼ = ennreal.of_real âˆ« a, f a âˆ‚Î¼ :=
begin
  simp_rw [integral_eq_lintegral_of_nonneg_ae (eventually_of_forall (Î» x, (f x).coe_nonneg))
    hfi.ae_measurable, â† ennreal.coe_nnreal_eq], rw [ennreal.of_real_to_real],
  rw [â† lt_top_iff_ne_top], convert hfi.has_finite_integral, ext1 x, rw [nnreal.nnnorm_eq]
end

lemma integral_to_real {f : Î± â†’ â„â‰¥0âˆ} (hfm : ae_measurable f Î¼) (hf : âˆ€áµ x âˆ‚Î¼, f x < âˆ) :
  âˆ« a, (f a).to_real âˆ‚Î¼ = (âˆ«â» a, f a âˆ‚Î¼).to_real :=
begin
  rw [integral_eq_lintegral_of_nonneg_ae _ hfm.ennreal_to_real],
  { rw lintegral_congr_ae, refine hf.mp (eventually_of_forall _),
    intros x hx, rw [lt_top_iff_ne_top] at hx, simp [hx] },
  { exact (eventually_of_forall $ Î» x, ennreal.to_real_nonneg) }
end

lemma integral_nonneg {f : Î± â†’ â„} (hf : 0 â‰¤ f) : 0 â‰¤ âˆ« a, f a âˆ‚Î¼ :=
integral_nonneg_of_ae $ eventually_of_forall hf

lemma integral_nonpos_of_ae {f : Î± â†’ â„} (hf : f â‰¤áµ[Î¼] 0) : âˆ« a, f a âˆ‚Î¼ â‰¤ 0 :=
begin
  have hf : 0 â‰¤áµ[Î¼] (-f) := hf.mono (assume a h, by rwa [pi.neg_apply, pi.zero_apply, neg_nonneg]),
  have : 0 â‰¤ âˆ« a, -f a âˆ‚Î¼ := integral_nonneg_of_ae hf,
  rwa [integral_neg, neg_nonneg] at this,
end

lemma integral_nonpos {f : Î± â†’ â„} (hf : f â‰¤ 0) : âˆ« a, f a âˆ‚Î¼ â‰¤ 0 :=
integral_nonpos_of_ae $ eventually_of_forall hf

lemma integral_eq_zero_iff_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) (hfi : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼] 0 :=
by simp_rw [integral_eq_lintegral_of_nonneg_ae hf hfi.1, ennreal.to_real_eq_zero_iff,
  lintegral_eq_zero_iff' (ennreal.measurable_of_real.comp_ae_measurable hfi.1),
  â† ennreal.not_lt_top, â† has_finite_integral_iff_of_real hf, hfi.2, not_true, or_false,
  â† hf.le_iff_eq, filter.eventually_eq, filter.eventually_le, (âˆ˜), pi.zero_apply,
  ennreal.of_real_eq_zero]

lemma integral_eq_zero_iff_of_nonneg {f : Î± â†’ â„} (hf : 0 â‰¤ f) (hfi : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼] 0 :=
integral_eq_zero_iff_of_nonneg_ae (eventually_of_forall hf) hfi

lemma integral_pos_iff_support_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) (hfi : integrable f Î¼) :
  (0 < âˆ« x, f x âˆ‚Î¼) â†” 0 < Î¼ (function.support f) :=
by simp_rw [(integral_nonneg_of_ae hf).lt_iff_ne, pos_iff_ne_zero, ne.def, @eq_comm â„ 0,
  integral_eq_zero_iff_of_nonneg_ae hf hfi, filter.eventually_eq, ae_iff, pi.zero_apply,
  function.support]

lemma integral_pos_iff_support_of_nonneg {f : Î± â†’ â„} (hf : 0 â‰¤ f) (hfi : integrable f Î¼) :
  (0 < âˆ« x, f x âˆ‚Î¼) â†” 0 < Î¼ (function.support f) :=
integral_pos_iff_support_of_nonneg_ae (eventually_of_forall hf) hfi

section normed_group
variables {H : Type*} [normed_group H] [second_countable_topology H] [measurable_space H]
          [borel_space H]

lemma L1.norm_eq_integral_norm (f : Î± â†’â‚[Î¼] H) : âˆ¥fâˆ¥ = âˆ« a, âˆ¥f aâˆ¥ âˆ‚Î¼ :=
begin
  simp only [snorm, snorm', ennreal.one_to_real, ennreal.rpow_one, Lp.norm_def,
    if_false, ennreal.one_ne_top, one_ne_zero, _root_.div_one],
  rw integral_eq_lintegral_of_nonneg_ae (eventually_of_forall (by simp [norm_nonneg]))
    (continuous_norm.measurable.comp_ae_measurable (Lp.ae_measurable f)),
  simp [of_real_norm_eq_coe_nnnorm]
end

lemma L1.norm_of_fun_eq_integral_norm {f : Î± â†’ H} (hf : integrable f Î¼) :
  âˆ¥hf.to_L1 fâˆ¥ = âˆ« a, âˆ¥f aâˆ¥ âˆ‚Î¼ :=
begin
  rw L1.norm_eq_integral_norm,
  refine integral_congr_ae _,
  apply hf.coe_fn_to_L1.mono,
  intros a ha,
  simp [ha]
end

end normed_group

lemma integral_mono_ae {f g : Î± â†’ â„} (hf : integrable f Î¼) (hg : integrable g Î¼) (h : f â‰¤áµ[Î¼] g) :
  âˆ« a, f a âˆ‚Î¼ â‰¤ âˆ« a, g a âˆ‚Î¼ :=
le_of_sub_nonneg $ integral_sub hg hf â–¸ integral_nonneg_of_ae $ h.mono (Î» a, sub_nonneg_of_le)

@[mono] lemma integral_mono {f g : Î± â†’ â„} (hf : integrable f Î¼) (hg : integrable g Î¼) (h : f â‰¤ g) :
  âˆ« a, f a âˆ‚Î¼ â‰¤ âˆ« a, g a âˆ‚Î¼ :=
integral_mono_ae hf hg $ eventually_of_forall h

lemma integral_mono_of_nonneg {f g : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼] f) (hgi : integrable g Î¼)
  (h : f â‰¤áµ[Î¼] g) : âˆ« a, f a âˆ‚Î¼ â‰¤ âˆ« a, g a âˆ‚Î¼ :=
begin
  by_cases hfm : ae_measurable f Î¼,
  { refine integral_mono_ae âŸ¨hfm, _âŸ© hgi h,
    refine (hgi.has_finite_integral.mono $ h.mp $ hf.mono $ Î» x hf hfg, _),
    simpa [real.norm_eq_abs, abs_of_nonneg hf, abs_of_nonneg (le_trans hf hfg)] },
  { rw [integral_non_ae_measurable hfm],
    exact integral_nonneg_of_ae (hf.trans h) }
end

lemma norm_integral_le_integral_norm (f : Î± â†’ E) : âˆ¥(âˆ« a, f a âˆ‚Î¼)âˆ¥ â‰¤ âˆ« a, âˆ¥f aâˆ¥ âˆ‚Î¼ :=
have le_ae : âˆ€áµ a âˆ‚Î¼, 0 â‰¤ âˆ¥f aâˆ¥ := eventually_of_forall (Î»a, norm_nonneg _),
classical.by_cases
( Î»h : ae_measurable f Î¼,
  calc âˆ¥âˆ« a, f a âˆ‚Î¼âˆ¥ â‰¤ ennreal.to_real (âˆ«â» a, (ennreal.of_real âˆ¥f aâˆ¥) âˆ‚Î¼) :
      norm_integral_le_lintegral_norm _
    ... = âˆ« a, âˆ¥f aâˆ¥ âˆ‚Î¼ : (integral_eq_lintegral_of_nonneg_ae le_ae $ ae_measurable.norm h).symm )
( Î»h : Â¬ae_measurable f Î¼,
  begin
    rw [integral_non_ae_measurable h, norm_zero],
    exact integral_nonneg_of_ae le_ae
  end )

lemma norm_integral_le_of_norm_le {f : Î± â†’ E} {g : Î± â†’ â„} (hg : integrable g Î¼)
  (h : âˆ€áµ x âˆ‚Î¼, âˆ¥f xâˆ¥ â‰¤ g x) : âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ â‰¤ âˆ« x, g x âˆ‚Î¼ :=
calc âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ â‰¤ âˆ« x, âˆ¥f xâˆ¥ âˆ‚Î¼ : norm_integral_le_integral_norm f
               ... â‰¤ âˆ« x, g x âˆ‚Î¼   :
  integral_mono_of_nonneg (eventually_of_forall $ Î» x, norm_nonneg _) hg h

lemma integral_finset_sum {Î¹} (s : finset Î¹) {f : Î¹ â†’ Î± â†’ E} (hf : âˆ€ i, integrable (f i) Î¼) :
  âˆ« a, âˆ‘ i in s, f i a âˆ‚Î¼ = âˆ‘ i in s, âˆ« a, f i a âˆ‚Î¼ :=
begin
  refine finset.induction_on s _ _,
  { simp only [integral_zero, finset.sum_empty] },
  { assume i s his ih,
    simp only [his, finset.sum_insert, not_false_iff],
    rw [integral_add (hf _) (integrable_finset_sum s hf), ih] }
end

lemma simple_func.integral_eq_integral (f : Î± â†’â‚› E) (hfi : integrable f Î¼) :
  f.integral Î¼ = âˆ« x, f x âˆ‚Î¼ :=
begin
  rw [integral_eq f hfi, â† L1.simple_func.to_Lp_one_eq_to_L1,
    L1.simple_func.integral_L1_eq_integral, L1.simple_func.integral_eq_integral],
  exact simple_func.integral_congr hfi (Lp.simple_func.to_simple_func_to_Lp _ _).symm
end

lemma simple_func.integral_eq_sum (f : Î± â†’â‚› E) (hfi : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ = âˆ‘ x in f.range, (ennreal.to_real (Î¼ (f â»Â¹' {x}))) â€¢ x :=
by rw [â† f.integral_eq_integral hfi, simple_func.integral]

@[simp] lemma integral_const (c : E) : âˆ« x : Î±, c âˆ‚Î¼ = (Î¼ univ).to_real â€¢ c :=
begin
  cases (@le_top _ _ (Î¼ univ)).lt_or_eq with hÎ¼ hÎ¼,
  { haveI : is_finite_measure Î¼ := âŸ¨hÎ¼âŸ©,
    calc âˆ« x : Î±, c âˆ‚Î¼ = (simple_func.const Î± c).integral Î¼ :
      ((simple_func.const Î± c).integral_eq_integral (integrable_const _)).symm
    ... = _ : _,
    rw [simple_func.integral],
    casesI is_empty_or_nonempty Î±,
    { simp [Î¼.eq_zero_of_is_empty] },
    { simp [preimage_const_of_mem] } },
  { by_cases hc : c = 0,
    { simp [hc, integral_zero] },
    { have : Â¬integrable (Î» x : Î±, c) Î¼,
      { simp only [integrable_const_iff, not_or_distrib],
        exact âŸ¨hc, hÎ¼.not_ltâŸ© },
      simp [integral_undef, *] } }
end

lemma norm_integral_le_of_norm_le_const [is_finite_measure Î¼] {f : Î± â†’ E} {C : â„}
  (h : âˆ€áµ x âˆ‚Î¼, âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ â‰¤ C * (Î¼ univ).to_real :=
calc âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ â‰¤ âˆ« x, C âˆ‚Î¼ : norm_integral_le_of_norm_le (integrable_const C) h
               ... = C * (Î¼ univ).to_real : by rw [integral_const, smul_eq_mul, mul_comm]

lemma tendsto_integral_approx_on_univ_of_measurable
  {f : Î± â†’ E} (fmeas : measurable f) (hf : integrable f Î¼) :
  tendsto (Î» n, (simple_func.approx_on f fmeas univ 0 trivial n).integral Î¼) at_top
    (ğ“ $ âˆ« x, f x âˆ‚Î¼) :=
begin
  have : tendsto (Î» n, âˆ« x, simple_func.approx_on f fmeas univ 0 trivial n x âˆ‚Î¼)
    at_top (ğ“ $ âˆ« x, f x âˆ‚Î¼) :=
    tendsto_integral_of_L1 _ hf
      (eventually_of_forall $ simple_func.integrable_approx_on_univ fmeas hf)
      (simple_func.tendsto_approx_on_univ_L1_nnnorm fmeas hf),
  simpa only [simple_func.integral_eq_integral, simple_func.integrable_approx_on_univ fmeas hf]
end

variable {Î½ : measure Î±}

private lemma integral_add_measure_of_measurable
  {f : Î± â†’ E} (fmeas : measurable f) (hÎ¼ : integrable f Î¼) (hÎ½ : integrable f Î½) :
  âˆ« x, f x âˆ‚(Î¼ + Î½) = âˆ« x, f x âˆ‚Î¼ + âˆ« x, f x âˆ‚Î½ :=
begin
  have hfi := hÎ¼.add_measure hÎ½,
  refine tendsto_nhds_unique (tendsto_integral_approx_on_univ_of_measurable fmeas hfi) _,
  simpa only [simple_func.integral_add_measure _
    (simple_func.integrable_approx_on_univ fmeas hfi _)]
    using (tendsto_integral_approx_on_univ_of_measurable fmeas hÎ¼).add
      (tendsto_integral_approx_on_univ_of_measurable fmeas hÎ½)
end

lemma integral_add_measure {f : Î± â†’ E} (hÎ¼ : integrable f Î¼) (hÎ½ : integrable f Î½) :
  âˆ« x, f x âˆ‚(Î¼ + Î½) = âˆ« x, f x âˆ‚Î¼ + âˆ« x, f x âˆ‚Î½ :=
begin
  have h : ae_measurable f (Î¼ + Î½) := hÎ¼.ae_measurable.add_measure hÎ½.ae_measurable,
  let g := h.mk f,
  have A : f =áµ[Î¼ + Î½] g := h.ae_eq_mk,
  have B : f =áµ[Î¼] g := A.filter_mono (ae_mono (measure.le_add_right (le_refl Î¼))),
  have C : f =áµ[Î½] g := A.filter_mono (ae_mono (measure.le_add_left (le_refl Î½))),
  calc âˆ« x, f x âˆ‚(Î¼ + Î½) = âˆ« x, g x âˆ‚(Î¼ + Î½) : integral_congr_ae A
  ... = âˆ« x, g x âˆ‚Î¼ + âˆ« x, g x âˆ‚Î½ :
    integral_add_measure_of_measurable h.measurable_mk ((integrable_congr B).1 hÎ¼)
      ((integrable_congr C).1 hÎ½)
  ... = âˆ« x, f x âˆ‚Î¼ + âˆ« x, f x âˆ‚Î½ :
    by { congr' 1, { exact integral_congr_ae B.symm }, { exact integral_congr_ae C.symm } }
end

@[simp] lemma integral_zero_measure {m : measurable_space Î±} (f : Î± â†’ E) :
  âˆ« x, f x âˆ‚(0 : measure Î±) = 0 :=
norm_le_zero_iff.1 $ le_trans (norm_integral_le_lintegral_norm f) $ by simp

private lemma integral_smul_measure_aux {f : Î± â†’ E} {c : â„â‰¥0âˆ}
  (h0 : 0 < c) (hc : c < âˆ) (fmeas : measurable f) (hfi : integrable f Î¼) :
  âˆ« x, f x âˆ‚(c â€¢ Î¼) = c.to_real â€¢ âˆ« x, f x âˆ‚Î¼ :=
begin
  refine tendsto_nhds_unique _
    (tendsto_const_nhds.smul (tendsto_integral_approx_on_univ_of_measurable fmeas hfi)),
  convert tendsto_integral_approx_on_univ_of_measurable fmeas (hfi.smul_measure hc),
  simp only [simple_func.integral, measure.smul_apply, finset.smul_sum, smul_smul,
    ennreal.to_real_mul]
end

@[simp] lemma integral_smul_measure (f : Î± â†’ E) (c : â„â‰¥0âˆ) :
  âˆ« x, f x âˆ‚(c â€¢ Î¼) = c.to_real â€¢ âˆ« x, f x âˆ‚Î¼ :=
begin
  -- First we consider â€œdegenerateâ€ cases:
  -- `c = 0`
  rcases (zero_le c).eq_or_lt with rfl|h0, { simp },
  -- `f` is not almost everywhere measurable
  by_cases hfm : ae_measurable f Î¼, swap,
  { have : Â¬ (ae_measurable f (c â€¢ Î¼)), by simpa [ne_of_gt h0] using hfm,
    simp [integral_non_ae_measurable, hfm, this] },
  -- `c = âˆ`
  rcases (le_top : c â‰¤ âˆ).eq_or_lt with rfl|hc,
  { rw [ennreal.top_to_real, zero_smul],
    by_cases hf : f =áµ[Î¼] 0,
    { have : f =áµ[âˆ â€¢ Î¼] 0 := ae_smul_measure hf âˆ,
      exact integral_eq_zero_of_ae this },
    { apply integral_undef,
      rw [integrable, has_finite_integral, iff_true_intro (hfm.smul_measure âˆ), true_and,
          lintegral_smul_measure, top_mul, if_neg],
      { apply lt_irrefl },
      { rw [lintegral_eq_zero_iff' hfm.ennnorm],
        refine Î» h, hf (h.mono $ Î» x, _),
        simp } } },
  -- `f` is not integrable and `0 < c < âˆ`
  by_cases hfi : integrable f Î¼, swap,
  { rw [integral_undef hfi, smul_zero],
    refine integral_undef (mt (Î» h, _) hfi),
    convert h.smul_measure (ennreal.inv_lt_top.2 h0),
    rw [smul_smul, ennreal.inv_mul_cancel (ne_of_gt h0) (ne_of_lt hc), one_smul] },
  -- Main case: `0 < c < âˆ`, `f` is almost everywhere measurable and integrable
  let g := hfm.mk f,
  calc âˆ« x, f x âˆ‚(c â€¢ Î¼) = âˆ« x, g x âˆ‚(c â€¢ Î¼) : integral_congr_ae $ ae_smul_measure hfm.ae_eq_mk c
  ... = c.to_real â€¢ âˆ« x, g x âˆ‚Î¼ :
    integral_smul_measure_aux h0 hc hfm.measurable_mk $ hfi.congr hfm.ae_eq_mk
  ... = c.to_real â€¢ âˆ« x, f x âˆ‚Î¼ :
    by { congr' 1, exact integral_congr_ae (hfm.ae_eq_mk.symm) }
end

lemma integral_map_of_measurable {Î²} [measurable_space Î²] {Ï† : Î± â†’ Î²} (hÏ† : measurable Ï†)
  {f : Î² â†’ E} (hfm : measurable f) :
  âˆ« y, f y âˆ‚(measure.map Ï† Î¼) = âˆ« x, f (Ï† x) âˆ‚Î¼ :=
begin
  by_cases hfi : integrable f (measure.map Ï† Î¼), swap,
  { rw [integral_undef hfi, integral_undef],
    rwa [â† integrable_map_measure hfm.ae_measurable hÏ†] },
  refine tendsto_nhds_unique (tendsto_integral_approx_on_univ_of_measurable hfm hfi) _,
  convert tendsto_integral_approx_on_univ_of_measurable (hfm.comp hÏ†)
    ((integrable_map_measure hfm.ae_measurable hÏ†).1 hfi),
  ext1 i,
  simp only [simple_func.approx_on_comp, simple_func.integral, measure.map_apply, hÏ†,
    simple_func.measurable_set_preimage, â† preimage_comp, simple_func.coe_comp],
  refine (finset.sum_subset (simple_func.range_comp_subset_range _ hÏ†) (Î» y _ hy, _)).symm,
  rw [simple_func.mem_range, â† set.preimage_singleton_eq_empty, simple_func.coe_comp] at hy,
  simp [hy]
end

lemma integral_map {Î²} [measurable_space Î²] {Ï† : Î± â†’ Î²} (hÏ† : measurable Ï†)
  {f : Î² â†’ E} (hfm : ae_measurable f (measure.map Ï† Î¼)) :
  âˆ« y, f y âˆ‚(measure.map Ï† Î¼) = âˆ« x, f (Ï† x) âˆ‚Î¼ :=
let g := hfm.mk f in calc
âˆ« y, f y âˆ‚(measure.map Ï† Î¼) = âˆ« y, g y âˆ‚(measure.map Ï† Î¼) : integral_congr_ae hfm.ae_eq_mk
... = âˆ« x, g (Ï† x) âˆ‚Î¼ : integral_map_of_measurable hÏ† hfm.measurable_mk
... = âˆ« x, f (Ï† x) âˆ‚Î¼ : integral_congr_ae $ ae_eq_comp hÏ† (hfm.ae_eq_mk).symm

lemma integral_map_of_closed_embedding {Î²} [topological_space Î±] [borel_space Î±]
  [topological_space Î²] [measurable_space Î²] [borel_space Î²]
  {Ï† : Î± â†’ Î²} (hÏ† : closed_embedding Ï†) (f : Î² â†’ E) :
  âˆ« y, f y âˆ‚(measure.map Ï† Î¼) = âˆ« x, f (Ï† x) âˆ‚Î¼ :=
begin
  by_cases hfm : ae_measurable f (measure.map Ï† Î¼),
  { exact integral_map hÏ†.continuous.measurable hfm },
  { rw [integral_non_ae_measurable hfm, integral_non_ae_measurable],
    rwa ae_measurable_comp_right_iff_of_closed_embedding hÏ† }
end

lemma integral_dirac' [measurable_space Î±] (f : Î± â†’ E) (a : Î±) (hfm : measurable f) :
  âˆ« x, f x âˆ‚(measure.dirac a) = f a :=
calc âˆ« x, f x âˆ‚(measure.dirac a) = âˆ« x, f a âˆ‚(measure.dirac a) :
  integral_congr_ae $ ae_eq_dirac' hfm
... = f a : by simp [measure.dirac_apply_of_mem]

lemma integral_dirac [measurable_space Î±] [measurable_singleton_class Î±] (f : Î± â†’ E) (a : Î±) :
  âˆ« x, f x âˆ‚(measure.dirac a) = f a :=
calc âˆ« x, f x âˆ‚(measure.dirac a) = âˆ« x, f a âˆ‚(measure.dirac a) :
  integral_congr_ae $ ae_eq_dirac f
... = f a : by simp [measure.dirac_apply_of_mem]

end properties

section group

variables {G : Type*} [measurable_space G] [topological_space G] [group G] [has_continuous_mul G]
  [borel_space G]
variables {Î¼ : measure G}

open measure

/-- Translating a function by left-multiplication does not change its integral with respect to a
left-invariant measure. -/
@[to_additive]
lemma integral_mul_left_eq_self (hÎ¼ : is_mul_left_invariant Î¼) {f : G â†’ E} (g : G) :
  âˆ« x, f (g * x) âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
begin
  have hgÎ¼ : measure.map (has_mul.mul g) Î¼ = Î¼,
  { rw â† map_mul_left_eq_self at hÎ¼,
    exact hÎ¼ g },
  have h_mul : closed_embedding (Î» x, g * x) := (homeomorph.mul_left g).closed_embedding,
  rw [â† integral_map_of_closed_embedding h_mul, hgÎ¼],
  apply_instance,
end

/-- Translating a function by right-multiplication does not change its integral with respect to a
right-invariant measure. -/
@[to_additive]
lemma integral_mul_right_eq_self (hÎ¼ : is_mul_right_invariant Î¼) {f : G â†’ E} (g : G) :
  âˆ« x, f (x * g) âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
begin
  have hgÎ¼ : measure.map (Î» x, x * g) Î¼ = Î¼,
  { rw â† map_mul_right_eq_self at hÎ¼,
    exact hÎ¼ g },
  have h_mul : closed_embedding (Î» x, x * g) := (homeomorph.mul_right g).closed_embedding,
  rw [â† integral_map_of_closed_embedding h_mul, hgÎ¼],
  apply_instance,
end

/-- If some left-translate of a function negates it, then the integral of the function with respect
to a left-invariant measure is 0. -/
@[to_additive]
lemma integral_zero_of_mul_left_eq_neg (hÎ¼ : is_mul_left_invariant Î¼) {f : G â†’ E} {g : G}
  (hf' : âˆ€ x, f (g * x) = - f x) :
  âˆ« x, f x âˆ‚Î¼ = 0 :=
begin
  refine eq_zero_of_eq_neg â„ (eq.symm _),
  have : âˆ« x, f (g * x) âˆ‚Î¼ = âˆ« x, - f x âˆ‚Î¼,
  { congr,
    ext x,
    exact hf' x },
  convert integral_mul_left_eq_self hÎ¼ g using 1,
  rw [this, integral_neg]
end

/-- If some right-translate of a function negates it, then the integral of the function with respect
to a right-invariant measure is 0. -/
@[to_additive]
lemma integral_zero_of_mul_right_eq_neg (hÎ¼ : is_mul_right_invariant Î¼) {f : G â†’ E} {g : G}
  (hf' : âˆ€ x, f (x * g) = - f x) :
  âˆ« x, f x âˆ‚Î¼ = 0 :=
begin
  refine eq_zero_of_eq_neg â„ (eq.symm _),
  have : âˆ« x, f (x * g) âˆ‚Î¼ = âˆ« x, - f x âˆ‚Î¼,
  { congr,
    ext x,
    exact hf' x },
  convert integral_mul_right_eq_self hÎ¼ g using 1,
  rw [this, integral_neg]
end

end group

mk_simp_attribute integral_simps "Simp set for integral rules."

attribute [integral_simps] integral_neg integral_smul L1.integral_add L1.integral_sub
  L1.integral_smul L1.integral_neg

attribute [irreducible] integral L1.integral

section integral_trim

variables {H Î² Î³ : Type*} [normed_group H] [measurable_space H]
  {m m0 : measurable_space Î²} {Î¼ : measure Î²}

/-- Simple function seen as simple function of a larger `measurable_space`. -/
def simple_func.to_larger_space (hm : m â‰¤ m0) (f : @simple_func Î² m Î³) : simple_func Î² Î³ :=
âŸ¨@simple_func.to_fun Î² m Î³ f, Î» x, hm _ (@simple_func.measurable_set_fiber Î² Î³ m f x),
  @simple_func.finite_range Î² Î³ m fâŸ©

lemma simple_func.coe_to_larger_space_eq (hm : m â‰¤ m0) (f : @simple_func Î² m Î³) :
  â‡‘(f.to_larger_space hm) = f :=
rfl

lemma integral_simple_func_larger_space (hm : m â‰¤ m0) (f : @simple_func Î² m F)
  (hf_int : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ = âˆ‘ x in (@simple_func.range Î² F m f), (ennreal.to_real (Î¼ (f â»Â¹' {x}))) â€¢ x :=
begin
  simp_rw â† f.coe_to_larger_space_eq hm,
  have hf_int : integrable (f.to_larger_space hm) Î¼, by rwa simple_func.coe_to_larger_space_eq,
  rw simple_func.integral_eq_sum _ hf_int,
  congr,
end

lemma integral_trim_simple_func (hm : m â‰¤ m0) (f : @simple_func Î² m F) (hf_int : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚(Î¼.trim hm) :=
begin
  have hf : @measurable _ _ m _ f, from @simple_func.measurable Î² F m _ f,
  have hf_int_m := hf_int.trim hm hf,
  rw [integral_simple_func_larger_space le_rfl f hf_int_m,
    integral_simple_func_larger_space hm f hf_int],
  congr,
  ext1 x,
  congr,
  exact (trim_measurable_set_eq hm (@simple_func.measurable_set_fiber Î² F m f x)).symm,
end

lemma integral_trim (hm : m â‰¤ m0) {f : Î² â†’ F} (hf : @measurable Î² F m _ f) :
  âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚(Î¼.trim hm) :=
begin
  by_cases hf_int : integrable f Î¼,
  swap,
  { have hf_int_m : Â¬ integrable f (Î¼.trim hm),
      from Î» hf_int_m, hf_int (integrable_of_integrable_trim hm hf_int_m),
    rw [integral_undef hf_int, integral_undef hf_int_m], },
  let f_seq := @simple_func.approx_on F Î² _ _ _ m _ hf set.univ 0 (set.mem_univ 0) _,
  have hf_seq_meas : âˆ€ n, @measurable _ _ m _ (f_seq n),
    from Î» n, @simple_func.measurable Î² F m _ (f_seq n),
  have hf_seq_int : âˆ€ n, integrable (f_seq n) Î¼,
    from simple_func.integrable_approx_on_univ (hf.mono hm le_rfl) hf_int,
  have hf_seq_int_m : âˆ€ n, integrable (f_seq n) (Î¼.trim hm),
    from Î» n, (hf_seq_int n).trim hm (hf_seq_meas n) ,
  have hf_seq_eq : âˆ€ n, âˆ« x, f_seq n x âˆ‚Î¼ = âˆ« x, f_seq n x âˆ‚(Î¼.trim hm),
    from Î» n, integral_trim_simple_func hm (f_seq n) (hf_seq_int n),
  have h_lim_1 : at_top.tendsto (Î» n, âˆ« x, f_seq n x âˆ‚Î¼) (ğ“ (âˆ« x, f x âˆ‚Î¼)),
  { refine tendsto_integral_of_L1 f hf_int (eventually_of_forall hf_seq_int) _,
    exact simple_func.tendsto_approx_on_univ_L1_nnnorm (hf.mono hm le_rfl) hf_int, },
  have h_lim_2 :  at_top.tendsto (Î» n, âˆ« x, f_seq n x âˆ‚Î¼) (ğ“ (âˆ« x, f x âˆ‚(Î¼.trim hm))),
  { simp_rw hf_seq_eq,
    refine @tendsto_integral_of_L1 Î² F _ _ _ _ _ _ m (Î¼.trim hm) _ f
      (hf_int.trim hm hf) _ _ (eventually_of_forall hf_seq_int_m) _,
    exact @simple_func.tendsto_approx_on_univ_L1_nnnorm Î² F m _ _ _ _ f _ hf (hf_int.trim hm hf), },
  exact tendsto_nhds_unique h_lim_1 h_lim_2,
end

lemma integral_trim_ae (hm : m â‰¤ m0) {f : Î² â†’ F} (hf : ae_measurable f (Î¼.trim hm)) :
  âˆ« x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚(Î¼.trim hm) :=
begin
  rw [integral_congr_ae (ae_eq_of_ae_eq_trim hf.ae_eq_mk), integral_congr_ae hf.ae_eq_mk],
  exact integral_trim hm hf.measurable_mk,
end

lemma ae_eq_trim_of_measurable [measurable_space Î³] [add_group Î³] [measurable_singleton_class Î³]
  [has_measurable_subâ‚‚ Î³] (hm : m â‰¤ m0) {f g : Î² â†’ Î³} (hf : @measurable _ _ m _ f)
  (hg : @measurable _ _ m _ g) (hfg : f =áµ[Î¼] g) :
  f =áµ[Î¼.trim hm] g :=
begin
  rwa [eventually_eq, ae_iff, trim_measurable_set_eq hm _],
  exact (@measurable_set.compl Î² _ m (@measurable_set_eq_fun Î² m Î³ _ _ _ _ _ _ hf hg)),
end

lemma ae_eq_trim_iff [measurable_space Î³] [add_group Î³] [measurable_singleton_class Î³]
  [has_measurable_subâ‚‚ Î³]
  (hm : m â‰¤ m0) {f g : Î² â†’ Î³} (hf : @measurable _ _ m _ f) (hg : @measurable _ _ m _ g) :
  f =áµ[Î¼.trim hm] g â†” f =áµ[Î¼] g :=
âŸ¨ae_eq_of_ae_eq_trim, ae_eq_trim_of_measurable hm hf hgâŸ©

end integral_trim

end measure_theory
