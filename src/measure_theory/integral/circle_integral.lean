/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import measure_theory.integral.interval_integral
import analysis.normed_space.pointwise
import analysis.special_functions.non_integrable
import analysis.analytic.basic

/-!
# Integral over a circle in `â„‚`

In this file we define `âˆ® z in C(c, R), f z` to be the integral $\oint_{|z-c|=|R|} f(z)\,dz$ and
prove some properties of this integral. We give definition and prove most lemmas for a function
`f : â„‚ â†’ E`, where `E` is a complex Banach space. For this reason,
some lemmas use, e.g., `(z - c)â»Â¹ â€¢ f z` instead of `f z / (z - c)`.

## Main definitions

* `circle_map c R`: the exponential map $Î¸ â†¦ c + R e^{Î¸i}$;

* `circle_integrable f c R`: a function `f : â„‚ â†’ E` is integrable on the circle with center `c` and
  radius `R` if `f âˆ˜ circle_map c R` is integrable on `[0, 2Ï€]`;

* `circle_integral f c R`: the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2Ï€}(c + Re^{Î¸ i})' f(c+Re^{Î¸ i})\,dÎ¸$;

* `cauchy_power_series f c R`: the power series that is equal to
  $\sum_{n=0}^{\infty} \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
  `w - c`. The coefficients of this power series depend only on `f âˆ˜ circle_map c R`, and the power
  series converges to `f w` if `f` is differentiable on the closed ball `metric.closed_ball c R`
  and `w` belongs to the corresponding open ball.

## Main statements

* `has_fpower_series_on_cauchy_integral`: for any circle integrable function `f`, the power series
  `cauchy_power_series f c R`, `R > 0`, converges to the Cauchy integral
  `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open disc `metric.ball c R`;

* `circle_integral.integral_sub_zpow_of_undef`, `circle_integral.integral_sub_zpow_of_ne`, and
  `circle_integral.integral_sub_inv_of_mem_ball`: formulas for `âˆ® z in C(c, R), (z - w) ^ n`,
  `n : â„¤`. These lemmas cover the following cases:

  - `circle_integral.integral_sub_zpow_of_undef`, `n < 0` and `|w - c| = |R|`: in this case the
    function is not integrable, so the integral is equal to its default value (zero);

  - `circle_integral.integral_sub_zpow_of_ne`, `n â‰  -1`: in the cases not covered by the previous
    lemma, we have `(z - w) ^ n = ((z - w) ^ (n + 1) / (n + 1))'`, thus the integral equals zero;

  - `circle_integral.integral_sub_inv_of_mem_ball`, `n = -1`, `|w - c| < R`: in this case the
    integral is equal to `2Ï€i`.

  The case `n = -1`, `|w -c| > R` is not covered by these lemmas. While it is possible to construct
  an explicit primitive, it is easier to apply Cauchy theorem, so we postpone the proof till we have
  this theorem (see #10000).

## Notation

- `âˆ® z in C(c, R), f z`: notation for the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2Ï€}(c + Re^{Î¸ i})' f(c+Re^{Î¸ i})\,dÎ¸$.

## Tags

integral, circle, Cauchy integral
-/

variables {E : Type*} [normed_group E]

noncomputable theory

open_locale real nnreal interval pointwise topological_space
open complex measure_theory topological_space metric function set filter asymptotics

/-!
### `circle_map`, a parametrization of a circle
-/

/-- The exponential map $Î¸ â†¦ c + R e^{Î¸i}$. The range of this map is the circle in `â„‚` with center
`c` and radius `|R|`. -/
def circle_map (c : â„‚) (R : â„) : â„ â†’ â„‚ := Î» Î¸, c + R * exp (Î¸ * I)

/-- `circle_map` is `2Ï€`-periodic. -/
lemma periodic_circle_map (c : â„‚) (R : â„) : periodic (circle_map c R) (2 * Ï€) :=
Î» Î¸, by simp [circle_map, add_mul, exp_periodic _]

lemma set.countable.preimage_circle_map {s : set â„‚} (hs : s.countable) (c : â„‚)
  {R : â„} (hR : R â‰  0) : (circle_map c R â»Â¹' s).countable :=
show (coe â»Â¹' ((* I) â»Â¹' (exp â»Â¹' ((*) R â»Â¹' ((+) c â»Â¹' s))))).countable,
  from (((hs.preimage (add_right_injective _)).preimage $ mul_right_injectiveâ‚€ $ of_real_ne_zero.2
    hR).preimage_cexp.preimage $ mul_left_injectiveâ‚€ I_ne_zero).preimage of_real_injective

@[simp] lemma circle_map_sub_center (c : â„‚) (R : â„) (Î¸ : â„) :
  circle_map c R Î¸ - c = circle_map 0 R Î¸ :=
by simp [circle_map]

lemma circle_map_zero (R Î¸ : â„) : circle_map 0 R Î¸ = R * exp (Î¸ * I) := zero_add _

@[simp] lemma abs_circle_map_zero (R : â„) (Î¸ : â„) : abs (circle_map 0 R Î¸) = |R| :=
by simp [circle_map]

lemma circle_map_mem_sphere' (c : â„‚) (R : â„) (Î¸ : â„) : circle_map c R Î¸ âˆˆ sphere c (|R|) :=
by simp

lemma circle_map_mem_sphere (c : â„‚) {R : â„} (hR : 0 â‰¤ R) (Î¸ : â„) : circle_map c R Î¸ âˆˆ sphere c R :=
by simpa only [_root_.abs_of_nonneg hR] using circle_map_mem_sphere' c R Î¸

lemma circle_map_mem_closed_ball (c : â„‚) {R : â„} (hR : 0 â‰¤ R) (Î¸ : â„) :
  circle_map c R Î¸ âˆˆ closed_ball c R :=
sphere_subset_closed_ball (circle_map_mem_sphere c hR Î¸)

/-- The range of `circle_map c R` is the circle with center `c` and radius `|R|`. -/
@[simp] lemma range_circle_map (c : â„‚) (R : â„) : range (circle_map c R) = sphere c (|R|) :=
calc range (circle_map c R) = c +áµ¥ R â€¢ range (Î» Î¸ : â„, exp (Î¸ * I)) :
  by simp only [â† image_vadd, â† image_smul, â† range_comp, vadd_eq_add, circle_map, (âˆ˜), real_smul]
... = sphere c (|R|) : by simp [smul_sphere R (0 : â„‚) zero_le_one, real.norm_eq_abs]

/-- The image of `(0, 2Ï€]` under `circle_map c R` is the circle with center `c` and radius `|R|`. -/
@[simp] lemma image_circle_map_Ioc (c : â„‚) (R : â„) :
  circle_map c R '' Ioc 0 (2 * Ï€) = sphere c (|R|) :=
by rw [â† range_circle_map, â† (periodic_circle_map c R).image_Ioc real.two_pi_pos 0, zero_add]

@[simp] lemma circle_map_eq_center_iff {c : â„‚} {R : â„} {Î¸ : â„} : circle_map c R Î¸ = c â†” R = 0 :=
by simp [circle_map, exp_ne_zero]

@[simp] lemma circle_map_zero_radius (c : â„‚) : circle_map c 0 = const â„ c :=
funext $ Î» Î¸, circle_map_eq_center_iff.2 rfl

lemma circle_map_ne_center {c : â„‚} {R : â„} (hR : R â‰  0) {Î¸ : â„} : circle_map c R Î¸ â‰  c :=
mt circle_map_eq_center_iff.1 hR

lemma has_deriv_at_circle_map (c : â„‚) (R : â„) (Î¸ : â„) :
  has_deriv_at (circle_map c R) (circle_map 0 R Î¸ * I) Î¸ :=
by simpa only [mul_assoc, one_mul, of_real_clm_apply, circle_map, of_real_one, zero_add]
 using ((of_real_clm.has_deriv_at.mul_const I).cexp.const_mul (R : â„‚)).const_add c

/- TODO: prove `cont_diff â„ (circle_map c R)`. This needs a version of `cont_diff.mul`
for multiplication in a normed algebra over the base field. -/

lemma differentiable_circle_map (c : â„‚) (R : â„) :
  differentiable â„ (circle_map c R) :=
Î» Î¸, (has_deriv_at_circle_map c R Î¸).differentiable_at

@[continuity] lemma continuous_circle_map (c : â„‚) (R : â„) : continuous (circle_map c R) :=
(differentiable_circle_map c R).continuous

@[measurability] lemma measurable_circle_map (c : â„‚) (R : â„) : measurable (circle_map c R) :=
(continuous_circle_map c R).measurable

@[simp] lemma deriv_circle_map (c : â„‚) (R : â„) (Î¸ : â„) :
  deriv (circle_map c R) Î¸ = circle_map 0 R Î¸ * I :=
(has_deriv_at_circle_map _ _ _).deriv

lemma deriv_circle_map_eq_zero_iff {c : â„‚} {R : â„} {Î¸ : â„} :
  deriv (circle_map c R) Î¸ = 0 â†” R = 0 :=
by simp [I_ne_zero]

lemma deriv_circle_map_ne_zero {c : â„‚} {R : â„} {Î¸ : â„} (hR : R â‰  0) :
  deriv (circle_map c R) Î¸ â‰  0 :=
mt deriv_circle_map_eq_zero_iff.1 hR

lemma lipschitz_with_circle_map (c : â„‚) (R : â„) :
  lipschitz_with R.nnabs (circle_map c R) :=
lipschitz_with_of_nnnorm_deriv_le (differentiable_circle_map _ _) $ Î» Î¸,
  nnreal.coe_le_coe.1 $ by simp

/-!
### Integrability of a function on a circle
-/

/-- We say that a function `f : â„‚ â†’ E` is integrable on the circle with center `c` and radius `R` if
the function `f âˆ˜ circle_map c R` is integrable on `[0, 2Ï€]`.

Note that the actual function used in the definition of `circle_integral` is
`(deriv (circle_map c R) Î¸) â€¢ f (circle_map c R Î¸)`. Integrability of this function is equivalent
to integrability of `f âˆ˜ circle_map c R` whenever `R â‰  0`. -/
def circle_integrable (f : â„‚ â†’ E) (c : â„‚) (R : â„) : Prop :=
interval_integrable (Î» Î¸ : â„, f (circle_map c R Î¸)) volume 0 (2 * Ï€)

@[simp] lemma circle_integrable_const (a : E) (c : â„‚) (R : â„) :
  circle_integrable (Î» _, a) c R :=
interval_integrable_const

namespace circle_integrable

variables {f g : â„‚ â†’ E} {c : â„‚} {R : â„}

lemma add (hf : circle_integrable f c R) (hg : circle_integrable g c R) :
  circle_integrable (f + g) c R :=
hf.add hg

lemma neg (hf : circle_integrable f c R) : circle_integrable (-f) c R := hf.neg

/-- The function we actually integrate over `[0, 2Ï€]` in the definition of `circle_integral` is
integrable. -/
lemma out [normed_space â„‚ E] (hf : circle_integrable f c R) :
  interval_integrable (Î» Î¸ : â„, deriv (circle_map c R) Î¸ â€¢ f (circle_map c R Î¸)) volume 0 (2 * Ï€) :=
begin
  simp only [circle_integrable, deriv_circle_map, interval_integrable_iff] at *,
  refine (hf.norm.const_mul (|R|)).mono' _ _,
  { exact ((continuous_circle_map _ _).ae_strongly_measurable.mul_const I).smul
      hf.ae_strongly_measurable },
  { simp [norm_smul] }
end

end circle_integrable

@[simp] lemma circle_integrable_zero_radius {f : â„‚ â†’ E} {c : â„‚} : circle_integrable f c 0 :=
by simp [circle_integrable]

lemma circle_integrable_iff [normed_space â„‚ E]
  {f : â„‚ â†’ E} {c : â„‚} (R : â„) : circle_integrable f c R â†”
  interval_integrable (Î» Î¸ : â„, deriv (circle_map c R) Î¸ â€¢ f (circle_map c R Î¸)) volume 0 (2 * Ï€) :=
begin
  by_cases hâ‚€ : R = 0,
  { simp [hâ‚€], },
  refine âŸ¨Î» h, h.out, Î» h, _âŸ©,
  simp only [circle_integrable, interval_integrable_iff, deriv_circle_map] at h âŠ¢,
  refine (h.norm.const_mul (|R|â»Â¹)).mono' _ _,
  { have H : âˆ€ {Î¸}, circle_map 0 R Î¸ * I â‰  0 := Î» Î¸, by simp [hâ‚€, I_ne_zero],
    simpa only [inv_smul_smulâ‚€ H]
      using (((continuous_circle_map 0 R).ae_strongly_measurable).mul_const I).ae_measurable
        .inv.ae_strongly_measurable.smul h.ae_strongly_measurable },
  { simp [norm_smul, hâ‚€] },
end

lemma continuous_on.circle_integrable' {f : â„‚ â†’ E} {c : â„‚} {R : â„}
  (hf : continuous_on f (sphere c (|R|))) :
  circle_integrable f c R :=
(hf.comp_continuous (continuous_circle_map _ _)
  (circle_map_mem_sphere' _ _)).interval_integrable _ _

lemma continuous_on.circle_integrable {f : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R)
  (hf : continuous_on f (sphere c R)) :
  circle_integrable f c R :=
continuous_on.circle_integrable' $ (_root_.abs_of_nonneg hR).symm â–¸ hf

/-- The function `Î» z, (z - w) ^ n`, `n : â„¤`, is circle integrable on the circle with center `c` and
radius `|R|` if and only if `R = 0` or `0 â‰¤ n`, or `w` does not belong to this circle. -/
@[simp] lemma circle_integrable_sub_zpow_iff {c w : â„‚} {R : â„} {n : â„¤} :
  circle_integrable (Î» z, (z - w) ^ n) c R â†” R = 0 âˆ¨ 0 â‰¤ n âˆ¨ w âˆ‰ sphere c (|R|) :=
begin
  split,
  { intro h, contrapose! h, rcases h with âŸ¨hR, hn, hwâŸ©,
    simp only [circle_integrable_iff R, deriv_circle_map],
    rw â† image_circle_map_Ioc at hw, rcases hw with âŸ¨Î¸, hÎ¸, rflâŸ©,
    replace hÎ¸ : Î¸ âˆˆ [0, 2 * Ï€], from Icc_subset_interval (Ioc_subset_Icc_self hÎ¸),
    refine not_interval_integrable_of_sub_inv_is_O_punctured _ real.two_pi_pos.ne hÎ¸,
    set f : â„ â†’ â„‚ := Î» Î¸', circle_map c R Î¸' - circle_map c R Î¸,
    have : âˆ€á¶  Î¸' in ğ“[â‰ ] Î¸, f Î¸' âˆˆ ball (0 : â„‚) 1 \ {0},
    { suffices : âˆ€á¶  z in ğ“[â‰ ] (circle_map c R Î¸), z - circle_map c R Î¸ âˆˆ ball (0 : â„‚) 1 \ {0},
        from ((differentiable_circle_map c R Î¸).has_deriv_at.tendsto_punctured_nhds
          (deriv_circle_map_ne_zero hR)).eventually this,
      filter_upwards [self_mem_nhds_within,
        mem_nhds_within_of_mem_nhds (ball_mem_nhds _ zero_lt_one)],
      simp [dist_eq, sub_eq_zero] { contextual := tt } },
    refine ((((has_deriv_at_circle_map c R Î¸).is_O_sub).mono inf_le_left).inv_rev
      (this.mono (Î» Î¸', and.right))).trans _,
    refine is_O.of_bound (|R|)â»Â¹ (this.mono $ Î» Î¸' hÎ¸', _),
    set x := abs (f Î¸'),
    suffices : xâ»Â¹ â‰¤ x ^ n, by simpa [inv_mul_cancel_leftâ‚€, mt _root_.abs_eq_zero.1 hR],
    have : x âˆˆ Ioo (0 : â„) 1, by simpa [and.comm, x] using hÎ¸',
    rw â† zpow_neg_one,
    refine (zpow_strict_anti this.1 this.2).le_iff_le.2 (int.lt_add_one_iff.1 _), exact hn },
  { rintro (rfl|H),
    exacts [circle_integrable_zero_radius,
      ((continuous_on_id.sub continuous_on_const).zpowâ‚€ _ $ Î» z hz, H.symm.imp_left $
        Î» hw, sub_ne_zero.2 $ ne_of_mem_of_not_mem hz hw).circle_integrable'] },
end

@[simp] lemma circle_integrable_sub_inv_iff {c w : â„‚} {R : â„} :
  circle_integrable (Î» z, (z - w)â»Â¹) c R â†” R = 0 âˆ¨ w âˆ‰ sphere c (|R|) :=
by { simp only [â† zpow_neg_one, circle_integrable_sub_zpow_iff], norm_num }

variables [normed_space â„‚ E] [complete_space E]

/-- Definition for $\oint_{|z-c|=R} f(z)\,dz$. -/
def circle_integral (f : â„‚ â†’ E) (c : â„‚) (R : â„) : E :=
âˆ« (Î¸ : â„) in 0..2 * Ï€, deriv (circle_map c R) Î¸ â€¢ f (circle_map c R Î¸)

notation `âˆ®` binders ` in ` `C(` c `, ` R `)` `, ` r:(scoped:60 f, circle_integral f c R) := r

lemma circle_integral_def_Icc (f : â„‚ â†’ E) (c : â„‚) (R : â„) :
  âˆ® z in C(c, R), f z = âˆ« Î¸ in Icc 0 (2 * Ï€), deriv (circle_map c R) Î¸ â€¢ f (circle_map c R Î¸) :=
by simp only [circle_integral, interval_integral.integral_of_le real.two_pi_pos.le,
  measure.restrict_congr_set Ioc_ae_eq_Icc]

namespace circle_integral

@[simp] lemma integral_radius_zero (f : â„‚ â†’ E) (c : â„‚) : âˆ® z in C(c, 0), f z = 0 :=
by simp [circle_integral]

lemma integral_congr {f g : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R) (h : eq_on f g (sphere c R)) :
  âˆ® z in C(c, R), f z = âˆ® z in C(c, R), g z :=
interval_integral.integral_congr $ Î» Î¸ hÎ¸, by simp only [h (circle_map_mem_sphere _ hR _)]

lemma integral_sub_inv_smul_sub_smul (f : â„‚ â†’ E) (c w : â„‚) (R : â„) :
  âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ (z - w) â€¢ f z = âˆ® z in C(c, R), f z :=
begin
  rcases eq_or_ne R 0 with rfl|hR, { simp only [integral_radius_zero] },
  have : countable (circle_map c R â»Â¹' {w}), from (countable_singleton _).preimage_circle_map c hR,
  refine interval_integral.integral_congr_ae ((this.ae_not_mem _).mono $ Î» Î¸ hÎ¸ hÎ¸', _),
  change circle_map c R Î¸ â‰  w at hÎ¸,
  simp only [inv_smul_smulâ‚€ (sub_ne_zero.2 $ hÎ¸)]
end

lemma integral_undef {f : â„‚ â†’ E} {c : â„‚} {R : â„} (hf : Â¬circle_integrable f c R) :
  âˆ® z in C(c, R), f z = 0 :=
interval_integral.integral_undef (mt (circle_integrable_iff R).mpr hf)

lemma integral_sub {f g : â„‚ â†’ E} {c : â„‚} {R : â„} (hf : circle_integrable f c R)
  (hg : circle_integrable g c R) :
  âˆ® z in C(c, R), f z - g z = (âˆ® z in C(c, R), f z) - âˆ® z in C(c, R), g z :=
by simp only [circle_integral, smul_sub, interval_integral.integral_sub hf.out hg.out]

lemma norm_integral_le_of_norm_le_const' {f : â„‚ â†’ E} {c : â„‚} {R C : â„}
  (hf : âˆ€ z âˆˆ sphere c (|R|), âˆ¥f zâˆ¥ â‰¤ C) :
  âˆ¥âˆ® z in C(c, R), f zâˆ¥ â‰¤ 2 * Ï€ * |R| * C :=
calc âˆ¥âˆ® z in C(c, R), f zâˆ¥ â‰¤ |R| * C * |2 * Ï€ - 0| :
  interval_integral.norm_integral_le_of_norm_le_const $ Î» Î¸ _,
    (calc âˆ¥deriv (circle_map c R) Î¸ â€¢ f (circle_map c R Î¸)âˆ¥ = |R| * âˆ¥f (circle_map c R Î¸)âˆ¥ :
      by simp [norm_smul]
    ... â‰¤ |R| * C : mul_le_mul_of_nonneg_left (hf _ $ circle_map_mem_sphere' _ _ _)
      (_root_.abs_nonneg _))
... = 2 * Ï€ * |R| * C :
  by { rw [sub_zero, _root_.abs_of_pos real.two_pi_pos], ac_refl }

lemma norm_integral_le_of_norm_le_const {f : â„‚ â†’ E} {c : â„‚} {R C : â„} (hR : 0 â‰¤ R)
  (hf : âˆ€ z âˆˆ sphere c R, âˆ¥f zâˆ¥ â‰¤ C) :
  âˆ¥âˆ® z in C(c, R), f zâˆ¥ â‰¤ 2 * Ï€ * R * C :=
have |R| = R, from _root_.abs_of_nonneg hR,
calc âˆ¥âˆ® z in C(c, R), f zâˆ¥ â‰¤ 2 * Ï€ * |R| * C :
  norm_integral_le_of_norm_le_const' $ by rwa this
... = 2 * Ï€ * R * C : by rw this

lemma norm_two_pi_I_inv_smul_integral_le_of_norm_le_const {f : â„‚ â†’ E} {c : â„‚} {R C : â„} (hR : 0 â‰¤ R)
  (hf : âˆ€ z âˆˆ sphere c R, âˆ¥f zâˆ¥ â‰¤ C) :
  âˆ¥(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), f zâˆ¥ â‰¤ R * C :=
begin
  have : âˆ¥(2 * Ï€ * I : â„‚)â»Â¹âˆ¥ = (2 * Ï€)â»Â¹, by simp [real.pi_pos.le],
  rw [norm_smul, this, â† div_eq_inv_mul, div_le_iff real.two_pi_pos, mul_comm (R * C), â† mul_assoc],
  exact norm_integral_le_of_norm_le_const hR hf
end

/-- If `f` is continuous on the circle `|z - c| = R`, `R > 0`, the `âˆ¥f zâˆ¥` is less than or equal to
`C : â„` on this circle, and this norm is strictly less than `C` at some point `z` of the circle,
then `âˆ¥âˆ® z in C(c, R), f zâˆ¥ < 2 * Ï€ * R * C`. -/
lemma norm_integral_lt_of_norm_le_const_of_lt {f : â„‚ â†’ E} {c : â„‚} {R C : â„} (hR : 0 < R)
  (hc : continuous_on f (sphere c R)) (hf : âˆ€ z âˆˆ sphere c R, âˆ¥f zâˆ¥ â‰¤ C)
  (hlt : âˆƒ z âˆˆ sphere c R, âˆ¥f zâˆ¥ < C) :
  âˆ¥âˆ® z in C(c, R), f zâˆ¥ < 2 * Ï€ * R * C :=
begin
  rw [â† _root_.abs_of_pos hR, â† image_circle_map_Ioc] at hlt,
  rcases hlt with âŸ¨_, âŸ¨Î¸â‚€, hmem, rflâŸ©, hltâŸ©,
  calc âˆ¥âˆ® z in C(c, R), f zâˆ¥ â‰¤ âˆ« Î¸ in 0..2 * Ï€, âˆ¥deriv (circle_map c R) Î¸ â€¢ f (circle_map c R Î¸)âˆ¥ :
    interval_integral.norm_integral_le_integral_norm real.two_pi_pos.le
  ... < âˆ« Î¸ in 0..2 * Ï€, R * C :
    begin
      simp only [norm_smul, deriv_circle_map, norm_eq_abs, complex.abs_mul, abs_I, mul_one,
        abs_circle_map_zero, abs_of_pos hR],
      refine interval_integral.integral_lt_integral_of_continuous_on_of_le_of_exists_lt
        real.two_pi_pos _ continuous_on_const (Î» Î¸ hÎ¸, _) âŸ¨Î¸â‚€, Ioc_subset_Icc_self hmem, _âŸ©,
      { exact continuous_on_const.mul (hc.comp (continuous_circle_map _ _).continuous_on
          (Î» Î¸ hÎ¸, circle_map_mem_sphere _ hR.le _)).norm },
      { exact mul_le_mul_of_nonneg_left (hf _ $ circle_map_mem_sphere _ hR.le _) hR.le },
      { exact (mul_lt_mul_left hR).2 hlt }
    end
  ... = 2 * Ï€ * R * C : by simp [mul_assoc]
end

@[simp] lemma integral_smul {ğ•œ : Type*} [is_R_or_C ğ•œ] [normed_space ğ•œ E] [smul_comm_class ğ•œ â„‚ E]
  (a : ğ•œ) (f : â„‚ â†’ E) (c : â„‚) (R : â„) :
  âˆ® z in C(c, R), a â€¢ f z = a â€¢ âˆ® z in C(c, R), f z :=
by simp only [circle_integral, â† smul_comm a, interval_integral.integral_smul]

@[simp] lemma integral_smul_const (f : â„‚ â†’ â„‚) (a : E) (c : â„‚) (R : â„) :
  âˆ® z in C(c, R), (f z â€¢ a) = (âˆ® z in C(c, R), f z) â€¢ a :=
by simp only [circle_integral, interval_integral.integral_smul_const, â† smul_assoc]

@[simp] lemma integral_const_mul (a : â„‚) (f : â„‚ â†’ â„‚) (c : â„‚) (R : â„) :
  âˆ® z in C(c, R), a * f z = a * âˆ® z in C(c, R), f z :=
integral_smul a f c R

@[simp] lemma integral_sub_center_inv (c : â„‚) {R : â„} (hR : R â‰  0) :
  âˆ® z in C(c, R), (z - c)â»Â¹ = 2 * Ï€ * I :=
by simp [circle_integral, â† div_eq_mul_inv, mul_div_cancel_left _ (circle_map_ne_center hR)]

/-- If `f' : â„‚ â†’ E` is a derivative of a complex differentiable function on the circle
`metric.sphere c |R|`, then `âˆ® z in C(c, R), f' z = 0`. -/
lemma integral_eq_zero_of_has_deriv_within_at' {f f' : â„‚ â†’ E} {c : â„‚} {R : â„}
  (h : âˆ€ z âˆˆ sphere c (|R|), has_deriv_within_at f (f' z) (sphere c (|R|)) z) :
  âˆ® z in C(c, R), f' z = 0 :=
begin
  by_cases hi : circle_integrable f' c R,
  { rw â† sub_eq_zero.2 ((periodic_circle_map c R).comp f).eq,
    refine interval_integral.integral_eq_sub_of_has_deriv_at (Î» Î¸ hÎ¸, _) hi.out,
    exact (h _ (circle_map_mem_sphere' _ _ _)).scomp_has_deriv_at Î¸
      (differentiable_circle_map _ _ _).has_deriv_at (circle_map_mem_sphere' _ _) },
  { exact integral_undef hi }
end

/-- If `f' : â„‚ â†’ E` is a derivative of a complex differentiable function on the circle
`metric.sphere c R`, then `âˆ® z in C(c, R), f' z = 0`. -/
lemma integral_eq_zero_of_has_deriv_within_at {f f' : â„‚ â†’ E} {c : â„‚} {R : â„} (hR : 0 â‰¤ R)
  (h : âˆ€ z âˆˆ sphere c R, has_deriv_within_at f (f' z) (sphere c R) z) :
  âˆ® z in C(c, R), f' z = 0 :=
integral_eq_zero_of_has_deriv_within_at' $ (_root_.abs_of_nonneg hR).symm.subst h

/-- If `n < 0` and `|w - c| = |R|`, then `(z - w) ^ n` is not circle integrable on the circle with
center `c` and radius `(|R|)`, so the integral `âˆ® z in C(c, R), (z - w) ^ n` is equal to zero. -/
lemma integral_sub_zpow_of_undef {n : â„¤} {c w : â„‚} {R : â„} (hn : n < 0) (hw : w âˆˆ sphere c (|R|)) :
  âˆ® z in C(c, R), (z - w) ^ n = 0 :=
begin
  rcases eq_or_ne R 0 with rfl|h0, { apply integral_radius_zero },
  apply integral_undef,
  simp [circle_integrable_sub_zpow_iff, *]
end

/-- If `n â‰  -1` is an integer number, then the integral of `(z - w) ^ n` over the circle equals
zero. -/
lemma integral_sub_zpow_of_ne {n : â„¤} (hn : n â‰  -1) (c w : â„‚) (R : â„) :
  âˆ® z in C(c, R), (z - w) ^ n = 0 :=
begin
  rcases em (w âˆˆ sphere c (|R|) âˆ§ n < -1) with âŸ¨hw, hnâŸ©|H,
  { exact integral_sub_zpow_of_undef (hn.trans dec_trivial) hw },
  push_neg at H,
  have hd : âˆ€ z, (z â‰  w âˆ¨ -1 â‰¤ n) â†’ has_deriv_at (Î» z, (z - w) ^ (n + 1) / (n + 1)) ((z - w) ^ n) z,
  { intros z hne,
    convert ((has_deriv_at_zpow (n + 1) _ (hne.imp _ _)).comp z
      ((has_deriv_at_id z).sub_const w)).div_const _ using 1,
    { have hn' : (n + 1 : â„‚) â‰  0,
        by rwa [ne, â† eq_neg_iff_add_eq_zero, â† int.cast_one, â† int.cast_neg, int.cast_inj],
      simp [mul_assoc, mul_div_cancel_left _ hn'] },
    exacts [sub_ne_zero.2, neg_le_iff_add_nonneg.1] },
  refine integral_eq_zero_of_has_deriv_within_at' (Î» z hz, (hd z _).has_deriv_within_at),
  exact (ne_or_eq z w).imp_right (Î» h, H $ h â–¸ hz)
end

end circle_integral

/-- The power series that is equal to
$\sum_{n=0}^{\infty} \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
`w - c`. The coefficients of this power series depend only on `f âˆ˜ circle_map c R`, and the power
series converges to `f w` if `f` is differentiable on the closed ball `metric.closed_ball c R` and
`w` belongs to the corresponding open ball. For any circle integrable function `f`, this power
series converges to the Cauchy integral for `f`. -/
def cauchy_power_series (f : â„‚ â†’ E) (c : â„‚) (R : â„) :
  formal_multilinear_series â„‚ â„‚ E :=
Î» n, continuous_multilinear_map.mk_pi_field â„‚ _ $
  (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - c)â»Â¹ ^ n â€¢ (z - c)â»Â¹ â€¢ f z

lemma cauchy_power_series_apply (f : â„‚ â†’ E) (c : â„‚) (R : â„) (n : â„•) (w : â„‚) :
  cauchy_power_series f c R n (Î» _, w) =
    (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (w / (z - c)) ^ n â€¢ (z - c)â»Â¹ â€¢ f z :=
by simp only [cauchy_power_series, continuous_multilinear_map.mk_pi_field_apply, fin.prod_const,
  div_eq_mul_inv, mul_pow, mul_smul, circle_integral.integral_smul, â† smul_comm (w ^ n)]

lemma norm_cauchy_power_series_le (f : â„‚ â†’ E) (c : â„‚) (R : â„) (n : â„•) :
  âˆ¥cauchy_power_series f c R nâˆ¥ â‰¤
    (2 * Ï€)â»Â¹ * (âˆ« Î¸ : â„ in 0..2*Ï€, âˆ¥f (circle_map c R Î¸)âˆ¥) * (|R|â»Â¹) ^ n :=
calc âˆ¥cauchy_power_series f c R nâˆ¥
    = (2 * Ï€)â»Â¹ * âˆ¥âˆ® z in C(c, R), (z - c)â»Â¹ ^ n â€¢ (z - c)â»Â¹ â€¢ f zâˆ¥ :
  by simp [cauchy_power_series, norm_smul, real.pi_pos.le]
... â‰¤ (2 * Ï€)â»Â¹ * âˆ« Î¸ in 0..2*Ï€, âˆ¥deriv (circle_map c R) Î¸ â€¢ (circle_map c R Î¸ - c)â»Â¹ ^ n â€¢
  (circle_map c R Î¸ - c)â»Â¹ â€¢ f (circle_map c R Î¸)âˆ¥ :
  mul_le_mul_of_nonneg_left (interval_integral.norm_integral_le_integral_norm real.two_pi_pos.le)
    (by simp [real.pi_pos.le])
... = (2 * Ï€)â»Â¹ * (|R|â»Â¹ ^ n * (|R| * (|R|â»Â¹ * âˆ« (x : â„) in 0..2 * Ï€, âˆ¥f (circle_map c R x)âˆ¥))) :
  by simp [norm_smul, mul_left_comm (|R|)]
... â‰¤ (2 * Ï€)â»Â¹ * (âˆ« Î¸ : â„ in 0..2*Ï€, âˆ¥f (circle_map c R Î¸)âˆ¥) * |R|â»Â¹ ^ n :
  begin
    rcases eq_or_ne R 0 with rfl|hR,
    { cases n; simp [-mul_inv_rev, real.two_pi_pos] },
    { rw [mul_inv_cancel_leftâ‚€, mul_assoc, mul_comm (|R|â»Â¹ ^ n)],
      rwa [ne.def, _root_.abs_eq_zero] }
  end

lemma le_radius_cauchy_power_series (f : â„‚ â†’ E) (c : â„‚) (R : â„â‰¥0) :
  â†‘R â‰¤ (cauchy_power_series f c R).radius :=
begin
  refine (cauchy_power_series f c R).le_radius_of_bound
    ((2 * Ï€)â»Â¹ * (âˆ« Î¸ : â„ in 0..2*Ï€, âˆ¥f (circle_map c R Î¸)âˆ¥)) (Î» n, _),
  refine (mul_le_mul_of_nonneg_right (norm_cauchy_power_series_le _ _ _ _)
    (pow_nonneg R.coe_nonneg _)).trans _,
  rw [_root_.abs_of_nonneg R.coe_nonneg],
  cases eq_or_ne (R ^ n : â„) 0 with hR hR,
  { rw [hR, mul_zero],
    exact mul_nonneg (inv_nonneg.2 real.two_pi_pos.le)
      (interval_integral.integral_nonneg real.two_pi_pos.le (Î» _ _, norm_nonneg _)) },
  { rw [inv_pow, inv_mul_cancel_rightâ‚€ hR] }
end

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R` multiplied
by `2Ï€I` converges to the integral `âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open disc
`metric.ball c R`. -/
lemma has_sum_two_pi_I_cauchy_power_series_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
  (hf : circle_integrable f c R) (hw : abs w < R) :
  has_sum (Î» n : â„•, âˆ® z in C(c, R), (w / (z - c)) ^ n â€¢ (z - c)â»Â¹ â€¢ f z)
    (âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z) :=
begin
  have hR : 0 < R := (abs_nonneg w).trans_lt hw,
  have hwR : abs w / R âˆˆ Ico (0 : â„) 1,
    from âŸ¨div_nonneg (abs_nonneg w) hR.le, (div_lt_one hR).2 hwâŸ©,
  refine interval_integral.has_sum_integral_of_dominated_convergence
    (Î» n Î¸, âˆ¥f (circle_map c R Î¸)âˆ¥ * (abs w / R) ^ n) (Î» n, _) (Î» n, _) _ _ _,
  { simp only [deriv_circle_map],
    apply_rules [ae_strongly_measurable.smul, hf.def.1];
    { apply measurable.ae_strongly_measurable, measurability } },
  { simp [norm_smul, abs_of_pos hR, mul_left_comm R, mul_inv_cancel_leftâ‚€ hR.ne', mul_comm (âˆ¥_âˆ¥)] },
  { exact eventually_of_forall (Î» _ _, (summable_geometric_of_lt_1 hwR.1 hwR.2).mul_left _) },
  { simpa only [tsum_mul_left, tsum_geometric_of_lt_1 hwR.1 hwR.2]
      using hf.norm.mul_continuous_on continuous_on_const },
  { refine eventually_of_forall (Î» Î¸ hÎ¸, has_sum.const_smul _),
    simp only [smul_smul],
    refine has_sum.smul_const _,
    have : âˆ¥w / (circle_map c R Î¸ - c)âˆ¥ < 1, by simpa [abs_of_pos hR] using hwR.2,
    convert (has_sum_geometric_of_norm_lt_1 this).mul_right _,
    simp [â† sub_sub, â† mul_inv, sub_mul, div_mul_cancel _ (circle_map_ne_center hR.ne')] }
end

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `metric.ball c R`. -/
lemma has_sum_cauchy_power_series_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
  (hf : circle_integrable f c R) (hw : abs w < R) :
  has_sum (Î» n, cauchy_power_series f c R n (Î» _, w))
    ((2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z) :=
begin
  simp only [cauchy_power_series_apply],
  exact (has_sum_two_pi_I_cauchy_power_series_integral hf hw).const_smul
end

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `metric.ball c R`. -/
lemma sum_cauchy_power_series_eq_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„} {w : â„‚}
  (hf : circle_integrable f c R) (hw : abs w < R) :
  (cauchy_power_series f c R).sum w =
    ((2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - (c + w))â»Â¹ â€¢ f z) :=
(has_sum_cauchy_power_series_integral hf hw).tsum_eq

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R`, `R > 0`,
converges to the Cauchy integral `(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z` on the open
disc `metric.ball c R`. -/
lemma has_fpower_series_on_cauchy_integral {f : â„‚ â†’ E} {c : â„‚} {R : â„â‰¥0}
  (hf : circle_integrable f c R) (hR : 0 < R) :
  has_fpower_series_on_ball
    (Î» w, (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - w)â»Â¹ â€¢ f z)
    (cauchy_power_series f c R) c R :=
{ r_le := le_radius_cauchy_power_series _ _ _,
  r_pos := ennreal.coe_pos.2 hR,
  has_sum := Î» y hy,
    begin
      refine has_sum_cauchy_power_series_integral hf _,
      rw [â† norm_eq_abs, â† coe_nnnorm, nnreal.coe_lt_coe, â† ennreal.coe_lt_coe],
      exact mem_emetric_ball_zero_iff.1 hy
    end }

namespace circle_integral

/-- Integral $\oint_{|z-c|=R} \frac{dz}{z-w}=2Ï€i$ whenever $|w-c|<R$. -/
lemma integral_sub_inv_of_mem_ball {c w : â„‚} {R : â„} (hw : w âˆˆ ball c R) :
  âˆ® z in C(c, R), (z - w)â»Â¹ = 2 * Ï€ * I :=
begin
  have hR : 0 < R := dist_nonneg.trans_lt hw,
  suffices H : has_sum (Î» n : â„•, âˆ® z in C(c, R), ((w - c) / (z - c)) ^ n * (z - c)â»Â¹) (2 * Ï€ * I),
  { have A : circle_integrable (Î» _, (1 : â„‚)) c R, from continuous_on_const.circle_integrable',
    refine (H.unique _).symm,
    simpa only [smul_eq_mul, mul_one, add_sub_cancel'_right]
      using has_sum_two_pi_I_cauchy_power_series_integral A hw },
  have H : âˆ€ n : â„•, n â‰  0 â†’ âˆ® z in C(c, R), (z - c) ^ (-n - 1 : â„¤) = 0,
  { refine Î» n hn, integral_sub_zpow_of_ne _ _ _ _, simpa },
  have : âˆ® z in C(c, R), ((w - c) / (z - c)) ^ 0 * (z - c)â»Â¹ = 2 * Ï€ * I, by simp [hR.ne'],
  refine this â–¸ has_sum_single _ (Î» n hn, _),
  simp only [div_eq_mul_inv, mul_pow, integral_const_mul, mul_assoc],
  rw [(integral_congr hR.le (Î» z hz, _)).trans (H n hn), mul_zero],
  rw [â† pow_succ', â† zpow_coe_nat, inv_zpow, â† zpow_neg, int.coe_nat_succ, neg_add,
    sub_eq_add_neg _ (1 : â„¤)]
end

end circle_integral
