/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import measure_theory.integral.interval_integral
import analysis.normed_space.pointwise
import analysis.special_functions.non_integrable
import analysis.analytic.basic

/-!
# Integral over a circle in `‚ÑÇ`

In this file we define `‚àÆ z in C(c, R), f z` to be the integral $\oint_{|z-c|=|R|} f(z)\,dz$ and
prove some properties of this integral. We give definition and prove most lemmas for a function
`f : ‚ÑÇ ‚Üí E`, where `E` is a complex Banach space with second countable topology. For this reason,
some lemmas use, e.g., `(z - c)‚Åª¬π ‚Ä¢ f z` instead of `f z / (z - c)`.

## Main definitions

* `circle_map c R`: the exponential map $Œ∏ ‚Ü¶ c + R e^{Œ∏i}$;

* `circle_integrable f c R`: a function `f : ‚ÑÇ ‚Üí E` is integrable on the circle with center `c` and
  radius `R` if `f ‚àò circle_map c R` is integrable on `[0, 2œÄ]`;

* `circle_integral f c R`: the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2œÄ}(c + Re^{Œ∏ i})' f(c+Re^{Œ∏ i})\,dŒ∏$;

* `cauchy_power_series f c R`: the power series that is equal to
  $\sum_{n=0}^{\infty} \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
  `w - c`. The coefficients of this power series depend only on `f ‚àò circle_map c R`, and the power
  series converges to `f w` if `f` is differentiable on the closed ball `metric.closed_ball c R`
  and `w` belongs to the corresponding open ball.

## Main statements

* `has_fpower_series_on_cauchy_integral`: for any circle integrable function `f`, the power series
  `cauchy_power_series f c R`, `R > 0`, converges to the Cauchy integral
  `(2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - w)‚Åª¬π ‚Ä¢ f z` on the open disc `metric.ball c R`;

* `circle_integral.integral_sub_zpow_of_undef`, `circle_integral.integral_sub_zpow_of_ne`, and
  `circle_integral.integral_sub_inv_of_mem_ball`: formulas for `‚àÆ z in C(c, R), (z - w) ^ n`,
  `n : ‚Ñ§`. These lemmas cover the following cases:

  - `circle_integral.integral_sub_zpow_of_undef`, `n < 0` and `|w - c| = |R|`: in this case the
    function is not integrable, so the integral is equal to its default value (zero);

  - `circle_integral.integral_sub_zpow_of_ne`, `n ‚â† -1`: in the cases not covered by the previous
    lemma, we have `(z - w) ^ n = ((z - w) ^ (n + 1) / (n + 1))'`, thus the integral equals zero;

  - `circle_integral.integral_sub_inv_of_mem_ball`, `n = -1`, `|w - c| < R`: in this case the
    integral is equal to `2œÄi`.

  The case `n = -1`, `|w -c| > R` is not covered by these lemmas. While it is possible to construct
  an explicit primitive, it is easier to apply Cauchy theorem, so we postpone the proof till we have
  this theorem (see #10000).

## Notation

- `‚àÆ z in C(c, R), f z`: notation for the integral $\oint_{|z-c|=|R|} f(z)\,dz$, defined as
  $\int_{0}^{2œÄ}(c + Re^{Œ∏ i})' f(c+Re^{Œ∏ i})\,dŒ∏$.

## Tags

integral, circle, Cauchy integral
-/

variables {E : Type*} [measurable_space E] [normed_group E]

noncomputable theory

open_locale real nnreal interval pointwise topological_space
open complex measure_theory topological_space metric function set filter asymptotics

/-!
### `circle_map`, a parametrization of a circle
-/

/-- The exponential map $Œ∏ ‚Ü¶ c + R e^{Œ∏i}$. The range of this map is the circle in `‚ÑÇ` with center
`c` and radius `|R|`. -/
def circle_map (c : ‚ÑÇ) (R : ‚Ñù) : ‚Ñù ‚Üí ‚ÑÇ := Œª Œ∏, c + R * exp (Œ∏ * I)

/-- `circle_map` is `2œÄ`-periodic. -/
lemma periodic_circle_map (c : ‚ÑÇ) (R : ‚Ñù) : periodic (circle_map c R) (2 * œÄ) :=
Œª Œ∏, by simp [circle_map, add_mul, exp_periodic _]

@[simp] lemma circle_map_sub_center (c : ‚ÑÇ) (R : ‚Ñù) (Œ∏ : ‚Ñù) :
  circle_map c R Œ∏ - c = circle_map 0 R Œ∏ :=
by simp [circle_map]

@[simp] lemma abs_circle_map_zero (R : ‚Ñù) (Œ∏ : ‚Ñù) : abs (circle_map 0 R Œ∏) = |R| :=
by simp [circle_map]

lemma circle_map_mem_sphere' (c : ‚ÑÇ) (R : ‚Ñù) (Œ∏ : ‚Ñù) : circle_map c R Œ∏ ‚àà sphere c (|R|) :=
by simp

lemma circle_map_mem_sphere (c : ‚ÑÇ) {R : ‚Ñù} (hR : 0 ‚â§ R) (Œ∏ : ‚Ñù) : circle_map c R Œ∏ ‚àà sphere c R :=
by simpa only [_root_.abs_of_nonneg hR] using circle_map_mem_sphere' c R Œ∏

lemma circle_map_mem_closed_ball (c : ‚ÑÇ) {R : ‚Ñù} (hR : 0 ‚â§ R) (Œ∏ : ‚Ñù) :
  circle_map c R Œ∏ ‚àà closed_ball c R :=
sphere_subset_closed_ball (circle_map_mem_sphere c hR Œ∏)

/-- The range of `circle_map c R` is the circle with center `c` and radius `|R|`. -/
@[simp] lemma range_circle_map (c : ‚ÑÇ) (R : ‚Ñù) : range (circle_map c R) = sphere c (|R|) :=
calc range (circle_map c R) = c +·µ• R ‚Ä¢ range (Œª Œ∏ : ‚Ñù, exp (Œ∏ * I)) :
  by simp only [‚Üê image_vadd, ‚Üê image_smul, ‚Üê range_comp, vadd_eq_add, circle_map, (‚àò), real_smul]
... = sphere c (|R|) : by simp [smul_sphere R (0 : ‚ÑÇ) zero_le_one, real.norm_eq_abs]

/-- The image of `(0, 2œÄ]` under `circle_map c R` is the circle with center `c` and radius `|R|`. -/
@[simp] lemma image_circle_map_Ioc (c : ‚ÑÇ) (R : ‚Ñù) :
  circle_map c R '' Ioc 0 (2 * œÄ) = sphere c (|R|) :=
by rw [‚Üê range_circle_map, ‚Üê (periodic_circle_map c R).image_Ioc real.two_pi_pos 0, zero_add]

@[simp] lemma circle_map_eq_center_iff {c : ‚ÑÇ} {R : ‚Ñù} {Œ∏ : ‚Ñù} : circle_map c R Œ∏ = c ‚Üî R = 0 :=
by simp [circle_map, exp_ne_zero]

@[simp] lemma circle_map_zero_radius (c : ‚ÑÇ) : circle_map c 0 = const ‚Ñù c :=
funext $ Œª Œ∏, circle_map_eq_center_iff.2 rfl

lemma circle_map_ne_center {c : ‚ÑÇ} {R : ‚Ñù} (hR : R ‚â† 0) {Œ∏ : ‚Ñù} : circle_map c R Œ∏ ‚â† c :=
mt circle_map_eq_center_iff.1 hR

lemma has_deriv_at_circle_map (c : ‚ÑÇ) (R : ‚Ñù) (Œ∏ : ‚Ñù) :
  has_deriv_at (circle_map c R) (circle_map 0 R Œ∏ * I) Œ∏ :=
by simpa only [mul_assoc, one_mul, of_real_clm_apply, circle_map, of_real_one, zero_add]
 using ((of_real_clm.has_deriv_at.mul_const I).cexp_real.const_mul (R : ‚ÑÇ)).const_add c

/- TODO: prove `times_cont_diff ‚Ñù (circle_map c R)`. This needs a version of `times_cont_diff.mul`
for multiplication in a normed algebra over the base field. -/

lemma differentiable_circle_map (c : ‚ÑÇ) (R : ‚Ñù) :
  differentiable ‚Ñù (circle_map c R) :=
Œª Œ∏, (has_deriv_at_circle_map c R Œ∏).differentiable_at

@[continuity] lemma continuous_circle_map (c : ‚ÑÇ) (R : ‚Ñù) : continuous (circle_map c R) :=
(differentiable_circle_map c R).continuous

@[measurability] lemma measurable_circle_map (c : ‚ÑÇ) (R : ‚Ñù) : measurable (circle_map c R) :=
(continuous_circle_map c R).measurable

@[simp] lemma deriv_circle_map (c : ‚ÑÇ) (R : ‚Ñù) (Œ∏ : ‚Ñù) :
  deriv (circle_map c R) Œ∏ = circle_map 0 R Œ∏ * I :=
(has_deriv_at_circle_map _ _ _).deriv

lemma deriv_circle_map_eq_zero_iff {c : ‚ÑÇ} {R : ‚Ñù} {Œ∏ : ‚Ñù} :
  deriv (circle_map c R) Œ∏ = 0 ‚Üî R = 0 :=
by simp [I_ne_zero]

lemma deriv_circle_map_ne_zero {c : ‚ÑÇ} {R : ‚Ñù} {Œ∏ : ‚Ñù} (hR : R ‚â† 0) :
  deriv (circle_map c R) Œ∏ ‚â† 0 :=
mt deriv_circle_map_eq_zero_iff.1 hR

lemma lipschitz_with_circle_map (c : ‚ÑÇ) (R : ‚Ñù) :
  lipschitz_with R.nnabs (circle_map c R) :=
lipschitz_with_of_nnnorm_deriv_le (differentiable_circle_map _ _) $ Œª Œ∏,
  nnreal.coe_le_coe.1 $ by simp

/-!
### Integrability of a function on a circle
-/

/-- We say that a function `f : ‚ÑÇ ‚Üí E` is integrable on the circle with center `c` and radius `R` if
the function `f ‚àò circle_map c R` is integrable on `[0, 2œÄ]`.

Note that the actual function used in the definition of `circle_integral` is
`(deriv (circle_map c R) Œ∏) ‚Ä¢ f (circle_map c R Œ∏)`. Integrability of this function is equivalent
to integrability of `f ‚àò circle_map c R` whenever `R ‚â† 0`. -/
def circle_integrable (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) (R : ‚Ñù) : Prop :=
interval_integrable (Œª Œ∏ : ‚Ñù, f (circle_map c R Œ∏)) volume 0 (2 * œÄ)

@[simp] lemma circle_integrable_const (a : E) (c : ‚ÑÇ) (R : ‚Ñù) :
  circle_integrable (Œª _, a) c R :=
interval_integrable_const

namespace circle_integrable

variables {f g : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù}

lemma add [borel_space E] [second_countable_topology E]
  (hf : circle_integrable f c R) (hg : circle_integrable g c R) :
  circle_integrable (f + g) c R :=
hf.add hg

lemma neg [borel_space E] (hf : circle_integrable f c R) : circle_integrable (-f) c R := hf.neg

/-- The function we actually integrate over `[0, 2œÄ]` in the definition of `circle_integral` is
integrable. -/
lemma out [borel_space E] [normed_space ‚ÑÇ E] [second_countable_topology E]
  (hf : circle_integrable f c R) :
  interval_integrable (Œª Œ∏ : ‚Ñù, deriv (circle_map c R) Œ∏ ‚Ä¢ f (circle_map c R Œ∏)) volume 0 (2 * œÄ) :=
begin
  simp only [circle_integrable, deriv_circle_map, interval_integrable_iff] at *,
  refine (hf.norm.const_mul (|R|)).mono' _ _,
  { exact (((continuous_circle_map _ _).ae_measurable _).mul_const I).smul hf.ae_measurable },
  { simp [norm_smul] }
end

end circle_integrable

@[simp] lemma circle_integrable_zero_radius {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} : circle_integrable f c 0 :=
by simp [circle_integrable]

lemma circle_integrable_iff [borel_space E] [normed_space ‚ÑÇ E] [second_countable_topology E]
  {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} (h‚ÇÄ : R ‚â† 0) : circle_integrable f c R ‚Üî
  interval_integrable (Œª Œ∏ : ‚Ñù, deriv (circle_map c R) Œ∏ ‚Ä¢ f (circle_map c R Œ∏)) volume 0 (2 * œÄ) :=
begin
  refine ‚ü®Œª h, h.out, Œª h, _‚ü©,
  simp only [circle_integrable, interval_integrable_iff, deriv_circle_map] at h ‚ä¢,
  refine (h.norm.const_mul (|R|‚Åª¬π)).mono' _ _,
  { have H : ‚àÄ {Œ∏}, circle_map 0 R Œ∏ * I ‚â† 0 := Œª Œ∏, by simp [h‚ÇÄ, I_ne_zero],
    simpa only [inv_smul_smul‚ÇÄ H]
      using (((continuous_circle_map 0 R).ae_measurable _).mul_const I).inv.smul h.ae_measurable },
  { simp [norm_smul, h‚ÇÄ] },
end

lemma continuous_on.circle_integrable' [borel_space E] {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù}
  (hf : continuous_on f (sphere c (|R|))) :
  circle_integrable f c R :=
(hf.comp_continuous (continuous_circle_map _ _)
  (circle_map_mem_sphere' _ _)).interval_integrable _ _

lemma continuous_on.circle_integrable [borel_space E] {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} (hR : 0 ‚â§ R)
  (hf : continuous_on f (sphere c R)) :
  circle_integrable f c R :=
continuous_on.circle_integrable' $ (_root_.abs_of_nonneg hR).symm ‚ñ∏ hf

/-- The function `Œª z, (z - w) ^ n`, `n : ‚Ñ§`, is circle integrable on the circle with center `c` and
radius `|R|` if and only if `R = 0` or `0 ‚â§ n`, or `w` does not belong to this circle. -/
@[simp] lemma circle_integrable_sub_zpow_iff {c w : ‚ÑÇ} {R : ‚Ñù} {n : ‚Ñ§} :
  circle_integrable (Œª z, (z - w) ^ n) c R ‚Üî R = 0 ‚à® 0 ‚â§ n ‚à® w ‚àâ sphere c (|R|) :=
begin
  split,
  { intro h, contrapose! h, rcases h with ‚ü®hR, hn, hw‚ü©,
    simp only [circle_integrable_iff hR, deriv_circle_map],
    rw ‚Üê image_circle_map_Ioc at hw, rcases hw with ‚ü®Œ∏, hŒ∏, rfl‚ü©,
    replace hŒ∏ : Œ∏ ‚àà [0, 2 * œÄ], from Icc_subset_interval (Ioc_subset_Icc_self hŒ∏),
    refine not_interval_integrable_of_sub_inv_is_O_punctured _ real.two_pi_pos.ne hŒ∏,
    set f : ‚Ñù ‚Üí ‚ÑÇ := Œª Œ∏', circle_map c R Œ∏' - circle_map c R Œ∏,
    have : ‚àÄ·∂† Œ∏' in ùìù[‚â†] Œ∏, f Œ∏' ‚àà ball (0 : ‚ÑÇ) 1 \ {0},
    { suffices : ‚àÄ·∂† z in ùìù[‚â†] (circle_map c R Œ∏), z - circle_map c R Œ∏ ‚àà ball (0 : ‚ÑÇ) 1 \ {0},
        from ((differentiable_circle_map c R Œ∏).has_deriv_at.tendsto_punctured_nhds
          (deriv_circle_map_ne_zero hR)).eventually this,
      filter_upwards [self_mem_nhds_within,
        mem_nhds_within_of_mem_nhds (ball_mem_nhds _ zero_lt_one)],
      simp [dist_eq, sub_eq_zero] { contextual := tt } },
    refine ((((has_deriv_at_circle_map c R Œ∏).is_O_sub).mono inf_le_left).inv_rev
      (this.mono (Œª Œ∏', and.right))).trans _,
    refine is_O.of_bound (|R|)‚Åª¬π (this.mono $ Œª Œ∏' hŒ∏', _),
    set x := abs (f Œ∏'),
    suffices : x‚Åª¬π ‚â§ x ^ n, by simpa [inv_mul_cancel_left‚ÇÄ, mt _root_.abs_eq_zero.1 hR],
    have : x ‚àà Ioo (0 : ‚Ñù) 1, by simpa [and.comm, x] using hŒ∏',
    rw ‚Üê zpow_neg_one,
    refine (zpow_strict_anti this.1 this.2).le_iff_le.2 (int.lt_add_one_iff.1 _), exact hn },
  { rintro (rfl|H),
    exacts [circle_integrable_zero_radius,
      ((continuous_on_id.sub continuous_on_const).zpow _ $ Œª z hz, H.symm.imp_left $
        Œª hw, sub_ne_zero.2 $ ne_of_mem_of_not_mem hz hw).circle_integrable'] },
end

@[simp] lemma circle_integrable_sub_inv_iff {c w : ‚ÑÇ} {R : ‚Ñù} :
  circle_integrable (Œª z, (z - w)‚Åª¬π) c R ‚Üî R = 0 ‚à® w ‚àâ sphere c (|R|) :=
by { simp only [‚Üê zpow_neg_one, circle_integrable_sub_zpow_iff], norm_num }

variables [normed_space ‚ÑÇ E] [complete_space E] [borel_space E] [second_countable_topology E]

/-- Definition for $\oint_{|z-c|=R} f(z)\,dz$. -/
def circle_integral (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) (R : ‚Ñù) : E :=
‚à´ (Œ∏ : ‚Ñù) in 0..2 * œÄ, deriv (circle_map c R) Œ∏ ‚Ä¢ f (circle_map c R Œ∏)

notation `‚àÆ` binders ` in ` `C(` c `, ` R `)` `, ` r:(scoped:60 f, circle_integral f c R) := r

namespace circle_integral

@[simp] lemma integral_radius_zero (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) : ‚àÆ z in C(c, 0), f z = 0 :=
by simp [circle_integral]

lemma integral_congr {f g : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} (hR : 0 ‚â§ R) (h : eq_on f g (sphere c R)) :
  ‚àÆ z in C(c, R), f z = ‚àÆ z in C(c, R), g z :=
interval_integral.integral_congr $ Œª Œ∏ hŒ∏, by simp only [h (circle_map_mem_sphere _ hR _)]

lemma integral_undef {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} (hf : ¬¨circle_integrable f c R) :
  ‚àÆ z in C(c, R), f z = 0 :=
begin
  rcases eq_or_ne R 0 with rfl|h0, { simp },
  exact interval_integral.integral_undef (mt (circle_integrable_iff h0).mpr hf)
end

lemma integral_sub {f g : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} (hf : circle_integrable f c R)
  (hg : circle_integrable g c R) :
  ‚àÆ z in C(c, R), f z - g z = (‚àÆ z in C(c, R), f z) - ‚àÆ z in C(c, R), g z :=
by simp only [circle_integral, smul_sub, interval_integral.integral_sub hf.out hg.out]

lemma norm_integral_le_of_norm_le_const' {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R C : ‚Ñù}
  (hf : ‚àÄ z ‚àà sphere c (|R|), ‚à•f z‚à• ‚â§ C) :
  ‚à•‚àÆ z in C(c, R), f z‚à• ‚â§ 2 * œÄ * |R| * C :=
calc ‚à•‚àÆ z in C(c, R), f z‚à• ‚â§ |R| * C * |2 * œÄ - 0| :
  interval_integral.norm_integral_le_of_norm_le_const $ Œª Œ∏ _,
    (calc ‚à•deriv (circle_map c R) Œ∏ ‚Ä¢ f (circle_map c R Œ∏)‚à• = |R| * ‚à•f (circle_map c R Œ∏)‚à• :
      by simp [norm_smul]
    ... ‚â§ |R| * C : mul_le_mul_of_nonneg_left (hf _ $ circle_map_mem_sphere' _ _ _)
      (_root_.abs_nonneg _))
... = 2 * œÄ * |R| * C :
  by { rw [sub_zero, _root_.abs_of_pos real.two_pi_pos], ac_refl }

lemma norm_integral_le_of_norm_le_const {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R C : ‚Ñù} (hR : 0 ‚â§ R)
  (hf : ‚àÄ z ‚àà sphere c R, ‚à•f z‚à• ‚â§ C) :
  ‚à•‚àÆ z in C(c, R), f z‚à• ‚â§ 2 * œÄ * R * C :=
have |R| = R, from _root_.abs_of_nonneg hR,
calc ‚à•‚àÆ z in C(c, R), f z‚à• ‚â§ 2 * œÄ * |R| * C :
  norm_integral_le_of_norm_le_const' $ by rwa this
... = 2 * œÄ * R * C : by rw this

@[simp] lemma integral_smul {ùïú : Type*} [is_R_or_C ùïú] [normed_space ùïú E] [smul_comm_class ùïú ‚ÑÇ E]
  (a : ùïú) (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) (R : ‚Ñù) :
  ‚àÆ z in C(c, R), a ‚Ä¢ f z = a ‚Ä¢ ‚àÆ z in C(c, R), f z :=
by simp only [circle_integral, ‚Üê smul_comm a, interval_integral.integral_smul]

@[simp] lemma integral_smul_const (f : ‚ÑÇ ‚Üí ‚ÑÇ) (a : E) (c : ‚ÑÇ) (R : ‚Ñù) :
  ‚àÆ z in C(c, R), (f z ‚Ä¢ a) = (‚àÆ z in C(c, R), f z) ‚Ä¢ a :=
by simp only [circle_integral, interval_integral.integral_smul_const, ‚Üê smul_assoc]

@[simp] lemma integral_const_mul (a : ‚ÑÇ) (f : ‚ÑÇ ‚Üí ‚ÑÇ) (c : ‚ÑÇ) (R : ‚Ñù) :
  ‚àÆ z in C(c, R), a * f z = a * ‚àÆ z in C(c, R), f z :=
integral_smul a f c R

@[simp] lemma integral_sub_center_inv (c : ‚ÑÇ) {R : ‚Ñù} (hR : R ‚â† 0) :
  ‚àÆ z in C(c, R), (z - c)‚Åª¬π = 2 * œÄ * I :=
by simp [circle_integral, ‚Üê div_eq_mul_inv, mul_div_cancel_left _ (circle_map_ne_center hR)]

/-- If `f' : ‚ÑÇ ‚Üí E` is a derivative of a complex differentiable function on the circle
`metric.sphere c |R|`, then `‚àÆ z in C(c, R), f' z = 0`. -/
lemma integral_eq_zero_of_has_deriv_within_at' {f f' : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù}
  (h : ‚àÄ z ‚àà sphere c (|R|), has_deriv_within_at f (f' z) (sphere c (|R|)) z) :
  ‚àÆ z in C(c, R), f' z = 0 :=
begin
  by_cases hi : circle_integrable f' c R,
  { rw ‚Üê sub_eq_zero.2 ((periodic_circle_map c R).comp f).eq,
    refine interval_integral.integral_eq_sub_of_has_deriv_at (Œª Œ∏ hŒ∏, _) hi.out,
    exact (h _ (circle_map_mem_sphere' _ _ _)).scomp_has_deriv_at Œ∏
      (differentiable_circle_map _ _ _).has_deriv_at (circle_map_mem_sphere' _ _) },
  { exact integral_undef hi }
end

/-- If `f' : ‚ÑÇ ‚Üí E` is a derivative of a complex differentiable function on the circle
`metric.sphere c R`, then `‚àÆ z in C(c, R), f' z = 0`. -/
lemma integral_eq_zero_of_has_deriv_within_at {f f' : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} (hR : 0 ‚â§ R)
  (h : ‚àÄ z ‚àà sphere c R, has_deriv_within_at f (f' z) (sphere c R) z) :
  ‚àÆ z in C(c, R), f' z = 0 :=
integral_eq_zero_of_has_deriv_within_at' $ (_root_.abs_of_nonneg hR).symm.subst h

/-- If `n < 0` and `|w - c| = |R|`, then `(z - w) ^ n` is not circle integrable on the circle with
center `c` and radius `(|R|)`, so the integral `‚àÆ z in C(c, R), (z - w) ^ n` is equal to zero. -/
lemma integral_sub_zpow_of_undef {n : ‚Ñ§} {c w : ‚ÑÇ} {R : ‚Ñù} (hn : n < 0) (hw : w ‚àà sphere c (|R|)) :
  ‚àÆ z in C(c, R), (z - w) ^ n = 0 :=
begin
  rcases eq_or_ne R 0 with rfl|h0, { apply integral_radius_zero },
  apply integral_undef,
  simp [circle_integrable_sub_zpow_iff, *]
end

/-- If `n ‚â† -1` is an integer number, then the integral of `(z - w) ^ n` over the circle equals
zero. -/
lemma integral_sub_zpow_of_ne {n : ‚Ñ§} (hn : n ‚â† -1) (c w : ‚ÑÇ) (R : ‚Ñù) :
  ‚àÆ z in C(c, R), (z - w) ^ n = 0 :=
begin
  rcases em (w ‚àà sphere c (|R|) ‚àß n < -1) with ‚ü®hw, hn‚ü©|H,
  { exact integral_sub_zpow_of_undef (hn.trans dec_trivial) hw },
  push_neg at H,
  have hd : ‚àÄ z, (z ‚â† w ‚à® -1 ‚â§ n) ‚Üí has_deriv_at (Œª z, (z - w) ^ (n + 1) / (n + 1)) ((z - w) ^ n) z,
  { intros z hne,
    convert ((has_deriv_at_zpow (n + 1) _ (hne.imp _ _)).comp z
      ((has_deriv_at_id z).sub_const w)).div_const _ using 1,
    { have hn' : (n + 1 : ‚ÑÇ) ‚â† 0,
        by rwa [ne, ‚Üê eq_neg_iff_add_eq_zero, ‚Üê int.cast_one, ‚Üê int.cast_neg, int.cast_inj],
      simp [mul_assoc, mul_div_cancel_left _ hn'] },
    exacts [sub_ne_zero.2, neg_le_iff_add_nonneg.1] },
  refine integral_eq_zero_of_has_deriv_within_at' (Œª z hz, (hd z _).has_deriv_within_at),
  exact (ne_or_eq z w).imp_right (Œª h, H $ h ‚ñ∏ hz)
end

end circle_integral

/-- The power series that is equal to
$\sum_{n=0}^{\infty} \oint_{|z-c|=R} \left(\frac{w-c}{z - c}\right)^n \frac{1}{z-c}f(z)\,dz$ at
`w - c`. The coefficients of this power series depend only on `f ‚àò circle_map c R`, and the power
series converges to `f w` if `f` is differentiable on the closed ball `metric.closed_ball c R` and
`w` belongs to the corresponding open ball. For any circle integrable function `f`, this power
series converges to the Cauchy integral for `f`. -/
def cauchy_power_series (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) (R : ‚Ñù) :
  formal_multilinear_series ‚ÑÇ ‚ÑÇ E :=
Œª n, continuous_multilinear_map.mk_pi_field ‚ÑÇ _ $
  (2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - c)‚Åª¬π ^ n ‚Ä¢ (z - c)‚Åª¬π ‚Ä¢ f z

lemma cauchy_power_series_apply (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) (R : ‚Ñù) (n : ‚Ñï) (w : ‚ÑÇ) :
  cauchy_power_series f c R n (Œª _, w) =
    (2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (w / (z - c)) ^ n ‚Ä¢ (z - c)‚Åª¬π ‚Ä¢ f z :=
by simp only [cauchy_power_series, continuous_multilinear_map.mk_pi_field_apply, fin.prod_const,
  div_eq_mul_inv, mul_pow, mul_smul, circle_integral.integral_smul, ‚Üê smul_comm (w ^ n)]

lemma norm_cauchy_power_series_le (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) (R : ‚Ñù) (n : ‚Ñï) :
  ‚à•cauchy_power_series f c R n‚à• ‚â§
    (2 * œÄ)‚Åª¬π * (‚à´ Œ∏ : ‚Ñù in 0..2*œÄ, ‚à•f (circle_map c R Œ∏)‚à•) * (|R|‚Åª¬π) ^ n :=
calc ‚à•cauchy_power_series f c R n‚à•
    = (2 * œÄ)‚Åª¬π * ‚à•‚àÆ z in C(c, R), (z - c)‚Åª¬π ^ n ‚Ä¢ (z - c)‚Åª¬π ‚Ä¢ f z‚à• :
  by simp [cauchy_power_series, norm_smul, real.pi_pos.le]
... ‚â§ (2 * œÄ)‚Åª¬π * ‚à´ Œ∏ in 0..2*œÄ, ‚à•deriv (circle_map c R) Œ∏ ‚Ä¢ (circle_map c R Œ∏ - c)‚Åª¬π ^ n ‚Ä¢
  (circle_map c R Œ∏ - c)‚Åª¬π ‚Ä¢ f (circle_map c R Œ∏)‚à• :
  mul_le_mul_of_nonneg_left (interval_integral.norm_integral_le_integral_norm real.two_pi_pos.le)
    (by simp [real.pi_pos.le])
... = (2 * œÄ)‚Åª¬π * (|R|‚Åª¬π ^ n * (|R| * (|R|‚Åª¬π * ‚à´ (x : ‚Ñù) in 0..2 * œÄ, ‚à•f (circle_map c R x)‚à•))) :
  by simp [norm_smul, mul_left_comm (|R|)]
... ‚â§ (2 * œÄ)‚Åª¬π * (‚à´ Œ∏ : ‚Ñù in 0..2*œÄ, ‚à•f (circle_map c R Œ∏)‚à•) * |R|‚Åª¬π ^ n :
  begin
    rcases eq_or_ne R 0 with rfl|hR,
    { cases n; simp [real.two_pi_pos] },
    { rw [mul_inv_cancel_left‚ÇÄ, mul_assoc, mul_comm (|R|‚Åª¬π ^ n)],
      rwa [ne.def, _root_.abs_eq_zero] }
  end

lemma le_radius_cauchy_power_series (f : ‚ÑÇ ‚Üí E) (c : ‚ÑÇ) (R : ‚Ñù‚â•0) :
  ‚ÜëR ‚â§ (cauchy_power_series f c R).radius :=
begin
  refine (cauchy_power_series f c R).le_radius_of_bound
    ((2 * œÄ)‚Åª¬π * (‚à´ Œ∏ : ‚Ñù in 0..2*œÄ, ‚à•f (circle_map c R Œ∏)‚à•)) (Œª n, _),
  refine (mul_le_mul_of_nonneg_right (norm_cauchy_power_series_le _ _ _ _)
    (pow_nonneg R.coe_nonneg _)).trans _,
  rw [_root_.abs_of_nonneg R.coe_nonneg],
  cases eq_or_ne (R ^ n : ‚Ñù) 0 with hR hR,
  { rw [hR, mul_zero],
    exact mul_nonneg (inv_nonneg.2 real.two_pi_pos.le)
      (interval_integral.integral_nonneg real.two_pi_pos.le (Œª _ _, norm_nonneg _)) },
  { rw [inv_pow‚ÇÄ, inv_mul_cancel_right‚ÇÄ hR] }
end

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R` multiplied
by `2œÄI` converges to the integral `‚àÆ z in C(c, R), (z - w)‚Åª¬π ‚Ä¢ f z` on the open disc
`metric.ball c R`. -/
lemma has_sum_two_pi_I_cauchy_power_series_integral {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} {w : ‚ÑÇ}
  (hf : circle_integrable f c R) (hw : abs w < R) :
  has_sum (Œª n : ‚Ñï, ‚àÆ z in C(c, R), (w / (z - c)) ^ n ‚Ä¢ (z - c)‚Åª¬π ‚Ä¢ f z)
    (‚àÆ z in C(c, R), (z - (c + w))‚Åª¬π ‚Ä¢ f z) :=
begin
  have hR : 0 < R := (abs_nonneg w).trans_lt hw,
  have hwR : abs w / R ‚àà Ico (0 : ‚Ñù) 1,
    from ‚ü®div_nonneg (abs_nonneg w) hR.le, (div_lt_one hR).2 hw‚ü©,
  refine interval_integral.has_sum_integral_of_dominated_convergence
    (Œª n Œ∏, ‚à•f (circle_map c R Œ∏)‚à• * (abs w / R) ^ n) (Œª n, _) (Œª n, _) _ _ _,
  { simp only [deriv_circle_map], have := hf.def.1, measurability },
  { simp [norm_smul, abs_of_pos hR, mul_left_comm R, mul_inv_cancel_left‚ÇÄ hR.ne', mul_comm (‚à•_‚à•)] },
  { exact eventually_of_forall (Œª _ _, (summable_geometric_of_lt_1 hwR.1 hwR.2).mul_left _) },
  { simpa only [tsum_mul_left, tsum_geometric_of_lt_1 hwR.1 hwR.2]
      using hf.norm.mul_continuous_on continuous_on_const },
  { refine eventually_of_forall (Œª Œ∏ hŒ∏, has_sum.const_smul _),
    simp only [smul_smul],
    refine has_sum.smul_const _,
    have : ‚à•w / (circle_map c R Œ∏ - c)‚à• < 1, by simpa [abs_of_pos hR] using hwR.2,
    convert (has_sum_geometric_of_norm_lt_1 this).mul_right _,
    simp [‚Üê sub_sub, ‚Üê mul_inv‚ÇÄ, sub_mul, div_mul_cancel _ (circle_map_ne_center hR.ne')] }
end

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R`, `R > 0`,
converges to the Cauchy integral `(2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - w)‚Åª¬π ‚Ä¢ f z` on the open
disc `metric.ball c R`. -/
lemma has_sum_cauchy_power_series_integral {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} {w : ‚ÑÇ}
  (hf : circle_integrable f c R) (hw : abs w < R) :
  has_sum (Œª n, cauchy_power_series f c R n (Œª _, w))
    ((2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - (c + w))‚Åª¬π ‚Ä¢ f z) :=
begin
  simp only [cauchy_power_series_apply],
  exact (has_sum_two_pi_I_cauchy_power_series_integral hf hw).const_smul
end

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R`, `R > 0`,
converges to the Cauchy integral `(2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - w)‚Åª¬π ‚Ä¢ f z` on the open
disc `metric.ball c R`. -/
lemma sum_cauchy_power_series_eq_integral {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù} {w : ‚ÑÇ}
  (hf : circle_integrable f c R) (hw : abs w < R) :
  (cauchy_power_series f c R).sum w =
    ((2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - (c + w))‚Åª¬π ‚Ä¢ f z) :=
(has_sum_cauchy_power_series_integral hf hw).tsum_eq

/-- For any circle integrable function `f`, the power series `cauchy_power_series f c R`, `R > 0`,
converges to the Cauchy integral `(2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - w)‚Åª¬π ‚Ä¢ f z` on the open
disc `metric.ball c R`. -/
lemma has_fpower_series_on_cauchy_integral {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ} {R : ‚Ñù‚â•0}
  (hf : circle_integrable f c R) (hR : 0 < R) :
  has_fpower_series_on_ball
    (Œª w, (2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - w)‚Åª¬π ‚Ä¢ f z)
    (cauchy_power_series f c R) c R :=
{ r_le := le_radius_cauchy_power_series _ _ _,
  r_pos := ennreal.coe_pos.2 hR,
  has_sum := Œª y hy,
    begin
      refine has_sum_cauchy_power_series_integral hf _,
      rw [‚Üê norm_eq_abs, ‚Üê coe_nnnorm, nnreal.coe_lt_coe, ‚Üê ennreal.coe_lt_coe],
      exact mem_emetric_ball_zero_iff.1 hy
    end }

namespace circle_integral

/-- Integral $\oint_{|z-c|=R} \frac{dz}{z-w}=2œÄi$ whenever $|w-c|<R$. -/
lemma integral_sub_inv_of_mem_ball {c w : ‚ÑÇ} {R : ‚Ñù} (hw : w ‚àà ball c R) :
  ‚àÆ z in C(c, R), (z - w)‚Åª¬π = 2 * œÄ * I :=
begin
  have hR : 0 < R := dist_nonneg.trans_lt hw,
  suffices H : has_sum (Œª n : ‚Ñï, ‚àÆ z in C(c, R), ((w - c) / (z - c)) ^ n * (z - c)‚Åª¬π) (2 * œÄ * I),
  { have A : circle_integrable (Œª _, (1 : ‚ÑÇ)) c R, from continuous_on_const.circle_integrable',
    refine (H.unique _).symm,
    simpa only [smul_eq_mul, mul_one, add_sub_cancel'_right]
      using has_sum_two_pi_I_cauchy_power_series_integral A hw },
  have H : ‚àÄ n : ‚Ñï, n ‚â† 0 ‚Üí ‚àÆ z in C(c, R), (z - c) ^ (-n - 1 : ‚Ñ§) = 0,
  { refine Œª n hn, integral_sub_zpow_of_ne _ _ _ _, simpa },
  have : ‚àÆ z in C(c, R), ((w - c) / (z - c)) ^ 0 * (z - c)‚Åª¬π = 2 * œÄ * I, by simp [hR.ne'],
  refine this ‚ñ∏ has_sum_single _ (Œª n hn, _),
  simp only [div_eq_mul_inv, mul_pow, integral_const_mul, mul_assoc],
  rw [(integral_congr hR.le (Œª z hz, _)).trans (H n hn), mul_zero],
  rw [‚Üê pow_succ', ‚Üê zpow_coe_nat, inv_zpow‚ÇÄ, ‚Üê zpow_neg‚ÇÄ, int.coe_nat_succ, neg_add,
    sub_eq_add_neg _ (1 : ‚Ñ§)]
end

end circle_integral
