/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import analysis.box_integral.divergence_theorem
import analysis.box_integral.integrability
import measure_theory.integral.interval_integral
import data.set.intervals.monotone

/-!
# Divergence theorem for Bochner integral

In this file we prove the Divergence theorem for Bochner integral on a box in
`â„â¿âºÂ¹ = fin (n + 1) â†’ â„`. More precisely, we prove the following theorem.

Let `E` be a complete normed space with second countably topology. If `f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹` is
differentiable on a rectangular box `[a, b] : set â„â¿âºÂ¹`, `a â‰¤ b`, with derivative
`f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹` and the divergence `Î» x, âˆ‘ i, f' x eáµ¢ i` is integrable on `[a, b]`,
where `eáµ¢ = pi.single i 1` is the `i`-th basis vector, then its integral is equal to the sum of
integrals of `f` over the faces of `[a, b]`, taken with appropriate signs. Moreover, the same is
true if the function is not differentiable but continuous at countably many points of `[a, b]`.

Once we prove the general theorem, we deduce corollaries for functions `â„ â†’ E` and pairs of
functions `(â„ Ã— â„) â†’ E`.

## Notations

We use the following local notation to make the statement more readable. Note that the documentation
website shows the actual terms, not those abbreviated using local notations.

* `â„â¿`, `â„â¿âºÂ¹`, `Eâ¿âºÂ¹`: `fin n â†’ â„`, `fin (n + 1) â†’ â„`, `fin (n + 1) â†’ E`;
* `face i`: the `i`-th face of the box `[a, b]` as a closed segment in `â„â¿`, namely `[a âˆ˜
  fin.succ_above i, b âˆ˜ fin.succ_above i]`;
* `e i` : `i`-th basis vector `pi.single i 1`;
* `front_face i`, `back_face i`: embeddings `â„â¿ â†’ â„â¿âºÂ¹` corresponding to the front face
  `{x | x i = b i}` and back face `{x | x i = a i}` of the box `[a, b]`, respectively.
  They are given by `fin.insert_nth i (b i)` and `fin.insert_nth i (a i)`.

## TODO

* Add a version that assumes existence and integrability of partial derivatives.

## Tags

divergence theorem, Bochner integral
-/

open set finset topological_space function box_integral measure_theory filter
open_locale big_operators classical topological_space interval

universes u

namespace measure_theory

variables {E : Type u} [normed_group E] [normed_space â„ E] [measurable_space E] [borel_space E]
  [second_countable_topology E] [complete_space E]

section
variables {n : â„•}

local notation `â„â¿` := fin n â†’ â„
local notation `â„â¿âºÂ¹` := fin (n + 1) â†’ â„
local notation `Eâ¿âºÂ¹` := fin (n + 1) â†’ E
local notation `e` i := pi.single i 1

section

-- Reformulate `has_integral_bot_divergence_of_forall_has_deriv_within_at` for Bochner integral
lemma integral_divergence_of_has_fderiv_within_at_off_countable_auxâ‚ (I : box (fin (n + 1)))
  (f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹) (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹) (s : set â„â¿âºÂ¹) (hs : countable s)
  (Hc : continuous_on f I.Icc) (Hd : âˆ€ x âˆˆ I.Icc \ s, has_fderiv_within_at f (f' x) I.Icc x)
  (Hi : integrable_on (Î» x, âˆ‘ i, f' x (e i) i) I.Icc) :
  âˆ« x in I.Icc, âˆ‘ i, f' x (e i) i =
    âˆ‘ i : fin (n + 1),
      ((âˆ« x in (I.face i).Icc, f (i.insert_nth (I.upper i) x) i) -
        âˆ« x in (I.face i).Icc, f (i.insert_nth (I.lower i) x) i) :=
begin
  simp only [â† set_integral_congr_set_ae (box.coe_ae_eq_Icc _)],
  have A := ((Hi.mono_set box.coe_subset_Icc).has_box_integral âŠ¥ rfl),
  have B := has_integral_bot_divergence_of_forall_has_deriv_within_at I f f' (s âˆ© I.Icc)
    (hs.mono (inter_subset_left _ _)) (Î» x hx, Hc _ hx.2)
    (Î» x hx, Hd _ âŸ¨hx.1, Î» h, hx.2 âŸ¨h, hx.1âŸ©âŸ©),
  rw continuous_on_pi at Hc,
  refine (A.unique B).trans (sum_congr rfl $ Î» i hi, _),
  refine congr_arg2 has_sub.sub _ _,
  { have := box.continuous_on_face_Icc (Hc i) (set.right_mem_Icc.2 (I.lower_le_upper i)),
    have := (this.integrable_on_compact (box.is_compact_Icc _)).mono_set box.coe_subset_Icc,
    exact (this.has_box_integral âŠ¥ rfl).integral_eq, apply_instance },
  { have := box.continuous_on_face_Icc (Hc i) (set.left_mem_Icc.2 (I.lower_le_upper i)),
    have := (this.integrable_on_compact (box.is_compact_Icc _)).mono_set box.coe_subset_Icc,
    exact (this.has_box_integral âŠ¥ rfl).integral_eq, apply_instance }
end

-- Prove with weaker assumptions
lemma integral_divergence_of_has_fderiv_within_at_off_countable_auxâ‚‚ (I : box (fin (n + 1)))
  (f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹) (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹) (s : set â„â¿âºÂ¹) (hs : countable s)
  (Hc : continuous_on f I.Icc) (Hd : âˆ€ x âˆˆ I.Ioo \ s, has_fderiv_at f (f' x) x)
  (Hi : integrable_on (Î» x, âˆ‘ i, f' x (e i) i) I.Icc) :
  âˆ« x in I.Icc, âˆ‘ i, f' x (e i) i =
    âˆ‘ i : fin (n + 1),
      ((âˆ« x in (I.face i).Icc, f (i.insert_nth (I.upper i) x) i) -
        âˆ« x in (I.face i).Icc, f (i.insert_nth (I.lower i) x) i) :=
begin
  rcases I.exists_seq_mono_tendsto with âŸ¨J, hJ_sub, hJl, hJuâŸ©,
  have hJ_sub' : âˆ€ k, (J k).Icc âŠ† I.Icc, from Î» k, (hJ_sub k).trans I.Ioo_subset_Icc,
  have hJ_le : âˆ€ k, J k â‰¤ I, from Î» k, box.le_iff_Icc.2 (hJ_sub' k),
  have HcJ : âˆ€ k, continuous_on f (J k).Icc, from Î» k, Hc.mono (hJ_sub' k),
  have HdJ : âˆ€ k (x âˆˆ (J k).Icc \ s), has_fderiv_within_at f (f' x) (J k).Icc x,
    from Î» k x hx, (Hd x âŸ¨hJ_sub k hx.1, hx.2âŸ©).has_fderiv_within_at,
  have HiJ : âˆ€ k, integrable_on (Î» x, âˆ‘ i, f' x (e i) i) (J k).Icc,
    from Î» k, Hi.mono_set (hJ_sub' k),
  have HJ_eq := Î» k, integral_divergence_of_has_fderiv_within_at_off_countable_auxâ‚ (J k) f f' s hs
    (HcJ k) (HdJ k) (HiJ k),
  have hI_tendsto : tendsto (Î» k, âˆ« x in (J k).Icc, âˆ‘ i, f' x (e i) i) at_top
    (ğ“ (âˆ« x in I.Icc, âˆ‘ i, f' x (e i) i)),
  { simp only [integrable_on, â† measure.restrict_congr_set (box.Ioo_ae_eq_Icc _)] at Hi âŠ¢,
    rw â† box.Union_Ioo_of_tendsto J.monotone hJl hJu at Hi âŠ¢,
    exact tendsto_set_integral_of_monotone (Î» k, (J k).measurable_set_Ioo)
      (box.Ioo.comp J).monotone Hi },
  refine tendsto_nhds_unique_of_eventually_eq hI_tendsto _ (eventually_of_forall HJ_eq),
  clear hI_tendsto,
  rw tendsto_pi_nhds at hJl hJu,
  suffices : âˆ€ (i : fin (n + 1)) (c : â„• â†’ â„) d,
    (âˆ€ k, c k âˆˆ Icc (I.lower i) (I.upper i)) â†’ tendsto c at_top (ğ“ d) â†’
      tendsto (Î» k, âˆ« x in ((J k).face i).Icc, f (i.insert_nth (c k) x) i) at_top
        (ğ“ $ âˆ« x in (I.face i).Icc, f (i.insert_nth d x) i),
  { rw box.Icc_eq_pi at hJ_sub',
    refine tendsto_finset_sum _ (Î» i hi, (this _ _ _ _ (hJu _)).sub (this _ _ _ _ (hJl _))),
    exacts [Î» k, hJ_sub' k (J k).upper_mem_Icc _ trivial,
      Î» k, hJ_sub' k (J k).lower_mem_Icc _ trivial] },
  intros i c d hc hcd,
  have hd : d âˆˆ Icc (I.lower i) (I.upper i),
    from is_closed_Icc.mem_of_tendsto hcd (eventually_of_forall hc),
  have Hic : âˆ€ k, integrable_on (Î» x, f (i.insert_nth (c k) x) i) (I.face i).Icc,
    from Î» k, (box.continuous_on_face_Icc ((continuous_apply i).comp_continuous_on Hc)
      (hc k)).integrable_on_Icc,
  have Hid : integrable_on (Î» x, f (i.insert_nth d x) i) (I.face i).Icc,
    from (box.continuous_on_face_Icc ((continuous_apply i).comp_continuous_on Hc)
      hd).integrable_on_Icc,
  have H : tendsto (Î» k, âˆ« x in ((J k).face i).Icc, f (i.insert_nth d x) i) at_top
      (ğ“ $ âˆ« x in (I.face i).Icc, f (i.insert_nth d x) i),
  { have hIoo : (â‹ƒ k, ((J k).face i).Ioo) = (I.face i).Ioo,
      from box.Union_Ioo_of_tendsto ((box.monotone_face i).comp J.monotone)
        (tendsto_pi_nhds.2 (Î» _, hJl _)) (tendsto_pi_nhds.2 (Î» _, hJu _)),
    simp only [integrable_on, â† measure.restrict_congr_set (box.Ioo_ae_eq_Icc _), â† hIoo] at Hid âŠ¢,
    exact tendsto_set_integral_of_monotone (Î» k, ((J k).face i).measurable_set_Ioo)
      (box.Ioo.monotone.comp ((box.monotone_face i).comp J.monotone)) Hid },
  refine H.congr_dist (metric.nhds_basis_closed_ball.tendsto_right_iff.2 (Î» Îµ Îµpos, _)),
  have hvol_pos : âˆ€ J : box (fin n), 0 < âˆ j, (J.upper j - J.lower j),
    from Î» J, (prod_pos $ Î» j hj, sub_pos.2 $ J.lower_lt_upper _),
  rcases metric.uniform_continuous_on_iff_le.1
    (I.is_compact_Icc.uniform_continuous_on_of_continuous Hc)
    (Îµ / âˆ j, ((I.face i).upper j - (I.face i).lower j)) (div_pos Îµpos (hvol_pos (I.face i)))
    with âŸ¨Î´, Î´pos, hÎ´âŸ©,
  refine (hcd.eventually (metric.ball_mem_nhds _ Î´pos)).mono (Î» k hk, _),
  have Hsub : ((J k).face i).Icc âŠ† (I.face i).Icc,
    from box.le_iff_Icc.1 (box.face_mono (hJ_le _) i),
  rw [mem_closed_ball_zero_iff, real.norm_eq_abs, abs_of_nonneg dist_nonneg,
    dist_eq_norm, â† integral_sub (Hid.mono_set Hsub) ((Hic _).mono_set Hsub)],
  calc âˆ¥(âˆ« x in ((J k).face i).Icc, f (i.insert_nth d x) i - f (i.insert_nth (c k) x) i)âˆ¥
      â‰¤ (Îµ / âˆ j, ((I.face i).upper j - (I.face i).lower j)) * (volume ((J k).face i).Icc).to_real :
    begin
      refine norm_set_integral_le_of_norm_le_const' (((J k).face i).measure_Icc_lt_top _)
        ((J k).face i).measurable_set_Icc (Î» x hx, _),
      rw â† dist_eq_norm,
      calc dist (f (i.insert_nth d x) i) (f (i.insert_nth (c k) x) i)
          â‰¤ dist (f (i.insert_nth d x)) (f (i.insert_nth (c k) x)) :
        dist_le_pi_dist (f (i.insert_nth d x)) (f (i.insert_nth (c k) x)) i
      ... â‰¤ (Îµ / âˆ j, ((I.face i).upper j - (I.face i).lower j)) :
        hÎ´ _ _ (I.maps_to_insert_nth_face_Icc hd (Hsub hx))
          (I.maps_to_insert_nth_face_Icc (hc _) (Hsub hx)) _,
      rw [fin.dist_insert_nth_insert_nth, dist_self, dist_comm],
      exact max_le hk.le Î´pos.lt.le 
    end
  ... â‰¤ Îµ :
    begin
      rw [box.Icc_def, real.volume_Icc_pi_to_real ((J k).face i).lower_le_upper,
        â† le_div_iff (hvol_pos _)],
      refine div_le_div_of_le_left Îµpos.le (hvol_pos _) (prod_le_prod (Î» j hj, _) (Î» j hj, _)),
      exacts [sub_nonneg.2 (box.lower_le_upper _ _),
        sub_le_sub ((hJ_sub' _ (J _).upper_mem_Icc).2 _) ((hJ_sub' _ (J _).lower_mem_Icc).1 _)]
    end
end

variables (a b : â„â¿âºÂ¹)

local notation `face` i := set.Icc (a âˆ˜ fin.succ_above i) (b âˆ˜ fin.succ_above i)
local notation `front_face` i:2000 := fin.insert_nth i (b i)
local notation `back_face` i:2000 := fin.insert_nth i (a i)

lemma integral_divergence_of_has_fderiv_within_at_off_countable (hle : a â‰¤ b) (f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹)
  (f' : â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] Eâ¿âºÂ¹) (s : set â„â¿âºÂ¹) (hs : countable s) (Hc : continuous_on f (Icc a b))
  (Hd : âˆ€ x âˆˆ set.pi univ (Î» i, Ioo (a i) (b i)) \ s, has_fderiv_at f (f' x) x)
  (Hi : integrable_on (Î» x, âˆ‘ i, f' x (e i) i) (Icc a b)) :
  âˆ« x in Icc a b, âˆ‘ i, f' x (e i) i =
    âˆ‘ i : fin (n + 1),
      ((âˆ« x in face i, f (front_face i x) i) - âˆ« x in face i, f (back_face i x) i) :=
begin
  rcases em (âˆƒ i, a i = b i) with âŸ¨i, hiâŸ©|hne,
  { /- First we sort out the trivial case `âˆƒ i, a i = b i`. -/
    simp only [volume_pi, â† set_integral_congr_set_ae measure.univ_pi_Ioc_ae_eq_Icc],
    have hi' : Ioc (a i) (b i) = âˆ… := Ioc_eq_empty hi.not_lt,
    have : pi set.univ (Î» j, Ioc (a j) (b j)) = âˆ…, from univ_pi_eq_empty hi',
    rw [this, integral_empty, sum_eq_zero],
    rintro j -,
    rcases eq_or_ne i j with rfl|hne,
    { simp [hi] },
    { rcases fin.exists_succ_above_eq hne with âŸ¨i, rflâŸ©,
      have : pi set.univ (Î» k : fin n, Ioc (a $ j.succ_above k) (b $ j.succ_above k)) = âˆ…,
        from univ_pi_eq_empty hi',
      rw [this, integral_empty, integral_empty, sub_self] } },
  { have hlt : âˆ€ i, a i < b i, from Î» i, (hle i).lt_of_ne (Î» hi, hne âŸ¨i, hiâŸ©),
    convert integral_divergence_of_has_fderiv_within_at_off_countable_auxâ‚‚ âŸ¨a, b, hltâŸ©
      f f' s hs Hc Hd Hi }
end

/-- **Divergence theorem** for a family of functions `f : fin (n + 1) â†’ â„â¿âºÂ¹ â†’ E`. See also
`measure_theory.integral_divergence_of_has_fderiv_within_at_off_countable'` for a version formulated
in terms of a vector-valued function `f : â„â¿âºÂ¹ â†’ Eâ¿âºÂ¹`. -/
lemma integral_divergence_of_has_fderiv_within_at_off_countable' (hle : a â‰¤ b)
  (f : fin (n + 1) â†’ â„â¿âºÂ¹ â†’ E) (f' : fin (n + 1) â†’ â„â¿âºÂ¹ â†’ â„â¿âºÂ¹ â†’L[â„] E)
  (s : set â„â¿âºÂ¹) (hs : countable s) (Hc : âˆ€ i, continuous_on (f i) (Icc a b))
  (Hd : âˆ€ (x âˆˆ pi set.univ (Î» i, Ioo (a i) (b i)) \ s) i, has_fderiv_at (f i) (f' i x) x)
  (Hi : integrable_on (Î» x, âˆ‘ i, f' i x (e i)) (Icc a b)) :
  âˆ« x in Icc a b, âˆ‘ i, f' i x (e i) =
    âˆ‘ i : fin (n + 1),
      ((âˆ« x in face i, f i (front_face i x)) - âˆ« x in face i, f i (back_face i x)) :=
integral_divergence_of_has_fderiv_within_at_off_countable a b hle (Î» x i, f i x)
  (Î» x, continuous_linear_map.pi (Î» i, f' i x)) s hs
  (continuous_on_pi.2 Hc) (Î» x hx, has_fderiv_at_pi.2 (Hd x hx)) Hi

end

/-- An auxiliary lemma that is used to specialize the general divergence theorem to spaces that do
not have the form `fin n â†’ â„`. -/
lemma integral_divergence_of_has_fderiv_within_at_off_countable_of_equiv
  {F : Type*} [normed_group F] [normed_space â„ F] [partial_order F] [measure_space F]
  [borel_space F] (eL : F â‰ƒL[â„] â„â¿âºÂ¹) (he_ord : âˆ€ x y, eL x â‰¤ eL y â†” x â‰¤ y)
  (he_vol : measure_preserving eL volume volume) (f : fin (n + 1) â†’ F â†’ E)
  (f' : fin (n + 1) â†’ F â†’ F â†’L[â„] E) (s : set F) (hs : countable s)
  (a b : F) (hle : a â‰¤ b) (Hc : âˆ€ i, continuous_on (f i) (Icc a b))
  (Hd : âˆ€ (x âˆˆ interior (Icc a b) \ s) i, has_fderiv_at (f i) (f' i x) x)
  (DF : F â†’ E) (hDF : âˆ€ x, DF x = âˆ‘ i, f' i x (eL.symm $ e i)) (Hi : integrable_on DF (Icc a b)) :
  âˆ« x in Icc a b, DF x =
    âˆ‘ i : fin (n + 1), ((âˆ« x in Icc (eL a âˆ˜ i.succ_above) (eL b âˆ˜ i.succ_above),
        f i (eL.symm $ i.insert_nth (eL b i) x)) -
      (âˆ« x in Icc (eL a âˆ˜ i.succ_above) (eL b âˆ˜ i.succ_above),
        f i (eL.symm $ i.insert_nth (eL a i) x))) :=
have he_emb : measurable_embedding eL := eL.to_homeomorph.to_measurable_equiv.measurable_embedding,
have hIcc : eL â»Â¹' (Icc (eL a) (eL b)) = Icc a b,
  by { ext1 x, simp only [set.mem_preimage, set.mem_Icc, he_ord] },
have hIcc' : Icc (eL a) (eL b) = eL.symm â»Â¹' (Icc a b),
  by rw [â† hIcc, eL.symm_preimage_preimage],
calc âˆ« x in Icc a b, DF x = âˆ« x in Icc a b, âˆ‘ i, f' i x (eL.symm $ e i) : by simp only [hDF]
... = âˆ« x in Icc (eL a) (eL b), âˆ‘ i, f' i (eL.symm x) (eL.symm $ e i) :
  begin
    rw [â† he_vol.set_integral_preimage_emb he_emb],
    simp only [hIcc, eL.symm_apply_apply]
  end
... = âˆ‘ i : fin (n + 1), ((âˆ« x in Icc (eL a âˆ˜ i.succ_above) (eL b âˆ˜ i.succ_above),
        f i (eL.symm $ i.insert_nth (eL b i) x)) -
      (âˆ« x in Icc (eL a âˆ˜ i.succ_above) (eL b âˆ˜ i.succ_above),
        f i (eL.symm $ i.insert_nth (eL a i) x))) :
  begin
    convert integral_divergence_of_has_fderiv_within_at_off_countable' (eL a) (eL b)
      ((he_ord _ _).2 hle) (Î» i x, f i (eL.symm x))
      (Î» i x, f' i (eL.symm x) âˆ˜L (eL.symm : â„â¿âºÂ¹ â†’L[â„] F))
      (eL.symm â»Â¹' s) (hs.preimage eL.symm.injective) _ _ _,
    { refine Î» i, (Hc i).comp eL.symm.continuous_on _,
      rw hIcc' },
    { refine Î» x hx i, (Hd (eL.symm x) âŸ¨_, hx.2âŸ© i).comp x eL.symm.has_fderiv_at,
      rw â† hIcc,
      refine preimage_interior_subset_interior_preimage eL.continuous _,
      simp only [set.mem_preimage, eL.apply_symm_apply, â† pi_univ_Icc,
        interior_pi_set (finite.of_fintype _), interior_Icc],
      exact hx.1 },
    { rw [â† he_vol.integrable_on_comp_preimage he_emb, hIcc],
      simp [â† hDF, (âˆ˜), Hi] }
  end

end

open_locale interval
open continuous_linear_map (smul_right)

local notation `â„Â¹` := fin 1 â†’ â„
local notation `â„Â²` := fin 2 â†’ â„
local notation `EÂ¹` := fin 1 â†’ E
local notation `EÂ²` := fin 2 â†’ E

/-- **Fundamental theorem of calculus, part 2**. This version assumes that `f` is differentiable off
a countable set `s`, and is continuous at the points of `s`.

See also

* `interval_integral.integral_eq_sub_of_has_deriv_right_of_le` for a version that only assumes right
differentiability of `f`;

* `measure_theory.integral_eq_of_has_deriv_within_at_off_countable` for a version that works both
  for `a â‰¤ b` and `b â‰¤ a` at the expense of using unordered intervals instead of `set.Icc`. -/
theorem integral_eq_of_has_deriv_within_at_off_countable_of_le (f f' : â„ â†’ E)
  {a b : â„} (hle : a â‰¤ b) {s : set â„} (hs : countable s)
  (Hc : continuous_on f (Icc a b)) (Hd : âˆ€ x âˆˆ Ioo a b \ s, has_deriv_at f (f' x) x)
  (Hi : interval_integrable f' volume a b) :
  âˆ« x in a..b, f' x = f b - f a :=
begin
  set e : â„ â‰ƒL[â„] â„Â¹ := (continuous_linear_equiv.fun_unique (fin 1) â„ â„).symm,
  have e_symm : âˆ€ x, e.symm x = x 0 := Î» x, rfl,
  set F' : â„ â†’ â„ â†’L[â„] E := Î» x, smul_right (1 : â„ â†’L[â„] â„) (f' x),
  have hF' : âˆ€ x y, F' x y = y â€¢ f' x := Î» x y, rfl,
  calc âˆ« x in a..b, f' x = âˆ« x in Icc a b, f' x :
    by simp only [interval_integral.integral_of_le hle, set_integral_congr_set_ae Ioc_ae_eq_Icc]
  ... = âˆ‘ i : fin 1, ((âˆ« x in Icc (e a âˆ˜ i.succ_above) (e b âˆ˜ i.succ_above),
        f (e.symm $ i.insert_nth (e b i) x)) -
      (âˆ« x in Icc (e a âˆ˜ i.succ_above) (e b âˆ˜ i.succ_above),
        f (e.symm $ i.insert_nth (e a i) x))) :
    begin
      simp only [â† interior_Icc] at Hd,
      refine integral_divergence_of_has_fderiv_within_at_off_countable_of_equiv e _ _
        (Î» _, f) (Î» _, F') s hs a b hle (Î» i, Hc) (Î» x hx i, Hd x hx) _ _ _,
      { exact Î» x y, (order_iso.fun_unique (fin 1) â„).symm.le_iff_le },
      { exact (volume_preserving_fun_unique (fin 1) â„).symm },
      { intro x, rw [fin.sum_univ_one, hF', e_symm, pi.single_eq_same, one_smul] },
      { rw [interval_integrable_iff_integrable_Ioc_of_le hle] at Hi,
        exact Hi.congr_set_ae Ioc_ae_eq_Icc.symm }
    end
  ... = f b - f a :
    begin
      simp only [fin.sum_univ_one, e_symm],
      have : âˆ€ (c : â„), const (fin 0) c = is_empty_elim := Î» c, subsingleton.elim _ _,
      simp [this, volume_pi, measure.pi_of_empty (Î» _ : fin 0, volume)]
    end
end

/-- **Fundamental theorem of calculus, part 2**. This version assumes that `f` is differentiable off
a countable set `s`, and is continuous at the points of `s`.

See also `measure_theory.interval_integral.integral_eq_sub_of_has_deriv_right` for a version that
only assumes right differentiability of `f`.
-/
theorem integral_eq_of_has_deriv_within_at_off_countable (f f' : â„ â†’ E) {a b : â„} {s : set â„}
  (hs : countable s) (Hc : continuous_on f [a, b])
  (Hd : âˆ€ x âˆˆ Ioo (min a b) (max a b) \ s, has_deriv_at f (f' x) x)
  (Hi : interval_integrable f' volume a b) :
  âˆ« x in a..b, f' x = f b - f a :=
begin
  cases le_total a b with hab hab,
  { simp only [interval_of_le hab, min_eq_left hab, max_eq_right hab] at *,
    exact integral_eq_of_has_deriv_within_at_off_countable_of_le f f' hab hs Hc Hd Hi },
  { simp only [interval_of_ge hab, min_eq_right hab, max_eq_left hab] at *,
    rw [interval_integral.integral_symm, neg_eq_iff_neg_eq, neg_sub, eq_comm],
    exact integral_eq_of_has_deriv_within_at_off_countable_of_le f f' hab hs Hc Hd Hi.symm }
end

/-- **Divergence theorem** for functions on the plane along rectangles. It is formulated in terms of
two functions `f g : â„ Ã— â„ â†’ E` and an integral over `Icc a b = [a.1, b.1] Ã— [a.2, b.2]`, where
`a b : â„ Ã— â„`, `a â‰¤ b`. When thinking of `f` and `g` as the two coordinates of a single function
`F : â„ Ã— â„ â†’ E Ã— E` and when `E = â„`, this is the usual statement that the integral of the
divergence of `F` inside the rectangle equals the integral of the normal derivative of `F` along the
boundary.

See also `measure_theory.integral2_divergence_prod_of_has_fderiv_within_at_off_countable` for a
version that does not assume `a â‰¤ b` and uses iterated interval integral instead of the integral
over `Icc a b`. -/
lemma integral_divergence_prod_Icc_of_has_fderiv_within_at_off_countable_of_le (f g : â„ Ã— â„ â†’ E)
  (f' g' : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] E) (a b : â„ Ã— â„) (hle : a â‰¤ b) (s : set (â„ Ã— â„)) (hs : countable s)
  (Hcf : continuous_on f (Icc a b)) (Hcg : continuous_on g (Icc a b))
  (Hdf : âˆ€ x âˆˆ (Ioo a.1 b.1).prod (Ioo a.2 b.2) \ s, has_fderiv_at f (f' x) x)
  (Hdg : âˆ€ x âˆˆ (Ioo a.1 b.1).prod (Ioo a.2 b.2) \ s, has_fderiv_at g (g' x) x)
  (Hi : integrable_on (Î» x, f' x (1, 0) + g' x (0, 1)) (Icc a b)) :
  âˆ« x in Icc a b, f' x (1, 0) + g' x (0, 1) =
    (âˆ« x in a.1..b.1, g (x, b.2)) - (âˆ« x in a.1..b.1, g (x, a.2)) +
      (âˆ« y in a.2..b.2, f (b.1, y)) - âˆ« y in a.2..b.2, f (a.1, y) :=
let e : (â„ Ã— â„) â‰ƒL[â„] â„Â² := (continuous_linear_equiv.fin_two_arrow â„ â„).symm in
calc âˆ« x in Icc a b, f' x (1, 0) + g' x (0, 1)
    = âˆ‘ i : fin 2, ((âˆ« x in Icc (e a âˆ˜ i.succ_above) (e b âˆ˜ i.succ_above),
        ![f, g] i (e.symm $ i.insert_nth (e b i) x)) -
      (âˆ« x in Icc (e a âˆ˜ i.succ_above) (e b âˆ˜ i.succ_above),
        ![f, g] i (e.symm $ i.insert_nth (e a i) x))) :
  begin
    refine integral_divergence_of_has_fderiv_within_at_off_countable_of_equiv e _ _
      ![f, g] ![f', g'] s hs a b hle _ (Î» x hx, _) _ _ Hi,
    { exact Î» x y, (order_iso.fin_two_arrow_iso â„).symm.le_iff_le },
    { exact (volume_preserving_fin_two_arrow â„).symm },
    { exact fin.forall_fin_two.2 âŸ¨Hcf, HcgâŸ© },
    { rw [Icc_prod_eq, interior_prod_eq, interior_Icc, interior_Icc] at hx,
      exact fin.forall_fin_two.2 âŸ¨Hdf x hx, Hdg x hxâŸ© },
    { intro x, rw fin.sum_univ_two, simp }
  end
... = (âˆ« y in Icc a.2 b.2, f (b.1, y)) - (âˆ« y in Icc a.2 b.2, f (a.1, y)) +
        ((âˆ« x in Icc a.1 b.1, g (x, b.2)) - âˆ« x in Icc a.1 b.1, g (x, a.2)) :
  begin
    have : âˆ€ (a b : â„Â¹) (f : â„Â¹ â†’ E), âˆ« x in Icc a b, f x = âˆ« x in Icc (a 0) (b 0), f (Î» _, x),
    { intros a b f,
      convert ((volume_preserving_fun_unique (fin 1) â„).symm.set_integral_preimage_emb
        (measurable_equiv.measurable_embedding _) _ _).symm,
      exact ((order_iso.fun_unique (fin 1) â„).symm.preimage_Icc a b).symm },
    simp only [fin.sum_univ_two, this],
    refl
  end
... = (âˆ« x in a.1..b.1, g (x, b.2)) - (âˆ« x in a.1..b.1, g (x, a.2)) +
        (âˆ« y in a.2..b.2, f (b.1, y)) - âˆ« y in a.2..b.2, f (a.1, y) :
  begin
    simp only [interval_integral.integral_of_le hle.1, interval_integral.integral_of_le hle.2,
      set_integral_congr_set_ae Ioc_ae_eq_Icc],
    abel
  end

/-- **Divergence theorem** for functions on the plane. It is formulated in terms of two functions
`f g : â„ Ã— â„ â†’ E` and iterated integral `âˆ« x in aâ‚..bâ‚, âˆ« y in aâ‚‚..bâ‚‚, _`, where
`aâ‚ aâ‚‚ bâ‚ bâ‚‚ : â„`. When thinking of `f` and `g` as the two coordinates of a single function
`F : â„ Ã— â„ â†’ E Ã— E` and when `E = â„`, this is the usual statement that the integral of the
divergence of `F` inside the rectangle with vertices `(aáµ¢, bâ±¼)`, `i, j =1,2`, equals the integral of
the normal derivative of `F` along the boundary.

See also `measure_theory.integral_divergence_prod_Icc_of_has_fderiv_within_at_off_countable_of_le`
for a version that uses an integral over `Icc a b`, where `a b : â„ Ã— â„`, `a â‰¤ b`. -/
lemma integral2_divergence_prod_of_has_fderiv_within_at_off_countable (f g : â„ Ã— â„ â†’ E)
  (f' g' : â„ Ã— â„ â†’ â„ Ã— â„ â†’L[â„] E) (aâ‚ aâ‚‚ bâ‚ bâ‚‚ : â„) (s : set (â„ Ã— â„)) (hs : countable s)
  (Hcf : continuous_on f ([aâ‚, bâ‚].prod [aâ‚‚, bâ‚‚])) (Hcg : continuous_on g ([aâ‚, bâ‚].prod [aâ‚‚, bâ‚‚]))
  (Hdf : âˆ€ x âˆˆ (Ioo (min aâ‚ bâ‚) (max aâ‚ bâ‚)).prod (Ioo (min aâ‚‚ bâ‚‚) (max aâ‚‚ bâ‚‚)) \ s,
    has_fderiv_at f (f' x) x)
  (Hdg : âˆ€ x âˆˆ (Ioo (min aâ‚ bâ‚) (max aâ‚ bâ‚)).prod (Ioo (min aâ‚‚ bâ‚‚) (max aâ‚‚ bâ‚‚)) \ s,
    has_fderiv_at g (g' x) x)
  (Hi : integrable_on (Î» x, f' x (1, 0) + g' x (0, 1)) ([aâ‚, bâ‚].prod [aâ‚‚, bâ‚‚])) :
  âˆ« x in aâ‚..bâ‚, âˆ« y in aâ‚‚..bâ‚‚, f' (x, y) (1, 0) + g' (x, y) (0, 1) =
    (âˆ« x in aâ‚..bâ‚, g (x, bâ‚‚)) - (âˆ« x in aâ‚..bâ‚, g (x, aâ‚‚)) +
      (âˆ« y in aâ‚‚..bâ‚‚, f (bâ‚, y)) - âˆ« y in aâ‚‚..bâ‚‚, f (aâ‚, y) :=
begin
  wlog hâ‚ : aâ‚ â‰¤ bâ‚ := le_total aâ‚ bâ‚ using [aâ‚ bâ‚, bâ‚ aâ‚] tactic.skip,
  wlog hâ‚‚ : aâ‚‚ â‰¤ bâ‚‚ := le_total aâ‚‚ bâ‚‚ using [aâ‚‚ bâ‚‚, bâ‚‚ aâ‚‚] tactic.skip,
  { simp only [interval_of_le hâ‚, interval_of_le hâ‚‚, min_eq_left, max_eq_right, hâ‚, hâ‚‚]
      at Hcf Hcg Hdf Hdg Hi,
    calc âˆ« x in aâ‚..bâ‚, âˆ« y in aâ‚‚..bâ‚‚, f' (x, y) (1, 0) + g' (x, y) (0, 1)
        = âˆ« x in Icc aâ‚ bâ‚, âˆ« y in Icc aâ‚‚ bâ‚‚, f' (x, y) (1, 0) + g' (x, y) (0, 1) :
      by simp only [interval_integral.integral_of_le, hâ‚, hâ‚‚,
        set_integral_congr_set_ae Ioc_ae_eq_Icc]
    ... = âˆ« x in (Icc aâ‚ bâ‚).prod (Icc aâ‚‚ bâ‚‚), f' x (1, 0) + g' x (0, 1) :
      (set_integral_prod _ Hi).symm
    ... = (âˆ« x in aâ‚..bâ‚, g (x, bâ‚‚)) - (âˆ« x in aâ‚..bâ‚, g (x, aâ‚‚)) +
            (âˆ« y in aâ‚‚..bâ‚‚, f (bâ‚, y)) - âˆ« y in aâ‚‚..bâ‚‚, f (aâ‚, y) :
      begin
        rw Icc_prod_Icc at *,
        apply integral_divergence_prod_Icc_of_has_fderiv_within_at_off_countable_of_le f g f' g'
          (aâ‚, aâ‚‚) (bâ‚, bâ‚‚) âŸ¨hâ‚, hâ‚‚âŸ© s; assumption
      end },
  { rw [interval_swap bâ‚‚ aâ‚‚, min_comm bâ‚‚ aâ‚‚, max_comm bâ‚‚ aâ‚‚] at this,
    intros Hcf Hcg Hdf Hdg Hi,
    simp only [interval_integral.integral_symm bâ‚‚ aâ‚‚, interval_integral.integral_neg],
    refine (congr_arg has_neg.neg (this Hcf Hcg Hdf Hdg Hi)).trans _, abel },
  { rw [interval_swap bâ‚ aâ‚, min_comm bâ‚ aâ‚, max_comm bâ‚ aâ‚] at this,
    intros Hcf Hcg Hdf Hdg Hi,
    simp only [interval_integral.integral_symm bâ‚ aâ‚],
    refine (congr_arg has_neg.neg (this Hcf Hcg Hdf Hdg Hi)).trans _, abel }
end

end measure_theory
