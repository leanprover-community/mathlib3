/-
Copyright (c) 2021 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/

import measure_theory.function.l1_space
import analysis.normed_space.indicator_function

/-! # Functions integrable on a set and at a filter

We define `integrable_on f s Î¼ := integrable f (Î¼.restrict s)` and prove theorems like
`integrable_on_union : integrable_on f (s âˆª t) Î¼ â†” integrable_on f s Î¼ âˆ§ integrable_on f t Î¼`.

Next we define a predicate `integrable_at_filter (f : Î± â†’ E) (l : filter Î±) (Î¼ : measure Î±)`
saying that `f` is integrable at some set `s âˆˆ l` and prove that a measurable function is integrable
at `l` with respect to `Î¼` provided that `f` is bounded above at `l âŠ“ Î¼.ae` and `Î¼` is finite
at `l`.

-/

noncomputable theory
open set filter topological_space measure_theory function
open_locale classical topology interval big_operators filter ennreal measure_theory

variables {Î± Î² E F : Type*} [measurable_space Î±]

section

variables [topological_space Î²] {l l' : filter Î±} {f g : Î± â†’ Î²} {Î¼ Î½ : measure Î±}

/-- A function `f` is strongly measurable at a filter `l` w.r.t. a measure `Î¼` if it is
ae strongly measurable w.r.t. `Î¼.restrict s` for some `s âˆˆ l`. -/
def strongly_measurable_at_filter (f : Î± â†’ Î²) (l : filter Î±) (Î¼ : measure Î± . volume_tac) :=
âˆƒ s âˆˆ l, ae_strongly_measurable f (Î¼.restrict s)

@[simp] lemma strongly_measurable_at_bot {f : Î± â†’ Î²} : strongly_measurable_at_filter f âŠ¥ Î¼ :=
âŸ¨âˆ…, mem_bot, by simpâŸ©

protected lemma strongly_measurable_at_filter.eventually (h : strongly_measurable_at_filter f l Î¼) :
  âˆ€á¶  s in l.small_sets, ae_strongly_measurable f (Î¼.restrict s) :=
(eventually_small_sets' $ Î» s t, ae_strongly_measurable.mono_set).2 h

protected lemma strongly_measurable_at_filter.filter_mono
  (h : strongly_measurable_at_filter f l Î¼) (h' : l' â‰¤ l) :
  strongly_measurable_at_filter f l' Î¼ :=
let âŸ¨s, hsl, hsâŸ© := h in âŸ¨s, h' hsl, hsâŸ©

protected lemma measure_theory.ae_strongly_measurable.strongly_measurable_at_filter
  (h : ae_strongly_measurable f Î¼) :
  strongly_measurable_at_filter f l Î¼ :=
âŸ¨univ, univ_mem, by rwa measure.restrict_univâŸ©

lemma ae_strongly_measurable.strongly_measurable_at_filter_of_mem
  {s} (h : ae_strongly_measurable f (Î¼.restrict s)) (hl : s âˆˆ l) :
  strongly_measurable_at_filter f l Î¼ :=
âŸ¨s, hl, hâŸ©

protected lemma measure_theory.strongly_measurable.strongly_measurable_at_filter
  (h : strongly_measurable f) :
  strongly_measurable_at_filter f l Î¼ :=
h.ae_strongly_measurable.strongly_measurable_at_filter

end

namespace measure_theory

section normed_add_comm_group

lemma has_finite_integral_restrict_of_bounded [normed_add_comm_group E] {f : Î± â†’ E} {s : set Î±}
  {Î¼ : measure Î±} {C}  (hs : Î¼ s < âˆž) (hf : âˆ€áµ x âˆ‚(Î¼.restrict s), â€–f xâ€– â‰¤ C) :
  has_finite_integral f (Î¼.restrict s) :=
by haveI : is_finite_measure (Î¼.restrict s) := âŸ¨by rwa [measure.restrict_apply_univ]âŸ©;
  exact has_finite_integral_of_bounded hf

variables [normed_add_comm_group E] {f g : Î± â†’ E} {s t : set Î±} {Î¼ Î½ : measure Î±}

/-- A function is `integrable_on` a set `s` if it is almost everywhere strongly measurable on `s`
and if the integral of its pointwise norm over `s` is less than infinity. -/
def integrable_on (f : Î± â†’ E) (s : set Î±) (Î¼ : measure Î± . volume_tac) : Prop :=
integrable f (Î¼.restrict s)

lemma integrable_on.integrable (h : integrable_on f s Î¼) :
  integrable f (Î¼.restrict s) := h

@[simp] lemma integrable_on_empty : integrable_on f âˆ… Î¼ :=
by simp [integrable_on, integrable_zero_measure]

@[simp] lemma integrable_on_univ : integrable_on f univ Î¼ â†” integrable f Î¼ :=
by rw [integrable_on, measure.restrict_univ]

lemma integrable_on_zero : integrable_on (Î» _, (0:E)) s Î¼ := integrable_zero _ _ _

@[simp] lemma integrable_on_const {C : E} : integrable_on (Î» _, C) s Î¼ â†” C = 0 âˆ¨ Î¼ s < âˆž :=
integrable_const_iff.trans $ by rw [measure.restrict_apply_univ]

lemma integrable_on.mono (h : integrable_on f t Î½) (hs : s âŠ† t) (hÎ¼ : Î¼ â‰¤ Î½) :
  integrable_on f s Î¼ :=
h.mono_measure $ measure.restrict_mono hs hÎ¼

lemma integrable_on.mono_set (h : integrable_on f t Î¼) (hst : s âŠ† t) :
  integrable_on f s Î¼ :=
h.mono hst le_rfl

lemma integrable_on.mono_measure (h : integrable_on f s Î½) (hÎ¼ : Î¼ â‰¤ Î½) :
  integrable_on f s Î¼ :=
h.mono (subset.refl _) hÎ¼

lemma integrable_on.mono_set_ae (h : integrable_on f t Î¼) (hst : s â‰¤áµ[Î¼] t) :
  integrable_on f s Î¼ :=
h.integrable.mono_measure $ measure.restrict_mono_ae hst

lemma integrable_on.congr_set_ae (h : integrable_on f t Î¼) (hst : s =áµ[Î¼] t) :
  integrable_on f s Î¼ :=
h.mono_set_ae hst.le

lemma integrable_on.congr_fun' (h : integrable_on f s Î¼) (hst : f =áµ[Î¼.restrict s] g) :
  integrable_on g s Î¼ :=
integrable.congr h hst

lemma integrable_on.congr_fun (h : integrable_on f s Î¼) (hst : eq_on f g s)
  (hs : measurable_set s) :
  integrable_on g s Î¼ :=
h.congr_fun' ((ae_restrict_iff' hs).2 (eventually_of_forall hst))

lemma integrable.integrable_on (h : integrable f Î¼) : integrable_on f s Î¼ :=
h.mono_measure $ measure.restrict_le_self

lemma integrable.integrable_on' (h : integrable f (Î¼.restrict s)) : integrable_on f s Î¼ :=
h

lemma integrable_on.restrict (h : integrable_on f s Î¼) (hs : measurable_set s) :
  integrable_on f s (Î¼.restrict t) :=
by { rw [integrable_on, measure.restrict_restrict hs], exact h.mono_set (inter_subset_left _ _) }

lemma integrable_on.left_of_union (h : integrable_on f (s âˆª t) Î¼) : integrable_on f s Î¼ :=
h.mono_set $ subset_union_left _ _

lemma integrable_on.right_of_union (h : integrable_on f (s âˆª t) Î¼) : integrable_on f t Î¼ :=
h.mono_set $ subset_union_right _ _

lemma integrable_on.union (hs : integrable_on f s Î¼) (ht : integrable_on f t Î¼) :
  integrable_on f (s âˆª t) Î¼ :=
(hs.add_measure ht).mono_measure $ measure.restrict_union_le _ _

@[simp] lemma integrable_on_union :
  integrable_on f (s âˆª t) Î¼ â†” integrable_on f s Î¼ âˆ§ integrable_on f t Î¼ :=
âŸ¨Î» h, âŸ¨h.left_of_union, h.right_of_unionâŸ©, Î» h, h.1.union h.2âŸ©

@[simp] lemma integrable_on_singleton_iff {x : Î±} [measurable_singleton_class Î±] :
  integrable_on f {x} Î¼ â†” f x = 0 âˆ¨ Î¼ {x} < âˆž :=
begin
  have : f =áµ[Î¼.restrict {x}] (Î» y, f x),
  { filter_upwards [ae_restrict_mem (measurable_set_singleton x)] with _ ha,
    simp only [mem_singleton_iff.1 ha], },
  rw [integrable_on, integrable_congr this, integrable_const_iff],
  simp,
end

@[simp] lemma integrable_on_finite_bUnion {s : set Î²} (hs : s.finite)
  {t : Î² â†’ set Î±} : integrable_on f (â‹ƒ i âˆˆ s, t i) Î¼ â†” âˆ€ i âˆˆ s, integrable_on f (t i) Î¼ :=
begin
  apply hs.induction_on,
  { simp },
  { intros a s ha hs hf, simp [hf, or_imp_distrib, forall_and_distrib] }
end

@[simp] lemma integrable_on_finset_Union {s : finset Î²} {t : Î² â†’ set Î±} :
  integrable_on f (â‹ƒ i âˆˆ s, t i) Î¼ â†” âˆ€ i âˆˆ s, integrable_on f (t i) Î¼ :=
integrable_on_finite_bUnion s.finite_to_set

@[simp] lemma integrable_on_finite_Union [finite Î²] {t : Î² â†’ set Î±} :
  integrable_on f (â‹ƒ i, t i) Î¼ â†” âˆ€ i, integrable_on f (t i) Î¼ :=
by { casesI nonempty_fintype Î²,
  simpa using @integrable_on_finset_Union _ _ _ _ _ f Î¼ finset.univ t }

lemma integrable_on.add_measure (hÎ¼ : integrable_on f s Î¼) (hÎ½ : integrable_on f s Î½) :
  integrable_on f s (Î¼ + Î½) :=
by { delta integrable_on, rw measure.restrict_add, exact hÎ¼.integrable.add_measure hÎ½ }

@[simp] lemma integrable_on_add_measure :
  integrable_on f s (Î¼ + Î½) â†” integrable_on f s Î¼ âˆ§ integrable_on f s Î½ :=
âŸ¨Î» h, âŸ¨h.mono_measure (measure.le_add_right le_rfl),
  h.mono_measure (measure.le_add_left le_rfl)âŸ©,
  Î» h, h.1.add_measure h.2âŸ©

lemma _root_.measurable_embedding.integrable_on_map_iff [measurable_space Î²] {e : Î± â†’ Î²}
  (he : measurable_embedding e) {f : Î² â†’ E} {Î¼ : measure Î±} {s : set Î²} :
  integrable_on f s (measure.map e Î¼) â†” integrable_on (f âˆ˜ e) (e â»Â¹' s) Î¼ :=
by simp only [integrable_on, he.restrict_map, he.integrable_map_iff]

lemma integrable_on_map_equiv [measurable_space Î²] (e : Î± â‰ƒáµ Î²) {f : Î² â†’ E} {Î¼ : measure Î±}
  {s : set Î²} :
  integrable_on f s (measure.map e Î¼) â†” integrable_on (f âˆ˜ e) (e â»Â¹' s) Î¼ :=
by simp only [integrable_on, e.restrict_map, integrable_map_equiv e]

lemma measure_preserving.integrable_on_comp_preimage [measurable_space Î²] {e : Î± â†’ Î²} {Î½}
  (hâ‚ : measure_preserving e Î¼ Î½) (hâ‚‚ : measurable_embedding e) {f : Î² â†’ E} {s : set Î²} :
  integrable_on (f âˆ˜ e) (e â»Â¹' s) Î¼ â†” integrable_on f s Î½ :=
(hâ‚.restrict_preimage_emb hâ‚‚ s).integrable_comp_emb hâ‚‚

lemma measure_preserving.integrable_on_image [measurable_space Î²] {e : Î± â†’ Î²} {Î½}
  (hâ‚ : measure_preserving e Î¼ Î½) (hâ‚‚ : measurable_embedding e) {f : Î² â†’ E} {s : set Î±} :
  integrable_on f (e '' s) Î½ â†”  integrable_on (f âˆ˜ e) s Î¼ :=
((hâ‚.restrict_image_emb hâ‚‚ s).integrable_comp_emb hâ‚‚).symm

lemma integrable_indicator_iff (hs : measurable_set s) :
  integrable (indicator s f) Î¼ â†” integrable_on f s Î¼ :=
by simp [integrable_on, integrable, has_finite_integral, nnnorm_indicator_eq_indicator_nnnorm,
  ennreal.coe_indicator, lintegral_indicator _ hs, ae_strongly_measurable_indicator_iff hs]

lemma integrable_on.integrable_indicator (h : integrable_on f s Î¼) (hs : measurable_set s) :
  integrable (indicator s f) Î¼ :=
(integrable_indicator_iff hs).2 h

lemma integrable.indicator (h : integrable f Î¼) (hs : measurable_set s) :
  integrable (indicator s f) Î¼ :=
h.integrable_on.integrable_indicator hs

lemma integrable_on.indicator (h : integrable_on f s Î¼) (ht : measurable_set t) :
  integrable_on (indicator t f) s Î¼ :=
integrable.indicator h ht

lemma integrable_indicator_const_Lp {E} [normed_add_comm_group E]
  {p : â„â‰¥0âˆž} {s : set Î±} (hs : measurable_set s) (hÎ¼s : Î¼ s â‰  âˆž) (c : E) :
  integrable (indicator_const_Lp p hs hÎ¼s c) Î¼ :=
begin
  rw [integrable_congr indicator_const_Lp_coe_fn, integrable_indicator_iff hs, integrable_on,
    integrable_const_iff, lt_top_iff_ne_top],
  right,
  simpa only [set.univ_inter, measurable_set.univ, measure.restrict_apply] using hÎ¼s,
end

/-- If a function is integrable on a set `s` and nonzero there, then the measurable hull of `s` is
well behaved: the restriction of the measure to `to_measurable Î¼ s` coincides with its restriction
to `s`. -/
lemma integrable_on.restrict_to_measurable (hf : integrable_on f s Î¼) (h's : âˆ€ x âˆˆ s, f x â‰  0) :
  Î¼.restrict (to_measurable Î¼ s) = Î¼.restrict s :=
begin
  rcases exists_seq_strict_anti_tendsto (0 : â„) with âŸ¨u, u_anti, u_pos, u_limâŸ©,
  let v := Î» n, to_measurable (Î¼.restrict s) {x | u n â‰¤ â€–f xâ€–},
  have A : âˆ€ n, Î¼ (s âˆ© v n) â‰  âˆž,
  { assume n,
    rw [inter_comm, â† measure.restrict_apply (measurable_set_to_measurable _ _),
      measure_to_measurable],
    exact (hf.measure_ge_lt_top (u_pos n)).ne },
  apply measure.restrict_to_measurable_of_cover _ A,
  assume x hx,
  have : 0 < â€–f xâ€–, by simp only [h's x hx, norm_pos_iff, ne.def, not_false_iff],
  obtain âŸ¨n, hnâŸ© : âˆƒ n, u n < â€–f xâ€–, from ((tendsto_order.1 u_lim).2 _ this).exists,
  refine mem_Union.2 âŸ¨n, _âŸ©,
  exact subset_to_measurable _ _ hn.le
end

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is null-measurable. -/
lemma integrable_on.of_ae_diff_eq_zero (hf : integrable_on f s Î¼)
  (ht : null_measurable_set t Î¼) (h't : âˆ€áµ x âˆ‚Î¼, x âˆˆ t \ s â†’ f x = 0) :
  integrable_on f t Î¼ :=
begin
  let u := {x âˆˆ s | f x â‰  0},
  have hu : integrable_on f u Î¼ := hf.mono_set (Î» x hx, hx.1),
  let v := to_measurable Î¼ u,
  have A : integrable_on f v Î¼,
  { rw [integrable_on, hu.restrict_to_measurable],
    { exact hu },
    { assume x hx, exact hx.2 } },
  have B : integrable_on f (t \ v) Î¼,
  { apply integrable_on_zero.congr,
    filter_upwards [ae_restrict_of_ae h't, ae_restrict_memâ‚€
      (ht.diff (measurable_set_to_measurable Î¼ u).null_measurable_set)] with x hxt hx,
    by_cases h'x : x âˆˆ s,
    { by_contra H,
      exact hx.2 (subset_to_measurable Î¼ u âŸ¨h'x, ne.symm HâŸ©) },
    { exact (hxt âŸ¨hx.1, h'xâŸ©).symm, } },
  apply (A.union B).mono_set _,
  rw union_diff_self,
  exact subset_union_right _ _
end

/-- If a function is integrable on a set `s`, and vanishes on `t \ s`, then it is integrable on `t`
if `t` is measurable. -/
lemma integrable_on.of_forall_diff_eq_zero (hf : integrable_on f s Î¼)
  (ht : measurable_set t) (h't : âˆ€ x âˆˆ t \ s, f x = 0) :
  integrable_on f t Î¼ :=
hf.of_ae_diff_eq_zero ht.null_measurable_set (eventually_of_forall h't)

/-- If a function is integrable on a set `s` and vanishes almost everywhere on its complement,
then it is integrable. -/
lemma integrable_on.integrable_of_ae_not_mem_eq_zero (hf : integrable_on f s Î¼)
  (h't : âˆ€áµ x âˆ‚Î¼, x âˆ‰ s â†’ f x = 0) : integrable f Î¼ :=
begin
  rw â† integrable_on_univ,
  apply hf.of_ae_diff_eq_zero null_measurable_set_univ,
  filter_upwards [h't] with x hx h'x using hx h'x.2,
end

/-- If a function is integrable on a set `s` and vanishes everywhere on its complement,
then it is integrable. -/
lemma integrable_on.integrable_of_forall_not_mem_eq_zero (hf : integrable_on f s Î¼)
  (h't : âˆ€ x âˆ‰ s, f x = 0) : integrable f Î¼ :=
hf.integrable_of_ae_not_mem_eq_zero (eventually_of_forall (Î» x hx, h't x hx))

lemma integrable_on_iff_integrable_of_support_subset (h1s : support f âŠ† s) :
  integrable_on f s Î¼ â†” integrable f Î¼ :=
begin
  refine âŸ¨Î» h, _, Î» h, h.integrable_onâŸ©,
  apply h.integrable_of_forall_not_mem_eq_zero (Î» x hx, _),
  contrapose! hx,
  exact h1s (mem_support.2 hx),
end

lemma integrable_on_Lp_of_measure_ne_top {E} [normed_add_comm_group E]
  {p : â„â‰¥0âˆž} {s : set Î±} (f : Lp E p Î¼) (hp : 1 â‰¤ p) (hÎ¼s : Î¼ s â‰  âˆž) :
  integrable_on f s Î¼ :=
begin
  refine mem_â„’p_one_iff_integrable.mp _,
  have hÎ¼_restrict_univ : (Î¼.restrict s) set.univ < âˆž,
    by simpa only [set.univ_inter, measurable_set.univ, measure.restrict_apply, lt_top_iff_ne_top],
  haveI hÎ¼_finite : is_finite_measure (Î¼.restrict s) := âŸ¨hÎ¼_restrict_univâŸ©,
  exact ((Lp.mem_â„’p _).restrict s).mem_â„’p_of_exponent_le hp,
end

lemma integrable.lintegral_lt_top {f : Î± â†’ â„} (hf : integrable f Î¼) :
  âˆ«â» x, ennreal.of_real (f x) âˆ‚Î¼ < âˆž :=
calc âˆ«â» x, ennreal.of_real (f x) âˆ‚Î¼
    â‰¤ âˆ«â» x, â†‘â€–f xâ€–â‚Š âˆ‚Î¼ : lintegral_of_real_le_lintegral_nnnorm f
... < âˆž : hf.2

lemma integrable_on.set_lintegral_lt_top {f : Î± â†’ â„} {s : set Î±} (hf : integrable_on f s Î¼) :
  âˆ«â» x in s, ennreal.of_real (f x) âˆ‚Î¼ < âˆž :=
integrable.lintegral_lt_top hf

/-- We say that a function `f` is *integrable at filter* `l` if it is integrable on some
set `s âˆˆ l`. Equivalently, it is eventually integrable on `s` in `l.small_sets`. -/
def integrable_at_filter (f : Î± â†’ E) (l : filter Î±) (Î¼ : measure Î± . volume_tac) :=
âˆƒ s âˆˆ l, integrable_on f s Î¼

variables {l l' : filter Î±}

lemma integrable.integrable_at_filter (h : integrable f Î¼) (l : filter Î±) :
  integrable_at_filter f l Î¼ :=
âŸ¨univ, filter.univ_mem, integrable_on_univ.2 hâŸ©

protected lemma integrable_at_filter.eventually (h : integrable_at_filter f l Î¼) :
  âˆ€á¶  s in l.small_sets, integrable_on f s Î¼ :=
iff.mpr (eventually_small_sets' $ Î» s t hst ht, ht.mono_set hst) h

lemma integrable_at_filter.filter_mono (hl : l â‰¤ l') (hl' : integrable_at_filter f l' Î¼) :
  integrable_at_filter f l Î¼ :=
let âŸ¨s, hs, hsfâŸ© := hl' in âŸ¨s, hl hs, hsfâŸ©

lemma integrable_at_filter.inf_of_left (hl : integrable_at_filter f l Î¼) :
  integrable_at_filter f (l âŠ“ l') Î¼ :=
hl.filter_mono inf_le_left

lemma integrable_at_filter.inf_of_right (hl : integrable_at_filter f l Î¼) :
  integrable_at_filter f (l' âŠ“ l) Î¼ :=
hl.filter_mono inf_le_right

@[simp] lemma integrable_at_filter.inf_ae_iff {l : filter Î±} :
  integrable_at_filter f (l âŠ“ Î¼.ae) Î¼ â†” integrable_at_filter f l Î¼ :=
begin
  refine âŸ¨_, Î» h, h.filter_mono inf_le_leftâŸ©,
  rintros âŸ¨s, âŸ¨t, ht, u, hu, rflâŸ©, hfâŸ©,
  refine âŸ¨t, ht, _âŸ©,
  refine hf.integrable.mono_measure (Î» v hv, _),
  simp only [measure.restrict_apply hv],
  refine measure_mono_ae (mem_of_superset hu $ Î» x hx, _),
  exact Î» âŸ¨hv, htâŸ©, âŸ¨hv, âŸ¨ht, hxâŸ©âŸ©
end

alias integrable_at_filter.inf_ae_iff â†” integrable_at_filter.of_inf_ae _

/-- If `Î¼` is a measure finite at filter `l` and `f` is a function such that its norm is bounded
above at `l`, then `f` is integrable at `l`. -/
lemma measure.finite_at_filter.integrable_at_filter {l : filter Î±} [is_measurably_generated l]
  (hfm : strongly_measurable_at_filter f l Î¼) (hÎ¼ : Î¼.finite_at_filter l)
  (hf : l.is_bounded_under (â‰¤) (norm âˆ˜ f)) :
  integrable_at_filter f l Î¼ :=
begin
  obtain âŸ¨C, hCâŸ© : âˆƒ C, âˆ€á¶  s in l.small_sets, âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ C,
    from hf.imp (Î» C hC, eventually_small_sets.2 âŸ¨_, hC, Î» t, idâŸ©),
  rcases (hfm.eventually.and (hÎ¼.eventually.and hC)).exists_measurable_mem_of_small_sets
    with âŸ¨s, hsl, hsm, hfm, hÎ¼, hCâŸ©,
  refine âŸ¨s, hsl, âŸ¨hfm, has_finite_integral_restrict_of_bounded hÎ¼ _âŸ©âŸ©,
  exact C,
  rw [ae_restrict_eq hsm, eventually_inf_principal],
  exact eventually_of_forall hC
end

lemma measure.finite_at_filter.integrable_at_filter_of_tendsto_ae
  {l : filter Î±} [is_measurably_generated l] (hfm : strongly_measurable_at_filter f l Î¼)
  (hÎ¼ : Î¼.finite_at_filter l) {b} (hf : tendsto f (l âŠ“ Î¼.ae) (ð“ b)) :
  integrable_at_filter f l Î¼ :=
(hÎ¼.inf_of_left.integrable_at_filter (hfm.filter_mono inf_le_left)
  hf.norm.is_bounded_under_le).of_inf_ae

alias measure.finite_at_filter.integrable_at_filter_of_tendsto_ae â†
  _root_.filter.tendsto.integrable_at_filter_ae

lemma measure.finite_at_filter.integrable_at_filter_of_tendsto {l : filter Î±}
  [is_measurably_generated l] (hfm : strongly_measurable_at_filter f l Î¼)
  (hÎ¼ : Î¼.finite_at_filter l) {b} (hf : tendsto f l (ð“ b)) :
  integrable_at_filter f l Î¼ :=
hÎ¼.integrable_at_filter hfm hf.norm.is_bounded_under_le

alias measure.finite_at_filter.integrable_at_filter_of_tendsto â†
  _root_.filter.tendsto.integrable_at_filter

lemma integrable_add_of_disjoint {f g : Î± â†’ E}
  (h : disjoint (support f) (support g)) (hf : strongly_measurable f) (hg : strongly_measurable g) :
  integrable (f + g) Î¼ â†” integrable f Î¼ âˆ§ integrable g Î¼ :=
begin
  refine âŸ¨Î» hfg, âŸ¨_, _âŸ©, Î» h, h.1.add h.2âŸ©,
  { rw â† indicator_add_eq_left h, exact hfg.indicator hf.measurable_set_support },
  { rw â† indicator_add_eq_right h, exact hfg.indicator hg.measurable_set_support }
end

end normed_add_comm_group

end measure_theory

open measure_theory

variables [normed_add_comm_group E]

/-- A function which is continuous on a set `s` is almost everywhere measurable with respect to
`Î¼.restrict s`. -/
lemma continuous_on.ae_measurable [topological_space Î±] [opens_measurable_space Î±]
  [measurable_space Î²] [topological_space Î²] [borel_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {Î¼ : measure Î±} (hf : continuous_on f s) (hs : measurable_set s) :
  ae_measurable f (Î¼.restrict s) :=
begin
  nontriviality Î±, inhabit Î±,
  have : piecewise s f (Î» _, f default) =áµ[Î¼.restrict s] f := piecewise_ae_eq_restrict hs,
  refine âŸ¨piecewise s f (Î» _, f default), _, this.symmâŸ©,
  apply measurable_of_is_open,
  assume t ht,
  obtain âŸ¨u, u_open, huâŸ© : âˆƒ (u : set Î±), is_open u âˆ§ f â»Â¹' t âˆ© s = u âˆ© s :=
    _root_.continuous_on_iff'.1 hf t ht,
  rw [piecewise_preimage, set.ite, hu],
  exact (u_open.measurable_set.inter hs).union ((measurable_const ht.measurable_set).diff hs)
end

/-- A function which is continuous on a separable set `s` is almost everywhere strongly measurable
with respect to `Î¼.restrict s`. -/
lemma continuous_on.ae_strongly_measurable_of_is_separable
  [topological_space Î±] [pseudo_metrizable_space Î±] [opens_measurable_space Î±]
  [topological_space Î²] [pseudo_metrizable_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {Î¼ : measure Î±} (hf : continuous_on f s) (hs : measurable_set s)
  (h's : topological_space.is_separable s) :
  ae_strongly_measurable f (Î¼.restrict s) :=
begin
  letI := pseudo_metrizable_space_pseudo_metric Î±,
  borelize Î²,
  rw ae_strongly_measurable_iff_ae_measurable_separable,
  refine âŸ¨hf.ae_measurable hs, f '' s, hf.is_separable_image h's, _âŸ©,
  exact mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _),
end

/-- A function which is continuous on a set `s` is almost everywhere strongly measurable with
respect to `Î¼.restrict s` when either the source space or the target space is second-countable. -/
lemma continuous_on.ae_strongly_measurable
  [topological_space Î±] [topological_space Î²] [h : second_countable_topology_either Î± Î²]
  [opens_measurable_space Î±] [pseudo_metrizable_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {Î¼ : measure Î±} (hf : continuous_on f s) (hs : measurable_set s) :
  ae_strongly_measurable f (Î¼.restrict s) :=
begin
  borelize Î²,
  refine ae_strongly_measurable_iff_ae_measurable_separable.2 âŸ¨hf.ae_measurable hs, f '' s, _,
    mem_of_superset (self_mem_ae_restrict hs) (subset_preimage_image _ _)âŸ©,
  casesI h.out,
  { let f' : s â†’ Î² := s.restrict f,
    have A : continuous f' := continuous_on_iff_continuous_restrict.1 hf,
    have B : is_separable (univ : set s) := is_separable_of_separable_space _,
    convert is_separable.image B A using 1,
    ext x,
    simp },
  { exact is_separable_of_separable_space _ }
end

/-- A function which is continuous on a compact set `s` is almost everywhere strongly measurable
with respect to `Î¼.restrict s`. -/
lemma continuous_on.ae_strongly_measurable_of_is_compact
  [topological_space Î±] [opens_measurable_space Î±] [topological_space Î²] [pseudo_metrizable_space Î²]
  {f : Î± â†’ Î²} {s : set Î±} {Î¼ : measure Î±}
  (hf : continuous_on f s) (hs : is_compact s) (h's : measurable_set s) :
  ae_strongly_measurable f (Î¼.restrict s) :=
begin
  letI := pseudo_metrizable_space_pseudo_metric Î²,
  borelize Î²,
  rw ae_strongly_measurable_iff_ae_measurable_separable,
  refine âŸ¨hf.ae_measurable h's, f '' s, _, _âŸ©,
  { exact (hs.image_of_continuous_on hf).is_separable },
  { exact mem_of_superset (self_mem_ae_restrict h's) (subset_preimage_image _ _) }
end

lemma continuous_on.integrable_at_nhds_within_of_is_separable
  [topological_space Î±] [pseudo_metrizable_space Î±]
  [opens_measurable_space Î±] {Î¼ : measure Î±} [is_locally_finite_measure Î¼]
  {a : Î±} {t : set Î±} {f : Î± â†’ E} (hft : continuous_on f t) (ht : measurable_set t)
  (h't : topological_space.is_separable t) (ha : a âˆˆ t) :
  integrable_at_filter f (ð“[t] a) Î¼ :=
begin
  haveI : (ð“[t] a).is_measurably_generated := ht.nhds_within_is_measurably_generated _,
  exact (hft a ha).integrable_at_filter âŸ¨_, self_mem_nhds_within,
    hft.ae_strongly_measurable_of_is_separable ht h'tâŸ© (Î¼.finite_at_nhds_within _ _),
end

lemma continuous_on.integrable_at_nhds_within
  [topological_space Î±] [second_countable_topology_either Î± E]
  [opens_measurable_space Î±] {Î¼ : measure Î±} [is_locally_finite_measure Î¼]
  {a : Î±} {t : set Î±} {f : Î± â†’ E} (hft : continuous_on f t) (ht : measurable_set t) (ha : a âˆˆ t) :
  integrable_at_filter f (ð“[t] a) Î¼ :=
begin
  haveI : (ð“[t] a).is_measurably_generated := ht.nhds_within_is_measurably_generated _,
  exact (hft a ha).integrable_at_filter âŸ¨_, self_mem_nhds_within, hft.ae_strongly_measurable htâŸ©
    (Î¼.finite_at_nhds_within _ _),
end

lemma continuous.integrable_at_nhds
  [topological_space Î±] [second_countable_topology_either Î± E]
  [opens_measurable_space Î±] {Î¼ : measure Î±} [is_locally_finite_measure Î¼]
  {f : Î± â†’ E} (hf : continuous f) (a : Î±) :
  integrable_at_filter f (ð“ a) Î¼ :=
begin
  rw â† nhds_within_univ,
  exact hf.continuous_on.integrable_at_nhds_within measurable_set.univ (mem_univ a),
end

/-- If a function is continuous on an open set `s`, then it is strongly measurable at the filter
`ð“ x` for all `x âˆˆ s` if either the source space or the target space is second-countable. -/
lemma continuous_on.strongly_measurable_at_filter [topological_space Î±]
  [opens_measurable_space Î±] [topological_space Î²] [pseudo_metrizable_space Î²]
  [second_countable_topology_either Î± Î²] {f : Î± â†’ Î²} {s : set Î±} {Î¼ : measure Î±}
  (hs : is_open s) (hf : continuous_on f s) :
  âˆ€ x âˆˆ s, strongly_measurable_at_filter f (ð“ x) Î¼ :=
Î» x hx, âŸ¨s, is_open.mem_nhds hs hx, hf.ae_strongly_measurable hs.measurable_setâŸ©

lemma continuous_at.strongly_measurable_at_filter
  [topological_space Î±] [opens_measurable_space Î±] [second_countable_topology_either Î± E]
  {f : Î± â†’ E} {s : set Î±} {Î¼ : measure Î±} (hs : is_open s) (hf : âˆ€ x âˆˆ s, continuous_at f x) :
  âˆ€ x âˆˆ s, strongly_measurable_at_filter f (ð“ x) Î¼ :=
continuous_on.strongly_measurable_at_filter hs $ continuous_at.continuous_on hf

lemma continuous.strongly_measurable_at_filter [topological_space Î±] [opens_measurable_space Î±]
  [topological_space Î²] [pseudo_metrizable_space Î²] [second_countable_topology_either Î± Î²]
  {f : Î± â†’ Î²} (hf : continuous f) (Î¼ : measure Î±) (l : filter Î±) :
  strongly_measurable_at_filter f l Î¼ :=
hf.strongly_measurable.strongly_measurable_at_filter

/-- If a function is continuous on a measurable set `s`, then it is measurable at the filter
  `ð“[s] x` for all `x`. -/
lemma continuous_on.strongly_measurable_at_filter_nhds_within {Î± Î² : Type*} [measurable_space Î±]
  [topological_space Î±] [opens_measurable_space Î±] [topological_space Î²] [pseudo_metrizable_space Î²]
  [second_countable_topology_either Î± Î²] {f : Î± â†’ Î²} {s : set Î±} {Î¼ : measure Î±}
  (hf : continuous_on f s) (hs : measurable_set s) (x : Î±) :
  strongly_measurable_at_filter f (ð“[s] x) Î¼ :=
âŸ¨s, self_mem_nhds_within, hf.ae_strongly_measurable hsâŸ©
