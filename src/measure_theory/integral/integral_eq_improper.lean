/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Bhavik Mehta
-/
import measure_theory.integral.interval_integral
import order.filter.at_top_bot
import measure_theory.function.jacobian

/-!
# Links between an integral and its "improper" version

In its current state, mathlib only knows how to talk about definite ("proper") integrals,
in the sense that it treats integrals over `[x, +âˆ)` the same as it treats integrals over
`[y, z]`. For example, the integral over `[1, +âˆ)` is **not** defined to be the limit of
the integral over `[1, x]` as `x` tends to `+âˆ`, which is known as an **improper integral**.

Indeed, the "proper" definition is stronger than the "improper" one. The usual counterexample
is `x â†¦ sin(x)/x`, which has an improper integral over `[1, +âˆ)` but no definite integral.

Although definite integrals have better properties, they are hardly usable when it comes to
computing integrals on unbounded sets, which is much easier using limits. Thus, in this file,
we prove various ways of studying the proper integral by studying the improper one.

## Definitions

The main definition of this file is `measure_theory.ae_cover`. It is a rather technical
definition whose sole purpose is generalizing and factoring proofs. Given an index type `Î¹`, a
countably generated filter `l` over `Î¹`, and an `Î¹`-indexed family `Ï†` of subsets of a measurable
space `Î±` equipped with a measure `Î¼`, one should think of a hypothesis `hÏ† : ae_cover Î¼ l Ï†` as
a sufficient condition for being able to interpret `âˆ« x, f x âˆ‚Î¼` (if it exists) as the limit
of `âˆ« x in Ï† i, f x âˆ‚Î¼` as `i` tends to `l`.

When using this definition with a measure restricted to a set `s`, which happens fairly often,
one should not try too hard to use a `ae_cover` of subsets of `s`, as it often makes proofs
more complicated than necessary. See for example the proof of
`measure_theory.integrable_on_Iic_of_interval_integral_norm_tendsto` where we use `(Î» x, Ioi x)`
as an `ae_cover` w.r.t. `Î¼.restrict (Iic b)`, instead of using `(Î» x, Ioc x b)`.

## Main statements

- `measure_theory.ae_cover.lintegral_tendsto_of_countably_generated` : if `Ï†` is a `ae_cover Î¼ l`,
  where `l` is a countably generated filter, and if `f` is a measurable `ennreal`-valued function,
  then `âˆ«â» x in Ï† n, f x âˆ‚Î¼` tends to `âˆ«â» x, f x âˆ‚Î¼` as `n` tends to `l`
- `measure_theory.ae_cover.integrable_of_integral_norm_tendsto` : if `Ï†` is a `ae_cover Î¼ l`,
  where `l` is a countably generated filter, if `f` is measurable and integrable on each `Ï† n`,
  and if `âˆ« x in Ï† n, â€–f xâ€– âˆ‚Î¼` tends to some `I : â„` as n tends to `l`, then `f` is integrable
- `measure_theory.ae_cover.integral_tendsto_of_countably_generated` : if `Ï†` is a `ae_cover Î¼ l`,
  where `l` is a countably generated filter, and if `f` is measurable and integrable (globally),
  then `âˆ« x in Ï† n, f x âˆ‚Î¼` tends to `âˆ« x, f x âˆ‚Î¼` as `n` tends to `+âˆ`.

We then specialize these lemmas to various use cases involving intervals, which are frequent
in analysis.
-/

open measure_theory filter set topological_space
open_locale ennreal nnreal topology

namespace measure_theory

section ae_cover

variables {Î± Î¹ : Type*} [measurable_space Î±] (Î¼ : measure Î±) (l : filter Î¹)

/-- A sequence `Ï†` of subsets of `Î±` is a `ae_cover` w.r.t. a measure `Î¼` and a filter `l`
    if almost every point (w.r.t. `Î¼`) of `Î±` eventually belongs to `Ï† n` (w.r.t. `l`), and if
    each `Ï† n` is measurable.
    This definition is a technical way to avoid duplicating a lot of proofs.
    It should be thought of as a sufficient condition for being able to interpret
    `âˆ« x, f x âˆ‚Î¼` (if it exists) as the limit of `âˆ« x in Ï† n, f x âˆ‚Î¼` as `n` tends to `l`.

    See for example `measure_theory.ae_cover.lintegral_tendsto_of_countably_generated`,
    `measure_theory.ae_cover.integrable_of_integral_norm_tendsto` and
    `measure_theory.ae_cover.integral_tendsto_of_countably_generated`. -/
structure ae_cover (Ï† : Î¹ â†’ set Î±) : Prop :=
(ae_eventually_mem : âˆ€áµ x âˆ‚Î¼, âˆ€á¶  i in l, x âˆˆ Ï† i)
(measurable : âˆ€ i, measurable_set $ Ï† i)

variables {Î¼} {l}

section preorder_Î±

variables [preorder Î±] [topological_space Î±] [order_closed_topology Î±]
  [opens_measurable_space Î±] {a b : Î¹ â†’ Î±}
  (ha : tendsto a l at_bot) (hb : tendsto b l at_top)

lemma ae_cover_Icc :
  ae_cover Î¼ l (Î» i, Icc (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (ha.eventually $ eventually_le_at_bot x).mp $
    (hb.eventually $ eventually_ge_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  measurable := Î» i, measurable_set_Icc }

lemma ae_cover_Ici :
  ae_cover Î¼ l (Î» i, Ici $ a i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (ha.eventually $ eventually_le_at_bot x).mono $
    Î» i hai, hai ),
  measurable := Î» i, measurable_set_Ici }

lemma ae_cover_Iic :
  ae_cover Î¼ l (Î» i, Iic $ b i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (hb.eventually $ eventually_ge_at_top x).mono $
    Î» i hbi, hbi ),
  measurable := Î» i, measurable_set_Iic }

end preorder_Î±

section linear_order_Î±

variables [linear_order Î±] [topological_space Î±] [order_closed_topology Î±]
  [opens_measurable_space Î±] {a b : Î¹ â†’ Î±}
  (ha : tendsto a l at_bot) (hb : tendsto b l at_top)

lemma ae_cover_Ioo [no_min_order Î±] [no_max_order Î±] :
  ae_cover Î¼ l (Î» i, Ioo (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (ha.eventually $ eventually_lt_at_bot x).mp $
    (hb.eventually $ eventually_gt_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  measurable := Î» i, measurable_set_Ioo }

lemma ae_cover_Ioc [no_min_order Î±] : ae_cover Î¼ l (Î» i, Ioc (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (ha.eventually $ eventually_lt_at_bot x).mp $
    (hb.eventually $ eventually_ge_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  measurable := Î» i, measurable_set_Ioc }

lemma ae_cover_Ico [no_max_order Î±] : ae_cover Î¼ l (Î» i, Ico (a i) (b i)) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (ha.eventually $ eventually_le_at_bot x).mp $
    (hb.eventually $ eventually_gt_at_top x).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ© ),
  measurable := Î» i, measurable_set_Ico }

lemma ae_cover_Ioi [no_min_order Î±] :
  ae_cover Î¼ l (Î» i, Ioi $ a i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (ha.eventually $ eventually_lt_at_bot x).mono $
    Î» i hai, hai ),
  measurable := Î» i, measurable_set_Ioi }

lemma ae_cover_Iio [no_max_order Î±] :
  ae_cover Î¼ l (Î» i, Iio $ b i) :=
{ ae_eventually_mem := ae_of_all Î¼ (Î» x,
    (hb.eventually $ eventually_gt_at_top x).mono $
    Î» i hbi, hbi ),
  measurable := Î» i, measurable_set_Iio }

end linear_order_Î±

section finite_intervals

variables [linear_order Î±] [topological_space Î±] [order_closed_topology Î±]
  [opens_measurable_space Î±] {a b : Î¹ â†’ Î±} {A B : Î±}
  (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B))

lemma ae_cover_Ioo_of_Icc :
  ae_cover (Î¼.restrict $ Ioo A B) l (Î» i, Icc (a i) (b i)) :=
{ ae_eventually_mem := (ae_restrict_iff' measurable_set_Ioo).mpr (
    ae_of_all Î¼ (Î» x hx,
    (ha.eventually $ eventually_le_nhds hx.left).mp $
    (hb.eventually $ eventually_ge_nhds hx.right).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ©)),
  measurable := Î» i, measurable_set_Icc, }

lemma ae_cover_Ioo_of_Ico :
  ae_cover (Î¼.restrict $ Ioo A B) l (Î» i, Ico (a i) (b i)) :=
{ ae_eventually_mem := (ae_restrict_iff' measurable_set_Ioo).mpr (
    ae_of_all Î¼ (Î» x hx,
    (ha.eventually $ eventually_le_nhds hx.left).mp $
    (hb.eventually $ eventually_gt_nhds hx.right).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ©)),
  measurable := Î» i, measurable_set_Ico, }

lemma ae_cover_Ioo_of_Ioc :
  ae_cover (Î¼.restrict $ Ioo A B) l (Î» i, Ioc (a i) (b i)) :=
{ ae_eventually_mem := (ae_restrict_iff' measurable_set_Ioo).mpr (
    ae_of_all Î¼ (Î» x hx,
    (ha.eventually $ eventually_lt_nhds hx.left).mp $
    (hb.eventually $ eventually_ge_nhds hx.right).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ©)),
  measurable := Î» i, measurable_set_Ioc, }

lemma ae_cover_Ioo_of_Ioo :
  ae_cover (Î¼.restrict $ Ioo A B) l (Î» i, Ioo (a i) (b i)) :=
{ ae_eventually_mem := (ae_restrict_iff' measurable_set_Ioo).mpr (
    ae_of_all Î¼ (Î» x hx,
    (ha.eventually $ eventually_lt_nhds hx.left).mp $
    (hb.eventually $ eventually_gt_nhds hx.right).mono $
    Î» i hbi hai, âŸ¨hai, hbiâŸ©)),
  measurable := Î» i, measurable_set_Ioo, }

variables [has_no_atoms Î¼]

lemma ae_cover_Ioc_of_Icc (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ioc A B) l (Î» i, Icc (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ioc.symm, ae_cover_Ioo_of_Icc ha hb]

lemma ae_cover_Ioc_of_Ico (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ioc A B) l (Î» i, Ico (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ioc.symm, ae_cover_Ioo_of_Ico ha hb]

lemma ae_cover_Ioc_of_Ioc (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ioc A B) l (Î» i, Ioc (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ioc.symm, ae_cover_Ioo_of_Ioc ha hb]

lemma ae_cover_Ioc_of_Ioo (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ioc A B) l (Î» i, Ioo (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ioc.symm, ae_cover_Ioo_of_Ioo ha hb]

lemma ae_cover_Ico_of_Icc (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ico A B) l (Î» i, Icc (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ico.symm, ae_cover_Ioo_of_Icc ha hb]

lemma ae_cover_Ico_of_Ico (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ico A B) l (Î» i, Ico (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ico.symm, ae_cover_Ioo_of_Ico ha hb]

lemma ae_cover_Ico_of_Ioc (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ico A B) l (Î» i, Ioc (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ico.symm, ae_cover_Ioo_of_Ioc ha hb]

lemma ae_cover_Ico_of_Ioo (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Ico A B) l (Î» i, Ioo (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Ico.symm, ae_cover_Ioo_of_Ioo ha hb]

lemma ae_cover_Icc_of_Icc (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Icc A B) l (Î» i, Icc (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Icc.symm, ae_cover_Ioo_of_Icc ha hb]

lemma ae_cover_Icc_of_Ico (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Icc A B) l (Î» i, Ico (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Icc.symm, ae_cover_Ioo_of_Ico ha hb]

lemma ae_cover_Icc_of_Ioc (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Icc A B) l (Î» i, Ioc (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Icc.symm, ae_cover_Ioo_of_Ioc ha hb]

lemma ae_cover_Icc_of_Ioo (ha : tendsto a l (ğ“ A)) (hb : tendsto b l (ğ“ B)) :
  ae_cover (Î¼.restrict $ Icc A B) l (Î» i, Ioo (a i) (b i)) :=
by simp [measure.restrict_congr_set Ioo_ae_eq_Icc.symm, ae_cover_Ioo_of_Ioo ha hb]

end finite_intervals

lemma ae_cover.restrict {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {s : set Î±} :
  ae_cover (Î¼.restrict s) l Ï† :=
{ ae_eventually_mem := ae_restrict_of_ae hÏ†.ae_eventually_mem,
  measurable := hÏ†.measurable }

lemma ae_cover_restrict_of_ae_imp {s : set Î±} {Ï† : Î¹ â†’ set Î±}
  (hs : measurable_set s) (ae_eventually_mem : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆ€á¶  n in l, x âˆˆ Ï† n)
  (measurable : âˆ€ n, measurable_set $ Ï† n) :
  ae_cover (Î¼.restrict s) l Ï† :=
{ ae_eventually_mem := by rwa ae_restrict_iff' hs,
  measurable := measurable }

lemma ae_cover.inter_restrict {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†)
  {s : set Î±} (hs : measurable_set s) :
  ae_cover (Î¼.restrict s) l (Î» i, Ï† i âˆ© s) :=
ae_cover_restrict_of_ae_imp hs
  (hÏ†.ae_eventually_mem.mono (Î» x hx hxs, hx.mono $ Î» i hi, âŸ¨hi, hxsâŸ©))
  (Î» i, (hÏ†.measurable i).inter hs)

lemma ae_cover.ae_tendsto_indicator {Î² : Type*} [has_zero Î²] [topological_space Î²]
  (f : Î± â†’ Î²) {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) :
  âˆ€áµ x âˆ‚Î¼, tendsto (Î» i, (Ï† i).indicator f x) l (ğ“ $ f x) :=
hÏ†.ae_eventually_mem.mono (Î» x hx, tendsto_const_nhds.congr' $
  hx.mono $ Î» n hn, (indicator_of_mem hn _).symm)

lemma ae_cover.ae_measurable {Î² : Type*} [measurable_space Î²] [l.is_countably_generated] [l.ne_bot]
  {f : Î± â†’ Î²} {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†)
  (hfm : âˆ€ i, ae_measurable f (Î¼.restrict $ Ï† i)) : ae_measurable f Î¼ :=
begin
  obtain âŸ¨u, huâŸ© := l.exists_seq_tendsto,
  have := ae_measurable_Union_iff.mpr (Î» (n : â„•), hfm (u n)),
  rwa measure.restrict_eq_self_of_ae_mem at this,
  filter_upwards [hÏ†.ae_eventually_mem] with x hx using
    let âŸ¨i, hiâŸ© := (hu.eventually hx).exists in mem_Union.mpr âŸ¨i, hiâŸ©
end

lemma ae_cover.ae_strongly_measurable {Î² : Type*} [topological_space Î²] [pseudo_metrizable_space Î²]
  [l.is_countably_generated] [l.ne_bot]
  {f : Î± â†’ Î²} {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†)
  (hfm : âˆ€ i, ae_strongly_measurable f (Î¼.restrict $ Ï† i)) : ae_strongly_measurable f Î¼ :=
begin
  obtain âŸ¨u, huâŸ© := l.exists_seq_tendsto,
  have := ae_strongly_measurable_Union_iff.mpr (Î» (n : â„•), hfm (u n)),
  rwa measure.restrict_eq_self_of_ae_mem at this,
  filter_upwards [hÏ†.ae_eventually_mem] with x hx using
    let âŸ¨i, hiâŸ© := (hu.eventually hx).exists in mem_Union.mpr âŸ¨i, hiâŸ©
end


end ae_cover

lemma ae_cover.comp_tendsto {Î± Î¹ Î¹' : Type*} [measurable_space Î±] {Î¼ : measure Î±} {l : filter Î¹}
  {l' : filter Î¹'} {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†)
  {u : Î¹' â†’ Î¹} (hu : tendsto u l' l) :
  ae_cover Î¼ l' (Ï† âˆ˜ u) :=
{ ae_eventually_mem := hÏ†.ae_eventually_mem.mono (Î» x hx, hu.eventually hx),
  measurable := Î» i, hÏ†.measurable (u i) }

section ae_cover_Union_Inter_countable

variables {Î± Î¹ : Type*} [countable Î¹]
  [measurable_space Î±] {Î¼ : measure Î±}

lemma ae_cover.bUnion_Iic_ae_cover [preorder Î¹] {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ at_top Ï†) :
  ae_cover Î¼ at_top (Î» (n : Î¹), â‹ƒ k (h : k âˆˆ Iic n), Ï† k) :=
{ ae_eventually_mem := hÏ†.ae_eventually_mem.mono
    (Î» x h, h.mono (Î» i hi, mem_bUnion right_mem_Iic hi)),
  measurable := Î» i, measurable_set.bUnion (to_countable _) (Î» n _, hÏ†.measurable n) }

lemma ae_cover.bInter_Ici_ae_cover [semilattice_sup Î¹] [nonempty Î¹] {Ï† : Î¹ â†’ set Î±}
  (hÏ† : ae_cover Î¼ at_top Ï†) : ae_cover Î¼ at_top (Î» (n : Î¹), â‹‚ k (h : k âˆˆ Ici n), Ï† k) :=
{ ae_eventually_mem := hÏ†.ae_eventually_mem.mono
    begin
      intros x h,
      rw eventually_at_top at *,
      rcases h with âŸ¨i, hiâŸ©,
      use i,
      intros j hj,
      exact mem_bInter (Î» k hk, hi k (le_trans hj hk)),
    end,
  measurable := Î» i, measurable_set.bInter (to_countable _) (Î» n _, hÏ†.measurable n) }

end ae_cover_Union_Inter_countable

section lintegral

variables {Î± Î¹ : Type*} [measurable_space Î±] {Î¼ : measure Î±} {l : filter Î¹}

private lemma lintegral_tendsto_of_monotone_of_nat {Ï† : â„• â†’ set Î±}
  (hÏ† : ae_cover Î¼ at_top Ï†) (hmono : monotone Ï†) {f : Î± â†’ â„â‰¥0âˆ} (hfm : ae_measurable f Î¼) :
  tendsto (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) at_top (ğ“ $ âˆ«â» x, f x âˆ‚Î¼) :=
let F := Î» n, (Ï† n).indicator f in
have keyâ‚ : âˆ€ n, ae_measurable (F n) Î¼, from Î» n, hfm.indicator (hÏ†.measurable n),
have keyâ‚‚ : âˆ€áµ (x : Î±) âˆ‚Î¼, monotone (Î» n, F n x), from ae_of_all _
  (Î» x i j hij, indicator_le_indicator_of_subset (hmono hij) (Î» x, zero_le $ f x) x),
have keyâ‚ƒ : âˆ€áµ (x : Î±) âˆ‚Î¼, tendsto (Î» n, F n x) at_top (ğ“ (f x)), from hÏ†.ae_tendsto_indicator f,
(lintegral_tendsto_of_tendsto_of_monotone keyâ‚ keyâ‚‚ keyâ‚ƒ).congr
  (Î» n, lintegral_indicator f (hÏ†.measurable n))

lemma ae_cover.lintegral_tendsto_of_nat {Ï† : â„• â†’ set Î±} (hÏ† : ae_cover Î¼ at_top Ï†)
  {f : Î± â†’ â„â‰¥0âˆ} (hfm : ae_measurable f Î¼) :
  tendsto (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) at_top (ğ“ $ âˆ«â» x, f x âˆ‚Î¼) :=
begin
  have limâ‚ := lintegral_tendsto_of_monotone_of_nat (hÏ†.bInter_Ici_ae_cover)
    (Î» i j hij, bInter_subset_bInter_left (Ici_subset_Ici.mpr hij)) hfm,
  have limâ‚‚ := lintegral_tendsto_of_monotone_of_nat (hÏ†.bUnion_Iic_ae_cover)
    (Î» i j hij, bUnion_subset_bUnion_left (Iic_subset_Iic.mpr hij)) hfm,
  have leâ‚ := Î» n, lintegral_mono_set (bInter_subset_of_mem left_mem_Ici),
  have leâ‚‚ := Î» n, lintegral_mono_set (subset_bUnion_of_mem right_mem_Iic),
  exact tendsto_of_tendsto_of_tendsto_of_le_of_le limâ‚ limâ‚‚ leâ‚ leâ‚‚
end

lemma ae_cover.lintegral_tendsto_of_countably_generated [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ â„â‰¥0âˆ}
  (hfm : ae_measurable f Î¼) : tendsto (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) l (ğ“ $ âˆ«â» x, f x âˆ‚Î¼) :=
tendsto_of_seq_tendsto (Î» u hu, (hÏ†.comp_tendsto hu).lintegral_tendsto_of_nat hfm)

lemma ae_cover.lintegral_eq_of_tendsto [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ â„â‰¥0âˆ} (I : â„â‰¥0âˆ)
  (hfm : ae_measurable f Î¼) (htendsto : tendsto (Î» i, âˆ«â» x in Ï† i, f x âˆ‚Î¼) l (ğ“ I)) :
  âˆ«â» x, f x âˆ‚Î¼ = I :=
tendsto_nhds_unique (hÏ†.lintegral_tendsto_of_countably_generated hfm) htendsto

lemma ae_cover.supr_lintegral_eq_of_countably_generated [nonempty Î¹] [l.ne_bot]
  [l.is_countably_generated] {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ â„â‰¥0âˆ}
  (hfm : ae_measurable f Î¼) : (â¨† (i : Î¹), âˆ«â» x in Ï† i, f x âˆ‚Î¼) = âˆ«â» x, f x âˆ‚Î¼ :=
begin
  have := hÏ†.lintegral_tendsto_of_countably_generated hfm,
  refine csupr_eq_of_forall_le_of_forall_lt_exists_gt
    (Î» i, lintegral_mono' measure.restrict_le_self le_rfl) (Î» w hw, _),
  rcases exists_between hw with âŸ¨m, hmâ‚, hmâ‚‚âŸ©,
  rcases (eventually_ge_of_tendsto_gt hmâ‚‚ this).exists with âŸ¨i, hiâŸ©,
  exact âŸ¨i, lt_of_lt_of_le hmâ‚ hiâŸ©,
end

end lintegral

section integrable

variables {Î± Î¹ E : Type*} [measurable_space Î±] {Î¼ : measure Î±} {l : filter Î¹}
  [normed_add_comm_group E]

lemma ae_cover.integrable_of_lintegral_nnnorm_bounded [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E} (I : â„) (hfm : ae_strongly_measurable f Î¼)
  (hbounded : âˆ€á¶  i in l, âˆ«â» x in Ï† i, â€–f xâ€–â‚Š âˆ‚Î¼ â‰¤ ennreal.of_real I) :
  integrable f Î¼ :=
begin
  refine âŸ¨hfm, (le_of_tendsto _ hbounded).trans_lt ennreal.of_real_lt_topâŸ©,
  exact hÏ†.lintegral_tendsto_of_countably_generated hfm.ennnorm
end

lemma ae_cover.integrable_of_lintegral_nnnorm_tendsto [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E} (I : â„)
  (hfm : ae_strongly_measurable f Î¼)
  (htendsto : tendsto (Î» i, âˆ«â» x in Ï† i, â€–f xâ€–â‚Š âˆ‚Î¼) l (ğ“ $ ennreal.of_real I)) :
  integrable f Î¼ :=
begin
  refine hÏ†.integrable_of_lintegral_nnnorm_bounded (max 1 (I + 1)) hfm _,
  refine htendsto.eventually (ge_mem_nhds _),
  refine (ennreal.of_real_lt_of_real_iff (lt_max_of_lt_left zero_lt_one)).2 _,
  exact lt_max_of_lt_right (lt_add_one I),
end

lemma ae_cover.integrable_of_lintegral_nnnorm_bounded' [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E} (I : â„â‰¥0) (hfm : ae_strongly_measurable f Î¼)
  (hbounded : âˆ€á¶  i in l, âˆ«â» x in Ï† i, â€–f xâ€–â‚Š âˆ‚Î¼ â‰¤ I) :
  integrable f Î¼ :=
hÏ†.integrable_of_lintegral_nnnorm_bounded I hfm
  (by simpa only [ennreal.of_real_coe_nnreal] using hbounded)

lemma ae_cover.integrable_of_lintegral_nnnorm_tendsto' [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E} (I : â„â‰¥0)
  (hfm : ae_strongly_measurable f Î¼)
  (htendsto : tendsto (Î» i, âˆ«â» x in Ï† i, â€–f xâ€–â‚Š âˆ‚Î¼) l (ğ“ I)) :
  integrable f Î¼ :=
hÏ†.integrable_of_lintegral_nnnorm_tendsto I hfm
  (by simpa only [ennreal.of_real_coe_nnreal] using htendsto)

lemma ae_cover.integrable_of_integral_norm_bounded [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E}
  (I : â„) (hfi : âˆ€ i, integrable_on f (Ï† i) Î¼)
  (hbounded : âˆ€á¶  i in l, âˆ« x in Ï† i, â€–f xâ€– âˆ‚Î¼ â‰¤ I) :
  integrable f Î¼ :=
begin
  have hfm : ae_strongly_measurable f Î¼ :=
    hÏ†.ae_strongly_measurable (Î» i, (hfi i).ae_strongly_measurable),
  refine hÏ†.integrable_of_lintegral_nnnorm_bounded I hfm _,
  conv at hbounded in (integral _ _)
  { rw integral_eq_lintegral_of_nonneg_ae (ae_of_all _ (Î» x, @norm_nonneg E _ (f x)))
    hfm.norm.restrict },
  conv at hbounded in (ennreal.of_real _) { dsimp, rw â† coe_nnnorm, rw ennreal.of_real_coe_nnreal },
  refine hbounded.mono (Î» i hi, _),
  rw â†ennreal.of_real_to_real (ne_top_of_lt (hfi i).2),
  apply ennreal.of_real_le_of_real hi,
end

lemma ae_cover.integrable_of_integral_norm_tendsto [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E}
  (I : â„) (hfi : âˆ€ i, integrable_on f (Ï† i) Î¼)
  (htendsto : tendsto (Î» i, âˆ« x in Ï† i, â€–f xâ€– âˆ‚Î¼) l (ğ“ I)) :
  integrable f Î¼ :=
let âŸ¨I', hI'âŸ© := htendsto.is_bounded_under_le in hÏ†.integrable_of_integral_norm_bounded I' hfi hI'

lemma ae_cover.integrable_of_integral_bounded_of_nonneg_ae [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ â„} (I : â„)
  (hfi : âˆ€ i, integrable_on f (Ï† i) Î¼) (hnng : âˆ€áµ x âˆ‚Î¼, 0 â‰¤ f x)
  (hbounded : âˆ€á¶  i in l, âˆ« x in Ï† i, f x âˆ‚Î¼ â‰¤ I) :
  integrable f Î¼ :=
hÏ†.integrable_of_integral_norm_bounded I hfi $ hbounded.mono $ Î» i hi,
  (integral_congr_ae $ ae_restrict_of_ae $ hnng.mono $ Î» x, real.norm_of_nonneg).le.trans hi

lemma ae_cover.integrable_of_integral_tendsto_of_nonneg_ae [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ â„} (I : â„)
  (hfi : âˆ€ i, integrable_on f (Ï† i) Î¼) (hnng : âˆ€áµ x âˆ‚Î¼, 0 â‰¤ f x)
  (htendsto : tendsto (Î» i, âˆ« x in Ï† i, f x âˆ‚Î¼) l (ğ“ I)) :
  integrable f Î¼ :=
let âŸ¨I', hI'âŸ© := htendsto.is_bounded_under_le in
  hÏ†.integrable_of_integral_bounded_of_nonneg_ae I' hfi hnng hI'

end integrable

section integral

variables {Î± Î¹ E : Type*} [measurable_space Î±] {Î¼ : measure Î±} {l : filter Î¹}
  [normed_add_comm_group E] [normed_space â„ E] [complete_space E]

lemma ae_cover.integral_tendsto_of_countably_generated [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E} (hfi : integrable f Î¼) :
  tendsto (Î» i, âˆ« x in Ï† i, f x âˆ‚Î¼) l (ğ“ $ âˆ« x, f x âˆ‚Î¼) :=
suffices h : tendsto (Î» i, âˆ« (x : Î±), (Ï† i).indicator f x âˆ‚Î¼) l (ğ“ (âˆ« (x : Î±), f x âˆ‚Î¼)),
by { convert h, ext n, rw integral_indicator (hÏ†.measurable n) },
tendsto_integral_filter_of_dominated_convergence (Î» x, â€–f xâ€–)
  (eventually_of_forall $ Î» i, hfi.ae_strongly_measurable.indicator $ hÏ†.measurable i)
  (eventually_of_forall $ Î» i, ae_of_all _ $ Î» x, norm_indicator_le_norm_self _ _)
  hfi.norm (hÏ†.ae_tendsto_indicator f)

/-- Slight reformulation of
    `measure_theory.ae_cover.integral_tendsto_of_countably_generated`. -/
lemma ae_cover.integral_eq_of_tendsto [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ E}
  (I : E) (hfi : integrable f Î¼)
  (h : tendsto (Î» n, âˆ« x in Ï† n, f x âˆ‚Î¼) l (ğ“ I)) :
  âˆ« x, f x âˆ‚Î¼ = I :=
tendsto_nhds_unique (hÏ†.integral_tendsto_of_countably_generated hfi) h

lemma ae_cover.integral_eq_of_tendsto_of_nonneg_ae [l.ne_bot] [l.is_countably_generated]
  {Ï† : Î¹ â†’ set Î±} (hÏ† : ae_cover Î¼ l Ï†) {f : Î± â†’ â„} (I : â„)
  (hnng : 0 â‰¤áµ[Î¼] f) (hfi : âˆ€ n, integrable_on f (Ï† n) Î¼)
  (htendsto : tendsto (Î» n, âˆ« x in Ï† n, f x âˆ‚Î¼) l (ğ“ I)) :
  âˆ« x, f x âˆ‚Î¼ = I :=
have hfi' : integrable f Î¼,
  from hÏ†.integrable_of_integral_tendsto_of_nonneg_ae I hfi hnng htendsto,
hÏ†.integral_eq_of_tendsto I hfi' htendsto

end integral

section integrable_of_interval_integral

variables {Î¹ E : Type*} {Î¼ : measure â„}
          {l : filter Î¹} [filter.ne_bot l] [is_countably_generated l]
          [normed_add_comm_group E]
          {a b : Î¹ â†’ â„} {f : â„ â†’ E}

lemma integrable_of_interval_integral_norm_bounded
  (I : â„) (hfi : âˆ€ i, integrable_on f (Ioc (a i) (b i)) Î¼)
  (ha : tendsto a l at_bot) (hb : tendsto b l at_top)
  (h : âˆ€á¶  i in l, âˆ« x in a i .. b i, â€–f xâ€– âˆ‚Î¼ â‰¤ I) :
  integrable f Î¼ :=
begin
  have hÏ† : ae_cover Î¼ l _ := ae_cover_Ioc ha hb,
  refine hÏ†.integrable_of_integral_norm_bounded I hfi (h.mp _),
  filter_upwards [ha.eventually (eventually_le_at_bot 0), hb.eventually (eventually_ge_at_top 0)]
    with i hai hbi ht,
  rwa â†interval_integral.integral_of_le (hai.trans hbi)
end

/-- If `f` is integrable on intervals `Ioc (a i) (b i)`,
where `a i` tends to -âˆ and `b i` tends to âˆ, and
`âˆ« x in a i .. b i, â€–f xâ€– âˆ‚Î¼` converges to `I : â„` along a filter `l`,
then `f` is integrable on the interval (-âˆ, âˆ) -/
lemma integrable_of_interval_integral_norm_tendsto
  (I : â„) (hfi : âˆ€ i, integrable_on f (Ioc (a i) (b i)) Î¼)
  (ha : tendsto a l at_bot) (hb : tendsto b l at_top)
  (h : tendsto (Î» i, âˆ« x in a i .. b i, â€–f xâ€– âˆ‚Î¼) l (ğ“ I)) :
  integrable f Î¼ :=
let âŸ¨I', hI'âŸ© := h.is_bounded_under_le in
  integrable_of_interval_integral_norm_bounded I' hfi ha hb hI'

lemma integrable_on_Iic_of_interval_integral_norm_bounded (I b : â„)
  (hfi : âˆ€ i, integrable_on f (Ioc (a i) b) Î¼) (ha : tendsto a l at_bot)
  (h : âˆ€á¶  i in l, (âˆ« x in a i .. b, â€–f xâ€– âˆ‚Î¼) â‰¤ I) :
  integrable_on f (Iic b) Î¼ :=
begin
  have hÏ† : ae_cover (Î¼.restrict $ Iic b) l _ := ae_cover_Ioi ha,
  have hfi : âˆ€ i, integrable_on f (Ioi (a i)) (Î¼.restrict $ Iic b),
  { intro i,
    rw [integrable_on, measure.restrict_restrict (hÏ†.measurable i)],
    exact hfi i },
  refine hÏ†.integrable_of_integral_norm_bounded I hfi (h.mp _),
  filter_upwards [ha.eventually (eventually_le_at_bot b)] with i hai,
  rw [interval_integral.integral_of_le hai, measure.restrict_restrict (hÏ†.measurable i)],
  exact id
end

/-- If `f` is integrable on intervals `Ioc (a i) b`,
where `a i` tends to -âˆ, and
`âˆ« x in a i .. b, â€–f xâ€– âˆ‚Î¼` converges to `I : â„` along a filter `l`,
then `f` is integrable on the interval (-âˆ, b) -/
lemma integrable_on_Iic_of_interval_integral_norm_tendsto (I b : â„)
  (hfi : âˆ€ i, integrable_on f (Ioc (a i) b) Î¼) (ha : tendsto a l at_bot)
  (h : tendsto (Î» i, âˆ« x in a i .. b, â€–f xâ€– âˆ‚Î¼) l (ğ“ I)) :
  integrable_on f (Iic b) Î¼ :=
let âŸ¨I', hI'âŸ© := h.is_bounded_under_le in
  integrable_on_Iic_of_interval_integral_norm_bounded I' b hfi ha hI'

lemma integrable_on_Ioi_of_interval_integral_norm_bounded (I a : â„)
  (hfi : âˆ€ i, integrable_on f (Ioc a (b i)) Î¼) (hb : tendsto b l at_top)
  (h : âˆ€á¶  i in l, (âˆ« x in a .. b i, â€–f xâ€– âˆ‚Î¼) â‰¤ I) :
  integrable_on f (Ioi a) Î¼ :=
begin
  have hÏ† : ae_cover (Î¼.restrict $ Ioi a) l _ := ae_cover_Iic hb,
  have hfi : âˆ€ i, integrable_on f (Iic (b i)) (Î¼.restrict $ Ioi a),
  { intro i,
    rw [integrable_on, measure.restrict_restrict (hÏ†.measurable i), inter_comm],
    exact hfi i },
  refine hÏ†.integrable_of_integral_norm_bounded I hfi (h.mp _),
  filter_upwards [hb.eventually (eventually_ge_at_top a)] with i hbi,
  rw [interval_integral.integral_of_le hbi, measure.restrict_restrict (hÏ†.measurable i),
      inter_comm],
  exact id
end

/-- If `f` is integrable on intervals `Ioc a (b i)`,
where `b i` tends to âˆ, and
`âˆ« x in a .. b i, â€–f xâ€– âˆ‚Î¼` converges to `I : â„` along a filter `l`,
then `f` is integrable on the interval (a, âˆ) -/
lemma integrable_on_Ioi_of_interval_integral_norm_tendsto (I a : â„)
  (hfi : âˆ€ i, integrable_on f (Ioc a (b i)) Î¼) (hb : tendsto b l at_top)
  (h : tendsto (Î» i, âˆ« x in a .. b i, â€–f xâ€– âˆ‚Î¼) l (ğ“ $ I)) :
  integrable_on f (Ioi a) Î¼ :=
let âŸ¨I', hI'âŸ© := h.is_bounded_under_le in
  integrable_on_Ioi_of_interval_integral_norm_bounded I' a hfi hb hI'

lemma integrable_on_Ioc_of_interval_integral_norm_bounded {I aâ‚€ bâ‚€ : â„}
  (hfi : âˆ€ i, integrable_on f $ Ioc (a i) (b i))
  (ha : tendsto a l $ ğ“ aâ‚€) (hb : tendsto b l $ ğ“ bâ‚€)
  (h : âˆ€á¶  i in l, (âˆ« x in Ioc (a i) (b i), â€–f xâ€–) â‰¤ I) : integrable_on f (Ioc aâ‚€ bâ‚€) :=
begin
  refine (ae_cover_Ioc_of_Ioc ha hb).integrable_of_integral_norm_bounded I
    (Î» i, (hfi i).restrict measurable_set_Ioc) (eventually.mono h _),
  intros i hi, simp only [measure.restrict_restrict measurable_set_Ioc],
  refine le_trans (set_integral_mono_set (hfi i).norm _ _) hi,
  { apply ae_of_all, simp only [pi.zero_apply, norm_nonneg, forall_const] },
  { apply ae_of_all, intros c hc, exact hc.1 },
end

lemma integrable_on_Ioc_of_interval_integral_norm_bounded_left {I aâ‚€ b : â„}
  (hfi : âˆ€ i, integrable_on f $ Ioc (a i) b) (ha : tendsto a l $ ğ“ aâ‚€)
  (h : âˆ€á¶  i in l, (âˆ« x in Ioc (a i) b, â€–f xâ€– ) â‰¤ I) : integrable_on f (Ioc aâ‚€ b) :=
integrable_on_Ioc_of_interval_integral_norm_bounded hfi ha tendsto_const_nhds h

lemma integrable_on_Ioc_of_interval_integral_norm_bounded_right {I a bâ‚€ : â„}
  (hfi : âˆ€ i, integrable_on f $ Ioc a (b i)) (hb : tendsto b l $ ğ“ bâ‚€)
  (h : âˆ€á¶  i in l, (âˆ« x in Ioc a (b i), â€–f xâ€– ) â‰¤ I) : integrable_on f (Ioc a bâ‚€) :=
integrable_on_Ioc_of_interval_integral_norm_bounded hfi tendsto_const_nhds hb h

end integrable_of_interval_integral

section integral_of_interval_integral

variables {Î¹ E : Type*} {Î¼ : measure â„}
          {l : filter Î¹} [is_countably_generated l]
          [normed_add_comm_group E] [normed_space â„ E] [complete_space E]
          {a b : Î¹ â†’ â„} {f : â„ â†’ E}

lemma interval_integral_tendsto_integral
  (hfi : integrable f Î¼) (ha : tendsto a l at_bot) (hb : tendsto b l at_top) :
  tendsto (Î» i, âˆ« x in a i .. b i, f x âˆ‚Î¼) l (ğ“ $ âˆ« x, f x âˆ‚Î¼) :=
begin
  let Ï† := Î» i, Ioc (a i) (b i),
  have hÏ† : ae_cover Î¼ l Ï† := ae_cover_Ioc ha hb,
  refine (hÏ†.integral_tendsto_of_countably_generated hfi).congr' _,
  filter_upwards [ha.eventually (eventually_le_at_bot 0), hb.eventually (eventually_ge_at_top 0)]
    with i hai hbi,
  exact (interval_integral.integral_of_le (hai.trans hbi)).symm
end

lemma interval_integral_tendsto_integral_Iic (b : â„)
  (hfi : integrable_on f (Iic b) Î¼) (ha : tendsto a l at_bot) :
  tendsto (Î» i, âˆ« x in a i .. b, f x âˆ‚Î¼) l (ğ“ $ âˆ« x in Iic b, f x âˆ‚Î¼) :=
begin
  let Ï† := Î» i, Ioi (a i),
  have hÏ† : ae_cover (Î¼.restrict $ Iic b) l Ï† := ae_cover_Ioi ha,
  refine (hÏ†.integral_tendsto_of_countably_generated hfi).congr' _,
  filter_upwards [ha.eventually (eventually_le_at_bot $ b)] with i hai,
  rw [interval_integral.integral_of_le hai, measure.restrict_restrict (hÏ†.measurable i)],
  refl,
end

lemma interval_integral_tendsto_integral_Ioi (a : â„)
  (hfi : integrable_on f (Ioi a) Î¼) (hb : tendsto b l at_top) :
  tendsto (Î» i, âˆ« x in a .. b i, f x âˆ‚Î¼) l (ğ“ $ âˆ« x in Ioi a, f x âˆ‚Î¼) :=
begin
  let Ï† := Î» i, Iic (b i),
  have hÏ† : ae_cover (Î¼.restrict $ Ioi a) l Ï† := ae_cover_Iic hb,
  refine (hÏ†.integral_tendsto_of_countably_generated hfi).congr' _,
  filter_upwards [hb.eventually (eventually_ge_at_top $ a)] with i hbi,
  rw [interval_integral.integral_of_le hbi, measure.restrict_restrict (hÏ†.measurable i),
      inter_comm],
  refl,
end

end integral_of_interval_integral

section Ioi_change_variables

open real
open_locale interval

variables {E : Type*} {Î¼ : measure â„} {f : â„ â†’ E}
  [normed_add_comm_group E] [normed_space â„ E] [complete_space E]

/-- Change-of-variables formula for `Ioi` integrals of vector-valued functions, proved by taking
limits from the result for finite intervals. -/
lemma integral_comp_smul_deriv_Ioi {f f' : â„ â†’ â„} {g : â„ â†’ E} {a : â„}
  (hf : continuous_on f $ Ici a)
  (hft : tendsto f at_top at_top)
  (hff' : âˆ€ x âˆˆ Ioi a, has_deriv_within_at f (f' x) (Ioi x) x)
  (hg_cont : continuous_on g $ f '' Ioi a)
  (hg1 : integrable_on g $ f '' Ici a)
  (hg2 : integrable_on (Î» x, f' x â€¢ (g âˆ˜ f) x) (Ici a)) :
  âˆ« x in Ioi a, f' x â€¢ (g âˆ˜ f) x = âˆ« u in Ioi (f a), g u :=
begin
  have eq : âˆ€ b : â„, a < b â†’ âˆ« x in a..b, f' x â€¢ (g âˆ˜ f) x = âˆ« u in f a .. f b, g u,
  { intros b hb,
    have i1 : Ioo (min a b) (max a b) âŠ† Ioi a,
    { rw min_eq_left hb.le, exact Ioo_subset_Ioi_self },
    have i2 : [a, b] âŠ† Ici a,
    { rw uIcc_of_le hb.le, exact Icc_subset_Ici_self },
    refine interval_integral.integral_comp_smul_deriv''' (hf.mono i2)
      (Î» x hx, hff' x $ mem_of_mem_of_subset hx i1) (hg_cont.mono $ image_subset _ _)
      (hg1.mono_set $ image_subset _ _) (hg2.mono_set i2),
    { rw min_eq_left hb.le, exact Ioo_subset_Ioi_self },
    { rw uIcc_of_le hb.le, exact Icc_subset_Ici_self } },
  rw integrable_on_Ici_iff_integrable_on_Ioi at hg2,
  have t2 := interval_integral_tendsto_integral_Ioi _ hg2 tendsto_id,
  have : Ioi (f a) âŠ† f '' Ici a := (Ioi_subset_Ici_self.trans $
    is_preconnected.intermediate_value_Ici is_preconnected_Ici left_mem_Ici
    (le_principal_iff.mpr $ Ici_mem_at_top _) hf hft),
  have t1 := (interval_integral_tendsto_integral_Ioi _ (hg1.mono_set this) tendsto_id).comp hft,
  exact tendsto_nhds_unique (tendsto.congr' (eventually_eq_of_mem (Ioi_mem_at_top a) eq) t2) t1,
end

/-- Change-of-variables formula for `Ioi` integrals of scalar-valued functions -/
lemma integral_comp_mul_deriv_Ioi {f f' : â„ â†’ â„} {g : â„ â†’ â„} {a : â„}
  (hf : continuous_on f $ Ici a)
  (hft : tendsto f at_top at_top)
  (hff' : âˆ€ x âˆˆ Ioi a, has_deriv_within_at f (f' x) (Ioi x) x)
  (hg_cont : continuous_on g $ f '' Ioi a)
  (hg1 : integrable_on g $ f '' Ici a)
  (hg2 : integrable_on (Î» x, (g âˆ˜ f) x * f' x) (Ici a)) :
  âˆ« x in Ioi a, (g âˆ˜ f) x * f' x = âˆ« u in Ioi (f a), g u :=
begin
  have hg2' : integrable_on (Î» x, f' x â€¢ (g âˆ˜ f) x) (Ici a) := by simpa [mul_comm] using hg2,
  simpa [mul_comm] using integral_comp_smul_deriv_Ioi hf hft hff' hg_cont hg1 hg2',
end

/-- Substitution `y = x ^ p` in integrals over `Ioi 0` -/
lemma integral_comp_rpow_Ioi (g : â„ â†’ E) {p : â„} (hp : p â‰  0) :
  âˆ« x in Ioi 0, (|p| * x ^ (p - 1)) â€¢ g (x ^ p) = âˆ« y in Ioi 0, g y :=
begin
  let S := Ioi (0 : â„),
  have a1 : âˆ€ x:â„, x âˆˆ S â†’ has_deriv_within_at (Î» (t:â„), t ^ p) (p * x ^ (p - 1)) S x :=
    Î» x hx, (has_deriv_at_rpow_const (or.inl (mem_Ioi.mp hx).ne')).has_deriv_within_at,
  have a2 : inj_on (Î» x:â„, x ^ p) S,
  { rcases lt_or_gt_of_ne hp,
    { apply strict_anti_on.inj_on,
      intros x hx y hy hxy,
      rw [â†inv_lt_inv (rpow_pos_of_pos hx p) (rpow_pos_of_pos hy p),
      â†rpow_neg (le_of_lt hx), â†rpow_neg (le_of_lt hy)],
      exact rpow_lt_rpow (le_of_lt hx) hxy (neg_pos.mpr h), },
    exact strict_mono_on.inj_on (Î» x hx y hy hxy, rpow_lt_rpow (mem_Ioi.mp hx).le hxy h),},
  have a3 : (Î» (t : â„), t ^ p) '' S = S,
  { ext1, rw mem_image, split,
    { rintro âŸ¨y, hy, rflâŸ©, exact rpow_pos_of_pos hy p },
    { intro hx, refine âŸ¨x ^ (1 / p), rpow_pos_of_pos hx _, _âŸ©,
      rw [â†rpow_mul (le_of_lt hx), one_div_mul_cancel hp, rpow_one], } },
  have := integral_image_eq_integral_abs_deriv_smul measurable_set_Ioi a1 a2 g,
  rw a3 at this, rw this,
  refine set_integral_congr measurable_set_Ioi _,
  intros x hx, dsimp only,
  rw [abs_mul, abs_of_nonneg (rpow_nonneg_of_nonneg (le_of_lt hx) _)],
end

lemma integral_comp_rpow_Ioi_of_pos {g : â„ â†’ E} {p : â„} (hp : 0 < p) :
  âˆ« x in Ioi 0, (p * x ^ (p - 1)) â€¢ g (x ^ p) = âˆ« y in Ioi 0, g y :=
begin
  convert integral_comp_rpow_Ioi g hp.ne',
  funext, congr, rw abs_of_nonneg hp.le,
end

end Ioi_change_variables

end measure_theory
