/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Patrick Massot, SÃ©bastien GouÃ«zel
-/

import data.set.intervals.disjoint
import measure_theory.integral.set_integral
import measure_theory.measure.haar_lebesgue

/-!
# Integral over an interval

In this file we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼` if `a â‰¤ b` and
`-âˆ« x in Ioc b a, f x âˆ‚Î¼` if `b â‰¤ a`.

## Implementation notes

### Avoiding `if`, `min`, and `max`

In order to avoid `if`s in the definition, we define `interval_integrable f Î¼ a b` as
`integrable_on f (Ioc a b) Î¼ âˆ§ integrable_on f (Ioc b a) Î¼`. For any `a`, `b` one of these
intervals is empty and the other coincides with `set.uIoc a b = set.Ioc (min a b) (max a b)`.

Similarly, we define `âˆ« x in a..b, f x âˆ‚Î¼` to be `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`.
Again, for any `a`, `b` one of these integrals is zero, and the other gives the expected result.

This way some properties can be translated from integrals over sets without dealing with
the cases `a â‰¤ b` and `b â‰¤ a` separately.

### Choice of the interval

We use integral over `set.uIoc a b = set.Ioc (min a b) (max a b)` instead of one of the other
three possible intervals with the same endpoints for two reasons:

* this way `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼` holds whenever
  `f` is integrable on each interval; in particular, it works even if the measure `Î¼` has an atom
  at `b`; this rules out `set.Ioo` and `set.Icc` intervals;
* with this definition for a probability measure `Î¼`, the integral `âˆ« x in a..b, 1 âˆ‚Î¼` equals
  the difference $F_Î¼(b)-F_Î¼(a)$, where $F_Î¼(a)=Î¼(-âˆ, a]$ is the
  [cumulative distribution function](https://en.wikipedia.org/wiki/Cumulative_distribution_function)
  of `Î¼`.

## Tags

integral
-/

noncomputable theory
open topological_space (second_countable_topology)
open measure_theory set classical filter function

open_locale classical topology filter ennreal big_operators interval nnreal

variables {Î¹ ğ•œ E F A : Type*} [normed_add_comm_group E]

/-!
### Integrability on an interval
-/

/-- A function `f` is called *interval integrable* with respect to a measure `Î¼` on an unordered
interval `a..b` if it is integrable on both intervals `(a, b]` and `(b, a]`. One of these
intervals is always empty, so this property is equivalent to `f` being integrable on
`(min a b, max a b]`. -/
def interval_integrable (f : â„ â†’ E) (Î¼ : measure â„) (a b : â„) : Prop :=
integrable_on f (Ioc a b) Î¼ âˆ§ integrable_on f (Ioc b a) Î¼

section

variables {f : â„ â†’ E} {a b : â„} {Î¼ : measure â„}

/-- A function is interval integrable with respect to a given measure `Î¼` on `a..b` if and
  only if it is integrable on `uIoc a b` with respect to `Î¼`. This is an equivalent
  definition of `interval_integrable`. -/
lemma interval_integrable_iff : interval_integrable f Î¼ a b â†” integrable_on f (Î™ a b) Î¼ :=
by rw [uIoc_eq_union, integrable_on_union, interval_integrable]

/-- If a function is interval integrable with respect to a given measure `Î¼` on `a..b` then
  it is integrable on `uIoc a b` with respect to `Î¼`. -/
lemma interval_integrable.def (h : interval_integrable f Î¼ a b) : integrable_on f (Î™ a b) Î¼ :=
interval_integrable_iff.mp h

lemma interval_integrable_iff_integrable_Ioc_of_le (hab : a â‰¤ b) :
  interval_integrable f Î¼ a b â†” integrable_on f (Ioc a b) Î¼ :=
by rw [interval_integrable_iff, uIoc_of_le hab]

lemma integrable_on_Icc_iff_integrable_on_Ioc' {f : â„ â†’ E} (ha : Î¼ {a} â‰  âˆ) :
  integrable_on f (Icc a b) Î¼ â†” integrable_on f (Ioc a b) Î¼ :=
begin
  cases le_or_lt a b with hab hab,
  { have : Icc a b = Icc a a âˆª Ioc a b := (Icc_union_Ioc_eq_Icc le_rfl hab).symm,
    rw [this, integrable_on_union],
    simp [ha.lt_top] },
  { simp [hab, hab.le] },
end

lemma integrable_on_Icc_iff_integrable_on_Ioc [has_no_atoms Î¼] {f : â„ â†’ E} {a b : â„} :
  integrable_on f (Icc a b) Î¼ â†” integrable_on f (Ioc a b) Î¼ :=
integrable_on_Icc_iff_integrable_on_Ioc' (by simp)

lemma integrable_on_Ioc_iff_integrable_on_Ioo'
  {f : â„ â†’ E} {a b : â„} (hb : Î¼ {b} â‰  âˆ) :
  integrable_on f (Ioc a b) Î¼ â†” integrable_on f (Ioo a b) Î¼ :=
begin
  cases lt_or_le a b with hab hab,
  { have : Ioc a b = Ioo a b âˆª Icc b b := (Ioo_union_Icc_eq_Ioc hab le_rfl).symm,
    rw [this, integrable_on_union],
    simp [hb.lt_top] },
  { simp [hab] },
end

lemma integrable_on_Ioc_iff_integrable_on_Ioo [has_no_atoms Î¼] {f : â„ â†’ E} {a b : â„} :
  integrable_on f (Ioc a b) Î¼ â†” integrable_on f (Ioo a b) Î¼ :=
integrable_on_Ioc_iff_integrable_on_Ioo' (by simp)

lemma integrable_on_Icc_iff_integrable_on_Ioo [has_no_atoms Î¼] {f : â„ â†’ E} {a b : â„} :
  integrable_on f (Icc a b) Î¼ â†” integrable_on f (Ioo a b) Î¼ :=
by rw [integrable_on_Icc_iff_integrable_on_Ioc, integrable_on_Ioc_iff_integrable_on_Ioo]

lemma interval_integrable_iff' [has_no_atoms Î¼] :
  interval_integrable f Î¼ a b â†” integrable_on f (uIcc a b) Î¼ :=
by rw [interval_integrable_iff, â†Icc_min_max, uIoc, integrable_on_Icc_iff_integrable_on_Ioc]

lemma interval_integrable_iff_integrable_Icc_of_le
  {f : â„ â†’ E} {a b : â„} (hab : a â‰¤ b) {Î¼ : measure â„} [has_no_atoms Î¼] :
  interval_integrable f Î¼ a b â†” integrable_on f (Icc a b) Î¼ :=
by rw [interval_integrable_iff_integrable_Ioc_of_le hab, integrable_on_Icc_iff_integrable_on_Ioc]

lemma integrable_on_Ici_iff_integrable_on_Ioi' {f : â„ â†’ E} (ha : Î¼ {a} â‰  âˆ) :
  integrable_on f (Ici a) Î¼ â†” integrable_on f (Ioi a) Î¼ :=
begin
  have : Ici a = Icc a a âˆª Ioi a := (Icc_union_Ioi_eq_Ici le_rfl).symm,
  rw [this, integrable_on_union],
  simp [ha.lt_top]
end

lemma integrable_on_Ici_iff_integrable_on_Ioi [has_no_atoms Î¼] {f : â„ â†’ E} :
  integrable_on f (Ici a) Î¼ â†” integrable_on f (Ioi a) Î¼ :=
integrable_on_Ici_iff_integrable_on_Ioi' (by simp)

/-- If a function is integrable with respect to a given measure `Î¼` then it is interval integrable
  with respect to `Î¼` on `uIcc a b`. -/
lemma measure_theory.integrable.interval_integrable (hf : integrable f Î¼) :
  interval_integrable f Î¼ a b :=
âŸ¨hf.integrable_on, hf.integrable_onâŸ©

lemma measure_theory.integrable_on.interval_integrable (hf : integrable_on f [a, b] Î¼) :
  interval_integrable f Î¼ a b :=
âŸ¨measure_theory.integrable_on.mono_set hf (Ioc_subset_Icc_self.trans Icc_subset_uIcc),
 measure_theory.integrable_on.mono_set hf (Ioc_subset_Icc_self.trans Icc_subset_uIcc')âŸ©

lemma interval_integrable_const_iff {c : E} :
  interval_integrable (Î» _, c) Î¼ a b â†” c = 0 âˆ¨ Î¼ (Î™ a b) < âˆ :=
by simp only [interval_integrable_iff, integrable_on_const]

@[simp] lemma interval_integrable_const [is_locally_finite_measure Î¼] {c : E} :
  interval_integrable (Î» _, c) Î¼ a b :=
interval_integrable_const_iff.2 $ or.inr measure_Ioc_lt_top

end

namespace interval_integrable

section

variables {f : â„ â†’ E} {a b c d : â„} {Î¼ Î½ : measure â„}

@[symm] lemma symm (h : interval_integrable f Î¼ a b) : interval_integrable f Î¼ b a :=
h.symm

@[refl] lemma refl : interval_integrable f Î¼ a a :=
by split; simp

@[trans] lemma trans {a b c : â„} (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) : interval_integrable f Î¼ a c :=
âŸ¨(hab.1.union hbc.1).mono_set Ioc_subset_Ioc_union_Ioc,
  (hbc.2.union hab.2).mono_set Ioc_subset_Ioc_union_IocâŸ©

lemma trans_iterate_Ico {a : â„• â†’ â„} {m n : â„•} (hmn : m â‰¤ n)
  (hint : âˆ€ k âˆˆ Ico m n, interval_integrable f Î¼ (a k) (a $ k+1)) :
  interval_integrable f Î¼ (a m) (a n) :=
begin
  revert hint,
  refine nat.le_induction _ _ n hmn,
  { simp },
  { assume p hp IH h,
    exact (IH (Î» k hk, h k (Ico_subset_Ico_right p.le_succ hk))).trans (h p (by simp [hp])) }
end

lemma trans_iterate {a : â„• â†’ â„} {n : â„•} (hint : âˆ€ k < n, interval_integrable f Î¼ (a k) (a $ k+1)) :
  interval_integrable f Î¼ (a 0) (a n) :=
trans_iterate_Ico bot_le (Î» k hk, hint k hk.2)

lemma neg (h : interval_integrable f Î¼ a b) : interval_integrable (-f) Î¼ a b :=
âŸ¨h.1.neg, h.2.negâŸ©

lemma norm (h : interval_integrable f Î¼ a b) :
  interval_integrable (Î» x, â€–f xâ€–) Î¼ a b  :=
âŸ¨h.1.norm, h.2.normâŸ©

lemma interval_integrable_norm_iff {f : â„ â†’ E} {Î¼ : measure â„} {a b : â„}
  (hf : ae_strongly_measurable f (Î¼.restrict (Î™ a b))) :
  interval_integrable (Î» t, â€–f tâ€–) Î¼ a b â†” interval_integrable f Î¼ a b :=
by { simp_rw [interval_integrable_iff, integrable_on], exact integrable_norm_iff hf }

lemma abs {f : â„ â†’ â„} (h : interval_integrable f Î¼ a b) :
  interval_integrable (Î» x, |f x|) Î¼ a b  :=
h.norm

lemma mono (hf : interval_integrable f Î½ a b) (h1 : [c, d] âŠ† [a, b]) (h2 : Î¼ â‰¤ Î½) :
  interval_integrable f Î¼ c d :=
interval_integrable_iff.mpr $ hf.def.mono
  (uIoc_subset_uIoc_of_uIcc_subset_uIcc h1) h2

lemma mono_measure (hf : interval_integrable f Î½ a b) (h : Î¼ â‰¤ Î½) :
  interval_integrable f Î¼ a b :=
hf.mono rfl.subset h

lemma mono_set (hf : interval_integrable f Î¼ a b) (h : [c, d] âŠ† [a, b]) :
  interval_integrable f Î¼ c d :=
hf.mono h rfl.le

lemma mono_set_ae (hf : interval_integrable f Î¼ a b) (h : Î™ c d â‰¤áµ[Î¼] Î™ a b) :
  interval_integrable f Î¼ c d :=
interval_integrable_iff.mpr $ hf.def.mono_set_ae h

lemma mono_set' (hf : interval_integrable f Î¼ a b) (hsub : Î™ c d âŠ† Î™ a b) :
  interval_integrable f Î¼ c d :=
hf.mono_set_ae $ eventually_of_forall hsub

lemma mono_fun [normed_add_comm_group F] {g : â„ â†’ F}
  (hf : interval_integrable f Î¼ a b) (hgm : ae_strongly_measurable g (Î¼.restrict (Î™ a b)))
  (hle : (Î» x, â€–g xâ€–) â‰¤áµ[Î¼.restrict (Î™ a b)] (Î» x, â€–f xâ€–)) : interval_integrable g Î¼ a b :=
interval_integrable_iff.2 $ hf.def.integrable.mono hgm hle

lemma mono_fun' {g : â„ â†’ â„} (hg : interval_integrable g Î¼ a b)
  (hfm : ae_strongly_measurable f (Î¼.restrict (Î™ a b)))
  (hle : (Î» x, â€–f xâ€–) â‰¤áµ[Î¼.restrict (Î™ a b)] g) : interval_integrable f Î¼ a b :=
interval_integrable_iff.2 $ hg.def.integrable.mono' hfm hle

protected lemma ae_strongly_measurable (h : interval_integrable f Î¼ a b) :
  ae_strongly_measurable f (Î¼.restrict (Ioc a b)):=
h.1.ae_strongly_measurable

protected lemma ae_strongly_measurable' (h : interval_integrable f Î¼ a b) :
  ae_strongly_measurable f (Î¼.restrict (Ioc b a)):=
h.2.ae_strongly_measurable

end

variables [normed_ring A] {f g : â„ â†’ E} {a b : â„} {Î¼ : measure â„}

lemma smul [normed_field ğ•œ] [normed_space ğ•œ E]
  {f : â„ â†’ E} {a b : â„} {Î¼ : measure â„} (h : interval_integrable f Î¼ a b) (r : ğ•œ) :
  interval_integrable (r â€¢ f) Î¼ a b :=
âŸ¨h.1.smul r, h.2.smul râŸ©

@[simp] lemma add (hf : interval_integrable f Î¼ a b) (hg : interval_integrable g Î¼ a b) :
  interval_integrable (Î» x, f x + g x) Î¼ a b :=
âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©

@[simp] lemma sub (hf : interval_integrable f Î¼ a b) (hg : interval_integrable g Î¼ a b) :
  interval_integrable (Î» x, f x - g x) Î¼ a b :=
âŸ¨hf.1.sub hg.1, hf.2.sub hg.2âŸ©

lemma sum (s : finset Î¹) {f : Î¹ â†’ â„ â†’ E} (h : âˆ€ i âˆˆ s, interval_integrable (f i) Î¼ a b) :
  interval_integrable (âˆ‘ i in s, f i) Î¼ a b :=
âŸ¨integrable_finset_sum' s (Î» i hi, (h i hi).1), integrable_finset_sum' s (Î» i hi, (h i hi).2)âŸ©

lemma mul_continuous_on {f g : â„ â†’ A}
  (hf : interval_integrable f Î¼ a b) (hg : continuous_on g [a, b]) :
  interval_integrable (Î» x, f x * g x) Î¼ a b :=
begin
  rw interval_integrable_iff at hf âŠ¢,
  exact hf.mul_continuous_on_of_subset hg measurable_set_Ioc is_compact_uIcc Ioc_subset_Icc_self
end

lemma continuous_on_mul {f g : â„ â†’ A}
  (hf : interval_integrable f Î¼ a b) (hg : continuous_on g [a, b]) :
  interval_integrable (Î» x, g x * f x) Î¼ a b :=
begin
  rw interval_integrable_iff at hf âŠ¢,
  exact hf.continuous_on_mul_of_subset hg is_compact_uIcc measurable_set_Ioc Ioc_subset_Icc_self
end

@[simp]
lemma const_mul {f : â„ â†’ A}
  (hf : interval_integrable f Î¼ a b) (c : A) : interval_integrable (Î» x, c * f x) Î¼ a b :=
hf.continuous_on_mul continuous_on_const

@[simp]
lemma mul_const {f : â„ â†’ A}
  (hf : interval_integrable f Î¼ a b) (c : A) : interval_integrable (Î» x, f x * c) Î¼ a b :=
hf.mul_continuous_on continuous_on_const

@[simp]
lemma div_const {ğ•œ : Type*} {f : â„ â†’ ğ•œ} [normed_field ğ•œ]
  (h : interval_integrable f Î¼ a b) (c : ğ•œ) :
  interval_integrable (Î» x, f x / c) Î¼ a b :=
by simpa only [div_eq_mul_inv] using mul_const h câ»Â¹

lemma comp_mul_left (hf : interval_integrable f volume a b) (c : â„) :
  interval_integrable (Î» x, f (c * x)) volume (a / c) (b / c) :=
begin
  rcases eq_or_ne c 0 with hc|hc, { rw hc, simp },
  rw interval_integrable_iff' at hf âŠ¢,
  have A : measurable_embedding (Î» x, x * câ»Â¹) :=
    (homeomorph.mul_rightâ‚€ _ (inv_ne_zero hc)).closed_embedding.measurable_embedding,
  rw [â†real.smul_map_volume_mul_right (inv_ne_zero hc), integrable_on, measure.restrict_smul,
    integrable_smul_measure (by simpa : ennreal.of_real (|câ»Â¹|) â‰  0) ennreal.of_real_ne_top,
    â†integrable_on, measurable_embedding.integrable_on_map_iff A],
  convert hf using 1,
  { ext, simp only [comp_app], congr' 1, field_simp, ring },
  { rw preimage_mul_const_uIcc (inv_ne_zero hc), field_simp [hc] },
end

lemma comp_mul_right (hf : interval_integrable f volume a b) (c : â„) :
  interval_integrable (Î» x, f (x * c)) volume (a / c) (b / c) :=
by simpa only [mul_comm] using comp_mul_left hf c

lemma comp_add_right (hf : interval_integrable f volume a b) (c : â„) :
  interval_integrable (Î» x, f (x + c)) volume (a - c) (b - c) :=
begin
  wlog h : a â‰¤ b,
  { exact interval_integrable.symm (this hf.symm _ (le_of_not_le h)) },
  rw interval_integrable_iff' at hf âŠ¢,
  have A : measurable_embedding (Î» x, x + c) :=
    (homeomorph.add_right c).closed_embedding.measurable_embedding,
  have Am : measure.map (Î» x, x + c) volume = volume,
  { exact is_add_left_invariant.is_add_right_invariant.map_add_right_eq_self _ },
  rw â†Am at hf,
  convert (measurable_embedding.integrable_on_map_iff A).mp hf,
  rw preimage_add_const_uIcc,
end

lemma comp_add_left (hf : interval_integrable f volume a b) (c : â„) :
  interval_integrable (Î» x, f (c + x)) volume (a - c) (b - c) :=
by simpa only [add_comm] using interval_integrable.comp_add_right hf c

lemma comp_sub_right (hf : interval_integrable f volume a b) (c : â„) :
  interval_integrable (Î» x, f (x - c)) volume (a + c) (b + c) :=
by simpa only [sub_neg_eq_add] using interval_integrable.comp_add_right hf (-c)

lemma iff_comp_neg  :
  interval_integrable f volume a b â†” interval_integrable (Î» x, f (-x)) volume (-a) (-b) :=
begin
  split, all_goals { intro hf, convert comp_mul_left hf (-1), simp, field_simp, field_simp },
end

lemma comp_sub_left (hf : interval_integrable f volume a b) (c : â„) :
  interval_integrable (Î» x, f (c - x)) volume (c - a) (c - b) :=
by simpa only [neg_sub, â†sub_eq_add_neg] using iff_comp_neg.mp (hf.comp_add_left c)

end interval_integrable

section

variables {Î¼ : measure â„} [is_locally_finite_measure Î¼]

lemma continuous_on.interval_integrable {u : â„ â†’ E} {a b : â„}
  (hu : continuous_on u (uIcc a b)) : interval_integrable u Î¼ a b :=
(continuous_on.integrable_on_Icc hu).interval_integrable

lemma continuous_on.interval_integrable_of_Icc {u : â„ â†’ E} {a b : â„} (h : a â‰¤ b)
  (hu : continuous_on u (Icc a b)) : interval_integrable u Î¼ a b :=
continuous_on.interval_integrable ((uIcc_of_le h).symm â–¸ hu)

/-- A continuous function on `â„` is `interval_integrable` with respect to any locally finite measure
`Î½` on â„. -/
lemma continuous.interval_integrable {u : â„ â†’ E} (hu : continuous u) (a b : â„) :
  interval_integrable u Î¼ a b :=
hu.continuous_on.interval_integrable

end

section

variables {Î¼ : measure â„} [is_locally_finite_measure Î¼] [conditionally_complete_linear_order E]
  [order_topology E] [second_countable_topology E]

lemma monotone_on.interval_integrable {u : â„ â†’ E} {a b : â„} (hu : monotone_on u (uIcc a b)) :
  interval_integrable u Î¼ a b :=
begin
  rw interval_integrable_iff,
  exact (hu.integrable_on_is_compact is_compact_uIcc).mono_set Ioc_subset_Icc_self,
end

lemma antitone_on.interval_integrable {u : â„ â†’ E} {a b : â„} (hu : antitone_on u (uIcc a b)) :
  interval_integrable u Î¼ a b :=
hu.dual_right.interval_integrable

lemma monotone.interval_integrable {u : â„ â†’ E} {a b : â„} (hu : monotone u) :
  interval_integrable u Î¼ a b :=
(hu.monotone_on _).interval_integrable

lemma antitone.interval_integrable {u : â„ â†’ E} {a b : â„} (hu : antitone u) :
  interval_integrable u Î¼ a b :=
(hu.antitone_on _).interval_integrable

end

/-- Let `l'` be a measurably generated filter; let `l` be a of filter such that each `s âˆˆ l'`
eventually includes `Ioc u v` as both `u` and `v` tend to `l`. Let `Î¼` be a measure finite at `l'`.

Suppose that `f : â„ â†’ E` has a finite limit at `l' âŠ“ Î¼.ae`. Then `f` is interval integrable on
`u..v` provided that both `u` and `v` tend to `l`.

Typeclass instances allow Lean to find `l'` based on `l` but not vice versa, so
`apply tendsto.eventually_interval_integrable_ae` will generate goals `filter â„` and
`tendsto_Ixx_class Ioc ?m_1 l'`. -/
lemma filter.tendsto.eventually_interval_integrable_ae {f : â„ â†’ E} {Î¼ : measure â„}
  {l l' : filter â„}  (hfm : strongly_measurable_at_filter f l' Î¼)
  [tendsto_Ixx_class Ioc l l'] [is_measurably_generated l']
  (hÎ¼ : Î¼.finite_at_filter l') {c : E} (hf : tendsto f (l' âŠ“ Î¼.ae) (ğ“ c))
  {u v : Î¹ â†’ â„} {lt : filter Î¹} (hu : tendsto u lt l) (hv : tendsto v lt l) :
  âˆ€á¶  t in lt, interval_integrable f Î¼ (u t) (v t) :=
have _ := (hf.integrable_at_filter_ae hfm hÎ¼).eventually,
((hu.Ioc hv).eventually this).and $ (hv.Ioc hu).eventually this

/-- Let `l'` be a measurably generated filter; let `l` be a of filter such that each `s âˆˆ l'`
eventually includes `Ioc u v` as both `u` and `v` tend to `l`. Let `Î¼` be a measure finite at `l'`.

Suppose that `f : â„ â†’ E` has a finite limit at `l`. Then `f` is interval integrable on `u..v`
provided that both `u` and `v` tend to `l`.

Typeclass instances allow Lean to find `l'` based on `l` but not vice versa, so
`apply tendsto.eventually_interval_integrable_ae` will generate goals `filter â„` and
`tendsto_Ixx_class Ioc ?m_1 l'`. -/
lemma filter.tendsto.eventually_interval_integrable {f : â„ â†’ E} {Î¼ : measure â„}
  {l l' : filter â„} (hfm : strongly_measurable_at_filter f l' Î¼)
  [tendsto_Ixx_class Ioc l l'] [is_measurably_generated l']
  (hÎ¼ : Î¼.finite_at_filter l') {c : E} (hf : tendsto f l' (ğ“ c))
  {u v : Î¹ â†’ â„} {lt : filter Î¹} (hu : tendsto u lt l) (hv : tendsto v lt l) :
  âˆ€á¶  t in lt, interval_integrable f Î¼ (u t) (v t) :=
(hf.mono_left inf_le_left).eventually_interval_integrable_ae hfm hÎ¼ hu hv

/-!
### Interval integral: definition and basic properties

In this section we define `âˆ« x in a..b, f x âˆ‚Î¼` as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`
and prove some basic properties.
-/

variables [complete_space E] [normed_space â„ E]

/-- The interval integral `âˆ« x in a..b, f x âˆ‚Î¼` is defined
as `âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼`. If `a â‰¤ b`, then it equals
`âˆ« x in Ioc a b, f x âˆ‚Î¼`, otherwise it equals `-âˆ« x in Ioc b a, f x âˆ‚Î¼`. -/
def interval_integral (f : â„ â†’ E) (a b : â„) (Î¼ : measure â„) : E :=
âˆ« x in Ioc a b, f x âˆ‚Î¼ - âˆ« x in Ioc b a, f x âˆ‚Î¼

notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := interval_integral r a b Î¼
notation `âˆ«` binders ` in ` a `..` b `, ` r:(scoped:60 f, interval_integral f a b volume) := r

namespace interval_integral

section basic

variables {a b : â„} {f g : â„ â†’ E} {Î¼ : measure â„}

@[simp] lemma integral_zero : âˆ« x in a..b, (0 : E) âˆ‚Î¼ = 0 :=
by simp [interval_integral]

lemma integral_of_le (h : a â‰¤ b) : âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in Ioc a b, f x âˆ‚Î¼ :=
by simp [interval_integral, h]

@[simp] lemma integral_same : âˆ« x in a..a, f x âˆ‚Î¼ = 0 :=
sub_self _

lemma integral_symm (a b) : âˆ« x in b..a, f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, neg_sub]

lemma integral_of_ge (h : b â‰¤ a) : âˆ« x in a..b, f x âˆ‚Î¼ = -âˆ« x in Ioc b a, f x âˆ‚Î¼ :=
by simp only [integral_symm b, integral_of_le h]

lemma interval_integral_eq_integral_uIoc (f : â„ â†’ E) (a b : â„) (Î¼ : measure â„) :
  âˆ« x in a..b, f x âˆ‚Î¼ = (if a â‰¤ b then 1 else -1 : â„) â€¢ âˆ« x in Î™ a b, f x âˆ‚Î¼ :=
begin
  split_ifs with h,
  { simp only [integral_of_le h, uIoc_of_le h, one_smul] },
  { simp only [integral_of_ge (not_le.1 h).le, uIoc_of_lt (not_le.1 h), neg_one_smul] }
end

lemma norm_interval_integral_eq (f : â„ â†’ E) (a b : â„) (Î¼ : measure â„) :
  â€–âˆ« x in a..b, f x âˆ‚Î¼â€– = â€–âˆ« x in Î™ a b, f x âˆ‚Î¼â€– :=
begin
  simp_rw [interval_integral_eq_integral_uIoc, norm_smul],
  split_ifs; simp only [norm_neg, norm_one, one_mul],
end

lemma abs_interval_integral_eq (f : â„ â†’ â„) (a b : â„) (Î¼ : measure â„) :
  |âˆ« x in a..b, f x âˆ‚Î¼| = |âˆ« x in Î™ a b, f x âˆ‚Î¼| :=
norm_interval_integral_eq f a b Î¼

lemma integral_cases (f : â„ â†’ E) (a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ âˆˆ ({âˆ« x in Î™ a b, f x âˆ‚Î¼, -âˆ« x in Î™ a b, f x âˆ‚Î¼} : set E) :=
by { rw interval_integral_eq_integral_uIoc, split_ifs; simp }

lemma integral_undef (h : Â¬ interval_integrable f Î¼ a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ = 0 :=
by cases le_total a b with hab hab;
  simp only [integral_of_le, integral_of_ge, hab, neg_eq_zero];
    refine integral_undef (not_imp_not.mpr _ h);
      simpa only [hab, Ioc_eq_empty_of_le, integrable_on_empty, not_true, false_or, or_false]
        using not_and_distrib.mp h

lemma interval_integrable_of_integral_ne_zero {a b : â„}
  {f : â„ â†’ E} {Î¼ : measure â„} (h : âˆ« x in a..b, f x âˆ‚Î¼ â‰  0) :
  interval_integrable f Î¼ a b :=
by { contrapose! h, exact interval_integral.integral_undef h }

lemma integral_non_ae_strongly_measurable
  (hf : Â¬ ae_strongly_measurable f (Î¼.restrict (Î™ a b))) :
  âˆ« x in a..b, f x âˆ‚Î¼ = 0 :=
by rw [interval_integral_eq_integral_uIoc, integral_non_ae_strongly_measurable hf, smul_zero]

lemma integral_non_ae_strongly_measurable_of_le (h : a â‰¤ b)
  (hf : Â¬ ae_strongly_measurable f (Î¼.restrict (Ioc a b))) :
  âˆ« x in a..b, f x âˆ‚Î¼ = 0 :=
integral_non_ae_strongly_measurable $ by rwa [uIoc_of_le h]

lemma norm_integral_min_max (f : â„ â†’ E) :
  â€–âˆ« x in min a b..max a b, f x âˆ‚Î¼â€– = â€–âˆ« x in a..b, f x âˆ‚Î¼â€– :=
by cases le_total a b; simp [*, integral_symm a b]

lemma norm_integral_eq_norm_integral_Ioc (f : â„ â†’ E) :
  â€–âˆ« x in a..b, f x âˆ‚Î¼â€– = â€–âˆ« x in Î™ a b, f x âˆ‚Î¼â€– :=
by rw [â† norm_integral_min_max, integral_of_le min_le_max, uIoc]

lemma abs_integral_eq_abs_integral_uIoc (f : â„ â†’ â„) :
  |âˆ« x in a..b, f x âˆ‚Î¼| = |âˆ« x in Î™ a b, f x âˆ‚Î¼| :=
norm_integral_eq_norm_integral_Ioc f

lemma norm_integral_le_integral_norm_Ioc :
  â€–âˆ« x in a..b, f x âˆ‚Î¼â€– â‰¤ âˆ« x in Î™ a b, â€–f xâ€– âˆ‚Î¼ :=
calc â€–âˆ« x in a..b, f x âˆ‚Î¼â€– = â€–âˆ« x in Î™ a b, f x âˆ‚Î¼â€– :
  norm_integral_eq_norm_integral_Ioc f
... â‰¤ âˆ« x in Î™ a b, â€–f xâ€– âˆ‚Î¼ :
  norm_integral_le_integral_norm f

lemma norm_integral_le_abs_integral_norm : â€–âˆ« x in a..b, f x âˆ‚Î¼â€– â‰¤ |âˆ« x in a..b, â€–f xâ€– âˆ‚Î¼| :=
begin
  simp only [â† real.norm_eq_abs, norm_integral_eq_norm_integral_Ioc],
  exact le_trans (norm_integral_le_integral_norm _) (le_abs_self _)
end

lemma norm_integral_le_integral_norm (h : a â‰¤ b) :
  â€–âˆ« x in a..b, f x âˆ‚Î¼â€– â‰¤ âˆ« x in a..b, â€–f xâ€– âˆ‚Î¼ :=
norm_integral_le_integral_norm_Ioc.trans_eq $ by rw [uIoc_of_le h, integral_of_le h]

lemma norm_integral_le_of_norm_le {g : â„ â†’ â„}
  (h : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), â€–f tâ€– â‰¤ g t)
  (hbound : interval_integrable g Î¼ a b) :
  â€–âˆ« t in a..b, f t âˆ‚Î¼â€– â‰¤ |âˆ« t in a..b, g t âˆ‚Î¼| :=
by simp_rw [norm_interval_integral_eq, abs_interval_integral_eq,
  abs_eq_self.mpr (integral_nonneg_of_ae $ h.mono $ Î» t ht, (norm_nonneg _).trans ht),
  norm_integral_le_of_norm_le hbound.def h]

lemma norm_integral_le_of_norm_le_const_ae {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€áµ x, x âˆˆ Î™ a b â†’ â€–f xâ€– â‰¤ C) :
  â€–âˆ« x in a..b, f xâ€– â‰¤ C * |b - a| :=
begin
  rw [norm_integral_eq_norm_integral_Ioc],
  convert norm_set_integral_le_of_norm_le_const_ae'' _ measurable_set_Ioc h,
  { rw [real.volume_Ioc, max_sub_min_eq_abs, ennreal.to_real_of_real (abs_nonneg _)] },
  { simp only [real.volume_Ioc, ennreal.of_real_lt_top] },
end

lemma norm_integral_le_of_norm_le_const {a b C : â„} {f : â„ â†’ E}
  (h : âˆ€ x âˆˆ Î™ a b, â€–f xâ€– â‰¤ C) :
  â€–âˆ« x in a..b, f xâ€– â‰¤ C * |b - a| :=
norm_integral_le_of_norm_le_const_ae $ eventually_of_forall h

@[simp] lemma integral_add (hf : interval_integrable f Î¼ a b) (hg : interval_integrable g Î¼ a b) :
  âˆ« x in a..b, f x + g x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in a..b, g x âˆ‚Î¼ :=
by simp only [interval_integral_eq_integral_uIoc, integral_add hf.def hg.def, smul_add]

lemma integral_finset_sum {Î¹} {s : finset Î¹} {f : Î¹ â†’ â„ â†’ E}
  (h : âˆ€ i âˆˆ s, interval_integrable (f i) Î¼ a b) :
  âˆ« x in a..b, âˆ‘ i in s, f i x âˆ‚Î¼ = âˆ‘ i in s, âˆ« x in a..b, f i x âˆ‚Î¼ :=
by simp only [interval_integral_eq_integral_uIoc,
  integral_finset_sum s (Î» i hi, (h i hi).def), finset.smul_sum]

@[simp] lemma integral_neg : âˆ« x in a..b, -f x âˆ‚Î¼ = -âˆ« x in a..b, f x âˆ‚Î¼ :=
by { simp only [interval_integral, integral_neg], abel }

@[simp] lemma integral_sub (hf : interval_integrable f Î¼ a b) (hg : interval_integrable g Î¼ a b) :
  âˆ« x in a..b, f x - g x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in a..b, g x âˆ‚Î¼ :=
by simpa only [sub_eq_add_neg] using (integral_add hf hg.neg).trans (congr_arg _ integral_neg)

@[simp] lemma integral_smul {ğ•œ : Type*} [nontrivially_normed_field ğ•œ] [normed_space ğ•œ E]
  [smul_comm_class â„ ğ•œ E]
  (r : ğ•œ) (f : â„ â†’ E) : âˆ« x in a..b, r â€¢ f x âˆ‚Î¼ = r â€¢ âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, integral_smul, smul_sub]

@[simp] lemma integral_smul_const {ğ•œ : Type*} [is_R_or_C ğ•œ] [normed_space ğ•œ E] (f : â„ â†’ ğ•œ) (c : E) :
  âˆ« x in a..b, f x â€¢ c âˆ‚Î¼ = (âˆ« x in a..b, f x âˆ‚Î¼) â€¢ c :=
by simp only [interval_integral_eq_integral_uIoc, integral_smul_const, smul_assoc]

@[simp] lemma integral_const_mul {ğ•œ : Type*} [is_R_or_C ğ•œ] (r : ğ•œ) (f : â„ â†’ ğ•œ) :
  âˆ« x in a..b, r * f x âˆ‚Î¼ = r * âˆ« x in a..b, f x âˆ‚Î¼ :=
integral_smul r f

@[simp] lemma integral_mul_const {ğ•œ : Type*} [is_R_or_C ğ•œ] (r : ğ•œ) (f : â„ â†’ ğ•œ) :
  âˆ« x in a..b, f x * r âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ * r :=
by simpa only [mul_comm r] using integral_const_mul r f

@[simp] lemma integral_div {ğ•œ : Type*} [is_R_or_C ğ•œ] (r : ğ•œ) (f : â„ â†’ ğ•œ) :
  âˆ« x in a..b, f x / r âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ / r :=
by simpa only [div_eq_mul_inv] using integral_mul_const râ»Â¹ f

lemma integral_const' (c : E) :
  âˆ« x in a..b, c âˆ‚Î¼ = ((Î¼ $ Ioc a b).to_real - (Î¼ $ Ioc b a).to_real) â€¢ c :=
by simp only [interval_integral, set_integral_const, sub_smul]

@[simp] lemma integral_const (c : E) : âˆ« x in a..b, c = (b - a) â€¢ c :=
by simp only [integral_const', real.volume_Ioc, ennreal.to_real_of_real', â† neg_sub b,
  max_zero_sub_eq_self]

lemma integral_smul_measure (c : â„â‰¥0âˆ) :
  âˆ« x in a..b, f x âˆ‚(c â€¢ Î¼) = c.to_real â€¢ âˆ« x in a..b, f x âˆ‚Î¼ :=
by simp only [interval_integral, measure.restrict_smul, integral_smul_measure, smul_sub]

end basic

lemma integral_of_real {a b : â„} {Î¼ : measure â„} {f : â„ â†’ â„} :
  âˆ« x in a..b, (f x : â„‚) âˆ‚Î¼ = â†‘(âˆ« x in a..b, f x âˆ‚Î¼) :=
by simp only [interval_integral, integral_of_real, complex.of_real_sub]

section continuous_linear_map

variables {a b : â„} {Î¼ : measure â„} {f : â„ â†’ E}
variables [is_R_or_C ğ•œ] [normed_space ğ•œ E] [normed_add_comm_group F] [normed_space ğ•œ F]

open continuous_linear_map

lemma _root_.continuous_linear_map.interval_integral_apply {a b : â„} {Ï† : â„ â†’ F â†’L[ğ•œ] E}
  (hÏ† : interval_integrable Ï† Î¼ a b) (v : F) :
  (âˆ« x in a..b, Ï† x âˆ‚Î¼) v = âˆ« x in a..b, Ï† x v âˆ‚Î¼ :=
by simp_rw [interval_integral_eq_integral_uIoc, â† integral_apply hÏ†.def v, coe_smul',
  pi.smul_apply]

variables [normed_space â„ F] [complete_space F]

lemma _root_.continuous_linear_map.interval_integral_comp_comm
  (L : E â†’L[ğ•œ] F) (hf : interval_integrable f Î¼ a b) :
  âˆ« x in a..b, L (f x) âˆ‚Î¼ = L (âˆ« x in a..b, f x âˆ‚Î¼) :=
by simp_rw [interval_integral, L.integral_comp_comm hf.1, L.integral_comp_comm hf.2, L.map_sub]

end continuous_linear_map
section comp

variables {a b c d : â„} (f : â„ â†’ E)

@[simp] lemma integral_comp_mul_right (hc : c â‰  0) :
  âˆ« x in a..b, f (x * c) = câ»Â¹ â€¢ âˆ« x in a*c..b*c, f x :=
begin
  have A : measurable_embedding (Î» x, x * c) :=
    (homeomorph.mul_rightâ‚€ c hc).closed_embedding.measurable_embedding,
  conv_rhs { rw [â† real.smul_map_volume_mul_right hc] },
  simp_rw [integral_smul_measure, interval_integral, A.set_integral_map,
          ennreal.to_real_of_real (abs_nonneg c)],
  cases hc.lt_or_lt,
  { simp [h, mul_div_cancel, hc, abs_of_neg, measure.restrict_congr_set Ico_ae_eq_Ioc] },
  { simp [h, mul_div_cancel, hc, abs_of_pos] }
end

@[simp] lemma smul_integral_comp_mul_right (c) :
  c â€¢ âˆ« x in a..b, f (x * c) = âˆ« x in a*c..b*c, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_mul_left (hc : c â‰  0) :
  âˆ« x in a..b, f (c * x) = câ»Â¹ â€¢ âˆ« x in c*a..c*b, f x :=
by simpa only [mul_comm c] using integral_comp_mul_right f hc

@[simp] lemma smul_integral_comp_mul_left (c) :
  c â€¢ âˆ« x in a..b, f (c * x) = âˆ« x in c*a..c*b, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_div (hc : c â‰  0) :
  âˆ« x in a..b, f (x / c) = c â€¢ âˆ« x in a/c..b/c, f x :=
by simpa only [inv_inv] using integral_comp_mul_right f (inv_ne_zero hc)

@[simp] lemma inv_smul_integral_comp_div (c) :
  câ»Â¹ â€¢ âˆ« x in a..b, f (x / c) = âˆ« x in a/c..b/c, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_add_right (d) :
  âˆ« x in a..b, f (x + d) = âˆ« x in a+d..b+d, f x :=
have A : measurable_embedding (Î» x, x + d) :=
  (homeomorph.add_right d).closed_embedding.measurable_embedding,
calc  âˆ« x in a..b, f (x + d)
    = âˆ« x in a+d..b+d, f x âˆ‚(measure.map (Î» x, x + d) volume)
                           : by simp [interval_integral, A.set_integral_map]
... = âˆ« x in a+d..b+d, f x : by rw [map_add_right_eq_self]

@[simp] lemma integral_comp_add_left (d) :
  âˆ« x in a..b, f (d + x) = âˆ« x in d+a..d+b, f x :=
by simpa only [add_comm] using integral_comp_add_right f d

@[simp] lemma integral_comp_mul_add (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (c * x + d) = câ»Â¹ â€¢ âˆ« x in c*a+d..c*b+d, f x :=
by rw [â† integral_comp_add_right, â† integral_comp_mul_left _ hc]

@[simp] lemma smul_integral_comp_mul_add (c d) :
  c â€¢ âˆ« x in a..b, f (c * x + d) = âˆ« x in c*a+d..c*b+d, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_add_mul (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (d + c * x) = câ»Â¹ â€¢ âˆ« x in d+c*a..d+c*b, f x :=
by rw [â† integral_comp_add_left, â† integral_comp_mul_left _ hc]

@[simp] lemma smul_integral_comp_add_mul (c d) :
  c â€¢ âˆ« x in a..b, f (d + c * x) = âˆ« x in d+c*a..d+c*b, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_div_add (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (x / c + d) = c â€¢ âˆ« x in a/c+d..b/c+d, f x :=
by simpa only [div_eq_inv_mul, inv_inv] using integral_comp_mul_add f (inv_ne_zero hc) d

@[simp] lemma inv_smul_integral_comp_div_add (c d) :
  câ»Â¹ â€¢ âˆ« x in a..b, f (x / c + d) = âˆ« x in a/c+d..b/c+d, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_add_div (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (d + x / c) = c â€¢ âˆ« x in d+a/c..d+b/c, f x :=
by simpa only [div_eq_inv_mul, inv_inv] using integral_comp_add_mul f (inv_ne_zero hc) d

@[simp] lemma inv_smul_integral_comp_add_div (c d) :
  câ»Â¹ â€¢ âˆ« x in a..b, f (d + x / c) = âˆ« x in d+a/c..d+b/c, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_mul_sub (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (c * x - d) = câ»Â¹ â€¢ âˆ« x in c*a-d..c*b-d, f x :=
by simpa only [sub_eq_add_neg] using integral_comp_mul_add f hc (-d)

@[simp] lemma smul_integral_comp_mul_sub (c d) :
  c â€¢ âˆ« x in a..b, f (c * x - d) = âˆ« x in c*a-d..c*b-d, f x  :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_sub_mul (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (d - c * x) = câ»Â¹ â€¢ âˆ« x in d-c*b..d-c*a, f x :=
begin
  simp only [sub_eq_add_neg, neg_mul_eq_neg_mul],
  rw [integral_comp_add_mul f (neg_ne_zero.mpr hc) d, integral_symm],
  simp only [inv_neg, smul_neg, neg_neg, neg_smul],
end

@[simp] lemma smul_integral_comp_sub_mul (c d) :
  c â€¢ âˆ« x in a..b, f (d - c * x) = âˆ« x in d-c*b..d-c*a, f x  :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_div_sub (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (x / c - d) = c â€¢ âˆ« x in a/c-d..b/c-d, f x :=
by simpa only [div_eq_inv_mul, inv_inv] using integral_comp_mul_sub f (inv_ne_zero hc) d

@[simp] lemma inv_smul_integral_comp_div_sub (c d) :
  câ»Â¹ â€¢ âˆ« x in a..b, f (x / c - d) = âˆ« x in a/c-d..b/c-d, f x  :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_sub_div (hc : c â‰  0) (d) :
  âˆ« x in a..b, f (d - x / c) = c â€¢ âˆ« x in d-b/c..d-a/c, f x :=
by simpa only [div_eq_inv_mul, inv_inv] using integral_comp_sub_mul f (inv_ne_zero hc) d

@[simp] lemma inv_smul_integral_comp_sub_div (c d) :
  câ»Â¹ â€¢ âˆ« x in a..b, f (d - x / c) = âˆ« x in d-b/c..d-a/c, f x :=
by by_cases hc : c = 0; simp [hc]

@[simp] lemma integral_comp_sub_right (d) :
  âˆ« x in a..b, f (x - d) = âˆ« x in a-d..b-d, f x :=
by simpa only [sub_eq_add_neg] using integral_comp_add_right f (-d)

@[simp] lemma integral_comp_sub_left (d) :
  âˆ« x in a..b, f (d - x) = âˆ« x in d-b..d-a, f x :=
by simpa only [one_mul, one_smul, inv_one] using integral_comp_sub_mul f one_ne_zero d

@[simp] lemma integral_comp_neg : âˆ« x in a..b, f (-x) = âˆ« x in -b..-a, f x :=
by simpa only [zero_sub] using integral_comp_sub_left f 0

end comp

/-!
### Integral is an additive function of the interval

In this section we prove that `âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼`
as well as a few other identities trivially equivalent to this one. We also prove that
`âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼` provided that `support f âŠ† Ioc a b`.
-/

section order_closed_topology

variables {a b c d : â„} {f g : â„ â†’ E} {Î¼ : measure â„}

/-- If two functions are equal in the relevant interval, their interval integrals are also equal. -/
lemma integral_congr {a b : â„} (h : eq_on f g [a, b]) :
  âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, g x âˆ‚Î¼ :=
by cases le_total a b with hab hab; simpa [hab, integral_of_le, integral_of_ge]
  using set_integral_congr measurable_set_Ioc (h.mono Ioc_subset_Icc_self)

lemma integral_add_adjacent_intervals_cancel (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ + âˆ« x in c..a, f x âˆ‚Î¼ = 0 :=
begin
  have hac := hab.trans hbc,
  simp only [interval_integral, sub_add_sub_comm, sub_eq_zero],
  iterate 4 { rw â† integral_union },
  { suffices : Ioc a b âˆª Ioc b c âˆª Ioc c a = Ioc b a âˆª Ioc c b âˆª Ioc a c, by rw this,
    rw [Ioc_union_Ioc_union_Ioc_cycle, union_right_comm, Ioc_union_Ioc_union_Ioc_cycle,
      min_left_comm, max_left_comm] },
  all_goals { simp [*, measurable_set.union, measurable_set_Ioc, Ioc_disjoint_Ioc_same,
    Ioc_disjoint_Ioc_same.symm, hab.1, hab.2, hbc.1, hbc.2, hac.1, hac.2] }
end

lemma integral_add_adjacent_intervals (hab : interval_integrable f Î¼ a b)
  (hbc : interval_integrable f Î¼ b c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in b..c, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼ :=
by rw [â† add_neg_eq_zero, â† integral_symm, integral_add_adjacent_intervals_cancel hab hbc]

lemma sum_integral_adjacent_intervals_Ico {a : â„• â†’ â„} {m n : â„•} (hmn : m â‰¤ n)
  (hint : âˆ€ k âˆˆ Ico m n, interval_integrable f Î¼ (a k) (a $ k+1)) :
  âˆ‘ (k : â„•) in finset.Ico m n, âˆ« x in (a k)..(a $ k+1), f x âˆ‚Î¼ = âˆ« x in (a m)..(a n), f x âˆ‚Î¼ :=
begin
  revert hint,
  refine nat.le_induction _ _ n hmn,
  { simp },
  { assume p hmp IH h,
    rw [finset.sum_Ico_succ_top hmp, IH, integral_add_adjacent_intervals],
    { apply interval_integrable.trans_iterate_Ico hmp (Î» k hk, h k _),
      exact (Ico_subset_Ico le_rfl (nat.le_succ _)) hk },
    { apply h,
      simp [hmp] },
    { assume k hk,
      exact h _ (Ico_subset_Ico_right p.le_succ hk) } }
end

lemma sum_integral_adjacent_intervals {a : â„• â†’ â„} {n : â„•}
  (hint : âˆ€ k < n, interval_integrable f Î¼ (a k) (a $ k+1)) :
  âˆ‘ (k : â„•) in finset.range n, âˆ« x in (a k)..(a $ k+1), f x âˆ‚Î¼ = âˆ« x in (a 0)..(a n), f x âˆ‚Î¼ :=
begin
  rw â† nat.Ico_zero_eq_range,
  exact sum_integral_adjacent_intervals_Ico (zero_le n) (Î» k hk, hint k hk.2),
end

lemma integral_interval_sub_left (hab : interval_integrable f Î¼ a b)
  (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in a..c, f x âˆ‚Î¼ = âˆ« x in c..b, f x âˆ‚Î¼ :=
sub_eq_of_eq_add' $ eq.symm $ integral_add_adjacent_intervals hac (hac.symm.trans hab)

lemma integral_interval_add_interval_comm (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ + âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in a..d, f x âˆ‚Î¼ + âˆ« x in c..b, f x âˆ‚Î¼ :=
by rw [â† integral_add_adjacent_intervals hac hcd, add_assoc, add_left_comm,
  integral_add_adjacent_intervals hac (hac.symm.trans hab), add_comm]

lemma integral_interval_sub_interval_comm (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in a..c, f x âˆ‚Î¼ - âˆ« x in b..d, f x âˆ‚Î¼ :=
by simp only [sub_eq_add_neg, â† integral_symm,
  integral_interval_add_interval_comm hab hcd.symm (hac.trans hcd)]

lemma integral_interval_sub_interval_comm' (hab : interval_integrable f Î¼ a b)
  (hcd : interval_integrable f Î¼ c d) (hac : interval_integrable f Î¼ a c) :
  âˆ« x in a..b, f x âˆ‚Î¼ - âˆ« x in c..d, f x âˆ‚Î¼ = âˆ« x in d..b, f x âˆ‚Î¼ - âˆ« x in c..a, f x âˆ‚Î¼ :=
by { rw [integral_interval_sub_interval_comm hab hcd hac, integral_symm b d, integral_symm a c,
  sub_neg_eq_add, sub_eq_neg_add], }

lemma integral_Iic_sub_Iic (ha : integrable_on f (Iic a) Î¼) (hb : integrable_on f (Iic b) Î¼) :
  âˆ« x in Iic b, f x âˆ‚Î¼ - âˆ« x in Iic a, f x âˆ‚Î¼ = âˆ« x in a..b, f x âˆ‚Î¼ :=
begin
  wlog hab : a â‰¤ b generalizing a b,
  { rw [integral_symm, â† this hb ha (le_of_not_le hab), neg_sub] },
  rw [sub_eq_iff_eq_add', integral_of_le hab, â† integral_union (Iic_disjoint_Ioc le_rfl),
    Iic_union_Ioc_eq_Iic hab],
  exacts [measurable_set_Ioc, ha, hb.mono_set (Î» _, and.right)]
end

/-- If `Î¼` is a finite measure then `âˆ« x in a..b, c âˆ‚Î¼ = (Î¼ (Iic b) - Î¼ (Iic a)) â€¢ c`. -/
lemma integral_const_of_cdf [is_finite_measure Î¼] (c : E) :
  âˆ« x in a..b, c âˆ‚Î¼ = ((Î¼ (Iic b)).to_real - (Î¼ (Iic a)).to_real) â€¢ c :=
begin
  simp only [sub_smul, â† set_integral_const],
  refine (integral_Iic_sub_Iic _ _).symm;
    simp only [integrable_on_const, measure_lt_top, or_true]
end

lemma integral_eq_integral_of_support_subset {a b} (h : support f âŠ† Ioc a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
begin
  cases le_total a b with hab hab,
  { rw [integral_of_le hab, â† integral_indicator measurable_set_Ioc, indicator_eq_self.2 h];
    apply_instance },
  { rw [Ioc_eq_empty hab.not_lt, subset_empty_iff, support_eq_empty_iff] at h,
    simp [h] }
end

lemma integral_congr_ae' (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ Ioc a b â†’ f x = g x)
  (h' : âˆ€áµ x âˆ‚Î¼, x âˆˆ Ioc b a â†’ f x = g x) :
  âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, g x âˆ‚Î¼ :=
by simp only [interval_integral, set_integral_congr_ae (measurable_set_Ioc) h,
              set_integral_congr_ae (measurable_set_Ioc) h']

lemma integral_congr_ae (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ f x = g x) :
  âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, g x âˆ‚Î¼ :=
integral_congr_ae' (ae_uIoc_iff.mp h).1 (ae_uIoc_iff.mp h).2

lemma integral_zero_ae (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ f x = 0) :
  âˆ« x in a..b, f x âˆ‚Î¼ = 0 :=
calc âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..b, 0 âˆ‚Î¼ : integral_congr_ae h
                     ... = 0                 : integral_zero

lemma integral_indicator {aâ‚ aâ‚‚ aâ‚ƒ : â„} (h : aâ‚‚ âˆˆ Icc aâ‚ aâ‚ƒ) :
  âˆ« x in aâ‚..aâ‚ƒ, indicator {x | x â‰¤ aâ‚‚} f x âˆ‚ Î¼ = âˆ« x in aâ‚..aâ‚‚, f x âˆ‚ Î¼ :=
begin
  have : {x | x â‰¤ aâ‚‚} âˆ© Ioc aâ‚ aâ‚ƒ = Ioc aâ‚ aâ‚‚, from Iic_inter_Ioc_of_le h.2,
  rw [integral_of_le h.1, integral_of_le (h.1.trans h.2),
      integral_indicator, measure.restrict_restrict, this],
  exact measurable_set_Iic,
  all_goals { apply measurable_set_Iic },
end

/-- Lebesgue dominated convergence theorem for filters with a countable basis -/
lemma tendsto_integral_filter_of_dominated_convergence {Î¹} {l : filter Î¹}
  [l.is_countably_generated] {F : Î¹ â†’ â„ â†’ E} (bound : â„ â†’ â„)
  (hF_meas : âˆ€á¶  n in l, ae_strongly_measurable (F n) (Î¼.restrict (Î™ a b)))
  (h_bound : âˆ€á¶  n in l, âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ â€–F n xâ€– â‰¤ bound x)
  (bound_integrable : interval_integrable bound Î¼ a b)
  (h_lim : âˆ€áµ x âˆ‚Î¼, x âˆˆ Î™ a b â†’ tendsto (Î» n, F n x) l (ğ“ (f x))) :
  tendsto (Î»n, âˆ« x in a..b, F n x âˆ‚Î¼) l (ğ“ $ âˆ« x in a..b, f x âˆ‚Î¼) :=
begin
  simp only [interval_integrable_iff, interval_integral_eq_integral_uIoc,
    â† ae_restrict_iff' measurable_set_uIoc] at *,
  exact tendsto_const_nhds.smul
    (tendsto_integral_filter_of_dominated_convergence bound hF_meas h_bound bound_integrable h_lim)
end

/-- Lebesgue dominated convergence theorem for series. -/
lemma has_sum_integral_of_dominated_convergence {Î¹} [countable Î¹]
  {F : Î¹ â†’ â„ â†’ E} (bound : Î¹ â†’ â„ â†’ â„)
  (hF_meas : âˆ€ n, ae_strongly_measurable (F n) (Î¼.restrict (Î™ a b)))
  (h_bound : âˆ€ n, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F n tâ€– â‰¤ bound n t)
  (bound_summable : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ summable (Î» n, bound n t))
  (bound_integrable : interval_integrable (Î» t, âˆ‘' n, bound n t) Î¼ a b)
  (h_lim : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ has_sum (Î» n, F n t) (f t)) :
  has_sum (Î»n, âˆ« t in a..b, F n t âˆ‚Î¼) (âˆ« t in a..b, f t âˆ‚Î¼) :=
begin
  simp only [interval_integrable_iff, interval_integral_eq_integral_uIoc,
    â† ae_restrict_iff' measurable_set_uIoc] at *,
  exact (has_sum_integral_of_dominated_convergence bound hF_meas h_bound bound_summable
    bound_integrable h_lim).const_smul _,
end

open topological_space

/-- Interval integrals commute with countable sums, when the supremum norms are summable (a
special case of the dominated convergence theorem). -/
lemma has_sum_interval_integral_of_summable_norm [countable Î¹] {f : Î¹ â†’ C(â„, E)}
  (hf_sum : summable (Î» i : Î¹, â€–(f i).restrict (âŸ¨uIcc a b, is_compact_uIccâŸ© : compacts â„)â€–)) :
  has_sum (Î» i : Î¹, âˆ« x in a..b, f i x) (âˆ« x in a..b, (âˆ‘' i : Î¹, f i x)) :=
begin
  refine has_sum_integral_of_dominated_convergence
    (Î» i (x : â„), â€–(f i).restrict â†‘(âŸ¨uIcc a b, is_compact_uIccâŸ© : compacts â„)â€–)
    (Î» i, (map_continuous $ f i).ae_strongly_measurable)
    (Î» i, ae_of_all _ (Î» x hx, ((f i).restrict â†‘(âŸ¨uIcc a b, is_compact_uIccâŸ© :
      compacts â„)).norm_coe_le_norm âŸ¨x, âŸ¨hx.1.le, hx.2âŸ©âŸ©))
    (ae_of_all _ (Î» x hx, hf_sum))
    interval_integrable_const
    (ae_of_all _ (Î» x hx, summable.has_sum _)),
  -- next line is slow, & doesn't work with "exact" in place of "apply" -- ?
  apply continuous_map.summable_apply (summable_of_summable_norm hf_sum) âŸ¨x, âŸ¨hx.1.le, hx.2âŸ©âŸ©,
end

lemma tsum_interval_integral_eq_of_summable_norm [countable Î¹] {f : Î¹ â†’ C(â„, E)}
  (hf_sum : summable (Î» i : Î¹, â€–(f i).restrict (âŸ¨uIcc a b, is_compact_uIccâŸ© : compacts â„)â€–)) :
  âˆ‘' (i : Î¹), âˆ« x in a..b, f i x = âˆ« x in a..b, (âˆ‘' i : Î¹, f i x) :=
(has_sum_interval_integral_of_summable_norm hf_sum).tsum_eq

variables {X : Type*} [topological_space X] [first_countable_topology X]

/-- Continuity of interval integral with respect to a parameter, at a point within a set.
  Given `F : X â†’ â„ â†’ E`, assume `F x` is ae-measurable on `[a, b]` for `x` in a
  neighborhood of `xâ‚€`Â within `s` and at `xâ‚€`, and assume it is bounded by a function integrable
  on `[a, b]` independent of `x` in a neighborhood of `xâ‚€`Â within `s`. If `(Î» x, F x t)`
  is continuous at `xâ‚€` within `s` for almost every `t` in `[a, b]`
  then the same holds for `(Î» x, âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€`. -/
lemma continuous_within_at_of_dominated_interval
  {F : X â†’ â„ â†’ E} {xâ‚€ : X} {bound : â„ â†’ â„} {a b : â„} {s : set X}
  (hF_meas : âˆ€á¶  x in ğ“[s] xâ‚€, ae_strongly_measurable (F x) (Î¼.restrict $ Î™ a b))
  (h_bound : âˆ€á¶  x in ğ“[s] xâ‚€, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F x tâ€– â‰¤ bound t)
  (bound_integrable : interval_integrable bound Î¼ a b)
  (h_cont : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ continuous_within_at (Î» x, F x t) s xâ‚€) :
  continuous_within_at (Î» x, âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€ :=
tendsto_integral_filter_of_dominated_convergence bound hF_meas h_bound bound_integrable h_cont

/-- Continuity of interval integral with respect to a parameter at a point.
  Given `F : X â†’ â„ â†’ E`, assume `F x` is ae-measurable on `[a, b]` for `x` in a
  neighborhood of `xâ‚€`, and assume it is bounded by a function integrable on
  `[a, b]` independent of `x` in a neighborhood of `xâ‚€`. If `(Î» x, F x t)`
  is continuous at `xâ‚€` for almost every `t` in `[a, b]`
  then the same holds for `(Î» x, âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€`. -/
lemma continuous_at_of_dominated_interval
  {F : X â†’ â„ â†’ E} {xâ‚€ : X} {bound : â„ â†’ â„} {a b : â„}
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_strongly_measurable (F x) (Î¼.restrict $ Î™ a b))
  (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F x tâ€– â‰¤ bound t)
  (bound_integrable : interval_integrable bound Î¼ a b)
  (h_cont : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ continuous_at (Î» x, F x t) xâ‚€) :
  continuous_at (Î» x, âˆ« t in a..b, F x t âˆ‚Î¼) xâ‚€ :=
tendsto_integral_filter_of_dominated_convergence bound hF_meas h_bound bound_integrable h_cont

/-- Continuity of interval integral with respect to a parameter.
  Given `F : X â†’ â„ â†’ E`, assume each `F x` is ae-measurable on `[a, b]`,
  and assume it is bounded by a function integrable on `[a, b]` independent of `x`.
  If `(Î» x, F x t)` is continuous for almost every `t` in `[a, b]`
  then the same holds for `(Î» x, âˆ« t in a..b, F x t âˆ‚Î¼) s xâ‚€`. -/
lemma continuous_of_dominated_interval {F : X â†’ â„ â†’ E} {bound : â„ â†’ â„} {a b : â„}
  (hF_meas : âˆ€ x, ae_strongly_measurable (F x) $ Î¼.restrict $ Î™ a b)
  (h_bound : âˆ€ x, âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ â€–F x tâ€– â‰¤ bound t)
  (bound_integrable : interval_integrable bound Î¼ a b)
  (h_cont : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ continuous (Î» x, F x t)) :
  continuous (Î» x, âˆ« t in a..b, F x t âˆ‚Î¼) :=
continuous_iff_continuous_at.mpr (Î» xâ‚€, continuous_at_of_dominated_interval
  (eventually_of_forall hF_meas) (eventually_of_forall h_bound) bound_integrable $ h_cont.mono $
  Î» x himp hx, (himp hx).continuous_at)

end order_closed_topology

section continuous_primitive
open topological_space

variables {a b bâ‚€ bâ‚ bâ‚‚ : â„} {Î¼ : measure â„} {f g : â„ â†’ E}

lemma continuous_within_at_primitive (hbâ‚€ : Î¼ {bâ‚€} = 0)
  (h_int : interval_integrable f Î¼ (min a bâ‚) (max a bâ‚‚)) :
  continuous_within_at (Î» b, âˆ« x in a .. b, f x âˆ‚ Î¼) (Icc bâ‚ bâ‚‚) bâ‚€ :=
begin
  by_cases hâ‚€ : bâ‚€ âˆˆ Icc bâ‚ bâ‚‚,
  { have hâ‚â‚‚ : bâ‚ â‰¤ bâ‚‚ := hâ‚€.1.trans hâ‚€.2,
    have minâ‚â‚‚ : min bâ‚ bâ‚‚ = bâ‚ := min_eq_left hâ‚â‚‚,
    have h_int' : âˆ€ {x}, x âˆˆ Icc bâ‚ bâ‚‚ â†’ interval_integrable f Î¼ bâ‚ x,
    { rintros x âŸ¨hâ‚, hâ‚‚âŸ©,
      apply h_int.mono_set,
      apply uIcc_subset_uIcc,
      { exact âŸ¨min_le_of_left_le (min_le_right a bâ‚),
                hâ‚.trans (hâ‚‚.trans $ le_max_of_le_right $ le_max_right _ _)âŸ© },
      { exact âŸ¨min_le_of_left_le $ (min_le_right _ _).trans hâ‚,
                le_max_of_le_right $ hâ‚‚.trans $ le_max_right _ _âŸ© } },
    have : âˆ€ b âˆˆ Icc bâ‚ bâ‚‚, âˆ« x in a..b, f x âˆ‚Î¼ = âˆ« x in a..bâ‚, f x âˆ‚Î¼ + âˆ« x in bâ‚..b, f x âˆ‚Î¼,
    { rintros b âŸ¨hâ‚, hâ‚‚âŸ©,
      rw â† integral_add_adjacent_intervals _ (h_int' âŸ¨hâ‚, hâ‚‚âŸ©),
      apply h_int.mono_set,
      apply uIcc_subset_uIcc,
      { exact âŸ¨min_le_of_left_le (min_le_left a bâ‚), le_max_of_le_right (le_max_left _ _)âŸ© },
      { exact âŸ¨min_le_of_left_le (min_le_right _ _),
                le_max_of_le_right (hâ‚.trans $ hâ‚‚.trans (le_max_right a bâ‚‚))âŸ© } },
    apply continuous_within_at.congr _ this (this _ hâ‚€), clear this,
    refine continuous_within_at_const.add _,
    have : (Î» b, âˆ« x in bâ‚..b, f x âˆ‚Î¼) =á¶ [ğ“[Icc bâ‚ bâ‚‚] bâ‚€]
            Î» b, âˆ« x in bâ‚..bâ‚‚, indicator {x | x â‰¤ b} f x âˆ‚ Î¼,
    { apply eventually_eq_of_mem self_mem_nhds_within,
      exact Î» b b_in, (integral_indicator b_in).symm },

    apply continuous_within_at.congr_of_eventually_eq _ this (integral_indicator hâ‚€).symm,
    have : interval_integrable (Î» x, â€–f xâ€–) Î¼ bâ‚ bâ‚‚,
      from interval_integrable.norm (h_int' $ right_mem_Icc.mpr hâ‚â‚‚),
    refine continuous_within_at_of_dominated_interval _ _ this _ ; clear this,
    { apply eventually.mono (self_mem_nhds_within),
      intros x hx,
      erw [ae_strongly_measurable_indicator_iff, measure.restrict_restrict, Iic_inter_Ioc_of_le],
      { rw minâ‚â‚‚,
        exact (h_int' hx).1.ae_strongly_measurable },
      { exact le_max_of_le_right hx.2 },
      exacts [measurable_set_Iic, measurable_set_Iic] },
    { refine eventually_of_forall (Î» x, eventually_of_forall (Î» t, _)),
      dsimp [indicator],
      split_ifs ; simp },
    { have : âˆ€áµ t âˆ‚Î¼, t < bâ‚€ âˆ¨ bâ‚€ < t,
      { apply eventually.mono (compl_mem_ae_iff.mpr hbâ‚€),
        intros x hx,
        exact ne.lt_or_lt hx },
      apply this.mono,
      rintros xâ‚€ (hxâ‚€ | hxâ‚€) -,
      { have : âˆ€á¶  x in ğ“[Icc bâ‚ bâ‚‚] bâ‚€, {t : â„ | t â‰¤ x}.indicator f xâ‚€ = f xâ‚€,
        { apply mem_nhds_within_of_mem_nhds,
          apply eventually.mono (Ioi_mem_nhds hxâ‚€),
          intros x hx,
          simp [hx.le] },
        apply continuous_within_at_const.congr_of_eventually_eq  this,
        simp [hxâ‚€.le] },
      { have : âˆ€á¶  x in ğ“[Icc bâ‚ bâ‚‚] bâ‚€, {t : â„ | t â‰¤ x}.indicator f xâ‚€ = 0,
        { apply mem_nhds_within_of_mem_nhds,
          apply eventually.mono (Iio_mem_nhds hxâ‚€),
          intros x hx,
          simp [hx] },
        apply continuous_within_at_const.congr_of_eventually_eq this,
        simp [hxâ‚€] } } },
  { apply continuous_within_at_of_not_mem_closure,
    rwa [closure_Icc] }
end

lemma continuous_on_primitive [has_no_atoms Î¼] (h_int : integrable_on f (Icc a b) Î¼) :
  continuous_on (Î» x, âˆ« t in Ioc a x, f t âˆ‚ Î¼) (Icc a b) :=
begin
  by_cases h : a â‰¤ b,
  { have : âˆ€ x âˆˆ Icc a b, âˆ« t in Ioc a x, f t âˆ‚Î¼ = âˆ« t in a..x, f t âˆ‚Î¼,
    { intros x x_in,
      simp_rw [â† uIoc_of_le h, integral_of_le x_in.1] },
    rw continuous_on_congr this,
    intros xâ‚€ hxâ‚€,
    refine continuous_within_at_primitive (measure_singleton xâ‚€) _,
    simp only [interval_integrable_iff_integrable_Ioc_of_le, min_eq_left, max_eq_right, h],
    exact h_int.mono Ioc_subset_Icc_self le_rfl },
  { rw Icc_eq_empty h,
    exact continuous_on_empty _ },
end

lemma continuous_on_primitive_Icc [has_no_atoms Î¼] (h_int : integrable_on f (Icc a b) Î¼) :
  continuous_on (Î» x, âˆ« t in Icc a x, f t âˆ‚ Î¼) (Icc a b) :=
begin
  rw show (Î» x, âˆ« t in Icc a x, f t âˆ‚Î¼) = Î» x, âˆ« t in Ioc a x, f t âˆ‚Î¼,
    by { ext x, exact integral_Icc_eq_integral_Ioc },
  exact continuous_on_primitive h_int
end

/-- Note: this assumes that `f` is `interval_integrable`, in contrast to some other lemmas here. -/
lemma continuous_on_primitive_interval' [has_no_atoms Î¼]
  (h_int : interval_integrable f Î¼ bâ‚ bâ‚‚) (ha : a âˆˆ [bâ‚, bâ‚‚]) :
  continuous_on (Î» b, âˆ« x in a..b, f x âˆ‚ Î¼) [bâ‚, bâ‚‚] :=
begin
  intros bâ‚€ hbâ‚€,
  refine continuous_within_at_primitive (measure_singleton _) _,
  rw [min_eq_right ha.1, max_eq_right ha.2],
  simpa [interval_integrable_iff, uIoc] using h_int,
end

lemma continuous_on_primitive_interval [has_no_atoms Î¼]
  (h_int : integrable_on f (uIcc a b) Î¼) :
  continuous_on (Î» x, âˆ« t in a..x, f t âˆ‚ Î¼) (uIcc a b) :=
continuous_on_primitive_interval' h_int.interval_integrable left_mem_uIcc

lemma continuous_on_primitive_interval_left [has_no_atoms Î¼]
  (h_int : integrable_on f (uIcc a b) Î¼) :
  continuous_on (Î» x, âˆ« t in x..b, f t âˆ‚ Î¼) (uIcc a b) :=
begin
  rw uIcc_comm a b at h_int âŠ¢,
  simp only [integral_symm b],
  exact (continuous_on_primitive_interval h_int).neg,
end

variables [has_no_atoms Î¼]

lemma continuous_primitive (h_int : âˆ€ a b, interval_integrable f Î¼ a b) (a : â„) :
  continuous (Î» b, âˆ« x in a..b, f x âˆ‚ Î¼) :=
begin
  rw continuous_iff_continuous_at,
  intro bâ‚€,
  cases exists_lt bâ‚€ with bâ‚ hbâ‚,
  cases exists_gt bâ‚€ with bâ‚‚ hbâ‚‚,
  apply continuous_within_at.continuous_at _ (Icc_mem_nhds hbâ‚ hbâ‚‚),
  exact continuous_within_at_primitive (measure_singleton bâ‚€) (h_int _ _)
end

lemma _root_.measure_theory.integrable.continuous_primitive (h_int : integrable f Î¼) (a : â„) :
  continuous (Î» b, âˆ« x in a..b, f x âˆ‚ Î¼) :=
continuous_primitive (Î» _ _, h_int.interval_integrable) a

end continuous_primitive

section

variables {f g : â„ â†’ â„} {a b : â„} {Î¼ : measure â„}

lemma integral_eq_zero_iff_of_le_of_nonneg_ae (hab : a â‰¤ b)
  (hf : 0 â‰¤áµ[Î¼.restrict (Ioc a b)] f) (hfi : interval_integrable f Î¼ a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼.restrict (Ioc a b)] 0 :=
by rw [integral_of_le hab, integral_eq_zero_iff_of_nonneg_ae hf hfi.1]

lemma integral_eq_zero_iff_of_nonneg_ae
  (hf : 0 â‰¤áµ[Î¼.restrict (Ioc a b âˆª Ioc b a)] f) (hfi : interval_integrable f Î¼ a b) :
  âˆ« x in a..b, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼.restrict (Ioc a b âˆª Ioc b a)] 0 :=
begin
  cases le_total a b with hab hab;
    simp only [Ioc_eq_empty hab.not_lt, empty_union, union_empty] at hf âŠ¢,
  { exact integral_eq_zero_iff_of_le_of_nonneg_ae hab hf hfi },
  { rw [integral_symm, neg_eq_zero, integral_eq_zero_iff_of_le_of_nonneg_ae hab hf hfi.symm] }
end

/-- If `f` is nonnegative and integrable on the unordered interval `set.uIoc a b`, then its
integral over `a..b` is positive if and only if `a < b` and the measure of
`function.support f âˆ© set.Ioc a b` is positive. -/
lemma integral_pos_iff_support_of_nonneg_ae'
  (hf : 0 â‰¤áµ[Î¼.restrict (Î™ a b)] f) (hfi : interval_integrable f Î¼ a b) :
  0 < âˆ« x in a..b, f x âˆ‚Î¼ â†” a < b âˆ§ 0 < Î¼ (support f âˆ© Ioc a b) :=
begin
  cases lt_or_le a b with hab hba,
  { rw uIoc_of_le hab.le at hf,
    simp only [hab, true_and, integral_of_le hab.le,
      set_integral_pos_iff_support_of_nonneg_ae hf hfi.1] },
  { suffices : âˆ« x in a..b, f x âˆ‚Î¼ â‰¤ 0, by simp only [this.not_lt, hba.not_lt, false_and],
    rw [integral_of_ge hba, neg_nonpos],
    rw [uIoc_swap, uIoc_of_le hba] at hf,
    exact integral_nonneg_of_ae hf }
end

/-- If `f` is nonnegative a.e.-everywhere and it is integrable on the unordered interval
`set.uIoc a b`, then its integral over `a..b` is positive if and only if `a < b` and the
measure of `function.support f âˆ© set.Ioc a b` is positive. -/
lemma integral_pos_iff_support_of_nonneg_ae (hf : 0 â‰¤áµ[Î¼] f) (hfi : interval_integrable f Î¼ a b) :
  0 < âˆ« x in a..b, f x âˆ‚Î¼ â†” a < b âˆ§ 0 < Î¼ (support f âˆ© Ioc a b) :=
integral_pos_iff_support_of_nonneg_ae' (ae_mono measure.restrict_le_self hf) hfi

/-- If `f : â„ â†’ â„` is integrable on `(a, b]` for real numbers `a < b`, and positive on the interior
of the interval, then its integral over `a..b` is strictly positive. -/
lemma interval_integral_pos_of_pos_on {f : â„ â†’ â„} {a b : â„}
  (hfi : interval_integrable f volume a b) (hpos : âˆ€ (x : â„), x âˆˆ Ioo a b â†’ 0 < f x) (hab : a < b) :
  0 < âˆ« (x : â„) in a..b, f x :=
begin
  have hsupp : Ioo a b âŠ† support f âˆ© Ioc a b :=
    Î» x hx, âŸ¨mem_support.mpr (hpos x hx).ne', Ioo_subset_Ioc_self hxâŸ©,
  have hâ‚€ : 0 â‰¤áµ[volume.restrict (uIoc a b)] f,
  { rw [eventually_le, uIoc_of_le hab.le],
    refine ae_restrict_of_ae_eq_of_ae_restrict Ioo_ae_eq_Ioc _,
    exact (ae_restrict_iff' measurable_set_Ioo).mpr (ae_of_all _ (Î» x hx, (hpos x hx).le)) },
  rw integral_pos_iff_support_of_nonneg_ae' hâ‚€ hfi,
  exact âŸ¨hab, ((measure.measure_Ioo_pos _).mpr hab).trans_le (measure_mono hsupp)âŸ©,
end

/-- If `f : â„ â†’ â„` is strictly positive everywhere, and integrable on `(a, b]` for real numbers
`a < b`, then its integral over `a..b` is strictly positive. (See `interval_integral_pos_of_pos_on`
for a version only assuming positivity of `f` on `(a, b)` rather than everywhere.) -/
lemma interval_integral_pos_of_pos {f : â„ â†’ â„} {a b : â„}
  (hfi : interval_integrable f measure_space.volume a b) (hpos : âˆ€ x, 0 < f x) (hab : a < b) :
  0 < âˆ« x in a..b, f x :=
interval_integral_pos_of_pos_on hfi (Î» x hx, hpos x) hab

/-- If `f` and `g` are two functions that are interval integrable on `a..b`, `a â‰¤ b`,
`f x â‰¤ g x` for a.e. `x âˆˆ set.Ioc a b`, and `f x < g x` on a subset of `set.Ioc a b`
of nonzero measure, then `âˆ« x in a..b, f x âˆ‚Î¼ < âˆ« x in a..b, g x âˆ‚Î¼`. -/
lemma integral_lt_integral_of_ae_le_of_measure_set_of_lt_ne_zero (hab : a â‰¤ b)
  (hfi : interval_integrable f Î¼ a b) (hgi : interval_integrable g Î¼ a b)
  (hle : f â‰¤áµ[Î¼.restrict (Ioc a b)] g) (hlt : Î¼.restrict (Ioc a b) {x | f x < g x} â‰  0) :
  âˆ« x in a..b, f x âˆ‚Î¼ < âˆ« x in a..b, g x âˆ‚Î¼ :=
begin
  rw [â† sub_pos, â† integral_sub hgi hfi, integral_of_le hab,
    measure_theory.integral_pos_iff_support_of_nonneg_ae],
  { refine pos_iff_ne_zero.2 (mt (measure_mono_null _) hlt),
    exact Î» x hx, (sub_pos.2 hx).ne' },
  exacts [hle.mono (Î» x, sub_nonneg.2), hgi.1.sub hfi.1]
end

/-- If `f` and `g` are continuous on `[a, b]`, `a < b`, `f x â‰¤ g x` on this interval, and
`f c < g c` at some point `c âˆˆ [a, b]`, then `âˆ« x in a..b, f x < âˆ« x in a..b, g x`. -/
lemma integral_lt_integral_of_continuous_on_of_le_of_exists_lt {f g : â„ â†’ â„} {a b : â„}
  (hab : a < b) (hfc : continuous_on f (Icc a b)) (hgc : continuous_on g (Icc a b))
  (hle : âˆ€ x âˆˆ Ioc a b, f x â‰¤ g x) (hlt : âˆƒ c âˆˆ Icc a b, f c < g c) :
  âˆ« x in a..b, f x < âˆ« x in a..b, g x :=
begin
  refine integral_lt_integral_of_ae_le_of_measure_set_of_lt_ne_zero hab.le
    (hfc.interval_integrable_of_Icc hab.le) (hgc.interval_integrable_of_Icc hab.le)
    ((ae_restrict_mem measurable_set_Ioc).mono hle) _,
  contrapose! hlt,
  have h_eq : f =áµ[volume.restrict (Ioc a b)] g,
  { simp only [â† not_le, â† ae_iff] at hlt,
    exact eventually_le.antisymm ((ae_restrict_iff' measurable_set_Ioc).2 $
      eventually_of_forall hle) hlt },
  simp only [measure.restrict_congr_set Ioc_ae_eq_Icc] at h_eq,
  exact Î» c hc, (measure.eq_on_Icc_of_ae_eq volume hab.ne h_eq hfc hgc hc).ge
end

lemma integral_nonneg_of_ae_restrict (hab : a â‰¤ b) (hf : 0 â‰¤áµ[Î¼.restrict (Icc a b)] f) :
  0 â‰¤ (âˆ« u in a..b, f u âˆ‚Î¼) :=
let H := ae_restrict_of_ae_restrict_of_subset Ioc_subset_Icc_self hf in
by simpa only [integral_of_le hab] using set_integral_nonneg_of_ae_restrict H

lemma integral_nonneg_of_ae (hab : a â‰¤ b) (hf : 0 â‰¤áµ[Î¼] f) :
  0 â‰¤ (âˆ« u in a..b, f u âˆ‚Î¼) :=
integral_nonneg_of_ae_restrict hab $ ae_restrict_of_ae hf

lemma integral_nonneg_of_forall (hab : a â‰¤ b) (hf : âˆ€ u, 0 â‰¤ f u) :
  0 â‰¤ (âˆ« u in a..b, f u âˆ‚Î¼) :=
integral_nonneg_of_ae hab $ eventually_of_forall hf

lemma integral_nonneg (hab : a â‰¤ b) (hf : âˆ€ u, u âˆˆ Icc a b â†’ 0 â‰¤ f u) :
  0 â‰¤ (âˆ« u in a..b, f u âˆ‚Î¼) :=
integral_nonneg_of_ae_restrict hab $ (ae_restrict_iff' measurable_set_Icc).mpr $ ae_of_all Î¼ hf

lemma abs_integral_le_integral_abs (hab : a â‰¤ b) :
  |âˆ« x in a..b, f x âˆ‚Î¼| â‰¤ âˆ« x in a..b, |f x| âˆ‚Î¼ :=
by simpa only [â† real.norm_eq_abs] using norm_integral_le_integral_norm hab

section mono

variables (hab : a â‰¤ b) (hf : interval_integrable f Î¼ a b) (hg : interval_integrable g Î¼ a b)

include hab hf hg

lemma integral_mono_ae_restrict (h : f â‰¤áµ[Î¼.restrict (Icc a b)] g) :
  âˆ« u in a..b, f u âˆ‚Î¼ â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ :=
let H := h.filter_mono $ ae_mono $ measure.restrict_mono Ioc_subset_Icc_self $ le_refl Î¼ in
by simpa only [integral_of_le hab] using set_integral_mono_ae_restrict hf.1 hg.1 H

lemma integral_mono_ae (h : f â‰¤áµ[Î¼] g) :
  âˆ« u in a..b, f u âˆ‚Î¼ â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ :=
by simpa only [integral_of_le hab] using set_integral_mono_ae hf.1 hg.1 h

lemma integral_mono_on (h : âˆ€ x âˆˆ Icc a b, f x â‰¤ g x) :
  âˆ« u in a..b, f u âˆ‚Î¼ â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ :=
let H := Î» x hx, h x $ Ioc_subset_Icc_self hx in
by simpa only [integral_of_le hab] using set_integral_mono_on hf.1 hg.1 measurable_set_Ioc H

lemma integral_mono (h : f â‰¤ g) :
  âˆ« u in a..b, f u âˆ‚Î¼ â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ :=
integral_mono_ae hab hf hg $ ae_of_all _ h

omit hg hab

lemma integral_mono_interval {c d} (hca : c â‰¤ a) (hab : a â‰¤ b) (hbd : b â‰¤ d)
  (hf : 0 â‰¤áµ[Î¼.restrict (Ioc c d)] f) (hfi : interval_integrable f Î¼ c d):
  âˆ« x in a..b, f x âˆ‚Î¼ â‰¤ âˆ« x in c..d, f x âˆ‚Î¼ :=
begin
  rw [integral_of_le hab, integral_of_le (hca.trans (hab.trans hbd))],
  exact set_integral_mono_set hfi.1 hf (Ioc_subset_Ioc hca hbd).eventually_le
end

lemma abs_integral_mono_interval {c d } (h : Î™ a b âŠ† Î™ c d)
  (hf : 0 â‰¤áµ[Î¼.restrict (Î™ c d)] f) (hfi : interval_integrable f Î¼ c d) :
  |âˆ« x in a..b, f x âˆ‚Î¼| â‰¤ |âˆ« x in c..d, f x âˆ‚Î¼| :=
have hf' : 0 â‰¤áµ[Î¼.restrict (Î™ a b)] f, from ae_mono (measure.restrict_mono h le_rfl) hf,
calc |âˆ« x in a..b, f x âˆ‚Î¼| = |âˆ« x in Î™ a b, f x âˆ‚Î¼| : abs_integral_eq_abs_integral_uIoc f
... = âˆ« x in Î™ a b, f x âˆ‚Î¼ : abs_of_nonneg (measure_theory.integral_nonneg_of_ae hf')
... â‰¤ âˆ« x in Î™ c d, f x âˆ‚Î¼ : set_integral_mono_set hfi.def hf h.eventually_le
... â‰¤ |âˆ« x in Î™ c d, f x âˆ‚Î¼| : le_abs_self _
... = |âˆ« x in c..d, f x âˆ‚Î¼| : (abs_integral_eq_abs_integral_uIoc f).symm

end mono

end

section has_sum
variables {Î¼ : measure â„} {f : â„ â†’ E}

lemma _root_.measure_theory.integrable.has_sum_interval_integral (hfi : integrable f Î¼) (y : â„) :
  has_sum (Î» (n : â„¤), âˆ« x in (y + n)..(y + n + 1), f x âˆ‚Î¼) (âˆ« x, f x âˆ‚Î¼) :=
begin
  simp_rw integral_of_le (le_add_of_nonneg_right zero_le_one),
  rw [â†integral_univ, â†Union_Ioc_add_int_cast y],
  exact has_sum_integral_Union (Î» i, measurable_set_Ioc) (pairwise_disjoint_Ioc_add_int_cast y)
    hfi.integrable_on,
end

lemma _root_.measure_theory.integrable.has_sum_interval_integral_comp_add_int
  (hfi : integrable f) :
  has_sum (Î» (n : â„¤), âˆ« x in 0..1, f (x + n)) (âˆ« x, f x) :=
begin
  convert hfi.has_sum_interval_integral 0 using 2,
  ext1 n,
  rw [integral_comp_add_right, zero_add, add_comm],
end

end has_sum

end interval_integral
