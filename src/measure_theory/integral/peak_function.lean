/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import measure_theory.integral.set_integral
import measure_theory.function.locally_integrable

/-!
# Integrals against peak functions

A sequence of peak functions is a sequence of functions with average one concentrating around
a point `xâ‚€`. Given such a sequence `Ï†â‚™`, then `âˆ« Ï†â‚™ g` tends to `g xâ‚€` in many situations, with
a whole zoo of possible assumptions on `Ï†â‚™` and `g`. This file is devoted to such results.

## Main results

* `tendsto_set_integral_peak_smul_of_integrable_on_of_continuous_within_at`: If a sequence of peak
  functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€`, and
  `g` is integrable and continuous at `xâ‚€`, then `âˆ« Ï†áµ¢ â€¢ g` converges to `g xâ‚€`.
* `tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_continuous_on`:
  If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
  then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
  concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€`
  if `g` is continuous on `s`.

Note that there are related results about convolution with respect to peak functions in the file
`analysis.convolution`, such as `convolution_tendsto_right` there.
-/

open set filter measure_theory measure_theory.measure topological_space metric
open_locale topology ennreal

/-- This lemma exists for finsets, but not for sets currently. porting note: move to
data.set.basic after the port. -/
lemma set.disjoint_sdiff_inter {Î± : Type*} (s t : set Î±) : disjoint (s \ t) (s âˆ© t) :=
disjoint_of_subset_right (inter_subset_right _ _) disjoint_sdiff_left

open set

variables {Î± E Î¹ : Type*} {hm : measurable_space Î±} {Î¼ : measure Î±}
  [topological_space Î±] [borel_space Î±]
  [normed_add_comm_group E] [normed_space â„ E]
  {g : Î± â†’ E} {l : filter Î¹} {xâ‚€ : Î±} {s : set Î±} {Ï† : Î¹ â†’ Î± â†’ â„}

/-- If a sequence of peak functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€`, and
`g` is integrable and continuous at `xâ‚€`, then `Ï†áµ¢ â€¢ g` is eventually integrable. -/
lemma integrable_on_peak_smul_of_integrable_on_of_continuous_within_at
  (hs : measurable_set s)
  (hlÏ† : âˆ€ (u : set Î±), is_open u â†’ xâ‚€ âˆˆ u â†’ tendsto_uniformly_on Ï† 0 l (s \ u))
  (hiÏ† : âˆ€á¶  i in l, âˆ« x in s, Ï† i x âˆ‚Î¼ = 1)
  (hmg : integrable_on g s Î¼)
  (hcg : continuous_within_at g s xâ‚€) :
  âˆ€á¶  i in l, integrable_on (Î» x, Ï† i x â€¢ g x) s Î¼ :=
begin
  obtain âŸ¨u, u_open, xâ‚€u, huâŸ© : âˆƒ u, is_open u âˆ§ xâ‚€ âˆˆ u âˆ§ âˆ€ x âˆˆ u âˆ© s, g x âˆˆ ball (g xâ‚€) 1,
    from mem_nhds_within.1 (hcg (ball_mem_nhds _ zero_lt_one)),
  filter_upwards [tendsto_uniformly_on_iff.1 (hlÏ† u u_open xâ‚€u) 1 zero_lt_one, hiÏ†]
    with i hi h'i,
  have A : integrable_on (Î» x, Ï† i x â€¢ g x) (s \ u) Î¼,
  { apply integrable.smul_of_top_right (hmg.mono (diff_subset _ _) le_rfl),
    apply mem_â„’p_top_of_bound
      ((integrable_of_integral_eq_one h'i).ae_strongly_measurable.mono_set ((diff_subset _ _))) 1,
    filter_upwards [self_mem_ae_restrict (hs.diff u_open.measurable_set)] with x hx,
    simpa only [pi.zero_apply, dist_zero_left] using (hi x hx).le },
  have B : integrable_on (Î» x, Ï† i x â€¢ g x) (s âˆ© u) Î¼,
  { apply integrable.smul_of_top_left,
    { exact integrable_on.mono_set (integrable_of_integral_eq_one h'i) (inter_subset_left _ _) },
    { apply mem_â„’p_top_of_bound (hmg.mono_set (inter_subset_left _ _)).ae_strongly_measurable
        (â€–g xâ‚€â€– + 1),
      filter_upwards [self_mem_ae_restrict (hs.inter u_open.measurable_set)] with x hx,
      rw inter_comm at hx,
      exact (norm_lt_of_mem_ball (hu x hx)).le } },
  convert A.union B,
  simp only [diff_union_inter],
end

variables [complete_space E]

/-- If a sequence of peak functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€`, and
`g` is integrable and continuous at `xâ‚€`, then `âˆ« Ï†áµ¢ â€¢ g` converges to `xâ‚€`. Auxiliary lemma
where one assumes additionally `g xâ‚€ = 0`. -/
lemma tendsto_set_integral_peak_smul_of_integrable_on_of_continuous_within_at_aux
  (hs : measurable_set s)
  (hnÏ† : âˆ€á¶  i in l, âˆ€ x âˆˆ s, 0 â‰¤ Ï† i x)
  (hlÏ† : âˆ€ (u : set Î±), is_open u â†’ xâ‚€ âˆˆ u â†’ tendsto_uniformly_on Ï† 0 l (s \ u))
  (hiÏ† : âˆ€á¶  i in l, âˆ« x in s, Ï† i x âˆ‚Î¼ = 1)
  (hmg : integrable_on g s Î¼) (h'g : g xâ‚€ = 0)
  (hcg : continuous_within_at g s xâ‚€) :
  tendsto (Î» i : Î¹, âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼) l (ğ“ 0) :=
begin
  refine metric.tendsto_nhds.2 (Î» Îµ Îµpos, _),
  obtain âŸ¨Î´, hÎ´, Î´posâŸ© : âˆƒ Î´, Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ + Î´ < Îµ âˆ§ 0 < Î´,
  { have A : tendsto (Î» Î´, Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ + Î´) (ğ“[>] 0) (ğ“ (0 * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ + 0)),
    { apply tendsto.mono_left _ nhds_within_le_nhds,
      exact (tendsto_id.mul tendsto_const_nhds).add tendsto_id },
    rw [zero_mul, zero_add] at A,
    exact (((tendsto_order.1 A).2 Îµ Îµpos).and self_mem_nhds_within).exists },
  suffices : âˆ€á¶  i in l, â€–âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ + Î´,
  { filter_upwards [this] with i hi,
    simp only [dist_zero_right],
    exact hi.trans_lt hÎ´ },
  obtain âŸ¨u, u_open, xâ‚€u, huâŸ© : âˆƒ u, is_open u âˆ§ xâ‚€ âˆˆ u âˆ§ âˆ€ x âˆˆ u âˆ© s, g x âˆˆ ball (g xâ‚€) Î´,
    from mem_nhds_within.1 (hcg (ball_mem_nhds _ Î´pos)),
  filter_upwards [tendsto_uniformly_on_iff.1 (hlÏ† u u_open xâ‚€u) Î´ Î´pos, hiÏ†, hnÏ†,
    integrable_on_peak_smul_of_integrable_on_of_continuous_within_at hs hlÏ† hiÏ† hmg hcg]
      with i hi h'i hÏ†pos h''i,
  have B : â€–âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ Î´, from calc
    â€–âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ âˆ« x in s âˆ© u, â€–Ï† i x â€¢ g xâ€– âˆ‚Î¼ :
      norm_integral_le_integral_norm _
    ... â‰¤ âˆ« x in s âˆ© u, â€–Ï† i xâ€– * Î´ âˆ‚Î¼ :
      begin
        refine set_integral_mono_on _ _ (hs.inter u_open.measurable_set) (Î» x hx, _),
        { exact integrable_on.mono_set h''i.norm (inter_subset_left _ _) },
        { exact integrable_on.mono_set ((integrable_of_integral_eq_one h'i).norm.mul_const _)
            (inter_subset_left _ _) },
        rw norm_smul,
        apply mul_le_mul_of_nonneg_left _ (norm_nonneg _),
        rw [inter_comm, h'g] at hu,
        exact (mem_ball_zero_iff.1 (hu x hx)).le,
      end
    ... â‰¤ âˆ« x in s, â€–Ï† i xâ€– * Î´ âˆ‚Î¼ :
      begin
        apply set_integral_mono_set,
        { exact ((integrable_of_integral_eq_one h'i).norm.mul_const _) },
        { exact eventually_of_forall (Î» x, mul_nonneg (norm_nonneg _) Î´pos.le) },
        { apply eventually_of_forall, exact inter_subset_left s u }
      end
    ... = âˆ« x in s, Ï† i x * Î´ âˆ‚Î¼ :
      begin
        apply set_integral_congr hs (Î» x hx, _),
        rw real.norm_of_nonneg (hÏ†pos _ hx),
      end
    ... = Î´ : by rw [integral_mul_right, h'i, one_mul],
  have C : â€–âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼, from calc
    â€–âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼â€– â‰¤ âˆ« x in s \ u, â€–Ï† i x â€¢ g xâ€– âˆ‚Î¼ :
      norm_integral_le_integral_norm _
    ... â‰¤ âˆ« x in s \ u, Î´ * â€–g xâ€– âˆ‚Î¼ :
      begin
        refine set_integral_mono_on _ _ (hs.diff u_open.measurable_set) (Î» x hx, _),
        { exact integrable_on.mono_set h''i.norm (diff_subset _ _) },
        { exact integrable_on.mono_set (hmg.norm.const_mul _) (diff_subset _ _) },
        rw norm_smul,
        apply mul_le_mul_of_nonneg_right _ (norm_nonneg _),
        simpa only [pi.zero_apply, dist_zero_left] using (hi x hx).le,
      end
    ... â‰¤ Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ :
      begin
        rw integral_mul_left,
        apply mul_le_mul_of_nonneg_left (set_integral_mono_set hmg.norm _ _) Î´pos.le,
        { exact eventually_of_forall (Î» x, norm_nonneg _) },
        { apply eventually_of_forall, exact diff_subset s u }
      end,
  calc
  â€–âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼â€– = â€–âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼ + âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– :
    begin
      conv_lhs { rw â† diff_union_inter s u },
      rw integral_union (disjoint_sdiff_inter _ _) (hs.inter u_open.measurable_set)
        (h''i.mono_set (diff_subset _ _)) (h''i.mono_set (inter_subset_left _ _))
    end
  ... â‰¤ â€–âˆ« x in s \ u, Ï† i x â€¢ g x âˆ‚Î¼â€– + â€–âˆ« x in s âˆ© u, Ï† i x â€¢ g x âˆ‚Î¼â€– : norm_add_le _ _
  ... â‰¤ Î´ * âˆ« x in s, â€–g xâ€– âˆ‚Î¼ + Î´ : add_le_add C B
end

/- If a sequence of peak functions `Ï†áµ¢` converges uniformly to zero away from a point `xâ‚€`, and
`g` is integrable and continuous at `xâ‚€`, then `âˆ« Ï†áµ¢ â€¢ g` converges to `xâ‚€`. -/
lemma tendsto_set_integral_peak_smul_of_integrable_on_of_continuous_within_at
  (hs : measurable_set s) (h's : Î¼ s â‰  âˆ)
  (hnÏ† : âˆ€á¶  i in l, âˆ€ x âˆˆ s, 0 â‰¤ Ï† i x)
  (hlÏ† : âˆ€ (u : set Î±), is_open u â†’ xâ‚€ âˆˆ u â†’ tendsto_uniformly_on Ï† 0 l (s \ u))
  (hiÏ† : (Î» i, âˆ« x in s, Ï† i x âˆ‚Î¼) =á¶ [l] 1)
  (hmg : integrable_on g s Î¼)
  (hcg : continuous_within_at g s xâ‚€) :
  tendsto (Î» i : Î¹, âˆ« x in s, Ï† i x â€¢ g x âˆ‚Î¼) l (ğ“ (g xâ‚€)) :=
begin
  let h := g - (Î» y, g xâ‚€),
  have A : tendsto (Î» i : Î¹, âˆ« x in s, Ï† i x â€¢ h x âˆ‚Î¼ + (âˆ« x in s, Ï† i x âˆ‚Î¼) â€¢ g xâ‚€) l
    (ğ“ (0 + (1 : â„) â€¢ g xâ‚€)),
  { refine tendsto.add _ (tendsto.smul (tendsto_const_nhds.congr' hiÏ†.symm) tendsto_const_nhds),
    apply tendsto_set_integral_peak_smul_of_integrable_on_of_continuous_within_at_aux
      hs hnÏ† hlÏ† hiÏ†,
    { apply integrable.sub hmg,
      apply integrable_on_const.2,
      simp only [h's.lt_top, or_true] },
    { simp only [h, pi.sub_apply, sub_self] },
    { exact hcg.sub continuous_within_at_const } },
  simp only [one_smul, zero_add] at A,
  refine tendsto.congr' _ A,
  filter_upwards [integrable_on_peak_smul_of_integrable_on_of_continuous_within_at
    hs hlÏ† hiÏ† hmg hcg, hiÏ†] with i hi h'i,
  simp only [h, pi.sub_apply, smul_sub],
  rw [integral_sub hi, integral_smul_const, sub_add_cancel],
  exact integrable.smul_const (integrable_of_integral_eq_one h'i) _,
end

/-- If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€` if `g` is
integrable on `s` and continuous at `xâ‚€`.

Version assuming that `Î¼` gives positive mass to all neighborhoods of `xâ‚€` within `s`.
For a less precise but more usable version, see
`tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_continuous_on`.
 -/
lemma tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_measure_nhds_within_pos
  [metrizable_space Î±] [is_locally_finite_measure Î¼] (hs : is_compact s)
  (hÎ¼ : âˆ€ u, is_open u â†’ xâ‚€ âˆˆ u â†’ 0 < Î¼ (u âˆ© s))
  {c : Î± â†’ â„} (hc : continuous_on c s) (h'c : âˆ€ y âˆˆ s, y â‰  xâ‚€ â†’ c y < c xâ‚€)
  (hnc : âˆ€ x âˆˆ s, 0 â‰¤ c x) (hncâ‚€ : 0 < c xâ‚€) (hâ‚€ : xâ‚€ âˆˆ s)
  (hmg : integrable_on g s Î¼)
  (hcg : continuous_within_at g s xâ‚€) :
  tendsto (Î» (n : â„•), (âˆ« x in s, (c x) ^ n âˆ‚Î¼)â»Â¹ â€¢ (âˆ« x in s, (c x) ^ n â€¢ g x âˆ‚Î¼)) at_top
    (ğ“ (g xâ‚€)) :=
begin
  /- We apply the general result
  `tendsto_set_integral_peak_smul_of_integrable_on_of_continuous_within_at` to the sequence of
  peak functions `Ï†â‚™ = (c x) ^ n / âˆ« (c x) ^ n`. The only nontrivial bit is to check that this
  sequence converges uniformly to zero on any set `s \ u` away from `xâ‚€`. By compactness, the
  function `c` is bounded by `t < c xâ‚€` there. Consider `t' âˆˆ (t, c xâ‚€)`, and a neighborhood `v`
  of `xâ‚€` where `c x â‰¥ t'`, by continuity. Then `âˆ« (c x) ^ n` is bounded below by `t' ^ n Î¼ v`.
  It follows that, on `s \ u`, then `Ï†â‚™ x â‰¤ t ^ n / (t' ^ n Î¼ v)`, which tends (exponentially fast)
  to zero with `n`. -/
  let Ï† : â„• â†’ Î± â†’ â„ := Î» n x, (âˆ« x in s, (c x) ^ n âˆ‚Î¼)â»Â¹ * (c x) ^ n,
  have hnÏ† : âˆ€ n, âˆ€ x âˆˆ s, 0 â‰¤ Ï† n x,
  { assume n x hx,
    apply mul_nonneg (inv_nonneg.2 _) (pow_nonneg (hnc x hx) _),
    exact set_integral_nonneg hs.measurable_set (Î» x hx, pow_nonneg (hnc x hx) _) },
  have I : âˆ€ n, integrable_on (Î» x, (c x)^n) s Î¼ :=
    Î» n, continuous_on.integrable_on_compact hs (hc.pow n),
  have J : âˆ€ n, 0 â‰¤áµ[Î¼.restrict s] Î» (x : Î±), c x ^ n,
  { assume n,
    filter_upwards [ae_restrict_mem hs.measurable_set] with x hx,
    exact pow_nonneg (hnc x hx) n },
  have P : âˆ€ n, 0 < âˆ« x in s, (c x) ^ n âˆ‚Î¼,
  { assume n,
    refine (set_integral_pos_iff_support_of_nonneg_ae (J n) (I n)).2 _,
    obtain âŸ¨u, u_open, xâ‚€_u, huâŸ© : âˆƒ (u : set Î±), is_open u âˆ§ xâ‚€ âˆˆ u âˆ§ u âˆ© s âŠ† c â»Â¹' Ioi 0 :=
      _root_.continuous_on_iff.1 hc xâ‚€ hâ‚€ (Ioi (0 : â„)) is_open_Ioi hncâ‚€,
    apply (hÎ¼ u u_open xâ‚€_u).trans_le,
    exact measure_mono (Î» x hx, âŸ¨ne_of_gt (pow_pos (hu hx) _), hx.2âŸ©) },
  have hiÏ† : âˆ€ n, âˆ« x in s, Ï† n x âˆ‚Î¼ = 1 :=
    Î» n, by rw [integral_mul_left, inv_mul_cancel (P n).ne'],
  have A : âˆ€ (u : set Î±), is_open u â†’ xâ‚€ âˆˆ u â†’ tendsto_uniformly_on Ï† 0 at_top (s \ u),
  { assume u u_open xâ‚€u,
    obtain âŸ¨t, t_pos, txâ‚€, htâŸ© : âˆƒ t, 0 â‰¤ t âˆ§ t < c xâ‚€ âˆ§ (âˆ€ x âˆˆ s \ u, c x â‰¤ t),
    { rcases eq_empty_or_nonempty (s \ u) with h|h,
      { exact âŸ¨0, le_rfl, hncâ‚€,
          by simp only [h, mem_empty_iff_false, is_empty.forall_iff, implies_true_iff]âŸ© },
      obtain âŸ¨x, hx, h'xâŸ© : âˆƒ x âˆˆ s \ u, âˆ€ y âˆˆ s \ u, c y â‰¤ c x :=
        is_compact.exists_forall_ge (hs.diff u_open) h (hc.mono (diff_subset _ _)),
      refine âŸ¨c x, hnc x hx.1, h'c x hx.1 _, h'xâŸ©,
      rintros rfl,
      exact hx.2 xâ‚€u },
    obtain âŸ¨t', tt', t'xâ‚€âŸ© : âˆƒ t', t < t' âˆ§ t' < c xâ‚€ := exists_between txâ‚€,
    have t'_pos : 0 < t' := t_pos.trans_lt tt',
    obtain âŸ¨v, v_open, xâ‚€_v, hvâŸ© : âˆƒ (v : set Î±), is_open v âˆ§ xâ‚€ âˆˆ v âˆ§ v âˆ© s âŠ† c â»Â¹' Ioi t' :=
      _root_.continuous_on_iff.1 hc xâ‚€ hâ‚€ (Ioi t') is_open_Ioi t'xâ‚€,
    have M : âˆ€ n, âˆ€ x âˆˆ s \ u, Ï† n x â‰¤ (Î¼ (v âˆ© s)).to_real â»Â¹ * (t / t') ^ n,
    { assume n x hx,
      have B : t' ^ n * (Î¼ (v âˆ© s)).to_real â‰¤ âˆ« y in s, (c y) ^ n âˆ‚Î¼, from calc
        t' ^ n * (Î¼ (v âˆ© s)).to_real = âˆ« y in v âˆ© s, t' ^ n âˆ‚Î¼ :
          by simp only [integral_const, measure.restrict_apply, measurable_set.univ, univ_inter,
              algebra.id.smul_eq_mul, mul_comm]
        ... â‰¤ âˆ« y in v âˆ© s, (c y) ^ n âˆ‚Î¼ :
          begin
            apply set_integral_mono_on _ _ (v_open.measurable_set.inter hs.measurable_set) _,
            { apply integrable_on_const.2 (or.inr _),
              exact lt_of_le_of_lt (measure_mono (inter_subset_right _ _)) hs.measure_lt_top },
            { exact (I n).mono (inter_subset_right _ _) le_rfl },
            { assume x hx,
              exact pow_le_pow_of_le_left t'_pos.le (le_of_lt (hv hx)) _ }
          end
        ... â‰¤ âˆ« y in s, (c y) ^ n âˆ‚Î¼ :
          set_integral_mono_set (I n) (J n) (eventually_of_forall (inter_subset_right _ _)),
      simp_rw [Ï†, â† div_eq_inv_mul, div_pow, div_div],
      apply div_le_div (pow_nonneg t_pos n) _ _ B,
      { exact pow_le_pow_of_le_left (hnc _ hx.1) (ht x hx) _ },
      { apply mul_pos (pow_pos (t_pos.trans_lt tt') _)
          (ennreal.to_real_pos (hÎ¼ v v_open xâ‚€_v).ne' _),
        have : Î¼ (v âˆ© s) â‰¤ Î¼ s := measure_mono (inter_subset_right _ _),
        exact ne_of_lt (lt_of_le_of_lt this hs.measure_lt_top) } },
    have N : tendsto (Î» n, (Î¼ (v âˆ© s)).to_real â»Â¹ * (t / t') ^ n) at_top
      (ğ“ ((Î¼ (v âˆ© s)).to_real â»Â¹ * 0)),
    { apply tendsto.mul tendsto_const_nhds _, { apply_instance },
      apply tendsto_pow_at_top_nhds_0_of_lt_1 (div_nonneg t_pos t'_pos.le),
      exact (div_lt_one t'_pos).2 tt' },
    rw mul_zero at N,
    refine tendsto_uniformly_on_iff.2 (Î» Îµ Îµpos, _),
    filter_upwards [(tendsto_order.1 N).2 Îµ Îµpos] with n hn x hx,
    simp only [pi.zero_apply, dist_zero_left, real.norm_of_nonneg (hnÏ† n x hx.1)],
    exact (M n x hx).trans_lt hn },
  have : tendsto (Î» (i : â„•), âˆ« (x : Î±) in s, Ï† i x â€¢ g x âˆ‚Î¼) at_top (ğ“ (g xâ‚€)) :=
    tendsto_set_integral_peak_smul_of_integrable_on_of_continuous_within_at hs.measurable_set
      hs.measure_lt_top.ne (eventually_of_forall hnÏ†) A (eventually_of_forall hiÏ†) hmg hcg,
  convert this,
  simp_rw [â† smul_smul, integral_smul],
end

/-- If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€` if `g` is
integrable on `s` and continuous at `xâ‚€`.

Version assuming that `Î¼` gives positive mass to all open sets.
For a less precise but more usable version, see
`tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_continuous_on`.
-/
lemma tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_integrable_on
  [metrizable_space Î±] [is_locally_finite_measure Î¼] [is_open_pos_measure Î¼] (hs : is_compact s)
  {c : Î± â†’ â„} (hc : continuous_on c s) (h'c : âˆ€ y âˆˆ s, y â‰  xâ‚€ â†’ c y < c xâ‚€)
  (hnc : âˆ€ x âˆˆ s, 0 â‰¤ c x) (hncâ‚€ : 0 < c xâ‚€) (hâ‚€ : xâ‚€ âˆˆ closure (interior s))
  (hmg : integrable_on g s Î¼)
  (hcg : continuous_within_at g s xâ‚€) :
  tendsto (Î» (n : â„•), (âˆ« x in s, (c x) ^ n âˆ‚Î¼)â»Â¹ â€¢ (âˆ« x in s, (c x) ^ n â€¢ g x âˆ‚Î¼)) at_top
    (ğ“ (g xâ‚€)) :=
begin
  have : xâ‚€ âˆˆ s,
  { rw â† hs.is_closed.closure_eq, exact closure_mono interior_subset hâ‚€ },
  apply tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_measure_nhds_within_pos
    hs _ hc h'c hnc hncâ‚€ this hmg hcg,
  assume u u_open xâ‚€_u,
  calc 0 < Î¼ (u âˆ© interior s) :
    (u_open.inter is_open_interior).measure_pos Î¼ (_root_.mem_closure_iff.1 hâ‚€ u u_open xâ‚€_u)
  ... â‰¤ Î¼ (u âˆ© s) : measure_mono (inter_subset_inter_right _ interior_subset)
end

/-- If a continuous function `c` realizes its maximum at a unique point `xâ‚€` in a compact set `s`,
then the sequence of functions `(c x) ^ n / âˆ« (c x) ^ n` is a sequence of peak functions
concentrating around `xâ‚€`. Therefore, `âˆ« (c x) ^ n * g / âˆ« (c x) ^ n` converges to `g xâ‚€` if `g` is
continuous on `s`. -/
lemma tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_continuous_on
  [metrizable_space Î±] [is_locally_finite_measure Î¼] [is_open_pos_measure Î¼] (hs : is_compact s)
  {c : Î± â†’ â„} (hc : continuous_on c s) (h'c : âˆ€ y âˆˆ s, y â‰  xâ‚€ â†’ c y < c xâ‚€)
  (hnc : âˆ€ x âˆˆ s, 0 â‰¤ c x) (hncâ‚€ : 0 < c xâ‚€) (hâ‚€ : xâ‚€ âˆˆ closure (interior s))
  (hmg : continuous_on g s) :
  tendsto (Î» (n : â„•), (âˆ« x in s, (c x) ^ n âˆ‚Î¼)â»Â¹ â€¢ (âˆ« x in s, (c x) ^ n â€¢ g x âˆ‚Î¼)) at_top
    (ğ“ (g xâ‚€)) :=
begin
  have : xâ‚€ âˆˆ s,
  { rw â† hs.is_closed.closure_eq, exact closure_mono interior_subset hâ‚€ },
  exact tendsto_set_integral_pow_smul_of_unique_maximum_of_is_compact_of_integrable_on
    hs hc h'c hnc hncâ‚€ hâ‚€ (hmg.integrable_on_compact hs) (hmg xâ‚€ this)
end
