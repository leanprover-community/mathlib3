/-
Copyright (c) 2020 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Yury Kudryashov
-/
import measure_theory.integral.integrable_on
import measure_theory.integral.bochner
import order.filter.indicator_function

/-!
# Set integral

In this file we prove some properties of `âˆ« x in s, f x âˆ‚Î¼`. Recall that this notation
is defined as `âˆ« x, f x âˆ‚(Î¼.restrict s)`. In `integral_indicator` we prove that for a measurable
function `f` and a measurable set `s` this definition coincides with another natural definition:
`âˆ« x, indicator s f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼`, where `indicator s f x` is equal to `f x` for `x âˆˆ s`
and is zero otherwise.

Since `âˆ« x in s, f x âˆ‚Î¼` is a notation, one can rewrite or apply any theorem about `âˆ« x, f x âˆ‚Î¼`
directly. In this file we prove some theorems about dependence of `âˆ« x in s, f x âˆ‚Î¼` on `s`, e.g.
`integral_union`, `integral_empty`, `integral_univ`.

We use the property `integrable_on f s Î¼ := integrable f (Î¼.restrict s)`, defined in
`measure_theory.integrable_on`. We also defined in that same file a predicate
`integrable_at_filter (f : Î± â†’ E) (l : filter Î±) (Î¼ : measure Î±)` saying that `f` is integrable at
some set `s âˆˆ l`.

Finally, we prove a version of the
[Fundamental theorem of calculus](https://en.wikipedia.org/wiki/Fundamental_theorem_of_calculus)
for set integral, see `filter.tendsto.integral_sub_linear_is_o_ae` and its corollaries.
Namely, consider a measurably generated filter `l`, a measure `Î¼` finite at this filter, and
a function `f` that has a finite limit `c` at `l âŠ“ Î¼.ae`. Then `âˆ« x in s, f x âˆ‚Î¼ = Î¼ s â€¢ c + o(Î¼ s)`
as `s` tends to `l.small_sets`, i.e. for any `Îµ>0` there exists `t âˆˆ l` such that
`âˆ¥âˆ« x in s, f x âˆ‚Î¼ - Î¼ s â€¢ câˆ¥ â‰¤ Îµ * Î¼ s` whenever `s âŠ† t`. We also formulate a version of this
theorem for a locally finite measure `Î¼` and a function `f` continuous at a point `a`.

## Notation

We provide the following notations for expressing the integral of a function on a set :
* `âˆ« a in s, f a âˆ‚Î¼` is `measure_theory.integral (Î¼.restrict s) f`
* `âˆ« a in s, f a` is `âˆ« a in s, f a âˆ‚volume`

Note that the set notations are defined in the file `measure_theory/integral/bochner`,
but we reference them here because all theorems about set integrals are in this file.

-/

noncomputable theory
open set filter topological_space measure_theory function
open_locale classical topological_space interval big_operators filter ennreal nnreal measure_theory

variables {Î± Î² E F : Type*} [measurable_space Î±]

namespace measure_theory

section normed_group

variables [normed_group E]  {f g : Î± â†’ E} {s t : set Î±} {Î¼ Î½ : measure Î±}
  {l l' : filter Î±}

variables [complete_space E] [normed_space â„ E]

lemma set_integral_congr_ae (hs : measurable_set s) (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x = g x) :
  âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼ :=
integral_congr_ae ((ae_restrict_iff' hs).2 h)

lemma set_integral_congr (hs : measurable_set s) (h : eq_on f g s) :
  âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, g x âˆ‚Î¼ :=
set_integral_congr_ae hs $ eventually_of_forall h

lemma set_integral_congr_set_ae (hst : s =áµ[Î¼] t) :
  âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in t, f x âˆ‚Î¼ :=
by rw measure.restrict_congr_set hst

lemma integral_union_ae (hst : ae_disjoint Î¼ s t) (ht : null_measurable_set t Î¼)
  (hfs : integrable_on f s Î¼) (hft : integrable_on f t Î¼) :
  âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in t, f x âˆ‚Î¼ :=
by simp only [integrable_on, measure.restrict_unionâ‚€ hst ht, integral_add_measure hfs hft]

lemma integral_union (hst : disjoint s t) (ht : measurable_set t)
  (hfs : integrable_on f s Î¼) (hft : integrable_on f t Î¼) :
  âˆ« x in s âˆª t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in t, f x âˆ‚Î¼ :=
integral_union_ae hst.ae_disjoint ht.null_measurable_set hfs hft

lemma integral_diff (ht : measurable_set t) (hfs : integrable_on f s Î¼)
  (hft : integrable_on f t Î¼) (hts : t âŠ† s) :
  âˆ« x in s \ t, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ - âˆ« x in t, f x âˆ‚Î¼ :=
begin
  rw [eq_sub_iff_add_eq, â† integral_union, diff_union_of_subset hts],
  exacts [disjoint_diff.symm, ht, hfs.mono_set (diff_subset _ _), hft]
end

lemma integral_finset_bUnion {Î¹ : Type*} (t : finset Î¹) {s : Î¹ â†’ set Î±}
  (hs : âˆ€ i âˆˆ t, measurable_set (s i)) (h's : set.pairwise â†‘t (disjoint on s))
  (hf : âˆ€ i âˆˆ t, integrable_on f (s i) Î¼) :
  âˆ« x in (â‹ƒ i âˆˆ t, s i), f x âˆ‚ Î¼ = âˆ‘ i in t, âˆ« x in s i, f x âˆ‚ Î¼ :=
begin
  induction t using finset.induction_on with a t hat IH hs h's,
  { simp },
  { simp only [finset.coe_insert, finset.forall_mem_insert, set.pairwise_insert,
      finset.set_bUnion_insert] at hs hf h's âŠ¢,
    rw [integral_union _ _ hf.1 (integrable_on_finset_Union.2 hf.2)],
    { rw [finset.sum_insert hat, IH hs.2 h's.1 hf.2] },
    { simp only [disjoint_Union_right],
      exact (Î» i hi, (h's.2 i hi (ne_of_mem_of_not_mem hi hat).symm).1) },
    { exact finset.measurable_set_bUnion _ hs.2 } }
end

lemma integral_fintype_Union {Î¹ : Type*} [fintype Î¹] {s : Î¹ â†’ set Î±}
  (hs : âˆ€ i, measurable_set (s i)) (h's : pairwise (disjoint on s))
  (hf : âˆ€ i, integrable_on f (s i) Î¼) :
  âˆ« x in (â‹ƒ i, s i), f x âˆ‚ Î¼ = âˆ‘ i, âˆ« x in s i, f x âˆ‚ Î¼ :=
begin
  convert integral_finset_bUnion finset.univ (Î» i hi, hs i) _ (Î» i _, hf i),
  { simp },
  { simp [pairwise_univ, h's] }
end

lemma integral_empty : âˆ« x in âˆ…, f x âˆ‚Î¼ = 0 := by rw [measure.restrict_empty, integral_zero_measure]

lemma integral_univ : âˆ« x in univ, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ := by rw [measure.restrict_univ]

lemma integral_add_compl (hs : measurable_set s) (hfi : integrable f Î¼) :
  âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in sá¶œ, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
by rw [â† integral_union (@disjoint_compl_right (set Î±) _ _) hs.compl
    hfi.integrable_on hfi.integrable_on, union_compl_self, integral_univ]

/-- For a function `f` and a measurable set `s`, the integral of `indicator s f`
over the whole space is equal to `âˆ« x in s, f x âˆ‚Î¼` defined as `âˆ« x, f x âˆ‚(Î¼.restrict s)`. -/
lemma integral_indicator (hs : measurable_set s) :
  âˆ« x, indicator s f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
begin
  by_cases hfi : integrable_on f s Î¼, swap,
  { rwa [integral_undef, integral_undef],
    rwa integrable_indicator_iff hs },
  calc âˆ« x, indicator s f x âˆ‚Î¼ = âˆ« x in s, indicator s f x âˆ‚Î¼ + âˆ« x in sá¶œ, indicator s f x âˆ‚Î¼ :
    (integral_add_compl hs (hfi.indicator hs)).symm
  ... = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in sá¶œ, 0 âˆ‚Î¼ :
    congr_arg2 (+) (integral_congr_ae (indicator_ae_eq_restrict hs))
      (integral_congr_ae (indicator_ae_eq_restrict_compl hs))
  ... = âˆ« x in s, f x âˆ‚Î¼ : by simp
end

lemma of_real_set_integral_one_of_measure_ne_top {Î± : Type*} {m : measurable_space Î±}
  {Î¼ : measure Î±} {s : set Î±} (hs : Î¼ s â‰  âˆ) :
  ennreal.of_real (âˆ« x in s, (1 : â„) âˆ‚Î¼) = Î¼ s :=
calc
ennreal.of_real (âˆ« x in s, (1 : â„) âˆ‚Î¼)
    = ennreal.of_real (âˆ« x in s, âˆ¥(1 : â„)âˆ¥ âˆ‚Î¼) : by simp only [norm_one]
... = âˆ«â» x in s, 1 âˆ‚Î¼ :
begin
  rw of_real_integral_norm_eq_lintegral_nnnorm (integrable_on_const.2 (or.inr hs.lt_top)),
  simp only [nnnorm_one, ennreal.coe_one],
end
... = Î¼ s : set_lintegral_one _

lemma of_real_set_integral_one {Î± : Type*} {m : measurable_space Î±} (Î¼ : measure Î±)
  [is_finite_measure Î¼] (s : set Î±) :
  ennreal.of_real (âˆ« x in s, (1 : â„) âˆ‚Î¼) = Î¼ s :=
of_real_set_integral_one_of_measure_ne_top (measure_ne_top Î¼ s)

lemma integral_piecewise [decidable_pred (âˆˆ s)] (hs : measurable_set s)
  {f g : Î± â†’ E} (hf : integrable_on f s Î¼) (hg : integrable_on g sá¶œ Î¼) :
  âˆ« x, s.piecewise f g x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ + âˆ« x in sá¶œ, g x âˆ‚Î¼ :=
by rw [â† set.indicator_add_compl_eq_piecewise,
  integral_add' (hf.indicator hs) (hg.indicator hs.compl),
  integral_indicator hs, integral_indicator hs.compl]

lemma tendsto_set_integral_of_monotone {Î¹ : Type*} [encodable Î¹] [semilattice_sup Î¹]
  {s : Î¹ â†’ set Î±} {f : Î± â†’ E} (hsm : âˆ€ i, measurable_set (s i))
  (h_mono : monotone s) (hfi : integrable_on f (â‹ƒ n, s n) Î¼) :
  tendsto (Î» i, âˆ« a in s i, f a âˆ‚Î¼) at_top (ğ“ (âˆ« a in (â‹ƒ n, s n), f a âˆ‚Î¼)) :=
begin
  have hfi' : âˆ«â» x in â‹ƒ n, s n, âˆ¥f xâˆ¥â‚Š âˆ‚Î¼ < âˆ := hfi.2,
  set S := â‹ƒ i, s i,
  have hSm : measurable_set S := measurable_set.Union hsm,
  have hsub : âˆ€ {i}, s i âŠ† S, from subset_Union s,
  rw [â† with_density_apply _ hSm] at hfi',
  set Î½ := Î¼.with_density (Î» x, âˆ¥f xâˆ¥â‚Š) with hÎ½,
  refine metric.nhds_basis_closed_ball.tendsto_right_iff.2 (Î» Îµ Îµ0, _),
  lift Îµ to â„â‰¥0 using Îµ0.le,
  have : âˆ€á¶  i in at_top, Î½ (s i) âˆˆ Icc (Î½ S - Îµ) (Î½ S + Îµ),
    from tendsto_measure_Union h_mono (ennreal.Icc_mem_nhds hfi'.ne (ennreal.coe_pos.2 Îµ0).ne'),
  refine this.mono (Î» i hi, _),
  rw [mem_closed_ball_iff_norm', â† integral_diff (hsm i) hfi (hfi.mono_set hsub) hsub,
    â† coe_nnnorm, nnreal.coe_le_coe, â† ennreal.coe_le_coe],
  refine (ennnorm_integral_le_lintegral_ennnorm _).trans _,
  rw [â† with_density_apply _ (hSm.diff (hsm _)), â† hÎ½, measure_diff hsub (hsm _)],
  exacts [tsub_le_iff_tsub_le.mp hi.1,
    (hi.2.trans_lt $ ennreal.add_lt_top.2 âŸ¨hfi', ennreal.coe_lt_topâŸ©).ne]
end

lemma has_sum_integral_Union_ae {Î¹ : Type*} [encodable Î¹] {s : Î¹ â†’ set Î±} {f : Î± â†’ E}
  (hm : âˆ€ i, null_measurable_set (s i) Î¼) (hd : pairwise (ae_disjoint Î¼ on s))
  (hfi : integrable_on f (â‹ƒ i, s i) Î¼) :
  has_sum (Î» n, âˆ« a in s n, f a âˆ‚ Î¼) (âˆ« a in â‹ƒ n, s n, f a âˆ‚Î¼) :=
begin
  simp only [integrable_on, measure.restrict_Union_ae hd hm] at hfi âŠ¢,
  exact has_sum_integral_measure hfi
end

lemma has_sum_integral_Union {Î¹ : Type*} [encodable Î¹] {s : Î¹ â†’ set Î±} {f : Î± â†’ E}
  (hm : âˆ€ i, measurable_set (s i)) (hd : pairwise (disjoint on s))
  (hfi : integrable_on f (â‹ƒ i, s i) Î¼) :
  has_sum (Î» n, âˆ« a in s n, f a âˆ‚ Î¼) (âˆ« a in â‹ƒ n, s n, f a âˆ‚Î¼) :=
has_sum_integral_Union_ae (Î» i, (hm i).null_measurable_set) (hd.mono (Î» i j h, h.ae_disjoint)) hfi

lemma integral_Union {Î¹ : Type*} [encodable Î¹] {s : Î¹ â†’ set Î±} {f : Î± â†’ E}
  (hm : âˆ€ i, measurable_set (s i)) (hd : pairwise (disjoint on s))
  (hfi : integrable_on f (â‹ƒ i, s i) Î¼) :
  (âˆ« a in (â‹ƒ n, s n), f a âˆ‚Î¼) = âˆ‘' n, âˆ« a in s n, f a âˆ‚ Î¼ :=
(has_sum.tsum_eq (has_sum_integral_Union hm hd hfi)).symm

lemma integral_Union_ae {Î¹ : Type*} [encodable Î¹] {s : Î¹ â†’ set Î±} {f : Î± â†’ E}
  (hm : âˆ€ i, null_measurable_set (s i) Î¼) (hd : pairwise (ae_disjoint Î¼ on s))
  (hfi : integrable_on f (â‹ƒ i, s i) Î¼) :
  (âˆ« a in (â‹ƒ n, s n), f a âˆ‚Î¼) = âˆ‘' n, âˆ« a in s n, f a âˆ‚ Î¼ :=
(has_sum.tsum_eq (has_sum_integral_Union_ae hm hd hfi)).symm

lemma set_integral_eq_zero_of_forall_eq_zero {f : Î± â†’ E} (hf : strongly_measurable f)
  (ht_eq : âˆ€ x âˆˆ t, f x = 0) :
  âˆ« x in t, f x âˆ‚Î¼ = 0 :=
begin
  refine integral_eq_zero_of_ae _,
  rw [eventually_eq, ae_restrict_iff (hf.measurable_set_eq_fun strongly_measurable_zero)],
  refine eventually_of_forall (Î» x hx, _),
  rw pi.zero_apply,
  exact ht_eq x hx,
end

lemma set_integral_union_eq_left {f : Î± â†’ E} (hf : strongly_measurable f) (hfi : integrable f Î¼)
  (hs : measurable_set s) (ht_eq : âˆ€ x âˆˆ t, f x = 0) :
  âˆ« x in (s âˆª t), f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
begin
  rw [â† set.union_diff_self, union_comm, integral_union,
    set_integral_eq_zero_of_forall_eq_zero _ (Î» x hx, ht_eq x (diff_subset _ _ hx)), zero_add],
  exacts [hf, disjoint_diff.symm, hs, hfi.integrable_on, hfi.integrable_on]
end

lemma set_integral_neg_eq_set_integral_nonpos [linear_order E] [order_closed_topology E]
  {f : Î± â†’ E} (hf : strongly_measurable f) (hfi : integrable f Î¼) :
  âˆ« x in {x | f x < 0}, f x âˆ‚Î¼ = âˆ« x in {x | f x â‰¤ 0}, f x âˆ‚Î¼ :=
begin
  have h_union : {x | f x â‰¤ 0} = {x | f x < 0} âˆª {x | f x = 0},
    by { ext, simp_rw [set.mem_union_eq, set.mem_set_of_eq], exact le_iff_lt_or_eq, },
  rw h_union,
  exact (set_integral_union_eq_left hf hfi (hf.measurable_set_lt strongly_measurable_const)
    (Î» x hx, hx)).symm,
end

lemma integral_norm_eq_pos_sub_neg {f : Î± â†’ â„} (hf : strongly_measurable f)
  (hfi : integrable f Î¼) :
  âˆ« x, âˆ¥f xâˆ¥ âˆ‚Î¼ = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ - âˆ« x in {x | f x â‰¤ 0}, f x âˆ‚Î¼ :=
have h_meas : measurable_set {x | 0 â‰¤ f x}, from strongly_measurable_const.measurable_set_le hf,
calc âˆ« x, âˆ¥f xâˆ¥ âˆ‚Î¼ = âˆ« x in {x | 0 â‰¤ f x}, âˆ¥f xâˆ¥ âˆ‚Î¼ + âˆ« x in {x | 0 â‰¤ f x}á¶œ, âˆ¥f xâˆ¥ âˆ‚Î¼ :
  by rw â† integral_add_compl h_meas hfi.norm
... = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ + âˆ« x in {x | 0 â‰¤ f x}á¶œ, âˆ¥f xâˆ¥ âˆ‚Î¼ :
begin
  congr' 1,
  refine set_integral_congr h_meas (Î» x hx, _),
  dsimp only,
  rw [real.norm_eq_abs, abs_eq_self.mpr _],
  exact hx,
end
... = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ - âˆ« x in {x | 0 â‰¤ f x}á¶œ, f x âˆ‚Î¼ :
begin
  congr' 1,
  rw â† integral_neg,
  refine set_integral_congr h_meas.compl (Î» x hx, _),
  dsimp only,
  rw [real.norm_eq_abs, abs_eq_neg_self.mpr _],
  rw [set.mem_compl_iff, set.nmem_set_of_eq] at hx,
  linarith,
end
... = âˆ« x in {x | 0 â‰¤ f x}, f x âˆ‚Î¼ - âˆ« x in {x | f x â‰¤ 0}, f x âˆ‚Î¼ :
by { rw â† set_integral_neg_eq_set_integral_nonpos hf hfi, congr, ext1 x, simp, }

lemma set_integral_const (c : E) : âˆ« x in s, c âˆ‚Î¼ = (Î¼ s).to_real â€¢ c :=
by rw [integral_const, measure.restrict_apply_univ]

@[simp]
lemma integral_indicator_const (e : E) â¦ƒs : set Î±â¦„ (s_meas : measurable_set s) :
  âˆ« (a : Î±), s.indicator (Î» (x : Î±), e) a âˆ‚Î¼ = (Î¼ s).to_real â€¢ e :=
by rw [integral_indicator s_meas, â† set_integral_const]

@[simp]
lemma integral_indicator_one â¦ƒs : set Î±â¦„ (hs : measurable_set s) :
  âˆ« a, s.indicator 1 a âˆ‚Î¼ = (Î¼ s).to_real :=
(integral_indicator_const 1 hs).trans ((smul_eq_mul _).trans (mul_one _))

lemma set_integral_indicator_const_Lp {p : â„â‰¥0âˆ} (hs : measurable_set s) (ht : measurable_set t)
  (hÎ¼t : Î¼ t â‰  âˆ) (x : E) :
  âˆ« a in s, indicator_const_Lp p ht hÎ¼t x a âˆ‚Î¼ = (Î¼ (t âˆ© s)).to_real â€¢ x :=
calc âˆ« a in s, indicator_const_Lp p ht hÎ¼t x a âˆ‚Î¼
    = (âˆ« a in s, t.indicator (Î» _, x) a âˆ‚Î¼) :
  by rw set_integral_congr_ae hs (indicator_const_Lp_coe_fn.mono (Î» x hx hxs, hx))
... = (Î¼ (t âˆ© s)).to_real â€¢ x : by rw [integral_indicator_const _ ht, measure.restrict_apply ht]

lemma integral_indicator_const_Lp {p : â„â‰¥0âˆ} (ht : measurable_set t) (hÎ¼t : Î¼ t â‰  âˆ) (x : E) :
  âˆ« a, indicator_const_Lp p ht hÎ¼t x a âˆ‚Î¼ = (Î¼ t).to_real â€¢ x :=
calc âˆ« a, indicator_const_Lp p ht hÎ¼t x a âˆ‚Î¼
    = âˆ« a in univ, indicator_const_Lp p ht hÎ¼t x a âˆ‚Î¼ : by rw integral_univ
... = (Î¼ (t âˆ© univ)).to_real â€¢ x : set_integral_indicator_const_Lp measurable_set.univ ht hÎ¼t x
... = (Î¼ t).to_real â€¢ x : by rw inter_univ

lemma set_integral_map {Î²} [measurable_space Î²] {g : Î± â†’ Î²} {f : Î² â†’ E} {s : set Î²}
  (hs : measurable_set s)
  (hf : ae_strongly_measurable f (measure.map g Î¼)) (hg : ae_measurable g Î¼) :
  âˆ« y in s, f y âˆ‚(measure.map g Î¼) = âˆ« x in g â»Â¹' s, f (g x) âˆ‚Î¼ :=
begin
  rw [measure.restrict_map_of_ae_measurable hg hs,
      integral_map (hg.mono_measure measure.restrict_le_self) (hf.mono_measure _)],
  exact measure.map_mono_of_ae_measurable measure.restrict_le_self hg
end

lemma _root_.measurable_embedding.set_integral_map {Î²} {_ : measurable_space Î²} {f : Î± â†’ Î²}
  (hf : measurable_embedding f) (g : Î² â†’ E) (s : set Î²) :
  âˆ« y in s, g y âˆ‚(measure.map f Î¼) = âˆ« x in f â»Â¹' s, g (f x) âˆ‚Î¼ :=
by rw [hf.restrict_map, hf.integral_map]

lemma _root_.closed_embedding.set_integral_map [topological_space Î±] [borel_space Î±]
  {Î²} [measurable_space Î²] [topological_space Î²] [borel_space Î²]
  {g : Î± â†’ Î²} {f : Î² â†’ E} (s : set Î²) (hg : closed_embedding g) :
  âˆ« y in s, f y âˆ‚(measure.map g Î¼) = âˆ« x in g â»Â¹' s, f (g x) âˆ‚Î¼ :=
hg.measurable_embedding.set_integral_map _ _

lemma measure_preserving.set_integral_preimage_emb {Î²} {_ : measurable_space Î²} {f : Î± â†’ Î²} {Î½}
  (hâ‚ : measure_preserving f Î¼ Î½) (hâ‚‚ : measurable_embedding f) (g : Î² â†’ E) (s : set Î²) :
  âˆ« x in f â»Â¹' s, g (f x) âˆ‚Î¼ = âˆ« y in s, g y âˆ‚Î½ :=
(hâ‚.restrict_preimage_emb hâ‚‚ s).integral_comp hâ‚‚ _

lemma measure_preserving.set_integral_image_emb {Î²} {_ : measurable_space Î²} {f : Î± â†’ Î²} {Î½}
  (hâ‚ : measure_preserving f Î¼ Î½) (hâ‚‚ : measurable_embedding f) (g : Î² â†’ E) (s : set Î±) :
  âˆ« y in f '' s, g y âˆ‚Î½ = âˆ« x in s, g (f x) âˆ‚Î¼ :=
eq.symm $ (hâ‚.restrict_image_emb hâ‚‚ s).integral_comp hâ‚‚ _

lemma set_integral_map_equiv {Î²} [measurable_space Î²] (e : Î± â‰ƒáµ Î²) (f : Î² â†’ E) (s : set Î²) :
  âˆ« y in s, f y âˆ‚(measure.map e Î¼) = âˆ« x in e â»Â¹' s, f (e x) âˆ‚Î¼ :=
e.measurable_embedding.set_integral_map f s

lemma norm_set_integral_le_of_norm_le_const_ae {C : â„} (hs : Î¼ s < âˆ)
  (hC : âˆ€áµ x âˆ‚Î¼.restrict s, âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in s, f x âˆ‚Î¼âˆ¥ â‰¤ C * (Î¼ s).to_real :=
begin
  rw â† measure.restrict_apply_univ at *,
  haveI : is_finite_measure (Î¼.restrict s) := âŸ¨â€¹_â€ºâŸ©,
  exact norm_integral_le_of_norm_le_const hC
end

lemma norm_set_integral_le_of_norm_le_const_ae' {C : â„} (hs : Î¼ s < âˆ)
  (hC : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆ¥f xâˆ¥ â‰¤ C) (hfm : ae_strongly_measurable f (Î¼.restrict s)) :
  âˆ¥âˆ« x in s, f x âˆ‚Î¼âˆ¥ â‰¤ C * (Î¼ s).to_real :=
begin
  apply norm_set_integral_le_of_norm_le_const_ae hs,
  have A : âˆ€áµ (x : Î±) âˆ‚Î¼, x âˆˆ s â†’ âˆ¥ae_strongly_measurable.mk f hfm xâˆ¥ â‰¤ C,
  { filter_upwards [hC, hfm.ae_mem_imp_eq_mk] with _ h1 h2 h3,
    rw [â† h2 h3],
    exact h1 h3 },
  have B : measurable_set {x | âˆ¥(hfm.mk f) xâˆ¥ â‰¤ C} :=
    hfm.strongly_measurable_mk.norm.measurable measurable_set_Iic,
  filter_upwards [hfm.ae_eq_mk, (ae_restrict_iff B).2 A] with _ h1 _,
  rwa h1,
end

lemma norm_set_integral_le_of_norm_le_const_ae'' {C : â„} (hs : Î¼ s < âˆ) (hsm : measurable_set s)
  (hC : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in s, f x âˆ‚Î¼âˆ¥ â‰¤ C * (Î¼ s).to_real :=
norm_set_integral_le_of_norm_le_const_ae hs $ by rwa [ae_restrict_eq hsm, eventually_inf_principal]

lemma norm_set_integral_le_of_norm_le_const {C : â„} (hs : Î¼ s < âˆ)
  (hC : âˆ€ x âˆˆ s, âˆ¥f xâˆ¥ â‰¤ C) (hfm : ae_strongly_measurable f (Î¼.restrict s)) :
  âˆ¥âˆ« x in s, f x âˆ‚Î¼âˆ¥ â‰¤ C * (Î¼ s).to_real :=
norm_set_integral_le_of_norm_le_const_ae' hs (eventually_of_forall hC) hfm

lemma norm_set_integral_le_of_norm_le_const' {C : â„} (hs : Î¼ s < âˆ) (hsm : measurable_set s)
  (hC : âˆ€ x âˆˆ s, âˆ¥f xâˆ¥ â‰¤ C) :
  âˆ¥âˆ« x in s, f x âˆ‚Î¼âˆ¥ â‰¤ C * (Î¼ s).to_real :=
norm_set_integral_le_of_norm_le_const_ae'' hs hsm $ eventually_of_forall hC

lemma set_integral_eq_zero_iff_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼.restrict s] f)
  (hfi : integrable_on f s Î¼) :
  âˆ« x in s, f x âˆ‚Î¼ = 0 â†” f =áµ[Î¼.restrict s] 0 :=
integral_eq_zero_iff_of_nonneg_ae hf hfi

lemma set_integral_pos_iff_support_of_nonneg_ae {f : Î± â†’ â„} (hf : 0 â‰¤áµ[Î¼.restrict s] f)
  (hfi : integrable_on f s Î¼) :
  0 < âˆ« x in s, f x âˆ‚Î¼ â†” 0 < Î¼ (support f âˆ© s) :=
begin
  rw [integral_pos_iff_support_of_nonneg_ae hf hfi, measure.restrict_applyâ‚€],
  rw support_eq_preimage,
  exact hfi.ae_strongly_measurable.ae_measurable.null_measurable (measurable_set_singleton 0).compl
end

lemma set_integral_trim {Î±} {m m0 : measurable_space Î±} {Î¼ : measure Î±} (hm : m â‰¤ m0) {f : Î± â†’ E}
  (hf_meas : strongly_measurable[m] f) {s : set Î±} (hs : measurable_set[m] s) :
  âˆ« x in s, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚(Î¼.trim hm) :=
by rwa [integral_trim hm hf_meas, restrict_trim hm Î¼]

lemma integral_Icc_eq_integral_Ioc' [partial_order Î±] {f : Î± â†’ E} {a b : Î±} (ha : Î¼ {a} = 0) :
  âˆ« t in Icc a b, f t âˆ‚Î¼ = âˆ« t in Ioc a b, f t âˆ‚Î¼ :=
set_integral_congr_set_ae (Ioc_ae_eq_Icc' ha).symm

lemma integral_Ioc_eq_integral_Ioo' [partial_order Î±] {f : Î± â†’ E} {a b : Î±} (hb : Î¼ {b} = 0) :
  âˆ« t in Ioc a b, f t âˆ‚Î¼ = âˆ« t in Ioo a b, f t âˆ‚Î¼ :=
set_integral_congr_set_ae (Ioo_ae_eq_Ioc' hb).symm

lemma integral_Icc_eq_integral_Ioc [partial_order Î±] {f : Î± â†’ E} {a b : Î±} [has_no_atoms Î¼] :
  âˆ« t in Icc a b, f t âˆ‚Î¼ = âˆ« t in Ioc a b, f t âˆ‚Î¼ :=
integral_Icc_eq_integral_Ioc' $ measure_singleton a

lemma integral_Ioc_eq_integral_Ioo [partial_order Î±] {f : Î± â†’ E} {a b : Î±} [has_no_atoms Î¼] :
  âˆ« t in Ioc a b, f t âˆ‚Î¼ = âˆ« t in Ioo a b, f t âˆ‚Î¼ :=
integral_Ioc_eq_integral_Ioo' $ measure_singleton b

end normed_group

section mono

variables {Î¼ : measure Î±} {f g : Î± â†’ â„} {s t : set Î±}
  (hf : integrable_on f s Î¼) (hg : integrable_on g s Î¼)

lemma set_integral_mono_ae_restrict (h : f â‰¤áµ[Î¼.restrict s] g) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ âˆ« a in s, g a âˆ‚Î¼ :=
integral_mono_ae hf hg h

lemma set_integral_mono_ae (h : f â‰¤áµ[Î¼] g) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ âˆ« a in s, g a âˆ‚Î¼ :=
set_integral_mono_ae_restrict hf hg (ae_restrict_of_ae h)

lemma set_integral_mono_on (hs : measurable_set s) (h : âˆ€ x âˆˆ s, f x â‰¤ g x) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ âˆ« a in s, g a âˆ‚Î¼ :=
set_integral_mono_ae_restrict hf hg
  (by simp [hs, eventually_le, eventually_inf_principal, ae_of_all _ h])

include hf hg  -- why do I need this include, but we don't need it in other lemmas?
lemma set_integral_mono_on_ae (hs : measurable_set s) (h : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ f x â‰¤ g x) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ âˆ« a in s, g a âˆ‚Î¼ :=
by { refine set_integral_mono_ae_restrict hf hg _, rwa [eventually_le, ae_restrict_iff' hs], }
omit hf hg

lemma set_integral_mono (h : f â‰¤ g) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ âˆ« a in s, g a âˆ‚Î¼ :=
integral_mono hf hg h

lemma set_integral_mono_set (hfi : integrable_on f t Î¼) (hf : 0 â‰¤áµ[Î¼.restrict t] f)
  (hst : s â‰¤áµ[Î¼] t) :
  âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in t, f x âˆ‚Î¼ :=
integral_mono_measure (measure.restrict_mono_ae hst) hf hfi

end mono

section nonneg

variables {Î¼ : measure Î±} {f : Î± â†’ â„} {s : set Î±}

lemma set_integral_nonneg_of_ae_restrict (hf : 0 â‰¤áµ[Î¼.restrict s] f) :
  0 â‰¤ âˆ« a in s, f a âˆ‚Î¼ :=
integral_nonneg_of_ae hf

lemma set_integral_nonneg_of_ae (hf : 0 â‰¤áµ[Î¼] f) : 0 â‰¤ âˆ« a in s, f a âˆ‚Î¼ :=
set_integral_nonneg_of_ae_restrict (ae_restrict_of_ae hf)

lemma set_integral_nonneg (hs : measurable_set s) (hf : âˆ€ a, a âˆˆ s â†’ 0 â‰¤ f a) :
  0 â‰¤ âˆ« a in s, f a âˆ‚Î¼ :=
set_integral_nonneg_of_ae_restrict ((ae_restrict_iff' hs).mpr (ae_of_all Î¼ hf))

lemma set_integral_nonneg_ae (hs : measurable_set s) (hf : âˆ€áµ a âˆ‚Î¼, a âˆˆ s â†’ 0 â‰¤ f a) :
  0 â‰¤ âˆ« a in s, f a âˆ‚Î¼ :=
set_integral_nonneg_of_ae_restrict $ by rwa [eventually_le, ae_restrict_iff' hs]

lemma set_integral_le_nonneg {s : set Î±} (hs : measurable_set s) (hf : strongly_measurable f)
  (hfi : integrable f Î¼) :
  âˆ« x in s, f x âˆ‚Î¼ â‰¤ âˆ« x in {y | 0 â‰¤ f y}, f x âˆ‚Î¼ :=
begin
  rw [â† integral_indicator hs,
      â† integral_indicator (strongly_measurable_const.measurable_set_le hf)],
  exact integral_mono (hfi.indicator hs)
    (hfi.indicator (strongly_measurable_const.measurable_set_le hf))
    (indicator_le_indicator_nonneg s f),
end

lemma set_integral_nonpos_of_ae_restrict (hf : f â‰¤áµ[Î¼.restrict s] 0) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ 0 :=
integral_nonpos_of_ae hf

lemma set_integral_nonpos_of_ae (hf : f â‰¤áµ[Î¼] 0) : âˆ« a in s, f a âˆ‚Î¼ â‰¤ 0 :=
set_integral_nonpos_of_ae_restrict (ae_restrict_of_ae hf)

lemma set_integral_nonpos (hs : measurable_set s) (hf : âˆ€ a, a âˆˆ s â†’ f a â‰¤ 0) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ 0 :=
set_integral_nonpos_of_ae_restrict ((ae_restrict_iff' hs).mpr (ae_of_all Î¼ hf))

lemma set_integral_nonpos_ae (hs : measurable_set s) (hf : âˆ€áµ a âˆ‚Î¼, a âˆˆ s â†’ f a â‰¤ 0) :
  âˆ« a in s, f a âˆ‚Î¼ â‰¤ 0 :=
set_integral_nonpos_of_ae_restrict $ by rwa [eventually_le, ae_restrict_iff' hs]

lemma set_integral_nonpos_le {s : set Î±} (hs : measurable_set s) (hf : strongly_measurable f)
  (hfi : integrable f Î¼) :
  âˆ« x in {y | f y â‰¤ 0}, f x âˆ‚Î¼ â‰¤ âˆ« x in s, f x âˆ‚Î¼ :=
begin
  rw [â† integral_indicator hs,
      â† integral_indicator (hf.measurable_set_le strongly_measurable_const)],
  exact integral_mono (hfi.indicator (hf.measurable_set_le strongly_measurable_const))
    (hfi.indicator hs) (indicator_nonpos_le_indicator s f),
end

end nonneg

section tendsto_mono

variables {Î¼ : measure Î±} [normed_group E] [complete_space E] [normed_space â„ E]
  {s : â„• â†’ set Î±} {f : Î± â†’ E}

lemma _root_.antitone.tendsto_set_integral (hsm : âˆ€ i, measurable_set (s i))
  (h_anti : antitone s) (hfi : integrable_on f (s 0) Î¼) :
  tendsto (Î»i, âˆ« a in s i, f a âˆ‚Î¼) at_top (ğ“ (âˆ« a in (â‹‚ n, s n), f a âˆ‚Î¼)) :=
begin
  let bound : Î± â†’ â„ := indicator (s 0) (Î» a, âˆ¥f aâˆ¥),
  have h_int_eq : (Î» i, âˆ« a in s i, f a âˆ‚Î¼) = (Î» i, âˆ« a, (s i).indicator f a âˆ‚Î¼),
    from funext (Î» i, (integral_indicator (hsm i)).symm),
  rw h_int_eq,
  rw â† integral_indicator (measurable_set.Inter hsm),
  refine tendsto_integral_of_dominated_convergence bound _ _ _ _,
  { intro n,
    rw ae_strongly_measurable_indicator_iff (hsm n),
    exact (integrable_on.mono_set hfi (h_anti (zero_le n))).1 },
  { rw integrable_indicator_iff (hsm 0),
    exact hfi.norm, },
  { simp_rw norm_indicator_eq_indicator_norm,
    refine Î» n, eventually_of_forall (Î» x, _),
    exact indicator_le_indicator_of_subset (h_anti (zero_le n)) (Î» a, norm_nonneg _) _ },
  { filter_upwards with a using le_trans (h_anti.tendsto_indicator _ _ _) (pure_le_nhds _), },
end

end tendsto_mono

/-! ### Continuity of the set integral

We prove that for any set `s`, the function `Î» f : Î± â†’â‚[Î¼] E, âˆ« x in s, f x âˆ‚Î¼` is continuous. -/

section continuous_set_integral
variables [normed_group E] {ğ•œ : Type*} [is_R_or_C ğ•œ] [normed_group F] [normed_space ğ•œ F]
  {p : â„â‰¥0âˆ} {Î¼ : measure Î±}

/-- For `f : Lp E p Î¼`, we can define an element of `Lp E p (Î¼.restrict s)` by
`(Lp.mem_â„’p f).restrict s).to_Lp f`. This map is additive. -/
lemma Lp_to_Lp_restrict_add (f g : Lp E p Î¼) (s : set Î±) :
  ((Lp.mem_â„’p (f + g)).restrict s).to_Lp â‡‘(f + g)
    = ((Lp.mem_â„’p f).restrict s).to_Lp f + ((Lp.mem_â„’p g).restrict s).to_Lp g :=
begin
  ext1,
  refine (ae_restrict_of_ae (Lp.coe_fn_add f g)).mp _,
  refine (Lp.coe_fn_add (mem_â„’p.to_Lp f ((Lp.mem_â„’p f).restrict s))
    (mem_â„’p.to_Lp g ((Lp.mem_â„’p g).restrict s))).mp _,
  refine (mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p f).restrict s)).mp _,
  refine (mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p g).restrict s)).mp _,
  refine (mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p (f+g)).restrict s)).mono (Î» x hx1 hx2 hx3 hx4 hx5, _),
  rw [hx4, hx1, pi.add_apply, hx2, hx3, hx5, pi.add_apply],
end

/-- For `f : Lp E p Î¼`, we can define an element of `Lp E p (Î¼.restrict s)` by
`(Lp.mem_â„’p f).restrict s).to_Lp f`. This map commutes with scalar multiplication. -/
lemma Lp_to_Lp_restrict_smul (c : ğ•œ) (f : Lp F p Î¼) (s : set Î±) :
  ((Lp.mem_â„’p (c â€¢ f)).restrict s).to_Lp â‡‘(c â€¢ f) = c â€¢ (((Lp.mem_â„’p f).restrict s).to_Lp f) :=
begin
  ext1,
  refine (ae_restrict_of_ae (Lp.coe_fn_smul c f)).mp _,
  refine (mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p f).restrict s)).mp _,
  refine (mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p (c â€¢ f)).restrict s)).mp _,
  refine (Lp.coe_fn_smul c (mem_â„’p.to_Lp f ((Lp.mem_â„’p f).restrict s))).mono
    (Î» x hx1 hx2 hx3 hx4, _),
  rw [hx2, hx1, pi.smul_apply, hx3, hx4, pi.smul_apply],
end

/-- For `f : Lp E p Î¼`, we can define an element of `Lp E p (Î¼.restrict s)` by
`(Lp.mem_â„’p f).restrict s).to_Lp f`. This map is non-expansive. -/
lemma norm_Lp_to_Lp_restrict_le (s : set Î±) (f : Lp E p Î¼) :
  âˆ¥((Lp.mem_â„’p f).restrict s).to_Lp fâˆ¥ â‰¤ âˆ¥fâˆ¥ :=
begin
  rw [Lp.norm_def, Lp.norm_def, ennreal.to_real_le_to_real (Lp.snorm_ne_top _) (Lp.snorm_ne_top _)],
  refine (le_of_eq _).trans (snorm_mono_measure _ measure.restrict_le_self),
  { exact s, },
  exact snorm_congr_ae (mem_â„’p.coe_fn_to_Lp _),
end

variables (Î± F ğ•œ)
/-- Continuous linear map sending a function of `Lp F p Î¼` to the same function in
`Lp F p (Î¼.restrict s)`. -/
def Lp_to_Lp_restrict_clm (Î¼ : measure Î±) (p : â„â‰¥0âˆ) [hp : fact (1 â‰¤ p)] (s : set Î±) :
  Lp F p Î¼ â†’L[ğ•œ] Lp F p (Î¼.restrict s) :=
@linear_map.mk_continuous ğ•œ ğ•œ (Lp F p Î¼) (Lp F p (Î¼.restrict s)) _ _ _ _ _ _ (ring_hom.id ğ•œ)
  âŸ¨Î» f, mem_â„’p.to_Lp f ((Lp.mem_â„’p f).restrict s), Î» f g, Lp_to_Lp_restrict_add f g s,
    Î» c f, Lp_to_Lp_restrict_smul c f sâŸ©
  1 (by { intro f, rw one_mul, exact norm_Lp_to_Lp_restrict_le s f, })

variables {Î± F ğ•œ}

variables (ğ•œ)
lemma Lp_to_Lp_restrict_clm_coe_fn [hp : fact (1 â‰¤ p)] (s : set Î±) (f : Lp F p Î¼) :
  Lp_to_Lp_restrict_clm Î± F ğ•œ Î¼ p s f =áµ[Î¼.restrict s] f :=
mem_â„’p.coe_fn_to_Lp ((Lp.mem_â„’p f).restrict s)
variables {ğ•œ}

@[continuity]
lemma continuous_set_integral [normed_space â„ E] [complete_space E] (s : set Î±) :
  continuous (Î» f : Î± â†’â‚[Î¼] E, âˆ« x in s, f x âˆ‚Î¼) :=
begin
  haveI : fact ((1 : â„â‰¥0âˆ) â‰¤ 1) := âŸ¨le_rflâŸ©,
  have h_comp : (Î» f : Î± â†’â‚[Î¼] E, âˆ« x in s, f x âˆ‚Î¼)
    = (integral (Î¼.restrict s)) âˆ˜ (Î» f, Lp_to_Lp_restrict_clm Î± E â„ Î¼ 1 s f),
  { ext1 f,
    rw [function.comp_apply, integral_congr_ae (Lp_to_Lp_restrict_clm_coe_fn â„ s f)], },
  rw h_comp,
  exact continuous_integral.comp (Lp_to_Lp_restrict_clm Î± E â„ Î¼ 1 s).continuous,
end

end continuous_set_integral


end measure_theory

open measure_theory asymptotics metric

variables {Î¹ : Type*} [normed_group E]

/-- Fundamental theorem of calculus for set integrals: if `Î¼` is a measure that is finite at a
filter `l` and `f` is a measurable function that has a finite limit `b` at `l âŠ“ Î¼.ae`, then `âˆ« x in
s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ b + o(Î¼ (s i))` at a filter `li` provided that `s i` tends to `l.small_sets`
along `li`. Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `(Î¼ (s i)).to_real` in the actual statement.

Often there is a good formula for `(Î¼ (s i)).to_real`, so the formalization can take an optional
argument `m` with this formula and a proof `of `(Î» i, (Î¼ (s i)).to_real) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).to_real` is used in the output. -/
lemma filter.tendsto.integral_sub_linear_is_o_ae
  [normed_space â„ E] [complete_space E]
  {Î¼ : measure Î±} {l : filter Î±} [l.is_measurably_generated]
  {f : Î± â†’ E} {b : E} (h : tendsto f (l âŠ“ Î¼.ae) (ğ“ b))
  (hfm : strongly_measurable_at_filter f l Î¼) (hÎ¼ : Î¼.finite_at_filter l)
  {s : Î¹ â†’ set Î±} {li : filter Î¹} (hs : tendsto s li l.small_sets)
  (m : Î¹ â†’ â„ := Î» i, (Î¼ (s i)).to_real)
  (hsÎ¼ : (Î» i, (Î¼ (s i)).to_real) =á¶ [li] m . tactic.interactive.refl) :
  is_o (Î» i, âˆ« x in s i, f x âˆ‚Î¼ - m i â€¢ b) m li :=
begin
  suffices : is_o (Î» s, âˆ« x in s, f x âˆ‚Î¼ - (Î¼ s).to_real â€¢ b) (Î» s, (Î¼ s).to_real) l.small_sets,
    from (this.comp_tendsto hs).congr' (hsÎ¼.mono $ Î» a ha, ha â–¸ rfl) hsÎ¼,
  refine is_o_iff.2 (Î» Îµ Îµâ‚€, _),
  have : âˆ€á¶  s in l.small_sets, âˆ€á¶  x in Î¼.ae, x âˆˆ s â†’ f x âˆˆ closed_ball b Îµ :=
    eventually_small_sets_eventually.2 (h.eventually $ closed_ball_mem_nhds _ Îµâ‚€),
  filter_upwards [hÎ¼.eventually, (hÎ¼.integrable_at_filter_of_tendsto_ae hfm h).eventually,
    hfm.eventually, this],
  simp only [mem_closed_ball, dist_eq_norm],
  intros s hÎ¼s h_integrable hfm h_norm,
  rw [â† set_integral_const, â† integral_sub h_integrable (integrable_on_const.2 $ or.inr hÎ¼s),
    real.norm_eq_abs, abs_of_nonneg ennreal.to_real_nonneg],
  exact norm_set_integral_le_of_norm_le_const_ae' hÎ¼s h_norm (hfm.sub ae_strongly_measurable_const)
end

/-- Fundamental theorem of calculus for set integrals, `nhds_within` version: if `Î¼` is a locally
finite measure and `f` is an almost everywhere measurable function that is continuous at a point `a`
within a measurable set `t`, then `âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at a filter `li`
provided that `s i` tends to `(ğ“[t] a).small_sets` along `li`.  Since `Î¼ (s i)` is an `â„â‰¥0âˆ`
number, we use `(Î¼ (s i)).to_real` in the actual statement.

Often there is a good formula for `(Î¼ (s i)).to_real`, so the formalization can take an optional
argument `m` with this formula and a proof `of `(Î» i, (Î¼ (s i)).to_real) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).to_real` is used in the output. -/
lemma continuous_within_at.integral_sub_linear_is_o_ae
  [topological_space Î±] [opens_measurable_space Î±]
  [normed_space â„ E] [complete_space E]
  {Î¼ : measure Î±} [is_locally_finite_measure Î¼] {a : Î±} {t : set Î±}
  {f : Î± â†’ E} (ha : continuous_within_at f t a) (ht : measurable_set t)
  (hfm : strongly_measurable_at_filter f (ğ“[t] a) Î¼)
  {s : Î¹ â†’ set Î±} {li : filter Î¹} (hs : tendsto s li (ğ“[t] a).small_sets)
  (m : Î¹ â†’ â„ := Î» i, (Î¼ (s i)).to_real)
  (hsÎ¼ : (Î» i, (Î¼ (s i)).to_real) =á¶ [li] m . tactic.interactive.refl) :
  is_o (Î» i, âˆ« x in s i, f x âˆ‚Î¼ - m i â€¢ f a) m li :=
by haveI : (ğ“[t] a).is_measurably_generated := ht.nhds_within_is_measurably_generated _;
exact (ha.mono_left inf_le_left).integral_sub_linear_is_o_ae
  hfm (Î¼.finite_at_nhds_within a t) hs m hsÎ¼

/-- Fundamental theorem of calculus for set integrals, `nhds` version: if `Î¼` is a locally finite
measure and `f` is an almost everywhere measurable function that is continuous at a point `a`, then
`âˆ« x in s i, f x âˆ‚Î¼ = Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at `li` provided that `s` tends to
`(ğ“ a).small_sets` along `li.  Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `(Î¼ (s i)).to_real` in
the actual statement.

Often there is a good formula for `(Î¼ (s i)).to_real`, so the formalization can take an optional
argument `m` with this formula and a proof `of `(Î» i, (Î¼ (s i)).to_real) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).to_real` is used in the output. -/
lemma continuous_at.integral_sub_linear_is_o_ae
  [topological_space Î±] [opens_measurable_space Î±]
  [normed_space â„ E] [complete_space E]
  {Î¼ : measure Î±} [is_locally_finite_measure Î¼] {a : Î±}
  {f : Î± â†’ E} (ha : continuous_at f a) (hfm : strongly_measurable_at_filter f (ğ“ a) Î¼)
  {s : Î¹ â†’ set Î±} {li : filter Î¹} (hs : tendsto s li (ğ“ a).small_sets)
  (m : Î¹ â†’ â„ := Î» i, (Î¼ (s i)).to_real)
  (hsÎ¼ : (Î» i, (Î¼ (s i)).to_real) =á¶ [li] m . tactic.interactive.refl) :
  is_o (Î» i, âˆ« x in s i, f x âˆ‚Î¼ - m i â€¢ f a) m li :=
(ha.mono_left inf_le_left).integral_sub_linear_is_o_ae hfm (Î¼.finite_at_nhds a) hs m hsÎ¼

/-- Fundamental theorem of calculus for set integrals, `nhds_within` version: if `Î¼` is a locally
finite measure, `f` is continuous on a measurable set `t`, and `a âˆˆ t`, then `âˆ« x in (s i), f x âˆ‚Î¼ =
Î¼ (s i) â€¢ f a + o(Î¼ (s i))` at `li` provided that `s i` tends to `(ğ“[t] a).small_sets` along `li`.
Since `Î¼ (s i)` is an `â„â‰¥0âˆ` number, we use `(Î¼ (s i)).to_real` in the actual statement.

Often there is a good formula for `(Î¼ (s i)).to_real`, so the formalization can take an optional
argument `m` with this formula and a proof `of `(Î» i, (Î¼ (s i)).to_real) =á¶ [li] m`. Without these
arguments, `m i = (Î¼ (s i)).to_real` is used in the output. -/
lemma continuous_on.integral_sub_linear_is_o_ae
  [topological_space Î±] [opens_measurable_space Î±]
  [normed_space â„ E] [complete_space E] [second_countable_topology_either Î± E]
  {Î¼ : measure Î±} [is_locally_finite_measure Î¼] {a : Î±} {t : set Î±}
  {f : Î± â†’ E} (hft : continuous_on f t) (ha : a âˆˆ t) (ht : measurable_set t)
  {s : Î¹ â†’ set Î±} {li : filter Î¹} (hs : tendsto s li (ğ“[t] a).small_sets)
  (m : Î¹ â†’ â„ := Î» i, (Î¼ (s i)).to_real)
  (hsÎ¼ : (Î» i, (Î¼ (s i)).to_real) =á¶ [li] m . tactic.interactive.refl) :
  is_o (Î» i, âˆ« x in s i, f x âˆ‚Î¼ - m i â€¢ f a) m li :=
(hft a ha).integral_sub_linear_is_o_ae ht
  âŸ¨t, self_mem_nhds_within, hft.ae_strongly_measurable htâŸ© hs m hsÎ¼

section
/-! ### Continuous linear maps composed with integration

The goal of this section is to prove that integration commutes with continuous linear maps.
This holds for simple functions. The general result follows from the continuity of all involved
operations on the space `LÂ¹`. Note that composition by a continuous linear map on `LÂ¹` is not just
the composition, as we are dealing with classes of functions, but it has already been defined
as `continuous_linear_map.comp_Lp`. We take advantage of this construction here.
-/

open_locale complex_conjugate

variables {Î¼ : measure Î±} {ğ•œ : Type*} [is_R_or_C ğ•œ] [normed_space ğ•œ E]
  [normed_group F] [normed_space ğ•œ F]
  {p : ennreal}

namespace continuous_linear_map

variables [complete_space F] [normed_space â„ F]

lemma integral_comp_Lp (L : E â†’L[ğ•œ] F) (Ï† : Lp E p Î¼) :
  âˆ« a, (L.comp_Lp Ï†) a âˆ‚Î¼ = âˆ« a, L (Ï† a) âˆ‚Î¼ :=
integral_congr_ae $ coe_fn_comp_Lp _ _

lemma set_integral_comp_Lp (L : E â†’L[ğ•œ] F) (Ï† : Lp E p Î¼) {s : set Î±} (hs : measurable_set s) :
  âˆ« a in s, (L.comp_Lp Ï†) a âˆ‚Î¼ = âˆ« a in s, L (Ï† a) âˆ‚Î¼ :=
set_integral_congr_ae hs ((L.coe_fn_comp_Lp Ï†).mono (Î» x hx hx2, hx))

lemma continuous_integral_comp_L1 (L : E â†’L[ğ•œ] F) :
  continuous (Î» (Ï† : Î± â†’â‚[Î¼] E), âˆ« (a : Î±), L (Ï† a) âˆ‚Î¼) :=
by { rw â† funext L.integral_comp_Lp, exact continuous_integral.comp (L.comp_LpL 1 Î¼).continuous, }

variables [complete_space E] [normed_space â„ E]

lemma integral_comp_comm (L : E â†’L[ğ•œ] F) {Ï† : Î± â†’ E} (Ï†_int : integrable Ï† Î¼) :
  âˆ« a, L (Ï† a) âˆ‚Î¼ = L (âˆ« a, Ï† a âˆ‚Î¼) :=
begin
  apply integrable.induction (Î» Ï†, âˆ« a, L (Ï† a) âˆ‚Î¼ = L (âˆ« a, Ï† a âˆ‚Î¼)),
  { intros e s s_meas s_finite,
    rw [integral_indicator_const e s_meas, â† @smul_one_smul E â„ ğ•œ _ _ _ _ _ (Î¼ s).to_real e,
      continuous_linear_map.map_smul, @smul_one_smul F â„ ğ•œ _ _ _ _ _ (Î¼ s).to_real (L e),
      â† integral_indicator_const (L e) s_meas],
    congr' 1 with a,
    rw set.indicator_comp_of_zero L.map_zero },
  { intros f g H f_int g_int hf hg,
    simp [L.map_add, integral_add f_int g_int,
      integral_add (L.integrable_comp f_int) (L.integrable_comp g_int), hf, hg] },
  { exact is_closed_eq L.continuous_integral_comp_L1 (L.continuous.comp continuous_integral) },
  { intros f g hfg f_int hf,
    convert hf using 1 ; clear hf,
    { exact integral_congr_ae (hfg.fun_comp L).symm },
    { rw integral_congr_ae hfg.symm } },
  all_goals { assumption }
end

lemma integral_apply {H : Type*} [normed_group H] [normed_space ğ•œ H]
  {Ï† : Î± â†’ H â†’L[ğ•œ] E} (Ï†_int : integrable Ï† Î¼) (v : H) :
  (âˆ« a, Ï† a âˆ‚Î¼) v = âˆ« a, Ï† a v âˆ‚Î¼ :=
((continuous_linear_map.apply ğ•œ E v).integral_comp_comm Ï†_int).symm

lemma integral_comp_comm' (L : E â†’L[ğ•œ] F) {K} (hL : antilipschitz_with K L) (Ï† : Î± â†’ E) :
  âˆ« a, L (Ï† a) âˆ‚Î¼ = L (âˆ« a, Ï† a âˆ‚Î¼) :=
begin
  by_cases h : integrable Ï† Î¼,
  { exact integral_comp_comm L h },
  have : Â¬ (integrable (L âˆ˜ Ï†) Î¼),
    by rwa lipschitz_with.integrable_comp_iff_of_antilipschitz L.lipschitz hL (L.map_zero),
  simp [integral_undef, h, this]
end

lemma integral_comp_L1_comm (L : E â†’L[ğ•œ] F) (Ï† : Î± â†’â‚[Î¼] E) : âˆ« a, L (Ï† a) âˆ‚Î¼ = L (âˆ« a, Ï† a âˆ‚Î¼) :=
L.integral_comp_comm (L1.integrable_coe_fn Ï†)

end continuous_linear_map

namespace linear_isometry

variables [complete_space F] [normed_space â„ F] [complete_space E] [normed_space â„ E]

lemma integral_comp_comm (L : E â†’â‚—áµ¢[ğ•œ] F) (Ï† : Î± â†’ E) : âˆ« a, L (Ï† a) âˆ‚Î¼ = L (âˆ« a, Ï† a âˆ‚Î¼) :=
L.to_continuous_linear_map.integral_comp_comm' L.antilipschitz _

end linear_isometry

variables [complete_space E] [normed_space â„ E] [complete_space F] [normed_space â„ F]

@[norm_cast] lemma integral_of_real {f : Î± â†’ â„} : âˆ« a, (f a : ğ•œ) âˆ‚Î¼ = â†‘âˆ« a, f a âˆ‚Î¼ :=
(@is_R_or_C.of_real_li ğ•œ _).integral_comp_comm f

lemma integral_re {f : Î± â†’ ğ•œ} (hf : integrable f Î¼) :
  âˆ« a, is_R_or_C.re (f a) âˆ‚Î¼ = is_R_or_C.re âˆ« a, f a âˆ‚Î¼ :=
(@is_R_or_C.re_clm ğ•œ _).integral_comp_comm hf

lemma integral_im {f : Î± â†’ ğ•œ} (hf : integrable f Î¼) :
  âˆ« a, is_R_or_C.im (f a) âˆ‚Î¼ = is_R_or_C.im âˆ« a, f a âˆ‚Î¼ :=
(@is_R_or_C.im_clm ğ•œ _).integral_comp_comm hf

lemma integral_conj {f : Î± â†’ ğ•œ} : âˆ« a, conj (f a) âˆ‚Î¼ = conj âˆ« a, f a âˆ‚Î¼ :=
(@is_R_or_C.conj_lie ğ•œ _).to_linear_isometry.integral_comp_comm f

lemma integral_coe_re_add_coe_im {f : Î± â†’ ğ•œ} (hf : integrable f Î¼) :
  âˆ« x, (is_R_or_C.re (f x) : ğ•œ) âˆ‚Î¼ + âˆ« x, is_R_or_C.im (f x) âˆ‚Î¼ * is_R_or_C.I = âˆ« x, f x âˆ‚Î¼ :=
begin
  rw [mul_comm, â† smul_eq_mul, â† integral_smul, â† integral_add],
  { congr,
    ext1 x,
    rw [smul_eq_mul, mul_comm, is_R_or_C.re_add_im] },
  { exact hf.re.of_real },
  { exact hf.im.of_real.smul is_R_or_C.I }
end

lemma integral_re_add_im {f : Î± â†’ ğ•œ} (hf : integrable f Î¼) :
  ((âˆ« x, is_R_or_C.re (f x) âˆ‚Î¼ : â„) : ğ•œ) + (âˆ« x, is_R_or_C.im (f x) âˆ‚Î¼ : â„) * is_R_or_C.I =
  âˆ« x, f x âˆ‚Î¼ :=
by { rw [â† integral_of_real, â† integral_of_real, integral_coe_re_add_coe_im hf] }

lemma set_integral_re_add_im {f : Î± â†’ ğ•œ} {i : set Î±} (hf : integrable_on f i Î¼) :
  ((âˆ« x in i, is_R_or_C.re (f x) âˆ‚Î¼ : â„) : ğ•œ) +
  (âˆ« x in i, is_R_or_C.im (f x) âˆ‚Î¼ : â„) * is_R_or_C.I = âˆ« x in i, f x âˆ‚Î¼ :=
integral_re_add_im hf

lemma fst_integral {f : Î± â†’ E Ã— F} (hf : integrable f Î¼) :
  (âˆ« x, f x âˆ‚Î¼).1 = âˆ« x, (f x).1 âˆ‚Î¼ :=
((continuous_linear_map.fst â„ E F).integral_comp_comm hf).symm

lemma snd_integral {f : Î± â†’ E Ã— F} (hf : integrable f Î¼) :
  (âˆ« x, f x âˆ‚Î¼).2 = âˆ« x, (f x).2 âˆ‚Î¼ :=
((continuous_linear_map.snd â„ E F).integral_comp_comm hf).symm

lemma integral_pair {f : Î± â†’ E} {g : Î± â†’ F} (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ« x, (f x, g x) âˆ‚Î¼ = (âˆ« x, f x âˆ‚Î¼, âˆ« x, g x âˆ‚Î¼) :=
have _ := hf.prod_mk hg, prod.ext (fst_integral this) (snd_integral this)

lemma integral_smul_const {ğ•œ : Type*} [is_R_or_C ğ•œ] [normed_space ğ•œ E] (f : Î± â†’ ğ•œ) (c : E) :
  âˆ« x, f x â€¢ c âˆ‚Î¼ = (âˆ« x, f x âˆ‚Î¼) â€¢ c :=
begin
  by_cases hf : integrable f Î¼,
  { exact ((1 : ğ•œ â†’L[ğ•œ] ğ•œ).smul_right c).integral_comp_comm hf },
  { by_cases hc : c = 0,
    { simp only [hc, integral_zero, smul_zero] },
    rw [integral_undef hf, integral_undef, zero_smul],
    simp_rw [integrable_smul_const hc, hf, not_false_iff] }
end

section inner

variables {E' : Type*} [inner_product_space ğ•œ E'] [complete_space E'] [normed_space â„ E']

local notation `âŸª`x`, `y`âŸ«` := @inner ğ•œ E' _ x y

lemma integral_inner {f : Î± â†’ E'} (hf : integrable f Î¼) (c : E') :
  âˆ« x, âŸªc, f xâŸ« âˆ‚Î¼ = âŸªc, âˆ« x, f x âˆ‚Î¼âŸ« :=
((@innerSL ğ•œ E' _ _ c).restrict_scalars â„).integral_comp_comm hf

lemma integral_eq_zero_of_forall_integral_inner_eq_zero (f : Î± â†’ E') (hf : integrable f Î¼)
  (hf_int : âˆ€ (c : E'), âˆ« x, âŸªc, f xâŸ« âˆ‚Î¼ = 0) :
  âˆ« x, f x âˆ‚Î¼ = 0 :=
by { specialize hf_int (âˆ« x, f x âˆ‚Î¼), rwa [integral_inner hf, inner_self_eq_zero] at hf_int }

end inner

lemma integral_with_density_eq_integral_smul
  {f : Î± â†’ â„â‰¥0} (f_meas : measurable f) (g : Î± â†’ E) :
  âˆ« a, g a âˆ‚(Î¼.with_density (Î» x, f x)) = âˆ« a, f a â€¢ g a âˆ‚Î¼ :=
begin
  by_cases hg : integrable g (Î¼.with_density (Î» x, f x)), swap,
  { rw [integral_undef hg, integral_undef],
    rwa [â† integrable_with_density_iff_integrable_smul f_meas];
    apply_instance },
  refine integrable.induction _ _ _ _ _ hg,
  { assume c s s_meas hs,
    rw integral_indicator s_meas,
    simp_rw [â† indicator_smul_apply, integral_indicator s_meas],
    simp only [s_meas, integral_const, measure.restrict_apply', univ_inter, with_density_apply],
    rw [lintegral_coe_eq_integral, ennreal.to_real_of_real, â† integral_smul_const],
    { refl },
    { exact integral_nonneg (Î» x, nnreal.coe_nonneg _) },
    { refine âŸ¨(f_meas.coe_nnreal_real).ae_measurable.ae_strongly_measurable, _âŸ©,
      rw with_density_apply _ s_meas at hs,
      rw has_finite_integral,
      convert hs,
      ext1 x,
      simp only [nnreal.nnnorm_eq] } },
  { assume u u' h_disj u_int u'_int h h',
    change âˆ« (a : Î±), (u a + u' a) âˆ‚Î¼.with_density (Î» (x : Î±), â†‘(f x)) =
      âˆ« (a : Î±), f a â€¢ (u a + u' a) âˆ‚Î¼,
    simp_rw [smul_add],
    rw [integral_add u_int u'_int, h, h', integral_add],
    { exact (integrable_with_density_iff_integrable_smul f_meas).1 u_int },
    { exact (integrable_with_density_iff_integrable_smul f_meas).1 u'_int } },
  { have C1 : continuous (Î» (u : Lp E 1 (Î¼.with_density (Î» x, f x))),
      âˆ« x, u x âˆ‚(Î¼.with_density (Î» x, f x))) := continuous_integral,
    have C2 : continuous (Î» (u : Lp E 1 (Î¼.with_density (Î» x, f x))),
      âˆ« x, f x â€¢ u x âˆ‚Î¼),
    { have : continuous ((Î» (u : Lp E 1 Î¼), âˆ« x, u x âˆ‚Î¼) âˆ˜ (with_density_smul_li Î¼ f_meas)) :=
        continuous_integral.comp (with_density_smul_li Î¼ f_meas).continuous,
      convert this,
      ext1 u,
      simp only [function.comp_app, with_density_smul_li_apply],
      exact integral_congr_ae (mem_â„’1_smul_of_L1_with_density f_meas u).coe_fn_to_Lp.symm },
    exact is_closed_eq C1 C2 },
  { assume u v huv u_int hu,
    rw [â† integral_congr_ae huv, hu],
    apply integral_congr_ae,
    filter_upwards [(ae_with_density_iff f_meas.coe_nnreal_ennreal).1 huv] with x hx,
    rcases eq_or_ne (f x) 0 with h'x|h'x,
    { simp only [h'x, zero_smul]},
    { rw [hx _],
      simpa only [ne.def, ennreal.coe_eq_zero] using h'x } }
end

lemma integral_with_density_eq_integral_smulâ‚€
  {f : Î± â†’ â„â‰¥0} (hf : ae_measurable f Î¼) (g : Î± â†’ E) :
  âˆ« a, g a âˆ‚(Î¼.with_density (Î» x, f x)) = âˆ« a, f a â€¢ g a âˆ‚Î¼ :=
begin
  let f' := hf.mk _,
  calc âˆ« a, g a âˆ‚(Î¼.with_density (Î» x, f x))
      = âˆ« a, g a âˆ‚(Î¼.with_density (Î» x, f' x)) :
  begin
    congr' 1,
    apply with_density_congr_ae,
    filter_upwards [hf.ae_eq_mk] with x hx,
    rw hx,
  end
  ... = âˆ« a, f' a â€¢ g a âˆ‚Î¼ : integral_with_density_eq_integral_smul hf.measurable_mk _
  ... = âˆ« a, f a â€¢ g a âˆ‚Î¼ :
  begin
    apply integral_congr_ae,
    filter_upwards [hf.ae_eq_mk] with x hx,
    rw hx,
  end
end

lemma set_integral_with_density_eq_set_integral_smul
  {f : Î± â†’ â„â‰¥0} (f_meas : measurable f) (g : Î± â†’ E) {s : set Î±} (hs : measurable_set s) :
  âˆ« a in s, g a âˆ‚(Î¼.with_density (Î» x, f x)) = âˆ« a in s, f a â€¢ g a âˆ‚Î¼ :=
by rw [restrict_with_density hs, integral_with_density_eq_integral_smul f_meas]

lemma set_integral_with_density_eq_set_integral_smulâ‚€ {f : Î± â†’ â„â‰¥0} {s : set Î±}
  (hf : ae_measurable f (Î¼.restrict s)) (g : Î± â†’ E) (hs : measurable_set s) :
  âˆ« a in s, g a âˆ‚(Î¼.with_density (Î» x, f x)) = âˆ« a in s, f a â€¢ g a âˆ‚Î¼ :=
by rw [restrict_with_density hs, integral_with_density_eq_integral_smulâ‚€ hf]

end
